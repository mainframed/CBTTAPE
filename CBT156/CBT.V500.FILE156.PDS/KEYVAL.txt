         MACRO                                                            KEYVAL
&NAME    KEYVAL &STRING,&TABLE,&PRINT=OFF                                 KEYVAL
         GBLC  &KVRCSEC            CSECT NAME                             KEYVAL
         LCLC  &CSNAME             OUTER CSECT NAME                       KEYVAL
         LCLB  &KVRCSON            LOCAL SWITCH FOR CSECT GENERATION      KEYVAL
.**********************************************************************   KEYVAL
.***                                                                ***   KEYVAL
.***  MACRO  NAME: KEYWORD VALIDATION ROUTINE                       ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***  GENERAL FUNCTION: TO VALIDATE A CHARACTER STRING CONTAINING   ***   KEYVAL
.***                    KEYWORDS AND VALUES AND PERFORM REQUESTED   ***   KEYVAL
.***                    OPERATIONS AS SPECIFIED IN THE KEYWORD      ***   KEYVAL
.***                    VALIDATION TABLE DEFINED WITH ONE OR MORE   ***   KEYVAL
.***                    KEYT MACROS.  THIS MACRO EXPANDS TO AN      ***   KEYVAL
.***                    INDEPENDENT CSECT UPON INITIAL INVOCATION   ***   KEYVAL
.***                    AND IS ACCESSED VIA V-TYPE ADDRESSING       ***   KEYVAL
.***                    FOR SUBSEQUENT INVOCATIONS.  THERE ARE 2    ***   KEYVAL
.***                    BASIC PARTS TO THIS MACRO.  THE FIRST PART  ***   KEYVAL
.***                    SETS UP THE APPROPRIATE PARMS BASED UPON    ***   KEYVAL
.***                    THE USER PARAMETERS SUPPLIED.  THE SECOND   ***   KEYVAL
.***                    PART DOES THE STRING MANIPULATION, PERFORMS ***   KEYVAL
.***                    THE REQUESTED FUNCTIONS AND SETS RETURN     ***   KEYVAL
.***                    CODES.                                      ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***  ENTRY REGS:   R1                                              ***   KEYVAL
.***  (PART 1)      R2                                              ***   KEYVAL
.***                R3                                              ***   KEYVAL
.***                R4                                              ***   KEYVAL
.***                R5                                              ***   KEYVAL
.***                R6                                              ***   KEYVAL
.***                R7                                              ***   KEYVAL
.***                R8                                              ***   KEYVAL
.***                R9                                              ***   KEYVAL
.***                R10                                             ***   KEYVAL
.***                R11                                             ***   KEYVAL
.***                R12                                             ***   KEYVAL
.***                R13  SAVE AREA                                  ***   KEYVAL
.***                R14                                             ***   KEYVAL
.***                R15                                             ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***  ENTRY REGS:   R0   AL4  ADDR OF CHAR STRING                   ***   KEYVAL
.***  (PART 2)      R1   AL4  ADDRESS OF 1ST KEYT ENTRY             ***   KEYVAL
.***                R2                                              ***   KEYVAL
.***                R3                                              ***   KEYVAL
.***                R4                                              ***   KEYVAL
.***                R5                                              ***   KEYVAL
.***                R6                                              ***   KEYVAL
.***                R7                                              ***   KEYVAL
.***                R8                                              ***   KEYVAL
.***                R9                                              ***   KEYVAL
.***                R10                                             ***   KEYVAL
.***                R11                                             ***   KEYVAL
.***                R12                                             ***   KEYVAL
.***                R13  SAVE AREA                                  ***   KEYVAL
.***                R14  RETURN ADDRESS                             ***   KEYVAL
.***                R15  ENTRY ADDRESS                              ***   KEYVAL
.***                                                                ***   KEYVAL
.***  REGISTER USAGE:  R0   WORK                                    ***   KEYVAL
.***                   R1   WORK                                    ***   KEYVAL
.***                   R2   WORK                                    ***   KEYVAL
.***                   R3   CHAR STRING LENGTH                      ***   KEYVAL
.***                   R4   CHAR STRING ADDRESS                     ***   KEYVAL
.***                   R5   WORK                                    ***   KEYVAL
.***                   R6   WORK                                    ***   KEYVAL
.***                   R7   WORK                                    ***   KEYVAL
.***                   R8   WORK                                    ***   KEYVAL
.***                   R9   SAVED RETURN CODE                       ***   KEYVAL
.***                   R10  WORK                                    ***   KEYVAL
.***                   R11  PROGRAM BASE                            ***   KEYVAL
.***                   R12  WORKAREA BASE                           ***   KEYVAL
.***                   R13  OLD SAVEAREA                            ***   KEYVAL
.***                   R14  KEYWORD TABLE BASE                      ***   KEYVAL
.***                   R15  RETURN CODE                             ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***  AUTHOR: E STEWART                DATE: 05/29/80               ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***  ATTRIBUTES: RENT,REUS,REFR                                    ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***  CALLED BY: UTILITY FUNCTION                                   ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***  MACROS USED: GETMAIN,FREEMAIN                                 ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***  ROUTINES CALLED: NONE                                         ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***  NORMAL EXIT: BR R14                                           ***   KEYVAL
.***                                                                ***   KEYVAL
.***  EXIT REGS:     R0   AL4  ADDR OF KEYWORD IN CHAR STRING WHERE ***   KEYVAL
.***                           VALIDATION ERROR OCCURED             ***   KEYVAL
.***                 R1   AL4  ADDR OF VALUE IN CHAR STRING WHERE   ***   KEYVAL
.***                           VALIDATION ERROR OCCURED             ***   KEYVAL
.***                 R15       00  KEYWORD(S)VALUE(S) FOUND AND     ***   KEYVAL
.***                               OP(S) PERFORMED                  ***   KEYVAL
.***                           04  KEYWORD(S) FOUND, SOME VALUE NOT ***   KEYVAL
.***                               FOUND IN KEYT                    ***   KEYVAL
.***                           08  A KEYWORD WAS NOT FOUND IN KEYT  ***   KEYVAL
.***                           12  'VALCNT' DID NOT PASS VALIDATION ***   KEYVAL
.***                           16  MORE INPUT EXPECTED, I.E. ONLY   ***   KEYVAL
.***                               BLANKS, COMMENTS, OR ),BLANK WAS ***   KEYVAL
.***                               FOUND. OTHERWISE RC=00 WOULD HAVE***   KEYVAL
.***                               BEEN RETURNED.                   ***   KEYVAL
.***                           20  'TYPE' DID NOT PASS VALIDATION.  ***   KEYVAL
.***                           24  'LEN' DID NOT PASS VALIDATION.   ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***  ABEND CODES: NONE (I HOPE)                                    ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***  MESSAGES ISSUED: NONE                                         ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***  DSECTS/MACROS USED: NONE FROM AN EXTERNAL SOURCE              ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***  SPECIAL NOTES:                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.***                                                                ***   KEYVAL
.**********************************************************************   KEYVAL
         AIF   ('&KVRCSEC' NE '').KVRBALR CSECT ALREADY ASSIGNED          KEYVAL
&KVRCSEC SETC  'KVR'.'&SYSNDX'   SET CSECT NAME                           KEYVAL
&KVRCSON SETB  1                   SAY CSECT ASSIGNED THIS PASS           KEYVAL
.KVRBALR ANOP                                                             KEYVAL
         AIF   (N'&SYSLIST(1) EQ  1 AND N'&SYSLIST(2) EQ 1).KVRM010       KEYVAL
         MNOTE 8,'OPERAND SUBLISTS NOT PERMITTED'                         KEYVAL
         MEXIT                                                            KEYVAL
.KVRM010 ANOP                                                             KEYVAL
         AIF   ('&NAME' EQ '').KVRM014                                    KEYVAL
&NAME    DS    0H                  SET LABEL NAME                         KEYVAL
.KVRM014 ANOP                                                             KEYVAL
         AIF   ('&SYSLIST(1)' EQ '(0)' OR '&SYSLIST(1)' EQ '(R0)').KVRMX  KEYVAL
               040           STRING AND LENGTH ALREADY OK                 KEYVAL
         AIF   ('&SYSLIST(1)' EQ '(0)' OR '&SYSLIST(2)' EQ '(R0)').KVRMX  KEYVAL
               020                CHAR STRING ALREADY IN R0               KEYVAL
         AIF   ('&STRING'(1,1) EQ '(').KVRM016 REG NOTATION               KEYVAL
         LA    0,&STRING           GET ADDR OF CHAR STRING                KEYVAL
         AGO   .KVRM020                                                   KEYVAL
.KVRM016 ANOP                                                             KEYVAL
         LR    0,&STRING           GET ADDR OF CHAR STRING                KEYVAL
.KVRM020 ANOP                                                             KEYVAL
         AGO   .KVRM040                                                   KEYVAL
.KVRM040 ANOP                                                             KEYVAL
         AIF   ('&SYSLIST(2)' EQ '(1)' OR '&SYSLIST(2)' EQ '(R1)').KVRMX  KEYVAL
               060                                                        KEYVAL
         AIF   ('&TABLE'(1,1) EQ '(').KVRM050 REG NOTATION                KEYVAL
         LA    1,&TABLE            KEYWORD TABLE ADDRESS                  KEYVAL
         AGO   .KVRM060                                                   KEYVAL
.KVRM050 ANOP                                                             KEYVAL
         LR    1,&TABLE            KEYWORD TABLE ADDRESS                  KEYVAL
.KVRM060 ANOP                                                             KEYVAL
         L     15,=V(&KVRCSEC)     GET CSECT NAME                         KEYVAL
         BALR  14,15               GO TO VALIDATION ROUTINE               KEYVAL
         AIF   (NOT(&KVRCSON)).NOGO TERMINATE IF ALREADY EXPANDED         KEYVAL
         PUSH  USING,PRINT                                                KEYVAL
         AIF   ('&PRINT' EQ 'ON').KVRM070 NO LIST                         KEYVAL
         PRINT OFF                 TURN PRINT OFF                         KEYVAL
         AGO   .KVRM072                                                   KEYVAL
.KVRM070 ANOP                                                             KEYVAL
         PRINT ON,GEN              TURN ON PRINT                          KEYVAL
.KVRM072 ANOP                                                             KEYVAL
&CSNAME  SETC  '&SYSECT'                                                  KEYVAL
&KVRCSEC CSECT                                                            KEYVAL
         USING *,11                SET UP BASE                            KEYVAL
         STM   14,12,12(13)        SAVE REGS                              KEYVAL
         LR    11,15               GET BASE ADDRESS                       KEYVAL
         USING KVDSVR,14           TABLE ADDRESS BASE                     KEYVAL
         USING KVWAVR,12           WORKAREA BASE                          KEYVAL
         GETMAIN R,LV=KVWALNTH     GET WORKAREA                           KEYVAL
         LR    12,1                PUT INTO BASE REG                      KEYVAL
         L     4,20(13)            GET STRING ADDRESS INTO REG 4          KEYVAL
         SR    3,3                 CLEAR WORK REG                         KEYVAL
         ICM   3,3,0(4)            GET STRING LENGTH                      KEYVAL
         LA    4,2(,4)             BUMP OVER STRING LENGTH                KEYVAL
         MVI   KVWAFL1,0           CLEAR FLAG BYTE                        KEYVAL
         MVI   KVWAKWVA,0          CLEAR COUNT BYTE                       KEYVAL
         MVC   KVWASAV1(4*13),20(13) SAVE REGS 0-12                       KEYVAL
         MVC   KVWASAV1+(4*14)(4*2),12(13) SAVE REGS 14-15                KEYVAL
         ST    13,KVWASAV1+(4*13)  SAVE REG 13 FROM CALLER                KEYVAL
KVR004   DS    0H                                                         KEYVAL
         L     14,24(13)           TABLE ADDRESS                          KEYVAL
         XC    0(KVWACLR,12),0(12)   CLEAR WORKAREA                       KEYVAL
         LTR   3,3                 Q. ANY DATA TO SCAN                    KEYVAL
         BP    KVR005              A. YES, CONTINUE AS NORMAL             KEYVAL
         LA    9,16                SET BAD RC                             KEYVAL
         B     KVR900              AND RETURN                             KEYVAL
KVR005   DS    0H                                                         KEYVAL
         SR    2,2                 CLEAR FOR LATER                        KEYVAL
         ST    4,KVWALOC           SAVE STRING LOC                        KEYVAL
         ST    4,KVWAKEY           SAVE STRING LOC                        KEYVAL
         BCTR  3,0                 REDUCE BY ONE                          KEYVAL
         EX    3,KVTR1VR           SEARCH FOR NON BLANK                   KEYVAL
         LA    3,1(,3)             GET ORIG LENGTH                        KEYVAL
         CLM   2,1,KVAX00          Q. ANYTHING FOUND                      KEYVAL
         BNE   KVR006              A. YES, SEE WHAT IT IS                 KEYVAL
         LA    9,16                SET BLANKS RETURN CODE                 KEYVAL
         B     KVR900              RETURN TO CALLER                       KEYVAL
KVR006   DS    0H                                                         KEYVAL
         LR    5,1                 SAVE REG 1                             KEYVAL
         SR    1,4                 GET NEW                                KEYVAL
         SR    3,1                 DATA LENGTH                            KEYVAL
         LR    4,5                 GET NEW LOCATION                       KEYVAL
         CLM   2,1,KVASLASH        Q. COULD THIS BE COMMENT               KEYVAL
         BNE   KVR020              A. NO, CONTINUE                        KEYVAL
         CLI   1(4),C'*'           Q .WAS THIS A COMMENT                  KEYVAL
         BE    KVR008              A. YES, CONTINUE                       KEYVAL
         LA    9,8                 SAY INVALID KEYWORD                    KEYVAL
         B     KVR900              AND RETURN                             KEYVAL
KVR008   DS    0H                  VALIDATE COMMENT                       KEYVAL
         LA    4,2(4)              BUMP OVER /*                           KEYVAL
         BCTR  3,0                 REDUCE COUNT                           KEYVAL
         BCTR  3,0                 REDUCE COUNT                           KEYVAL
KVR010   DS    0H                                                         KEYVAL
         CLI   0(4),C'*'           Q. IS IT REALLY A COMMENT              KEYVAL
         BE    KVR014              A. YES, CONTINUE                       KEYVAL
KVR012   DS    0H                                                         KEYVAL
         LA    4,1(4)              BUMP ADDRESS                           KEYVAL
         BCT   3,KVR010            LOOP THROUGH                           KEYVAL
         LA    9,8                 SET RETURN CODE                        KEYVAL
         B     KVR900              AND RETURN                             KEYVAL
KVR014   DS 0H                                                            KEYVAL
         CLI   1(4),C'/'           Q. IS 2ND CHAR A SLASH                 KEYVAL
         BNE   KVR012              A. NO, CONTINUE SCAN FOR END           KEYVAL
         LA    4,2(4)              BUMP OVER */                           KEYVAL
         BCTR  3,0                 REDUCE COUNT                           KEYVAL
         BCTR  3,0                 REDUCE COUNT                           KEYVAL
         B     KVR005              AND CONTINUE SCAN                      KEYVAL
KVR020   DS    0H                  VALIDATE CHARACTERS FOUND              KEYVAL
         ST    4,KVWALOC           SAVE STRING ADDR                       KEYVAL
         ST    4,KVWAKEY           SAVE STRING ADDR                       KEYVAL
         SR    5,5                 CLEAR FOR KEYWORD COMPARE              KEYVAL
         SR    6,6                 CLEAR FOR KEYWORD COMPARE              KEYVAL
         SR    7,7                 CLEAR FOR KEYWORD COMPARE              KEYVAL
KVR022   DS    0H                                                         KEYVAL
         IC    5,KVLL0001          GET KEYWORD LEN                        KEYVAL
         EX    5,KVCVR             Q. KEYWORD FOUND                       KEYVAL
         BE    KVR080              A. YES,CONTINUE                        KEYVAL
KVR023   DS    0H                                                         KEYVAL
         IC    6,KVVN0001          GET COUNT OF VALUE ENTRIES             KEYVAL
         LA    14,0(14,5)          BUMP OVER VARIABLE KEYWORD             KEYVAL
         LTR   6,6                 Q. ANY MORE VALUES TO DO               KEYVAL
         BZ    KVR028              A. NO, CONTINUE AS IS                  KEYVAL
KVR024   DS    0H                                                         KEYVAL
         IC    7,KVVL0001          LENGTH OF VALUE KEYWORD                KEYVAL
         LA    14,KVVG0001(7,14)   BUMP OVER VALUE ENTRY                  KEYVAL
         BCT   6,KVR024            CONTINUE THRU ALL VALUES               KEYVAL
KVR028   DS    0H                                                         KEYVAL
         LA    14,KVKL0001+1(14)   BUMP OVER KEYWORD ENTRY                KEYVAL
         CLC   KVAFFFF,KVDSVR      Q. LAST ENTRY                          KEYVAL
         BNE   KVR022              A. NO, CONTINUE SCAN                   KEYVAL
         LA    9,8                 SET RETURN CODE                        KEYVAL
         B     KVR900              AND RETURN                             KEYVAL
KVR080   DS    0H                  KEYWORD FOUND                          KEYVAL
         LA    6,1(5,4)            GO TO NEXT BYTE BEYOND KEYWORD         KEYVAL
         CLI   0(6),C'('           Q. IS THIS DELIMITER                   KEYVAL
         BE    KVR082              A. YES, CONTINUE                       KEYVAL
         CLI   0(6),C' '           Q. IS THIS DELIMITER                   KEYVAL
         BE    KVR082              A. YES, CONTINUE                       KEYVAL
         CLI   0(6),C','           Q. IS THIS DELIMITER                   KEYVAL
         BE    KVR082              A. YES, CONTINUE                       KEYVAL
         LR    9,3                 CURRENT LENGTH                         KEYVAL
         SR    9,5                 MINUS KEYWORD EX LENGTH                KEYVAL
         BCTR  9,0                 MINUS ONE                              KEYVAL
         LTR   9,9                 Q. ANY DATA LEFT IN STRING             KEYVAL
         BZ    KVR082              A. NO, JUST CONTINUE NORMALLY          KEYVAL
         SR    6,6                 CLEAR FOR LATER                        KEYVAL
         B     KVR023              ELSE LOOK FOR ANOTHER KEYWORD          KEYVAL
KVR082   DS    0H                                                         KEYVAL
         SR    6,6                 CLEAR WORK REG                         KEYVAL
         ICM   6,3,KVRA0001        GET EXIT ROUTINE ADDR                  KEYVAL
         BZ    KVR084              BRANCH IF NOT SPECIFIED                KEYVAL
         SRDL  6,12                GET REG INTO R6                        KEYVAL
         SLL   6,2                 X 4                                    KEYVAL
         LA    1,KVWASAV1          ADDR OF SAVE AREA                      KEYVAL
         L     6,0(6,1)            GET CONTENTS OF RTN BASE REG           KEYVAL
         SRL   7,20                SHIFT TO LOWER END                     KEYVAL
         LA    7,0(7,6)            GET ROUTINE ADDRESS                    KEYVAL
         ST    7,KVWARTN           SAVE RTN ADDRESS                       KEYVAL
KVR084   DS    0H                                                         KEYVAL
         SR    6,6                 CLEAR WORK REG                         KEYVAL
         ICM   6,3,KVRP0001        GET EXIT ROUTINE PARM                  KEYVAL
         BZ    KVR088              BRANCH IF NOT SPECIFIED                KEYVAL
         SRDL  6,12                GET REG INTO R6                        KEYVAL
         SLL   6,2                 X 4                                    KEYVAL
         LA    1,KVWASAV1          ADDR OF SAVE AREA                      KEYVAL
         L     6,0(6,1)            GET CONTENTS OF PARM BASE REG          KEYVAL
         SRL   7,20                SHIFT TO LOWER END                     KEYVAL
         LA    7,0(7,6)            GET PARM ADDRESS                       KEYVAL
         ST    7,KVWAPRM           SAVE PARM ADDRESS                      KEYVAL
KVR088   DS    0H                                                         KEYVAL
         LA    4,1(5,4)            GO TO NEXT BYTE BEYOND KEYWORD         KEYVAL
         SR    3,5                 REDUCE STRING COUNT                    KEYVAL
         BCTR  3,0                 REDUCE STRING COUNT                    KEYVAL
         CLI   0(4),C'('           Q. VALUE(S) SPECIFIED                  KEYVAL
         BE    KVR110              A. YES, CONTINUE                       KEYVAL
         CLI   KVVN0001,0          Q. ANY VALUES REQUIRED                 KEYVAL
         BNE   KVR090              A. NO, JUST RETURN                     KEYVAL
         SR    9,9                 SET ZERO RETURN CODE                   KEYVAL
         B     KVR900              AND RETURN                             KEYVAL
KVR090   DS    0H                                                         KEYVAL
         MVI   KVWAKWVA,0          CLEAR COUNT                            KEYVAL
         B     KVR172              GO PROCESS THIS KEYWORD                KEYVAL
KVR110   DS    0H                  MOVE ALL VALUES TO LOCAL STORAGE       KEYVAL
         SR    1,1                 CLEAR FOR LATER                        KEYVAL
         MVI   KVWAKWVA,0          CLEAR COUNT                            KEYVAL
         BCTR  3,0                 REDUCE COUNT                           KEYVAL
         LA    4,1(4)              BUMP TO NEXT BYTE                      KEYVAL
         CLI   0(4),C')'           Q. NULL KEYWORDS                       KEYVAL
         BNE   KVR120              A. NO, CONTINUE NORMALLY               KEYVAL
         B     KVR140              GO PROCESS NULL ENTRY                  KEYVAL
KVR120   DS    0H                                                         KEYVAL
         SR    1,1                 CLEAR FOR LATER                        KEYVAL
         CLI   0(4),C''''          Q. QUOTED STRING FOR THIS VALUE        KEYVAL
         BNE   KVR124              A. NO, DONT SET FLAG                   KEYVAL
         MVI   KVWAQUO,1           SAY IN QUOTE MODE                      KEYVAL
         LA    4,1(,4)             BUMP OVER QUOTE                        KEYVAL
         BCTR  3,0                 REDUCE STRING COUNT                    KEYVAL
KVR124   DS    0H                                                         KEYVAL
         LA    2,KVWATMP           ADDR OF TEMP WORK AREA                 KEYVAL
         LR    5,4                 SAVE OLD VALUES LOCATION               KEYVAL
         ST    5,KVWALOC           SAVE STRING ADDR                       KEYVAL
KVR130   DS    0H                                                         KEYVAL
         CLI   0(4),C','           Q. COMMA DELIMITER                     KEYVAL
         BE    KVR140              A. YES, SAVE THIS KEYWORD              KEYVAL
         CLI   0(4),C')'           Q. TERMINATING DELIMITER               KEYVAL
         BE    KVR140              A. YES, PROCESS END KEYWORD VALUES     KEYVAL
         CLI   0(4),C''''          Q. END OF QUOTED STRING                KEYVAL
         BNE   KVR134              A. NO, CONTINUE                        KEYVAL
         LA    4,1(,4)             BUMP ADDRESS                           KEYVAL
         BCTR  3,0                 REDUCE COUNT                           KEYVAL
         LA    1,1(,1)             ADD 1 FOR LATER STRING MOVE            KEYVAL
         CLI   0(4),C''''          Q. REALLY END OF STRING                KEYVAL
         BE    KVR134              A. NO, NOT YET JUST DOUBLE QUOTES      KEYVAL
         MVI   KVWAQUO,0           RESET QUOTE MODE FLAG                  KEYVAL
         B     KVR130              AND CONTINUE                           KEYVAL
KVR134   DS    0H                                                         KEYVAL
         MVC   0(1,2),0(4)         SAVE THIS BYTE                         KEYVAL
         LA    2,1(,2)             BUMP WORK BYTE                         KEYVAL
         LA    4,1(4)              GO TO NEXT BYTE                        KEYVAL
         BCT   3,KVR130            CONTINUE SCAN                          KEYVAL
         LA    9,4                 SET RETURN CODE                        KEYVAL
         B     KVR900              AND RETURN                             KEYVAL
KVR140   DS    0H                  PROCESS FOUND VALUE                    KEYVAL
         CLI   KVWAQUO,0           Q. ARE WE IN QUOTE MODE                KEYVAL
         BNE   KVR134              A. YES, IGNORE OTHER DELIMITERS        KEYVAL
         LR    6,4                 SAVE NEW ADDR                          KEYVAL
         SR    6,5                 GET VALUE LENGTH                       KEYVAL
         SR    6,1                 SUBTRACT ANY QUOTED STRING BYTES       KEYVAL
         SR    5,5                 CLEAR WORK REG                         KEYVAL
         ICM   5,1,KVWAKWVA        GET WORK AREA COUNT                    KEYVAL
         BNZ   KVR146              BRANCH IF ALREADY SOME IN THERE        KEYVAL
         LA    8,KVWAKWVA+1        GET NEXT LOC ADDRESS                   KEYVAL
         B     KVR148              AND CONTINUE                           KEYVAL
KVR146   DS    0H                  LOOK FOR VALUES END LOCATION           KEYVAL
         LA    8,KVWAKWVA+1        GET START ADDRESS                      KEYVAL
         SR    7,7                 CLEAR WORK REG                         KEYVAL
         ICM   7,1,KVWAKWVA+1      GET LENGTH OF LAST ONE                 KEYVAL
KVR147   DS    0H                                                         KEYVAL
         LA    8,1(7,8)            GO TO NEXT SLOT                        KEYVAL
         ICM   7,1,0(8)            GET NEXT LENGTH                        KEYVAL
         BCT   5,KVR147            LOOP THRU                              KEYVAL
KVR148   DS    0H                  MOVE NEW VALUE IN NOW                  KEYVAL
         LTR   6,6                 Q. NULL VALUE SPECIFIED                KEYVAL
         BNZ   KVR150              A. NO, CONTINUE MVC                    KEYVAL
         STC   6,0(8)              SAVE NULL VALUE LENGTH                 KEYVAL
         B     KVR160              AND UPDATE VALUES COUNT                KEYVAL
KVR150   DS    0H                  MOVE NEW VALUES INTO WORKAREA          KEYVAL
         L     5,KVWALOC           RESTORE STRING ADDR                    KEYVAL
         STC   6,0(8)              SAVE STRING LENGTH                     KEYVAL
         BCTR  6,0                 REDUCE COUNT                           KEYVAL
         EX    6,KVM1VR            PERFORM THE MOVE                       KEYVAL
         TM    KVVT0001,X'FF'      Q. ANY VALIDATION REQUIRED             KEYVAL
         BZ    KVR160              A. NO, JUST CONTINUE                   KEYVAL
         SR    2,2                 CLEAR WORK REG                         KEYVAL
         ICM   2,1,KVVT0001        GET VALIDATION CODE                    KEYVAL
         SLL   2,2                 X 4                                    KEYVAL
         B     *+0(2)              GO TO VALIDATE IT                      KEYVAL
         B     KVR154              ALPHA                                  KEYVAL
         B     KVR156              NUMERIC                                KEYVAL
         B     KVR158              ALPH/NUMERIC                           KEYVAL
KVR154   DS    0H                  ALPHA VALIDATION                       KEYVAL
         EX    6,KVTR2VR           A. ALPHA FIELD                         KEYVAL
         BZ    KVR160              A. YES, CONTINUE                       KEYVAL
KVR155   DS    0H                  ERROR RETURN                           KEYVAL
         LA    9,20                SET RC                                 KEYVAL
         B     KVR900              AND RETURN                             KEYVAL
KVR156   DS    0H                  NUMERIC                                KEYVAL
         EX    6,KVTR3VR           Q. NUMERIC FIELD                       KEYVAL
         BZ    KVR160              A. YES, CONTINUE                       KEYVAL
         B     KVR155              ELSE SET BAD RC                        KEYVAL
KVR158   DS    0H                  ALPHA/NUMERIC                          KEYVAL
         EX    6,KVTR4VR           Q. ALPHA/NUMERIC                       KEYVAL
         BZ    KVR160              A. YES, CONTINUE                       KEYVAL
         B     KVR155              ELSE ERROR RETURN                      KEYVAL
KVR160   DS    0H                                                         KEYVAL
         CLI   KVVLL001,0          Q. WAS VALUE LEN VALIDATION REQUSTED   KEYVAL
         BE    KVR169              A. NO, BYPASS LENGTH VALIDATION        KEYVAL
         LA    6,1(,6)             RESTORE FULL LENGTH                    KEYVAL
         CLM   6,1,KVVLL001        Q. WAS LOW VALUE PRECEEDED             KEYVAL
         BL    KVR168              A. YES, ERROR CONDITION                KEYVAL
         CLM   6,1,KVVLH001        Q. WAS HIGH VALUE EXCEEDED             KEYVAL
         BH    KVR168              A. YES, ERROR CONDITION                KEYVAL
         B     KVR169              VALUE LENGTH PASSED VALIDATION         KEYVAL
KVR168   DS    0H                                                         KEYVAL
         LA    9,24                SET VALUE ERROR                        KEYVAL
         B     KVR900              AND RETURN                             KEYVAL
KVR169   DS    0H                                                         KEYVAL
         IC    7,KVWAKWVA          GET CURRENT COUNT                      KEYVAL
         LA    7,1(,7)             ADD ONE                                KEYVAL
         STC   7,KVWAKWVA          AND SAVE THE COUNT                     KEYVAL
         CLI   0(4),C')'           Q. WAS IT ENDING DELIMETER             KEYVAL
         BE    KVR170              A. YES, TERMINATE SCAN                 KEYVAL
         LA    4,1(,4)             BUMP OVER COMMA                        KEYVAL
         BCTR  3,0                 REDUCE STRING LENGTH                   KEYVAL
         B     KVR120              CONTINUE FOR MORE KEYWORDS             KEYVAL
KVR170   DS    0H                  PROCESS ENDING DELIMITER               KEYVAL
         LA    4,1(,4)             BUMP OVER )                            KEYVAL
         BCTR  3,0                 REDUCE COUNT                           KEYVAL
KVR172   DS    0H                  PROCESS ENDING DELIMITER               KEYVAL
         SR    5,5                 CLEAR WORK REG                         KEYVAL
         SR    6,6                 CLEAR WORK REG                         KEYVAL
         ICM   5,1,KVHV0001        GET VALUE(S) COUNT HIGH NUMBER         KEYVAL
         BNZ   KVR180              GO HERE IF NOT ZERO COUNT              KEYVAL
         CLM   5,1,KVWAKWVA        Q. WAS COUNT FOUND ALSO ZERO           KEYVAL
         BE    KVR180              A. YES, CONTINUE WITHOUT ERROR YET     KEYVAL
KVR174   DS    0H                                                         KEYVAL
         LA    9,12                SAY COUNT EXCEEDED                     KEYVAL
         B     KVR900              AND RETURN                             KEYVAL
KVR180   DS    0H                                                         KEYVAL
         CLM   5,1,KVWAKWVA        Q. HIGH COUNT EXCEEDED                 KEYVAL
         BL    KVR174              A. YES, ERROR AND GET OUT              KEYVAL
         ICM   5,1,KVLV0001        GET LOW VALUE(S) COUNT                 KEYVAL
         BZ    KVR182              GET OUT IF ZERO COUNT                  KEYVAL
         CLM   5,1,KVWAKWVA        Q. COUNT LESS THAN LOW VALUE           KEYVAL
         BH    KVR174              A. YES, GET OUT WITH ERROR             KEYVAL
KVR182   DS    0H                                                         KEYVAL
         ICM   5,3,KVAA0001        Q. IS THERE AN AREA PROVIDED           KEYVAL
         BZ    KVR200              A. NO, JUST CONTINUE                   KEYVAL
         SR    6,6                 CLEAR WORK REG                         KEYVAL
         SR    7,7                 CLEAR WORK REG                         KEYVAL
         ICM   6,3,KVAA0001        GET WORKAREA ADDRESS                   KEYVAL
         SRDL  6,12                GET REG NO. INTO R2                    KEYVAL
         SLL   6,2                 X 4                                    KEYVAL
         LA    1,KVWASAV1          ADDR OF SAVE AREA                      KEYVAL
         L     6,0(6,1)            GET CONTENTS OF AREA BASE REG          KEYVAL
         SRL   7,20                SHIFT TO LOWER END                     KEYVAL
         LA    6,0(7,6)            GET WORKAREA ADDRESS                   KEYVAL
         LA    5,KVWAKWVA          GET MY AREA ADDRESS                    KEYVAL
         SR    2,2                 CLEAR WORK REG                         KEYVAL
         IC    2,KVWAKWVA          GET COUNT                              KEYVAL
         MVC   0(1,6),KVWAKWVA     MOVE COUNT BYTE                        KEYVAL
         LA    6,1(,6)             BUMP OVER COUNT BYTE                   KEYVAL
         LA    5,1(,5)             BUMP OVER COUNT BYTE                   KEYVAL
KVR184   DS    0H                                                         KEYVAL
         MVC   0(1,6),0(5)         MOVE FIRST LENGTH BYTE                 KEYVAL
         LTR   2,2                 WAS ANYTHING IN THE AREA               KEYVAL
         BNZ   KVR186              YES, CONTINUE WITH MOVE                KEYVAL
         MVI   KVWAKWVA,1          SAY SOMETHING WAS THERE                KEYVAL
         B     KVR200              AND CONTINUE                           KEYVAL
KVR186   DS    0H                                                         KEYVAL
         SR    7,7                 CLEAR FOR NEXT INSTR                   KEYVAL
         ICM   7,1,0(5)            GET BYTE COUNT                         KEYVAL
         BNZ   KVR188              BRANCH FOR NON ZERO MOVE               KEYVAL
         LA    6,1(,6)             BUMP OVER NULL BYTE                    KEYVAL
         LA    5,1(,5)             BUMP OVER NULL BYTE                    KEYVAL
         B     KVR194              AND CONTINUE                           KEYVAL
KVR188   DS    0H                                                         KEYVAL
         BCTR  7,0                 REDUCE BY ONE                          KEYVAL
         EX    7,KVM2VR            MOVE DATA OVER                         KEYVAL
KVR190   DS    0H                                                         KEYVAL
         LA    6,2(7,6)            BUMP OVER OUTPUT                       KEYVAL
         LA    5,2(7,5)            BUMP OVER INPUT                        KEYVAL
KVR194   DS    0H                                                         KEYVAL
         BCT   2,KVR184            COUNTINUE MOVE                         KEYVAL
KVR200   DS    0H                  SEE IF VALUES TO BE COMPARED           KEYVAL
         SPACE                                                            KEYVAL
*        AT THIS POINT THE VALUES HAVE BEEN MOVED TO MY WORKAREA          KEYVAL
*        AND TO THE USER WORK AREA IF AREA HAS BEEN SPECIFIED             KEYVAL
*        IN THE KEYT MACRO                                                KEYVAL
         SPACE                                                            KEYVAL
         SR    2,2                 CLEAR WORK REG                         KEYVAL
         ICM   2,1,KVVN0001        Q. WERE ANY VALUES TO BE VERIFIED      KEYVAL
*                                     OR WAS THERE WORK TO BE DONE        KEYVAL
         BNZ   KVR220              A. YES, CONTINUE AS USUAL              KEYVAL
         SR    9,9                 CLEAR RC                               KEYVAL
         B     KVR900              AND RETURN TO CALLER                   KEYVAL
KVR220   DS    0H                                                         KEYVAL
         ST    2,KVVNCNT           SAVE COUNT                             KEYVAL
         ST    14,KVWAR14          SAVE KEYT ADDR                         KEYVAL
         LA    5,KVWAKWVA+1        ADDR OF FOUND VALUES                   KEYVAL
         ST    5,KVWASV            SAVE VALUES ADDR                       KEYVAL
         MVC   KVWASVC,KVWAKWVA    SAVE VALUES COUNT                      KEYVAL
         SR    2,2                 CLEAR REG                              KEYVAL
         ICM   2,1,KVWASVC         Q. ANY VALUES FOUND                    KEYVAL
         BNZ   KVR230              A. YES, CONTINUE WITH COUNT            KEYVAL
         MVI   KVWASVC,1           ELSE SAY AT LEAST ONE FOUND            KEYVAL
KVR230   DS    0H                                                         KEYVAL
         L     2,KVVNCNT           GET COUNT                              KEYVAL
         L     14,KVWAR14          GET TABLE ADDR                         KEYVAL
         L     5,KVWASV            GET ADDR OF CURRENT VALUE TO FIND      KEYVAL
         SR    1,1                 CLEAR WORK REG                         KEYVAL
         ICM   1,1,KVWASVC         GET COUNT                              KEYVAL
         BZ    KVR500              STOP SCAN IF NONE LEFT                 KEYVAL
         BCTR  1,0                 REDUCE COUNT                           KEYVAL
         STC   1,KVWASVC           SAVE NEW COUNT                         KEYVAL
         ICM   1,1,0(5)            GET LENGTH OF CURRENT VALUE            KEYVAL
         LA    1,1(1,5)            BUMP TO WHERE NEW VALUE WILL BE        KEYVAL
         ST    1,KVWASV            AND SAVE ADDRESS                       KEYVAL
         SR    7,7                 CLEAR FOR LATER                        KEYVAL
         IC    7,KVLL0001          GET KEYWORD LENGTH                     KEYVAL
         LA    14,0(14,7)          BUMP OVER VARIABLE LENGTH KEYWORD      KEYVAL
KVR240   DS    0H                  SCAN TABLE FOR THIS VALUE              KEYVAL
         ST    2,KVWAR2            SAVE CURRENT COUNT                     KEYVAL
         ICM   7,1,KVVL0001        LENGTH OF VALUE                        KEYVAL
         BZ    KVR300              IF NO KEYWORD VALUE HAS BEEN PUT       KEYVAL
*                                  IN KEYT DONT LOOK FOR A MATCH, JUST    KEYVAL
*                                  DO THE WORK IF ANY                     KEYVAL
         BCTR  7,0                 REDUCE FOR EX INSTR                    KEYVAL
         EX    7,KVC2VR            Q. ARE VALUES EQUAL                    KEYVAL
         BE    KVR300              A. YES, GO SEE IF ANY WORK TO DO       KEYVAL
KVR250   DS    0H                                                         KEYVAL
         L     2,KVWAR2            RELOAD LOOP COUNTER                    KEYVAL
         SR    1,1                 CLEAR WORK REG                         KEYVAL
         IC    1,KVVL0001          GET LENGTH OF VALUE                    KEYVAL
         LA    14,KVVG0001(1,14)   GO TO NEXT ENTRY                       KEYVAL
         BCT   2,KVR240            AND CONTINUE SCAN                      KEYVAL
         CLI   KVWAVF,X'FF'        Q. WERE VALUE(S) MATCHED IN KEYT       KEYVAL
         BE    KVR260              A. YES, CONTINUE WITH NO ERRORS        KEYVAL
         LA    9,4                 VALUE NOT FOUND RC                     KEYVAL
         B     KVR900              AND RETURN                             KEYVAL
KVR260   DS    0H                                                         KEYVAL
         MVI   KVWAVF,0            RESET VALUES FOUND FLAG                KEYVAL
         B     KVR230              AND CONTINUE WITH OTHER VALUES         KEYVAL
KVR300   DS    0H                  VALUE FOUND SEE IF ANY WORK TO DO      KEYVAL
         MVI   KVWAVF,X'FF'        SAY VALUES FOUND                       KEYVAL
         TM    KVOP0001,X'FF'      Q. ANY OP CODES SPECIFIED              KEYVAL
         BNZ   KVR320              A. YES, SEE IF SOME WORK TO DO         KEYVAL
         B     KVR250              ELSE CONTINUE VALUES SCAN              KEYVAL
KVR320   DS    0H                  DECODE OP CODES AND DO THEM            KEYVAL
         LA    6,KVVV0001          GET ADDR OF VALUES ENTRY               KEYVAL
         LA    1,KVWAFLN           ADDR OF SAVE TABLE                     KEYVAL
         ICM   7,15,KVWACN1        GET CURRENT COUNT OF VALUES ALREADY    KEYVAL
*                                  PROCESSED                              KEYVAL
         BZ    KVR326              ADD THIS ONE IF NONE YET               KEYVAL
KVR322   DS    0H                                                         KEYVAL
         C     6,0(1)              Q. ALREADY PROCESSED THIS ENTRY        KEYVAL
         BE    KVR250              A. YES BYPASS FURTHER PROCESSING       KEYVAL
         LA    1,4(,1)             BUMP TABLE ADDRESS                     KEYVAL
         BCT   7,KVR322            CONTINUE SCAN                          KEYVAL
KVR326   DS    0H                                                         KEYVAL
         ST    6,0(1)              SAVE ADDRESS OF THIS ENTRY FOR LATER   KEYVAL
*                                  COMPARE OF OTHER VALUES                KEYVAL
         L     7,KVWACN1           GET OLD COUNT                          KEYVAL
         LA    7,1(,7)             ADD 1 TO IT                            KEYVAL
         ST    7,KVWACN1           AND SAVE IT                            KEYVAL
         SR    6,6                 CLEAR WORK REG                         KEYVAL
         SR    7,7                 CLEAR WORK REG                         KEYVAL
         ICM   6,3,KVTO0001        GET TO ADDRESS                         KEYVAL
         SRDL  6,12                GET REG NO. INTO R2                    KEYVAL
         SLL   6,2                 X 4                                    KEYVAL
         LA    1,KVWASAV1          ADDR OF SAVE AREA                      KEYVAL
         L     6,0(6,1)            GET CONTENTS OF TO BASE REG            KEYVAL
         SRL   7,20                SHIFT TO LOWER END                     KEYVAL
         LA    2,0(7,6)            GET TO ADDREESS                        KEYVAL
         SR    6,6                 CLEAR WORK REG                         KEYVAL
         SR    7,7                 CLEAR WORK REG                         KEYVAL
         ICM   6,3,KVFR0001        GET FROM ADDRESS                       KEYVAL
         SRDL  6,12                GET REG NO. INTO R6                    KEYVAL
         SLL   6,2                 X 4                                    KEYVAL
         LTR   6,6                 Q. WAS THIS SELF DEFINING TERM         KEYVAL
         BZ    KVR330              A. YES, DONT LOAD NONEXISTENT BASE     KEYVAL
         LA    1,KVWASAV1          ADDR OF SAVE AREA                      KEYVAL
         L     6,0(6,1)            GET CONTENTS OF FROM BASE REG          KEYVAL
KVR330   DS    0H                                                         KEYVAL
         SRL   7,20                SHIFT TO LOWER END                     KEYVAL
         LA    8,0(7,6)            GET FROM ADDRESS                       KEYVAL
         SR    1,1                 CLEAR WORK REG                         KEYVAL
         ICM   1,3,KVOL0001        GET LENGTH OF FIELD                    KEYVAL
         BCTR  1,0                 GET EX LENGTH                          KEYVAL
         SR    7,7                 CLEAR WORK REG                         KEYVAL
         IC    7,KVOP0001          GET OP CODE                            KEYVAL
         SLL   7,2                 MULTIPLY BY 4                          KEYVAL
         B     *+0(7)              BRANCH TO OP CODE ROUTINE              KEYVAL
         B     KVR360              N OP CODE SPECIFIED                    KEYVAL
         B     KVR380              O OP CODE SPECIFIED                    KEYVAL
         B     KVR400              M OP CODE SPECIFIED                    KEYVAL
         B     KVR420              X OP CODE SPECIFIED                    KEYVAL
KVR360   DS    0H                  N OP CODE                              KEYVAL
         LTR   6,6                 Q. WAS FROM FIELD SELF DEFINING        KEYVAL
         BNZ   KVR366              A. NO, CONTINUE STOR-STOR OPERATION    KEYVAL
         EX    8,KVOPNI            ISSUE THE AND OP CODE STOR-IMED        KEYVAL
         B     KVR250              AND CONTINUE WITH OTHER VALUES         KEYVAL
KVR366   DS    0H                  N OP CODE                              KEYVAL
         EX    1,KVOPN             ISSUE THE AND OP CODE STOR-STOR        KEYVAL
         B     KVR250              AND CONTINUE WITH OTHER VALUES         KEYVAL
KVR380   DS    0H                  O OP CODE                              KEYVAL
         LTR   6,6                 Q. WAS FROM FIELD SELF DEFINING        KEYVAL
         BNZ   KVR386              A. NO, CONTINUE STOR-STOR OPERATION    KEYVAL
         EX    8,KVOPOI            ISSUE THE OR OP CODE STOR-IMED         KEYVAL
         B     KVR250              AND CONTINUE WITH OTHER VALUES         KEYVAL
KVR386   DS    0H                  N OP CODE                              KEYVAL
         EX    1,KVOPO             ISSUE THE OR OP CODE STOR-STOR         KEYVAL
         B     KVR250              AND CONTINUE WITH OTHER VALUES         KEYVAL
KVR400   DS    0H                  M OP CODE                              KEYVAL
         LTR   6,6                 Q. WAS FROM FIELD SELF DEFINING        KEYVAL
         BNZ   KVR406              A. NO, CONTINUE STOR-STOR OPERATION    KEYVAL
         EX    8,KVOPMI            ISSUE THE MVC OP CODE STOR-IMED        KEYVAL
         B     KVR250              AND CONTINUE WITH OTHER VALUES         KEYVAL
KVR406   DS    0H                  N OP CODE                              KEYVAL
         EX    1,KVOPM             ISSUE THE MVC OP CODE STOR-STOR        KEYVAL
         B     KVR250              AND CONTINUE WITH OTHER VALUES         KEYVAL
KVR420   DS    0H                  N OP CODE                              KEYVAL
         LTR   6,6                 Q. WAS FROM FIELD SELF DEFINING        KEYVAL
         BNZ   KVR426              A. NO, CONTINUE STOR-STOR OPERATION    KEYVAL
         EX    8,KVOPXI            ISSUE THE XI OP CODE STOR-IMED         KEYVAL
         B     KVR250              AND CONTINUE WITH OTHER VALUES         KEYVAL
KVR426   DS    0H                  N OP CODE                              KEYVAL
         EX    1,KVOPX             ISSUE THE XC OP CODE STOR-STOR         KEYVAL
         B     KVR250              AND CONTINUE WITH OTHER VALUES         KEYVAL
KVR500   DS    0H                  ALL VALUES SCANNED                     KEYVAL
         SR    9,9                 CLEAR RC                               KEYVAL
         B     KVR900              AND RETURN                             KEYVAL
KVR900   DS    0H                  RETURN POINT                           KEYVAL
         LTR   9,9                 Q . RC=00                              KEYVAL
         BNZ   KVR930              A. NO, CONTINUE RETURN TO CALLER       KEYVAL
         ICM   1,15,KVWARTN        Q. RTN TO CALL                         KEYVAL
         BZ    KVR910              A. NO, CONTINUE NO CALL                KEYVAL
         ST    14,KVWAR14          SAVE REG 14                            KEYVAL
         LR    15,1                GET RTN ADDR                           KEYVAL
         LA    0,KVWAKWVA          GET STRING ADDR                        KEYVAL
         ST    13,KVWASAV+4        SAVE CALLERS SAVE AREA ADDR            KEYVAL
         LA    13,KVWASAV          MY SAVE AREA                           KEYVAL
         L     1,KVWAPRM           GET PARM DATA ADDR                     KEYVAL
         BALR  14,15               CALL USERS ROUTINE                     KEYVAL
         L     14,KVWAR14          RESTORE REG 14                         KEYVAL
         L     13,KVWASAV+4        RESTORE REG 13                         KEYVAL
KVR910   DS    0H                                                         KEYVAL
         MVI   KVWAFL1,1           SAY AT LEAST 1 GOOD FIND               KEYVAL
         B     KVR004              GO DO MORE NOW                         KEYVAL
KVR930   DS    0H                                                         KEYVAL
         L     4,KVWALOC           SAVE STRING LOCATION                   KEYVAL
         L     5,KVWAKEY           SAVE KEYWORD LOCATION                  KEYVAL
         CLI   KVWAFL1,0           Q. ANY GOOD FINDS                      KEYVAL
         BE    KVR950              A. NO, LEAVE RC AS IS                  KEYVAL
         C     9,KVASIX            Q. WAS RC=16                           KEYVAL
         BNE   KVR950              A. NO, LEAVE RC AS IS                  KEYVAL
         SR    9,9                 SET ZERO RC                            KEYVAL
KVR950   DS    0H                                                         KEYVAL
         LR    1,12                GET WORKAREA ADDRESS                   KEYVAL
         LA    0,KVWALNTH          GET WORKAREA LENGTH                    KEYVAL
         FREEMAIN R,LV=(0),A=(1)   FREE WORKAREA                          KEYVAL
         LR    15,9                SETUP RETURN CODE                      KEYVAL
         LM    6,12,11*4(13)       RESTORE REGS                           KEYVAL
         LR    0,5                 PUT IN KEYWORD LOCATION                KEYVAL
         LR    1,4                 PUT IN STRING LOCATION                 KEYVAL
         LM    2,5,7*4(13)         RESTORE OTHER REGS                     KEYVAL
         L     14,3*4(13)          RESTORE RETURN REG                     KEYVAL
         BR    14                  RETURN TO CALLER                       KEYVAL
         SPACE 4                                                          KEYVAL
KVOPN    NC    0(0,2),0(8)                                                KEYVAL
KVOPNI   NI    0(2),0                                                     KEYVAL
KVOPO    OC    0(0,2),0(8)                                                KEYVAL
KVOPOI   OI    0(2),0                                                     KEYVAL
KVOPM    MVC   0(0,2),0(8)                                                KEYVAL
KVOPMI   MVI   0(2),0                                                     KEYVAL
KVOPX    XC    0(0,2),0(8)                                                KEYVAL
KVOPXI   XI    0(2),0                                                     KEYVAL
KVCVR    CLC   KVKV0001(0),0(4)                                           KEYVAL
KVTR1VR  TRT   0(0,4),KTVRT1-X'40'                                        KEYVAL
KVTR2VR  TRT   1(0,8),KTVRT3-X'40' ALPHA TEST                             KEYVAL
KVTR3VR  TRT   1(0,8),KTVRT2-X'40' NUMERIC TEST                           KEYVAL
KVTR4VR  TRT   1(0,8),KTVRT4-X'40' ALPHA/NUMERIC TEST                     KEYVAL
KVC2VR   CLC   KVVV0001(0),1(5)                                           KEYVAL
KVM1VR   MVC   1(0,8),KVWATMP                                             KEYVAL
KVM2VR   MVC   1(0,6),1(5)                                                KEYVAL
KTVRT1   DC    10X'00',C'.<(+|&&',9X'00',C'!$*);-/',10X'00',C'%_>?'     KEYVAL
         DC    9X'00',C''':#@ ="',X'00',C'ABCDEFGHI',7X'00'               KEYVAL
         DC    C'JKLMNOPQR',8X'00',C'STUVWXYZ',23X'00',C'ABCDEFGHI'       KEYVAL
         DC    7X'FF'                                                     KEYVAL
         DC    C'ABCDEFGHI',7X'00',C'JKLMNOPQR',8X'00',C'STUVWXYZ'        KEYVAL
         DC    6X'00',C'0123456789'                                       KEYVAL
KTVRT2   DC    176X'FF',10X'00'                                           KEYVAL
KTVRT3   DC    129X'FF',41X'00',16X'FF'                                   KEYVAL
KTVRT4   DC    11X'FF',X'00',15X'FF',2X'00',30X'FF',2X'00'                KEYVAL
         DC    68X'FF',63X'00'                                            KEYVAL
KVAKVVG  DC    Y(KVVG0001)                                                KEYVAL
KVAFFFF  DC    X'FFFF'                                                    KEYVAL
KVASIX   DC    F'16'                                                      KEYVAL
KVAX00   DC    X'00'                                                      KEYVAL
KVASLASH DC    C'/'                                                       KEYVAL
KVDSVR   DSECT                                                            KEYVAL
KVLL0001 DS    AL1                 LENGTH OF KEY-1                        KEYVAL
KVRA0001 DS    SL2                 RTN ADDRESS                            KEYVAL
KVRP0001 DS    SL2                 RTN PARM                               KEYVAL
KVAA0001 DS    SL2                 AREA ADDRESS                           KEYVAL
KVVLL001 DS    AL1                 VALUE LENGTH LOW                       KEYVAL
KVVLH001 DS    AL1                 VALUE LENGTH HIGH                      KEYVAL
KVLV0001 DS    AL1                 VALUE COUNT LOW                        KEYVAL
KVHV0001 DS    AL1                 VALUE COUNT HIGH                       KEYVAL
KVVT0001 DS    XL1                 VALUE TYPE                             KEYVAL
KVTA0001 EQU   X'01'               ALPHA TYPE                             KEYVAL
KVTN0001 EQU   X'02'               NUMERIC TYPE                           KEYVAL
KVTB0001 EQU   X'03'               ALPHA OR NUMERIC TYPE                  KEYVAL
KVVN0001 DS    AL1                 COUNT OF VALUE ENTRIES                 KEYVAL
KVKL0001 EQU   *-KVLL0001          LENGTH OF TABLE ENTRY MINUS KEYWORD    KEYVAL
KVKV0001 DS    C                   KEYWORD                                KEYVAL
KVVL0001 DS    AL1                 VALUE LENGTH                           KEYVAL
KVOP0001 DS    XL1                 OP CODE                                KEYVAL
KVVA0001 EQU   X'01'               N  OP CODE                             KEYVAL
KVVO0001 EQU   X'02'               O  OP CODE                             KEYVAL
KVVM0001 EQU   X'03'               M  OP CODE                             KEYVAL
KVVX0001 EQU   X'04'               X  OP CODE                             KEYVAL
KVTO0001 DS    SL2                 TO ADDRESS                             KEYVAL
KVFR0001 DS    SL2                 FROM ADDRESS                           KEYVAL
KVOL0001 DS    AL2                 LENGTH OF TO FIELD                     KEYVAL
KVVG0001 EQU   *-KVVL0001          ENTRY LENGTH MINUS VALUE CONSTANT      KEYVAL
KVVV0001 DS    C                   VALUE CONSTANT                         KEYVAL
KVLA0001 EQU   *,2                 LAST ENTRY =X'FFFF'                    KEYVAL
KVWAVR   DSECT                                                            KEYVAL
KVWAVF   DS    X                   VALUES FOUND FLAG                      KEYVAL
KVWAQUO  DS    X                   QUOTED STRING MODE FLAG                KEYVAL
KVWASV   DS    F                   ADDR OF VALUES FOUND                   KEYVAL
KVWASAV  DS    18F                 SAVE AREA TO CALL RTN                  KEYVAL
KVWARTN  DS    F                   ADDRESS OF RTN TO CALL                 KEYVAL
KVWAPRM  DS    F                   ADDRESS OF RTN PARM                    KEYVAL
KVWALOC  DS    F                   LOCATION OF INPUT STRING               KEYVAL
KVWAR14  DS    F                   SAVE AREA FOR REG 14                   KEYVAL
KVWAR2   DS    F                   SAVE AREA FOR REG 2                    KEYVAL
KVVNCNT  DS    F                   VALUE COUNT SAVEAREA                   KEYVAL
KVWAKEY  DS    F                   ADDRESS OF CURRENT KEYWORD IN STRING   KEYVAL
KVWACN1  DS    F                   COUNT OF VALUE ADDRS IN KVWAFLN        KEYVAL
KVWASVC  DS    X                   COUNT OF VALUES FOUND                  KEYVAL
KVWACLR  EQU   *-KVWAVR            LENGTH OF WORKAREA TO CLEAR            KEYVAL
KVWAFL1  DS    X                   FLAG TO SHOW SOME GOOD WORDS FOUND     KEYVAL
KVWASAV1 DS    16F                 SAVE AREA FOR CALLERS REGS             KEYVAL
KVWAKWVA DS    512X                WORKAREA FOR VALUE MOVE                KEYVAL
KVWAFLN  DS    15XL4               SAVEAREA FOR MAX COUNT OF FIELDN'S     KEYVAL
KVWATMP  DS    256X                WORKAREA FOR TEMP VALUE MOVE           KEYVAL
KVWALNTH EQU   *-KVWAVR            LENGTH OF WORKAREA TO ALLOC            KEYVAL
&CSNAME  CSECT                                                            KEYVAL
         POP   USING,PRINT                                                KEYVAL
.NOGO    ANOP                                                             KEYVAL
         MEND                                                             KEYVAL
