BKSRV    TITLE 'BOOKSERVER FRONT-END SERVICE PROGRAM'
***********************************************************************
*                                                                     *
*        CCCBKSRV                                                     *
*                                                                     *
*        THIS PROGRAM IS DESIGNED TO BE INVOKED FROM REXX VIA THE     *
*        "LINKMVS" FACILITY.  IT WILL PERFORM A VARIETY OF FUNCTIONS  *
*        BASED ON THE PARAMETERS PASSED TO IT.                        *
*                                                                     *
*        PARAMETERS ARE PASSED IN THE REXX LINKMVS FORMAT. EACH       *
*        ONE CONSISTS OF A HALFWORD LENGTH FOLLOWED BY THE            *
*        PARAMETER IN CHARACTER FORMAT.                               *
*                                                                     *
*        PARAMETER 1: FUNCTION                                        *
*              'BKSC' - COUNT OF BOOKSHELVES                          *
*              'BOOC' - COUNT OF UNIQUE BOOKS                         *
*              'SRCH' - SEARCH BOOKS                                  *
*                                                                     *
*        PARAMETER 2: RETURN VARIABLE STEM PREFIX                     *
*                                                                     *
*        PARAMETER 3: SEARCH ARGUMENTS                                *
*                                                                     *
*        PARAMETER 4: STARTING HIT NUMBER                             *
*                                                                     *
*        PARAMETER 5: ENDING HIT NUMBER                               *
*                                                                     *
*        RETURNED DATA:                                               *
*              FOR THE 'BKSC' AND 'BOOC' FUNCTIONS, THE RETURN        *
*              CODE IS THE REQUESTED NUMBER.                          *
*              FOR THE 'SRCH' FUNCTION, A NON-ZERO RETURN CODE        *
*              INDICATES AN ERROR AND NOTHING ELSE IS RETURNED.       *
*              WITH A ZERO RETURN CODE FROM THE 'SRCH' FUNCTION,      *
*              A SERIES OF REXX VARIABLES ARE CREATED USING THE       *
*              STEM PREFIX PASSED TO THIS PROGRAM.  THESE ARE:        *
*                                                                     *
*              STEM.SEARCHARGUMENTS                                   *
*                   THIS STRING CONTAINS THE SEARCH ARGUMENTS         *
*                   REFORMATED TO BE AN UPPER CASE VERSION OF         *
*                   WHAT THE USER ENTERED (%XX TRANSLATED, +'S        *
*                   CONVERTED TO BLANKS).                             *
*                                                                     *
*              STEM.SEARCHHITS                                        *
*                   THIS IS THE TOTAL NUMBER OF UNIQUE BOOK HITS      *
*                   THAT HAVE BEEN FOUND, REGARDLESS OF THE           *
*                   ACTUAL NUMBER BEING RETURNED.                     *
*                                                                     *
*              STEM.SEARCHRETURN                                      *
*                   THIS IS THE COUNT OF UNIQUE BOOK HITS BEING       *
*                   RETURNED, I.E., THE NUMBER OF UNIQUE BOOK         *
*                   HITS BEING RETURNED IN THE STEM.I VARIABLES.      *
*                   IN THE ABSENSE OF THE HIT LIMITS, THIS WOULD BE   *
*                   THE SAME AS STEM.SEARCHHITS.                      *
*                                                                     *
*              STEM.0                                                 *
*                   THIS IS THE COUNT OF "STEM.I" VARIABLES           *
*                   BEING RETURNED.                                   *
*                                                                     *
*              STEM.I (I = 1 TO STEM.0)                               *
*                   ONE OF THESE VARIABLES IS RETURNED FOR EACH       *
*                   BOOK SEARCH MATCH RETURNED.  THIS INCLUDES        *
*                   DUPLICATE HITS FOR BOOKS THAT ARE LOCATED IN      *
*                   MORE THAN ONE BOOKSHELF.                          *
*                                                                     *
*        ATTRIBUTES - REENTRANT, REFRESHABLE, AMODE 31, RMODE ANY,    *
*                     UNAUTHORIZED                                    *
*                                                                     *
***********************************************************************
         EJECT
*
         PUNCH '         PAGE  CCCBKSRV'  MAKE DEBUGGING EASIER
*
CCCBKSRV CSECT
CCCBKSRV AMODE 31
CCCBKSRV RMODE ANY
*
         USING CCCBKSRV,R15
         B     AROUNDID
         DC    CL8'CCCBKSRV'
         DC    CL8' V1.1.1'
         DC    CL8'&SYSDATC'
         DC    CL8' &SYSTIME'
AROUNDID DS    0H
         STM   R14,R12,12(R13)     SAVE THE REGISTERS
*
         LR    R12,R15             LOAD REGISTER 12 WITH ENTRY
         DROP  R15
         USING CCCBKSRV,R12        ESTABLISH ADDRESSABILITY
*
         LR    R8,R1               LOAD REGISTER 8 TO PRESERVE PARMADDR
*
         L     R2,=F'-1'           LOAD NEGATIVE RETURN CODE
         L     R15,CVTPTR          GET THE CVT
         USING CVT,R15
         L     R15,CVTJESCT        GET THE JES CONTROL TABLE
         USING JESCT,R15
         L     R15,JESSSCT         LOAD THE FIRST SSCT ADDRESS
         USING SSCT,R15
*
FINDSSCT DS    0H
         CLC   SSCTSNAM,=A(BKASSNAM)    LOOK FOR OUR SUBSYSTEM
         BE    FOUNDSCT            BRANCH IF SO
         ICM   R15,15,SSCTSCTA     GET THE NEXT SSCT
         BNZ   FINDSSCT            AND LOOP
         B     QUIKEXIT            ERROR IF WE CAN'T FIND OUT SSCT
*
FOUNDSCT DS    0H
         ICM   R10,15,SSCTSUSE     LOAD THE ANCHOR BLOCK POINTER
         BZ    QUIKEXIT            LEAVE IF IT'S NOT THERE
*
         DROP  R15
         USING BKADSECT,R10
*
         CLC   BKABLKID,=A(BKAIDEQU)    CHECK IF IT'S THE REAL THING
         BNE   QUIKEXIT            AND LEAVE IF WE DON'T HAVE IT
*
         EJECT
***********************************************************************
*                                                                     *
*        CHECK FOR QUICK FUNCTIONS THAT DON'T REQUIRE GETTING         *
*        A SAVE AREA AND SO ON.                                       *
*                                                                     *
***********************************************************************
*
         L     R1,0(,R8)           GET THE FIRST PARAMETER PASSED
         LH    R0,0(,R1)           LOAD THE PARAMETER LENGTH
         CH    R0,=H'4'            THIS PARAMETER MUST BE FOUR (ALWAYS)
         BNE   QUIKEXIT            LEAVE QUICKLY IF NOT
*
         ICM   R7,15,2(R1)         LOAD FUNCTION CODE
         O     R7,BLANKS           MAKE SURE IT IS UPPER CASE
*
         CLM   R7,15,=C'BKSC'      IS THIS THE BOOKSHELF COUNT REQUEST
         BNE   NOTBKSC             BRANCH IF NOT
*
         L     R9,BKABKSEX         LOAD THE BOOKSHELF BLOCK ADDRESS
         USING BKEDSECT,R9
*
         L     R2,BKEUNQUE         LOAD THE UNIQUE COUNT OF BOOKSHELVES
         B     QUIKEXIT            AND LEAVE WITH THIS AS RETURN CODE
*
         DROP  R9
*
NOTBKSC  DS    0H
         CLM   R7,15,=C'BOOC'      IS THIS THE BOOK COUNT REQUEST
         BNE   NOTBOOC             BRANCH IF NOT
*
         L     R9,BKABOOEX         LOAD THE BOOKSHELF BLOCK ADDRESS
         USING BKEDSECT,R9
*
         L     R2,BKEUNQUE         LOAD THE UNIQUE COUNT OF BOOKSHELVES
         B     QUIKEXIT            AND LEAVE WITH THIS AS RETURN CODE
*
         DROP  R9
*
NOTBOOC  DS    0H
         CLM   R7,15,=C'SRCH'      CHECK FOR THE REMAINING FUNCTION
         BNE   QUIKEXIT            LEAVE IF NOT
*
         TM    0(R8),X'80'         TEST FOR ONLY ONE ARGUMENT
         BO    QUIKEXIT            ERROR IF SO, DO QUICK RETURN
         TM    4(R8),X'80'         TEST FOR ONLY TWO ARGUMENTS
         BO    QUIKEXIT            ERROR IF SO, DO QUICK RETURN
*
         EJECT
         LA    R0,WORKLENG         LOAD LENGTH OF GETMAINED AREA
         GETMAIN RU,LV=(0),LOC=ANY
         LR    R14,R1              COPY FOR THE MOVE LONG
         LA    R15,WORKLENG        GET THE LENGTH
         SLR   R3,R3
         MVCL  R14,R2              CLEAR THE AREA
*
         ST    R13,4(,R1)          STORE ADDRESS OF PREVIOUS
*
         ST    R1,8(,R13)          STORE ADDRESS OF CURRENT
*
         LR    R13,R1              LOAD REGISTER 13 WITH SAVEAREA
*
         USING WORKAREA,R13        ESTABLISH ADDRESSABILITY
*
***********************************************************************
*                                                                     *
*        INITIALIZE THE BASE WORK AREA                                *
*                                                                     *
***********************************************************************
*
         STCM  R7,15,FUNCCODE      SAVE THE FUNCTION CODE
         ST    R2,RETCODE          ASSUME BAD RETURN CODE
*
         MVC   HITBLOCK(HITBLOCL),HITMODEL SET UP THE HIT POOL
         MVC   SRCBLOCK(SRCBLOCL),SRCMODEL SET UP THE SEARCH POOL
*
         LOAD  EPLOC=CPSNAME,ERRET=RETURN LOAD THE CELL POOL ROUTINE
         ST    R0,CPSADDR          SAVE THIS ADDRESS
*
         LOAD  EPLOC=EXCOMID       GET ADDRESS OF IRXEXCOM
         ST    R0,AIRXEXCM         STORE FOR LATER
*
         LOAD  EPLOC=BPXWRNAM      GET ADDRESS OF BPX1WRT
         ST    R0,BPXWRITE         STORE FOR LATER
*
         LA    R1,EXCOMID          POINT TO IRXEXCOM ID
         ST    R1,AEXCOMP1         STORE IN PARM LIST
         LA    R1,ZERO             POINT TO FULLWORD ZERO
         ST    R1,AEXCOMP2         STORE IN PARM LIST
         ST    R1,AEXCOMP3         STORE IN PARM LIST
         ST    R1,AEXCOMP4         STORE IN PARM LIST (SHVBLOCK)
         ST    R1,AEXCOMP5         STORE IN PARM LIST
         LA    R1,EXCOMRC          POINT TO RETURN CODE
         ST    R1,AEXCOMP6         STORE IN PARM LIST
         OI    AEXCOMP6,X'80'      INDICATE END OF LIST
*
         LA    R1,OUTFILED         POINT AT FILE DESCRIPTOR
         ST    R1,BPXWRFD          STORE IN PARAMETER LIST
         LA    R0,STDOUT_FILENO    GET STANDARD OUT FILE NUMBER
         ST    R0,OUTFILED         STORE THE FILE NUMBER
*
         LA    R1,OUTBUFAD         POINT AT BUFFER ADDRESS ADDRESS
         ST    R1,BPXWRBUF         STORE IN PARAMETER LIST
         LA    R0,OUTBUFFR         GET OUTPUT BUFFER ADDRESS
         ST    R0,OUTBUFAD         STURE THE BUFFER ADDRESS
*
         LA    R1,ZERO             POINT AT A ZERO
         ST    R1,BPXWRALT         STORE THE ALTER ADDRESS
*
         LA    R1,OUTBUFLN         POINT AT BUFFER LENGTH
         ST    R1,BPXWRBFL         STORE IN PARAMETER LIST
*
         LA    R1,OUTBUFRV         POINT AT RETURN VALUE WORD
         ST    R1,BPXWRRVL         STORE IN PARAMETER LIST
         LA    R1,OUTRETCD         POINT AT RETURN CODE WORD
         ST    R1,BPXWRRET         STORE IN PARAMETER LIST
         LA    R1,OUTRSNCD         POINT AT REASON CODE
         ST    R1,BPXWRRSN         STORE IN PARAMETER LIST
*
         OI    BPXWRLST,X'80'      FLAG LAST PARAMETER
*
         LA    R0,1                SET DEFAULT STARTING HIT NUMBER
         ST    R0,HITMIN           SET THIS
         MVC   HITMAX,=F'16777215' SET DEFAULT MAXIMUM HIT NUMBER
*
         EJECT
***********************************************************************
*                                                                     *
*        REQUEST THAT THE COMMON STORAGE TABLES BE LOADED INTO        *
*        MEMORY TO MINIMIZE PAGE FAULTING WHEN SEARCHING THE          *
*        RESIDENT BOOK TABLES.                                        *
*                                                                     *
***********************************************************************
*
         LA    R7,BKAANCHR         POINT AT FIRST ANCHOR WORD
         LA    R6,BKAANUMB         GET NUMBER OF ANCHOR WORDS
*
PGSERLP  DS    0H
         ICM   R1,15,0(R7)         GET THE ANCHOR WORD
         BZ    PGSERNXT            BRANCH IF ZERO
         USING BKEDSECT,R1
*
         L     R15,BKELENG         LOAD THE LENGTH OF THE AREA
         BCTR  R15,0               BACK UP ONE
         AR    R15,R1              ADD THE STARTING ADDRESS
         DROP  R1
*
         PGSER R,LOAD,A=(1),EA=(15)     REQUEST THE PAGES IN
*
PGSERNXT DS    0H
         LA    R7,4(,R7)           INCREMENT TO NEXT ARCHOR WORD
         BCT   R6,PGSERLP          AND LOOP THROUGH THE ANCHORS
*
         EJECT
***********************************************************************
*                                                                     *
*        EXTRACT THE REMAINING PROGRAM PARAMETERS                     *
*                                                                     *
***********************************************************************
*
         L     R1,4(,R8)           GET THE SECOND PARAMETER (STEM)
         LH    R15,0(,R1)          LOAD THE PARAMETER LENGTH
         LTR   R15,R15             CHECK FOR NON-NULL
         BZ    PARMERR2            RETURN IN ERROR IF NULL
         CH    R15,=Y(L'STEMPREF)  CHECK MAXIMUM LENGTH
         BH    PARMERR2            THIS IS ALSO NOT GOOD
*
         MVC   STEMPREF,BLANKS     BLANK OUT STEM VARIABLE NAME
         STH   R15,STEMPREL        STORE THE STEM PREFIX LENGTH
         BCTR  R15,0               DECREMENT FOR OC
         EX    R15,STEMOCOC        MOVE THE STEM AND UPPER CASE IT
*
         LA    R15,STEMPREF(R15)   POINT AT LAST CHARACTER
         CLI   0(R15),C'.'         CHECK FOR DELIMITER
         BNE   PARMERR2            ERROR IF NO PERIOD
*
         L     R1,8(,R8)           GET THE THIRD PARAMETER (SRC ARGS)
         LH    R15,0(,R1)          LOAD THE PARAMETER LENGTH
         LTR   R15,R15             CHECK FOR NON-NULL
         BZ    PARMERR3            RETURN IN ERROR IF NULL
         CH    R15,=Y(L'SRCHARGS)  CHECK MAXIMUM LENGTH
         BH    PARMERR3            THIS IS ALSO NOT GOOD
*
         MVC   SRCHARGS,BLANKS     BLANK OUT RAW SEARCH ARGUMENTS
         STH   R15,SRCHARGL        STORE THE ARGUMENT LENGTH
         BCTR  R15,0               DECREMENT FOR OC
         EX    R15,ARGSOCOC        MOVE THE STEM AND UPPER CASE IT
*
         CLI   SRCHARGS,C' '       MAKE SURE THAT IT ISN'T BLANK
         BE    PARMERR3            ERROR IF SO
*
         TM    8(R8),X'80'         TEST FOR ONLY THREE PARMS
         BO    PARMDONE            IF SO, NO HIT LIMITS
         TM    12(R8),X'80'        TEST FOR ONLY FOUR PARAMETERS
         BO    PARMERR4            ERROR - MUST HAVE BOTH PARM 4 AND 5
*
         L     R1,12(,R8)          LOAD THE HIT MIN PARAMETER
         LH    R15,0(,R1)          LOAD THE LENGTH OF THE HIT MIN
         LTR   R15,R15             TEST FOR NULL
         BZ    PARMERR4            ERROR IF NULL
         CH    R15,=H'8'           CHECK AGAINST LOGICAL MAXIMIM
         BH    PARMERR4            ERROR IF TOO LARGE
*
         BCTR  R15,0               DECREMENT FOR VARIOUS INSTRUCTIONS
         EX    R15,HITMMVC1        COPY THE PARM TO THE WORK AREA
         EX    R15,HITMNCNC        CLEAR OUT THE NUMERICS
         EX    R15,HITMCLC1        CHECK THE ZONE FIELDS
         BNE   PARMERR4            ERROR IF NOT NUMERIC
*
         EX    R15,HITMPACK        PACK THE PARAMETER
         CVB   R0,DOUBLE           MAKE THIS BINARY
         ST    R0,HITMIN           AND SET THE MIN VALUE
*
         L     R1,16(,R8)          LOAD THE HIT MAX PARAMETER
         LH    R15,0(,R1)          LOAD THE LENGTH OF THE HIT MAX
         LTR   R15,R15             TEST FOR NULL
         BZ    PARMERR4            ERROR IF NULL
         CH    R15,=H'8'           CHECK AGAINST LOGICAL MAXIMIM
         BH    PARMERR4            ERROR IF TOO LARGE
*
         BCTR  R15,0               DECREMENT FOR VARIOUS INSTRUCTIONS
         EX    R15,HITMMVC1        COPY THE PARM TO THE WORK AREA
         EX    R15,HITMNCNC        CLEAR OUT THE NUMERICS
         EX    R15,HITMCLC1        CHECK THE ZONE FIELDS
         BNE   PARMERR4            ERROR IF NOT NUMERIC
*
         EX    R15,HITMPACK        PACK THE PARAMETER
         CVB   R0,DOUBLE           MAKE THIS BINARY
         ST    R0,HITMAX           AND SET THE MAX VALUE
*
         B     PARMDONE            AND WE ARE DONE ANALYZING PARMS
*
STEMOCOC OC    STEMPREF(*-*),2(R1) *** EXECUTE ONLY ***
*
ARGSOCOC OC    SRCHARGS(*-*),2(R1) *** EXECUTE ONLY ***
*
HITMMVC1 MVC   DOUBLE(*-*),2(R1)   *** EXECUTE ONLY ***
HITMNCNC NC    DOUBLE(*-*),=8C'0'  *** EXECUTE ONLY ***
HITMCLC1 CLC   DOUBLE(*-*),=8C'0'  *** EXECUTE ONLY ***
HITMPACK PACK  DOUBLE,2(*-*,R1)    *** EXECUTE ONLY ***
*
PARMDONE DS    0H
*
         EJECT
***********************************************************************
*                                                                     *
*           DECODE THE SEARCH ARGUMENTS, CONVERTING THE %XX TO        *
*           THE APPROPRIATE CHARACTERS AND SEPARATING THEM AT         *
*           EACH +.                                                   *
*                                                                     *
***********************************************************************
*
         LH    R2,SRCHARGL         LOAD THE TOTAL ARGUMENT LENGTH
         LA    R3,SRCHARGS         POINT AT FIRST CHARACTER
*
SPLTLOOP DS    0H
         LTR   R2,R2               DO WE HAVE ANY SEARCH ARGUMENTS LEFT
         BZ    SPLTDONE            BRANCH IF NOT
         CLI   0(R3),C'+'          DOES IT START WITH A +
         BE    PARMER10            NOT GOOD IF IT DOES
*
         LA    R0,SABLNGTH         GET A SEARCH ARGUMENT BLOCK
         LA    R1,SRCBLOCK         POINT AT CELL POOL HEADER
         L     R15,CPSADDR         LOAD CELL POOL ROUTINE ADDRESS
         BASSM R14,R15             GET A SEARCH ARGUMENT BLOCK
*
         LR    R4,R15              COPY THE ADDRESS
         USING SABDSECT,R4
         SLR   R5,R5               INITIALIZE SPLIT ARGUMENT LENGTH
         ST    R5,SABNEXT          ZERO THE CHAIN POINTER
         STH   R5,SABLENG          ZERO THE DATA LENGTH
         MVC   SABDATA,BLANKS      PRE-BLANK SEARCH ARGUMENT
*
         LA    R6,SABDATA          POINT AT START OF SPLIT ARGUMENT
*
SPLTCHAR DS    0H
         CH    R5,=Y(L'SABDATA)    IS THIS ARGUMENT TOO LONG
         BNL   SPLTERR1
         CLI   0(R3),C'%'          CHECK FOR SPECIAL TRANSLATION
         BE    SPLTTRAN            BRANCH IF SO
*
         MVC   0(1,R6),0(R3)       COPY THE ONE CHARACTER
         LA    R6,1(,R6)           INCREMENT SPLIT TARGET
         LA    R5,1(,R5)           INCREMENT SPLIT LENGTH
         B     SPLTNEXT            AND GO INCREMENT THE SOURCE
*
SPLTTRAN DS    0H
         CH    R2,=H'3'            WE BETTER HAVE AT LEAST 3 CHARS
         BL    SPLTERR2            ERROR IF NOT
*
         CLI   1(R3),C'2'          CHECK LOW VALUE
         BL    SPLTERR3            ERROR IF TOO LOW
         CLI   1(R3),C'7'          CHECK HIGH VALUE
         BH    SPLTERR3            ALSO AN ERROR
*
         SLR   R15,R15             CLEAR FOR INSERT
         NI    1(R3),X'0F'         CLEAR THE ZONE
         IC    R15,1(,R3)          LOAD THE CHARACTER
         SLL   R15,2               MULTIPLY IT BY 4
         LA    R15,SPECTBLI-8(R15) POINT AT SPECIAL CHARACTER INDEX
         LH    R1,0(,R15)          LOAD THE OFFSET
         LA    R1,SPECTBLS(R1)     GET THE ACTUAL TABLE START
         LH    R0,2(,R15)          GET THE NUMBER OF ENTRIES
*
SPLTSPCL DS    0H
         CLC   0(1,R1),2(R3)       COMPARE HEX STRING
         BE    SPLTSPCF            BRANCH IF FOUND
         LA    R1,L'SPECTBL2(,R1)  INCREMENT CHARACTER TABLE
         BCT   R0,SPLTSPCL         LOOP THROUGH THE TABLE
         B     SPLTERR3            ERROR IF NOT IN TABLE
*
SPLTSPCF DS    0H
         MVC   0(1,R6),1(R1)       MOVE SPECIAL CHARACTER
         LA    R6,1(,R6)           INCREMENT TARGET
         LA    R5,1(,R5)           INCREMENT TARGET LENGTH
         LA    R3,2(,R3)           INCREMENT PAST 2/3 OF SOURCE
         BCTR  R2,0                DECREMENT PAST 2/3
         BCTR  R2,0                 OF SOURCE LENGTH
*
SPLTNEXT DS    0H
         LA    R3,1(,R3)           INCREMENT THE SOURCE POINTER
         BCT   R2,*+8              DECREMENT LENGTH
         B     SPLTLAST            WE ARE DONE HERE
*
         CLI   0(R3),C'+'          CHECK FOR SEPARATOR
         BNE   SPLTCHAR            LOOP TO NEXT CHARACTER
*
SPLTLAST DS    0H
         STH   R5,SABLENG          STORE THE LENGTH
         DROP  R4
*
         LA    R1,SRCHAIN          POINT AT SEARCH ARGUMENT CHAIN
         USING SABDSECT,R1
*
SPLTCHN1 DS    0H
         ICM   R15,15,SABNEXT      TEST IF THERE'S A CHAIN POINTER
         BZ    SPLTADD1            BRANCH IF AT THE END
         LR    R1,R15              CHAIN TO NEXT BLOCK
         B     SPLTCHN1            AND LOOP
*
SPLTADD1 DS    0H
         ST    R4,SABNEXT          CHAIN ON
         DROP  R1
*
         LA    R0,1                LOAD A ONE
         A     R0,SRCHNUM          ADD PREVIOUS COUNT
         ST    R0,SRCHNUM          STORE UPDATED COUNT
*
         LTR   R2,R2               TEST FOR NOTHING LEFT
         BZ    SPLTDONE            IF SO, WE ARE DONE
*
         LA    R3,1(,R3)           INCREMENT PAST THE + SIGN
         BCT   R2,SPLTLOOP         DECREMENT SOURCE LENGTH AND LOOP
         B     SPLTERR4            ERROR IF IT ENDS IN A PLUS SIGN
*
SPLTDONE DS    0H
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE FIRST HIT BLOCK TO CONTAIN THE NUMBER OF HITS      *
*        (I.E., STEM.0)                                               *
*                                                                     *
***********************************************************************
*
         LA    R0,BHBLNGTH         GET A BOOK HIT BLOCK
         LA    R1,HITBLOCK         POINT AT CELL POOL HEADER
         L     R15,CPSADDR         LOAD CELL POOL ROUTINE ADDRESS
         BASSM R14,R15             GET A SEARCH ARGUMENT BLOCK
*
         LA    R0,1                GET A ONE
         STH   R0,SUFXLENG         STORE THIS LENGTH
         MVC   SUFXDATA,BLANKS     BLANK FOR POSTERITY
         MVI   SUFXDATA,C'0'       THIS ONE IS EACH
*
         LA    R0,SUFXLENG         POINT AT SUFFIX
         LR    R1,R15              COPY STORAGE ADDRESS
         BAL   R14,INITBHB         GO INITIALIZE IT
         ST    R1,HITCHAIN         STORE FIRST BLOCK ADDRESS
         ST    R1,HITLAST          STORE LAST BLOCK ADDRESS
         ST    R1,AEXCOMP4         AND FIRST SHV BLOCK POINTER
*
***********************************************************************
*                                                                     *
*        BUILD THE SEARCH ARGUMENT RETURN BLOCK                       *
*        (I.E., STEM.SEARCHARUGMENTS)                                 *
*                                                                     *
***********************************************************************
*
         LA    R0,BHBLNGTH         GET A BOOK HIT BLOCK
         LA    R1,HITBLOCK         POINT AT CELL POOL HEADER
         L     R15,CPSADDR         LOAD CELL POOL ROUTINE ADDRESS
         BASSM R14,R15             GET A BOOK HOT BLOCK
*
         LA    R0,SRCHARGN         POINT AT THE SEARCH ARGUMENT SUFFIX
         LR    R1,R15              COPY THIS ADDRESS
         BAL   R14,INITBHB         GO INITIALIZE IT
*
         L     R15,HITLAST         LOAD PREVIOUS LAST RECORD
         ST    R1,0(,R15)          CHAIN THIS ONE ON TO PREVIOUS
         ST    R1,HITLAST          MAKE THIS THE NEW LAST ONE
*
         LR    R2,R1
         USING BHBDSECT,R2
         USING SHVBLOCK,BHBSHV
*
         L     R15,SRCHAIN         POINT AT FIRST SERACH ARGUMENT
         USING SABDSECT,R15
*
         LA    R3,BHBBKRET         POINT AT START OF VARIABLE DATA AREA
         L     R0,SRCHNUM          SEED THE LENGTH AS THE NUMBER OF
         BCTR  R0,0                 ARGUMENTS MINUS ONE (BLANKS)
*
RETSRCHL DS    0H
         LH    R14,SABLENG         LOAD THE SEARCH ARGUMENT LENGTH
         AR    R0,R14              ADD THIS LENGTH
*
         BCTR  R14,0               DECREMENT FOR MVC
         EX    R14,RETSMVCS        MOVE THIS SEARCH ARGUMENT
         LA    R3,1(R14,R3)        POINT PAST THE ARGUMENT
         MVI   0(R3),C' '          MOVE IN A BLANK
         LA    R3,1(,R3)           INCREMENT PAST THE BLANK
*
         ICM   R15,15,SABNEXT      GET NEXT ARGUMENT
         BNZ   RETSRCHL            AND LOOP THROUGH THE ARGUMENTS
*
         ST    R0,SHVVALL          STORE THE DATA LENGTH
         B     RETSRCHD            AND SKIP EXDCUTED INSTRUCTION
*
RETSMVCS MVC   0(*-*,R3),SABDATA   *** EXECUTE ONLY ***
*
         DROP  R2,R15
*
RETSRCHD DS    0H
*
         EJECT
***********************************************************************
*                                                                     *
*        NOW BEGIN THE ARDUOUS TASK OF SEARCHING THE IN STORAGE       *
*        COPY OF THE BOOK EXTRACT.                                    *
*                                                                     *
***********************************************************************
*
         L     R9,BKABOOEX         LOAD THE BOOK EXTRACT TABLE ADDRESS
         USING BKEDSECT,R9
*
         L     R8,BKENUMBR         LOAD TOTAL NUMBER OF ENTRIES
         L     R9,BKEFIRST         POINT AT FIRST ENTRY
         USING BKEENTRY,R9
         USING BKNDSECT,BKEEDATA
*
***********************************************************************
*                                                                     *
*        BUILD THE SEARCH DATA FROM THE PUBLICATION NUMBER AND        *
*        THE BOOK TITLE (UPPER-CASED).                                *
*                                                                     *
***********************************************************************
*
SRCHLOOP DS    0H
         MVC   SRCHBKNM,BKNNAME    GET THE CURRENT BOOK NAME
*
         MVC   SRCHDATA,BLANKS     BLANK THE SEARCH DATA WORK AREA
         MVC   SRCHDATA(L'BKNPUBNO),BKNPUBNO COPY THE PUB NUMBER
*
         LH    R3,BKNLENG          LOAD THE RECORD LENGTH
         SH    R3,=Y(BKNTITLE-BKNRDW+1) GET TITLE LENGTH MINUS ONE
         EX    R3,SRCHOCTT         COPY AND UPPER CASE THE TITLE
         LA    R3,L'BKNPUBNO+1(,R3)     GET LENGTH - 1 TO SEARCH
         LA    R14,SRCHDATA(R3)    POINT AT LAST BYTE OF SEARCH DATA
         ST    R14,SRCHDEND        SAVE THIS
         STH   R3,SRCHDLNG         AND SAVE IT
*
***********************************************************************
*                                                                     *
*        RUN THE SEARCH ARGUMENT CHAIN.  A MATCH IS REQUIRED ON       *
*        EVERY SEARCH ARGUMENT OR THE BOOK IS REJECTED.               *
*                                                                     *
***********************************************************************
*
         L     R15,SRCHAIN         POINT AT FIRST SEARCH ARGUMENT
         USING SABDSECT,R15
*
SRCHSTRT DS    0H
         XC    SRCHTRTT,SRCHTRTT   CLEAR THE TRT TABLE
         SLR   R14,R14
         IC    R14,SABDATA         LOAD FIRST ARGUMENT CHARACTER
         STC   R14,SRCHTRTT(R14)   STORE THIS IN THE TRT TABLE
*
         LA    R1,SRCHDATA         SET UP FOR START OF SCAN
         LH    R14,SABLENG         LOAD SEARCH ARGUMENT LENGTH
         BCTR  R14,0               DECREMENT FOR CLC
         LH    R3,SRCHDLNG         LOAD THE SEARCH STRING LENGTH
*
SRCHSCN1 DS    0H
         EX    R3,SRCHTRT1         LOOK FOR A MATCHING CHARACTER
         BZ    SRCHNXT1            IF NO HITS, GET NEXT SEACH ARGUMENT
*
         EX    R14,SRCHCLC1        SEE IF THE ENTIRE ARGUMENT MATCHES
         BE    SRCHHIT1            BRANCH IF WE HAVE A MATCH
*
         LA    R1,1(,R1)           INCREMENT SEARCH DATA POINTER
         L     R3,SRCHDEND         GET LAST BYTE ADDRESS
         SR    R3,R1               CALCULATE NEW LENGTH
         BH    SRCHSCN1            AND TRY AGAIN IF WE HAVE SOME LEFT
*
***********************************************************************
*                                                                     *
*        THE SEARCH ARGUMENT WAS NOT FOUND IN THE SEARCH DATA,        *
*        SO GO TO THE NEXT BOOK.                                      *
*                                                                     *
***********************************************************************
*
SRCHNXT1 DS    0H
         ICM   R9,15,BKEENEXT      NO MATCH - GET NEXT BOOK
         BZ    SRCHDONE            IF NO MORE, WE BE DONE
*
         CLC   SRCHBKNM,BKNNAME    IS THIS THE SAME BOOK
         BNE   SRCHLOOP            IF NOT, GO TRY AGAIN
         B     SRCHNXT1            IF SO, SKIP THIS BOOK
*
SRCHCLC1 CLC   SABDATA(*-*),0(R1)  *** EXECUTE ONLY ***
SRCHTRT1 TRT   0(*-*,R1),SRCHTRTT  *** EXECUTE ONLY ***
SRCHOCTT OC    SRCHDATA+L'BKNPUBNO+1(*-*),BKNTITLE *** EXECUTE ONLY ***
*
***********************************************************************
*                                                                     *
*        A MATCH WAS MADE BETWEEN THE CURRENT SEARCH ARGUMENT AND     *
*        THE EXTRACTED BOOK SEARCH DATA.  GET THE NEXT SEARCH         *
*        ARGUMENT.                                                    *
*                                                                     *
***********************************************************************
*
SRCHHIT1 DS    0H
         ICM   R15,15,SABNEXT      GET NEXT SEARCH AGRUMENT
         BNZ   SRCHSTRT            AND LOOP IF WE HAVE MORE
*
         DROP  R15                 DONE WITH THE SEARCH DATA BLOCK
*
         EJECT
***********************************************************************
*                                                                     *
*        EVERY SEARCH ARGUMENT WAS FOUND IN THE SEARCH DATA, SO       *
*        THIS BOOK IS A HIT.  NOW WE WILL BUILD A RETURN DATA         *
*        VARIABLE.                                                    *
*                                                                     *
***********************************************************************
*
         LA    R0,1                LOAD A ONE
         A     R0,HITCOUNT         ADD PREVIOUS HIT COUNT
         ST    R0,HITCOUNT         UPDATE THIS COUNT
*
         CLC   HITCOUNT,HITMIN     CHECK AGAINST MINIMUM
         BL    SRCHNXT2            SKIP RETURN IF TOO LOW
         CLC   HITCOUNT,HITMAX     CHECK AGAINST MAXIMUM
         BH    SRCHNXT2            SKIP RETURN IF TOO HIGH
*
         LA    R0,1                LOAD A ONE
         A     R0,RETNUM           ADD PREVIOUS HIT COUNT
         ST    R0,RETNUM           STORE THE UPDATED COUNT
*
         BAL   R14,BUILDSUF        BUILD THE VARIABLE SUFFIX
*
         LA    R0,BHBLNGTH         GET A BOOK HIT BLOCK
         LA    R1,HITBLOCK         POINT AT CELL POOL HEADER
         L     R15,CPSADDR         LOAD CELL POOL ROUTINE ADDRESS
         BASSM R14,R15             GET A BOOK HIT BLOCK
*
         LA    R0,SUFXLENG         POINT AT VARIABLE SUFFIX
         LR    R1,R15              COPY THIS ADDRESS
         BAL   R14,INITBHB         GO INITIALIZE IT
*
         LR    R3,R1               COPY THE BOOK HIT BLOCK ADDRESS
         USING BHBDSECT,R3
         USING SHVBLOCK,BHBSHV
*
         L     R15,HITLAST         GET LAST RECORD ADDRESS
         ST    R3,0(,R15)          CHAIN NEW RECORD ONTO OLD
         ST    R3,HITLAST          AND UPDATE THE LAST POINTER
*
         MVC   BHBBKNAM,BKNNAME    COPY THE BOOK NAME
         TRT   BHBBKRET(BHBBKLEN),BLANKTRT   FIND THE FIRST BLANK
         LA    R4,1(,R1)           POINT AT FIRST AVAILABLE SPACE
*
         LA    R0,1                LOAD A ONE
         A     R0,HITRETN          ADD PREVIOUS RETURNED COUNT
         ST    R0,HITRETN          UPDATE THIS COUNT
*
         MVC   0(L'BHBBKPUB,R4),BKNPUBNO   COPY THE PUBLICATION NUMBER
         TRT   0(BHBBKLEN,R4),BLANKTRT FIND THE NEXT BLANK
         LA    R4,1(,R1)           SAVE AND INCREMENT NEXT FIELD
*
         MVC   0(L'BHBBKDTE,R4),BKNBKDTE   COPY THE BOOK BUILD DATE
         TRT   0(BHBBKLEN,R4),BLANKTRT FIND THE NEXT BLANK
         LA    R4,1(,R1)           SAVE AND INCREMENT NEXT FIELD
*
         MVC   0(L'BHBBKTIM,R4),BKNBKTIM   COPY THE BOOK BUILD TIME
         TRT   0(BHBBKLEN,R4),BLANKTRT FIND THE NEXT BLANK
         LA    R4,1(,R1)           SAVE AND INCREMENT NEXT FIELD
*
         MVC   0(L'BHBBKDSN,R4),BKNDSNME   COPY THE DATA SET NAME
         TRT   0(BHBBKLEN,R4),BLANKTRT FIND THE NEXT BLANK
         LA    R4,1(,R1)           SAVE AND INCREMENT NEXT FIELD
*
         LA    R1,BKNDSNME         POINT AT THE DATA SET NAME
         BAL   R14,LOCATVOL        GO GET THE VOL SER INFO
         MVC   0(L'BHBBKVOL,R4),0(R15)     COPY THE RETURNED DATA
         TRT   0(BHBBKLEN,R4),BLANKTRT FIND THE NEXT BLANK
         LA    R4,1(,R1)           SAVE AND INCREMENT NEXT FIELD
*
         MVC   0(L'BHBBKSHF,R4),BKNBKSID   COPY THE BOOKSHELF NAME
         TRT   0(BHBBKLEN,R4),BLANKTRT FIND THE NEXT BLANK
         LA    R4,1(,R1)           SAVE AND INCREMENT NEXT FIELD
*
         LA    R1,BKNBKSID         POINT AT BOOKSHELF NAME
         BAL   R14,LKUPBKS         GO FIND THE BOOKSHELF DATA SET NAME
         MVC   0(L'BHBBKSHD,R4),0(R15) COPY THE BOOKSHELF DATA SET NAME
         TRT   0(BHBBKLEN,R4),BLANKTRT FIND THE NEXT BLANK
         LA    R4,1(,R1)           SAVE AND INCREMENT NEXT FIELD
*
         LA    R0,BKNPUBNO         POINT AT PUBLICATION NUMBER
         LA    R1,BKNDSNME         POINT AT ORIGINAL DATA SET NAME
         BAL   R14,FINDPDFN        GO FIND IT
*
         MVC   0(L'BHBBKPDF,R4),0(R1)   COPY THE RETURNED DATA SET NAME
         TRT   0(BHBBKLEN,R4),BLANKTRT FIND THE NEXT BLANK
         LA    R4,1(,R1)           SAVE AND INCREMENT NEXT FIELD
*
         MVC   0(L'BHBBKPDV,R4),0(R15)  COPY THIS RETURNED DATA
         TRT   0(BHBBKLEN,R4),BLANKTRT FIND THE NEXT BLANK
         LA    R4,1(,R1)           SAVE AND INCREMENT NEXT FIELD
*
         LH    R15,BKNRDW          LOAD THE RECORD LENGTH
         SH    R15,=Y(BKNTITLE-BKNRDW+1) GET TITLE LENGTH MINUS 1
         EX    R15,SRCHMVCT        COPY THE TITLE
*
         LA    R4,1(R4,R15)        POINT PAST THE TITLE
*
         LA    R15,BHBBKRET        GET START OF RETURNED DATA
         SR    R4,R15              CALCULATE RETURNED LENGTH
         ST    R4,SHVVALL          STORE THIS LENGTH
*
         B     SRCHDUP1            AND GO LOOK FOR DUPLICATES
*
         DROP  R3
*
SRCHMVCT MVC   0(*-*,R4),BKNTITLE  *** EXECUTE ONLY ***
*
         EJECT
***********************************************************************
*                                                                     *
*        BOOK HIT IS OUTSIDE THE HIT RANGE, SO WE WANT TO SKIP IT     *
*        (AND ALL DUPLICATE HITS AS WELL).                            *
*                                                                     *
***********************************************************************
*
SRCHNXT2 DS    0H
         ICM   R9,15,BKEENEXT      NO MATCH - GET NEXT BOOK
         BZ    SRCHDONE            IF NO MORE, WE BE DONE
*
         CLC   SRCHBKNM,BKNNAME    IS THIS THE SAME BOOK
         BNE   SRCHLOOP            IF NOT, GO TRY AGAIN
         B     SRCHNXT2            IF SO, SKIP THIS BOOK
*
         EJECT
***********************************************************************
*                                                                     *
*        HAVING BUILT A RETURN BLOCK FOR THE MATCH, WE NOW LOOK       *
*        TO SEE IF THERE ARE ANY MORE MATCHES FOR THE SAME BOOK       *
*        ON ADDITIONAL BOOKSHELVES.  IF SO, WE BUILD "STRIPPED DOWN"  *
*        RETURN RECORDS.                                              *
*                                                                     *
***********************************************************************
*
SRCHDUP1 DS    0H
         ICM   R9,15,BKEENEXT      NO MATCH - GET NEXT BOOK
         BZ    SRCHDONE            IF NO MORE, WE BE DONE
*
         CLC   SRCHBKNM,BKNNAME    IS THIS THE SAME BOOK
         BNE   SRCHLOOP            IF NOT, GO TRY AGAIN
*
         LA    R0,1                LOAD A ONE
         A     R0,RETNUM           ADD PREVIOUS HIT COUNT
         ST    R0,RETNUM           STORE THE UPDATED COUNT
*
         BAL   R14,BUILDSUF        BUILD THE VARIABLE SUFFIX
*
         LA    R0,BHBLNGTH         GET A BOOK HIT BLOCK
         LA    R1,HITBLOCK         POINT AT CELL POOL HEADER
         L     R15,CPSADDR         LOAD CELL POOL ROUTINE ADDRESS
         BASSM R14,R15             GET A BOOK HIT BLOCK
*
         LA    R0,SUFXLENG         POINT AT VARIABLE SUFFIX
         LR    R1,R15              COPY THIS ADDRESS
         BAL   R14,INITBHB         GO INITIALIZE IT
*
         LR    R3,R1               COPY THE BOOK HIT BLOCK ADDRESS
         USING BHBDSECT,R3
         USING SHVBLOCK,BHBSHV
*
         L     R15,HITLAST         GET LAST RECORD ADDRESS
         ST    R3,0(,R15)          CHAIN NEW RECORD ONTO OLD
         ST    R3,HITLAST          AND UPDATE THE LAST POINTER
*
         MVC   BHBBKNAM,BKNNAME    COPY THE BOOK NAME
         TRT   BHBBKRET(BHBBKLEN),BLANKTRT   FIND THE FIRST BLANK
         LA    R4,1(,R1)           POINT AT FIRST AVAILABLE SPACE
*
         MVI   0(R4),C'*'          PUBLICATION NUMBER PLACEHOLDER
         LA    R4,2(,R4)           INCREMENT TO NEXT FIELD
*
         MVI   0(R4),C'*'          BOOK BUILD DATE PLACEHOLDER
         LA    R4,2(,R4)           INCREMENT TO NEXT FIELD
*
         MVI   0(R4),C'*'          BOOK BUILD TIME PLACEHOLDER
         LA    R4,2(,R4)           INCREMENT TO NEXT FIELD
*
         MVI   0(R4),C'*'          BOOK DATA SET NAME PLACEHOLDER
         LA    R4,2(,R4)           INCREMENT TO NEXT FIELD
*
         MVI   0(R4),C'*'          BOOK DATA SET VOL SER PLACEHOLDER
         LA    R4,2(,R4)           INCREMENT TO NEXT FIELD
*
         MVC   0(L'BHBBKSHF,R4),BKNBKSID   COPY THE BOOKSHELF NAME
         TRT   0(BHBBKLEN,R4),BLANKTRT FIND THE NEXT BLANK
         LA    R4,1(,R1)           SAVE AND INCREMENT NEXT FIELD
*
         LA    R1,BKNBKSID         POINT AT BOOKSHELF NAME
         BAL   R14,LKUPBKS         GO FIND THE BOOKSHELF DATA SET NAME
         MVC   0(L'BHBBKSHD,R4),0(R15) COPY THE BOOKSHELF DATA SET NAME
         TRT   0(BHBBKLEN,R4),BLANKTRT FIND THE NEXT BLANK
         LA    R4,1(,R1)           SAVE AND INCREMENT NEXT FIELD
*
         MVI   0(R4),C'*'          PDF DATA SET NAME PLACEHOLDER
         LA    R4,2(,R4)           INCREMENT TO NEXT FIELD
*
         MVI   0(R4),C'*'          PDF DATA SET VOL SER PLACEHOLDER
         LA    R4,2(,R4)           INCREMENT TO NEXT FIELD
*
         MVI   0(R4),C'*'          TITLE PLACEHOLDER
         LA    R4,1(,R4)           INCREMENT TO END OF DATA
*
         LA    R15,BHBBKRET        GET START OF RETURNED DATA
         SR    R4,R15              CALCULATE RETURNED LENGTH
         ST    R4,SHVVALL          STORE THIS LENGTH
*
         B     SRCHDUP1            GO LOOK FOR MORE DUP HITS
*
         DROP  R3
*
         DROP  R9                  DONE WITH THE IN STORAGE BOOK BLOCK
*
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE ACTUAL NUMBER OF SEARCH MATCHES                    *
*        (I.E., STEM.SEARCHHITS)                                      *
*                                                                     *
***********************************************************************
*
SRCHDONE DS    0H
         LA    R0,BHBLNGTH         GET A BOOK HIT BLOCK
         LA    R1,HITBLOCK         POINT AT CELL POOL HEADER
         L     R15,CPSADDR         LOAD CELL POOL ROUTINE ADDRESS
         BASSM R14,R15             GET A SEARCH ARGUMENT BLOCK
*
         LA    R0,SRCHHITN         POINT AT THE SEARCH HIT SUFFIX
         LR    R1,R15              COPY THE ADDRESS
         BAL   R14,INITBHB         GO INITIALIZE IT
*
         L     R15,HITLAST         LOAD PREVIOUS LAST RECORD
         ST    R1,0(R15)           CHAIN THIS ONE ON TO REPVIOUS
         ST    R1,HITLAST          MAKE THIS THE NEW LAST ONE
*
         LR    R2,R1
         USING BHBDSECT,R2
         USING SHVBLOCK,BHBSHV
*
         L     R0,HITCOUNT         LOAD THE NUMBER OF UNIQUE MATCHES
         BAL   R14,BUILDSUF        GO CREATE IT LIKE A SUFFIX
         MVC   BHBBKRET(L'SUFXDATA),SUFXDATA  COPY THE ENTIRE DATA
         LH    R0,SUFXLENG         LOAD THE LENGTH
         ST    R0,SHVVALL          AND STORE THE LENGTH
*
         DROP  R2
*
         EJECT
***********************************************************************
*                                                                     *
*        BUILD THE ACTUAL NUMBER OF SEARCH HITS RETURNED              *
*        (I.E., STEM.SEARCHRETURN)                                    *
*                                                                     *
***********************************************************************
*
         LA    R0,BHBLNGTH         GET A BOOK HIT BLOCK
         LA    R1,HITBLOCK         POINT AT CELL POOL HEADER
         L     R15,CPSADDR         LOAD CELL POOL ROUTINE ADDRESS
         BASSM R14,R15             GET A SEARCH ARGUMENT BLOCK
*
         LA    R0,SRCHRETN         POINT AT THE SEARCH HIT SUFFIX
         LR    R1,R15              COPY THE ADDRESS
         BAL   R14,INITBHB         GO INITIALIZE IT
*
         L     R15,HITLAST         LOAD PREVIOUS LAST RECORD
         ST    R1,0(R15)           CHAIN THIS ONE ON TO REPVIOUS
         ST    R1,HITLAST          MAKE THIS THE NEW LAST ONE
*
         LR    R2,R1
         USING BHBDSECT,R2
         USING SHVBLOCK,BHBSHV
*
         L     R0,HITRETN          LOAD THE NUMBER OF UNIQUE MATCHES
         BAL   R14,BUILDSUF        GO CREATE IT LIKE A SUFFIX
         MVC   BHBBKRET(L'SUFXDATA),SUFXDATA  COPY THE ENTIRE DATA
         LH    R0,SUFXLENG         LOAD THE LENGTH
         ST    R0,SHVVALL          AND STORE THE LENGTH
*
         DROP  R2
*
         EJECT
***********************************************************************
*                                                                     *
*        GET THE NUMBER OF RETURNED HITS AND UPDATE THE STEM.0        *
*                                                                     *
***********************************************************************
*
         L     R2,HITCHAIN         POINT AT FIRST HIT BLOCK
         USING BHBDSECT,R2
         USING SHVBLOCK,BHBSHV
*
         L     R0,RETNUM           LOAD THE NUMBER OF HITS
         BAL   R14,BUILDSUF        GO CREATE IT LIKE A SUFFIX
         MVC   BHBBKRET(L'SUFXDATA),SUFXDATA  COPY THE ENTIRE DATA
         LH    R0,SUFXLENG         LOAD THE LENGTH
         ST    R0,SHVVALL          AND STORE THE LENGTH
*
         DROP  R2
*
***********************************************************************
*                                                                     *
*        CALL REXX TO HAVE ALL OF THE COLLECTED VARIABLES CREATED     *
*                                                                     *
***********************************************************************
*
         LA    R1,EXCOMPRM         POINT TO THE PARM LIST
         L     R15,AIRXEXCM        LOAD ADDRESS OF ROUTINE
         BALR  R14,R15             CALL IT
*
         ST    R15,RETCODE         SET THE EXCOM RETURN CODE
*
         MVC   OUTBUFFR(HTMLCOML),HTMLCOMM
         LA    R0,HTMLCOML         GET LENGTH OF COMMENT
         ST    R0,OUTBUFLN         STORE THE LENGTH
*
         LA    R1,BPXWRPRM         POINT AT PARAMETER LIST
         L     R15,BPXWRITE        LOAD WRITE ROUTINE ADDRESS
         BALR  R14,R15             CALL WRITE ROUTINE
*
SLIPTRAP NOP   0                   PLACE TO SETP SLIP TRAP
*
         B     RETURN              AND LEAVE
*
         EJECT
***********************************************************************
*                                                                     *
*        ERROR CONTITIONS - SET RETURN CODE VALUE FOR                 *
*        FEEDBACK TO PROGRAM                                          *
*                                                                     *
***********************************************************************
*
PARMERR2 DS    0H
         LA    R0,2                INDICATE ERROR
         ST    R0,RETCODE          STORE THE ERROR
         B     RETURN              AND RETURN
*
PARMERR3 DS    0H
         LA    R0,3                INDICATE ERROR
         ST    R0,RETCODE          STORE THE ERROR
         B     RETURN              AND RETURN
*
PARMERR4 DS    0H
         LA    R0,4                INDICATE ERROR
         ST    R0,RETCODE          STORE THE ERROR
         B     RETURN              AND RETURN
*
PARMER10 DS    0H
         LA    R0,10               INDICATE ERROR
         ST    R0,RETCODE          STORE THE ERROR
         B     RETURN              AND RETURN
*
SPLTERR1 DS    0H
         LA    R0,11               INDICATE ERROR
         ST    R0,RETCODE          STORE THE ERROR
         B     RETURN              AND RETURN
*
SPLTERR2 DS    0H
         LA    R0,12               INDICATE ERROR
         ST    R0,RETCODE          STORE THE ERROR
         B     RETURN              AND RETURN
*
SPLTERR3 DS    0H
         LA    R0,13               INDICATE ERROR
         ST    R0,RETCODE          STORE THE ERROR
         B     RETURN              AND RETURN
*
SPLTERR4 DS    0H
         LA    R0,14               INDICATE ERROR
         ST    R0,RETCODE          STORE THE ERROR
         B     RETURN              AND RETURN
*
***********************************************************************
*                                                                     *
*           SET RETURN CODE, RESTORE REGISTERS AND                    *
*           RETURN TO CALLER                                          *
*                                                                     *
***********************************************************************
*
RETURN   DS    0H
         SLR   R0,R0               INDICATE FREEING OF THE SAB'S
         LA    R1,SRCBLOCK         POINT AT CELL POOL HEADER
         L     R15,CPSADDR         LOAD CELL POOL ROUTINE ADDRESS
         BASSM R14,R15             GET A SEARCH ARGUMENT BLOCK
*
         SLR   R0,R0               INDICATE FREEING OF THE BHB'S
         LA    R1,HITBLOCK         POINT AT CELL POOL HEADER
         L     R15,CPSADDR         LOAD CELL POOL ROUTINE ADDRESS
         BASSM R14,R15             GET A SEARCH ARGUMENT BLOCK
*
         DELETE EPLOC=BPXWRNAM     DELETE BPX1WRT FROM STORAGE
*
         DELETE EPLOC=EXCOMID      DELETE IRXEXCOM FROM STORAGE
*
         DELETE EPLOC=CPSNAME      DELETE THE CELL POOL ROUTINE
*
         L     R2,RETCODE          LOAD THE RETURN CODE
*
         LR    R1,R13              LOAD ADDRESS OF GETMAINED AREA
         L     R13,4(,R13)         RELOAD ADDRESS OF PREVIOUS SAVE
*
         LA    R0,WORKLENG         LOAD LENGTH OF GETMAINED AREA
         FREEMAIN RU,LV=(0),A=(1)  FREE GETMAINED AREA
*
QUIKEXIT DS    0H
         LR    R15,R2              SET THE RETURN CODE
         L     R14,12(,R13)        LOAD REGISTER 14 WITH RETURN
         LM    R0,R12,20(R13)      RESTORE REGISTERS
         BSM   0,R14               RETURN
*
         EJECT
***********************************************************************
*                                                                     *
*        INITIALIZE A BOOK HIT BLOCK                                  *
*                                                                     *
*        REG 1 POINTS TO THE BLOCK TO BE INITALIZED                   *
*        REG 0 POINTS TO THE SUFFIX OF THE CARIABLE NAME              *
*                                                                     *
***********************************************************************
*
INITBHB  DS    0H
         STM   R14,R12,12(R13)     SAVE THE REGISTERS
         LR    R2,R1               COPY THE BOOK HIT BLOCK ADDR
         LR    R3,R0               SAVE THE VARIABLE SUFFIX POINTER
         USING BHBDSECT,R2
         USING SHVBLOCK,BHBSHV
*
         XC    BHBSHV,BHBSHV       CLEAR THE REXX SHV
         MVI   SHVCODE,SHVSTORE    SET VARIABLE STORE
         MVC   BHBVARN,BLANKS      CLEAR THE VARIABLE NAME FIELD
         MVC   BHBBKRET(BHBBKLEN),BLANKS BLANK THE BASE RETURN DATA
         MVC   BHBBKTTL,BLANKS     BLANK THE BOOK TITLE
*
         LA    R0,BHBBKRET         POINT AT START OF DATA
         ST    R0,SHVVALA          STORE THE DATA ADDRESS
*
         LH    R15,STEMPREL        LOAD THE STEM PREFIX LENGTH
         BCTR  R15,0               DECREMENT FOR MVC
         EX    R15,INITVARN        MVC THE STEM PREFIX
         LA    R15,BHBVARN+1(R15)  POINT PAST THE STEM PREFIX
*
         LH    R14,0(,R3)          LOAD THE SUFFIX LENGTH
         BCTR  R14,0               DECREMENT FOR MVC
         EX    R14,INITVARS        MOVE THE SUFFIX DATA
         LA    R0,BHBVARN          POINT AT START OF VARIABLE NAME
         ST    R0,SHVNAMA          STORE THE VARIABLE NAME ADDRESS
         LH    R0,STEMPREL         LOAD THE PREFIX LENGTH
         AH    R0,0(,R3)           ADD THE SUFFIX LENGTH
         ST    R0,SHVNAML          STORE THE VARIABLE NAME LENGTH
*
         LM    R14,R12,12(R13)     RESTORE THE REGISTERS
         BR    R14                 RETURN TO CALLER
*
INITVARN MVC   BHBVARN(*-*),STEMPREF    *** EXECUTE ONLY ***
*
INITVARS MVC   0(*-*,R15),2(R3)    *** EXECUTE ONLY ***
*
         DROP  R2
*
         EJECT
***********************************************************************
*                                                                     *
*        CREATE A NUMERIC SUFFIX                                      *
*                                                                     *
*        REG 0 CONTAINS A BINARY VALUE THAT WILL BE CONVERTED TO      *
*        CHARACTER AND USED TO BUILD A SUFFIX FIELD.                  *
*                                                                     *
***********************************************************************
*
BUILDSUF DS    0H
         STM   R14,R1,12(R13)      SAVE REGISTERS
         CVD   R0,DOUBLE           FIRST, CONVERT TO PACKED DECIMAL
         MVC   GENLWORK(9),=X'402020202020212040'
         LA    R1,GENLWORK+7       POINT AT WHERE ZERO WOULD BE
         EDMK  GENLWORK(9),DOUBLE+4     ALLOW FOR 9,999,999 HITS
*
         MVC   SUFXDATA,BLANKS     BLANK FOR GOOD MEASURE
         LA    R15,SUFXDATA        POINT AT TARGET AREA
*
BUILDSUL DS    0H
         MVC   0(1,R15),0(R1)      COPY ONE CHARACTER
         LA    R15,1(,R15)         INCREMENT SUFFIX AREA
         LA    R1,1(,R1)           INCREMENT EDITED NUMBER POINTER
         CLI   0(R1),C' '          CHECK FOR END OF THE NUMBER
         BNE   BUILDSUL            LOOP IF NOT AT THE END
*
         LA    R1,SUFXDATA         POINT AT START OF DATA
         SR    R15,R1              GET LENGTH OF THE SUFFIX
         STH   R15,SUFXLENG        STORE THE SUFFIX LENGTH
*
         LM    R14,R1,12(R13)      RESTORE SAVED REGISTERS
         BR    R14
*
         EJECT
***********************************************************************
*                                                                     *
*        FIND THE BOOKSHELF NAME AND RETURN THE BOOKSHELF DATA        *
*        SET NAME.                                                    *
*                                                                     *
*        REGISTER 1 POINTS TO THE BOOKSHELF NAME                      *
*        REGISTER 15 ON RETURN POINTS TO THE BOOKSHELF DATA SET NAME  *
*                                                                     *
***********************************************************************
*
LKUPBKS  DS    0H
         STM   R14,R12,12(R13)     SAVE THE REGISTERS
*
         L     R9,BKABKSEX         LOAD BOOKSHELF EXTRACT TABLE
         USING BKEDSECT,R9
*
         L     R9,BKEFIRST         POINT AT FIRST ENTRY
         USING BKEENTRY,R9
         USING BKNDSECT,BKEEDATA
*
LKUPBKSL DS    0H
         CLC   BKNBKSID,0(R1)      MATCH NAMES
         BE    LKUPBKSF            BRANCH IF FOUND
         ICM   R9,15,BKEENEXT      LOAD THE NEXT ADDRESS
         BNZ   LKUPBKSL            LOOP IF NOT AT THE END
*
         LA    R15,=CL44'BOOKSHELF.DATA.SET.NOT.FOUND'
         B     LKUPBKSX            AND RETURN
*
LKUPBKSF DS    0H
         LA    R15,BKNDSNME        POINT AT DATA SET NAME
*
LKUPBKSX DS    0H
         ST    R15,16(,R13)        STORE RETURN DATA SET NAME
         LM    R14,R12,12(R13)     RESTORE THE REGISTERS
         BR    R14
*
         DROP  R9
*
         EJECT
***********************************************************************
*                                                                     *
*        LOCATE A DATA SET NAME                                       *
*                                                                     *
*        REGISTER 1 POINTS TO THE DATA SET NAME ON ENTRY              *
*                                                                     *
*        ON RETURN, REGISTER 15 POINTS TO A SEVEN BYTE AREA,          *
*        CONTAINING THE VOLUME SERIAL NUMBER FOLLOWED BY THE          *
*        MIGRATION LEVEL, 0 (REAL DASD), 1 (DASD MIGRATION),          *
*        2 (TAPE MIGRATION), OR 3 (DATA SET NOT FOULD).               *
*                                                                     *
***********************************************************************
*
LOCATVOL DS    0H
         MVC   CAMLST(CAMLSTML),CAMLSTMD    COPY CAMLIST MODEL
         ST    R1,CAMLST+4         STORE DSNAME ADDR IN CAMLST
         LA    R15,CAMLSTWK        POINT AT LOCATE WORK AREA
         ST    R15,CAMLST+12       STORE IN CAMLST
*
         LOCATE CAMLST             DO THE LOCATE
         LTR   R15,R15             TEST RETURN CODE
         BNZ   LOCATNOT            IF NON-ZERO ASSUME NOT CATALOGED
*
         LA    R15,CAMLSTVL        POINT AT VOLUME SERIAL NUMBER
         MVI   CAMLSTMG,C'0'       ASSUME CATALOGUED TO DASD
*
         CLC   CAMLSTVL,=C'MIGRAT' CHECK FOR HSM ARCHIVE
         BNE   LOCATRET            RETURN IF NOT
*
         MVI   CAMLSTMG,C'2'       ASSUME TAPE ARCHIVE
         TM    CAMLSTD3,UCB3TAPE   CHECK THIS ASSUMPTION
         BO    LOCATRET            BRANCH IF THIS IS TRUE
         MVI   CAMLSTMG,C'1'       SET DASD MIGRATION LEVEL
         B     LOCATRET            AND RETURN
*
LOCATNOT DS    0H
         LA    R15,=C'******3'     POINT AT NOT FOUND
*
LOCATRET DS    0H
         BR    R14                 RETURN TO CALLER
*
         EJECT
***********************************************************************
*                                                                     *
*        DETERMINE THE PDF DATA SET NAME                              *
*                                                                     *
*        REGISTER 1 POINTS TO THE DATA SET NAME ON ENTRY              *
*        REGISTER 0 IS ZERO FOR A CONVENTION PDF DATA SET NAME        *
*        REGISTER 0 POINTS TO THE 12 CHARACTER PUB NUMBER FOR         *
*        THE ALTERNAME PDF DATA SET NAME CASE                         *
*                                                                     *
*        ON RETURN, REGISTER 1 POINTS TO THE PDF DATA SET NAME        *
*        AND REGISTER 15 POINTS TO THE VOLSER/MIGRATE LEVEL.          *
*                                                                     *
***********************************************************************
*
FINDPDFN DS    0H
         STM   R14,R12,12(R13)     SAVE THE REGISTERS
         LR    R3,R0               SAVE THE PUBNO POINTER
*
         MVC   PDFDSN,0(R1)        COPY THE DATA SET NAME
*
         LA    R1,PDFDSN+L'PDFDSN  ASSUME IT'S THE FULL LENGTH
         TRT   PDFDSN,BLANKTRT     SCAN FOR THE END OF THE DSN
*
         SH    R1,=H'5'            DECREMENT FOR THE ".BOOK"
         MVC   0(5,R1),=C'.PDF '   CONVERT TO A PDF NAME
         LR    R2,R1               SAVE FOR ALTERNATE CASE
*
         LA    R1,PDFDSN           POINT AT PDF DATA SET NAME
         BAL   R14,LOCATVOL        SEE IF IT EXISTS
         CLI   0(R15),C'*'         CHECK THE RETURN VOLUME
         BNE   FINDPDFX            EXIT IF PDF DATA SET FOUND
*
         LTR   R0,R0               IS THIS AN ALTERNAME REQUEST
         BZ    FINDPDFX            EXIT IF NOT
*
         LA    R1,PDFDSN           POINT AT START OF NAME
FINDPDFL DS    0H
         BCTR  R2,0                BACK UP ONE
         CR    R2,R1               MAKE SURE WE DON'T LOOP TOO FAR
         BNH   FINDPDFB            BAD NEWS IF WE CAN'T FIND A PERIOD
         CLI   0(R2),C'.'          FIND THE PREVIOUS PERIOD
         BNE   FINDPDFL
*
         MVC   1(4,R2),0(R3)       COPY THE FIRST 4 CHARS OF PUB NO
         MVC   5(4,R2),5(R3)       COPY CHARS 6-9 OF PUB NO
         MVC   9(5,R2),=C'.PDF '   COPY THE PDF SUFFIX
*
         LA    R1,PDFDSN           POINT AT PDF DATA SET NAME
         BAL   R14,LOCATVOL        SEE IF IT EXISTS
         CLI   0(R15),C'*'         CHECK THE RETURN VOLUME
         BNE   FINDPDFX            EXIT IF PDF DATA SET FOUND
*
FINDPDFB DS    0H
         MVC   PDFDSN,BLANKS       BLANK OUT THE NAME
         MVC   PDFDSN(5),=C'NONE ' AND INDICATE WE DON'T HAVE ONE
*
FINDPDFX DS    0H
         LA    R1,PDFDSN           POINT AT PDF DATA SET NAME
         ST    R1,24(,R13)         RETURN DATA SET NAME
         ST    R15,16(,R13)        RETURN VOLUME SERIAL NUMBER
*
         LM    R14,R12,12(R13)     RESTORE THE REGISTERS
         BR    R14
*
         EJECT
***********************************************************************
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
***********************************************************************
*
         DS    0D
         DS    CL7                 PADDING FOR ALIGNMENT
NULLVALU DS    0CL44               DEFINE A NULL VALUE
         DC    C'*'
BLANKS   DC    CL256' '            BLANKS FOR BLANKING AND UPPERCASING
*
EXCOMID  DC    CL8'IRXEXCOM'       IRXEXCOM ID
*
CPSNAME  DC    CL8'CCCBKCPS'       CELL POOL SUBROUTINE NAME
*
BPXWRNAM DC    CL8'BPX1WRT'        USS WRITE ROUTINE ADDRESS
*
ZERO     DC    F'0'                A WORD OF ZEROS
*
HITMODEL BKMGRCPS ID=HIT,SP=10,SIZE=100,LOC=ANY
SRCMODEL BKMGRCPS ID=SRC,SP=20,SIZE=4,LOC=ANY
*
SPECTBLI DS    0F
         DC    Y(SPECTBL2-SPECTBLS),Y(SPECTB2N)
         DC    Y(SPECTBL3-SPECTBLS),Y(SPECTB3N)
         DC    Y(SPECTBL4-SPECTBLS),Y(SPECTB4N)
         DC    Y(SPECTBL5-SPECTBLS),Y(SPECTB5N)
         DC    Y(SPECTBL6-SPECTBLS),Y(SPECTB6N)
         DC    Y(SPECTBL7-SPECTBLS),Y(SPECTB7N)
*
SPECTBLS DS    0H
SPECTBL2 DS    0CL2
         DC    C'0',C' '
         DC    C'1',C'!'
         DC    C'2',C'"'
         DC    C'3',C'#'
         DC    C'4',C'$'
         DC    C'5',C'%'
         DC    C'6',C'&&'
         DC    C'7',C''''
         DC    C'8',C'('
         DC    C'9',C')'
         DC    C'A',C'*'
         DC    C'B',C'+'
         DC    C'C',C','
         DC    C'D',C'-'
         DC    C'E',C'.'
         DC    C'F',C'/'
SPECTB2N EQU   (*-SPECTBL2)/L'SPECTBL2
*
SPECTBL3 DS    0CL2
         DC    C'A',C':'
         DC    C'B',C';'
         DC    C'C',C'<'
         DC    C'D',C'='
         DC    C'E',C'>'
         DC    C'F',C'?'
SPECTB3N EQU   (*-SPECTBL3)/L'SPECTBL3
*
SPECTBL4 DS    0CL2
         DC    C'0',C'@'
SPECTB4N EQU   (*-SPECTBL4)/L'SPECTBL4
*
SPECTBL5 DS    0CL2
         DC    C'B',X'AD'         LEFT BRACKET
         DC    C'C',C'\'
         DC    C'D',X'BD'         RIGHT BRACKET
         DC    C'E',C''
         DC    C'F',C'_'
SPECTB5N EQU   (*-SPECTBL5)/L'SPECTBL5
*
SPECTBL6 DS    0CL2
         DC    C'0',C'`'
SPECTB6N EQU   (*-SPECTBL6)/L'SPECTBL6
*
SPECTBL7 DS    0CL2
         DC    C'B',C'{'
         DC    C'C',C'|'
         DC    C'D',C'}'
         DC    C'E',C'~'
         DC    C'F',C'?'
SPECTB7N EQU   (*-SPECTBL7)/L'SPECTBL7
*
SRCHARGN DC    Y(L'SRCHARGA)
SRCHARGA DC    C'SEARCHARGUMENTS'
*
SRCHHITN DC    Y(L'SRCHHITA)
SRCHHITA DC    C'SEARCHHITS'
*
SRCHRETN DC    Y(L'SRCHRETA)
SRCHRETA DC    C'SEARCHRETURN'
*
BLANKTRT DC    XL256'00'           TABLE FOR LOCATING BLANKS
         ORG   BLANKTRT+C' '
         DC    X'FF'
         ORG   ,
*
HTMLCOMM DC    C'<!-- CCCBKSRV -->'
         DC    X'15'
HTMLCOML EQU   *-HTMLCOMM
*
CAMLSTMD CAMLST NAME,*-*,,*-*      MODEL CAMLST
CAMLSTML EQU   *-CAMLSTMD
*
PATCH    DC    0D'0',32S(*)
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
LITERALS LTORG ,
*
         EJECT
***********************************************************************
*                                                                     *
*        DYNAMIC WORK AREA                                            *
*                                                                     *
***********************************************************************
*
WORKAREA DSECT
SAVEAREA DS    9D
*
DOUBLE   DS    D                   DOUBLE WORD WORK AREA
*
RETCODE  DS    F
FUNCCODE DS    CL4                 FUNCTION CODE
*
HITBLOCK BKMGRCPS ID=XXX,SP=0,SIZE=0
HITBLOCL EQU   *-HITBLOCK
*
SRCBLOCK BKMGRCPS ID=XXX,SP=0,SIZE=0
SRCBLOCL EQU   *-SRCBLOCK
*
AIRXEXCM DS    A(*-*)              ADDRESS OF REXX VARIABLE ROUTINE
*
CPSADDR  DS    A(*-*)              ADDRESS OF CELL POOL ROUTINE
*
BPXWRITE DS    A(*-*)              ADDRESS OF BPX1WRT
*
EXCOMPRM DS    0F                  PARM LIST FOR IRXEXCOM
AEXCOMP1 DS    F                      ---> IRXEXCOM ID
AEXCOMP2 DS    F                      ---> 0
AEXCOMP3 DS    F                      ---> 0
AEXCOMP4 DS    F                      ---> SHVBLOCK
AEXCOMP5 DS    F                      ---> ENVBLOCK
AEXCOMP6 DS    F                      ---> RETURN CODE
*
EXCOMRC  DS    F                   RETURN CODE FROM IRXEXCOM
*
STEMPREL DS    H                   LENGTH OF STEM VARIABLE PREFIX
STEMPREF DS    CL32                STEM VARIABLE PREFIX
*
SUFXLENG DS    H                   SUFFIX LENGTH
SUFXDATA DS    CL16                SUFFIX VARIABLE NAME
*
RETNUM   DS    F                   NUMBER OF RECORDS RETURNED
HITCOUNT DS    F                   NUMBER OF UNIQUE HITS
HITRETN  DS    F                   NUMBER OF UNIQUE HITS RETURNED
*
HITCHAIN DS    A                   ADDRESS OF FIRST HIT BLOCK
HITLAST  DS    A                   ADDRESS OF LAST HIT BLOCK
*
SRCHBKNM DS    CL8                 LAST BOOK NAME (FOR SEARCHING)
*
HITMIN   DS    F                   MINIMUM HIT NUMBER RETURNED
HITMAX   DS    F                   MAXIMUM HIT NUMBER RETURNED
*
SRCHNUM  DS    F                   NUMBER OF SEARCH ARGUMENTS
SRCHAIN  DS    A                   CHAIN OF SEARCH ARGUMENTS
*
BPXWRPRM DS    0F                  BPX1WRT PARAMETER LIST
BPXWRFD  DS    A                   FILE DESCRIPTION POINTER
BPXWRBUF DS    A                   BUFFER ADDRESS ADDRESS
BPXWRALT DS    A                   ALET ADDRESS
BPXWRBFL DS    A                   BUFFER LENGTH ADDRESS
BPXWRRVL DS    A                   WRITE LENGTH RETURN ADDRESS
BPXWRRET DS    A                   RETURN CODE ADDRESS
BPXWRRSN DS    A                   REASON CODE ADDRESS
BPXWRLST EQU   *-4,1               LAST PARAMETER
*
OUTFILED DS    F                   FILE DESCRIPTION NUMBER
OUTBUFAD DS    A                   BUFFER ADDRESS
OUTBUFLN DS    F                   BUFFER LENGTH
OUTBUFRV DS    F                   WRITE COUNT RETURN VALUE
OUTRETCD DS    F                   RETURN CODE
OUTRSNCD DS    F                   REASON CODE
*
         DS    0D
OUTBUFFR DS    CL256               OUTPUT BUFFER
*
         DS    0D
GENLWORK DS    XL1024              GENERAL WORK AREA
*
         ORG   GENLWORK
SRCHARGL DS    H                   LENGTH OF SEARCH ARGUMENTS
SRCHARGS DS    CL256               SAVE AREA FOR RAW SEARCH ARGUMENTS
*
         ORG   GENLWORK
SRCHDEND DS    A                   ADDRESS OF LAST BYTE TO SEARCH
SRCHDLNG DS    H                   LENGTH OF DATA TO SEARCH
SRCHDATA DS    CL256               AREA TO MOVE BOOK SEARCH DATA
SRCHTRTT DS    XL256               SEARCH TRT TABLE
*
         ORG   GENLWORK
CAMLST   CAMLST NAME,*-*,,*-*
*
PDFDSN   DS    CL44                AREA TO BUILD PDF DATA SET NAME
*
         DS    0D
CAMLSTWK DS    CL265
         ORG   CAMLSTWK
CAMLSTCT DS    H
CAMLSTDV DS    XL4
CAMLSTD3 EQU   CAMLSTDV+2,1
CAMLSTVL DS    CL6
CAMLSTMG DS    0C
CAMLSTSQ DS    H
*
         ORG   ,                   GET MAXIMUM LENGTH
*
         DS    0D
WORKLENG EQU   *-WORKAREA
*
***********************************************************************
*                                                                     *
*        AREA FOR EACH RETURNED VARIABLE                              *
*                                                                     *
***********************************************************************
*
BHBDSECT DSECT ,                   BOOK HIT BLOCK
BHBSHV   DS    CL(SHVBLEN)         REXX SHARED VARIABLE BLOCK
*
BHBVARN  DS    CL48                VARIABLE NAME
*
***********************************************************************
*                                                                     *
*        WHILE EACH RETURNED FIELD IS UNIQUELY DEFINED, THE ACTUAL    *
*        RETURNED DATA IS COMPRESSED WITH ONLY A SINGLE BLANK         *
*        BETWEEN EACH DATA ITEM.  THE DEFINITION OF EACH FIELD        *
*        IS TO INSURE THE MAXIMUM SPACE IS AVAILABLE.                 *
*                                                                     *
*        ANY RETURNED FIELD WITH IS ALL BLANK MUST BE REPLACED WITH   *
*        A NON-BLANK VALUE SINCE EVERYTHING IS COMPRESSED.            *
*                                                                     *
*        ON A SECONDARY HIT RECORD, THE FIELDS MARKED BY "*" ARE      *
*        SIMPLY RETURNED AS "*" TO SAVE OVERHEAD.                     *
*                                                                     *
***********************************************************************
*
BHBBKRET DS    0C                  RETURNED VARIABLE DATA
BHBBKNAM DS    CL8                 BOOK NAME
         DS    C
BHBBKPUB DS    CL12               *BOOK PUBLICATION NUMBER
         DS    C
BHBBKDTE DS    CL10               *BOOK BUILD DATE
         DS    C
BHBBKTIM DS    CL8                *BOOK BUILD TIME
         DS    C
BHBBKDSN DS    CL44               *BOOK DATA SET NAME
         DS    C
BHBBKVOL DS    CL7                *BOOK DATA SET VOL SER/MIGRATE LEVEL
         DS    C
BHBBKSHF DS    CL8                 BOOKSHELF NAME
         DS    C
BHBBKSHD DS    CL44                BOOKSHELF DATA SET NAME
         DS    C
BHBBKPDF DS    CL44               *BOOK PDF DATA SET NAME
         DS    C
BHBBKPDV DS    CL7                *PDF DATA SET VOLUME/MIGRATE LEVEL
         DS    C
BHBBKLEN EQU   *-BHBBKRET          LENGTH OF RETURN DATA MINUS TITLE
BHBBKTTL DS    CL200              *BOOK TITLE
*
         DS    0D
BHBLNGTH EQU   *-BHBDSECT          LENGTH OF BOOK HIT BLOCK
*
SABDSECT DSECT ,                   SEARCH ARGUMENT DSECT
SABNEXT  DS    A                   ADDRESS OF NEXT SEARCH ARGUMENT
SABLENG  DS    H                   LENGTH OF THIS SEARCH ARGUMENT
SABDATA  DS    CL64                SEARCH ARGUMENT DATA
         DS    0D
SABLNGTH EQU   *-SABDSECT          LENGTH OF SEARCH ARGUMENT BLOCK
*
         PUSH  PRINT
         PRINT NOGEN
         CVT   DSECT=YES
*
*        IHAPVT
*
         IEFJESCT
*
         IEFJSCVT
*
         IEFUCBOB
*
         IRXSHVB                   REXX SHARED VARIABLE BLOCK
*
         BPXYCONS                  OPENEDITION DEFINITIONS
*
         BKMGRCSA
*
         BKMGRINF
*
         POP   PRINT
*
         END   CCCBKSRV
