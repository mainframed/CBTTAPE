/* REXX */
env.date = date()
env.time = time()

begintime = time('E')

parse arg upper parmfield

parse source source.tso source.mode source.command,
  source.dd source.file source.exec,
  source.cmdenv source.addrspace source.token

/* Set key CGI information */
cgi.execlevel = '2000/02/15-00'
cgi.construction = 0
cgi.ssi = 0
cgi.compiled = 1
cgi.sitename = 'Amdahl Corporate Computer Center'
cgi.titletext = cgi.sitename' Web BookManager'
cgi.bkmgrguru = 'Keith_Moe@notes.amdahl.com'
cgi.mailsubject = 'BookManager BookServer'

/* Put whichever one last for the level of the BookServer */
cgi.booksrv = '2.2.0'
cgi.booksrv = '2.2.0x'

/* BookManager Statistics */
function = 'BKSC'
address LINKMVS 'CCCBKSRV function'
book.shelfcount = rc
function = 'BOOC'
address LINKMVS 'CCCBKSRV function'
book.total = rc

/* BookManager Sequential Data Sets */
book.msgshelf = 'CCCPUBS.AMDAHL.@@MSGS@@.BKSHELF'

/* Define the URL prefixes for ease of altering */
url.booksrvcgibase = '/bookmgr-cgi/'
url.booksrvcgicmd = 'bookmgr.cmd'
url.booksrvcgiexe = 'bookmgr.exe'
url.booksrvcgilpa = 'booksrvr'
url.books = '/BOOKS/'
url.noframes = '/NOFRAMES/'
url.bookshelves = '/Shelves'
url.booklibrary = '/library'
url.bookabout = '/About'
url.bookcases = '/Cases'
url.booktoc = '/CCONTENTS'
url.bookhelp = '/help/library'

select
  when cgi.booksrv = '2.2.0' then
    url.booksrvcgi = url.booksrvcgibase||url.booksrvcgiexe
  when cgi.booksrv = '2.2.0x' then
    url.booksrvcgi = url.booksrvcgibase||url.booksrvcgilpa
  otherwise
    url.booksrvcgi = url.booksrvcgibase||url.booksrvcgicmd
end

url.booksrv = '/bookmgr'
url.bookpdf = '/bookmanager-pdf'
url.mvsimage = '/os390web/images'
url.wwwccc = 'http://www.ccc.amdahl.com'

/* Define the Search Limits */
hit.limit = 50
hit.start = 1
hit.end = 0
hit.next = 0

/* Define the Processing Mode */
query.debug = 0
query.method = 'POST'

/* Define common html verbs */
html.center = '<center>'
html.ecenter = '</center>'
html.br = '<br>'
html.p = '<p>'
html.apost = "'"

/* Archived Data Set display characteristics */
archive.word = 'underlined'
archive.word = 'RED'
archive.colorcode = 'ff0000'

if archive.word = 'underlined' then
  do
    archive.front = '<u>'
    archive.back = '</u>'
  end
else
  do
    archive.front = '<font color="'archive.colorcode'">'
    archive.back = '</font>'
  end

/* Extract information from the environmental variables */
do i = 1 to __environment.0
  parse var __environment.i keyword'='value
  envvar.keyword = value
end

query.pathinfo = envvar.PATH_INFO
env.scriptname = envvar.SCRIPT_NAME
env.server = envvar.SERVER_SOFTWARE
env.serverlevel = '******'
env.browser = envvar.HTTP_USER_AGENT

if envvar.REQUEST_METHOD <> 'POST' then
  do
    query.string = envvar.QUERY_STRING
  end
else
  do
    len = envvar.CONTENT_LENGTH
    if len = '' then
      len = 0
    address syscall 'read 0 query.string' len
  end

env.domino = 0
i = index(env.server,'/')
if i <> 0 then
  env.serverlevel = substr(env.server,i+1)

if index(env.server,'Domino') > 0 then
  env.domino = 1
if env.serverlevel > 'V5' then
  env.domino = 1

env.pdf = 0
if env.domino then
  do
    env.pdf = 1
  end

/* Unfortunately, there is no way to determine compiled dynamically */
if cgi.compiled then
  cgi.execlevel = cgi.execlevel '(compiled)'

/* Get the MVS System Level */
os390.sysopsys = MVSVAR('SYSOPSYS')
os390.sysname = MVSVAR('SYSNAME')
parse var os390.sysopsys os390.name os390.release .
os390.level = os390.name os390.release

/* Initialize key variables */
query.function = ''
query.pathdata = ''
query.data = ''
query.arguments = ''
query.request = ''
query.resume = ''
query.details = 0
query.help = 0
query.noframes = 0

tableheading = 0
hit.books = 0
hit.archived = 0
pdf.books = 0
pdf.archived = 0
booktable.searcharguments = ''
booktable.searchhits = 0
booktable.searchreturn = 0

/* Parse the input data */
if query.pathinfo <> '' then
  do
    parse upper var query.pathinfo '/'query.function'/'query.pathdata
    query.function = strip(query.function)
    query.pathdata = strip(query.pathdata)
  end

if substr(query.function,1,1) = '*' then
  do
    query.debug = 1
    query.function = substr(query.function,2)
  end

if query.function = 'DEBUG' then
  do
    query.debug = 1
    query.function = ''
  end

if query.function = 'HELP' then
  do
    query.help = 1
    query.function = ''
  end

query.action = 'find'

/* Parse the forms query data */
queryscan = translate(query.string)
if queryscan <> '' then
  do i = 1 to 100 until queryscan = ''
    parse var queryscan queryparm'&'queryscan
    parse var queryparm queryverb'='queryoperand
    queryoperand = strip(queryoperand)
    select
      when queryverb = 'SUBMIT' then
        query.request = queryoperand
      when queryverb = 'DATA' then
        query.data = queryoperand
      when queryverb = 'RESUME' then
        query.resume = queryoperand
      when queryverb = 'DETAILS' then
        query.details = 1
      when queryverb = 'NOFRAMES' then
        do
          query.noframes = 1
          url.books = url.noframes
        end
      when queryverb = 'DEBUG' then
        do
          if queryoperand = 'YES' then
            query.debug = 1
          if queryoperand = 'NO' then
            query.debug = 0
        end
      otherwise
        nop
    end
  end

/* Set the Forms debug value */
if query.debug then
  query.debugform = 'YES'
else
  query.debugform = 'NO'

/* Determine the hit range to display */
if query.resume <> '' then
  hit.start = query.resume
hit.end = hit.start + hit.limit - 1
hit.next = hit.end + 1

/* Begin creating the output */
if query.request = 'FIND' then
  do
    cgi.titletext = cgi.titletext 'Search Results'
    if query.resume <> '' then
      cgi.titletext = cgi.titletext '(continued)'
  end
else
  if query.help then
    cgi.titletext = cgi.titletext 'Hints, Tips, and Information'
  else
    cgi.titletext = cgi.titletext 'Entry Page'

call htmlheader

call issheader

if query.debug then
  call debugdump

/* For a Help request, just display information */
if query.help then
  do
    call helpinfo
    signal footing
  end

/* For a non-search, simply display the search field and links */
if query.request <> 'FIND' then
  do
    call searchblock
    call msgblock
    call linkblock
    signal footing
  end

if query.data = '' then
  signal wrapup

function = 'SRCH'
stemprefix = 'BOOKTABLE.'
address LINKMVS,
  'CCCBKSRV function stemprefix query.data hit.start hit.end'
if rc <> 0 then
  signal wrapup

query.arguments = booktable.searcharguments
if booktable.searchhits = 0 then
  signal wrapup

/* Main Book scan and table build */
j = 1
do i = 1 to booktable.searchreturn

  parse var booktable.j bookname bookpubno bookdate booktime,
    bookdsn bookvol bookshelf bookshelfdsn,
    bookpdfdsn bookpdfvol booktitle

  hit.books = hit.books + 1

  ibks = 1
  bookshelfx.1 = bookshelf
  bookshelfdsnx.1 = bookshelfdsn

  if j < booktable.0 then
    do until j = booktable.0
      j = j + 1
      parse var booktable.j nextname . . . . . nextshelf nextshelfdsn .
      if bookname <> nextname then
        leave
      ibks = ibks + 1
      bookshelfx.ibks = nextshelf
      bookshelfdsnx.ibks = nextshelfdsn
    end

  if Â¬tableheading then
    do
      tableheading = 1
      call resultsinstructions
      say html.p'<table border cellspacing=0 cellpadding=6>'
      if query.debug then
        say '<th>Record'html.br'Number</th>'
      say '<th>Publication'html.br'Number</th>'
      say '<th>Book'html.br'Name</th>'
      if env.pdf then
      /*say '<th>PDF'html.br'Avail</th>'*/
        say '<th>PDF</th>'
      if query.details then
        say '<th>Book'html.br'Date & Time</th>'
      say '<th>BookShelf'html.br'Name(s)</th>'
      say '<th>Book Title</th>'
    end

  say '<tr>'
  if query.debug then
    say '<td valign=top>'i'</td>'

  say '<td valign=top>',
    '<a href="'url.booksrvcgi||url.books||bookdsn'/CCONTENTS">',
    bookpubno'</a></td>'

  if substr(bookvol,1,6) = 'MIGRAT' then
    do
      hit.archived = hit.archived + 1
      say '<td valign=top>'archive.front bookname archive.back'</td>'
    end
  else
    say '<td valign=top>'bookname'</td>'

  if env.pdf then
    do
      if substr(bookpdfvol,1,1) <> '*' then
        do
          pdf.books = pdf.books + 1
          bookpdfdsn = "'"bookpdfdsn"'"
          say '<td valign=top><a href="'url.bookpdf'/'bookpdfdsn'">'
          if substr(bookpdfvol,1,6) = 'MIGRAT' then
            do
              pdf.archived = pdf.archived + 1
              say archive.front'YES'archive.back'</a></td>'
            end
          else
            say 'YES</a></td>'
        end
      else
        say '<td valign=top>NO</td>'
    end
  if query.details then
    do
      say '<td valign=top>'bookdate booktime'</td>'
    end

  say '<td valign=top>'
  do k = 1 to ibks
    say '<a href="'url.booksrvcgi'/Shelves/'bookshelfdsnx.k'">',
      bookshelfx.k'</a>'html.br
  end
  say '</td>'

  say '<td valign=top>'booktitle'</td>'
end

/* After search wrap-up */
wrapup:
select
  when booktable.searchhits = 1 then
    do
      if hit.archived <> 0 then
        arc = ' and it is archived.'
      else
        arc = '.'
      say '<caption align=top>'
      say 'There was only 1 match for this search request'arc
      say '</caption>'
    end

  when booktable.searchhits > 1 then
    do
      say '<caption align=top>'
      hittop = hit.start + booktable.searchreturn - 1
      say 'These are hits 'hit.start' through 'hittop,
        'out of 'booktable.searchhits' matches in 'book.total' books.'
      if booktable.searchreturn = hit.archived then
        hit.archived = 'All'
      if hit.archived = 1 then
        arc = 'is'
      else
        arc = 'are'
      say archive.front hit.archived archive.back,
        'of these hits 'arc' archived.'
      if env.pdf then
        select
          when pdf.books = 0 then
            say html.br,
              'There are no PDF Book Files available for these books.'
          when pdf.books = 1 then
            do
              if pdf.archived = 1 then
                arc = ' and it is archived.'
              else
                arc = '.'
              say html.br'There is 1 PDF Book File available'arc
            end
          otherwise
            do
              say html.br'There are 'pdf.books' PDF Files available.'
              if pdf.archived = 1 then
                arc = 'is'
              else
                arc = 'are'
              say archive.front pdf.archived archive.back,
                'of the PDF Book Files 'arc' archived.'
            end
        end
      say '</caption>'
    end

  otherwise
    do
      say '<hr><h2 align=center>No matches on search criteria</h2>'
    end

end

if tableheading then
  say '</table>'html.p

if booktable.searchhits > hit.end then
  do
    hitmore = min(hit.limit,booktable.searchhits - hit.end)
    say html.center,
      'Please refine your criteria to reduce the number of hits',
      'or for the next 'hitmore' matches, click'
    say '<a href="'env.scriptname'/'query.action'?'
    say 'data='query.data'&debug='query.debugform
    say '&submit='query.request'&resume='hit.next
    if query.details then
      say '&details=on'
    if query.noframes then
      say '&noframes=on'
    say '"<b>here</b></a>.'html.ecenter html.p
  end

call searchblock
call msgblock
call linkblock

signal footing

/* End of page processing */
footing:
say '<hr>'
say '</body>'

call processfooter

call issfooter

say '</html>'
exit 0

/* Display the Search Input Form */
searchblock: procedure expose query. url. html. env.
  say html.center'Click here for:&nbsp;&nbsp;&nbsp;'
  say '<a href="'env.scriptname'/help#introduction">',
    'BookServer Overview</a>&nbsp;&nbsp;&nbsp;'
  say '<a href="'env.scriptname'/help#hints">',
    'Usage Hints and Tips</a>&nbsp;&nbsp;&nbsp;'
  say '<a href="'env.scriptname'/help#changes">',
    'Recent Changes</a>&nbsp;&nbsp;&nbsp;'
  say '<a href="'env.scriptname'/help#problems">',
    'Problem Reporting Procedures</a>'html.ecenter

  say '<hr><h2 align=center>'
  if query.request = 'FIND' then
    say 'Enter or Change Search Criteria</h2>'
  else
    say 'Search Book Titles and Document Numbers</h2>'
  say '<form ACTION="'query.action'" METHOD="'query.method'">'
  say '<input type="hidden" name="debug" value="'query.debugform'">'
  say html.center'<table border=0>'
  say '<tr><td> </td><td>Find Books with Titles or',
    'Document Numbers containing (NOT case sensitive):</td>'
  say '<tr><td><img src="'url.booksrv'/search.gif" alt="Find"></td>'
  say '<td><input type="text" name="data"',
    'value="'query.arguments'"size=60 maxlength=64></td>'
  say '<td><input type="submit" name="submit" value="Find"></td>'

  say '<tr><td> </td>'
  checked = ''
  if query.details then
    checked = 'checked'
  say '<td><input type="checkbox" name="details"' checked'>',
    'Display Book Build Date & Time'
  checked = ''
  if query.noframes then
    checked = 'checked'
  say html.br'<input type="checkbox" name="noframes"' checked'>',
    'Disable use of FRAMES in Book Display'
  say '</td></table>'html.ecenter'</form>'
return

/* Display the Link to Search Messages */
msgblock: procedure expose url. html. book.
  say '<h2 align=center>OR'html.br,
    'Search the Messages and Codes BookShelf</h2>'
  say '<form action="'url.booksrvcgi'/Shelves/'book.msgshelf'"',
    'method="get">'
  say html.center'<table border=0>'
  say '<tr><td><img src="'url.booksrv'/search.gif" alt="Search"></td>'
  say '<td><input type="text" name="searchRequest" value=""',
    'size=60 maxlength=64></td>'
  say '<td><input type="submit" name="SEARCH" value="Search"></td>'
  say '<tr><td> </td>'
  say '</table>'html.ecenter'</form>'

  say '<h2 align=center>OR</h2>'
  say '<a href="'url.booksrvcgi'/Shelves/'book.msgshelf'">'
  say html.center'<font size=+2>',
    'Display the complete Messages and Codes BookShelf</a>'
  say '</font>'html.ecenter
return

/* Display the BookServer Links */
linkblock: procedure expose url. html. book.
  say html.center
  say '<h2 align=center>OR'html.br,
    'Use one of these to enter the BookServer</h2>'

  say '<table border=0 cellspacing=0 cellpadding=6>'
  say '<tr><td><a href="'url.booksrvcgi'/library">'
  say html.center'<img src="'url.booksrv'/libicon2.gif"',
    'border=0 alt="BookServer">'html.br
  say 'The IBM BookManager BookServer</a>'
  say html.br'Main Entry Point'html.ecenter'</td>'
  say '<td><a href="'url.booksrvcgi'/Cases">'
  say html.center'<img src="'url.booksrv'/bookcase.gif"',
    'border=0 alt="BookCases">'html.br
  say 'BookServer BookCases</a>'html.br
  say '(currently not very useful)'html.ecenter'</td>'
  say '<tr><td><a href="'url.booksrvcgi'/Shelves">'
  say html.center'<img src="'url.booksrv'/bookshf.gif"',
    'border=0 alt="BookShelves">'html.br
  say 'BookServer BookShelves</a>'html.br,
    '('book.shelfcount')'html.ecenter'</td>'
  say '<td><a href="'url.booksrvcgi'/help/library">'
  say html.center'<img src="'url.booksrv'/help.gif"',
    'border=0 alt="BookServer Help">'html.br
  say 'BookServer'html.br'Help</a>'html.ecenter'</td>'
  say '</table>'html.ecenter
return

/* Display the Under Construction Block */
underconstruction: procedure expose url. html.
  say html.center
  say '<img src="'url.booksrv'/construc.gif" align=middle',
    'alt="Under Construction">'
  say html.br'When this appears, it means that there have been'
  say 'recent changes.  Hopefully, they will be error free.'
  say 'But if they aren'html.apost't, let us know.'
  say 'Please use the <b>e-mail</b> link near the bottom of this page.'
  say html.ecenter html.p
return

/* Display the Help Information */
helpinfo: procedure expose url. html. book. hit. env.

  say html.center'<a href="#hints">',
    'Usage Hints and Tips</a>'html.ecenter
  say html.center'<a href="#changes">',
    'Recent Changes</a>'html.ecenter
  say html.center'<a href="#problems">',
    'Problem Reporting Procedures</a>'html.ecenter

  say '<hr><h2 align=center><a name="introduction">'
  say 'Introduction to the IBM BookServer</a></h2>'

  say 'The IBM BookServer is an alternative to the MVS/TSO based'
  say 'BookManager Read method of accessing the large collection'
  say 'of online publications stored on the MainFrames in the'
  say 'Corporate Computer Center.  Just as the MVS/TSO BookManager'
  say 'Read uses an existing tool (ISPF) to perform the display of'
  say 'output, the BookServer also uses an existing tool (The Web'
  say 'Browser) as its method of access and display.',
    'This means that no special'
  say 'programs or files are needed.  By virtue of you reading this,'
  say 'it can be assummed that you have a Web Browser!'

  say html.p'The BookServer accesses the exact same set of Books and'
  say 'BookShelves that the MVS/TSO BookManager Read accesses.'
  say 'The use of Web access instead of TSO provides capabilities'
  say 'that are not available in a 3270/TSO/ISPF environment.  Some'
  say 'of these are:<ul>'
  say '<li>No need to log on with a User ID and Password'
  say '<li>Graphics in Books are displayed inline'
  say '<li>Easier navigation through Books and BookShelves'
  say '</ul>'

  say html.p'There are two basic ways to use the IBM BookServer.'
  say 'The first (and original) way is to display the List of'
  say 'BookShelves ('book.shelfcount'), scroll through it, and select'
  say 'the BookShelf containing the Book(s) you are interesting in.'
  say 'You can then perform a search on the BookShelf (much like'
  say 'the MVS/TSO environment) or select a specific Book.  While'
  say 'this way is no worse than the MVS/TSO environment, it depends'
  say 'on you having a good idea of which BookShelves the Book(s) you'
  say 'are looking for are located in.'
  say 'Hence, enter the second method.'

  say html.p'The second way makes use of a Data Base that CCC had been'
  say 'utilizing to produce the TSO Browseable list of all Books that'
  say 'are online in BookManager.',
    'By CCC providing a search process against'
  say 'this Data Base, you are able to search for specific Books by'
  say 'Publication Number or words in the Book Titles.  In addition,'
  say 'a direct search of the Messages and Codes BookShelf is provided'
  say 'to simplify searching on messages.'

  say '<hr><h2 align=center><a name="hints">'
  say 'Usage Hints and Tips</a></h2>'
  say 'When searching on Publication Numbers or Book Titles, keep'
  say 'the following in mind:<ul>'
  say '<li>All search arguments are <b>NOT</b> case sensitive.'
  say '<li>Avoid including the "dash number" when specifying'
  say 'Publication Numbers, as paper and online publishing schedules'
  say 'seldom correspond.'
  say '<li>A maximum of 'hit.limit' search hits will be displayed.'
  say 'Although you have the ability to request the second 'hit.limit','
  say 'the third 'hit.limit', etc., you should consider refining the'
  say 'search parameters if you receive too many matches.'
  say '<li>The "Display Book Build Date & Time"'
  say 'checkbox allows you to see when the BookManager Book was built'
  say 'by the supplier.  It <b>may not</b> correspond to the date the'
  say 'corresponding HardCopy document was published, as many Books'
  say 'have multiple SoftCopy revisions without a HardCopy revision.'
  if env.pdf then
    do
      say '<li>The "Display PDF File availability"'
      say 'checkbox allows you to see if the SoftCopy Books have'
      say 'corresponding PDF Files which can be browsed and/or printed'
      say 'with your Web Browser (Adobe Acrobat Plug-in required).'
      say 'Requesting PDF File information will <b>elongate</b> the'
      say 'search time, so don'html.apost't request PDF information'
      say 'if you don'html.apost't need it.'
    end
  say '<li>The "Disable use of FRAMES in Book Display"'
  say 'checkbox allows you to see a Book as a full frame in your'
  say 'Browser, rather than in three frames.  While the use of frames'
  say 'provides for enhanced Book navigation, it requires the use of'
  say 'JavaScript and can be a performance problem on slower machines.'
  say '</ul>'

  say '<hr><h2 align=center><a name="changes">'
  say 'Recent Changes</a></h2>'
  say 'Changes made to the Amdahl BookServer Front-End are:<ul>'
  say '<li>Rewritten to improve performance.'
  if env.pdf then
    say '<li>PDF Book information is always displayed.'
  say '<li>Support for the IBM BookServer Release 2.2.0.'
  say '<li>Ability to disable frames (temporary bypass).'
  say '</ul>'
  say 'Changes made to the IBM BookServer are:<ul>'
  say '<li>BookServer Release 2.2.0.'
  say '<li>The URL for the BookServer has changed.'
  say 'The portion of the URL that was "'url.booksrvcgicmd'"',
    'has been changed to "'url.booksrvcgilpa'".'
  say '<li>By default, Books are now displayed using frames:'
  say 'An Icon Frame; A Table of Contents Frame; and the Topic Frame.'
  say 'The use of frames requires a Browser that supports JavaScript.'
  say '</ul>'

  say '<hr><h2 align=center><a name="problems">'
  say 'Problem Reporting</a></h2>'
  say 'Like anything, try as we might, neither the IBM BookServer'
  say 'nor the search "Front-End" are likely to be error free.'
  say 'To that end, we need to remind you of several things:<ul>'
  say '<li>We aren'html.apost't likely to fix bugs we',
    'don'html.apost't know about.'
  say '<li>We can'html.apost't fix bugs in the IBM BookServer (but we'
  say '<b>can</b> report them to IBM).'
  say '<li>We can'html.apost't fix a broken Book (but we <b>can</b>'
  say 'report it to the supplier).'
  say '<li>We are <b>not</b> responsible for the publication and/or'
  say 'availability cycles of Online Books.  We upload what we receive.'
  say '<li>We <b>are</b> responsible for the search "Front-End" and'
  say 'much appreciate feedback on problems and suggestions.'
  say '</ul>'

  say html.p'That said, problem reporting and feedback can be best'
  say 'accomplished by using the contact address'
  say 'at the end of each Front-End page.  Although the IBM'
  say 'BookServer pages are built by the IBM BookServer product,'
  say 'they now have contact information on them.'

  say html.p'In reporting a problem, it is important to provide'
  say 'information that we can use to recreate the problem and'
  say 'determine if we can fix it or report it to the vendor.'
  say 'The BookServer will display a whole bunch of diagnostic'
  say 'information when it encounters an error, but it unfortunately'
  say 'does not bother to contain important information like:<ul>'
  say '<li>The Book Name (the funny name like IEA1AB01, not the title'
  say 'or Publication Number).'
  say '<li>Which BookShelf it was on (if accessing from a BookShelf).'
  say '<li>The Topic Number that was being requested or displayed.'
  say '<li>The search arguments used if a search was used.'
  say '</ul>'
  say 'So when reporting a problem while you are within the IBM'
  say 'BookServer (at the Book or BookShelf level), make sure that'
  say 'you provide any of the applicable above information.'
  say 'If you are reporting a problem with the search Front-End,'
  say 'the search arguments are perhaps the most important item.'

return

/* Display the Instructions for the Search Results */
resultsinstructions: procedure expose archive. html. query.
  say '<hr><h2 align=center>Search Results</h2>'
  if query.resume <> '' then
    return
  say 'Click on <b>Publication Number</b> to go to the Table of'
  say 'Contents for the Book.  If the <b>Book Name</b>',
    'or <b>PDF </b> column is'
  say archive.front||archive.word||archive.back','
  say 'the Book or PDF Data Set has been archived by HSM and there'
  say 'will be a delay while it is recalled from tape.'
  say 'The <b>BookShelf Name(s)</b> that a Book is contained'
  say 'in are provided to allow easy access to related Books.'
  say 'Note that many Books appear in more than ONE BookShelf.'
  if query.details then
    do
      say 'The <b>Book Date & Time</b>'
      say 'are when the SoftCopy Book was built and may'
      say '<b>not</b> correspond to a hardcopy publication date.'
    end
  say 'PDF Book Files are accessible only if the'
  say 'Adobe Acrobat Reader in installed and available'
  say 'as a Plug-in on your Web Browser.'
return

/* Create the beginning of the html output */
htmlheader: procedure expose cgi.
  if cgi.ssi then
    say 'Content-type: text/x-ssi-html'
  else
    say 'Content-type: text/html'
  say
  say '<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">'
  say '<html>'
  say '<head>'
  say '<title>'cgi.titletext'</title>'
  say '</head>'
  say '<body>'
return

/* Display the processing footer */
processfooter: procedure expose env. os390. url. html. cgi.
  elapsed = time('R')
  elapsed = substr(elapsed,1,5)
  address syscall 'times cputime.'
  cpuused = (cputime.1 + cputime.2) / 100

  say '<font size=-1>'
  say 'This request started on 'env.date' at 'env.time,
    'and took <font color="0000ff">'elapsed'</font> elapsed seconds',
    'and <font color="0000ff">'cpuused'</font> CPU seconds, using the',
    env.server' on 'os390.level' on the',
    cgi.sitename' System 'os390.sysname'.'
  say '</font>'

  say html.p html.center'<img src="'url.mvsimage'/amdahl_s.gif"',
    'alt="AMDAHL logo">'html.ecenter

  if cgi.construction then
    call underconstruction

  say html.center html.p '<font size=-1><b>'
  say 'If you have any questions or comments about this page,',
    'please contact the</b>'
  say '<a href="mailto:'cgi.bkmgrguru'?subject='cgi.mailsubject,
    cgi.execlevel os390.sysname'">'
  say '<i>BookManager Guru</i></a></font>'

  say html.br'<font size=-1>CGI Exec Level:</font>',
    '<font size=-1 color="#d2691e">'cgi.execlevel'</font>'
  say '<a href="mailto:'cgi.bkmgrguru'?subject='cgi.mailsubject,
    cgi.execlevel os390.sysname'">'
  say html.br'<img src="'url.mvsimage'/mail.gif" alt="Mail"></a>'
  say html.ecenter
return

/* Display the ISS Header */
issheader: procedure expose url. html.
  say html.center,
    '<a href="'url.wwwccc'/cgi-bin/imagemap/intranet/home.map">'
  say '<img border=0 src="'url.mvsimage'/isslogo.gif"'
  say 'alt="Information Systems and Services (imagemap)"'
  say 'width=617 height=96 ismap></a>'html.ecenter html.p
return

/* Display the ISS Footer */
issfooter: procedure expose url. html.
  say html.p html.center,
    '<a href="'url.wwwccc'/cgi-bin/imagemap/intranet/xfoot.map">'
  say '<img src="'url.mvsimage'/xfooter.gif"',
    'alt="Navigation bar (imagemap)"'
  say 'border=0 height=20 width=537 ismap></a>'
  say html.p'<font size=-1>',
    '<a href="'url.wwwccc'/intranet/map.html">Map</a> |'
  say '<a href="'url.wwwccc'/WWW/ogma.html">White Pages</a> |'
  say '<a href="'url.wwwccc'/yellowpages/">Yellow Pages</a> |'
  say '<a href="http://search.amdahl.com/search/">Search</a> |'
  say '<a href="'url.wwwccc'/intranet/">Amdahl Global Access</a> |'
  say '<a href="'url.wwwccc'/intranet/feedback.htm">Feedback</a>',
    html.br
  say '<a href="'url.wwwccc'/intranet/downloads.htm">Downloads</a> |'
  say '<a href="'url.wwwccc'/amdahl.online/">Amdahl Online</a> |'
  say '<a href="http://www.amdahl.com">External Home</a> |'
  say '<a href="http://ldap.amdahl.com/ds/phone">Phone Book</a> |'
  say '<a href="'url.wwwccc'/field/field1.htm">Field Info</a> |'
  say '<a href="'url.wwwccc'/intranet/tools.html">Tools</a>'html.br
  say '<a href="'url.wwwccc'/ags/">Amdahl Global Services</a> |'
  say '<a href="'url.wwwccc'/pss/">Product Support Services</a> |'
  say '<a href="'url.wwwccc'/intranet/news.html">',
    'News & Events</a>'html.br
  say '<a href="'url.wwwccc'/intranet/servorg.html">',
    'Internal Service Organizations</a> |'
  say '<a href="'url.wwwccc'/intranet/corpinfo.html">',
    'Corporate Info</a>'html.br
  say '<a href="'url.wwwccc'/WWW/copyright.html">Copyright</a>'
  say '</font>'html.ecenter
return

/* Dump a bunch of variables for debugging purposes */
debugdump: procedure expose query. __environment. source. html.
  say html.p'Source Information:',
    source.tso source.mode source.command,
    source.dd source.file source.exec,
    source.cmdenv source.addrspace source.token

  say html.p'Path Data is 'query.pathinfo html.br
  say 'Function is 'query.function,
    'and the data object is 'query.pathdata html.br
  say 'The Query String is 'query.string html.br
  say 'The Query Data Verb is DATA',
    'and the Query Data is 'query.data html.br
  say 'The Query Request Verb is SUBMIT',
    'and the Query Request is 'query.request html.br
  say 'The Query Resume Verb is RESUME',
    'and the Query Resume is 'query.resume html.br

  say html.p'Environmental variables:'html.br
  do i = 1 to __environment.0
    say '__environment.'i' is '__environment.i html.br
  end
  say html.p

return

