TAPEMAP  TITLE 'TAPE ANALYSIS PROGRAM  (T A P E M A P)'     2006316 GYP
*          FROM CBT MODS TAPE, FILE 582 (REWORKED 299)      2006316 GYP
XYZZY    OPSYN PRINT               CUSTOMIZED PRINT CONTROL   06/84 DBC
PRINT    OPSYN ANOP                SUPPRESS STD PRINT CONTROL 06/84 DBC
*------------------------------------------------------------ 07/92 SBG
*                                                             07/92 SBG
*  *************                                              SBG 03/00
*  * ATTENTION * YOU CAN USE IECTDEBX INSTEAD OF IEZDEB TO    SBG 03/00
*  ************* SPECIFY DEB FIELDS.  I DON'T RECOMMEND THE   SBG 03/00
*                IDEA.  IT IS BETTER TO CODE DEBX=NO.         SBG 03/00
*                                                             SBG 03/00
*   IEZDEB IS ALIVE AND WELL.  AT THE SP4 LEVEL, I THOUGHT    SBG 03/00
*    IT WASN'T, SO I CODED THIS FLEXIBILITY.  BETTER TO       SBG 03/00
*    LEAVE THINGS AS THEY ALWAYS WERE.                        SBG 03/00
*                                                             SBG 03/00
         GBLC  &DEBX                                          07/92 SBG
*&DEBX   SETC  'YES'    'YES' TO USE IECTDEBX MACRO FOR DEB.  SBG 03/00
&DEBX    SETC  'NO'     'NO'  TO USE IEZDEB   MACRO FOR DEB.  SBG 03/00
*------------------------------------------------------------ 02/93 RT
         GBLC  &XA                                            02/93 RT
&XA      SETC  'L'      'L' TO USE NON-XA BAL INSTRUCTION     02/93 RT
*&XA     SETC  'S'      'S' TO USE     XA BAS INSTRUCTION     02/93 RT
         LCLB  &HERC    SET TO TRUE IF COMPILING IN HERCULES  07/02 SRS
&HERC    SETB  1        YES, THIS IS A HERCULES VERSION       07/02 SRS
*&HERC   SETB  0        NO                                    07/02 SRS
*                                                             07/92 SBG
*------------------------------------------------------------ 07/92 SBG
         TITLE 'TAPE ANALYSIS PROGRAM  (T A P E M A P)'
         GBLA  &LINEPPG                                       11/88 SBG
&LINEPPG SETA  60                                             11/88 SBG
TAPEMAP  CSECT
         USING *,R15
         B     ARNDCONS
         DC    AL1(25)
         DC    CL8'TAPEMAP'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         DS    0F
GETM     DC    AL1(0),AL3(WORKLEN)
ARNDCONS DS    0H
         SAVE  (14,12)
         DROP  R15
         USING TAPEMAP,R12,R11,R7,R9
         LR    R12,R15
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LA    R7,4095(,R11)
         LA    R7,1(,R7)
         LA    R9,4095(,R7)
         LA    R9,1(,R9)
         L     R2,0(,R1)            PICK UP PARM POINTER
         L     R0,GETM
         GETMAIN R,LV=(0)
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE-AREA ADDR
         USING WORKD,R13
         L     R15,=A(LITMOVES)     POINT TO LITERALS TO LOAD 12/88 SBG
         MVC   EMPTYLIT(256),0(R15)     MOVE THEM TO WORKAREA 12/88 SBG
         MVC   EMPTYLIT+256(256),256(R15)                     10/90 SBG
         MVC   EMPTYLIT+512(LITMVLEN-512),512(R15)            10/90 SBG
         MVI   BLANKS,C' '
         MVC   BLANKS+1(L'BLANKS+L'OUTBUFF),BLANKS  GET OUTBUFF ALSO
         MVC   OUTCLR2(L'OUTBUFF2+2),OUTCLEAR  GET CC FOR "DASHES" ALSO
         MVC   DASHES(2),=C' -'    PRIME                      06/84 DBC
         MVC   DASHES+2(L'DASHES-2),DASHES+1
         MVC   PARM#MSG+1(L'PARM#MSG-1),PARM#MSG
         XC    JFCB(176),JFCB
         LA    R0,JFCB
         ST    R0,EXLST
         MVI   EXLST,X'87'
         LA    R0,TAPEBUFF              GET ADDR OF DATA BUFFER
         ST    R0,TCCW#DAT              SET INTO CCW
         MVI   TCCW#DAT,RD              RESTORE THE OPCODE
         LA    R1,FL1LABI               GET LABEL BUFFER ADDR
         ST    R1,TCCW#LBL              SET IN CCW
         MVI   TCCW#LBL,RD              RESTORE OPCODE
         ST    R1,TCCW#EOV              SET IN CCW            02/93 RT
         MVI   TCCW#EOV,RD              RESTORE OPCODE        02/93 RT
         LA    R0,FL2LABI               GET LABEL BUFFER ADDR
         ST    R0,TCCW#LBL+8            SET IN CCW
         MVI   TCCW#LBL+8,RD            RESTORE OPCODE
         LA    R1,MV#BUFF2              GET 2ND BUFFER ADDR
         ST    R1,MV#ABUF2              SAVE
         LA    R0,OUTBUFF2+110          GET ADDR OF END OF OUTBUFF2
         ST    R0,OB2END                SAVE FOR FUTURE REFERENCE
         LA    R0,OUTBUFF2+110+10       FOR UPDTE STUFF
         ST    R0,OB2END2
         SPACE 2
         OPEN  MF=(E,OPENMFL)           OPEN PRINT FILE
         L     R1,OPENMFL               GET SYSPRINT DCB ADDR
         TM    48(R1),X'10'             OPEN?
         BO    PRINTOK                  YES
         LA    R1,100                   GET ABEND CODE
         SVC   13                       AND LEAVE
         SPACE 2
PRINTOK  LH    R15,0(,R2)               GET PARM LEN
         LTR   R15,R15                  ANY?
         BNP   DONEPARM                 NO
         LA    R14,2(,R2)               SET ADDR OF FIRST ITEM
         LR    R0,R14                   SAVE IT
         MVC   PARM#MSG(8),=C' PARM='' '
         MVC   PARM#MSG+8(L'PARM#MSG-8),PARM#MSG+7   BLANK THE REST
         CH    R15,=H'21'               PARM TOO LONG FOR "TITLE2"?
         BNH   *+8                      NO - SKIP
         OI    TFLAG2,T2@LPARM          YES - SET FLAG
         EX    R15,MVCPARM              SAVE PARM IN OUTBUFF2
         LA    R1,PARM#MSG+7(R15)       GET ADDR OF END+1
         MVI   0(R1),C''''              PUT IN ENDING QUOTE
         OI    TFLAG1,T1@PARM           SET "HAVE PARM" FLAG
         SPACE 2
NEXTPARM LM    R1,R3,=A(PARMTAB,12,PARMLAST)
TESTPARM LH    R4,0(,R1)                GET LENGTH OF ITEM
         EX    R4,CLCPARM               THIS IT?
         BNE   INCRPARM                 NO - TRY NEXT
         OC    PARMFLAG,11(R1)          TURN ON BITS
         LA    R14,2(R14,R4)            INCR SCAN PTR
         SR    R15,R4                   DECR LENGTH LEFT
         SH    R15,=H'2'
         BP    NEXTPARM                 LOOP IF ANY MORE
         B     DONEPARM                 DONE...
         SPACE 2
NULLPARM SH    R15,=H'5'                DECR LENGTH LEFT            U14
         BNP   DONEPARM                 NOTHING LEFT                U14
         LA    R14,5(,R14)              POINT TO OPERAND            U14
         SR    R1,R1                    CLEAR ACCUMULATOR           U14
         SPACE 1
NULLLOOP CLI   0(R14),C'0'              DIGIT?                      U14
         BL    HAVENULL                 NO - HAVE COMPLETE NUMBER   U14
         IC    R0,0(,R14)               GET A DIGIT                 U14
         N     R0,F15                   STRIP IT                    U14
         MH    R1,=H'10'                SHIFT PREVIOUS              U14
         AR    R1,R0                    ADD NEW                     U14
         LA    R14,1(,R14)              BUMP SCAN PTR               U14
         BCT   R15,NULLLOOP             CONTINUE FOR LENGTH OF PARM U14
         SPACE 2
HAVENULL LTR   R1,R1                    ANYTHING GIVEN?             U14
         BNP   *+8                      YES - USE IT                U14
         STH   R1,NULLNUMB              SET NUMBER                  U14
         LA    R14,1(,R14)              BUMP SCAN PTR               U14
         SH    R15,H1                   DECR LENGTH LEFT            U14
         BP    NEXTPARM                 CONTINUE IF MORE            U14
         B     DONEPARM                 NONE LEFT                   U14
         SPACE 2
INCRPARM BXLE  R1,R2,TESTPARM           KEEP LOOKING
*  DIDN'T FIND IT IN TABLE
         CLC   =C'NULL=',0(R14)         SPECIAL KIND?               U14
         BE    NULLPARM                 YES - PROCESS IT            U14
*  INVALID PARM ITEM
         SR    R14,R0                   COMPUTE OFFSET INTO PARM
         LA    R14,1(,R14)              FIX IT
         CVD   R14,DWD                  CONVERT TO PACKED
         UNPK  PARMERR+28(3),DWD+6(2)   PUT INTO MSG
         OI    PARMERR+30,C'0'          FIX SIGN
         OI    TFLAG1,T1@PERR           SET "PARM ERROR" FLAG
         SPACE 3
DONEPARM LA    LCTR,&LINEPPG            INIT LINECOUNTER      11/88 SBG
         CALL  TODAY,(TTL1DATE,TTL1DAY,TTL1TIME)              02/93 RT
         L     R1,16                    -> CVT
         L     R1,0(,R1)                -> TCBWORDS
         L     R1,4(,R1)                -> TCB
         L     R1,12(,R1)               -> TIOT
         LA    R1,24(,R1)               -> DD SECTION
         ST    R1,DD#PTR                SAVE PTR
         SPACE 1
         TM    PARMFLAG,PF@INLIN+PF@NATTR PARM=INLINE OR NOATTR?
         BNZ   OK$PRT2                  YES - SKIP SECOND PRINT FILE
         L     R2,OPENMFL2              GET SYSPRNT2 DCB ADDR
         ICALL TIOTSCAN,R10,R15=40(,R2) SEE IF WE HAVE IT
         B     OK$PRT2                  NOPE
         SPACE 1
         OPEN  MF=(E,OPENMFL2)          OPEN SECOND PRINT FILE
         OI    TFLAG2,T2@PRT2           ASSUME IT WORKS.
         TM    48(R2),X'10'             DID IT OPEN?
         BO    OK$PRT2                  YES
         SPACE 2
ABEND99  LA    R1,99                    GET ABEND CODE
         SVC   13                       BYE
         SPACE 2
OK$PRT2  L     R2,OPENMFLI              GET SYSIN DCB ADDR
         ICALL TIOTSCAN,R10,R15=40(,R2) SEE IF IT'S THERE
         B     RDJFCB                   NOPE
         MVC   RET#ADDR,=A(READCARD)    SET RETURN ADDR
         SPACE 2
         OPEN  MF=(E,OPENMFLI)          OPEN INPUT FILE
         TM    48(R2),X'10'             OPEN?
         BZ    ABEND99                  NO.  (THIS SHOULD NEVER HAPPEN)
         MVI   OPENMFL,0                SET TO CLOSE BOTH FILES
         SPACE 2
READCARD L     R1,OPENMFLI              GET SYSIN DCB ADDR
         GET   (1),INBUFF
         OI    TFLAG1,T1@DATA           SET FLAG
         CLC   =C'./DDNAME',INBUFF      IS IT THIS?
         BE    INDDNAME                 YES - PROCESS
         CLC   =C'./VOLUME',INBUFF      IS IT THIS?
         BNE   *+10                     NO - MUST BE VOL NAME
         MVC   INBUFF(6),INBUFF+9       MOVE OVER
         MVI   JFCB+117,1               # OF VOL SER #'S
         MVC   JFCB+118(6),INBUFF       VOL SER #
         MVI   JFCB+52,X'40'            VOLUME SERIAL LIST CHANGED
         B     TAPEINDX                 SKIP THE RDJFCB, GO TO SUBROUTN
         SPACE 2
INDDNAME L     R1,TAPEMFL               GET ADDR OF TAPEDCB         U14
         MVC   40(8,R1),INBUFF+9        MOVE DDNAME INTO DCB        U15
         SPACE 2
RDJFCB   RDJFCB  MF=(E,TAPEMFL)
         B     TAPEINDX                 GO TO SUBROUTINE
         SPACE 3
EOD2     CLOSE MF=(E,OPENMFL)
         IFP2  N,LEAVE                  SKIP IF NOT OPEN
         CLOSE MF=(E,OPENMFL2)
         B     LEAVE                    RETURN
         SPACE 2
EOD      TM    TFLAG1,T1@DATA           DATA READ IN?
         BO    EOD2                     YES.
         MVC   RET#ADDR,=A(EOD2)        SET NEW RETURN ADDR
         B     RDJFCB
         SPACE 2
MVCPARM  MVC   PARM#MSG+7(0),0(R14)     << EXECUTED >>
CLCPARM  CLC   2(0,R1),0(R14)           << EXECUTED >>
         SPACE 2
******** PRINT GEN
         EJECT
***********************************************************************
*                                                                     *
* T A P E I N D X                                                     *
*                                                                     *
*    A ROUTINE WHICH READS IN THE VOLUME NAME & ALL THE DATA SET NAMES*
*        (FROM THE TRAILER LABELS) OF A TAPE (WITH STANDARD LABELS)   *
*        AND FORMATS AND PRINTS THE INFORMATION FROM THE LABELS.      *
*                                                                     *
***********************************************************************
         SPACE 1
TAPEINDX MVC   LCTR2,=F'0'              INIT 2ND LINE COUNTER 02/93 RT
         MVC   NUMBNULL,=F'0'           INIT COUNT OF NULL FILES    RT
         OI    TFLAG1,T1@PAGE1+T1@ANLZ  1ST PAGE/THIS VOL + ANLZ LABEL
         SPACE 1
* OPEN TAPE VOLUME FOR LABEL=BLP
         MVC   JFCB+44(8),BLANKS        BLANK OUT ELEM NAME
         MVC   JFCB+68(2),=H'1'         FILE SEQ #                 WBF
         MVC   JFCB+70(2),=H'1'         VOL SEQ #
         MVC   JFCB+83(3),=X'620000'    SET EXPDT=98000 FOR UCC-1  WBF
         MVI   JFCB+98,X'40'            DSORG=PS
         MVI   JFCB+100,X'90'           RECFM=FB
         MVC   TITLE1+5(6),JFCB+118     INIT IN CASE NOT SL
         SPACE 1
         OPEN  MF=(E,TAPEMFL),TYPE=J
         TM    TAPEDCB+DCBOFLGS-IHADCB,DCBOFOPN   FILE OPEN ?       THO
         BO    TPOPENOK                 YES.                        THO
         NEWPAGE ,                      PRINT THE HEADER
         PRTLN '-*** TAPE VOLUME COULD NOT BE OPENED ***'
         B     RETURN
         SPACE 3
TPOPENOK NI    TAPEDCB+DCBMACF1-IHADCB,X'F7'  BLK COUNT INACCURATE  THO
         XC    LEN#TAPE(4*2),LEN#TAPE   CLEAR SL & NL LENGTHS
         NI    TFLAG1,255-T1@BADEN-T1@SL RESET FLAGS
         MVI   WHERE,0                  SET "WHERE ARE WE" FLAG
         TAPIO TCCW#RWD,TM=*+4     INSURE PROPER POSITION     09/84 DBC
         XC    FILE#SEQ,FILE#SEQ   CLEAR PHYSICAL FILE#       07/85 DBC
         TAPIO TCCW#DAT,TM=LEADTPMK     READ IN VOLUME LABEL
         MVC   VOLLABI(80),TAPEBUFF     SAVE VOLUME LABEL
         SPACE 1
DO$SENSE L     R2,LASTSIZE         PRESERVE LENGTH OVER SENSE 09/84 DBC
         TAPIO TCCW#SNS                 READ SENSE DATA
         L     R1,TAPEDCB+DCBDEBAD-IHADCB   LOAD ADDRESS OF DEB     THO
         AIF   ('&DEBX' EQ 'YES').NEWDEB                            SBG
         L     R1,DEBSUCBA-DEBBASIC(,R1)    LOAD ADDRESS OF UCB     THO
         AGO   .OLDDEB                                              SBG
.NEWDEB  ANOP                                                       SBG
         L     R1,DEBUCBAD-DEBNMSUB(,R1)    LOAD ADDRESS OF UCB     SBG
.OLDDEB  ANOP                                                       SBG
         CLC   UTY3420C(4),UCBTYP-UCB(R1)        3480 COMPAT MODE? ^SBG
         BE    IS3480                   YEP. TREAT AS 3480.        ^SBG
         CLI   UCBTBYT4-UCB(R1),X'81'       THIS A 3490 TAPE DRIVE ?SBG
* --------->>> DON'T USE MAPPING MACRO BECAUSE OF OLDER SYSTEMS.    SBG
         BE    IS3480                   YEP. TREAT AS 3480.        ^SBG
*
*------- CLI   UCBTBYT4-UCB(R1),UCB3480     THIS A 3480 TAPE DRIVE ?THO
         CLI   UCBTBYT4-UCB(R1),X'80'       THIS A 3480 TAPE DRIVE ?SRS
         BNE   IS3420                   NO, THEN A 3420 DRIVE       THO
IS3480   LA    R1,C'5'                  THEN INDICATE SO     ^SBG - THO
         MVC   TITLE1+34(4),=C'CART'    THEN INDICATE CARTRIDGE     THO
         NI    PARMFLAG,255-PF@DEN1     TURN OFF                    THO
         B     TRUE$GOT                                             THO
IS3420   LA    R1,C'3'                  ASSUME 1600 BPI             THO
         TM    SENSDATA+3,B'00000100'   PE MODE?
         BO    TRUE$GOT                 YES
         LA    R1,C'4'                  ASSUME 6250 BPI
         TM    SENSDATA+9,B'00001000'   CAPABLE OF 6250 BPI?
         BO    TRUE$GOT                 YES
         AIF   (&HERC).TRUE$            FORCE 6250 IF HERCULES 07/02SRS
         LA    R1,C'2'                  ELSE, 800 BPI
         SPACE 2
.TRUE$   ANOP
TRUE$GOT LR    R0,R1                    COPY DENSITY CHAR
         N     R1,F15                   STRIP IT
         SLL   R1,2                     *4
         LA    R1,DEN#LIST(R1)          POINT TO 4 CHAR FORM
         ST    R1,TRUE#DEN              SAVE ADDR
         STC   R0,TRUE#DEN              SAVE 1 CHAR FORM
         SPACE 2
         CLI   NUMBNULL+1,0             HOW MANY NULL SKIPPED?      U14
         BNE   NULL1ST                  MORE THAN ZERO.             U14
         CLC   VOLLABI(4),=C'VOL1VOL1'  VOLUME LABEL?
         BE    TPVOL1OK                 YES.
         SPACE 3
         MVC   VOLSERNO(6),JFCB+118     SET HERE FOR OVERPRINTING   U15
         NEWPAGE ,                      PRINT THE HEADER LINES
         PRTLN '0++++ NO VOL1 LABEL FOUND.  NL ANALYSIS FOLLOWS ++++'
         BCTR  LCTR,0                   ACCOUNT FOR EXTRA BLANK LINE
         PRTLN OUTBUFF                  BLANK LINE
         SPACE 1
NULL1ST  MVC   FL1ID,BLANKS             SET THE DSN                 U14
         SPACE 3
NL$NEXT  ST    R2,LASTSIZE         RESTORE SIZE OF FIRST BLOC 09/84 DBC
         ST    R2,BYTECNT          INIT TOTAL BYTES READ      09/84 DBC
         ST    R2,MAXBLKSI         INIT BIGGEST BLOCK READ    09/84 DBC
         MVC   BLOCKCNT,F1              INIT # OF BLOCKS READ
         MVI   NLFLAGS,NL@F             INIT NL TYPE FLAGS
         CH    R2,TAPEBUFF              IS FIRST BLOCK RECFM=V?
         BNE   *+8                      NO - SKIP
         OI    NLFLAGS,NL@V             YES - SET FLAG
         MVI   WHERE,3                  SET "WHERE ARE WE" FLAG
         MVC   UNLOADER,UNLOADER-1      CLEAR INDICATOR
         LH    R1,FILE#SEQ              GET FILE NUMBER
         LA    R1,1(,R1)           ADJ TO IDENTIFY THE NUMBER 07/85 DBC
*                                  OF THE FILE CURRENTLY      07/85 DBC
*                                  BEING SCANNED              07/85 DBC
         CVD   R1,DWD                   -> PACKED
         UNPK  DWD(5),DWD+5(3)          -> EBCDIC
         OI    DWD+4,C'0'               FIX SIGN
         MVC   FL1FILSQ,DWD+1           PUT INTO "LABEL"
         SPACE 1
*  NOW SEE IF THIS FILE IS ANYTHING SPECIAL THAT WE KNOW ABOUT.
***      CLC   =H'800',LASTSIZE         RIGHT SIZE?
***      BNE   NL$NMOVE                 NOT IEHMOVE
         BA&XA R10,TEST$MV              IS IT IEHMOVE?
         B     NL$NMOVE                 NO
         B     IEHMOVE2                 YES - 2 PRINT FILE FORMAT
         B     IEHMOVE1                 YES - 1 PRINT FILE FORMAT
         SPACE 2
NL$NMOVE CLC   TAPEBUFF(8),=H'60,0,56,0' RECFM=V,BLK=60,REC=56?
         BE    NLTSTCPY             YES-IT'S THE OLD IEBCOPY FORMAT+SBG
         CLC   TAPEBUFF(8),=H'64,0,60,0' IS IT IEBCOPY NEW FORMAT? +SBG
         BNE   NL$NCOPY                 NO - NOT IEBCOPY
NLTSTCPY BA&XA R10,TEST$CPY             IS IT VS2COPY?             +SBG
         B     NL$NCOPY                 NO
         B     IEBCOPY2                 YES - 2 PRINT FILE FORMAT
         B     IEBCOPY1                 YES - 1 PRINT FILE FORMAT
         SPACE 2
NL$NCOPY BA&XA R10,TEST$DMP             IS IT IEHDASDR?
         B     NL$NDSDR                 NO
         NOP   0                        YES - BUT DON'T USE 2ND     WBF
*                                             PRINT FILE            WBF
         B     IHDASDR1
         SPACE 2
******************************************************************* SBG
*  ORIGINAL CODE WOULD NOT DETECT AN APAR TAPE AS SUCH.             SBG
*                                                                   SBG
*  WHEN READING PTF TAPES WHICH HAVE APARS FIRST, AND               SBG
*   THE APARS HAVE IEBUPDTE CONTROL CARDS IN THEM FOR SOURCE UPDATE,SBG
*   WE WANT TAPEMAP TO THINK THAT THE TAPE HAS PTFS OR APARS,       SBG
*   NOT IEBUPDTE CONTROL STATEMENTS, WHICH ARE SECONDARY            SBG
*   TO THE FACT THAT WE HAVE AN APAR TAPE.                          SBG
*                                                                   SBG
*  THEREFORE I AM MOVING THE CODE WHICH FEELS FOR SMPPTFIN FORMAT   SBG
*  BEFORE THE CODE WHICH FEELS FOR IEBUPDTE FORMAT, TO DETECT       SBG
*  AN APAR TAPE WHEN WE HAVE ONE.                                   SBG
******************************************************************* SBG
*  NOTE THAT WE DON'T WANT TO TEST FOR SMPPTFIN ON A CBT TAPE.12/88 SBG
******************************************************************* SBG
NL$NDSDR DS    0H                                             11/88 SBG
         BA&XA R10,TEST$CBT             IS IT CBT973 COMPRESS 11/88 SBG
         B     NL$NCBT                  NO                    11/88 SBG
         B     CBT2                     YES - 2 PRINT FILE FMT11/88 SBG
         B     CBT1                     YES - 1 PRINT FILE FMT11/88 SBG
         SPACE 3                                              11/88 SBG
NL$NCBT  DS    0H                       LABEL FOR NOT DASDR         SBG
         SPACE 3                                              07/85 DBC
         BA&XA R10,TEST$PTF        DOES FILE CONTAIN PTFS?    07/85 DBC
         B     NL$NPTFS            +0 NO, SKIP                06/86 SBG
         B     PTFS2               +4 YES, GO DUMP IT         07/85 DBC
         B     PTFS1               +8 YES, JUST SAY SO        07/85 DBC
*       NEXT 5 LINES ARE SWITCHED WITH PREV 6 LINES & NAMES CHANGED SBG
NL$NPTFS DS    0H                                             06/86 SBG
         BA&XA R10,TEST$UPS             MAYBE IEBUPDTE INPUT STREAM?
         B     NL$NUPDT                 BOY WILL THIS BE UNRELIABLE SBG
         B     IEBUPDT2                 YES - 2 PRINT FILE FORMAT
         B     IEBUPDT1            +8 YES & 1-PRT: HANDLE     07/85 DBC
NL$NUPDT DS    0H                                             06/86 SBG
         BA&XA R10,TEST$FDR             IS IT FDR?            07/85 DBC
         B     NL$NFDR                  NO
         B     FDR2                     YES - 2 PRINT FILE FORMAT   WBF
         B     FDR1                     YES,- 1 PRINT FILE FORMAT   WBF
         SPACE 2
NL$NFDR  BA&XA R10,TEST$IS              IS IT IEBISAM?        11/88 SBG
         B     NL$NCDOC                 DIDN'T THINK SO...
         B     IEBISAM2                 YES - 2 PRINT FILE FORMAT
         B     IEBISAM1                 YES - 1 PRINT FILE FORMAT
         SPACE 3
NL$NCDOC BA&XA R10,TEST$CH              IS IT CBT DOC FILE?   12/88 SBG
         B     NL$NIS                   NUH UH.               12/88 SBG
         B     NL$LOOP                  YES - DISPLAY EXTRCT'D12/88 SBG
         B     CBTDOC1                   DATA ON REPORT #1    12/88 SBG
         SPACE 3                                              12/88 SBG
NL$NIS   BA&XA R10,TEST$SLK             IS IT A SLICK BACKUP?
         B     NL$NSLK                  DIDN'T THINK SO...
         B     SLK2                     YES - 2 PRINT FILE FORMAT
         B     SLK1                     YES - 1 PRINT FILE FORMAT
         SPACE 3
NL$NSLK  BA&XA R10,TEST$MAP             IS IT A TAPEMAP PRINT FILE?
         B     NL$LOOP                  NO
         B     MAP2                     YES - 2 PRINT FILE FORMAT
****     B     NL$LOOP                  YES - 1 PRINT FILE FORMAT
         SPACE 3
NL$LOOP  TAPIO TCCW#DAT,TM=NL$EOF       READ A BLOCK
         B     NL$LOOP                  DO THAT TILL EOF
         SPACE 2
NL$EOF   CLI   UNLOADER,C' '            A SECOND LINE?
         BE    *+12                     NO - SKIP
         NEWPAGE  2                     MAKE SURE WE HAVE ENOUGH
         SPACE 1
NL$SL    DS    0H                                             07/85 DBC
         LH    R1,FILE#SEQ              GET THE FILE SEQ #
         CVD   R1,DWD                   PACK IT
         MVC   #FILE-2(6),=X'402020202120'                    09/84 DBC
         ED    #FILE-2(6),DWD+5
         SPACE 1                                              09/84 DBC
         MVC   #UNLOAD,UNLOADER
************************************************************* 12/88 SBG
*  IF THIS IS A CBT HEADER, PUT THE DISPLAY INFO INTO       * 12/88 SBG
*    THE DSNAME SLOT.  VER XXX  MM-DD-YY.                   * 12/88 SBG
************************************************************* 12/88 SBG
         SPACE 1                                              12/88 SBG
         CLC   UNLOADER(8),CBHEADER     WAS IT CBTHEADER?     12/88 SBG
         BNE   NL$CBHD                  NO. NORMAL PROCESSING 12/88 SBG
         MVC   #DSN(17),CBHLIT     DISPLAY CBT DOC INFO       12/88 SBG
         MVC   #DSN+4(4),CBTVERS   PUT IN VERSION NUMBER      12/88 SBG
         MVC   #DSN+9(8),CBTDATE   PUT IN RELEASE DATE        12/88 SBG
NL$CBHD  DS    0H                                             12/88 SBG
         SPACE 3
         CLC   =C'(NULL)',UNLOADER+2 SL NULL FILE?            07/85 DBC
         BE    NLSLDONE                 YES - CAN'T DO ANYTHING
         SPACE 1
         MVC   #INFOSRC,=C' SCAN ' IDENTIFY INFO SOURCE       06/84 DBC
         MVI   #RECFM,C'V'              ASSUME RECFM=V
         TM    NLFLAGS,NL@V             IS IT?
         BO    NL$1                     YES - CONTINUE
         MVI   #RECFM,C'F'              ASSUME RECFM=F
         TM    NLFLAGS,NL@F             IS IT?
         BO    NL$1                     YES - CONTINUE
         MVI   #RECFM,C'U'              ANYTHING ELSE IS RECFM=U
         SPACE 2
NL$1     L     R0,MAXBLKSI         GET SIZE OF LARGEST BLOCK  09/84 DBC
         CVD   R0,DWD
         MVC   #BLKSIZE(5),=X'2020202120'
         ED    #BLKSIZE-1(6),DWD+5
         SPACE 1
*******  MVI   #LRECL+4,C'?'
         SPACE 1
         L     R0,BLOCKCNT              GET BLOCK COUNT
         CVD   R0,DWD
         MVC   #BLKCNT-1(7),=X'20202020202120'
         ED    #BLKCNT-2(8),DWD+4
         SPACE 1
         MVC   #DEN+1(1),TRUE#DEN
         L     R14,TRUE#DEN
         TM    PARMFLAG,PF@DEN1
         BO    *+10
         MVC   #DEN,24(R14)                                         THO
         SPACE 1
         L     R15,BLOCKCNT             COUNT # OF GAPS
         L     R1,BYTECNT
         M     R1-1,=F'100'             SCALE
         D     R1-1,0(,R14)             DIVIDE BY BYTES/INCH
         CLI   TRUE#DEN,C'5'            IS IT A 3480 TAPE DRIVE ?   THO
         BNE   DEN3420A                 NO, THEN 3420               THO
         AH    R1,=Y(8)                 ADD 1 TAPEMARK              THO
         TM    TFLAG1,T1@SL             REALLY SL?
         BNO   *+12                     NO - SKIP
         AH    R1,=Y(2*8)               YES - 2 MORE TAPEMARKS      THO
         LA    R15,4(,R15)              INCR # OF GAPS
         M     R15-1,=F'08'             EACH GAP IS .08 INCH        THO
         B     LENCALC1                                             THO
DEN3420A AH    R1,=Y(375)               ADD 1 TAPEMARK              THO
         TM    TFLAG1,T1@SL             REALLY SL?                  THO
         BNO   *+12                     NO - SKIP                   THO
         AH    R1,=Y(2*375)             YES - 2 MORE TAPEMARKS      THO
         LA    R15,4(,R15)              INCR # OF GAPS              THO
         M     R15-1,=F'60'             EACH GAP IS .60 INCH
         CLI   TRUE#DEN,C'4'            UNLESS IT'S A 6250 TAPE
         BNE   *+8                      IT ISN'T
         SRL   R15,1                    THEN THEY'RE .3 INCH EACH
LENCALC1 AR    R1,R15                   ADD IN LENGTH OF GAPS       THO
         LR    R2,R1                    SAVE
         ICALL TAPEFEET,R14,R15=#LENGTH-2                     06/84 DBC
         A     R2,LEN#TAPE+4
         ST    R2,LEN#TAPE+4
         LR    R1,R2                    COPY FOR SUBROUTINE
         ICALL TAPEFEET,R14,R15=#CUMLEN-2                     06/84 DBC
         SPACE 2
         PRTLN OUTBUFF
         CLI   UNLOADER,C' '            ANY SECOND PRINT LINE?
         BE    NL$DONE                  NO - SKIP
         TM    PARMFLAG,PF@INLIN        PARM=INLINE?
         BNO   NL$DONE                  NO - NO SECOND LINE
         PRTLN OUTBUFF2
         MVC   OUTBUFF2,OUTCLR2
         SPACE 2
NL$DONE  TM    TFLAG1,T1@SL             THIS TAPE SL?
         BO    NLSLDONE                 YES - DIFFERENT HANDLING
         SPACE 1
NL$DONEX TAPIO TCCW#DAT,TM=NULL$NL      READ NEXT FILE'S FIRST BLK  U14
         L     R2,LASTSIZE         GET SIZE OF LAST BLOCK     09/84 DBC
         B     NL$NEXT                  PROCESS NEXT FILE
         SPACE 3                                              07/85 DBC
NULL$NL  LH    R1,NUMBNULL              GET NUMBER OF NULL FILES    U14
         LA    R1,1(,R1)                INCR                        U14
         STH   R1,NUMBNULL              SAVE NEW COUNT              U14
         CH    R1,NULLNUMB              DONE ENOUGH?                U14
         BH    HAVE$EOV                 YES - STOP                  U14
         LH    R1,FILE#SEQ         NO, GET FILE #             07/85 DBC
         MVC   #FILE-1(5),=X'2020202120'  MOVE IN EDIT MASK         U14
         CVD   R1,DWD                                               U14
         ED    #FILE-2(6),DWD+5                                     U14
         MVC   #UNLOAD+2(6),=C'(NULL)'  PUT IN INDICATOR      06/84 DBC
         PRTLN OUTBUFF                                              U14
         L     R0,LEN#TAPE+4            GET ACCUMULATED TAPE LENGTH U14
         CLI   TRUE#DEN,C'5'            IS IT A 3480 TAPE DRIVE ?   THO
         BNE   DEN3420B                 NO, THEN 3420               THO
         AH    R0,=Y(8)                 COUNT 1 TAPEMARK            THO
         ST    R0,LEN#TAPE+4            SAVE UPDATED                U14
         B     NL$DONEX                                             U14
DEN3420B AH    R0,=Y(375)               COUNT 1 TAPEMARK            THO
         ST    R0,LEN#TAPE+4            SAVE UPDATED                THO
         B     NL$DONEX                                             THO
         SPACE 3
************************************************************* 12/88 SBG
*   THIS IS THE START OF NORMAL STANDARD LABEL PROCESSING.  * 12/88 SBG
************************************************************* 12/88 SBG
TPVOL1OK OI    TFLAG1,T1@SL             INDICATE SL PROCESSING12/88 SBG
         MVC   TITLE1+5(6),VOLSERNO
         MVC   TITLE1+19(10),VOLOWNER
         MVC   TITLE1+13(6),TITLE1+12   BLANK OUT KEYWORD
         CLC   VOLOWNER,BLANKS          OWNER ALL BLANK?
         BE    *+10
         MVC   TITLE1+13(6),=C'OWNER='
         NI    TFLAG2,255-T2@RQVOL      TURN OFF FLAG
         CLC   VOLSERNO(6),JFCB+118     GET WHAT WE ASKED FOR?
         BE    MATCHVOL                 YES - CONTINUE
         OI    TFLAG2,T2@RQVOL          SET FLAG
         SPACE 2
MATCHVOL NEWPAGE ,                      PRINT HEADER LINES
         NI    TFLAG1,255-T1@ANLZ       RESET FLAG
         TM    PARMFLAG,PF@NL      NL PROCESSING FORCED?      06/84 DBC
         BNZ   ANLZ1               YES, SET ANALYZE SIGNAL    06/84 DBC
         TM    PARMFLAG,PF@NOCHK   NO, SCAN BYPASSED?         06/84 DBC
         BNZ   NOANLZ1             YES, SKIP                  06/84 DBC
ANLZ1    OI    TFLAG1,T1@ANLZ      NO, SET ANALYZE SIGNAL     06/84 DBC
NOANLZ1  DS    0H                                             06/84 DBC
         SPACE 1
         TM    PARMFLAG,PF@NL           FORCE NL ANALYSIS?
         BNO   *+12                     NO - CONTINUE
FORCENL  XI    TFLAG1,T1@SL             YES - TURN OFF FLAG
         B     NL$NEXT                  AND GO TO NL ROUTINE
         SPACE 1
         MVI   WHERE,1                  SET "WHERE ARE WE" FLAG
         MVC   FL1LABI(4),BLANKS        CLEAR OUT 'HDR1'      02/93 RT
         TAPIO TCCW#LBL,TM=MAYBETT      READ THE HDR LABELS
         B     DATAREAD                 SKIP FIRST TIME
MAYBETT  EQU   *                                               1/93 RT
         CLC   FL1LABI(4),=C'HDR1'      IS IT SL DOS TAPE?     1/93 RT
         BNE   FORCENL                                         1/93 RT
         MVI   FL2LABI,C'?'             CLEAR HDR2 TO '?'      1/93 RT
         MVC   FL2LABI+1(70),FL2LABI                           1/93 RT
         CLI   FL1LABI+4,C'0'      POSSIBLY IEHINITT?          1/93 RT
         BNE   DECRFILE             NO                         1/93 RT
         CLC   FL1LABI+5(70),FL1LABI+4      IEHINITT?          1/93 RT
         BE    IEHINITT             YES                        1/93 RT
DECRFILE LH    R0,FILE#SEQ         DECR TAPEMARK COUNTER       1/93 RT
         BCTR  R0,0                                            1/93 RT
         STH   R0,FILE#SEQ         STORE NEW VALUE             1/93 RT
         TAPIO TCCW#BSF            BACKSPACE A FILE            1/93 RT
         B     DATAREAD                 SKIP FIRST TIME
         SPACE 3                                              07/85 DBC
NLSLDONE TM    TFLAG1,T1@SL        SL-TAPE?                   07/85 DBC
         BZ    SPACER              NO, SKIP THIS              07/85 DBC
         CLC   =C'0001',FL1VOLSQ        FIRST VOLUME OF FILE?
         BE    TEST$EOV                 YES.
         CLC   =C'    ',FL1VOLSQ        MISSING VOL SEQ FLD?  02/93 RT
         BE    TEST$EOV                 YES.                  02/93 RT
         MVC   FVOL#MSG+22(4),FL1VOLSQ  VOL SEQ #
         MVC   FVOL#MSG+80(6),FL1FILSR  FIRST VOLSER
         PRTLN FVOL#MSG
         SPACE 2
TEST$EOV CLC   =C'EOV2',FL2LABI
         BNE   NOT$EOV                  EOV.
         PRTLN ' *** THE ABOVE FILE IS CONTINUED ON ANOTHER VOLUME'
         B     HAVE$EOV            ALL DONE                   07/85 DBC
NOT$EOV  DS    0H                                             07/85 DBC
         SPACE 3                                              07/85 DBC
SPACER   PRTLN OUTBUFF                  BLANK LINE
         TAPIO TCCW#FSF                 FWD SPACE
         MVI   WHERE,1                  SET "WHERE ARE WE" FLAG
         TAPIO TCCW#LBL,TM=MAYBET2      READ HDR LABELS
         B     DATAREAD                                       02/93 RT
         SPACE 1
MAYBET2  EQU   *                                               1/93 RT
         CLC   FL1LABI(4),=C'HDR1'      IS IT SL DOS TAPE?     1/93 RT
         BNE   HAVE$EOV                                        1/93 RT
         MVI   FL2LABI,C'?'             CLEAR HDR2 TO '?'      1/93 RT
         MVC   FL2LABI+1(70),FL2LABI                           1/93 RT
         LH    R0,FILE#SEQ         DECR TAPEMARK COUNTER       1/93 RT
         BCTR  R0,0                                            1/93 RT
         STH   R0,FILE#SEQ         STORE NEW VALUE             1/93 RT
         TAPIO TCCW#BSF            BACKSPACE A FILE            1/93 RT
         B     DATAREAD
DATAREAD TAPIO TCCW#FSF                 SKIP TO END OF HDR LABELS
         MVC   UNLOADER,UNLOADER-1      CLEAR UNLOADED FLAG
         MVI   NLFLAGS,NL@V+NL@F        ASSUME RECFM=V & RECFM=F
         MVI   WHERE,3                  SET "WHERE ARE WE" FLAG
         TAPIO TCCW#DAT,TM=NULLFILE     READ DATA
         L     R1,LASTSIZE         GET SIZE OF (FIRST) BLOCK  09/84 DBC
         ST    R1,MAXBLKSI         INIT BIGGEST BLOCK READ    09/84 DBC
         AH    R1,=Y(4*80)              2 HDR + 2 EOF LABELS
         ST    R1,BYTECNT               INIT TOTAL BYTES READ
         MVC   BLOCKCNT,F1              INIT NUMBER OF BLOCKS READ
         NI    NLFLAGS,255-NL@CHANG     FIRST BLOCK NOT A CHANGE
         OI    NLFLAGS,NL@F             STILL COULD BE RECFM=F
         SPACE 2
         BA&XA R10,TEST$CH              TEST FOR CBT HEADER   10/90 SBG
         B     TSTRECFM                 NOT CBTHEADER, GO ON  10/90 SBG
         NOP   0                        NO EFFECT ON REPORT 2 10/90 SBG
         B     TEST$LCT                 PROCESS HEADER FILE   10/90 SBG
         SPACE 2                                              10/90 SBG
TSTRECFM CLI   FL2RECFM,C'F'            RECFM=FB?             10/90 SBG
         BNE   NOT$RF$F                 NO - NOT IEHMOVE OR IEBISAM
         CLC   FL2LRECL,C00080          LRECL=80?
         BNE   FIND$EOF                 NO - NOT IEHMOVE OR IEBISAM
***      CLC   FL2BLKL,C00800           BLKSIZE=800?
***      BNE   TST$UPDT                 NO - NOT IEHMOVE'D
         CLI   FL2BLKA,C'B'             BLOCKED?
*  SWITCH TESTS FOR SMPPTFIN AND IEBUPDTE                           SBG
         BNE   TST$UPDT                 NO - NOT IEHMOVE'D     2/93 RT
         BA&XA R10,TEST$MV              IS IT IEHMOVE?
         B     TST$UPDT                 NO - TRY NEXT               SBG
         B     IEHMOVE2                 YES - 2 PRINT FILE FORMAT
         B     TEST$LCT                 YES - 1 PRINT FILE FORMAT
         SPACE 3
************************************************************* 06/86 SBG
*  ORIGINAL CODE WOULD NOT DETECT AN APAR TAPE AS SUCH.             SBG
*                                                                   SBG
*  WHEN READING PTF TAPES WHICH HAVE APARS FIRST, AND               SBG
*   THE APARS HAVE IEBUPDTE CONTROL CARDS IN THEM FOR SOURCE UPDATE,SBG
*   WE WANT TAPEMAP TO THINK THAT THE TAPE HAS PTFS OR APARS,       SBG
*   NOT IEBUPDTE CONTROL STATEMENTS, WHICH ARE SECONDARY            SBG
*   TO THE FACT THAT WE HAVE AN APAR TAPE.                          SBG
*                                                                   SBG
*  THEREFORE I AM MOVING THE CODE WHICH FEELS FOR SMPPTFIN FORMAT   SBG
*  BEFORE THE CODE WHICH FEELS FOR IEBUPDTE FORMAT, TO DETECT       SBG
*  AN APAR TAPE WHEN WE HAVE ONE.                                   SBG
************************************************************* 06/86 SBG
         SPACE 3                                              07/85 DBC
TST$UPDT DS    0H                LABEL FOR TEST IEBUPDTE            SBG
*                          BUT TEST FOR PTFIN COMES FIRST, SO       SBG
*  I SWITCHED DAVE'S CODE AROUND BETWEEN THESE 3 LABELS.            SBG
         BA&XA R10,TEST$PTF        DOES FILE CONTAIN PTFS?    07/85 DBC
         B     TST$NPTF            +0 NO, SKIP                07/85 DBC
         B     PTFS2               +4 YES & 2-PRT FMT; HANDLE 07/85 DBC
         B     TEST$LCT            +8 YES & 1-PRT FMT; HANDLE 07/85 DBC
TST$NPTF DS    0H                                             07/85 DBC
         BA&XA R10,TEST$UPS             SEE IF IEBUPDTE INPUT STREAM
         B     TST$NUPD            +0 PROBABLY NOT            07/85 DBC
         B     IEBUPDT2                 YES - 2 PRINT FILE FORMAT
         B     TEST$LCT            +8 YES, JUST SAY SO        07/85 DBC
TST$NUPD DS    0H                                             07/85 DBC
*  I SWITCHED DAVE'S CODE AROUND BETWEEN THESE 3 LABELS.            SBG
         SPACE 3
TST$ISAM BA&XA R10,TEST$IS              IS IT IEBISAM?
         B     TST$MAP                  NO                    11/88 SBG
         B     IEBISAM2                 YES - 2 PRINT FILE FORMAT
         B     TEST$LCT                 YES - 1 PRINT FILE FORMAT
         SPACE 3
TST$MAP  BA&XA R10,TEST$MAP             IS IT A TAPEMAP PRINT FILE?
         B     FIND$EOF                 NO
         B     MAP2                     YES - 2 PRINT FILE FORMAT
         B     TEST$LCT                 YES - 1 PRINT FILE FORMAT
         SPACE 3
NOT$RF$F CLC   =X'00CA6D0F',TAPEBUFF+8  TEST IEBCOPY FIRST    08/90 SBG
         BE    FORGTLBL         IEBCOPY-IGNORE WHAT'S IN LABEL08/90 SBG
         CLC   =X'01CA6D0F',TAPEBUFF+8  TEST IEBCOPY FOR PDSE 06/93 RT
         BE    FORGTLBL         IEBCOPY-IGNORE WHAT'S IN LABEL06/93 RT
         CLI   FL2RECFM,C'V'            RECFM=V?              08/90 SBG
         BNE   NOT$RF$V                 NO - NOT IEBCOPY'D
         CLI   FL2BLKA,C'S'             RECFM=VS?
         BNE   TST$CBT                  NOT VS. MAYBE CBT973. 11/88 SBG
FORGTLBL BA&XA R10,TEST$CPY             IS IT VS2COPY?        08/90 SBG
         B     TST$CBT                  NO - MAYBE CBT
         B     IEBCOPY2                 YES - 2 PRINT FILE FORMAT
         B     TEST$LCT                 YES - 1 PRINT FILE FORMAT
         SPACE 3
TST$CBT  BA&XA R10,TEST$CBT             IS IT CBT973 COMPRESS 11/88 SBG
         B     FIND$EOF                 NO                    11/88 SBG
         B     CBT2                     YES - 2 PRINTFILE FMT 11/88 SBG
         B     TEST$LCT                 YES - 1 PRINTFILE FMT 11/88 SBG
         SPACE 3                                              11/88 SBG
NOT$RF$V CLI   FL2RECFM,C'U'            RECFM=U?
         BNE   FIND$EOF                 NO - NOT IEHDASDR
         BA&XA R10,TEST$DMP             IS IT IEHDASDR?
         B     TST$FDR                  NO
         NOP   0                        DON'T USE 2ND PRINT FILE    WBF
         B     TEST$LCT                 CONTINUE
         SPACE 3
TST$FDR  BA&XA R10,TEST$FDR             IS IT FDR?
         B     TST$SLK                  NO
         B     FDR2                     YES - 2 PRINT FILE FORMAT   WBF
         B     TEST$LCT                 YES - 1 PRINT FILE FORMAT   WBF
         SPACE 3
TST$SLK  BA&XA R10,TEST$SLK             IS IT IEBISAM?
         B     FIND$EOF                 NO
         B     SLK2                     YES - 2 PRINT FILE FORMAT
         B     TEST$LCT                 YES - 1 PRINT FILE FORMAT
         SPACE 2
TEST$LCT TM    PARMFLAG,PF@INLIN        PARM=INLINE?
         BNO   FIND$EOF                 NO - SKIP THIS
         NEWPAGE  2                     MORE THAN 1 LINE LEFT?
         SPACE 1
         CLC   IEBCOPY,UNLOADER         UNLOADED BY IEBCOPY?
         BE    IEBCOPY1                 YES - PRINT EXTRA INFO
         CLC   IEHMOVE,UNLOADER         UNLOADED BY IEHMOVE?
         BE    IEHMOVE1                 YES - PRINT EXTRA INFO
         CLC   IEBISAM,UNLOADER         UNLOADED BY IEBISAM?
         BE    IEBISAM1                 YES - PRINT EXTRA INFO
         CLC   IEHDASDR,UNLOADER        DUMPED BY IEHDASDR?
         BE    IHDASDR1                 YES - PRINT EXTRA INFO
         CLC   FDR,UNLOADER             DUMPED BY FDR?
         BE    FDR1                     YES - PRINT EXTRA INFO
         CLC   SLICKMNG,UNLOADER        UNLOADED BY SLICK?
         BE    SLK1                     YES - PRINT EXTRA INFO
         CLC   CBT973,UNLOADER          CBT973 COMPRESS FORMAT11/88 SBG
         BE    CBT1                     YES - PRINT EXTRA INFO11/88 SBG
         CLC   CBTEMPTY,UNLOADER        CBT TAPE EMPTY FILE?  11/88 SBG
         BE    CBTEMP1                  YES - PRINT EXTRA INFO11/88 SBG
         CLC   CBHEADER,UNLOADER        CBT TAPE DOC FILE?    12/88 SBG
         BE    CBTDOC1                  YES - PRINT DESCRPTN  12/88 SBG
         CLC   SMPPTFIN,UNLOADER   SYSMODS?                   07/85 DBC
         BE    PTFS1               YES, GO SAY SO             07/85 DBC
         CLC   IEBUPXXX,UNLOADER   NO, UPDATE STREAM?         07/85 DBC
         BE    IEBUPDT1            YES, GO SAY SO             07/85 DBC
****     CLC   CTAPEMAP,UNLOADER        CREATED FOR TAPEMAP?
****     BE    MAP1                     DOESN'T MATTER - MORE THAN
****                                    ONE LINE OF INFO, SO CAN'T
****                                    PRINT IT INLINE
         SPACE 3
FIND$EOF TM    PARMFLAG,PF@NOCHK        CHECKING SL TAPE?     06/84 DBC
         BZ    SCAN$EOF                 YES - READ TILL EOF   06/84 DBC
         SPACE 1
         TAPIO TCCW#FSF                 FSF PAST REST OF DATA
         SPACE 2
READ$EOF MVI   WHERE,2                  SET "WHERE ARE WE" FLAG
         MVC   FL1LABI(4),BLANKS                             02/93 RT
         TAPIO TCCW#EOV,TM=MISS$EOF     READ TRAILER LABEL
TEST$EOF CLC   =C'EOF1',FL1LABI         TRAILER LABEL?
         BE    HAVE$EOF                 YES - OK
         CLC   =C'EOV1',FL1LABI         OTHER KIND?
         BE    HAVE$EOF                 YES - OK
         SPACE 1
MISS$EOF PRTLN '0EOF OR EOV LABEL NOT FOUND WHERE EXPECTED'
         B     REWINDIT                 REWIND AND LEAVE    %%%%%%%%%
         SPACE 2
SCAN$EOF TAPIO TCCW#DAT,TM=SL$SAVE      READ & TALLY A DATA BLOCK
         B     SCAN$EOF                 DO THAT TILL EOF
         SPACE 2
SL$SAVE  MVC   NL#SAVE(16),MAXBLKSI SAVE ALL INFO            09/84 DBC
         MVC   NL#FLAGX,NLFLAGS         MORE...
         B     READ$EOF                 GO READ THE EOF LABELS
         SPACE 2
HAVE$EOF LA    R1,1                     ASSUME 1-LINE GROUPS
         TM    PARMFLAG,PF@NOCHK        CHECKING?             06/84 DBC
         BNZ   *+8                      NO - SKIP             06/84 DBC
         LA    R1,2(,R1)                YES - NEED 2 MORE EACH
         CLI   UNLOADER,C' '            IS THIS FILE UNLOADED?
         BE    *+8                      NO - SKIP
         LA    R1,1(,R1)                YES - NEED 1 MORE LINE
         TM    PARMFLAG,PF@INLIN        INLINE INFO?          12/88 SBG
         BZ    *+8                      NO - SKIP             12/88 SBG
         LA    R1,1(,R1)                YES - NEED EXTRA LINE 12/88 SBG
         LR    R0,R1                    COPY FOR STUPID MACRO
         NEWPAGE  (R0)                  CHECK FOR ENOUGH LINES
         SPACE 2
         TM    PARMFLAG,PF@NOCHK   SL TAPE WITH NL SCANNING?  07/85 DBC
         BZ    BYWHOZ              YES, DEFER NAMING THE      07/85 DBC
*                                  UNLOADER UNTIL I BUILD THE 07/85 DBC
*                                  "SCAN" LINE MESSAGE.       07/85 DBC
         MVC   #UNLOAD,UNLOADER         SAY WHO UNLOADED IT
BYWHOZ   DS    0H                                             07/85 DBC
         MVC   #FILE(4),FL1FILSQ        FILE #
         ICALL DEZERO,R14,R1=#FILE      REMOVE LEADING ZEROES
         MVC   #DSN(17),FL1ID           DSN
         CLI   FL1FSEC,C'0'             PASSWORD PROTECTED?
         BE    NOTPSWD                  NO - SKIP
         CLI   FL1FSEC,C'1'             PASSWORD PROTECTED?
         BE    YESPSWD                  YES - MOVE IT IN
         CLI   FL1FSEC,C'3'             NOPWREAD?
         BNE   WHATPSWD                 NO - WHAT THEN????
         MVC   #PSWD,=C' WRT'           YES                   06/84 DBC
         B     NOTPSWD                  CONTINUE
WHATPSWD MVC   #PSWD,=C' ???'           SAY WHAT?             06/84 DBC
         B     NOTPSWD
YESPSWD  MVC   #PSWD,=C' YES'           SAY SO.               06/84 DBC
         SPACE 3
NOTPSWD  MVC   #CDATE(2),FL1CREDT+1     YEAR CREATED
         MVI   #CDATE+2,C'.'
         MVC   #CDATE+3(3),FL1CREDT+3   DAY CREATED
         MVC   #EDATE(2),FL1EXPDT+1     YEAR EXPIRES
         MVI   #EDATE+2,C'.'
         MVC   #EDATE+3(3),FL1EXPDT+3   DAY EXPIRES
         SPACE 3                                              06/84 DBC
         MVC   #INFOSRC,=C'LABELS' SHOW INFO SOURCE           06/84 DBC
         MVC   #BLKCNT(6),FL1BLKCT      BLOCK COUNT
         ICALL DEZERO,R14,R1=#BLKCNT    REMOVE LEADING ZEROES
         SPACE 2
         MVC   #RECFM(1),FL2RECFM       RECFM
         MVC   #RECFM+1(1),FL2BLKA      BLOCK ATTRIBUTE
         MVC   #RECFM+2(1),FL2CNTRL     CONTROL CHARACTER
         CLI   #RECFM+1,C'R'            WEIRDO RECFM?  (FR OR VR)
         BNE   NOT$RF$R                 NOT RECFM=FR OR VR
         MVC   #RECFM+3(1),#RECFM+2     MOVE CTL CHAR OVER
         MVC   #RECFM+1(2),=C'BS'       IT'S REALLY FBS OR VBS
NOT$RF$R MVC   #BLKSIZE(5),FL2BLKL      BLOCK LENGTH
         ICALL DEZERO,R14,R1=#BLKSIZE   REMOVE LEADING ZEROES
         MVC   #LRECL(5),FL2LRECL       RECORD LENGTH
         ICALL DEZERO,R14,R1=#LRECL     REMOVE LEADING ZEROES
         CLI   FL2DEN,C'?'              WAS A "DOS SL" TAPE FOUND?  JCH
         BNE   *+10                     NO -> HDR2 VALUE IS VALID   JCH
         MVC   FL2DEN(1),TRUE#DEN       YES-> USE ACTUAL AS VALUE   JCH
         CLI   TRUE#DEN,C'5'            3480 CARTRIDGE ?            THO
         BNE   *+8                      NO THEN BYPASS MOD          THO
         MVI   FL2DEN,C'5'              MODIFY WITH 3480 DEN        THO
         IC    R15,FL2DEN               GET TAPE DENSITY
         STC   R15,#DEN+1               SET IN PRINT LINE
         CLC   TRUE#DEN(1),FL2DEN       LABEL CORRECT?
         BE    *+8                      YES - OK
         OI    TFLAG1,T1@BADEN          SET BAD DENSITY FLAG
         N     R15,F15                  JUST DIGIT
         SLL   R15,2                    MULTIPLY BY 4
         LA    R3,DEN#LIST(R15)         POINT TO DENSITY VALUE
         TM    PARMFLAG,PF@DEN1         PARM=DEN1?
         BO    DEN$1                    YES - LEAVE 1 CHAR DEN
         CLI   FL2DEN,C'5'              TOO HIGH?                   THO
         BH    DEN$1                    YES - SKIP
         CLI   FL2DEN,C'0'              TOO LOW?
         BL    DEN$1                    YES - SKIP
         MVC   #DEN,24(R3)              MOVE CHAR FORM OF DENSITY   THO
         SPACE 1
DEN$1    MVC   #TRTCH+1(2),FL2TRTCH     TAPE RECORDING TECHNI 06/84 DBC
         SPACE 1                                              06/84 DBC
         MVC   #CREATOR(8),FL2JOBD GET CREATING JOBNAME       06/84 DBC
         CLI   FL2STEPD,C' '       IS THERE A STEPNAME?       06/84 DBC
         BE    GOTCREAT            NO, SKIP                   06/84 DBC
         MVI   #CREATOR-1,C'X'     YES, SET SCAN STOPPER      06/84 DBC
         LA    R1,#CREATOR+8       LOAD BACK-SCANNER          06/84 DBC
         BA&XA.R R14,0             LOAD LOOP HEAD             06/84 DBC
         BCTR  R1,0                BACK SCAN                  06/84 DBC
         CLI   0(R1),C' '          EO-JOBNAME YET?            06/84 DBC
         BER   R14                 NO, CONTINUE BACKSCAN      06/84 DBC
         MVI   1(R1),C'/'          YES, INSERT SEPARATOR      06/84 DBC
         MVC   2(8,R1),FL2STEPD    GET STEPNAME               06/84 DBC
         MVI   #CREATOR-1,C' '     CLEAR STOPPER              06/84 DBC
GOTCREAT DS    0H                                             06/84 DBC
         SPACE 2
*    *****  COMPUTE THE LENGTH (INCHES) OF THIS TAPE DATA SET.
         SPACE 1                                              06/84 DBC
         TM    PARMFLAG,PF@NOCHK   DON'T DO IT IF A SCAN IS - 06/84 DBC
         BZ    SKIPGUES             BEING DONE ANYWAY         06/84 DBC
         SPACE 1
         PACK  DWD,FL1BLKCT(6)          BLOCK COUNT
         CVB   R15,DWD                  SAVE BLOCK COUNT
         LA    R1,4(,R15)               ADD 4 GAPS TO BLOCK COUNT.
         L     R3,TRUE#DEN              GET PTR TO TRUE DENSITY
         CLI   TRUE#DEN,C'5'            IS IT A 3480 TAPE DRIVE ?   THO
         BNE   *+12                     NO, THEN 3420               THO
         M     R1-1,=F'08'              EACH GAP IS .08 INCH        THO
         B     *+20                                                 THO
         M     R1-1,=F'60'              EACH GAP IS 6/10 INCH
*  NOTE - LENGTH KEPT IN .01 INCHES
         CLI   TRUE#DEN,C'4'            6250 BPI?
         BNE   *+8                      NO - SKIP
         SRL   R1,1                     GAPS AT 6250 ARE .3 INCH
         LR    R2,R1                    SAVE LENGTH OF GAPS
         PACK  DWD,FL2BLKL(5)           BLKSIZE.
         CVB   R1,DWD
         MR    R1-1,R15                 BLKSIZE * BLOCK_COUNT
         AH    R1,=Y(4*80)              2 HDR & 2 EOF LABELS
         M     R1-1,=F'100'             CONVERT TO .01
         D     R1-1,0(,R3)              DIVIDE BY DENSITY.
         CLI   TRUE#DEN,C'5'            IS IT A 3480 TAPE DRIVE ?   THO
         BNE   *+12                     NO, THEN 3420               THO
         AH    R1,=Y(8*3)               3 TAPEMARKS @ 0.08 INCH     THO
         B     *+8                                                  THO
         AH    R1,=Y(375*3)             3 TAPEMARKS @ 3.75 INCH
         AR    R2,R1                    ADD TO LENGTH OF DATA
         LR    R1,R2                    COPY FOR SUBROUTINE
         ICALL TAPEFEET,R14,R15=#LENGTH-2 FORMAT FILE LENGTH  06/84 DBC
         A     R2,LEN#TAPE              ADD TO TOTAL LENGTH
         ST    R2,LEN#TAPE              SAVE NEW TOTAL LENGTH
         LR    R1,R2                    COPY FOR SUBROUTINE
         ICALL TAPEFEET,R14,R15=#CUMLEN-2 FORMAT CUMULATIVE L 06/84 DBC
SKIPGUES DS    0H                                             06/84 DBC
         SPACE 1
         PRTLN OUTBUFF
         SPACE 2
         TM    PARMFLAG,PF@NOCHK        SL WITH NL ANALYSIS?  06/84 DBC
         BNZ   NO$CHECK                 NO - SEE IF SECOND LI 06/84 DBC
         MVC   MAXBLKSI(16),NL#SAVE RESTORE INFO              09/84 DBC
         MVC   NLFLAGS,NL#FLAGX         MORE...
         B     NL$SL                    YES - GO DO IT
         SPACE 2
NO$CHECK TM    PARMFLAG,PF@INLIN        INLINE MESSAGES?
         BNO   NLSLDONE                 NO - DO NEXT FILE
         CLI   UNLOADER,C' '            UNLOADED?
         BE    NLSLDONE                 NO - DO NEXT FILE
         PRTLN OUTBUFF2                 PRINT TRUE ATTRS OF UNLOADED DS
         MVC   OUTBUFF2,OUTCLR2         CLEAR OTHER PRINT LINE
         B     NLSLDONE                 DO NEXT FILE
         SPACE 3
HAVE$EOV PRTLN '-     *** EOV ***'
         CLC   LCTR2,=F'0'              ANYTHING PRINTED ON OTHER PAGE?
         BZ    NO$EOV2                  NO - SKIP
         MVC   OUTBUFF2(17),=C'-     *** EOV ***'
         PRTLN2                         FLAG END OF OTHER PRINT FILE
         SPACE 1
NO$EOV2  TM    TFLAG1,T1@BADEN          ANY INCORRECT DENSITIES?
         BNO   NO$BADEN                 NO - SKIP
         LA    R0,9                     ASSUME NOT PARM=NONOTE
         TM    PARMFLAG,PF@NONOT        WANT THE NOTE?
         BNO   *+8                      YES - SO USE 9
         LA    R0,6                     PARM=NONOTE, SO USE 6
         NEWPAGE  (R0)                  GO TO TOP OF PAGE IF NEEDED
         LA    LCTR,1(,LCTR)            WILL CALL PRTLN 1 EXTRA TIME
         MVC   OUTBUFF(113),INCORLAB    REPL LITERAL BY FIELD 10/90 SBG
*                                       FOR ADDRESSABILITY    10/90 SBG
         L     R2,TRUE#DEN              GET PTR TO 4 CHAR TRUE DEN
         CLI   TRUE#DEN,C'2'            800 BPI?
         BNE   *+6                      NO - OK
         BCTR  R2,0                     RIGHT JUSTIFY THE "800"
         MVC   OUTBUFF+98(4),20(R2)     MOVE INTO PRINT LINE
         PRTLN OUTBUFF
         MVI   OUTBUFF,C'+'             SET FOR OVERPRINT
         MVC   OUTBUFF+98(4),20(R2)     LIGHT UP THE DENSITY
         LA    R0,OUTBUFF               POINT TO OUTPUT LINE
         BA&XA R14,PUTPRTLN             PRINT IT ONCE (NO CLEAR OUTBUFF
         PRTLN OUTBUFF                  AND THE THIRD TIME
         SPACE 2
NO$BADEN TM    PARMFLAG,PF@NONOT        WANT THE NOTE?
         BO    REWINDIT                 NO - SO DON'T
         NEWPAGE  4                     ENOUGH LINES LEFT ON PAGE?
         PRTLN '0NOTE:  LENGTH(S) ARE COMPUTED, (BASED ON BLKSIZE, BLKC$
               OUNT, AND DENSITY), AND ARE THEREFORE ONLY APPROXIMATE.'
         TM    TFLAG1,T1@ANLZ           ANALYZING?
         BO    REWINDIT                 YES - GOOD LENGTHS PRINTED
         PRTLN '        LENGTHS FOR RECFMS OTHER THAN F,FB,ETC MAY NOT $
               BE VERY CLOSE TO THE TRUE LENGTH.'
         SPACE 2
REWINDIT TAPIO TCCW#RWD                 REWIND THE TAPE
         SPACE 2
CLOSE$TP CLOSE MF=(E,TAPEMFL)
         SPACE 2
RETURN   L     R14,RET#ADDR             GET RETURN ADDR
         BR    R14
         SPACE 2
NULLFILE MVC   UNLOADER+2(6),=C'(NULL)' PUT IN INDICATOR      06/84 DBC
         TAPIO TCCW#LBL   %%%%%         READ EOF'S                %%%%%
         B     TEST$EOF   %%%%%         TEST FOR 'EOF'            %%%%%
         SPACE 2
UNEXTPMK PRTLN '0*** UNEXPECTED TAPE MARK ***'
         B     REWINDIT                 CLOSE UP & LEAVE
         SPACE 2
LEADTPMK NEWPAGE  ,                     PRINT THE HEADER LINES
         PRTLN '0++++ TAPE HAS LEADING TAPE MARK ++++'
         BCTR  LCTR,0                   ACCOUNT FOR EXTRA BLANK LINE
         PRTLN OUTBUFF                  BLANK LINE
         MVI   VOLLABI,C'X'             INSURE NL ROUTINE
         MVI   NUMBNULL+1,1             INIT NUMBER OF NULL FILES   U14
         TAPIO TCCW#DAT,TM=LEAD$EOV     READ FIRST BLOCK FROM FILE  U14
         B     DO$SENSE                 DO NL ANALYSIS
         SPACE 2
LEAD$EOV MVI   NUMBNULL+1,2             SET NUMBER OF NULL FILES    U14
         CLI   NULLNUMB+1,2             TOO MANY?                   U14
         BL    HAVE$EOV                 YES - STOP                  U14
         MVC   #UNLOAD+2(6),=C'(NULL)'  FLAG IT               06/84 DBC
         MVI   #FILE+3,C'1'             SET FOR PRINT               U14
         PRTLN OUTBUFF                                              U14
         B     DO$SENSE                                             U14
         SPACE 2
IEHINITT PRTLN '-++++ TAPE HAS BEEN RE-LABELLED WITH IEHINITT ++++'
         B     REWINDIT                 ...
         EJECT
TEST$MV  CLC   =C'UNLOADED',TAPEBUFF+16 IEHMOVE'S TRADEMARK?
         BNER  R10                      NO
         MVC   UNLOADER(7),IEHMOVE      SAY WHO
         SPACE 1
TEST$RET IFP2  Y,4(,R10)                RETURN POINT FOR 2 PRINT FILES
         B     8(,R10)                  RETURN POINT FOR 1 PRINT FILE
         SPACE 2
************************************************************  11/88 SBG
* THE CBT973 COMPRESS PROGRAM MAY BE FOUND IN SOURCE FORM  *  11/88 SBG
* ON THE CBT MODS TAPE, FILE (2,NL), RECFM=FB,LRECL=80,    *  11/88 SBG
* BLKSIZE=32720.  IT TAKES 80-BYTE FB RECORDS AS INPUT,    *  11/88 SBG
* AND PRODUCES 94-BYTE LRECL, VB RECORDS AS OUTPUT.        *  11/88 SBG
*                                                          *  11/88 SBG
* DATA COMPRESSION TAKES THE FORM OF SQUEEZING OUT BLANKS. *  11/88 SBG
* A 10-BYTE BIT MAP HEADER TELLS THE PROGRAM WHICH OF THE  *  11/88 SBG
* 80 CHARACTERS WERE BLANKS, AND WHICH WERE NOT.  A RDW    *  11/88 SBG
* PRECEDES THE 10-BYTE BIT MAP, FOLLOWED BY THE NON-BLANK  *  11/88 SBG
* BYTES OF THE RECORD.  THE PROGRAM COUNTS ON A LOT OF     *  11/88 SBG
* BLANK CHARACTERS IN THE RECORD, TO ALLOW COMPRESSION     *  11/88 SBG
* TO BE ACHIEVED.                                          *  11/88 SBG
*                                                          *  11/88 SBG
* IT IS THE PRACTICE ON THE CBT TAPE TO IEBUPDTE-UNLOAD    *  11/88 SBG
* A SOURCE-FORMAT PDS TO A SEQUENTIAL FILE, AND THEN TO    *  11/88 SBG
* COMPRESS THE BLANKS OUT WITH THE CBT973 PROGRAM WHEN     *  11/88 SBG
* UNLOADING THE FILE ON TO THE CBT TAPE.  TAPEMAP USES     *  11/88 SBG
* THE CBT973 DECOMPRESS ALGORITHM TO DECOMPRESS ONE        *  11/88 SBG
* 80-BYTE RECORD AT A TIME, AND THEN USES ITS IEBUPDTE     *  11/88 SBG
* MEMBER NAME INTERPRETATION ON EACH DECOMPRESSED RECORD.  *  11/88 SBG
*                                                          *  11/88 SBG
* MEMBER NAMES IN UNLOADED LIBRARIES ARE THUS DIRECTLY     *  12/88 SBG
* REPORTED BY DOING A "TAPEMAP" RUN ON A CBT TAPE.         *  11/88 SBG
*                                                          *  12/88 SBG
* USUALLY ANY FB 80-BYTE LRECL NON-IEBUPDTE FILES ARE ALSO *  12/88 SBG
* COMPRESSED.  THESE FILES WILL APPEAR ON REPORT 2 BUT     *  12/88 SBG
* WILL NOT SHOW AS HAVING ANY MEMBERS.                     *  12/88 SBG
************************************************************  11/88 SBG
TEST$CBT CLC   TAPEBUFF+2(3),=X'000000' LOOK LIKE BDW+X'00'?  11/88 SBG
         BNER  R10                      NO CAN'T BE CBT973 FMT11/88 SBG
         CLC   TAPEBUFF+0(2),=X'0000'   LOOK LIKE BDW?        11/88 SBG
         BER   R10                      NO CAN'T BE CBT973 FMT11/88 SBG
         CLC   TAPEBUFF+6(2),=X'0000'   LOOK LIKE RDW?        11/88 SBG
         BNER  R10                      NO CAN'T BE CBT973 FMT11/88 SBG
         CLC   TAPEBUFF+4(2),=X'0000'   LOOK LIKE RDW?        11/88 SBG
         BER   R10                      NO CAN'T BE CBT973 FMT11/88 SBG
TEST$CBA LA    R1,TAPEBUFF+4    IF CBT973, THEN START OF RDW  11/88 SBG
         LH    R3,0(,R1)    DON'T BE MODULO 256..SAVE IT ALL. 07/90 SBG
         C     R3,=F'94'            LRECL=94 MAX FOR CBT973.  11/88 SBG
         BHR   R10                  BETWEEN 95 & 255, NOT CBT 11/88 SBG
         ST    R4,CBSAVE4           USE R4 FOR WKREG          11/88 SBG
         LH    R4,TAPEBUFF          GET BLKSIZE FROM BDW      11/88 SBG
         S     R4,=F'4'             4 LESS FOR COMPARE W/RECSZ11/88 SBG
         CR    R3,R4                ONE RECORD IN THIS BLOCK? 11/88 SBG
         BL    TEST$CBN             YES. SKIP TEST PAST EO-REC11/88 SBG
************************************************************* 12/88 SBG
*   "EMPTY FILES" ON THE CBT TAPE ARE NOT REALLY EMPTY.     * 12/88 SBG
*    THEY ARE ONE-RECORD CBT973 COMPRESSED FILES WITH A     * 12/88 SBG
*    CERTAIN FIXED LITERAL EXPRESSION ON THE ONE CARD.      * 12/88 SBG
*    A USER OF THE CBT TAPE WOULD LIKE TO KNOW WHICH TAPE   * 12/88 SBG
*    FILES ARE CONSIDERED "EMPTY" BY THE TAPE ADMINISTRATOR.* 12/88 SBG
************************************************************* 12/88 SBG
TEST$CBL CLC   14(L'EMPTYLIT,R1),EMPTYLIT   CBT "EMPTY" FILE? 11/88 SBG
         BE    TEST$CBE             FLAG AS CBT EMPTY FILE    12/88 SBG
         CLC   14(L'EMPTYLI2,R1),EMPTYLI2   OLD "EMPTY" FILE? 12/88 SBG
         BNE   TEST$CBN             NO-STILL TEST FOR CBT973. 12/88 SBG
************************************************************* 12/88 SBG
*   AN "EMPTY FILE" ON THE CBT TAPE HAS BEEN FOUND.         * 12/88 SBG
************************************************************* 12/88 SBG
TEST$CBE MVC   UNLOADER(8),CBTEMPTY  REPORT AS EMPTY.         12/88 SBG
         L     R4,CBSAVE4           RESTORE REG 4             11/88 SBG
         TM    TFLAG2,T2@PRT2       IS SECOND REPORT WANTED?  12/88 SBG
         BOR   R10            NO SECOND REPORT FOR EMPTY FILE 12/88 SBG
         B     8(,R10)        WE WANT TO FLAG IF ONE REPORT   12/88 SBG
************************************************************* 12/88 SBG
*   CBTEMPTY IS FLAGGED ON 'PARM=INLINE', BUT WE DON'T WANT * 12/88 SBG
*    WANT EMPTY CBT FILES REPORTED ON THE SECOND REPORT     * 12/88 SBG
*    TO CONFUSE SOMEONE WHO MAY THINK IT IS A NONTRIVIAL    * 12/88 SBG
*    NON-IEBUPDTE CBT973-COMPRESSED FILE.                   * 12/88 SBG
************************************************************* 12/88 SBG
TEST$CBN DS    0H                   NOT CBT EMPTY FILE        11/88 SBG
         AR    R3,R1                PAST RECORD END           11/88 SBG
         LH    R4,0(,R3)            GET SECOND RECORD SIZE    11/88 SBG
         C     R4,=F'14'            SEE IF LESS THAN 14       11/88 SBG
         BL    TEST$CJ1             LESS THAN 14 IS NO GOOD   08/90 SBG
         C     R4,=F'94'            SEE IF MORE THAN 94       08/90 SBG
         BNH   TEST$CJ2             IN BETWEEN, TEST FURTHER  08/90 SBG
TEST$CJ1 L     R4,CBSAVE4           RESTORE REG 4 BEFORE RET  08/90 SBG
         BR    R10                  GO BACK TO CALLER         08/90 SBG
TEST$CJ2 CLI   0(R3),X'00'          IF CBT973, MUST BE X'00'  08/90 SBG
         BNER  R10                  NOT SO, THEN NOT CBT973   11/88 SBG
         CLI   1(R3),X'00'          IF CBT973, CAN'T BE X'00' 11/88 SBG
         BER   R10                  IT IS, THEN NOT CBT973    11/88 SBG
         CLC   2(2,R3),=X'0000'     LOOK LIKE RDW?            11/88 SBG
         BNER  R10                  NO. CAN'T BE CBT973 FMT   11/88 SBG
TEST$CBM LA    R1,14(,R1)           GO TO "NONBLANK" DATA     11/88 SBG
TEST$CBO CLI   0(R1),X'40'          HAD BETTER NOT BE A BLANK 11/88 SBG
         BER   R10                  IT IS A BLANK. NO GOOD.   11/88 SBG
         LA    R1,1(,R1)            BUMP ONE BYTE             11/88 SBG
         CR    R1,R3                END OF NONBLANK DATA?     11/88 SBG
         BL    TEST$CBO             NO, KEEP TRYING           11/88 SBG
         MVC   UNLOADER(6),CBT973   ASSUME CBT973 FORMAT      11/88 SBG
         B     TEST$RET                 RETURN                11/88 SBG
         SPACE 2                                              11/88 SBG
TEST$CPY CLC   =X'00CA6D0F',TAPEBUFF+8  VS2COPY'S TRADEMARK?
         BE    TEST$CYY                 YES                   06/93 RT
         CLC   =X'01CA6D0F',TAPEBUFF+8  VS2COPY'S PDSE MK?    06/93 RT
         BNER  R10                      NO
TEST$CYY MVC   UNLOADER(7),IEBCOPY      SAY WHO
         B     TEST$RET                 RETURN
         SPACE 2
TEST$UPS ST    R10,DWD                  SAVE MY RETURN ADDR
         LA    R2,80                    BXLE INCR
         LA    R15,TAPEBUFF             BXLE START
         LR    R3,R15                   COPY FOR END
         A     R3,LASTSIZE         POINT PAST END            09/84 DBC
         SR    R3,R2                    POINT TO LAST CARD IN BLOCK
         SPACE 1
TEST$UPL LR    R1,R15                   COPY CARD START ADDR
         BA&XA R10,TEST$UP              IS THIS IEBUPDTE CTL CARD?
         B     TEST$UPB                 NO - GO BXLE
         MVC   UNLOADER(8),IEBUPXXX SAY WHO                   06/84 DBC
         L     R10,DWD                  GET BACK MY RETURN ADDR
         B     TEST$RET                 AND RETURN TO CALLER
         SPACE 1
TEST$UPB BXLE  R15,R2,TEST$UPL          TRY ALL CARDS IN THIS BLOCK
         L     R10,DWD                  RESTORE RETURN ADDR
         BR    R10                      NOT IEBUPDTE INPUT STREAM
         SPACE 2
TEST$UP  CLC   =C'./',0(R1)             START RIGHT?
         MVI   UPDTXFLG,C' '            ASSUME ./ CONTROL CARD11/88 SBG
         BE    TEST$UPC                 CONTINUE              11/88 SBG
TEST$UPX CLC   =C'><',0(R1)             SUBORDINATE MEMBER?   11/88 SBG
         BNER  R10                      NO - THAT WAS QUICK
         MVI   UPDTXFLG,C'>'            REALLY >< CONTROL CARD11/88 SBG
TEST$UPC DS    0H                       CONTINUE PROCESSING   11/88 SBG
         LA    R1,2(,R1)                POINT TO SCAN START
         LA    R0,69                    MAX SCAN LENGTH
         BA&XA R14,F$BLANK              FIND END OF NAME FIELD
         BA&XA R14,F$CHARS              FIND THE VERB
         CLC   =C'ADD ',0(R1)
         BE    TU$SAVE
         CLC   =C'REPL ',0(R1)
         BE    TU$SAVE
         CLC   =C'CHANGE ',0(R1)
         BE    TU$SAVE
         CLC   =C'CHNGE ',0(R1)    IEBUPDAT TYPE CHANGE?      06/84 DBC
         BNER  R10                 NO, RETURN EMPTY HANDED    06/84 DBC
         SPACE 2
TU$SAVE  MVC   UPDT#TYP(1),0(R1)        A,R,C
         MVC   IEBUPXXX,=C'IEBUPDTE' ASSUMPTION               06/84 DBC
         STM   R0,R1,TU$SAVE2      SAVE SCANNER               06/84 DBC
         SPACE 1
TU$LOOP  LA    R1,1(,R1)
         CLC   =C'MEMBER=',0(R1)
         BE    TU$MEM
         CLC   =C'NAME=',0(R1)
         BE    TU$NAM
         BCT   R0,TU$LOOP               SCAN THE REST OF THE CARD
         SPACE 3                                              06/84 DBC
************************************************************* 06/84 DBC
*        THIS IS NOT AN INTERESTING IEBUPDTE TYPE CARD, BUT * 06/84 DBC
*        IT STILL MIGHT BE AN INTERESTING IEBUPDAT TYPE     * 06/84 DBC
*        CARD. SCAN TO SEE IF THE OPERAND FIELDS START WITH * 06/84 DBC
*        A FIRST POSITIONAL PARAMETER THAT IS A MEMBER      * 06/84 DBC
*        NAME.                                              * 06/84 DBC
************************************************************* 06/84 DBC
         SPACE 1                                              06/84 DBC
         LM    R0,R1,TU$SAVE2      RESTORE VERB PTR           06/84 DBC
         BA&XA R14,F$BLANK         SCAN PAST THE VERB         06/84 DBC
         BA&XA R14,F$CHARS         SCAN TO THE OPERANDS       06/84 DBC
         STM   R0,R1,TU$SAVE2      SAVE OPERANDS PTR          06/84 DBC
         SPACE 1                                              06/84 DBC
         LA    R0,9                MAX L'NAME+1               06/84 DBC
         BA&XA.R  R14,0            LOOP HEAD                  06/84 DBC
         CLI   0(R1),C'='          IEBUPDTE TYPE CARD?        06/84 DBC
         BER   R10                 YES, GIVE UP               06/84 DBC
         CLI   0(R1),C','          NO, EO-1ST OPERAND?        06/84 DBC
         BE    TU$UPDAT            YES, GO SAVE               06/84 DBC
         CLI   0(R1),C' '          MAYBE, CHECK AGAIN         06/84 DBC
         BE    TU$UPDAT            YES, GO SAVE               06/84 DBC
         LA    R1,1(,R1)           NO, ADVANCE SCANNER        06/84 DBC
         BCTR  R0,R14              LOOP BACK                  06/84 DBC
         BR    R10                 1ST OPERAND TOO LONG; GVUP 06/84 DBC
         SPACE 1                                              06/84 DBC
TU$UPDAT MVC   IEBUPXXX,=C'IEBUPDAT' RESET "UNLOADER" NAME    06/84 DBC
         LM    R0,R1,TU$SAVE2      RESTORE NAME POINTER       06/84 DBC
         B     4(,R10)             RETURN WITH NAME           06/84 DBC
         SPACE 2
TU$MEM   LA    R1,7(,R1)                POINT TO MEMBER NAME
         B     *+8                      SKIP OTHER LA
         SPACE 1
TU$NAM   LA    R1,5(,R1)                POINT TO MEMBER NAME
         SPACE 1
         MVI   8(R1),C','               BE SURE TO STOP
         B     4(,R10)                  FOUND A NAME
         EJECT ,                                              07/85 DBC
************************************************************* 07/85 DBC
*        TEST TO SEE IF THE CURRENT FILE IS A SMPPTFIN      * 07/85 DBC
*        FILE.                                              * 07/85 DBC
************************************************************* 07/85 DBC
         SPACE 1                                              07/85 DBC
TEST$PTF ST    R10,DWD             SAVE THE RETURN PTR        07/85 DBC
         LA    R2,80               GET BXLE SCANNER           07/85 DBC
         LA    R15,TAPEBUFF        --> TAPE BUFFER            07/85 DBC
         LR    R3,R15              GET -                      07/85 DBC
         AL    R3,LASTSIZE          BXLE -                    07/85 DBC
         SLR   R3,R2                 LIMIT                    07/85 DBC
         SPACE 1                                              07/85 DBC
TEST$PTL LR    R1,R15              --> NEXT CARD              07/85 DBC
         BA&XA R10,TEST$PT         SEE IF IT'S ++PTF, ++APAR, 07/85 DBC
*                                  ++FUNCTION, OR ++USERMOD   07/85 DBC
         B     TEST$PTB            +0 NOPE, SKIP TO NEXT CARD 07/85 DBC
         MVC   UNLOADER(8),SMPPTFIN +4 YEP, NAME THE          07/85 DBC
*                                  "UNLOADER"                 07/85 DBC
         L     R10,DWD             RESTORE RETURN ADDRESS     07/85 DBC
         B     TEST$RET            GO RETURN TO CALLER        07/85 DBC
         SPACE 1                                              07/85 DBC
TEST$PTB BXLE  R15,R2,TEST$PTL     ADVANCE TO NEXT CARD AND   07/85 DBC
*                                  LOOP TO TEST IT            07/85 DBC
         SPACE 1                                              07/85 DBC
         L     R10,DWD             NO ++PTF ATC. CARD WAS     07/85 DBC
*                                  FOUND. RESTORE LINK        07/85 DBC
         BR    R10                 RETURN EMPTY HANDED        07/85 DBC
         SPACE 3                                              07/85 DBC
************************************************************* 07/85 DBC
*        SEE IF THE CURRENT RECORD IS A SMP SYSMOD HEADER   * 07/85 DBC
*        CARD (++PTF, ++APAR, ++FUNCTION, OR ++USERMOD).    * 07/85 DBC
*        NOTE, THIS SCAN EXAMINES ONLY ONE CARD AT A TIME.  * 07/85 DBC
*        ACCORDINGLY, IT WILL WORK ONLY IF THE SYSMOD'S     * 07/85 DBC
*        NAME IS NOT CONTINUED ONTO ANOTHER CARD.           * 07/85 DBC
************************************************************* 07/85 DBC
         SPACE 1                                              07/85 DBC
TEST$PT  CLC   =C'++',0(R1)        PLUSPLUS CARD?             07/85 DBC
         BNER  R10                 NO, RETURN +0              07/85 DBC
         LA    R1,2(,R1)           YES, ADVANCE SCANNER       07/85 DBC
         LA    R0,69               LOAD SCAN LIMIT            07/85 DBC
         BA&XA R14,F$CHARS         SKIP BLANKS                07/85 DBC
         SPACE 1                                              07/85 DBC
         LA    R14,7               L'SYSMOD TYPE NAME         07/85 DBC
         CLC   =C'USERMOD',0(R1)   ++USERMOD?                 07/85 DBC
         BE    TP$SAVE             YES, PROCEED               07/85 DBC
         LA    R14,3               NO, RESET L'NAME           07/85 DBC
         CLC   =C'PTF',0(R1)       ++PTF?                     07/85 DBC
         BE    TP$SAVE             YES, PROCEED               07/85 DBC
         LA    R14,8               NO, RESET L'NAME           07/85 DBC
         CLC   =C'FUNCTION',0(R1)  ++FUNCTION?                07/85 DBC
         BE    TP$SAVE             YES, PROCEED               07/85 DBC
         CLC   =C'APAR',0(R1)      NO, ++APAR?                07/85 DBC
         BNER  R10                 NO, RETURN +0              07/85 DBC
         LA    R14,4               YES, SET L'NAME            07/85 DBC
         SPACE 1                                              07/85 DBC
TP$SAVE  MVC   PTF#TYPE,0(R1)      SAVE SYSMOD TYPE           07/85 DBC
         SR    R0,R14              DECR LOOP CNTL; TOO FAR?   07/85 DBC
         BNPR  R10                 YES, RETURN +0             07/85 DBC
         ALR   R1,R14              NO, ADVANCE SCANNER        07/85 DBC
         BA&XA R14,F$CHARS         SKIP MORE BLANKS, IF ANY   07/85 DBC
         CLI   0(R1),C'('          SUB-VALUE?                 07/85 DBC
         BNER  R10                 NO, RETURN +0              07/85 DBC
         BCT   R0,TP$PTF           YES, DECR SCAN LIMIT       07/85 DBC
         BR    R10                 RETURN +0 IF TOO FAR       07/85 DBC
TP$PTF   LA    R1,1(,R1)           ADVANCE ONE                07/85 DBC
         BA&XA R14,F$CHARS         SKIP MORE BLANKS, IF ANY   07/85 DBC
         B     4(,R10)             RETRUN +4 WITH A POINTER   07/85 DBC
*                                  TO THE SYSMOD NAME.        07/85 DBC
         EJECT ,                                              07/85 DBC
************************************************************* 12/88 SBG
*  WE WANT TO CHECK IF WE HAVE FILE 1 FROM A REAL CBT TAPE. * 12/88 SBG
*   IF THAT'S WHAT WE HAVE, WE WANT TO REPORT THE VERSION   * 12/88 SBG
*   NUMBER AND CREATION DATE OF THAT CBT TAPE.              * 12/88 SBG
************************************************************* 12/88 SBG
TEST$CH  ST    R15,CBSAVE15        SAVE REG 15                12/88 SBG
         LA    R15,TAPEBUFF        CBT HEADER. LOOK FOR       12/88 SBG
         CLC   CHJOBLIT,0(R15)     ARNIE CASINGHINO'S JOBNAME 12/88 SBG
         BE    TEST$CHG            CBT HEADER                 12/88 SBG
         L     R15,CBSAVE15        RESTORE REG 15             12/88 SBG
         BR    R10                 RETURN NOT FOUND           12/88 SBG
TEST$CHG MVC   UNLOADER(8),CBHEADER     FLAG AS FILE 1 OF CBT 12/88 SBG
         MVI   CSTATCLR,X'40'      CLEAR VERS & DATE          12/88 SBG
         MVC   CBTVERS(12),CSTATCLR   NOW DO IT .             12/88 SBG
         ST    R4,CBSAVE4          SAVE REG 4                 12/88 SBG
         LR    R4,R15              ADDR OF TAPE BUFFER        12/88 SBG
         A     R4,CDOCSIZE         END OF BLOCK ADDRESS       12/88 SBG
TEST$CHL LA    R15,1(,R15)         SEARCH FOR DATE AND VERSION12/88 SBG
         CR    R15,R4              END OF BLOCK YET?          12/88 SBG
         BNL   TEST$CHR            NO DATE OR VERSION FOUND   12/88 SBG
         CLC   VERSLIT1,0(R15)     DATE HDR FOUND?            12/88 SBG
         BE    TEST$CHD            YEP. GET DATE.             12/88 SBG
         CLC   VERSLIT2,0(R15)     VERSION # HDR FOUND?       12/88 SBG
         BNE   TEST$CHL            NOPE. KEEP TRYING.         12/88 SBG
TEST$CHV MVC   CBTVERS(4),10(R15)  SAVE VERSION #             12/88 SBG
         L     R4,CBSAVE4          RESTORE REG 4              12/88 SBG
         L     R15,CBSAVE15        RESTORE REG 15             12/88 SBG
         B     TEST$RET            DONE WITH EXTRACTION.      12/88 SBG
TEST$CHD MVC   CBTDATE(8),10(R15)  SAVE TAPE RELEASE DATE     12/88 SBG
         B     TEST$CHL            KEEP TRYING FOR VERSION #. 12/88 SBG
TEST$CHR L     R4,CBSAVE4          RESTORE REG 4              12/88 SBG
         L     R15,CBSAVE15        RESTORE REG 15             12/88 SBG
         BR    R10                 RETURN UNSUCCESSFUL        12/88 SBG
         EJECT                                                12/88 SBG
F$BLANK  BCTR  R0,0                     -1 FROM LENGTH
         CLI   0(R1),C' '               GOT A BLANK YET?
         BER   R14                      YES - RETURN
         LA    R1,1(,R1)                MOVE SCAN PTR
         BCT   R0,F$BLANK+2             KEEP SCANNING
         BR    R10                      NONE--RETURN TO CALLER'S CALLER
         SPACE 2
F$CHARS  BCTR  R0,0                     -1 FROM LENGTH
         CLI   0(R1),C' '               FIND A CHAR?
         BNER  R14                      YES - RETURN
         LA    R1,1(,R1)                INCR SCAN PTR
         BCT   R0,F$CHARS+2             KEEP SCANNING
         BR    R10                      NONE--RETURN TO CALLER'S CALLER
         SPACE 2
TEST$DMP CLC   =X'F47006016663B24D',TAPEBUFF+12  IEHDASDR'S TRADEMARK?
         BNER  R10                      NO
         MVC   UNLOADER(8),IEHDASDR     SAY WHO
         MVC   DASDRSAV(24),TAPEBUFF    SAVE HEADER RECORD
         LR    R2,R10                   SAVE RETURN ADDR
         TAPIO TCCW#DAT                 IGNORE CCWS
         SPACE 1
COM$DUMP TAPIO TCCW#DAT                 READ FIRST TRK'S DATA
         MVC   FDRSAVE(8),TAPEBUFF+12   SAVE IN CASE FDR
         LR    R10,R2                   RETURN
         B     TEST$RET
         SPACE 2
TEST$FDR CLC   =C'THATS ALL FOLK',TAPEBUFF+5   HIS TRADEMARK?
         BNER  R10                      NO
         LR    R2,R10                   SAVE RETURN ADDR
         MVC   DASDRSAV(24),TAPEBUFF    SAVE HEADER RECORD
         MVC   UNLOADER(3),FDR          SAY FDR FOR NOW
         CLI   TAPEBUFF+19,C'S'         WAS IT FDR?
         BE    COM$DUMP                 YES
         MVC   UNLOADER(6),FDRDSF       NO  - MAYBE FDRDSF
         CLI   TAPEBUFF+19,C'F'         FDRDSF?
         BE    COM$DUMP                 YES
         MVC   UNLOADER(6),FDRXXX       NO  - BUT SOME KIND OF FDR?
         B     COM$DUMP                 CONTINUE
         SPACE 2
TEST$SLK CLC   =F'20',LASTSIZE     RIGHT LENGTH FOR SLICK HDR 09/84 DBC
         BNER  R10
         CLC   TAPEBUFF+8(12),FFS         FF'S AT END OF RECORD?
         BNER  R10
         CLI   TAPEBUFF+1,C'B'          BACKUP FLAG PRESENT?
         BNER  R10
         MVC   DASDRSAV(20),TAPEBUFF    SAVE THE FIRST RECORD
         LR    R2,R10                   SAVE RETURN ADDR
         TAPIO TCCW#DAT                 GET 2ND RECORD
         LR    R10,R2                   RESTORE RETURN ADDR
         CLC   TAPEBUFF(2),=H'0'        1ST HALFWORD = 0?
         BNER  R10
         CLC   TAPEBUFF+2(2),=H'0'      2ND HALFWORD = 0 OR FF'S?
         BE    TEST$SK1
         CLC   TAPEBUFF+2(2),FFS
         BNER  R10
TEST$SK1 LH    R2,TAPEBUFF+4            VERIFY LENGTH OF RECORD
         LTR   R2,R2
         BNPR  R10
         LA    R2,8(,R2)
         C     R2,LASTSIZE         LENGTH OK?                09/84 DBC
         BNER  R10
         CLC   TAPEBUFF+6(2),=H'0'      LAST HALF OF RDW = 0?
         BNER  R10
         MVC   UNLOADER(8),SLICKMNG     SHOW SLICKMNG AS UNLOADER
         B     TEST$RET                 RETURN TO MAINLINE
         SPACE 3
* FOR IEBISAM, THE BEGINNING OF THE DCB IS UNLOADED TO THE FIRST
* TWO CARDS.  SO CHECK (GUESS?) IF IT LOOKS LIKE AN ISAM DCB
TEST$IS  CLC   TAPEBUFF(4),=Y(0,154)    SEQ # AND LENGTH CORRECT?
         BNER  R10                      NO
         CLC   TAPEBUFF+80(2),H1        SECOND SEQ # CORRECT
         BNER  R10                      NO
         TM    TAPEBUFF+4+48,X'10'      DCBOFLGS,DCBOFOPN
         BNOR  R10                      NOT "OPEN"
         CLI   TAPEBUFF+4+26,X'80'      DSORG=IS?
         BE    *+10                     YES - CONTINUE
         CLI   TAPEBUFF+4+26,X'81'      DSORG=ISU?
         BNER  R10                      NO
         MVC   UNLOADER(7),IEBISAM      SAY WHO
         B     TEST$RET                 RETURN
         SPACE 3
TEST$MAP CLC   =C'$$TAPEMAP.PRINT.FILE$$',TAPEBUFF  CHECK FOR HEADER
         BNER  R10                      NO
         L     R1,LASTSIZE         VERIFY BLOCK MULT OF 80 BY 09/84 DBC
         SR    R0,R0
         LA    R2,80
         DR    R0,R2
         LTR   R0,R0
         BNZR  R10                      NO
         MVC   UNLOADER(7),CTAPEMAP     YES - THIS IS FOR US
         B     TEST$RET
         EJECT
IEBUPDT1 MVC   OUTBUFF2+8(27),=C'IS AN IEBUPXXX INPUT STREAM' 07/85 DBC
         MVC   OUTBUFF2+14(8),IEBUPXXX CORRECT RELOADER NAME  07/85 DBC
         B     PRT$ONE             GO RELOOP                  07/85 DBC
         SPACE 3                                              07/85 DBC
PTFS1    MVC   OUTBUFF2+8(35),=C'IS AN SMPPTFIN SYSMODS INPUT STREAM' C
         B     PRT$ONE             GO RELOOP                  07/85 DBC
         SPACE 3                                              07/85 DBC
CBT1     CLC   CBTEMPTY,UNLOADER        CBT EMPTY FILE        11/88 SBG
         BE    CBTEMP1                  PRINT SPECIAL INFO    11/88 SBG
         MVC   OUTBUFF2+8(28),=C'IS IN CBT973 COMPRESS FORMAT'  /88 SBG
         B     PRT$ONE             GO RELOOP                  11/88 SBG
         SPACE 3                                              07/85 DBC
CBTEMP1  MVC   OUTBUFF2+8(25),=C'IS AN EMPTY CBT TAPE FILE'   11/88 SBG
         B     PRT$ONE             GO RELOOP                  11/88 SBG
         SPACE 3                                              07/85 DBC
CBTDOC1  MVC   OUTBUFF2+8(27),=C'CBT TAPE DOCUMENTATION FILE' 12/88 SBG
         B     PRT$ONE             GO RELOOP                  12/88 SBG
         SPACE 3                                              12/88 SBG
IEHMOVE1 MVC   #BLKCNT+OUTBUFF2-OUTBUFF(4),=C'DSN='
         MVC   #BLKCNT+OUTBUFF2-OUTBUFF+4(44),TAPEBUFF+85 MOVE DISK DSN
         SPACE 1
         BA&XA R10,SET$ONE
         DC    Y(169)                   DSORG OFFSET
         DC    Y(171)                   RECFM OFFSET
         DC    Y(173)                   BLKSIZE OFFSET
         DC    Y(175)                   LRECL OFFSET
         SPACE 3
IEBCOPY1 BA&XA R10,SET$ONE
         DC    Y(12)                    DSORG OFFSET
         DC    Y(18)                    RECFM OFFSET
         DC    Y(14)                    BLKSIZE OFFSET
         DC    Y(16)                    LRECL OFFSET
         SPACE 3
IEBISAM1 BA&XA R10,SET$ONE
         DC    Y(4+26)                  DSORG OFFSET
         DC    Y(4+36)                  RECFM OFFSET
         DC    Y(4+62)                  BLKSIZE OFFSET
         DC    Y(4+2+82)                LRECL OFFSET
         SPACE 3
SET$ONE  MVC   OUTBUFF2+8(24),=C'TRUE ATTRIBUTES:  DSORG='
         ICALL CNVDSORG,R14,R15=OUTBUFF2+32 FORMAT THE DSORG
         ICALL CNVRECFM,R14,R15=#RECFM+OUTBUFF2-OUTBUFF
         SPACE 1
         LA    R1,TAPEBUFF
         AH    R1,4(,R10)               POINT TO BLKSIZE
         ICALL CONVHALF,R14,R15=#BLKSIZE-1+OUTBUFF2-OUTBUFF
         SPACE 1
         LA    R1,TAPEBUFF
         AH    R1,6(,R10)               POINT TO LRECL
         ICALL CONVHALF,R14,R15=#LRECL-1+OUTBUFF2-OUTBUFF
         SPACE 2
*  FOR PARM=INLINE, THE REPORT MUST PRINT BOTH THE RESULTS    12/88 SBG
*  OF THE LABEL PROCESSING (FOR SL) AND THE RESULTS OF THE    12/88 SBG
*  SCAN PROCESSING.                                           12/88 SBG
PRT$ONE  TM    TFLAG1,T1@SL             SL TAPE?              12/88 SBG
         BNO   PRT$ONEX                 NO. DO NORMAL STUFF   12/88 SBG
         TM    PARMFLAG,PF@INLIN        INLINE ?              12/88 SBG
         BO    FIND$EOF                 REGULAR INLINE STUFF  12/88 SBG
PRT$ONEX TM    TFLAG1,T1@ANLZ           ANALYZING?            12/88 SBG
         BNO   FIND$EOF                 NO
         B     NL$LOOP                  YES - KEEP SCANNING NL TAPE
         SPACE 3
*  FIRST RECORD SAVED IN "DASDRSAV"
IHDASDR1 MVC   OUTBUFF2+8(15),=C'DUMP FROM CCHH='
         UNPK  OUTBUFF2+23(9),DASDRSAV+0(5)
         TR    OUTBUFF2+23(8),HEXTAB    MAKE EBCDIC
         MVC   OUTBUFF2+31(5),=C' OF A'
         IC    R1,DASDRSAV+21           GET DEVTYPE
         N     R1,=F'255'               CLEAN
         MH    R1,=H'6'
         LA    R14,=C'2321  2311  2314  2302  2301  2305-12305-23330  3*
               340  3350  3375  3330-13380  3390  '           03/94 SBG
         LA    R14,0(R14,R1)            GET ADDR OF THIS DEVTYPE
         MVC   OUTBUFF2+37(6),0(R14)    MOVE TO PRINT LINE
         CLC   =F'0',DASDRSAV+8         THIS TAPE START AT 0?
         BNE   PRT$ONE                  NO - CAN'T FIND VOLSER
*  SECOND RECORD IS IN "TAPEBUFF"
         CLC   =C'VOL1VOL1',TAPEBUFF+216  LABEL?
         BNE   PRT$ONE                  NO - SKIP
         MVC   OUTBUFF2+46(7),=C'VOLUME='
         MVC   OUTBUFF2+53(6),TAPEBUFF+224
         MVC   OUTBUFF2+61(6),=C'OWNER='
         MVC   OUTBUFF2+67(10),TAPEBUFF+261
         B     PRT$ONE
         SPACE 3
* FIRST RECORD SAVED IN "DASDRSAV"
FDR1     MVC   OUTBUFF2+8(20),=C'DUMPED FROM A UUUUUU'
         MVC   FDRDVEND(1),DASDRSAV+4   SET UP FENCE FOR DEV TYPE
         LA    R1,FDRDVTAB              POINT TO FDR DEV TYPE TABLE
FDR1LOOP CLC   DASDRSAV+4(1),0(R1)      MATCH?
         BE    FDR1FND                  YES
         LA    R1,7(,R1)                NO  - BUMP TO NEXT ENTRY
         B     FDR1LOOP
FDR1FND  MVC   OUTBUFF2+22(6),1(R1)     PUT DEV TYPE IN MESSAGE
         CLC   =C'DUMMYDSF',FDRSAVE     IS IT A NORMAL DUMP?
         BE    PRT$ONE                  YES - ALL FOR NOW
*                                       NO  - MUST BE ABS TRK DUMP
         MVC   OUTBUFF2+28(22),=C' (ABSOLUTE TRACK DUMP)'
         B     PRT$ONE
         SPACE 3
SLK1     MVC   OUTBUFF2+8(30),=C'SLICK LIBRARY BACKUP, CYCLE = '
         MVC   OUTBUFF2+38(1),DASDRSAV  PUT IN THE CYCLE NUMBER
         MVC   OUTBUFF2+39(12),=C', MAXSIZE = '
         ICALL CONVHALF,R14,R1=DASDRSAV+2,R15=OUTBUFF2+41
         B     PRT$ONE                  PRINT THE INFO LINE
         EJECT
IEHMOVE2 LA    R0,7                     ASSUME PARM=NOMEM
         TM    PARMFLAG,PF@NOMEM        IS IT?
         BO    *+8                      YES
         LA    R0,9                     NO
         TM    TAPEBUFF+169,X'02'       DSORG=PO?
         BO    *+8                      YES
         LA    R0,7                     NO
         NEWPAGE  (R0),2                ENOUGH LINES LEFT ON PAGE?
         SPACE 1
         BA&XA R10,SET$FULL             SET UP HEADER
         DC    Y(169)                   DSORG OFFSET
         DC    Y(171)                   RECFM OFFSET
         DC    Y(173)                   BLKSIZE OFFSET
         DC    Y(175)                   LRECL OFFSET
         SPACE 1
         MVC   OUTBUFF2+47(7),IEHMOVE   WHO IT WAS UNLOADED BY
         SPACE 1
         ICALL CONVUNIT,R10,R1=TAPEBUFF+282 CONVERT UNIT TYPE
         SPACE 1
         PRTLN2                         PRINT FIRST INFO LINE
         SPACE 1
         MVI   OUTBUFF2,C'0'            DOUBLE SPACE FOR MSM
         MVC   OUTBUFF2+25(7),=C'C-DATE='
         ICALL CONVDATE,R10,R1=TAPEBUFF+138,R15=OUTBUFF2+32 C-DATE
         SPACE 1
         MVC   OUTBUFF2+42(7),=C'E-DATE='
         ICALL CONVDATE,R10,R1=TAPEBUFF+141,R15=OUTBUFF2+49 E-DATE
         SPACE 1
         MVC   OUTBUFF2+59(4),=C'DSN='
         MVC   OUTBUFF2+63(44),TAPEBUFF+85       MOVE IN OLD DSN
         SPACE 1
****     CLC   =C'VS2',TAPEBUFF+152     CHECK PROGRAMMING SYSTEM
****     BE    MV$NOVOL                 SKIP IF OS/VS2
         TM    TAPEBUFF+132,X'0F'       SEE IF LOOKS LIKE PACKED DATE
         BO    MV$NOVOL                 SKIP IF NEW FORMAT IEHMOVE DS
         CLI   TAPEBUFF+135,0           INVALID FOR VOLSER ANYWAY?
         BE    MV$NOVOL                 SKIP IF NEW FORMAT IEHMOVE DS
         MVC   OUTBUFF2+120(4),=C'VOL='
         MVC   OUTBUFF2+124(6),TAPEBUFF+130
         SPACE 1
MV$NOVOL PRTLN2                         PRINT SECOND INFO LINE
         SPACE 2
         TM    TAPEBUFF+169,X'02'       DSORG=PO?
         BNO   STAR$TWO                 NO - SKIP
         BA&XA R10,FMU$                 PRINT HEADER
         SPACE 2
         L     BLEN,LASTSIZE       GET SIZE OF BLOCK          09/84 DBC
         SH    BLEN,=H'80'              MINUS FIRST CARD USED
         LA    BPTR,TAPEBUFF+80         -> NEXT CARD TO USE
         BA&XA R10,SEG$NEXT             NOW HAVE CARDS 1 & 2
         BA&XA R10,SEG$NEXT             NOW 2 & 3
         BA&XA R10,SEG$NEXT             NOW 3 & 4
         BA&XA R10,SEG$NEXT             NOW 4 & 5
         LA    DPTR,MV#BUFF1+46         -> FIRST MEMBER NAME -4
         CLI   0(DPTR),X'C8'            START OF MEMBER?
         BE    MV$GOT1                  YES
         LA    DPTR,3(,DPTR)            -> OTHER PLACE
         CLI   0(DPTR),X'C8'            START OF MEMBER?
         BE    MV$GOT1                  YES
         BA&XA R14,BOOM                 PRINT LOGIC ERROR MSG
         SPACE 2
MV$GOT1  LA    DPTR,4(,DPTR)            -> FIRST MEMBER NAME
         SPACE 2
MV$LOOP  BA&XA R10,MOVE$MEM             MEMBER NAME TO PRINT LINE
         BA&XA R10,SEG$TEST             SEE IF WITHIN RANGE
         SPACE 2
*  RECORDS IN AN IEHMOVE UNLOADED DATASET HAVE THE FOLLOWING FORMAT:
*  FIRST 2 BYTES OF EACH CARD IS A BINARY SEQUENCE NUMBER.
*  AFTER THAT, THERE IS SOME HEADER INFO (THE FMT1 DSCB + OTHER JUNK),
*  THEN THE RECORDS IN THIS FORMAT:  LENGTH OF THIS "RECORD", 1 BYTE
*  INDICATOR WITH THE FOLLOWING BIT MEANINGS:
*    X'80' -> 3 BYTE TTR FOLLOWS INDICATOR BYTE
*    X'40' -> UNLOADED DS IS PDS
*    X'20' -> RECORD IS PART OF A MEMBER
*    X'10' -> RECORD IS A NOTE LIST
*    X'08' -> RECORD IS A DIRECTORY ENTRY
*    X'04' -> RECORD IS A DUMMY RECORD
*  THIS INFORMATION IS IN THE SOURCE TO MODULE IEHMVSRA
         SPACE 1
MV$NXTBL CLI   2(DPTR),X'E0'            DATA RECORD?
         BE    MV$REC                   YES
         CLI   2(DPTR),X'D0'            NOTE LIST RECORD?
         BE    MV$REC                   YES
         CLI   2(DPTR),X'C8'            DIRECTORY RECORD?
         BE    MV$MEND                  YES
         CLI   2(DPTR),X'C4'            DUMMY RECORD?
         BE    MV$DUMMY                 YES
         CLI   2(DPTR),X'0A'            END OF DS?
         BNH   DIR$END                  YES
*** LOGIC ERROR ***
         PRTLN2                         PRINT (POSSIBLY) UNFINISHED LIN
         BA&XA R14,BOOM                 PRINT LOGIC ERROR MSG
         SPACE 2
MV$DUMMY MVC   DWD(2),0(DPTR)           COPY LENGTH TO ALIGNED PLACE
         LH    DLEN,DWD                 LOAD LENGTH OF DUMMY RECORD
         LA    DPTR,12(DPTR,DLEN)       POINT TO NEXT
         BA&XA R10,SEG$TEST             MAKE SURE STILL WITHIN RANGE
         B     MV$LOOP                  DO NEXT MEMBER NAME
         SPACE 2
MV$REC   MVC   DWD(2),0(DPTR)           COPY LEN TO ALIGNED PLACE
         LH    DLEN,DWD                 GET BLOCK LENGTH
         LA    DPTR,6(DPTR,DLEN)        POINT PAST LEN,E0,TTR,DATA
         SPACE 2
MV$SCAN  CL    DPTR,MV#ABUF2            WITHIN RANGE?
         BL    MV$NXTBL                 YES - PROCESS NEXT BLOCK
         BA&XA R10,SEG$NEXT             SKIP 78 BYTES
         B     MV$SCAN                  KEEP GOING
         SPACE 2
MV$MEND  LA    DPTR,6(,DPTR)            -> MEMBER NAME
         BA&XA R10,SEG$TEST             WITHIN RANGE?
         B     MV$LOOP                  GO PRINT NEXT NAME
         SPACE 2
SEG$TEST CL    DPTR,MV#ABUF2            WITHIN RANGE?
         BLR   R10                      YES - JUST RETURN
         SPACE 1
SEG$NEXT MVC   MV#BUFF1,MV#BUFF2
         LTR   BLEN,BLEN                ANY LEFT?
         BP    SEG$MVC                  YES - USE IT
         BZ    *+8                      NO
* RAN OFF END OF DATASET
***      EX    0,*                      *** LOGIC ERROR ***
         B     DIR$END                  FORGET THAT - IT'S THE END
         SPACE 1
         ST    R10,R10SAVE              SAVE RETURN ADDR
         TAPIO TCCW#DAT,TM=SEG$EOF      READ ANOTHER BLOCK
         L     R10,R10SAVE              RESTORE RETURN ADDR
         LA    BPTR,TAPEBUFF            RESET PTR
         L     BLEN,LASTSIZE       AND LENGTH LEFT            09/84 DBC
         SPACE 1
SEG$MVC  SH    BLEN,=H'80'              DECR LENGTH LEFT / THIS BLOCK
         MVC   MV#BUFF2,2(BPTR)         MOVE A SEGMENT
         LA    BPTR,80(,BPTR)           -> NEXT SEGMENT
         SH    DPTR,=H'78'              BACK UP DATA PTR
         BR    R10                      RETURN TO CALLER
         SPACE 2
SEG$EOF  BCTR  BLEN,0                   SET FLAG
         L     R10,R10SAVE              RESTORE RETURN ADDR
         OI    TFLAG1,T1@MVEOF          REMEMBER HIT EOF
         BR    R10                      RETURN TO CALLER
         SPACE 3
CONVDATE SR    R0,R0                    CLEAR FOR IC
         IC    R0,0(,R1)                GET THE YEAR
         CVD   R0,DWD                   CONVERT TO PACKED
         UNPK  0(3,R15),DWD+6(2)        AND THEN TO EBCDIC
         OI    2(R15),C'0'              FIX SIGN
         MVC   0(2,R15),1(R15)          MOVE YEAR OVER
         MVI   2(R15),C'.'              PUT IN THE DOT
         MVC   DWD(2),1(R1)             MOVE DAY TO ALIGNED PLACE
         LH    R0,DWD                   GET THE YEAR
         CVD   R0,DWD                   CONVERT TO PACKED
         UNPK  3(3,R15),DWD+6(2)        AND THEN TO EBCDIC
         OI    5(R15),C'0'              FIX THE SIGN
         BR    R10                      RETURN TO CALLER
         EJECT
IEBCOPY2 LA    R0,7                     ASSUME MEMBERS
         TM    PARMFLAG,PF@NOMEM        PARM=NOMEMBERS?
         BNO   *+8                      NOPE - OK
         LA    R0,5                     NO MEMBERS
         NEWPAGE  (R0),2                TEST LINE COUNTER
         SPACE 1
         BA&XA R10,SET$FULL             SET UP HEADER
         DC    Y(12)                    DSORG OFFSET
         DC    Y(18)                    RECFM OFFSET
         DC    Y(14)                    BLKSIZE OFFSET
         DC    Y(16)                    LRECL OFFSET
         SPACE 1
         MVC   OUTBUFF2+47(7),IEBCOPY   WHO IT WAS UNLOADED BY
         SPACE 1
         ICALL CONVUNIT,R10,R1=TAPEBUFF+24  FORMAT THE UNIT TYPE
         SPACE 2
         BA&XA R10,FMU                  'FOLLOWING MEMBERS UNLOADED:'
         SPACE 2
         TAPIO TCCW#DAT                 IGNORE THE NEXT BLOCK
         SPACE 2
IBCPY$LP TAPIO TCCW#DAT                 READ A DIRECTORY BLOCK
         LH    BLEN,TAPEBUFF+4          GET RDW
         SH    BLEN,=H'8'               -8 FOR BDR & RDW
         LA    BPTR,TAPEBUFF+8          INIT BLOCK PTR
         SPACE 2
DIR$NEXT LA    DPTR,22(,BPTR)           INIT DIR BLK PTR
         LH    DLEN,20(,BPTR)           GET LENGTH USED
         SH    DLEN,=H'2'               LENGTH OF DATA
         SPACE 2
DIR$LP   CLC   0(8,DPTR),=8X'FF'        END OF DIRECTORY?
         BE    DIR$END                  YES - DONE
         BA&XA R10,MOVE$MEM             MOVE NAME TO PRINT LINE
         LTR   DLEN,DLEN                ANYTHING LEFT?
         BP    DIR$LP                   CONTINUE IF MORE LEFT
         LA    BPTR,256+12+8(,BPTR)     NEXT DIRECTORY BLOCK
         SH    BLEN,=Y(256+12+8)        DECR LENGTH LEFT
         BP    DIR$NEXT                 MORE IN THIS TAPE BLOCK
         B     IBCPY$LP                 GET NEXT BLOCK FROM TAPE
         SPACE 2
DIR$END  LA    R0,OUTBUFF2+16           GET START PTR FOR COMPARE
         CR    R0,PPTR                  ANY ON THIS LINE?
         BE    STAR$TWO                 NO - DONE
         PRTLN2                         PRINT PARTIAL LINE
         B     STAR$TWO                 CONTINUE PROCESSING
         SPACE 3
MOVE$MEM MVC   0(8,PPTR),0(DPTR)        MOVE NAME TO PRINT LINE
         IC    R1,11(,DPTR)             GET USER DATA LENGTH
         N     R1,=F'31'                STRIP OFF JUNK
         LA    R1,12(R1,R1)             GET LENGTH OF ENTRY
         AR    DPTR,R1                  ADVANCE DIR PTR
         SR    DLEN,R1                  DECR LENGTH LEFT
         SPACE 1
         LA    PPTR,10(,PPTR)           NEXT PRINT LINE POSITION
         CL    PPTR,OB2END              END OF PRINT LINE?
         BNHR  R10                      NO - RETURN
         LR    PPTR,R10                 SAVE RETURN ADDR
         PRTLN2                         PRINT IT
         LR    R10,PPTR                 RESTORE RETURN ADDR
         LA    PPTR,OUTBUFF2+16         RESET PTR
         BR    R10                      RETURN TO CALLER
         EJECT ,                                              09/84 DBC
IEBUPDT2 LA    R0,7
         TM    PARMFLAG,PF@NOMEM        PARM=NOMEM?
         BNO   *+8                      NO
         LA    R0,5                     YES
         NEWPAGE  (R0),2
         BA&XA R10,SET$FULD             SET UP HEADER INFO
         DC    4Y(0)                    FILLER
         MVC   OUTBUFF2+31(27),=C'IS AN IEBUPXXX INPUT STREAM' 6/84 DBC
         MVC   OUTBUFF2+37(8),IEBUPXXX CORRECT RELOADER NAME  06/84 DBC
         MVC   OUTBUFF2+58(55),=C':   A-ADD   C-CHANGE   R-REPLACE   > X
               = SUBORDINATE NAME'                            11/88 SBG
         BA&XA R10,FMU                  'FOLLOWING MEMBERS UNLOADED:'
         SPACE 2
UPDTE$L1 LA    BLEN,80                  BXLE INCR
         LA    DPTR,TAPEBUFF            POINT TO START
         LR    BPTR,DPTR                COPY START ADDR
         A     BPTR,LASTSIZE       POINT PAST END OF BLOCK    09/84 DBC
         SR    BPTR,BLEN                BACK UP FOR BXLE
         SPACE 1
UPDTE$L2 LR    R1,DPTR                  COPY CARD ADDR
         BA&XA R10,TEST$UP              IS THIS ONE?
         B     UPDTE$BX                 NO - SKIP
         SPACE 1
         MVC   0(1,PPTR),UPDT#TYP       MOVE A,R,C
         MVI   1(PPTR),C'-'             SEPARATOR
         CLI   UPDTXFLG,C'>'            SUBORDINATE MEMBER?   11/88 SBG
         BNE   UPDTE$LB                 NO. USE - AS SEPARATOR11/88 SBG
         MVI   1(PPTR),C'>'             SUBORD.  USE >.       11/88 SBG
UPDTE$LB DS    0H                       GO PUT IN MEMBER NAME 11/88 SBG
         MVC   2(8,PPTR),0(R1)          MEMBER NAME
         LA    R1,1(,PPTR)
UPDTE$L3 CLI   0(R1),C','               END OF MEMBER NAME?
         BE    UPDTE$EM                 YES
         CLI   0(R1),C' '               END OF MEMBER NAME?
         BE    UPDTE$EM                 YES
         LA    R1,1(,R1)                INCR SCAN PTR
         B     UPDTE$L3                 CONTINUE
         SPACE 1
UPDTE$EM MVC   0(7,R1),OUTBUFF+1        BLANK OUT GARBAGE
         LA    PPTR,11(,PPTR)           BUMP PRINT LINE PTR
         CL    PPTR,OB2END2             PAST END?
         BNH   UPDTE$BX                 NO - CONTINUE
         PRTLN2                         PRINT THE MEMBERS
         LA    PPTR,OUTBUFF2+16         RESET PTR
         SPACE 1
UPDTE$BX BXLE  DPTR,BLEN,UPDTE$L2       KEEP SCANNING
         TAPIO TCCW#DAT,TM=UPDT$EOF     GET NEXT BLOCK
         B     UPDTE$L1                 GO CHECK IT
         SPACE 2
UPDT$EOF OI    TFLAG1,T1@MVEOF          SET "HIT EOF"
         B     DIR$END                  CLEAN UP
         EJECT ,                                              07/85 DBC
CBT2     DS    0H   INSERT CODE TO DISPLAY MEMBERS FOR CBT973.11/88 SBG
         LA    R0,7                                           11/88 SBG
         TM    PARMFLAG,PF@NOMEM        PARM=NOMEM?           11/88 SBG
         BNO   *+8                      NO                    11/88 SBG
         LA    R0,5                     YES                   11/88 SBG
         NEWPAGE  (R0),2                                      11/88 SBG
         BA&XA R10,SET$FULD             SET UP HEADER INFO    11/88 SBG
         DC    4Y(0)                    FILLER                11/88 SBG
         MVC   OUTBUFF2+31(27),=C'IS A CBT973 COMPRESSED FILE' 1/88 SBG
         MVC   OUTBUFF2+58(55),=C':   A-ADD   C-CHANGE   R-REPLACE   > X
               = SUBORDINATE NAME'                            11/88 SBG
         BA&XA R10,FMU         'FOLLOWING MEMBERS UNLOADED:'  11/88 SBG
         SPACE 2                                              11/88 SBG
*   SAVE REGISTERS USED FOR CBT973 DECOMPRESS ROUTINE.        11/88 SBG
         SPACE 2                                              11/88 SBG
*   **************          CBT973 DECOMPRESS ROUTINE.        11/88 SBG
         SPACE 1                                              11/88 SBG
CBT$D00  STM   R1,R5,CBSTOR01           REGS 1 THRU 5         11/88 SBG
         STM   R8,R10,CBSTOR08          REGS 8 THRU 10        11/88 SBG
         LA    R5,TAPEBUFF     BEGINNING OF BLOCK PROCESSING. 11/88 SBG
         ST    R1,CBSTOR1          USE R1 FOR WORK REG NOW    11/88 SBG
         LH    R1,0(,R5)           GET BLOCK SIZE FROM BDW    11/88 SBG
         ST    R1,CBTBLK1          STORE BLOCK SIZE FOR LATER 11/88 SBG
         AR    R1,R5               POINT PAST E-O-BLOCK       11/88 SBG
         ST    R1,CBTAPEND         REMEMBER E-O-BLOCK ADDRESS 11/88 SBG
         L     R1,CBSTOR1          RESTORE R1.                11/88 SBG
         LA    R5,4(,R5)                POINT TO RDW          11/88 SBG
*                              BEGINNING OF RECORD PROCESSING.11/88 SBG
CBT$D0B  LA    R8,14(,R5)          POINT TO BEGIN OF DATA     11/88 SBG
         ST    R1,CBSTOR1          USE R1 FOR WORK REG NOW    11/88 SBG
         ST    R6,CBSTOR06         USE R6 FOR WORK REG NOW    11/88 SBG
         LH    R1,0(,R5)           STORE RECORD SIZE FROM RDW 07/90 SBG
*   THIS IS NEVER BIGGER THAN 94 -- LET ALONE 255 ||          11/88 SBG
         C     R1,=F'94'           LRECL=94 MAX FOR CBT973    07/90 SBG
         BH    CBT$EOF             BREAK OUT OF LOOP IF BIGGER07/90 SBG
         S     R1,=F'12'           -12 JUST TO GO PAST DATA   11/88 SBG
*                                   AND LAST 2 HEADER BITS    11/88 SBG
         LA    R5,2(,R5)           HALFWAY TO HEADER BITS     11/88 SBG
         MVI   CBTCLEAR,X'40'      MOVE IN BLANK TO ...       11/88 SBG
         MVC   CBTCARD,CBTCLEAR       CLEAR 80-BYTE WORKAREA. 11/88 SBG
         LA    R2,CBTCARD          ADDRESS OF 80-BYTE CARD    11/88 SBG
         LA    R6,5                5 LOOPS TO EXPAND CARD     11/88 SBG
CBT$D0C  EQU   *                   EXPAND 16 BYTES AT A TIME  11/88 SBG
         LA    R4,16               LOOP COUNTER               11/88 SBG
         LA    R5,2(,R5)           BUMP HEADER BIT POINTER    11/88 SBG
         ICM   R3,12,0(R5)         2 BYTES MASK BITS TO SLIDE 11/88 SBG
CBT$D0D  EQU   *                   INNER LOOP TO DO 16 BYTES  11/88 SBG
         ST    R2,CBSTOR2          TEMP SAVE R2               02/93 RT
         SLR   R2,R2               CLEAR TARGET REGISTER      11/88 SBG
         SLDL  R2,1                SLIDE OVER ONE MASK BIT    11/88 SBG
         LTR   R2,R2               IS BIT 0 OR 1 ?            11/88 SBG
         L     R2,CBSTOR2          RESTORE R2                 02/93 RT
         BZ    CBT$D0F             IF 0, NEEDS BLANK TO FILL  11/88 SBG
         MVC   0(1,R2),0(R8)       MOVE THE NON-BLANK         11/88 SBG
         LA    R8,1(,R8)           BUMP DATA PTR TO NEXT BYTE 11/88 SBG
CBT$D0E  EQU   *                   RESTORE THE CARD-IMAGE     11/88 SBG
         LA    R2,1(,R2)           POINT TO NEXT CARD BYTE    11/88 SBG
         BCT   R4,CBT$D0D          RESTORE 16-BYTE GROUP      11/88 SBG
         BCT   R6,CBT$D0C          DO THAT 5 TIMES            11/88 SBG
         AR    R5,R1               BUMP POINTER TO NEXT RDW.  11/88 SBG
         LM    R1,R4,CBSTOR01      REGS 1 THRU 4. 5 HAS PTR   11/88 SBG
         LM    R8,R10,CBSTOR08     REGS 8 THRU 10             11/88 SBG
*        L     R6,CBSTOR06         RESTORE R6. USED FOR PPTR. 11/89 SBG
         B     CBT$L2              ONE CARD IS RESTORED, FOR  11/88 SBG
*                                   PURPOSES OF EXAMINATION.  11/88 SBG
CBT$D0F  EQU   *                   RTN TO INSERT A BLANK      11/88 SBG
         MVI   0(R2),X'40'         MOVE BLANK IN TO DECOMPRESS11/88 SBG
         B     CBT$D0E             CONTINUE RESTORING CARD.   11/88 SBG
         SPACE 1                                              11/88 SBG
*   **************   END OF CBT973 DECOMPRESS ROUTINE.        11/88 SBG
         SPACE 2                                              11/88 SBG
*  AT THIS POINT, A CARD-IMAGE OF DATA IS RESTORED INTO       11/88 SBG
*    THE FIELD, CBTCARD.  WE THEN USE REGULAR IEBUPDTE        11/88 SBG
*    PROCESSING TO LOOK FOR MEMBER NAMES.                     11/88 SBG
         SPACE 2                                              11/88 SBG
CBT$L2   L     PPTR,CBSTOR06       RESTORE REGISTER PPTR      11/88 SBG
         LA    R1,CBTCARD          LOAD CARD ADDRESS          11/88 SBG
         ST    R5,CBSTOR05         SAVE RECD PTR              11/88 SBG
         BA&XA R10,TEST$UP         IS THIS A MEMBER INDICATOR 11/88 SBG
         B     CBT$BX              NO - SKIP TO THE NEXT CARD 11/88 SBG
         L     R5,CBSTOR05         RESTORE RECD PTR           11/88 SBG
         SPACE 1                                              11/88 SBG
         MVC   0(1,PPTR),UPDT#TYP    MOVE A,R,C               11/88 SBG
         MVI   1(PPTR),C'-'             SEPARATOR             11/88 SBG
         CLI   UPDTXFLG,C'>'            SUBORDINATE MEMBER?   11/88 SBG
         BNE   CBT$LB                   NO. USE - AS SEPARATOR11/88 SBG
         MVI   1(PPTR),C'>'             SUBORD.  USE >.       11/88 SBG
CBT$LB   DS    0H                       GO PUT IN MEMBER NAME 11/88 SBG
         MVC   2(8,PPTR),0(R1)          MEMBER NAME           11/88 SBG
         LA    R1,1(,PPTR)                                    11/88 SBG
CBT$L3   CLI   0(R1),C','               END OF MEMBER NAME?   11/88 SBG
         BE    CBT$EM                   YES                   11/88 SBG
         CLI   0(R1),C' '               END OF MEMBER NAME?   11/88 SBG
         BE    CBT$EM                   YES                   11/88 SBG
         LA    R1,1(,R1)                INCR SCAN PTR         11/88 SBG
         B     CBT$L3                   CONTINUE              11/88 SBG
         SPACE 1                                              11/88 SBG
CBT$EM   MVC   0(7,R1),OUTBUFF+1        BLANK OUT GARBAGE     11/88 SBG
         LA    PPTR,11(,PPTR)           BUMP PRINT LINE PTR   11/88 SBG
         CL    PPTR,OB2END2             PAST END?             11/88 SBG
         BNH   CBT$BX                   NO - CONTINUE         11/88 SBG
         ST    R5,CBSTOR05              SAVE RECD PTR         11/88 SBG
         PRTLN2                         PRINT THE MEMBERS     11/88 SBG
         LA    PPTR,OUTBUFF2+16         RESET PTR             11/88 SBG
         SPACE 1                                              11/88 SBG
CBT$BX   L     R5,CBSTOR05              RESTORE RECD PTR      11/88 SBG
         C     R5,CBTAPEND              AT END OF BLOCK YET?  11/88 SBG
         BL    CBT$D0B                  NO. GET NEXT RECORD   11/88 SBG
*    TRY TO RESTORE REGISTERS THAT MIGHT BE USED ELSEWHERE.   11/88 SBG
         TAPIO TCCW#DAT,TM=CBT$EOF      GET NEXT BLOCK        11/88 SBG
         B     CBT$D00                  GO CHECK IT           11/88 SBG
         SPACE 2                                              11/88 SBG
CBT$EOF  OI    TFLAG1,T1@MVEOF          SET "HIT EOF"         11/88 SBG
         B     DIR$END                  CLEAN UP              11/88 SBG
         EJECT ,                                              11/88 SBG
************************************************************* 07/85 DBC
*                                                           * 07/85 DBC
*        THE CURRENT TAPE FILE CONTAINS SMPPTFIN SYSMODS.   * 07/85 DBC
*        SCAN THE FILE TO GET THE NAMES OF THE SYSMODS      * 07/85 DBC
*        CONTAINED THEREIN, AND DISPLAY THEM.               * 07/85 DBC
*                                                           * 07/85 DBC
************************************************************* 07/85 DBC
         SPACE 1                                              07/85 DBC
PTFS2    LA    R0,7                REQUIRED HEADER SPACE      07/85 DBC
         TM    PARMFLAG,PF@NOMEM   SCAN FOR "MEMBERS"?        07/85 DBC
         BNO   *+8                 YES, PROCEED               07/85 DBC
         LA    R0,5                NO, DON'T NEED SO MUCH     07/85 DBC
         NEWPAGE (R0),2            WIDOW PREVENTION           07/85 DBC
         SPACE 1                                              07/85 DBC
         BA&XA R10,SET$FULD        MESSAGE SETUP              07/85 DBC
         DC    4Y(0)                    FILLER                07/85 DBC
         MVC   OUTBUFF2+31(35),=C'IS AN SMPPTFIN SYSMODS INPUT STREAM'
         MVC   OUTBUFF2+66(43),=C':   A-APAR   F-FUNCTION   P-PTF   U-U*
               SERMOD'             DEFINITIONS                07/85 DBC
         BA&XA R10,FMU             DISPLAY THE MESSAGE        07/85 DBC
         SPACE 3                                              07/85 DBC
PTF$L1   LA    BLEN,80             BXLE INCRIMENT             07/85 DBC
         LA    DPTR,TAPEBUFF       BXLE SCANNER               07/85 DBC
         LR    BPTR,DPTR           SET -                      07/85 DBC
         AL    BPTR,LASTSIZE        BXLE-                     07/85 DBC
         SLR   BPTR,BLEN             LIMIT                    07/85 DBC
         SPACE 1                                              07/85 DBC
PTF$L2   LR    R1,DPTR             MESSAGE SINK SCANNER       07/85 DBC
         BA&XA R10,TEST$PT         DOES THE CURRENT CARD NAME 07/85 DBC
*                                  THE START OF A NEW SYSMOD? 07/85 DBC
         B     PTF$BX              +0 NO, SKIP                07/85 DBC
         MVC   0(1,PPTR),PTF#TYPE  +4 YES, GET SYSMOD TYPE    07/85 DBC
         MVI   1(PPTR),C'-'        FRAME                      07/85 DBC
         MVC   2(7,PPTR),0(R1)     SYSMOD NAME (ALWAYS SEVEN  07/85 DBC
*                                  CHARACTERS LONG)           07/85 DBC
         LA    PPTR,10(,PPTR)      ADVANCE SINK SCANNERR      07/85 DBC
         CL    PPTR,OB2END2        IS MSG LINE FULL?          07/85 DBC
         BNH   PTF$BX              NOT YET; GOO LOOP          07/85 DBC
         PRTLN2 ,                  YES, PRINT THE LINE        07/85 DBC
         LA    PPTR,OUTBUFF2+16    RESET SINK SCANNER         07/85 DBC
PTF$BX   BXLE  DPTR,BLEN,PTF$L2    CONTINUE SCANNING THE BLK  07/85 DBC
         SPACE 1                                              07/85 DBC
         TAPIO TCCW#DAT,TM=PTF$EOF READ NEXT BLOCK            07/85 DBC
         B     PTF$L1              LOOP TO SCAN IT            07/85 DBC
         SPACE 1                                              07/85 DBC
PTF$EOF  OI    TFLAG1,T1@MVEOF     EOF; REMEMBER              07/85 DBC
         B     DIR$END             GO DISPLAY FILE            07/85 DBC
*                                  DESCRIPTION MESSAGES       07/85 DBC
         EJECT ,                                              07/85 DBC
IEBISAM2 BA&XA R10,SET$FULL             SET UP HEADER
         DC    Y(4+26)                  DSORG OFFSET
         DC    Y(4+36)                  RECFM OFFSET
         DC    Y(4+62)                  BLKSIZE OFFSET
         DC    Y(4+2+82)                LRECL OFFSET
         SPACE 1
         MVC   OUTBUFF2+47(7),IEBISAM   WHO IT WAS UNLOADED BY
         SPACE 1
         ICALL CONVUNIT,R10,R1=TAPEBUFF+4+17-3  CONVERT UNIT TYPE
         SPACE 1
         PRTLN2                         PRINT SECOND INFO LINE
         B     STAR$TWO
         EJECT ,                                              09/84 DBC
FDR2     NEWPAGE 9,2                    CHECK FOR ROOM ON PAGE
         BA&XA R10,SET$FULD             SET UP HEADER INFO
         DC    4Y(0)                    FILLER
         MVC   OUTBUFF2+31(14),=C'WAS DUMPED BY ' SET UP UNLOADER
         MVC   OUTBUFF2+45(6),UNLOADER            MESSAGE
         CLC   FDRSAVE,=C'DUMMYDSF'     NORMAL DUMP?
         BE    *+10                     YES
*                                       NO  - WARN USER
         MVC   OUTBUFF2+54(22),=C' (ABSOLUTE TRACK DUMP)'
         PRTLN2                         PRINT THE HEADER
         TM    PARMFLAG,PF@NOMEM        DETAILS WANTED?
         BO    FDR2NODT                 NO
*                                       YES - CHECK DUMP TYPE
         CLC   FDRSAVE,=C'DUMMYDSF'     WAS IT A DSF'ABLE DUMP?
         BNE   FDR2ABS                  NO
*                                       YES - ISSUE MESSAGE
         MVI   OUTBUFF2,C'0'
         MVC   OUTBUFF2+16(27),=C'FOLLOWING DATA SETS DUMPED:'
         PRTLN2
         ST    R15,CBSAVE15             SAVE REG 15           11/88 SBG
         L     R15,=A(DSFHDING)         ADDRESSABILITY TO VAR 11/88 SBG
         MVC   OUTBUFF2(L'DSFHDING),0(R15)     COLUMN HEADING 11/88 SBG
         L     R15,CBSAVE15             RESTORE REG 15        11/88 SBG
         PRTLN2
FDR2LOOP CLC   =C'DUMMYDSF',TAPEBUFF+12 IS THIS A DSCB BLOCK?
         BNE   FDR2VOL                  NO  - TRY FOR VOLSER
*                                       YES
         LA    R5,TAPEBUFF+40           POINT TO FIRST DSCB
         LH    R6,TAPEBUFF+30           GET DSCB COUNT THIS BLOCK
FDR2DSCB LTR   R6,R6                    MORE DSCB'S TO GO?
         BNP   FDR2NBLK                 NO  - GET NEXT BLOCK
*                                       YES
         CLI   44(R5),C'1'              FORMAT 1 DSCB?
         BNE   FDR2NDSC                 NO
*                                       YES
         CLC   LCTR2,=F'1'              ROOM ON PAGE FOR NEXT LINE?
         BH    FDR2DSCC                 NO
         NEWPAGE ,2                     FORCE EJECT
         ST    R15,CBSAVE15             SAVE REG 15           11/88 SBG
         L     R15,=A(DSFHDING)         ADDRESSABILITY TO VAR 11/88 SBG
         MVC   OUTBUFF2(L'DSFHDING),0(R15)     COLUMN HEADING 11/88 SBG
         L     R15,CBSAVE15             RESTORE REG 15        11/88 SBG
         PRTLN2                         PRINT THEM
FDR2DSCC MVC   OUTBUFF2+DSFDSN(44),0(R5)   MOVE IN DSNAME
         LH    R14,98(,R5)                 GET LAST TRACK USED
         LA    R14,1(,R14)                 MAKE 1 RELATIVE
         STH   R14,98(,R5)                 SAVE IT BACK
         ICALL CONVHALF,R14,R1=98(,R5),R15=OUTBUFF2+DSFTRK
         ICALL CNVRECFX,R14,R1=84(,R5),R15=OUTBUFF2+DSFRECFM
         ICALL CONVHALF,R14,R1=88(,R5),R15=OUTBUFF2+DSFLRECL-1
         ICALL CONVHALF,R14,R1=86(,R5),R15=OUTBUFF2+DSFBLKSZ+1
         ICALL CNVDSORX,R14,R1=82(,R5),R15=OUTBUFF2+DSFDSORG
         ICALL CONVDATE,R10,R1=53(,R5),R15=OUTBUFF2+DSFCDATE
         ICALL CONVDATE,R10,R1=56(,R5),R15=OUTBUFF2+DSFEDATE
         TM    93(R5),X'40'            RACF DEFINED?
         BZ    *+10                    NO
         MVC   OUTBUFF2+DSFSECUR+5(4),=C'RACF'   YES
         TM    93(R5),X'10'            PASSWORD?
         BZ    FDR2PRT                 NO
         MVC   OUTBUFF2+DSFSECUR(4),=C'WPWD'  YES - ASSUME WPWD
         TM    93(R5),X'04'            READ PSWD REQUIRED?
         BO    *+8                     NO
         MVI   OUTBUFF2+DSFSECUR,C'R'  YES
         TM    93(R5),X'50'            BOTH RACF/PSWD?
         BNO   FDR2PRT                 NO
         MVI   OUTBUFF2+DSFSECUR+4,C'/'  YES
FDR2PRT  PRTLN2                        PRINT THE LINE
FDR2NDSC LA    R5,148(,R5)             BUMP PAST DSCB/COUNT FIELD
         S     R6,=F'1'
         B     FDR2DSCB
FDR2NBLK TAPIO TCCW#DAT,TM=FDR2EOF     GET NEXT BLOCK
         B     FDR2LOOP
FDR2EOF  MVI   OUTBUFF2,C'0'
         OI    TFLAG1,T1@MVEOF         REMEMBER WE HIT EOF
       MVC  OUTBUFF2+12(63),=C'0***LISTING TERMINATED. FDR FILE NOT COM$
               PLETE ON THIS VOLUME***'
         PRTLN2
FDR2FAKE MVC   TAPEBUFF+4(4),=X'FFFFFFFF'   FAKE OUT FDR2VOL
FDR2VOL  MVI   OUTBUFF2,C'0'
         MVC   OUTBUFF2+8(20),=C'DUMPED FROM A UUUUUU'
         MVC   FDRDVEND(1),DASDRSAV+4   SET UP FENCE FOR DEV TYPE
         LA    R1,FDRDVTAB              POINT TO FDR DEV TYPE TABLE
FDR2VOLA CLC   DASDRSAV+4(1),0(R1)      MATCH?
         BE    FDR2VOLB                 YES
         LA    R1,7(,R1)                NO  - BUMP TO NEXT ENTRY
         B     FDR2VOLA
FDR2VOLB MVC   OUTBUFF2+22(6),1(R1)     PUT DEV TYPE IN MESSAGE
         MVC   OUTBUFF2+28(16),=C'         VOLSER='
         CLC   UNLOADER(6),FDRDSF       WAS THIS DSF?
         BE    FDR2VOLM                 YES - NO VOLSER AVAILABLE
         CLC   =XL4'00',TAPEBUFF+4      DOES DUMP INCLUDE CYL/TRK 0/0?
         BNE   FDR2VOLM                 NO  - NO VOLSER AVAILABLE
*                                       YES
         LA    R1,TAPEBUFF+20           POINT TO FIRST COUNT FIELD
         LA    R0,TAPEBUFF              POINT TO 2ND TRACK IN BLOCK
         AH    R0,TAPEBUFF+2
FDR2VOLC CLC   0(5,R1),=X'0000000003'   POINTING TO REC 3 ON TRK 0?
         BE    FDR2VOLG                 YES
         BH    FDR2VOLM                 NO - AND NO VOLSER AVAIL
         SR    R15,R15
         IC    R15,5(,R1)               GET KEY LENGTH
         AH    R15,6(,R1)               ADD IN DATA LENGTH
         LA    R1,8(R15,R1)             POINT TO NEXT RECORD
         CR    R1,R0                    PAST TRACK 0?
         BNL   FDR2VOLM                 YES
         B     FDR2VOLC                 NO  - KEEP GOING
FDR2VOLG CLC   8(8,R1),=C'VOL1VOL1'     GOT VOL LABEL?
         BNE   FDR2VOLM                 NO  - STRANGE
         MVC   OUTBUFF2+44(6),16(R1)    YES - MOVE IT INTO MSG
         B     FDR2VOLP
FDR2VOLM MVC   OUTBUFF2+43(14),=C' NOT AVAILABLE'
FDR2VOLP PRTLN2
         B     STAR$TWO
FDR2ABS  MVI   OUTBUFF2,C'0'
         MVC   OUTBUFF2+16(63),=C'ABSOLUTE TRACK DETAILS NOT AVAILABLE $
               IN THIS VERSION OF TAPEMAP'
         PRTLN2
         B     FDR2FAKE
FDR2NODT CLC   =C'DUMMYDSF',TAPEBUFF+12  DSCB BLOCK?
         BNE   FDR2VOL                   NO  - CHECK FOR VOLSER
         TAPIO TCCW#DAT,TM=FDR2EOF       YES - CHECK NEXT BLOCK
         B     FDR2NODT
         SPACE 3
SLK2     NEWPAGE 9,2                    CHECK FOR ROOM ON PAGE
         BA&XA R10,SET$FULD             SET UP HEADER INFO
         DC    4Y(0)                    FILLER
         MVC   OUTBUFF2+31(16),=C'WAS UNLOADED BY ' SET UP UNLOADER
         MVC   OUTBUFF2+47(8),UNLOADER            MESSAGE
         PRTLN2                         PRINT THE HEADER
         MVC   OUTBUFF2+8(30),=C'SLICK LIBRARY BACKUP, CYCLE = '
         MVC   OUTBUFF2+38(1),DASDRSAV  PUT IN THE CYCLE NUMBER
         MVC   OUTBUFF2+39(12),=C', MAXSIZE = '
         ICALL CONVHALF,R14,R1=DASDRSAV+2,R15=OUTBUFF2+41
         PRTLN2                         PRINT FIRST INFO LINE
         TM    PARMFLAG,PF@NOMEM        DETAILS WANTED?
         BO    STAR$TWO                 NO
*                                       YES
         MVI   OUTBUFF2,C'0'
         MVC   OUTBUFF2+16(27),=C'FOLLOWING MEMBERS UNLOADED:'
         PRTLN2
         L     R15,=A(SLKHDING)         ADDRESSABILITY TO VAR 11/88 SBG
         MVC   OUTBUFF2(L'SLKHDING),0(R15)     COLUMN HEADING 11/88 SBG
         PRTLN2
SLK2LOOP OC    TAPEBUFF(2),TAPEBUFF     IS THIS A HEADER BLOCK?
         BNZ   SLK2NBLK                 NO  - TRY NEXT BLOCK
*                                       YES
         CLC   LCTR2,=F'1'              ROOM ON PAGE FOR NEXT LINE?
         BH    SLK2MGO                  NO
         NEWPAGE ,2                     FORCE EJECT
         ST    R15,CBSAVE15             SAVE REG 15           11/88 SBG
         L     R15,=A(SLKHDING)         ADDRESSABILITY TO VAR 11/88 SBG
         MVC   OUTBUFF2(L'SLKHDING),0(R15)     COLUMN HEADING 11/88 SBG
         L     R15,CBSAVE15             RESTORE REG 15        11/88 SBG
         PRTLN2                         PRINT THEM
SLK2MGO  MVC   OUTBUFF2+SLKMEM(10),TAPEBUFF+8   MEMBER NAME
         MVI   OUTBUFF2+SLKSTAT-1,C'.'          .
         MVC   OUTBUFF2+SLKSTAT(4),TAPEBUFF+18  STATUS
         ICALL CONVP2,R14,R1=TAPEBUFF+32,       REVISION               X
               R15=OUTBUFF2+SLKREV
         MVC   OUTBUFF2+SLKINFO(8),TAPEBUFF+34  INFO
         MVC   OUTBUFF2+SLKTYPE(8),TAPEBUFF+42  TYPE
         ICALL CONVP4,R14,R1=TAPEBUFF+58,       ACTST                  X
               R15=OUTBUFF2+SLKACTST
         ICALL CONVP4,R14,R1=TAPEBUFF+62,       DELST                  X
               R15=OUTBUFF2+SLKDELST
         ICALL CONVP3,R14,R1=TAPEBUFF+66,       BLOCKS                 X
               R15=OUTBUFF2+SLKBLKS
         ICALL CONVMDY,R14,R1=TAPEBUFF+74,      C-DATE                 X
               R15=OUTBUFF2+SLKCREAT
         ICALL CONVMDY,R14,R1=TAPEBUFF+122,     U-DATE                 X
               R15=OUTBUFF2+SLKUPD
         PRTLN2
SLK2NBLK TAPIO TCCW#DAT,TM=SLK2EOF         GET NEXT BLOCK
         B     SLK2LOOP
SLK2EOF  OI    TFLAG1,T1@MVEOF             SET "HIT EOF"
         B     STAR$TWO
         EJECT ,                                              09/84 DBC
MAP2     NEWPAGE 9,2                    CHECK FOR ROOM ON PAGE
         BA&XA R10,SET$FULD             SET UP HEADER INFO
         DC    4Y(0)                    FILLER
         MVC   OUTBUFF2+31(49),=C'CONTAINS THE FOLLOWING USER-SUPPLIED $
               INFORMATION:'
         PRTLN2                         PRINT THE HEADER INFO
** USE R5 TO POINT TO INFO TO BE PRINTED,
** AND R6 TO CONTROL DEBLOCKING LOOP
         LA    R5,TAPEBUFF+80           SKIP FILE HEADER INFO
         L     R6,LASTSIZE                                    09/84 DBC
         SH    R6,=H'80'
         MVI   OUTBUFF2,C'0'            DOUBLE SPACE FIRST LINE
         B     MAPLOOPX                 BRANCH TO MIDDLE OF LOOP
MAPLOOP  LA    R5,80(,R5)               GET TO NEXT INFO LOGICAL RECORD
         SH    R6,=H'80'                CHECK FOR END OF BLOCK
MAPLOOPX BNP   MAPNEXT                  GET NEXT BLOCK IF NECESSARY
MAPLOOPY MVC   OUTBUFF2+16(72),0(R5)    PRINT 72 CHARS OF INFO
         PRTLN2
         B     MAPLOOP                  GET NEXT LOGICAL RECORD
MAPNEXT  TAPIO TCCW#DAT,TM=MAPEOF       READ NEXT BLOCK
         L     R1,LASTSIZE         VERIFY BLOCK MULT OF 80 BY 09/84 DBC
         SR    R0,R0
         LA    R2,80
         DR    R0,R2
         LTR   R0,R0
         BNZ   MAPERR                   NO  - ERROR
         LA    R5,TAPEBUFF              RESET LOOP REGS
         L     R6,LASTSIZE                                    09/84 DBC
         B     MAPLOOPY
MAPERR   MVC   OUTBUFF2+12(53),=C'*** LISTING TERMINATED, INVALID BLOCK$
                ENCOUNTERED ***'
         PRTLN2
         B     STAR$TWO
MAPEOF   OI    TFLAG1,T1@MVEOF          REMEMBER EOF FOUND
****     B     STAR$TWO
         EJECT ,                                              09/84 DBC
STAR$TWO MVC   OUTBUFF2(2),=C'0*'
         MVC   OUTBUFF2+2(L'OUTBUFF2-2),OUTBUFF2+1
         PRTLN2
         TM    TFLAG1,T1@MVEOF          ALREADY HIT EOF?
         BO    CHKMVEOF                 YES - SEE WHERE TO GO
         TM    TFLAG1,T1@SL             SL TAPE?
         BO    FIND$EOF                 YES - CONTINUE PROCESSING
         B     NL$LOOP                  SCAN THRU REST OF FILE
         SPACE 1
CHKMVEOF NI    TFLAG1,255-T1@MVEOF      RESET FLAG
         TM    TFLAG1,T1@SL             SL TAPE?
         BO    SL$SAVE                  YES - GO READ THE EOF LABELS
         B     NL$EOF                   NO
         SPACE 3
FMU      PRTLN2                         PRINT INFO LINE
FMU$     TM    PARMFLAG,PF@NOMEM        WANT MEMBER LISTING?
         BO    STAR$TWO                 NO - DONE
         MVI   OUTBUFF2,C'0'            SET CC
         MVC   OUTBUFF2+16(27),=C'FOLLOWING MEMBERS UNLOADED:'
         PRTLN2                         PRINT HEADER
         MVI   OUTBUFF2,C'0'            DOUBLE SPACE FIRST MEMBER LINE
         LA    PPTR,OUTBUFF2+16         INIT PRINT LINE PTR
         BR    R10                      RETURN TO CALLER
         SPACE 3
SET$FULL MVC   OUTBUFF2+54(17),=C'...  WAS:  DSORG='
         ICALL CNVDSORG,R14,R15=OUTBUFF2+71  CONVERT DSORG
         SPACE 1
         MVC   OUTBUFF2+76(6),=C'LRECL='
         LA    R1,TAPEBUFF
         AH    R1,6(,R10)               GET LRECL ADDR
         ICALL CONVHALF,R14,R15=OUTBUFF2+82  CONVERT LRECL
         BA&XA R14,SLIDE                MOVE IT LEFT
         SPACE 1
         MVC   OUTBUFF2+90(6),=C'RECFM='
         ICALL CNVRECFM,R14,R15=OUTBUFF2+96  CONVERT RECFM
         SPACE 1
         MVC   OUTBUFF2+104(8),=C'BLKSIZE='
         LA    R1,TAPEBUFF
         AH    R1,4(,R10)               GET BLKSIZE ADDR
         ICALL CONVHALF,R14,R15=OUTBUFF2+112 CONVERT BLKSIZE
         BA&XA R14,SLIDE                MOVE IT LEFT
         SPACE 2
SET$FULD MVI   OUTBUFF2,C'-'            SET CC
         MVC   OUTBUFF2+1(17),FL1ID     MOVE IN (TAPE) DSN
         MVC   OUTBUFF2+19(5),=C'(FILE'
         MVC   OUTBUFF2+25(4),FL1FILSQ  MOVE IN FILE SEQ#
         MVC   OUTBUFF2+29(17),=C') WAS UNLOADED BY'
         B     8(,R10)                  RETURN TO CALLER
         SPACE 3
BOOM     STM   R0,R15,BOOMREGS          SAVE ALL REGS
         MVC   OUTBUFF2(50),=C'0*** TAPEMAP LOGIC ERROR - CONTACT TEC S$
               UPPORT ***'                                          WBF
         PRTLN2                         PRINT ERROR MSG
         TM    PARMFLAG,PF@TEST         ABEND?
         BNO   STAR$TWO                 NO - IGNORE REST OF FILE
         SPACE 1
         CLOSE MF=(E,OPENMFL)           CLOSE SYSPRINT
         IFP2  N,BOOM2                  SKIP IF NOT OPEN
         CLOSE MF=(E,OPENMFL2)          CLOSE SYSPRNT2
         SPACE 1
BOOM2    LA    R14,BOOMREGS             POINT TO REG SAVE AREA
         EX    0,*                      AND AWAY WE GO
         EJECT
***********************************************************************
*                                                                     *
*  EXIT THE PROGRAM                                                   *
*                                                                     *
***********************************************************************
LEAVE    DS    0H                                              1/93 RT
         LR    R1,R13                                          1/93 RT
         L     R13,4(,R13)                                     1/93 RT
         L     R0,GETM                                         1/93 RT
         FREEMAIN R,A=(1),LV=(0)                               1/93 RT
         MVI   12(R13),X'FF'        INVALIDATE FWD-CHAIN       1/93 RT
         RETURN (14,12),RC=0                                   1/93 RT
         EJECT                                                 1/93 RT
***********************************************************************
*                                                                     *
*  SUBROUTINE TO DO I/O TO TAPE                                       *
*                                                                     *
***********************************************************************
TAPIO    ST    R0,TAPEIOB+16            SET CCW ADDR
         EXCP  TAPEIOB
         WAIT  ECB=TAPEECB
         CLI   TAPEECB,X'7F'            DID IT WORK?
         BE    TAPIO$OK                 YES - CONTINUE
         CLI   TAPEECB,X'41'            CHANNEL PROGRAM ERROR?
         BNE   TIONTMK             NO, NOT TAPEMARK           07/85 DBC
         CLI   IOBCSW+4,X'0D'      YES, CHEND, DEVEND, UNITEXCPT?
         BNE   TIONTMK             NO, NOT FILE MARK          07/85 DBC
         LA    R0,1                YES, GET FILE INCRIMENT    07/85 DBC
         AH    R0,FILE#SEQ         INCR TAPEMARK COUNTER      07/85 DBC
         STH   R0,FILE#SEQ         STORE NEW VALUE            07/85 DBC
         BR    R10                 RETURN TO CALLER           07/85 DBC
TIONTMK  DS    0H                                             07/85 DBC
         SPACE 1                                              07/85 DBC
         ST    R10,LKRTAPIO             SAVE R10 FOR RECURSION      U16
         CH    LCTR,=H'&LINEPPG'        ANYTHING ON PAGE? U13 11/88 SBG
         BL    *+8                      YES - CONTINUE              U13
         NEWPAGE  ,                     PRINT THE TITLE             U13
         MVC   OUTBUFF(44),=C'-*** TAPE I/O ERROR *** ECB CODE=XX *** C$
               SW='                                                 U13
         HEX   OUTBUFF+33,TAPEECB,LEN=1                             U13
         HEX   OUTBUFF+44,IOBCSW+1,LEN=7                            U13
         MVC   OUTBUFF+59(24),=C'*** IOBSENSE0,1=XXXX ***'          U13
         HEX   OUTBUFF+75,TAPEIOB+2,LEN=2                           U13
         PRTLN OUTBUFF
         SPACE 1
**       L     R1,DD#PTR                -> DD SECTION OF TIOT       U13
**       SH    R1,=H'24'                -> TIOT                     U13
**       AH    R1,TAPEDCB+40            -> DD ENTRY FOR TAPE        U13
**       L     R1,16(,R1)               -> UCB                      U13
         L     R1,TAPEMFL               -> DCB                      U13
         L     R1,44(,R1)               -> DEB                      U13
         L     R1,32(,R1)               -> UCB                      U13
         L     R2,20(,R1)               -> EXTENDED SENSE INFO      U13
         MVC   OUTBUFF(16),=C' *** SENSE DATA='                     U13
         HEX   OUTBUFF+16,(00,R2),LEN=4                             U13
         HEX   OUTBUFF+25,(04,R2),LEN=4                             U13
         HEX   OUTBUFF+35,(08,R2),LEN=4                             U13
         HEX   OUTBUFF+44,(12,R2),LEN=4                             U13
         HEX   OUTBUFF+54,(16,R2),LEN=4                             U13
         HEX   OUTBUFF+63,(20,R2),LEN=4                             U13
         PRTLN OUTBUFF                  PRINT THE SENSE DATA        U13
         SPACE 3
         NEWPAGE  10                    NEXT PAGE IF NEED ROOM      U15
         MVC  OUTBUFF(35),=C'0    BITS SET ARE FLAGGED ''*'' BELOW' U15
         PRTLN OUTBUFF                  PRINT HEADER                U15
         MVI   OUTBUFF,C'0'             DOUBLE SPACE FIRST ONE      U15
         SPACE 2
*  R2 -> SENSE DATA                                                 U15
         LA    R3,10                    NUMBER OF BYTES             U15
         L     R4,=A(ERR#LIST)          -> ERROR MESSAGES           U15
         SPACE 1
NEXTBYTE LA    R6,OUTBUFF+5             -> LINE POS                 U15
         LA    R1,X'80'                 MASK BIT                    U15
         SPACE 1
TEST$BIT MVC   2(10,R6),0(R4)           MOVE TEXT                   U15
         EX    R1,BIT$TM                IS THIS BIT SET?            U15
         BZ    *+8                      NO - SKIP                   U15
         MVI   0(R6),C'*'               FLAG THIS ONE               U15
         SPACE 1
         LA    R4,10(,R4)               -> NEXT MSG                 U15
         LA    R6,16(,R6)               -> NEXT PRINT LINE POS      U15
         SRA   R1,1                     MOVE BIT RIGHT              U15
         BNZ   TEST$BIT                 CONTINUE WITH THIS BYTE     U15
         PRTLN OUTBUFF                  PRINT DESC OF THIS BYTE     U15
         LA    R2,1(,R2)                -> NEXT BYTE OF SENSE DATA  U15
         BCT   R3,NEXTBYTE              CONTINUE IF MORE            U15
         SPACE 3
         CLI   WHERE,3                  READING DATA?
         BNE   CLOSE$TP                 NO - CLOSE UP AND LEAVE
         L     R1,TAPEIOB+16            GET CCW ADDR
         CLI   0(R1),RD                 WAS IT A READ?
         BNE   CLOSE$TP                 NO - CLOSE UP AND LEAVE
         LA    R0,TCCW#FSF              SKIP REST OF DATA...
         L     R10,LKRTAPIO             RESTORE R10                 U16
         B     TAPIO                    START AT TOP OF THIS SUBRTN
         SPACE 2
BIT$TM   TM    0(R2),*-*                << EXECUTED >>              U15
         SPACE 3                                              07/85 DBC
TAPIO$OK LA    R0,TCCW#FSF         WAS THIS A -               07/85 DBC
         CLM   R0,7,TAPEIOB+17      FORWARD SKIP FILE CMD?    07/85 DBC
         BNE   TIOSAMFL            NO, STILL IN SAME FILE     07/85 DBC
         LA    R0,1                YES, GET FILE INCRIMENT    07/85 DBC
         AH    R0,FILE#SEQ         GET NEW FILE# -1           07/85 DBC
         STH   R0,FILE#SEQ         STORE BACK                 07/85 DBC
TIOSAMFL DS    0H                                             07/85 DBC
         SPACE 1                                              07/85 DBC
         L     R0,=A(BUFFSIZE)          GET LENGTH REQUESTED  07/85 DBC
         MVC   DWD+2(2),IOBCSW+6        COPY LENGTH LEFT
         XC    DWD(2),DWD               CLEAR HIGH 2 BYTES
         S     R0,DWD                   MINUS LENGTH UNUSED
         ST    R0,LASTSIZE         EQUALS SIZE READ           09/84 DBC
         TM    TFLAG1,T1@ANLZ           ANALYZING?
         BNO   4(,R10)                  NO - SKIP THIS JUNK
         A     R0,BYTECNT               ADD PREVIOUS BYTE COUNT
         ST    R0,BYTECNT               SAVE NEW TOTAL
         L     R1,BLOCKCNT              GET BLOCK COUNT
         LA    R1,1(,R1)                INCR
         ST    R1,BLOCKCNT              SAVE NEW COUNT
         CLC   LASTSIZE+2(2),TAPEBUFF START WITH BLKSIZE?     09/84 DBC
         BE    *+8                      YES - COULD BE RECFM=V
         NI    NLFLAGS,255-NL@V         NO - NOT RECFM=V
         CLC   LASTSIZE,MAXBLKSI        SAME SIZE?
         BE    4(,R10)                  YES - DONE HERE
         BH    TAPIO$H                  LAST BLOCK IS BIGGEST
         TM    NLFLAGS,NL@CHANG         ALREADY CHANGED?
         BO    TAPIO$NL                 YES - TURN OFF RECFM=F
         OI    NLFLAGS,NL@CHANG         SET "CHANGED"
         B     4(,R10)                  RETURN
         SPACE 1
TAPIO$H  OI    NLFLAGS,NL@CHANG         SET "CHANGED"
         MVC   MAXBLKSI,LASTSIZE        SAVE NEW BIGGEST BLKSIZE
TAPIO$NL NI    NLFLAGS,255-NL@F         NOT RECFM=F
         B     4(,R10)                  RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*  SUBROUTINE TO HANDLE PRINT OUTPUT                                  *
*                                                                     *
***********************************************************************
NEWPAGE  CR    LCTR,R0                  ENOUGH LINES LEFT?
         BNLR  R10                      YES - JUST RETURN
         LA    LCTR,LINESPP-2           RESET LINE COUNTER
         PRTLN TITLE1
         TM    TFLAG1,T1@PAGE1          IS THIS FIRST PAGE OF VOL?
         BNO   NEWPAG$2                 NO - SKIP THIS STUFF.
         NI    TFLAG1,255-T1@PAGE1      TURN OFF FLAG
         MVC   TITLE2+1(28),TITLE2      BLANK IT OUT
         SPACE 1
NEWPAG$3 TM    TFLAG1,T1@PARM           HAVE PARM FIELD?
         BNO   NEWPAG$4                 NO - SKIP
         TM    TFLAG2,T2@LPARM          IS IT TOO LONG?
         BO    NEWPAG$4                 YES - SKIP
         MVC   TITLE2+1(28),PARM#MSG+1  MOVE IN THE PARM FIELD
         SPACE 1
NEWPAG$4 PRTLN TITLE2                   PRINT AUTHOR LINE
         SPACE 1
NEWPAG$1 TM    TFLAG1,T1@PARM           HAVE PARM FIELD?
         BNO   NEWPAG$2                 NO - DON'T PRINT IT
         TM    TFLAG2,T2@LPARM          LONG PARM FIELD?
         BNO   NEWPAG$5                 NO - ALREADY PRINTED        U15
         PRTLN PARM#MSG                 PRINT IT
NEWPAG$5 TM    TFLAG1,T1@PERR           ERROR IN PARM FIELD?
         BNO   NEWPAG$2                 NO - DON'T PRINT IT
         PRTLN PARMERR                  PRINT IT
         SPACE 1
NEWPAG$2 LA    R0,DASHES
         BA&XA R14,PUTPRTLN             SEPARATOR LINE
         PRTLN COLHEAD1,I
         PRTLN COLHEAD2,I
         LA    R0,DASHES
         BA&XA R14,PUTPRTLN             SEPARATOR LINE
         MVI   OUTBUFF,C'0'             DOUBLE SPACE THE FIRST FILE U15
         BCTR  LCTR,0                   ADJUST THE LINE COUNT       U15
         TM    TFLAG2,T2@RQVOL          WRONG VOLUME MOUNTED?       U15
         BNOR  R10                      NO - ALL DONE               U15
         NI    TFLAG2,255-T2@RQVOL      TURN OFF FLAG NOW           U15
         MVI   OUTBUFF,C'-'             TRIPLE SPACE                U15
         MVI   OUTBUFF+41,C'*'          START OF BOX                U15
         MVC   OUTBUFF+42(49),OUTBUFF+41  REST OF IT                U15
         PRTLN OUTBUFF                  PRINT TOP OF BOX            U15
         MVC   OUTBUFF+41(50),=C'* ID OF MOUNTED VOLUME DIFFERS FROM ID$
                REQUESTED *'            MOVE IN MSG                 U15
         PRTLN OUTBUFF                  PRINT MSG                   U15
         SH    LCTR,=H'2'               ACCOUNT FOR TRIPLE SPACE    U15
         MVI   OUTBUFF+41,C'*'          START OF BOX                U15
         MVC   OUTBUFF+42(49),OUTBUFF+41  REST OF IT                U15
         PRTLN OUTBUFF                  PRINT BOTTOM OF BOX         U15
         MVI   OUTBUFF,C'0'             DOUBLE SPACE FIRST FILE     U15
         BR    R10                      RETURN
         SPACE 3
PRTLN    ST    R14,PRT#SAVE             SAVE RETURN ADDR
         LA    R0,OUTBUFF               POINT TO OUTPUT LINE
         BA&XA R14,PUTPRTLN             WRITE OUTPUT LINE
         L     R14,PRT#SAVE             RESTORE RETURN ADDR
         MVC   OUTBUFF,OUTCLEAR         CLEAR OUTPUT LINE
         BCTR  LCTR,R14                 RETURN IF MORE LINES LEFT
         ST    R14,R14SAVE              SAVE MY R14
         ST    R10,R10SAVE              SAVE SOMEONE'S R10
         NEWPAGE ,                      DO A NEW PAGE
         L     R14,R14SAVE              RESTORE MY R14
         L     R10,R10SAVE              RESTORE SOMEONE'S R10
         BR    R14                      RETURN TO CALLER
         SPACE 2
PUTPRTLN L     R1,OPENMFL               GET DCB ADDR
         PUT   (1),(0)                  WRITE OUTPUT LINE
         ORG   *-2                      BACK OVER BALR
         BR    R15                      MAKE HIM RETURN TO MY CALLER
         SPACE 3
************************************************************* 12/88 SBG
* A BAL TO NEWPAGE2+4 IS DONE IN SEVERAL PLACES.            * 12/88 SBG
* THE CONDITION CODE AT THOSE POINTS IS NOT SET AT LABEL    * 12/88 SBG
* NEWPAGE2, BUT RATHER, JUST ABOVE THOSE BAL INSTRUCTIONS.  * 12/88 SBG
* IT IS THEREFORE IMPORTANT THAT THE INSTRUCTION JUST BELOW * 12/88 SBG
* LABEL NEWPAGE2 HAVE THE CORRECT CONDITION SETTINGS.       * 12/88 SBG
************************************************************* 12/88 SBG
NEWPAGE2 C     R0,LCTR2                 ENOUGH LINES LEFT?    12/88 SBG
         BLR   R10                      YES - JUST RETURN     12/88 SBG
         SPACE 1
         LA    R0,TITLE1                GET RECORD ADDR
         BA&XA R14,PUTPRNT2+4           WRITE IT OUT
         LA    R0,DASHES
         BA&XA R14,PUTPRNT2+4           WRITE IT OUT
         LA    R14,LINESPP-3            RESET LINE COUNTER    02/93 RT
         ST    R14,LCTR2                                      02/93 RT
         BR    R10                      RETURN TO CALLER
         SPACE 3
PRTLN2   ST    R14,PRT#SAVE             SAVE RETURN ADDR
         LA    R1,3                     ASSUME TRIPLE SPACE
         CLI   OUTBUFF2,C'-'            IS IT?
         BE    P2$OK                    YES
         BCTR  R1,0                     ASSUME DOUBLE SPACE
         CLI   OUTBUFF2,C'0'            IS IT?
         BE    P2$OK                    YES
         BCTR  R1,0                     ASSUME SINGLE SPACE
         CLI   OUTBUFF2,C' '            IS IT?
         BE    P2$OK                    YES
         MVI   OUTBUFF2,C' '            MAKE A BLANK
         SPACE 1
P2$OK    L     R14,LCTR2                DECR LINE COUNT       02/93 RT
         SR    R14,R1                                         02/93 RT
         ST    R14,LCTR2                                      02/93 RT
         BP    P2$GO                    CONTINUE IF OK
         NEWPAGE  ,2                    SET NEW PAGE
         MVI   OUTBUFF2,C'-'            TRIPLE SPACE FIRST LINE     U13
         L     R14,LCTR2                ACCOUNT FOR IT        02/93 RT
         SH    R14,=H'3'                                      02/93 RT
         ST    R14,LCTR2                                      02/93 RT
         SPACE 1
P2$GO    BA&XA R14,PUTPRNT2             WRITE OUTPUT LINE
         MVC   OUTBUFF2,OUTCLR2         CLEAR OUTPUT LINE
         L     R14,PRT#SAVE             GET RETURN ADDR
         BR    R14                      RETURN TO CALLER
         SPACE 3
PUTPRNT2 LA    R0,OUTBUFF2              GET RECORD ADDR
         L     R1,OPENMFL2              GET SYSPRNT2 DCB ADDR
         PUT   (1),(0)                  WRITE IT OUT
         ORG   *-2                      BACK OVER BALR
         BR    R15                      MAKE HIM RETURN TO MY CALLER
         SPACE 3
         PUSH  PRINT                                                U13
         PRINT GEN                                                  U13
         SPACE 1
PRT$EXIT DCBEXIT  BLKSIZE=3192,BUFNO=1                              U15
         POP   PRINT                                                U13
         EJECT
***********************************************************************
*                                                                     *
*    ICALLABLE SUBROUTINES                                            *
*                                                                     *
***********************************************************************
         SPACE 3
*  SUBROUTINE TO CONVERT A HALFWORD TO PRINTABLE FORM
CONVHALF MVC   DWD(2),0(R1)             MOVE FIELD TO AN ALIGNED PLACE
         LH    R0,DWD                   PICK IT UP
         CVD   R0,DWD                   CONVERT TO PACKED
         MVC   1(5,R15),=X'2020202120'  MOVE IN EDIT MASK
         ED    0(6,R15),DWD+5           DO THE EDIT
         BR    R14                      RETURN TO CALLER
         SPACE 3
*  SUBROUTINE TO CONVERT A 2-BYTE PACKED FIELD TO PRINTABLE FORM
CONVP2   MVC   1(3,R15),=X'202120'      MOVE EDIT PATTERN TO OUT AREA
         ED    0(4,R15),0(R1)           CONVERT PACKED TO DECIMAL
         BR    R14
         SPACE 3
*  SUBROUTINE TO CONVERT A 3-BYTE PACKED FIELD TO PRINTABLE FORM
CONVP3   MVC   1(5,R15),=X'2020202120'  MOVE EDIT PATTERN TO OUT AREA
         ED    0(6,R15),0(R1)           CONVERT PACKED TO DECIMAL
         BR    R14
         SPACE 3
*  SUBROUTINE TO CONVERT A 4-BYTE PACKED FIELD TO PRINTABLE FORM
CONVP4   MVC   1(7,R15),=X'20202020202120'  MOVE EDIT PATTERN
         ED    0(8,R15),0(R1)           CONVERT PACKED TO DECIMAL
         BR    R14
         SPACE 3
*  SUBROUTINE TO CONVERT A PACKED DATE FIELD TO PRINTABLE FORM
CONVMDY  MVC   MDYP(4),=PL4'0'          ZERO MDYP (4-BYTES PACKED)
         MVC   MDYP(3),0(R1)            GET MMDDYY (PACKED W/O SIGN)
         UNPK  MDYU(7),MDYP(4)          UNPACK IT
         MVC   0(2,R15),MDYU            MOVE MM
         MVI   2(R15),C'/'                   /
         MVC   3(2,R15),MDYU+2               DD
         MVI   5(R15),C'/'                   /
         MVC   6(2,R15),MDYU+4               YY
         BR    R14                      RETURN
         SPACE 3
*  SUBROUTINE TO REMOVE LEADING BLANKS
SLIDE    CLI   0(R15),C' '              NON-BLANK YET?
         BNER  R14                      YES - RETURN TO CALLER
         MVC   0(6,R15),1(R15)          MOVE IT LEFT 1 POS
         B     SLIDE                    AND CHECK IT AGAIN
         SPACE 3
*  SUBROUTINE TO REMOVE LEADING ZEROES
DEZERO   CLI   0(R1),C'0'               NON-ZERO YET?
         BNER  R14                      YES - RETURN
         MVI   0(R1),C' '               MAKE IT A BLANK
         A     R1,=F'1'                 ADVANCE
         B     DEZERO                   LOOP
         SPACE 3
*  SUBROUTINE TO CONVERT A UNIT TYPE
CONVUNIT MVC   OUTBUFF2+120(9),=C'UNIT=????'
         CLI   3(R1),X'0F'              TOO BIG?              03/94 SBG
         BHR   R10                      YES - RETURN
         IC    R14,3(,R1)               ELSE GET BYTE
         N     R14,=F'255'              ISOLATE IT
         BZR   R10                      INVALID IF ZERO
         BCTR  R14,0                    -1 BECAUSE NO ZERO
         MH    R14,=H'6'                LENGTH OF A TABLE ENTRY
         LA    R15,=C'2311  2301  2303  2302  2321  2305-12305-22314  3$
               330  3340  3350  3375  3330-13380  3390  '     03/94 SBG
         LA    R14,0(R14,R15)           POINT TO CORRECT ENTRY
         MVC   OUTBUFF2+125(6),0(R14)   MOVE DEVTYPE TO LINE
         BR    R10                      RETURN TO CALLER
         SPACE 3
*  SUBROUTINE TO CONVERT A DSORG
CNVDSORG LA    R1,TAPEBUFF
         AH    R1,0(,R10)               GET DSORG ADDR
         SPACE 1
CNVDSORX TM    0(R1),X'01'              DSORG=**U
         BNO   *+8                      NO - SKIP
         MVI   2(R15),C'U'
         SPACE 1
         MVC   0(2,R15),=C'PO'          ASSUME DSORG=PO
         TM    0(R1),X'02'              IS IT?
         BOR   R14                      YES - RETURN TO CALLER
         SPACE 1
         MVC   0(2,R15),=C'DA'          ASSUME DSORG=DA
         TM    0(R1),X'20'              IS IT?
         BOR   R14                      YES - RETURN TO CALLER
         SPACE 1
         MVC   0(2,R15),=C'PS'          ASSUME DSORG=PS
         TM    0(R1),X'40'              IS IT?
         BOR   R14                      YES - RETURN TO CALLER
         SPACE 1
         MVC   0(2,R15),=C'IS'          ASSUME DSORG=IS
         TM    0(R1),X'80'              IS IT?
         BOR   R14                      YES - RETURN TO CALLER
         SPACE 1
         MVC   0(4,R15),=C'VSAM'        ASSUME VSAM
         CLC   0(2,R1),=X'0008'         IS IT?
         BER   R14                      YES - RETURN TO CALLER
         SPACE 1
         MVC   0(4,R15),=C'**  '        NONE OF THE ABOVE???
         BR    R14                      RETURN TO CALLER
         SPACE 3
*  SUBROUTINE TO CONVERT A RECFM
CNVRECFM LA    R1,TAPEBUFF
         AH    R1,2(,R10)               GET RECFM ADDR
         SPACE 1
CNVRECFX TM    0(R1),X'C0'              RECFM=U?
         BNO   *+12                     NO - TRY NEXT
         MVI   0(R15),C'U'
         B     RECFM$2                  CONTINUE
         SPACE 1
         TM    0(R1),X'80'              RECFM=F?
         BNO   *+12                     NO - TRY NEXT
         MVI   0(R15),C'F'
         B     RECFM$2                  CONTINUE
         SPACE 1
         TM    0(R1),X'40'              RECFM=V?
         BNO   *+8                      NO - TRY NEXT
         MVI   0(R15),C'V'
         SPACE 1
RECFM$2  LA    R15,1(,R15)
         TM    0(R1),X'10'              BLOCKED?
         BNO   *+12                     NO - TRY NEXT
         MVI   0(R15),C'B'
         LA    R15,1(,R15)
         SPACE 1
         TM    0(R1),X'08'              SPANNED?
         BNO   *+12                     NO - TRY NEXT
         MVI   0(R15),C'S'
         LA    R15,1(,R15)
         SPACE 1
         TM    0(R1),X'20'              TRK OVFL?
         BNO   *+12                     NO - TRY NEXT
         MVI   0(R15),C'T'
         LA    R15,1(,R15)
         SPACE 1
         TM    0(R1),X'02'              RECFM=A?
         BNO   *+12                     NO - TRY NEXT
         MVI   0(R15),C'A'
         LA    R15,1(,R15)
         SPACE 1
         TM    0(R1),X'01'              RECFM=M?
         BNOR  R14                      NO - RETURN TO CALLER
         MVI   0(R15),C'M'
         BR    R14                      RETURN TO CALLER
         SPACE 3
*  SUBROUTINE TO COMPUTE TAPEFEET
TAPEFEET XR    R0,R0
         D     R0,=F'1200'              GET LENGTH IN FEET
         CH    R0,=H'600'               ROUND UP?
         BL    *+8                      NO
         A     R1,=F'1'                 ROUND UP.
         CVD   R1,DWD
         MVC   1(5,R15),=X'2020202120'
         ED    0(6,R15),DWD+5
         BR    R14                      RETURN TO CALLER
         SPACE 3
*  SUBROUTINE TO FIND A TIOT ENTRY
TIOTSCAN L     R1,DD#PTR                -> DD SECTION
         XR    R0,R0                    CLEAR FOR IC
         BA&XA R14,*+6                  SET LOOP ADDR & SKIP
         SPACE 1
         AR    R1,R0                    POINT TO NEXT ENTRY
         IC    R0,0(,R1)                GET LENGTH OF THIS ENTRY
         LTR   R0,R0                    END OF TIOT?
         BZR   R10                      YES - "NOT FOUND" RETURN
         CLC   4(8,R1),0(R15)           THIS IT?
         BNER  R14                      NO - LOOP
         CLC   =F'0',16(R1)             DD DUMMY? (LIKE MSM?)
         BNE   4(,R10)                  NO - RETURN (FOUND)
         BR    R10                      SAY NOT FOUND
         EJECT
         LTORG ,
         SPACE 6
*  REGISTER EQUATES                                            1/93 RT
R0       EQU   0                                               1/93 RT
R1       EQU   1                                               1/93 RT
R2       EQU   2                                               1/93 RT
R3       EQU   3                                               1/93 RT
R4       EQU   4                                               1/93 RT
R5       EQU   5                                               1/93 RT
R6       EQU   6                                               1/93 RT
R7       EQU   7                                               1/93 RT
R8       EQU   8                                               1/93 RT
R9       EQU   9                                               1/93 RT
R10      EQU   10                                              1/93 RT
R11      EQU   11                                              1/93 RT
R12      EQU   12                                              1/93 RT
R13      EQU   13                                              1/93 RT
R14      EQU   14                                              1/93 RT
R15      EQU   15                                              1/93 RT
*  REGISTERS USED IN ROUTINES TO LIST IEHMOVE & IEBCOPY UNLOADED MEMBRS
BLEN     EQU   R2      *PAIR*           BLOCK LENGTH LEFT
BPTR     EQU   R3      *PAIR*           -> CURRENT POS IN BLOCK
DLEN     EQU   R4                       DIRBLK OR DATA LENGTH LEFT
DPTR     EQU   R5                       -> CURRENT POS IN BLOCK
PPTR     EQU   R6                       -> POS IN PRINT LINE
         SPACE 1
*  OTHER MISC REGISTERS
LCTR     EQU   R8                       # OF LINES LEFT THIS PAGE
LCTR2    DS    F                        STORAGE INSTEAD OF REG 1/93 RT
         SPACE 1
CC       EQU   X'40'                    COMMAND CHAINING
SLI      EQU   X'20'                    SUPPRESS LENGTH INDIC.
         SPACE 1
RD       EQU   X'02'                    READ
BSF      EQU   X'2F'                    BACKSPACE FILE        02/93 RT
FSF      EQU   X'3F'                    FORWARD SPACE FILE
RWD      EQU   X'07'                    REWIND
         SPACE 1
BUFFSIZE EQU   X'FFFF'             SIZE OF TAPE BUFFER        09/84 DBC
LINESPP  EQU   &LINEPPG                 LINES PRINTED PER PAGE11/88 SBG
         SPACE 2
OPENMFL2 OPEN  (SYSPRNT2,OUTPUT),MF=L
OPENMFL  OPEN  (SYSPRINT,OUTPUT),MF=L
OPENMFLI OPEN  SYSIN,MF=L
TAPEMFL  OPEN  TAPEDCB,MF=L
         SPACE 1
EXLST    DC    A(0)
PRTEXLST DC    X'85',AL3(PRT$EXIT)
         SPACE 2
TAPEECB  DC    A(0)
TAPEIOB  DC    X'42000000'              IOBFLAG1 (SET FOR CMD CHAINING)
         DC    A(TAPEECB)               ECB ADDR
IOBCSW   DC    2F'0'                    CSW
         DC    A(0)                     CHANNEL PGM ADDR
         DC    A(TAPEDCB),A(0)          DCB ADDR
         DC    H'1',H'0'                TAPE BLOCK COUNT INCREMENT
         SPACE 1
TCCW#BSF CCW   BSF,0,0,1                BACKSPACE FILE
         SPACE 1
TCCW#FSF CCW   FSF,0,0,1                FWD SPACE FILE
         SPACE 1
TCCW#LBL CCW   RD,*-*,CC,80             READ 80 BYTE BLOCK INTO FL1LABI
         CCW   RD,*-*,00,80             READ IN NEXT BLOCK INTO FL2LABI
         SPACE 1
TCCW#EOV CCW   RD,*-*,00,80             READ 80 BYTE BLOCK INTO FL1LABI
         SPACE 1
TCCW#DAT CCW   RD,*-*,SLI,BUFFSIZE      READ DATA BLOCK
         SPACE 1
TCCW#RWD CCW   RWD,0,0,1                REWIND THE TAPE
         SPACE 1
TCCW#SNS CCW   4,SENSDATA,SLI,24        READ SENSE DATA
         SPACE 2
SENSDATA DC    XL24'00'                 SENSE DATA BUFFER
         SPACE 2
TFLAG1   DC    X'00'                    FLAGS
T1@PARM  EQU   X'80'                    HAVE PARM FIELD
T1@PERR  EQU   X'40'                    ERROR IN PARM FIELD
T1@PAGE1 EQU   X'20'                    THIS IS FIRST TITLE GROUP
T1@BADEN EQU   X'10'                    DENSITY ERROR IN TAPE LABEL(S)
T1@SL    EQU   X'08'                    THIS TAPE IS SL
T1@ANLZ  EQU   X'04'                    PARM=CHECK OR NL TAPE
T1@MVEOF EQU   X'02'                    IEHMOVE2 HAS READ TILL EOF
T1@DATA  EQU   X'01'                    HAVE INPUT DATA
         SPACE 1
TFLAG2   DC    X'00'                    MORE
T2@RQVOL EQU   X'80'                    REQUESTED VOL MISMATCH
T2@LPARM EQU   X'40'                    PARM FIELD LONGER THAN 21 CHARS
T2@PRT2  EQU   X'20'                    SECOND PRINT FILE IN USE
         SPACE 1
NLFLAGS  DC    X'00'                    NON-LABELLED TAPE FLAGS
NL@V     EQU   X'80'                    RECFM=V
NL@F     EQU   X'40'                    RECFM=F
NL@CHANG EQU   X'20'                    BLOCKSIZE HAS CHANGED
         SPACE 1
PARMFLAG DC    X'00'
PF@TEST  EQU   X'80'                    TEST
PF@INLIN EQU   X'40'                    INLINE
PF@NOMEM EQU   X'20'                    NOMEMBERS (NOMEM)
PF@DEN1  EQU   X'10'                    DEN1
PF@NATTR EQU   X'08'                    NOATTR
PF@NONOT EQU   X'04'                    NONOTE
PF@NOCHK EQU   X'02'                    NOCHECK (NOSCAN)      06/84 DBC
PF@NL    EQU   X'01'                    NL
         SPACE 1
PARMTAB  DC    H'3',C'TEST     ',AL1(PF@TEST)
         DC    H'5',C'INLINE   ',AL1(PF@INLIN)
         DC    H'8',C'NOMEMBERS',AL1(PF@NOMEM)
         DC    H'4',C'NOMEM    ',AL1(PF@NOMEM)
         DC    H'3',C'DEN1     ',AL1(PF@DEN1)
         DC    H'5',C'NOATTR   ',AL1(PF@NATTR)
         DC    H'5',C'NONOTE   ',AL1(PF@NONOT)
         DC    H'6',C'NOCHECK  ',AL1(PF@NOCHK)                06/84 DBC
         DC    H'5',C'NOSCAN   ',AL1(PF@NOCHK)                06/84 DBC
PARMLAST DC    H'1',C'NL       ',AL1(PF@NL)
         SPACE 2
         DC    C' '                     FOR CLEARING "UNLOADER"
UNLOADER DC    CL8' '             /   /IEBCOPY/IEHMOVE/IEBISA 06/84 DBC
C00080   DC    C'00080'
         ORG   C00080+1
C00800   DC    C'00800'
RET#ADDR DC    A(EOD2)                  RETURN ADDR
DEN#LIST DC    F'200,556,800,1600,6250,37871'  DENSITY VALUES       THO
         DC    C'200 556 800 16006250N/A '     RIGHT AFTER ABO LINE THO
TRUE#DEN DC    C'3',AL3(DEN#LIST+8)     REAL DEN;  -> 4 CHAR REAL DEN
F1       DC    F'1'                                                 U14
H1       EQU   F1+2                                                 U14
F15      DC    F'15'                                                U14
NULLNUMB DC    H'0'                     # OF NULL FILES TO SKIP     U14
NUMBNULL DC    H'0'                     # OF NULL FILES / THIS TAPE U14
WHERE    DC    X'00'                    WHERE-WE-ARE FLAG
         SPACE 1
         PUSH  PRINT
         PRINT GEN
         SPACE 1
HEXTAB   HEXTAB  DUAL=NO
         SPACE 3                                                    THO
         POP   PRINT                                                THO
         SPACE 1                                                    THO
FDRDVTAB DS    0C                                             09/84 DBC
         DC    C'A2305-1'                                     09/84 DBC
         DC    C'C2305-2'                                     09/84 DBC
         DC    C'M3380  '                                     09/84 DBC
         DC    C'N3375  '                                     09/84 DBC
         DC    C'T2314  '                                     09/84 DBC
         DC    C'U3340-1'          TABLE OF FDR DEVICE TYPES  09/84 DBC
         DC    C'V3340-2'          AND CODES
         DC    C'X3330  '
         DC    C'Y3330-1'
         DC    C'Z3350  '
FDRDVEND DC    C' ??????'          FENCE FOR UNKNOWN FDR DEVICES
         EJECT
WORKD    DSECT                          RESUME
         DS    9D                  SAVE AREA                  01/93 RT
DD#PTR   DS    A                        -> DD SECTION OF TIOT
LEN#TAPE DS    F                        TAPE LENGTH
         DS    F                        NL LENGTH
FILE#SEQ DS    H                        NL FILE SEQ #
         DS    H                        (UNUSED)
PRT#SAVE DS    A                        SAVE FOR RETURN ADDR
R10SAVE  DS    A                        MORE...
LKRTAPIO DS    A                        ...OF THE...
R14SAVE  DS    A                        ...SAME
TU$SAVE2 DS    2A                  USED BY TEST$UP            06/84 DBC
BOOMREGS DS    16F                      REG SAVE AREA FOR LOGIC ERROR
MAXBLKSI DS    F                   BIGGEST BLOCK READ SO FAR  09/84 DBC
LASTSIZE DS    F                   SIZE OF THE LAST BLK READ  09/84 DBC
BLOCKCNT DS    F                        # OF BLOCKS READ
BYTECNT  DS    F                        TOTAL # OF BYTES READ
CBTBLK1  DS    F                   STORE BLOCKSIZ FOR CBT973. 11/88 SBG
CBTAPEND DS    F                   STORE EO-BLOCK FOR CBT973. 11/88 SBG
CBSTOR1  DS    F                   STORE REGISTER FOR CBT973. 11/88 SBG
CBSTOR2  DS    F                   STORE REGISTER FOR CBT973. 01/93 RT
CBSTOR01 DS    F                   STORE REGISTER FOR CBT973. 11/88 SBG
CBSTOR02 DS    F                   STORE REGISTER FOR CBT973. 11/88 SBG
CBSTOR03 DS    F                   STORE REGISTER FOR CBT973. 11/88 SBG
CBSTOR04 DS    F                   STORE REGISTER FOR CBT973. 11/88 SBG
CBSTOR05 DS    F                   STORE REGISTER FOR CBT973. 11/88 SBG
CBSTOR06 DS    F                   STORE REGISTER FOR CBT973. 11/88 SBG
CBSTOR08 DS    F                   STORE REGISTER FOR CBT973. 11/88 SBG
CBSTOR09 DS    F                   STORE REGISTER FOR CBT973. 11/88 SBG
CBSTOR10 DS    F                   STORE REGISTER FOR CBT973. 11/88 SBG
CBSAVE4  DS    F                   STORE REGISTER FOR CBT973. 11/88 SBG
CBSAVE15 DS    F                   STORE REGISTER 15          11/88 SBG
NL#SAVE  DS    4F                  SAVE FOR NL STUFF IF SL    09/84 DBC
NL#FLAGX DS    X                        SAME
UPDT#TYP DS    C                        A,R,C FOR UPDTE
PTF#TYPE DS    C                   A,F,P,U FOR SYSMODS        07/85 DBC
UPDTXFLG DS    C                   FLAG IF ./ OR >< IN UPDTE  11/88 SBG
BLANKS   DS    0CL17,CL16' '
OUTCLEAR DC    C' '                     FOR CLEARING OUTBUFF
OUTBUFF  DS    CL133
OUTCLR2  DC    C' '                     FOR CLEARING OUTBUFF2
OUTBUFF2 DS    CL133,7C                                             U14
DASHES   DS    CL133
INBUFF   DS    CL80
LABELS   DS    3CL80                    FOR VOL,FL1,FL2
CBTCLEAR DC    C' '                     FOR CLEARING CBT CARD 11/88 SBG
CBTCARD  DS    CL80              CBT973 CARD-IMAGE EXPANSION  11/88 SBG
CSTATCLR DC    C' '                     FOR CLEARING CBT DOC  12/88 SBG
CBTVERS  DS    CL4                      CBT TAPE VERSION #    12/88 SBG
CBTDATE  DS    CL8                      CBT TAPE RELEASE DATE 12/88 SBG
MV#ABUF2 DS    A(MV#BUFF2)              ADDR OF SECOND DE-SPANNING BUFF
MV#BUFF1 DS    CL78                     FIRST DE-SPANNING BUFFER
MV#BUFF2 DS    CL78                     SECOND DE-SPANNING BUFFER
PARM#MSG DS    CL110                    FOR PARM
OB2END   DS    A(OUTBUFF2+110)          ADDR OF END OF OUTBUFF2
OB2END2  DS    A(OUTBUFF2+110+10)       SAME FOR UPDTE
DASDRSAV DS    XL24                     FOR HEADER RECORD
FDRSAVE  DS    CL8                      FOR 'DUMMYDSF' IF PRESENT
MDYP     DS    PL4'0'                   FOR CONVMDY
MDYU     DS    CL7                      FOR CONVMDY
         SPACE 2                                              12/88 SBG
***** LOADED  LITERALS  -  SEE END OF THE PROGRAM  ********** 12/88 SBG
         DS    0D                                             12/88 SBG
EMPTYLIT DC    C'THISFILEHASBEENDELETEDORISRESERVED'  NEWVERS 12/88 SBG
EMPTYLI2 DC    C'DELETEDBYPREVIOUSVERSIONOFTHISTAPE'  OLDVERS 12/88 SBG
REQ#VOL  DC    C'+** REQUESTED VOL="123456" **'
         SPACE 1
FVOL#MSG DC    C' *** THIS IS VOLUME # XXXX OF THE ABOVE MULTIPLE VOLUM$
               E FILE.  FIRST VOLUME WAS XXXXXX.'
         SPACE 1
PARMERR  DC    C' UNKNOWN PARM OPTION AT COL=XXX.  ANY REMAINING PARM I$
               GNORED.'
FFS      DC    XL12'FFFFFFFFFFFFFFFFFFFFFFFF'
CHJOBLIT DC    C'//MVSMODS'       ARNIE CASINGHINO'S JOBNAME  12/88 SBG
VERSLIT1 DC    C'* VERSION'       DATE OF CBT TAPE - PREFIX   12/88 SBG
VERSLIT2 DC    C'VERSION #'       VERSION OF CBT TAPE PREFIX  12/88 SBG
CBHLIT   DC    C'VER        -  -  '   DISPLAY FOR CBTHEADER   12/88 SBG
         SPACE 1                                              12/88 SBG
************************************************************* 12/88 SBG
*  TABLE OF UNLOADER LITERALS - NOTE, IF YOU ADD TO THIS      12/88 SBG
*   TABLE, YOU MUST ADD TO THE LIST OF LITERALS TO BE MOVED   12/88 SBG
*   INTO THIS WORKAREA THAT IS AT THE END OF THIS PROGRAM.    12/88 SBG
*  PLEASE SEE THE LABEL CALLED "LITMOVES" -  THIS WAS BECAUSE 12/88 SBG
*   THE 3RD BASE REGISTER WAS EXCEEDED, AND I MOVED SOME OF   12/88 SBG
*   THE LITERALS INTO THE WORKAREA DSECT.  THEY THEREFORE     12/88 SBG
*   HAVE TO BE INITIALIZED IN THE GETMAINED WORKAREA STORAGE. 12/88 SBG
************************************************************* 12/88 SBG
IEBCOPY  DC    C'IEBCOPY'
IEHMOVE  DC    C'IEHMOVE'
IEBISAM  DC    C'IEBISAM'
IEHDASDR DC    C'IEHDASDR'
IEBUPXXX DC    C'IEBUPXXX'         IEBUPDTE OR IEBUPDAT       06/84 DBC
FDR      DC    C'FDR   '
FDRDSF   DC    C'FDRDSF'
FDRXXX   DC    C'FDR???'
SLICKMNG DC    C'SLICKMNG'                                    09/84 DBC
CTAPEMAP DC    C'TAPEMAP'
SMPPTFIN DC    C'SMPPTFIN'                                    07/85 DBC
CBT973   DC    C'CBT973'                                      11/88 SBG
CBTEMPTY DC    C'CBTEMPTY'         EMPTY FILE ON THE CBT TAPE 11/88 SBG
CBHEADER DC    C'CBT DOC '         DOC FILE FOR THE CBT TAPE  12/88 SBG
UTY3420C DC    X'33008003'        UNIT TYPE FOR 3420C CARTS   09/89^SBG
         DS    0F                 ALIGNMENT                   12/88 SBG
CDOCSIZE DC    F'32720'           BLOCK SIZE OF CBT DOC FILE  12/88 SBG
INCORLAB DC    C'-*****  ONE OR MORE FILES HAVE DENSITY INDICATED INCOR$
               RECTLY IN LABELS.  ALL FILES ARE WRITTEN AT XXXX BPI  **$
               ***'
***** LOADED  LITERALS  -  SEE END OF THE PROGRAM  ********** 12/88 SBG
         SPACE 2
DWD      DS    D
JFCB     DS    XL176                    DWD ALIGNED
************************************************************* 12/88 SBG
*  WE CONTINUE TO ADDRESS THE PROBLEM OF EXCEEDING 3 BASE   * 12/88 SBG
*  REGISTERS.  PREVIOUSLY, THE TAPE BUFFER WAS ALIGNED ON   * 12/88 SBG
*  HALF A PAGE.  WE WANT TO HAVE MOST OF THIS HALF A PAGE   * 12/88 SBG
*  FOR COPYING LITERALS SO THEY CAN BE ADDRESSABLE FROM THE * 12/88 SBG
*  WORKAREA'S BASE REGISTER.  WE HOPE THE ALIGNMENT OF      * 12/88 SBG
*  WORKD+2048 WASN'T REALLY NECESSARY.                      * 12/88 SBG
*-----------------------------------------------------------* 12/88 SBG
*  HOWEVER, IF TAPEBUFF+NNN HAS BEEN SPECIFIED IN ANY       * 12/88 SBG
*  INSTRUCTION, THE SUM OF THE DISPLACEMENT OF TAPEBUFF     * 12/88 SBG
*  AND NNN MUST NOT EXCEED 4095.  THE LARGEST SUCH          * 12/88 SBG
*  DISPLACEMENT WE HAVE FOUND AT THIS WRITING IS 282.       * 12/88 SBG
*  THAT IS TO SAY, TAPEBUFF+282 IS THE FARTHEST OUT         * 12/88 SBG
*  ADDRESS THAT THE CURRENT INCARNATION OF THIS PROGRAM     * 12/88 SBG
*  HAS TO GET TO, FROM REGISTER 13.  THEREFORE, I HAVE      * 12/88 SBG
*  SUBTRACTED 300 FROM 4096 AND SET TAPEBUFF AT WORKD+3796. * 12/88 SBG
*  PLEASE REMEMBER THAT AT LEAST DOUBLEWORD ALIGNMENT       * 12/88 SBG
*  SHOULD BE OBSERVED.                                      * 12/88 SBG
************************************************************* 12/88 SBG
         ORG   WORKD+3796   ALIGN THE BUFFER NOT SO REAL WELL 12/88 SBG
TAPEBUFF DS    CL(BUFFSIZE)             TAPE INPUT DATA BUFFER
WORKLEN  EQU   *-WORKD
         SPACE 2
         ORG   OUTBUFF+1
#UNLOAD  DS    CL8,1C                                         06/84 DBC
#FILE    DS    CL4,2C                                         06/84 DBC
#DSN     DS    CL17,2C
#PSWD    DS    CL4,2C                                         06/84 DBC
#CDATE   DS    CL6,2C
#EDATE   DS    CL6,2C                                         06/84 DBC
#INFOSRC DS    CL6,2C                                         06/84 DBC
#RECFM   DS    CL5,2C                                         06/84 DBC
#LRECL   DS    CL5,2C
#BLKSIZE DS    CL5,2C
#BLKCNT  DS    CL6,2C
#DEN     DS    CL4,1C                                         06/84 DBC
#TRTCH   DS    CL3,2C                                         06/84 DBC
#LENGTH  DS    CL4,2C                                         06/84 DBC
#CUMLEN  DS    CL4,2C                                         06/84 DBC
#CREATOR DS    CL17                                           06/84 DBC
         SPACE 2
         PUSH  PRINT
         PRINT GEN
         SPACE 2
         ORG   LABELS
         SPACE 2
         VOL   DSECT=NO
         FL1   DSECT=NO
         FL2   DSECT=NO
         POP   PRINT
         EJECT
TAPEMAP  CSECT                          RESUME
         SPACE 2
         XYZZY NOGEN               SUPPRESS EXPANSION         06/84 DBC
         SPACE 1                                              06/84 DBC
TAPEDCB  DCB   DSORG=PS,MACRF=E,DDNAME=SYSUT1,DEVD=TA,EXLST=EXLST,     $
               IOBAD=TAPEIOB
         XYZZY GEN                                            09/84 DBC
         SPACE 2
TITLE1   DC    C'1VOL=                             TAPE ANALYSIS PROGRA$
               M   (T A P E M A P)  V2.1          DAYDAYDAY  MON DD, 19$
               YY  (YY.DDD)  HH:MM:SS'
TTL1TIME EQU   *-9
TTL1DATE EQU   TTL1TIME-23
TTL1DAY  EQU   TTL1DATE-11
         SPACE 2
TITLE2   DC    C'                                                      $
                                                                       $
                                    '
         SPACE 2
DSFHDING DS    CL133
         ORG   DSFHDING
         DC    CL17'0'
DSFDSN   EQU   *-DSFHDING
         DC    CL44'-------------------DSNAME-------------------',C'-'
DSFTRK   EQU   *-DSFHDING
         DC    CL9'TRKS USED',C'-'                     TRACKS USED
DSFRECFM EQU   *-DSFHDING
         DC    CL5'RECFM',C'-'                         RECFM
DSFLRECL EQU   *-DSFHDING
         DC    CL5'LRECL',C'-'                         LRECL
DSFBLKSZ EQU   *-DSFHDING
         DC    CL7'BLKSIZE',C'-'                       BLKSIZE
DSFDSORG EQU   *-DSFHDING
         DC    CL5'DSORG',C'-'                         DSORG
DSFCDATE EQU   *-DSFHDING
         DC    CL7'CREATED',C'-'                       C-DATE
DSFEDATE EQU   *-DSFHDING
         DC    CL7'EXPIRES',C'-'                       E-DATE
DSFSECUR EQU   *-DSFHDING
         DC    CL8'SECURITY',C'-'                      SECURITY
         ORG   ,
SLKHDING DS    CL133
         ORG   SLKHDING
         DC    CL17'0'
SLKMEM   EQU   *-SLKHDING
         DC    CL10'NAME------.'          MEMBER
SLKSTAT  EQU   *-SLKHDING
         DC    CL4'STAT',C'-'             STATUS
SLKREV   EQU   *-SLKHDING-1
         DC    CL3'REV',C'-'              REVISION
SLKINFO  EQU   *-SLKHDING
         DC    CL10'---INFO---',C'-'      INFO
SLKTYPE  EQU   *-SLKHDING
         DC    CL8'--TYPE--',C'-'         TYPE
SLKACTST EQU   *-SLKHDING-1
         DC    CL7'--ACTST',C'-'          ACTST
SLKDELST EQU   *-SLKHDING-1
         DC    CL7'--DELST',C'-'          DELST
SLKBLKS  EQU   *-SLKHDING-1
         DC    CL5'-BLKS',C'-'            BLOCKS
SLKCREAT EQU   *-SLKHDING
         DC    CL8'-CREATED',C'-'         CREATE DATE
SLKUPD   EQU   *-SLKHDING
         DC    CL8'-UPDATED'              LAST UPDATE DATE
         ORG   ,
         SPACE 2
         XYZZY NOGEN                                          09/84 DBC
         SPACE 1                                              09/84 DBC
SYSIN    DCB  DSORG=PS,MACRF=GM,DDNAME=SYSIN,LRECL=80,EODAD=EOD,BUFNO=1
         SPACE 2
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,LRECL=133,            $
               RECFM=FBA,EXLST=PRTEXLST
         SPACE 2
SYSPRNT2 DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRNT2,LRECL=133,            $
               RECFM=FBA,EXLST=PRTEXLST
         SPACE 1                                              06/84 DBC
         XYZZY GEN                 RESTORE EXPANSIONS         06/84 DBC
COLHEAD1 DC    CL133' '            1ST TITLE LINE             06/84 DBC
         ORG   COLHEAD1+1          RELOCATE BACK              06/84 DBC
         DC    CL8'RELOAD',C' '    #UNLOAD                    06/84 DBC
         DC    CL4'FILE',C'  '     #FILE                      06/84 DBC
         DC    CL17' ',C'  '       #DSN                       06/84 DBC
         DC    CL4'PSWD',C'  '     #PSWD                      06/84 DBC
         DC    CL6' ',C'  '        #CDATE                     06/84 DBC
         DC    CL6' ',C'  '        #EDATE                     06/84 DBC
         DC    CL6' INFO',C'  '    #INFOSRC                   06/84 DBC
         DC    CL5' ',C'  '        #RECFM                     06/84 DBC
         DC    CL5' ',C'  '        #LRECL                     06/84 DBC
         DC    CL5'BLOCK',C'  '    #BLKSIZE                   06/84 DBC
         DC    CL6' BLOCK',C'  '   #BLKCNT                    06/84 DBC
         DC    CL3' ',C'  '        #DEN                       06/84 DBC
         DC    CL3' ',C'  '        #TRTCH                     06/84 DBC
         DC    CL4'LNTH',C'  '     #LENGTH                    06/84 DBC
         DC    CL4'TOTL',C'  '     #CUMLEN                    06/84 DBC
         DC    C'CREATOR'          #CREATOR                   06/84 DBC
         ORG   ,                   RESTORE                    06/84 DBC
         SPACE 3                                              06/84 DBC
COLHEAD2 DC    CL133' '            2ND TITLE LINE             06/84 DBC
         ORG   COLHEAD2+1          RELOCATE BACK              06/84 DBC
         DC    CL8'FORMAT',C' '    #UNLOAD                    06/84 DBC
         DC    CL4'SEQ#',C'  '     #FILE                      06/84 DBC
         DC    CL17'DATASET NAME',C'  ' #DSN                  06/84 DBC
         DC    CL4'REQD',C'  '     #PSWD                      06/84 DBC
         DC    CL6'C-DATE',C'  '   #CDATE                     06/84 DBC
         DC    CL6'E-DATE',C'  '   #EDATE                     06/84 DBC
         DC    CL6'SOURCE',C'  '   #INFOSRC                   06/84 DBC
         DC    CL5'RECFM',C'  '    #RECFM                     06/84 DBC
         DC    CL5'LRECL',C'  '    #LRECL                     06/84 DBC
         DC    CL5' SIZE',C'  '    #BLKSIZE                   06/84 DBC
         DC    CL6' COUNT',C'  '   #BLKCNT                    06/84 DBC
         DC    CL3'DEN',C'  '      #DEN                       06/84 DBC
         DC    CL3'TRT',C'  '      #TRTCH                     06/84 DBC
         DC    CL4'(FT)',C'  '     #LENGTH                    06/84 DBC
         DC    CL4'LNTH',C'  '     #CUMLEN                    06/84 DBC
         DC    C'JOBNAME/STEPNAME' #CREATOR                   06/84 DBC
         ORG   ,                   RESTORE                    06/84 DBC
         SPACE 3                                              06/84 DBC
         EJECT
ERR#LIST DC    0F'0'                                                U15
*                                                                   U15
*  ERROR MSGS FOR EACH SENSE BIT FOR 3420 TAPE DRIVES               U15
*                                                                   U15
         SPACE 1
*** BYTE 0                       BIT                                U15
         DC    C'CMD REJ   '      0     80                          U15
         DC    C'INT REQ   '      1     40                          U15
         DC    C'BUS OUT CK'      2     20                          U15
         DC    C'EQC CHK   '      3     10                          U15
         DC    C'DATA CHK  '      4     08                          U15
         DC    C'OVERRUN   '      5     04                          U15
         DC    C'WORD CNT 0'      6     02                          U15
         DC    C'DATA CNVTR'      7     01                          U15
         SPACE 1
*** BYTE 1                       BIT                                U15
         DC    C'NOISE     '      0     80                          U15
         DC    C'SEL+RDY+NB'      1     40                          U15
         DC    C'NOT READY '      2     20                          U15
         DC    C'7 TRK FEAT'      3     10                          U15
         DC    C'@ LOAD PT '      4     08                          U15
         DC    C'WRITE STAT'      5     04                          U15
         DC    C'FILE PROT '      6     02                          U15
         DC    C'NOT CAPABL'      7     01                          U15
         SPACE 1
*** BYTE 2                       BIT                                U15
         DC    C'TIE BIT 0 '      0     80                          U15
         DC    C'TIE BIT 1 '      1     40                          U15
         DC    C'TIE BIT 2 '      2     20                          U15
         DC    C'TIE BIT 3 '      3     10                          U15
         DC    C'TIE BIT 4 '      4     08                          U15
         DC    C'TIE BIT 5 '      5     04                          U15
         DC    C'TIE BIT 6 '      6     02                          U15
         DC    C'TIE BIT 7 '      7     01                          U15
         SPACE 1
*** BYTE 3                       BIT                                U15
         DC    C'R/W VRC   '      0     80                          U15
         DC    C'MT/LRC    '      1     40                          U15
         DC    C'SKEW      '      2     20                          U15
         DC    C'END DC/CRC'      3     10                          U15
         DC    C'ENV/ECC   '      4     08                          U15
         DC    C'1600 BPI  '      5     04                          U15
         DC    C'BACKWARD  '      6     02                          U15
         DC    C'C/P COMPAR'      7     01                          U15
         SPACE 1
*** BYTE 4                       BIT                                U15
         DC    C'MP H E    '      0     80                          U15
         DC    C'DROP READY'      1     40                          U15
         DC    C'TAPE INDIC'      2     20                          U15
         DC    C'W T VRC   '      3     10                          U15
         DC    C'MICROPGM  '      4     08                          U15
         DC    C'LWR       '      5     04                          U15
         DC    C'UNIT CHECK'      6     02                          U15
         DC    C'RSRVD RPQ '      7     01                          U15
         SPACE 1
*** BYTE 5                       BIT                                U15
         DC    C'NEW SUBSYS'      0     80                          U15
         DC    C'NEW SUBSYS'      1     40                          U15
         DC    C'WTM CHECK '      2     20                          U15
         DC    C'ID BURST  '      3     10                          U15
         DC    C'START READ'      4     08                          U15
         DC    C'PART''L REC'     5     04                          U15
         DC    C'POSTAMBLE '      6     02                          U15
         DC    C'RSRVD RPQ '      7     01                          U15
         SPACE 1
*** BYTE 6                       BIT                                U15
         DC    C'7 TRK UNIT'      0     80                          U15
         DC    C'WRT CURR  '      1     40                          U15
         DC    C'DUAL DEN  '      2     20                          U15
         DC    C'NOT 1600  '      3     10                          U15
         DC    C'MOD 4,6,8 '      4     08                          U15
         DC    C'MD 5,6,7,8'      5     04                          U15
         DC    C'MOD 3,4   '      6     02                          U15
         DC    C'MD 3,4,7,8'      7     01                          U15
         SPACE 1
*** BYTE 7                       BIT                                U15
         DC    C'LAMP FAIL '      0     80                          U15
         DC    C'BOTM LEFT '      1     40                          U15
         DC    C'BOTM RIGHT'      2     20                          U15
         DC    C'RESET KEY '      3     10                          U15
         DC    C'DSE       '      4     08                          U15
         DC    C'ERASE HEAD'      5     04                          U15
         DC    C'AIR BEARNG'      6     02                          U15
         DC    C'LOAD FAIL '      7     01                          U15
         SPACE 1
*** BYTE 8                       BIT                                U15
         DC    C'IBG       '      0     80                          U15
         DC    C'SPARE ?   '      1     40                          U15
         DC    C'SPARE ?   '      2     20                          U15
         DC    C'EARLY READ'      3     10                          U15
         DC    C'CTL BURST '      4     08                          U15
         DC    C'SLOW RD B '      5     04                          U15
         DC    C'SLOW RD E '      6     02                          U15
         DC    C'VELOCITY  '      7     01                          U15
         SPACE 1
*** BYTE 9                       BIT                                U15
         DC    C'6250 CORR '      0     80                          U15
         DC    C'WRT VEL CH'      1     40                          U15
         DC    C'CHAN BUFF '      2     20                          U15
         DC    C'CRC III   '      3     10                          U15
         DC    C'6250 FEAT '      4     08                          U15
         DC    C'SPARE ?   '      5     04                          U15
         DC    C'SPARE ?   '      6     02                          U15
         DC    C'TCU RSRVD '      7     01                          U15
         EJECT                                                12/88 SBG
************************************************************* 12/88 SBG
*    THE FOLLOWING LITERALS ARE MOVED INTO THE WORKAREA     * 12/88 SBG
*    DSECT.  THE REASON IS THAT THE 3RD BASE REGISTER HAS   * 12/88 SBG
*    BEEN EXCEEDED IN THE COURSE OF PROGRAM MODIFICATIONS,  * 12/88 SBG
*    AND SOME LITERALS WHICH WERE ADDRESSABLE BY THE BASE   * 12/88 SBG
*    REGISTERS, HAVE NOW BEEN MOVED TO THE WORK AREA DSECT. * 12/88 SBG
*    THEREFORE, ANY CHANGES TO THAT PART OF THE WORKAREA    * 12/88 SBG
*    DSECT MUST BE REFLECTED BY CORRESPONDING CHANGES       * 12/88 SBG
*    HERE.                                                  * 12/88 SBG
************************************************************* 12/88 SBG
         DS    0D                  ALIGNMENT                  12/88 SBG
LITMOVES DC    C'THISFILEHASBEENDELETEDORISRESERVED'  NEWVERS 12/88 SBG
         DC    C'DELETEDBYPREVIOUSVERSIONOFTHISTAPE'  OLDVERS 12/88 SBG
         DC    C'+** REQUESTED VOL="123456" **'
         SPACE 1
         DC    C' *** THIS IS VOLUME # XXXX OF THE ABOVE MULTIPLE VOLUM$
               E FILE.  FIRST VOLUME WAS XXXXXX.'
         SPACE 1
         DC    C' UNKNOWN PARM OPTION AT COL=XXX.  ANY REMAINING PARM I$
               GNORED.'
         DC    XL12'FFFFFFFFFFFFFFFFFFFFFFFF'
         DC    C'//MVSMODS'       ARNIE CASINGHINO'S JOBNAME  12/88 SBG
         DC    C'* VERSION'       DATE OF CBT TAPE - PREFIX   12/88 SBG
         DC    C'VERSION #'       VERSION OF CBT TAPE PREFIX  12/88 SBG
         DC    C'VER        -  -  '   DISPLAY FOR CBTHEADER   12/88 SBG
************************************************************* 12/88 SBG
         DC    C'IEBCOPY'          UNLOADER NAMES             12/88 SBG
         DC    C'IEHMOVE'                                     12/88 SBG
         DC    C'IEBISAM'                                     12/88 SBG
         DC    C'IEHDASDR'                                    12/88 SBG
         DC    C'IEBUPXXX'         IEBUPDTE OR IEBUPDAT       12/88 SBG
         DC    C'FDR   '                                      12/88 SBG
         DC    C'FDRDSF'                                      12/88 SBG
         DC    C'FDR???'                                      12/88 SBG
         DC    C'SLICKMNG'                                    12/88 SBG
         DC    C'TAPEMAP'                                     12/88 SBG
         DC    C'SMPPTFIN'                                    12/88 SBG
         DC    C'CBT973'                                      12/88 SBG
         DC    C'CBTEMPTY'         EMPTY FILE ON THE CBT TAPE 12/88 SBG
         DC    C'CBT DOC '         DOC FILE FOR THE CBT TAPE  12/88 SBG
         DC    X'33008003'        UNIT TYPE FOR 3420C CARTS   09/89^SBG
         DS    0F                 ALIGNMENT                   12/88 SBG
         DC    F'32720'           BLOCK SIZE OF CBT DOC FILE  12/88 SBG
         DC    C'-*****  ONE OR MORE FILES HAVE DENSITY INDICATED INCOR$
               RECTLY IN LABELS.  ALL FILES ARE WRITTEN AT XXXX BPI  **$
               ***'
LITMVLEN EQU   *-LITMOVES                                     12/88 SBG
************************************************************* 12/88 SBG
*    END OF LITERAL MOVES.                                  * 12/88 SBG
************************************************************* 12/88 SBG
         EJECT                                                12/88 SBG
*************************************************************  1/93 RT
*    TODAY CSECT                                            *  1/93 RT
*                                                           *  1/93 RT
*   PARAMETERS:                                             *  1/93 RT
*     DATE FIELD (LEN 22)                                   *  1/93 RT
*     DAY FIELD (LEN 9)                                     *  1/93 RT
*     TIME FIELD (LEN 9)                                    *  1/93 RT
*                                                           *  1/93 RT
*                                                           *  1/93 RT
*************************************************************  1/93 RT
TODAY    CSECT                                                 1/93 RT
         DROP  R12,R11,R7,R9,R13                               1/93 RT
         USING *,R15                                           1/93 RT
         B     TODARND                                         1/93 RT
         DC    AL1(25)                                         1/93 RT
         DC    CL8'TODAY'                                      1/93 RT
         DC    CL8'&SYSDATE'                                   1/93 RT
         DC    CL8'&SYSTIME'                                   1/93 RT
TODARND  DS    0H                                              1/93 RT
         SAVE  (14,12)                                         1/93 RT
         DROP  R15                                             1/93 RT
         USING TODAY,R4                                        1/93 RT
         LM    R5,R7,0(R1)          LOAD THE PARAMETER PTRS    1/93 RT
         LR    R4,R15                                          1/93 RT
         LA    R1,TODSAVE                                      1/93 RT
         ST    R1,8(,R13)           FWD SAVE-AREA CHAIN PTR    1/93 RT
         ST    R13,4(,R1)           BKWD SAVE-AREA CHAIN PTR   1/93 RT
         LR    R13,R1               NEW SAVE-AREA ADDR         1/93 RT
         TIME  DEC                      GET TIME
         ST    R0,TODDWT                PUT TIME INTO WORK AREA
         ST    R1,TODPDAT               PUT DATE INTO WORK AREA
         MVC   TOD1DDT(40),TOD1DDT-1    BLANK THE DATE, DAY, TIME
         UNPK  TOD1DATE+16(5),TODPDAT+1(3) UNPK THE DATE
         OI    TOD1DATE+20,C'0'         FIX THE SIGN
         MVC   TOD1DATE+8(2),=C'19'     DEFAULT YEAR TO 19XX
         CLI   TODPDAT,X'00'            TWENTIETH CENTURY?
         BE    TOD19XX                   YES
         MVC   TOD1DATE+8(2),=C'20'      NO, MUST BE 20XX
TOD19XX  MVC   TOD1DATE+15(2),TOD1DATE+16   MOVE THE YEAR OVER
         MVI   TOD1DATE+17,C'.'         PUT IN THE DOT
         MVI   TOD1DATE+6,C','          PUT IN THE COMMA
         MVI   TOD1DATE+21,C')'         PUT IN THE PARENTHESES
         MVI   TOD1DATE+14,C'('
         TM    TODPDAT+1,X'01'          POSSIBLE LEAP YEAR?
         BO    TODNOLP                   NO (ODD YEAR) - NOT LEAP
         TM    TODPDAT+1,X'12'          DIVISIBLE BY 4?
         BM    TODNOLP                   NO - NOT LEAP YEAR
         MVI   MONTHS+7,29              FEB HAS 29 DAYS THIS YEAR
TODNOLP  MVC   TODDWD+6(2),TODPDAT+2    COPY DDDF
         XC    TODDWD(6),TODDWD         CLEAR BEGINNING
         CVB   R0,TODDWD                GET DAY OF YEAR IN BINARY
         LA    R15,MONTHS               POINT TO TABLE
TODFINMO SH    R0,0(,R15)               SUB NUMBER OF DAYS THIS MONTH
         BNP   TODGOTMO                 HAVE MONTH
         LA    R15,6(,R15)              NEXT TABLE ENTRY
         B     TODFINMO                 KEEP LOOKING
TODGOTMO MVC   TOD1DATE(3),2(R15)       MOVE NAME OF MONTH
         AH    R0,0(,R15)               GET BACK CORRECT RESIDUAL
         CVD   R0,TODDWD
         UNPK  TOD1DATE+4(2),TODDWD+6(2)  DAY OF MONTH
         OI    TOD1DATE+5,C'0'          FIX SIGN
         MVC   TOD1DATE+10(2),TOD1DATE+15 MOVE IN YEAR
         ST    R0,TODDWT     SAVE THE DAY
         OI    TODDWT+3,X'0F'           FIX THE SIGN
         MVC   TOD1TIME(9),=X'4021207A20207A2020'    ' DD:DD:DD'
         ED    TOD1TIME(9),TODDWT
         ZAP   TODDWD,TODPDAT  STASH DATE                       GP06316
         CLI   TODDWD+4,X'19'    DID USER SUPPLY A CENTURY?     GP06316
         BL    CENTGONE      NO                                 GP06316
         SP    TODDWD+4(4),=P'1900000'  NORMALIZE TO BASE 1900  GP06316
CENTGONE CVB   R3,TODDWD     CONVERT DATE TO BINARY             GP06316
         SLR   R2,R2         CLEAR FOR DIVIDE                   GP06316
         D     R2,=F'1000'   PLACE DAYS IN R2, YEAR IN R3       GP06316
         LA    R3,6(,R3)     ADD A FINAGLE FACTOR               GP06316
         LA    R1,1(,R3)     YEAR + SECOND FINAGLE FACTOR       GP06316
         SRA   R1,2          # LEAP YEARS SINCE 1900            GP06316
         MH    R3,=H'365'    DAYS SINCE 1900                    GP06316
         AR    R3,R1         INCLUDING LEAP DAYS                GP06316
         AR    R3,R2         PLUS DAYS IN THIS YEAR             GP06316
         LA    R3,6(,R3)     THIRD FINAGLE FACTOR               GP06316
         SLR   R2,R2         CLEAR FOR DIVIDE                   GP06316
         D     R2,=F'7'      GET DAY OF WEEK IN R2              GP06316
         MH    R2,=Y(L'DAYS) MAKE OFFSET IN TABLE               GP06316
         LA    R3,DAYS(R2)   POINT TO TEXT                      GP06316
         MVC   TOD1DAY(L'DAYS),0(R3)   DAY TO OUTPUT LOCATION   GP06316
         MVC   0(22,R5),TOD1DATE                               1/93 RT
         MVC   0(9,R6),TOD1DAY                                 1/93 RT
         MVC   0(9,R7),TOD1TIME                                1/93 RT
         L     R13,4(,R13)                                     1/93 RT
         MVI   12(R13),X'FF'        INVALIDATE FWD-CHAIN       1/93 RT
         RETURN (14,12),RC=0                                   1/93 RT
         SPACE 1
DAYS     DAYS  LEFT                                                 THO
         SPACE 1                                                    THO
MONTHS   MONTHS                                                     THO
         LTORG
         DROP  R4
TODSAVE  DS    9D
TODDWT   DS    D                    PACKED TIME
TODDWD   DS    D                    PACKED DATE WORK FIELD
TODPDAT  DS    F                    PACKED DATE
         DC    CL8' '
TOD1DDT  DS    0CL40          DATE, DAY, TIME TAKEN TOGETHER
TOD1DATE DC    CL22' '
TOD1DAY  DC    CL9' '
TOD1TIME DC    CL9' '
         DC    CL8' '
         EJECT
         XYZZY NOGEN
         DCBD  DSORG=PS,DEVD=DA                                     THO
UCB      DSECT                                                      THO
         IEFUCBOB                                                   THO
         AIF   ('&DEBX' EQ 'YES').NEWDEB1                           SBG
         IEZDEB                                                     THO
         AGO   .OLDDEB1                                             SBG
.NEWDEB1 ANOP                                                       SBG
         IECTDEBX                                                   SBG
.OLDDEB1 ANOP                                                       SBG
         END   TAPEMAP
