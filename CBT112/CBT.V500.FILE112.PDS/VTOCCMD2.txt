PROC 0 DEBUG
 CONTROL NOLIST NOFLUSH NOMSG NOPROMPT
 IF &DEBUG NE THEN CONTROL LIST NOFLUSH MSG CONLIST SYMLIST PROMPT
 IF &SYSPREF EQ &STR() THEN +
   SET DSPRFX = &SYSUID
 ELSE +
   SET DSPRFX = &SYSPREF
 ISPEXEC CONTROL ERRORS RETURN
 SET AOR1 = &STR(AND)
 SET AOR2 = &STR(AND)
 SET AOR3 = &STR(AND)
RESTART: +
 ERROR OFF
 ISPEXEC DISPLAY PANEL(VTOC02)
 IF &LASTCC = 8 THEN EXIT
/*  SET STDOTH = &STR(PRINT(REP (EXP PASS)))   */
 SET STDOTH =
 FREE F(VTOCOUT)
 SET LASTCC = 0
 SET MAXCC = 0
 SET HOLD = &STR(NOHOLD)
 IF &C1 EQ Q THEN SET HOLD = &STR(HOLD)
 IF &DEST =  THEN SET DEST2 =
 ELSE SET DEST2 = &STR(DEST(&DEST))
 IF &VOLLIST EQ &STR() THEN SET VOLLIST = &STR(TSOS01)
 SET NUM = 1
 SET SWITCH = 0
 DO WHILE &SWITCH = 0
   LISTDSI '&DSPRFX..TEMP&NUM..VTOCLIST' NORECALL
   IF &LASTCC >= 4 AND &SYSREASON ¬= 9 THEN SET SWITCH = 1
   ELSE DO
     SET NUM = &NUM + 1
     IF &NUM GT 12 THEN DO
       SET &ZERRMSG = &STR(ALL &DSPRFX..TEMP#.VTOCLIST DATASETS FULL)
       ISPEXEC DISPLAY MSG(&ZERRMSG)
       EXIT CODE(12)
       END
     END
   END
 ERROR EXIT CODE(20)
 IF &P EQ P THEN ALLOC F(VTOCOUT) SYSOUT(&C1) &HOLD &DEST2
 ELSE ALLOC F(VTOCOUT) DA('&DSPRFX..TEMP&NUM..VTOCLIST') +
      CATALOG SPACE(5 5) +
      TRACKS NEW
 ERROR OFF
 ERROR DO
   ERROR OFF
   FREE F(VTOCOUT) DEL
   EXIT
   END
 ATTN  DO
   ERROR OFF
   FREE F(VTOCOUT) DEL
   END
 IF &LEVEL NE &STR( ) THEN +
   SET VLEVEL = &STR(LEVEL(&LEVEL))
 ELSE +
   SET VLEVEL = &STR()
 IF &EXLEVEL NE &STR( ) THEN +
   SET VEXLEVEL = &STR(EXLEVEL(&EXLEVEL))
 ELSE +
   SET VEXLEVEL = &STR()
 IF &CONTAIN NE &STR( ) THEN +
   SET VCONTAIN = &STR(CONTAINING(&CONTAIN))
 ELSE +
   SET VCONTAIN = &STR()
 IF &ENDING NE &STR( ) THEN +
    SET VENDING = &STR(ENDING(&ENDING))
 ELSE +
    SET VENDING = &STR()
 IF &SRTFLDS NE &STR( ) THEN +
   SET VSRTFLDS = &STR(SORT(&SRTFLDS))
 ELSE +
   SET VSRTFLDS = &STR()
 IF &SRTFLDS EQ &STR(NOSORT) THEN +
   SET VSRTFLDS = &STR(NOSORT)
 SET VLIMIT = &STR()
 SET VAND1 = &STR()
 SET VAND2 = &STR()
 SET VAND3 = &STR()
 IF &KEYWD  NE &STR( ) THEN +
     SET VLIMIT = &STR(LIMIT(&STR(&KEYWD) &STR(&OP) &STR(&VALUE)))
 IF &KEYWD1 NE &STR( ) THEN +
     SET VAND1 = &STR(&AOR1.1(&STR(&KEYWD1) &STR(&OP1) &STR(&VALUE1)))
 IF &KEYWD2 NE &STR( ) THEN +
     SET VAND2 = &STR(&AOR2.2(&STR(&KEYWD2) &STR(&OP2) &STR(&VALUE2)))
 IF &KEYWD3 NE &STR( ) THEN +
     SET VAND3 = &STR(&AOR3.3(&STR(&KEYWD3) &STR(&OP3) &STR(&VALUE3)))
 CONTROL MSG PROMPT
 IF    ¬= &SUBSTR(1,&OTHER ) THEN SET USEOTH = &STR(&OTHER)
 ELSE  SET USEOTH = &STR(&STDOTH)
 VTOCAR2 &VOLLIST HEADING('1 VTOC LIST OF &VOLLIST VOLUME(S)') +
  &VLEVEL +
  &VEXLEVEL +
  &VCONTAIN +
  &VENDING +
  &VSRTFLDS +
  &VLIMIT +
  &VAND1 +
  &VAND2 +
  &VAND3 +
  &USEOTH
 CONTROL NOMSG NOPROMPT
 FREE F(VTOCOUT)
 IF &P NE P THEN  ISPEXEC BROWSE DATASET('&DSPRFX..TEMP&NUM..VTOCLIST')
 IF &P EQ   THEN +
   DELETE  '&DSPRFX..TEMP&NUM..VTOCLIST'
 IF &P EQ K THEN DO
   CONTROL MSG
   RLSE  '&DSPRFX..TEMP&NUM..VTOCLIST'
   WRITE &DSPRFX..TEMP&NUM..VTOCLIST HAS BEEN SAVED
   CONTROL NOMSG
 END
 ERROR OFF
 ATTN OFF
 GOTO RESTART
 END
