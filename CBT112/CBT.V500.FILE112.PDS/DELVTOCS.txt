  /*  REXX exec to delete temp vtoc list datasets  */

  arg temp1
  do while temp1 ¬= ''
    parse var temp1 temp2 temp1
    if right(temp2,1) = ')' then
      temp2 = translate(temp2,'= ','()')
    if pos('=',temp2) = 0 then temp2 = temp2 || '= ""'
    interpret temp2
  end
  drop temp1
  drop temp2
  if debug = 'TRACE' then trace ?i
  else if debug ¬= "DEBUG" then trace i

  /* dsn=vtoclist for u.v    dsn=listcat for u.l  */
  if dsn = 'DSN' then dsn = 'VTOCLIST'

  say 'REXX exec to delete temporary' dsn 'datasets '
  if pref = 'PREF' then pref = sysvar(syspref)
  if pref = '' then pref = sysvar(sysuid)
  do i=1 to 12
    tempdsn = "'" || pref || ".TEMP" || i || "." || dsn || "'"
    temp = listdsi(tempdsn "NORECALL")
    select
      when temp = 0 then
        "DELETE" tempdsn "NONVSAM"
      when temp = 16 then
        select
          when sysreason = 9 then
            "HDELETE" tempdsn
          when sysreason = 5 then
            nop
          otherwise
            say tempdsn '-' sysreasn(sysreason)
        end
      otherwise do
        say 'Return code' temp 'for' tempdsn
        say tempdsn '-' sysreasn(sysreason)
      end
    end
  end
  exit 0
SYSREASN:
  procedure
 /* rexx exec to format listdsi sysreason codes */
  reason.0 = "normal completion"
  reason.1 = "error parsing the function"
  reason.2 = "dynamic allocation error (SVC 99 error)"
  reason.3 = "dataset type is not valid"
  reason.4 = "error determining UNIT name (IEFEB4UV error)"
  reason.5 = "dataset is not catalogued (LOCATE error)"
  reason.6 = "error obtaining dsn (OBTAIN macro error)"
  reason.7 = "error finding device type (DEVTYPE error)"
  reason.8 = "dataset is not on DASD"
  reason.9 = "dataset is migrated and NORECALL specified"
  reason.10= "10 is not a valid code - contact systems support"
  reason.11= "read access is required for full information"
  reason.12= "VSAM datasets are not supported"
  reason.13= "dataset could not be opened"
  reason.14= "device type is not found in UCB tables"
  reason.15= "15 is not a valid code - contact systems support"
  reason.16= "16 is not a valid code - contact systems support"
  reason.17= "ABEND occured"
  reason.18= "partial dataset information was obtained"
  reason.19= "dataset is multi-volume"
  reason.20= "device type not found in EDT"
  reason.21= "catalog error during LOCATE"
  reason.22= "volume is not mounted (OBTAIN error)"
  reason.23= "permanent I/O error on volume (OBTAIN error)"
  reason.24= "dataset is not found by OBTAIN macro"
  reason.25= "dataset is migrated to non-DASD"
  reason.26= "dataset is on MSS"
  reason.27= "no volser is allocated to the dataset"
  reason.28= "invalid DDNAME - must be 1 to 8 characters"
  reason.29= "dataset name or DDNAME must be specified"
  temp = arg(1)
  temp = temp+0
  return reason.temp
