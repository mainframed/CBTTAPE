/* REXX FileList */
true = "1"; false = "0";  /* switches */
refresh_flag = false; /* no refresh */
sname_flag = false; /* dont sort by name */
sttr_flag = false; /* dont sort by ttr */
sdate_flag = false; /* dont sort by date */
ssize_flag = false; /* dont sort by size */
scommand = "S"; /* current meaning of the select command */
parse upper arg filter;
address "ISPEXEC"; /* ISPF/PDF services */
if filter ¬= "" & , /* filter is dsname */
   (pos(".",filter)¬=0 | substr(filter,1,1)="'") then do;
        parse var filter dsname "(" filter ")"; /* components */
        if substr(dsname,1,1)="'" then ,
            parse var dsname "'" dsname "'"; /* strip quotes */
        end;
  else dsname = userid()".USER.CNTLLIB"; /* personal PDS */
if filter¬="" & pos("*",filter)=0 then , /* 1 member only */
    member_count = 1;
  else member_count = 0; /* code for more than 1 member */
/* Sysrecfm set */
dummy = listdsi("'"dsname"'"); /* recfm ? */
if dummy ¬= 0 then do;
       zedsmsg = "Invalid dsname";
       zedlmsg = "Invalid dsname or dataset not currently available.";
       signal error_exit;
       end;
if sysrecfm ¬= "U" then sdate_flag = true;
    else ssize_flag = true;
refresh:
"LMINIT DATAID(FLISTID) DATASET('"dsname"') ENQ(SHRW)";
if rc ¬= 0 then do;
       zedsmsg = "Invalid dsname";
       zedlmsg = "Invalid dsname or dataset not currently available.";
       signal error_exit;
       end;
"LMOPEN DATAID("flistid")";
if rc ¬= 0 then do;
      "LMFREE DATAID("flistid")";
       zedsmsg = "Open failure";
       zedlmsg = "LMOPEN failure; dataset cannot be opened.";
       signal error_exit;
       end;
if ssize_flag then ,
    zcmd = "SORT SIZE NAME"; /* sort by size */
if sttr_flag then ,
    zcmd = "SORT TTR"; /* sort by ttr */
if sdate_flag then ,
    zcmd = "SORT CHA NAME"; /* sort by time stamp */
if sname_flag then ,
    zcmd = "SORT NAME"; /* sort by name */
"CONTROL NONDISPL ENTER"; /* non display mode first panel */
if rc ¬= 0 then do;
      "LMCLOSE DATAID("flistid")";
      "LMFREE DATAID("flistid")";
      "LMFREE DATAID("flistid")";
       zedsmsg = "Control failure";
       zedlmsg = "Control failure; filelist aborted.";
       signal error_exit;
       end;
if filter = "" then do;
      "LMMDISP DATAID("flistid") COMMANDS(ANY)";
      if rc > 8 then do;
          "LMCLOSE DATAID("flistid")";
          "LMFREE DATAID("flistid")";
          zedsmsg = "Memlist failure";
          zedlmsg = "LMMDISP failure; filelist aborted.";
          signal error_exit;
          end;
      else if rc = 8 then signal finis;
      else if rc = 4 then do;
             zedsmsg = "No members found";
             zedlmsg = "No members meet the selection criteria.";
             signal error_exit;
             end;
      end;
else do;
      "LMMDISP DATAID("flistid") MEMBER("filter") COMMANDS(ANY)";
      if rc > 8 then do;
          "LMCLOSE DATAID("flistid")";
          "LMFREE DATAID("flistid")";
          zedsmsg = "Memlist failure";
          zedlmsg = "LMMDISP failure; filelist aborted.";
          signal error_exit;
          end;
      else if rc = 8 then signal finis;
      else if rc = 4 then do;
             zedsmsg = "No members found";
             zedlmsg = "No members meet the selection criteria.";
             signal error_exit;
             end;
      end;
process:
selected = true;
do while(selected);
  upper zcmd;
  zcmd = strip(zcmd);
  zllcmd = strip(zllcmd); /* cleanup */
  if zllcmd = "S" then zllcmd = scommand; /* substitute */
  zlmember = strip(zlmember);
  zludata = strip(zludata);
  if zllcmd = "=" then zllcmd = lastcmd; /* repeat last command */
  select;
     when ((zllcmd = "E" | zllcmd = "X") & sysrecfm¬="U") then do;
        "EDIT DATAID("flistid") MEMBER("zlmember")";
        "LMMDISP DATAID("flistid") OPTION(PUT) MEMBER("zlmember")" ,
            "ZLUDATA(*Edited)";
        lastcmd = "E"; /* remember last command */
        end;
     when (zllcmd = "B" | zllcmd = "S") then do;
        "BROWSE DATAID("flistid") MEMBER("zlmember")";
        "LMMDISP DATAID("flistid") OPTION(PUT) MEMBER("zlmember")" ,
            "ZLUDATA(*Browsed)";
        lastcmd = "B"; /* remember last command */
        end;
     when (zllcmd = "D") then do;
        dummy = msg("OFF");
        address "TSO" "DELETE '"dsname"("zlmember")'";
        dummy = msg("ON");
        "LMMDISP DATAID("flistid") OPTION(PUT) MEMBER("zlmember")" ,
            "ZLUDATA(*Deleted)";
        lastcmd = "D"; /* remember last command */
        refresh_flag = true; /* refresh screen */
        end;
     when (zllcmd = "R") then do;
        dummy = msg("OFF");
        address "TSO" "RENAME '"dsname"("zlmember")'" ,
            "("zludata")";
        saverc = rc;
        dummy = msg("ON");
        if saverc = 0 then do;
        "LMMDISP DATAID("flistid") OPTION(PUT) MEMBER("zlmember")" ,
            "ZLUDATA(*Renamed)";
        if member_count = 1 then , /* member count in list */
             filter = zludata; /* refresh on new member name */
        refresh_flag = true; /* refresh screen */
        end;
        else ,
        "LMMDISP DATAID("flistid") OPTION(PUT) MEMBER("zlmember")" ,
            "ZLUDATA(*Name?)";
        lastcmd = "R"; /* remember last command */
        end;
     when (zllcmd = "") then nop;
     otherwise do;
        "LMMDISP DATAID("flistid") OPTION(PUT) MEMBER("zlmember")" ,
            "ZLUDATA(*?)";
       end;
  end;
  "LMMDISP DATAID("flistid") OPTION(GET)";
  disprc = rc;
  select;
    when disprc = 0 then nop; /* loop and do next member */
    when disprc = 8 then selected = false; /* redisplay screen */
    otherwise do;
          "LMCLOSE DATAID("flistid")";
          "LMFREE DATAID("flistid")";
          zedsmsg = "Memlist failure";
          zedlmsg = "LMMDISP failure; filelist aborted.";
          signal error_exit;
          end;
  end;
end;
scommand = "S"; /* reset */
select; /* zcmd */
   when (zcmd = "REFRESH") then refresh_flag = true; /* refresh */
   when (zcmd = "SNAME") then do;
           sname_flag = true;
           sdate_flag = false;
           ssize_flag = false;
           sttr_flag = false;
           refresh_flag = true;
           end;
   when (zcmd = "SDATE" & sysrecfm¬="U") then do;
           sdate_flag = true;
           sname_flag = false;
           ssize_flag = false;
           sttr_flag = false;
           refresh_flag = true;
           end;
   when (zcmd = "SSIZE") then do;
           sdate_flag = false;
           sname_flag = false;
           ssize_flag = true;
           sttr_flag = false;
           refresh_flag = true;
           end;
   when (zcmd = "STTR" & sysrecfm="U") then do;
           sdate_flag = false;
           sname_flag = false;
           ssize_flag = false;
           sttr_flag = true;
           refresh_flag = true;
           end;
   when (substr(zcmd,1,2)="B ") then do;
           scommand = "B"; /* simulate browse */
           zcmd = "S "substr(zcmd,3); /* simulate entry */
           "CONTROL NONDISPL ENTER"; /* non display mode first panel */
           "LMMDISP DATAID("flistid")"; /* simulate redisplay */
           signal process;
           end;
   when (substr(zcmd,1,2)="E ") then do;
           scommand = "E"; /* simulate edit */
           zcmd = "S "substr(zcmd,3); /* simulate entry */
           "CONTROL NONDISPL ENTER"; /* non display mode first panel */
           "LMMDISP DATAID("flistid")"; /* simulate redisplay */
           signal process;
           end;
   when (substr(zcmd,1,2)="X ") then do;
           scommand = "X"; /* simulate edit */
           zcmd = "S "substr(zcmd,3); /* simulate entry */
           "CONTROL NONDISPL ENTER"; /* non display mode first panel */
           "LMMDISP DATAID("flistid")"; /* simulate redisplay */
           signal process;
           end;
   when (substr(zcmd,1,2)="D ") then do;
           scommand = "D"; /* simulate edit */
           zcmd = "S "substr(zcmd,3); /* simulate entry */
           "CONTROL NONDISPL ENTER"; /* non display mode first panel */
           "LMMDISP DATAID("flistid")"; /* simulate redisplay */
           signal process;
           end;
   otherwise nop;
end;
if refresh_flag then do;
     refresh_flag = false;
     "LMMDISP DATAID("flistid") OPTION(FREE)"; /* clean up */
     "LMCLOSE DATAID("flistid")";
     "LMFREE DATAID("flistid")";
     signal refresh;
     end;
"LMMDISP DATAID("flistid")";
      if rc > 8 then do;
          "LMCLOSE DATAID("flistid")";
          "LMFREE DATAID("flistid")";
          zedsmsg = "Memlist failure";
          zedlmsg = "LMMDISP failure; filelist aborted.";
          signal error_exit;
          end;
      else if rc > 0 then signal finis;
signal process;
error_exit:
"SETMSG MSG(ISRZ001)"; /* display error message */
exit 0;
finis:
"LMMDISP DATAID("flistid") OPTION(FREE)"; /* clean up */
"LMCLOSE DATAID("flistid")";
"LMFREE DATAID("flistid")";
exit 0;
