/* REXX EXEC : ( CCFJRNAL ).
   FUNCTION  : THIS EXEC IS INVOKED DURING CCF TURNOVER PROCESSING
               FROM CCF SKELETON ( $CCFJ004 ) TO CHECK FOR TARGET "PROD"
               LIBRARIAN MOVEMENT.
               THE FUNCTION OF THIS EXEC IS TO WRITE A JOURNAL RECORD
               ENTRY TO TRACK ALL PRODUCTION MODULES THAT ARE TURNED
               OVER DURING A 24 HOUR PERIOD.
               JOURNAL RECORD ENTRIES ARE WRITTEN TO A PDS MEMBER WHICH
               IS MAINTAINED BY JULIAN DATE.
   INPUT     : ( SYSS.CCF.OSJOB.CNTLLIB(&MODULENM)).
   OUTPUT    : ( SYSS.CCF.TURNOVER.JOURNAL ).
                                                                      */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
ARG MASTER MODULE WORKORDR LOADLIB
PARMS = 'MASTER MODULE WORKORDR'
DO I = 1 TO 3
   IPARM = WORD(PARMS,I)
   XPARM = VALUE(IPARM)
   IF XPARM ¬= '' THEN ITERATE
   SAY '*** PARM ERROR: MISSING PARM VALUE FOR ('IPARM'). ***'
   SAY '*** CCFJRNAL EXEC TASK CANCELLED - RC = (024). ***'
   EXIT 024
END
PRDLIB = POS('PROD',MASTER)
IF PRDLIB = 0 THEN EXIT 0
JULDATE = DATE(J)
PDSMEMB = "J"||JULDATE
CDATE = DATE(U)
CTIME = TIME()
NOMEMB = 'MEMBER NOT FOUND'
OSJERR = 'DATASET NOT FOUND'
OSJOBIN = 'SYSS.CCF.OSJOB.CNTLLIB'
JOURLIB = 'SYSS.CCF.TURNOVER.JOURNAL'
MEMBER = MODULE
MASTER = LEFT(MASTER,21)
MODULE = LEFT(MODULE,8)
HEADER1 = 'DATE     TIME     WORKORDR CCFID   PROD LIBRARIAN MASTER MODULE   LAN
GUAGE'
HEADER2 = '-------- -------- -------- ------- --------------------- -------- ---
-----'

X = MSG("OFF")
"FREE F(JOURDD)"
"FREE F(OSJOBDD)"
MASTER_NODE = 'PRODCOPY PRODJCL PRODPROC PRODRAMI PRODSYSI'
MODULE_LANG = 'COPYBOOK JCL PROC RAMIS SYSIN'
/*     */
MAIN_ROUTINE:
CALL ALLOC_OSJOB
INTERPRET CALL LABEL
CALL GET_TURNOVER_PROGRAMMER
CALL GET_MODULE_LANGUAGE
CALL ALLOC_JOURNAL
RECORD = CDATE CTIME WORKORDR PROGRAMMER MASTER MODULE LANGUAGE
INTERPRET CALL LABEL
IF WRITESW = 'ERROR' THEN CALL JOURNAL_WRITE_ERROR
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
ALLOC_OSJOB:
LABEL = 'NOOP_ROUTINE'
DSN = SYSDSN("'"OSJOBIN"'")
IF DSN = 'OK' THEN DO
   "ALLOC DA('"OSJOBIN"("MEMBER")') F(OSJOBDD) SHR"
   "EXECIO * DISKR OSJOBDD (FINIS STEM DATA."
   SRC = RC
   "FREE F(OSJOBDD)"
   IF SRC ¬= 0 THEN LABEL = 'OSJOB_DATASET_READ_ERROR'
   RETURN
END
LABEL = 'OSJOB_DATASET_ERROR'
RETURN

/*     */
GET_TURNOVER_PROGRAMMER:
DO I = 1 TO DATA.0
   CCFID = POS('-PGMR',DATA.I)
   IF CCFID ¬= 0 THEN DO
      PROGRAMMER = SUBSTR(DATA.I,8,7)
      LEAVE
   END
END
RETURN

/*     */
GET_MODULE_LANGUAGE:
MNODE = POS('PRODSRCE',MASTER)
IF MNODE ¬= 0 THEN DO
   LANGUAGE = 'BATCH SRCE'
   LNODE = POS('CICS',LOADLIB)
   IF LNODE ¬= 0 THEN LANGUAGE = 'CICS SRCE'
   RETURN
END
DO I = 1 TO 5
   MNODE = WORD(MASTER_NODE,I)
   LNODE = POS(MNODE,MASTER)
   IF LNODE ¬= 0 THEN LANGUAGE = WORD(MODULE_LANG,I)
END
RETURN

/*     */
ALLOC_JOURNAL:
LABEL = 'JOURNAL_DATASET_ERROR'
DSN = SYSDSN("'"JOURLIB"("PDSMEMB")'")
IF DSN = 'OK' THEN DO
   "ALLOC DA('"JOURLIB"("PDSMEMB")') F(JOURDD) SHR"
   "DELSTACK"
   "EXECIO * DISKR JOURDD (FINIS"
   RCDCNT = QUEUED()
   LABEL = 'REWRITE_JOURNAL'
END
IF DSN = NOMEMB THEN DO
   "ALLOC DA('"JOURLIB"("PDSMEMB")') F(JOURDD) SHR"
   LABEL = 'WRITE_JOURNAL'
END
RETURN

/*     */
WRITE_JOURNAL:
WRITESW = 'OK'
"DELSTACK"
QUEUE HEADER1
QUEUE HEADER2
QUEUE RECORD
"EXECIO 3 DISKW JOURDD (FINIS"
SRC = RC
"FREE F(JOURDD)"
IF SRC ¬= 0 THEN WRITESW = 'ERROR'
RETURN

/*     */
REWRITE_JOURNAL:
WRITESW = 'OK'
RCDCNT = RCDCNT + 1
QUEUE RECORD
"EXECIO" RCDCNT "DISKW JOURDD (FINIS"
SRC = RC
"FREE F(JOURDD)"
IF SRC ¬= 0 THEN WRITESW = 'ERROR'
RETURN

/*     */
NOOP_ROUTINE:
RETURN

/*     E R R O R   R O U T I N E S   S E C T I O N     */
/*     */
OSJOB_DATASET_ERROR:
SAY '*** OSJOB DATASET ERROR: 'DSN' ('OSJOBIN'). ***'
SAY '*** JOURNAL RECORD ENTRIES CANNOT BE WRITTEN. ***'
SAY '*** CCFJRNAL EXEC TASK CANCELLED. ***'
EXIT 100

/*     */
OSJOB_DATASET_READ_ERROR:
SAY '*** OSJOB DATASET READ ERROR: ('OSJOBIN'). ***'
SAY '*** JOURNAL RECORD ENTRIES CANNOT BE WRITTEN. ***'
SAY '*** CCFJRNAL EXEC TASK CANCELLED. ***'
EXIT SRC

/*     */
JOURNAL_DATASET_ERROR:
SAY '*** JOURNAL DATASET ERROR: 'DSN' ('JOURLIB'). ***'
SAY '*** JOURNAL RECORD ENTRIES CANNOT BE WRITTEN. ***'
SAY '*** CCFJRNAL EXEC TASK CANCELLED. ***'
EXIT 110

/*     */
JOURNAL_WRITE_ERROR:
SAY '*** JOURNAL WRITE ERROR: ('JOURLIB'('PDSMEMB'). ***'
SAY '*** JOURNAL RECORD ENTRIES NOT WRITTEN. ***'
SAY '*** CCFJRNAL EXEC TASK CANCELLED. ***'
EXIT SRC
