PROC 0 DEBUG() MASTLIB() DAYLIB() JCLLIB(NO)
/*******************************************************/
/**  P.P.F. - PARAMETERS PROMPTING FACILITY - TYPE2   **/
/**                                                   **/
/**  THE CLIST ALLOCATE A DAILY LIBRARY AND TAKES THE **/
/**  ALLOCATION PARAMETERS FROM THE MASTER LIBRARY.   **/
/**                                                   **/
/**  AFTER ALLOCATING THE DAILY JCL LIBRARY, ALL THE  **/
/**  MEMBERS ARE COPIED FROM THE MASTER JCL LIBRARY   **/
/**  TO THE DAILY LIBRARY.                            **/
/**                                                   **/
/*******************************************************/
 ISPEXEC CONTROL ERRORS RETURN
 IF &DEBUG = DEBUG +
 THEN +
    CONTROL LIST MSG SYMLIST CONLIST
 ELSE +
    CONTROL NOLIST NOMSG

 ALLOC FILE(TEMPALC) DA('&DAYLIB') LIKE('&MASTLIB') NEW CATALOG
 SET &RETCODE = &LASTCC
 FREE FILE(TEMPALC)

 IF &RETCODE ¬= 0 +
 THEN +
    GOTO END

 IF &JCLLIB ¬= YES +
 THEN +
    GOTO END

 /* COPY THE MASTER JCL LIBRARY TO THE DAILY JCL LIBRARY */

 ISPEXEC LMINIT DATAID(FROMID) DATASET('&MASTLIB') ENQ(SHR)
 SET &RETCODE = &LASTCC

 IF &RETCODE ¬= 0  +
 THEN +
    GOTO END

 ISPEXEC LMINIT DATAID(TOID) DATASET('&DAYLIB') ENQ(SHR)
 SET &RETCODE = &LASTCC

 IF &RETCODE ¬= 0  +
 THEN +
    GOTO BEFEND

 ISPEXEC LMOPEN DATAID(&FROMID) OPTION(INPUT)
 SET &RETCODE = &LASTCC

 IF &RETCODE ¬= 0  +
 THEN +
    GOTO BEFEND

 ISPEXEC LMCOPY FROMID(&FROMID) TODATAID(&TOID) +
         FROMMEM(*)
 SET &RETCODE = &LASTCC

 BEFEND: ISPEXEC LMFREE DATAID(&FROMID)
         ISPEXEC LMFREE DATAID(&TOID)

 END: EXIT CODE(&RETCODE)
