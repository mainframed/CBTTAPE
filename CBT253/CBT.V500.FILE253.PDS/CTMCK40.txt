PROC 0 DEBUG(X)
CONTROL MAIN NOLIST NOCONLIST NOSYMLIST NOMSG
IF &DEBUG=DEBUG THEN CONTROL MAIN LIST CONLIST SYMLIST MSG
ISPEXEC CONTROL ERRORS RETURN

/********************************************************/
/**                                                    **/
/**    CONTROL-M QUICK SCHEDULE DEFINITION             **/
/**                                                    **/
/**    CLIST CTMCK40  - PRODUCE SCHED. TABLE BY        **/
/**    MERGING THE SKELETON AND THE JOB TABLE          **/
/**                                                    **/
/********************************************************/

SET &NUL = &STR()
SET &BLK44 = &STR(                                            )
  /* THE ABOVE CARD HAS 44 BLANKS - LEAVE IT ALONE  */

ISPEXEC VGET (OLIB OTABLE MFOUND)
ISPEXEC VGET (GRP SGRP  OPT SF)

ISPEXEC VGET (CARDD CARDM CARDL CARDH CARDO CARDZ CARDV)
ISPEXEC VGET -
          (NECARD NQCARD NRCARD NPCARD NCCARD NTCARD NBCARD NSHOUT)
ISPEXEC VGET (NWCARD)

ISPEXEC VGET (ZTEMPN) SHARED
SET &TEMPNAME = CTM#&EVAL(&SYSSRV//10000)
ISPEXEC VPUT (TEMPNAME)

/********************************************************/
/**    SET UP ALL NON JOB-DEPENDENT DATA IN CARD IMAGES**/
/********************************************************/

/********************/
/**    SHOUT CARD  **/
/********************/

SET &SVAR = &NUL
SET &NDX = 1
DO WHILE &NDX <= &NSHOUT
   ISPEXEC VGET (CARDS&NDX)
   SET &TEMPU = &SYSNSUB(2,&&CARDS&NDX)
   SET &SVAR = &NRSTR(&SVAR)&SUBSTR( +
        2:80,&NRSTR(&TEMPU)&STR(&BLK44)&STR(&BLK44))
   SET &NDX = &NDX + 1
END

SET &SCOL = 0
SET &NACHUZ = 0
SET &LSVAR = &LENGTH(&NRSTR(&SVAR))
DO WHILE (&SCOL < &LSVAR-8)
  SET &SCOL = &SCOL + 1
  IF  &SUBSTR(&SCOL:&SCOL+7,&NRSTR(&SVAR)) = %%JOBNAM THEN +
    DO
      SET &NACHUZ = &NACHUZ + 1
      SET &ACHUZ&NACHUZ = &SCOL
      SET &SCOL = &SCOL + 8
      IF (SCOL < &LSVAR) THEN +
        IF &SUBSTR(&SCOL:&SCOL,&NRSTR(&SVAR)) = E THEN SET &NACHUZ=0
    END
END

/********************/
/**    DATES CARD  **/
/********************/
ISPEXEC VGET (CARDD)

/***********************/
/**    WEEKDAYS CARD  **/
/***********************/
ISPEXEC VGET (NWCARD)
IF (&NWCARD=1) THEN +
        ISPEXEC VGET (CARDW)

/********************************************************/
/**    INITIALIZE TO TOP OF THE TABLE                  **/
/********************************************************/

ISPEXEC CONTROL ERRORS RETURN
ISPEXEC TBTOP     CTMPKT00


/********************************************************/
/**    FOR OPT 2 AND 3, CREATE OUTCOND TABLE           **/
/********************************************************/

IF ((&OPT = 2) OR (&OPT=3)) THEN +
  DO
    ISPEXEC TBTOP CTMPKT00
    SET &MADE = 0
    DO WHILE (&MADE = 0)
      ISPEXEC TBSKIP CTMPKT00
      SET &RC = &LASTCC
      IF &RC = 8 THEN SET &MADE = 1
      IF (&MADE = 0) THEN +
        DO
          SET &NDX = 1
          SET &TOJOB = &NRSTR(&JNAM)
          DO WHILE (&NDX <= &NUMDEPS)

            SET &FRJOBNAM = &NUL
            SET &FRJOBNUM = 0

            SET &TY = &&T&NDX
            SET &TY = &TY

            IF &TY¬=C THEN +
              DO
                IF &TY = M THEN SET &FRJOBNUM = &JNUM - 1
                ELSE IF &TY = N THEN +
                     SET &FRJOBNUM = &SYSNSUB(2,&&DJ&NDX)
                ELSE IF &TY = A THEN +
                     SET &FRJOBNAM = &SYSNSUB(2,&&DJ&NDX)
                ISPEXEC TBADD CTMPKT99
              END /* NOT A TYPE C DEPENDENCE */

            SET &NDX = &NDX + 1
          END /* LOOPING OVER DEPENDENCIES */
        END  /* OF A ROW IN THE TABLE */
    END  /* MAKING THE TABLE */
  END


/********************************************************/
/**    OPEN THE FILE  FOR OUTPUT                       **/
/********************************************************/

ISPEXEC LMFREE DATAID(&CTMPKOT)  /* IN CASE SOMETHING THERE */

SET &IOERROR=Y
SET &CTMPKOT=&NUL
ISPEXEC VPUT (CTMPKOT IOERROR)

ISPEXEC LMINIT  DATAID(CTMPKOT) DATASET('&OLIB') ENQ(SHRW)
SET &RC = &LASTCC
IF &RC ¬= 0 THEN +
   DO
     ISPEXEC SETMSG MSG(CTMK003Z)
     EXIT QUIT
   END

ISPEXEC VPUT (CTMPKOT)

ISPEXEC LMOPEN  DATAID(&CTMPKOT) OPTION(OUTPUT)
SET &RC = &LASTCC
IF &RC ¬= 0 THEN +
   DO
     ISPEXEC SETMSG MSG(CTMK003Z)
     EXIT QUIT
   END


SET &DONE = 0
SET &JNAM = &NUL
DO WHILE (&DONE = 0)
/********************************************************/
/**    GET NEXT LINE OF THE TABLE                      **/
/********************************************************/
    SET &PREVJOB = &NRSTR(&JNAM)
    ISPEXEC TBSKIP CTMPKT00 POSITION(SAVEROW)
    SET &RC = &LASTCC
    IF &RC = 8 THEN +
      DO
         SET &DONE = 1    /* END OF THE TABLE WAS REACHED */
      END

    IF (Z&NRSTR(&JNAM) ¬= Z) AND (&DONE=0)  THEN +
      DO

      /**********************************************/
      /**    CREATE SCHED TABLE ENTRY FOR JOB      **/
      /**********************************************/

       SET &PADJOB = &STR(&SUBSTR(1:8,&NRSTR(&JNAM)&BLK44))

      /********************/
      /**  MEMBERS CARD  **/
      /********************/

       IF (&NRSTR(&GRP)¬=&NUL) THEN +
       SET &TEMPU=M&NRSTR(&PADJOB)&SUBSTR(1:20,&NRSTR(&GRP)&STR(&BLK44))
       ELSE +
         SET &TEMPU=M&NRSTR(&PADJOB)&SUBSTR(10:29,&NRSTR(&CARDM))
       SET &CARDM = &NRSTR(&TEMPU)&SUBSTR(30:80,&NRSTR(&CARDM))

      /********************/
      /**  DOCUMENT CARD **/
      /********************/

       SET &CARDZ = Z&NRSTR(&PADJOB)&SUBSTR(10:80,&NRSTR(&CARDZ))

      /********************/
      /**  DESCRIPTION   **/
      /********************/

       SET &TEMPU = H&SUBSTR(1:50,&NRSTR(&DESC)&STR(&BLK44)&STR(&BLK44))
       SET &CARDH = &NRSTR(&TEMPU)&SUBSTR(52:80,&NRSTR(&CARDH))

      /********************/
      /**  SHOUT    CARD **/
      /********************/

       SET &ACHUZX = 1
       DO WHILE (&ACHUZX <= &NACHUZ)
         SET &ACHUZCOL = &&ACHUZ&ACHUZX
         SET &TEMPU = &STR(&SUBSTR(  +
               1:&ACHUZCOL-1,&NRSTR(&SVAR))&NRSTR(&PADJOB)
         SET &TEMPU = &NRSTR(&TEMPU)&STR(&SUBSTR( +
               &ACHUZCOL+8:&LSVAR,&NRSTR(&SVAR)))
         SET &SVAR  = &NRSTR(&TEMPU)
         SET &ACHUZX = &ACHUZX + 1
       END

      /************************/
      /**  WRITE D,M,L,H,V   **/
      /************************/

      ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
         DATALOC(CARDD) DATALEN(80)
      SET &RC = &LASTCC
      IF &RC ¬= 0 THEN +
         DO
           ISPEXEC SETMSG MSG(CTMK003Z)
           EXIT QUIT
         END

      IF (&NWCARD¬=0) THEN +
        DO
          ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
             DATALOC(CARDW) DATALEN(80)
          SET &RC = &LASTCC
          IF &RC ¬= 0 THEN +
             DO
               ISPEXEC SETMSG MSG(CTMK003Z)
               EXIT QUIT
             END
        END

      ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
         DATALOC(CARDM) DATALEN(80)
      SET &RC = &LASTCC
      IF &RC ¬= 0 THEN +
         DO
           ISPEXEC SETMSG MSG(CTMK003Z)
           EXIT QUIT
         END

      ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
         DATALOC(CARDL) DATALEN(80)
      SET &RC = &LASTCC
      IF &RC ¬= 0 THEN +
         DO
           ISPEXEC SETMSG MSG(CTMK003Z)
           EXIT QUIT
         END

      ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
         DATALOC(CARDH) DATALEN(80)
      SET &RC = &LASTCC
      IF &RC ¬= 0 THEN +
         DO
           ISPEXEC SETMSG MSG(CTMK003Z)
           EXIT QUIT
         END

      ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
         DATALOC(CARDV) DATALEN(80)
      SET &RC = &LASTCC
      IF &RC ¬= 0 THEN +
         DO
           ISPEXEC SETMSG MSG(CTMK003Z)
           EXIT QUIT
         END

      /************************/
      /**  WRITE T CARDS      */
      /************************/

      SET &NDX = 1
      DO WHILE &NDX <= &NTCARD
        ISPEXEC VGET (CARDT&NDX)
        ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
           DATALOC(CARDT&NDX) DATALEN(80)
        SET &RC = &LASTCC
        IF &RC ¬= 0 THEN +
           DO
             ISPEXEC SETMSG MSG(CTMK003Z)
             EXIT QUIT
           END
        SET &NDX = &NDX + 1
      END

      /************************/
      /**  WRITE Z CARD       */
      /************************/

      ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
         DATALOC(CARDZ) DATALEN(80)
      SET &RC = &LASTCC
      IF &RC ¬= 0 THEN +
         DO
           ISPEXEC SETMSG MSG(CTMK003Z)
           EXIT QUIT
         END

      /************************/
      /**  CREATE I CARD(S)  **/
      /************************/

      SET &NDX = 1
      DO  WHILE &NDX <=  &NUMDEPS

         SET &TJOB = &&T&NDX

         IF &TJOB = C THEN +
           SET &TEMPU=&SUBSTR(2:21,&NRSTR(&XCOND)&NRSTR(&BLK44))
         ELSE +
           DO

             IF &TJOB = M THEN SET &DJOB = &NRSTR(&PREVJOB)

             IF &TJOB = A THEN SET &DJOB = &SYSNSUB(2,&&DJ&NDX)

             IF &TJOB = N THEN +
               DO
                 SET &DJOB = &&DJ&NDX
                 SET &DJOB = &DJOB
                 ISPEXEC TBTOP CTMPKT00
                 ISPEXEC TBSKIP CTMPKT00 NUMBER(&DJOB)
                 SET &DJOB = &NRSTR(&JNAM)
                 ISPEXEC TBTOP CTMPKT00
                 ISPEXEC TBSKIP CTMPKT00 NUMBER(&SAVEROW)
               END

             IF &OPT=1 THEN +
     SET &TEMPU = &NRSTR(&SGRP)&STR(-)&NRSTR(&DJOB)&STR(-)&NRSTR(&SF)

             IF &OPT=2 THEN +
     SET &TEMPU = &NRSTR(&DJOB)&STR(-)&NRSTR(&JNAM)&STR(-)&NRSTR(&SF)

             IF &OPT=3 THEN +
     SET &TEMPU = &NRSTR(&SF)&STR(-)&NRSTR(&DJOB)&STR(-)&NRSTR(&JNAM)

           END /* NOT AN ASTERISK  */

         SET &TEMPU = &STR(&SUBSTR(1:20,&NRSTR(&TEMPU)&STR(&BLK44)))
         SET &CARDI = I&NRSTR(&TEMPU)&STR(ODAT)

         ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
            DATALOC(CARDI) DATALEN(80)
         SET &RC = &LASTCC
         IF &RC ¬= 0 THEN +
            DO
              ISPEXEC SETMSG MSG(CTMK003Z)
              EXIT QUIT
            END

         SET &NDX = &NDX + 1
      END  /* LOOP OVER SUBFIELDS */


      /************************/
      /**  WRITE E AND Q CARDS*/
      /************************/

      SET &NDX = 1
      DO WHILE &NDX <= &NECARD
        ISPEXEC VGET (CARDE&NDX)
        ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
           DATALOC(CARDE&NDX) DATALEN(80)
        SET &RC = &LASTCC
        IF &RC ¬= 0 THEN +
           DO
             ISPEXEC SETMSG MSG(CTMK003Z)
             EXIT QUIT
           END
        SET &NDX = &NDX + 1
      END

      SET &NDX = 1
      DO WHILE &NDX <= &NQCARD
        ISPEXEC VGET (CARDQ&NDX)
        ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
           DATALOC(CARDQ&NDX) DATALEN(80)
        SET &RC = &LASTCC
        IF &RC ¬= 0 THEN +
           DO
             ISPEXEC SETMSG MSG(CTMK003Z)
             EXIT QUIT
           END
        SET &NDX = &NDX + 1
      END

      /********************/
      /**  OUT-COND CARDS**/
      /********************/

       IF &OPT = 1 THEN +
         DO
     SET &TEMPU = &NRSTR(&SGRP)&STR(-)&NRSTR(&JNAM)&STR(-)&NRSTR(&SF)
           SET &CARDO = +
             O&STR(&SUBSTR(1:20,&NRSTR(&TEMPU)&STR(&BLK44))ODAT&STR(+)
           ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
              DATALOC(CARDO) DATALEN(80)
           SET &RC = &LASTCC
           IF &RC ¬= 0 THEN +
              DO
                ISPEXEC SETMSG MSG(CTMK003Z)
                EXIT QUIT
              END
         END

       IF ((&OPT=2) OR (&OPT=3)) THEN +
         DO

           SET &FOUND = 1
           DO WHILE (&FOUND=1)
             SET &FRJOBNAM = &NRSTR(&JNAM)
             ISPEXEC TBTOP CTMPKT99
           ISPEXEC TBSCAN CTMPKT99 ARGLIST(FRJOBNAM) CONDLIST(EQ)
             SET &RC = &LASTCC
             IF &RC = 8 THEN SET &FOUND = 0
             ELSE  +
               DO
                 IF &OPT = 2 THEN +
     SET &TEMPU=&NRSTR(&JNAM)&STR(-)&NRSTR(&TOJOB)&STR(-)&NRSTR(&SF)
                 IF &OPT = 3 THEN +
     SET &TEMPU=&NRSTR(&SF)&STR(-)&NRSTR(&JNAM)&STR(-)&NRSTR(&TOJOB)
                 SET &CARDO = +
             O&STR(&SUBSTR(1:20,&NRSTR(&TEMPU)&STR(&BLK44))ODAT&STR(+)
                 ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
                    DATALOC(CARDO) DATALEN(80)
                 SET &RC = &LASTCC
                 IF &RC ¬= 0 THEN +
                    DO
                      ISPEXEC SETMSG MSG(CTMK003Z)
                      EXIT QUIT
                    END
                 ISPEXEC TBDELETE CTMPKT99
               END   /* WROTE AN O CARD */
           END

           SET &FOUND = 1
           DO WHILE (&FOUND=1)
             SET &FRJOBNUM = &JNUM
             ISPEXEC TBTOP CTMPKT99
           ISPEXEC TBSCAN CTMPKT99 ARGLIST(FRJOBNUM) CONDLIST(EQ)
             SET &RC = &LASTCC
             IF &RC = 8 THEN SET &FOUND = 0
             ELSE  +
               DO
                 IF &OPT = 2 THEN +
     SET &TEMPU=&NRSTR(&JNAM)&STR(-)&NRSTR(&TOJOB)&STR(-)&NRSTR(&SF)
                 IF &OPT = 3 THEN +
     SET &TEMPU=&NRSTR(&SF)&STR(-)&NRSTR(&JNAM)&STR(-)&NRSTR(&TOJOB)
                 SET &CARDO = +
             O&STR(&SUBSTR(1:20,&NRSTR(&TEMPU)&STR(&BLK44))ODAT&STR(+)
                 ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
                    DATALOC(CARDO) DATALEN(80)
                 SET &RC = &LASTCC
                 IF &RC ¬= 0 THEN +
                    DO
                      ISPEXEC SETMSG MSG(CTMK003Z)
                      EXIT QUIT
                    END
                 ISPEXEC TBDELETE CTMPKT99
               END   /* WROTE AN O CARD */
           END


         END

      /************************/
      /**WRITE B,R,P,C CARDS **/
      /************************/

      SET &NDX = 1
      DO WHILE &NDX <= &NBCARD
        ISPEXEC VGET (CARDB&NDX)
        SET &TEMPU = &&CARDB&NDX
        SET &TEMPU = &STR(&TEMPU)
        ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
           DATALOC(TEMPU) DATALEN(80)
        SET &RC = &LASTCC
        IF &RC ¬= 0 THEN +
           DO
             ISPEXEC SETMSG MSG(CTMK003Z)
             EXIT QUIT
           END
        SET &NDX = &NDX + 1
      END

      SET &NDX = 1
      DO WHILE &NDX <= &NRCARD
        ISPEXEC VGET (CARDR&NDX)
        SET &TEMPU = &&CARDR&NDX
        SET &TEMPU = &STR(&TEMPU)
        ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
           DATALOC(TEMPU) DATALEN(80)
        SET &RC = &LASTCC
        IF &RC ¬= 0 THEN +
           DO
             ISPEXEC SETMSG MSG(CTMK003Z)
             EXIT QUIT
           END
        SET &NDX = &NDX + 1
      END

      SET &NDX = 1
      DO WHILE &NDX <= &NPCARD
        ISPEXEC VGET (CARDP&NDX)
        ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
           DATALOC(CARDP&NDX) DATALEN(80)
        SET &RC = &LASTCC
        IF &RC ¬= 0 THEN +
           DO
             ISPEXEC SETMSG MSG(CTMK003Z)
             EXIT QUIT
           END
        SET &NDX = &NDX + 1
      END

      SET &NDX = 1
      DO WHILE &NDX <= &NCCARD
        ISPEXEC VGET (CARDC&NDX)
        ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
           DATALOC(CARDC&NDX) DATALEN(80)
        SET &RC = &LASTCC
        IF &RC ¬= 0 THEN +
           DO
             ISPEXEC SETMSG MSG(CTMK003Z)
             EXIT QUIT
           END
        SET &NDX = &NDX + 1
      END

      /************************/
      /**  WRITE SHOUT CARDS **/
      /************************/

      SET &NDX = 1
      DO WHILE &NDX <= &NSHOUT
        SET &CEND = 79 * &NDX
        SET &CBEG = &CEND-78
        SET &OUTSHT = S&SUBSTR(&CBEG:&CEND,&NRSTR(&SVAR))
        ISPEXEC LMPUT DATAID(&CTMPKOT) MODE(INVAR) +
           DATALOC(OUTSHT) DATALEN(80)
        SET &RC = &LASTCC
        IF &RC ¬= 0 THEN +
           DO
             ISPEXEC SETMSG MSG(CTMK003Z)
             EXIT QUIT
           END
        SET &NDX = &NDX + 1
      END

      END   /* END PROCESSING A TABLE LINE */

END /* ALL LINES PROCESSED */

IF (&MFOUND=N) THEN +
  DO
    ISPEXEC SETMSG MSG(CTMK003A)
    ISPEXEC LMMADD  DATAID(&CTMPKOT) MEMBER(&NRSTR(&OTABLE))
    SET &RC = &LASTCC
  END

IF (&MFOUND=Y) THEN +
  DO
    ISPEXEC SETMSG MSG(CTMK003H)
    ISPEXEC LMMREP  DATAID(&CTMPKOT) MEMBER(&NRSTR(&OTABLE))
    SET &RC = &LASTCC
  END

IF &RC ¬= 0 THEN +
  DO
    IF ((&ZERRMSG¬=ISRLS080) AND (&ZERRMSG¬=ISPS109)) THEN +
      DO
        ISPEXEC SETMSG MSG(CTMK003Z)
        EXIT QUIT
      END
    ISPEXEC LMMADD  DATAID(&CTMPKOT) MEMBER(&TEMPNAME)
    SET &RC = &LASTCC
    IF &RC ¬= 0 THEN +
      DO
        ISPEXEC SETMSG MSG(CTMK003Z)
        EXIT QUIT
      END
    SET &CTMCKSV=MEMRENAM
    ISPEXEC VPUT (CTMCKSV)
    CTMTQSR       /* RENAME TEMP MEMBER NAME TO REAL NAME */
    ISPEXEC VGET (IOERROR)
    IF (&IOERROR=Y) THEN +
      DO
        ISPEXEC SETMSG MSG(CTMK003Z)
        EXIT QUIT
      END
  END

ISPEXEC LMCLOSE DATAID(&CTMPKOT)
SET &RC = &LASTCC
IF (&RC ¬= 0) THEN +
   DO
     ISPEXEC SETMSG MSG(CTMK003Z)
     EXIT QUIT
   END

ISPEXEC LMFREE  DATAID(&CTMPKOT)
SET &RC = &LASTCC
IF (&RC ¬= 0) THEN +
   DO
     ISPEXEC SETMSG MSG(CTMK003Z)
     EXIT QUIT
   END

SET &CTMPKOT=&NUL
SET &IOERROR=N

ISPEXEC VPUT (CTMPKOT IOERROR)

EXIT

END
