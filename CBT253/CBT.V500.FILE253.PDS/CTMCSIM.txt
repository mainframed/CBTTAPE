 /********************************************************************/
 /*                                                                  */
 /*       INTERACTIVE BUILD OF SIMULATION JOB                        */
 /*                                                                  */
 /*              CTMCSIM - RELEASE 3.0.0                             */
 /*                                                                  */
 /********************************************************************/

PROC 0 DEBUG SKELETON(CTMPSIMS)                 /* SKELETON NAME */ +
                                                   +
   /* PARAMETERS WHOSE VALUES ARE INSERTED DURING INSTALLATION */ +
                                                    +
             ILPREFM(SYSM.CTMI)  ILVERM(V410)   +
             OLPREFM(SYSS.CTMO)  OLVERM(V410)   +
             DBPREFM(SYSS.CTMO)  DBVERM(V410)   +
             DBVOLM(SYS021)    DBUNITM(3390) +
             DATEFMT(USA)                     +
                                                    +
           SIM(Y)          /* DEFAULT OF PANEL-DEFINED VARIABLES */ +
           STN(Y)                                                   +
           DW('  ') MW('  ') YW('  ')                               +
           SNA(N)   SOR(N)   SKA(Y)                                 +
           SIMPARM(SIMPARM)                                         +
           RJL(Y) RNS(Y)  TAP(N)  TVL(Y)  TTM(Y)  TJB(N)  TDS(N)    +
           JS(Y) JSJ(Y)  JSE(N)  JSS(N)                             +
           TAPULPRM(TAPULPRM)

IF &DEBUG=DEBUG THEN CONTROL LIST CONLIST SYMLIST MSG FLUSH
                ELSE CONTROL NOLIST NOCONLIST NOSYMLIST NOMSG NOFLUSH

ISPEXEC CONTROL ERRORS RETURN

ISPEXEC VGET JOBSCAN PROFILE
IF &JOBSCAN=YES THEN +
  SET &PANEL=CTMPSIMJ
ELSE +
  DO
    SET &PANEL=CTMPSIM
    SET &JS=N
  END
ISPEXEC VPUT DATEFMT PROFILE

SET &UID=&SUBSTR(1:3,&SYSUID.XXX)           /* FOR JOBNAME'S PREFIX */
SET &YY=&SUBSTR(7:8,&SYSDATE)
SET &MM=&SUBSTR(1:2,&SYSDATE)
SET &DD=&SUBSTR(4:5,&SYSDATE)

SET &SIMFROM = &STR(&YY.&MM.&DD.0900)  /* ITS ALWAYS THE SAME FORMAT */
SET &SIMUNTL = &STR(&YY.&MM.&DD.1600)

/* SAVE THE DEFAILT VALUES OF THE FIRST SECTION OF PARAMETERS */
SET &DEFAULT1=&STR(&SIMFROM.&SIMUNTL.&SNA.&SOR)

SET &CURSOR=SIM                    /* THE DEFAULT CURSOR FIELD */
DISPLAYP: +
ISPEXEC DISPLAY PANEL(&PANEL) CURSOR(&CURSOR)
SET &RC=&LASTCC
SET &CURSOR=SIM

IF &RC=8 +
THEN +
   GOTO EXIT                            /* PF03/15 WAS KEYED IN PANEL */
ELSE +
   IF &RC¬=0 +
   THEN +
      DO
         WRITE UNABLE TO LOCATE PANEL &PANEL . (  RC=&RC  )
         EXIT
      END

                     /* WHEN NO SIMULATION IS WANTED ( JUST REPORTS ) */
IF &SIM=N +
THEN +
  DO
    /* CHECK VALUES OF THE FIRST SECTION OF PARAMETERS AGAINST DEFAULT*/
   SET &SECTION1=&STR(&SIMFROM.&SIMUNTL.&SNA.&SOR)
   IF '&SECTION1'¬='&DEFAULT1' +
   THEN +
   ISPEXEC SETMSG MSG(CTMS001W) /* WARNING, NON-RELEVANT PARAMS */
   GOTO PREPJOB   /* ANYWAY, WHEN &SIM=N, FOLLOWING CHECKS NOT NEEDED */
  END

   /* CREATE_NEW_AJF AND ORDER_DAILY  ALLOWED  ONLY WHEN TONIGHT=N  */
IF &STN=Y +
THEN +
   IF  ( &SNA=Y OR &SOR=Y )   THEN  GOTO ERR8

    /*      VALIDITY CHECKS ON THE SIMFROM AND SIMUNTL DATES       */
IF  &LENGTH(&STR(&SIMFROM)) ¬= 10             THEN  GOTO ERR2
IF  &DATATYPE(&SUBSTR(1:5,&SIMFROM)) ¬= NUM   THEN  GOTO ERR2
IF  &DATATYPE(&SUBSTR(6:10,&SIMFROM))¬= NUM   THEN  GOTO ERR2
IF  ( &SUBSTR(3:4,&SIMFROM) <01  OR  &SUBSTR(3:4,&SIMFROM) > 12   OR +
      &SUBSTR(5:6,&SIMFROM) <01  OR  &SUBSTR(5:6,&SIMFROM) > 31   OR +
      &SUBSTR(1:2,&SIMFROM) <86  OR  &SUBSTR(1:2,&SIMFROM) > 99   OR +
      &SUBSTR(7:8,&SIMFROM) <01  OR  &SUBSTR(7:8,&SIMFROM) > 24   OR +
      &SUBSTR(9:10,&SIMFROM) <00 OR  &SUBSTR(9:10,&SIMFROM) > 59  )   +
                                              THEN  GOTO ERR2
IF  &LENGTH(&STR(&SIMUNTL)) ¬= 10             THEN  GOTO ERR3
IF  &DATATYPE(&SUBSTR(1:5,&SIMUNTL)) ¬= NUM   THEN  GOTO ERR3
IF  &DATATYPE(&SUBSTR(6:10,&SIMUNTL))¬= NUM   THEN  GOTO ERR3
IF  ( &SUBSTR(3:4,&SIMUNTL) <01  OR  &SUBSTR(3:4,&SIMUNTL) > 12   OR +
      &SUBSTR(5:6,&SIMUNTL) <01  OR  &SUBSTR(5:6,&SIMUNTL) > 31   OR +
      &SUBSTR(1:2,&SIMUNTL) <86  OR  &SUBSTR(1:2,&SIMUNTL) > 99   OR +
      &SUBSTR(7:8,&SIMUNTL) <01  OR  &SUBSTR(7:8,&SIMUNTL) > 24   OR +
      &SUBSTR(9:10,&SIMUNTL) <00 OR  &SUBSTR(9:10,&SIMUNTL) > 59  )   +
                                              THEN  GOTO ERR3
IF  &SUBSTR(1:10,&SIMUNTL) < &SUBSTR(1:10,&SIMFROM) THEN  GOTO ERR4

IF ( &RJL=Y AND &SKA=N ) +
THEN +
   GOTO ERR9             /* FOR SKL REPORT, SIMOAJF MUST BE CREATED */

IF ( &STN=Y AND &STR(&DW.&MW.&YW)¬=   ) +
THEN +
   GOTO ERR5                   /* ANOTHER DATE VALID ONLY WITH STN=N */

IF &STN=N +
THEN +
   DO
      IF ( &DW>31 OR &DW<1 OR &MW<1 OR &MW>12 ) +
      THEN +
        GOTO ERR1               /* INVALID DATE ,DISPLAY MESSAGE */

    SET &PYW=&YW                 /**** CALCULATE PREVIOUS DAY *****/
    IF &DW GT 1 THEN DO
                      SET &PDW=&DW-1
                      SET &PMW=&MW
                     END
          ELSE DO
           IF &MW GT 1 THEN  SET &PMW = &MW-1
              ELSE DO
               SET &PMW=12
               SET &PYW=&YW-1
              END
           SET &PDW=31
           IF &PMW=4 OR &PMW=6 OR &PMW=9 OR &PMW=11 THEN SET &PDW=30
           IF &PMW=2  THEN SET &PDW=28
           IF &PMW=2 AND (&YY=88 OR &YY=92 OR &YY=96) THEN SET &PDW=29
          END
    SET &PYW=&SUBSTR(2:3,&EVAL(&PYW+100))
    SET &PMW=&SUBSTR(2:3,&EVAL(&PMW+100))
    SET &PDW=&SUBSTR(2:3,&EVAL(&PDW+100))

    IF &DATEFMT=JPN +
    THEN +
       DO
          SET &DATEW=&STR(&YW.&MW.&DW)
          SET &PDATE=&STR(&PYW.&PMW.&PDW)     /* PREVIOUS DAY */
       END
    ELSE +
    IF &DATEFMT=USA +
    THEN +
       DO
          SET &DATEW=&STR(&MW.&DW.&YW)
          SET &PDATE=&STR(&PMW.&PDW.&PYW)     /* PREVIOUS DAY */
       END
    ELSE +
       DO
          SET &DATEW=&STR(&DW.&MW.&YW)
          SET &PDATE=&STR(&PDW.&PMW.&PYW)     /* PREVIOUS DAY */
       END

                              /* THE FOLLOWING - AS THE CTMLCK DSECT */
SET DACHK=&STR(+
&DATEW           &PDATE &PDATE            &PDATE &PDATE    &PDATE &PDATE)
   END                  /****** END TONIGHT=N  *******/

PREPJOB: +
SET &SEPERATE=&STR(-----------------------------------------------------
---------------*)

ISPEXEC FTOPEN TEMP
ISPEXEC FTINCL &SKELETON
SET &RC=&LASTCC
IF &RC=8  THEN GOTO ERR6                    /* SKELETON WAS NOT FOUND */
ELSE IF &RC=12 THEN GOTO ERR7      /* SKELETON MEMBER IS ALREADY OPEN */

ISPEXEC FTCLOSE
ISPEXEC VGET (ZTEMPF,ZTEMPN) ASIS

ISPEXEC LMFREE DATAID(TEMP)
ISPEXEC LMINIT DATAID(TEMP) DDNAME(&ZTEMPN)
ISPEXEC EDIT DATAID(&TEMP)      /* EDIT THE PREPARED JOB */
ISPEXEC LMFREE DATAID(TEMP)
GOTO   DISPLAYP                   /* BM 1783 */

 /* *** REMARK ***                                                   */
 /* IF THIS SPF VERSION DOES NOT SUPPORT THESE OPTIONS, THEN REPLACE */
 /* THE LAST 4 COMMANDS BY :                                         */
 /*    ISPEXEC EDIT DATASET(&ZTEMPF)                            */

EXIT: EXIT

 ERR1:   ISPEXEC SETMSG MSG(CTMS001E)
 SET &CURSOR=DW
 GOTO DISPLAYP
 ERR2:   ISPEXEC SETMSG MSG(CTMS002E)
 SET &CURSOR=SIMFROM
 GOTO DISPLAYP
 ERR3:   ISPEXEC SETMSG MSG(CTMS003E)
 SET &CURSOR=SIMUNTL
 GOTO DISPLAYP
 ERR4:   ISPEXEC SETMSG MSG(CTMS004E)
 SET &CURSOR=SIMUNTL
 GOTO DISPLAYP
 ERR5:   ISPEXEC SETMSG MSG(CTMS005E)
 SET &CURSOR=STN
 GOTO DISPLAYP
 ERR6:   ISPEXEC SETMSG MSG(CTMS006E)
 GOTO DISPLAYP
 ERR7:   ISPEXEC SETMSG MSG(CTMS007E)
 GOTO DISPLAYP
 ERR8:   ISPEXEC SETMSG MSG(CTMS008E)
 SET &CURSOR=STN
 GOTO DISPLAYP
 ERR9:   ISPEXEC SETMSG MSG(CTMS010E)
 SET &CURSOR=SKA
 GOTO DISPLAYP

 END
