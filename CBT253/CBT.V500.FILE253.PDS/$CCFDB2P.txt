/*  REXX EXEC : ( $CCFDB2P ).
    FUNCTION  : CALLED BY TSO COMMAND APPLICATION TABLE TASK ( DB2PC )
                TO PROVIDE AN ISPF DIALOG PANEL FOR DB2 COBOL2 BATCH
                AND CICS APPLICATION PROGRAM COMPILE SERVICES FROM A
                LIBRARIAN MASTER FOR QA AND PRODUCTION CONTROL.
                                                                      */

TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS ISPEXEC
X = MSG("OFF")
TSOID = SYSVAR(SYSUID)
ACCTCDE = 5302010530000000
C2P = 'APOST,LIB,RENT,RES,DYNAM,OFFSET,NOCMPR2'
EXPLN = 'NO'
LIBPRJ = 'SYS1'
LIBGRP = 'QASRCE'
LIBTYP = 'MASTER'
JCL.1 = 'CCFDB2Q1'                /* BATCH DB2 SKELETON JCL */
JCL.2 = 'CCFDB2Q2'                /* CICS DB2 SKELETON JCL */
/* ***  DB2SUBSYS = 'DSNX DSNA DSNA DB2Q DB2P DB2P' *** */
DB2SUBSYS = 'DSNX DSNA DSNA DSNX DSNA DSNA'
RUNLIB.1 = 'SYSS.DB2.V3R1M0.DB2X.RUNLIB.LOAD'
RUNLIB.2 = 'SYSS.DB2.V2R3M0.RUNLIB.LOAD'
RUNLIB.3 = 'SYSS.DB2.V2R3M0.RUNLIB.LOAD'
RUNLIB.4 = 'SYSS.DB2.V3R1M0.DB2Q.RUNLIB.LOAD'
RUNLIB.5 = 'SYSS.DB2.V3R1M0.DB2P.RUNLIB.LOAD'
RUNLIB.6 = 'SYSS.DB2.V3R1M0.DB2P.RUNLIB.LOAD'
DB2LOAD = 'SYS1.DB2.V2R3M0.DSNLOAD'
APSLIB = 'SYSS.APS.V5R2M1.COPYLIB'
FCSLIB = 'SYSS.FCS.V4R5M0.COPYLIB'
DBRMNODE = 'QA PROD EMRG QA PROD EMRG'
CNTLNODE = 'QA PROD PROD QA PROD PROD'
LOADNODE = 'QA PROD EMERGNCY QA PROD EMERGNCY'
LOADTYPE = 'BATCH CICS'
DB2QUAL = 'BUILD'
CLASS = 'P'
MVS = 'SYSA'
COPYLIB.1 = 'SYS1.QACOPY.MASTER'
COPYLIB.2 = 'SYS1.PRODCOPY.MASTER'
COPYLIB.3 = 'SYS1.TURNCOPY.MASTER'
COPYLIB.4 = 'SYS1.EMRGCOPY.MASTER'
/*     */
DISPLAY_MAIN_PANEL:
DO FOREVER
   ADDRESS ISPEXEC "DISPLAY PANEL($CCFQPNL)"
   SRC = RC
   IF SRC = 8 THEN LEAVE
   IF SRC > 8 THEN DO
      SAY '*** ERROR INVOKING ISPF DIALOG PANEL ( $CCFQPNL ). ***'
      SAY '*** CCF DB2PC COMPILE MAIN TASK CANCELLED - RC = 'SRC'. ***'
      EXIT SRC
   END
   MASTER = LIBPRJ||'.'||LIBGRP||'.'||LIBTYP
   CALL CHECK_MASTER_MEMBER
   IF MODSW = 'NO' THEN ITERATE
   CTYP = COMPTYP
   CALL ASSIGN_DB2_PARMS
   LABEL = 'BUILD_COPYBOOK_SEARCH_CHAIN_'LNODE1
   INTERPRET CALL LABEL
   CALL SUBMIT_COMPILE_JCL
   COMPTYP = ''
   MEMBER = ''
END
ADDRESS TSO
"FREE F("LIBDD")"
"FREE F("MASTDD")"
EXIT 0
/*
       S U B R O U T I N E    S E C T I O N     */
/*     */
CHECK_MASTER_MEMBER:
ADDRESS TSO
MODSW = 'NO'
LIBDSN = "'"MASTER"("MEMBER")'"
LIBDD = LIBALLOC(LIBDSN)
IF LIBDD = "ERROR" THEN DO
   SAY '*** ALLOCATION ERROR: ('MASTER'('MEMBER'). ***'
   SAY '*** DYNAMIC ALLOCATION FAILED FOR THE MASTER DATASET - RETRY TASK. ***'
   SAY '*** DB2PC COMPILE SUBTASK CANCELLED. ***'
   EXIT
END
MASTDD = LIBALLOC("'"MASTER"'")
MASTMEM = XLIBEMEM(MASTDD,MEMBER)
"EXECIO 0 DISKR" MASTDD "(FINIS"
IF MASTMEM = ""  THEN DO
   "FREE F("LIBDD")"
   "FREE F("MASTDD")"
   ADDRESS ISPEXEC
   ZERRMSG = $CCFA007
   "SETMSG MSG($CCFA007)"
   RETURN
END
MODSW = 'YES'
RETURN
/*     */
ASSIGN_DB2_PARMS:
SKELJCL = JCL.CTYP
DB2SYS = WORD(DB2SUBSYS,LL)
RUNLIB = RUNLIB.LL
DNODE1 = WORD(DBRMNODE,LL)
DNODE2 = WORD(LOADTYPE,CTYP)
DBRMLIB = 'SYSS.'DNODE1'.'DNODE2'.DBRMLIB'
CNODE1 = WORD(CNTLNODE,LL)
BINDPDS = 'SYSS.'CNODE1'.CCF.DB2.BIND.CNTLLIB'
PACKGPDS = 'SYSS.'CNODE1'.CCF.DB2.PACKAGE.CNTLLIB'
GRANTPDS = 'SYSS.'CNODE1'.CCF.DB2.GRANT.CNTLLIB'
LNODE1 = WORD(LOADNODE,LL)
LNODE2 = WORD(LOADTYPE,CTYP)
COMPMSG = 'PC.DB2.'LNODE2'.COMPILE'
LOADLIB = 'SYS1.'LNODE1'.'LNODE2'.LOADLIB'
IF DB2SYS = 'DSNX' | DB2SYS = 'DB2Q' | DB2SYS = 'DB2P' THEN DO
   DB2LOAD = 'SYS1.DB2.V3R1M0.SDSNLOAD'
END
RETURN
/*     */
BUILD_COPYBOOK_SEARCH_CHAIN_QA:
C1 = 1
C2 = 2
C3 = 3
D1 = 1
D2 = 2
D3 = 3
RETURN
/*     */
BUILD_COPYBOOK_SEARCH_CHAIN_PROD:
C1 = 2
C2 = 1
C3 = 2
D1 = 2
D2 = 1
D3 = 2
RETURN
/*     */
BUILD_COPYBOOK_SEARCH_CHAIN_EMERGNCY:
C1 = 4
C2 = 2
C3 = 1
D1 = 4
D2 = 2
D3 = 1
RETURN
/*     */
SUBMIT_COMPILE_JCL:
ADDRESS ISPEXEC
COPYLB1 = COPYLIB.C1
COPYLB2 = COPYLIB.C2
COPYLB3 = COPYLIB.C3
DCLLIB1 = COPYLIB.D1
DCLLIB2 = COPYLIB.D2
DCLLIB3 = COPYLIB.D3
"FTOPEN TEMP"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA001
   "SETMSG MSG($CCFA001)"
   RETURN
END
"FTINCL $CCFJOBD"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA002
   "SETMSG MSG($CCFA002)"
   RETURN
END
"FTINCL" SKELJCL
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA003
   "SETMSG MSG($CCFA003)"
   RETURN
END
"FTCLOSE"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA005
   RETURN
END
"VGET ZTEMPF"
ADDRESS TSO "SUBMIT '"ZTEMPF"'"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA006
   RETURN
END
ZERRMSG = $CCFA000
ADDRESS ISPEXEC "SETMSG MSG($CCFA000)"
RETURN
