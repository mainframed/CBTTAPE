PROC 0 DEBUG()
/*******************************************************/
/**  P.P.F. - PARAMETERS PROMPTING FACILITY - TYPE2   **/
/**                                                   **/
/**  THE CLIST DISPLAYS A SCREEN THAT GETS THE NAME   **/
/**  AND THE DESCRIPTION OF A MASTER PROMPTING PLAN   **/
/**  THAT WILL BE CREATED.                            **/
/**                                                   **/
/*******************************************************/
 ISPEXEC CONTROL ERRORS RETURN
 IF &DEBUG = DEBUG +
 THEN +
    CONTROL LIST MSG SYMLIST CONLIST
 ELSE +
    CONTROL NOLIST NOMSG
 ISPEXEC VGET ( PLNPREF PLANLIB) PROFILE
 SET &CREATED = NO
 ISPEXEC VGET (ZUSER) SHARED
 REDISP: -
 ISPEXEC DISPLAY PANEL(CTMPF12W)
 SET &RC = &LASTCC
 IF &RC ¬= 0  -
 THEN -
    IF &RC ¬= 8  -
    THEN -
        DO
          ISPEXEC SETMSG MSG(CTMF000Z)
          GOTO END
        END
    ELSE -
       GOTO END
 IF &PLANLIB ¬= &STR() AND &PLANLIB ¬= &STR( )        -
 THEN -
    DO
        SET &IND = 1
 INITRY: SET &TBFILE = &STR(TBFILE&IND)
       ISPEXEC LMINIT DATAID(TRYID) DDNAME (&TBFILE) ENQ(SHR)
       SET &RC = &LASTCC
       IF &RC = 0 -
       THEN -
          DO
            ISPEXEC LMFREE (&TRYID)
            SET &DSNTRY = DSNTRY&IND
            SET &DSNTR  = &&DSNTRY&IND
            ISPEXEC VGET ( &DSNTRY ) SHARED
            SET DSNTT = &SYSNSUB(2,&DSNTR)
            IF &DSNTT = &PLANLIB -
            THEN -
               GOTO CONT
            ELSE -
               DO
                 SET &IND = &IND + 1
                 GOTO INITRY
               END
          END
       IF &IND > 99 -
       THEN -
          SET &IND = 1
       SET &TBFILE = &STR(TBFILE&IND)
       SET &DSNTRY = DSNTRY&IND
       SET &&DSNTRY = &PLANLIB
       ISPEXEC VPUT (&DSNTRY) SHARED
       FREE FILE(&TBFILE)
       ALLOC FILE(&TBFILE) DA('&PLANLIB') SHR
       SET &RC = &LASTCC
       IF &RC ¬= 0 -
       THEN -
          DO
              SET &DDERR = &TBFILE
              SET &DSNERR = &PLANLIB
              ISPEXEC SETMSG MSG(CTMF005D)
              GOTO END
          END
 CONT: SET &TBDD = &STR(&TBFILE)
       SET &TBLIB = &STR(&PLANLIB)
    END
 IF &PLANLIB = &STR() OR &PLANLIB =&STR( ) -
 THEN   -
    DO
       SET &TBDD = &STR(ISPTABL)
       SET &TBLIB = &STR(ISPTABL)
    END
 SET &MANUMSNM = &STR(&PLNPREF.&STR(MS) )
 /*  SET &MANUMSNM = &STR(&PLNPREF)
 ISPEXEC TBCREATE &MANUMSNM +
       NAMES(PARMNAME PROCCUR# PRMJOBN PROMPIND PRDEFIND PRDEFVAL +
       PRMDEFST PRVALIND PRMVAL PRMTYPE PROMPMSG ) WRITE LIBRARY(&TBDD)
 SET &RC=&LASTCC
 IF &RC = 8 -
 THEN -
    DO
      ISPEXEC SETMSG MSG(CTMF009F)
      GOTO REDISP
    END
 IF &RC > 8 -
 THEN    -
    DO
      ISPEXEC SETMSG MSG(CTMF000Z)
      GOTO REDISP
    END
 ISPEXEC TBVCLEAR &MANUMSNM
 ISPEXEC TBADD  &MANUMSNM
 SET &RC = &LASTCC
 IF &RC ¬= 0 -
 THEN    -
    DO
      ISPEXEC TBEND  &MANUMSNM
      ISPEXEC SETMSG MSG(CTMF009A)
      GOTO REDISP
    END
 SET &TBNAME =  &MANUMSNM
 ISPEXEC SETMSG MSG(CTMF009B)
 ISPEXEC TBOPEN FETCHTB WRITE
 SET &RETC = &LASTCC
 IF &RETC GT 0 -
 THEN -
    IF &RETC > 12 -
    THEN -
       DO
         ISPEXEC TBEND  &MANUMSNM
         ISPEXEC SETMSG MSG (CTMF000Z)
         GOTO END
       END
    ELSE -
       IF &RETC = 8 -
       THEN -
          DO
             /* TRY TO CREATE IT NOW */
            ISPEXEC TBCREATE FETCHTB KEYS(TBNAME) +
               NAMES(USERID PLANLIB DESCRIPT) +
               WRITE
            SET &RC=&LASTCC
            IF &RC > 8 -
            THEN    -
               DO
                 ISPEXEC TBEND  &MANUMSNM
                 ISPEXEC SETMSG MSG(CTMF000Z)
                 GOTO REDISP
               END
          END
       ELSE -
          DO
            ISPEXEC TBEND  &MANUMSNM
            ISPEXEC SETMSG MSG (CTMF009E)
            GOTO REDISP
          END

 SET &TBNAME = &PLNPREF
 SET &USERID = &ZUSER
 ISPEXEC TBADD  FETCHTB
 SET &RETC = &LASTCC
 IF &RETC GT 0 -
 THEN -
    IF &RETC = 8 -
    THEN -
       DO
         ISPEXEC TBEND  &MANUMSNM
         ISPEXEC TBEND  FETCHTB
         ISPEXEC SETMSG MSG (CTMF009F)
         GOTO REDISP
       END
    ELSE -
       DO
         ISPEXEC TBEND  &MANUMSNM
         ISPEXEC TBEND  FETCHTB
         ISPEXEC SETMSG MSG (CTMF009E)
         GOTO REDISP
       END
 ISPEXEC TBCLOSE  FETCHTB
 SET &RETC = &LASTCC
 IF &RETC GT 0 -
 THEN -
    IF &RETC > 12 -
    THEN -
       DO
         ISPEXEC TBEND  &MANUMSNM
         ISPEXEC TBEND  FETCHTB
         ISPEXEC SETMSG MSG (CTMF000Z)
         GOTO REDISP
       END
    ELSE -
       DO
         ISPEXEC TBEND  &MANUMSNM
         ISPEXEC TBEND  FETCHTB
         ISPEXEC SETMSG MSG (CTMF009E)
         GOTO REDISP
       END
 ELSE -
    DO
        SET &CREATED = YES
        SET &TABNAME = &MANUMSNM
        ISPEXEC TBCLOSE  &MANUMSNM LIBRARY(&TBDD)
    END

 END: ISPEXEC VPUT (PLANLIB PLNPREF ) PROFILE
      ISPEXEC VPUT ( TBNAME TBLIB CREATED TABNAME ) SHARED
      ISPEXEC LMFREE DATAID(&FROMID)
