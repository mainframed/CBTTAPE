/*  REXX EXEC :
    THIS EXEC IS DESIGNED TO FACILITATE THE CA-LIBRARIAN SCAN UTILITY
    OF MODULES RESIDING ON A MASTER DATASET.  */

TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS ISPEXEC
"CONTROL ERRORS RETURN"
ACCTCCF = 5200010520000000
LIBSTAT.1 = 'TEST'
LIBSTAT.2 = 'TURN'
LIBSTAT.3 = 'QA'
LIBSTAT.4 = 'EMRG'
LIBSTAT.5 = 'PROD'
LIBTYPE.1 = 'COPY'
LIBTYPE.2 = 'JCL'
LIBTYPE.3 = 'PROC'
LIBTYPE.4 = 'RAMI'
LIBTYPE.5 = 'SRCE'
LIBTYPE.6 = 'SYSI'
C = 'T'
M = 'S'
STACOL = 001
ENDCOL = 072
BEGMOD = '*'
ENDMOD = 'ALL'
LNE = 0
TSOID = SYSVAR(SYSUID)
TSOID = SUBSTR(TSOID,1,4)
IF TSOID = 'TECH' THEN DO
   C = 'S'
   M = 'G'
END
TSOUS = SYSVAR(SYSUID)
"VGET (ACCTCDE) PROFILE"
IF RC ¬= 0 THEN ACCTCDE = ACCTCCF
ADDRESS TSO
ISPFDA = SYSDSN(DIALOG.ISPFILE)
IF ISPFDA = 'OK' THEN DO
   X = MSG("OFF")
   "FREE DA(DIALOG.ISPFILE)"
   "DELETE (DIALOG.ISPFILE) SCRATCH PURGE NONVSAM"
END
"ALLOC DA(DIALOG.ISPFILE) LIKE(USER.CNTLLIB) NEW SPACE(1,0) DIR(15) CYLINDERS"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** ALLOCATE ERROR: ERROR ALLOCATING TEMP ISPFILE DATASET. ***'
   SAY '*** LIBSCAN EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   "DELETE (DIALOG.ISPFILE) SCRATCH PURGE NONVSAM"
   EXIT
END
"FREE DA(DIALOG.ISPFILE)"
"ALLOC DA(DIALOG.ISPFILE(LIBRSCAN)) FILE(ISPFILE) OLD"

/*     */
DISPLAY_LIBSCAN_PANELS:
ADDRESS ISPEXEC
"DISPLAY PANEL(LBSCANP1)"
SRC = RC
IF SRC > 8 THEN DO
   SAY '*** PANEL ERROR: ISPF DIALOG ERROR INVOKING PANEL ( LBSCANP1 ). ***'
   SAY '*** LIBSCAN EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   "FREE DA(DIALOG.ISPFILE)"
   "DELETE (DIALOG.ISPFILE) SCRATCH PURGE NONVSAM"
   EXIT
END
IF SRC = 8 THEN DO
   "FREE DA(DIALOG.ISPFILE)"
   "DELETE (DIALOG.ISPFILE) SCRATCH PURGE NONVSAM"
   EXIT
END
CALL ASSIGN_SCAN_VARIABLES
"DISPLAY PANEL(LBSCANP2)"
SRC = RC
IF SRC > 8 THEN DO
   SAY '*** PANEL ERROR: ISPF DIALOG ERROR INVOKING PANEL ( LBSCANP2 ). ***'
   SAY '*** LIBSCAN EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   "FREE DA(DIALOG.ISPFILE)"
   "DELETE (DIALOG.ISPFILE) SCRATCH PURGE NONVSAM"
   EXIT
END
IF SRC = 8 THEN DO
   "FREE DA(DIALOG.ISPFILE)"
   "DELETE (DIALOG.ISPFILE) SCRATCH PURGE NONVSAM"
   EXIT
END

/*     */
BUILD_SCAN_SUBMIT_JCL:
ADDRESS TSO
CALL BUILD_SCAN_JCL
CALL BUILD_SCAN_SYSIN
CALL WRITE_SCAN_JOB
CALL SUBMIT_SCAN_JOB
/* X = MSG("OFF") */
"FREE DA(DIALOG.ISPFILE)"
/* "DELETE (DIALOG.ISPFILE) SCRATCH PURGE NONVSAM" */
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */

/*     */
ASSIGN_SCAN_VARIABLES:
S = LIBSTAT
T = LIBTYPE
JN = JOBNAME
JOBN = LENGTH(JN)
IF JOBN < 8 THEN JN = LEFT(JN,8)
MASTRDSN = 'SYS1.'LIBSTAT.S||LIBTYPE.T'.MASTER'
SCOL = LENGTH(STACOL)
IF SCOL < 3 THEN STACOL = RIGHT(STACOL,3,0)
ECOL = LENGTH(ENDCOL)
IF ECOL < 3 THEN ENDCOL = RIGHT(ENDCOL,3,0)
IF BEGMOD = '*' THEN RETURN
BMOD = LENGTH(BEGMOD)
IF BMOD < 8 THEN BEGMOD = LEFT(BEGMOD,8)
EMOD = LENGTH(ENDMOD)
IF EMOD < 8 THEN ENDMOD = LEFT(ENDMOD,8)
RETURN

/*     */
BUILD_SCAN_JCL:
J = 0
"DELSTACK"
RCD.1 = "//"JN" JOB" ACCTCDE",'"TSOUS".FAIRSCAN',NOTIFY="TSOUS","
RCD.2 = "//             CLASS="C",MSGCLASS="M",MSGLEVEL=(1,1),REGION=4096K"
RCD.3 = "//LIBRSCAN EXEC PGM=FAIRSCAN"
RCD.4 = "//PRINTOUT DD SYSOUT=*"
RCD.5 = "//SYSOUT   DD SYSOUT=*"
RCD.6 = "//MASTER   DD DSN="MASTRDSN",DISP=SHR"
RCD.7 = "//CARDIN   DD *"
DO 7
   J = J + 1
   QUEUE RCD.J
END
RETURN

/*     */
BUILD_SCAN_SYSIN:
N = 0
R = 0
SCNCNT = 0
SCAN.1 = SCAN1
SCAN.2 = SCAN2
SCAN.3 = SCAN3
SCAN.4 = SCAN4
SCAN.5 = SCAN5
SCAN.6 = SCAN6
SCAN.7 = SCAN7
SCAN.8 = SCAN8
SCAN.9 = SCAN9
SCAN.10 = SCAN10
DO 10
   N = N + 1
   IF SCAN.N = '' THEN ITERATE
   SCNCNT = SCNCNT + 1
END
DO SCNCNT
   R = R + 1
   RCD.R = "-SCAN     "SCAN.R"#$#"
END
R = R + 1
RCD.R = "-COLM     "STACOL ENDCOL
R = R + 1
RCD.R = "-LINE     "LNE
IF BEGMOD ¬= '*' THEN DO
   R = R + 1
   RCD.R = "-PROG     "BEGMOD    ENDMOD
END
R = R + 1
RCD.R = "/*"
R = R + 1
RCD.R = "//"
RCDCNT = J + R
RETURN

/*     */
WRITE_SCAN_JOB:
ADDRESS TSO
R = 0
DO RCDCNT
   R = R + 1
   QUEUE RCD.R
END
"EXECIO" RCDCNT "DISKW ISPFILE"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING SYSIN ENTRIES TO TEMP ISPFILE. ***'
   SAY '*** LIBSCAN EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   "FREE DA(DIALOG.ISPFILE)"
   "DELETE (DIALOG.ISPFILE) SCRATCH PURGE NONVSAM"
   EXIT
END
"EXECIO 0 DISKW ISPFILE (FINIS"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** FTCLOSE ERROR: ERROR CLOSING TEMP ISPFILE DATASET FOR SUBMIT. ***'
   SAY '*** LIBSCAN EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   "FREE DA(DIALOG.ISPFILE)"
   "DELETE (DIALOG.ISPFILE) SCRATCH PURGE NONVSAM"
   EXIT
END
RETURN

/*     */
SUBMIT_SCAN_JOB:
ADDRESS TSO
X = MSG("ON")
"SUBMIT DIALOG.ISPFILE(LIBRSCAN)"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** SUBMIT ERROR: ERROR SUBMITTING' JOBNAME' FROM TEMP ISPFILE . ***'
   SAY '*** LIBSCAN EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   "FREE DA(DIALOG.ISPFILE)"
   "DELETE (DIALOG.ISPFILE) SCRATCH PURGE NONVSAM"
   EXIT
END
RETURN
