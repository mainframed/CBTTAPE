PROC 0 DEBUG FUNC(B) SMPE(YES) +
             STEPLIB(SYS1.IOA.V410.LOAD)     +
             MAINTLIB(SYSM.IOA.V410.MAINTLIB)
/*******************************************************************/
/**             IOA MAINTENANCE CLIST                             **/
/*******************************************************************/

ATTN GOTO FREE
ISPEXEC CONTROL ERRORS RETURN
IF &DEBUG=DEBUG   THEN  CONTROL LIST MSG CONLIST SYMLIST NOFLUSH
                  ELSE  CONTROL NOMSG NOFLUSH
SET &CURSOR = &STR(MEMBER)
SET &INIT    = &Z
IF &SMPE = &STR(YES) THEN SET &SUB=&STR(SMP)
ELSE                      SET &SUB=&STR(IOA)
MAINPNL: +
SET &ZEDSMSG = &Z
SET &ZEDLMSG = &Z
SET &MEMBER  = &Z
SET &F       = &FUNC
MAINPANL: +
SET &AST     = &Z
SET &ZCMD    = &Z
ISPEXEC SETMSG MSG(ISRZ001)
ISPEXEC DISPLAY PANEL(IOAMNTP1) CURSOR(CURSOR)
IF &RESP = &STR(END) THEN GOTO FREE
SET &ZEDSMSG = &Z
SET &ZEDLMSG = &Z
ISPEXEC CONTROL ERRORS RETURN
IF &SYSDSN('&MAINTLIB') ¬= OK THEN DO
  SET &ZEDSMSG = &STR(LIBRARY NOT FOUND)
  SET &ZEDLMSG = +
      &STR(LIBRARY &MAINTLIB NOT FOUND. PLEASE CREATE IT)
  ISPEXEC SETMSG MSG(ISRZ001)
  EXIT QUIT
END
IF &INIT = &STR(YES) THEN GOTO INITDONE
ALLOC DD(ISPFILE) DS('&MAINTLIB') SHR REUSE
ISPEXEC LMINIT DATAID(LIB) DATASET('&MAINTLIB')
SET &INIT = &STR(YES)
INITDONE: +
IF &STR(&MEMBER) ¬= &Z THEN DO
  SET &LTH = &LENGTH(&STR(&MEMBER))
  IF  &SUBSTR(&LTH:&LTH,&MEMBER) = &STR(*) THEN SET &AST = &STR(YES)
END
CHKFUNC:  +
IF &F = &STR(B) THEN DO
/******************
/** BROWSE FUNCTION
/******************
 ISPEXEC BROWSE DATAID(&LIB) MEMBER(&MEMBER) MIXED(NO)
 SET &RC = &LASTCC
 IF  &RC = 0 THEN GOTO MAINPNL
 ELSE +
 IF  &RC = 16 THEN DO
   IF &STR(&MEMBER) = &Z THEN DO
      SET &ZEDSMSG = &STR(NO MEMBERS IN DATA SET)
      SET &ZEDLMSG = &STR(THE PDS SPECIFIED CONTAINS NO MEMBERS).
   END
   ELSE DO
      SET &ZEDSMSG = &STR(NO MATCHES)
      SET &ZEDLMSG = +
          &STR(THE SPECIFIED PATTERN PRODUCED NO MATCHING MEMBERS).
   END
 END
 ELSE +
 IF  &RC = 14 THEN DO
  SET &ZEDSMSG = &STR(MEMBER NOT FOUND)
  SET &ZEDLMSG = +
   &STR(THE REQUESTED MEMBER WAS NOT FOUND IN THE PDS DIRECTORY)
 END
 GOTO MAINPANL
END
ELSE +
IF &F = &STR(E) THEN DO
/******************
/*** EDIT FUNCTION
/******************
 ISPEXEC CONTROL ERRORS RETURN
 IF &AST ¬= &STR(YES) THEN +
  IF &MEMBER ¬= &Z THEN +
   IF &SYSDSN('&MAINTLIB(&MEMBER)') = &STR(MEMBER NOT FOUND) THEN DO
    ISPEXEC  FTOPEN
    ISPEXEC  FTINCL IOAMNTS2
    ISPEXEC  FTINCL IOAMNTS1
    ISPEXEC  FTINCL IOAMNTS3
    ISPEXEC  FTCLOSE NAME(&MEMBER)
    IF &LASTCC = 0 THEN DO
      SET &ZEDSMSG=&STR(MEMBER &MEMBER CREATED)
      SET &ZEDLMSG=&STR(MEMBER &MEMBER CREATED IN MAINTENANCE LIBRARY)
    END
    GOTO CHKFUNC
   END
 ISPEXEC EDIT DATAID(&LIB) MEMBER(&MEMBER) MIXED(NO)
 SET &RC = &LASTCC
 IF &RC <= 4  THEN GOTO MAINPNL
 ELSE +
 IF &RC = 14 THEN DO
  IF (&MEMBER = &Z OR &AST = &STR(YES)) THEN DO
   SET &ZEDSMSG=&STR(DATASET IN USE)
   SET &ZEDLMSG=&STR(THE SPECIFIED DATA SET IN USE)
   GOTO MAINPANL
  END
  ELSE DO
   SET &ZEDSMSG=&STR(MEMBER &MEMBER IN USE)
   SET &ZEDLMSG=&STR(MEMBER &MEMBER IN USE. BROWSE OPTION WAS FORCED)
   ISPEXEC SETMSG MSG(ISRZ001)
   SET &F = &STR(B)
   GOTO CHKFUNC
  END
 END
 ELSE +
 IF &RC = 16 THEN DO
  IF &AST = &STR(YES) THEN DO
    SET &ZEDSMSG = &STR(NO MATCHES)
    SET &ZEDLMSG = +
        &STR(THE SPECIFIED PATTERN PRODUCED NO MATCHING MEMBERS).
    GOTO MAINPANL
  END
 END
 ELSE DO
  SET &ZEDSMSG = &STR(SEVERE ERROR RC=&RC)
  SET &ZEDLMSG = +
      &STR(SEVERE ERROR DURING EDIT THE MEMBER &MEMBER. RC=&RC.)
  GOTO MAINPANL
 END
END
ELSE GOTO MAINPNL
FREE: +
FREE F(ISPFILE)
IF &LIB = &Z THEN EXIT QUIT
ISPEXEC LMCLOSE DATAID(&LIB)
ISPEXEC LMFREE DATAID(&LIB)
ISPEXEC SETMSG MSG(ISRZ001)
EXIT QUIT
