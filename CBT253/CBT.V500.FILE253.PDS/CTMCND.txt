PROC 0 DD()                 +
       MM()                 +
       FUNC()               +
       COND()               +
       DATEFMT(USA)   +
       DBPREFA(SYSS.IOA)   +
       DBVERA(V410)     +
       LOADLIB(SYS1.IOA.V410.LOAD)   +
       LANG()         +
       DBG(DUMMY)
/*******************************************************************/
/*                                                                 */
/* THIS CLIST IS SIMILAR TO THE CTMCCND CLIST, BUT WITHOUT DISPLAY */
/* OF PANEL.  THE PANEL INFORMATION IS TRANSFERED VIA PARAMETERS   */
/* TO CLIST. E.G., DD() MM() FUNC() AND COND().                    */
/*                                                                 */
/*******************************************************************/
WRITE
WRITE *** CTMCND CLIST STARTED
WRITE
FREE F(PRTDBG,DALOG,DASINC,DARESF,DAPRINT)
SET &MAXCC=0     /* THIS IS BECAUSE THE FREE COMMAND RETURNS 12 */
ALLOC F(PRTDBG) &DBG SYSOUT(A)  HOLD

ALLOC DD(DALOG)    DA('&DBPREFA..&DBVERA..LOG') SHR
IF &LASTCC¬=0 +
THEN +
   WRITE *** ALLOCATION FOR DDNAME=DALOG    FAILED. RC=&LASTCC

ALLOC DD(DASINC)   DA('&DBPREFA..&DBVERA..SNC') SHR
IF &LASTCC¬=0 +
THEN +
   WRITE *** ALLOCATION FOR DDNAME=DASINC   FAILED. RC=&LASTCC

ALLOC DD(DARESF)   DA('&DBPREFA..&DBVERA..RES') SHR
IF &LASTCC¬=0 +
THEN +
   WRITE *** ALLOCATION FOR DDNAME=DARESF   FAILED. RC=&LASTCC

ALLOC DD(DAPRINT)  DS(*)

                             /* IF NO DD() MM() GIVEN, TAKE TODAYS */
IF &MM= +
THEN +
   SET &MM=&SUBSTR(1:2,&SYSDATE)
IF &DD= +
THEN +
   SET &DD=&SUBSTR(4:5,&SYSDATE)

SET &CTMFUN=&STR(&FUNC)
SET &CTMCONDN=&STR(&COND)

IF &DATEFMT=JPN  +
   THEN CALL  '&LOADLIB&LANG(CTMCND)' '&CTMFUN COND &CTMCONDN &MM.&DD'
   ELSE +
IF &DATEFMT=USA  +
   THEN CALL  '&LOADLIB&LANG(CTMCND)' '&CTMFUN COND &CTMCONDN &MM.&DD'
   ELSE CALL  '&LOADLIB&LANG(CTMCND)' '&CTMFUN COND &CTMCONDN &DD.&MM'
   SET &RC=&LASTCC
   WRITE *** CTMCND PROGRAM ENDED WITH RC=&RC.

WRITE
WRITE *** CTMCND CLIST ENDED
WRITE
 /* RETURN CODE TO IKJEFT01 */
EXIT CODE(&MAXCC)
