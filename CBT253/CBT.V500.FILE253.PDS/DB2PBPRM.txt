/*  REXX EXEC : ( DB2PBPRM ).
    FUNCTION  : CALLED BY CCF QA AND PROD COMPILE SKELETON JCL
                ( CCFDB2Q1 ) FOR PRODUCTION CONTROL DB2 BATCH PROGRAMS.
                BUILDS THE PACKAGE/BIND/GRANT CONTROL PARM MEMBERS AND
                WRITES THE MEMBERS INTO CORRESPONDING QA AND PROD LIBS.
    OUTPUT    : SYSS.&CCFNODE.CCF.DB2.BIND.CNTLLIB(&PROGRAM).
                SYSS.&CCFNODE.CCF.DB2.GRANT.CNTLLIB(&PROGRAM).
                SYSS.&CCFNODE.CCF.DB2.PACKAGE.CNTLLIB(&PROGRAM).
                                                                     */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
ARG MEMBER LIBNODE DBSUBSYS DB2QUAL .
PARMS = 'MEMBER LIBNODE DBSUBSYS DB2QUAL'
SRC = 0
DO I = 1 TO 4
   IPARM = WORD(PARMS,I)
   XPARM = VALUE(IPARM)
   IF XPARM ¬= '' THEN ITERATE
   SAY
   SAY '*** PARM ERROR: MISSING PARM VALUE FOR ('IPARM'). ***'
   SAY '*** DB2PBPRM EXEC TASK CANCELLED - RC = (024). ***'
   SAY
   EXIT 024
END
X = MSG("OFF")
"FREE FILE(PARMDD)"
"FREE FILE(DB2DD)"
TSOID = SYSVAR(SYSUID)
LABEL = 'MAIN_ROUTINE_'LIBNODE
INTERPRET CALL LABEL
/*     */
MAIN_ROUTINE_QA:
DB2ACT = 'ADD'
DSNTPLAN = 'DSNTIA31'
PARMLIB = 'SYSS.TECH.COMMON.PARMLIB'
PARMPDS = 'SYSS.QA.CCF.DB2.TURNOVER.PARMLIB'
BINDPDS = 'SYSS.QA.CCF.DB2.BIND.CNTLLIB'
PACKPDS = 'SYSS.QA.CCF.DB2.PACKAGE.CNTLLIB'
GRNTPDS = 'SYSS.QA.CCF.DB2.GRANT.CNTLLIB'
RUNLIB = 'SYSS.DB2.V3R1M0.DB2X.RUNLIB.LOAD'
IF DBSUBSYS = 'DB2Q' THEN RUNLIB = 'SYSS.DB2.V3R1M0.DB2Q.RUNLIB.LOAD'
CALL GET_DB2_PACKAGE_BIND_PARMS
CALL BUILD_PACKAGE_SYSIN
OUTPDS = PACKPDS
CALL WRITE_DB2_PARMS
CALL BUILD_BIND_SYSIN
OUTPDS = BINDPDS
CALL WRITE_DB2_PARMS
CALL BUILD_GRANT_SYSIN
OUTPDS = GRNTPDS
CALL WRITE_DB2_PARMS
EXIT 0
/*     */
MAIN_ROUTINE_PROD:
DB2ACT = 'ADD'
DSNTPLAN = 'DSNTIA23'
PARMLIB = 'SYSS.TECH.COMMON.PARMLIB'
PARMPDS = 'SYSS.QA.CCF.DB2.TURNOVER.PARMLIB'
BINDPDS = 'SYSS.PROD.CCF.DB2.BIND.CNTLLIB'
PACKPDS = 'SYSS.PROD.CCF.DB2.PACKAGE.CNTLLIB'
GRNTPDS = 'SYSS.PROD.CCF.DB2.GRANT.CNTLLIB'
RUNLIB = 'SYSS.DB2.V2R3M0.RUNLIB.LOAD'
IF DBSUBSYS = 'DB2P' THEN RUNLIB = 'SYSS.DB2.V3R1M0.DB2Q.RUNLIB.LOAD'
CALL GET_DB2_PACKAGE_BIND_PARMS
CALL BUILD_PACKAGE_SYSIN
OUTPDS = PACKPDS
CALL WRITE_DB2_PARMS
CALL BUILD_BIND_SYSIN
OUTPDS = BINDPDS
CALL WRITE_DB2_PARMS
CALL BUILD_GRANT_SYSIN
OUTPDS = GRNTPDS
CALL WRITE_DB2_PARMS
EXIT 0
/*     */
MAIN_ROUTINE_EMERGNCY:
DB2ACT = 'REPLACE'
DSNTPLAN = 'DSNTIA23'
PARMLIB = 'SYSS.TECH.COMMON.PARMLIB'
BINDPDS = 'SYSS.PROD.CCF.DB2.BIND.CNTLLIB'
PACKPDS = 'SYSS.PROD.CCF.DB2.PACKAGE.CNTLLIB'
GRNTPDS = 'SYSS.PROD.CCF.DB2.GRANT.CNTLLIB'
RUNLIB = 'SYSS.DB2.V2R3M0.RUNLIB.LOAD'
IF DBSUBSYS = 'DB2P' THEN RUNLIB = 'SYSS.DB2.V3R1M0.DB2Q.RUNLIB.LOAD'
CALL READ_PACKAGE_SYSIN_MEMBER
CALL BUILD_PACKAGE_SYSIN
OUTPDS = PACKPDS
CALL WRITE_DB2_PARMS
CALL BUILD_BIND_SYSIN
OUTPDS = BINDPDS
CALL WRITE_DB2_PARMS
CALL BUILD_GRANT_SYSIN
OUTPDS = GRNTPDS
CALL WRITE_DB2_PARMS
EXIT 0
/*
       S U B R O U T I N E S   S E C T I O N     */
/*     */
GET_DB2_PACKAGE_BIND_PARMS:
DB2MEMB = SYSDSN("'"PACKPDS"("MEMBER")'")
IF DB2MEMB = 'MEMBER NOT FOUND' THEN DO
   DB2COLL = MEMBER
   DB2PLAN = MEMBER
   RETURN
END
"ALLOC DA('"PACKPDS"("MEMBER")') FILE(PARMDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   DB2COLL = MEMBER
   DB2PLAN = MEMBER
   RETURN
END
"EXECIO * DISKR PARMDD (STEM DATA. FINIS"
SRC = RC
"FREE FILE(PARMDD)"
IF SRC ¬= 0 THEN DO
   DB2COLL = MEMBER
   DB2PLAN = MEMBER
   RETURN
END
DO I = 1 TO DATA.0
   PKEY = POS('BIND PACKAGE',DATA.I)
   IF PKEY ¬= 0 THEN DO
      COLLKEY = WORD(DATA.I,3)
      COLLID = STRIP(COLLKEY,"L","(")
      COLLID = STRIP(COLLID,"T",")")
      DB2COLL = COLLID
      DB2PLAN = COLLID
      LEAVE
   END
END
RETURN
/*     */
BUILD_PACKAGE_SYSIN:
"DELSTACK"
RCDCNT = 15
RCD.1 = " DSN SYSTEM("DBSUBSYS")"
RCD.2 = " BIND PACKAGE ("DB2COLL") -"
RCD.3 = "      OWNER("TSOID") -"
RCD.4 = "      QUALIFIER("DB2QUAL") -"
RCD.5 = "      MEMBER ("MEMBER") - "
RCD.6 = "      SQLERROR(NOPACKAGE) -"
RCD.7 = "      VALIDATE(BIND) -"
RCD.8 = "      FLAG(I) -"
RCD.9 = "      ISOLATION (CS) -"
RCD.10 = "      RELEASE(COMMIT) -"
RCD.11 = "      EXPLAIN(NO) -"
RCD.12 = "      CURRENTDATA(NO) -"
RCD.13 = "      ACTION ("DB2ACT") -"
RCD.14 = "      ENABLE(*)"
RCD.15 = " END"
DO R = 1 TO RCDCNT
   QUEUE RCD.R
END
RETURN
/*     */
BUILD_BIND_SYSIN:
"DELSTACK"
RCDCNT = 15
REC.1 = " DSN SYSTEM("DBSUBSYS")"
REC.2 = " BIND PLAN ("DB2PLAN") -"
REC.3 = "      OWNER("TSOID") -"
REC.4 = "      VALIDATE(BIND) -"
REC.5 = "      FLAG(I) -"
REC.6 = "      ACQUIRE(USE) -"
REC.7 = "      ISOLATION(CS) -"
REC.8 = "      RELEASE(COMMIT) -"
REC.9 = "      EXPLAIN(NO) -"
REC.10 = "      ACTION (REPLACE) -"
REC.11 = "      PKLIST("DB2COLL".*) -"
REC.12 = "      ENABLE(*)"
REC.13 = " RUN PROGRAM(DSNTIAD) PLAN("DSNTPLAN") -"
REC.14 = "     LIB('"RUNLIB"')"
REC.15 = " END"
DO R = 1 TO RCDCNT
   QUEUE REC.R
END
RETURN
/*     */
BUILD_GRANT_SYSIN:
"DELSTACK"
PKGCNT = 4
PKG.1 = "        GRANT"
PKG.2 = "        BIND,"
PKG.3 = "        COPY"
PKG.4 = "  ON PACKAGE "DB2COLL".* TO"
PKG.5 = "  COMMIT;"
BNDCNT = 4
BND.1 = "  GRANT EXECUTE ON PACKAGE "DB2COLL".* TO PUBLIC;"
BND.2 = "  COMMIT;"
BND.3 = "  GRANT EXECUTE ON PLAN "DB2PLAN" TO PUBLIC;"
BND.4 = "  COMMIT;"
DB2PARM = SYSDSN("'"PARMLIB"(DB2ADMIN)'")
IF DB2PARM = 'MEMBER NOT FOUND' THEN DO
   SAY '*** MEMBER ERROR: MEMBER NOT FOUND ('PARMLIB'(DB2ADMIN). ***'
   SAY '*** DB2PBPRM EXEC SUBTASK CANCELLED - RETURN CODE: ('028'). ***'
   EXIT 028
END
"ALLOC DA('"PARMLIB"(DB2ADMIN)') FILE(PARMDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('PARMLIB'(DB2ADMIN). ***'
   SAY '*** DB2PBPRM EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR PARMDD (FINIS STEM AUTH."
SRC = RC
"FREE FILE(PARMDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('PARMLIB'(DB2ADMIN). ***'
   SAY '*** DB2PBPRM EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
AUSER = TSOID','
DO P = 1 TO PKGCNT
   QUEUE PKG.P
END
DO I = 1 TO AUTH.0
   AUTHUSR = WORD(AUTH.I,1)
   IF AUSER = AUTHUSR THEN ITERATE
   DB2AUTH = '        '||AUTHUSR
   QUEUE DB2AUTH
END
QUEUE PKG.5
DO B = 1 TO BNDCNT
   QUEUE BND.B
END
RETURN
/*     */
BUILD_GRANT_SYSIN_NOOP:
"DELSTACK"
PKGCNT = 5
COMRCD = "  COMMIT;"
PKG.1 = "        GRANT"
PKG.2 = "        BIND,"
PKG.3 = "        COPY,"
PKG.4 = "        EXECUTE"
PKG.5 = "  ON PACKAGE "DB2COLL".* TO"
BNDCNT = 4
BND.1 = "        GRANT"
BND.2 = "        BIND,"
BND.3 = "        EXECUTE"
BND.4 = "  ON PLAN "DB2PLAN" TO"
DB2PARM = SYSDSN("'"PARMLIB"(DB2ADMIN)'")
IF DB2PARM = 'MEMBER NOT FOUND' THEN DO
   SAY '*** MEMBER ERROR: MEMBER NOT FOUND ('PARMLIB'(DB2ADMIN). ***'
   SAY '*** DB2PBPRM EXEC SUBTASK CANCELLED - RETURN CODE: ('028'). ***'
   EXIT 028
END
"ALLOC DA('"PARMLIB"(DB2ADMIN)') FILE(PARMDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('PARMLIB'(DB2ADMIN). ***'
   SAY '*** DB2PBPRM EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR PARMDD (STEM AUTH. FINIS"
SRC = RC
"FREE FILE(PARMDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('PARMLIB'(DB2ADMIN). ***'
   SAY '*** DB2PBPRM EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
AUSER = TSOID','
DO P = 1 TO PKGCNT
   QUEUE PKG.P
END
DO I = 1 TO AUTH.0
   AUTHUSR = WORD(AUTH.I,1)
   IF AUSER = AUTHUSR THEN ITERATE
   DB2AUTH = '        '||AUTHUSR
   QUEUE DB2AUTH
END
QUEUE COMRCD
DO B = 1 TO BNDCNT
   QUEUE BND.B
END
DO I = 1 TO AUTH.0
   AUTHUSR = WORD(AUTH.I,1)
   IF AUSER = AUTHUSR THEN ITERATE
   DB2AUTH = '        '||AUTHUSR
   QUEUE DB2AUTH
END
QUEUE COMRCD
RETURN
/*     */
WRITE_DB2_PARMS:
"ALLOC DA('"OUTPDS"("MEMBER")') FILE(DB2DD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('OUTPDS'('MEMBER'). ***'
   SAY '*** DB2PBPRM EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW DB2DD (FINIS"
SRC = RC
"FREE FILE(DB2DD)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING ('OUTPDS'('MEMBER').  ***'
   SAY '*** DB2PBPRM EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN
