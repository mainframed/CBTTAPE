CONTROL END(END)
/*                                              */
/* ADD LINE BEFORE THE FIRST "EXEC PGM" CARD    */
/*                                              */
ISREDIT MACRO

/* CONTROL LIST SYMLIST CONLIST MSG

SET &ROW=0
SET &KEEPPROC=0
SET &KEEPEXEC=0
SET &SWEXEC=0
SET &SWPROC=0
SET &REMARK=&STR(//*)
SET &SLASH=&STR(//)
ISPEXEC VGET (M1 O1 F1 F2 F3 F4 S1 A1 CL )
SET &ACTCL=&STR(&A1)
IF &ACTCL=&STR(C)  +
  THEN GOTO NOCHANG
IF &CL=&STR(NULL)   +
  THEN GOTO NOCHANG
ISREDIT SCAN = OFF
ISREDIT FIND ' JOB ' ALL
FINDJOB: +
IF &S1=SAVE2 +
  THEN DO
       SET &FROM=2
       SET &TO=4
       END
  ELSE DO
       SET &FROM=1
       SET &TO=3
       END
ISREDIT (ROW,COL) = CURSOR
SET &COLJOB=&COL
SET &ROWJOB=&ROW
ISREDIT (STRING) = LINE .ZCSR
IF '&STR(&SUBSTR(&FROM:&TO,&STR(&STRING).000))'='&STR(&REMARK)' +
THEN DO
       SET &KEEPJOB=&ROWJOB
       ISREDIT FIND ' JOB ' NEXT
       GOTO FINDJOB
     END
ELSE DO
     IF &S1=SAVE2 +
       THEN DO
            SET &FROM=2
            SET &TO=3
            END
       ELSE DO
            SET &FROM=1
            SET &TO=2
            END
     IF '&STR(&SUBSTR(&FROM:&TO,&STR(&STRING).000))'='&STR(&SLASH)' +
     THEN DO
          IF &S1=SAVE2 +
            THEN DO
                 SET &JOB1 = &STR( //)
                 SET &JOB3 = &STR(&SUBSTR(8:72,&STR(&STRING)))
                 END
            ELSE DO
                 SET &JOB1 = &STR(//)
                 SET &JOB3 = &STR(&SUBSTR(7:72,&STR(&STRING)))
                 END
          SET &JOB2 = &STR(&SUBSTR(1:4,&STR(&SYSUID)))
          SET &JOBC = &STR(&JOB1.&JOB2.&JOB3)
          ISREDIT LINE &ROWJOB = "&JOBC."
          END
      ELSE DO
           SET &KEEPJOB=&ROWJOB
           ISREDIT FIND ' JOB ' NEXT
           GOTO FINDJOB
           END
     END
ISREDIT FIND 'CLASS='
FINDCLS: +
ISREDIT (ROW,COL) = CURSOR
SET &COLCLS=&COL
SET &ROWCLS=&ROW
ISREDIT (KEYWRD) = LINE .ZCSR
IF '&STR(&SUBSTR(&FROM:&TO,&STR(&KEYWRD).000))'='&STR(&REMARK)' +
THEN DO
       SET &KEEPCLS=&ROWCLS
       ISREDIT FIND 'CLASS=' NEXT
       GOTO FINDCLS
     END
ELSE DO
       SET &BEFCOL = &COLCLS-3
IF '&STR(&SUBSTR(&BEFCOL:&COLCLS,&STR(&KEYWRD).000))'='&STR(MSGC)' +
       THEN DO
              SET &KEEPCLS=&ROWCLS
              ISREDIT FIND 'CLASS=' NEXT
              GOTO FINDCLS
            END
       ELSE DO
              SET &COLNEW = &COLCLS+5
              SET &CLS1 = &STR(&SUBSTR(1:&COLNEW,&STR(&KEYWRD)))
              SET &CLS2 = &STR(&CL)
              SET &COLNEXT = &COLNEW+2
              SET &CLS3 = &STR(&SUBSTR(&COLNEXT:72,&STR(&KEYWRD)))
              SET &CLSC = &STR(&CLS1.&CLS2.&CLS3)
              ISREDIT LINE &ROWCLS = "&CLSC."
            END
    END
NOCHANG:  +
SET &FROM=1
SET &TO=3
IF &S1=SAVE2 +
THEN DO
        SET &FROM=2
        SET &TO=4
     END

ISREDIT SCAN = OFF
ISREDIT FIND ' EXEC ' ALL
MOREEXEC: +
ISREDIT (ROW) = LINENUM .ZCSR
SET &ROWEXEC=&ROW
IF &ROWEXEC<2 THEN GOTO ENDEXEC
IF &ROWEXEC=&KEEPEXEC THEN GOTO ENDEXEC
ISREDIT (STRING) = LINE .ZCSR
IF '&STR(&SUBSTR(&FROM:&TO,&STR(&STRING).000))'='&STR(&REMARK)' +
THEN DO
       SET &KEEPEXEC=&ROWEXEC
       ISREDIT FIND ' EXEC ' NEXT
       GOTO MOREEXEC
     END
ELSE SET &SWEXEC=1
ENDEXEC: +
IF   &SWEXEC=0 THEN SET &ROWEXEC=1

SET &ROW=0
ISREDIT SCAN = OFF
ISREDIT FIND ' PROC ' ALL
MOREPROC: +
ISREDIT (ROW) = LINENUM .ZCSR
SET &ROWPROC=&ROW
IF &ROWPROC<2 THEN GOTO ENDPROC
IF &ROWPROC=&KEEPPROC THEN GOTO ENDPROC
ISREDIT (STRING) = LINE .ZCSR
IF '&STR(&SUBSTR(&FROM:&TO,&STR(&STRING).000))'='&STR(&REMARK)' +
THEN DO
       SET &KEEPPROC=&ROWPROC
       ISREDIT FIND ' PROC ' NEXT
       GOTO MOREPROC
     END
ELSE SET &SWPROC=1
ENDPROC: +
IF &SWPROC=0 THEN SET &ROWPROC=1

IF (&ROWPROC<2 AND &ROWEXEC<2) THEN GOTO FINISH
IF &ROWEXEC<2  THEN SET &ROWEXEC=&ROWPROC
IF &ROWPROC>1 +
THEN  IF &ROWPROC<&ROWEXEC THEN SET &ROWEXEC=&ROWPROC

PROCEED: +
SET &F11=&STR(&F1)
SET &F22=&STR(&F2)
SET &F33=&STR(&F3)
SET &F44=&STR(&F4)
SET &A11=&STR(&A1)
SET &B11=&STR(RL)
IF &A1=&STR(P) +
THEN DO
     SET &A11=&STR(2)
     SET &B11=&STR(BL)
     SET &F11=
     SET &F22=&STR($FIRST)
     SET &F33=
     SET &F44=
     END
IF &A1=&STR(C) +
  THEN SET &ASR11=&STR(RC)
  ELSE SET &ASR11=&STR(S&A11)
SET &F12=&STR(&F11..&F22)
SET &F34=&STR(&F33&F44)
IF &F34=  +
THEN +
  SET &CARD=&STR(// EXEC CONTROLR,PARM='&ASR11,&M1,&O1,&B11,&F12')
ELSE DO
     SET &CARD=&STR(// EXEC CONTROLR,)
     SET &CARD1=&STR(//     PARM='&ASR11,&M1,&O1,&B11,&F12,&F33..&F44')
  END

IF &S1=SAVE2 +
THEN SET &CARD=&STR(0&CARD)
IF &F34¬= +
THEN IF &S1=SAVE2 +
     THEN  SET &CARD1=&STR(0&CARD1)
     ELSE SET &CARD1=&STR(&CARD1)

ISREDIT LINE_BEFORE &ROWEXEC = "&CARD."
IF &F34¬= +
THEN DO
      SET &ROWEXEC = &ROWEXEC +1
      ISREDIT LINE_BEFORE &ROWEXEC = "&CARD1."
     END
SET &ROWEXEC = &ROWEXEC +1
SET &CARD2=&STR(//DAARCH  DD DSN=&SYSUID..O&O1..ARCHIVE,)
SET &CARD3=&STR(//  DISP=(SHR,KEEP) )
IF &S1=SAVE2 +
THEN DO
       SET &CARD2=&STR(0&CARD2)
       SET &CARD3=&STR(0&CARD3)
     END
ISREDIT LINE_BEFORE &ROWEXEC = "&CARD2."
SET &ROWEXEC = &ROWEXEC +1
ISREDIT LINE_BEFORE &ROWEXEC = "&CARD3."

FINISH: +
ISREDIT END
