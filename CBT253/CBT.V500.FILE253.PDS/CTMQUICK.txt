PROC 0 DEBUG(X) D10(X) D15(X) D20(X) D30(X) D40(X)

ISPEXEC CONTROL ERRORS CANCEL
SET &RC = &LASTCC
IF &RC ¬= 0 THEN EXIT QUIT

CONTROL MAIN NOLIST NOCONLIST NOSYMLIST NOMSG
IF &DEBUG=DEBUG THEN CONTROL MAIN LIST CONLIST SYMLIST MSG
CONTROL ASIS
SET &ACTION = CANCEL

/********************************************************/
/**                                                    **/
/**    CONTROL-M QUICK SCHEDULE DEFINITION             **/
/**                                                    **/
/**    CLIST CTMQUICK                                  **/
/**                                                    **/
/**    THIS IS THE ENTRY CLIST AND HIGH LEVEL LOGIC    **/
/**                                                    **/
/********************************************************/

ISPEXEC VPUT (ZHTOP)

SET &KILL = N   /* USER HAS NOT ENDED THE CLIST */

DO WHILE &KILL¬=X /*THAT IS, REPEAT UNTIL USER ENDS IT ALL */
SET &SKELVALD=N  /* WE DONT YET HAVE A VALID SKELETON */

DO WHILE (&SKELVALD=N)

  /********************************************************/
  /**    GET LIBRARY, TABLE, GROUP NAMES                 **/
  /********************************************************/

    %CTMCK10   DEBUG(&D10)

    ISPEXEC VGET (ZVERB)
    IF (&ZVERB = END) OR (&ZVERB = RETURN) THEN EXIT    QUIT

  /********************************************************/
  /**    GET SKELETON SCHEDULING TABLE                   **/
  /********************************************************/

    %CTMCK15   DEBUG(&D15)
    ISPEXEC VGET (IOERROR CTMPKIN SKELVALD)
    IF &IOERROR=Y THEN +
      DO
        ISPEXEC CONTROL ERRORS RETURN
        ISPEXEC LMCLOSE DATAID(&CTMPKIN)
        ISPEXEC LMFREE  DATAID(&CTMPKIN)
      END

END

/********************************************************/
/**    CREATE A CLEAN JOBNAME TABLE                    **/
/********************************************************/

  ISPEXEC CONTROL ERRORS RETURN

  ISPEXEC TBQUERY CTMPKT00
  SET &RC = &LASTCC
  IF &RC = 0 THEN  +
     ISPEXEC TBCLOSE CTMPKT00

  ISPEXEC TBQUERY CTMPKT99
  SET &RC = &LASTCC
  IF &RC = 0 THEN  +
     ISPEXEC TBCLOSE CTMPKT99

  ISPEXEC CONTROL ERRORS &ACTION

  ISPEXEC TBCREATE CTMPKT00 NAMES(POPER  OPER   JNAM +
     DJ1 DJ2 DJ3 DJ4 DJ5 DJ6 DJ7 DJ8 DJ9 DJ10 DJ11 DJ12 +
     T1 T2 T3 T4 T5 T6 T7 T8 T9 T10 T11 T12 +
     JNUM DEPS NUMDEPS DESC XCOND REBUILD DEPVAL WASCHG) NOWRITE

  ISPEXEC TBCREATE CTMPKT99 NAMES(TOJOB FRJOBNUM FRJOBNAM) NOWRITE

/********************************************************/
/**    GET JOBS LIST FROM THE USER                     **/
/********************************************************/

  %CTMCK20  DEBUG(&D20)

  ISPEXEC VGET (ZVERB HIUSED)
  SET &RESP = &ZVERB

  IF &RESP = RETURN THEN  EXIT QUIT /* USER HIT RETURN KEY */

  IF &HIUSED=0 THEN SET &ENDED=Y
     ELSE SET &ENDED=N

  DO WHILE (&ENDED=N)
    /*****************************************/
    /**    GET FINAL DESTINATION OF TABLE   **/
    /*****************************************/
    %CTMCK30    DEBUG(&D30)
    ISPEXEC VGET (ENDED)
    IF (&ENDED=Y) THEN ISPEXEC SETMSG MSG(CTMK003B)
    ELSE +
      DO
        /*****************************************/
        /**    WRITE THE SCHEDULING TABLE       **/
        /*****************************************/
        %CTMCK40   DEBUG(&D40)
        ISPEXEC VGET (IOERROR CTMPKOT)
        IF &IOERROR=N THEN SET &ENDED=Y
        ELSE +
          DO
            ISPEXEC LMCLOSE DATAID(&CTMPKOT)
            ISPEXEC LMFREE  DATAID(&CTMPKOT)
          END
      END
  END
END  /* OF MAIN LOOP AT HIGHEST SCREEN LEVEL */
EXIT
