/*  CA-LIBRARIAN LIB/CCF/ELIPS ASSEMBLE/COMPILE REXX EXEC INTERFACE
    TO ISPF PANEL DIALOG - ( BATCH PROGRAMS AND OS/VS COBOL COMPILER ).
                                                                      */

TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
SAVEMEMB = 'NULLMEMB'
ACCTCDE = 5200010520000000
JCL.1 = 'CCFCOBOL'
JCL.2 = 'CCFCOBCO'
TSOID = SYSVAR(SYSUID)
CP = 'APOST,LIB'
CCFLOAD = 'SYS1.TEST.BATCH.LOADLIB'
EMERGENCY_ID= SUBSTR(TSOID,1,4)
IF EMERGENCY_ID= 'OPER' THEN CCFLOAD = 'SYS1.EMERGNCY.BATCH.LOADLIB'
ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
ADDRESS ISPEXEC "FTCLOSE"

/*     */
DISPLAY_PANEL:
DO FOREVER
   ADDRESS ISPEXEC "DISPLAY PANEL(COBCOMPB)"
   SRC = RC
   IF SRC = 8 THEN LEAVE
   IF SRC > 8 THEN DO
      SAY '*** ERROR INVOKING ISPF DIALOG PANEL COBCOMPB - RC = 'SRC'.***'
      EXIT SRC
   END
   CTYP = COMPTYP
   IF MEMBER = SAVEMEMB THEN EXIT
   CALL SELECT_COMPILE_JCL
   CALL SUBMIT_COMPILE_JCL
END
EXIT

/*     */
SELECT_COMPILE_JCL:
SAVEMEMB = MEMBER
SKELJCL = JCL.CTYP
MASTER = LIBPRJ||'.'LIBGRP||'.'LIBTYP
COPYLB1 = LIBPRJ||'.TESTCOPY.MASTER'
COPYLB2 = LIBPRJ||'.PRODCOPY.MASTER'
SUBNODE = SUBSTR(LIBGRP,1,4)
IF SUBNODE = 'PROD' THEN COPYLB1 = LIBPRJ||'.PRODCOPY.MASTER'
LOADLIB = CCFLOAD
IF EMERGENCY_ID = 'OPER' THEN DO
   MASTER = 'SYS1.EMRGSRCE.MASTER'
   COPYLB1 = 'SYS1.EMRGCOPY.MASTER'
   LOADLIB = 'SYS1.EMERGNCY.BATCH.LOADLIB'
END
SELECT
   WHEN SKELJCL = 'CCFCOBOL' THEN COMPMSG = TSOID||'.BATCH.COBOL'
   WHEN SKELJCL = 'CCFCOBCO' THEN COMPMSG = TSOID||'.COBOL.NOLINK'
  OTHERWISE COMPMSG = TSOID||'.COMPILE.LINK'
END
RETURN

/*     */
SUBMIT_COMPILE_JCL:
ADDRESS ISPEXEC "FTOPEN TEMP"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA001
   ADDRESS ISPEXEC "SETMSG MSG($CCFA001)"
   RETURN
   END
ADDRESS ISPEXEC "FTINCL $CCFJOBC"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA002
   ADDRESS ISPEXEC "SETMSG MSG($CCFA002)"
   RETURN
   END
ADDRESS ISPEXEC "FTINCL" SKELJCL
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA003
   ADDRESS ISPEXEC "SETMSG MSG($CCFA003)"
   RETURN
   END
ADDRESS ISPEXEC "FTCLOSE"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA005
   RETURN
   END
ADDRESS ISPEXEC "VGET ZTEMPF"
ADDRESS TSO "SUBMIT '"ZTEMPF"'"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA006
   RETURN
   END
ZERRMSG = $CCFA000
ADDRESS ISPEXEC "SETMSG MSG($CCFA000)"
RETURN
