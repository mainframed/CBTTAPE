/* REXX CICS transaction dump processor */
tasklist = ""; /* List of tasks abended */
codelist = ""; /* List of codes encountered */
cicslist = ""; /* LIst of CICS regions processed */
firstdate="";
lastdate="";
outctr = 0;
true = "1";
false = ¬true;
eof = false;
do while(¬eof); /* Process all records */
     "EXECIO 1 DISKR DUMPIN (STEM DUMPIN.";
     if rc¬=0 then do;
            eof = true;
            iterate;
            end;
     piece = substr(dumpin.1,1,1);
     if piece¬="1" & piece¬="-" then iterate;
     parse var dumpin.1 p1 p2 p3 p4 p5 p6 p7 p8 p9 p10 p11 p12 p13;
     if p3="CICS" & p4="TRANSACTION" & p5="DUMP" & p11="PAGE" & ,
        p12="1" then do;
            cicsname = substr(p1,2);
            codename = strip(substr(p7,6));
            if codename="" then codename="????";
            if codename=" " then codename="????";
            taskname = strip(substr(p8,6));
            if taskname="" then taskname="????";
            if taskname=" " then taskname="????";
            savecode = p7;
            savetask = p8;
            savedate = p9;
            parse var savedate "DATE=" lastdate;
            if firstdate="" then ,
                parse var savedate "DATE=" firstdate;
            savetime = p10;
            end;
     else if p1="-SYMPTOMS=" then do;
            parse var dumpin.1 . "RIDS/" rids;
            rids=left("RIDS="strip(rids),15);
            outctr = outctr + 1;
            output.outctr = ,
                 savedate savetime  ,
                 left(savetask,10) left(savecode,10) rids ,
                 substr(dumpin.1,2);
            if wordpos(cicsname,cicslist)=0 then ,
                 cicslist=cicslist cicsname;
            if wordpos(taskname,tasklist)=0 then ,
                 tasklist=tasklist taskname;
            if wordpos(codename,codelist)=0 then ,
                 codelist=codelist codename;
            if symbol('task.taskname')="VAR" then ,
                 task.taskname=task.taskname+1;
             else task.taskname=1;
            if symbol('code.codename')="VAR" then ,
                 code.codename=code.codename+1;
             else code.codename=1;
            end;
     else iterate;
end;
cicsctr = words(cicslist);
say center("    Charming Shoppes, Inc.    ",130,"*");
say center(" Technical Support Department ",130,"*");
say center("    CICS Daily ABEND Report   ",130,"*");
say center("    For "firstdate" - "lastdate"   ",130,"*");
say "";
say "";
say "";
say "CICS Region(s) Processed";
say "--------------------------";
do i = 1 to cicsctr;
   xcics = word(cicslist,i);
   say xcics;
end i;
taskctr = words(tasklist);
say "";
say "";
say "";
say "Task ABEND Summary with Counters";
say "--------------------------------";
do i = 1 to taskctr;
   xtask = word(tasklist,i);
   say xtask right(task.xtask,5);
end i;
codectr = words(codelist);
say "";
say "";
say "";
say "Code ABEND Summary with Counters";
say "--------------------------------";
do i = 1 to codectr;
   xcode = word(codelist,i);
   say xcode right(code.xcode,5);
end i;
say "";
say "";
say "";
say "Detailed ABEND Records";
say "----------------------";
do i = 1 to outctr;
   say output.i;
   say "";
end i;
