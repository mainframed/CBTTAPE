/* REXX EXEC  */
/* LIB/CCF DYLAKOR QA TURNOVER COMPILE FACILITY TO DETERMINE IF THE
   SOURCE PROGRAM CONTAINS COMMENT TO INDICATE A MULTIPLE REPORT AND
   MULTIPLE LOAD MODULE TO BE MOVED.   */

TRACE O
/*     */
HOUSE_KEEPING:
ARG TSOUSER MEMBER
IF MEMBER = '' THEN DO
   SAY '*** MEMBER NAME NOT PASSED ON COMMAND LINE. ***'
   EXIT 4
END
EXITRC = 4
PREFIX = SUBSTR(MEMBER,1,6)
DELTEST = '  DELETE SYS1.TEST.BATCH.LOADLIB'
DELQA = '  DELETE SYS1.QA.BATCH.LOADLIB'
ADDRESS TSO
"ALLOC DA('"TSOUSER"."MEMBER".SEQFILE') F(ISPFILE) SHR"
IF RC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR:' TSOUSER||'.'||MEMBER||'.'||SEQFILE '***'
   EXIT 8
END
"EXECIO 1 DISKR ISPFILE (FINIS STEM REPORT."
SRC = RC
"FREE F(ISPFILE)"
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET EXECIO ERROR:' TSOUSER||'.'||MEMBER||'.'||SEQFILE '***'
   EXIT 8
END
REPTFLG = POS('* REPORTS = ',REPORT.1)
IF REPTFLG ¬= 0 THEN DO
   NUMREPTS = WORD(REPORT.1,4)
   IF NUMREPTS = '' THEN EXIT EXITRC
   RTYP = DATATYPE(NUMREPTS)
   IF RTYP = 'NUM' THEN DO
      CALL BUILD_REPORT_MODULE_LIST
      CALL BUILD_DELETE_TEST_SYSIN
      CALL BUILD_COPY_MODULE_SYSIN
      CALL BUILD_DELETE_QA_SYSIN
      EXITRC = 0
   END
END
EXIT EXITRC
/*     */
BUILD_REPORT_MODULE_LIST:
M = 0
MCNT = 0
DO NUMREPTS
   M = M + 1
   IF M = 1 THEN ITERATE
   MCNT = MCNT + 1
   MLEN = LENGTH(M)
   IF MLEN = 1 THEN MOD.MCNT = PREFIX||'0'M
   IF MLEN = 2 THEN MOD.MCNT = PREFIX||M
END
RETURN
/*     */
BUILD_DELETE_TEST_SYSIN:
M = 0
PDSMEMB = 'DYLDELTS'
"DELSTACK"
DO MCNT
   M = M + 1
   RECORD = DELTEST||"("MOD.M") FILE(DASDVOL)"
   QUEUE RECORD
END
CALL WRITE_PDS_MEMBER
RETURN
/*     */
BUILD_COPY_MODULE_SYSIN:
I = 0
M = 0
PDSMEMB = 'DYLCOPY'
CNTL = "       SELECT MEMBER=("
"DELSTACK"
RECORD = "  COPY OUTDD=OUTDD1"
QUEUE RECORD
RECORD = "       INDD=((INDD1,R))"
QUEUE RECORD
DO MCNT
   M = M + 1
   I = I + 1
   IF I = 1 THEN SYSIN = CNTL||MOD.M
   IF (I > 1) & (I < 5) THEN SYSIN = SYSIN||","||MOD.M
   IF (I = 5) THEN DO
      SYSIN = SYSIN||","||MOD.M||")"
      QUEUE SYSIN
      I = 0
   END
END
IF I > 0 THEN DO
   SYSIN = SYSIN||")"
   QUEUE SYSIN
END
CALL WRITE_PDS_MEMBER
RETURN
/*     */
BUILD_DELETE_QA_SYSIN:
M = 0
PDSMEMB = 'DYLDELQA'
"DELSTACK"
DO MCNT
   M = M + 1
   RECORD = DELQA||"("MOD.M") FILE(DASDVOL)"
   QUEUE RECORD
END
CALL WRITE_PDS_MEMBER
RETURN
/*     */
WRITE_PDS_MEMBER:
ADDRESS TSO
"ALLOC DA('SYSS.TESTCCF.TURNOVER.CNTLLIB("PDSMEMB")') F(DYLCNTL) OLD"
IF RC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ( SYSS.TESTCCF.TURNOVER.CNTLLIB ). ***'
   EXIT 8
END
RCDCNT = QUEUED()
"EXECIO" RCDCNT "DISKW DYLCNTL (FINIS"
SRC = RC
"FREE F(DYLCNTL)"
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET EXECIO ERROR: ( SYSS.TESTCCF.TURNOVER.CNTLLIB ). ***'
   EXIT 8
END
RETURN
