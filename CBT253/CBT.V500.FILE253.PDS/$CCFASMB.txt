/*  CA-LIBRARIAN LIB/CCF/ELIPS LIBRARIAN ASSEMBLE/COMPILE REXX EXEC
    INTERFACE TO ISPF PANEL DIALOG - ( BATCH PROGRAMS - COBOL2 ).
                                                                      */

TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS ISPEXEC
CMPONLY = 'N'
JCL.1 = 'CCFCOB2'
JCL.2 = 'CCFDYL'
JCL.3 = 'CCFASM'
JCL.4 = 'CCFRPGIA'
JCL.5 = 'CCFRPGII'
JCL.6 = 'CCFOPTII'
X = MSG("OFF")
TSOID = SYSVAR(SYSUID)
ACCTCCF = 5200010520000000
COBOPT = 'APOST,LIB,RENT,RES,DYNAM,OFFSET,CMPR2'
PCOBOPT = 'APOST,LIB,RENT,RES,DYNAM,OFFSET,NOCMPR2'
TSTCAOPT = 'DCR,MMA,MOF,MXR,FDC,PAR,SUB,PFL,XCO,XTI,WSI'
PRDCAOPT = 'COPT,NOWSI,NODTE,NOFDC,NOMOF,NOPFL,NOSUB,NOXCO,NOXTI'
ACCTSW = 'NO'
QACOMP = 'N'
C1 = 1
C2 = 2
C3 = 4
C4 = 5
COPYLIB.1 = 'SYS1.TESTCOPY.MASTER'
COPYLIB.2 = 'SYS1.TURNCOPY.MASTER'
COPYLIB.3 = 'SYS1.RJCTCOPY.MASTER'
COPYLIB.4 = 'SYS1.QACOPY.MASTER'
COPYLIB.5 = 'SYS1.PRODCOPY.MASTER'
COPYLIB.6 = 'SYS1.EMRGCOPY.MASTER'
CCFLOAD = 'SYS1.TEST.BATCH.LOADLIB'
EMRGID = SUBSTR(TSOID,1,6)
IF EMRGID = 'TECH29' THEN DO
   EMERGSW = 'YES'
   C1 = 6
   C2 = 5
   C3 = 4
   C4 = 2
   CCFLOAD = 'SYS1.EMERGNCY.BATCH.LOADLIB'
END
"CONTROL ERRORS RETURN"
"FTCLOSE"
"VGET (ACCTCDE) PROFILE"
IF RC ¬= 0 THEN DO
   ACCTSW = 'YES'
   ACCTCDE = ACCTCCF
END
"VGET (CCOPT) PROFILE"
IF RC ¬= 0 THEN CCOPT = COBOPT
"VGET (CAOPT) PROFILE"
IF RC ¬= 0 THEN CAOPT = TSTCAOPT
/*
        M A I N   R O U T I N E   S E C T I O N      */
/*     */
DISPLAY_PANEL:
DO FOREVER
   ADDRESS ISPEXEC "DISPLAY PANEL($CCFBPN1)"
   SRC = RC
   IF SRC = 8 THEN LEAVE
   IF SRC > 8 THEN DO
      SAY '*** ERROR INVOKING ISPF DIALOG PANEL $CCFBPN1 - RC = 'SRC'.***'
      EXIT SRC
   END
   MASTER = LIBPRJ||'.'||LIBGRP||'.'||LIBTYP
   CALL CHECK_MASTER_MEMBER
   IF MODSW = 'NO' THEN ITERATE
   CTYP = COMPTYP
   IF CTYP = 1 THEN DO
      CALL CHECK_COBOL2_NATIVE_FLAG
      CALL CHECK_OPTIMIZE_FLAG
      C2P = CCOPT
      CMPR = POS('NOCMPR2',C2P)
      IF CMPR = 0 & NATIVE_SW = 'YES' THEN C2P = C2P||',NOCMPR2'
      IF QACOMP = 'Y' & NATIVE_SW = 'YES' THEN C2P = PCOBOPT
      IF QACOMP = 'Y' & NATIVE_SW = 'NO' THEN C2P = COBOPT
   END
   IF OPTIMIZE_SW = 'YES' THEN DO
      CTYP = 6
      C2P = CAOPT
      IF QACOMP = 'Y' THEN C2P = PRDCAOPT
   END
   MVSSYS = 'SYSA'
   CALL BUILD_COPYBOOK_SEARCH_CHAIN
   CALL SELECT_COMPILE_JCL
   IF SKELSW = 'ERROR' THEN ITERATE
   CALL SELECT_JOB_MESSAGE_TEXT
   CALL SUBMIT_COMPILE_JCL
   COMPTYP = ''
   MEMBER = ''
END
ADDRESS ISPEXEC
"VPUT (CCOPT) PROFILE"
"VPUT (CAOPT) PROFILE"
IF ACCTSW = 'YES' THEN "VPUT (ACCTCDE) PROFILE"
ADDRESS TSO
"FREE F("LIBDD")"
"FREE F("MASTDD")"
EXIT 0
/*
        S U B R O U T I N E   S E C T I O N      */
/*     */
CHECK_MASTER_MEMBER:
ADDRESS TSO
MODSW = 'NO'
LIBDSN = "'"MASTER"("MEMBER")'"
LIBDD = LIBALLOC(LIBDSN)
IF LIBDD = "ERROR" THEN DO
   SAY '*** ALLOCATION ERROR: ('MASTER'('MEMBER'). ***'
   SAY '*** DYNAMIC ALLOCATION FAILED FOR THE MASTER DATASET - RETRY TASK. ***'
   SAY '*** COMPB COMPILE SUBTASK CANCELLED. ***'
   EXIT
END
MASTDD = LIBALLOC("'"MASTER"'")
MASTMEM = XLIBEMEM(MASTDD,MEMBER)
"EXECIO 0 DISKR" MASTDD "(FINIS"
IF MASTMEM = ""  THEN DO
   "FREE F("LIBDD")"
   "FREE F("MASTDD")"
   ADDRESS ISPEXEC
   ZERRMSG = $CCFA007
   "SETMSG MSG($CCFA007)"
   RETURN
END
MODSW = 'YES'
RETURN
/*     */
CHECK_COBOL2_NATIVE_FLAG:
ADDRESS TSO
NATIVE_SW = 'NO'
"EXECIO * DISKR" LIBDD "(STEM PROGDATA. FINIS"
DO I = 1 TO PROGDATA.0
   NOCMPR2 = POS('NOCMPR2-FLAG',PROGDATA.I)
   IF NOCMPR2 ¬= 0 THEN DO
      NATIVE_SW = 'YES'
      LEAVE
   END
END
RETURN
/*     */
CHECK_OPTIMIZE_FLAG:
OPTIMIZE_SW = 'NO'
DO I = 1 TO PROGDATA.0
   OPTFLAG = POS('OPT2-FLAG',PROGDATA.I)
   IF OPTFLAG ¬= 0 THEN DO
      OPTIMIZE_SW = 'YES'
      LEAVE
   END
END
RETURN
/*     */
BUILD_COPYBOOK_SEARCH_CHAIN:
COPYLB1 = COPYLIB.C1
COPYLB2 = COPYLIB.C2
COPYLB3 = COPYLIB.C3
COPYLB4 = COPYLIB.C4
RETURN
/*     */
SELECT_COMPILE_JCL:
SKELSW = 'OK'
SKELJCL = JCL.CTYP
MASTER = LIBPRJ||'.'LIBGRP||'.'LIBTYP
LOADLIB = CCFLOAD
IF EMERGSW = 'YES' THEN DO
   MASTER = 'SYS1.EMRGSRCE.MASTER'
   LOADLIB = 'SYS1.EMERGNCY.BATCH.LOADLIB'
   IF CTYP = 6 THEN C2P = PRDCAOPT
END
IF LOADLIB = 'SYS1.EMERGNCY.BATCH.LOADLIB' THEN DO
   IF MASTER ¬= 'SYS1.EMRGSRCE.MASTER' THEN DO
      SKELSW = 'ERROR'
      EMRGMAST = 'SYS1.EMRGSRCE.MASTER'
      ZERRMSG = $CCFA100
      ADDRESS ISPEXEC "SETMSG MSG($CCFA100)"
      RETURN
   END
END
IF CTYP = 1 & CMPONLY = 'Y' THEN SKELJCL = 'CCFCOB2O'
IF CTYP = 6 & CMPONLY = 'Y' THEN SKELJCL = 'CCFOPTCO'
IF LIBGRP = 'RJCTSRCE' THEN DO
   LOADLIB = 'SYS1.REJECT.BATCH.LOADLIB'
   QACOMP = 'Y'
   CMPONLY = 'N'
   IF CTYP = 1 THEN SKELJCL = 'CCFCOBR1'
   IF CTYP = 6 THEN DO
      SKELJCL = 'CCFCOBR2'
      C2P = PRDCAOPT
   END
END
RETURN
/*     */
SELECT_JOB_MESSAGE_TEXT:
SELECT
   WHEN SKELJCL = 'CCFASM' THEN COMPMSG = TSOID||'.BAL.ASSEM'
   WHEN SKELJCL = 'CCFCOB2' THEN COMPMSG = TSOID||'.BATCH.COBII'
   WHEN SKELJCL = 'CCFCOB2O' THEN COMPMSG = TSOID||'.COBII.NOLINK'
   WHEN SKELJCL = 'CCFOPTII' THEN COMPMSG = TSOID||'.COB2.OPTII'
   WHEN SKELJCL = 'CCFOPTCO' THEN COMPMSG = TSOID||'.COBOPT.NOLNK'
   WHEN SKELJCL = 'CCFDYL' THEN COMPMSG = TSOID||'.DYL280.COMP'
   WHEN SKELJCL = 'CCFRPGIA' THEN COMPMSG = TSOID||'.RPGIAUTO'
   WHEN SKELJCL = 'CCFRPGII' THEN COMPMSG = TSOID||'.RPGII.COMP'
  OTHERWISE COMPMSG = TSOID||'.COMPILE.LINK'
END
RETURN
/*     */
SUBMIT_COMPILE_JCL:
ADDRESS ISPEXEC
"FTOPEN TEMP"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA001
   "SETMSG MSG($CCFA001)"
   RETURN
END
"FTINCL $CCFJOBC"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA002
   "SETMSG MSG($CCFA002)"
   RETURN
END
"FTINCL" SKELJCL
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA003
   "SETMSG MSG($CCFA003)"
   RETURN
END
"FTCLOSE"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA005
   "SETMSG MSG($CCFA005)"
   RETURN
END
"VGET ZTEMPF"
ADDRESS TSO "SUBMIT '"ZTEMPF"'"
SRC = RC
ADDRESS ISPEXEC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA006
   "SETMSG MSG($CCFA006)"
   RETURN
END
ZERRMSG = $CCFA000
"SETMSG MSG($CCFA000)"
RETURN
