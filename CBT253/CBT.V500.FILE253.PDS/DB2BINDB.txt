/*  REXX EXEC : ( DB2BINDB ).
    FUNCTION  : CALLED BY THE DB2 BATCH COMPILE TASK ( DB2B ) TO
                BUILD THE DB2 BIND PLAN SYSIN MEMBER.
                A BIND JOB WILL BE WRITTEN AND SUBMITTED TO THE
                SYSA SYSTEM ( DB2SYSX - DSNX ).
    INPUT     : TSOID.USER.DBRMLIB(&&MEMBER).
    OUTPUT    : SYSS.TESTCCF.ISPFILE.CNTLLIB(&&MEMBER) -
                BATCH JOB SUBMITTED TO SYSA SYSTEM CLASS=P.
                                                                     */
TRACE O
/*      */
HOUSE_KEEPING:
ADDRESS TSO
ARG TSOID ACCTCDE MEMBER DB2COLL DB2SYS
PARMS = 'TSOID ACCTCDE MEMBER DB2COLL DB2SYS'
SRC = 0
DO I = 1 TO 5
   IPARM = WORD(PARMS,I)
   XPARM = VALUE(IPARM)
   IF XPARM ¬= '' THEN ITERATE
   SAY '*** PARM ERROR: MISSING PARM VALUE FOR ('IPARM'). ***'
   SAY '*** DB2BINDB EXEC TASK CANCELLED - RC = (024). ***'
   EXIT 024
END
IF DB2SYS = 'DSNX' THEN EXIT 0   /* ***   N O O P   D S N X   *** */
GRANTPDS = 'SYSS.TESTCCF.GRANT.CNTLLIB'
ISPFILE = 'SYSS.TESTCCF.ISPFILE.CNTLLIB'
UCNTL = SYSDSN("'"ISPFILE"'")
IF UCNTL = 'DATASET NOT FOUND' THEN DO
   IF SRC ¬= 0 THEN DO
      SAY '*** ALLOCATE ERROR: DATASET NOT FOUND ('ISPFILE'). ***'
      SAY '*** DB2BINDB EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
      EXIT 100
   END
END
X = MSG("OFF")
"FREE FILE(ISPDD)"
"FREE FILE(PARMDD)

/*     */
MAIN_ROUTINE_DSNX:
DB2PLAN = DB2COLL
DB2NODE = 'DB2X'
EXPLN = 'YES'
CALL CHECK_GRANT_CNTLLIB
CALL BUILD_BIND_PLAN_JOBCARD
INTERPRET CALL LABEL
CALL BUILD_BIND_PLAN_SYSIN
IF LABEL = 'BUILD_IKJEFT01_GRANTJCL' THEN CALL BUILD_GRANT_CNTLLIB_JCL
CALL BUILD_IDCAMS_DELETE_MEMBER
CALL WRITE_BIND_PLAN_JOB
CALL SUBMIT_BIND_PLAN_JOB
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
CHECK_GRANT_CNTLLIB:
LABEL = 'BUILD_IKJEFT01_GRANTJCL'
GMEMB = SYSDSN("'"GRANTPDS"("DB2PLAN")'")
IF GMEMB = 'MEMBER NOT FOUND' THEN RETURN
"ALLOC DA('"GRANTPDS"("DB2PLAN")') F(PARMDD) SHR"
"EXECIO * DISKR PARMDD (FINIS STEM GPARM."
"FREE F(PARMDD)"
DO I = 1 TO GPARM.0
   IF GPARM.I = DB2SYS THEN LABEL = 'BUILD_IKJEFT01_NOGRANT'
END
RETURN

/*     */
BUILD_BIND_PLAN_JOBCARD:
JN = MEMBER
"DELSTACK"
JCL.1 = "//"JN" JOB" ACCTCDE",'"TSOID".DB2.BINDPLAN',NOTIFY="TSOID","
JCL.2 = "//          CLASS=P,MSGCLASS=S,MSGLEVEL=(1,0),REGION=6M"
JCL.3 = "/*JOBPARM SYSAFF=SYSA"
DO J = 1 TO 3
   QUEUE JCL.J
END
RETURN

/*     */
BUILD_IKJEFT01_GRANTJCL:
RCDCNT = 10
JCL.1 = "//DSNXBIND EXEC PGM=IKJEFT01,DYNAMNBR=20"
JCL.2 = "//DBRMLIB  DD DSN="TSOID".USER.DBRMLIB,DISP=SHR"
JCL.3 = "//SYSTSPRT DD SYSOUT=*"
JCL.4 = "//SYSPRINT DD SYSOUT=*"
JCL.5 = "//SYSUDUMP DD SYSOUT=*"
JCL.6 = "//SYSOUT   DD SYSOUT=*"
JCL.7 = "//SYSIN    DD *"
JCL.8 = " GRANT BIND, EXECUTE ON PLAN "DB2PLAN" TO PUBLIC;"
JCL.9 = "/*"
JCL.10 = "//SYSTSIN  DD *"
DO J = 1 TO RCDCNT
   QUEUE JCL.J
END
RETURN

/*     */
BUILD_IKJEFT01_NOGRANT:
RCDCNT = 8
JCL.1 = "//DSNXBIND EXEC PGM=IKJEFT01,DYNAMNBR=20"
JCL.2 = "//DBRMLIB  DD DSN="TSOID".USER.DBRMLIB,DISP=SHR"
JCL.3 = "//SYSTSPRT DD SYSOUT=*"
JCL.4 = "//SYSPRINT DD SYSOUT=*"
JCL.5 = "//SYSUDUMP DD SYSOUT=*"
JCL.6 = "//SYSOUT   DD SYSOUT=*"
JCL.7 = "//SYSIN    DD DUMMY,DCB=BLKSIZE=80"
JCL.8 = "//SYSTSIN  DD *"
DO J = 1 TO RCDCNT
   QUEUE JCL.J
END
RETURN

/*     */
BUILD_BIND_PLAN_SYSIN:
RCDCNT = 16
REC.1 = " DSN SYSTEM("DB2SYS")"
REC.2 = " BIND PLAN ("DB2PLAN") -"
REC.3 = "      OWNER("TSOID") -"
REC.4 = "      VALIDATE(BIND) -"
REC.5 = "      FLAG(I) -"
REC.6 = "      ACQUIRE(USE) -"
REC.7 = "      ISOLATION(CS) -"
REC.8 = "      RELEASE(COMMIT) -"
REC.9 = "      EXPLAIN("EXPLN") -"
REC.10 = "      ACTION (REPLACE) RETAIN -"
REC.11 = "      PKLIST("DB2COLL".*) -"
REC.12 = "      ENABLE(*)"
REC.13 = " RUN PROGRAM(DSNTIAD) PLAN(DSNTIA31) -"
REC.14 = "     LIB('SYSS.DB2.V2R3M0."DB2NODE".RUNLIB.LOAD')"
REC.15 = " END"
REC.16 = "/*"
DO R = 1 TO RCDCNT
   QUEUE REC.R
END
RETURN

/*     */
BUILD_GRANT_CNTLLIB_JCL:
JCLCNT = 10
JCL.1 = "//DB2GRNTP EXEC PGM=IKJEFT01,DYNAMNBR=30,"
JCL.2 = "//  PARM='%DB2GRNTP "DB2PLAN" "DB2SYS"',COND=(0,LT,DSNXBIND)"
JCL.3 = "//SYSTSPRT DD SYSOUT=*"
JCL.4 = "//SYSPRINT DD SYSOUT=*"
JCL.5 = "//SYSIN    DD DUMMY"
JCL.6 = "//SYSLBC   DD DSN=SYS1.BRODCAST,DISP=SHR"
JCL.7 = "//SYSUADS  DD DSN=SYS1.UADS,DISP=SHR"
JCL.8 = "//SYSPROC  DD DSN=SYSS.TECH.COMMON.EXECLIB,DISP=SHR"
JCL.9 = "//SYSTSIN  DD DUMMY"
JCL.10 = "/*"
DO J = 1 TO JCLCNT
   QUEUE JCL.J
END
RETURN

/*     */
BUILD_IDCAMS_DELETE_MEMBER:
RCDCNT = 11
JCL.1 = "//DELETE   EXEC PGM=IDCAMS,COND=(0,LT,DSNXBIND)"
JCL.2 = "//ISPDD    DD DSN="ISPFILE",DISP=SHR"
JCL.3 = "//SYSPRINT DD SYSOUT=*"
JCL.4 = "//SYSIN    DD *"
JCL.5 = "  DELETE  "ISPFILE"("MEMBER")  FILE(ISPDD)"
JCL.6 = "  IF LASTCC = 8 THEN"
JCL.7 = "     DO"
JCL.8 = "       SET MAXCC = 0"
JCL.9 = "     END"
JCL.10 = "/*"
JCL.11 = "//"
DO J = 1 TO RCDCNT
   QUEUE JCL.J
END
RETURN

/*     */
WRITE_BIND_PLAN_JOB:
"ALLOC DA('"ISPFILE"("MEMBER")') FILE(ISPDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('ISPFILE'('MEMBER'). ***'
   SAY '*** DB2BINDB EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT 100
END
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW ISPDD (FINIS"
SRC = RC
"FREE FILE(ISPDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING ('ISPFILE'('MEMBER'). ***'
   SAY '*** DB2BINDB EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
SUBMIT_BIND_PLAN_JOB:
"SUBMIT ('"ISPFILE"("MEMBER")')"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** SUBMIT ERROR: ('ISPFILE'('MEMBER'). ***'
   SAY '*** DB2BINDB EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN
