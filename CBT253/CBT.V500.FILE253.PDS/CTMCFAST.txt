PROC 0 DATEFMT(USA) DEBUG
/*******************************************************/
/**  P.P.F. - PARAMETERS PROMPTING FACILITY - TYPE1   **/
/**                                                   **/
/**  THE CLIST DISPLAYS A SCREEN THAT GETS THE NAME   **/
/**  OF THE TABLE TO WORK ON.                         **/
/**  THE PARAMETERS VALUES WILL BE UPDATED IN THIS    **/
/**  TABLE.                                           **/
/**                                                   **/
/**  CALLED BY THE PROGRAM : CTMTPPF.                 **/
/**                                                   **/
/*******************************************************/
 ISPEXEC CONTROL ERRORS RETURN
 IF &DEBUG = DEBUG +
 THEN +
    CONTROL LIST MSG CONLIST SYMLIST
 ELSE +
    CONTROL NOLIST NOMSG
 SET &T = T
 SET &M = M
 ISPEXEC VGET ( ZDAY ZMONTH ) SHARED
 ISPEXEC VPUT (DATEFMT) SHARED
 SET &DD = &STR(&ZDAY)
 SET &MM = &STR(&ZMONTH)
 SET &DDMM = &STR(&ZDAY.&ZMONTH)
 SET &PFK3 = ' '
 ISPEXEC VPUT (DDMM) SHARED
 ISPEXEC TBOPEN PROMPTB NOWRITE
 SET &RC = &LASTCC
 IF &RC ¬= 0  -
 THEN -
    IF &RC = 8 -
    THEN +
       DO
         SET &PFK3 = PFK3
         ISPEXEC SETMSG MSG(CTMP005B)
         GOTO END
       END
    ELSE +
       DO
         SET &PFK3 = PFK3
         ISPEXEC SETMSG MSG(CTMP000Z)
         GOTO END
       END
DISP0: +
 ISPEXEC TBTOP PROMPTB
 ISPEXEC TBSORT PROMPTB FIELDS(TBNAME)
DISP01: +
 ISPEXEC TBVCLEAR PROMPTB
 SET &TBNAME = &STR(&PREFIX.&STR(*))
 ISPEXEC TBSARG PROMPTB
 SET &RC = &LASTCC
 IF &RC = 0 -
 THEN -
    GOTO REDISP
 ELSE -
    DO
      SET &SPFRC = &RC
      ISPEXEC SETMSG MSG(CTMP004F)
      GOTO REDISP
    END
 /* SET QCRP = 0
 SET CURNAME = TCMD
REDISP: +
 IF &DATEFMT = JPN -
 THEN -
    DO
       ISPEXEC TBDISPL PROMPTB PANEL(CTMPP05J)
    END
 ELSE -
    IF &DATEFMT = USA -
    THEN -
       DO
          ISPEXEC TBDISPL PROMPTB PANEL(CTMPP05A)
       END
    ELSE -
       DO
          ISPEXEC TBDISPL PROMPTB PANEL(CTMPP05W)
       END
 SET &RC = &LASTCC
 IF &RC ¬= 0 -
 THEN -
    IF &RC ¬= 8 -
    THEN -
       DO
          SET &PFK3 = PFK3
          ISPEXEC SETMSG MSG(CTMP000Z)
          GOTO END
       END
    ELSE -
       DO
          SET &PFK3 = PFK3
          GOTO END
       END
 IF &LENGTH(&TCMD) > 2 -
 THEN -
    DO
      SET &CMDPREF = &SUBSTR(1:2,&TCMD)
      IF &CMDPREF = L OR &CMDPREF = l -
      THEN -
         DO
            SET &LOCARG = &SUBSTR(3:&LENGTH(&TCMD),&TCMD)
            SET &LOCARG = &STR(&LOCARG.&STR(*))
            SET &TBNAME = &STR(&LOCARG)
            ISPEXEC TBSCAN PROMPTB ARGLIST(TBNAME) NOREAD
            SET &RC = &LASTCC
            IF &RC = 0 -
            THEN +
               GOTO REDISP
            ELSE +
               IF &RC = 8 +
               THEN +
                  DO
                    ISPEXEC SETMSG MSG(CTMP004C)
                    GOTO REDISP
                  END
               ELSE +
                  DO
                    ISPEXEC SETMSG MSG(CTMP000Z)
                    GOTO REDISP
                  END
         END
      ELSE -
         DO
           ISPEXEC SETMSG MSG(CTMP004O)
           GOTO REDISP
         END
    END
 ELSE -
    IF &TCMD ¬= &STR() -
    THEN -
       DO
          ISPEXEC SETMSG MSG(CTMP004O)
           GOTO REDISP
       END
 IF &LASTPREF ¬= &PREFIX +
 THEN +
    DO
       SET &LASTPREF = &STR(&PREFIX)
       GOTO DISP01
    END
 IF &ZTDSELS = 0 -
 THEN +
    GOTO REDISP
 IF &SRCMM = &STR() -
 THEN -
    DO
       SET &SRCDD = &DD
       SET &SRCMM = &MM
    END
 IF &DATEFMT = JPN -
 THEN -
    SET &SRCDATE = &STR(&SRCMM.&SRCDD)
 ELSE -
    IF &DATEFMT = USA -
    THEN -
       SET &SRCDATE = &STR(&SRCMM.&SRCDD)
    ELSE -
       SET &SRCDATE = &STR(&SRCDD.&SRCMM)
 AFDISP: +
 SET &TABNAME = &STR(&TBNAME.&T.&SRCDATE)
 SET &CALCMEM = &STR(&TBNAME.&M.&SRCDATE)
 SET &TOMEM = &TABNAME
 SET &CONDATE = &STR(&SRCDATE)
 SET &FROMMEM = &STR(&TBNAME.TMSTR)
 SET &TBPREF = &TBNAME
 ISPEXEC VPUT ( TBPREF MANULIB ) PROFILE
 ISPEXEC VPUT ( TBNAME MANULIB TABNAME CALCMEM CONDATE TBPREF) -
           SHARED
 IF &MANULIB = &STR() -
 THEN -
    DO
      SET &TBLIB = &STR(ISPTLIB)
      ISPEXEC LMINIT DATAID(TOID) DDNAME(&TBLIB) ENQ(SHR)
      SET &RC = &LASTCC
      IF &RC ¬= 0  -
      THEN -
      DO
         ISPEXEC SETMSG MSG(CTMP000Z)
         GOTO REDISP
      END
    END
 ELSE -
    DO
      SET &TBLIB = &MANULIB
      ISPEXEC LMINIT DATAID(TOID) DATASET('&MANULIB') ENQ(SHR)
      SET &RC = &LASTCC
      IF &RC ¬= 0  -
      THEN -
      DO
         ISPEXEC SETMSG MSG(CTMP000Z)
         GOTO REDISP
      END
    END
 ISPEXEC LMOPEN DATAID(&TOID) OPTION(INPUT)
 SET &RC = &LASTCC
 IF &RC ¬= 0  -
 THEN -
    DO
      ISPEXEC SETMSG MSG(CTMP000Z)
      GOTO REDISP
    END
 ISPEXEC LMMFIND DATAID(&TOID) MEMBER(&TABNAME)
 SET &RC = &LASTCC
 IF &RC ¬= 0  -
 THEN -
    IF &RC > 8   -
    THEN -
       DO
          ISPEXEC SETMSG MSG(CTMP000Z)
          GOTO REDISP
       END
    ELSE -
       DO
          /* COPY HERE  */

          ISPEXEC LMCLOSE DATAID(&TOID)
          IF &MANULIB = &STR() -
          THEN -
             DO
               SET &TBLIB = &STR(ISPTLIB)
            ISPEXEC LMINIT DATAID(FROMID) DDNAME(&TBLIB) ENQ(SHR)
               SET &RC = &LASTCC
               IF &RC ¬= 0  -
               THEN -
               DO
                  ISPEXEC SETMSG MSG(CTMP000Z)
                  GOTO REDISP
               END
             END
          ELSE -
            DO
             SET &TBLIB = &MANULIB
        ISPEXEC LMINIT DATAID(FROMID) DATASET('&MANULIB') ENQ(SHR)
             SET &RC = &LASTCC
             IF &RC ¬= 0  -
             THEN -
               DO
                  ISPEXEC SETMSG MSG(CTMP000Z)
                  GOTO REDISP
               END
             END
          ISPEXEC LMOPEN DATAID(&FROMID) OPTION(INPUT)
          SET &RC = &LASTCC
          IF &RC ¬= 0  -
          THEN -
          DO
             ISPEXEC SETMSG MSG(CTMP000Z)
             GOTO REDISP
          END
          ISPEXEC LMMFIND DATAID(&FROMID) MEMBER(&FROMMEM)
          SET &RC = &LASTCC
          IF &RC ¬= 0  -
          THEN -
             IF &RC > 8   -
             THEN -
                DO
                   ISPEXEC SETMSG MSG(CTMP000Z)
                   GOTO REDISP
                END
             ELSE -
                DO
                   ISPEXEC SETMSG MSG(CTMP002A)
                   GOTO REDISP
                END
           ISPEXEC LMCOPY FROMID(&FROMID) TODATAID(&TOID) -
                   FROMMEM(&FROMMEM) TOMEM(&TOMEM)
           SET &RC = &LASTCC
           IF &RC ¬= 0  -
           THEN -
              IF &RC > 8   -
              THEN -
                 DO
                    ISPEXEC SETMSG MSG(CTMP000Z)
                    GOTO REDISP
                 END
              ELSE -
                 DO
                    ISPEXEC SETMSG MSG(CTMP002F)
                    GOTO REDISP
                 END
      /*  ISPEXEC SETMSG MSG(CTMP003B)
      /*  GOTO REDISP
       END
 IF &MANULIB ¬= &STR() -
 THEN   -
    DO
        SET &IND = 1
 INITRY: SET &TBFILE = &STR(TBFILE&IND)
       ISPEXEC LMINIT DATAID(TRYID) DDNAME (&TBFILE) ENQ(SHR)
       SET &RC = &LASTCC
       IF &RC = 0 -
       THEN -
          DO
            ISPEXEC LMFREE (&TRYID)
            SET &DSNTRY = DSNTRY&IND
            SET &DSNTR  = &&DSNTRY&IND
            ISPEXEC VGET ( &DSNTRY ) SHARED
            SET DSNTT = &SYSNSUB(2,&DSNTR)
            IF &DSNTT = &MANULIB -
            THEN -
               GOTO CONT
            ELSE -
               DO
                 SET &IND = &IND + 1
                 GOTO INITRY
               END
          END
       IF &IND > 99 -
       THEN -
          SET &IND = 1
       SET &TBFILE = &STR(TBFILE&IND)
       SET &DSNTRY = DSNTRY&IND
       SET &&DSNTRY = &MANULIB
       ISPEXEC VPUT (&DSNTRY) SHARED
       FREE FILE(&TBFILE)
       ALLOC FILE(&TBFILE) DA('&MANULIB') SHR
       SET &RC = &LASTCC
       IF &RC ¬= 0 -
       THEN -
          DO
              SET &DDERR = &TBFILE
              SET &DSNERR = &MANULIB
              ISPEXEC SETMSG MSG(CTMP004L)
              GOTO END
          END
 CONT: +
       SET &TBDD = &STR(&TBFILE)
       SET &TBLIB = &STR(&MANULIB)
    END
 IF &MANULIB = &STR() -
 THEN   -
    DO
      SET &TBDD = &STR(ISPTABL)
      SET &TBLIB = &STR(ISPTABL)
    END
 ISPEXEC VPUT (TABNAME TBFILE) SHARED
END: +
 ISPEXEC VPUT (TBLIB PFK3) SHARED
 ISPEXEC TBCLOSE PROMPTB
 ISPEXEC LMFREE DATAID(&TOID)
 ISPEXEC LMFREE DATAID(&FROMID)
