/*  REXX EXEC : ( CCFRARCH ).
    FUNCTION  : ISPF PANEL INTERFACE TO ALLOW THE RESTORE OF "TEST"
                LIBRARIAN MEMBER(S) FROM THE ARCHIVE MAINT BACKUP TAPE.
    INPUT     : ARCHIVE LIBRARIAN BACKUP TAPES.
    OUTPUT    : BATCH JOB WRITTEN: ( TSOID.DIALOG.ISPFILE(JOBNAME))
                AND SUBMITTED TO SYSA SYSTEM CLASS=W.
                                                                     */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
UCNTL = SYSDSN(DIALOG.ISPFILE)
IF UCNTL = 'DATASET NOT FOUND' THEN DO
"ALLOC DA(DIALOG.ISPFILE) LIKE(USER.CNTLLIB) NEW SPACE(1,0) DIR(5) CYLINDERS"
   SRC = RC
   IF SRC ¬= 0 THEN DO
   SAY '*** ALLOCATE ERROR: ERROR ALLOCATING DATASET (DIALOG.ISPFILE). ***'
   SAY '*** CCFRARCH EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
   END
END
HLQ = 'PTAP.BACKUP.ARCHIVE'
ACCTCCF = '5200010520000000'
TSOID = SYSVAR(SYSUID)
LIBNODES = 'TESTPROC TESTSRCE TESTSYSI'
TAPELBL = '1 1 1'
X = MSG("OFF")
"FREE FILE(ISPFILE)"
ADDRESS ISPEXEC
"VGET (ACCTCDE) PROFILE"
IF RC ¬= 0 THEN ACCTCDE = ACCTCCF

/*     */
MAIN_ROUTINE:
CALL DISPLAY_PANEL
ADDRESS TSO
CALL ASSIGN_RESTORE_VARIABLES
CALL ALLOC_CNTLPDS
CALL BUILD_LBRESTORE_JOBJCL
CALL BUILD_LBRESTORE_MEMBERS
CALL BUILD_LBRESTORE_ENDJCL
CALL WRITE_LBRESTORE_PDSMEMB
CALL SUBMIT_LBRESTORE_JOB
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
DISPLAY_PANEL:
ADDRESS ISPEXEC
"CONTROL ERRORS RETURN"
"DISPLAY PANEL(CCFRARCP)"
SRC = RC
IF SRC = 8 THEN EXIT 0
IF SRC > 8 THEN DO
   SAY '*** ERROR INVOKING ISPF DIALOG PANEL: CCFRARCP - RC = 'SRC'. ***'
   SAY '*** CCFRARCH EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
ASSIGN_RESTORE_VARIABLES:
G = 0
JN = JOBNAME
ACCTUSR = ACCTCDE
NODE = WORD(LIBNODES,LIB)
LBL = WORD(TAPELBL,LIB)
M = 0
R = 0
MEMCNT = 0
DO 20
   R = R + 1
   MEMBER = VALUE('MEMB'R)
   IF MEMBER = '' THEN ITERATE
   M = M + 1
   MEMB.M = MEMBER
   MEMCNT = MEMCNT + 1
END
RETURN

/*     */
ALLOC_CNTLPDS:
"ALLOC DA(DIALOG.ISPFILE("JOBNAME")) FILE(ISPFILE) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: (DIALOG.ISPFILE('JOBNAME'). ***'
   SAY '*** CCFRARCH EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
BUILD_LBRESTORE_JOBJCL:
J = 0
RCDCNT = 16
"DELSTACK"
JCL.1 = "//"JN" JOB" ACCTUSR",'"TSOID".LIB.REST.MOD',NOTIFY="TSOID","
JCL.2 = "//             CLASS=W,MSGCLASS=S,MSGLEVEL=(1,0),REGION=4M"
JCL.3 = "/*JOBPARM SYSAFF=SYSA"
JCL.4 = "//SELECT   EXEC PGM=AFOLIBR,PARM='NRJS,NJTA'"
JCL.5 = "//SYSAF01  DD UNIT=SYSDA,SPACE=(TRK,(10,5))"
JCL.6 = "//SYSAF02  DD UNIT=SYSDA,SPACE=(TRK,(10,5))"
JCL.7 = "//MASTIN   DD UNIT=SILO,DSN="HLQ"."NODE"("G"),"
JCL.8 = "//         LABEL=("LBL",SL),DISP=(OLD,KEEP)"
JCL.9 = "//OSJOB    DD DSN=&&SELMEMB,UNIT=SYSDA,"
JCL.10 = "//         DCB=(RECFM=FB,LRECL=80,BLKSIZE=8000),"
JCL.11 = "//         SPACE=(CYL,(1,1)),DISP=(NEW,PASS)"
JCL.12 = "//SYSPRINT DD SYSOUT=*"
JCL.13 = "//LIST     DD SYSOUT=*"
JCL.14 = "//INDEX    DD SYSOUT=*"
JCL.15 = "//SYSIN    DD *"
JCL.16 = "-OPT UTILITY,NOCYCLE"
DO RCDCNT
   J = J + 1
   QUEUE JCL.J
END
RETURN

/*     */
BUILD_LBRESTORE_MEMBERS:
M = 0
DO MEMCNT
   M = M + 1
   RECORD = "-COPY" MEMB.M
   QUEUE RECORD
END
RETURN

/*     */
BUILD_LBRESTORE_ENDJCL:
J = 0
RCDCNT = 11
JCL.1 = "-END"
JCL.2 = "/*"
JCL.3 = "//RESTORE  EXEC PGM=AFOLIBR,PARM='NRJS,NJTA',COND=(0,LT,SELECT)"
JCL.4 = "//MASTER   DD DSN=SYS1."NODE".MASTER,DISP=SHR"
JCL.5 = "//OSJOB    DD DUMMY"
JCL.6 = "//SYSPRINT DD SYSOUT=*"
JCL.7 = "//LIST     DD SYSOUT=*"
JCL.8 = "//INDEX    DD SYSOUT=*"
JCL.9 = "//SYSIN    DD DSN=&&SELMEMB,DISP=(OLD,DELETE)"
JCL.10 = "/*"
JCL.11 = "//"
DO RCDCNT
   J = J + 1
   QUEUE JCL.J
END
RETURN

/*     */
WRITE_LBRESTORE_PDSMEMB:
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW ISPFILE (FINIS"
SRC = RC
"FREE FILE(ISPFILE)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING 'TSOID'.DIALOG.ISPFILE('JOBNAME'). ***'
   SAY '*** CCFRARCH EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
SUBMIT_LBRESTORE_JOB:
"SUBMIT (DIALOG.ISPFILE("JOBNAME"))"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** SUBMIT ERROR: 'TSOID'.DIALOG.ISPFILE('JOBNAME'). ***'
   SAY '*** CCFRARCH EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
SAY '+++ JOB: 'JOBNAME' SUBMITTED TO THE SYSA SYSTEM - CLASS=W. +++'
"DELETE (DIALOG.ISPFILE) SCRATCH PURGE NONVSAM"
RETURN
