/*  REXX EXEC : ( DB2QAPRM ).
    FUNCTION  : CALLED BY CCF QA AND PROD TURNOVER PROCESSING SKELETONS
                ( CCFBDB2Q AND CCFCDB2Q ) AT STEP ( STEP0010 ).
                VERIFIES THE EXISTENCE OF THE PACKAGE/BIND PARMLIB
                WORKORDER MEMBER FOR EXECIO WRITE TO THE QA PARMLIB.
    INPUT     : SYSS.TESTCCF.DB2.TURNOVER.PARMLIB(WORKORDR).
    OUTPUT    : SYSS.QA.CCF.DB2.TURNOVER.PARMLIB(WORKORDR).
                                                                     */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
ARG MASTER MEMBER WORKORDR
PARMS = 'MASTER MEMBER WORKORDR'
SRC = 0
DO I = 1 TO 3
   IPARM = WORD(PARMS,I)
   XPARM = VALUE(IPARM)
   IF XPARM ¬= '' THEN ITERATE
   SAY '*** PARM ERROR: MISSING PARM VALUE FOR ('IPARM'). ***'
   SAY '*** DB2QAPRM EXEC TASK CANCELLED - RC = (024). ***'
   EXIT 024
END
X = MSG("OFF")
"FREE FILE(TESTDD)"
"FREE FILE(QADD)"
LIBNODE = POS('QASRCE',MASTER)
IF LIBNODE = 0 THEN EXIT 0
/*     */
MAIN_ROUTINE_QA:
TESTCCF = 'SYSS.TESTCCF.DB2.TURNOVER.PARMLIB'
QACCF = 'SYSS.QA.CCF.DB2.TURNOVER.PARMLIB'
CALL CHECK_TESTCCF_PARMS
IF MEMBSW = 'YES' THEN DO
   CALL CHECK_QACCF_PARMS
   INTERPRET CALL LABEL
   IF WRITESW = 'ERROR' THEN CALL PARM_WRITE_ERROR
END
IF MEMBSW = 'NO' THEN DO
   SAY
   SAY '*** PARMLIB MEMBER ERROR: ('TESTCCF'('WORKORDR') ('MEMBER'). ***'
   SAY '*** DB2 PACKAGE/BIND PARMS CANNOT BE WRITTEN FOR QA TURNOVER. ***'
   SAY '+ REJECT MEMBER, PROGRAMMER MUST SPECIFY TURNOVER (YES) COMPILE OPTION.'
   SAY '*** DB2QAPRM EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   SAY
   EXIT SRC
END
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
CHECK_TESTCCF_PARMS:
SRC = 28
MEMBSW = 'NO'
DB2PARM = SYSDSN("'"TESTCCF"("WORKORDR")'")
IF DB2PARM = 'MEMBER NOT FOUND' THEN RETURN
"ALLOC DA('"TESTCCF"("WORKORDR")') FILE(TESTDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('TESTCCF'('WORKORDR'). ***'
   RETURN
END
"EXECIO * DISKR TESTDD (FINIS STEM TPARM."
SRC = RC
"FREE FILE(TESTDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('TESTCCF'('WORKORDR'). ***'
   RETURN
END
DO I = 1 TO TPARM.0
   PMEMBER = WORD(TPARM.I,1)
   IF PMEMBER = MEMBER THEN DO
      MEMBSW = 'YES'
      RECORD = TPARM.I
      LEAVE
   END
END
RETURN
/*     */
CHECK_QACCF_PARMS:
MATCHSW = 'NO'
LABEL = REWRITE_QA_PARMS
DB2PARM = SYSDSN("'"QACCF"("WORKORDR")'")
IF DB2PARM = 'MEMBER NOT FOUND' THEN DO
   LABEL = WRITE_QA_PARMS
   RETURN
END
"ALLOC DA('"QACCF"("WORKORDR")') FILE(QADD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('QACCF'('WORKORDR'). ***'
   SAY '*** DB2QAPRM EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR QADD (FINIS STEM QPARM."
SRC = RC
"FREE FILE(QADD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('QACCF'('WORKORDR'). ***'
   SAY '*** DB2QAPRM EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
DO I = 1 TO QPARM.0
   PMEMBER = WORD(QPARM.I,1)
   IF PMEMBER = MEMBER THEN DO
      MATCHSW = 'YES'
      QPARM.I = RECORD
      LEAVE
   END
END
RETURN
/*     */
WRITE_QA_PARMS:
WRITESW = 'OK'
"ALLOC DA('"QACCF"("WORKORDR")') FILE(QADD) SHR"
"DELSTACK"
QUEUE RECORD
"EXECIO 1 DISKW QADD (FINIS"
SRC = RC
"FREE FILE(QADD)"
IF SRC ¬= 0 THEN WRITESW = 'ERROR'
RETURN
/*     */
REWRITE_QA_PARMS:
WRITESW = 'OK'
"ALLOC DA('"QACCF"("WORKORDR")') FILE(QADD) SHR"
"DELSTACK"
DO I = 1 TO QPARM.0
   QUEUE QPARM.I
END
IF MATCHSW = 'NO' THEN QUEUE RECORD
RCDCNT = QUEUED()
"EXECIO" RCDCNT "DISKW QADD (FINIS"
SRC = RC
"FREE FILE(QADD)"
IF SRC ¬= 0 THEN WRITESW = 'ERROR'
RETURN
/*     */
NOOP_ROUTINE:
WRITESW = 'OK'
RETURN

/*     */
PARM_WRITE_ERROR:
SAY '************************************************************************'
SAY '*** PARMLIB WRITE ERROR: ('QACCF'('WORKORDR'). ***'
SAY '*** DB2QAPRM EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
SAY '************************************************************************'
EXIT SRC
