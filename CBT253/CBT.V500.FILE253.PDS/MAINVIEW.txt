        PROC 0 PREFIX('SYS1.BABBAGE.V4R3M0') +
               PREFX1('SYSS.BABBAGE.V4R3M0') +
               SS()             /* BBI-SS ID                      */ +
               PROFILE(YES)     /* USER DATASETS                  */ +
               IMAGE(NO)        /* IMAGE LOGGING                  */ +
               JOURNAL(YES)     /* JOURNAL LOGGING                */ +
               PRINT(YES)       /* SCREEN PRINTING                */ +
               UPREFIX()        /* PROFILE PREFIX                 */ +
               ALLOCATE                                              +
               T(CALL)

        /*------------------------------------------------------------*/
        /* SOURCE: BAIMAINV                                           */
        /*                                                            */
        /*                                                            */
        /* IF REQUIRED, THIS CLIST MAY BE MODIFIED.  COMMENTS HAVE    */
        /* BEEN INCLUDED TO AID YOU IN EVALUATING EACH SECTION.       */
        /*                                                            */
        /*           USE OF SYMBOLICS SHOWN ABOVE                     */
        /*                                                            */
        /*                                                            */
        /* PREFIX  - HIGH-LEVEL QUALIFIER OF THE DATA SETS USED       */
        /*                                                            */
        /* SS      - 1 TO 4 CHARACTER ID OF THE B&B SUBSYSTEM WITH    */
        /*           WHICH THE TERMINAL SESSION IS TO CONNECT.        */
        /*           REQUIRED FOR FOCAL POINT, AUTOOPERATOR,          */
        /*           MAINVIEW FOR CICS, MAINVIEW FOR DB2,             */
        /*           MAINVIEW FOR IMS,  BUT IGNORED FOR OTHER         */
        /*           PRODUCTS.                                        */
        /*                                                            */
        /* PROFILE - YES/NO.  SPECIFIES IF USER PROFILE DATA SET      */
        /*           SHOULD BE ALLOCATED.  IF NOT ALLOCATED, THE      */
        /*           COMMON PROFILE DATA SETS WILL BE SHARED.         */
        /*                                                            */
        /* IMAGE   - YES/NO.  SPECIFIES IF USER IMAGE LOG DATA SET    */
        /*           SHOULD BE ALLOCATED.                             */
        /*           OPTIONAL FOR FOCAL POINT,                        */
        /*           MAINVIEW FOR CICS, MAINVIEW FOR DB2,             */
        /*           MAINVIEW FOR IMS,  BUT IGNORED FOR OTHER         */
        /*           PRODUCTS.                                        */
        /*                                                            */
        /* JOURNAL - YES/NO.  SPECIFIES IF USER JOURNAL DATA SET      */
        /*           SHOULD BE ALLOCATED.                             */
        /*           OPTIONAL FOR FOCAL POINT, AUTOOPERATOR,          */
        /*           MAINVIEW FOR CICS, MAINVIEW FOR DB2,             */
        /*           MAINVIEW FOR IMS,  BUT IGNORED FOR OTHER         */
        /*           PRODUCTS.                                        */
        /*                                                            */
        /* PRINT   - YES/NO.  SPECIFIES IF USER SCREEN PRINT DATA SET */
        /*           SHOULD BE ALLOCATED.                             */
        /*           OPTIONAL FOR FOCAL POINT, AUTOOPERATOR,          */
        /*           MAINVIEW FOR CICS, MAINVIEW FOR DB2,             */
        /*           MAINVIEW FOR IMS,  BUT IGNORED FOR OTHER         */
        /*           PRODUCTS.                                        */
        /*                                                            */
        /* UPREFIX - SPECIFIES THE PREFIX OF THE USER PROFILE DATA    */
        /*           SET, BBIPROF.  THIS DATA SET CONTAINS TERMINAL   */
        /*           SESSION DEFAULTS AND SAVED USER PROFILES.  THIS  */
        /*           CLIST WILL CREATE ONE IF IT DOES NOT EXIST.      */
        /*           IF A VALUE IS NOT SPECIFIES, DEFAULT IS CURRENT  */
        /*           TSO USER PREFIX.                                 */
        /*                                                            */
        /* ALLOCATE- IF SPECIFIED, WILL FORCE REALLOCATION.           */
        /*                                                            */
        /* T       - USED ONLY IF YOUR BOOLE AND BABBAGE SUPPORT      */
        /*           REPRESENTATIVE REQUESTS THAT YOU SPECIFY A VALUE.*/
        /*                                                            */
        /*------------------------------------------------------------*/

            CONTROL NOMSG NOFLUSH
            SET &SYSASIS = ON

            IF &STR(&UPREFIX) = &STR() THEN DO
              IF &STR(&SYSPREF) =                OR   +
                 &STR(&SYSUID)  = &STR(&SYSPREF) THEN +
                   SET &UPREFIX = &SYSUID
              ELSE SET &UPREFIX = &STR(&SYSPREF..&SYSUID)
              END

            FREE DD(IMFSSDM  IMFSPRNT) /* IN CASE LEFT FROM 2.5.0 RUN */
            FREE DD(IMFPROF  IMFLOAD ) /* IN CASE LEFT FROM 2.5.0 RUN */
            FREE DD(IMFJRNL1 IMFIMAG1) /* IN CASE LEFT FROM 2.5.0 RUN */

        /*------------------------------------------------------------*/
        /*  DETERMINE ENTRY POINT                                     */
        /*------------------------------------------------------------*/

            IF &SYSDSN('&PREFIX..BBLINK(TSLOAD)') EQ OK THEN +
               SET &BBI  = YES
            IF &SYSDSN('&PREFIX..BBLINK(BBM9TC20)') EQ OK THEN +
               SET &BBM = YES

            IF &BBM = YES THEN SET &ENTRY = BBM9TC20
            IF &BBI = YES THEN SET &ENTRY = TSLOAD

        /*------------------------------------------------------------*/
        /*  FORCE ALLOCATION?                                         */
        /*------------------------------------------------------------*/

            IF &ALLOCATE = ALLOCATE THEN GOTO ALLOCIT

        /*------------------------------------------------------------*/
        /*                                                            */
        /*  CALL MAINVIEW                                             */
        /*      DURING STARTUP, MAINVIEW CHECKS TO ENSURE THAT        */
        /*      CERTAIN DATASETS ARE ALLOCATED (BBILOAD, PNLLIB,      */
        /*      MSGLIB, AND BBIPROF) AND IF THEY ARE NOT, IT          */
        /*      EXITS WITH A RETURN CODE OF 8. THUS, THE RETURN       */
        /*      CODES FROM MAINVIEW ARE AS FOLLOWS:                   */
        /*                                                            */
        /*        0  - MAINVIEW SESSION ENDED NORMALLLY               */
        /*        8  - SOME REQUIRED ALLOCATION WAS NOT FOUND         */
        /*             IN THE TIOT. CHECK THE ALLOCATIONS AND         */
        /*             TRY AGAIN.                                     */
        /*       20  - SEVERE ERROR                                   */
        /*                                                            */
        /*------------------------------------------------------------*/

        MAINLOOP: +
            &T '&PREFIX..BBLINK(&ENTRY)' '&SS'
            SET &CC = &LASTCC
            IF &CC EQ  8 THEN GOTO ALLOCIT
            IF &CC EQ 12 THEN GOTO RC12
            IF &CC NE  0 THEN WRITE ERROR IN TSLOAD, RC = &CC
            GOTO FINI

        /*------------------------------------------------------------*/
        /*  RETURN CODE 12 FROM CALL                                  */
        /*------------------------------------------------------------*/

        RC12: +
            WRITE ENTRY POINT '&ENTRY' NOT FOUND IN '&PREFIX..BBLINK'
            GOTO FINI


        /*------------------------------------------------------------*/
        /*                                                            */
        /*  ALLOCATE REQUIRED DATASETS                                */
        /*                                                            */
        /*  NOTE: THIS CLIST IS DESIGNED TO BE CALLED MULTIPLE        */
        /*        TIMES UNDER ISPF. SINCE IT WOULD BE WASTEFULL       */
        /*        OF USER AND CPU TIME TO REALLOCATE THE DATASETS     */
        /*        EACH TIME IT IS CALLED, AND THE ALLOCATED DATASETS  */
        /*        MIGHT BE IN USE ON THE OTHER SIDE OF A SPLIT        */
        /*        SCREEN, THIS CLIST IS DESIGNED TO ONLY DO THE       */
        /*        ALLOCATIONS IN TWO CASES:                           */
        /*                                                            */
        /*          1) IF BBILOAD, MSGLIB, PNLLIB, OR BBIPROF HAVE    */
        /*             NOT BEEN ALLOCATED.                            */
        /*                                                            */
        /*          2) IF THIS CLIST IS CALLED WITH THE "ALLOCATE"    */
        /*             PARAMETER.                                     */
        /*                                                            */
        /*        NOTE THAT YOU CANNOT FORCE THE ALLOCATIONS IF       */
        /*        MAINVIEW IS IN USE ON THE OTHER SIDE OF A SPLIT     */
        /*        SCREEN. YOU MUST LEAVE AND REENTER ISPF, THEN       */
        /*        CALL MAINVIEW AGAIN WITH THE ALLOCATE PARAMETER.    */
        /*        NORMALLY, YOU WOULD NOT NEED TO DO THIS. THE        */
        /*        ALLOCATION PARAMETER IS PROVIDED IN CASE YOU        */
        /*        CHANGE THE ALLOCATIONS, AND WANT TO RESTART         */
        /*        MAINVIEW WITHOUT LOGGING OFF.                       */
        /*                                                            */
        /*------------------------------------------------------------*/

        ALLOCIT: +
            IF &A = YES THEN DO                   /* LOOP PREVENTION */
              WRITE
              WRITE Error allocating datasets - SESSION TERMINATED
              WRITE
              GOTO FINI
              END
            SET &A = YES
            FREE DD(BBISSDM BBISPRNT)

        /*------------------------------------------------------------*/
        /*  IF NO BBI PRODUCTS ARE INSTALLED, THEN IMAGE, JOURNAL     */
        /*  AND PRINT DATASET ARE NOT NEEDED.                         */
        /*------------------------------------------------------------*/

            IF &BBI NE YES THEN DO
               SET &IMAGE   = NO
               SET &JOURNAL = NO
               SET &PRINT   = NO
               END

        /*------------------------------------------------------------*/
        /*                                                            */
        /*  IF THE PROFILE OPTION IS YES, THEN ALLOCATE THE USER      */
        /*  PROFILE DATA SET. IF ONE DOES NOT EXIST, THE USER IS      */
        /*  QUERIED AS TO HOW TO PROCEED.                             */
        /*                                                            */
        /*  IF THE PROFILE OPTION IS NO, THE COMMON PROFILE DATA SET  */
        /*  WILL BE THE ONLY DATA SET ALLOCATED FOR THIS USER.        */
        /*                                                            */
        /*------------------------------------------------------------*/

        PLOOP: +
            SET &VDEFDSNS =
            SET &SDEFDSNS =
            IF &SYSDSN('&PREFX1..SBBVDEF')  EQ OK THEN +
              SET &VDEFDSNS = &STR('&PREFX1..SBBVDEF')
            IF &SYSDSN('&PREFX1..SBBSDEF')  EQ OK THEN +
              SET &SDEFDSNS = &STR('&PREFX1..SBBSDEF')
            SET &PROFDSNS = &STR('&PREFX1..BBPROF')
            IF &SYSDSN('&PREFX1..SBBPROF')  EQ OK THEN +
              SET &PROFDSNS = &STR('&PREFX1..SBBPROF' &PROFDSNS)

            IF &SUBSTR(1:1,&PROFILE ) NE Y THEN DO
              IF &BBM EQ YES THEN DO
                FREE  DD(BBVDEF BBSDEF)
                IF &VDEFDSNS ¬=    THEN +
                  ALLOC DD(BBVDEF) DS(&VDEFDSNS) SHR
                IF &SDEFDSNS ¬=    THEN +
                  ALLOC DD(BBSDEF) DS(&SDEFDSNS) SHR
                END
              FREE  DD(BBIPROF)
              ALLOC DD(BBIPROF) DS(&PROFDSNS) SHR
              END

            ELSE DO
              IF &BBI EQ YES THEN DO
                FREE  DD(BBIPROF)
                ALLOC DD(BBIPROF) DS('&UPREFIX..BBPROF') SHR
                SET &ERC = &LASTCC

                IF &ERC NE 0 THEN DO
                  WRITE
                  WRITE
                  WRITE Unable to allocate '&UPREFIX..BBPROF'.
                  WRITE
                  WRITE The user profile dataset is required.  It holds
                  WRITE information unique to your BBI/TS session, such
                  WRITE as PFKEY settings, your default target and
                  WRITE other such things.
                  WRITE
                  WRITE
                  WRITENR WOULD YOU LIKE THIS CLIST TO CREATE IT? +
                          (ENTER YES OR EXIT)
                  SET &SYSASIS = OFF
                  READ &ANS
                  SET &SYSASIS = ON

                  IF &SUBSTR(1,&ANS ) EQ N THEN EXIT
                  IF &SUBSTR(1,&ANS ) EQ E THEN EXIT
                  IF &SUBSTR(1,&ANS ) NE Y THEN GOTO PLOOP

                  WRITE
                  WRITENR SPECIFY VOLUME FOR ALLOCATION +
                          (OR JUST PRESS ENTER FOR DEFAULT VOLUME)
                  SET &SYSASIS = OFF
                  READ &VOL
                  SET &SYSASIS = ON
                  IF &STR(&VOL) NE    THEN SET &VOL = &STR(VOL(&VOL))

                  CONTROL MSG
                  ALLOC DD(BBIPROF) DS('&UPREFIX..BBPROF') +
                        CAT TRACK SPACE(15 5) &VOL DIR(5)  +
                        RECFM(F B) LRECL(80) BLK(6160)
                  SET &ERC = &LASTCC
                  CONTROL NOMSG

                  IF &ERC NE 0 THEN DO
                    WRITE
                    WRITE &STR(** Error allocating BBPROF;  **)
                    WRITE &STR(** Correct problem and retry **)
                    WRITE
                    END /* ERC IF*/

                  GOTO PLOOP
                  END

                SET &PROFDSNS = &STR('&UPREFIX..BBPROF' &PROFDSNS)
                END   /* END IF &BBI = YES */

              IF &BBM EQ YES THEN DO
                IF &SYSDSN('&UPREFIX..BBVDEF') EQ OK THEN +
                  SET &VDEFDSNS = &STR('&UPREFIX..BBVDEF' &VDEFDSNS)
                IF &SYSDSN('&UPREFIX..BBSDEF') EQ OK THEN +
                  SET &SDEFDSNS = &STR('&UPREFIX..BBSDEF' &SDEFDSNS)

                FREE  DD(BBVDEF,BBSDEF)
                IF &VDEFDSNS ¬=    THEN +
                  ALLOC DD(BBVDEF)  DS(&VDEFDSNS) SHR
                IF &SDEFDSNS ¬=    THEN +
                  ALLOC DD(BBSDEF)  DS(&SDEFDSNS) SHR
                END   /* END IF &BBM = YES */

              FREE  DD(BBIPROF)
              ALLOC DD(BBIPROF) DS(&PROFDSNS) SHR
              END /* IF PROFILE = Y */

        /*------------------------------------------------------------*/
        /*                                                            */
        /*  FREE UP ALL ALLOCATIONS INITIALLY AND THEN ALLOCATE THE   */
        /*  DATA SETS THAT ARE ALWAYS NEEDED.                         */
        /*                                                            */
        /*------------------------------------------------------------*/

            FREE DD(PNLLIB,MSGLIB,BBILOAD,BBIIMAG1,BBIJRNL1,SYSUDUMP)
            FREE DD(ABNLDUMP)

            ALLOC DD(ABNLDUMP) DUMMY             /* DISABLE ABENDAID */
            ALLOC DD(SYSUDUMP) SYSOUT(A) HOLD

            CONTROL MSG FLUSH

            ALLOC DD(PNLLIB)  DS('&PREFX1..BBPPLIB') SHR
            ALLOC DD(MSGLIB)  DS('&PREFX1..BBMLIB') SHR

            CONTROL NOMSG NOFLUSH
            ALLOC DD(BBILOAD) DS('&PREFIX..BBLINK') SHR

            FREE DD(BBILINK)
            ALLOC DD(BBILINK) DS('&PREFIX..BBLINK') SHR

            FREE DD(BBTLIB)
            ALLOC DD(BBTLIB)  DS('&PREFX1..BBTLIB') SHR

            FREE DD(BBCLIB)
            ALLOC DD(BBCLIB)  DS('&PREFX1..BBCLIB') SHR

        /*------------------------------------------------------------*/
        /*                                                            */
        /*  IF THE JOURNAL OPTION IS YES, THEN ALLOCATE THE USER      */
        /*  JOURNAL DATA SET.  IF ONE DOES NOT EXIST, THE USER        */
        /*  IS QUERIED AS TO HOW TO PROCEED.                          */
        /*                                                            */
        /*------------------------------------------------------------*/

            IF &SUBSTR(1:1,&JOURNAL ) EQ Y THEN DO
              ALLOC DD(BBIJRNL1) DS('&UPREFIX..BBIJRNLL') SHR
              IF &LASTCC NE 0 THEN DO
                WRITE
                WRITE
                WRITE Unable to allocate '&UPREFIX..BBIJRNLL'
                WRITE
                WRITE The journal dataset is optional.
                WRITE It will contain a log of commands issued by your
                WRITE MAINVIEW session. The journal can be viewed by
                WRITE specifying 'LOCAL' in the target field of the LOG
                WRITE display panel. Various other commands will
                WRITE automatically return their output to this log. If
                WRITE you skip allocating this dataset, these commands
                WRITE will behave as no-ops.  You will still be able to
                WRITE view the 'GLOBAL' SS log even if you do not
                WRITE allocate this dataset.
                WRITE
                WRITE
                WRITE Would you like this CLIST to create it?
                WRITENR (ENTER 'YES' OR PRESS ENTER TO SKIP)
                SET &SYSASIS = OFF
                READ &ANS
                SET &SYSASIS = ON

                IF &SUBSTR(1,&ANS ) EQ   THEN GOTO JLOOP
                IF &SUBSTR(1,&ANS ) EQ N THEN GOTO JLOOP
                IF &SUBSTR(1,&ANS ) EQ S THEN GOTO JLOOP
                IF &SUBSTR(1,&ANS ) NE Y THEN GOTO PLOOP

                WRITE
                WRITENR SPECIFY VOLUME FOR ALLOCATION +
                        (OR JUST PRESS ENTER FOR DEFAULT VOLUME)
                SET &SYSASIS = OFF
                READ &VOL
                SET &SYSASIS = ON

                IF &STR(&VOL) NE &STR() THEN SET &VOL = &STR(VOL(&VOL))

                CONTROL MSG
                ALLOC DS('&UPREFIX..BBIJRNLL') CAT TRACK SPACE(15) &VOL
                SET &ERC = &LASTCC
                CONTROL NOMSG

                IF &ERC NE 0 THEN DO
                  WRITE
                  WRITE ** Error allocating BBIJRNLL; **
                  WRITE ** Correct problem and retry  **
                  WRITE
                  GOTO PLOOP
                  END
                END

              FREE               DS('&UPREFIX..BBIJRNLL')
              ALLOC DD(BBIJRNL)  DS('&UPREFIX..BBIJRNLL')   SHR
              &T '&PREFIX..BBLINK(LGILOG)' 'L,&UPREFIX'

              FREE               DS('&UPREFIX..BBIJRNLL')
              CONTROL MSG FLUSH
              ALLOC DD(BBIJRNL1) DS('&UPREFIX..BBIJRNLL') SHR

              END /* IF JOURNAL EQ Y */


        /*------------------------------------------------------------*/
        /*                                                            */
        /*  IF THE IMAGE OPTION IS YES, THEN ALLOCATE THE USER        */
        /*  IMAGE DATA SET.  IF ONE DOES NOT EXIST, THE USER          */
        /*  IS QUERIED AS TO HOW TO PROCEED.                          */
        /*                                                            */
        /*------------------------------------------------------------*/

        JLOOP: +
            CONTROL NOMSG NOFLUSH
            IF &SUBSTR(1:1,&IMAGE ) EQ Y THEN DO
              ALLOC DD(BBIIMAG1) DS('&UPREFIX..BBIIMAGL') SHR
              IF &LASTCC NE 0 THEN DO
                WRITE
                WRITE
                WRITE Unable to allocate '&UPREFIX..BBIIMAGL'.
                WRITE
                WRITE The image dataset is optional.
                WRITE It will contain screen immages of certain panels
                WRITE where you have specified LOG=YES on the panel. If
                WRITE you do not allocate this dataset, this image
                WRITE logging function will not be available, but the
                WRITE panels will function normally otherwise. Not all
                WRITE panels have this capability.
                WRITE
                WRITE
                WRITE Would you like this CLIST to create it?
                WRITENR (ENTER 'YES' OR PRESS ENTER TO SKIP)
                SET &SYSASIS = OFF
                READ &ANS
                SET &SYSASIS = ON

                IF &SUBSTR(1,&ANS ) EQ   THEN GOTO ILOOP
                IF &SUBSTR(1,&ANS ) EQ N THEN GOTO ILOOP
                IF &SUBSTR(1,&ANS ) EQ S THEN GOTO ILOOP
                IF &SUBSTR(1,&ANS ) NE Y THEN GOTO JLOOP

                WRITE
                WRITENR SPECIFY VOLUME FOR ALLOCATION +
                        (OR JUST PRESS ENTER FOR DEFAULT VOLUME)
                SET &SYSASIS = OFF
                READ &VOL
                SET &SYSASIS = ON

                IF &STR(&VOL) NE &STR() THEN SET &VOL = &STR(VOL(&VOL))

                CONTROL MSG
                ALLOC DS('&UPREFIX..BBIIMAGL') CAT TRACK SPACE(15) &VOL
                SET &ERC = &LASTCC
                CONTROL NOMSG

                IF &ERC NE 0 THEN DO
                  WRITE
                  WRITE ** Error allocating BBIIMAGL; **
                  WRITE ** Correct problem and retry  **
                  WRITE
                  GOTO JLOOP
                  END /* IF ERC NE 0 */
                END /* IF LASTCC NE 0 */

              FREE               DS('&UPREFIX..BBIIMAGL')
              CONTROL MSG FLUSH
              ALLOC DD(BBIIMAG1) DS('&UPREFIX..BBIIMAGL') SHR

              END /* IF IMAGE EQ Y */

        /*------------------------------------------------------------*/
        /*                                                            */
        /*  IF THE PRINT OPTION IS YES, THEN ALLOCATE THE USER        */
        /*  PRINT DATA SET.  IF ONE DOES NOT EXIST, THE USER          */
        /*  IS QUERIED AS TO HOW TO PROCEED.                          */
        /*                                                            */
        /*------------------------------------------------------------*/

        ILOOP: +
            CONTROL NOMSG NOFLUSH
            IF &SUBSTR(1:1,&PRINT ) EQ Y THEN DO
              ALLOC DD(BBISPRNT) DS('&UPREFIX..BBISPRNT') SHR
              IF &LASTCC = 0 THEN DO
                FREE DD(BBISPRNT)
                ALLOC DD(BBISPRNT) DS('&UPREFIX..BBISPRNT') MOD
                END
               ELSE DO
                WRITE
                WRITE
                WRITE Unable to allocate &UPREFIX..BBISPRNT.
                WRITE
                WRITE The screen print dataset is optional.
                WRITE It will contain a copy of any panel where you
                WRITE press the 'PRINT' PF KEY.
                wRITE
                WRITE Note that this dataset is only used when the ISPF
                WRITE PRINT function is NOT active.  When running under
                WRITE ISPF, the ISPF PRINT function will write the
                WRITE screen to the ISPF LIST dataset.
                WRITE
                WRITE
                WRITE Would you like this clist to create it?
                WRITENR (ENTER 'YES' OR PRESS ENTER TO SKIP)
                SET &SYSASIS = OFF
                READ &ANS
                SET &SYSASIS = ON

                IF &SUBSTR(1,&ANS ) EQ   THEN GOTO MAINLOOP
                IF &SUBSTR(1,&ANS ) EQ N THEN GOTO MAINLOOP
                IF &SUBSTR(1,&ANS ) EQ S THEN GOTO MAINLOOP
                IF &SUBSTR(1,&ANS ) NE Y THEN GOTO ILOOP

                WRITE
                WRITENR SPECIFY VOLUME FOR ALLOCATION +
                        (OR JUST PRESS ENTER FOR DEFAULT VOLUME)
                SET &SYSASIS = OFF
                READ &VOL
                SET &SYSASIS = ON

                IF &STR(&VOL) NE &STR() THEN SET &VOL = &STR(VOL(&VOL))
                CONTROL MSG
                ALLOC DS('&UPREFIX..BBISPRNT') CAT TRACK +
                      SPACE(15) &VOL RECFM(F B A) LRECL(80) BLK(6160)
                SET &ERC = &LASTCC
                CONTROL NOMSG
                IF &ERC NE 0 THEN DO
                  WRITE
                  WRITE ** Error allocating BBISPRNT; **
                  WRITE ** Correct problem and retry  **
                  WRITE
                  GOTO ILOOP
                  END
                FREE DS('&UPREFIX..BBISPRNT')
                ALLOC DD(BBISPRNT) DS('&UPREFIX..BBISPRNT') SHR
                END
              END
            GOTO MAINLOOP

        FINI: +
            CONTROL NOMSG
            EXIT
