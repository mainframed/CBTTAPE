/*  REXX EXEC : ( CCFRMEMB ).
    FUNCTION  : ISPF PANEL INTERFACE TO ALLOW THE RESTORE OF "TEST"
                LIBRARIAN MEMBER(S) FROM THE DAILY BACKUP TAPES.
    INPUT     : DAILY LIBRARIAN BACKUP TAPES.
    OUTPUT    : BATCH JOB WRITTEN: ( SYSS.TESTCCF.ISPFILE.CNTLLIB ).
                AND SUBMITTED TO SYSA SYSTEM CLASS=W.
                                                                     */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
HLQ = 'PTAP'
ACCTCCF = '5200010520000000'
TSOID = SYSVAR(SYSUID)
LIBNODES = 'TESTCOPY TESTJCL TESTPROC TESTRAMI TESTSRCE TESTSYSI'
TAPELBL = '12 6 9 18 3 15'
CNTLLIB = 'SYSS.TESTCCF.ISPFILE.CNTLLIB'
X = MSG("OFF")
"FREE FILE(CNTLDD)"
ADDRESS ISPEXEC
"VGET (ACCTCDE) PROFILE"
IF RC ¬= 0 THEN ACCTCDE = ACCTCCF

/*     */
MAIN_ROUTINE:
CALL DISPLAY_PANEL
ADDRESS TSO
CALL ASSIGN_RESTORE_VARIABLES
CALL BUILD_LBRESTORE_JOBJCL
CALL BUILD_LBRESTORE_MEMBERS
CALL BUILD_LBRESTORE_ENDJCL
CALL WRITE_LBRESTORE_PDSMEMB
CALL SUBMIT_LBRESTORE_JOB
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
DISPLAY_PANEL:
ADDRESS ISPEXEC
"CONTROL ERRORS RETURN"
"DISPLAY PANEL(CCFRMEMP)"
SRC = RC
IF SRC = 8 THEN EXIT 0
IF SRC > 8 THEN DO
   SAY '*** ERROR INVOKING ISPF DIALOG PANEL: CCFRMEMP - RC = 'SRC'. ***'
   SAY '*** CCFRMEMB EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
ASSIGN_RESTORE_VARIABLES:
G = GDG
IF GDG > 0 THEN G = '-'||GDG
JN = JOBNAME
ACCTUSR = ACCTCDE
NODE = WORD(LIBNODES,LIB)
LBL = WORD(TAPELBL,LIB)
M = 0
R = 0
MEMCNT = 0
DO 20
   R = R + 1
   MEMBER = VALUE('MEMB'R)
   IF MEMBER = '' THEN ITERATE
   M = M + 1
   MEMB.M = MEMBER
   MEMCNT = MEMCNT + 1
END
RETURN

/*     */
BUILD_LBRESTORE_JOBJCL:
J = 0
RCDCNT = 16
"DELSTACK"
JCL.1 = "//"JN" JOB" ACCTUSR",'"TSOID".LIB.REST.MOD',NOTIFY="TSOID","
JCL.2 = "//          CLASS=W,MSGCLASS=S,MSGLEVEL=(1,0),REGION=4M"
JCL.3 = "/*JOBPARM SYSAFF=SYSA"
JCL.4 = "//SELECT   EXEC PGM=AFOLIBR,PARM='NRJS,NJTA'"
JCL.5 = "//SYSAF01  DD UNIT=SYSDA,SPACE=(TRK,(10,5))"
JCL.6 = "//SYSAF02  DD UNIT=SYSDA,SPACE=(TRK,(10,5))"
JCL.7 = "//MASTIN   DD UNIT=SILO,DSN="HLQ"."NODE".MASTER("G"),"
JCL.8 = "//         LABEL=("LBL",SL),DISP=(OLD,KEEP)"
JCL.9 = "//OSJOB    DD DSN=&&SELMEMB,UNIT=SYSDA,"
JCL.10 = "//         DCB=(RECFM=FB,LRECL=80,BLKSIZE=8000),"
JCL.11 = "//         SPACE=(CYL,(1,1)),DISP=(NEW,PASS)"
JCL.12 = "//SYSPRINT DD SYSOUT=*"
JCL.13 = "//LIST     DD SYSOUT=*"
JCL.14 = "//INDEX    DD SYSOUT=*"
JCL.15 = "//SYSIN    DD *"
JCL.16 = "-OPT UTILITY,NOCYCLE"
DO RCDCNT
   J = J + 1
   QUEUE JCL.J
END
RETURN

/*     */
BUILD_LBRESTORE_MEMBERS:
M = 0
DO MEMCNT
   M = M + 1
   RECORD = "-COPY" MEMB.M
   QUEUE RECORD
END
RETURN

/*     */
BUILD_LBRESTORE_ENDJCL:
J = 0
RCDCNT = 11
JCL.1 = "-END"
JCL.2 = "/*"
JCL.3 = "//RESTORE  EXEC PGM=AFOLIBR,PARM='NRJS,NJTA',COND=(0,LT,SELECT)"
JCL.4 = "//MASTER   DD DSN=SYS1."NODE".MASTER,DISP=SHR"
JCL.5 = "//OSJOB    DD DUMMY"
JCL.6 = "//SYSPRINT DD SYSOUT=*"
JCL.7 = "//LIST     DD SYSOUT=*"
JCL.8 = "//INDEX    DD SYSOUT=*"
JCL.9 = "//SYSIN    DD DSN=&&SELMEMB,DISP=(OLD,DELETE)"
JCL.10 = "/*"
JCL.11 = "//"
DO RCDCNT
   J = J + 1
   QUEUE JCL.J
END
RETURN

/*     */
WRITE_LBRESTORE_PDSMEMB:
ADDRESS TSO
"ALLOC DA('"CNTLLIB"("JOBNAME")') FILE(CNTLDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('CNTLLIB'('JOBNAME'). ***'
   SAY '*** CCFSCAN EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW CNTLDD (FINIS"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING 'CNTLLIB'('JOBNAME'). ***'
   SAY '*** CCFRMEMB EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
SUBMIT_LBRESTORE_JOB:
ADDRESS TSO
"SUBMIT ('"CNTLLIB"("JOBNAME")')"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** SUBMIT ERROR: 'CNTLLIB'('JOBNAME'). ***'
   SAY '*** CCFRMEMB EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
SAY '+++ JOB: 'JOBNAME' SUBMITTED TO THE SYSA SYSTEM - CLASS=W. +++'
"DELETE '"CNTLLIB"("JOBNAME")'  FILE(CNTLDD)"
"FREE FILE(CNTLDD)"
RETURN
