/* REXX EXEC : ( CCFQADB2 ).
   FUNCTION  : READS SOURCE PROGRAM STATEMENTS TO DETERINE COBOL-II
               COMPILE TYPE FOR QA MOVEMENT COMPILE DB2 ONLY.         */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
ARG MASTER MEMBER
PARMS = 'MASTER MEMBER'
DO I = 1 TO 2
   IPARM = WORD(PARMS,I)
   XPARM = VALUE(IPARM)
   IF XPARM ¬= '' THEN ITERATE
   SAY '*** PARM ERROR: MISSING PARM VALUE FOR ('IPARM'). ***'
   SAY '*** CCFQADB2 EXEC TASK CANCELLED - RC = (024). ***'
   EXIT 024
END
OPTIMIZE_SW = 'NO'
C2P = 'APOST,LIB,CMPR2,RES,RENT,DYNAM'

/*     */
MAIN_ROUTINE:
CALL CHECK_NATIVE_FLAG
CALL SET_QA_COMPILE_TYPE_RETURN_CODE
EXIT EXITRC

/*     S U B R O U T I N E   S E C T I O N     */
/*     */
CHECK_NATIVE_FLAG:
NATIVE_SW = 'NO'
LIBDSN = "'"MASTER"("MEMBER")'"
LIBDD = LIBALLOC(LIBDSN)  /* DYNALLOC LIBMASTER W/LAM; RET DDNAME*/
IF LIBDD = "ERROR" THEN DO
   ERC = 100
   SAY '*** DYNAMIC ALLOCATION ERROR: ('MASTER'). ***'
   SAY '*** CANNOT READ COBOL2 COMPILE TYPE FLAG FOR ('MEMBER') PROGRAM. ***'
   SAY '*** CCFQADB2 EXEC SUBTASK CANCELLED - RC = ('ERC'). ***'
   EXIT ERC
END
"EXECIO * DISKR" LIBDD "(FINIS STEM DATA."
SRC = RC
IF SRC ¬= 0 THEN DO
   ERC = 100
   SAY '*** EXECIO READ ERROR: ('MASTER'('MEMBER'). ***'
   SAY '*** CANNOT READ COBOL2 COMPILE TYPE FLAG FOR ('MEMBER') PROGRAM. ***'
   SAY '*** CCFQADB2 EXEC SUBTASK CANCELLED - RC = ('ERC'). ***'
   EXIT ERC
END
DO I = 1 TO DATA.0
   NOCMPR2 = POS('NOCMPR2-FLAG',DATA.I)
   IF NOCMPR2 ¬= 0 THEN DO
      NATIVE_SW = 'YES'
      C2P = 'APOST,LIB,NOCMPR2,RES,RENT,DYNAM'
      LEAVE
   END
END
RETURN
/*     */
SET_QA_COMPILE_TYPE_RETURN_CODE:
IF NATIVE_SW = 'YES' & OPTIMIZE_SW = 'NO'  THEN EXITRC = 1
IF NATIVE_SW = 'NO'  & OPTIMIZE_SW = 'NO'  THEN EXITRC = 2
IF NATIVE_SW = 'YES' & OPTIMIZE_SW = 'YES' THEN EXITRC = 3
IF NATIVE_SW = 'NO'  & OPTIMIZE_SW = 'YES' THEN EXITRC = 4
RETURN
