PROC 0
/*******************************************************************/
/*   CREATE TEMPORARY TABLE FILE                                   */
/*                                                                 */
/*******************************************************************/
CONTROL NOMSG
SET &TBLNAME = &SYSUID..$$@@TBL.TABLE
FREE F(TEMPTBL TTBL)
DEL '&TBLNAME'
ATTR TTBL BLKSIZE(3120) LRECL(80) RECFM(F B) DSORG(PO)
ALLOC F(TEMPTBL) DA('&TBLNAME') USING(TTBL) NEW SP(5 1) TR DIR(10)
FREE F(TEMPTBL TTBL)
SET XSPTABL = &STR('&TBLNAME')
CONTROL MSG
/*******************************************************************/
/*                                                                 */
/* THIS CLIST STARTS THE ISPF INTERFACE OF CARTS-TS                */
/*                                                                 */
/*******************************************************************/
/*******************************************************************/
/* SET DATASET NAMES ASSIGNED TO VARIOUS CARTS-TS FILES            */
/*******************************************************************/
/*******************************************************************/
/* REPLACE THE CHARACTER STRING BETWEEN THE ' WITH                 */
/*   THE SAME DATASET NAMES YOU USED WHEN ORIGINALLY               */
/*   DOWNLOADED THE CARTS-TS DISRTIBUTION TAPE                     */
/*******************************************************************/
SET CARTPLIB = &STR('SYSS.CARTS.V3R0M0.ISPPLIB')
SET CARTLOAD = &STR('SYS1.CARTS.V3R0M0.LOADLIB')
SET CARTSLIB = &STR('SYSS.CARTS.V3R0M0.ISPSLIB')
SET CARTCLIB = &STR('SYSS.CARTS.V3R0M0.CLISTLIB')
SET XSPFILE  = &STR('SYSS.CARTS.V3R0M0.TAILOR')
/*******************************************************************/
ISPEXEC VPUT (CARTPLIB CARTLOAD CARTSLIB XSPFILE) SHARED
ISPEXEC VPUT (CARTCLIB) SHARED
ISPEXEC VPUT (XSPTABL) SHARED
/*******************************************************************/
/* ASSIGN THE TEMPORARY CARTS-TS LIBRARIES                         */
/*******************************************************************/
ISPEXEC LIBDEF ISPLLIB DATASET ID(&CARTLOAD.)
ISPEXEC LIBDEF ISPPLIB DATASET ID(&CARTPLIB.)
ISPEXEC LIBDEF ISPSLIB DATASET ID(&CARTSLIB.)
ISPEXEC LIBDEF ISPTLIB DATASET ID(&XSPTABL.)
/*******************************************************************/
/* ALLOC ISPF TAILOR FILE                                          */
/*******************************************************************/
ALLOC FI(ISPFILE) DA(&XSPFILE) SHR
/*******************************************************************/
/* ALLOCATE THE CARTS-TS ISPTABL FILE                              */
/*   THE FOLLOWING SUB-CLIST WILL DETERMINE IF ISPTABL IS          */
/*   CURRENTLY ALLOCATED, FREE THE EXISTING DATASET NAME AND       */
/*   ALLOCATE THE CARTS-TS ISPTABL DATASET.  THE ORIGINAL          */
/*   DATASET ALLOCATED WILL BE RESTORED AT THE END OF              */
/*   THIS CLIST.                                                   */
/*******************************************************************/
SET OLDTABLS = N        /* SET ISPTABL SWITCH TO OFF */
 SET SYSDVAL = 32767 1
 READDVAL SYSOUTTRAP I SYSPROCDSNS DDNAME
 LISTA ST
 SET BL = &SUBSTR(1:03,                            )
 DO WHILE &DDNAME ¬= ISPTABL AND &I <= &SYSOUTLINE
    SET  X = &STR(&&SYSOUTLINE&I)
    IF &SUBSTR(1:2,&X.&BL) = &BL +
    THEN SET DDNAME = &SUBSTR(3:10,&X)
    SET I = &I + 1
      END
IF &DDNAME = ISPTABL +
THEN
    SET I = &I - 2
    SET OLDTABLS = Y
    SET OLDTABL = &STR(&&SYSOUTLINE&I)
    ISPEXEC VPUT (OLDTABL OLDTABLS) SHARED
    FREE FI(ISPTABL)

ALLOC FI(ISPTABL) DA(&XSPTABL) SHR

/*******************************************************************/
/* CONCATENATE THE CARTS-TS CLISTL FILE                            */
/*   THE FOLLOWING SUB-CLIST WILL DETERMINE IF SYSPROC IS          */
/*   CURRENTLY ALLOCATED, FREE THE EXISTING DATASET NAME(S) AND    */
/*   ALLOCATE THE CARTS-TS CLISTL DATASET.  THE ORIGINAL           */
/*   DATASET(S) ALLOCATED WILL BE RESTORED AT THE END OF           */
/*   THIS CLIST.                                                   */
/*******************************************************************/
 SET SYSDVAL = 32767 1
 READDVAL SYSOUTTRAP I SYSLIBSDSNS DDNAME
 LISTA ST
 SET BL = &SUBSTR(1:03,                            )
 DO WHILE &DDNAME ¬= SYSPROC AND &I <= &SYSOUTLINE
    SET  X = &STR(&&SYSOUTLINE&I)
    IF &SUBSTR(1:2,&X.&BL) = &BL +
    THEN SET DDNAME = &SUBSTR(3:10,&X)
    SET I = &I + 1
   END

   IF &DDNAME = SYSPROC +
   THEN DO
          SET I = &I - 2
          SET DSN = &&SYSOUTLINE&I
          SET SYSLIBSDSNS = '&DSN'
          DO WHILE &I +3 <= &SYSOUTLINE
            SET I = &I +3
            SET DSN = &&SYSOUTLINE&I
            IF &SUBSTR(1:5,&DSN,&BL.&BL) ¬= &BL +
            THEN SET I = &SYSOUTLINE
            ELSE DO
                   SET I = &I-1
                   SET DSN = &&SYSOUTLINE&I
                   SET SYSLIBSDSNS = &STR(&SYSLIBSDSNS '&DSN')
                 END
          END
        END
FREE FI(SYSPROC)
ALLOC FI(SYSPROC) DA(&SYSLIBSDSNS &CARTCLIB) SHR REUSE
/*******************************************************************/
/* START CARTS-TS UPDATE/HISTORY INQUIRY INTERFACE                 */
/*******************************************************************/
ISPEXEC SELECT PGM(CARTSTSO)
/*******************************************************************/
/* DEALLOC ISPF TAILOR FILE                                        */
/*******************************************************************/
FREE FI(ISPFILE)
/*******************************************************************/
/* RELEASE THE CARTSTS-TS LIBRARIES                                */
/*******************************************************************/
ISPEXEC LIBDEF ISPLLIB
ISPEXEC LIBDEF ISPSLIB
ISPEXEC LIBDEF ISPPLIB
ISPEXEC LIBDEF ISPTLIB
ISPEXEC VGET (OLDTABL) SHARED
FREE FI(ISPTABL)
IF OLDTABL ¬= +
THEN DO
    ALLOC FI(ISPTABL) DA('&OLDTABL') SHR REUSE
END
IF &STR(&SYSLIBSDSNS) = +
THEN DO
      CONTROL NOMSG
      FREE FI(SYSPROC)
     END
ELSE ALLOC FI(SYSPROC) DA(&SYSLIBSDSNS) SHR REUSE
CONTROL NOMSG
/*******************************************************************/
/* DELETE TEMPORARY TABLE FILE                                     */
/*******************************************************************/
DEL '&TBLNAME'
CONTROL MSG
/*******************************************************************/
/* EXIT-ALL DONE                                                   */
/*******************************************************************/
DONE: +
  EXIT
