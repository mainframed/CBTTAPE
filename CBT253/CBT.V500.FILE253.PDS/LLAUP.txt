/* REXX automatic LLA update command */
parse upper arg dsname . ;
if dsname="" then do;
     say "Invalid dsname argument passed";
     exit 0;
     end;
if substr(dsname,1,1)="'" then , /* strip enclose quotes */
     dsname=substr(dsname,2,length(dsname)-2);
"ALLOC F(INPUT) DA('SYS1.PARMLIB(CSVLLASS)') SHR";
"EXECIO * DISKR INPUT (FINIS STEM LLADATA.";
"FREE F(INPUT)";
parmlib_suffix = "";
do i = 1 to lladata.0;
    parse var lladata.i "/*" "SUFFIX(" parmsufx ")" sufdsn "*/";
    parmsufx=strip(parmsufx); sufdsn=strip(sufdsn);
    if sufdsn=dsname then do;
          parmlib_suffix=parmsufx;
          leave i;
          end;
end;
cvtptr = storage("00000010",4); /* locate llt/smca from cvt */
cvtsmca = storage(d2x(c2d(cvtptr)+196),4); /* mapping smca */
savehi=substr(c2x(cvtsmca),1,2);
smcasid = savehi||d2x(x2d(substr(c2x(cvtsmca),3))+16);
smcasid = storage(smcasid,4); /* SMF id */
if parmlib_suffix="" then do;
cvtllta = d2x(c2d(cvtptr)+1244); /* mapping ihallt */
llta = c2d(storage(cvtllta,4));
lltacount = c2d(storage(d2x(llta+4),4));
lltaentry = d2x(llta+9); /* entry ptr */
do i = 1 to lltacount;
     lnkdsn= strip(storage(lltaentry,44));
     if dsname=lnkdsn then do;
         parmlib_suffix="00";
         leave i; end;
     lltaentry = d2x(x2d(lltaentry)+45);
end;
if parmlib_suffix="" then do;
     say "Invalid dsname argument passed";
     exit 0;
     end;
     end;
if parmlib_suffix = "00" then ,
   say "LinkList dataset is being LLA-refreshed";
   else ,
   say "Non-LinkList dataset is being LLA-refreshed";
   say "Please LLAUP refresh on ALL other running systems";
command = "F LLA,UPDATE="parmlib_suffix;
"CONSOLE SYSCMD("command")";
select;
  when (smcasid="SYSA") then sysid="2"; /* other side */
  when (smcasid="SYSB") then sysid="1";
  otherwise sysid = "";
end;
command = "$M"sysid",'"command"'";
if sysidÂ¬="" then "CONSOLE SYSCMD("command")";
exit 0;
