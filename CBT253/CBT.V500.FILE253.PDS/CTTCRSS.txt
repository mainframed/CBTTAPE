PROC 0 DEBUG                  +
       PARM()                 +
       LOADLIB(SYS1.IOA.V410.LOAD) +
       DARLLIB(SYSM.CTTI.V1R2M2.PARM)
IF &PARM =DEBUG THEN SET &DEBUG=DEBUG
IF &DEBUG=DEBUG THEN DO
   CONTROL MAIN LIST CONLIST SYMLIST MSG
   FREE  F(PRTDBG,SYSPRINT)
   ALLOC F(PRTDBG) DA(*)
   ALLOC F(SYSPRINT) DA(*)
   END
  ELSE DO
   CONTROL NOMSG NOFLUSH
   FREE  F(PRTDBG,SYSPRINT)
   ALLOC F(PRTDBG) DUMMY
   ALLOC F(SYSPRINT) DUMMY
  END
SET &CRSFLD = CTTDSN
REDISP:+
 ISPEXEC DISPLAY PANEL(CTTPRSS) CURSOR(&CRSFLD)
   SET &RC = &LASTCC
   IF (&RC > 8) THEN DO
      WRITE ********************************************************
      WRITE ***** ERROR IN NEXT PANEL TO BE DISPLAYED. RC = &RC  ****
      WRITE ***** TERMINATING ...                               ****
      WRITE ********************************************************
      EXIT
   END DO
 IF ( &YES = NO ) THEN GOTO REDISP
 ISPEXEC VPUT +
   (CTTVOL,CTTDSN,CTTJBNAM,CTTACCNT,CTTPGM,CTTUSRID,      +
    CTTMEDIA,CTTLOAD,CTTRLLST,CTTJCLXD,CTTLDATE)          +
   PROFILE
 IF &RESP = END THEN EXIT
 IF ( &CTTLOAD = Y ) THEN DO
    FREE  F(DARULLST)
    ALLOC F(DARULLST) DS('&DARLLIB(&CTTRLLST)') SHR
    /*
    /* MAKE SURE THAT &CTTRLLST MEMBER EXISTS
    /*
   ISPEXEC LMINIT   DATAID(DARLL) DATASET('&DARLLIB') ENQ(SHR)
   IF (&LASTCC ¬= 0) THEN DO
      ISPEXEC SETMSG MSG(CTTR000Z)
      SET &CRSFLD = CTTRLLST
      GOTO REDISP
   END DO
   ISPEXEC LMOPEN   DATAID(&DARLL) OPTION(INPUT)
   SET &RC = &LASTCC
   IF (&RC ¬= 0) THEN DO
      ISPEXEC SETMSG MSG(CTTR000Z)
      GOTO REDISP
   END DO
   ISPEXEC LMMFIND  DATAID(&DARLL) MEMBER(&CTTRLLST)
   SET RC = &LASTCC
   IF ( &RC = 8 ) THEN DO
     ISPEXEC SETMSG MSG(CTTR000A)    /* MEMBER NOT FOUND
     SET &CRSFLD = CTTRLLST
     GOTO REDISP
   END DO
   IF ( &RC ¬= 0 ) THEN DO
     ISPEXEC SETMSG MSG(CTTR000B)    /* ALLOCATION ERROR
     GOTO  REDISP
   END DO
   ISPEXEC LMFREE DATAID(DARLL)
 END DO
   FREE  DD(DARSS)
   ALLOC F(DARSS) NEW TRACKS SP(1 1)
   CALL '&LOADLIB(CTTRSS)'   '&PARM'
   SET RC = &LASTCC
   IF ( &RC = 4 ) THEN ISPEXEC SETMSG MSG(CTTR000E)  /* NO MATCHES   */
   IF ( &RC = 8 ) THEN ISPEXEC SETMSG MSG(CTTR000D)  /*C-T NOT ACTIVE*/
   IF ( &RC = 12) THEN ISPEXEC SETMSG MSG(CTTR000F)  /*BAD RLD CARDS */
   IF ( &RC = 16) THEN ISPEXEC SETMSG MSG(CTTR000G)  /*GETMAIN ERROR */
   IF ( &RC = 20) THEN ISPEXEC SETMSG MSG(CTTR000H)  /* CTTGTH ERROR */
   IF ( &RC > 20) THEN ISPEXEC SETMSG MSG(CTTR000I)  /*OTHERWISE     */
   IF ( &RC ¬= 0 ) THEN GOTO REDISP
   ISPEXEC LMINIT DATAID(OUTF) DDNAME(DARSS)
   IF ( &LASTCC ¬= 0 ) THEN DO
      ISPEXEC SETMSG MSG(CTTR000C)
      GOTO REDISP
   END DO
   IF ( &CTTBE = BROWSE ) THEN +
       ISPEXEC BROWSE  DATAID(&OUTF)
      ELSE +
       ISPEXEC EDIT   DATAID(&OUTF)
   ISPEXEC LMFREE DATAID(&OUTF)
GOTO  REDISP
END
