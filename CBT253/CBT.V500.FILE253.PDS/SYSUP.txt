/* REXX automatic System dataset update command */
true=1;
false=0;
parse source "TSO". execname . ;
select;
  when (execname="SYSUP") then func="UPDATE";
  when (execname="SYSUPADD") then func="ADD";
  when (execname="SYSUPDEL") then func="DELETE";
  otherwise nop;
end;
parse upper arg dsname . ;
if dsname="" then do;
     say "Invalid dsname argument passed";
     exit 12;
     end;
quote = substr(dsname,1,1);
if quote="'" | quote='"'  then , /* strip enclose quotes */
     dsname=substr(dsname,2,length(dsname)-2);
lla=false;
vlf=false;
if pos("(",dsname)¬=0 then do;
      vlf=true;
      parse var dsname dsname "(" member ")" . ;
      parmmem = "COFVLFSS";
      end;
  else do;
      lla=true;
      parmmem = "CSVLLASS";
      end;
"ALLOC F(INPUT) DA('SYS1.PARMLIB("parmmem")') SHR";
"EXECIO * DISKR INPUT (FINIS STEM SYSDATA.";
"FREE F(INPUT)";
parmlib_suffix = "";
vlffound=false;
if lla then ,
do i = 1 to sysdata.0;
    parse var sysdata.i "/*" "SUFFIX(" parmsufx ")" sufdsn "*/";
    parmsufx=strip(parmsufx); sufdsn=strip(sufdsn);
    if sufdsn=dsname then do;
          parmlib_suffix=parmsufx;
          leave i;
          end;
end;
if vlf then ,
do i = 1 to sysdata.0;
    parse var sysdata.i . "EDSN(" vlfdsn ")" . ;
    vlfdsn=strip(vlfdsn);
    if vlfdsn=dsname then do;
          vlffound=true;
          leave i;
          end;
end;
if vlf & ¬vlffound then do;
     say "Invalid dsname argument passed";
     exit 12;
     end;
cvtptr = storage("00000010",4); /* locate llt/smca from cvt */
cvtsmca = storage(d2x(c2d(cvtptr)+196),4); /* mapping smca */
savehi=substr(c2x(cvtsmca),1,2); /* hi order bit is on */
smcasid = savehi||d2x(x2d(substr(c2x(cvtsmca),3))+16);
smcasid = storage(smcasid,4); /* SMF id */
if lla then do; /* start of lla only code */
if parmlib_suffix="" then do;
cvtllta = d2x(c2d(cvtptr)+1244); /* mapping ihallt */
llta = c2d(storage(cvtllta,4));
lltacount = c2d(storage(d2x(llta+4),4));
lltaentry = d2x(llta+9); /* entry ptr */
do i = 1 to lltacount;
     lnkdsn= strip(storage(lltaentry,44));
     if dsname=lnkdsn then do;
         parmlib_suffix="00";
         leave i; end;
     lltaentry = d2x(x2d(lltaentry)+45);
end;
if parmlib_suffix="" then do;
     say "Invalid dsname argument passed";
     exit 12;
     end;
     end;
if parmlib_suffix = "00" then ,
   say "LinkList dataset is being LLA-refreshed";
   else ,
   say "Non-LinkList dataset is being LLA-refreshed";
   say "Please LLAUP refresh on ALL other running systems";
command = "F LLA,UPDATE="parmlib_suffix;
"CONSOLE ACTIVATE"; /* make extended console */
"CONSOLE SYSCMD("command")";
end; /* end off lla only */
if vlf then do;
     "CONSOLE ACTIVATE"; /* extended console */
    command="S VLFUP,,,VLFPARM=(VLFDSN="dsname",VLFMEM="member"," || ,
                 "VLFFUNC="func")";
     end;
select;
  when (smcasid="SYSA") then sysid="2"; /* sysid of other side */
  when (smcasid="SYSB") then sysid="1";
  otherwise sysid = "";
end;
command = "$M"sysid",'"command"'";
if sysid¬="" then "CONSOLE SYSCMD("command")";
"CONSOLE DEACTIVATE"; /* terminate extended console */
exit 0;
