/*  REXX EXEC : ( NWBACKUP ).
    FUNCTION  : LAN BACKUP PANEL INTERFACE FACILITY FOR RESTART OF A
                SPECIFIED SERVER VOLUME(S).
                BUILDS AND SUBMITS THE JOB REQUIRED TO PERFORM THE
                BACKUP OF THE FAILED VOLUME(S).
    INPUT     : PTECH2.NETCA.&&SERVER.J&&JULIAN(&&SERVER).
    OUTPUT    : &&USERID.DIALOG.ISPFILE(&&SERVER).
                                                                    */
TRACE O
/*      */
HOUSE_KEEPING:
ADDRESS ISPEXEC
TSOID = SYSVAR(SYSUID)
SERVER.1 = 'CS1 PTTS12'
SERVER.2 = 'CS2 PTTS13'
SERVER.3 = 'FIN PTTS14'
SERVER.4 = 'MIS PTTS15'
SERVER.5 = 'CS3 PTTS16'
SERVER.6 = 'CSI PTTS17'
SERVER.7 = 'DB1 PTTS18'
SERVER.8 = 'SYS PTTS19'
SERVER.9 = 'COM PTTS20M'         /*  MONTHLY ONLY  */
SERVER.10 = 'POS PTTS31D'
SERVER.11 = 'MAGIC PTTS32D'
SERVER.12 = 'PAD PTTS39D'        /* CS_PDAD SERVER ALWAYS FULL BACKUP */
BUSW.1 = 'SELECT'
BUSW.2 = 'ALL'
BUTP.1 = 'INCR'
BUTP.2 = 'FULL'
JS = 'D'
CONFIG = 'CONFIGTI'
BUTYPE = 'INCR'
BUPARM = 'DAILY'
SUB_IMMED = 'COM MAGIC PAD POS'
IMMEDCNT = 4
JCLLIB = 'SYSS.NETCA.PRODJCL.JCLLIB'
TEXTLIB = 'SYSS.NETCA.CONTROL.TEXTLIB'
PARMLIB = 'SYSS.NETCA.RESTART.PARMLIB'
MSGLIB = 'SYSS.NETCA.MESSAGE.CNTLLIB'
CTEXTLIB = 'SYSS.NETUA.CA.V1R1M1.TEXTLIB'
"CONTROL ERRORS RETURN"
X = MSG("OFF")

/*     */
DISPLAY_SERVER_PANEL:
"DISPLAY PANEL(NWSRVPNL)"
SRC = RC
IF SRC = 8 THEN EXIT 0
IF SRC > 8 THEN DO
   SAY '*** ERROR INVOKING ISPF DIALOG PANEL ( NWSRVPNL ). ***'
   SAY '*** NWBACKUP EXEC TASK CANCELLED - RC = 'SRC'. ***'
   EXIT SRC
END

/*     */
CHECK_RESTART_PARMS:
ADDRESS TSO
"FREE FILE(NWDD)"
"FREE FILE(ISPFILE)"
SERVER = WORD(SERVER.LAN,1)
TLMS1 = SERVER
DO S = 1 TO IMMEDCNT
   BYPASS = WORD(SUB_IMMED,S)
   IF BYPASS = SERVER THEN DO
      TLMS1 = SUBSTR(SERVER,1,3)
      SUBMEMB = WORD(SERVER.LAN,2)
      SUBPDS = JCLLIB
      CALL SUBMIT_RESTART_JOB
      EXIT 0
   END
END
RSPARM = SERVER||'RSPRM'
TLMSMEMB = TLMS1||'TLMS'
NWPDS = PARMLIB
NWDSN = SYSDSN("'"NWPDS"("RSPARM")'")
IF NWDSN = 'OK' THEN DO
   VOLSW = 'RSTPARM'
   CALL GET_RESTART_PARMS
   IF BUTYPE = 'FULL' THEN DO
      JS = 'W'
      CONFIG = 'CONFIGTF'
      BUPARM = 'DAILYF'
   END
   IF BLDJOB = 'NO' THEN DO
      SUBPDS = JCLLIB
      SMEMB = WORD(SERVER.LAN,2)
      SUBMEMB = SMEMB||JS
      CALL SUBMIT_RESTART_JOB
      EXIT 0
   END
   CALL BUILD_RESTART_VOLUME_MEMBER
   CALL WRITE_PDS_MEMBER
   CALL BUILD_RESTART_CONFIG_MEMBER
   IF CFM = 'MEMBER NOT FOUND' THEN CALL WRITE_PDS_MEMBER
   CALL ALLOC_ISPFILE_PDS
   CALL BUILD_RESTART_JOB
   CALL WRITE_PDS_MEMBER
   SMEMB = WORD(SERVER.LAN,2)
   SUBMEMB = SMEMB||JS
   SUBPDS = TSOID||'.DIALOG.ISPFILE'
   CALL SUBMIT_RESTART_JOB
   CALL DELETE_ISPFILE_PDS
   EXIT 0
END

/*     */
BUILD_RESTART_VOLUME_CONFIG:
ADDRESS TSO
BUTYPE = 'FULL'
CALL READ_SERVER_VOLUMES_PARMS
BUPARM = 'DAILY'
CALL DISPLAY_VOLUME_PANEL
IF BUSW.LBU = 'ALL' THEN DO
   JS = 'D'
   SUBPDS = JCLLIB
   SMEMB = WORD(SERVER.LAN,2)
   IF BUTP.BUT = 'FULL' THEN JS = 'W'
   SUBMEMB = SMEMB||JS
   CALL SUBMIT_RESTART_JOB
   EXIT 0
END
BUTYPE = BUTP.BUT
IF BUTYPE = 'FULL' THEN BUPARM = 'DAILYF'
CALL BUILD_PANEL_RESTART_VOLUME_MEMBER
IF RCNT = 0 THEN DO FOREVER
   ADDRESS ISPEXEC "SETMSG MSG(NWLAN001)"
   CALL DISPLAY_VOLUME_PANEL
   CALL BUILD_PANEL_RESTART_VOLUME_MEMBER
   IF RCNT > 0 THEN LEAVE
END
CALL WRITE_PDS_MEMBER
CALL BUILD_RESTART_CONFIG_MEMBER
IF CFM = 'MEMBER NOT FOUND' THEN CALL WRITE_PDS_MEMBER
CALL ALLOC_ISPFILE_PDS
CALL GET_PANEL_VOLUMES
CALL BUILD_RESTART_JOB
CALL WRITE_PDS_MEMBER
SMEMB = WORD(SERVER.LAN,2)
SUBMEMB = SMEMB||JS
SUBPDS = TSOID||'.DIALOG.ISPFILE'
CALL SUBMIT_RESTART_JOB
CALL DELETE_ISPFILE_PDS
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
GET_RESTART_PARMS:
ADDRESS TSO
BLDJOB = 'YES'
"ALLOC DA('"NWPDS"("RSPARM")') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('NWPDS'('RSPARM'). ***'
   SAY '*** NWBACKUP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR NWDD (FINIS STEM VOLR."
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('NWPDS'('RSPARM'). ***'
   SAY '*** NWBACKUP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
SERVOL = WORD(VOLR.1,1)
BUTYPE = WORD(VOLR.1,3)
IF SERVOL = 'ALL' THEN BLDJOB = 'NO'
RETURN

/*     */
BUILD_RESTART_VOLUME_MEMBER:
ADDRESS TSO
"DELSTACK"
OUTMEMB = SERVER||'VOLS'
OUTPDS = PARMLIB
VOLPARMS = SERVER||'FVOLS'
"ALLOC DA('"TEXTLIB"("VOLPARMS")') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('TEXTLIB'('VOLPARMS'). ***'
   SAY '*** NWBACKUP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR NWDD (FINIS STEM VOLP."
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('TEXTLIB'('VOLPARMS'). ***'
   SAY '*** NWBACKUP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
DO I = 1 TO VOLR.0
   SERVOL = WORD(VOLR.I,1)
   DO V = 1 TO VOLP.0
      UPPER VOLP.V
      PVOL = WORD(VOLP.V,1)
      IF PVOL = SERVOL THEN QUEUE VOLP.V
   END
END
RETURN

/*     */
BUILD_RESTART_CONFIG_MEMBER:
ADDRESS TSO
"DELSTACK"
OUTMEMB = SERVER||'CONFG'
OUTPDS = PARMLIB
VOLMEMB = SERVER||'VOLS'
CFM = SYSDSN("'"PARMLIB"("OUTMEMB")'")
IF CFM = 'OK' THEN RETURN
"ALLOC DA('"TEXTLIB"("CONFIG")') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('TEXTLIB'('CONFIG'). ***'
   SAY '*** NWBACKUP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR NWDD (FINIS STEM PARM."
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('TEXTLIB'('CONFIG'). ***'
   SAY '*** NWBACKUP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
DO I = 1 TO PARM.0
   CHG = POS('VOLUMES',PARM.I)
   IF CHG ¬= 0 THEN DO
      PARM.I = "         VOLUMES      '"PARMLIB"("VOLMEMB")'"
   END
   QUEUE PARM.I
END
RETURN

/*     */
BUILD_RESTART_JOB:
"DELSTACK"
J = 22
SRVCONFG = SERVER||'CONFG'
SMEMB = WORD(SERVER.LAN,2)
JN = SMEMB||JS
OUTMEMB = JN
WTR = SUBSTR(JN,2)
WTRNAME = WTR'00'
OUTPDS = TSOID||'.DIALOG.ISPFILE'
SCHPRT = 'PTECH2.NETCA.'SERVER'.SCHPRINT'
SYSPRT = 'PTECH2.NETCA.'SERVER'.SYSPRINT'
JCL.1 = "//"JN"  JOB O030OVRHD0000000,'"SERVER" LAN BACKUP',NOTIFY=TECH251,"
JCL.2 = "//             CLASS=F,MSGCLASS=Q,MSGLEVEL=(1,1),REGION=6M"
JCL.3 = "//STEP010  EXEC PGM=IKJEFT01,DYNAMNBR=30,"
JCL.4 = "//         PARM='%NWALLOC "SERVER" "BUPARM"'"
JCL.5 = "//SYSTSPRT DD SYSOUT=*"
JCL.6 = "//SYSPRINT DD SYSOUT=*"
JCL.7 = "//SYSIN DD DUMMY"
JCL.8 = "//SYSLBC DD DSN=SYS1.BRODCAST,DISP=SHR"
JCL.9 = "//SYSUADS DD DSN=SYS1.UADS,DISP=SHR"
JCL.10 = "//SYSPROC DD DSN=SYSS.TECH.COMMON.EXECLIB,DISP=SHR"
JCL.11 = "//SYSTSIN DD DUMMY"
JCL.12 = "/*"
JCL.13 = "//ABEND010 EXEC PGM=ABENDWTO,COND=(0,GE,STEP010)"
JCL.14 = "//* *** STEPLIB DD DSN=SYS1.PROD.BATCH.LOADLIB,DISP=SHR"
JCL.15 = "/*"
JCL.16 = "//STEP020 EXEC PGM=NUACLIEN"
JCL.17 = "//SCHPRINT DD DSN="SCHPRT",DISP=(MOD,CATLG,DELETE),"
JCL.18 = "// SPACE=(CYL,(2,0,0)),DCB=(RECFM=FBA,LRECL=133,BLKSIZE=133),"
JCL.19 = "// DATACLAS=LISTING"
JCL.20 = "//SYSPRINT DD DSN="SYSPRT",DISP=(MOD,CATLG,DELETE),"
JCL.21 = "// SPACE=(CYL,(2,0,0)),DCB=(RECFM=FBA,LRECL=133,BLKSIZE=133),"
JCL.22 = "// DATACLAS=LISTING"
IF VOLSW = 'RSTPARM' THEN DO
   DO I = 1 TO VOLR.0
      JVOL = WORD(VOLR.I,1)
      DDVOL = 'DDC'||JVOL
      DDINDX = 'DDI'||JVOL
      BT = BUTYPE
      IF I = 1 THEN DO
         TUNIT = 'UNIT=(SILO,,DEFER)'
         SAVEDD = DDVOL
      END
      IF I > 1 THEN TUNIT = 'UNIT=AFF='SAVEDD
      REC.1 = "//"DDVOL"  DD DSN=PTAP.NETCA."SERVER"."JVOL"."BT".CONTAINR(+1),"
      REC.2 = "//         DISP=(NEW,CATLG,DELETE),"TUNIT","
      REC.3 = "//         VOLUME=(,,,99),LABEL=(1,SL),"
      REC.4 = "//   DCB=(MODEL.DSCB,RECFM=U,LRECL=0,BLKSIZE=32760,DSORG=PS)"
      REC.5 = "//"DDINDX" DD DSN=PTECH2.NETCA."SERVER"."JVOL"."BT".INDEX(+1),"
      REC.6 = "//            DISP=(NEW,CATLG,DELETE),"
      REC.7 = "//  DCB=(BLKSIZE=27998,LRECL=27994,RECFM=VB,DSORG=PS),"
      REC.8 = "//  DATACLAS=DATAPS,MGMTCLAS=STANDARD,STORCLAS=SCSTNRDG,"
      REC.9 = "//  SPACE=(27998,(50,10),RLSE)"
      DO E = 1 TO 9
         J = J + 1
         JCL.J = REC.E
      END
   END
END
IF VOLSW = 'PANEL' THEN DO
   DO I = 1 TO VOLP.0
      JVOL = WORD(VOLP.I,1)
      DDVOL = 'DDC'||JVOL
      DDINDX = 'DDI'||JVOL
      BT = BUTYPE
      IF I = 1 THEN DO
         TUNIT = 'UNIT=(SILO,,DEFER)'
         SAVEDD = DDVOL
      END
      IF I > 1 THEN TUNIT = 'UNIT=AFF='SAVEDD
      REC.1 = "//"DDVOL"  DD DSN=PTAP.NETCA."SERVER"."JVOL"."BT".CONTAINR(+1),"
      REC.2 = "//         DISP=(NEW,CATLG,DELETE),"TUNIT","
      REC.3 = "//         VOLUME=(,,,99),LABEL=(1,SL),"
      REC.4 = "//   DCB=(MODEL.DSCB,RECFM=U,LRECL=0,BLKSIZE=32760,DSORG=PS)"
      REC.5 = "//"DDINDX" DD DSN=PTECH2.NETCA."SERVER"."JVOL"."BT".INDEX(+1),"
      REC.6 = "//            DISP=(NEW,CATLG,DELETE),"
      REC.7 = "//  DCB=(BLKSIZE=27998,LRECL=27994,RECFM=VB,DSORG=PS),"
      REC.8 = "//  DATACLAS=DATAPS,MGMTCLAS=STANDARD,STORCLAS=SCSTNRDG,"
      REC.9 = "//  SPACE=(27998,(50,10),RLSE)"
      DO E = 1 TO 9
         J = J + 1
         JCL.J = REC.E
      END
   END
END
JCLCNT = J
DO J = 1 TO JCLCNT
   QUEUE JCL.J
END
R = 0
RCDCNT = 35
RCD.1 = "//NUACACON DD DSN="PARMLIB"("SRVCONFG"),DISP=SHR"
RCD.2 = "//NUACATXT DD DSN="CTEXTLIB",DISP=SHR"
RCD.3 = "//PARAMS DD *"
RCD.4 = "-HOST "SERVER" -OUTPUT 'PTECH2.NETCA."SERVER".SCRIPT'"
RCD.5 = "    DD:NUACACON -CHECK"
RCD.6 = "//SYSIN DD *"
RCD.7 = "SET INPUT VERIFY ON"
RCD.8 = "INPUT DD:NUACATXT(BASIC) 'PTECH2.NETCA."SERVER".SCRIPT'"
RCD.9 = "/*"
RCD.10 = "//STEP030 EXEC PGM=IKJEFT01,DYNAMNBR=30,"
RCD.11 = "// PARM='%NWABEND "SERVER" "BUTYPE" RESTART',"
RCD.12 = "// COND=(4,GE,STEP020)"
RCD.13 = "//SYSTSPRT DD SYSOUT=*"
RCD.14 = "//SYSPRINT DD SYSOUT=*"
RCD.15 = "//SYSIN DD DUMMY"
RCD.16 = "//SYSLBC DD DSN=SYS1.BRODCAST,DISP=SHR"
RCD.17 = "//SYSUADS DD DSN=SYS1.UADS,DISP=SHR"
RCD.18 = "//SYSPROC DD DSN=SYSS.TECH.COMMON.EXECLIB,DISP=SHR"
RCD.19 = "//SYSTSIN DD DUMMY"
RCD.20 = "/*"
RCD.21 = "//STEP040  EXEC CATRPTS,COND=((4,GE,STEP020),(1,NE,STEP030))"
RCD.22 = "//SYSIN    DD DSN="PARMLIB"("TLMSMEMB"),DISP=SHR"
RCD.23 = "/*"
RCD.24 = "//ABEND050 EXEC WTO,COND=(4,GE,STEP020)"
RCD.25 = "//SYSIN DD DSN="MSGLIB"("SERVER"),DISP=SHR"
RCD.26 = "/*"
RCD.27 = "//STEP060 EXEC PGM=IEBGENER"
RCD.28 = "//SYSUT1 DD DSN="SYSPRT",DISP=SHR"
RCD.29 = "//SYSUT2 DD SYSOUT=(V,"WTRNAME"),DCB=BLKSIZE=133"
RCD.30 = "//SYSPRINT DD DUMMY"
RCD.31 = "//SYSIN DD DUMMY"
RCD.32 =  "/*"
RCD.33 = "//*          ABEND STEP FOR RETURN CODE GE 4"
RCD.34 = "//ABEND070 EXEC PGM=ABENDWTO,COND=(4,GE,STEP020)"
RCD.35 = "//"
DO RCDCNT
   R = R + 1
   QUEUE RCD.R
END
RETURN

/*     */
READ_SERVER_VOLUMES_PARMS:
VOLPARMS = SERVER||'FVOLS'
"ALLOC DA('"TEXTLIB"("VOLPARMS")') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('TEXTLIB'('VOLPARMS'). ***'
   SAY '*** NWBACKUP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR NWDD (FINIS STEM VOLB."
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('TEXTLIB'('VOLPARMS'). ***'
   SAY '*** NWBACKUP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
V = 0
VOL1 = '*****'
VOL2 = '*****'
VOL3 = '*****'
VOL4 = '*****'
VOL5 = '*****'
VOL6 = '*****'
VOL7 = '*****'
VOL8 = '*****'
DO I = 1 TO VOLB.0
   UPPER VOLB.I
   SVOL = POS(BUPARM,VOLB.I)
   IF SVOL = 0 THEN ITERATE
   V = V + 1
   SERVOL = WORD(VOLB.I,1)
   IF V = 1 THEN VOL1 = LEFT(SERVOL,5,'*')
   IF V = 2 THEN VOL2 = LEFT(SERVOL,5,'*')
   IF V = 3 THEN VOL3 = LEFT(SERVOL,5,'*')
   IF V = 4 THEN VOL4 = LEFT(SERVOL,5,'*')
   IF V = 5 THEN VOL5 = LEFT(SERVOL,5,'*')
   IF V = 6 THEN VOL6 = LEFT(SERVOL,5,'*')
   IF V = 7 THEN VOL7 = LEFT(SERVOL,5,'*')
   IF V = 8 THEN VOL8 = LEFT(SERVOL,5,'*')
END
RETURN

/*     */
DISPLAY_VOLUME_PANEL:
ADDRESS ISPEXEC "DISPLAY PANEL(NWVOLPNL)"
SRC = RC
IF SRC = 8 THEN EXIT 0
IF SRC > 8 THEN DO
   SAY '*** ERROR INVOKING ISPF DIALOG PANEL ( NWVOLPNL ). ***'
   SAY '*** NWBACKUP EXEC TASK CANCELLED - RC = 'SRC'. ***'
   EXIT SRC
END
RETURN

/*     */
BUILD_PANEL_RESTART_VOLUME_MEMBER:
ADDRESS TSO
"DELSTACK"
VOLSW = 'PANEL'
OUTMEMB = SERVER||'VOLS'
OUTPDS = PARMLIB
VOLPARMS = SERVER||'FVOLS'
"ALLOC DA('"TEXTLIB"("VOLPARMS")') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('TEXTLIB'('VOLPARMS'). ***'
   SAY '*** NWBACKUP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR NWDD (FINIS STEM VOLP."
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('TEXTLIB'('VOLPARMS'). ***'
   SAY '*** NWBACKUP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
DO I = 1 TO 8
   SELVOL = VALUE('VBU'I)
   IF SELVOL = '' THEN ITERATE
   BUVOL = VALUE('VOL'I)
   IF BUVOL = '' THEN ITERATE
   SVR = POS('*',BUVOL)
   SVRVOL = BUVOL
   IF SVR ¬= 0 THEN DO
      SVR = SVR - 1
      SVRVOL = SUBSTR(BUVOL,1,SVR)
   END
   DO V = 1 TO VOLP.0
      UPPER VOLP.V
      SVOL = POS(BUPARM,VOLP.V)
      IF SVOL = 0 THEN ITERATE
      PVOL = WORD(VOLP.V,1)
      IF PVOL = SVRVOL THEN QUEUE VOLP.V
   END
END
RCNT = QUEUED()
RETURN

/*     */
GET_PANEL_VOLUMES:
VOLMEMB = SERVER||'VOLS'
"ALLOC DA('"PARMLIB"("VOLMEMB")') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('PARMLIB'('VOLMEMB'). ***'
   SAY '*** NWBACKUP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR NWDD (FINIS STEM VOLP."
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('PARMLIB'('VOLMEMB'). ***'
   SAY '*** NWBACKUP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
ALLOC_ISPFILE_PDS:
ADDRESS TSO
UCNTL = SYSDSN(DIALOG.ISPFILE)
IF UCNTL = 'DATASET NOT FOUND' THEN DO
"ALLOC DA(DIALOG.ISPFILE) LIKE(USER.CNTLLIB) NEW SPACE(1,0) DIR(5) CYLINDERS"
   SRC = RC
   IF SRC ¬= 0 THEN DO
   SAY '*** ALLOCATE ERROR: ERROR ALLOCATING DATASET (DIALOG.ISPFILE). ***'
   SAY '*** NWBACKUP EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
   END
END
RETURN

/*     */
WRITE_PDS_MEMBER:
"ALLOC DA('"OUTPDS"("OUTMEMB")') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('OUTPDS'('OUTMEMB'). ***'
   SAY '*** NWBACKUP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW NWDD (FINIS"
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING 'OUTPDS'('OUTMEMB'). ***'
   SAY '*** NWBACKUP EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
SUBMIT_RESTART_JOB:
ADDRESS TSO
"SUBMIT '"SUBPDS"("SUBMEMB")'"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** SUBMIT ERROR: MEMBER ( 'SUBPDS'('SUBMEMB'). ***'
   SAY '*** NWBACKUP EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
IF NWDSN = 'OK' THEN DO
   "ALLOC DA('"NWPDS"("RSPARM")') FILE(NWDD) SHR"
   "DELETE ('"NWPDS"("RSPARM")') FILE(NWDD)"
   "FREE FILE(NWDD)"
END
RETURN

/*     */
DELETE_ISPFILE_PDS:
ADDRESS TSO
"DELETE  (DIALOG.ISPFILE)   NONVSAM SCRATCH PURGE"
RETURN
