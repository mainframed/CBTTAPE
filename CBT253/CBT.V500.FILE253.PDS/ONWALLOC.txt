/*  REXX EXEC : ( NWALLOC ).
    FUNCTION  : VERIFIES AND/OR ALLOCATES THE HISTORY AN OLD HISTORY
                DATASETS FOR EACH SERVER VOLUME SPECIFIED IN THE VOLUMES
                PARM FILE FOR THE NETWORK SYSTEMS USER ACCESS LAN BACKUP.
                PERFORMS MAINTENANCE CLEANUP OF HISTORY DATASET ENTRIES
                FOR DATASETS NO LONGER FOUND IN THE CATALOG.
                                                                    */
TRACE O
/*      */
HOUSE_KEEPING:
ADDRESS TSO
ARG SERVER BACKUP_TYPE
IF SERVER = '' THEN DO
   SAY '*** PARM ERROR: LAN SERVER NAME NOT PASSED ON COMMAND LINE.***'
   SAY '*** NWALLOC EXEC TASK CANCELLED. ***'
   EXIT 101
END
IF BACKUP_TYPE = '' THEN DO
   SAY '*** PARM ERROR: BACKUP TYPE NOT SPECIFIED FOR 'SERVER' LAN BACKUP. ***'
   SAY '*** NWALLOC EXEC TASK CANCELLED. ***'
   EXIT 102
END
X = MSG("OFF")
BUTYPE = 'NOOP'
DSNSUFX = 'H O UPDPRINT'
LBLNODE = 'HISTORY HISTORY LISTING'
PRTNODE = 'SCHPRINT SYSPRINT UPDPRINT'
VOLCNT = 0
ALLOCSW = 'NO'
TEXTLIB = 'SYSS.NETCA.CONTROL.TEXTLIB'
PARMLIB = 'SYSS.NETCA.RESTART.PARMLIB'
HSTMODEL = 'SYSS.NETCA.MODEL.HIST'
PRTMODEL = 'SYSS.NETCA.MODEL.SYSPRINT'
IF BACKUP_TYPE = 'DAILY' THEN BUTYPE = 'INCR'
IF BACKUP_TYPE = 'DAILYF' THEN BUTYPE = 'FULL'
/*     */
/*   S T A R T   O F   M A G I C   A N D   P O S   S E C T I O N   */
IF BACKUP_TYPE = 'MAGIC' | BACKUP_TYPE = 'POS' THEN DO
   SERVOL = BACKUP_TYPE
   BUTYPE = 'FULL'
   HSTDSN = 'PTECH2.NETCA.'SERVER'.'SERVOL'.H'
   VOLKEY = SUBSTR(BACKUP_TYPE,1,3)
   TLMSMEMB = VOLKEY'TLMS'
   CALL DELETE_TLMS_MEMBER
   DO V = 1 TO 3
      SUFX = WORD(DSNSUFX,V)
      LNODE = WORD(LBLNODE,V)
      PNODE = WORD(PRTNODE,V)
      "DELETE ('PTECH2.NETCA."SERVER"."SERVOL"."PNODE"') NONVSAM SCRATCH PURGE"
      VOLDSN = SYSDSN("'PTECH2.NETCA."SERVER"."SERVOL"."SUFX"'")
      IF VOLDSN = 'DATASET NOT FOUND' THEN DO
         LABEL = 'ALLOCATE_'LNODE'_DATASET'
         INTERPRET CALL LABEL
      END
   END
   IF ALLOCSW = 'YES' THEN EXIT 0
   SERVOL = SUBSTR(BACKUP_TYPE,1,3)
   CALL LISTCAT_FULL_CONTAINER
   IF MNTSW = 'NO' THEN EXIT 0
   CALL READ_HISTORY_DATASET
   IF MNTSW = 'NO' THEN EXIT 0
   IF HIST.0 = 0 THEN EXIT 0
   CALL SAVE_HISTORY_DATASET
   IF MNTSW = 'NO' THEN EXIT 0
   IF BACKUP_TYPE = 'MAGIC' THEN VOLKEY = 'MAGIC SYS:'
   IF BACKUP_TYPE = 'POS' THEN VOLKEY = 'POS VOL1:'
   CALL PERFORM_HISTORY_MAINTENANCE
   IF QUESW = 'NO' THEN EXIT 0
   CALL REWRITE_HISTORY_DATASET
   EXIT 0
END
/*   E N D   O F   M A G I C   A N D   P O S   S E C T I O N   */
/*     */
TLMSMEMB = SERVER'TLMS'
CALL DELETE_TLMS_MEMBER
VOLMEMB = SERVER||'IVOLS'
IF BUTYPE = 'FULL' THEN VOLMEMB = SERVER||'FVOLS'
VOLPARM = SYSDSN("'"TEXTLIB"("VOLMEMB")'")
IF VOLPARM = 'MEMBER NOT FOUND' THEN DO
   SAY '*** PARM ERROR: INVALID BACKUP PARM ('BACKUP_TYPE'). ***'
   SAY '*** MEMBER NOT FOUND: ('TEXTLIB'('VOLMEMB'). ***'
   SAY '*** NWALLOC EXEC TASK CANCELLED. ***'
   EXIT 103
END
"FREE FILE(NWDD)"
"DELETE   ('PTECH2.NETCA."SERVER".SCHPRINT')   NONVSAM SCRATCH PURGE"
"DELETE   ('PTECH2.NETCA."SERVER".SYSPRINT')   NONVSAM SCRATCH PURGE"

/*     */
MAIN_ROUTINE:
CALL READ_VOLUMES_PARMS
DO V = 1 TO VOL.0
   UPPER VOL.V
   LANBU = POS(BACKUP_TYPE,VOL.V)
   IF LANBU = 0 THEN ITERATE
   VOLCNT = VOLCNT + 1
   SERVOL = WORD(VOL.V,1)
   HSTDSN = 'PTECH2.NETCA.'SERVER'.'SERVOL'.H'
   DO S = 1 TO 3
      SUFX = WORD(DSNSUFX,S)
      LNODE = WORD(LBLNODE,S)
      VOLDSN = SYSDSN("'PTECH2.NETCA."SERVER"."SERVOL"."SUFX"'")
      IF VOLDSN = 'DATASET NOT FOUND' THEN DO
         LABEL = 'ALLOCATE_'LNODE'_DATASET'
         INTERPRET CALL LABEL
      END
   END
   IF ALLOCSW = 'YES' THEN ITERATE
   CALL LISTCAT_INCR_CONTAINER
   IF MNTSW = 'NO' THEN ITERATE
   CALL LISTCAT_FULL_CONTAINER
   IF MNTSW = 'NO' THEN ITERATE
   CALL READ_HISTORY_DATASET
   IF MNTSW = 'NO' THEN ITERATE
   IF HIST.0 = 0 THEN ITERATE
   CALL SAVE_HISTORY_DATASET
   IF MNTSW = 'NO' THEN ITERATE
   VOLKEY = SERVOL SERVOL':'
   CALL PERFORM_HISTORY_MAINTENANCE
   IF QUESW = 'NO' THEN ITERATE
   CALL REWRITE_HISTORY_DATASET
END
IF VOLCNT = 0 THEN DO
   SAY '*** PARM ERROR: BACKUP TYPE ( 'BACKUP_TYPE' ) NOT FOUND FOR ('SERVER')'
   SAY '                IN ('TEXTLIB'('VOLMEMB'). ***'
   SAY '*** NWALLOC EXEC SUBTASK CANCELLED. ***'
   EXIT 8
END
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
DELETE_TLMS_MEMBER:
NWDEL = SYSDSN("'"PARMLIB"("TLMSMEMB")'")
IF NWDEL = 'OK' THEN DO
   "ALLOC DA('"PARMLIB"("TLMSMEMB")') FILE(NWDD) SHR"
   "DELETE ('"PARMLIB"("TLMSMEMB")') FILE(NWDD)"
   "FREE FILE(NWDD)"
END
RETURN

/*     */
READ_VOLUMES_PARMS:
"ALLOC DA('"TEXTLIB"("VOLMEMB")') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('TEXTLIB'('VOLMEMB'). ***'
   SAY '*** NWALLOC EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR NWDD (FINIS STEM VOL."
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('TEXTLIB'('VOLMEMB'). ***'
   SAY '*** NWALLOC EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
ALLOCATE_HISTORY_DATASET:
ALLOCSW = 'YES'
"ALLOC DA('PTECH2.NETCA."SERVER"."SERVOL"."SUFX"')
LIKE('"HSTMODEL"') NEW SPACE(1,1) TRACKS BLKSIZE(3120)"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** ALLOCATE ERROR: ERROR ALLOCATING 'SERVER' DATASET:'
   SAY '                    (PTECH2.NETCA.'SERVER'.'SERVOL'.'SUFX'). ***'
   SAY '*** NWALLOC EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"FREE DA('PTECH2.NETCA."SERVER"."SERVOL"."SUFX"')"
RETURN

/*     */
ALLOCATE_LISTING_DATASET:
"ALLOC DA('PTECH2.NETCA."SERVER"."SERVOL"."SUFX"')
LIKE('"PRTMODEL"') NEW SPACE(1,1) CYLINDERS BLKSIZE(133) DATACLAS(LISTING)"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** ALLOCATE ERROR: ERROR ALLOCATING 'SERVER' DATASET:'
   SAY '                    (PTECH2.NETCA.'SERVER'.'SERVOL'.'SUFX'). ***'
   SAY '*** NWALLOC EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"FREE DA('PTECH2.NETCA."SERVER"."SERVOL"."SUFX"')"
RETURN

/*     */
LISTCAT_FULL_CONTAINER:
IF ALLOCSW = 'YES' THEN RETURN
F = 0
MNTSW = 'YES'
DSNLVL = 'PTAP.NETCA.'SERVER'.'SERVOL'.FULL.CONTAINR'
X = OUTTRAP("LSTCAT.","*","CONCAT")
"LISTCAT LEVEL("DSNLVL") NAMES"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** LISTCAT ERROR: MAINT CLEANUP FOR 'SERVER' HIST FILES BYPASSED. ***'
   MNTSW = 'NO'
   RETURN
END
DO L = 1 TO LSTCAT.0
   DSNKEY = POS("NONVSAM ------- ",LSTCAT.L)
   IF DSNKEY ¬= 0 THEN DO
      F = F + 1
      FULL.F = WORD(LSTCAT.L,3)
   END
END
FULLCNT = F
RETURN

/*     */
LISTCAT_INCR_CONTAINER:
IF ALLOCSW = 'YES' THEN RETURN
D = 0
MNTSW = 'YES'
DSNLVL = 'PTAP.NETCA.'SERVER'.'SERVOL'.INCR.CONTAINR'
X = OUTTRAP("LSTCAT.","*","CONCAT")
"LISTCAT LEVEL("DSNLVL") NAMES"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** LISTCAT ERROR: MAINT CLEANUP FOR 'SERVER' HIST FILES BYPASSED. ***'
   MNTSW = 'NO'
   RETURN
END
DO L = 1 TO LSTCAT.0
   DSNKEY = POS("NONVSAM ------- ",LSTCAT.L)
   IF DSNKEY ¬= 0 THEN DO
      D = D + 1
      INCR.D = WORD(LSTCAT.L,3)
   END
END
INCRCNT = D
RETURN

/*     */
READ_HISTORY_DATASET:
IF ALLOCSW = 'YES' THEN RETURN
MNTSW = 'YES'
"ALLOC DA('"HSTDSN"') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** ALLOCATE ERROR: ERROR ALLOCATING ('HSTDSN'). ***'
   SAY '*** HISTORY MAINTENANCE CLEANUP NOT PERFORMED FOR ('SERVOL'). ***'
   MNTSW = 'NO'
   RETURN
END
"EXECIO * DISKR NWDD (FINIS STEM HIST."
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('HSTDSN'). ***'
   SAY '*** HISTORY MAINTENANCE CLEANUP NOT PERFORMED FOR ('SERVOL'). ***'
   MNTSW = 'NO'
   RETURN
END
RETURN

/*     */
SAVE_HISTORY_DATASET:
IF ALLOCSW = 'YES' THEN RETURN
"DELSTACK"
MNTSW = 'YES'
SAVEHIST = HSTDSN'.SAVE'
"DELETE   ('"SAVEHIST"')  NONVSAM SCRATCH PURGE"
"ALLOC DA('"SAVEHIST"') LIKE('SYSS.NETCA.MODEL.HIST') NEW SPACE(1,1) TRACKS
BLKSIZE(3120)"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** ALLOCATE ERROR: ('SAVEHIST'). ***'
   SAY '*** HISTORY MAINTENANCE CLEANUP NOT PERFORMED FOR ('SERVOL'). ***'
   MNTSW = 'NO'
   RETURN
END
DO H = 1 TO HIST.0
   QUEUE HIST.H
END
"ALLOC DA('"SAVEHIST"') FILE(NWDD) SHR"
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW NWDD (FINIS"
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING ('SAVEHIST'). ***'
   SAY '*** HISTORY MAINTENANCE CLEANUP NOT PERFORMED FOR ('SERVOL'). ***'
   MNTSW = 'NO'
   RETURN
END
RETURN

/*     */
PERFORM_HISTORY_MAINTENANCE:
IF ALLOCSW = 'YES' THEN RETURN
"DELSTACK"
QUESW = 'NO'
DO H = 1 TO HIST.0
   HISTRCD = HIST.H
   UPPER HISTRCD
   VLOC = POS(VOLKEY,HISTRCD)
   IF VLOC ¬= 0 THEN DO
      BUSTATUS = WORD(HISTRCD,9)
      IF BUSTATUS = 'SSSS' THEN DO
         HIST_BUTYPE = WORD(HISTRCD,5)
         IF HIST_BUTYPE = 'INCR' THEN DO
            N = H + 1
            HISTRCD2 = HIST.N
            UPPER HISTRCD2
            HIST_BUDSN = WORD(HISTRCD2,2)
            HIST_BUDSN = STRIP(HIST_BUDSN,"B","'")
            DO D = 1 TO INCRCNT
               IF HIST_BUDSN = INCR.D THEN DO
                  CALL QUEUE_HISTORY_RECORDS
                  LEAVE
               END
            END
         END
         IF HIST_BUTYPE = 'FULL' THEN DO
            N = H + 1
            HISTRCD2 = HIST.N
            UPPER HISTRCD2
            HIST_BUDSN = WORD(HISTRCD2,2)
            HIST_BUDSN = STRIP(HIST_BUDSN,"B","'")
            DO F = 1 TO FULLCNT
               IF HIST_BUDSN = FULL.F THEN DO
                  CALL QUEUE_HISTORY_RECORDS
                  LEAVE
               END
            END
         END
      END
   END
END
RETURN

/*     */
QUEUE_HISTORY_RECORDS:
QUESW = 'YES'
W = H - 1
DO 5
   W = W + 1
   QUEUE HIST.W
END
RETURN

/*     */
REWRITE_HISTORY_DATASET:
IF ALLOCSW = 'YES' THEN RETURN
"DELETE   ('"HSTDSN"')    NONVSAM SCRATCH PURGE"
"ALLOC DA('"HSTDSN"') LIKE('SYSS.NETCA.MODEL.HIST') NEW SPACE(1,1) TRACKS
BLKSIZE(3120)"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** ALLOCATE ERROR: ('HSTDSN'). ***'
   SAY '*** HISTORY MAINTENANCE CLEANUP NOT PERFORMED FOR ('SERVOL'). ***'
   SAY '*** CONTACT TECHNICAL SUPPORT CRITICAL CALL LIST PERSONNEL. ***'
   EXIT 8
END
"ALLOC DA('"HSTDSN"') FILE(NWDD) SHR"
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW NWDD (FINIS"
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR RE-WRITING ('HSTDSN'). ***'
   SAY '*** HISTORY MAINTENANCE CLEANUP NOT PERFORMED FOR ('SERVOL'). ***'
   SAY '*** CONTACT TECHNICAL SUPPORT CRITICAL CALL LIST PERSONNEL. ***'
   EXIT 8
END
"FREE DA('"HSTDSN"')"
RETURN
