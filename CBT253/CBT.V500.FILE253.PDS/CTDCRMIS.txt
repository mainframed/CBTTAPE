PROC 0 DEBUG   STEPLIB(SYS1.IOA.V410.LOAD) LANG() +
         MISDEF(SYSM.CTDI.V310.PARM) +
         BKPMIS(SYSS.CTDO.V310.BKPMIS) +
         RSTMIS(SYSS.CTDO.V310.RSTMIS) +
         BKPSKL(SYSS.CTDO.V310.BKPSKL)
  CONTROL NOLIST NOCONLIST NOSYMLIST NOMSG FLUSH
IF &DEBUG=DEBUG THEN +
    CONTROL LIST CONLIST SYMLIST MSG FLUSH
FREE  DD(SYSPRINT)
FREE  DD(DAMISI)
FREE  DD(DASKL)
FREE  DD(DABKPO)
FREE  DD(DARSTO)
ALLOC DD(DAMISI) DSN('&MISDEF') SH
ALLOC DD(DASKL) DSN('&BKPSKL') SH
ALLOC DD(DABKPO) DSN('&BKPMIS') SH
ALLOC DD(DARSTO) DSN('&RSTMIS') SH
ALLOC DD(SYSPRINT) DA(*)
ISPEXEC CONTROL ERRORS RETURN
REDISP: +
DO WHILE &RESP ¬= END
   SET &MEM = &STR()
   ISPEXEC DISPLAY PANEL(CTDPCRMS) MSG(&MSG) CURSOR(&CUR)
   SET &RC = &LASTCC
   SET NNNN = &STR()
   SET MSG  = &STR()
   IF &RC = 8 THEN EXIT
   ELSE +
     IF &RC ¬= 0 THEN +
        DO
           WRITE UNABLE TO LOCATE PANEL CTDPCRMS (  RC=&RC  )
           EXIT
        END
     ELSE
       /*---------- CHECK AND EDIT BACKUP REQUEST  --------------*/
       /*                                                        */
       IF &BKR = BKP THEN +
             DO
               SET LEN = 4 - &LENGTH(&STR(&RETP))
                 DO WHILE &LEN > 0
                   SET NNNN = &STR(&NNNN.0)
                   SET LEN = &LEN - 1
                 END
               SET &RETP = &STR(&NNNN&RETP)
               SET &INT  = &STR(000)
               IF  &LENGTH(&STR(&MEM)) = 0  THEN +
                     SET &MEM = &STR(&BKR&RETP.D)
             END
       /*---------- CHECK AND EDIT RESTORE REQUEST  --------------*/
       /*                                                         */
       ELSE +
         DO
              IF &LENGTH(&STR(&INT)) = 0 AND +
                 &LENGTH(&STR(&MEM)) = 0 THEN +
                   DO
                     SET &MSG = CTDPC332
                     SET &CUR = MEM
                     GOTO ENDLOOP
                   END
              ELSE +
                   DO
                    SET LEN = 3 - &LENGTH(&STR(&INT))
                      DO WHILE &LEN>0
                        SET NNNN = &STR(&NNNN.0)
                        SET LEN = &LEN - 1
                      END
                     SET &INT = &STR(&NNNN&INT)
                     SET &RETP= &STR(0000)
                     IF  &LENGTH(&STR(&MEM)) = 0  THEN +
                           SET &MEM = &STR(&BKR&INT.M)
                   END
         END
     CALL  '&STEPLIB&LANG(CTDCBK)' '&BKR &RETP &INT &MEM'
ENDLOOP: END
EXIT: +
 END
