/*  CA-LIBRARIAN LIB/CCF/ELIPS LIBRARIAN ASSEMBLE/COMPILE REXX
    EXEC INTERFACE TO ISPF PANEL DIALOG - ( CICS PROGRAMS- COBOL2 ).
    ASSEMBLE OR COMPILE FROM CA-LIBRARIAN MASTER DATASETS.            */

TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
MAPONLY = 'Y'
INTRTST = 'Y'
QACOMP = 'N'
ACCTSW = 'NO'
EMERGSW = 'NO'
X = MSG("OFF")
ACCTCCF = 5200010520000000
TSOID = SYSVAR(SYSUID)
JCL.1 = 'CCFASM'
JCL.2 = 'CCFBMS'
JCL.3 = 'CCFCICS2'
JCL.4 = 'CCFDMS'
JCL.5 = 'CCFCI2CO'
JCL.6 = 'CCFTCIC2'
JCL.7 = 'CCFBMSMO'
JCL.8 = 'CCFICIC2'
JCL.9 = 'CCFCICOP'
JCL.10 = 'CCFCICOI'
JCL.11 = 'CCFCICOQ'
JCL.12 = 'CCFCICR1'
JCL.13 = 'CCFCICR2'
C1 = 1
C2 = 2
C3 = 4
C4 = 5
COPYLIB.1 = 'SYS1.TESTCOPY.MASTER'
COPYLIB.2 = 'SYS1.TURNCOPY.MASTER'
COPYLIB.3 = 'SYS1.RJCTCOPY.MASTER'
COPYLIB.4 = 'SYS1.QACOPY.MASTER'
COPYLIB.5 = 'SYS1.PRODCOPY.MASTER'
COPYLIB.6 = 'SYS1.EMRGCOPY.MASTER'
CPYMAST = 'SYS1.TESTCOPY.MASTER'
EMRGID = SUBSTR(TSOID,1,6)
IF EMRGID = 'TECH29' THEN DO
   EMERGSW = 'YES'
   C1 = 6
   C2 = 5
   C3 = 4
   C4 = 2
   CPYMAST = 'SYS1.EMRGCOPY.MASTER'
   CCFLOAD = 'SYS1.EMERGNCY.CICS.LOADLIB'
END
ADDRESS ISPEXEC
"CONTROL ERRORS RETURN"
"FTCLOSE"
"VGET (ACCTCDE) PROFILE"
IF RC ¬= 0 THEN DO
   ACCTSW = 'YES'
   ACCTCDE = ACCTCCF
END
"VGET (CCFLOAD) PROFILE"
IF RC ¬= 0 THEN CCFLOAD = 'SYS1.XXXXX.CICS.LOADLIB'
/*
        M A I N   R O U T I N E   S E C T I O N      */
/*     */
DISPLAY_PANEL:
DO FOREVER
   ADDRESS ISPEXEC "DISPLAY PANEL($CCFCPN1)"
   SRC = RC
   IF SRC = 8 THEN LEAVE
   IF SRC > 8 THEN DO
      SAY '*** ERROR INVOKING ISPF DIALOG PANEL $CCFCPN1 - RC = 'SRC'.***'
      EXIT SRC
   END
   MVSSYS = 'SYSA'
   MASTER = LIBPRJ||'.'||LIBGRP||'.'||LIBTYP
   CALL CHECK_MASTER_MEMBER
   IF MODSW = 'NO' THEN ITERATE
   CALL CHECK_OPTIMIZE_FLAG
   CALL BUILD_COPYBOOK_SEARCH_CHAIN
   CALL ASSIGN_SKELETON_JCL
   CALL ASSIGN_SKELETON_VARIABLES
   IF SKELSW = 'ERROR' THEN ITERATE
   CALL SUBMIT_COMPILE_JCL
   MEMBER = ''
   COMPTYP = ''
END
ADDRESS ISPEXEC
IF ACCTSW = 'YES' THEN "VPUT (ACCTCDE) PROFILE"
IF EMERGSW = 'NO' THEN "VPUT (CCFLOAD) PROFILE"
ADDRESS TSO
"FREE F("LIBDD")"
"FREE F("MASTDD")"
EXIT 0
/*
         S U B R O U T I N E   S E C T I O N     */
/*     */
CHECK_MASTER_MEMBER:
ADDRESS TSO
MODSW = 'NO'
LIBDSN = "'"MASTER"("MEMBER")'"
LIBDD = LIBALLOC(LIBDSN)
IF LIBDD = "ERROR" THEN DO
   SAY '*** ALLOCATION ERROR: ('MASTER'('MEMBER'). ***'
   SAY '*** DYNAMIC ALLOCATION FAILED FOR THE MASTER DATASET - RETRY TASK. ***'
   SAY '*** COMPC COMPILE SUBTASK CANCELLED. ***'
   EXIT
END
MASTDD = LIBALLOC("'"MASTER"'")
MASTMEM = XLIBEMEM(MASTDD,MEMBER)
"EXECIO 0 DISKR" MASTDD "(FINIS"
IF MASTMEM = ""  THEN DO
   "FREE F("LIBDD")"
   "FREE F("MASTDD")"
   ADDRESS ISPEXEC
   ZERRMSG = $CCFA007
   "SETMSG MSG($CCFA007)"
   RETURN
END
MODSW = 'YES'
RETURN
/*     */
CHECK_OPTIMIZE_FLAG:
ADDRESS TSO
OPTIMIZE_SW = 'NO'
"EXECIO * DISKR" LIBDD "(STEM DATA. FINIS"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('MASTER'('MEMBER'). ***'
   SAY '*** UNABLE TO READ/CHECK LIBRARIAN MASTER MEMBER - RETRY TASK. ***'
   SAY '*** COMPC COMPILE SUBTASK CANCELLED. ***'
   EXIT
END
DO I = 1 TO DATA.0
   OPTFLAG = POS('OPT2-FLAG',DATA.I)
   IF OPTFLAG ¬= 0 THEN DO
      OPTIMIZE_SW = 'YES'
      LEAVE
   END
END
RETURN
/*     */
BUILD_COPYBOOK_SEARCH_CHAIN:
COPYLB1 = COPYLIB.C1
COPYLB2 = COPYLIB.C2
COPYLB3 = COPYLIB.C3
COPYLB4 = COPYLIB.C4
RETURN
/*     */
ASSIGN_SKELETON_JCL:
CTYP = COMPTYP
IF COMPTYP = 3 & QACOMP = 'Y' THEN DO
   IF OPTIMIZE_SW = 'YES' THEN CTYP = 11
   SKELJCL = JCL.CTYP
   RETURN
END
IF COMPTYP = 2 & MAPONLY = 'Y' THEN CTYP = 7
IF COMPTYP = 3 & INTRTST = 'Y' & OPTIMIZE_SW = 'NO' THEN CTYP = 8
IF COMPTYP = 3 & INTRTST = 'N' & OPTIMIZE_SW = 'YES' THEN CTYP = 9
IF COMPTYP = 3 & INTRTST = 'Y' & OPTIMIZE_SW = 'YES' THEN CTYP = 10
SKELJCL = JCL.CTYP
RETURN
/*     */
ASSIGN_SKELETON_VARIABLES:
SKELSW = 'OK'
LOADLIB = CCFLOAD
IF EMERGSW = 'YES' THEN DO
   MASTER = 'SYS1.EMRGSRCE.MASTER'
   LOADLIB = 'SYS1.EMERGNCY.CICS.LOADLIB'
END
IF LOADLIB = 'SYS1.EMERGNCY.CICS.LOADLIB' THEN DO
   IF MASTER ¬= 'SYS1.EMRGSRCE.MASTER' THEN DO
      SKELSW = 'ERROR'
      EMRGMAST = 'SYS1.EMRGSRCE.MASTER'
      ZERRMSG = $CCFA100
      ADDRESS ISPEXEC "SETMSG MSG($CCFA100)"
      RETURN
   END
END
IF LIBGRP = 'RJCTSRCE' THEN DO
   CPYMAST = 'SYS1.RJCTCOPY.MASTER'
   LOADLIB = 'SYS1.REJECT.CICS.LOADLIB'
   IF COMPTYP = 3 THEN DO
      CTYP = 12
      IF OPTIMIZE_SW = 'YES' THEN CTYP = 13
   END
   SKELJCL = JCL.CTYP
END
SELECT
   WHEN SKELJCL = 'CCFASM' THEN COMPMSG = TSOID||'.BAL.ASSEM'
   WHEN SKELJCL = 'CCFBMS' THEN COMPMSG = TSOID||'.BMSMAP.ASSEM'
   WHEN SKELJCL = 'CCFBMSMO' THEN COMPMSG = TSOID||'.BMSMAP.ONLY'
   WHEN SKELJCL = 'CCFCICS2' THEN COMPMSG = TSOID||'.CICS.COBII'
   WHEN SKELJCL = 'CCFCI2CO' THEN COMPMSG = TSOID||'.CICS.COMPILE'
   WHEN SKELJCL = 'CCFICIC2' THEN COMPMSG = TSOID||'.CICSCOB2.INT'
   WHEN SKELJCL = 'CCFDMS' THEN COMPMSG = TSOID||'.DMSMAP.ASSEM'
   WHEN SKELJCL = 'CCFTCIC2' THEN COMPMSG = TSOID||'.CICSCB2.TRN'
   WHEN SKELJCL = 'CCFCICOP' THEN COMPMSG = TSOID||'.CICS.CMP.OPT'
   WHEN SKELJCL = 'CCFCICOI' THEN COMPMSG = TSOID||'.CICS.INT.OPT'
  OTHERWISE COMPMSG = TSOID||'.COMPILE.LINK'
END
RETURN
/*     */
SUBMIT_COMPILE_JCL:
ADDRESS ISPEXEC "FTOPEN TEMP"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA001
   ADDRESS ISPEXEC "SETMSG MSG($CCFA001)"
   RETURN
   END
ADDRESS ISPEXEC "FTINCL $CCFJOBC"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA002
   ADDRESS ISPEXEC "SETMSG MSG($CCFA002)"
   RETURN
   END
ADDRESS ISPEXEC "FTINCL" SKELJCL
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA003
   ADDRESS ISPEXEC "SETMSG MSG($CCFA003)"
   RETURN
   END
ADDRESS ISPEXEC "FTCLOSE"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA005
   RETURN
   END
ADDRESS ISPEXEC "VGET ZTEMPF"
ADDRESS TSO "SUBMIT '"ZTEMPF"'"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $CCFA006
   RETURN
   END
ZERRMSG = $CCFA000
ADDRESS ISPEXEC "SETMSG MSG($CCFA000)"
RETURN
