/* REXX DASD PULL ANALYSIS */
TRUE = "1";
FALSE = "0";
PULLCTR=1;
"EXECIO * DISKR SUMM (FINIS STEM VOL.";
LINES = VOL.0/2; /* HALVE THE REPORT LINES */
VOLLIST = "";
DO I = 1 TO LINES;
   PARSE VAR VOL.I VOLSER . ;
   IF VOLSER="******" THEN ITERATE;
   ROOT1 = SUBSTR(VOLSER,1,5);
   IF ROOT1="SYSDA" | ROOT1="SYSTA" THEN ITERATE;
   ROOT2 = SUBSTR(VOLSER,1,3);
   IF ROOT2="DRV" | ROOT2="TEM" | ROOT2="LAN" THEN ITERATE;
   VOLLIST = VOLLIST VOLSER;
END;
DROP VOL.;
SYSIN.1 = " P CATLG";
"EXECIO * DISKW SYSIN (FINIS STEM SYSIN.";
DROP SYSIN.;
"CALL 'SYS1.FDR.V5R2M9.LOADLIB(FDRABRP)'";
"EXECIO * DISKR ABRMAP (FINIS STEM CAT.";
VOL_FOUND=FALSE;
DO I = 1 TO CAT.0;
   IF SUBSTR(CAT.I,51,8)¬="FDRABR.V" THEN ITERATE I;
   ISVOL=SUBSTR(CAT.I,13,6);
   ISGEN=SUBSTR(CAT.I,23,2);
   LINE = STRIP(SPACE(CAT.I));
   IF ISVOL¬=" " THEN DO;
       PARSE VAR LINE VOLSER GEN CYCLE TYPE DATE DSNAME ,
                   COPY FILE TVOL;
       VOL_FOUND=TRUE;
       END;
   ELSE IF ISGEN¬=" " THEN DO; /* IGNORE OLDER GENS */
       VOL_FOUND=FALSE;
       ITERATE I;
       END;
   ELSE IF VOL_FOUND THEN ,
       PARSE VAR LINE CYCLE TYPE DATE DSNAME ,
                   COPY FILE TVOL;
   T1=I+1; T2=I+2;
   TEMP1=STRIP(SPACE(CAT.T1));
   TEMP2=STRIP(SPACE(CAT.T2));
   IF TEMP1¬="" & TEMP1¬=" " & WORDS(TEMP1)=1 THEN ,
        TVOL=TVOL","WORD(TEMP1,1);
   IF TEMP2¬="" & TEMP2¬=" " & WORDS(TEMP2)=1 THEN ,
        TVOL=TVOL","WORD(TEMP2,1);
   IF WORDPOS(VOLSER,VOLLIST)¬=0 THEN DO;
   TVOL = TRANSLATE(TVOL," ",",");
   JEND = WORDS(TVOL); /* MULTIVOL */
   DO J = 1 TO JEND;
   TTVOL = WORD(TVOL,J);
PULLOUT.PULLCTR=DATE LEFT(TTVOL,6) RIGHT(FILE,4,"0") DSNAME GEN CYCLE;
   PULLCTR=PULLCTR+1;
   END;
   END;
END;
"EXECIO * DISKW PULLLIST (FINIS STEM PULLOUT.";
EXIT 0;
