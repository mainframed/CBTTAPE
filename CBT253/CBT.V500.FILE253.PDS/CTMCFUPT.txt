PROC 0 OLPREFM(SYSS.CTMO) OLVERM(V410) DEBUG +
       PROMPT(PROMPT)
/*******************************************************/
/**  P.P.F. - PARAMETERS PROMPTING FACILITY - TYPE1   **/
/**                                                   **/
/**  THE CLIST DISPLAYS A SCREEN THAT GETS THE NAME   **/
/**  OF THE TABLE TO WORK ON.                         **/
/**  THE TABLE IS DISPLAYED SO CONDITIONS AND         **/
/**  PARAMETERS ARE DECLARED TO IT.                   **/
/**                                                   **/
/*******************************************************/
 ISPEXEC CONTROL ERRORS RETURN
 IF &DEBUG = DEBUG +
 THEN +
    CONTROL LIST MSG CONLIST SYMLIST NOCAPS
 ELSE +
    CONTROL NOLIST NOMSG NOCAPS
 SET &T = T
 ISPEXEC VGET ( TBPREF ) PROFILE
 ISPEXEC VGET ( MANULIB ) PROFILE
 SET &RC = &LASTCC
 IF &RC = 8  -
 THEN -
    SET &MANULIB = &STR(&OLPREFM..&OLVERM..&PROMPT)
 REDISP: +
 SET &CHGIND = 0
 ISPEXEC DISPLAY PANEL(CTMPP03W)
 SET &RC = &LASTCC
 IF &RC ¬= 0 -
 THEN -
    IF &RC ¬= 8 -
    THEN -
       DO
          ISPEXEC SETMSG MSG(CTMP000Z)
          GOTO END
       END
    ELSE -
       GOTO END
 IF &TBPREF ¬= &STR() -
 THEN -
    DO
      SET &ISMSTR = 1
      SET &TABNAME  = &STR(&TBPREF.&STR(TMSTR))
    END
 ELSE -
    DO
      ISPEXEC SETMSG MSG(CTMP003A)
      GOTO REDISP
    END

 IF &MANULIB = &STR() -
 THEN -
    DO
      SET &TBLIB = &STR(ISPTLIB)
      ISPEXEC LMINIT DATAID(FROMID) DDNAME(&TBLIB) ENQ(SHR)
      SET &RC = &LASTCC
      IF &RC ¬= 0  -
      THEN -
         DO
           ISPEXEC SETMSG MSG(CTMP000Z)
           GOTO REDISP
         END
    END
 ELSE -
    DO
      SET &TBLIB = &MANULIB
      ISPEXEC LMINIT DATAID(FROMID) DATASET('&MANULIB') ENQ(SHR)
      SET &RC = &LASTCC
      IF &RC ¬= 0  -
      THEN -
         DO
           ISPEXEC SETMSG MSG(CTMP000Z)
           GOTO REDISP
         END
    END
 ISPEXEC LMOPEN DATAID(&FROMID) OPTION(INPUT)
 SET &RC = &LASTCC
 IF &RC ¬= 0  -
 THEN -
    DO
      ISPEXEC SETMSG MSG(CTMP000Z)
      GOTO REDISP
 END
 ISPEXEC LMMFIND DATAID(&FROMID) MEMBER(&TABNAME)
 SET &RC = &LASTCC
 IF &RC ¬= 0  -
 THEN -
    IF &RC > 8   -
    THEN -
       DO
          ISPEXEC SETMSG MSG(CTMP000Z)
          GOTO REDISP
       END
    ELSE -
       DO
          IF &ISMSTR = 1 -
          THEN +
             DO
               /* MSG TO CREATE A NEW MASTER TABLE */
                ISPEXEC SETMSG MSG(CTMP003J)
                ISPEXEC VPUT (MANULIB TBPREF)
                %CTMCFMST
                ISPEXEC VGET (CREATED TABNAME MANULIB TBPREF)
                IF &CREATED ¬= YES -
                THEN +
                   DO
                      ISPEXEC LMFREE DATAID(&FROMID)
                      GOTO REDISP
                   END
             END
          ELSE +
             DO
                ISPEXEC SETMSG MSG(CTMP003B)
                GOTO REDISP
             END
       END
 IF &MANULIB ¬= &STR() -
 THEN   -
    DO
        SET &IND = 1
 INITRY: SET &TBFILE = &STR(TBFILE&IND)
       ISPEXEC LMINIT DATAID(TRYID) DDNAME (&TBFILE) ENQ(SHR)
       SET &RC = &LASTCC
       IF &RC = 0 -
       THEN -
          DO
            ISPEXEC LMFREE (&TRYID)
            SET &DSNTRY = DSNTRY&IND
            SET &DSNTR  = &&DSNTRY&IND
            ISPEXEC VGET ( &DSNTRY ) SHARED
            SET DSNTT = &SYSNSUB(2,&DSNTR)
            IF &DSNTT = &MANULIB -
            THEN -
               GOTO CONT
            ELSE -
               DO
                 SET &IND = &IND + 1
                 GOTO INITRY
               END
          END
       IF &IND > 99 -
       THEN -
          SET &IND = 1
       SET &TBFILE = &STR(TBFILE&IND)
       SET &DSNTRY = DSNTRY&IND
       SET &&DSNTRY = &MANULIB
       ISPEXEC VPUT (&DSNTRY) SHARED
       FREE FILE(&TBFILE)
       ALLOC FILE(&TBFILE) DA('&MANULIB') SHR
       SET &RC = &LASTCC
       IF &RC ¬= 0 -
       THEN -
          DO
              SET &DDERR = &TBFILE
              SET &DSNERR = &MANULIB
              ISPEXEC SETMSG MSG(CTMP004L)
              GOTO END
          END
 CONT: SET &TBDD = &STR(&TBFILE)
       SET &TBLIB = &STR(&MANULIB)
    END
 IF &MANULIB = &STR() -
 THEN   -
    DO
      SET &TBDD = &STR(ISPTABL)
      SET &TBLIB = &STR(ISPTABL)
    END

 ISPEXEC VGET (ZUSER)
 SET TMPMANU = &STR(&ZUSER.&STR(TMP))
 IF &LENGTH(&TMPMANU) > 8 -
 THEN -
    SET &TMPMANU = &SUBSTR(1:8,&TMPMANU)
 ISPEXEC TBEND &TMPMANU
 ISPEXEC TBCREATE &TMPMANU +
        NAMES (CONDNAME PARMIN PARMOUT DESC UPDATEDD +
           UPDATEMM INDIC ) WRITE REPLACE
 SET RC=&LASTCC
 IF &RC GT 4 -
 THEN -
    IF &RC > 12 -
    THEN -
       DO
         ISPEXEC SETMSG MSG(CTMP000Z)
         GOTO END
       END
    ELSE -
       DO
         ISPEXEC SETMSG MSG(CTMP003I)
         GOTO END
       END
 ISPEXEC TBOPEN &TABNAME WRITE LIBRARY(&TBFILE)
 SET &RETC = &LASTCC
 IF &RETC GT 0 -
 THEN -
    IF &RETC > 12 -
    THEN -
       DO
         ISPEXEC SETMSG MSG (CTMP000Z)
         GOTO END
       END
    ELSE -
       DO
         ISPEXEC SETMSG MSG (CTMP003I)
         GOTO END
       END
LOOPSKIP: +
 ISPEXEC TBSKIP &TABNAME
 IF &LASTCC = 8 -
 THEN -
    GOTO DISP0
 ISPEXEC TBADD &TMPMANU
 GOTO LOOPSKIP
DISP0: +
 ISPEXEC TBTOP &TMPMANU
DISP01: +
 ISPEXEC TBVCLEAR &TMPMANU
 SET &PARMIN = &STR(&PREFIX.&STR(*))
 ISPEXEC TBSARG &TMPMANU
 SET &RC = &LASTCC
 IF &RC = 0 -
 THEN +
    GOTO DISPPAN1
 ELSE +
    DO
      SET &SPFRC = &RC
      ISPEXEC SETMSG MSG(CTMP004F)
      GOTO DISPPAN1
    END
 /* SET QCRP = 0
 SET CURNAME = TCMD
DISPPAN1: +
 ISPEXEC TBDISPL &TMPMANU PANEL(CTMPP31W)
 SET RC = &LASTCC
CANC0: +
 IF &TCMD = CANCEL OR &TCMD =cancel +
 THEN +
    GOTO ENDCAN
 IF &RC GT 8 -
 THEN -
    DO
       ISPEXEC SETMSG MSG(CTMP000Z)
       GOTO END
    END
ED2: +
 SET RC1 = &RC
 SET QCRP = 0
 SET CURNAME = TCMD
 IF &TSEL NE D AND &TSEL NE I AND &TSEL NE A AND +
           &TSEL NE R AND &TSEL NE &STR( ) AND +
    &TSEL NE d AND &TSEL NE i AND &TSEL NE a AND +
           &TSEL NE r                          +
 THEN -
    DO
       ISPEXEC SETMSG MSG(CTMP003C)
       GOTO ED4
    END
 IF &TSEL = D OR &TSEL = d -
 THEN -
    DO
       SET &CHGIND = 1                             /* WM1382 */
       ISPEXEC TBDELETE &TMPMANU
       GOTO ED4
    END
 IF &TSEL = A OR &TSEL = R OR &TSEL = a OR &TSEL = r -
 THEN -
    DO
       SET &CHGIND = 1                         /* WM1382 */
       ISPEXEC TBPUT &TMPMANU
       IF &LASTCC GT 8 -
       THEN -
          DO
             SET &RETC = &LASTCC
             ISPEXEC SETMSG MSG(CTMP003E)
             GOTO DISPPAN1
          END
       SET UPDATEDD =
       SET UPDATEMM =
       ISPEXEC TBADD &TMPMANU
       IF &LASTCC GT 0 -
       THEN -
          DO
             SET &RETC = &LASTCC
             ISPEXEC SETMSG MSG(CTMP003D)
             GOTO DISPPAN1
          END
       GOTO ED4
    END
 IF &TSEL = I OR &TSEL = i -
 THEN -
    DO
       SET &CHGIND = 1                       /* WM1382 */
       ISPEXEC TBPUT &TMPMANU
       IF &LASTCC GT 8 -
       THEN -
          DO
             SET &RETC = &LASTCC
             ISPEXEC SETMSG MSG(CTMP003E)
             GOTO DISPPAN1
          END
       ISPEXEC TBVCLEAR &TMPMANU
       SET &DAILYM = Y
       ISPEXEC TBQUERY &TMPMANU POSITION(QCRP)
       ISPEXEC TBADD &TMPMANU
       IF &LASTCC GT 0 -
       THEN -
          DO
             SET &RETC = &LASTCC
             ISPEXEC SETMSG MSG(CTMP003D)
          END
       SET QCRP = &QCRP + 1
       GOTO ED4
    END
 IF &ZTDSELS = 0 -
 THEN +
    GOTO CKLINCMD
 IF &TSEL = &STR( ) -
 THEN -
    DO
      SET &CHGIND = 1                        /* WM1382 */
      ISPEXEC TBPUT &TMPMANU
      IF &LASTCC GT 8 -
      THEN -
         DO
            SET &RETC = &LASTCC
            ISPEXEC SETMSG MSG(CTMP003E)
         END
    END
ED4: +
 IF &RC1 = 4  -
 THEN -
    DO
       ISPEXEC TBDISPL &TMPMANU
       SET RC = &LASTCC
       GOTO ED2
    END
 CKLINCMD: +
 IF &TCMD = SAVE OR &TCMD = save +
 THEN +
    DO
       ISPEXEC TBEND &TABNAME
ISPEXEC TBCLOSE &TMPMANU NAME(&TABNAME) LIBRARY(&TBFILE) PAD(20)
       GOTO END
    END
 IF &RESP = END OR &RC1 = 8 -
 THEN -
    DO
  /*   ISPEXEC SETMSG MSG(CTMP003M) */             /* WM1382 */
  /*   GOTO DISPPAN1  */                             /* WM1382 */
       IF &CHGIND = 0 +
       THEN +
          DO                                         /* WM1382 */
            SET &TCMD = CANCEL                       /* WM1382 */
            GOTO CANC0                               /* WM1382 */
          END                                        /* WM1382 */
       ELSE +
       DO                                            /* WM1382 */
         SAVWND: +
         ISPEXEC DISPLAY PANEL(CTMPP37W)           /* WM1382 */
         IF &SV = Y +
         THEN +
            DO                                       /* WM1382 */
              SET &TCMD = SAVE                       /* WM1382 */
              GOTO CKLINCMD                          /* WM1382 */
            END                                      /* WM1382 */
         IF &SV = N +
         THEN +
            DO
              SET &TCMD = CANCEL                     /* WM1382 */
              GOTO CANC0                             /* WM1382 */
            END                                      /* WM1382 */
         ISPEXEC SETMSG MSG(CTMP005C)              /* WM1382 */
         GOTO SAVWND                                 /* WM1382 */
       END                                           /* WM1382 */
    END
 IF &LENGTH(&TCMD) > 2 -
 THEN -
    DO
      SET &CMDPREF = &SUBSTR(1:2,&TCMD)
      IF &CMDPREF = L OR &CMDPREF = l -
      THEN -
         DO
            SET &LOCARG = &SUBSTR(3:&LENGTH(&TCMD),&TCMD)
            SET &LOCARG = &STR(&LOCARG.&STR(*))
            SET &PARMIN = &STR(&LOCARG)
            ISPEXEC TBSCAN &TMPMANU ARGLIST(PARMIN) NOREAD
            SET &RC = &LASTCC
            IF &RC = 0 -
            THEN +
               GOTO DISPPAN1
            ELSE +
               IF &RC = 8 +
               THEN +
                  DO
                    ISPEXEC SETMSG MSG(CTMP004C)
                    GOTO REPOSTAB
                  END
               ELSE +
                  DO
                    ISPEXEC SETMSG MSG(CTMP000Z)
                    GOTO REPOSTAB
                  END
         END
      ELSE -
         IF &TCMD = INSERT OR &TCMD = insert  -
         THEN -
            DO
               ISPEXEC TBVCLEAR &TMPMANU
               SET &DAILYM = Y
               ISPEXEC TBQUERY &TMPMANU POSITION(QCRP)
               ISPEXEC TBADD &TMPMANU
               IF &LASTCC GT 0 -
               THEN -
                  DO
                     SET &RETC = &LASTCC
                     ISPEXEC SETMSG MSG(CTMP003D)
                  END
               SET QCRP = &QCRP + 1
               GOTO DISPPAN1
            END
         ELSE +
            DO
               ISPEXEC SETMSG MSG(CTMP004O)
               GOTO DISPPAN1
            END
    END
 ELSE -
    IF &TCMD ¬= &STR() -
    THEN -
       DO
         ISPEXEC SETMSG MSG(CTMP004O)
         GOTO DISPPAN1
       END
 IF &LASTPREF ¬= &PREFIX +
 THEN +
    DO
       SET &LASTPREF = &STR(&PREFIX)
       GOTO DISP0
    END
REPOSTAB: +
 ISPEXEC TBTOP &TMPMANU
 ISPEXEC TBSKIP &TMPMANU NUMBER(&ZTDTOP)
 GOTO DISPPAN1
ENDCAN: +
 ISPEXEC TBEND &TABNAME
 ISPEXEC TBEND &TMPMANU
 IF &CHGIND = 0 +
 THEN +
    GOTO END                                        /* WM1382 */
 ISPEXEC SETMSG MSG(CTMP003F)
END: +
 ISPEXEC VPUT (MANULIB TBPREF) PROFILE
 ISPEXEC VPUT (TBNAME TBLIB) SHARED
 ISPEXEC LMFREE DATAID(&FROMID)
