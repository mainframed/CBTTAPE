PROC 0 DEBUG() DEBUGD() +
       DATEFMT(USA)     +
       USRLIB(SYSS.CTMO.V410.SCHEDULE) +
       USRTBL(@@USRTBL)

IF &DEBUG=DEBUG THEN CONTROL MAIN LIST CONLIST SYMLIST MSG
                ELSE CONTROL MAIN NOLIST NOCONLIST NOSYMLIST NOMSG

ISPEXEC CONTROL ERRORS CANCEL
SET &RC = &LASTCC
IF &RC ¬= 0 THEN EXIT QUIT   /*NOT RUNNING UNDER ISPF  */
SET ACTION=RETURN
ISPEXEC CONTROL ERRORS &ACTION

 /********************************************************/
 /**                                                    **/
 /**    CONTROL-M END USER JOB SELECTION INTERFACE      **/
 /**                                                    **/
 /**    CLIST CTMJBINT                                  **/
 /**                                                    **/
 /********************************************************/

ISPEXEC VPUT (ZHTOP)

 /********************************************************/
 /**    PROGRAM VARIABLE INITIALIZATION                 **/
 /********************************************************/
SET BLANKS = &STR(                                                   )
   /* NOTE - ABOVE LINE HAS 50 BLANKS - LEAVE IT ALONE */
SET USER   = &SYSUID
SET NOALC = &STR()
SET CTMGRP = '1'
SET FRC = NO

 /********************************************************/
 /**                                                    **/
 /**    ALLOCATE AND OPEN THE FILE CONTAINING           **/
 /**    THE CORRESPONDENCE BETWEEN USERID AND           **/
 /**    SCHEDULING TABLE                                **/
 /**                                                    **/
 /********************************************************/

ISPEXEC LMINIT  DATAID(CTMPJIN) DATASET('&USRLIB') ENQ(SHR)
SET &RC = &LASTCC
IF &RC ¬= 0 THEN +
   DO
      ISPEXEC SETMSG MSG(CTMJ005A)
      EXIT QUIT
   END

ISPEXEC LMOPEN  DATAID(&CTMPJIN) OPTION(INPUT)
SET &RC = &LASTCC
IF &RC ¬= 0 THEN +
   DO
      ISPEXEC SETMSG MSG(CTMJ006A)
      EXIT QUIT
   END

ISPEXEC LMMFIND DATAID(&CTMPJIN) MEMBER(&USRTBL)
SET &RC = &LASTCC
IF &RC ¬= 0 THEN +
      DO
      ISPEXEC SETMSG MSG(CTMJ007A)
      EXIT QUIT
   END

 /********************************************************/
 /**    FIND THE RECORD CORRESPONDING TO THE USER       **/
 /********************************************************/

SET USERFND = N   /* DIDNT FIND USER YET */
SET EOF     = N   /* NOT AT EOF ON THE INPUT MEMBER */

DO WHILE ((&EOF=N) AND (&USERFND=N))
ISPEXEC LMGET   DATAID(&CTMPJIN) MODE(INVAR) DATALEN(CARDLEN) +
                      DATALOC(CARD) MAXLEN(80)
   SET &RC = &LASTCC
   IF &RC ¬= 0 THEN +
     IF &RC = 8 THEN SET EOF=Y
        ELSE +
      DO
         ISPEXEC SETMSG MSG(CTMJ000A)
         EXIT QUIT
      END

    IF (&EOF=N) THEN +
      IF Z&SUBSTR(1:1,&NRSTR(&CARD)&STR(&BLANKS))¬=Z&STR(*) THEN +
       DO
          IF &SUBSTR(1:8,&NRSTR(&CARD)&STR(&BLANKS))=&USER +
               THEN SET USERFND = Y
          ELSE +
             DO
               SET I = 0
               SET STAR = 0
               DO WHILE ((&I<8) AND (&STAR=0))
                 SET I = &I + 1
                 SET CHAR = &SUBSTR(&I:&I,&NRSTR(&CARD)&STR(&BLANKS))
                 IF Z&STR(&CHAR)=Z&STR(*) THEN SET STAR = &I
               END
               IF &STAR=1 THEN +
                 DO
                   SET USERFND = Y
                   SET STAR = 0
                 END
               IF &STAR>0 THEN +
                 DO
                    IF &SUBSTR(1:&STAR-1,&NRSTR(&CARD))=&SUBSTR( +
                         1:&STAR-1,&NRSTR(&USER)&STR(&BLANKS)) THEN +
                    SET USERFND = Y
                 END
             END
       END
END      /* OF THE LOOP READING RECORDS FROM MEMBER */

ISPEXEC LMCLOSE DATAID(&CTMPJIN)
ISPEXEC LMFREE  DATAID(&CTMPJIN)

IF &USERFND=N THEN +
   DO
      ISPEXEC SETMSG MSG(CTMJ008A)
      EXIT  QUIT
   END

 /********************************************************/
 /**    EXTRACT LIBRARY AND MEMBER NAME                 **/
 /********************************************************/

SET MEM =&SUBSTR(10:17,&NRSTR(&CARD)&STR(&BLANKS))
SET LIB =&SUBSTR(20:63,&NRSTR(&CARD)&STR(&BLANKS))
SET LIB =&LIB
IF Z&LIB=Z THEN SET LIB = &USRLIB       /* LIB WAS BLANK ON CARD */

 /********************************************************/
 /**    ALLOCATE AND OPEN THE SCHEDULE TABLE FILE       **/
 /********************************************************/

ISPEXEC LMINIT  DATAID(CTMPJIN) DATASET('&LIB') ENQ(SHR)
SET &RC = &LASTCC
IF &RC ¬= 0 THEN +
   DO
      ISPEXEC SETMSG MSG(CTMJ005A)
      EXIT QUIT
   END

ISPEXEC LMOPEN  DATAID(&CTMPJIN) OPTION(INPUT)
SET &RC = &LASTCC
IF &RC ¬= 0 THEN +
   DO
      ISPEXEC SETMSG MSG(CTMJ006A)
      EXIT QUIT
   END

ISPEXEC LMMFIND DATAID(&CTMPJIN) MEMBER(&MEM)
SET &RC = &LASTCC
IF &RC ¬= 0 THEN +
      DO
      ISPEXEC SETMSG MSG(CTMJ008A)
      EXIT QUIT
   END

 /********************************************************/
 /**    CREATE A CLEAN JOBNAME TABLE                    **/
 /********************************************************/

ISPEXEC CONTROL ERRORS RETURN

ISPEXEC TBQUERY CTMPJT00
SET &RC = &LASTCC
IF &RC = 0 THEN  +
   ISPEXEC TBCLOSE CTMPJT00

ISPEXEC CONTROL ERRORS &ACTION

ISPEXEC TBCREATE CTMPJT00 NAMES(OPER  JNAM DESC) NOWRITE
ISPEXEC TBVCLEAR CTMPJT00

 /********************************************************/
 /**    READ THE MEMBER AND FILL THE JOBNAME TABLE      **/
 /********************************************************/

SET EOF=N
DO WHILE (&EOF=N)
ISPEXEC LMGET   DATAID(&CTMPJIN) MODE(INVAR) DATALEN(CARDLEN) +
                      DATALOC(CARD) MAXLEN(80)
   SET &RC = &LASTCC
   IF &RC ¬= 0 THEN +
     IF &RC = 8 THEN SET EOF=Y
        ELSE +
      DO
         ISPEXEC SETMSG MSG(CTMJ000A)
         EXIT QUIT
      END

   IF &EOF=N THEN +
      DO
         SET CARDTYP=&SUBSTR(1,&NRSTR(&CARD)&STR(&BLANKS))
         IF &CARDTYP=H THEN +
             SET DESC=&SUBSTR(2:51,&NRSTR(&CARD)&STR(&BLANKS))
         IF &CARDTYP=M THEN +
            DO
               IF Z&JNAM¬=Z THEN ISPEXEC TBADD CTMPJT00
               ISPEXEC TBVCLEAR CTMPJT00
               SET JNAM=&SUBSTR(2:9,&NRSTR(&CARD)&STR(&BLANKS))
            END
      END

END /*OF THE READ LOOP */

IF Z&JNAM¬=Z  THEN ISPEXEC TBADD CTMPJT00
ISPEXEC LMCLOSE DATAID(&CTMPJIN)
ISPEXEC LMFREE  DATAID(&CTMPJIN)

 /********************************************************/
 /**    NOW PRESENT THE SCREEN TO THE USER              **/
 /********************************************************/
SET MARK1=&STR( ====== >>>>>>>>>>>>>>>>>>>    NO MORE JOBS IN)
SET MARK2=&STR( TABLE     <<<<<<<<<<<<<<<< =====)
SET &ZTDMARK=&STR(&MARK1&MARK2)
SET MEMNAME = &MEM          /* CLEAN UP MEMBER NAME */
SET &ZTDMSG = CTMK002A      /* NO "ROW NN OF NN" */
SET CUR     = 1             /* WHERE THE CURSOR IS */

ISPEXEC TBTOP   CTMPJT00

SET &KILL = N   /* USER HAS NOT ENDED THE CLIST */

DO WHILE &KILL¬=Y /*THAT IS, REPEAT UNTIL USER ENDS IT ALL */

   ISPEXEC TBTOP   CTMPJT00
   ISPEXEC TBSKIP  CTMPJT00 NUMBER(&CUR)
ISPEXEC TBDISPL CTMPJT00 PANEL(CTMPJ00W) AUTOSEL(NO) CSRROW(&CUR)

   SET &NUMROWS = &ZTDROWS
   SET &CHANGED = &ZTDSELS

   ISPEXEC VGET (ZVERB)
   SET &RESP = &ZVERB
   IF Z&RESP = Z THEN  SET &RESP = ENTER
   IF ((&RESP=RETURN) OR (&RESP=END)) THEN SET KILL=Y

   IF ((&KILL=N) AND (&CHANGED>0)) THEN   +
     DO
         ISPEXEC TBPUT  CTMPJT00
         DO WHILE ((&ZTDSELS>1) AND (&KILL=N))

               ISPEXEC TBDISPL CTMPJT00
               ISPEXEC VGET (ZVERB)
               SET &RESP = &ZVERB
               IF Z&RESP = Z THEN  SET &RESP = ENTER
               IF ((&RESP=RETURN) OR (&RESP=END)) THEN SET KILL=Y

               ISPEXEC TBPUT  CTMPJT00
         END /* OF PICKING UP MARKED LINES */

         ISPEXEC TBTOP   CTMPJT00
         SET LOOKROW=1
         DO WHILE ((&LOOKROW<=&NUMROWS) AND (&KILL=N))
           ISPEXEC TBSKIP  CTMPJT00
           IF Z&OPER=ZS THEN +
             DO
                SET CUR=&LOOKROW
                SET &OPER=&STR( )
                ISPEXEC TBPUT CTMPJT00
                SET &M=&MEM    /* CLEAN TRAILING BLANKS */
                SET &J=&JNAM
                SET CTMSLIB = &LIB
                SET CTMSMEM = &M
                SET CTMSJOB = &JNAM
                ISPEXEC VGET (ZMONTH,ZDAY,ZYEAR)
                IF &DATEFMT=WST THEN +
                  DO
                     SET &DTSTR=&STR(DD MM YY)
                     SET D1 = &ZDAY
                     SET D2 = &ZMONTH
                     SET D3 = &ZYEAR
                  END
                IF &DATEFMT=USA THEN +
                  DO
                     SET &DTSTR=&STR(MM DD YY)
                     SET D1 = &ZMONTH
                     SET D2 = &ZDAY
                     SET D3 = &ZYEAR
                  END
                IF &DATEFMT=JPN THEN +
                  DO
                     SET &DTSTR=&STR(YY MM DD)
                     SET D1 = &ZYEAR
                     SET D2 = &ZMONTH
                     SET D3 = &ZDAY
                  END
                SET D1 = &SUBSTR(2:3,&EVAL(&D1+100))
                SET D2 = &SUBSTR(2:3,&EVAL(&D2+100))
                SET D3 = &SUBSTR(2:3,&EVAL(&D3+100))
                SET THIS=Y
                ISPEXEC DISPLAY PANEL(CTMPJ01W)
                ISPEXEC VGET (ZVERB)
                SET &RESP = &ZVERB
                IF Z&RESP = Z THEN  SET &RESP = ENTER
                IF ((&RESP=RETURN) OR (&RESP=END)) THEN SET &THIS=N

                IF &THIS=Y THEN +
                  DO
                    IF &DATEFMT=WST THEN +
                      DO
                         SET DD = &D1
                         SET MM = &D2
                         SET YY = &D3
                      END
                    IF &DATEFMT=USA THEN +
                      DO
                         SET MM = &D1
                         SET DD = &D2
                         SET YY = &D3
                      END
                    IF &DATEFMT=JPN THEN +
                      DO
                         SET YY = &D1
                         SET MM = &D2
                         SET DD = &D3
                      END

                    /*****************************/
                    /*  COMPUTE PREVIOUS DATE    */
                    /*****************************/
                     SET ZYEAR=&YY
                     SET ZMONTH=&MM
                     SET ZDAY=&DD
                     IF &ZDAY GT 1 THEN DO
                         SET DD=&ZDAY-1
                         SET MM=&ZMONTH
                       END
                     ELSE DO
                         IF &ZMONTH GT 1 THEN  SET MM=&ZMONTH-1
                             ELSE DO
                                SET MM=12
                                SET YY=&ZYEAR-1
                             END
                         SET DD=31
                         IF &MM=4 OR &MM=6 OR &MM=9 OR &MM=11 +
                                        THEN SET DD=30
                         IF &MM=2  THEN SET DD=28
                         IF &MM=2 AND (&YY=88 OR &YY=92 OR &YY=96) +
                                        THEN SET DD=29
                          END

                    SET MM=&SUBSTR(2:3,&EVAL(&MM+100))
                    SET DD=&SUBSTR(2:3,&EVAL(&DD+100))
                    SET ZDAY=&SUBSTR(2:3,&EVAL(&ZDAY+100))
                    SET ZMONTH=&SUBSTR(2:3,&EVAL(&ZMONTH+100))

                    IF &DATEFMT=USA THEN +
                         %CTMCDAIL  CTMSLIB(&CTMSLIB) &NOALC +
                                    CTMSMEM(&CTMSMEM) +
                                    CTMSJOB(&CTMSJOB)   +
                                    CTMGRP(&CTMGRP)     +
                                    FORCE(&FRC)         +
                                    &DEBUGD DATE1(&MM&DD&YY) +
                                    DATE2(&ZMONTH&ZDAY&ZYEAR)

                    IF &DATEFMT=WST THEN +
                         %CTMCDAIL  CTMSLIB(&CTMSLIB) &NOALC +
                                    CTMSMEM(&CTMSMEM) +
                                    CTMSJOB(&CTMSJOB)   +
                                    CTMGRP(&CTMGRP)     +
                                    FORCE(&FRC)         +
                                    &DEBUGD DATE1(&DD&MM&YY) +
                                    DATE2(&ZDAY&ZMONTH&ZYEAR)

                    IF &DATEFMT=JPN THEN +
                         %CTMCDAIL  CTMSLIB(&CTMSLIB) &NOALC +
                                    CTMSMEM(&CTMSMEM) +
                                    CTMSJOB(&CTMSJOB)   +
                                    CTMGRP(&CTMGRP)     +
                                    FORCE(&FRC)         +
                                    &DEBUGD DATE1(&YY&MM&DD) +
                                    DATE2(&ZYEAR&ZMONTH&ZDAY)

                    SET &NOALC=NOALC

                  END  /* IF USER ALLOWED IT */
             END   /*  PROCESSING A ROW  */

           SET LOOKROW=&LOOKROW+1
         END /* OF ORDERING JOBS */

     END


END  /* OF MAIN LOOP AT HIGHEST SCREEN LEVEL */
ISPEXEC TBCLOSE CTMPJT00
EXIT
