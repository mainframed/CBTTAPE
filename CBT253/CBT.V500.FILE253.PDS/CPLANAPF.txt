/* REXX Capacity Planning APAF Analysis feed into DB2               */
/* categories for domains */
outptr=1;
domains="SYSP SYST SYSM SA SB SM SYSU SYSV";
groups = ,
   "MVSprod MVStest MVSmaint MVSprod MVSprod MVSmaint UNXprod UNXmaint";
glist="MVSprod MVSprod1 MVSprod2 MVStest MVSmaint UNXprod UNXmaint";
datelist="";
outptr=1;
"EXECIO * DISKR APAFIN (STEM APAF. FINIS";
lastset=""; /* used in indiv domain processing */
do i = 1 to apaf.0;
    parse var apaf.i ansicc 2 apafdata;
    if word(apafdata,4)="_SYSTEM_" then do;
        z=words(lastset); /* prev min data */
        if lastsetÂ¬="" then do;
        do y = 1 to z;
          uname=word(lastset,y);
          if symbol('data.uname.tempdate')="LIT" then do;
             data.uname.tempdate=util.uname;
             end;
           else data.uname.tempdate=data.uname.tempdate util.uname;
        end; /* end y */
        end; /* end if*/
        lastset=""; /* used in indiv domain processing */
        parse var apafdata current_date time . . . . . . . sysutil . ;
        parse var current_date mm "/" dd "/" yy;
        parse var time hh ":" .;
        if hh>=7 & hh<=18 then shift=1;
           else shift=2;
        parse value weekday(mm"/"dd"/19"yy) with day julian;
        tempdate=mm||dd||yy;
        if sysutil="" | sysutil=" " | substr(sysutil,1,1)="*" then ,
             iterate i;
        if symbol('data.SYSTEM.tempdate')="LIT" then do;
             datelist=datelist tempdate;
             data.SYSTEM.tempdate=sysutil;
             end;
         else data.SYSTEM.tempdate=data.SYSTEM.tempdate sysutil;
        systemshift="SYSTEM"||shift;
        if symbol('data.systemshift.tempdate')="LIT" then do;
             data.systemshift.tempdate=sysutil;
             end;
     else data.systemshift.tempdate=data.systemshift.tempdate sysutil;
        end; /* end system line */
    if word(apafdata,4)="L" then do;
        parse var apafdata sysname . . . . . . . . . . sysutil . ;
        if sysutil="" | sysutil=" " | substr(sysutil,1,1)="*" then ,
             iterate i;
        index=wordpos(sysname,domains);
        if index=0 then iterate i;
        sysname=word(groups,index);
        gptr=wordpos(sysname,lastset);
        if gptr=0 then do; /* first entry for that min */
            lastset=lastset sysname;
            util.sysname=sysutil;
            if sysname="MVSprod" then do;
              sysnamesh="MVSprod"||shift;
              lastset=lastset sysnamesh;
              util.sysnamesh=sysutil;
              end;
            end;
          else do;
            util.sysname=util.sysname+sysutil;
            if sysname="MVSprod" then do;
              sysnamesh="MVSprod"||shift;
              util.sysnamesh=util.sysnamesh+sysutil;
              end;
           end;
        end;
end;
call calc "SYSTEM";
call calc "SYSTEM1";
call calc "SYSTEM2";
z = words(glist);
do y = 1 to z;
   callsysname = word(glist,y);
   call calc callsysname;
end;
"EXECIO * DISKW APAFOUT (STEM REPORT. FINIS";
exit 0;
calc: /* full expose */
parse arg sysvar;
k=words(datelist); /* count of different days */
do i = 1 to k;
     tempdate=word(datelist,i);
     l = words(data.sysvar.tempdate);
     if l=1 then iterate i; /* catches no data avail */
     sum=0;
     do j = 1 to l;
          sum=sum+word(data.sysvar.tempdate,j);
     end;
     mean=sum/l;
     sumdiff=0;
     max=0;
     do j = 0 to 9;
        freq.sysvar.j=0;
     end;
     do j = 1 to l;
          point=word(data.sysvar.tempdate,j);
          if point>max then max=point;
          select;
            when(point<10) then freq.sysvar.0=freq.sysvar.0+1;
            when(point<20) then freq.sysvar.1=freq.sysvar.1+1;
            when(point<30) then freq.sysvar.2=freq.sysvar.2+1;
            when(point<40) then freq.sysvar.3=freq.sysvar.3+1;
            when(point<50) then freq.sysvar.4=freq.sysvar.4+1;
            when(point<60) then freq.sysvar.5=freq.sysvar.5+1;
            when(point<70) then freq.sysvar.6=freq.sysvar.6+1;
            when(point<80) then freq.sysvar.7=freq.sysvar.7+1;
            when(point<90) then freq.sysvar.8=freq.sysvar.8+1;
            otherwise           freq.sysvar.9=freq.sysvar.9+1;
          end;
          diff=point-mean;    /* variance calc */
          sumdiff=sumdiff+(diff**2);
     end;
     var=right(trunc(sumdiff/l,2),6,"0");
     parse var tempdate mm 3 dd 5 yy;
     date=mm"/"dd"/"yy;
     max=right(max".00",6,"0");
     mean=right(trunc(mean,2),6,"0");
     parse value weekday(mm"/"dd"/19"yy) with day julian;
output=date||day||left(sysvar,8)||mean||var||max;
     do j = 0 to 9;
      output=output||right(trunc((freq.sysvar.j/l)*100,2),6,"0");
     end;
REPORT.outptr=output;
outptr=outptr+1;
end;
return;
