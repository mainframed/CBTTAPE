/*  REXX EXEC :   ( EDITPDS )
    FUNCTION  : THIS REXX UTILITY WILL GENERATE A LIST OF ALL MEMBERS
                CONTAINED IN A PARTITIONED DATA SET.
                THE MEMBER LIST IS USED AS INPUT TO ISPF MACRO TO
                PERFORM THE EDIT CHANGES OF ALL ITERATIONS OF THE
                SPECIFIED CHARACTER STRING.
                THE INITIAL MACRO ( @EDITMEM ) IS INVOKED TO PERFORM
                THE EDIT CHANGES.
                                                                     */
TRACE O
/*     */
DISPLAY_PANEL:
ADDRESS ISPEXEC
TSOUSER = USERID()
PDS = TSOUSER".USER.CNTLLIB"
DO FOREVER
   "DISPLAY PANEL(EDITPDSP)"
   SRC = RC
   IF SRC = 0 THEN LEAVE
   IF SRC = 8 THEN DO
      SAY '*** EDITPDS EXEC UTILITY TERMINATED DUE TO USER REQUEST. ***'
      EXIT
   END
   IF SRC > 8 THEN DO
      SAY '*** ERROR INVOKING ISPF DIALOG PANEL EDITPDSP - RC = 'SRC'.***'
      EXIT SRC
   END
END
/*     */
HOUSE_KEEPING:
MCNT = 0
XSTRING = 'ISF ISP ISR'
UPPER PDS
DSN = SYSDSN("'"PDS"'")
IF DSN = 'DATASET NOT FOUND' THEN DO
   SAY '*** DATASET ERROR: DATASET NOT FOUND FOR ('PDS'). ***'
   SAY '*** EDITPDS EXEC MAINTASK CANCELLED. ***'
   EXIT
END
"VPUT OLDSTR SHARED"
"VPUT NEWSTR SHARED"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** VPUT ERROR: ISPF DIALOG VPUT ERROR FOR CHANGE STRING STOW. ***'
   SAY '*** EDITPDS EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
ADDRESS TSO
X = OUTTRAP("PDSMEMB.","*","CONCAT")
"LISTDS ('"PDS"') MEMBERS"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** LISTDS ERROR: ERROR LISTING PDS MEMBERS FROM 'PDS'. ***'
   SAY '*** EDITPDS EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
/*     */
EDIT_MEMBERS:
ADDRESS ISPEXEC
DO I = 7 TO PDSMEMB.0
   MEMBER = STRIP(PDSMEMB.I)
   CALL CHECK_MEMBER_EXCLUDE
   IF EXCLSW = 'YES' THEN ITERATE
   MCNT = MCNT + 1
   SAY '+++ NOW EDITTING PDS MEMBER: 'MEMBER' +++'
   SAY 'EDIT DATASET('PDS'('MEMBER')) MACRO(@EDITPDS)'
   "EDIT DATASET('"PDS"("MEMBER")') MACRO(@EDITPDS)"
   SRC = RC
   IF SRC = 12 THEN EXIT
   SAY '+++ EDIT OF PDS MEMBER: 'MEMBER'  RC = 'SRC' +++'
END
SAY '+++ EDIT OF 'PDS' MEMBERS COMPLETE - ('MCNT') MEMBERS EDITTED. +++'
EXIT
/*     */
CHECK_MEMBER_EXCLUDE:
EXCLSW = 'NO'
MLEN = LENGTH(MEMBER)
SCOL = MLEN - 3
XPROF = SUBSTR(MEMBER,SCOL,4)
IF XPROF = 'PROF' THEN DO
   EXCLSW = 'YES'
   RETURN
END
XISPF = SUBSTR(MEMBER,1,3)
XCLUDE = POS(XISPF,XSTRING)
IF XCLUDE ¬= 0 THEN DO
   EXCLSW = 'YES'
   RETURN
END
XLIBR = SUBSTR(MEMBER,1,4)
IF XLIBR = 'LIB@' THEN DO
   EXCLSW = 'YES'
   RETURN
END
RETURN
