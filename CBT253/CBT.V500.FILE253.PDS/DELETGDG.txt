/*  REXX EXEC : ( DELETGDG ).
    FUNCTION  : PERFORMS IDCAMS LISTCAT LEVEL FOR A SPECIFIED BASE GDG
                DATASET AND DELETES THE ASSOCIATED GENERATIONS.
    AUTHOR    : DAN BOWEN, TECH SERVICES.
                                                                    */
TRACE O
/*      */
HOUSE_KEEPING:
ADDRESS TSO
PARSE UPPER ARG GDGLVL .
IF GDGLVL = '' THEN DO
   SAY '***********************************************************************'
   SAY '*** PARM ERROR: GDG BASE DATASET NAME LEVEL QUALIFIER NOT PASSED. ***'
   SAY '*** DELETEGDG EXEC TASK CANCELLED. ***'
   SAY '***********************************************************************'
   EXIT 024
END
X = MSG("OFF")
CURTIME = TIME()
HH = SUBSTR(CURTIME,1,2)
MM = SUBSTR(CURTIME,4,2)
SS = SUBSTR(CURTIME,7,2)
TIMEP = HH||MM||SS
OUTPDS = 'SYSS.TLMS.CNTLLIB'
MEMBER = 'TL'TIMEP
TAPEKEY = SUBSTR(GDGLVL,1,4)
ENDCNT = 8
JCLCNT = 5
JCL.1 = "//CTTUTIL  JOB 5304010530000000,'CTT SCRATCH ULITITY',"
JCL.2 = "//         CLASS=H,MSGCLASS=S,MSGLEVEL=(1,1),NOTIFY=TECH251"
JCL.3 = "//SCRATCH  EXEC CTTMUP"
JCL.4 = "//SYSIN    DD *"
JCL.5 = "  TYPERUN  MODE=NORMAL,RULEINFO=NO,CHECK=NO"
END.1 = "/*"
END.2= "//DELETMEM  EXEC PGM=IDCAMS"
END.3 = "//UTILPDS  DD DSN="OUTPDS",DISP=SHR"
END.4 = "//SYSPRINT DD SYSOUT=*"
END.5 = "//SYSIN    DD *"
END.6 = "  DELETE  "OUTPDS"("MEMBER")  FILE(UTILPDS)"
END.7 = "/*"
END.8 = "//"
IF TAPEKEY = 'PTAP' THEN SIGNAL MAIN_ROUTINE_TAPE
/*
           M A I N   R O U T I N E   S E C T I O N           */
/*     */
MAIN_ROUTINE_DASD:
CALL LISTCAT_DASD_GDG
CALL DELETE_DASD_GDG
EXIT 0
/*     */
MAIN_ROUTINE_TAPE:
CALL LISTCAT_TAPE_GDG
CALL WRITE_CONTROLT_JCL
CALL SUBMIT_CONTROLT_JOB
EXIT 0
/*
           S U B R O U T I N E   S E C T I O N           */
/*     */
LISTCAT_DASD_GDG:
D = 0
X = OUTTRAP("LSTCAT.","*","CONCAT")
"LISTCAT LEVEL('"GDGLVL"') NAMES"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '***********************************************************************'
   SAY '*** LISTCAT ERROR: NO GDG DATASETS FOUND FOR: ('GDGLVL'). ***'
   SAY '*** DELETEGDG EXEC TASK CANCELLED. ***'
   SAY '***********************************************************************'
   EXIT SRC
END
DO L = 1 TO LSTCAT.0
   DSNKEY = POS("NONVSAM ------- ",LSTCAT.L)
   IF DSNKEY ¬= 0 THEN DO
      D = D + 1
      GDG.D = WORD(LSTCAT.L,3)
   END
END
GDGCNT = D
RETURN
/*     */
DELETE_DASD_GDG:
DO I = 1 TO GDGCNT
   "ALLOC DA('"GDG.I"') FILE(GDGDD) SHR"
   "DELETE   ('"GDG.I"')   SCRATCH PURGE FILE(GDGDD)"
   "FREE FILE(GDGDD)"
END
RETURN
/*     */
LISTCAT_TAPE_GDG:
T = 0
GDGSW = 'NO'
X = OUTTRAP("LSTCAT.","*","CONCAT")
"LISTCAT LVL('"GDGLVL"') VOL"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '***********************************************************************'
   SAY '*** LISTCAT ERROR: NO GDG DATASETS FOUND FOR: ('GDGLVL'). ***'
   SAY '*** DELETEGDG EXEC TASK CANCELLED. ***'
   SAY '***********************************************************************'
   EXIT SRC
END
DO L = 1 TO LSTCAT.0
   DSNKEY = POS("NONVSAM ------- ",LSTCAT.L)
   IF DSNKEY ¬= 0 THEN GDGSW = 'YES'
   IF GDGSW = 'YES' THEN DO
      VOLKEY = POS("VOLSER------------",LSTCAT.L)
      IF VOLKEY ¬= 0 THEN DO
         STA = VOLKEY + 18
         T = T + 1
         TAPE.T = SUBSTR(LSTCAT.L,STA,6)
         GDGSW = 'NO'
      END
   END
END
TAPECNT = T
RETURN
/*     */
WRITE_CONTROLT_JCL:
"DELSTACK"
"ALLOC DA('"OUTPDS"("MEMBER")') FILE(CTTDD) SHR"
DO J = 1 TO JCLCNT
   QUEUE JCL.J
END
DO I = 1 TO TAPECNT
   CTTRCD = "  GRPSCR VOLSER="TAPE.I
   QUEUE CTTRCD
END
DO E = 1 TO ENDCNT
   QUEUE END.E
END
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW CTTDD (FINIS"
SRC = RC
"FREE FILE(CTTDD)"
IF SRC ¬= 0 THEN DO
   SAY '******************************************************************'
   SAY '*** EXECIO WRITE ERROR: RETURN CODE: ('SRC'). ***'
   SAY '*** ERROR WRITING ('OUTPDS'('MEMBER') CTT SYSIN RECORDS. ***'
   SAY '*** CTT TAPE GDG VOLSER SCRATCH FACILITY CANCELLED. ***'
   SAY '******************************************************************'
   EXIT SRC
END
RETURN
/*     */
SUBMIT_CONTROLT_JOB:
ADDRESS TSO
"SUBMIT '"OUTPDS"("MEMBER")'"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '***********************************************************************'
   SAY '*** SUBMIT ERROR: ('OUTPDS'('MEMBER') - RETURN CODE: ('SRC'). ***'
   SAY '*** CTT TAPE GDG VOLSER SCRATCH FACILITY CANCELLED. ***'
   SAY '***********************************************************************'
   EXIT SRC
END
RETURN
