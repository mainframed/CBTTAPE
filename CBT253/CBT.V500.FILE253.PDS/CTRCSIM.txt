PROC 0 DEBUG                +
       DATEFMT(WST)   +
       ILPREFM(CTMP)   +
       ILVERM(V400)     +
       OLVERM(V400)     +
       DBPREFM(IOAP)   +
       DBVERM(V400)     +
       LANG()         +
       LOADLIB(IOAP.V400.LOAD)   +
       DBG(DUMMY)           +
       CLASS(NULL)          +
       PRTDBG               +
       NOALC
 /*                                                        */
 /* THE "CLASS" PARAMETER SHOULD CONTAIN 'NULL' IN CASE NO */
 /* CLASS/JOBNAME REPLACEMENT IS REQUIRED.                 */
 /*                                                        */
 /* WM1286  22.09.92 SUPPORT JAPANESE DATE                 */
CONTROL MAIN NOLIST NOCONLIST NOMSG NOFLUSH
FREE DD(PRTDBG)
IF &PRTDBG=PRTDBG +
THEN +
  ALLOC F(PRTDBG) DA(*)
ELSE +
  ALLOC F(PRTDBG) &DBG SYSOUT(A)  HOLD
IF &DEBUG=DEBUG  +
   THEN CONTROL MAIN LIST CONLIST SYMLIST MSG

ISPEXEC VGET (ZDAY,ZMONTH,ZYEAR,ZUSER)
SET CTMFUN=SUBMIT
SET CTMLIBN=&ILPREFM..&ILVERM..JCL
SET CTMGLOB=&ILPREFM..&ILVERM..PARM
SET CTMUSERN=&ZUSER
SET CL=&CLASS
REDISP:+
SET YW=&ZYEAR
SET YO=&ZYEAR
SET MW=&ZMONTH
SET MO=&ZMONTH
SET DW=&ZDAY
SET DO=&ZDAY
SET &NOALC=&STR()
SET MW=&SUBSTR(2:3,&EVAL(&MW+100))
SET MO=&SUBSTR(2:3,&EVAL(&MO+100))
SET DW=&SUBSTR(2:3,&EVAL(&DW+100))
SET DO=&SUBSTR(2:3,&EVAL(&DO+100))
SET &ALLOCAES=0
SET &ALLOCSUB=0
SET &ALLOCSTR=0
ISPEXEC VPUT DATEFMT PROFILE
ISPEXEC DISPLAY PANEL(CTRPSIM)
IF &RESP = END THEN GOTO FINISH
LISTDSI DALIB FILE PREALLOC
SET  OLDDALIB = &SYSDSNAME
FREE  DD(DALIB)
ALLOC DD(DALIB)    DA('&ILPREFM..&ILVERM..PARM') SHR
LISTDSI DACKPT FILE PREALLOC
SET  OLDDACKP = &SYSDSNAME
FREE  DD(DACKP)
ALLOC DD(DACKP)    DA('&DBPREFM..&DBVERM..CKP') SHR
FREE  DD(DAGLOBAL,DASUBMIT,DASIM,SYSPRINT,DACTRFL,ATTR)
ALLOC F(ATTR) BLKSIZE(8000)
ALLOC DD(DAGLOBAL) DA('&CTMGLOB') SHR

DELETE '&SYSUID..&OLVERM..AESBRO'
ALLOC DD(SYSPRINT) DA('&SYSUID..&OLVERM..AESBRO') TR SP(1 1) MOD

IF &LASTCC¬=0 +
 THEN SET &ALLOCAES=1

DELETE '&SYSUID..&OLVERM..SUBMIT'
ALLOC DD(DASUBMIT) DA('&SYSUID..&OLVERM..SUBMIT') -
      TR SP(1 1) NEW DELETE US(ATTR)

IF &LASTCC¬=0 +
 THEN SET &ALLOCSUB=1
ALLOC DD(DASIM)    NEW TRACKS SP(1 1)
OPENFILE DASIM OUTPUT
SET DASIM=&STR(+
LIBRARY &CTMLIBN)
PUTFILE DASIM
SET DASIM=&STR(+
MEMBER  &CTMMEMN)
PUTFILE DASIM
SET DASIM=&STR(+
USERID  &CTMUSERN)
PUTFILE DASIM
SET DASIM=&STR(+
WDATE   &DW.&MW.&YW)
PUTFILE DASIM
SET DASIM=&STR(+
ODATE   &DO.&MO.&YO)
PUTFILE DASIM
SET DASIM=&STR(+
&CTMFUN)
PUTFILE DASIM
CLOSFILE DASIM
TSOEXEC CALL '&LOADLIB&LANG(CTMAES)'

SET &M1=&CTMMEMN
IF &DATATYPE(&ORDER)=NUM +
THEN +
  SET &O1=&SUBSTR(2:6,&EVAL(&ORDER+100000))
ELSE +
  SET &O1=&ORDER
SET &F1=&FROMPGM
SET &F2=&FROMSTEP
SET &F3=&TOPGM
SET &F4=&TOSTEP
SET &S1=SAVE2
SET &A1=&ACT

DELETE '&SYSUID..O&O1..ARCHIVE'
ALLOC DD(DACTRFL) DA('&SYSUID..O&O1..ARCHIVE') SPACE(2 1) TR


TSOEXEC CALL '&LOADLIB&LANG(CTMAJF)' 'ORDERID=&ORDER MEMBER=&M1'
SET &AJFRC=&LASTCC

FREE F(DACTRFL)
FREE  DD(SYSPRINT)

ALLOC DD(SYSPRINT) DA('&SYSUID..&OLVERM..AESBRO') SHR DELETE


IF &ALLOCAES=1 +
THEN DO
     WRITE ALLOCATION ERROR
     GOTO REDISP
     END
ISPEXEC LMINIT DATAID(CTMAES2) DDNAME(SYSPRINT)
ISPEXEC VPUT (M1 O1 F1 F2 F3 F4 S1 A1 CL)
ISPEXEC EDIT DATAID(&CTMAES2)  MACRO(CTRMACRO)
ISPEXEC BROWSE  DATAID(&CTMAES2)
ISPEXEC LMFREE DATAID(&CTMAES2)

IF &AJFRC NE 0 +
THEN DO
     ISPEXEC SETMSG MSG(CTRC001E)
     GOTO REDISP
     END

FREE  DD(SYSPRINT)
ALLOC DD(SYSPRINT) DA(*)
IF '&OLDDALIB' = '' THEN GOTO NODEALL
FREE DD(DALIB)
ALLOC DD(DALIB) DA('&OLDDALIB')  SHR
NODEALL:+
IF '&OLDDACKP' = '' THEN GOTO NODEALC
FREE DD(DACKP)
ALLOC DD(DACKP) DA(&OLDDACKP)  SHR
NODEALC:+
IF &SYSINDEX(SUB,&CTMFUN) NE 0  AND &ALLOCSUB=0 +
THEN DO
       SET &S1=SAVE1
       ISPEXEC VPUT (S1)
       ISPEXEC LMINIT DATAID(CTMSUB2) DDNAME(DASUBMIT)
       ISPEXEC CONTROL ERRORS RETURN
       ISPEXEC EDIT  DATAID(&CTMSUB2) MACRO(CTRMACRO)
       ISPEXEC LMFREE DATAID(&CTMSUB2)
       CONTROL MSG
       SUB '&SYSUID..&OLVERM..SUBMIT'
       CONTROL NOMSG
     END
GOTO  REDISP
FINISH: EXIT
