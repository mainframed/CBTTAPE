/*   REXX EXEC : ( ABRTLMS )
     FUNCTION  : BUILDS THE SYSIN FOR THE TLMS VMF UPDATE TO SCRATCH
                 TAPES HELD BY THE DAILY AND WEEKLY FDRABR SYSTEM
                 DASD VOLUME BACKUPS.
     INPUT     : ABRMAP OUTPUT DASD DATASET CREATED BY THE FDRABRCM
                 UTILITY ( PURGE BACKUP SIMULATE ) COMMAND.
     OUTPUT    : &USERID.USER.CNTLLIB:
                 PDS MEMBER=(ABRTLMSI)  ---> INPUT TO ABRPURG4 JOB
                 PDS MEMBER=(ABRERRLG)  ---> ERRLOG FROM ABRPURG2 JOB
     REFERENCE : ALL JOBS TO BE SUBMITTED RESIDE IN THE FOLLOWING PDS:
                 DSN=(SYSS.TECH.COMMON.CNTLLIB).
                 JOB 1 = ABRPURG1    ( SIMULATE RUN )
                 JOB 2 = ABRPURG2    ( EXECUTES THIS REXX EXEC )
                 JOB 3 = ABRPURG3    ( PURGE ABR CATALOG ENTRIES )
                 JOB 4 = ABRPURG4    ( UPDATES THE TLMS VMF ENTRIES ).
                                                                      */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
PARSE UPPER ARG ABRMAP .
TSOID = SYSVAR(SYSUID)
IF ABRMAP = '' THEN ABRMAP = TSOID'.FDRABR.ABRMAP'
ABRDSN = SYSDSN("'"ABRMAP"'")
IF ABRDSN = 'DATASET NOT FOUND' THEN DO
   SAY '*** DATASET ERROR: DATASET NOT FOUND FOR ('ABRMAP'). ***'
   SAY '*** ABRTLMS EXEC MAINTASK CANCELLED. ***'
   EXIT 028
END
E = 0
T = 0
X = MSG("OFF")
"FREE FILE(ABRDD)"
OUTPDS = TSOID'.USER.CNTLLIB'
/*
       M A I N   R O U T I N E   S E C T I O N     */
/*     */
MAIN_ROUTINE:
CALL READ_ABRMAP_DATASET
DO I = 1 TO ABRDATA.0
   DSNKEY = POS('FDRABR.V',ABRDATA.I)
   IF DSNKEY = 0 THEN ITERATE
   TAPEDSN = WORD(ABRDATA.I,1)
   CALL LISTCAT_TAPE_DATASET
   IF LSTSW = 'NO' THEN DO
      CALL BUILD_ERRLOG_RECORD
      ITERATE
   END
   CALL CHECK_DUPLICATE_TAPEVOL
END
CALL BUILD_TLMS_SYSIN_RECORDS
MEMBER = 'ABRTLMSI'
CALL WRITE_PDS_MEMBER
ECNT = E
IF ECNT > 0 THEN DO E = 1 TO ECNT
   "DELSTACK"
   MEMBER = 'ABRERRLG'
   QUEUE LOGRCD.E
END
IF ECNT > 0 THEN CALL WRITE_PDS_MEMBER
EXIT 0
/*
       S U B R O U T I N E   S E C T I O N     */
/*     */
READ_ABRMAP_DATASET:
"ALLOC DA('"ABRMAP"') FILE(ABRDD) SHR"
"EXECIO * DISKR ABRDD (STEM ABRDATA. FINIS"
SRC = RC
"FREE FILE(ABRDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO DATASET READ ERROR: ('ABRMAP'). ***'
   SAY '*** ABRTLMS EXEC MAINTASK CANCELLED. ***'
   EXIT SRC
END
RETURN
/*     */
LISTCAT_TAPE_DATASET:
LSTSW = 'NO'
X = OUTTRAP("LSTCAT.","*","CONCAT")
"LISTCAT ENT('"TAPEDSN"') VOL"
IF RC ¬= 0 THEN RETURN
LSTSW = 'YES'
DO L = 1 TO LSTCAT.0
   VOLKEY = POS("VOLSER------------",LSTCAT.L)
   IF VOLKEY ¬= 0 THEN DO
      STA = VOLKEY + 18
      TAPEVOL = SUBSTR(LSTCAT.L,STA,6)
      LEAVE
   END
END
RETURN
/*     */
BUILD_ERRLOG_RECORD:
E = E + 1
LOGRCD.E = TAPEDSN' : LISTCAT ERROR - DATASET NOT IN ICFCAT ***'
RETURN
/*     */
CHECK_DUPLICATE_TAPEVOL:
BYPASS = 'NO'
IF T = 0 THEN DO
   T = T + 1
   TVOL.T = TAPEVOL
   RETURN
END
TAPECNT = T
DO T = 1 TO TAPECNT
   IF TAPEVOL = TVOL.T THEN BYPASS = 'YES'
END
T = TAPECNT
IF BYPASS = 'YES' THEN RETURN
T = T + 1
TVOL.T = TAPEVOL
RETURN
/*     */
BUILD_TLMS_SYSIN_RECORDS:
"DELSTACK"
MEMBER = 'ABRTLMSI'
TAPECNT = T
DO T = 1 TO TAPECNT
   TLMSRCD = 'UPV 'TVOL.T',SCRATCH=YES,CDS=0000'
   QUEUE TLMSRCD
END
RETURN
/*     */
WRITE_PDS_MEMBER:
"ALLOC DA('"OUTPDS"("MEMBER")') FILE(ABRDD) SHR"
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW ABRDD (FINIS"
SRC = RC
"FREE FILE(ABRDD)"
IF SRC ¬= 0 THEN DO
   SAY '***************************************************************'
   SAY '*** EXECIO WRITE ERROR: RETURN CODE: ('SRC'). ***'
   SAY '*** ERROR WRITING ('OUTPDS'('MEMBER'). ***'
   SAY '*** ABRTLMS EXEC SUBTASK CANCELLED. ***'
   SAY '***************************************************************'
   EXIT SRC
END
RETURN
