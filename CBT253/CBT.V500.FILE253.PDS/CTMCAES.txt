PROC 0 DEBUG                +
       DATEFMT(USA)   +
       ILPREFM(SYSM.CTMI)   +
       ILVERM(V410)     +
       OLVERM(V410)     +
       LANG()         +
       LOADLIB(SYS1.IOA.V410.LOAD)   +
       DBG(DUMMY)           +
       NOALC
 /* CONTROL MAIN LIST CONLIST SYMLIST MSG
 /* WM1286 AM 200992 SUPPORT JAPANESE DATE */
CONTROL NOMSG NOFLUSH
FREE DD(PRTDBG)
ALLOC F(PRTDBG) &DBG SYSOUT(A)  HOLD
CONTROL MSG
IF &DEBUG=DEBUG  +
   THEN CONTROL MAIN LIST CONLIST SYMLIST MSG
ISPEXEC VGET (ZDAY,ZMONTH,ZYEAR,ZUSER)
ISPEXEC VGET (JOBSCAN)
SET CTMFUN=LIST
SET CTMLIBN=&ILPREFM..&ILVERM..JCL
SET CTMGLOB=&ILPREFM..&ILVERM..PARM
SET CTMUSERN=&ZUSER
REDISP:+
SET YW=&ZYEAR
SET YO=&ZYEAR
SET MW=&ZMONTH
SET MO=&ZMONTH
SET DW=&ZDAY
SET DO=&ZDAY
SET &NOALC=&STR()
SET MW=&SUBSTR(2:3,&EVAL(&MW+100))
SET MO=&SUBSTR(2:3,&EVAL(&MO+100))
SET DW=&SUBSTR(2:3,&EVAL(&DW+100))
SET DO=&SUBSTR(2:3,&EVAL(&DO+100))
SET YW=&SUBSTR(2:3,&EVAL(&YW+100))
SET YO=&SUBSTR(2:3,&EVAL(&YO+100))
IF &DATEFMT=JPN THEN SET &AMERDATE =NO
                ELSE +
IF &DATEFMT=USA THEN SET &AMERDATE =YES
                ELSE SET &AMERDATE =NO
ISPEXEC  VPUT  AMERDATE PROFILE
IF &JOBSCAN=YES  THEN +
   ISPEXEC DISPLAY PANEL(CTMPAESJ)
ELSE +
   ISPEXEC DISPLAY PANEL(CTMPAES)
IF &RESP = END THEN EXIT
IF &SYSINDEX(JOBSCAN,&CTMFUN) NE 0 THEN +
  DO
    SET &CTMFUN= SUBMIT
    SET &JBS = YES
  END
ELSE +
  SET &JBS = NO
CONTROL NOMSG
LISTDSI DALIB FILE PREALLOC
SET  OLDDALIB = &SYSDSNAME
FREE  DD(DALIB)
ALLOC DD(DALIB)    DA('&ILPREFM..&ILVERM..PARM') SHR
FREE  DD(ATTR,DAGLOBAL,DASIM,DASUBMIT,SYSPRINT)
CONTROL MSG
ALLOC DD(ATTR) BLKSIZE(8000)
ALLOC DD(DAGLOBAL) DA('&CTMGLOB') SHR
ALLOC DD(SYSPRINT) DA('&SYSUID..&OLVERM..AESBRO') TR SP(1 1) NEW DELETE
ALLOC DD(DASUBMIT) DA('&SYSUID..&OLVERM..SUBMIT')  +
      TR SP(1 1) NEW DELETE US(ATTR)
ALLOC DD(DASIM)    NEW TRACKS SP(1 1)
OPENFILE DASIM OUTPUT
SET DASIM=&STR(+
LIBRARY &CTMLIBN)
PUTFILE DASIM
SET DASIM=&STR(+
MEMBER  &CTMMEMN)
PUTFILE DASIM
SET DASIM=&STR(+
OWNER   &CTMUSERN)
PUTFILE DASIM
SET DASIM=&STR(+
WDATE   &DW.&MW.&YW)
PUTFILE DASIM
SET DASIM=&STR(+
ODATE   &DO.&MO.&YO)
PUTFILE DASIM
SET DASIM=&STR(+
&CTMFUN)
PUTFILE DASIM
CLOSFILE DASIM
TSOEXEC CALL '&LOADLIB&LANG(CTMAES)'
IF &JBS = YES THEN +
  DO
    ISPEXEC LMINIT DATAID(CTMAES2) DDNAME(DASUBMIT)
    IF &LASTCC ¬= 0 THEN DO
     SET ECODE= 8
     WRITE JOB/SCAN COULD NOT BE ACTIVATED FOR JCL SCAN
    END
    ELSE DO
    ISPEXEC EDIT DATAID(&CTMAES2) MACRO(CTMDT)
    ISPEXEC LMFREE DATAID(&CTMAES2)
    ISPEXEC VGET ECODE SHARED
    END
    SET CTMFUN=JOBSCAN
  END
IF (&JBS ¬= YES) | (&ECODE = 8)  THEN +
  DO
    ISPEXEC LMINIT DATAID(CTMAES2) DDNAME(SYSPRINT)
    ISPEXEC BROWSE  DATAID(&CTMAES2)
    ISPEXEC LMFREE DATAID(&CTMAES2)
  END
FREE  DD(SYSPRINT)
ALLOC DD(SYSPRINT) DA(*)
IF '&OLDDALIB' = '' THEN GOTO NODEALL
FREE DD(DALIB)
ALLOC DD(DALIB) DA('&OLDDALIB')  SHR
NODEALL:+
IF &JBS ¬= YES THEN +
  IF &SYSINDEX(SUB,&CTMFUN) NE 0 THEN SUB '&SYSUID..&OLVERM..SUBMIT'
GOTO  REDISP
END
