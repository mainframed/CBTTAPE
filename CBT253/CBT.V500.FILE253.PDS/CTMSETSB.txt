ISREDIT MACRO (PARM)
/********************************************************************/
/*                                                                  */
/* PURPOSE: THIS CLIST MAY BE USED TO REPLACE THE STANDARD SUBMIT   */
/*          COMMAND BY CONTROL-M QUICK-SUBMIT FUNCTION.             */
/*                                                                  */
/*          TO TURN THIS OPTION ON, THE USER SHOULD INVOKE THAT     */
/*          CLIST UNDER ISPF WHILE EDITING A MEMBER. FROM           */
/*          THAT MOMENT ON, WHENEVER HE SUBMITS A MEMBER FROM A     */
/*          LIBRARY WITH THE SAME 'PROJECT' - CONTROL-M'S QUICK-    */
/*          SUBMIT WILL BE OPERATED INSTEAD.                        */
/*                                                                  */
/*          TO TURN THIS OPTION OFF, THE USER MAY INVOKE THAT       */
/*          CLIST UNDER ISPF WHILE HE IS EDITING A MEMBER, BUT      */
/*          THIS TIME WITH THE PARAMETER 'DELETE'.                  */
/*                                                                  */
/*         E.G.                                                     */
/*            COMMAND ===> CTMSETSB                                 */
/*            COMMAND ===> CTMSETSB DELETE                          */
/*                                                                  */
/* REMARK:                                                          */
/*                                                                  */
/*           THIS COMMAND IS EFFECTIVE FOR ALL LIBRARIES THAT HAVE  */
/*           THE SAME 'PROJECT' DEFINITION.  IF YOU USE LIBRARIES   */
/*           WITH DIFFERENT 'PROJECTS' DEFINITIONS, YOU WILL NEED   */
/*           TO EXECUTE THIS CLIST FOR EACH PROJECT.                */
/*                                                                  */
/********************************************************************/

      IF &PARM=DEBUG +
      THEN +
         CONTROL LIST MSG SYMLIST

      IF &PARM=DELETE +
      THEN +
         DO                          /* ERASE EXISTANCE OF IMACRO */
            ISREDIT PROFILE UNLOCK
            ISREDIT IMACRO  NONE
            ISREDIT PROFILE LOCK
            ISREDIT END              /* TO SAVE THE PROFILE CHANGES */
            WRITE THE SUBMIT COMMAND HAS BEEN RESTORED SUCCESSFULLY
           EXIT
         END

      ISREDIT (IMACRO) = IMACRO      /* IDENTIFY THE CURRENT IMACRO */
      IF &IMACRO=NONE +
      THEN +
         DO
            WRITE ************************************************
            WRITE * IN ORDER TO REPLACE THE SUBMIT COMMAND THERE *
            WRITE * IS A NEED TO SAVE AND EXIT THE MEMBER YOU ARE*
            WRITE * CURRENTLY EDITTING.                          *
            WRITENR * DO YOU AGREE FOR DOING "SAVE" NOW (Y,N) ?
            READ  &ANS
            IF &ANSÂ¬=Y THEN DO
                      WRITE
                      WRITE REPLACE WAS NOT DONE (AS YOU REQUESTED).
                      EXIT
                            END
            ISREDIT PROFILE UNLOCK
            ISREDIT IMACRO  CTMSETSB
            ISREDIT PROFILE LOCK
            ISREDIT END              /* TO SAVE THE PROFILE CHANGES */
            WRITE
            WRITE THE SUBMIT COMMAND HAS BEEN REPLACED SUCCESSFULLY
            EXIT
        END
      ELSE +
      IF &IMACRO=CTMSETSB +
      THEN +
         DO                                   /* REGULAR ENTRY        */
            ISREDIT DEFINE CTMQSB PGM MACRO   /* IMACRO IS ALREADY ON */
            ISREDIT DEFINE SUB    ALIAS CTMQSB   /* USUAL ENTRY  */
            ISREDIT DEFINE SUBMIT ALIAS CTMQSB
            EXIT
         END
      ELSE +
         DO                   /* THE USER HAVE ALREADY ANOTHER IMACRO */
            WRITE
            WRITE YOU ALREADY HAVE AN "IMACRO" . CONTACT YOUR SYSTEM
            WRITE PROGRAMMER TO ADD THE 3 FOLLOWING COMMANDS TO YOUR
            WRITE IMACRO :
            WRITE            ISREDIT DEFINE CTMQSB PGM MACRO
            WRITE            ISREDIT DEFINE SUB    ALIAS CTMQSB
            WRITE            ISREDIT DEFINE SUBMIT ALIAS CTMQSB
            WRITE
            EXIT
         END
