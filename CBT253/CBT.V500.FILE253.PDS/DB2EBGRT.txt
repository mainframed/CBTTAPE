/*  REXX EXEC : ( DB2EGRNT ).
    FUNCTION  : CALLED BY DB2B COMPILE TASK TO BUILD THE GRANT
                AUTHORIZATION SYSIN MEMBER FOR EMERGENCY BATCH COMPILES
                FROM AN EMERGENCY CCFID ( TECH290 THRU TECH295 ).
    INPUT     : SYSS.TECH.COMMON.PARMLIB(DB2ADMIN).
    OUTPUT    : &TSOID.USER.DBRMLIB(&PROGRAM).
                                                                     */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
ARG TSOID MEMBER DB2COLL DB2PLAN
PARMS = 'TSOID MEMBER DB2COLL DB2PLAN'
SRC = 0
DO I = 1 TO 4
   IPARM = WORD(PARMS,I)
   XPARM = VALUE(IPARM)
   IF XPARM ¬= '' THEN ITERATE
   SAY '*** PARM ERROR: MISSING PARM VALUE FOR ('IPARM'). ***'
   SAY '*** DB2EGRNT EXEC TASK CANCELLED - RC = (024). ***'
   EXIT 024
END
X = MSG("OFF")
"FREE FILE(PARMDD)"
"FREE FILE(DB2DD)"
DUNIT = 'APPLTA'
TSOPRFX = SUBSTR(TSOID,1,4)
IF TSOPRFX = 'TECH' THEN DUNIT = 'SMS'
UCNTL = SYSDSN(USER.DBRMLIB)
IF UCNTL = 'DATASET NOT FOUND' THEN DO
"ATTRIB ATTRB1 DSORG(PO) RECFM(F,B) LRECL(80) BLKSIZE(27920)"
"ALLOC DA(USER.DBRMLIB) NEW SPACE(3,1) DIR(44) USING(ATTRB1) UNIT("DUNIT")"
   SRC = RC
   IF SRC ¬= 0 THEN DO
   SAY '*** ALLOCATE ERROR: ERROR ALLOCATING DATASET (USER.DBRMLIB). ***'
   SAY '*** DB2EGRNT EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
   END
END
"FREE DA(USER.DBRMLIB)"
OUTPDS = TSOID'.USER.DBRMLIB'
PARMLIB = 'SYSS.TECH.COMMON.PARMLIB'

/*     */
MAIN_ROUTINE:
CALL BUILD_GRANT_SYSIN
CALL WRITE_DB2_PARMS
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
BUILD_GRANT_SYSIN:
"DELSTACK"
PKGCNT = 5
COMRCD = "  COMMIT;"
PKG.1 = "        GRANT"
PKG.2 = "        BIND,"
PKG.3 = "        COPY,"
PKG.4 = "        EXECUTE"
PKG.5 = "  ON PACKAGE "DB2COLL".* TO"
BNDCNT = 4
BND.1 = "        GRANT"
BND.2 = "        BIND,"
BND.3 = "        EXECUTE"
BND.4 = "  ON PLAN "DB2PLAN" TO"
DB2PARM = SYSDSN("'"PARMLIB"(DB2ADMIN)'")
IF DB2PARM = 'MEMBER NOT FOUND' THEN DO
   SAY '*** MEMBER ERROR: MEMBER NOT FOUND ('PARMLIB'(DB2ADMIN). ***'
   SAY '*** DB2EGRNT EXEC SUBTASK CANCELLED - RETURN CODE: ('028'). ***'
   EXIT 028
END
"ALLOC DA('"PARMLIB"(DB2ADMIN)') FILE(PARMDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('PARMLIB'(DB2ADMIN). ***'
   SAY '*** DB2EGRNT EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR PARMDD (FINIS STEM AUTH."
SRC = RC
"FREE FILE(PARMDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('PARMLIB'(DB2ADMIN). ***'
   SAY '*** DB2EGRNT EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
DO P = 1 TO PKGCNT
   QUEUE PKG.P
END
DO I = 1 TO AUTH.0
   AUTHUSR = WORD(AUTH.I,1)
   AUSER = TSOID','
   IF AUSER = AUTHUSR THEN ITERATE
   DB2AUTH = '        '||AUTHUSR
   QUEUE DB2AUTH
END
QUEUE COMRCD
DO B = 1 TO BNDCNT
   QUEUE BND.B
END
DO I = 1 TO AUTH.0
   AUTHUSR = WORD(AUTH.I,1)
   AUSER = TSOID','
   IF AUSER = AUTHUSR THEN ITERATE
   DB2AUTH = '        '||AUTHUSR
   QUEUE DB2AUTH
END
QUEUE COMRCD
RETURN

/*     */
WRITE_DB2_PARMS:
"ALLOC DA('"OUTPDS"("MEMBER")') FILE(DB2DD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('OUTPDS'('MEMBER'). ***'
   SAY '*** DB2EGRNT EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW DB2DD (FINIS"
SRC = RC
"FREE FILE(DB2DD)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING ('OUTPDS'('MEMBER').  ***'
   SAY '*** DB2EGRNT EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN
