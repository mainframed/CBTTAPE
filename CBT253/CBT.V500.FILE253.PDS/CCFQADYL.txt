/* REXX EXEC : ( CCFQADYL ).
   FUNCTION: INVOKED BY CCF SKELETON JCL ( CCFDYLQ ) FOR PROMOTION OF
             DYLAKOR SOURCE PROGRAMS INTO QA OR PROD LIBRARIES.
             THIS REXX EXEC PROGRAM WILL PERFORM THE FOLLOWING TASKS:
             1. ALLOCATE A TSO USER WORK PDS FOR A PROGRAM MEMBER.
             2. READ/CHECK THE SOURCE PROGRAM FOR THE NUMBER OF REPORTS
                COMMENT INDICATOR.
             3. BUILD THE IDCAMS DELETE LOAD MODULE SYSIN MEMBER.
             4. BUILD THE IEBCOPY COPY LOAD MODULE SYSIN MEMBER.
             INPUT  : ( SYS1.QASRCE.MASTER(&MEMBER).
             OUTPUT : ( SYSS.QA.CCF.DYLAKOR.DYLCOPY.CNTLLIB(&MEMBER).
                      ( SYSS.QA.CCF.DYLAKOR.DYLDEL01.CNTLLIB(&MEMBER).
                      ( SYSS.QA.CCF.DYLAKOR.DYLDEL02.CNTLLIB(&MEMBER).
                                                                      */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
ARG TSOID MASTER MEMBER
PARMS = 'TSOID MASTER MEMBER'
DO I = 1 TO 3
   IPARM = WORD(PARMS,I)
   XPARM = VALUE(IPARM)
   IF XPARM ¬= '' THEN ITERATE
   SAY '*** PARM ERROR: MISSING PARM VALUE FOR ('IPARM'). ***'
   SAY '*** CCFQADYL EXEC TASK CANCELLED - RC = (024). ***'
   EXIT 024
END
LIBNODE = POS('PRODSRCE',MASTER)
IF LIBNODE ¬= 0 THEN EXIT 2
LIBNODE = POS('QASRCE',MASTER)
IF LIBNODE = 0 THEN EXIT 0
PREFIX = SUBSTR(MEMBER,1,6)
DEL.1 = "  IF LASTCC = 8 THEN"
DEL.2 = "     DO"
DEL.3 = "       SET MAXCC = 0"
DEL.4 = "     END"

/*     */
MAIN_ROUTINE:
CALL GET_REPORTS_NUMBER
CALL BUILD_REPORT_MODULE_LIST
LOADLIB = 'SYS1.TEST.BATCH.LOADLIB'
CALL BUILD_IDCAMS_TEST_DELETE_SYSIN
OUTMEMB = 'DYLDEL01'
CALL WRITE_SYSIN_MEMBER
LOADLIB = 'SYS1.QA.BATCH.LOADLIB'
CALL BUILD_IDCAMS_QA_DELETE_SYSIN
OUTMEMB = 'DYLDEL02'
CALL WRITE_SYSIN_MEMBER
CALL BUILD_IEBCOPY_COPY_SYSIN
OUTMEMB = 'DYLCOPY'
CALL WRITE_SYSIN_MEMBER
EXIT 1

/*     S U B R O U T I N E   S E C T I O N     */
/*     */
GET_REPORTS_NUMBER:
ADDRESS TSO
RPTSW = 'NO'
LIBDSN = "'"MASTER"("MEMBER")'"
LIBDD = LIBALLOC(LIBDSN)  /* DYNALLOC LIBMASTER W/LAM; RET DDNAME*/
IF LIBDD = "ERROR" THEN DO
   ERC = 100
   SAY '*** DYNAMIC ALLOCATION ERROR: ('MASTER'). ***'
   SAY '*** REPORTS FLAG CANNOT BE READ FOR ('MEMBER') PROGRAM. ***'
   SAY '*** CCFQADYL EXEC SUBTASK CANCELLED - RC = ('ERC'). ***'
   EXIT ERC
END
"EXECIO * DISKR" LIBDD "(FINIS STEM DATA."
SRC = RC
IF SRC ¬= 0 THEN DO
   ERC = 100
   SAY '*** EXECIO READ ERROR: ('MASTER'('MEMBER'). ***'
   SAY '*** REPORTS FLAG CANNOT BE READ FOR ('MEMBER') PROGRAM. ***'
   SAY '*** CCFQADYL EXEC SUBTASK CANCELLED - RC = ('ERC'). ***'
   EXIT ERC
END
DO I = 1 TO DATA.0
   REPTFLG = POS('* REPORTS = ',DATA.1)
   IF REPTFLG ¬= 0 THEN DO
      NUMREPTS = WORD(DATA.1,4)
      IF NUMREPTS = '' THEN ITERATE
      RTYP = DATATYPE(NUMREPTS)
      IF RTYP = 'NUM' THEN DO
         RPTSW = 'YES'
         LEAVE
      END
   END
END
IF RPTSW = 'NO' THEN NUMREPTS = 1
RETURN

/*     */
BUILD_REPORT_MODULE_LIST:
M = 0
MCNT = 0
DO NUMREPTS
   M = M + 1
   IF M = 1 THEN DO
      MCNT = MCNT + 1
      MOD.MCNT = MEMBER
      ITERATE
   END
   MCNT = MCNT + 1
   MLEN = LENGTH(M)
   IF MLEN = 1 THEN MOD.MCNT = PREFIX||'0'M
   IF MLEN = 2 THEN MOD.MCNT = PREFIX||M
END
RETURN

/*     */
BUILD_IDCAMS_TEST_DELETE_SYSIN:
"DELSTACK"
DO M = 1 TO MCNT
   RECORD = "  DELETE "LOADLIB"("MOD.M") FILE(LOADLIB)"
   QUEUE RECORD
END
DO D = 1 TO 4
   QUEUE DEL.D
END
RETURN

/*     */
BUILD_IDCAMS_QA_DELETE_SYSIN:
"DELSTACK"
DO M = 1 TO MCNT
   RECORD = "  DELETE "LOADLIB"("MOD.M") FILE(LOADLIB)"
   QUEUE RECORD
END
DO D = 1 TO 4
   QUEUE DEL.D
END
RETURN

/*     */
BUILD_IEBCOPY_COPY_SYSIN:
I = 0
M = 0
CNTL = "       SELECT MEMBER=("
"DELSTACK"
RECORD = "  COPY OUTDD=OUTDD1"
QUEUE RECORD
RECORD = "       INDD=((INDD1,R))"
QUEUE RECORD
DO MCNT
   M = M + 1
   I = I + 1
   IF I = 1 THEN SYSIN = CNTL||MOD.M
   IF (I > 1) & (I < 5) THEN SYSIN = SYSIN||","||MOD.M
   IF (I = 5) THEN DO
      SYSIN = SYSIN||","||MOD.M||")"
      QUEUE SYSIN
      I = 0
   END
END
IF I > 0 THEN DO
   SYSIN = SYSIN||")"
   QUEUE SYSIN
END
RETURN

/*     */
WRITE_SYSIN_MEMBER:
"ALLOC DA('SYSS.QA.CCF.DYLAKOR."OUTMEMB".CNTLLIB("MEMBER")') FILE(DYLDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('OUTMEMB'('MEMBER'). ***'
   SAY '*** CCFQADYL EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT 8
END
RCDCNT = QUEUED()
"EXECIO" RCDCNT "DISKW DYLDD (FINIS"
SRC = RC
"FREE FILE(DYLDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO WRITE ERROR: ('OUTMEMB'('MEMBER'). ***'
   SAY '*** CCFQADYL EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT 8
END
RETURN
