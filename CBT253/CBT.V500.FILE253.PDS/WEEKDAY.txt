/* REXX Weekday Calculation from 1901-2099 */
/* Input is MM/DD/YYYY                     */
/* Output is day of the week; i.e., Sun Julian-date-within-year */
perpetual_pattern = , /* periodic calendar pattern */
  "3 4 5 13 1 2 3 11 6 7 1 9 4 5 6 14 2 3 4 12 7 1 2 10 5 6 7 8";
months_non_leapyear="01 32 60 91 121 152 182 213 244 274 305 335";
months_leapyear    ="01 32 61 92 122 153 183 214 245 275 306 336";
weekdays="Sun Mon Tue Wed Thu Fri Sat";
parse arg mm "/" dd "/" yyyy; /* input calc date */
if (yyyy< 1901) | (yyyy>2099) then return ""; /* error */
if (mm< 1) | (mm>12) then return ""; /* error */
if (dd< 1) | (dd>31) then return ""; /* error */
pattern = word(perpetual_pattern,(((yyyy - 1901) // 28) + 1));
if pattern <= 7 then do;
          months_pattern = months_non_leapyear;
          week_offset    = pattern;
          end;
else do;
          months_pattern = months_leapyear;
          week_offset    = pattern-7;
          end;
julian_date= word(months_pattern,mm)+dd-1;
day        = (julian_date//7)-1+week_offset;
if day=0 then day=7; /* adjustment */
if day>7 then day=day-7;
return word(weekdays,day) julian_date;
