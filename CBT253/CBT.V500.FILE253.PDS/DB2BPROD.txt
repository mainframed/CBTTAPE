/*  REXX EXEC : ( DB2BPROD ).
    FUNCTION  : CALLED BY CCF QA AND PROD TURNOVER PROCESSING SKELETON
                ( CCFBDB2Q - STEP0160 ) FOR DB2 BATCH COBOL2 PROGRAMS.
                BUILDS THE PACKAGE/BIND/GRANT JOBS FOR SUBMISSION TO
                THE SYSB DSNB SYSTEM.
    INPUT     : SYSS.QA.CCF.DB2.TURNOVER.PARMLIB(&WORKORDR).
    OUTPUT    : SYSS.QA.CCF.DB2.STAGING.CNTLLIB(&MEMBER).
                                                                     */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
ARG TSOID MASTER MEMBER WORKORDR
PARMS = 'TSOID MASTER MEMBER WORKORDR'
EXIT 0
/* ***   N O O P   *** */
SRC = 0
DO I = 1 TO 4
   IPARM = WORD(PARMS,I)
   XPARM = VALUE(IPARM)
   IF XPARM ¬= '' THEN ITERATE
   SAY '*** PARM ERROR: MISSING PARM VALUE FOR ('IPARM'). ***'
   SAY '*** DB2BPROD EXEC TASK CANCELLED - RC = (024). ***'
   EXIT 024
END
X = MSG("OFF")
"FREE FILE(PARMDD)"
"FREE FILE(DB2DD)"
LIBNODE = POS('PRODSRCE',MASTER)
IF LIBNODE = 0 THEN EXIT 0

/*     */
MAIN_ROUTINE_PROD:
ACCTCDE = '5302010530000000'
DBSUBSYS = 'DSNB'
OUTMEMB = MEMBER
PARMLIB = 'SYSS.TECH.COMMON.PARMLIB'
PARMPDS = 'SYSS.QA.CCF.DB2.TURNOVER.PARMLIB'
OUTPDS = 'SYSS.QA.CCF.DB2.STAGING.CNTLLIB'
RUNLIB = 'SYSS.DB2.V2R3M0.DB2B.RUNLIB.LOAD'
GRANTPDS = 'SYSS.PROD.CCF.DB2.GRANT.CNTLLIB'
DBRMLIB = 'SYSS.PROD.BATCH.DBRMLIB'
CALL GET_DB2_PACKAGE_BIND_PARMS
IF MEMBSW = 'NO' THEN DO
   SAY '*** PARM ERROR: ENTRY NOT FOUND ('PARMPDS'('WORKORDR') ('MEMBER'). ***'
   SAY '*** DB2BPROD EXEC SUBTASK CANCELLED - RETURN CODE: ('028'). ***'
   EXIT 028
END
"DELSTACK"
CALL BUILD_JOBCARD
CALL BUILD_PACKAGE_IKJEFT01_JCL
CALL BUILD_PACKAGE_SYSIN
CALL BUILD_BIND_IKJEFT01_JCL
CALL BUILD_BIND_SYSIN
CALL BUILD_IDCAMS_DELETE_MEMBER
CALL WRITE_DB2_PARMS
CALL SUBMIT_JOB
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
GET_DB2_PACKAGE_BIND_PARMS:
MEMBSW = 'NO'
DB2PARM = SYSDSN("'"PARMPDS"("WORKORDR")'")
IF DB2PARM = 'MEMBER NOT FOUND' THEN DO
   SAY '*** MEMBER ERROR: MEMBER NOT FOUND ('PARMPDS'('WORKORDR'). ***'
   SAY '*** DB2BPROD EXEC SUBTASK CANCELLED - RETURN CODE: ('028'). ***'
   EXIT 028
END
"ALLOC DA('"PARMPDS"("WORKORDR")') FILE(PARMDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('PARMPDS'('WORKORDR'). ***'
   SAY '*** DB2BPROD EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR PARMDD (FINIS STEM QPARM."
SRC = RC
"FREE FILE(PARMDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('PARMPDS'('WORKORDR'). ***'
   SAY '*** DB2BPROD EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
DO I = 1 TO QPARM.0
   PMEMBER = WORD(QPARM.I,1)
   COLLID = WORD(QPARM.I,2)
   IF PMEMBER = MEMBER THEN DO
      MEMBSW = 'YES'
      DB2COLL = COLLID
      DB2PLAN = COLLID
      LEAVE
   END
END
RETURN

/*     */
BUILD_JOBCARD:
JN = WORKORDR
JCL.1 = "//"JN" JOB" ACCTCDE",'"TSOID".DB2.PKG.BIND',NOTIFY="TSOID","
JCL.2 = "//          CLASS=C,MSGCLASS=S,MSGLEVEL=(1,1),REGION=6M"
JCL.3 = "/*JOBPARM SYSAFF=SYSB"
DO J = 1 TO 3
   QUEUE JCL.J
END
RETURN

/*     */
BUILD_PACKAGE_IKJEFT01_JCL:
RCDCNT = 8
JCL.1 = "//PACKAGE  EXEC PGM=IKJEFT01,DYNAMNBR=20"
JCL.2 = "//DBRMLIB  DD DSN="DBRMLIB",DISP=SHR"
JCL.3 = "//SYSTSPRT DD SYSOUT=*"
JCL.4 = "//SYSPRINT DD SYSOUT=*"
JCL.5 = "//SYSUDUMP DD SYSOUT=*"
JCL.6 = "//SYSOUT   DD SYSOUT=*"
JCL.7 = "//SYSIN    DD DUMMY,DCB=BLKSIZE=80"
JCL.8 = "//SYSTSIN  DD *"
DO J = 1 TO RCDCNT
   QUEUE JCL.J
END
RETURN

/*     */
BUILD_PACKAGE_SYSIN:
RCDCNT = 16
RCD.1 = " DSN SYSTEM("DBSUBSYS")"
RCD.2 = " BIND PACKAGE ("DB2COLL") -"
RCD.3 = "      OWNER("TSOID") -"
RCD.4 = "      QUALIFIER(BUILD) -"
RCD.5 = "      MEMBER ("MEMBER") - "
RCD.6 = "      SQLERROR(NOPACKAGE) -"
RCD.7 = "      VALIDATE(BIND) -"
RCD.8 = "      FLAG(I) -"
RCD.9 = "      ISOLATION (CS) -"
RCD.10 = "      RELEASE(COMMIT) -"
RCD.11 = "      EXPLAIN(NO) -"
RCD.12 = "      CURRENTDATA(NO) -"
RCD.13 = "      ACTION (ADD) -"
RCD.14 = "      ENABLE(*)"
RCD.15 = " END"
RCD.16 = "/*"
DO R = 1 TO RCDCNT
   QUEUE RCD.R
END
RETURN

/*     */
BUILD_BIND_IKJEFT01_JCL:
RCDCNT = 8
JCL.1 = "//BIND     EXEC PGM=IKJEFT01,DYNAMNBR=20,COND=(0,LT,PACKAGE)"
JCL.2 = "//DBRMLIB  DD DSN="DBRMLIB",DISP=SHR"
JCL.3 = "//SYSTSPRT DD SYSOUT=*"
JCL.4 = "//SYSPRINT DD SYSOUT=*"
JCL.5 = "//SYSUDUMP DD SYSOUT=*"
JCL.6 = "//SYSOUT   DD SYSOUT=*"
JCL.7 = "//SYSIN    DD DSN="GRANTPDS"("MEMBER"),DISP=SHR"
JCL.8 = "//SYSTSIN  DD *"
DO J = 1 TO RCDCNT
   QUEUE JCL.J
END
RETURN

/*     */
BUILD_BIND_SYSIN:
RCDCNT = 16
REC.1 = " DSN SYSTEM("DBSUBSYS")"
REC.2 = " BIND PLAN ("DB2PLAN") -"
REC.3 = "      OWNER("TSOID") -"
REC.4 = "      VALIDATE(BIND) -"
REC.5 = "      FLAG(I) -"
REC.6 = "      ACQUIRE(USE) -"
REC.7 = "      ISOLATION(CS) -"
REC.8 = "      RELEASE(COMMIT) -"
REC.9 = "      EXPLAIN(NO) -"
REC.10 = "      ACTION (REPLACE) -"
REC.11 = "      PKLIST("DB2COLL".*) -"
REC.12 = "      ENABLE(*)"
REC.13 = " RUN PROGRAM(DSNTIAD) PLAN(DSNTIA31) -"
REC.14 = "     LIB('"RUNLIB"')"
REC.15 = " END"
REC.16 = "/*"
DO R = 1 TO RCDCNT
   QUEUE REC.R
END
RETURN

/*     */
BUILD_IDCAMS_DELETE_MEMBER:
RCDCNT = 8
JCL.1 = "//DELETE   EXEC PGM=IDCAMS,COND=(0,LT)"
JCL.2 = "//ISPFILE  DD DSN="OUTPDS",DISP=SHR"
JCL.3 = "//SYSPRINT DD SYSOUT=*"
JCL.4 = "//SYSIN    DD *"
JCL.5 = "  DELETE   "OUTPDS"("MEMBER")  -"
JCL.6 = "  FILE(ISPFILE)"
JCL.7 = "/*"
JCL.8 = "//"
DO J = 1 TO RCDCNT
   QUEUE JCL.J
END
RETURN

/*     */
WRITE_DB2_PARMS:
"ALLOC DA('"OUTPDS"("OUTMEMB")') FILE(DB2DD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('OUTPDS'('OUTMEMB'). ***'
   SAY '*** DB2BPROD EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW DB2DD (FINIS"
SRC = RC
"FREE FILE(DB2DD)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING ('OUTPDS'('OUTMEMB').  ***'
   SAY '*** DB2BPROD EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
SUBMIT_JOB:
"SUBMIT ('"OUTPDS"("MEMBER")')"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** SUBMIT ERROR: ('OUTPDS'('MEMBER'). ***'
   SAY '*** DB2BPROD EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN
