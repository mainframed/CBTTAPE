/*  REXX EXEC : ( DB2GRNTP ).
    FUNCTION  : CALLED BY THE DB2 BATCH AND CICS COMPILE TASKS:
                ( DB2B AND DB2C ).
                A MEMBER WILL BE WRITTEN TO TRACK THE GRANT DB2 TASK
                FOR A BIND PLAN EXECUTION.
    INPUT     : DB2PLAN AND DB2SUBSYS PARMS.
    OUTPUT    : SYSS.TESTCCF.GRANT.CNTLLIB(&DB2PLAN).
                                                                     */
TRACE O
/*      */
HOUSE_KEEPING:
ADDRESS TSO
ARG DB2PLAN DBSUBSYS
PARMS = 'DB2PLAN DBSUBSYS'
SRC = 0
DO I = 1 TO 2
   IPARM = WORD(PARMS,I)
   XPARM = VALUE(IPARM)
   IF XPARM ¬= '' THEN ITERATE
   SAY '*** PARM ERROR: MISSING PARM VALUE FOR ('IPARM'). ***'
   SAY '*** DB2GRNTP EXEC TASK CANCELLED - RC = (024). ***'
   EXIT 024
END
IF DBSUBSYS = 'DSNX' THEN EXIT 0
GRANTPDS = 'SYSS.TESTCCF.GRANT.CNTLLIB'
UCNTL = SYSDSN("'"GRANTPDS"'")
IF UCNTL = 'DATASET NOT FOUND' THEN DO
   IF SRC ¬= 0 THEN DO
      SAY '*** ALLOCATE ERROR: DATASET NOT FOUND ('GRANTPDS'). ***'
      SAY '*** DB2GRNTP EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
      EXIT 100
   END
END
X = MSG("OFF")
"FREE FILE(PARMDD)"

/*     */
MAIN_ROUTINE:
CALL CHECK_GRANT_CNTLLIB
"DELSTACK"
INTERPRET CALL LABEL
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
CHECK_GRANT_CNTLLIB:
LABEL = 'WRITE_GRANT_PARMS'
GMEMB = SYSDSN("'"GRANTPDS"("DB2PLAN")'")
IF GMEMB = 'MEMBER NOT FOUND' THEN RETURN
"ALLOC DA('"GRANTPDS"("DB2PLAN")') F(PARMDD) SHR"
"EXECIO * DISKR PARMDD (FINIS STEM GPARM."
"FREE F(PARMDD)"
LABEL = 'REWRITE_GRANT_PARMS'
DO I = 1 TO GPARM.0
   IF GPARM.I = DBSUBSYS THEN LABEL = 'NOOP_GRANT_PARMS'
END
RETURN

/*     */
NOOP_GRANT_PARMS:
RETURN

/*     */
WRITE_GRANT_PARMS:
"ALLOC DA('"GRANTPDS"("DB2PLAN")') FILE(PARMDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('GRANTPDS'('DB2PLAN'). ***'
   SAY '*** DB2GRNTP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT 100
END
RECORD = DBSUBSYS
QUEUE RECORD
"EXECIO 1 DISKW PARMDD (FINIS"
SRC = RC
"FREE FILE(PARMDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING ('GRANTPDS'('DB2PLAN'). ***'
   SAY '*** DB2GRNTP EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
REWRITE_GRANT_PARMS:
"ALLOC DA('"GRANTPDS"("DB2PLAN")') FILE(PARMDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('GRANTPDS'('DB2PLAN'). ***'
   SAY '*** DB2GRNTP EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT 100
END
DO I = 1 TO GPARM.0
   QUEUE GPARM.I
END
RECORD = DBSUBSYS
QUEUE RECORD
NUMRCDS = QUEUED()
"EXECIO "NUMRCDS" DISKW PARMDD (FINIS"
SRC = RC
"FREE FILE(PARMDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING ('GRANTPDS'('DB2PLAN'). ***'
   SAY '*** DB2GRNTP EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN
