PROC 0 DEBUG(X)
CONTROL MAIN NOLIST NOCONLIST NOSYMLIST NOMSG
IF &DEBUG=DEBUG THEN CONTROL MAIN LIST CONLIST SYMLIST MSG
ISPEXEC CONTROL ERRORS RETURN

/********************************************************/
/**                                                    **/
/**    CONTROL-M QUICK SCHEDULE DEFINITION             **/
/**                                                    **/
/**    CLIST CTMCK30  - EXIT OPTIONS WINDOW            **/
/**    FINAL DETERMINATION OF LIBRARY AND TABLE NAME   **/
/**                                                    **/
/********************************************************/

ISPEXEC VGET (LIB SCHEDTB GRP  SGRP SKELMEM)
SET &OLIB = &LIB
SET &OTABLE = &NRSTR(&SCHEDTB)
SET &NUL=()
SET &CUR=CSAVE
SET &VALID=N

ISPEXEC VGET (LIB SCHEDTB)
SET &XLIB = &STR(LIB: )&LIB
SET &XTAB = &STR(TABLE: )&NRSTR(&SCHEDTB)

DO WHILE (&VALID=N)

  ISPEXEC DISPLAY PANEL(CTMPK03W) CURSOR(&CUR)

  SET &ENDED = N
  IF (&RESP = END) OR (&RESP=RETURN) THEN +
    DO
      SET &ENDED = Y
      ISPEXEC VPUT (OLIB OTABLE ENDED)
      EXIT QUIT
    END

  SET &VALID=Y
  IF (&NRSTR(&OTABLE)=&NUL)  THEN +
    DO
      SET &VALID=N
      SET &CUR=OTABLE
      ISPEXEC SETMSG MSG(CTMK001F)
    END

  IF (&OLIB=&NUL) THEN +
    DO
      SET &VALID=N
      SET &CUR=OLIB
      ISPEXEC SETMSG MSG(CTMK001H)
    END

  IF ((&CSAVE=&NUL) AND (&CCREATE=&NUL) OR +
     ((&CSAVE¬=&NUL) AND (&CCREATE¬=&NUL)) THEN +
    DO
      SET &VALID=N
      SET &CUR=CSAVE
      ISPEXEC SETMSG MSG(CTMK003C)
    END

  IF (&CCREATE¬=Y) AND (&CCREATE¬=N) AND (&CCREATE¬=&NUL) THEN +
    DO
      SET &VALID=N
      SET &CUR=CCREATE
      ISPEXEC SETMSG MSG(CTMK001K)
    END

  IF (&CSAVE¬=Y) AND (&CSAVE¬=N) AND (&CSAVE¬=&NUL) THEN +
    DO
      SET &VALID=N
      SET &CUR=CSAVE
      ISPEXEC SETMSG MSG(CTMK001K)
    END

  IF ((&CSAVE=N) OR (&CCREATE=N)) AND (&VALID=Y) THEN +
    DO
      SET &ENDED = Y
      ISPEXEC VPUT (OLIB OTABLE ENDED)
      EXIT QUIT
    END


   IF (&OLIB=&LIB) THEN SET &LIBVALID=1
   ELSE +
     IF &VALID=Y THEN +
     DO
       SET &LIBVALID = 1
       SET &DSORG = PO
       SET &LRECL = 80
       SET &RECFM = FB

       ISPEXEC LMFREE DATAID(CTMPKF00)
       ISPEXEC LMINIT DATAID(CTMPKF00) DATASET('&OLIB') +
                           ENQ(SHR) ORG(DSORG)
     ISPEXEC LMOPEN DATAID(&CTMPKF00) LRECL(LRECL) RECFM(RECFM) +
                      OPTION(INPUT)
       SET &RC = &LASTCC
       IF &RC ¬= 0 THEN +
          DO
            ISPEXEC SETMSG MSG(CTMK003E)
            SET &LIBVALID = 0
          END

       IF &LIBVALID  = 1 THEN +
          DO
            IF &DSORG ¬= PO  THEN +
             DO
               ISPEXEC SETMSG MSG(CTMK001D)
               SET &LIBVALID = 0
             END
            IF &LRECL ¬= 80  THEN +
             DO
               ISPEXEC SETMSG MSG(CTMK001C)
               SET &LIBVALID = 0
             END
            IF ((&RECFM ¬= FB) AND (&RECFM ¬= F))  THEN +
             DO
               ISPEXEC SETMSG MSG(CTMK001B)
               SET &LIBVALID = 0
             END
          END

       IF &LIBVALID = 0 THEN +
         DO
           SET &CUR=OLIB
           SET &VALID=N
         END
     END /* OF VALIDATING THE LIBRARY */

   /********************************************************/
   /**                                                    **/
   /**    DOES THE REQUESTED MEMBER EXIST                 **/
   /**                                                    **/
   /********************************************************/

  IF (&VALID=Y) AND (&LIBVALID=1) THEN +
    DO
      IF (&LIB=&OLIB THEN +
      DO
        ISPEXEC LMFREE DATAID(CTMPKF00)
        ISPEXEC LMINIT DATAID(CTMPKF00) DATASET('&OLIB') +
                            ENQ(SHR) ORG(DSORG)
      ISPEXEC LMOPEN DATAID(&CTMPKF00) LRECL(LRECL) RECFM(RECFM) +
                       OPTION(INPUT)
      END
      ISPEXEC LMMFIND DATAID(&CTMPKF00)  MEMBER(&NRSTR(&OTABLE))
      SET &RC = &LASTCC
      IF &RC = 0 THEN SET &MFOUND=Y
      ELSE SET &MFOUND=N
    END


  IF (&CTMPKF00¬=&NUL) THEN +
  DO
    ISPEXEC LMCLOSE DATAID(&CTMPKF00)
    ISPEXEC LMFREE  DATAID(&CTMPKF00)
  END

  IF (&VALID=Y) AND (&MFOUND=Y) AND (&CCREATE=Y) THEN +
    DO
      SET &VALID=N
      ISPEXEC SETMSG MSG(CTMK003F)
      SET &CUR=OTABLE
    END

  IF (&VALID=Y) AND (&MFOUND=N) AND (&CSAVE=Y) THEN +
    DO
      SET &VALID=N
      ISPEXEC SETMSG MSG(CTMK003G)
      SET &CUR=OTABLE
    END


  ISPEXEC VPUT (OLIB OTABLE ENDED MFOUND)

END
EXIT
