/*   REXX EXEC : ( TLMSDISP )
     FUNCTION  : READS THE DISPATCH GENERATED TLMS UPDATE SYSIN
                 DATASET TO EXTRACT THE TAPE VOLSER(S) AND INVOKE
                 THE TLMSRPTS PROGRAM SERVICES TO DISPLAY THE TAPE
                 DATASET INFORMATION TO DETERMINE IF THE TAPE VOLSER
                 INDICATOR SHOULD BE SET TO SCRATCH=YES.
     INPUT     : ( SYSS.DISPATCH.V5R0M0.TMSFILE ).
                                                                      */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
X = MSG("OFF")
WORKRCD = '***** DUMMY *****'
TLMSDSN.1  = 'SYSS.TLMS.V5R3M0.VMF'
TLMSDSN.2  = 'SYSS.TLMS.V5R3M0.VMF'
TLMSDSN.3  = 'SYSS.TLMS.V5R3M0.VMF'
TLMSDSN.4  = 'SYSS.TLMS.V5R3M0.RMF.CLUSTER'
TLMSDSN.5 = 'SYSS.TLMS.V5R3M0.VMFINDEX.CLUSTER'
TLMSDSN.6 = 'SYSS.DISPATCH.TLMS.SYSIN'
TLMSDSN.7 = 'SYSS.DISPATCH.TLMS.SYSPRINT'
TLMSFIDD = 'CAIVMF CAIVMFI CAIVMFS CAIRMF CAIVMFXI SYSIN SYSPRINT'
TMSFILE = 'SYSS.DISPATCH.V5R0M0.TMSFILE'
LOADLIB = 'SYS1.TLMS.V5R3M0.CAILIB.LOADLIB'
/*
       M A I N   R O U T I N E   S E C T I O N     */
/*     */
MAIN_ROUTINE:
CALL READ_CHECK_DISPATCH_TMSFILE
IF DISPSW = 'NO' THEN EXIT 0
CALL ALLOCATE_WORK_DATASETS
CALL WRITE_TLMS_DVA_SYSIN
CALL ALLOCATE_TLMS_DATASETS
TASK = 'DVA'
CALL FETCH_TLMS_LOAD_MODULE
CALL FREE_DATASETS
CALL READ_CHECK_TLMS_DVA_SYSPRINT
IF UPVSW = 'NO' THEN EXIT 0
CALL ALLOCATE_WORK_DATASETS
CALL WRITE_TLMS_UPV_SYSIN
CALL ALLOCATE_TLMS_DATASETS
TASK = 'UPV'
CALL FETCH_TLMS_LOAD_MODULE
CALL FREE_DATASETS
EXIT 0
/*
       S U B R O U T I N E   S E C T I O N     */
/*     */
READ_CHECK_DISPATCH_TMSFILE:
T = 0
DISPSW = 'NO'
TDSN = SYSDSN("'"TMSFILE"'")
IF TDSN = 'DATASET NOT FOUND' THEN DO
   SAY '***********************************************************************'
   SAY '*** DATASET ALLOCATE ERROR: ('TMSFILE'). ***'
   SAY '*** DISPATCH TMSFILE INPUT DATASET NOT FOUND. ***'
   SAY '*** TLMS TAPE DATASET VOLSER LIST FACILITY CANCELLED. ***'
   SAY '***********************************************************************'
   EXIT 028
END
"ALLOC DA('"TMSFILE"') FILE(DISPDD) SHR"
"EXECIO * DISKR DISPDD (STEM TLMS. FINIS"
SRC = RC
"FREE FILE(DISPDD)"
IF SRC ¬= 0 THEN RETURN
IF TLMS.0 = 0 THEN RETURN
DISPSW = 'YES'
DO I = 1 TO TLMS.0
   UPDKEY = WORD(TLMS.I,1)
   IF UPDKEY = 'UPD' THEN DO
      T = T + 1
      TAPEVOL.T = SUBSTR(TLMS.I,5,6)
   END
END
TAPECNT = T
RETURN
/*     */
ALLOCATE_WORK_DATASETS:
ADDRESS TSO
"DELSTACK"
QUEUE WORKRCD
"ALLOC DA('"TLMSDSN.6"') FILE(WORKDD) SHR"
"EXECIO 1 DISKW WORKDD (FINIS"
"FREE FILE(WORKDD)"
QUEUE WORKRCD
"ALLOC DA('"TLMSDSN.7"') FILE(WORKDD) SHR"
"EXECIO 1 DISKW WORKDD (FINIS"
"FREE FILE(WORKDD)"
RETURN
/* ***       N O O P
"DELETE ('"TLMSDSN.6"')  NONVSAM SCRATCH PURGE FILE(WORKDD)"
"FREE DA('"TLMSDSN.6"')"
"FREE FILE(WORKDD)"
"ALLOC DA('"TLMSDSN.7"') FILE(WORKDD)"
"DELETE ('"TLMSDSN.7"')  NONVSAM SCRATCH PURGE FILE(WORKDD)"
"FREE DA('"TLMSDSN.7"')"
"FREE FILE(WORKDD)"
"ATTRIB ATTRB1 DSORG(PS) RECFM(F B) LRECL(80) BLKSIZE(80)"
"ATTRIB ATTRB2 DSORG(PS) RECFM(F B A) LRECL(133) BLKSIZE(133)"
W = 6
"ALLOC DA('"TLMSDSN.6"') NEW SPACE(1,0) CYLINDERS USING(ATTRB1) UNIT(SMS)"
SRC = RC
"FREE DA('"TLMSDSN.6"')"
"FREE ATTRLIST(ATTRB1)"
IF SRC ¬= 0 THEN SIGNAL WORK_DATASET_ALLOCATE_ERROR
W = 7
"ALLOC DA('"TLMSDSN.7"') NEW SPACE(1,0) CYLINDERS USING(ATTRB2) UNIT(SMS)"
SRC = RC
"FREE ATTRLIST(ATTRB2)"
"FREE DA('"TLMSDSN.7"')"
IF SRC = 0 THEN RETURN
                             *** */
/*     */
WORK_DATASET_ALLOCATE_ERROR:
SAY '**************************************************************************'
SAY '*** ALLOCATE ERROR: RETURN CODE: ('SRC'). ***'
SAY '*** ERROR ALLOCATING DATASET: ('TLMSDSN.W'). ***'
SAY '*** TLMS TAPE DATASET VOLSER LIST FACILITY CANCELLED. ***'
SAY '**************************************************************************'
"FREE DA("TLMSDSN.W")"
EXIT SRC
/*     */
WRITE_TLMS_DVA_SYSIN:
"DELSTACK"
"FREE FILE(TLMSIN)"
"ALLOC DA('"TLMSDSN.6"') FILE(TLMSIN) SHR"
DO I = 1 TO TAPECNT
   TLMSRCD = 'DVA 'TAPEVOL.I
   QUEUE TLMSRCD
END
"EXECIO "TAPECNT" DISKW TLMSIN (FINIS"
SRC = RC
"FREE FILE(TLMSIN)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO WRITE ERROR: RETURN CODE: ('SRC'). ***'
   SAY '*** ERROR WRITING ('TLMSDSN.6') TLMS DVA SYSIN RECORDS. ***'
   SAY '*** TLMS TAPE DATASET VOLSER LIST FACILITY CANCELLED. ***'
   EXIT SRC
END
RETURN
/*     */
ALLOCATE_TLMS_DATASETS:
CALL FREE_DATASETS
DO D = 1 TO 7
   TLMSDD = WORD(TLMSFIDD,D)
   "ALLOC DA('"TLMSDSN.D"') FILE("TLMSDD") SHR"
   SRC = RC
   IF SRC ¬= 0 THEN DO
      SAY '*** ALLOCATE ERROR: RETURN CODE: ('SRC'). ***'
      SAY '*** ERROR ALLOCATING DATASET: ('TLMSDSN.D'). ***'
      SAY '*** TLMS TAPE DATASET VOLSER LIST FACILITY CANCELLED. ***'
      CALL FREE_DATASETS
      EXIT SRC
   END
END
RETURN
/*     */
FETCH_TLMS_LOAD_MODULE:
ADDRESS TSO
"CALL '"LOADLIB"(TLMSRPTS)'"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '***********************************************************************'
   SAY '*** TLMS UTILITY ERROR: RETURN CODE: ('SRC'). ***'
   SAY '*** ('TASK') UTILITY  FUNCTION DID NOT COMPLETE SUCCESSFULLY. ***'
   SAY '***********************************************************************'
   CALL FREE_DATASETS
   EXIT SRC
END
RETURN
/*     */
READ_CHECK_TLMS_DVA_SYSPRINT:
U = 0
UPVSW = 'NO'
"FREE FILE(TLMSPRT)"
"ALLOC DA('"TLMSDSN.7"') FILE(TLMSPRT) SHR"
"EXECIO * DISKR TLMSPRT (STEM DVA. FINIS"
SRC = RC
"FREE FILE(TLMSPRT)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: RETURN CODE: ('SRC'). ***'
   SAY '*** ERROR READING ('TLMSDSN.7') TLMS SYSOUT RECORDS. ***'
   SAY '*** TLMS TAPE DATASET VOLSER LIST FACILITY CANCELLED. ***'
   EXIT SRC
END
DO T = 1 TO TAPECNT
   VSNKEY = 'VSN='TAPEVOL.T
   DO I = 1 TO DVA.0
      VOLKEY = POS(VSNKEY,DVA.I)
      IF VOLKEY ¬= 0 THEN DO
         N = WORDS(DVA.I)
         CDS = WORD(DVA.I,N)
         R = I + 3
         DVARCD = STRIP(DVA.R)
         VSNKEY = POS('DSN=',DVARCD)
         V = VSNKEY + 4
         TAPEDSN = SUBSTR(DVARCD,V)
         VCIKEY = POS('VCI.DCAR.NEW',TAPEDSN)
         IF VCIKEY ¬= 0 THEN DO
            UPVSW = 'YES'
            U = U + 1
            UPDVOL.U = TAPEVOL.T
            SAY '++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++'
            SAY '<<< TAPE VSN: ('TAPEVOL.T') *** TAPE DSN: ('TAPEDSN') >>>'
            SAY '++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++'
         END
         LEAVE
      END
   END
END
UPDCNT = U
RETURN
/*     */
WRITE_TLMS_UPV_SYSIN:
"DELSTACK"
"FREE FILE(TLMSIN)"
"ALLOC DA('"TLMSDSN.6"') FILE(TLMSIN) SHR"
DO I = 1 TO UPDCNT
   TLMSRCD = 'UPV 'UPDVOL.I',SCRATCH=YES,'CDS
   QUEUE TLMSRCD
END
"EXECIO "UPDCNT" DISKW TLMSIN (FINIS"
SRC = RC
"FREE FILE(TLMSIN)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO WRITE ERROR: RETURN CODE: ('SRC'). ***'
   SAY '*** ERROR WRITING ('TLMSDSN.6') TLMS UPV SYSIN RECORDS. ***'
   SAY '*** TLMS TAPE DATASET VOLSER LIST FACILITY CANCELLED. ***'
   EXIT SRC
END
RETURN
/*     */
FREE_DATASETS:
ADDRESS TSO
"FREE FILE("TLMSFIDD")"
RETURN
