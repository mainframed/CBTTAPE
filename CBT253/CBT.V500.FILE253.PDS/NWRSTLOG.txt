/*  REXX EXEC : ( NWRSTLOG ).
    FUNCTION  : LAN SERVER RESTORE FROM MVS/ESA CENTRAL ARCHIVE HOST
                WRITE JOURNAL ENTRY TO RECORD RESTORE FUNCTION TASKS.
                EXECUTED ONLY IF CONDITION CODE ZERO IS RETURNED FROM
                STEP 1 ( STEPNAME=RESTORE ) IN JOBSTREAM.
    INPUT     : ( SYSS.NETCA.RESTORE.JOURNAL ).
                ( &&USERID.DIALOG.ISPFILE ).
    OUTPUT    : ( SYSS.NETCA.RESTORE.JOURNAL ).
                                                                    */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
ARG JRNALMEM
IF JRNALMEM = '' THEN DO
   SAY '*** PARM ERROR: MISSING PARM VALUE FOR JOURNAL MEMBER. ***'
   SAY '*** NWRSTLOG EXEC TASK CANCELLED - RC = (024). ***'
   EXIT 024
END
CDATE = DATE(U)
CTIME = TIME()
TSOID = SYSVAR(SYSUID)
JOURLIB = 'PTECH3.NETCA.RESTORE.JOURNAL'
JOURPDS = 'PTECH3.NETCA.RESTORE.SCRIPT.CNTLLIB'
X = MSG("OFF")
NWDSN = SYSDSN("'"JOURLIB"'")
IF NWDSN = 'DATASET NOT FOUND' THEN DO
   SAY '*** DATASET ERROR: DATASET NOT FOUND FOR ('JOURLIB'). ***'
   SAY '*** NWRSTLOG EXEC SUBTASK CANCELLED - RETURN CODE: (028). ***'
   EXIT SRC
END
"FREE FILE(NWDD)"

/*     */
MAIN_ROUTINE:
CALL GET_JOURNAL_PARMS
CALL READ_JOURNAL_DATASET
CALL WRITE_JOURNAL_DATASET
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
GET_JOURNAL_PARMS:
"ALLOC DA('"JOURPDS"("JRNALMEM")') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('JOURPDS'('JRNALMEM'). ***'
   SAY '*** NWRSTLOG EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR NWDD (FINIS STEM PARM."
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('JOURPDS'('JRNALMEM'). ***'
   SAY '*** NWRSTLOG EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
SERVER = STRIP(PARM.1)
RESTVOL = STRIP(PARM.2)
RESTDSN = STRIP(PARM.3)
RESTDPN = STRIP(PARM.4)
RESTFN = STRIP(PARM.5)
RETURN

/*     */
READ_JOURNAL_DATASET:
"ALLOC DA('"JOURLIB"') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('JOURLIB'). ***'
   SAY '*** NWRSTLOG EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR NWDD (FINIS STEM DATA."
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('JOURLIB'). ***'
   SAY '*** NWRSTLOG EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
WRITE_JOURNAL_DATASET:
"DELSTACK"
DO I = 1 TO DATA.0
   QUEUE DATA.I
END
RESTVOL = LEFT(RESTVOL,6)
RESTDSN = LEFT(RESTDSN,44)
RESTDPN = LEFT(RESTDPN,50)
RECORD = CDATE CTIME TSOID SERVER RESTVOL RESTDSN RESTDPN RESTFN
QUEUE RECORD
"ALLOC DA('"JOURLIB"') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('JOURLIB'). ***'
   SAY '*** NWRSTLOG EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW NWDD (FINIS"
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING DSN ('JOURLIB'). ***'
   SAY '*** NWRSTLOG EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN
