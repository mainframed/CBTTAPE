/*  REXX EXEC : ( DB2PACKB ).
    FUNCTION  : CALLED BY THE DB2 BATCH COMPILE TASK ( DB2B ) TO
                BUILD THE DB2 PACKAGE SYSIN MEMBER.
                A PACKAGE JOB WILL BE WRITTEN AND SUBMITTED TO THE
                SYSA SYSTEM ( DB2SYSX - DSNX ).
    INPUT     : TSOID.USER.DBRMLIB(MEMBER).
    OUTPUT    : BATCH JOB WRITTEN: ( TSOID.DIALOG.ISPFILE(MEMBER))
                AND SUBMITTED TO SYSA SYSTEM CLASS=P.
                                                                     */
TRACE O
/*      */
HOUSE_KEEPING:
ADDRESS TSO
ARG TSOID ACCTCDE MEMBER DB2COLL
PARMS = 'TSOID ACCTCDE MEMBER DB2COLL'
EXIT 0       /* ***  N O O P   R E T U R N   *** */
SRC = 0
DO I = 1 TO 4
   IPARM = WORD(PARMS,I)
   XPARM = VALUE(IPARM)
   IF XPARM ¬= '' THEN ITERATE
   SAY '*** PARM ERROR: MISSING PARM VALUE FOR ('IPARM'). ***'
   SAY '*** DB2PACKB EXEC TASK CANCELLED - RC = (024). ***'
   EXIT 024
END
ISPFILE = 'SYSS.TESTCCF.ISPFILE.CNTLLIB'
UCNTL = SYSDSN("'"ISPFILE"'")
IF UCNTL = 'DATASET NOT FOUND' THEN DO
   IF SRC ¬= 0 THEN DO
      SAY '*** ALLOCATE ERROR: DATASET NOT FOUND ('ISPFILE'). ***'
      SAY '*** DB2PACKB EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
      EXIT 100
   END
END
X = MSG("OFF")
"FREE FILE(ISPDD)"

/*     */
MAIN_ROUTINE:
DB2SYS = 'DSNX'
CALL BUILD_PACKAGE_JOBCARD
CALL BUILD_IKJEFT01_EXECJCL
CALL BUILD_PACKAGE_SYSIN
CALL BUILD_IDCAMS_DELETE_MEMBER
CALL WRITE_PACKAGE_JOB
CALL SUBMIT_PACKAGE_JOB
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
BUILD_PACKAGE_JOBCARD:
JN = MEMBER
"DELSTACK"
JCL.1 = "//"JN" JOB" ACCTCDE",'"TSOID".DB2.PACKAGE',NOTIFY="TSOID","
JCL.2 = "//          CLASS=P,MSGCLASS=S,MSGLEVEL=(1,0),REGION=6M"
JCL.3 = "/*JOBPARM SYSAFF=SYSA"
DO J = 1 TO 3
   QUEUE JCL.J
END
RETURN

/*     */
BUILD_IKJEFT01_EXECJCL:
RCDCNT = 8
JCL.1 = "//DSNXPKG  EXEC PGM=IKJEFT01,DYNAMNBR=20,COND=(0,LT,DSNYPKG)"
JCL.2 = "//DBRMLIB  DD DSN="TSOID".USER.DBRMLIB,DISP=SHR"
JCL.3 = "//SYSTSPRT DD SYSOUT=*"
JCL.4 = "//SYSPRINT DD SYSOUT=*"
JCL.5 = "//SYSUDUMP DD SYSOUT=*"
JCL.6 = "//SYSOUT   DD SYSOUT=*"
JCL.7 = "//SYSIN    DD DUMMY,DCB=BLKSIZE=80"
JCL.8 = "//SYSTSIN  DD *"
DO J = 1 TO RCDCNT
   QUEUE JCL.J
END
RETURN

/*     */
BUILD_PACKAGE_SYSIN:
RCDCNT = 16
RCD.1 = " DSN SYSTEM("DB2SYS")"
RCD.2 = " BIND PACKAGE ("DB2COLL") -"
RCD.3 = "      OWNER("TSOID") -"
RCD.4 = "      QUALIFIER(BUILD) -"
RCD.5 = "      MEMBER ("MEMBER") - "
RCD.6 = "      SQLERROR(NOPACKAGE) -"
RCD.7 = "      VALIDATE(BIND) -"
RCD.8 = "      FLAG(I) -"
RCD.9 = "      ISOLATION (CS) -"
RCD.10 = "      RELEASE(COMMIT) -"
RCD.11 = "      EXPLAIN(YES) -"
RCD.12 = "      CURRENTDATA(NO) -"
RCD.13 = "      ACTION (REPLACE) -"
RCD.14 = "      ENABLE(*)"
RCD.15 = " END"
RCD.16 = "/*"
DO R = 1 TO RCDCNT
   QUEUE RCD.R
END
RETURN

/*     */
BUILD_IDCAMS_DELETE_MEMBER:
RCDCNT = 11
JCL.1 = "//DELETE   EXEC PGM=IDCAMS,COND=(0,LT,DSNXPKG)"
JCL.2 = "//ISPDD    DD DSN="ISPFILE",DISP=SHR"
JCL.3 = "//SYSPRINT DD SYSOUT=*"
JCL.4 = "//SYSIN    DD *"
JCL.5 = "  DELETE  "ISPFILE"("MEMBER")  FILE(ISPDD)"
JCL.6 = "  IF LASTCC = 8 THEN"
JCL.7 = "     DO"
JCL.8 = "       SET MAXCC = 0"
JCL.9 = "     END"
JCL.10 = "/*"
JCL.11 = "//"
DO J = 1 TO RCDCNT
   QUEUE JCL.J
END
RETURN

/*     */
WRITE_PACKAGE_JOB:
"ALLOC DA('"ISPFILE"("MEMBER")') FILE(ISPDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('ISPFILE'('MEMBER'). ***'
   SAY '*** DB2PACKB EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT 100
END
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW ISPDD (FINIS"
SRC = RC
"FREE FILE(ISPDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING ('ISPFILE'('MEMBER'). ***'
   SAY '*** DB2PACKB EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
SUBMIT_PACKAGE_JOB:
"SUBMIT ('"ISPFILE"("MEMBER")')"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** SUBMIT ERROR: ('ISPFILE'('MEMBER'). ***'
   SAY '*** DB2PACKB EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN
