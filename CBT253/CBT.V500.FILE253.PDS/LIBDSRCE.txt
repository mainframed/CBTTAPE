/*  REXX EXEC : ( LIBDSRCE ).
    FUNCTION: PERFORM MAINTENANCE CLEANUP OF THE CCF TEST SRCE MASTER
              ( SYS1.TESTSRCE.MASTER ) DATASET.

    INPUT   : LIBRARIAN GPO INDEX SYSOUT DATASETS:
              DSN = ( SYSS.TESTSRCE.LIBINDEX )

    OUTPUT  : PDS WITH LIBRARIAN DLM ( DELETE MEMBER ) JOBSTREAM:
              DSN = ( TSOID.USER.CNTLLIB(LIBDSRCJ).

            : JOURNAL DATASET:
              DSN = ( SYSS.TESTSRCE.DELETE.JOURNAL ).
                                                                      */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
PARSE UPPER ARG TSTINDEX
IF TSTINDEX = '' THEN DO
   SAY '*** PARM ERROR: GPO INDEX DATASET NAME NOT PASSED ON COMMAND LINE. ***'
   SAY '*** LIBDSRCE EXEC TASK CANCELLED. ***'
   EXIT 024
END
LBDSN = SYSDSN("'"TSTINDEX"'")
IF LBDSN = 'DATASET NOT FOUND' THEN DO
   SAY '*** DATASET ERROR: GPO INDEX DATASET ('TSTINDEX') NOT FOUND. ***'
   SAY '*** LIBDSRCE EXEC TASK CANCELLED. ***'
   EXIT 028
END
D = 0
X = MSG("OFF")
"FREE FILE(LIBDD)"
TSOID = SYSVAR(SYSUID)
ACCTCDE = '5304010530000000'
PDSMEMB = 'LIBDSRCJ'
SUBPDS = TSOID'.USER.CNTLLIB'
MASTER = 'SYS1.TESTSRCE.MASTER'
CALL READ_TEST_LIBINDEX
/*     */
MAIN_ROUTINE:
DO I = 1 TO TDATA.0
   VALID_MODULE = POS('$NOJCL',TDATA.I)
   IF VALID_MODULE = 0 THEN ITERATE
   MODULE = WORD(TDATA.I,1)
   PASSWORD = WORD(TDATA.I,2)
   D = D + 1
   DLMRCD.D = "-DLM "MODULE","PASSWORD
END
DLMCNT = D
CALL BUILD_LIBDELET_JOB
CALL WRITE_LIBDELET_JOB
CALL SUBMIT_LIBDELET_JOB
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
READ_TEST_LIBINDEX:
"ALLOC DA('"TSTINDEX"') FILE(LIBDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ( 'TSTINDEX' ). ***'
   SAY '*** DATASET IN USE OR UNAVAILABLE - RETURN CODE: (' SRC '). ***'
   SAY '*** LIBDSRCE EXEC SUBTASK CANCELLED. ***'
   EXIT SRC
END
"EXECIO * DISKR LIBDD (FINIS STEM TDATA."
SRC = RC
"FREE FILE(LIBDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET EXECIO ERROR: ( 'TSTINDEX' ). ***'
   SAY '*** EXECIO DATASET READ RETURN CODE: (' SRC '). ***'
   SAY '*** LIBDSRCE EXEC SUBTASK CANCELLED. ***'
   EXIT SRC
END
RETURN
/*     */
BUILD_LIBDELET_JOB:
JN = PDSMEMB
"DELSTACK"
JCL.1 = "//"JN"  JOB "ACCTCDE",'TECH.SUPP.LIBDELETE',NOTIFY="TSOID","
JCL.2 = "//             CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),REGION=6M"
JCL.3 = "/*JOBPARM SYSAFF=SYSA"
JCL.4 = "//LIBDLM   EXEC PGM=AFOLIBR"
JCL.5 = "//MASTER   DD DSN="MASTER",DISP=SHR"
JCL.6 = "//OSJOB    DD DUMMY"
JCL.7 = "//SYSPRINT DD SYSOUT=*"
JCL.8 = "//LIST     DD SYSOUT=*"
JCL.9 = "//INDEX    DD SYSOUT=*"
JCL.10 = "//SYSIN    DD *"
DO J = 1 TO 10
   QUEUE JCL.J
END
DO I = 1 TO DLMCNT
   QUEUE DLMRCD.I
END
JCL.1 = "-END"
JCL.2 = "/*"
JCL.3 = "//"
DO J = 1 TO 3
   QUEUE JCL.J
END
RETURN
/*     */
WRITE_LIBDELET_JOB:
"ALLOC DA('"SUBPDS"("PDSMEMB")') FILE(LIBDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ( 'SUBPDS'('PDSMEMB'). ***'
   SAY '*** DATASET IN USE OR UNAVAILABLE - RETURN CODE: (' SRC '). ***'
   SAY '*** LIBDSRCE EXEC SUBTASK CANCELLED. ***'
   EXIT SRC
END
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW LIBDD (FINIS"
SRC = RC
"FREE FILE(LIBDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** PDS WRITE ERROR: 'SUBPDS'('PDSMEMB'). ***'
   SAY '*** EXECIO WRITE RETURN CODE: (' SRC '). ***'
   SAY '*** LIBDSRCE EXEC SUBTASK CANCELLED. ***'
   EXIT SRC
END
RETURN
/*     */
SUBMIT_LIBDELET_JOB:
"SUBMIT ('"SUBPDS"("PDSMEMB")')"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** SUBMIT ERROR: MEMBER ( 'SUBPDS'('PDSMEMB'). ***'
   SAY '*** LIBDSRCE EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN
