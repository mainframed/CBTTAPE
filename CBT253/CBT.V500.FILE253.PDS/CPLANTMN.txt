/* REXX Capacity Planning TMON Analysis feed into DB2               */
lowers="abcdefghijklmnopqrstuvwxyz";
uppers="ABCDEFGHIJKLMNOPQRSTUVWXYZ";
outptr=1;
"execio * diskr TMONIN (finis stem tmon.";
do i = 1 to tmon.0;
    parse var tmon.i ansicc 2 tmondata;
    if word(tmondata,1)="TIME:" then do;
        tmondata=strip(subword(tmondata,3));
        do while(pos("  ",tmondata)¬=0);
           tmondata=overlay("*",tmondata,pos("  ",tmondata));
        end;
        tmondata=space(translate(space(tmondata,0)," ","*"));
        if wordpos("RPT",tmondata)¬=0 then ,
            tmondata=delword(tmondata,wordpos("RPT",tmondata),1);
        if wordpos("TOTAL",tmondata)¬=0 then ,
            tmondata=delword(tmondata,wordpos("TOTAL",tmondata),1);
        if wordpos("TRANS",tmondata)¬=0 then ,
            tmondata=delword(tmondata,wordpos("TRANS",tmondata),1);
        if wordpos("TRANSACTION",tmondata)¬=0 then ,
          tmondata=delword(tmondata,wordpos("TRANSACTION",tmondata),1);
        tmondata=translate(tmondata,lowers,uppers);
        k = words(tmondata);
        temp="";
        do j = 1 to k;
           tword1=translate(substr(word(tmondata,j),1,1),uppers,lowers);
           tword2=substr(word(tmondata,j),2);
           temp=temp tword1||tword2;
        end;
        if word(temp,1)="Daily" then temp="*All";
/*      if word(temp,1)="Terminal" then temp="*Terminal";   */
        if word(temp,1)="Pcics1" then temp="*Terminal";
        current_area = left(strip(temp),40);
        i = i +1;
        current_date = word(tmon.i,4);
        end;
    if word(tmondata,1)="TOTALS" then do;
        current_data=space(subword(tmondata,2));
        current_data=remove(",",current_data);
        k = words(current_data);
        temp="";
        do j = 1 to k;
            piece=word(current_data,j);
            if pos(".",piece)=0 then ,
               piece=piece".0000";
            if pos(":",piece)¬=0 then do;
               hh=0;
               if length(piece)>10 then ,
                  parse var piece hh ":" mm ":" ss "." frac;
               else if length(piece)>7 then ,
                  parse var piece mm ":" ss "." frac;
               piece=trunc((3600*hh)+(60*mm)+ss)||"."frac;
               end;
            temp=temp right(piece,14,"0");
        end;
        current_data=space(temp,0);
        parse var current_date mm "/" dd "/" yy;
        parse value weekday(mm"/"dd"/19"yy) with day julian;
        output.outptr=current_date||day||current_area||current_data;
        outptr=outptr+1;
        end;
end;
"EXECIO * DISKW TMONOUT (FINIS STEM OUTPUT.";
exit 0;
/* Remove Function */
Remove: procedure;
needle   = arg(1);
haystack = arg(2);
do while (pos(needle,haystack)¬=0);
    haystack=delstr(haystack,pos(needle,haystack),1);
end;
return haystack;
