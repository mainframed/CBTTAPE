/*  REXX EXEC : ( NWREALTY ).
    FUNCTION  : LAN SERVER RESTORE TO FACILITATE THE REALITY CS1
                VOL(REA/VOL2) RESTORE TO ALTERNATE PROD_SUPPORT VOLUME.
                BUILDS AND WRITES THE JOB JCL AND INPUT SCRIPT TO
                PERFORM THE LAN SERVER RESTORE AND SUBMITS THE JOB
                FOR EXECUTION ON THE SYSA SYSTEM CLASS=J.
    INPUT     : ( PTECH2.NETCA.CS1.REA.H )
                ( PTECH2.NETCA.CS1.REA.SYSPRINT ).
    OUTPUT    : ( PTECH3.NETCA.RESTORE.SCRIPT.CNTLLIB(&MEMBER).
                                                                    */
TRACE O
/*      */
HOUSE_KEEPING:
ADDRESS TSO
SERVER = 'CS1'                   /* LAN SERVER name */
RESTVOL = 'VOL2'                 /* LAN SERVER input restore volume */
LANVOL = 'REA'                   /* LAN SERVER alias restore volume */
ALTVOL = 'PROD_SUPPORT:'         /* LAN SERVER -ALT target volume */
DRCTPATH = '/REALITY/MMDATA/'    /* target directory path */
RESTFN = '*'                     /* directory path data file specs */
DATAKEY = 'VOLUME 'LANVOL' BACKUP SUCCESSFUL,'
NWPRINT = 'PTECH2.NETCA.'SERVER'.'LANVOL'.SYSPRINT'
CNTLLIB = 'PTECH3.NETCA.RESTORE.SCRIPT.CNTLLIB'
JOURLIB = 'PTECH3.NETCA.RESTORE.JOURNAL'
TEXTLIB = 'SYSS.NETCA.CONTROL.TEXTLIB'
LANID = 'TECH399'
TSOID = 'TECH251'
JOBNAME = 'PTTS41R'
CURTIME = TIME()
HH = SUBSTR(CURTIME,1,2)
MM = SUBSTR(CURTIME,4,2)
SS = SUBSTR(CURTIME,7,2)
TIMEP = HH||MM||SS
JSUFFIX = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
JMEMB = 'J'TIMEP                  /* JOURNAL PARMS MEMBER */
RMEMB = 'R'TIMEP                  /* RESTORE JOB JCL MEMBER */
OUTPDS = CNTLLIB
X = MSG("OFF")
"FREE FILE(NWDD)"
/*      M A I N   R O U T I N E   S E C T I O N     */
/*     */
MAIN_ROUTINE:
CALL GET_INPUT_CONTAINER_DATASET_NAME
CALL CHECK_CONTAINER_DATASET_CATALOG
CALL BUILD_JOBNAME_PARM
CALL BUILD_TCPIP_RESTORE_JOB
OUTMEMB = RMEMB
CALL WRITE_PDS_MEMBER
CALL BUILD_JOURNAL_PARMS
OUTMEMB = JMEMB
CALL WRITE_PDS_MEMBER
CALL SUBMIT_RESTORE_JOB
EXIT 0
/*
       S U B R O U T I N E S   S E C T I O N     */
/*     */
GET_INPUT_CONTAINER_DATASET_NAME:
DSW = 'NO'
NWDSN = SYSDSN("'"NWPRINT"'")
IF NWDSN = 'DATASET NOT FOUND' THEN DO
   SAY '***********************************************************************'
   SAY '*                                                                     *'
   SAY '* *** DATASET ERROR: DATASET NOT FOUND FOR ('NWPRINT').               *'
   SAY '* *** UNABLE TO DETERMINE THE INPUT CONTAINER DATASET FOR RESTORE.    *'
   SAY '* *** NWREALTY EXEC SUBTASK CANCELLED - RETURN CODE: (028).           *'
   SAY '*                                                                     *'
   SAY '***********************************************************************'
   EXIT 028
END
"ALLOC DA('"NWPRINT"') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '***********************************************************************'
   SAY '*                                                                     *'
   SAY '* *** DATASET ALLOCATE ERROR: ('NWPRINT').                            *'
   SAY '* *** UNABLE TO DETERMINE THE INPUT CONTAINER DATASET FOR RESTORE.    *'
   SAY '* *** NWREALTY EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC').         *'
   SAY '*                                                                     *'
   SAY '***********************************************************************'
   EXIT SRC
END
"EXECIO * DISKR NWDD (STEM DATA. FINIS"
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '***********************************************************************'
   SAY '*                                                                     *'
   SAY '* *** EXECIO READ ERROR: ('NWPRINT').                                 *'
   SAY '* *** UNABLE TO DETERMINE THE INPUT CONTAINER DATASET FOR RESTORE.    *'
   SAY '* *** NWREALTY EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC').         *'
   SAY '*                                                                     *'
   SAY '***********************************************************************'
   EXIT SRC
END
DO I = 1 TO DATA.0
   UPPER DATA.I
   DKEY = POS(DATAKEY,DATA.I)
   IF DKEY ¬= 0 THEN DO
      DSW = 'YES'
      N = I + 1
      PRINT_DATA = STRIP(DATA.N)
      LEAVE
   END
END
IF DSW = 'NO' THEN RETURN
CONTAINER_DSN = WORD(PRINT_DATA,3)
RESTDSN = STRIP(CONTAINER_DSN,"B","'")
RETURN
/*     */
CHECK_CONTAINER_DATASET_CATALOG:
X = OUTTRAP("LSTCAT.","*","CONCAT")
"LISTCAT ENT('"RESTDSN"') VOL"
IF SRC ¬= 0  THEN DO
   SAY '***********************************************************************'
   SAY '*                                                                     *'
   SAY '* *** DATASET ERROR: DATASET NOT FOUND FOR ('RESTDSN').               *'
   SAY '* *** THE INPUT TAPE CONTAINER DATASET WAS NOT FOUND IN A CATALOG.    *'
   SAY '* *** NWREALTY EXEC SUBTASK CANCELLED - RETURN CODE: (028).           *'
   SAY '*                                                                     *'
   SAY '***********************************************************************'
   EXIT 028
END
RETURN
/*     */
BUILD_JOBNAME_PARM:
RETURN
/* ***     n o o p     *** */
JN1 = 'NWR'
JN2 = SUBSTR(TSOID,5,3)
DO N = 1 TO 26
   JN3 = SUBSTR(JSUFFIX,N,1)
   JOBNAME = JN1||JN2||JN3
   JOB = SYSDSN("'"CNTLLIB"("JOBNAME")'")
   IF JOB = 'MEMBER NOT FOUND' THEN LEAVE
END
JN = JOBNAME
RETURN
/*     */
BUILD_TCPIP_RESTORE_JOB:
ADDRESS TSO
"DELSTACK"
JN = JOBNAME
LANPW = 'BCKLAN'
RESTDSN = "'"||RESTDSN||"'"
RESTPRM1 = DRCTPATH||RESTFN '-PDT ON -SUB ON -REP ON -'
RESTPRM2 = '-ALT 'ALTVOL||DRCTPATH'"'
RCD.1 = "SET INPUT VERIFY ON"
RCD.2 = "ON ERROR EXIT ERROR"
RCD.3 = "CONNECT "SERVER" "LANID" "LANPW
RCD.4 = "SEND -MODE RESTORE "RESTDSN" -"
RCD.5 = '    "!UARESTORE 'RESTPRM1
RCD.6 = '    '||RESTPRM2
JCL.1 = "//"JN"  JOB 5304010530000000,'"SERVER" REA RESTORE',NOTIFY="TSOID","
JCL.2 = "//             CLASS=F,MSGCLASS=G,MSGLEVEL=(1,1),REGION=7M"
JCL.3 = "//RESTORE  EXEC PGM=NUACLIEN"
JCL.4 = "//STEPLIB  DD DSN=SYS1.NETUA.V2R10M12.LOADLIB,DISP=SHR"
JCL.5 = "//         DD DSN=SYS1.NETCA.V1R1M1.H213CMT.LOADLIB,DISP=SHR"
JCL.6 = "//SYSPRINT DD SYSOUT=*"
JCL.7 = "//SYSIN DD *"
JCL.8 = "EXIT"
JCL.9 = "/*"
JCL.10 = "//WRITELOG EXEC PGM=IKJEFT01,DYNAMNBR=30,"
JCL.11 = "//   PARM='%NWRSTLOG "JMEMB"',COND=(0,LT,RESTORE)"
JCL.12 = "//SYSPRINT DD SYSOUT=*"
JCL.13 = "//SYSTSPRT DD SYSOUT=*"
JCL.14 = "//SYSIN    DD DUMMY"
JCL.15 = "//SYSLBC   DD DSN=SYS1.BRODCAST,DISP=SHR"
JCL.16 = "//SYSUADS  DD DSN=SYS1.UADS,DISP=SHR"
JCL.17 = "//SYSPROC  DD DSN=SYSS.TECH.COMMON.EXECLIB,DISP=SHR"
JCL.18 = "//SYSTSIN  DD DUMMY"
JCL.19 = "/*"
JCL.20 = "//DELETE   EXEC PGM=IDCAMS,COND=(0,LT,RESTORE)"
JCL.21 = "//CNTLDD   DD DSN="CNTLLIB",DISP=SHR"
JCL.22 = "//SYSPRINT DD SYSOUT=*"
JCL.23 = "//SYSIN    DD *"
JCL.24 = "  DELETE   "CNTLLIB"("JMEMB")  FILE(CNTLDD)"
JCL.25 = "  DELETE   "CNTLLIB"("RMEMB")  FILE(CNTLDD)"
JCL.26 = "/*"
JCL.27 = "//ABEND     EXEC PGM=ABENDWTO,COND=(4,GE,RESTORE)"
JCL.28 = "//"
DO J = 1 TO 7
   QUEUE JCL.J
END
DO R = 1 TO 6
   QUEUE RCD.R
END
DO J = 8 TO 28
   QUEUE JCL.J
END
RETURN
/*     */
BUILD_JOURNAL_PARMS:
"DELSTACK"
QUEUE SERVER
QUEUE RESTVOL
QUEUE RESTDSN
QUEUE DRCTPATH
QUEUE RESTFN
RETURN
/*     */
WRITE_PDS_MEMBER:
ADDRESS TSO
"ALLOC DA('"OUTPDS"("OUTMEMB")') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '***********************************************************************'
   SAY '*                                                                     *'
   SAY '* *** DATASET ALLOCATE ERROR: ('OUTPDS'('OUTMEMB').                   *'
   SAY '* *** UNABLE TO WRITE REALITY PROD_SUPPORT RESTORE JOB.               *'
   SAY '* *** NWREALTY EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC').         *'
   SAY '*                                                                     *'
   SAY '***********************************************************************'
   EXIT SRC
END
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW NWDD (FINIS"
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '***********************************************************************'
   SAY '*                                                                     *'
   SAY '* *** DATASET WRITE ERROR: ('OUTPDS'('OUTMEMB').                      *'
   SAY '* *** UNABLE TO WRITE REALITY PROD_SUPPORT RESTORE JOB.               *'
   SAY '* *** NWREALTY EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC').         *'
   SAY '*                                                                     *'
   SAY '***********************************************************************'
   EXIT SRC
END
RETURN
/*     */
SUBMIT_RESTORE_JOB:
ADDRESS TSO
"SUBMIT ('"CNTLLIB"("RMEMB")')"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '***********************************************************************'
   SAY '*                                                                     *'
   SAY '* *** JOB SUBMIT ERROR: ('CNTLLIB'('RMEMB').                          *'
   SAY '* *** UNABLE TO SUBMIT THE REALITY PROD_SUPPORT RESTORE JOB.          *'
   SAY '* *** NWREALTY EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC').         *'
   SAY '*                                                                     *'
   SAY '***********************************************************************'
   EXIT SRC
END
RETURN
