PROC 0 DEBUG
 /*******************************************************************/
 /**                                                               **/
 /**              C  T  M  C  A  U  P  T                           **/
 /**             ========================                          **/
 /**                                                               **/
 /**  PARAMETER PROMPTING FACILITY - TYPE2                         **/
 /**                                                               **/
 /**  THIS CLIST IS THE FIRST PHASE OF THE PARAMETERS PROMPTING    **/
 /**  FACILITY (SECOND PHASE IS FETCHING THE PLAN INTO THE DAILY   **/
 /**  LIBRARIES AND THE THIRD PHASE IS EXECUTING THE PLAN).        **/
 /**                                                               **/
 /**  THE CLIST LETS THE USER TO CREATE / UPDATE THE MASTER        **/
 /**  PROMPTING PLAN AND DEFINE THE PARAMETERS WITHIN IT.          **/
 /**                                                               **/
 /*******************************************************************/
 ISPEXEC CONTROL ERRORS RETURN
 IF &DEBUG = DEBUG +
 THEN +
    CONTROL LIST MSG CONLIST SYMLIST NOCAPS
 ELSE +
    CONTROL NOLIST NOMSG NOCAPS
 SET &T = T
 ISPEXEC VGET ( PLNPREF ) PROFILE
 SET &RC = &LASTCC
 IF &RC = 8  -
 THEN -
    SET &PLNPREF = REPTS
 ISPEXEC VGET ( PLANLIB ) PROFILE
 SET &RC = &LASTCC
 IF &RC = 8  -
 THEN -
    SET &PLANLIB = &STR(SYSS.CTMO.V410.PLANMSTR)
 ISPEXEC VGET ( ZDAY ZMONTH ) SHARED
 SET &DD = &STR(&ZDAY)
 SET &MM = &STR(&ZMONTH)
 REDISP: +
 ISPEXEC DISPLAY PANEL(CTMPF10W)
 SET &RC = &LASTCC
 IF &RC ¬= 0 -
 THEN -
    IF &RC ¬= 8 -
    THEN -
       DO
          ISPEXEC SETMSG MSG(CTMF000Z)
          GOTO END
       END
    ELSE -
       GOTO END
 SET &ISMSTR = 1
 SET &TABNAME  = &STR(&PLNPREF.&STR(MS))
 /*  SET &TABNAME  = &STR(&PLNPREF)

 SET &TBLIB = &PLANLIB
 ISPEXEC LMINIT DATAID(FROMID) DATASET('&PLANLIB') ENQ(SHR)
 SET &RC = &LASTCC
 IF &RC ¬= 0  -
 THEN -
    DO
      ISPEXEC SETMSG MSG(CTMF000Z)
      GOTO REDISP
    END

 ISPEXEC LMOPEN DATAID(&FROMID) OPTION(INPUT)
 SET &RC = &LASTCC
 IF &RC ¬= 0  -
 THEN -
    DO
      ISPEXEC SETMSG MSG(CTMF000Z)
      GOTO REDISP
 END
 ISPEXEC LMMFIND DATAID(&FROMID) MEMBER(&TABNAME)
 SET &RC = &LASTCC
 IF &RC ¬= 0  -
 THEN -
    IF &RC > 8   -
    THEN -
       DO
          ISPEXEC SETMSG MSG(CTMF000Z)
          GOTO REDISP
       END
    ELSE -
       DO
          IF &ISMSTR = 1 -
          THEN +
             DO
               /* MSG TO CREATE A NEW MASTER PLAN */
                ISPEXEC SETMSG MSG(CTMF005B)
                ISPEXEC VPUT (PLANLIB PLNPREF)
                %CTMCAMST
                ISPEXEC VGET (CREATED TABNAME PLANLIB PLNPREF)
                IF &CREATED ¬= YES -
                THEN +
                   DO
                      ISPEXEC LMFREE DATAID(&FROMID)
                      GOTO REDISP
                   END
             END
          ELSE +
             DO
                ISPEXEC SETMSG MSG(CTMF005C)
                GOTO REDISP
             END
       END
 IF &PLANLIB ¬= &STR() -
 THEN   -
    DO
        SET &IND = 1
 INITRY: SET &TBFILE = &STR(TBFILE&IND)
       ISPEXEC LMINIT DATAID(TRYID) DDNAME (&TBFILE) ENQ(SHR)
       SET &RC = &LASTCC
       IF &RC = 0 -
       THEN -
          DO
            ISPEXEC LMFREE (&TRYID)
            SET &DSNTRY = DSNTRY&IND
            SET &DSNTR  = &&DSNTRY&IND
            ISPEXEC VGET ( &DSNTRY ) SHARED
            SET DSNTT = &SYSNSUB(2,&DSNTR)
            IF &DSNTT = &PLANLIB -
            THEN -
               GOTO CONT
            ELSE -
               DO
                 SET &IND = &IND + 1
                 GOTO INITRY
               END
          END
       IF &IND > 99 -
       THEN -
          SET &IND = 1
       SET &TBFILE = &STR(TBFILE&IND)
       SET &DSNTRY = DSNTRY&IND
       SET &&DSNTRY = &PLANLIB
       ISPEXEC VPUT (&DSNTRY) SHARED
       FREE FILE(&TBFILE)
       ALLOC FILE(&TBFILE) DA('&PLANLIB') SHR
       SET &RC = &LASTCC
       IF &RC ¬= 0 -
       THEN -
          DO
              SET &DDERR = &TBFILE
              SET &DSNERR = &PLANLIB
              ISPEXEC SETMSG MSG(CTMF005D)
              GOTO END
          END
 CONT: SET &TBDD = &STR(&TBFILE)
       SET &TBLIB = &STR(&PLANLIB)
    END
 IF &PLANLIB = &STR() -
 THEN   -
    DO
      SET &TBDD = &STR(ISPTABL)
      SET &TBLIB = &STR(ISPTABL)
    END

 ISPEXEC VGET (ZUSER)
 SET TMPMST = &STR(&ZUSER.&STR(TMP))
 IF &LENGTH(&TMPMST) > 8 -
 THEN -
    SET &TMPMST = &SUBSTR(1:8,&TMPMST)
 ISPEXEC TBEND &TMPMST
 ISPEXEC TBCREATE &TMPMST +
         NAMES( PARMNAME PROCCUR# PRMJOBN PROMPIND PRDEFIND PRDEFVAL +
         PRMDEFST PRVALIND PRMVAL PRMTYPE PROMPMSG ) WRITE
 SET RC=&LASTCC
 IF &RC GT 4 -
 THEN -
    IF &RC > 12 -
    THEN -
       DO
         ISPEXEC SETMSG MSG(CTMF000Z)
         GOTO END
       END
    ELSE -
       DO
         ISPEXEC SETMSG MSG(CTMF005E)
         GOTO END
       END
 ISPEXEC TBOPEN &TABNAME WRITE LIBRARY(&TBFILE)
 SET &RETC = &LASTCC
 IF &RETC GT 0 -
 THEN -
    IF &RETC > 12 -
    THEN -
       DO
         ISPEXEC SETMSG MSG (CTMF000Z)
         GOTO END
       END
    ELSE -
       DO
         ISPEXEC SETMSG MSG (CTMF005E)
         GOTO END
       END
LOOPSKIP: +
 ISPEXEC TBSKIP &TABNAME
 IF &LASTCC = 8 -
 THEN -
    GOTO DISP0
 ISPEXEC TBADD &TMPMST
 GOTO LOOPSKIP
DISP0: +
 ISPEXEC TBTOP &TMPMST
DISP01: +
 ISPEXEC TBVCLEAR &TMPMST
 SET &PARMNAME = &STR(&PREFIX.&STR(*))
 ISPEXEC TBSARG &TMPMST
 SET &RC = &LASTCC
 IF &RC = 0 -
 THEN +
    GOTO DISPPAN1
 ELSE +
    DO
      SET &SPFRC = &RC
      ISPEXEC SETMSG MSG(CTMF000A)
      GOTO DISPPAN1
    END
 /* SET QCRP = 0
 SET CURNAME = TCMD
DISPPAN1: +
 ISPEXEC TBDISPL &TMPMST PANEL(CTMPF11W) ROWID(KEEPROW) &CSR
 SET RC = &LASTCC
 IF &RC > 8 +
 THEN +
    DO
       ISPEXEC TBEND &TABNAME
       ISPEXEC TBEND &TMPMST
       ISPEXEC SETMSG MSG (CTMF000Z)
       GOTO END
    END
 SET &CSR = &STR()
AFTDISP: +
 IF &TCMD = CANCEL OR &TCMD =cancel +
 THEN +
    GOTO ENDCAN
ED2: +
 SET RC1 = &RC
 SET QCRP = 0
 SET CURNAME = TCMD
 IF &ZTDSELS = 0 -
 THEN +
    GOTO CKLINCMD
 IF &TSEL NE D AND &TSEL NE I AND &TSEL NE A AND +
           &TSEL NE R AND &TSEL NE &STR( )       +
 THEN -
    DO
       ISPEXEC SETMSG MSG(CTMF005G)
       GOTO ED4
    END
 IF &TSEL = D OR &TSEL = d -
 THEN -
    DO
       ISPEXEC TBDELETE &TMPMST
       GOTO ED4
    END
 IF &TSEL = A OR &TSEL = R OR &TSEL = a OR &TSEL = r -
 THEN -
    DO
       SET &UPDTRET = BEFADD
       GOTO GENUPDT
       BEFADD: +
       SET &UPDTRET = &STR()
       SET UPDATEDD =
       SET UPDATEMM =
       ISPEXEC TBADD &TMPMST
       IF &LASTCC GT 0 -
       THEN -
          DO
             SET &RETC = &LASTCC
             ISPEXEC SETMSG MSG(CTMF000A)
             GOTO DIPPAN1
          END
       GOTO ED4
    END
 IF &TSEL = I OR &TSEL = i -
 THEN -
    DO
       SET &UPDTRET = BEFINS
       GOTO GENUPDT
       BEFINS: +
       SET &UPDTRET = &STR()
       ISPEXEC TBVCLEAR &TMPMST
       SET &DAILYM = Y
       ISPEXEC TBQUERY &TMPMST    POSITION(QCRP)
       ISPEXEC TBADD  &TMPMST
       IF &LASTCC GT 0 -
       THEN -
          DO
             SET &RETC = &LASTCC
             ISPEXEC SETMSG MSG(CTMF000A)
          END
       SET QCRP = &QCRP + 1
       GOTO ED4
    END
 IF &TSEL = &STR( ) -
 THEN -
    DO
      GENUPDT: +
      IF &PRDEFIND = Y +
      THEN +
         DO
           SET &PRMVAL = &STR(&PRDEFVAL)
           SET &PRMDEFST = &STR(DEF EXISTS)
         END
      ELSE +
         SET &PRMDEFST = &STR(NO DEFAULT)
      ISPEXEC TBPUT &TMPMST
      IF &LASTCC GT 8 -
      THEN -
         DO
            SET &RETC = &LASTCC
            ISPEXEC SETMSG MSG(CTMF000A)
            GOTO DISPPAN1
         END
      IF &PRMTYPE ¬= &STR() +
      THEN +
         DO
            ISPEXEC VPUT (PRMTYPE PRMVAL) SHARED
            ISPEXEC SELECT PGM(CTMTVAL) PARM(SYN)
            ISPEXEC VGET (VALIDRC) SHARED
            IF &VALIDRC ¬= 0 +
            THEN +
               DO
                  SET &VALIDERR = YES
                  SET &LASTROW = &KEEPROW
                  SET &CSR = &STR(CURSOR(PRMTYPE))
                  SET &ERRCSR = &CSR
                  GOTO DISPPAN1
               END
            ELSE +
               SET &VALIDERR = NO
            IF &PRDEFIND = Y +
            THEN +
               DO
      /*          SET &KEEPVAL = &STR(&PRMVAL)
      /*          SET &PRMVAL = &STR(&PRDEFVAL)
                  ISPEXEC VPUT (PRMTYPE PRMVAL) SHARED
                  ISPEXEC SELECT PGM(CTMTVAL) PARM(VAL)
      /*          SET &PRMVAL = &STR(&KEEPVAL)
                  ISPEXEC VGET (VALIDRC) SHARED
                  IF &VALIDRC ¬= 0 +
                  THEN +
                     DO
                        SET &LASTROW = &KEEPROW
                        SET &VALIDERR = YES
                        SET &CSR = &STR(CURSOR(PRDEFVAL))
                        SET &ERRCSR = &CSR
                        GOTO DISPPAN1
                     END
                  ELSE +
                     DO
                        SET &VALIDERR = NO
                     END
               END
         END
      IF &UPDTRET = BEFADD +
      THEN +
         GOTO BEFADD
      IF &UPDTRET = BEFINS +
      THEN +
         GOTO BEFINS
    END
ED4: +
 IF &RC1 = 4  -
 THEN -
    DO
       ISPEXEC TBDISPL &TMPMST
       SET RC = &LASTCC
       GOTO ED2
    END
 CKLINCMD: +
 IF &RESP = END OR &RC1 = 8 OR &TCMD = END OR &TCMD = end +
 THEN +
    DO
       IF &VALIDERR = YES +
       THEN +
          DO
             ISPEXEC SETMSG MSG (CTMF007G)
             ISPEXEC TBSKIP &TMPMST ROW(&LASTROW)
             ISPEXEC TBDISPL &TMPMST PANEL(CTMPF11W) &ERRCSR +
                    ROWID(KEEPROW)
             SET RC = &LASTCC
             IF &RC > 8 +
             THEN +
                DO
                   ISPEXEC TBEND &TABNAME
                   ISPEXEC TBEND &TMPMST
                   ISPEXEC SETMSG MSG (CTMF000Z)
                   GOTO END
                END
             GOTO AFTDISP
          END
  ISPEXEC TBEND &TABNAME
  ISPEXEC TBCLOSE &TMPMST NAME(&TABNAME) LIBRARY(&TBFILE) PAD(20)
  GOTO END
    END
 IF &RC GT 8 -
 THEN -
    DO
       ISPEXEC SETMSG MSG(CTMF000Z)
       GOTO END
    END
 IF &LENGTH(&TCMD) > 2 -
 THEN -
    DO
      SET &CMDPREF = &SUBSTR(1:2,&TCMD)
      IF &CMDPREF = L OR &CMDPREF = l -
      THEN -
         DO
            SET &LOCARG = &SUBSTR(3:&LENGTH(&TCMD),&TCMD)
            SET &LOCARG = &STR(&LOCARG.&STR(*))
            SET &PARMNAME = &STR(&LOCARG)
            ISPEXEC TBSCAN &TMPMST ARGLIST(PARMNAME) NOREAD
            SET &RC = &LASTCC
            IF &RC = 0 -
            THEN +
               GOTO DISPPAN1
            ELSE +
               IF &RC = 8 +
               THEN +
                  DO
                    ISPEXEC SETMSG MSG(CTMF000B)
                    GOTO DISPPAN1
                  END
               ELSE +
                  DO
                    ISPEXEC SETMSG MSG(CTMF000Z)
                    GOTO DISPPAN1
                  END
         END
      ELSE -
         IF &TCMD = INSERT OR &TCMD = insert  -
         THEN -
             DO
                ISPEXEC TBVCLEAR &TMPMST
                SET &DAILYM = Y
                ISPEXEC TBQUERY &TMPMST    POSITION(QCRP)
                ISPEXEC TBADD  &TMPMST
                IF &LASTCC GT 0 -
                THEN -
                   DO
                      SET &RETC = &LASTCC
                      ISPEXEC SETMSG MSG(CTMF000A)
                   END
                SET QCRP = &QCRP + 1
                GOTO DISPPAN1
             END
         ELSE +
            DO
               ISPEXEC SETMSG MSG(CTMF000C)
               GOTO DISPPAN1
            END
    END
 ELSE -
    IF &TCMD ¬= &STR() -
    THEN -
       DO
         ISPEXEC SETMSG MSG(CTMF000C)
         GOTO DISPPAN1
       END
 IF &LASTPREF ¬= &PREFIX +
 THEN +
    DO
       SET &LASTPREF = &PREFIX
       GOTO DISP01
    END
REPOSTAB: +
 ISPEXEC TBTOP &TMPMST
 ISPEXEC TBSKIP &TMPMST NUMBER(&ZTDTOP)
 GOTO DISPPAN1
ENDCAN: +
 ISPEXEC TBEND &TABNAME
 ISPEXEC TBEND   &TMPMST
 ISPEXEC SETMSG MSG(CTMF005H)
END: +
 ISPEXEC VPUT (PLANLIB PLNPREF) PROFILE
 ISPEXEC VPUT (TBNAME TBLIB) SHARED
 ISPEXEC LMFREE DATAID(&FROMID)
