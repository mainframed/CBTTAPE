/*  REXX EXEC : ( NWRESTOR ).
    FUNCTION  : LAN SERVER BACKUP PANEL INTERFACE TO FACILITATE FILE
                RESTORES FOR A SERVER VOLUME FROM THE MVS HOST CENTRAL
                ARCHIVE BACKUP.
                BUILDS AND WRITES THE JOB JCL AND INPUT SCRIPT TO
                PERFORM THE LAN SERVER RESTORE AND SUBMITS THE JOB
                FOR EXECUTION ON THE SYSB SYSTEM CLASS=S.
    INPUT     : ( PTECH2.NETCA.&&SERVER.&&VOLUME.H ).
                ( SYSS.NETCA.LAN.MVSNW.UA ).
    OUTPUT    : ( PTECH3.NETCA.RESTORE.SCRIPT.PARMLIB(&MEMBER).
                ( PTECH3.NETCA.RESTORE.SCRIPT.CNTLLIB(&MEMBER).
    1/26/94   - ADDED REALITY AND ASP
                                                                    */
TRACE O
/*      */
HOUSE_KEEPING:
ADDRESS TSO
SERVER.1 = 'COM'
SERVER.2 = 'MAGIC'
SERVER.3 = 'DB1'
SERVER.4 = 'MIS'
SERVER.5 = 'CS1'
SERVER.6 = 'POS'
SERVER.7 = 'CSI'
SERVER.8 = 'SYS'
SERVER.9 = 'CS2'
SERVER.10 = 'CS3'
SERVER.11 = 'FIN'
SERVER.12 = 'PAD'
SERVER.13 = 'REA'
SERVER.14 = 'ASP'
SERVER.15 = 'CS4'
SERVER.16 = 'DB1'
SERVER.17 = 'C4A'
SERVER.18 = 'C4B'
SERVER.19 = 'CS5'
SERVER.20 = 'C5A'
SERVER.21 = 'C6A'
SERVER.22 = 'C6B'
RESTYPE.1 = 'FILE'
RESTYPE.2 = 'DRCT'
RESTYPE.3 = 'VOL'
RESTMOD.1 = 'INCR'
RESTMOD.2 = 'FULL'
PARMLIB = 'SYSS.TECH.COMMON.PARMLIB'
SCRIPTLIB = 'PTECH3.NETCA.RESTORE.SCRIPT.PARMLIB'
CNTLLIB = 'PTECH3.NETCA.RESTORE.SCRIPT.CNTLLIB'
TEXTLIB = 'SYSS.NETCA.CONTROL.TEXTLIB'
TSOID = SYSVAR(SYSUID)
CURTIME = TIME()
HH = SUBSTR(CURTIME,1,2)
MM = SUBSTR(CURTIME,4,2)
SS = SUBSTR(CURTIME,7,2)
TIMEP = HH||MM||SS
JSUFFIX = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
X = MSG("OFF")
"FREE FILE(NWDD)"
ADDRESS ISPEXEC "CONTROL ERRORS RETURN"

/*     */
DISPLAY_SERVER_PANEL:
AUTHPARM = 'NWHPDESK'
NWMSG = 'NWLAN002'
DO FOREVER
   ADDRESS ISPEXEC
   "DISPLAY PANEL(NWRSTPN1)"
   SRC = RC
   IF SRC = 8 THEN EXIT 0
   IF SRC > 8 THEN DO
      SAY '*** ERROR INVOKING ISPF DIALOG PANEL ( NWRSTPN1 ). ***'
      SAY '*** NWRESTOR EXEC TASK CANCELLED - RC = 'SRC'. ***'
      EXIT SRC
   END
   IF RESTYPE.RT = 'VOL' THEN DO
      AUTHPARM = 'NWLANGRP'
      NWMSG = 'NWLAN003'
   END
   CALL CHECK_USER_AUTHORIZATION
   IF AUTHSW = 'YES' THEN LEAVE
   "SETMSG MSG("NWMSG")"
END
SERVER = SERVER.LAN
IF SERVER = 'MAGIC' THEN DO
   VOLKEY = 'MAGIC SYS:'
   RESTVOL = 'SYS:'
/* RESTDPN = 'MAGIC' */
   VOLUME = 'MAGIC'
   SERVER = 'CS3'
   RM = 2
   SIGNAL BUILD_CONTAINER_DATASET_PARMS
END
IF SERVER = 'POS' THEN DO
   VOLKEY = 'POS VOL1:'
   RESTVOL = 'VOL1:'
/* RESTDPN = 'POS' */
   VOLUME = 'POS'
   SERVER = 'CS3'
   RM = 2
   SIGNAL BUILD_CONTAINER_DATASET_PARMS
END
IF SERVER = 'REA' THEN DO
   VOLKEY = 'REA VOL2:'
   RESTVOL = 'VOL2:'
   RESTDPN = 'REA'
   VOLUME = 'REA'
   SERVER = 'CS1'
   RM = 2
   SIGNAL BUILD_CONTAINER_DATASET_PARMS
END
IF SERVER = 'ASP' THEN DO
   VOLKEY = 'ASP VOL1:'
   RESTVOL = 'VOL1:'
   RESTDPN = 'ASP'
   VOLUME = 'ASP'
   SERVER = 'CSI'
   RM = 2
   SIGNAL BUILD_CONTAINER_DATASET_PARMS
END

/*     */
DISPLAY_VOLUME_PANEL:
ADDRESS TSO
"FREE FILE(NWDD)"
"FREE FILE(ISPFILE)"
CALL READ_SERVER_VOLUMES_PARMS
ADDRESS ISPEXEC "DISPLAY PANEL(NWRSTPN2)"
SRC = RC
IF SRC = 8 THEN EXIT 0
IF SRC > 8 THEN DO
   SAY '*** ERROR INVOKING ISPF DIALOG PANEL ( NWRSTPN2 ). ***'
   SAY '*** NWRESTOR EXEC TASK CANCELLED - RC = 'SRC'. ***'
   EXIT SRC
END
CALL GET_SELECTED_SERVER_VOLUME
VOLKEY = VOLUME RESTVOL

/*     */
BUILD_CONTAINER_DATASET_PARMS:
CALL GET_HISTORY_CONTAINER_DATASETS
IF DSN = 0 THEN DO
   SAY '*** HISTORY DATASET ERROR: NO VALID CONTAINER DATASET NAMES FOUND. ***'
   SAY '*** DSN ('NWHSTLIB') DOES NOT CONTAIN A BACKUP STATUS OF (SSSS). ***'
   SAY '*** NWRESTOR EXEC TASK CANCELLED - RC = (028). ***'
   EXIT 028
END
GDGCNT = 18
NWPANEL = 'NWRSTPN3'
IF RESTMOD.RM = 'FULL' & SERVER ¬= 'PAD' THEN DO
   GDGCNT = 6
   NWPANEL = 'NWRSTPN4'
END
CALL BUILD_PANEL_DATASET_PARMS

/*     */
DISPLAY_DATASET_LIST_PANEL:
DO FOREVER
   ADDRESS ISPEXEC
   "DISPLAY PANEL("NWPANEL")"
   SRC = RC
   IF SRC = 8 THEN EXIT 0
   IF SRC > 8 THEN DO
      SAY '*** ERROR INVOKING ISPF DIALOG PANEL ( 'NWPANEL' ). ***'
      SAY '*** NWRESTOR EXEC TASK CANCELLED - RC = 'SRC'. ***'
      EXIT SRC
   END
   CALL GET_PANEL_INPUT_DATASET
   IF DSNSW = 'YES' THEN LEAVE
   "SETMSG MSG(NWLAN004)"
END
IF RESTYPE.RT = 'DRCT' & RESTFN = '' THEN RESTFN = '*'
IF RESTYPE.RT = 'VOL' THEN DO
   RESTDPN = '*'
   RESTFN = '*'
END

/*     */
DISPLAY_INPUT_PARMS_CONFIRMATION_PANEL:
CALL LISTCAT_CONTAINER_DATASET
CALL BUILD_PANEL_TAPE_VOLSERS
ADDRESS ISPEXEC
"DISPLAY PANEL(NWRSTPN5)"
SRC = RC
IF SRC = 8 THEN EXIT 0
IF SRC > 8 THEN DO
   SAY '*** ERROR INVOKING ISPF DIALOG PANEL ( NWRSTPN5 ). ***'
   SAY '*** NWRESTOR EXEC TASK CANCELLED - RC = 'SRC'. ***'
   EXIT SRC
END

/*     */
CREATE_RESTORE_JOB_PARMS:
ADDRESS TSO
SUBPDS = CNTLLIB
JMEMB = 'J'TIMEP                  /* JOURNAL PARMS MEMBER */
RMEMB = 'R'TIMEP                  /* RESTORE JOB JCL MEMBER */
SMEMB = 'S'TIMEP                  /* SCRIPT INPUT MEMBER */
"DELSTACK"
CALL BUILD_JOBNAME_PARM
OUTPDS = CNTLLIB
OUTMEMB = JOBNAME
QUEUE JOBNAME
CALL WRITE_PDS_MEMBER
CALL BUILD_RESTORE_JOB
OUTMEMB = RMEMB
CALL WRITE_PDS_MEMBER
"DELSTACK"
QUEUE SERVER
QUEUE RESTVOL
QUEUE RESTDSN
QUEUE RESTDPN
QUEUE RESTFN
OUTMEMB = JMEMB
CALL WRITE_PDS_MEMBER
CALL BUILD_RESTORE_PARMS
OUTMEMB = SMEMB
OUTPDS = SCRIPTLIB
CALL WRITE_PDS_MEMBER
CALL SUBMIT_RESTORE_JOB
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */
/*     */
CHECK_USER_AUTHORIZATION:
ADDRESS TSO
AUTHSW = 'NO'
"ALLOC DA('"PARMLIB"("AUTHPARM")') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('PARMLIB'('AUTHPARM'). ***'
   SAY '*** NWRESTOR EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR NWDD (FINIS STEM AUTH."
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('PARMLIB'('AUTHPARM'). ***'
   SAY '*** NWRESTOR EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
DO I = 1 TO AUTH.0
   LAU = POS(TSOID,AUTH.I)
   IF LAU ¬= 0 THEN AUTHSW = 'YES'
END
RETURN

/*     */
READ_SERVER_VOLUMES_PARMS:
ADDRESS TSO
VOLCONFG = SERVER'IVOLS'
IF RESTMOD.RM = 'FULL' THEN VOLCONFG = SERVER'FVOLS'
"ALLOC DA('"TEXTLIB"("VOLCONFG")') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('TEXTLIB'('VOLCONFG'). ***'
   SAY '*** NWRESTOR EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR NWDD (FINIS STEM VOLP."
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('TEXTLIB'('VOLCONFG'). ***'
   SAY '*** NWRESTOR EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
V = 0
VOL1 = '*****'
VOL2 = '*****'
VOL3 = '*****'
VOL4 = '*****'
VOL5 = '*****'
VOL6 = '*****'
VOL7 = '*****'
VOL8 = '*****'
DO I = 1 TO VOLP.0
   UPPER VOLP.I
   SVOL = POS('DAILY',VOLP.I)
   IF SVOL = 0 THEN ITERATE
   V = V + 1
   SERVOL = WORD(VOLP.I,2)
   RSTVOL.V = WORD(VOLP.I,2)
   IF V = 1 THEN VOL1 = LEFT(SERVOL,5,'*')
   IF V = 2 THEN VOL2 = LEFT(SERVOL,5,'*')
   IF V = 3 THEN VOL3 = LEFT(SERVOL,5,'*')
   IF V = 4 THEN VOL4 = LEFT(SERVOL,5,'*')
   IF V = 5 THEN VOL5 = LEFT(SERVOL,5,'*')
   IF V = 6 THEN VOL6 = LEFT(SERVOL,5,'*')
   IF V = 7 THEN VOL7 = LEFT(SERVOL,5,'*')
   IF V = 8 THEN VOL8 = LEFT(SERVOL,5,'*')
END
RETURN

/*     */
GET_SELECTED_SERVER_VOLUME:
DO I = 1 TO 8
   SELVOL = VALUE('RV'I)
   IF SELVOL = '' THEN ITERATE
   RESTVOL = RSTVOL.I
   VOLUME = STRIP(RESTVOL,'T',':')
   LEAVE
END
RETURN

/*     */
GET_HISTORY_CONTAINER_DATASETS:
ADDRESS TSO
DSN = 0
NWHSTLIB = 'PTECH2.NETCA.'SERVER'.'VOLUME'.H'
NWDSN = SYSDSN("'"NWHSTLIB"'")
IF NWDSN = 'DATASET NOT FOUND' THEN DO
   SAY '*** DATASET ERROR: DATASET NOT FOUND FOR ('NWHSTLIB'). ***'
   SAY '*** NWRESTOR EXEC SUBTASK CANCELLED - RETURN CODE: (028). ***'
   EXIT 028
END
"ALLOC DA('"NWHSTLIB"') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('NWHSTLIB'). ***'
   SAY '*** NWRESTOR EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR NWDD (FINIS STEM HIST."
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('NWHSTLIB'). ***'
   SAY '*** NWRESTOR EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
BUDATE.1 = 'XXXX-XX-XX'
BUDATE.2 = 'XXXX-XX-XX'
BUDATE.3 = 'XXXX-XX-XX'
BUDATE.4 = 'XXXX-XX-XX'
BUDATE.5 = 'XXXX-XX-XX'
BUDATE.6 = 'XXXX-XX-XX'
BUDATE.7 = 'XXXX-XX-XX'
BUDATE.8 = 'XXXX-XX-XX'
BUDATE.9 = 'XXXX-XX-XX'
BUDATE.10 = 'XXXX-XX-XX'
BUDATE.11 = 'XXXX-XX-XX'
BUDATE.12 = 'XXXX-XX-XX'
BUDATE.13 = 'XXXX-XX-XX'
BUDATE.14 = 'XXXX-XX-XX'
BUDATE.15 = 'XXXX-XX-XX'
BUDATE.16 = 'XXXX-XX-XX'
BUDATE.17 = 'XXXX-XX-XX'
BUDATE.18 = 'XXXX-XX-XX'
BUDSN.1 = '--------------------------------------------'
BUDSN.2 = '--------------------------------------------'
BUDSN.3 = '--------------------------------------------'
BUDSN.4 = '--------------------------------------------'
BUDSN.5 = '--------------------------------------------'
BUDSN.6 = '--------------------------------------------'
BUDSN.7 = '--------------------------------------------'
BUDSN.8 = '--------------------------------------------'
BUDSN.9 = '--------------------------------------------'
BUDSN.10 = '--------------------------------------------'
BUDSN.11 = '--------------------------------------------'
BUDSN.12 = '--------------------------------------------'
BUDSN.13 = '--------------------------------------------'
BUDSN.14 = '--------------------------------------------'
BUDSN.15 = '--------------------------------------------'
BUDSN.16 = '--------------------------------------------'
BUDSN.17 = '--------------------------------------------'
BUDSN.18 = '--------------------------------------------'
DO I = 1 TO HIST.0
   UPPER HIST.I
   VLOC = POS(VOLKEY,HIST.I)
   IF VLOC ¬= 0 THEN DO
      BUSTATUS = WORD(HIST.I,9)
      IF BUSTATUS = 'SSSS' THEN DO
         BUTYPE = WORD(HIST.I,5)
         IF BUTYPE = RESTMOD.RM THEN DO
            DSN = DSN + 1
            BUDATE.DSN = WORD(HIST.I,1)
            BUTIME.DSN = WORD(HIST.I,2)
            L = I + 1
            UPPER HIST.L
            HSTDSN = WORD(HIST.L,2)
            BUDSN.DSN = STRIP(HSTDSN,"B","'")
         END
      END
   END
END
RETURN

/*     */
BUILD_PANEL_DATASET_PARMS:
DO D = 1 TO GDGCNT
   IF BUDSN.D = '' THEN ITERATE
   IF D = 1 THEN DO
      BUDATE01 = BUDATE.D
      BUDSN01 = BUDSN.D
   END
   IF D = 2 THEN DO
      BUDATE02 = BUDATE.D
      BUDSN02 = BUDSN.D
   END
   IF D = 3 THEN DO
      BUDATE03 = BUDATE.D
      BUDSN03 = BUDSN.D
   END
   IF D = 4 THEN DO
      BUDATE04 = BUDATE.D
      BUDSN04 = BUDSN.D
   END
   IF D = 5 THEN DO
      BUDATE05 = BUDATE.D
      BUDSN05 = BUDSN.D
   END
   IF D = 6 THEN DO
      BUDATE06 = BUDATE.D
      BUDSN06 = BUDSN.D
   END
   IF D = 7 THEN DO
      BUDATE07 = BUDATE.D
      BUDSN07 = BUDSN.D
   END
   IF D = 8 THEN DO
      BUDATE08 = BUDATE.D
      BUDSN08 = BUDSN.D
   END
   IF D = 9 THEN DO
      BUDATE09 = BUDATE.D
      BUDSN09 = BUDSN.D
   END
   IF D = 10 THEN DO
      BUDATE10 = BUDATE.D
      BUDSN10 = BUDSN.D
   END
   IF D = 11 THEN DO
      BUDATE11 = BUDATE.D
      BUDSN11 = BUDSN.D
   END
   IF D = 12 THEN DO
      BUDATE12 = BUDATE.D
      BUDSN12 = BUDSN.D
   END
   IF D = 13 THEN DO
      BUDATE13 = BUDATE.D
      BUDSN13 = BUDSN.D
   END
   IF D = 14 THEN DO
      BUDATE14 = BUDATE.D
      BUDSN14 = BUDSN.D
   END
   IF D = 15 THEN DO
      BUDATE15 = BUDATE.D
      BUDSN15 = BUDSN.D
   END
   IF D = 16 THEN DO
      BUDATE16 = BUDATE.D
      BUDSN16 = BUDSN.D
   END
   IF D = 17 THEN DO
      BUDATE17 = BUDATE.D
      BUDSN17 = BUDSN.D
   END
   IF D = 18 THEN DO
      BUDATE18 = BUDATE.D
      BUDSN18 = BUDSN.D
   END
END
RETURN

/*     */
GET_PANEL_INPUT_DATASET:
DSNSW = 'NO'
DO I = 1 TO GDGCNT
   SELDSN = VALUE('RD'I)
   IF SELDSN = '' THEN ITERATE
   IF I < 10 THEN DO
      RESTDAT = VALUE('BUDATE0'I)
      RESTDSN = VALUE('BUDSN0'I)
   END
   IF I >= 10 THEN DO
      RESTDAT = VALUE('BUDATE'I)
      RESTDSN = VALUE('BUDSN'I)
   END
   IF RESTDSN = '' THEN ITERATE
   DSNSW = 'YES'
   LEAVE
END
RETURN

/*     */
LISTCAT_CONTAINER_DATASET:
ADDRESS TSO
T = 0
X = OUTTRAP("LSTCAT.","*","CONCAT")
"LISTCAT ENT('"RESTDSN"') ALL"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** LISTCAT ERROR: 'RESTDSN'. ***'
   SAY '*** NWRESTOR EXEC SUBTASK CANCELLED - RC = ('SRC'). ***'
   EXIT SRC
END
DO L = 1 TO LSTCAT.0
   VOLKEY = POS("VOLSER------------",LSTCAT.L)
   IF VOLKEY ¬= 0 THEN DO
      STA = VOLKEY + 18
      T = T + 1
      TAPE.T = SUBSTR(LSTCAT.L,STA,6)
   END
END
TAPECNT = T
RETURN

/*     */
BUILD_PANEL_TAPE_VOLSERS:
DO T = 1 TO TAPECNT
   IF T = 1 THEN TAPE1 = TAPE.T
   IF T = 2 THEN TAPE2 = TAPE.T
   IF T = 3 THEN TAPE3 = TAPE.T
   IF T = 4 THEN TAPE4 = TAPE.T
   IF T = 5 THEN TAPE5 = TAPE.T
   IF T = 6 THEN TAPE6 = TAPE.T
   IF T = 7 THEN TAPE7 = TAPE.T
   IF T = 8 THEN TAPE8 = TAPE.T
END
RETURN

/*     */
BUILD_JOBNAME_PARM:
JN1 = 'NWR'
JN2 = SUBSTR(TSOID,5,3)
DO N = 1 TO 26
   JN3 = SUBSTR(JSUFFIX,N,1)
   JOBNAME = JN1||JN2||JN3
   JOB = SYSDSN("'"CNTLLIB"("JOBNAME")'")
   IF JOB = 'MEMBER NOT FOUND' THEN LEAVE
END
JN = JOBNAME
RETURN

/*     */
BUILD_RESTORE_JOB:
"DELSTACK"
RCDCNT = 36
DIRPATH = STRIP(RESTDPN,'T','/')
STARGET = RESTVOL||DIRPATH
IF RESTDPN = '*' THEN STARGET = RESTVOL
IF SERVER  = 'C4A' THEN SERVER = 'CS4'
IF SERVER  = 'C4B' THEN SERVER = 'CS4'
JCL.1 = "//"JN"  JOB 5304010530000000,'"SERVER" LAN RESTORE',NOTIFY="TSOID","
JCL.2 = "//             CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),REGION=6M"
JCL.3 = "/*JOBPARM SYSAFF=SYSB"
JCL.4 = "//RESTORE  EXEC PGM=NUACLIEN"
JCL.5 = "//NETUADD  DD DSN=PTECH3.NETCA.RESTORE.SCRIPT.PARMLIB,DISP=SHR"
JCL.6 = "//SYSPRINT DD SYSOUT=*"
JCL.7 = "//SYSIN DD *"
JCL.8 = "CONNECT RS6000 -SERV "SERVER" "LANID" "LANPW
JCL.9 = "SET DEBUG LOG SYS:/SICOM/CA/RESTORE/DEBUG.LOG"
JCL.10 = "SET GLOBAL TARGET" STARGET
JCL.11 = "SET GLOBAL OPTIONS -LOG SYS:/SICOM/CA/DB/RESTORE.RPT"
JCL.12 = "INPUT 'SYSS.NETCA.LAN.MVSNW.UA'"
JCL.13 = "INPUT DD:NETUADD("SMEMB")"
JCL.14 = "EXIT"
JCL.15 = "/*"
JCL.16 = "//WRITELOG EXEC PGM=IKJEFT01,DYNAMNBR=30,"
JCL.17 = "//   PARM='%NWRSTLOG "JMEMB"',COND=(0,LT,RESTORE)"
JCL.18 = "//SYSPRINT DD SYSOUT=*"
JCL.19 = "//SYSTSPRT DD SYSOUT=*"
JCL.20 = "//SYSIN    DD DUMMY"
JCL.21 = "//SYSLBC   DD DSN=SYS1.BRODCAST,DISP=SHR"
JCL.22 = "//SYSUADS  DD DSN=SYS1.UADS,DISP=SHR"
JCL.23 = "//SYSPROC  DD DSN=SYSS.TECH.COMMON.EXECLIB,DISP=SHR"
JCL.24 = "//SYSTSIN  DD DUMMY"
JCL.25 = "/*"
JCL.26 = "//DELETE   EXEC PGM=IDCAMS"
JCL.27 = "//CNTLDD   DD DSN="CNTLLIB",DISP=SHR"
JCL.28 = "//SCRIPTDD DD DSN="SCRIPTLIB",DISP=SHR"
JCL.29 = "//SYSPRINT DD SYSOUT=*"
JCL.30 = "//SYSIN    DD *"
JCL.31 = "  DELETE   "SCRIPTLIB"("SMEMB")  FILE(SCRIPTDD)"
JCL.32 = "  DELETE   "CNTLLIB"("JMEMB")  FILE(CNTLDD)"
JCL.33 = "  DELETE   "CNTLLIB"("RMEMB")  FILE(CNTLDD)"
JCL.34 = "  DELETE   "CNTLLIB"("JOBNAME")  FILE(CNTLDD)"
JCL.35 = "/*"
JCL.36 = "//"
DO J = 1 TO RCDCNT
   QUEUE JCL.J
END
RETURN

/*     */
BUILD_RESTORE_PARMS:
"DELSTACK"
RESTDSN = "'"||RESTDSN||"'"
RESTPRM = '"'||RESTDPN||RESTFN||' -PDT ON -REP ON"'
IF RESTYPE.RT = 'FILE' THEN DO
   RECORD1 = 'CA_RESTORE 'RESTMOD.RM' 'RESTDSN' 'RESTVOL' -'
   QUEUE RECORD1
   RECORD2 = '   '||RESTPRM
   QUEUE RECORD2
END
IF RESTYPE.RT = 'DRCT' THEN DO
   RESTPRM = '"'||RESTDPN'*.* -PDT ON -REP ON"'
   RECORD1 = 'CA_RESTORE 'RESTMOD.RM' 'RESTDSN' 'RESTVOL' -'
   QUEUE RECORD1
   RECORD2 = '   '||RESTPRM
   QUEUE RECORD2
END
IF RESTYPE.RT = 'VOL' THEN DO
   RECORD = 'CA_RESTORE 'RESTMOD.RM' 'RESTDSN' 'RESTVOL' -'
   QUEUE RECORD
   RECORD2 = '    "/* -PDT ON REP ON"'
   QUEUE RECORD2
END
RETURN

/*     */
WRITE_PDS_MEMBER:
ADDRESS TSO
"ALLOC DA('"OUTPDS"("OUTMEMB")') FILE(NWDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('OUTPDS'('OUTMEMB'). ***'
   SAY '*** NWRESTOR EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW NWDD (FINIS"
SRC = RC
"FREE FILE(NWDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING 'OUTPDS'('OUTMEMB'). ***'
   SAY '*** NWRESTOR EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
SUBMIT_RESTORE_JOB:
ADDRESS TSO
"SUBMIT ('"SUBPDS"("RMEMB")')"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** SUBMIT ERROR: MEMBER ( 'SUBPDS'('RMEMB'). ***'
   SAY '*** NWRESTOR EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN
