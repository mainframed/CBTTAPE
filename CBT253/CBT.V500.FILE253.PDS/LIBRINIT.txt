/*  REXX EXEC : ( LIBRINIT ).
    FUNCTION  : BUILD JCL JOBSTREAM TO ALLOCATE/FORMAT A CCF LIBRARIAN
                MASTER DATASET.
                OUTPUT JOB WILL BE SUBMITTED TO SYSA SYSTEM CLASS=S
                FOR EXECUTION.
    AUTHOR    : DAN BOWEN, MIS,TECH SERVICES.
                                                                    */
TRACE O
/*      */
HOUSE_KEEPING:
ADDRESS TSO
HLQ = 'SYS1'
LLQ = 'MASTER'
VOL = 'LIBR01'
LNODE1 = 'ARCH BACK EMRG HIST PROD QA RJCT TECH TEST TURN'
LNODE2 = 'COPY JCL PROC RAMI SRCE SYSI TABLE WRITER SYSTEM.CONTROL'
LIBLOCK = '2942 1086 7548 1086 27998 1086 4136 13682 2942'
ACCTCDE = '5304010530000000'
MVS = 'SYSA'
CLASS= 'S'
X = MSG("OFF")
"FREE FILE(LIBDD)"
ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
/*
      M A I N   R O U T I N E   S E C T I O N         */
/*     */
DISPLAY_LIBPARM_PANEL:
DO FOREVER
   ADDRESS ISPEXEC
   "DISPLAY PANEL(LIBRPANL)"
   SRC = RC
   IF SRC = 8 THEN EXIT 0
   IF SRC > 8 THEN DO
      SAY '*** ERROR INVOKING ISPF DIALOG PANEL ( LIBRPANL ). ***'
      SAY '*** LIBRINIT EXEC TASK CANCELLED - RC = 'SRC'. ***'
      EXIT SRC
   END
   CALL BUILD_SKELETON_PARMS
   CALL SUBMIT_JOB
END
EXIT 0
/*
       S U B R O U T I N E S   S E C T I O N     */
/*     */
BUILD_SKELETON_PARMS:
JOBNAME = JN
MEMBER = JN
ALLOC = SPACE
LIBVOL = VOL
SKELJCL = 'LIBRSKL1'
NODE2A = WORD(LNODE1,N1)
NODE2B = WORD(LNODE2,N2)
MLQ = NODE2A||NODE2B
IF N2 > 6 THEN MLQ = NODE2B
MASTER = HLQ'.'MLQ'.'LLQ
NEWMSTR = HLQ'.'MLQ'.NEW.'LLQ
SAVMSTR = HLQ'.'MLQ'.SAVE.'LLQ
BLKSIZE = WORD(LIBLOCK,N2)
SEQNR = '81,6,10,10'
IF (NODE2A = 'HIST') | (NODE2B = 'SYSTEM.CONTROL') THEN DO
   BLKSIZE = '2942'
   SKELJCL = 'LIBRSKL2'
END
IF NODE2B = 'SRCE' THEN SEQNR = '73,8,10,10'
RETURN
/*     */
SUBMIT_JOB:
ADDRESS ISPEXEC
"FTOPEN TEMP"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $LIBR002
   "SETMSG MSG($LIBR002)"
   RETURN
END
"FTINCL $CCFLJOB"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $LIBR003
   "SETMSG MSG($LIBR003)"
   RETURN
END
"FTINCL" SKELJCL
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $LIBR004
   "SETMSG MSG($LIBR004)"
   RETURN
END
"FTCLOSE"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $LIBR005
   "SETMSG MSG($LIBR005)"
   RETURN
END
"VGET ZTEMPF"
ADDRESS TSO "SUBMIT '"ZTEMPF"'"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = $LIBR006
   "SETMSG MSG($LIBR006)"
   RETURN
END
ADDRESS ISPEXEC
ZERRMSG = $LIBR001
"SETMSG MSG($LIBR001)"
RETURN
