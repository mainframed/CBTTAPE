/* REXX EXEC : ( CCFQACIC ).
   FUNCTION  : READS SOURCE PROGRAM STATEMENTS TO DETERINE COBOL-II
               CICS COMPILE TYPE FOR QA MOVEMENT COMPILE.             */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
ARG MASTER MEMBER
PARMS = 'MASTER MEMBER'
DO I = 1 TO 2
   IPARM = WORD(PARMS,I)
   XPARM = VALUE(IPARM)
   IF XPARM ¬= '' THEN ITERATE
   SAY '*** PARM ERROR: MISSING PARM VALUE FOR ('IPARM'). ***'
   SAY '*** CCFQACIC EXEC TASK CANCELLED - RC = (024). ***'
   EXIT 024
END

/*     */
MAIN_ROUTINE:
EXITRC = 1
CALL CHECK_OPTIMIZE_FLAG
IF OPTIMIZE_SW = 'YES' THEN EXITRC = 2
EXIT EXITRC

/*     S U B R O U T I N E   S E C T I O N     */
/*     */
CHECK_OPTIMIZE_FLAG:
OPTIMIZE_SW = 'NO'
LIBDSN = "'"MASTER"("MEMBER")'"
LIBDD = LIBALLOC(LIBDSN)  /* DYNALLOC LIBMASTER W/LAM; RET DDNAME*/
IF LIBDD = "ERROR" THEN DO
   ERC = 100
   SAY '*** DYNAMIC ALLOCATION ERROR: ('MASTER'). ***'
   SAY '*** CANNOT READ COBOL2 COMPILE TYPE FLAG FOR ('MEMBER') PROGRAM. ***'
   SAY '*** CCFQACIC EXEC SUBTASK CANCELLED - RC = ('ERC'). ***'
   EXIT ERC
END
"EXECIO * DISKR" LIBDD "(FINIS STEM DATA."
SRC = RC
IF SRC ¬= 0 THEN DO
   ERC = 100
   SAY '*** EXECIO READ ERROR: ('MASTER'('MEMBER'). ***'
   SAY '*** CANNOT READ COBOL2 COMPILE TYPE FLAG FOR ('MEMBER') PROGRAM. ***'
   SAY '*** CCFQACIC EXEC SUBTASK CANCELLED - RC = ('ERC'). ***'
   EXIT ERC
END
DO I = 1 TO DATA.0
   OPTFLAG = POS('OPT2-FLAG',DATA.I)
   IF OPTFLAG ¬= 0 THEN DO
      OPTIMIZE_SW = 'YES'
      LEAVE
   END
END
RETURN
