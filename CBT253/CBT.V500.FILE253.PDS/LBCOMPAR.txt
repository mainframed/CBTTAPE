/* This REXX EXEC is designed to facilitate the CA-LIBRARIAN member
   compare of librarian modules residing in the TURNOVER pre-QA master
   to the current arc level0 member in the PROD master.              */

TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
TSOID = SYSVAR(SYSUID)
ACCTCDE = 5300010530000000
JOBNAME = 'LBCOMPAR'
SAVEMEMB= 'NULLMEMB'
LIBTEST.1 = 'SYS1.QACOPY.MASTER'
LIBTEST.2 = 'SYS1.QAJCL.MASTER'
LIBTEST.3 = 'SYS1.QAPROC.MASTER'
LIBTEST.4 = 'SYS1.QARAMI.MASTER'
LIBTEST.5 = 'SYS1.QASRCE.MASTER'
LIBTEST.6 = 'SYS1.QASYSI.MASTER'
LIBPROD.1 = 'SYS1.PRODCOPY.MASTER'
LIBPROD.2 = 'SYS1.PRODJCL.MASTER'
LIBPROD.3 = 'SYS1.PRODPROC.MASTER'
LIBPROD.4 = 'SYS1.PRODRAMI.MASTER'
LIBPROD.5 = 'SYS1.PRODSRCE.MASTER'
LIBPROD.6 = 'SYS1.PRODSYSI.MASTER'
ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
ADDRESS ISPEXEC "FTCLOSE"

/*     */
DISPLAY_PANEL:
DO FOREVER
   ADDRESS ISPEXEC "DISPLAY PANEL(LBCOMPAR)"
   SRC = RC
   IF SRC = 0 THEN CALL GET_LBCOMPARE_PARMS
   IF SRC = 8 THEN LEAVE
   IF SRC > 8 THEN DO
      SAY '*** ERROR INVOKING ISPF DIALOG PANEL LBCOMPAR - RC = 'SRC'.***'
      EXIT SRC
   END
   IF MEMBER = '' |  M = '' THEN ITERATE
   IF MEMBER = SAVEMEMB & M = SAVELIB THEN EXIT
   CALL SUBMIT_LBCOMPARE_JOB
END
EXIT

/*     */
GET_LBCOMPARE_PARMS:
SYSUT1 = LIBPROD.M
SYSUT2 = LIBTEST.M
RETURN

/*     */
SUBMIT_LBCOMPARE_JOB:
SAVEMEMB = MEMBER
SAVELIB = M
ADDRESS ISPEXEC "FTOPEN TEMP"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = LBCOM001
   ADDRESS ISPEXEC "SETMSG MSG(LBCOM001)"
   RETURN
   END
ADDRESS ISPEXEC "FTINCL LBCMPJOB"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = LBCOM002
   ADDRESS ISPEXEC "SETMSG MSG(LBCOM002)"
   RETURN
   END
ADDRESS ISPEXEC "FTINCL LBCOMPAR"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = LBCOM003
   ADDRESS ISPEXEC "SETMSG MSG(LBCOM003)"
   RETURN
   END
ADDRESS ISPEXEC "FTCLOSE"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = LBCOM004
   RETURN
   END
ADDRESS ISPEXEC "VGET ZTEMPF"
ADDRESS TSO "SUBMIT '"ZTEMPF"'"
SRC = RC
IF SRC ¬= 0 THEN DO
   ZERRMSG = LBCOM005
   RETURN
   END
ZERRMSG = LBCOM000
ADDRESS ISPEXEC "SETMSG MSG(LBCOM000)"
RETURN
