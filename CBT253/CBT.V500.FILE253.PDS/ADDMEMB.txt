/*  REXX EXEC : ( ADDMEMB ).
    FUNCTION  : BUILDS A IEBGENER JOB TO ADD|REPLACE A MEMBER INTO
                THE USER'S TSO USER.CNTLLIB (SYSA) OR MAINT.CNTLLIB
                (SYSM).
                                                                    */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS ISPEXEC
ARG MEMBER
IF MEMBER = '' THEN DO
   SAY '*** MEMBER NAME NOT PASSED ON COMMAND LINE.***'
   SAY '*** ADDMEMB EXEC TASK CANCELLED. ***'
   EXIT
END
ACCTDFT = '5304010530000000'
"VGET (ACCTCDE) PROFILE"
IF RC ¬= 0 THEN ACCTCDE = ACCTDFT
ADDRESS TSO
TSOID = SYSVAR(SYSUID)
UCNTL = SYSDSN(DIALOG.ISPFILE)
IF UCNTL = 'DATASET NOT FOUND' THEN DO
"ALLOC DA(DIALOG.ISPFILE) LIKE(USER.CNTLLIB) NEW SPACE(1,0) DIR(5) CYLINDERS"
   SRC = RC
   IF SRC ¬= 0 THEN DO
   SAY '*** ALLOCATE ERROR: ERROR ALLOCATING DATASET (DIALOG.ISPFILE). ***'
   SAY '*** ADDMEMB EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
   END
END
MEM = SYSDSN("USER.CNTLLIB("MEMBER")")
IF MEM = 'MEMBER NOT FOUND' THEN DO
   SAY '*** MEMBER NOT FOUND IN ("TSOID".USER.CNTLLIB("MEMBER"). ***'
   SAY '*** ADDMEMB EXEC TASK CANCELLED - RETURN CODE: (28). ***'
   EXIT
END
X = MSG("OFF")
"FREE FILE(ISPFILE)"

/*     */
MAIN_ROUTINE:
CALL READ_MEMBER
CALL BUILD_IEBGENER_JOBJCL
CALL BUILD_IEBGENER_SYSIN
CALL BUILD_IEBGENER_ENDJCL
CALL WRITE_IEBGENER_JOB
CALL SUBMIT_IEBGENER_JOB
EXIT 0

/*     S U B R O U T I N E S   S E C T I O N     */

/*     */
READ_MEMBER:
"ALLOC DA(USER.CNTLLIB("MEMBER")) FILE(ISPFILE) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: (USER.CNTLLIB('MEMBER'). ***'
   SAY '*** ADDMEMB EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR ISPFILE (FINIS STEM DATA."
SRC = RC
"FREE FILE(ISPFILE)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: (USER.CNTLLIB('MEMBER'). ***'
   SAY '*** ADDMEMB EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
BUILD_IEBGENER_JOBJCL:
J = 0
JN = MEMBER
"DELSTACK"
JCL.1 = "//"JN" JOB" ACCTCDE",'GENER ADD PDS MEMBER',NOTIFY="TSOID","
JCL.2 = "//             CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),REGION=4M"
JCL.3 = "/*ROUTE  XEQ CHRMSYSM"
JCL.4 = "/*ROUTE  PRINT CHRMSYSM."TSOID
JCL.5 = "//PDSADD    EXEC PGM=IEBGENER"
JCL.6 = "//SYSIN     DD DUMMY"
JCL.7 = "//SYSPRINT  DD SYSOUT=*"
JCL.8 = "//SYSUT2    DD DSN="TSOID".MAINT.CNTLLIB("MEMBER"),DISP=SHR"
JCL.9 = "//SYSUT1    DD DATA,DLM=$$"
DO 9
   J = J + 1
   QUEUE JCL.J
END
RETURN

/*     */
BUILD_IEBGENER_SYSIN:
DO I = 1 TO DATA.0
   QUEUE DATA.I
END
RETURN

/*     */
BUILD_IEBGENER_ENDJCL:
E = 0
END.1 = "$$"
END.2 = "/*"
END.3 = "//"
DO 3
   E = E + 1
   QUEUE END.E
END
RETURN

/*     */
WRITE_IEBGENER_JOB:
"ALLOC DA(DIALOG.ISPFILE("MEMBER")) FILE(ISPFILE) SHR"
NUMRCDS = QUEUED()
"EXECIO" NUMRCDS "DISKW ISPFILE (FINIS"
SRC = RC
"FREE FILE(ISPFILE)"
IF SRC ¬= 0 THEN DO
   SAY '*** WRITE ERROR: ERROR WRITING ('TSOID'.DIALOG.ISPFILE('MEMBER'). ***'
   SAY '*** ADDMEMB EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN

/*     */
SUBMIT_IEBGENER_JOB:
"SUBMIT '"TSOID".DIALOG.ISPFILE("MEMBER")'"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** SUBMIT ERROR: ( 'TSOID'.DIALOG.ISPFILE('MEMBER'). ***'
   SAY '*** ADDMEMB EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
RETURN
