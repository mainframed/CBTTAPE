/* REXX LBEXPAND LAM Support  */
address "ISPEXEC";
"ISREDIT MACRO (TYPEEXP)";
upper typeexp;
"ISREDIT (CURLINE,CURCOL) = CURSOR"; /* do full xpnd if cursr at top */
if typeexp="" & curline¬=0 & curcol ¬=0 then do; /* 1 line expand onl*/
    "ISREDIT LIBEXPND"; /* perform expansion */
    exit 0;
    end;
if typeexp¬="DSN" & typeexp¬="PROC" then do; /* invalid param */
         zedsmsg = "INVPRM?";
         zedlmsg = "Invalid parameter; DSN or PROC";
         "SETMSG MSG(ISRZ001)";
         exit 4;
         end;
"ISREDIT (TOP) = LINENUM .ZFIRST"; /* Top */
"ISREDIT (BOTTOM) = LINENUM .ZLAST"; /* Bottom */
if typeexp = "PROC" then do;
  do curline = bottom to top by -1; /* loop backwards */
    "ISREDIT (LINEDATA) = LINE" curline; /* Get data line */
    if substr(linedata,1,2) ¬= "//" then iterate; /*data*/
    if substr(linedata,3,1) = "*" then iterate; /* comment */
    if pos(" EXEC ",linedata)=0 then iterate; /*execonly */
    if pos("PGM=",linedata)¬=0 then iterate;
    "ISREDIT CURSOR =" curline; /* reset line ptr */
    "ISREDIT LIBEXPND"; /* perform expansion */
   end; /* end proc do loop */
     end; /* proc end */
/* dsn processing */
dslist = "BUILDING.PRODSYSI.MASTER" ,
         "BUILDING.PRODRAMI.MASTER" ,
         "BUILDING.EMRGSYSI.MASTER" ,
         "BUILDING.EMRGRAMI.MASTER" ,
         "SYS1.PRODSYSI.MASTER" ,
         "SYS1.PRODRAMI.MASTER" ,
         "SYS1.EMRGSYSI.MASTER" ,
         "SYS1.EMRGRAMI.MASTER" ,
         "CICS.VSAM.MASTER" ,
         "PD.BATCH.VSAM.MASTER";
do curline = bottom to top by -1; /* loop backwards */
    "ISREDIT (LINEDATA) = LINE" curline; /* Get data line */
    if substr(linedata,1,2) ¬= "//" then iterate; /*data*/
    if substr(linedata,3,1) = "*" then iterate; /* comment */
    if pos(" DD ",linedata)=0 then iterate; /* DD only */
    if pos("DSN=",linedata)=0 & pos("DSNAME=",linedata)=0 then iterate;
    if pos("DSN=",linedata)¬=0 then ,
        parse var linedata . "DSN=" dsname "(" member ")" . ;
     else ,
        parse var linedata . "DSNAME=" dsname "(" member ")" . ;
    if member = "" then iterate;
    if wordpos(dsname,dslist)=0 then iterate; /* not dsname */
    "ISREDIT CURSOR =" curline; /* reset line ptr */
    "ISREDIT LIBEXPND"; /* perform expansion */
end;
"ISREDIT CURSOR = 1"; /* reset line ptr */
exit 1; /* reset to screen top */
