/* REXX LIBFCRSR LAM Support  */
address "ISPEXEC";
"ISREDIT MACRO (LIBDSN)"; /* Access dsname */
if libdsn = "" then do;
      zedsmsg = "LIBDSN?";
      zedlmsg = "Re-enter LIBFCRSR with Librarian master filename"|| ,
            " and member name";
      "SETMSG MSG(ISRZ001)";
      exit 4;
      end;
if substr(libdsn,1,9)="BUILDING." then do;
     parse var libdsn . "." rdsn;
     libdsn = "SYS1."rdsn;
     end;
if substr(libdsn,1,10)="'BUILDING." then do;
     parse var libdsn . "." rdsn;
     libdsn = "'SYS1."rdsn;
     end;
/* "ISREDIT PROCESS DEST"; */
ddname = liballoc(LIBDSN); /* Dynalloc libmaster w/lam; ret ddname*/
if ddname = "ERROR" then do;
      zedsmsg = "LIBDSN?";
      zedlmsg = "Invalid Librarian master filename";
      "SETMSG MSG(ISRZ001)";
      exit 4;
      end;
parse var libdsn "'" bldldsn "(" bldlmem ")'" . ;
if bldlmem = "" then do;
      zedsmsg = "MEMBER?";
      zedlmsg = "Invalid Librarian member name";
      "SETMSG MSG(ISRZ001)";
      exit 4;
      end;
bldddn = liballoc("'"bldldsn"'"); /*Dynal libmast w/lam; ret ddname*/
if bldddn = "ERROR" then do;
      zedsmsg = "LIBDSN?";
      zedlmsg = "Invalid Librarian master filename";
      "SETMSG MSG(ISRZ001)";
      exit 4;
      end;
bldldir = xlibemem(bldddn,bldlmem); /* fetch dir entry */
if bldldir = ""  then do;
      zedsmsg = "MEMBER?";
      zedlmsg = "Invalid Librarian member name";
      "SETMSG MSG(ISRZ001)";
      exit 4;
      end;
msg_status = MSG("OFF");
address "TSO" "EXECIO * DISKR" ddname "(STEM LIBFCRSR. FINIS";
saverc = rc;
msg_status = MSG("ON");
if saverc Â¬= 0 then do;
      zedsmsg = "MEMBER?";
      zedlmsg = "Invalid Librarian member name";
      "SETMSG MSG(ISRZ001)";
      exit 4;
      end;
"ISREDIT (CURLINE,CURCOL) = CURSOR"; /* Access cursor position */
do i = 1 to libfcrsr.0;
   libfcrsr.i = translate(libfcrsr.i,"'",'"'); /* no double quotes*/
   dataline = libfcrsr.i;
   "ISREDIT LINE_AFTER" CURLINE+i-1 '= (DATALINE)';
end;
exit 0;
