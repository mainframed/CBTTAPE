/* REXX EXEC : ( CCFDYLDQ ).
   FUNCTION: INVOKED BY CCF SKELETON JCL ( CCFDYLDB ) FOR PROMOTION OF
             DYLAKOR/DB2 SOURCE PROGRAMS INTO QA OR PROD LIBRARIES.
             THIS REXX EXEC PROGRAM WILL PERFORM THE FOLLOWING TASKS:
             1. READ/CHECK THE SOURCE PROGRAM TO GET THE DYLAKOR
                REPORT AND DB2 LOAD MODULE NAMES, AND THE DB2 PLAN.
             3. BUILD THE IDCAMS DELETE LOAD MODULE SYSIN MEMBER.
             4. BUILD THE IEBCOPY COPY LOAD MODULE SYSIN MEMBER.
             5. BUILD THE DB2 BIND CONTROL STATEMENTS SYSIN MEMBER.
             6. BUILD THE DB2 GRANT CONTROL STATEMENTS SYSIN MEMBER.
             7. BUILD IEHPROGM RENAME GENERATED ASSEMBLER PROGRAM.
             8. BUILD LINKAGE EDITOR SYSLIN CONTROL SYSIN MEMBER.
             9. BUILD LINKAGE EDITOR SYSLIN CONTROL SYSIN MEMBER.
             INPUT  : ( SYS1.QASRCE.MASTER(&MEMBER).
             OUTPUT : ( SYSS.QA.CCF.DYLAKOR.DYLCOPY.CNTLLIB(&MEMBER).
                      ( SYSS.QA.CCF.DYLAKOR.DYLDEL01.CNTLLIB(&MEMBER).
                      ( SYSS.QA.CCF.DYLAKOR.DYLDEL02.CNTLLIB(&MEMBER).
                      ( SYSS.QA.CCF.DYLAKOR.DB2BIND.CNTLLIB(&MEMBER).
                      ( SYSS.QA.CCF.DYLAKOR.DB2GRANT.CNTLLIB(&MEMBER).
                      ( SYSS.QA.CCF.DYLAKOR.IEHPROGM.CNTLLIB(&MEMBER).
                      ( SYSS.QA.CCF.DYLAKOR.LINKEDIT.CNTLLIB(&MEMBER).
                                                                      */
TRACE O
/*     */
HOUSE_KEEPING:
ADDRESS TSO
ARG TSOID MASTER MEMBER
PARMS = 'TSOID MASTER MEMBER'
DO I = 1 TO 3
   IPARM = WORD(PARMS,I)
   XPARM = VALUE(IPARM)
   IF XPARM ¬= '' THEN ITERATE
   SAY '*** PARM ERROR: MISSING PARM VALUE FOR ('IPARM'). ***'
   SAY '*** CCFDYLDQ EXEC TASK CANCELLED - RC = (024). ***'
   EXIT 024
END
MCNT = 2
PARMLIB = 'SYSS.TECH.COMMON.PARMLIB'
DB2PARM = SYSDSN("'"PARMLIB"(DB2ADMIN)'")
IF DB2PARM = 'MEMBER NOT FOUND' THEN DO
   SAY '*** MEMBER ERROR: MEMBER NOT FOUND ('PARMLIB'(DB2ADMIN). ***'
   SAY '*** CCFDYLDQ EXEC SUBTASK CANCELLED - RETURN CODE: ('028'). ***'
   EXIT 028
END
X = MSG("OFF")
"FREE FILE(PARMDD)"
"FREE FILE(DB2DD)"
DEL.1 = "  IF LASTCC = 8 THEN"
DEL.2 = "     DO"
DEL.3 = "       SET MAXCC = 0"
DEL.4 = "     END"
LIBNODE = POS('PRODSRCE',MASTER)
IF LIBNODE ¬= 0 THEN DO
   CALL GET_DYLAKOR_MODULE_NAMES
   IF ASMSW = 'NO' THEN CALL ASSEMBLE_NAME_ERROR
   IF RPTSW = 'NO' THEN CALL REPORT_NAME_ERROR
   DB2SYS = 'DSNA'
   CALL BUILD_DB2_BIND_SYSIN
   OUTMEMB = 'DB2BIND'
   CALL WRITE_SYSIN_MEMBER
   CALL BUILD_DB2_GRANT_SYSIN
   OUTMEMB = 'DB2GRANT'
   CALL WRITE_SYSIN_MEMBER
   EXIT 2
END
LIBNODE = POS('EMRGSRCE',MASTER)
IF LIBNODE ¬= 0 THEN DO
   CALL GET_DYLAKOR_MODULE_NAMES
   IF ASMSW = 'NO' THEN CALL ASSEMBLE_NAME_ERROR
   IF RPTSW = 'NO' THEN CALL REPORT_NAME_ERROR
   LOADLIB = 'SYS1.EMERGNCY.BATCH.LOADLIB'
   CALL BUILD_IDCAMS_DELETE_SYSIN
   OUTMEMB = 'DYLDEL02'
   CALL WRITE_SYSIN_MEMBER
   DB2SYS = 'DSNA'
   CALL BUILD_DB2_BIND_SYSIN
   OUTMEMB = 'DB2BIND'
   CALL WRITE_SYSIN_MEMBER
   CALL BUILD_DB2_GRANT_SYSIN
   OUTMEMB = 'DB2GRANT'
   CALL WRITE_SYSIN_MEMBER
   EXIT 3
END
LIBNODE = POS('QASRCE',MASTER)
IF LIBNODE = 0 THEN EXIT 0
/*
        M A I N   R O U T I N E   S E C T I O N     */
/*     */
MAIN_ROUTINE:
DB2SYS = 'DSNX'
CALL GET_DYLAKOR_MODULE_NAMES
IF ASMSW = 'NO' THEN CALL ASSEMBLE_NAME_ERROR
IF RPTSW = 'NO' THEN CALL REPORT_NAME_ERROR
LOADLIB = 'SYS1.TEST.BATCH.LOADLIB'
CALL BUILD_IDCAMS_DELETE_SYSIN
OUTMEMB = 'DYLDEL01'
CALL WRITE_SYSIN_MEMBER
LOADLIB = 'SYS1.QA.BATCH.LOADLIB'
CALL BUILD_IDCAMS_DELETE_SYSIN
OUTMEMB = 'DYLDEL02'
CALL WRITE_SYSIN_MEMBER
CALL BUILD_IEBCOPY_COPY_SYSIN
OUTMEMB = 'DYLCOPY'
CALL WRITE_SYSIN_MEMBER
CALL BUILD_DB2_BIND_SYSIN
OUTMEMB = 'DB2BIND'
CALL WRITE_SYSIN_MEMBER
CALL BUILD_DB2_GRANT_SYSIN
OUTMEMB = 'DB2GRANT'
CALL WRITE_SYSIN_MEMBER
CALL BUILD_IEHPROGM_SYSIN
OUTMEMB = 'IEHPROGM'
CALL WRITE_SYSIN_MEMBER
CALL BUILD_LINKEDIT_SYSIN
OUTMEMB = 'LINKEDIT'
CALL WRITE_SYSIN_MEMBER
EXIT 1
/*
       S U B R O U T I N E   S E C T I O N     */
/*     */
GET_DYLAKOR_MODULE_NAMES:
ASMSW = 'NO'
RPTSW = 'NO'
LIBDSN = "'"MASTER"("MEMBER")'"
LIBDD = LIBALLOC(LIBDSN)
IF LIBDD = "ERROR" THEN DO
   ERC = 100
   SAY '*** DYNAMIC ALLOCATION ERROR: ('MASTER'). ***'
   SAY '*** MODULE NAMES CANNOT BE READ FOR ('MEMBER') PROGRAM. ***'
   SAY '*** CCFDYLDQ EXEC SUBTASK CANCELLED - RC = ('ERC'). ***'
   EXIT ERC
END
ADDRESS TSO "EXECIO * DISKR" LIBDD "(FINIS STEM PROGDATA."
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** INVALID OR MISSING LIBRARIAN MEMBER NAME. ***'
   SAY '*** CCF DB2B EXEC TASK CANCELLED - RC = '024'. ***'
   EXIT
END
DO I = 1 TO PROGDATA.0
   KEY1 = POS('OPTION FREEZE',PROGDATA.I)
   IF KEY1 ¬= 0 THEN DO
      KEY2 = WORDPOS('FREEZE',PROGDATA.I)
      INDEXKEY = KEY2 + 1
      DYLMOD = WORD(PROGDATA.I,INDEXKEY)
      RPTSW = 'YES'
      MOD.1 = DYLMOD
   END
   KEY3 = POS('TSO_ATTACH PLANID',PROGDATA.I)
   IF KEY3 ¬= 0 THEN DO
      KEY4 = WORDPOS('PLANID',PROGDATA.I)
      INDEXKEY = KEY4 + 1
      ASMMOD = WORD(PROGDATA.I,INDEXKEY)
      DB2PLAN = WORD(PROGDATA.I,INDEXKEY)
      ASMSW = 'YES'
      MOD.2 = ASMMOD
   END
END
RETURN
/*     */
ASSEMBLE_NAME_ERROR:
ERC = 024
SAY '*** SYNTAX REQUIREMENTS ERROR FOR ('MEMBER') PROGRAM. ***'
SAY '*** ASSEMBLE MODULE NAME NOT FOUND FROM KEY SYNTAX DATA. ***'
SAY '*** CCFDYLDQ EXEC SUBTASK CANCELLED - RC = ('ERC'). ***'
EXIT ERC
/*     */
REPORT_NAME_ERROR:
ERC = 024
SAY '*** SYNTAX REQUIREMENTS ERROR FOR ('MEMBER') PROGRAM. ***'
SAY '*** REPORTS MODULE NAME NOT FOUND FROM KEY SYNTAX DATA. ***'
SAY '*** CCFDYLDQ EXEC SUBTASK CANCELLED - RC = ('ERC'). ***'
EXIT ERC
/*     */
BUILD_IDCAMS_DELETE_SYSIN:
"DELSTACK"
DO M = 1 TO MCNT
   RECORD = "  DELETE "LOADLIB"("MOD.M") FILE(LOADLIB)"
   QUEUE RECORD
END
DO D = 1 TO 4
   QUEUE DEL.D
END
RETURN
/*     */
BUILD_IEBCOPY_COPY_SYSIN:
I = 0
M = 0
CNTL = "       SELECT MEMBER=("
"DELSTACK"
RECORD = "  COPY OUTDD=OUTDD1"
QUEUE RECORD
RECORD = "       INDD=((INDD1,R))"
QUEUE RECORD
DO MCNT
   M = M + 1
   I = I + 1
   IF I = 1 THEN SYSIN = CNTL||MOD.M
   IF (I > 1) & (I < 5) THEN SYSIN = SYSIN||","||MOD.M
   IF (I = 5) THEN DO
      SYSIN = SYSIN||","||MOD.M||")"
      QUEUE SYSIN
      I = 0
   END
END
IF I > 0 THEN DO
   SYSIN = SYSIN||")"
   QUEUE SYSIN
END
RETURN
/*     */
BUILD_DB2_BIND_SYSIN:
"DELSTACK"
BIND.1 = " DSN SYSTEM("DB2SYS")"
BIND.2 = " BIND PLAN("DB2PLAN") -"
BIND.3 = "      RETAIN -"
BIND.4 = "      MEMBER("MEMBER")"
BIND.5 = " END"
DO B = 1 TO 5
   QUEUE BIND.B
END
RETURN
/*     */
BUILD_DB2_GRANT_SYSIN:
"DELSTACK"
COMRCD = "  COMMIT;"
BNDCNT = 4
BND.1 = "        GRANT"
BND.2 = "        BIND,"
BND.3 = "        EXECUTE"
BND.4 = "  ON PLAN "DB2PLAN" TO"
"ALLOC DA('"PARMLIB"(DB2ADMIN)') FILE(PARMDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('PARMLIB'(DB2ADMIN). ***'
   SAY '*** CCFDYLDQ EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
"EXECIO * DISKR PARMDD (FINIS STEM AUTH."
SRC = RC
"FREE FILE(PARMDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO READ ERROR: ('PARMLIB'(DB2ADMIN). ***'
   SAY '*** CCFDYLDQ EXEC TASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT SRC
END
DO B = 1 TO BNDCNT
   QUEUE BND.B
END
DO I = 1 TO AUTH.0
   AUTHUSR = WORD(AUTH.I,1)
   AUSER = TSOID','
   IF AUSER = AUTHUSR THEN ITERATE
   DB2AUTH = '        '||AUTHUSR
   QUEUE DB2AUTH
END
QUEUE COMRCD
RETURN
/*     */
BUILD_IEHPROGM_SYSIN:
"DELSTACK"
CONTX = "         X"
RENAM = "    RENAME  DSNAME=SYSS.QA.CCF.DYLAKOR.ASMLIB,VOL=3390=SYS017,"
RCD.1 = RENAM||CONTX
RCD.2 = "               NEWNAME="MEMBER",MEMBER="ASMMOD
DO R = 1 TO 2
   QUEUE RCD.R
END
RETURN
/*     */
BUILD_LINKEDIT_SYSIN:
"DELSTACK"
RCD.1 = "  INCLUDE DB2LIB(DSNELI)"
RCD.2 = "  NAME "ASMMOD"(R)"
DO R = 1 TO 2
   QUEUE RCD.R
END
RETURN
/*     */
WRITE_SYSIN_MEMBER:
"ALLOC DA('SYSS.QA.CCF.DYLAKOR."OUTMEMB".CNTLLIB("MEMBER")') FILE(DYLDD) SHR"
SRC = RC
IF SRC ¬= 0 THEN DO
   SAY '*** DATASET ALLOCATE ERROR: ('OUTMEMB'('MEMBER'). ***'
   SAY '*** CCFDYLDQ EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT 8
END
RCDCNT = QUEUED()
"EXECIO" RCDCNT "DISKW DYLDD (FINIS"
SRC = RC
"FREE FILE(DYLDD)"
IF SRC ¬= 0 THEN DO
   SAY '*** EXECIO WRITE ERROR: ('OUTMEMB'('MEMBER'). ***'
   SAY '*** CCFDYLDQ EXEC SUBTASK CANCELLED - RETURN CODE: ('SRC'). ***'
   EXIT 8
END
RETURN
