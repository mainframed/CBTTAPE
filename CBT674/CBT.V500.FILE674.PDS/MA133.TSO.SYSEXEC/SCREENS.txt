/* REXX - exec to list all active ISPF screens by name            */
/* scammed and altered from a bunch of examples from:             */
/* http://listserv.nd.edu/archives/ispf-l.html                    */
Address ISPEXEC
"VGET ZSCREEN SHARED"

Tcb = Ptr(132+Ptr(540))       /* current ISPTASK TCB              */
Tcb = Ptr(Tcb+132)            /* ISPMAIN  tcb via tcbotc          */
Tcb = Ptr(Tcb+136)            /* ISPTASK  tcb via tcbltc          */
Do While Tcb \= 0
  Parse Value Tldname(Tcb) With ScrNum ScrName
  If ScrNum = ZSCREEN Then Active = '*'
  Else Active = ''
  Say 'TCB='d2x(Tcb) 'Screen number='ScrNum 'Screen name='ScrName Active
  Tcb = Ptr(128+Tcb)          /* chain thru tcbntc                */
End
Return

Tldname: Procedure
SaveArea = Ptr(112+Arg(1))    /* get save area off tcb            */
If Storage(d2x(SaveArea+40),4) = 'ISPF' Then Do
  R1 = Ptr(SaveArea+24)       /* reg1 is 24 into the save area    */
  Tld = Ptr(R1)               /* reg1 is the pointer to the tld   */
  Num = Tld+3                 /* the screen num is +3 into tld    */
  Perm = Tld+841              /* the perm option is +841 into tld */
  Name = Tld+852              /* the screen name is +852 into tld */
  Return Translate(Storage(d2x(Num),1),' ','00'x) ,
    Translate(Storage(d2x(Name),8),' ','00'x) ,
    Translate(Bitand(Storage(d2x(Perm),1),'80'x),'PT','8000'x)
End
Return ' '

Ptr: Return c2d(Bitand(Storage(d2x(Arg(1)),4),'7FFFFFFF'x))
