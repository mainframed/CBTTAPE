  /* rexx */
  trace o

  /*___________________________________________________________*/
  /*                                                           */
  say
  say "Examples of call:                                      "
  say "Command ===> tso c d a         <-- MVS  command D A    "
  say "Command ===> tso c $di         <-- JES2 command $DI    "
  say "Command ===> tso viewit c d a  <-- result in View      "
  say
  /*___________________________________________________________*/
  /* Set up command                                            */

  drop c.
  c.0 = 1
  c.1 = left(arg(1),80)

  /*___________________________________________________________*/
  /* Execute command.                                          */
  /* Result in isfulog. stem                                   */

  rc = isfcalls("on")                    /* access = on        */
  isfcons = "sdsf"random()               /* console = sdsfxxx  */
  isfdelay = 1 /* Wait 1 second before retrieving results      */
  address sdsf isfslash "("c.") (wait)"
  xrc = rc                               /* save return code   */
  rc = isfcalls("off")                   /* access = off       */

  /*___________________________________________________________*/
  /* If error, display command and error code.                 */
  /*                                                           */

  if xrc > 0 then do
     say "execution of "
     do i = 1 to c.0
        say '  'c.i
      end /* do i = 1 to c.0                                   */
      say "gave code "xrc
      exit 16
  end /* if xrc > 0                                            */

  /*___________________________________________________________*/
  /* If ok, display result.                                    */
  /*                                                           */

  do i = 1 to isfulog.0
     say isfulog.i
  end /* do i = 1 to isfulog.0                                 */
  exit

  /*___________________________________________________________*/
