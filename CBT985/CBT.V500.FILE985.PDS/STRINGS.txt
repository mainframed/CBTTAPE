/* rexx */

/* Execute strings then fold Unix commands.                 */
/* Display printable strings folded to 72 columns.          */
/* There is an optional parameter which indicates           */
/* the minimum printable string length. It may be           */
/* obtained from the Prompt field.                          */

/* Example:                                                 */
/*          VIEW      XX.YYYY.USER01.LOADLIB                */
/*  Command ===>                                            */
/*             Name     Prompt        Alias-of     Size     */
/*  _________ ABCLDCN0                           000019D8   */
/*  strings__ DEFGB790 1   <-- mimimum length  1 000035C0   */
/*  strings__ DEFGB80D     default min length 20 00007F10   */

trace o
dsnprint_prefix = "XX.YYYY" /* please change dataset high level qualifiers */

/*__________________________________________________________*/
/* Get parameters                                           */

parse arg dsn atleast .
/* atleast  optional parameter, may be obtained from prompt */
/*          field.                                          */
/* atleast  Display printable strings of at least           */
/*          atleast characters.                             */
if atleast = '' then atleast = 20 /* at least 20 char.      */

/*__________________________________________________________*/
/* Allocate STDIN.                                          */
dsn = strip(dsn,,"'") /* remove quotes if any               */
"alloc fi(stdin) da('"dsn"') shr reu"

/*__________________________________________________________*/
/* Allocate STDOUT dsnprint dataset.                        */
time_stamp = t!!time('s')
parse source . . rexxname .
dsnprint = dsnprint_prefix"."userid()"."rexxname"."time_stamp
"allocate fi(stdout) da('"dsnprint"') reuse",
"new catalog",
"lrecl(80) recfm(f b) blksize(0)",
"space(1 15) tracks"
arc = rc
if arc > 0 then do
   say "STDOUT DSNPRINT allocate gave code "arc
   exit(12)
end /* if arc > 0 */

/*__________________________________________________________*/
/* Initialize stem stderr.                                  */
drop stderr.
stderr.0 = 0

/*__________________________________________________________*/
/* Set up env. environment variables stem.                  */
drop env.
/* z/OS Unix commands in local codepage                     */
env.1 = "LANG=En_US.IBM-1140"
/* env.1 = "LANG=Fr_FR.IBM-1147" */ /* locale gives LANG=C  */
env.0 = 1

/*__________________________________________________________*/
/* Build z/OS Unix command.                                 */

cmd = "  strings -z -n "atleast" | fold -w 72"
/* strings Only printable fields                            */
/*  -z    Ignores the POSIX definition of a string and      */
/*        searches for any group of printable characters    */
/*        greater than four in length.                      */
/*  -n    number Displays strings of printable characters   */
/*        that are at least number characters in length.    */
/* fold  Visualize on w columns                             */
/*  -w    maximum line length of width characters           */

call bpxwunix cmd,'DD:STDIN','DD:STDOUT',stderr.,env.

/* say 'stderr.0 = 'stderr.0                                */

if stderr.0 <> 0 then /* z/OS Unix command failed           */
   do i = 1 to stderr.0
      say 'stderr.'i' ='stderr.i
   end                /* z/OS Unix command failed           */
else do               /* z/OS Unix command succeeded        */

/*__________________________________________________________*/
/* Display DSNPRINT                                         */
  "ispexec lminit dataid(dd1) dataset('"dsnprint"') enq(shr)"
  "ispexec view   dataid("dd1")"
  "ispexec lmfree dataid("dd1")"

end                   /* z/OS Unix command succeeded        */

/*__________________________________________________________*/
/* Clean up                                                 */
"free fi(stdin stdout)"
msgs = msg("off")
address tso
"delete '"dsnprint"'"
msgs = msg(msgs)
exit
