  /* rexx */
  trace o

  /* Driver
    dsn = 'USER01.LOAD'
    enq_number = whohas(dsn) /* how many enqueues?             */
    say "enq_number = "enq_number
    exit
    whohas: procedure
  */

  /*___________________________________________________________*/
  /*                                                           */
  /* Example of call:                                          */
  /* enq_count = whohas(dsn)                                   */
  /*                                                           */
  /* Display result of /d grs,res=(sysdsn,dsn)                 */
  /* and enq_count, the number of owners.                      */
  /*                                                           */
  dsn = word(arg(1),1)    /* first word                        */
  dsn = strip(dsn,,"'")   /* remove surrounding quotes if any  */
  upper dsn              /* uppercase                          */
  say
  say "Example of use on Command line:                         "
  say "Command ===> tso whohas USER01.LOAD                     "
  say
  say "Example of use in ISPF 3.4, before dsname:              "
  say "whohas   USER01.LOAD1                                   "
  say
  /*___________________________________________________________*/
  /*                                                           */
  /* Set up commands                                           */

  drop command.
  command.0 = 1
  command.1 = left('D GRS,RES=(SYSDSN,'dsn')',80)
  /*___________________________________________________________*/
  /*                                                           */
  /* Execute command                                           */
  /* Result in isfulog. stem                                   */

  rc = isfcalls("on")                    /* access = on        */
  isfcons = "sdsf"random()               /* console = sdsfxxx  */
  isfdelay = 1 /* Wait 1 second before retrieving results      */
  /* isfulog.0 = 2 if isfdelay = 0  delay is necessary         */
  address sdsf isfslash "("command.") (wait)"
  xrc = rc                               /* save return code   */
  rc = isfcalls("off")                   /* access = off       */

  if xrc > 0 then do
     say "execution of "
     do i = 1 to command.0
        say '  'command.i
      end /* do i = 1 to command.0                             */
      say "gave code "xrc
      return -1                          /* abnormal return    */
  end /* if xrc > 0                                            */
  /*___________________________________________________________*/
  /*                                                           */
  /* Examine result                                            */

  /* enq_count is the number of owners                         */
  /* Search for 'NO REQUESTORS'                                */
  do i = 2 to isfulog.0
     if pos('NO REQUESTORS',isfulog.i) > 0 then do
        enq_count = 0
        leave
     end /* if pos('NO REQUESTORS',isfulog.i) > 0              */
  end /* do i = 2 to isfulog.0                                 */

  if enq_count <> 0 then enq_count = isfulog.0 - 5

  /* List owners                                               */
  say 'Contention analysis for 'dsn':'
  do i = 2 to isfulog.0
     say isfulog.i
  end /* do i = 2 to isfulog.0                                 */
  say "Number of owners of dsn '"dsn"' = "enq_count

  return enq_count                       /* normal return      */
  /*___________________________________________________________*/
