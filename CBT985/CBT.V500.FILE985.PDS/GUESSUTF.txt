/* rexx */

/* Check heuristically if string is in UTF-8                   */
/* (result will be a guess).                                   */

/* Example of call:                                            */
/* call 'GUESSUTF' string                                      */
/* answer = result /* 'utf8' or 'not-utf8' */                  */

string = arg(1)

/* Most used characters in English language:                   */
/* (cf. "Letter frequency" article of Wikipedia.               */
/* Space is slightly more frequent than the top letter "e" )   */

/*        space "E"   "e"   "T"   "t"    "A"   "a"             */
EBCDIC = "X'40'  X'C5' X'85' X'E3' X'A3'  X'C1' X'81' "
ASCII  = "X'20'  X'45' X'65' X'54' X'74'  X'41' X'61' "
/* weight    8      3     3     2     2      2     2           */
list = EBCDIC!!ASCII

do m = 1 to words(list)
    w = word(list,m)                    /* m-th word           */
    two = substr(w,3,2)                 /* X'40' -->  40       */
    interpret "tX"two" = tally_char(x2c('"two"'),string)"
end /* do m = 1 to words(list)                                 */

/* Calculate weighted sums                                     */
/* Prefix t for tally.                                         */
/*        space  "E"      "e"      "T"    "t"    "A"   "a"     */
tEBCDIC = tX40*8 +tXC5*2 + tX85*2 + tXE3 + tXA3 + tXC1 +tX81
tASCII  = tX20*8 +tX45*2 + tX65*2 + tX54 + tX74 + tX41 +tX61

if tASCII < tEBCDIC then return "not-utf8"
                    else return "utf8"
/* ___________________________________________________________ */
tally_char: procedure
/* returns how many times char in string                       */
char   = arg(1)
string = arg(2)

/* wo is length of (string WithOut char)                       */
/* space(string,0) remove all spaces from string               */
if char = ' ' then wo = length(space(string,0))
/* this translate change blank in '%' and char in blank        */
else interpret "wo = length(space(translate(string,'% ',' "char"'),0))"

return length(string) - wo
