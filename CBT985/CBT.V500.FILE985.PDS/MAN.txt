/* rexx */
/* Execute man Unix command in MVS or z/OS Unix.               */
/* man ouput is translated to terminal codepage if obtained    */
/* (or to a rexx default codepage if the terminal codepage     */
/* is not obtained)                                            */
/* then it is displayed with ISPF VIEW.                        */

/* If the terminal code page is not obtained, we use:          */
ccsid_default = '01140' /* '01140' English US with Euro sign   */
                        /* '01147' French     with Euro sign   */

/* rexx is placed in a MVS library concatenated to SYSPROC     */
/* or SYSEXEC                                                  */

/* Examples:                                                   */
/*  Command ===> tso man grep                                  */

/*  Command ===> cmde          (to preserve lower case)        */
/*  Enter TSO commands below:                                  */
/*  ===> man -k compare                                        */

/* In z/OS Unix,                                               */
/*                                                             */
/*  Command ===> tso omvs                                      */
/*  $ tso man ls                                               */
/*        versus                                               */
/*  $ man ls                                                   */
/*  $ exit                                                     */

/* Alternative:                                                */
/*  Command ===> epdf /        then                            */
/*  Command ===> tso man grep  instead of man grep             */
/*_____________________________________________________________*/

trace o
parse source var
rexxname = word(var,3)

ccsid = get_terminal_codepage() /* from ISPF variable ztermcp5 */
/* terminal codepage not obtained if the rexx is called from OMVS */
if ccsid = 'ZTERMCP5' then ccsid = ccsid_default

/*_____________________________________________________________*/
/* Build man z/OS Unix command in IBM-1047 codepage (default). */
parse arg rest
/* rest is what is at right of man.                            */
rest = strip(rest)
pipe  = '4F'x    /* pipe symbol (vertical bar) in IBM-1047     */

/* cmd = 'MANPATH=/usr/man/%L man' rest <-- result in IBM-1047 */
cmd = 'man -M /usr/man/%L 'rest pipe 'iconv -f 1047 -t 'ccsid

/*_____________________________________________________________*/
/* Allocate STDOUT dsnprint dataset.                           */
call set_up_prefix /* create prefix for new file               */
time_stamp = t!!time('s')
dsnprint = prefix"."rexxname"."time_stamp
"allocate fi(stdout) da('"dsnprint"') reuse",
"new catalog",
"lrecl(132) recfm(f b) blksize(0)",
"space(1 15) tracks"
arc = rc
if arc > 0 then do
   say "STDOUT DSNPRINT allocate gave code "arc
   exit(12)
end /* if arc > 0 */

/*_____________________________________________________________*/
/* Initialize stem stderr.                                     */
drop stderr.
stderr.0 = 0

/*_____________________________________________________________*/
/* Initialize env. environment variables stem.                 */
drop env.

/* bpxwunix command in French:                                 */
/* env.1 = "LC_ALL=Fr_FR.IBM-1147"; env.0 = 1                  */
/* bpxwunix command in English US:                             */
/* env.1 = "LC_ALL=En_US.IBM-1140"; env.0 = 1                  */

/* bpxwunix command is in IBM-1047 (z/OS Unix and C) default:  */
env.0 = 0

/*_____________________________________________________________*/
/* Execute Unix command                                        */
call bpxwunix cmd,,'DD:STDOUT',stderr.,env.

if stderr.0 <> 0 then /* z/OS Unix command failed              */
   do i = 1 to stderr.0
      say 'stderr.'i' ='stderr.i
   end                /* z/OS Unix command failed              */
else do               /* z/OS Unix command succeeded           */

   /* Display DSNPRINT                                         */
    address ispexec
      "control errors return"
      "control display line start(0)" /* if rexx called under OMVS */
      "view   dataset('"dsnprint"')"
      view_rc = rc
    address

    if view_rc > 0 then do
       say "view for dataset "dsnprint" gave code "view_rc
       "free fi(stdin)"
       exit(16)
    end /* if view_rc > 0                                      */

end                   /* z/OS Unix command succeeded           */

/*_____________________________________________________________*/

/* Clean up                                                    */
"free fi(stdout)"
msgs = msg("off")
address tso
  "delete '"dsnprint"'"
msgs = msg(msgs)
exit

/*___________________________________________________________________*/
set_up_prefix:

/* Set up new output file prefix.                                    */
prefix = sysvar('syspref')              /* tso profile prefix        */
uid    = sysvar('sysuid')               /* tso userid                */
if prefix = '' then prefix = uid        /* use uid if null prefix    */
if prefix <> '' & prefix <> uid then    /* different prefix than uid */
   prefix = prefix'.'uid                /* use prefix.uid            */

return
/*___________________________________________________________________*/
get_terminal_codepage:

/* example of call: ccsid = get_terminal_codepage()                  */
/* e.g. ccsid = '001140' English US                                  */
/*      ccsid = '001147' French                                      */

/* ccsid with 5 digits may be used with iconv -f or -t               */
/* ccsid with 5 digits is required if using                          */
/* address ispexec                                                   */
/*   "trans fromccsid(...) toccsid(...) fromvar(...) tovar(...)"     */

address ispexec
  "control errors return"
  "vget ztermcp5 shared"
address

return(ztermcp5)
/*___________________________________________________________________*/
