//BOZO01  JOB CLASS=D,MSGCLASS=S,NOTIFY=&SYSUID
/*JOBPARM L=999999,BYTES=999999,PAGES=99999,S=*
//*
//* Some of the code below may be useful
//* to send .bat files to Windows,
//* to have them executed by a scheduled task and
//* to return the result to z/OS.
//*
//*
//* Send hello.bat to Windows server.
//* It will be converted in Windows 1252 aka IBM-1252, cp1252.
//* It will be executed by a periodically scheduled task
//* and return result to sender.
//*______________________________________________________________
//* Remark regarding UTF-8:
//*   FTP will not accept as input or output a RECFM=FB file
//*   for multibyte transfer.
//*   1 UTF-8 character = 1 to 4 bytes
//*   1 Windows 1252 character = 1 byte
//*   We use to download with FTP:
//*   locsite en=mbcs mb=(IBM-1140,UTF-8) mbsendeol=crlf nordw
//*   (en: encoding; mbcs: multibyte character set
//*   mb: mbdataconn multi byte dataconn
//*   mbsendeol: multibyte send end of line
//*   crlf: carriage return, line feed, Windows end of line
//*   nordw: no (Record Descriptor Word 4 characters before data
//*             containing length))
//*   With Windows FTP client:
//*   quote site en=mbcs mb=(IBM-1140,UTF-8) mbsendeol=crlf nordw
//*      FTP constraint:
//*      Sending to UTF-8 or receiving from UTF-8
//*      is a multibyte transfer.
//*      A multibyte transfer cannot use a RECFM=FB z/OS file.
//*      Conversion to RECFM=VB may be done with
//*      DFSORT (FTOV) for a sequential file
//*      LMCOPY        for a library
//*
//*   Alternative using BPXBATCH or (BPXWUNIX in a rexx):
//*      cat "//'MVSsequential'" | iconv -f IBM-1140 -t UTF-8
//*      then pipe the result to ftp with transfer in binary
//*      If transfering to Windows, convert end of line LF
//*      to CRLF.
//*      GNU utilities (sed, awk, unix2dos)
//*      (using the correct SET PATH)
//*      ported on Windows from (for instance)
//*      https://gitforwindows.org/
//*      could be used in the periodically scheduled script
//*      processing transmitted files.
//*
//*   A transmitted .bat file should begin by:
//*   chcp 65001
//*   which is valid only for the length of the script.
//*   Remark: (Windows cp65001) = (z/OS IBM-1208) = UTF-8
//*__ End of remark regarding UTF-8 _____________________________
//*
//       EXPORT SYMLIST=*
//       SET WINIP='Windows_server_ip_address' please change
//       SET BATPATH='C:\development\cntl\bat'
//*      .bat files in this directory wait for execution
//       SET ZOSIP='zOS_ip_address'
//       SET RESULT=&SYSUID..RESULT
//       SET PASS='mypassword'
//*
//RESULT EXEC DELDEF,DSN=&RESULT
//*
//SUBBAT   EXEC PGM=FTP
//SYSPRINT DD  SYSOUT=*
//SYSIN    DD  *,SYMBOLS=EXECSYS
 &WINIP
 &SYSUID
 &PASS
 locsite sbd=(ibm-1140,ibm-1252)
 cd &BATPATH
 put dd:bat hello.bat
 qui
//BAT DD *,SYMBOLS=EXECSYS Content of hello.bat
rem C functions will use temporarily the Windows 1252 codepage
chcp 1252
echo Hello > result.txt
rem  Upload result.txt to z/OS.
call :ftp result.txt
del result.txt
rem  /b  Return to caller
exit /b
rem  _____________________________________________
:ftp
rem  Return %1 to z/OS
rem  %1 is first parameter such as result.txt
rem  EBCDIC codepage conversion only done by z/OS
rem  Build ftp command file.
rem  Inner parentheses to remove extra blanks at right
rem  ^ is Windows escape character
> ftp.txt (
 echo &SYSUID
 (echo &PASS)
 echo quote site sbd=^(ibm-1140,ibm-1252^)
 echo put %1 '&RESULT'
 echo qui
)
ftp -s:ftp.txt &ZOSIP
rem pause     for debugging
del ftp.txt
rem  _____________________________________________
//
________________________________________________________
             Scheduling Windows scripts
1) sch_intrdr.cmd is executed once.
     Every 10 minutes or at a specified periodicity,
     it makes the script intrdr.cmd to execute all
     the files with extension .bat present in a
     specified directory.
     The script intrdr.cmd changes, after executing
     the .bat file, its extension .bat into .done
2) sch_list_intrdr.cmd is executed to know the task status.
3) sch_del_intrdr.cmd is executed to delete the task.

After the sources of these three scheduling scripts,
is presented the script intrdr.cmd which is executed
every 10 minutes.
________________________________________________________
Content of script sch_intrdr.cmd executed once
________________________________________________________
@echo off
setlocal

echo  _______________________________________________________________
echo ! Define task to execute                                        !
echo !_______________________________________________________________!


rem Use full name for command

rem Test set "pgm=C:\Windows\System32\notepad.exe"
rem Test set "file=C:\development\cntl\test.txt"
rem Test set "this_cmd='%pgm%' '%file%'"

set "this_cmd='C:\development\cntl\intrdr.cmd'"
echo this_cmd=%this_cmd%

set "task_name=intrdr"
echo task_name=%task_name%

echo  _______________________________________________________________
echo ! Execute task every ten minutes                                !
echo !_______________________________________________________________!

rem https://docs.microsoft.com/en-us/windows-server/administration
rem         /windows-commands/schtasks

rem Execute task every ten minutes, every day until deleted
rem /sc schedule type
rem /mo modification
schtasks /create /tn "%task_name%" /tr "%this_cmd%" /sc minute /mo 10

exit
___________________________________________________________
Content of script sch_list_intrdr.cmd lists the task status
___________________________________________________________
@echo off
setlocal

echo  _______________________________________________________________
echo ! Define task to list                                           !
echo !_______________________________________________________________!

set "task_name=intrdr"
echo %task_name%

echo  _______________________________________________________________
echo ! List task                                                     !
echo !_______________________________________________________________!

rem https://docs.microsoft.com/en-us/windows-server/administration
rem         /windows-commands/schtasks


rem  fo format
rem  v  verbose

echo schtasks /query /tn "%task_name%" /fo list /v
schtasks /query /tn "%task_name%" /fo list /v

rem pause to see result
rem Check if scheduling off battery.
pause
endlocal

exit

________________________________________________________
Content of script sch_del_intrdr.cmd delete the task
________________________________________________________
@echo off
setlocal

echo  _______________________________________________________________
echo ! Define task to delete                                         !
echo !_______________________________________________________________!

set "task_name=intrdr"
echo task_name=%task_name%

echo  _______________________________________________________________
echo ! Delete task                                                   !
echo !_______________________________________________________________!

rem https://docs.microsoft.com/en-us/windows-server/administration/
rem         /windows-commands/schtasks

rem      /f does not confirm
schtasks /delete /tn "%task_name%" /f

pause
endlocal
exit
________________________________________________________
Content of script intrdr.cmd executed every 10 minutes.
       Executes every .bat file in specified directory.
       Renames the .bat files into .done after execution.
________________________________________________________
rem @echo off
setlocal enabledelayedexpansion

rem %~dp0 history.txt in same directory as intrdr.cmd
echo  =================================== >> %~dp0history.txt

rem   _______________________________________________________________
rem  ! Execute each file with extension .bat in directory BAT        !
rem  !_______________________________________________________________!

set "BATPATH=C:\development\cntl\BAT\"
set "all=%BATPATH%*.bat"

for %%f in (%all%) do (
  echo  ___________________________________
  echo "Processing: %%f"
  rem Individual .bat files must end by exit /b to return
  rem Execute .bat file
  call %%f
  rem Rename extension bat into done
  ren "%%~f" *.done
) >> %~dp0history.txt

rem pause
endlocal
exit /b
________________________________________________________
