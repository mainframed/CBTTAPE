/* rexx */

arg dsn maxgens .
trace o

/* Individual convert a pds or a pdse to                    */
/*  a pdse version 2 with the implied or specified          */
/*  maxgens parameter.                                      */

say "                           "
say "Example of use in ISPF 3.4:                            "
say "//L2     USER01.LOAD1      <-- Group convert           "
say "         USER01.LOAD2          to DSNTYPE(LIBRARY,2)   "
say "//       USER01.LOAD3          with default MAXGENS    "
say "         USER01.LOAD4                                  "
say "L2       USER01.LOAD5      <-- Default convert         "
say "         USER01.LOAD6                                  "
say "L2 / 0   USER01.LOAD7      <-- Convert with MAXGENS=0  "
say "L2 / 2   USER01.LOAD8      <-- Convert with MAXGENS=2  "
say "                           "

/* dsn of dataset to become library of type 2               */
/* maxgens is the number of member generations              */
/*         with the oldest deleted.                         */
/*         If not specified, then system default            */
/*         (parameter MAXGENS_LIMIT( ) in MACLIB(IGDSMSXX)).*/
/* maxgens of 1 means member plus one generation            */

dsn = strip(dsn,,"'")    /* remove surrounding quotes */

time_stamp = t!!time('s')
dsnprint = userid()."IEBCOPY."time_stamp   /* SYSPRINT file */

"allocate fi(SYSPRINT) da('"dsnprint"')           reuse",
"new catalog",
"lrecl(125) recfm(v b a) blksize(27875)",
"space(1 1) tracks"

x = msg("off")      /* quiet free           */
"free     fi(SYSUT1)   da('"dsn"')"
y = msg(x)          /* reset msg status     */
"allocate fi(SYSUT1)   da('"dsn"')    shr         reuse"

x = msg("off")      /* quiet free and delete attempt */
"free     fi(SYSUT2)"
"delete "dsn".L2"   /* avoid duplicate      */
y = msg(x)          /* reset msg status     */

if maxgens = "" then do           /* max number of generation */
"allocate fi(SYSUT2)   da('"dsn".L2') new catalog reuse",
                    "like('"dsn"')",
                    "dsntype(library,2)"
                end /* maxgens not precised => system default */
                else do       /* maxgens precised: 0, 1, 2 etc... */
"allocate fi(SYSUT2)   da('"dsn".L2') new catalog reuse",
                    "like('"dsn"')",
                    "dsntype(library,2) maxgens("maxgens")"
                end /* maxgens precised: 0, 1, 2 etc... */
arc = rc
if arc > 0 then do
   say "Allocate of new file gave code "arc
   say dsn" unchanged"
   exit(12)
end /* if arc > 0 */

lib = "sys1.linklib"
"call '"lib"(iebcopy)' 'COPY' "
xrc = rc                /* save iebcopy return code */

"ispexec lminit dataid(dd1) dataset('"dsnprint"') enq(shr)"
"ispexec view   dataid("dd1")"

end:
"ispexec lmfree dataid("dd1")"

/* if iebcopy ok delete old dsn                                      */
/*               rename new dsn into old dsn                         */
/*               delete dsnprint                                     */
if xrc = 0 then do /* iebcopy ok */
           x = msg("off")      /* quiet alter               */
           "alter  '"dsn"' newname('"dsn".ANC')"
                 brc = rc
           y = msg(x)          /* reset msg status          */
                 if brc > 0 then do
                    say "IEBCOPY ok"
                    say "Copy "dsn".L2 created"
                    say "but "dsn" could not be renamed into "dsn".ANC"
                    say "Code "brc" at rename time"
                    say dsn" unchanged"
                    exit(10)
                 end /* if brc > 0 */
           x = msg("off")      /* quiet alter               */
           "alter  '"dsn".L2' newname('"dsn"')"
                 crc = rc
           y = msg(x)          /* reset msg status          */
                 if crc > 0 then do
                    say "IEBCOPY ok"
                    say dsn" was renamed into "dsn".ANC"
                    say "but "dsn".L2 could not renamed into "dsn
                    say "and gave code "crc
                    exit(10)
                 end /* if crc > 0 */
           say "IEBCOPY return code = "xrc
           x = msg("off")      /* quiet delete attempt      */
           "delete '"dsn".ANC' scratch purge"
                 drc = rc
           y = msg(x)          /* reset msg status          */
                 if drc > 0 then do
                    say "IEBCOPY ok but delete of ",
                        "old "dsn".ANC not done"
                    say "code "drc" at delete time."
                    say dsn" changed and correct"
                    exit(4)
                 end /* if drc > 0 */
           say "_______________________________________________"
           say "                                               "
           say dsn" changed to DSNTYPE=(LIBRARY,2)"
           if maxgens = "" then
           say " and default MAXGENS              "
                           else
           say " and MAXGENS="maxgens
           say "_______________________________________________"

           x = msg("off")      /* quiet free and delete attempt      */
           "free fi(SYSUT1,SYSUT2)"
           /*  Attempt to delete IEBCOPY SYSPRINT:                   */
           "delete '"dsnprint"' scratch purge"
           y = msg(x)          /* reset msg status                   */
           exit(0)
end /* if xrc = 0  iebcopy ok */

else do          /* xrc <> 0 iebcopy ko */
/* if iebcopy ko keep old dsn not modified                           */
/*               keep new L2 file                                    */
/*               keep dsnprint                                       */
           say "IEBCOPY return code = "xrc
           say dsn" not modified"
           say "New file "dsn".L2 kept"
           say "IEBCOPY SYSPRINT kept"
           exit(12)         /* xrc <> 0 iebcopy ko */
end /* if xrc <> 0  iebcopy ko */
