 /* rexx */
 trace o

 /* Change a library member ID.                                       */


 /* Before:                                                           */
 /*         VIEW      BOZO.CNTL                                       */
 /* Command ===>                                                      */
 /*            Name     Prompt       Size   Created    ...  ID        */
 /* id_______ J        <°(((><          6  2018/03/15  ...  BOZO      */
 /*                                                                   */
 /* After:                                                            */
 /*         VIEW      BOZO.CNTL                                       */
 /* Command ===>                                                      */
 /*            Name     Prompt       Size   Created    ...  ID        */
 /* _________ J                         6  2018/03/15  ...  <°(((><   */

 parse arg dsn rest
 dsn0 = strip(dsn,,"'")               /* remove surrounding quotes    */
 parse var dsn0 dsn '(' member ')' .  /* extract member if present    */

 call update
 exit
 /*___________________________________________________________________*/

 update:

 /*  Update member statistics                                         */
 /*
 >>-ISPEXEC--LMMSTATS--DATAID(data-id)--MEMBER(member-name)------>
    '-VERSION(version-number)-'      '-MODLEVEL(mod-level)-'
    '-CREATED(create-date)-'         '-MODDATE(last-modified-date)-'
    '-MODTIME(last-modified-time)-'  '-CURSIZE(current-size)-'
    '-INITSIZE(initial-size)-'       '-MODRECS(records-modified)-'
    '-USER(user-id)-'                '-DELETE-'
    '-CREATED4(4-char-year-create-date)-'
    '-MODDATE4(4-char-year-last-modified-date)-'
    |       .-Off--.   |  '-NOLLA-'  |      .-NO--.   |
    '-SCLM(-+-On---+-)-'             '-EXT(-+-YES-+-)-'
               '-Asis-'
    '-USER8(user-id)-'
 */

 address ispexec
 'control errors return'
 "lminit dataid(dd1) dataset('"dsn"') enq(shr)"
        lminit_rc = rc
        if lminit_rc > 0 then do
           say 'lminit_rc = 'lminit_rc
           address
           return
        end /* if lminit_rc > 0                                       */

 /* Use isplink instead of ispexec and                                */
 /* hexa userid representation if problem                             */
 /* as space or right parent in userid.                               */
 /* ><)))°>    '6E4C5D5D5D4A6E'x                                      */
 /* userid   = '6E4C5D5D5D4A6E'x                                      */
 userid = left(rest,7)

 function = 'LMMSTATS'
 dataid   = dd1
 member   = left(member,8)
 b        = ' '

 /* "lmmstats dataid("dd1") member("member") user8("rest")" */  /* ok */
 address linkpgm 'isplink function dataid member b b b b b b b b userid'

        lmmstats_rc = rc
        if lmmstats_rc > 0 then do
           say 'lmmstats_rc = 'lmmstats_rc
           say 'zerrlm      = 'zerrlm
        end /* if lmmstats_rc > 0                                     */

 "lmfree dataid("dd1")"
        lmfree_rc = rc
        if lmfree_rc > 0 then
           say 'lmfree_rc = 'lmfree_rc
 address

 return

 /*___________________________________________________________________*/
