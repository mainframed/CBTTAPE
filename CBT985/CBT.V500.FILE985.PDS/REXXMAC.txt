/* rexx */
trace('o')

/* Example:                                                */
/* An edit macro in rexx's clothing.                       */

/* Purpose:                                                */
/*         VIEW      BOZO.PDS                              */
/* Command ===> s *2* rexx01                               */
/*            Name     Prompt                              */
/* _________ MBR1                                          */
/* _________ MBR2                                          */
/* _________ MBR3                                          */
/* will execute rexx rexx01 on all members with name       */
/* containing 2.                                           */
/* If instead of a rexx, I would like an edit macro        */
/* to process all members with name containing 2,          */
/* I should make the edit macro to look like a rexx.       */
/* This is the case with rexxmac.                          */
/* Command ===> s *2* rexxmac                              */

/* Other example:                                          */
/* Command ===> cmde                                       */
/* Enter TSO commands below:                               */
/* ===> rexxmac 'bozo.pds(mbr2)' aA bB                     */

parse arg dsn parm
dsn  = strip(dsn,,"'") /* remove surrounding quotes if any */

call rexx_to_edit_macro

/* I am an edit macro from thereon until exit.             */
address isredit

  say "parm   = "parm
  '(mbr) = member '
  say "member = "mbr
  "c 'MBR1' 'MBR2' all"
  'save'
  'end'
address /* resume environment prior to address isredit     */

exit /* leave edit macro and return to rexx                */

/* _______________________________________________________ */

rexx_to_edit_macro: procedure expose dsn parm

/* edit macro will return.                                 */
/* rexx will exit.                                         */

address isredit
'macro (parm)'
isr_rc = rc

if isr_rc <> 0 then do
   /* Actually rc = 20                                     */
   /* I am a rexx                                          */
   address ispexec; 'control errors return'
     parse source . . rexxname .
     "edit dataset('"dsn"') macro("rexxname") parm(parm)"
  /*  edit if changes have to be made.                     */
  /* "view dataset('"dsn"') macro("rexxname") parm(parm)"  */
   address
   exit /* final rexx exit                                 */
end         /* if isr_rc <> 0       rexx                   */
else return /*    isr_rc =  0       edit macro             */
/* _______________________________________________________ */
