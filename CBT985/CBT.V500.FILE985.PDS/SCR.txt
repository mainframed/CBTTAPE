/* rexx */
/*_________________________________________________________*/
/*    scr:                                                 */
/*    With no parameter: refresh panel cache               */
/*    With one parameter: refresh panel cache              */
/*                      and set permanent screen name.     */
/*    May be used as a rexx or edit macro.                 */
/*    Examples:                                            */
/*                                                         */
/*    Command ===> tso scr                                 */
/*    or scr before panel name                             */
/*    (after modifying a panel)                            */
/*    Flushes the panel cache.                             */
/*                                                         */
/*    Command ===> tso scr screen1                         */
/*    Flushes the panel cache and                          */
/*    set permanently present screen name to screen1       */
/*                                                         */
/*    In Edit or View mode:                                */
/*    Command ===> scr                                     */
/*    Command ===> scr screen1                             */
/*_________________________________________________________*/
/*                                                         */
/* Obtain parameter if any (edit macro or rexx).           */
/* If no parameter, scrname = ''                           */
address isredit /* Am I an edit macro?                     */
"macro (scrname)"
isr_rc = rc
if isr_rc <> 0 then parse arg scrname . /* rexx            */
address
/*_________________________________________________________*/
/*                                                         */
address ispexec
"control errors return"
  call refresh_panel_cache
  if scrname <> '' then do
     call check_and_try_to_correct_screen_name
     call set_screename_permanent
  end /* if scrname <> ''                                  */
address
exit
/*_________________________________________________________*/
/*                                                         */
refresh_panel_cache: procedure
/* Refresh panel cache method from                         */
/* http://ibmmainframes.com/about59391.html                */
/* Author: Stefan  Germany                                 */
/* Thank you Stefan.                                       */
  do i = 1 to 12
     /* non display, simulate END key                      */
     "control nondispl end"
     /* isr00301 isr00302 ... isr00312 reflist panels      */
     "display panel(isr003"right(i,2,'0')")"
  end /* do i = 1 to 12                                    */
return
/*_________________________________________________________*/
/*                                                         */
set_screename_permanent: procedure expose scrname
  stack = 'scrname 'scrname' perm'
  /* stack ="command1;command2"                            */
  inocuous_panel = ispblank
  /* command(   ) implies:                                 */
  /* panel processed in CONTROL NONDISPL ENTER mode        */
  /* non display, simulate ENTER key                       */
  "display panel("inocuous_panel") command(stack)"
return
/*_________________________________________________________*/
/*                                                         */
check_and_try_to_correct_screen_name:,
  procedure expose scrname
/* scrname is an input output parameter                    */
/* scrname 2 to 8 characters that conform to member naming */
/* rules , except NEXT, PREV, LIST, ON and OFF             */

scrname = translate(scrname) /* set in uppercase           */

if length(scrname) > 8 then do /* too long                 */
  say 'scrname 'scrname' should have at most 8 characters.'
  scrname = right(scrname,8) /* retain the 8 rightmost characters */
end /* when length(scrname) > 8                            */

if length(scrname) < 2 then do /* too short                */
  say 'scrname 'scrname' should have at least 2 characters.'
  scrname = scrname!!scrname
end /* when length(scrname) < 2                            */

/* All characters alphanumeric or national                 */
do i = 1 to length(scrname)
   char = substr(scrname,i,1) /* ith character             */
   if datatype(char,alphanumeric) then iterate
   if pos(char,'à£$') then iterate /* national char Ok     */
    scrname = overlay('X',scrname,i)  /* overlay ith char  */
end /* do i = 1 to length(scrname)                         */

/* First character not numeric                             */
char = left(scrname,1)
if datatype(char,number) then
    scrname = overlay('X',scrname,1)  /* overlay 1st char  */

/* scrname must not be NEXT, PREV, LIST, ON, OFF.          */
if wordpos(scrname,'NEXT PREV LIST ON OFF') > 0 then
  scrname = scrname!!'X'
return
/* End of check_and_try_to_correct_screen_name ___________ */
