 /* rexx */
 trace o

 /* Command ===> cmde                                                 */
 /* ===> viewit lmmstats 'XX.YYYY.BOZO.CNTL(abcde)' created(66/12/01) */
 /* ===> viewit lmmstats 'XX.YYYY.BOZO.CNTL(abcde)' user(<BOZO>)      */

 parse arg dsn rest
 dsn0 = strip(dsn,,"'")               /* remove surrounding quotes    */
 parse var dsn0 dsn '(' member ')' .  /* extract member if present    */

 call update
 exit
 /*___________________________________________________________________*/

 update:

 /*  Update member statistics                                         */
 /*
 >>-ISPEXEC--LMMSTATS--DATAID(data-id)--MEMBER(member-name)------>
    '-VERSION(version-number)-'      '-MODLEVEL(mod-level)-'
    '-CREATED(create-date)-'         '-MODDATE(last-modified-date)-'
    '-MODTIME(last-modified-time)-'  '-CURSIZE(current-size)-'
    '-INITSIZE(initial-size)-'       '-MODRECS(records-modified)-'
    '-USER(user-id)-'                '-DELETE-'
    '-CREATED4(4-char-year-create-date)-'
    '-MODDATE4(4-char-year-last-modified-date)-'
    |       .-Off--.   |  '-NOLLA-'  |      .-NO--.   |
    '-SCLM(-+-On---+-)-'             '-EXT(-+-YES-+-)-'
               '-Asis-'
    USER8(user-id)
 */

 address ispexec
 'control errors return'
 "lminit dataid(dd1) dataset('"dsn"') enq(shr)"
        lminit_rc = rc
        if lminit_rc > 0 then do
           say 'lminit_rc = 'lminit_rc
           address
           return
        end /* if lminit_rc > 0                                       */

 "lmmstats dataid("dd1") member("member") "rest
        lmmstats_rc = rc
        if lmmstats_rc > 0 then do
           say 'lmmstats_rc = 'lmmstats_rc
           say 'zerrlm      = 'zerrlm
        end /* if lmmstats_rc > 0                                     */

 "lmfree dataid("dd1")"
        lmfree_rc = rc
        if lmfree_rc > 0 then
           say 'lmfree_rc = 'lmfree_rc
 address

 return

 /*___________________________________________________________________*/
