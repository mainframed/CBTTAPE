/* rexx */

lc_all = "En_US.IBM-1140"
/* lc_all = "Fr_FR.IBM-1147" */
dsnprint_prefix = "XX.YYYY" /* please change dataset high level qualifiers */
/* Execute strings, fold then grep Unix commands.           */
/* Display printable strings folded to 72 columns.          */

/* Syntax:                                                  */
/* Command ===> tso grepit 'regex' dsname                   */
/* Command ===> tso grepit 'regex' 'dsname'                 */
/* Command ===> tso grepit 'regex' dsname(member)           */
/* Command ===> tso grepit 'regex' 'dsname(member)'         */
/* Command ===> tso cmde                                    */
/*              grepit 'regex' dsname                       */
/* The regular expression regex is in local codepage        */
/* (or is |) and must be between '                          */
/* The dsname may or may not be surrounded by '             */

/* Example:                                                 */

/* Before:                                                  */
/* In ISPF 3.4:                                             */
/*           Data Sets Matching HLQ1.SRCHIMS                */
/*  Command ===>                                            */
/*           HLQ1.SRCHIMS.CNTL                              */
/*           HLQ1.SRCHIMS.XXX0999.DU210618                  */

/* Use:                                                     */
/*  Command ===>                                            */
/*           HLQ1.SRCHIMS.CNTL                              */
/* GREPIT 'Contrat:|Client :|Capital' /                     */
/* The / indicates the dsname on the line.                  */

/* Result:                                                  */
/*            XX.YYYY.USERID.GREPIT.T47584                  */
/* Command ===>                                             */
/* ****** ********************************* Top of Data *****/
/* 000001 é0â5Contrat: 11111111111 *ABC*   Solde D : 2570,12*/
/* 000002 é0â5Client : 22222222222         LD liste: 0,00   */
/* 000003 é0â5UNTEL  FIRSTNAME             Capital : 3000,00*/
/* 000004 é0â5Contrat: 33333333333 *ABC*   Solde D : 1799,95*/
/* 000005 é0â5Client : 44444444444         LD liste: 0,00   */

/* Grepit could be used with a load library member.         */

trace o

/*__________________________________________________________*/
/* Get parameters                                           */
/* Uses regex in-between '                                  */
parm = arg(1)
parse var parm "'" regex "'" dsn .

/*__________________________________________________________*/
/* Allocate STDIN.                                          */
dsn = strip(dsn,,"'") /* remove quotes if any               */
"alloc fi(stdin) da('"dsn"') shr reu"
src = rc
if src > 0 then do
   say "STDIN allocate of "dsn" gave code "src
   exit(12)
end /* if src > 0 */

/*__________________________________________________________*/
/* Allocate STDOUT dsnprint dataset.                        */
time_stamp = t!!time('s')
parse source . . rexxname .
dsnprint = dsnprint_prefix"."userid()"."rexxname"."time_stamp
"allocate fi(stdout) da('"dsnprint"') reuse",
"new catalog",
"lrecl(80) recfm(f b) blksize(0)",
"space(1 15) tracks"
arc = rc
if arc > 0 then do
   say "STDOUT DSNPRINT allocate gave code "arc
   exit(12)
end /* if arc > 0 */

/*__________________________________________________________*/
/* Initialize stem stderr.                                  */
drop stderr.
stderr.0 = 0

/*__________________________________________________________*/
/* Set up env. environment variables stem.                  */
drop env.
env.1 = "LANG="lc_all
/* z/OS Unix commands in IBM-1147 codepage                  */
/* env.1 = "LANG=Fr_FR.IBM-1147" */ /* locale gives LANG=C  */
env.0 = 1

/*__________________________________________________________*/
/* Build z/OS Unix command.                                 */

cmd = "  strings -z -n 8 | fold -w 72 | grep -Ei '"regex"'"
/* strings Only printable fields                            */
/*  -z    Ignores the POSIX definition of a string and      */
/*        searches for any group of printable characters    */
/*        greater than four in length.                      */
/*  -n 8  Displays strings of printable characters          */
/*        that are at least 8 characters in length.         */
/* fold  Visualize on w columns                             */
/*  -w    maximum line length of width characters           */
/* grep  Search for regular expressions                     */
/*  -E    matches using extended regular expressions.       */
/*  -i    case insensitive.                                 */

call bpxwunix cmd,'DD:STDIN','DD:STDOUT',stderr.,env.

/* say 'stderr.0 = 'stderr.0                                */

if stderr.0 <> 0 then /* z/OS Unix command failed           */
   do i = 1 to stderr.0
      say 'stderr.'i' ='stderr.i
   end                /* z/OS Unix command failed           */
else do               /* z/OS Unix command succeeded        */

/*__________________________________________________________*/
/* Display DSNPRINT                                         */
  "ispexec lminit dataid(dd1) dataset('"dsnprint"') enq(shr)"
  "ispexec view   dataid("dd1")"
  "ispexec lmfree dataid("dd1")"

end                   /* z/OS Unix command succeeded        */

/*__________________________________________________________*/
/* Clean up                                                 */
msgs = msg("off")
address tso
"free fi(stdin stdout)"
"delete '"dsnprint"'"
msgs = msg(msgs)
exit
