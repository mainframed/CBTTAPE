/* BUILDING DEPOSIT, INC. SMS ACS DATACLASS ROUTINE                 */
PROC &DATACLAS
/* LIST OF ACCEPTED DSNAME HIGH LEVEL QUALIFIERS                    */
FILTLIST &VALID_DSNHLQ_VALUES INCLUDE(
/* PRODUCTION SYSTEM HLQ SET                                        */
     'DB2A',
     'DB2B',
     'DB2X',
     'DB2Y',
     PACCT%,
     PAPPL%,
     PAUDT%,
     PCORP%,
     PCRDT%,
     PDATA%,
     PDIST%,
     PEXEC%,
     PFINL%,
     PHKNG%,
     PINFO%,
     PMANI%,
    'PMERC7',
     PREAL%,
     PSTOP%,
     PSTOR%,
     PTECH%,
     'QMFA',
     'QMFB',
     'VAPSCD00',
     'VUDS',
/* QA SYSTEM HLQ SET                                                */
     'DB2Q',
     QACCT%,
     QAPPL%,
     QAUDT%,
     QCORP%,
     QCRDT%,
     QDATA%,
     QDIST%,
     QEXEC%,
     QFINL%,
     QHKNG%,
     QINFO%,
     QMANI%,
     QMERC%,
     QREAL%,
     QSTOP%,
     QSTOR%,
     QTECH%,
/* TEST SYSTEM HLQ SET                                              */
     'DB2M',
     TACCT%,
     TAPPL%,
     TAUDT%,
     TCORP%,
     TCRDT%,
     TDATA%,
     TDIST%,
     TEXEC%,
     TFINL%,
     THKNG%,
     TINFO%,
     TMANI%,
    'TMERC7',
     TREAL%,
     TSTOP%,
     TSTOR%,
     TTECH%,
/* TSO USER HLQ SET - ALL DSN ARE USERID.*                          */
     'ACCT0',
     'APPL0',
     'APPL1',
     'APPL100',
     'APPL2',
     'APPL200',
     'APPL3',
     'CORP1',
     'CORP2',
     'CORP4',
     'CRDT1',
     DATA*,
     'DIST0',
     'DIST1',
     'DIST2',
     'DIST3',
     'EXEC0',
     'FINL6',
     'FINL7',
     HKNG*,
     'INFOV01',
     'INFO0',
     'INFO002',
     'INFO005',
     'INFO1',
     'JCITRAN',
     'MANI1',
     'MERCA',
     'MERC5',
     'MERC8',
     'MERC9',
     'REAL0',
     'REAL1',
     'RECEIV',
     'STOP1',
     'STOR0',
     'STOR001',
     SYSADM*,
     TECH0*,
     TECH1*,
     TECHT*,
     TECH2*,
     TECH3*,
     TECH4*,
     TECH5*,
     TECH6*,
     'WHSE001',
/* SYSTEM HLQ SET - SYS% INCLUDES SYS1., SYSA.,...SYSM.             */
     'MODEL',FDR*,SYS%,'IBMUSER','DUMY')
/* LIST OF ACCEPTED JCL DATACLAS= VALUES                            */
FILTLIST &VALID_DATACLAS_VALUES INCLUDE(
     'ASSEMBLE',  /* ASSEMBLER LANGUAGE SOURCE                      */
     'BACKUP',    /* FILE BACKUP                                    */
     'BRODCAST',  /* SYSTEM BRODCAST                                */
     'CATALOG',   /* SYSTEM ICF CATALOG                             */
     'CKPT',      /* JES2 CHECKPOINT DATASET                        */
     'CLIST',     /* TSO CLIST SOURCE                               */
     'CLISTSEQ',  /* TSO CLIST SOURCE                               */
     'CNTL',      /* GENERAL JCL/SOURCE/PARAMETERS                  */
     'COBOL',     /* COBOL LANGUAGE SOURCE                          */
     'COPY',      /* LANGUAGE COPY BOOK SOURCE                      */
     'CSI',       /* SMP CSI DATASET                                */
     'DATADA',    /* APPLICATION DATA                               */
     'DATAES',    /* APPLICATION DATA                               */
     'DATAIN',    /* APPLICATION DATA                               */
     'DATAKS',    /* APPLICATION DATA                               */
     'DATALS',    /* APPLICATION DATA                               */
     'DATAPO',    /* APPLICATION DATA                               */
     'DATAPS',    /* APPLICATION DATA                               */
     'DATARR',    /* APPLICATION DATA                               */
     'DB2',       /* DB2                                            */
     'DMS',       /* DEVELOPMENT MANAGEMENT SYSTEM                  */
     'DOC',       /* DOCUMENTS AND MEMOS                            */
     'DTL',       /* DIALOG TAG LANGUAGE                            */
     'DYLAKOR',   /* DYLAKOR LANGUAGE SOURCE                        */
     'EXEC',      /* TSO REXX LANGUAGE SOURCE                       */
     'FDRABR',    /* FDRABR MANAGED DATASET OR MODEL                */
     'FORTRAN',   /* FORTRAN LANGUAGE SOURCE                        */
     'HELP',      /* TSO HELP SOURCE                                */
     'IMAGE',     /* SYSTEM IMAGELIB                                */
     'IODF',      /* SYSTEM IODF FILE                               */
     'JCL',       /* JCL SOURCE                                     */
     'LIBRARY',   /* LIBRARIAN FILE                                 */
     'LISTING',   /* OUTPUT LISTING                                 */
     'LOGREC',    /* SYSTEM LOGREC                                  */
     'MACRO',     /* LANGUAGE MACRO SOURCE                          */
     'MESSAGE',   /* PDF MESSAGE LIBRARY                            */
     'MODULE',    /* LOAD MODULE LIBRARY                            */
     'NONSMS',    /* FORCE NON-SMS DATASET                          */
     'OBJ',       /* OBJECT CODE                                    */
     'PAGE',      /* SYSTEM PAGE DATASET                            */
     'PANEL',     /* PDF PANEL LIBRARY                              */
     'PARM',      /* PARAMETER LIBRARY                              */
     'PLI',       /* PLI LANGUAGE SOURCE                            */
     'PROC',      /* PROCLIB                                        */
     'RACF',      /* SYSTEM RACF                                    */
     'RAMIS',     /* RAMIS LANGUAGE SOURCE                          */
     'RPG',       /* RPG LANGUAGE SOURCE                            */
     'SDUMP',     /* SYSTEM DUMP DATASETS                           */
     'SKELETON',  /* PDF SKELETON LIBRARY                           */
     'SMF',       /* SYSTEM SMF CLUSTER                             */
     'SMPTLIB',   /* SMP/E TLIB                                     */
     'SMSUSER',   /* TEMP                                           */
     'SMS',       /* SYSTEM SMS CONTROL CLUSTER                     */
     'SOURCE',    /* GENERAL LANGUAGE SOURCE                        */
     'SPOOL',     /* SYSTEM SPOOL FILE                              */
     'STGINDEX',  /* SYSTEM STGINDEX DATASET                        */
     'SYSWDBA',   /* SYSTEM W DATABASE                              */
     'SYSWMODL',  /* SYSTEM W MODEL                                 */
     'TABLE',     /* PDF TABLE LIBRARY                              */
     'UADS',      /* SYSTEM UADS                                    */
     'XCF',       /* SYSTEM XCF DATASET                             */
/* NULL IS ALSO ACCEPTED AS A VALID DATACLAS VALUE AT THIS TIME     */
     '')
/* LIST OF ACCEPTED DSN LLQ VALUES USED AS DEFAULT DATACLAS VALUES  */
FILTLIST &LLQ_ASSEMBLE  INCLUDE('ASMLIB')
FILTLIST &LLQ_BACKUP    INCLUDE('BACKUP')
FILTLIST &LLQ_CKPT      INCLUDE('HASPCKPT')
FILTLIST &LLQ_CLIST     INCLUDE('CLISTLIB')
FILTLIST &LLQ_CLISTSEQ  INCLUDE('CLIST')
FILTLIST &LLQ_CNTL      INCLUDE('CNTLLIB')
FILTLIST &LLQ_COBOL     INCLUDE('COBLIB')
FILTLIST &LLQ_COPY      INCLUDE('COPYLIB')
FILTLIST &LLQ_CSI       INCLUDE('CSI')
FILTLIST &LLQ_DMS       INCLUDE('DMSLIB')
FILTLIST &LLQ_DOC       INCLUDE('DOCLIB','MEMOLIB')
FILTLIST &LLQ_DTL       INCLUDE('ISPGLIB')
FILTLIST &LLQ_DYLAKOR   INCLUDE('DYLLIB')
FILTLIST &LLQ_EXEC      INCLUDE('EXECLIB')
FILTLIST &LLQ_FORTRAN   INCLUDE('FORLIB')
FILTLIST &LLQ_HELP      INCLUDE('HELP','HELPLIB')
FILTLIST &LLQ_IMAGE     INCLUDE('IMAGELIB')
FILTLIST &QUAL_IODF     INCLUDE(IODF%%)
FILTLIST &LLQ_JCL       INCLUDE('JCLLIB')
FILTLIST &LLQ_LIBRARY   INCLUDE('MASTER')
FILTLIST &LLQ_LISTING   INCLUDE('LISTING','SYSOUT')
FILTLIST &LLQ_MACRO     INCLUDE('MACLIB')
FILTLIST &LLQ_MESSAGE   INCLUDE('ISPMLIB')
FILTLIST &LLQ_MODULE    INCLUDE('ISPLLIB','LINKLIB','LOADLIB','MODLIB')
FILTLIST &LLQ_OBJ       INCLUDE('OBJLIB','TXTLIB')
FILTLIST &LLQ_PANEL     INCLUDE('ISPPLIB','ISPPPLIB')
FILTLIST &LLQ_PARM      INCLUDE('PARMLIB')
FILTLIST &LLQ_PLI       INCLUDE('PLILIB')
FILTLIST &LLQ_PROC      INCLUDE('PROCLIB')
FILTLIST &LLQ_RACF      INCLUDE('RACF')
FILTLIST &LLQ_RAMIS     INCLUDE('RAMLIB')
FILTLIST &LLQ_RPG       INCLUDE('RPGLIB')
FILTLIST &LLQ_SKELETON  INCLUDE('ISPSLIB')
FILTLIST &LLQ_SMF       INCLUDE(MAN%)
FILTLIST &LLQ_SOURCE    INCLUDE('SRCELIB')
FILTLIST &LLQ_SPOOL     INCLUDE('HASPACE')
FILTLIST &LLQ_STGINDEX  INCLUDE('STGINDEX')
FILTLIST &LLQ_SYSWDBA   INCLUDE('DBA')
FILTLIST &LLQ_SYSWMODL  INCLUDE('MODEL')
FILTLIST &LLQ_TABLE     INCLUDE('ISPTLIB')
FILTLIST &LLQ_UADS      INCLUDE('UADS')
FILTLIST &QUAL_VVDS     INCLUDE('VVDS')
FILTLIST &LLQ_XCF       INCLUDE(XCF%)
/* DASD UNITS                                                       */
FILTLIST DASD_UNITS INCLUDE(
    '3380','3390','DISK','PAGEIO','SYSDA','SYSSQ','SYSTEST',
    'SMS','QA','PARTH',
    'VIO','SYSALLDA','APPLTA','SEQDA','SYSTA','USRDA','')
/* SMPE RELFILES                                                    */
FILTLIST &SYSHLQ   INCLUDE(SYS%)
FILTLIST &DB2HLQ   INCLUDE(DB2%)
FILTLIST &RELFILES INCLUDE(F%*)
FILTLIST &VALID_ARTHUR INCLUDE(PARTH.DATA%*.DBA)
/* IGNORE IF NON-DASD ALLOCATION                                    */
IF &UNIT ¬= &DASD_UNITS THEN DO
     SET &DATACLAS = 'NONSMS'
     EXIT CODE(0)
     END
/* IGNORE IF USER CODED DATACLAS=NONSMS                             */
IF &DATACLAS = 'NONSMS' THEN DO
     SET &DATACLAS = 'NONSMS'
     EXIT CODE(0)
     END
/* SYST TEMP FIX                                                    */
IF &HLQ = 'SYST' | &HLQ = 'RESTART' | &HLQ = 'SYSY' | &HLQ = 'SYSZ' | +
    &DSN(2) = 'SOURCING' | &DSN(2) = 'JARSCHRG' | +
    &DSN(2) = 'JOBACCT' THEN DO
     SET &DATACLAS = 'SMS'
     EXIT CODE(0)
     END
/* IGNORE IF VVDS                                                   */
IF &DSN(2) = &QUAL_VVDS THEN DO
     SET &DATACLAS = 'VVDS'
     WRITE '** STATUS  - DATACLAS VALUE (' &DATACLAS ') ASSIGNED **'
     EXIT CODE(0)
     END
/* LOOK FOR IODF DATASET                                            */
IF &DSN(2) = &QUAL_IODF THEN DO
     SET &DATACLAS = 'IODF'
     WRITE '** STATUS  - DATACLAS VALUE (' &DATACLAS ') ASSIGNED **'
     EXIT CODE(0)
     END
/* IGNORE IF TRUE TEMP DATASET                                      */
IF &DSTYPE = 'TEMP' THEN DO
     IF &DATACLAS ¬= '' THEN SET &DATACLAS = &DATACLAS
             ELSE DO
                   WRITE '** WARNING - DATACLAS VALUE DEFAULTED **'
                   SELECT (&DSORG)
                     WHEN ('PS') SET &DATACLAS = 'DATAPS'
                     WHEN ('PO') SET &DATACLAS = 'DATAPO'
                     WHEN ('DA') SET &DATACLAS = 'DATADA'
                     WHEN ('VS') DO
                            SELECT (&RECORG)
                               WHEN ('KS') SET &DATACLAS = 'DATAKS'
                               WHEN ('ES') SET &DATACLAS = 'DATAES'
                               WHEN ('RR') SET &DATACLAS = 'DATARR'
                               WHEN ('LS') SET &DATACLAS = 'DATALS'
                               OTHERWISE   SET &DATACLAS = 'DATAIN'
                            END
                          END
                     OTHERWISE   SET &DATACLAS = 'DATAIN'
                   END
              END
     WRITE '** STATUS  - DATACLAS VALUE (' &DATACLAS ') ASSIGNED **'
     EXIT CODE(0)
     END
/* IF FDRABR HLQ THEN SET DATACLAS = FDRABR                         */
IF &HLQ = 'FDRABR' | &HLQ = 'FDR' THEN DO
     SET &DATACLAS = 'SMS'
     WRITE '** STATUS  - DATACLAS VALUE (' &DATACLAS ') ASSIGNED **'
     EXIT CODE(0)
     END
/* PERFORM THE ACTUAL DSNAME HIGH LEVEL QUALIFIER VALIDATION        */
SELECT (&HLQ)
     WHEN (&VALID_DSNHLQ_VALUES) DO
/* CURRENTLY PERFORM NO ACTION WHEN VALID DSN HLQ SPECIFIED         */
          END
     OTHERWISE DO
/* FOR INVALID DSN HLQ IGNORE ANY DATACLAS SPECIFIED                */
             IF &DSN = &VALID_ARTHUR THEN DO
                  END
               ELSE DO
                  SET &DATACLAS = ''
                  END
/* IF INVALID DSNAME HLQ IS SPECIFIED WITH A PERM PDSE DATASET THEN */
/* FAIL THE ALLOCATION BECAUSE PDSE FILES MUST BE SMS DATASETS      */
             IF &DSNTYPE = 'LIBRARY' AND &DSTYPE = 'PERM' THEN DO
                   WRITE '** ERROR   - INVALID DSN HLQ VALUE ' +
                   'ASSIGNED WITH A PDSE DATASET **'
                   EXIT CODE(16)
                   END
          END
END
/* PERFORM THE ACTUAL DATACLAS VALUE VALIDATION                     */
SELECT (&DATACLAS)
     WHEN (&VALID_DATACLAS_VALUES) DO
/* WE HAVE A VALID DATACLAS OR NULL VALUE                           */
/* IF NULL DATACLAS AND OLD HLQ THEN SET CLASS TO NONSMS; ELSE      */
/*  IF NULL DATACLAS WITH AN SMS-DEFINED HLQ THEN SET DEFAULT       */
             IF &DATACLAS = '' THEN DO
                   SELECT (&HLQ)
                       WHEN(&VALID_DSNHLQ_VALUES) DO
/* NULL DATACLAS VALUE; SET VALUE TO 'NULLDFLT' FOR LATTER PROCESS  */
                       SET &DATACLAS = 'NULLDFLT'
                       END /* END OF VALID DSNHLQ IN NULL DATACLAS*/
                   OTHERWISE DO
                       SET &DATACLAS = 'NONSMS'
                       END /* NULL DATACLAS WITH INVALID HLQ */
                  END /* END OF SELECT HLQ GROUP */
                 END /* END OF NULL DATACLAS DO GROUP */
          END /* END OF MAJOR VALID_DATCLAS GROUP */
     OTHERWISE DO
/* WE HAVE AN INVALID DATACLAS VALUE CODED IN THE JCL               */
/* CAUSE ALLOCATION FAILURE WITH NON-ZERO EXIT CODE                 */
             WRITE '** ERROR   - INVALID DATACLAS VALUE (' &DATACLAS +
             ') ASSIGNED **'
             EXIT CODE(16)
          END
END
IF &DATACLAS = 'NULLDFLT' THEN DO
            SELECT (&LLQ)
               WHEN (&LLQ_ASSEMBLE) SET &DATACLAS = 'ASSEMBLE'
               WHEN (&LLQ_BACKUP)   SET &DATACLAS = 'BACKUP'
               WHEN (&LLQ_CKPT)     SET &DATACLAS = 'CKPT'
               WHEN (&LLQ_CLIST)    SET &DATACLAS = 'CLIST'
               WHEN (&LLQ_CLISTSEQ) SET &DATACLAS = 'CLISTSEQ'
               WHEN (&LLQ_CNTL)     SET &DATACLAS = 'CNTL'
               WHEN (&LLQ_COBOL)    SET &DATACLAS = 'COBOL'
               WHEN (&LLQ_COPY)     SET &DATACLAS = 'COPY'
               WHEN (&LLQ_CSI)      SET &DATACLAS = 'CSI'
               WHEN (&LLQ_DMS)      SET &DATACLAS = 'DMS'
               WHEN (&LLQ_DOC)      SET &DATACLAS = 'DOC'
               WHEN (&LLQ_DTL)      SET &DATACLAS = 'DTL'
               WHEN (&LLQ_DYLAKOR)  SET &DATACLAS = 'DYLAKOR'
               WHEN (&LLQ_EXEC)     SET &DATACLAS = 'EXEC'
               WHEN (&LLQ_FORTRAN)  SET &DATACLAS = 'FORTRAN'
               WHEN (&LLQ_HELP)     SET &DATACLAS = 'HELP'
               WHEN (&LLQ_IMAGE)    SET &DATACLAS = 'IMAGE'
               WHEN (&LLQ_JCL)      SET &DATACLAS = 'JCL'
               WHEN (&LLQ_LIBRARY)  SET &DATACLAS = 'LIBRARY'
               WHEN (&LLQ_LISTING)  SET &DATACLAS = 'LISTING'
               WHEN (&LLQ_MACRO)    SET &DATACLAS = 'MACRO'
               WHEN (&LLQ_MESSAGE)  SET &DATACLAS = 'MESSAGE'
               WHEN (&LLQ_MODULE)   SET &DATACLAS = 'MODULE'
               WHEN (&LLQ_OBJ)      SET &DATACLAS = 'OBJ'
               WHEN (&LLQ_PANEL)    SET &DATACLAS = 'PANEL'
               WHEN (&LLQ_PARM)     SET &DATACLAS = 'PARM'
               WHEN (&LLQ_PLI)      SET &DATACLAS = 'PLI'
               WHEN (&LLQ_PROC)     SET &DATACLAS = 'PROC'
               WHEN (&LLQ_RACF)     SET &DATACLAS = 'RACF'
               WHEN (&LLQ_RAMIS)    SET &DATACLAS = 'RAMIS'
               WHEN (&LLQ_RPG)      SET &DATACLAS = 'RPG'
               WHEN (&LLQ_SKELETON) SET &DATACLAS = 'SKELETON'
               WHEN (&LLQ_SMF)      SET &DATACLAS = 'SMF'
               WHEN (&LLQ_SOURCE)   SET &DATACLAS = 'SOURCE'
               WHEN (&LLQ_SPOOL)    SET &DATACLAS = 'SPOOL'
               WHEN (&LLQ_STGINDEX) SET &DATACLAS = 'STGINDEX'
               WHEN (&LLQ_SYSWDBA)  SET &DATACLAS = 'SYSWDBA'
               WHEN (&LLQ_SYSWMODL) SET &DATACLAS = 'SYSWMODL'
               WHEN (&LLQ_TABLE)    SET &DATACLAS = 'TABLE'
               WHEN (&LLQ_UADS)     SET &DATACLAS = 'UADS'
               WHEN (&LLQ_XCF)      SET &DATACLAS = 'XCF'
               OTHERWISE DO
                   WRITE '** WARNING - DATACLAS VALUE DEFAULTED **'
                   SELECT (&DSORG)
                     WHEN ('PS') SET &DATACLAS = 'DATAPS'
                     WHEN ('PO') SET &DATACLAS = 'DATAPO'
                     WHEN ('DA') SET &DATACLAS = 'DATADA'
                     WHEN ('VS') DO
                            SELECT (&RECORG)
                               WHEN ('KS') SET &DATACLAS = 'DATAKS'
                               WHEN ('ES') SET &DATACLAS = 'DATAES'
                               WHEN ('RR') SET &DATACLAS = 'DATARR'
                               WHEN ('LS') SET &DATACLAS = 'DATALS'
                               OTHERWISE   SET &DATACLAS = 'DATAIN'
                            END
                          END
                     OTHERWISE   SET &DATACLAS = 'DATAIN'
                   END
                   END /* IF NOT VALID LLQ, LEAVE AS 'DATAXX' */
            END /* SELECT LLQ END */
            IF (&HLQ = &SYSHLQ) && (&LLQ = &RELFILES) +
                THEN SET &DATACLAS = 'SMPTLIB'
            IF (&HLQ = &DB2HLQ) +
                THEN SET &DATACLAS = 'DB2'
          END /* END OF NULLDFLT DO GROUP */
WRITE '** STATUS  - DATACLAS VALUE (' &DATACLAS ') ASSIGNED **'
EXIT CODE(0)
END
