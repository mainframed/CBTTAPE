/* BUILDING DEPOSIT, INC. SMS ACS STORCLASS ROUTINE                 */
PROC &STORCLAS
/* LIST OF VALID STORAGE CLASSES                                    */
/* SMS VOLUME VTOC AND VVDS ARE ALWAYS CACHED                       */
FILTLIST &VALID_STORAGE_CLASSES INCLUDE(
   'ANYWHERE',
   'CICSR',
   'CICSW',
   'SCPERFR',
   'SCPERFW',
   'SCCACHR',
   'SCCACHW',
   'SCSTNRD',
   'SCSTNRDG',
   'SCTEMP',
   'SYSSHARE',
   'SYSMAINT',
   'SMSTEST',
   'SCDPLXR',
   'SCDPLXW')
/* LIST OF VALID AUTHORIZED TSO USERIDS THAT ARE PERMITTED TO       */
/* REQUEST A SPECIFIC STORAGE CLASS ASSIGNMENT                      */
FILTLIST &VALID_AUTHORIZED_RACF_GROUPS INCLUDE('TECHSUPT',
                                               'STCGROUP',
                                               'SECURITY',
                                               'DB2ADMIN',
                                               'OPERATON',
                                               'PRODCNTL')
FILTLIST &SYSHLQ INCLUDE(SYS%)
/* SORTWORK DDNAMES AND SORTOUT DDNAMES                             */
FILTLIST &VALID_SORTWORK_DDNAMES INCLUDE(SORT*)
/* SMPE RELFILES                                                    */
FILTLIST &RELFILES INCLUDE(F%*)
/* FDR                                                              */
FILTLIST &FDRABR   INCLUDE(FDR.PASSWORD.**,FDRABR.V*.**)
/* UTILITY PROGRAMS                                                 */
FILTLIST &COPYPGM INCLUDE(FDR*,ARC*,'ADRDSSU','GIMSMP')
/* FDR SPECIAL MUST CHEK BEFORE ACSENVIR CHECK                      */
/* EXIT HERE FOR NON-SMS */
IF (&STORCLAS = 'NONSMS') THEN DO
      SET &STORCLAS = ''
      EXIT CODE(0)
   END
IF (&DSN = &FDRABR) THEN DO
    SET &STORCLAS = 'PREALLOC'
      EXIT CODE(0)
   END
/* ASSIGN A NEW STORAGE CLASS ONLY IF THIS IS NEW SMS ALLOCATION    */
/* I.E. IF RECALL OR RECOVER OR ETC. THEN KEEP PREV ASSIGNED VALUE  */
IF (&ACSENVIR = 'RECALL' | &ACSENVIR = 'RECOVER') THEN DO
      SET &STORCLAS = &STORCLAS
      EXIT CODE(0)
   END
IF (&ACSENVIR ¬= 'ALLOC' && &ACSENVIR ¬= 'CONVERT') THEN DO
      SET &STORCLAS = &STORCLAS
      EXIT CODE(0)
   END
/* IF CONVERT PROCESS IN PLACE THEN SET STORCLAS=NULL               */
/* THIS WILL IN EFFECT PREVENT USAGE OF THIS FACILITY               */
/* ALL CONVERSIONS TO SMS SHOULD BE DONE BY DATASET                 */
IF (&ACSENVIR = 'CONVERT') THEN DO
      SET &STORCLAS = ''
      EXIT CODE(0)
   END
/* EXIT HERE FOR NON-SMS */
IF (&DATACLAS = 'NONSMS' | &DATACLAS = 'VVDS' | &DATACLAS = '') +
     THEN DO
      SET &STORCLAS = ''
      EXIT CODE(0)
   END
/* CHECK FOR UTILITY                                                */
IF (&STORCLAS ¬= '') && (&PGM = &COPYPGM) THEN DO
      SET &STORCLAS = &STORCLAS
      EXIT CODE(0)
   END
IF (&HLQ = 'SYST' | &HLQ = 'RESTART' | &STORCLAS = 'PREALLOC') THEN DO
    SET &STORCLAS = 'PREALLOC'
      EXIT CODE(0)
   END
IF (&HLQ = 'SYSY' | &HLQ = 'SYSZ') THEN DO
    SET &STORCLAS = 'ANYWHERE'
      EXIT CODE(0)
   END
IF (&STORCLAS = 'FASTREAD' | &STORCLAS = 'FASTWRIT') THEN DO
    SET &STORCLAS = &STORCLAS
      EXIT CODE(0)
   END
IF &DSN(2) = 'SOURCING' | &DSN(2) = 'JARSCHRG' | +
   &DSN(2) = 'JOBACCT' THEN DO
    SET &STORCLAS = 'SCSTNRDG'
    EXIT CODE(0)
   END
/* TRUE TEMPORARY DATASET PROCESSING; TRY TO GET VIO                */
IF &DSTYPE = 'TEMP' THEN DO
   IF (&DD ¬= &VALID_SORTWORK_DDNAMES) &&
      (&PGM ¬= 'GIMSMP') &&
      (&PGM ¬= 'IEBCOPY') THEN DO
          SET &STORCLAS = 'SCTEMP'
          WRITE '** STATUS  - STORCLAS VALUE (' &STORCLAS +
             ') ASSIGNED **'
          EXIT CODE(0)
          END
   END
/* ALLOCATION STORAGE CLASS PROCESSING                              */
/* IF AUTHORIZED RACF GROUP THEN ENABLE REQUESTED STORAGE CLASS     */
/* IF VALID NONNULL VALUE SPECIFIED OR FAIL IF INVALID VALUE        */
/* IF NONNULL VALUE SPECIFIED FOR NON-AUTHORIZED TSO USER THEN      */
/* FAIL ALLOCATION                                                  */
/* ELSE SET STANDARD VALUE OF 'SCSTNRD'                             */
IF &ANYVOL = MIGR* THEN DO
      SET &STORCLAS = ''
      EXIT CODE(0)
   END
IF (&STORCLAS ¬= '') && (&PGM = 'GVRESTOR') THEN DO
                   SET &STORCLAS = &STORCLAS
                   WRITE '** STATUS  - STORCLAS VALUE (' &STORCLAS +
                         ') ASSIGNED **'
                   EXIT CODE(0)
                END
IF (&STORCLAS ¬= '') THEN DO
      SELECT (&STORCLAS)
         WHEN(&VALID_STORAGE_CLASSES)
             SELECT (&GROUP)
                WHEN(&VALID_AUTHORIZED_RACF_GROUPS) DO
                   SET &STORCLAS = &STORCLAS
                   WRITE '** STATUS  - STORCLAS VALUE (' &STORCLAS +
                         ') ASSIGNED **'
                   EXIT CODE(0)
                END
                OTHERWISE DO
      WRITE '** ERROR   - UNAUTHORIZED STORCLAS VALUE (' &STORCLAS +
                   ') ASSIGNED **'
                EXIT CODE(16)
                END
             END
         OTHERWISE DO
             WRITE '** ERROR   - INVALID STORCLAS VALUE (' &STORCLAS +
                   ') ASSIGNED **'
             EXIT CODE(16)
             END
      END /* END OF MAJOR SELECT */
   END /* END OF MAJOR STORCLAS NONNULL GROUP */
IF (&HLQ = &SYSHLQ) | (&DATACLAS = 'DB2') +
  THEN SET &STORCLAS = 'SCSTNRDG'
   ELSE IF &ALLVOL = '' +
         THEN SET &STORCLAS = 'SCSTNRDG'
   ELSE SET &STORCLAS = 'SCSTNRD'
IF (&HLQ = &SYSHLQ) && (&LLQ = &RELFILES) +
   THEN SET &STORCLAS = 'SCSTNRD'
IF (&HLQ = &SYSHLQ) | (&DATACLAS = 'XCF') | (&DATACLAS = 'IODF') +
   THEN SET &STORCLAS = 'SYSSHARE'
IF (&HLQ = 'SYST') +
   THEN SET &STORCLAS = 'SYSMAINT'
WRITE '** STATUS  - STORCLAS VALUE (' &STORCLAS +
      ') ASSIGNED **'
EXIT CODE(0)
END
