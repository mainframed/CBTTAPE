/* BUILDING DEPOSIT, INC. SMS ACS MGMTCLASS ROUTINE                 */
PROC &MGMTCLAS
/* LIST OF VALID MANAGEMENT CLASSES                                 */
/* ALL CLASSES HAVE PARTIAL RELEASE = NO                            */
FILTLIST &VALID_MANAGEMENT_CLASSES INCLUDE(
   'INTERIM',
   'NOACTION',
   'INFREQBK',
   'STANDARD',
   'NOMIG',
   'TEMP')
/* FDR                                                              */
FILTLIST &FDRABRM    INCLUDE(FDR.PASSWORD.VMNT*.**,FDRABR.VMNT*.**)
FILTLIST &FDRABR     INCLUDE(FDR.PASSWORD.**,FDRABR.V*.**)
/* SYS1 HLQ                                                         */
FILTLIST &SYSTEM_HLQ INCLUDE('SYS1')
/* LIST OF VALID AUTHORIZED RACF GROUPS THAT ARE PERMITTED TO       */
/* REQUEST A SPECIFIC MANAGEMENT CLASS ASSIGNMENT                   */
FILTLIST &VALID_AUTHORIZED_RACF_GROUPS INCLUDE('TECHSUPT',
                                               'STCGROUP',
                                               'SECURITY',
                                               'DB2ADMIN',
                                               'OPERATON',
                                               'PRODCNTL')
/* UTILITY PROGRAMS                                                 */
FILTLIST &COPYPGM INCLUDE(FDR*,ARC*,'ADRDSSU')
/* TEST SYSTEM HLQ SET                                              */
FILTLIST &TEST_DSNHLQ_VALUES INCLUDE(
     TACCT*,
     TAPPL*,
     TAUDT*,
     TCORP*,
     TCRDT*,
     TDATA*,
     TDIST*,
     TEXEC*,
     TFINL*,
     THKNG*,
     TINFO*,
     TMANI*,
    'TMERC7',
     TREAL*,
     TSTOP*,
     TSTOR*,
     TTECH*)
/* ASSIGN A NEW MGMT    CLASS ONLY IF THIS IS NEW SMS ALLOCATION    */
/* I.E. IF RECALL OR RECOVER OR ETC. THEN KEEP PREV ASSIGNED VALUE  */
IF (&ACSENVIR ¬= 'ALLOC') THEN DO
      SET &MGMTCLAS = &MGMTCLAS
      EXIT CODE(0)
   END
/* UTILITY PROGRAM                                                  */
IF (&MGMTCLAS ¬= '') && (&PGM = &COPYPGM) THEN DO
      SET &MGMTCLAS = &MGMTCLAS
      EXIT CODE(0)
   END
/* TEMP SYST FIX                                                    */
IF &HLQ = 'SYST' OR &HLQ = 'SYSY' OR &HLQ = 'SYSZ' OR +
     &DSN = &FDRABRM THEN DO
      SET &MGMTCLAS = 'MVSMAINT'
      EXIT CODE(0)
   END
IF &MGMTCLAS = 'MVSPROD' OR &DSN = &FDRABR THEN DO
      SET &MGMTCLAS = 'MVSPROD'
      EXIT CODE(0)
   END
IF &DSN(2) = 'SOURCING' | &DSN(2) = 'JARSCHRG' | +
   &DSN(2) = 'JOBACCT' THEN DO
  SELECT (&HLQ)
     WHEN ('PACCT') SET &MGMTCLAS = 'PACCT'
     WHEN ('PAUDT') SET &MGMTCLAS = 'PAUDT'
     WHEN ('PCORP') SET &MGMTCLAS = 'PCORP'
     WHEN ('PCRDT') SET &MGMTCLAS = 'PCRDT'
     WHEN ('PDIST') SET &MGMTCLAS = 'PDIST'
     WHEN ('PFINL') SET &MGMTCLAS = 'PFINL'
     WHEN ('PMERC') SET &MGMTCLAS = 'PMERC'
     WHEN ('PREAL') SET &MGMTCLAS = 'PREAL'
     WHEN ('PSTOP') SET &MGMTCLAS = 'PSTOP'
     WHEN ('PSTOR') SET &MGMTCLAS = 'PSTOR'
     WHEN ('PTECH') SET &MGMTCLAS = 'PTECH'
     OTHERWISE SET &MGMTCLAS = 'STANDARD'
  END
  EXIT CODE(0)
END
/* ALLOCATION MANAGEMENT CLASS PROCESSING                           */
/* IF AUTHORIZED TSO USERID THEN ENABLE REQUESTED MANAGEMENT CLASS  */
/* IF VALID NONNULL VALUE SPECIFIED OR FAIL IF INVALID VALUE        */
/* IF NONNULL VALUE SPECIFIED FOR NON-AUTHORIZED TSO USER THEN      */
/* FAIL ALLOCATION                                                  */
/* IF TEST DATASET SET AS 'INTERIM'                                 */
/* ELSE SET AS 'STANDARD'                                           */
/* PERMIT MANAGEMENT CLASS OF TEMP FOR EVERYBODY                    */
IF (&MGMTCLAS ¬= '') && (&PGM = 'GVRESTOR') THEN DO
                   SET &MGMTCLAS = &MGMTCLAS
                   WRITE '** STATUS  - MGMTCLAS VALUE (' &MGMTCLAS +
                         ') ASSIGNED **'
                   EXIT CODE(0)
                END
IF (&MGMTCLAS = 'TEMP') THEN DO
      SET &MGMTCLAS = &MGMTCLAS
      WRITE '** STATUS  - MGMTCLAS VALUE (' &MGMTCLAS +
                 ') ASSIGNED **'
      EXIT CODE(0)
   END
IF (&MGMTCLAS ¬= '') THEN DO
      SELECT (&MGMTCLAS)
         WHEN(&VALID_MANAGEMENT_CLASSES)
             SELECT (&GROUP)
                WHEN(&VALID_AUTHORIZED_RACF_GROUPS) DO
                   SET &MGMTCLAS = &MGMTCLAS
                   WRITE '** STATUS  - MGMTCLAS VALUE (' &MGMTCLAS +
                         ') ASSIGNED **'
                   EXIT CODE(0)
                END
                OTHERWISE DO
      WRITE '** ERROR   - UNAUTHORIZED MGMTCLAS VALUE (' &MGMTCLAS +
                   ') ASSIGNED **'
                EXIT CODE(16)
                END
             END
         OTHERWISE DO
             WRITE '** ERROR   - INVALID MGMTCLAS VALUE (' &MGMTCLAS +
                   ') ASSIGNED **'
             EXIT CODE(16)
             END
      END /* END OF MAJOR SELECT */
   END /* END OF MAJOR MGMTCLAS NONNULL GROUP */
IF (&DATACLAS = 'IODF') THEN DO
    SET &MGMTCLAS = 'NOMIG'
    WRITE '** STATUS  - MGMTCLAS VALUE (' &MGMTCLAS +
      ') ASSIGNED **'
    EXIT CODE(0)
    END
IF (&DATACLAS = 'XCF') THEN DO
    SET &MGMTCLAS = 'INFREQBK'
    WRITE '** STATUS  - MGMTCLAS VALUE (' &MGMTCLAS +
      ') ASSIGNED **'
    EXIT CODE(0)
    END
IF (&DATACLAS = 'DB2') THEN DO
    SET &MGMTCLAS = 'NOMIG'
    WRITE '** STATUS  - MGMTCLAS VALUE (' &MGMTCLAS +
      ') ASSIGNED **'
    EXIT CODE(0)
    END
SELECT (&HLQ)
    WHEN(&TEST_DSNHLQ_VALUES) SET &MGMTCLAS = 'INTERIM'
    WHEN(&SYSTEM_HLQ) SET &MGMTCLAS = 'NOMIG'
    OTHERWISE SET &MGMTCLAS = 'STANDARD'
END
WRITE '** STATUS  - MGMTCLAS VALUE (' &MGMTCLAS +
      ') ASSIGNED **'
EXIT CODE(0)
END
