JRP300 TITLE '328X PRINTER SUBTASK'
         TITLE 'START-UP'
***********************************************************************
*                                                                     *
*             MODULE NAME = JRP300                                    *
*                                                                     *
*             DESCRIPTIVE NAME =  PRINTER SUBTASK ROUTINE  FOR JRP    *
*                            328X PRINTER"S                           *
*                                                                     *
*             AUTHOR = MIKE SCHORTMAN -                               *
*                                                                     *
*             COPYRIGHT = 1/8/84                                      *
*                                                                     *
*              MODIFIED =                                             *
*                                                                     *
*             STATUS =                                                *
*                                                                     *
*                                                                     *
*             FUNCTION =                                              *
*                                                                     *
*                                                                     *
*                                                                     *
*                OPERATION =                                          *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*              NOTES =                                                *
*                                                                     *
*                 DEPENDENCIES =                                      *
*                                                                     *
*                                                                     *
*                                                                     *
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        *
*                    REGISTERS 1 TO 11 = WORK REGISTERS               *
*                    REGISTER  12      = ADDRESSABILITY TO JRP300     *
*                                        CSECT                        *
*                    REGISTER  13      = SAVE AREA REGISTER AND       *
*                                        ADDRESSABILITY TO GETMAINED  *
*                                        WORK AREA                    *
*                    REGISTERS 14,15   = WORK REGISTERS               *
*                                                                     *
*                                                                     *
*                PROCESSOR = ASM                                      *
*                                                                     *
*                MODULE SIZE = -- BYTES                               *
*                                                                     *
*                ATTRIBUTES = SCHEDULER KEY 8, PROBLEM STATE          *
*                   PROBLEM PROGRAM STATE                             *
*                                                                     *
*             ENTRY POINTS = JRP---- (ONLY ENTRY POINT)               *
*                                                                     *
*                LINKAGE =                                            *
*                   FROM JRP---- AS A SUBROUTINE                      *
*                                                                     *
*             INPUT = R9 POINTS TO JRPPFLG ,R10 POINTS TO JRP"S       *
*                DYNAMIC PRINTER AREAS,R11 POINTS TO COMMAND OR       *
*                INITIALIZATION INPUT.                                *
*                                                                     *
*             OUTPUT = NONE                                           *
*                                                                     *
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   *
*                                                                     *
*                                                                     *
*             EXTERNAL REFERENCES =                                   *
*                                                                     *
*                ROUTINES = IKJPARS, DAIRFAIL, GNRLFAIL               *
*                                                                     *
*                DATA AREAS = NONE                                    *
*                                                                     *
*                CONTROL BLOCKS = PWT                                 *
*                                                                     *
*                                                                     *
*             TABLES = DATA AREA TO BE GETMAINED.  MAPPED BY DSECT    *
*                BEGINNING AT LABEL P3DWA.                            *
*                                                                     *
*             MACROS = JRPSAVE,JRPRTRN,TPUT,                          *
*                                                                     *
*             CHANGE ACTIVITY = NONE                                  *
*                                                                     *
*             MESSAGES =                                              *
*                                                                     *
*                THIS PROGRAM IS THE PROPERTY OF MIKE SCHORTMAN,      *
*                CONN. MUTUAL LIFE INS. CO.                           *
*                                                                     *
*             ABEND CODES = U300                                      *
*                                                                     *
*        *SKV*  CHANGED TRANSLATE TABLE SO IT WOULD WORK PROPERLY     *
*        *SKV*  CHANGED BANNER PAGE PROCESSING.                       *
*        *SKV*  CHANGED TO INCLUDE DISPLAYING AN END OF PRINT         *
*               MESSAGE.                                              *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                   SKV
         COPY  JRPGLBL                                              SKV
*                                                                   SKV
JRP300 CSECT
*        JRPIMAC                                                   *SKV
         JRPSAVE REG=12,ID=*,LV=P3DSASIZ
         USING P3DSA,R13
         L     R11,0(R1)            GETMAINED AREA
         USING J1DWA,R11
         MODESET KEY=ZERO,MODE=SUP
         LA    R0,41
         SVC   95
         MODESET KEY=NZERO,MODE=PROB
*
**       MOVE MODEL DATA TO GOTTEN AREA, ZERO REST.
*
P3WINIT  DS    0H
         LA    R0,P3DWA          START OF DYNAMIC AREA
         LA    R1,(P3DCLRE-P3DWA) LENGTH OF DYNAMIC AREA
         LA    R14,P3SWA           START OF STATIC DATA
         LA    R15,P3SMDL          LENGTH OF MODEL DATA
         MVCL  R0,R14              MOVE W/ZERO TO BE RENT,REUS
         ZAP   P3DIOER,=P'0'
         NI    J1DFLAG2,255-J1DFLUSH
         NI    J1DFLAG1,255-J1DRTLM
         NI    J1DFLAG1,255-J1DKEEP
         NI    J1DFLAG1,255-J1DBEGIN
         MVC   P3DLUID(8),J1DLUID
         MVC   P3DLGMD(8),J1DLGMD
         MVC   J1DBROK1(2),=C'00'
         MVC   J1DBROK2(2),=C'00'
         MVC   J1DBROK3(8),=C'00000000'
P3GPRST  DS    0H
         XR    R2,R2               CLEAR R2 FOR COUNTER
         MVI   J1DOPENX,X'80'      ONLY ONE IN LIST
         LA    R6,P3DBUFR
         CLI   J1DHSCS,C'Y'
         BE    P382NNEW
         MVC   0(2,R6),=X'F5C8'
         LA    R6,2(R6)
P382NNEW DS    0H
         ST    R6,J1DBFPTR
***                                                                 ***
*   INITIALIZE INPUT DCB                                              *
***                                                                 ***
         MVC   DCBDDNAM-IHADCB+P3DDCBIS(8),J1DDNM PUT DDNAME IN DCB
         LA    R15,P3DDCBI        GET INPUT DCB ADDR
         OPEN  ((R15),INPUT),MF=(E,J1DOPENX)
*SKV     TM    J1DFLAG2,J1DHDR
*SKV     BNO   P3GNTPUT
         MVC   J1DOPERM(4),=C'I400'
         MVC   J1DBFR+18(9),=C'BEGINNING'                          *SKV
P3UNSOL  DS    0H
         LA    R0,L'J1DBFR         LENGTH
         LA    R1,J1DBFR           ADDR OF P3G
         STM   R0,R1,J1DMSGLN
         ST    R11,P3DATACH
         LINK   EP=JRP310,MF=(E,P3DATACH)
TPUT     LA    R0,L'J1DBFR         LENGTH
         LA    R1,J1DBFR           ADDR OF P3G
         TPUT  (1),(0),EDIT,NOWAIT,NOHOLD,BREAKIN,LOWP,           XXXXXX
               USERIDL=J1DJOBN
         MVC   J1DOPER1(L'J1DBFR),J1DBFR
         LA    R1,J1DOPER
         SVC   35
*    WRITE SMF RECORD FOR BEGINNING                                *SKV
         USING JRP4DS,R1                                           *SKV
         L     R1,J1DJRP4D                                         *SKV
         MVI   JRP4STAT,C'R'                                       *SKV
         MVI   JRP4DSRN,X'10'                                      *SKV
         MVI   JRP4DATA+00,X'40'                                   *SKV
         MVC   JRP4DATA+01(39),JRP4DATA                            *SKV
         MVC   JRP4DATA+00(8),J1DBFR+00                            *SKV
         MVC   JRP4DATA+09(8),J1DBFR+09                            *SKV
         MVC   JRP4DATA+18(5),J1DBFR+31                            *SKV
         MVC   JRP4DATA+24(6),J1DBFR+37                            *SKV
*        LR    R2,R1                                               *SKV
         L     R15,J1DJRP4A                                        *SKV
         BALR  R14,R15                                             *SKV
         DROP  R1                                                  *SKV
P3GNTPUT NI    J1DFLAG1,255-J1DEOF RESET END-OF-FILE FLAG
         CLI   J1DSEP,C'N'         PAGE SEPERATORS DESIRED?
         BE    P3GNSEP             NO-SKIP IT
         TM    J1DFLAG2,J1DHDR     ARE THERE HEADERS REQUIRED??
         BNO   P3GNSEP             NO-SKIP IT
P3GNDATE DS    0H                  NO PRINT TODAY
         LA    R1,J1DBEG           LINE 1
         LA    R0,L'J1DBEG
         MVC   01(07,R1),=C'*******'                               *SKV
         MVC   08(01,R1),=C' '                                     *SKV
         MVC   09(15,R1),J1DBFR+31        DEVICE NAME              *SKV
         MVC   24(01,R1),=C' '                                     *SKV
         MVC   25(07,R1),=C'*******'                               *SKV
         MVC   32(07,R1),=C' START '                               *SKV
         MVC   39(07,R1),=C'*******'                               *SKV
         MVC   46(01,R1),=C' '                                     *SKV
         MVC   47(09,R1),J1DBFR          JOBNAME                   *SKV
         MVC   56(01,R1),=C' '                                     *SKV
         MVC   57(03,R1),=C'***'                                   *SKV
         MVC   60(01,R1),=C' '                                     *SKV
         MVC   61(08,R1),J1DBFR+9        STC/JOB/TSU NUMBER        *SKV
         MVC   69(01,R1),=C' '                                     *SKV
         MVC   70(07,R1),=C'*******'                               *SKV
*                                                                  *SKV
         MVI   J1DCONCH,X'09'       SP1 AFTER
         BAL   R14,P3WLOCS         PRINT HEADER
         LA    R1,J1DBEG           LINE 2                          *SKV
         LA    R0,L'J1DBEG                                         *SKV
         MVI   J1DCONCH,X'09'       SP1 AFTER                      *SKV
         BAL   R14,P3WLOCS         PRINT HEADER                    *SKV
         LA    R1,J1DBEG           LINE 3                          *SKV
         LA    R0,L'J1DBEG                                         *SKV
         MVI   J1DCONCH,X'09'       SP1 AFTER                      *SKV
         BAL   R14,P3WLOCS         PRINT HEADER                    *SKV
         LA    R1,J1DBEG           LINE 4                          *SKV
         LA    R0,L'J1DBEG                                         *SKV
         MVI   J1DCONCH,X'09'       SP1 AFTER                      *SKV
         BAL   R14,P3WLOCS         PRINT HEADER                    *SKV
         LA    R1,J1DBEG           LINE 5                          *SKV
         LA    R0,L'J1DBEG                                         *SKV
         MVI   J1DCONCH,X'89'       SP1 AFTER                      *SKV
         BAL   R14,P3WLOCS         PRINT HEADER                    *SKV
         MVI   J1DCONCH,X'8B'       SP1 AFTER                      *SKV
P3GNSEP  DS    0H                  SKIP DATASET HEADER
         OI    J1DFLAG1,J1DNEWDS   SET FLAG TO SUPPRESS 1ST SKIP
         B     P3GET
         TITLE  'LOOP GETTING/WRITING DATA SET UNTIL END'
P3GLOOP  DS    0H
         GET   P3DDCBI
         CLC   J1DMLNE,=F'0'      THIS FIX ALLOWS MLIN TO      JRP0001
         BE    P3KPRNT             BE NULLIFIED BY MLIN=0      JRP0001
         LA    R2,1(R2)
         C     R2,J1DMLNE
         BNH   P3KPRNT
         OI    J1DFLAG2,J1DFLUSH   NO RESET OPNDST FLAG
         NI    J1DFLAG1,255-J1DKEEP
         MVC   J1DWHY,=CL10'TERMINATED'
         MVC   J1DBYWHO,=CL8'MAXLINE'
         MVC   J1DOPERM(4),=C'W400'
*
**       GET CARRAGE, LINE LEN., AND LOCATION FOR WRITER.
*
P3KPRNT  LH    R0,DCBLRECL-IHADCB+P3DDCBI GET DEF. LRECL
         TM    DCBRECFM-IHADCB+P3DDCBI,DCBRECU UNDEFINED
         BO    P3GNOTV             YES
         TM    DCBRECFM-IHADCB+P3DDCBI,DCBRECV VARIABLE
         BNO   P3GNOTV             NO
         LH    R0,0(R1)            GET REC LEN
         LA    R1,4(R1)            SKIP RDW
         SH    R0,=H'4'            SKIP RDW LEN.
P3GNOTV  DS    0H
         TM    DCBRECFM-IHADCB+P3DDCBI,X'02' MACH. CARRIAGE
         BNO   P3FRC1SP            NO-TRY ASCII
         MVC   J1DCONCH,0(R1)
P3ADJREG DS    0H
         LA    R1,1(R1)            SKIP CC
         BCTR  R0,0                SKIP CC LEN.
         B     P3PUTIT             SETUP FOR WRITER
P3FRC1SP DS    0H
         MVI   J1DCONCH,X'09'      1SP AFTER
         TM    DCBRECFM-IHADCB+P3DDCBI,X'04' ASCII
         BNO   P3PUTIT             NO CARRIAGE
*
**       BREAK ASCII COMMAND DOWN INTO IMM. AND PRINT
*
         STM   R0,R1,J1DSAV01      SAVE STRT, LEN
         LA    R14,=X'F00B6013F18B'  ASCII XLATE TBL
         LA    R15,3          L' OF TABLE(3 PAIRS)
***********************************************************************
*        IDEA IS TO PRINT ALL W/ 1SP AFTER.
*        NOTE NO SUPPORT FOR '+' - OVERPRINT.
*        SO '0' = (SP 1 IMM)+(PR W/ 1 SP AFTER)
*           '-' = (SP 2 IMM)+(PR W/ 1 SP AFTER)
*           '1' = (SK CH 1 IMM)+(PR W/ 1 SP AFTER)
*           ' ' OR '+' = (PR W/ 1 SP AFTER)
***********************************************************************
P3FRLOC  DS    0H
         CLC   0(1,R14),0(R1)
         BE    P3FRFND             FOUND ASCII ARG. CHAR
         LA    R14,2(R14)          NEXT PAIR
         BCT   R15,P3FRLOC
         B     P3ADJREG            UNKNOWN - FORCE 1 SP
P3FRFND  DS    0H
         MVC   J1DCONCH,1(R14)     MOVE IMMEDIATE PART OF ASCII
         XR    R0,R0               ZERO LENGTH
         BAL   R14,P3WLOCS         PUT IMMEDIATE PART
         LM    R0,R1,J1DSAV01      RESTORE JRPREGS
         MVI   J1DCONCH,X'09'      1SP AFTER
         B     P3ADJREG            ADJ JRPREGS AROUND CARRIAGE
*
**       NORMAL PUT INTERFACE
*
P3PUTIT  DS    0H
         BAL   R14,P3WLOCS
         B     P3GET
P3GCALLW DS    0H                  RE-CALL WTR - USE PREV. R0/R1
         BAL   R14,P3WLOC          GO TO VTAM WRITER
P3GET    DS    0H                  GET NEXT RECORD
         TM    J1DFLAG2,J1DPURGE
         BNO   P3CKSTOP
         MVC   J1DHSTAT(10),=CL10'DRAINING'
P3CKSTOP TM    J1DFLAG1,J1DSTOP    WAS STOP ISSUED DURING VTAM WRITE
         BNO   P3CKFLSH
         OI    J1DFLAG1,J1DKEEP
         B     P3GEOD              YES-SIM. EODAD
P3CKFLSH TM    J1DFLAG2,J1DFLUSH   FLUSH AND PURGE CURR. D.S.
         BNO   P3CKIOER            NO FLUSH - CK FOR I/O ERR
         TM    J1DFLAG1,J1DKEEP
         BNO   P3SYNFL
         MVC   J1DOPERM(4),=C'I402'
         MVC   J1DWHY,=CL10'RESTARTED'
         B     P3FLUSHR
P3SYNFL DS     0H                                               9041
         MVC   J1DOPERM(4),=C'I401'
         MVC   J1DWHY,=CL10'TERMINATED'
P3FLUSHR NI    J1DFLAG2,255-J1DFLUSH
         CLI   J1DHSCS,C'Y'
         BE    *+10
         MVC   P3DBUFR(2),=X'F5C8' RESET WRITE CONTROL CHAR
         LA    R0,L'J1DFLSMS
         LA    R1,J1DFLSMS         FLUSH MESSAGE
         STM   R0,R1,J1DMSGLN
         ST    R11,P3DATACH
         LINK   EP=JRP310,MF=(E,P3DATACH)
         LA    R1,J1DFLSMS         GET FLUSH P3G
         LA    R0,L'J1DFLSMS
* SEND FLUSH MESSAGE TO USER IF APPLICABLE
         TPUT  (1),(0),EDIT,NOWAIT,NOHOLD,BREAKIN,LOWP,           XXXXXX
               USERIDL=J1DJOBN
         MVC   J1DOPER1(L'J1DFLSMS),J1DFLSMS
         LA    R1,J1DOPER          GET FLUSH P3G
         SVC   35
         LA    R1,J1DFLSMS         GET FLUSH P3G
         LA    R0,L'J1DFLSMS
         MVI   J1DCONCH,X'89'      CH1 AFTER
         OI    J1DFLAG2,J1DTRUNC   FORCE PHYSICAL WRITE(TRUNC)
         BAL   R14,P3WLOCS
         B     P3GECLS             CLOSE OUT
*
**       SYNAD  ROUTINE ON JES INPUT D.S.
*
P3SYN    DS    0H                                               9041
         AP    P3DIOER,=P'1'
         CP    P3DIOER,=P'20'
         BL    P3GLOOP
         MVC   J1DWHY(10),=C'CANCELED'
         MVC   J1DBYWHO,=CL8'IO ERROR'                          9041
*        OI    J1DFLAG1,J1DKEEP                 LINE DELETED
         B     P3SYNFL             FLUSH DATASET                9041
*
**       CHECK FOR I/O ERROR
*
P3CKIOER DS    0H
         TM    J1DFLAG1,J1DIOERR         IO ERR
         BNO   P3GRSET             NO - GET NEXT
         MVC   J1DHSTAT(10),=CL10'RETRYING'
         LH    R1,J1DRETLM         GET CURRENT RETRY LIMIT
         LA    R1,1(R1)            BUMP
         STH   R1,J1DRETLM
         L     R9,J1DRPL
         MVC   P3DWORK+6(1),RPLRTNCD-IFGRPL(R9) RTNCD=
         MVI   P3DWORK+7,X'0C'
         UNPK  J1DBROK1(3),P3DWORK+6(2)
         LA    R14,J1DBROK1
         LA    R15,2
         LA    R6,P3STBL1
         EX    R15,P3STR
         MVI   J1DBROK1+2,C' '
         MVC   P3DWORK+6(1),RPLFDB2-IFGRPL(R9) FDBK2=
         MVI   P3DWORK+7,X'0C'
         UNPK  J1DBROK2(3),P3DWORK+6(2)
         LA    R14,J1DBROK2
         LA    R15,2
         LA    R6,P3STBL1
         EX    R15,P3STR
         MVI   J1DBROK2+2,C' '
         MVC   P3DWORK+3(4),RPLSSEI-IFGRPL(R9) SENSE=
         MVI   P3DWORK+7,X'0C'
         UNPK  J1DBROK3(9),P3DWORK+3(5)
         LA    R14,J1DBROK3
         LA    R15,8
         LA    R6,P3STBL1
         EX    R15,P3STR
         MVI   J1DBROK3+8,C' '
         TM    J1DRETLM+1,X'64'    IS IT AT 100 YET
         BNO   P3WAIT              NO - NO NOTIFY OF OPER
         MVC   J1DHSTAT(10),=CL10'RETRYLM'
         OI    J1DFLAG1,J1DRTLM    RETRY LIMIT EXCEEDED
         OI    J1DFLAG1,J1DKEEP    SAVE DATA-SET
         MVC   J1DBEGJ(8),BLANKS   CLEAR JOB NUMBER IN HEADER
         XC    J1DRETLM,J1DRETLM   CLEAR COUNTER
         LA    R0,J1DTPUTL         LENGTH
         LA    R1,J1DTPUT          ADDR OF P3G
         STM   R0,R1,J1DMSGLN
         ST    R11,P3DATACH
         LINK   EP=JRP310,MF=(E,P3DATACH)
         LA    R1,J1DCMT2          WTO LIST
         SVC   35
         B     P3GECLS
P3WAIT   DS    0H
         STIMER WAIT,BINTVL==F'2000' WAIT A WHILE (SEC * 100)
         NI    J1DFLAG1,255-J1DIOERR
         B     P3GCALLW            RE-TRY WRITE AGAIN
P3GRSET  DS    0H
         TM    J1DFLAG1,J1DEOF     AT EOF?
         BO    P3GEOD1             YES-RETURN TO CLOSE CODE
         B     P3GLOOP
         TITLE  'END OF DATA SET'
P3GEOD   DS    0H
         OI    J1DFLAG2,J1DTRUNC   FORCE PHYSICAL WRITE
         MVI   J1DCONCH,X'8B'      SKIP CH1 IMM.
         LA    R1,J1DCONCH         DUMMY START ADDR
         L     R9,J1DRPL
         LTR   R9,R9
         BZ    P3GEOD1
         OI    RPLRH3-IFGRPL(R9),RPLEB   TELL VTAM END OF BRACKET
         XR    R0,R0               LEN. = ZERO
         BAL   R14,P3WLOCS         SKIP
P3GEOD1  DS    0H                  SECONDARY ENTRY POINT
         OI    J1DFLAG1,J1DEOF     SET END OF FILE
         TM    J1DFLAG1,J1DSTOP    STOPPING?
         BO    P3GECLS             YES-DONT CHECK FOR I/O ERROR
         TM    J1DFLAG1,J1DIOERR   I/O ERROR ON FLUSH
         BO    P3CKIOER            YES-GOTO ERROR RECOVERY CODE
P3GECLS  EQU   *
*
**       CLOSE INPUT DCB AND FREE BUFFER POOL
*
         LA    R15,P3DDCBI
         CLOSE ((R15),DISP),MF=(E,J1DOPENX)
         FREEPOOL P3DDCBI          FREE BUFFER POOL
         MVC   J1DOPERM(4),=C'I403'                                *SKV
         MVC   J1DBFR+18(9),=C'  ENDED  '                          *SKV
P3UNSOL1 DS    0H                                                  *SKV
         LA    R0,L'J1DBFR         LENGTH                          *SKV
         LA    R1,J1DBFR           ADDR OF P3G                     *SKV
         STM   R0,R1,J1DMSGLN                                      *SKV
         ST    R11,P3DATACH                                        *SKV
         LINK   EP=JRP310,MF=(E,P3DATACH)                          *SKV
TPUT1    LA    R0,L'J1DBFR         LENGTH                          *SKV
         LA    R1,J1DBFR           ADDR OF P3G                     *SKV
         TPUT  (1),(0),EDIT,NOWAIT,NOHOLD,BREAKIN,LOWP,           XXXXXX
               USERIDL=J1DJOBN                                     *SKV
         MVC   J1DOPER1(L'J1DBFR),J1DBFR                           *SKV
         LA    R1,J1DOPER                                          *SKV
         SVC   35                                                  *SKV
*    WRITE SMF RECORD FOR TERMINATION                              *SKV
         USING JRP4DS,R1                                           *SKV
         L     R1,J1DJRP4D                                         *SKV
         MVI   JRP4STAT,C'R'                                       *SKV
         MVI   JRP4DSRN,X'11'                                      *SKV
         MVI   JRP4DATA+00,X'40'                                   *SKV
         MVC   JRP4DATA+01(39),JRP4DATA                            *SKV
         MVC   JRP4DATA+00(8),J1DBFR+00                            *SKV
         MVC   JRP4DATA+09(8),J1DBFR+09                            *SKV
         MVC   JRP4DATA+18(5),J1DBFR+31                            *SKV
         MVC   JRP4DATA+24(6),J1DBFR+37                            *SKV
*        LR    R2,R1                                               *SKV
         L     R15,J1DJRP4A                                        *SKV
         BALR  R14,R15                                             *SKV
         DROP  R1                                                  *SKV
         TITLE  'W A I T  F O R   W O R K  O R  S T O P  W R I T E R '
         TM    J1DFLAG1,J1DBEGIN
         BNO   JRPRTRN
         L     R9,J1DRPL
         CLSDST RPL=(R9),OPTCD=SYN
***                                                                 ***
*        STOP THE WRITER                                              *
***                                                                 ***
JRPRTRN  DS   0H
         XR    R15,R15
*        RETURN TO CALLER
         JRPRTRN
         TITLE  'V T A M  W R I T E R'
P3WLOCS  DS   0H
         ST    R0,J1DISIZ          L'
         ST    R1,J1DILOC          ADDR
P3WLOC   DS    0H
         ST    R14,J1DWRET         SAVE RETURN
*
**       SUPRESS SKIP IF FIRST FOR D.S.
*
         TM    J1DFLAG1,J1DNEWDS
         BNO   P3WNNEW             NOT A NEW D.S.
         NI    J1DFLAG1,255-J1DNEWDS
         CLI   J1DCONCH,X'8B'      IS 1ST CMD A SKIP CH1?
         BE    P3WRET              YES-SUPRESS IT-HEADER ALREADY AT CH1
         CLI   J1DCONCH,X'89'      CH 1 AFTER
         BNE   P3WNNEW             NO-NORMAL
         MVI   J1DCONCH,X'09'      MAKE 1 SP AFTER
P3WNNEW  DS    0H                  NO SUPRESSION
**       XLATE 328X CTRL/NON-PRINTS OUT
         LM    R0,R1,J1DILOC       GET ADDR,LENGTH OF LINE
         LM    R14,R15,J1DILOC     "
         SH    R15,=H'1'           -1 FOR EXECUTE INSTR
         BM    P3WNOTR             LEN = ZERO - NO XLATE
         LA    R6,P3STBL
         EX    R15,P3STR           XLATE 3270 CTRL CHARS OUT
P3WNOTR  DS    0H
*        MOVE PRINTLINE TO BUFFER
         C     R1,=F'256'          MAX WIDTH (LENGTH OF LINE)
         BL    *+8                 OK
         L     R1,=F'256'          FORCE DOWN TO MAX LENGTH
         L     R14,J1DBFPTR        GET OUTPUT BUFFER NXT AVAL.*
         OC    J1DRETLM,J1DRETLM   IO ERR RETRY?
         BNZ   P3WRETRY            YES-GO TO WRITE
         TM    J1DCONCH,X'02'      IMMEDIATE COMMAND (SPACE ONLY)
         BO    P3WIMM              YES - DONT MOVE DATA
         LR    R15,R1              NOT IMM. MOVE DATA TO BUFFER
         MVCL  R14,R0              R14 WILL POINT AFTER LAST BYTE
P3BKSCAN DS    0H
         BCTR  R14,0               BACK UP ONE BYTE
         CLI   0(R14),C' '         IS IT BLANK
         BE    P3BKSCAN            YES-BACK UP ANOTHER
*              CANT BACK UP OUT OF  BUFFER SINCE WCC IS FIRST BYTE
         LA    R14,1(R14)          R14 POINTS TO NEXT AVALIABLE BYTE
P3WIMM   DS    0H                  IMMEDIATE DATA
         TM    J1DCONCH,X'C0'      SKIP COMMAND
         BNZ   P3SKIP              YES-CALC # LINE FEEDS TO HEAD
P3WSPAC  DS    0H                  CHANGE SPACING TO NL(X'15')S
         IC    R1,J1DCONCH         GET CARRIAGE
         N     R1,=X'00000018'     MASK OFF JUNK
         BNZ   P3ADDSRL            PUT OUT NLS
         LA    R1,1*8              FORCE AT LEAST 1 SP
P3ADDSRL DS    0H
         SRL   R1,3                SHIFT TO GET SPACING
P3ADDLN  DS    0H                  LOOP ADDING NLS TO BUFFER
         L     R0,J1DCURPO         GET CURRENT LINE #
         AR    R0,R1               ADD NEW SPACING
         ST    R0,J1DCURPO         STORE IT
P3ADDNL  DS    0H
         MVI   0(R14),X'15'        NEW LINE CHAR
         LA    R14,1(R14)          NEW OUTPUT ADDR
         BCT   R1,P3ADDNL          LOOP
P3ADDEM  DS    0H
*
**       SEE IF BUFFER FULL YET
*
         ST    R14,J1DBFPTR        SAVE CURR POS.
         TM    J1DFLAG2,J1DTRUNC   FORCE WRITE
         BO    P3WFRC              YES
         LA    R0,P3DBUFR+400       LOAD END OF BUFFER-LEADWAY
         CR    R14,R0               SQUEZE ANOTHER RECORD?
         BL    P3WRET               YES-GET NEXT
P3WFRC   DS    0H
P3WRETRY DS    0H
         CLI   J1DHSCS,C'Y'
         BE    P3WRYSCS
         MVI   0(R14),X'19'        PUT EOM IN
         LA    R14,1(R14)          NEXT BYTE
P3WRYSCS LA    R4,P3DBUFR          GET START
         SR    R14,R4              LENGTH
         LR    R10,R14             LENGTH
         TM    J1DFLAG1,J1DBEGIN
         BO    VTMWRITE          NO  CONTINUE TO PROCESS WRITE
         L     R9,J1DRPL
         CLI   RPLREQ-IFGRPL(R9),RPLOPNDS WAS OPNDST LAST REQ ??
         BE    SIMLOG              YES  CONTROL BLOCKS O.K.
*
**       BUILD DYNAMIC VTAM CONTROL BLOCKS
*
VTAMDYN  DS    0H
         GENCB AM=VTAM,BLK=NIB,MODE=RECORD,LISTEND=YES,                X
               MF=(G,P3DNIBGN)
         MVC   J1DWERRM(4),=C'E400'
         MVC   J1DWERR1,=CL25'NIB GENCB FAILED'
         LTR   R15,R15
         BNZ   P3FATALF
         LR    R4,R1
         ST    R4,J1DNIB
         SPACE
P3WRPLGN DS    0H
         L     R6,J1DACB
         GENCB AM=VTAM,BLK=RPL,ACB=((R6)),POST=RESP,OPTCD=SYN,         X
               NIB=((R4)),CONTROL=DATA,MF=(G,P3DRPLGN)
         MVC   J1DWERRM(4),=C'E401'
         MVC   J1DWERR1(25),=CL25'RPL GENCB FAILED'
         LTR   R15,R15          CHECK RETURN CODE
         BNZ   P3FATALF         IF NOT ZERO DIE
         LR    R9,R1           LOAD ADDRESS OF RPL IN REG 9
         ST    R9,J1DRPL        SAVE IN FULL WORD AREA
         USING IFGRPL,R9
         CLI   J1DHSCS,C'Y'
         BE    P3RPLSCS
         OI    RPLRH3,P3DBBFLG   BRACKET=(BB,EB) NON SNA
         OI    RPLRH3,P3DEBFLG
         MVC   P3DMDFLG(1),RPLRH3 SET RPL FOR NON SNA BRACKETS
P3RPLSCS MVC   NIBSYM-ISTDNIB(8,R4),P3DLUID
         OI    RPLRH3-IFGRPL(R9),RPLBB
SIMLOG   DS    0H
         CLI   P3DLGMD,C' '
         BNE   P3DACQ
         SIMLOGON RPL=(R9),OPTCD=(SYN,RELRQ,Q)
         LTR    R15,R15
         BNZ    P3RETRY
OPENPRT  DS    0H
         OPNDST RPL=(R9),OPTCD=(SYN,ACCEPT,NQ,SPEC)
         LTR   R15,R15
         BZ    VTMWRITE
         B     P3RETRY
P3DACQ   DS    0H
         MVC   NIBLMODE-ISTDNIB(8,R4),P3DLGMD
         OPNDST RPL=(R9),OPTCD=(SYN,ACQUIRE)
         LTR   R15,R15
         BNZ   P3RETRY
VTMWRITE DS    0H
         OI    J1DFLAG1,J1DBEGIN OPNDST HAS BEEN SUCCESSFULLY ISSUED
         L     R9,J1DRPL
         ST    R10,RPLRLEN-IFGRPL(R9)     STORE RECORD LENGTH
         SEND  RPL=(R9),STYPE=REQ,CONTROL=DATA,POST=RESP,              X
               OPTCD=(SYN,CA),AREA=P3DBUFR,                            X
               RESPOND=(NEX,FME,NRRN),CHAIN=ONLY
P3VTAMXX DS    0H
         LTR   R15,R15             SUCCESSFUL
         BNZ   P3RETRY             YES CONTINUE
P3WCKCMP DS    0H
         MVC   J1DHSTAT(10),=CL10'ACTIVE'
         MVC   RPLRH3-IFGRPL(1,R9),P3DMDFLG   RESET BRACKET
         LA    R14,P3DBUFR                    GET BUFFER
         CLI   J1DHSCS,C'Y'                   IS THIS SCS
         BE    P3WSCSY                        YES BRANCH AROUND
         MVC   0(2,R14),=X'F5C8'              MOVE IN BISYNC 3270
         LA    R14,2(R14)          LOCAL CONTROL CHAR BYPASS
P3WSCSY  ST    R14,J1DBFPTR        RESET PTR
         NI    J1DFLAG2,255-J1DTRUNC RESET FORCED WRITE
         XC    J1DRETLM,J1DRETLM   YES - RESET RETRY COUNTER
P3WRET   DS    0H                  RETURN
         L     R14,J1DWRET         "
         BR    R14                 "
P3SKIP   DS    0H                  HANDLE SKIP COMMAND
         CLI   J1DCONCH,X'89'      SKIP CH 1 AFTER
         BE    P3SKIP1             YES
         CLI   J1DCONCH,X'8B'      SKIP CH 1 IMM.
         BE    P3SKIP1             YES
         B     P3WSPAC             FORCE TO SPACING COMMAND
P3SKIP1  DS    0H                  CALC # OF NLS TO TOP OF FORM
         CLI   J1DHSCS,C'Y'        GET CURRENT POS.
         BNE   P3SKIP2             CLEAR
         MVI   0(R14),X'0C'        DIVIDE BY LINES/PAGE
         LA    R14,1(R14)          LINES/PAGE
         B     P3ADDEM             MINUS REMAINDER = # OF NLS
P3SKIP2  DS    0H                  CALC # OF NLS TO TOP OF FORM
         L     R1,J1DCURPO         GET CURRENT POS.
         XR    R0,R0               CLEAR
         D     R0,J1DLEN           DIVIDE BY LINES/PAGE
         L     R1,J1DLEN           LINES/PAGE
         SR    R1,R0                MINUS REMAINDER = # OF NLS
         B     P3ADDLN             INSERT NLS
P3RETRY  DS    0H
P30811   CLC   RPLSSEI-IFGRPL(2,R9),=X'0811'    PRINT CANCEL
         BNE   P30821
         OI    J1DFLAG2,J1DFLUSH   NO RESET OPNDST FLAG
         NI    J1DFLAG1,255-J1DKEEP
         MVC   J1DWHY,=CL10'TERMINATED'
         MVC   J1DBYWHO,=CL8'CANCEL'
         B     P3WCKCMP
P30821   CLC   RPLSSEI-IFGRPL(2,R9),=X'0821'   BIND ERROR
         BNE   RETRYCMP
         MVI   J1DRETLM+1,X'64'    IS IT AT 100 YET
RETRYCMP DS    0H
         CLI   RPLREQ-IFGRPL(R9),RPLOPNDS WAS OPNDST LAST REQ ??
         BE    P3RETRYA            YES  NO CLSDST RPL NOT OPENED.
         CLSDST RPL=(R9),OPTCD=(SYN,RELEASE)
         NI    J1DFLAG1,255-J1DBEGIN  TURN OFF OPNDST FLAG
P3RETRYA OI    J1DFLAG1,J1DIOERR   LET CALL DECIDE WHAT TO DO
         B     P3WRET              RETURN(RE-CALL AT TWWLOC TO RETRY)
         TITLE 'CHECK FOR STOP/MODIFY'
P3FATALF DS    0H
         CVD   R15,J1DWORK         CONVERT CALLERS RETURN CODE IN R15
         STM   R14,R12,12(R13)     SAVE MY JRPREGS
         UNPK  J1DWERR2,J1DWORK+6(2) 999 MAX RC SUPPORTED
         OI    J1DWERR2+L'J1DWERR2-1,C'0' MAKE PRINTABLE
         LA    R0,L'J1DWERR-4      LENGTH
         LA    R1,J1DWERR+4        ADDR OF P3G
         STM   R0,R1,J1DMSGLN
         ST    R11,P3DATACH
         LINK   EP=JRP310,MF=(E,P3DATACH)
         LA    R1,J1DWERR          GET MF=L FORM OF WTO
         SVC   35                  ISSUE WTO
         MVC   J1DHSTAT(6),=C'ABENDU'
         MVC   J1DHSTAT+6(3),=C'300'
         OI    J1DFLAG1,J1DRTLM    MUST ISSUE $S
         OI    J1DFLAG1,J1DKEEP    UNALLOCATE AND KEEP DATASET
         MVC   J1DBEGJ(8),BLANKS   CLEAR JOBNUM FOR HEADER
         ABEND  300,DUMP           ABEND USER 300
J2SYN    EQU    *
***********************************************************************
*  EVERYTHING FROM HERE ON IS NOT COPIED TO DYNAMIC AREA              *
***********************************************************************
         TITLE 'LITERAL POOL'
*        JRPSMAC
P3STR    TR    0(*-*,R14),0(R6)    EXECUTED
         DS    0F
*DEFINE DCB AND OPEN AND CLOSE. (EODAD,SYNAD,OPEN AND CLOSE)
BLANKS   DC    CL16' '
         SPACE 2
P3STBL   DC    256C' '
UPCTBL   EQU   P3STBL
         ORG   UPCTBL+C' '
         DC    C' '
*        ORG   UPCTBL+C':'
*        DC    C':.<(+:&&'
         ORG   UPCTBL+X'4A'      *SKV* CHANGED TRANSLATES SO IT WOULD
         DC    X'4A'                   PROPERLY
         ORG   UPCTBL+X'4B'      *SKV* CHANGED TRANSLATES SO IT WOULD
         DC    C'.<(+|'                PROPERLY
         ORG   UPCTBL+X'50'      *SKV* CHANGED TRANSLATES SO IT WOULD
         DC    X'50'                   PROPERLY
         ORG   UPCTBL+C'!'
         DC    C'!$*);Â¬-/'
         ORG   UPCTBL+C','
         DC    C',%_>?'
         ORG   UPCTBL+C':'
         DC    C':#@''="'
         ORG   UPCTBL+X'81'
         DC    X'81,82,83,84,85,86,87,88,89'
         ORG   UPCTBL+X'91'
         DC    X'91,92,93,94,95,96,97,98,99'
         ORG   UPCTBL+X'A2'
         DC    X'A2,A3,A4,A5,A6,A7,A8,A9'
         ORG   UPCTBL+C'A'
         DC    C'ABCDEFGHI'
         ORG   UPCTBL+C'J'
         DC    C'JKLMNOPQR'
         ORG   UPCTBL+C'S'
         DC    C'STUVWXYZ'
         ORG   UPCTBL+C'0'
         DC    C'0123456789'
         ORG
P3STBL1  DC    256X'F0'
         ORG   P3STBL1+X'F0'
         DC    X'F0F1F2F3F4F5F6F7F8F9C1C2C3C4C5C6'
         ORG
         LTORG
         TITLE 'DYNAMIC - GOTTEN AREA'
P3SWA    DS    0D
**********************************************************************
****NOTE NOTE NOTE  MUST KEEP STATIC AND DYNAMIC AREAS IN SYNC***
****DYNAMIC IS GETMAINED AND STATIC IS USED TO INITIALIZE IT.
**********************************************************************
P3SDCBIS EQU   *
P3SDCBI  DCB   DSORG=PS,MACRF=GL,BUFNO=2,EODAD=P3GEOD,SYNAD=P3SYN
P3SDCBIE EQU   *
P3SATACH CALL  ,J2SYN,MF=L
         DS    0F
P3SWERR  WTO   'JRPI405 RXXXXXXX OUTPUT REQUED , RTNCD=XX ,FDBK2=XX  SEX
               NSE=00000000 ',MF=L,ROUTCDE=(8)
P3SMDL   EQU   *-P3SWA
         SPACE 2
         LTORG
         TITLE 'DYNAMIC - GOTTEN AREA'
P3DSA    DSECT
         DS    9D                  O/S SAVE
****NOTE NOTE NOTE  MUST KEEP STATIC AND DYNAMIC AREAS IN SYNC***
****DYNAMIC IS GETMAINED AND STATIC IS USED TO INITIALIZE IT.
P3DWA    DS    0F
P3DDCBIS EQU   *
P3DDCBI  DCB   DSORG=PS,MACRF=GL,BUFNO=2,EODAD=P3GEOD,SYNAD=P3SYN
P3DDCBIE EQU   *
P3DATACH CALL  ,J2SYN,MF=L
         DS    0F
P3DWERR  WTO   'JRPI405 RXXXXXXX OUTPUT REQUED , RTNCD=XX ,FDBK2=XX  SEX
               NSE=00000000 ',MF=L,ROUTCDE=(8)
***********************************************************************
*  EVERYTHING FROM HERE ON IS SET TO LOW VALUES AT INIT TIME          *
***********************************************************************
         DS    0F                  ALIGN
P3DNIBGN NIB   MODE=RECORD
P3DRPLGN RPL   AM=VTAM
         DS    0F
ST4      DS    F
ST5      DS    F
ST6      DS    F
P3DBUFR  DS    CL768
P3DWORK  DS    D
P3DLUID  DS    CL8
P3DLGMD  DS    CL8
P3DIOER  DS    PL2
P3DMDFLG DS    X
*
P3DCLRE  EQU   *                   END OF DYNAMIC AREA
P3DSASIZ EQU   *-P3DWA             SIZE OF DYNAMIC AREA
P3DBBFLG EQU   X'80'
P3DEBFLG EQU   X'40'
         JRPREGS
*        AIF   ('&TCPIP' EQ 'NO').NOTCPI4                          *SKV
*        COPY  MDCB                                            *DHC*SKV
*.NORCPI4 ANOP                                                     *SKV
         DCBD   DSORG=PS
         IFGRPL AM=VTAM             RPL
         ISTDNIB                     NIB
         JRPDMAC
         JRP4DS                                                    *SKV
         END
