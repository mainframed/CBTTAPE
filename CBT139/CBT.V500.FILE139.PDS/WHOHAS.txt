WHOHAS   CSECT
***********************************************************************
* FUNCTION  THIS MODULE USES THE GQSCAN MACRO TO LOOK AT THE          *
*           RESOURCE QUEUES TO SEE WHO HAS CONTROL OF A               *
*           DATASET.                                                  *
***********************************************************************
*   FIXED BY SAM GOLOB SO AN EQUAL SIGN ( = ) WILL END THE PROGRAM.   *
*     Also "BYE" or "QUIT".  After label START, you can adjust these. *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SAVE  (14,12),,WHOHAS-C.LAWLESS-&SYSDATE
         LR    R12,R15
         USING WHOHAS,R12
         LR    R11,R13
         CNOP  0,4
         BAL   13,*+76
         DC    18A(0)                  OUR SAVE AREA
         ST    13,8(11)
         ST    11,4(13)
         TPUT  INMSGA,L'INMSGA
START    TPUT  INMSG1,L'INMSG1
         XC    TOKEN,TOKEN
         MVI   BUFADR,X'40'
         MVC   BUFADR+1(132),BUFADR
         TGET  BUFADR,L'BUFADR
         LTR   R1,R1                   CHECK FOR NULL INPUT
         BZ    EXITMAIN                IF SO THEN EXIT
         CLI   BUFADR,C'='             =
         BE    EXITMAIN                IF SO THEN EXIT
         CLC   =C'BYE'(3),BUFADR       Uppercase BYE
         BE    EXITMAIN                IF SO THEN EXIT
         CLC   =C'QUIT'(4),BUFADR      Uppercase QUIT
         BE    EXITMAIN                IF SO THEN EXIT
         CLC   =C'bye'(3),BUFADR       Lowercase bye
         BE    EXITMAIN                IF SO THEN EXIT
         CLC   =C'quit'(4),BUFADR      Lowercase quit
         BE    EXITMAIN                IF SO THEN EXIT
*                                      Mixed case will not end the pgm
         MVC   RNAME(44),BUFADR
         TR    RNAME,TRUPTAB           TRANSLATE TO UPPER CASE
         TRT   RNAME,TRTAB             FIND LENGTH OF RNAME FIELD
         LA    R7,RNAME
         SR    R1,R7
         LR    R2,R1
CONT     LA    R6,AREA
         GQSCAN AREA=(AREA,8000),SCOPE=ALL,TOKEN=TOKEN,                X
               RESNAME=(QNAME,RNAME,(R2))
         ST    R15,SAVR15              SAVE RETURN CODE
         LTR   R15,R15                 RC=0, AREA IS BIG ENOUGH
         BE    SCANL1
         C     R15,=F'8'
         BE    SCANL1                  AREA WAS TOO SMALL
         C     R15,=F'4'               NO RESOURCES
         BE    SCANL2
         B     ERROR                   RC>8
SCANL1   STCM  R0,12,TEMP2             TEMP2 = 1ST HWRD OF R0
         L     R8,TEMPF                R8 = LENGTH OF RIB
         STCM  R0,3,TEMP2              TEMP2 = 2ND HWRD OF R0
         L     R9,TEMPF                R9 = LENGTH OF RIBE
         LR    R5,R1                   R5 = NO OF RIBS IN AREA
         USING RIB,R6
RIBLOOP  LR    R7,R6
         AR    R7,R8
         AH    R7,RIBVLEN
         L     R3,RIBNTO               R3 = NO OF OWNERS
         CVD   R3,DWORD
         MVC   EDWORK,EDMASK
         ED    EDWORK,DWORD+4
         MVC   OWN1,EDWORK+1
         L     R3,RIBNTWE              R3 = WAIT (EXCLUSIVE)
         CVD   R3,DWORD
         MVC   EDWORK,EDMASK
         ED    EDWORK,DWORD+4
         MVC   EXCL1,EDWORK+1
         L     R3,RIBNTWS              R3 = WAIT (SHARED)
         CVD   R3,DWORD
         MVC   EDWORK,EDMASK
         ED    EDWORK,DWORD+4
         MVC   SHR1,EDWORK+1
         TPUT  MSG1,L'MSG1
         L     R10,RIBNRIBE            NO. OF RIBE FOR RIB IN AREA
         USING RIBE,R7
RIBELOOP MVC   MSG2JBN,RIBEJBNM
         TM    RIBESFLG,X'80'          0 = WAITING,1 = OWNER
         BO    OWNER
         MVC   MSG2TYP,MSGWAIT
         B     CHKUSE
OWNER    MVC   MSG2TYP,MSGOWN
CHKUSE   TM    RIBERFLG,X'80'
         BO    SHARED
         MVC   MSG2USE,MSGEXCL
         B     OUTMSG2
SHARED   MVC   MSG2USE,MSGSHR
OUTMSG2  TPUT  MSG2,L'MSG2
         AR    R7,R9                   SKIP TO END OF RIBE
         BCT   R10,RIBELOOP            LOOP IF MORE RIBE FOR THIS RIB
         LR    R6,R7                   SET ADDRES OF NEXT RIB
         BCT   R5,RIBLOOP              LOOP IF MORE RIB IN AREA
         CLC   SAVR15,=F'8'
         BE    CONT
         B     EXIT1
SCANL2   TPUT  ERROR1,L'ERROR1
         B     EXIT1
ERROR    TPUT  ERROR2,L'ERROR2
         B     EXIT1
EXIT1    B     START                   GO AROUND AGAIN
EXITMAIN L     13,4(13)                REAL EXIT POINT
         RETURN (14,12),RC=0
DWORD    DC    D'0'
SAVR15   DS    F
RLEN     DS    F'0'
TEMPF    DS    0F
TEMP1    DC    H'0'
TEMP2    DC    H'0'
TRTAB    DS    0CL256
         DC    XL64'00'
         DC    XL1'40'
         DC    XL191'00'
* TABLE BELOW IS USED TO TRANSLATE LOWERCASE DATA TO UPPERCASE
TRUPTAB  DS    0CL256
         DC    XL16'000102030405060708090A0B0C0D0E0F'
         DC    XL16'101112131415161718191A1B1C1D1E1F'
         DC    XL16'202122232425262728292A2B2C2D2E2F'
         DC    XL16'303132333435363738393A3B3C3D3E3F'
         DC    XL16'404142434445464748494A4B4C4D4E4F'
         DC    XL16'505152535455565758595A5B5C5D5E5F'
         DC    XL16'606162636465666768696A6B6C6D6E6F'
         DC    XL16'707172737475767778797A7B7C7D7E7F'
         DC    XL16'80C1C2C3C4C5C6C7C8C98A8B8C8D8E8F'
         DC    XL16'90D1D2D3D4D5D6D7D8D99A9B9C9D9E9F'
         DC    XL16'A0A1E2E3E4E5E6E7E8E9AAABACADAEAF'
         DC    XL16'F0F1F2F3F4F5F6F7F8F9BABBBCBDBEBF'
         DC    XL16'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    XL16'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    XL16'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    XL16'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
EDMASK   DC    XL9'402020202020202120'
EDWORK   DC    XL9'00'
ERROR1   DC    CL44'RESOURCE NOT FOUND'
ERROR2   DC    CL44'ERROR IN GQSCAN'
INMSGA   DC    CL50'TERMINATE BY ENTERING NO DATA, =, BYE, QUIT'
INMSG1   DC    CL60'ENTER DSNAME WITHOUT QUOTES - NO LEADING BLANKS'
QNAME    DC    CL8'SYSDSN'
BUFADR   DS    CL133
RNAME    DC    CL45' '
TOKEN    DS    A
MSG1     DS    0CL60
         DC    C'OWNERS='
OWN1     DC    CL8' '
         DC    C',WAIT EXCLUSIVE='
EXCL1    DC    CL8' '
         DC    C',WAIT SHARED='
SHR1     DC    CL8' '
MSG2     DS    0CL43
         DC    C'JOBNAME='
MSG2JBN  DS    CL8
         DC    C',TYPE='
MSG2TYP  DS    CL7
         DC    C',USE='
MSG2USE  DS    CL9
MSGWAIT  DC    CL7'WAITING'
MSGOWN   DC    CL7'OWNER'
MSGEXCL  DC    CL9'EXCLUSIVE'
MSGSHR   DC    CL9'SHARED'
         LTORG
AREA     DS    CL8000
         ISGRIB
         END
