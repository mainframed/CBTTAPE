DAYOWEEK TITLE 'SET RETURN CODE BY DAY OF WEEK'
***********************************************************************
*        PDSDOC *SET RETURN CODE FROM DAY OF WEEK.....................* DAYOWEEK
*                                                                     *
*        PROGRAM ID. :             DAYOWEEK                           *
*                                                                     *
*        DATE WRITTEN :            SEPTEMBER 1992                     *
*                                                                     *
*        AUTHOR :                  D.H.CARTWRIGHT.                    *
*                                                                     *
*        LOCATION :                SPRECHER ENERGIE                   *
*                                  KIRCHWEG 5                         *
*                                  5036 OBERENTFELDEN                 *
*                                  SWITZERLAND                        *
*                                                                     *
*        HISTORY  :                                                   *
*                                                                     *
* 03SEP92 CART1 STARTED WRITING                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*       ---------                                                     *
*                                                                     *
* THIS PROGRAM USES THE 'GETDATE' ROUTINE FROM THE U.S. AIR FORCE VIA *
* THE CBT TAPE TO FIND THE DAY OF THE WEEK. IT USES THIS TO SET THE   *
* PROGRAM RETURN CODE. IF THE DAY IS A PUBLIC HOLIDAY, IT ADDS 10     *
* (TEN) TO THE DAY NUMBER.                                            *
*                                                                     *
* IT IS INTENDED TO ALLOW CONDITIONAL EXECUTION OF STEPS IN JCL.      *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*       ------------                                                  *
*                                                                     *
* THE NUMBERING SCHEME FOR DAYS OF THE WEEK DOES NOT FOLLOW GENERAL   *
* PRACTICE IN THAT MONDAY = DAY 1, SUNDAY = DAY 7. HOWEVER, THIS      *
* ALLOWS US TO MORE EASILY SELECT WORKDAYS BY TESTING FOR A VALUE     *
* LESS THAN 6 (SATURDAY). THIS TEST IS ALSO VALID WHEN TEN IS ADDED   *
* TO THE DAY NUMBER TO INDICATE A PUBLIC HOLIDAY.                     *
*                                                                     *
* THE PROGRAM CALLS 'GETDATE' FOR ITS BASIC DATE CONVERSION, THEN     *
* RE-CONVERTS THE ANSWER. THIS MAY NOT BE WILDLY EFFICIENT, BUT IF    *
* YOU HAVE A STANDARD DATE ROUTINE YOU MIGHT AS WELL USE IT. THEN YOU *
* ONLY HAVE TO WORRY ABOUT SUCH THINGS AS LEAP YEARS IN ONE PROGRAM.  *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*       OUTPUT                                                        *
*       ------                                                        *
*                                                                     *
* THE RETURN CODE IS SET TO INDICATE THE DAY OF THE WEEK, PLUS TEN    *
* ON PUBLIC HOLIDAYS.                                                 *
*                                                                     *
*       INPUT  -  NONE                                                *
*       -----                                                         *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         PRINT ON,GEN              SHOW MACRO EXPANSIONS
*        PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
         SPACE 1
DAYOWEEK CSECT
         SPACE 1
***********************************************************************
*        REGISTER EQUATES AND USAGES.                                 *
***********************************************************************
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
REG2     EQU   R2
R3       EQU   3
R4       EQU   4                   LINKAGE RETURN
R5       EQU   5
R6       EQU   6                   DAY COUNTER
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  PROGRAM BASE
R13      EQU   13                  SAVE AND WORK AREA POINTER
R14      EQU   14
R15      EQU   15
         SPACE 1
         ZCSECT GETMAIN=1,WKLEN=LWORK HOUSEKEEPING
         USING CLOSET,R13          ESTABLISH WORK AREA ADDSESSABILITY
         EJECT
***********************************************************************
*        MAIN PROCESSING ROUTINE                                      *
***********************************************************************
         SPACE 1
MAINLINE DS    0H                  PROGRAM FUNCTION
*
*        SET UP PARAMETERS AND CALL 'GETDATE' TO CHECK FOR HOLIDAY
*
         LA    R1,DATEPALM         POINT TO PARAMETER LIST
         LA    R15,GETDATE         POINTER TO GETDATE CPB
         GOSUB CALLIT              GO INVOKE GETDATE CODE
         SPACE 1
         LA    R4,TABDAY           POINT TO DAY TABLE (REVERSE)
         LA    R6,7                START WITH DAY 7
LPSDAY   DS    0H                  START OF DAY SEARCH LOOP
         CLC   0(3,R4),DAY         TEST DAY TABLE
         BE    LPEDAY              MATCH SO EXIT
         LA    R4,3(,R4)           ELSE UPDATE TABLE POINTER
         BCT   R6,LPSDAY           THEN GO ROUND AGAIN. 0 = ERROR
         SPACE 1
LPEDAY   DS    0H                  END OF DAY SEARCH LOOP
         CLI   HOLIDAY,C'H'        IS IT A HOLIDAY?
         BNE   REFORMED            NO, WE HAVE FINISHED
         LA    R6,10(,R6)          YES, ADD TEN TO SIGNIFY HOLIDAY
         SPACE 2
REFORMED DS    0H                  RETURN TO CALLER
         SPACE 1
         LA    R15,GETDATE         POINTER TO GETDATE CPB
         GOSUB UNCALL              GO REMOVE GETDATE CODE
         SPACE 2
ENDOFPGM DS    0H                  NOW SHUTTING DOWN
         LR    R15,R6              COPY DAY NO. AS RETURN CODE
         ZRETURN RC=(15)           EXIT PROGRAM
         TITLE 'SUB-ROUTINES'
         COPY CALLIT               INCLUDE STANDARD PROGRAM CALL
         TITLE 'DATA AREAS'
BEGSNAP  DS    0D                  SNAP DATA AREAS FOLLOWING
***********************************************************************
*        DATA AREAS - CONSTANTS                                       *
***********************************************************************
         SPACE 1
DATE     DC    CL9' '              AREA FOR DATE
         ORG   DATE                RE-DEFINE FOR ANSWER
CDD      DS    CL2'DD'             DAY
         DS    CL1' '              PADDING
CMMM     DS    CL3'MMM'            MONTH NAME
         DS    CL1' '              PADDING
CYY      DS    CL2'YY'             YEAR
         ORG   ,                   END OF RE-DEFINE
DAY      DS    0CL10               DAY-OF-WEEK RETURNED HERE
         DC    CL9' '              DAY OF WEEK
HOLIDAY  DC    CL1' '              HOLIDAY FLAG ('H')
         SPACE 1
TABDAY   EQU   *                   START OF DAY TABLE
         DC    CL3'SUN'            NAME OF DAY 7
         DC    CL3'SAT'            NAME OF DAY 6
         DC    CL3'FRI'            NAME OF DAY 5
         DC    CL3'THU'            NAME OF DAY 4
         DC    CL3'WED'            NAME OF DAY 3
         DC    CL3'TUE'            NAME OF DAY 2
         DC    CL3'MON'            NAME OF DAY 1
         SPACE 2
GETDATE  CPB   GETDATE             CONTROL BLOCKS FOR 'GETDATE' PROGRAM
         SPACE 1
DATEPALM DC    A(DATE)             GETDATE PARAMETER LIST
         DC    X'80'               VL BIT
         DC    AL3(DAY)            DAYNAME AREA
         TITLE 'LITERAL POOL'
***********************************************************************
*        LITERAL POOL                                                 *
***********************************************************************
         SPACE 1
         LTORG ,                   LITERAL POOL GOES HERE
         SPACE 2
EOSNAP   DS    0H                  END OF STORAGE AREA TO BE SNAPPED
         TITLE 'DSECT MAPS'
***********************************************************************
*        HOUSEKEEPING STORAGE AREA                                    *
***********************************************************************
         SPACE 1
CLOSET   DSECT ,                   KEEP ALL OUR SKELETONS HERE
         DS    18F                 STANDARD SAVE AREA
         RIN   GENERATE            SUB-ROUTINE SAVE AREA
         SPACE 1
LWORK    EQU   *-CLOSET            SIZE OF THE CLOSET
         EJECT
         CPB   DSECT               MAP PROGRAM CONTROL BLOCK
         SPACE 3
         END
