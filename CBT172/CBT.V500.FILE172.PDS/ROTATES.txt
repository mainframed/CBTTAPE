         TITLE '   R O T A T E S '
***********************************************************************
*        PDSDOC *90 DEGREE PRINT ROTATION PROGRAM, VARIABLE PAGE SIZE.* ROTATES
*                                                                     *
*  ROTATES                                                            *
*                                                                     *
***********************************************************************
*                                                                     *
*  WRITTEN BY. BILL GODFREY, PRC (PLANNING RESEARCH CORPORATION).     *
*  A MODIFIED VERSION OF A PROGRAM BY BRUCE E. HOGMAN,                *
*  WHICH HAD ASSEMBLER-H DEPENDENT CODE AND USER MACROS.              *
*  INSTALLATION. AIR FORCE DATA SERVICES CENTER, THE PENTAGON.        *
*  DATE WRITTEN. MARCH 12 1982.                                       *
*  DATE UPDATED. MARCH 15 1982.                                       *
*                                                                     *
*  TIDIED UP AND MODIFIED FOR SMALLER PAGES BY D.H.CARTWRIGHT,        *
*  INTERDATA A.G., 4900 LANGENTHAL, SWITZERLAND. AUGUST 1987          *
*                                                                     *
*  NEW PAGE SIZE LOGIC WRITTEN BY D.H.CARTWRIGHT,                     *
*  SPRECHER ENERGIE  A.G., 5036 OBERENTFELDEN, SWITZERLAND. JAN.1990  *
*  THIS SUPPORTS TWO EXEC PARMS;                                      *
*                                                                     *
*    A) LL=NN TO SPECIFY THE LINE LENGTH                              *
*    B) PL=NN TO SPECIFY THE PAGE LENGTH                              *
*                                                                     *
*  BOTH PARAMETERS REFER TO THE PHYSICAL PAPER, NOT TO THE LOGICAL    *
*  PAGE, WHICH (BEING ROTATED) WOULD REQUIRE THE TWO VALUES TO BE     *
*  EXCHANGED. IN PRACTICE THE VAGARIES OF PRINTER HARDWARE MAKE IT    *
*  PRETTY MUCH TRIAL-AND-ERROR TO FIND THE RIGHT VALUES.              *
*                                                                     *
***********************************************************************
*                                                                     *
*  THIS PROGRAM MANIPULATES THE DATA OF A PRINTED PAGE SO THAT        *
*  IT CAN BE PRINTED ON THE 3800 PRINTER USING A CHARACTER SET        *
*  THAT CONTAINS CHARACTERS ROTATED 90 DEGREES CLOCKWISE.             *
*  FCB=LN12 (A 12-LINE-PER-INCH FCB) AND CHARS=TURN (A ROTATED        *
*  CHARACTER SET) ARE REQUIRED FOR THE SYSOUT FILE.                   *
*  OUTPUT IS REALLY 132 BYTES WIDE, 120 LINES PER PAGE, BUT SINCE     *
*  YOU WILL READ THE PAGE SIDEWAYS IT LOOKS LIKE THE                  *
*  OUTPUT IS 132 LINES PER PAGE, EACH LINE 120 BYTES WIDE, WITH       *
*  EVERY OTHER SKIP-TO-CH1 CHANGED TO SKIP TO LINE 67                 *
*  (OR TWO PAGES OF 66 LINES-PER-PAGE ON EACH SHEET OF PAPER).        *
*                                                                     *
***********************************************************************
         EJECT ,
*        PRINT NOGEN
ROTATES  CSECT
         USING *,R12
         B     @PROLOG-*(,R15)
         DC    AL1(11),CL11'ROTATES'
         DC    CL16' &SYSDATE &SYSTIME '
@SIZE    DC    0F'0',AL1(0),AL3(@DATAL)  SUBPOOL AND LENGTH
@PROLOG  STM   14,12,12(R13)
         LR    R12,R15        BASE REGISTER
         LR    R2,R1               SAVE PARM
         L     R0,@SIZE
         GETMAIN R,LV=(0)
         XC    0(72,R1),0(R1)
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1              STD SAVE AREA LINKAGE COMPLETE
         USING @DATA,R13
         XC    @ZERO(@ZEROL),@ZERO CLEAR SOME FIELDS TO ZEROS
         EJECT ,
***********************************************************************
*        TEST FOR A PARM FIELD AND PROCESS IT                         *
***********************************************************************
         SPACE 2
PARMTEST DS    0H
         L     R1,0(R2)            LOAD ADDRESS OF PARM LENGTH FIELD
         LH    R2,0(R1)            LOAD LENGTH OF PARM FIELD
         LTR   R2,R2               ANYTHING THERE ?
         BZ    NOPARM              NO. BYPASS PARM PROCESSING
         PUSH  PRINT               SAVE PRINT STATUS
         PRINT GEN                 EXPAND MY PRIVATE MACRO
         SCAN  DATA=2(R1),LENGTH=(R2),DELIM=(',','='),RETAREA=SCAN
         POP   PRINT               RESTORE PRINT STATUS
         L     R2,SCAN             LOAD ADDRESS OF FIRST PART
         LTR   R2,R2               NOTHING BUT DELIMITERS ?
         BZ    BADPARM             YES. BETTER TELL HIM ABOUT IT
PARMLOOP XR    R15,R15             CLEAR WORK REGISTER
         IC    R15,4(R2)           LOAD LENGTH OF KEYWORD
         L     R14,4(R2)           LOAD ADDRESS OF KEYWORD
         BAL   R11,TESTKEY         GO CHECK VALIDITY
         BALR  R11,R1              GO PROCESS THE KEYWORD
         L     R2,0(R2)            LOAD ADDRESS OF NEXT ENTRY
         LTR   R2,R2               TEST FOR MORE DATA IN PARM FIELD
         BNZ   PARMLOOP            MORE. SO GO PROCESS IT.
         EJECT
NOPARM   EQU   *
         ICM   R1,B'1111',DEVLGTH  PICK UP LINE LENGTH
         BNZ   *+4+4+4             IF WE HAVE A VALUE, USE IT
         LA    R1,11*8+1           ELSE DEFAULT 11 INCH, 8 PITCH + ASA
         ST    R1,DEVLGTH          SAVE DEFAULT VALUE
         SPACE 1
         ICM   R1,B'1111',DEVPAGE  PICK UP PAGE LENGTH
         BNZ   *+4+4+4             IF WE HAVE A VALUE, USE IT
         LA    R1,8*10             ELSE DEFAULT TO 8 INCH, 10 LPI
         ST    R1,DEVPAGE          SAVE DEFAULT VALUE
         SPACE 1
***********************************************************************
*                                                                     *
*         OPEN THE INPUT AND SYSOUT FILES                             *
*                                                                     *
***********************************************************************
         SPACE 1
         L     R1,DEVLGTH          GET DEVICE WIDTH
         STH   R1,PRTDCB+82        USE AS OUTPUT LRECL
         SRL   R1,1                DIVIDE BY TWO
         ST    R1,HALFPAGE         SAVE HALF PAGE SIZE
         SPACE 1
         OPEN  (INPUTDCB,INPUT,PRTDCB,OUTPUT)
         SPACE 1
***********************************************************************
*                                                                     *
*         DETERMINE TYPE OF INPUT FILE                                *
*                                                                     *
***********************************************************************
         SPACE 1
         MVC   UT1LRECL,INPUTDCB+82
         CLC   UT1LRECL,=H'0'      IS LRECL 0
         BNE   *+10                NO
         MVC   UT1LRECL,INPUTDCB+62  YES, USE BLKSIZE
         SPACE 1
         MVC   UT1RECFM,INPUTDCB+36
         TM    UT1RECFM,X'C0'      IS RECFM U
         BNO   *+8                 NO
         NI    UT1RECFM,255-X'C0'  YES, TURN OFF F AND V BITS
         SPACE 1
         L     R1,DEVPAGE          GET DEVICE LIMITATION
         STH   R1,PRTLGTH          SAVE LINE LENGTH
         TM    UT1RECFM,X'06'      HAS CARRIAGE CONTROL?
         BNZ   GOTLENTH            YES, THIS LENGTH WILL DO
         BCTR  R1,0                DECREASE FOR ASA CHARACTER
         STH   R1,PRTLGTH          SAVE LINE LENGTH
         LA    R1,1(,R1)           ADD BACK ASA CHARACTER
GOTLENTH DS    0H                  NOW KNOW MAX INPUT LENGTH
         M     R0,DEVLGTH          HOW MANY LINES PER PAGE
         ST    R1,BUFFLEN          .. MAKE A BUFFER
         LR    R0,R1
         GETMAIN R,LV=(0)
         ST    R1,@BUFFER
         BAL   R8,CLEARBUF
         MVI   PREV,0
         MVI   BLINE,C' '
         MVC   BLINE+1(L'BLINE-1),BLINE
         EJECT ,
***********************************************************************
*                                                                     *
*         READ A RECORD                                               *
*                                                                     *
***********************************************************************
         SPACE 1
READLOOP EQU   *
         GET   INPUTDCB
         ST    R1,@RECORD
         TM    UT1RECFM,X'40'      IS RECFM V
         BZ    READNOTV            NO, BRANCH
         LH    R0,0(R1) GET RDW
         L     R1,@RECORD
         LA    R1,4(R1)         POINT TO DATA PORTION
         ST    R1,@RECORD
         SH    R0,=H'4'         LESS RDW
         B     READX
         SPACE 1
READNOTV LH    R0,INPUTDCB+82      NO, GET LRECL FOR FB OR U
         LTR   R0,R0               IS RECFM F UNBLOCKED
         BP    READX               NO, BRANCH
         LH    R0,INPUTDCB+62      YES, GET BLKSIZE
         SPACE 1
READX    CH    R0,PRTLGTH          IS RECORD TOO LONG
         BNH   *+4+4               NO
         LH    R0,PRTLGTH          YES, TRUNCATE IT
         ST    R0,RECSIZE          SAVE USABLE RECORD LENGTH
         TM    UT1RECFM,X'02'      MACHINE CC
         BZ    READNOTM            NO
         BAL   R14,CONVERT         YES, CONVERT TO ASA
         LTR   R15,R15             DOES IT CONVERT TO NO-OP
         BNZ   READLOOP            YES, READ ANOTHER RECORD
         SPACE 1
READNOTM DS    0H
         TM    UT1RECFM,X'06'      IS THERE CARRIAGE CONTROL
         BZ    READNOCC            NO, BRANCH
         L     R1,@RECORD          YES,
         CLI   0(R1),C'+'          IS IT OVERPRINT?
         BE    READLOOP            YES, CANNOT DO IT, SO SKIP RECORD
         EJECT ,
***********************************************************************
*                                                                     *
*         ROTATE AND PRINT THE DATA                                   *
*                                                                     *
***********************************************************************
         SPACE 1
READNOCC DS    0H                  FINAL LOOK AT SKIP CHARACTERS
         BAL   R8,ADJLINNO
         TM    UT1RECFM,X'06'      IS THERE CARRIAGE CONTROL
         BZ    CCABS               NO, BRANCH
         L     R1,@RECORD          YES,
         LA    R1,1(R1)             POINT
         ST    R1,@RECORD            PAST IT
         L     R1,RECSIZE             AND
         BCTR  R1,0                    REDUCE
         ST    R1,RECSIZE               LENGTH
CCABS    DS    0H                   DONE WITH CC CHARACTER
         CLC   LINENO,DEVLGTH       WILL IT FIT IN?
         BNH   SAMEBUFR
         BAL   R8,PRINTBUF
         BAL   R8,CLEARBUF
         MVC   LINENO,=F'1'
SAMEBUFR EQU   *
         BAL   R8,MOVEREC
         B     READLOOP
         EJECT ,
***********************************************************************
*                                                                     *
*         SUBROUTINES                                                 *
*                                                                     *
***********************************************************************
         SPACE 1
MOVEREC  EQU   *
         L     R1,@RECORD
         L     R5,RECSIZE
         LTR   R5,R5               TEST RECORD LENGTH
         BZR   R8                  RETURN IF NOTHING THERE
         C     R5,MAXLRECL         SAVE
         BNH   *+8                  MAX
         ST    R5,MAXLRECL           LRECL
         BCTR  R5,0
         AR    R5,R1               POINT TO LAST BYTE FOR BXLE
         LA    R4,1                INCREMENT FOR BXLE
         LA    R3,0(,R1)           FIRST BYTE FOR BXLE
         L     R2,DEVLGTH          GET PAGELENGTH
         S     R2,LINENO           GET LINES REMAINING
         L     R6,@BUFFER          DEST
         AR    R6,R2
MLOOP    MVC   0(1,R6),0(R3)
         A     R6,DEVLGTH
         BXLE  R3,R4,MLOOP
         BR    R8
         SPACE 1
***********************************************************************
         EJECT ,
PRINTBUF EQU   *
         L     R3,BUFFLEN          GET LENGTH OF BUFFER
         L     R2,DEVLGTH          GET LINE LENGTH
         L     R4,@BUFFER
         AR    R3,R4
         BCTR  R3,0                LESS ONE FOR BXLE
PLOOP    PUT   PRTDCB,(R4)
         BXLE  R4,R2,PLOOP
         XC    MAXLRECL,MAXLRECL ZERO MAXLRECL FOR VB FORMAT
         L     R14,RCNTO           ADD TO COUNT OF PAGES OUTPUT
         AL    R14,=F'1'
         ST    R14,RCNTO
         BR    R8
         SPACE 1
***********************************************************************
         EJECT ,
ADJLINNO EQU   *
         L     R1,@RECORD
         L     R2,LINENO
         LA    R0,1(R2)            ADD 1 TO LINENO
         TM    UT1RECFM,X'06'      CC PRESENT
         BZ    ADJX                NO, EXIT
         CLI   0(R1),C' '          SINGLE SPACE 1
         BE    ADJX                YES, EXIT
         LA    R0,2(R2)            ADD 2 TO LINENO
         CLI   0(R1),C'0'          DOUBLE SPACE 1
         BE    ADJX                YES, EXIT
         LA    R0,3(R2)            ADD 3 TO LINENO
         CLI   0(R1),C'-'          TRIPLE SPACE 1
         BE    ADJX                YES, EXIT
         LA    R0,1(R2)            ADD 1 TO LINENO
         CLI   0(R1),C'1'          NEW PAGE
         BNE   ADJX                NO, BRANCH
         L     R14,RCNTI            ADD TO COUNT OF INPUT PAGES
         LA    R14,1(,R14)      ADD ONE
         ST    R14,RCNTI        SAVE COUNT
         LA    R0,999           CAUSE PAGE OVERFLOW
         CLI   PGMSW,4          SQUISH
         BNE   ADJX             NO, BRANCH - HONOURS PAGE SKIPS
         C     R2,HALFPAGE      IF
         BNL   ADJX               GE HALF A PAGE
         L     R0,HALFPAGE      MAKE IT HALF
         LTR   R2,R2            IF
         BNZ   ADJX               NOT FIRST TIME
         LA    R0,1             FIRST TIME MAKE IT 1
ADJX     ST    R0,LINENO
         BR    R8
         SPACE 1
***********************************************************************
         EJECT ,
CLEARBUF L     R14,@BUFFER
         L     R15,BUFFLEN
         XR    R0,R0
         L     R1,=A(X'40000000')
         MVCL  R14,R0
         L     R1,@BUFFER
         MVI   0(R1),C'1'
         BR    R8
         EJECT ,
***********************************************************************
*                                                                     *
*         CONVERT CARRIAGE CONTROLS TO ASA                            *
*                                                                     *
***********************************************************************
*
*               ANY RECORD NOT CONTAINING A MACHINE CC WILL BE
*               COPIED WITH ITS OWN CC, EXCEPT:
*                   IF IT IS THE FIRST RECORD OF ITS TYPE
*                   AND IS A SINGLE-SPACE IT WILL BE CHANGED TO
*                   SKIP-TO-CHANNEL 1.
*
*               A RECORD CONTAINING A MACHINE CC (WRITE) WILL BE
*               COPIED WITH AN ASA BASED ON THE PREVIOUS MACHINE CC.
*               IF THE RECORD WAS NOT PRECEDED WITH A MACHINE CC
*               RECORD, IT IS WRITTEN WITH SKIP-TO-CHANNEL 1.
*
*               A RECORD CONTAINING A MACHINE CC (IMMEDIATE) WILL
*               NOT BE COPIED, BUT ITS CC WILL DETERMINE THE CC
*               OF THE NEXT RECORD (UNLESS NEXT IS ASA OR IMM).
*
*               IF A MACHINE WRTSP1 OR IMMSP1 IS FOLLOWED BY
*               A MACHINE IMMEDIATE SKIP, THE SPACE 1 WILL BE
*               DROPPED.  THIS IS BECAUSE A BLANK LINE WOULD
*               HAVE TO BE WRITTEN, AND IT IS PROBABLY UNNECESSARY.
*               IF THIS EVER CAUSES A PROBLEM, IT IS EASY TO REMOVE
*               THIS LOGIC AND WRITE THE BLANK LINE.  JUST REMOVE
*               THE GROUP OF INSTRUCTIONS MARKED '-OPT-'
*
*               CONDITIONS NOT PROPERLY HANDLED (BUT INSIGNIFICANT).
*                  IMMEDIATE FOLLOWED BY ASA OR EOF.
*                     (IGNORED)
*                  WRITE FOLLOWED BY ASA OR EOF.
*                     (SP/SK IGNORED)
*
CONVERT  MVC   HPREV,PREV
         L     R8,@RECORD
         TRT   0(1,R8),TABMACH     MACHINE CC
         BZ    ASA                 NO, BRANCH
*
*               MACHINE CC
*
         STC   R2,PREV             STORE ASA FOR NEXT RECORD
         TM    0(R8),X'02'         IS IT IMMEDIATE
         BZ    PUTP                NO, ITS A WRITE
         CLI   HPREV,0             IS THERE AN OUTSTANDING ACTION
         BE    NOPUT               NO, BRANCH
         CLI   HPREV,C'+'          WAS PREV WRTSP0 OR IMMSP0
         BE    NOPUT               YES, BRANCH
         CLI   HPREV,C' '   -OPT-  WAS PREV WRTSP1 OR IMMSP1
         BNE   PREVSIG      -OPT-  NO, ITS PROBABLY SIGNIFICANT
         TM    0(R8),X'80'  -OPT-  IS THIS A SKIP, NOT A SPACE 1
         BO    NOPUT        -OPT-  ITS A SKIP, SO PREV CAN BE IGNORED
PREVSIG  MVC   BLINE(1),HPREV      WRITE A BLANK LINE WITH HPREV
         LA    R8,BLINE
         ST    R8,@RECORD
         MVC   RECSIZE,=F'2'       IN CASE RECFM V
         SR    R15,R15             WRITE THE LINE POINTED TO BY @RECORD
         BR    R14
NOPUT    LA    R15,4               DO NOT WRITE A LINE
         BR    R14
         SPACE 1
PUTP     MVC   0(1,R8),HPREV
         CLI   0(R8),0             IS THERE A CC BASED ON PREV RECORD
         BNE   PUT                 YES, GO USE IT
*
*               WE'VE HIT A MACHINE WRT CC NOT PRECEDED BY
*               A MACHINE CC.
*               MAKE IT SKIP TO CHANNEL 1.
*
         MVI   0(R8),C'1'
         B     PUT
*
*               ASA CC
*
ASA      CLI   PREV,0              WAS PREVIOUS RECORD MACHINE
         BE    PUT                 NO, BRANCH
         MVI   PREV,0
*
*               WE'VE HIT AN ASA RECORD NOT PRECEDED BY AN ASA RECORD.
*
*               IF IT'S A SINGLE SPACE, CHANGE IT TO SKIP TO CH 1.
*
         CLI   0(R8),C' '
         BNE   PUT
         MVI   0(R8),C'1'
PUT      SR    R15,R15
         BR    R14
         EJECT ,
***********************************************************************
*                                                                     *
*         TERMINATE                                                   *
*                                                                     *
***********************************************************************
         SPACE 1
EOF      CLC   LINENO,=F'1'        ANYTHING TO PRINT
         BNH   *+8                 NO, SKIP PRINTBUF
         BAL   R8,PRINTBUF         PURGE PRINT BUFFER
         CLOSE (INPUTDCB,,PRTDCB)  CLOSE BOTH DCBS
         L     R0,RCNTI            COUNT OF INPUT LINES CC1=C'1'
         CVD   R0,DOUBLE           CONVERT TO DECIMAL
         OI    DOUBLE+7,X'0F'      MAKE PRINTABLE
         UNPK  PRCNTI,DOUBLE       UNPACK TO WTO MESSAGE
         L     R0,RCNTO            DO SAME FOR OUTPUT PAGE COUNT
         CVD   R0,DOUBLE           CONVERT TO DECIMAL
         OI    DOUBLE+7,X'0F'      MAKE PRINTABLE
         UNPK  PRCNTO,DOUBLE       UNPACK TO WTO MESSAGE
         WTO   MF=(E,CNTWTO)
         ORG   *-2                 DONT WTO
         NOPR  0                   DONT WTO
         L     R0,BUFFLEN          PICK UP BUFFER LENGTH
         L     R1,@BUFFER          ADDRESS FOR FREEMAIN
         FREEMAIN R,LV=(0),A=(1)
         L     R15,RCNTO           RETURN COUNT OF PAGES OUTPUT
         LR    R1,R13              ADDRESS FOR FREEMAIN
         L     R0,@SIZE            LENGTH FOR FREEMAIN
         L     R13,4(R13)
         LR    R2,R15
         FREEMAIN R,LV=(0),A=(1)
         LR    R15,R2              RETURN COUNT OF PAGES OUTPUT
         RETURN (14,12),RC=(15)
         EJECT ,
***********************************************************************
*        CHECK THE VALIDITY OF KEYWORDS IN THE PARM FIELD             *
***********************************************************************
         SPACE 2
TESTKEY  DS    0H
         LA    R0,WORKKEY          START ADDRESS OF KEYWORD WORK AREA
         LA    R1,L'WORKKEY        LENGTH OF WORK AREA
         ICM   R15,8,=C' '         LOAD PADDING CHARACTER FOR MOVE
         MVCL  R0,R14              MOVE PADDED KEY WORD TO WORK AREA
         LA    R1,PARMTABL         LOAD START ADDRESS OF KEYWORD TABLE
         LA    R6,PARMLEN          LENGTH OF A PARMTAB ENTRY
         LA    R7,PARMEND-1        END ADDRESS FOR BXLE LOOP
KEYLOOP  CLC   WORKKEY,0(R1)       VALID KEYWORD FOUND ?
         BE    GOTAKEY             YES. GO LOAD ADDRESS OF PROCESSOR
         BXLE  R1,R6,KEYLOOP       NO.  TRY NEXT ENTRY IN TABLE
         B     BADPARM             TROUBLE. KEYWORD NOT FOUND
GOTAKEY  L     R1,L'KEYNAME(R1) LOAD ADDRESS OF PROCESSOR
         BR    R11                 RETURN TO CALLER
         EJECT
***********************************************************************
*        SUPPLY DEFAULT RECORD LENGTH AND/OR BLOCK SIZE               *
*         IF NOT CODED.                                               *
***********************************************************************
         SPACE 1
BLKSZRTN SUBIN ,                   SUPPLY DEFAULT LRECL AND/OR BLKS
         USING IHADCB,R1           ESTABLISH DCB ADDRESSABILITY
         LH    R4,DCBLRECL         GET LRECL VALUE
         LTR   R4,R4               ANY SUPPLIED?
         BNZ   *+4+4+4             YES, SKIP SETTING DEFAULT
         LA    R4,80               FORCE LRECL TO 80
         STH   R4,DCBLRECL         SET DEFAULT
         CH    R4,DCBBLKSI         HOW ABOUT BLOCKSIZE
         BNH   0(R14)              YES, RETURN TO OPEN
         SLL   R4,3                DEFAULT BLOCK IN EIGHTS
         STH   R4,DCBBLKSI         SET DEFAULT
         SUBOUT RETURN=(R14)       RETURN TO OPEN ROUTINES
         SPACE 1
         DROP  R1                  END DCB ADDRESSING
         SPACE 2
***********************************************************************
*        ERROR ROUTINES                                               *
***********************************************************************
         SPACE 2
BADPARM  DS    0H
         WTO   '**** R O T A T E S ****  SORRY. BUT YOUR PARM FIELD IS X
               INVALID AND PROCESSING IS TERMINATED',ROUTCDE=11
         B     NOPARM              END PARM PROCESSING
         EJECT
***********************************************************************
*        PROCESSORS FOR PARM KEYWORDS                                 *
***********************************************************************
         SPACE 2
PARMPL   DS    0H                  PAGE LENGTH
         L     R2,0(R2)            LOAD ADDRESS OF OPERAND FIELD
         LTR   R2,R2               HAS HE FORGOTTEN TO CODE IT ?
         BZ    BADPARM             YES. FORGET EVERYTHING AND QUIT
         XR    R15,R15             CLEAR LENGTH REGISTER
         IC    R15,4(R2)           LOAD LENGTH OF OPERAND
         L     R14,4(R2)           LOAD ADDRESS OF FIELD
         BCT   R15,*+10            SUBTRACT 1 FROM LENGTH
         PACK  DBLWORD,0(0,R14)    PACK THE NUMBER TO WORK AREA
         EX    R15,*-6             EXECUTE THE PACK INST
         CVB   R8,DBLWORD          CONVERT THE NUMBER TO BINARY
         ST    R8,DEVPAGE          SAVE VALUE
         BR    R11                 RETURN TO CALLER
         SPACE 3
PARMLL   DS    0H                  LINE LENGTH
         L     R2,0(R2)            LOAD ADDRESS OF OPERAND FIELD
         LTR   R2,R2               HAS HE FORGOTTEN TO CODE IT ?
         BZ    BADPARM             YES. FORGET EVERYTHING AND QUIT
         XR    R15,R15             CLEAR LENGTH REGISTER
         IC    R15,4(R2)           LOAD LENGTH OF OPERAND
         L     R14,4(R2)           LOAD ADDRESS OF FIELD
         BCT   R15,*+10            SUBTRACT 1 FROM LENGTH
         PACK  DBLWORD,0(0,R14)    PACK THE NUMBER TO WORK AREA
         EX    R15,*-6             EXECUTE THE PACK INST
         CVB   R3,DBLWORD          CONVERT THE NUMBER TO BINARY
         ST    R3,DEVLGTH          SAVE VALUE
         BR    R11                 RETURN TO CALLER
         EJECT
SPLIT    DS    0H                  PUT TWO PAGES PER PHYSICAL PAGE
         MVI   PGMSW,4
         BR    R11                 RETURN TO CALLER
         EJECT ,
***********************************************************************
*                                                                     *
*         CONSTANTS AND WORKAREA                                      *
*                                                                     *
***********************************************************************
         SPACE 1
         LTORG ,
         SPACE 1
         PRINT NOGEN
         SPACE 1
INPUTDCB DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=GL,EODAD=EOF,EROPT=ACC
         SPACE 1
PRTDCB   DCB   DDNAME=SYSUT2,DSORG=PS,MACRF=PM,                        +
               EXLST=EXITLIST,                                         +
               RECFM=FBA,LRECL=0
         SPACE 1
EXITLIST DC    X'85',AL3(BLKSZRTN) ENTRY FOR DCB MODIFICATION
         SPACE 3
         PRINT GEN
         SPACE 1
CNTWTO   WTO   'SFI1005I ROTATED PAGES IN=00000, OUT=00000',ROUTCDE=11,+
               MF=L
PRCNTI   EQU   CNTWTO+30,5,C'C'
PRCNTO   EQU   CNTWTO+41,5,C'C'
         SPACE 1
YES      EQU   C'Y'
NO       EQU   C'N'
         SPACE 2
DBLWORD  DS    D                   WORK AREA FOR ARITHMETIC
WORKKEY  DS    CL(L'KEYNAME)       WORK AREA FOR PARM KEYWORDS
PARMTABL DS    0F                  PARM FIELD KEYWORD TABLE
KEYNAME  DC    CL8'PL'             FIRST KEYWORD
         DC    AL4(PARMPL)         ADDRESS OF PROCESSOR
PARMLEN  EQU   *-PARMTABL          LENGTH OF AN ENTRY
         DC    CL8'LL'             SECOND KEYWORD
         DC    AL4(PARMLL)         ADDRESS OF PROCESSOR
         DC    CL8'S'              KEYWORD
         DC    AL4(SPLIT)          ADDRESS OF PROCESSOR
PARMEND  EQU   *                   END OF PARM KEYWORD TABLE
         SPACE 3
***********************************************************************
*         PAPER DEPENDANT CONSTANTS                                   *
***********************************************************************
         SPACE 1
DEVLGTH  DC    F'0'                DEVICE LINE LENGTH, INCLUDING ASA
DEVPAGE  DC    F'0'                DEVICE PAGE LENGTH
         EJECT ,
         DC    0D'0'
TABMACH  DC    256X'00'
         ORG   TABMACH+X'01'       WRT SP0
         DC    C'+'
         ORG   TABMACH+X'09'       WRT SP1
         DC    C' '
         ORG   TABMACH+X'11'       WRT SP2
         DC    C'0'
         ORG   TABMACH+X'19'       WRT SP3
         DC    C'-'
         ORG   TABMACH+X'89'       WRT SK1
         DC    C'1'
         ORG   TABMACH+X'91'       WRT SK2
         DC    C'2'
         ORG   TABMACH+X'99'       WRT SK3
         DC    C'3'
         ORG   TABMACH+X'A1'       WRT SK4
         DC    C'4'
         ORG   TABMACH+X'A9'       WRT SK5
         DC    C'5'
         ORG   TABMACH+X'B1'       WRT SK6
         DC    C'6'
         ORG   TABMACH+X'B9'       WRT SK7
         DC    C'7'
         ORG   TABMACH+X'C1'       WRT SK8
         DC    C'8'
         ORG   TABMACH+X'C9'       WRT SK9
         DC    C'9'
         ORG   TABMACH+X'D1'       WRT SK10
         DC    C'A'
         ORG   TABMACH+X'D9'       WRT SK11
         DC    C'B'
         ORG   TABMACH+X'E1'       WRT SK12
         DC    C'C'
         ORG   TABMACH+X'03'       IMM SP0 (NO-OP)
         DC    C'+'
         ORG   TABMACH+X'0B'       IMM SP1
         DC    C' '
         ORG   TABMACH+X'13'       IMM SP2
         DC    C'0'
         ORG   TABMACH+X'1B'       IMM SP3
         DC    C'-'
         ORG   TABMACH+X'8B'       IMM SK1
         DC    C'1'
         ORG   TABMACH+X'93'       IMM SK2
         DC    C'2'
         ORG   TABMACH+X'9B'       IMM SK3
         DC    C'3'
         ORG   TABMACH+X'A3'       IMM SK4
         DC    C'4'
         ORG   TABMACH+X'AB'       IMM SK5
         DC    C'5'
         ORG   TABMACH+X'B3'       IMM SK6
         DC    C'6'
         ORG   TABMACH+X'BB'       IMM SK7
         DC    C'7'
         ORG   TABMACH+X'C3'       IMM SK8
         DC    C'8'
         ORG   TABMACH+X'CB'       IMM SK9
         DC    C'9'
         ORG   TABMACH+X'D3'       IMM SK10
         DC    C'A'
         ORG   TABMACH+X'DB'       IMM SK11
         DC    C'B'
         ORG   TABMACH+X'E3'       IMM SK12
         DC    C'C'
         ORG
         TITLE 'DSECT MAPS'
         PUSH  PRINT               SAVE PRINT STATUS
         PRINT ON,NOGEN            DO NOT EXPAND SYSTEM DSECTS
         SPACE 3
         DCBD  DSORG=PS,DEVD=DA    DCB DSECT
         POP   PRINT               RESTORE PRINT STATUS
@DATA    DSECT
         DS    18F
@ZERO    DS    0D
DOUBLE   DS    D                   DOUBLE WORD WORK AREA
RCNTI    DS    F                   COUNT OF PAGES INPUT
RCNTO    DS    F                   COUNT OF PAGES ACTUALLY PRINTED
PGMSW    DS    F                   SWITCH
@BUFFER  DS    A                   ADDRESS OF ROTATED BUFFER
@RECORD  DS    A                   ADDRESS OF INPUT RECORD
BUFFLEN  DS    A                   LENGTH OF GOTTEN AREA
LINENO   DS    A                   NUMBER OF LINE OF OUTPUT
RECSIZE  DS    A                   RECORD LENGTH (UP TO 120)
*                                  (BECOMES NUMBER OF LINES)
MAXLRECL DS    A                   MAXLRECL FOR VB INPUT/PAGE
HALFPAGE DS    A                   LENGTH OF HALF A PAGE
PREV     DS    C
HPREV    DS    C
UT1RECFM DS    X
UT1LRECL DS    H
PRTLGTH  DS    H                   LENGTH OF OUTPUT LINE
SCAN     DS    20F                 WORK AREA FOR SCAN MACRO
@ZEROL   EQU   *-@ZERO             LENGTH OF AREA TO BE ZERO'D
BLINE    DS    CL120
         DS    0D
@DATAL   EQU   *-@DATA
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
REG2     EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
