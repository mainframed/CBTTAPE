***********************************************************************
*        PDSDOC *CONVERT RMF INTERVAL TO BINARY HUNDREDTHS OF SECOND  * RMFCHUND
*                                                                     *
* ON ENTRY R1 -> 4 BYTE PACKED INTERVAL IN FORM MMSSTTTF              *
* ON EXIT  THIS FIELD HAS BEEN CONVERTED TO BINARY HUNDREDTHS OF SECS.*
*                                                                     *
* THE FIELD IS ROUNDED IN PLACE TO FACILITATE USE OF THE 'PACK2'      *
* CONVERSION ROUTINE. USE OF THE 'SHIFT AND ROUND DECIMAL' INSTRUCTION*
* WOULD BE NEATER, BUT WOULD INVOLVE ANOTHER WORK AREA. NOTE THAT WE  *
* CAN ALLOW SECONDS TO ROUND UP TO THE ILLOGICAL VALUE '60', BUT THEY *
* WILL NEVER ROLL OVER INTO THE MINUTES FIELD.                        *
*                                                                     *
***********************************************************************
         SPACE 1
RMFCHUND DS    0H                  CONVERT INTERVAL FIELD
         SUBIN SAVE=(R1,R4,R15)    SUBROUTINE ENTRY
         AP    1(3,R1),=PL1'5'     ROUND THOUSANDTHS
         SR    R15,R15             ZERO ACCUMULATOR
         GOSUB PACK2               GO PACK MINUTES
         LR    R15,R0              COPY MINUTES
         MH    R15,=H'60'          CONVERT TO SECONDS
         LA    R1,1(,R1)           POINT TO SECONDS
         GOSUB PACK2               GO CONVERT IT
         AR    R15,R0              ACCUMULATE SECONDS
         MH    R15,=H'100'         CONVERT TO HUNDREDTHS
         LA    R1,1(,R1)           POINT TO HUNDREDTHS
         GOSUB PACK2               GO CONVERT IT
         AR    R15,R0              ACCUMULATE ANSWER
         SUBLR R1                  RESTORE INPUT POINTER
         STCM  R15,B'1111',0(R1)   SAVE ANSWER IN INPUT FIELD
         SPACE 1
RMFCHUNX DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R1,R4,R15) SUBROUTINE EXIT
