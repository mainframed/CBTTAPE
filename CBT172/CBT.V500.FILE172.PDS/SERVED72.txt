SERVED72 TITLE 'S.E. RMF VARIABLE EXTRACTED DATA, TYPE 72'
***********************************************************************
*        PDSDOC *CREATE SUMMARY RECORDS FROM SMF72 DATA...............* SERVED72
*                                                                     *
*        PROGRAM ID. :             SERVED72                           *
*                                                                     *
*        AUTHOR :                  D.H.CARTWRIGHT.                    *
*                                                                     *
*        LOCATION :                SPRECHER ENERGIE                   *
*                                  KIRCHWEG 5                         *
*                                  5036 OBERENTFELDEN                 *
*                                  SWITZERLAND                        *
*                                                                     *
*        HISTORY  :                                                   *
*                                                                     *
* 15JUN95 CARTW18 CORRECT LENGTH OF PG PERIOD DATA SECTION            *
* 12JUN95 CARTW18 COPY SMF72FG1 FLAG                                  *
* 12MAY95 CARTW18 CHECK FOR SUBTYPE 1                                 *
* 16AUG90 CART1 WROTE RMF72SUM TO EXTRACT SMF72 DATA                  *
* 16JAN92 CART1 CHANGED FORMAT OF SUMMARY RECORDS, RENAMED PROGRAM    *
* 04FEB92 CART1 EXTENDED RECORDS WITH TRANSACTION DATA, REFORMAT INTVL*
*               MAKE MORE USE OF STANDARD CODE, LIKE SERVED71         *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*       ---------                                                     *
*                                                                     *
* THIS PROGRAM WILL EXTRACT DATA FROM SMF 72 RECORDS (RMF WORKLOAD    *
* ACTIVITY) AND WILL WRITE IT OUT IN MULTIPLE FIXED LENGTH RECORDS    *
* SUITABLE FOR ANALYSIS BY SIMPLE PROGRAMS SUCH AS 'PARROT' OR        *
* CA/EARL. THE PERFORMANCE GROUP PERIOD DATA IS DE-BLOCKED BY THIS    *
* VERSION OF THE PROGRAM.                                             *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*       ------------                                                  *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*       INPUT                                                         *
*       -----                                                         *
*                                                                     *
* 1)SYSUT1 - INPUT SMF RECORDS                                        *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*       OUTPUT                                                        *
*       ------                                                        *
*                                                                     *
* 1)SYSUT2 - SUMMARY RECORDS. THESE ARE ALWAYS THE SAME LENGTH, BUT   *
*            MAY BE WRITTEN AS VB RECORDS IF DESIRED.                 *
*                                                                     *
***********************************************************************
         EJECT
*        PRINT ON,GEN              SHOW MACRO EXPANSIONS
         PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
         SPACE 1
SERVED72 CSECT
         SPACE 1
***********************************************************************
*        REGISTER EQUATES AND USAGES.                                 *
***********************************************************************
         SPACE 1
R0       EQU   0
R1       EQU   1                   WORK
R2       EQU   2                   DCB TABLE POINTER
REG2     EQU   R2                  WORK REGISTER IN 'SCAN' MACRO
R3       EQU   3                   WORK
R4       EQU   4                   LINKAGE RETURN
R5       EQU   5                   WORK
R6       EQU   6                   SMF 72 SEGMENT POINTER - BASE
R7       EQU   7                   WORK
R8       EQU   8                   PRODUCT SECTION BASE
R9       EQU   9                   WORKLOAD CONTROL BASE
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13                  SAVE AREA / BASE REG. 1
R14      EQU   14                  WORK
R15      EQU   15
         SPACE 1
         PGMIN EQUATES=NO,USING=(R13) STANDARD HOUSEKEEPING
         SPACE 1
         L     R5,0(R1)            GET ADDR OF PARM FIELD (NOT USED)
         GOSUB OPENEM              GO OPEN FILES
         EJECT
***********************************************************************
*        MAIN PROCESSING ROUTINE                                      *
***********************************************************************
         SPACE 1
MAINLINE DS    0H                  PROGRAM FUNCTION
         BAL   R4,SYSUT1RD         GO READ A RECORD
         USING SMF72HDR,R6         ESTABLISH SMF RECORD ADDRESSABILITY
         CLI   SMF72RTY,72         IS THIS A TYPE 72 RECORD?
         BNE   MAINLINE            NO, IGNORE IT
         CLI   SMF72STY+1,1        IS THIS A SUB-TYPE 1?
         BNE   MAINLINE            NO, IGNORE IT
         SPACE 1
         MVC   SE72TIM,SMF72TME    COPY SMF RECORD TIME ..
         MVC   SE72DAY,SMF72DTE    .. DATE ..
         MVC   SE72SID,SMF72SID    .. AND SYSTEM ID
         L     R1,SMF72PRS         GET RMF PRODUCT SECTION OFFSET
         LA    R8,0(R6,R1)         GET RMF PRODUCT SECTION ADDRESS
         USING SMF72MFV,R8         ESTABLISH PRODUCT ADDRESSABILITY
         ZAP   SE72IST,SMF72IST    GET INTERVAL START TIME
         ZAP   SE72DAT,SMF72DAT    GET INTERVAL START DATE
         ZAP   SE72INT,SMF72INT    GET INTERVAL DURATION
         ZAP   SE72BINT,SMF72INT   COPY INTERVAL DURATION AGAIN
         LA    R1,SE72BINT         POINT AT PACKED DURATION
         GOSUB RMFCHUND            GO CONVERT IT TO BINARY HUNDREDTHS
         ZAP   SE72ITU,SMF72INT    COPY INTERVAL DURATION AGAIN
         LA    R1,SE72ITU          POINT AT PACKED DURATION
         GOSUB RMFCTU              GO CONVERT IT TO TIMER UNITS
         DROP  R8                  END PRODUCT SECTION ADDRESSABILITY
         SPACE 1
         L     R1,SMF72WLS         GET RMF WORKLOAD SECTION OFFSET
         LA    R9,0(R6,R1)         GET RMF WORKLOAD SECTION ADDRESS
         USING SMF72CTL,R9         ESTABLISH WORKLOAD ADDRESSABILITY
         MVC   SE72PGN,SMF72SUB    COPY PERFORMANCE GROUP NUMBER
         MVC   SE72FG1,SMF72FG1    COPY FLAGS
         DROP  R9                  END WORKLOAD SECTION ADDRESSABILITY
         SPACE 1
         L     R1,SMF72PGS         GET OFFSET TO PERIOD DATA SECTION
         LA    R7,0(R6,R1)         GET ADDRESS OF PGN SECTION
         USING SMF72TTX,R7         ESTABLISH PG DATA ADDRESSABILITY
         SR    R5,R5               CLEAR COUNTER
         LR    R10,R5              SET PERIOD COUNTER
         ICM   R5,B'0011',SMF72PGN GET NUMBER OF PG SECTIONS
         SPACE 1
LOOPPGN  DS    0H                  RUN THROUGH THE PG SECTIONS
         BAL   R4,PGNPROC          GO PROCESS A PERFORMANCE GROUP
         LA    R7,LNG72PGN(,R7)    UPDATE PGN POINTER
         BCT   R5,LOOPPGN          GO THROUGH ALL SECTIONS
         SPACE 1
         B     MAINLINE            IN ALL THE RECORDS
         EJECT
***********************************************************************
*        END OF PROCESSING. TIDY UP, REPORT AND LEAVE.                *
***********************************************************************
         SPACE 1
STOPPGM  DS    0H                  NOW SHUTTING DOWN
*        GOSUB REPORT              GO ISSUE FINAL REPORT
         SPACE 3
         GOSUB CLOSEM              GO CLOSE FILES
         SPACE 2
ENDOFPGM DS    0H                  NOW SHUTTING DOWN
         PGMOUT RC=RETCODE         EXIT PROGRAM
         TITLE 'PERFORMANCE GROUP PROCESSING'
***********************************************************************
*        CREATE A SUMMARY RECORD FOR EACH PERFORMANCE GROUP           *
***********************************************************************
         SPACE 1
PGNPROC  DS    0H                  PROCESS PERFORMANCE GROUP DATA
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         LA    R10,1(,R10)         UPDATE PERIOD COUNTER
         STC   R10,SE72PRD         STORE PERIOD COUNTER
         MVC   SE72SER,SMF72SER    COPY TOTAL SERVICE UNITS
         MVC   SE72MTS,SMF72MTS    COPY MSO SERVICE UNITS
         MVC   SE72ITS,SMF72ITS    COPY I/O SERVICE UNITS
         MVC   SE72CTS,SMF72CTS    COPY CPU SERVICE UNITS
         MVC   SE72CDN,SMF72CDN    COPY DOMAIN NUMBER
         MVC   SE72STS,SMF72STS    .. SRB SERVICE
         MVC   SE72TTX,SMF72TTX    .. TERMINATED TRANSACTIONS
         MVC   SE72ACT,SMF72ACT    .. TRANSACTION ACTIVE TIEM
         MVC   SE72TTM,SMF72TTM    .. TRANSACTION ELAPSED TIME
         MVC   SE72TAT,SMF72TAT    .. TRANSACTION RESIDENCY TIME
         MVC   SE72SPP,SMF72SPP    .. NO. SWAP SEQUENCES
         MVC   SE72PIN,SMF72PIN    .. PAGE INS
         MVC   SE72AFT,SMF72FT1    .. ACTIVE FRAME TIME
         SPACE 1
         LA    R0,SE72REC          POINT TO OUTPUT RECORD
         LA    R2,SYSUT2SW         POINT TO OUTPUT MDCB
         USING MDCB,R2             ESTABLISH ADDRESSABILITY
         GOSUB WRITING             GO OUTPUT RECORD
         SPACE 1
PGNPROCX DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R4)       SUBROUTINE EXIT
         TITLE 'SUB-ROUTINES'
***********************************************************************
*        READ SUBROUTINE FOR SYSUT1                                   *
***********************************************************************
         SPACE 1
SYSUT1RD DS    0H                  READ A SUMMARY RECORD
         SUBIN ,                   SUBROUTINE ENTRY
         LA    R2,SYSUT1SW         POINT TO INPUT MDCB
         TM    MDCBFLAG,DDINT+DDPRES+DDOPEN TEST IF NEED TO READ
         BNO   SYSUT1RX            BRANCH IF NO
         GET   SYSUT1              READ IN A SUMMARY RECORD
         LR    R6,R1               REMEMBER ITS ADDRESS
         AP    MDCBCNT,ONE         INCREMENT RECORD COUNTER
         SPACE 1
SYSUT1RX DS    0H                  RETURN TO CALLER
         SUBOUT ,                  SUBROUTINE EXIT
         SPACE 1
         DROP  R2                  END MDCB ADDRESSABILITY
         EJECT ,
         COPY  RMFCHUND            USE STANDARD CODE FOR RMF INTERVAL
         EJECT ,
         COPY  RMFCTU              USE STANDARD CODE FOR RMF INTERVAL
         EJECT ,
         COPY  PACK2               USE STANDARD CODE TO UNPACK A BYTE
         EJECT ,
         COPY  WRITING             USE STANDARD CODE FOR OUTPUT
         EJECT ,
         COPY  PUTMOVE             USE STANDARD CODE FOR QSAM
         EJECT ,
         COPY  OPENEM              USE STANDARD CODE FOR OPEN LOOP
         EJECT ,
         COPY  CLOSEM              USE STANDARD CODE FOR CLOSE LOOP
         EJECT ,
         COPY  OPENCLOS            USE STANDARD CODE FOR OPEN/CLOSE
         EJECT ,
         COPY  JFCBREAD            USE STANDARD CODE FOR READJFCB
         EJECT ,
         COPY  TESTDD              USE STANDARD CODE FOR DD TEST
         EJECT ,
         COPY  BLKSZRTN            USE STANDARD CODE FOR OPEN EXIT
         TITLE 'DATA AREAS'
BEGSNAP  DS    0D                  SNAP DATA AREAS FOLLOWING
***********************************************************************
*        CONSTANTS AND WORK AREAS                                     *
***********************************************************************
         SPACE 1
DWORK    DC    D'0'                DOUBLEWORD WORK AREA
ONE      DC    PL1'1'              COUNT INCREMENT
SIGN     EQU   X'F0'               POSITIVE PACKED SIGN
         SPACE 1
COMPCODE DC    H'0'                PROGRAM COMPLETION CODE
ZEROWORD DC    F'0'                A LOT OF NOTHING
RETCODE  DC    H'0'                MACRO RETURN CODE SAVE AREA
SNAPID   DC    X'00'               FOR IDENTIFYING ORIGIN OF SNAP
         TITLE 'DCB''S AND I/O CONTROL TABLES'
***********************************************************************
*        DCB'S TABLE                                                  *
***********************************************************************
         SPACE 1
DCBTABLE DS    0F
SYSUT1SW DC    X'00',AL3(SYSUT1),CL8'SYSUT1',CL8'INPUT',CL8'LEAVE'
CNTSUT1  DC    PL8'0'              INPUT RECORD COUNT FIELD
DCBNTRY  EQU   *-DCBTABLE          LENGTH OF ONE TABLE ELEMENT
SYSUT2SW DC    X'00',AL3(SYSUT2),CL8'SYSUT2',CL8'OUTPUT',CL8' '
CNTSUT2  DC    PL8'0'              OUTPUT RECORD COUNT FIELD
SNAPSW   DC    X'00',AL3(SNAPDCB),CL8'SNAP',CL8'OUTPUT',CL8' '
         DC    PL8'0'              RECORD COUNT FIELD
OPENSIZE EQU   *-DCBTABLE          TABLE SIZE OF FULL TIME FILES
         SPACE 1
MAXOPEN  EQU   OPENSIZE/DCBNTRY    MAXIMUM NUMBER OF DCB'S TO OPEN
TABSIZE  EQU   *-DCBTABLE          DCB'S TABLE SIZE
MAXDS    EQU   TABSIZE/DCBNTRY     MAXIMUM NUMBER OF DCB'S
         SPACE 2
EXITLIST DC    X'05',AL3(BLKSZRTN) ENTRY FOR DCB MODIFICATION
         DC    X'87',AL3(IHAJFCB)  ENTRY FOR READJFCB
         EJECT
***********************************************************************
*        DATA CONTROL BLOCKS                                          *
***********************************************************************
         SPACE 1
SYSUT2   DCB   DDNAME=SYSUT2,                                          *
               DSORG=PS,                                               *
               EXLST=EXITLIST,                                         *
               MACRF=PM,                                               *
               LRECL=SE72RECL
         SPACE 3
SYSUT1   DCB   DDNAME=SYSUT1,                                          *
               DSORG=PS,                                               *
               EODAD=STOPPGM,                                          *
               EXLST=EXITLIST,                                         *
               MACRF=GL
         SPACE 2
SNAPDCB  DCB   DDNAME=SNAP,DSORG=PS,MACRF=W,                           *
               RECFM=VBA,LRECL=125,BLKSIZE=882
         TITLE 'JFCB AREA'
***********************************************************************
*        JOB FILE CONTROL BLOCK STORAGE                               *
***********************************************************************
         SPACE 1
IHAJFCB  DS    0CL176              JFCB AREA
         IEFJFCBN LIST=YES         MAP JFCB
         SPACE 2
         TITLE 'LITERAL POOL'
***********************************************************************
*        LITERAL POOL                                                 *
***********************************************************************
         SPACE 1
         LTORG ,                   LITERAL POOL GOES HERE
         TITLE 'SUMMARY RECORD FORMAT'
         COPY  SE72REC             COPY SUMMARY RECORD FORMAT
         SPACE 3
EOSNAP   EQU   *                   END OF STORAGE AREA TO BE SNAPPED
         TITLE 'DSECT MAPS'
         COPY  MDCB                USE STANDARD CODE FOR MDCB
         EJECT
         PUSH  PRINT               SAVE PRINT STATUS
         PRINT ON,GEN              EXPAND SYSTEM DSECTS
         SPACE 3
         ERBSMFR 72                MAP WORKLOAD ACTIVITY RECORD
         SPACE 3
LNG72PGN EQU   SMF72TST+L'SMF72FT2-SMF72TTX LENGTH OF PG SECTION
         SPACE 1
         POP   PRINT               RESTORE PRINT STATUS
         EJECT
CVTPTR   EQU   16                  CVT POINTER
         SPACE 1
CVTMAP   DSECT                     CVT MAP
         SPACE 3
         DCBD  DSORG=PS,DEVD=DA    DCB DSECT
         SPACE 3
         END
