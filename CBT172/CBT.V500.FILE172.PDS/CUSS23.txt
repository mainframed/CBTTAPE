CUSS23   TITLE 'CARTWRIGHT''S UTILITY TO SUPPRESS SMF 2 AND 3'
***********************************************************************
*        PDSDOC *USER2 EXIT FOR SMF COPY PROGRAM TO DELETE SMF2 AND 3.* CUSS23
*                                                                     *
*                                                                     *
*        PROGRAM ID. :             CUS23                              *
*                                                                     *
*        DATE WRITTEN :            APRIL 1992                         *
*                                                                     *
*        AUTHOR :                  D.H.CARTWRIGHT.                    *
*                                                                     *
*        LOCATION :                SPRECHER ENERGIE                   *
*                                  KIRCHWEG 5                         *
*                                  5036 OBERENTFELDEN                 *
*                                  SWITZERLAND                        *
*                                                                     *
*        HISTORY  :                                                   *
*                                                                     *
* 06APR92 CART1 STARTED TO WRITE                                      *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*       ---------                                                     *
*                                                                     *
* THIS PROGRAM IS A USER2 EXIT FOR THE SMF DUMP PROGRAM, IFASMFDP.    *
* IT WILL SUPPRESS THE SMF 2 AND 3 RECORDS CREATED BY IFASMFDP ITSELF.*
* THESE RECORDS SERVE NO PURPOSE IF IFASMFDP IS BEING USED TO COPY SMF*
* RECORDS. AS THEY ARE VERY SHORT THEY TEND TO INTERFERE WITH THE USE *
* OF DF/SORT RECORD SELECTION OR MANIPULATION FACILITIES, LEADING TO  *
* ABENDS OR OTHER FAILURES.                                           *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*       ------------                                                  *
*                                                                     *
* 1) THE PROGRAM SHOULD BE IN A LIBRARY ACCESSIBLE TO IFASMFDP - A    *
*    LINKLIST LIBRARY IS RECOMMENDED.                                 *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*       INPUT                                                         *
*       -----                                                         *
*                                                                     *
* 1)USER EXIT PARAMETER LIST FROM IFASMFDP. SEE THE SMF SPL.          *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*       OUTPUT                                                        *
*       ------                                                        *
*                                                                     *
* 1)RETURN CODE TO SAY WHETHER RECORD SHOULD BE ACCEPTED (ZERO) OR NOT*
*                                                                     *
***********************************************************************
         EJECT
         PRINT ON,GEN              SHOW MACRO EXPANSIONS
*        PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
         SPACE 1
CUSS23   CSECT
         SPACE 1
***********************************************************************
*        REGISTER EQUATES AND USAGES.                                 *
***********************************************************************
         SPACE 1
R0       EQU   0
R1       EQU   1                   WORK/LINKAGE
R2       EQU   2
R3       EQU   3
R4       EQU   4                   LINKAGE RETURN
R5       EQU   5
R6       EQU   6                   POINTER TO USER EXIT WORK AREA
R7       EQU   7                   POINTER TO SMF RECORD
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  BASE REGISTER 1
R13      EQU   13                  SAVE AREA
R14      EQU   14                  WORK/LINKAGE
R15      EQU   15                  WORK/LINKAGE
         SPACE 1
         PGMIN EQUATES=NO,USING=(R12) STANDARD HOUSEKEEPING
         EJECT
***********************************************************************
*        MAIN PROCESSING ROUTINE                                      *
*                                                                     *
* CHECK SMF RECORD TYPE. IF TYPE 2 OR 3, REJECT. ACCEPT ALL OTHERS.   *
*                                                                     *
***********************************************************************
         SPACE 1
CHECKSMF DS    0H                  TEST TYPE OF SMF RECORD
         SR    R15,R15             SET TO ACCEPT SMF RECORD
         LM    R6,R8,0(R1)         PICK UP USER EXIT PARAMETER LIST
         LTR   R7,R7               DO WE HAVE AN SMF RECORD?
         BZ    ENDOFPGM            NO, ABANDON PROCESSING
         USING SMFREC,R7           ESTABLISH SMF RECORD ADDRESSABILITY
         CLI   SMFTYPE,3           IS IT A DUMP TRAILER?
         BH    ENDOFPGM            NO, ACCEPT MOST THINGS
         CLI   SMFTYPE,2           IS IT A DUMP HEADER?
         BL    ENDOFPGM            NO, ACCEPT IPL RECORDS
         SPACE 1
DISCARD  DS    0H                  SET TO REJECT SMF RECORD
         LA    R15,4               SET RETURN CODE 4
         SPACE 2
***********************************************************************
*        END OF PROCESSING.                                           *
***********************************************************************
         SPACE 1
ENDOFPGM DS    0H                  RETURN TO CALLER
         L     R13,4(,R13)         RESTORE OLD SAVE AREA
         RETURN (14,12),,RC=(15)   EXIT PROGRAM
         SPACE 2
         DROP  R7                  END SMF RECORD ADDRESSABILITY
         TITLE 'SMF HEADER FORMAT'
         COPY SMFHEAD              INCLUDE SMF HEADER DSECT
         SPACE 3
         END
