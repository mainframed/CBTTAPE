***********************************************************************
*        PDSDOC *WRITE SUBROUTINE FOR SYSPRINT........................* PRINT
*                                                                     *
*        ON ENTRY R0 -----> PRINT LINE                                *
***********************************************************************
         SPACE 1
PRINT    DS    0H                  WRITE A REPORT
         SUBIN SAVE=(R0,R3,R4)     SUBROUTINE ENTRY
         LR    R3,R0               SAVE DATA ADDRESS
         TM    PRINTSW,DDINT+DDPRES+DDOPEN   TEST IF NEED TO WRITE
         BNO   PRINTX              BRANCH IF NO
         L     R1,PRINTSW          GET DCB ADDRESS
         AP    LINECOUN,ONE        ADD ONE TO LINE COUNTER
         CLI   0(R3),C' '          JUST ONE SKIP?
         BE    *+4+6               IF MORE,COUNT TWO
         AP    LINECOUN,ONE        ADD ONE TO LINE COUNTER
         CP    LINECOUN,MAXLINE    PAGE OVERFLOW ?
         BL    PRINTY              NO, CARRY ON NORMALLY
         SP    LINECOUN,LINECOUN   YES, ZEROISE LINE COUNTER
         AP    PAGECOUN,ONE        ADD ONE TO PAGE COUNTER
         MVC   HEADPAGE,EDITPL2    MOVE IN EDIT MASK
         ED    HEADPAGE,PAGECOUN   SHOW PAGE NUMBER
         LA    R0,TITLE1           POINT AT PAGE HEADING
         GOSUB PUTMOVE             GO OUTPUT HEADING
         LA    R0,SUBTITLE         POINT AT SECOND HEADING
         GOSUB PUTMOVE             GO OUTPUT IT
         LR    R0,R3               RESTORE DATA ADDRESS
         SPACE 1
PRINTY   EQU   *
         GOSUB PUTMOVE             GO OUTPUT A DETAIL LINE
PRINTX   SUBOUT RESTORE=(R0,R3,R4) RETURN TO CALLER
