./ ADD NAME=$DOC                                   12JAN00 12.31.49
<> ADD NAME=$EDXDOC  0103-00007-00010-1523-00045-00030-00000-SYSDHC
This function is based on EDX written by Jim Lane of Toronta Hydro and
donated to the Public Domain in file 047 of the CBT tape. Over the years
I have found this to be an extremely useful little utility and IBM have
even tried to do something similar with ISPF Option 11 - Workplace.
However, I have got EDX working the way that I like, so I tend to use
that. Refer to the tutorial panels for changes I have made to the
selection operations.  Some of the options I have included require other
programs from the CBT tape, such as PDS (I use level 84, but higher
levels are available now), REVIEW (file 134), $HBLIST (Greg Price again)
and some version of PRINTOFF.  I have also included an option to call
FIXPDS, which may be found in file 036 of the CBT tape.  If you have a
favourite utility it is easy to build that function into EDX.

However, the main change that I have made is not well shown. I have
added the capability of using multiple dataset lists and switching
between them dynamically. This is the 'T' list command, and the
parameter supplied is concatenated to the Userid to form the name of an
SPF table used to hold the list of datasets.  Personally I think this
makes EDX just wonderful - I can have lots of different lists of
datasets grouped by function. For example I usually have a list of SPF
datasets such as CLIST and PANEL libraries, a list of VTAM datasets such
as VTAMLST and a list of TCPIP datasets. The default list has a suffix
of zero, so from my VTAM list I enter 'T 0' on the command line to take
me back to my initial list. This will typically contain my private JCL
libraries, PARMLIB, maybe PROCLIB and stuff I find generally useful.
Note that any file (doesn't have to be a PDS) can appear in any number
of lists - many of my lists contain PARMLIB. Each list is a
self-contained table in the ISPF Profile dataset - make sure you give
yourself lots of directory space to accommodate all those lists you will
probably end up creating.  In my current shop Userids are six
characters, so I have two characters to play with for table suffixes.
This is enough for me, but if you want more then use some prefix other
than Userid - maybe the literal 'EDX' or a subset of the Uid, depending
on how you set up your ISPF profiles. Whatever works for you.

Another change I made that can be slightly confusing is that I now use
the volume serial shown in the dataset list rather than just going
through the catalog.  This allows me to maintain cloned MVS systems by
having dataset lists for each system, pointing to SYS1.PARMLIB on
different volumes, for example.  If the dataset is not on the indicated
volume and is not archived by HSM there is no error message. Sorry about
that.

Dave Cartwright
dc@dcuk.demon.co.uk
./ ADD NAME=CLIST                                  12JAN00 12.31.49
<> ADD NAME=EDX      0156-99053-00007-1431-00600-00587-00154-SYSDHC
PROC 0 TABLE(0) DEBUG
 /*                                                                  */
 /* 26JUL99 DHC USE VOLSER IN BROWSE, ALSO                           */
 /* 05JUL99 DHC USE VOLSER IN ADD, EDIT, SELECT, PDS                 */
 /* 10MAR99 DHC MADE TABLE OPTION 'T' FOR SPEED                      */
 /* 22FEB99 DHC ADDED TABLE() OPTION TO CHANGE TABLE                 */
 /*                                                                  */
ISPEXEC CONTROL ERRORS RETURN
CONTROL NOMSG NOFLUSH NOLIST
IF &SF = THEN SET &SF = &STR(DSNAME)
IF &SD = THEN SET &SD = &STR(A)
IF &TABLE = THEN SET &TABLE = &STR(0)
SET &SPFTAB = &SYSUID.&TABLE
ISPEXEC VPUT (SF SD) PROFILE
IF &DEBUG = DEBUG THEN CONTROL LIST MSG
  ISPEXEC  TBOPEN &SPFTAB           WRITE
  IF &LASTCC = 8 THEN DO
  ISPEXEC  TBCREATE  &SPFTAB   KEYS(EDBRDSNM) +
                     NAMES(EDBRVOLS EDBRLSTU EDBRLSTA EDBRLST4) +
                     WRITE
  END
DISPLAY:+
  ISPEXEC TBDISPL &SPFTAB PANEL(EDBRPANL)
  SET &LCC = &LASTCC
RESTART:+
  IF &LCC >= 8 THEN GOTO EXIT
  ISPEXEC TBQUERY &SPFTAB POSITION(CRP)
ADD:+
  IF &OPT = &STR(ADD) +
  THEN DO
     ISPEXEC  TBVCLEAR &SPFTAB
     SET &EDBRLSTA = &STR(** ADD    )
     SET &EDBRLSTU = &STR(&SYSSDATE)
     SET &EDBRLST4 = &STR(&SYS4SDATE)
     ISPEXEC DISPLAY PANEL(EDBRADD)
     IF &LASTCC = 8 THEN GOTO DISPLAY
     IF &EDBRDSNM = &STR() +
     THEN DO
        SET &SYSOUTTRAP = 5000
        LISTC LEV(&EDBRLV)
        SET &SYSOUTTRAP = 0
        SET COUNT = 1
        SET &LIMIT = &SYSOUTLINE
        DO WHILE &COUNT <= &LIMIT
           SET &LINE = &&SYSOUTLINE&COUNT
           IF (&SUBSTR(1:7,&LINE) = NONVSAM) OR +
           (&SUBSTR(1:7,&LINE) = CLUSTER) +
           THEN DO
              SET EDBRDSNM = &SUBSTR(17:&LENGTH(&STR(&LINE)),&LINE)
              LISTDSI '&EDBRDSNM' NORECALL
              SET &LCC = &LASTCC
              IF &LCC = 16 && &SYSREASON = 0008 +
              THEN
              ELSE DO
                 IF &LCC = 16 && &SYSREASON = 0009 +
                 THEN SET &EDBRVOLS = MIGRAT
                 ELSE SET &EDBRVOLS = &SYSVOLUME
                 ISPEXEC  TBADD &SPFTAB ORDER
                 IF &LASTCC = 8 THEN  ISPEXEC  TBPUT &SPFTAB ORDER
              END
           END
           SET &COUNT = &COUNT + 1
        END
     END
     ELSE DO
        IF &EDBRVOLS = &STR() THEN +
        LISTDSI '&EDBRDSNM' NORECALL
        ELSE +
        LISTDSI '&EDBRDSNM' VOLUME(&EDBRVOLS) NORECALL
        IF &LASTCC = 16 && &SYSREASON = 0009 +
        THEN SET &EDBRVOLS = MIGRAT
           ELSE SET &EDBRVOLS = &SYSVOLUME
        ISPEXEC  TBADD &SPFTAB                  ORDER
        IF &LASTCC = 8 THEN DO
           ISPEXEC  TBPUT &SPFTAB ORDER
        END
     END
  END
  ELSE IF &OPT = &STR(DROP)   +
  THEN DO
     ISPEXEC DISPLAY PANEL(EDBRDROP)
     IF &LASTCC = 8 THEN GOTO DISPLAY
     SET &DL = &LENGTH(&EDBRDLEV)
     ISPEXEC TBTOP &SPFTAB
     SET &DCC = 0
     SET &DCNT = 1
     DO WHILE (&DCC = 0)
        IF &SUBSTR(1:&DL,&STR(&EDBRDSNM)&STR(                    )) =+
        &STR(&EDBRDLEV) +
        THEN DO
           SET &DCNT = &DCNT + 1
           ISPEXEC  TBDELETE &SPFTAB
        END
        ISPEXEC  TBSKIP &SPFTAB
        SET &DCC = &LASTCC
     END
     ISPEXEC SETMSG MSG(EDX017)
  END
  ELSE IF &OPT = &STR(VERIFY) +
  THEN DO
     ISPEXEC TBTOP &SPFTAB
     SET &VCC = 0
     DO WHILE (&VCC = 0)
        LISTDSI '&EDBRDSNM' NORECALL
        SET &LDCC = &LASTCC
        IF &LDCC = 16 && &SYSREASON = 0009 THEN DO
           SET &EDBRVOLS = MIGRAT
           SET &EDBRLSTA = &STR(** VERIFY )
           ISPEXEC  TBPUT &SPFTAB ORDER
        END
        ELSE IF &LDCC = 16 && &SYSREASON = 0005 THEN DO
           ISPEXEC  TBDELETE &SPFTAB
        END
        ELSE DO
           SET &EDBRVOLS = &SYSVOLUME
           SET &EDBRLSTA = &STR(** VERIFY )
           SET &EDBRLSTU = &STR(&SYSSDATE)
           SET &EDBRLST4 = &STR(&SYS4SDATE)
           ISPEXEC  TBPUT &SPFTAB ORDER
        END
        ISPEXEC  TBSKIP &SPFTAB
        SET &VCC = &LASTCC
     END
     ISPEXEC SETMSG MSG(EDX017)
  END
 /*                                                                  */
 TABLE: ELSE +
  IF &LENGTH(&OPT) > 2 +
  THEN DO
     IF &SUBSTR(1:1,&STR(&OPT)) = &STR(T) +
     THEN DO
       SET &TABLE = &SUBSTR(3:&LENGTH(&OPT),&STR(&OPT))
       SET &SPFTST = &SYSUID.&TABLE
       IF &LENGTH(&SPFTST) < 9 +
       THEN DO
          ISPEXEC TBCLOSE &SPFTAB
          SET &SPFTAB = &SYSUID.&TABLE
            ISPEXEC  TBOPEN &SPFTAB           WRITE
            IF &LASTCC = 8 THEN DO
            ISPEXEC  TBCREATE  &SPFTAB   KEYS(EDBRDSNM) +
                     NAMES(EDBRVOLS EDBRLSTU EDBRLSTA EDBRLST4) +
                     WRITE
            END
       END
       ELSE ISPEXEC SETMSG MSG(EDX018)
     GOTO DISPLAY
     END
 /*                                                                  */
     ELSE IF &SUBSTR(1:4,&STR(&OPT)) = &STR(SORT) +
     THEN DO
        SET &S = &SUBSTR(6:&LENGTH(&OPT),&STR(&OPT))
        IF &SUBSTR(1:5,&STR(&S)) = &STR(LSTUS) +
        THEN DO
           SET &SF = &STR(EDBRLST4)
           SET &SD = &SUBSTR(&LENGTH(&S):&LENGTH(&S),&STR(&S))
        GOTO SORTOK
        END
        ELSE IF &SUBSTR(1:6,&STR(&S)) = &STR(DSNAME) +
        THEN DO
           SET &SF = &STR(EDBRDSNM)
           SET &SD = &SUBSTR(&LENGTH(&S):&LENGTH(&S),&STR(&S))
        END
        ELSE IF &SUBSTR(1:6,&STR(&S)) = &STR(VOLSER) +
        THEN DO
           SET &SF = &STR(EDBRVOLS)
           SET &SD = &SUBSTR(&LENGTH(&S):&LENGTH(&S),&STR(&S))
        END
SORTOK: ISPEXEC  TBSORT   &SPFTAB FIELDS(&SF C,&SD)
     END
  END
 /*                                                                  */
  IF &SELECT = &STR(B) +
  THEN DO
     IF &EDBRVOLS = &STR() THEN +
     LISTDSI '&EDBRDSNM' NORECALL
     ELSE +
     LISTDSI '&EDBRDSNM' VOLUME(&EDBRVOLS) NORECALL
     IF &LASTCC = 16 THEN DO
        IF &SYSREASON = 0009 THEN DO
           SET &EDBRVOLS = MIGRAT
           ISPEXEC  TBPUT &SPFTAB ORDER
           ISPEXEC SETMSG MSG(EDX001)
        END
        ELSE IF &SYSREASON = 0005 THEN DO
           ISPEXEC SETMSG MSG(EDX013)
        END
     END
     ELSE DO
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** BROWSE )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
        ISPEXEC  TBPUT &SPFTAB ORDER
        ISPEXEC CONTROL DISPLAY SAVE
        IF &SYSDSORG ¬= &STR(VS) THEN +
        ISPEXEC BROWSE DATASET('&EDBRDSNM') VOLUME(&SYSVOLUME)
        ELSE ISPEXEC SETMSG MSG(EDX019)
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(E) +
  THEN DO
     IF &EDBRVOLS = &STR() THEN +
     LISTDSI '&EDBRDSNM' NORECALL
     ELSE +
     LISTDSI '&EDBRDSNM' VOLUME(&EDBRVOLS) NORECALL
     IF &LASTCC = 16 THEN DO
        IF &SYSREASON = 0009 THEN DO
           SET &EDBRVOLS = MIGRAT
           ISPEXEC  TBPUT &SPFTAB ORDER
           ISPEXEC SETMSG MSG(EDX001)
        END
        ELSE IF &SYSREASON = 0005 THEN DO
           ISPEXEC SETMSG MSG(EDX014)
        END
     END
     ELSE DO
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** EDIT )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
        ISPEXEC  TBPUT &SPFTAB ORDER
        ISPEXEC CONTROL DISPLAY SAVE
        IF &SYSDSORG ¬= &STR(VS) THEN +
        ISPEXEC EDIT DATASET('&EDBRDSNM') VOLUME(&SYSVOLUME)
        ELSE ISPEXEC SETMSG MSG(EDX019)
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(S) +
  THEN DO
     IF &EDBRVOLS = &STR() THEN +
     LISTDSI '&EDBRDSNM' NORECALL
     ELSE +
     LISTDSI '&EDBRDSNM' VOLUME(&EDBRVOLS) NORECALL
     IF &LASTCC = 16 THEN DO
        IF &SYSREASON = 0009 THEN DO
           SET &EDBRVOLS = MIGRAT
           ISPEXEC  TBPUT &SPFTAB ORDER
           ISPEXEC SETMSG MSG(EDX001)
        END
        ELSE IF &SYSREASON = 0005 THEN DO
           ISPEXEC SETMSG MSG(EDX014)
        END
     END
     ELSE DO
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** SELECT_)
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
        ISPEXEC  TBPUT &SPFTAB ORDER
        ISPEXEC CONTROL DISPLAY SAVE
        IF &SYSDSORG ¬= &STR(VS) THEN +
        ISPEXEC EDIT DATASET('&EDBRDSNM') VOLUME(&SYSVOLUME)
        ELSE ISPEXEC SETMSG MSG(EDX019)
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(F) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL
     IF &LASTCC = 16 THEN DO
        IF &SYSREASON = 0009 THEN DO
           SET &EDBRVOLS = MIGRAT
           ISPEXEC  TBPUT &SPFTAB ORDER
           ISPEXEC SETMSG MSG(EDX001)
        END
        ELSE IF &SYSREASON = 0005 THEN DO
           ISPEXEC SETMSG MSG(EDX014)
        END
     END
     ELSE DO
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** FIX__)
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
        ISPEXEC  TBPUT &SPFTAB ORDER
        ISPEXEC CONTROL DISPLAY SAVE
        IF &SYSDSORG ¬= &STR(PO) THEN +
           ISPEXEC SETMSG MSG(EDX010)
        ELSE DO
          ALLOC FI(FPDS0001) DA(&SYSUID..FPDS0001.CNTL) SP(2 2) CYL +
                NEW CATALOG UNIT(3390)
          ALLOC FI(FPDS0000) DA('&EDBRDSNM') SHR
          ISPEXEC SELECT PGM(SPFPDS)
          FREE FI(FPDS0000 FPDS0001)
          DELETE FPDS0001.CNTL
        END
        ISPEXEC CONTROL DISPLAY RESTORE
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(R) +
  THEN DO
     IF &EDBRVOLS = &STR() THEN +
     LISTDSI '&EDBRDSNM' NORECALL
     ELSE +
     LISTDSI '&EDBRDSNM' VOLUME(&EDBRVOLS) NORECALL
     IF &LASTCC = 16 THEN DO
        IF &SYSREASON = 0009 THEN DO
           SET &EDBRVOLS = MIGRAT
           ISPEXEC  TBPUT &SPFTAB ORDER
           ISPEXEC SETMSG MSG(EDX001)
        END
        ELSE IF &SYSREASON = 0005 THEN DO
           ISPEXEC SETMSG MSG(EDX014)
        END
     END
     ELSE DO
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** REVIEW)
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
        ISPEXEC  TBPUT &SPFTAB ORDER
        ISPEXEC CONTROL DISPLAY SAVE
        ISPEXEC SELECT CMD(REVIEW '&EDBRDSNM' VOLUME(&EDBRVOLS) )
        ISPEXEC CONTROL DISPLAY RESTORE
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(P) +
  THEN DO
     IF &EDBRVOLS = &STR() THEN +
     LISTDSI '&EDBRDSNM' NORECALL
     ELSE +
     LISTDSI '&EDBRDSNM' VOLUME(&EDBRVOLS) NORECALL
     IF &LASTCC = 16 && &SYSREASON = 0009 THEN DO
        SET &EDBRVOLS = MIGRAT
        ISPEXEC  TBPUT &SPFTAB ORDER
        ISPEXEC SETMSG MSG(EDX001)
     END
     ELSE DO
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** PDS  )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
        ISPEXEC  TBPUT &SPFTAB ORDER
        ISPEXEC CONTROL DISPLAY SAVE
        PDS84 '&EDBRDSNM' VOLUME(&SYSVOLUME)
        ISPEXEC CONTROL DISPLAY RESTORE
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(U) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL
     IF &LASTCC = 16 THEN DO
        IF &SYSREASON = 0009 THEN DO
           SET &EDBRVOLS = MIGRAT
           ISPEXEC  TBPUT &SPFTAB ORDER
           ISPEXEC SETMSG MSG(EDX001)
        END
        ELSE IF &SYSREASON = 0005 THEN DO
           ISPEXEC SETMSG MSG(EDX014)
        END
     END
     ELSE DO
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** UTILITY)
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
        SET &ZDLDSNLV = &EDBRDSNM
        SET &ZDLPVL   =
        ISPEXEC  TBPUT &SPFTAB ORDER
        ISPEXEC  VPUT (ZDLDSNLV ZDLPVL)
        ISPEXEC CONTROL DISPLAY SAVE
        ISPEXEC SELECT PGM(ISRUDL) PARM(ISRUDLP)
        ISPEXEC CONTROL DISPLAY RESTORE
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(Q) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL
     IF &LASTCC = 16 THEN DO
        IF &SYSREASON = 0009 THEN DO
           SET &EDBRVOLS = MIGRAT
           ISPEXEC  TBPUT &SPFTAB ORDER
           ISPEXEC SETMSG MSG(EDX001)
        END
        ELSE IF &SYSREASON = 0005 THEN DO
           ISPEXEC SETMSG MSG(EDX014)
        END
     END
     ELSE DO
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** QUERY__)
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
        SET &ZRDSN = &STR('&EDBRDSNM(*)')
        SET &ZRDVOL =
        ISPEXEC  TBPUT &SPFTAB ORDER
        ISPEXEC  VPUT (ZRDSN ZRDVOL) SHARED
        ISPEXEC CONTROL DISPLAY SAVE
        ISPEXEC SELECT PGM(ISRSFM)
        ISPEXEC CONTROL DISPLAY RESTORE
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(HL) +
  THEN DO
     ALLOC DA('&SYSUID..HL') NEW CATALOG +
             UNIT(DEVL) LRECL(121) BLKSIZE(1210) RECFM(F B A) +
             DSORG(PS) SPACE(1 1) TRACK
     HLIST DA('&EDBRDSNM') BOTH OUTDATASET('&SYSUID..HL')
     ISPEXEC BROWSE DATASET('&SYSUID..HL')
     DELETE '&SYSUID..HL'
     SET &EDBRLSTA = &STR(** HLIST )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
     ISPEXEC  TBPUT &SPFTAB ORDER
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(PR) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL
     IF &LASTCC = 16 && &SYSREASON = 0009 THEN DO
        SET &EDBRVOLS = MIGRAT
        ISPEXEC  TBPUT &SPFTAB ORDER
        ISPEXEC SETMSG MSG(EDX001)
     END
     ELSE DO
        ISPEXEC  SELECT CMD(%PRT '&EDBRDSNM' +
                 CLASS(D) FORMS(CHNG) NOHEADING)
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** PRINT )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
        ISPEXEC  TBPUT &SPFTAB ORDER
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(V) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL
     SET &LDCC = &LASTCC
     IF &LDCC = 16 && &SYSREASON = 0009 THEN DO
        SET &EDBRVOLS = MIGRAT
        ISPEXEC  TBPUT &SPFTAB ORDER
        ISPEXEC SETMSG MSG(EDX001)
     END
     ELSE IF &LDCC = 16 && &SYSREASON = 0005 THEN DO
        SET &EDBRVOLS = &STR(??????)
        SET &EDBRLSTA = &STR(** INFO )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
        ISPEXEC  TBPUT &SPFTAB ORDER
        ISPEXEC SETMSG MSG(EDX016)
     END
     ELSE DO
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRLSTA = &STR(** VOLSER )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
        ISPEXEC  TBPUT &SPFTAB ORDER
     END
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(D) +
  THEN DO
     ISPEXEC  DISPLAY  PANEL(EDBRDELT)
     IF &LASTCC = 8 THEN GOTO ENDSEL
     DELETE '&EDBRDSNM'
     IF &LASTCC = 0 +
     THEN DO
        ISPEXEC  TBDELETE &SPFTAB
        ISPEXEC SETMSG MSG(EDX005)
     END
     ELSE ISPEXEC SETMSG MSG(EDX006)
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(A) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL DIRECTORY
     IF &LASTCC ¬= 0 THEN GOTO ENDSEL
     SET &EDBRBLK  = &SYSBLKSIZE
     SET &EDBRPRIM = &SYSPRIMARY
     SET &EDBRSECQ = &SYSSECONDS
     SET &EDBRDIRB = &SYSADIRBLK
     SET &EDBRVOLS = &SYSVOLUME
     SET &EDBRMDSN = &EDBRDSNM
     SET &EDBRDSNM = &STR(')&EDBRDSNM&STR(')
     ISPEXEC  DISPLAY  PANEL(EDBRMODL)
     IF &LASTCC = 8 THEN GOTO ENDSEL
     IF &SYSRECFM = &STR(FB) +
     THEN SET &SYSRECFM = &STR(F B)
     ELSE IF &SYSRECFM = &STR(VB) +
     THEN SET &SYSRECFM = &STR(V B)
     ELSE IF &SYSRECFM = &STR(FBA) +
     THEN SET &SYSRECFM = &STR(F B A)
     ELSE IF &SYSRECFM = &STR(VBA) +
     THEN SET &SYSRECFM = &STR(V B A)
     IF &SYSUNITS = &STR(BLOCK) +
     THEN SET &SYSUNITS = &STR(BLOCK(&EDBRBLK))
     IF &SYSDSORG = &STR(PO) THEN +
     ALLOC DA('&EDBRDSNM') NEW CATALOG +
         UNIT(&SYSUNIT) VOL(&EDBRVOLS) +
         LRECL(&SYSLRECL) BLKSIZE(&EDBRBLK) +
         RECFM(&SYSRECFM) DIR(&EDBRDIRB) DSORG(&SYSDSORG) +
         SPACE(&SYSPRIMARY &SYSSECONDS) &SYSUNITS
     ELSE +
     ALLOC DA('&EDBRDSNM') NEW CATALOG +
         UNIT(&SYSUNIT) VOL(&EDBRVOLS) +
         LRECL(&SYSLRECL) BLKSIZE(&EDBRBLK) +
         RECFM(&SYSRECFM)  DSORG(&SYSDSORG) +
         SPACE(&SYSPRIMARY &SYSSECONDS) &SYSUNITS
     IF &LASTCC = 0 +
     THEN ISPEXEC SETMSG MSG(EDX007)
     ELSE DO
        ISPEXEC SETMSG MSG(EDX008)
        GOTO ENDSEL
     END
     SET &EDBRLSTA = &STR(** ALLOC  )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
     ISPEXEC  TBADD &SPFTAB                  ORDER
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(X) +
  THEN DO
     ISPEXEC  TBDELETE &SPFTAB
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(LB) +
  THEN DO
     ISPEXEC SELECT CMD(%LB '&EDBRDSNM')
     SET &EDBRLSTA = &STR(** LBCK )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
     ISPEXEC  TBPUT &SPFTAB                  ORDER
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(HM) +
  THEN DO
     HMIGRATE '&EDBRDSNM' NOWAIT
     ISPEXEC SETMSG MSG(EDX002)
     SET &EDBRVOLS = MIGRAT
     SET &EDBRLSTA = &STR(** MIGRATE )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
     ISPEXEC  TBPUT &SPFTAB ORDER
     GOTO ENDSEL
  END
  ELSE IF &SELECT = &STR(HR) +
  THEN DO
     HRECALL  '&EDBRDSNM' NOWAIT
     ISPEXEC SETMSG MSG(EDX003)
     SET &EDBRVOLS = ONLINE
     SET &EDBRLSTA = &STR(** RECALL )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
     ISPEXEC  TBPUT &SPFTAB ORDER
  END
  ELSE IF &SELECT = &STR(I) +
  THEN DO
     LISTDSI '&EDBRDSNM' NORECALL
     IF &LASTCC = 16 THEN DO
        IF &SYSREASON = 0009 THEN DO
           SET &EDBRVOLS = MIGRAT
           ISPEXEC  TBPUT &SPFTAB ORDER
           ISPEXEC SETMSG MSG(EDX001)
        END
        ELSE IF &SYSREASON = 0005 THEN DO
           ISPEXEC SETMSG MSG(EDX015)
        END
     END
     ELSE DO
        SET &EDBRBLK  = &SYSBLKSIZE
        SET &EDBRPRIM = &SYSPRIMARY
        SET &EDBRSECQ = &SYSSECONDS
        SET &EDBREXT  = &SYSEXTENTS
        SET &EDBRVOLS = &SYSVOLUME
        SET &EDBRCDTE = &STR(&SYSCREATE)
        SET &EDBREFDT = &STR(&SYSREFDATE)
        SET &EDBRUPDT = &SYSUPDATED
        ISPEXEC  DISPLAY  PANEL(EDBRINFO)
        SET &EDBRLSTA = &STR(** INFO )
        SET &EDBRLSTU = &STR(&SYSSDATE)
        SET &EDBRLST4 = &STR(&SYS4SDATE)
        ISPEXEC  TBPUT &SPFTAB ORDER
     END
  END
 ENDSEL:+
 ISPEXEC TBTOP  &SPFTAB
 ISPEXEC TBSKIP &SPFTAB NUMBER(&ZTDTOP)
 ISPEXEC VGET   (ZVERB ZSCROLLN)
 SELECT
  WHEN (&ZVERB = 'UP') THEN ISPEXEC TBSKIP &SPFTAB NUMBER(-&ZSCROLLN)
  WHEN (&ZVERB = 'DOWN') THEN ISPEXEC TBSKIP &SPFTAB NUMBER(&ZSCROLLN)
  OTHERWISE
 END
  IF &LCC = 4 THEN DO
     ISPEXEC TBDISPL &SPFTAB PANEL(EDBRPANL)
     SET &LCC = &LASTCC
     GOTO RESTART
  END
  ELSE GOTO DISPLAY
EXIT:+
ISPEXEC TBCLOSE &SPFTAB
END
<> ADD NAME=PRT      0100-99138-99138-1053-00011-00011-00000-SYSDHC
PROC 1 KW1 KW2 KW3 KW4

 /*                                                                  */
 /*      PDSDOC *TSO  CLIST..........................................*/ CLIST
 /*                                                                  */

  IF &DEBUG = DEBUG THEN CONTROL  MSG LIST CONLIST SYMLIST FLUSH
                    ELSE CONTROL  NOMSG NOLIST FLUSH
  PRINTOFF &KW1 &KW2 &KW3 &KW4
 /*                                                                  */
  EXIT CODE(0)
<> ADD NAME=LB       0104-99118-00007-1433-00007-00006-00004-SYSDHC
 PROC 1 DSN DEBUG
CONTROL NOLIST MSG
IF &DEBUG = DEBUG THEN CONTROL LIST MSG
 /*                                                                  */
 /*      PDSDOC *HSM LIST BACKUP DATA USING $HBLIST FROM CBT FILE134 */ CLIST
 /*                                                                  */
  $HBLIST LEVEL(&DSN) DETAIL
./ ADD NAME=ISPMLIB                                12JAN00 12.31.49
<> ADD NAME=EDX00                                  12JAN00 12.31.36
EDX001   'DATASET MIGRATED'                  .ALARM=YES
'&EDBRDSNM has been migrated by DFHSM. HRECALL it first.'

EDX002   'MIGRATE REQUESTED'
'A request to migrate &EDBRDSNM has been sent to DFHSM.'

EDX003   'RECALL REQUESTED'
'A request to recall &EDBRDSNM has been sent to DFHSM.'

EDX004   'DATASET NOT FOUND!'                .ALARM=YES
'&EDBRDSNM cannot be found. Check the catalog.'

EDX005   'DELETED'
'&EDBRDSNM has been deleted.'

EDX006   'NOT DELETED!'                      .ALARM=YES
'&EDBRDSNM has not been deleted. RC=&LASTCC'

EDX007   'ALLOCATED'
'&EDBRDSNM has been allocated.'

EDX008   'NOT ALLOCATED!'                    .ALARM=YES
'&EDBRDSNM has not been allocated. RC=&LASTCC'

EDX009   'INVALID UNIT!'                     .ALARM=YES
'Valid units are: 3330,3350,3375 and 3380'

<> ADD NAME=EDX01    0105-99055-00007-1355-00030-00027-00000-SYSDHC
EDX010   'INVALID DSORG'                     .ALARM=YES
'Only PS and PO datasets can be allocated from here.'

EDX011   'INVALID RECFM'                     .ALARM=YES
'Try one of: V,F,VB,FB,VBA,FBA,VBM or FBM.'

EDX012   'INVALID UNIT'                      .ALARM=YES
'Valid units of allocation are: BLOCK, TRACK and CYLINDER.'

EDX013   'BROWSE FAILED'                     .ALARM=YES
'BROWSE failed for &EDBRDSNM - dataset not found.'

EDX014   'EDIT FAILED'                       .ALARM=YES
'EDIT failed for &EDBRDSNM - dataset not found.'

EDX015   '? FAILED'                          .ALARM=YES
'INFO request failed for &EDBRDSNM - dataset not found.'

EDX016   'V FAILED'                          .ALARM=YES
'&EDBRDSNM is not on any volume.'

EDX017   'Verified.'                         .ALARM=YES
'Your list is now synchronized with reality.'

EDX018   'Length Error'                      .ALARM=YES
'Profile table name exceeds 8 characters, use a shorter suffix.'

EDX019   'VSAM File'                         .ALARM=YES
'This operation can not be performed on a VSAM dataset'

./ ADD NAME=ISPPLIB                                12JAN00 12.31.49
<> ADD NAME=EDBOLINE 0103-95354-99193-1619-00033-00036-00000-SYSDHC
)ATTR DEFAULT(%+_)
      /*  % TYPE(TEXT) INTENS(HIGH)      defaults displayed for      */
      /*  + TYPE(TEXT) INTENS(LOW)       information only            */
      /*  _ TYPE(INPUT) INTENS(HIGH) CAPS(ON) JUST(LEFT)             */
)BODY  EXPAND(//)
%EDX -/-/- Line commands -/-/- EDX
%SELECTION ===>_ZCMD
+
   The following commands may be entered in the selection field beside
   any of the dataset names:

  %A+        to ALLOCATE a new dataset using this one as a model.
  %B+        to ISPF BROWSE the dataset.
  %D+        to delete the dataset.
  %E+        to ISPF EDIT the dataset.
  %F+        to run FIXPDS to recover deleted members
  %HL+       to display DFHSM information about a dataset.
  %HM+       to migrate the dataset with DFHSM.
  %HR+       to recall the dataset from DFHSM migration.
  %I+        to display information about the dataset's attributes.
  %LB+       to list DFHSM backup version of the dataset.
  %P+        to invoke the PDS84 dialog on the dataset.
  %PR+       to print the dataset.
  %V+        to set or update the VOLSER field.
  %X+        to drop the dataset from the list.

%NOTE:+this dialog picks up on the fact that a dataset has been migrated
       displays a message to that effect if you try to do something that
       the dataset unmigrated. You have to explicitly HRECALL it first.
)PROC
  &ZCONT = EDBRHELP
  &ZUP   = EDBRHELP
)END
<> ADD NAME=EDBRADD                                12JAN00 12.31.36
)ATTR DEFAULT(%+_)
  $ TYPE(INPUT) INTENS(LOW) PAD(_)    /* input field padded with '_' */
  ! TYPE(INPUT) INTENS(LOW) PAD(' ')  /* input field padded with ' ' */
)BODY  EXPAND(//)
%EDX -/-/- Add more dataset(s) to your list -/-/- EDX
%COMMAND ===>_ZCMD
%
+   Enter information about the dataset you want to add:
+
+   Dataset Name  ......$EDBRDS                                       +
+   Level         ......$EDBRLV               + (Dsname takes precedence
+   Volume Serial ......$EDBRVOLS+ (Optional)
+
+
)INIT
)PROC
 IF (&EDBRDS ¬= &Z)
     &TEMP = TRUNC (&EDBRDS,'''')
     &TRAIL = .TRAIL
     IF (&TRAIL = &Z)
         &EDBRDSNM = '&ZPREFIX..&EDBRDS'
     IF (&TRAIL ¬= &Z)
         &EDBRDSNM = TRUNC (&TRAIL,'''')
 IF (&EDBRDS = &Z)
    VER (&EDBRLV,NONBLANK)
)END
<> ADD NAME=EDBRDELT                               12JAN00 12.31.36
)ATTR DEFAULT(%+_)
       %   TYPE(TEXT) INTENS(HIGH)
       +   TYPE(TEXT) INTENS(LOW)
       _   TYPE(INPUT) INTENS(HIGH) CAPS(ON) JUST(LEFT)
  @   TYPE(OUTPUT)  INTENS(HIGH)
)BODY  EXPAND(//)
%EDX -/-/- Delete your dataset? -/-/- EDX
%Command ===>_ZCMD
+
+  Please confirm your request to delete this dataset.
+
+      Data Set Name%===>@EDBRDSNM
+          on Volume%===>@EDBRVOLS  +
+
+  Press%ENTER+to proceed with delete
+  Press%PF3+or enter the%END+command to cancel
+
)INIT
)PROC
)END
<> ADD NAME=EDBRDROP                               12JAN00 12.31.36
)ATTR DEFAULT(%+_)
  $ TYPE(INPUT) INTENS(LOW) PAD(_)    /* input field padded with '_' */
  ! TYPE(INPUT) INTENS(LOW) PAD(' ')  /* input field padded with ' ' */
)BODY  EXPAND(//)
%EDX -/-/- Drop some datasets from your list -/-/- EDX
%COMMAND ===>_ZCMD
%
+   Enter information about the datasets you want to drop:
+
+   Level         ......$EDBRDLEV             +
+
+
)INIT
)PROC
 VER (&EDBRDLEV,NB)
)END
<> ADD NAME=EDBRHELP                               12JAN00 12.31.36
)ATTR DEFAULT(%+_)
      /*  % TYPE(TEXT) INTENS(HIGH)      defaults displayed for      */
      /*  + TYPE(TEXT) INTENS(LOW)       information only            */
      /*  _ TYPE(INPUT) INTENS(HIGH) CAPS(ON) JUST(LEFT)             */
)BODY  EXPAND(//)
%EDX -/-/- Tutorial -/-/- EDX
%SELECTION ===>_ZCMD
%
+
+
   Personal Dataset Manager is an ISPF dialog that provides an improved
   interface to%EDIT+and%BROWSE+as well as other dataset-oriented
   functions. It does this by maintaining an ISPF table of the datasets
   you are currently working with and providing command functions both
   to maintain the list and to operate on the datasets.

   The following topics are presented in sequence, or may be selected by
   number:

  %1+ LIST Commands
  %2+ LINE Commands

)PROC
     &ZSEL = TRANS( &ZCMD
                1,EDBRLIST
                2,EDBRLINE
                *,'?'
                )
)END
<> ADD NAME=EDBRINFO                               12JAN00 12.31.36
)ATTR DEFAULT(%+_)
  $ TYPE(OUTPUT) INTENS(HIGH) PAD(' ')
  ! TYPE(INPUT) INTENS(LOW) PAD(' ')  /* input field padded with ' ' */
)BODY  EXPAND(//)
%EDX -/-/- Information about your dataset -/-/- EDX
%COMMAND ===>_ZCMD
%
+   Dataset Name  ......$EDBRDSNM
+   Volume Serial ......$EDBRVOLS+
+   Unit Name     ......$SYSUNIT+
+   Organization  ......$SYSDSORG+
+   Record Format ......$SYSRECFM+
+   Record Length ......$SYSLRECL+
+   Block Size    ......$EDBRBLK+
+   Allocation unit ....$SYSUNITS+
+   Allocated Space ....$SYSALLOC+     (in the above allocation units)
+   Used Space      ....$SYSUSED+      (in the above allocation units)
+   Primary Space   ....$EDBRPRIM+     (in the above allocation units)
+   Secondary Space ....$EDBRSECQ+     (in the above allocation units)
+   Extents         ....$EDBREXT+
+   Creation Date   ....$EDBRCDTE+
+   Last Used Date  ....$EDBREFDT+
+   Change Indicator....$EDBRUPDT+
+
+
)INIT
)PROC
)END
<> ADD NAME=EDBRLINE 0115-95354-99061-1345-00032-00036-00000-SYSDHC
)ATTR DEFAULT(%+_)
      /*  % TYPE(TEXT) INTENS(HIGH)      defaults displayed for      */
      /*  + TYPE(TEXT) INTENS(LOW)       information only            */
      /*  _ TYPE(INPUT) INTENS(HIGH) CAPS(ON) JUST(LEFT)             */
)BODY  EXPAND(//)
%EDX -/-/- Line commands -/-/- EDX
%SELECTION ===>_ZCMD
+               Enter one of these commands beside the dataset name:
<>./. %Control Commands +././.
%I+display the dataset's attributes.        %V+set or update the VOLSER
%A+ALLOCATE a new dataset using model       %D+Delete the dataset.
%PR+Print the dataset.                      %X+Drop the dataset from the
+
<>./. %ISPF Commands +././.
%B+BROWSE the dataset.                      %E+EDIT the dataset.
%S+EDIT the dataset.                        %U+Prime DSLIST dialog for d
%Q+Search dataset for string(s).
+
<>./. %Utility Commands +././.
%F+FIXPDS to undelete       %P+PDS84 on the dataset.   %R+REVIEW browser
+
<>./. %HSM Commands +././.
%HL+HLIST the dataset       %HM+HMIGRATE the dataset   %HR+HRECALL the d
                            %LB+List dataset backups
+
%NOTE:+this dialog picks up on the fact that a dataset has been migrated
       displays a message to that effect if you try to do something that
       the dataset unmigrated. You have to explicitly HRECALL it first.
)PROC
  &ZCONT = EDBRHELP
  &ZUP   = EDBRHELP
)END
<> ADD NAME=EDBRLIST 0103-99053-99069-1644-00024-00024-00000-SYSDHC
)ATTR DEFAULT(%+_)
      /*  % TYPE(TEXT) INTENS(HIGH)      defaults displayed for      */
      /*  + TYPE(TEXT) INTENS(LOW)       information only            */
      /*  _ TYPE(INPUT) INTENS(HIGH) CAPS(ON) JUST(LEFT)             */
)BODY  EXPAND(//)
%EDX -/-/- List commands -/-/- EDX
%SELECTION ===>_ZCMD
%
+
+
   The following commands may be entered in the command entry field at
   the top of the screen to act on the entire list of datasets:

  %ADD+      to add another dataset or group of datasets to the list.
  %DROP+     to exclude some of the datasets from the list.
  %SORT+     to reorder the list by dsname,volume or last use date.
  %T x  +    to switch to an alternate list of datasets (default x=0)
  %VERIFY+   to delete from the list datasets that no longer exist, and
             the VOLSERS of all others.

)PROC
  &ZCONT = EDBRLINE
  &ZUP   = EDBRHELP
)END
<> ADD NAME=EDBRMODL                               12JAN00 12.31.36
)ATTR DEFAULT(%+_)
  # TYPE(OUTPUT) INTENS(HIGH) PAD(' ')
  $ TYPE(INPUT) INTENS(HIGH) PAD(' ')
  ! TYPE(INPUT) INTENS(LOW) PAD(' ') /* INPUT FIELD PADDED WITH '
)BODY  EXPAND(//)
%EDX -/-/- Allocate a new dataset using #EDBRMDSN%as a model -/-/- EDX
%COMMAND ===>_ZCMD
%
+   Dataset Name  ......$EDBRDSNM
+   Volume Serial ......$EDBRVOLS+
+   Unit Name     ......$SYSUNIT+
+   Organization  ......$SYSDSORG+
+   Record Format ......$SYSRECFM+
+   Record Length ......$SYSLRECL+
+   Block Size    ......$EDBRBLK+
+   Allocation unit ....$SYSUNITS+
+   Allocated Space ....$SYSALLOC+     (in the above allocation units)
+   Primary Space   ....$EDBRPRIM+     (in the above allocation units)
+   Secondary Space ....$EDBRSECQ+     (in the above allocation units)
+   Directory Blocks ...$EDBRDIRB+
+
+
+ Change any of the above data and press%ENTER+to allocate
+ Press%PF3+or enter the%END+command to cancel.
     REFRESH (FIELD1,FIELD2,....)
)INIT
     .CURSOR = EDBRDSNM
     &EDBRDIRB = TRANS (&EDBRDIRB ' ',0)
)PROC
    VER (&EDBRDSNM,NB)
    VER (&EDBRDSNM,DSNAME)
    IF (&EDBRDSNM ¬= &Z)
        &INPUTDSN = &EDBRDSNM
        &TEMP = TRUNC (&INPUTDSN,'''')
        &TRAIL = .TRAIL
        IF (&TRAIL = &Z)
            &EDBRDSNM = '&ZPREFIX..&INPUTDSN'
        IF (&TRAIL ¬= &Z)
            &EDBRDSNM = TRUNC (&TRAIL,'''')
    IF (&INPUTDSN = &Z)
        &EDBRDSNM = &Z
    VER (&EDBRVOLS,NB)
    VER (&EDBRVOLS,PICT,CCCCCC)
    VER (&SYSUNIT,NB)
    VER (&SYSUNIT,NUM)
    VER (&SYSUNIT,LIST,3380,3375,3350,3330,MSG=EDX0009)
    VER (&SYSDSORG,NB)
    VER (&SYSDSORG,LIST,PS,PO,MSG=EDX0010)
    VER (&SYSRECFM,NB)
    VER (&SYSRECFM,LIST,F,V,FB,VB,FBA,VBA,FBM,VBM,MSG=EDX0011)
    VER (&SYSLRECL,NB)
    VER (&SYSLRECL,NUM)
    VER (&SYSLRECL,RANGE,1,32760)
    VER (&EDBRBLK,NB)
    VER (&EDBRBLK,NUM)
    VER (&EDBRBLK,RANGE,1,32760)
    VER (&SYSUNITS,NB)
    VER (&SYSUNITS,LIST,TRACK,BLOCK,CYLINDER,MSG=EDX0012)
    VER (&SYSALLOC,NB)
    VER (&SYSALLOC,NUM)
    VER (&EDBRPRIM,NB)
    VER (&EDBRPRIM,NUM)
    VER (&EDBRPRIM,RANGE,1,9999)
    VER (&EDBRSECQ,NB)
    VER (&EDBRSECQ,NUM)
    VER (&EDBRDIRB,NB)
    VER (&EDBRDIRB,NUM)
)END
<> ADD NAME=EDBRPANL 0101-97336-97336-1521-00021-00025-00000-$SYSP17
)ATTR DEFAULT(%+_)
   ! TYPE( INPUT) INTENS(HIGH) CAPS(ON ) JUST(LEFT ) PAD('''')
   ¬ TYPE(OUTPUT) INTENS(LOW ) CAPS(OFF) JUST(ASIS ) PAD('_')
)BODY  EXPAND(//)
%EDX -/-/- Personal Dataset Manager -/-/- EDX
%Command ===>_OPT                                  / /%Scroll ===>_AMT +
%
+Enter a line command in the selection field to process a dataset:
+
%Opt  Dataset name                                 Volume  Lstus     Las
+
)MODEL
!Z +¬Z                                           +¬Z     +¬Z       +¬Z
)INIT
  .HELP = EDBRHELP                /* INSERT NAME OF TUTORIAL PANEL
  .ZVARS = '(SELECT EDBRDSNM EDBRVOLS EDBRLSTU EDBRLSTA) '
 &AMT = 'PAGE'
  &SELECT = &Z
  &OPT = &Z
)PROC
)END
<> ADD NAME=EDBRPRNT                               12JAN00 12.31.36
)ATTR DEFAULT(%+_)
       %   TYPE(TEXT) INTENS(HIGH)
       +   TYPE(TEXT) INTENS(LOW)
       _   TYPE(INPUT) INTENS(HIGH) CAPS(ON) JUST(LEFT)
  @   TYPE(OUTPUT)  INTENS(HIGH)
)BODY  EXPAND(//)
%EDX -/-/- Print your dataset -/-/- EDX
%Command ===>_ZCMD
+
+
+
+Enter Data Set Name%===>@EDBRDSNM
+       Sysout Class%===>_POSOC+
+               Dest%===>_PODEST                +
+
+         Copy Count%===>_POCOPY+       Hold%===>_POHOLD +
+              Forms%===>_FORM+         UCS%===>_UCS +
+                FCB%===>_FCB +         Pagelength%===>_POPAGELN
+
+
+Single-Double Space%===>_POSPACE    +SS or DS or blank for CC
)INIT
 .CURSOR = POSOC
 &POSOC = TRANS (&POSOC ' ',A *,*)
 &POCOPY = TRANS (&POCOPY ' ',1 *,*)
 &PODEST = TRANS (&PODEST ' ',&Z *,*)
 &POHOLD = TRANS (&POHOLD ' ',NO *,*)
 &POPAGELN = TRANS (&POPAGELN ' ',60 *,*)
)PROC
 VER (&POSOC,NONBLANK)     VER (&POSOC,PICT,C)
 IF (&PODEST ¬= &Z)
    VER(&PODEST,EBCDIC)
 VER (&POCOPY,NONBLANK)        VER (&POCOPY,NUM)
 &POHOLD = TRANS (TRUNC (&POHOLD,1) Y,HOLD H,HOLD N,NOHOLD *,*)
 VER (&POHOLD,NONBLANK)        VER (&POHOLD,LIST,HOLD,NOHOLD)
 IF (&FORM ¬= &Z)
    VER (&FORM,PICT,CCCC)
 IF (&UCS ¬= &Z)
    VER (&UCS,PICT,CCCC)
 VER (&POPAGELN,NONBLANK)    VER (&POPAGELN,NUM)
 &POSPACE = TRANS (TRUNC (&POSPACE,1) S,SS D,DS *,*)
 VER (&POSPACE,LIST,SS,DS)
 VPUT (PODSN POSOC PODEST POCOPY POHOLD FORM UCS ) PROFILE
 VPUT (POPAGELN  FCB) PROFILE
 VPUT (POSPACE) PROFILE
)END
<> ADD NAME=EDDDSN                                 12JAN00 12.31.36
)ATTR
  ¬ TYPE(INPUT) INTENS(NON)
)BODY EXPAND(¢¢)
%¢-¢  &FUNC - ENTRY PANEL  ¢-¢
%COMMAND ===>_ZCMD
%
+ENTER PARTITIONED OR SEQUENTIAL DATA SET NAME
+   DATA SET NAME %===>_DSN
+   VOLUME SERIAL %===>_VOL   +  (if not cataloged)
+
+   PASSWORD      %===>¬PASSWORD+
+
+   PROFILE NAME  %===>_PROFILE +
+   INITIAL MACRO %===>_MACRO   +
+
)INIT
)PROC
   IF (&ZCMD ¬= ' ') .MSG = ISPZ001
   VER (&DSN,NB,DSNAME)
   VER (&VOL,NAME)
)END
