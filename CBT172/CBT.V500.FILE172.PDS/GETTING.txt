***********************************************************************
*                                                                     *
*        PDSDOC *GENERIC GET LOCATE. THIS IS CODED FOR RE-ENTRANCY....* GETTING
*                                                                     *
*        ON ENTRY R2 --> MDSCB                                        *
*                 R4  =  RETURN ADDRESS                               *
*        ON EXIT  R1 --> RECORD DATA, OR ZERO IF CANNOT READ          *
*                 R0  =  DATA LENGTH                                  *
*                                                                     *
***********************************************************************
         SPACE 1
GETTING  RIN   ,                   READ AN INPUT RECORD
         PUSH  USING               SAVE CURRENT STATUS
         USING MDCB,R2             ESTABLISH MDCB ADDRESSABILITY
         SR    R1,R1               ASSUME WE WILL FAIL
         TM    MDCBFLAG,DDINT+DDPRES+DDOPEN TEST IF WE CAN READ
         BO    GETTINGS            YES, CARRY ON
         GOSUB OPEN                GO OPEN FILE
         TM    MDCBFLAG,DDINT+DDPRES+DDOPEN TEST IF CAN READ
         BNO   GETTINGX            NO, ASSUMED FAILURE DID OCCUR
         SPACE 1
GETTINGS DS    0H                  GET A RECORD
         L     R4,MDCBDCB          GET DCB ADDRESS
         USING IHADCB,R4           ESTABLISH MDCB ADDRESSABILITY
         GET   (R4)                READ IN A RECORD
         AP    MDCBCNT,ONE         INCREMENT RECORD COUNTER
         SR    R0,R0               CLEAR LENGTH REGISTER
         ICM   R0,B'0011',DCBLRECL GET RECORD LENGTH
         TM    DCBRECFM,DCBRECV    ARE THEY VARIABLE LENGTH RECORDS?
         BNO   GETTINGU            NO, TEST FOR UNDEFINED
         ICM   R0,B'0011',0(R1)    ELSE GET LENGTH FROM RDW
         LA    R1,4(,R1)           SKIP OVER RDW
         SH    R0,=H'4'            AND SUBTRACT IT FROM THE LENGTH
GETTINGU DS    0H                  PROCESS RECFM=U FILES
         TM    DCBRECFM,DCBRECU    ARE THEY UNDEFINED LENGTH RECORDS?
         BNO   GETTINGX            NO, USE DCBLRECL VALUE
         ICM   R0,B'0011',DCBBLKSI ELSE GET LENGTH FROM BLOCKSIZE
         SPACE 1
GETTINGX DS    0H                  RETURN TO CALLER
         ROUT  ,                   SUBROUTINE EXIT
         DROP  R2,R4               FINISHED WITH MDCB AND DCB
         POP   USING               RESTORE STATUS
