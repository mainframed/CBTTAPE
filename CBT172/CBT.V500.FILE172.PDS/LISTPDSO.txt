LPDS TITLE 'LIST  PARTITIONED  DATA  SET  UTILITY  PROGRAM'
***$DOC@**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
**                                                                  **
** TITLE - LIST PARTITIONED DATA SET UTILITY PROGRAM                **
**                                                                  **
** NAME - LISTPDS,  ENTRY POINT - LISTPDS                           **
**                                                                  **
** STATUS - VERSION 6.2,  UPDATED 10JUL73                           **
**          UPDATED 03DEC80 AT AFDSC/PENTAGON, BILL GODFREY         **
**            .  MOVE SSI= KEYWORD AFTER MEMBER NAME ON ./ ADD      **
**            .  FORMAT SPF STATISTICS ON SYSPRINT AND ON ./ ADD    **
**            .  ADD NEW PARM OPTIONS 'SPF' AND 'NOSPF',            **
**               DEFAULT IS 'SPF'.  IF 'NOSPF' IS SPECIFIED, OR     **
**               IF NO MEMBERS IN A PDS CONTAIN SPF STATISTICS,     **
**               THEN THE OLD HEX FORMAT WILL BE USED ON SYSPRINT.  **
**            .  ADD NEW PARM OPTION 'NOSEL' TO IGNORE SYSIN.       **
**               SIMPLIFIES USE OF LISTPDS FROM TSO.                **
**            .  GET TIOT ADDRESS FROM TCB INSTEAD OF EXTRACT SVC.  **
**          UPDATED 23FEB81 AT AFDSC/PENTAGON, BILL GODFREY         **
**            .  UPDTE(XX) IN PARM SPECIFIES XX AS REPLACEMENT      **
**               STRING FOR './' IF FOUND WITHIN A MEMBER.          **
**          UPDATED 06JUL94 D.H.CARTWRIGHT                        .DHC.
**            .  'TRUNC' WITH CARD IMAGES DELETES SEQ. NOS. 73-80 .DHC.
**          UPDATED 08FEB95 D.H.CARTWRIGHT                        .DHC.
**            .  IN THE ABOVE CASE SPECIAL PROCESS FOR 'PDSDOC'   .DHC.
**                                                                  **
** DEVELOPED BY . . .                                               **
**       GENE CZARCINSKI                                            **
**       NASA/GODDARD SPACE FLIGHT CENTER                           **
**       GREENBELT, MARYLAND                                        **
**                                                                  **
** FUNCTION/OPERATION - "LISTPDS" IS AN OS/360 DATA SET UTILITY     **
**       PROGRAM FOR LISTING AND/OR PUNCHING SOURCE/OBJECT LIBRARIES**
**       (PARTITIONED DATA SETS).  ALL MEMBERS PROCESSED BY LISTPDS **
**       ARE ALWAYS OUTPUTTED IN COLLATING SEQUENCE (EXCEPT WHEN    **
**       SELECTED MEMBERS ARE PROCESSED).  LISTPDS GENERATES        **
**       TWO FORMATTED PRINTOUTS -                                  **
**          1. (ON SYSPRINT) A FORMATTED LISTING OF THE PDS'S       **
**             DIRECTORY TOGETHER WITH THE PAGE NUMBERS OF THE      **
**             OUTPUT LISTING (FOR EACH MEMBER).                    **
**          2. (ON SYSLIST) A FORMATTED PRINTOUT OF THE CONTENTS    **
**             OF EACH MEMBER PROCESSED.                            **
**                                                                  **
** ATTRIBUTES - ENABLED, SERIALLY REUSABLE, BLOCK LOADED,           **
**       SINGLE LOAD MODULE, NO OVERLAY.                            **
**                                                                  **
** ACCESS METHODS - QSAM LOCATE MODE (RECFM=FB) USED FOR            **
**       SYSPRINT, SYSLIST, SYSPUNCH, SYSIN;  BSAM/BPAM USED FOR    **
**       PDS I/O (SYSLIB).                                          **
**                                                                  **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 EJECT
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
**                                                                  **
** NOTES -                                                          **
**       1. VERSION 6 OF "LISTPDS" IS A MAJOR REDESIGN OF THE       **
**          PROGRAM AND REPLACES ALL PREVIOUS VERSIONS.             **
**       2. ASIDE FROM THE EDIT (ED) DECIMAL INSTRUCTION, "LISTPDS" **
**          IS CODED ENTIRELY WITH STANDARD INSTRUCTION SET (360)   **
**          INSTRUCTIONS.                                           **
**       3. "LISTPDS" DOES ITS OWN DIRECTORY PROCESSING AND ALWAYS  **
**          READS THE ENTIRE DIRECTORY (USING BSAM).                **
**       4. DEFAULT SWITCH AND DDNAMES CAN BE CHANGED BY MODIFYING  **
**          THE ASSEMBLY VARIABLES.                                 **
**       5. THE STANDARD TRANSLATE TABLES ARE SETUP FOR 'QN'        **
**          PRINT CHAIN OUTPUT.                                     **
**       6. ALL PRINTED OUTPUT USES ASA CONTROL CHARACTERS.         **
**       7. TO BE PROPERLY PROCESSED BY LISTPDS, ALL PDS            **
**          DIRECTORIES *MUST* BE IN STANDARD FORMAT: THE LAST      **
**          ENTRY IN EACH DIRECTORY IS HEX F'S.                     **
**       8. CODE HAS BEEN ADDED TO HANDLE PROCESSING OF CONCATON-   **
**          ATED SYSLIB DD STATEMENTS.                              **
**       9. LISTPDS OPERATION IS DEPENDENT ON THE ASSUMED LAYOUT    **
**          OF THE SYSLIB JFCB'S, DSCB'S AND THE "CAMLST" GENERATED **
**          CONTROL BLOCK FOR READING THE DSCB.                     **
**      10. LISTPDS OPERATION IS DEPENDENT ON THE ASSUMED OPERATION **
**          OF THE BPAM READ & FIND (C) ROUTINES FOR DIRECTORY AND  **
**          MEMBER READING.                                         **
**      11. DEFAULT BLKSIZES HAVE BEEN CHANGED FROM UNBLOCKED       **
**          TO BLOCKED --                                           **
**                 SYSPRINT - 3509                                  **
**                 SYSLIST  - 7260                                  **
**                 SYSPUNCH - 3200                                  **
**                 SYSIN    - 3200                                  **
**      12. THE DEFAULT BUFNO IS NOW SET TO 2.                      **
**      13. THE DEFAULT MAX LINES/PAGE IS NOW SET TO 60.            **
**                                                                  **
**$DOC$***==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 TITLE 'LISTPDS  ASSEMBLY  VARIABLES'
**
** LOCAL MACRO DEFINITIONS
**
         MACRO
&N       CRJE      &A,&B,&C,&D,&E
&N       TM        &A.(R2),&B
         BNO       X&SYSNDX
         MVC       0(&D,R1),=C&C
         LA        R1,&D.(R1)
         B         &E
X&SYSNDX EQU       *
         MEND
**
** ASSEMBLY VARIABLES
**
         LCLC      &RELEASE,&ID
         LCLC      &DDIN,&DDPRINT,&DDPUNCH,&DDLIB,&DDLIST
         LCLB      &A(8),&B(8)
         LCLA      &MAXLINE
**
&RELEASE SETC      '10JUL73'           RELEASE IDENT - MAX 7 BYTES
**
&ID      SETC      'AFDSC' (WAS GSFC)  'LOCAL' IDENT 5 BYTES MAX  .PRC.
**
&MAXLINE SETA      56                  DEFAULT MAX LINES/PAGE
**
&DDIN    SETC      'SYSIN'             DDNAME: CONTROL INPUT
&DDPRINT SETC      'SYSPRINT'          DDNAME: MAIN-PRINT OUTPUT
&DDLIST  SETC      'SYSLIST'           DDNAME: LISTING OUTPUT
&DDPUNCH SETC      'SYSPUNCH'          DDNAME: FOR PUNCHED OUTPUT
&DDLIB   SETC      'SYSLIB'            DDNAME: LIBRARY INPUT
**
&A(1)    SETB      1                   LIST
&A(2)    SETB      0                   NODECK
&A(3)    SETB      0                   NOT 'LIST DIR ONLY'
&A(4)    SETB      0                   NOUPDTE
&A(5)    SETB      0                   NO RITS/CRBE
&A(6)    SETB      1                   XLATE
&A(7)    SETB      1                   SSI
&A(8)    SETB      0                   TRUNC
**
&B(1)    SETB      0                   NODEBUG
&B(2)    SETB      0                   NOHEXOUT
&B(3)    SETB      1                   SPF                        .SPF.
&B(4)    SETB      1                   TEXT
&B(5)    SETB      0                   EROPT=TERM
&B(8)    SETB      1                   SEL                        .SEL.
**
 TITLE 'LISTPDS  SYMBOLIC  PARAMETER  EQUATES'
LISTPDS  CSECT
**
** LISTPDS  SYMBOLIC  PARAMETER  EQUATES
**
R0       EQU       0                   OS PARM REG; WORK REG
R1       EQU       1                   OS PARM REG; WORK REG
R2       EQU       2                   LOCAL WORK REG
R3       EQU       3                   LOCAL WORK REG
R4       EQU       4
R5       EQU       5                   LRECL CNTR FOR MEMBER
R6       EQU       6                   BLOCK LENGTH
R7       EQU       7                   BLOCK POINTER
R8       EQU       8                   IHADCB BASE REG
R9       EQU       9                   MAIN BASE REG              .PRC.
R10      EQU       10                  LOCAL LINK REG
R11      EQU       11                  LOCAL/SECONDARY BASE REG
R12      EQU       12                  MAIN BASE REG
R13      EQU       13                  POINTER TO OS SAVE AREA
R14      EQU       14                  OS LINK REG; WORK REG
R15      EQU       15                  OS BRANCH REG; WORK REG
**
SWA0     EQU       B'10000000'         NOLIST/LIST
SWA1     EQU       B'01000000'         NODECK/DECK
SWA2     EQU       B'00100000'         LIST DIRECTORY ONLY
SWA3     EQU       B'00010000'         NOUPDTE/UPDTE
SWA4     EQU       B'00001000'         NORITS-CRBE-CRJE/RITS-CRBE-CRJE
SWA5     EQU       B'00000100'         NOXLATE/XLATE
SWA6     EQU       B'00000010'         NOSSI/SSI
SWA7     EQU       B'00000001'         NOTRUNC/TRUNC
**
SWB0     EQU       B'10000000'         NODEBUG/DEBUG
SWB1     EQU       B'01000000'         NOHEXOUT/HEXOUT
SWB2     EQU       B'00100000'         NOSPF/SPF IN PARM          .SPF.
SWB3     EQU       B'00010000'         NOTEXT/TEXT
SWB4     EQU       B'00001000'         EROPT=TERM/ACC
SWB5     EQU       B'00000100'         SPF PRESENT IN CURRENT PDS .SPF.
SWB6     EQU       B'00000010'         SPF PRESENT IN ANY PDS     .SPF.
SWB7     EQU       B'00000001'         NOSEL/SEL                  .SEL.
**
SWC0     EQU       B'10000000'         PARM ANALYSIS ERROR
SWC1     EQU       B'01000000'         FLAG FOR NEW PAGE BEFORE PRINT
SWC2     EQU       B'00100000'
SWC3     EQU       B'00010000'         TERMINATE MODE SWITCH
SWC4     EQU       B'00001000'         SYSPRINT I/O ACTIVE
SWC5     EQU       B'00000100'         SYSLIST  I/O ACTIVE
SWC6     EQU       B'00000010'         SYSPUNCH I/O ACTIVE
SWC7     EQU       B'00000001'         DCB EXIT TAKEN
**
SWD0     EQU       B'10000000'         SELECTED NAMES SPECIFIED
SWD1     EQU       B'01000000'         MEMBER IS AN ALIAS
SWD2     EQU       B'00100000'
SWD3     EQU       B'00010000'         SYSPRINT FIRST TIME THRU
SWD4     EQU       B'00001000'         "00"=NOTHING, "10"=RITS,
SWD5     EQU       B'00000100'         "01"=CRBE, "11"=CRJE.
**
OFLGS    EQU       B'00010000'         DCB OPEN ERROR FLAG
ALIAS    EQU       B'10000000'         ALIAS BIT IN DIRECTORY ENTRY
**
JFCBDSNM EQU       0                   DSNAME OFFSET IN JFCB
JFCBVOLS EQU       118                 VOLSER OFFSET IN JFCB
JFCRECFM EQU       100                 RECFM  OFFSET IN JFCB
JFCBLKSI EQU       102                 BLKSIZ OFFSET IN JFCB
JFCLRECL EQU       104                 LRECL  OFFSET IN JFCB
DS1RECFM EQU       84-44               RECFM  OFFSET IN DSCB
DS1BLKSI EQU       86-44               BLKSIZ OFFSET IN DSCB
DS1LRECL EQU       88-44               LRECL  OFFSET IN DSCB
DSXRECFM EQU       176
DSXBLKSI EQU       178
DSXLRECL EQU       180
**
 TITLE 'LISTPDS ENTRY POINT AND PROGRAM INITIALIZATION'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
**                                                                  **
** LISTPDS ENTRY POINT AND PROGRAM INITIALIZATION                   **
**                                                                  **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
LISTPDS  CSECT
         SAVE      (14,12),,LISTPDS(&RELEASE)_GENE-CZARCINSKI
         LR        R12,R15             INIT MAIN BASE REG
         USING     LISTPDS,R12         AND PGM ADDRESSABILITY.
         LA        R15,1                                          .PRC.
         LA        R9,4095(R15,R12)                               .PRC.
         USING     LISTPDS+4096,R9                                .PRC.
         USING     IHADCB,R8           SET DCB ADDRESSABILITY
         LR        R4,R1               SAVE PARM LIST POINTER.
         LA        R15,SAVEAREA        INIT SAVE AREAS
         ST        R13,4(R15)
         ST        R15,8(R13)
         LR        R13,R15
         L         R11,=V(PGMINIT)     PGM INIT; PARM ANAL
         BALR      R10,R11
         TM        SWC,SWC0            PARM ERROR?
         BZ        LISTPDS1            NO.
         BAL       R14,PUTMSG          YES, ISSUE MSG
         MVC       0(L'LPDS05I,R1),LPDS05I
         LA        R1,4
         BAL       R14,SAVERC
         OI        SWC,SWC1            FLAG FOR RE-INIT SYSPRINT
LISTPDS1 L         R11,=V(BLDSELCT)    TO BUILD SELECTED NAME TABLE
         BALR      R10,R11
         L         R11,=V(FILEINIT)    TO INIT THE FILES
         BALR      R10,R11
         L         R11,=V(CHKLIB)
         BALR      R10,R11
         L         R11,=V(BLDNAMET)    TO BUILD THE NAME TABLE
         BALR      R10,R11
         TM        SWC,SWC1            SYSPRINT USED?
         BZ        MAIN00              NO, GO PROCESS PDS
         LH        R1,LINEMAX          RE-INIT SYSPRINT PAGE
         STH       R1,LINECNT1
         OI        SWD,SWD3            TURN OFF FIRST TIME SW
         L         R11,=V(PRNT)
         BALR      R10,R11
 TITLE 'MAIN  LINE  CODE  TO  PROCESS  THE  PDS'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** MAIN LINE CODE TO PROCESS THE PDS                                **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
MAIN00   L         R11,=V(PRNT)        GET NEW LINE ON SYSPRINT
         BALR      R10,R11
         ST        R1,LASTLINE         SAVE BUFFER POINTER
         NI        SWD,255-SWD1        TURN OFF ALIAS SWITCH
         L         R11,=V(NEXTMEMB)    POSITION TO NEXT MEMBER
         BALR      R10,R11
         B         MAIN10              MEMBER NOT FOUND
         B         MAIN20              ALIAS
         B         MAIN30              MAIN PROCESSING
** MEMBER NOT FOUND
MAIN10   L         R1,LASTLINE
         MVC       6(L'NAME,R1),NAME
         MVC       16(L'MSG3,R1),MSG3
         LA        R1,8                RC=8
         BAL       R14,SAVERC
         B         MAIN00
** ALIAS ... ISSUE MESSAGE
MAIN20   L         R1,LASTLINE
         MVC       6(L'NAME,R1),NAME
         MVC       L'CAPTIONS(L'MSG4,R1),MSG4
         MVC       L'CAPTIONS+L'MSG4(L'NAME2,R1),NAME2
         LA        R15,L'CAPTIONS-12(R1)
         LA        R0,L'TTR
         LA        R1,TTR
         L         R11,=V(HEXCON)
         BALR      R10,R11
         LH        R1,MEMCNT           UPDATE MEMBER COUNTER
         LA        R1,1(R1)
         STH       R1,MEMCNT
         B         MAIN00
 EJECT
** MEMBER PROCESSING
MAIN30   LH        R1,MEMCNT           UPDATE MEMBER COUNTER
         LA        R1,1(R1)
         STH       R1,MEMCNT
         TM        SWA,SWA0            LIST?
         BZ        MAIN31              NO.
         L         R11,=V(LOUT1)       INIT LISTING
         BALR      R10,R11
         L         R1,LASTLINE
         MVC       15(6,R1),WORK1+2    INSERT PAGE NO. ON SYSPRINT
MAIN31   L         R15,LASTLINE
         MVC       6(L'NAME,R15),NAME
         LA        R15,L'CAPTIONS-12(R15)
         LA        R0,L'TTR
         LA        R1,TTR
         L         R11,=V(HEXCON)
         BALR      R10,R11
         LH        R15,USERLEN         ANY USER INFO?
         LTR       R15,R15
         BNP       MAIN33              NO, SKIP FORMAT & OUTPUT
         TM        SWA,SWA4            RITS/CRBE/CRJE?
         BZ        MAIN32              NO.
         L         R1,LASTLINE         YES, FORMAT DIR ENTRY FOR PRN
         LA        R1,L'CAPTIONS(R1)
         LA        R2,USERDATA
         L         R11,=V(RITSINFO)
         BALR      R10,R11
         B         MAIN33
MAIN32   L         R15,LASTLINE
         LA        R15,L'CAPTIONS(R15)
         LA        R1,USERDATA
         LH        R0,USERLEN
         CH        R0,=H'30'       ARE SPF STATISTICS PRESENT     .SPF.
         BNE       MAIN32X         NO, BRANCH                     .SPF.
         TM        SWB,SWB2+SWB5   SPF IN THIS PDS                .SPF.
         BNO       MAIN32X         NO, BRANCH                     .SPF.
         TM        TTR+3,X'60'     TTR'S IN USER DATA             .SPF.
         BNZ       MAIN32X         YES, THIS ISNT SPF DATA        .SPF.
         IC        R0,USERDATA     GET V OF V.M                   .SPF.
         CVD       R0,WORK1                                       .SPF.
         OI        WORK1+7,X'0F'                                  .SPF.
         UNPK      1(2,R15),WORK1+6(2)                            .SPF.
         MVI       3(R15),C'.'                                    .SPF.
         IC        R0,USERDATA+1   GET M OF V.M                   .SPF.
         CVD       R0,WORK1                                       .SPF.
         OI        WORK1+7,X'0F'                                  .SPF.
         UNPK      4(2,R15),WORK1+6(2)                            .SPF.
*        UNPK      11(5,R15),USERDATA+4(4) YYDDD CREATED          .SPF.
*        MVC       10(2,R15),11(R15)                              .SPF.
*        MVI       12(R15),C'/'                                   .SPF.
         L         R0,USERDATA+4           YYDDD CREATED          .SPF.
         BAL       R14,JULIAN                                     .SPF.
         MVC       9(7,R15),WORK1+1                               .SPF.
*        UNPK      21(5,R15),USERDATA+8(4) YYDDD LAST MODIFIED    .SPF.
*        MVC       20(2,R15),21(R15)                              .SPF.
*        MVI       22(R15),C'/'                                   .SPF.
         L         R0,USERDATA+8           YYDDD LAST MODIFIED    .SPF.
         BAL       R14,JULIAN                                     .SPF.
         MVC       19(7,R15),WORK1+1                              .SPF.
         MVO       WORK1(3),USERDATA+12(2) HHMM  LAST MODIFIED    .SPF.
         OI        WORK1+2,X'0F'                                  .SPF.
         UNPK      28(4,R15),WORK1(3)                             .SPF.
         MVC       27(2,R15),28(R15)                              .SPF.
         MVI       29(R15),C':'                                   .SPF.
         LH        R0,USERDATA+14  CURRENT SIZE                   .SPF.
         N         R0,=A(X'0000FFFF')                             .SPF.
         CVD       R0,WORK1                                       .SPF.
         MVC       32(6,R15),=X'402020202120'                     .SPF.
         ED        32(6,R15),WORK1+5                              .SPF.
         LH        R0,USERDATA+16  INITIAL SIZE                   .SPF.
         N         R0,=A(X'0000FFFF')                             .SPF.
         CVD       R0,WORK1                                       .SPF.
         MVC       38(6,R15),=X'402020202120'                     .SPF.
         ED        38(6,R15),WORK1+5                              .SPF.
         LH        R0,USERDATA+18  LINES MODIFIED                 .SPF.
         N         R0,=A(X'0000FFFF')                             .SPF.
         CVD       R0,WORK1                                       .SPF.
         MVC       44(6,R15),=X'402020202120'                     .SPF.
         ED        44(6,R15),WORK1+5                              .SPF.
         MVC       52(10,R15),USERDATA+20  USER ID                .SPF.
         B         MAIN33                                         .SPF.
JULIAN   STM       R15,R1,JULIANS                                 .SPF.
         XC        WORK2,WORK2                                    .SPF.
         ST        R0,WORK2+4                                     .SPF.
         CVB       R1,WORK2            CONVERT DATE TO BINARY     .SPF.
         XR        R0,R0                                          .SPF.
         D         R0,=F'1000'         TO SEP YEAR AND DAY        .SPF.
         ST        R1,WORK2            SAVE YEAR                  .SPF.
         L         R15,=V(MONTHS1)     FOR STD YEAR               .SPF.
         TM        WORK2+3,X'03'       LEAP YEAR?                 .SPF.
         BNZ       *+8                 NO                         .SPF.
         L         R15,=V(MONTHS2)     FOR LEAP YEAR              .SPF.
         XR        R1,R1                                          .SPF.
JULIANX  SH        R0,0(R15)                                      .SPF.
         BNP       JULIANY                                        .SPF.
         LA        R1,3(R1)                                       .SPF.
         LA        R15,2(R15)                                     .SPF.
         B         JULIANX                                        .SPF.
JULIANY  AH        R0,0(R15)                                      .SPF.
         MH        R0,=H'10'                                      .SPF.
         CVD       R0,WORK2            FOR DAY OF MONTH           .SPF.
         AL        R1,=V(MONTHS3)      FOR MON IN CHARS           .SPF.
         MVC       WORK1(8),=X'4020204B4B4B2020'                  .SPF.
         MVC       WORK1+3(3),0(R1)                               .SPF.
         MVC       JULIANS+4(1),WORK2+6 DAY OF MONTH DDYYDDDF     .SPF.
         ED        WORK1(8),JULIANS+4                             .SPF.
         CLI       WORK1+1,C' '                                   .SPF.
         BNE       *+8                                            .SPF.
         MVI       WORK1+1,C'0'                                   .SPF.
         LM        R15,R1,JULIANS                                 .SPF.
         BR        R14                                            .SPF.
MAIN32X  EQU   *                                                  .SPF.
         CH        R0,=H'32'           ONLY FIRST 32 BYTES
         BNH       *+8
         LA        R0,32
         L         R11,=V(HEXCON)
         BALR      R10,R11
MAIN33   TM        SWA,SWA2            LIST DIR ONLY?
         BO        MAIN95              YES, SKIP PROCESSING
         TM        SWA,SWA1+SWA3       DECK OUTPUT WITH UPDTE
         BNO       MAIN35              NO
         BAL       R10,PUNCHIT         YES, OUTPUT CONTROL CARD
         MVC       50(L'HEAD1DAT,R1),HEAD1DAT
         MVC       50+L'HEAD1DAT(L'HEAD1TIM,R1),HEAD1TIM
         LH        R15,USERLEN         ANY USER INFO?
         CH        R15,=H'4'           (MUST BE 4 BYTES)
         BNE       *+12                NO, THERE NO SSI
         TM        SWA,SWA6            SSI SPECIFIED?
         BO        MAIN34              YES.
         MVC       0(L'UPDTE1,R1),UPDTE1  CC WITH NO SSI
         MVC       L'UPDTE1(L'NAME,R1),NAME
         CH        R15,=H'30'          ARE SPF STATS PRESENT      .SPF.
         BNE       MAIN35              NO, BRANCH                 .SPF.
         TM        SWB,SWB2+SWB5       SPF IN THIS PDS            .SPF.
         BNO       MAIN35              NO, BRANCH                 .SPF.
         LA        R15,L'UPDTE1+9(R1)  WHERE TO PUT THEM          .SPF.
         MVI       0(R15),C'-'         HYPHEN TO BE PROPOGATED    .SPF.
         MVC       1(49,R15),0(R15)    FILL AREA WITH HYPHENS     .SPF.
         SLR       R0,R0               CLEAR FOR INSERT           .SPF.
         IC        R0,USERDATA         GET V OF V.M               .SPF.
         CVD       R0,WORK1                                       .SPF.
         OI        WORK1+7,X'0F'                                  .SPF.
         UNPK      0(2,R15),WORK1+6(2)                            .SPF.
         IC        R0,USERDATA+1       GET M OF V.M               .SPF.
         CVD       R0,WORK1                                       .SPF.
         OI        WORK1+7,X'0F'                                  .SPF.
         UNPK      2(2,R15),WORK1+6(2)                            .SPF.
         UNPK      05(5,R15),USERDATA+4(4) YYDDD CREATED          .SPF.
         UNPK      11(5,R15),USERDATA+8(4) YYDDD LAST MODIFIED    .SPF.
         MVO       WORK1(3),USERDATA+12(2) HHMM  LAST MODIFIED    .SPF.
         OI        WORK1+2,X'0F'                                  .SPF.
         UNPK      17(4,R15),WORK1(3)                             .SPF.
         LH        R0,USERDATA+14      CURRENT SIZE               .SPF.
         N         R0,=A(X'0000FFFF')                             .SPF.
         CVD       R0,WORK1                                       .SPF.
         OI        WORK1+7,X'0F'                                  .SPF.
         UNPK      22(5,R15),WORK1+5(3)                           .SPF.
         LH        R0,USERDATA+16      INITIAL SIZE               .SPF.
         N         R0,=A(X'0000FFFF')                             .SPF.
         CVD       R0,WORK1                                       .SPF.
         OI        WORK1+7,X'0F'                                  .SPF.
         UNPK      28(5,R15),WORK1+5(3)                           .SPF.
         LH        R0,USERDATA+18      LINES MODIFIED             .SPF.
         N         R0,=A(X'0000FFFF')                             .SPF.
         CVD       R0,WORK1                                       .SPF.
         OI        WORK1+7,X'0F'                                  .SPF.
         UNPK      34(5,R15),WORK1+5(3)                           .SPF.
         MVC       40(10,R15),USERDATA+20  USER ID                .SPF.
         B         MAIN35
*MAIN34  MVC       0(L'UPDTE2,R1),UPDTE2  CC WITH SSI
*        MVC       L'UPDTE2(L'NAME,R1),NAME
*        LA        R15,11(R1)          FORMAT SSI
*                  NEW CODE FOR ADD NAME=MEMBER,SSI=XXXXXXXX      .PRC.
*                  INSTEAD OF   ADD SSI=XXXXXXXX,NAME=MEMBER      .PRC.
MAIN34   MVC       0(L'UPDTE1,R1),UPDTE1                          .PRC.
         MVC       L'UPDTE1(L'NAME,R1),NAME                       .PRC.
         LA        R15,L'UPDTE1+7(,R1) LAST BYTE OF NAME          .PRC.
         CLI       0(R15),C' '         LOOK FOR LAST NONBLANK     .PRC.
         BNE       *+8                 BRANCH IF FOUND            .PRC.
         BCT       R15,*-8             LOOP TO CLI                .PRC.
         MVC       1(5,R15),=C',SSI='  APPEND KEYWORD             .PRC.
         LA        R15,6(,R15)         POINT PAST KEYWORD         .PRC.
         LA        R0,4
         LA        R1,USERDATA
         L         R11,=V(HEXCON)
         BALR      R10,R11
MAIN35   L         R11,=V(GETBLK)      INIT READING
         BAL       R10,4(R11)
**
MAIN40   L         R11,=V(GETBLK)      TO READ IN THE NEXT BLK
         BALR      R10,R11
**
MAIN45   TM        SWA,SWA4            RITS/CRBE/CRJE?
         BO        MAIN50              YES.
         B         MAIN60              NO
 EJECT
** PROCESS A RITS/CRBE RECORD
MAIN50   TM        SWA,SWA0            LIST?
         BZ        MAIN55              NO.
         L         R11,=V(LOUT2)       YES, OUTPUT FORMATTED LINE
         BALR      R10,R11
         MVC       5(8,R1),0(R7)
         MVC       15(80,R1),8(R7)
         TM        SWA,SWA5            XLATE?
         BZ        *+14                NO.
         L         R15,TRTBL           YES, TRANSLATE OUTPUT
         TR        5(90,R1),0(R15)
MAIN55   TM        SWA,SWA1            DECK?
         BZ        MAIN80              NO.
         BAL       R10,PUNCHIT         YES, OUTPUT CARD IMAGE
         MVC       0(80,R1),8(R7)
         B         MAIN80
 EJECT
** 'LIST' PROCESSING
MAIN60   TM        SWA,SWA0            LIST?
         BZ        MAIN70              NO.
         TM        SAVRECFM,B'11000000'    RECFM=U?
         BO        *+12                YES
         TM        SAVRECFM,B'01000000'    RECFM=V?
         BO        MAIN61              YES
         LH        R3,DCBLRECL         RECFM = F OR U.
         LA        R2,0(R7)
         B         MAIN62
MAIN61   MVC       DCBLRECL,0(R7)
         LA        R2,4(R7)
         LH        R3,DCBLRECL
         SH        R3,=H'4'
MAIN62   TM        SWB,SWB1            HEXOUT?
         BO        MAIN66              YES.
         L         R11,=V(LOUT2)       OUTPUT FIRST LINE
         BALR      R10,R11
         LR        R15,R3
         CH        R3,=H'100'
         BNH       *+8
         LA        R15,100
         BCTR      R15,0
         EX        R15,VARMVC1
         TM        SWA,SWA5            XLATE?
         BZ        *+14                NO.
         L         R14,TRTBL
         TR        5(100,R1),0(R14)
         TM        SWA,SWA7            TRUNC?
         BO        MAIN70              YES.
MAIN64   LA        R2,100(R2)          OUTPUT OTHER LINES
         SH        R3,=H'100'
         BNP       MAIN70              DONE IF NO MORE DATA
         L         R11,=V(LOUT2)
         BALR      R10,R11
         MVC       110(3,R1),=C'***'   FLAG CONTINUATION LINE
         MVC       1(3,R1),=C'***'
         LR        R15,R3
         CH        R3,=H'100'
         BNH       *+8
         LA        R15,100
         BCTR      R15,0
         EX        R15,VARMVC1
         TM        SWA,SWA5            XLATE?
         BZ        *+14                NO.
         L         R14,TRTBL
         TR        05(100,R1),0(R14)
         B         MAIN64
**
MAIN66   L         R11,=V(LOUT2)       OUTPUT HEX
         BALR      R10,R11
         LA        R15,5(R1)
         LR        R0,R3
         CH        R3,=H'50'           FIFTY BYTES PER LINE
         BNH       *+8
         LA        R0,50
         LR        R1,R2
         L         R11,=V(HEXCON)
         BALR      R10,R11
         TM        SWA,SWA7            TRUNC?
         BO        MAIN70              YES.
MAIN67   LA        R2,50(R2)           OUTPUT OTHER LINES
         SH        R3,=H'50'
         BNP       MAIN70              DONE
         L         R11,=V(LOUT2)
         BALR      R10,R11
         MVC       110(3,R1),=C'***'   FLAG CONTINUATION LINE
         MVC       1(3,R1),=C'***'
         LA        R15,5(R1)
         LR        R0,R3
         CH        R3,=H'50'
         BNH       *+8
         LA        R0,50
         LR        R1,R2
         L         R11,=V(HEXCON)
         BALR      R10,R11
         B         MAIN67              LOOP
 EJECT
** 'DECK' PROCESSING
MAIN70   TM        SWA,SWA1            DECK?
         BZ        MAIN80              NO.
         TM        SAVRECFM,B'11000000'    RECFM=U?
         BO        *+12                YES
         TM        SAVRECFM,B'01000000'    RECFM=V?
         BO        MAIN71              YES
         LH        R3,DCBLRECL         RECFM = U OR F
         LA        R2,0(R7)
         B         MAIN72
MAIN71   MVC       DCBLRECL,0(R7)
         LA        R2,4(R7)
         LH        R3,DCBLRECL
         SH        R3,=H'4'
MAIN72   BAL       R10,PUNCHIT         OUTPUT ONE CARD IMAGE
         TM        SWA,SWA3            UPDTE                      .PRC.
         BNO       MAIN73                                         .PRC.
         CH        R3,=H'80'           IS LRECL=80                .PRC.
         BNE       MAIN73                                         .PRC.
         CLC       0(2,R2),=C'./'      IS THIS AN UPDTE STATEMENT .PRC.
         BNE       MAIN73                                         .PRC.
         MVC       0(2,R2),UPDTESUB    YES, SUBSTITUTE CHARS      .PRC.
MAIN73   EQU       *                                              .PRC.
         LR        R15,R3
         CH        R3,=H'80'
         BNH       *+8
         LA        R15,80
         BNE       MAIN74              NOT CARD IMAGES            .DHC.
         TM        SWA,SWA7            TRUNC CARD IMAGE? (NONUM)  .DHC.
         BNO       MAIN74              NO, COPY WHOLE IMAGE       .DHC.
         LA        R15,72              YES, SET SHORT LENGTH      .DHC.
         CLC       =C'PDSDOC',9(R2)    IS IT A 'PDSDOC'?          .DHC.
         BNE       MAIN74              NO, TRUNCATE IT            .DHC.
         MVC       72(8,R2),NAME       YES, PUT NAME IN SEQ. FIELD.DHC.
         LA        R15,80              AND DO NOT TRUNCATE        .DHC.
MAIN74   DS        0H                                             .DHC.
         BCTR      R15,0
         EX        R15,VARMVC3
         TM        SWA,SWA7            TRUNC?
         BO        MAIN80              YES.
         LA        R2,80(R2)
         SH        R3,=H'80'
         BP        MAIN72              CONTINUE PUNCHING
         B         MAIN80              PUNCHING COMPLETE.
 EJECT
** END OF LOGICAL RECORD
MAIN80   LA        R5,1(R5)            UPDATE REC COUNT
         TM        SAVRECFM,B'11000000'    RECFM=U?
         BO        MAIN40              YES, GO GET NEXT BLOCK.
         TM        SAVRECFM,B'01000000'  RECFM=V?
         BZ        *+10                NO
         MVC       DCBLRECL,0(R7)      YES, USE RECORD DESC. LEN
         AH        R7,DCBLRECL         UPDATE BUFFER POINTER
         SH        R6,DCBLRECL         UPDATE BYTES COUNT
         BP        MAIN45              CONTINUE WITH THIS BLOCK
         B         MAIN40              GO GET NEXT BLOCK
 SPACE 2
** END OF MEMBER
MAIN90   CVD       R5,WORK2
         AL        R5,RECORDS          UPDATE TOTAL REC COUNT
         ST        R5,RECORDS
         TM        SWA,SWA0            LIST?
         BZ        MAIN93              NO.
         L         R11,=V(LOUT2)       YES
         LA        R1,2                SKIP A LINE
         BAL       R10,4(R11)
         MVC       0(MSG2L,R1),MSG2
         ED        L'MSG2(L'MSG2A,R1),WORK2+5
MAIN93   L         R2,LASTLINE
         MVC       24(L'MSG2A,R2),MSG2A
         ED        24(L'MSG2A,R2),WORK2+5
         LH        R0,USERLEN          ANY MORE INFO TO PRINT?
         SH        R0,=H'32'
         BNP       MAIN95              NO.
         L         R11,=V(PRNT)        YES, PRINT REST OF INFO
         BALR      R10,R11
         LA        R15,L'CAPTIONS(R1)
         LH        R0,USERLEN
         SH        R0,=H'32'
         LA        R1,USERDATA+32
         L         R11,=V(HEXCON)
         BALR      R10,R11
MAIN95   TM        SWD,SWD1            ALIAS?
         BZ        MAIN00              NO.
         L         R11,=V(PRNT)        YES, OUTPUT ALIAS MSG
         BALR      R10,R11
         MVC       16(L'MSG5,R1),MSG5
         MVC       16+L'MSG5(L'NAME2,R1),NAME2
         B         MAIN00
 TITLE 'PROGRAM INITIALIZATION'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** PROGRAM INITIALIZATION ROUTINE                                   **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
PGMINIT  CSECT
         USING     PGMINIT,R11         SET ADDRESSABILITY
         XC        SWITCHES,SWITCHES   CLEAR SIWTCHES, ETC.
         MVC       SWA,SWAX
         MVC       SWB,SWBX
         XC        RECORDS,RECORDS     CLEAR COUNTER
         XC        TTRK,TTRK           FOR FIND
         XC        RETCODE,RETCODE
         XC        PAGENUM1,PAGENUM1
         XC        PAGENUM2,PAGENUM2
         XC        LINECNT1,LINECNT1
         XC        LINECNT2,LINECNT2
         XC        BUFFER1,BUFFER1     CLEAR BUFFERS
         XC        BUFFER2,BUFFER2
         LA        R15,&MAXLINE        SET DEFAULT LINE COUNT
         STH       R15,LINEMAX
         XC        TOTALREC,TOTALREC
         XC        NAMETBL1,NAMETBL1
         XC        NAMETBL2,NAMETBL2
         XC        JFCB1,JFCB1
         XC        JFCB2,JFCB2
**
         MVC       DDPRINT(RESETDDL),RESETDD
         TM        0(R4),X'80'         ONLY EXEC PARMS SPECIFIED?
         BO        EXTRACT             YES.
         L         R2,4(R4)            NO, INIT THE DDNAMES.
         LH        R3,0(R2)            GET NO. OF BYTES IN LIST
         LTR       R3,R3               IS IT ZERO?
         BNP       XPA2                YES, SKIP
         CH        R3,=H'40'
         BNH       *+8
         LA        R3,40
         LA        R2,2(R2)            BEGINNING OF DD LIST
         LA        R3,2(R3,R2)         END OF DD LIST
         LA        R1,DDPRINT          LISTPDS'S DD LIST
XPA1     CR        R2,R3               END OF LIST?
         BNL       XPA2                YES, GOTO NEXT ONE
         CLI       0(R2),0             DUMMY ENTRY?
         BE        *+10                YES, SKIP MOVE
         MVC       0(8,R1),0(R2)
         LA        R1,8(R1)
         LA        R2,8(R2)
         B         XPA1
XPA2     TM        4(R4),X'80'         END OF PARM LIST?
         BO        EXTRACT             YES
         L         R2,8(R4)
         L         R2,0(R2)
         STH       R2,PAGENUM1         INITIAL PAGE NO - SYSPRINT
         TM        8(R4),X'80'
         BO        EXTRACT
         L         R2,12(R4)
         L         R2,0(R2)
         STH       R2,PAGENUM2         INITIAL PAGE NO - SYSLIST
**
** GET ADDRESS OF TIOT
*XTRACT  EXTRACT   TIOT,'S',FIELDS=(TIOT)                         .EXT.
EXTRACT  L         R1,16               CVTPTR                     .EXT.
         L         R1,0(,R1)           TCB WORDS                  .EXT.
         L         R1,4(,R1)           CURRENT TCB                .EXT.
         L         R1,12(,R1)          TIOT                       .EXT.
         ST        R1,TIOT             SAVE ADDRESS OF TIOT       .EXT.
** INIT SYSPRINT FILE
         LA        R8,SYSPRINT
         MVC       DCBDDNAM,DDPRINT
         OPEN      (SYSPRINT,OUTPUT)
         TM        DCBOFLGS,OFLGS      OPEN OK?
         BO        PGMINIT1            YES.
** SYSPRINT OPEN ERROR
         L         R2,TIOT
         MVC       WTP+17(8),0(R2)     MOVE JOBNAME TO MSG.
         MVC       WTP+40(L'DDPRINT),DDPRINT
WTP      WTO       'LPDS00I  JOBNAMEX OPEN ERROR ON SYSPRINT',         +
               ROUTCDE=(11),DESC=7
         TM        SWB,SWB0            DEBUG?
         BZ        ABORT               NO
         ABEND     20,DUMP             YES, ABEND TO GET DUMP.
ABORT    L         R13,4(R13)          ELSE ... ABORT
         RETURN    (14,12),T,RC=20
** INIT STAE EXIT
PGMINIT1 STAE      STAEXIT,CT
         B         PA
RESETDD  DC        CL8'&DDPRINT',CL8'&DDLIST',CL8'&DDPUNCH'
         DC        CL8'&DDLIB',CL8'&DDIN'
RESETDDL EQU       *-RESETDD
 EJECT
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** PARAMETER  ANALYSIS  ROUTINE                                     **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
PA       L         R2,0(R4)
         LH        R1,0(R2)
         LA        R2,2(R2)            BEGINNING OF STRING
         LTR       R1,R1               ANY PARMS SEPECIFIED?
         BZ        PGMINITA            NO PARMS...CONTINUE INIT
         LA        R3,0(R1,R2)         END OF STRING
**
PALOOP   CR        R2,R3               END OF PARMS?
         BNL       PGMINITA            YES, GOTO COMPLETE INIT
         CLI       0(R2),C','          NULL PARM?
         BE        PAEND               YES.
         CLC       =C'LISTDIR',0(R2)
         BE        PA01
         CLC       =C'DEBUG',0(R2)
         BE        PA02
         CLC       =C'NODEBUG',0(R2)
         BE        PA03
         CLC       =C'LIST',0(R2)
         BE        PA04
         CLC       =C'NOLIST',0(R2)
         BE        PA05
         CLC       =C'DECK',0(R2)
         BE        PA06
         CLC       =C'NODECK',0(R2)
         BE        PA07
         CLC       =C'UPDTE',0(R2)
         BE        PA08
         CLC       =C'NOUPDTE',0(R2)
         BE        PA09
         CLC       =C'SSI',0(R2)
         BE        PA10
         CLC       =C'NOSSI',0(R2)
         BE        PA11
         CLC       =C'TEXT',0(R2)
         BE        PA12
         CLC       =C'NOTEXT',0(R2)
         BE        PA13
         CLC       =C'RITS',0(R2)
         BE        PA14RITS
         CLC       =C'CRBE',0(R2)
         BE        PA14CRBE
         CLC       =C'CRJE',0(R2)
         BE        PA14CRJE
         CLC       =C'LINECNT=',0(R2)
         BE        PA15
         CLC       =C'XLATE',0(R2)
         BE        PA16
         CLC       =C'NOXLATE',0(R2)
         BE        PA17
         CLC       =C'TRUNC',0(R2)
         BE        PA18
         CLC       =C'NOTRUNC',0(R2)
         BE        PA19
         CLC       =C'HEXOUT',0(R2)
         BE        PA20
         CLC       =C'NOHEXOUT',0(R2)
         BE        PA21
         CLC       =C'EROPT=',0(R2)
         BE        PA22
         CLC       =C'SPF',0(R2)                                  .SPF.
         BE        PA23                                           .SPF.
         CLC       =C'NOSPF',0(R2)                                .SPF.
         BE        PA24                                           .SPF.
         CLC       =C'SEL',0(R2)                                  .SEL.
         BE        PA25                                           .SEL.
         CLC       =C'NOSEL',0(R2)                                .SEL.
         BE        PA26                                           .SEL.
PAERROR  OI        SWC,SWC0            FLAG PARM ERROR
         B         PGMINITA            AND GOTO COMPLETE INIT.
**
PAEND    CLI       0(R2),C','          COMMA?
         BNE       PALOOP              NO, LOOP.
         LA        R2,1(R2)            YES, BUMP OVER IT.
         B         PALOOP
**
PA01     OI        SWA,SWA2            LISTDIR
         NI        SWA,255-SWA0        ALSO NO LIST
         LA        R2,7(R2)
         B         PAEND
PA02     OI        SWB,SWB0            DEBUG
         LA        R2,5(R2)
         B         PAEND
PA03     NI        SWB,255-SWB0        NODEBUG
         LA        R2,7(R2)
         B         PAEND
PA04     OI        SWA,SWA0            LIST
         LA        R2,4(R2)
         B         PAEND
PA05     NI        SWA,255-SWA0        NOLIST
         LA        R2,6(R2)
         B         PAEND
PA06     OI        SWA,SWA1            DECK
         LA        R2,4(R2)
         B         PAEND
PA07     NI        SWA,255-SWA1        NODECK
         LA        R2,6(R2)
         B         PAEND
PA08     OI        SWA,SWA3            UPDTE CONTROL CARDS
         LA        R2,5(R2)
         CLI       0(R2),C'('          IS IT UPDTE(XX)            .PRC.
         BNE       PAEND                                          .PRC.
         CLI       3(R2),C')'          IS IT UPDTE(XX)            .PRC.
         BNE       PAEND                                          .PRC.
         MVC       UPDTESUB,1(R2)      XX WILL REPLACE './'       .PRC.
         LA        R2,4(,R2)                                      .PRC.
         B         PAEND
PA09     NI        SWA,255-SWA3        NO UPDTE CONTROL CARDS
         LA        R2,7(R2)
         B         PAEND
PA10     OI        SWA,SWA6            SSI
         LA        R2,3(R2)
         B         PAEND
PA11     NI        SWA,255-SWA6        NOSSI
         LA        R2,5(R2)
         B         PAEND
PA12     LA        R2,4(R2)            TEXT
         OI        SWB,SWB3
         B         PAEND
PA13     LA        R2,6(R2)            NOTEXT
         NI        SWB,255-SWB3
         B         PAEND
PA14RITS NI        SWD,255-SWD5        FLAG RITS
         OI        SWD,SWD4
         B         PA14
PA14CRBE NI        SWD,255-SWD4        FLAG CRBE
         OI        SWD,SWD5
         B         PA14
PA14CRJE OI        SWD,SWD4+SWD5       FLAG CRJE
PA14     OI        SWA,SWA4            FLAG RITS/CRBE/CRJE
         LA        R2,4(R2)
         B         PAEND
PA15     LA        R2,8(R2)            LINECNT
         XR        R15,R15
         CR        R2,R3
         BNL       PAERROR
PA15A    CR        R2,R3
         BNL       PA15B
         CLI       0(R2),C','
         BE        PA15B
         CLI       0(R2),C'0'
         BL        PAERROR
         CLI       0(R2),C'9'
         BH        PAERROR
         MH        R15,=H'10'
         IC        R1,0(R2)            PICK UP CHAR
         N         R1,=F'15'           AND MASK OFF HIGH BITS
         LA        R15,0(R1,R15)
         LA        R2,1(R2)
         B         PA15A
PA15B    N         R15,=F'32767'
         STH       R15,LINEMAX
         B         PAEND
PA16     OI        SWA,SWA5            XLATE
         LA        R2,5(R2)
         B         PAEND
PA17     NI        SWA,255-SWA5        NOXLATE
         LA        R2,7(R2)
         B         PAEND
PA18     OI        SWA,SWA7            TRUNC
         LA        R2,5(R2)
         B         PAEND
PA19     NI        SWA,255-SWA7        NOTRUNC
         LA        R2,7(R2)
         B         PAEND
PA20     OI        SWB,SWB1            HEXOUT
         LA        R2,6(R2)
         B         PAEND
PA21     NI        SWB,255-SWB1        NOHEXOUT
         LA        R2,8(R2)
         B         PAEND
PA22     LA        R2,6(R2)            EROPT=
         CLC       =C'TERM',0(R2)
         BNE       *+16
         LA        R2,4(R2)
         NI        SWB,255-SWB4
         B         PAEND
         CLC       =C'ACC',0(R2)
         BNE       PAERROR
         LA        R2,3(R2)
         OI        SWB,SWB4
         B         PAEND
PA23     OI        SWB,SWB2            SPF                        .SPF.
         LA        R2,3(R2)                                       .SPF.
         B         PAEND                                          .SPF.
PA24     NI        SWB,255-SWB2        NOSPF                      .SPF.
         LA        R2,5(R2)                                       .SPF.
         B         PAEND                                          .SPF.
PA25     OI        SWB,SWB7            SEL                        .SEL.
         LA        R2,3(R2)                                       .SEL.
         B         PAEND                                          .SEL.
PA26     NI        SWB,255-SWB7        NOSEL                      .SEL.
         LA        R2,5(R2)                                       .SEL.
         B         PAEND                                          .SEL.
 EJECT
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** PGM INITIALIZATION CONTINUED                                     **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
PGMINITA MVC       HEAD1(L'HEAD1R),HEAD1R  INIT HEADER LINE
         L         R15,=V(XLATE1)      INIT XLATE TABLE
         TM        SWB,SWB3            TEXT?
         BZ        *+8                 NO.
         L         R15,=V(XLATE2)      YES.
         ST        R15,TRTBL
         TM        SWD,SWD4+SWD5       RITS/CRBE/CRJE?
         BZ        *+12                NO.
         OI        SWA,SWA4            YES.
         NI        SWA,255-SWA6        NO SSI IF RITS/CRBE
** GET AND FORMAT VOL, DSN FOR HEADER
         LA        R8,SYSLIB           INIT DDNAME
         MVC       DCBDDNAM,DDLIB
         GETMAIN   R,LV=176            FOR JFCB
         ST        R1,JFCBADRS
         MVI       JFCBADRS,X'87'
         MVI       0(R1),X'00'
         LR        R2,R1               SAVE POINTER
         RDJFCB    SYSLIB
         MVI       JFCBADRS,X'80'      TURN OFF EXIT
         CLI       0(R2),X'00'         DD THERE?
         BNE       PGMINITB            YES.
         BAL       R14,PUTMSG          NO, OUTPUT MESSAGE
         MVC       0(L'LPDS04I,R1),LPDS04I
         MVC       L'LPDS04I(8,R1),DDLIB
         FREEMAIN  R,LV=176,A=(R2)
         B         TERMINAT            AND TERMINATE.
PGMINITB MVC       HEAD1DSN,JFCBDSNM(R2)  MOVE DSN TO HEADER
         MVC       HEAD1VOL,JFCBVOLS(R2)  MOVE VOLSER TO HEADER
         FREEMAIN  R,LV=176,A=(R2)
** GET DATE AND TIME FOR HEADING
         TIME      DEC
         ST        R0,WORK1
         XC        WORK2,WORK2         COMPUTE AND FORMAT DATE
         ST        R1,WORK2+4
         MVC       WORK1+5(1),WORK2+5  SAVE YEAR
         CVB       R3,WORK2            CONVERT DATE TO BINARY
         XR        R2,R2
         D         R2,=F'1000'         TO SEP YEAR AND DAY
         ST        R3,WORK2
         L         R3,=V(MONTHS1)      FOR STD YEAR
         TM        WORK2+3,X'03'       LEAP YEAR?
         BNZ       *+8                 NO
         L         R3,=V(MONTHS2)      FOR LEAP YEAR
         XR        R1,R1
PGMINITX SH        R2,0(R3)
         BNP       PGMINITY
         LA        R1,3(R1)
         LA        R3,2(R3)
         B         PGMINITX
PGMINITY AH        R2,0(R3)
         MH        R2,=H'10'
         CVD       R2,WORK2            FOR DAY OF MONTH
         AL        R1,=V(MONTHS3)      FOR MON IN CHARS
         MVC       HEAD1DAT+3(3),0(R1)
         MVC       WORK1+4(1),WORK2+6  DAY OF MONTH
         ED        HEAD1DAT,WORK1+4
         CLI       HEAD1DAT+1,C' '
         BNE       *+8
         MVI       HEAD1DAT+1,C'0'
         ED        HEAD1TIM,WORK1
         CLI       HEAD1TIM+1,C' '
         BNE       *+8
         MVI       HEAD1TIM+1,C'0'
         MVC       HEAD2DAT,HEAD1DAT
         MVC       HEAD2TIM,HEAD1TIM
         MVC       HEAD1PAG,PAGEPAT
         LH        R1,PAGENUM1
         LA        R1,1(R1)
         STH       R1,PAGENUM1
         CVD       R1,WORK1
         ED        HEAD1PAG,WORK1+4
         BAL       R14,PUTMSG
         MVC       0(121,R1),HEAD1
         LA        R15,1
         STH       R15,LINECNT1
         BR        R10                 RETURN TO CALLER
 SPACE 2
MONTHS1  CSECT     ,                   STD YEAR
         DC        H'31,28,31,30,31,30,31,31,30,31,30,31'
MONTHS2  CSECT     ,                   LEAP YEAR
         DC        H'31,29,31,30,31,30,31,31,30,31,30,31'
MONTHS3  CSECT     ,                   MONTHS
         DC        C'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'
 TITLE 'INITIALIZE THE MAIN FILES'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** INITIALIZE THE MAIN LISTPDS FILES                                **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
FILEINIT CSECT
         USING     FILEINIT,R11
         LA        R8,SYSLIB
         OPEN      (SYSLIB,INPUT)
         TM        DCBOFLGS,OFLGS      OPEN OK?
         BO        FI07                YES.
         BAL       R14,PUTMSG          NO, ISSUE MESSAGE
         MVC       0(L'LPDS04I,R1),LPDS04I
         MVC       L'LPDS04I(8,R1),DDLIB
         B         TERMINAT
 SPACE 2
** JFCB INIT FOR SYSLIB CONCATONATION
FI07     MVC       WORK1(L'DCBTIOT),DCBTIOT  SAVE ENTRY
         LH        R1,SAVBLKSI         INIT BUFFER LENGTH
         ST        R1,BUFFER0
         LH        R2,DCBTIOT
         L         R3,TIOT
         LA        R3,0(R3,R2)         PNTR TO SYSLIB ENTRY
         LA        R4,JFCB1
         L         R5,=V(DSCB)
         USING     DSCB,R5
FI07A    XR        R1,R1
         IC        R1,0(R3)
         LA        R2,0(R2,R1)         POINT TO NEXT ENTRY
         LA        R3,0(R3,R1)
         CLI       0(R3),0             END OF TIOT?
         BE        FI07Z               YES
         CLC       =CL8' ',4(R3)       CONCATONATION?
         BNE       FI07Z               NO, END.
         GETMAIN   R,LV=260            ALLOC BLOCK FOR JFCB
         ST        R1,0(R4)
         XC        0(4,R1),0(R1)
         LR        R4,R1
         LA        R1,4(R1)
         ST        R1,JFCBADRS
         MVI       JFCBADRS,X'87'
         STH       R2,DCBTIOT
         RDJFCB    SYSLIB
         MVC       DSNAME,4+JFCBDSNM(R4)
         MVC       VOLSER,4+JFCBVOLS(R4)
         OBTAIN    DSCB
         MVC       DSXRECFM+4(1,R4),WORKAREA+DS1RECFM
         MVC       DSXBLKSI+4(2,R4),WORKAREA+DS1BLKSI
         MVC       DSXLRECL+4(2,R4),WORKAREA+DS1LRECL
         LH        R1,JFCBLKSI(R4)
         LTR       R1,R1
         BNZ       *+8
         LH        R1,WORKAREA+DS1BLKSI
         C         R1,BUFFER0
         BL        *+8
         ST        R1,BUFFER0
         B         FI07A
FI07Z    MVI       JFCBADRS,X'80'      END OF JFCB INIT
         MVC       DCBTIOT,WORK1       RESET TIOT PNTR
         MVC       JFCB2,JFCB1
         DROP      R5
**
         TM        SWA,SWA2            LIST DIR ONLY?
         BOR       R10                 YES.
** BUFFER ALLOCATION
         L         R2,BUFFER0
         GETMAIN   R,LV=(R2)
         ST        R1,BUFFER1
         CLI       SAVBUFNO,2          DOUBLE BUFFERING?
         BNE       FI10                NO.
         GETMAIN   R,LV=(R2)
         ST        R1,BUFFER2
**
FI10     TM        SWA,SWA0            LIST?
         BZ        FI20                NO.
         LA        R8,SYSLIST
         MVC       DCBDDNAM,DDLIST
         OPEN      (SYSLIST,OUTPUT)
         TM        DCBOFLGS,OFLGS      OPEN OK?
         BO        FI20                YES.
         NI        SWA,255-SWA0        NO, SET NOLIST.
         OI        SWC,SWC1            ISSUE WARNING MSG
         BAL       R14,PUTMSG
         MVC       0(LPDS09IL,R1),LPDS09I
         MVC       L'LPDS09I(L'DDLIST,R1),DDLIST
         LA        R1,4
         BAL       R14,SAVERC
**
FI20     LA        R8,SYSPUNCH
         MVC       DCBDDNAM,DDPUNCH
         TM        SWA,SWA1            NODECK?
         BZR       R10                 YES.
         OPEN      (SYSPUNCH,OUTPUT)
         TM        DCBOFLGS,OFLGS      OPEN OK?
         BOR       R10                 YES.
         NI        SWA,255-SWA1        TURN OFF DECK OPTION
         OI        SWC,SWC1            ISSUE WARNING MSG
         BAL       R14,PUTMSG
         MVC       0(LPDS09IL,R1),LPDS09I
         MVC       L'LPDS09I(L'DDPUNCH,R1),DDPUNCH
         LA        R1,4
         BAL       R14,SAVERC
         BR        R10
**
LPDS09I  DC        C'0LPDS09I  UNABLE TO OPEN ',CL8' '
         DC        C'.  OPTION DELETED.'
LPDS09IL EQU       *-LPDS09I
 TITLE 'CHECK LIBRARY''S DCB PARAMETERS'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** CHKLIB -- CHECK LIBRARY'S DCB PARAMETERS                         **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
CHKLIB   CSECT
         USING     CHKLIB,R11
         LA        R8,SYSLIB
**
         LH        R15,SAVBLKSI        BLKSIZE SPECIFIED?
         LTR       R15,R15
         BNZ       *+24                YES.
         BAL       R14,PUTMSG          NO, ISSUE MESSAGE
         MVC       0(L'LPDS06I,R1),LPDS06I
         MVC       L'LPDS06I(L'LPDS06IA,R1),LPDS06IA
         B         TERMINAT
         TM        SAVRECFM,B'00000010'  MACH. CONTROL CHARS?
         BZ        *+30                NO.
         BAL       R14,PUTMSG          YES, ISSUE MESSAGE
         MVC       0(L'LPDS06I,R1),LPDS06I
         MVC       L'LPDS06I(L'LPDS06I1,R1),LPDS06I1
         MVC       L'LPDS06I+L'LPDS06I1(L'LPDS06I0,R1),LPDS06I0
         B         TERMINAT
         TM        SAVRECFM,B'11000000'
         BO        CHKLIB11
         TM        SAVRECFM,B'01001000'  VARIABLE SPANNED?
         BNO       CHKLIB11            NO
         BAL       R14,PUTMSG          YES, ISSUE MESSAGE
         MVC       0(L'LPDS06I,R1),LPDS06I
         MVC       L'LPDS06I(L'LPDS06I2,R1),LPDS06I2
         MVC       L'LPDS06I+L'LPDS06I2(L'LPDS06I0,R1),LPDS06I0
         B         TERMINAT
 SPACE 1
** RITS/CRBE
CHKLIB11 TM        SWA,SWA4            RITS/CRBE/CRJE SPECIFIED?
         BZ        CHKLIB15            NO.
         TM        SAVRECFM,B'11000000'  YES, CHECK PARMS
         BO        CHKLIB12            INVALID
         TM        SAVRECFM,B'10000000'
         BZ        CHKLIB12            INVALID
         LH        R15,SAVLRECL
         CH        R15,=H'88'
         BNE       *+8
         B         CHKLIB40
CHKLIB12 NI        SWA,255-SWA4        TURN OFF RITS/CRBE
         OI        SWC,SWC1            FLAG REINIT SYSPRINT
         BAL       R14,PUTMSG          ISSUE MESSAGE
         MVC       0(L'LPDS07I,R1),LPDS07I
         LA        R1,4                RC=4
         BAL       R14,SAVERC
** CHECK RECFM TYPES
CHKLIB15 TM        SAVRECFM,B'11000000'    RECFM=U?
         BO        CHKLIB16
         TM        SAVRECFM,B'10000000'    RECFM=F?
         BO        CHKLIB17
         TM        SAVRECFM,B'01000000'    RECFM=V?
         BO        CHKLIB18
         BAL       R14,PUTMSG          INVALID RECFM ...
         MVC       0(L'LPDS06I,R1),LPDS06I
         MVC       L'LPDS06I(L'LPDS06IB,R1),LPDS06IB
         B         TERMINAT
** RECFM=U
CHKLIB16 LH        R15,SAVLRECL
         LTR       R15,R15
         BNZ       *+8
         LH        R15,SAVBLKSI
         CH        R15,SAVBLKSI
         BNH       *+8
         LH        R15,SAVBLKSI
         STH       R15,SAVLRECL
         B         CHKLIB40
** RECFM=F
CHKLIB17 TM        SAVRECFM,B'00010000'    BLOCKED?
         BO        *+16
         LH        R15,SAVBLKSI
         STH       R15,SAVLRECL
         B         CHKLIB40
         LH        R15,SAVLRECL
         LTR       R15,R15
         BNZ       *+8
         LH        R15,SAVBLKSI
         STH       R15,SAVLRECL
         B         CHKLIB40
** RECFM=V
CHKLIB18 TM        SAVRECFM,B'00100000'  TRACK OVERFLOW?
         BZ        CHKLIB40            NO.
         BAL       R14,PUTMSG          YES, ISSUE MESSAGE
         MVC       0(L'LPDS06I,R1),LPDS06I
         MVC       L'LPDS06I(L'LPDS06I3,R1),LPDS06I3
         MVC       L'LPDS06I+L'LPDS06I3(L'LPDS06I0,R1),LPDS06I0
         B         TERMINAT
** END OF LIB CHECK
CHKLIB40 BR        R10
 TITLE 'BUILD THE MEMBER NAME TABLE'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** BUILD THE MEMBER NAME TABLE                                      **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
BLDNAMET CSECT
         USING     BLDNAMET,R11
         LA        R8,SYSLIB
         MVC       WORK1(4),DCBEODAD   INIT DCB FOR
         MVC       DCBEODAD+1(3),BLDNAMX1+1 DIRECTORY
         MVI       DCBRECFM,B'10000000'    RECFM=F
         MVC       DCBLRECL,=H'256'
         MVC       DCBBLKSI,=H'256'
         MVC       DCBSYNAD+1(3),BLDNAMX2+1
         NI        SWB,255-SWB5        RESET SPF-THIS-PDS FLAG    .SPF.
         LA        R3,NAMETBL1
**
BLDNAM01 L         R1,0(R3)            PNTR TO CORE BLOCK.
         LTR       R1,R1               BLOCK ALLOC?
         BNZ       BLDNAM02            YES.
         GETMAIN   R,LV=260            NO, ALLOC IT.
         XC        0(4,R1),0(R1)
         ST        R1,0(R3)
BLDNAM02 LR        R3,R1
         LA        R2,4(R1)
         MVI       6(R1),X'FF'
         MVC       4(2,R1),=H'2'
         XC        DIRDECB,DIRDECB     CLEAR ECB
         READ      DIRDECB,SF,SYSLIB,(R2),'S'
         CHECK     DIRDECB
** GOT IT, NOW ANALYZE TO DIRECTORY BLOCK.
         LH        R1,0(R2)
         LA        R15,2
         LA        R2,2(R2)
         LA        R0,30               LENGTH OF SPF STATS        .SPF.
BLDNAM05 CLI       0(R2),X'FF'         END OF THIS DIRECTORY?
         BE        BLDNAM95            YES.
         IC        R14,11(R2)
         N         R14,=F'31'
         SLL       R14,1
         CR        R14,R0              ARE SPF STATISTICS PRESENT .SPF.
         BNE       BLDNOSPF            NO, BRANCH                 .SPF.
         TM        11(R2),X'60'        ANY TTR'S IN USERDATA      .SPF.
         BNZ       BLDNOSPF            YES, NOT SPF DATA          .SPF.
         OI        SWB,SWB5+SWB6       YES, SPF MEMBER FOUND      .SPF.
BLDNOSPF EQU       *                                              .SPF.
         LA        R2,12(R2,R14)
         LA        R15,12(R15,R14)
         CR        R15,R1
         BL        BLDNAM05  CONTINUE WITH THIS BLOCK
         B         BLDNAM01            GO PROCESS NEXT BLOCK
**
BLDNAM90 BAL       R14,PUTMSG          EOF ... ERROR
         MVC       0(L'LPDS08I,R1),LPDS08I
         B         TERMINAT
** END OF DIRECTORY.
BLDNAM95 MVC       CURRENT1,NAMETBL1
         MVC       CURRENT1+4,=F'6'
         MVC       DCBEODAD+1(3),WORK1+1      RESET DCB
         MVC       DCBRECFM,SAVRECFM
         MVC       DCBLRECL,SAVLRECL
         MVC       DCBBLKSI,SAVBLKSI
         MVC       DCBSYNAD+1(3),BLDNAMX3+1
         BR        R10                 TO CONTINUE PROCESSING
**
         DS        0F
BLDNAMX1 DC        AL1(0),AL3(BLDNAM90)
BLDNAMX2 DC        AL1(0),AL3(SYNERR1)
BLDNAMX3 DC        AL1(0),AL3(SYNERR2)
LPDS08I  DC        C'0LPDS08I  ERROR--PDS DIRECTORY NOT STANDARD FORMAT+
               .'
 TITLE 'BUILD THE SELECTED NAME TABLE'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** BUILD THE SELECTED NAME TABLE                                    **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
BLDSELCT CSECT
         USING     BLDSELCT,R11
         XC        NAMETBL2,NAMETBL2   SET PNTR TO NULL.
         TM        SWB,SWB7            SEL                        .SEL.
         BZ        BLDSEL99            BRANCH IF NOSEL            .SEL.
         L         R2,TIOT             SCAN THE TIOT FOR MATCH
         LA        R2,24(R2)
         XR        R3,R3
BLDSEL01 CLC       4(8,R2),DDIN
         BE        BLDSEL02            MATCH.
         CLI       0(R2),0             END OF TIOT?
         BE        BLDSEL99            YES, RETURN.
         IC        R3,0(R2)
         LA        R2,0(R2,R3)
         B         BLDSEL01
**
BLDSEL02 LA        R8,SYSIN
         MVC       DCBDDNAM,DDIN
         OPEN      (SYSIN,INPUT)
         TM        DCBOFLGS,OFLGS      OPEN OK?
         BZ        BLDSEL99            NO, RETURN.
         XR        R3,R3
         LA        R4,NAMETBL2
**
BLDSEL03 GET       SYSIN
         LA        R0,72(R1)           POINT AT END OF CARD.
         LA        R1,0(R1)
BLDSEL04 CLI       0(R1),C' '          BLANK?
         BE        BLDSEL03            YES, GO GET NEXT CARD.
         CR        R0,R1               END OF CARD?
         BL        BLDSEL03            YES, GO GET NEXT CARD.
         CLI       0(R1),C','          COMMA?
         BNE       BLDSEL05            NO.
         LA        R1,1(R1)            YES, SKIP OVER IT.
         B         BLDSEL04
BLDSEL05 LA        R14,WORK1
         MVC       WORK1,=CL8' '
**
BLDSEL06 CLI       0(R1),C' '          BLANK?
         BE        BLDSEL08            YES, END OF NAME
         CLI       0(R1),C','          COMMA?
         BE        BLDSEL08            YES, END OF NAME
         CR        R0,R1               END OF CARD?
         BL        BLDSEL08            YES, END OF NAME
         C         R14,=A(WORK1+8)    HAVE 8 BYTES BEEN PROCESSED?
         BNL       *+16                YES.
         IC        R15,0(R1)
         STC       R15,0(R14)
         LA        R14,1(R14)
         LA        R1,1(R1)
         B         BLDSEL06            GO GET NEXT CHAR.
**
BLDSEL08 LTR       R3,R3               BLOCK ALLOCATED?
         BNZ       BLDSEL10            YES.
         STM       R0,R1,WORK2         SAVE RECORD POINTERS
         GETMAIN   R,LV=260            ALLOC. AND INIT. BLOCK
         ST        R1,0(R4)
         LA        R3,256/8
         XC        0(4,R1),0(R1)
         LA        R2,4(R1)
         LR        R4,R1
         MVI       4(R1),C' '
         MVC       5(255,R1),4(R1)
         LM        R0,R1,WORK2         RESTORE RECORD POINTERS
BLDSEL10 MVC       0(8,R2),WORK1       INSERT NAME IN TABLE.
         LA        R2,8(R2)
         BCTR      R3,0
         B         BLDSEL04
**
BLDSEL90 L         R1,NAMETBL2         NULL?
         LTR       R1,R1
         BZ        *+8                 YES.
         OI        SWD,SWD0            NO, FLAG SELECTS.
         CLOSE     SYSIN
         FREEPOOL  SYSIN
BLDSEL99 MVC       CURRENT2,NAMETBL2   INIT POINTERS
         XC        CURRENT2+4,CURRENT2+4
         BR        R10
**
SYSIN    DCB       DSORG=PS,MACRF=(GL),DDNAME=&DDIN,EODAD=BLDSEL90,    +
               RECFM=FB,LRECL=80,EXLST=EXLST1
 TITLE 'RITS/CRBE DIRECTORY INFORMATION FORMAT ROUTINE'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** RITS/CRBE/CRJE DIRECTORY INFORMATION FORMAT ROUTINE             **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
RITSINFO CSECT
         USING     RITSINFO,R11
** UPON ENTRY, R1=A(BUFFER), R2=A(DIRECTORY ENTRY)
 SPACE 1
**
         CLI       TTR+3,5             CHECK LENGTH OF USER FIELD
         BL        UNKNOWN             SHORT.
         MVC       0(L'PAT1,R1),PAT1
         ED        0(L'PAT1,R1),0(R2)
         MVC       10(L'PAT1,R1),PAT1
         ED        10(L'PAT1,R1),3(R2)
         XC        WORK1,WORK1
         MVC       WORK1+2(2),6(R2)
         L         R15,WORK1
         CVD       R15,WORK1
         MVC       WORK2,PAT2
         ED        WORK2,WORK1+4
         MVC       23(4,R1),WORK2+4
         TM        SWD,SWD4+SWD5       CRJE?
         BO        CRJEINFO            YES.
**
CRBEINFO MVC       42(3,R1),=C'SEQ'
         TM        9(R2),X'40'         SEQ/NOSEQ?
         BO        *+10                SEQ.
         MVC       40(2,R1),=C'NO'
         MVC       52(4,R1),=C'SCAN'
         TM        9(R2),X'80'         SCAN/NOSCAN?
         BO        *+10
         MVC       50(2,R1),=C'NO'
         TM        8(R2),X'40'         FORTRAN FILE?
         BZ        *+12                NO.
         MVC       30(7,R1),=C'FORTRAN'
         BR        R10
         TM        8(R2),X'20'         OTHER FILE?
         BZ        *+12                NO.
         MVC       30(5,R1),=C'OTHER'
         BR        R10
         TM        8(R2),X'08'         FLIST FILE?
         BZ        *+12                NO.
         MVC       30(5,R1),=C'FLIST'
         BR        R10
         TM        SWD,SWD5            RITS?
         BZ        *+20                YES
         TM        8(R2),X'10'         OBJMOD FILE?
         BZ        *+12                NO.
         MVC       30(6,R1),=C'OBJMOD'
         BR        R10
UNKNOWN  MVC       30(7,R1),=C'???????'
         BR        R10
**
CRJEINFO CLI       TTR+3,11            CHECK USER LENGTH
         BL        CRBEINFO            SHORT...HANDLE AS CRBE
         LA        R1,30(R1)           POINT AT ATTR AREA OF LINE
         CRJE      8,B'00100000','DATA,',5,CRJE20
         CRJE      8,B'00010000','TEXT,',5,CRJE20
         CRJE      8,B'00001000','DSLIST,',7,CRJE20
         CRJE      8,B'00000100','CLIST,',6,CRJE20
         CRJE      8,B'01000001','FORTH,',6,CRJE20
         CRJE      8,B'01000000','FORTG,',6,CRJE20
         CRJE      8,B'00000001','FORTE,',6,CRJE20
         TM        8(R2),B'10000010'   PL1?
         BNZ       CRJE15              YES.
         MVC       0(7,R1),=C'???????' NO, UNKNOWN.
         BR        R10                 QUIT.
CRJE15   MVC       0(7,R1),=C'PL1(  ,'
         XR        R15,R15
         IC        R15,13(R2)          PL1 SORMARGIN
         CVD       R15,WORK1
         OI        WORK1+7,X'0F'
         UNPK      4(2,R1),WORK1(8)
         IC        R15,14(R2)
         CVD       R15,WORK1
         OI        WORK1+7,X'0F'
         UNPK      7(2,R1),WORK1(8)
         LA        R1,10(R1)
         CRJE      8,B'10000000','C48),',5,CRJE20
         MVC       0(5,R1),=C'C60),'
         LA        R1,5(R1)
CRJE20   CRJE      9,B'10000000','SEQ,',4,CRJE21
         MVC       0(6,R1),=C'NOSEQ,'
         LA        R1,6(R1)
CRJE21   TM        8(R2),B'11000011'   PL1 OR FORT?
         BZ        CRJE22              NO...SKIP SCAN
         CRJE      9,B'01000000','SCAN,',5,CRJE22
         MVC       0(7,R1),=C'NOSCAN,'
         LA        R1,7(R1)
CRJE22   MVC       0(4,R1),=C'BLK='
         LH        R15,20(R2)
         CVD       R15,WORK1
         OI        WORK1+7,X'0F'
         UNPK      4(4,R1),WORK1(8)
         MVC       8(3,R1),=C',K='
         MVC       11(3,R1),10(R2)
         BR        R10
 TITLE 'HEX-BINARY  TO  HEX-CHARACTER  CONVERSION  ROUTINE'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** HEX-BINARY  TO  HEX-CHARACTER  CONVERSION  ROUTINE               **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
HEXCON   CSECT
         USING     HEXCON,R11
** UPON ENTRY, R0=LENGTH OF SOURCE, R1=A(SOURCE), R15=A(TARGET)
 SPACE 1
HEXCONL  IC        R14,0(R1)           PROCESS FOUR LSB'S
         N         R14,=F'15'
         IC        R14,HEXTBL(R14)
         STC       R14,1(R15)
         IC        R14,0(R1)           PROCESS FOUR MSB'S
         SRL       R14,4
         IC        R14,HEXTBL(R14)
         STC       R14,0(R15)
         LA        R1,1(R1)
         LA        R15,2(R15)
         SH        R0,=H'1'            LOOP?
         BP        HEXCONL             YES.
         BR        R10                 NO, RETURN.
**
HEXTBL   DC        CL16'0123456789ABCDEF'
 TITLE 'PROCESS THE NAMETBLS AND POSITION TO THE NEXT MEMBER'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** PROCESS THE NAMETBLS AND POSITION TO THE NEXT MEMBER             **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
NEXTMEMB CSECT
         USING     NEXTMEMB,R11
         TM        SWD,SWD0            SELECTED NAMES SPECIFIED?
         BZ        NEXT20              NO.
** GET NEXT NAME FROM NAMETBL2
         LM        R2,R3,CURRENT2
         LTR       R2,R2               END OF TABLE?
         BZ        XENDLIB             YES.
         LA        R1,4(R2,R3)
         CLI       0(R1),C' '          END OF THIS DIRECTORY?
         BE        XENDLIB             YES,  : END OF RUN
         MVC       NAME,0(R1)
         LA        R3,8(R3)
         CH        R3,=H'256'          END OF BLOCK?
         BL        *+10                NO.
         L         R2,0(R2)
         XR        R3,R3
         STM       R2,R3,CURRENT2
** SEARCH NAMETBL1 FOR ENTRY.
         LA        R2,NAMETBL1
NEXT10   L         R2,0(R2)
         LTR       R2,R2               END OF TABLE?
         BZ        NEXT30              YES, MEMBER NOT FOUND.
         LH        R3,4(R2)
         LA        R1,6(R2)
         LA        R14,2
NEXT11   CLI       0(R1),X'FF'         END OF TABLE?
         BE        NEXT30              YES, MEMBER NOT FOUND.
         CLC       NAME,0(R1)          MEMBER FOUND?
         BE        NEXT32              YES, CONTINUE PROCESSING
         IC        R15,11(R1)          NO, CONTINUE SEARCH.
         N         R15,=F'31'
         SLL       R15,1
         LA        R1,12(R1,R15)
         LA        R14,12(R14,R15)
         CR        R14,R3              END OF BLOCK?
         BL        NEXT11              NO, CONTINUE WITH THIS BLOCK.
         B         NEXT10              YES, GO GET NEXT BLOCK.
** GET NEXT ENTRY FROM NAMETBL1
NEXT20   LM        R2,R3,CURRENT1
         LA        R1,0(R2,R3)         COMPUTE CURRENT ENTRY POINTER
         CLI       0(R1),X'FF'         END OF THIS DIRECTORY?
         BE        XENDLIB             YES, END OF THIS LIB.
         IC        R15,11(R1)
         N         R15,=F'31'
         SLL       R15,1
         LH        R14,4(R2)
         LA        R14,4(R14)
         LA        R3,12(R3,R15)
         CR        R3,R14              END OF BLOCK?
         BL        *+12                NO, CONTINUE
         L         R2,0(R2)            YES, GET NEXT BLOCK
         LA        R3,6
         STM       R2,R3,CURRENT1
         B         NEXT32
** EXIT FOR MEMBER NOT FOUND
NEXT30   BR        R10
** MEMBER FOUND ... SAVE ENTRY
NEXT32   MVC       NAME,0(R1)
         MVC       TTR,8(R1)
         IC        R15,11(R1)
         N         R15,=F'31'
         SLL       R15,1
         STH       R15,USERLEN
         LTR       R15,R15             ANY INFO?
         BNP       *+22                NO.
         CH        R15,=AL2(L'USERDATA)
         BNH       *+8
         LA        R15,L'USERDATA
         BCTR      R15,0
         EX        R15,USERMVC
         TM        11(R1),ALIAS        IS THIS AN ALIAS?
         BZ        NEXT40              NO, CONTINUE.
** ALIAS ENTRY ... FIND PRIME ENTRY (IF IT EXISTS)
         LA        R2,NAMETBL1
NEXT34   L         R2,0(R2)
         LTR       R2,R2               END OF TABLE?
         BZ        NEXT40              YES, ALIAS NOT FOUND.
         LH        R3,4(R2)
         LA        R1,6(R2)
         LA        R14,2
NEXT35   CLI       0(R1),X'FF'         END OF TABLE?
         BE        NEXT40              YES, IGNORE ALIAS.
         CLC       TTR(3),8(R1)        TTR MATCH?
         BNE       *+12                NO
         TM        11(R1),ALIAS        YES, IS THIS PRIME ENTRY?
         BZ        NEXT36              YES, GO PROCESS IT.
         IC        R15,11(R1)          NO, CONTINUE SEARCH.
         N         R15,=F'31'
         SLL       R15,1
         LA        R1,12(R1,R15)
         LA        R14,12(R14,R15)
         CR        R14,R3              END OF BLOCK?
         BL        NEXT35              NO
         B         NEXT34              YES, GO GET NEXT BLOCK
** PRIME ENTRY FOUND FOR ALIAS
NEXT36   MVC       NAME2,0(R1)
         OI        SWD,SWD1            FLAG ALIAS
         TM        SWD,SWD0            SELECTED NAMES SPECIFIED?
         BO        NEXT40              YES, CONTINUE.
         B         4(R10)              NO, ALIAS RETURN.
** POSITION TO NEXT MEMBER
NEXT40   MVC       TTRK(3),TTR         FOR FIND
         FIND      SYSLIB,TTRK,C
         B         8(R10)              NORMAL RETURN
 TITLE 'PDS  DATA  READ  ROUTINE'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** PDS  DATA  READ  ROUTINE                                         **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
GETBLK   CSECT
         USING     GETBLK,R11
         B         GETBLK10            FOR REGULAR PROCESSING
 SPACE 1
** INIT FOR READING
         LA        R8,SYSLIB
         LM        R2,R3,BUFFER1       INIT BUFFERS
         STM       R2,R3,BFRS
         XR        R5,R5               CLEAR CURRENT MEM REC CNTR
         CLI       SAVBUFNO,2          DOUBLE BUFFERING?
         BNER      R10                 NO.
         XC        PDSDECB,PDSDECB     YES, ISSUE FIRST READ.
         READ      PDSDECB,SF,,(R2),MF=E
         BR        R10                 RETURN
 SPACE 1
** READ IN NEXT BLOCK
GETBLK10 CLI       SAVBUFNO,2          DOUBLE BUFFERING?
         BE        GETBLK20            YES.
         L         R2,BFRS             NO...ISSUE READ
         XC        PDSDECB,PDSDECB     CLEAR ECB
         READ      PDSDECB,SF,SYSLIB,(R2),'S'
GETBLK20 CHECK     PDSDECB
         L         R15,PDSDECB+16      COMPUTE LEN OF REC READ
         LH        R6,DCBBLKSI
         SH        R6,14(R15)
         L         R7,BFRS             LOAD PNTR TO DATA
         CLI       SAVBUFNO,2          DOUBLE BUFFERING?
         BNE       GETBLK22            NO.
         L         R2,BFRS+4           YES, INIT NEXT READ
         ST        R7,BFRS+4
         ST        R2,BFRS
         XC        PDSDECB,PDSDECB     CLEAR ECB
         READ      PDSDECB,SF,,(R2),MF=E
GETBLK22 TM        SAVRECFM,B'10000000'  RECFM U OR F?
         BO        GETBLK24            YES.
         LH        R6,0(R7)            NO, MUST BE V ...
         LA        R7,4(R7)            PROCESS BLOCK DESCRIPTOR
         SH        R6,=H'4'
GETBLK24 BR        R10                 RETURN.
**
BFRS     DC        2A(0)
 TITLE 'SYSPRINT AND SYSLIST OUTPUT ROUTINES'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** OUTPUT A LINE TO SYSPRINT                                        **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
PRNT     CSECT
         USING     PRNT,R11
         LA        R1,1                DEFAULT= 1 LINE
         TM        SWD,SWD3            FISRT TIME THRU?
         BZ        PRNTA               YES.
         LH        R15,LINECNT1
         LA        R15,0(R15,R1)
         STH       R15,LINECNT1
         CH        R15,LINEMAX         AT LIMIT?
         BNH       PRNTC               NO.
PRNTA    LA        R15,5(R1)
         STH       R15,LINECNT1
         TM        SWD,SWD3            FIRST TIME THRU?
         BZ        PRNTB               YES.
         MVC       HEAD1PAG,PAGEPAT
         LH        R15,PAGENUM1
         LA        R15,1(R15)
         STH       R15,PAGENUM1
         CVD       R15,WORK1
         ED        HEAD1PAG,WORK1+4
         BAL       R14,PUTMSG
         MVC       0(121,R1),HEAD1
PRNTB    OI        SWD,SWD3
         BAL       R14,PUTMSG
         MVC       0(L'CAPTIONS,R1),CAPTIONS
         TM        SWA,SWA4            RITS/CRBE?
         BZ        *+14                NO.
         MVC       L'CAPTIONS(L'RITSCAP,R1),RITSCAP
         B         *+10
         MVC       L'CAPTIONS(L'USERCAP,R1),USERCAP
         TM        SWB,SWB2+SWB5       SPF IN THIS PDS?           .SPF.
         BNO       *+10                NO.                        .SPF.
         MVC       L'CAPTIONS(L'SPFCAP,R1),SPFCAP                 .SPF.
         BAL       R14,PUTMSG
PRNTC    BAL       R14,PUTMSG
         BR        R10
 EJECT
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** PAGE INITIALIZE FOR MEMBER LISTING                               **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
LOUT1    CSECT
         USING     LOUT1,R11
         TM        SWA,SWA0            LIST?
         BZR       R10                 NO.
         OI        SWC,SWC5            SYSLIST ACTIVE
         MVC       HEAD2NAM,NAME       INIT HEADING
         MVC       HEAD2PAG,PAGEPAT
         LH        R1,PAGENUM2
         LA        R1,1(R1)
         STH       R1,PAGENUM2
         CVD       R1,WORK1
         ED        HEAD2PAG,WORK1+4
         MVC       WORK1,HEAD2PAG
         PUT       SYSLIST
         MVC       0(121,R1),HEAD2
         PUT       SYSLIST
         BAL       R14,CLEARBUF
         NI        SWC,255-SWC5        SYSLIST NOT ACTIVE
         MVI       0(R1),C'0'
         LA        R15,3
         STH       R15,LINECNT2
         BR        R10
 EJECT
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** ONE LINE OF OUTPUT AND RETURN BUFFER ADRS IN REG 1.              **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
LOUT2    CSECT
         USING     LOUT2,R11
         LA        R1,1                DEFAULT = 1 LINE
         TM        SWA,SWA0            LIST?
         BZR       R10                 NO.
         OI        SWC,SWC5            SYSLIST ACTIVE
         LH        R15,LINECNT2
         LA        R15,0(R15,R1)
         STH       R15,LINECNT2
         CH        R15,LINEMAX         AT LIMIT?
         BNH       LOUT2A              NO.
         LA        R15,3(R1)
         STH       R15,LINECNT2
         MVC       HEAD2PAG,PAGEPAT
         LH        R15,PAGENUM2
         LA        R15,1(R15)
         STH       R15,PAGENUM2
         CVD       R15,WORK1
         ED        HEAD2PAG,WORK1+4
         PUT       SYSLIST
         MVC       0(121,R1),HEAD2
         PUT       SYSLIST
         BAL       R14,CLEARBUF
         MVI       0(R1),C'0'
LOUT2A   PUT       SYSLIST
         BAL       R14,CLEARBUF
         NI        SWC,255-SWC5        SYSLIST NOT ACTIVE
         BR        R10
 TITLE 'END OF LIBRARY ROUTINE'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** END OF LIBRARY ROUTINE                                           **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
ENDLIB   CSECT
         USING     ENDLIB,R11
         LA        R8,SYSLIB
** ISSUE END OF LIBRARY MESSAGE
         LR        R4,R11              SAVE BASE
         L         R11,=V(PRNT)
         BALR      R10,R11
         LR        R11,R4              RESTORE BASE
         MVC       0(MSG1L,R1),MSG1
         LH        R15,MEMCNT
         CVD       R15,WORK1
         ED        L'MSG1(L'MSG1A,R1),WORK1+5
         L         R15,RECORDS
         CVD       R15,WORK1
         ED        L'MSG1+L'MSG1A+L'MSG1AX(L'MSG1B,R1),WORK1+4
         TM        SWD,SWD0            SELECTED NAMES?
         BO        ENDLIB99            YES.
**
         AL        R15,TOTALREC
         ST        R15,TOTALREC
         XC        RECORDS,RECORDS
         XC        MEMCNT,MEMCNT
**
         L         R15,JFCB2
         LTR       R15,R15
         BZ        ENDLIB99            NO MORE LIBS
         LA        R2,4(R15)           POINT AT JFCB.
         MVC       HEAD1DSN,JFCBDSNM(R2)
         MVC       HEAD1VOL,JFCBVOLS(R2)
         L         R15,0(R15)
         ST        R15,JFCB2
         XR        R1,R1
         IC        R1,JFCRECFM(R2)
         LTR       R1,R1
         BNZ       *+8
         IC        R1,DSXRECFM(R2)
         STC       R1,SAVRECFM
         LH        R1,JFCBLKSI(R2)
         LTR       R1,R1
         BNZ       *+8
         LH        R1,DSXBLKSI(R2)
         STH       R1,SAVBLKSI
         LH        R1,JFCLRECL(R2)
         LTR       R1,R1
         BNZ       *+8
         LH        R1,DSXLRECL(R2)
         STH       R1,SAVLRECL
         TM        SWD,SWD4+SWD5       RITS/CRBE/CRJE?
         BZ        *+8                 NO.
         OI        SWA,SWA4            YES, FLAG.
         STM       R10,R11,SAVE1011
         L         R11,=V(CHKLIB)
         BALR      R10,R11
         LM        R10,R11,SAVE1011
         LH        R1,LINEMAX          TO PAGE EJECT
         STH       R1,LINECNT1
         XR        R15,R15             UPDATE TTRK.
         IC        R15,TTRK+3
         LA        R15,1(R15)
         STC       R15,TTRK+3
         XC        TTRK(2),TTRK        POINT AT NEW DIRECTORY
         MVI       TTRK+2,1
         FIND      SYSLIB,TTRK,C
         LA        R10,MAIN00          FOR NEXT LIB               .SPF.
         L         R11,=V(BLDNAMET)
         BR        R11                                            .SPF.
**
ENDLIB99 L         R11,=V(ENDRUN)
         BR        R11
 TITLE 'LISTPDS  END  OF  RUN  ROUTINE'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** END OF RUN ROUTINE                                               **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
ENDRUN   CSECT
         USING     ENDRUN,R11
         TM        SWB,SWB0            DEBUG?
         BO        ENDRUN10            YES, BYPASS FREE CORE
 SPACE 2
** FREE CORE FOR NAME TABLE #1
         L         R2,NAMETBL1
ENDRUN01 LTR       R3,R2               END OF TABLE?
         BZ        ENDRUN02            YES.
         L         R2,0(R2)            LOAD NEXT POINTER
         FREEMAIN  R,LV=260,A=(R3)
         B         ENDRUN01
** FREE CORE FOR NAME TABLE #2
ENDRUN02 L         R2,NAMETBL2         YES, FREE THE BLOCK(S)
ENDRUN03 LTR       R3,R2               END OF TABLE?
         BZ        ENDRUN04            YES.
         L         R2,0(R2)            LOAD NEXT POINTER
         FREEMAIN  R,LV=260,A=(R3)
         B         ENDRUN03
** FREE CORE FOR JFCB TABLE
ENDRUN04 L         R2,JFCB1
ENDRUN05 LTR       R3,R2               END OF TABLE?
         BZ        ENDRUN10            YES.
         L         R2,0(R2)            LOAD NEXT POINTER
         FREEMAIN  R,LV=260,A=(R3)
         B         ENDRUN05
 SPACE 2
ENDRUN10 TM        SWC,SWC3            TERMINATE MODE?
         BO        ENDRUN11            YES.
         LR        R4,R11              SAVE BASE ADRS
         L         R11,=V(PRNT)        ISSUE FINAL MESSAGE
         LA        R1,2                TO SKIP A LINE
         BAL       R10,4(R11)
         LR        R11,R4              RESTORE BASE ADRS
         MVC       0(L'ENDMSG0,R1),ENDMSG0
         CLI       TTRK+3,0
         BE        ENDRUN11            NO MULTI LIBS
         XR        R15,R15
         IC        R15,TTRK+3
         LA        R15,1(R15)
         CVD       R15,WORK1
         OI        WORK1+7,X'0F'
         UNPK      L'ENDMSG0(2,R1),WORK1+6(2)
         MVC       L'ENDMSG0+2(ENDMSG1L,R1),ENDMSG1
         L         R15,TOTALREC
         CVD       R15,WORK1
         ED        L'ENDMSG0+L'ENDMSG1+2(L'ENDMSG1A,R1),WORK1+4
ENDRUN11 LA        R8,SYSPUNCH         CLOSE ALL DCBS
         OI        SWC,SWC6            TURN-ON ACTIVE SWITCH
         BAL       R10,TERMIO
         TM        SWB,SWB0            DEBUG?
         BO        ENDRUN31            YES, SKIP CLOSING SYSLIB
         LA        R8,SYSLIB
         CLOSE     SYSLIB
         L         R2,BUFFER0          FREE CORE FOR PDS BUFFERS
         L         R3,BUFFER1
         LTR       R3,R3               BUFFERS ALLOC?
         BZ        ENDRUN31            NO.
         FREEMAIN  R,LV=(R2),A=(R3)
         L         R3,BUFFER2
         LTR       R3,R3               BUFFER #2 ALLOC?
         BZ        ENDRUN31            NO.
         FREEMAIN  R,LV=(R2),A=(R3)
ENDRUN31 LA        R8,SYSLIST
         OI        SWC,SWC5            TURN-ON ACTIVE SWITCH
         BAL       R10,TERMIO
         TM        SWC,SWC3            TERMINATE MODE?
         BO        ENDRUN90            YES.
 SPACE 2
** OUTPUT LISTING OF 'OPTIONS IN EFFECT' FOR RUN
         STM       R10,R11,SAVE1011
         L         R11,=V(PRNT)
         LA        R1,3
         BAL       R10,4(R11)
         LM        R10,R11,SAVE1011
         MVC       0(L'ENDMSG2,R1),ENDMSG2
         MVI       0(R1),C'-'
         LA        R2,L'ENDMSG2(R1)
         LA        R3,60(R1)
**
         TM        SWB,SWB0            DEBUG?
         BZ        *+18                NO
         MVC       0(5,R2),=C'DEBUG'   YES
         LA        R2,5(R2)
         BAL       R10,ENDRUNXX
**
         TM        SWB,SWB6            WAS THERE ANY SPF?         .SPF.
         BZ        *+18                NO                         .SPF.
         MVC       0(3,R2),=C'SPF'     YES                        .SPF.
         LA        R2,3(R2)                                       .SPF.
         BAL       R10,ENDRUNXX                                   .SPF.
**                                                                .SPF.
         TM        SWA,SWA2            LISTDIR?
         BZ        *+18                NO.
         MVC       0(7,R2),=C'LISTDIR' YES.
         LA        R2,7(R2)
         BAL       R10,ENDRUNXX
**
         TM        SWA,SWA4            RITS/CRBE/CRJE?
         BZ        ENDRUN40
         MVC       0(4,R2),=C'CRJE'    ASSUME CRJE
         TM        SWD,SWD4+SWD5       CRJE?
         BO        *+24                YES.
         MVC       0(4,R2),=C'CRBE'    NO, ASSUME CRBE
         TM        SWD,SWD5            CRBE?
         BO        *+10                YES.
         MVC       0(4,R2),=C'RITS'    NO, MUST BE RITS.
         LA        R2,4(R2)
         BAL       R10,ENDRUNXX
**
ENDRUN40 TM        SWA,SWA2            LISTDIR?
         BO        ENDRUN50            YES.
**
         TM        SWA,SWA0            LIST?
         BO        *+18
         MVC       0(6,R2),=C'NOLIST'
         LA        R2,6(R2)
         B         ENDRUN45
         MVC       0(4,R2),=C'LIST'
         LA        R2,4(R2)
         TM        SWA,SWA4            RITS/CRBE/CRJE?
         BO        ENDRUN43            YES, SKIP
         BAL       R10,ENDRUNXX
         TM        SWA,SWA7            TRUNC?
         BO        *+18
         MVC       0(7,R2),=C'NOTRUNC'
         LA        R2,7(R2)
         B         *+14
         MVC       0(5,R2),=C'TRUNC'
         LA        R2,5(R2)
         TM        SWB,SWB1            HEXOUT?
         BO        ENDRUN44            YES, SKIP
ENDRUN43 BAL       R10,ENDRUNXX
         TM        SWA,SWA5            XLATE?
         BO        *+18
         MVC       0(7,R2),=C'NOXLATE'
         LA        R2,7(R2)
         B         ENDRUN45
         MVC       0(5,R2),=C'XLATE'
         LA        R2,5(R2)
         BAL       R10,ENDRUNXX
         TM        SWB,SWB3            TEXT?
         BO        *+18
         MVC       0(6,R2),=C'NOTEXT'
         LA        R2,6(R2)
         B         *+14
         MVC       0(4,R2),=C'TEXT'
         LA        R2,4(R2)
ENDRUN44 TM        SWA,SWA4            RITS/CRBE/CRJE?
         BO        ENDRUN45
         BAL       R10,ENDRUNXX
         TM        SWB,SWB1            HEXOUT?
         BO        *+18
         MVC       0(8,R2),=C'NOHEXOUT'
         LA        R2,8(R2)
         B         ENDRUN45
         MVC       0(6,R2),=C'HEXOUT'
         LA        R2,6(R2)
ENDRUN45 BAL       R10,ENDRUNXX
**
         TM        SWA,SWA1            DECK?
         BO        *+18
         MVC       0(6,R2),=C'NODECK'
         LA        R2,6(R2)
         B         ENDRUN49
         MVC       0(4,R2),=C'DECK'
         LA        R2,4(R2)
         BAL       R10,ENDRUNXX
         TM        SWA,SWA3            UPDTE?
         BO        *+18
         MVC       0(7,R2),=C'NOUPDTE'
         LA        R2,7(R2)
         B         ENDRUN49
         MVC       0(5,R2),=C'UPDTE'
         LA        R2,5(R2)
         TM        SWA,SWA4            RITS/CRBE/CRJE?
         BO        ENDRUN49            YES, SKIP
         BAL       R10,ENDRUNXX
         TM        SWA,SWA6            SSI?
         BO        *+18
         MVC       0(5,R2),=C'NOSSI'
         LA        R2,5(R2)
         B         ENDRUN49
         MVC       0(3,R2),=C'SSI'
         LA        R2,3(R2)
ENDRUN49 BAL       R10,ENDRUNXX
**
ENDRUN50 MVC       0(8,R2),=C'LINECNT='
         LA        R2,8(R2)
         LH        R1,LINEMAX
         CVD       R1,WORK1
         OI        WORK1+7,X'0F'
         CH        R1,=H'999'
         BH        *+18
         UNPK      0(3,R2),WORK1
         LA        R2,3(R2)
         B         *+14
         UNPK      0(5,R2),WORK1
         LA        R2,5(R2)
         BAL       R10,ENDRUNXX
         MVC       0(6,R2),=C'EROPT='
         LA        R2,6(R2)
         TM        SWB,SWB4
         BO        *+18
         MVC       0(4,R2),=C'TERM'
         LA        R2,4(R2)
         B         *+14
         MVC       0(3,R2),=C'ACC'
         LA        R2,3(R2)
**
         TM        SWD,SWD0            SELECTED MEMBERS?
         BZ        *+18                NO.
         BAL       R10,ENDRUNXX
         MVC       0(L'ENDMSG3,R2),ENDMSG3
         LA        R2,L'ENDMSG3(R2)
**
 SPACE 2
ENDRUN90 STAE      0                   TERM STAE HANDLING
         LA        R8,SYSPRINT
         BAL       R10,TERMIO
         TM        SWB,SWB0            DEBUG?
         BZ        ENDRUN99            NO
         LH        R2,RETCODE          YES, ABEND
         ABEND     (R2),DUMP
ENDRUN99 L         R13,4(R13)
         LH        R15,RETCODE
         RETURN    (14,12),T,RC=(15)
 SPACE 2
**
ENDRUNXX CR        R2,R3
         BNL       *+14
         MVI       0(R2),C','
         LA        R2,1(R2)
         BR        R10
         STM       R10,R11,SAVE1011
         L         R11,=V(PRNT)
         BALR      R10,R11
         LM        R10,R11,SAVE1011
         MVC       0(L'ENDMSG2,R1),ENDMSG2
         LA        R2,L'ENDMSG2(R1)
         LA        R3,60(R1)
         BR        R10
 SPACE 2
**
ENDMSG0  DC        C'0*** END OF RUN ***   '
ENDMSG1  DC        C' LIBRARIES PROCESSED WITH A TOTAL OF '
ENDMSG1A DC        X'4020202020202120',C' RECORDS.'
ENDMSG1L EQU       *-ENDMSG1
ENDMSG2  DC        C' *** OPTIONS IN EFFECT *** '
ENDMSG3  DC        C'SELECTED-MEMBERS'
 TITLE 'LISTPDS  SUBROUTINES'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** LISTPDS  SUBROUTINES                                             **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
 SPACE 2
LISTPDS  CSECT
**
** VARIABLE MVC'S (ACCESSSED BY EXECUTE)
**
VARMVC1  MVC       5(0,R1),0(R2)       FOR PRINTED OUTPUT
VARMVC3  MVC       0(0,R1),0(R2)       FOR PUNCHED OUTPUT
USERMVC  MVC       USERDATA(0),12(R1)  FOR USER DIR INFO
**
** SAVE RETURN CODE
**
SAVERC   CH        R1,RETCODE          COMPARE TO CURRENT VALUE
         BNHR      R14                 RETURN IF NOT HIGHER
         STH       R1,RETCODE          REPLACE WITH NEW VALUE
         BR        R14
**
** TERMINATE PROCESSING
**
TERMINAT LA        R13,SAVEAREA
         BAL       R14,PUTMSG
         MVC       0(L'LPDS03I,R1),LPDS03I
         LA        R1,16               SET RETURN CODE
         STH       R1,RETCODE
         OI        SWC,SWC3            SET TERMINATE MODE
         L         R11,=V(ENDRUN)      GOTO END ROUTINE
         BR        R11
**
** END OF LIBRARY . . . THIS LIB'S PROCESSING IS COMPLETE
**
XENDLIB  L         R11,=V(ENDLIB)      GOTO END-OF-LIB ROUTINE
         BR        R11
**
** SETUP FOR MESSAGE OUTPUT
**
PUTMSG   ST        R14,SAVE14
         OI        SWC,SWC4            SYSPRINT ACTIVE
         PUT       SYSPRINT
         NI        SWC,255-SWC4        SYSPRINT NOT ACTIVE
         L         R14,SAVE14          AND FALL THRU TO CLEARBUF
**
** CLEAR PRINT BUFFER
**
CLEARBUF MVI       0(R1),C' '
         MVC       1(121,R1),0(R1)
         BR        R14
**
** GET AND CLEAR BUFFER FOR PUNCHED OUTPUT
**
PUNCHIT  OI        SWC,SWC6            SYSPUNCH ACTIVE
         PUT       SYSPUNCH
         NI        SWC,255-SWC6        SYSPUNCH NOT ACTIVE
         MVI       0(R1),C' '
         MVC       1(79,R1),0(R1)
         BR        R10
**
** TERMINATE I/O:  CLOSE DCB AND FREE BUFFER POOL
**
TERMIO   TM        DCBOFLGS,OFLGS      DCB OPEN?
         BZR       R10                 NO
         CLOSE     ((R8))              YES, CLOSE THE DCB
         FREEPOOL  (R8)                AND FREE THE BUFFER POOL
         BR        R10                 RETURN
 TITLE 'DCB EXIT ROUTINES'
**
** STANDARD FILES DCB EXIT ROUTINES
**
DCBEXIT1 LA        R5,3200             FOR SYSIN & SYSPUNCH
         B         DCBEXITZ
DCBEXITA LA        R5,3509             FOR SYSPRINT
         B         DCBEXITZ
DCBEXITB LH        R5,=Y(7260)         FOR SYSLIST
DCBEXITZ LH        R4,DCBLRECL         GET LRECL
         CH        R4,DCBBLKSI         BLKSIZE SPECIFIED?
         BNH       DCBEXITY            YES.
         STH       R5,DCBBLKSI         NO, USE DEFAULT.
DCBEXITY XR        R2,R2               FORCE MULT BLKSIZE
         LH        R3,DCBBLKSI
         DR        R2,R4
         MH        R3,DCBLRECL
         STH       R3,DCBBLKSI
         BR        R14
**
** PDS DATA READ DCB EXIT ROUTINE
**
DCBEXIT2 MVC       SAVBLKSI,DCBBLKSI   SAVE DCB PARMS
         MVC       SAVLRECL,DCBLRECL
         MVC       SAVRECFM,DCBRECFM
         MVC       SAVBUFNO,DCBBUFNO
         MVI       DCBBUFNO,0          BUFFERING HANDLED INTERNALLY
         CLI       SAVBUFNO,0          IS BUFNO SPECIFIED?
         BNER      R14                 YES.
         MVI       SAVBUFNO,2          NO, USE DEFAULT.
         BR        R14
 TITLE 'SYNCHRONOUS ERROR EXIT ROUTINES'
**
** DIRECTORY  I/O  ERROR
**
SYNERR1  SYNADAF   ACSMETH=BSAM
         LR        R2,R1               SAVE POINTER TO MSG
         BAL       R14,PUTMSG
         MVC       0(L'LPDS01I,R1),LPDS01I
         MVC       L'LPDS01I(78,R1),50(R2)
         LR        R1,R2
         SYNADRLS
         B         TERMINAT
**
** PDS DATA READ ERROR
**
SYNERR2  SYNADAF   ACSMETH=BPAM
         STM       R14,R12,SYNADSAV    SAVE REGS
         LR        R2,R1               SAVE POINTER TO MESSAGE
         L         R1,LASTLINE
         CLI       0(R1),C'+'          WERE THERE OTHER MSGS?
         BE        *+10                YES.
         L         R11,=V(PRNT)        NO, GET NEW LINE.
         BALR      R10,R11
         MVC       0(L'LPDS02I,R1),LPDS02I
         MVC       L'LPDS02I(78,R1),50(R2)
         BAL       R14,PUTMSG          REDO LASTLINE.
         MVI       0(R1),C'+'          ...NO SPACEING
         ST        R1,LASTLINE
         LA        R1,8                SET THE RETURN CODE
         BAL       R14,SAVERC
         LM        R14,R12,SYNADSAV    RESTORE REGS
         SYNADRLS
         TM        SWB,SWB4            EROPT=TERM/ACC?
         BZ        TERMINAT            FOR TERM.
         BR        R14                 FOR ACC.
 TITLE 'STAE (TASK ABNORMAL END) EXIT ROUTINE'
         USING     STAEXIT,R15
STAEXIT  CH        R0,STAEXITA         CHECK FOR CODE OF '12'
         BE        STAEXIT9            NOT '12'
         TM        SWC,SWC4            SYSPRINT ACTIVE?
         BO        STAEXIT1            YES, BYPASS CLOSE
         CLOSE     SYSPRINT
STAEXIT1 BALR      R15,0               DESTROYED BY CLOSE
         USING     *,R15
         TM        SWC,SWC5            SYSLIST ACTIVE?
         BO        STAEXIT2            YES, BYPASS CLOSE
         CLOSE     SYSLIST
STAEXIT2 BALR      R15,0               DESTROYED BY CLOSE
         USING     *,R15
         TM        SWC,SWC6            SYSPUNCH ACTIVE?
         BO        STAEXIT9            YES, BYPASS CLOSE
         CLOSE     SYSPUNCH
STAEXIT9 XR        R15,R15             SET RETURN CODE
         BR        R14                 AND RETURN TO SYSTEM.
STAEXITA DC        H'12'
         DROP      R15
 TITLE 'LISTPDS STORAGE AREAS, CONSTANTS, ETC.'
LISTPDS  CSECT
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** STORAGE AREAS, CONSTANTS, ETC.                                   **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
SAVEAREA DC        9D'0'               STD OS SAVE AREA
WORK1    DC        1D'0'
WORK2    DC        1D'0'
SYNADSAV DC        8D'0'               TO SAVE REGS IN SYNAD EXIT
SWITCHES DC        XL4'00'             RUN SWITCHES AND FLAGS
         ORG       *-4
SWA      DC        X'00'
SWB      DC        X'00'
SWC      DC        X'00'
SWD      DC        X'00'
         ORG
TIOT     DC        A(0)                POINTER TO TIOT
TRTBL    DC        A(0)                ADDRESS OF XLATE TABLE IN USE
SAVE1011 DC        2A(0)               TO SAVE REGS 10 & 11
SAVE14   DC        A(0)                TO SAVE REG 14
BUFFER0  DC        A(0)                PDS BUFFER LENGTH
BUFFER1  DC        A(0)                PDS BUFFER #1 ADDRESS
BUFFER2  DC        A(0)                PDS BUFFER #2 ADDRESS
TTRK     DC        F'0'                FOR FIND/POINT.
LASTLINE DC        A(0)                BUFFER ADRS OF LAST SYSPRINT
RECORDS  DC        F'0'                COUNT OF TOTAL RECORDS IN LIB
TOTALREC DC        F'0'                TOTAL NO. OF LRECL PROCESSED
JFCB1    DC        A(0)                SYSLIB JFCB POINTERS
JFCB2    DC        A(0)
NAMETBL1 DC        A(0)                POINTER TO DIRECTORY TABLE
NAMETBL2 DC        A(0)                POINTER TO SELECTED NAMES
CURRENT1 DC        2A(0)               PNTRS TO NAMETBL1 CURRENT
CURRENT2 DC        2A(0)               PNTRS TO NAMETBL2 CURRENT
**
DSCB     CSECT     ,                   FOR OBTAIN'S CAM LIST
         CAMLST    SEARCH,DSNAME,VOLSER,WORKAREA
         DS        0D
WORKAREA DC        XL160'00'           OBTAIN'S WORKAREA
DSNAME   DC        CL44' '
VOLSER   DC        CL6' '
LISTPDS  CSECT     ,                   REVERT TO REGULAR CSECT
**
EXLST1   DC        0F'0',X'85',AL3(DCBEXIT1)     SYSIN & SYSPUNCH
EXLSTA   DC        0F'0',X'85',AL3(DCBEXITA)     SYSPRINT
EXLSTB   DC        0F'0',X'85',AL3(DCBEXITB)     SYSLIST
EXLST2   DC        0F'0',X'05',AL3(DCBEXIT2)     FOR PDS DCB
JFCBADRS DC        X'80',AL3(0)
**
SAVBLKSI DC        H'0'
SAVLRECL DC        H'0'
SAVRECFM DC        X'00'
SAVBUFNO DC        AL1(0)
**
RETCODE  DC        H'0'                RETURN CODE
PAGENUM1 DC        H'0'                CURRENT SYSPRINT PAGE NUMBER
PAGENUM2 DC        H'0'                CURRENT SYSLIST  PAGE NUMBER
LINECNT1 DC        H'0'                CURRENT SYSPRINT LINE NUMBER
LINECNT2 DC        H'0'                CURRENT SYSLIST  LINE NUMBER
LINEMAX  DC        H'0'                MAXIMUM LINE NUMBER
MEMCNT   DC        H'0'
         DS        0F
NAME2    DC        CL8' '              ALIAS NAME
NAME     DC        CL8' '              MEMBER NAME
TTR      DC        F'0'                TTRC
USERDATA DC        XL62'00'            USER INFO
USERLEN  DC        H'0'                LENGTH OF USER INFO
DDPRINT  DC        CL8'&DDPRINT'       SYSPRINT
DDLIST   DC        CL8'&DDLIST'        SYSLIST
DDPUNCH  DC        CL8'&DDPUNCH'       SYSPUNCH
DDLIB    DC        CL8'&DDLIB'         SYSLIB
DDIN     DC        CL8'&DDIN'          SYSIN
SWAX     DC        B'&A(1)&A(2)&A(3)&A(4)&A(5)&A(6)&A(7)&A(8)'
SWBX     DC        B'&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)&B(8)'
UPDTE1   DC        C'./ ADD NAME='
UPDTE2   DC        C'./ ADD SSI=00000000,NAME='
UPDTESUB DC        C'./'                                          .PRC.
PAT1     DC        X'4021204B202020'
PAT2     DC        X'4020202020202120'
JULIANS  DC        3F'0'
**
 TITLE 'LISTPDS  LITERALS'
         LTORG
 TITLE 'LISTPDS  MESSAGES'
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
** MESSAGES,  ETC.                                                  **
**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**==**
HEAD1    DC        C' '                SYSPRINT HEADING
HEAD1DAT DC        CL8' '
HEAD1TIM DC        CL9' ',C' - VOL='
HEAD1VOL DC        CL6' ',C', DSN='
HEAD1DSN DC        CL44' ',C'   ',CL5'&ID',C'-LISTPDS('
         DC        CL7'&RELEASE',CL4')',C'PAGE'
HEAD1PAG DC        CL8' '
HEAD1R   DC        X'F14021204B4B4B20204021204B20204B2020'
**
HEAD2    DC        C'1'                SYSLIST HEADING
HEAD2DAT DC        CL8' '
HEAD2TIM DC        CL9' ',CL38' '
HEAD2NAM DC        CL8' ',CL45' ',C'PAGE'
HEAD2PAG DC        CL8' '
PAGEPAT  DC        X'4020202020202120'
CAPTIONS DC        C'-     NAME       PAGE   RECORDS    T T R C    '
RITSCAP  DC        C' CREATED  MODIFIED  ACCESSED  ATTRIBUTES'
USERCAP  DC        C'USER INFORMATION (HEX)'
SPFCAP   DC        C'VER.MOD  CREATED   LAST MODIFIED  SIZE  INIT   MOD+
                   ID'
*                     01.00    YY/DDD    YY/DDD HH:MM  XXXX  XXXX  XXXX
*                USERID
LPDS01I  DC        C'0LPDS01I  DIRECTORY I/O ERROR - '
LPDS02I  DC        C' LPDS02I  PDS DATA READ ERROR - '
LPDS03I  DC        C'0LPDS03I  EXECUTION ABORTED.'
LPDS04I  DC        C'0LPDS04I  OPEN ERROR FOR '
LPDS05I  DC        C'0LPDS05I  WARNING--PARAMETER SPECIFICATION ERROR'
LPDS06I  DC        C'0LPDS06I  INVALID DCB SPECIFIECATIONS FOR &DDLIB D+
               D.  '
LPDS06I0 DC        C' NOT SUPPORTED.'
LPDS06I1 DC        C'MACHINE CONTROL CHARACTERS'
LPDS06I2 DC        C'VARIABLE SPANNED RECORDS'
LPDS06I3 DC        C'TRACK OVERFLOW'
LPDS06IA DC        C'BLKSIZE INVALID.'
LPDS06IB DC        C'INVALID RECFM.'
LPDS07I  DC        C'0LPDS07I WARNING--RITS/CRBE/CRJE PROCESSING DELETED
               ,  LIBRARY DCB PARMS NOT COMPATABLE.'
MSG1     DC        C' *** END OF LIBRARY *** '
MSG1A    DC        X'402020202120'
MSG1AX   DC        C' MEMBERS PROCESSED WITH A TOTAL OF'
MSG1B    DC        X'4020202020202120',C' RECORDS'
MSG1L    EQU       *-MSG1
MSG2     DC        C'0*** END OF MEMBER *** '
MSG2A    DC        X'402020202120',C' RECORDS PROCESSED.'
MSG2L    EQU       *-MSG2
MSG3     DC        C'****  MEMBER NOT FOUND IN LIBRARY'
MSG4     DC        C'****  IS AN ALIAS FOR '
MSG5     DC        C'****  ABOVE MEMBER IS AN ALIAS FOR '
 TITLE 'LISTPDS  DCB''S'
SYSPRINT DCB       DSORG=PS,MACRF=(PL),DDNAME=&DDPRINT,                +
               RECFM=FBA,LRECL=121,EXLST=EXLSTA
SYSLIST  DCB       DSORG=PS,MACRF=(PL),DDNAME=&DDLIST,                 +
               RECFM=FBA,LRECL=121,EXLST=EXLSTB
SYSPUNCH DCB       DSORG=PS,MACRF=(PL),DDNAME=&DDPUNCH,                +
               RECFM=FB,LRECL=80,EXLST=EXLST1
SYSLIB   DCB       DSORG=PO,MACRF=(R),DDNAME=&DDLIB,EODAD=MAIN90,      +
               SYNAD=SYNERR2,NCP=1,EXLST=EXLST2
 TITLE 'LISTPDS OUTPUT TRANSLATE TABLES'
**
**  STANDARD TRANSLATE TABLE FOR 'QN' PRINT CHAIN
**
XLATE1   CSECT
         DC        CL74' ',CL15' .<(+|&&'
         DC        CL15'  $*);-/',CL15'   ,%_>?'
         DC        CL74'   :#@''="'
         DC        CL16'ABCDEFGHI',CL17'JKLMNOPQR',CL14'STUVWXYZ'
         DC        CL16'0123456789'
**
** TEXT TRANSLATE TABLE FOR 'QN' PRINT CHAIN
**
XLATE2   CSECT
         DC        CL74' ',CL15' .<(+|&&'
         DC        CL15'  $*);-/',CL15'   ,%_>?'
         DC        CL09'   :#@''="'
         DC        CL17' ABCDEFGHI',CL17'JKLMNOPQR',CL31'STUVWXYZ'
         DC        CL16'ABCDEFGHI',CL17'JKLMNOPQR',CL14'STUVWXYZ'
         DC        CL16'0123456789'
 TITLE 'LISTPDS DSECTS'
**
** DCB LABELS DEFINITION DSECT
**
         DCBD      DSORG=(PS,PO),DEVD=(DA)
 SPACE 3
         END       LISTPDS
