         MACRO
&LABEL   PGMOUT &RC=0
.*
.*       PDSDOC *MACRO TO RETURN FROM PROGRAM.........................* PGMOUT
.*
.*
.*  &&RC=          NUMBER TO BE RETURNED TO CALLING CSECT, OR REG 15
.*                 SPECIFIED FOR INDICATING PRESET CODE  ALREADY
.*
&LABEL   DS    0H
         AIF   ('&RC' EQ '(15)' OR '&RC' EQ '(R15)').SIZEX
         AIF   (T'&RC EQ 'N').NUMBER
         AIF   ('&RC'(1,1) EQ '(').REGSTER
         AIF   (T'&RC EQ 'F').FLLWORD
         AIF   (T'&RC EQ 'H').HLFWORD
         MNOTE 4,'INVALID RC OPERAND - &RC - CONTENTS R15 ASSUMED'
         AGO   .SIZEX
.NUMBER  LA    15,&RC              LOAD RETURN CODE
         AGO   .SIZEX
.REGSTER LA    15,0&RC             LOAD RETURN CODE
         AGO   .SIZEX
.FLLWORD L     15,&RC              LOAD RETURN CODE
         AGO   .SIZEX
.HLFWORD LH    15,&RC              LOAD RETURN CODE
.SIZEX   ANOP
         L     13,4(13)            LOAD ADDRESS PREVIOUS SAVE AREA
         RETURN (14,12),T,RC=(15)
         MEND
