***********************************************************************
*        PDSDOC *SUB-ROUTINE TO FIND LAST NON-BLANK CHAR..............* ENDBLANK
*                                                                     *
*                                                                     *
*        ON ENTRY:                                                    *
*              R0   =  LENGTH OF STORAGE ELEMENT                      *
*              R1   -> ADDRESS OF STORAGE ELEMENT                     *
*                                                                     *
*        ON EXIT:                                                     *
*              R0   =  LENGTH OF DATA UP TO FIRST TRAILING BLANK      *
*              R1   -> FIRST OF THE TRAILING BLANKS                   *
*                                                                     *
* 24APR93 CART1 WROTE CODE                                            *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
ENDBLANK DS    0H                  ELIMINATE TRAILING BLANKS
         RIN   (R5)                SUBROUTINE ENTRY
         LR    R4,R0               COPY LENGTH
         BCTR  R4,0                DECREMENT TO LAST CHARACTER
         SPACE 1
ENDBLNKL DS    0H                  LOOP TO FIND NON-BLANK CHAR.
         LA    R5,0(R1,R4)         POINT TO LAST CHARACTER
         CLI   0(R5),C' '          IS IT A BLANK?
         BNE   ENDBLNKN            NO, FOUND THE END
         BCT   R4,ENDBLNKL         YES, LOOP AROUND WHOLE LENGTH
         SPACE 1
ENDBLNKN DS    0H                  GOT THE END
         LR    R0,R5               COPY LAST CHARACTER ADDRESS
         CR    R5,R1               IS IT ALL BLANKS?
         BE    *+4+4               YES, NO LAST CHARACTER
         LA    R0,1(,R5)           BUMP UP TO LAST TRAILING BLANK
         SR    R0,R1               CALCULATE LENGTH
         LR    R1,R5               COPY END POINTER
         SPACE 1
ENDBLANX DS    0H                  END OF SUBROUTINE
         ROUT  ,                   RETURN TO CALLER
