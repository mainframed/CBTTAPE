IXVTOCF5 CSECT
********************************************************************
*        PDSDOC *PSEUDO FORMAT 5 DSCB'S FOR INDEXED VTOCS.............* IXVTOCF5
*
*        THIS PROGRAM RETURNS TO THE CALLER A FORMAT 5 DSCB. THE
*        FORMAT 5 DSCB IS CONSTRUCTED BY THIS PROGRAM IF THE
*        VOLUME CONTAINS AN INDEXED VTOC. THE FORMAT 5 DSCB IS
*        READ BY ANOTHER PROGRAM, F5RTN (NOT DESCRIBED HERE),
*        IF THE VOLUME CONTAIND AN UNINDEXED VTOC OR IF DF/DS
*        IS NOT ON THE SYSTEM.
*
*        THE CVAFTST MACRO IS USED TO DETERMINE IF AN UNINDEXED
*        VTOC IS ON  THE VOLUME, OR IF DF/DS IS NOT INSTALLED ON THE
*        SYSTEM.
*
*        IF THE CVAFTST RETURN CODE IS NEITHER 0 (DF/DS IS NOT
*        ON THE SYSTEM) NOR 4 (AN UNINDEXED VTOC IS ON THE
*        VOLUME). THE CVAFDSM MACRO IS ISSUED TO OBTAIN
*        UP TO 27 EXTENTS FROM THE VPSM IN THE VTOC INDEX. THE
*        PROGRAM DOES NOT DETERMINE WHETHER THE CVAFTST
*        RETURN CODE IS 8 (VOLUME CONTAINS INDEXED VTOC) OR 12
*        IT CANNOT BE DETERMINED WHAT TYPE OF VTOC IS ON THE
*        VOLUME). IN EITHER CASE, THE CVAFDSM MACRO IS ISSUED.
*        IF THE CVAFTST RETURN CODE IS 12, THE CVAFDSM MACRO
*        CALL WILL CAUSE CVAF TO DETERMINE WHETHER AN INDEXED
*        OR UNINDEXED VTOC IS ON THE VOLUME, AND THE CV1IVT
*        BIT WILL BE SET TO ONE OR ZERO, ACCORDINGLY.
*
*        THE EXTENT TABLE (AT LABEL EXTABL) IS INITIALIZED TO
*        REQUEST 27 EXTENTS FROM THE CVAFDSM MACRO, WHICH IS
*        ONE OR MORE THAN THE NUMBER OF EXTENTS THAT FIT IN A
*        FORMAT-5 DSCB. THE FORMAT-5 DSCB IS CONSTRUCTED FROM
*        THE FIRST 26 EXTENTS RETURNED FROM THE CVAFDSM CALL.
*
*        THE FIRST EXTENT IN THE EXTENT TABLE IS INITIALIZED
*        FROM THE LAST EXTENT IN THE FORMAT-5 DSCB AREA SUPPLIED
*        BY THE CALLER OF THE PROGRAM. IF THIS IS THE FIRST CALL,
*        THE PROGRAM ASSUMES THAT THE FORMAT-5 AREA IS INITIALIZED
*        TO ZERO. THUS, THE FIRST EXTENT IN THE EXTENT TABLE HAS
*        A VALUE OF ZERO TO SERVE AS THE STARTING PLACE FOR THE
*        EXTENT SEARCH. IF THIS IS THE SECOND OR SUBSEQUENT CALL,
*        THE LAST EXTENT IN THE FORMAT-5 AREA WOULD BE THE LAST
*        EXTENT OBTAINED FROM THE PREVIOUS CVAFDSM CALL.
*
*        THE FORMAT-5 CHAIN POINTER FIELD (DS5PTRDS) IS SET TO A
*        NONZERO VALUE IF CVAFDSM RETURNED A 27TH EXTENT. IN THIS
*        CASE, THE PROGRAM WILL BE CALLED AGAIN TO OBTAIN ANOTHER
*        FORMAT-5 DSCB.
*        THE PROGRAMS RETURN CODE IS ZERO IF NO ERRORS WERE
*        ENCOUNTERED AND 4 IF AN ERROR WAS ENCOUNTERED.
*
********************************************************************
*
*
*        TAKEN FROM: DF/DS USERS GUIDE & REFERENCE (EXAMPLES
*        OF VTOC ACCESS MACROS--EXAMPLE 5: CVAFTST & CVAFDSM
*        MACROS)
*
*
********************************************************************
         EJECT
         STM   14,12,12(13)
         BALR  12,0
         USING *,12
         ST    13,SAVEAREA+4
         LA    RWORK,SAVEAREA
         ST    RWORK,8(,13)
         LR    13,RWORK
         SPACE 3
********************************************************************
*
*        U S E R   R E G I S T E R   E Q U A T E S
*
********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
********************************************************************
*
*        I. B. M. --- T Y P E   E Q U A T E S
*
********************************************************************
RUCB     EQU   4         UCB ADDRESS SUPPLIED BY CALLER
RF5      EQU   5         ADDRESS OF FORMAT5 BUFFER SUPPLIED BY CALLER
RWORK    EQU   6         WORK REGISTER
         SPACE 2
KF5      EQU   26        NUMBER OF FORMAT 5 EXTENTS
         EJECT
********************************************************************
*
*        READ FORMAT 5 DSCB OR BUILD A FORMAT 5 DSCB IF AN
*        INDEXED VTOC. UCB ADDRESS SUPPLIED IN RUCB.
*        RF5 CONTAINS THE ADDRESS OF THE FORMAT 5 DSCB BUFFER.
*        IT CONTAINS THE LAST FORMAT 5 DSCB READ OR BUILT.
*        THE FORMAT 5 BUFFER IS ZERO IF THIS IS THE FIRST CALL.
*        IF THE FORMAT 5 DSCB BUFFER RETURNED TO THE CALLER HAS
*        A NON ZERO VALUE IN DS5PTRDS, THIS ROUTINE WILL BE
*        CALLED AGAIN TO OBTAIN THE NEXT FORMAT 5 DSCB.
*
********************************************************************
         SPACE 2
         USING IECSDSF5,RF5          ADDRESSABILITY TO FORMAT 5 BUFFER
         CVAFTST UCB=(RUCB)          TEST VTOC
         LTR   R15,R15
         BZ    UNINDXD               READ NEXT FORMAT 5
         C     R15,NOTIXRC           UNINDEXED VTOC ?
         BE    UNINDXD               READ NEXT FORMAT 5
         EJECT
********************************************************************
*        ASSUME INDEXED VTOC UNLESS CVAFDSM CALL INDICATES
*        UNINDEXED.
********************************************************************
         MVC   EXTS(L'DS5AVEXT),DS5MAVET+L'DS5MAVET-L'DS5AVEXT
*
*        ABOVE LINE MOVES THE LAST EXTENT FROM FORMAT 5 TO FIRST
*        ENTRY IN THE EXTENT TABLE.
*
         CVAFDSM MF=(E,CVPL),BRANCH=YES,UCB=(RUCB)  GET 27 EXTS
         TM   CVFL1,CV1IVT           IS THIS INDEXED FORMAT
         BZ   UNINDXD                READ FORMAT 5 IF NOT
         LTR  R15,R15                ANY ERROR ?
         BZ   NOERROR
         C    R15,RC04
         BNE  OTHERERR               UNEXPECTED ERROR
         CLI  CVSTAT,STAT032         END OF DATA
         BNE  OTHERERR               UNEXPECTED ERROR
NOERROR  EQU  *                      BUILD FORMAT 5
         MVC  DS5KEYID,F5ID
         MVC  DS5AVEXT(L'DS5AVEXT+L'DS5EXTAV),EXTS
*                                    MOVE IN EXTENTS TO DS5FMTID
         MVI  DS5FMTID,C'5'
         MVC  DS5MAVET,EXTS+L'DS5AVEXT+L'DS5EXTAV
*                                    MOVE REMAINING EXTENTS
         XR   R15,R15
         XC   DS5PTRDS,DS5PTRDS      ZERO CHAIN POINTER
         NC   EXTS+L'EXTS-L'DS5AVEXT(L'DS5AVEXT),EXTS+L'EXTS-L'DS5AVEXT
*                                    IS LAST (27TH) EXTENT
*                                    FROM CVAF ZERO?
         BZ   RETURN                 BRANCH IF YES-LAEVE DS5PTRDS ZERO
         MVI  DS5PTRDS+1,1     SET DS5PTRDS NONZERO TO
*                              SIMULATE THERE BEING ANOTHER FORMAT 5
         B    RETURN
UNINDXD  EQU  *                    CALL ROUTINE TO READ NEXT FORMAAT 5
*        LINK EP=F5RTN             LINK TO FORMAT 5 ROUTINE.
*                                  RETURN CODE PASSED BACK IN R15
         LA   R15,256(,R15)          RETURN CODE 16 (NOT IXVTOC)
RETURN   EQU  *                      RETURN TO CALLER
         L    R13,SAVEAREA+4
         RETURN (14,12),RC=(15)
OTHERERR EQU  *                      ERROR
         L    R15,RC04               ERROR RETURN CODE
         B    RETURN
         EJECT
********************************************************************
*         FORMAT 5 DSCB
********************************************************************
DSCB     DSECT
         IECSDSL1 (5)
         EJECT
********************************************************************
*         RETURN TO IXVTOCF5 CSECT
********************************************************************
IXVTOCF5 CSECT
NOTIXRC  DC   F'4'                   CVAFTST RETURN CODE-UNDINDEXED
RC04     DC   F'4'                   RETURN CODE 4
F5ID     DC   XL4'0505050505'        FORMAT 5 FIELD, DS5KEYID
SAVEAREA DS   18F                    REGISTER SAVE AREA
EXTABL   DS   0CL(1+(KF5+1)*L'DS5AVEXT)      EXTENT TABLE
EXTNO    DC   AL1(KF5+1)             NUMBER OF EXTENTS IN TABLE
EXTS     DS   CL((KF5+1)*L'DS5AVEXT)         EXTENTS
CVPL     CVAFDSM ACCESS=MAPDATA,                                       *
               COUNT=NO,             DO NOT COUNT EXTENTS              *
               MAP=VOLUME,           ACCESS VOLUME SPACE MAP           *
               EXTENTS=EXTABL,       EXTENT TAPLE ADDRESS              *
               MF=L,                 LIST FORM OF MACRO                *
               BRANCH=(YES,PGM)
         EJECT
         ORG  CVPL                 OVERLAY CVPL WITH EXPANSION OF MAP
CVPLMAP  ICVAFPL DSECT=NO
         END
