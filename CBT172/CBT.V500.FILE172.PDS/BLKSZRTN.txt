***********************************************************************
*        PDSDOC *STANDARD OPEN EXIT CODE..............................* BLKSZRTN
*                                                                     *
*        SUPPLY DEFAULT RECORD LENGTH AND/OR BLOCK SIZE               *
*         IF NOT CODED.                                               *
*        ON ENTRY    R1 --> DCB                                       *
*                                                                     *
* 26OCT98 DHC   LET SYSTEM DETERMINE BLOCKSIZE                        *
* 30MAY95 CART1 ADD CODE TO HANDLE SPANNED INPUT                      *
* 17JUL92 CART1 RAISE DEFAULT LRECL FROM 80 BYTE CARD IMAGE TO 1/2 TRK*
*                                                                     *
***********************************************************************
         SPACE 1
BLKSZRTN SUBIN ,                   SUPPLY DEFAULT LRECL AND/OR BLKS
         USING IHADCB,R1           ESTABLISH DCB ADDRESSABILITY
         TM    DCBRECFM,X'FF'      ANY RECORD FORMAT?
         BNZ   *+4+4               YES, SKIP SETTING DEFAULT
         OI    DCBRECFM,DCBRECV    DEFAULT VARIABLE LENGTH RECORDS?
         TM    DCBMACR,DCBMRGET    USING GET LOCATE?
         BNO   BLKSZR02            NO, SKIP BFTEK=A CODE
         TM    DCBRECFM,DCBRECV+DCBRECSB VARIABLE SPANNED?
         BNO   BLKSZR02            NO, SKIP BFTEK=A
         OI    DCBBFALN,DCBBFTA    YES, SET BFTEK=A
BLKSZR02 DS    0H                  SORT OUT REOCRD SIZES
         LH    R4,DCBLRECL         GET LRECL VALUE
         LTR   R4,R4               ANY SUPPLIED?
         BNZ   *+4+4+4             YES, SKIP SETTING DEFAULT
         LH    R4,=H'23472'        DEFAULT LRECL - HALF TRACK 3380
         STH   R4,DCBLRECL         SET DEFAULT
         LH    R4,DCBLRECL         RE-FETCH LRECL VALUE
         SLL   R4,3                DEFAULT BLOCK IN EIGHTS
         TM    DCBRECFM,DCBRECV    ARE THEY VARIABLE LENGTH RECORDS?
         BO    *+4+4               NO, DO NOT ADD BDW
         LA    R4,4(R4)            DEFAULT BLOCKSIZE INCLUDES BDW
         OC    DCBBLKSI,DCBBLKSI   HOW ABOUT BLOCKSIZE
         BNZ   0(R14)              YES, RETURN TO OPEN
*        STH   R4,DCBBLKSI         SET DEFAULT
         SUBOUT RETURN=(R14)       RETURN TO OPEN ROUTINES
         SPACE 1
         DROP  R1                  END DCB ADDRESSING
