         MACRO
&TABNAME CPB   &PROGRAM
.*********************************************************************
.*       PDSDOC *CALLED PROGRAM CONTROL BLOCK.........................* CPB
.*
.*       THIS MACRO CAN BE USED TO CONTROL THE DYNAMIC LINKAGE BETWEEN
.*       PROGRAMS. IT BUILDS, OR MAPS, A CONTROL BLOCK FOR USE BY THE
.*       'CALLIT' COMMON CODE SEGMENT.
.*
.*       IN THE SPECIAL CASE THAT THE PROGRAM NAME IS 'DSECT', A
.*       DSECT IS DECLARED WHICH MAPS THE PARAMETER BLOCK USED BY THE
.*       'CALLIT' COMMON CODE SEGMENT.
.*
.*       25JAN95 CART1 TEST IF WXTRN DEFINED ALREADY
.*       03NOV92 CART1 ADD WXTRN CODE
.*       15JUL92 CART1 STARTED WRITING
.*
.*********************************************************************
.*
.*
.*
.*********************************************************************
.*       TEST FOR 'DSECT' TYPE ENTRY
.*********************************************************************
         AIF   ('&PROGRAM' EQ 'DSECT').GENER GENERATE STORAGE MAP
.*********************************************************************
.*       DECLARE PROGRAM BLOCK
.*********************************************************************
         DS    0F                  ALIGNMENT
&TABNAME DC    CL8'&PROGRAM'       CALLED PROGRAM NAME
         AIF   (D'&PROGRAM).CPBDEF SKIP WXTRN IF DEFINED
         WXTRN &PROGRAM            CALLED PROGRAM IS A WEAK REFERENCE
.CPBDEF  ANOP
         DC    V(&PROGRAM)         CALLED PROGRAM ADDRESS
         DC    AL4(0)              COUNT OF CALLS
.*********************************************************************
.*       EXIT FROM THIS MACRO
.*********************************************************************
.MEND1  MEXIT
.*********************************************************************
.*      GENERATE STORAGE AREA MAP DSECT
.*********************************************************************
.GENER   ANOP
CPB      DSECT ,                   MAP PROGRAM BLOCK
CPBNAME  DS    CL8                 CALLED PROGRAM NAME
CPBADDR  DS    AL4                 CALLED PROGRAM ADDRESS
CPBCOUNT DS    AL4                 COUNT OF CALLS
         MEND
