IEAVMXIT TITLE 'IEAVMXIT -- DEFAULT MPF EXIT. SUPPRESS WTO TEXT'
***********************************************************************
*        PDSDOC *DEFAULT MPF EXIT - LABEL AND SUPPRESS WTO ...........* IEAVMXIT
*                                                                     *
* THIS MPF EXIT WILL PROCESS PROBLEM PROGRAM WTO'S. IT WILL ALTER THE *
* TEXT TO SHOW THE NAME OF THE ISSUING JOB, AND IT WILL STOP THE      *
* MESSAGE BEING DISPLAYED ON THE CONSOLES. IT ASSUMES THAT THE DEFAULT*
* ROUTE CODE (OR 'OLDWTO' AS IT USED TO BE CALLED) IS SET TO '14'.    *
*                                                                     *
*                                                                     *
* 07JAN92 CART1 STARTED WRITING                                       *
* 08JAN92 CART1 ADDED TEST FOR IEFACRT MESSAGES FROM 'INIT'           *
* 09JAN92 CART1 ADD SUPPRESSION OF RC=11 MESSAGES, MAINLY FOR STEEB   *
*                                                                     *
***********************************************************************
         SPACE 1
IEAVMXIT CSECT
IEAVMXIT AMODE 31
IEAVMXIT RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'IEAVMXIT'
         DC    C'&SYSDATE',C' &SYSTIME'
*        PRINT NOGEN
         PRINT ON,GEN
START    L     R3,0(R1)      LOAD POINTER TO PASSED PARM LIST      JWP
         SPACE 1
         GETMAIN RU,LV=AREAREGS,SP=230,LOC=(BELOW,ANY) GETMAIN FOR JWP
         SPACE 1
         LR    R10,R1        SAVE POINTER TO GOTTEN AREA           JWP
         MVI   0(R10),X'00'  CLEAR GOTTEN AREA                     JWP
         MVC   1(AREAREGS-1,R10),0(R10)                            JWP
         ST    R13,4(R10)    EXCHANGE THE REGISTER SAVE AREA       JWP
         ST    R10,8(R13)    POINTERS TRICK                        JWP
         LR    R13,R10                                             JWP
         USING WORKREGS,R13  ESTABLISH ADDRESSABILITY TO GOTTEN AREA
         SPACE 1
         LR    R10,R3         PICK UP ADDRESS OF CTXT
         USING CTXT,R10
         ICM   R8,B'1111',CTXTTXPJ LOAD POINTER TO THE MAJOR LINE
         BZ    RETURN              EXIT IF MINOR LINE
         USING CTXTATTR,R8         ESTABLISH ADDRESSABILITY
         L     R7,CTXTRCP          LOAD ROUTE CODE POINTER
         USING CTXTROUT,R7         ESTABLISH ADDRESSABILITY
         TM    CTXTR002,CTXTR11    IS IT RC=11? (WRITE TO LOG)
         BO    WTL                 YES, PROCESS THOSE
         CLI   CTXTR002,CTXTR14    IS IT RC=14? (USER WTO)
         BNE   RETURN              NO, FORGET IT
         TM    CTXTRFB2,CTXTRBCN   IS THIS AN UNBROADCAST MSG?
         BO    RETURN              YES, FORGET IT
         TM    CTXTTFB1,CTXTTFWR   WTOR?
         BO    RETURN              YES, DO NOT PROCESS
         EJECT ,
START1   DS    0H                  PROCESS A MESSAGE
         CLC   CTXTJBNM,IEFACTRT   IS IT ACCOUNTING?
         BE    RETURN              YES, FORGET IT
         MVC   CMESTEXT,CTXTTMSG   COPY MESSAGE
         LH    R5,CTXTTLEN         GET MESSAGE LENGTH
         LA    R3,L'CMESTEXT       AND OUR AREA'S LENGTH
         CR    R5,R3               COMPARE LENGTHS
         BNH   WRITEMSG            OK, IT FITS
         LR    R5,R3               ELSE USE OUR LENGTH
         MVI   CMESTRNC,CMESPLUS   AND SHOW TRUNCATION MARK
         SPACE 1
WRITEMSG DS    0H                  MODIFY THE MESSAGE
         MVC   CMESJBNM,CTXTJBNM   COPY JOBNAME
         MVI   CMESSPRE,CMESPLUS   DELIMIT JOBNAME
         MVC   CTXTTMSG,CMESSAGE   COPY MESSAGE BACK
         LA    R5,9(,R5)           UPDATE LENGTH FOR OUR PREFIX
         STH   R5,CTXTTLEN         AND SAVE FOR COMMTASK
         OI    CTXTRFB1,CTXTRCMT   TELL COMMTASK TO CHANGE THE MSG
         NI    CTXTR002,255-CTXTR14 NULLIFY ROUTE CODE 14
         OI    CTXTRFB1,CTXTRCRC   TELL COMMTASK TO CHANGE ROUTECODE
         SPACE 2
RETURN   LR    R1,R13       LOAD ADDRESS OF GOTTEN AREA           JWP
         L     R13,4(R13)   LOAD PREVIOUS SAVE AREA ADDRESS       JWP
         LA    R0,AREAREGS  LOAD SIZE OF GOTTEN AREA              JWP
         FREEMAIN RU,LV=(0),A=(1),SP=230 FREE THE GOTTEN AREA     JWP
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 2
WTL      DS    0H                  PROCESS ROUTCODE=11
         NI    CTXTR002,255-CTXTR11 NULLIFY ROUTE CODE 11
         OI    CTXTRFB1,CTXTRCRC   TELL COMMTASK TO CHANGE ROUTECODE
         CLI   CTXTR001,CTXTR02    MASTCONS INFO? (STEEB)
         BNE   RETURN              NO, THEN DO NOT EDIT
         NI    CTXTR001,255-CTXTR02 NULLIFY MASTER CONSOLE IF THERE
         B     START1              THEN PROCESS AS USER WTO
         SPACE 1
         DROP  R10                 END CTXT ADDRESSABILITY
         DROP  R8                  END CTXTATTR ADDRESSABILITY
         DROP  R7                  END CTXTROUT ADDRESSABILITY
         TITLE 'DATA AREAS'
IEFACTRT DC    CL8'INIT'           JOB NAME FROM BATCH ACCOUNTING MSGS
         SPACE 1
         LTORG *
         EJECT ,
**********************************************************************
*                                                                    *
*        DSECT USED TO MAP GOTTEN AREA                               *
*                                                                    *
**********************************************************************
         SPACE
WORKREGS DSECT
         SPACE
REGAREA  DS    18F                 REGISTER SAVE AREA
         SPACE 2
CMESSAGE DS    0CL126              AREA FOR MESSAGE TEXT
CMESJBNM DS    CL8' '              JOBNAME (INSERTED)
CMESSPRE DS    C'+'                PREFIX FOR USER WTO REPEATED
CMESTEXT DS    CL117' '            MESSAGE TEXT
         ORG   *-1                 LAST CHARACTER
CMESTRNC DS    C'+'                INDICATE IF TRUNCATED
         SPACE 1
CMESPLUS EQU   C'+'                OUR FLAG
         SPACE 1
AREAREGS EQU   *-WORKREGS
         EJECT
         IEZVX100
         PRINT NOGEN
         REQU  ,              STANDARD REGISTER EQUATES
         CVT   DSECT=YES,LIST=YES
         SPACE 3
         END   IEAVMXIT
