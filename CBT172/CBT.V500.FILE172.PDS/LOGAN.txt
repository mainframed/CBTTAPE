 /* SYSLOG MESSAGE ANALYSIS ROUTINE - VERSION 2.01                   */
 /*                                                                  */
 /*      PDSDOC *SYSLOG ANALYSIS PROGRAM FROM GG24-3142-01...........*/ LOGAN
 /*                                                                  */
 /* ********** IBM INTERNAL USE ONLY ****************                */
 /* ***NOTE*** NOTE ****NOTE ***** NOTE *******                      */
 /*                                                                  */
 /* FOR SYSLOGS PRODUCED BY MVS/XA 2.2.0 OR LATER, YOU               */
 /* MUST CHANGE THE DCL STATEMENT FOR THE VARIABLE                   */
 /* 'ROUTECODE' TO BE CHAR(7) INSTEAD OF CHAR(4).                    */
 /* THI S WILL ALLOW FOR THE DIFFERENT SYSLOG FORMAT                 */
 /* ****************                                                 */
 /*                                                                  */
 /*                                                                  */
 /* CHANGES FOR 2.01                                                 */
 /* TIDYING UP OF MESSAGES                                           */
 /* INCREASED SIZE OF FILTER TABLE TO 999                            */
 /* FIX ERRORS IN TRAFFIC ANALYSIS                                   */
 /* CHANGE TRAFFIC ANALYSIS INCREMENT TO 0.5 LINES/SEC               */
 /* ALLOW FOR LEADING BLANKS IN FILTER DATA SET RECORDS              */
 /*                                                                  */
 /* AUTHOR: STEWART LUNN , ITSC POUGHKEEPSIE                         */
 /*                                                                  */
 /* FUNCTION:                                                        */
 /*                                                                  */
 /* THIS PROGRAM IS A SIMPLE ANALYSIS OF THE MESSAGES ISSUED         */
 /* TO THE SYSLOG DATA SET BY AN MVS/JES2 SYSTEM. IN ITS BASIC       */
 /* FORM, IT PRODUCES A FREQUENCY ANALYSIS OF THE DIFFERENT          */
 /* MESSAGES, IDENTIFIED BY THE MSGID (FIRST 1-10 NON-BLANK          */
 /* CHARACTERS IN THE MESSAGE).                                      */
 /*                                                                  */
 /* OPTIONALLY MESSAGES MAY BE 'SELECTED' BY THE ROUTECODE AND BY    */
 /* THE CONSOLE ID, TO IDENTIFY MESSAGE TRAFFIC TO A SPECIFIC        */
 /* CONSOLE FOR INSTANCE, OR TO ESTIMATE THE EFFECT OF CHANGING      */
 /* ROUTECODE SPECIFICATIONS.                                        */
 /*                                                                  */
 /* TO ESTIMATE THE EFFECT US ING THE MESSAGE SUPPRESSION FACILITIES */
 /* OF MPF, A 'FILTERING' CAPABILITY IS PROVIDED. IF THIS IS         */
 /* REQUESTED, A SECOND ANALYSIS IS MADE IN WHICH THE MESSAGE        */
 /* TRAFFIC IS ANALYSED AS IF MPF MESSAGE SUPRRESSION WERE IN        */
 /* EFFECT. THE MESSAGES TO BE SUPPRESSED ARE IDENTIFIED IN A        */
 /* 'FILTER' DATA SET WHICH CONTAINS THE MESSAGE IDS.                */
 /*                                                                  */
 /* IN A NORMAL ANALYSIS, ONLY UNSOLICITED MESSAGES ARE COUNTD.      */
 /* THUS, MESSAGES ISSUED AS A RESULT OF A COMMNAD ARE NOT.          */
 /* OPTIONALLY, THESE CAN BE INCLUDED.                               */
 /* WHEN THIS OPTION IS REQUESTED, A FREQUENCY ANALYSIS OF MULTI-    */
 /* LINE RESPONSES IS ALSO PRODUCED. THIS CAN HELP IDENTIFY THE      */
 /* EFFICIENCY OF OPERATOR COMMANDS. (THERE IS ANOTHER PROGRAM       */
 /* TO ANALYSE OPERAOR INPUT).                                       */
 /*                                                                  */
 /* OPTIONALLY, A 'TRAFFIC' ANALYSIS CAN BE DONE. IN THIS, THE RATE  */
 /* AT WHICH MESSAGE LINES ARE ARRIVING AT THE CONSOLE IS REPORTED.  */
 /* EACH LOG IS BROKEN UP INTO 30-SECOND INTERVALS AND THE RATE      */
 /* AT WHICH MESSAGE LINES ARE SENT TO THE OPERATOR IS RECORDED.     */
 /* A REPORT GIVING THE FREQUENCIES OF DIFFERENT MESSAGE RATES       */
 /* IS PRODUCED.                                                     */
 /* THE INTERVAL SIZE CAN BE OPTIONALLY MODIFIED.                    */
 /*                                                                  */
 /*                                                                  */
 /* INPUT:                                                           */
 /*                                                                  */
 /* THERE ARE TWO DATA SETS USED AS INPUT. ONE IS REQUIRED & IS A    */
 /* COPY OF THE SYSLOG DATA SET. IF MADE UP OF CONCATENATED FILES    */
 /* IT SHOULD BE IN TIME SEQUENCE. THE FIRST CHARACTER OF THE        */
 /* RECORD IS TREATED AS A PRINT CONTROL CHARACTER.                  */
 /*                                                                  */
 /* THE SECOND IS THE OPTIONAL 'FILTER' DATA SET GIVING A LIST OF    */
 /* MESSAGES TO BE CONSIDERED AS SUPPRESSED IN THE FILTER ANALYSIS.  */
 /* THE RECORD SIZE IS 80 BYTES.                                     */
 /* THE MSGIDS SHOULD START IN COLUMN 1 AND ARE DELIMITED BY THE     */
 /* FIRST BLANK OR COMMA. MSGIDS ARE 10 CHARACTERS MAXIMUM LENGTH.   */
 /* UP TO 99 MESSAGES MAY BE SPECIFIED. IF THE FILTER DATA SET       */
 /* CONTAINS MORE, ONLY THE FIRST 99 ARE SELECTED.                   */
 /* BECAUSE A COMMA IS ACCEPTED, AN ACTAL MPFLSTXX MEMBER MAY BE     */
 /* USED AS INPUT.                                                   */
 /*                                                                  */
 /*                                                                  */
 /* RUN-TIME OPTIONS:                                                */
 /*                                                                  */
 /* THESE ARE SPECIFIED BY PARM FIELDS IN THE EXEC STATEMENT, IN     */
 /* THE FORM "PARM' /AAAA, BBBBB CCCCC'                              */
 /* IF THE '/' IS OMITTED A PL/I MESSAGE WILL BE ISSUED THOUGH       */
 /* EXECUTION WILL CONTINUE                                          */
 /* NO SPECIFIC ORDER IS REQUIRED AND THERE IS NO REQUIREMENT        */
 /* (OTHER THAN FOR CLARITY) FOR COMMAS OR BLAKSAS DELIMITERS.       */
 /* THE WHOLE PARM FIELD IS SCANNED FOR OPTIONS.                     */
 /*                                                                  */
 /* ALL - IN COUNTING MESSAGES AND TRAFFIC, LINES PRODUCED AS        */
 /*                                                                  */
 /* A RESULT OF OPERATOR COMMANDS ARE INCLUDED. THIS WILL            */
 /* ALSO CAUSE AN ANALYSIS OF LINE LENGTHS FOR COMMAND               */
 /* OUTPUT.                                                          */
 /*                                                                  */
 /* CONSOLE(XX) -                                                    */
 /* WHERE THERE ARE MESSAGES THAT ARE DIRECTED TO A                  */
 /* SPECIFIC CONSOLE, THEY WILL ONLY BE SELECTED                     */
 /* FOR CONSOLE XX.                                                  */
 /* DEFAULT CONSOLE IS 01.                                           */
 /*                                                                  */
 /* ROUTECODES(N,NN,NN)                                              */
 /* A LIST OF ROUTECODES ARE SPECIFIED. IF ANY ARE                   */
 /* SPECIFIED, ONLY MESSAGES WITH THOSE ROUTECODES ARE               */
 /* SELECTED. VALID ROUTRCODES ARE 1 THROUGH 16.                     */
 /* ALL REQUESTED ROUTECODES FOR SELECTION SHOULD APPEAR             */
 /* THE ONLY ROUTECODES OPTION.                                      */
 /* IF THERE IS NO ROUTECODES OPERAND SPECIFIED THEN ALL             */
 /* ARE SELECTED.                                                    */
 /*                                                                  */
 /* TRAFFIC -                                                        */
 /* INDICATES THAT TRAFFIC ANALYSIS SHOULD BE DONE.                  */
 /* IF 'FILTER' IS REQUESTED A SECOND TRAFFIC ANALYSIS               */
 /* WILL REFLECT THE EFFECT OF SUPPRESSED MESSAGES.                  */
 /*                                                                  */
 /* INTERVAL=XX -                                                    */
 /* GIVES THE INTERVAL SIZE, IN SECONDS, FOR THE TRAFFIC             */
 /* ANALYSIS. VALID VALUES ARE 10-99. DEFAULT IS 30.                 */
 /* IF SPECIFIED WITHOUT TRAFFIC, A TRAFFIC ANALYSIS IS              */
 /* ASSUMED.                                                         */
 /*                                                                  */
 /* FILTER -                                                         */
 /* CAUSES A SECOND ANALYSIS TO TAKE PLACE GIVING THE                */
 /* EFFECT OF SUPPRESSING A SELECTED NUMBER OF MESSAGES.             */
 /*                                                                  */
 /*                                                                  */
 /*                                                                  */
 /* OUTPUT:                                                          */
 /*                                                                  */
 /* OUTPUT IS A SET OF REPORTS GIVING:                               */
 /*                                                                  */
 /* LIST OF OPTIONS SPECIFIED AND IN EFFECT FOR THIS RUN             */
 /*                                                                  */
 /* SUMMARY GIVING:                                                  */
 /* TOTAL NUMBER OF RECORDS IN SYSLOG                                */
 /* TOTAL TIME (BEGIN TO END - GAPS NOT IDENTIFIED)                  */
 /* THEN, FOR THE MESSAGES SELECTED (SEE ABOVE):                     */
 /* NUMBER OF RETAINED MESSAGES                                      */
 /* NUMBER OF WTORS                                                  */
 /* NUMBER OF MESSAGE LINES                                          */
 /* NUMBER OF UNIQUE MESSAGE IDS                                     */
 /* A BREAKDOWN BY ROUTECODE OF THE MESSAGE LINES                    */
 /* AVERAGE MESSAGE RATE ACROSS THE WHOLF PERIOD                     */
 /* NUMBER OF SOLICITED MESSAGES (IF 'ALL' OPTION)                   */
 /* FREQUENCY REPORT OF MULTI LINE MESSAGES (IF 'ALL                 */
 /*                                                                  */
 /* FREQUENCY REPORT OF MESSAGE IDS SORTED BY FREQUENCY              */
 /* EACH ENTRY GIVES THE ID, THE NUMBER OF LINES                     */
 /* THAT THIS MESSAGE ACCOUNTS FOR, ITS PERCENTAGE                   */
 /* OF THE TOTAL UNSOLICITED MESSAGE LINE TRAFFIC,                   */
 /* THE CUMULATIVE PERCENTAGE AND THE PARTIAL TEXT                   */
 /* (I. E. FIRST 40 CHARS) OF THE FIRST OCCURRENCE                   */
 /* OF THE MESSAGE.                                                  */
 /*                                                                  */
 /* NOTE THAT A MESSAGE ID IS TAKEN TO BE THE FIRST                  */
 /* NON-BLANK STRING IN THE MESSAGE, TRUNCATED TO                    */
 /* TEN CHARACTERS IF NECESSARY. THIS MEANS THAT                     */
 /* 'NON-STANDARD' MESSAGES WILL ALSO BE REPORTED.                   */
 /* THIS INCLUDES A LOT OF WHAT MIGHT BE THOUGHT OF                  */
 /* AS RUBBISH, E. G. MESSAGE LINES CONTAINING                       */
 /* VARIABLE DATA. THE REPORTING OF THIS IS STILL                    */
 /* VALUABLE SINCE THESE REPRESENT LINES THAT AN                     */
 /* OPERATOR MUST DEAL WITH.                                         */
 /*                                                                  */
 /*                                                                  */
 /*                                                                  */
 /* FILTER REPORT (OPTIONAL)                                         */
 /* THIS GIVES A LIST OF THE MESSAGE IDS USED IN                     */
 /* THE FILTER ANALYSIS. IT THEN REPEATS THE REPORTS                 */
 /* OF THE FIRST PASS (USING THE SAME SELECTION                      */
 /* CRITERIA). IT ALSO GIVES A VALUE FOR THE                         */
 /* LEVEL OF SUPPRESSION THAT THIS SPECIFICATION                     */
 /* WOULD ACHIEVE.                                                   */
 /*                                                                  */
 /* TRAFFIC REPORT (OPTIONAL)                                        */
 /* THIS GIVES A FREQUENCY BREAKDOWN OF HOW MANY                     */
 /* INTERVALS WERE MEASURED WHERE THE MESSAGE LINE                   */
 /* TRAFFIC RATE FELL WITHIN CERTAIN VALUES.                         */
 /*                                                                  */
 /* MESSAGES:                                                        */
 /*                                                                  */
 /* A NUMBER OF MESSAGES ARE ISSUED. THE INTIAL SET INDICATE         */
 /* THE OPTIONS SELECTED AND DEFAULTED FOR THIS EXECUTION AND ARE    */
 /* (HOPEFULLY) SELF EXPLANATORY.                                    */
 /*                                                                  */
 /* ADDITIONAL ONES REFER TO SOME ERROR THAT HAS CAUSED NORMAL       */
 /* PROCESSING NOT TO TAKE PLACE.                                    */
 /*                                                                  */
 /*                                                                  */
 /*                                                                  */
 /*********************************************************************/
 LOGAN: PROC(PARMS) OPTIONS(MAIN);
 DCL PARMS CHAR(100) VARYING;   /* PARAMETERS PASSED TO ANALYSIS     */
 DCL SYSLOG FILE INPUT;
 ON ENDFILE(SYSLOG) GO TO ENDF;
 DCL FILTER FILE INPUT;
 ON ENDFILE(FILTER) GO TO ENDFIL;
 DCL FILTWA CHAR(80);           /* INPUT AREA FOR FILTER FILE        */
 DCL 1 BUFIN,                   /* INPUT RECORD FROM SYSLOG DS       */
     2 CCTL CHAR(1),            /* CARRIAGE CONTROL IF VIA SDSF      */
     2 TYPE CHAR(1),            /* TYPE OF LINE                      */
     2 COMRESP CHAR(1),         /* C=COMMAND, R=RESPONSE, ELSE BLANK */
     2 ROUTECODE CHAR(7),       /* ROUTECOCDE FLAGS                  */
     2 BLANK1 CHAR(1),
     2 SYSNAME CHAR(8),         /* NAME OF SYSTEM                    */
     2 PADIT CHAR(1),           /*                                   */
     2 DATE,                    /* FORM YYDDD                        */
        3 YEAR CHAR(2),         /* YEAR                              */
        3 DAY CHAR(3),          /* DAY NUMBER                        */
     2 BLANKLA CHAR(1),
     2 TIME,                     /* TIME OF DAY                      */
        3 HOUR CHAR(2),
        3 COLON1 CHAR(1),
        3 MINUTE CHAR(2),
        3 COLON2 CHAR(1),
        3 SECOND CHAR(2),
        3 DOT CHAR(1),
        3 HUNDR CHAR(2),
     2 BLANK2 CHAR(1),
     2 STJ CHAR(3),              /* STARTED TASK, JOB OR TSO         */
     2 BLANK3 CHAR(1),
     2 JOBNUM CHAR(4),           /* JOB, STC NUMBER MSGID            */
     2 BLANK4 CHAR(1),
     2 MORFLGS CHAR(8),          /* REQUEST FLAGS FROM EXIT          */
     2 BLANK5 CHAR(1),           /* PLUS OTHER FLAGS                 */
     2 RETAIN CHAR(1),           /* IF * THEN RETAIN MESSAGE         */
     2 TEXT CHAR(76);
 DCL HEXDIGITS CHAR(16) STATIC INIT('0123456789ABCDEF');
 DCL XFLAGS (16) CHAR(4) STATIC INIT (
 '0000', '0001', '0010', '0011', '0100', '0101', '0110', '0111',
 '1000', '1001', '1010', '1011', '1100', '1101', '1110', '1111');
 DCL CONSOLE CHAR(2) STATIC INIT ('01'); /* DEFAULT VALUE            */
 DCL MAXNUMID FIXED BIN(15) INIT (999); /* SIZE OF FREQUENCY TABLE   */
 DCL FREQ (999) FIXED BIN(31);         /* FREQUENCY TABLE - FREQUENCY*/
 DCL IDTAB (999) CHAR(10);              /* - MSGID                   */
 DCL MTEXT (999) CHAR(40);            /* & FIRST 40 CHARS FROM FIRST */
 /* OCCURRENCE OF MESSAGE                                            */
 DCL MSGID CHAR(10);                    /* MSG ID AS IDENTIFIED      */
 DCL NUMIDS FIXED BIN(15);            /* CURRENT TALLY OF DISRETE IDS*/
 DCL IDPTR FIXED BIN(15);                /* POINTER TO CURRENT MSGID */
 DCL WTOCOUNT FIXED BIN(31);             /* COUNT OF WTOS ISSUED     */
 DCL NUMRET FIXED BIN(31);             /* COUNT OF RETAINED MESSAGES */
 DCL RUN_TIME FIXED BIN(31);         /* TIMESPAN OF SYSLOG FILE(SECS)*/
 DCL ERROR STATIC BIT(1) INIT('0'B); /* OVERALL ERROR SWITCH         */
 DCL ALLSW STATIC BIT(1) INIT('0'B); /* PROCESS RESPONSE MSGS IF SET */
 DCL RCSELSW STATIC BIT(1) INIT('0'B); /* MSG SELECTED BY ROUTECODE  */
 DCL FILTERSW STATIC BIT(1) INIT('0'B); /* FILTER REQUSTED SW        */
 DCL MSGMLTSW STATIC BIT(1) INIT('0'B); /*MULTI LINE SWITCH          */
 DCL MSGFILSW STATIC BIT(1) INIT('0'B);
          /* NUM_MSGS IS THE NUMBER OF MESSAGE LINES                 */
          /* SEEN BY OPERATOR (FOR SPECIFIC FILTERING                */
          /* CONDITIONS ALSO)                                        */
          /* INCLUDING CONTINUATIONS AND                             */
          /* MULTI LINE ELEMENTS                                     */
 DCL (NUM_MSGS, NUMRECS, TOTMSGS, UNFIL) FIXED BIN(31);
 DCL RESPCT FIXED BIN(31);        /* COUNT OF RESPONSE LINES         */
 DCL LINE FIXED BIN(15);          /* LINES IN CURRENT MULTILINE      */
 DCL RESP_FREQ (20) FIXED BIN(31); /* FREQUENCIES OF MULTILINES      */
 DCL (MCACT, MCINFOCT) FIXED BIN(31);/* MASTER CONSOLE MESSAGE CNTS  */
 DCL (TPPCT, DAPCT, TPLCT, DALCT, URPCT, TPCCT, SECCT, MTCCT, PGMCT)
 FIXED BIN(31); /* COUNTS FOR OTHER RTCDES                           */
 DCL ALLCT FIXED BIN(31);         /* ALL ROUTE CODES - COUNT OF      */
 /* ROUTE CODE PROCESSING DCL' S                                     */
 DCL RCWA CHAR(100);              /* PARM PROCESS WORK AREA          */
 DCL RCCHAR CHAR(2);              /* ROUTE CODE SPECIFIED            */
 DCL NEXTCHAR CHAR(1);            /*                                 */
 DCL (RCODES, SELRC) CHAR(16);    /*                                 */
 DCL ONE_TO_16 CHAR(32) STATIC
 INIT( ' 1 2 3 4 5 6 7 8 910111213141516' );
 DCL RCDESC (16) CHAR(30) STATIC INIT (
 ' NUMBER OF MC ACTION MESSAGES.',
 ' NUMBER OF MC INFO MESSAGES. ',
 ' NUMBER OF TAPE POOL MESSAGES.',
 ' NUMBER OF DASD POOL MESSAGES.',
 ' NUMBER OF TAPE LIB MESSAGES. ',
 ' NUMBER OF DASD LIB MESSAGES. ',
 ' NUMBER OF U/R POOL MESSAGES. ',
 ' NUMBER OF TP CNTRL MESSAGES.',
 ' NUMBER OF SECURITY MESSAGES.',
 ' NUMBER OF SYS ERROR MESSAGES.',
 ' NUMBER OF PROGRAMMR MESSAGES.',
 ' NUMBER OF RC=12 MESSAGES.',
 ' NUMBER OF RC=13 MESSAGES.',
 ' NUMBER OF RC=14 MESSAGES.',
 ' NUMBER OF RC=15 MESSAGES.',
 ' NUMBER OF RC=16 MESSAGES.');
 DCL RCCT (16) FIXED BIN(31); /* COUNT OF MSGS WITH RC=N             */
 DCL FTOT FLOAT;              /* VARIABLE USED IN DO LOOPS           */
1BEGIN:
 PUT SKIP EDIT (' ******* SYSLOG MESSAGE ANALYSIS PROGAM *******')
              (A);
 PUT SKIP(2) EDIT(' OPTIONS SELECTED WERE:', PARMS) (A,SKIP,X(3),A);
 IF INDEX(PARMS, 'ALL') ¬= 0 THEN DO;
 ALLSW='1'B;
 PUT SKIP(2) EDIT(' ''ALL'' SPECIFIED, RESPONSES ARE COUNTED') (A);
 END;
 ELSE
 PUT SKIP(2) EDIT(' ''ALL'' NOT SPECIFIED, RESPONSES NOT COUNTED')
              (A);
 I = INDEX(PARMS, 'CONSOLE=');
 CONSOLE = '01';              /* DEFAULT CONSOLE ID                  */
 IF I ¬= 0 THEN               /* IF SPECIFIC CONSOLE SELECTED        */
 CONSOLE=SUBSTR(PARMS, I+8, 2); /* EXTRACT THE ID                    */
 PUT SKIP EDIT (' CONSOLE ID FOR SEECTION IS ', CONSOLE)(A,A);
 SELRC = (16) '1';             /* SELECT ALL RC'S BY DEFAULT         */
 I = INDEX(PARMS, 'ROUTECODES=('); /* ANY RC'S SPECIFIED ?           */
 IF I ¬= 0 THEN DO;            /* IF YES                             */
 SELRC = (16) '0';             /* INITIALISE SELECT STRING           */
 L = INDEX(PARMS,')');         /* LOOK FOR ENDING PARENTHESIS        */
 IF L=0 THEN GOTO INVRC;       /* IF NONE FOUND                      */
 IF L < I+2 THEN GO TO INVRC;  /* OR TOO SMALL                       */
 I =I+12;                      /* POINT TO FIRST ROUTECODE           */
 RCWA = SUBSTR(PARMS, I, (L-I+1) );
 I = 1;                        /* START OF RC VALUE                  */
 NEXTRC:
 RCCHAR =' ';                  /* INITIALISE                         */
 NEXTCHAR = SUBSTR(RCWA ,I+1,1); /* LOOK AT NEXT CHARACTER           */
 IF ((NEXTCHAR=',') | (NEXTCHAR=')') ) THEN DO;
 SUBSTR(RCCHAR, 2,1)=SUBSTR(RCWA ,I, 1);
 I =I+2;
 END;
 ELSE DO;                      /* ELSE 2-CHARACTER ROUTE CODE        */
 RCCHAR = SUBSTR(RCWA ,I ,2);
 NEXTCHAR = SUBSTR(RCWA ,I+2,1);
 I=I+3;
 END;
 K=INDEX(ONE_TO_16, RCCHAR);    /* WHICH ROUT CODE ?                 */
 IF K=0 THEN GO TO INVRC;       /* NOT FOUND                         */
 K = K/2 +1;
 SUBSTR(SELRC,K,1)='1';         /* SET SELECT SWITCH ON              */
 PUT SKIP EDIT (' SELECTED ROUTE CODE .......',RCCHAR)(A,A);
 IF NEXTCHAR=',' THEN GO TO NEXTRC; /* MORE ROUTECODES               */
 IF NEXTCHAR=')' THEN GO TO RCDONE; /* NO MORE                       */
 INVRC:                         /* IF NEITHER - INVALID SPEC'N       */
 PUT SKIP EDIT(' *****INVALID RC SPECIFICATION******')(A);
 GO TO PROGEND;
 RCDONE:
 END;                           /* END OF ROUTE CODE PARM ANALYSIS   */
 IF INDEX(PARMS, 'TRAFFIC')¬=0  /* IF TRAFIC ANALYSIS REQUESTED      */
 THEN TRAFSW = '1'B;            /* THEN SET ITS SWITCH               */
 ELSE
 PUT SKIP EDIT(' NO TRAFFIC ANALYSIS REQUESTED')(A);
 I = INDEX(PARMS, 'INTERVAL='); /* INTERVAL SPECIFIED                */
 INTERVAL = 30.0;              /* DEFAULT INTERVAL IS 30 SECS        */
 IF I ¬=0 THEN DO;             /* IF SPECIFIED                       */
 IF TRAFSW='0'B THEN DO;       /* IF TRAFFIC NOT REQUESTED           */
 PUT SKIP EDIT (' INTERVAL SPECIFIED - ''TRAFFIC'' ASSUMED')(A);
 TRAFSW='1'B;
 END;
 INTERVAL=SUBSTR(PARMS, I+9,2);
 END;
 IF TRAFSW='1'B THEN
 PUT SKIP EDIT (' INTERVAL USED IS ', INTERVAL)(A,F(2));
 IF INDEX(PARMS, 'FILTER')¬=0 THEN DO;
 FILTERSW='1'B;                /* SET ON FILTER REQUESTED SWITCH     */
 PUT SKIP EDIT (' FILTERING REPORT REQUESTED')(A);
 END;
 ELSE DO;
 PUT SKIP EDIT (' NO FILTERING REPORT REQUESTED')(A);
 END;
1START:                         /* TWO PASS PGM - SO INITIALISE      */
 PUT SKIP (3) EDIT (' ANALYSIS STARTED ')(A);
 ALLCT=0;
 RCCT = 0;
 NUM_MSGS, NUMRECS, TOTMSGS = 0;
 RESPCT=0;
 NUMIDS, WTOCOUNT, NUMRET = 0;
 BASE_TIME = 0;
 IDTAB = ' ';                   /* BLANK OUT THE FREQUENCY TABLE     */
 FREQ = 0;                      /* ZERO THE FREQUENCY COUNTS         */
 RESP_FREQ = 0;
 BASE_YR = ' ';
 TFREQ=0;                       /* INIT TRAFFIC FREQ ARRAY           */
 INTERVAL_END_MSGS=0;           /* AND COUNT OF MSGS AT LAST INTERVAL*/
1
 ON ERROR GO TO ERRRTN;
 GO TO STTT;
 ERRRTN:
 PUT SKIP DATA(BUFIN);
 ERROR = '1'B;
 RETURN;
 STTT:
 ERROR = '0'B;                 /* MAIN PROCESSING LOOP OF PROGRAM    */
 DO WHILE (ERROR = '0'B);
 GETR: CALL GETREC;            /* GET NEXT RECORD TO PROCESS         */
 CALL MSGSTAT;                 /* COLLECT STATISTICS ON MESSAGE FREQ */
 IF MSGFILSW = '1'B THEN DO;/* IF FILTERED OR UNSELECTED MESSAGE,    */
 IF MSGMLTSW ¬= '1'B THEN      /* IF IT'SA SINGE LINE MESSAGE        */
 MSGFILSW='0'B;                /* SET OFF SW FOR NEXT MESSAGE        */
 GO TO GETR;                   /* ANYWAY - NO TIMING OR TRAFFIC REQD */
 END;
 CALL TIMECNV;                 /* CONVERT THE TIME FIELD             */
 IF TRAFSW = '1'B THEN         /* CALL TRAFFIC ANALYSIS IF REQUESTED */
 CALL TRAFFIC;
 END;                          /* END OF 'MAIN LOOP                  */
 ENDF:                         /* END OF FILE ENTRY FOR SYSLOG FILE  */
 IF NUMRECS = 0 THEN DO;
 PUT SKIP(2) EDIT (' *** ERROR***')(A);
 PUT SKIP EDIT (' NO RECORDS READ FROM SYSLOG FILE')(A);
 PUT SKIP EDIT (' NO ANALYSIS DONE')(A);
 GO TO PROGEND;
 END;
 IF NUM_MSGS = 0 THEN DO;
 PUT SKIP(2) EDIT (' *** ERROR***')(A);
 PUT SKIP EDIT (' NO MESSAGE IDS SELECTED FROM SYSLOG FILE')(A);
 PUT SKIP EDIT (' NO ANALYSIS DONE')(A);
 GO TO PROGEND;
 END;
1                               /* REPORTING PHASE                   */
 RUN_TIME = (CONVERTED_TIME - BASE_TIME) +
 ( (END_YR - BASE_YR)*365 +(END_DY - BASE_DY)) * 24* 3600 ;
 PUT SKIP(3) EDIT (' RUN TIME IN SECONDS ......... ',
                         RUN_TIME)(A,F(10) );
 PUT SKIP EDIT (' RECORDS IN SYSLOG DATA SET...',NUMRECS) (A,F(10) );
 PUT SKIP EDIT (' NUMBER OF RETAINED MESSAGES..',
                         NUMRET)(A,F(10) );
 PUT SKIP EDIT (' NUMBER OF WTOR MESSAGES......',
                           WTOCOUNT)(A, F(10) );
 PUT SKIP EDIT (' NUMBER OF MESSAGE LINES......',
                         NUM_MSGS)(A,F(10) );
 PUT SKIP EDIT (' AVG MSG RATE (LINES/SEC).....',
            FLOAT(NUM_MSGS)/RUN_TIME)(A,F(10,2));
 PUT SKIP EDIT (' MUMBER OF UNIQUE MESSAGE IDS.', NUMIDS) (A,F(10) );
 PUT SKIP(2) EDIT ('  *** NO. OF LINES BY ROUTE CODES ***') (A);
 PUT SKIP(2) EDIT (' NUMBER WITH ALL ROUTECODES...',ALLCT)(A,F(10));
 DO I=1 TO 16;
 PUT SKIP EDIT ( RCDESC(I), RCCT(I) )(A, F(10) );
 END;
 IF SELSW='1'B THEN              /* IF THIS IS 'FILTER ' REPORT      */
 PUT SKIP EDIT (' PERCENTAGE OF MSGS FILTERED.',
 ((UNFIL - NUM_MSGS)*100)/UNFIL, '%' )(A,F(10),A);
 IF ALLSW='1'B THEN DO;
 PUT PAGE EDIT (' NUMBER OF SOLICITED MESSAGES.', RESPCT) (A,F(10) );
 PUT SKIP(3) EDIT ('  ***LENGTHS OF MULTILINE MESSAGES **') (A);
 PUT SKIP EDIT ('  *** FOR COMMAND RESPONSES ***') (A);
 PUT SKIP(2) EDIT( ' LENGTH    FREQUENCY', ' ') ( A,SKIP);
 DO I=2 TO 19;                   /* FREQ OF LENGTHS OF SOLICITED     */
 PUT SKIP EDIT ( I, RESP_FREQ(I) )( F(5), X(1), F(10) ); /* MESSAGES */
 END;
 PUT SKIP EDIT ( ' >19', RESP_FREQ(20) )( A(5), X(1), F(10) );
 END;                            /* END LINE FREQ REPORTING          */
 IF TRAFSW='1'B THEN DO;         /* IF TRAFFIC ANALYSIS REQUESTED    */
 FTOT=0.0;                      /* RUNNING COUNT - NO OF INTVLS      */
 PUT PAGE EDIT('     *** TRAFFIC ANALYSIS ******')(A);
 PUT SKIP(2) EDIT
 (' THIS GIVES THE FREQUENCIES OF INTERVALS FOR WHICH THE',
 ' MESSAGE TRAFFIC RATE (LINES/SEC) FELL WITHIN VARIOUS ',
 ' BANDS. INTERVALS WITH ZERO RATE ARE EXCLUDED.      ') (A, SKIP);
 PUT SKIP(2) EDIT('  RATE      FREQ      PERCENT') (A);
 DO I=1 TO 100;
 FTOT = FTOT + TFREQ (I);         /* GET SUM OF FREQUENCIES          */
 END;
 DO I=1 TO 19 ;
 PUT SKIP EDIT((I-1)/2, '-', (I/2), TFREQ(I),
          (TFREQ(I)/FTOT*100) )
          ( F(4,1),A, F(4,1), F(8), F(10,2) );
 END;
 PUT SKIP EDIT( '>9.9', TFREQ(20),
          (TFREQ(I)/FTOT*100) )
             (A, X(5), F(8), F(10,2) );
 END;                              /* END TRAFFIC ANALYSIS REPORT    */
 CALL FREQAN;                      /* ANALYSE FREQ OF MSGS           */
 IF SELSW='1'B THEN GO TO PROGEND; /* IF THIS IS 'FILTER ' REPORT    */
 IF FILTERSW='1'B THEN GO TO GETFIL;
 PROGEND:
 PUT SKIP(2) EDIT (' ***  END OF PROGRAM ***')(A);
 RETURN;                           /* SINGLE RETURN FROM PROGRAM     */
 GETFIL:                           /* FILTER ROUTINE                 */
 UNFIL = NUM_MSGS;                 /* SAVE TOTAL NUMBER OF MESSAGES  */
 PUT PAGE EDIT(' *** FILTERING REPORT ****') (A);
 MSGFILT = '      ';
 DO I = 1 TO NUMFIL;              /* SET UP FILTER MATRIX            */
 READFIL:
 READ FILE(FILTER) INTO (FILTWA);
 IF ( (SUBSTR(FILTWA, 1,1)='.' ) |  /* DOT IN COL 1 IS SCREEN HANDLING*/
 (INDEX(FILTWA, 'SUP(NO)') > 0))  /* SUP(NO) MEANS NO SUPPRESS       */
 THEN GO TO READFIL;
 DO II=1 TO 80;                   /* REMOVE ANY LEADING BLANKS IN    */
 IF SUBSTR(FILTWA, 1,1)=' ' THEN  /* THE PARMLIB RECORD              */
 FILTWA = SUBSTR(FILTWA,2,79) ||' ';
 ELSE GO TO FINDFILT;
 END;
 GO TO READFIL;                   /* IF COMPLETELY BLANK             */
 FINDFILT:
 J=INDEX(FILTWA,' ');             /* FIND FIRST BLANK                */
 K=INDEX(FILTWA,',');             /* AND FIRST COMMA                 */
 IF ( (K>0) & (K<J) )
      THEN J=K;                   /* CHOOSE FIRST OF EITHER          */
 IF SUBSTR(FILTWA,J-1,1)='*'      /* IF GENERIC FILTERING            */
 THEN FIL_LEN(I) = J-2;           /* SET COMPARE TO LENTH OF GENERIC */
 ELSE FIL_LEN(I) = J;             /* ELSE FULL LENGTH OF MSGID       */
 MSGFILT(I)=SUBSTR(FILTWA,1,J-1) ||' '; /* PUT IN FILTER ARRAY       */
 END;
 ENDFIL: MSGSSEL = I - 1;         /* END OF FILE FOR FILTER FILE     */
 PUT SKIP(2) EDIT (' FILTERED MSGS.................', MSGSSEL)
                 (A, F(10) );
 IF MSGSSEL = 0 THEN GO TO PROGEND;
 PUT SKIP(2) EDIT(' MESSAGES FILTERED ARE: ',' ')(A,SKIP);
 DO I=1 TO MSGSSEL+4 BY 4;
 PUT SKIP EDIT (MSGFILT(I), MSGFILT(I+1), MSGFILT(I+2), MSGFILT( I+3) )
    ( A(10), X(2) );
 END;
 CLOSE FILE(SYSLOG);               /* CLOSE - TO REOPEN AT START     */
 SELSW = '1'B;
 GO TO START   ;
 DCL (SELSW) BIT(1) STATIC INIT('0'B); /* INDICATES A FILTERING PASS */
 DCL MSGSSEL FIXED BIN(15) INIT(0);   /* NO. OF FILTERED MSGS SELI'D */
 DCL NUMFIL FIXED BIN(15) STATIC INIT(999); /* MAX VALUE OF MSGSSEL  */
 DCL MSGFILT (999) CHAR(10);           /* TABLE OF FILTERS           */
 DCL FIL_LEN (999) FIXED BIN(15);      /* TABLE OF LENGTH OF FILTERS */
1GETREC: PROC;                     /* GET NEXT RECORD FOR PROCESSING */
 GET:
 TEXT=(74)' ';                    /* CLEAR TO AVOID 'RESIDUE' PROBS */
 READ FILE(SYSLOG) INTO(BUFIN);
 /*   IF CCTL='1' THEN GOTO GET;      IGNORE THIS AS IT'S A PRINT CC */
 IF TYPE = ' ' THEN GOTO GET;      /* IGNORE BLANK RECORD            */
 NUMRECS = NUMRECS + 1;            /* COUNT RECORDS IN SYSLOG        */
 IF ((TYPE='X') | (TYPE='O') )    /* IGNORE - RECORD EXTERNAL TO MCS */
    THEN GOTO GET;                 /* OR RECORD WRITTEN BY LOG CMD   */
 IF SUBSTR(TEXT, 1,1) ='-'         /* IF IT'S A MSG FROM IEFACTRT IDL*/
 THEN SUBSTR(TEXT,1,6) = 'ACTRT '; /*  ... FLAG WITH UNIQUE ID    IDL*/
 IF SUBSTR(TEXT, 1,2) ='R '        /* IF IT'S A REPLY TO A WTOR      */
 THEN DO;                          /* ONE MESSAGES TO SYSLOG BUT     */
 RESPCT = RESPCT + 2;            /* TWO (SOLICITED) MESSAGES TO OPER */
 GO TO GET;
 END;
 IF ( (COMRESP='C') | (COMRESP= 'I') ) THEN GO TO GET;
 IF SUBSTR(TEXT,1,1) = '+' THEN TEXT = SUBSTR(TEXT,2,73);
 IF TYPE = 'W' THEN SUBSTR(TEXT, 1,2)='  ';
 REMBLK:
 DO I = 1 TO 50;
 IF SUBSTR(TEXT,1,1)=' ' THEN DO;    /* REMOVE LEADING BLANKS        */
 TEXT=SUBSTR(TEXT,2,73) ||' ';
 END;
 END;
 J=INDEX(TEXT,' ');                  /* FIND FIRST BLANK AND         */
 MSGID=SUBSTR(TEXT,1,J);             /* ISOLATE THE MSGID            */
 TEXT = SUBSTR(TEXT, J+1,40);        /* SQUEEZE UP REMAINDER OF TXXT */
 IF SUBSTR(TEXT,1,3)='NIP' THEN GO TO GET;
 END GETREC;
1MSGSTAT: PROC;                      /* KEEP COUNT OF MSGIDS         */
 IF MSGFILSW='1'B THEN        /* IF THE SUBSEQUENT LINES OF A MULTI */
 GO TO EXSTAT;                       /* LINE MSG TO BE NOT SELECTED  */
 IF TYPE='S' THEN GOTO ADD1;    /* JUST COUNT CONTINUATIONS           */
 IF ( (SUBSTR(JOBNUM,1,2)='  ') & (SUBSTR(JOBNUM,3,2)¬='  ') )
  THEN DO;
 IF SUBSTR(JOBNUM,3,2)¬=CONSOLE /* DOES IT MATCH REQUESTED CONSOLE   */
 THEN DO;
 IF TYPE='M' THEN DO            /* IF MAJOR LINE                      */
                  MSGFILSW='1'B;/*               SET ON FILTER SWITCH */
                  MSGMLTSW='1'B;/*               SET ON MULTI  SWITCH */
                  END;          /*               END MAJOR LINE       */
 GO TO EXSTAT;                 /* AND IGNORE                         */
 END;
 END;
 IF COMRESP='R' THEN DO;       /* SOLICITED MESSAGES I. E. RESPONSES */
 IF ALLSW = '0'B THEN          /* IF NOT COUNTING RESPONSE MESSAGES  */
 GOTO EXSTAT;                  /* THEN RETURN                        */
 RESPCT = RESPCT + 1;          /* KEEP COUNT OF LINES TO OPERATOR    */
 IF TYPE='M' THEN LINE = 1;    /* FIRST LINE OF MULTILINE - INIT CTR */
 IF ( (TYPE='D') | (TYPE='L') ) /* IF DATA OR LABEL, ADD 1 TO LINE CT*/
   THEN LINE = LINE + 1;
 IF TYPE='E' THEN DO;          /* IF FINAL LINE                      */
 LINE = LINE +1;               /* GET TOTAL LINES, WITH MAX OF 20)   */
 IF LINE > 20 THEN LINE = 20;
 RESP_FREQ(LINE) = RESP_FREQ(LINE) + 1;    /* KEEP FREQ DISTRIBUTION */
 END;
 GO TO RCDONE;                 /* NO NEED FOR RC CHECK ON RESPONSES  */
 END;                          /* END OF SOLICITED MESSAGE PROCESSNG */
 IF ROUTECODE='    ' THEN GO TO RC2;
 DO J=1 TO 4;                  /* PROCESS 4 HEX DIGITS OF ROUTECODE  */
 I = INDEX( HEXDIGITS, SUBSTR(ROUTECODE,J,1) );
 SUBSTR(RCODES, (J*4-3), 4) = XFLAGS(I);
 END;
 RC2:
 RCSELSW='0'B;
 DO I=1 TO 16;
 IF ( (SUBSTR(RCODES,I,1) = '1') & (SUBSTR(SELRC,I,1) = '1') ) THEN DO;
 RCCT(I) = RCCT(I)+1;           /* ADD 1 TO CNT OF ROUTECODE         */
 RCSELSW = '1'B;                /* SHOW THIS MESSAGE SELECTED BY     */
 END;
 END;                           /* END I LOOP                        */
 IF RCSELSW='0'B THEN DO;       /* IF MESSAGE NOT SELECTED BY RC     */
 IF TYPE='M' THEN DO            /* IF MAJOR LINE                      */
                  MSGFILSW='1'B;/*               SET ON FILTER SWITCH */
                  MSGMLTSW='1'B;/*               SET ON MULTI  SWITCH */
                  END;          /*               END MAJOR LINE       */
 GO TO EXSTAT;                  /* NOT COUNTED                       */
 END;
 RCDONE:
 IF ( (RETAIN='*') | (RETAIN='@') ) THEN NUMRET=NUMRET+1;
 IF TYPE='W' THEN               /* IF IT'S A WTOR MESSAGE THEN KEEP  */
 WTOCOUNT = WTOCOUNT + 1;       /* A SPECIAL TALLY OF THEM           */
 IF ( (TYPE='L') | (TYPE='D') | (TYPE='E') |
 (TYPE='S') ) THEN DO;
 IF MSGFILSW = '1'B THEN        /* DON'T COUNT FOR FILTERED MESSAGE  */
        GO TO EXSTAT;
 GO TO UPFREQ;                  /* BUT DO FOR THE REST               */
 END;
 IF SELSW='1'B THEN DO;         /* IF DOING FILTERING REPORT         */
 DO J=1 TO MSGSSEL;             /* NO COUNT IF IT MATCHES FILTER     */
 K=FIL_LEN(J);
 IF SUBSTR(MSGID,1,K)=SUBSTR(MSGFILT(J),1,K) THEN DO;
 MSGFILSW = '1'B;               /* DON'T COUNT - FILTERED MESSAGE    */
 GOTO EXSTAT;END;
 END;                           /* END J LOOP                        */
 MSGFILSW = '0'B;               /* DO COUNT THIS MESSAGE             */
 END;                           /* END OF FILTER CODE                */
 IF NUMIDS = 0 THEN DO;         /* FIRST MSGID TO GO IN TABLE        */
 NUMIDS, IDPTR=1;               /* INITIALISE COUNT & CURRT POINTER  */
 GO TO INITID;                  /* GO ADD FIRST MESSAGE TO TABLE     */
 END;
 DO IDPTR = 1 TO NUMIDS;        /* SEE IF IT'S ALREADY BEEN SEEN     */
 IF IDTAB(IDPTR) = MSGID THEN GO TO UPFREQ;    /*      YES           */
 END;
 IF NUMIDS = MAXNUMID THEN DO;  /* CHECK TABLE SIZE NOT EXCEEDED     */
 PUT SKIP(2) EDIT (' *** ERROR***')(A);
 PUT SKIP EDIT (' TOO MANY UNIQUE MESSAGE IDS HAVE BEEN FOUND') (A);
 PUT SKIP EDIT (' MAXIMUM IS ', MAXNUMID)(A,F(6) );
 PUT SKIP EDIT (' ANALYSIS WILL BE DONE FOR PARTIAL SYSLOG FILE') (A);
 GO TO ENDF;
 END;
 NUMIDS = NUMIDS +1;             /* NEW ID RECOGNISED CAN BE ADDED   */
 IDPTR = NUMIDS;                 /* IDPTR SHOWS LAST MESSAGE ID      */
 INITID:                         /* ADD A NEW MSG TO THE TABLE       */
 IDTAB(IDPTR ) = MSGID;          /* REMEMBER MESSAGE ID              */
 FREQ(IDPTR ) = 0;               /* NOTE - FREQ CT IS INIT'D TO ZERO */
 MTEXT(IDPTR ) = SUBSTR (TEXT,1,40);/* PART TEXT OF 1ST OCCURRENCE   */
 UPFREQ:
 IF ( (TYPE='M') | (TYPE='N') | (TYPE='W') ) THEN
 FREQ(IDPTR ) = FREQ(IDPTR) + 1; /* FREQUENCY OF MSG IDENT           */
 IF ROUTECODE='FFFF' THEN DO;    /* ALL SET - TREAT A SPECIAL CASE   */
 ALLCT = ALLCT +1;               /* SINCE LIKELY USER CODE           */
 GOTO ADD1;                      /* ADD TO TOTAL AND EXIT            */
 END;
 ADD1:
 NUM_MSGS = NUM_MSGS + 1;        /* COUNT LINES SEEN BY OPERATOR     */
 EXSTAT:
 IF TYPE='M' THEN MSGMLTSW='1'B; /* SET ON SWITCH FOR FIRST LINE     */
 IF TYPE='E' THEN MSGMLTSW='0'B; /* SET OFF SWITCH FOR LAST LINE     */
 RETURN;
 END MSGSTAT;
 FREQAN: PROC ;
 SORT:                            /* SORT THE FREQUENCY TABLE INTO   */
 SORTSW='0'B;                     /* DESCENDING SEQUENCE             */
 DO I = 1 TO NUMIDS - 1;
 IF FREQ(I+1) > FREQ(I) THEN DO;
 TFREQ = FREQ(I);
 TIDT = IDTAB(I);
 TMTXT = MTEXT(I);
 IDTAB(I) = IDTAB(I+1);
 FREQ(I) = FREQ(I+1);
 MTEXT(I) = MTEXT(I+1);
 IDTAB(I+1) = TIDT;
 FREQ(I+1) = TFREQ;
 MTEXT(I+1) = TMTXT;
 SORTSW = '1'B;
 END;
 END;                              /* END I LOOP                     */
 IF SORTSW = '1'B THEN GO TO SORT;
 SORTDON:
 FTOT = FLOAT(NUM_MSGS);
 R_COUNT = 0;
 PUT PAGE EDIT ('*** MESSAGE ID FREQUENCY ANALYSIS ***')(A);
 PUT SKIP(2) EDIT
 ( ' MSGID       FREQ  %AGE    CUM%  PARTIAL TEXT OF FIRST OCCURRENCE',
 ' ') (A, SKIP);
 DO I = 1 TO NUMIDS;
 R_COUNT = R_COUNT + FREQ(I);
 PUT SKIP EDIT( IDTAB(I), FREQ(I), FLOAT(FREQ(I))/FTOT*100.0,
  FLOAT(R_COUNT)/FTOT*100.0  , MTEXT(I) )
        ( X(1),A(8), F(8), F(6,2), X(2), F(6,2),X(2),A );
  FREQ(I) = 0 ;                   /* RESET MATRIX ENTRY              */
  IDTAB(I) = ' ' ;                /* RESET MATRIX ENTRY              */
  MTEXT(I) = ' ' ;                /* RESET MATRIX ENTRY              */
 END;
 PUT SKIP(2) EDIT ( ' TOTAL ', R_COUNT) ( A(7), F(10));
 NUMIDS   = 0 ;                   /* RESET COUNT                     */
 RETURN;
 DCL TIDT CHAR(10);               /* TEMPORARY STORAGE FOR SORT      */
 DCL TFREQ FIXED BIN(31);         /*   ..    DITTO        ..         */
 DCL TMTXT CHAR(40);              /*   ..    DITTO        ..         */
 DCL R_COUNT FIXED BIN(31) INIT(0); /* RUNNING CNT OF NO OF LINES    */
 DCL SORTSW BIT(1) STATIC INIT('0'B);

 END FREQAN;

 TIMECNV: PROC;       /* CONVERTS TIME & KEEPS TRACK OF DAYS & YEARS */
 IF( (COLON1 ¬= ':') | (COLON2 ¬= ':') ) THEN RETURN;
 CONVERTED_TIME =
           HOUR*3600 + MINUTE*60 + SECOND;
 IF BASE_TIME=0 THEN DO;
 BASE_TIME = CONVERTED_TIME;
 INTERVAL_END = BASE_TIME;         /* INIT FOR TRAFFIC ANALYSIS      */
 END;
 IF YEAR ¬=' ' THEN DO;
 IF BASE_YR = ' ' THEN DO; BASE_YR = YEAR; BASE_DY = DAY; END;
 END_YR = YEAR; END_DY = DAY;
 END;
 END TIMECNV;
 DCL CONVERTED_TIME FIXED BIN(31);
 DCL BASE_TIME FIXED BIN(31) INIT(0) STATIC;
 DCL (BASE_YR, END_YR) CHAR(2) INIT(' ') STATIC;
 DCL (BASE_DY, END_DY) CHAR(3) INIT(' ') STATIC;
 TRAFFIC: PROC ;                  /* THIS PROCEDURE ANALYSES RATE AT */
 IF CONVERTED_TIME > INTERVAL_END THEN DO;
 INTERVAL_MSGS = NUM_MSGS - INTERVAL_END_MSGS;
 INTERVAL_END_MSGS = NUM_MSGS;
 INTERVAL_END = INTERVAL_END + INTERVAL;
 IF INTERVAL_END < CONVERTED_TIME
 THEN INTERVAL_END = CONVERTED_TIME + INTERVAL;
 IRATE =( (INTERVAL_MSGS*2)     /* RATE OF MESSAGES IN THIS INTERVAL */
       / INTERVAL) + 1;         /* RATE IN UNITS OF 0.5 MSGS/SEC     */
 IF IRATE>20 THEN IRATE=20 ;    /* SET MAX OF 10 MSGS/SEC            */
 TFREQ(IRATE) = TFREQ(IRATE) + 1;/* KEEP FREQ COUNT                  */
 END;                           /* END OF INTERVAL END PROCESSING    */
 RETURN;                        /* EXIT TRAFFIC ROUTINE              */
 END TRAFFIC;
 DCL TRAFSW BIT(1) STATIC INIT('0'B); /* INDICATES TRAFFIC REQUESTED */
 DCL INTERVAL FLOAT STATIC;     /* INTERVAL USED IN SECONDS          */
 DCL INTERVAL_END FLOAT STATIC; /* TIME NEXT INTERVAL EXPIRES        */
 DCL INTERVAL_MSGS FIXED BIN(31);/* NUMBER OF MSGS THIS INTERVAL     */
 DCL INTERVAL_END_MSGS FIXED BIN(31);
 DCL IRATE FIXED BIN(15);       /* RATE THIS INTERVAL                */
 DCL TFREQ (100) FIXED BIN(31); /* TRAFFIC FREQUENCY ARRAY           */
 END;                           /* END OF PROGRAM                    */
