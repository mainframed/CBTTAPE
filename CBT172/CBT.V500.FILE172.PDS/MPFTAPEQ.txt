MPFTAPEQ TITLE 'SET RETURN CODE IF TAPE AVAILABLE'
***********************************************************************
*        PDSDOC *SET RETURN CODE IF TAPE MAY BE IN A STACKER..........* MPFTAPEQ
*                                                                     *
*        PROGRAM ID. :             MPFTAPEQ                           *
*                                                                     *
*        DATE WRITTEN :            OCTOBER   1992                     *
*                                                                     *
*        AUTHOR :                  D.H.CARTWRIGHT.                    *
*                                                                     *
*        LOCATION :                SPRECHER ENERGIE                   *
*                                  KIRCHWEG 5                         *
*                                  5036 OBERENTFELDEN                 *
*                                  SWITZERLAND                        *
*                                                                     *
*        HISTORY  :                                                   *
*                                                                     *
* 20OCT92 CART1 STARTED WRITING                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*       ---------                                                     *
*                                                                     *
* IN SPITE OF ITS NAME THIS IS NOT AN MPF EXIT, BUT IS PART OF THE    *
* 'MPFTAPE.' FAMILY. THIS PROGRAM SCANS THE IN-STORAGE TABLES OF      *
* 'MPFTAPEM' AND 'MPFTAPEK' TO SEARCH FOR THE VOLUME SERIAL PASSED TO *
* IT AS A PARM. IF FOUND THE RETURN CODE IS SET TO ZERO AND R0 IS SET *
* TO THE DEVICE NUMBER WHERE THE TAPE SHOULD BE. OTHERWISE VARIOUS    *
* ERROR RETURN CODES ARE SET AND R0 IS SET TO ZERO.                   *
*                                                                     *
* IT IS INTENDED TO BE USED FROM WITHIN TSSO CLISTS TO AUTOMATE HSM   *
* OPERATOR REPLIES WHICH QUERY TAPE AVAILABILITY.                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*       ------------                                                  *
*                                                                     *
* NONE                                                                *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*       INPUT                                                         *
*       -----                                                         *
* PARM FIELD - ONE TO SIX CHARACTER VOLUME SERIAL NUMBER              *
*                                                                     *
*                                                                     *
*       OUTPUT                                                        *
*       ------                                                        *
*                                                                     *
* THE RETURN CODE IS SET TO INDICATE IF AND WHERE A TAPE MAY BE FOUND *
*                                                                     *
* RC =  0      VOLUME FOUND. R0 CONTAINS THE HEX DEVICE NUMBER.       *
* RC =  4      VOLUME NOT FOUND. R0 IS SET TO ZERO                    *
* RC =  8      INVALID PARM.                                          *
* RC = 12      MPFTAPE TABLES DO NOT EXIST.                           *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         PRINT ON,GEN              SHOW MACRO EXPANSIONS
*        PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
         SPACE 1
MPFTAPEQ CSECT
MPFTAPEQ AMODE 31
MPFTAPEQ RMODE ANY
         SPACE 1
***********************************************************************
*        REGISTER EQUATES AND USAGES.                                 *
***********************************************************************
         SPACE 1
R0       EQU   0
R1       EQU   1
R2       EQU   2
REG2     EQU   R2
R3       EQU   3                   CVT POINTER, SET RETURN CODE
R4       EQU   4                   PARM POINTER, SET REG. ZERO
R5       EQU   5                   TABLE VOLUME POINTER
R6       EQU   6
R7       EQU   7
R8       EQU   8                   CVTUSER POINTER
R9       EQU   9
R10      EQU   10
R11      EQU   11                  TAPE TABLES POINTER
R12      EQU   12                  PROGRAM BASE
R13      EQU   13                  SAVE AND WORK AREA POINTER
R14      EQU   14
R15      EQU   15
         SPACE 1
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'MPFTAPEQ'
         DC    C'&SYSDATE',C' &SYSTIME'
*        PRINT NOGEN
         PRINT ON,GEN
START    L     R4,0(R1)      LOAD POINTER TO PASSED PARM LIST      JWP
         SPACE
         GETMAIN RU,LV=LWORK,SP=30,LOC=(BELOW,ANY) SAVE AREA GETMAIN
         SPACE 1
         LR    R10,R1        SAVE POINTER TO GOTTEN AREA           JWP
         MVI   0(R10),X'00'  CLEAR GOTTEN AREA                     JWP
         MVC   1(LWORK-1,R10),0(R10)                               JWP
         ST    R13,4(R10)    EXCHANGE THE REGISTER SAVE AREA       JWP
         ST    R10,8(R13)    POINTERS TRICK                        JWP
         LR    R13,R10                                             JWP
         USING WORKREGS,R13        ESTABLISH WORK AREA ADDSESSABILITY
         EJECT
***********************************************************************
*        TEST FOR A PARM FIELD AND PROCESS IT                         *
***********************************************************************
         SPACE 2
PARMTEST DS    0H
         LH    R2,0(R4)            LOAD LENGTH OF PARM FIELD
         LTR   R2,R2               ANYTHING THERE ?
         BZ    BADPARM             NO. BYPASS PARM PROCESSING
         CH    R2,=H'6'            CHECK SIZE
         BH    BADPARM             ERROR IF TOO LONG
         MVC   PARMAREA,BLANKS     INITIALISE RECEIVING AREA
         BCT   R2,*+4+6            SUBTRACT 1 FROM LENGTH
         MVC   PARMAREA,2(R4)      COPY THE VOLUME SERIAL     *EXECUTE*
         EX    R2,*-6              EXECUTE THE MOVE INSTRUCTION
         EJECT
***********************************************************************
*        CHECK FOR TAPE IN STACKERS                                   *
***********************************************************************
         SPACE 1
         L     R3,CVTPTR           FIND USERCVT
         USING CVT,R3
         ICM   R8,B'1111',CVTUSER  GET CVTUSER ADDRESS
         BZ    NOTABLE             ERROR IF NOT THERE
         USING USERCVT,R8          ESTABLISH ADDRESSABILITY
         DROP  R3                  END CVT ADDRESSABILITY
         SPACE 1
START2   DS    0H                  CHECK VOLUME SERIALS
         SPACE 1
         ICM   R11,B'1111',UCVTTAPE FIND THE TAPE UNIT ADDRESS TABLE
         BZ    NOTABLE             ERROR IF NOT THERE
         LA    R11,HDRLEN(R11)     SKIP PAST THE TAPE TABLE HEADER
         USING TAPETAB,R11         ESTABLISH TABLE ADDRESSABILITY
CKVOLSER CLC   =X'FFFFFFFF',0(R11) IS THIS THE END OF THE TABLE?
         BE    NOTFOUND            YES, FORGET IT
         LH    R4,TABCOUNT         PICK UP THE NUMBER OF TABLE ENTRIES
         LA    R5,TABVOLS          FIND THE FIRST TABLE ENTRY
CKVOLSE1 CLC   PARMAREA,0(R5)      IS THIS THE VOLUME SERIAL NUMBER?
         BE    FOUNDIT             YES, CONTINUE
         LA    R5,6(R5)            NO, TRY ANOTHER
         BCT   R4,CKVOLSE1         TRY ANOTHER VOL=SER=
         LR    R11,R5              SET UP THE NEW POINTER
         B     CKVOLSER            GO TO IT
         SPACE 1
         EJECT ,
***********************************************************************
*        FOUND THE VOLUME. SET DEVICE ADDRESS IN REGISTER ZERO        *
***********************************************************************
         SPACE 1
FOUNDIT  DS    0H                  GOT UNIT TABLE
         MVC   DEVADR,TABUNIT      COPY DEVICE ADDRESS
         TR    DEVADR,TBLA         TRANSLATE DEVICE ADDRESS
         PACK  PDEVADR,DEVADR      PACK DEVICE ADDRESS
         L     R4,PDEVADR+4        PICK UP HEX REPRESENTATION OF CUU
         SRL   R4,4                SHIFT OUT SIGN
         SR    R3,R3               SET RETURN CODE ZERO
         SPACE 2
ENDOFPGM DS    0H                  NOW SHUTTING DOWN
         LR    R1,R13       LOAD ADDRESS OF GOTTEN AREA           JWP
         L     R13,4(R13)   LOAD PREVIOUS SAVE AREA ADDRESS       JWP
         LA    R0,LWORK     LOAD SIZE OF GOTTEN AREA              JWP
         FREEMAIN RU,LV=(0),A=(1),SP=30 FREE THE GOTTEN AREA
         LR    R0,R4               RELOAD REGISTER ZERO
         LR    R15,R3              RELOAD REGISTER 15
         LM    R1,R12,24(R13)      RELOAD GENERAL PURPOSE REGISTERS
         L     R14,12(R13)         RELOAD RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         SPACE 2
         DROP  R8                  END USERCVT ADDRESSABILITY
         DROP  R11                 END TAPETAB ADDRESSABILITY
         EJECT ,
***********************************************************************
*        ERROR ROUTINES                                               *
***********************************************************************
         SPACE 2
NOTFOUND DS    0H                  VOLUME NOT IN STACKERS
         LA    R3,4                SET RETURN CODE
         B     ERROREXT            GO TO END OF PROCESSING
         SPACE 2
BADPARM  DS    0H
         WTO   'TQE00001 YOUR PARM FIELD IS INVALID AND PROCESSING IS  X
               TERMINATED',ROUTCDE=11
         LA    R3,8                SET RETURN CODE
         B     ERROREXT            GO TO END OF PROCESSING
         SPACE 2
NOTABLE  DS    0H                  MPFTAPE. TABLES NOT FOUND
         LA    R3,12               SET RETURN CODE
         SPACE 2
ERROREXT DS    0H                  ERROR EXIT TO COMMON CODE
         SR    R4,R4               CLEAR REGISTER ZERO MEMORY
         B     ENDOFPGM            GO TERMINATE
         TITLE 'DATA AREAS'
BLANKS   DC    CL8' '              SOME HANDY BLANKS
*
HEXADDR  DC    XL4'00000000'       DEVICE ADDRESS
*
TBLA     DS    0F                  VALID DISPLAY-HEX CHARACTER TABLE
         DC    193X'00'            HEX 00 TO C0
         DC    XL6'FAFBFCFDFEFF'       C1 TO C6 (ALPHA A-F)
         DC    41X'00'                 C7 TO EF
         DC    CL10'0123456789'        F0 TO F9 (NUMERIC 0-9)
         DC    6X'00'                  FA TO FF
         SPACE 1
         LTORG *
         EJECT ,
**********************************************************************
*                                                                    *
*        DSECT USED TO MAP GOTTEN AREA                               *
*                                                                    *
**********************************************************************
         SPACE 1
WORKREGS DSECT
         SPACE 1
REGAREA  DS    18F                 REGISTER SAVE AREA
         SPACE 1
         DS    0D
PDEVADR  DS    PL8'0'              PACKED DECIMAL WORK AREA
DEVADR   DS    CL3'0'              DEVICE NUMBER WHERE FOUND
PARMAREA DS    CL6' '              RECEIVING AREA FOR PARM
         SPACE 1
LWORK    EQU   *-WORKREGS          LENGTH OF WORK AREA
         EJECT ,
         COPY CSATABLE             COPY TABLE VOLUME TABLE FORMAT
         EJECT ,
         COPY USERCVT              COPY USERCVT FORMAT
         SPACE 1
         PRINT NOGEN
         CVT   DSECT=YES,LIST=YES
         SPACE 3
         END   MPFTAPEQ
