MPFTAPEK TITLE 'MPFTAPEK -- WTO EXIT TO MAINTAIN TAPE UNLOAD DATA'
***********************************************************************
*        PDSDOC *MAINTAIN TAPE TABLES IN CSA..........................* MPFTAPEK
*                                                                     *
* THIS MPF EXIT WAS ORIGINALY ON THE CBT TAPE LEVEL 321 FILE 111.     *
* ITS PURPOSE IS TO MAINTAIN AN IN-CORE TABLE OF TAPES USED SO THAT   *
* IF THE SAME VOLUME IS REQUESTED SOON AFTER BEING DISMOUNTED A HINT  *
* CAN BE GIVEN TO THE OPERATOR. IT SHOULD BE RE-ENTRANT.              *
*                                                                     *
* 05DEC91 CART1 REWROTE WTOETPS1 TO USE DSECTS AND COMMON CODE        *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
MPFTAPEK CSECT
MPFTAPEK AMODE 31
MPFTAPEK RMODE ANY
         PRINT ON,NOGEN
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'MPFTAPEK'
         DC    C'&SYSDATE',C' &SYSTIME'
START    LR    R3,R1         PRESERVE POINTER TO PASSED PARM LIST  JWP
         SPACE
         GETMAIN RU,LV=AREAREGS,SP=230,LOC=(BELOW,ANY) GETMAIN FOR REGS
         SPACE
         GOSUB CLEAR         CLEAR GOTTEN AREA
         ST    R13,4(R1)     EXCHANGE THE REGISTER SAVE AREA
         ST    R1,8(R13)     POINTERS TRICK
         LR    R13,R1
         USING WORKREGS,R13  ESTABLISH ADDRESSABILITY TO GOTTEN AREA
         LR    R1,R3          RESTORE REGISTER 1
         EJECT ,
         L     R8,0(R1)       PICK UP ADDRESS OF CTXT
         USING CTXT,R8
         L     R7,CTXTTXPJ    LOAD POINTER TO THE MAJOR LINE
         USING CTXTATTR,R7
         TM    CTXTTFB1,CTXTTFWR  WTOR?
         BO    RETURN          YES, SOMETHING IS WRONG
         CLC   =C'IEC502E',CTXTTMSG IS THIS THE CORRECT MAJOR LINE?
         BE    START1          YES, PROCESS IT
         CLC   =C'IEF234E',CTXTTMSG IS THIS THE CORRECT MAJOR LINE?
         BNE   RETURN          NO, DON'T KNOW WHY I WAS CALLED
         SPACE 2
START1   DS    0H                  PROCESS A MESSAGE
         LA    R6,CTXTTMSG+10
         USING MSGAREA,R6
         CLI   UNITADDR,C' '  IS THIS A TWO-CHARACTER DISPOSITION
         BNE   START2           NO, SKIP CHANGES
         LA    R6,1(R6)         YES, MOVE FURTHER OUT
         SPACE 1
START2   DS    0H            SMF RECORDING
         GOSUB GETUCVT             FIND THE USERCVT
         LR    R9,R1               COPY ITS ADDRESS
         USING USERCVT,R9          ESTABLISH ITS ADDRESSABILITY
         LA    R0,SMFSLEN          GET RECORD LENGTH
         LA    R1,SMF234           POINT TO SMF RECORD
         BAL   R4,CLEAR            GO CLEAR SMF RECORD
         MVC   SMFJOBN,BLANKS      BLANK OUT ..
         MVC   SMFVOL,BLANKS       ..CHARACTER FIELDS
         MVI   SMFRECTY,234  SET RECORD TYPE
         MVI   SMFTYPE,C'K'        SHOW IT IS A 'KEEP' RECORD
         MVC   SMFUNIT,UNITADDR MOVE IN UNIT ADDRESS               JWP
         LA    R1,VOLSER           POINT TO VOLUME SERIAL
         DROP  R6                  END MESSAGE FIXED FORMAT
         LR    R6,R1               SAVE ADDRESS OF VOLSER
         BAL   R4,CHARWORD         FIND LENGTH OF VOLSER
         LTR   R1,R1               TEST FOR ANY
         BZ    LJOBNM              NONE, GO COPY JOBNAME
         BCTR  R1,0                DECREMENT FOR EXECUTE
         LA    R2,SMFVOL           POINT TO RECEIVING FIELD
         EX    R1,MOVESMF          COPY VOLUME SERIAL
         LA    R1,1(,R1)           RESTORE REAL LENGTH
         EJECT ,
LJOBNM   DS    0H                  ROUTINE TO COPY JOBNAME
         LA    R6,1(R1,R6)         MOVE MESSAGE POINTER OVER VOLSER
         CLI   2(R6),C','    SEE IF DISP IS WHAT WE WANT
         BE    *+4+4         YES, POINTER STAYS
         LA    R6,1(,R6)     NO, INCREMENT POINTER
         LA    R1,3(,R6)           POINT AT JOBNAME
         LR    R6,R1               UPDATE POINTER AS WELL
         BAL   R4,CHARWORD         FIND LENGTH OF JOBNAME
         LTR   R1,R1               TEST FOR ANY
         BZ    LMSGDONE            NONE, GO ON
         BCTR  R1,0                DECREMENT FOR EXECUTE
         LA    R2,SMFJOBN          POINT TO RECEIVING FIELD
         EX    R1,MOVESMF          COPY VOLUME SERIAL
         EJECT ,
LMSGDONE DS    0H                  MESSAGE DATA PROCESSED
         SPACE 1
         COPY MPFTAPET             COPY TABLE BUILD COMMON CODE
         EJECT ,
         LA    R11,HDRLEN(R11)   SKIP PAST THE TAPE TABLE HEADER
         USING TAPETAB,R11         ESTABLISH TABLE ADDRESSABILITY
CKVOLSER CLC   =X'FFFFFFFF',0(R11) IS THIS THE END OF THE TABLE?
         BE    CKVOLSE3              YES, FORGET IT
         LH    R10,TABCOUNT        PICK UP THE NUMBER OF TABLE ENTRIES
         LA    R3,TABVOLS          FIND THE FIRST TABLE ENTRY
CKVOLSE1 CLC   0(6,R3),SMFVOL
         BE    CKVOLSE2              YES, ERASE IT
         LA    R3,6(R3)              NO, TRY ANOTHER
         BCT   R10,CKVOLSE1        TRY ANOTHER VOL=SER=
         LR    R11,R3              SET UP THE NEW POINTER
         B     CKVOLSER            GO TO IT
CKVOLSE2 MVC   0(6,R3),=XL6'00'  CLOBBER THE OLD VOLUME SERIAL NUMBER
         SPACE 1
CKVOLSE3 CLC   =CL6' ',SMFVOL    IS THE VOLUME SERIAL NUMBER BLANK?
         BE    RETURN              YES, FORGET IT
         L     R11,UCVTTAPE   FIND THE TAPE UNIT ADDRESS TABLE
         LA    R11,HDRLEN(R11)   SKIP PAST THE TAPE TABLE HEADER
CHKUNIT  CLC   =X'FFFFFFFF',0(R11) IS THIS THE END OF THE TABLE?
         BE    RETURN                YES, FORGET IT
*              ASSUMES A THREE DIGIT UNIT ADDRESS
         CLC   TABUNIT,SMFUNIT     IS THIS THE CORRECT UNIT ADDRESS?
         BE    FOUNDIT             YES, PROCESS IT
         LH    R1,TABCOUNT         NO, FIND THE NEXT ONE
         MH    R1,=H'6'          NUMBER OF TABLE ENTRIES * 6
         LA    R11,TABVOLS(R1)   SKIP TO NEXT TABLE ENTRY
         B     CHKUNIT
STACKER  CLC   =XL6'00',0(R11)   IS THERE A TAPE HERE?
         BE    FOUNDIT1            NO, SKIP THE MOVE
         BCTR  R1,0              SUBTRACT ONE
         LR    R3,R1             CALCULATE OFFSET
         MH    R3,=H'6'          CALCULATE END OF TABLE
         L     R10,=F'-6'        LOAD INCREMENT
         LA    R3,0(R3,R11)      POINT TO THE NEXT-TO-LAST ENTRY
STACKER1 MVC   6(6,R3),0(R3)     WIPE OUT THE OLDEST ONE
         BXH   R3,R10,STACKER1   LOOP TO PUSH DOWN THE STACK
         MVC   6(6,R3),0(R3)     WIPE OUT THE OLDEST ONE
         B     FOUNDIT1
         EJECT ,
FOUNDIT  DS    0H                GOT THE UNIT TABLE
         MVC   SMFOLDTM,TABTMSP    SAVE OLD TIMESTAMP
         STCK  TABTMSP             UPDATE THE TIMESTAMP
         MVC   SMFTSMP,TABTMSP     COPY NEW TIMESTAMP
         LH    R1,TABCOUNT       PICK UP THE UNIT ADDRESS COUNTER
         LA    R11,TABVOLS       SKIP THE HEADING INFORMATION
         BCT   R1,STACKER        SUBTRACT 1 AND HANDLE STACKER
FOUNDIT1 MVC   0(6,R11),SMFVOL   MOVE VOLSER TO TABLE
WRITESMF DS    0H            WRITE SMF RECORD
         LA    R0,SMFSLEN          GET RECORD LENGTH
         LA    R1,SMF234           POINT TO SMF RECORD
         GOSUB SMFWRITE            GO TO SMF SUB-ROUTINE
RETURN   LR    R1,R13            LOAD ADDRESS OF GOTTEN AREA
         L     R13,4(R13)        LOAD PREVIOUS SAVE AREA ADDRESS
         LA    R0,AREAREGS       LOAD LENGTH OF AREA
         FREEMAIN RU,LV=(0),A=(1),SP=230
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 2
MOVESMF  MVC   0(*-*,R2),0(R6)     COPY DATA TO SMF RECORD    *EXECUTE*
         SPACE 1
         DROP  R9                  END USERCVT ADDRESSABILITY
         DROP  R11                 END TAPETAB ADDRESSABILITY
         EJECT ,
         COPY  GETUCVTR            INCLUDE USERCVT LOCATE ROUTINE
         EJECT ,
         COPY  CLEARR              INCLUDE STORAGE CLEAR ROUTINE
         EJECT ,
         COPY  CHARWORR            INCLUDE FIELD LENGTH COUNT CODE
         EJECT ,
         COPY  SMFWRITR            INCLUDE SMF WRITER CODE
         TITLE 'DATA AREAS'
BLANKS   DC    CL8' '              SOME HANDY BLANKS
         SPACE 1
         LTORG
         EJECT ,
**********************************************************************
*                                                                    *
*        DSECT USED TO MAP GOTTEN AREA                               *
*                                                                    *
**********************************************************************
         SPACE
WORKREGS DSECT
         SPACE
REGAREA  DS    18F       REGISTER SAVE AREA
         SPACE
         CNOP  2,4
         COPY  SMF234         INCLUDE SMF RECORD FORMAT
         SPACE
         SEGIN GENERATE       GENERATE REGISTER SAVE AREA
         SPACE
AREAREGS EQU   *-WORKREGS
         EJECT ,
         COPY CSATABLE        COPY TABLE VOLUME TABLE FORMAT
         EJECT ,
         COPY USERCVT         COPY USERCVT FORMAT
         SPACE 1
MSGAREA  DSECT
UNITADDR DS    CL3            UNIT ADDRESS IN THE MESSAGE
         DS    C              ,
VOLSER   DS    CL6            VOLUME SERIAL NUMBER IN THE MESSAGE
         DS    C              ,
DISPSL   DS    CL2            SL
         DS    C              ,
JOBNAME  DS    CL8            JOBNAME IN THE MESSAGE
         EJECT ,
         IEZVX100
         PRINT NOGEN
         REQU  ,              STANDARD REGISTER EQUATES
         CVT   DSECT=YES,LIST=YES
         END   MPFTAPEK
