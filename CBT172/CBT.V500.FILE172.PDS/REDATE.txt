         TITLE 'REFORMAT THE DATE FIELD'
***********************************************************************
*        PDSDOC *REFORMAT SMF DATE TO SOMETHING USEFUL................*
* ON ENTRY R1 --> PACKED DATE FIELD 00YYDDDF                          *
*                                                                     *
* 05NOV98 DHC CREATED OUT OF CODE IN 'SEAFOOD'                        *
***********************************************************************
         SPACE 1
REDATE   DS    0H                  PROCESS PACKED DECIMAL DATE
         SUBIN SAVE=(R4,R7)        SUBROUTINE ENTRY
         LR    R7,R1               SAVE POINTER TO DATE FIELD
*
*        SET UP PARAMETERS AND CALL 'GETDATE' TO CHECK FOR HOLIDAY
*
         MVC   PJULIAN,0(R7)       COPY DATE
         LA    R1,ADATES           POINT TO 'GETDATE' PARAMETERS
         LA    R15,CDATER          POINT TO CPB  'GETDATE'
         GOSUB CALLIT              CALL 'GETDATE'
         SPACE 1
         MVO   DWORK,0(2,R7)       COPY YEARS SINCE 1900
         OI    DWORK+7,X'0F'       ENSURE PROPERLY PACKED
         CVB   R1,DWORK            AND CONVERT TO BINARY
         STC   R1,0(,R7)           SAVE IN ANSWER
         LA    R4,TABMONTH         POINT TO MONTH TABLE (REVERSE)
         LA    R1,12               START WITH MONTH 12
LPSMONTH DS    0H                  START OF MONTH SEARCH LOOP
         CLC   CMMM,0(R4)          TEST MONTH TABLE
         BE    LPEMONTH            MATCH SO EXIT
         LA    R4,L'CMMM(,R4)      ELSE UPDATE TABLE POINTER
         BCT   R1,LPSMONTH         THEN GO ROUND AGAIN. 0 = ERROR
         SPACE 1
LPEMONTH DS    0H                  END OF MONTH SEARCH LOOP
         STC   R1,1(,R7)           SAVE MONTH IN ANSWER
         PACK  DWORK,CDD           PACK DAY NUMBER FROM 'GETDATE'
         CVB   R1,DWORK            AND CONVERT TO BINARY
         STC   R1,2(,R7)           SAVE IN ANSWER
         LA    R4,TABDAY           POINT TO DAY TABLE (REVERSE)
         LA    R1,7                START WITH DAY 7
LPSDAY   DS    0H                  START OF DAY SEARCH LOOP
         CLC   0(3,R4),DAY         TEST DAY TABLE
         BE    LPEDAY              MATCH SO EXIT
         LA    R4,3(,R4)           ELSE UPDATE TABLE POINTER
         BCT   R1,LPSDAY           THEN GO ROUND AGAIN. 0 = ERROR
         SPACE 1
LPEDAY   DS    0H                  END OF DAY SEARCH LOOP
         STC   R1,3(,R7)           SAVE DAY IN ANSWER
         CLI   HOLIDAY,C'H'        IS IT A HOLIDAY?
         BNE   REDATED             NO, WE HAVE FINISHED
         OI    3(R7),X'F0'         YES, SET FLAG BIT
         SPACE 1
REDATED  DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R4,R7)    SUBROUTINE EXIT
         SPACE 1
ADATES   DC    AL4(DATE)           ADDRESS OF DATE FIELD
         DC    XL1'80'             DELIMITER
         DC    AL3(DAY)            ADDRESS FOR DAY OF WEEK
CDATER   CPB   GETDATE             NAME OF DATE ROUTINE
         SPACE 1
TABMONTH EQU   *                   START OF MONTH TABLE
         DC    CL3'DEC'            NAME OF MONTH 12
         DC    CL3'NOV'            NAME OF MONTH 11
         DC    CL3'OCT'            NAME OF MONTH 10
         DC    CL3'SEP'            NAME OF MONTH 9
         DC    CL3'AUG'            NAME OF MONTH 8
         DC    CL3'JUL'            NAME OF MONTH 7
         DC    CL3'JUN'            NAME OF MONTH 6
         DC    CL3'MAY'            NAME OF MONTH 5
         DC    CL3'APR'            NAME OF MONTH 4
         DC    CL3'MAR'            NAME OF MONTH 3
         DC    CL3'FEB'            NAME OF MONTH 2
         DC    CL3'JAN'            NAME OF MONTH 1
         SPACE 1
TABDAY   EQU   *                   START OF DAY TABLE
         DC    CL3'SUN'            NAME OF DAY 7
         DC    CL3'SAT'            NAME OF DAY 6
         DC    CL3'FRI'            NAME OF DAY 5
         DC    CL3'THU'            NAME OF DAY 4
         DC    CL3'WED'            NAME OF DAY 3
         DC    CL3'TUE'            NAME OF DAY 2
         DC    CL3'MON'            NAME OF DAY 1
         EJECT ,
DATE     DS    0CL9                AREA FOR DATE
PJULIAN  DC    PL4'0099365'        JULIAN DATE
         DC    CL5' '              REST OF DATE FIELD
         ORG   DATE                RE-DEFINE FOR ANSWER
CDD      DS    CL2'DD'             DAY
         DS    CL1' '              PADDING
CMMM     DS    CL3'MMM'            MONTH NAME
         DS    CL1' '              PADDING
CYY      DS    CL2'YY'             YEAR
         ORG   ,                   END OF RE-DEFINE
DAY      DS    0CL10               DAY-OF-WEEK RETURNED HERE
         DC    CL9' '              DAY OF WEEK
HOLIDAY  DC    CL1' '              HOLIDAY FLAG ('H')
