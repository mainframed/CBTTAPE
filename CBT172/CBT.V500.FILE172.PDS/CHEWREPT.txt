CHEWREPT TITLE 'CHEW REPORTING ROUTINE'
***********************************************************************
*        PDSDOC *WRITE REPORT TO SYSOUT...............................* CHEWREPT
*                                                                     *
*        PROGRAM ID. :             CHEWREPT                           *
*                                                                     *
*        DATE WRITTEN :            JANUARY   1995                     *
*                                                                     *
*        AUTHOR :                  D.H.CARTWRIGHT.                    *
*                                                                     *
*        LOCATION :                SUTTON COLDFIELD                   *
*                                                                     *
*        HISTORY  :                                                   *
*                                                                     *
*   JAN95 STARTED WRITING BASED ON THE OLD 'MONEXWTR' PROGRAM         *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*       ---------                                                     *
*                                                                     *
* THIS IS A SUB-PROGRAM WITHIN THE 'CHEW' EXTERNAL WRITER SUITE.      *
* IT WRITES AN ACTIVITY REPORT TO A DYNAMICALLY ALLOCATED SYSOUT FILE.*
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*       ------------                                                  *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT ,
***********************************************************************
*                                                                     *
*       INPUT                                                         *
*       -----                                                         *
*                                                                     *
* ON ENTRY R1 ---> CHEWBACA COMMON AREA WHICH IS ADDRESSED BY R11.    *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*       OUTPUT                                                        *
*       ------                                                        *
*                                                                     *
* 1) A DYNAMICALLY ALLOCATED SYSOUT DATASET REPORTING ACTIVITY TO DATE*
*                                                                     *
* VARIOUS FIELDS WITHIN 'CHEWBACA' MAY BE UPDATED BY THIS ROUTINE.    *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT ON,GEN              SHOW MACRO EXPANSIONS
*        PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
         SPACE 1
CHEWREPT CSECT ,
         SPACE 1
         PGMIN EQUATES=NO,USING=(R13) STANDARD HOUSEKEEPING
         SPACE 1
         LM    R11,R12,0(R1)       (RE)COPY COMMON AREA POINTERS
         USING CHEWBACA,R11,R12    ESTABLISH ADDRESSABILITY
         XC    RETREPT,RETREPT     SET DEFAULT RETURN CODE
         EJECT ,
***********************************************************************
*        ROUTINE TO PRODUCE ACTIVITY REPORT                           *
***********************************************************************
         SPACE 1
REPORT   DS    0H                  PRINT LIST OF FILES PROCESSED
         SR    R1,R1               ASK FOR CURRENT TIME
         BAL   R4,TODDATE          GO FORMAT TOD-CLOCK
         DATE  HEADDATE,FORMAT='DD MMM YY',FROM=TODPJUL FORMAT DATE ..
         MVC   HEADTIME,TODTIME     .. AND TIME IN HEADING
         TM    SYSUT3SW,DDINT+DDPRES HAVE WE GOT A SUMMARY FILE?
         BNO   REPORTEX            NO,GIVE UP
         LA    R2,SYSUT3SW         POINT AT SUMMARY FILE
         USING MDCB,R2             ESTABLISH DD LIST ADDRESSABILITY
         MVC   MDCBCLOS,=CL8'REREAD' SET CLOSE OPTION
         BAL   R4,CLOSE            CLOSE IT
         MVC   MDCBOPEN,=CL8'INPUT' CHANGE OPEN OPTION
         LA    R1,EODUT3           POINT TO EOD ROTUINE
         BAL   R4,SETEOD           PUT INTO DCB
         BAL   R4,OPEN             GO OPEN IT
         BAL   R4,GETSOUT          GET A REPORT DATASET
         AP    REPTCOUN,ONE        ADD ONE TO REPORT COUNTER
         SP    JOBCOUNT,JOBCOUNT   RESET
         SP    JESCOUNT,JESCOUNT   ..... STATISTIC
         SP    CNTSUT2,CNTSUT2     ..... ......... COUNTERS
         MVC   DETJOBID,=CL8' '    CLEAR JOBNAME
         SPACE 1
REPORTGO DS    0H                  TOP OF REPORTING LOOP
         BAL   R4,SYSUT3RD         GO GET A SUMMARY RECORD
         BAL   R4,PRNTLINE         GO REPORT ON IT
         B     REPORTGO            GO ROUND FOR NEXT RECORD
         SPACE 1
EODUT3   DS    0H                  END OF SUMMARY RECORDS
         BAL   R4,SUMMLINE         GO PRINT LAST LINE
         BAL   R4,CLOSOUT          CLOSE AND DEALLOC REPORT
         AP    LINECOUN,MAXLINE    ENSURE WE HAVE HEADERS ON NEXT ONE
         SP    PAGECOUN,PAGECOUN   AND RESTART PAGE NUMBERS
         LA    R2,SYSUT3SW         POINT AT INPUT SUMMARY FILE
         MVC   MDCBCLOS,=CL8'LEAVE' SET CLOSE OPTION
         MVC   MDCBOPEN,=CL8'EXTEND' CHANGE OPEN OPTION
         BAL   R4,CLOSE            GO CLOSE INPUT FILE
         SPACE 1
REPORTEX DS    0H                  END OF REPORT PROCESSING
         EJECT ,
***********************************************************************
*        END OF PROCESSING - RETURN TO CALLER                         *
***********************************************************************
         SPACE 1
OPTNEXIT DS    0H                  RETURN TO CALLER
         LH    R15,RETREPT         PICK UP RETURN CODE
         B     ENDOFPGM            GO TO COMMON EXIT
         TITLE 'SUB-ROUTINES'
***********************************************************************
*        CREATE DETAIL PRINT LINE                                     *
***********************************************************************
         SPACE 1
PRNTLINE DS    0H                  PRINT A REPORT LINE
         SUBIN SAVE=(R4,R10)       SUBROUTINE ENTRY
         AP    JESCOUNT,ONE        INCREMENT DATASET COUNT
         AP    CNTSUT2,ISUMCNT     INCREMENT LINE COUNT
         LA    R10,ISUMREC         POINT TO SUMMARY RECORD
         USING SSOB,R10            ESTABLISH ADDRESSABILITY
         MVI   DETASA,C' '         ASSUME WE WILL ONLY SPACE ONE
         CLC   DETJOBID,SSSOJOBI   IS THIS THE SAME JOB?
         BE    PRNDETL             YES,LEAVE DEFAULT ONE SPACE
         AP    JOBCOUNT,ONE        NO, INCREMENT JOB COUNT
         MVI   DETASA,C'0'         LEAVE BLANK LINE BETWEEN JOBS
         SPACE 1
PRNDETL  DS    0H                  PRINT A DETAIL LINE
         MVC   DETJOBID,SSSOJOBI   COPY JOB ID.
         MVC   DETJNAM,SSSOJOBN    ....... NAME
         MVC   DETCLASS,SSSOCLSL   CLASS WE WERE SELECTING
         MVC   DETCC,ISUMCC        SHOW CONTROL CHARACTERS
         MVC   DETFORM,SSSOFORM    SHOW FORM NUMBER
         LH    R4,SSSOCOPY         GET NUMBER OF COPIES
         CVD   R4,WORKKEY          MAKE IT DECIMAL
         MVC   DETCOPY,EDITPL2     MOVE IN EDIT MASK
         ED    DETCOPY,WORKKEY+6   SHOW NO. COPIES
         MVC   DETDSN,SSSODSN+24   SHOW SYSOUT DATASET NAME
         MVC   DETDEST,SSSODEST    AND DESTINATION
         MVC   DETNOTE,ISUMNOTE    GET OPERATOR COMMENTS
         MVC   DETCOUNT,EDITPL4    MOVE IN EDIT MASK
         ED    DETCOUNT,ISUMCNT    SHOW RECORD COUNT
         MVC   DETHH,ISUMTIME      PUT IN HOURS
         MVC   DETMM,ISUMTIME+2    AND MINUTES
         LA    R0,DETAIL           POINT TO PRINT LINE
         BAL   R4,PRINT            GO OUTPUT IT
         SPACE 1
         SUBOUT RESTORE=(R4,R10)   RETURN TO CALLER
         DROP  R10                 END SSOB ADDREESING
         EJECT ,
***********************************************************************
*        CREATE SUMMARY PRINT LINE                                    *
***********************************************************************
         SPACE 1
SUMMLINE DS    0H                  PRINT LAST REPORT LINE
         SUBIN SAVE=(R4,R6)        SUBROUTINE ENTRY
         MVC   PSUMJOB,EDITPL4     MOVE IN EDIT MASK
         ED    PSUMJOB,JOBCOUNT    SHOW TOTAL JOB COUNT
         MVC   PSUMFILE,EDITPL4    MOVE IN EDIT MASK
         ED    PSUMFILE,JESCOUNT   SHOW TOTAL DATASET COUNT
         MVC   PSUMCNT,EDITPL8     MOVE IN EDIT MASK
         ED    PSUMCNT,CNTSUT2     SHOW TOTAL RECORD COUNT
         LA    R6,ISUMJFCB         POINT AT SAVED JFCB
         USING INFMJFCB,R6         ESTABLISH ADDRESSABILITY
         MVC   PSUMDSN,JFCBDSNM    SHOW DATASET NAME
         LA    R0,PSUMLINE         POINT TO PRINT LINE
         BAL   R4,PRINT            GO OUTPUT IT
         SPACE 1
         SUBOUT RESTORE=(R4,R6)    RETURN TO CALLER
         SPACE 1
         DROP  R6                  END JFCB MAPPING
         EJECT ,
         COPY  PRINT               INCLUDE STANDARD PAGE HANDLER
         EJECT ,
***********************************************************************
*        READ SUBROUTINE FOR SYSUT3                                   *
***********************************************************************
         SPACE 1
SYSUT3RD DS    0H                  READ A SUMMARY RECORD
         SUBIN ,                   SUBROUTINE ENTRY
         TM    SYSUT3SW,DDINT+DDPRES+DDOPEN TEST IF NEED TO READ
         BNO   SYSUT3RX            BRANCH IF NO
         GET   SYSUT3,ISUMREC      READ IN A SUMMARY RECORD
         AP    CNTSUTR3,ONE        INCREMENT RECORD COUNTER
         SPACE 1
SYSUT3RX DS    0H                  RETURN TO CALLER
         SUBOUT ,                  SUBROUTINE EXIT
         EJECT ,
***********************************************************************
*        ROUTINE TO CREATE A SYSOUT DATASET                           *
***********************************************************************
         SPACE 1
GETSOUT  DS    0H                  ALLOCATE, OPEN OUTPUT
         SUBIN SAVE=(R1,R4)        SUBROUTINE ENTRY
         LA    R1,S99PARM2         NEW JOB. PREPARE TO ALLOC OUTPUT DS
         DYNALLOC ,                ALLOCATE A SYSOUT DATA SET
         SPACE 1
         STH   R15,RETCODE         SAVE SVC 99 RETURN CODE
         LTR   R15,R15             TEST SVC 99 RETURN CODE
         BNZ   ENDOFPGM            NO GOOD. TERMINATE
         SPACE 1
         LA    R2,PRINTSW          POINT AT PRINT TABLE ENTRY
         OI    PRINTSW,DDINT       SAY WE ARE INTERESTED
         BAL   R4,TESTIOT          IS IT THERE?
         BAL   R4,OPEN             IF IT IS,OPEN IT
         SPACE 1
         SUBOUT RESTORE=(R1,R4)    RETURN TO CALLER
         SPACE 3
***********************************************************************
*        ROUTINE TO CLOSE SYSOUT AND THEREFORE DE-ALLOCATE IT         *
***********************************************************************
         SPACE 1
CLOSOUT  DS    0H                  CLOSE WITH FREE=CLOSE
         SUBIN SAVE=(R1,R4)        SUBROUTINE ENTRY
         LA    R2,PRINTSW          POINT AT PRINT TABLE ENTRY
         BAL   R4,CLOSE            CLOSE SYSOUT DATASET
         NI    PRINTSW,FF-DDINT-DDPRES NO LONGER INTERESTED
         SPACE 1
         SUBOUT RESTORE=(R1,R4)    RETURN TO CALLER
         TITLE 'DATA AREAS'
         PUSH PRINT
         PRINT ON,NOGEN            DO NOT EXPAND STANDARD STUFF
***********************************************************************
*        PRIVATE CONSTANTS AND WORK AREAS                             *
***********************************************************************
         SPACE 2
         DS    0D                  GET ADDRESSES ALIGNED
         DC    CL8'EYECATCH'       EYECATCHER
         RIN   GENERATE            SAVE AREA
         SPACE 2
***********************************************************************
*        INPUT SUMMARY RECORD AREA                                    *
***********************************************************************
         SPACE 1
         DS    0F                  SAME ALIGNMENT AS OUTPUT AREA
ISUMREC  EQU   *                   AREA FOR READING BACK SUMMARY RECORD
ISUMSSOB DC    CL(SSOBLEN1)' '     SSOB AREA
         ORG   ,                   RESET ASSEMBLER
         SPACE 1
ISUMCC   DC    CL1' '              TYPE OF FORMS CONTROL (A OR M)
ISUMDATE DC    CL6'YYMMDD'         DATE PROCESSED
ISUMTIME DC    CL6'HHMMSS'         TIME DATASET DE-ALLOCATED
ISUMCNT  DC    PL4'0'              RECORD COUNT
ISUMNOTE DC    CL80' '             MESSAGE FROM OPERATOR
ISUMJFCB DS    CL176' '            JFCB AREA
ISUMRECL EQU   *-ISUMREC           LENGTH OF SUMMARY RECORD
         EJECT ,
***********************************************************************
*        DYNAMIC ALLOCATION TEXT BLOCKS FOR REPORT FILE               *
***********************************************************************
         SPACE 1
         DS    0F
S99PARM2 DC    X'80',AL3(S99AREA2) ADDR OF SVC 99 RB FOR SYSOUT ALLOC
S99AREA2 DC    AL1(S99RBEND-S99RB) LENGTH OF REQUEST BLOCK
         DC    X'01'               VERB CODE - ALLOCATE
         DC    XL6'00'
         DC    A(OUTTUPTR)         POINTER TO LIST OF TEXT UNIT PTRS
         DC    XL8'00'
         SPACE 1
OUTTUPTR DC    A(PRDDNAM)          ADDR OF DDNAME TEXT UNIT
         DC    A(PRSYSOUT)         ADDR OF SYSOUT DS TEXT UNIT
NORMTRTE DC    X'80',AL3(PRFREE)   ADDR OF FREE=CLOSE TEXT UNIT
         DC    X'80',AL3(PRROUTE)  ADDR OF REMOTE ROUTING TEXT UNIT
         SPACE 1
PRDDNAM  DC    X'0001'             ALLOCATION BY DDNAME
         DC    X'0001'             ONE PARAMETER
         DC    X'0008'             LENGTH OF DDNAME
         DC    CL8'SYSOUT'         DDNAME FOR ALLOCATION
         DS    0H
PRSYSOUT DC    X'0018'             SYSOUT DATA SET ALLOCATION
         DC    X'0001'             ONE PARAMETER
         DC    X'0001'             LENGTH OF SYSOUT CLASS FIELD
         DC    CL1'D'              SYSOUT CLASS
         DS    0H
PRFREE   DC    X'001C'             FREE=CLOSE
         DC    X'0000'             NO PARAMETERS
         DS    0H
PRROUTE  DC    X'0058'             DEST= - IF SUPPLIED IN PARM FIELD
         DC    X'0001'             ONE PARAMETER
PRDSTLNG DC    X'0008'             LENGTH OF DESTINATION FIELD
PRDEST   DC    CL8' '              RMT DEST - SUPPLIED BY PARM FIELD
         EJECT ,
***********************************************************************
*        REPORT LINE SKELETONS                                        *
***********************************************************************
         SPACE 1
TITLE1   DS    0CL133              START OF PAGE HEADER
         DC    CL1'1'              ASA SKIP TO PAGE             1
         DC    CL40' '             PADDING                     41
         DC    CL24'CHEW EXTERNAL WRITER'                      65
         DC    CL4' '              PADDING                     69
         DC    CL10'REPORT NO.'                                79
HEADNUM  DC    CL4'0000'           REPORT NUMBER               83
         DC    CL8' '              PADDING                     91
HEADDATE DC    CL9' '              REPORT DATE                100
         DC    CL8' '              PADDING                    108
HEADTIME DS    0CL5                REPORT TIME                110
HEADHH   DC    CL2' '              HOUR OF REPORT             110
         DC    CL1'.'              SEPARATOR                  111
HEADMM   DC    CL2' '              MINUTE OF REPORT           113
         DC    CL10' '             PADDING                    123
         DC    CL4'PAGE'                                      127
HEADPAGE DC    CL4'0000'           PAGE NUMBER                131
         DC    CL2' '              PADDING                    133
         SPACE 2
SUBTITLE DS    0CL133              COLUMN HEADINGS
         DC    CL1'0'              ASA SKIP 2                   1
         DC    CL8'JOB ID.'        JOB IDENT                    9
         DC    CL2' '              PADDING                     11
         DC    CL8'JOB NAME'       JOBNAME                     19
         DC    CL2' '              PADDING                     21
         DC    CL5'CLASS'          SYSOUT CLASS                26
         DC    CL2' '              PADDING                     28
         DC    CL4'TIME'           HOURS AND MINUTES           32
         DC    CL2' '              PADDING                     34
         DC    CL8'DESTN.'         DESTINATION                 42
         DC    CL1' '              PADDING                     43
         DC    CL2'CC'             CONTROL CHARACTER TYPE      45
         DC    CL2' '              PADDING                     47
         DC    CL4'FORM'           FORM NUMBER                 51
         DC    CL2' '              PADDING                     53
         DC    CL3'CPY'            NUMBER OF COPIES            56
         DC    CL2' '              PADDING                     58
         DC    CL20'SYSOUT DATASET NAME'  DSNAME (TRUNCATED)   78
         DC    CL2' '              PADDING                     80
         DC    CL8' RECORDS'       RECORD COUNT                88
         DC    CL2' '              PADDING                     90
         DS    0CL43               ROOM FOR NOTES              90 133
         DC    CL1'*'              PRETTY                      91
         DC    17CL1'.'            PADDING                    108
         DC    CL7' NOTES '        REMARKS COLUMN             115
         DC    17CL1'.'            PADDING                    132
         DC    CL1'*'              PRETTY                     133
         EJECT ,
DETAIL   DS    0CL133              COLUMN ENTRIES
DETASA   DC    CL1' '              ASA SKIP 1                   1
DETJOBID DC    CL8'JOB ID.'        JOB IDENT                    9
         DC    CL2' '              PADDING                     11
DETJNAM  DC    CL8'JOB NAME'       JOBNAME                     19
         DC    CL4' '              PADDING                     23
DETCLASS DC    CL1' '              SYSOUT CLASS                24
         DC    CL3' '              PADDING                     27
DETHH    DC    CL2'HH'             HOURS                       29
         DC    CL1'.'              DELIMITER                   30
DETMM    DC    CL2'MM'             MINUTES                     32
         DC    CL2' '              PADDING                     34
DETDEST  DC    CL8'DESTN.'         DESTINATION                 42
         DC    CL2' '              PADDING                     44
DETCC    DC    CL1' '              CONTROL CHARACTER TYPE      45
         DC    CL2' '              PADDING                     47
DETFORM  DC    CL4'FORM'           FORM NUMBER                 51
DETCOPY  DC    CL4' CPY'           NUMBER OF COPIES            55
         DC    CL3' '              PADDING                     58
DETDSN   DC    CL20' '             TRUNCATED SYSOUT DSNAME     78
         DC    CL2' '              PADDING                     80
DETCOUNT DC    CL8'RECORDS'        RECORD COUNT                88
         DC    CL2' '              PADDING                     90
DETNOTE  DC    CL43'NOTES'         REMARKS COLUMNS            133
         SPACE 2
PSUMLINE DS    0CL133              PRINT SUMMARY LINE (LAST LINE)
         DC    CL1'0'              ASA SKIP 2                   1
PSUMJOB  DC    CL8'00000000'       NO. OF JOBS                  9
         DC    CL2' '              PADDING                     11
         DC    CL4'JOBS'           TEXT                        15
         DC    CL2' '              PADDING                     19
PSUMFILE DC    CL8'00000000'       NO. OF DATASETS             27
         DC    CL2' '              PADDING                     29
         DC    CL5'FILES'          TEXT                        34
         DC    CL2' '              PADDING                     36
         DC    CL14'TOTAL RECORDS=' TEXT                       50
PSUMCNT  DC    CL16' '             COUNT                       66
         DC    CL2' '              PADDING                     68
         DC    CL18'ARCHIVE DATASET =' TEXT                    86
PSUMDSN  DC    CL44' '             DATASET NAME               130
         DC    CL3' '              PADDING                    133
         TITLE 'LITERAL POOL'
***********************************************************************
*        LITERAL POOL                                                 *
***********************************************************************
         SPACE 1
         LTORG ,                   LITERAL POOL GOES HERE
         SPACE 3
         POP   PRINT               RESTORE PRINT STATUS
         SPACE 3
REPTBACA CHEWBACA TYPE=DSECT       MAP COMMON AREA
         TITLE 'DSECT MAPS'
         SPACE 3
         END
