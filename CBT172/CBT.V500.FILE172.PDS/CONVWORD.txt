***********************************************************************
*        PDSDOC *CONVERT A FULLWORD TO ZONED DECIMAL DATA.............* CONVWORD
*                                                                     *
* ON ENTRY R1 =  A FULLWORD VALUE, ASSUMED TO BE POSITIVE             *
*          R4 -> RETURN ADDRESS                                       *
* ON EXIT  R1 -> FIRST SIGNIFICANT DIGIT                              *
*          R4 =  NUMBER OF SIGNIFICANT DIGITS - 1 (EXECUTABLE LENGTH) *
*          R14-> RETURN ADDRESS                                       *
*                                                                     *
* THE ROUTINE ASSUMES THE EXISTENCE OF A DOUBLEWORD WORKAREA 'DWORK'  *
* AND A 15 CHARACTER ANSWER FIELD CALLED 'CONVWORK'. IF THE VALUE IS  *
* ZERO ONE SIGNIFICANT ZERO IS RETURNED. ALL VALUES ARE ASSUMED TO BE *
* POSITIVE - IT IS UP TO THE USER TO TEST FOR NEGATIVE AND DO A 'LOAD *
* NEGATIVE' BEFORE CALLING THIS CODE.                                 *
*                                                                     *
***********************************************************************
         SPACE 1
CONVWORD DS    0H                  CONVERT A BINARY FULLWORD
         SUBIN ,                   SUBROUTINE ENTRY
         LR    R14,R4              COPY RETURN ADDRESS
         CVD   R1,DWORK            CONVERT R1 TO DECIMAL
         UNPK  CONVWORK,DWORK      CONVERT TO ZONED
         OI    CONVWORK+14,X'F0'   ASSUME POSITIVE
         LA    R1,CONVWORK         POINT TO ANSWER
         LA    R4,14               SET MAXIMUM LENGTH
         SPACE 1
CONVLOOP DS    0H                  FIND THE FIRST SIGNIFICANT DIGIT
         CLI   0(R1),X'F0'         A LEADING ZERO?
         BNE   CONVWORX            NO, RETURN THIS TO USER
         LA    R1,1(,R1)           YES, INCREMENT POINTER
         BCT   R4,CONVLOOP         GO ROUND UNTIL LAST DIGIT
         SPACE 1
CONVWORX DS    0H                  RETURN TO CALLER
         SUBOUT RETURN=(R14)       SUBROUTINE EXIT
