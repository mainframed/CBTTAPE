//*
//*      PDSDOC *ASSEMBLY OF OPC INCIDENT RECORD CREATE EXIT..........* DRKUX006
//*
//STEP01  EXEC ASMHCL,
//             PARM.C='NODECK,OBJECT,TEST,RENT',
//*             COND.L=(99,NE),
//             PARM.L='LIST,MAP,REFR,REUS,XREF,LET,RENT'    ,TEST
//C.SYSLIB   DD DISP=SHR,DSN=CART1.MACLIB.DATA,DCB=BLKSIZE=32000
//           DD  DISP=SHR,DSN=SYS1.MACLIB,UNIT=DISK,VOL=SER=SYSS01
//           DD  DISP=SHR,DSN=SYS1.AMODGEN,UNIT=DISK,VOL=SER=SYSD01
//C.SYSIN    DD  *
DRKUX006 TITLE 'OPC INCIDENT RECORD CREATE EXIT'
***********************************************************************
*                                                                     *
* THIS PROGRAM HAS BEEN WRITTEN TO REPLACE THE IBM SUPPLIED EXIT.     *
* IT CREATES THE SAME INCIDENT LOG RECORDS, THEN GOES ON TO START     *
* (VIA SVC 34) A TASK CALLED 'BLEEP', PASSING THE FAILING JOBNAME.    *
* THE STC 'BLEEP' CAN DECIDE WHAT TO DO ABOUT THE FAILURE, SO OPC     *
* DOES NOT HAVE TO LOOK UP OR MAINTAIN TABLES OF WHAT JOBS REQUIRE    *
* WHAT ACTION. 'BLEEP' IS NOT SUPPLIED - WRITE YOUR OWN.              *
*                                                                     *
*                                                                     *
* THE ONLY OUTPUT FROM THE PROGRAM IS THE RETURN CODE IN REGISTER 15. *
*                                                                     *
*     RC = 0       -   THE INCIDENT RECORDS ARE TO BE WRIITEN TO THE  *
*                      INCIDENT LOG DATA SET.                         *
*                                                                     *
*                                                                     *
* THE PROGRAM IS RE-ENTRANT.                                          *
*                                                                     *
* D.H.CARTWRIGHT SPRECHER ENERGIE AG OBERENTFELDEN SWITZERLAND        *
*                                                                     *
* 24OCT88 CART1 WROTE PROGRAM TO REPLACE PATCHED UP OPC SAMPLE.       *
* 04JAN90 CART1 CORRECTED BUG IN JOBNAME LENGTH ROUTINE.              *
*                                                                     *
***********************************************************************
         SPACE 2
DRKUX006 ZCSECT GETMAIN=1,REG=R,WKLEN=LENWORK
         USING WORK,R13            ESTABLISH WORK AREA ADDRESSABILITY
         SPACE 1
         BAL   R14,RECORDIT        GO CREATE INCIDENT RECORDS
         BAL   R14,STARTIT         GO START BLEEP
         SPACE 1
ENDPROG  DS    0H                  END OF PROGRAM
         LA    R15,0               ALWAYS LOG THE RECORDS
         ZRETURN RC=(15)           RETURN TO CALLER
         TITLE 'CREATE INCIDENT RECORDS'
***********************************************************************
* THIS SUBROUTINE CREATES INCIDENT RECORDS LIKE THE IBM ONES.         *
***********************************************************************
         SPACE 1
RECORDIT DS    0H                  CREATE INCIDENT RECORDS
         MVC   72(60,R13),0(R1)    SAVE PARAMETER QUEUE
         L     R3,128(,R13)
         SLR   R4,R4
         ST    R4,0(,R3)
         L     R2,AREA             POINT TO RECORD AREA
         MVI   9(R2),X'5C'
         MVI   20(R2),X'5C'
         MVI   31(R2),X'5C'
         MVI   39(R2),X'5C'
         MVI   47(R2),X'5C'
         MVI   55(R2),X'5C'
         L     R3,DATE
         MVC   0(8,R2),0(R3)
         L     R3,JOBNAME
         MVC   11(8,R2),0(R3)
         L     R3,JOBNUM
         MVC   22(8,R2),0(R3)
         L     R3,JOBSTART
         MVC   33(5,R2),0(R3)
         L     R3,JOBEND
         MVC   41(5,R2),0(R3)
         L     R3,SYSTEM
         MVC   49(5,R2),0(R3)
         L     R3,EID
         MVC   57(4,R2),0(R3)
         L     R3,TID
         MVI   70(R2),C' '
         MVI   71(R2),C' '
         MVC   62(8,R2),0(R3)
         L     R3,RFLAG
         CLI   0(R3),RFLAGT
         BE    A000154
         L     R4,NRECS
         LA    R5,1
         ST    R5,0(,R4)
         B     A000172
A000154  L     R3,NRECS
         LA    R4,2
         ST    R4,0(,R3)
         L     R3,RSIZE
         AL    R2,0(,R3)
         L     R3,RECORD
         MVC   0(72,R2),0(R3)
A000172  DS    0H
         SPACE 1
         BR    R14                 RETURN TO CALLER
         TITLE 'START BLEEP TASK'
***********************************************************************
* THIS SUBROUTINE SETS UP THE MGCR PARAMETER BLOCK AND ISSUES THE SVC *
***********************************************************************
         SPACE 2
STARTIT  DS    0H                  START BLEEP STC
         LR    R4,R14              SAVE RETURN ADDRESS
         L     R3,JOBNAME          GET JOBNAME POINTER
         LA    R9,7                SET DEFAULT LENGTH - 1
JOBNLEN  DS    0H                  FIND JOBNAME LENGTH
         LA    R1,0(R3,R9)         POINT TO END OF JOBNAME
         CLI   0(R1),C' '          IS CHARACTER A BLANK?
         BNE   SETMGCR             NO,FOUND END OF JOBNAME
         BCT   R9,JOBNLEN          YES, DECREMENT AND TRY AGAIN
         SPACE 1
SETMGCR  DS    0H                  CREATE MGCR PARAMETER LIST
         XC    MGCRPL(MGCRLTH),MGCRPL  CLEAR THE PARAMETER LIST
         MVC   MGCRTEXT(L'TXTINSRT),TXTINSRT MOVE IN THE START BUFFER
         MVC   MGCRTEXT+L'TXTINSRT(8),0(R3) PUT JOBNAME INTO COMMAND
         LA    R1,(MGCRTEXT-MGCRPL)+L'TXTINSRT GET MGCRPL LENGTH
         LA    R1,1(R1,R9)         ADJUST FOR LENGTH OF JOBNAME
         STC   R1,MGCRLGTH         SAVE LENGTH IN THE MGCRPL
         MODESET MODE=SUP,KEY=ZERO GET INTO SUPERVISOR STATE
         SR    R0,R0               CLEAR REGISTER ZERO
         MGCR  MGCRPL              ISSUE THE COMMAND
DONEMGCR DS    0H                  START COMMAND HAS BEEN ISSUED
         MODESET MODE=PROB,KEY=NZERO GET BACK TO PROBLEM STATE
         SPACE 1
         LR    R14,R4              RESTORE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         SPACE 2
         SPACE 1
         DROP  R13                 END ADDRESSABILITY
         TITLE 'DATA AREA MAPPING'
***********************************************************************
*                                                                     *
* CHARACTER CONSTANTS                                                 *
*                                                                     *
***********************************************************************
TXTINSRT DC    C'START BLEEP,JOBNAME=' COMMAND TEXT
         EJECT ,
WORK     DSECT ,                   WORK AREA ACQUIRED
         DS    18F                 SAVEAREA
         SPACE 1
***********************************************************************
* PARAMETERS PASSED BY OPC                                            *
***********************************************************************
PARAMS   EQU   *                  START OF PARAMETER SAVE AREA
AREA     DS    A                   --> DS CL1024 RECORD BUILD AREA
NRECS    DS    A                   --> DS F      NO. RECORDS CREATED
RSIZE    DS    A                   --> DS F      INCIDENT RECORD SIZE
DATE     DS    A                   --> DS CL8    CURRENT DATE YY/MM/DD
JOBNAME  DS    A                   --> DS CL8    JOB NAME
JOBNUM   DS    A                   --> DS CL8    JOB NUMBER
JOBSTART DS    A                   --> DS CL8    START TIME HH.MM.SS
JOBEND   DS    A                   --> DS CL8    END   TIME HH.MM.SS
SYSTEM   DS    A                   --> DS CL5    NAME OF MVS SYSTEM
EID      DS    A                   --> DS CL8    ERROR CODE
TID      DS    A                   --> DS CL8    TRACKING IDENT.
STEPNAME DS    A                   --> DS CL8    STEP NAME
RFLAG    DS    A                   --> DS CL1    RECORD FLAG
RFLAGT   EQU   C'T'                = SYSOUT MATCHED WITH MESSAGE TABLE
RECORD   DS    A                   --> DS CL72   START OF SYSOUT RECD.
RETCODE  DS    A                   --> DS F      RETURN CODE 0=LOG
PARAMLEN EQU   *-PARAMS            LENGTH OF PARAMETER BLOCK
         EJECT ,
MGCR     IEZMGCR DSECT=NO
         ORG   MGCRTEXT
COMMAND  DS    CL50                SPACE FOR THE START COMMAND
         ORG
         SPACE 2
LENWORK  EQU   *-WORK              LENGTH OF DATA AREA
         SPACE 3
         END   DRKUX006
//L.SYSLMOD DD DISP=SHR,DSN=SYS1.LINKLIB,UNIT=DISK,VOL=SER=SYSS01
//L.SYSLIB  DD DISP=SHR,DSN=SYS1.LINKLIB,UNIT=DISK,VOL=SER=SYSS01
//L.SYSIN   DD *
 INCLUDE SYSLIB(DRKJCTOP)
 ENTRY DRKJCTOP
 SETCODE AC(1)
 NAME DRKJCTOP(R)
/*
//NOTNOW    DD *
 NAME DRKUX006(R)
 INCLUDE SYSLIB(DRKUX006)
 INCLUDE SYSLIB(DRKJCTOP)
 ENTRY DRKJCTOP
 SETCODE AC(1)
 NAME DRKJCTOP(R)
/*
