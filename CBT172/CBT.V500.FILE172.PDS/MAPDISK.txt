MAPDISK9 TITLE 'DISK MAPPING PROGRAM'
*********************************************************************
*        PDSDOC *MAP DISKS WITH ICF/VSAM DETAILS - SMS VERSION........* MAPDISK
*
*
*        PRODUCED AT TRIANGLE UNIVERSITIES COMPUTATION CENTER,
*                    RESEARCH TRIANGLE PARK,
*                    NORTH CAROLINA, 27709
*        SYSTEMS PROGRAMMER - H. L. JACKSON
*        MODIFIED FOR 2314 DEC67 BY TED BAUER & FRED FELBER MINNEAPOLIS
*
*
*              MONSANTO COMPANY
*             ==================
*
*   1960'S _ PROGRAM INTRODUCED IN THE INSTALLATION OF MONSANTO LTD
*             LONDON - SOMETIMES IN LAST THREE YEARS OF THE YEARS 60.
*
*   1970'S - MODIFIED A LOT BY SYSTEMS PROGRAMMERS OF MONSANTO EUROPE
*             BRUSSELS - DURING THE YEARS 70.
*
*                _ FOR 3330-11 SUPPORT  (MARCH 72 - P.S. GRANT)
*                _ MFT TO MVS COMPATIBILITY (1977)
*                _ FOR 3350 SUPPORT (DEC. 79 - J. NOLS)
*                _ ETC ................
*
*   1980'S - JUNE 81 - K. O'NEILL
*              PRINT OUT COMPLETELY REFORMATTED.
*
*          - SEPTEMBER 81 - K. O'NEILL
*              SUPPORT FOR INDEXED VTOC (DF/DS RELEASE 1.2)
*              MEMBERS PER PDS ADDED
*
*          - JANUARY   82 - K. O'NEILL
*              BYPASS DDNAME JOBLIB
*              EXTENT NUMBER INCREASED FROM 2 TO 3 CHARACTERS
*              CORRECTION OF LOGIC ERROR - ABEND S0C9 IF
*              BLOCKSIZE 19069 ENCOUNTERED ON 3350.
*
*          - JANUARY   83 - K. O'NEILL
*              PARM PROCESSING
*              REFDT WAS BEING CHANGED BY THE 'COUNT MEMBERS
*              IN PDS' ROUTINE. BY DEFAULT WILL NOT CHECK NUMBER.
*              IF PDS INFO NEEDED SPECIFY PARM='PDS'.
*
*********************************************************************
        EJECT
*********************************************************************
*
*
*    INTERDATA A.G.
*          - AUGUST  1987 - D.H.CARTWRIGHT
*              MODIFIED TO BYPASS BLOCKS/TRACK CALCULATIONS
*              BUT INSTEAD USE THE 'STAR' ROUTINE TO CALCULATE
*              TRACK CAPACITY.
*
*          - MARCH   1988 - D.H.CARTWRIGHT
*              ADDED ALTERNATE CYLINDER VALUES FOR 3380
*
*    SPRECHER ENERGIE A.G.
*          - JUNE 1989 - D.H.CARTWRIGHT
*          V6  MODIFIED TO WRITE SUMMARY RECORDS FOR LATER ANALYSIS
*              PROCESS CHAINED FMT3 DSCB FOR VSAM EXTENTS
*
*          - AUGUST 1989 - D.H.CARTWRIGHT
*          V7  MODIFIED TO SHOW DATA-SET-CHANGED BIT
*              26JUN90 CART1 CHANGE REPORT TITLE TO SHOW LATEST VERSION
*
*          - OCTOBER 1990 - D.H.CARTWRIGHT
*          V8  COMBINED WITH THE 'LISTICF' PROGRAM FROM THE GERMAN
*              G.U.I.D.E. GOODIES TAPE. THIS ALLOWS MORE DETAIL TO BE
*              SHOWN FOR ICF VSAM DATASETS
*          V8.1 CALCULATE SPACE UTILISATION AND WASTE IN SUMMARY RECS.
*
*          - MAY 1991     - D.H.CARTWRIGHT
*          V8.2 ADDED TODDATE ROUTINE TO PROCESS VSAM TIMESTAMPS
*               ADDED DVCT PROCESSING TO GET NO. SPARE TRACKS
*               SHOW PERCENT CA AND CI SPLITS
*
*          - SEPT 1991    - D.H.CARTWRIGHT
*          V8.3 ADDED SHAREOPTIONS TO SUMMARY RECORD
*
*          - 30MAR92      - D.H.CARTWRIGHT
*          V8.4 SHOW VSAM COMPONENT TYPE IN DSORG
*
*          - 04MAY92      - D.H.CARTWRIGHT
*          V8.5 GIVE VVDS FIVE DATA BUFFERS FOR PERFORMANCE
*
*          - 22MAY92      - D.H.CARTWRIGHT
*          V8.6 CALCULATE DAYS SINCE LAST REFERENCE FOR SUMMARY
*
*    DARNICK COMPUTING LTD.
*          - JULY 1994 - D.H.CARTWRIGHT
*          V8.7 CORRECT TRKCALC CODE FOR BLK ALLOCATIONS
*
*          - JULY 1995 - D.H.CARTWRIGHT
*          V9.0 ADD SMS SUPPORT FOR VVR TYPE 'N' RECORDS
*
*          - NOV. 1997 - D.H.CARTWRIGHT
*          V9.1 DF/SMS 1.2 MAY NOT PRODUCE TYPE 22 VVR CELLS
*
*          - DEC. 1999 - D.H.CARTWRIGHT
*          V9.2 RECOGNISE HFS FILES
*
*          - AUG. 2001 - D.H.CARTWRIGHT
*          V9.3 INCLUDE CODE FROM GREG PRICE CBT FILE 134 TO NOT
*               UPDATE LAST REF. DATE FOR DIRECTORY READ.
*
*********************************************************************
        EJECT
*********************************************************************
*
* FUNCTION
*
*  TO MAP THE ALLOCATION OF DATA SETS ON DISK PACKS.
*
*********************************************************************
* INPUT    PARM - IF PDS INFORMATION IS REQUIRED A PARM OF:-
*                 PARM='PDS' IS REQUIRED. THE DEFAULT IS TO NOT
*                 RECEIVE PDS INFORMATION.
*
*
* VOLUMES TO BE MAPPED  ALL VOLUMES TO BE MAPPED MUST HAVE A DD CONTROL
*                  CARD, THE DDNAME MAY BE ANYTHING, IT IS PREFFERED
*                  IF THEY ARE DD1, DD2, ETC.  DISP=SHR,VOLUME=SER AND
*                  UNIT=  MUST BE GIVEN. NO OTHER PARAMETERS ARE NEEDED
*                  THESE DD CARDS MUST FOLLOW THE SYSUT1 DD CARD.
*
* OUTPUT  SYSPRINT : MAP OF ALL DATA SETS
*         SYSOUT   : EXTENT MAP OF THE DISK
*
*
*********************************************************************
*
*        THIS  PROGRAM MODIFIED  16 MARCH 1972 TO ALLOW FOR
*        3330'S.  THE MAIN PROBLEM WAS THAT SPACE WAS ALLOCATED
*        IN-LINE FOR TABLES AND THESE TABLES WERE NOT LARGE ENOUGH
*        FOR 3330'S.  THE PROGRAM WAS CHANGED TO OBTAIN CORE (GETMAIN)
*        FOR THE MAJORITY OF THE TABLES NEEDED.  ALSO NO CHECKING
*        WAS MADE FOR END OF TABLE SO THIS CODE WAS ALSO INSERTED.
*        IF TABLE OVERFLOW OCCURS A MESSAGE WILL BE PRINTED WHICH
*        INDICATES WHICH TABLE NEEDS TO BE INCREASED:
*          ***MAPDISK TABLE OVERFLOW***D
*                                      *
*          *****************************
*          *
*          ***WHERE D=0  INCREASE FMT1SIZE
*                   D=1  INCREASE F356SIZE
*                   D=3  INCREASE ACCTSIZE
*                   D=7  INCREASE SRTSIZE
*        EQU'S WERE USED TO FACILITATE CHANGES
*        IT IS ACKNOWLEDGED BY THE MODIFIER THAT THE MODIFIEE
*        SHOULD PROBABLY BE CHANGED SO THAT NO BOUNDS EXIST FOR
*        TABLES OTHER THAN REGION SIZE BUT TIME WAS NOT AVAILABLE
*
*********************************************************************
         EJECT
*******************************************************************
*                                                                 *
*        LIMITATIONS                                              *
*       ------------                                              *
*                                                                 *
* 1) MUST BE RUN AUTHORISED FROM AN AUTHORISED LIBRARY            *
*                                                                 *
* 2) VSAM CODE ONLY WORKS WITH ICF/VSAM CATALOGS. NOTE THAT THE   *
*    VSAM ERROR HANDLING CODE OCCASIONALLY TERMINATES THE PROGRAM *
*    FOR NO KNOWN REASON. THIS BUG IS UNDER INVESTIGATION.        *
*    THIS VERSION IS COMPATIBLE WITH DFP 2.4, BUT MAY NEED TO     *
*    BE REWORKED FOR DFP 3.X                                      *
*                                                                 *
* 3) REQUIRES SUB-ROUTINES FROM CBT FILE 172;                     *
*             IXVTOCF5                                            *
*             VSMERROR                                            *
*    THESE SHOULD BE LOADED FROM AN APF AUTHORISED LIBRARY.       *
*                                                                 *
*                                                                 *
*******************************************************************
         SPACE 2
*********************************************************************
*
*         REGISTER EQUATES
*
*********************************************************************
*
* GENERAL REGISTER USAGE
*        0     TTR OF VTOC PLUS STANDARD PARAM REGISTER
*        1     DEB ADDRESS PLUS STANDARD PARAM REGISTER
*        2     WORK
*        2     PARAMETER TO CONVERT TTR - BOOST R IN ABSOLUTE
*        2     CONVERT TO RELATIVE TRACK, TEMP. REG.
*        2     MDCB POINTER
*        3     WORKING REG - SORT FORMAT1 DSCB'S DS TABLE ADDRESS
*        3     ABSOLUTE TO RELATIVE TRACK, TEMP. STORE
*        4     WORKING REG - SORT F1  KOUNT REGISTER
*        4     RETURN FOR SUBROUTINES
*        5     WORKING REG - SORT F1 ADDRESS OF DSCB
*        5     WORKING REG - VVDS RECORD LENGTH
*        6     BASE FOR PROGRAM, SECOND
*        7     BASE ADDRESS FOR 3-5-6 DSCB TABLE
*        7     VVDS RECORD POINTER
*        8     BASE REGISTER
*        9     VVDS ENTRIES TABLE POINTER
*        9     BASE FOR UCB ADDRESS LIST
*        10    BASE FOR DSCB DESCRIPTION BLOCK
*        11    BASE FOR UCB DESCRIPTION BLOCK
*        12
*        12    BASE REGISTER #4
*        13    SAVE AREA POINTER AND BASE #1
*        14    RETURN FOR SUBS
*        15    POINTER AND CONDITION CODE RETURN
*
*********************************************************************
R0       EQU   0                   SYSTEMS
R1       EQU   1                   SYSTEMS
R2       EQU   2                   WORK & TEMP
R3       EQU   3                   WORK & TEMP
R4       EQU   4                   WORK & TEMP
R5       EQU   5                   WORK & TEMP
R6       EQU   6                   BASE REGISTER FOR PROGRAM
R7       EQU   7                   BASE TABLE
R8       EQU   8                   BASE REGISTER
R9       EQU   9                   WORK UCB DSECT
R10      EQU   10                  BASE DSCB DSECT
R11      EQU   11                  BASE REGISTER #3
R12      EQU   12                  BASE REGISTER #2
R13      EQU   13                  SAVE AND BASE #1
R14      EQU   14                  RETURN
R15      EQU   15                  REGISTER ASSIGNMENTS
RCVT     EQU   16                  FETCH ADDRESS OF CVT TABLE
         EJECT
*********************************************************************
*
*         INITIALIZATION
*
*********************************************************************
MAPDISK  CSECT ,
         PRINT GEN
         PGMIN USING=(R13,R12,R11,R8,R6),EQUATES=NO
         EJECT
*******************************************************************
*        SAVE ADDRESS AND LENGTH OF PARM OF EXEC CARD             *
*******************************************************************
         SPACE 1
         L     R1,0(R1)            LOAD ADDRESS OF PARM INTO R1
         MVC   PARMLGTH+2(2),0(R1)  STORE LENGTH OF PARM INFO'S
         LA    R1,2(R1)            GET ADDRESS OF PARM INFO'S
         ST    R1,PARMADDR         STORE ADDRESS OF PARM INFO'S
         SR    R1,R1               FINISHED WITH REGISTER 1
         SPACE 3
*******************************************************************
*        PROCESS PARM FIELD                                       *
*******************************************************************
         SPACE 1
         L     R1,PARMADDR         LOAD ADDRESS OF PARM AREA
         LTR   R2,R1               TEST IF PARM SUPPLIED
         BZ    ENDPARM             BRANCH IF NO
         CLC   PARMLGTH,=F'3'      SEE IF PARM CORRECT LENGTH
         BNE   ENDPARM             BRANCH IF NO
         CLC   0(3,R2),=C'PDS'     SEE IF PDS INTERROGATION ASKED
         BNE   ENDPARM             BRANCH IF NO
         OI    PARMSWCH,X'FF'      SET PARM SWITCH ON
ENDPARM  DS    0H                  FINISHED PARM PROCESSING
         EJECT
*********************************************************************
*         DATE PROCESSING
*********************************************************************
         SR    R1,R1               ASK FOR CURRENT TIME
         GOSUB TODDATE             GO FORMAT TOD-CLOCK
         DATE  HEADDATE,FORMAT='DD MMM YY',FROM=TODPJUL FORMAT DATE ..
         MVC   HEADTIME,TODTIME    .. AND TIME IN HEADING
         LA    R1,TODDSCB          POINT TO BINARY DATE
         GOSUB STDDAY01            CONVERT TO STANDARD DAY NO.
         STH   R1,STDRUN           SAVE DAY NO. OF THIS RUN
         EJECT
*********************************************************************
*         OPEN FILES
*********************************************************************
         SPACE 2
         LA    R2,SYSUT2SW         POINT AT MDCB LIST
         USING MDCB,R2             ESTABLISH ITS ADDRESSSABILITY
         OI    MDCBFLAG,DDINT      EXPRESS INTEREST
         GOSUB TESTIOT             GO FIND IF IT IS PRESENT
         GOSUB OPEN                GO OPEN IT
         DROP  R2                  END MDCB ADDRESSABILITY
         SPACE 1
         OPEN  (PRINT,(OUTPUT))
         USING IHADCB,R9
         LA    R9,PRINT            OPEN SUCCESSFUL?
         TM    DCBOFLGS,X'10'
         BZ    RETURN              NO
         OPEN  (SYSOUT,(OUTPUT))
         LA    R9,SYSOUT
         TM    DCBOFLGS,X'10'      OPEN SUCCESSFUL?
         DROP  R9
         BZ    NOSYSOUT            NO
         EJECT
NOSYSOUT DS    0H
         GETMAIN VC,LA=BOUNDS,A=TBLADDR  FIND CORE AVAILABLE
         FREEMAIN  V,A=TBLADDR     FREE IT
         NC    TBLLENG+2,=X'F800'  TRUNCATE TO 2K
         L     R0,TBLLENG          *    SAVE 10K FOR PDS
         S     R0,=F'10240'        ***  PROCESSING AND ABEND
         ST    R0,TBLLENG          *
         GETMAIN R,LV=(0)          GET TABLE AREA
         LR    R7,R1               LOAD START OF AREA
         ST    R1,VSAMAREA         SET VSAM AREA POINTER
         A     R1,VSAMLEN          BUMP STORAGE OVER INPUT AREA
         ST    R1,TBLADDR
         ST    R1,VVETBL           SAVE START OF VVE TABLE
         ST    R1,VVENOW           SAVE 1ST ENTRY OF VVE TABLE
         A     R7,TBLLENG          ADD LENGTH OF TABLE
         ST    R7,F356TBL          START OF F356 TABLE
         ST    R7,F356END          END OF F356 TABLE
         L     R15,RCVT            LOAD CVT POINTER
         USING CVT,R15
         MVC   APPEN,CVTXAPG+1     MOVE ADDRESS ONCE
         DROP  R15                 END CVT ADDRESSABILITY
         EXTRACT TIOT,'S',FIELDS=(TIOT) GET ADDRESS OF TIOT IN WORK
         L     R2,TIOT             BRING ADDRESS OF TIOT IN
         LA    R2,24(R2)           BUILD UP TO FIRST DDNAME
         ST    R2,TIOT             STORE VALUE FOR NEXT TIME AROUND
         GETAUTH TYPE=GET          AUTHORISATION
         SPACE 1
DISKLOOP DS    0H                  ( CLEAR PRINT LINE
         MVI   DATANAME,C' '       ( CLEAR PRINT LINE
         MVC   DATANAME+1(132),DATANAME  ( CLEAR PRINT LINE
         MVI   ERMS+L'ERMS-1,C'0'     SET UP ERROR MESSAGE VALUE
         SR    R15,R15             ZEROISE 4 BYTES
         ST    R15,THISALLC        ZEROISE TRACKS ALLOC
         ST    R15,THISUSED        ZEROISE TRACKS USEDC
         L     R15,F356TBL         GET F356TBL START
         ST    R15,F356END         RESET TABLE END
         EJECT
*********************************************************************
*         DD CHECKS
*********************************************************************
SWT1     NOP   MANY                SWITCH FOR MORE THAN ONE VOL IN DD
         L     R2,TIOT             CHECK FOR END OF WORK
TIOTLOOP DS    0H
         SR    R3,R3               CLEAR COUNT REG
         IC    R3,0(R2)
         LTR   R3,R3               TEST FOR LAST ENTRY IN TIOT
         BZ    ALLDONE             BRANCH IF DONE
         B     LOOKON              GO TEST FOR SPECIAL NAMES
BYPASSDD DS    0H
         LA    R2,0(R2,R3)         BUMP TO NEST DD NAME
         B     TIOTLOOP            CONTINUE TO LOOK FOR DDNAMES
LOOKON   DS    0H
         CLC   4(3,R2),=C'SYS'
         BE    BYPASSDD            BYPASS IF  SYSTEM DDNAME
         CLC   4(4,R2),=CL4'PGM='
         BE    BYPASSDD            BYPASS IF DD IS PGM=
         CLC   4(8,R2),=CL8'STEPCAT'
         BE    BYPASSDD            BYPASS  DD  IF STEPCAT DD
         CLC   4(8,R2),=CL8'JOBLIB'
         BE    BYPASSDD            BYPASS IF JOBLIB
         CLC   4(8,R2),=CL8'STEPLIB'
         BE    BYPASSDD            BYPASS IF STEPLIB
         CLC   4(8,R2),=CL8' '     IS THERE A DD NAME?
         BE    BYPASSDD            BYPASS IF NO DD NAME
         MVC   DCBDDNM(8),4(R2)    MOVE THIS NAME TO DCB
         MVC   DCBDDNM2(8),4(R2)   MOVE NAME TO UTILITY DATA BLOCK
         LA    R9,12(R2)           SAVE START OF UCB'S
         LA    R2,0(R2,R3)         READY FOR NEXT DDNAME
         ST    R2,TIOT             REPLACE
         RDJFCB  (TEST)            GET VOLUMN NO. FOR THIS DD
         LA    R4,JFCBAREA         ESTABLISH ADDRESSABILITY
         USING IEFJFCBN,R4         FOR JFCB
         TM    JFCBTSDM,X'20'      SEE IF USER SPECIFIED A SYSOUT
         BNO   CHECKV              OR SYSIN,BRANCH IF NOT
         MVC   EXTOUT+10(L'ERMS1),ERMS1
         DROP  R4                  END JFCB ADDRESSABILITY
         SPACE 1
PRINTIT  DS    0H
         BAL   R14,LINE1OUT        PRINT FROM EXTOUT
         B     DISKLOOP            PROCESS NEXT ONE
CHECKV   SH    R3,=H'16'           REDUCE BY OVERHEAD
         SRL   R3,2                R3=NUMBER OF VOLUMES
         STC   R3,TIOTNVOL         SAVE NUMBER OF VOLUMES
         OI    SWT1+1,X'F0'        TURN ON SW FOR MULTI VOLS
MANY     SR    R3,R3
         IC    R3,TIOTNVOL         GET NUMBER OF VOLS REMAINING
         LTR   R3,R3
         BZ    DISKLOOP            LOOK FOR MORE DD'S
VOLICK   BCT   R3,SLIPIN           LAST ONE DOES NOT BRANCH
         NI    SWT1+1,X'0F'        TURN OFF SWITCH
SLIPIN   LA    R9,4(R9)            GET ADDR OF NEXT UCB
         ST    R9,UCBADDR          SAVE IT
         STC   R3,TIOTNVOL         SAVE NUMBER OF VOLS
         ICM   R7,B'0111',1(R9)    GET UCB ADDRESS
         BZ    DISKLOOP            SKIP IF NONE
         USING UCB,R7              ESTABLISH UCB ADDRESSABILITY
         MVC   SAVESER,SRTEVOLI    SAVE VOLUME SERIAL SERIAL
         MVC   SERMSG,SRTEVOLI     PUT IN VOLSER
         MVC   MSGTP,SRTEVOLI      PUT IN VOLSER
         ZAP   PAGEKNT,=P'0'       CLEAR PAGE COUNTS
         ZAP   GPAGEKNT,=P'0'
         EJECT
*********************************************************************
*         DASD TYPE CHECKS
*********************************************************************
         CLI   UCBTYP+2,X'20'      CHECK IF DIRECT ACCESS
         BE    ONOFF               BRANCH IF UNIT IS A DASD
UNITERR  DS    0H                  WRONG UNIT TYPE
         MVC   EXTOUT+10(L'ERMS2),ERMS2
         B     PRINTIT             GO PRINT IT
ONOFF    DS    0H                  CHECK FOR ON LINE
         TM    SRTESTAT,X'80'      IS UNIT ON-LINE?
         BZ    UNITERR             BRANCH IF OFF-LINE
         LR    R1,R7               COPY UCB ADDRESS
         GOSUB GETDVCT             GET DEVICE CHARACTERISTICS
         USING DVCT,R1             ESTABLISH TABLE ADDRESSABILITY
         MVC   NOSPARES,DVCALT     SET UP NUMBER OF SPARES
         DROP  R1                  END DVCT ADDRESSABILITY
         SPACE 1
ITSADISK DS    0H                  NOW KNOW DEVICE TYPE
         MVC   TTRVTOC,UCBVTOC
         MVI   SEEK,0
         MVC   SEEK+1(7),SEEK      CLEAR CONVERT BIN
         ST    R7,UCBXREF          STORE UCB ADDRESS
         MODESET KEY=ZERO,MODE=SUP GET INTO RIGHT FRAME OF MIND  *DHC*
         L     R15,RCVT            LOAD CVT POINTER
         USING CVT,R15
         L     R15,CVTPCNVT        LOAD ADDRESS OF CONVERT ROUTINE
         DROP  R15                 END CVT ADDRESSABILITY
         L     R0,UCBVTOC
         LA    R1,MYDEB            ADDRESS OF PSEUDO DEB
         LA    R2,SEEK             ADDRESS TO STORE
         LA    R4,SAVECNVT         SAVE AREA INDEPENDENT OF BASE REGS
         STM   R9,R13,0(R4)        SAVE REGS
GOCNVT   DS    0H                  INVOKE IECPCNVT
         BALR  R14,R15             GO TO CONVERT ROUTINE
EXCNVT   DS    0H                  RETURN FROM IECPCNVT
         LM    R9,R13,0(R4)        RETURN REGS
         MODESET KEY=NZERO,MODE=PROB  RETURN TO PREVIOUS STATE *DHC*
         B     GETVVRS             GO PROCESS VVDS
         SPACE 1
         DROP  R7                  END UCB ADDRESSABILITY
         EJECT
*********************************************************************
*        VVDS PROCESSING - READ VVR'S AND SAVE ADDRESSES
*********************************************************************
         SPACE 1
GETVVRS  DS    0H                  LOOK FOR VVR'S
         L     R9,VVETBL           GET ADDRESS OF VVR TABLE
         XC    VVECNT,VVECNT       RESET RECORD COUNT
         USING VVETABLE,R9         ESTABLISH TABLE ADDRESSABILITY
         GOSUB DYNAVVDS            GO DYNAMICALLY ALLOCATE VVDS
         LTR   R15,R15             ALLOCATED OK?
         BNZ   VVDSDONE            NO, HANDLE THAT SITUATION
         OI    VSAMSW,DDPRES       SHOW DD PRESENT
         SPACE 1
         XC    VVDSRBA,VVDSRBA     RBA = 0
         OPEN  (VVDSACB),MODE=24   OPEN THE ACB
         LTR   R15,R15             OPEN OK?
*        BNZ   OPENERR             NO, ABEND
         BNZ   VVDSDONE            NO, SKIP THIS BIT
         OI    VSAMSW,DDOPEN       SHOW DD OPEN
         GOSUB VVDSGET             GO READ THE VOLUME CONTROL RECORD
         SPACE 1
VVDSLOOP DS    0H                  READ THE WHOLE VVDS
         GOSUB VVDSGET             GO READ THE VVVDS
         L     R2,VVDSAREA         GET RECORD POINTER
         USING VVR,R2              ESTABLISH ADDRESSABILITY TO HEADER
         CLI   VVRTYPE,C'N'        NONVSAM RECORD?
         BE    VVDSMRBA            YES, REMEMBER
         CLI   VVRTYPE,C'Z'        PRIMARY RECORD?
         BNE   VVDSLOOP            NO, FORGET IT
         SPACE 1
VVDSMRBA DS    0H                  MEMORISE THE RBA OF THIS ENTRY
         MVI   VVENAME,BLANK       CLEAR NAME ..
         MVC   VVENAME+1(L'VVENAME-1),VVENAME .. TO BLANKS
         IC    R1,VVRCMPNL         GET COMPONENT NAME LENGTH
         BCTR  R1,0                DECREMENT FOR LENGTH FIELD
         BCTR  R1,0                DECREMENT FOR EXECUTE
         EX    R1,VVEMOVE          COPY COMPONENT NAME
         SHOWCB  RPL=VVDSRPL,AREA=(S,VVERBA),                          +
               LENGTH=L'VVERBA,FIELDS=(RBA)
         LTR   R15,R15             TEST RETURN CODE
         BNZ   SHOWERR             HANDLE PROBLEMS
         LH    R1,VVECNT           GET RECORD COUNT
         LA    R1,1(R1)            ADD ONE
         STH   R1,VVECNT           SAVE NEW COUNT
         LA    R9,VVETABL(R9)      POINT TO NEXT TABLE SLOT
         C     R9,F356TBL          HAVE WE RUN OUT OF ROOM?
         BNL   VVDS090             YES, ISSUE WARNING
         B     VVDSLOOP            GO ROUND FOR NEXT RECORD
         SPACE 2
VVEMOVE  MVC   VVENAME(*-*),VVRCMPNM  MOVE COMPONENT NAME    *EXECUTED*
         SPACE 1
         DROP  R2                  END HEADER ADDRESSABILITY
         EJECT
VVDSEOF  DS    0H                  END OF SEQUENTIAL VVDS
         MODCB RPL=VVDSRPL,OPTCD=(ADR,DIR,LOC,NSP)  SWITCH TO DIRECT
         LTR   R15,R15             MODCB OK ?
         BNZ   MODCBERR            NO ==> ABEND
         SPACE 1
         XC    VVDSRBA,VVDSRBA     RBA = 0
         POINT RPL=VVDSRPL         RESET FILE
*        CLOSE (VVDSACB)
*        OPEN  (VVDSACB)
*        WTO   'MAPDISK9 ==> VVDS END OF FILE - CALL SYSTEMS'
         SPACE 1
         MODCB EXLST=VVDSEXLS,EODAD=VSAMEOF  CHANGE EOD ADDRESS
         LTR   R15,R15             MODCB OK ?
         BNZ   MODCBERR            NO ==> ABEND
         B     VVDSDONE            CARRY ON
         SPACE 1
         DROP  R9                  END VVR TABLE ADDRESSABILITY
         SPACE 2
VVDS090  DS    0H
         WTO   'MAPDISK9 ==> VVDS TABLE OVERFLOW - CALL SYSTEMS',      *
               ROUTCDE=11
         XC    VVECNT,VVECNT       RESET RECORD COUNT
         SPACE 1
VVDSDONE DS    0H                  VVDS SEQUENTIAL READ IS OVER
         LA    R2,VVETABL          GET LENGTH OF VVE TABLE ELEMENT
         MH    R2,VVECNT           CALCULATE TABLE LENGTH
         L     R7,TBLADDR          LOAD START OF TABLE
         LA    R7,0(R7,R2)         CALCULATE START OF F1 TABLE
         ST    R7,DSTBL            START OF F1 TABLE
         ST    R7,DSEND            END OF F1 TABLE
         L     R7,F356TBL          R7=START OF 3-5-6 DSCB POINTERS
         L     R9,UCBADDR          R9=ADDR OF NEXT UCB ADDRESS
         MVC   NTRKNT,=F'1'        START DSCB'S AT BOTTOM OF TABLE
         MVC   SRTKNT,=F'1'        START SORT COUNT AT BOTTOM
         GOSUB SORTVVR             GO SORT THE VVR ENTRY TABLE
         EJECT
*********************************************************************
*        VTOC PROCESSING - READ AND SAVE DSCB'S
*********************************************************************
         SPACE 1
GETDSCB  DS    0H                  LOOK FOR DSCB'S
         LA    R10,WORK            ADDRESS FOR DSCB FORMAT 4
         USING DSCBS,R10           SETTING UP ADDRESSES
         OBTAIN  LIST              LOOK FOR DSCB'S
         LTR   R15,R15             SEE IF OK
         BZ    NEXTCHK             OK
         SPACE 1
CHECKIT  C     R15,=F'8'           MIDDLE OF TEST RANGE
         BL    VOL                 VOLUME NOT MOUNTED
         BE    FETCHNXT            NOT THERE ANY MORE, GET NEXT DSCB
         B     IOER                TERMINAL ERROR
         SPACE 1
NEXTCHK  CLI   DS4IDFMT,C'4'       CHECK FOR FORMAT 4
         BNE   IOER                FIRST BLOCK MUST BE
         MVC   SAVEF4,DSCBS        SAVE ALL OF FORMAT 4
         MVC   SVCOUNT,DS4DEVDT    SAVE NUMBER OF DSCBS PER TRACK
         MVC   SAVEHWM,DS4HPCHR    SAVE HIGH WATER MARK IN VTOC
         CLC   SAVEHWM,DS4F6PTR    IS THERE A FORMAT6 PAST THIS
         BNL   FETCHNXT            BRANCH IF NOT
         MVC   SAVEHWM,DS4F6PTR    OTHERWISE, SAVE THIS ADDRESS
         EJECT
FETCHNXT SR    R2,R2               CLEAR WORKING REG
         LA    R10,SAVEF4          SET UP ADDRESS FOR IT
         IC    R2,ADDRS+4          RECORD COUNT IN
         LA    R2,1(R2)            BOOST BY ONE
         CH    R2,TRKCNT           CHECK NUMBER OF RECORDS ON THE TRACK
         BNH   FETCHSQ             GO FETCH NEXT
         MVC   WORK(4),ADDRS       MOVE TO TAKE CARE OF BOUNDRY
         LH    R2,WORK+2           LOAD HH OF CCHH
         LA    R2,1(R2)            BUMP TO NEXT TRACK
         STH   R2,WORK+2
         CH    R2,DS4DEVSZ+2       END OF CYL?
         BL    PUTBACK             NO
         SR    R2,R2
         STH   R2,WORK+2           RESET TRACK TO ZERO
         SR    R2,R2               CLEAR WORK REG
         ICM   R2,B'0011',WORK     GET CYL
         LA    R2,1(R2)            BUMP
         STH   R2,WORK
PUTBACK  DS    0H
         MVC   ADDRS(4),WORK       PLACE BACK (CYL + 1)
         LA    R2,1                RECORD = 1
FETCHSQ  STC   R2,ADDRS+4          STORE RECORD COUND
FETCHRND CLC   SAVEHWM,ADDRS       SEE IF VTOC FINISHED
         BL    PRMAP               GO PRINT OUT MAP OF DISK
F1DSCBS  OBTAIN  LIST              NEXT DSCB
         LTR   R15,R15             CHECK FOR SUCCESS
         BNE   CHECKIT1            JUMP IF BAD
         LA    R10,WORK            RE INSTATE FOR NEXT TWO CHECKS
         CLI   DS4IDFMT,0          CHECK FOR FORMAT 0
         BE    FETCHNXT            IGNORE IF SO
         CLC   SAVEHWM,DS1PTRDS    SEE IF CHAIN ADDRESS HIGHER THAN
         BNL   CHECKF1             ANY SO FAR.  BRANCH IF NOT
         MVC   SAVEHWM,DS1PTRDS    SAVE IT IF SO
CHECKF1  CLI   DS4IDFMT,C'1'       CHECK FOR FORMAT 1
         BE    FORMAT1             GO PROCESS
         SH    R7,H160             BACK UP ENTRY
         CL    R7,DSEND            TABLES OVERLAPPED?
         BNH   TABLOVF0            YES
         ST    R7,F356END          STORE NEW END
         LA    R1,4(R7)
         ST    R1,0(R7)            STORE PTR TO DSCB
         MVC   4(140,R7),WORK      SAVE DSCB
         MVC   144(8,R7),SEEK      SAVE CCHHR
         B     FETCHNXT            GO GET NEXT DSCB
         EJECT
FORMAT1  DS    0H
         NOP   SORTGEN
SORTGEN  DS    0H
         L     R3,DSEND            END OF TABLE
         MVC   4(140,R3),WORK      SAVE DSCB
         MVC   144(8,R3),SEEK      SAVE CCHHR
         LA    R1,4(R3)            PTR TO DSCB
         LA    R3,160(R3)          NEW END
         CL    R3,F356END          TABLES OVERLAPPED?
         BNL   TABLOVF0            YES
         ST    R3,DSEND            SAVE NEW END
         L     R3,DSTBL            START OF TABLE
         SH    R3,H160
         L     R4,NTRKNT           NO OF ENTRIES
SORTF12  DS    0H
         BCT   R4,SORTF11
         ST    R1,160(R3)          STORE DSCB PTR
SORTF14  L     R4,NTRKNT           BRING IN COUNT
         LA    R4,1(R4)            TO BOOST BY ONE
         ST    R4,NTRKNT
         B     FETCHNXT            GO GET NEXT DSCB
SORTF11  LA    R3,160(R3)          BOOST TABLE ADDR
         L     R5,0(R3)            POINTER TO DSCB
         CLC   WORK(44),0(R5)      LOOK AT DSNAME
         BH    SORTF12             JUMP AROUND IF IT DOES NOT FIT
SORTF13  ST    R1,0(R3)            PUT ADDRESS IN TABLE
         LR    R1,R5               LAST ADDRESS MOVED AROUND
         L     R5,160(R3)          NEXT ADDR
         LA    R3,160(R3)          BOOST TO NEXT
         BCT   R4,SORTF13          MOVE ALL UP
         ST    R1,0(R3)            PUT OUT LAST ADDRESS
         B     SORTF14             FINISH UP THIS DSCB
         SPACE 1
CHECKIT1 C     R15,=F'8'           MIDDLE OF AREA
         BL    VOL1
         BE    FETCHNXT
         B     IOER
         EJECT
PRMAP    DS    0H
         NI    HEADER+1,X'0F'      PRINT FULL HEADER
         BAL   R14,HEADING         PRINT HEADING
         L     R9,DSEND
         ST    R9,SRTBL            SRT TABLE
         ST    R9,SREND
         L     R9,F356END
         ST    R9,ACCTABLE         ACC TABLE
         ST    R9,ACCEND
         MVI   OUTBIN,C'0'         SKIP TO NEXT LINE, ONE BLANK LINE
         MVC   DATANAME(4),=C'VTOC'
         LA    R1,DS4VTOCE         PUT OUT EXTENT SIZE
         BAL   R14,ABTOREL         CONVERT TO RELATIVE TRACKS R2 BACK
         LA    R3,1                PSUEDO EXTENT NO 1
         BAL   R14,STORMAP         STORE EXTENT FOR MAP R1,R2,DATANAME
         BAL   R14,LENGTH          FIND LENGTH OF DATA SET R2-R3 BACK
         BAL   R14,NEDIT           PREPARE TO PRINT R3,R1
         MVC   SPALLOC,EDITWA+3    PUT IN SPACE USED
         LR    R9,R2               SAVE TOTAL FOR AWHILE
         MVC   HOLD+4(4),DS4HPCHR  GET END MARKER
         SRL   R2,16
         BAL   R14,ABTOREL1        DO ONLY USED
         BAL   R14,LENGTH
         BAL   R14,NEDIT
         MVC   SPUSED,EDITWA+3     PUT IN SPACE USED
         BAL   R14,LINETEST        TEST PAGE EJECT
         PUT   PRINT,OUTBIN        PRINT LINE OF OUTPUT
         LR    R2,R9               OUT OF ICE
         LA    R3,1                EXTENT NUMBER
         BAL   R14,FULLEDIT        DO FULL EDIT
         BAL   R14,LINE1OUT
         NI    GENOPT,FF-GENSMS    RESET FLAG
         TM    DS4SMSFG,DS4SMSCV   SMS VOLUME?
         BNO   PRL0001             BRANCH IF NO
         MVC   DSORG,=CL4'SMS'     YES, REPORT IT
         OI    GENOPT,GENSMS       AND SET FLAG
         SPACE 1
PRL0001  DS    0H                  CONTINUE
         TM    DS4VTOCI,DS4DOSBT+DS4IVTOC  INDEXED VTOC ?
         BNO   DOSONLY             BRANCH IF NO
         MVC   DATANAME(10),=CL10'FREE SPACE'
         EJECT
*********************************************************************
*
*         START INDEXED VTOC/OS VTOC ROUTINE FOR FREE SPACE INFO.
*         LINK TO IXVTOCF5 PROGRAM IF IT IS INDEXED.
*
*********************************************************************
         MVC   DATANAME+L'DATANAME-7(7),=C'IX VTOC'
         B     CARON
DOSONLY  TM    DS4VTOCI,DS4DOSBT   TEST FOR DSCB 5
         BO    DOF1S               NO DSCB 5 JUMPS
         MVC   DATANAME(10),=CL10'FREE SPACE'
         MVC   DATANAME+L'DATANAME-30(30),=C'* NOT AN IX VTOC--INFORM TX
               SG                  *'
BLANK    EQU   X'40'               *
CARON    MVI   SPUSED,BLANK        *****    CLEAR USED SPACE
         MVC   SPUSED+1(4),SPUSED  *
         L     R10,F356TBL         START OF TABLE
         SH    R10,H160
         L     R10,0(R10)          ADDR FO F5 DSCB
         SR    R3,R3               CLEAR EXTENT COUNT
         TM    SAVEF4+58,DS4DOSBT+DS4IVTOC  INDEXED VTOC ?
         BO    IXVTOC1             BRANCH IF YES
         CLI   DS5FMTID,C'5'       CHECK FOR 5
         BNE   IOER1               BAD, GO TO NEXT VOL
IXVTOC1  L     R9,ACCTABLE         GET TO START OF ACCUM TABLE
         SR    R1,R1               CLEAR ACCUM REG.
         TM    SAVEF4+58,DS4DOSBT+DS4IVTOC  INDEXED VTOC ?
         BNO   NEXTDS              BRANCH IF NO
         XC    PSEUDOF5,PSEUDOF5   YES, CLEAR BUFFER TO INDICATE 1ST
IXVTOC2  L     R4,UCBXREF          LOAD UCB ADDRESS
         LA    R5,PSEUDOF5         POINT R5 TO BUFFER OF PSEUDO DSCB F5
         LINK  EP=IXVTOCF5         ASK FOR IT
         DROP  R10                 DROP CURRENT ADDRESSABILITY
         USING DSCBS,R4            ESTABLISH TEMPORARY ADDRESSABILITY
PSEUDGO  LA    R4,PSEUDOF5         POINT R4 TO PSEUDO DSCB FORMAT 5
         LA    R5,8                ESTABLISH LOOP FOR THE FIRST 8 EXTS
         LA    R4,DS5AVEXT         POINT R4 TO AREA OF EXTENTS
         BAL   R14,NOTHER          DECODE FIRST EIGHT EXTENTS
         LA    R4,PSEUDOF5         POINT R4 TO PSEUDO DSCB FORMAT 5
         LA    R5,18               ESTABLISH LOOP FOR THE LAST 18 EXTS
         LA    R4,DS5MAVET         POINT R4 TO AREA OF EXTENTS
         BAL   R14,NOTHER          DECODE LAST 18 EXTENTS
         LA    R4,PSEUDOF5         POINT R4 TO PSEUDO DSCB FORMAT 5
         C     R5,DS5PTRDS+1       CHECK IF MORE EXTENTS
         BE    ENDDSCB5            NO, QUIT
         B     IXVTOC2             YES, GO READ NEXT ONE
         DROP  R4                  DROP TEMPORARY ADDRESSABILITY
         USING DSCBS,R10           RE-ESTABLISH NORMAL ADDRESSABILITY
NEXTDS   LA    R5,8                8 FIELDS FIRST
         LA    R4,DS5AVEXT         AREA OF EXTENTS
         BAL   R14,NOTHER          DO FIRST 8 EXTENTS
         LA    R5,18               LAST IN DSCB
         LA    R4,DS5MAVET
         BAL   R14,NOTHER          DO LAST 18 EXTENTS
         C     R5,DS5PTRDS+1       CHECK IF ONE MORE DSCB5 AROUND
         BE    ENDDSCB5            NO, QUIT
         BAL   R14,SRCHDSCB        GO SEARCH FOR DSCB 5
         B     NEXTDS              KEEP GOING TILL END
ENDDSCB5 SH    R9,H4
         MVC   0(4,R9),=F'0'       END OF LIST
         ST    R9,ACCEND           SAVE ADDR
         BAL   R14,NEDIT           MAKE EXTENT COUNT PRINTABLE
         MVC   EXTENTS,EDITWA+5    AND PRINT IT
         LR    R3,R1               COUNT IN PARM AREA
         BAL   R14,NEDIT           READY FOR PRINT
         MVC   SPALLOC,EDITWA+3    PUT IN SPACE ALLOCATED
         GOSUB SUMMARY             GO CREATE SUMMARY RECORD
         PUT   PRINT,OUTBIN
         MVC   DATANAME,=CL44'          FREE SPACE                    '
         B     PRNTCOM             GO PRINT AND SORT ALL EXTENTS
         EJECT
*********************************************************************
*
*         PROCESS FREE SPACE EXTENTS
*
*********************************************************************
         SPACE 1
NOTHER   DS    0H
         SUBIN SAVE=(R3)           SUBROUTINE ENTRY
NOTHER1  DS    0H                  PROCESS ONE EXTENT
         MVC   WORK(5),0(R4)       LOAD UP  EXTENT VALUE
         SR    R15,R15             CLEAR WORKING REGISTER
         ICM   R15,B'0011',WORK+2
         MH    R15,SAVEF4+64
         SR    R2,R2
         IC    R2,WORK+4
         AR    R15,R2              FIRST TRACK PLUS CYLINDER
         BZ    ENDDSCB5            DONE IF NOTHING THERE
         AR    R1,R15              ACCUMULATE TOTAL
         ICM   R2,B'0011',WORK
         AR    R15,R2              STOP ADDRESS PLUS ONE
         SLL   R2,16               MOVE TO LEFT
         ALR   R2,R15              ALL TOGETHER
         BCTR  R2,0                LESS ONE
         SH    R9,H4               NEXT ENTRY
         CL    R9,SREND            TABLE OVFL?
         BNH   TABLOVF0            YES
         ST    R2,0(R9)            PUT IT AWAY IN ACCUM
         LA    R4,5(R4)            GO TO NEXT ONE
         LA    R3,1(R3)            COUNT EXTENTS
         BCT   R5,NOTHER1          GO DO FOR NEXT
         SPACE 1
         SUBOUT RETURN=(R14)       RETURN TO CALLER
         EJECT
*********************************************************************
*
*         START REAL DATA SET PRINT OUTS HERE, THRU WITH 4 AND 5
*         DO  FORMAT  1  DSCB'S
*
*********************************************************************
FINEST   ST    R4,NTRKNT           STORE CORRECT VALUE FOR COUNT
NXTF1    NOP   DONXTF1             BR ALWAYS EXCEPT 1ST TIME
         OI    NXTF1+1,X'F0'       SET BR ON
         BAL   R14,HEADING         START NEW PAGE FOR DATA SETS
         MVC   ANXTDSCB,DSTBL      SET START OF DSCB TABLE
DONXTF1  DS    0H                  PROCESS NEXT DSCB
         L     R1,ANXTDSCB         GET ADDRESS OF NEXT DSCB TABLE ENTRY
         L     R10,0(R1)           ADDRESS OF DSCB IS IN
         MVC   DATANAME,DS1DSNAM   BRING IN NAME OF DATA SET
         LA    R1,DS1CREDT         LOAD ADDRESS OF CREATION DATE
         LA    R2,CRDATE           PLACE TO STORE
         BAL   R14,CALEN           GO DO CALENDAR
         LA    R1,DS1EXPDT         PICKUP EXPIRY DATE
         LA    R2,PURGDATE         STORE
         BAL   R14,CALEN           GO DO CALENDAR
         LA    R1,DS1REFD          PICKUP LAST-REFERENCED DATE
         LA    R2,REFDATE          STORE
         BAL   R14,CALEN           GO DO CALENDAR
         ICM   R14,B'0111',0(R1)   TEST IF EVER REFERENCED
         BNZ   *+4+4               YES, USE THAT DATE
         LA    R1,DS1CREDT         NO,COUNT FROM CREATE DATE
         GOSUB STDDAY01            GO CONVERT REFERENCE DATE
         STH   R1,STDREF           SAVE VALUE
         SPACE 1
TYPON    SR    R3,R3
         IC    R3,DS1NOEPV         GET EXTENT NO.
         ST    R3,EXTKNT           STORE EXTENT VALUE FOR CALCULATIONS
         BAL   R14,NEDIT           BRANCH TO THE EDIT
         MVC   EXTENTS,EDITWA+5    PUT IN EXTENTS
SEC2     L     R9,ACCTABLE         NOW READY TO FIND PRIMARY ALLOC
         LH    R3,DS1LSTAR         RELATIVE NUMBER OF TRACKS USED
         CLI   DS1LSTAR+2,X'00'    PARTIAL TRACK USED?
         BE    *+8                 NO. SKIP NEXT INSTN.
         LA    R3,1(0,R3)          ADD 1 FOR PARTIAL TRACK USED
         ST    R3,THISUSED         SAVE THIS VALUE
         BAL   R14,NEDIT           BRANCH TO THE EDIT
         MVC   SPUSED,EDITWA+3     PUT IN SPACE USED
         XC    DSLEN,DSLEN         CLEAR ACCUMULATOR
         LA    R1,DS1EXT1          F1 FIRST EXTENT
         L     R5,EXTKNT           NUMBER OF EXTENTS
         LTR   R5,R5               TEST FOR AN EMPTY DATA SET
         BZ    EXTSS3              JUMP IF EMPTY
         C     R5,=F'3'            CHECK FOR ALL EXTENTS IN F1
         BNH   *+4+4               YES, USE THIS VALUE
         LA    R5,3                SET FOR FIRST 3
         L     R14,EXTKNT          EXTENT COUNT BACK
         SR    R14,R5              REDUCE BY THOSE TO BE DONE NOW
         ST    R14,EXTKNT          SAVE NEW EXTENT COUNT
         BAL   R14,ALLSUM          SUM UP ALLOCATION
         EJECT
EXTSS1   DS    0H                  CHECK FOR FMT 3 DSCB
         L     R5,EXTKNT           EXTENT COUNT BACK
         LTR   R5,R5               CHECK FOR MORE EXTENTS
         BNP   EXTSS3              THRU
         BAL   R14,SRCHDSCB        GET NEW FORMAT 3 DSCB
         CLI   DS3FMTID,C'2'       IS THIS A FORMAT2
         BNE   EXTDO3              BRANCH IF NOT
         BAL   R14,SRCHDSCB        GO GET FORMAT 3
EXTDO3   DS    0H
         LA    R1,DS3EXTNT         START OF GROUP
         L     R5,EXTKNT           EXTENT COUNT BACK
         C     R5,=F'4'            FOUR MORE FIELDS
         BNH   *+4+4               YES, USE THIS VALUE
         LA    R5,4                SET TO 4
         L     R14,EXTKNT          EXTENT COUNT BACK
         SR    R14,R5              REDUCE BY THOSE TO BE DONE NOW
         ST    R14,EXTKNT          SAVE NEW EXTENT COUNT
EXTSS2   BAL   R14,ALLSUM          SUM UP SOME MORE
         L     R5,EXTKNT           LOAD IN COUNT
         LTR   R5,R5               CHECK FOR MORE EXTENTS
         BNP   EXTSS3              THRU
         C     R5,=F'9'            SET TO DO LAST 9
         BNH   *+4+4               YES, USE THIS VALUE
         LA    R5,9                SET TO 9
         L     R14,EXTKNT          EXTENT COUNT BACK
         SR    R14,R5              REDUCE BY THOSE TO BE DONE NOW
         ST    R14,EXTKNT          SAVE NEW EXTENT COUNT
         LA    R1,DS3ADEXT
         BAL   R14,ALLSUM          DO LAST OF THEM
         B     EXTSS1              GO TEST FURTHER EXTENTS
         EJECT
EXTSS3   L     R3,DSLEN            PUT IN OUTPUT AREA
         ST    R3,THISALLC         SAVE FOR LATER
         BAL   R14,NEDIT           PUT OUT TOTAL
         MVC   SPALLOC,EDITWA+3    PUT IN TOTAL ALLOCATED
         SH    R9,H4
         MVC   0(4,R9),=F'0'       INDICATE END OF LIST BY ZERO
         CL    R9,SREND
         BNH   TABLOVF0
         ST    R9,ACCEND
         L     R1,ANXTDSCB         GET ADDRESS OF CURRENT TABLE ENTRY
         L     R10,0(R1)           POINT BACK TO F1
         TM    DS1DSORG,X'02'
         BZ    NOPDS
         TM    PARMSWCH,X'FF'      IF PARM NOT 'PDS' .......
         BZ    NOPDS               .... THEN BYPASS
         RDJFCB  (UTILITY)         READ IN PARMS
         LA    R4,PDSAREA          ESTABLISH ADDRESSABILITY
         USING IEFJFCBN,R4         FOR JFCB
         MVC   JFCBDSNM(44),DS1DSNAM
         MVI   JFCBNVOL,1          ONE VOLUME
         MVC   JFCBVOLS(6),SAVESER  PUT IN VOLUME SERIAL NUMBER
         OI    JFCBIND2,JFCSHARE   SET DISP FOR SHARED USE
         OI    JFCBTSDM,JFCNWRIT   DO NOT UPDATE LAST REFERENCE  *DHC*
         MVC   JFCBCRDT(6),DS1CREDT MOVE IN BOTH DATES
         DROP  R4                  END JFCB ADDRESSABILITY
         SPACE 1
         OPEN  (UTILITY,(INPUT)),TYPE=J
         SR    R2,R2               JUST ZERO FOR TEST
         SR    R3,R3               CLEAR NO. USED REG
         SR    R4,R4               CLEAR TOTAL SIZE REG
         SR    R5,R5               ZEROIZE FOR NO. MEMBS STEP
PDSLOOP  GET   UTILITY,WORK
         CH    R2,WORK             SEE IF BLOCK IS USED
         BZ    PDSLOP              IF ZERO, AVAILABLE
         EJECT
*********************************************************************
*       WORK OUT NUMBER OF MEMBERS IN A PDS
*********************************************************************
MEMBERS  LR    R0,R3               SAVE REGISTER 3
         LA    R3,WORK             LOAD LENGTH
         AH    R3,WORK             ADD LENGTH
         LA    R1,WORK+2           MOVE TO START
TESTNEXT MVC   WBYTE,11(R1)        SAVE LENGTH VARIABLE FIELDS
         NI    WBYTE,X'1F'         ZEROIZE 3 FIRST BITS (USED FLAG)
         IC    R2,WBYTE            LOAD LENGTH
         SLA   R2,1                MULTIPLY BY 2
         LA    R1,12(R2,R1)        POINT R1 TO NEXT MEMBER
         A     R5,=F'1'            INCREMENT MEMBERS COUNT BY 1
         CR    R3,R1               IS IT END OF CURRENT DIR. BLK.?
         BE    ENDMEMB             YES...QUIT LOOP
         B     TESTNEXT            NO....GO PROCESS NEXT ONE
ENDMEMB  LR    R3,R5               MAKE R3 NO. OF MEMBERS
         S     R3,=F'1'            TAKE ONE OFF ..........
*                                 FOR SOME REASON ONE TOO MANY ||
         BAL   R14,NEDIT           GO TO EDIT ROUTINE
         MVC   ISRKP+2,EDITWA+3    PUT NO. IN ISRKP (+A BIT) AREA
         LR    R3,R0               RESTORE REGISTER 3
         SR    R2,R2               ZEROIZE REGISTER 2
*********************************************************************
         A     R3,=F'1'
PDSLOP   A     R4,=F'1'
         B     PDSLOOP             MERRY GO ROUND TILL EOV (DS END)
PDSEND   LA    R1,DRBLUSED
         BAL   R14,NEDIT           BLOCKS USED
         MVC   DRBLUSED,EDITWA+4   PUT IN DIRECTORY BLOCKS USED
         LR    R3,R4
         BAL   R14,NEDIT           TOTAL NUMBER OF BLOCKS
         MVC   DRBLOCKS,EDITWA+4   PUT IN DIRECTORY BLOCKS ALLOC.
CLOSEPDS DS    0H
         CLOSE (UTILITY)           READY FOR NEXT ROUND
         EJECT
********************************************************************
*
*         CHECK DATA SET ORGANIZATION
*
********************************************************************
NOPDS    DS    0H                  WE ENDED PDS PROCESSING
         L     R1,ANXTDSCB         GET NEXT DSCB ADDRESS
         LA    R1,160(R1)          INCREMENT TABLE ENTRY LENGTH
         ST    R1,ANXTDSCB         SAVE THE RESULT
         LA    R5,DSORG
         MVI   0(R5),BLANK         CLEAR AREA
         MVC   1(3,R5),0(R5)       CLEAR AREA
         TM    DS1DSORG,X'FF'      CHECK TO SEE IF NOTHING THERE
         BNZ   QUESTION            IF THERE IS -- BRANCH
         TM    DS1DSORG+1,X'08'    PUT CHECK ON NEXT BIT (VSAM)
         BZ    QUESTION            BRANCH IF NOT
         MVC   0(L'CHARVSAM,R5),CHARVSAM   YES, PUT IT IN REPORT
         B     CHBIT3              AND BYPASS OTHER ORGS
QUESTION DS    0H                  LOOK AT DSORG
         MVC   0(2,R5),=C'??'      IF NOT PUT IN EXCLAMATION MARKS
         TM    DS1SMSFG,DS1PDSE    IS IT A PDSE?
         BZ    CHBITHF             NO, TRY SOMETHING ELSE
         MVC   0(4,R5),=C'PDSE'    YES, SHOW THAT
*        B     CHBIT4              END OF STORY
CHBITHF  TM    DS1SMSFG,DS1PDSEX   IS IT A HFS FILE?
         BZ    CHBIT01             NO, TRY SOMETHING ELSE
         MVC   0(4,R5),=C'HFS '    YES, SHOW THAT
         B     CHBIT4              END OF STORY
CHBIT01  TM    DS1DSORG,X'40'      SEE IF BIT IS THERE
         BZ    CHBIT1              BRANCH IF NOT
         MVC   0(2,R5),=C'PS'      IF SO PUT IN TYPE--SEQUENTIAL
         LA    R5,2(R5)
CHBIT1   TM    DS1DSORG,X'20'      PUT CHECK ON NEXT BIT
         BZ    CHBIT2              BRANCH IF NOT
         MVC   0(2,R5),=C'DA'      IF SO PUT IN TYPE--DIRECT ACCESS
         LA    R5,2(R5)
CHBIT2   TM    DS1DSORG,X'02'      CHECK MORE BITS
         BZ    CHBIT25             BRANCH IF NOT
         MVC   0(2,R5),=C'PO'      IF SO PUT IN TYPE--PARTITIONED
         LA    R5,2(R5)
CHBIT25  TM    DS1DSORG,X'80'      CHECK FOR INDEXED SEQ.
         BZ    CHBIT3              BRANCH IF NOT
         MVC   0(2,R5),=C'IS'      IF SO PUT IN TYPE--ISAM
         LA    R5,2(,R5)
         SR    R3,R3               CLEAR REGISTER 3
         ICM   R3,B'0011',DS1RKP   AS ITS I.S. DO RKP STATS
*************** NOTE:- DS1RKP NOT ON HALF-WORD BOUNDARY, HENCE ICM
         BAL   R14,NEDIT           BRANCH TO NEDIT
         MVC   ISRKP,EDITWA+3      PUT IN RELATIVE KEY POSITION
         IC    R3,DS1KEYL          ALSO DO KEY LENGTH STATS
         BAL   R14,NEDIT           BRANCH TO NEDIT
         MVC   ISKEYLEN,EDITWA+5   PUT IN KEY LENGTH
CHBIT3   TM    DS1DSORG,X'01'      CHECK MORE BITS
         BZ    CHBIT4              BRANCH IF NOT
         MVI   0(R5),C'U'          IF SO PUT IN TYPE--UNMOVABLE
         EJECT
********************************************************************
*
*         CHECK RECORD FORMAT
*
********************************************************************
CHBIT4   LA    R5,RECFM
         MVI   0(R5),BLANK         CLEAR AREA FROM TIME BEFORE
         MVC   1(4,R5),0(R5)
         TM    DS1RECFM,X'FF'      CHECK TO SEE IF NOTHING THERE
         BNZ   CHBITNO             IF THERE IS -- BRANCH
         MVC   0(1,R5),=C'?'       IF NOT PUT IN EXCLAMATION MARK
         LA    R5,12(R5)
CHBITNO  TM    DS1RECFM,X'C0'
         BZ    CHBIT8              NOTHING AT ALL - GO OUT
         BM    CHBIT5
         MVI   0(R5),C'U'          PUT IN UNDEFINED
CHBIT6   LA    R5,1(R5)
         B     CHBIT8
CHBIT5   TM    DS1RECFM,X'80'      CHECK FIXED
         BZ    CHBIT7
         MVI   0(R5),C'F'          FIXED FORMAT
         B     CHBIT6
CHBIT7   MVI   0(R5),C'V'          VARIABLE FORMAT
         B     CHBIT6
CHBIT8   TM    DS1RECFM,X'20'      CHECK TRACK OVERFLOW
         BZ    CHBIT9
         MVI   0(R5),C'T'          TRACK OVERFLOW
         LA    R5,1(R5)
CHBIT9   TM    DS1RECFM,X'10'      CHECK
         BZ    CHBIT10
         MVI   0(R5),C'B'          BLOCKED
         LA    R5,1(R5)
CHBIT10  TM    DS1RECFM,X'08'      CHECK NEXT BIT
         BZ    CHBIT11
         MVI   0(R5),C'S'          STANDARD BLOCKS
         LA    R5,1(R5)
CHBIT11  TM    DS1RECFM,X'04'
         BZ    CHBIT12
         MVI   0(R5),C'A'          ASA CONTROL CHARACTER
         B     CHBIT13
CHBIT14  LH    R3,DS1SCALO+2       GET 2ND ALLOCATION
         MH    R3,SAVEF4+64
         B     CHBIT15             GO IN
CHBIT12  TM    DS1RECFM,X'02'
         BZ    CHBIT13
         MVI   0(R5),C'M'          MACHINE CONTROL CHARACTER
         EJECT
********************************************************************
*
*         CHECK BLOCKSIZE, LRECL, ALLOC UNITS ETC
*
********************************************************************
CHBIT13  LH    R3,DS1BLKL          BRING IN BLOCK SIZE
         BAL   R14,NEDIT
         MVC   BLKSIZE,EDITWA+3    PUT IN BLOCKSIZE
         LH    R3,DS1LRECL         RECORD LENGTH
         BAL   R14,NEDIT           SYMBOLS IN
         MVC   LRECL,EDITWA+3      PUT IN LRECL
         SPACE 1
         TM    DS1DSIND,DS1DSCHA   TEST DATA-SET-CHANGED BIT
         BNO   SEAG001             NOT ON, PASS
         MVI   DSCHA,C'*'          SHOW DATA SET CHANGED
         SPACE 1
SEAG001  DS    0H                  JUST A LABEL
         MVC   ALUNIT,=C' CYL '
         TM    DS1SCALO,X'C0'      CHECK FOR CYLINDER
         BO    CHBIT14             IT IS IF JUMPS
         MVC   ALUNIT,=C' TRK '
         TM    DS1SCALO,X'40'      TEST FOR BLOCK ALLOCATION
         BNO   TRKS                BRANCH IF NOT BLOCKS
         MVC   ALUNIT,=C' BLK '
         EJECT
*********************************************************************
*
*         ROUTINE TO CALCULATE BLOCKS PER TRACK
*
*********************************************************************
         SPACE 2
STARRING DS    0H                  INVOKE STAR SERVICES
         SR    R0,R0               ASSUME NO ANSWER
         SR    R1,R1               CLEAR REG.
         IC    R1,DS1KEYL          GET KEY LENGTH
         STH   R1,RKDD             SAVE R=0 AND K=KEYLEN
         MVI   RKDD,1              SET R=1 FOR TRACK CAPACITY
         LH    R1,DS1BLKL          NOW GET BLOCKSIZE
         LTR   R1,R1               TEST FOR VALUE
         BZ    STARROUT            EXIT IF NONE
         STH   R1,RKDD+2           SAVE DD=BLOCKSIZE
         SPACE 1
         TRKCALC FUNCTN=TRKCAP,UCB=UCBXREF,                            X
               RKDD=RKDD,                                              X
               REGSAVE=YES,MF=I    STAR MACRO
         SPACE 1
STARROUT DS    0H                  EXIT FROM STAR ROUTINE
         LH    R3,DS1SCALO+2       BLKS REQUESTED/BLKS PER TRK=TRKS
         LTR   R3,R3               TEST FOR VALUE
         BZ    CHBIT15             SKIP IF ZERO
         LTR   R14,R0              GET BLOCKS PER TRACK
         BZ    CHBIT15             SKIP IF ZERO
         XR    R2,R2
         DR    R2,R14
         LTR   R2,R2               SEE IF REMAINDER
         BZ    CHBIT15
         LA    R3,1(R3)            BUMP IF REMAINDER
         B     CHBIT15
         EJECT ,
TRKS     LH    R3,DS1SCALO+2       GET SECONDARY  BY TRACKS
CHBIT15  DS    0H                  COME BACK
         BAL   R14,NEDIT
         MVC   ALL2ND,EDITWA+4     PUT IN 2ND ALLOCATION
         MVI   OUTBIN,C'0'         DOUBLE SPACE
         CLC   CHARVSAM,DSORG      IS IT A VSAM DATASET?
         BNE   SKIP1               NO, CARRY ON
         GOSUB VVDSDATA            YES, GO FIND OUT MORE ABOUT IT
         B     SKIP1BIS            GO BUILD SUMMARY RECORD
         SPACE 1
SKIP1    DS    0H                  PROCESS NON-VSAM DATASETS
         TM    GENOPT,GENSMS       IS THIS AN SMS VOLUME?
         BNO   SKIP1BIS            NO, SO NO NVSAM VVDS RECORDS
         GOSUB VVDSDATA            YES, GO FIND OUT MORE ABOUT DATASET
         SPACE 1
SKIP1BIS DS    0H                  CREATE SUMMARY RECORD
         GOSUB SUMMARY             GO CREATE SUMMARY RECORD
         PUT   PRINT,OUTBIN        PRINT LINE
         AP    LINEKNT,=P'1'       BUMP LINE COUNT
PRNTCOM  BAL   R14,PRNTEXTS        GO PRINT AND SORT ALL EXTENTS
         MVI   DATANAME,BLANK      CLEAR PRINT LINE
         MVC   DATANAME+1(132),DATANAME  CLEAR PRINT LINE
         LA    R2,SRSPACE          POINT TO SUMMARY RECORD
         MVI   0(R2),C' '          CLEAR AREA
         MVC   1(SRRECLEN-1,R2),0(R2)  ..TO BLANKS
         BAL   R14,LINETEST        TEST PAGE EJECT
DOF1S    L     R4,NTRKNT           LOAD COUNT OF FMT 1'S
         BCT   R4,FINEST           GET NEXT F1
         NI    NXTF1+1,X'0F'       SET BR OFF, NO MORE F1'S
         EJECT
*********************************************************************
*
*         START OF ALLOCATION MAP FOR SYSOUT
*
*********************************************************************
MAPZ     OI    HEADER+1,X'F0'      SET TO NOP COLUMN HEADING
         L     R5,SRTKNT           LOAD UP NO OF EXTENTS
         BCTR  R5,0                CORRECT COUNT
         L     R9,SRTBL            START OF SORT BIN
         XC    CHECKER,CHECKER     ZERO EXTENT CHECKER
MAP3     BAL   R14,SYSHEAD         TITLE
         MVI   SOUTBIN,C' '        CLEAR SYSOUT PRINT LINE
         MVC   SOUTBIN+1(132),SOUTBIN
MAP2     L     R10,0(R9)           BRING IN ADDRESS STORED IN TABLE
         L     R2,44(R10)          BRING IN SIZE
         BAL   R14,LENGTH          LENGTH
         BAL   R14,NEDIT           EDIT IT
         MVC   LNGTH,EDITWA+3      PUT IN LENGTH OF EXTENT
         SR    R3,R3               CLEAR WORK REG
         ICM   R3,B'0011',HOLD     FIRST TRACK
         BAL   R14,NEDIT           EDIT IT
         MVC   STRTT,EDITWA+3      PUT IN START TRACK
         BCTR  R3,0                SUBTRACT 1
         C     R3,CHECKER          IS IT EQUAL TO END OF LAST XTENT
         BNE   ERRFLAG             BRANCH IF NOT
         C     R5,=F'1'            IS THIS LAST EXTENT
         BNE   OK                  BRANCH IF NOT
         ICM   R3,B'0011',SAVEF4+62  NUMBER OF CYLINDERS
         MH    R3,SAVEF4+64        TIMES NO. OF TRACKS/CYLINDER
         LH    R14,NOSPARES        GET NO. SPARES
         SR    R3,R14              SUBTRACT FROM TOTAL NO. TRACKS
         BCTR  R3,0                MINUS 1
         ICM   R14,B'0011',HOLD+2  GET EXTENT END
         CR    R3,R14              COMPARE WITH DEVICE END
         BE    OK
ERRFLAG  MVC   STRTT-2(2),=C'**'   FLAG IT
OK       DS    0H
         ICM   R3,B'0011',HOLD+2   LAST TRACK
         BAL   R14,NEDIT
         MVC   LSTT,EDITWA+3       PUT IN LAST TRACK NO.
         ST    R3,CHECKER          SAVE TO CHECK AGAINST NEXT XTENT
         SR    R3,R3
         IC    R3,48(R10)          EXTENT VALUE
         BAL   R14,NEDIT
         MVC   EXTT,EDITWA+5       PUT IN EXTENT
         MVC   SHLINE,0(R10)       BRING IN DATA SET NAME
         BAL   R14,SLNE1OUT        PRINT LINE
         LA    R9,56(R9)
         BCT   R5,MAP2             FALL THRU ON LAST
         EJECT
*********************************************************************
*
************  THROUGH WITH THIS VOLUME, PREPARE TO GO TO NEXT
*
*********************************************************************
         SPACE 1
MAPPED   DS    0H                  FINISHED MAPPING ONE VOLUME
         L     R3,ACCEND           CALC FREE SPACE
         S     R3,SREND            IN TABLE
         SRL   R3,11               DIVIDE BY 2K
         SLL   R3,1                MULT BY 2
         BAL   R14,NEDIT           PUT IN MESSAGE
         MVC   SIZEF,EDITWA+4      PUT IN SIZE
         PUT   SYSOUT,FREESPC      WRITE IT
         XC    STDREF,STDREF       CLEAR REFERENCE DAY COUNT
         TM    VSAMSW,DDPRES+DDOPEN WAS ACB PROCESSED?
         BNO   DISKLOOP            NO, GO ROUND FOR NEXT DISK
         SPACE 1
         MVC   VVENOW,VVETBL       RESET CURRENT POINTER TO START
         MODCB RPL=VVDSRPL,OPTCD=(ADR,SEQ,LOC) SWITCH TO SEQUENTIAL
         LTR   R15,R15             MODCB OK ?
         BNZ   MODCBERR            NO ==> ABEND
         SPACE 1
         XC    VVDSRBA,VVDSRBA     RBA = 0
         POINT RPL=VVDSRPL         RESET FILE
*        CLOSE (VVDSACB)
*        OPEN  (VVDSACB)
*        WTO   'MAPDISK9 ==> VVDS END OF FILE - CALL SYSTEMS'
         SPACE 1
         MODCB EXLST=VVDSEXLS,EODAD=VVDSEOF RESTORE EOD ADDRESS
         LTR   R15,R15             MODCB OK ?
         BNZ   MODCBERR            NO ==> ABEND
         SPACE 1
         CLOSE (VVDSACB)           CLOSE THE VVDS
         NI    VSAMSW,FF-DDOPEN    TURN OFF FLAG
         LA    R4,S99AREA          SET UP SVC 99 PARM AREA BASE REG
         USING S99RB,R4            ESTABLISH ADDRESSABILITY
         MVI   S99VERB,S99VRBUN    INDICATE UNALLOCATION TO BE DONE
         LA    R1,S99PARM          LOAD ADDRESS OF SVC 99 PARM AREA
         DYNALLOC  ,               PERFORM DYNAMIC UNALLOCATION
         LTR   R15,R15             DYNALLOC OK ?
         BNZ   DYNAERR             NO, SHOW ERROR
         DROP  R4                  END DYNALLOC ADDRESSABILITY
         NI    VSAMSW,FF-DDPRES    TURN OFF FLAG
         B     DISKLOOP            GO LOOK FOR NEXT VOL
         EJECT
*********************************************************************
*
*        ALL VOLUMES PROCESSED. TIDY UP AND LEAVE.
*
*********************************************************************
         SPACE 1
ALLDONE  PUT   SYSOUT,=CL133'- END OF UTILITY - DISK PACKS ARE MAPPED'
         CLOSE (PRINT)
         CLOSE (SYSOUT)
         LA    R2,SYSUT2SW         POINT AT MDCB LIST
         USING MDCB,R2             ESTABLISH ITS ADDRESSSABILITY
         GOSUB CLOSE               GO CLOSE IT
         DROP  R2                  END MDCB ADDRESSABILITY
         SPACE 1
RETURN   DS    0H
*        GETAUTH TYPE=OFF          UN-AUTHORISATION
*        ABEND 1111,DUMP           ===>TESTING<===
         L     13,4(13)            RESTORE REG 13 TO CALLER'S SAVE
         LM    14,12,12(13)        RESTORE REGS
         SR    15,15               SET RETURN CODE OF ZERO 08/11/71
         BR    14                  RETURN TO CALLER
         EJECT ,
*        *********************************
*        *     OBTAIN INFORMATION FROM   *
*        *           V  V  D  S          *
*        *********************************
         SPACE 1
VVDSDATA DS    0H
         SUBIN SAVE=(R3,R4,R5,R7,R9)  SUBROUTINE ENTRY
         TM    VSAMSW,DDPRES+DDOPEN CAN WE DO IT?
         BNO   VSAMDONE            NO, GO OUT AGAIN
         L     R9,VVENOW           LOAD TABLE POINTER
         USING VVETABLE,R9         ESTABLISH ADDRESSABILITY
         SPACE 1
VSAM010  DS    0H                  SEARCH FOR VVE ENTRY
         CLC   DS1DSNAM,VVENAME    RIGHT DSNAME?
         BE    VSAM020             YES, PROCESS IT
         BL    VSAMDONE            NAME NOT IN TABLE
         LA    R9,VVETABL(R9)      INCREMENT TABLE POINTER
         C     R9,DSTBL            RAN OFF END OF VVE TABLE?
         BL    VSAM010             NO, TRY NEXT ENTRY
         B     VSAMDONE            NAME NOT IN TABLE
         SPACE 1
VSAM020  DS    0H
         ST    R9,VVENOW           SAVE TABLE POINTER
         MVC   VVDSRBA,VVERBA      INSERT RBA OF VVR
         SPACE 1
VSAM025  DS    0H
         GOSUB VVDSGET             GO READ THE VVDS ENTRY
         L     R7,VVDSAREA         GET RECORD POINTER
         USING VVR,R7              ESTABLISH HEADER ADDRESSABILITY
         CLI   VVRTYPE,C'N'        NONVSAM VVR ?
         BE    NVSAM30             YES, GO PROCESS
         CLI   VVRTYPE,C'Z'        PRIMARY VVR ?
         BNE   VSAM028             NO ==> GET NEXT RECORD
         SPACE 1
VSAM027  DS    0H
         LH    R5,VVRLEN           GET RECORD LENGTH
         SR    R4,R4               CLEAR A REGISTER
         ICM   R4,B'0001',VVRCMPNL  GET LENGTH OF NAME
         BCTR  R4,0                REDUCE FOR LENGTH FIELD
         BCTR  R4,0                REDUCE FOR EXECUTE
         EX    R4,VSAMCOMP         EXECUTE COMPARE
         BE    VSAM030             RICHTIGER SATZ
         SPACE 1
VSAM028  DS    0H
         MODCB RPL=VVDSRPL,OPTCD=(ADR,SEQ,LOC)
         LTR   R15,R15             MODCB OK ?
         BNZ   MODCBERR            NO ==> ABEND
         SPACE 1
         B     VSAM025             TRY NEXT RECORD
         EJECT
VSAM030  DS    0H
         MODCB RPL=VVDSRPL,OPTCD=(ADR,DIR,LOC,NSP)
         LTR   R15,R15             MODCB OK ?
         BNZ   MODCBERR            NO ==> ABEND
         SPACE 1
         OI    VVDSSWCH,VVDSNCL    SET SWITCH FOR HEADER NAME CELL
         MVI   DSORG+3,C'I'        ASSUME IT IS INDEX COMPONENT
         TM    VVRFLAG,VVRCOMPT    IS IT AN INDEX?
         BO    *+4+4               YES, WE WERE RIGHT
         MVI   DSORG+3,C'D'        NO, FLAG AS DATA
         LA    R7,2(R7)            SKIP VVR RECORD LENGTH FIELD
         SH    R5,0(R7)            SUBTRACT FROM RECORD LENGTH
         AH    R7,0(R7)            POINT TO NEXT CELL
         SPACE 1
         CLI   2(R7),X'21'         DATASET INFORMATION CELL ?
         BNE   VSAM040             NO ==> GO ON
         LR    R1,R7               POINT TO NEW CELL
         USING TYPE@21,R1          ESTABLISH ADDRESSABILITY
         OI    VVDSSWCH,VVDST21    SET SWITCH FOR SUBTYPE X'21'
         SR    R3,R3               CLEAR LENGTH REGISTER
         CLC   0(2,R7),=XL2'0060'  TEST CELL LENGTH
         BNH   VSAM30C             SKIP IF NO SMS DATA
         LA    R4,VVR21SCL         POINT TO STORAGE CLASS LENGTH
         ICM   R3,B'0011',0(R4)    GET LENGTH OF STORAGE CLASS
         BZ    VSAM30A             SKIP IT IF NONE
         BCTR  R3,0                DECREMENT FOR EXECUTE
         LA    R2,SRSPACE          POINT TO RECORD AREA
         USING SRRECORD,R2         ESTABLISH ADDRESSABILITY
         LA    R15,SRSTOGCL        POINT TO SUMMARY FIELD
         EX    R3,NVSMOVE          COPY CLASS NAME TO SUMMARY RECORD
         LA    R3,1(,R3)           RESTORE LENGTH
         SPACE 1
VSAM30A  DS    0H                  PROCESS NEXT SMS FIELD
         LA    R4,2(R3,R4)         UPDATE INPUT POINTER
         ICM   R3,B'0011',0(R4)    GET LENGTH OF DATA CLASS
         BZ    VSAM30B             SKIP IT IF NONE
         BCTR  R3,0                DECREMENT FOR EXECUTE
         LA    R15,SRDATACL        POINT TO SUMMARY FIELD
         EX    R3,NVSMOVE          COPY CLASS NAME TO SUMMARY RECORD
         LA    R3,1(,R3)           RESTORE LENGTH
         SPACE 1
VSAM30B  DS    0H                  PROCESS NEXT SMS FIELD
         LA    R4,2(R3,R4)         UPDATE INPUT POINTER
         ICM   R3,B'0011',0(R4)    GET LENGTH OF MANAGEMENT CLASS
         BZ    VSAM30C             SKIP IT IF NONE
         BCTR  R3,0                DECREMENT FOR EXECUTE
         LA    R15,SRMGMTCL        POINT TO SUMMARY FIELD
         EX    R3,NVSMOVE          COPY CLASS NAME TO SUMMARY RECORD
         SPACE 1
VSAM30C  DS    0H                  PROCESS NEXT SMS FIELD
         SH    R5,0(R7)            SUBTRACT FROM RECORD LENGTH
         LTR   R5,R5               END OF RECORD ?
         BZ    VSAM060
         AH    R7,0(R7)            POINT TO NEXT CELL
         DROP  R2                  END SUMMARY ADDRESSING
         SPACE 1
VSAM040  DS    0H
         CLI   2(R7),X'60'         AMDSB CELL ?
         BNE   VSAM050             NO ==> GO ON
         LR    R2,R7               GET AMDSB CELL ADDRESS
         USING TYPE@60,R2          ESTABLISH ADDRESSABILITY
         SPACE 1
         OI    VVDSSWCH,VVDST60    SET SWITCH FOR SUBTYPE X'60'
         SH    R5,0(R7)            SUBTRACT FROM RECORD LENGTH
         LTR   R5,R5               END OF RECORD ?
         BZ    VSAM060             YES ==> TRY Q TYPE
         AH    R7,0(R7)            POINT TO NEXT CELL
         SPACE 1
VSAM050  DS    0H
         CLI   2(R7),X'23'         VOLUME  INFORMATION CELL ?
         BNE   VSAM060             NO ==> GO ON
         LR    R4,R7               GET VOLUME CELL ADDRESS
         USING TYPE@23,R4          ESTABLISH ADDRESSABILITY
         SPACE 1
         OI    VVDSSWCH,VVDST23    SET SWITCH FOR SUBTYPE X'23'
         B     VSAM060             GO PROCESS VSAM COMPONENTS
         SPACE 2
VSAMCOMP CLC   VVENAME(*-*),VVRCMPNM  COMPARE NAMES         *EXECUTE*
         EJECT
*********************************************************************
*
*        LIST ATTRIBUTES OF VSAM COMPONENT, NOW THAT WE KNOW THEM
*
*********************************************************************
         SPACE 1
VSAM060  DS    0H
         TM    VVDSSWCH,VVDST21+VVDST23+VVDST60 ENOUGH INFORMATION?
*        BNO   VSAM028                 NO ==> TRY NEXT RECORD
         BNO   VSAM099             NO, CONTINUED DATASET. FORGET
         LA    R5,RECFM
         MVI   0(R5),BLANK         CLEAR AREA FROM TIME BEFORE
         MVC   1(4,R5),0(R5)
         MVI   RECFM,C'V'          DEFAULT VARIABLE RECORDS
         ICM   R3,B'1111',VVRAMLCL GET MAX RECORD SIZE
         BAL   R14,NEDIT           MAKE LRECL PRINTABLE
         MVC   LRECL,EDITWA+3      AND PRINT IT
         CLC   VVRAMLCL,VVRLRECL   MAX SAME AS AVERAGE?
         BNE   *+4+4               NO, LEAVE AS RECFM=V
         MVI   RECFM,C'F'          YES, FIXED LENGTH RECORDS
         TM    VVRAMATR,VVRAMSPN   SPANNED RECORDS?
         BNO   *+4+4               NO, LEAVE BLANK
         MVI   RECFM+1,C'S'        YES, SHOW IT
         LH    R3,VVRAMNXT         GET NUMBER OF EXTENTS
         BAL   R14,NEDIT           MAKE PRINTABLE
         MVC   EXTENTS,EDITWA+5    AND PRINT IT
         ICM   R3,B'1111',VVRAMCIV GET CI SIZE
         BAL   R14,NEDIT           MAKE CISIZE PRINTABLE
         MVC   BLKSIZE,EDITWA+3    PRINT IT AS BLOCKSIZE
         LR    R15,R3              COPY CI SIZE
         SR    R14,R14             CLEAR EVEN REGISTER
         LH    R5,VVRAMCCA         GET #CI'S PER CA
         MR    R14,R5              CALCULATE THEIR PRODUCT
         LR    R5,R15              AND REMEMBER IT
         SR    R14,R14             CLEAR EVEN REGISTER
         ICM   R15,B'1111',VVRDSHU GET HIGH USED RBA
         DR    R14,R5              GET NUMBER OF CA'S IN DATASET
         LTR   R14,R14             ANY REMAINDER?
         BZ    *+4+4               NO, DO NOT ROUND
         LA    R15,1(R15)          YES, SO ROUND UP TRACKS
         LR    R0,R15              SAVE NUMBER OF CA'S
         SR    R14,R14             CLEAR EVEN REGISTER
         SR    R5,R5               CLEAR REGISTER
         ICM   R5,B'0011',VVRTRKAU GET TRACKS PER CA
         MR    R14,R5              CALCULATE SPACE USED
         LR    R3,R15              GET SPACE USED
         ST    R3,THISUSED         SAVE THIS VALUE
         BAL   R14,NEDIT           MAKE PRINTABLE
         MVC   SPUSED,EDITWA+3     AND PRINT IT
         IC    R15,VVRATTR2        PICK UP SHAREOPTIONS
         LA    R3,VVRA2REG         SET REGION SHARING MASK
         NR    R3,R15              STRIP OUT REGION VALUE
         SRL   R3,6                SHIFT DOWN
         LA    R3,1(,R3)           ADD ONE TO DECODE
         BAL   R14,NEDIT           MAKE PRINTABLE
         MVC   CTREGSHR,EDITWA+7   COPY TO SAVE AREA
         LA    R3,VVRA2SYS         SET REGION SHARING MASK
         NR    R3,R15              STRIP OUT REGION VALUE
         SRL   R3,4                SHIFT DOWN
         LA    R3,1(,R3)           ADD ONE TO DECODE
         BAL   R14,NEDIT           MAKE PRINTABLE
         MVC   CTSYSSHR,EDITWA+7   COPY TO SAVE AREA
         SPACE 1
         DROP  R1                  END DATASET CELL ADDRESSING
         DROP  R4                  END VOLUME CELL ADDRESSING
         EJECT ,
         ICM   R1,B'1111',VVRAMCAS GET NO. CA SPLITS
         GOSUB PERCENT0            GO CALCULATE PERCENTAGE
         LR    R3,R1               GET THE ANSWER
         BAL   R14,NEDIT           MAKE IT PRINTABLE
         MVC   VSCASPLT,EDITWA+5   COPY TO PRINT LINE
         MH    R0,VVRAMCCA         CALCULATE NO. CI'S
         ICM   R1,B'1111',VVRAMCIS GET NO. CI SPLITS
         GOSUB PERCENT0            GO CALCULATE PERCENTAGE
         LR    R3,R1               GET THE ANSWER
         BAL   R14,NEDIT           MAKE IT PRINTABLE
         MVC   VSCISPLT,EDITWA+5   COPY TO PRINT LINE
         SPACE 1
         LA    R1,VVRAMSTS         POINT AT TIMESTAMP
         GOSUB TODDATE             GO CONVERT IT
         CLC   REFDATE,TODYY       COMPARE VVDS AND VTOC DATES
         BNL   VSAM061             USE VTOC IF VALID
         MVC   REFDATE,TODYY       ELSE USE VVDS DATE
         LA    R1,TODDSCB          POINT TO BINARY DATE
         GOSUB STDDAY01            CONVERT TO STANDARD DAY NO.
         STH   R1,STDREF           SAVE DAY NO. OF REFERENCE
VSAM061  DS    0H                  PROCESS KEY INFORMATION
         TM    VVRAMATR,VVRAMDST   TEST FOR KSDS
         BNO   VSAM099             NO, SKIP KEY DETAILS
         SPACE 1
         SR    R3,R3               CLEAR WORK REGISTER
         ICM   R3,B'0011',VVRAMRKP GET KEY POSITION
         BAL   R14,NEDIT           MAKE PRINTABLE
         MVC   ISRKP,EDITWA+3      AND PRINT IT
         ICM   R3,B'0011',VVRAMKLN GET KEY LENGTH
         BAL   R14,NEDIT           MAKE PRINTABLE
         MVC   ISKEYLEN,EDITWA+5   AND PRINT IT
         B     VSAM099             GO TO RETURN
         SPACE 1
         DROP  R2                  END AMDSB CELL ADDRESSING
         EJECT
*********************************************************************
*
*        LIST SMS ATTRIBUTES OF NONVSAM COMPONENT
*
*********************************************************************
         SPACE 1
NVSAM30  DS    0H                  PROCESS SMS NONVSAM VVR
         DROP  R7                  END 'Z' RECORD ADDRESSABILITY
         USING TYPE@N,R7           ESTABLISH NONVSAM ADDRESSABILITY
         LH    R5,VVRNLEN          GET RECORD LENGTH
         SR    R3,R3               CLEAR LENGTH REGISTER
         ICM   R3,B'0011',VVRNENTL GET LENGTH OF ENTRY NAME
         BCTR  R3,0                DECREMENT FOR EXECUTE
         EX    R3,NVSCOMP          EXECUTE COMPARE
*        BNE   VSAM028             WRONG, GO GET NEXT RECORD
         MODCB RPL=VVDSRPL,OPTCD=(ADR,DIR,LOC,NSP)
         LA    R7,2(R7)            SKIP VVR RECORD LENGTH FIELD
         SH    R5,0(R7)            SUBTRACT FROM RECORD LENGTH
         BZ    NVSAM39             SKIP IT IF NOTHING LEFT
         AH    R7,0(R7)            POINT TO NEXT CELL
         SPACE 1
         CLI   2(R7),X'22'         SMS DATA  CELL ?
         BNE   NVSAM39             NO ==> GO ON
         OI    VVDSSWCH,VVDST22    SET SWITCH FOR SUBTYPE X'22'
         LR    R1,R7               POINT TO NEW CELL
         USING TYPE@22,R1          ESTABLISH ADDRESSABILITY
         LA    R2,SRSPACE          POINT TO RECORD AREA
         USING SRRECORD,R2         ESTABLISH ADDRESSABILITY
         LA    R4,VVRSMSCL         POINT TO STORAGE CLASS LENGTH
         SR    R3,R3               CLEAR LENGTH REGISTER
         ICM   R3,B'0011',0(R4)    GET LENGTH OF STORAGE CLASS
         BZ    NVSAM32             SKIP IT IF NONE
         BCTR  R3,0                DECREMENT FOR EXECUTE
         LA    R15,SRSTOGCL        POINT TO SUMMARY FIELD
         EX    R3,NVSMOVE          COPY CLASS NAME TO SUMMARY RECORD
         LA    R3,1(,R3)           RESTORE LENGTH
         SPACE 1
NVSAM32  DS    0H                  PROCESS NEXT SMS FIELD
         LA    R4,2(R3,R4)         UPDATE INPUT POINTER
         ICM   R3,B'0011',0(R4)    GET LENGTH OF DATA CLASS
         BZ    NVSAM34             SKIP IT IF NONE
         BCTR  R3,0                DECREMENT FOR EXECUTE
         LA    R15,SRDATACL        POINT TO SUMMARY FIELD
         EX    R3,NVSMOVE          COPY CLASS NAME TO SUMMARY RECORD
         LA    R3,1(,R3)           RESTORE LENGTH
         SPACE 1
NVSAM34  DS    0H                  PROCESS NEXT SMS FIELD
         LA    R4,2(R3,R4)         UPDATE INPUT POINTER
         ICM   R3,B'0011',0(R4)    GET LENGTH OF MANAGEMENT CLASS
         BZ    NVSAM39             SKIP IT IF NONE
         BCTR  R3,0                DECREMENT FOR EXECUTE
         LA    R15,SRMGMTCL        POINT TO SUMMARY FIELD
         EX    R3,NVSMOVE          COPY CLASS NAME TO SUMMARY RECORD
         SPACE 1
NVSAM39  DS    0H                  PROCESS NEXT SMS FIELD
*        DC    A(0)                ABEND S0C1                  *DEBUG*
         B     VSAM099             GO TO RETURN
         SPACE 2
NVSCOMP  CLC   VVENAME(*-*),VVRNENTN  COMPARE NAMES           *EXECUTE*
NVSMOVE  MVC   0(*-*,R15),2(R4)    GENERIC MOVE INSTRUCTION   *EXECUTE*
         SPACE 2
         DROP  R2                  END SUMMARY RECORD ADDRESSABILITY
         EJECT
VSAMEOF  DS    0H
         XC    VVDSRBA,VVDSRBA     RBA = 0
         POINT RPL=VVDSRPL
*        CLOSE (VVDSACB)
*        OPEN  (VVDSACB)
*        WTO   'LISTICF ==> VVDS END OF FILE - CALL SYSTEMS'
         B     VSAM099             GO TO RETURN
         SPACE 1
VSAM099  DS    0H
         MVI   VVDSSWCH,X'00'      RESET SWITCH
         SPACE 1
VSAMDONE DS    0H                  VSAM DATASET PROCESSING FINISHED
         SUBOUT  RESTORE=(R3,R4,R5,R7,R9)  SUBROUTINE EXIT
         SPACE 1
         DROP  R7                  END HEADER CELL ADDRESSING
         EJECT
*********************************************************************
*
*         START OF ALLOCATION MAP FOR SYSPRINT EXTENTS
*
*********************************************************************
ABTOREL  MVC   HOLD(8),2(R1)       MOVE FOR BOUNDRIES
         SR    R2,R2               CLEAR WORK REG
         ICM   R2,B'0011',HOLD     CC
         MH    R2,SAVEF4+64
         SR    R3,R3               CLEAR WORK REG
         ICM   R3,B'0011',HOLD+2   HH
         ALR   R2,R3
ABTOREL1 DS    0H
         SR    R3,R3               CLEAR WORK REG
         ICM   R3,B'0011',HOLD+4   CC
         MH    R3,SAVEF4+64
         AH    R3,HOLD+6           HH
         SLL   R2,16               MOVE TO TOP
         ALR   R2,R3               PUT TOGETHER
         BR    R14                 EXIT
         SPACE 2
ALLSUM   ST    R14,HOLD+8          TAKE CARE OF FETCHING ALLOCATIONS
ALLSUM1  BAL   R14,ABTOREL         CONVERT TO RELATIVE TRACKS
         SH    R9,H4
         ST    R2,0(R9)            STORE IN TABLE
         BAL   R14,LENGTH          GET LENGTH IN R3
         L     R14,DSLEN           GET CURRENT ACCUMULATOR
         AR    R14,R3              TOTAL UP ALL LENGTHS FOR DS
         ST    R14,DSLEN           SAVE UPDATED ACCUMULATOR
         LA    R1,10(R1)           SET FOR NEXT
         BCT   R5,ALLSUM1          LOOP
         L     R14,HOLD+8          RESTORE
         BR    R14                 BACK OUT
         EJECT
**********************************************************************
*        ROUTINE TO CONVERT PACKED DATESTAMP TO CHARACTER YYDDD      *
**********************************************************************
         SPACE 1
CALEN    SR    R3,R3               COMPUTE YEARS AND DAYS
         IC    R3,0(R1)            PICK UP YEAR
         CVD   R3,WORK
         UNPK  0(2,R2),WORK+6(2)   YEAR
         OI    1(R2),X'F0'         ZONE
         MVC   WORK(2),1(R1)       FETCH LAST TWO BYTES
         LH    R3,WORK
         CVD   R3,WORK             TO DECIMAL
         UNPK  2(3,R2),WORK+6(2)   DAYS
         OI    4(R2),X'F0'         ZONE
         BR    R14                 RETURN
         SPACE 2
**********************************************************************
*        DATA EDIT ROUTINES                                          *
**********************************************************************
         SPACE 1
NEDIT    DS    0H                  EDIT ROUTINE
         CVD   R3,WORK             CONVERT TO PACKED
         MVC   EDITWA,PATTERN      MOVE PATTERN
         ED    EDITWA,WORK+4       EDIT
         BR    R14                 RETURN
         SPACE 2
FULLEDIT ST    R14,HOLD+4          SAVE RETURN
         BAL   R14,NEDIT
         MVC   ZEXT,EDITWA+5       PUT IN EXTENT
         BAL   R14,LENGTH          DETERMINE LENGTH
         BAL   R14,NEDIT
         MVC   ZLNGTH,EDITWA+3     PUT IN LENGTH
         LH    R3,HOLD
         BAL   R14,NEDIT
         MVC   ZFIRST,EDITWA+3     PUT IN FIRST TRACK
         LH    R3,HOLD+2
         BAL   R14,NEDIT
         MVC   ZLAST,EDITWA+3      PUT IN LAST TRACK
         L     R14,HOLD+4          RESTORE RETURN ADDRESS
         BR    R14
         SPACE 2
LENGTH   SUBIN SAVE=(R4)           LENGTH CALCULATIONP
         ST    R2,HOLD             SAVE VALUE FOR TEMP
         SR    R3,R3               CLEAR WORK REG
         ICM   R3,B'0011',HOLD+2   BRING IN LAST POSITION
         A     R3,=F'1'            UPDATE
         SR    R4,R4               CLEAR WORK REG
         ICM   R4,B'0011',HOLD     BRING IN FIRST POSITION
         SR    R3,R4               TAKE OFF START TRACK VALUE
         SUBOUT  RESTORE=(R4),RETURN=(14)  GO BACK
         EJECT
*********************************************************************
*         PAGE COUNT PROCESSING FOR SYSPRINT
*********************************************************************
LINE0OUT MVI   SWIT,X'00'          SET FOR NO PAGE TEST
LINE1OUT ST    R14,HOLD+16         SAVE RETURN
         TM    GENOPT,X'20'        NEW PAGE?
         BZ    NOFORM              NO
         BAL   R14,HEADING         YES
         NI    GENOPT,X'DF'        RESET SWITCH
NOFORM   PUT   PRINT,EXTOUT
         MVC   EXTOUT+1(132),EXTOUT  CLEAR LINE
         TS    SWIT                DO PAGE TEST?
         BC    8,LINEOUT           NO
LINE2OUT CP    LINEKNT,=P'56'      END OF PAGE?
         BL    LINEOUT             NO
         OI    GENOPT,X'20'        YES SET FLAG
LINEOUT  AP    LINEKNT,=P'1'       BUMP LINE COUNT
         L     R14,HOLD+16
         BR    R14
LINETEST ST    R14,HOLD+16
         CP    LINEKNT,=P'56'
         BL    NOEJE
         NI    GENOPT,X'DF'
         BAL   R14,HEADING
NOEJE    AP    LINEKNT,=P'1'
         L     R14,HOLD+16
         BR    R14
         EJECT
PRNTEXTS L     R9,ACCTABLE         SET UP TABLE OF STORED EXTENTS
         SH    R9,H4
         MVC   EXTKNT,=F'0'        SET EXTENT COUNT TO 1
         ST    R14,HOLD+8
         LA    R2,SRSPACE          POINT TO RECORD AREA
         USING SRRECORD,R2         ESTABLISH ADDRESSABILITY
         MVC   ZMGMTCL,SRMGMTCL    COPY MANAGEMENT CLASS IN FIRST LINE
         MVC   ZDATACL,SRDATACL    COPY DATA CLASS IN FIRST LINE
         MVC   ZSTORCL,SRSTOGCL    COPY STORAGE CLASS IN FIRST LINE
         DROP  R2                  END SUMMARY RECORD ADDRESSABILITY
         MVI   SWIT,0              NO TEST  EJECT 1ST TIME
PRNTXT1  L     R2,0(R9)            LOAD IN EXTENT SIZE
         LTR   R2,R2               CHECK FOR END OF LIST
         BZ    JUMPS1              NO MORE LINES OUT
         L     R3,EXTKNT           BRING EXTENT COUNT IN
         LA    R3,1(R3)            BOOST
         ST    R3,EXTKNT           RESTORE
         BAL   R14,STORMAP         STORE EXTENT FOR MAP OUTPUT
         L     R3,EXTKNT           COUNT BACK IN
         BAL   R14,FULLEDIT        CONVERT AND DISPLAY IN EBCDIC
         SH    R9,H4               NEXT ENTRY
         BAL   R14,LINE1OUT        PRINT LINE W/ LINE TEST
         B     PRNTXT1
JUMPS1   L     R14,HOLD+8          RESTORE RETURN ADDRESS
         BR    R14
         EJECT
*********************************************************************
*         PAGE HEADING PROCESSING
*********************************************************************
SYSHEAD  DS    0H
         ST    R14,HOLD            SAVE RETURN
         AP    PAGEKNT,=P'1'       ADD ONE TO PAGE COUNT
         MVC   PAGEPLC,PATTERN     PUT IN EDIT MASK
         ED    PAGEPLC,PAGEKNT     NUMBER FOR PAGE
         PUT   SYSOUT,HEAD1
         PUT   SYSOUT,GSKIP2
         PUT   SYSOUT,TEXTT        PUT IN HEADING
         ZAP   LINEKNT,=P'3'       INITIATE 3 LINES PRINTED
         L     R14,HOLD
         BR    R14                 RETURN
         SPACE 2
HEADRET  DS    0H
         L     R14,HOLD
         BR    R14                 RETURN
         EJECT
HEADING  ST    R14,HOLD            SAVE RETURN
         AP    PAGEKNT,=P'1'       ADD ONE TO PAGE COUNT
         MVC   PAGEPLC,PATTERN     PUT IN EDIT MASK
         ED    PAGEPLC,PAGEKNT     NUMBER FOR PAGE
         PUT   PRINT,HEAD1         TITLE HEADING
         ZAP   LINEKNT,=P'2'       INITIATE 3 LINES PRINTED
HEADER   B     HEADTXT             NOP TO PRINT TEXTT
         PUT   PRINT,HEAD3         COLUMN HEADING
         PUT   PRINT,HEAD4         COLUMN HEADING
         PUT   PRINT,HEAD5         COLUMN HEADING
         AP    LINEKNT,=P'3'       3 MORE LINES PRINTED
HEADTXT  L     R14,HOLD            RESTORE
         BR    R14                 CONTINUE
         EJECT
*********************************************************************
*         LINE PROCESSING FOR SYSOUT
*********************************************************************
SLNE0OUT MVI   SWIT,X'00'          SET FOR NO PAGE TEST
SLNE1OUT ST    R14,HOLD+16         SAVE RETURN
         TM    GENOPT,X'20'        NEW PAGE?
         BZ    SOFORM              NO
         BAL   R14,SYSHEAD         YES
         NI    GENOPT,X'DF'        RESET SWITCH
SOFORM   PUT   SYSOUT,EXTOUT
         MVC   EXTOUT+1(132),EXTOUT  CLEAR LINE
         TS    SWIT                DO PAGE TEST?
         BC    8,SLNEOUT           NO
SLNE2OUT CP    LINEKNT,=P'56'      END OF PAGE?
         BL    SLNEOUT             NO
         OI    GENOPT,X'20'        YES SET FLAG
SLNEOUT  AP    LINEKNT,=P'1'       BUMP LINE COUNT
         L     R14,HOLD+16
         BR    R14
         SPACE 2
SRCHDSCB SUBIN ,                   LOOK FOR DSCB BY DISK ADDRESS
         L     R5,F356TBL          GET TO START OF TABLE AFTER F5
         SH    R5,H160             BACK UP IN TABLE
         SH    R5,H160
NEXTETRY CLC   147(5,R5),DS1PTRDS  COMPARE CCHHR
         BE    SETITUP             THERE
         SH    R5,H160             NEXT ONE
         CR    R5,R7               END OF TABLE
         BNL   NEXTETRY            NO
         B     IOER2               NOT THER SO ERROR
SETITUP  L     R10,0(R5)           LOAD DSCB ADDR
         SUBOUT RETURN=(R14)       RETURN TO CALLER
         EJECT ,
STORMAP  MVC   WORK(44),DATANAME   FETCH NAME
         ST    R2,WORK+44          STORE TRACKS
         STC   R3,WORK+48          STORE EXTENT NUMBER
         L     R3,SREND            END OF TABLE
         MVC   4(49,R3),WORK       STORE EXTENTS
         LA    R1,4(R3)            SAVE ADDR
         LA    R3,56(R3)           BUMP END
         CL    R3,ACCEND           TABLE OVERLAP
         BNL   TABLOVF0            YES
         ST    R3,SREND            SAVE END
         L     R3,SRTBL            GET START
         SH    R3,H56
         L     R4,SRTKNT           NO OF ENTRIES
SORTE12  BCT   R4,SORTE11
         ST    R1,56(R3)           STORE ADDRESS
SORTE14  L     R4,SRTKNT
         LA    R4,1(R4)            BOOST FOR NEXT ENTRY
         ST    R4,SRTKNT
         BR    R14                 EXIT
         SPACE 2
SORTE11  LA    R3,56(R3)           BOOST TO NEXT
         L     R5,0(R3)            POINTER TP SORT MATERIAL
         CL    R2,44(R5)           LOOK AT REL TRACK
         BH    SORTE12             JUMP AROUND IF TOO BIG
SORTE13  ST    R1,0(R3)            STORE ADDRESS IN TABLE
         LR    R1,R5               LAST ADDRESS
         L     R5,56(R3)           NEXT ADDRESS
         LA    R3,56(R3)           BOOST
         BCT   R4,SORTE13          MOVE ALL REST POINTERS UP
         ST    R1,0(R3)            CATCH LAST ONE
         B     SORTE14             GO FINISH
         EJECT
*        *********************************
*        *     DYNALLOC VVDS             *
*        *********************************
         SPACE 1
DYNAVVDS DS    0H
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         SPACE 1
         MVC   DYNVOL,SAVESER      PUT VOLSER IN DSNAME
         MVC   DYNVVOL,SAVESER     AND IN VOLUME TEXT
         LA    R1,DYNCBP           LOAD CB-CHAIN START ADDR.
         SPACE 1
         DYNALLOC  ,
         LTR   R15,R15             DYNALLOC OK ?
         BNZ   DYNAERR             NO, SHOW ERROR
         MVC   VVDSACB+40(8),DYNDNAME  INSERT DDNAME
         MVC   UALDDNAM,DYNDNAME   MOVE DDNAME TO UNALLOCATION TXT UNIT
         XR    R15,R15
         SPACE 1
DYNVVDSE DS    0H
         SUBOUT  RESTORE=(R4)      RETURN TO CALLER
         EJECT
*******************************************************************
*        THIS ROUTINE USES A BUBBLE SORT TO SEQUENCE THE          *
*        VVR  LIST ON THE KEY FIELD.                              *
*******************************************************************
         SPACE 2
SORTVVR  SUBIN SAVE=(R4,R5,R7,R9)  SUBROUTINE ENTRY
         L     R9,VVETBL           GET ADDRESS OF VVR TABLE
         USING VVETABLE,R9         ESTABLISH TABLE ADDRESSABILITY
         SPACE 1
         LR    R7,R9               SAVE START ADDRESS
         LH    R4,VVECNT           GET NUMBER OF ENTRIES
         LTR   R5,R4               SET NUMBER OF LOOPS
         BZ    SORTED              EXIT IF NONE
         BCTR  R5,0                TO NO. OF MEMBERS-1
         SPACE 2
HUBBLE   DS    0H                  TOP OF BUBBLE SORT LOOP
         LR    R9,R7               RESTORE INPUT POINTER
         LTR   R4,R5               SET NUMBER OF MEMBERS
         BZ    SORTED              EXIT IF NONE
         MVI   FLAG,X'00'          RESET FLAG
         SPACE 1
BUBBLE   DS    0H                  TOP OF LOOP FOR CURRENT PASS
         CLC   VVENAME,VVENAME+VVETABL  TEST ADJOINING ENTRIES
         BNH   NEXTMEMB            BYPASS SWAP IF IN SEQUENCE
         SPACE 1
         XC    0(VVETABL,R9),VVETABL(R9)  SWAP
         XC    VVETABL(VVETABL,R9),0(R9)  ENTRIES
         XC    0(VVETABL,R9),VVETABL(R9)  OVER
         OI    FLAG,X'FF'          SET FLAG FOR SEQUENCE CHANGED
         SPACE 1
NEXTMEMB DS    0H                  TEST NEXT SEQUENCE
         LA    R9,VVETABL(,R9)     UPDATE POINTER
         BCT   R4,BUBBLE           GO TRY NEXT PAIR
         TM    FLAG,X'FF'          WERE ANY SEQUENCE CHANGES MADE?
         BNO   SORTED              NO,LIST IS IN SEQUENCE
         BCT   R5,HUBBLE           ELSE GO ROUND AGAIN
         SPACE 2
SORTED   SUBOUT  RESTORE=(R4,R5,R7,R9)  SORT RETURN
         DROP  R9                  END ADDRESSABILITY
         EJECT
*******************************************************************
*        SUMMARY RECORD CREATION
*******************************************************************
         SPACE 1
SUMMARY  DS    0H                  WRITE A RECORD TO SYSUT2
         SUBIN SAVE=(R2,R3,R4)     SUBROUTINE ENTRY
         LA    R2,SYSUT2SW         POINT TO FILE CONTROL BLOCK
         USING MDCB,R2             ESTABLISH ADDRESSABILITY
         TM    MDCBFLAG,DDPRES+DDOPEN  TEST IF ABLE TO WRITE
         BNO   SUMMARYX            NO, FORGET THE WHOLE THING
         DROP  R2                  FINISHED WITH MDCB
         SPACE 1
         LA    R2,SRSPACE          POINT TO RECORD AREA
         USING SRRECORD,R2         ESTABLISH ADDRESSABILITY
         MVI   SRSPUSED+4,C'0'     PRIME SPACE USED
         MVI   SRSPWST+4,C'0'      .. SPACE WASTED
         MVI   SRSP100+2,C'0'      .. SPACE PERCENT
         CLC   SPUSED,HEAD4        COMPARE SPACE USED WITH BLANKS
         BE    *+4+6               YES, LEAVE PRIMED VALUE
         MVC   SRSPUSED,SPUSED     ELSE COPY SPACE USED
         MVC   SRVOLSER,SAVESER    COPY VOLUME SERIAL
         MVC   SRDSNAME,DATANAME   ..DATASET NAME
         MVC   SRCRDATE,CRDATE     ..CREATE DATE
         MVC   SREXPDT,PURGDATE    ..EXPIRE DATE
         MVC   SRREFDT,REFDATE     ..REFERENCE DATE
         MVC   SRDSORG,DSORG       ..DSORG
         MVC   SRDSCHA,DSCHA       ..DATA SET CHANGED
         MVC   SRRECFM,RECFM       ..RECFM
         MVC   SRLRECL,LRECL       ..LRECL
         MVC   SRBLKSZE,BLKSIZE    ..BLOCKSIZE
         MVC   SRALUNIT,ALUNIT     ..UNIT OF ALLOCATION
         MVC   SRSPALC,SPALLOC     ..SPACE ALLOC
         MVC   SRALL2ND,ALL2ND     ..SECONDARY ALLOCATION
         MVC   SREXTENT,EXTENTS    ..NO. EXTENTS
         LH    R3,STDREF           GET LAST REFERENCE STD. DAY (LARGER)
         SH    R3,STDRUN           MINUS RUN STD. DAY NO. (SMALLER)
         STCM  R3,B'0011',SRBDAYS  EQUALS DAYS SINCE REFERENCE
         EJECT
         CLC   DSORG(2),=C'PS'     IS IT A SEQUENTIAL FILE?
         BE    SUMM003             YES, GO STRAIGHT ON
         CLC   DSORG(2),=C'PO'     IS IT A PDS?
         BNE   SUMM001             NO,SKIP DIRECTORY INFORMATION
         MVC   SRDRBALC,DRBLOCKS   ..NO. DIRECTORY BLOCKS
         MVC   SRDRBUSE,DRBLUSED   ..NO. DIRECTORY BLOCKS USED
         B     SUMM003             SKIP KEY INFORMATION
         SPACE 1
SUMM001  DS    0H                  TEST FOR INDEX INFORMATION
         CLC   DSORG(2),=C'IS'     IS IT AN ISAM FILE?
         BE    SUMM002             YES, GET KEY INFORMATION
         CLC   CHARVSAM,DSORG      IS IT A VSAM FILE?
         BNE   SUMM003             NO,SKIP KEY INFORMATION
         MVC   SRCASPLT,VSCASPLT   COPY CA SPLIT PERCENT
         MVC   SRCISPLT,VSCISPLT    ..  CI SPLIT PERCENT
         MVC   SRREGSHR,CTREGSHR    ..  REGION SHAREOPTION
         MVC   SRSYSSHR,CTSYSSHR    ..  SYSTEM SHAREOPTION
         SPACE 1
SUMM002  DS    0H                  COPY KEY DATA
         MVC   SRISRKP,ISRKP       ..RELATIVE KEY POSITION
         MVC   SRISKLN,ISKEYLEN    ..KEY LENGTH
         SPACE 1
SUMM003  DS    0H                  BASIC DATA COMPLETE
         L     R0,THISALLC         GET SPACE ALLOCATED
         LR    R3,R0               COPY TO WORK REGISTER
         S     R3,THISUSED         MINUS SPACE USED
         BM    SUMM004             IF NEGATIVE, FORGET IT
         BAL   R14,NEDIT           OR GIVE SPACE WASTED
         MVC   SRSPWST,EDITWA+3    MOVE IT TO SUMMARY RECORD
         L     R1,THISUSED         GET SPACE USED
         GOSUB PERCENT0            GO CALCULATE PERCENTAGE
         LR    R3,R1               GET THE ANSWER
         BAL   R14,NEDIT           MAKE IT PRINTABLE
         MVC   SRSP100,EDITWA+5    COPY TO SUMMARY RECORD
         SPACE 1
SUMM004  DS    0H                  PROCES DSNAME QUALIFIERS
         LA    R3,3                SET NUMBER OF QUALIFIERS
         LA    R14,SRDSNAME        POINT TO DSNAME
         LA    R2,SRQUAL1          POINT TO ANSWER FIELD
         DROP  R2                  FINISHED WITH ADDRESSABILITY
         EJECT
SUMMQUAL DS    0H                  WRITE QUALIFIERS IN SUMMARY RECORD
         LR    R1,R14              COPY START ADDRESS
         GOSUB CHARWORD            GO FIND LENGTH OF QUALIFIER
         LTR   R1,R1               IS THERE ONE?
         BZ    SUMMOUT             NO, LEAVE BLANK
         BCTR  R1,0                DECREMENT FOR EXECUTE
         EX    R1,CHARMOVE         MOVE QUALIFIER TO RECORD
         LA    R14,2(R1,R14)       POINT TO NEXT INDEX
         LA    R2,8(,R2)           POINT TO NEXT OUTPUT FIELD
         BCT   R3,SUMMQUAL
         SPACE 1
SUMMOUT  DS    0H                  WRITE SUMMARY RECORD
         LA    R2,SRSPACE          POINT TO RECORD AREA
         GOSUB SYSUT2WT            GO WRITE RECORD
         SPACE 1
SUMMARYX DS    0H                  RETURN TO CALLER
         SUBOUT  RESTORE=(R2,R3,R4)  SUBROUTINE EXIT
         SPACE 1
CHARMOVE MVC   0(*-*,R2),0(R14)    MOVE DSN QUALIFIER      *EXECUTED*
         EJECT
*******************************************************************
*        WRITE SUBROUTINE FOR SYSUT2                              *
*******************************************************************
         SPACE 1
SYSUT2WT DS    0H                  WRITE A RECORD TO SYSUT2
         SUBIN SAVE=(R4,R3)        SUBROUTINE ENTRY
         LR    R3,R2               SAVE RECORD ADDRESS
         LA    R2,SYSUT2SW         POINT TO FILE CONTROL BLOCK
         USING MDCB,R2             ESTABLISH ADDRESSABILITY
         TM    MDCBFLAG,DDPRES+DDOPEN  TEST IF ABLE TO WRITE
         BO    SYSUT2WU            YES, CONTINUE
         TM    MDCBFLAG,DDPRES+DDINT  HAVE WE ALLOCATED IT?
         BM    SYSUT2WX            UNABLE TO, FORGET IT
         GOSUB OPEN                NO, GO OPEN IT
         DROP  R2                  FINISHED WITH MDCB
         SPACE 1
SYSUT2WU DS    0H                  WRITE A RECORD TO SYSUT2
         LA    R1,SYSUT2           POINT TO DCB
         LR    R2,R3               AND RECORD
         GOSUB PUTMOVE             GO OUTPUT RECORD
         SPACE 1
SYSUT2WX DS    0H                  RETURN TO CALLER
         SUBOUT  RESTORE=(R4,R3)   SUBROUTINE EXIT
         EJECT
*******************************************************************
*        GENERALISED PUT/MOVE SUBROUTINE                          *
*        ON ENTRY    R1 --> DCB                                   *
*                    R2 --> DATA                                  *
*******************************************************************
         SPACE 1
PUTMOVE  DS    0H                  WRITE A RECORD TO SYSUT2
         SUBIN SAVE=(R1,R2)        SUBROUTINE ENTRY
         LR    R9,R4               SAVE LAST BRANCH  *DEBUG*
         PUT   (R1),(R2)           WRITE A RECORD
         SPACE 1
         SUBOUT  RESTORE=(R1,R2)   SUBROUTINE EXIT
         SPACE 2
*******************************************************************
*        SUB-ROUTINE TO READ THE VVDS                             *
*******************************************************************
         EJECT ,
VVDSGET  DS    0H                  READ A RECORD FROM THE VVDS
         SUBIN ,                   SUBROUTINE ENTRY
         TM    VSAMSW,DDPRES+DDOPEN CAN WE READ THE FILE?
         BNO   VVDSGETX            NO,RETURN
         GET   RPL=VVDSRPL         READ A RECORD
         LTR   R15,R15             READ OK?
         BNZ   GETERROR            NO, PROCESS ERROR
         SPACE 1
VVDSGETX DS    0H                  RETURN TO CALLER
         SUBOUT  ,                 SUBROUTINE EXIT
         EJECT
*******************************************************************
*        SUPPLY DEFAULT RECORD LENGTH AND/OR BLOCK SIZE           *
*         IF NOT CODED.                                           *
*******************************************************************
         SPACE 1
BLKSZRTN SUBIN ,                   SUPPLY DEFAULT LRECL AND/OR BLKS
         USING IHADCB,R1           ESTABLISH DCB ADDRESSABILITY
         LH    R4,DCBLRECL         GET LRECL VALUE
         LTR   R4,R4               ANY SUPPLIED?
         BNZ   *+4+4+4             YES, SKIP SETTING DEFAULT
         LA    R4,SRRECLEN         FORCE LRECL TO SUMMARY RECORD LEN
         STH   R4,DCBLRECL         SET DEFAULT
         CH    R4,DCBBLKSI         HOW ABOUT BLOCKSIZE
         BNH   0(R14)              YES, RETURN TO OPEN
         SLL   R4,3                DEFAULT BLOCK IN EIGHTS
         STH   R4,DCBBLKSI         SET DEFAULT
         SUBOUT  RETURN=(R14)      RETURN TO OPEN ROUTINES
         SPACE 1
         DROP  R1                  END DCB ADDRESSING
         SPACE 3
*******************************************************************
*        TEST IF A DD STATEMENT IS PRESENT BEFORE OPEN            *
*******************************************************************
         SPACE 1
TESTIOT  SUBIN ,                   TEST TASK INPUT/OUTPUT TABLE
         TESTIOT DDNAME=4(R2)      TEST IF DDNAME FOUND IN TIOT
         B     *+4(R15)            TEST CONDITION CODE
         OI    0(R2),DDPRES        TELL THAT DD STATEMENT SUPPLIED
         SUBOUT  ,                 RETURN TO CALLER
         EJECT
*******************************************************************
*        OPEN ROUTINE                                             *
*******************************************************************
         SPACE 1
OPEN     SUBIN SAVE=(R3,R4)        OPEN DATA SET
         USING MDCB,R2             ESTABLISH DD LIST ADDRESSABILITY
         TM    MDCBFLAG,DDINT+DDPRES  TEST IF DD INTERESTING AND
         BNO   OPENX               BRANCH IF NOT
         TM    MDCBFLAG,DDOPEN     TEST IF DD ALREADY OPEN
         BO    OPENX               BRANCH IF OPEN
         L     R3,MDCBDCB          GET DCB ADDRESS
         USING IHADCB,R3           ESTABLISH ADDRESSABILITY
         CLC   MDCBOPEN,=CL8'INPUT'  TEST FOR INPUT D/S
         BE    OPENZ               BRANCH IF INPUT D/S
         CLC   MDCBOPEN,=CL8'OUTPUT'  TEST FOR OUTPUT D/S
         BE    OPENW               BRANCH IF INPUT D/S
         CLC   MDCBOPEN,=CL8'EXTEND'  TEST FOR EXTEND D/S
         BNE   OPENX               GIVE UP IF NOT KNOWN
         OPEN  ((R3),(EXTEND))     OPEN EXTEND DATA SET
         B     OPENY               GO SET SWITCH
OPENW    OPEN  ((R3),(OUTPUT))     OPEN OUTPUT DATA SET
         B     OPENY               GO SET SWITCH
OPENZ    OPEN  ((R3),(INPUT))      OPEN INPUT DATA SET
OPENY    OI    MDCBFLAG,DDOPEN     INDICATE DATA SET OPEN
OPENX    SUBOUT  RESTORE=(R3,R4)   RETURN TO CALLER
         SPACE 1
         DROP  R3                  END DCB ADDRESSING
         EJECT
*******************************************************************
*        CLOSE ROUTINE                                            *
*******************************************************************
         SPACE 1
CLOSE    SUBIN SAVE=(R3)           CLOSE DATA SET
         TM    MDCBFLAG,DDOPEN     TEST IF DATA SET OPEN
         BZ    CLOSEX              EXIT IF NOT
         L     R3,MDCBDCB          GET DCB ADDRESS
         CLC   MDCBCLOS,=CL8'LEAVE'  TEST FOR REPOSITIONING
         BNE   CLSNLV              REPOSITIONING REQUIRED
         CLOSE ((R3),LEAVE)        CLOSE DATA SET, NO REPOSITION
         B     CLOSEX              GO RESET SWITCH
         SPACE 1
CLSNLV   DS    0H                  CLOSE WITH DEFAULT OPTIONS
         CLOSE ((R3))              CLOSE DATA SET
         SPACE 1
CLOSEX   DS    0H                  RETURN TO CALLER
         NI    MDCBFLAG,FF-DDOPEN  SET OFF 'OPEN' SWITCH
         SUBOUT  RESTORE=(R3)      SUBROUTINE EXIT
         SPACE 1
         DROP  R2                  END DD LIST ADDRESSABILITY
         EJECT
         COPY  CHARWORD            INCLUDE QUALIFIER LENGTH ROUTINE
         EJECT
         COPY  PERCENT0            INCLUDE PERCENTAGE CODE
         EJECT
*        *********************************
*        *     CONVERT HEX CODE TO CHAR  *
*        *********************************
         SPACE 1
         DS    F
CODECONV EQU   *
         ST    R4,*-4
         SPACE 1
         XC    FW,FW
         STH   R1,FW+2
         XR    R1,R1
         ICM   R1,8,FW+2
         SRL   R1,4
         STCM  R1,8,FW
         SLL   R1,8
         SRL   R1,4
         STCM  R1,8,FW+1
         ICM   R1,8,FW+3
         SRL   R1,4
         STCM  R1,8,FW+2
         SLL   R1,8
         SRL   R1,4
         STCM  R1,8,FW+3
         TR    FW,TRTAB
         SPACE 1
         L     R1,FW
         SPACE 1
         L     R4,CODECONV-4       RESTORE LINK REGISTER
         BR    R4                  RETURN
         EJECT
         COPY TODDATE              COPY TOD-CLOCK ROUTINE
         EJECT
         COPY STDDAY01             COPY STANDARD DAY CONVERSION
         EJECT
         COPY GETDVCT              COPY DEVICE CHARACTERISTICS CODE
         EJECT
*********************************************************************
*         ERROR MESSAGES
*********************************************************************
IOER     WTO   MF=(E,MSSAGE)       PRINT IO ERROR
IOER3    PUT   PRINT,MSSAGE+4      PUT MESSAGE ON PRINTER
         B     PRINTIT             GO PRINT IT
IOER1    MVC   EXTOUT+1(L'ERMS3),ERMS3      ERROR MSG
         B     IOER                PRINT ERROR
         SPACE 1
TABLOVF0 MVC   EXTOUT+10(L'ERMS),ERMS      PRINT ERROR MESSAGE
         BAL   R14,LINE1OUT        GO OUTPUT MESSAGE
         B     MAPPED              END THIS VOLUME
         SPACE 1
IOER2    MVC   EXTOUT+1(L'ERMS4),ERMS4      ERROR MSG
         B     IOER                PRINT ERROR
VOL      WTO   MF=(E,LISTWTO)      PRINT INTERVENTION REQD
         TIME  BIN                 GET TIME
         LR    R1,R0
         A     R1,=F'4000'         ADD FORTY SECONDS
         ST    R1,HOLD             SAVE
STALL    TIME  BIN
         C     R0,HOLD
         BL    STALL               STALL FORTY  SECONDS
         B     GETDSCB             TRY AGAIN
VOL1     MVC   MSGTP,SAVESER       INTERVENTION REQUIRED
         WTO   MF=(E,LISTWTO)
         TIME  BIN                 GIVE OPERATOR TIME TO MOUNT
         A     R0,=F'4000'         ADD FORTY SECONDS
         ST    R0,HOLD             SAVE
STALL1   TIME  BIN
         C     R0,HOLD             SEE IF AT 40 SECONDS
         BL    STALL1              JUMP TO STALL
         B     F1DSCBS             TRY AGAIN TO FETCH IT
NOTHERE  MVC   EXTOUT+1(31),WARNING  VOLUME NOT MOUNTED
         B     PRINTIT             GO PRINT IT
         EJECT
*        *********************************
*        *     LOGICAL ERROR ROUTINE     *
*        *********************************
         SPACE 1
VSMLERAD DS    0H
         L     R0,12(R7)           LOAD RPL ADDRESS
         LA    R1,VSAMMESS
         LINK  EP=VSMERROR
         SPACE 1
         ST    R1,FW               SAVE FDBK CODE
         SPACE 1
         BAL   R1,VSAMWTO
         DC    AL2(121)
         DC    XL2'0000'
VSAMMESS DS    CL121
VSAMWTO  DS    0H
         SVC   35
         SPACE 1
         B     UABEND
         EJECT
*        *********************************
*        *     PROGRAMM EXITS/ABENDS     *
*        *********************************
         SPACE 1
DATERROR DS    0H
         WTO   'LISTICF ==> ERROR ON DATE CONVERSION - ABEND WITH DUMP'*
               ,ROUTCDE=11
         B     UABEND
         SPACE 2
DYNERROR DS    0H
         WTO   'MAPDISK9 ==> ERROR ALLOCATING VVDS - ABEND WITH DUMP', *
               ROUTCDE=11
         B     UABEND
         SPACE 2
GETERROR DS    0H
         WTO   'MAPDISK9 ==> ERROR ON VSAM GET - ABEND WITH DUMP',     *
               ROUTCDE=11
         B     UABEND
         SPACE 2
GENCBERR DS    0H
         WTO   'MAPDISK9 ==> ERROR ON VSAM GENCB - ABEND WITH DUMP',   *
               ROUTCDE=11
         B     UABEND
         SPACE 1
DYNAERR  DS    0H
*        MVC   CATRRELT(18),=C'VVDS ALLOCATION ER'
*        MVC   CATRRELT+18(14),=C'ROR - CODE => '
*        LH    R1,DYNCB+4
*        GOSUB CODECONV
*        ST    R1,FW
*        MVC   CATRRELT+32(4),FW
*        LA    R15,4
         ABEND 999,DUMP            =========>   ONLY FOR TEST
*                                  <==========
         B     DYNVVDSE
         SPACE 2
MODCBERR DS    0H
         WTO   'MAPDISK9 ==> ERROR ON VSAM MODCB - ABEND WITH DUMP',   *
               ROUTCDE=11
         B     UABEND
         SPACE 2
SHOWERR  DS    0H
         WTO   'MAPDISK9 ==> ERROR ON VSAM SHOWCB - ABEND WITH DUMP',  *
               ROUTCDE=11
         B     UABEND
         SPACE 2
OPENERR  DS    0H
         WTO   'MAPDISK9 ==> ERROR ON VSAM OPEN - ABEND WITH DUMP',    *
               ROUTCDE=11
         B     UABEND
         SPACE 2
UABEND   DS    0H
         ABEND 1111,DUMP
         EJECT
*********************************************************************
*
*         SYNAD ROUTINE *
*
*********************************************************************
PDSIOER  DS    0H
         SYNADAF ACSMETH=QSAM
         MVC   SYNADMS2+1(132),8(R1)  SAVE ERROR MESSAGE
         MVC   SYNADMS1+65(44),DS1DSNAM  GET DSNAME
         PUT   PRINT,SYNADMS1      PUT IN ERROR MESSAGE1
         BAL   R14,LINE1OUT        PRINT LINE W/ LINE TEST
         SYNADRLS
         B     CLOSEPDS            PRINT PDS DSN INFO
SYNADMS1 DC    CL133' * * * I/O ERROR OCCURRED READING DIRECTORY OF PDS1
               .* * * DSNAME='
         EJECT
*********************************************************************
*
*         VARIABLES, CONSTANTS, EQUATES , ETC
*
*********************************************************************
         DS    0H
NOSPARES DS    H                   NO OF SPARES
TRKCNT   DC    AL1(0)              HALF WORD CHECK FOR NUMBER OF
SVCOUNT  DC    AL1(0)              DSCB'S PER TRACK
LINEKNT  DC    PL2'0'              KEEP TRACK OF LINES PER PAGE
PAGEKNT  DC    PL2'0'
GPAGEKNT DC    PL2'0'
GLINEKNT DC    PL2'0'
PATTERN  DC    X'4020202020202120'  NEW EDITTING PATTERN
SWIT     DC    X'FF'
FLAG     DC    X'00'               FLAG FOR BUBBLE SORT
EDITWA   DS    D                   DOUBLE-WORD WORK AREA FOR NEW EDIT
TIOTNVOL DS    CL1                 NUMBER OF VOLS PER DD ENTRY
         DS    0D
PSEUDOF5 DS    CL140               WORK AREA FOR PSEUDO DSCB FORMAT 5
*        RETURNED BY 'IXVTOCF5' WHEN DISK SCANNED HAS AN INDEXED VTOC
SAVEF4   DS    CL140               STORE F4 DSCB FOR MORE REFS
CHECKER  DC    F'0'                TO CHECK FOR MISSING TRACKS
EXTKNT   DC    F'0'                SAVE EXTENT COUNT OF F1
THISALLC DC    F'0'                BINARY TRACKS ALLOCATED
THISUSED DC    F'0'                BINARY TRACKS USED
DSLEN    DC    F'0'                DS SIZE (TRACKS)
TTRVTOC  DC    1F'0'               SAVE TTR0 OF VTOC
HOLD     DS    5F                  SAVE RETURN ADDRESS
SRTKNT   DC    F'1'                ENTRIES IN SORT TABLE
NTRKNT   DC    F'1'                NEXT ENTRY POSITION FOR DSCB F1
TIOT     DS    1F                  SAVE TIOT  ADDRESS
UCBADDR  DS    F                   ADDRESS OF NEXT UCB ADDRESS
TYPE     DC    X'30002001'         TYPE FOR DISK
STDRUN   DC    H'0'                STANDARD DAY NO. OF RUN
STDREF   DC    H'0'                REFRENCE DATE STANDARD DAY NO.
         RIN   GENERATE            REGISTER SAVE AREA
         SPACE 1
PARMLGTH DC    F'0'                LENGTH OF PARM OF EXEC CARD
PARMADDR DS    F                   ADDRESS OF PARM INFO'S
PARMSWCH DC    X'00'               SWITCH FOR 'FULL' PARM
         EJECT ,
**********************************************************************
*                                                          *         *
*         DIRECTORY OPEN USAGE EXIT                        *  GP@GE  *
*                                                          *  11/97  *
**********************************************************************
         SPACE 2
         DS    0F
PDSJFCB  DC    X'07'               READ JFCB
         DC    AL3(PDSAREA)
         DC    X'94',AL3(UDA)
         SPACE 1
PDSAREA  DS    CL176
         IHAUDA DSECT=NO
         ORG   UDAFLAG3
         DC    AL1(UDANORFU)       NO REFERENCE DATE UPDATE
         ORG   ,
         DS    0F
STRJFCB  DC    X'87'               ONE ENTRY ONLY
         DC    AL3(JFCBAREA)
JFCBAREA DS    CL176               JFCB AREA FOR TEST
LIST     CAMLST  SEEK,ADDRS,SAVESER,WORK
         DS    0D
SEEK     DC    AL3(0)              DUMMY PART
ADDRS    DC    XL5'0'              ADDRESS PART
MYDEB    DC    4F'0'               FAKE UP A DEB FOR TRACK CONVERSION
         DC    AL1(1)              ONE EXTENT
         DC    AL3(0)
         DC    1F'0'               NO PURGE
         DC    X'0F'               ID
         DC    AL3(0)
         DC    X'04'               EXTENT SCALE
APPEN    DC    AL3(0)              APPENTAGE TABLE ADDRESS
UCBXREF  DC    XL4'C0000000'       UCB ADDRESS - PLACED BY PROGRAM
         DC    3AL2(0)             STARTING ADDRESS
         DC    X'FFFFFFFF7FFF'     LAST CCHH NUMBER POSSIBLE
         SPACE 1
RKDD     DS    0F                  RKDD FOR STAR
         DC    AL2(0)              RECORD NO. AND KEYLEN
         DC    AL2(0)              RECORD LENGTH
         EJECT
SRTBL    DS    F                   ADDRESS OF SORT TABLE
VVETBL   DS    F                   POINTER TO VVR ENTRIES TABLE
VVENOW   DS    F                   POINTER TO CURRENT VVR ENTRY
DSTBL    DS    F                   POINTER TO DSCB TABLE
ANXTDSCB DS    F                   POINTER TO DSCB TABLE ENTRY
F356TBL  DS    F                   ADDR OF START OF F356 TABLE
F356END  DS    F                   SAVED END OF FORMAT356 TABLE
DSEND    DS    F                   END OF F1 TABLE
BOUNDS   DC    F'1024'             BOUNDS FOR GETMAIN
         DC    F'600000'           * CART1 09AUG91 *
TBLADDR  DC    F'0'                START OF TABLE
TBLLENG  DC    F'0'                LENGTH OF TABLE
ACCEND   DS    F
SREND    DS    F
ACCTABLE DS    F
H160     DC    H'160'
H4       DC    H'4'
H56      DC    H'56'
VVECNT   DC    H'0'                COUNT OF VVR ENTRIES
GENOPT   DC    X'00'               GENERAL FLAGS
GENPAGE  EQU   X'20'               NEW PAGE SWITCH
GENSMS   EQU   X'08'               VOLUME IS SMS
         SPACE 1
         DS    0D
WORK     DS    CL350               WORKING STORAGE FOR OBTAIN
WBYTE    DS    C                   ONE BYTE FOR MEMBERS WORK AREA
SAVEHWM  DC    XL5'0'              SAVE FOR HIGHEST CCHHR TO CHECK
CHARVSAM DC    CL2'VS'             DSORG FOR REPORT
         DS    0F                  ALIGN FOR SPS
MSSAGE   DC    AL2(MSEND-*)
         DC    AL2(0)
         DC    C' SER='
SERMSG   DC    CL6' '
         DC    C' PERMANENT I/O ERROR ON DISK. NO RECOVERY.'
MSEND    DC    CL80' '
ERMS     DC    C'***OUT OF SPACE - INCREASE REGION SIZE***'
ERMS1    DC    C'****IMPOSSIBLE TO MAP A SYSIN OR SYSOUT DD'
ERMS2    DC    C'*****UNIT NOT A DASD OR UNIT OFF-LINE'
ERMS3    DC    C'FMT 5 DSCB NOT FOUND, POINTED TO BY FMT 4 DSCB'
ERMS4    DC    C'SEARCH FOR FMT 3 OR 5 DSCB UNSUCCESSFUL'
         DS    0F
LISTWTO  DC    AL2(TOPMSG-*)
         DC    AL2(0)
         DC    C' SER '
MSGTP    DC    CL7' '
         DC    C'INTERVENTION REQUIRED'
GSKIP2   DC    CL133'0'
TOPMSG   DS    0C
SKIP2    DC    C'0'                ONE SPACE PLUS LINE
         SPACE 2
SRSPACE  DC    CL(SRRECLEN)' '     AREA FOR SUMMARY RECORD
         EJECT
*********************************************************************
*
*               DETAIL LINE INFORMATION
*
*********************************************************************
OUTBIN   DC    C' '                OUTPUT BIN -  NO  SPACE (PRINT)
DATANAME DC    CL44'WHO'           DATASET NAME
         DC    CL1' '
CRDATE   DC    CL5'01234'          CREATION DATE
         DC    CL1' '
PURGDATE DC    CL5'99350'          PURGE DATE
         DC    CL1' '
REFDATE  DC    CL5' '              REFERRED-TO DATE
DSCHA    DC    CL1' '              DATA SET CHANGED
         DC    CL1' '
DSORG    DC    CL4' '              DATA SET ORGANISATION
         DC    CL3' '
RECFM    DC    CL3' '              RECORD FORMAT
         DC    CL1' '
LRECL    DC    CL5' '              LOGICAL RECORD LENGTH
         DC    CL1' '
BLKSIZE  DC    CL5' '              BLOCK SIZE
         DC    CL1' '
ALUNIT   DC    CL5' '              ALLOC UNITS--(TRK,CYL,BLK)
SPALLOC  DC    CL5' '              SPACE ALLOCATED TRACKS TOTAL
         DC    CL1' '
SPUSED   DC    CL5' '              SPACE USED - TOTAL TRACKS
         DC    CL1' '
ALL2ND   DC    CL4' '              SECONDARY ALLOCATION
         DC    CL1' '
EXTENTS  DC    CL3'00'             NUMBER OF EXTENTS
         DC    CL1' '
VSCASPLT DC    0CL3'   '           CA SPLITS PERCENT
DRBLOCKS DC    CL4'  35'           NO. DIRECTORY BLOCKS ALLOCATED
         DC    CL1' '
VSCISPLT DC    0CL3'   '           CI SPLITS PERCENT
DRBLUSED DC    CL4'   0'           NUMBER OF DIRECTORY BLOCKS USED
         DC    CL1' '
ISRKP    DC    CL5'    0'          I.S. R.K.P.(RELATIVE KEY POSITION)
         DC    CL1' '
ISKEYLEN DC    CL3'  0'            I.S. KEYLENGTH
         SPACE 2
CALCU    DC    CL10' '
         SPACE 2
EXTOUT   DC    CL133' '            SPACE FOR OUTPUT LINES
         ORG   EXTOUT+13
ZEXT     DC    CL3' '              EXTENT NUMBER
         DC    CL1' '
ZFIRST   DC    CL5' '              FIRST TRACK HERE
         DC    CL2' '
ZLAST    DC    CL5' '              LAST TRACK NEXT
         DC    CL1' '
ZLNGTH   DC    CL5' '              LENGTH OF EXTENT
         DC    CL16' '
ZMGMTCL  DC    CL8' '              SMS MANAGEMENT CLASS
         DC    CL10' '
ZDATACL  DC    CL8'  '             SMS DATA CLASS
         DC    CL16' '
ZSTORCL  DC    CL8' '              SMS STORAGE CLASS
         SPACE 2
         ORG   EXTOUT
SYNADMS2 EQU   EXTOUT
         SPACE 2
SOUTBIN  DC    C' '                OUTPUT BIN -  NO  SPACE (SYSOUT)
MAPS     DC    CL3' '
STRTT    DC    CL5' '              START TRACK
         DC    CL9' '
LSTT     DC    CL5' '              LAST TRACK
         DC    CL10' '
LNGTH    DC    CL5' '              LENGTH OF EXTENT
         DC    CL6' '
EXTT     DC    CL3' '              EXTENT NUMBER
         DC    CL6' '
SHLINE   DC    CL44' '             DATA SET NAME
         DC    CL36' '             REST ARE BLANK
         EJECT
*********************************************************************
*
*               PAGE AND COLUMN HEADINGS
*
*********************************************************************
HEAD1    DC    CL1'1'              SKIP TO TOP OF PAGE
HEAD2    DC    CL37'DARNICK COMPUTING LTD. (V9.3)'
         DC    CL35'  MAP OF DISK CONTENTS ON VOLUME * '
SAVESER  DC    CL6'SYSOUT'
         DC    CL14' *     DATE : '
HEADDATE DS    CL9
         DC    CL9'  TIME : '
HEADTIME DC    CL8'  .  .  '
         DC    CL5'     '
         DC    CL5'PAGE'
PAGEPLC  DC    CL4' '              PAGE COUNT HERE
         SPACE 2
HEAD3    DC    CL1'0'
         DC    CL44'*-------------- DATA SET NAME -------------*'
         DC    CL1' '
         DC    CL17'*---- DATES ----*'
         DC    CL1' '
         DC    CL23'*-------- DCB --------*'
         DC    CL1' '
         DC    CL25'*--ALLOCATION (TRACKS)--*'
         DC    CL1' '
         DC    CL19'*-- VS/PO STATS --*'
         SPACE 2
HEAD4    DC    CL1' '
         DC    CL34' '
         DC    CL10'           '
         DC    CL6' CREDT'
         DC    CL6' EXPDT'
         DC    CL6' REFDT'
         DC    CL6' DSORG'
         DC    CL6' RECFM'
         DC    CL6' LRECL'
         DC    CL6' CISIZ'
         DC    CL5' TYPE'
         DC    CL6' ALLOC'
         DC    CL6'  USED'
         DC    CL1' '
         DC    CL5'2NDY '
         DC    CL3'NO.'
         DC    CL1' '
         DC    CL5' CA% '
         DC    CL5' CI% '
         DC    CL9'VS RKP-KL'
         SPACE 2
HEAD5    DC    CL1' '
         DC    CL34'            EXT-FIRST---LAST-LNGTH'
         DC    CL10'           '
         DC    CL1' '
         DC    CL16'MANAGEMENT CLASS'
         DC    CL1' '
         DC    CL4' '
         DC    CL10'DATA CLASS'
         DC    CL4' '
         DC    CL6' BLKSZ'
         DC    CL1' '
         DC    CL3' '
         DC    CL13'STORAGE CLASS'
*        DC    CL3' '
         DC    CL5'     '
         DC    CL4' EXT'
         DC    CL1' '
         DC    CL4' DIR'
         DC    CL5' -USE'
         DC    CL1' '
         DC    CL9'PO MEMBS'
         SPACE 2
WARNING  DC    CL31'VOLUME NOT MOUNTED OR AVAILABLE'
         SPACE 2
TEXTT    DC    CL133' FIRST TRACK   LAST TRACK       LENGTH       EXTEN1
               T  DATA SET NAME'
         SPACE 2
FREESPC  DS    0CL133
         DC    CL42'0 REGION MAY BE REDUCED BY APPROXIMATELY  '
SIZEF    DC    CL4' '
         DC    CL87'K FOR MAPPING THIS PACK'
         TITLE 'DCB''S AND I/O CONTROL TABLES'
***********************************************************************
*        DCB'S TABLE                                                  *
***********************************************************************
         SPACE 1
DCBTABLE DS    0F
SYSUT2SW DC    X'00',AL3(SYSUT2),CL8'SYSUT2',CL8'OUTPUT',CL8' '
DCBNTRY  EQU   *-DCBTABLE          LENGTH OF ONE TABLE ELEMENT
OPENSIZE EQU   *-DCBTABLE          TABLE SIZE OF FULL TIME FILES
MAXOPEN  EQU   OPENSIZE/DCBNTRY    MAXIMUM NUMBER OF DCB'S TO OPEN
         SPACE 1
VSAMSW   DC    X'00',AL3(VVDSACB),CL8' ',CL8' ',CL8' '
TABSIZE  EQU   *-DCBTABLE          DCB'S TABLE SIZE
MAXDS    EQU   TABSIZE/DCBNTRY     MAXIMUM NUMBER OF DCB'S
         SPACE 1
DDBYPASS EQU   128                 DD NON-PROCESS
DDINT    EQU   16                  DD INTEREST
DDPRES   EQU   8                   DD PRESENT
DDOPEN   EQU   1                   DATA SET IS OPEN
         SPACE 2
EXITLIST DC    X'85',AL3(BLKSZRTN)  ENTRY FOR DCB MODIFICATION
FF       EQU   255                 ALL BITS ON
         EJECT ,
*******************************************************************
*        DATA CONTROL BLOCKS                                      *
*******************************************************************
         PRINT NOGEN
         SPACE 3
*********************************************************************
*              SYSUT2 - SUMMARY FILE
*********************************************************************
SYSUT2   DCB   DDNAME=SYSUT2,                                          *
               DSORG=PS,                                               *
               EXLST=EXITLIST,                                         *
               MACRF=PM
*********************************************************************
*               PRINT
*********************************************************************
PRINT    DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,BFTEK=S,RECFM=FA,     1
               BLKSIZE=133,LRECL=133,BUFNO=14
         SPACE 1
*********************************************************************
*              SYSOUT
*********************************************************************
SYSOUT   DCB   DSORG=PS,RECFM=FA,BFTEK=S,DDNAME=SYSOUT,MACRF=PM,       1
               LRECL=133,BLKSIZE=133,BUFNO=10
         SPACE 1
*********************************************************************
*              UTILITY
*********************************************************************
UTILITY  DCB   DSORG=PS,MACRF=GM,DDNAME=SYSUT1,BFTEK=S,RECFM=F,        1
               BLKSIZE=256,LRECL=256,BUFNO=4,EODAD=PDSEND,             X
               EXLST=PDSJFCB,SYNAD=PDSIOER
DCBDDNM2 EQU   UTILITY+40          WHERE DDNAME GOES
         SPACE 1
*********************************************************************
*               TEST
*********************************************************************
TEST     DCB   DSORG=PS,MACRF=GM,RECFM=F,BLKSIZE=140,BFTEK=S,          1
               EXLST=STRJFCB
DCBDDNM  EQU   TEST+40
         EJECT
*        *********************************
*        *     VSAM CONTROL BLOCKS       *
*        *********************************
         SPACE 1
VVDSACB  ACB   RMODE31=NONE,                                           +
               BUFND=5,                                                *
               EXLST=VVDSEXLS,                                         *
               MACRF=(ADR,DIR,SEQ,IN)
         SPACE 1
VVDSRPL  RPL   ACB=VVDSACB,                                            *
               AREA=VVDSAREA,                                          *
               AREALEN=4,                                              *
               ARG=VVDSRBA,                                            *
               OPTCD=(ADR,SEQ,LOC,NSP)
         SPACE 1
VVDSEXLS EXLST EODAD=VVDSEOF,LERAD=VSMLERAD
         SPACE 2
         PRINT GEN
         EJECT
*        *********************************
*        *     SAVE AREAS / CONSTANTS    *
*        *********************************
         SPACE 1
         DS    0D
DW       DS    0D
         DS    F
FW       DS    0F
         DS    X
CL3      DS    XL3
         SPACE 1
DATE     DS    F
TIME     DS    F
VSAMLEN  DC    A(32760)            SIZE OF AREA FOR VVR RECORDS
VSAMAREA DC    A(0)                ADDRESS OF AREA FOR VVR RECORDS
VVDSAREA DS    F
VVDSRBA  DS    F
         SPACE 1
SAVECNVT DC    5F'0'               TEMPORARY SAVE AREA
         SPACE 2
PSWD     DC    FL1'8'
PASSWORD DC    CL8' '
CTREGSHR DC    CL1' '              SAVE AREA FOR REGION SHAREOPTION
CTSYSSHR DC    CL1' '              SAVE AREA FOR SYSTEM SHAREOPTION
         SPACE 1
GDGBASEL DS    H
GDGBNML  DS    H
HW       DS    H
         SPACE 2
SWITCH   DC    X'00'               PROCESSING SWITCH
CARDENDE EQU   X'80'               EOF INPUT
EMPTYGDG EQU   X'40'               EMPTY GDG BASE
NODEYES  EQU   X'20'               NODE PRESENT
         SPACE 1
VVDSSWCH DC    X'00'               PROCESSING SWITCH FOR VVDS
VVDSALL  EQU   X'F8'               VVR ALL SUBTYPES
VVDSNCL  EQU   X'80'               VVR HEADER CELL
VVDST21  EQU   X'40'               VVR TYPE X'21'
VVDST23  EQU   X'20'               VVR TYPE X'23'
VVDST60  EQU   X'10'               VVR TYPE X'60'
VVDST22  EQU   X'08'               VVR TYPE X'22'
         SPACE 2
TRTAB    DC    C'0123456789ABCDEF'
         EJECT
*********************************************************************
*         LITERALS ETC.
*********************************************************************
         DS    0D
         LTORG
         EJECT
*        *********************************
*        *     DYNALLOC CONTROL BLOCKS   *
*        *********************************
         SPACE 1
DYNCBP   DS    0F
         DC    X'80'
         DC    AL3(DYNCB)
         SPACE 1
DYNCB    DC    X'1401'             ALLOCATE
         DC    3H'0'
         DC    A(DYNCBTXT)         TEXT POINTER
         DC    2F'0'
         SPACE 1
DYNCBTXT DS    0F
         DC    A(DYNDDN)
         DC    A(DYNDSN)
         DC    A(DYNUNIT)
         DC    A(DYNVOLS)
         DC    X'80'
         DC    AL3(DYNDISP)
         SPACE 1
DYNDDN   DC    X'005500010008'     DDNAME = VARIABLE
DYNDNAME DC    CL8' '
         SPACE 1
DYNDSN   DC    X'000200010011'     DSN=SYS1.VVDS.VVOLSER
         DC    C'SYS1.VVDS.V'
DYNVOL   DC    CL6'      '
DYNVVENM EQU   DYNDSN+L'DYNDSN,17  NAME OF VVDS
         SPACE 1
DYNVOLS  DC    X'001000010006'     VOL=
DYNVVOL  DC    CL6' '
         SPACE 1
DYNUNIT  DC    X'001500010008'     UNIT =
DYNVUNIT DC    CL8'SYSALLDA'
         SPACE 1
DYNDISP  DC    X'00040001000108'   DISP=SHR
         EJECT
***********************************************************************
*        DYNAMIC UNALLOCATION TEXT BLOCKS                             *
***********************************************************************
         SPACE 1
         DS    0F
S99PARM  DC    X'80',AL3(S99AREA)  ADDRESS OF SVC 99 REQUEST BLOCK
S99AREA  DC    AL1(S99RBEND-S99RB)  LENGTH OF REQUEST BLOCK
         DC    X'01'               VERB CODE
         DC    XL6'00'
         DC    A(UALTUPTR)         POINTER TO LIST OF TEXT UNIT PTRS
         DC    XL8'00'
         SPACE 1
         SPACE 1
UALTUPTR DC    A(UALDDTU)          ADDR OF UNALLOCATION DDNAME TXT UNIT
         DC    X'80',AL3(UALOVDSP)  ADDR OF UNALLOC OVERIDING DISP TXT
         SPACE 1
UALDDTU  DC    X'0001'             UNALLOCATION BY DDNAME
         DC    X'0001'             ONE PARAMETER
         DC    X'0008'             LENGTH OF PARAMETER
UALDDNAM DC    CL8' '              DDNAME TO BE UNALLOCATED
         DS    0H
UALOVDSP DC    X'0005'             OVERIDING DISPOSITION SPECIFICATION
         DC    X'0001'             ONE PARAMETER
         DC    X'0001'             LENGTH OF PARAMETER
UALODISP DC    X'08'               KEEP DATA SET
         TITLE 'DSECT MAPS'
***********************************************************************
*        DD TABLE ENTRY FORMAT                                        *
***********************************************************************
         SPACE 1
MDCB     DSECT ,                   FORMAT OF A DCB TABLE ENTRY
MDCBDCB  DS    0F                  DCB POINTER
MDCBFLAG DS    XL1                 FLAGS
         DS    AL3                 ADDRESS OF DCB
MDCBDDNM DS    CL8'DDNAME'         DDNAME
MDCBOPEN DS    CL8'INPUT'          TYPE OF OPEN REQUIRED
MDCBCLOS DS    CL8'REREAD'         TYPE OF CLOSE REQUIRED
         SPACE 1
MDCBLEN  EQU   *-MDCB              LENGTH OF ONE TABLE ENTRY
         SPACE 2
***********************************************************************
*        VVDS ENTRY TABLE FORMAT                                      *
***********************************************************************
         SPACE 1
VVETABLE DSECT ,                   FORMAT OF VVDS ENTRY TABLE
VVENAME  DS    CL45                NAME OF ENTRY
         DS    AL3                 PADDING FOR ALIGNMENT
VVERBA   DS    AL4                 RBA NUMBER
         SPACE 1
VVETABL  EQU   *-VVETABLE          LENGTH OF ONE TABLE ENTRY
         EJECT
*********************************************************************
*
*         DSECT   OF  DSCB1
*
*********************************************************************
DSCBS    DSECT
         PRINT NOGEN
         IECSDSL1  (1)
         SPACE 1
*********************************************************************
*
*         DSECT   OF DSCB2
*
*********************************************************************
         ORG   DSCBS
DSCBS    DSECT
         IECSDSL1  (2)
         SPACE 1
*********************************************************************
*
*         DSECT   OF DSCB3
*
*********************************************************************
         ORG   DSCBS
DSCBS    DSECT
         IECSDSL1  (3)
         EJECT
*********************************************************************
*
*         DSECT   OF DSCB4
*
*********************************************************************
         ORG   DSCBS
DSCBS    DSECT
         DC    44X'04'             44 FIRST BYTES OF FORMAT 4
         IECSDSL1  (4)
         SPACE 1
*********************************************************************
*
*         DSECT   OF DSCB5
*
*********************************************************************
         ORG   DSCBS
DSCBS    DSECT
         IECSDSL1  (5)
         SPACE 1
*********************************************************************
*
*         DSECT   OF DSCB6
*
*********************************************************************
         ORG   DSCBS
DSCBS    DSECT
         IECSDSL1  (6)
         EJECT
*********************************************************************
*
*         UCB DSECT
*
*********************************************************************
UCB      DSECT
         IEFUCBOB
         SPACE 2
*********************************************************************
*
*         CVT DSECT
*
*********************************************************************
         SPACE 1
         CVT   DSECT=YES,LIST=YES
         SPACE 2
*********************************************************************
*
*         DEB MAPPING MACRO
*
*********************************************************************
         IEZDEB  LIST=YES
         EJECT
*********************************************************************
*
*         JFCB MACRO (IEFJFCBN DSECT)
*
*********************************************************************
         SPACE 1
IEFJFCBN DSECT ,                   DUMMY SECTION
         IEFJFCBN  LIST=YES        IBM MAPPING MACRO
         SPACE 2
*********************************************************************
*
*         DCBD MACRO (IHADCB DSECT)
*
*********************************************************************
         DCBD  DSORG=PS,DEVD=DA    DCB DSECT
         EJECT
         IHADVCT   ,               DEVICE CHARACTERISTICS TABLE ENTRY
         EJECT
         IEFZB4D0  ,               SVC 99 REQUEST BLOCK DSECT
         EJECT
         COPY  MAPDISKF            FORMAT OF SUMMARY RECORDS
         SPACE 2
         COPY  VVRDSECT            FORMAT OF ICF VSAM CATALOG AREAS
         SPACE 2
         END   MAPDISK
*
