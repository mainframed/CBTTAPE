SE30EXT  CSECT
*
*        PDSDOC *A SPECIAL VERSION OF SUM30EXT INCLUDING RACF FIELDS..* SE30EXT
*
* 20MAR92 CART1 PROCESS INTERVAL RECORDS
*
*
*
         PGMIN USING=(13)         STANDARD HOUSEKEEPING
         USING SMFRCD30,R9
         OPEN  (SMFIN,,RECOUT,(OUTPUT))
         EJECT
READREC  EQU   *
         GET   SMFIN              * READ SMF RECORD
         LR    R9,R1              * LOAD ADDRESS OF RECORD INTO R9
         CLI   SMF30RTY,X'1E'     * IS THIS A TYPE 30 RECORD
         BNE   READREC            * NO - GO GET NEXT RECORD
         CLI   SMF30SON+1,1       * IS SUBSYSTEM SEGMENT THERE
         BL    READREC            * NO - GO GET NEXT RECORD
         CLC   SMF30WID(3),STC    * IS THIS RECORD FOR STARTED TASKS
         BE    READREC            * YES - IGNORE STARTED TASKS
         XC    SE30DATA(SE30DATL),SE30DATA CLEAR RECORD DATA AREA
         MVC   SE30TIM,SMF30TME    COPY SMF RECORD TIME ..
         MVC   SE30DAY,SMF30DTE    .. DATE ..
         MVC   SE30SID,SMF30SID    .. AND SYSTEM ID
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30SOF        * POINT TO SUBSYSTEM SEGMENT
         USING SMF30PSS,R4
         CLC   SMF30TYP,R30STEP   * IS THIS A STEP TERMINATION RECORD
         BE    STEP               * YES - GO DO STEP PROCESSING
         CLC   SMF30TYP,R30JOB    * IS THIS A JOB TERMINATION RECORD
         BE    JOB                * YES - GO DO JOB PROCESSING
         CLC   SMF30TYP,R30INTV   * IS THIS AN INTERVAL RECORD
         BNE   READREC            * NO - GO GET NEXT RECORD
         MVI   RECTYPE,C'I'       * MOVE IN INTERVAL INDICATOR
         B     PROCESS            * GO PROCESS DATA
JOB      DS    0H                  RECORD IS FOR JOB END
         MVI   RECTYPE,C'J'       * MOVE IN JOB INDICATOR
         B     PROCESS            * GO PROCESS DATA
STEP     EQU   *
         MVI   RECTYPE,C'S'       * MOVE IN STEP INDICATOR
         EJECT
PROCESS  EQU   *
         MVC   ENDTIME,SMF30TME   * GET ENDED TIME
         MVC   ENDDATE,SMF30DTE   * GET ENDED DATE
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30IOF        * POINT TO ID SEGMENT
         USING SMF30ID,R4
         CLI   SMF30ION+1,1       * IS ID SEGMENT THERE
         BL    IOACT              * NO - GO CHECK I/O ACTIVITY SEGMENT
         MVC   JOBNAME,SMF30JBN   * GET JOBNAME/SESSION NAME
         MVC   PGMNAME,SMF30PGM   * GET PROGRAME NAME
         MVC   STEPNAME,SMF30STM  * GET STEPNAME
         MVC   STEPNUMB,SMF30STN  * GET STEP NUMBER
         MVC   JOBCLASS,SMF30CLS  * GET JOB CLASS
         MVC   PGN,SMF30PGN       * GET PERFORMANCE GROUP NUMBER
         MVC   PPGMSTRT,SMF30PPS  * GET PROBLEM PROGRAM START TIME
         MVC   STEPINIT,SMF30SIT  * GET STEP INITIATION TIME
         MVC   STEPDATE,SMF30STD  * GET STEP INITIATION DATE
         MVC   RACFUID,SMF30RUD   * GET RACF USERID
         MVC   RACFGRP,SMF30GRP   * GET RACF GROUP NAME
IOACT    EQU   *
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30UOF        * POINT TO I/O ACTIVITY SEGMENT
         USING SMF30URA,R4
         CLI   SMF30UON+1,1       * IS I/O ACTIVITY SEGMENT THERE
         BL    CCODE              * NO - GO CHECK CONDITION CODE
         MVC   CARDCT,SMF30INP    * GET NUMBER OF SYSIN DD *,DATA IMAGE
         MVC   NUMTPUT,SMF30TPT   * GET NUMBER OF TPUTS
         MVC   NUMTGET,SMF30TGT   * GET NUMBER OF TGETS
         MVC   TOTEXCP,SMF30TEP   * GET TOTAL BLOCKS(EXCP) TRANSFERED
CCODE    EQU   *
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30TOF        * POINT TO COMPLETION CODE SEGMENT
         USING SMF30CMP,R4
         CLI   SMF30TON+1,1       * IS COMPTETION CODE SEGMENT THERE
         BL    CPU                * NO - GO CHECK CPU SEGMENT
         MVC   COMPCODE,SMF30SCC  * GET COMPLETION CODE
         MVC   TERMINDC,SMF30STI  * GET TERMINATION INDICATOR
CPU      EQU   *
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30COF        * POINT TO CPU SEGMENT
         USING SMF30CAS,R4
         CLI   SMF30CON+1,1       * IS CPU SEGMENT THERE
         BL    STORAGE            * NO - GO CHECK STORAGE SEGMENT
         MVC   DPRTY,SMF30PTY     * GET CPU DISPATCHING PRIORITY
         MVC   TCBTIME,SMF30CPT   * GET STEP TCB TIME
         MVC   SRBTIME,SMF30CPS   * GET STEP SRB TIME
         L     R1,SMF30CPT        * GET STEP SRB TIME IN REG 1
         A     R1,SMF30CPS        * ADD STEP SRB TIME IN REG 1
         ST    R1,TOTCPU          * SAVE TOTAL CPU TIME
         EJECT
STORAGE  EQU   *
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30ROF        * POINT TO STORAGE SEGMENT
         USING SMF30SAP,R4
         CLI   SMF30RON+1,1       * IS STORAGE SEGMENT THERE
         BL    PERFORM            * NO - GO CHECK PERFORMANCE SEGMENT
         MVC   REGREQ,SMF30RGN    * GET REQUESTED REGION SIZE
         MVC   PVTSTG,SMF30PRV    * GET PVT VIRTUAL STG ALLOCATED
         MVC   SYSSTG,SMF30SYS    * GRT SYSTEM VIRTUAL STG ALLOCATED
         MVC   PAGEINS,SMF30PGI   * GET NUMBER OF PAGINS
         MVC   PAGEOUTS,SMF30PGO  * GET NUMBER OF PAGE OUTS
         MVC   STEPRECL,SMF30REC  * GET NUMBER OF STEP RECLAIMS
         MVC   SWAPS,SMF30NSW     * GET NUMBER OF SWAPS
         MVC   SWAPINS,SMF30PSI   * GET NUMBER OF SWAPINS
         MVC   SWAPOUTS,SMF30PSO  * GET NUMBER OF SWAP OUTS
         MVC   VIOPGIN,SMF30VPI   * GET NUMBER OF VIO PAGE INS
         MVC   VIOPGOUT,SMF30VPO  * GET NUMBER OF VIO PAGE OUTS
         MVC   VIOPGREC,SMF30VPR  * GET NUMBER OF VIO RECLAIMS
         MVC   CMNPGIN,SMF30CPI   * GET NUMBER OF COMMON PAGE INS
         MVC   CMNRECL,SMF30CPR   * GET NUMBER OF COMMON RECLAIMS
         MVC   PGSTOLEN,SMF30PST  * GET NUMBER OF PAGES STOLEN
         MVC   PAGESEC,SMF30PSC   * GET NUMBER OF PAGES PER SECOND
PERFORM  EQU   *
         LR    R4,R9              * GET RECORD ADDRESS
         A     R4,SMF30POF        * POINT TO PERFORMANCE SEGMENT
         USING SMF30PRF,R4
         CLI   SMF30PON+1,1       * IS PERFORMANCE SEGMENT THERE
         BL    ENDPROC            * NO - GO DO END OF DATA PROCESSING
         MVC   SERVICE,SMF30SRV   * GET TOTAL SERVICE UNITS
         MVC   CPUSRV,SMF30CSU    * GET CPU SERVICE UNITS
         MVC   SRBSRV,SMF30SRB    * GET SRB SERVICE UNITS
         MVC   IOSRV,SMF30IO      * GET I/O SERVICE UNITS
         MVC   MSOSRV,SMF30MSO    * GET MSO SERVICE UNITS
         MVC   XACTTIME,SMF30TAT  * GET TRANSACTION TIME
         MVC   XACTRES,SMF30RES   * GET TRANSACTION RESIDENCY TIME
         MVC   NUMXACT,SMF30TRS   * GET NUMBER OF TRANSACTIONS
ENDPROC  EQU   *
         PUT   RECOUT,SE30REC     * WRITE RECORD TO OUTPUT FILE
         B     READREC            * GO GET NEXT RECORD
         EJECT
DONE     EQU   *
         CLOSE (SMFIN,,RECOUT)
         LR    R1,R13
         L     R13,4(R13)
         RETURN (14,12),RC=0
         TITLE 'DATA AREAS'
R30INTV  DC    H'2'               * INTERVAL RECORD INDICATOR
R30JOB   DC    H'5'               * JOB TERMINATION RECORD INDICATOR
R30STEP  DC    H'4'               * STEP TERMINATION RECORD INDICATOR
STC      DC    CL3'STC'           * STARTED TASK SUBSYSTEM INDICATOR
         EJECT
SMFIN    DCB   DDNAME=SMFIN,DSORG=PS,MACRF=GL,EODAD=DONE
         EJECT
RECOUT   DCB   DDNAME=RECOUT,DSORG=PS,MACRF=PM
         EJECT
         COPY SE30REC              COPY SUMMARY RECORD FORMAT
         EJECT
         IFASMFR 30
         END
