MPFTAPEM TITLE 'MPFTAPEM -- WTO EXIT TO DISPLAY TAPE UNLOADED DATA'
***********************************************************************
*        PDSDOC *MPF EXIT TO SMF RECORD TAPE MOUNT, FIND VOLUME.......* MPFTAPEM
*                                                                     *
* THIS MPF EXIT HAS TWO FUNCTIONS:                                    *
* A) TO RECORD IN USER SMF RECORDS TAPE UNIT ACTIVITY                 *
* B) WHEN A TAPE MOUNT IS REQUESTED THIS EXIT WILL CHECK THE IN-CORE  *
*    TABLES MAINTAINED BY 'MPFTAPEK' TO SEE IF THE VOLUME MAY BE LYING*
*    IN THE STACKER OF ANOTHER DRIVE. IF SO, THE MOUNT MESSAGE IS     *
*    EXTENDED TO SAY WHERE THE TAPE MIGHT BE FOUND.                   *
*                                                                     *
* 06DEC91 CART1 REWROTE WTOETPS2 FROM CBT321.FILE111 USING DSECTS     *
*               AND COMMON CODE. SEE ALSO 'MPFTAPEK'.                 *
***********************************************************************
         SPACE 1
MPFTAPEM CSECT
MPFTAPEM AMODE 31
MPFTAPEM RMODE ANY
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         B     START
         DC    C'MPFTAPEM'
         DC    C'&SYSDATE',C' &SYSTIME'
*        PRINT NOGEN
         PRINT ON,GEN
START    L     R3,0(R1)      LOAD POINTER TO PASSED PARM LIST      JWP
         SPACE
         GETMAIN RU,LV=AREAREGS,SP=230,LOC=(BELOW,ANY) GETMAIN FOR JWP
         SPACE
         LR    R10,R1        SAVE POINTER TO GOTTEN AREA           JWP
         MVI   0(R10),X'00'  CLEAR GOTTEN AREA                     JWP
         MVC   1(AREAREGS-1,R10),0(R10)                            JWP
         ST    R13,4(R10)    EXCHANGE THE REGISTER SAVE AREA       JWP
         ST    R10,8(R13)    POINTERS TRICK                        JWP
         LR    R13,R10                                             JWP
         USING WORKREGS,R13  ESTABLISH ADDRESSABILITY TO GOTTEN AREA
         SPACE 1
         LR    R10,R3         PICK UP ADDRESS OF CTXT
         USING CTXT,R10
         L     R8,CTXTTXPJ    LOAD POINTER TO THE MAJOR LINE
         USING CTXTATTR,R8
         TM    CTXTTFB1,CTXTTFWR  WTOR?
         BO    RETURN          YES, SOMETHING IS WRONG
         CLC   =C'IEC501',CTXTTMSG IS THIS THE CORRECT MAJOR LINE?
         BE    START1          YES, PROCESS IT
         CLC   =C'IEF233',CTXTTMSG IS THIS THE CORRECT MAJOR LINE?
         BNE   RETURN          NO, DON'T KNOW WHY I WAS CALLED
         EJECT ,
START1   DS    0H                  PROCESS A MESSAGE
         LA    R0,SMFSLEN          GET RECORD LENGTH
         LA    R1,SMF234           POINT TO SMF RECORD
         BAL   R4,CLEAR            GO CLEAR SMF RECORD
         MVC   SMFJOBN,BLANKS      BLANK OUT ..
         MVC   SMFVOL,BLANKS       ..CHARACTER FIELDS
         MVI   SMFRECTY,234        SET RECORD TYPE
         MVI   SMFTYPE,C'M'        SHOW IT IS A 'MOUNT' RECORD
         LA    R6,CTXTTMSG+10      POINT TO UNIT ADDRESS
         MVC   SMFUNIT,0(R6)       MOVE IN UNIT ADDRESS
         LA    R1,4(,R6)           POINT TO VOLUME SERIAL
         LR    R6,R1               SAVE ADDRESS OF VOLSER
         BAL   R4,CHARWORD         FIND LENGTH OF VOLSER
         LTR   R1,R1               TEST FOR ANY
         BZ    LIDRC               NONE, GO PROCESS IDRC COMPONENT
         BCTR  R1,0                DECREMENT FOR EXECUTE
         LA    R2,SMFVOL           POINT TO RECEIVING FIELD
         EX    R1,MOVESMF          COPY VOLUME SERIAL
         LA    R1,1(,R1)           RESTORE REAL LENGTH
         EJECT ,
LIDRC    DS    0H                  ROUTINE TO SKIP TO JOBNAME
         LA    R1,1(R1,R6)         MOVE TEMPORARY POINTER OVER VOLSER
         LR    R6,R1               UPDATE POINTER AS WELL
         BAL   R4,CHARWORD         FIND LENGTH OF LABEL FIELD
         LA    R1,1(R1,R6)         POINT AT NEXT PARAMETER
         LR    R6,R1               UPDATE POINTER AS WELL
         CLC   =C'IEC501',CTXTTMSG DOES IT SHOW IDRC?
         BNE   LJOBNM              NO, NEXT FIELD IS JOBNAME
         BAL   R4,CHARWORD         FIND LENGTH OF IDRC
         LA    R6,1(R1,R6)         MOVE MESSAGE POINTER OVER IDRC PARAM
         SPACE 2
LJOBNM   DS    0H                  ROUTINE TO COPY JOBNAME
         LR    R1,R6               POINT AT JOBNAME
         BAL   R4,CHARWORD         FIND LENGTH OF JOBNAME
         LTR   R1,R1               TEST FOR LENGTH
         BZ    LMSGDONE            EXIT IF NONE
         BCTR  R1,0                DECREMENT FOR EXECUTE
         LA    R2,SMFJOBN          POINT TO RECEIVING FIELD
         EX    R1,MOVESMF          COPY VOLUME SERIAL
         EJECT ,
LMSGDONE DS    0H                  MESSAGE HAS BEEN PROCESSED
         GOSUB GETUCVT             FIND OR BUILD USER CVT
         LR    R9,R1               SAVE ITS ADDRESS
         USING USERCVT,R9          ESTABLISH ADDRESSABILITY
         SPACE 1
         COPY MPFTAPET             COPY TABLE BUILD COMMON CODE
         EJECT ,
START2   DS    0H                  CHECK VOLUME SERIALS
         CLC   SMFVOL,BLANKS       IS THE VOLUME SERIAL NUMBER BLANK?
         BE    WRITESMF            YES, FORGET IT
         CLC   SMFVOL,=C'SCRTCH'   IS THE VOLUME SERIAL NUMBER SCRTCH?
         BE    WRITESMF            YES, FORGET IT
         CLC   SMFVOL,=C'PRIVAT'   IS THE VOLUME SERIAL NUMBER PRIVAT?
         BE    WRITESMF            YES, FORGET IT
         CLC   =H'108',CTXTTLMX    ENOUGH ROOM FOR THE MESSAGE?
         BH    WRITESMF            NO, FORGET IT
         SPACE 1
         L     R11,UCVTTAPE   FIND THE TAPE UNIT ADDRESS TABLE
         LA    R11,HDRLEN(R11)   SKIP PAST THE TAPE TABLE HEADER
         USING TAPETAB,R11         ESTABLISH TABLE ADDRESSABILITY
CKVOLSER CLC   =X'FFFFFFFF',0(R11) IS THIS THE END OF THE TABLE?
         BE    WRITESMF              YES, FORGET IT
         LH    R4,TABCOUNT         PICK UP THE NUMBER OF TABLE ENTRIES
         LA    R5,TABVOLS          FIND THE FIRST TABLE ENTRY
CKVOLSE1 CLC   SMFVOL,0(R5)        IS THIS THE VOLUME SERIAL NUMBER?
         BE    WRITEMSG              YES, CONTINUE
         LA    R5,6(R5)              NO, TRY ANOTHER
         BCT   R4,CKVOLSE1         TRY ANOTHER VOL=SER=
         LR    R11,R5              SET UP THE NEW POINTER
         B     CKVOLSER            GO TO IT
         SPACE 1
WRITEMSG DS    0H                  WRITE OPERATOR A MESSAGE
         MVC   CTXTTLEN,CTXTTLMX   GIVE THE MESSAGE ENOUGH ROOM
         MVC   CTXTTMSG+(108-L'UNITMSG)(L'UNITMSG),UNITMSG
         MVC   CTXTTMSG+104(3),TABUNIT MOVE THE UNIT ADDRESS
         OI    CTXTRFB1,CTXTRCMT      TELL COMMTASK TO CHANGE THE MSG
         SPACE 1
WRITESMF DS    0H                  WRITE SMF RECORD
         L     R11,UCVTTAPE   FIND THE TAPE UNIT ADDRESS TABLE
         LA    R11,HDRLEN(R11)   SKIP PAST THE TAPE TABLE HEADER
CHKUNIT  CLC   =X'FFFFFFFF',0(R11) IS THIS THE END OF THE TABLE?
         BE    RETURN                YES, FORGET IT
*              ASSUMES A THREE DIGIT UNIT ADDRESS
         CLC   TABUNIT,SMFUNIT     IS THIS THE CORRECT UNIT ADDRESS?
         BE    FOUNDIT             YES, PROCESS IT
         LH    R1,TABCOUNT         NO, FIND THE NEXT ONE
         MH    R1,=H'6'          NUMBER OF TABLE ENTRIES * 6
         LA    R11,TABVOLS(R1)   SKIP TO NEXT TABLE ENTRY
         B     CHKUNIT
         EJECT ,
FOUNDIT  DS    0H                  GOT UNIT TABLE
         MVC   SMFOLDTM,TABTMSP    SAVE OLD TIMESTAMP
         STCK  TABTMSP             UPDATE THE TIMESTAMP
         MVC   SMFTSMP,TABTMSP     COPY NEW TIMESTAMP
         LA    R0,SMFSLEN          GET RECORD LENGTH
         LA    R1,SMF234           POINT TO SMF RECORD
         GOSUB SMFWRITE            GO TO SMF SUB-ROUTINE
         SPACE 2
RETURN   LR    R1,R13       LOAD ADDRESS OF GOTTEN AREA           JWP
         L     R13,4(R13)   LOAD PREVIOUS SAVE AREA ADDRESS       JWP
         LA    R0,AREAREGS  LOAD SIZE OF GOTTEN AREA              JWP
         FREEMAIN RU,LV=(0),A=(1),SP=230 FREE THE GOTTEN AREA     JWP
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 2
MOVESMF  MVC   0(*-*,R2),0(R6)     COPY DATA TO SMF RECORD    *EXECUTE*
         SPACE 1
         DROP  R9                  END USERCVT ADDRESSABILITY
         DROP  R11                 END TAPETAB ADDRESSABILITY
         EJECT ,
         COPY  GETUCVTR            INCLUDE USERCVT LOCATE ROUTINE
         EJECT ,
         COPY  CLEARR              INCLUDE STORAGE CLEAR ROUTINE
         EJECT ,
         COPY  CHARWORR            INCLUDE FIELD LENGTH COUNT CODE
         EJECT ,
         COPY  SMFWRITR            INCLUDE SMF WRITER CODE
         TITLE 'DATA AREAS'
BLANKS   DC    CL8' '              SOME HANDY BLANKS
         SPACE 1
UNITMSG  DC    C'  IT MAY STILL BE ON XXX.'
         SPACE 1
         LTORG *
         EJECT ,
**********************************************************************
*                                                                    *
*        DSECT USED TO MAP GOTTEN AREA                               *
*                                                                    *
**********************************************************************
         SPACE
WORKREGS DSECT
         SPACE
REGAREA  DS    18F       REGISTER SAVE AREA
         SPACE
         CNOP  2,4
         COPY  SMF234         INCLUDE SMF RECORD FORMAT
         SPACE
         SEGIN GENERATE       GENERATE REGISTER SAVE AREA
         SPACE
AREAREGS EQU   *-WORKREGS
         EJECT ,
         COPY CSATABLE        COPY TABLE VOLUME TABLE FORMAT
         EJECT ,
         COPY USERCVT         COPY USERCVT FORMAT
         SPACE 1
         EJECT
         IEZVX100
         PRINT NOGEN
         REQU  ,              STANDARD REGISTER EQUATES
         CVT   DSECT=YES,LIST=YES
         SPACE 3
         END   MPFTAPEM
