         MACRO
&NAME1   STRING  &ADDRESS=,&STACKER=N
         GBLA  &OFFSET
         LCLA  &RANGE,&I
         LCLC  &CONST,&START,&A,&STKR
.**********************************************************************
.*       PDSDOC *MACRO SPECIFIC TO MPFTAPE. EXITS - BUILD UNIT TABLES.* STRING
.*                                                                    *
.* 06DEC91 CART1 EXTRACT FROM WTOETPS1 IN CBT321.FILE111              *
.**********************************************************************
         SPACE 1
         AIF   (&OFFSET NE 0).AFTINIT
&NAME1   B     STRINGGM       BRANCH TO DO THE GETMAIN
STRINGIN DS    0H             INITIALIZE THE TAPE TABLE
.AFTINIT ANOP
&MAXIMUM SETC  '0123456789ABCDEF'
&CPTR    SETA  0
&CONST   SETC  '&ADDRESS(1)'(1,2)
.*               ASSUMES A THREE DIGIT DEVICE ADDRESS
&START   SETC  '&ADDRESS(1)'(3,1)
&RANGE   SETA  1
         AIF   (K'&ADDRESS LT 2).NORANGE
&RANGE   SETA  &ADDRESS(2)
.NORANGE ANOP
&CPTR    SETA  &CPTR+1
         AIF   ('&START' NE '&MAXIMUM'(&CPTR,1)).NORANGE
         SPACE 1
&ENTRIES SETA  2             AN ORDINARY TAPE DRIVE
         SPACE 1
&STKR    SETC  '&STACKER'
         AIF   ('&STKR' EQ 'N').NOSTKR
         SPACE 1
&ENTRIES SETA  6 A TAPE DRIVE WITH A STACKER FOR UNLOADED TAPES
         SPACE 1
.NOSTKR  ANOP
         MVC   TAPETBL+&OFFSET.(3),=C' &CONST.' INITIALIZE EACH ENTRY
.*               ASSUMES A THREE DIGIT DEVICE ADDRESS
&A       SETC  '&MAXIMUM'(&CPTR,1)
&I       SETA  &OFFSET+3
         MVI   TAPETBL+&I.,C'&A.'         CALCULATED UNIT NUMBER
&OFFSET  SETA  &OFFSET+8
         LA    R1,&ENTRIES                VOLUMES IN DEVICE STACKER
&I       SETA  &OFFSET+4
         STH   R1,TAPETBL+&I              STORED IN THE TAPE TABLE
&OFFSET  SETA  &OFFSET+6+&ENTRIES*6
&RANGE   SETA  &RANGE-1
&CPTR    SETA  &CPTR+1
         AIF   (&CPTR GT 16).MEND
         AIF   (&RANGE GT 0).NOSTKR
.MEND    MEND
