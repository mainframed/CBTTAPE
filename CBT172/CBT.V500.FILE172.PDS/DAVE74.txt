DAVE74   TITLE 'DARNICK AUTO VARIABLES EXTRACTOR, TYPE 74'
***********************************************************************
*        PDSDOC *CREATE SUMMARY RECORDS FROM SMF74 DATA...............* SERVED72
*                                                                     *
*        PROGRAM ID. :             DAVE74                             *
*                                                                     *
*        AUTHOR :                  D.H.CARTWRIGHT.                    *
*                                                                     *
*        LOCATION :                DARNICK COMPUTING LTD.             *
*                                  103, DARNICK ROAD,                 *
*                                  SUTTON COLDFIELD                   *
*                                  UK - B73  6PF                      *
*                                                                     *
*        HISTORY  :                                                   *
*                                                                     *
* 12NOV98 DHC COPIED DAVE73,   MODIFIED FOR DIFFERENT RECORDS         *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*       ---------                                                     *
*                                                                     *
* THIS PROGRAM WILL EXTRACT DATA FROM SMF 74 RECORDS (RMF DEVICE      *
* ACTIVITY) AND WILL WRITE IT OUT IN MULTIPLE FIXED LENGTH RECORDS    *
* SUITABLE FOR ANALYSIS BY SIMPLE PROGRAMS SUCH AS 'PARROT' OR        *
* CA/EARL.                                                            *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*       ------------                                                  *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*       INPUT                                                         *
*       -----                                                         *
*                                                                     *
* 1)SYSUT1 - INPUT SMF RECORDS                                        *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*       OUTPUT                                                        *
*       ------                                                        *
*                                                                     *
* 1)SYSUT2 - SUMMARY RECORDS. THESE ARE ALWAYS THE SAME LENGTH, BUT   *
*            MAY BE WRITTEN AS VB RECORDS IF DESIRED.                 *
*                                                                     *
***********************************************************************
         EJECT
         PRINT ON,GEN              SHOW MACRO EXPANSIONS
*        PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
*        PRINT OFF                 SUPPRESS OUTPUT
         SPACE 1
DAVE74   CSECT
         SPACE 1
***********************************************************************
*        REGISTER EQUATES AND USAGES.                                 *
***********************************************************************
         SPACE 1
R0       EQU   0
R1       EQU   1                   WORK
R2       EQU   2                   DCB TABLE POINTER
REG2     EQU   R2                  WORK REGISTER IN 'SCAN' MACRO
R3       EQU   3                   WORK
R4       EQU   4                   LINKAGE RETURN
R5       EQU   5                   COUNTER
R6       EQU   6                   SMF 74 SEGMENT POINTER - BASE
R7       EQU   7                   DATA SECTION BASE
R8       EQU   8                   PRODUCT SECTION BASE
R9       EQU   9                   DEVICE  CONTROL BASE
R10      EQU   10                  WORK
R11      EQU   11                  WORK
R12      EQU   12
R13      EQU   13                  SAVE AREA / BASE REG. 1
R14      EQU   14                  WORK
R15      EQU   15
         SPACE 1
         PGMIN EQUATES=NO,USING=(R13) STANDARD HOUSEKEEPING
         SPACE 1
         L     R5,0(R1)            GET ADDR OF PARM FIELD (NOT USED)
         GOSUB OPENEM              GO OPEN FILES
         EJECT
***********************************************************************
*        MAIN PROCESSING ROUTINE                                      *
***********************************************************************
         SPACE 1
MAINLINE DS    0H                  PROGRAM FUNCTION
         BAL   R4,SYSUT1RD         GO READ A RECORD
         USING SMF74HDR,R6         ESTABLISH SMF RECORD ADDRESSABILITY
         CLI   SMF74RTY,74         IS THIS A TYPE 74 RECORD?
         BNE   MAINLINE            NO, IGNORE IT
         CLI   SMF74STY+1,1        IS THIS A SUB-TYPE 1?
         BNE   MAINLINE            NO, IGNORE IT
         SPACE 1
         MVC   DC74TIM,SMF74TME    COPY SMF RECORD TIME ..
         MVC   DC74DAY,SMF74DTE    .. DATE ..
         MVC   DC74SID,SMF74SID    .. AND SYSTEM ID
         L     R1,SMF74PRS         GET RMF PRODUCT SECTION OFFSET
         LA    R8,0(R6,R1)         GET RMF PRODUCT SECTION ADDRESS
         USING SMF74MFV,R8         ESTABLISH PRODUCT ADDRESSABILITY
         MVC   DC74SAM,SMF74SAM    COPY NO. SAMPLES
         MVC   DC74PTN,SMF74PTN    GET LPAR NUMBER
         MVC   DC74IST,SMF74IST    COPY INTERVAL START TIME
         CNVTHMS SMF74IST,DWORK,DWORK2 CONVERT TO SECONDS
         CVB   R11,DWORK           CONVERT IT TO BINARY
         SR    R10,R10             CLEAR EVEN REGISTER
         M     R10,=F'100'         HUNDREDTHS OF SECONDS
         ST    R11,DC74STR         SAVE INTERVAL START
         LA    R1,DC74STR          POINT TO TIMESTAMP
         BAL   R4,RETIME           GO REFORMAT IT
         MVC   DC74DAT,SMF74DAT    GET INTERVAL START DATE
         MVC   DC74INT,SMF74INT    GET INTERVAL DURATION
         MVC   DC74BINT,SMF74INT   COPY INTERVAL DURATION AGAIN
         LA    R1,DC74BINT         POINT AT PACKED DURATION
         GOSUB RMFCHUND            GO CONVERT IT TO BINARY HUNDREDTHS
         DROP  R8                  END PRODUCT SECTION ADDRESSABILITY
         SPACE 1
         L     R1,SMF74DDS         GET OFFSET TO DEVICE DATA SECTION
         LA    R7,0(R6,R1)         GET ADDRESS OF DATA SECTION
         USING SMF74NUM,R7         ESTABLISH  DATA ADDRESSABILITY
         SR    R5,R5               CLEAR COUNTER
         LR    R8,R5               AND LENGTH
         LR    R10,R5              SET DEVICE COUNTER
         ICM   R5,B'0011',SMF74DDN  GET NUMBER OF DATA SECTIONS
         ICM   R8,B'0011',SMF74DDL  GET LENGTH OF DATA SECTIONS
         SPACE 1
LOOPDATA DS    0H                  RUN THROUGH THE DATA SECTIONS
         TM    SMF74CNF,X'FE'      ANY BAD FLAGS?
         BNZ   *+4+4               YES, SKIP IT
         BAL   R4,DATAPROC         GO PROCESS ONE SECTION
         LA    R7,0(R8,R7)         UPDATE DATA POINTER
         BCT   R5,LOOPDATA         GO THROUGH ALL SECTIONS
         SPACE 1
         B     MAINLINE            IN ALL THE RECORDS
         EJECT
***********************************************************************
*        END OF PROCESSING. TIDY UP, REPORT AND LEAVE.                *
***********************************************************************
         SPACE 1
STOPPGM  DS    0H                  NOW SHUTTING DOWN
*        GOSUB REPORT              GO ISSUE FINAL REPORT
         SPACE 3
         GOSUB CLOSEM              GO CLOSE FILES
         SPACE 2
ENDOFPGM DS    0H                  NOW SHUTTING DOWN
         PGMOUT RC=RETCODE         EXIT PROGRAM
         TITLE 'PERFORMANCE GROUP PROCESSING'
***********************************************************************
*        CREATE A SUMMARY RECORD FOR EACH ONLINE DEVICE               *
***********************************************************************
         SPACE 1
DATAPROC DS    0H                  PROCESS DEVICE DATA
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         MVC   DC74DCT,SMF74DCT    COPY DEVICE NUMBER
         MVC   DC74NUM,SMF74NUM    COPY DEVICE NUMBER
         MVC   DC74LCU,SMF74LCU    COPY LCU
         MVC   DC74SER,SMF74SER    COPY VOLSER
         MVC   DC74SSC,SMF74SSC    COPY SSC COUNT
         MVC   DC74MEC,SMF74MEC    COPY EVENT COUNT
         MVC   DC74CNN,SMF74CNN    COPY CONNECT TIME
         MVC   DC74PEN,SMF74PEN    COPY PENDING TIME
         MVC   DC74ATV,SMF74ATV    COPY ACTIVE  TIME
         MVC   DC74DIS,SMF74DIS    COPY DISCONNECT TIME
         MVC   DC74QUE,SMF74QUE    COPY IOSQ COUNT
         MVC   DC74ALC,SMF74ALC    COPY ALLOC COUNT
         MVC   DC74ICT,SMF74ICT    COPY INCORRECT SAMPLE COUNT
         MVC   DC74DVB,SMF74DVB    COPY DEVICE BUSY DELAY
         MVC   DC74CUB,SMF74CUB    COPY C.U. DELAY TIME
         MVC   DC74NDA,SMF74NDA    COPY TOTAL ALLOCATIONS
         MVC   DC74MTC,SMF74MTC    COPY TAPE MOUNTS
         SPACE 1
         LA    R0,DC74REC          POINT TO OUTPUT RECORD
         LA    R2,SYSUT2SW         POINT TO OUTPUT MDCB
         USING MDCB,R2             ESTABLISH ADDRESSABILITY
         GOSUB WRITING             GO OUTPUT RECORD
         SPACE 1
DATAPROX DS   0H                   RETURN TO CALLER
         LA    R10,1(,R10)         UPDATE DEVICE COUNTER
         SUBOUT RESTORE=(R4)       SUBROUTINE EXIT
         TITLE 'SUB-ROUTINES'
***********************************************************************
*        READ SUBROUTINE FOR SYSUT1                                   *
***********************************************************************
         SPACE 1
SYSUT1RD DS    0H                  READ A SUMMARY RECORD
         SUBIN ,                   SUBROUTINE ENTRY
         LA    R2,SYSUT1SW         POINT TO INPUT MDCB
         TM    MDCBFLAG,DDINT+DDPRES+DDOPEN TEST IF NEED TO READ
         BNO   SYSUT1RX            BRANCH IF NO
         GET   SYSUT1              READ IN A SUMMARY RECORD
         LR    R6,R1               REMEMBER ITS ADDRESS
         AP    MDCBCNT,ONE         INCREMENT RECORD COUNTER
         SPACE 1
SYSUT1RX DS    0H                  RETURN TO CALLER
         SUBOUT ,                  SUBROUTINE EXIT
         SPACE 1
         DROP  R2                  END MDCB ADDRESSABILITY
         EJECT ,
         COPY  RMFCHUND            USE STANDARD CODE FOR RMF INTERVAL
         COPY  RETIME              COPY SEAFOOD TIME FORMATTER
         EJECT ,
         COPY  PACK2               USE STANDARD CODE TO UNPACK A BYTE
         EJECT ,
         COPY  WRITING             USE STANDARD CODE FOR OUTPUT
         EJECT ,
         COPY  PUTMOVE             USE STANDARD CODE FOR QSAM
         EJECT ,
         COPY  OPENEM              USE STANDARD CODE FOR OPEN LOOP
         EJECT ,
         COPY  CLOSEM              USE STANDARD CODE FOR CLOSE LOOP
         EJECT ,
         COPY  OPENCLOS            USE STANDARD CODE FOR OPEN/CLOSE
         EJECT ,
         COPY  JFCBREAD            USE STANDARD CODE FOR READJFCB
         EJECT ,
         COPY  TESTDD              USE STANDARD CODE FOR DD TEST
         EJECT ,
         COPY  BLKSZRTN            USE STANDARD CODE FOR OPEN EXIT
         TITLE 'DATA AREAS'
BEGSNAP  DS    0D                  SNAP DATA AREAS FOLLOWING
***********************************************************************
*        CONSTANTS AND WORK AREAS                                     *
***********************************************************************
         SPACE 1
DWORK    DC    D'0'                DOUBLEWORD WORK AREA
DWORK2   DC    D'0'                DOUBLEWORD WORK AREA
ONE      DC    PL1'1'              COUNT INCREMENT
SIGN     EQU   X'F0'               POSITIVE PACKED SIGN
         SPACE 1
COMPCODE DC    H'0'                PROGRAM COMPLETION CODE
ZEROWORD DC    F'0'                A LOT OF NOTHING
RETCODE  DC    H'0'                MACRO RETURN CODE SAVE AREA
SNAPID   DC    X'00'               FOR IDENTIFYING ORIGIN OF SNAP
         TITLE 'DCB''S AND I/O CONTROL TABLES'
***********************************************************************
*        DCB'S TABLE                                                  *
***********************************************************************
         SPACE 1
DCBTABLE DS    0F
SYSUT1SW DC    X'00',AL3(SYSUT1),CL8'SYSUT1',CL8'INPUT',CL8'LEAVE'
CNTSUT1  DC    PL8'0'              INPUT RECORD COUNT FIELD
DCBNTRY  EQU   *-DCBTABLE          LENGTH OF ONE TABLE ELEMENT
SYSUT2SW DC    X'00',AL3(SYSUT2),CL8'SYSUT2',CL8'OUTPUT',CL8' '
CNTSUT2  DC    PL8'0'              OUTPUT RECORD COUNT FIELD
SNAPSW   DC    X'00',AL3(SNAPDCB),CL8'SNAP',CL8'OUTPUT',CL8' '
         DC    PL8'0'              RECORD COUNT FIELD
OPENSIZE EQU   *-DCBTABLE          TABLE SIZE OF FULL TIME FILES
         SPACE 1
MAXOPEN  EQU   OPENSIZE/DCBNTRY    MAXIMUM NUMBER OF DCB'S TO OPEN
TABSIZE  EQU   *-DCBTABLE          DCB'S TABLE SIZE
MAXDS    EQU   TABSIZE/DCBNTRY     MAXIMUM NUMBER OF DCB'S
         SPACE 2
EXITLIST DC    X'05',AL3(BLKSZRTN) ENTRY FOR DCB MODIFICATION
         DC    X'87',AL3(IHAJFCB)  ENTRY FOR READJFCB
         EJECT
***********************************************************************
*        DATA CONTROL BLOCKS                                          *
***********************************************************************
         SPACE 1
SYSUT2   DCB   DDNAME=SYSUT2,                                          *
               DSORG=PS,                                               *
               EXLST=EXITLIST,                                         *
               MACRF=PM,                                               *
               LRECL=DC74RECL
         SPACE 3
SYSUT1   DCB   DDNAME=SYSUT1,                                          *
               DSORG=PS,                                               *
               EODAD=STOPPGM,                                          *
               EXLST=EXITLIST,                                         *
               MACRF=GL
         SPACE 2
SNAPDCB  DCB   DDNAME=SNAP,DSORG=PS,MACRF=W,                           *
               RECFM=VBA,LRECL=125,BLKSIZE=882
         TITLE 'JFCB AREA'
***********************************************************************
*        JOB FILE CONTROL BLOCK STORAGE                               *
***********************************************************************
         SPACE 1
IHAJFCB  DS    0CL176              JFCB AREA
         IEFJFCBN LIST=YES         MAP JFCB
         SPACE 2
         TITLE 'LITERAL POOL'
***********************************************************************
*        LITERAL POOL                                                 *
***********************************************************************
         SPACE 1
         LTORG ,                   LITERAL POOL GOES HERE
         TITLE 'SUMMARY RECORD FORMAT'
         COPY  DC74REC             COPY SUMMARY RECORD FORMAT
         SPACE 3
EOSNAP   EQU   *                   END OF STORAGE AREA TO BE SNAPPED
         TITLE 'DSECT MAPS'
         COPY  MDCB                USE STANDARD CODE FOR MDCB
         EJECT
         PUSH  PRINT               SAVE PRINT STATUS
         PRINT ON,GEN              EXPAND SYSTEM DSECTS
         SPACE 3
         ERBSMFR 74                MAP DEVICE  ACTIVITY RECORD
         SPACE 3
LNG74DEV EQU   *-SMF74NUM          LENGTH OF DATA SECTION
         SPACE 1
         POP   PRINT               RESTORE PRINT STATUS
         EJECT
CVTPTR   EQU   16                  CVT POINTER
         SPACE 1
CVTMAP   DSECT                     CVT MAP
         SPACE 3
         DCBD  DSORG=PS,DEVD=DA    DCB DSECT
         SPACE 3
         END
