         MACRO
&SEGNAME RIN   &REGS,&LINK=R4
.*********************************************************************
.*       PDSDOC *RE-ENTRANT SUBROUTINE ENTRY MACRO....................* RIN
.*
.*       THIS MACRO CAN BE USED TO CONTROL THE LINKAGE BETWEEN
.*       SUBROUTINES AND EFFECT THE SAVING AND RESTORING OF
.*       LINK REGISTERS AND PARAMETER REGISTERS. A TYPICAL SEQUENCE
.*       MIGHT BE:
.*
.* DOWORK RIN   (R6,R7,R8) ENTER ROUTINE AND SAVE REGS 6,7,8
.*        ----
.*        ----
.*        ----
.*        ----
.*        CODEOUT                RESTORE REGS 6,7,8 AND RETURN
.*
.*       THE LINK REGISTER IS ASSUMED TO BE REGISTER 4 BUT OPERAND
.*       'LINK=' CAN BE USED TO VARY THIS AT WILL.
.*
.*       THE MACRO ASSUMES THAT AN AREA EXISTS IN THE PROGRAM CALLED
.*       'SGBXSAVE' AND WILL SAVE REGISTERS IN SUCCESSIVE FULLWORD
.*       LOCATIONS IN THAT AREA. IN ORDER TO GENERATE AN AREA OF THE
.*       CORRECT SIZE, THE MACRO CAN BE CODED WITH THE SINGLE
.*       OPERAND - 'GENERATE'.
.*
.*       THIS MACRO WAS DEVELOPED OUT OF 'SEGIN', SO SHARES MANY GLOBAL
.*       VARIABLES WITH THAT MACRO. THESE TWO MACROS MAY THEREFORE BE
.*       INTERMIXED WITHIN ONE PROGRAM.
.*
.*       01APR92 CART1 COPIED 'SEGIN', CHANGED TO MY REG. CONVENTIONS
.*
.*********************************************************************
.*
.*
.*
         GBLA  &SEGBCTR            COUNTER OF FULLWORDS USED
         LCLC  &WORKC              USED TO TIDY UP NAME FIELDS
         LCLA  &INDX               WORK INDEX
         GBLC  &SGBARR1(200)       HOLD SAVE REG DETAILS ACROSS SEGMNT
         GBLC  &SGBARR2(200)       HOLD SAVE REG DETAILS ACROSS SEGMNT
         GBLA  &SGBINDX            INDEX FOR THE ARRAY
.*********************************************************************
.*       TEST FOR 'GENERATE' TYPE ENTRY
.*********************************************************************
         AIF   ('&REGS' EQ 'GENERATE').GENER GENERATE WORK AREA
.*********************************************************************
.*       INITIALISE INEX DATA ETC.
.*********************************************************************
&SGBINDX SETA  1                   INITIALISE THE ARRAY INDEX
&SEGNAME DS    0H                  SEGMENT ENTRY POINT
.*********************************************************************
.*       SAVE THE LINK REGISTER
.*********************************************************************
&WORKC   SETC  'SGBXSAVE'.'+&SEGBCTR' GENERATE NEXT SAVE AREA NAME
&SEGBCTR SETA  &SEGBCTR+4          INCREMENT THE SAVE AREA CTR
&SGBARR1(&SGBINDX) SETC '&WORKC'   SAVE NAME OF AREA
&SGBARR2(&SGBINDX) SETC '&LINK'    SAVE REGISTER ID
&SGBINDX SETA  &SGBINDX+1          INCREMENT GLOBAL INDEX
         ST    &LINK,&WORKC        SAVE LINK REGISTER
.*********************************************************************
.*       SAVE REGISTERS AS REQUIRED
.*********************************************************************
         AIF   (T'&REGS EQ 'O').MEND1 EXIT IF NO REGS 2B SAVED
&INDX    SETA  1                   INITIALISE LOOP COUNTER
.LOOP1   ANOP                      LOOP FOR REGISTERS TO BE SAVED
&WORKC   SETC  'SGBXSAVE'.'+&SEGBCTR' GENERATE THE NEXT SLOT NAME
&SEGBCTR SETA  &SEGBCTR+4            GENERATE OFFS NEXT SAVE AREA
&SGBARR1(&SGBINDX) SETC '&WORKC'   SAVE NAME OF AREA
&SGBARR2(&SGBINDX) SETC '&REGS(&INDX)' SAVE REGISTER ID
&SGBINDX SETA  &SGBINDX+1          INCREMENT GLOBAL INDEX
         ST    &REGS(&INDX),&WORKC SAVE REGISTER.
&INDX    SETA  &INDX+1             INCREMENT LOOP COUNTER
         AIF   (&INDX LE N'&REGS).LOOP1 PROCESS ALL SPECIFIED OPERANDS
.*********************************************************************
.*       EXIT FROM THIS MACRO
.*********************************************************************
.MEND1  MEXIT
.*********************************************************************
.*      GERNERATE WORK AREA ENTRIES FOR THE REQUIRED SAVE AREAS
.*********************************************************************
.GENER   ANOP
SGBXSAVE DS    0F                  RIN SAVE AREAS
         DS    &SEGBCTR.X          STORAGE FOR SAVE AREAS
         MEND
