DAVE73PR TITLE 'PRINT CHANNEL UTILISATION'
***********************************************************************
*        PDSDOC *SHOW AVERAGE CHANNEL UTILISATION.....................* DCPCOMP2
*                                                                     *
*        PROGRAM ID. :             DAVE73PR                           *
*                                                                     *
*        DATE WRITTEN :            NOVEMBER  1998                     *
*                                                                     *
*        AUTHOR :                  D.H.CARTWRIGHT.                    *
*        COPYRIGHT:                DARNICK COMPUTING LTD.             *
*                                                                     *
*        LOCATION :                SUTTON COLDFIELD                   *
*                                  ENGLAND                            *
*                                                                     *
*        HISTORY  :                                                   *
*                                                                     *
* 02NOV98  DC   STARTED WRITING                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*       ---------                                                     *
*                                                                     *
* THIS PROGRAM IS DESIGNED TO SUMMARISE THE RECORDS EXTRACTED FROM    *
* SMF73 RECORDS BY DAVE73.  IT WILL REPORT ON AVERAGE CHANNEL         *
* UTILISATION ON CHANGE OF CHPID.                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*       ------------                                                  *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*       INPUT                                                         *
*       -----                                                         *
*                                                                     *
* 1)SYSUT1 - DEFINES THE INPUT FILE                                   *
*                                                                     *
* PARM=NOSUM INHIBITS DATA ACCUMULATION - IT REPORTS ON EVERY RECORD  *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*       OUTPUT                                                        *
*       ------                                                        *
*                                                                     *
* 1)SYSUT2   - CSD FILE, VB RECORDS                                   *
*                                                                     *
* 2)SYSPRINT - MESSAGE DATASET. VBA RECORDS.                          *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         PRINT ON,GEN              SHOW MACRO EXPANSIONS
*        PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
         SPACE 1
DAVE73PR CSECT ,                   MAIN CSECT
         SPACE 1
***********************************************************************
*        REGISTER EQUATES AND USAGES.                                 *
***********************************************************************
         SPACE 1
R0       EQU   0
R1       EQU   1                   WORK
R2       EQU   2                   DCB TABLE POINTER,STRING LENGTH
REG2     EQU   R2                  WORK REGISTER IN 'SCAN' MACRO
R3       EQU   3                   INPUT RECORD POINTER
R4       EQU   4                   LINKAGE RETURN
R5       EQU   5                   .
R6       EQU   6                   .
R7       EQU   7                   .
R8       EQU   8                   .
R9       EQU   9                   .
R10      EQU   10                  .
R11      EQU   11                  .
R12      EQU   12                  BASE REGISTER 2
R13      EQU   13                  SAVE AREA / BASE REG. 1
R14      EQU   14                  .
R15      EQU   15                  .
         SPACE 1
         PGMIN EQUATES=NO,USING=(R13,R12) STANDARD HOUSEKEEPING
         GOSUB PARMPROC            GO CHECK PARM VALUE
         GOSUB INITIAL             PERFORM INITIALISATION
         LA    R2,SYSUT1SW         POINT TO INPUT FILE
         BAL   R4,GETTING          GO GET FIRST RECORD
         LR    R3,R1               COPY RECORD ADDRESS
         USING DC73REC,R3          ESTABLISH RECORD ADDRESSABILITY
         BAL   R4,ADDACC           PRIME ACCUMULATORS
         SR    R1,R1               CLEAR REGISTER
         IC    R1,DC73PTN          PICK UP LPAR NUMBER
         BAL   R4,CONVWORD         MAKE PRINTABLE
         MVC   HEADLPAR,CONVWORK+13 COPY TO TITLE
         EJECT
***********************************************************************
*        MAIN PROCESSING ROUTINE                                      *
***********************************************************************
         SPACE 1
MAINLINE DS    0H                  PROGRAM FUNCTION
         SPACE 1
         LA    R2,SYSUT1SW         POINT TO INPUT FILE
         BAL   R4,GETTING          GO GET SOME DATA
         LR    R3,R1               COPY RECORD ADDRESS
         TM    PARMFLAG,PARMGOOD   PARM=NOSUM?
         BO    NEWCHAN             YES, REPORT ON EVERY RECORD
         CLC   DC73PID,ACCHPID     IS IT FOR THE SAME CHANNEL?
         BNE   NEWCHAN             NO, SUMMARISE AT CHPID LEVEL
         BAL   R4,ADDACC           YES, ACCUMULATE DATA
         B     MAINLINE            THEN GET MORE
         SPACE 2
NEWCHAN  DS    0H                  KEY BREAK PROCESSING
         BAL   R4,CSDOUT           GO OUTPUT COMMA SEPARATED DATA
         BAL   R4,CLEARAC          CLEAR OLD VALUES
         BAL   R4,ADDACC           START NEW ONES
         B     MAINLINE            GO ROUND THE LOOP
         EJECT
***********************************************************************
*        END OF PROCESSING. TIDY UP, REPORT AND LEAVE.                *
***********************************************************************
         SPACE 1
STOPPGM  DS    0H                  NOW SHUTTING DOWN
         BAL   R4,CSDOUT           GO OUTPUT LAST COMMA SEPARATED DATA
         GOSUB REPORT              GO ISSUE FINAL REPORT
         SPACE 1
         GOSUB CLOSEM              GO CLOSE ALL FILES
         SPACE 2
ENDOFPGM DS    0H                  NOW SHUTTING DOWN
         PGMOUT RC=RETCODE         EXIT PROGRAM
         TITLE 'CREATE CSD FILE'
***********************************************************************
*        CREATE COMMA SEPARATED DATA AND DETAIL REPORT                *
***********************************************************************
         SPACE 1
CSDOUT   DS    0H                  CREATE SUMMARY DATA
         RIN   ,                   SUBROUTINE ENTRY
         LA    R1,ACDATE           POINT TO DATE FIELD
         BAL   R4,REDATE           GO REFORMAT IT A LA SEAFOOD
         SR    R1,R1               CLEAR REGISTER
         IC    R1,ACDATE+1         PICK UP MONTH (SEAFOOD)
         BAL   R4,CONVWORD         MAKE PRINTABLE
         MVC   CSDMONTH,CONVWORK+13 COPY TO OUTPUT
         MVC   DLMONTH,CSDMONTH    COPY TO REPORT LINE
         SR    R1,R1               CLEAR REGISTER
         IC    R1,ACDATE+2         PICK UP DAY (SEAFOOD)
         BAL   R4,CONVWORD         MAKE PRINTABLE
         MVC   CSDDAY,CONVWORK+13  COPY TO OUTPUT
         MVC   DLDAY,CSDDAY        COPY TO REPORT LINE
         LTR   R4,R4               TEST NO. SIGNIFCANT DIGITS
         BNZ   *+4+4               TWO, DON'T BLANK
         MVI   DLDAY,C' '          NO LEADING ZERO ON REPORT
         MVC   DWORK+4,ACTIME      MOVE TIME TO WORK AREA
         BAL   R4,FRIGIT           RE-POSITION FIELDS
         MVC   DLTIME,EDITTIME     PUT EDIT MASK IN PLACE
         ED    DLTIME,FRIGWORK     PUT TIME IN REPORT
         MVC   CSDHOUR,DLTIME+1    COPY TO DATA RECORD
         CLI   CSDHOUR,C' '        AFTER 12?
         BNE   *+4+4               YES, LEAVE ALONE
         MVI   CSDHOUR,C'0'        NO LEADING ZERO ON REPORT
         SPACE 1
         LA    R1,ACCHPID          POINT TO CHPID
         LA    R15,CSDCHPID        POINT TO CHARACTER FIELD
         LA    R0,1                SET LENGTH ONE BYTE
         BAL   R4,UNPACK           GO CONVERT HEX
         MVI   CSDCHPID+2,C','     INSERT A COMMA
         MVC   DLCHPID,CSDCHPID    COPY TO REPORT LINE
         SPACE 1
         SR    R6,R6               CLEAR EVEN REGISTER
         L     R7,ACBSY            GET NUMBER OF BUSY SAMPLES
         M     R6,=F'100000'       PERCENT TO THREE DECIMALS
         D     R6,ACSMP            OF TOTAL SAMPLES
         LR    R1,R7               GET RESULT
         LA    R1,5(,R1)           ROUND IT
         LR    R2,R1               REMEMBER IT IN SHORT TERM
         BAL   R4,CONVWORD         CONVERT TO PRINTABLE
         BAL   R4,FRIGIT           KNOCK OFF THE ROUNDING DIGIT
         MVC   CSDTOTAL,CONVWORK+9 EXTRACT ANSWER
         MVC   DLTOTAL,EDITPCNT    MOVE EDIT MASK TO PRINT LINE
         ED    DLTOTAL,FRIGWORK    MAKE RESULT PRINTABLE
         SR    R6,R6               CLEAR EVEN REGISTER
         L     R7,ACPBY            GET LPAR BUSY TIME
         M     R6,=F'100000'       PERCENT TO THREE DECIMALS
         D     R6,ACPTI            OF TOTAL TIME
         LR    R1,R7               GET RESULT
         LA    R1,5(,R1)           ROUND IT
         LR    R6,R1               REMEMBER IT IN SHORT TERM
         BAL   R4,CONVWORD         CONVERT TO PRINTABLE
         BAL   R4,FRIGIT           KNOCK OFF THE ROUNDING DIGIT
         MVC   CSDLPAR,CONVWORK+9  EXTRACT ANSWER
         MVC   DLLPAR,EDITPCNT     MOVE EDIT MASK TO PRINT LINE
         ED    DLLPAR,FRIGWORK     MAKE RESULT PRINTABLE
         SR    R1,R1               DEFAULT ANSWER IS ZERO
         CR    R2,R6               HOW DOES IT LOOK COMPARED TO TOTAL?
         BL    CSDOTHER            WRONG, IGNORE IT
         SR    R2,R6               WORK OUT THE DIFFERENCE
         LR    R1,R2               PUT IN CORRECT REGISTER
CSDOTHER DS    0H                  SHOW OTHER UTILISATION
         BAL   R4,CONVWORD         CONVERT TO PRINTABLE
         BAL   R4,FRIGIT           KNOCK OFF THE ROUNDING DIGIT
         MVC   DLOTHER,EDITPCNT    MOVE EDIT MASK TO PRINT LINE
         ED    DLOTHER,FRIGWORK    MAKE RESULT PRINTABLE
         SPACE 1
         LA    R2,SYSUT2SW         GET MDCB ADDRESS
         LA    R0,CSDREC           POINT AT OUTPUT RECORD
         GOSUB WRITEV              GO OUTPUT RECORD
         LA    R0,DETAILNN         POINT AT DETAIL REPORT
         GOSUB PRINTV              GO OUTPUT REPORT LINE
         SPACE 1
CSDOUTX  DS    0H                  END OF ROUTINE
         ROUT  ,                   RETURN TO CALLER
         SPACE 2
FRIGIT   DS    0H                  DATA SHUFFLE ROUTINE
         ZAP   FRIGWORK,ONE        CLEAR WORK AREA
         MVO   FRIGWORK,DWORK+4(3) DIVIDE RESULT BY TEN
         BR    R4                  RETURN TO CALLER
         SPACE 1
FRIGWORK DC    PL3'0'              WORK AREA FOR UN-ROUNDING
         TITLE 'ACCUMULATORS PROCESSING'
***********************************************************************
*        CLEAR ALL ACCUMULATOR FIELDS                                 *
***********************************************************************
         SPACE 1
CLEARAC  DS    0H                  CLEAR ACCUMULATORS
         RIN   ,                   SUBROUTINE ENTRY
         SR    R1,R1               CLEAR A REGISTER
         ST    R1,ACSMP            USE TO CLEAR NO. SAMPLES
         ST    R1,ACBSY            USE TO CLEAR BUSY SAMPLES
         ST    R1,ACPTI            USE TO CLEAR INTERVAL TIME
         ST    R1,ACPBY            USE TO CLEAR BUSY INTERVALS
         STCM  R1,B'1111',ACDATE   USE TO CLEAR DATE
         STCM  R1,B'1111',ACTIME   USE TO CLEAR TIME
         STC   R1,ACCHPID          USE TO CLEAR CHPID
         SPACE 2
CLEARACX DS    0H                  END OF ROUTINE
         ROUT  ,                   RETURN TO CALLER
         SPACE 2
***********************************************************************
*        ADDS VALUE TO ACCUMULATOR FIELDS                             *
***********************************************************************
         SPACE 1
ADDACC   DS    0H                  ACCUMULATE VALUES
         RIN   ,                   SUBROUTINE ENTRY
         L     R1,ACSMP            GET NO. SAMPLES
         A     R1,DC73SMP          ADD NO. SAMPLES
         ST    R1,ACSMP            SAVE UPDATED NO. SAMPLES
         L     R1,ACBSY            GET BUSY SAMPLES
         A     R1,DC73BSY          ADD BUSY SAMPLES
         ST    R1,ACBSY            SAVE UPDATED BUSY SAMPLES
         L     R1,ACPTI            GET INTERVAL TIME
         A     R1,DC73PTI          ADD INTERVAL TIME
         ST    R1,ACPTI            SAVE UPDATED INTERVAL TIME
         L     R1,ACPBY            GET BUSY TIME
         A     R1,DC73PBY          ADD BUSY TIME
         ST    R1,ACPBY            SAVE UPDATED BUSY TIME
         MVC   ACDATE,DC73DAT      COPY DATE OF START
         MVC   ACTIME,DC73IST      SAVE INTERVAL START TIME
         MVC   ACCHPID,DC73PID     .. AND CHPID
         SPACE 2
ADDACCX  DS    0H                  END OF ROUTINE
         ROUT  ,                   RETURN TO CALLER
         TITLE 'INITIALISATION PROCESSING'
***********************************************************************
*        OPEN SUPPLIED DD CARDS ETC.                                  *
***********************************************************************
         SPACE 1
INITIAL  DS    0H                  PROGRAM INITIALISATION
         RIN   ,                   SUBROUTINE ENTRY
         BAL   R4,OPENEM           GO OPEN SUPPLIED DD CARDS
         LA    R4,DETAILH          POINT TO DETAIL REPORT SUBTITLE
         ST    R4,ASUBTIT          TELL PRINT ROUITNE TO USE THAT
         SR    R1,R1               ASK FOR CURRENT TIME
         GOSUB TODDATE             GO FORMAT TOD-CLOCK
         DATE  HEADDATE,FORMAT='DD MMM YY',FROM=TODPJUL FORMAT DATE ..
         MVC   HEADTIME,TODTIME    .. AND TIME IN HEADING
         SPACE 2
INITIALX DS    0H                  END OF INITIALISATION
         ROUT  ,                   RETURN TO CALLER
         TITLE 'SUB-ROUTINES'
***********************************************************************
*        OUTPUT RECORD COUNT REPORT                                   *
***********************************************************************
         SPACE 1
REPORT   SUBIN SAVE=(R4)
         AP    LINECOUN,ONE        ADD ONE TO LINE COUNTER
         AP    LINECOUN,MAXLINE    THEN ENSURE PAGE SKIP
         LA    R4,RPTAHDR2         POINT TO THIS REPORT SUBTITLE
         ST    R4,ASUBTIT          TELL PRINT ROUTINE TO USE THAT
         LA    R0,RPTAHDR3         POINT AT HEADINGS
         GOSUB PRINTV              GO OUTPUT LINE
         SPACE 2
***********************************************************************
*        CREATE REPORT DETAIL LINE 1                                  *
***********************************************************************
         SPACE 1
         MVC   RPTADITM(L'RADIRECS),RADIRECS MOVE ITEM NAME
         LA    R2,SYSUT1SW         POINT TO MDCB
         USING MDCB,R2             ESTABLISH MDCB ADDRESSABILITY
         CVB   R1,MDCBCNT          GET INPUT COUNT
         LR    R0,R1               SAVE VALUE
         LA    R15,RPTADIN+4       POINT TO RIGHT BOUNDARY -1
         BAL   R10,RPTMOVE         GO FILL IN THE VALUE
         LA    R2,SYSUT2SW         POINT TO OUTPUT MDCB
         CVB   R1,MDCBCNT          GET RECORD COUNT
         LR    R3,R1               SAVE VALUE
         LA    R15,RPTADOUT+5      POINT TO RIGHT BOUNDARY -1
         BAL   R10,RPTMOVE         GO FILL IN THE VALUE
         LR    R1,R3               RESTORE INPUT VALUE
         BAL   R4,PERCENT0         GO CALCULATE PERCENTAGE
         LA    R15,RPTADPCT+2      POINT TO RIGHT BOUNDARY -1
         BAL   R10,RPTMOVE         GO FILL IN THE VALUE
         LA    R0,RPTADETL         POINT AT DETAIL LINE
         GOSUB PRINTV              GO OUTPUT LINE
         SPACE 1
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         SPACE 1
         DROP  R2                  END MDCB ADDRESSABILITY
         SPACE 2
***********************************************************************
*        OUTPUT A VALUE                                               *
*        ON INPUT R1  = VALUE                                         *
*                 R10 = RETURN ADDRESS                                *
*                 R15-->OUTPUT FIELD RIGHT BOUNDARY MINUS ONE         *
***********************************************************************
         SPACE 1
RPTMOVE  DS    0H                  DATA MOVING
         BAL   R4,CONVWORD         GO CONVERT VALUE
         SR    R15,R4              START OF FIGURES
         EX    R4,RPTMVC           MOVE FIGURES INTO REPORT
         SPACE 1
RPTMOVED DS    0H                  END OF SUB-ROUTINE
         BR    R10                 RETURN TO CALLER
         SPACE 1
RPTMVC   MVC   0(*-*,R15),0(R1)    PUT VALUE IN COLUMN        *EXECUTE*
         EJECT
         TITLE 'COPY BLOCKS'
         COPY  BIOS                STANDARD I/O PROCESSING
         EJECT ,
DAVE73PR CSECT ,                   MAIN CSECT
         COPY  GETTINV             INCLUDE GET/LOCATE (VARIABLE)
*        COPY  GETTING             INCLUDE GET/LOCATE
         EJECT ,
         COPY  PRINTV              INCLUDE PRINT MECHANISM
         EJECT ,
         COPY  WRITEV              INCLUDE VB WRITE
         SPACE 2
         COPY  PUTMOVE             INCLUDE STANDARD I/O SKELETON
         EJECT ,
         COPY  CONVWORD            INCLUDE STANDARD I/O SKELETON
         EJECT ,
         COPY  PARMPROC            STANDARD TEST FOR ONE PARM
         SPACE 1
PARMADDR DC    A(0)                ADDRESS OF PARM FIELD
PARMLGTH DC    A(0)                LENGTH OF PARM FIELD
PARMFLAG DC    XL1'00'             FLAGS FOR PARM VALUES
GOODPARM DC    C'NOSUM'            VALID PARM OPTION
PARMGOOD EQU   X'80'               AND ITS FLAG VALUE
         EJECT ,
         COPY  CALLIT              STANDARD DYNAMIC LINKAGE
         EJECT ,
         COPY  UNPACK              STANDARD HEX PRINTING
         EJECT ,
         COPY  BLANK               STANDARD STORAGE BLANKING
         SPACE 2
         COPY  PERCENT0            STANDARD PERCENTAGE SUMS
         COPY  REDATE              CODE TO REFORMAT DATES
         EJECT ,
         COPY  TODDATE             STANDARD DATE FORMATTING
         TITLE 'DATA AREAS'
BEGSNAP  DS    0D                  SNAP DATA AREAS FOLLOWING
***********************************************************************
*        CONSTANTS AND WORK AREAS                                     *
***********************************************************************
         SPACE 2
         DS    0F                  GET ADDRESSES ALIGNED
EYECATCH DC    CL8'EYECATCH'       EYE CATCHER
         SPACE 1
DWORK    DC    D'0'                DOUBLEWORD WORK AREA
ZEROWORD DC    F'0'                A LOT OF NOTHING
COMPCODE DC    H'0'                PROGRAM COMPLETION CODE
RETCODE  DC    H'0'                MACRO RETURN CODE SAVE AREA
MAXLEN   DC    H'64'               MAXIMUM STRING LENGTH
         SPACE 1
CONVWORK DC    CL15' '             DECIMAL WORK AREA
ONE      DC    PL1'1'              COUNT INCREMENT
SIGN     EQU   X'F0'               POSITIVE PACKED SIGN
         SPACE 1
ACCHPID  DC    XL1'00'             LAST CHPID WE WERE PROCESING
ACDATE   DC    AL4(0)              LAST DAY WE WERE PROCESSING
ACTIME   DC    AL4(0)              LAST TIME WE WERE PROCESSING
ACSMP    DC    A(0)                ACCUMULATE NO. SAMPLES
ACBSY    DC    A(0)                ACCUMULATE BUSY SAMPLES
ACPTI    DC    A(0)                ACCUMULATE CHANNEL INTERVAL TIME
ACPBY    DC    A(0)                ACCUMULATE CHANNEL BUSY TIME
         SPACE 1
         RIN   GENERATE            SUBROUTINE SAVE AREA
         TITLE 'SPREADSHEET RECORDS'
***********************************************************************
*        CSD RECORD FORMAT                                            *
***********************************************************************
         SPACE 1
CSDREC   DC    AL2(CSDRECLN)       LENGTH OF CSD RECORD        CC
         DC    AL2(0)
CSDMONTH DC    CL2'  '             MONTH                        5
         DC    CL1','              PADDING                      7
CSDDAY   DC    CL2'  '             DAY                          8
         DC    CL1','              PADDING                     10
CSDHOUR  DC    CL2'  '             HOUR                        11
         DC    CL1','              PADDING                     13
CSDCHPID DC    CL2'  '             CHPID                       14
         DC    CL1','              PADDING                     16
CSDLPAR  DC    CL5'  '             LPAR UTILISATION (2 DEC.)   17
         DC    CL1','              PADDING                     22
CSDTOTAL DC    CL5'  '             TOTAL UTILISATION (2 DEC.)  23
         SPACE 1
CSDRECLN EQU   *-CSDREC            LENGTH OF CSD RECORD        28
         TITLE 'REPORT LINES'
***********************************************************************
*        REPORT LINE SKELETONS                                        *
***********************************************************************
         SPACE 1
TITLE1   EQU   *                   STANDARD NAME
RPTAHDR1 DC    AL2(RPTAH1LN)       LENGTH OF HEADER RECORD     CC
         DC    AL2(0)
RPTAEJCT DC    C'1'                SKIP TO TOP OF FORM          1
         DC    CL10' '             PADDING                      2
         DC    CL5'LPAR '          TITLE                       12
HEADLPAR DC    CL2' '              PARTITION NUMBER            17
         DC    CL1' '              PADDING                     19
         DC    CL35'CHANNEL UTILISATION SUMMARY REPORT' TITLE  20
         DC    CL10' '             PADDING                     55
HEADDATE DC    CL9' '              REPORT DATE                 65
         SPACE 1
         DC    CL8' '              PADDING                     75
HEADTIME DS    0CL5                TIME OF REPORT              83
HEADHH   DC    CL2' '              CHPID OF REPORT             83
         DC    CL1'.'              SEPARATOR                   84
HEADMM   DC    CL2' '              MINUTE OF REPORT            85
         DC    CL6' '              PADDING                     87
         DC    CL4'PAGE'                                       23
HEADPAGE DC    CL4'0000'           PAGE NUMBER                127
         SPACE 1
RPTAH1LN EQU   *-RPTAHDR1          LENGTH OF HEADER RECORD    130
         SPACE 2
SUBTITLE EQU   *                   STANDARD NAME, DUMMY RECORD
RPTAHDR2 DC    AL2(RPTAH2LN)       LENGTH OF TITLE  RECORD     CC
         DC    AL2(0)
         DC    C'-'                ASA SKIP 3                   1
         DC    CL1' '              PADDING                      2
         SPACE 1
RPTAH2LN EQU   *-RPTAHDR2          LENGTH OF HEADER RECORD      .
         EJECT ,
***********************************************************************
*        HERE ARE DETAIL RECORDS                                      *
***********************************************************************
         SPACE 1
DETAILH  DC    AL2(DETAILHL)       LENGTH OF DETAIL HEADER     CC
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
         DC    CL2' '              PADDING                      5
         DC    CL5'MONTH'          TITLE                        7
         DC    CL2' '              PADDING                     12
         DC    CL3'DAY'            TITLE                       14
         DC    CL6' '              PADDING                     17
         DC    CL4'TIME'           TITLE                       23
         DC    CL3' '              PADDING                     27
         DC    CL5'CHPID'          TITLE                       30
         DC    CL3' '              PADDING                     35
         DC    CL7'TOTAL %'        TITLE                       38
         DC    CL4' '              PADDING                     45
         DC    CL6'LPAR %'         TITLE                       49
         DC    CL6' '              PADDING                     55
         DC    CL7'OTHER %'        TITLE                       61
         SPACE 1
DETAILHL EQU   *-DETAILH           LENGTH OF DETAIL HEADER     67
         SPACE 2
DETAILNN DC    AL2(DETAILLN)       LENGTH OF DETAIL RECORD     CC
         DC    AL2(0)
         DC    C' '                ASA SKIP 1                   4
         DC    CL4' '              PADDING                      5
DLMONTH  DC    CL2' '              MONTH                        9
         DC    CL3' '              PADDING                     11
DLDAY    DC    CL2' '              DAY                         14
         DC    CL5' '              PADDING                     16
DLTIME   DC    CL6' '              TIME                        21
         DC    CL5' '              PADDING                     27
DLCHPID  DC    CL2' '              CHANNEL IDENT               32
         DC    CL4' '              PADDING                     34
DLTOTAL  DC    CL7'       '        TOTAL UTILISATION           38
         DC    CL3' '              PADDING                     45
DLLPAR   DC    CL7'       '        LPAR  UTILISATION           48
         DC    CL6' '              PADDING                     55
DLOTHER  DC    CL7'       '        UTILISATION FROM OTHER LPAR 61
         SPACE 1
DETAILLN EQU   *-DETAILNN          LENGTH OF DETAIL RECORD     67
***********************************************************************
*        HERE ARE STATISTICS MESSAGES SKELETONS                       *
***********************************************************************
         SPACE 1
RPTAHDR3 DC    AL2(RPTAH3LN)       LENGTH OF HEADINGS RECORD   CC
         DC    AL2(0)
         DC    C'-'                ASA SKIP 3                   1
         DC    CL8' '              PADDING                      2
         DC    CL20'ITEM'          HEADING                     10
         DC    CL20'INPUT'         HEADING                     30
         DC    CL20'OUTPUT'        HEADING                     50
         DC    CL3'  %'            HEADING                     70
         SPACE 1
RPTAH3LN EQU   *-RPTAHDR3          LENGTH OF HEADING RECORD    77
         SPACE 2
RPTADETL DC    AL2(RPTADTLN)       LENGTH OF DETAIL RECORD     CC
         DC    AL2(0)
RPTADASA DC    C'0'                ASA SKIP 2                   1
         DC    CL8' '              PADDING                      2
RPTADITM DC    CL20' '             ITEM                        10
RPTADIN  DC    CL20' '             INPUT COUNT                 30
RPTADOUT DC    CL20' '             OUTPUT COUNT                50
RPTADPCT DC    CL3' '              PERCENT                     70
         SPACE 1
RPTADCLN EQU   *-RPTADITM          LENGTH TO BLANK
RPTADTLN EQU   *-RPTADETL          LENGTH OF DETAIL RECORD     77
         SPACE 1
RADIRECS DC    C'RECORDS'          ITEM TYPE
RADIBYTS DC    C'BYTES'            ITEM TYPE
RADIAVG  DC    C'AVERAGE'          ITEM TYPE
         SPACE 1
REPTCOUN DC    PL2'0'              COUNT OF REPORTS
PAGECOUN DC    PL2'0'              COUNT OF PAGES
LINECOUN DC    PL2'60'             COUNT OF LINES
MAXLINE  DC    PL2'56'             MAXIMUM NUMBER OF LINES PER PAGE
         SPACE 1
EDITPL2  DC    X'40202120'         COPIES EDIT MASK
EDITPL4  DC    X'4020202020202120' COUNT EDIT MASK
EDITPL8  DC    X'40202020202020202020202020202120' COUNT EDIT MASK
EDITPCNT DC    X'402021204B2020'   PERCENTAGE EDIT MASK
EDITTIME DC    X'4021207A2020'     HHMM EDIT MASK
         TITLE 'LITERAL POOL'
***********************************************************************
*        LITERAL POOL                                                 *
***********************************************************************
         SPACE 1
         LTORG ,                   LITERAL POOL GOES HERE
         TITLE 'DCB''S AND I/O CONTROL TABLES'
***********************************************************************
*        DCB'S TABLE                                                  *
***********************************************************************
         SPACE 1
DCBTABLE DS    0F
SYSUT1SW DC    X'00',AL3(SYSUT1),CL8'SYSUT1',CL8'INPUT',CL8' '
         DC    PL8'0'              RECORD COUNT
DCBNTRY  EQU   *-DCBTABLE          LENGTH OF ONE TABLE ELEMENT
         SPACE 1
SYSUT2SW DC    X'00',AL3(SYSUT2),CL8'SYSUT2',CL8'OUTPUT',CL8' '
         DC    PL8'0'              RECORD COUNT
         SPACE 1
PRINTSW  DC    X'00',AL3(SYSPRINT)
         DC    CL8'SYSPRINT'
         DC    CL8'OUTPUT'
         DC    CL8'LEAVE'
         DC    PL8'0'              RECORD COUNT
         SPACE 1
OPENSIZE EQU   *-DCBTABLE          TABLE SIZE OF FULL TIME FILES
MAXOPEN  EQU   OPENSIZE/DCBNTRY    MAXIMUM NUMBER OF DCB'S TO OPEN
         SPACE 1
TABSIZE  EQU   *-DCBTABLE          DCB'S TABLE SIZE
MAXDS    EQU   TABSIZE/DCBNTRY     MAXIMUM NUMBER OF DCB'S
         SPACE 2
         EJECT ,
         PUSH  PRINT               SAVE PRINT STATUS
         PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
***********************************************************************
*        DATA CONTROL BLOCKS                                          *
***********************************************************************
         SPACE 1
SYSUT1   DCB   DDNAME=SYSUT1,                                          *
               DSORG=PS,                                               *
               EODAD=STOPPGM,                                          *
               EXLST=EXITLIST,                                         *
               MACRF=GL
         SPACE 2
SYSUT2   DCB   DDNAME=SYSUT2,                                          *
               DSORG=PS,                                               *
               LRECL=CSDRECLN,                                         *
               EXLST=EXITLIST,                                         *
               MACRF=PM
         SPACE 2
SYSPRINT DCB   DDNAME=SYSPRINT,                                        *
               DSORG=PS,                                               *
               RECFM=VBA,LRECL=137,                                    *
               EXLST=EXITLIST,                                         *
               MACRF=PM
         SPACE 2
SNAPDCB  DCB   DDNAME=SNAP,DSORG=PS,MACRF=W,                           *
               RECFM=VBA,LRECL=125,BLKSIZE=882
         SPACE 2
WTOIOE   DS    0F
         DC    AL2(WTOEND-*),H'0',CL109'******** - I/O ERROR -'
WTOEND   EQU   *
EOSNAP   DS    0H                  END OF STORAGE AREA TO BE SNAPPED
         TITLE 'INPUT RECORD'
DC73R    DSECT ,                   STORAGE MAPPING, NOT RESERVATION
         COPY DC73REC              INCLUDE DATA RECORD
         EJECT ,
         POP   PRINT               RESTORE PRINT STATUS
         CPB   DSECT               MAP PROGRAM CALL CB
         SPACE 3
         END
