SENDOFF  TITLE 'SPRECHER ENERGIE NON-WORK DAYS OUT FROM FILE'
***********************************************************************
*        PDSDOC *USER EXIT FOR SMFDUMP PROGRAM TO ONLY SELECT WORKDAYS* SENDOFF
*                                                                     *
*                                                                     *
*        PROGRAM ID. :             SENDOFF                            *
*                                                                     *
*        DATE WRITTEN :            JANUARY 1992                       *
*                                                                     *
*        AUTHOR :                  D.H.CARTWRIGHT.                    *
*                                                                     *
*        LOCATION :                SPRECHER ENERGIE                   *
*                                  KIRCHWEG 5                         *
*                                  5036 OBERENTFELDEN                 *
*                                  SWITZERLAND                        *
*                                                                     *
*        HISTORY  :                                                   *
*                                                                     *
* 10JAN92 CART1 STARTED TO WRITE                                      *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*       ---------                                                     *
*                                                                     *
* THIS PROGRAM IS A USER EXIT FOR THE SMF DUMP PROGRAM, IFASMFDP.     *
* IT IN TURN CALLS THE USAF PROGRAM 'GETDATE' TO DETERMINE IF THE SMF *
* RECORD IS FOR A WORKDAY. IF NOT, THE RECORD IS REJECTED.            *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*       ------------                                                  *
*                                                                     *
* 1) THE PROGRAM DOES A LOAD FOR 'GETDATE', BUT DOES NOT DELETE IT.   *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*       INPUT                                                         *
*       -----                                                         *
*                                                                     *
* 1)USER EXIT PARAMETER LIST FROM IFASMFDP. THE THIRD WORD OF THE     *
*   USER WORK AREA PASSED TO THE EXIT IS USED TO HOLD THE ADDRESS     *
*   OF THE 'GETDATE' ROUTINE. IF THIS IS ZERO A 'LOAD' OPERATION      *
*   IS PERFORMED FOR 'GETDATE'.                                       *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*       OUTPUT                                                        *
*       ------                                                        *
*                                                                     *
* 1)RETURN CODE TO SAY WHETHER RECORD SHOULD BE ACCEPTED (ZERO) OR NOT*
*                                                                     *
***********************************************************************
         EJECT
         PRINT ON,GEN              SHOW MACRO EXPANSIONS
*        PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
         SPACE 1
SENDOFF  CSECT
         SPACE 1
***********************************************************************
*        REGISTER EQUATES AND USAGES.                                 *
***********************************************************************
         SPACE 1
R0       EQU   0
R1       EQU   1                   WORK/LINKAGE
R2       EQU   2
REG2     EQU   R2                  WORK REGISTER IN 'SCAN' MACRO
R3       EQU   3
R4       EQU   4                   LINKAGE RETURN
R5       EQU   5
R6       EQU   6                   POINTER TO USER EXIT WORK AREA
R7       EQU   7                   POINTER TO SMF RECORD
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                  BASE REGISTER 1
R13      EQU   13                  SAVE AREA
R14      EQU   14                  WORK/LINKAGE
R15      EQU   15                  WORK/LINKAGE
         SPACE 1
         PGMIN EQUATES=NO,USING=(R12) STANDARD HOUSEKEEPING
         EJECT
***********************************************************************
*        PROGRAM INITIALISATION. CHECK FOR 'GETDATE' AND LOAD IT      *
*                                                                     *
***********************************************************************
         SPACE 1
         LM    R6,R8,0(R1)         PICK UP USER EXIT PARAMETER LIST
         LTR   R7,R7               DO WE HAVE AN SMF RECORD?
         BZ    ENDOFPGM            NO, ABANDON PROCESSING
         USING SMFREC,R7           ESTABLISH SMF RECORD ADDRESSABILITY
         ICM   R15,B'1111',8(R6)   GET THIRD WORD OF WORK AREA
         BNZ   CHECKDAY            HAVE ALREADY LOADED 'GETDATE'
         LOAD  EPLOC=CDATER        LOCATE DATE CHECK ROUTINE
         ST    R0,8(,R6)           SAVE ENTRY POINT
         LR    R15,R0              AND COPY IT
         EJECT
***********************************************************************
*        MAIN PROCESSING ROUTINE                                      *
*                                                                     *
* SET UP PARAMETERS AND CALL 'GETDATE'. SET RETURN CODE DEPENDING ON  *
* DAY OF WEEK AND HOLIDAY FLAG.                                       *
***********************************************************************
         SPACE 1
CHECKDAY DS    0H                  TEST DATE OF SMF RECORD
         MVC   PJULIAN,SMFDATE     COPY SMF DATE
         CALL  (15),(DATE,DAY),VL  CALL 'GETDATE'
         CLI   DAY,C'S'            IS IT A WEEKEND DAY?
         BE    DISCARD             YES, DO NOT WANT IT
         CLI   HOLIDAY,C'H'        IS IT A HOLIDAY?
         BE    DISCARD             YES, IGNORE
         SR    R15,R15             SET TO ACCEPT SMF RECORD
         B     ENDOFPGM            RETURN TO IFASMFDP
         SPACE 1
DISCARD  DS    0H                  SET TO REJECT SMF RECORD
         LA    R15,4               SET RETURN CODE 4
*        DC    A(0)                ABEND S0C1
         SPACE 2
***********************************************************************
*        END OF PROCESSING.                                           *
***********************************************************************
         SPACE 1
ENDOFPGM DS    0H                  RETURN TO CALLER
         L     R13,4(,R13)         RESTORE OLD SAVE AREA
         RETURN (14,12),,RC=(15)   EXIT PROGRAM
         SPACE 2
         DROP  R7                  END SMF RECORD ADDRESSABILITY
         TITLE 'DATA AREAS'
BEGSNAP  DS    0D                  SNAP DATA AREAS FOLLOWING
***********************************************************************
*        CONSTANTS AND WORK AREAS                                     *
***********************************************************************
         SPACE 2
CDATER   DC    CL8'GETDATE'        NAME OF DATE ROUTINE
DATE     DS    0CL9                AREA FOR DATE
PJULIAN  DC    PL4'0099365'        JULIAN DATE
         DC    CL5' '              REST OF DATE FIELD
DAY      DS    0CL10               DAY-OF-WEEK RETURNED HERE
         DC    CL9' '              DAY OF WEEK
HOLIDAY  DC    CL1' '              HOLIDAY FLAG ('H')
         SPACE 2
***********************************************************************
*        LITERAL POOL                                                 *
***********************************************************************
         SPACE 1
         LTORG ,                   LITERAL POOL GOES HERE
         SPACE 3
SMFHEAD  DSECT ,                         START OF SMF USER RECORD
         COPY  SMFHEAD             INCLUDE SMF HEADER FORMAT
*
         SPACE 3
         END
