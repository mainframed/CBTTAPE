***********************************************************************
*                                                                     *
*        PDSDOC *STANDARD DAY NUMBER CONVERSION ROUTINE (DSCB1 FORMAT)* STDDAY01
*                                                                     *
* ON ENTRY TO  THIS ROUTINE R1 -->  3-BYTE BINARY DATE X'YYDDDD'      *
* ON EXIT FROM THIS ROUTINE R1  =   BINARY STANDARD DAY NO.           *
*                                                                     *
* THE IDEA OF A STANDARD DAY VALUE CAME FROM J.P.GRASSBY PUBLISHED IN *
* 'FREELANCE INFORMER' VOL. 5 NO.2 BUT MODIFIED TO ONLY COUNT FROM THE*
* NEXT IBM TOD-CLOCK ROLLOVER DATE IN 2042, RATHER THAN 31/12/9999.   *
* THIS CONCEPT ALLOWS ARITHMETIC TO BE PERFORM DIRECTLY ON DATES, CAN *
* BE USED AS AN INDEX KEY (GET NEXT EQUAL OR GT), AND IS A DATE FORM  *
* INDEPENDENT OF NATIONAL FORMATS OR CALENDAR SCHEMES (EG. ISLAMIC)   *
*                                                                     *
* HISTORY:                                                            *
* 22MAY92 CART1 WROTE BASED ON STDDAY11                               *
*                                                                     *
***********************************************************************
         SPACE 1
STDDAY01 RIN   (R0,R2,R3)          SUBROUTINE ENTRY
         SR    R4,R4               CLEAR REGISTER
         IC    R4,0(,R1)           PICK UP YEAR
         STH   R4,HYEARINP         SAVE YEAR VALUE
         ICM   R4,B'0011',1(R1)    PICK UP DAY
         STH   R4,HDAYINP          SAVE THIS VALUE
         LA    R4,42               SET ROLLOVER YEAR
         CH    R4,HYEARINP         COMPARE TO INPUT YEAR
         BNL   *+4+4               CARRY ON IN 21ST CENTURY
         LA    R4,100(,R4)         ADD A CENTURY IN 20TH
         STH   R4,HYEARSD1         SAVE ROLLOVER YEAR
         LH    R1,HYEARINP         PICK UP THE INPUT YEAR
         GOSUB SD1LEAP             FIND OUT IF LEAP
         LA    R4,260              SET ROLLOVER JULIAN DAY
         LTR   R2,R2               IS INPUT YEAR LEAP?
         BNZ   *+4+4               NO, SKIP THE EXTRA DAY
         LA    R4,1(,R4)           YES, COUNT AN EXTRA DAY
         STH   R4,HDAYSD1          SAVE NO. OF STD DAY ZERO
         CH    R4,HDAYINP          CHECK NEED TO BORROW A YEAR
         BNL   STDDAY1A            NO, GO STRAIGHT AHEAD
         LA    R4,365(,R4)         BORROW A YEAR
         LA    R1,1(,R1)           EVEN IT UP ON THE INPUT
         STH   R1,HYEARINP         SAVE UPDATED YEAR VALUE
         EJECT ,
STDDAY1A DS    0H                  CALCULATE DIFFERENCE OF YEARS
         SH    R4,HDAYINP          CALCULATE SPAN BETWEEN DAYS
         LR    R0,R4               SAVE VALUE
         LH    R4,HYEARSD1         GET ROLLOVER YEAR
         SR    R4,R1               SUBTRACT THE YEARS
         SR    R2,R2               CLEAR EVEN REGISTER
         LA    R3,365              SET NO. DAYS IN A YEAR
         MR    R2,R4               CALCULATE DIFFERENCE IN DAYS
         AR    R0,R3               COMPUTE TOTAL DAYS FROM ROLLOVER
         LR    R1,R4               COPY DIFFERENCE IN YEARS
         GOSUB SD1LEAP             HOW MANY LEAP YEARS?
         LR    R1,R0               RECALL SUM TO DATE
         AR    R1,R3               ADD EXTRA DAYS FOR LEAP YEARS
         SPACE 1
STDDAY1X DS    0H                  SUBROUTINE EXIT
         ROUT  ,                   RETURN TO CALLER
         EJECT ,
***********************************************************************
* SUB-ROUTINE TO CALCULATE LEAP YEARS - SIMPLY DIVIDE BY FOUR         *
*                                                                     *
* ON ENTRY TO  THIS ROUTINE    R1   =  BINARY YEAR NUMBER             *
* ON EXIT                      R3   =  NUMBER OF LEAP YEARS           *
*                              R2   =  REMAINING NORMAL YEARS         *
***********************************************************************
         SPACE 1
SD1LEAP  RIN   (R1)                SUBROUTINE ENTRY
         SR    R2,R2               CLEAR EVEN REGISTER
         LR    R3,R1               LOAD DIVIDEND
         LA    R1,4                SET DIVISOR
         DR    R2,R1               DO THE DIVISION
         SPACE 1
SD1LEAPX DS    0H                  SUBROUTINE EXIT
         ROUT ,                    RETURN TO CALLER
         SPACE 2
HDAYINP  DC    H'0'                BINARY DAYS
HYEARINP DC    H'0'                BINARY YEARS
HDAYSD1  DC    H'0'                BINARY DAYS IN ROLLOVER YEAR
HYEARSD1 DC    H'0'                BINARY YEARS IN ROLLOVER YEAR
