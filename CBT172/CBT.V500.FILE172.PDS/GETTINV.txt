***********************************************************************
*                                                                     *
*        PDSDOC *GET LOCATE FOR VARIABLE LENGTH RECORDS...............* GETTINV
*                                                                     *
*        ON ENTRY R2 --> MDSCB                                        *
*                 R4  =  RETURN ADDRESS                               *
*        ON EXIT  R1 --> RECORD DATA (RDW), OR ZERO IF CANNOT READ    *
*                 R0  =  DATA LENGTH                                  *
*                                                                     *
* 20AUG02 DC  ADD CODE TO HANDLE RECFM=U                              *
* 06FEB95 DC  CHANGE FROM 'RIN' TO 'SUBIN'. LOSE RE-ENTRANCY          *
*                                                                     *
***********************************************************************
         SPACE 1
GETTING  SUBIN SAVE=(R3,R4)        READ AN INPUT RECORD
         PUSH  USING               SAVE CURRENT STATUS
         USING MDCB,R2             ESTABLISH MDCB ADDRESSABILITY
         SR    R1,R1               ASSUME WE WILL FAIL
         TM    MDCBFLAG,DDINT+DDPRES+DDOPEN TEST IF WE CAN READ
         BO    GETTINVS            YES, CARRY ON
         GOSUB OPEN                GO OPEN FILE
         TM    MDCBFLAG,DDINT+DDPRES+DDOPEN TEST IF CAN READ
         BNO   GETTINVX            NO, ASSUMED FAILURE DID OCCUR
         SPACE 1
GETTINVS DS    0H                  GET A RECORD
         L     R3,MDCBDCB          GET DCB ADDRESS
         USING IHADCB,R3           ESTABLISH MDCB ADDRESSABILITY
         GET   (R3)                READ IN A RECORD
         AP    MDCBCNT,ONE         INCREMENT RECORD COUNTER
         SR    R0,R0               CLEAR LENGTH REGISTER
         ICM   R0,B'0011',DCBLRECL GET RECORD LENGTH
         TM    DCBRECFM,DCBRECV    ARE THEY VARIABLE LENGTH RECORDS?
         BNO   GETTINVU            NO, TEST FOR UNDEFINED
         ICM   R0,B'0011',0(R1)    ELSE GET LENGTH FROM RDW
GETTINVU DS    0H                  HANDLE RECFM=U
         TM    DCBRECFM,DCBRECU    ARE THEY UNDEFINED LENGTH RECORDS?
         BNO   GETTINVX            NO, USE DCBLRECL VALUE
         ICM   R0,B'0011',DCBBLKSI GET RECORD LENGTH
         SPACE 1
GETTINVX DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R3,R4)    SUBROUTINE EXIT
         DROP  R2,R3               FINISHED WITH MDCB AND DCB
         POP   USING               RESTORE STATUS
