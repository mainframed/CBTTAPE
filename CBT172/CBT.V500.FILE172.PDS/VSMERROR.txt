*        PDSDOC *VSAM ERROR ROUTINE FROM GERMAN G.U.I.D.E. GOODIES....* VSMERROR
*$
*$
*$.      +1
*$       AENDERUNGSSTAND
*$
*$       ERSTELLUNGSDATUM:   17.04.1980
*$
*$       AENDERUNG:          02.11.1981    VERSION 1.1
*$                           UEBERGABE DER ERGEBNISSE IN EINEN
*$                           VOM RUFENDEN PROGRAMM BEREITGESTELL-
*$                           TEN BEREICH (121 BYTES LANG).
*$
*$       VERANTWORTLICH:     R. MEYER / SYSTEMPROGRAMMIERUNG
*$
*$
*$.      +1
*$       AUFGABE DES PROGRAMMS
*$
*$       MIT DIESER ROUTINE KOENNEN LOGISCHE FEHLER BEI DER
*$       VERARBEITUNG VON VSAM-DATEIEN AUFBEREITET WERDEN.
*$       DAS PROGRAMM STELLT DEN FEEDBACK-CODE UND EINE
*$       KURZGEFASSTE FEHLERBESCHREIBUNG ZUR VERFUEGUNG.
*$
*$.      +1
*$       EINGABE:
*$
*$       DAS PROGRAMM WIRD MIT LOAD/CALL AUFGERUFEN.
*$       FOLGENDE REGISTERSTAENDE WERDEN ERWARTET:
*$       R0 ===> ADRESSE DER RPL
*$       R1 ===> ADRESSE EINES 121 BYTES LANGEN MESSAGE BEREICHES
*$
*$.      +1
*$       AUSGABE:
*$
*$       DIE ROUTINE UEBERGIBT DIE ERGEBNISSE IN FOLGENDEN
*$       REGISTERN:
*$       R0 ===> ADRESSE DES MESSAGE-BEREICHES
*$       R1 ===> FEEDBACK-CODE
         EJECT
***********************************************************************
*                                                                     *
*   ANFANG    EROEFFNUNG VON ASSEMBLER-PROGRAMMEN                     *
*             LETZTE AENDERUNG :  12.03.80                            *
*                                                                     *
***********************************************************************
         MACRO
&NAME    ANFANG &V,&CD=C,&LC=0,&BR=1,&EP=,&L=0
         MNOTE  *,'        ***   VERSION VOM 12.03.80   ***'
.*
.* V   WIRD *V* ANGEGEBEN UND IST *BR>=1*, ERFOLGT DIE ADRESSIERUNG DER
.*     ERSTEN INSTRUKTION SCHON UEBER DAS BASISREG. 3, SONST UEBER R15.
.*     WIRD *R* ANGEGEBEN, WERDEN BIS ZUR DEFINITION DER SAVEAREA R15,
.*     VON DORT AB R13 UND NACH DESSEN ERSCHOEPFUNG MIT DEM PARAMETER
.*     *BR= * EVTL. AUSSERDEM VERLANGTE REGISTER ALS BASISREGISTER
.*     VERWENDET.
.*     WIRD *O* ANGEGEBEN ODER WIRD DER PARAMETER AUSGELASSEN UND IST
.*     *BR>=1*, WERDEN BIS ZUR BEENDIGUNG DES SAVEAREA HANDLINGS R15,
.*     VON DORT AB R3 UND SPAETER MIT DEM PARAMETER *BR= * EVENTUELL
.*     AUSSERDEM VERLANGTE REGISTER ALS BASISREGISTER VERWENDET. IST
.*     *BR=0*, WIRD DURCHGEHEND R15 ALS BASISREGISTER VERWENDET.
.*     DER PARAMETER *O* BEWIRKT AUSSERDEM, DASS FEHLER IN EINER ASS.-
.*     UNTERROUTINE EINES PL/I-OPT. HAUPTPROGRAMMS VOM PL/I-ERRORHAND-
.*     LER BEHANDELT UND FEHLERMELDUNGEN, DIE DIE FEHLERQUELLE IDENTI-
.*     FIZIEREN, AUSGEGEBEN WERDEN.
.* CD= STANDARDWERT IST *C* WIE *CSECT*
.*     ZULAESSIG IST AUCH *S* WIE *START*
.* LC= STANDARDWERT IST *0*, SETZT DEN ZUORDNUNGZAEHLER (AUF 0).
.*     SOWIE *NO*, WENN WEDER CSECT NOCH START ERSCHEINEN SOLL
.*     NUR ZULAESSIG IN VERBINDUNG MIT *START*, SIEHE CD=
.* BR= STANDARDWERT IST *1*, GIBT DIE ANZAHL DER BASISREGISTER AN.
.*     ZULAESSIG SIND *0*,*1*,*2* ODER *3*. ES WERDEN DANN ENTWEDER
.*     KEINE ODER IN DER REIHENFOLGE R3,R4,R5 DIE ANGEGEBENE ANZAHL AN
.*     BASISREGISTERN GENERIERT.ZULAESSIG SIND AUCH DIE FORMEN
.*     BR=(X)  BR=(X,Y)  BR=(X,Y,Z)
.*     WOBEI X,Y,Z JEWEILS FUER DIE REGISTER STEHEN, DIE ALS BASIS-
.*     REGISTER DIENEN SOLLEN, Z.B.  BR=(R5,R6)
.* EP= NAME DES EINGANGSPUNKTES. ANGABE EMPFEHLENSWERT, WENN OBERPRO-
.*     GRAMM IN PL/1 GESCHRIEBEN
.* L=  LETZTES ZEICHEN DES LABELS, DER DEN BEGINN DER BASISREGISTER-
.*     ZUORDNUNG KENNZEICHNET (=BASIS&L, WENN &L Â¬= 0).
.*
.* GLOBALE VARIABLE ZUM TEST GEGEN DOPPELDEFINITIONEN DER REGISTER
         GBLB  &GLBIT  BETROFFEN SIND DIE EQU'S
         GBLA  &GLBR
         LCLA  &SETBR  ZUM TESTEN FUER BR=
         LCLA  &SL,&OFS
         LCLC  &MM,&NN,&BR1,&BR2,&BR3
.*
         AIF   (T'&NAME NE 'O').M005
         MNOTE 12,'UNBENANNTE CONTROL-SECTION'
         AGO   .M130
.M005    ANOP
&NN      SETC  '&NAME'
         AIF   ('&EP' NE '&NAME').M010
         AIF   ('&V' NE 'O').M010
         AIF   (K'&NAME EQ 8).M007
&MM      SETC  '1'
         AGO   .M010
.M007    ANOP
&NN      SETC  '&NAME'(1,7)
&MM      SETC  '$'
.M010    ANOP
         AIF   ('&CD' EQ 'C').M020
         AIF   ('&CD' EQ 'S').M030
         AIF   ('&CD' EQ 'NO').M040
         MNOTE 4,'CD= IST UNGUELTIG'
.M020    ANOP
&NN&MM   CSECT
         AGO   .M042
.M030    ANOP
&NN&MM   START &LC
         AGO   .M042
.M040    ANOP
&NN&MM   DS    0F
.M042    ANOP
         AIF   ('&BR'(1,1) EQ '(').M043
&SETBR   SETA  &BR
&BR1     SETC  'R3'
&BR2     SETC  'R4'
&BR3     SETC  'R5'
         AGO   .M044
.M043    ANOP
&SETBR   SETA N'&BR
&BR1     SETC  '&BR(1)'
         AIF   (&SETBR LT 2).M044
&BR2     SETC  '&BR(2)'
         AIF   (&SETBR LT 3).M044
&BR3     SETC  '&BR(3)'
.M044    ANOP
         AIF   (&GLBIT).M045
*
*
R0       EQU   0 PARAMETER-REGISTER
R1       EQU   1 PARAMETER-REGISTER
R2       EQU   2 ARBEITSREGISTER
R3       EQU   3 ARBEITSREGISTER
R4       EQU   4 ARBEITSREGISTER
R5       EQU   5 ARBEITSREGISTER
R6       EQU   6 ARBEITSREGISTER
R7       EQU   7 ARBEITSREGISTER
R8       EQU   8 ARBEITSREGISTER
R9       EQU   9 ARBEITSREGISTER
R10      EQU   10 LINK-REGISTER
R11      EQU   11 3. BASISREGISTER
R12      EQU   12 2. BASISREGISTER
R13      EQU   13 1. BASISREGISTER
R14      EQU   14 RUECKKEHR-REGISTER
R15      EQU   15 VERZWEIG-REGISTER
*
*
         MNOTE *,'SYMBOLISCHE REGISTER GENERIERT'
*
*
.M045    AIF   ('&V' EQ 'V').M200
         AIF   ('&V' EQ 'R').M050
         AIF   ('&V' EQ 'O').M055
         AIF   (T'&V EQ 'O').M050
         MNOTE 4,'UNGUELTIGER POSITIONSPARAMETER'
.M050    ANOP
         USING *,R15
.M055    ANOP
&SL      SETA  K'&EP
         AIF   (T'&EP EQ 'O').M100
         AIF   ('&V' NE 'O').M080
         ENTRY &EP
         AIF   (&SL/2 NE (&SL+1)/2).M077
         DC    C' &EP'
         AGO   .M078
.M075    ANOP
         B     *+7+8+5+2*((&SL-1)/2)
         DC    AL1(&SL)
.M077    ANOP
         DC    C'&EP'
.M078    ANOP
         AIF   ('&V' NE 'O').M090
         DC    AL1(&SL)
         CNOP  0,4
         USING *,R15
&EP      EQU   *
         AGO   .M095
.M080    ANOP
         AIF   ('&EP' EQ '&NAME').M075
         ENTRY &EP
&EP      DS    0H
         AGO   .M075
.M090    ANOP
         DC    C'&SYSDATE'
         DC    C'&SYSTIME'
.M095    ANOP
*
*
         MNOTE *,'ENTRY - EINGANGSPUNKT GENERIERT'
*
*
.M100    ANOP
         AIF   ('&V' NE 'O').M104
         STM   R14,R12,12(R13) .  SAVE REGISTERS
         B     *+12               BRANCH AROUND CONSTANT
         DC    A(88)              LENGTH OF DSA
         DC    A(0)
         L     R0,*-8
         LR    R4,R1 .            SAVE ANY PARAMETERLIST-ADDR.
         L     R1,76(0,R13) .     ADDR. OF NEXT AVAILABLE BYTE IN STOR.
         ALR   R0,R1
         CL    R0,12(0,R12) .     COMP. WITH LAST AVAIL. BYTE IN STOR.
         BNH   *+10
         L     R15,116(0,R12) .   PL/I STORAGE OVERFLOW ROUTINE
         BALR  R14,R15 .          ATTEMPT TO OBTAIN MORE STORAGE
         ST    R0,76(0,R1) .      ADDR. OF NEXT AVAIL. BYTE IN STOR.
         ST    R13,4(0,R1)
         ST    R1,8(0,R13)
         MVC   72(4,R1),72(R13) . COPY ADDR. OF LIBRARY WORKSPACE
         LR    R13,R1
         MVI   0(R13),X'80'
         MVI   86(R13),X'91'
         MVI   87(R13),X'C0'
         LR    R1,R4
*
*
*        ENDE DES SAVEAREA-HANDLINGS
*
*
         AGO   .M200
.M104    ANOP
         CNOP  2,4
         STM   R14,R12,12(R13) .  SAVE REGISTERS
         ST    R13,*+14
         LR    R11,R13
         BAL   R13,*+19*4
         AIF   ('&V' NE 'R').M105
         USING *,R13
.M105    ANOP
ANFA&SYSNDX DS 0F   WORT 0 NUR VON PL1 VERWENDET
         DC    X'0080'
         DC    AL2(72)
         DC    F'0' WORT 1 A(SAVEAREA IM RUFENDEN PROGRAMM)
         DC    F'0' WORT 2 A(SAVEAREA IM GERUFENEN PROGRAMM)
         DC    F'0' WORT 3 R14
         DC    F'0' WORT 4 R15
         DC    F'0' WORT 5 R0        ** BEI         R14
         DC    F'0' WORT 6 R1        ** ZUGRIFFS-   R15
         DC    F'0' WORT 7 R2        ** ROUTINEN    R0
         DC    F'0' WORT 8 R3 BASIS1 ** WERDEN      R1
         DC    F'0' WORT 9 R4 BASIS2 ** HIER        R2
         DC    F'0' WORT10 R5 BASIS3 ** FOLGENDE    R3
         DC    F'0' WORT11 R6        ** REGISTER    R4
         DC    F'0' WORT12 R7        ** VON DER     R5
         DC    F'0' WORT13 R8        ** ZUGRIFFS-   R6
         DC    F'0' WORT14 R9        ** ROUTINE     R7
         DC    F'0' WORT15 R10       ** GESPEICHERT R8
         DC    F'0' WORT16 R11
         DC    F'0' WORT17 R12
*
         ST    R13,8(0,R11)
*
*
*        ENDE DES SAVEAREA-HANDLINGS
*
*
         AIF   ('&V' NE 'V').M200
         AGO   .M120
.M110    ANOP
.M120    AIF   (&GLBR GT &SETBR).M130
&GLBR    SETA  &SETBR
         AIF   ('&V' EQ 'V').M130
         DROP  R15
.M130    ANOP
&GLBIT   SETB  1
*
*
*        ENDE DES VORSPANNS
*
*
         MEXIT
.M200    AIF   (&SETBR NE 0).M220
         MNOTE *,'KEINE BASISREGISTER GENERIERT'
         AIF   ('&V' EQ 'V').M050
         AGO   .M110
.M220    AIF   ('&V' EQ 'R').M250
         BALR  &BR1,0
         AIF   ('&L' EQ '0').M222
BASIS&L  EQU   *
.M222    USING *+&OFS,&BR1
         AIF   (&GLBR GT 0).M225
.M225    AIF   (&SETBR GE 2).M240
.M230    AIF   ('&V' EQ 'V').M055
         AGO   .M110
.M240    LA    &BR2,4095(,&BR1)
         USING *+&OFS+4091,&BR2
         AIF   (&GLBR GT 1).M245
.M245    AIF   (&SETBR EQ 2).M230
         LA    &BR3,4095(,&BR2)
         USING *+&OFS+2*4091,&BR3
         AIF   (&GLBR GT 2).M230
         AGO   .M230
.M250    LA    &BR1,4095(,R13)
&OFS     SETA  4095-80
         AGO   .M222
         MEND
         EJECT
***********************************************************************
*                                                                     *
*   ENDE      ABSCHLUSS VON ASSEMBLER-PROGRAMMEN                      *
*             LETZTE AENDERUNG :  12.03.80                            *
*                                                                     *
***********************************************************************
         MACRO
&NAME    ENDE &L,&R1,&RC=
         MNOTE  *,'      ***   VERSION VOM 12.03.80   *** '
*
         LCLC  &ENDEC1,&ENDEC2
         AIF   (T'&RC EQ 'O').S040
         AIF   ('&RC'(1,1) EQ '(').S010 REG.NOTATION
         AIF   ('&RC'(1,1) LT '0').S020
&NAME    LA    R15,&RC            RETURNCODE LADEN
         AGO   .S030
.S010    ANOP
&NAME    LA    R15,0(,&RC(1))     RETURNCODE LADEN
         AGO   .S030
.S020    ANOP
&NAME    L     R15,&RC            RETURNCODE LADEN
.S030    L     R13,4(,R13)        A(SAVEAREA IM RUFENDEN PROGRAMM)
&ENDEC1  SETC  'R0'
&ENDEC2  SETC  '20'
         AGO   .S050
.S040    ANOP
&NAME    L     R13,4(,R13)        A(SAVEAREA IM RUFENDEN PROGRAMM)
.S050    AIF   (T'&R1 EQ 'O').S060
         AIF   ('&R1' NE 'R1').S900
&ENDEC1  SETC  'R2'
&ENDEC2  SETC  '28'
.S060    AIF   (T'&R1 EQ 'O' AND T'&RC EQ 'O').S070
         L     R14,12(,R13)       ALTE REG.STAENDE WIEDERHERSTELLEN
         LM    &ENDEC1,R12,&ENDEC2.(R13)
         AGO   .S080
.S070    LM    R14,R12,12(R13)    ALTE REG.STAENDE WIEDERHERSTELLEN
.S080    BR    R14                RETURN
         AIF   (T'&L EQ 'O').S090
         AIF   ('&L' NE 'L').S910
         AGO   .S920
.S090    MEXIT
.S900    IHBERMAC 37,,&R1         INVALID OPERAND
         MEXIT
.S910    AIF   (K'&L GT 1).S090
         IHBERMAC 183,&L,L
.S920    LTORG
         MEND
         EJECT
VSMERROR ANFANG R
         SPACE 5
         LR    R2,R0                          R0 SICHERN
         LR    R3,R1                          R1 SICHERN
         EJECT
LERAD    EQU   *
*               ***********************************
*               *                                 *
*               *      LERAD-ROUTINE VSAM-DSET    *
*               *                                 *
*               ***********************************
         SPACE
         SHOWCB RPL=(R2),AREA=ERRORFLD,LENGTH=4,FIELDS=FDBK
         SPACE
         LA    R5,ERRORTAB-43
*
*               ***********************************
*               *                                 *
*               *      LOGICAL-ERROR-MESSAGE      *
*               *      ---------------------      *
*               ***********************************
LOOP     EQU   *
         LA    R5,43(R5)
         CLC   0(2,R5),=C'%%'
         BE    LOOPEND
         CLC   ERRORFLD+3(1),0(R5)
         BNE   LOOP
         MVC   ERRORREC+50(2),1(R5)
         MVC   ERRORREC+54(40),3(R5)
         B     ROUTENDE
LOOPEND  EQU   *
         MVC   ERRORREC+1(62),=C'EIN FEHLER MIT UNBEKANNTEM CODE TRAT A*
               UF! BITTE DUMP BEACHTEN!'
         LA    R15,12
         EJECT
ROUTENDE EQU   *
         SPACE 2
         LA    R0,ERRORREC                    SATZ-ADRESSE LADEN
         MVC   0(121,R3),ERRORREC             MESSAGE UEBERTRAGEN
         L     R1,ERRORFLD                    ADRESSE FDBK-FELD LADEN
         SPACE
         ENDE  L,R1,RC=(R15)
         SPACE
         EJECT
*
*        BEREICHE FUER LERAD-ROUTINEN
*
ERRORREC DC    CL121'-ES TRAT EIN LOGISCHER FEHLER AUF! FEEDBACK-CODE='
*
ERRORFLD DS    F
*
*
ERRORTAB DC    X'00',CL2'00'
         DC    CL40'KEIN FEHLER - ALLES IN ORDNUNG        '
         DC    X'04',CL2'04'
         DC    CL40'END OF FILE (BEI GET-OPERATION)       '
         DC    X'08',CL2'08'
         DC    CL40'DUPLICATE KEY                         '
         DC    X'0C',CL2'0C'
         DC    CL40'KEY OUT OF SEQUENCE (BEIM LADEN)      '
         DC    X'10',CL2'10'
         DC    CL40'RECORD NOT FOUND                      '
         DC    X'14',CL2'14'
         DC    CL40'DATASET ALLOCATED TO ANOTHER USER/JOB '
         DC    X'18',CL2'18'
         DC    CL40'REQ. VOLUME NOT MOUNTED               '
         DC    X'1C',CL2'1C'
         DC    CL40'NOT ENOUGH SPACE FOR INSERT/EXTENT    '
         DC    X'20',CL2'20'
         DC    CL40'RBA-ANGABE FALSCH                     '
         DC    X'24',CL2'24'
         DC    CL40'NICHT DEF. KEY-RANGE BEI GET/PUT      '
         DC    X'28',CL2'28'
         DC    CL40'NICHT AUSREICHENDER PLATZ IM ADR.BER. '
         DC    X'2C',CL2'2C'
         DC    CL40'WORK-AREA ZU KLEIN                    '
         DC    X'40',CL2'40'
         DC    CL40'STRNO UEBERSCHRITTEN                  '
         DC    X'44',CL2'44'
         DC    CL40'VERARBEITUNGSART NICHT SPEZIFIZIERT   '
         DC    X'48',CL2'48'
         DC    CL40'ZUGRIFF MIT SCHLUESSEL AUF ESDS       '
         DC    X'50',CL2'50'
         DC    CL40'ERASE FUER ESDS AUSGEGEBEN            '
         DC    X'54',CL2'54'
         DC    CL40'OPTCD=LOC BEI PUT-REQUEST             '
         DC    X'58',CL2'58'
         DC    CL40'                                      '
         DC    X'5C',CL2'5C'
         DC    CL40'PUT UPDATE/ERASE OHNE VORHER. GET     '
         DC    X'60',CL2'60'
         DC    CL40'VERSUCH, DEN SCHLUESSEL ZU VERAENDERN '
         DC    X'64',CL2'64'
         DC    CL40'BEI RBA-ZUGRIFF:LAENGENAENDERUNG VERS.'
         DC    X'68',CL2'68'
         DC    CL40'RPL-OPTIONS NICHT ZULAESSIG           '
         DC    X'6C',CL2'6C'
         DC    CL40'REC-LAENGE ZU GROSS BZW. NULL         '
         DC    X'70',CL2'70'
         DC    CL40'KEY-LAENGE ZU GROSS BZW NULL          '
         DC    X'74',CL2'74'
         DC    CL40'NICHT ERLAUBTE ZUGRIFF AUF LEERE DATEI'
         DC    CL2'%%'
         END   VSMERROR
