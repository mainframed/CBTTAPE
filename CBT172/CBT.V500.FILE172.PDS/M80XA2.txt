MAP3380 TITLE 'LIST FREE SPACE ON ALL ONLINE 3380'
*
*        PDSDOC *ONE PAGE SUMMARY OF ONLINE DISKS (3380) FOR MVS/XA V2* M80XA2
*
*
* PURPOSE:
* TO GIVE A SHORT OVERVIEW OF 3380 SPACE AVAILABILITY, BY TYPE.
* COULD EASILY BE REPRODUCED IN A 3390 VERSION.
*
* HISTORY:
*   DEC90 CART1 LARGELY REWROTE THE INTERDATA AG PROGRAM
*
*
*
* DDNAME -     SYSPRINT, ASA LRECL FROM 80 - 120 BYTES
         EJECT
MAP3380 CSECT
         SAVE  (14,12)
         USING MAP3380,R12
         LR    R12,R15                  LOAD BASE-REG
         ST    R13,SAVE+4               SAVE HSA
         LR    R14,R13                  SAVE REG
         LA    R13,SAVE                 SAVE LSA
         ST    R13,8(R14)               SAVE LSA IN HSA
         OPEN  (PRINT,(OUTPUT))
         TITLE 'DATE ROUTINE'
*********************************************************************
*         DATE PROCESSING
*********************************************************************
         SR    R1,R1               ASK FOR CURRENT TIME
         GOSUB TODDATE             GO FORMAT TOD-CLOCK
         DATE  H1DATE,FORMAT='DD MMM YY',FROM=TODPJUL FORMAT DATE ..
         MVC   H2TIME,TODTIME       .. AND TIME IN HEADING
         PUT   PRINT,H1                 HEADINGS
         PUT   PRINT,H2
         MVI   P1,C'0'                  SET TO DOUBLE SPACE
         TITLE 'DEVICE PROCESSING'
         L     R3,CVTPTR           GET CVT PTR
         USING CVTMAP,R3           SET UP ADDRESSABILITYTO CVT
         USING UCBOB,R2            SET ADDRESSABILITY TO UCB
         MVI   DEVCLASS,UCB3DACC   INDICATE ONLY DIRECT ACCESS UCBS
GETUCB   DS    0H
         LA    R1,PARMLIST         PUT PARMLIST ADDRESS IN REG 1
         L     R15,CVTUCBSC        GET SCAN SERVICE ADDRESS
         BALR  R14,R15             GO TO SCAN SERVICE
         LTR   R15,R15             LAST UCB
         BNZ   GETUCBE             YES, END
         L     R1,ADDRUCB          GET UCB-ADDR
         TM    3(R1),X'80'         IS IT ONLINE
         BZ    GETUCB              NO, SKIP
         CLI   19(R1),X'0E'        TYPE 3380
         BNE   GETUCB              NO, NEXT UCB
         BAL   R4,GETDEV           GO ISSUE DEVTYPE
GET3380D DS    0H
         USING TAB,R5              ESTABLISH TABLE ADDRESSABILITY
         LA    R5,T3380D                 AT 3380-D DATA
         CLC   DVACYL,NUMCYLS      IS THIS THE RIGHT MODEL?
         BE    GETUCBA             YES, GO PROCESS IT
GET3380E DS    0H
         LA    R5,T3380E                 AT 3380-E DATA
         CLC   DVACYL,NUMCYLS      IS THIS THE RIGHT MODEL?
         BE    GETUCBA             YES, GO PROCESS IT
GET3380K DS    0H
         LA    R5,T3380K                 AT 3380-K DATA
         CLC   DVACYL,NUMCYLS      IS THIS THE RIGHT MODEL?
         BNE   GETUCB              NO, NEXT UCB
         SPACE 2
GETUCBA  DS    0H
         LA    R0,8                SET LENGTH OF QUEUE ELEMENT
         LA    R1,UCBTAB           POINT TO HEAD OF CHAIN
         BAL   R4,QLADD            GO ADD NEW ELEMENT IN QUEUE
         MVC   4(4,R1),ADDRUCB          COPY UCB-ADDRESS
         B     GETUCB                   GET NEXT UCB
         SPACE 1
         DROP  R5                  END TABLE ADDRESSABILITY
         EJECT ,
GETUCBE  DS    0H
DOLINE   DS    0H                       SET UP AND PRINT A LINE
*                                       LINE IS ALWAYS SET UP IN LEFT
*                                        HAND SIDE
         LA    R5,T3380K                 AT 3380-K DATA
         USING TAB,R5              ESTABLISH TABLE ADDRESSABILITY
         BAL   R10,DOUCB                PROCESS NEXT UCB
         MVC   P1A3,P1A1                COPY LEFT SIDE TO RIGHT SIDE
*                                        OF PRINT LINE
         LA    R5,T3380E                 AT 3380-E DATA
         USING TAB,R5
         BAL   R10,DOUCB                PROCESS NEXT UCB
         MVC   P1A2,P1A1                COPY LEFT SIDE TO RIGHT SIDE
*                                        OF PRINT LINE
         LA    R5,T3380D                 AT 3380-D DATA
         BAL   R10,DOUCB                 PROCESS NEXT UCB
         CLC   P1A1,P1A2                 BOTH SIDES SAME (BLANK)
         BE    SUMMARY                   YES, END - GO SUMMARIZE
         PUT   PRINT,P1                  NO, PRINT A LINE
         MVC   P1,BLANKS                 CLEAR IT
         B     DOLINE                    GO, DO NEXT LINE
         SPACE 1
DOUCB    DS    0H
         MVC   P1A1,BLANKS              INIT
         ICM   R4,15,0(R5)              GET QUEUE-ELEMENT
         BZ    EXITUCB                  YES, DONE
         ICM   R3,15,4(R4)              GET UCB-ADDRESS
         MVC   P1CUU,13(R3)             ADDRESS
         MVC   P1VOL,28(R3)             VOLSER
         ICM   R4,15,0(R4)              GET NEXT QUEUE-ELEMENT
         STCM  R4,15,0(R5)              AND SAVE
         LR    R0,R3                    UCB ADDRESS
         LA    R1,SVCOUT                OUTPUT MESSAGE
*
         SVC   78                       MAPSPACE
*
         PACK  WORK1,OUTCYL
         ZAP   WORK3,WORK1              CYLS
         AP    FREECYL,WORK1            SUM FREE CYLS
         MP    WORK3,TRKCYL             * TRK/CYL = TRACKS
         MVC   WORK2,=X'402020202120'
         ED    WORK2,WORK1
         MVC   P1CYL,WORK2+2
         PACK  WORK1,OUTTRK
         AP    WORK3,WORK1              + TRACKS = TOT FREE TRACKS
         AP    FREETRK,WORK1            SUM FREE TRACKS
         AP    NOVOLS,=P'1'             SUM VOLUMES
         MVC   WORK2,=X'402020202120'
         ED    WORK2,WORK1
         MVC   P1TRK,WORK2+2
         PACK  WORK1,OUTEXT
         MVC   WORK2,=X'402020202120'
         ED    WORK2,WORK1
         MVC   P1EXT,WORK2+2
         MP    WORK3,=P'100'            * 100 (FOR PERCENT)
         AP    WORK3,TRKPACKR           + ROUNDED FACTOR
         DP    WORK3,TRKPACK            / TRACKS PER PACK
         MVC   WORK2,=X'402020202120'
         ED    WORK2,WORK3+2            PERCENT FREE
         MVC   P1PCT,WORK2+3            IN PRINT
         TM    34(R3),X'04'             IF STORAGE
         BZ    DOUCBTI                  NO, SKIP SET
         MVI   P1SP,C'S'                SET STORAGE
DOUCBTI  DS    0H
         CVAFTST UCB=(R3)               SEE IF DFDS INSTALLED
         CH    R15,=H'8'                CHECK RETURN FROM CVATST
         BNE   EXITUCB                  BRANCH IF NOT INDEXED
         MVI   P1INX,C'I'               SET INDEXED
EXITUCB  DS    0H
         BR    R10                      EXIT
         SPACE 1
SUMMARY  DS    0H
*                                       LINE IS ALWAYS SET UP IN LEFT
*                                        HAND SIDE
         LA    R5,T3380K                AT 3380-K DATA
         BAL   R10,SUM1                 SUMMARY ONE
         MVC   S1A3,S1A1                COPY LEFT SIDE TO RIGHT SIDE
         LA    R5,T3380E                AT 3380-E DATA
         BAL   R10,SUM1                 SUMMARY ONE
         MVC   S1A2,S1A1                COPY LEFT SIDE TO RIGHT SIDE
         LA    R5,T3380D                AT 3380-D DATA
         BAL   R10,SUM1                 SUMMARY ONE
         PUT   PRINT,S1                 PRINT SUMMARY LINE ONE
         MVC   P1,BLANKS                CLEAR IT
         LA    R5,T3380K                AT 3380-K DATA
         BAL   R10,SUM2                 SUMMARY TWO
         MVC   S2A3,S2A1                COPY LEFT SIDE TO RIGHT SIDE
         LA    R5,T3380E                AT 3380-E DATA
         BAL   R10,SUM2                 SUMMARY TWO
         MVC   S2A2,S2A1                COPY LEFT SIDE TO RIGHT SIDE
         LA    R5,T3380D                AT 3380-D DATA
         BAL   R10,SUM2                 SUMMARY TWO
         PUT   PRINT,S2                 PRINT SUMMARY LINE TWO
         B     EOJ                      DONE
SUM1     DS    0H                       CALC TOTAL FREE CYLS
         ZAP   WORK3,FREETRK
         ZAP   WORK4,TRKCYL
         DP    WORK3,WORK4              = FREE TRACKS (IN CYLS)
         AP    WORK3(4),FREECYL         = TOTAL FREE CYLS
         MVC   S1CYL,=X'402020202120'
         ED    S1CYL,WORK3+1            IN PRINT
*                                       NOW ABOVE IN PERCENT
         ZAP   WORK3,FREECYL
         MP    WORK3,TRKCYL             FREE CYLS IN TRACKS
         AP    WORK3,FREETRK            + FREE TRACKS
         MP    WORK3,=P'100'            * 100
         ZAP   WORK5,TRKPACK
         MP    WORK5,NOVOLS             TOTAL CAPACITY
         DP    WORK3,WORK5              / CAPACITY
         MVC   WORK2,=X'402020202120'
         ED    WORK2,WORK3              PERCENT FREE
         MVC   S1PCT,WORK2+3            IN PRINT
         BR    R10
SUM2     DS    0H                       CALC AVAIL VOLS AND GB
         MVC   S2VOL,=X'40202120'
         ED    S2VOL,NOVOLS
         ZAP   WORK3,NOVOLS             NUMBER VOLS
         MP    WORK3,MBPACK             * MB (*10) PER PACK
         AP    WORK3,=P'500'            ROUND
         DP    WORK3,=P'1000'           = XX.X GB
         MVC   S2GB,=X'4020214B20'
         ED    S2GB,WORK3+3             IN PRINT
         BR    R10
         SPACE 2
EOJ      DS    0H
         CLOSE (PRINT)
         L     R13,SAVE+4
         RETURN (14,12),RC=0
         TITLE 'SUBROUTINES'
         COPY  QLADD               DYNAMIC CONTROL BLOCK CHAINING
         EJECT
         COPY  GETDEV              FIND DEVICE CHARACTERISTICS
         EJECT
         COPY  TODDATE             PROCESS TOD CLOCK
         TITLE 'DATA AREAS'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
         DS    0D
SAVE     DC    18F'0'
P1DAT    DC    C'ZE'
TTMMJJ   DC    CL6'000000'              FOR DATE MODULE
WORK1    DS    PL3
WORK2    DS    CL6
WORK3    DS    PL8
WORK4    DS    PL4
WORK5    DS    PL5
T3380D   DS    0F                       3380 DATA
P3380D   DC    A(0)                     UCB QUEUE POINTER
         DC    XL2'0376'           TOTAL NUMBER OF CYLINDERS
         DC    PL2'15'                  TRK/CYL
         DC    PL3'13275'               TRK/PACK
         DC    PL3'06638'               TRK/PACK ROUNDING NUMBER
         DC    PL3'6302'                MB/PACK (*10)
         DC    XL2'00'                  LAST ADDRESS
         DC    2PL4'0'                  FREE CYL/TRKS
         DC    PL2'0'                   NUMBER VOLS
         SPACE 1
T3380E   DS    0F                       3380-E DATA
P3380E   DC    A(0)                     UCB QUEUE POINTER
         DC    XL2'06EB'           TOTAL NUMBER OF CYLINDERS
         DC    PL2'15'                  TRK/CYL
         DC    PL3'26550'               TRK/PACK
         DC    PL3'06638'               TRK/PACK ROUNDING NUMBER
         DC    PL3'12604'               MB/PACK (*10)
         DC    XL2'00'                  LAST ADDRESS
         DC    2PL4'0'                  FREE CYL/TRKS
         DC    PL2'0'                   NUMBER VOLS
         SPACE 1
T3380K   DS    0F                       3380-K DATA
P3380K   DC    A(0)                     UCB QUEUE POINTER
         DC    XL2'0A60'           TOTAL NUMBER OF CYLINDERS
         DC    PL2'15'                  TRK/CYL
         DC    PL3'39825'               TRK/PACK
         DC    PL3'06638'               TRK/PACK ROUNDING NUMBER
         DC    PL3'18907'               MB/PACK (*10)
         DC    XL2'00'                  LAST ADDRESS
         DC    2PL4'0'                  FREE CYL/TRKS
         DC    PL2'0'                   NUMBER VOLS
         SPACE 1
SVCOUT   DC    CL30' '
         ORG   SVCOUT+6
OUTCYL   DS    CL4
         DS    CL1
OUTTRK   DS    CL4
         DS    CL1
OUTEXT   DS    CL4
         ORG
BLANKS   DC    CL121' '
H1       DC    CL121'1'
         ORG   H1+1
         DC    C'3 3 8 0 D ----FREE SPACE----     '
         DC    C'3 3 8 0 E ----FREE SPACE----     '
         DC    C'3 3 8 0 K ----FREE SPACE----     '
H1DATE   DC    CL9' '
         ORG
H2       DC    CL121' '
         ORG   H2+1
         DC    C'CUU VOLSER CYLS TRKS EXTS   %    '
         DC    C'CUU VOLSER CYLS TRKS EXTS   %    '
         DC    C'CUU VOLSER CYLS TRKS EXTS   %    '
H2TIME   DC    CL8' '
         ORG
P1       DC    CL121' '
         ORG   P1+1
P1A1     DS    0CL32
P1CUU    DS    CL3
         DS    CL1
P1VOL    DS    CL6
         DS    CL1
P1CYL    DS    CL4
         DS    CL1
P1TRK    DS    CL4
         DS    CL1
P1EXT    DS    CL4
         DS    CL1
P1PCT    DS    CL3
         DS    CL1
P1INX    DS    CL1
P1SP     DS    CL1
         DS    CL1
P1A2     DS    CL32
         DS    CL1
P1A3     DS    CL32
         ORG
S1       DC    CL121' '
         ORG   S1+1
S1A1     DS    0CL29
         DC    C'FREE     '
S1CYL    DS    CL6
         DS    CL11
S1PCT    DS    CL3
         DS    CL4
S1A2     DS    CL29
         DS    CL4
S1A3     DS    CL29
         ORG
S2       DC    CL121' '
         ORG   S2+1
S2A1     DS    0CL29
         DC    C'AVAILABLE'
S2VOL    DS    CL4
         DC    C' VOLUMES'
S2GB     DS    CL5
         DC    C' GB'
         DS    CL4
S2A2     DS    CL29
         DS    CL4
S2A3     DS    CL29
         ORG
         PRINT NOGEN
PRINT    DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=(PM)
         PRINT GEN
PARMLIST DS    0F
PARMWA   DC    A(WORKAREA)    ADDRESS OF 100 BYTE WORKAREA
PARMDEVT DC    A(DEVCLASS)    ADDRESS OF BYTE CONTAINING THE DEVTYPE
PARMUCB  DC    A(ADDRUCB)     ADDRESS OF WORD CONTAIN THE UCBADDR
ADDRUCB  DC    A(0)
DEVCLASS DC    X'00'          X'00' = ALL
WORKAREA DS    CL100
         SPACE 2
         LTORG
         EJECT ,
TAB      DSECT
UCBTAB   DS    A                        UCB QUEUE POINTER
NUMCYLS  DS    AL2                      TOTAL NUMBER OF CYLINDERS
TRKCYL   DS    PL2
TRKPACK  DS    PL3
TRKPACKR DS    PL3
MBPACK   DS    PL3
LASTUCB  DS    CL2                      LAST UCB ADDRESS
FREECYL  DS    PL4                      FREE CYLINDERS
FREETRK  DS    PL4                      FREE TRACKS
NOVOLS   DS    PL2                      NUMBER VOLUMES
         EJECT ,
         DSECT
         IEFUCBOB
         EJECT ,
         CVT   DSECT=YES,LIST=YES
         SPACE 2
         END
