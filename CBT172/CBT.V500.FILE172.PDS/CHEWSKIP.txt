CHEWSKIP TITLE 'CHEW CARRIAGE CONTROL CONVERSION UTILITY'
***********************************************************************
*        PDSDOC *CONVERT ALL ARCHIVE DATASETS TO ASA CONTROL..........* CHEWSKIP
*                                                                     *
*        PROGRAM ID. :             CHEWSKIP                           *
*                                                                     *
*        DATE WRITTEN :            JANUARY   1995                     *
*                                                                     *
*        AUTHOR :                  D.H.CARTWRIGHT.                    *
*                                                                     *
*        LOCATION :                SUTTON COLDFIELD                   *
*                                                                     *
*        HISTORY  :                                                   *
*                                                                     *
*   JAN95 STARTED WRITING BASED ON THE OLD 'UT531' PROGRAM            *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*       ---------                                                     *
*                                                                     *
* THIS IS A SUB-PROGRAM WITHIN THE 'CHEW' EXTERNAL WRITER SUITE.      *
* IT CONVERTS THE CARRIAGE CONTROL CHARACTER AT 'JESCC' TO THE ASA    *
* STANDARD REGARDLESS OF WHETHER IT WAS CREATED WITH ASA OR MACHINE   *
* CONTROL CHARACTERS.                                                 *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*       ------------                                                  *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT ,
***********************************************************************
*                                                                     *
*       INPUT                                                         *
*       -----                                                         *
*                                                                     *
* ON ENTRY R1 ---> CHEWBACA COMMON AREA WHICH IS ADDRESSED BY R11.    *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*       OUTPUT                                                        *
*       ------                                                        *
*                                                                     *
* VARIOUS FIELDS WITHIN 'CHEWBACA' MAY BE UPDATED BY THIS ROUTINE.    *
* IN PARTICLUR IT WILL UPDATE THE ADDRESS OF THE CARRIAGE CONVERSION  *
* TABLE ENTRY APPLICABLE TO THIS LINE.                                *
*                                                                     *
***********************************************************************
         EJECT ,
*        PRINT ON,GEN              SHOW MACRO EXPANSIONS
         PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
         SPACE 1
CHEWSKIP CSECT ,
         SPACE 1
         PGMIN EQUATES=NO,USING=(R13) STANDARD HOUSEKEEPING
         SPACE 1
         LM    R11,R12,0(R1)       (RE)COPY COMMON AREA POINTERS
         USING CHEWBACA,R11,R12    ESTABLISH ADDRESSABILITY
         XC    RETSKIP,RETSKIP     SET DEFAULT RETURN CODE
         L     R2,APRINT           GET ADDRESS OF PRINTLINE
         EJECT ,
***********************************************************************
*        CONTROL CHARACTER CONVERSION                                 *
***********************************************************************
         SPACE 1
CONVERT  DS    0H                  CHANGE ALL PRINT CONTROL TO ANSI
         SR    R1,R1               CLEAR A WORK REGISTER
         IC    R1,0(R2)            PICK UP CONTROL CHARACTER
         LA    R7,CCTABLE          POINT AT CONTROL CHARACTER TABLE
         USING CCTAB,R7            ESTABLISH TABLE ADDRESSABILITY
         LA    R2,CCTABNUM         SET NUMBER OF ENTRIES
         SPACE 1
CONVLOOP DS    0H                  FIND CORRECT TABLE ENTRY
         EX    R1,CONVTEST         DOES CONTROL CHARACTER MATCH?
         BE    CONVGOT             YES, GO THINK ABOUT IT
         LA    R7,CCTABLEN(,R7)    NO, INCREMENT TABLE POINTER
         BCT   R2,CONVLOOP         TRY EVERY TABLE ENTRY
         SPACE 1
CONVGOT  DS    0H                  FOUND CORRECT TABLE ENTRY
         ST    R7,ACC              SAVE TABLE ENTRY ADDRESS
         EJECT ,
***********************************************************************
*        END OF PROCESSING - RETURN TO CALLER                         *
***********************************************************************
         SPACE 1
ENDJOB   DS    0H                  NO MORE DATA FOR JOB
SKIPEND  DS    0H                  RETURN TO CALLER
         LH    R15,RETSKIP         PICK UP RETURN CODE
         B     ENDOFPGM            GO TO COMMON EXIT
         SPACE 1
CONVTEST CLI   CC,*-*              COMPARE CONTROL CHARACTER *EXECUTE*
         DROP  R7                  END TABLE ADDRESSING
         TITLE 'DATA AREAS'
***********************************************************************
*        CONSTANTS AND WORK AREAS                                     *
***********************************************************************
         SPACE 1
         RIN   GENERATE            SAVE AREA
STORSIZE DC    F'0'                SIZE OF GETMAIN AREA
STORLOCN DC    F'0'                LOCATION OF GETMAIN AREA
HEX40    DC    X'40'               BLANK PADDING CHARACTER
         TITLE 'CONTROL CHARACTER TABLES'
***********************************************************************
*                                                                     *
*        CONTROL CHARACTER CONVERSION TABLE                           *
*                                                                     *
***********************************************************************
         SPACE 1
CCTABLE  DS    0F                  START OF TABLE, ALIGNED
         DC    XL1'40',AL1(A),XL1'40',XL1'00'       ASA SPACE ONE
         DC    XL1'09',AL1(M),XL1'00',XL1'40'       M SPACE ONE
         DC    XL1'0B',AL1(M+IMMED),XL1'00',XL1'40' M SPACE ONE
         DC    CL1'0',AL1(A),CL1'0',XL1'00'         ASA SPACE TWO
         DC    XL1'11',AL1(M),XL1'00',CL1'0'        M SPACE TWO
         DC    XL1'13',AL1(M+IMMED),XL1'00',CL1'0'  M SPACE TWO
         DC    CL1'-',AL1(A),CL1'-',XL1'00'         ASA SPACE THREE
         DC    XL1'19',AL1(M),XL1'00',CL1'-'        M SPACE THREE
         DC    XL1'1B',AL1(M+IMMED),XL1'00',CL1'-'  M SPACE THREE
         DC    CL1'+',AL1(A),CL1'+',XL1'00'         ASA SPACE NONE
         DC    XL1'01',AL1(M),XL1'00',CL1'+'        M SPACE NONE
         SPACE 1
         DC    CL1'1',AL1(A),CL1'1',XL1'00'         A SKIP CH 1
         DC    CL1'2',AL1(A),CL1'2',XL1'00'         A SKIP CH 2
         DC    CL1'3',AL1(A),CL1'3',XL1'00'         A SKIP CH 3
         DC    CL1'4',AL1(A),CL1'4',XL1'00'         A SKIP CH 4
         DC    CL1'5',AL1(A),CL1'5',XL1'00'         A SKIP CH 5
         DC    CL1'6',AL1(A),CL1'6',XL1'00'         A SKIP CH 6
         DC    CL1'7',AL1(A),CL1'7',XL1'00'         A SKIP CH 7
         DC    CL1'8',AL1(A),CL1'8',XL1'00'         A SKIP CH 8
         DC    CL1'9',AL1(A),CL1'9',XL1'00'         A SKIP CH 9
         DC    CL1'A',AL1(A),CL1'A',XL1'00'         A SKIP CH 10
         DC    CL1'B',AL1(A),CL1'B',XL1'00'         A SKIP CH 11
         DC    CL1'C',AL1(A),CL1'C',XL1'00'         A SKIP CH 12
         DC    XL1'89',AL1(M),XL1'00',CL1'1'        M SKIP CH 1
         DC    XL1'91',AL1(M),XL1'00',CL1'2'        M SKIP CH 2
         DC    XL1'99',AL1(M),XL1'00',CL1'3'        M SKIP CH 3
         DC    XL1'A1',AL1(M),XL1'00',CL1'4'        M SKIP CH 4
         DC    XL1'A9',AL1(M),XL1'00',CL1'5'        M SKIP CH 5
         DC    XL1'B1',AL1(M),XL1'00',CL1'6'        M SKIP CH 6
         DC    XL1'B9',AL1(M),XL1'00',CL1'7'        M SKIP CH 7
         DC    XL1'C1',AL1(M),XL1'00',CL1'8'        M SKIP CH 8
         DC    XL1'C9',AL1(M),XL1'00',CL1'9'        M SKIP CH 9
         DC    XL1'D1',AL1(M),XL1'00',CL1'A'        M SKIP CH 10
         DC    XL1'D9',AL1(M),XL1'00',CL1'B'        M SKIP CH 11
         DC    XL1'E1',AL1(M),XL1'00',CL1'C'        M SKIP CH 12
         DC    XL1'8B',AL1(M+IMMED),XL1'00',CL1'1'  M SKIP CH 1
         DC    XL1'93',AL1(M+IMMED),XL1'00',CL1'2'  M SKIP CH 2
         DC    XL1'9B',AL1(M+IMMED),XL1'00',CL1'3'  M SKIP CH 3
         DC    XL1'A3',AL1(M+IMMED),XL1'00',CL1'4'  M SKIP CH 4
         DC    XL1'AB',AL1(M+IMMED),XL1'00',CL1'5'  M SKIP CH 5
         DC    XL1'B3',AL1(M+IMMED),XL1'00',CL1'6'  M SKIP CH 6
         DC    XL1'BB',AL1(M+IMMED),XL1'00',CL1'7'  M SKIP CH 7
         DC    XL1'C3',AL1(M+IMMED),XL1'00',CL1'8'  M SKIP CH 8
         DC    XL1'8C',AL1(M+IMMED),XL1'00',CL1'9'  M SKIP CH 9
         DC    XL1'D3',AL1(M+IMMED),XL1'00',CL1'A'  M SKIP CH 10
         DC    XL1'DB',AL1(M+IMMED),XL1'00',CL1'B'  M SKIP CH 11
         DC    XL1'E3',AL1(M+IMMED),XL1'00',CL1'C'  M SKIP CH 12
*                 *CC* *TYPE* *THIS*  *NEXT*
         SPACE 1
CCTABSIZ EQU   *-CCTABLE           LENGTH OF TABLE
CCTABNUM EQU   CCTABSIZ/CCTABLEN   NUMBER OF ENTRIES
         SPACE 1
         DC    XL1'40',AL1(A),XL1'40',XL1'40'    ENTRY FOR NO MATCH
         TITLE 'DATA AREAS'
         PUSH PRINT
         PRINT ON,NOGEN            DO NOT EXPAND STANDARD STUFF
***********************************************************************
*        PRIVATE CONSTANTS AND WORK AREAS                             *
***********************************************************************
         SPACE 2
         DS    0D                  GET ADDRESSES ALIGNED
         DC    CL8'EYECATCH'       EYECATCHER
         TITLE 'LITERAL POOL'
***********************************************************************
*        LITERAL POOL                                                 *
***********************************************************************
         SPACE 1
         LTORG ,                   LITERAL POOL GOES HERE
         SPACE 3
         POP   PRINT               RESTORE PRINT STATUS
         SPACE 3
SKIPBACA CHEWBACA TYPE=DSECT       MAP COMMON AREA
         SPACE 3
         END
