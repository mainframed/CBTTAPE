         MACRO
&LABEL   ZRETURN &RC=0
.*
.*
.*       PDSDOC *PROGRAM EXIT MACRO, RE-ENTRANT.......................* ZRETURN
.*
.*  &&RC=          NUMBER TO BE RETURNED TO CALLING CSECT, OR REG 15
.*                 SPECIFIED FOR INDICATING PRESET CODE  ALREADY
.*
         GBLA  &SIZE         PASSED FROM GETMAIN IN ZCSECT FOR FREEMAIN
         GBLC  &NAME(100)              ARRAY HOLDING &SYSECT AND &SIZE
         GBLA  &INDX                   LENGTH OF ARRAY
.*
         LCLA  &B                      INDEX FOR ARRAY
         LCLC  &WORKC                  WORK AREA SIZE
         LCLC  &WORKD                  SUBPOOL FOR STORAGE
.*
.* ****  RESTORE SIZE TO CORRECT CSECT VALUE
.*
&B       SETA  1
.SEARCH  AIF   ('&NAME(&B)' EQ '&SYSECT').SETSIZE
&B       SETA  &B+4
         AIF   ('&B' GE '&INDX').SETA0
         AGO   .SEARCH
.SETSIZE ANOP
&B       SETA  &B+1
&SIZE    SETA  &NAME(&B)
&WORKC   SETC  '&NAME(&B+1)'
&WORKD   SETC  '&NAME(&B+2)'
         AGO   .RESTORE
.SETA0   ANOP
&SIZE    SETA  0
.*
.* ****  RESTORE REGISTERS AND FREE SAVEAREA(S) IF NECESSARY
.*
.RESTORE ANOP
&LABEL   L     13,4(0,13)
         AIF   (&SIZE EQ 0).RETURN
         L     1,8(13)
         LR    11,15                   SAVE RETURN CODE REGISTER
         FREEMAIN R,SP=&WORKD,LV=&SIZE+&WORKC,A=(1)
         LR    15,11                   RESTORE RETURN CODE
.RETURN  ANOP
         RETURN (14,12),T,RC=&RC
         MEND
