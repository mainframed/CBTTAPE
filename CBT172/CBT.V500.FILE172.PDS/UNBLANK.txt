***********************************************************************
*        PDSDOC *SUB-ROUTINE REMOVE IMBEDDED BLANKS FROM TEXT - TO END* UNBLANK
*                                                                     *
*                  LEFT SHUFFLE SUBROUTINE                            *
*              THIS ROUTINE WILL ELIMINATE EMBEDDED SPACES FROM       *
*              AN AREA OF CORE SPECFIED BY THE USER. THE RESULT       *
*              WILL BE LEFT JUSTIFIED WITH TRAILING SPACES.           *
*                                                                     *
*        ON ENTRY:                                                    *
*              R0   =  LENGTH OF STORAGE ELEMENT                      *
*              R1   -> ADDRESS OF STORAGE ELEMENT                     *
*                                                                     *
*        ON EXIT:                                                     *
*              R0   =  LENGTH OF NON-BLANK DATA                       *
*              R1   -> ADDRESS OF STORAGE ELEMENT  (UNCHANGED)        *
*                                                                     *
* 17JAN95 CART1 UPDATE LENGTH AT END                                  *
* 22APR93 CART1 COPIED OUT OF T583P03                                 *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
UNBLANK  DS    0H                  ELIMINATE BLANKS
         RIN   (R2,R3,R5,R6)       SUBROUTINE ENTRY
         LR    R2,R1
         LR    R5,R0
         BCTR  R5,0
LFTSPC   EQU   *
         CLI   0(R2),C' '
         BE    LFTNOSPC
         LA    R2,1(0,R2)
         BCT   R5,LFTSPC
         B     LFTEXIT
         SPACE
LFTNOSPC EQU   *
         SR    R6,R6
          LA   R3,1(0,R2)
         BCT   R5,LFTNOLP
*              SPECIAL CASE OF BLANK SECOND TO LAST DIGIT.
         MVC   0(1,R2),0(R3)
         MVI   0(R3),C' '
         B     LFTEXIT
         SPACE
LFTNOLP  EQU   *
         CLI   0(R3),C' '
         BNE   LFTMOVE
         LA    R3,1(0,R3)
         LA    R6,1(0,R6)
         BCT   R5,LFTNOLP
         B     LFTEXIT
         EJECT ,
LFTMOVE  EQU   *
         EX    R5,LFTMOVE1
         LA    R3,1(R5,R2)
         MVI   0(R3),C' '
         LTR   R6,R6
         BZ    LFTSPC
         BCTR  R6,0
         EX    R6,LFTMOVE2
         LA    R2,1(0,R2)
         BCT   R5,LFTSPC
         SPACE 1
LFTEXIT  DS    0H                  END OF SUBROUTINE
         SR    R2,R1               CALCULATE LENGTH
         LR    R0,R2               RETURN IT IN R0
         ROUT  ,                   RETURN TO CALLER
         SPACE 2
LFTMOVE1 MVC   0(0,R2),0(R3)                                  *EXECUTE*
LFTMOVE2 MVC   1(0,R3),0(R3)                                  *EXECUTE*
