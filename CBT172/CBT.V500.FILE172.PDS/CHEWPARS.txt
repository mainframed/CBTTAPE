CHEWPARS TITLE 'CHEW COMMAND PARSING ROUTINE'
***********************************************************************
*        PDSDOC *PARSE COMMANDS AND PROCESS...........................* CHEWPARS
*                                                                     *
*        PROGRAM ID. :             CHEWPARS                           *
*                                                                     *
*        DATE WRITTEN :            JANUARY   1995                     *
*                                                                     *
*        AUTHOR :                  D.H.CARTWRIGHT.                    *
*                                                                     *
*        LOCATION :                SUTTON COLDFIELD                   *
*                                                                     *
*        HISTORY  :                                                   *
*                                                                     *
*   29OCT98 DHC CORRECTED WTO OFFSETS                                 *
*   JAN95 STARTED WRITING BASED ON THE OLD 'MONEXWTR' PROGRAM         *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*       ---------                                                     *
*                                                                     *
* THIS IS A SUB-PROGRAM WITHIN THE 'CHEW' EXTERNAL WRITER SUITE.      *
* IT PROCESSES COMMANDS WHICH ARE INPUT FROM THE CONSOLE, FROM SYSIN  *
* OR FROM THE PROGRAM PARM.                                           *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*       ------------                                                  *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT ,
***********************************************************************
*                                                                     *
*       INPUT                                                         *
*       -----                                                         *
*                                                                     *
* ON ENTRY R1 ---> CHEWBACA COMMON AREA WHICH IS ALWAYS ADDRESSED BY  *
* R11. THE FULLWORD AT 'ACOMMAND' IS THE ADDRESS OF A PARAMETER AREA. *
* THE PARAMETER AREA CONSISTS OF A HALFWORD LENGTH FIELD FOLLOWED BY  *
* A COMMAND AREA.  THE LENGTH IS THAT OF THE COMMAND AREA AND DOES    *
* NOT INCLUDE ITSELF.                                                 *
*                                                                     *
***********************************************************************
         SPACE 3
***********************************************************************
*                                                                     *
*       OUTPUT                                                        *
*       ------                                                        *
*                                                                     *
* VARIOUS FIELDS WITHIN 'CHEWBACA' MAY BE UPDATED BY THIS ROUTINE.    *
* THIS PROGRAM MAY OR MAY NOT ISSUE A WTO MESSAGE.                    *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT ON,GEN              SHOW MACRO EXPANSIONS
*        PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
         SPACE 1
CHEWPARS CSECT ,
         SPACE 1
         PGMIN EQUATES=NO,USING=(R13) STANDARD HOUSEKEEPING
         SPACE 1
         LM    R11,R12,0(R11)      (RE)COPY COMMON AREA POINTERS
         USING CHEWBACA,R11,R12    ESTABLISH ADDRESSABILITY
         XC    RETPARS,RETPARS     SET DEFAULT RETURN CODE
         TITLE 'OPERATOR COMMAND HANDLING'
***********************************************************************
*        VALIDATE INPUT DATA FROM OPERATOR                            *
***********************************************************************
         SPACE 2
         L     R1,ACOMMAND         PICK UP PARAMETER AREA ADDRESS
         LH    R15,0(,R1)          GET LENGTH OF PARAMETERS
         LTR   R15,R15             ANYTHING?
         BZ    OPTNEXIT            NO, GO BACK TO CALLER
         LA    R14,2(,R1)          JUMP OVER THE LENGTH
         PUSH PRINT                SAVE PRINT VALUES
         PRINT GEN                 SHOW SCAN MACRO
         SCAN  DATA=(R14),LENGTH=(R15),DELIM=('=',','),END=(' '),      *
               RETAREA=SCANBLKS    BREAK DOWN PARAMETERS
         POP   PRINT               RESTORE PRINT VALUES
         SPACE 2
***********************************************************************
*        ANALYZE HIS INPUT PARAMETERS AND REPORT                      *
***********************************************************************
         SPACE 1
         L     R2,SCANBLKS         LOAD ADDRESS OF FIRST PART
         LTR   R2,R2               NOTHING BUT DELIMITERS ?
         BNZ   PARMLOOP            NO, SOMETHING THERE TO ANALYSE
         BAL   R4,ERROR1           YES, BETTER TELL HIM ABOUT IT
         B     OPTNEXIT            THEN GIVE UP
         SPACE 1
PARMLOOP XR    R15,R15             CLEAR WORK REGISTER
         IC    R15,4(R2)           LOAD LENGTH OF KEYWORD
         L     R14,4(R2)           LOAD ADDRESS OF KEYWORD
         BAL   R4,TESTKEY          GO CHECK VALIDITY
         BALR  R4,R1               GO PROCESS THE KEYWORD
         SPACE 1
         L     R2,0(R2)            LOAD ADDRESS OF NEXT ENTRY
         LTR   R2,R2               TEST FOR MORE DATA IN PARM FIELD
         BNZ   PARMLOOP            MORE. SO GO PROCESS IT.
         EJECT ,
***********************************************************************
*        END OF PROCESSING - RETURN TO CALLER                         *
***********************************************************************
         SPACE 1
OPTNEXIT DS    0H                  RETURN TO CALLER
         LH    R15,RETPARS         PICK UP RETURN CODE
         B     ENDOFPGM            GO TO COMMON EXIT
         TITLE 'SUB-ROUTINES'
***********************************************************************
*        CHECK THE VALIDITY OF KEYWORDS IN THE PARM FIELD             *
***********************************************************************
         SPACE 2
TESTKEY  DS    0H
         SUBIN ,                   SUBROUTINE ENTRY
         LA    R0,WORKKEY          START ADDRESS OF KEYWORD WORK AREA
         LA    R1,L'WORKKEY        LENGTH OF WORK AREA
         ICM   R15,8,=C' '         LOAD PADDING CHARACTER FOR MOVE
         MVCL  R0,R14              MOVE PADDED KEY WORD TO WORK AREA
         LA    R1,PARMTABL         LOAD START ADDRESS OF KEYWORD TABLE
         LA    R14,PARMLEN         LENGTH OF A PARMTAB ENTRY
         LA    R15,PARMEND-1       END ADDRESS FOR BXLE LOOP
KEYLOOP  CLC   WORKKEY,0(R1)       VALID KEYWORD FOUND ?
         BE    GOTAKEY             YES. GO LOAD ADDRESS OF PROCESSOR
         BXLE  R1,R14,KEYLOOP      NO.  TRY NEXT ENTRY IN TABLE
         LA    R1,ERROR2           TROUBLE. KEYWORD NOT FOUND
         B     TESTKEYX            RETURN WITH ERROR PROCESSOR
         SPACE 1
GOTAKEY  L     R1,L'KEYNAME(R1)    LOAD ADDRESS OF PROCESSOR
         SPACE 1
TESTKEYX DS    0H                  RETURN TO CALLER
         SUBOUT ,                  SUBROUTINE EXIT
         EJECT ,
***********************************************************************
*        TABLE OF VALID KEYWORDS AND PROCESSING ROUTINES              *
***********************************************************************
         SPACE 2
PARMTABL DS    0F                  PARM FIELD KEYWORD TABLE
KEYNAME  DC    CL8'QUEUE'          FIRST KEYWORD
         DC    AL4(QUEUEKEY)       ADDRESS OF PROCESSOR
PARMLEN  EQU   *-PARMTABL          LENGTH OF AN ENTRY
         DC    CL8'Q'              ALTERNATE KEYWORD
         DC    AL4(QUEUEKEY)       ADDRESS OF PROCESSOR
         DC    CL8'CLASS'          ALTERNATE KEYWORD
         DC    AL4(QUEUEKEY)       ADDRESS OF PROCESSOR
         DC    CL8'ROUTE'          OPTIONAL KEYWORD
         DC    AL4(ROUTEKEY)       ADDRESS OF PROCESSOR
         DC    CL8'R'              ALTERNATE KEYWORD
         DC    AL4(ROUTEKEY)       ADDRESS OF PROCESSOR
         DC    CL8'DEST'           OPTIONAL KEYWORD
         DC    AL4(NOSUPORT)       ADDRESS OF PROCESSOR
         DC    CL8'D'              ALTERNATE KEYWORD
         DC    AL4(NOSUPORT)       ADDRESS OF PROCESSOR
         DC    CL8'FORM'           OPTIONAL KEYWORD
         DC    AL4(NOSUPORT)       ADDRESS OF PROCESSOR
         DC    CL8'F'              ALTERNATE KEYWORD
         DC    AL4(NOSUPORT)       ADDRESS OF PROCESSOR
         DC    CL8'REPORT'         ALTERNATE KEYWORD
         DC    AL4(REQREPT)        ADDRESS OF PROCESSOR
         DC    CL8'STOPEOF'        OPTIONAL KEYWORD
         DC    AL4(EOFKEY)         ADDRESS OF PROCESSOR
         DC    CL8'STATUS'         OPTIONAL KEYWORD
         DC    AL4(STATUS)         ADDRESS OF PROCESSOR
         DC    CL8'NOMSG'          OPTIONAL KEYWORD
         DC    AL4(NOWTOKEY)       ADDRESS OF PROCESSOR
         DC    CL8'MSG'            OPTIONAL KEYWORD
         DC    AL4(WTOKEY)         ADDRESS OF PROCESSOR
         DC    CL8'NOTSUMSG'       OPTIONAL KEYWORD
         DC    AL4(NOTSUKEY)       ADDRESS OF PROCESSOR
         DC    CL8'TSUMSG'         OPTIONAL KEYWORD
         DC    AL4(TSUKEY)         ADDRESS OF PROCESSOR
         DC    CL8'PRINT'          OPTIONAL KEYWORD
         DC    AL4(REQALL)         ADDRESS OF PROCESSOR
         DC    CL8'NOPRINT'        OPTIONAL KEYWORD
         DC    AL4(NREQALL)        ADDRESS OF PROCESSOR
         DC    CL8'NOGO'           OPTIONAL KEYWORD
         DC    AL4(TERMIN8)        ADDRESS OF PROCESSOR
         SPACE 1
PARMEND  EQU   *                   END OF PARM KEYWORD TABLE
         EJECT ,
***********************************************************************
*        THE OPERATOR HAS ENTERED A QUEUE CLASS LIST                  *
***********************************************************************
         SPACE 2
QUEUEKEY DS    0H                  PROCESS A QUEUE DEFINITION
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         LA    R10,SSAREA          LOAD ADDRESS OF SSOB
         USING SSOB,R10            ESTABLISH ADDRESSABILITY
         SPACE 2
         L     R4,0(R2)            LOAD ADDRESS NEXT SCAN BLOCK
         LTR   R4,R4               CODED ?
         BNZ   QPARAM              YES, GO PROCESS IT
         TM    SSSOFLG1,FF-SSSOSCLS-SSSOHLD THE ONLY CURRENT SELECTION?
         BNZ   QNULLOK             NO, WE CAN LET HIM NULLIFY
         BAL   R4,ERROR4           TELL HIM NOT ALLOWED
         B     QEXIT               THEN GET OUT QUICK
         SPACE 1
QNULLOK  DS    0H                  NULLIFY SELECTION CRITERION
         NI    SSSOFLG1,FF-SSSOSCLS TURN OFF SELECTION CRITERION
         B     QEXIT               END OF PROCESSING
         SPACE 2
QPARAM   DS    0H                  PROCESS A QUEUE DEFINITION
         L     R2,0(R2)            LOAD ADDRESS NEXT SCAN BLOCK
         XR    R15,R15             CLEAR WORK REGISTER
         IC    R15,4(R2)           LOAD LENGTH OF DATA
         CH    R15,=H'8'           IF IT'S MORE THAN 8 I DONT WANT IT
         BNH   QPARMOK             NO, WE CAN LET HIM DO THAT
         BAL   R4,ERROR8           TELL HIM NOT ALLOWED
         B     QEXIT               THEN GET OUT QUICK
         SPACE 1
QPARMOK  DS    0H                  CHANGE SELECTION CRITERION
         L     R14,4(R2)           LOAD ADDRESS OF DATA
         ICM   R15,8,=C' '         LOAD UP A PADDING CHARACTER
         LA    R0,CHEWCLSL         LOAD ADDRESS OF RECEIVING FIELD
         LA    R1,L'CHEWCLSL       LOAD LENGTH OF IT
         MVCL  R0,R14              MOVE THE PARAMETER TO WORK AREA
         OI    SSSOFLG1,SSSOSCLS   ENSURE SELECTION CRITERION USED
         BAL   R4,STATCLS          GO CONFIRM ACTIVITY
         SPACE 1
QEXIT    DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R4)       SUBROUTINE EXIT
         SPACE 1
         DROP  R10                 END SSOB ADDRESSABILITY
         EJECT ,
***********************************************************************
*        THE OPERATOR WISHES TO CHANGE THE INPUT ROUTE CODE           *
***********************************************************************
         SPACE 2
ROUTEKEY DS    0H
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         LA    R10,SSAREA          LOAD ADDRESS OF SSOB
         USING SSOB,R10            ESTABLISH ADDRESSABILITY
         SPACE 1
         L     R4,0(R2)            LOAD ADDRESS NEXT SCAN BLOCK
         LTR   R4,R4               CODED ?
         BNZ   RPARAM              YES, GO PROCESS IT
         TM    SSSOFLG1,FF-SSSODST-SSSOHLD THE ONLY CURRENT SELECTION?
         BNZ   RNULLOK             NO, WE CAN LET HIM NULLIFY
         BAL   R4,ERROR4           TELL HIM NOT ALLOWED
         B     REXIT               THEN GET OUT QUICK
         SPACE 1
RNULLOK  DS    0H                  NULLIFY SELECTION CRITERION
         NI    SSSOFLG1,FF-SSSODST  TURN OFF SELECTION CRITERION
         B     REXIT               END OF PROCESSING
         SPACE 2
RPARAM   DS    0H                  PROCESS A DEFINITION
         L     R2,0(R2)            LOAD ADDRESS NEXT SCAN BLOCK
         XR    R15,R15             CLEAR WORK REGISTER
         IC    R15,4(R2)           LOAD LENGTH OF DATA
         CH    R15,=H'8'           IF IT'S MORE THAN 8 I DONT WANT IT
         BNH   RPARMOK             NO, WE CAN LET HIM DO THAT
         BAL   R4,ERROR8           TELL HIM NOT ALLOWED
         B     REXIT               THEN GET OUT QUICK
         SPACE 1
RPARMOK  DS    0H                  CHANGE SELECTION CRITERION
         L     R14,4(R2)           LOAD ADDRESS OF DATA
         ICM   R15,8,=C' '         LOAD UP A PADDING CHARACTER
         LA    R0,CHEWDEST         LOAD ADDRESS OF RECEIVING FIELD
         LA    R1,L'CHEWDEST       LOAD LENGTH OF IT
         MVCL  R0,R14              MOVE THE JOBNAME TO WORK AREA
         OI    SSSOFLG1,SSSODST    ENSURE SELECTION CRITERION USED
         BAL   R4,STATRMT          GO CONFIRM ACTIVITY
         SPACE 1
REXIT    DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R4)       SUBROUTINE EXIT
         SPACE 1
         DROP  R10                 END SSOB ADDRESSABILITY
         EJECT ,
***********************************************************************
*        THE OPERATOR WANTS TO KNOW THE CURRENT STATUS                *
***********************************************************************
         SPACE 2
STATUS   DS    0H                  TELL OPERATOR OF SELECTION CRITERIA
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         LA    R10,SSAREA          LOAD ADDRESS OF SSOB
         USING SSOB,R10            ESTABLISH ADDRESSABILITY
         SPACE 1
         TM    SSSOFLG1,SSSOSCLS   ARE WE SELECTING ON CLASS?
         BNO   STATUS02            NO,TRY NEXT PARAMETER
         BAL   R4,STATCLS          GO TELL OPERATOR ABOUT IT
         SPACE 1
STATUS02 DS    0H                  TELL OPERATOR OF ROUTE
         TM    SSSOFLG1,SSSODST    ARE WE SELECTING ON ROUTE?
         BNO   STATUS03            NO,TRY NEXT PARAMETER
         BAL   R4,STATRMT          GO TELL OPERATOR ABOUT IT
         SPACE 1
STATUS03 DS    0H                  TELL OPERATOR OF
         BAL   R4,STATNUMS         GO TELL OPERATOR ABOUT NUMBERS
         BAL   R4,STATFLAG         GO TELL OPERATOR ABOUT OPTIONS
         SPACE 1
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         SPACE 1
         DROP  R10                 END SSOB ADDRESSABILITY
         EJECT ,
***********************************************************************
*        TELL THE OPERATOR THE CURRENT CLASS                          *
***********************************************************************
         SPACE 2
STATCLS  DS    0H                  TELL OPERATOR OF CLASS
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         USING SSOB,R10            ESTABLISH ADDRESSABILITY
         SPACE 1
         MVC   WTOSTKEY,=CL8'CLASS' TELL HIM ABOUT CLASS
         MVC   WTOSTVAL,=CL8' '    ENSURE DISPLAY FIELD CLEAR
         MVC   WTOSTVAL,CHEWCLSL   SHOW CURRENT VALUE
         BAL   R4,WTOSTATE         GO TELL OPERATOR ABOUT IT
         SPACE 1
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         SPACE 2
***********************************************************************
*        TELL THE OPERATOR THE CURRENT ROUTE CODE                     *
***********************************************************************
         SPACE 2
STATRMT  DS    0H                  TELL OPERATOR OF CLASS
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         USING SSOB,R10            ESTABLISH ADDRESSABILITY
         SPACE 1
         MVC   WTOSTKEY,=CL8'ROUTE' TELL HIM
         MVC   WTOSTVAL,=CL8' '    ENSURE DISPLAY FIELD CLEAR
         MVC   WTOSTVAL(L'CHEWDEST),CHEWDEST SHOW CURRENT VALUES
         BAL   R4,WTOSTATE         GO TELL OPERATOR ABOUT IT
         SPACE 1
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         SPACE 1
         DROP  R10                 END SSOB ADDRESSABILITY
         SPACE 2
***********************************************************************
*        TELL THE OPERATOR THE CURRENT JOB COUNTS                     *
***********************************************************************
         SPACE 2
STATNUMS DS    0H                  TELL OPERATOR STATISTICS
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         MVC   WTOSTKEY,WTOSTKJB   TELL HIM OF JOBS
         MVC   WTOSTVAL,EDITPL4    MOVE IN EDIT PATTERN
         ED    WTOSTVAL,JOBCOUNT   SHOW OPERATOR CURRENT VALUE
         BAL   R4,WTOSTATE         GO TELL OPERATOR ABOUT IT
         SPACE 1
         MVC   WTOSTKEY,WTOSTKDS   TELL HIM OF DATASETS
         MVC   WTOSTVAL,EDITPL4    MOVE IN EDIT PATTERN
         ED    WTOSTVAL,JESCOUNT   SHOW OPERATOR CURRENT VALUE
         BAL   R4,WTOSTATE         GO TELL OPERATOR ABOUT IT
         SPACE 1
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         EJECT ,
***********************************************************************
*        TELL THE OPERATOR THE RUNFLAG STATUS                         *
***********************************************************************
         SPACE 2
STATFLAG DS    0H                  TELL OPERATOR OF OPTIONS
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
STATFLG1 DS    0H                  LOOK AT NEXT OPTION
         TM    RUNFLAG,RUNNOTSU    DOING TSU MESSAGES?
         BNO   STATFLG2            NO, SKIP IT
         MVC   WTOFKEY,WTOFNTSU    SET UP MESSAGE
         BAL   R4,WTOFLAG          GO TELL OPERATOR ABOUT IT
         SPACE 1
STATFLG2 DS    0H                  LOOK AT NEXT OPTION
         TM    RUNFLAG,PRINTALL    PRINTING EVERYTHING?
         BNO   STATFLG3            NO, SKIP IT
         MVC   WTOFKEY,WTOFPALL    SET UP MESSAGE
         BAL   R4,WTOFLAG          GO TELL OPERATOR ABOUT IT
         SPACE 1
STATFLG3 DS    0H                  LOOK AT NEXT OPTION
         TM    RUNFLAG,RUNFSEOF    STOPPING AT END OF WORK?
         BNO   STATFLG4            NO, SKIP IT
         MVC   WTOFKEY,WTOFSEOF    SET UP MESSAGE
         BAL   R4,WTOFLAG          GO TELL OPERATOR ABOUT IT
         SPACE 1
STATFLG4 DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R4)       SUBROUTINE EXIT
         EJECT ,
***********************************************************************
*        OPERATOR WANTS EVERYTHING REPRINTED                          *
***********************************************************************
         SPACE 2
REQALL   DS    0F                  OPERATOR/USER WANTS REPRINTS
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         OI    RUNFLAG,PRINTALL    SET FLAG
         BAL   R4,WTOOK            CONFIRM ACTION
         SPACE 1
         DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R4)       SUBROUTINE EXIT
         SPACE 3
***********************************************************************
*        OPERATOR DOES NOT WANT EVERYTHING REPRINTED                  *
***********************************************************************
         SPACE 2
NREQALL  DS    0F                  OPERATOR/USER DOEN'T WANT REPRINTS
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         NI    RUNFLAG,FF-PRINTALL RESET FLAG
         BAL   R4,WTOOK            CONFIRM ACTION
         SPACE 1
         DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R4)       SUBROUTINE EXIT
         EJECT ,
***********************************************************************
*        OPERATOR REQUESTS A REPORT                                   *
***********************************************************************
         SPACE 2
REQREPT  DS    0F                  OPERATOR WANTS A REPORT
         SUBIN SAVE=(R2,R4)        SUBROUTINE ENTRY
         LA    R2,SYSUT3SW         CHECK ON SUMMARY FILE
         USING MDCB,R2             ESTABLISH ADDRESSABILITY
         TM    MDCBFLAG,DDINT      STATUS KNOWN?
         BO    REQREPT2            YES,CARRY ON
         OI    MDCBFLAG,DDINT      NO, EXPRESS INTEREST
         BAL   R4,TESTIOT          SEE IF PRESENT
         SPACE 1
REQREPT2 DS    0F                  ABLE TO TRY TO REPORT
         LR    R1,R11              COPY COMMON AREA ADDRESS
         LA    R15,CPBREPT
         BAL   R4,CALLIT           GO CREATE REPORT
         BAL   R4,WTOOK            CONFIRM ACTION
         SPACE 1
         DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R2,R4)    SUBROUTINE EXIT
         SPACE 1
         DROP  R2                  END MDCB ADDRESSABILITY
         EJECT ,
***********************************************************************
*        THE OPERATOR WISHES THE PROGRAM TO STOP AT END OF WORK       *
***********************************************************************
         SPACE 2
EOFKEY   DS    0H                  STOP INSTEAD OF WAITING FOR WORK
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         OI    RUNFLAG,RUNFSEOF    SET FLAG
         BAL   R4,WTOOK            GO CONFIRM ACTIVITY
         SPACE 1
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         SPACE 3
***********************************************************************
*        SUPPRESS WTO PROCESSING                                      *
***********************************************************************
         SPACE 2
NOWTOKEY DS    0F                  OPERATOR/USER DOESN'T WANT WTO'S
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         OI    RUNFLAG,RUNNOWTO    SET FLAG
         BAL   R4,WTOOK            GO CONFIRM ACTIVITY
         SPACE 1
         DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         SPACE 3
***********************************************************************
*        REINSTATE WTO PROCESSING                                     *
***********************************************************************
         SPACE 2
WTOKEY   DS    0F                  OPERATOR/USER WANTS WTO'S
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         NI    RUNFLAG,FF-RUNNOWTO RESET FLAG
         BAL   R4,WTOOK            GO CONFIRM ACTIVITY
         SPACE 1
         DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         EJECT ,
***********************************************************************
*        SUPPRESS WTOR PROCESSING FOR TSU OUTPUT                      *
***********************************************************************
         SPACE 2
NOTSUKEY DS    0F                  USER DOESN'T WANT WTOR'S FOR TSU
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         OI    RUNFLAG,RUNNOTSU    SET FLAG
         BAL   R4,WTOOK            GO CONFIRM ACTIVITY
         SPACE 1
         DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         SPACE 3
***********************************************************************
*        REINSTATE WTOR PROCESSING FOR TSU OUTPUT                     *
***********************************************************************
         SPACE 2
TSUKEY   DS    0F                  USER WANTS WTOR'S FOR TSU
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         NI    RUNFLAG,FF-RUNNOTSU RESET FLAG
         BAL   R4,WTOOK            GO CONFIRM ACTIVITY
         SPACE 1
         DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         EJECT ,
         SPACE 3
***********************************************************************
*        THE USER WANTS TO TERMINATE - NOW                            *
***********************************************************************
         SPACE 2
TERMIN8  DS    0F                  USER WANTS TO END
         SUBIN ,                   SUBROUTINE ENTRY
         LA    R15,8               SET HIGH RETIURN CODE
         STH   R15,RETPARS         SAVE IN DATA AREA
         B     ENDOFPGM            IMMEDIATE EXIT
         EJECT ,
***********************************************************************
*        WRITE TO OPERATOR SUBROUTINE                                 *
***********************************************************************
         SPACE 2
CHEWWTO  DS     0F
         SUBIN ,                   SUBROUTINE ENTRY
         TM    RUNFLAG,RUNNOWTO    ARE WE SUPPRESSING WTOS?
         BO    CHEWWTX             YES,SKIP IT
         XR    R0,R0               CLEAR MCS FLAGS AND ID
         IC    R0,UCMID            LOAD ID OF RECEIVING CONSOLE
         WTO   MF=(E,(1)),MCSFLAG=(0) WRITE OUT THE ERROR MESSAGE
         SPACE 1
CHEWWTX  DS     0H                  RETURN TO CALLER
         SUBOUT ,                  SUBROUTINE EXIT
         EJECT ,
         COPY  CALLITR             COPY PROGRAM LINKAGE
         EJECT ,
         TITLE 'DATA AREAS'
         PUSH PRINT
*        PRINT ON,NOGEN            DO NOT EXPAND STANDARD STUFF
***********************************************************************
*        PRIVATE CONSTANTS AND WORK AREAS                             *
***********************************************************************
         SPACE 2
         DS    0D                  GET ADDRESSES ALIGNED
         DC    CL8'EYECATCH'       EYECATCHER
         RIN   GENERATE            SAVE AREA
         EJECT ,
***********************************************************************
*        INPUT MODIFY COMMAND RESPONSES                               *
***********************************************************************
         SPACE 2
ERROR1   DS    0F                  NO PARAMETERS SPECIFIED
         BAL   R1,CHEWWTO          LOAD ADDRESS OF PARM LIST
         WTO   'CHEW301 - NO PARAMETERS SPECIFIED',MF=L
ERROR2   DS    0F                  INVALID KEYWORD
         BAL   R1,CHEWWTO          LOAD ADDRESS OF PARM LIST
         WTO   'CHEW302 - KEYWORD IS INVALID',MF=L
ERROR3   DS    0F                  NO PERCENTAGE SPECIFIED
         BAL   R1,CHEWWTO          LOAD ADDRESS OF PARM LIST
         WTO   'CHEW303 - OPERAND FIELD OF KEYWORD MISSING',MF=L
ERROR4   DS    0F                  OPERAND NULLIFIED IS LAST REMAINING
         BAL   R1,CHEWWTO          LOAD ADDRESS OF PARM LIST
         WTO   'CHEW304 - INVALID REMOVAL OF ONLY SELECTION',MF=L
ERROR8   DS    0F                  PARAMETER EXCEEDS EIGHT CHARACTERS
         BAL   R1,CHEWWTO          LOAD PARAMETER REGISTER
         WTO   'CHEW305 - EXCESSIVE PARAMETER LENGTH',MF=L
NOSUPORT DS    0F                  PARAMETER NOT SUPPORTED
         BAL   R1,CHEWWTO          LOAD PARAMETER REGISTER
         WTO   'CHEW399 - REQUEST NOT YET SUPPORTED',MF=L
WTOOK    DS    0F                  PARAMETERS ACCEPTED
         BAL   R1,CHEWWTO          LOAD ADDRESS OF PARM LIST
         WTO   'CHEW100 - PARAMETERS ACCEPTED',MF=L
         SPACE 1
WTOSTATE DS    0F                  STATUS MESSAGE SKELETON
         BAL   R1,CHEWWTO          LOAD ADDRESS OF PARM LIST
         WTO   'CHEW101: XXXXXXXX = YYYYYYYY',MF=L
WTOSTNUM EQU   WTOSTATE+4+WTOLEN+6,1 MESSAGE NUMBER POSITION, LENGTH
WTOSTKEY EQU   WTOSTATE+4+WTOLEN+9,8 KEYWORD POSITION,LENGTH
WTOSTVAL EQU   WTOSTATE+4+WTOLEN+20,8 VALUE POSITION,LENGTH
         SPACE 1
WTOSTKJB DC    CL8'NO. JOBS'       NUMBER OF JOBS KEYWORD
WTOSTKDS DC    CL8'DATASETS='      NUMBER OF DATASETS KEYWORD
         SPACE 1
WTOFLAG  DS    0F                  OPTIONS MESSAGE SKELETON
         BAL   R1,CHEWWTO          LOAD ADDRESS OF PARM LIST
         WTO   'CHEW111: XXXXXXXX OPTION IS IN EFFECT',MF=L
WTOFNUM  EQU   WTOFLAG+4+WTOLEN+6,1 MESSAGE NUMBER POSITION, LENGTH
WTOFKEY  EQU   WTOFLAG+4+WTOLEN+9,8 KEYWORD POSITION,LENGTH
         SPACE 1
WTOFNTSU DC    CL8'NOTSUMSG'       NOTSUMSG KEYWORD OPTION
WTOFPALL DC    CL8'PRINT'          PRINT    KEYWORD OPTION
WTOFSEOF DC    CL8'STOPEOF'        STOPEOF  KEYWORD OPTION
         TITLE 'LITERAL POOL'
***********************************************************************
*        LITERAL POOL                                                 *
***********************************************************************
         SPACE 1
         LTORG ,                   LITERAL POOL GOES HERE
         SPACE 3
         POP   PRINT               RESTORE PRINT STATUS
         SPACE 3
PARSBACA CHEWBACA TYPE=DSECT       MAP COMMON AREA
         TITLE 'DSECT MAPS'
***********************************************************************
*        MESSAGE SCANNING TABLE FORMAT                                *
***********************************************************************
         SPACE 1
MSGENTRY DSECT ,                   FORMAT OF A MSG TABLE ENTRY
MSGTEXT  DS    CL7                 MESSAGE NUMBER (SEE MSGS & CODES)
MSGFLAGJ DS    AL1(0)              JOB FLAGS CAUSED BY MSG
MSGFLAGD DS    AL1(0)              DATASET FLAGS CAUSED BY MSG
MSGFLAGL DS    AL1(0)              LINE FLAGS FOR MSG
         SPACE 1
MSGHILYT EQU   X'80'               MESSAGE TO BE HI-LIGHTED <---------
         SPACE 1
MSGTLEN  EQU   *-MSGENTRY          LENGTH OF TABLE ENTRY
         EJECT ,
         PUSH  PRINT               SAVE PRINT STATUS
         PRINT ON,GEN              EXPAND SYSTEM DSECTS
         SPACE 3
COMDSECT DSECT ,                   COMMUNICATIONS AREA
         IEZCOM ,                  MAP COMMUNICATIONS AREA
         EJECT ,
CIBDSECT DSECT ,                   COMMAND INPUT BUFFER
         IEZCIB ,                  MAP COMMAND BUFFER
         EJECT ,
CVTPTR   EQU   16                  CVT POINTER
         SPACE 1
CVTMAP   DSECT                     CVT MAP
         DS    37D                 FILLER
CVTJESCT DS    F                   JES COMMUNICATION TABLE ADDRESS
         SPACE 3
         END
         SPACE 3
