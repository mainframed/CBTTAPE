***********************************************************************
*        PDSDOC *ROUTINE TO FIND COMPONENTS OF A VVR..................* PARSEVVR
*                                                                     *
*        THIS ROUTINE WILL FIND THE DATASET INFORMATION CELL AND THE  *
*        AMDSB CELL FROM THE VVR RECORD POINTED TO BY REG. 9          *
*                                                                     *
*                                                                     *
*        ON ENTRY:                                                    *
*                                                                     *
*        R9  --> AREA CONTAINING VVR, FIRST WORD BEING THE LENGTH     *
*                                                                     *
*        ON EXIT:                                                     *
*                                                                     *
*        R5  --> VOL INFO CELL    (TYPE@23)                           *
*        R6  --> AMDSB CELL       (TYPE@60)                           *
*        R7  --> DSINFO CELL      (TYPE@21)                           *
*                                                                     *
***********************************************************************
         SPACE 1
PARSEVVR RIN   ,                   PROCESS VVR RECORD
         LA    R9,4(,R9)           POINT TO VVR
         SR    R6,R6               CLEAR AMDSB POINTER
         SR    R7,R7               CLEAR DS.INFO POINTER
         LH    R15,0(,R9)          GET RECORD LENGTH
         LA    R4,2(R9)            SKIP VVR RECORD LENGTH FIELD
         SH    R15,=H'2'           REDUCE LENGTH BY THIS
         SH    R15,0(R4)           SUBTRACT FROM RECORD LENGTH
         AH    R4,0(R4)            POINT TO NEXT CELL
         SPACE 1
VSAM040  DS    0H
         CLI   2(R4),X'21'         DATASET INFORMATION CELL ?
         BNE   *+4+2               NO ==> GO ON
         LR    R7,R4               YES, REMEMBER THIS ADDRESS
         CLI   2(R4),X'23'         VOLUME INFORMATION CELL ?
         BNE   *+4+2               NO ==> GO ON
         LR    R5,R4               YES, REMEMBER THIS ADDRESS
         CLI   2(R4),X'60'         AMDSB CELL ?
         BNE   *+4+2               NO ==> GO ON
         LR    R6,R4               YES, REMEMBER THIS ADDRESS
         SH    R15,0(R4)           SUBTRACT FROM RECORD LENGTH
         BNP   PARSEVVX            EXIT IF FALLEN OFF THE END
         AH    R4,0(R4)            POINT TO NEXT CELL
         B     VSAM040             NO ==> GO ON
         SPACE 2
PARSEVVX DS     0H                 END OF SEARCH
         ROUT   ,                  RETURN TO CALLER
