***********************************************************************
*        PDSDOC *CONVERT RMF INTERVAL TO 1024 MICROSECOND UNITS.......* RMFCTU
*                                                                     *
* ON ENTRY R1 -> 4 BYTE PACKED INTERVAL IN FORM MMSSTTTF              *
* ON EXIT  THIS FIELD HAS BEEN CONVERTED TO 1024 MICROSECOND UNITS.   *
*                                                                     *
***********************************************************************
         SPACE 1
RMFCTU   DS    0H                  CONVERT INTERVAL FIELD
         SUBIN SAVE=(R1,R4,R14,R15) SUBROUTINE ENTRY
         SR    R15,R15             ZERO ACCUMULATOR
         LR    R14,R15             AND EVEN REGISTER
         GOSUB PACK2               GO PACK MINUTES
         LR    R15,R0              COPY MINUTES
         MH    R15,=H'60'          CONVERT TO SECONDS
         LA    R1,1(,R1)           POINT TO SECONDS
         GOSUB PACK2               GO CONVERT IT
         AR    R15,R0              ACCUMULATE SECONDS
         M     R14,=F'1000'        CONVERT TO THOUSANDTHS
         ZAP   DWORK,1(2,R1)       SPLIT OFF THOUS.
         CVB   R0,DWORK            CONVERT THEM TO BINARY
         AR    R15,R0              ACCUMULATE ANSWER
         M     R14,=F'1000'        CONVERT TO MICROSECONDS
         D     R14,=F'1024'        CONVERT TO TIMER UNITS
         L     R0,=F'1024'         GET DIVISOR
         SRL   R0,1                DIVIDE BY 2
         CR    R0,R14              TEST REMAINDER
         BL    *+4+4               SKIP ROUNDING IF LOW
         LA    R15,1(,R15)         ROUND UP
         SUBLR R1                  RESTORE INPUT POINTER
         STCM  R15,B'1111',0(R1)   SAVE ANSWER IN INPUT FIELD
         SPACE 1
RMFCTUX  DS    0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R1,R4,R14,R15) SUBROUTINE EXIT
