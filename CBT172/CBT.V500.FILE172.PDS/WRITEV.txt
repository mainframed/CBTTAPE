***********************************************************************
*        PDSDOC *GENERALISED VARIABLE LENGTH WRITE ROUTINE............* WRITEV
*                                                                     *
*        ON ENTRY R0 --> RDW FOLLOWED BY DATA                         *
*                 R2 --> MDCB FOR FILE                                *
*                                                                     *
* 14OCT98 DHC ADD TEST THAT IT REALLY IS VARIABLE                     *
*                                                                     *
***********************************************************************
         SPACE 1
WRITEV   DS    0H                  WRITE A RECORD TO INDICATED FILE
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         USING MDCB,R2             ESTABLISH ADDRESSABILITY
         TM    MDCBFLAG,DDPRES+DDOPEN TEST IF ABLE TO WRITE
         BO    WRITEVON            YES, CONTINUE
         GOSUB OPEN                NO, GO OPEN IT
         TM    MDCBFLAG,DDPRES+DDOPEN TEST IF ABLE TO WRITE
         BNO   WRITEVEX            BRANCH IF NO DDCARD
         SPACE 1
WRITEVON DS    0H                  ABLE TO WRITE NOW
         L     R1,MDCBDCB          POINT TO DCB
         USING IHADCB,R1           ESTABLISH DCB ADDRESSABILITY
         TM    DCBRECFM,DCBRECV    IS OUTPUT VARIABLE LENGTH?
         BNO   WRITEVOU            NO,USE LRECL SUPPLIED
         LH    R15,0(,R4)          GET LRECL
         STH   R15,DCBLRECL        SAVE IN DCB
         SPACE 1
WRITEVOU DS    0H                  DO WRITE NOW
         GOSUB PUTMOVE             GO OUTPUT RECORD
         AP    MDCBCNT,ONE         INCREMENT RECORD COUNTER
         SPACE 1
WRITEVEX DS   0H                  RETURN TO CALLER
         SUBOUT RESTORE=(R4)       SUBROUTINE EXIT
         SPACE 1
         DROP  R1                  END DCB ADDRESSING
         DROP  R2                  FINISHED WITH MDCB
