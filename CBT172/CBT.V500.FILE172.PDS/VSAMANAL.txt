./ ADD NAME=$$$XXXX  0101-94284-12359-2253-00002-00002-00000-SBGCSC
 This member really goes with File 294.  Only here for reference.
 Copied some of the File 294 into here, so everything will assemble.
./ ADD NAME=$INSTALL 0124-94195-12359-2243-00114-00051-00000-SBGOLOB
//SBGOLOBV JOB ,'SAM GOLOB',CLASS=B,
//    MSGCLASS=X,NOTIFY=&SYSUID
//*
//*
//*      PDSDOC *COMPILE AND ASSEMBLE VSAMANAL TO STEPLIB.............* VSAMANAL
//*
//PLIXCL   PROC LKLBDSN='PLI.PLI140.PLIBASE'
//PLI      EXEC PGM=IEL0AA,PARM='OBJECT,NODECK'
//STEPLIB  DD  DISP=SHR,DSN=PLI.PLI140.PLIBASE
//         DD  DISP=SHR,DSN=PLI.PLI140.PLICOMP
//         DD  DISP=SHR,DSN=PLI.PLI140.PLILINK
//         DD  DISP=SHR,DSN=PLI.PLI140.PLITASK
//SYSPRINT DD  SYSOUT=*
//SYSLIN   DD  DSN=&&LOADSET,DISP=(MOD,PASS),UNIT=SYSDA,
//             SPACE=(80,(250,100))
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=SYSDA,
//             SPACE=(1024,(200,50),,CONTIG,ROUND),DCB=BLKSIZE=1024
//LKED     EXEC PGM=IEWL,PARM='XREF,LIST',COND=(9,LT,PLI)
//SYSLIB   DD  DSN=&LKLBDSN,DISP=SHR
//         DD  DISP=SHR,DSN=PLI.PLI140.PLIBASE
//         DD  DISP=SHR,DSN=PLI.PLI140.PLICOMP
//         DD  DISP=SHR,DSN=PLI.PLI140.PLILINK
//         DD  DISP=SHR,DSN=PLI.PLI140.PLITASK
//SYSLMOD  DD  DSN=&&GOSET(GO),DISP=(MOD,PASS),UNIT=SYSDA,
//             SPACE=(1024,(50,20,1))
//SYSUT1   DD  DSN=&&SYSUT1,UNIT=SYSDA,
//             SPACE=(1024,(200,50),,CONTIG,ROUND),DCB=BLKSIZE=1024
//SYSPRINT DD  SYSOUT=*
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSIN    DD  DUMMY
//*
//       PEND  PLIXCL
//*
//STEP01  EXEC PLIXCL,
//             PARM.PLI='OBJECT,NODECK,XREF(SHORT),ATTR',
//             PARM.LKED='LIST,XREF,MAP,REUS,MAP'
//PLI.SYSIN DD DISP=SHR,DSN=SBGOLOB.VSAMANAL.PDS(VSAMANDX)
//LKED.SYSLMOD DD  DISP=SHR,SPACE=,UNIT=,
//             DSN=SBGOLOB.VSAMANAL.LOAD
//SYSIN    DD  *
 NAME VSAMANDX(R)
/*
//*
//ANAL    EXEC ASMACL,
//             PARM.C='NODECK,OBJECT,TERM,XREF(SHORT)',
//*            COND.L=(99,NE),
//             PARM.L='LIST,MAP,REUS,XREF,LET'
//C.SYSLIB   DD  DISP=SHR,DSN=SBGOLOB.VSAMANAL.PDS
//*          DD  DISP=SHR,DSN=SBGOLOB.CBT486.FILE294
//           DD  DISP=SHR,DSN=SYS1.MACLIB
//C.SYSTERM  DD  SYSOUT=*
//C.SYSIN    DD  DISP=SHR,DSN=SBGOLOB.VSAMANAL.PDS(VSAMANAL)
/*
//L.SYSLMOD   DD  DISP=SHR,DSN=SBGOLOB.VSAMANAL.LOAD
//L.SYSLIB    DD  DISP=SHR,DSN=PLI.PLI140.PLIBASE
//            DD  DISP=SHR,DSN=PLI.PLI140.PLILINK
//L.SYSIN    DD *
  INCLUDE SYSLMOD(VSAMANDX)
 INCLUDE SYSLIB(IBMBPIIA)
 INCLUDE SYSLIB(IBMBPGRA)
 INCLUDE SYSLIB(IBMBPITA)
  NAME VSAMANAL(R)
/*
//*
//ASM3     EXEC ASMACL,
//*
//*
//*  THE PARM.C OVERRIDE IS FOR TESTING ONLY
//*
//  PARM.C='XREF(SHORT),NODECK'
//C.SYSLIB DD DSN=SBGOLOB.VSAMANAL.PDS,DISP=SHR
//*          DD  DISP=SHR,DSN=SBGOLOB.CBT486.FILE294
// DD DSN=SYS1.MACLIB,DISP=SHR
//C.SYSPRINT DD SYSOUT=A
//C.SYSIN DD DSN=SBGOLOB.VSAMANAL.PDS(VSAMAGET),DISP=SHR
//L.SYSLMOD DD DSN=SBGOLOB.VSAMANAL.LOAD,
//   DISP=SHR
//SYSIN  DD  *
 NAME VSAMAGET(R)
/*
//ASM4     EXEC ASMACL,
//*
//*
//*  THE PARM.C OVERRIDE IS FOR TESTING ONLY
//*
//  PARM.C='XREF(SHORT),NODECK'
//C.SYSLIB DD DSN=SBGOLOB.VSAMANAL.PDS,DISP=SHR
//*          DD  DISP=SHR,DSN=SBGOLOB.CBT486.FILE294
// DD DSN=SYS1.MACLIB,DISP=SHR
//C.SYSPRINT DD SYSOUT=A
//C.SYSIN DD DSN=SBGOLOB.VSAMANAL.PDS(VSAMADTL),DISP=SHR
//L.SYSLMOD DD DSN=SBGOLOB.VSAMANAL.LOAD,
//   DISP=SHR
//SYSIN  DD  *
 NAME VSAMADTL(R)
/*
//ASM5     EXEC ASMACL,
//*
//*
//*  THE PARM.C OVERRIDE IS FOR TESTING ONLY
//*
//  PARM.C='XREF(SHORT),NODECK'
//C.SYSLIB DD DSN=SBGOLOB.VSAMANAL.PDS,DISP=SHR
//*          DD  DISP=SHR,DSN=SBGOLOB.CBT486.FILE294
// DD DSN=SYS1.MACLIB,DISP=SHR
//C.SYSPRINT DD SYSOUT=A
//C.SYSIN DD DSN=SBGOLOB.VSAMANAL.PDS(VSAMAHLP),DISP=SHR
//L.SYSLMOD DD DSN=SBGOLOB.VSAMANAL.LOAD,
//   DISP=SHR
//SYSIN  DD  *
 NAME VSAMAHLP(R)
/*
//
./ ADD NAME=BUF3270  0084-84085-84085-1200-00075-00075-00000-OLDF294
         MACRO
&NAME    BUF3270 &WCC=,&TYPE=
.********** S T A R T    O F    G L O B A L S / L O C A L S ***********
         GBLC  &DSECTNM           NAME OF SCREEN DSECT
         GBLC  &FLINE        LINE POSITION OF FIRST FIELD IN SCREEN
         GBLC  &FCOLMN     COLUMN POSITION OF FIRST FIELD IN SCREEN
         LCLA  &COUNT             DSECT NAME -1
.**********   E N D      O F    G L O B A L S / L O C A L S ***********
         AIF   ('&TYPE' EQ '').TYPERR
         AIF   ('&TYPE' EQ 'END').ENDBUF
         AIF   ('&TYPE' NE 'START').TYPERR
.STRTBUF ANOP
         AIF ('&NAME' EQ '').NAMERR
         AIF (K'&NAME GT 7).NAMERR
         AIF   ('&DSECTNM' NE '').ENDERR
&DSECTNM SETC  '&NAME.D'          SET SCREEN DSECT NAME
.START   ANOP
         SPACE 3
         AGO   .WCC
.NAMERR  ANOP
         MNOTE 12,'MACRO ''NAME'' SYMBOL NOT SPECIFIED OR GREATER THAN X
               7 CHARACTERS'
         MEXIT
.COMERR  ANOP
         MNOTE 12,'COMMAND-CODE AND WCC NOT SPECIFIED IN ''WCC'' PARAMEX
               TER.'
         MEXIT
.TYPERR  ANOP
         MNOTE 12,'''TYPE'' NOT SPECIFIED OR IS INVALID.'
         MEXIT
.ENDERR  ANOP
         MNOTE 12,'NO ''BUF3270 TYPE=END'' SPECIFIED FOR PREVIOUSLY DEFI
               INED SCREEN.'
         MEXIT
.WCC     ANOP
         SPACE 3
********************************************************************
***********  S T A R T    O F    3 2 7 0    B U F F E R ************
***********                                             ************
***********                                             ************
********************        W C C              *********************
&NAME    DS    0F                 START OF 3270 BUFFER
         AIF  ('&WCC' EQ '').WCCDFLT
         DC    XL1'&WCC'          3270(WCC)
         AGO  .CONTWCC
.WCCDFLT ANOP
         DC    XL1'C2'            3270(WCC)
.CONTWCC ANOP
         DC    XL3'115D7E'        SPF FULLSCR SIGNAL STRING(TYPE B)
&DSECTNM DSECT                    START OF SCREEN DSECT
&NAME.W  DS    XL1                3270(WCC)
         DS    XL3                SPF FULLSCR SIGNAL STRING(TYPE B)
&SYSECT  CSECT                    CONTINUE USERS CSECT
         MEXIT
.END     ANOP
*******************************************************************
         SPACE 2
         MEXIT
.ENDBUF  ANOP
         AIF   ('&WCC' EQ '').ENDCONT
         MNOTE '''WCC'' PARAMETER IGNORED WHEN ''TYPE=END'' SPECIFIED.'
.ENDCONT ANOP
         AIF   ('&FLINE' EQ '').GENLEN
         FIELD POS=(&FLINE,&FCOLMN),CUR=YES
.GENLEN  ANOP
&COUNT   SETA   K'&DSECTNM-1
&DSECTNM SETC '&DSECTNM'(1,&COUNT)
&DSECTNM.L EQU *-&DSECTNM        LENGTH OF 3270 SCREEN BUFFER
&DSECTNM SETC  ''                    SET DSECT NAME TO NULL
***********                                             ***********
***********                                             ***********
***********                                             ***********
***********    E N D      O F    3 2 7 0    B U F F E R ***********
*******************************************************************
         MEND
./ ADD NAME=DATASCRN 0100-90282-90282-1308-00043-00043-00000-CART1
DATA     BUF3270 TYPE=START
         FIELD POS=(13,1),ATTR=(ASKIP,MDTOFF)
DATAS1   FIELD POS=(14,1),ATTR=(UP,MDTOFF,HI),DATA=' '
INFO1    FIELD POS=(14,3),ATTR=(ASKIP,MDTOFF),                         X
               DATA='                                                  X
                                          '
DATAS2   FIELD POS=(15,1),ATTR=(UP,MDTOFF,HI),DATA=' '
INFO2    FIELD POS=(15,3),ATTR=(ASKIP,MDTOFF),                         X
               DATA='                                                  X
                                          '
DATAS3   FIELD POS=(16,1),ATTR=(UP,MDTOFF,HI),DATA=' '
INFO3    FIELD POS=(16,3),ATTR=(ASKIP,MDTOFF),                         X
               DATA='                                                  X
                                          '
DATAS4   FIELD POS=(17,1),ATTR=(UP,MDTOFF,HI),DATA=' '
INFO4    FIELD POS=(17,3),ATTR=(ASKIP,MDTOFF),                         X
               DATA='                                                  X
                                          '
DATAS5   FIELD POS=(18,1),ATTR=(UP,MDTOFF,HI),DATA=' '
INFO5    FIELD POS=(18,3),ATTR=(ASKIP,MDTOFF),                         X
               DATA='                                                  X
                                          '
DATAS6   FIELD POS=(19,1),ATTR=(UP,MDTOFF,HI),DATA=' '
INFO6    FIELD POS=(19,3),ATTR=(ASKIP,MDTOFF),                         X
               DATA='                                                  X
                                          '
DATAS7   FIELD POS=(20,1),ATTR=(UP,MDTOFF,HI),DATA=' '
INFO7    FIELD POS=(20,3),ATTR=(ASKIP,MDTOFF),                         X
               DATA='                                                  X
                                          '
DATAS8   FIELD POS=(21,1),ATTR=(UP,MDTOFF,HI),DATA=' '
INFO8    FIELD POS=(21,3),ATTR=(ASKIP,MDTOFF),                         X
               DATA='                                                  X
                                          '
DATAS9   FIELD POS=(22,1),ATTR=(UP,MDTOFF,HI),DATA=' '
INFO9    FIELD POS=(22,3),ATTR=(ASKIP,MDTOFF),                         X
               DATA='                                                  X
                                          '
DATAMSG  FIELD POS=(23,1),ATTR=(ASKIP,MDTOFF,HI),                      X
               DATA='SELECT (KEY ''S'') CONTROL INTERVAL(S) FOR MORE INX
               FORMATION!!!!                 '
         FIELD POS=(14,2),CUR=YES
         BUF3270 TYPE=END
./ ADD NAME=DESCSCRN 0085-84085-12359-1249-00037-00037-00001-FOR3390
DESC     BUF3270 TYPE=START
         FIELD POS=(1,1),ATTR=(ASKIP,MDTOFF,HI),                       *
               DATA='------- TUTORIAL --------- V S A M   A N A L Y Z E*
                R ------- TUTORIAL ---------'
         FIELD POS=(3,1),ATTR=(ASKIP,OFF),                             X
               DATA='THIS UTILITY WILL CALCULATE VSAM DATASET INFORMATIX
               ON FOR 3390 AND 3380 DASD.'
         FIELD POS=(5,3),ATTR=(ASKIP,OFF),                             X
               DATA='THE FOLLOWING INFORMATION WILL BE CALCULATED AND DX
               ISPLAYED FOR A VSAM DATASET'
         FIELD POS=(7,10),ATTR=(ASKIP,OFF),                            X
               DATA='CONTROL INTERVAL SIZES AND RELATED ATTRIBUTES.'
         FIELD POS=(8,10),ATTR=(ASKIP,OFF),                            X
               DATA='CONTROL AREA SIZES AND RELATED ATTRIBTES.'
         FIELD POS=(9,10),ATTR=(ASKIP,OFF),                            X
               DATA='PHYSICAL SPACE REQUIREMENTS AND RELATED ATTRIBUTESX
               .'
         FIELD POS=(10,10),ATTR=(ASKIP,OFF),                           X
               DATA='FREESPACE SPECIFICATIONS AND RELATED ATTRIBUTES.'
         FIELD POS=(12,3),ATTR=(ASKIP,OFF),                            X
               DATA='ALL VSAM DATASET TYPES(KSDS/AIX/ESDS/RRDS) ARE SUPX
               PORTED.'
         FIELD POS=(14,3),ATTR=(ASKIP,OFF),                            X
               DATA='SPANNED RECORDS ARE NOT SUPPORTED.'
         FIELD POS=(16,1),ATTR=(ASKIP,OFF),                            X
               DATA='PF KEY ASSIGNMENTS FOR ALL MENUS;'
         FIELD POS=(17,3),ATTR=(ASKIP,OFF),                            X
               DATA='PF1=TUTORIAL'
         FIELD POS=(18,3),ATTR=(ASKIP,OFF),                            X
               DATA='PF3=GO BACK TO PREVIOUS MENU'
         FIELD POS=(19,3),ATTR=(ASKIP,OFF),                            X
               DATA='PF4=SPF PRIMARY OPTION MENU'
         FIELD POS=(20,3),ATTR=(ASKIP,OFF),                            X
               DATA='PF7=PAGE BACKWARD IN CURRENT MENU'
         FIELD POS=(21,3),ATTR=(ASKIP,OFF),                            X
               DATA='PF8=PAGE FORWARD IN CURRENT MENU'
         BUF3270 TYPE=END
./ ADD NAME=DTLSCRN  0100-94187-94187-0739-00081-00081-00000-CART1
DETAIL   BUF3270 TYPE=START
        FIELD POS=(09,1),ATTR=(ASKIP,OFF),                             *
               DATA=' CONTROL INTERVAL  *   CONTROL AREA    * VOLUME-PH*
               YSICAL  '
@FREE1   FIELD POS=(09,60),ATTR=(ASKIP,OFF),DATA='*    FREESPACE      '
         FIELD POS=(10,1),ATTR=(ASKIP,OFF),DATA='      DATA         *  *
                   DATA         *      DATA         '
@FREE2   FIELD POS=(10,60),ATTR=(ASKIP,OFF),DATA='*       DATA        '
         FIELD POS=(11,1),ATTR=(ASKIP,OFF),                            *
               DATA='------------------ * ----------------- * ---------*
               --------'
@FREE12  FIELD POS=(11,60),ATTR=(ASKIP,OFF),DATA='* ------------------'
         FIELD POS=(12,1),ATTR=(ASKIP,OFF),DATA='CISIZE'
@DCISZ   FIELD POS=(12,8),ATTR=(ASKIP,OFF,HI),DATA='          '
         FIELD POS=(12,19),ATTR=(ASKIP,OFF),DATA=' * CASIZE(TRKS)'
@DCASZ   FIELD POS=(12,35),ATTR=(ASKIP,OFF,HI),DATA='    '
         FIELD POS=(12,40),ATTR=(ASKIP,OFF),DATA='* BLKSIZE '
@BLKSZ   FIELD POS=(12,51),ATTR=(ASKIP,OFF,HI),DATA='        '
@FREE3   FIELD POS=(12,60),ATTR=(ASKIP,OFF),DATA='* CA%        '
@CAFREE  FIELD POS=(12,74),ATTR=(ASKIP,OFF,HI),DATA='      '
         FIELD POS=(13,1),ATTR=(ASKIP,OFF),DATA='CI/CA '
@CICA    FIELD POS=(13,8),ATTR=(ASKIP,OFF,HI),DATA='          '
         FIELD POS=(13,19),ATTR=(ASKIP,OFF),                           *
               DATA=' *                   * BLKS/TRK'
@BLKTRK  FIELD POS=(13,51),ATTR=(ASKIP,OFF,HI),DATA='        '
@FREE4   FIELD POS=(13,60),ATTR=(ASKIP,OFF),DATA='* FREECI''S/CA'
@FCICA   FIELD POS=(13,74),ATTR=(ASKIP,OFF,HI),DATA='      '
         FIELD POS=(14,1),ATTR=(ASKIP,OFF),DATA='RECS-MAX'
@MRECCI  FIELD POS=(14,10),ATTR=(ASKIP,OFF,HI),DATA='        '
         FIELD POS=(14,19),ATTR=(ASKIP,OFF),DATA=' * RECS-MAX'
@MRECCA  FIELD POS=(14,31),ATTR=(ASKIP,OFF,HI),DATA='        '
         FIELD POS=(14,40),ATTR=(ASKIP,OFF),DATA='* TRK-UTIL% '
@TRKUTL  FIELD POS=(14,53),ATTR=(ASKIP,OFF,HI),DATA='      '
@FREE5   FIELD POS=(14,60),ATTR=(ASKIP,OFF),DATA='* CI%        '
@CIFREE  FIELD POS=(14,74),ATTR=(ASKIP,OFF,HI),DATA='      '
@FREE6   FIELD POS=(15,1),ATTR=(ASKIP,OFF),DATA='RECS-LOAD '
@LRECCI  FIELD POS=(15,12),ATTR=(ASKIP,OFF,HI),DATA='      '
@FREE7   FIELD POS=(15,19),ATTR=(ASKIP,OFF),DATA=' * RECS-LOAD '
@LRECCA  FIELD POS=(15,33),ATTR=(ASKIP,OFF,HI),DATA='      '
         FIELD POS=(15,40),ATTR=(ASKIP,OFF),DATA='*                  '
@FREE8   FIELD POS=(15,60),ATTR=(ASKIP,OFF),DATA='* FREERECS/CI'
@FRECCI  FIELD POS=(15,74),ATTR=(ASKIP,OFF,HI),DATA='      '
         FIELD POS=(16,1),ATTR=(ASKIP,OFF),                            *
               DATA='          TOTAL-RECS-DSET'
@DRECS   FIELD POS=(16,27),ATTR=(ASKIP,OFF,HI),DATA='            '
@TYPESD  FIELD POS=(16,40),ATTR=(ASKIP,OFF),DATA='* CYLNDERS'
@SPACED  FIELD POS=(16,51),ATTR=(ASKIP,OFF,HI),DATA='        '
@FREE9   FIELD POS=(16,60),ATTR=(ASKIP,OFF),DATA='* FREERECS/CA'
@FRECCA  FIELD POS=(16,74),ATTR=(ASKIP,OFF,HI),DATA='      '
@FREE10  FIELD POS=(17,1),ATTR=(ASKIP,OFF),                            *
               DATA='          TOTAL-RECS-LOAD'
@LRECS   FIELD POS=(17,27),ATTR=(ASKIP,OFF,HI),DATA='            '
@FREE11  FIELD POS=(17,40),ATTR=(ASKIP,OFF),                           *
               DATA='*      TOTAL-FREE-RECS-DSET'
@FDRECS  FIELD POS=(17,68),ATTR=(ASKIP,OFF,HI),DATA='            '
         FIELD POS=(18,1),ATTR=(ASKIP,OFF)
         DC    X'3C5D7F00'   R/T/A FOR LINES 18,19,20,21,22,23,24
         BUF3270 TYPE=END
INDEX    BUF3270 TYPE=START
         FIELD POS=(18,1),ATTR=(ASKIP,OFF),                            *
               DATA='--------------------------------------------------*
               -----------------------------'
         FIELD POS=(19,1),ATTR=(ASKIP,OFF),                            *
               DATA='      INDEX        *                   *      INDE*
               X'
         FIELD POS=(20,1),ATTR=(ASKIP,OFF),                            *
               DATA='------------------ * ----------------- * ---------*
               --------'
         FIELD POS=(21,1),ATTR=(ASKIP,OFF),DATA='CISIZE'
@ICISZ   FIELD POS=(21,8),ATTR=(ASKIP,OFF,HI),DATA='          '
         FIELD POS=(21,19),ATTR=(ASKIP,OFF),                           *
               DATA=' *                   * BLKSIZE '
@IBLKSZ  FIELD POS=(21,51),ATTR=(ASKIP,OFF,HI),DATA='        '
         FIELD POS=(21,60),ATTR=(ASKIP,OFF),                           *
               DATA='                     INDEX LEVELS'
@ILEVLS  FIELD POS=(22,14),ATTR=(ASKIP,OFF,HI),DATA='    '
         FIELD POS=(22,19),ATTR=(ASKIP,OFF),                           *
               DATA=' *'
@TYPESI  FIELD POS=(22,40),ATTR=(ASKIP,OFF),DATA='*   TRACKS'
@SPACEI  FIELD POS=(22,51),ATTR=(ASKIP,OFF,HI),DATA='        '
         BUF3270 TYPE=END
./ ADD NAME=FIELD    0084-84085-84085-1200-00087-00087-00000-OLDF294
         MACRO
&NAME    FIELD &POS=,&ATTR=,&DATA=,&CUR=NO
.********** S T A R T    O F    G L O B A L S / L O C A L S ***********
         GBLC  &DSECTNM           NAME OF SCREEN DSECT
         GBLC  &FLINE        LINE POSITION OF FIRST FIELD
         GBLC  &FCOLMN     COLUMN POSITION OF FIRST FIELD
         LCLC  &DATALEN           LENGTH OF DATA FIELD
         LCLC  &CURNAME           NAME OF CURSOR FIELD
         LCLA  &LEN               LENGTH OF DATA FIELD
         LCLA  &COUNT             WORK COUNTER
.**********   E N D      OF     G L O B A L S / L O C A L S ***********
         AIF ('&DSECTNM' EQ '').NOSCRN
         AIF (K'&NAME GT 7).NAMERR
********************  S T A R T    O F    F I E L D  ******************
         AIF   ('&FLINE' NE '').SBA
&FLINE   SETC  '&POS(1)'     LINE POSITION OF FIRST FIELD IN SCREEN
&FCOLMN  SETC  '&POS(2)'   COLUMN POSITION OF FIRST FIELD IN SCREEN
.SBA     ANOP
         SBA   &POS
&DSECTNM DSECT                    CONTINUE SCREEN DSECT
         AIF ('&NAME' EQ '').SBANON
&NAME.S  DS    XL3                SBA-ADDRESS
         AGO   .CONT1
.SBANON  ANOP
         AIF  ('&CUR' EQ 'YES').CURSBA
         DS    XL3                SBA-ADDRESS
         AGO  .CONT1
.CURSBA  ANOP
         DS    XL1                SBA
&COUNT   SETA  K'&DSECTNM-1
&CURNAME SETC '&DSECTNM'(1,&COUNT).'X'
&CURNAME DS    XL2                CURSOR ADDRESS
.CONT1   ANOP
&SYSECT  CSECT                    CONTINUE USERS CSECT
         AIF   ('&CUR' EQ 'YES').CURSOR
         SF    &ATTR
&DSECTNM DSECT                    CONTINUE SCREEN DSECT
         AIF   ('&NAME' EQ '').SFNON
         DS    XL1                START FIELD(X'1D')
&NAME.A  DS    XL1                ATTRIBUTE BYTE
         AGO   .CONT2
.SFNON   ANOP
         DS    XL1                START FIELD(X'1D')
         DS    XL1                ATTRIBUTE BYTE
.CONT2   ANOP
&SYSECT  CSECT                    CONTINUE USERS CSECT
         AIF   ('&CUR' EQ 'YES').CURSOR
         AIF   ('&DATA' NE '').DATA
         AGO   .END
.DATA    ANOP
&LEN     SETA  K'&DATA-2
&DATALEN SETC  '&LEN'
         DC    CL&DATALEN&DATA    FIELD DATA
&DSECTNM DSECT                    CONTINUE SCREEN DSECT
         AIF   ('&NAME' EQ '').DATANON
&NAME    DS    CL&DATALEN         FIELD DATA
&NAME.L  EQU   &DATALEN          FIELD LENGTH
         AGO   .CONT3
.DATANON ANOP
         DS    CL&DATALEN
.CONT3   ANOP
&SYSECT  CSECT                    CONTINUE USERS CSECT
         AGO   .END
.CURSOR  ANOP
         AIF   ('&DATA' EQ '').CURCONT
         MNOTE 'DATA PARAMETER IGNORED WHEN CUR PARAMETER SPECIFIED.'
.CURCONT ANOP
         AIF   ('&ATTR' EQ '').CURAGAN
         MNOTE 'ATTR PARAMETER IGNORED WHEN CUR PARAMETER SPECIFIED.'
.CURAGAN ANOP
&FLINE   SETC  ''
&FCOLMN  SETC  ''
         DC    XL1'13'            CURSOR
&DSECTNM DSECT                    CONTINUE SCREEN DSECT
         DS    XL1                CURSOR
&SYSECT  CSECT                    CONTINUE USERS CSECT
.END     ANOP
********************    E N D      O F    F I E L D  ******************
         SPACE 2
         MEXIT
.NOSCRN  ANOP
         MNOTE 12,'''BUF3270'' MACRO REQUIRED BEFORE FIRST ''FIELD'' MAC
               CRO.'
         MEXIT
.NAMERR  ANOP
         MNOTE 12,'MACRO ''NAME'' SYMBOL IS GREATER THAN 7 CHARACTERS.'
         MEND
./ ADD NAME=HEADSCRN 0100-94187-94187-0740-00012-00012-00000-CART1
HEADR    BUF3270 TYPE=START
         FIELD POS=(9,1),ATTR=(ASKIP,MDTOFF),DATA='DATA COMPONENT'
FILETYP  FIELD POS=(9,16),ATTR=(ASKIP,MDTOFF),DATA='           '
         FIELD POS=(9,28),ATTR=(ASKIP,MDTOFF)
         DC    CL161' '
         FIELD POS=(11,30),ATTR=(ASKIP,MDTOFF),                        X
               DATA='LOAD     LOAD    FRSPC     MAX      MAX     SPACE X
                   DCISZ   TRK/CA   CI/CA   REC/CA   REC/CI   CI-CA   RX
               EC/CA   REC/CI   CYL-TRK'
         FIELD POS=(13,1),ATTR=(ASKIP,MDTOFF)
         DC    CL80' '
         BUF3270 TYPE=END
./ ADD NAME=HELP     0087-84085-12359-2159-00267-00262-00000-FILE294
)F FUNCTION -
                 The VSAMANAL command is used to provide the user
                 with a method of determining how much physical
                 space a dataset will require.  It can also be used
                 to determine an optimum data CI-SIZE, CI and CA
                 freespace percentages based on free record values,
                 track utilization percentages, and other VSAM
                 dataset statistics, which are helpful when defining
                 a new dataset, or when analysing existing dataset
                 definitions.

                 The devices currently supported are 3390's and
                 3380's as well as 3350's and 3330's.

                 VSAMANAL is a TSO command processor for 3270 displays.
                 The analyzer uses 3270 field and PF keys processing
                 and can be executed by entering "VSAMANAL".

)X SYNTAX -
                 VSAMANAL

)O OPERANDS -

))AVG-RECSIZE -  REQUIRED
                 This value can be any number from 1 to 32761.  For
                 fixed length records this is the record size.  For
                 variable-length records this is the average record-
                 size value used in all space and freespace cal-
                 culations.  Remember a true average record size
                 should be based on record size and occurrences of
                 those record sizes.  An inadequate or excessive
                 value will cause corresponding space and free-
                 space values to be calculated.

))MAX-RECSIZE -  REQUIRED
                 Maximum record size for variable length records. This
                 value can be any number from 1 to 32761, but cannot be
                 lower than 'AVG-RECSIZE'.  It represents the largest
                 recordsize your dataset will contain.

))RECORDS T/B LOADED - REQUIRED
                 Number of records to be loaded into your dataset. This
                 value can be any number from 1 to 9999999 and is used
                 in the calculation of non free space for your dataset.

))RECORDS T/B ADDED - OPTIONAL
                 Number of records to be added to your dataset
                 between reorganizations.  This value can be any
                 number from 1 to 9999999 and is used to calculate
                 freespace for your dataset.

                 For NON-KSDS datasets or for KSDS datasets with no CI
                 and CA free record percentages specified, this value
                 will be added to "RECORDS T/B LOADED" value to
                 determine space allocation.

                 For KSDS datasets with a CI and/or CA free record
                 percentages specified, this value will be used to
                 calcuate appropriate VSAM CI/CA freespace percentages
                 that will accommodate the indicated number of free
                 records.

))% ADD RECS CA FREESPACE - OPTIONAL
                 Percentage of "RECORDS T/B ADDED" value.  This value
                 can be any number from 1 to 100 and represents a
                 number of records value that will be used to calculate
                 a corresponding VSAM CA freespace percentage.

                 This percentage is analagous to the VSAM CA
                 freespace percentage, except it indicates a number of
                 free records rather than a number of free control
                 intervals.  The VSAM CA freespace percentage that is
                 calculated is based on the even distribution of the
                 free records, throughout the dataset, in free
                 control intervals within a control area.

                 This value and the CI percentage value cannot be
                 collectively over 100.  If they are under 100, the
                 difference (also a percentage) is converted to a free
                 record value, and a corresponding space allocation
                 will be added to the space allocation needed for
                 "RECORDS T/B LOADED" and any CI/CA freespace
                 requirements.

))% ADD RECS CI FREESPACE - OPTIONAL
                 Percentage of "RECORDS T/B ADDED" value.  This value
                 can be any number from 1 to 100, and represents a
                 number of records value that will be used to calculate
                 a corresponding VSAM CI freespace percentage.

                 This percentage is analagous to the VSAM CI
                 freespace, except that it indicates a number of free
                 records, rather than a number of free bytes.  The VSAM
                 CI freespace percentage that is calculated, is based
                 on the even distribution of the free records,
                 throughout the dataset, in those control intervals
                 not affected by the CA freespace percentage.

                 This value and the CA percentage value cannot be
                 collectively over 100.  If they are under 100, the
                 difference (also a percentage) is converted to a
                 free record value, and a corresponding space
                 allocation which will be added to the space
                 allocation needed for "RECORDS T/B LOADED", and any
                 CI/CA freespace requirements.

))DEVICE TYPE -  REQUIRED
                 DASD device type your dataset will be on.  Can be
                 "3390" or "3380" or "3350" or "3330".

))KEYLENGTH -    REQUIRED FOR KSDS
                 Length of dataset key.

))IMBED -        OPTIONAL
                 Can be "YES" or "NO".  See "VSAM Access
                 Method Services".

))REPLICATE -    OPTIONAL
                 Can be "YES" or "NO".  See "VSAM Access
                 Method Services".

))PF KEY ASSIGNMENTS

                 PFK KEY       FUNCTION
                 -------       --------
                 1             TUTORIAL
                 2             NOT USED
                 3             RETURN TO PREVIOUS SCREEN
                 4             EXIT VSAM ANALYZER
                 5             NOT USED
                 6             NOT USED
                 7             PAGE BACK    IN SAME SCREEN
                 8             PAGE FORWARD IN SAME SCREEN
                 9,10,11,12    NOT USED

))DATA CI-SIZE SELECTION AND DISPLAY
                 When all input parameters have been entered correctly,
                 a data CI-SIZE selection screen will be displayed in
                 the lower half of the tube.  All available data CI-
                 SIZEs will be displayed.

                 The screen is pageable by using PF keys 7 & 8 for
                 backward and forward displays of all data CI-SIZEs.
                 Using the data CI-SIZE selection chart, you can select
                 one or more CI-SIZEs from this screen.  When the data
                 CI-SIZE(s) have been selected and "enter" is pressed,
                 a detail display screen for each CI-SIZE selected
                 (starting with the first), will be displayed in the
                 lower half of your tube.  Use PF keys 7 & 8 for
                 backward and forward displays of each detail CI-SIZE
                 screen.

                 The detail display screen gives you statistics about
                 your VSAM dataset based on a data CI-SIZE selected.
                 The following list is an explanation by display
                 keyword of each statistic displayed.

))CONTROL INTERVAL STATISTICS

                 CISIZE (DATA)
                 Size of the selected data CI.

                 CI/CA
                 Number of control intervals in a control area.

                 RECS-MAX
                 Maximum number of records that can fit in a
                 control interval.  Based on "AVG-RECSIZE".

                 RECS-LOAD
                 Number of records that will be loaded into a control
                 interval of a KSDS/AIX with the CI-CA freespace
                 indicated. based on "AVG-RECSIZE".

                 CISIZE (INDEX)
                 Control interval size calculated for the index
                 components of a KSDS/AIX.

                 INDEX LEVELS
                 Number of index levels the index components of a
                 KSDS/AIX will contain.

))CONTROL AREA STATISTICS

                 CASIZE (trks)
                 Control area size in tracks used for all
                 calculations.

                 RECS-MAX
                 Maximum number of records that can fit in a control
                 area.  Based on "AVG-RECSIZE".

                 REC-LOAD
                 Number of records that will be loaded into a control
                 area of a KSDS/AIX with the CI-CA freespace specified.

))DATASET STATISTICS

                 TOTAL-RECS-DSET
                 Maximum number of records that the dataset can hold.
                 This value is the ideal number, but will most likely
                 be too high for those datasets with free space,
                 because of ci/ca splitting.  It may also vary for
                 variable length records if the "AVG-RECSIZE" is not a
                 true average.

                 TOTAL-RECS-LOAD
                 Number of records that can be loaded inta a KSDS/AIX
                 dataset with free space specified.

))VOLUME-PHYSICAL STATISTICS

                 BLKSIZE (data)
                 Physical block size of the data component.

                 BLKS/TRK
                 Number of physical blocks per track.

                 TRK-UTIL %
                 Percentage of tracks utilization based on physical
                 block size.

                 CYLINDERS
                 TRACKS
                 Number of cylinders (if CASIZE = 1 cyl) or tracks (if
                 CASIZE less than cyl) needed for the data component.

                 BLKSIZE(INDEX)
                 Physical blocksize of the index component.

                 TRACKS (INDEX)
                 Number of tracks needed for the index component.

))FREESPACE STATISTICS

                 CA %
                 Control Area freespace percentage.  This value should
                 be used in the define cluster.

                 FREE CI'S/CA
                 Number of free control intervals per control area.

                 CI %
                 Control Interval freespace percentage.  This value
                 should be used in the define cluster.

                 FREERECS/CI
                 Number of free records in a non-free control interval.

                 FREERECS/CA
                 Number of free records in a control area.

                 TOTAL-FREE-RECS-DSET
                 Number of free records in the data component.  Based
                 on "AVG-RECSIZE".

))WARNING
                 ********** WARNING **********

                 All free record values are ideal numbers and in most
                 cases will always be high.  This is due to CI and
                 CA splitting that can occur while inserting records.
                 Therefore, it is recommended that the "RECORDS T/B
                 ADDED" value be large enough to accommodate lost
                 space through CI-CA splits.


./ ADD NAME=HLP0SCRN 0085-84085-12359-1252-00055-00055-00001-FOR3390
MENU     BUF3270 TYPE=START
         FIELD POS=(1,1),ATTR=(ASKIP,MDTOFF,HI),                       *
               DATA='------- TUTORIAL --------- V S A M   A N A L Y Z E*
                R ------- TUTORIAL ---------'
         FIELD POS=(2,1),ATTR=(ASKIP,MDTOFF),                          *
               DATA='ENTER PARAMETERS BELOW:'
         FIELD POS=(3,4),ATTR=(ASKIP,MDTOFF),DATA='AVG-RECSIZE'
         FIELD POS=(3,23),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
RECA     FIELD POS=(3,28),ATTR=(UP,MDTOFF,HI),DATA='     '
         FIELD POS=(3,34),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(3,46),ATTR=(ASKIP,MDTOFF),DATA='MAX-RECSIZE'
         FIELD POS=(3,64),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
RECM     FIELD POS=(3,69),ATTR=(UP,MDTOFF,HI),DATA='     '
         FIELD POS=(3,75),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(4,4),ATTR=(ASKIP,MDTOFF),DATA='RECORDS T/B LOADED'
         FIELD POS=(4,23),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
RECLOAD  FIELD POS=(4,28),ATTR=(UP,MDTOFF,HI),DATA='       '
         FIELD POS=(4,36),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(4,46),ATTR=(ASKIP,MDTOFF),DATA='RECORDS T/B ADDED'
         FIELD POS=(4,64),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
RECADD   FIELD POS=(4,69),ATTR=(UP,MDTOFF,HI),DATA='       '
         FIELD POS=(4,77),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(5,4),ATTR=(ASKIP,MDTOFF),DATA='DEVICE TYPE'
         FIELD POS=(5,23),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
DEVTYPE  FIELD POS=(5,28),ATTR=(UP,MDTON,HI),DATA='3390'
         FIELD POS=(5,33),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(5,46),ATTR=(ASKIP,MDTOFF),DATA='KEYLENGTH'
         FIELD POS=(5,64),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
KEYL     FIELD POS=(5,69),ATTR=(UP,MDTOFF,HI),DATA='   '
         FIELD POS=(5,73),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(6,4),ATTR=(ASKIP,MDTOFF),DATA='IMBED'
         FIELD POS=(6,23),ATTR=(ASKIP,HI,MDTOFF),DATA='===>'
IMB      FIELD POS=(6,28),ATTR=(UP,MDTON,HI),DATA='NO '
         FIELD POS=(6,32),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(6,46),ATTR=(ASKIP,MDTOFF),DATA='REPLICATE'
         FIELD POS=(6,64),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
REPL     FIELD POS=(6,69),ATTR=(UP,MDTON,HI),DATA='NO '
         FIELD POS=(6,74),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(7,4),ATTR=(ASKIP,MDTOFF),DATA='%-ADD RECS CI FREESP*
               ACE'
         FIELD POS=(7,28),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
CIPER    FIELD POS=(7,33),ATTR=(UP,MDTON,HI),DATA='   '
         FIELD POS=(7,38),ATTR=(ASKIP,MDTOFF),DATA='%-ADD RECS CA FREES*
               PACE '
         FIELD POS=(7,64),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
CAPER    FIELD POS=(7,69),ATTR=(UP,MDTON,HI),DATA='   '
         FIELD POS=(7,73),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(8,1),ATTR=(ASKIP,MDTOFF,HI),                       *
               DATA='-------------------'
ERROR    FIELD POS=(8,21),ATTR=(ASKIP,MDTOFF,HI),                      *
               DATA='----------------------------------------'
         FIELD POS=(8,62),ATTR=(PL,MDTOFF,HI),                         *
               DATA='------------------'
         FIELD POS=(3,29),CUR=YES
         BUF3270 TYPE=END
./ ADD NAME=HLP1SCRN 0084-84085-84085-1200-00033-00033-00000-OLDF294
HELP1    BUF3270 TYPE=START
         FIELD POS=(1,1),ATTR=(ASKIP,OFF,HI),                          X
               DATA='------- TUTORIAL --------- V S A M   A N A L Y Z EX
                R ------- TUTORIAL ---------'
         FIELD POS=(3,17),ATTR=(ASKIP,OFF),                            X
               DATA='MENU                                  DESCRIPTION'
         FIELD POS=(4,1),ATTR=(ASKIP,OFF),                             X
               DATA='--------------------------------------------------X
               -----------------------------'
         FIELD POS=(5,6),ATTR=(ASKIP,OFF),                             X
               DATA='INPUT SELECTION'
         FIELD POS=(5,41),ATTR=(ASKIP,OFF),                            X
               DATA='REQUIRED+OPTIONAL INPUT PARAMETERS.'
         FIELD POS=(7,6),ATTR=(ASKIP,OFF),                             X
               DATA='CONTROL INTERVAL SELECTION'
         FIELD POS=(7,41),ATTR=(ASKIP,OFF),                            X
               DATA='OUTPUT OF INPUT SELECTION MENU.'
         FIELD POS=(8,41),ATTR=(ASKIP,OFF),                            X
               DATA='LIST OF AVAILABLE CONTROL INTERVAL'
         FIELD POS=(9,41),ATTR=(ASKIP,OFF),                            X
               DATA='SIZES AND DATASET ATTRIBUTES FOR'
         FIELD POS=(10,41),ATTR=(ASKIP,OFF),                           X
               DATA='SELECTIVE PROCESSING.'
         FIELD POS=(13,6),ATTR=(ASKIP,OFF),                            X
               DATA='CONTROL INTERVAL DISPLAY'
         FIELD POS=(13,41),ATTR=(ASKIP,OFF),                           X
               DATA='OUTPUT OF CONTROL INTERVAL SELECTION'
         FIELD POS=(14,41),ATTR=(ASKIP,OFF),DATA='MENU.'
         FIELD POS=(15,41),ATTR=(ASKIP,OFF),                           X
               DATA='ALL AVAILABLE INFORMATION FOR A'
         FIELD POS=(16,41),ATTR=(ASKIP,OFF),                           X
               DATA='SELECTED CONTROL INTERVAL SIZE.'
         BUF3270 TYPE=END
./ ADD NAME=HLP2SCRN 0084-84085-84085-1200-00031-00031-00000-OLDF294
HELP2    BUF3270 TYPE=START
         FIELD POS=(2,1),ATTR=(ASKIP,OFF),                             X
               DATA='INPUT SELECTION MENU DESCRIPTION.'
         FIELD POS=(9,3),ATTR=(ASKIP,OFF),                             X
               DATA='AVG-RECSIZE (REQUIRED) VALUE 1 TO 32761.'
         FIELD POS=(10,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  RECORD SIZE FOR FIXED LENGTH RECORDS.'
         FIELD POS=(11,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  AVERAGE RECSIZE FOR VARIABLE LENGTH RECORDS.'
         FIELD POS=(12,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  THIS VALUE IS THE BASIS FOR MOST SPACE CALCULATX
               IONS'
         FIELD POS=(13,24),ATTR=(ASKIP,OFF),                           X
               DATA='   SO FOR VARIABLE LENGTH RECORDS IT SHOULD BE'
         FIELD POS=(14,24),ATTR=(ASKIP,OFF),                           X
               DATA='   DETERMINED BY RECORDSIZE RANGE AND RECORDSIZE'
         FIELD POS=(15,24),ATTR=(ASKIP,OFF),                           X
               DATA='   OCCURANCES SO THAT INADEQUATE OR EXCESSIVE SPACX
               E'
         FIELD POS=(16,24),ATTR=(ASKIP,OFF),                           X
               DATA='   AND FREESPACE SPECIFICATIONS ARE NOT CALCULATEDX
               .'
         FIELD POS=(18,3),ATTR=(ASKIP,OFF),                            X
               DATA='MAX=RECSIZE (REQUIRED-VARIABLE) VALUE 1 TO 32761.'
         FIELD POS=(19,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  MAXIMUM RECORDSIZE FOR VARIABLE LENGTH RECORDS.X
               '
         FIELD POS=(20,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  MUST BE GREATER THAN OR EQUAL TO ''AVG-RECSIZE'X
               '.'
         BUF3270 TYPE=END
./ ADD NAME=HLP3SCRN 0085-84085-12359-1253-00023-00023-00001-FOR3390
HELP3    BUF3270 TYPE=START
         FIELD POS=(2,1),ATTR=(ASKIP,OFF),                             X
               DATA='INPUT SELECTION MENU DESCRIPTION:'
         FIELD POS=(9,3),ATTR=(ASKIP,OFF),                             X
               DATA='RECORDS T/B LOADED (REQUIRED) VALUE 1 TO 9999999.'
         FIELD POS=(10,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  NUMBER OF RECORDS TO BE LOADED INTO DATASET.'
         FIELD POS=(12,3),ATTR=(ASKIP,OFF),                            X
               DATA='RECORDS T/B ADDED (OPTIONAL) VALUE 1 TO 9999999.'
         FIELD POS=(13,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  NUMBER OF FREE RECORDS POSITIONS YOU WANT LEFT'
         FIELD POS=(14,24),ATTR=(ASKIP,OFF),                           X
               DATA='   AFTER INITIAL DATASET LOAD.'
         FIELD POS=(15,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  USED TO DETERMINE CI-CA FREESPACE PERCENTAGES'
         FIELD POS=(16,24),ATTR=(ASKIP,OFF),                           X
               DATA='   FOR KSDS AND AIX DATASETS.'
         FIELD POS=(18,3),ATTR=(ASKIP,OFF),                            X
               DATA='DEVICE TYPE (REQUIRED,DEFAULT=3390) VALUE ''3380''X
                OR ''3350''.'
         FIELD POS=(19,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  TYPE OF DASD WHERE DATASET IS DEFINED.'
         BUF3270 TYPE=END
./ ADD NAME=HLP4SCRN 0084-84085-84085-1200-00018-00018-00000-OLDF294
HELP4    BUF3270 TYPE=START
         FIELD POS=(2,1),ATTR=(ASKIP,OFF),                             X
               DATA='INPUT SELECTION MENU DESCRIPTION:'
         FIELD POS=(9,3),ATTR=(ASKIP,OFF),                             X
               DATA='KEYLENGTH (REQUIRED FOR KSDS/AIX) VALUE 1 TO 255.'
         FIELD POS=(10,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  LENGTH OF KSDS/AIX RECORD KEY.'
         FIELD POS=(12,3),ATTR=(ASKIP,OFF),                            X
               DATA='IMBED (OPTIONAL,DEFAULT=NO) VALUE ''NO'' OR ''YES'X
               '.'
         FIELD POS=(13,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  IGNORED IF KEYLENGTH IS NULL OR ''0''.'
         FIELD POS=(15,3),ATTR=(ASKIP,OFF),                            X
               DATA='REPLICATE (OPTIONAL,DEFAULT=NO) VALUE ''NO'' OR ''X
               YES''.'
         FIELD POS=(16,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  IGNORED IF KEYLENGTH IS NULL OF ''0''.'
         BUF3270 TYPE=END
./ ADD NAME=HLP5SCRN 0084-84085-84085-1200-00044-00044-00000-OLDF294
HELP5    BUF3270 TYPE=START
         FIELD POS=(2,1),ATTR=(ASKIP,OFF),                             X
               DATA='INPUT SELECTION MENU DESCRIPTION:'
         FIELD POS=(9,3),ATTR=(ASKIP,OFF),                             X
               DATA='%-ADD RECS CI FREESPACE (OPTIONAL) DEFAULT=0  VALUX
               E=0 TO 100'
         FIELD POS=(10,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  INDICATES THE PERCENTAGE OF "RECORDS T/B ADDED"X
               '
         FIELD POS=(11,24),ATTR=(ASKIP,OFF),                           X
               DATA='   (FREE RECORD SLOTS) THAT ARE TO BE LEFT IN CONTX
               ROL'
         FIELD POS=(12,24),ATTR=(ASKIP,OFF),                           X
               DATA='   INTERVAL FREESPACE.'
         FIELD POS=(13,3),ATTR=(ASKIP,OFF),                            X
               DATA='%-ADD RECS CA FREESPACE (OPTIONAL) DEFAULT=0 VALUEX
               E=0 TO 100'
         FIELD POS=(14,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  INDICATES THE PERCENTAGE OF "RECORDS T/B ADDED"X
               '
         FIELD POS=(15,24),ATTR=(ASKIP,OFF),                           X
               DATA='   (FREE RECORDS SLOTS) THAT ARE TO BE LEFT IN CONT
               TROL'
         FIELD POS=(16,24),ATTR=(ASKIP,OFF),                           X
               DATA='   AREA FREESPACE.'
         FIELD POS=(17,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  BOTH PARAMETERS ARE VALID ONLY FOR KSDA/AIX DATA
               SETS.'
         FIELD POS=(19,24),ATTR=(ASKIP,OFF),                           X
               DATA='*  NOTE: "%-ADD RECS CI FREESPACE" AND "%-FREE RECX
               S'
         FIELD POS=(20,24),ATTR=(ASKIP,OFF),                           X
               DATA='         CA FREESPACE" CANNOT COLLECTIVELY BE OVERX
                100.'
         FIELD POS=(21,24),ATTR=(ASKIP,OFF),                           X
               DATA='*        IF THEY ARE UNDER 100 THE DIFFERENCE (IN X
               FREE'
         FIELD POS=(22,24),ATTR=(ASKIP,OFF),                           X
               DATA='         RECORD SLOTS) WILL BE CALCULATED TO EXISTX
               '
         FIELD POS=(23,24),ATTR=(ASKIP,OFF),                           X
               DATA='         AT THE END OF DATASET AFTER THE INITIAL LX
               OAD.'
         BUF3270 TYPE=END
./ ADD NAME=HLP6SCRN 0084-84085-84085-1200-00041-00041-00000-OLDF294
HELP6    BUF3270 TYPE=START
         FIELD POS=(1,1),ATTR=(ASKIP,MDTOFF,HI),                       *
               DATA='------- TUTORIAL --------- V S A M   A N A L Y Z E*
                R ------- TUTORIAL ---------'
         FIELD POS=(2,1),ATTR=(ASKIP,OFF),                             X
               DATA='CONTROL INTERVAL SELECTION MENU DESCRIPTION:'
         FIELD POS=(4,1),ATTR=(ASKIP,MDTOFF),DATA='DATA COMPONENT'
FILETYP  FIELD POS=(4,16),ATTR=(ASKIP,MDTOFF),DATA='(KSDS/AIX) '
         FIELD POS=(4,28),ATTR=(ASKIP,MDTOFF)
         DC    CL161' '
         FIELD POS=(6,30),ATTR=(ASKIP,MDTOFF),                         X
               DATA='LOAD     LOAD    FRSPC     MAX      MAX     SPACE X
                   DCISZ   TRK/CA   CI/CA   REC/CA   REC/CI   CI-CA   RX
               EC/CA   REC/CI   CYL-TRK'
         FIELD POS=(8,1),ATTR=(ASKIP,OFF),                             X
               DATA='S     512    30      810      2000         3   50-X
                50    4000        6    100 C'
         FIELD POS=(9,1),ATTR=(ASKIP,OFF),                             X
               DATA='     1024    30      400      2500         5   40-X
                45    5000        9     85 C'
         FIELD POS=(10,1),ATTR=(ASKIP,OFF),                            X
               DATA='S    1536    30      330      2100         4   38-X
                41    4700        7     93 C'
         FIELD POS=(12,1),ATTR=(ASKIP,MDTOFF),                         X
               DATA='SELECT (KEY ''S'') CONTOL INTERVAL(S) FOR MORE INFX
               ORMATION             '
         FIELD POS=(13,1),ATTR=(ASKIP,OFF),                            X
               DATA='---------------------------------------------------
               -----------------------------'
         FIELD POS=(15,1),ATTR=(ASKIP,OFF),                            X
               DATA='THIS SCREEN LISTS AVAILABLE CONTROL INTERVAL SIZESX
                FOR YOUR DATASET.'
         FIELD POS=(17,1),ATTR=(ASKIP,OFF),                            X
               DATA='ONE OR MORE CONTROL INTERVALS CAN BE SELECTED FOR X
               A MORE EXTENSIVE DISPLAY'
         FIELD POS=(18,1),ATTR=(ASKIP,OFF),                            X
               DATA='OF ASSOCIATED ATTRIBUTES.'
         FIELD POS=(20,1),ATTR=(ASKIP,OFF),                            X
               DATA='THIS IS A MULTIPAGE SCREEN(PF7=PAGE BACKWARD, PF8=X
               PAGE FORWARD).'
         BUF3270 TYPE=END
./ ADD NAME=HLP7SCRN 0084-84085-84085-1200-00093-00093-00000-OLDF294
HELP7    BUF3270 TYPE=START
         FIELD POS=(1,1),ATTR=(ASKIP,MDTOFF,HI),                       *
               DATA='------- TUTORIAL --------- V S A M   A N A L Y Z E*
                R -------- TUTORIAL --------'
         FIELD POS=(2,1),ATTR=(ASKIP,OFF),                             X
               DATA='CONTROL INTERVAL DISPLAY MENU DESCRIPTION:'
        FIELD POS=(03,1),ATTR=(ASKIP,OFF),                             *
               DATA=' CONTROL INTERVAL  *   CONTROL AREA    * VOLUME-PH*
               YSICAL  '
@FREE1   FIELD POS=(03,60),ATTR=(ASKIP,OFF),DATA='*    FREESPACE      '
          FIELD POS=(4,1),ATTR=(ASKIP,OFF),DATA='      DATA         *  *
                   DATA         *      DATA         '
@FREE2   FIELD POS=(4,60),ATTR=(ASKIP,OFF),DATA='*       DATA        '
         FIELD POS=(05,1),ATTR=(ASKIP,OFF),                            *
               DATA='------------------ * ----------------- * ---------*
               --------'
@FREE12  FIELD POS=(05,60),ATTR=(ASKIP,OFF),DATA='* ------------------'
         FIELD POS=(06,1),ATTR=(ASKIP,OFF),DATA='CISIZE'
@DCISZ   FIELD POS=(06,8),ATTR=(ASKIP,OFF,HI),DATA='-------512'
         FIELD POS=(06,19),ATTR=(ASKIP,OFF),DATA=' * CASIZE(TRKS)'
@DCASZ   FIELD POS=(06,35),ATTR=(ASKIP,OFF,HI),DATA='--30'
         FIELD POS=(06,40),ATTR=(ASKIP,OFF),DATA='* BLKSIZE '
@BLKSZ   FIELD POS=(06,51),ATTR=(ASKIP,OFF,HI),DATA='-----512'
@FREE3   FIELD POS=(06,60),ATTR=(ASKIP,OFF),DATA='* CA%        '
@CAFREE  FIELD POS=(06,74),ATTR=(ASKIP,OFF,HI),DATA='----50'
         FIELD POS=(07,1),ATTR=(ASKIP,OFF),DATA='CI/CA '
@CICA    FIELD POS=(07,8),ATTR=(ASKIP,OFF,HI),DATA='-------810'
         FIELD POS=(07,19),ATTR=(ASKIP,OFF),                           *
               DATA=' *                   * BLKS/TRK'
@BLKTRK  FIELD POS=(07,51),ATTR=(ASKIP,OFF,HI),DATA='------27'
@FREE4   FIELD POS=(07,60),ATTR=(ASKIP,OFF),DATA='* FREECI''S/CA'
@FCICA   FIELD POS=(07,74),ATTR=(ASKIP,OFF,HI),DATA='---405'
         FIELD POS=(08,1),ATTR=(ASKIP,OFF),DATA='RECS-MAX'
@MRECCI  FIELD POS=(08,10),ATTR=(ASKIP,OFF,HI),DATA='-------6'
         FIELD POS=(08,19),ATTR=(ASKIP,OFF),DATA=' * RECS-MAX'
@MRECCA  FIELD POS=(08,31),ATTR=(ASKIP,OFF,HI),DATA='----4000'
         FIELD POS=(08,40),ATTR=(ASKIP,OFF),DATA='* TRK-UTIL% '
@TRKUTL  FIELD POS=(08,53),ATTR=(ASKIP,OFF,HI),DATA='----77'
@FREE5   FIELD POS=(08,60),ATTR=(ASKIP,OFF),DATA='* CI%        '
@CIFREE  FIELD POS=(08,74),ATTR=(ASKIP,OFF,HI),DATA='----50'
@FREE6   FIELD POS=(09,1),ATTR=(ASKIP,OFF),DATA='RECS-LOAD '
@LRECCI  FIELD POS=(09,12),ATTR=(ASKIP,OFF,HI),DATA='-----3'
@FREE7   FIELD POS=(09,19),ATTR=(ASKIP,OFF),DATA=' * RECS-LOAD '
@LRECCA  FIELD POS=(09,33),ATTR=(ASKIP,OFF,HI),DATA='--2000'
         FIELD POS=(09,40),ATTR=(ASKIP,OFF),DATA='*                  '
@FREE8   FIELD POS=(09,60),ATTR=(ASKIP,OFF),DATA='* FREERECS/CI'
@FRECCI  FIELD POS=(09,74),ATTR=(ASKIP,OFF,HI),DATA='-----1'
         FIELD POS=(10,1),ATTR=(ASKIP,OFF),                            *
               DATA='          TOTAL-RECS-DSET'
@DRECS   FIELD POS=(10,27),ATTR=(ASKIP,OFF,HI),DATA='------400000'
@TYPESD  FIELD POS=(10,40),ATTR=(ASKIP,OFF),DATA='* CYLNDERS'
@SPACED  FIELD POS=(10,51),ATTR=(ASKIP,OFF,HI),DATA='-----100'
@FREE9   FIELD POS=(10,60),ATTR=(ASKIP,OFF),DATA='* FREERECS/CA'
@FRECCA  FIELD POS=(10,74),ATTR=(ASKIP,OFF,HI),DATA='--1640'
@FREE10  FIELD POS=(11,1),ATTR=(ASKIP,OFF),                            *
               DATA='          TOTAL-RECS-LOAD'
@LRECS   FIELD POS=(11,27),ATTR=(ASKIP,OFF,HI),DATA='-------81000'
@FREE11  FIELD POS=(11,40),ATTR=(ASKIP,OFF),                           *
               DATA='*      TOTAL-FREE-RECS-DSET'
@FDRECS  FIELD POS=(11,68),ATTR=(ASKIP,OFF,HI),DATA='------419000'
         FIELD POS=(12,1),ATTR=(ASKIP,OFF),                            *
               DATA='***************************************************
               *****************************'
         FIELD POS=(13,1),ATTR=(ASKIP,OFF),                            *
               DATA='      INDEX        *                   *      INDE*
               X'
         FIELD POS=(14,1),ATTR=(ASKIP,OFF),                            *
               DATA='------------------ * ----------------- * ---------*
               --------'
         FIELD POS=(15,1),ATTR=(ASKIP,OFF),DATA='CISIZE'
@ICISZ   FIELD POS=(15,8),ATTR=(ASKIP,OFF,HI),DATA='------1024'
         FIELD POS=(15,19),ATTR=(ASKIP,OFF),                           *
               DATA=' *                   * BLKSIZE '
@IBLKSZ  FIELD POS=(15,51),ATTR=(ASKIP,OFF,HI),DATA='----1024'
         FIELD POS=(15,60),ATTR=(ASKIP,OFF),                           *
               DATA='                     INDEX LEVELS'
@ILEVLS  FIELD POS=(16,14),ATTR=(ASKIP,OFF,HI),DATA='---3'
         FIELD POS=(16,19),ATTR=(ASKIP,OFF),                           *
               DATA=' *'
@TYPESI  FIELD POS=(16,40),ATTR=(ASKIP,OFF),DATA='*   TRACKS'
@SPACEI  FIELD POS=(16,51),ATTR=(ASKIP,OFF,HI),DATA='-------7'
         FIELD POS=(18,1),ATTR=(ASKIP,OFF),                            *
               DATA='THIS SCREEN IS A DISPLAY OF ALL AVAILABLE INFORMAT*
               ION FOR EACH CONTROL'
         FIELD POS=(19,1),ATTR=(ASKIP,OFF),                            *
               DATA='INTERVAL SIZE THAT YOU SELECTED FROM THE CONTROL I*
               NTERVAL SELECTION MENU.'
         FIELD POS=(21,1),ATTR=(ASKIP,OFF),                            *
               DATA='THIS IS A MULTIPAGE SCREEN(PF7=PAGE BACKWARD, PF8=*
               PAGE FORWARD).'
         FIELD POS=(23,1),ATTR=(ASKIP,OFF),                            *
               DATA='END OF TUTORIAL.'
         BUF3270 TYPE=END
./ ADD NAME=MENUSCRN 0102-90281-94195-0838-00055-00055-00000-TSDDC
MENU     BUF3270 TYPE=START
         FIELD POS=(1,1),ATTR=(ASKIP,MDTOFF,HI),                       *
               DATA='-------------------------- V S A M   A N A L Y Z E*
                R --------------------------'
         FIELD POS=(2,1),ATTR=(ASKIP,MDTOFF),                          *
               DATA='ENTER PARAMETERS BELOW:'
         FIELD POS=(3,4),ATTR=(ASKIP,MDTOFF),DATA='AVG-RECSIZE'
         FIELD POS=(3,23),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
RECA     FIELD POS=(3,28),ATTR=(UP,MDTOFF,HI),DATA='     '
         FIELD POS=(3,34),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(3,46),ATTR=(ASKIP,MDTOFF),DATA='MAX-RECSIZE'
         FIELD POS=(3,64),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
RECM     FIELD POS=(3,69),ATTR=(UP,MDTOFF,HI),DATA='     '
         FIELD POS=(3,75),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(4,4),ATTR=(ASKIP,MDTOFF),DATA='RECORDS T/B LOADED'
         FIELD POS=(4,23),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
RECLOAD  FIELD POS=(4,28),ATTR=(UP,MDTOFF,HI),DATA='        '
         FIELD POS=(4,37),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(4,46),ATTR=(ASKIP,MDTOFF),DATA='RECORDS T/B ADDED'
         FIELD POS=(4,64),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
RECADD   FIELD POS=(4,69),ATTR=(UP,MDTOFF,HI),DATA='       '
         FIELD POS=(4,77),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(5,4),ATTR=(ASKIP,MDTOFF),DATA='DEVICE TYPE'
         FIELD POS=(5,23),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
DEVTYPE  FIELD POS=(5,28),ATTR=(UP,MDTON,HI),DATA='3390'
         FIELD POS=(5,33),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(5,46),ATTR=(ASKIP,MDTOFF),DATA='KEYLENGTH'
         FIELD POS=(5,64),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
KEYL     FIELD POS=(5,69),ATTR=(UP,MDTOFF,HI),DATA='   '
         FIELD POS=(5,73),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(6,4),ATTR=(ASKIP,MDTOFF),DATA='IMBED'
         FIELD POS=(6,23),ATTR=(ASKIP,HI,MDTOFF),DATA='===>'
IMB      FIELD POS=(6,28),ATTR=(UP,MDTON,HI),DATA='NO '
         FIELD POS=(6,32),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(6,46),ATTR=(ASKIP,MDTOFF),DATA='REPLICATE'
         FIELD POS=(6,64),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
REPL     FIELD POS=(6,69),ATTR=(UP,MDTON,HI),DATA='NO '
         FIELD POS=(6,74),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(7,4),ATTR=(ASKIP,MDTOFF),DATA='%-ADD RECS CI FREESP*
               ACE'
         FIELD POS=(7,28),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
CIPER    FIELD POS=(7,33),ATTR=(UP,MDTON,HI),DATA='   '
         FIELD POS=(7,37),ATTR=(ASKIP,MDTOFF),DATA='%-ADD RECS CA FREES*
               PACE   '
         FIELD POS=(7,64),ATTR=(ASKIP,MDTOFF,HI),DATA='===>'
CAPER    FIELD POS=(7,69),ATTR=(UP,MDTON,HI),DATA='   '
         FIELD POS=(7,73),ATTR=(ASKIP,MDTOFF)
         FIELD POS=(8,1),ATTR=(ASKIP,MDTOFF,HI),                       *
               DATA='-------------------'
ERROR    FIELD POS=(8,21),ATTR=(ASKIP,MDTOFF,HI),                      *
               DATA='----------------------------------------'
         FIELD POS=(8,62),ATTR=(PL,MDTOFF,HI),                         *
               DATA='------------------'
         FIELD POS=(3,29),CUR=YES
         BUF3270 TYPE=END
./ ADD NAME=OSINIT   0084-84085-84085-1200-00070-00070-00000-OLDF294
*          DATA SET MACRO      AT LEVEL 001 AS OF 06/14/78
         MACRO
         OSINIT &NAME,&BASE,&SAVE
         LCLA  &Z
         LCLC  &A,&B,&C,&D,&E,&F,&G,&H
         AIF (N'&BASE GT 0 AND N'&BASE LE 8 ).BASEOK
         MNOTE ,'NUMBER OF BASE REGISTERS MUST BE 1 TO 8'
         MEXIT
.BASEOK  ANOP
&Z       SETA  0
.LOOP    ANOP
&Z       SETA  1+&Z
&A       SETC   '&A,&BASE(&Z)'
         AIF   (N'&BASE EQ &Z).DONE
         AGO   .LOOP
.DONE    ANOP
         AIF   (T'&BASE(1) NE 'N').BADREG
&NAME    CSECT
         B     50(15)          AROUND CONSTANTS
         DC    C'PROG ID:'
         DC    CL8'&NAME'
         DC    C'  DATE: '
         DC    CL8'&SYSDATE'
         DC    C'  TIME: '
         DC    CL6'&SYSTIME'
         DS    0H
         SAVE  (14,12)
         USING &NAME&A
         LR    &BASE(1),15
         AIF   (N'&BASE EQ 1 ).FINISH
        AIF   (T'&BASE(2) NE 'N').BADREG
         LA    &BASE(2),4095(&BASE(1))
         LA    &BASE(2),1(&BASE(2))
         AIF   (N'&BASE  EQ 2 ).FINISH
        AIF   (T'&BASE(3) NE 'N').BADREG
         LA    &BASE(3),4095(&BASE(2))
         LA    &BASE(3),1(&BASE(3))
         AIF   (N'&BASE  EQ 3 ).FINISH
        AIF   (T'&BASE(4) NE 'N').BADREG
         LA    &BASE(4),4095(&BASE(3))
         LA    &BASE(4),1(&BASE(4))
         AIF   (N'&BASE  EQ 4 ).FINISH
        AIF   (T'&BASE(5) NE 'N').BADREG
         LA    &BASE(5),4095(&BASE(4))
         LA    &BASE(5),1(&BASE(5))
         AIF   (N'&BASE  EQ 5 ).FINISH
         AIF   (T'&BASE(6) NE 'N').BADREG
         LA    &BASE(6),4095(&BASE(5))
         LA    &BASE(6),1(&BASE(6))
         AIF   (N'&BASE  EQ 6 ).FINISH
         AIF   (T'&BASE(7) NE 'N').BADREG
         LA    &BASE(7),4095(&BASE(6))
         LA    &BASE(7),1(&BASE(7))
         AIF   (N'&BASE  EQ 7 ).FINISH
         AIF   (T'&BASE(8) NE 'N').BADREG
         LA    &BASE(8),4095(&BASE(7))
         LA    &BASE(8),1(&BASE(8))
.FINISH  ANOP
         AIF   ('&SAVE' EQ 'NO').NOSAVE
         LA    15,&SAVE
         ST    15,8(13)
         ST    13,4(15)
         LR    13,15
         MEXIT
.NOSAVE  ANOP
         MNOTE *,'NO SAVE AREA SPECIFIED, MUST BE USER SUPPLIED'
         MEXIT
.BADREG ANOP
         MNOTE ,'BASE REGISTERS MUST BE NUMERIC'
         MEND
./ ADD NAME=SBA      0084-84085-84085-1200-00094-00094-00000-OLDF294
         MACRO
&NAME    SBA   &POS,&OVRD
         LCLC  &BIT0,&BIT1,&BIT2,&BIT3,&BIT4,&BIT5,&BIT6,&BIT7
         LCLC  &BIT8,&BIT9,&BIT10,&BIT11,&BIT12,&BIT13,&BIT14,&BIT15
         LCLC  &ADR1,&ADR2,&TYP
         LCLA  &DISP
         AIF   ('&OVRD' EQ '3C').OVRD
&TYP     SETC  '11'
         AGO   .NOVRD
.OVRD    ANOP
&TYP     SETC  '&OVRD'
.NOVRD   ANOP
         AIF   ('&POS(2)' NE '').NASB
&DISP    SETA  &POS
         AGO   .ASB
.NASB    ANOP
&DISP    SETA  80*(&POS(1)-1)+&POS(2)-1
.ASB     ANOP
&BIT0    SETC  '0'
&BIT1    SETC  '1'
&BIT2    SETC  '0'
&BIT3    SETC  '0'
&BIT4    SETC  '0'
&BIT5    SETC  '0'
&BIT6    SETC  '0'
&BIT7    SETC  '0'
&BIT8    SETC  '0'
&BIT9    SETC  '1'
&BIT10   SETC  '0'
&BIT11   SETC  '0'
&BIT12   SETC  '0'
&BIT13   SETC  '0'
&BIT14   SETC  '0'
&BIT15   SETC  '0'
         AIF   (&DISP LT 1024).NOT3
&DISP    SETA  &DISP-1024
&BIT3    SETC  '1'
.NOT3    ANOP
         AIF   (&DISP LT 512).NOT4
&DISP    SETA  &DISP-512
&BIT4    SETC  '1'
.NOT4    ANOP
         AIF   (&DISP LT 256).NOT5
&DISP    SETA  &DISP-256
&BIT5    SETC  '1'
.NOT5    ANOP
         AIF   (&DISP LT 128).NOT6
&DISP    SETA  &DISP-128
&BIT6    SETC  '1'
.NOT6    ANOP
         AIF   (&DISP LT 64).NOT7
&DISP    SETA  &DISP-64
&BIT7    SETC  '1'
.NOT7    ANOP
         AIF   (&DISP LT 32).NOT10
&DISP    SETA  &DISP-32
&BIT10   SETC  '1'
.NOT10   ANOP
         AIF   (&DISP LT 16).NOT11
&DISP    SETA  &DISP-16
&BIT11   SETC  '1'
.NOT11   ANOP
         AIF   (&DISP LT 8).NOT12
&DISP    SETA  &DISP-8
&BIT12   SETC  '1'
.NOT12   ANOP
         AIF   (&DISP LT 4).NOT13
&DISP    SETA  &DISP-4
&BIT13   SETC  '1'
.NOT13   ANOP
         AIF   (&DISP LT 2).NOT14
&DISP    SETA  &DISP-2
&BIT14   SETC  '1'
.NOT14   ANOP
         AIF   (&DISP LT 1).NOT15
&BIT15   SETC  '1'
.NOT15   ANOP
&ADR1    SETC  '&BIT1'.'&BIT2'.'&BIT3'.'&BIT4'.'&BIT5'.'&BIT6'.'&BIT7'
&ADR2    SETC  '&BIT9'.'&BIT10'.'&BIT11'.'&BIT12'.'&BIT13'.'&BIT14'
&ADR2    SETC  '&ADR2'.'&BIT15'
         AIF   ('&ADR1'(4,4) GT '1001').NOT0
         AIF   ('&ADR1'(4,4) EQ '0000' AND '&ADR1'(2,2) NE '11').NOT0
         AIF   ('&ADR1'(2,6) EQ '100001').NOT0
&BIT0    SETC  '1'
.NOT0    ANOP
         AIF   ('&ADR2'(4,4) GT '1001').NOT8
         AIF   ('&ADR2'(4,4) EQ '0000' AND '&ADR2'(2,2) NE '11').NOT8
         AIF   ('&ADR2'(2,6) EQ '100001').NOT8
&BIT8    SETC  '1'
.NOT8    ANOP
&ADR1    SETC  '&BIT0'.'&ADR1'
&ADR2    SETC  '&BIT8'.'&ADR2'
&NAME    DC    X'&TYP',B'&ADR1',B'&ADR2'
         MEND
./ ADD NAME=SF       0084-84085-84085-1200-00073-00073-00000-OLDF294
         MACRO
&NAME    SF
         LCLC  &BIT2,&BIT3,&BIT4,&BIT5,&BIT7,&CURR,&ATTR
         LCLA  &N
&BIT2    SETC  '0'
&BIT3    SETC  '0'
&BIT4    SETC  '0'
&BIT5    SETC  '0'
&BIT7    SETC  '0'
.LOOP    ANOP
&N       SETA  &N+1
&CURR    SETC  '&SYSLIST(1,&N)'
         AIF   ('&CURR' EQ '').ESCAN
         AIF   ('&CURR' EQ 'PROT').PL
         AIF   ('&CURR' EQ 'PL').PL
         AIF   ('&CURR' EQ 'ASKIP').PS
         AIF   ('&CURR' EQ 'PS').PS
         AIF   ('&CURR' EQ 'UNPROT').LOOP
         AIF   ('&CURR' EQ 'UP').LOOP
         AIF   ('&CURR' EQ 'NUM').NU
         AIF   ('&CURR' EQ 'NU').NU
         AIF   ('&CURR' EQ 'AN').LOOP
         AIF   ('&CURR' EQ 'NORM').LOOP
         AIF   ('&CURR' EQ 'NI').LOOP
         AIF   ('&CURR' EQ 'BRT').HI
         AIF   ('&CURR' EQ 'HI').HI
         AIF   ('&CURR' EQ 'DRK').ND
         AIF   ('&CURR' EQ 'ND').ND
         AIF   ('&CURR' EQ 'SPD').SPD
         AIF   ('&CURR' EQ 'NSP').LOOP
         AIF   ('&CURR' EQ 'FSET').MDTON
         AIF   ('&CURR' EQ 'MDTON').MDTON
         AIF   ('&CURR' EQ 'MDTOFF').LOOP
         AIF   ('&CURR' EQ 'ON').MDTON
         AIF   ('&CURR' EQ 'OFF').LOOP
         MNOTE 4,&CURR IS NOT A SF OPTION'
         AGO   .LOOP
.PL      ANOP
&BIT2    SETC  '1'
&BIT3    SETC  '0'
         AGO   .LOOP
.PS      ANOP
&BIT2    SETC  '1'
&BIT3    SETC  '1'
         AGO   .LOOP
.NU      ANOP
&BIT3    SETC  '1'
         AGO   .LOOP
.HI      ANOP
&BIT4    SETC  '1'
         AGO   .LOOP
.ND      ANOP
&BIT4    SETC  '1'
&BIT5    SETC  '1'
         AGO   .LOOP
.SPD     ANOP
&BIT5    SETC  '1'
         AGO   .LOOP
.MDTON   ANOP
&BIT7    SETC  '1'
         AGO   .LOOP
.ESCAN   ANOP
&ATTR    SETC  '1'.'&BIT2'.'&BIT3'.'&BIT4'.'&BIT5'.'0'.'&BIT7'
         AIF   ('&ATTR'(4,4) GT '1001').USE0
         AIF ('&ATTR'(4,4) EQ '0000' AND '&ATTR'(2,6) NE '110000').USE0
         AIF   ('&ATTR'(2,6) EQ '100001').USE0
&ATTR    SETC  '1'.'&ATTR'
         AGO   .EOJ
.USE0    ANOP
&ATTR    SETC  '0'.'&ATTR'
.EOJ     ANOP
&NAME    DC    X'1D',B'&ATTR'
         MEND
./ ADD NAME=VSAMADEV 0101-90278-90282-0844-00049-00049-00001-CART1
         TITLE 'DEVICE TABLE DSECT'
***********************************************************************
*                                                                     *
* THIS IS THE COMMOM DSECT FOR A DEVICE TABLE ENTRY USED BY THE       *
* VSAMANAL(TSO COMMAND PROCESSOR) AND ITS SUB-MODULES.....            *
*                                                                     *
DEVENTRY DSECT
DSELECT  DS    0X   DATA CONTROL INTERVAL(DETAIL SELECT INDICATOR)
*              X'00'=NOT SELECTED.
*              X'01'=SELETED FOR DETAIL SCREEN DISPLAY.
DCISZ    DS    F    DATA CONTROL INTERVAL SIZE
PHYBLKSZ DS    F    PHYSICAL BLOCKSIZE
#BLKSTRK DS    F    NUMBER OF PHYSICAL BLOCKS PER TRACK
TRKUTL   DS    F    PERCENTAGE OF TRACK UTILIZATION
#TRKCA   DS    F    NUMBER OF TRACK PER CONTROL AREA (DEFAULT = 1 CYL)
DCIRATIO DS    F    PERCENTAGE OF RECORDS ADDED NEEDED IN CIFREESPACE
DCARATIO DS    F    PERCENTAGE OF RECORDS ADDED NEEDED IN CAFREESPACE
#CICA    DS    F    NUMBER OF CONTROL INTERVALS PER CONTROL AREA
#RCINFSP DS    F    NUMBER OF RECORDS PER CONTROL INTERVAL-NO FREESPACE
#RCANFSP DS    F    NUMBER OF RECORDS PER CONTROL AREA -   NO FREESPACE
#FRCA    DS    F    NUMBER OF FREE RECORDS NEED PER CONTROL AREA
FRCAFSP  DS    F    NUMBER OF FREE RECORDS NEED/CA FREESPACE
FRCIFSP  DS    F    NUMBER OF FREE RECORDS NEED/CI FREESPACE
PERFSPCI DS    F    PERCENTAGE OF FREESPACE PER CONTROL INTERVAL
PERFSPCA DS    F    PERCENTAGE OF FREESPACE PER CONTROL AREA
#FCICA   DS    F    NUMBER OF FREE CONTROL INTERVALS PER CONTROL AREA
#RCILOAD DS    F    NUMBER OF RECORDS IN A CONTROL INTERVAL AT LOADTIME
#RCALOAD DS    F    NUMBER OF RECORDS IN A CONTROL AREA AT LOADTIME
#RCIFREE DS    F    NUMBER OF FREE RECORDS-IN A NON-FREE CI
#RCAFREE DS    F    NUMBER OF FREE RECORDS IN FREE CI'S IN A CA
#TRKDATA DS    F    NUMBER OF TRACKS FOR DATA COMPONENT WITH FREESPACE
#TRKXTRA DS    F    NUMBER OF TRACKS FOR FREE RECS AT END OF DATASET
#TRKNFSP DS    F    NUMBER OF TRACKS FOR DATA COMPONENT WITHOUT FREESPC
XRCCAFSP DS    F    NUMBER OF EXCESS RECORDS IN FREE CI'S(CAFREESPACE)
*                   NUMBER OF EXCESS RECORDS(IN MULTIPLES OF NON-FREE
*                                            CI'S PER CA)
ZRCCAFSP DS    F    TRUE NUMBER OF EXCESS FREE RECORDS PER CA
#TRKINDX DS    F    NUMBER OF TRACKS NEED FOR INDEX COMPONENT
#INDXLV  DS    F    NUMBER OF INDEX LEVELS NEEDED TO ADDR DATA
ICISZ    DS    F    INDEX CONTROL INTERVAL SIZE
#CIHIGHL DS    F    NUMBER OF HIGH LEVEL INDEX RECORDS
DEVENLEN EQU   *-DCISZ
         ORG
DEVCLLEN EQU   *-#CICA  LENGTH OF CLEAR FOR RECALCULATION
*                                                                     *
*                                                                     *
*           E N D   O F    D E V I C E    E N T R Y   D S E C T       *
*                                                                     *
***********************************************************************
./ ADD NAME=VSAMADTL 0100-94187-94187-0737-00331-00331-00000-CART1
***********************************************************************
*                                                                     *
* THIS PROGRAM IS A SUB-MODULE OF VSAMANAL(TSO COMMAND PROCESSOR)     *
*                                                                     *
* ITS FUNCTION IS TO PROCESS THE DETAIL DISPLAY OF THE DATA AND INDEX *
* COMPONENT INFORMATION FROM THE DEVICE TABLE FOR THOSE DATA CISZ'S   *
* SELECTED BY THE TSO USER FROM THE DATA SCREEN.                      *
*                                                                     *
*                                                                     *
* THIS PROGRAM IS RE-ENTRANT.                                         *
*                                                                     *
* AUTHOR: ARTHUR J. CARTIER                                           *
* DATA:   11/16/78                                                    *
* OWNER:  STATE OF CONNECTICUT                                        *
*         CENTRAL DATA PROCESSING SERVICES(CDPS)                      *
*         TECHNICAL SUPPORT SECTION                                   *
*                                                                     *
***********************************************************************
*                                                                     *
* LINKAGE TO THIS PROGRAM IS BY THE LINK MACRO IN THE FORM...         *
*                                                                     *
*        LA  R0,WORKAREA   WORKAREA=STARTING ADDRESS OF VSAMANAL      *
*                                   COMMON WORKAREA                   *
*        LA  R13,CALLERSAVEAREA                                       *
*        LINK EP=VSAMADTL                                             *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         PRINT NOGEN
         EJECT
         OSINIT VSAMADTL,(11,12),NO
         USING WORKAREA,R10       ADDRESSABILITY TO COMMON WORKAREA
         LR    R10,R0             COMMON WORKAREA ADDRESS
         LA    R14,SAVEDTL        OUR SAVE AREA
         ST    R14,8(R13)         FORWARD SAVE AREA CHAIN
         ST    R13,SAVEDTL+4      BACKWARD SAVE AREA CHAIN
         LR    R13,R14            OUR SAVE AREA AS CURRENT
         GETMAIN R,LV=DETAILL+INDEXL
         LR    R6,R1              ADDRESS OF GETMAINED AREA
* INITIALIZE DETAIL SCREEN (3270 FULLSCR OUTPUT BUFFER)
         ST    R6,ADETAIL         SAVE DETAIL SCREEN ADDRESS
         LA    R7,DETAILL         LENGTH OF DETAIL SCREEN
         ST    R7,LDETAIL         SAVE DETAIL SCREEN LENGTH
         LR    R9,R7
         LA    R8,DETAIL          ADDR OF ORIGINAL SCREEN
         MVCL  R6,R8              MOVE DETAIL SCREEN TO WORKAREA
* INITIALIZE INDEX SCREEN (3270 FULLSCR OUTPUT BUFFER)
         ST    R6,AINDEX          SAVE INDEX SCREEN ADDRESS
         LA    R7,INDEXL          LENGTH OF INDEX SCREEN
         ST    R7,LINDEX          SAVE INDEX SCREEN LENGTH
         LR    R9,R7
         LA    R8,INDEX           ADDRESS OF ORIGINAL INDEX SCREEN
         MVCL  R6,R8              MOVE INDEX SCREEN TO WORKAREA
         EJECT
***********************************************************************
*                                 *
* THE DEVICE TABLE WILL BE SCANNED (TOP-DOWN) FOR THE FIRST DCISZ     *
* SELECTED FOR DETAILED DATA-INDEX INFORMATION. ONCE FOUND THE DETAIL *
* SCREEN WILL BE FORMATTED AND DISPLAYED ON THE TUBE...               *
*                                 *
* A READ(TGET) FROM THE TUBE WILL THEN BE EXECUTED FOR PFKEY ANALYSIS *
* IN ORDER TO DETERINE IF THE TSO USER WANTS TO LOOK AT NEXT SELECTED *
* DCISZ, THE PREVIOUSLY SELECTED DCISZ, BACK TO THE PREVIOUS PHASE, OR*
* BACK TO THE MENU SELECTION SCREEN...                                *
*                                 *
         L     R7,DEVREGSV        START OF DEVICE TABLE
         USING DEVENTRY,R7        ADDRESSABILITY TO DEVICE TABLE ENTRY
DTLSCNDV DS    0H SCAN THE DEVICE TABLE FOR A SELECTED DCISZ
         CLC   DCISZ,=F'-1'       END OF DEVICE TABLE?
         BE    ANYSEL             YES-GO READ FROM THE TUBE..
         CLI   DSELECT,X'01'      WAS THIS DCISZ SELECTED??
         BE    FORMTDTL           YES-GO FORMAT THE DETAIL SCREEN
NXTDEV   DS    0H
         LA    R7,DEVENLEN(R7)    NEXT DEVICE TABLE ENTRY
         B     DTLSCNDV           GO LOOK FOR NEXT SELECTED DCISZ
ANYSEL   DS    0H
         CLI   ADETAIL,X'01'      WERE ANY DCISZ'S SELECTED...
         BNE   RETURN             NO-GO BACK TO CALLING MODULE..
         B     PAGEBACK           YES-GO REDISPLAY LAST SEL ENTRY..
         EJECT
FORMTDTL DS    0H
         L     SCRNREG,ADETAIL    START OF DETAIL SCREEN(WORKAREA COPY)
         USING DETAILD,SCRNREG    ADDRESSABILITY TO SCREEN DSECT
         MVI   ADETAIL,X'01'      SET SELECTED DCISZ WAS FOUND..
         OC    #RECADD,#RECADD    DO WE HAVE TO WORRY ABOUT FREESPACE
         BNZ   BYFREECL           YES-DON'T CLEAR FREESPACE CAPTIONS
         MVC   @FREE1,BLANKS      **************************
         MVC   @FREE2,BLANKS      * C L E A R              *
         MVC   @FREE3,BLANKS      *                        *
         MVC   @FREE4,BLANKS      *     F R E E S P A C E  *
         MVC   @FREE5,BLANKS      *                        *
         MVC   @FREE6,BLANKS      *        S E C T I O N   *
         MVC   @FREE7,BLANKS      *                        *
         MVC   @FREE8,BLANKS      *              O F       *
         MVC   @FREE9,BLANKS      *                        *
         MVC   @FREE10,BLANKS     *        S C R E E N     *
         MVC   @FREE11,BLANKS     *                        *
         MVC   @FREE12,BLANKS     **************************
BYFREECL DS    0H
         L     WORKREG,DCISZ      DATA CI SIZE
         LA    WORKREG,0(WORKREG) CLEAR SELECTION INDICATOR
         CVD   WORKREG,DOUBLE
         MVC   @DCISZ,DMASK
         ED    @DCISZ,DOUBLE+3
         SPACE 3
         L     WORKREG,#TRKCA     NUMBER OF TRACKS PER CONTROL AREA
         CVD   WORKREG,DOUBLE
         MVC   @DCASZ,DMASK
         ED    @DCASZ,DOUBLE+6
         SPACE 3
         L     WORKREG,PHYBLKSZ   PHYSICAL BLOCK SIZE
         CVD   WORKREG,DOUBLE
         MVC   @BLKSZ,DMASK
         ED    @BLKSZ,DOUBLE+4
         SPACE 3
         L     WORKREG,#CICA      NUMBER OF CI'S PER CA
         CVD   WORKREG,DOUBLE
         MVC   @CICA,DMASK
         ED    @CICA,DOUBLE+3
         SPACE 3
         L     WORKREG,#BLKSTRK   NUMBER OF PHSYICAL BLOCKS PER TRACK
         CVD   WORKREG,DOUBLE
         MVC   @BLKTRK,DMASK
         ED    @BLKTRK,DOUBLE+4
         SPACE 3
         L     WORKREG,#RCINFSP   NUMBER OF RECS IN A CI (MAXIMUM)
         CVD   WORKREG,DOUBLE
         MVC   @MRECCI,DMASK
         ED    @MRECCI,DOUBLE+4
         SPACE 3
         L     WORKREG,#RCANFSP   NUMBER OF RECS IN A CA (MAXIMUM)
         CVD   WORKREG,DOUBLE
         MVC   @MRECCA,DMASK
         ED    @MRECCA,DOUBLE+4
         SPACE 3
         L     WORKREG,TRKUTL
         CVD   WORKREG,DOUBLE
         MVC   @TRKUTL,DMASK
         ED    @TRKUTL,DOUBLE+5
         SPACE 3
*                                 TOTAL RECS IN DATASET
         XR    R8,R8              CLEAR FOR MULTIPLY
         L     R9,#RCILOAD        NUMBER OF RECS IN CI(NON-FREE)
         A     R9,#RCIFREE        NUMBER OF RECS IN CI(FREE)
         L     WORKREG,#CICA      NUMBER OF CI'S PER CA
         S     WORKREG,#FCICA     SUB FREE CI'S PER CA GIVING NON-FREE
         MR    R8,WORKREG         MAX RECORDS IN NON-FREE CI'S PER CA
         A     R9,#RCAFREE        PLUS TOTAL RECS IN FREE CI'S PER CA
         LR    WORKREG,R9         GIVES MAX RECS PER CA(FREE+NONFREE)
         L     R9,#TRKDATA        TOTAL TRACKS IN DATA SET
         D     R8,#TRKCA          TATAL CA'S IN DATA SET
         MR    R8,WORKREG         MAX RECORDS(AVG) IN DATA SET
         CVD   R9,DOUBLE
         MVC   @DRECS,DMASK
         ED    @DRECS,DOUBLE+2
         SPACE 3
         L     R9,#TRKDATA        TOTAL TRACKS IN DATA SET
         CLC   #TRKCA,TRKCYL      IS THE CA A CYLINDER?
         BNE   TRKALLOC           NO- INDICATE ALLOCATION IN TRACKS
         XR    R8,R8              TYPE OF ALLOCATION(CYL OR TRK)
         L     R1,TRKCYL          TRACKS PER CYLINER
         DR    R8,R1              NUMBER OF CYLINDERS NEEDED
         MVC   @TYPESD,=C'* CYLNDERS'  INDICATE ALLOCATION IN CYLINDERS
         B     SETSPACE
TRKALLOC DS    0H
         MVC   @TYPESD,=C'*   TRACKS'  INDICATE ALLOCATION IN TRACKS
         B     SETSPACE           GO MOVE SPACE VALUE TO SCREEN
SETSPACE DS    0H
         CVD   R9,DOUBLE
         MVC   @SPACED,DMASK
         ED    @SPACED,DOUBLE+4
         SPACE 3
         OC    #RECADD,#RECADD    ANY FREESPACE
         BZ    PUTDTL             NO-GO DISPLAY DETAIL(DATA CMPNT)
*
*
* MOVE FREESPACE INFORMATION TO SCREEN BUFFER
*
*
         L     WORKREG,PERFSPCA
         CVD   WORKREG,DOUBLE
         MVC   @CAFREE,DMASK
         ED    @CAFREE,DOUBLE+5
         SPACE 3
         L     WORKREG,#FCICA     FREE CI'S PER CA
         CVD   WORKREG,DOUBLE
         MVC   @FCICA,DMASK
         ED    @FCICA,DOUBLE+5
         SPACE 3
         L     WORKREG,PERFSPCI  CONTROL INTERVAL FREESPACE PERCENTAGE
         CVD   WORKREG,DOUBLE
         MVC   @CIFREE,DMASK
         ED    @CIFREE,DOUBLE+5
         SPACE 3
         L     WORKREG,#RCILOAD   NUMBER OF RECS IN CI AT LOAD TIME
         CVD   WORKREG,DOUBLE
         MVC   @LRECCI,DMASK
         ED    @LRECCI,DOUBLE+5
         SPACE 3
         L     WORKREG,#RCALOAD   NUMBER OF RECS IN CA AT LOAD TIME
         CVD   WORKREG,DOUBLE
         MVC   @LRECCA,DMASK
         ED    @LRECCA,DOUBLE+5
         SPACE 3
         L     WORKREG,#RCIFREE   NUMBER OF FREE RECS IN A CI
         CVD   WORKREG,DOUBLE
         MVC   @FRECCI,DMASK
         ED    @FRECCI,DOUBLE+5
         SPACE 3
         XR    R8,R8
         L     R9,#CICA           NUMBER OF CI'S PER CA
         S     R9,#FCICA         SUB FREE CI'S PER CA GIVING NONFREE
         MR    R8,WORKREG         MULT FREE RECS PER CI BY NONFREE CI'S
         A     R9,#RCAFREE        ADD FREE RECS IN FREE CI'S PER CA
         CVD   R9,DOUBLE
         MVC   @FRECCA,DMASK
         ED    @FRECCA,DOUBLE+5
         SPACE 3
         L     WORKREG,#RCALOAD NUMBER OF RECS IN CA AT LOAD TIME
         XR    R8,R8
         L     R9,#TRKDATA        NUMBER OF TRACKS IN DATA SET
         D     R8,#TRKCA       BY TRACKS PER CA GIVING CA'S IN DATASET
         XR    R8,R8
         ST    R9,SAVEREG         SAVE NUMBER OF CA'S IN DATASET
         MR    R8,WORKREG         MULT BY NUMBER OF LOAD RECS/CA
         CVD   R9,DOUBLE          GIVING NUMBER OF LOAD RECS IN DATASET
         MVC   @LRECS,DMASK
         ED    @LRECS,DOUBLE+2
         SPACE 3
         L     R9,#CICA           NUMBER OF CI'S PER CA
         S     R9,#FCICA          MINUS FREE CI'S PER CA
         XR    R8,R8
         M     R8,#RCIFREE        NON FREE CI'S PER CA BY FREERECS/CI
         A     R9,#RCAFREE        PLUS FREE RECS IN FREE CI'S PER CA
*                                 GIVES FREE RECS PER CA..
         M     R8,SAVEREG         MULT BY CA'S IN DATASET GIVES
         CVD   R9,DOUBLE          FREE RECORDS IN DATA SETR
         MVC   @FDRECS,DMASK
         ED    @FDRECS,DOUBLE+2
         SPACE 3
PUTDTL   DS    0H
         L     R1,ADETAIL         3270 SCREEN ADDRESS
         L     R0,LDETAIL         LENGTH OF SCREEN
         ICM   R1,8,FULLSCR       OPTION BYTE
         TPUT  ((R1)),((R0)),R
         OC    KEYLEN,KEYLEN      IS THIS A KSDS/AIX?
         BZ    DTLGET             NO-BYPASS INDEX SCREEN
         L     SCRNREG,AINDEX     YES-INDEX SCREEN ADDRESS
         USING INDEXD,SCRNREG     ESTABLISH ADDRESSABILITY
         SPACE 3
         L     WORKREG,ICISZ      INDEX CISIZE
         CVD   WORKREG,DOUBLE
         MVC   @ICISZ,DMASK
         ED    @ICISZ,DOUBLE+3
         MVC   @IBLKSZ,DMASK
         ED    @IBLKSZ,DOUBLE+4
         SPACE 3
         L     WORKREG,#INDXLV    NUMBER OF INDEX LEVELS
         CVD   WORKREG,DOUBLE
         MVC   @ILEVLS,DMASK
         ED    @ILEVLS,DOUBLE+6
         SPACE 3
         L     R9,#TRKINDX
         CVD   R9,DOUBLE
         MVC   @SPACEI,DMASK
         ED    @SPACEI,DOUBLE+4
         LR    R1,SCRNREG
         L     R0,LINDEX
         ICM   R1,8,FULLSCR
         TPUT  ((R1)),((R0)),R
DTLGET   DS    0H
         L     R0,LDETAIL         SCREEN LENGTH
         L     R1,ADETAIL         SCREEN ADDRESS
         LR    R2,R10             COMMON WORKAREA ADDRESS
         LINK  EP=VSAMAGET        GO READ FROM THE TUBE
         CLI   KEYHIT,PF1         WAS-PF1 KEY HIT(HELP)?
         BNE   NXTKEY             NO-LOOK AGAIN
         LR    R0,R10             COMMON WORKAREA ADDRESS
         MVC   HLPTYPE,=CL3'DTL'
         LINK  EP=VSAMAHLP
         CLI   KEYHIT,PF4         WAS-PF4(SPF PRIMARY OPTION MENU)
         BE    RETURN
         TPUT  CLEARBUF,CLEARLEN,FULLSCR CLEAR SCREEN
         L     R1,AMENUD          MENU SCREEN ADDRESS
         L     R0,LMENUD          MENU SCREEN LENGTH
         ICM   R1,8,FULLSCR       FULLSCR OPTION
         TPUT  ((R1)),((R0)),R    REDISPLAY MENU SCREEN
         B     PUTDTL             GO REDISPLAY LAST PAGE AGAIN
NXTKEY   DS    0H
         CLI   KEYHIT,CLEAR       WAS-CLEAR KEY HIT?
         BE    PUTDTL             YES-GO REDISPLAY LAST PAGE AGAIN..
         CLI   KEYHIT,PF7         WAS PF7(PAGE BACK) HIT?
         BE    PAGEBACK           YES-DISPLAY LAST PAGE IF POSSIBLE
         CLI   KEYHIT,PF3         WAS PF3(BACK TO DATASELECT) HIT?
         BE    RETURN             YES-GO BACK TO CALLING MODULE..
         CLI   KEYHIT,PF4         WAS PF4(END COMMAND HIT) HIT?
         BE    RETURN             YES-GO BACK TO CALLING MODULE...
         CLI   KEYHIT,PF8         WAS PF8(PAGE FORWARD) HIT?
         BNE   PUTDTL             GO REDISPLAY LAST PAGE AGIAN....
         CLC   DCISZ,=F'-1'       YES-ARE WE ALREADY AT END OF TABLE
         BNE   NXTDEV             NO-GO POINT TO NEXT TABLE ENTRY
         B     PUTDTL             YES-GO REDISPLAY LAST PAGE AGAIN..
         EJECT
PAGEBACK DS    0H
         C     R7,DEVREGSV  ARE WE AT THE BEGINNING OF DEVICE TABLE
         BE    PUTDTL             YES-GO REDISPLAY LAST PAGE AGAIN..
         SH    R7,=Y(DEVENLEN)    NO-THEN BACK UP ONE TABLE ENTRY
         CLI   DSELECT,X'01'      IS IT A SELECTED ENTRY???
         BE    FORMTDTL           YES-GO FORMAT IT AGAIN...
         B     PAGEBACK           NO-GO BACKUP ANOTHER TABLE ENTRY
         SPACE 3
RETURN   DS    0H
         L     R1,ADETAIL         SET UP STORAGE ADDRESS FOR FREEMAIN
         FREEMAIN R,A=(1),LV=DETAILL+INDEXL
         XC    ADETAIL,ADETAIL    CLEAR STORAGE POINTER
         L     R13,SAVEDTL+4
         RETURN (14,12)
         EJECT
BLANKS   DC    CL80' '
DMASK    DC    XL12'602020202020202020202020'
FULLSCR  DC    X'03'
CLEARBUF DC    X'C2115D7F1140403C5D7F0011404013'  *POSITIONAL*
CLEARLEN EQU   *-CLEARBUF                         *POSITIONAL*
         LTORG
         EJECT
         COPY VSAMAEQU
         COPY DTLSCRN
         COPY VSAMAWRK
         COPY VSAMADEV
         END  VSAMADTL
./ ADD NAME=VSAMAEQU 0100-94187-94187-0738-00067-00067-00000-CART1
         EJECT
***********************************************************************
*                                                                     *
* THE FOLLOWING ARE THE SYMBOLIC EQUATES USED BY VSAMANAL(TSO COMMAND *
* PROCESSOR) AND SUB-MODULES(VSAMAGET VSAMADTL)......                 *
*                                                                     *
*
*
****** SYMBOLIC VALUES.............
CLEAR   EQU  X'6D'
ENTER   EQU  X'7D'
PF1     EQU  X'F1'
PF2     EQU  X'F2'
PF3     EQU  X'F3'
PF4     EQU  X'F4'
PF5     EQU  X'F5'
PF6     EQU  X'F6'
PF7     EQU  X'F7'
PF8     EQU  X'F8'
PF9     EQU  X'F9'
SBA      EQU   X'11'
*
*
*  R E G I S T E R    E Q U A T E S
*
*
*
WORKREG  EQU   5
SCRNREG  EQU   4
R1       EQU   1
DATALEN  EQU   4
DATASW   EQU   6
LINKREG  EQU   6
DEVREG   EQU   4
R15      EQU   15
EVENODD  EQU   8
DIVIDEND EQU   9
REMAINDR EQU   8
QUOTIENT EQU   9
DIVISOR  EQU   15
R8       EQU   8
R5       EQU   5
R4       EQU   4
WAREAREG EQU   13
R2       EQU   2
R3       EQU   3
R6       EQU   6
R7       EQU   7
R9       EQU   9
R13      EQU   13
R14      EQU   14
R0       EQU   0
R10      EQU   10
R12      EQU   12
BUFFLEN  EQU   15
SCRN     EQU   4
SCRNLEN  EQU   5
SCRNEND  EQU   7
BUFF     EQU   8
BUFFEND  EQU   9
LINE     EQU   7
LINECNT  EQU   8
PAGEREG  EQU   9
*                                                                     *
*      E N D    O F    S Y M B O L I C    E Q U A T E S               *
*                                                                     *
***********************************************************************
./ ADD NAME=VSAMAGET 0100-94187-94187-0738-00280-00280-00000-CART1
***********************************************************************
*                                                                     *
* THIS PROGRAM IS A SUB-MODULE OF THE VSAMANAL(TSO COMMAND PROCESSOR) *
*                                                                     *
* THE MAIN FUNCTION OF THIS PROGRAM IS TO PERFORM A TSO TGET W/ASIS   *
* OPTION INTO AN INPUT WORK BUFFER.....THE BUFFER WILL BE EDITED FOR  *
* VALIDITY AND ALL DATA FOUND IN THE BUFFER WILL BE MOVED TO ITS      *
* CORRESPONDING 3270(FULLSCRN) SCREEN BUFFER.  ALL MOVING IS ON A 3270*
* FIELD(SBA,ADDR,SF,ATTR,DATA) LEVEL, AND INPUT FIELDS ARE MOVED ONLY *
* IF A CORRESPONDING FIELD IS FOUND IN THE SCREEN BUFFER. ALL DATA IS *
* MOVED FOR THE LENGTH OF THE SCREEN FIELD SO AS TO PROTECT THE       *
* INTEGRITY OF THE SCREEN BUFFER FOR SUBSEQUENT TPUTS(FULLSCRN).      *
* THE SCREEN FIELD WILL PADDED RIGHT W/X'40'S IF NECESSARY.           *
*                                                                     *
* ERRORS:   ANY PREDICTABLE PROGRAM ERRORS WILL CAUSE AN ERROR DESCRIP*
*           TION TO BE PLACED IN THE COMMON WORKAREA FIELD(PROCERR).  *
*                                                                     *
* LINKAGE:  LINKAGE IS EXPECTED THROUGH THE LINK MACRO IN THE FORM..  *
*            LA  0,LENGTH    LENGTH=LENGTH OF 3270 SCREEN BUFFER      *
*                                   BUFFER.
*            LA  2,WORKAREA  WORKAREA=STARTING LOCATION OF THE VSAMANAL
*            LA  13,CALLERSAVEAREA
*            LINK EP=VSAMAGET                                         *
*                                                                     *
***********************************************************************
*                                                                     *
* THIS PROGRAM IS WRITTEN AS A RE-ENTRANT MODULE......                *
*                                                                     *
*  AUTHOR:  ARTHUR J. CARTIER                                         *
* DATE: 11/14/78                                                      *
* PROPERTY OF:   STATE OF CONNECTICUT                                 *
*                CENTRAL DATA PROCESSING SERVICES(CDPS)               *
*                TECHNICAL SUPPORT SECTION.                           *
*                                                                     *
* ASSOCIATED PROGRAMS:                                                *
*                                                                     *
***********************************************************************
         EJECT
         PRINT NOGEN
         EJECT
         OSINIT VSAMAGET,(11,12),NO
         USING WORKAREA,R10       ADDRESSABILITY TO VSAMANAL WORKAREA
         LR    R10,R2             POINT THE WORKAREA DSECT REG. TO IT
         ST    R0,SCRNLSV         SAVE SCREEN LENGTH
         ST    R1,SCRNSAVE        SAVE SCREEN ADDRESS
         LA    R14,SAVEGET   THIS PROGRAMS SAVE AREA(PART OF WORKAREA)
         ST    R14,8(R13)         FORWARD SAVE AREA(CHAIN)
         ST    R13,4(R14)         BACKWARD SAVE AREA(CHAIN)
         LR    R13,R14            SET YOUR SAVE AREA POINTER
         XC    PROCERR,PROCERR    CLEAR ERROR INDICATOR
         LA    R1,INBUFF
         ICM   R1,8,TGETASIS
         LA    R0,INBUFFL
         TGET  ((R1)),((R0)),R
         OR    R15,R15            TGET SUCESSFUL
         BNZ   TGETERR            NO GO PROCESS ERROR
*
         MVC   KEYHIT,INBUFF      SAVE KEY ENTERED
         MVC   CURPOS,INBUFF+1    SAVE WHERE CURSOR WAS
         LR    BUFFLEN,R1         BUFFER LENGTH
         SH    BUFFLEN,=H'4'     SUB FOR KEYENTRD,CURSOR,EXECUTE INSTR
         BNP   EXITSCRN           GO EXIT IF NO DATA ENTERED
         LA    BUFF,INBUFF+3      START OF INPUT BUFFER+3
         LA    BUFFEND,0(BUFF,BUFFLEN) END OF BUFFER DATA
         CLI   0(BUFF),SBA        IS THERE AN SBA
         BNE   BUFFERR            NO BUFFER ERROR
SETSCRN  DS    0H
         L     SCRN,SCRNSAVE      START OF INPUT SCREEN
         LA    SCRN,4(SCRN)       FIRST SBA IN SCREEN
         L     SCRNLEN,SCRNLSV    LENGTH OF INPUT SCREEN
         SH    SCRNLEN,=H'5'      SUB FOR WCC,SPF STRING,EXECUTE
         LA    SCRNEND,0(SCRN,SCRNLEN) END OF INPUT SCREEN
         CLI   0(SCRN),SBA        IS INPUT SCREEN GOOD
         BNE   SCRNERR            NO ERROR IN INPUT SCREEN
         B     BUFFSCAN           GO MATCH UP BUFFER TO SCREEN FIELDS
         EJECT
FINDSBA  TRT   0(0,R1),SBATABLE    TRANSLATE FOR SBA(X'11')
MOVFIELD MVC   5(0,SCRN),0(BUFF)   MOVE BUFF FIELD TO SCREEN FIELD
UPPERCSE OC    5(0,SCRN),BLANKS    SET ALL ALPHA TO UPPER CASE
         SPACE 3
BUFFSCAN DS    0H
         CR    BUFF,BUFFEND       END OF INPUT BUFFER
         BNL   EXITSCRN           YES-GET OUT
         LA    BUFF,1(BUFF)       3270 ADDRESS BYTES
         BCTR  BUFFLEN,0          ADJUST
         MVC   ADDRESS,0(BUFF)    MOVE FOR ADDRESS ADJUSTMNT
         LA    BUFF,2(BUFF)       DATA FIELD IN INPUT BUFFER
         BCTR  BUFFLEN,0          ADJUST
         BCTR  BUFFLEN,0          ADJUST
         BAL   LINKREG,SBAMINUS   GO ADJUST 3270 ADDRESS (-1)
         CR    BUFF,BUFFEND       ANY DATA IN FIELD???
         BH    CLRFIELD           NO-HIT (EOF) CLEAR LAST FIELD
         CLI   0(BUFF),SBA        ANY DATA FIELD????
         BE    CLRFIELD           NO-HIT (EOF) GO CLEAR FIELD
COMPADDR DS    0H
         CLC   ADDRESS,1(SCRN)    IS THIS THE FIELD
         BNE   SCRNSCAN           NO-GO FIND NEXT FIELD
EXBUFF   DS    0H
         LR    R1,BUFF            SET FOR TRANSLATE
         LR    R14,BUFFLEN        SAVE FOR TRANSLATE LENGTH
         C     R14,=F'255'        BUFFLEN GREATER THAN MAX TRANSLATE
         BNH   EXSBABUF           NO-USE BUFFLEN FOR TRANSLATE
         LA    R14,255            YES-USE MAX TRANSLATE LENGTH
EXSBABUF DS    0H
         EX    R14,FINDSBA        FIND NEXT SBA
         BNZ   MOVEDATA           FOUND IT-GO MOVE DATA
         CR    R14,BUFFLEN        NO SBA FOUND-DID WE LOOK AT
*                                 ALL THE BUFFER....
         BL    TRYABUFF           NO WE DIDN'T- GO TRY NEXT 256 OR LESS
         LR    R1,BUFFEND         YES WE DID-SET END-OF-BUFFER
         LA    R1,1(R1)           END-OF-BUFFER +1
         B     MOVEDATA           NOW GO MOVE THE LAST DATA FIELD
TRYABUFF DS    0H
         SR    BUFFLEN,R14        ADJUST BUFFER LENGTH
         BCTR  BUFFLEN,0
         MVC   5(256,SCRN),0(BUFF)
         LA    SCRN,1(R14,SCRN)   BUMP SCREEN POINTER BY 256
         LA    BUFF,1(R14,BUFF)   BUMP BUFFER POINTER BY 256
         B     EXBUFF             GO LOOK AT NEXT 256 OR LESS FOR SBA
MOVEDATA DS    0H
         LA    R1,0(R1)           CLEAR HIGH ORDER BYTE
         LR    R2,R1              SAVE NEW SBA OR END-OF-BUFFER
         SR    R1,BUFF            LENGTH OF DATA
         BCTR  R1,0               LENGTH OF DATA-1
         EX    R1,MOVFIELD        MOVE BUFF DATA TO SCREEN FIELD
         EX    R1,UPPERCSE        GO SET ALL ALPHA TO UPPER CASE
         LR    BUFF,R2            NEW SBA OR END-OF-BUFFER
         SR    BUFFLEN,R1         NEW BUFFER LENGTH
         BCTR  BUFFLEN,0          ADJUST
         LA    SCRN,6(R1,SCRN)    LAST BYTE MOVED + 1
DATALOOP DS    0H
         CR    SCRN,SCRNEND       END OF SCREEN?????
         BH    SETSCRN            YES-GO LOOK AT NEXT BUFFER FIELD
         CLI   0(SCRN),SBA        NO-AT NEXT FIELDS SBA
         BE    SETSCRN            YES-GO LOOK AT NEXT BUFFER FIELD
         MVI   0(SCRN),X'40'      PAD END OF SCREEN FIELD
         LA    SCRN,1(SCRN)       NEXT SCREEN BYTE
         B     DATALOOP           GO CHECK AGAIN FOR PADDING
         SPACE 3
SCRNSCAN DS    0H
         CR    SCRN,SCRNEND       END OF SCREEN
         BNL   SETSCRN            YES-GO PROCESS NEXT BUFFER FIELD
         LA    SCRN,5(SCRN)       FIELD DATA
         SH    SCRNLEN,=H'5'      ADJUST
         LR    R1,SCRN            SET FOR TRANSLATE
EXSCRN   DS    0H
         LR    R14,SCRNLEN        SAVE SCRNLEN FOR TRANSLATE
         C     R14,=F'255'        SCRNLEN GREATER THAN MAX TRANSLATE
         BNH   EXSBASCR           NO-USE SCRNLEN FOR TRANSLATE
         LA    R14,255            YES-USE MAX TRANSLATE LENGTH
EXSBASCR DS    0H
         EX    R14,FINDSBA        FIND NEXT SBA IN SCREEN
         BNZ   SCRNSBA            FOUND IT-GO ADJUST SCRN POINTERS
         CR    R14,SCRNLEN        NO SBA FOUND- DID WE LOOK AT
*                                 ALL OF THE SCREEN....
         BNL   SETSCRN            YES WE DID-NEXT BUFFER FIELD
*                                 NO WE DIDN'T...
         SR    SCRNLEN,R14        TRY NEXT 256 OR LESS
         LA    SCRN,1(R14,SCRN)   BUMP SCREEN POINTER
         B     EXSCRN             GO LOOK AGAIN...
SCRNSBA  DS    0H
         LA    R1,0(R1)           CLEAR HIGH ORDER BYTE
         LR    SCRN,R1
         CLI   3(SCRN),X'13'      IS IT THE CURSOR FIELD
         BE    SETSCRN            YES-GO PROCESS NEXT BUFFER FIELD
         B     COMPADDR           GO BACK-CHECK FOR FIELD MATCH
         SPACE 3
CLRFIELD DS    0H
         CLC   ADDRESS,1(SCRN)    CHECK FOR FIELD MATCH
         BNE   CLRFLDSC           NO MATCH-FIND NEXT FIELD
         LA    SCRN,5(SCRN)       MATCH-START OF DATA???
         SH    SCRNLEN,=H'5'      ADJUST
CLRLOOP  DS    0H
         CR    SCRN,SCRNEND       END OF SCREEN
         BNL   EXITCLR            YES-GET OUT OF CLEAR FIELD LOGIC
         CLI   0(SCRN),SBA        IS THERE ANY DATA IN FIELD
         BE    EXITCLR            NO-GET OUT OF CLEAR FIELD LOGIC
         MVI   0(SCRN),X'40'      YES-CLEAR THIS BYTE
         LA    SCRN,1(SCRN)       NEXT BYTE
         BCTR  SCRNLEN,0          ADJUST
         B     CLRLOOP            GO CLEAR THIS BYTE??????
CLRFLDSC DS    0H
         CR    SCRN,SCRNEND       END OF SCREEN
         BNL   EXITCLR            YES-GET OUT CLEAR FIELD LOGIC
         LA    SCRN,5(SCRN)       NO-POINT TO FIELD DATA
         SH    SCRNLEN,=H'5'      ADJUST
         LR    R1,SCRN            LOAD FOR TRANSLATE SBA
EXCLR    DS    0H
         LR    R14,SCRNLEN        SAVE FOR TRANSLATE
         C     R14,=F'255'        IS SCRNLEN GREATER THAN MAX TRANSLT
         BNH   EXSBACLR           NO-US THIS LENGTH FOR TRANSLATE
         LA    R14,255            YES-SET LENGTH TO MAX
EXSBACLR DS    0H
         EX    R14,FINDSBA        GO FIND NEXT SBA
         BNZ   CLRSBA             FOUND IT-GO SET UP FOR RETRY
         CR    R14,SCRNLEN        NOT FOUND-AT END OF SCREEN
         BNL   EXITCLR            YES-GET OUT OF CLEAR FIELD LOGIC
         SR    SCRNLEN,R14        NO-ADJUST FOR ANOTHER TRANSLATE
         LA    SCRN,1(R14,SCRN)      ADJUST FOR ANOTHER TRANSLATE
         B     EXCLR              GO TRY TRANSLATE AGAIN
CLRSBA   DS    0H
         LA    R1,0(R1)           CLEAR HI-ORDER BYTE
         LR    SCRN,R1            NEW SBA
         CLI   3(SCRN),X'13'      ARE WE AT THE CURSOR FIELD(LAST)
         BE    EXITCLR            YES-GET OUT OF CLEAR FIELD LOGIC
         B     CLRFIELD           NO-GO LOOK FOR FIELD MATCH AGAIN
EXITCLR  DS    0H
         CR    BUFF,BUFFEND       END OF INPUT BUFFER
         BNL   EXITSCRN           YES-ALL THROUGH
         B     SETSCRN            NO-GO LOOK NEXT BUFFER FIELD
TGETERR  DS    0H
         MVC   PROCERR,=CL40'TGET ERROR'
         B     EXITSCRN
BUFFERR  DS    0H
         MVC   PROCERR,=CL40'NO SBA FOUND IN TGET BUFFER'
         B     EXITSCRN
SCRNERR  DS    0H
         MVC   PROCERR,=CL40'INVALID SCREEN IN GETSCRN LOGIC.'
         B     EXITSCRN
EXITSCRN DS    0H
         L     R13,SAVEGET+4      POINT TO CALLER SAVEAREA FOR RETURN
         RETURN (14,12) GO BACK TO CALLER
         EJECT
SBAMINUS DS    0H                 THIS ROUTINE SUBS 1 FROM 3270 ADDR
         ST    R15,SAVEREG        SAVE
         XR    R15,R15            CLEAR
         XR    R14,R14            CLEAR
         IC    R14,ADDRESS        FIRST 3270 ADDRESS BYTE
         N     R14,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         IC    R15,ADDRESS+1      SECOND 3270 ADDRESS BYTE
         N     R15,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         SLL   R14,6
         AR    R14,R15            RELATIVE BUFFER POSITION
         BCTR  R14,0              SUB 1 FROM RELATIVE POSITION
         SPACE 3
         XR    R15,R15            CLEAR
         SLL   R14,20
         SRDL  R14,26
         SRL   R15,26
         STC   R14,ADDRESS
         STC   R15,ADDRESS+1
         TR    ADDRESS,BUFTABLE
         L     R15,SAVEREG        RESTORE BEFORE EXITING
         BR    LINKREG
         EJECT
SBAPLUS  DS    0H                 THIS ROUTINE ADDS 1 TO 3270 ADDR
         XR    R15,R15            CLEAR
         XR    R14,R14            CLEAR
         IC    R14,ADDRESS        FIRST 3270 ADDRESS BYTE
         N     R14,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         IC    R15,ADDRESS+1      SECOND 3270 ADDRESS BYTE
         N     R15,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         SLL   R14,6
         AR    R14,R15            RELATIVE BUFFER POSITION
         LA    R14,1(R14)         ADD 1 TO RELATIVE POSITION
         SPACE 3
         XR    R15,R15            CLEAR
         SLL   R14,20
         SRDL  R14,26
         SRL   R15,26
         STC   R14,ADDRESS
         STC   R15,ADDRESS+1
         TR    ADDRESS,BUFTABLE
         BR    LINKREG
         LTORG
TGETASIS DC    X'81'              TGET ASIS OPTION BYTE
BUFTABLE DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F' 64 BYTE TRANSLATE
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F' TABLE FOR CONVERT
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F' TO 3270 LINE CODE
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'
         SPACE 3
SBATABLE DC    17X'00'  256 BYTE TRANSLATE TABLE TO FIND SBA(X'11)
         DC    X'11'    IN THE TGET INPUT BUFFER AND CORRESPONDING 3270
         DC    238X'00' SCREEN......
BLANKS   DC    CL256' '
         EJECT
         LTORG
         COPY VSAMAEQU            VSAMANAL EQUATES
         COPY VSAMAWRK            VSAMANAL COMMON WORKAREA
         END   VSAMAGET
./ ADD NAME=VSAMAHLP 0100-94187-94187-0738-00258-00258-00000-CART1
***********************************************************************
*                                                                     *
* THIS PROGRAM IS A SUB-MODULE OF VSAMANAL(TSO COMMAND PROCESSOR).    *
* IT FUNCTION IS TO DISPLAY HELP DATA TO VSAMANAL USERS(INITIATED BY  *
* PRESSING PF1 KEY). THE DATA DESCRIBES THE COMMAND PROCESSOR MENUS   *
* AND INPUT PARAMETERS..                                              *
*                                                                     *
* AUTHOR: ARTHUR J. CARTIER                                           *
* DATA:   12/06/78                                                    *
* OWNER:  STATE OF CONNECTICUT                                        *
*         CENTRAL DATA PROCESSING SERVICES                            *
*         TECHNICAL SUPPORT SECTION                                   *
*                                                                     *
* LINKAGE:                                                            *
*          LINKAGE IS BY THE OS LINK MACRO AND IS AS FOLLOWS;         *
*                       LA  R0,VSAMANALWORKAREA                       *
*                       LINK EP=VSAMAHLP                              *
***********************************************************************
         EJECT
         PRINT NOGEN
         EJECT
         OSINIT VSAMAHLP,(10,11,12),NO
         USING  WORKAREA,R2
         LR    R2,R0
         LA    R14,SAVEHLP OUR SAVE AREA
         ST    R14,8(R13)  FORWARD SAVE AREA CHAIN
         ST    R13,SAVEHLP+4  BACKWARD SAVE AREA CHAIN
         LR    R13,R14     OUR SAVE AREA AS CURRENT
         CLC   HLPTYPE,=CL3'DTL'  WAS CI DISPLAY HELP REQSTD
         BE    PAGE7              YES-DISPLAY HELP PAGE 7 ONLY
         CLC   HLPTYPE,=CL3'CIS'  WAS CI SELECTION HELP REQSTD
         BE    PAGE6              YES-DISPLAY HELP PAGE 6 ONLY
         USING MENUD,SCRNREG      NO- ALL HELP DATA WAS REQSTD
         LA    SCRNREG,MENU
         EJECT
PAGE0    DS    0H
         TPUT  CLEARBUF,CLEARLEN,FULLSCR
         LA    R1,DESC    HELP SCREEN NO. 0
         LA    R0,DESCL   LENGTH OF SCREEN
         ICM   R1,8,FULLSCR SET FULLSCRN OPTION
         TPUT  ((R1)),((R0)),R
PAGE0GET DS    0H
         LA    R1,DESC    HELP SCREEN
         LA    R0,DESCL   LENGTH OF SCREEN
         LINK  EP=VSAMAGET READ FROM TUBE
         CLI   KEYHIT,ENTER  SHOULD WE DISPLAY NEXT HELP PAGE
         BE    PAGE1
         CLI   KEYHIT,PF3    EXIT HELP FUNCTION????
         BE    RETURN        YES GET OUT
         CLI   KEYHIT,PF4    SPF PRIMARY OPTION MENU
         BE    RETURN
         CLI   KEYHIT,PF8    PAGE FORWARD???
         BE    PAGE1
         B     PAGE0GET      GO SEE WHAT TO DO NEXT
         EJECT
PAGE1    DS    0H
         TPUT  CLEARBUF,CLEARLEN,FULLSCR
         LA    R1,HELP1    HELP SCREEN NO. 1
         LA    R0,HELP1L   LENGTH OF SCREEN
         ICM   R1,8,FULLSCR SET FOR FULLSCREEN OPTION
         TPUT  ((R1)),((R0)),R
PAGE1GET DS    0H
         LA    R1,HELP1    HELP SCREEN
         LA    R0,HELP1L
         LINK  EP=VSAMAGET   READ FROM TUBE
         CLI   KEYHIT,ENTER  SHOULD WE DISPLAY NEXT HELP PAGE
         BE    PAGE2
         CLI   KEYHIT,PF3    EXIT HELP FUNCTION????
         BE    RETURN        YES GET OUT
         CLI   KEYHIT,PF4    SPF PRIMARY OPTION MENU
         BE    RETURN
         CLI   KEYHIT,PF7    PAGE BACK
         BE    PAGE0
         CLI   KEYHIT,PF8    PAGE FORWARD???
         BE    PAGE2
         B     PAGE1GET      GO SEE WHAT TO DO NEXT
         EJECT
PAGE2    DS    0H
         BAL   LINKREG,RESETATR GO RESET ATTRIBUTES TO ASKIP,MDTOFF
         BAL   LINKREG,DISMENU GO DISPLAY INPUT SELECTION MENU
         LA    R1,HELP2
         LA    R0,HELP2L
         ICM   R1,8,FULLSCR
         TPUT  ((R1)),((R0)),R
PAGE2GET DS    0H
         LA    R1,HELP2
         LA    R0,HELP2L
         LINK  EP=VSAMAGET
         CLI   KEYHIT,ENTER  SHOULD WE DISPLAY NEXT HELP PAGE
         BE    PAGE3
         CLI   KEYHIT,PF3    EXIT HELP FUNCTION????
         BE    RETURN        YES GET OUT
         CLI   KEYHIT,PF4    SPF PRIMARY OPTION MENU
         BE    RETURN
         CLI   KEYHIT,PF7    PAGE BACK
         BE    PAGE1
         CLI   KEYHIT,PF8    PAGE FORWARD
         BE    PAGE3
         B     PAGE2GET      GO SEE WHAT TO DO NEXT
         EJECT
PAGE3    DS    0H
         BAL   LINKREG,RESETATR GO RESET ATTRIBUTES TO ASKIP,MDTOFF
         BAL   LINKREG,DISMENU GO DISPLAY INPUT SELECTION MENU
         LA    R1,HELP3
         LA    R0,HELP3L
         ICM   R1,8,FULLSCR
         TPUT  ((R1)),((R0)),R
PAGE3GET DS    0H
         LA    R1,HELP3
         LA    R0,HELP3L
         LINK  EP=VSAMAGET
         CLI   KEYHIT,ENTER  SHOULD WE DISPLAY NEXT HELP PAGE
         BE    PAGE4
         CLI   KEYHIT,PF3    EXIT HELP FUNCTION????
         BE    RETURN        YES GET OUT
         CLI   KEYHIT,PF4    SPF PRIMARY OPTION MENU
         BE    RETURN
         CLI   KEYHIT,PF7    PAGE BACK
         BE    PAGE2
         CLI   KEYHIT,PF8    PAGE FORWARD
         BE    PAGE4
         B     PAGE3GET      GO SEE WHAT TO DO NEXT
         EJECT
PAGE4    DS    0H
         BAL   LINKREG,RESETATR GO RESET ATTRIBUTES TO ASKIP,MDTOFF
         BAL   LINKREG,DISMENU GO DISPLAY INPUT SELECTION MENU
         LA    R1,HELP4
         LA    R0,HELP4L
         ICM   R1,8,FULLSCR
         TPUT  ((R1)),((R0)),R
PAGE4GET DS    0H
         LA    R1,HELP4
         LA    R0,HELP4L
         LINK  EP=VSAMAGET
         CLI   KEYHIT,ENTER  SHOULD WE DISPLAY NEXT HELP PAGE
         BE    PAGE5
         CLI   KEYHIT,PF3    EXIT HELP FUNCTION????
         BE    RETURN        YES GET OUT
         CLI   KEYHIT,PF4    SPF PRIMARY OPTION MENU
         BE    RETURN
         CLI   KEYHIT,PF7    PAGE BACK
         BE    PAGE3
         CLI   KEYHIT,PF8    PAGE FORWARD
         BE    PAGE5
         B     PAGE4GET      GO SEE WHAT TO DO NEXT
         EJECT
PAGE5    DS    0H
         BAL   LINKREG,RESETATR GO RESET ATTRIBUTES TO ASKIP,MDTOFF
         BAL   LINKREG,DISMENU GO DISPLAY INPUT SELECTION MENU
         LA    R1,HELP5
         LA    R0,HELP5L
         ICM   R1,8,FULLSCR
         TPUT  ((R1)),((R0)),R
PAGE5GET DS    0H
         LA    R1,HELP5
         LA    R0,HELP5L
         LINK  EP=VSAMAGET
         CLI   KEYHIT,ENTER  WAS ENTER KEY HIT?
         BE    PAGE6         YES-DISPLAY NEXT HELP PAGE.
         CLI   KEYHIT,PF3    GET OUT OF HELP
         BE    RETURN
         CLI   KEYHIT,PF4    SPF PRIMARY OPTION MENU
         BE    RETURN
         CLI   KEYHIT,PF7    PAGE BACK
         BE    PAGE4
         CLI   KEYHIT,PF8    PAGE FORWARD
         BE    PAGE6
         B     PAGE5GET
PAGE6    DS    0H
         TPUT  CLEARBUF,CLEARLEN,FULLSCR
         LA    R1,HELP6   SCREEN ADDRESS
         LA    R0,HELP6L  SCREEN LENGTH
         ICM   R1,8,FULLSCR  OPTION BYTE
         TPUT  ((R1)),((R0)),R
PAGE6GET DS    0H
         LA    R1,HELP6   SCREEN ADDRESS
         LA    R0,HELP6L  SCREEN LENGTH
         LINK  EP=VSAMAGET GO READ FROM TUBE
         CLI   KEYHIT,PF3  EXIT HELP FUNCTION?
         BE    RETURN      YES-GET OUT
         CLI   KEYHIT,PF4  SPF PRIMARY OPTION MENU?
         BE    RETURN      YES-GET OUT
         CLC   HLPTYPE,=CL3'CIS'  CI SELECTION HELP ONLY REQSTD?
         BE    PAGE6GET    YES-GO SEE WHAT TO DO?
         CLI   KEYHIT,ENTER ENTER-NEXT HELP PAGE?
         BE    PAGE7        YES-GO DISPLAY IT
         CLI   KEYHIT,PF7   PAGE BACKWARD?
         BE    PAGE5        YES-GO DO IT
         CLI   KEYHIT,PF8   PAGE FORWARD?
         BE    PAGE7        YES-GO DO IT
         B     PAGE6GET     GO SEE WHAT TO DO AGAIN!
PAGE7    DS    0H
         TPUT  CLEARBUF,CLEARLEN,FULLSCR
         LA    R1,HELP7   SCREEN ADDRESS
         LA    R0,HELP7L  SCREEN LENGTH
         ICM   R1,8,FULLSCR  OPTION BYTE
         TPUT  ((R1)),((R0)),R
PAGE7GET DS    0H
         LA    R1,HELP7   SCREEN ADDRESS
         LA    R0,HELP7L  SCREEN LENGTH
         LINK  EP=VSAMAGET GO READ FROM TUBE
         CLI   KEYHIT,PF3  EXIT HELP FUNCTION?
         BE    RETURN      YES-GET OUT
         CLI   KEYHIT,PF4  SPF PRIMARY OPTION MENU?
         BE    RETURN      YES-GET OUT
         CLC   HLPTYPE,=CL3'DTL'  CI DISPLAY HELP ONLY REQSTD?
         BE    PAGE7GET    YES-GO SEE WHAT TO DO?
         CLI   KEYHIT,PF7   PAGE BACKWARD?
         BE    PAGE6        YES-GO DO IT
         B     PAGE7GET     GO SEE WHAT TO DO AGAIN!
DISMENU  DS    0H
         TPUT  CLEARBUF,CLEARLEN,FULLSCR
         LA    R1,MENU
         LA    R0,MENUL
         ICM   R1,8,FULLSCR
         TPUT  ((R1)),((R0)),R
         BR    LINKREG
         SPACE 3
RESETATR DS    0H
         MVC   RECAA,ASKIPOFF
         MVC   RECMA,ASKIPOFF
         MVC   RECLOADA,ASKIPOFF
         MVC   RECADDA,ASKIPOFF
         MVC   DEVTYPEA,ASKIPOFF
         MVC   KEYLA,ASKIPOFF
         MVC   CIPERA,ASKIPOFF
         MVC   IMBA,ASKIPOFF
         MVC   REPLA,ASKIPOFF
         BR    LINKREG
         SF    (ASKIP,MDTOFF)
         ORG   *-1
ASKIPOFF DS    0X
         ORG
         EJECT
RETURN   DS    0H
         MVC   HLPTYPE,=CL3' '  CLEAR BEFORE RETURNING
         L     R13,SAVEHLP+4
         RETURN (14,12)
         EJECT
         COPY  VSAMAEQU
FULLSCR  DC    X'03'
CLEARBUF DC    X'C2115D7F1140403C5D7F0011404013' *POSITIONAL*
CLEARLEN EQU   *-CLEARBUF                        *POSITIONAL*
         EJECT
         LTORG
         EJECT
         COPY  DESCSCRN
         COPY  HLP0SCRN
         COPY  HLP1SCRN
         COPY  HLP2SCRN
         COPY  HLP3SCRN
         COPY  HLP4SCRN
         COPY  HLP5SCRN
         COPY  HLP6SCRN
         COPY  HLP7SCRN
         EJECT
         COPY  VSAMAWRK
         END   VSAMAHLP
./ ADD NAME=VSAMANAL 0119-90278-12359-1501-01849-01807-00000-FOR3390
*
*        PDSDOC *VSAMANAL UPDATED FOR 3380 (AND 3390?)................* VSAMANAL
*
***********************************************************************
*                                                                     *
* THIS PROGRAM IS A TSO COMMAND PROCESSOR CALLED 'VSAMANAL'           *
*                                                                     *
* THE FUNCTION OF THIS COMMAND PROCESSOR IS TO DISPLAY TO THE USER    *
* THE AVAILABLE CONTROL INTERVAL SIZES FOR A VSAM DATASET HAVING THE  *
* CHARACTERISTICS DESCRIBED BY THE USER THROUGH THE FILLING IN OF     *
* A MENU SELECTION SCREEN.                                            *
* ALONG WITH THE AVAILABLE CONTROL INTERVAL SIZES OTHER               *
* CHARACTERISTICS PERTINENT TO A VSAM DATASET WILL BE DISPLAYED       *
* INCLUDING ESTIMATED SPACE REQUIREMENTS FOR THE DESCRIBED DATASET    *
*                                                                     *
* ALL COMMUNICATION WITH THE TSO USER IS THROUGH THE USE OF TPUT'S    *
* AND TGET'S.  THE FULLSCR OPTION IS USED FOR ALL TPUT'S AND THE      *
* BUFFERS USED ARE CREATED THROUGH THE CODING OF SPECIALIZED MACROS   *
* WHICH GENERATE ALL THE 3270 BUFFER CONTROL CHARACTERS....           *
*                                                                     *
* AUTHOR: ARTHUR J. CARTIER                                           *
* DATE:   10/10/78                                                    *
* OWNER:  STATE OF CONNECTICUT                                        *
*         CENTRAL DATA PROCESSING SERVICES                            *
*         TECHNICAL SUPPORT SECTION                                   *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* THE FOLLOWING SPECIAL MACROS ARE USED BY THIS MODULE:               *
*     BUF3270                                                         *
*     FIELD (NESTED MACROS= SBA,SF)                                   *
*                                                                     *
***********************************************************************
*        PRINT NOGEN
         PRINT ON,GEN              ASSEMBLER OPTIONS
         EJECT
         OSINIT  VSAMANAL,(3,10,11,12),NO
GETSTRG  GETMAIN R,                                                    X
               LV=WORKLEN+(DEVTABLN*DEVICES)+MENUL+HEADRL+(DATAL*4)
         USING WORKAREA,WAREAREG        ADDRESSABILITY  TO OUR WORKAREA
*INITIALIZE WORKAREA DSECT.......................................
         LR    R6,R1             SET FOR CLEAR TO LOW VALUES(X'00')
         LA    R7,WORKLEN        LENGTH OF WORKAREA FOR CLEAR
         XR    R8,R8                   ZERO FOR CLEAR
         XR    R9,R9                   ZERO FOR CLEAR
         MVCL  R6,R8           CLEAR WORKAREA TO BINARY ZEROS(X'00'S)
         ST    R1,8(R13)                SET FORWORD SAVEAREA CHAIN
         ST    R13,4(R1)                SET BACKWORD SAVEAREA CHAIN
         LR    WAREAREG,R1              WORKAREA ADDRESS
*INITIALIZE MENU SCREEN (3270 FULLSCR OUTPUT BUFFER).............
         ST    R6,AMENUD               SET WORKAREA MENU SCREEN POINTER
         LA    R7,MENUL                 LENGTH OF MVCL OPERAND 1
         ST    R7,LMENUD                SAVE LENGTH FOR OTHER MODS
         LR    R9,R7                    SAVE LENGTH FOR OPERAND 2
         LA    R8,MENU                  ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8                    MOVE MENU SCREEN TO WORKAREA
*INITIALIZE HEADR SCREEN (3270 FULLSCR OUTPUT BUFFER).............
         ST    R6,AHEADRD             SET WORKAREA HEADR SCREEN POINTER
         LA    R7,HEADRL                LENGTH OF MVCL OPERAND 1
         ST    R7,LHEADRD               SAVE LENGTH FOR OTHER MODS
         LR    R9,R7                    SAVE LENGTH FOR OPERAND 2
         LA    R8,HEADR                 ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8                    MOVE MENU SCREEN TO WORKAREA
INITPGE1 DS    0H
*INITIALIZE(PAGE1) DATA SCREEN (3270 FULLSCR OUTPUT BUFFER)...........
         ST    R6,DATA1PGE             SET WORKAREA DATA SCREEN(PAGE 1)
         LA    R7,DATAL                 LENGTH OF MVCL OPERAND 1
         ST    R7,LDATAD                SAVE LENGTH FOR OTHER MODS
         LR    R9,R7                    SAVE LENGTH FOR OPERAND 2
         LA    R8,DATA                  ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8                    MOVE MENU SCREEN TO WORKAREA
*INITIALIZE(PAGE2) DATA SCREEN (3270 FULLSCR OUTPUT BUFFER)...........
         ST    R6,DATA2PGE             SET WORKAREA DATA SCREEN(PAGE 1
         LA    R7,DATAL                 LENGTH OF MVCL OPERAND 1
         LR    R9,R7                    SAVE LENGTH FOR OPERAND 2
         LA    R8,DATA                  ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8                    MOVE MENU SCREEN TO WORKAREA
*INITIALIZE(PAGE3) DATA SCREEN (3270 FULLSCR OUTPUT BUFFER)...........
         ST    R6,DATA3PGE             SET WORKAREA DATA SCREEN(PAGE 1
         LA    R7,DATAL                 LENGTH OF MVCL OPERAND 1
         LR    R9,R7                    SAVE LENGTH FOR OPERAND 2
         LA    R8,DATA                  ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8                    MOVE MENU SCREEN TO WORKAREA
*INITIALIZE(PAGE4) DATA SCREEN (3270 FULLSCR OUTPUT BUFFER)...........
         ST    R6,DATA4PGE             SET WORKAREA DATA SCREEN(PAGE 4
         LA    R7,DATAL                 LENGTH OF MVCL OPERAND 1
         LR    R9,R7                    SAVE LENGTH FOR OPERAND 2
         LA    R8,DATA                  ADDR OF OPERAND 2 FOR MVCL
         MVCL  R6,R8                    MOVE MENU SCREEN TO WORKAREA
         EJECT
INITDEVC DS    0H
*INITIALIZE ALL DEVICE TABLES ...................................
*  EACH DEVICE TYPE SUPPORTED BY THIS PROGRAM HAS ITS OWN TABLE OF
*  CI SIZES AND OTHER DEVICE DEPENDENT DATA.
*  FOR RE-ENTRENCY PURPOSES THE DEVICE TABLES, WHICH ARE USED
*  FOR CALCULATIONS, WILL BE COPIES OF THE REAL DEVICE TABLES.
*  THE STORAGE FOR THESE TABLES(COPIES) WAS JUST GETMAINED SO NOW
*  THE WORKING DEVICE TABLES MUST BE INITIALIZED.
*  THIS IS ACCOMPLISHED THROUGH THE USE OF STATIC DEVICE LIST WHICH
*  CONTAINS THE NO. OF DEVICES TYPES SUPPORTED (DEVICE TABLES),
*  AND AND ENTRY FOR EACH DEVICE TYPE SUPPORTED,  WHICH CONSISTS
*  OF THE DEVICE TYPE AND THE ADDRESS OF THE REAL DEVICE TABLE.
*
*  THERE ALSO IS A WORK DEVICE LIST IN OUR COMMON WORKAREA THAT
*  CORRESPONDS TO THE STATIC DEVICE LIST.            .
*  THIS WORK LIST WILL BE INITIALIZED WITH THE ADDRESSES OF THE
*  WORKING DEVICE TABLES.
*
*  AFTER INITIALIZATION THE WORK DEVICE TABLES, THE WORK DEVICE
*  LIST POINTING TO THESE DEVICE TABLES WILL BE USED IN ALL
*  PROCESSING.
*
         MVC   DEVLSTWK,DEVLSTST    MAKE BOTH DEVICE LISTS THE SAME
         LA    R1,DEVLSTST    START OF STATIC DEVICE LIST
         LA    R2,DEVLSTWK    START OF WORKING DEVICE LIST
         LH    R15,0(R1)      NUMBER OF DEVICES IN DEVICE LIST
         LA    R1,2(R1)       FIRST DEVICE ENTRY IN STATIC LIST
         LA    R2,2(R2)       FIRST DEVICE ENTRY IN WORKING LIST
DEVTBLP  DS    0H             ENTRY POINT FOR DEVICE LIST LOOP
         ST    R6,4(R2)       SET WORKING LIST DEVICE TABLE ADDRESS
         LA    R7,DEVTABLN    LENGTH OF DEVICE TABLE FOR MVCL
         LR    R9,R7          SAVE LENGTH FOR OPERAND 2
         L     R8,4(R1)       ADDR OF STATIC LIST DEVICE ENTRY
         MVCL  R6,R8          MOVE DEVICE TABLE TO WORK COPY
         LA    R1,8(R1)       NEXT STATIC LIST ENTRY
         LA    R2,8(R2)       NEXT WORKING LIST ENTRY
         BCT   R15,DEVTBLP
         EJECT
PUTMENU  DS    0H
         L     SCRNREG,AMENUD  ESTABLISH ADDRESSABILITY FOR SCREEN
         USING MENUD,SCRNREG            ADDRESSABILITY FOR DSECT
         TPUT CLEARBUF,CLEARLEN,FULLSCR
         LR    R1,SCRNREG               LOAD FOR TPUT REGISTER FORMAT
         L     R0,LMENUD                LOAD FOR TPUT REGISTER FORMAT
         ICM   R1,8,FULLSCR             SET FOR FULLSCR OPTION
         TPUT ((1)),((0)),R
         LA    R1,MENUD                 ADDR OF MENU SCREEN IN WORKAREA
         L     R0,LMENUD                LENGTH OF MENU SCREEN
         LR    R2,WAREAREG              ADDR OF COMMON WORKAREA
         MVC   HLPTYPE,=CL3'ALL'        HELP TYPE REQUESTED
         LINK  EP=VSAMAGET              GO AND READ FROM TUBE
*                                       AND PROCESS INPUT
         OC    PROCERR,PROCERR          ANY PROCESSING ERRORS?
         BZ    CKMENUKY                 NO GO EDIT INPUT DATA
         MVC   ERROR,PROCERR            SET PROCESSING ERROR IN MENU
         B     PUTMENU
CKMENUKY DS    0H
         CLI   KEYHIT,CLEAR
         BE    RETURN
         CLI   KEYHIT,PF4               WAS PF4-HIT??
         BE    RETURN                   YES-GET OUT OF THIS PROGRAM
         CLI   KEYHIT,PF3               WAS PF3-HIT??
         BE    REINIT                   NO-GO CHECK PARMAMETERS
         CLI   KEYHIT,PF1               WAS HELP REQUESTED?
         BNE   PARM1
         LR    R0,WAREAREG              WORKAREA ADDRESS
         LINK  EP=VSAMAHLP
         CLI   KEYHIT,PF4               SPF PRIMARY OPTION MENU
         BE    RETURN
         B     PUTMENU
REINIT   DS    0H
         LR    R1,WAREAREG              SET FOR FREEMAIN
         L     R13,SAVE+4               RESET SAVE AREA POINTER
FREESTRG FREEMAIN R,A=(1),                                             X
               LV=WORKLEN+(DEVTABLN*DEVICES)+MENUL+HEADRL+(DATAL*3)
         B     GETSTRG                  GO GET ANOTHER PROGRAM WORKAREA
         EJECT
PARM1    DS    0H
         LA    R15,RECA
         LH    R14,=Y(RECAL)          LENGTH OF FIELD
         BAL   LINKREG,NUMERIC         GO SEE IF DATA IS NUMERIC
         B     BADRECSA                NOT NUMERIC
         CLC   RECA,BLANKS             WAS DATA KEYED
         BE    NORECA                  NO-ERROR PARAMETER NOT OPTIONAL
         BAL   LINKREG,CVB             YES-CONVERT DATA TO BINARY
         OR    R1,R1              WAS RECSIZE-AVG ZEROS????
         BZ    RECAZERO           YES-ERROR
         ST    R1,RECSZ                 STORE DATA
         C     R1,=F'32761'             IS IT GREATER THAN MAX ALLOWED
         BH    RECALARG                 YES - ERROR
         SPACE 3
PARM2    DS    0H
         LA    R15,RECM
         LH    R14,=Y(RECML)           LENGTH OF DATA 730
         BAL   LINKREG,NUMERIC         GO SEE IF DATA IS NUMERIC
         B     BADRECSM                NOT NUMERIC
         CLC   RECM,BLANKS             WAS DATA KEYED
         BNE   PARM2C                  YES-GO CONVERT IT TO BINARY
SAVAVG   DS    0H
         MVC   MAXRECSZ,RECSZ          NO-MAKE IT SAVE AS AVERAGE
         B     PARM3                   GO PROCESS NEXT PARAMETER
PARM2C   DS    0H
         BAL   LINKREG,CVB              CONVERT DATA TO BINARY
         ST    R1,MAXRECSZ              STORE DATA
         C     R1,=F'32761'             IS MAXIMUM GREATER THAN ALLOWED
         BH    RECMLARG                 YES-ERROR IN RECSIZE
         C     R1,RECSZ                 IS MAX GREATER THAN AVG
         BL    LORECM                   NO-MAX RECSZ ERROR(LOW)
         OR    R1,R1                    WAS ZEROS KEYED???
         BZ    SAVAVG                   YES-USE AVG-RECSIZE
         SPACE 3
PARM3    DS    0H
         CLC   RECLOAD,BLANKS          WAS DATA KEYED
         BE    NORECLD                 NO-ERROR PARAMETER NOT OPTIONAL
         LA    R15,RECLOAD
         LH    R14,=Y(RECLOADL)        LENGTH OF DATA FIELD910
         BAL   LINKREG,NUMERIC         GO SEE IF DATA IS NUMERIC
         B     BADRECLD                NOT NUMERIC
         BAL   LINKREG,CVB              CONVERT DATA TO BINARY
         OR    R1,R1              WAS RECLOAD ZEROS???????
         BZ    RECLZERO           YES-ERROR
         ST    R1,#RECLD                STORE DATA
         SPACE 3
PARM4    DS    0H
         CLC   RECADD,BLANKS            WAS PARAMETER KEYED
         BNE   CONTP4                   YES-GO PROCESS THIS PARAMETER
         XC    #RECADD,#RECADD          ZERO OUT PARAMETER
         B     PARM5
CONTP4   DS    0H
         LA    R15,RECADD
         LH    R14,=Y(RECADDL)          LENGTH OF DATA FIELD
         BAL   LINKREG,NUMERIC          GO SEE IF DATA IS NUMERIC
         B     BADRECAD                 NOT NUMERIC
         BAL   LINKREG,CVB              YES-CONVERT DATA TO BINARY
         ST    R1,#RECADD               STORE DATA
PARM5    DS    0H
         LA    R15,KEYL
         LH    R14,=Y(KEYLL)           LENGTH OF DATA FIELD
         BAL   LINKREG,NUMERIC          GO SEE IT DATA IS NUMERIC
         B     BADKLEN                  NOT NUMERIC
         CLC   KEYL,BLANKS              WAS PARAMETER KEYED
         BE    CHKRADD                  NO- GO SEE IF RADD KEYED
         BAL   LINKREG,CVB              YES- CONVERT DATA TO BINARY
         STH   R1,KEYLEN                STORE DATA
         CH    R1,=H'255'               IS KEYLEN GREATER THAN MAX
         BH    HIKLEN                   YES- ERROR IN KEY
         C     R1,MAXRECSZ              IS MAXRECSZ LESS THAN KEYLEN
         BH    BADMAXRC                 YES-ERROR IN KEYLEN OR MAXRSZ
         B     PARM6                    GO PROCESS NEXT PARAMETER
CHKRADD  DS    0H
         XC    KEYLEN,KEYLEN            ZERO OUT PARAMETER
         OC    #RECADD,#RECADD          WAS RADD PARAMETER KEYED
         BZ    PARM6                    NO-GO PROCESS NEXT PARAMETER
         L     R1,#RECADD
         A     R1,#RECLD               ADD RADD TO RLOAD
         ST    R1,#RECLD               SET NEW RECLOAD
         XC    #RECADD,#RECADD          CLEAR BECAUSE NOT KSDS
         SPACE 3
PARM6    DS    0H
         OC    #RECADD,#RECADD   #RECADD NOT ENTERED OR ESDS/RRDS?
         BZ    NOCICAPR          YES-GO SET CICA RATIOS TO ZERO?
         CLC   CIPER,BLANKS   WAS CIPERCENTAGE KEYED??
         BE    SETCIPD        NO-GO SET CI/CA RATIO DEFAULTS...
         LA    R15,CIPER      ADDRESS OF DATA
         LH    R14,=Y(CIPERL) LENGTH OF DATA
         BAL   LINKREG,NUMERIC IS DATA NUMERIC????
         B     BADCIPER       NOT NUMERIC
         BAL   LINKREG,CVB    NUMERIC-CONVERT IT TO BINARY
         C     R1,=F'100'     IS PERCENTAGE GREATER THAN 100
         BH    CIPERHI        YES-ERROR
         ST    R1,CIRATIO       SET CI FREESPACE RATIO(INSRT RATE)
         B     PARM6A
SETCIPD  DS    0H
         MVC   CIRATIO,=F'0'    CIRATIO  DEFAULT
         B     PARM6A
NOCICAPR DS    0H
         XC    CIRATIO,CIRATIO  ZERO
         XC    CARATIO,CARATIO  ZERO
         XC    XTRARECS,XTRARECS ZERO
         B     PARM7
         SPACE 3
PARM6A   DS    0H
         CLC   CAPER,BLANKS   WAS CAPERCENTAGE KEYED??
         BE    SETCAPD        NO-GO SET CA RATIO DEFAULTS...
         LA    R15,CAPER      ADDRESS OF DATA
         LH    R14,=Y(CAPERL) LENGTH OF DATA
         BAL   LINKREG,NUMERIC IS DATA NUMERIC????
         B     BADCAPER       NOT NUMERIC
         BAL   LINKREG,CVB    NUMERIC-CONVERT IT TO BINARY
         C     R1,=F'100'     IS PERCENTAGE GREATER THAN 100
         BH    CAPERHI        YES-ERROR
         A     R1,CIRATIO     ADD CI RATIO TO CA RATIO
         C     R1,=F'100'     ARE THEY COLLECTIVELY GREATER THAN 100
         BH    CICAPRHI       YES-ERROR
         S     R1,CIRATIO     NO- SUB CI RATIO
         ST    R1,CARATIO       SET CA FREESPACE RATIO(INSRT RATE)
         B     XTRAINSR       GO CI CA RATIOS ARE COLLECTIVELY LESS
*                             THAN 100 IF SO THEN GIVE USER FREE RECS
*                             AT THE END OF THE DATASET.....
SETCAPD  DS    0H
         MVC   CARATIO,=F'0'   CARATIO  DEFAULT
         B     XTRAINSR
         SPACE 3
XTRAINSR DS    0H
         OC    CIRATIO,CIRATIO    WAS CI% KEYED?
         BNZ   CONTXTRA           YES-CONT PROCESSING
         OC    CARATIO,CARATIO    WAS CA% KEYED?
         BZ    NULLRECA           NO-BOTH CI% AND CA% NOT KEYED.
CONTXTRA DS    0H
         XC    XTRARECS,XTRARECS  ZERO
         L     R14,CIRATIO
         A     R14,CARATIO
         LA    WORKREG,100
         SR    WORKREG,R14
         OR    WORKREG,WORKREG    DO CI CA RATIO EQUAL 100
         BZ    PARM7              YES-NO XTRA RECS AT END OF DATASET
* THE (ADD RECORD PERCENTAGES FOR CI AND CA FREESPACE CALCULATIONS DO
* NOT ADD UP TO 100, THEREFORE THE NUMBER OF ADD RECORDS NOT ACCOUNTED
* FOR IN THE TWO PERCENTAGES WILL BE CALCULATED TO FIT AT THE END OF
* THE DATASET AND NOT WITHIN CI-CA FREESPACE.
*
         XR    R14,R14            CLEAR
         L     R15,#RECADD        SET FOR MULTIPLY
         MR    R14,WORKREG        #RECADD X (100 - (CIRATIO + CARATIO))
         D     R14,=F'100'        FIND HOW MANY XTRA RECS AT END DSET
         ST    R15,XTRARECS       STORE XTRARECS VALUE FOR FUTURE CALC
* NOW THAT THE EXTRA RECORDS SLOTS TO BE LEFT AT THE END OF THE DATASET
* HAS BEEN CALCULATED THE CIRATIO AND CARATIO MUST BE RATIOED SO THEY
* ADD UP TO 100.
* THE CALCULATION FOR ADJUSTMENT IS:
*
*   CIRATIO = (CIRATIO * 100)  /  (CIRATIO + CARATIO)   (DON'T ROUND)
*
*   CARATIO = (CARATIO * 100)  /  (CIRATIO + CARATIO)   (ROUNDED UP)
*
*
         L     WORKREG,CIRATIO     SET UP DIVISOR
         A     WORKREG,CARATIO     WORKREG = (CIRATIO + CARATIO)
         XR    R14,R14             CLEAR FOR MULTIPLY
         L     R15,CIRATIO         SET UP FOR MULTIPLY
         MH    R15,=H'100'         MULTIPLY BY 100 FOR PERCENTAGE
         DR    R14,WORKREG         DIVIDE GIVING ADJUSTED PERCENTAGE
         ST    R15,CIRATIO         SAVE NEW PERCENTAGE
         XR    R14,R14             CLEAR FOR MULTIPLY
         L     R15,CARATIO         SET UP FOR MULTIPLY
         MH    R15,=H'100'         MULTIPLY BY 100 FOR PERCENTAGE
         DR    R14,WORKREG         DIVIDE GIVING ADJUSTED PERCENTAGE
         OR    R14,R14             CHECK IF ROUNDING NEEDED
         BZ    BYRATRND            NO - BYPASS ROUNDING UP
         LA    R15,1(R15)          ROUND UP
BYRATRND DS    0H
         ST    R15,CARATIO         SAVE NEW PERCENTAGE
         B     PARM7
         SPACE 3
NULLRECA DS    0H
         L     WORKREG,#RECLD     CI AND CA ADD REC %'S WERE NOT KEYED
         A     WORKREG,#RECADD    SO RECORDS T/B ADDED WILL BE ADDED TO
         ST    WORKREG,#RECLD     RECORDS T/B LOADED. AND THEN MADE 0
         XC    #RECADD,#RECADD
         B     PARM7
         SPACE 3
PARM7    DS    0H
         LA    R1,DEVLSTWK              START OF WORK DEVICE LIST
         XR    R14,R14
         ICM   R14,3,0(R1)              NO OF DEVICES IN LIST
         LA    R1,2(R1)                 FIRST ENTRY IN DEVICE LIST
DEVLOOP  DS    0H
         CLC   DEVTYPE,0(R1)            IS DEVICE IN LIST
         BE    SETDEVIC                 YES GO SETUP AS ACTIVE
         LA    R1,8(R1)                 NEXT ENTRY IN LIST
         BCT   R14,DEVLOOP              GO CHECK IT OUT
         B     BADDEV                   DEVICE TYPE NOT IN LIST
SETDEVIC DS    0H
         MVC   DEVICE,DEVTYPE           SET DEVICE AS KEYED
         MVC   DEVREGSV,4(R1)           SAVE WORK DEVICE TABLE ADDR
         B     PARM8
PARM8    DS    0H
         OC    IMB,BLANKS          MAKE UPPERCASE
         CLC   IMB,BLANKS          WAS IMBED KEYED
         BE    SETNOIMB            NO-SET TO NOIMBED
         CLC   IMB,=C'NO '         WAS NO KEYED
         BE    SETNOIMB            YES-GO SET
         CLC   IMB,=C'YES'         WAS YES KEYED
         BNE   BADIMB              NO-ERROR IS SPECIFICATION
         MVC   IMBFCTR,=H'1'       YES -SET IMBED FACTOR
         B     PARM9
SETNOIMB DS    0H
         XC    IMBFCTR,IMBFCTR     SET IMBED FACTOR TO ZERO
         B     PARM9
*
PARM9    DS    0H
         OC   REPL,BLANKS          MAKE UPPERCASE
         CLC  REPL,BLANKS          WAS REPLICATE KEYED
         BE    SETNOREP            NO-SET TO NOREPL
         CLC  REPL,=C'NO '         WAS NO KEYED
         BE    SETNOREP            YES-GO SET
         CLC  REPL,=C'YES'         WAS YES KEYED
         BNE   BADREPL             NO-ERROR IS SPECIFICATION
         MVC   REPLFCTR,=H'1'      YES -SET REPLICATE FACTOR
         B     RESTMENU       GO RESET MENU FIELDS
SETNOREP DS    0H
         XC    REPLFCTR,REPLFCTR   SET REPLICATE FACTOR TO ZERO
         B     RESTMENU
* CONVERT A PARAMETER TO BINARY
CVB      DS    0H
         XR    R1,R1               CLEAR
         LA    WORKREG,0(R14,R15)  END OF DATA + 1
         BCTR  WORKREG,0           END OF DATA
CVBCOMP  DS    0H
         CLI   0(WORKREG),C' '     IS THERE A BLANK
         BNE   CVBCONT             NO-CONTINUE
         BCTR  R14,0               YES- SUB 1 FROM DATALENGTH
         BCTR  WORKREG,0           BACKUP 1 BYTE
         CR    WORKREG,R15         ARE WE AT THE BEGINNING OF DATA
         BLR   LINKREG             YES-EXIT DATA IS ALL BLANK
         B     CVBCOMP        NO-GO FIND NON-BLANK
CVBCONT  DS    0H
         IC    R1,PACKPARM+1       SET TO INSTRUCTION LENGHTS
         AR    R1,R14              ADD LENGTH OF KEYED DATA (OPERAND 2)
         BCTR  R1,0                SUB 1 FOR EXECUTE  INSTRUCTION
         EX    R1,PACKPARM         DO VARIABLE PACK
         CVB   R1,DOUBLE           CONVERT PARM VALUE TO BINARY
         BR    LINKREG
PACKPARM PACK  DOUBLE,0(0,R15)     VARIABLE LENGTH PACK
         SPACE 3
NUMERIC  DS    0H
         BCTR  R14,0               SUB 1 FOR EXECUTE
         EX    R14,NUMTRT          GO CHECK FOR ALL NUMERICS OR BLANKS
         LA    R14,1(R14)          RESET TO DATALENGTH
         BNZR  LINKREG             GO TO ERROR BRANCH(NOT NUMERIC)
         B     4(LINKREG)          NUMERIC BRANCH
NUMTRT   TRT   0(0,R15),NUMTABLE
NUMTABLE DS    0CL256
         DC    CL64' '
         DC    X'00'      BLANK
         DC    CL175' '
         DC    10X'00'    0 THRU 9
         DC    CL6' '
RESTMENU DS    0H
* THIS ROUTINE WILL RESET ALL KEYABLE FIELDS IN THE MENU SCREEN
* TO ASKIP,MDTOFF BECAUSE NO FIELDS WERE IN ERROR.........
*
         MVC   RECAA,ASKIPOFF     RESET ATTRIBUTE
         MVC   RECMA,ASKIPOFF     RESET ATTRIBUTE
         MVC   RECLOADA,ASKIPOFF  RESET ATTRIBUTE
         MVC   RECADDA,ASKIPOFF   RESET ATTRIBUTE
         MVC   DEVTYPEA,ASKIPOFF  RESET ATTRIBUTE
         MVC   KEYLA,ASKIPOFF     RESET ATTRIBUTE
         MVC   CIPERA,ASKIPOFF    RESET ATTRIBUTE
         MVC   CAPERA,ASKIPOFF    RESET ATTRIBUTE
         MVC   IMBA,ASKIPOFF      RESET ATTRIBUTE
         MVC   REPLA,ASKIPOFF     RESET ATTRIBUTE
         MVC   ERRORA,ASKIPOFF    RESET ATTRIBUTE
         MVC   ERROR,MENU+ERROR-MENUD
         LR    R1,SCRNREG         MENU SCREEN ADDRESS
         L     R0,LMENUD          MENU SCREEN LENGTH
         ICM   R1,8,FULLSCR       TPUT,FULLSCR OPTION BYTE
         TPUT  ((R1)),((R0)),R
         B     CKDEVTYP           KEEP ON T
         SF    (ASKIP,MDTOFF,HI)
         ORG   *-1                LOCATION TO ATTRIBUTE
ASKIPOFF DS    0X                 SYMBOLIC REFERENCE TO ATTRIBUTE BYTE
         ORG
         EJECT
* INPUT ERROR ROUTINES...DESCRIPTIVE MESSAGE OF ERROR
* CONDITION WILL BE DISPLAYED ON TERMINAL........
*
*
BADKLEN  DS    0H
         MVC   ERROR,KLENMSG
         MVC   ADDRESS,KEYLS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADCIPER DS    0H
         MVC   ERROR,CIPMSG
         MVC   ADDRESS,CIPERS+1   3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
CIPERHI  DS    0H
         MVC   ERROR,CIPHIMSG
         MVC   ADDRESS,CIPERS+1   3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADCAPER DS    0H
         MVC   ERROR,CAPMSG
         MVC   ADDRESS,CAPERS+1   3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
CAPERHI  DS    0H
         MVC   ERROR,CAPHIMSG
         MVC   ADDRESS,CAPERS+1   3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
CICAPRHI DS    0H
         MVC   ERROR,CICAHMSG
         MVC   ADDRESS,CIPERS+1   3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
HIKLEN   DS    0H
         MVC   ERROR,HIKEYMSG
         MVC   ADDRESS,KEYLS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADMAXRC DS    0H
         MVC   ERROR,BADKYMSG
         MVC   ADDRESS,KEYLS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADDEV   DS    0H
         MVC   ERROR,DEVMSG
         MVC   ADDRESS,DEVTYPES+1 3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADRECSA DS    0H
         MVC   ERROR,RECAMSG
         MVC   ADDRESS,RECAS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
RECAZERO DS    0H
         MVC   ERROR,RECAZMSG
         MVC   ADDRESS,RECAS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADRECSM DS    0H
         MVC   ERROR,RECMMSG
         MVC   ADDRESS,RECMS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
RECMLARG DS    0H
         MVC   ERROR,RECMLMSG
         MVC   ADDRESS,RECMS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
RECALARG DS    0H
         MVC   ERROR,RECALMSG
         MVC   ADDRESS,RECAS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADIMB   DS    0H
         MVC   ERROR,IMBMSG
         MVC   ADDRESS,IMBS+1     3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADREPL  DS    0H
         MVC   ERROR,REPLMSG
         MVC   ADDRESS,REPLS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADRECLD DS    0H
         MVC   ERROR,RECLDMSG
         MVC   ADDRESS,RECLOADS+1 3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
BADRECAD DS    0H
         MVC   ERROR,RECADMSG
         MVC   ADDRESS,RECADDS+1  3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
NORECLD  DS    0H
         MVC   ERROR,NRECLMSG
         MVC   ADDRESS,RECLOADS+1 3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
RECLZERO DS    0H
         MVC   ERROR,RECLZMSG
         MVC   ADDRESS,RECLOADS+1 3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
NORECA   DS    0H
         MVC   ERROR,NRECAMSG
         MVC   ADDRESS,RECAS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         SPACE 3
LORECM   DS    0H
         MVC   ERROR,LORECMSG
         MVC   ADDRESS,RECMS+1    3270 ADDRESS OF FIELDS ATTR BYTE
         BAL   LINKREG,SBAPLUS    GO ADD 1 TO 3270 ADDRESS
         MVC   MENUX,ADDRESS      SET TO FIRST BYTE IN ERROR FIELD
         B     PUTMENU
         EJECT
SBAMINUS DS    0H                 THIS ROUTINE SUBS 1 FROM 3270 ADDR
         ST    R15,SAVEREG        SAVE
         XR    R15,R15            CLEAR
         XR    R14,R14            CLEAR
         IC    R14,ADDRESS        FIRST 3270 ADDRESS BYTE
         N     R14,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         IC    R15,ADDRESS+1      SECOND 3270 ADDRESS BYTE
         N     R15,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         SLL   R14,6
         AR    R14,R15            RELATIVE BUFFER POSITION
         BCTR  R14,0              SUB 1 FROM RELATIVE POSITION
         SPACE 3
         XR    R15,R15            CLEAR
         SLL   R14,20
         SRDL  R14,26
         SRL   R15,26
         STC   R14,ADDRESS
         STC   R15,ADDRESS+1
         TR    ADDRESS,BUFTABLE
         L     R15,SAVEREG        RESTORE BEFORE EXITING
         BR    LINKREG
         EJECT
SBAPLUS  DS    0H                 THIS ROUTINE ADDS 1 TO 3270 ADDR
         XR    R15,R15            CLEAR
         XR    R14,R14            CLEAR
         IC    R14,ADDRESS        FIRST 3270 ADDRESS BYTE
         N     R14,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         IC    R15,ADDRESS+1      SECOND 3270 ADDRESS BYTE
         N     R15,=X'0000003F'   TURN OFF UNUSED BITS 0-1
         SLL   R14,6
         AR    R14,R15            RELATIVE BUFFER POSITION
         LA    R14,1(R14)         ADD 1 TO RELATIVE POSITION
         SPACE 3
         XR    R15,R15            CLEAR
         SLL   R14,20
         SRDL  R14,26
         SRL   R15,26
         STC   R14,ADDRESS
         STC   R15,ADDRESS+1
         TR    ADDRESS,BUFTABLE
         BR    LINKREG
         EJECT
* THIS ROUTINE WILL SET UP THE CORRECT DEVICE TABLE POINTERS ACCORDING
* TO THE DEVICE TYPE SPECIFIED BY INPUT OR THE DEFAULT DEVICE TYPE..
*
*
CKDEVTYP DS    0H
         USING DEVENTRY,DEVREG     ADDRESSABILITY FOR DEVICE DSECT
         L     DEVREG,DEVREGSV     POINT TO BEGINNING OF DEVICE TABLE
         LA    DEVREG,DEVTABLN-4(DEVREG) BLOCK OVERHEAD VALUE
         MVC   BLKOVRHD,0(DEVREG)  BLOCK OVERHEAD IN BYTES
         SH    DEVREG,=H'4'        TRACK CAPACITY VALUE
         MVC   TRKCAP,0(DEVREG) MOVE IT
         SH    DEVREG,=H'2'   TRACKS PER CYLINDER VALUE
         LH    WORKREG,0(DEVREG)   GET TRACKS PER CYLINDER
         ST    WORKREG,TRKCYL      SAVE IT
         SH    DEVREG,=H'2'        DEVICE TYPE CODE
         MVC   DEVCODE,0(DEVREG)   MOVE IT
         B     NULLCISZ            GO NULLIFY DCISZ TABLE ENTRIES
         EJECT
* THIS ROUTINE WILL SCAN THE DATA CI SIZE FIELD IN EACH ENTRY OF THE
* DEVICE TABLE TO SEE IF THE DATA CI SIZE IS TO SMALL FOR THE
* MAXRECSZ+7.. IF SO THEN THAT DATA CI SIZE ENTRY WILL NULLED(ZEROS)
* SO THAT IT WILL NOT BE CONSIDERED WHEN FINDING THE BEST DATA CI SIZES
*
*
NULLCISZ DS    0H
         L     DEVREG,DEVREGSV         FIRST TABLE ENTRY
         L     R15,MAXRECSZ
         LA    R15,7(R15)              ADD 7 TO MAXRECSZ
NULLLOOP DS    0H
         CLC   DCISZ,=F'-1'            ARE WE AT THE END OF THE TABLE
         BE    PROCCISZ                YES- GO PROCESS DCISZ'S IN TABLE
         C     R15,DCISZ               IS MAXRECSZ+7 GREATER THAN DCISZ
         BNH   PROCCISZ                NO-GO PROCESS DCISZ'S IN TABLE
         XC    DCISZ,DCISZ             YES-NULLIFY THIS TABLE ENTRY
         LA    DEVREG,DEVENLEN(DEVREG) POINT TO THE NEXT TABLE ENTRY
         B     NULLLOOP                GO LOOK AGAIN
         EJECT
* THIS ROUTINE WILL PROCESS EACH DATA CI SIZE ENTRY IN THE DEVICE TABLE
* IN ORDER TO DETERMINE WHICH DATA CI SIZES ARE THE BEST CHOICES FOR
* THE INDICATED RECORDSIZES........
*
*
PROCCISZ DS    0H
         L     DEVREG,DEVREGSV     SET TO TOP OF DEVICE TABLE
DCISZP2  OC    DCISZ,DCISZ         DOES THIS DCISZ QUALIFY
         BZ    NXTDCISZ            NO -  GO BUMP TO NEXT ENTRY
         CLC   DCISZ,=F'-1'        IS IT THE END OF THE DEVICE TABLE
         BE    PUTHEADR            YES - GO DISPLAY THE INFO
         MVC   DCARATIO,CARATIO    MOVE PERCENTAGE OF ADD RECORDS
         MVC   DCIRATIO,CIRATIO    NEEDED IN CI-CA FREESPACE FOR EACH
*                                  DCISZ TABLE ENTRY
         BAL   LINKREG,CALC#0D     GO CALCULATE #TRKCA,#CICA,#RCINFSP,
*                                  #RCANFSP
         B     DCALC#4
DCALC#1  DS    0H
         XC    #CICA(DEVCLLEN),#CICA CLEAR FOR RE-CALCULATION
         BAL   LINKREG,CALC#1D     GO CALCULATE #CICA
         BAL   LINKREG,CALC#2D     GO CALCULATE #RCINFSP
         BAL   LINKREG,CALC#3D     GO CALCULATE #RCANFSP
DCALC#4  DS    0H
         BAL   LINKREG,CALC#4D     GO CALCULATE #TRKNFSP
         OC    #RECADD,#RECADD     ARE RECORDS GOING TO BE ADDED
         BZ    NOFREESP            NO- GO PROCESS NO FREESPACE LOGIC
* RECORDS TO BE ADDED WAS SPECIFIED(RECADD INPUT KEYWORD)
* SO WE MUST CALCULATE HOW MUCH FREESPACE WILL BE NEEDED TO ADD THE
* RECORDS SPECIFIED TO THE PRIMARY DATASET ALLOCATION
         BAL   LINKREG,CALC#5D     GO CALCULATE PERRADD
         BAL   LINKREG,CALC#6D     GO CALCULATE #FRCA
         BAL   LINKREG,CALC#7D     GO CALCULATE PERFSPCA
         BAL   LINKREG,CALC#8D     GO CALCULATE PERFSPC2
         BAL   LINKREG,CALC#9D     GO CALCULATE #RCILOAD
         BAL   LINKREG,CALC#10D    GO CALCULATE #RCALOAD
         BAL   LINKREG,CALC#11D    GO CALCULATE #TRKXTRA
         BAL   LINKREG,CALC#12D    GO CALCULATE #TRKDATA
         EJECT
CALCINDX DS    0H
         OC    KEYLEN,KEYLEN       IS THIS A KSDS/AIX?
         BZ    NXTDCISZ            NO-GO PROCESS NEXT DCISZ
* SET UP PARAMETER LIST TO LINK TO PL/1 INDEX CALCULATION ROUTINE.
* ALL PARAMETERS PASSED ARE FIXED BIN(15,0) OR (31,0).
         LA    WORKREG,KEYLEN
         ST    WORKREG,PLIPLIST       PARM. 1  FIXED BIN(15,0)
         LA    WORKREG,IMBFCTR
         ST    WORKREG,PLIPLIST+4*1   PARM. 2  FIXED BIN(15,0)
         LA    WORKREG,REPLFCTR
         ST    WORKREG,PLIPLIST+4*2   PARM. 3  FIXED BIN(15,0)
         LA    WORKREG,#CICA
         ST    WORKREG,PLIPLIST+4*3   PARM. 4  FIXED BIN(31,0)
         LA    WORKREG,#TRKDATA
         ST    WORKREG,PLIPLIST+4*4   PARM. 5  FIXED BIN(31,0)
         LA    WORKREG,#TRKCA
         ST    WORKREG,PLIPLIST+4*5   PARM. 6  FIXED BIN(31,0)
         LA    WORKREG,#TRKINDX
         ST    WORKREG,PLIPLIST+4*6   PARM. 7  FIXED BIN(31,0)
         LA    WORKREG,TRKCAP
         ST    WORKREG,PLIPLIST+4*7   PARM. 8  FIXED BIN(31,0)
         LA    WORKREG,BLKOVRHD
         ST    WORKREG,PLIPLIST+4*8   PARM. 9  FIXED BIN(31,0)
         LA    WORKREG,#INDXLV
         ST    WORKREG,PLIPLIST+4*9   PARM. 10 FIXED BIN(31,0)
         LA    WORKREG,ICISZ
         ST    WORKREG,PLIPLIST+4*10  PARM. 11 FIXED BIN(31,0)
         LA    WORKREG,#CIHIGHL
         ST    WORKREG,PLIPLIST+4*11  PARM. 12 FIXED BIN(31,0)
         MVI   PLIPLIST+4*11,X'80'    FLAG LAST PARM IN LIST
*         L     R15,=V(IHESAPB)        PLI ENTRY POINT(VSAMANDX)
         L     R15,=V(PLICALLA)       PLI ENTRY POINT(VSAMANDX)
         LA    R1,PLIPLIST            PARAMETER LIST
         BALR  R14,R15                GO DO INDEX CALCULATIONS
         OC    #INDXLV,#INDXLV        WAS INDEX CISZ TO LARGE
         BNZ   INDXSTAR               NO GO PROCESS NEXT DCISZ
* INDEX CISZ WAS TO COMPUTED AND FOUND TO BE LARGER THAN THE MAXIMUM
* ALLOWED (4096). SO NOW WE DECRESE THE SIZE OF THE DATA CONTROL
* AREA BY 1 TRK AND TRY OUR  CALCULATIONS AGAIN.
         L     WORKREG,#TRKCA         CONTROL AREA SIZE(IN TRACKS)
         SH    WORKREG,=H'1'          SUB 1 TRK
         BP    NEWTRKCA               IF NOT NEGATIVE OR ZERO GO
*                                     RETRY CALCULATIONS, OTHERWISE
         XC    DCISZ,DCISZ            NULLIFY THIS DCISZ(NOT ACCEPTED)
INDXSTAR DS    0H                     CHECK NUMBER OF INDEX TRACKS
         OC    BLKOVRHD,BLKOVRHD      DO WE HAVE A BLOCK OVERHEAD?
         BNZ   NXTDCISZ               NO, #TRKINDX IS VALID
         OC    #TRKINDX,#TRKINDX   INDEX TRACKS CALCULATED?
         BNZ   NXTDCISZ            YES, CARRY ON
         TRKCALC FUNCTN=TRKCAP,    FIND TRACK CAPACITY                 +
               TYPE=DEVCODE,       FOR THIS DEVICE                     +
               R=1,K=0,DD=ICISZ+2, WITH THIS INDEX                     +
               REGSAVE=YES,        SAVE THE REGISTERS                  +
               MF=(E,TRKCALST)     USING A REMOTE LIST
         LTR   R15,R15             GET AN ANSWER?
         BNZ   NXTDCISZ            NO, FORGET IT
         SR    EVENODD,EVENODD     CLEAR EVEN REGISTER
         LR    DIVISOR,R0          GET RECORDS PER TRACK
         L     DIVIDEND,#CIHIGHL   GET TOTAL NUMBER OF RECORDS
         DR    EVENODD,DIVISOR     CALCULATE TRACKS REQUIRED
         LTR   REMAINDR,REMAINDR   ANY REMAINDER?
         BZ    *+4+4               NO, NO ADJUSTMENT NEEDED
         LA    QUOTIENT,1(QUOTIENT) YES, CANNOT HAVE PART OF A TRACK
         ST    QUOTIENT,#TRKINDX   SAVE NUMBER OF INDEX TRACKS
         EJECT
NXTDCISZ DS    0H
         LA    DEVREG,DEVENLEN(DEVREG)  POINT TO NEXT TABLE ENTRY
         B     DCISZP2
NEWTRKCA DS    0H
         ST    WORKREG,#TRKCA         NEW TRK/CA VALUE
         B     DCALC#1                GO RECALCULATE
         SPACE 3
NOFREESP DS    0H
         MVC   #RCILOAD,#RCINFSP        SAME AS NO FREESPACE
         MVC   #RCALOAD,#RCANFSP        SAME AS NO FREESPACE
         MVC   #TRKDATA,#TRKNFSP        SAME AS NO FREESPACE
         B     CALCINDX                 GO CALC INDEX IF NECESSARY
         EJECT
CALC#0D  DS    0H
*
* THIS ROUTINE WILL CALCULATE THE NUMBER OF TRACKS PER CONTROL AREA
*
* THE VALUE IS DETERMINED BY THE SMALLER OF:
*
*        NUMBER OF TRACKS PER CYLINDER.
*
*                    OR
*
*        NUMBER OF TRACKS NEEDED TO CONTAIN (#RECLD + #RECADD).
*
*
* THIS CALCULATION WILL FORCE A CA SIZE OF 1 CYL IF (#RECLD + #RECADD)
* WILL NEED 1 CYLINDER OF MORE OF SPACE
*
*
*
* NOTE: IF (#RECLD+#RECADD) IS LESS THAN OR EQUAL TO THE #RCINFSP VALUE
* (NUMBER OF RECORDS THAT FIT IN A CI WITH NO FREESPACE)
* FOR THE CURRENT DCISZ TABLE ENTRY THEN THIS ENTRY(IF NOT THE FIRST)
* AND ALL SUBSEQUENT DCISZ TABLE
* ENTRIES WILL BE NULLED(ZEROS) AND THE PROCESSING OF THE DCISZ TABLE
* WILL BE STOPPED AND ALL VALID DCISZ'S WILL BE DISPLAYED TO THE USER
* FOR SELECTION.
*
         ST    LINKREG,DOUBLE   SAVE OUR LINK REGISTER
         BAL   LINKREG,CALC#1D      GO CALC #CICA
         BAL   LINKREG,CALC#2D      GO CALC #RCINFSP
         BAL   LINKREG,CALC#3D      GO CALC #RCANFSP
         L     WORKREG,#RECLD
         A     WORKREG,#RECADD
         C     WORKREG,#RCINFSP     WILL 1 CI HANDLE ALL RECORDS
         BNH   NULL#0D              YES - GO NULL THE REMAINING DCISZ'S
BYNULLCK DS    0H
         C     WORKREG,#RCANFSP     IS CASIZE(TRACKS) TO LARGE FOR
*                                   TOTAL RECORD SPACE NEEDED
         BNL   EXITC#0              NO - WERE ALL THRU HERE
         OC    DCIRATIO,DCIRATIO    IF TRKS NEEDED FOR DATASET IS LESS
         BNZ   EXITC#0              THAN TRKS PER CYL AND FREESPACE
         OC    DCARATIO,DCARATIO    WAS REQUEST (CI OR CA) THEN USE
         BNZ   EXITC#0              TRKS PER CYL.
         LH    R1,IMBFCTR           IMBED FACTOR (IF SPECIFIED)
*                                   REGISTER 1 NOW CONTAINS THE MINIMUM
*                                   ALLOWABLE CASIZE - 1
RE#TRKCA DS    0H                   OTHERWISE,
         L     R8,#TRKCA            WE MUST DECREASE THE CASIZE BY
         SH    R8,=H'1'             1 TRACK UNTIL WE GET A GOOD FIT.
         CR    R8,R1                ARE BELOW THE MIMIMUM CASIZE
         BE    MINTRKCA             IF IF SO GO MAKE IT THE MINIMUM
         ST    R8,#TRKCA            OTHERWISE SAVE NEW #TRKCA
         ST    WORKREG,DOUBLE+4     SAVE WORKREG
         BAL   LINKREG,CALC#1D      GO RE-CALC #CICA
         BAL   LINKREG,CALC#2D      GO RE-CALC #RCINFSP
         BAL   LINKREG,CALC#3D      GO RE-CALC #RCANFSP
         L     WORKREG,DOUBLE+4     RESTORE WORKREG
         C     WORKREG,#RCANFSP     IS CASIZE(TRACKS) STILL TO LARGE
         BL    RE#TRKCA             YES - GO MAKE IT SMALLER
         BE    EXITC#0              NO - ITS JUST WHERE WE WANT IT
*                                   NO - NOW ITS TO SMALL SO
         L     R8,#TRKCA            PUT IT BACK IN REGISTER
         LA    R8,1(R8)             INCREASE IT BY 1 TRACK
         ST    R8,#TRKCA            STORE FINAL VALUE AND DO
*                                   LAST RE-CALCULATION FOR:
         BAL   LINKREG,CALC#1D      #CICA
         BAL   LINKREG,CALC#2D      #RCINFSP
         BAL   LINKREG,CALC#3D      #RCANFSP
         B     EXITC#0              AND EXIT THIS ROUTINE
MINTRKCA DS    0H                   BECAUSE ITS NOW TO SMALL
         LA    R8,1(R8)             SO LETS INCREASE IT BY 1 TRACK
         ST    R8,#TRKCA            AND CALL IT A DAY
EXITC#0  DS    0H                   EXIT CALCULATION
         L     LINKREG,DOUBLE       RESTORE LINK REGISTER
         BR    LINKREG              AND GO BACK TO WHEREVER
NULL#0D  DS    0H
         ST    DEVREG,DOUBLE+4      SAVE CURRENT DCISZ ENTRY PTR
NULL#0DL DS    0H
         LA    DEVREG,DEVENLEN(DEVREG) POINT TO NEXT DCISZ ENTRY
         CLC   DCISZ,=F'-1'       IS IT THE END OF THE TABLE
         BE    NULL#0DE           YES - GO RESET DCISZ PTR
         XC    DCISZ,DCISZ        NULL THIS DCISZ ENTRY (TO LARGE)
         B     NULL#0DL           AND GO CHECK OUT THE NEXT ENTRY...
NULL#0DE DS    0H
         L     DEVREG,DOUBLE+4    RESTORE PTR TO LAST GOOD DCISZ ENTRY
         B     BYNULLCK           GO FINISH UP WITH THIS DCISZ
         EJECT
CALC#1D  DS    0H       ROUNDED DOWN
* #CICA= #TRKCA*#BLKSTRK/(DCISZ/PHYBLKSZ)
* THIS ROUTINE WILL CALCULATE THE NUMBER OF CT'S PER CA FOR THE CONTROL
* INTERVAL SIZE DEVICE TABLE ENTRY
         L     DIVIDEND,#TRKCA     NUMBER OF TRACKS PER CONTROL AREA
         SH    DIVIDEND,IMBFCTR    SUB IMBED FACTOR (IF ANY)
         M     EVENODD,#BLKSTRK    MULTIPLY BY THE NUMBER OF PHYSICAL
         LR    WORKREG,QUOTIENT    BLOCKS PER TRACK
         L     DIVIDEND,DCISZ      LOAD FOR DIVIDE
         XR    EVENODD,EVENODD     CLEAR
         L     DIVISOR,PHYBLKSZ    LOAD FOR DIVIDE
         DR    EVENODD,DIVISOR     DIVIDE DCISZ BY PHYSBLK SIZE
         LR    DIVISOR,QUOTIENT    USE RESULT FOR NEXT DIVIDE
         XR    EVENODD,EVENODD     CLEAR
         LR    DIVIDEND,WORKREG    NUMBER OF PHYSICAL BLOCKS PER CA
         DR    EVENODD,DIVISOR     DIVIDE PHYSBLKS/CA BY PHYSBLKS/CI
         ST    QUOTIENT,#CICA      GIVING #CICA(ROUNDED DOWN)
         BR    LINKREG             GO BACK
         SPACE 3
CALC#2D  DS    0H  ROUNDED DOWN                 ROUNDED DOWN
* #RCINFSP = (DCISZ-10)/(RECSZ) FIXED OR (DCISZ-4)/(RECSZ+3) VARIABL
* THIS ROUTINE WILL CALCULATE THE NUMBER OF RECORDS THAT CAN FIT IN A
* CONTROL INTERVAL WITH NO FREESPACE SPECIFIED
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,DCISZ      DATA CONTROL INTERVAL SIZE
         L     DIVISOR,RECSZ       AVERAGE RECORD SIZE
         CLC   RECSZ,MAXRECSZ      IS IT FIXED LENGTH RECORDS
         BNE   VAR#2D              NO - GO DO VARIABLE LENGTH LOGIC
* FIXED LENGTH RECORDS
         SH    DIVIDEND,=H'07'     SUB LENGTH OF CIDF+RDF FIELDS
         B     DOCALC#2
* VARIBLE LENGTH RECORDS
VAR#2D   DS    0H
         SH    DIVIDEND,=H'4'      SUB LENGTH OF CIDF FIELD
         LA    DIVISOR,3(DIVISOR)  ADD LENGTH OF RDF  FIELD
DOCALC#2 DS    0H
         DR    EVENODD,DIVISOR     DIVIDE DCISZ BY RECSZ
         ST    QUOTIENT,#RCINFSP   GIVING #RCINFSP(ROUNDED DOWN)
         BR    LINKREG
         EJECT
CALC#3D  DS    0H
*  #RCANFSP =  #RCINFSP * #CICA
* THIS ROUTINE WILL CALCULATE THE NUMBER OF RECORDS PER CONTROL AREA
* WITH NO FREESPACE
*
         L     DIVIDEND,#RCINFSP
         M     EVENODD,#CICA
         ST    QUOTIENT,#RCANFSP
         BR    LINKREG
         SPACE 3
CALC#4D  DS    0H ROUNDED UP (PUTS #TRKNFSP ON CA BOUNDARY)
*  #TRKNFSP =  #RECLD/#RCANFSP*#TRKCA
* THIS ROUTINE WILL CALCULATE THE NUMBER OF TRACKS NEEDED FOR THE DATA
* COMPONENT (NO FREESPACE)
         L     DIVISOR,#RCANFSP
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,#RECLD
         DR    EVENODD,DIVISOR
         OR    REMAINDR,REMAINDR   IS THIS THERE A REMINDER
         BZ    MULT#4D             NO-GO MULTIPLY
         LA    QUOTIENT,1(QUOTIENT) YES ADD 1 TO QUOTIENT
MULT#4D  M     EVENODD,#TRKCA      MULT BY TRACK PER CA
         ST    QUOTIENT,#TRKNFSP   GIVING #TRKNFSP
         BR    LINKREG
         SPACE 3
CALC#5D  DS    0H        ROUNDED UP
*  PERRADD =  (#RECADD-XTRARECS)/(#RECLD+#RECADD-XTRARECS)
* THIS ROUTINE CALCULATES THE INSERTION RATE USING THE RECADD FIELD
* OBTAINED FROM INPUT......
*
*
         XR    EVENODD,EVENODD
         L     DIVISOR,#RECLD
         L    DIVIDEND,#RECADD
         S    DIVIDEND,XTRARECS
         AR    DIVISOR,DIVIDEND ADD RECADD TO RECLOADED FOR INSRT RATE
         MH   DIVIDEND,=H'100'          MULT DIVIDEND BY 100
         DR    EVENODD,DIVISOR          YES- FIND PERCENTAGE
         OR    REMAINDR,REMAINDR        IS THERE A REMAINDER
         BZ    SETPERCT                 NO-GO SET PERCENTAGE
         LA    QUOTIENT,1(QUOTIENT)     YES-ROUND UP
SETPERCT DS    0H
         ST    QUOTIENT,PERRADD         SET PERCENTAGE
         BR    LINKREG
         EJECT
CALC#6D  DS    0H     ROUNDED DOWN
*  #FRCA =  #RCANFSP*PERRADD
*  THIS ROUTINE WILL DETERMINE THE NUMBER OF FREE RECORDS NEEDED PER
*  CONTROL AREA
*
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,#RCANFSP   NUMBER OF RECORDS PER CA
         M     EVENODD,PERRADD     MULT BY PERCENT OF RECADD TO RECLOAD
         LH    DIVISOR,=H'100'     SET FOR TRUE PERCENTAGE CALC
         DR    EVENODD,DIVISOR     DIVIDE BY 100
         OC    DCIRATIO,DCIRATIO   ANY CI FREESPACE NEEDED??
         BNZ   BYRND#6             YES - THAT WILL TAKE CARE OF ANY
*                                  RECORD FRACTIONS.
         OR    REMAINDR,REMAINDR   NO  -  CI FREESPACE WILL NOT BE
*                                  CALCULATED SO IF THERE IS A REMAINDR
*                                  (RECORD FRACTION) WE HAVE TO ROUND
*                                  UP FOR THIS CALCULATION.
         BZ    BYRND#6             NO FRACTION (DONT ROUND)
         LA    QUOTIENT,1(QUOTIENT) RECORD FRACTION (ROUND UP)
BYRND#6  DS    0H
         LTR   QUOTIENT,QUOTIENT   IS IT LESS THAN 1
         BZ    SETFRCA1            YES SET #FRCA TO 1
         C     QUOTIENT,#RCANFSP   ARE WE ASKING FOR 100% FREESPACE
         BL    NOSUB#6 NO THEN DONT SUB #CICA
         S     QUOTIENT,#CICA  SUB SO 1 REC/CI IS LOADED
NOSUB#6  DS    0H
         ST    QUOTIENT,#FRCA      SET #FRCA
         M     EVENODD,DCARATIO  NO. FREE RECORDS NEEDED FOR FREE CI'S
         D     EVENODD,=F'100'          TRUE VALUE
         ST    QUOTIENT,FRCAFSP
         L     EVENODD,#FRCA    FREE RECORDS NEEDED PER CA
         SR    EVENODD,QUOTIENT SUB FREE RECORDS NEEDED IN CAFREESPACE
         ST    EVENODD,FRCIFSP  SET NUMBER OF RECORDS NEED IN CIFREESP
         BR    LINKREG
SETFRCA1 DS    0H
         MVC   #FRCA,=F'1'         SET TO | (MINIMUM)
         BR    LINKREG
         EJECT
CALC#7D  DS   0H
* PERFSPCA  = (FRCAFSP/#RCINFSP)/#CICA
* THIS CALCULATION DETERMINES THE CA FREESPACE PERCENTAGE BY
* DETERMINING THE NUMBER OF FREE CI'S NEEDED TO CONTAIN ENOUGH
* RECORDS TO SATISFY THE CARATIO OF THE INSERTION RATE..
*
*
         OC    FRCAFSP,FRCAFSP          DO WE NEED CA FREESPACE
         BZR   LINKREG                  NO GET OUT
         XR    EVENODD,EVENODD          CLEAR
         L     DIVIDEND,FRCAFSP         RECS NEEDED IN CA FREESPACE
         D     EVENODD,#RCINFSP         NO OF FREE CI'S NEEDED
         OR    REMAINDR,REMAINDR        IS THERE A REMAINDER
         BZ    BYRND#71                 NO DONT ROUND
         LA    QUOTIENT,1(QUOTIENT)     YES ROUND UP
BYRND#71 DS    0H
         M     EVENODD,=F'100' FOR TRUE PERCENTAGE
         D     EVENODD,#CICA
         OR    REMAINDR,REMAINDR        IS THERE A REMAINDER
         BZ    BYRND#72                 NO DONT ROUND
         LA    QUOTIENT,1(QUOTIENT)     YES ROUND UP
BYRND#72 DS 0H
         ST    QUOTIENT,PERFSPCA        SET CA FREESPACE PERCENTAGE
         L     DIVIDEND,#CICA           NO OF CI'S PER CA
         M     EVENODD,PERFSPCA         FOR PERCENTAGE
         D     EVENODD,=F'100'          FIND FREE CI'S PER CA
         OR    QUOTIENT,QUOTIENT        IS RESULT ZERO
         BNZ   BYRND#73                 NO DONT ROUND
         LA    QUOTIENT,1(QUOTIENT)     ROUND UP
BYRND#73 DS    0H
         C     QUOTIENT,#CICA        ARE FREE CI'S EQUAL TO MAX CI/CA
         BNE   STORE#7
         BCTR  QUOTIENT,0             SUB 1(CANT BE SAME AS #CICA)
STORE#7  DS    0H
         ST    QUOTIENT,#FCICA          STORE IT
         M     EVENODD,#RCINFSP HOW MANY REC FIT IN FREE CI'S
         ST    QUOTIENT,#RCAFREE SAVE RECS IN FREE CI'S
         S     QUOTIENT,FRCAFSP  SUB FREE RECORDS NEEDED IF FREE CI'S
         BMR   LINKREG           LEAVE ZERO IF NEGATIVE
         ST    QUOTIENT,XRCCAFSP SAVE EXCESS FREE RECORDS IN FREE CI'S
         BR    LINKREG                  GET OUT
         EJECT
CALC#8D  DS    0H
         SPACE 3
********************************************************************
*                                                                  *
*              IF  FRCIFSP = 0 OR,                                 *
*                                                                  *
*              IF  FREE RECS NEEDED PER NON FREE CI IS GREATER OR  *
*                  EQUAL TO #RCINFSP                               *
*                                                                  *
* PERFSPCI = 0.                                                    *
*                                                                  *
*              IF  XRCCAFSP (IS GREATER THAN OR EQUAL TO) FRCIFSP, *
*                                                                  *
* PERFSPCI = FRCIFSP/B*(RECSZ+X)/DCISZ                             *
*                                                                  *
*              IF XRCCAFSP (IS LESS THAN) FRCIFSP,                 *
*                                                                  *
* PERFSPCI = ((FRCIFSP-A)/B))*(RECSZ+C)/DCISZ  ROUNDED UP          *
*                                                                  *
*                                                                  *
********************************************************************
*                                                                  *
*  WHERE:A=(XRCCAFSP/B) DONT ROUND---THE REMAINDER IS ZRCCAFSP     *
*                                                                  *
*        B=(#CICA-#FCICA)                                          *
*                                                                  *
*        C=0 FOR FIXED LENGTH RECORDS                              *
*                                                                  *
*        C=3 FOR VARIABLE LENGTH RECORDS                           *
*                                                                  *
*  AND  : ((FRCIFSP-A)/B)) IS ROUNDED UP IF                        *
*                          REMAINDER IS GREATER THAN ZRCCAFSP      *
*                                                                  *
*                                                                  *
*                                                                  *
*                                                                  *
*                                                                  *
********************************************************************
         OC    FRCIFSP,FRCIFSP          DO WE NEED ANY CI FREESPACE
         BZR   LINKREG                  NO-GET OUT
         CLC   XRCCAFSP,FRCIFSP         EXCESS RECS LESS THAN FRCIFSP
         BL    USEXCESS                 YES-USE EXCESS RECS IN CALC
         XC    XRCCAFSP,XRCCAFSP        ZERO OUT EXCESS FREE RECS
         B     CALC8P2                  BY PASS USING EXCESS FREE RECS
USEXCESS DS    0H
         XR    EVENODD,EVENODD          CLEAR FOR DIVIDE
         L     DIVIDEND,XRCCAFSP        WHAT WE ARE DIVIDING INTO
         L     DIVISOR,#CICA
         S     DIVISOR,#FCICA  DIVISOR=B  WHAT WE ARE DIVIDING BY;
*                                       NON-FREE CI'S IN A CA
         DR    EVENODD,DIVISOR          DIVIDE GIVING A
         ST    REMAINDR,ZRCCAFSP        SAVE NEW EXCESS FREE RECS
         ST    QUOTIENT,XRCCAFSP QUOTIENT=A -NO. OF EXCESS FREE RECS
*                                       PER NON-FREE CI IN A CA
CALC8P2  DS    0H
         L     DIVIDEND,FRCIFSP
         XR    EVENODD,EVENODD          CLEAR
         S     QUOTIENT,XRCCAFSP        SUB EXCESS
         DR    EVENODD,DIVISOR          FREE RECS NEEDED NON-FREE CI'S
         C     REMAINDR,ZRCCAFSP        IS REMAINDER GREATER THAN
*                                       THE NEW EXCESS VALUE
         BNH   BYRND#81                 NO-THEN DONT ROUND UP
         LA    QUOTIENT,1(QUOTIENT)     ROUND UP
BYRND#81 DS    0H
         C     QUOTIENT,#RCINFSP        ARE FREE RECS NEEDED GREATER
*                                       OR EQUAL TO MAX RECS-CI
         BNL   NOCIPR                   YES - GO NULL OUT CI FREESPACE
         L     DIVISOR,RECSZ  DIVISOR=C RECORD SIZE
         CLC   RECSZ,MAXRECSZ           CHECK FOR FIXED/VARIABLE
         BE    FIX#8                    FIXED LENGTH
         LA    DIVISOR,3(DIVISOR)       ADD 3 FOR VAR RECS
FIX#8    DS    0H
         MR    EVENODD,DIVISOR          BYTES NEEDED FOR FREE RECS/CI
         M     EVENODD,=F'100'          FOR PERCENTAGE
         L     DIVISOR,DCISZ            DATA CI SIZE
         DR    EVENODD,DIVISOR          PERCENT OF CI
         OR    REMAINDR,REMAINDR        DO WE HAVE A REMAINDER
         BZ    BYRND#82                 NO DONT ROUND
         LA    QUOTIENT,1(QUOTIENT)     YES ROUND UP
BYRND#82 DS    0H
         ST    QUOTIENT,PERFSPCI        SET CI FREESPACE PERCENTAGE
* NOW CALCULATE FREE RECS PER CI USING THE CI FREESPACE PERCENTAGE
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,DCISZ      SET FOR MULTIPLY
         M     EVENODD,PERFSPCI    NUMBER OF FREE BYTES PER CI * 100
         D     EVENODD,=F'100'     NUMBER OF FREE BYTES PER CI
         L     DIVISOR,RECSZ       SET UP FOR FINAL DIVIDE
         CLC   RECSZ,MAXRECSZ      FIXED OR VARIABLE LENGTH RECS
         BE    FIX#8B              FIXED
         LA    DIVISOR,3(DIVISOR)  VARIABLE
FIX#8B   DS    0H
         XR    EVENODD,EVENODD     CLEAR
         DR    EVENODD,DIVISOR     DIVIDE GIVING NO OF FREE RECS PER CI
         ST    QUOTIENT,#RCIFREE        SAVE FREE RECS-NON FREE CI'S
         BR    LINKREG
NOCIPR   DS    0H
         L     WORKREG,DCARATIO
         A     WORKREG,DCIRATIO    MAKE ALL RECS TO BE ADDED
         ST    WORKREG,DCARATIO    GO INTO CA FREESPACE
         XC    DCIRATIO,DCIRATIO   CLEAR OUT CIRATIO
         B     DCALC#1             GO DO IT ALL OVER FOR THIS DCISZ
         EJECT
CALC#9D  DS    0H
*
* THIS CALCULATION WILL COMPUTE THE NUMBER OF RECORDS THAT CAN FIT IN
* A CI AT LOAD TIME AND THE MAXIMUM NUMBER OF RECORDS THAT CAN FIT IN
* IN A CI TAKING INTO ACCOUNT THE POSSIBLE RECORD FRACTION LOST
* BECAUSE CI FREESPACE.
*
* #RCILOAD = (DCISZ*(1-PERFSPCI)-10)/(RECSZ+0)  FOR FIXED LENGTH RECS
*          = (DCISZ*(1-PERFSPCI)-4)/(RECSZ+3)   FOR VARIABLE LENGTH REC
*
*
* #RCIMAX = (DCISZ - (DCISZ * PERFSPCI / 100)) / RECSZ + #RCILOAD
*    OR
* #RCIMAX = (DCISZ - (DCISZ * PERFSPCI / 100)) / (RECSZ + 3) + #RCILOAD
*
*
         L     DIVIDEND,DCISZ
         LH    WORKREG,=H'100'     SET FOR (1-PERFSPCI)
         S     WORKREG,PERFSPCI    SUB CI FREESPACE PERCENTAGE
         BZ    RCIDFLT1            IF ZERO DEFAULT TO 1 REC-CI-LOAD
         MR    EVENODD,WORKREG     GET NUMBER OF CI BYTES*100 AVAIL LD
         XR    EVENODD,EVENODD     CLEAR
         D     EVENODD,=F'100'     GET NUMBER OF CI'S BYTES AVAIL AT LD
         SH    QUOTIENT,=H'07'     SUB CIDF AND RDF FIELDS
         BNP   CIPER100            IF NOT PLUS MAKE CI FREESPACE 100
         XR    EVENODD,EVENODD     CLEAR
         L     DIVISOR,RECSZ
         CLC   RECSZ,MAXRECSZ      IS IT FIXED LENGTH RECORDS
         BE    FIXCALC9            YES - DONT AD 3 BYTE RDF
         LA    DIVISOR,3(DIVISOR)  NO- VARIABLE SO ADD 3 BYTE RDF
         LA    QUOTIENT,3(QUOTIENT) NEGATE PREVIOUS RDF SUBTRACTION
FIXCALC9 DS    0H
         DR    EVENODD,DIVISOR     PERFORM FINAL CALC
         OR    QUOTIENT,QUOTIENT  IS IT LESS THAN ONE RECORD
         BNZ   STORE                    NO GO STORE IT
CIPER100 DS    0H
         MVC   PERFSPCI,=F'100'  CHANGE CI FREESPACE TO 100 PERCENT
RCIDFLT1 DS    0H
         MVC   #RCILOAD,=F'1'           SET REC PER CI TO MINIMUM
         BR    LINKREG                  GET THE HELL OUT
STORE    DS    0H
         ST    QUOTIENT,#RCILOAD
         BR    LINKREG
         EJECT
CALC#10D DS    0H
* #RCALOAD = #RCILOAD*(#CICA-#FCICA)
* THIS CALCULATION WILL COMPUTE   THE NUMBER OF RECORDS THAT CAN FIT
* IN A CONTROL AREA AT LOAD TIME
*
         XR    EVENODD,EVENODD     CLEAR
         L     WORKREG,#CICA       CI'S PER CA
         S     WORKREG,#FCICA      SUB FREE CI'S PER CA
         L     QUOTIENT,#RCILOAD
         MR    EVENODD,WORKREG     DETERMINE NUMBER OF REC/CA AT LOAD
         ST    QUOTIENT,#RCALOAD
         BR    LINKREG
         SPACE 3
CALC#11D DS    0H
* #TRKXTRA = XTRARECS/#RCANFSP*#TRKCA
* THIS CALCULATION WILL COMPUTE THE NUMBER OF TRACKS NEEDED FOR THE
* XTRA FREE RECS NEEDED AT END OF DATA COMPONENT
* (THAT IS THOSE FREE RECORDS NOT INCLUDED IN CI AND CA FREESPACE
* CALCULATIONS).
*
         OC    XTRARECS,XTRARECS
         BZR   LINKREG
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,XTRARECS
         L     DIVISOR,#RCANFSP
         DR    EVENODD,DIVISOR
         OR    REMAINDR,REMAINDR   IS THIS THERE A REMAINDER
         BZ    BYRND#11            NO - DONT ROUND
         LA    QUOTIENT,1(QUOTIENT) ROUND UP
BYRND#11 DS    0H
         L     WORKREG,#TRKCA
         MR    EVENODD,WORKREG
         ST    QUOTIENT,#TRKXTRA
         BR    LINKREG
CALC#12D DS    0H
*
* #TRKDATA = THE GREATER OF:
*
*       #RECLD /  #RCALOAD * #TRKCA + #TRKXTRA
*
*     OR
*
*                (#RECADD-XTRARECS)
*       -----------------------------------------   * #TRKCA
*       (#RCAFREE + (#RCIFREE * (#CICA - #FCICA)))
*
*
*
* THIS CALCULATION WILL COMPUTE THE NUMBER OF TRACKS NEEDED FOR THE
* DATA COMPONENT:
*
*
* START 1ST CALCULATION:
*
*       #RECLD /  #RCALOAD * #TRKCA + #TRKXTRA
*
         XR    EVENODD,EVENODD     CLEAR
         L     DIVIDEND,#RECLD
         L     DIVISOR,#RCALOAD
         DR    EVENODD,DIVISOR
         OR    REMAINDR,REMAINDR   IS THIS THERE A REMAINDER
         BZ    BYRND#12            NO - DONT ROUND
         LA    QUOTIENT,1(QUOTIENT) ROUND UP
BYRND#12 DS    0H
         L     WORKREG,#TRKCA
         MR    EVENODD,WORKREG
         A     QUOTIENT,#TRKXTRA
         ST    QUOTIENT,#TRKDATA
*
* START 2ND CALCULATION:
*
*                (#RECADD-XTRARECS)
*       -----------------------------------------   * #TRKCA
*       (#RCAFREE + (#RCIFREE * (#CICA - #FCICA)))
*
         XR    EVENODD,EVENODD     CLEAR
         L     WORKREG,#CICA
         S     WORKREG,#FCICA      NO. OF NON-FREE CI'S PER CA
         L     DIVIDEND,#RCIFREE   LOAD FOR MULTIPLY
         MR    EVENODD,WORKREG     NO. OF FREE RECS IN NON-FREE CI'S
*                                  IN A CA
         LR    DIVISOR,DIVIDEND    SET UP FOR DIVIDE
         A     DIVISOR,#RCAFREE    ADD FREE RECS IN FREE CI'S IN A CA
         L     DIVIDEND,#RECADD    SET UP FOR DIVIDE
         S     DIVIDEND,XTRARECS   SUB XTRARECS AT END OF DATASET
         DR    EVENODD,DIVISOR     FREE RECS NEEDED IN A CA
         OR    REMAINDR,REMAINDR   ROUND UP
         BZ    BYRND12B
         LA    QUOTIENT,1(QUOTIENT)
BYRND12B DS    0H
         XR    EVENODD,EVENODD     CLEAR
         M     EVENODD,#TRKCA      MULTIPLY BY TRACK PER CA
         C     QUOTIENT,#TRKDATA   CHECK IF GREATER THAN PREVIOUS CALC
         BNHR  LINKREG             IF LOW OR EQUAL USE PREVIOUS RESULT
         ST    QUOTIENT,#TRKDATA   IF HIGH USE 2ND CALC RESULT
         BR    LINKREG             GET OUT
         EJECT
PUTHEADR DS    0H
*
* THIS ROUTINE WILL DISPLAY THE HEADR SCREEN FOR THE DATACISZ
* SELECTION SCREEN...
*
*
*
         L     SCRNREG,AHEADRD     ADDRESS OF HEADR SCREEN
         USING HEADRD,SCRNREG      ADDRESSABILITY FOR SCREEN DSECT
         OC    KEYLEN,KEYLEN       IT THIS A KSDS??
         BZ    ESDSRRDS            NO-MUST BE AN ESDS OR RRDS
         MVC   FILETYP,=CL11'(KSDS)' YES-SET HEADR FILETYP
         B     TPUTHEAD            GO DISPLAY HEADR ON TUBE
ESDSRRDS DS    0H
         MVC   FILETYP,=CL11'(ESDS/RRDS)' SET HEADR FILETYP
TPUTHEAD DS    0H
         LR    R1,SCRNREG          BUFFER(SCREEN) ADDRESS
         L     R0,LHEADRD          BUFFER(SCREEN) LENGTH
         ICM   R1,8,FULLSCR        SET OPTION BYTE
         TPUT  ((R1)),((R0)),R
         L     R2,DEVREGSV         START OF DEVICE TABLE
         USING DEVENTRY,R2         ADDRESSABILITY FOR DEVENTRY DSECT
         LA    WORKREG,DATA1PGE    ADDR OF PAGE 1 SCREEN POINTER
         ST    WORKREG,FRSTPAGE    SAVE AS FIRST PAGE POINTER
         ST    WORKREG,CURPAGE     SAVE AS CURRENT PAGE POINTER
         L     SCRNREG,DATA1PGE    PAGE 1 SCREEN ADDR
         LA    PAGEREG,DATA1PGE    ADDR OF PAGE 1 SCREEN ADDR
         BAL   LINKREG,DPAGEBLD    GO BUILD 1ST DATACISZ PAGE
         L     SCRNREG,DATA2PGE    PAGE 2 SCREEN ADDR
         LA    PAGEREG,DATA2PGE    ADDR OF PAGE 1 SCREEN ADDR
         BAL   LINKREG,DPAGEBLD    GO BUILD 2ND DATACISZ PAGE
         L     SCRNREG,DATA3PGE    PAGE 3 SCREEN ADDR
         LA    PAGEREG,DATA3PGE    ADDR OF PAGE 3 SCREEN ADDR
         BAL   LINKREG,DPAGEBLD    GO BUILD 3RD DATACISZ PAGE
         L     SCRNREG,DATA4PGE    PAGE 4 SCREEN ADDR
         LA    PAGEREG,DATA4PGE    ADDR OF PAGE 4 SCREEN ADDR
         BAL   LINKREG,DPAGEBLD    GO BUILD 4TH DATACISZ PAGE
         B     PUTDPAGE            GO DISPLAY 1ST PAGE
         EJECT
DPAGEBLD DS    0H
         USING DATAD,SCRNREG       ADDRSABLTY TO DATA SCREEN DSECT
         LA    LINE,INFO1          1ST INFO LINE ON SCREEN
         LA    LINECNT,9           NUMBER OF INFO LINES ON SCREEN
DETLOOP  DS    0H
*                                  WAIT TO MOVE PERIOD SO WE CAN FIT
*                                  THE EDIT MASK FOR DCISZ
* CONVERT CONTROL INTERVAL SIZE
*
         OC    DCISZ,DCISZ        IS THIS DCISZ NULLED
         BZ    NXTD               YES-GO LOOK AT THE NEXT DCISZ
         L     WORKREG,DCISZ            CONTROL INTERVAL SIZE
         CVD   WORKREG,DOUBLE
         MVC   1(6,LINE),MASK6
         ED    1(6,LINE),DOUBLE+5
*
*
* CONVERT NUMBER OF TRACKS PER CONTROL AREA
*
         L    WORKREG,#TRKCA
         CVD   WORKREG,DOUBLE
         MVC   9(4,LINE),MASK4
         ED    9(4,LINE),DOUBLE+6
*
*
* CONVERT NUMBER OF CONTROL INTERVALS PER CONTROL AREA
*
         L    WORKREG,#CICA
         CVD   WORKREG,DOUBLE
         MVC  18(4,LINE),MASK4
         ED   18(4,LINE),DOUBLE+6
*
*
* CONVERT RECORDS PER CONTROL AREA (TO BE LOADED)
*
         L    WORKREG,#RCALOAD
         CVD   WORKREG,DOUBLE
         MVC  26(6,LINE),MASK6
         ED   26(6,LINE),DOUBLE+5
*
*
* CONVERT RECORDS PER CONTROL INTERVAL (TO BE LOADED)
*
         L    WORKREG,#RCILOAD
         CVD   WORKREG,DOUBLE
         MVC  35(6,LINE),MASK6
         ED   35(6,LINE),DOUBLE+5
*
*
* CONVERT                  FREESPACE PERCENTAGE
*
         L    WORKREG,PERFSPCI
         CVD   WORKREG,DOUBLE
         MVC  42(4,LINE),MASK4
         ED   42(4,LINE),DOUBLE+6
         L    WORKREG,PERFSPCA
         CVD   WORKREG,DOUBLE
         MVC  46(4,LINE),MASK4
         ED   46(4,LINE),DOUBLE+6
         MVI  46(LINE),C'-'
*
*
* CONVERT RECORDS PER CONTROL AREA WITH NO FREESPACE
*
         L     WORKREG,#RCANFSP
         CVD   WORKREG,DOUBLE
         MVC   52(6,LINE),MASK6
         ED    52(6,LINE),DOUBLE+5
*
*
* CONVERT RECORDS PER CONTROL AREA WITH NO FREESPACE
*
         L     WORKREG,#RCINFSP
         CVD   WORKREG,DOUBLE
         MVC   61(6,LINE),MASK6
         ED    61(6,LINE),DOUBLE+5
*
* CONVERT TOTAL TRACKS NEEDED TO CYLS IF #TRKCA IS EQUAL TO TRKCYL
*
*
         CLC   #TRKCA,TRKCYL    IS #TRKCA EQUAL TO TRKCYL
         BNE   USET             NO- SPECIFIFY SPACE IN TRACKS
         XR    R14,R14          YES-GET SET TO CALC NO. OF CYLS
         L     R15,#TRKDATA
         L     R1,TRKCYL
         DR    R14,R1
         LR    WORKREG,R15
         MVI   76(LINE),C'C'
         B     CONVIT
USET     DS    0H
         L     WORKREG,#TRKDATA
         MVI   76(LINE),C'T'
CONVIT   DS    0H
         CVD   WORKREG,DOUBLE
         MVC   67(8,LINE),MASK8
         ED    67(8,LINE),DOUBLE+4
NXTDETAL DS    0H
         LA    LINE,INFO2-INFO1(LINE)  POINT TO NEXT INFO LINE
         LA    R2,DEVENLEN(R2)    NEXT DCISZ
         CLC   DCISZ,=F'-1'       IS IT THE END OF TABLE
         BE    SETLASTP           YES-GO SET LAST PAGE POINTER
         BCT   LINECNT,DETLOOP    GO PROCESS NEXT LINE
         BR    LINKREG            GO BACK TO CALLER
NXTD     DS    0H
         LA    R2,DEVENLEN(R2)    NEXT DCISZ
         CLC   DCISZ,=F'-1'       IS IT THE END OF TABLE
         BE    SETLASTP           YES-GO SET LAST PAGE POINTER
         B     DETLOOP            GO LOOK AT NEXT DCISZ
SETLASTP DS    0H
         ST    PAGEREG,LASTPAGE   SET THIS PAGE AS LAST PAGE POINTER
         B     PUTDPAGE
         EJECT
PUTDPAGE DS    0H
         L     WORKREG,CURPAGE    CURRENT PAGE POINTER
         L     R1,0(WORKREG)      PAGE(SCREEN) ADDRESS
         LA    R0,DATAL           LENGTH OF PAGE(SCREEN)
         ICM   R1,8,FULLSCR       OPTION BYTE
         TPUT  ((R1)),((R0)),R
         LA    R0,DATAL           SCREEN LENGTH
         L     R1,0(WORKREG)      SCREEN ADDRESS
         LR    R2,WAREAREG        COMMON WORKAREA ADDRESS
         LINK  EP=VSAMAGET        LINK TO MODULE FOR TGET/ASIS
*                                 AND INPUT BUFFER EDITING...
         OC    PROCERR,PROCERR    ANY ERRORS IN SCREEN PROCESSING..
         BNZ   PUTDPAGE           YES-GO TRY IT AGAIN.....
*
         CLI   KEYHIT,CLEAR       WAS CLEAR KEY PRESSED?
         BE    RETURN             YES-GET OUT THIS PROGRAM
         CLI   KEYHIT,PF3         WAS PF3 (LAST MENU) PRESSED?
         BE    RSETATTR           YES-RESET KEYABLE FIELDS(UP,MDTOFF)
         CLI   KEYHIT,PF4         WAS PF4-SPF PRIMARY OPTION MENU?
         BE    RETURN             YES-GET OUT OF THIS..
         CLI   KEYHIT,PF7         WAS PF7 (PAGE BACKWARD) HIT?
         BE    SELLOGIC           YES-GO SEE IF ANY DCISZ'S SELCTD
         CLI   KEYHIT,PF8         WAS PF8 (PAGE FORWARD) HIT?
         BE    SELLOGIC           YES-GO SEE IF ANY DCISZ'S SELCTD
         CLI   KEYHIT,ENTER       WAS ENTER(DETAIL INFO) HIT?
         BE    SELLOGIC           YES-GO SEE IF ANY DCISZ'S SELCTD
         CLI   KEYHIT,PF1         WAS HELP REQUESTED?
         BNE   PUTDPAGE           NO-GO REDISPLAY CURRENT PAGE.
         LR    R0,WAREAREG        WORKAREA ADDRESS
         MVC   HLPTYPE,=CL3'CIS'  INDICATE CI SELECTION HELP RQSTD
         LINK  EP=VSAMAHLP
         CLI   KEYHIT,PF4         WAS PF4-SPF PRIMARY OPTION MENU?
         BE    RETURN             YES-GET OUT OF THIS...
         TPUT  CLEARBUF,CLEARLEN,FULLSCR
         L     R1,AMENUD          MENU SCREEN ADDRESS
         L     R0,LMENUD          MENU SCREEN LENGTH
         ICM   R1,8,FULLSCR       FULLSCR OPTION
         TPUT  ((R1)),((R0)),R    REDISPLAY MENU SCREEN
         L     R1,AHEADRD         HEADER SCREEN ADDRESS
         L     R0,LHEADRD         HEADER SCREEN LENGTH
         ICM   R1,8,FULLSCR       FULLSCR OPTION
         TPUT  ((R1)),((R0)),R
         B     PUTDPAGE           GO REDISPLAY CURRENT DATA PAGE..
SELLOGIC DS    0H
*
* THIS NEXT ROUTINE WILL LOOK IF ANY DCISZ'S WERE SELECTED FOR
* ADDITIONAL INFORMATION AND SET THE SELECT INDICATOR FOR THOSE
* SELECTED.
*
         L     SCRNREG,0(WORKREG) SET UP SCREEN POINTER
         USING DATAD,SCRNREG      ADDRESSABILITY TO SCREEN DSECT
         LA    WORKREG,9          LOOP FACTOR(MAX 9 DCISZ'S PER PAGE)
         LA    R7,DATAS1          FIRST SELECT FIELD IN SCREEN
         SPACE 2
CHKSEL   DS    0H
         CLC   INFO1-DATAS1+1(6,R7),BLANKS  IS THIS SCREEN LINE BLANKS
         BE    CHKPFKEY                     YES-CONT PFKEY CHECKING
         LA    R15,INFO1-DATAS1+1(R7)  ADDR OF DCISZ(CHARACTER)
         LA    R14,6              LENGTH OF DCISZ
         ST    WORKREG,SAVEREG    TEMP SAVE
         BAL   LINKREG,CVB        GO CONVERT IT TO BINARY
         L     WORKREG,SAVEREG    RESTORE AFTER CVB ROUTINE
         USING DEVENTRY,DEVREG    ADDRESSABILITY TO DEVICE ENTRY DSECT
         L     DEVREG,DEVREGSV    START OF DEVICE TABLE
CHKSEL2  DS    0H
         CLM   R1,7,DCISZ+1       IS THE RIGHT DEVICE TABLE ENTRY
         BNE   SELP2              NO-GO CHECK NEXT DEVICE TABLE ENTRY
         CLI   0(R7),C'S'         YES-WAS THIS DCISZ SELECTED???
         BNE   DSELOFF            NO-TURN OFF SELECTION INDICATOR..
         MVI   DSELECT,X'01'      YES-INDICATE IT WAS SELECTED...
         B     SELP3              GO-PROCESS NEXT SCREEN ENTRY
DSELOFF  DS    0H
         MVI   DSELECT,X'00'      TURN OFF SELECTION INDICATOR
         B     SELP3              GO PROCESS NEXT SCREEN ENTRY
SELP2    DS    0H
         LA    DEVREG,DEVENLEN(DEVREG) NEXT DEVICE TABLE ENTRY
         CLC   DCISZ,=F'-1'       END OF DEVICE TABLE
         BE    CHKPFKEY           YES-CONT. PFKEY PROCESSING
         B     CHKSEL2
SELP3    DS    0H
         LA    R7,DATAS2-DATAS1(R7) NEXT SCREEN ENTRY
         BCT   WORKREG,CHKSEL     DO LOOP
* END OF DETAIL SELECTION LOGIC...............
CHKPFKEY DS    0H
         CLI   KEYHIT,PF7         WAS PF7 (PAGE BACKWARD) PRESSED?
         BE    DPAGBACK           YES-GO AND PAGE BACKWARD
         CLI   KEYHIT,PF8         WAS PF8 (PAGE FORWARD) PRESSED?
         BE    DPAGFRWD           YES-GO AND PAGE FORWARD
         CLI   KEYHIT,ENTER       WAS ENTER (DETAIL PHASE) PRESSED?
         BNE   PUTDPAGE           NO-GO DISPLAY CURRENT PAGE AGAIN..
         LR    R0,WAREAREG        COMMON WORKAREA ADDRESS
         LINK  EP=VSAMADTL        GO TO DETAIL PHASE PROCESSING
         CLI   KEYHIT,CLEAR       WAS CLEAR KEY PRESSED?
         BE    RETURN             YES-GET OUT OF THIS PROGRAM
         CLI   KEYHIT,PF3         WAS-PF3 (LAST DATA) PRESSED?
         BE    DPAGFRST           YES-GO REDISPLAY FIRST DATA SCREEN
         CLI   KEYHIT,PF4         WAS-PF4 (LAST MENU) PRESSED?
         BE    RETURN             YES-GO BACK TO SPF PRIMARY OPTION
         B     PUTDPAGE           NO-GO REDISPLAY CURRENT PAGE....
RSETATTR DS    0H
         L     SCRNREG,AMENUD     ADDR OF MENU SCREEN
         USING MENUD,SCRNREG      ADDRESSABILITY TO SCREEN DSECT
         MVC   RECAA,UPOFF        RESET ATTRIBUTE
         MVC   RECMA,UPOFF        RESET ATTRIBUTE
         MVC   RECLOADA,UPOFF     RESET ATTRIBUTE
         MVC   RECADDA,UPOFF      RESET ATTRIBUTE
         MVC   DEVTYPEA,UPOFF     RESET ATTRIBUTE
         MVC   KEYLA,UPOFF        RESET ATTRIBUTE
         MVC   CIPERA,UPOFF       RESET ATTRIBUTE
         MVC   CAPERA,UPOFF       RESET ATTRIBUTE
         MVC   IMBA,UPOFF         RESET ATTRIBUTE
         MVC   REPLA,UPOFF        RESET ATTRIBUTE
         MVC   MENUX,MENU+MENUX-MENUD  RESET CURSOR TO FIRST KEYABLE
         L     R6,DATA1PGE        SET FOR REINITIALIZATION OF PAGES
         B     INITPGE1           RENINIT(PAGES-DEV TABLES) + REDISPLAY
         SF    (UP,MDTOFF,HI)
         ORG   *-1
UPOFF    DS    0X
         ORG
         EJECT
DPAGBACK DS    0H
         L     PAGEREG,CURPAGE    ADDR OF CURRENT PAGE POINTER
         L     WORKREG,FRSTPAGE   ADDR OF FIRST PAGE POINTER
         CR    PAGEREG,WORKREG    CAN WE PAGE BACKWARDS
         BNH   PUTDPAGE           NO-THEN REDISPLAY CURRENT PAGE
         SH    PAGEREG,=H'4'      YES-BACK UP 1 PAGE POINTER
         ST    PAGEREG,CURPAGE    SET AS CURRENT PAGE
         B     PUTDPAGE           GO DISPLAY NEW PAGE ON TUBE
DPAGFRST DS    0H
         L     WORKREG,FRSTPAGE   ADDR OF FIRST PAGE POINTER
         ST    WORKREG,CURPAGE    SET AS CURRENT PAGE
         L     R1,AHEADRD         ADDRESS OF HEADER SCREEN
         LA    R0,HEADRL          LENGTH OF HEADER SCREEN
         ICM   R1,8,FULLSCR       SET OPTION BYTE
         TPUT  ((R1)),((R0)),R
         B     PUTDPAGE           GO DISPLAY NEW PAGE ON TUBE
         SPACE 3
DPAGFRWD DS    0H
         L     PAGEREG,CURPAGE    ADDR OF CURRENT PAGE POINTER
         L     WORKREG,LASTPAGE   ADDR OF FIRST PAGE POINTER
         CR    PAGEREG,WORKREG    CAN WE PAGE FORWARD??
         BNL   PUTDPAGE           NO-THEN REDISPLAY CURRENT PAGE
         LA    PAGEREG,4(PAGEREG) YES-FORWARD 1 PAGE POINTER
         ST    PAGEREG,CURPAGE    SET AS CURRENT PAGE
         B     PUTDPAGE           GO DISPLAY NEW PAGE ON TUBE
         EJECT
RETURN   DS    0H
         TPUT CLEARBUF,CLEARLEN,FULLSCR
         L     R13,SAVE+4
         RETURN (14,12),RC=0
         TITLE 'VSAM ANALYZER STATIC DEVICE TABLE LIST'
         CNOP  2,4       ALIGN LIST ON PROPER BOUNDARY
DEVLSTST DS    0H
         DC    Y(DEVICES)   NUMBER OF DEVICE TABLES IN LIST
         DC    CL4'3330',A(DEV3330)
         DC    CL4'3350',A(DEV3350)
         DC    CL4'3380',A(DEV3380)
         DC    CL4'3390',A(DEV3390)
*
*  THIS NEXT VALUE MUST BE CHANGED WHEN ANY DEVICE TABLES ARE ADDED
*  OR DELETED FROM THIS PROGRAM. IT INDICATES THE NUMBER OF DEVICES
*  SUPPORTED BY THIS PROGRAM.
*
*  WHEN ADDING ANY NEW DEVICES TO THIS PROGRAM A DEVICE TABLE MUST
*  BE CODED WITH THE DEVICE DEPENDENT DATA, AN ENTRY FOR IT PLACED IN
*  THE DEVICE LIST (DEVLSTST), AND THE NUMBER OF DEVICES AS INDICATED
*  BY THE SYMBOL (DEVICES) MUST BE INCREMENTED ACCORDINGLY.
*
*  ALSO THE VSAMAWRK MODULE LABEL=DEVLSTWK MUST ALSO BE INCREMENTED
*  TO REFLECT THE SIZE OF THE DEVICE LIST (DEVLSTST). A CHANGE TO
*  THIS MODULE WILL NECESSITATE ALL OTHER VSAMANAL SUB-MODULES TO
*  BE RE-ASSEMBLED.
*
DEVICES  EQU   4           NUMBER OF DEVICES TABLES
         EJECT
*
*   P R O G R A M   C O N S T A N T S
*
MASK8    DC    XL8'4020202020202120'
MASK6    DC    XL6'402020202120'
MASK4    DC    XL4'40202120'
*
*
*
KLENMSG  DC CL40'KEYLENGTH(NONNUMERIC)'
*
CIPMSG   DC CL40'%-ADD RECS CI-FREESPACE(NONNUMERIC)'
*
CAPMSG   DC CL40'%-ADD RECS CA-FREESPACE(NONNUMERIC)'
*
DEVMSG   DC CL40'DEVICE TYPE NOT ''3390''/''3380'''
*
RECAMSG  DC CL40'AVG-RECSIZE(NONNUMERIC)'
RECMMSG  DC CL40'MAX-RECSIZE(NONNUMERIC)'
*
CIPHIMSG DC CL40'%-ADD RECS CI-FREESPACE OVER 100'
*
CAPHIMSG DC CL40'%-ADD RECS CA-FREESPACE OVER 100'
*
CICAHMSG DC CL40'%-ADD RECS CI-CA COLLECTIVELY OVER 100'
*
IMBMSG   DC CL40'IMBED NOT ''NO''/''YES'''
*
REPLMSG  DC CL40'REPLICATE NOT ''NO''/''YES'''
*
RECLDMSG DC CL40'RECORDS T/B LOADED(NONNUMERIC)'
*
RECADMSG DC CL40'RECORDS T/B ADDED(NONNUMERIC)'
*
HIKEYMSG DC CL40'KEYLENGTH GREATER THAN 255'
BADKYMSG DC CL40'KEYLENGTH GREATER THAN MAX-RECSIZE'
*
NRECLMSG DC CL40'RECORDS T/B LOADED REQUIRED'
*
NRECAMSG DC CL40'AVG-RECSIZE REQUIRED'
*
LORECMSG DC CL40'MAX-RECSIZE LESS THAN AVG-RECSIZE'
RECAZMSG DC CL40'AVG-RECSIZE CANNOT BE 0'
*
RECLZMSG DC CL40'RECORDS T/B LOADED CANNOT BE 0'
*
RECMLMSG DC CL40'MAX-RECSIZE GREATER THAN 32761'
*
RECALMSG DC CL40'AVG-RECSIZE GREATER THAN 32761'
*
*
         SPACE 3
FULLSCR  DC    X'03'                    FULLSCRN OPTION BYTE
TGETASIS DC    X'81'                    ASIS OPTION BYTE
         SPACE 3
BUFTABLE DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F' 64 BYTE TRANSLATE
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F' TABLE FOR CONVERT
         DC    X'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F' TO 3270 LINE CODE
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'
         SPACE 3
SBATABLE DC    17X'00'  256 BYTE TRANSLATE TABLE TO FIND SBA(X'11)
         DC    X'11'    IN THE TGET INPUT BUFFER AND CORRESPONDING 3270
         DC    238X'00' SCREEN......
         SPACE 3
BLANKS   DC    CL200' '
CLEARBUF DC X'C2115D7F1140403C5D7F0011404013'  *POSITIONAL*
CLEARLEN EQU *-CLEARBUF                        *POSITIONAL*
         LTORG
         COPY  VSAMAEQU    SYMBOLIC EQUATES
         COPY  MENUSCRN    MENU SCREEN(3270 OUTPUT BUFFER)
         COPY  HEADSCRN    HEADER SCREEN(3270 OUTPUT BUFFER)
         COPY  DATASCRN    DATA SCREEN(3270 OUTPUT BUFFER)
         TITLE 'VSAM ANALYZER DEVICE TABLE FOR 3330S MOD 1 AND 2'
DEV3330  DS    0F
         DC    F'512',F'512',F'20',F'77',F'19',24F'0'
         DC    F'1024',F'1024',F'11',F'85',F'19',24F'0'
         DC    F'1536',F'512',F'20',F'77',F'19',24F'0'
         DC    F'2048',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'2560',F'512',F'20',F'77',F'19',24F'0'
         DC    F'3072',F'1024',F'11',F'85',F'19',24F'0'
         DC    F'3584',F'512',F'20',F'77',F'19',24F'0'
         DC    F'4096',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'4608',F'512',F'20',F'77',F'19',24F'0'
         DC    F'5120',F'1024',F'11',F'85',F'19',24F'0'
         DC    F'5632',F'512',F'20',F'77',F'19',24F'0'
         DC    F'6144',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'6656',F'512',F'20',F'77',F'19',24F'0'
         DC    F'7168',F'1024',F'11',F'85',F'19',24F'0'
         DC    F'7680',F'1536',F'7',F'82',F'19',24F'0'
         DC    F'8192',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'10240',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'12288',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'14336',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'16384',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'18432',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'20480',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'22528',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'24576',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'26624',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'28672',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'30720',F'2048',F'6',F'93',F'19',24F'0'
         DC    F'32768',F'4096',F'3',F'93',F'19',24F'0'
         DC    F'-1'                              END OF TABLE INDICATR
         DC    X'00'       DEVICE TYPE
         DC    X'00'       RESERVED
         DC    H'19'         CYLINDERS PER TRACK
         DC    F'13165'      TRACK CAPACITY IN BYTES
         DC    F'135'        BLOCK OVERHEAD(INTER BLOCK GAP)
DEVTABLN EQU   *-DEV3330
         TITLE 'VSAM ANALYZER DEVICE TABLE FOR 3350 DEVICE'
DEV3350  DS    0F
         DC    F'512',F'512',F'27',F'71',F'30',24F'0'
         DC    F'1024',F'1024',F'15',F'79',F'30',24F'0'
         DC    F'1536',F'512',F'27',F'71',F'30',24F'0'
         DC    F'2048',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'2560',F'512',F'27',F'71',F'30',24F'0'
         DC    F'3072',F'1024',F'15',F'79',F'30',24F'0'
         DC    F'3584',F'512',F'27',F'71',F'30',24F'0'
         DC    F'4096',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'4608',F'512',F'27',F'71',F'30',24F'0'
         DC    F'5120',F'1024',F'15',F'79',F'30',24F'0'
         DC    F'5632',F'512',F'27',F'71',F'30',24F'0'
         DC    F'6144',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'6656',F'512',F'27',F'71',F'30',24F'0'
         DC    F'7168',F'3584',F'5',F'93',F'30',24F'0'
         DC    F'7680',F'2560',F'7',F'93',F'30',24F'0'
         DC    F'8192',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'10240',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'12288',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'14336',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'16384',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'18432',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'20480',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'22528',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'24576',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'26624',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'28672',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'30720',F'2048',F'8',F'85',F'30',24F'0'
         DC    F'32768',F'4096',F'4',F'85',F'30',24F'0'
         DC    F'-1'                              END OF TABLE INDICATR
         DC    X'00'       DEVICE TYPE
         DC    X'00'       RESERVED
         DC    H'30'       CYLINDERS PER TRACK
         DC    F'19254'    TRACK CAPACITY IN BYTES
         DC    F'185'      BLOCK OVERHEAD(INTER BLOCK GAP)
         TITLE 'VSAM ANALYZER DEVICE TABLE FOR 3390 DEVICE'
DEV3390  DS    0F
         DC     F'512',F'512',F'49',F'45',F'15',24F'0'
         DC     F'1024',F'1024',F'33',F'63',F'15',24F'0'
         DC     F'1536',F'1536',F'26',F'71',F'15',24F'0'
         DC     F'2048',F'2048',F'20',F'78',F'15',24F'0'
         DC     F'2560',F'2560',F'17',F'81',F'15',24F'0'
         DC     F'3072',F'3072',F'15',F'84',F'15',24F'0'
         DC     F'3584',F'3584',F'13',F'87',F'15',24F'0'
         DC     F'4096',F'4096',F'12',F'88',F'15',24F'0'
         DC     F'4608',F'4608',F'10',F'89',F'15',24F'0'
         DC     F'5120',F'5120',F'9',F'91',F'15',24F'0'
         DC     F'5632',F'5632',F'9',F'91',F'15',24F'0'
         DC     F'6144',F'6144',F'8',F'92',F'15',24F'0'
         DC     F'6656',F'6656',F'7',F'93',F'15',24F'0'
         DC     F'7168',F'7168',F'7',F'93',F'15',24F'0'
         DC     F'7680',F'7680',F'6',F'94',F'15',24F'0'
         DC     F'8192',F'8192',F'6',F'94',F'15',24F'0'
         DC     F'10240',F'10240',F'5',F'95',F'15',24F'0'
         DC     F'12288',F'12288',F'4',F'97',F'15',24F'0'
         DC     F'14336',F'7168',F'7',F'93',F'15',24F'0'
         DC     F'16384',F'16384',F'3',F'98',F'15',24F'0'
         DC     F'18432',F'18432',F'3',F'98',F'15',24F'0'
         DC     F'20480',F'10240',F'5',F'95',F'15',24F'0'
         DC     F'22528',F'5632',F'9',F'91',F'15',24F'0'
         DC     F'24576',F'24576',F'2',F'99',F'15',24F'0'
         DC     F'26624',F'26624',F'2',F'99',F'15',24F'0'
         DC     F'28672',F'7168',F'7',F'93',F'15',24F'0'
         DC     F'30720',F'7680',F'6',F'94',F'15',24F'0'
         DC     F'32768',F'16384',F'3',F'98',F'15',24F'0'
         DC    F'-1'                              END OF TABLE INDICATR
         DC    X'0F'       DEVICE TYPE
         DC    X'00'       RESERVED
         DC    H'15'       CYLINDERS PER TRACK
         DC    F'56664'    TRACK CAPACITY IN BYTES
         DC    F'0'        BLOCK OVERHEAD(INTER BLOCK GAP)
         TITLE 'VSAM ANALYZER DEVICE TABLE FOR 3380 DEVICE'
DEV3380  DS    0F
         DC     F'512',F'512',F'46',F'49',F'15',24F'0'
         DC     F'1024',F'1024',F'31',F'66',F'15',24F'0'
         DC     F'1536',F'1536',F'23',F'74',F'15',24F'0'
         DC     F'2048',F'2048',F'18',F'77',F'15',24F'0'
         DC     F'2560',F'2560',F'15',F'80',F'15',24F'0'
         DC     F'3072',F'3072',F'13',F'83',F'15',24F'0'
         DC     F'3584',F'3584',F'11',F'82',F'15',24F'0'
         DC     F'4096',F'4096',F'10',F'85',F'15',24F'0'
         DC     F'4608',F'4608',F'9',F'86',F'15',24F'0'
         DC     F'5120',F'5120',F'8',F'85',F'15',24F'0'
         DC     F'5632',F'5632',F'7',F'82',F'15',24F'0'
         DC     F'6144',F'6144',F'7',F'90',F'15',24F'0'
         DC     F'6656',F'6656',F'6',F'83',F'15',24F'0'
         DC     F'7168',F'7168',F'6',F'90',F'15',24F'0'
         DC     F'7680',F'7680',F'5',F'80',F'15',24F'0'
         DC     F'8192',F'8192',F'5',F'85',F'15',24F'0'
         DC     F'10240',F'10240',F'4',F'85',F'15',24F'0'
         DC     F'12288',F'6144',F'7',F'90',F'15',24F'0'
         DC     F'14336',F'14336',F'3',F'90',F'15',24F'0'
         DC     F'16384',F'8192',F'5',F'85',F'15',24F'0'
         DC     F'18432',F'6144',F'7',F'90',F'15',24F'0'
         DC     F'20480',F'20480',F'2',F'85',F'15',24F'0'
         DC     F'22528',F'22528',F'2',F'94',F'15',24F'0'
         DC     F'24576',F'6144',F'7',F'90',F'15',24F'0'
         DC     F'26624',F'6656',F'6',F'83',F'15',24F'0'
         DC     F'28672',F'14336',F'3',F'90',F'15',24F'0'
         DC     F'30720',F'6144',F'7',F'90',F'15',24F'0'
         DC     F'32768',F'8192',F'5',F'85',F'15',24F'0'
         DC    F'-1'                              END OF TABLE INDICATR
         DC    X'0E'       DEVICE TYPE
         DC    X'00'       RESERVED
         DC    H'15'       CYLINDERS PER TRACK
         DC    F'47968'    TRACK CAPACITY IN BYTES
         DC    F'0'        BLOCK OVERHEAD(INTER BLOCK GAP)
         COPY  VSAMAWRK    COMMON WORKAREA DSECT
         COPY  VSAMADEV    DEVICE TABLE ENTRY DSECT
         END
./ ADD NAME=VSAMANDX 0101-94221-94221-1502-00080-00080-00000-TSDDC
 /*                                                                 */
 /*      PDSDOC *VSAMANDX - CALCULATE INDEX PARAMETERS..............*/  VSAMANAL
 /*                                                                 */
 /* 09AUG93 DARNICK COMPUTING LTD. ALLOW UNLIMITED ICF INDEX CISIZE */
 /*                                                                 */
 /*                                                                 */
  VINDEX: PROC (KEYLEN,IMBFCTR,REPLFCTR,#CICA,#TRKDATA,#TRKCA,#TRKINDX,
                TRKCAP,BLKOVRHD,#INDXLV,ICISZ,#CIHIGHL) OPTIONS(MAIN);
          DCL (KEYLEN,IMBFCTR,REPLFCTR) FIXED BIN (15,0);
          DCL (#CICA,#TRKDATA,#TRKCA,#TRKINDX,TRKCAP,BLKOVRHD,#INDXLV,
               ICISZ,#CIHIGHL) FIXED BIN (31,0);
          DCL (CICAROOT,DROOT) FIXED BIN(31,14);
          DCL (A,B,C,D,X,Y,REMAINDR,MAXPTRS,#CILV2,#CILV3H,
              #CINXTLL,#ICITRK,#CISS,IBLKSZ,IWORK) FIXED BIN(31,0)
              INITIAL (0);
     CICAROOT = SQRT(#CICA);
            X = 31+2*(KEYLEN+2)+(3*#CICA);
            Y = 31+(#CICA*8)+(2*CICAROOT);
 /*  IF X > Y THEN ICISZ = X;                                       */
 /*           ELSE ICISZ = Y;                                       */
 /*                                                                 */
            ICISZ = ((#CICA * (KEYLEN / 2)) + 7 );
            DO IWORK = 512 TO 8192 BY 512;
               IF IWORK > ICISZ THEN GOTO FND#CISS;
            END;
            DO IWORK = 8192 BY 2048;
               IF IWORK > ICISZ THEN GOTO FND#CISS;
            END;
 /*                                                                 */
 FND#CISS:  ICISZ = IWORK;
            #CISS=#TRKDATA/#TRKCA;
             A = 0;
             B = 0;
             C = #CICA;
             D = C;
                     DO WHILE (A < ICISZ);
                         A = 31+2*(KEYLEN+2)+(3*C);
                         C = C+1;
                     END;
                     DO WHILE (B < ICISZ);
                         DROOT = SQRT(D);
                         B = 31+(D*8)+(2*DROOT);
                         D = D+1;
                     END;
                     IF C > D THEN MAXPTRS = D;
                              ELSE GO TO MAXISC;
                     IF B > ICISZ THEN MAXPTRS = MAXPTRS - 1;
                              GO TO CILEVELS;
             MAXISC: IF A > ICISZ THEN MAXPTRS = C - 1;
                              ELSE MAXPTRS = C;
 CILEVELS: #CILV2 = #CISS/MAXPTRS;
                REMAINDR = MOD(#CISS,MAXPTRS);
                IF REMAINDR = 0 THEN #CILV2 = #CILV2 + 1;
       #CINXTLL = #CILV2;
       #INDXLV  = 2;
                IF #CILV2 = 1 THEN GO TO CALC#CII;
                     DO WHILE (#CINXTLL = 1);
                         A = #CINXTLL/MAXPTRS;
                             REMAINDR = MOD(#CINXTLL,MAXPTRS);
                             IF REMAINDR = 0 THEN #CINXTLL = A+1;
                                              ELSE #CINXTLL = A;
                         #INDXLV = #INDXLV+1;
                         #CILV3H = #CILV3H+#CINXTLL;
                     END;
 CALC#CII: #CIHIGHL = #CILV2+#CILV3H;
           IF (IMBFCTR = 1 & REPLFCTR = 1) THEN GO TO IMBREPL;
           IF IMBFCTR = 1 THEN GO TO CALCITRK;
           IF REPLFCTR = 1 THEN #TRKINDX = #CIHIGHL + #CISS;
                           ELSE GO TO NOIMBREP;
           GO TO EXITINDX;
 IMBREPL:  #TRKINDX = #CIHIGHL;
           GO TO EXITINDX;
 NOIMBREP: #CIHIGHL = #CIHIGHL + #CISS;
 CALCITRK: IF BLKOVRHD = 0 THEN GO TO EXITINDX;
           IBLKSZ = ICISZ+BLKOVRHD;
           #ICITRK = TRKCAP/IBLKSZ;
           #TRKINDX = #CIHIGHL/#ICITRK;
                      REMAINDR = MOD(#CIHIGHL,#ICITRK);
                      IF REMAINDR = 0 THEN #TRKINDX = #TRKINDX+1;
 EXITINDX: END VINDEX;
./ ADD NAME=VSAMAWRK 0107-90278-94175-0738-00076-00071-00000-CART1
         EJECT
***********************************************************************
*                                                                     *
*  THIS IS THE COMMON WORKAREA DSECT FOR ALL VSAMANAL(TSO COMMAND     *
*  PROCESSOR) MODULES.....                                            *
*                                                                     *
WORKAREA DSECT
SAVE     DS    9D *POSITIONAL*
SAVEGET  DS    9D *POSITIONAL*     SAVE AREA FOR VSAMAGET SUB-MODULE
SAVEDTL  DS    9D *POSITIONAL*     SAVE AREA FOR VSAMADLT SUB-MODULE
SAVEHLP  DS    9D *POSITIONAL*     SAVE AREA FOR VSAMAHLP SUB-MODULE
SCRNLSV  DS    F                   LENGTH OF INPUT SCREEN
SCRNSAVE DS    F                   ADDRESS OF INPUT SCREEN
SAVEREG  DS    F                   MISC SAVE AREA
AMENUD   DS    F                   ADDRESS OF MENU SCREEN
LMENUD   DS    F                  LENGTH OF MENU SCREEN
AHEADRD  DS    F                  ADDRESS OF HEADR SCREEN
LHEADRD  DS    F                  LENGTH OF HEADR SCREEN
LDATAD   DS    F                  LENGTH OF DATA SCREEN
DATA1PGE DS    F  *POSITIONAL*    ADDRESS OF DATA SCREEN(PAGE1)
DATA2PGE DS    F  *POSITIONAL*    ADDRESS OF DATA SCREEN(PAGE2)
DATA3PGE DS    F  *POSITIONAL*    ADDRESS OF DATA SCREEN(PAGE3)
DATA4PGE DS    F  *POSITIONAL*    ADDRESS OF DATA SCREEN(PAGE4)
FRSTPAGE DS    F                  ADDR OF FIRST PAGE POINTER
LASTPAGE DS    F                  ADDR OF LAST PAGE POINTER
CURPAGE  DS    F                  ADDR OF CURRENT PAGE POINTER
ADETAIL  DS    F                  ADDR OF DETAIL SCREEN
LDETAIL  DS    F                  LENGTH OF DETAIL SCREEN
AINDEX   DS    F                  ADDR OF INDEX SCREEN
LINDEX   DS    F                  LENGTH OF INDEX SCREEN
PLIPLIST DS    12F                PARM LIST FOR VSAMANDX(PLI) CALL
         SPACE 3
***********************************************************************
* THE FOLLOWING VALUES ARE ENTERED AS INPUT TO THIS COMMAND
*
RECSZ    DC    F'0'                INPUT=RECA
MAXRECSZ DC    F'0'                INPUT=RECM
#RECLD   DC    F'0'                INPUT=RECLOAD
#RECADD  DC    F'0'                INPUT=RECADD
KEYLEN   DC    H'0'                INPUT=KEYL
PERRADD  DC    F'0'                COMPUTE: #RECADD / #RECLD
CIRATIO  DC    F'0'                INPUT=CIPER
CARATIO  DC    F'0'                INPUT=CAPER
XTRARECS DC    F'0'                COMPUTE:
*                                  #RECADD-(#RECADD-(CIRATIO+CARATIO))
DEVICE   DC    CL4' '              INPUT=DEVTYPE
IMBFCTR  DC    H'0'                INPUT=IMB 0=IF 'NO' OR NOT ENTERD
*                                            1=IF 'YES'
REPLFCTR DC    H'0'                INPUT=REPL 0=IF 'NO' OR NOT ENTERD
*                                             1=IF ENTERED
***********************************************************************
         SPACE 3
DOUBLE   DS    D
DEVLSTWK DS    CL34                ADDRESS OF WORK DEVICE LIST
DEVREGSV DS    F                   ADDRESS OF ACTIVE WORK DEVICE TABLE
TRKCAP   DS    F           TRACK CAPACITY IN BYTES(ACTIVE DEVICE TAB)
TRKCYL   DS    F           TRACKS/CYLINDER(CURRENT DEVICE TAB)
BLKOVRHD DS    F           SIZE OF INTER BLOCK GAP(ACTIVE DEVICE TAB)
KEYHIT   DS    CL1                    KEY PRESSED BY TERMINAL OPERATOR
CURPOS   DS    CL2                    CURSOR POSITION WHEN KEY PRESSED
HLPTYPE  DS    CL3                    TYPE OF HELP REQUESTED.
INBUFF   DS    CL500   TGET INPUT BUFFER(USED BY GETSCRN ROUTINE)
INBUFFL  EQU   *-INBUFF                 LENGTH OF INPUT BUFFER
ADDRESS  DS    CL2     3270 BUFFER ADDRESS FOR CONVERSION
PROCERR  DC    CL40' '           ERROR DESCRIPTION FROM GETSCRN ROUTINE
         SPACE 2
TRKCALST TRKCALC MF=L              TRKCALC MACRO PARAMETER LIST
DEVCODE  DS    XL1'00'             DEVICE TYPE CODE
         DS    XL7'00'             RESERVED
         DS    0D                  ALIGN FOR GETMAIN
WORKLEN  EQU   *-WORKAREA
*                                                                     *
*             E N D   O F   V S A M A N A L   C O M M O N             *
*                          W O R K A R E A                            *
*                                                                     *
***********************************************************************
