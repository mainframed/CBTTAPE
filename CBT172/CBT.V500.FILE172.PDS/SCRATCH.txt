SCRATCH  TITLE 'SUPER SCRATCH UTILITY FROM MVS UPDATE #85'
***********************************************************************
*        PDSDOC *SCRATCH WITHOUT ENQ..................................*
*                                                                     *
*        PROGRAM ID     :          SCRATCH                            *
*                                                                     *
*        AUTHOR         :          D.H.CARTWRIGHT                     *
*                                                                     *
*        LOCATION       :          SUTTON COLDFIELD                   *
*                                                                     *
*        HISTORY        :                                             *
*                                                                     *
* 28NOV96 DHC USED STANDARD CODE AS BASIS FOR 'SCRATCH' FROM XEPHON   *
*         'MVS UPDATE' NO. 85 OF OCT. 93.  ORIGINAL PROGRAM BY        *
*         MARTIN HAMILTON OF THOMSON TOUR OPERATIONS                  *
*                                                                     *
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        PURPOSE                                                      *
*        -------                                                      *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT ,
***********************************************************************
*                                                                     *
*        LIMITATIONS                                                  *
*        -----------                                                  *
*                                                                     *
* 1) MUST RUN AUTHORISED FROM AN AUTHORISED LIBRARY                   *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*        INPUT                                                        *
*        -----                                                        *
*                                                                     *
* 1) SCRATCH  SPECIFYING A DSNAME OF X. SOMETING                      *
*                                                                     *
***********************************************************************
         SPACE 1
***********************************************************************
*                                                                     *
*        OUTPUT                                                       *
*        ------                                                       *
*                                                                     *
* 1) THE RETURN CODE IS THE ONLY EXTERNAL OUTPUT FROM THE PROGRAM     *
*        0     REQUESTED FUNCTION COMPLETED                           *
*    100+X     ERROR RETURN OF X FROM CAMLST                          *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT ON,GEN              SHOW MACRO EXPANSIONS
*        PRINT ON,NOGEN            SUPPRESS MACRO EXPANSIONS
         SPACE 1
SCRATCH  CSECT ,
         SPACE 1
***********************************************************************
*        REGISTER EQUATES AND USAGES                                  *
***********************************************************************
         SPACE 1
R0       EQU   0
R1       EQU   1                   PARAMETER PASSING
R2       EQU   2                   WORK
R3       EQU   3                   .
R4       EQU   4                   SUB-ROUTINE LINKAGE
R5       EQU   5                   PARM POINTER
R6       EQU   6                   LENGTH REGISTER
R7       EQU   7                   TIOT POINTER
R8       EQU   8                   .
R9       EQU   9                   WORK
R10      EQU   10                  WORK
R11      EQU   11                  WORK
R12      EQU   12                  WORK
R13      EQU   13                  SAVE AREA POINTER AND BASE 1
R14      EQU   14                  SYSTEM LINKAGE RETURN
R15      EQU   15                  SYSTEM LINKAGE AND RETURN CODE
         SPACE 1
         PGMIN EQUATES=NO,USING=(R13) STANDARD HOUSEKEEPING
         L     R5,0(,R1)           GET ADDRESS OF PARM FIELD
         EJECT ,
***********************************************************************
*        PROGRAM MAIN LINE                                            *
***********************************************************************
         SPACE 1
         GETAUTH TYPE=GET          ENSURE WE ARE AUTHORISED
         GOSUB TIOTPROC            GO EXTRACT DDNAME=SCRATCH
         OI    SCRCAMLS+2,X'08'    BYPASS RACF PROTECTION AND ENQ
         MODESET MODE=SUP,KEY=ZERO GET INTO SUPERVISOR MODE, PK0
         SR    R0,R0               NO UCB SUPPLIED
         SCRATCH SCRCAMLS          DO THE SCRATCH
         LA    R7,100(,R15)        SAVE RETURN CODE
         MODESET MODE=PROB,KEY=NZERO RETURN TO OUR OWN PK
         STH   R7,RETCODE          SET PROGRAM RETURN CODE
         EJECT ,
***********************************************************************
*        PROGRAM TERMINATION                                          *
***********************************************************************
         SPACE 1
GOODEND  DS    0H                  PARM WAS AT LEAST VALID
         SPACE 2
PGMEND   DS    0H                  PROGRAM CAME TO SOME END
         PGMOUT RC=RETCODE         RETURN TO CALLER
         TITLE 'FUNCTIONAL SUB-ROUTINES'
***********************************************************************
*        TIOT PROCESSING ROUTINE                                      *
***********************************************************************
         SPACE 1
TIOTPROC SUBIN SAVE=(R4)           PROCESS THE TIOT ENTRY FOR 'SCRATCH'
         TESTIOT DDNAME=SCRDDNM    SEARCH FOR DD CARD
         B     *+4(R15)            TEST RETURN CODE
         B     *+4+4               SKIP ERROR IF OK
         B     NODDERR             ELSE SHOW THE FAILURE
         LR    R7,R1               COPY TIOT DD ENTRY POINTER
         USING TIOENTRY,R7         ESTABLISH ADDRESSABILITY
         SR    R6,R6               CLEAR LENGTH
         IC    R6,TIOELNGH         GET ENTRY LENGTH
         LA    R2,VOLIST+2         GET FIRST 'VOLIST' ENTRY
         SR    R3,R3               RESET VOLUME COUNT
         LA    R5,0(R6,R7)         POINT TO END OF DD ENTRY
         LA    R4,TIOESTTB         GET FIRST UCB ADDRESS
TPNXUCB  DS    0H                  GET ALL VOL ENTRIES
         ICM   R6,B'0111',TIOEFSRT-TIOESTTB(R4) GET UCB ADDRESS
         BZ    NODDERR             UNIT IS A DUMMY
         USING UCBOB,R6 ESTABLISH UCB ADDRESSABILITY
         MVC   0(4,R2),UCBTYP      MOVE DEVICE TYPE TO VOLIST
         MVC   4(6,R2),UCBVOLI     MOVE VOLSER TO VOLIST
         XC    4+6(2,R2),4+6(R2)   RESET STATUS_CODE
         LA    R4,4(,R4)           MOVE ON TO NEXT UCB POINTER
         LA    R2,4+6+2(,R2)       UPDATE VOLIST POINTER
         LA    R3,1(,R3)           INDICATE ANOTHER VOLUME
         CR    R4,R5               LAST UCB DONE?
         BL    TPNXUCB             NO - GO DO NEXT
         STH   R3,VOLIST           SAVE NUMBER OF VOLUMES
         RDJFCB (SCRDCB,)          GET DATASET NAME
         MVC   WTODSNM,JFCBDSNM    COPY DSNAME
         CLC   =C'X.',WTODSNM      PREFIXED BY 'X.'?
         BNE   NODDERR             NO, ERROR
         MVC   WTODSNM,WTODSNM+2   REMOVE X.PREFIX
         MVC   WTODSNM+L'WTODSNM-2(2),=CL2' ' AND BLANK END
         SPACE 1
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         DROP  R6                  END UCB ADDRESSABILITY
         DROP  R7                  END TIOT ADDRESSABILITY
         EJECT ,
         TITLE 'ERROR HANDLING'
ERROR4   DS    0H                  REDUNDANT REQUEST
         MVC   RETCODE,=H'4'       SET RETURN CODE
         B     GOODEND             GO TO EXIT
         SPACE 1
ERROR8   DS    0H                  JOBNAME NOT FOUND
         MVC   RETCODE,=H'8'       SET RETURN CODE
         B     GOODEND             GO TO EXIT
         SPACE 1
ERROR12  DS    0H                  JOBNAME SWAPPED OUT
         MVC   RETCODE,=H'12'      SET RETURN CODE
         B     GOODEND             GO TO EXIT
         SPACE 1
NODDERR  DS    0H                  MISSING DDNAME
         DC    A(0)                ABEND S0C1
ERROR16  DS    0H                  INVALID REQUEST
         MVC   RETCODE,=H'16'      SET RETURN CODE
         B     PGMEND              GO TO EXIT
         SPACE 1
         TITLE 'DATA AREAS'
***********************************************************************
*        CONSTANTS AND WORK AREAS                                     *
***********************************************************************
         SPACE 1
RETCODE  DC    H'0'                PROGRAM RETURN CODE
         SPACE 2
ERRWTOA  WTO   'SCRATCH - "SCRATCH" ERROR FOR DSN "12345678901234567890X
               123456789012345678901234", VOL "VVVVVV",',              X
               ROUTCDE=11
WTODSNM  EQU   ERRWTOA+8+35,44,C'C'  DSNAME
         SPACE 1
SCRCAMLS CAMLST                    SCRATCH,WTODSNM,,VOLIST,,OVRD
         SPACE 1
SCRDCB   DCB   DDNAME=SCRATCH,DSORG=PS,MACRF=E,EXLST=EXLST
EXLST    DC    0D'0',AL1(7+X'80'),AL3(INFMJFCB) EXIT LIST
         IEFJFCBN ,                AREA FOR READJFCB
         SPACE 3
SCRDDNM  DC    CL8'SCRATCH'        DDNAME TO LOOK FOR
VOLIST   DC    H'0',20XL(4+6+2)'00' SPACE FOR 20 VOLUMES
WORK     DC    F'0'                WORK AREA FOR CONVERT
HEXTAB   DC    C'0123456789ABCDEF' TRANSLATION TABLE
         SPACE 2
         LTORG ,                   LITERAL POOL
         EJECT ,
IEFTIOT  DSECT ,                   MAP THE TIOT
         IEFTIOT1 ,                MAP TASK I/O TABLE
         EJECT ,
UCB      DSECT ,                   MAP THE UCB
         IEFUCBOB ,                MAP UNIT CONTROL BLOCK
         EJECT ,
         IKJTCB SYS=AOS2,DSECT=YES,LIST=YES TCB MAPPING                 CB MAPPI
         EJECT ,
         IHAPSA ,                  PSA MAPPING                          CB MAPPI
         SPACE 3
         END   SCRATCH
