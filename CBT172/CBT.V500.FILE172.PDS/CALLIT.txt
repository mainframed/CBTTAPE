***********************************************************************
*        PDSDOC *DYNAMICALLY INVOKE ANOTHER PROGRAM...................* CALLIT
*                                                                     *
*        ON ENTRY:                                                    *
*              R15  -> CPB FOR REQUIRED PROGRAM                       *
*              R1 MAY POINT TO PARAMETERS TO BE PASSED TO THE PROGRAM *
*                                                                     *
*        ON EXIT:                                                     *
*              R15  =  RETURN CODE FROM PROGRAM, OR FROM THE LOAD     *
*                      IF IT FAILS                                    *
*                                                                     *
* 06FEB95 DC    CHANGED 'RIN' TO 'SUBIN'. NOT NOW RE-ENTRANT          *
* 21JUL92 CART1 ADDED 'UNCALL'                                        *
* 15JUL92 CART1 WROTE CODE.                                           *
*                                                                     *
***********************************************************************
         SPACE 2
CALLIT   DS    0H                  DYNAMICALLY CALL SUB-PROGRAM
         SUBIN SAVE=(R2,R4)        SUBROUTINE ENTRY
         LR    R4,R15              COPY CPB ADDRESS
         USING CPB,R4              ESTABLISH CPB ADDRESSABILITY
         ICM   R15,B'1111',CPBADDR GET ADDRESS OF SUB-PROGRAM
         BNZ   CALLIT24            HAVE ALREADY LOADED IT
         ST    R15,CPBCOUNT        IF NOT LOADED, RESET USE COUNT
         LR    R2,R1               COPY ANY PARAMETER ADDRESS
         LOAD  EPLOC=CPBNAME       LOCATE REQUIRED ROUTINE
         LTR   R15,R15             TEST RETURN
         BNZ   CALLITX             GIVE UP IF BAD
         ST    R0,CPBADDR          SAVE ENTRY POINT
         LR    R15,R0              AND COPY IT
         LR    R1,R2               REFRESH PARAMETER ADDRESS
         SPACE 1
CALLIT24 DS    0H                  INVOKE THE SUB-PROGRAM
         L     R2,CPBCOUNT         GET USE COUNT
         LA    R2,1(,R2)           COUNT ONE FOR THIS CALL
         ST    R2,CPBCOUNT         SAVE UPDATED COUNT
         TM    CPBADDR,X'80'       IS IT 31-BIT RESIDENT?
         BO    CALLIT31            YES, TAKE APPROPRIATE ACTION
         BALR  R14,R15             BRANCH INTO THE PROGRAM
         B     CALLITX             THEN RETURN TO CALLER
         SPACE 1
CALLIT31 DS    0H                  INVOKE THE SUB-PROGRAM IN XA MODE
         BASSM R14,R15             BRANCH INTO THE PROGRAM
         LA    R14,6(,R14)         UPDATE RETURN ADDRESS
         BSM   0,R14               AND RESTORE MODE
         SPACE 2
CALLITX  DS    0H                  END OF SUBROUTINE
         SUBOUT RESTORE=(R2,R4)    RETURN TO CALLER
         EJECT ,
***********************************************************************
* THIS ROUTINE WILL DELETE A PROGRAM LOADED BY 'CALLIT'.              *
*                                                                     *
*        ON ENTRY:                                                    *
*              R15  -> CPB FOR REQUIRED PROGRAM                       *
*                                                                     *
*        ON EXIT:                                                     *
*              R15  =  RETURN CODE FROM THE DELETE MACRO.             *
*                                                                     *
***********************************************************************
         SPACE 2
UNCALL   DS    0H                  DELETE DYNAMICALLY LOADED PROGRAM
         SUBIN SAVE=(R4)           SUBROUTINE ENTRY
         LR    R4,R15              COPY CPB ADDRESS
         ICM   R15,B'1111',CPBADDR GET ADDRESS OF SUB-PROGRAM
         BZ    UNCALLX             NEVER LOADED
         SR    R15,R15             CLEAR REGISTER
         ST    R15,CPBADDR         RESET ENTRY POINT ADDRESS
         DELETE EPLOC=CPBNAME      REMOVE REQUIRED ROUTINE
         SPACE 2
UNCALLX  DS    0H                  END OF SUBROUTINE
         SUBOUT RESTORE=(R4)       RETURN TO CALLER
         SPACE 1
         DROP  R4                  END CPB ADDRESSABILITY
