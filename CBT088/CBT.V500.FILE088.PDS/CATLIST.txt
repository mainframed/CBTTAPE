/* CATLIST  REXX                                  */
/*                                                */
/* - Obtain Master Catalog name from SYS1.PROCLIB */
/* - Obtain Master Catalog volume from LISTDSI    */
/* - Obtain usercat names/volumes from LISTCAT    */
/* - Generate catalog backup job                  */
/*                                                */
/* Created 23Jan95 TRS                            */
/* fixed   23Nov97 BHW                            */
/*                                                */

trace off

strt = 0                  /* counters for substr  */
fnsh = 0                  /* counters for substr  */
indx = 0                  /* usercatalog counter  */

profsave = SYSVAR(syspref)
"PROFILE NOPREFIX MSGID"

/* Do LISTCAT for SYS1.PROCLIB to get mcat name   */

MCTrap = outtrap('MCTrap.')
"LISTCAT VOL ENTRY(SYS1.PROCLIB)"

if rc > 0 then do           /* get out if errors  */
   do y = 1 to MCTrap.0
      say MCTrap.y
      end
   "PROFILE PREFIX(" profsave
   exit
   end

else do y = 1 to MCTrap.0
   if index(MCTrap.y,'IN-CAT') > 0 then do
      strt = index(MCTrap.y,'IN-CAT') + 11
      fnsh = 44
      mcatname = strip(substr(MCTrap.y,strt,fnsh))
      end
   end

/* Do LISTDSI on "mcatname" to get mcat volume    */

dsinfo = LISTDSI(mcatname)
mcatvol = SYSVOLUME

/* Do LISTCAT for usercatalogs/volsers            */

LCTrap = outtrap('LCTrap.')
"LISTCAT USERCATALOG VOLUME CATALOG(" mcatname ")"

if rc > 0 then do
   do y = 1 to LCTrap.0
      say LCTrap.y
      end
   "PROFILE PREFIX(" profsave
   exit
   end

else do y = 1 to LCTrap.0

   strt = 0
   fnsh = 0

   if index(LCTrap.y,'USERCATALOG') > 0 then do
      indx = indx + 1
      strt = index(LCTrap.y,'USERCATALOG') + 16
      fnsh = 44
      ucatname.indx = strip(substr(LCTrap.y,strt,fnsh))
      strtex = index(ucatname.indx,'.') + 1
      ucatex.indx = substr(ucatname.indx,strtex)
      end

   else if index(LCTrap.y,'VOLSER') > 0 then do
      strt = index(LCTrap.y,'VOLSER') + 18
      fnsh = 6
      ucatvol.indx = strip(substr(LCTrap.y,strt,fnsh))
      end

   end

/* Build jobstream here                           */

queue '//SYSCATBK  JOB (SYST,06),''Catalog Backup'','
queue '//        CLASS=7,'
queue '//        MSGCLASS=W,'
queue '//        REGION=0M,'
queue '//        TIME=1439'
queue '//*'
queue '/*JOBPARM LINES=9999'
queue '//*************************************************************'
queue '//*'
queue '//* SYSCATBK JOB built by SYSBLD00 on' date() 'at' time()
queue '//*'
queue '//*************************************************************'
queue '//*'
queue '//SYSCATBK EXEC PGM=IDCAMS,TIME=1439'
queue '//MASTER    DD  DISP=SHR,DSN=' || mcatname
queue '//EXPMSTR   DD  DISP=(,CATLG),DSN=MVS.CATBKUP(+1),'
queue '//          DCB=(MODEL.DCB,RECFM=VBA,LRECL=125,BLKSIZE=629),'
queue '//          LABEL=(1,SL,EXPDT=99000),'
queue '//          UNIT=3490,VOL=(,RETAIN)'
queue '//LSTMSTR   DD  DISP=(,KEEP),DSN=MVS.MCATLIST,'
queue '//          DCB=(RECFM=VBA,LRECL=125,BLKSIZE=629),'
queue '//          LABEL=(2,SL,RETPD=0),'
queue '//          UNIT=AFF=EXPMSTR,VOL=REF=*.EXPMSTR'

do i = 1 to indx

   if index(ucatname,'ABR.UCAT') = 0 then do

      expfile = 2*i+1
      lstfile = 2*i+2
      catddn  = '//INUCAT' || i
      expddn  = '//OUTEXP' || i
      lstddn  = '//OUTLST' || i

      queue '//*'
      queue catddn || ' DD DISP=SHR,DSN=' || ucatname.i
      queue expddn || ' DD DISP=(,KEEP),DSN=MVS.EX.' || ucatex.i || ','
      queue '//          DCB=(RECFM=VBA,LRECL=125,BLKSIZE=629),'
      queue '//          LABEL=(' || expfile || ',SL,RETPD=0),'
      queue '//          UNIT=AFF=EXPMSTR,VOL=REF=*.EXPMSTR'
      queue lstddn || ' DD DISP=(,KEEP),DSN=MVS.LC.' || ucatex.i || ','
      queue '//          DCB=(RECFM=VBA,LRECL=125,BLKSIZE=629),'
      queue '//          LABEL=(' || lstfile || ',SL,RETPD=0),'
      queue '//          UNIT=AFF=EXPMSTR,VOL=REF=*.EXPMSTR'

      end

   end

queue '//SYSPRINT  DD  SYSOUT=*'
queue '//SYSIN     DD  *'

queue ' EXPORT' mcatname 'OUTFILE(EXPMSTR) TEMPORARY'
queue ' IF MAXCC<=4 THEN -'
queue '    LISTCAT CAT(' || mcatname || ') OUTFILE(LSTMSTR) ALL'

do i = 1 to indx

   queue ' IF MAXCC<=4 THEN -'
   queue '    EXPORT' ucatname.i 'OUTFILE(OUTEXP' || i || ') TEMPORARY'
   queue ' IF MAXCC<=4 THEN -'
   queue '    LISTCAT CAT(' || ucatname.i || ') OUTFILE(OUTLST',
         || i || ') VOL'

   end

/* TAPEMAP step maps bkup tape and sends to Brian */

queue '//******************************************'
queue '//* SEND TAPEMAP OF BKUP TAPE TO PRINTER ***'
queue '//******************************************'
queue '//TAPEMAP  EXEC  PGM=TAPEMAP,               '
queue '//             COND=(4,LT)                  '
queue '//SYSPRINT DD  SYSOUT=X                     '
queue '//SYSPRNT2 DD  DUMMY                        '
queue '//SYSUT1   DD  DSN=MVS.CATBKUP(+1),         '
queue '//             DCB=BUFNO=30,                '
queue '//             DISP=SHR                     '
queue '//******************************************'

/* EXIT code goes here                            */

"EXECIO * DISKW jobout (FINIS"

"PROFILE PREFIX(" profsave
exit

