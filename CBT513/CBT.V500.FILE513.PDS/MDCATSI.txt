* +--- ZOS170A --------- MDCATSI -------------------------------------+
* |                                                                   |
* | NOMBRE ....... MDCATSI                                            |
* |                                                                   |
* | UBICACION .... SYST.LINKLIB           AMODE(ANY) RMODE(24)        |
* |                                                                   |
* | AUTOR ........ ANGEL LUIS DOMINGUEZ MARTIN. (C) 1992              |
* |                                                                   |
* | FUNCIONES .... MODULO PARA OBTENCION DE DATOS DE DATASETS A       |
* |                PARTIR DEL CATALOGO, MEDIANTE LA UTILIZACION DEL   |
* |                CSI-CATALOG SEARCH INTERFACE                       |
* |                                                                   |
* | OBJETIVOS .... FACILITAR ACCESO A DATOS DE CATALOGOS              |
* |                                                                   |
* | LENGUAJE ..... ASSEMBLER H.                                       |
* |                                                                   |
* | TIPO ......... MODULO                                             |
* |                                                                   |
* | PARAMETROS ... AREA PASADA POR EL PROGRAMA LLAMANTE               |
* |                VER LA DSECT $PARAM AL FINAL DEL FUENTE            |
* |                                                                   |
* | COD.LLAMADA    POSITIVOS:  FUNCIONES A REALIZAR                   |
* |                            0 - OBTENER FICHEROS SEGUN FILTRO      |
* |                            1 - FINAL DE DATOS                     |
* |                            2 -                                    |
* |                NEGATIVOS:  ERRORES                                |
* |                           -1 - EL FILTRO NO PROPORCIONA DATOS     |
* |                           -2 - NO LOCALIZA EL MODULO IGGCSI00     |
* |                           -3 - ERRORES EN ACCESO A INTERFACE CSI  |
* |                           -4 - AREA TRABAJO INSUFICIENTE          |
* +-------------------------------------------------------------------+
MDCATSI  INICIO    (12,11),COMEN='OBTENCION DE DATOS DE CATALOGOS.',   X
               VERSION=ZOS170A,                                        X
               AMODE=ANY,                                              X
               RMODE=24,                                               X
               TIPO=NORENT
*        +--------------------------+
*--------|   OBTENCION DE PARAMETRO |
*        +--------------------------+
         L         R2,0(,R2)
         USING     $PARAM,R2
*        +-----------------------------------+
*--------| CAMBIO DE ESTADO SI ES NECESARIO  |-------------------------
*        | PARA EJECUTAR SIEMPRE EN AMODE 31 |
*        +-----------------------------------+
         LA        R1,*+4+4+2
         O         R1,KBSM
         BSM       R0,R1
*        +----------------------------------------+
*--------|   CARGA DEL MODULO CSI                 |--------------------
*        +----------------------------------------+
         CLC       $CLAVEMD,CEROSBIN   <> PRIMERA LLAMADA
         BNE       LB10                <> SALTAR CARGA MODULO
*
*        INICIALIZACION DE CONTADORES
*
         CLC       $WORKLEN,KF4096     <> AL MENOS 2048
         BNL       LB00A
         LA        R1,4
         B         RETNEG
*
LB00A    DS        0H
         XC        SWFIRST,SWFIRST     <> INDICAR PRIMERA VEZ
         XC        PENTE,PENTE         <> CLEAR DATOS PENDIENTES
         XC        SWDATOS,SWDATOS     <> CLEAR ALGUN DATO DEVUELTO
         XC        POSIC,POSIC         <> CELAR POSI.
*
         LOAD      EP=IGGCSI00,ERRET=LB00B
         STCM      R0,B'1111',$CLAVEMD <> GUARDAR LA DIRECCION
         B         LB10
LB00B    EQU       *
         LA        R1,2
         B         RETNEG
*
*        +----------------------------------------+
*--------|   PROCESO DE PROGRAMA                  |--------------------
*        +----------------------------------------+
LB10     DS        0H
         CLC       PENTE,CEROSBIN      <> HAY DATOS PENDIENTES ?
         BNE       LB20                <> SI, PROCESARLOS
*
         TM        SWFIRST,X'80'       <> ES LA PRIMERA VEZ
         BO        LB10CA
         OI        SWFIRST,X'80'       <> DES-INDICAR PRIMERA VEZ
*
* - INICIALIZACION AREA DE LLAMADA
*
         MVC       CSIFILTK,$FILTRO
         MVI       CSICATNM,C' '
         MVC       CSICATNM+1(L'CSICATNM-1),CSICATNM
         MVI       CSIRESNM,C' '
         MVC       CSIRESNM+1(L'CSIRESNM-1),CSICATNM
         MVI       CSIDTYPS,C' '
         MVC       CSIDTYPS+1(L'CSIDTYPS-1),CSIDTYPS
         MVI       CSICLDI,C'Y'
         MVI       CSIRESUM,C' '
         MVI       CSIS1CAT,C'Y'
         XC        CSINUMEN,CSINUMEN
         B         LB10CC
*
LB10CA   DS        0H
         CLI       CSIRESUM,C'Y'       <> HABRA MAS ENTRADAS ?
         BE        LB10CC
         LA        R1,1
         B         ENDOFPGM
*
LB10CC   DS        0H
         ICM       R1,B'1111',$WORKLEN <> AREA DECLARADA
         LA        R15,CSIDSTAR-$WORKA <> AREA GASTADA POR DELANTE
         SR        R1,R15              <> RESTO LIBRE
         ST        R1,CSIDWLEN         <> TAMA#O DE AREA MODULO
         LA        R1,CSIRTCD
         ST        R1,CSIPARM
         LA        R1,CSIFLLAM
         ST        R1,CSIPARM+4
         LA        R1,CSIDATD
         ST        R1,CSIPARM+8
*
         OI        SWFIRST,X'01'
         LA        R1,CSIPARM
         ICM       R15,B'1111',$CLAVEMD <> DIRECCION DE IGGCSI
         CALL      (15)
         LTR       R15,R15
         BZ        LB10IN              <> EXTRAER NOMBRES
         LA        R15,3
         B         RETNEG
*
LB10IN   DS        0H
*
*        PENDIENTE DE VERIFICAR QUE HA ENCONTRADO EL FILTRO
*
         L         R3,CSIDULEN         <> LONGITUD USADA
         LA        R1,CSIDSTAR-CSIDWLEN
         SR        R3,R1               <> RESTAR CABECERA DATOS
         ST        R3,PENTE
         LTR       R3,R3
         BNZ       LB10IN2
         LA        R1,1                <> DEVOLVEMOS UNO (POS O NEG)
         TM        SWDATOS,X'80'       <> SE DEVOLVIO ALGO YA
         BO        ENDOFPGM
         B         RETNEG
*
LB10IN2  DS        0H
         OI        SWDATOS,X'80'       <> ALGUN DATO PARA ESTE FILTRO
         LA        R4,CSIDSTAR
         ST        R4,POSIC
*        +----------------------------------------+
*--------|   EXTRACCION DE NOMBRES DE FICHEROS    |--------------------
*        +----------------------------------------+
LB20     DS        0H
LB20BUC  EQU       *
         MVC       $DRCATNA,CSIDCATN
         L         R3,PENTE
         L         R4,POSIC
         USING     CSIDDSEC,R4
         MVC       $DRTIPO,CSIDETIP
         MVC       $DRENAME,CSIDENAM
         DROP      R4
         LA        R1,CSIDEEND-CSIDEFLG
         AR        R4,R1               <> AVANCE POSICIONAMIENTO
         ST        R4,POSIC
         SR        R3,R1               <> RESTAR TOTAL DATOS
         ST        R3,PENTE
         SLR       R1,R1
         B         ENDOFPGM
*
*        +----------------------------------------+
*--------|   FINALIZACION DEL PROGRAMA            |--------------------
*        +----------------------------------------+
ENDOFPGM EQU       *
         XC        $CODIGO,$CODIGO
         STCM      R1,B'0011',$CODIGO
         FIN       CR,TIPO=NORENT
RETNEG   EQU       *
         XC        $CODIGO,$CODIGO
         MH        R1,KBIN1NEG
         STCM      R1,B'0011',$CODIGO
         FIN       CR,TIPO=NORENT
*        +--------------------------------+
*--------| RESERVA DE AREAS ESTATICAS     |----------------------------
*        +--------------------------------+
         DS        0F
KBSM     DC        X'80000000'
*
CSIPARM  DS        3F
*
KBIN1NEG DC        H'-1'
CEROSBIN DC        X'00000000'
KF4096   DC        F'4096'
         LTORG
*        +--------------------------------+
*--------| DSECT DE PARAMETROS            |----------------------------
*        +--------------------------------+
$PARAM   DSECT
$CODIGO  DS        H                   <> CODIGOS DE LLAMADA
*                                         0   OBTIENE DATOS
*                                         1   FINAL DE DATOS
*                                      <> CODIGOS DE RETORNO
*                                         0   FUNCION CORRECTAMENTE
*                             -1 - EL FILTRO NO PROPORCIONA DATOS
*                             -2 - NO LOCALIZA EL MODULO IGGCSI00
*                             -3 - ERRORES EN ACCESO A INTERFACE CSI
*                             -4 - AREA TRABAJO INSUFICIENTE
*
$FILTRO  DS        CL44                <> FILTRO DE BUSQUEDA
$CLAVEMD DS        CL4                 <> CLAVE PARA MODULO
$RELLE1  DS        CL2                 <> PARA ALINEAMIENTO (CLASICO)
*
$DATLEN  DS        F     >>> 89 <<<    <> LONGITUD DATOS A DEVOLVER
$DATOSR  DS        0CL1                <> COMIENZO DATOS RECUPERADOS
$DRCATNA DS        CL44                <> NOMBRE DE CATALOGO
$DRTIPO  DS        CL1                 <> TIPO DE ENTRADA
*                                         A: FICHERO NO VSAM
*                                         B: GRUPO DE GENERACION
*                                         C: CLUSTER
*                                         D: COMPONENTE DATOS VSAM
*                                         G: ALTERNATE INDEX
*                                         I: COMPONENTE INDICES VSAM
*                                         H: GENERATION DATASET
*                                         R: PATH DE VSAMSAM
*                                         X: ALIAS
*                                         U: ENTRADA CONEX.CAT.USUARIO
*                                         L: LIBRARY TAPE AM
*                                         W: ENTRY VOLUME TAPE
$DRENAME DS        CL44                <> NOMBRE DE ENTRADA
$RELLE2  DS        CL3                 <> AREA RESERVADA ALINEAMIENTO
*
$WORKLEN DS        CL4                 <> LONGITUD DE LA WORKA QUE
*                                         EL PROGRAMA LLAMANTE
*                                         SUMINISTRA A CONTINUACION
$WORKA   DS        0CL1                <> AREA TRABAJO SUMINISTRADA
PENTE    DS        F
POSIC    DS        F
SWFIRST  DS        CL1
SWDATOS  DS        CL1
*----------------------------RETURN CODE CSI
CSIRTCD  DS        0F
PARMRC   DS        0CL4
MODID    DC        XL2'0000'           <> MODULE ID
RSNCODE  DC        XL1'00'             <> REASON CODE
RTNCODE  DC        XL1'00'             <> RETURN CODE
*----------------------------CABECERA DE LLAMADA
CSIFLLAM DS        0F
CSIFILTK DS        CL44                <> FILTER KEY
CSICATNM DS        CL44                <> CATNAME O BLANCOS
CSIRESNM DS        CL44                <> RESUME NAME O BLANCOS
CSIDTYPD DS        0CL16               <> TIPOS DE ENTRADA
CSIDTYPS DS        CL16
CSIOPTS  DS        0CL4                <> OPCIONES
CSICLDI  DS        CL1                 <> RETURN D&I IF MATCH Y/BLANK
CSIRESUM DS        CL1                 <> RESUME FLAG  Y/BLANK
CSIS1CAT DS        CL1                 <> SEARCH CATALOG Y/BLANK
CSIRESRV DS        XL1                 <> RESERVED
CSINUMEN DS        H                   <> NUM.ENTRADAS QUE SIGUEN
CSIENTS  DS        0CL8                <> VARIABLE NUMERO ENTRADAS
CSIFLDNM DS        CL8                 <> SOLO EL NOMBRE
*----------------------------DATOS DEVUELTOS
*----------------------------DATOS DEVUELTOS
*----------------------------DATOS DEVUELTOS
CSIDATD  DS        0F
CSIDWLEN DS        F                   <> CALCULAR EL TAMAÂ¦O RESTANTE
CSIDRLEN DS        F                   <> REQUERIDA EN CASO DE PEQUE#A
CSIDULEN DS        F                   <> LONGITUD REAL DEVUELTA
CSIDNUMC DS        H
CSIDFLG1 DS        CL1
CSIDCTIP DS        CL1                 <> TIPO DE CATALOGO
CSIDCATN DS        CL44                <> NOMBRE DE CATALOGO
CSIDMODR DS        F                   <> MODULO Y RETURN CODES
CSIDSTAR EQU       *                   <> COMIENZAN LOS DATOS
*
* ESTE AREA ESTARA SOBRE LA ZONA FINAL DEL BUFFER PASADO AL MODULO
*
CSIDDSEC DSECT
CSIDEFLG DS        XL1
CSIDETIP DS        XL1
CSIDENAM DS        CL44
CSIDELEN DS        XL2                 <> VALOR 4 NO HAY CAMPOS
CSIDEFI1 DS        XL2                 <> RESERVADO
CSIDEEND EQU       *
*
         END       MDCATSI
