         PROC 0
 /* 01/03/84 MURA - SPACE ALLOCATION FOR OUTLIST LOWERED             */
 /* 04/23/84 MURA - REMOVE TBSAVE                                    */
 /* 06/22/84 MURA - USE 3.6 JOBCARDS: ZOJB1 -> HJC1, ZOJB2 -> HJC2,  */
 /*                 ZOJB3 -> HJC3, ZOJB4 -> HJC4, ZOCLA -> HCLA      */
 /* 07/06/84 MURA - CHANGE ALLOC. TO TEMP. DS FOR BROWSE, NOT PRINT  */
 /* 07/06/84 MURA - ADD TITLE USING JHABROB                          */
           CONTROL NOFLUSH                     /* OUTLIST UTILITY(3.8)/
           SET &MSGID =
         LOOP: +
           ISPEXEC DISPLAY PANEL(ISRUOL01) MSG(&MSGID)
           IF &LASTCC = 8 THEN +
             EXIT CODE(0)                      /* END WAS REQUESTED  */
           ELSE +
             DO                                /* END NOT REQUESTED  */
               ISPEXEC CONTROL DISPLAY LINE START(20)
               SET &MSGID =
               IF &C ¬=  THEN +
                 SET &CLASS = CLASS(&C.)       /* SET UP CLASS       */
               ELSE +
                 SET &CLASS =
               IF &TJID ¬=  THEN +
                 SET &JOBID = &STR((&TJID.&STR())) /* SET UP JOBID   */
               ELSE +
                 SET &JOBID =
               IF (&ZCMD = P OR &ZCMD =  )  THEN +
                 DO                            /* ALLOC PRINT DATASET*/
                   SET &ZOLID = 100            /*                    */
                   ISPEXEC VGET (ZOLID) PROFILE
                   IF (&LASTCC = 8 OR &ZOLID = 999) THEN +
                     SET &ZOLID = 100          /* GENERATE 3 DIGIT # */
                   ELSE                        /* TO MAKE DSN UNIQUE */+
                     SET &ZOLID = &ZOLID + 1
                   ISPEXEC VPUT (ZOLID) PROFILE
                   ISPEXEC VGET (ZAPPLID) ASIS   /* GET APPLICATION ID  AND */
 /* 04/23/84 MURA  ISPEXEC TBSAVE &ZAPPLID.PROF  /* SAVE IT IN CASE OF      */
                                                 /* ABNORMAL TERMINATION    */
                   IF (&SYSUID = &SYSPREF OR &LENGTH(&SYSPREF)=0) THEN +
                     SET &DSN = &SYSUID..SPF&ZOLID..OUTLIST
                   ELSE  +
                     SET &DSN = &SYSPREF..&SYSUID..SPF&ZOLID..OUTLIST
                   ISPEXEC VGET (ZSCREEN) ASIS /*            07/06/84*/
                   CONTROL NOMSG
                   FREE FI(SPFUOL1 JHUOL&ZSCREEN) /*         07/06/84*/
                   ATTR SPFUOL1 BLKSIZE(3059) LRECL(133) +
                     DSORG(PS) RECFM(F B &R)
                   CONTROL MSG
                   IF &ZCMD = P      /*                      07/06/84*/-
                      THEN DO        /*                      07/06/84*/
                             SET JHOLDISP = CATALOG /*       07/06/84*/
                             SET JHOLDA = &STR(DA('&DSN')) /*07/06/84*/
                           END       /*                      07/06/84*/
                      ELSE DO        /*                      07/06/84*/
                             SET JHOLDISP = DELETE  /*       07/06/84*/
                             SET JHOLDA = &STR()  /*         07/06/84*/
                           END       /*                      07/06/84*/
                   ERROR +
                     DO                        /* EXIT USED FOR      */
                       SET &MSGID = ISRU220    /* ALLOCATION ERROR   */
                       RETURN
                     END
 /* FOLLOWING LINE MODIFIED BY MURA                          07/06/84*/
                   ALLOC &JHOLDA    TRACKS     /* ATTRIBUTES OF THE  */+
                     USING(SPFUOL1) RELEASE    /* PRINT DATASET MAY  */+
                     FI(JHUOL&ZSCREEN) &JHOLDISP /*LINE ADDED07/06/84*/+
                     SPACE(10 10) /*CATALOG    /* BE CHANGED TO FIT  */
                                     /* SPACE CHANGED - MURA 01/03/84*/
                   ERROR OFF                   /* LOCAL ENVIRONMENT. */
                   ISPEXEC SELECT PGM(JH#GTDSN) /*           07/06/84*/-
                      PARM(JHUOL&ZSCREEN)       /*           07/06/84*/
                   ISPEXEC VGET (JHTDSN JHTVOL) /*           07/06/84*/
                 END                           /* TAKE CARE          */
               IF &ZCMD = P AND &MSGID =   THEN +
                 DO                            /* PRINT OPTION       */
                   OUTPUT &TJNM.&JOBID &CLASS PRINT('&DSN') BEGIN
                   ISPEXEC CONTROL NONDISPL END
                   ISPEXEC CONTROL ERRORS RETURN
                   ISPEXEC BROWSE DATASET('&DSN')
                   SET &RC = &LASTCC
                   ISPEXEC CONTROL NONDISPL END
                   ISPEXEC DISPLAY
                   IF &RC ¬= 0 THEN            /* CHECK FOR          */+
                     DO                        /* EMPTY DATASET      */
                       SET &MSGID = ISRU222
                       CONTROL NOMSG
                       DELETE '&DSN'
                       CONTROL MSG
                     END
                   ELSE +
                     DO                        /* FOREGROUND PRINT   */
                       FREE DA('&DSN')
                       ISPEXEC CONTROL ERRORS CANCEL
 /* FOLLOWING LINE CHANGED BY MURA                           06/22/84*/
                       SET &HCLA =               /* INIT VARIABLES   */
                       SET &ZOPID =              /* IN CASE THEY ARE */
 /* FOLLOWING 7 LINES CHANGED BY MURA                        06/22/84*/
                       SET &HJC1 =               /* NOT IN THE       */
                       SET &HJC2 =               /* PROFILE POOL     */
                       SET &HJC3 =
                       SET &HJC4 =
                       ISPEXEC VGET (HCLA,ZOPID, +
                         HJC1,HJC2,HJC3,HJC4) PROFILE /*     06/22/84*/
                       SET &ZCLA = &STR(&HCLA)   /* THESE VARIABLES  */
                       SET &ZPID = &STR(&ZOPID)  /* ARE NEEDED BY THE*/
 /* FOLLOWING 4 LINES CHANGED BY MURA                        06/22/84*/
                       SET &ZJB1 = &STR(&HJC1)   /* ISRFPT PROGRAM   */
                       SET &ZJB2 = &STR(&HJC2)   /* IN THE SHARED    */
                       SET &ZJB3 = &STR(&HJC3)   /* VARIABLE POOL    */
                       SET &ZJB4 = &STR(&HJC4)
                       SET &ZFPRINT = ISRFPPRT
                       SET &ZFLID = &DSN
                       ISPEXEC VPUT (ZFPRINT,ZFLID,  +
                         ZCLA,ZPID,ZJB1,ZJB2,ZJB3,ZJB4)
                       ISPEXEC SELECT PGM(ISRFPT)
                       ISPEXEC VGET (ZCLA,ZPID,ZJB1,ZJB2,ZJB3,ZJB4)
 /* FOLLOWING LINE CHANGED BY MURA                           06/22/84*/
                       SET &HCLA = &STR(&ZCLA)   /* SAVE VARIABLES   */
                       SET &ZOPID = &STR(&ZPID)  /* FROM THE PRINT   */
 /* FOLLOWING 4 LINES CHANGED BY MURA                        06/22/84*/
                       SET &HJC1 = &STR(&ZJB1)   /* PANEL IN THE     */
                       SET &HJC2 = &STR(&ZJB2)   /* PROFILE POOL     */
                       SET &HJC3 = &STR(&ZJB3)
                       SET &HJC4 = &STR(&ZJB4)
                       SET &MSGID = ISRU227
                       ISPEXEC VPUT (HCLA,ZOPID, +
                         HJC1,HJC2,HJC3,HJC4) PROFILE /*     06/22/84*/
                     END
                 END
               ELSE +
                 IF &ZCMD =  AND &MSGID =   THEN +
                   DO                          /* BROWSE OPTION      */
 /* 07/06/84 FOLLOWING LINE CHANGED BY R. MURA               07/06/84*/
                     OUTPUT &TJNM.&JOBID &CLASS PRINT('&JHTDSN') +
                       BEGIN KEEP HOLD
                     SET &JHTITLE =     /* FOR JHABROB PANEL 07/06/84*/-
                      &STR(OUTPUT OF &TJNM&JOBID) /*         07/06/84*/
                     ISPEXEC CONTROL ERRORS RETURN
                     ISPEXEC BROWSE DATASET('&JHTDSN') /*    07/06/84*/-
                       VOLUME(&JHTVOL) PANEL(JHABROB) /*     07/06/84*/
                     IF &LASTCC ¬= 0 THEN      /* CHECK FOR          */+
                       SET &MSGID = ISRU221    /* EMPTY DATASET      */
                     ISPEXEC CONTROL ERRORS CANCEL
                     CONTROL NOMSG
  /*                 DELETE '&DSN'        LINE REMOVED       07/06/84*/
                     FREE F(JHUOL&ZSCREEN) /* FREE/DELETE    07/06/84*/
                     CONTROL MSG
                   END
                 ELSE +
                   IF &ZCMD = L THEN +
                     DO                        /* LIST OPTION        */
                       SET &L = &LENGTH(&SYSUID)
                       IF &LENGTH(&TJNM) = &EVAL(&L + 1) THEN +
                         IF (&SUBSTR(1:&L,&TJNM) = &SYSUID) THEN +
                           STATUS
                         ELSE +
                           STATUS &TJNM
                       ELSE +
                         STATUS &TJNM
                     END
                   ELSE +
                     IF &ZCMD = R THEN +
                       DO                     /* REQUEUE OPTION      */
                         OUTPUT &TJNM.&JOBID &CLASS NEWCLASS(&N)
                         ISPEXEC LOG MSG(ISRU223)
                         SET &MSGID = ISRU225  /* REQUEUE COMPLETED  */
                       END
                     ELSE +
                       IF &ZCMD = D THEN +
                         DO                   /* DELETE OPTION       */
                           OUTPUT &TJNM.&JOBID &CLASS DELETE
                           ISPEXEC LOG MSG(ISRU224)
                           SET &MSGID = ISRU226 /* DELETE COMPLETED  */
                         END
             END
           GOTO LOOP                           /* LOOP UNTIL END     */
         END
