PROC 0                              /*                               */
/*    RON MURA, TECHNICAL SERVICES                                   */
/* 01/16/84 KMURA - PANEL, MSG ID NAMES CHANGED                      */
/*    RECONSTRUCT (ADD/DELETE FIELDS) AN EXISTING TABLE              */
CONTROL NOFLUSH                     /*                               */
SET &MSG1 =                         /*                               */
DISPLAY1:                           /*                               */-
ISPEXEC DISPLAY PANEL(JHAYP12) MSG(&MSG1) /* ENTRY PANEL     01/16/84*/
IF &LASTCC > 4 THEN EXIT            /*                               */
SET &MSG1 =                         /*                               */
ISPEXEC TBOPEN &JHCTNM1 NOWRITE     /* OPEN INPUT TABLE              */
IF &LASTCC > 4                      /*                               */-
   THEN DO                          /*                               */
           SET &MSG1 = JHAYM121     /* TABLE NOT FOUND       01/16/84*/
           GOTO DISPLAY1            /*                               */
        END                         /*                               */
ISPEXEC TBQUERY &JHCTNM1 KEYS(JHCTKEYS) NAMES(JHCTNMS1) /*           */
SET &JHCTLN = &LENGTH(&STR(&JHCTKEYS)) /*                            */
IF &JHCTLN > 1                      /*                               */-
   THEN SET &JHCTKEYS = &SUBSTR(2:&JHCTLN-1,&STR(&JHCTKEYS)) /*      */
SET &JHCTLN = &LENGTH(&STR(&JHCTNMS1)) /*                            */
IF &JHCTLN > 1                      /*                               */-
   THEN SET &JHCTNMS1 = &SUBSTR(2:&JHCTLN-1,&STR(&JHCTNMS1)) /*      */
/* SEPARATE NON-KEY FIELD LIST INTO TWO PORTIONS IF THE LENGTH       */
/* EXCEEDS 219 CHARACTERS.  (MAX. VAR. LENGTH ON ANY ISPF PANEL      */
/* IS 255.)                                                          */
SET &JHCTLN = &LENGTH(&STR(&JHCTNMS1)) /*                            */
SET &JHCTNMS2 =                     /*                               */
IF &JHCTLN > 219                    /* 219 CHARS PER FIELD ON PANEL  */-
   THEN DO                          /*                               */
           SET &JHCTI = 212         /* LOOK FOR BLANK IN POS. 212+   */
           DO WHILE &SUBSTR(&JHCTI,&JHCTNMS1) ¬= &STR( )
              SET &JHCTI = &JHCTI + 1 /* SKIP TO NEXT POSITION       */
           END                      /*                               */
           SET &JHCTNMS2 = &SUBSTR(&JHCTI+1:&JHCTLN,&STR(&JHCTNMS1))
           SET &JHCTNMS1 = &SUBSTR(1:&JHCTI,&STR(&JHCTNMS1))
        END                         /*                               */
SET &MSG2 =                         /*                               */
DISPLAY2:                           /*                               */-
ISPEXEC DISPLAY PANEL(JHAYP12B) MSG(&MSG2) /* 2ND PANEL      01/16/84*/
IF &LASTCC > 4                      /*                               */-
   THEN DO                          /*                               */
           ISPEXEC TBEND &JHCTNM1   /*                               */
           GOTO DISPLAY1            /*                               */
        END                         /*                               */
SET &MSG2 =                         /*                               */
SET &SAMESW = OFF                   /* NORMALLY NEW NAME IS DIFFERENT*/
IF &JHCTNM1 = &JHCTNM2              /* IF NEW NAME = OLD NAME        */-
   THEN DO                          /*                               */
           IF &JHCTREPL ¬= REPLACE  /* IF REPLACE NOT SPECIFIED      */-
              THEN GOTO ERR002      /* THEN SAMEW NAME NOT ALLOWED   */
           SET &JHCTNM2 = JHCTTEMP  /* CREATE WITH TEMPORARY NAME    */
           SET &SAMESW = ON         /* SET SWITCH                    */
        END                         /*                               */
ISPEXEC TBCREATE &JHCTNM2 +
            KEYS(&JHCTKEYS) NAMES(&JHCTNMS1 &JHCTNMS2) WRITE &JHCTREPL
IF &LASTCC = 8                      /*                               */-
   THEN DO                          /*                               */
ERR002:    SET &MSG2 = JHAYM122     /* TABLE ALREADY EXISTS  01/16/84*/
           GOTO DISPLAY2            /*                               */
        END                         /*                               */
SET &JHCTCTI = 0                    /* INITIALIZE INPUT COUNTER      */
SET &JHCTCTO = 0                    /* INITIALIZE OUTPUT COUNTER     */
TBSKIP: ISPEXEC TBSKIP &JHCTNM1 SAVENAME(JHCTSVNM) /*                */
IF &LASTCC = 8 THEN GOTO FINISH     /*                               */
SET &JHCTCTI = &JHCTCTI + 1         /*                               */
IF &STR(&JHCTSVNM) =                /*                               */-
   THEN SET &JHCTSVNM = &STR(())    /*                               */
IF &JHCTFUNQ = Y                    /* IF FORCE UNIQUE KEY SPECIFIED */-
   THEN IF &STR(&JHCTKEY1) ¬=       /* AND THERE IS A KEY FIELD      */-
           THEN DO                  /* THEN SET 1ST KEY FLD = REC NO.*/
                   SET &JHCTI = &STR(X0000000&JHCTCTI) /*MIN 8 DIGITS*/
                   SET &JHCTLN = &LENGTH(&JHCTI)
                   SET &&JHCTKEY1 = &SUBSTR(&JHCTLN-7:&JHCTLN,&JHCTI)
                END                 /*                               */
ISPEXEC TBADD &JHCTNM2 SAVE&JHCTSVNM /*                              */
IF &LASTCC = 0                      /* IF THE ADD WORKED             */-
   THEN SET &JHCTCTO = &JHCTCTO + 1 /* THEN INCREMENT OUTPUT COUNTER */
GOTO TBSKIP                         /*                               */
FINISH: ISPEXEC TBEND &JHCTNM1      /*                               */
IF &SAMESW = ON                     /*                               */-
   THEN SET &WNAME = &STR(NAME(&JHCTNM1)) /*                         */
   ELSE SET &WNAME =                /*                               */
ISPEXEC TBCLOSE &JHCTNM2 PAD(50) &WNAME /*                           */
IF &SAMESW = ON                     /*                               */-
   THEN SET &JHCTNM2 = &JHCTNM1     /* RESET NEW NAME FOR MSGS       */
ISPEXEC LOG MSG(JHAYM127)           /*                       01/16/84*/
ISPEXEC LOG MSG(JHAYM128)           /*                       01/16/84*/
ISPEXEC LOG MSG(JHAYM129)           /*                       01/16/84*/
IF &JHCTCTI = &JHCTCTO              /*                               */-
   THEN SET &MSG1 = JHAYM123        /* OPERATION COMPLETED   01/16/84*/
   ELSE SET &MSG1 = JHAYM124      /* SOME RECS LOST (DUP KEY)01/16/84*/
GOTO DISPLAY1                       /*                               */
