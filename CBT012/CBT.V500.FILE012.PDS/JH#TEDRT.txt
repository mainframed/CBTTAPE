PROC 0 PANEL1(JHTEPE41) PANEL2(JHTEPE42) W1(Z&SYSUID)
 CONTROL NOFLUSH                     /*                              */
 ISPEXEC VGET (ZSCREEN) SHARED       /*                              */
 SET &DDN = JHEDRT&ZSCREEN           /*                              */
 CONTROL NOMSG                       /*                              */
 FREE F(&DDN)                        /*                              */
 CONTROL MSG                         /*                              */
 ALLOC F(&DDN) SP(2 2) TR DIR(8) BLKSIZE(3120) LRECL(80) RECFM(F B)
DISPLAY:                             /*                              */-
 ISPEXEC DISPLAY PANEL(&PANEL1)      /*                              */
 IF &LASTCC ¬= 0                     /*                              */-
    THEN GOTO FINISH                 /*                              */
 SET &CSR =                          /*                              */
PROCESS:                             /*                              */-
 SET &TB = &STR(&JHEDRTAP.EDRT)      /* SET NAME OF EDIT PROF TABLE  */
 ISPEXEC SELECT PGM(JH#GTDSN) PARM(&DDN) /*                          */
 ISPEXEC VGET (JHTDSN JHTVOL) SHARED /*                              */
 SET &JHPROFDS = &STR(ISP.PROFILE.&JHEDRTID) /*                      */
 ISPEXEC LMINIT DATAID(JHDID) DATASET('&JHPROFDS')
 IF &LASTCC ¬= 0                     /*                              */-
    THEN DO                          /*                              */
           SET &TDSN = &STR(&JHPROFDS) /*                            */
           SET &MSG = JHA@M003       /*                              */
           GOTO ERRMSG               /*                              */
         END                         /*                              */
 ISPEXEC LMOPEN DATAID(&JHDID)       /*                              */
 ISPEXEC LMMFIND DATAID(&JHDID) MEMBER(&TB)
 IF &LASTCC ¬= 0                     /*                              */-
    THEN DO                          /*                              */
           SET &TDSN = &STR(&JHPROFDS) /*                            */
           SET &TMEM = &TB           /*                              */
           SET &MSG = JHA@M007       /*                              */
           SET &CSR = JHEDRTAP       /*                              */
           GOTO ERRMSG               /*                              */
         END                         /*                              */
 ISPEXEC LMCLOSE DATAID(&JHDID)      /*                              */
 ISPEXEC LMFREE DATAID(&JHDID)       /*                              */
 COPY '&JHPROFDS(&TB)' '&JHTDSN(&W1)' NON /*                         */
 ISPEXEC CONTROL ERRORS RETURN       /*                              */
 ISPEXEC TBCLOSE &W1                 /*                              */
 ISPEXEC CONTROL ERRORS CANCEL       /*                              */
 ISPEXEC TBOPEN &W1 LIBRARY(&DDN)    /* OPEN FOR UPDATE              */
 SET &PNL = &PANEL2                  /* SET NAME OF TABLE DISPLAY PNL*/
 ISPEXEC TBTOP &W1                   /*                              */
 SET &MSG2 =                         /*                              */
 SET &CSR =                          /*                              */
 SET &CSRROW =                       /*                              */
 SET &ZTDTOP = 1                     /*                              */
 SET &AUTOSEL = YES                  /*                              */

TBDISPL: +
 ISPEXEC TBTOP &W1                   /*                              */
 ISPEXEC TBSKIP &W1 NUMBER(&ZTDTOP)  /* POSITION AS USER LAST SAW IT */
 ISPEXEC TBDISPL &W1 PANEL(&PNL) MSG(&MSG2) CURSOR(&CSR) +
             CSRROW(&CSRROW) AUTOSEL(&AUTOSEL)
 SET &RC = &LASTCC                   /*                              */
 SET &PNL =                          /*                              */
 SET &AUTOSEL = YES                  /*                              */
 SET &CSR =                          /*                              */
 SET &CSRROW =                       /*                              */
 SET &MSG2 =                         /*                              */
 ISPEXEC TBQUERY &W1 POSITION(POS)   /*                              */
 IF &ZCMD = CAN | &ZCMD = CANCEL     /*                              */-
    THEN DO                          /*                              */
           SET &ZCMD =               /*                              */
           GOTO TBEND                /*                              */
         END                         /*                              */
 IF &RC = 0 && &ZTDSELS = 0 +
    THEN DO
            SET &PNL = &PANEL2
            GOTO TBDISPL
         END
 IF &SELN = THEN SET &SELN = 1       /* PROCESS MODIFIED LINE        */
 IF &SELT ¬= D                       /*                              */-
    THEN ISPEXEC TBPUT &W1           /* MODIFY ROW                   */
 SET &X = 0
 IF &SELT = R +
    THEN DO
            SET &CSR    = ZEDSTAT
            SET &CSRROW = &POS + 1
            SET &AUTOSEL = NO
            DO WHILE &X < &SELN
               ISPEXEC TBADD &W1
               SET &X = &X + 1
            END
         END
 IF &SELT = I +
    THEN DO
            SET &CSR    = ZEDSTAT
            SET &CSRROW = &POS + 1
            DO WHILE &X < &SELN
               SET &ZEDSTAT =
               SET &ZEDDISP =
               SET &ZEDBDSN =
               SET &ZEDTDSN =
               SET &ZEDTMEM =
               SET &ZEDTVOL =
               ISPEXEC TBADD &W1
               SET &X = &X + 1
            END
         END
 IF &SELT = D +
    THEN DO WHILE &X < &SELN
               ISPEXEC TBDELETE &W1
               ISPEXEC TBSKIP &W1 NUMBER(+1)
               IF &LASTCC = 8 +
                  THEN SET &X = &SELN
               SET &X = &X + 1
         END
 IF &RC < 8 +
    THEN DO                          /* ENTER KEY WAS PRESSED        */
            SET &PNL = &PANEL2
            GOTO TBDISPL             /* GO DISPLAY TABLE AGAIN       */
         END
 ISPEXEC TBSAVE &W1 LIBRARY(&DDN)    /* END KEY PRESSED      11/27/84*/

TBEND:                               /*                              */-
 ISPEXEC TBEND &W1                   /*                              */
 ISPEXEC CONTROL ERRORS RETURN       /*                              */
 ISPEXEC TBOPEN &W1 LIBRARY(NOSUCH)  /* FORCE CLOSING OF DDN         */
 ISPEXEC CONTROL ERRORS CANCEL       /*                              */
 IF &ZCMD ¬= CAN && &ZCMD ¬= CANCEL  /*                      11/27/84*/-
    THEN COPY '&JHTDSN(&W1)' '&JHPROFDS(&TB)' NONUM /* COPY EDRT BACK*/
 GOTO DISPLAY                        /*                              */
ERRMSG:                              /*                              */-
 ISPEXEC DISPLAY MSG(&MSG) CURSOR(&CSR) /*                           */
 IF &LASTCC = 0                      /*                              */-
    THEN GOTO PROCESS                /*                              */
    ELSE GOTO FINISH                 /*                              */
FINISH:                              /*                              */-
 FREE F(&DDN)                        /*                              */
 EXIT                                /*                              */
