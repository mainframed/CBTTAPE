PROC 0 PANEL1(JHTEPE31) PANEL2(JHTEPE32) W(W&SYSUID) Y(Y&SYSUID)
 CONTROL NOFLUSH                     /*                      07/10/84*/
 ISPEXEC VGET (ZSCREEN) SHARED       /*                      07/10/84*/
 SET &DDN = JHEDPR&ZSCREEN           /*                      07/10/84*/
 CONTROL NOMSG                       /*                      07/10/84*/
 FREE F(&DDN)                        /*                      07/10/84*/
 CONTROL MSG                         /*                      07/10/84*/
 ALLOC F(&DDN) SP(2 2) TR DIR(8) BLKSIZE(3120) LRECL(80) RECFM(F B)
DISPLAY:                             /*                      07/10/84*/-
 ISPEXEC DISPLAY PANEL(&PANEL1)      /*                      07/10/84*/
 IF &LASTCC ¬= 0                     /*                      07/10/84*/-
    THEN GOTO FINISH                 /*                      07/10/84*/
 SET &CSR =                          /*                      07/12/84*/
PROCESS:                             /*                      07/10/84*/-
 SET &TB = &STR(&JHEDPRAP.EDIT)      /* SET NAME OF EDIT PROF TABLE  */
 ISPEXEC SELECT PGM(JH#GTDSN) PARM(&DDN) /*                  07/10/84*/
 ISPEXEC VGET (JHTDSN JHTVOL) SHARED /*                      07/10/84*/
 SET &JHPROFDS = &STR(ISP.PROFILE.&JHEDPRID) /*              07/12/84*/
 ISPEXEC LMINIT DATAID(JHDID) DATASET('&JHPROFDS')
 IF &LASTCC ¬= 0                     /*                      07/10/84*/-
    THEN DO                          /*                      07/10/84*/
           SET &TDSN = &STR(&JHPROFDS) /*                    07/12/84*/
           SET &MSG = JHA@M003       /*                      07/10/84*/
           GOTO ERRMSG               /*                      07/10/84*/
         END                         /*                      07/10/84*/
 ISPEXEC LMOPEN DATAID(&JHDID)       /*                      07/12/84*/
 ISPEXEC LMMFIND DATAID(&JHDID) MEMBER(&TB)
 IF &LASTCC ¬= 0                     /*                      07/10/84*/-
    THEN DO                          /*                      07/10/84*/
           SET &TDSN = &STR(&JHPROFDS) /*                    07/12/84*/
           SET &TMEM = &TB           /*                      07/12/84*/
           SET &MSG = JHA@M007       /*                      07/10/84*/
           SET &CSR = JHEDPRAP       /*                      07/12/84*/
           GOTO ERRMSG               /*                      07/10/84*/
         END                         /*                      07/10/84*/
 ISPEXEC LMCLOSE DATAID(&JHDID)      /*                      07/12/84*/
 ISPEXEC LMFREE DATAID(&JHDID)       /*                      07/12/84*/
 COPY '&JHPROFDS(&TB)' '&JHTDSN(&Y)' NONUM /*                07/10/84*/
 SET &TB = &STR(&Y)                  /* SET NAME OF EDIT PROF TABLE  */
 ISPEXEC CONTROL ERRORS RETURN       /*                      07/10/84*/
 ISPEXEC TBCLOSE &TB                 /*                      07/10/84*/
 ISPEXEC CONTROL ERRORS CANCEL       /*                      07/10/84*/
 SET JHEDUDT =                       /*                      08/14/84*/
 ISPEXEC TBSTATS &TB UDATE(JHEDUDT) UTIME(JHEDUTM) LIBRARY(&DDN)
 SET &ZEDPIMAC =                     /* NOT IN OLD PROFILES  07/11/84*/
 ISPEXEC TBOPEN &TB LIBRARY(&DDN) NOWRITE  /*                07/10/84*/
 SET &PNL = &PANEL2                  /* SET NAME OF TABLE DISPLAY PNL*/
 ISPEXEC TBTOP &TB                   /*                              */
 ISPEXEC TBCREATE &W NOWRITE +
           NAMES(JHEDPROF JHEDATTR JHEDLOCK JHEDIMAC JHEDSTAT +
                 JHEDPACK JHEDRECV JHEDNULL JHEDCAPS JHEDTABZ +
                 JHEDNUMB JHEDAUTN JHEDBNDL JHEDBNDR)
 SET &MSG =                          /*                      07/10/84*/
TBSKIP: +
ISPEXEC TBSKIP &TB NUMBER(1)         /*                              */
IF &LASTCC = 8 THEN GOTO TBDISPL     /*                              */
SET &JHEDPROF = &ZEDPTYPE            /*                              */
SET &JHEDATTR = &STR(&ZEDPRCFM-&ZEDPLRCL)
IF &SUBSTR(21,&ZEDPFLAG) = 0         /* RECOVERY                     */-
   THEN SET &JHEDRECV = &STR(-)      /*                              */
   ELSE SET &JHEDRECV = ON           /*                              */
IF &SUBSTR(6,&ZEDPFLAG) = 0          /* PACK                         */-
   THEN SET &JHEDPACK = &STR(-)      /*                              */
   ELSE SET &JHEDPACK = ON           /*                              */
IF &SUBSTR(7,&ZEDPFLAG) = 0          /* NULLS                        */-
   THEN SET &JHEDNULL = OFF          /*                              */
   ELSE IF &SUBSTR(8,&ZEDPFLAG) = 0  /*                              */-
           THEN SET &JHEDNULL = STD  /*                              */
           ELSE SET &JHEDNULL = ALL  /*                              */
IF &SUBSTR(2,&ZEDPFLAG) = 0          /* CAPS                         */-
   THEN SET &JHEDCAPS = OFF          /*                              */
   ELSE SET &JHEDCAPS = ON           /*                              */
IF &SUBSTR(14,&ZEDPFLAG) = 0         /* TABS                         */-
   THEN SET &JHEDTABZ = OFF          /*                              */
   ELSE IF &SUBSTR(15:16,&ZEDPFLAG) = 0 +
           THEN SET &JHEDTABZ = STD  /*                              */
           ELSE IF &SUBSTR(15:16,&ZEDPFLAG) = 01 +
                   THEN SET &JHEDTABZ = &STR(ON &ZEDPTABC)
                   ELSE SET &JHEDTABZ = ANY
IF &SUBSTR(13,&ZEDPFLAG) = 0         /* STATS                        */-
   THEN SET &JHEDSTAT = OFF          /*                              */
   ELSE SET &JHEDSTAT = ON           /*                              */
SET &JHEDNUMB =                      /* NUMBER                       */
IF &SUBSTR(11,&ZEDPFLAG) = 1         /*                              */-
   THEN SET &JHEDNUMB = &STR(&JHEDNUMB DISPLAY)
IF &SUBSTR(10,&ZEDPFLAG) = 1         /*                              */-
   THEN SET &JHEDNUMB = &STR(&JHEDNUMB STD)
IF &SUBSTR(9,&ZEDPFLAG) = 1          /*                              */-
   THEN SET &JHEDNUMB = &STR(&JHEDNUMB COB)
IF &SUBSTR(18,&ZEDPFLAG) = 0         /* AUTONUM                      */-
   THEN SET &JHEDAUTN = &STR(-)      /*                              */
   ELSE SET &JHEDAUTN = ON           /*                              */
IF &SUBSTR(19,&ZEDPFLAG) = 0         /* AUTOLIST                     */-
   THEN SET &JHEDAUTL = &STR(-)      /*                              */
   ELSE SET &JHEDAUTL = ON           /*                              */
IF &SUBSTR(22,&ZEDPFLAG) = 0         /* LOCK                         */-
   THEN SET &JHEDLOCK = NO           /*                              */
   ELSE SET &JHEDLOCK = YES          /*                              */
IF &STR(&JHEDNUMB) =                 /* NUMBER                       */-
   THEN SET &JHEDNUMB = OFF          /*                              */
SET &JHEDBNDL = &STR(&ZEDPBNDL)      /* LEFT BOUNDS                  */
SET &JHEDBNDR = &STR(&ZEDPBNDR)      /* RIGHT BOUNDS                 */
SET &JHEDIMAC = &STR(&ZEDPIMAC)      /* INITIAL EDIT MACRO           */
ISPEXEC TBADD &W                     /*                              */
GOTO TBSKIP                          /*                              */

TBDISPL: +
 ISPEXEC TBTOP &W                    /*                              */
 ISPEXEC TBDISPL &W PANEL(&PNL) MSG(&MSG)
 SET &RC = &LASTCC                   /*                              */
 SET &PNL =                          /*                      07/12/84*/
 IF &ZCMD ¬=                         /*                              */-
    THEN DO                          /*                              */
           SET &MSG = ISPZ001        /* INVALID COMMAND              */
           GOTO TBDISPL              /*                              */
         END                         /*                              */
 IF &RC = 0                          /* JUST PRESSED ENTER   07/12/84*/-
    THEN GOTO TBDISPL                /*                      07/12/84*/
 ISPEXEC TBEND &W                    /*                              */

 ISPEXEC TBEND &TB                   /*                      07/10/84*/
 ISPEXEC CONTROL ERRORS RETURN       /*                      07/12/84*/
 ISPEXEC TBOPEN &TB LIBRARY(NOSUCH)  /* FORCE CLOSING OF DDN 07/12/84*/
 ISPEXEC CONTROL ERRORS CANCEL       /*                      07/12/84*/
 GOTO DISPLAY                        /*                      07/10/84*/
ERRMSG:                              /*                      07/10/84*/-
 ISPEXEC DISPLAY MSG(&MSG) CURSOR(&CSR) /*                   07/10/84*/
 IF &LASTCC = 0                      /*                      07/10/84*/-
    THEN GOTO PROCESS                /*                      07/10/84*/
    ELSE GOTO FINISH                 /*                      07/10/84*/
FINISH:                              /*                      07/10/84*/-
 FREE F(&DDN)                        /*                      07/10/84*/
 EXIT                                /*                      07/10/84*/
