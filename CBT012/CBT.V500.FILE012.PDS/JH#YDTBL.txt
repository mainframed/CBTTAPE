PROC 0                               /*                              */
/*    RON MURA, TECHNICAL SERVICES, ISPF V1R1M0                      */
/* 10/18/83 MURA - ADDED SUPPORT FOR MULTIPLE DISPLAY FORMATS        */
/* 10/18/83 MURA - ALL VARIABLES CHANGED TO BEGIN WITH "JHY"         */
CONTROL NOFLUSH                      /*                              */
SET &JHYMSG1 =                       /* MESSAGE FOR ENTRY PANEL      */
SET &JHYMSG2 =                       /* MESSAGE FOR TABLE DISPLAY    */
INIT: +
ISPEXEC DISPLAY PANEL(ZYDTBL0) MSG(&JHYMSG1) /* GET TABLE NM 10/18/83*/
IF &LASTCC = 8 THEN EXIT CODE(0)     /* QUIT IF END COMMAND ENTERED  */
SET &JHYMSG1 =                       /* RESET MESSAGE                */
SET &JHYTBDPL = &STR(ZYDTBL&JHYDFMT) /* SET NAME/TBDISPL PNL 10/18/83*/
SET &JHYPNL2 = &JHYTBDPL             /* SET NAME OF TABLE DSPL PANEL */
SET &JHYCLOSE = Y                    /* NORMALLY CLOSE TBL WHEN DONE */
OPENTBL: ISPEXEC CONTROL ERRORS RETURN /*                            */
ISPEXEC TBOPEN &JHYTBNM NOWRITE      /*                              */
SET &JHYRC = &LASTCC                 /*                              */
ISPEXEC CONTROL ERRORS CANCEL        /*                              */
IF &JHYRC = 8                        /* TABLE DOES NOT EXIST         */-
   THEN DO                           /*                              */
           SET &JHYMSG1 = ZYDT001    /*                              */
           GOTO INIT                 /*                              */
        END                          /*                              */
IF &JHYRC > 8                        /*                      10/18/83*/-
   THEN IF &ZERRMSG = ISPT033        /* IF TBL ALREADY OPEN  10/18/83*/-
           THEN SET &JHYCLOSE = N    /* THEN JUST USE IT     10/18/83*/
           ELSE DO                   /*                      10/18/83*/
                   SET &JHYMSG1 = &ZERRMSG /*                10/18/83*/
                   GOTO INIT         /*                      10/18/83*/
                END                  /*                      10/18/83*/
ISPEXEC TBQUERY &JHYTBNM KEYS(JHYKEYS) NAMES(JHYNAMES) ROWNUM(JHYRWNUM)
IF &JHYRWNUM = 0                     /* IF TABLE IS EMPTY            */-
   THEN DO                           /*                              */
           SET &JHYMSG1 = ZYDT004    /* THEN SHOW ERROR MSG          */
           GOTO INIT                 /*                              */
        END                          /*                              */
SET &JHYI = 1                        /*                      10/18/83*/
DO WHILE &JHYI <= &JHYMXFLD          /*                      10/18/83*/
   SET &JHYVAR&JHYI = &STR()         /* SET ALL VAR NAMES TO NULLS   */
   SET &JHYTTL&JHYI = &STR()         /* SET ALL VAR TITLES TO NULLS  */
   SET &JHYI = &JHYI + 1             /*                      10/18/83*/
END                                  /*                      10/18/83*/
SET &JHYNUMVR = 1                    /* PROCESS LIST OF VAR NAMES    */
SET &JHYPOS = 1                      /*                              */
SET &JHYWORK =                       /*                              */
SET &JHYSTR = &STR(&JHYKEYS)         /* PROCESS KEYS FIRST           */
IF &STR(&JHYKEYS) =                  /* IF NO KEYS                   */-
   THEN IF &STR(&JHYNAMES) =         /* THEN IF NO "NAME" VARS       */-
           THEN DO                   /*                              */
                   SET &JHYMSG1 = ZYDT002  /* CANNOT PROCESS TABLE   */
                   GOTO INIT         /*                              */
                END                  /*                              */
           ELSE GOTO ENDSTR          /* NO KEYS BUT "NAMES"          */
LOOP: SET &JHYPOS = &JHYPOS + 1      /*                              */
SET &JHYC = &SUBSTR(&JHYPOS,&JHYSTR) /* PICK OFF ONE CHARACTER       */
IF &JHYC = &STR()) | &JHYC = &STR( ) /* IF END OF A VARIABLE NAME    */-
   THEN DO                           /*                              */
           SET &JHYVAR&JHYNUMVR = &JHYWORK /* SAVE FLD NAME IN VAR#  */
           SET &JHYWORK =            /*                              */
           SET &JHYTITLE = &&JHYVAR&JHYNUMVR /* SET TO "&JHYVAR#"    */
           SET &JHYTITLE = &JHYTITLE /* SET TO VALUE OF &JHYVAR#     */
           IF &STR(&JHYSTR) = &STR(&JHYKEYS) /* IF PROCESSING "KEYS" */-
              THEN SET &JHYTTL&JHYNUMVR = &STR(&JHYTITLE&STR(*))
              ELSE SET &JHYTTL&JHYNUMVR = &STR(&JHYTITLE) /* 10/18/83*/
           SET &JHYNUMVR = &JHYNUMVR + 1 /*                          */
           IF &JHYNUMVR > &JHYMXFLD  /*                              */-
              THEN GOTO HAVEALL      /*                              */
           IF &JHYC = &STR())        /*                              */-
              THEN GOTO ENDSTR       /*                              */
           GOTO LOOP                 /*                              */
        END                          /*                              */
SET &JHYWORK = &STR(&JHYWORK&JHYC)   /* CONCATENATE NEXT CHAR        */
GOTO LOOP                            /*                              */

ENDSTR: SET &JHYPOS = 1              /*                              */
IF &STR(&JHYSTR) = &STR(&JHYNAMES) | /*                              */-
      &STR(&JHYNAMES) =              /* IF FINISHED WITH ALL "NAMES" */-
   THEN GOTO HAVEALL                 /*                              */
SET &JHYSTR = &STR(&JHYNAMES)        /* NOW PROCESS NON-KEYS         */
GOTO LOOP                            /*                              */

HAVEALL: ISPEXEC TBDISPL &JHYTBNM PANEL(&JHYPNL2) MSG(&JHYMSG2)
SET &JHYRC = &LASTCC                 /*                              */
IF &JHYRC < 8 && &ZCMD ¬=            /*                              */-
   THEN DO                           /*                              */
           SET &JHYMSG2 = ISPZ001    /* INVALID COMMAND              */
           SET &JHYPNL2 =            /*                              */
           GOTO HAVEALL              /*                              */
        END                          /*                              */
IF &JHYCLOSE ¬= N                    /*                      10/18/83*/-
   THEN ISPEXEC TBEND &JHYTBNM       /*                              */
SET &JHYMSG2 =                       /*                              */
SET &JHYPNL2 = &JHYTBDPL             /*                              */
SET &ZCMD =                          /*                              */
GOTO INIT                            /*                              */
