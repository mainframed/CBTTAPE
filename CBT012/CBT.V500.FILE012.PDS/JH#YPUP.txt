PROC 0 T(CLOSE) DEBUG TBNM(SYSPUP)
/*    RON MURA, TECHNICAL SERVICES                          */
/* 01/16/84 KMURA - PANEL, MSG ID, CLIST NAMES CHANGED               */
/* 01/16/84 KMURA - VERIFICATION MOVED TO PANEL                      */
CONTROL NOFLUSH
SET &PNL = JHAYP141                 /* INITIALIZE VALUES     01/16/84*/
SET &MSG =
SET &CSR =
SET &CSRROW =
SET &ZTDTOP = 1

ISPEXEC TBOPEN &TBNM
IF &LASTCC = 8 +
   THEN DO                          /* CREATE NEW TABLE              */
           ISPEXEC TBCREATE &TBNM NAMES(PANEL OPTION KEYWORD KEYFLAG +
                        TEXTNUM TEXT1 TEXT2 TEXT3 TEXT4 SELECT +
                        DATE USERID)
           GOTO FILLTBL
        END
ISPEXEC TBQUERY &TBNM ROWNUM(ROWNUM) /* MAKE SURE TABLE NOT EMPTY    */
IF &ROWNUM ¬= 0 THEN GOTO TBDISPL
FILLTBL: +
SET &PANEL =
SET &OPTION =
SET &KEYWORD =
SET &KEYFLAG =
SET &TEXTNUM =
SET &TEXT1 =
SET &TEXT2 =
SET &TEXT3 =
SET &TEXT4 =
SET &SELECT =
SET &DATE = &STR(&SYSDATE)
SET &USERID = &STR(&SYSUID)
ISPEXEC TBADD &TBNM
ISPEXEC TBADD &TBNM
ISPEXEC TBADD &TBNM
ISPEXEC TBTOP &TBNM

TBDISPL: +
ISPEXEC TBTOP &TBNM                 /* POSITION CRP TO CORRECT ROW   */
ISPEXEC TBSKIP &TBNM NUMBER(&ZTDTOP)
ISPEXEC TBQUERY &TBNM POSITION(POS)
IF &DEBUG = DEBUG +
   THEN WRITE TABLE POSITIONED TO ROW &POS (&ZTDTOP)
TBDISPL2: +
IF &DEBUG = DEBUG +
   THEN WRITE PANEL=&PNL MSG=&MSG CURSOR=&CSR CSRROW=&CSRROW +
              ZTDTOP=&ZTDTOP
ISPEXEC TBQUERY &TBNM ROWNUM(ROWNUM)
IF &EVAL(&ROWNUM) = 0 +
   THEN DO                          /* MUST HAVE AT LEAST 1 ROW      */
           SET &PANEL =
           SET &OPTION =
           SET &KEYWORD =
           SET &KEYFLAG =
           SET &TEXTNUM =
           SET &TEXT1 =
           SET &TEXT2 =
           SET &TEXT3 =
           SET &TEXT4 =
           SET &SELECT =
           SET &DATE = &STR(&SYSDATE)
           SET &USERID = &STR(&SYSUID)
           ISPEXEC TBADD &TBNM
        END
SET &SEL =                          /* ALWAYS RESET BEFORE TBDISPL   */
ISPEXEC TBDISPL &TBNM PANEL(&PNL) MSG(&MSG) CURSOR(&CSR) +
                                              CSRROW(&CSRROW)
SET &RC = &LASTCC
SET &PNL =                          /* ONLY USE PANEL NAME ONCE      */
SET &MSG =                          /* INITIALIZE AFTER TBDISPL      */
SET &CSR =                          /* INITIALIZE AFTER TBDISPL      */
SET &CSRROW =                       /* INITIALIZE AFTER TBDISPL      */
ISPEXEC VGET ZTDTOP                 /* GET MOST RECENT TOP ROW       */
ISPEXEC TBQUERY &TBNM POSITION(POS) /* DETERMINE CRP AFTER TBDISPL */
IF &DEBUG = DEBUG +
   THEN WRITE RC=&RC POS=&POS PANEL=&PANEL ZTDTOP=&ZTDTOP

/* PROCESS PRIMARY COMMAND, IF ANY                                   */
IF &STR(&ZCMD) ¬= +
   THEN IF &STR(&ZCMD) = C | &STR(&ZCMD) = CAN | &STR(&ZCMD) = CANCEL +
           THEN DO
                   ISPEXEC TBEND &TBNM
                   EXIT CODE(0)
                END

IF &POS = 0 THEN GOTO RCCHK         /* CRP AT TOP MEANS NO MORE CHNGS*/

/* VALIDATE EACH ROW THAT HAS BEEN ALTERED                           */
/* VALIDATE SELECTION CODE ON THE LINE, IF ANY                       */
IF &SELN = THEN SET &SELN = 1
IF &SELT ¬= D THEN +
IF &STR(&PANEL) = +
   THEN IF &STR(&OPTION&KEYWORD&KEYFLAG&TEXT1&TEXT2+
                &TEXT3&TEXT4&SELECT)¬= +
           THEN DO                  /* PANEL NAME IS REQUIRED        */
                   SET &MSG = JHAYM141 /*                    01/16/84*/
                   SET &CSR = PANEL
                   SET &CSRROW = &POS
                   GOTO TBDISPL2
                END
           ELSE
   ELSE DO
           IF &SEL = THEN SET &DATE=&STR(&SYSDATE)
           IF &SEL = THEN SET &USERID =&STR(&SYSUID)
           IF &TEXT1 ¬= THEN SET &TEXTNUM = 1
           IF &TEXT2 ¬= THEN SET &TEXTNUM = 2
           IF &TEXT3 ¬= THEN SET &TEXTNUM = 3
           IF &TEXT4 ¬= THEN SET &TEXTNUM = 4
           ISPEXEC TBPUT &TBNM           /* MODIFY ROW     */
        END
SET &X = 0
IF &SELT = R +
   THEN DO
           SET &CSR    = PANEL
           SET &CSRROW = &POS + 1
           SET &DATE=&STR(&SYSDATE)
           SET &USERID =&STR(&SYSUID)
           DO WHILE &X < &SELN
              ISPEXEC TBADD &TBNM
              SET &X = &X + 1
           END
        END
IF &SELT = I +
   THEN DO
           SET &CSR    = PANEL
           SET &CSRROW = &POS + 1
           DO WHILE &X < &SELN
              SET &PANEL =
              SET &OPTION =
              SET &KEYWORD =
              SET &KEYFLAG =
              SET &TEXTNUM =
              SET &TEXT1 =
              SET &TEXT2 =
              SET &TEXT3 =
              SET &TEXT4 =
              SET &SELECT =
              SET &DATE=&STR(&SYSDATE)
              SET &USERID =&STR(&SYSUID)
              ISPEXEC TBADD &TBNM
              SET &X = &X + 1
           END
        END
IF &SELT = D +
   THEN DO WHILE &X < &SELN
              ISPEXEC TBDELETE &TBNM
              ISPEXEC TBSKIP &TBNM NUMBER(+1)
              IF &LASTCC = 8 +
                 THEN SET &X = &SELN
              SET &X = &X + 1
        END
IF &RC = 0 +
   THEN DO
           SET &PNL = JHAYP141       /*                      01/16/84*/
           GOTO TBDISPL
        END
GOTO TBDISPL2                       /* ADDITONAL DATA REMAINS        */

RCCHK: +
IF &RC < 8 +
   THEN DO                          /* ENTER KEY WAS PRESSED         */
           SET &PNL = JHAYP141       /*                      01/16/84*/
           GOTO TBDISPL             /* GO DISPLAY TABLE AGAIN        */
        END

ISPEXEC TBTOP &TBNM                 /* END KEY PRESSED               */
TBSKIP: +
ISPEXEC TBSKIP &TBNM NUMBER(+1)     /* GET NEXT TABLE RECORD         */
IF &LASTCC = 8 THEN GOTO TBCLOSE
IF &STR(&PANEL) = +
   THEN ISPEXEC TBDELETE &TBNM      /* REMOVE BLANK LINES FROM TABLE */
GOTO TBSKIP

TBCLOSE: +
ISPEXEC TB&T &TBNM PAD(50) REPLCOPY /* CLOSE TABLE BEFORE LEAVING    */
EXIT CODE(0)
