         TITLE   'PL360 Standard I/O Routines'
*"The source code for this computer program is placed in the public
* domain and may be used by any party without notice to the copyright
* holder, Stanford University. Stanford University provides no
* support of any kind to this computer program.  Further:
*
* STANFORD MAKES NO REPRESENTATIONS OR WARRANTIES, EXPRESS OR IMPLIED.
* BY WAY OF EXAMPLE, BUT NOT LIMITATION, STANFORD MAKES NO
* REPRESENTATIONS OR WARRANTIES OF MERCHANTABILITY OR FITNESS FOR ANY
* PARTICULAR PURPOSE OR THAT THE USE OF THE SOFTWARE WILL NOT INFRINGE
* ANY PATENTS, COPYRIGHTS, TRADEMARKS, OR OTHER RIGHTS. STANFORD SHALL
* NOT BE LIABLE FOR ANY LIABILITY OR DAMAGES WITH RESPECT TO ANY CLAIM
* BY A USER OF THIS COMPUTER PROGRAM OR ANY DERIVATIVES OF THIS
* COMPUTER PROGRAM."
***********************************************************************
*                  STANDARD I/O ROUTINES                              *
*                  OS PL360 PROGRAMS (SUBSYSTEM COMPATIBLE)           *
*                  STANFORD UNIVERSITY                                *
*                       MAY 1971                                      *
*                                                                     *
*  Modifications:                                                     *
*     11/21/90  Niz  Add ESTAE exit so output DCBs will be closed     *
*                    normally even if the program abends or is        *
*                    cancelled.                                       *
*                                                                     *
***********************************************************************
         SPACE
$PL360IO CSECT
         SPACE
         MACRO
&EP      ENTER
         ENTRY   &EP
         USING   &EP,15
&EP      STM     12,2,SAVE             SAVE REGISTERS
         L       12,=A($PL360IO)       ESTABLISH ADDRESSING
         DROP    15
         USING   $PL360IO,12
         MEND
         SPACE
         MACRO
&L       EXIT
&L       LM      12,2,SAVE             RESTORE REGISTERS
         BR      14
         DROP    12
         MEND
         SPACE   2
         PUSH    PRINT
         PRINT   NOGEN
         CVT     DSECT=YES             CVT mapping
         DCBD    ,                     DCB mapping
         IHASDWA ,                     SDWA mapping
         POP     PRINT
*
DOPEN    EQU     DCBOFLGS-IHADCB       BYTE SET BY OPEN
OPENMASK EQU     X'10'
OFLGS    EQU     DCBOFLGS-IHADCB
*
BLKSZ    EQU     62
LRECL    EQU     82
LINELEN  EQU     133                   SYSOUT LINE SIZE
LINESMAX EQU     66                    SYSOUT LINES/PAGE
         TITLE   'PL360 I/O Routines -- CANCEL'
$PL360IO CSECT
*
*   GLOBAL PROCEDURE CANCEL(R14)
*        (R13) = SAVE AREA ADDRESS
*        (R14) = RETURN ADDRESS
CANCEL   ENTER
         TM      SYSIN+DOPEN,OPENMASK  TEST FOR OPEN DCB
         BNO     CANCEL1
         CLOSE   (SYSIN,LEAVE)
CANCEL1  TM      SYSPUNCH+DOPEN,OPENMASK TEST FOR OPEN DCB
         BNO     CANCEL2
         CLOSE   (SYSPUNCH,LEAVE)
CANCEL2  TM      SYSOUT+DOPEN,OPENMASK TEST FOR OPEN DATA SET
         BNO     CANCEL3
         CLOSE   (SYSOUT,LEAVE)
CANCEL3  EXIT
         TITLE   'PL360 I/O Routines -- READ'
*
*   GLOBAL PROCEDURE READ(R14)
*        (R0)  = BUFFER ADDRESS
*        (R13) = SAVE AREA ADDRESS
*        (R14) = RETURN ADDRESS
READ     ENTER
         LR      2,0
         TM      SYSIN+DOPEN,OPENMASK  TEST FOR OPEN DCB
         BO      READ1
         OPEN    (SYSIN,(INPUT))       ISSUE OPEN
         TM      SYSIN+DOPEN,OPENMASK
         BNO     DIDNOPEN
READ1    LR      0,2
         CLI     EOF,X'FF'             TEST FOR PREVIOUS END-OF-FILE
         BNE     GET1
         ABEND   96,DUMP
GET1     GET     SYSIN,(0)             GET CARD
         CLI     REREAD,X'FF'          TEST FOR CONCATENATION
         BNE     READ2
         MVI     REREAD,0
         B       READ1                 REISSUE GET
READ2    CLI     EOF,0                 SET CONDITION CODE
         EXIT
         SPACE
*                  EOD EXIT ROUTINE                                   *
         USING   $PL360IO,12
ENDRDR   MVI     EOF,X'FF'             EODAD EXIT
         B       READ2
         DROP    12
         SPACE
*                  DCB EXIT ROUTINE                                   *
         SPACE
         DS      0F
ADCBEXIT DC      X'85',AL3(DCBEXIT)    DCB EXIT CODE AND ADDRESS
         USING   DCBEXIT,15
DCBEXIT  OI      SYSIN+OFLGS,X'08'     SET UNLIKE ATTRIBUTES BIT
         CLI     RDRSTAT,0             TEST FOR PREVIOUS ENTRY
         BNE     SETRETRY              YES - DATA SETS CONCATENATED
         MVI     RDRSTAT,X'FF'         NO - INDICATE ENTRY
         BR      14
SETRETRY MVI     REREAD,X'FF'          INDICATE GET MUST BE REISSUED
         BR      14
         DROP    15
         SPACE   2
BLKLST   DC      X'85',AL3(BLKXIT)
         USING   BLKXIT,15
BLKXIT   LH      2,LRECL(,1)
         LTR     2,2
         BNH     BLKLST0
         C       2,=A(256)
         BNH     BLKXIT0
BLKLST0  LA      2,LINELEN
         STH     2,LRECL(,1)
BLKXIT0  SR      2,2
         CH      2,BLKSZ(,1)  TEST IF BLKSIZE HAS BEEN SPECIFIED
         BNE     BLKXIT1  IF YES, THEN GET OUT
         LH      2,LRECL(1)   IF NO, SET IT EQUAL TO LRECL
         STH     2,BLKSZ(,1)
BLKXIT1  BR      14
         DROP    15
         TITLE   'PL360 I/O Routines -- WRITE'
*
*   GLOBAL PROCEDURE WRITE(R14)
*        (R0)  = BUFFER ADDRESS  (132 CHARACTERS)
*        (R13) = SAVE AREA ADDRESS
*        (R14) = RETURN ADDRESS
WRITE    ENTER
         LR      2,0
         TM      SYSOUT+DOPEN,OPENMASK TEST FOR OPEN DATA SET
         BO      WRITE0
         CLI     STAEFLAG,0            Is STAE exit set?
         BNE     WRNOSTAE              Yes, only do this once
         ESTAE   STAEEXIT,TERM=YES     Set STAE exit
         MVI     STAEFLAG,X'FF'        Only do this one
WRNOSTAE OPEN    (SYSOUT,(OUTPUT))
         TM      SYSOUT+DOPEN,OPENMASK
         BNO     DIDNOPEN
WRITE0   CLI     LINECNT,6
         BNL     WRITE1
         MVI     CARRCONT,C'1'
WRITE1   LA      1,OUTBUF              Build output line here
         MVC     0(1,1),CARRCONT       SUPPLY CONTROL CHARACTER
         CLI     CARRCONT,C'1'
         BNE     WRITE2
         MVI     LINECNT,6             RESET LINE COUNT AND SPACING
         MVI     CARRCONT,C' '
WRITE2   IC      14,LINECNT            INCREMENT LINE COUNT
         LA      14,1(,14)
         STC     14,LINECNT
         CLI     LINECNT,LINESMAX      TEST FOR FULL PAGE
         BL      WRITE3
         MVI     CARRCONT,C'1'         SET SKIP
WRITE3   LH      14,SYSOUT+LRECL
         BCTR    14,0
         BCTR    14,0
         LTR     14,14
         BL      WRITE4
         LA      1,1(,1)
         EX      14,MOVIT              MVC 0(0,1),0(2)
WRITE4   PUT     SYSOUT,OUTBUF         Write next line
         EXIT
*
MOVIT    MVC     0(0,1),0(2)           (Executed)
         TITLE   'PL360 I/O Routines -- PAGE'
*
*   GLOBAL PROCEDURE PAGE(R14)
*        (R14) = RETURN ADDRESS
         ENTRY   PAGE
         USING   PAGE,15
PAGE     MVI     CARRCONT,C'1'         SET
         BR      14
         EJECT
         TITLE   'PL360 I/O Routines -- PUNCH'
*
*   GLOBAL PROCEDURE PUNCH(R14)
*        (R0)  = BUFFER ADDRESS
*        (R13) = SAVE AREA ADDRESS
*        (R14) = RETURN ADDRESS
PUNCH    ENTER
         TM      SYSPUNCH+DOPEN,OPENMASK
         BO      PUNCH1
         LR      2,0
         CLI     STAEFLAG,0            Is STAE exit set?
         BNE     PUNOSTAE              Yes, only do this once
         ESTAE   STAEEXIT,TERM=YES     Set STAE exit
         MVI     STAEFLAG,X'FF'        Only do this one
PUNOSTAE OPEN    (SYSPUNCH,(OUTPUT))
         TM      SYSPUNCH+DOPEN,OPENMASK
         BNO     DIDNOPEN
         LR      0,2
PUNCH1   PUT     SYSPUNCH,(0)          PUT CARD IMAGE
         EXIT
         TITLE   'PL360 I/O Routines -- PRINT'
*
*  GLOBAL PROCEDURE  PRINT (R14)
*      (R0) = BUFFER ADDRESS  (133 CHARACTERS)
*      (R13) = SAVE AREA ADDRESS
*      (R14) = RETURN ADDRESS
         SPACE
PRINT    ENTER
         LR      2,0
         TM      SYSOUT+DOPEN,OPENMASK
         BO      PRINT1
         CLI     STAEFLAG,0            Is STAE exit set?
         BNE     PRNOSTAE              Yes, only do this once
         ESTAE   STAEEXIT,TERM=YES     Set STAE exit
         MVI     STAEFLAG,X'FF'        Only do this one
PRNOSTAE OPEN    (SYSOUT,(OUTPUT))
         TM      SYSOUT+DOPEN,OPENMASK
         BNO     DIDNOPEN
PRINT1   PUT     SYSOUT,(2)            Write the line
         SPACE
*  CARRCONT RESULTING FROM A CALL ON PAGE DOES NOT AFFECT PRINT
         SPACE
         IC      2,LINECNT
         LA      2,1(,2)
         STC     2,LINECNT
         CLI     LINECNT,LINESMAX      TEST FOR FULL PAGE
         BL      PRINT3
         MVI     LINECNT,X'00'
PRINT3   EXIT
*
         USING   $PL360IO,12
DIDNOPEN ABEND   95,DUMP
         DROP    12
         TITLE   'PL360 I/O Routines -- STAE exit'
*
*  STAEEXIT -- STAE exit is entered as an exit routine from MVS when
*    we are abending.
*
*    The STAE exit tries to normally close the SYSPRINT and SYSPUNCH
*    DCBs so that the last buffer gets written.
*
         USING   STAEEXIT,15
STAEEXIT STM     14,12,12(13)          OS linkage
         L       12,=A($PL360IO)
         DROP    15
         USING   $PL360IO,12
         LR      2,1                   Save SDWA ptr
         TM      SYSPUNCH+DOPEN,OPENMASK TEST FOR OPEN DCB
         BNO     STNOPUN
         CLOSE   (SYSPUNCH)
STNOPUN  TM      SYSOUT+DOPEN,OPENMASK TEST FOR OPEN DATA SET
         BNO     STNOOUT
         CLOSE   (SYSOUT)
STNOOUT  LR      1,2                   Needed for SETRP
         SETRP   RC=0                  Continue abending
         LM      14,12,12(13)          Restore regs
         SLR     15,15                 No retries
         BR      14
         TITLE   'PL360 I/O Routines -- Data areas'
SYSIN    DCB   DSORG=PS,MACRF=GM,DDNAME=SYSIN,DEVD=DA,RECFM=FB,        X
               LRECL=80,EODAD=ENDRDR,EXLST=ADCBEXIT
SYSOUT   DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,DEVD=DA,RECFM=FBA,    X
               EXLST=BLKLST
SYSPUNCH DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPUNCH,DEVD=DA,RECFM=FB,     X
               LRECL=80,EXLST=BLKLST
SAVE     DS      7F
STAEFLAG DC      X'00'                 Set if STAE initialized
EOF      DC      X'00'
RDRSTAT  DC      X'00'                 SET IF DCB PREVIOUSLY OPEN
REREAD   DC      X'00'                 SET IF GET MUST BE REISSUED
LINECNT  DC      X'03'                 SYSOUT LINE COUNT
CARRCONT DC      C' '                  SYSOUT CARRIAGE CONTROL
*
OUTBUF   DS      CL256                 Working PUT buffer
         END
