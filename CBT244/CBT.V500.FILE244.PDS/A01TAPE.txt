LEBTAPE  CSECT
         PRINT NOGEN
*  LEBTAPE - UTILITY TO REPORT ON CONTENTS OF A MAGNETIC TAPE
*  OR MULTIVOLUME SET OF TAPES.
* LISTS TAPE MARKS,TAPE LABELS,HEADER,TRAILER AND EOV LABELS,
*  AND COUNTS OF DATA BLOCKS
*  NEIL MERCHANT DEC 77
*
         REGS  ,
         STM   R14,R12,12(R13)
         BALR  R12,0               ESTABLISH BASE.
         USING HERE,R12,R7
HERE     L     R7,BASE
         B     BEGIN
BASE     DC    A(HERE+4096)
BEGIN    LA    R2,SAVE
         ST    R2,8(,R13)          FORWARD CHAIN
         ST    R13,4(,R2)          BACKWARD CHAIN
         LR    R13,R2
         B     START
         DC    C'LEBTAPE '
SAVE     DS    18F
*
START    EQU   *
         MVC   LNCNT,FULL
         ZAP   PCNT,ZERO
         OPEN  (SYSPRINT,OUTPUT)
         TM    SYSPRINT+48,X10     CHECK FOR SUCCESSFUL OPEN
         BO    PRINTOPN
         WTO   'LEBTAPE - SYSPRINT DD STATEMENT MISSING',ROUTCDE=11,DES*
               C=6
         ABEND 12
*                             READ JFCB FOR TAPE
PRINTOPN EQU   *
         RDJFCB (TAPE)
         LTR   R15,R15
         BZ    JFCBIN
         WTO   'LEBTAPE - RDJFCB ERROR',ROUTCDE=11,DESC=6
         ABEND 16,DUMP
*                             MODIFY TAPE JFCB
JFCBIN   EQU   *
         OI    JFCBMASK+4,X80      FORCE WRITING BACK
         OI    JFCBMASK+4,X01      BYPASS SECURITY PROCESSING
         OI    JFCBLTYP,X10        SET LABEL PROCESSING
         NI    JFCBLTYP,XF0         TO BLP
         IC    R3,JFCBNVOL         REDUCE VOLUME COUNT
         BCTR  R3,0                BY ONE FOR MULTIVOLUME
         STC   R3,JFCBNVOL         CHECKING LATER
*
         OPEN  (TAPE,INPUT),TYPE=J OPEN TAPE DCB USING MODIFIED
*                                  JCB
         TM    TAPE+48,X10
         BO    TAPEOPN
         WTO   'LEBTAPE - SYSUT1 OPEN ERROR',ROUTCDE=11,DESC=6
         ABEND 20,DUMP
*
TAPEOPN  EQU   *
         BAL   R11,PRIME           INITIALIZE PRINT LINE
*
REWIND   EQU   *                   REWIND TO LOAD POINT
         XC    TECB,TECB           IF NECESSARY
         LA    R1,SEN1        SENSE TO CHECK FOR LOAD POINT
         STCM  R1,7,IOBSTART
         LA    R1,TIOB
         EXCP  (R1)
*
         WAIT  ECB=TECB
         TM    SENS1+1,X08         IF AT LOAD POINT,
         BO    RBLK                SKIP REWIND
*
         LA    R1,REW
         STCM  R1,7,IOBSTART
         LA    R1,TIOB
*
         EXCP  (R1)                ISSUE REWIND COMMAND
         WAIT  ECB=TECB
*
         CLI   IOBECBCC,X7F        CHECK ECB FOR SUCCESSFUL COMPLETION
         BNE   ABND
         SPACE 4
RBLK     EQU   *                   READ A BLOCK (NO DATA TRANSFER)
         XC    TECB,TECB           RESET ECB
         LA    R1,RDB
         STCM  R1,7,IOBSTART
         LA    R1,TIOB
*
         EXCP  (R1)
         WAIT  ECB=TECB
*
         CLI   IOBECBCC,X41
         BE    ECBOK
         CLI   IOBECBCC,X7F        CHECK ECB CC FOR COMPLETION OK
         BNE   ABND
*
ECBOK    EQU   *
         TM    IOBCSW+3,X01        CHECK FOR TAPEMARK (UNIT EXCEPTION)
         BO    TMK
*
         NI    TMFLG,X00           SET OFF TAPEMARK-JUST-READ FLAG,
*                                                               IF SET
         XR    R2,R2
         ICM   R2,03,IOBCSW+5      PICK UP RESIDUAL COUNT IN CSW
         C     R2,RLEN             WAS IT AN 80-BYTE RECORD?
         BE    MAYBELAB
UPCNT    EQU   *
         L     R2,RCOUNT           INCREMENT COUNT OF BLOCKS READ
         LA    R2,1(,R2)
         ST    R2,RCOUNT
         L     R3,MAXLEN
         SR    R2,R2
         ICM   R2,03,IOBCSW+5
         SR    R3,R2
         ST    R3,BLKLEN           SAVE LENGTH OF DATA BLOCK
*
         B     RBLK                GO TO READ NEXT BLOCK
TMK      EQU   *
         BAL   R8,BLKPRNT
         LA    R10,TMLINE
         LA    R9,3                LINECOUNT INCREMENT
         BAL   R11,PRINT           PRINT TAPEMARK LINE
*
         TM    HDRFLG,XFF
         BO    *+12           TWO TM'S AFTER HDR MEANS NULL FILE
*
         TM    TMFLG,XFF           TWO CONSECUTIVE TAPEMARKS
         BO    FINISH               INDICATES END OF LAST FILE ON TAPE
*
         OI    TMFLG,XFF           SET ON TAPEMARK-JUST-READ FLAG
*
         TM    EOVFLG,XFF          IF EOV ENCOUNTERED, CHECK JFCB
         BNO   RBLK                FOR NO OF VOLUMES SPECIFIED IN JCL,
*                                  AND ISSUE EOV MACRO TO FORCE VOLUME
         MVC   LNCNT,FULL          SWITCHING IF NON-ZERO
         SR    R3,R3
         IC    R3,JFCBNVOL         DECREMENT VOLUME COUNT
         LTR   R3,R3               STORED IN JFCB TO CONTROL
         BZ    ASKOP               MULTI-VOLUME PROCESSING
         BCTR  R3,0
         STC   R3,JFCBNVOL
*
         NI    TAPE+48,X33         FORCE DCBOFLGS SETTING
         OI    TAPE+48,X04         (SYS PROGS' DATA MANAGEMENT MANUAL)
*
         EOV   TAPE                FORCE END-OF-VOLUME PROCESSING
*
         NI    EOVFLG,X00          SET OFF EOV-LABEL-READ FLAG
*
         B     REWIND              GO TO PROCESS NEXT VOLUME
         SPACE 3
ASKOP    EQU   *                   REWIND AND UNLOAD TAPE
         XC    WECB,WECB           IF OPERATOR DECIDES
         WTOR  MF=(E,CONT)         TO CONTINUE WITH MORE
         WAIT  ECB=WECB            VOLUMES
*
         CLI   WREP,E
         BE    FINISH
         CLI   WREP,U
         BNE   ASKOP
*
         OI    IOBFLAG1,X40       SET ON COMMAND CHAINING BIT
         XC    TECB,TECB
         LA    R1,REWU
         STCM  R1,7,IOBSTART
         LA    R1,TIOB
         EXCP  (R1)
         WAIT  ECB=TECB
         NI    IOBFLAG1,XBF        SET OFF COMMAND CHAINING BIT
*                             OBTAIN CUU OF TAPE DRIVE FOR WTOR
         L     R1,TAPE+44          R1 -> DEB
         L     R1,32(,R1)          R1 -> UCB
         LH    R2,4(,R1)           PICK UP XCUU
         LA    R4,WTOCUU+2         ADDRESS END OF TARGET AREA
         LA    R5,XTAB             ADDRESS XLATION TABLE
         LA    R6,3                LOOP CONTROL ( C U U )
CUULP    EQU   *
         SRDL  R2,4                SHIFT ONE HEX DIGIT INTO R3
         SRL   R3,28               SHIFT IT TO THE LOW END
         IC    R1,0(R3,R5)         USE IT TO INDEX INTO XTAB
         STC   R1,0(,R4)           STORE CHARACTER IN TARGET AREA
         BCTR  R4,0                REDUCE TARGET POINTER
         BCT   R6,CUULP            LOOP AROUND
WTOR     EQU   *
         XC    WECB,WECB
         WTOR  ,,,,MF=(E,WLIST)    PROMPT OPERATOR TO MOUNT NEXT TAPE
         WAIT  ECB=WECB
*
*
         CLI   WREP,U
         BNE   WTOR                WRONG REPLY,SO GO
*                                  ROUND AGAIN
         NI    EOVFLG,X00          SET OFF EOV FLAG
         B     REWIND
         SPACE 4
*
MAYBELAB EQU   *                   LAST BLOCK READ (NO DATA TRANSFER)
*                                  WAS 80 BYTES LONG, SO MAY HAVE BEEN
*                                  A LABEL ..... SO BACKSPACE AND
         OI    IOBFLAG1,X40        READ IT AGAIN PROPERLY TO CHECK
         LA    R1,BSRD             (USING COMMAND CHAINING)
         STCM  R1,7,IOBSTART
         XC    TECB,TECB
         LA    R1,TIOB
         EXCP  (R1)
         WAIT  ECB=TECB
         NI    IOBFLAG1,XBF        SET OFF COMMAND CHAINING BIT
         TM    CODE,ASCII
         BNO   EBCDIC
         TR    LABAREA,TRTABLE    TRANSLATE FROM ASCII
EBCDIC   EQU   *
         CLC   TYPE,UHL
         BE    USRLAB              USER LABEL
         CLC   TYPE,UTL
         BE    USRLAB              USER LABEL
*
         CLC   TYPE(4),VOL1
         BE    VOLLABEL            VOLUME LABEL
*
         CLC   TYPE,HDR
         BNE   *+12
         OI    HDRFLG,XFF
         B     LABOK
         CLC   TYPE,EOV
         BNE   TRYEOF              EOV LABEL
         OI    EOVFLG,XFF          SET EOV-LABEL-READ FLAG
         B     LABOK
TRYEOF   EQU   *
         CLC   TYPE,EOF            EOF LABEL
         BNE   TRYASC             UNKNOWN SO TRY ASCII
         NI    HDRFLG,X00
*
LABOK    EQU   *
         CLI   ID,CONE             TYPE ONE
         BE    ONETYPE
         CLI   ID,CTWO             TYPE TWO
         BE    TWOTYPE
         B     TRYASC             UNKNOWN SO TRY ASCII
         SPACE 4
TRYASC   EQU   *
         TR    LABAREA,TRTABLE
         CLC   TYPE(4),VOL1
         BNE   UPCNT
         OI    CODE,ASCII
         B     VOLLABEL
         SPACE 3
*
USRLAB   EQU   *              PROCESS USER LABELS
         BAL   R8,BLKPRNT          PRINT DATA BLOCK COUNT
         LA    R9,2
         LA    R10,UHLTL
         BAL   R11,PRINT           PRINT HEADING
*
         MVC   UTYPE,TYPE
         MVC   ULABEL,UDETAIL
         LA    R9,3
         LA    R10,UHLDTL
         BAL   R11,PRINT           PRINT LABEL
         B     RBLK
*
VOLLABEL EQU   *              PROCESS VOLUME LABELS
         BAL   R8,BLKPRNT          PRINT BLOCK COUNT
         LA    R9,2
         LA    R10,VOLHDR
         BAL   R11,PRINT           PRINT HEADING
*
         MVC   VOLIDV,VOLID
         MVC   OWNERV,OWNER
         LA    R9,3
         LA    R10,VOLLAB
         BAL   R11,PRINT           PRINT LABEL
         B     RBLK
ONETYPE  EQU   *              PROCESS TYPE ONE STANDARD LABELS
         BAL   R8,BLKPRNT          PRINT BLOCK COUNT
         LA    R9,2
         LA    R10,T1HDR
         BAL   R11,PRINT           PRINT HEADING
*
         MVC   HEE1,TYPE
         MVC   DSN1,DSN
         MVC   VOLSER1,VOLSER
         MVC   VOLSEQ1,VOLSEQ
         MVC   DSSEQ1,DSSEQ
         MVC   GDGNO1,GDGNO
         MVC   GDGVN1,GDGVN
         MVC   CRTDTE1,CRTDTE
         MVC   EXPDTE1,EXPDTE
         MVC   SECSTAT1,SECSTAT
         MVC   BLKS1,BLKS
*
         LA    R9,3
         LA    R10,LABONE
         BAL   R11,PRINT           PRINT LABEL DETAILS
         CLC   TYPE,EOF
         BE    CHKCOUNT
         CLC   TYPE,EOV
         BE    CHKCOUNT
         B     RBLK
*
CHKCOUNT EQU   *                 CHECK THAT BLOCK COUNTS MATCH
         CLC   STORERCT,FULLZERO   IF ACCUMULATED BLOCK COUNT ZERO
         BNE   B1                 THEN ENSURE EOF BLOCK COUNT THE SAME.
*
         CLC   BLKS1,CHARZERO
         BE    RBLK
         B     C1
B1       XC    STORERCT,STORERCT
         CLC   BLKNO+2(6),BLKS1
         BE    RBLK
*                                  IN EVENT OF MISMATCH PRINT MESSAGE.
C1       CLC   LNCNT,SIXTYTWO      SPACE TO PRINT MESSAGE?
         BL    A1
         MVC   LNCNT,FULL
A1       LA    R9,2
         LA    R10,MISMATCH
         MVI   CC1,X13
         BAL   R11,PRINT
         LA    R9,1
         MVI   CC1,X09
         MVC   MESFLD,OPFLDA
         BAL   R11,PRINT
         LA    R9,1
         MVI   MESFLD,X40
         MVC   MESFLD+1(77),MESFLD
         BAL   R11,PRINT
         LA    R9,1
         MVC   MESFLDA,OPFLDB
         BAL   R11,PRINT
         LA    R9,1
         MVI   MESFLD,X40
         MVC   MESFLD+1(77),MESFLD
         BAL   R11,PRINT
         LA    R9,3
         MVI   CC1,X19
         MVC   MESFLD,OPFLDA
         BAL   R11,PRINT
         B     RBLK
*
TWOTYPE  EQU   *                  PROCESS TYPE TWO STANDARD LABELS
         BAL   R8,BLKPRNT         PRINT BLOCK COUNT
         LA    R9,2
         LA    R10,T2HDR
         BAL   R11,PRINT           PRINT HEADING
*
         MVC   HEE2,TYPE
         MVC   RECFM2,RECFM
         MVC   BLKATT2,BLKATT
         MVC   BLKSZE2,BLKSZE
         MVC   RECSZE2,RECSZE
         MVC   TDEN2,TDEN
         MVC   VOLSWIT2,VOLSWIT
         MVC   JOBID2,JOBID
         MVC   RECTEC2,RECTEC
         MVC   CONCHAR2,CONCHAR
*
         LA    R9,3
         LA    R10,LABTWO
         BAL   R11,PRINT           PRINT LABEL DETAILS
         B     RBLK
         SPACE 4
*
BLKPRNT  EQU   *                   PRINT COUNT OF DATA BLOCKS
         L     R2,RCOUNT           AND ZEROIZE IT
         LTR   R2,R2
         BZR   R8
         CVD   R2,RCOUNTD
         MVC   BLKNO,BPAT
         ED    BLKNO,RCOUNTD+4
*
         L     R2,BLKLEN
         CVD   R2,LASTBLK
         MVC   LASTLEN,LPAT
         ED    LASTLEN,LASTBLK+5
*
         LA    R9,3
         LA    R10,BLKLINE
         BAL   R11,PRINT
         L     R2,RCOUNT
         ST    R2,STORERCT
         XC    RCOUNT,RCOUNT
         BR    R8
         SPACE 2
PRIME    EQU   *
         TIME  DEC                 ESTABLISH DATE AND TIME
         ST    R1,DTSTOR
         ST    R0,TMSTOR
         MVI   TMSTOR+3,SIGN
         SRP   TMSTOR,64-1,0
         ED    DATE,DTSTOR
         ED    TIME,TMSTOR
         BR    R11                 RETURN
         SPACE 2
PRINT    EQU   *
         CLC   LNCNT,FULL          CHECK FOR NEW PAGE REQUIRED
         BL    ONPAGE
*
         PUT   SYSPRINT,SKIP       SKIP TO NEW PAGE
         MVC   LNCNT,THREE
         AP    PCNT,ONE            PAGE NUMBER
         MVC   PNO,PPAT
         ED    PNO,PCNT
         PUT   SYSPRINT,PROG       PRINT HEADING LINE
         TM    CODE,ASCII
         BNO   NOTASC
         PUT   SYSPRINT,ASCIILN
NOTASC   EQU   *
ONPAGE   EQU   *
         A     R9,LNCNT
         ST    R9,LNCNT
         PUT   SYSPRINT,(R10)      PRINT ITEM LINE
         BR    R11
         SPACE 2
ABND     EQU   *
         WTO   'LEBTAPE - I/O ERROR ON TAPE',ROUTCDE=11,DESC=6
         ABEND 32,DUMP
         SPACE 2
ENDT     EQU   *
         B     ASKOP
FINISH   EQU   *
         BAL   R8,BLKPRNT
         CLOSE (TAPE,,SYSPRINT)
         LA    R13,SAVE
         L     R13,4(,R13)
         LM    R14,R12,12(R13)
         XR    R15,R15
         BR    R14
         EJECT
*
*  STORAGE,CONSTANTS,PRINT LINES,EQUATES,LITERAL POOL,CONTROL BLOCKS
*  AND CCW'S FOLLOW
*
*
*              ASSORTED STORAGE AREAS
*
LASTBLK  DS    D
RCOUNTD  DS    D
STORERCT DC    F'0'
DTSTOR   DS    F
TMSTOR   DS    F
LNCNT    DS    F
RCOUNT   DS    F
BLKLEN   DS    F
PCNT     DS    PL2
SENS1    DC    H'0'
         SPACE 2
*
*              ASSORTED CONSTANTS AND FLAGS
*
BPAT     DC    X'4021202020202020'
PPAT     DC    X'40212020'
LPAT     DC    X'402120202020'
FULLZERO DC    F'0'
SIXTYTWO DC    F'62'
FULL     DC    F'72'
ONE      DC    PL1'1'
THREE    DC    F'3'
RLEN     DC    A(32767-80)
MAXLEN   DC    A(32767)
VOL1     DC    C'VOL1'
HDR      DC    C'HDR'
UHL      DC    C'UHL'
EOF      DC    C'EOF'
UTL      DC    C'UTL'
EOV      DC    C'EOV'
*
EOVFLG   DC    X'00'
TMFLG    DC    X'00'
HDRFLG   DC    X'00'
CODE     DC    X'00'
*
XTAB     DC    C'0123456789ABCDEF'
CHARZERO DC    CL6'000000'
ZERO     DC    PL2'0'
         SPACE 2
*
*              CONTROL BLOCKS FOR EXCP PROCESSING
*
TAPE     DCB   DDNAME=SYSUT1,                                          *
               MACRF=(E),                                              *
               EXLST=LSTA,                                             *
               EODAD=ENDT,                                             *
               DSORG=PS,                                               *
               IOBAD=TIOB,                                             *
               DEVD=TA
*
LSTA     DS    0F
         DC    X'87'
         DC    AL3(JFCBAREA)
JFCBAREA DS    64F
         ORG   JFCBAREA+52
JFCBTSDM DS    X
         ORG   JFCBAREA+66
JFCBLTYP DS    X
         ORG   JFCBAREA+72
JFCBMASK DS    5X
         ORG   JFCBAREA+117
JFCBNVOL DS    X
         ORG   JFCBAREA+174
JFCBVLCT DS    X
         ORG   ,
         SPACE 2
TIOB     DS    0CL32               IOB FOR TAPE DCB
IOBFLAG1 DC    X'02'
IOBFLAG2 DC    X'00'
IOBSENS0 DC    X'00'
IOBSENS1 DC    X'00'
IOBECBCC DC    X'00'
IOBECBPT DC    AL3(TECB)
IOBFLAG3 DC    X'00'
IOBCSW   DC    XL7'00000000000000'
IOBSIOCC DC    X'00'
IOBSTART DC    AL3(0)
         DC    X'00'
IOBDCBPT DC    AL3(TAPE)
IOBRESTR DC    AL4(0)
IOBINCAM DC    AL2(0)
IOBERRCT DC    AL2(0)
         SPACE 2
TECB     DC    F'0'                ECB FOR TAPE IOB
         SPACE 2
*
*              OTHER CONTROL BLOCKS
*
SYSPRINT DCB   DDNAME=SYSPRINT,                                        *
               MACRF=(PM),                                             *
               DSORG=PS,                                               *
               RECFM=FM,                                               *
               BLKSIZE=121
         SPACE 2
WLIST    DS    0F
         DC    AL1(1)
         DC    AL3(WREP)
         DC    A(WECB)
         DC    AL2(55)             LENGTH
         DC    X'8000'             MCS FLAGS
         DC    C'LEBTAPE - MOUNT NEXT VOLUME ON '
WTOCUU   DC    C'   '
         DC    C' '
         DC    C'AND THEN REPLY U'
         DC    X'0400'             DESC CODES
         DC    X'0020'             ROUTE CODES
         SPACE 2
CONT     DS    0F
         DC    AL1(1)
         DC    AL3(WREP)
         DC    A(WECB)
         DC    AL2(86)
         DC    X'8000'
         DC    C'LEBTAPE - EOV ON THIS VOLUME.REPLY E TO END,OR U TO CO*
               NTINUE WITH A FURTHER VOLUME'
         DC    X'0400'
         DC    X'0020'
         SPACE 3
WECB     DC    F'0'
WREP     DC    C' '
*
*
*
         SPACE 2
*
*              CCW'S
*
         DS    0D
REW      CCW   X07,0,X30,X01       REWIND TO LOAD POINT
*
RDB      CCW   X02,0,X30,32767     FORWARD SPACE A BLOCK (NDT,SILI)
*
BSRD     CCW   X27,0,X70,X01       BACKSPACE A BLOCK AND
         CCW   X02,LABAREA,X20,32767   READ IT (SILI)
*
REWU     CCW   X0F,0,X70,X01       REWIND/UNLOAD AND NOP
         CCW   X03,0,X20,X01
*
SEN1     CCW   X04,SENS1,X20,X02
*
         SPACE 2
*
*              ASSORTED EQUATES
*
X00      EQU   X'00'
X01      EQU   X'01'
X02      EQU   X'02'
X03      EQU   X'03'
X04      EQU   X'04'
X07      EQU   X'07'
X08      EQU   X'08'
X09      EQU   X'09'
X0F      EQU   X'0F'
X10      EQU   X'10'
X13      EQU   X'13'
X19      EQU   X'19'
X20      EQU   X'20'
X27      EQU   X'27'
X30      EQU   X'30'
X33      EQU   X'33'
X37      EQU   X'37'
X40      EQU   X'40'
X41      EQU   X'41'
X70      EQU   X'70'
X7F      EQU   X'7F'
X80      EQU   X'80'
X82      EQU   X'82'
XBF      EQU   X'BF'
XF0      EQU   X'F0'
XFF      EQU   X'FF'
*
SIGN     EQU   X'0F'
*
CONE     EQU   C'1'
CTWO     EQU   C'2'
E        EQU   C'E'
U        EQU   C'U'
ASCII    EQU   X'FF'
         SPACE 2
*                   IOAREA
*
*
LABAREA  DS    0CL80
TYPE     DS    CL3
ID       DS    C
         DS    CL76
         ORG   LABAREA        IBM STANDARD VOL1 LABEL
         DS    CL4
VOLID    DS    CL6
         DS    CL31
OWNER    DS    CL10
         DS    CL29
         ORG   LABAREA        IBM STANDARD HDR1/EOV1/EOF1 LABEL
         DS    CL4
DSN      DS    CL17
VOLSER   DS    CL6
VOLSEQ   DS    CL4
DSSEQ    DS    CL4
GDGNO    DS    CL4
GDGVN    DS    CL2
CRTDTE   DS    CL6
EXPDTE   DS    CL6
SECSTAT  DS    C
BLKS     DS    CL6
SYSCODE  DS    CL13
         DS    CL7
         ORG   LABAREA        IBM STANDARD HDR2/EOV2/EOF2 LABEL
         DS    CL4
RECFM    DS    C
BLKSZE   DS    CL5
RECSZE   DS    CL5
TDEN     DS    C
VOLSWIT  DS    C
JOBID    DS    CL17
RECTEC   DS    CL2
CONCHAR  DS    C
         DS    C
BLKATT   DS    C
         DS    CL8
         DS    C
         DS    CL32
         ORG   LABAREA        IBM STANDARD USER LABEL
         DS    CL4
UDETAIL  DS    CL76
         SPACE 2
*
*              ASSORTED PRINT LINES
*
SKIP     DS    0CL121
         DC    X'8B'
         SPACE 2
PROG     DS    0CL121
         DC    X'19'
         DC    CL90'LEBTAPE - MAGNETIC TAPE CONTENTS ANALYSIS'
DATE     DC    X'40202120204B202020'
         DC    C' '
TIME     DC    X'402120204B20204B2020'
         DC    C'  PAGE'
PNO      DS    CL4
         SPACE 2
TMLINE   DS    0CL121
         DC    X'19'
         DC    C'*** TM ***'
         DC    108C' '
         DC    2C' '
         SPACE 2
BLKLINE  DS    0CL121         PRINT LINE FOR NUMBER OF DATA BLOCKS
         DC    X'19'
         DC    20C'*'
BLKNO    DS    CL8
         DC    C'  DATA BLOCKS  '
         DC    C'*'
         DC    C'  LAST BLOCK'
LASTLEN  DS    CL6
         DC    C' BYTES LONG'
         DC    47C'*'
         SPACE 2
UHLTL    DS    0CL121
         DC    X'11'
         DC    10C' '
         DC    CL110' USER LABEL'
         SPACE 2
UHLDTL   DS    0CL121
         DC    X'19'
         DC    C'** '
UTYPE    DS    CL4
         DC    C' **'
ULABEL   DS    CL76
         DC    44C' '
         SPACE 2
VOLHDR   DS    0CL121              VOLUME LABEL HEADER
         DC    X'11'
         DC    11C' '
         DC    C'VOL ID'
         DC    13C' '
         DC    C'OWNER ID  '
         DC    82C' '
         SPACE 2
T1HDR    DS    0CL121              TYPE ONE LABEL (HDR/EOV/EOF) HEADER
         DC    X'11'
         DC    11C' '
         DC    C'DATA SET NAME     '
         DC    C'VOLSER  '
         DC    C'VOLSEQ  '
         DC    C'DSSEQ  '
         DC    C'GDGGNO  '
         DC    C'GDGVNO  '
         DC    C'CRTDTE  '
         DC    C'EXPDTE  '
         DC    C'SEC  '
         DC    C'BLK CNT  '
         DC    42C' '
MISMATCH DS    0CL121
CC1      DS    X
         DC    20C' '
         DC    C'/'
MESFLD   DS    0CL78
         DS    CL29
MESFLDA  DS    CL20
         DS    CL29
         DC    C'/'
         DC    20C' '
OPFLDA   DC    78C'/'
OPFLDB   DC    C'BLOCK COUNT MISMATCH'
         SPACE 2
T2HDR    DS    0CL121              TYPE TWO LABEL (HDR/EOV/EOF) HEADER
         DC    X'11'
         DC    11C' '
         DC    C'RECFM   '
         DC    C'BLKSIZE '
         DC    C'LRECL  '
         DC    C'DEN     '
         DC    C'VOLSWIT     '
         DC    C'JOBID            '
         DC    C'RECTEQ   '
         DC    C'CC'
         DC    38C' '
         SPACE 2
VOLLAB   DS    0CL121              VOLUME LABEL DETAIL
         DC    X'19'
         DC    C'** VOL1 ** '
VOLIDV   DS    CL6
         DC    13C' '
OWNERV   DS    CL10
         DC    80C' '
         SPACE 2
LABONE   DS    0CL121              TYPE ONE (HDR/EOV/EOF) LABEL DETAIL
         DC    X'19'
         DC    C'** '
HEE1     DS    CL4
         DC    C' **'
         DC    C' '
DSN1     DS    CL17
         DC    C' '
VOLSER1  DS    CL6
         DC    3C' '
VOLSEQ1  DS    CL4
         DC    3C' '
DSSEQ1   DS    CL4
         DC    3C' '
GDGNO1   DS    CL4
         DC    4C' '
GDGVN1   DS    CL2
         DC    6C' '
CRTDTE1  DS    CL6
         DC    2C' '
EXPDTE1  DS    CL6
         DC    3C' '
SECSTAT1 DS    C
         DC    4C' '
BLKS1    DS    CL6
         DC    24C' '
         SPACE 2
LABTWO   DS    0CL121              TYPE TWO (HDR/EOV/EOF) LABEL DETAIL
         DC    X'19'
         DC    C'** '
HEE2     DS    CL4
         DC    C' **'
         DC    C'  '
RECFM2   DS    C
BLKATT2  DS    C
         DC    6C' '
BLKSZE2  DS    CL5
         DC    2C' '
RECSZE2  DS    CL5
         DC    3C' '
TDEN2    DS    C
         DC    9C' '
VOLSWIT2 DS    C
         DC    4C' '
JOBID2   DS    CL17
         DC    6C' '
RECTEC2  DS    CL2
         DC    5C' '
CONCHAR2 DS    C
         DC    39C' '
TRTABLE  DC    256X'40'           UNPRINTABLE TO BLANKS
         ORG   TRTABLE+33
         DC    C'!"#Â¢%',X'507D'
         DC    C'()*+,-./0123456789:;<=>?@ABCDEFGHIJKLMNOPQRSTUVWXYZ'
         ORG
ASCIILN  DS    0CL121
         DC    X'11'
         DC    CL120'    **** ASCII TAPE BEING PROCESSED ****'
         SPACE 2
         LTORG ,
         END   LEBTAPE
