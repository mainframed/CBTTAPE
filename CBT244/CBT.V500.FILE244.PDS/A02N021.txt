    PROC 0 JOBOPT() TEST DEBUG
/**********************************************************************
/* EDIT MACRO LDGN021
/* Will generate a Job card
/*
/*--------------------------------------------------------------------*/
/* Amended by
/* Amended by NS on 20.10.88 to remove old-style logonid processing.
/*                              Also improve documentation.
/* Completed by NS in Dec. 87
/*
/*--------------------------------------------------------------------*/
/* Members used:-                Purpose:-
/*   Macros:-
/*     SG1.LPF.CLIST(JOBC)         Validates/defaults KEEP option,
/*                                   calls LDGN021
/*
/*     SG1.LPF.CLIST(LDGN021)      Calls LDGN0211 if KEEP option on
/*              (this member)        and the cursor is at start of
/*                                   member, then creates JOB card.
/*
/*     SG1.LPF.CLIST(LDGN0211)     Looks for existing JOB card and
/*                                   extracts values for new card.
/*
/*   Data panels:-
/*     SG1.LPF.PANELS(LPGN021)     Data entry
/*
/*   Help Panels:-
/*     SG1.LPF.PANELS(LHGN021)     Tutorial menu
/*     SG1.LPF.PANELS(LHGN0211-4)  Explain fields in panel
/*     SG1.LPF.PANELS(LHGN021A-H)  General info
/*
/*     SG1.LPF.PANELS(LHGN021Z)    Application codes
/*     SG1.LPF.PANELS(LHGN021Y)    Job classes
/*     SG1.LPF.PANELS(LHGN021X)    Message classes
/*     SG1.LPF.PANELS(LHGN021W)    New Jobname standards
/*
/*   Messages:-
/*     SG1.LPF.MSGS(LMGN02)        All messages
/*
/**********************************************************************
    CONTROL         END(ENDO)          /*   DEFAULTS ARE:  MSG NOLIST*/+
            PROMPT  MSG
    IF &TEST = TEST THEN +
      CONTROL LIST
    IF &DEBUG = DEBUG THEN +
      CONTROL LIST CONLIST SYMLIST
    ISPEXEC CONTROL ERRORS RETURN

/*--------------------------------------------------------------------*/
/* Set some function variables.
/*--------------------------------------------------------------------*/
    SET &TIME     = &SUBSTR(1:5,&SYSTIME)
    SET &INDATE   = &SUBSTR(4:6,&SYSDATE)+
                    &SUBSTR(1:3,&SYSDATE)+
                    &SUBSTR(7:8,&SYSDATE)

    SET &DASH20 = &STR(--------------------)
    SET &DASH60 = &STR(&DASH20&DASH20&DASH20)
    SET &BLANKS = &STR(                                               -
                        .)
    SET &NULL   =
    SET &KEEPCC = 0

/*--------------------------------------------------------------------*/
/* Obtain DSN & Member names and CURSOR position
/*--------------------------------------------------------------------*/
    ISREDIT (DSN) = DATASET
    ISREDIT (MEM) = MEMBER
    IF &MEM  =      THEN +
      SET &DATASET = &DSN
    ELSE +
      SET &DATASET = &DSN(&MEM)

    ISREDIT (LINE COL) = CURSOR
    ISREDIT (SAVELINE SAVECOL) = CURSOR

/*--------------------------------------------------------------------*/
/* Obtain user's group                                                */
/*--------------------------------------------------------------------*/
    SET &GROUP = &SUBSTR(1,&SYSUID)&SUBSTR(3:4,&SYSUID)

/*--------------------------------------------------------------------*/
/* Try and retrieve info from old JOB Card                            */
/*--------------------------------------------------------------------*/

                                        /*----------------------------*/
                                        /* If cursor is at line 1 or  */
                                        /* less, and if JOBOPT =      */
                                        /* KEEP, call LDGN011 to try  */
                                        /* and retrieve info from     */
                                        /* current job card, if any.  */
                                        /*                            */
                                        /* Otherwise, set &JOBCLX     */
                                        /* which will get a blinking  */
                                        /* panel msg to warn user that*/
                                        /* the job card will not be   */
                                        /* inserted at the start of   */
                                        /* the member.                */
                                        /*----------------------------*/
    IF &LINE = 1      THEN +
      SET &LINE = 0
    SET &JOBCL2 = &LINE
    IF &LINE = 0      THEN +
      DO
      SET &JOBCLX = NO
      IF &JOBOPT = KEEP THEN +
        DO
        ISREDIT LABEL 1 = .L
        ISREDIT SEEK ' JOB '  .L .L FIRST 1 71
        IF &LASTCC = 0 THEN +
          DO
          %LDGN0211 &TEST &DEBUG
          SET &KEEPCC = &LASTCC
          ENDO
        ENDO

                                        /*----------------------------*/
                                        /* Clist LDGN0211 tries to    */
                                        /* retrieve into variables:   */
                                        /* JOBCJ1 - last 4/5 of jobn  */
                                        /* JOBCA1 - account code      */
                                        /* JOBCA2 - application code  */
                                        /* JOBCP1 - pgmr name field   */
                                        /* JOBCC1 - job class         */
                                        /* JOBCM1 - message class     */
                                        /* JOBCR1 - region size       */
                                        /*----------------------------*/
      ENDO
    ELSE +
      DO
      SET &JOBCLX = YES
      ISPEXEC SETMSG MSG(LMGN021J)
      ENDO

/*--------------------------------------------------------------------*/
/* For SPG only: if REGION parm not already found, see if SMP/E is    */
/* involved.                                                          */
/*--------------------------------------------------------------------*/
    ISPEXEC VGET (JOBCR1)
    IF &GROUP = UTW +
     | &GROUP = UTX THEN +
      IF &JOBCR1 = &NULL THEN +
        DO
        ISREDIT SEEK GIMSMP WORD FIRST
        IF &LASTCC = 0 THEN +
          SET &JOBCR1 = &STR(SMP/E)
        ENDO

/*--------------------------------------------------------------------*/
/* Display panel
/*--------------------------------------------------------------------*/
    ISPEXEC DISPLAY PANEL(LPGN021)
    SET &RC = &LASTCC
    IF &RC = 8 THEN +
      DO
      ISREDIT CURSOR = &SAVELINE &SAVECOL
      EXIT CODE(0)
      ENDO
    IF &RC ¬= 0 THEN +
      DO
      SET &ZEDSMSG = &STR(BAD DISPLAY)
      SET &ZEDLMSG = &STR(RETURN CODE FROM DISPLAY WAS &RC)
      ISPEXEC SETMSG MSG(ISRZ001)
      EXIT CODE(12)
      ENDO

/*--------------------------------------------------------------------*/
/* Set up Job card
/*--------------------------------------------------------------------*/
                                        /*----------------------------*/
                                        /* Ensure "JOB" in right col. */
                                        /* and cursor to go to job-   */
                                        /* name if not full eight     */
                                        /* characters.                */
                                        /*----------------------------*/
    SET &BLKNO = 8 - &LENGTH(&STR(&JOBCJN))
    IF &BLKNO > 0 THEN +
      DO
      SET &JOBCJN = &STR(&JOBCJN.&SUBSTR(1:&BLKNO,&BLANKS))
      SET &COLC1 = 11 - &BLKNO
      ENDO
    ELSE +
      SET &COLC1 = 0

                                        /*----------------------------*/
                                        /* Ensure Pgmr Name field     */
                                        /* 20 chars, and cursor here  */
                                        /* if all blanks, and if not  */
                                        /* to jobname                 */
                                        /*----------------------------*/
    SET &BLKNO = 20 - &LENGTH(&STR(&JOBCP1))
    IF &BLKNO > 0 THEN +
      SET &JOBCP1 = &STR(&JOBCP1.&SUBSTR(1:&BLKNO,&BLANKS))

                                        /*----------------------------*/
                                        /* Set line as requested or   */
                                        /* original line (if not      */
                                        /* nulled out in panel).  As  */
                                        /* before, line1 -> line 0.   */
                                        /*----------------------------*/
    IF &JOBCL2 ¬= THEN +
      SET &LINE = &JOBCL2
    IF &LINE = 1 THEN +
      SET &LINE = 0

                                        /*----------------------------*/
                                        /* Set up job card            */
                                        /*----------------------------*/
    SET &LINE1 = &STR("//&JOBCJN JOB (&JOBCA1,&JOBCA2),")
    SET &LINE2 = &STR("//             '&JOBCP1',")
    IF &JOBCR1 ¬= &NULL +
      && &JOBCR1 ¬= &STR(Default) THEN +
      DO
      SET &JOBCRX = &STR(REGION=&STR(&JOBCR1),)
      ENDO
    ELSE +
      SET &JOBCRX = &NULL

    SET &LINE3 = &STR("//             CLASS=&JOBCC1,MSGCLASS=&JOBCM1,)
    SET &LINE3 = &STR(&LINE3&STR(&JOBCRX)")
    SET &LINE4 = &STR("//***          TYPRUN=HOLD,PRTY=1,")
    SET &LINE5 = &STR("//             NOTIFY=&SYSUID")
    SET &LINE6 = &STR("//* ")
    SET &LINE7 = &STR("//* REFER TO &DATASET")
    ISREDIT LINE_AFTER    &LINE  = DATALINE  &LINE1
    SET &LINE = &LINE + 1
    SET &LINEC1 = &LINE
    ISREDIT LINE_AFTER    &LINE  = DATALINE  &LINE2
    SET &LINE = &LINE + 1
    SET &LINEC2 = &LINE
    ISREDIT LINE_AFTER    &LINE  = DATALINE  &LINE3
    SET &LINE = &LINE + 1
    ISREDIT LINE_AFTER    &LINE  = DATALINE  &LINE4
    SET &LINE = &LINE + 1
    ISREDIT LINE_AFTER    &LINE  = DATALINE  &LINE5
    SET &LINE = &LINE + 1
    ISREDIT LINE_AFTER    &LINE  = DATALINE  &LINE6
    SET &LINE = &LINE + 1
    ISREDIT LINE_AFTER    &LINE  = DATALINE  &LINE7
    SET &LINE = &LINE + 1
    ISREDIT LINE_AFTER    &LINE  = DATALINE  &LINE6
    SET &LINE = &LINE + 1

                                        /*----------------------------*/
                                        /* For SPG/IMS add extra      */
                                        /* comments                   */
                                        /*----------------------------*/
    IF &GROUP = UTW +
                    THEN +
      DO
      SET &LINE8 = &STR("//*--------&DASH60")
      SET &LINE9 = &STR(//* JCL GENERATED FOR &SYSUID  AT  &TIME)
      SET &LINE9 = &STR("&LINE9  ON  &INDATE (&SYSJDATE)")
      SET &LINE10 = &STR(//* JOB SUBMITTED BY  &SYSUID  AT  HH:MM)
      SET &LINE10 = &STR("&LINE10  ON  &INDATE")
      ISREDIT LINE_AFTER  &LINE  = DATALINE  &LINE8
      SET &LINE = &LINE + 1
      ISREDIT LINE_AFTER  &LINE  = DATALINE  &LINE9
      SET &LINE = &LINE + 1
      ISREDIT LINE_AFTER  &LINE  = DATALINE  &LINE10
      SET &LINE = &LINE + 1
      ISREDIT LINE_AFTER  &LINE  = DATALINE  &LINE8
      SET &LINE = &LINE + 1
      ENDO

                                        /*----------------------------*/
                                        /* Locate new JOB Card.       */
                                        /* Set cursor to blank field, */
                                        /* if appropriate, else to    */
                                        /* line following (inserted   */
                                        /* if necessary).             */
                                        /*----------------------------*/
    IF &COLC1 >0 THEN +
      ISREDIT CURSOR = &LINEC1 &COLC1
    ELSE +
      IF &STR(&JOBCP1) = THEN +
        ISREDIT CURSOR = &LINEC2 17
      ELSE +
        DO
        ISREDIT (LASTLINE) = LINENUM .ZLAST
        IF &LINE < &LASTLINE THEN +
          DO
          SET &LINE = &LINE + 1
          IF &JOBOPT = KEEP && +
             &KEEPCC = 0 THEN +
            DO
            ISREDIT CURSOR = &LINE 0
            ENDO
          ELSE +
          ISREDIT CURSOR = &LINE 1
          ENDO
        ELSE +
          ISREDIT INSERT .ZLAST
        ENDO
                                        /*----------------------------*/
                                        /* Return to caller           */
                                        /*----------------------------*/

    ISPEXEC SETMSG MSG(LMGN021)
    EXIT CODE(0)

