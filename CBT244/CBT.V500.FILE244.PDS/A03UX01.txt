//SYSPCHL  JOB  '9999,C4003,HASPPRPU',
//       'PRPU',CLASS=H,PRTY=9,MSGCLASS=P,NOTIFY=TSOSYSH
//*
//* 6/1/92 - broadcast
//* 20/1/92 - " " " "  live
//*
//STEP1    EXEC PGM=IEV90,PARM='OBJECT,NODECK,XREF(SHORT)'
//SYSLIB   DD   DSN=DCC.MACLIB,DISP=SHR
//         DD   DSN=SYS1.AMODGEN,DISP=SHR
//         DD   DSN=SYS1.HASPSRC,DISP=SHR
//         DD   DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD   UNIT=SYSDA,SPACE=(1700,(1200,300))
//SYSUT2   DD   UNIT=SYSDA,SPACE=(1700,(1200,300))
//SYSUT3   DD   UNIT=SYSDA,SPACE=(1700,(1200,300))
//SYSPRINT DD   SYSOUT=*
//SYSLIN   DD   DSN=&&OBJ,DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSIN    DD   DDNAME=ASSMIN
//ASSMIN   DD   *
         TITLE 'APSUX01 - INSTALLATION EXIT JOB HEADER ROUTINE'
**** START OF SPECIFICATIONS *****************************************
*                                                                    *
*01* MODULE NAME = APSUX01                                           *
*                                                                    *
* $MOD(APSUX01 ) COMP(APS) PROD(PSF) : RELEASE 3.0                   *
*                                                                    *
*01* DESCRIPTIVE NAME = D.C.C. SEPARATOR, BORROWS FROM I.B.M.        *
*                                                                    *
*01* COPYRIGHT=5665-275 (C) COPYRIGHT IBM CORPORATION 1984, 1989     *
*      LICENSED MATERIAL - PROPERTY OF IBM                           *
*      SEE COPYRIGHT INSTRUCTIONS, G120-2083                         *
*                                                                    *
*01* STATUS = RELEASE 0003, LEVEL 0000                               *
*                                                                    *
*01* FUNCTION = DEFINES A JOB HEADER SEPARATOR PAGE WITH THE         *
*               JOBNAME IN BLOCK LETTERS AND FOLLOWED BY JOB         *
*               RELATED INFORMATION LINES.                           *
*               THIS EXIT WILL ALSO PUT OUT A CONTINUATION HEADER    *
*               PAGE, IF PRINTING OF A DATA SET IS INTERRUPTED BY    *
*               JES COMMAND, AND THEN RESUMES.  THE CONTINUATION     *
*               HEADER PAGE IS IDENTICAL TO THE START HEADER PAGE    *
*               EXCEPT THE CONTINUATION HEADER PAGE PRINTS THE WORD  *
*               'CONT' INSTEAD OF THE WORD OF 'START'.               *
*                                                                    *
* .CHDEC91      ALSO PRINTS THE DCC BROADCAST MESSAGE                *
*                                                                    *
*02*    OPERATION = THE FOLLOWING ITEMS ARE DONE BY THIS EXIT:       *
*               1. CREATE 12 BLOCK LETTER LINE RECORDS FOR           *
*                  THE JOB NAME.                                     *
*               2. PASS THE BLOCK LETTER LINES TO PSF.               *
*               3. BUILD JOB INFORMATION LINES.                      *
*               4. PASS THE INFORMATION LINE RECORD TO PSF.          *
*                                                                    *
*01* NOTES =                                                         *
*02*   DEPENDENCIES = NONE                                           *
*02*   RESTRICTIONS = NONE                                           *
*02*   REGISTER-CONVENTIONS =  R15 = ENTRY ADDRESS, RESET ON RETURN  *
*                              R13 = SAVE AREA ADDRESS               *
*                              R12 = BASE REGISTER                   *
*                           R4-R11 = SEE ASSEMBLER EQUATES BELOW     *
*02*   PATCH LABEL = PSPACE                                          *
*                                                                    *
*01* MODULE TYPE = PROCEDURE                                         *
*02*   PROCESSOR = ASSEMBLER                                         *
*02*   ATTRIBUTES = REENTRANT                                        *
*                                                                    *
*01* ENTRY POINT = APSUX01                                           *
*02*   LINKAGE = STANDARD CALL                                       *
*                 R1  = ADDRESS OF A 4 BYTE FIELD WHICH              *
*                       CONTAINS THE ADDRESS OF APSGEXTP             *
*                                                                    *
*01* INPUT =                                                         *
*             APSUCOM   -  PSF EXITS CONSTANTS TABLE                 *
*             APSGEXTP  -  PSF INSTALLATION EXIT PARAMETER AREA      *
*             APSUECA   -  PSF EXIT COMMUNICATIONS AREA              *
*             IAZJSPA   -  JES SEPARATOR PAGE AREA                   *
*             IEFJMR    -  MVS JOB MANAGEMENT RECORD                 *
*                                                                    *
*             IDUSERS   -  VSAM DATASET CONTAINING BROADCAST         *
*                                                                    *
*01* OUTPUT =                                                        *
*             LINE DATA RECORDS                                      *
*                                                                    *
*01* EXIT NORMAL = RETURN TO CALLER                                  *
*                                                                    *
*01* EXIT ERROR = NONE                                               *
*                                                                    *
*01* EXTERNAL REFERENCES =                                           *
*02*    ROUTINES =                                                   *
*             APSUBLK - BUILD BLOCK LETTER RECORDS                   *
*             APSUPUT - PUT RECORD TO PSF                            *
*02*    DATA AREAS =                                                 *
*             APSUCOM   -  PSF EXITS CONSTANTS TABLE                 *
*             APSGEXTP  -  PSF INSTALLATION EXIT PARAMETER AREA      *
*             APSUECA   -  PSF EXIT COMMUNICATIONS AREA              *
*             IAZJSPA   -  JES SEPARATOR PAGE AREA                   *
*             IEFJMR    -  MVS JOB MANAGEMENT RECORD                 *
*02*    INCLUDES = NONE                                              *
*                                                                    *
*01* MACROS = NONE                                                   *
*                                                                    *
*01* MESSAGES = NONE                                                 *
*                                                                    *
*01* CHANGE ACTIVITY =                                               *
* $H3=LAPS0003,HAF1220,032588,53KRSB: RELEASE 2.1                    *
* $01=OY12136C,HAF1220,032588,53KRSB: CONTINUATION SEPARATOR NOT     *
*                                     PRINTING                       *
* $L1=LAPS0004, HAF1228, 880601, B53KELJ: RELEASE 3.0                *
*                                                                    *
**** END OF SPECIFICATIONS *******************************************
APSUX01  START 0
         TITLE 'DSECT - XTP'
         APSGEXTP LIST=YES
         TITLE 'DSECT - ECA'
         APSUECA  LIST=YES
         TITLE 'DSECT - JSPA'
         IAZJSPA  LIST=YES
         TITLE 'DSECT - JMR'
         IEFJMR
         TITLE 'APSUX01 - INSTALLATION EXIT JOB HEADER ROUTINE'
APSUX01  CSECT ,
         USING *,@15
         B     PROLOG
         DC    AL1(16)
         DC    CL8'APSUX01'                                       @L1C
         DC    CL8'&SYSDATE'                                      @L1C
         DC    C'5665-275 (C) COPYRIGHT IBM CORP. 1984,1989'
         DC    C'LICENSED MATERIAL - PROGRAM PROPERTY OF IBM'
         DROP  @15
**********************************************************************
*        PROLOG
**********************************************************************
PROLOG   DS    0H
         STM   @14,@12,12(@13)              SAVE CALLERS REGISTERS
         LR    @12,@15                      R12 IS BASE REG
         USING APSUX01,@12                  BASE APSUX01 ON R12
         USING APSGEXTP,XTPPTR              BASE APSGEXTP ON XTPPTR
         USING APSUECA,ECAPTR               BASE APSUECA ON ECAPTR
         USING IAZJSPA,JSPAPTR              BASE IAZJSPA ON JSPAPTR
         USING JMR,JMRPTR                   BASE JMR ON JMRPTR
         L     @04,0(,@01)                  LOAD ADDRESS OF APSGEXTP
         L     @05,XTPECAP                  LOAD ADDRESS OF APSUECA
         LA    ECAWKPTR,ECAWKBUF            LOAD ADDRESS OF ECA WORK
*                                            BUFFER
         USING BUFWRK,ECAWKPTR              BASE WORK AREA ON ECA WORK
*                                            BUFFER
         LR    @02,@13                      LOAD ADDRESS OF CALLER SAVE
*                                            AREA
         LA    @13,ECAUSAVE                 R13 POINTS TO APSUX01
*                                            SAVE AREA
         ST    @02,4(,@13)                  SAVE CALLERS SAVE AREA ADDR
         SLR   RTNCODE,RTNCODE              RESET RETURN CODE
         XC    ECAFLAGS(2),ECAFLAGS         RESET ECAFLAGS
         L     JSPAPTR,XTPJSPAP             LOAD ADDRESS OF IAZJSPA
         L     JMRPTR,JSPAJMR               LOAD ADDRESS OF IEFJMR
         L     PUTPTR,ECAPUTP               LOAD ADDRESS OF APSUPUT
         NI    ECAFLAGS,B'10111111'         RESET LEFT ADJUST FLAG
         MVI   ECADRF,X'00'                 RESET RECORD TYPE FLAG
         OI    ECADRF,B'01010000'           SET LINE MODE AND MACHINE
*                                            CODE FLAGS
**********************************************************************
*        DCC CODE FOLLOWS - three blank lines
**********************************************************************
         LA    @02,3                        3 LINES
DCCLOOP1 DS    0H
         LA    @14,BUFWRK                   SET RECORD ADDRESS BACK TO
         ST    @14,ECARECAD                  BEGINNING OF WORK BUFFER
         MVC   ECARECLN(4),TWO              GET LENGTH OF RECORD
         MVI   WRKCC,X'09'                  SET CC TO WRITE
         MVC   PRINTPOS(1),BLANK            SET PRINTPOS TO BLANK
         LR    @15,PUTPTR                   CALL
         BALR  @14,@15                       APSUPUT
         LTR   RTNCODE,RTNCODE              IF APSUPUT FAILED
         BNZ   OUT                           EXIT
         BCT   @02,DCCLOOP1
**********************************************************************
*        DCC VARIABLE SECTION
**********************************************************************
         MVC   WRKDCC,DCCLINE4
         MVC   DCCJNAME,JSPAJBNM         OBTAIN JOB NAME
**********************************************************************
         LA    @14,BUFWRK                   SET RECORD ADDRESS BACK TO
         ST    @14,ECARECAD                  BEGINNING OF WORK BUFFER
         MVC   ECARECLN(4),DCCLLGTH         GET LENGTH OF RECORD
         LR    @15,PUTPTR                   CALL
         BALR  @14,@15                       APSUPUT
         LTR   RTNCODE,RTNCODE              IF APSUPUT FAILED
         BNZ   OUT                           EXIT
**********************************************************************
         MVC   WRKDCC,DCCLINE5
**       MVC   DCCJNAME,JSPAJBID         OBTAIN JOB ID
         MVC   DCCJNAME(8),BLANK            OBTAIN JOB ID
         MVC   DCCJNAME(1),JSPAJBID
         MVC   DCCJNAME+1(4),JSPAJBID+4
JUSTIF0  DS    0H
         CLI   DCCJNAME+1,C' '    IS THE FIRST CHARACTER BLANK
         BNE   CONTINU0           NO - DO NOT LEFT JUSTIFY
         MVC   DCCJNAME+1(4),DCCJNAME+2     LEFT JUSTIFY
         B     JUSTIF0
CONTINU0 DS    0H
**********************************************************************
         LA    @14,BUFWRK                   SET RECORD ADDRESS BACK TO
         ST    @14,ECARECAD                  BEGINNING OF WORK BUFFER
         MVC   ECARECLN(4),DCCLLGTH         GET LENGTH OF RECORD
         LR    @15,PUTPTR                   CALL
         BALR  @14,@15                       APSUPUT
         LTR   RTNCODE,RTNCODE              IF APSUPUT FAILED
         BNZ   OUT                           EXIT
**********************************************************************
         MVC   WRKDCC,DCCLINE6
**********************************************************************
*          OBTAIN TIME AND DATE
**********************************************************************
         LA    @01,2                        SET R1
         SLR   @00,@00                      RESET R0
         SVC   11                           ISSUE TIME SVC
         ST    @01,WRKDATE                  SAVE THE DATE (PACKED DEC)
**********************************************************************
*          OBTAIN MONTH, DAY AND YEAR
**********************************************************************
         LA    @01,4                        ADDRESSABILITY TO
         AL    @01,ECAUCOMP                  JULIAN TABLE
         MVC   WRKJTBL(48),DAYTBL(@01)      COPY TABLE FOR LEAP YEAR
*                                            ADJUSTMENT
         MVC   WRKWORK+4(4),WRKDATE         OBTAIN DATE FROM SAVED AREA
         TM    WRKWORK+5,X'01'              TEST
         BO    DOLEAPYR                      FOR
         TM    WRKWORK+5,X'12'               LEAP
         BM    DOLEAPYR                      YEAR
         MVI   WRKJTBL+4,29                 ADJUST FEB FOR LEAP YEAR
DOLEAPYR MVC   WRKYY(2),=X'2120'            PLACE PATTERN FOR EDIT
         ED    WRKYY(2),WRKWORK+5           EDIT THE YEAR
         MVC   WRKWORK(6),ZEROES            RESET ALL BUT JULIAN DATE
         UNPK  DCCJDATE,WRKWORK+6(2)        UNPACK YEAR
         OI    DCCJDATE+2,X'F0'             UNPACK YEAR
         SLR   @00,@00                      CLEAR FOR IC
         CVB   1,WRKWORK                    CONVERT TO BINARY DAY
         LA    2,WRKJTBL-4                  ADDRESS OF DATE CONVERSION
*                                            TABLE
DSEARCH  SLR   @01,@00                      CONVERT
         LA    @02,4(,@02)                   JULIAN DAY
         IC    @00,0(,@02)                   TO
         CLR   @00,@01                       STANDARD DAY
         BL    DSEARCH
         CVD   1,WRKWORK                    CONVERT TO DECIMAL DAY
         UNPK  WRKDD(2),WRKWORK+6(2)        UNPACK THE DAY
         OI    WRKDD+1,X'F0'                INSURE SIGN NIBBLE
         MVC   WRKMMM(3),1(@02)             SET EBCIDIC ALPHA MONTH
**********************************************************************
         MVC   DCCDTD,WRKDD
         MVC   DCCDTM,WRKMMM
         MVC   DCCDTY,WRKYY
**********************************************************************
         LA    @14,BUFWRK                   SET RECORD ADDRESS BACK TO
         ST    @14,ECARECAD                  BEGINNING OF WORK BUFFER
         MVC   ECARECLN(4),DCCLLGTH         GET LENGTH OF RECORD
         LR    @15,PUTPTR                   CALL
         BALR  @14,@15                       APSUPUT
         LTR   RTNCODE,RTNCODE              IF APSUPUT FAILED
         BNZ   OUT                           EXIT
**********************************************************************
         MVC   WRKDCC,DCCLINE7
**********************************************************************
*          OBTAIN TIME AND DATE
**********************************************************************
         LA    @01,2                        SET R1
         SLR   @00,@00                      RESET R0
         SVC   11                           ISSUE TIME SVC
         ST    @01,WRKDATE                  SAVE THE DATE (PACKED DEC)
**********************************************************************
*          ADJUST TIME FOR AM/PM
**********************************************************************
         LA    WRKPTR,WRKTIME               GET ADDRESS OF WORK AREA
         LA    AMPMPTR,DCCAMPM              GET ADDRESS OF AM/PM WORK
*                                            AREA
         MVC   DCCAMPM,AM                   SET AM/PM TO AM
         CL    @00,=X'12000000'             TEST FOR ZERO HOURS
         BL    DMORNING                     BRANCH IF AM
         MVI   0(@03),C'P'                  CHANGE FROM AM TO PM
         SL    @00,=X'12000000'             SUBTRACT TWELVE HOURS
DMORNING ST    @00,0(,@02)                  STORE ADJUSTED TIME
         CLI   0(@02),X'00'                 TEST FOR ZERO HOURS
         BNE   DADJERR                      BR IF NOT TO TEST ADJ ERR
         MVI   0(@02),X'12'                 CONVERT ZERO TO TWELVE
DADJERR  TM    0(@02),X'08'                 TEST FOR ADJUSTMENT ERRORS
         BZ    DEDTIME                      BRANCH IF NO ERROR
         NI    0(@02),X'09'                 CORRECT FOR BINARY
*                                            SUBSTRACT ERROR
DEDTIME  DS    0H
**********************************************************************
*          UNPACK HOURS MINUTES SECONDS
**********************************************************************
         MVI   WRKTH,X'0C'                  RESET LOW ORDER 2 BYTES
*                                            WITH SIGN FOR PACKED DEC
         UNPK  WRKUTIME(7),WRKTIME(4)       UNPACK TIME
         CLI   WRKHR,X'F0'                  IF HOUR HAS LEADING ZERO
         BNE   DNEXT
         MVC   WRKHR(1),BLANK               CHANGE ZERO TO BLANK
**********************************************************************
DNEXT    DS    0H
         MVC   DCCTIMH,WRKHR
         MVC   DCCTIMM,WRKMIN
         MVC   DCCTIMS,WRKSEC
**********************************************************************
         LA    @14,BUFWRK                   SET RECORD ADDRESS BACK TO
         ST    @14,ECARECAD                  BEGINNING OF WORK BUFFER
         MVC   ECARECLN(4),DCCLLGTH         GET LENGTH OF RECORD
         LR    @15,PUTPTR                   CALL
         BALR  @14,@15                       APSUPUT
         LTR   RTNCODE,RTNCODE              IF APSUPUT FAILED
         BNZ   OUT                           EXIT
**********************************************************************
*        DCC CODE FOLLOWS - TEN BLANK LINES
**********************************************************************
         LA    @02,10                      10 LINES
DCCLOOP2 DS    0H
         LA    @14,BUFWRK                   SET RECORD ADDRESS BACK TO
         ST    @14,ECARECAD                  BEGINNING OF WORK BUFFER
         MVC   ECARECLN(4),TWO              GET LENGTH OF RECORD
         MVI   WRKCC,X'09'                  SET CC TO WRITE
         MVC   PRINTPOS(1),BLANK            SET PRINTPOS TO BLANK
         LR    @15,PUTPTR                   CALL
         BALR  @14,@15                       APSUPUT
         LTR   RTNCODE,RTNCODE              IF APSUPUT FAILED
         BNZ   OUT                           EXIT
         BCT   @02,DCCLOOP2
**********************************************************************
*        PRINT THE JOB NAME IN STRAIGHT BLOCK LETTERS
**********************************************************************
         MVC   ECABLKIN(8),JSPAJBNM         OBTAIN JOB NAME
         NI    ECAFLAGS,B'01111111'         TURN OFF SLANT INDICATOR
         L     @15,BLKPTR                   CALL
         BALR  @14,@15                       APSUBLK
         MVC   ECARECLN(4),LINELGTH         SET LENGTH OF THE BLOCK
*                                            LETTER
         LA    @14,BUFWRK                   SET ADDRESS OF
         ST    @14,ECARECAD                  RECORD
         MVI   PRINTCC(@14),X'09'           SET CC IN PRINT RECORD
         LA    @14,1                        LOOP
         STH   @14,INDEX                     INDEX
LOOP     LTR   RTNCODE,RTNCODE              CHECK RC OF APSUPUT
         BNZ   OUT                          NOT ZERO, EXIT
         LR    @15,PUTPTR                   CALL
         BALR  @14,@15                       APSUPUT
         LA    @14,132                      GET NEXT
         AL    @14,ECARECAD                  RECORD
         ST    @14,ECARECAD                  TO PRINT
         MVI   PRINTCC(@14),X'09'           SET CC
         LA    @14,1                        CHECK IF
         AH    @14,INDEX                     PRINTED
         STH   @14,INDEX                     TWELVE
         C     @14,TWELVE                    LINES
         BNH   LOOP
         LTR   RTNCODE,RTNCODE              IF APSUPUT FAILED
         BNZ   OUT                           EXIT
**********************************************************************
*          PROVIDE 3 BLANK LINES BETWEEN BLOCK LETTER GROUPS
**********************************************************************
         LA    @14,BUFWRK                   SET RECORD ADDRESS BACK TO
         ST    @14,ECARECAD                  BEGINNING OF WORK BUFFER
         MVC   ECARECLN(4),TWO              GET LENGTH OF RECORD
         MVI   WRKCC,X'09'                  SET CC TO WRITE
         MVC   PRINTPOS(1),BLANK            SET PRINTPOS TO BLANK
         LA    @14,1                        LOOP
         STH   @14,INDEX                     INDEX
LOOP1    LTR   RTNCODE,RTNCODE              CHECK RC OF APSUPUT
         BNZ   OUT                          NOT ZERO, EXIT
         LR    @15,PUTPTR                   CALL
         BALR  @14,@15                       APSUPUT
         LA    @14,1                        CHECK IF
         AH    @14,INDEX                     PRINTED
         STH   @14,INDEX                     THREE
         C     @14,THREE                     LINES
         BNH   LOOP1
         LTR   RTNCODE,RTNCODE              IF APSUPUT FAILED
         BNZ   OUT                           EXIT
**********************************************************************
*          PROVIDE USER-ID LINE BETWEEN BLOCK LETTER GROUPS
**********************************************************************
         LA    @14,BUFWRK                   SET RECORD ADDRESS BACK TO
         ST    @14,ECARECAD                  BEGINNING OF WORK BUFFER
         MVC   ECARECLN(4),DCCLLGTH         GET LENGTH OF RECORD
         MVI   BUFPRT,C' '                SET
         MVC   BUFPRT+1(131),BUFPRT          PRINT LINE TO BLANK
         L     @15,JSPAUSR1                 ANY USER-ID
         CLC   ZEROES(4),0(@15)
         BE    NOUSER
         MVC   DCCUID,SUBED
         MVC   DCCTID,TERMID
         MVC   DCCUSER,0(@15)
         L     @15,JSPAUSR2                 ANY USER-ID
         MVC   DCCTERM,0(@15)
NOUSER   MVI   WRKCC,X'11'                  SET CC TO WRITE AND SPACE
         LR    @15,PUTPTR                   CALL
         BALR  @14,@15                       APSUPUT
         LTR   RTNCODE,RTNCODE              IF APSUPUT FAILED
         BNZ   OUT                           EXIT
**********************************************************************
*          PROVIDE DCC LINE BETWEEN BLOCK LETTER GROUPS
**********************************************************************
         LA    @14,BUFWRK                   SET RECORD ADDRESS BACK TO
         ST    @14,ECARECAD                  BEGINNING OF WORK BUFFER
         MVC   ECARECLN(4),DCCLLGTH         GET LENGTH OF RECORD
         MVC   WRKDCC,DCCJNO
         LR    @15,PUTPTR                   CALL
         BALR  @14,@15                       APSUPUT
         LTR   RTNCODE,RTNCODE              IF APSUPUT FAILED
         BNZ   OUT                           EXIT
**********************************************************************
*      PRINT THE JOB ID IN STRAIGHT BLOCK LETTERS
**********************************************************************
         MVC   ECABLKIN(8),BLANK            OBTAIN JOB ID
         MVC   ECABLKIN(1),JSPAJBID
         MVC   ECABLKIN+1(4),JSPAJBID+4
JUSTIFY  DS    0H
         CLI   ECABLKIN+1,C' '    IS THE FIRST CHARACTER BLANK
         BNE   CONTINUE           NO - DO NOT LEFT JUSTIFY
         MVC   ECABLKIN+1(4),ECABLKIN+2     LEFT JUSTIFY
         B     JUSTIFY
CONTINUE DS    0H
         MVC   ECABLKIN+6(1),JSPJSOCL
         NI    ECAFLAGS,B'01111111'         TURN OFF SLANT INDICATOR
         L     @15,BLKPTR                   CALL
         BALR  @14,@15                       APSUBLK
         MVC   ECARECLN(4),LINELGTH         SET LENGTH OF RECORD
         LA    @14,BUFWRK                   SET RECORD ADDRESS BACK TO
         ST    @14,ECARECAD                  BEGINNING OF PRINT RECORDS
         MVI   PRINTCC(@14),X'09'           SET CC IN PRINT RECORD
         LA    @14,1                        LOOP
         STH   @14,INDEX                     INDEX
LOOP2    LTR   RTNCODE,RTNCODE              CHECK RC OF APSUPUT
         BNZ   OUT                          NOT ZERO, EXIT
         LR    @15,PUTPTR                   CALL
         BALR  @14,@15                       APSUPUT
         LA    @14,132                      GET NEXT
         AL    @14,ECARECAD                  RECORD
         ST    @14,ECARECAD                  TO PRINT
         MVI   PRINTCC(@14),X'09'           SET CC
         LA    @14,1                        CHECK IF
         AH    @14,INDEX                     PRINTED
         STH   @14,INDEX                     TWELVE
         C     @14,TWELVE                    RECORDS
         BNH   LOOP2
         LTR   RTNCODE,RTNCODE              IF APSUPUT FAILED
         BNZ   OUT                           EXIT
**********************************************************************
*          SOME IBM CODE DROPPED
**********************************************************************
**********************************************************************
*          PROVIDE 2 BLANK LINES AFTER BLOCK LETTERS
**********************************************************************
         LA    @14,BUFWRK                   SET ECARECAD TO WORK
         ST    @14,ECARECAD                  BUFFER ADDRESS
         MVC   ECARECLN(4),ONE              SET LENGTH OF RECORD
         MVI   WRKCC,X'09'                  SET CC TO WRITE
         MVC   PRINTPOS(1),BLANK            SET PRINT POSITION TO BLANK
         LA    @14,1                        LOOP
         STH   @14,INDEX                     INDEX
LOOP5    LTR   RTNCODE,RTNCODE              CHECK RC OF APSUPUT
         BNZ   OUT                          NOT ZERO, EXIT
         LR    @15,PUTPTR                   CALL
         BALR  @14,@15                       APSUPUT
         LA    @14,1                        CHECK IF
         AH    @14,INDEX                     PRINTED
         STH   @14,INDEX                     TWO
         C     @14,TWO                       LINES
         BNH   LOOP5
         LTR   RTNCODE,RTNCODE              APSUPUT FAILED
         BNZ   OUT                          EXIT
**********************************************************************
*          BEGIN BUILDING THE MAIN PRINT LINE
**********************************************************************
         MVI   BUFPRT,C' '                SET
         MVC   BUFPRT+1(131),BUFPRT          PRINT LINE TO BLANK
*
*   TRANSLATE AND TEST FOR /
*
****************   GO FOR A / *****************************************
         SR    @01,@01              CLEAR R1 FOR RETURN CODE
         SR    @02,@02              CLEAR R2 FOR LENGTH
         TRT   JSPJPNAM,TABLE     TEST PROGNAME FOR /
         LTR   @02,@02
         BZ    MOVEPROG           NO - FORGET IT THEN
         LA    @03,JSPJPNAM
         CLI   0(@03),C'/'         IS IT THE FIRST CHARACTER
         BE    MOVEPROG           YES - WELL FORGET IT THEN
         LR    @15,@01              SAVE R1
         SR    @15,@03
         C     @15,=F'20'          IS IT THE LAST CHARACTER
         BNL   MOVEPROG           YES - JUST MOVE THE WHOLE LOT IN
         BCTR  @15,0               DOWN ONE MORE FOR EXECUTE
         EX    @15,MOVENAME        GO MOVE IN THE NAME
         LA    @15,2(@15)           POINT AFTER THE SLASH
         LR    @14,@15              SAVE LENGTH IN R7
         LA    @15,20
         SR    @15,@14              FIND LENGTH REMAINING
         BCTR  @15,0               DECR BY 1 FOR EXECUTE
         LA    @03,0(@14,@03)        SKIP PASSED PROG NAME AND /
         EX    @15,MOVEROOM        MOVE IN ROOM NUMBER
         MVC   BUFPRT+29(24),PLEASE
         MVC   BUFPRT+73(6),=C' Room '
         B     PROGNAME
MOVEPROG MVC   BUFPRT+65(20),JSPJPNAM
PROGNAME DS    0H
**********************************************************************
*          FILL REST OF SEPARATOR PAGE WITH MAIN PRINT LINE
**********************************************************************
         MVI   WRKCC,X'09'                  CC IS WRITE WITH SPACE
         MVC   ECARECLN(4),DCCLLGTH         GET LENGTH OF RECORD
         LA    @14,BUFWRK                   GET RECORD
         ST    @14,ECARECAD                  ADDRESS
         LA    @14,1                        LOOP
         STH   @14,INDEX                     INDEX
LOOP6    DS    0H                           CHECK RC OF APSUPUT
         LR    @15,PUTPTR                   CALL
         BALR  @14,@15                       APSUPUT
         LTR   RTNCODE,RTNCODE              CHECK RC OF APSUPUT
         BNZ   OUT                          NOT ZERO, EXIT
         LA    @14,1                        CHECK IF
         AH    @14,INDEX                     PRINTED
         STH   @14,INDEX                     TWO (was five)
*        C     @14,FIVE                      LINES
         C     @14,TWO                       LINES            .CHDEC91
         BNH   LOOP6
* one blank line                                              .CHDEC91
         LA    @14,BUFWRK                   SET ECARECAD TO WORK  "
         ST    @14,ECARECAD                  BUFFER ADDRESS       "
         MVC   ECARECLN(4),TWO              SET LENGTH OF RECORD  "
         MVI   WRKCC,X'09'                  SET CC TO WRITE       "
         MVC   PRINTPOS(1),BLANK            SET PRINT POSITION TO BLANK
         LR    @15,PUTPTR                   CALL                  "
         BALR  @14,@15                       APSUPUT              "
         LTR   RTNCODE,RTNCODE              APSUPUT FAILED        "
         BNZ   OUT                          EXIT                  "
*
* 8 lines of broadcast
*
         CLI   BROAD1,X'00'                 BROADCAST INFO PRESENT?
         BE    GETCAST                      NO - GO GET IT        "
GOTCAST  EQU   *
         MVI   BUFPRT,C' '                                    .CHDEC91
         MVC   BUFPRT+1(131),BUFPRT                               "
         MVI   WRKCC,X'09'                  SET CC TO WRITE       "
         LA    @14,BUFWRK                   SET ECARECAD TO WORK  "
         ST    @14,ECARECAD                  BUFFER ADDRESS       "
         MVC   ECARECLN(4),DCCLLGTH         GET LENGTH OF RECORD  "
         LA    @08,8                        8 LINES               "
         LA    @06,BROAD1                   FIRST LINE            "
LOOPB    EQU   *
         MVC   BUFPRT+40(50),0(@06)         CENTRE MESSAGE TEXT   "
         LR    @15,PUTPTR                   PUT LINE ROUTINE      "
         BALR  @14,@15                      WRITE A LINE          "
         LTR   RTNCODE,RTNCODE              OK?                   "
         BNZ   OUT                          NO - STOP             "
         LA    @06,50(@06)                  NEXT LINE OF BROADCAST"
         BCT   @08,LOOPB                                          "
**********************************************************************
*                         EPILOGUE
**********************************************************************
OUT      DS    0H
**********************************************************************
         SLR   RTNCODE,RTNCODE              RESET RETURN CODE
         L     @13,4(,@13)                  RESTORE CALLERS SAVE AREA
*                                            ADDRESS
         L     @14,12(,@13)                 RESTORE CALLERS RETURN
*                                            ADDRESS
         LM    @00,@12,20(@13)              RESTORE CALLERS REGISTERS
         BR    @14                          RETURN TO CALLER
         EJECT
*
* put out an error message                                    .CHDEC91
*
ERRMESS  EQU   *
         LA    @14,BUFWRK                   SET ECARECAD TO WORK  "
         ST    @14,ECARECAD                  BUFFER ADDRESS       "
         MVC   ECARECLN(4),DCCLLGTH         SET LENGTH OF RECORD  "
         MVI   BUFPRT,C' '                                    .CHDEC91
         MVC   BUFPRT+1(131),BUFPRT                               "
         MVI   WRKCC,X'09'                  SET CC TO WRITE       "
         MVC   BUFPRT+45(14),BROAD1+5
         LR    @15,PUTPTR                   CALL                  "
         BALR  @14,@15                       APSUPUT              "
         B     OUT                          EXIT                  "
         EJECT
*
* READ 8 BROADCAST RECORDS FROM IDUSERS AND STORE IN EXTENDED
* WORK BUFFER. HOPEFULLY ONLY DONE WHEN FIRST JOB OF A NEW LASER
* PROCEDURE IS PRINTED.
*
GETCAST  EQU   *
         MVC   BROAD1+5(14),=C'ERROR IN ACB  '
         LA    @03,ACBA
IDUSERS  GENCB BLK=ACB,                                                *
               AM=VSAM,                                                *
               DDNAME=IDUSERS,                                         *
               PASSWD=PASSAD,                                          *
               MACRF=(KEY,SEQ,IN),                                     *
               MF=(G,(@03),LEN1)
         LTR   @15,@15                      OK?                   "
         BNZ   ERRMESS                                            "
         ST    @01,ACBADDR
         LR    @03,@01
*
         LA    @02,RPLA
         LA    @06,RECADDR                  FIELD FOR RECORD ADDRESS
         MVC   BROAD1+5(14),=C'ERROR IN GENCB'
GENRPL   GENCB BLK=RPL,                                                *
               ACB=(@03),                                              *
               AM=VSAM,                                                *
               AREA=(@06),                                             *
               AREALEN=4,                                              *
               OPTCD=(KEY,SEQ,SYN,LOC),                                *
               ARG=KEYBROD,                                            *
               MF=(G,(@02),LEN2)
         LTR   @15,@15                      OK?                   "
         BNZ   ERRMESS                                            "
         ST    @01,RPLADDR                  RPL ADDRESS           "
*
         MVC   BROAD1+5(14),=C'ERROR IN OPEN '
         MVC   OPENA(OPENL),OPSTRUC
         L     @02,ACBADDR
         OPEN  ((@02)),MF=(E,OPENA)
         LTR   @15,@15                      OK?                   "
         BNZ   ERRMESS
*
OPENOK   EQU   *
*
         L     @02,RPLADDR
         LA    @03,8                        8 RECORDS             "
         LA    @06,BROAD1                   BEGINNING OF SAVE AREA"
LOOPC    EQU   *
         GET   RPL=(@02)                    READ A RECORD         "
         LTR   @15,@15                      OK?                   "
         BZ    GOTOK
         MVC   BROAD1+5(14),=C'ERROR IN GET  '
         B     ERRMESS                                            "
GOTOK    EQU   *
         L     @08,RECADDR                  ADDRESS OF RECORD     "
         MVC   0(50,@06),11(@08)            STORE IN WORK BUFFER  "
         LA    @06,50(@06)                  NEXT LINE OF BROADCAST"
         BCT   @03,LOOPC
*
         MVC   OPENA(CLOSEL),CLSTRUC
         L     @02,ACBADDR
         CLOSE ((@02)),MF=(E,OPENA)
         B     GOTCAST
         EJECT
*
*
*
         DS    0H
PSIZE    EQU   ((*-APSUX01+99)/100)*5       PATCH AREA SIZE
         DC    C'PATCH AREA - APSUX01  88.XXX'
PSPACE   DC    25S(*)                       PATCH AREA
         ORG   PSPACE
         DC    ((PSIZE+1)/2)S(*)
         ORG   ,
**********************************************************************
*              MISC CONSTANTS
**********************************************************************
ONE      DC    F'1'
TWO      DC    F'2'
THREE    DC    F'3'
FIVE     DC    F'5'
EIGHT    DC    F'8'
TWELVE   DC    F'12'
SIXTEEN  DC    F'16'
LINELGTH DC    F'127'
DCCLLGTH DC    F'133'
ZEROES   DC    X'000000000000'
PRT5     DC    CL8'PRT5    '
BLANK    DC    CL8' '
START    DC    CL5'START'
CONT     DC    CL5'CONT '
LOCALPR  DC    CL5'LOCAL'
ROOM     DC    CL4'ROOM'
AM       DC    CL2'AM'
SUBED    DC    CL20'Submitted by USERID '
TERMID   DC    CL13' on terminal '
PLEASE   DC    CL24'Please return output to '
MOVENAME MVC   BUFPRT+53(0),JSPJPNAM
MOVEROOM MVC   BUFPRT+79(0),0(@03)
TABLE    DC    XL97'00'
         DC    XL1'61'                 ONLY / HAS A VALUE
         DC    XL160'00'
         LTORG
*
ZEROLINE DC    X'01'                          WRITE WITHOUT SKIP
         DC    C'0'
LINELINE DC    X'09'                          SKIP 1 AFTER WRITE
         DC    C'|'
DCCJNO   DC    X'11'                          SKIP 2 AFTER WRITE
         DC    CL30' '                                                *
         DC    C'--------- J O B   N U M B E R   A N D   O U T P U T' *
         DC    C'   C L A S S ---------'                              *
         DC    CL29' '
DCCLINE4 DC    X'09'                       SKIP 1 AFTER WRITE
         DC    C' '                   THIS IS THE JOB LINE
         DC    CL99' '
         DC    CL22'Job                   '
         DC    CL10' '
DCCLINE5 DC    X'09'                       SKIP 1 AFTER WRITE
         DC    C' '                   THIS IS THE JOB LINE
         DC    CL99' '
         DC    CL22'Number                '
         DC    CL10' '
DCCLINE6 DC    X'09'                       SKIP 1 AFTER WRITE
         DC    C' '                   THIS IS THE JOB LINE
         DC    CL99' '
         DC    CL22'Printed dd mmm yy.jjj '
         DC    CL10' '
DCCLINE7 DC    X'09'                       SKIP 1 AFTER WRITE
         DC    C' '                   THIS IS THE JOB LINE
         DC    CL99' '
         DC    CL22'Time    hh.mm.ss AM   '
         DC    CL10' '
**********************************************************************
*              EQUATES FOR REGISTERS 0-15
**********************************************************************
@00      EQU   00
@01      EQU   01
@02      EQU   02
@03      EQU   03
@04      EQU   04
@05      EQU   05
@06      EQU   06
@07      EQU   07
@08      EQU   08
@09      EQU   09
@10      EQU   10
@11      EQU   11
@12      EQU   12
@13      EQU   13
@14      EQU   14
@15      EQU   15
**********************************************************************
*              POINTER REGISTERS AND ROUTINES
**********************************************************************
PUTPTR   EQU   @10                          APSUPUT POINTER
WRKPTR   EQU   @02                          WORK AREA POINTER
AMPMPTR  EQU   @03                          POINTER TO AM/PM PRINT AREA
XTPPTR   EQU   @04                          APSGEXTP POINTER
ECAPTR   EQU   @05                          APSUECA POINTER
JMRPTR   EQU   @07                          JMR POINTER
ECAWKPTR EQU   @09                          ECA WORK BUFFER POINTER
JSPAPTR  EQU   @11                          JSPA POINTER
RTNCODE  EQU   @15                          RETURN CODE
BLKPTR   EQU   ECABLKP                      APSUBLK POINTER
APSUPUT  EQU   0
APSUBLK  EQU   0
BUFPRI   EQU   0
PRINTCC  EQU   BUFPRI
DAYTBL   EQU   0
         EJECT
         DS    0F
OPSTRUC  EQU   *
         OPEN  (WACBADDR),MF=L              OPEN FILE         .CHDEC91
OPENL    EQU   *-OPSTRUC
         DS    0F
CLSTRUC  EQU   *
         CLOSE (WACBADDR),MF=L              CLOSE FILE        .CHDEC91
CLOSEL   EQU   *-CLSTRUC
PASSAD   DC    X'08'
         DC    C'AFRICAAA'
*
KEYBROD  DC    XL8'000000000000001F'
*
BRODADDR DS    F                            ADDRESS OF IDUSERS RECORD
WACBADDR DS    F                            ADDRESS OF IDUSERS ACB
*                                            - CHANGED
**********************************************************************
*              APSUECA WORK BUFFER
**********************************************************************
BUFWRK   DSECT
WRKDCC   DS    0CL133                       DCC LINE
WRKCC    DS    CL1                          CARRIAGE CONTROL
WRKDATA  DS    CL132                        DATA LINE
         DS    CL3                          RESERVED
WRKDATE  DS    CL4                          CURRENT DATE - PACKED
WRKTIME  DS    CL4                          CURRENT TIME - PACKED
         ORG   WRKTIME
         DS    CL3                          HOURS MINUTES SECONDS
WRKTH    DS    CL1                          TENTHS AND HUNDREDTHS
WRKUDATE DS    CL8                          CURRENT DATE - UNPACKED
         ORG   WRKUDATE
WRKMMM   DS    CL3                          MONTH
WRKDD    DS    CL2                          DAY
WRKYY    DS    CL2                          YEAR
         DS    CL1                          RESERVED
WRKUTIME DS    CL7                          CURRENT TIME - UNPACKED
         ORG   WRKUTIME
WRKHR    DS    CL2                          HOUR
WRKMIN   DS    CL2                          MINUTE
WRKSEC   DS    CL2                          SECOND
         DS    CL1                          SIGN BYTE
         DS    CL1                          RESERVED
WRKJID1  DS    F                            WORK AREA FOR JOE ID 1
WRKJID2  DS    F                            WORK AREA FOR JOE ID 2
WRKJIDEC DS    CL8                          WORK AREA JOE ID TO DECIMAL
WRKJID1Z DS    CL8                          WORK AREA JOE ID 1 TO ZONED
WRKJID2Z DS    CL8                          WORK AREA JOE ID 2 TO ZONED
WRKWORK  DS    CL8                          WORK AREA FOR CONVERSION
WRKJTBL  DS    CL48                         JULIAN CONVERSION TABLE
**********************************************************************
*              D.C.C. USER NAME PRINT LINE
**********************************************************************
         ORG   WRKDATA
         DS    CL50
DCCUID   DS    CL20
DCCUSER  DS    CL8
DCCTID   DS    CL13
DCCTERM  DS    CL4
**********************************************************************
*              D.C.C. MAIN PRINT LINE
**********************************************************************
         ORG   WRKDATA
         DS    CL108
DCCJNAME DS    CL8
         ORG   WRKDATA
         DS    CL108
DCCDTD   DS    CL2
         DS    CL1
DCCDTM   DS    CL3
         DS    CL1
DCCDTY   DS    CL2
         DS    CL1
DCCJDATE DS    CL3
         ORG   WRKDATA
         DS    CL108
DCCTIMH  DS    CL2
         DS    CL1
DCCTIMM  DS    CL2
         DS    CL1
DCCTIMS  DS    CL2
         DS    CL1
DCCAMPM  DS    CL2
**********************************************************************
*              INFORMATION PRINT LINE
**********************************************************************
BUFPRT   EQU   WRKDATA
         ORG   WRKDATA
PRTFRAME DS    CL1                          FRAME '*'
PRTFORM  DS    CL5                          HEADER TYPE
         DS    CL1                          SPACE
PRTNUM   DS    CL8                          JOB NUMBER
         DS    CL1                          SPACE
PRTNAME  DS    CL8                          JOB NAME
         DS    CL1                          SPACE
PRTJNAME DS    CL8                          JOE NAME
         DS    CL1                          SPACE
PRTJID1  DS    CL3                          JOE ID1
         DS    CL1                          SPACE
PRTJID2  DS    CL3                          JOE ID2
         DS    CL1                          SPACE
PRTJROUT DS    CL8                          JOE ROUTE CODE
         DS    CL1                          SPACE
PRTPNAME DS    CL20                         PROGRAMMER NAME
         DS    CL1                          SPACE
PRTRKEY  DS    CL4                          'ROOM'
         DS    CL1                          SPACE
PRTROOM  DS    CL4                          ROOM NUMBER
         DS    CL1                          SPACE
PRTTIME  DS    CL8                          PRINT TIME
         ORG   PRTTIME
PRTHR    DS    CL2                          HOUR
PRTTS1   DS    CL1                          SEPARATOR
PRTMIN   DS    CL2                          MINUTE
PRTTS2   DS    CL1                          SEPARATOR
PRTSEC   DS    CL2                          SECOND
         DS    CL1                          SPACE
PRTAMPM  DS    CL2                          AM/PM
         DS    CL1                          SPACE
PRTDATE  DS    CL9                          PRINT DATE
         ORG   PRTDATE
PRTDD    DS    CL2                          DAY
         DS    CL1                          SPACE
PRTMMM   DS    CL3                          MONTH
         DS    CL1                          SPACE
PRTYY    DS    CL2                          YEAR
         DS    CL1                          SPACE
PRTDNAME DS    CL8                          DEVICE NAME
         DS    CL1                          SPACE
PRTSYS   DS    CL4                          SYSTEM NAME
         DS    CL1                          SPACE
PRTFORMX DS    CL5                          HEADER TYPE
         DS    CL1                          SPACE
PRTCLASX DS    CL1                          CLASS
PRTFRAMX DS    CL1                          FRAME '*'
         ORG   WRKDATA                                        .CHDEC91
RECADDR  DS    F                            ADDRESS OF IDUSERS RECORD
RPLADDR  DS    F                            ADDRESS OF IDUSERS RPL
RPLA     DS    CL96                         IDUSERS RPL parmlist
ACBADDR  DS    F                            ADDRESS OF IDUSERS ACB
ACBA     DS    CL68                         IDUSERS ACB parmlist
OPENADDR DS    F                            ADDRESS OF IDUSERS OPENLIST
OPENA    DS    CL10                         IDUSERS OPEN LIST
SHOWA    DS    CL28                         ERROR LIST
DBLWRD   DS    D
FULLWRD  DS    F
FULLWRD1 DS    F
FULLWRD2 DS    F
FULLWRD3 DS    F
*
* DCC EXTENDED WORK BUFFER TO HOLD BROADCAST LINES
* THE EXIT TABLE APSUCOM HAS TO BE AMENDED WITH NEW LENGTH
*
         ORG   WRKDATA+1599                                   .CHDEC91
BROAD1   DS    CL50
BROAD2   DS    CL50
BROAD3   DS    CL50
BROAD4   DS    CL50
BROAD5   DS    CL50
BROAD6   DS    CL50
BROAD7   DS    CL50
BROAD8   DS    CL50
* DELETE 1 LINE
**********************************************************************
*              WORK AREA
**********************************************************************
BUFBLK   EQU   WRKDATA
SHORTLIN EQU   WRKDATA
PRINTPOS EQU   SHORTLIN
INDEX    EQU   ECAGWRK
         END   APSUX01
//*
//LINK     EXEC PGM=IEWL,COND=(0,NE),
//          PARM='XREF,LET,REUS,RENT,REFR,AC=1'
//SYSPRINT DD   SYSOUT=*
//SYSUT1   DD   UNIT=SYSDA,SPACE=(CYL,(1,1))
//SYSLMOD  DD   DSN=SYS1.LINKLIB,DISP=SHR
//* SLMOD  DD   DSN=ZZ.TESTLIB,DISP=SHR
//SYSLIN   DD   DSN=&&OBJ,DISP=(OLD,DELETE)
//         DD   *
 NAME APSUX01(R)
/*
//
