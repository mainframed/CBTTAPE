Z3426    TITLE 'Z3426  CPU SOAK PROGRAM                                *
               - MODULE PROLOGUE'
*****                                                             *****
*****                                                             *****
*                                                                     *
* ID:          Z3426  CPU SOAK PROGRAM.                               *
*                                                                     *
* DESCRIPTION: THIS PROGRAM IS STARTED ON BOTH CPU'S AND WILL SOAK UP *
*              A PERCENTAGE OF THE CPU ACCORDING TO THE OS PARM VALUES*
*              SPECIFIED.                                             *
*                                                                     *
*              A STOP/MODIFY COMMAND FACILITY IS PROVIDED TO ALLOW    *
*              ACF2 AUTHORISED PERSONNEL THE ABILITY TO  STOP OR      *
*              CHANGE THE VALUES WHILE IT IS ACTIVE.                  *
*                                                                     *
* TASKS:       1. SETUP - INCLUDING GETMAIN STORAGE REQUIRED.         *
*                                                                     *
*              2. SET UP FOR STOP/MODIFY PROCESSING.                  *
*                                                                     *
*              3. VALIDATE PARM FIELD OR VALUES PASSED VIA MODIFY     *
*                 COMMAND AND SET UP CYCLE VALUES.                    *
*                                                                     *
*              4. SET STIMER FOR WAIT TIME AND WAIT.                  *
*                                                                     *
*              5. SET STIMER FOR BUSY TIME AND LOOP UNTIL STIMER      *
*                 POPS.                                               *
*                                                                     *
*              6. CHECK FOR STOP/MODIFY COMMAND AT VARIOUS POINTS.    *
*                                                                     *
*              7.  ASK OPERATOR FOR HIS LOGONID AND PASSWROD.         *
*                                                                     *
*              8.  OBTAIN THE OPERATOR'S ACF2 LOGONID RECORD AND      *
*                  CHECK FOR THE REFRESH ATTRIBUTE.                   *
*                                                                     *
*              9.  EITHER WTOR HIM TO SAY NOT AUTHORISED OR VALIDATE  *
*                  VALUES SPECIFIED IN COMMAND. ISSUE WTO WITH NEW    *
*                  VALUES.                                            *
*                                                                     *
*              10. IF STOP COMMAND THEN WTO AND STOP.                 *
*                                                                     *
*                                                                     *
*     THE PARM VALUES SPECIFIED ARE 'TOTAL CYCLE TIME,BUSY TIME'      *
*                                                                     *
*     IF RUN WITH PARM='18000,190' THIS PROGRAM WILL SOAK APPROX      *
*     1% OF THE CPU.                                                  *
*                                                                     *
*     THIS PROGRAM SHOULD BE NON-CANCELLABLE,NON-SWAPPABLE AND RUN    *
*     WITH A HIGH DISPATCHING PRIORITY.                               *
*                                                                     *
*     TO MODIFY THE PARM VALUES USE 'F XXXXX,TTTTT,BBB' WHERE         *
*                                                                     *
*                     XXXXX = STARTED TASK NAME                       *
*                     TTTTT = TOTAL CYCLE TIME                        *
*                     BBB   = PROGRAM BUSY TIME                       *
*                                                                     *
*     TO STOP THIS PROGRAM USE 'P XXXXX' WHERE XXXXX IS THE STARTED   *
*     TASK NAME.                                                      *
*                                                                     *
*     BOTH THE MODIFY AND STOP COMMANDS REQUIRE AN AUTHORISED ACF2    *
*     LOGONID AND PASSWORD TO BE ENTERED BEING BEING ACTIONED. THE    *
*     LOGONID MUST HAVE THE ATTRIBUTE REFRESH.                        *
*                                                                     *
*                                                                     *
*                                                                     *
* ENTRY INTFCE:R1 POINTS TO OS PARM FIELD.                            *
*                                                                     *
* EXIT INTFCES:1. N/A                                                 *
*                                                                     *
* RETURN CODES:   N/A                                                 *
*                                                                     *
* REG USAGE:   R0  -  SYSTEM AND TEMP USE                             *
*              R1  -> OS PARM ON ENTRY/ TEMP USE                      *
*              R2  - WORK                                             *
*              R3  - PARM POINTER
*              R4  - WORK                                             *
*              R5  -> CIB ADDRESS                                     *
*              R6  - WORK                                             *
*              R7  - WORK                                             *
*              R8  -> MODIFY ECB ADDRESS                              *
*              R9  - ADDRESS OF WTO MESSAGES                          *
*              R10 - LINKAGE REGISTER                                 *
*              R11 - SECOND BASE REGISTER                             *
*              R12 - BASE REGISTER                                    *
*              R13 - SAVE AREA                                        *
*              R14 - RETURN ADDRESS                                   *
*              R15 - RETURN CODE                                      *
*                                                                     *
*****                                                             *****
*****                                                             *****
*                                                                     *
* Z3426    MESSAGES                                                   *
*                                                                     *
*           Z3426A NO PARAMETERS SPECIFIED                            *
*                                                                     *
*           EXPLANATION: THE STARTED TASK JCL PROCEDURE DOES NOT      *
*                        SPECIFY ANY PARAMETERS.                      *
*                                                                     *
*           SYSTEM ACTION: PROGRAM ABENDS WITH USER 1.                *
*                                                                     *
*           OPERATOR ACTION: EITHER CORRECT THE START COMMAND OR      *
*                            INFORM TECHNICAL SERVICES IF THE JCL     *
*                            PROCEDURE IS INCORRECT.                  *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426B UNBALANCED PARENTHESES IN PARM FIELD               *
*                                                                     *
*           EXPLANATION: UNBALANCED PARENTHESES IN THE PARAMETER      *
*                        FIELD.                                       *
*                                                                     *
*           SYSTEM ACTION: PROGRAM ABENDS WITH USER 1.                *
*                                                                     *
*           OPERATOR ACTION: EITHER CORRECT THE START COMMAND OR      *
*                            INFORM TECHNICAL SERVICES IF THE JCL     *
*                            PROCEDURE IS INCORRECT.                  *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426C ONLY ONE PARAMETER FOUND - TWO REQUIRED            *
*                                                                     *
*           EXPLANATION: BOTH THE TOTAL CYCLE TIME AND THE BUSY       *
*                        TIME ARE REQUIRED.                           *
*                                                                     *
*           SYSTEM ACTION: PROGRAM ABENDS WITH USER 1.                *
*                                                                     *
*           OPERATOR ACTION: EITHER CORRECT THE START COMMAND OR      *
*                            INFORM TECHNICAL SERVICES IF THE JCL     *
*                            PROCEDURE IS INCORRECT.                  *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426D PARM FIELD CONTAINS NON-NUMERICS                   *
*                                                                     *
*           EXPLANATION: THE TOTAL CYCLE TIME AND THE BUSY TIME       *
*                        ARE NUMERIC FIELDS.                          *
*                                                                     *
*           SYSTEM ACTION: PROGRAM ABENDS WITH USER 1.                *
*                                                                     *
*           OPERATOR ACTION: EITHER CORRECT THE START COMMAND OR      *
*                            INFORM TECHNICAL SERVICES IF THE JCL     *
*                            PROCEDURE IS INCORRECT.                  *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426E INVALID PARAMETER LENGTH                           *
*                                                                     *
*           EXPLANATION: THE TOTAL CYCLE TIME AND THE BUSY TIME       *
*                        MUST EACH BE NO LONGER THAN SIX CHARACTERS.  *
*                                                                     *
*           SYSTEM ACTION: PROGRAM ABENDS WITH USER 1.                *
*                                                                     *
*           OPERATOR ACTION: EITHER CORRECT THE START COMMAND OR      *
*                            INFORM TECHNICAL SERVICES IF THE JCL     *
*                            PROCEDURE IS INCORRECT.                  *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426F PARAMETER OUT OF RANGE 1 - 999999                  *
*                                                                     *
*           EXPLANATION: THE TOTAL CYCLE TIME AND THE BUSY TIME       *
*                        MUST BE IN THE RANGE 1 TO 999999.            *
*                                                                     *
*           SYSTEM ACTION: PROGRAM ABENDS WITH USER 1.                *
*                                                                     *
*           OPERATOR ACTION: EITHER CORRECT THE START COMMAND OR      *
*                            INFORM TECHNICAL SERVICES IF THE JCL     *
*                            PROCEDURE IS INCORRECT.                  *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426G START CIB NOT FREED                                *
*                                                                     *
*           EXPLANATION: THE MODIFY SET UP ROUTINE COULD NOT FREE     *
*                        THE START COMMAND INPUT BUFFER.              *
*                                                                     *
*           SYSTEM ACTION: PROGRAM ABENDS WITH USER 1.                *
*                                                                     *
*           OPERATOR ACTION: INFORM TECHNICAL SERVICES.               *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426H UNKNOWN CIBVERB                                    *
*                                                                     *
*           EXPLANATION: THE MODIFY CHECK ROUTINE DETECTED AN         *
*                        INVALID CIBVERB. THE ONLY ALLOWABLE ONES     *
*                        ARE MODIFY OR STOP.                          *
*                                                                     *
*           SYSTEM ACTION: MODIFY COMMAND IS IGNORED AND THE          *
*                          PROGRAM CONTINUES.                         *
*                                                                     *
*           OPERATOR ACTION: CORRECT THE MODIFY COMMAND AND INFORM    *
*                            TECHNICAL SERVICES IF THIS STILL OCCURS. *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426I ENTER AN AUTHORISED LOGONID                        *
*                                                                     *
*           EXPLANATION: A REQUEST FOR AN AUTHORISED LOGONID. THIS    *
*                        LOGONID WILL BE CHECKED AGAINST ACF2 FOR     *
*                        THE ABILITY TO ISSUE MODIFY COMMANDS.        *
*                                                                     *
*           SYSTEM ACTION: WAITS FOR LOGONID TO BE ENTERED            *
*                                                                     *
*           OPERATOR ACTION: REPLY WITH A VALID LOGONID.              *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426J ENTER YOUR PASSWORD                                *
*                                                                     *
*           EXPLANATION: A REQUEST FOR YOUR PASSWORD. THIS WILL BE    *
*                        CHECKED AGAINST ACF2 FOR VALIDITY.           *
*                                                                     *
*           SYSTEM ACTION: WAITS FOR PASSWORD TO BE ENTERED.          *
*                                                                     *
*           OPERATOR ACTION: REPLY WITH YOUR PASSWORD.                *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426V XXXXXXXX HAS BEEN STOPPED BY LID YYYYYYYY          *
*                                                                     *
*           EXPLANATION: THE CPU SOAK TASK HAS BEEN STOPPED BY THE    *
*                        AUTHORISED LOGONID.                          *
*                                                                     *
*           SYSTEM ACTION: THE PROGRAM STOPS.                         *
*                                                                     *
*           OPERATOR ACTION: NONE.                                    *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426W XXXXXXXX NOT AUTHORISED TO MODIFY YYYYYYYY         *
*                                                                     *
*           EXPLANATION: THE LOGONID IS NOT AUTHORISED TO MODIFY THE  *
*                        CPU SOAK TASK. IF THIS MESSAGE IS PRECEDED   *
*                        BY ACF01200 INVALID PASSWORD AUTHORITY FOR   *
*                        LOGONID XXXXXXX, THE PASSWORD WAS ENTERED    *
*                        INCORRECTLY.                                 *
*                                                                     *
*           SYSTEM ACTION: THE PROGRAM CONTINUES.                     *
*                                                                     *
*           OPERATOR ACTION: NONE.                                    *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426X BUSY TIME XXXXXX IS MORE THAN 80% OF TOTAL CYCLE   *
*                  TIME YYYYYY.                                       *
*                                                                     *
*           EXPLANATION: THE BUSY TIME IS NOT ALLOWED TO BE MORE THAN *
*                        80% OF THE TOTAL CYCLE TIME.                 *
*                                                                     *
*           SYSTEM ACTION: IF THE VALUES ARE INCORRECT AT THE START   *
*                          OF THE PROGRAM, IT WILL ABEND WITH USER 1. *
*                                                                     *
*                          IF THE VALUES WERE SPECIFIED INCORRECTLY   *
*                          VIA A MODIFY COMMAND, THE PROGRAM CONTINUES*
*                          WITH THE LAST SET OF VALIDATED VALUES.     *
*                                                                     *
*                          MESSAGE Z IS ISSUED FOLLOWING THIS MESSAGE *
*                          SHOWING THE VALUES IN USE.                 *
*                                                                     *
*           OPERATOR ACTION: CORRECT THE SPECIFIED PARM VALUES.       *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426Y BUSY TIME XXXXXX IS GREATER THAN THE TOTAL CYCLE   *
*                  TIME YYYYYY.                                       *
*                                                                     *
*           EXPLANATION: THE BUSY TIME IS NOT ALLOWED TO BE GREATER   *
*                        THAN THE TOTAL CYCLE TIME.                   *
*                                                                     *
*           SYSTEM ACTION: IF THE VALUES ARE INCORRECT AT THE START   *
*                          OF THE PROGRAM, IT WILL ABEND WITH USER 1. *
*                                                                     *
*                          IF THE VALUES WERE SPECIFIED INCORRECTLY   *
*                          VIA A MODIFY COMMAND, THE PROGRAM CONTINUES*
*                          WITH THE LAST SET OF VALIDATED VALUES.     *
*                                                                     *
*                          MESSAGE Z IS ISSUED FOLLOWING THIS MESSAGE *
*                          SHOWING THE VALUES IN USE.                 *
*                                                                     *
*           OPERATOR ACTION: CORRECT THE SPECIFIED PARM VALUES.       *
*                                                                     *
*                                                                     *
*                                                                     *
*           Z3426Z VALUES IN USE - WAIT TIME: XXXXXX BUSY TIME: YYYYYY*
*                                                                     *
*           EXPLANATION: INFORMATIONAL MESSAGE SHOWING THE VALUES     *
*                        THAT ARE IN USE.                             *
*                                                                     *
*           SYSTEM ACTION: NONE                                       *
*                                                                     *
*           OPERATOR ACTION: NONE.                                    *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* ABENDS:      USER 1 ABEND ACCOMPANIED BY ONE OF THE ABOVE MESSAGES. *
*                                                                     *
*              USER 2 ABEND IF ACF2 IS NOT ACTIVE.                    *
*                                                                     *
* NOTES:       1. THIS PROGRAM HAS BEEN WRITTEN WITH RE-ENTRANCY IN   *
*                 MIND. HOWEVER IT IS NOT TOTALLY RE-ENTRANT.         *
*                                                                     *
*              2. MUST BE RMODE 24 AND AMODE 24.                      *
*                                                                     *
*              3. MUST BE NON-SWAPPABLE AND NON-CANCELABLE.           *
*                 THIS IS ACHIEVED VIA PARMLIB MEMBER SCHED00.        *
*                                                                     *
*              4. ITS PERFORMANCE GROUP AND DISPATCHING PRIORITY      *
*                 MUST BE FIXED TO SPG'S RECOMMENDATIONS.             *
*                                                                     *
* VERSION:     00 -  MJT  - 09.01.90   ORIGINAL VERSION               *
*                                                                     *
*****                                                             *****
*****                                                             *****
         TITLE 'Z3426  CPU SOAK PROGRAM                                *
               - SET UP'
Z3426    SETUP DATE='Â¢DATEACS',                                        *
               12,11,RE=YES,LDSA=WALEN
Z3426    AMODE 24
Z3426    RMODE 24
*                                       +--------------------------+
*                                       |  ADDRESS WORK AREA AND   |
*                                       |  CLEAR IT WITH THE       |
*                                       |  EXCEPTION OF THE SAVE   |
*                                       |  AREA.                   |
*                                       |  MOVE IN EYECATCHER      |
*                                       +--------------------------+
         USING WORKAREA,R13
         LA    R2,WACLEAR
         LA    R3,WACLRLEN
         SR    R14,R14
         SR    R15,R15
         MVCL  R2,R14
         MVC   WAEYE,=CL8'WORKAREA'
         SPACE 2
*                                       +--------------------------+
*                                       |  CALL STOP/MODIFY ROUTINE|
*                                       |  AND ABEND IF PROBLEMS   |
*                                       +--------------------------+
         L     R3,0(R1)                 R3 -> OS PARM
         BAL   R10,MODSETUP
         LTR   R15,R15
         BZ    PROCPARM
         BAL   R10,ISSUEWTO
         ABEND 1
         SPACE 2
*                                       +--------------------------+
*                                       |  CALL PARM ANALYSIS RTNE |
*                                       |  AND ABEND IF PROBLEMS   |
*                                       +--------------------------+
PROCPARM DS    0H
         BAL   R10,PARMANAL             BRANCH TO PARM ANALYSIS
         LTR   R15,R15                  TEST COND CODE
         BZ    PROCMSG                  OK CONTINUE
         BAL   R10,ISSUEWTO             ELSE ISSUE ERROR MESSAGE
         ABEND 1
         SPACE 2
*                                       +--------------------------+
*                                       |  ISSUE VALUES USED MSG   |
*                                       +--------------------------+
PROCMSG  DS    0H
         LA    R9,MSGZ
         BAL   R10,ISSUEWTO
         SPACE 2
*                                       +--------------------------+
*                                       |  CHECK FOR MODIFY        |
*                                       |  ISSUE WAITTIME STIMER   |
*                                       +--------------------------+
WAITHERE DS    0H
         BAL   R10,MODCHECK
         LTR   R15,R15
         BZ    STIMER1
         BAL   R10,ISSUEWTO              ISSUE NOT AUTH MESSAGE
         B     WAITHERE
         SPACE 1
STIMER1  DS    0H
         STIMER WAIT,MICVL=WAITTIME
         SPACE 2
*                                       +--------------------------+
*                                       |  CHECK FOR MODIFY        |
*                                       |  ISSUE BUSYTIME STIMER   |
*                                       +--------------------------+
GETBUSY  DS    0H
         BAL   R10,MODCHECK
         LTR   R15,R15
         BZ    STIMER2
         BAL   R10,ISSUEWTO              ISSUE NOT AUTH MESSAGE
         B     GETBUSY
STIMER2  DS    0H
         NI    SWITCH,X'FF'-SWTIMEUP     SWITCH OFF TIMEUP
         STIMER REAL,EXITRTN,MICVL=BUSYTIME
         SPACE 2
*                                       +--------------------------+
*                                       |  LOOP UNTIL STIMER EXIT  |
*                                       |  ROUTINE TURNS ON SWITCH |
*                                       +--------------------------+
TESTSW   DS    0H
         TM    SWITCH,SWTIMEUP
         BO    WAITHERE
         B     TESTSW
*                                       +--------------------------+
*                                       |  RETURN TO OS            |
*                                       +--------------------------+
RETURN   DS    0H
         CLEAR RE=YES
         SPACE 2
         TITLE 'Z3426  CPU SOAK PROGRAM                                *
               - SET UP MODIFY/STOP SUBROUTINE'
***********************************************************************
*                                                                     *
*  SET UP MODIFY/STOP SUBROUTINE                                      *
*  =============================                                      *
*                                                                     *
***********************************************************************
         SPACE 2
MODSETUP DS    0H
         LA    R8,EXTRCTWD
         EXTRACT (8),FIELDS=COMM
         L     R8,EXTRCTWD
         USING COMLIST,R8
         L     R5,COMCIBPT
         USING CIBNEXT,R5
         CLI   CIBVERB,CIBSTART         IS IT CIB START?
         BNE   MODSETCT                 NO THEN SET COUNT
         QEDIT ORIGIN=COMCIBPT,BLOCK=(5)
         LTR   R15,R15
         BZ    MODSETCT
         LA    R9,MSGG
         BR    R10
         SPACE 2
MODSETCT DS    0H
         QEDIT ORIGIN=COMCIBPT,CIBCTR=1
         BR    R10
         SPACE 2
         TITLE 'Z3426  CPU SOAK PROGRAM                                *
               - CHECK FOR MODIFY SUBROUTINE'
***********************************************************************
*                                                                     *
*  CHECK FOR MODIFY SUBROUTINE                                        *
*  ===========================                                        *
*                                                                     *
*  ON EXIT:-                                                          *
*    R15 = 4 IF USER IS NOT AUTHORISED.                               *
*    R9  -> MESSAGE TO BE ISSUED.                                     *
*                                                                     *
*    R15 = 0 IF USER IS AUTHORISED.                                   *
*                                                                     *
***********************************************************************
         SPACE 2
MODCHECK DS    0H
         SLR   R15,R15                  SET RETURN CODE TO ZERO
         L     R2,COMECBPT              GET ECB ADDRESS
         TM    0(R2),ECBPOST            CHECK IF POST BIT ON
         BNOR  R10                      NO - THEN RETURN
         SPACE 1
         L     R5,COMCIBPT
         BAL   R4,CALLACF2              YES - ISSUE WTORS AND CALL ACF2
         LTR   R15,R15                  CHECK FOR AUTH
         BZ    CHKCIB                   YES THEN SEE IF MOD OR STOP
         LA    R9,MSGW                  NOT AUTHORISED MESSAGE
         QEDIT ORIGIN=COMCIBPT,BLOCK=(5) FREE THE CIB
         LA    R15,4
         BR    R10                      RETURN TO MAIN ROUTINE
         SPACE 1
CHKCIB   DS    0H
         CLI   CIBVERB,CIBMODFY         IS IT MODIFY?
         BE    PROCMDFY
         CLI   CIBVERB,CIBSTOP          IS IT STOP?
         BE    PROCSTOP
         LA    R9,MSGH                  OTHERS = ERROR.
         LA    R15,4
         BR    R10                      RETURN TO MAIN CONTROL
         SPACE 1
PROCSTOP DS    0H
         LA    R9,MSGV                  ISSUE STOP ACKNOWLEDGED MSG
         BAL   R10,ISSUEWTO
         B     RETURN                   AND RETURN TO OS
         SPACE 1
PROCMDFY DS    0H
         LH    R6,CIBDATLN              GET LENGTH OF DATA
         LA    R6,1(R6)                 ADD 1 TO MAKE LENGTH OF DATA
*                                       INCLUDE LENGTH FIELD BUT ALLOW
*                                       FOR EXECUTE INSTRUCTION.
         EX    R6,CIBMOVE               MVC CIBSAVE(0),CIBDATLN
         QEDIT ORIGIN=COMCIBPT,BLOCK=(5) FREE THE CIB
         LA    R3,CIBSAVE               R3 -> CIBSAVE LL + DATA
         ST    R10,R10SAVE              SAVE R10
         BAL   R10,PARMANAL             CHECK VALUES IN COMMAND
         LTR   R15,R15                  CHECK PARMANAL RC
         BZ    PROCMSGZ                 OK THEN GOOD MSG
         BAL   R10,ISSUEWTO             ISSUE ERROR MSG FROM PARMANAL
PROCMSGZ DS    0H
         LA    R9,MSGZ
         BAL   R10,ISSUEWTO
         L     R10,R10SAVE              RESTORE R10
         SLR   R15,R15
         BR    R10
         SPACE 2
CIBMOVE  MVC   CIBSAVE(0),CIBDATLN
         TITLE 'Z3426  CPU SOAK PROGRAM                                *
               - CALL ACF2'
***********************************************************************
*                                                                     *
*  ISSUE WTORS AND CALL ACF2 SUBROUTINE                               *
*  ====================================                               *
*                                                                     *
*   CALLED FROM MODCHECK SUBROUTINE.                                  *
*                                                                     *
*   R15 ON EXIT CAN BE RETURN CODE FROM ACF2 CALL (IF NON-ZERO)       *
*       OR 0 - GOOD ACF2 RETURN AND REFRESH BIT ON                    *
*       OR 4 - GOOD ACF2 RETURN AND REFRESH BIT NOT ON                *
*                                                                     *
***********************************************************************
         SPACE 2
CALLACF2 DS    0H
         MVC   WTORAREA(138),MSGI       MOVE LID WTOR TO WORKAREA
         XC    WTORLID(20),WTORLID      CLEAR AREA BEFORE FIRST USE
         LA    R6,WTORLID
         LA    R7,WTORECB
         WTOR  ,(6),,(7),MF=(E,WTORAREA)      ISSUE WTOR NOW
         WAIT  ECB=WTORECB              WAIT FOR REPLY
         SPACE 1
         MVC   WTORAREA(138),MSGJ       MOVE PSD WTOR TO WORKAREA
         XC    WTORECB,WTORECB          CLEAR ECB
         LA    R6,WTORPSD
         LA    R7,WTORECB
         WTOR  ,(6),,(7),MF=(E,WTORAREA)      ISSUE WTOR NOW
         WAIT  ECB=WTORECB              WAIT FOR REPLY
         SPACE 1
         XC    ACVALD(ACVLEN),ACVALD    CLEAR ACF2 BLOCK
         MVI   ACVFCN,1
         MVI   ACVSFCN,ACVSVALD
         OI    ACVCNTL,ACVCIPSD+ACVCNACT+ACVCAUTH+ACVCTSOP
         MVC   ACVLID,WTORLID
         MVC   ACVPSWD,WTORPSD
         XC    WTORPSD,WTORPSD
         LA    R1,WORKAMSG
         ST    R1,ACVMSG
         LA    R1,LIDREC
         ST    R1,ACVRECB
         LA    R1,1024
         ST    R1,ACVRECL
         ACFSVC ACVALD,TYPE=A,CVT=FIND,NONE=NOACF2
         LTR   R15,R15
         BNZR  R4
         SPACE 1
         TM    LIDFLAG5,LID5REFR
         BNO   ACF2BAD
         SLR   R15,R15
         BR    R4
ACF2BAD  DS    0H
         LA    R15,4
         BR    R4
NOACF2   DS    0H
         ABEND 2
         TITLE 'Z3426  CPU SOAK PROGRAM                                *
               - ISSUE WTO SUBROUTINE'
***********************************************************************
*                                                                     *
*  ISSUE WTO SUBROUTINE                                               *
*  ====================                                               *
*                                                                     *
*       ON ENTRY R9 POINTS TO MESSAGE TO BE ISSUED.                   *
*                                                                     *
*       Z3426Z MESSAGE SHOWS THE VALUES USED.                         *
*                                                                     *
*       Z3426V/W MESSAGES REQUIRE JOBNAME AND LOGONID IN THEM.        *
*                                                                     *
*       Z3426X/Y MESSAGES REQUIRE VALUES IN THEM.                     *
*                                                                     *
***********************************************************************
         SPACE 2
ISSUEWTO DS    0H
         MVC   WTORAREA(138),0(R9)      MOVE MSG TO AREA
         CLI   WTORAREA+9,C'Z'          CHECK FOR GOOD MSG
         BE    WTOGOOD                  YES THEN BRANCH
         SPACE 1
         CLI   WTORAREA+9,C'V'          CHECK FOR MSG V/W/X/Y
         BL    MSGNOW
         SPACE 1
         CLI   WTORAREA+9,C'X'          CHECK FOR MSG X/Y
         BL    WTOVW
         SPACE 1
WTOXY    DS    0H
         L     R1,PARM1LEN              GET PARM POINTER AND LENGTH
         L     R2,PARM1PTR
         EX    R1,WTOMVC1               MOVE INTO MESSAGE
         L     R2,PARM2PTR
         L     R1,PARM2LEN              AND AGAIN FOR 2ND PARM
         EX    R1,WTOMVC2
         B     MSGNOW
         SPACE 1
WTOMVC1  MVC   WTORAREA+66(0),0(R2)
WTOMVC2  MVC   WTORAREA+22(0),0(R2)
         SPACE 2
WTOVW    DS    0H
         LA    R1,0                     R1 -> PSA
         USING PSA,R1
         L     R1,PSATOLD               R1 -> TCB
         USING TCB,R1
         L     R1,TCBTIO                R1 -> TIOT
         USING TIOT1,R1
         CLI   WTORAREA+9,C'V'
         BNE   WTOW
WTOV     DS    0H
         MVC   WTORAREA+12(8),TIOCNJOB
         MVC   WTORAREA+46(8),WTORLID
         B     MSGNOW
WTOW     DS    0H
         MVC   WTORAREA+46(8),TIOCNJOB
         DROP  R1
         MVC   WTORAREA+12(8),WTORLID
         B     MSGNOW
         SPACE 2
WTOGOOD  DS    0H
         LA    R9,WTORAREA
         MVC   40(9,R9),PATTERN         MOVE IN EDIT PATTERN
         MVC   62(9,R9),PATTERN
         L     R1,WAITBIN               PICK UP CALC WAIT TIME
         CVD   R1,PACKED
         ED    40(9,R9),PACKED+4        EDIT INTO MESSAGE
         L     R1,PARM2PRV
         CVD   R1,PACKED
         ED    62(9,R9),PACKED+4
MSGNOW   DS    0H
         LA    R9,WTORAREA
         WTO   MF=(E,(9))
         BR    R10
         TITLE 'Z3426  CPU SOAK PROGRAM                                *
               - PARM ANALYSIS SUBROUTINE'
PARMANAL DS    0H
***********************************************************************
*                                                                     *
*  PARM ANALYSIS SUBROUTINE                                           *
*  ========================                                           *
*                                                                     *
*       ON ENTRY R3 POINTS TO HALFWORD LENGTH FOLLOWED BY PARM VALUE  *
*                                                                     *
*       ON EXIT R9 POINTS TO ERROR MESSAGE IF APPLICABLE              *
*               R15 CONTAINS RETURN CODE 0 OR 4                       *
*                                                                     *
***********************************************************************
         SPACE 2
*                                       +--------------------------+
*                                       |  CHECK FOR NO PARM       |
*                                       +--------------------------+
         LA    R9,MSGA
         LH    R4,0(R3)
         LTR   R4,R4
         BZ    PARMBAD
         SPACE 2
*                                       +--------------------------+
*                                       |  IF OPEN BRACKET FOUND   |
*                                       |  THEN CLOSE BRACKET MUST |
*                                       |  BE PRESENT.             |
*                                       +--------------------------+
         LA    R9,MSGB
         CLI   2(R3),C'('
         BNE   PARMADJ2
         LA    R1,1(R4,R3)
         CLI   0(R1),C')'
         BNE   PARMBAD
         SPACE 2
*                                       +--------------------------+
*                                       | ADJUST R3 -> ACTUAL PARM |
*                                       | AND R4 TO ACTUAL LENGTH  |
*                                       +--------------------------+
PARMADJ1 DS    0H
         LA    R9,MSGA
         SH    R4,=H'2'
         BNP   PARMBAD
         LA    R3,3(R3)
         B     PACHKNUM
         SPACE 1
PARMADJ2 DS    0H
         LA    R3,2(R3)
         B     PACHKNUM
         SPACE 2
*                                       +--------------------------+
*                                       | CHECK FOR TWO PARAMETERS |
*                                       | ENSURE THEY ARE NUMERIC  |
*                                       | AND THE LENGTH OF THEM   |
*                                       | IS BETWEEN 1 AND 6. THIS |
*                                       | IS 7 AND A HALF PENCE IN |
*                                       | DECIMAL CURRENCY.        |
*                                       +--------------------------+
PACHKNUM DS    0H
         ST    R3,PARM1PTR
         LA    R9,MSGC
         BCTR  R4,0
         SLR   R1,R1
         EX    R4,PATRT1
         BZ    PARMBAD                  ALL NUMS = 1 PARM ONLY
         BC    2,PARMBAD                AT END = 1 PARM ONLY
         SPACE 1
         LA    R9,MSGD
         CLI   0(R1),C','               HAVE WE STOPPED AT COMMA?
         BNE   PARMBAD                  NO THEN NON NUM FOUND
         SPACE 1
         LA    R9,MSGE
         SR    R1,R3                    CALCULATE LENGTH FIRST PARM
         BNP   PARMBAD                  BAD LENGTH
         C     R1,=F'6'
         BH    PARMBAD                  BAD LENGTH
         BCTR  R1,0                     MINUS 1 FOR EXEC
         ST    R1,PARM1LEN
         EX    R1,PAPACK1               GO PACK IT
         CVB   R2,PACKED                INTO BINARY
         ST    R2,PARM1BIN              AND STORE IT
         SPACE 1
         LA    R9,MSGD
         LA    R3,2(R1,R3)              POINT TO SECOND PARM
         ST    R3,PARM2PTR
         SR    R4,R1                    GET MACHINE LENGTH FOR PARM
         SH    R4,=H'2'                 R4 = LENGTH OF SECOND PARM - 1
         ST    R4,PARM2LEN
         SLR   R1,R1
         EX    R4,PATRT1
         BNZ   PARMBAD                  NON NUM IN SECOND PARM
         SPACE 1
         LA    R9,MSGE
         LA    R4,1(R4)                 MUST BE AT LEAST 1 CHAR LONG
         C     R4,=F'6'                 IF IT GETS HERE. CHECK THAT IT
         BH    PARMBAD                  IS NO LONGER THAN SIX CHARS.
         SPACE 1
         BCTR  R4,0                     MINUS 1 FOR EXEC
         EX    R4,PAPACK1               GO PACK IT
         CVB   R2,PACKED
         ST    R2,PARM2BIN
         SPACE 2
*                                       +--------------------------+
*                                       | CHECK THAT PARMS ARE IN  |
*                                       | THE ALLOWABLE RANGE.     |
*                                       +--------------------------+
         LA    R9,MSGF
         LA    R2,2
         LA    R1,PARM1BIN
PARANGE  DS    0H
         L     R7,0(R1)
         C     R7,=F'1'
         BL    PARMBAD
         C     R7,=F'999999'
         BH    PARMBAD
         LA    R1,4(R1)
         BCT   R2,PARANGE
         SPACE 2
*                                       +--------------------------+
*                                       | ENSURE SECOND PARM IS    |
*                                       | NOT MORE THAN 80% OF THE |
*                                       | FIRST PARM (TOTAL CYCLE  |
*                                       | TIME).                   |
*                                       +--------------------------+
         LA    R9,MSGX
         L     R7,PARM2BIN
         SLR   R6,R6
         M     R6,=F'100'
         D     R6,PARM1BIN
         C     R7,=F'80'
         BH    PARMBAD
*                                       +--------------------------+
*                                       | CALCULATE WAIT TIME AND  |
*                                       | BUSY TIME. ADJUST FOR THE|
*                                       | STIMER MACRO AND STORE IN|
*                                       | DOUBLEWORDS.             |
*                                       +--------------------------+
         LA    R9,MSGY
         L     R7,PARM1BIN              TO GET WAIT TIME
         S     R7,PARM2BIN
         BNP   PARMBAD
         ST    R7,WAITBIN               STORE WAIT TIME B4 ADJUSTMENT
         SLR   R6,R6                    CLEAR R6
         SLDL  R6,12                    SHIFT LEFT FOR STIMER MICVL
         STM   R6,R7,WAITTIME           AND STORE IT
         SPACE 1
         SLR   R6,R6                    CLEAR R6
         L     R7,PARM2BIN              GET BUSY TIME
         ST    R7,PARM2PRV
         SLDL  R6,12                    SHIFT LEFT FOR STIMER MICVL
         STM   R6,R7,BUSYTIME           AND STORE IT
         SPACE 2
*                                       +--------------------------+
*                                       | PARM IS GOOD THEREFORE   |
*                                       | RETURN WITH R15 = 0      |
*                                       +--------------------------+
PARMGOOD DS    0H
         SLR   R15,R15
         BR    R10
*                                       +--------------------------+
*                                       | PARM IS BAD  THEREFORE   |
*                                       | RETURN WITH R15 = 4      |
*                                       +--------------------------+
PARMBAD  DS    0H
         LA    R15,4
         BR    R10
         SPACE 2
PATRT1   TRT   0(0,R3),PATABLE
PAPACK1  PACK  PACKED,0(0,R3)
         TITLE 'Z3426  CPU SOAK PROGRAM                                *
               - STIMER SUBROUTINE'
         USING EXITRTN,R15
EXITRTN  DS    0H
         STM   R14,R12,12(R13)
         OI    SWITCH,SWTIMEUP
         LM    R14,R12,12(R13)
         BR    R14
         DROP  R15
         TITLE 'Z3426  CPU SOAK PROGRAM                                *
               - CONSTANTS AND DSECTS'
MSGA     WTO   'Z3426A  NO PARAMETERS SPECIFIED',                      *
               ROUTCDE=(1,11),DESC=(2),MF=L
MSGB     WTO   'Z3426B  UNBALANCED PARENTHESES IN PARM FIELD',         *
               ROUTCDE=(1,11),DESC=(2),MF=L
MSGC     WTO   'Z3426C  ONLY ONE PARAMETER FOUND - TWO REQUIRED',      *
               ROUTCDE=(1,11),DESC=(2),MF=L
MSGD     WTO   'Z3426D  PARM FIELD CONTAINS NON-NUMERICS',             *
               ROUTCDE=(1,11),DESC=(2),MF=L
MSGE     WTO   'Z3426E  INVALID PARAMETER LENGTH',                     *
               ROUTCDE=(1,11),DESC=(2),MF=L
MSGF     WTO   'Z3426F  PARAMETER OUT OF RANGE : 1 - 999999',          *
               ROUTCDE=(1,11),DESC=(2),MF=L
MSGG     WTO   'Z3426G  START CIB NOT FREED',                          *
               ROUTCDE=(1,11),DESC=(2),MF=L
MSGH     WTO   'Z3426H  UNKNOWN CIBVERB',                              *
               ROUTCDE=(1,11),DESC=(2),MF=L
MSGI     WTOR  'Z3426I  ENTER AN AUTHORISED LOGONID',                  *
               6,8,7,ROUTCDE=(1,11),DESC=(1),                          *
               MF=L
MSGJ     WTOR  'Z3426J  ENTER YOUR PASSWORD',                          *
               6,8,7,ROUTCDE=(1,9,11),DESC=(1),                        *
               MF=L
MSGV     WTO   'Z3426V           HAS BEEN STOPPED BY LID           ',  *
               ROUTCDE=(1,11),DESC=(2),MF=L
MSGW     WTO   'Z3426W           NOT AUTHORISED TO MODIFY         ',   *
               ROUTCDE=(1,11),DESC=(2),MF=L
MSGX     WTO   'Z3426X  BUSY TIME        IS MORE THAN 80% OF TOTAL CYCL*
               E TIME       ',ROUTCDE=(1,11),DESC=(2),MF=L
MSGY     WTO   'Z3426Y  BUSY TIME        IS GREATER THAN THE TOTAL CYCL*
               E TIME       ',ROUTCDE=(1,11),DESC=(2),MF=L
MSGZ     WTO   'Z3426Z  VALUES IN USE -  WAIT TIME :           BUSY TIM*
               E :         ',ROUTCDE=(1,11),DESC=(2),MF=L
PATTERN  DC    X'402020202020202020'
PATABLE  DC    256X'FF'                 ****************************
         ORG   PATABLE+C','             *  TABLE FOR ENSURING      *
         DC    C','                     *  OS PARM/MODIFY PARM     *
         ORG   PATABLE+C'0'             *  IS NUMERIC AND HAS TWO  *
         DC    10X'00'                  *  PARAMETERS.             *
*                                       ****************************
         ORG
SWITCH   DS    X                         HAS TO BE NON-REENTRANT COS
SWTIMEUP EQU   X'80'                     THIS SWITCH CANT BE ADDRESSED
*                                        IN THE STIMER EXIT ROUTINE
*
COM      DSECT
         IEZCOM
CIB      DSECT
         IEZCIB
WORKAREA DSECT
         DS    18F
WACLEAR  EQU   *
WAEYE    DS    CL8
PACKED   DS    PL8                      TEMPORARY PACK AREA
PARM1BIN DS    F                        FIRST PARM TEMP STORAGE (BIN)
PARM2BIN DS    F                        SECOND PARM TEMP STORAGE (BIN)
PARM2PRV DS    F                        SECOND PARM PREVIOUS FOR WTO
WAITBIN  DS    F                        CALCULATED WAIT TIME IN BINARY
PARM1PTR DS    F                        POINTER TO PARM 1  FOR WTO
PARM1LEN DS    F                        MACHINE LENGTH OF PARM 1
PARM2PTR DS    F                        POINTER TO PARM 2  FOR WTO
PARM2LEN DS    F                        MACHINE LENGTH OF PARM 2
WAITTIME DS    D                        CALCULATED WAIT TIME FOR STIMER
BUSYTIME DS    D                        BUSY TIME FOR SECOND STIMER
R10SAVE  DS    F                        AREA TO SAVE BAL REGISTER
EXTRCTWD DS    F                        EXTRACT WORD -> CIB
WORKAMSG DS    CL128                    ACF2 RETURN MSG AREA
WTORLID  DS    CL8                      WTOR REPLY AREA FOR LID
WTORPSD  DS    CL8                      WTOR REPLY AREA FOR PASSWORD
WTORECB  DS    F                        WTOR ECB
CIBSAVE  DS    CL30                     CIBSAVE AREA FOR MODIFY VALUES
         ACVALD DSECT=NO
         LIDREC DSECT=NO
         DS    0F
WTORAREA DS    CL138                    FOR MESSAGES TO BE ISSUED
WALEN    EQU   *-WORKAREA
WACLRLEN EQU   *-WACLEAR
         ACCVT  DSECT=YES
         ACUCB  DSECT=YES
         IHAPSA DSECT=YES
         IKJTCB DSECT=YES
         IEFTIOT1
         IHAECB
         END
