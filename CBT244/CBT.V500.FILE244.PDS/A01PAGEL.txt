**********************************************************************
* OD00.LEB.SOURCE(PAGELST)                                           *
*                                                                    *
* THIS IS IS THE SOURCE CODE FOR A PROGRAM WHICH LISTS ON A WTO      *
* TO SYSLOG THE ACTIVE SWAP AND PAGE DATASETS.                       *
*                                                                    *
* THE ASSEMBLED AND LINK-EDITED MODULE IS IN                         *
* SYS4.LEB.LINKLIB(PAGELST)                                          *
*                                                                    *
* T J STAMMERS  17/07/85                                             *
* S MOSS        17/03/89                                             *
*               AMNEDED TO CONFIRM WITH MVS 2.2.0 CHANGES            *
**********************************************************************
*
*HOUSEKEEPING OPERATIONS
*================================================
PAGELST  CSECT
*
         SAVE  (14,12)       * SAVE SYSTEM REGISTER CONTENTS
*
         BALR  12,0          * ADDRESSABILITY ESTABLISHED,USING
         USING *,12          * REGISTER 12 TO HOLD BASE ADDRESS
*
         ST    13,SAVE+4     * ESTABLISH CHAIN FROM REGISTER 13 TO
         LA    R13,SAVE      * OS STORAGE OF SYSTEM REGISTER CONTENTS
*
PAGELST  RMODE ANY           * SET RESIDENCY MODE TO 'ANY'
*                            * ADRESS MODE WILL NOW DEFAULT AS
*                            * APPROPIATE
R0       EQU 0
R1       EQU 1
R2       EQU 2
R3       EQU 3
R4       EQU 4
R5       EQU 5
R6       EQU 6
R7       EQU 7
R8       EQU 8
R9       EQU 9
R10      EQU 10
R11      EQU 11
R12      EQU 12
R13      EQU 13
R14      EQU 14
R15      EQU 15
*
*PROCEDURE
*=========================================
*
*CREATE ACTIVE SWAP DATASET LIST
*--------------------------------------
LIST1    WTO   ' ****   ACTIVE SWAP DATASET LIST   **** ',ROUTCDE=1
*
         L     R2,16             * POINTER TO CVT
         L     R3,X'2C0'(R2)     * POINTER TO CVT ASMVT
         L     R4,X'4'(R3)       * POINTER TO ASMSART
         BAL   R9,PRINTRT        * PERFORM PRINT ROUTINE
*
         LTR   R15,R15           * TEST - WERE ANY DATASETS LISTED ?    NTRT ?
         BZ    LIST2             * IF YES, SKIP NEXT WTO, GO TO LIST 2
*
         WTO   ' **** NO ACTIVE SWAP DATASET FOUND ***** ',ROUTCDE=1
*
* CREATE ACTIVE PAGE DATASET LIST
*-------------------------------------
LIST2    WTO   ' ****   ACTIVE PAGE DATASET LIST   ***** ',ROUTCDE=1
*
         L     R4,X'8'(R3)       * POINTER TO ASMPART
         BAL   R9,PRINTRT        * PERFORM PRINT ROUTINE
         BR    R14               * EXIT FROM PROGRAM
*
* PRINT ROUTINE
*-------------------------------------
PRINTRT  LA    R15,4             * SET ACTIVE DATASET FLAG TO 'NONE'
         L     R5,X'18'(R4)      * POINTER TO DSNAME LIST
*                                * (PARTDSNL/SARTDSNL)
         LH    R6,X'8'(R4)       * POINTER TO COUNT OF ACTIVE DATASETS
*                                * (PARTEUSE/SARTEUSE)
*
         LTR   R6,R6             * TEST - ARE THERE ANY ACTIVE DSETS ?
         BZR   R9                * IF NONE, RETURN FROM THIS ROUTINE
*
         LA    R7,44             * ADD 44 FOR NEXT DSET NAME
LOOP     CLC   BLANKS,0(R5)      * TEST - DOES R5 POINT TO 44 BLANKS ?
         BE    BRANCH            * IF YES, GO TO BRANCH
*
         MVC   DSNAME+2(44),0(R5)  * MOVE DSNAME TO PRINT AREA (1)
PRNAME   MVC   DSLIST+4(30),DSNAME * MOVE DSNAME TO PRINT AREA (2)
         WTO   MF=(E,DSLIST)       * WRITE THE DSNAME
*
BRANCH   LA    R5,0(R7,R5)        * ADD 44 FOR THE NEXT DSNAME
         BCT   R6,LOOP            * GO TO LOOP, UNTIL ALL DS PRINTED
         BR    R9                 * RETURN
*
*STORAGE
*--------------------------------
DSLIST   WTO   '                                ',ROUTCDE=1,MF=L
DSNAME   DC    CL80' '
BLANKS   DC    CL44' '
SAVE     DS    18F
         END
