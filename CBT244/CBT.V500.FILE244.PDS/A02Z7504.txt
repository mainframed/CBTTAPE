         TITLE 'Z7504: CONVERT CHARACTERS IN TSO VARS                  *
               - PROGRAM DOCUMENTATION'
*****                                                             *****
*****                                                             *****
*                                                                     *
*        Z7504: CONVERT CHARACTERS IN TSO VARS                        *
*                                                                     *
* Z7504.1  PROGRAM DESCRIPTION                                        *
*                                                                     *
*   THIS PROGRAM WILL CONVERT THE FOLLOWING CHARACTERS:-              *
*                                                                     *
*        FROM                       TO                                *
*        ====                       ==                                *
*                                                                     *
*         &                         +                                 *
*         (                         {                                 *
*         )                         }                                 *
*                                                                     *
*   THE VARIABLE NAMES ARE PASSED TO IT VIA THE PARM FIELD            *
*   OF THE CALL COMMAND. THE ONLY LIMIT TO THE NUMBER OF VARIABLES    *
*   THAT CAN BE PASSED IS LIMITED BY THE SIZE OF THE PARM FIELD (100  *
*   BYTES).                                                           *
*                                                                     *
*   THIS PROGRAM WAS WRITTEN FOR THE LOLA LIBRARY INDEX DIALOGUE      *
*   BECAUSE IF AN AMPERSAND IS ENTERED IN A FIELD OF A PANEL WHICH    *
*   IS THEN INTERROGATED BY A CLIST, THE CLIST THROWS ITS HANDS IN    *
*   THE AIR AND GIVES UP.  IT HAS BEEN AMENDED TO INCLUDE THE         *
*   CHARACTERS '(' AND ')' WHICH CAUSED PROBLEMS IN THE PSIP          *
*   DIALOGUE.                                                         *
*                                                                     *
*****                                                             *****
*****                                                             *****
         EJECT
*****                                                             *****
*****                                                             *****
*                                                                     *
* Z7504.2  USER INSTRUCTIONS                                          *
*                                                                     *
*   Z7504 MUST BE CALLED FROM WITHIN A "PROPER" CLIST.  THAT IS,      *
*   THE FIRST STATEMENT OF THE CLIST MUST BE A "PROC" STATEMENT.      *
*                                                                     *
*   THE STATEMENT TO CALL Z7504 IS:                                   *
*      CALL 'SG1.LOAD(Z7504)'  'VAR1,VAR2,VAR3'                       *
*                                                                     *
*      WHERE VAR1,VAR2 AND VAR3 ARE THE VARIABLE NAMES WHOSE          *
*      VALUES MAY CONTAIN AMPERSANDS OR BRACKETS.                     *
*                                                                     *
*                                                                     *
*                                                                     *
*****                                                             *****
*****                                                             *****
         EJECT
*****                                                             *****
*****                                                             *****
*                                                                     *
* Z7504.3  RETURN CODES                                               *
*                                                                     *
*   RC = 0     NORMAL COMPLETION                                      *
*                                                                     *
*                                                                     *
*      THE FOLLOWING RETURN CODES ARE THE STANDARD CODES RETURNED     *
*      FROM IKJCT441 WITH A VALUE ADDED TO SHOW WHERE IN Z7504        *
*      THE ERROR OCCURRED.                                            *
*                                                                     *
*                                                                     *
*   RC = 1000  PARM INFORMATION MISSING.                              *
*                                                                     *
*   RC = 20XX  INVALID RETURN CODE FROM IKJCT441 AT LABEL GETVAR      *
*              WHEN RETRIEVING A TSO VARIABLE.                        *
*                                                                     *
*   RC = 30XX  INVALID RETURN CODE FROM IKJCT441 AT LABEL UPDTVAR     *
*              WHEN UPDATING A TSO VARIABLE.                          *
*                                                                     *
*****                                                             *****
*****                                                             *****
         EJECT
*****                                                             *****
*****                                                             *****
*                                                                     *
* Z7504.4  MESSAGES                                                   *
*                                                                     *
*          NONE.                                                      *
*                                                                     *
*                                                                     *
*                                                                     *
*****                                                             *****
*****                                                             *****
         EJECT
*****                                                             *****
*****                                                             *****
*                                                                     *
* Z7504.5  ABEND CODES                                                *
*                                                                     *
*          NONE.                                                      *
*                                                                     *
*****                                                             *****
*****                                                             *****
         EJECT
*****                                                             *****
*****                                                             *****
*                                                                     *
* Z7504.6  OPERATING INSTRUCTIONS                                     *
*                                                                     *
*   THIS SECTION SHOULD BE LAID OUT AS FAR AS POSSIBLE ACCORDING      *
*   TO THE STANDARDS MANUAL SECTION 6, APPENDIX 1, BUT WITHOUT        *
*   DUPLICATING OTHER SECTIONS OF THIS PROGRAM DOCUMENTATION,         *
*   SUCH AS DESCRIPTION, MSGS & CODES.                                *
*                                                                     *
*   IT IS ONLY REQUIRED IF THE PROGRAM IS TO BE RUN BY OPERATIONS     *
*                                                                     *
*   SEE SECTION Z7504.2 (USER INTRUCTIONS) FOR USE.                   *
*                                                                     *
*****                                                             *****
*****                                                             *****
         EJECT
*****                                                             *****
*****                                                             *****
*                                                                     *
* Z7504.7  IMPLEMENTATION METHOD                                      *
*                                                                     *
*        COPY-UP TO LOLA1.SYSG.SOURCE AND SG1.LOAD.                   *
*        THIS PROGRAM IS RE - ENTRANT.                                *
*                                                                     *
*****                                                             *****
*****                                                             *****
         EJECT
*****                                                             *****
*****                                                             *****
*                                                                     *
* Z7504.8  AMENDMENT HISTORY                                          *
*                                                                     *
*   THIS SHOULD BE COMPLETED JUST BEFORE FINAL COPY-UP OF ORIGINAL    *
*   OR NEW VERSION OF ANY MODULE OF THE PROGRAM. ENSURE REST OF       *
*   PROGRAM DOCUMENTATION IS COMPLETED/AMENDED. THEN ISSUE/RE-ISSUE   *
*   THIS DOCUMENTATION BY PHOTO-COPYING COPY-UP LISTING, OR BY        *
*   EXTRACTING & PRINTING NECESSARY SECTIONS. SECTIONS REQUIRED ARE:  *
*                                                                     *
*     FOR STANDARD FACILITIES           SECTIONS 1-5        ONE       *
*         SYSTEM   FACILITIES           SECTIONS 1-8          OF      *
*         Z34/Z66 BINDERS               SECTIONS 1-8            THESE *
*                                                                     *
*         LOLA TP MSGS & CODES          SECTIONS 4-5        IF ANY, & *
*                                                ONLY FOR TP PROGRAMS *
*                                                                     *
*         OPERATING INSTRUCTIONS        SECTIONS 1,3-6      IF NEC.   *
*                                                                     *
*   DOCUMENTATION GOING OUTSIDE TSG SHOULD BE TYPED, AND MAY NEED     *
*   FORMATTING INSTRUCTIONS. OP. INS. NEED FURTHER SECTIONS - SEE     *
*   STANDARDS MANUAL SECTION 6 APPENDIX 1.                            *
*                                                                     *
*                                                                     *
*   THE AMENDMENTS SECTION OF THE RELEVANT MODULE PROLOGUE(S) MUST    *
*   ALSO BE COMPLETED.                                                *
*                                                                     *
*   DO NOT DELETE THESE INSTRUCTIONS.                                 *
*                                                                     *
***********************************************************************
*       *     *     *          *                                      *
* MODULE*VERSN*INTLS* COPY-UP  * AMENDMENT                            *
*   ID  * NO  *     *  DATE    *        DESCRIPTION                   *
*       *     *     *          *                                      *
***********************************************************************
*       *     *     *          *                                      *
* Z7504 * 00  * MJT * 11.11.87 * ORIGINAL VERSION                     *
* Z7504 * 01  * MJT * 09.06.89 * AMENDED TO CONVERT BRACKETS          *
* Z7504 * 02  * MJT * 23.11.89 * AMENDED RE: TSO V2.1 ISPF V3.1       *
*****                                                             *****
*****                                                             *****
         EJECT
*****                                                             *****
*****                                                             *****
*                                                                     *
* Z7504.9 MODULARISATION DETAILS                                      *
*                                                                     *
*         NONE                                                        *
*                                                                     *
*****                                                             *****
*****                                                             *****
         TITLE 'Z7504: CONVERT CHARACTERS IN TSO VARS                  *
               - MODULE PROLOGUE'
*****                                                             *****
*****                                                             *****
*                                                                     *
* ID:          Z7504  CONVERT CHARACTERS IN TSO VARS                  *
*                                                                     *
*                                                                     *
* TASKS:       1. CHECK IF ANY PARAMETERS PASSED AND ISSUE RETURN     *
*                 CODE OF 1000  IF NONE.                              *
*                                                                     *
*              2. FIND THE FIRST PARAMETER. CALCULATE ITS LENGTH      *
*                 STORE THE LENGTH IN WAVARLEN AND ITS NAME IN        *
*                 WAVARNAM.                                           *
*                                                                     *
*              3. SET UP IKJCT441 PARMS AND CALL IT TO RETRIEVE       *
*                 THE FIRST VARIABLE.IF AN ERROR OCCURS THEN ADD      *
*                 2000 TO THE RETURN CODE AND EXIT.                   *
*                                                                     *
*              4. CHECK THE VARIABLE VALUE FOR ANY AMPERSANDS OR      *
*                 BRACKETS AND CHANGE THEM ACCORDINGLY.               *
*                                                                     *
*              5. IF ANY CHANGES HAVE BEEN MADE CALL IKJCT441 TO      *
*                 UPDATE THE TSO VARIABLE. IF AN ERROR OCCURS AT      *
*                 THIS POINT THEN ADD 3000 TO THE RETURN CODE AND     *
*                 EXIT.                                               *
*                                                                     *
*              6. REPEAT FROM STEP 2 UNTIL THERE ARE NO MORE          *
*                 PARAMETERS LEFT TO CHECK.                           *
*                                                                     *
* ENTRY INTFCE:R1 POINTS TO  PARM LIST                                *
*                                                                     *
* EXIT INTFCES: NONE.                                                 *
*                                                                     *
* RETURN CODES: SEE SECTION Z7504.3 ABOVE.                            *
*                                                                     *
*                                                                     *
* REG USAGE:   R0    -  WORK AND SYSTEM USAGE                         *
*              R1    -  WORK AND SYSTEM USAGE                         *
*              R2    -  WORK                                          *
*              R3    -> PARM VALUE                                    *
*              R4    -  LENGTH OF PARM FIELD                          *
*              R5    -  LENGTH OF VARIABLE VALUE                      *
*              R6    -> VARIABLE VALUE                                *
*              R7    -  WORK                                          *
*              R8    -  NOT USED                                      *
*              R9    BAL REG FOR COMMON SUBROUTINES                   *
*              R10   BAL REG FROM MAIN ROUTINE                        *
*              R11   SECOND BASE REGISTER                             *
*              R12   FIRST BASE REGISTER                              *
*              R13   SAVE AREA + WORK AREA                            *
*              R14   -  WORK AND SYSTEM USAGE                         *
*              R15   -  SYSTEM USAGE                                  *
*                                                                     *
* MESSAGES:    SEE SECTION Z7504.4 ABOVE                              *
*                                                                     *
* ABENDS:      SEE SECTION Z7504.5 ABOVE                              *
*                                                                     *
* NOTES:       THIS PROGRAM IS RE - ENTRANT                           *
*                                                                     *
* VERSION:     00 - MJT   - 11.11.87 - ORIGINAL VERSION               *
* VERSION:     01 - MJT   - 09.06.89 - AMENDED FOR BRACKETS           *
* VERSION:     02 - MJT   - 23.11.89 - AMENDED FOR TSO V2.1 ISPF V3.1 *
*                                                                     *
*****                                                             *****
*****                                                             *****
         TITLE 'Z7504: CONVERT CHARACTERS IN TSO VARS                  *
               - ENTRY HOUSEKEEPING'
Z7504    SETUP DATE='Â¢DATEACS',                                        *
               12,11,RE=YES,LDSA=WALEN
*
*                                       *******************************
*                                       * CLEAR WORK AREA (NOT SAVE   *
*                                       * AREA) AND ADDRESS IT.       *
*                                       *******************************
*
         USING WORKAREA,R13
         LA    R2,WACLEAR
         LA    R3,WACLRLEN
         SR    R14,R14
         SR    R15,R15
         MVCL  R2,R14
         MVC   WAEYE,=CL8'WORKAREA'
         TITLE 'Z7504: CONVERT CHARACTERS IN TSO VARS                  *
               - MAIN ROUTINE'
*+--------------------------------------------------------------------+
*|       CHECK FOR PASSED PARAMETERS                                  |
*+--------------------------------------------------------------------+
         SPACE 2
         LR    R3,R1
         L     R3,0(R3)                 R3 -> PARM
         LH    R4,0(R3)                 R4 = LENGTH OF PARM
         LA    R3,2(R3)                 R3 -> START OF PARM
         LTR   R4,R4                    CHECK IF PARM PRESENT
         BNZ   PARMST
         LA    R15,1000                 INDICATE ERROR
         B     EXIT                     AND RETURN
         SPACE 2
*
*        SCAN FOR PARM VALUES
*
         SPACE 1
PARMST   DS    0H
         TM    SWPARM,SWPARMON
         BO    EXIT0
         BCTR  R4,0                     R4 -1
         EX    R4,COMMASRC              SEARCH FOR COMMA
         BZ    PARMEND                  END OF PARM THEN BRANCH
         SR    R1,R3                    R1 = LENGTH OF VAR NAME
         ST    R1,WAVARLEN              STORE VAR NAME LENGTH
         BCTR  R1,0                     LESS 1 FOR EXEC
         EX    R1,MVCNAME               EX MOVE LEN=R1 -> BY R3
         AR    R3,R1                    POINT R3 TO NEXT VAR NAME
         LA    R3,2(R3)                   "    "  "   "   "   "
         SR    R4,R1                    ADJUST LENGTH OF PARM
         BCTR  R4,0                        "     "    "    "
         B     RETRIEVE                 GO AND RETRIEVE VARIABLE
PARMEND  DS    0H
         EX    R4,MVCNAME               IF LAST OR ONLY PARM MOVE
*                                       FOR FULL LENGTH
         LA    R4,1(R4)
         ST    R4,WAVARLEN
         OI    SWPARM,SWPARMON
         B     RETRIEVE                 GO AND RETRIEVE VARIABLE
         SPACE 3
RETRIEVE DS    0H
         BAL   R10,GETVAR
         LTR   R15,R15
         BNZ   EXIT
         SPACE 3
*+--------------------------------------------------------------------+
*|       TRANSLATE CHARACTERS IN VARIABLE VALUE.                      |
*+--------------------------------------------------------------------+
         SPACE 2
TRANSLTE DS    0H
         L     R5,WAFLDVLN              OBTAIN LENGTH OF VAR VALUE
         LTR   R5,R5                    IF ZERO THEN TRY FOR NEXT
         BZ    PARMST
         SPACE 1
         L     R6,WAFLDVAL              R6 -> VAR VALUE
         BCTR  R5,0                     LEN -1 FOR EXEC
         EX    R5,MVCVAL                SAVE VIRGIN VALUE FOR LATER
         EX    R5,MVCVAL2               SAVE MODIFYABLE VALUE
         LA    R5,1(R5)                 R5 = CORRECT LENGTH
         LR    R2,R5                    R2 = LENGTH
         LA    R7,WASAVVL2              R7 -> VAR VALUE
TLOOP    DS    0H
         CLI   0(R7),C'&&'              LOOK FOR AMPERSANDS
         BE    CHAMPER                  AND CHANGE THEM
         CLI   0(R7),C'('               LOOK FOR OPEN BRACKET
         BE    CHBRKOPE                 AND CHANGE THEM
         CLI   0(R7),C')'               LOOK FOR CLOSE BRACKET
         BE    CHBRKCLS                 AND CHANGE THEM
         B     UPVAR
         SPACE 1
CHAMPER  DS    0H
         MVI   0(R7),C'+'               IF ANY CHANGE TO PLUSSES
         B     UPVAR
         SPACE 1
CHBRKOPE DS    0H
         MVI   0(R7),C'{'               IF ANY CHANGE TO SPEC BRKS
         B     UPVAR
         SPACE 1
CHBRKCLS DS    0H
         MVI   0(R7),C'}'               IF ANY CHANGE TO SPEC BRKS
         B     UPVAR
         SPACE 1
UPVAR    DS    0H
         LA    R7,1(R7)                 BUMP R7
         BCT   R2,TLOOP                 AND LOOP IF LENGTH PERMITS
         BCTR  R5,0
         EX    R5,CLCVAL                SEE IF UPDATE VAR REQUIRED
         BE    PARMST                   NO THEN PROCESS NEXT VAR.
         BAL   R10,UPDTVAR              YES THEN UPDATE
         LTR   R15,R15
         BNZ   EXIT
         B     PARMST
EXIT0    DS    0H
         LA    R15,0
EXIT     DS    0H
         CLEAR RC=(15),RE=YES
         SPACE 3
COMMASRC TRT   0(0,R3),COMMATAB
MVCNAME  MVC   WAVARNAM(0),0(R3)        MOVE NAME TO IKJ AREA
MVCVAL   MVC   WASAVVAL(0),0(R6)        SAVE VALUE BEFORE MODIFICATION
MVCVAL2  MVC   WASAVVL2(0),0(R6)        SAVE VALUE FOR MODIFYING
CLCVAL   CLC   WASAVVAL(0),WASAVVL2     COMPARE VALUES
         TITLE 'Z7504: CONVERT CHARACTERS IN TSO VARS                  *
               - RETURN THE VARIABLE VALUE'
*+--------------------------------------------------------------------+
*|       GET VALUE OF VARIABLE NAME .                                 |
*+--------------------------------------------------------------------+
         SPACE 2
GETVAR   DS    0H
         SPACE 2
         LA    R1,CODERETR
         ST    R1,WAIKCODE
         LA    R1,WAVARNAM
         ST    R1,WAIKNAME
         LA    R1,WAIKNAME
         ST    R1,WAIKNPTR
         LA    R1,WAVARLEN
         ST    R1,WAIKNLEN
         LA    R1,WAFLDVAL
         ST    R1,WAIKVPTR
         LA    R1,WAFLDVLN
         ST    R1,WAIKVLEN
         OI    WAIKTOKN,X'80'
         BAL   R9,IKJRTN
         SPACE 2
         LTR   R15,R15
         BZR   R10                      OK  THEN RETURN
         SPACE 2
*                                       +-----------------------------+
*                                       | IF BAD RETURN CODE FROM     |
*                                       | IKJCT441, ADD 2000 TO SHOW  |
*                                       | IT WAS INVOKED IN "GETVAR"  |
*                                       +-----------------------------+
         SPACE 1
         LA    R15,2000(R15)
         BR    R10
         SPACE 2
         TITLE 'Z7504: CONVERT CHARACTERS IN TSO VARS                  *
               - UPDATE THE TSO VARIABLE '
         SPACE 3
*+--------------------------------------------------------------------+
*|       UPDATE VARIABLE                                              |
*+--------------------------------------------------------------------+
         SPACE 2
UPDTVAR  DS    0H
         SPACE 2
*                                       +-----------------------------+
*                                       | SET UP PARM LIST            |
*                                       +-----------------------------+
         SPACE 1
         LA    R1,CODEUPDT
         ST    R1,WAIKCODE
         LA    R1,WAVARNAM
         ST    R1,WAIKNAME
         LA    R1,WAIKNAME
         ST    R1,WAIKNPTR
         LA    R1,WAVARLEN
         ST    R1,WAIKNLEN
         LA    R1,WASAVVL2
         ST    R1,WAIKVALU
         LA    R1,WAIKVALU
         ST    R1,WAIKVPTR
         LA    R1,WAFLDVLN
         ST    R1,WAIKVLEN
         OI    WAIKTOKN,X'80'
         SPACE 2
*                                       +-----------------------------+
*                                       | GO AND CALL TSO ROUTINE     |
*                                       +-----------------------------+
         SPACE 1
         BAL   R9,IKJRTN
         LTR   R15,R15
         BZR   R10                      RETURN HERE - O.K.
         LA    R15,3000(R15)            SHOW ERROR IS HERE
         BR    R10
         SPACE 2
         TITLE 'Z7504: CONVERT CHARACTERS IN TSO VARS                  *
               - "IKJRTN": ACCESS A TSO VARIABLE'
*+--------------------------------------------------------------------+
*|       ACCESS A TSO VARIABLE                                        |
*+--------------------------------------------------------------------+
         SPACE 2
IKJRTN   DS    0H
         TM    WASWIKJ,SWTRIED+SWFOUND  HAVE WE LOOKED FOR A(IKJCT441)?
*                                         AND DID WE FIND IT?
         BO    IKJCALL                  YES TO BOTH
         BM    IKJLINK                  DIDN'T FIND IT
         OI    WASWIKJ,SWTRIED          SHOW WE (WILL) HAVE TRIED
         SPACE 2
*                                       +-----------------------------+
*                                       | FIRST TIME: TRY AND GET     |
*                                       | ADDRESS OF IKJCT441 VIA CVT |
*                                       | TSO VECTOR TABLE.  SET      |
*                                       | SWITCH ACCORDINGLY.         |
*                                       +-----------------------------+
         SPACE 1
         USING PSA,R0
         L     R15,FLCCVT               CVT POINTER
         DROP  R0
         USING CVT,R15
         L     R15,CVTTVT               TSO VECTOR TABLE POINTER
         USING TSVT,R15
         L     R15,TSVTVACC             IKJCT441 POINTER
         DROP  R15
         LTR   R15,R15
         BZ    IKJLINK                  NO ADDRESS FOUND - LINK TO IT
         OI    WASWIKJ,SWFOUND          ELSE SHOW WE FOUND IT
         ST    R15,WAIKSVAD               SAVE IT
         B     IKJCALL                    AND GO AND CALL IT
         SPACE 2
*                                       +-----------------------------+
*                                       | CALL IKJCT441 AND RETURN    |
*                                       +-----------------------------+
         SPACE 1
IKJCALL  DS    0H
         LA    R1,WAIKPARM
         L     R15,WAIKSVAD
         BALR  R14,R15
         BR    R9
         SPACE 2
*                                       +-----------------------------+
*                                       | LINK TO IKJCT441 AND RETURN |
*                                       +-----------------------------+
         SPACE 1
IKJLINK  DS    0H
         LA    R1,WAIKPARM
         LINK  EP=IKJCT441
         BR    R9
         TITLE 'Z7504: CONVERT CHARACTERS IN TSO VARS                  *
               - CONSTANTS AND DSECTS'
*+--------------------------------------------------------------------+
*|       CONSTANTS                                                    |
*+--------------------------------------------------------------------+
         SPACE 2
CODERETR DC    A(TSVERETR)              RETRIEVE A CLIST VAR VALUE
CODEUPDT DC    A(TSVEUPDT)              UPDATE A CLIST VAR VALUE
         SPACE 1
         DS    0D
COMMATAB DC    XL256'00'
         ORG   COMMATAB+C','
         DC    C','
         ORG   COMMATAB+C' '
         DC    C' '
         ORG
         EJECT
*+--------------------------------------------------------------------+
*|       WORKAREA DSECT                                               |
*+--------------------------------------------------------------------+
         SPACE 2
WORKAREA DSECT
         DS    18F                      RESERVED FOR SAVEAREA
WACLEAR  EQU   *
WAEYE    DS    CL8
WAFLDVAL DS    A                      A(RETURNED VALUE OF VARIABLE )
WAFLDVLN DS    A                        RETURNED LENGTH OF VALUE
WAVARNAM DS    CL100                  VARIABLE NAME MAX 100
WAVARLEN DS    A                      LENGTH OF VAR NAME
WASAVVAL DS    CL256                  ARE TO SAVE VALUE BEFORE MOD.
WASAVVL2 DS    CL256                  ARE TO SAVE VALUE FOR MOD.
*
WASWPARM DS    X                        PARM SWITCH
SWPARM   EQU   WASWPARM
SWPARMON EQU   X'80'
SWPARMOF EQU   X'FF'-SWPARMON
         SPACE 3
*+--------------------------------------------------------------------+
*| THE FOLLOWING AREA IS FOR USE BY THE COMMON SUBROUTINE "IKJRTN"    |
*|   AND ITS CALLERS                                                  |
*+--------------------------------------------------------------------+
         SPACE 2
WAIKPARM DS    0A                       PARM LIST FOR IKJCT441:
WAIKCODE DS    A                          A(ENTRY CODE)
WAIKNPTR DS    A                          A(WAIKNAME)
WAIKNLEN DS    A                          A(LENGTH OF NAME)
WAIKVPTR DS    A                          A(WAIKVALU)
WAIKVLEN DS    A                          A(LENGTH OF VALUE STRING)
WAIKTOKN DS    A                          A(TOKEN)
*
WAIKNAME DS    A                          A(NAME OF VARIABLE)
WAIKVALU DS    A                          A(VALUE OF VARIABLE)
*
WAIKSVAD DS    A                        SAVE ADDR OF IKJCT441
*
WASWIKJ  DS    X
SWTRIED  EQU   X'80'                    WE'VE TRIED TO FIND A(IKJCT441)
SWFOUND  EQU   X'40'                    WE'VE FOUND A(IKJCT441)
         SPACE 3
         DS    0D
WALEN    EQU   *-WORKAREA               LENGTH FOR LBRSETUP MACRO
WACLRLEN EQU   *-WACLEAR                LENGTH TO CLEAR
         EJECT
*+--------------------------------------------------------------------+
*|       OTHER DSECTS                                                 |
*+--------------------------------------------------------------------+
         SPACE 2
*
         IKJTSVT ,                      REQUIRED BY IKJCT441
*                                         (TSO VECTOR TABLE)
         PRINT NOGEN
*
         IHAPSA ,                       FOR CVT POINTER "FLCCVT"
*
         CVT   DSECT=YES                FOR TSO VECTOR TABLE ADDRESS
*
         END
