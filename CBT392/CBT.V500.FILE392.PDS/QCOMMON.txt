         MACRO
         QCOMMON &CSECT=NO
.*--------------------------------------------------------------------*
.*                                                                    *
.*  QCOMMON - Map the QUEUE command common data area                  *
.*                                                                    *
.*  Note - On subsequent invocations of this macro in the same        *
.*.        assembly, only the DSECT (or CSECT) statement will be      *
.*         generated.                                                 *
.*                                                                    *
.*  Updates:                                                          *
.*     20Dec91  LDW  Add QM2RSCA                                      *
.*     22Sep91  LDW  Add QSC3EDIT                                     *
.*     05Aug91  LDW  Add QTBDROW per Guy Albertelli 16Jul91 update    *
.*     29Jun91  LDW  Add trace (debug) logic to QCALL, QSTOP, QTILT   *
.*     08Jun91  LDW  Misc minor cleanup; eliminate dup patch space    *
.*     06Jun91  EMS  For 420:  Add QCJCTNJH, QLJCTNJH, QCJCTNJN,      *
.*                      QJ2VERSN                                      *
.*     26May91  LDW  Add QBRIFHDR                                     *
.*     11Feb91  LDW  Move some ISPF areas to literals and local work  *
.*                      areas in INITIAL, DISPLAY, LISTDS, and QUEUE  *
.*                      (V2 VLEN2 V3 VLEN3 V4 VLEN4 V5 VLEN5 V6 VLEN6 *
.*                       V7 VLEN7 V9 VLEN9 V10 VLEN10 BRWCLST BRWDBLW *
.*                       MYFREE TBLST VCALLST)                        *
.*                   Delete unused data (V1, VLEN1)                   *
.*     01Feb91  LDW  Change table panel name from QNM2 to QPTBL       *
.*     30Jan91  LDW  Don't generate ENTRY QLEVEL if dsect!            *
.*     29Jan91  LDW  Shuffle QCJQELEN and QCJIXL in a feeble attempt  *
.*                      to regain 2 bytes of addressability           *
.*                   Add QVPRNTWA; move many QPxxx fields into new    *
.*                      QPRNTWA to regain more addressability         *
.*                   Move QCLRSCR subroutine to CLEARSCR in TSOCMD    *
.*                   Retrofit GLA changes below:                      *
.*    (15Jan91) GLA  Define flag to output OUTGRP instead of device   *
.*    (21Sep90) GLA  Move things for addressability sake              *
.*    (02Aug90) GLA  Fix loop in LISTDS after job purged and space    *
.*                   reused - day one bug                             *
.*                   Add field for saving addr of SVT/CCT             *
.*    (10Jul90) GLA  Add items for JES2 Appl CKPT support             *
.*                   Fix definition of QPJOBID and QPDSID             *
.*    (17Apr90) GLA  Set correct length of ZCMD area                  *
.*    (15Nov89) GLA  Change attributes of HASPPRNT and field of       *
.*                      QPHEAD1                                       *
.*                   Correct NUMCONV for length < 0                   *
.*    (05May89) GLA  Add support of authorized under ISPF             *
.*    (29Mar89) GLA  Add support for ISPF BROWSE and Tables           *
.*     22Dec90  LDW  Add field QLEVEL                                 *
.*     28Nov90  EMS  Enlarge fields for dsn prefix                    *
.*     20Jun90  LDW  Use new QLCB macro for LCB                       *
.*     16Jun90  LDW  Add QAMODE24 and QAMODE31 subroutines            *
.*                   Remove $AMODE from QTILT and QSTOP since we      *
.*                      now run (mostly) AMODE 31                     *
.*     13Jun90  LDW  Add unstack code (like QSTOP) into QTILT to fix  *
.*                      S0C4 problem after repeated immediate cmds    *
.*                   Minor adjustment to $AMODE invocation in QSTOP   *
.*     23May90  LDW  Add QSC3USER                                     *
.*     09Jul89  LDW  Add QSC1PCT, change QSC1FLSH to QSC3FLSH         *
.*     04Jun89  LDW  Add QXSUPR, QXAPFENT                             *
.*     25Apr89  EMS  Add QKACA -> incore checkpoint copy              *
.*                   Updates for SP311                                *
.*                   QSTOP and friends reset to 24 bit mode           *
.*     23Jan89  LDW  Set default PFKeys from QGLOBALS for EMS         *
.*     10Apr88  LDW  Add QFFPREV                                      *
.*     06Apr88  LDW  Swap PF2/14 (RC) and PF12/24 (ST) default        *
.*                      definitions for ISPF 2.3 compatibility        *
.*     20Feb88  LDW  Add QSLFLDS and Q3SYSLOG                         *
.*     03Feb88  LDW  For 2.2.0, don't generate QCTGMLEN               *
.*     11Aug87  EMS  Update for page mode with getrec routine         *
.*                   Add more IKJDAP08 equates                        *
.*      8Jul87  EMS  Fix for Starting/Draining spool volumes          *
.*                   Add QCITEM for Qitem support                     *
.*                   Delete QSORT fields, QCEND                       *
.*                   Fix for base JES2                                *
.*                   Update for GETPDDB routine                       *
.*     22May87  EMS  Add QSORT req fields                             *
.*                   Add fields to support Page mode; col GETREC flds *
.*                   Rename fields QPPAGE;QPPAGE# -> QPAGEN;QPAGEN#   *
.*                   Add QITEM entry point for enhanced QITEM macro   *
.*     31Mar87  LDW  Add Q2VALIDS                                     *
.*     07Feb87  LDW  Add QCMDNUM, QRETNUM, QCMDRING                   *
.*     04Jan87  LDW  FOR 136:  ADD QCJQELEN                           *
.*     14DEC86  LDW  FOR 136:  ADD QCNUMTGA, QCJQEXA, QCDASL, QCDASA, *
.*                      QCPSTL, QCPSTA                                *
.*     29NOV86  LDW  ADD QM1JDATE                                     *
.*     05OCT86  LDW  ADD QSC3WTRI                                     *
.*     23AUG86  LDW  CHANGE QCTGML GEN CHECK FROM SP133 TO SP130      *
.*     17JUN86  LDW  ADD QCJQETTR, QCJQENUM, QCJOETTR, QCJOENUM,      *
.*                      QMFMVSE                                       *
.*     03JUN86  LDW  ADD QVCKDECB AND QLCKDECB                        *
.*     04/22/86 LDW  MOVE ATTN FROM QTERMFLG TO QMISCFLG              *
.*     04/03/86 LDW  ASSIGN INDIVIDUAL NAMES TO EACH BIT IN QDEBUG    *
.*     03/31/86 LDW  ADD QTFSW2, QTFSW5, CHANGE QTFLARGE TO QTFWIDE   *
.*                   ADD QDCOLNOW, DELETE QSTAX                       *
.*     12/04/84 LDW  ADD QSC2DI, QSC2AFF                              *
.*     08/30/84 LDW  ADD QSC2DBL, QM1STACK, QDLINES, QFREEMFL         *
.*                   SUPPORT QM1STACK                                 *
.*                   ADD DESCRIPTOR WORDS FOR GETMAINED STORAGE       *
.*     08/07/84 LDW  ADD QALTCHAR                                     *
.*                   UPDATE FOR SP134 (DON'T GENERATE QCPDDB1)        *
.*     06/05/84 LDW  ADD QCTGML & QCTGMA FOR SP133                    *
.*     01/27/84 LDW  CHANGE PF6/PF18 FROM 'NS' TO 'NS *'              *
.*                   SET DEFAULT QCNSDIR TO 'N'                       *
.*     01/23/84 LDW  FIX BUG IN QSTACK ROUTINE                        *
.*                   ADD QFLAG4, QFFLAG, QHDCBA, QHDECBA              *
.*                   ADD MORE SPARE SPACE IN VARIOUS PLACES           *
.*                   EXPAND SCRN BUFF TO ALLOW FOR ATTR BYTES / LINE  *
.*                   CHANGE SIZE OF BLOCK TABLE FROM 64K TO 32K       *
.*                   ADD POINTERS TO DSID TABLE FOR 'NS' SUBCOMMAND   *
.*                   EXPAND QDHJOBID TO ENABLE FIX FOR JESNEWS ID     *
.*                   CHANGE DEFINITION OF PF6/18 FROM 'DI' TO 'NS'    *
.*                   PUT 'EXIT' AT BOTTOM OF QDSTACK                  *
.*     12/19/83 LDW  ADD QCTGMLEN, QSC1SPC                            *
.*     10/28/83 SDM  FIX QCLRSCR FOR LARGER SCREENS                   *
.*     03/18/83 LDW  PUT IN QSTACK SUBROUTINE                         *
.*     03/16/83 LDW  CHANGE PF3/15 DEFINITION FROM "*" TO "RC"        *
.*     03/11/83 LDW  CHANGE LCBUFF DSECT                              *
.*                   ADD QSC2LINE, QM1PFK24, QERRMSG, QACTION,        *
.*                      DEFINITIONS FOR PFK13-24                      *
.*                   NEW QTILT ROUTINE:  MOVE MSG TEXT FROM QERRMSG   *
.*     02/23/83 LDW  ADD QVHELP, QVCHGS, LCTABLE, LCBUFF              *
.*     02/08/83 LDW  DELETE QSC1DHX                                   *
.*     02/03/83 LDW  DELETE Q1INQ1, Q1INQ2, QINQLEN, DELETE QDINQCMD, *
.*                      ADD QINQCMD                                   *
.*     01/25/83 LDW  ADD QCLSNAME                                     *
.*     01/19/83 LDW  ADD QSMFID, QVSYSID, QDLCTBL, QVLCBUF, QDLCBUF,  *
.*                      QM2NTRAN, QDEBUG (REPLACES QM1PDBFL),         *
.*                      QTPAGEX, QTHALFX, Q2COLIND                    *
.*                   DELETE SOME OTHER (NO LONGER USED) FIELDS        *
.*                   INCREASE SIZE OF SCREEN BUFFER TO ALLOW FOR      *
.*                      EXTRA SPACE TAKEN BY ATTR BYTES               *
.*     01/14/83 LDW  ADD QTSOCMD, QMVSCMD                             *
.*     01/13/83 LDW  ADD QXAPFSET                                     *
.*     01/10/83 LDW  ADD Q2SHORT, Q2NEWSCR, QDENDPTR                  *
.*     01/03/83 LDW  ADD QSTDEFKY, Q2OWNJOB, Q2LISTDS, Q2REPOS,       *
.*                      Q2SPF, QSC2CKPT, QTFLARGE, QTFFULL, QM1NOVFY, *
.*                      QPFORM                                        *
.*                   QSTOP - DON'T RESTORE R15, R0, R1                *
.*                   QCALL - USE LOCAL WORK AREA SIZE                 *
.*                   MORE SP SUPPORT                                  *
.*                   CHANGE QCLRFLGS TO LITERALS                      *
.*                   INCREASE LENGTH OF QBLANK FOR ATTR BYTES         *
.*     12/23/82 LDW  ADD QPUCS, QPFCB, QPLINEPP, QLWASIZE, QHEXTAB,   *
.*                      QSC2SLOG, QFLAG3, QSCFLAG4, QMODE2            *
.*                   LENGTHEN QDMSG FOR POSSIBLE 7 ATTR BYTES         *
.*     12/21/82 LDW  ADD QSC2ALNA                                     *
.*     12/10/82 LDW  FIX REPROMPT BUG IN QTILT - DON'T RESTORE        *
.*                      FIELDS FROM QSAVE1 AND QSAVE2                 *
.*     12/08/82 LDW  DON'T CLEAR SCREEN BUFFER WHEN QTILT'ING         *
.*                   ADD QFLAG2, QSCFLAG2, QSCFLAG3, QKEYWORD         *
.*                   CHANGE QFLAGS TO QFLAG1, QFXXXXX TO Q1XXXXX      *
.*                   ADD QPOFFDEF                                     *
.*                                                                    *
.*--------------------------------------------------------------------*
.*
.*  SUPPRESS COMMENTS AROUND GLOBALS
         PRINT NOGEN
.*  GET ALL GLOBAL VARIABLE DEFINITIONS AND SETTINGS
         COPY  QGLOBALS
         AIF   ('&@@QDOC &CSECT' EQ 'NOGEN NO').NOGEN1
         PRINT GEN
.NOGEN1   ANOP
         GBLB  &QCOMMON                 QCOMMON WAS GENNED THIS ASM
         GBLC  &QDTYPE                  CSECT OR DSECT FOR QCOMMON
         GBLC  &QSECT                   CURRENT CSECT NAME
         AIF   ('&QSECT' NE '').QSECTOK SKIP IF ALREADY SET
&QSECT   SETC  '&SYSECT'                ELSE SET IT
         AIF   ('&QSECT' NE '').QSECTOK SKIP IF ALREADY SET
         AIF   ('&CSECT' NE 'YES').QSECTOK SKIP IF NOT GENERATING CSECT
&QSECT   SETC  'QCOMMON'                TRY TO AVOID ZERO LENGTH PC
         AIF   ('&@@QPRNT' EQ 'NO').QSECTOK
         COPY  $UPDATES
.QSECTOK AIF   (&QCOMMON).SKIP          SKIP BOX IF ALREADY GENERATED
         TITLE '--- QUEUE--COMMON AREA DSECT ---'
***********************************************************************
*                                                                     *
*   QUEUE COMMAND - COMMON AREA                                       *
*                                                                     *
***********************************************************************
.SKIP    AIF   ('&CSECT' EQ 'YES').CSECT
         AIF   ('&QDTYPE' EQ 'CSECT').CSECT   RESUME CSECT IF WAS CSECT
QCOMMON  DSECT
&QDTYPE  SETC  'DSECT'
         AGO   .CONT
.CSECT   ANOP
QCOMMON  CSECT
&QDTYPE  SETC  'CSECT'
.CONT    AIF   (&QCOMMON).MEND          SKIP IF ALREADY GENERATED
&QCOMMON SETB  1                        REMEMBER ALREADY GENERATED
         SPACE 2
***********************************************************************
*                                                                     *
*   QCALL SUBROUTINE - INVOKED BY "QCALL" MACRO VIA "BALR R14,R11"    *
*                                                                     *
*   NOTE:  THIS IS THE MIKE STEIN GIMMICK OF HAVING EXECUTABLE CODE   *
*        AT THE BEGINNING OF THE COMMON AREA, TO BE ABLE TO DO THE    *
*        CALL WITH A BALR.  NOTE ALSO, TO CHANGE THE CALLING          *
*        SEQUENCE, ALL MODIFICATIONS ARE NOW LOCALIZED TO THIS CODE   *
*        AND "QSTOP" ROUTINE FURTHER DOWN IN QCOMMON.                 *
*                                                                     *
***********************************************************************
         SPACE 1
         PUSH  USING
         USING QCOMMON,R11
         SPACE 1
         STM   R14,R12,12(R13)          SAVE CALLER'S REGISTERS
         LR    R12,R15                  SET CALLED ROUTINE'S BASE
         LR    R15,R13                  SAVE ADDR OF SAVE AREA
         AH    R13,2(,R13)              -> NEXT SAVE AREA (INCLUDES A
*                                       VARIABLE SIZE LOCAL WORK AREA)
         ST    R13,8(,R15)              CHAIN FORWARD
         ST    R15,4(,R13)              CHAIN BACKWARD
         TM    QDEBUG4,QDEBUG48         want trace?
         BZR   R12                      no - pass control to called rtn
         LR    R14,R11                  copy qcommon address
         AH    R14,=Y(QTRACEWA-QCOMMON) -> trace workarea
         STM   R15,R3,0(R14)            save some registers
         LR    R3,R14                   move trace work area address
         MVC   20(17,R3),=C'trace... QCALL:  '
         BAL   R2,QTRACE                set up caller & callee
         MVC   20+25(4,R3),=C' -> '     show direction
         TPUTX 20(R3),37                show caller -> callee
         LM    R15,R3,0(R3)             restore clobbered registers
         L     R15,4(,R13)              -> previous save area
         L     R14,12(,R15)             restore clobbered register
         BR    R12                      PASS CONTROL TO CALLED ROUTINE
         SPACE 3
QCMN#ID  DC    C'****QCOMMON &SYSDATE &SYSTIME'  GENERATE ID FIELD
         SPACE 1
         QREGS                          GENERATE REGISTERS
         SPACE 3
***********************************************************************
*                                                                     *
*   ADD DISPLAY LINE IN "QDMSG" TO SCREEN BUFFER                      *
*                                                                     *
***********************************************************************
*QADDLINE QCALL DISPADD
QADDLINE QCALL DISPADD
         ORG   *-2                      BACK OVER THE BALR
         BR    R11                      INVOKE "QCALL" WITH R14 INTACT
         SPACE 3
***********************************************************************
*                                                                     *
*   DISPLAY ERROR MESSAGE AND TERMINATE CURRENT ROUTINE               *
*                                                                     *
***********************************************************************
         SPACE 1
QTILT    TM    QDEBUG4,QDEBUG48         want trace?
         BZ    QTILTNTR                 no - skip
         TPUTX 'trace... QTILT'
QTILTNTR TM    QISFLAG1,QIS1ISPF        are we in ISPF?
         BO    *+10    >============+   yes - don't overlay hdr
         MVC   QDHLINE,QERRMSG      |   COPY THE ERROR MESSAGE
         TM    QFLAG1,Q1IMMED   <===+   ARE WE TILTING IN IMMED CMD?
         BNO   QTILT$NI                 NO - CONTINUE
*                                       YES - "IMMED" WILL BE OVERLAYED
         TM    QSCFLAG1,QSC1PMPT        SET CC FOR "REPROMPT REQUESTED"
         L     R8,QVSAVE                -> SAVE AREA FOR "DISPLAY"
         MVC   QSAVE1(QSAVE1L),0(R8)    RESTORE FIRST PART
         MVC   QSAVE2(QSAVE2L),QSAVE1L(R8)     SECOND PART
         BNO   *+8                      SKIP IF NO REPROMPT
         OI    QSCFLAG1,QSC1PMPT        SET FLAG AGAIN
QTILT$NI TM    QISFLAG1,QIS1ISPF        are we in ISPF?
         BO    *+8     >=============+  yes - don't overlay hdr
         OI    QFLAG1,Q1MVHDR        |  REQUEST "MOVE HEADER"
         TM    QXAUTH,QXAPFSET   <===+  are we authorized?
         BZ    QTILTLP                  no - so all ok
         TM    QISFLAG1,QIS1ISPF        are we auth and in ispf
         BZ    QTILTLP                  no - so all ok
         CLC   QERRMSG+39(2),=C'AP'     APF logic error
         BNE   *+8                      no - ok
         EX    0,*                      GOD HELP US
         LA    R1,8                     req auth set off
         QCALL AUTHFUNC                 reset apf auth
         SPACE 1
QTILTLP  TM    QDEBUG4,QDEBUG48         want trace?
         BZ    QTILTLP1                 no - skip
         TPUTX 'trace... QTILTLP'
*QTILTLP1 QCALL DISPLAY                 DISPLAY THE ERROR MESSAGE
QTILTLP1 QCALL DISPLAY                  DISPLAY THE ERROR MESSAGE
         B     QTILTLP                  KEEP HIM HERE
         SPACE 3
***********************************************************************
*                                                                     *
*   HANDLE ENTRY FROM ISRAUTH                                         *
*                                                                     *
***********************************************************************
QAUTHZ   DS    0H
         L     R14,6(,R2)               get entry address
         BR    R14                      go there
         SPACE 3
***********************************************************************
*                                                                     *
*   Mode switch subroutines                                           *
*                                                                     *
***********************************************************************
          AIF   (&@@SPLVL GE 200).QAMODE1
QAMODE24 BR    R14                      just return
QAMODE31 EQU   QAMODE24                 just return
          AGO   .QAMODE2
.QAMODE1  ANOP
QAMODE24 N     R14,=X'00FFFFFF'         strip BAL junk in case A=24 now
         BSM   0,R14                    switch to AMODE 24 and return
         SPACE 1
QAMODE31 N     R14,=X'00FFFFFF'         strip BAL junk
         O     R14,=X'80000000'         make it AMODE 31
         BSM   0,R14                    switch to AMODE 31 and return
.QAMODE2  ANOP
         SPACE 3
***********************************************************************
*                                                                     *
*   NUMERIC CONVERT ROUTINE                                           *
*                                                                     *
***********************************************************************
QNUMCONV LTR   R1,R1                    check length
         BMR   R14                      bad - return w/cc = "ne"
         MVC   QFZONES,QFZONE           INIT FIELD
         EX    R1,QMVZ                  COPY THE ZONES
         CLC   QFZONES,QFZONE           ALL NUMBERS?
         BNER  R14                      NO - RETURN WITH CC SET TO "NE"
         EX    R1,QPACK                 PACK IT
         CVB   R15,QNUMWORK             GET IT IN BINARY
         BR    R14                      RETURN WITH CC SET TO "EQ"
         SPACE 1
QMVZ     MVZ   QFZONES(*-*),0(R15)      << EXECUTED >>
QPACK    PACK  QNUMWORK,0(*-*,R15)      << EXECUTED >>
         SPACE 3
***********************************************************************
*                                                                     *
*   COMMON EXIT CODE (REPLACES "QSTOP" MACRO)                         *
*                                                                     *
***********************************************************************
QSTOP00  SR    R15,R15                  SET RETURN CODE ZERO
QSTOP    EQU   QSTOP00,2,C'I'           DEFAULT RC IS ZERO
QSTOPRC  TM    QDEBUG4,QDEBUG48         want trace?
         BZ    QSTOPNTR                 no - skip
         LR    R14,R11                  copy qcommon address
         AH    R14,=Y(QTRACEWA-QCOMMON) -> trace workarea
         STM   R15,R3,0(R14)            save some registers
         LR    R3,R14                   move trace work area address
         MVC   20(17,R3),=C'trace... QSTOP:  '
         BAL   R2,QTRACE                set up caller & callee
         MVC   20+25(4,R3),=C' <- '     show direction
         MVC   20+37(5,R3),=C'  R1='
         HEX   (20+42,R3),(8,R3),LEN=4,HEXTAB=QHEXTAB  R1 in QTRACEWA
         MVC   20+51(5,R3),=C' R15='
         HEX   (20+56,R3),(0,R3),LEN=4  R15 saved in QTRACEWA
         TPUTX 20(R3),64
         LM    R15,R3,0(R3)             restore clobbered registers
QSTOPNTR L     R13,4(,R13)              LOAD NEXT LOWER SAVE AREA SET
***      RETURN  (R2,R13),T
         L     R14,12(,R13)             STANDARD REGISTER RESTORE
         LM    R2,R12,12+4*4(R13)       STANDARD REGISTER RESTORE
         MVI   12(R13),X'FF'            FLAG SAVE AREA INACTIVE
         BR    R14                      RETURN TO CALLER
         SPACE 3
***********************************************************************
*                                                                     *
*   Subroutine to extract caller & callee.  Called by QCALL & QSTOP.  *
*                                                                     *
***********************************************************************
QTRACE   L     R1,4(,R13)               -> previous save area
         L     R1,12+2*4+R12*4(,R1)     caller's R12
         LA    R1,5(,R1)                -> csect name
         LA    R15,20+18(,R3)           -> target
         MVC   0(8,R15),QBLANK          clear target
         BALR  R14,0                    set loop address
         MVC   0(1,R15),0(R1)           move one char of csect name
         LA    R1,1(,R1)                bump source
         LA    R15,1(,R15)              bump target
         CLI   0(R1),C'A'               end of csect name?
         BNLR  R14                      no - loop
         LA    R1,5(,R12)               -> csect name
         LA    R15,20+29(,R3)           -> target
         MVC   0(8,R15),QBLANK          clear target
         BALR  R14,0                    set loop address
         MVC   0(1,R15),0(R1)           move one char of csect name
         LA    R1,1(,R1)                bump source
         LA    R15,1(,R15)              bump target
         CLI   0(R1),C'A'               end of csect name?
         BNLR  R14                      no - loop
         BR    R2                       return to QCALL or QSTOP
         SPACE 3
***********************************************************************
*                                                                     *
*   SUBROUTINE TO ADD CURRENT SUBCOMMAND TO STACK FOR 'END' FUNCTION  *
*                                                                     *
***********************************************************************
QSTACK   TM    QMODE1,QM1STACK          SUPPORTED?
         BNOR  R14                      NO - JUST RETURN
         STM   R14,R12,12(R13)          SAVE SOME REGISTERS
         L     R2,QVSTACK               -> BOTTOM OF STACK
         L     R3,QVSTACKC              -> TOP OF STACK
         LTR   R0,R0                    NEW LEVEL = 0?
         BZ    QSTACK$2                 YES - PUT AT BOTTOM OF STACK
         SPACE 1
QSTACK$1 CLM   R0,B'0001',0(R2)         COMPARE NEW LEVEL WITH ELEMENT
         BNH   QSTACK$2                 LEVEL:  NEW<=THIS, ADD HERE
         LA    R2,QDSTACKL(,R2)         -> NEXT STACK ELEMENT
         CLR   R2,R3                    PAST TOP (END) OF STACK?
         BNH   QSTACK$1                 NO - KEEP LOOKING
         SPACE 1
QSTACK$2 CLC   0(QDREPLYL,R1),QBLANK    IS NEW BLANK?
         BE    QSTACK$3                 Yes -  ignore
         STC   R0,0(,R2)                SAVE NEW LEVEL
         MVC   2(QDREPLYL,R2),0(R1)     SAVE NEW SUBCOMMAND
         ST    R2,QVSTACKC              SAVE POINTER TO BOTTOM OF STACK
*
QSTACK$3 LM    R14,R12,12(R13)          RESTORE REGISTERS
         BR    R14                      RETURN TO CALLER
         SPACE 3
***********************************************************************
*                                                                     *
*   Subroutine to call QITEM driver routine                           *
*                                                                     *
***********************************************************************
*QITEM   QCALL QSUBS                    -> routine addr
QITEM    QCALL QSUBS                    -> routine addr
         ORG   *-2
         BR    R11                      return to routine addr
         SPACE 3
*  GENERAL PATCH AREA - ADDRESSABLE FROM ALL ROUTINES
QPATCH   DC    100S(*)                  TEMP PATCH SPACE
         SPACE 3
***********************************************************************
*                                                                     *
*   MISCELLANEOUS NUTS AND BOLTS                                      *
*                                                                     *
***********************************************************************
QHEXTAB  EQU   *-C'0'
         DC    C'0123456789ABCDEF'
QFRSTSA  DC    A(0)                     ADDRESS OF FIRST SAVE AREA
         SPACE 2
QFREEMFL FREEMAIN  LU,SP=1,A=QADDRS-QADDRS,LA=QLENGTHS-QLENGTHS,MF=L
         SPACE 2
QADDRS   DC    0A(0)                    ADDRESSES OF GETMAINED STORAGE
QCSTART  DC    A(0)                     ADDRESS OF TABLE START
QDSTART  DC    A(0)                     -> Ddname table
QCJQTL   DC    A(0)                     ADDRESS OF FIRST CKPT RECORD
QVSCRBUF DC    A(0)                     ADDRESS OF SCREEN BUFFER
QVSPDCB  DC    A(0)                     ADDRESS OF SPOOL DCBS AREA
QVCKDECB DC    A(0)                     ADDRESS OF CHKPT DECBS AREA
          AIF   (&QLEVEL LT 12).QCNJCTE
QCJCTNJH DC    A(0)                     addr of NJE sections for SP420
.QCNJCTE  ANOP  ,
         DC    3A(0)                    SPARE
         SPACE 1
QLENGTHS DC    0F'0'                    LENGTHS OF GETMAINED STORAGE
QCSIZE   DC    A(32*1024)               CURRENT SIZE OF TABLE
QDSIZE   DC    A(8*1024)                CURRENT SIZE OF TABLE
QCKPTLEN DC    A(0)                     LENGTH OF CHKPT BUFFER
QLSCRBUF DC    A(0)                     LENGTH OF SCREEN BUFFER
QLSPDCB  DC    A(0)                     LENGTH OF SPOOL DCBS AREA
QLCKDECB DC    A(0)                     LENGTH OF CHKPT DECBS AREA
          AIF   (&QLEVEL LT 12).QCNJCTL
QLJCTNJH DC    A(0)                     len of NJE sections for SP420
.QCNJCTL  ANOP  ,
QLENLAST EQU   *-4                      ADDRESS OF LAST LENGTH WORD
         DC    3A(0)                    SPARE
         SPACE 3
QHDCBA   DC    A(0)                     -> QHELP DCB
QHDECBA  DC    A(0)                     -> QHELP DECB
QDEBUG   DC    0XL4'00'                 MISC USE DEBUGGING FLAGS
QDEBUG1  DC    X'00'                    ...
QDEBUG11  EQU   X'80'
QDEBUG12  EQU   X'40'
QDEBUG13  EQU   X'20'
QDEBUG14  EQU   X'10'
QDEBUG15  EQU   X'08'
QDEBUG16  EQU   X'04'
QDEBUG17  EQU   X'02'
QDEBUG18  EQU   X'01'
QDEBUG2  DC    X'00'                       ...
QDEBUG22  EQU   X'40'
QDEBUG23  EQU   X'20'
QDEBUG24  EQU   X'10'
QDEBUG25  EQU   X'08'
QDEBUG26  EQU   X'04'
QDEBUG27  EQU   X'02'
QDEBUG28  EQU   X'01'
QDEBUG3  DC    X'00'                          ...
QDEBUG32  EQU   X'40'
QDEBUG33  EQU   X'20'
QDEBUG34  EQU   X'10'
QDEBUG35  EQU   X'08'
QDEBUG36  EQU   X'04'
QDEBUG37  EQU   X'02'
QDEBUG38  EQU   X'01'
QDEBUG4  DC    X'00'                             ...
QDEBUG42  EQU   X'40'
QDEBUG43  EQU   X'20'
QDEBUG44  EQU   X'10'
QDEBUG45  EQU   X'08'
QDEBUG46  EQU   X'04'
QDEBUG47  EQU   X'02'
QDEBUG48  EQU   X'01'
QDENDPTR DC    A(0)                     -> PAST IC AT END OF SCREEN
QVSYSID  DC    A(QSYSID)                -> SYSTEM ID TABLE IN USE
QVHELP   DC    A(*-*)                   -> HELP TEXT MODULE, IF LOADED
QVCHGS   DC    A(*-*)                   -> CHGS TEXT MODULE, IF LOADED
QVSORT   DC    A(*-*)                   -> sort routine
          AIF   (&QLEVEL LT 8).NOPC
*
*        Areas needed to define PC routines
*
          AIF   (&QLEVEL GE 9).NEWETD
QPCDESC  DC    0F'0',XL(ETDLEN+ETDELEN)'00'
          AGO   .ETDDONE
.NEWETD   ANOP
*QPCDESC ETDEF TYPE=INITIAL
QPCDESC  ETDEF TYPE=INITIAL
*QPCETD1 ETDEF TYPE=ENTRY,ROUTINE=0,SSWITCH=NO,                        $
               STATE=SUPERVISOR,AKM=(0:15),EKM=(0:15),EK=0,            $
               PKM=OR
QPCETD1  ETDEF TYPE=ENTRY,ROUTINE=0,SSWITCH=NO,                        $
               STATE=SUPERVISOR,AKM=(0:15),EKM=(0:15),EK=0,            $
               PKM=OR
*        ETDEF TYPE=FINAL
         ETDEF TYPE=FINAL
.ETDDONE  ANOP
QPCLXL   DC    0F'0'
QPCLXCNT DC    F'0'                     NUMBER OF LXS REQUESTED
QPCLXVAL DC    F'0'                     LX RETURNED BY LXRES
*
QPCTKL   DC    0F'0'                    TOKEN LIST
QPCTKCNT DC    F'0'                     NUMBER OF ETS CREATED
QPCTKVAL DC    F'0'                     TOKEN RETURNED BY ETCRE
QPCCKPT  DC    A(0)                     PC NUMBER - CKPT
.NOPC     ANOP
         DC    3F'0'                    EXPANSION SPACE
          AIF  (&QLEVEL GE 4).QCSPNOD   DIFFERENT FIELDS IF SP
QCLNODE  DS    0HL1                     LOCAL SYSTEM NODE NUMBER
QCLOCAL  DC    XL2'00'                  "LOCAL" OUTPUT DESTINATION ID
          AGO  .QCOKNOD
.QCSPNOD  ANOP
QCLNODE  DS    0H                       LOCAL SYSTEM NODE NUMBER
QCLOCAL  DC    XL4'00'                  "LOCAL" OUTPUT DESTINATION ID
.QCOKNOD  ANOP
*  THE FOLLOWING FIELD IS FILLED IN BY A BACK ORG IN QDCB.
QGETMAIN DC    A(0)                     LENGTH OF QCOMMON FOR GETMAIN
QJ2VERSN DC    CL8'&QJ2VERS'            JES2 version (SP N.N.N)
*  THE FOLLOWING 6 FIELDS ARE DATA SPECIFIC TO THE JES SYSTEM WE ARE
*      CURRENTLY LOOKING AT
QJESNAME DC    CL4'JES2'                NAME OF JES SUBSYSTEM
QSSCT    DC    A(0)                     -> SSCT
          AIF  (&QLEVEL GE 9).NOSVT
QSVT     DC    A(0)                     -> SSVT
          AGO   .NOCCT
.NOSVT    ANOP
QCCT     DC    A(0)                     -> CCT
.NOCCT    ANOP
QCKPTVOL DC    CL6' '                   CHECKPOINT VOLSER
QCKPTPFX DC    CL44' '                  PREFIX FOR HASPCKPT & HASPACE
QCOMCHAR DC    X'EE'                    COMMUNICATIONS CHARACTER ("$")
QALTCHAR DC    X'EE'                    ALTERNATE COMM CHAR, IF ANY
         SPACE 1
QLOGON   DC    CL7' '                   LOGON ID (PSCBUSER)
QLOGONL  DC    AL1(0)                   LENGTH OF LOGON ID
QDEST    DC    CL8' '                   DEFAULT LOGON PRINT ROUTE
QXBTRAK  DC    XL4'00'                  LAST MTTR ENTERED FOR "XB"
*---  QTPAGE AND QTHALF ARE USED IF THE COLUMN INDICATOR IS OFF    ---*
QTPAGE   DC    P'21'                    # OF USABLE LINES ON SCREEN
QTHALF   DC    P'10'                    SAME FOR HALF A SCREEN
*---  QTPAGEX AND QTHALFX ARE USED IF THE COLUMN INDICATOR IS ON   ---*
QTPAGEX  DC    P'20'                    # OF USABLE LINES ON SCREEN
QTHALFX  DC    P'10'                    SAME FOR HALF A SCREEN
QCLASSH  DC    0H'0',X'00'              HALFWORD BOUNDARY
QCLASS   DC    C' '                     SEARCH CLASS
QCLSNAME DC    CL2' '                   DISPLAYABLE CLASS
         DC    XL2'00'                  CURRENTLY WASTED FOR ALIGNMENT
QJOBID   DC    H'0',CL8' '              LAST JOBID (AND LENGTH)
QINTJBID DC    F'0'                     JES2 INTERNAL JOB NUMBER
QEXTJBID DC    CL8' '                   JES2 EXTERNAL JOB NUMBER
QJOBNAME DC    CL8' '                   JOB NAME
          AIF   (&QLEVEL LT 5).QCNPDB1
QSTEPNAM DC    CL8' '                   stepname
QDDNAME  DC    CL8' '                   ddname
.QCNPDB1  ANOP
QSTKEY   DC    CL8' '                   SEARCH KEY FOR "ST" SUBCMD
QSTDEFKY DC    CL8' '                   DEFAULT FOR "ST *"
         DC    XL40'00'                 SPARE
         SPACE 1
QXAUTH   DC    X'00'
QXPSWD    EQU   X'01'                   HE ENTERED THE PASSWORD
QXOPER    EQU   X'02'                   HE HAS THE OPER BIT
QXSYSP    EQU   X'04'                   SYSTEMS PROGRAMMER
QXAUTHX   EQU   QXPSWD+QXOPER+QXSYSP    AT LEAST ONE OF THEM
QXSUPR    EQU   X'10'                   QUEUE entered SUPR (and key 0)
QXAPFFL   EQU   X'20'                   Attempt to set APF fail
QXAPFENT  EQU   X'40'                   QUEUE entered with APF auth
QXAPFSET  EQU   X'80'                   WE SET APF AUTH ON VIA SVC
         SPACE 1
QMISCFLG DC    B'00000000'              MISC FLAGS
QMFSYNAD  EQU   X'80'                    SYNAD FLAG
QMFATTN   EQU   X'40'                    ATTN PRESSED
QMFMVSE   EQU   X'20'                    RUNNING MVS/XA
QMFX22    EQU   X'10'                    Running MVS/XA 2.2 or higher
QMFAXS    EQU   X'08'                    AXSET was issued
QMFIS31   EQU   X'04'                    ISPF running above line
QMFRMAX   EQU   X'02'                    Reset max indicator
*=====================================================================*
QSAVE1   EQU   *                        START FIELDS SAVED IN DISPLAY |
*  KEEP FIELDS QSAVE1 THRU QSAVE1L TOGETHER FOR MVC IN "DISPLAY"      |
QCODEH   DC    H'0'                     HALFWORD BOUNDARY             |
QCODE    EQU   QCODEH+1,1,C'X'          SEARCH FUNCTION CODE          |
QSUBCMD  DC    V(SEARCH)                ADDR OF CURRENT SUBCOMMAND    |
QCITEMS  DC    A(0)                     -> this command's Qitem table |
QOFF0    DC    H'0'                     OFFSET TO SUBCOMMAND          |
         DC    H'0'                     LENGTH OF SUBCOMMAND          |
QSUBNAME DC    CL8' '                   NAME OF SUBCOMMAND            |
QOFF1    DC    H'0'                     OFFSET TO QPARM1              |
QLNG1    DC    H'0'                     LENGTH OF QPARM1              |
QPARM1   DC    CL8' '                   USER SUPPLIED PARAMETER #1    |
QOFF2    DC    H'0'                                                   |
QLNG2    DC    H'0'                                                   |
QPARM2   DC    CL8' '                                           #2    |
QOFF3    DC    H'0'                                                   |
QLNG3    DC    H'0'                                                   |
QPARM3   DC    CL8' '                                           #3    |
QPARMEND EQU   *                                                      |
*                                                                     |
QKEYWORD DC    (2*10)CL8' '             10 KEYWORD/VALUE PAIRS        |
QKEYLAST EQU   *                                                      |
*                                                                     |
QFLAG1   DC    X'00'                    MISCELLANEOUS FLAGS           |
.*        EQU   X'80'                                                 |
.*        EQU   X'40'                                                 |
Q1VALJOB  EQU   X'20'                    FINDJOB MUST CHK ACCSS TO JOB|
Q1LSTCMD  EQU   X'10'                    SUBCMD NEEDS ONLY READ ACCESS|
Q1JOBID   EQU   X'08'                    QPARM1 IS A JOBID, Â¬ JOBNAME |
Q1MVHDR   EQU   X'04'                    DISPLAY: MOVE HDR TO SCRN BUF|
Q1IMMED   EQU   X'02'                    EXEC THIS SUBCMD IMMEDIATELY |
Q1PROFOK  EQU   X'01'                    INIT PROFILE COMPLETE        |
*                                                                     |
QFLAG2   DC    X'00'                    MISCELLANEOUS FLAGS           |
Q2OWNJOB  EQU   X'80'                    USER "OWNS" THIS JOB (AT     |
*                                        LEAST FOR ACTIVE SUBCMD)     |
Q2LISTDS  EQU   X'40'                    LISTDS IN CONTROL            |
Q2REPOS   EQU   X'20'                    SUBCMD IS REPOSITIONING TYPE |
Q2SPF     EQU   X'10'                    RUNNING UNDER SPF ENVIRONMENT|
Q2SHORT   EQU   X'08'                    WRITE ONLY TOP 3 LINES       |
Q2NEWSCR  EQU   X'04'                    PUT NEXT LINE ON LINE 1      |
Q2COLIND  EQU   X'02'                    DISPLAY COLUMN INDICATOR     |
Q2VALIDS  EQU   X'01'                    QPDSID IS VALID              |
*                                                                     |
QFLAG3   DC    X'00'                    MISCELLANEOUS FLAGS           |
Q3SYSLOG  EQU   X'80'                    SYSLOG command in use        |
*                                                                     |
QFLAG4   DC    X'00'                    MISCELLANEOUS FLAGS           |
*                                                                     |
*---  QSCFLAGS ARE CLEARED WHEN A NEW SUBCOMMAND IS PARSED            |
QSCFLAGS DS    0XL4                     HANDLE FOR ALL SUBCMD FLAGS   |
*                                                                     |
QSCFLAG1 DC    X'00'                    MISC FLAGS FOR SUBCOMMANDS    |
QSC1JOB   EQU   X'80'                    JOBID TO BE DISP'D IN QDTOP  |
QSC1CPU   EQU   X'40'                    SUBCOMMAND IS "CPU"          |
QSC1TIME  EQU   X'20'                    INPUT TIME TO BE FORMATTED   |
QSC1PGMR  EQU   X'10'                    PGMRNAME TO BE FORMATTED     |
QSC1PCT   EQU   X'08'                    SHOW SPOOL PCT, NOT LINES    |
QSC1DH    EQU   X'04'                    THIS IS "DH" SUBCOMMAND      |
QSC1SPC   EQU   X'02'                    SHOW SPOOL SPC, NOT LINES    |
QSC1PMPT  EQU   X'01'                    REPROMPT LAST COMMAND ENTERED|
*                                                                     |
QSCFLAG2 DC    X'00'                    MISC FLAGS FOR SUBCOMMANDS    |
QSC2ALNA  EQU   X'80'                    ALLOC SHOULD NOT ABEND IF ERR|
QSC2SLOG  EQU   X'40'                    CURRENT SUBCOMMAND IS "SLOG" |
QSC2CKPT  EQU   X'20'                    FORCE CKPT READ              |
QSC2DJX   EQU   X'10'                    'HEX' OPERAND OF "DJ" GIVEN  |
QSC2LINE  EQU   X'08'                    CURRENT SUBCMD IS A LINE S.C.|
QSC2DBL   EQU   X'04'                    DOUBLE LINE OUTPUT FORMAT    |
QSC2DI    EQU   X'02'                    SUBCOMMAND IS DI/AI/HI       |
QSC2AFF   EQU   X'01'                    SYSTEM AFFINITY SELECTED     |
*                                                                     |
QSCFLAG3 DC    X'00'                    MISC FLAGS FOR SUBCOMMANDS    |
QSC3WTRI  EQU   X'80'                    DISPLAY WTRID INSTEAD OF FCB |
QSC3FLSH  EQU   X'40'                    DISPLAY FLASH INSTEAD OF RMT |
QSC3USER  EQU   X'20'                    DISPLAY USERID INSTEAD OF RMT|
QSC3OUTG  EQU   X'10'                    DISPLAY OUTGRP vs DEV        |
QSC3EDIT  EQU   X'08'                    use EDIF instead of BRIF     |
*                                                                     |
QSCFLAG4 DC    X'00'                    MISC FLAGS FOR SUBCOMMANDS    |
*                                                                     |
QSAVE1L  EQU   *-QSAVE1                 LENGTH OF SAVED FIELDS        |
*=====================================================================*
         SPACE 1
QTERMFLG DC    AL1(QTFSW2)              TERMINAL ORIENTED FLAGS
QTFVTAM   EQU   X'80'                    VTAM TERMINAL
QTFTTY    EQU   X'40'                    NON-VIDEO TERMINAL IN USE
QTFMOD5   EQU   X'20'                    TERMINAL IS A MODEL 5
QTFFULL   EQU   X'10'                    CURRENTLY IN FULLSCR MODE
QTFNOW2   EQU   X'08'                    CURRENTLY IN MOD2 MODE
QTFNOW5   EQU   X'04'                    CURRENTLY IN MOD5 MODE
QTFSW2    EQU   X'02'                    SWITCH TO MOD2 MODE NEEDED
QTFSW5    EQU   X'01'                    SWITCH TO MOD5 MODE NEEDED
         SPACE 1
QMODE1   DC    AL1(QM1BEEP+QM1STACK)    VARIOUS USER-SET MODE FLAGS
QM1LCC    EQU   X'80'                    LISTDS - DISPLAY CARRIAGE CTL
QM1FANUM  EQU   X'40'                    FINDALL SHOULD DISPLAY REC #'S
QM1JDATE  EQU   X'20'                    FORMATTED DATES S/B JULIAN
QM1BEEP   EQU   X'10'                    BEEP DESIRED FOR ERROR MSGS
QM1PAGE   EQU   X'08'                    LISTDS - USE PRINT PAGE METHOD
QM1NOVFY  EQU   X'04'                    ACTION SUBCMDS: BYPASS VERIFY
QM1PFK24  EQU   X'02'                    ALLOW PF13-24 TO BE DIFFERENT
QM1STACK  EQU   X'01'                    COMMAND STACK MODE
         SPACE 1
QMODE2   DC    AL1(QM2CKPT)             VARIOUS USER-SET MODE FLAGS
QM2CKPT   EQU   X'80'                    use incore copy of ckpt
QM2RSCA   EQU   X'40'                    reissue stacked cmd after act
         SPACE 1
QUCMID   DC    AL1(0)                   ID OF CURRENT CONSOLE UCM
QUCMNAME DC    CL3'*'                   DISPLAYABLE ADDR OF CURR CONS
QDSNCKPT DC    H'13',CL44'SYS1.HASPCKPT' DSNAME FOR HASPCKPT
QDSNSPC  DC    H'12',CL44'SYS1.HASPACE'  DSNAME FOR HASPACE
QBLANK   DC    CL(132+7*2)' '           HANDY CONSTANT
         SPACE 1
QNUMWORK DC    D'0'                     CONVERT WORK AREA
QFZONE   DC    C'0'                     USED TO CLEAR QFZONES
QFZONES  DC    C'00000000'              USED FOR NUMERIC CHECK
QCOUNT   DC    PL3'0'                   NUMBER OF ELEMENTS IN QUEUE
QDELIMIT DC    C' '                     DELIMITER
         SPACE 1
QFFLAG   DC    X'00'                    FLAGS FOR FIND
QFFBOUND  EQU   X'80'                    BOUNDARY CHECK NEEDED
QFFLEFT   EQU   X'40'                    LEFT BOUNDARY NEEDED
QFFRIGHT  EQU   X'20'                    RIGHT BOUNDARY NEEDED
QFFPREV   EQU   X'08'                    FIND PREVious
QFFALL    EQU   X'04'                    FIND ALL MATCHES
QFFASIS   EQU   X'02'                    DO NOT XLATE TO UPPER CASE
QFFFOUND  EQU   X'01'                    A MATCH WAS FOUND
         SPACE 1
QSLFLDS  DC    AL1(QSLFLTIM+QSLFLJID)   Fields for SYSLOG
QSLFLIND  EQU   X'80'                    indicators
QSLFLRTC  EQU   X'40'                    route codes
QSLFLSID  EQU   X'20'                    system id
QSLFLDAT  EQU   X'10'                    date
QSLFLTIM  EQU   X'08'                    time
QSLFLJID  EQU   X'04'                    jobid
QSLFLRFL  EQU   X'02'                    request flags
*         EQU   X'01'
         SPACE 1
QISFLAG1 DC    X'00'                    ISPF flags
QIS1ISPF  EQU   X'80'                    QUEUE INVOKED VIA ISPF
QIS1V2    EQU   X'40'                    ISPF VERSION 2 RUNNING
QIS1V23   EQU   X'20'                    ISPF V2.3 RUNNING (BRIF)
QIS1V32   EQU   X'10'                    ISPF V3.2 RUNNING (BRIF works)
QIS1TBOP  EQU   X'02'                   Table open
QIS1BRCP  EQU   X'01'                   on if BROWSE complete
         SPACE 1
QKFLAG   DC    X'00'                    Application CKPT flags
QKFKAC    EQU   X'80'                    KAC access valid
         SPACE 1
         DC    XL9'00'                  SPARE
QOFFS    DC    H'0'                     START COLUMN FOR FIND
QOFFE    DC    H'0'                     END COLUMN FOR FIND
*  QPJOBID & QPDSID MUST BE CONTIG
QPJOBID  DC    XL4'0'                   JOB ID FOR LISTDS
          AIF  (&QLEVEL GE 9).DSID4
QPDSID   DC    H'0'                     DATASET ID FOR LISTDS
          AGO  .DSID2
.DSID4    ANOP ,
QPDSID   DC    XL4'0'                   dataset id for listds
.DSID2    ANOP ,
QPBFDKEY EQU   *-QPJOBID                Length of buffer key
QPOFFSET DC    H'0'                     PRINT OFFSET FROM BEG. OF REC
QPOFFDEF DC    H'0'                     DFLT PRINT OFFSET FOR THIS DSID
QPLNG    DC    PL2'0'                   LEN OF COMPARE FIELD FOR FIND
QPFIND   DC    CL64' '                  COMPARE FIELD FOR FIND
         SPACE 2
***********************************************************************
*                                                                     *
*   ISPF WORK AREAS                                                   *
*                                                                     *
***********************************************************************
TBNAM    DC    CL8'TBL1'                TABLE NAME
TBPNL    DC    CL8'QPTBL'               TABLE DISPLAY PANEL NAME
VLEN8    DC    A(LCBELEN,L'QF1,L'QF2,L'QF3,L'QF4,L'QF5,L'QF6,L'QF7)
V8       DC    C'(QLCBE QF1 QF2 QF3 QF4 QF5 QF6 QF7)'
QBRIFHDR DC    C' '                     flag for LISTDS to rebuild hdr
ISPLNK   DC    A(0)                     ADDRESS OF ISPLNK ROUTINE
QROWID   DC    F'0'                     row id value
QTBDROW  DC    F'0'                     TBDISPL row id
QMDLPT   DC    F'0'                     current table model line
QAPFR13  DC    F'0'                     save area for R13 across auth
QAPFSV   DC    7F'0'                    parm list for IKJEFTSR call
***********************************************************************
*                                                                     *
*   ISPF BROWSE INTERFACE                                             *
*                                                                     *
***********************************************************************
QBROWSE  DC    0F'0'
BRWSAVE  DC    F'0'                     ADDR OF LISTDS SAVEAREA
BRWEPARM DC    F'0'                     ADDR OF TLD FROM INPUT
BRWCBTP  DC    F'0'                     COMMON BROWSE TABLE POINTER
QVTFD    DC    A(TFD)                   Point to block
TFDMENUP DC    CL142' '                 BLANK VARs (V2ISPF LARGER)
TFDDCB   DC    (92/4)F'0'               DUMMY DCB
TFDDSN   DC    H'0',CL44' '             DUMMY DSN
BRWHDR   DC    CL72' '                  HEADER LINE FOR BROWSE
BRWMEM   DC    CL8' '                   MEMBER NAME
BRWSTAMP DC    CL5' '                   LEVEL STAMP
BRWIOB   DC    8F'0'                    DUMMY IOB
LISTSW   DC    X'00'                    FLAG IN LISTDS
*  SMF ID TABLE
*---  (LOOK IN "INITIAL" TO SEE HOW QSYSID IS CREATED FROM QSMFID) ---*
QSMFID   DC    CL8'????'                SMF ID OF SELECTED JES2
         DC    CL8'SMF1'                SMF ID FOR SYSTEM 1
         DC    CL8'SMF2'                                  2
         DC    CL8'SMF3'                                  3
         DC    CL8'SMF4'                                  4
         DC    CL8'SMF5'                                  5
         DC    CL8'SMF6'                                  6
         DC    CL8'SMF7'                                  7
         SPACE 2
*  SYSTEM ID TABLE
QSYSID   DC    CL8'????'                SYSTEM ID OF SELECTED JES2
         DC    CL8'SID1'                SYSTEM ID FOR SYSTEM 1
         DC    CL8'SID2'                                     2
         DC    CL8'SID3'                                     3
         DC    CL8'SID4'                                     4
         DC    CL8'SID5'                                     5
         DC    CL8'SID6'                                     6
         DC    CL8'SID7'                                     7
         SPACE 3
         POP   USING
         SPACE 1
         LTORG ,                        SHOULD BE ONLY LITS FROM QCOMM
         EJECT
***********************************************************************
*                                                                     *
*   DATASET ALLOCATION FIELDS                                         *
*      (ADAPTED FROM SYS1.MACLIB (IKJDAPL,IKJDAP08,18,2C))            *
*                                                                     *
***********************************************************************
DAIRFLAG DC    X'00'                    FLAG FOR ALLOCATE SUBROUTINE
DAIRECB  DC    F'0'                     ECB USED BY DAIR
         DC    0D'0'                    ALIGNMENT
***********************************************************************
*    THE DYNAMIC ALLOCATION INTERFACE ROUTINE (DAIR) PARAMETER LIST   *
*    (DAPL) IS A LIST OF ADDRESSES PASSED FROM THE INVOKER TO DAIR    *
*    VIA REGISTER 1                                                   *
***********************************************************************
DAPLUPT  DC    A(0)                     PTR TO UPT
DAPLECT  DC    A(0)                     PTR TO ECT
DAPLECB  DC    A(DAIRECB)               PTR TO CP'S ECB
DAPLPSCB DC    A(0)                     PTR TO PSCB
DAPLDAPB DC    A(0)                     PTR TO DAIR PARAMETER BLOCK
***********************************************************************
*                                                                     *
*   CHECK TO SEE IF A DSN IS ALLOCATED                                *
*                                                                     *
***********************************************************************
DA00CD   DC    X'0008'                  DAIR ENTRY CODE
DA00FLG  DC    X'00'                    FLAGS RETURNED
         DC    X'00'                    RESERVED
DA00PDSN DC    A(0)                     PTR TO DSN TO BE SRCHED IN DSE
DA00DDN  DC    CL8' '                   DDNAME TO BE SEARCHED IN DSE
DA00CTL  DC    X'00'                    FLAG BYTE
         DC    XL2'0'                   RESERVED
DA00DSO  DC    X'00'                    RETURNED DSORG
***********************************************************************
*                                                                     *
*   ALLOCATE DDNAME(W) DSNAME(X) SHR UNIT(Y) VOLUME(Z)                *
*                                                                     *
***********************************************************************
DA08CD   DC    X'0008'                  DAIR ENTRY CODE
DA08FLG  DC    X'00'                    FLAGS SET BY DAIR
         DC    X'00'
DA08DARC DC    H'0'                     DYN ALLOC RETURN CODE
DA08CTRC DC    H'0'                     CATALOG RETURN CODE
DA08PDSN DC    A(0)                     PTR TO DSN TO BE SRCHED IN DSE
DA08DDN  DC    CL8' '                   DDNAME TO BE SEARCHED IN DSE
DA08UNIT DC    CL8'SYSALLDA'            UNIT FOR SYS1.HASPCKPT
DA08SER  DC    CL8' '                   VOLSER FOR SYS1.HASPCKPT
DA08BLK  DC    F'0'                     DATA SET AVERAGE REC LENGTH
DA08PQTY DC    F'0'                     PRIMARY SPACE QUANTITY
DA08SQTY DC    F'0'                     SECONDARY SPACE QUANTITY
DA08DQTY DC    F'0'                     DIRECTORY BLOCK QUANTITY
DA08MNM  DC    CL8' '                   MEMBER NAME
DA08PSWD DC    CL8' '                   PASSWORD
DA08DSP1 DC    X'08'                    STATUS FLAGS - SHR
DA08DPS2 DC    X'08'                    DATA SET DISPOSITION - KEEP
DA08DPS3 DC    X'08'                    DATA SET COND. DISP. - KEEP
DA08CTL  DC    X'00'                    DAIR ACTION FLAGS
DA08TRKS EQU   X'80'                    units are trks
DA08ABKL EQU   X'40'                    units are blocks
DA08CYLS EQU   X'C0'                    units are cyls
         DC    XL3'0'                   RESERVED
DA08DSO  DC    X'00'                    DSORG
DA08ALN  DC    CL8' '                   ATTR-LIST-NAME           C99236
***********************************************************************
*                                                                     *
*   FREE DDNAME(XXXXXXXX)                                             *
*                                                                     *
***********************************************************************
DA18CD   DC    X'0018'                  DAIR ENTRY CODE
DA18FLG  DC    X'00'                    FLAGS SET BY DAIR
         DC    X'00'
DA18DARC DC    H'0'                     DYNAMIC ALLOCATION RETURN CODE
DA18CTRC DC    H'0'                     CATALOG RETURN CODE AREA
DA18PDSN DC    A(0)                     PTR TO DSN TO BE SRCHED IN DSE
DA18DDN  DC    CL8' '                   DDNAME TO BE SEARCHED IN DSE
DA18MNM  DC    CL8' '                   MEMBER NAME
DA18SCLS DC    CL2' '                   SYSOUT CLASS DESIRED WHEN
*                                       UNALLOC'ING A SYSOUT DATA SET
DA18DPS2 DC    X'08'                    DATA SET DISPOSITION - KEEP
DA18CTL  DC    X'10'                    FLAGS FOR SPECIAL DAIR PROC'ING
DA18JBNM DC    CL8' '                   IGNORED AS OF OS VS/2 RELEASE 2
***********************************************************************
*                                                                     *
*   MARK A DATASET "NOT IN USE"                                       *
*                                                                     *
***********************************************************************
DA2CCD   DC    X'002C'                  DAIR ENTRY CODE
DA2CFLG  DC    X'0001'                  MARK SPECIFIC DDN "NOT IN USE"
DA2CTCB  DC    XL4'00'                  UNUSED FOR THIS CALL TYPE
DA2CDDN  DC    CL8' '                   DDNAME TO FLAG
         EJECT
***********************************************************************
*                                                                     *
*   CHECKPOINT WORK AREAS                                             *
*                                                                     *
***********************************************************************
QCJQELEN DC    Y(0)                     $JQELEN
          AIF  (&QLEVEL LT 4).QCNOJIX   SKIP IF NOT SP
QCJIXL   DC    H'0'
QCJIXA   DC    A(0)                     ADDRESS OF JIX IOAREA
          AIF  (&QLEVEL LT 7).QCNOJIX   SKIP IF NOT SP136
QCJQEXA  DC    A(0)                     -> JQE EXTENSIONS
QCDASL   DC    A(0)                     NBR OF DAS CHKPT BLOCKS
QCDASA   DC    A(0)                     -> FIRST DAS BLOCK
QCPSTL   DC    A(0)                     NBR OF PST CHKPT BLOCKS
QCPSTA   DC    A(0)                     -> FIRST PST BLOCK
.QCNOJIX  ANOP
QCNUMTGA DC    A(0)                     NBR OF TRK GRPS ON ACTIVE VOLS
          AIF  (&QLEVEL GE 8).QCNOTGL   SKIP IF SP220
QCTGMLEN DC    H'0'                     NUMBER OF BYTES IN 'TGMAP'
.QCNOTGL  ANOP
QCJCTA   DC    A(0)                     ADDRESS OF JCT IOAREA
QCIOTA   DC    A(0)                     ADDRESS OF IOT IOAREA
QCBLKA   DC    A(0)                     ADDRESS OF DATASET BLOCK IOAREA
QCBLKTTR DC    F'0'                     MTTR OF QCBLKA -> BLOCK
QCKMLEN  DC    F'0'                     length of master
QCSMLEN  DC    F'0'                     rounded length of master
QCHSBLK  DC    H'0'                     HASPACE BLOCK SIZE
QCJCTNJN DC    H'0'                     number of haspace blocks alloc
*                                          for NJE section (SP420 only)
QCKPTIME DC    F'-1'                    TIME (BIN) CKPT LAST READ
QC##CKPT DC    F'0'                     NUMBER OF CALLS TO "CKPT"
QC#CKPT# DC    F'0'                     NUMBER OF TIMES READ AVOIDED
QC##RSPC DC    F'0'                     NUMBER OF CALLS TO "READSPC"
QC#RSPC# DC    F'0'                     NUMBER OF TIMES READ AVOIDED
          AIF  (&QLEVEL LT 8).QCNOKAC   skip if before SP220
QKACA    DC    A(0)                     -> KAC
QKACTUP  DC    XL8'0'                   Time (stck) of last KAC update
.QCNOKAC  ANOP
          AIF   (&QLEVEL LT 4).NOTGM    SKIP IF NOT SP
         DC    H'0'                     EXPANSION SPACE
QCTGML   DC    H'0'                     NUMBER OF BLOCKS FOR TG MAPS
QCTGMA   DC    A(0)                     ADDRESS OF TGM IOAREA
.NOTGM    ANOP
*  THE FOLLOWING 3 FIELDS MUST BE TOGETHER, IN ORDER
QCJQTA   DC    A(0)   *ORDER*           ADDRESS OF JQE IOAREA
QCJQETTR DC    F'0'   *ORDER*           TTR OF FIRST JQE CKPT BLOCK
QCJQENUM DC    F'0'   *ORDER*           NUMBER OF JQE CKPT BLOCKS
*  THE FOLLOWING 3 FIELDS MUST BE TOGETHER, IN ORDER
QCJOTA   DC    A(0)   *ORDER*           ADDRESS OF JOE IOAREA
QCJOETTR DC    F'0'   *ORDER*           TTR OF FIRST JOE CKPT BLOCK
QCJOENUM DC    F'0'   *ORDER*           NUMBER OF JOE CKPT BLOCKS
*
QCJQEA   DC    A(0)                     ADDR OF CURRENT JQE
QCJOTL   DC    F'0'                     COUNT OF RECORDS ON CKPT DS
          AIF   (&QLEVEL GE 5).QCNPDB2
QCPDDB1  DC    F'0'                     OFFSET IN IOT TO FIRST PDDB
.QCNPDB2  ANOP
QCCBID   DC    CL4' '                   CONTROL BLOCK ID TO READ
QCTRAK   DC    0F'0'                    DISK ADDR IN THE FORM MTTR
QCTRAKM  DC    X'0'                     EXTENT NUMBER
QCTRAKTT DC    X'0000'                  ABSOLUTE TRACK NUMBER
QCTRAKR  DC    X'0'                     RECORD NUMBER
         DC    X'0'                     EXTRA SPACE NEEDED FOR HEX CONV
QCDAD    DS    0XL8                     DISK ADDR IN THE FORM MBBCCHHR
         DC    X'0',X'0000'             EXTENT NUMBER, BIN NUMBER
QCDADCC  DC    X'0000'                  CYLINDER NUMBER
QCDADHH  DC    X'0000'                  HEAD NUMBER
QCDADR   DC    X'0'                     RECORD NUMBER
QCRECFM  DC    X'0'                     RECFM FOR SAVE
QCLRECL  DC    H'0'                     LRECL FOR SAVE
QCJQHEAD DC    A(0)                     OFFSET TO JQE HEADERS
QCDDTBLA DC    A(0)                     -> DSID TABLE FOR 'NS' SUBCMD
QCDDTBLL DC    F'0'                     LENGTH OF EXISTING TABLE
QCDDTBLE DC    A(0)                     ADDR OF CURRENT ENTRY
*  FORMAT IS C'*T',H'DSID',H'DSID',...,C'*E'
QCNSDIR  DC    C'N'                     'NS' SUBCMD CURRENT DIRECTION
         DC    XL3'00'                  SPARE (OR WASTED)
QCCKPT   DC    A(0)                     -> HASPCKPT DCB
QCSAVE   DC    A(0)                     -> HASPSAVE DCB
QCDECB1  DC    A(0)                     -> HASPCKPT DECB
QCDECB2  DC    A(0)                     -> HASPACE DECB
QCSPOOLS DC    A(0)                     -> dcblist for haspace
QCTRKCYL DC    A(0)                     -> trk/cyl list for dcbs
QCEXTMAP DC    A(0)                     -> map for extents -> dcb list
         SPACE 10
***********************************************************************
*                                                                     *
*   PRINT WORKAREA part 1                                             *
*                                                                     *
***********************************************************************
QVPRNTWA DC    A(QPRNTWA)               -> most of print work area
*  The rest of the PRINT fields below are referenced by other routines
QPPRNT   DC    A(0)                     -> HASPPRNT DCB
QPFLAG   DC    XL1'00'
HARDCPY   EQU   X'80'
QPCLASS  DC    C'A'                     PRINT OUTPUT CLASS
QPDEST   DC    CL8' '                   PRINT OUTPUT DEST
         EJECT ,
***********************************************************************
*                                                                     *
*   GETREC, GETPDDB WORK FIELDS                                       *
*                                                                     *
***********************************************************************
QCPDBOFF DC    H'0'                     offset of current pddb in iot
         QDSTAB  PREFIX=QP
         QDSTAB  PREFIX=QCC,PTR=YES
         QDSTAB  PREFIX=QCH,PTR=YES
QCGRMTTR DC    A(0)                     mttr for getrec to start at
QCGRPTR  DC    A(0)                     -> current record
QCGRLEN  DC    H'0'                     len of current record
QMLINECT DC    AL1(60)                  qmode lines/page
QSLINECT DC    AL1(0)                   jes2  lines/page
QJLINECT DC    AL1(0)                   jct   lines/page
QPLINECT DC    AL1(0)                   dsid  lines/page
QCRHOLD  DC    PL1'0'                   hold for line count
QCRFLAG  DC    X'00'                    flags for prev/next line
QRFSKP1  EQU   X'80'                    prev cmd was skp ch 1
QRFEOF   EQU   X'40'                    end of data reached
QRFINIT  EQU   X'20'                    initialize to top of dataset
QRFDIR   EQU   X'10'                    specific rec num request
QRFERR   EQU   X'08'                    error occured reading b
QGPFLAG  DC    X'00'                    getpddb flags
QPFINIT  EQU   X'80'                    init to first pddb
QPFDIR   EQU   X'40'                    get a specific pddb
QPFSPIN  EQU   X'20'                    searching spin iot chain
QPFSNUL  EQU   X'10'                    skip null pddbs
QPFNMSG  EQU   X'08'                    return direct; Â¬found
QPFNOSW  EQU   X'04'                    don't go to spin iot chain
QPFNIOT  EQU   X'02'                    new iot has been read
         SPACE 5
***********************************************************************
*                                                                     *
*   DISPLAY WORK FIELDS                                               *
*                                                                     *
***********************************************************************
QVPFKEYS DC    A(QPFKAREA)              PTR TO PFK AREA
QVSAVE   DC    A(QDSAVE)                PTR TO DISPLAY SAVE AREA
QVSTACK  DC    A(QDSTACK)               PTR TO SUBCOMMAND STACK AREA
QVSTACKC DC    A(QDSTACK)               PTR TO CURRENT TOP OF STACK
QVRING   DC    A(QCMDRING)              PTR TO COMMAND RING FOR RECALL
QCMDNUM  DC    H'0'                     CURRENT COMMAND NUMBER
QRETNUM  DC    H'0'                     COMMAND NUMBER TO RECALL NEXT
QVINPUT  DC    A(*-*)                   -> INPUT AREA IN SCREEN BUFFER
QVHLINE  DC    A(*-*)                   -> HEADER LINE
QVLINE1  DC    A(*-*)                   -> FIRST DATA LINE
QDLCTBL  DC    A(*-*)                   -> VALID LINE COMMANDS TABLE
*                                       (SEE DSECT IN FRONT OF QDLCBUF)
QVLCBUF  DC    A(QDLCBUF)               -> LINE COMMAND WORK AREA
QDCLEARL DC    F'0'                     LENGTH OF DATA AREA TO CLEAR
QDNEXT   DC    A(0)                     -> NEXT LOC IN OUTPUT SCRN BUFF
QDREPLYL EQU   66                       LENGTH OF REPLY AREA
QACTION  DC    CL8'***OK***'            RESPONSE FROM LINE COMMAND
*=====================================================================*
QSAVE2   EQU   *                        START FIELDS SAVED IN DISPLAY |
*  KEEP FIELDS QSAVE2 THRU QSAVE2L TOGETHER FOR MVC IN "DISPLAY"      |
QDREPLY  DC    CL(QDREPLYL)'STATUS'     TERMINAL USER REPLY           |
QDRLNG   DC    H'6'                     REPLY LENGTH                  |
QSAVE2L  EQU   *-QSAVE2                 LENGTH OF SAVED FIELDS        |
*=====================================================================*
QERRMSG  DC    CL79' '                  ERROR MESSAGE, IF ANY
QDPROMPT DC    CL(QDREPLYL)'STATUS'     USER'S PREVIOUS COMMAND
QINQCMD  DC    CL(QDREPLYL)'STATUS'     LAST INQUIRY COMMAND
QFINDCMD DC    CL(QDREPLYL)'FIND'       LAST "FIND" COMMAND
QTSOCMD  DC    CL(QDREPLYL)'TSO'        LAST "TSO" COMMAND
QMVSCMD  DC    CL(QDREPLYL)'/'          LAST "/" OR JES2 COMMAND
QDMSG    DC    CL(132+7*2)' '           AREA FOR BUILDING OUTPUT LINE
QCPU     DC    C'nnn'                   CPU PERCENTAGE
QDPR     DC    C'nnn'                   DEMAND PAGE RATE
QDHDSID  DC    CL8' '                   CURRENT DATASET ID
QDHJOBID DC    C'JOBNAMEX(JOBNNNNN)',C' '
QDHISFID DC    C'------------------'    Header for ISPF line
QDCOLNOW DC    H'80'                    SCREEN WIDTH ON LAST WRITE
QDLINES  DC    H'24'                    NUMBER OF ACTUAL DISPLAY LINES
QDROWS   DC    H'21'                    NUMBER OF USABLE DISPLAY LINES
QDCOLS   DC    H'80'                    LENGTH OF EACH DISPLAY LINE
QDLINECT DC    H'0'                     CURRENT LINE NUMBER ON SCREEN
QDATRNUM DC    H'0'                     NUMBER OF ATTR BYTES IN LINE
QDOVER   DC    X'00'                    PAGE OVERFLOW INDICATOR
QDERASE  DC    X'F5'                    ERASE WRITE OR ERASE WRITE ALT
QDHLINE  DC    CL79' ',CL(132-80)' '    HEADING LINE
         SPACE 1
QDSCREEN DS    0C                       DISPLAY SCREEN BUFFER
         DC    X'27'                    "ESC": 3270 COMMAND FOLLOWS
QD3270OP DC    X'F5'                    3270 ERASE WRITE COMMAND
*                                       X'F1' = 3270 NORMAL WRITE CMD
*                                       X'7E' = 3270 ERASE WRITE ALT
QDWCC    DC    X'C1'                    WCC:  RESET MDT
         DC    X'115D7F'                SPF-TCAM PREFIX
         DC    X'114040'                SBA TO TOP LEFT CORNER
         DC    X'1DE8'                  ATTR - PROT BRIGHT
QDQUEUE  DC    C'QUEUE -'               TITLE IN UPPER LEFT CORNER
QDTOP    DC    CL72' '
         DC    42CL(80+8*2)' ',CL20' '  REST OF SCREEN BUFFER
QDSCRLEN EQU   *-QDSCREEN               LENGTH OF BUFFER
         SPACE 2
*  AREA FOR "DISPLAY" TO SAVE IMPORTANT THINGS WHICH "PARSE" CHANGES
QDSAVE   DS    0XL(QSAVE1L+QSAVE2L)'00'
         DC    (L'QDSAVE)X'00'          TO GET LENGTH ATTRIBUTE CORRECT
         SPACE 2
***********************************************************************
*                                                                     *
*   PRINT WORKAREA part 2 (not directly addressable)                  *
*                                                                     *
***********************************************************************
*  Fields in this section are referenced only by PRINT
QPRNTWA  DC    0F'0'
QPHEAD0  DC    H'137,0'                 VBA length
QPHEAD1  DC    CL1'1'
*                   123456789.123456789.123456789.123456789.'
         DC    CL40'QUEUE HARDCOPY LOG  USER=XXXXXXX   DATE='
         DC    CL40'YY.DDD  HH:MM:SS DAY MON DD,19XX   XXXXX'
         DC    CL52' '
         ORG   QPHEAD1+26
QPUSER   DC    CL7' '                   FOR USERID
         ORG   QPHEAD1+41
QPDATE   DC    CL32' '
         ORG   QPHEAD1+75
QPAGEN#  DC    CL6' '
         ORG   ,
QPAGEN   DC    PL3'1'
QPUCS    DC    CL4' '                   PRINT OUTPUT UCS
QPFCB    DC    CL4' '                   PRINT OUTPUT FCB
QPFORM   DC    CL4' '                   PRINT OUTPUT FORM
         SPACE 3
LCTABLE  DSECT                          TABLE OF VALID LINE COMMANDS
LCTCMDNM DS    CL2                      LINE COMMAND NAME
LCTTRUNM DS    CL4                      TRUE COMMAND NAME
LCTFLAGS DS    B
LCTFDCMD  EQU   X'80'                   CMD MODIFIES SCREEN BUFFER
LCTFPRIV  EQU   X'40'                   CMD IS PRIVILEGED
LCTFDD    EQU   X'20'                   THIS IS "DD" SCREEN
LCTELEN  EQU   *-LCTABLE                LENGTH OF A TABLE ENTRY
         SPACE 2
LCBUFF   DSECT                          LINE COMMANDS INPUT SAVE AREA
         QLCB  ,
LCBELEN  EQU   *-LCBUFF                 LENGTH OF A TABLE ENTRY
         SPACE 2
QCOMMON  &QDTYPE                        RESUME
         SPACE 3
*  WORK AREA TO SUPPORT LINE COMMANDS -- FORMAT IS LCBUFF DSECT
QDLCBUF  DC    40XL(LCBELEN)'00'
*
*    VARIABLES FOR ISPF TABLES SUPPORT
*
         ORG   QDTOP
V8VAR    DS    0F            --|
QLCBE    DS    CL(LCBELEN)     |
QF1      DS    CL79            |     must remain together
QF2      DS    CL18            |     to allow a single
QF3      DS    CL8             |     VDEFINE to work
QF4      DS    CL61            |     and the code in DISPLAY
QF5      DS    CL17            |
QF6      DS    CL32            |     lengths must match VLEN8
QF7      DS    CL30          --|
         ORG   ,
         SPACE 3
***********************************************************************
*                                                                     *
*   ISPF BROWSE INTERFACE   -   independent addressability            *
*            Initialized in LISTDS                                    *
*                                                                     *
***********************************************************************
         DC    0F'0'
TFD      DC    CL4'TFD:',XL204'00'
         ORG   TFD+5
         DC    X'7E'                    TFDSTAT2 (CDO/CDC STAT BITS)
         ORG   TFD+50
         DC    X'2400'                  TFDMACRF (READ POINT)
         DC    X'08'                    TFDDSP1 (STATUS)
         DC    X'08'                    TFDDSP2 (DISP)
         ORG   TFD+64
         DC    A(TFDMENUP)              POINTER TO VAR VALUES
         DC    A(TFDDCB)                POINTER TO DCB
         ORG   TFD+128
         DC    A(TFDDSN)                PTR TO DSN
         ORG   ,                        END OF TFD
         SPACE 3
QPFKAREA DC    0D'0'                    ADDRESSABLE VIA "QVPFKEYS"
         @GLOBALC  QUEUE,PFKDFLT        generate default PFkeys
         SPACE 3
QDSTACK  DC    XL2'00',CL(QDREPLYL)'EXIT'  BOTTOM STACK ENTRY
QDSTACKL EQU   *-QDSTACK                LENGTH OF A STACK ENTRY
         DC    9XL(QDSTACKL)'00'        SPACE FOR 10 DEEP STACKING
         SPACE 3
QCMDRING DC    16CL(QDREPLYL)' '        COMMAND RING FOR RECALL
         SPACE 3
QTRACEWA DC    5F'0',CL80' '            QTRACE work area
         SPACE 3
         AIF   ('&CSECT' NE 'YES').NOLEVEL SKIP IF NOT GENERATING CSECT
         ENTRY QLEVEL
.NOLEVEL ANOP
QLEVEL   DC    C'&&QLEVEL='             eyecatcher
         AIF   (&QLEVEL GT 9).LEVEL2
         DC    C' &QLEVEL'
         AGO   .LEVELOK
.LEVEL2  ANOP
         DC    C'&QLEVEL'
.LEVELOK ANOP
         EJECT
         QZIHAETD ,
         EJECT
QCOMMON  &QDTYPE ,
.MEND    MEND
