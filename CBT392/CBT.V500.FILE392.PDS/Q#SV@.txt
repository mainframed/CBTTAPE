         TITLE '--- QUEUE--SAVEDS -- CREATE A COPY OF CURRENT DS ---'
*---------------------------------------------------------------------*
*                                                                     *
*   SAVEDS - CREATE A DISK COPY OF CURRENTLY SELECTED DATASET         *
*                                                                     *
*   Updates:                                                          *
*      03May91  LDW  Only compute output blksize if DISP=NEW          *
*       7Aug90  LDW  Fix EMS's fix                                    *
*       7Aug90  EMS  Fix 26Jun90 in a better fashion                  *
*      26Jun90  LDW  Fix S0C9 when very large lrecl & small ds        *
*      16Jun90  LDW  Run (mostly) in AMODE31 now                      *
*      23Aug89  LDW  Fix for LRECL=0 (like internal text)             *
*      05Sep88  LDW  Add BLKSIZE operand (ignored unless disp=NEW)    *
*                    Add BLK to keyword table so it can be used       *
*      13Aug88  LDW  Fix various bugs caused by error in 17Jan88 updt *
*      17Jan88  LDW  Fix problems with space calculations due to use  *
*                       of integer arithmetic without taking into     *
*                       account roundoff errors                       *
*                    Only compute space quantities for disp=NEW to    *
*                       avoid DAIR rc=4 (invalid parmlist)            *
*      11Aug87  EMS  Add CYL, TRK keywords (default to blocks)        *
*      21Mar87  LDW  Fix for DSID = 0 (from RSCS)                     *
*      04/22/86 LDW  MOVE ATTN FLAG FROM QTERMFLG TO QMISCFLG         *
*      04/26/84 LDW  USE GETREC ROUTINE TO READ INPUT DATASET, TO     *
*                       ALLOW IMPLEMENTATION OF SAVE RANGE            *
*                    ADD PRI=, SEC=, AND DIR= KEYWORDS                *
*                    ADD START= AND STOP= (END=) KEYWORDS             *
*                    SHOW RECORD COUNT SAVED                          *
*      01/23/84 LDW  ADD SUPPORT FOR COMMAND STACKING                 *
*                    FIX PROCESSING OF DSNAMES LONGER THAN 8 CHARS    *
*                    DISPLAY DSNAME IN 'SAVED' MESSAGE                *
*                    FIX ATTENTION PROCESSING                         *
*      03/18/83 LDW  CHANGE DISP AND NORLSE TO RESERVED WORDS         *
*                    ADD VOL= AND UNIT= KEYWORDS                      *
*                    REWRITE DSNAME LENGTH LOGIC                      *
*      12/21/82 LDW  SET NEW FLAG "QSC2ALNA" TO TELL ALLOC NOT TO     *
*                       ABEND IF UNABLE TO ALLOCATE                   *
*      12/20/82 LDW  CHANGE QTILT REPRMPT=YES TO OPTIONS=REPROMPT     *
*      12/10/82 LDW  PUT IN FREEPOOL FOR HASPSAVE DCB.  THIS WILL     *
*                       CORRECT A POTENTIAL PROBLEM CAUSED BY UPDATE  *
*                       DATED 11/11/82, WHICH REMOVED BUFL=8192       *
*                    DON'T SET "QSC2SAVE" - ALLOC WILL KNOW IT'S ME   *
*                    CHANGE NAME OF THIS CSECT TO "SAVEDS" TO AVOID   *
*                       ASSEMBLY PROBLEMS WITH NJE                    *
*      12/08/82 LDW  USE $LRC DSECT                                   *
*                    SET NEW FLAG "QSC2SAVE" SO "ALLOC" KNOWS IT'S ME *
*      11/11/82 EMS  ALLOW FULLY QUALIFIED DATASET NAME               *
*                    USE IHADCB SYMBOLS                               *
*                    HANDLE RECFM=VB OUTPUT DATASET                   *
*                    INSTALL DCB EXIT TO DO THINGS RIGHT              *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
SAVEDS   QSTART  Q19,WORKLEN=SV@WORKL,STACK=X'F0'
         USNGX WORK,R13                 LOCAL WORK AREA
***********************************************************************
*                                                                     *
*   ALLOCATE OUTPUT DATASET                                           *
*                                                                     *
***********************************************************************
*%%%     CLC   QPDSID,=H'0'             IS THERE A VALID DATASET?
*%%%     BE    SV$TILT1                 NO. TELL THE USER.
         TM    QFLAG2,Q2VALIDS          IS THERE A VALID DATASET?
         BZ    SV$TILT1                 NO. TELL THE USER.
         MVC   DSNAME+2(44),QBLANK      BLANK THE DSNAME
         LA    R1,DSNAME                -> TEXT UNIT
         ST    R1,DA08PDSN              SAVE IN DAIR PARM LIST
**%%     MVC   SV@BLKSZ,=F'6233'        assume 1/3 3350
         XC    SV@BLKSZ,SV@BLKSZ        initialize output blksize
         LH    R2,QOFF1                 OFFSET OF DSNAME
         LTR   R2,R2                    ANY OPERAND?
         BZ    SV$TILT3                 NO - ERROR
         LA    R2,QDREPLY(R2)           -> START OF DSNAME
         LR    R1,R2                    COPY PTR TO START OF DSN
         LA    R1,1(,R1)                -> NEXT CHAR OF DSN
         CLI   0(R1),C' '               END?
         BNE   *-8                      NO - KEEP LOOKING
         SR    R1,R2                    COMPUTE LENGTH OF DSN
         BCTR  R1,0                     -1 FOR EX
         CLI   QPARM1,C''''             QUOTED DSN?
         BNE   SAVE$Q1                  NO, CONT
         LA    R2,1(,R2)                -> PAST QUOTE
         SH    R1,=H'2'                 LESS SURROUNDING QUOTES
SAVE$Q1  EX    R1,SAVE$OC               FOLD DSN INTO UPPER CASE
         LA    R1,1(,R1)                TRUE LEN OF DSN
         CLI   QPARM1,C''''             QUOTED DSN?
         BE    SAVE$DSN                 YES, CONTINUE
         LA    R2,DSNAME+2(R1)          -> PAST CURRENT DSN
         MVC   0(8,R2),=C'.OUTLIST'     SUFFIX WITH OUTLIST
         LA    R1,8(,R1)                NEW LEN
SAVE$DSN STH   R1,DSNAME                STORE LENGTH OF DSNAME
         SPACE 3
         MVC   DA08DDN(72),ALLOCNEW     REMAINDER OF DISP=NEW PARM LIST
         LA    R2,QKEYWORD              -> FIRST KEYWORD
         LA    R4,16                    BXLE INCR
         LA    R5,QKEYLAST-16           BXLE END
         MVI   SV@FLAGS,0               INITIALIZE FLAGS
         ZAP   SV@FIRST,=P'1'           INIT STARTING RECORD NUMBER
         ZAP   SV@LAST,=P'9999999'      INIT ENDING   RECORD NUMBER
         SPACE 1
SV$KEYLP CLI   0(R2),C' '               ANY MORE KEYWORDS?
         BE    SV$KEYOK                 NO - DONE
         CLC   =C'NORLSE',0(R2)         NORLSE?
         BE    SV$NRLSE                 YES - GO SET BIT
         CLC   =C'NEW',0(R2)            DISP=NEW?
         BE    SV$NEW
         CLC   =C'OLD',0(R2)            DISP=OLD?
         BE    SV$OLD
         CLC   =C'MOD',0(R2)            DISP=OLD?
         BE    SV$MOD
         CLC   =C'UNIT',0(R2)           UNIT?
         BE    SV$UNIT
         CLC   =C'VOL',0(R2)            VOLSER?
         BE    SV$VOL
         CLC   =C'START',0(R2)          STARTING LINE NUMBER?
         BE    SV$START
         CLC   =C'END',0(R2)            ENDING LINE NUMBER?
         BE    SV$STOP
         CLC   =C'STOP',0(R2)           ENDING LINE NUMBER?
         BE    SV$STOP
         CLC   =C'PRI',0(R2)            PRIMARY?
         BE    SV$PRI
         CLC   =C'SEC',0(R2)            SECONDARY?
         BE    SV$SEC
         CLC   =C'DIR',0(R2)            DIRECTORY?
         BE    SV$DIR
         CLC   =C'TRK',0(R2)            tracks?
         BE    SV$TRK
         CLC   =C'TRACK',0(R2)          tracks?
         BE    SV$TRK
         CLC   =C'CYL',0(R2)            tracks?
         BE    SV$CYL
         CLC   =C'BLK ',0(R2)           blocks?
         BE    SV$BLK
         CLC   =C'BLKS',0(R2)           blksize/blksz?
         BE    SV$BLKSZ
         EX    0,*                      SAY WHAT???
SV$NRLSE NI    DA08CTL,255-X'10'        TURN OFF RLSE BIT
         B     SV$BXLE                  CHECK NEXT OPTION
SV$NEW   TM    SV@FLAGS,SV@DISP         ANY PREVIOUS DISP?
         BO    SV$ERR1                  YES - ERROR
         OI    SV@FLAGS,SV@DISP         REMEMBER WE HAD A DISP
         B     SV$BXLE                  CHECK NEXT OPTION
SV$OLD   TM    SV@FLAGS,SV@DISP         ANY PREVIOUS DISP?
         BO    SV$ERR1                  YES - ERROR
         OI    SV@FLAGS,SV@DISP         REMEMBER WE HAD A DISP
         MVC   DA08BLK(36),ALLOCOLD     MOVE IN DISP=OLD SETUP
         B     SV$BXLE                  CHECK NEXT OPTION
SV$MOD   TM    SV@FLAGS,SV@DISP         ANY PREVIOUS DISP?
         BO    SV$ERR1                  YES - ERROR
         OI    SV@FLAGS,SV@DISP         REMEMBER WE HAD A DISP
         MVC   DA08BLK(36),ALLOCOLD     MOVE IN DISP=OLD SETUP
         MVI   DA08DSP1,X'02'           CHANGE TO MOD
         B     SV$BXLE                  CHECK NEXT OPTION
SV$UNIT  MVC   DA08UNIT,8(R2)           SET UNIT NAME
         B     SV$BXLE                  CHECK NEXT OPTION
SV$VOL   MVC   DA08SER,8(R2)            SET VOLSER
         B     SV$BXLE                  CHECK NEXT OPTION
SV$START BAL   R3,SV$NUM                CONVERT NUMERIC OPERAND
         ZAP   SV@FIRST,QNUMWORK        SAVE STARTING NUMBER
         OI    SV@FLAGS,SV@RANGE        REMEMBER RANGE IN USE
         B     SV$BXLE                  CHECK NEXT OPTION
SV$STOP  BAL   R3,SV$NUM                CONVERT NUMERIC OPERAND
         ZAP   SV@LAST,QNUMWORK         SAVE ENDING NUMBER
         OI    SV@FLAGS,SV@RANGE        REMEMBER RANGE IN USE
         B     SV$BXLE                  CONVERT NUMERIC OPERAND
SV$PRI   BAL   R3,SV$NUM                CONVERT NUMERIC OPERAND
         OI    SV@FLAGS,SV@SPC#         space qty
         ST    R15,DA08PQTY             -> OUTPUT AREA (BINARY)
         B     SV$BXLE                  CHECK NEXT OPTION
SV$SEC   BAL   R3,SV$NUM                CONVERT NUMERIC OPERAND
         ST    R15,DA08SQTY             -> OUTPUT AREA (BINARY)
         OI    SV@FLAGS,SV@SPC#         space qty
         B     SV$BXLE                  CHECK NEXT OPTION
SV$DIR   BAL   R3,SV$NUM                CONVERT NUMERIC OPERAND
         ST    R15,DA08DQTY             -> OUTPUT AREA (BINARY)
         B     SV$BXLE                  CHECK NEXT OPTION
SV$TRK   TM    SV@FLAGS,SV@SPACE        any prev space?
         BO    SV$ERR2                  yes - error
         OI    SV@FLAGS,SV@SPACE        remember we had a space
         NI    DA08CTL,255-DA08CYLS     reset space
         OI    DA08CTL,DA08TRKS         set tracks
         B     SV$BXLE                  check next option
SV$CYL   TM    SV@FLAGS,SV@SPACE        any prev space?
         BO    SV$ERR2                  yes - error
         OI    SV@FLAGS,SV@SPACE        remember we had a space
         OI    DA08CTL,DA08CYLS         set cylinders
         B     SV$BXLE                  check next option
SV$BLK   TM    SV@FLAGS,SV@SPACE        any prev space?
         BO    SV$ERR2                  yes - error
         OI    SV@FLAGS,SV@SPACE        remember we had a space
         NI    DA08CTL,255-DA08CYLS     reset space
         OI    DA08CTL,DA08ABKL         set blocks
         B     SV$BXLE                  check next option
SV$BLKSZ BAL   R3,SV$NUM                CONVERT NUMERIC OPERAND
         ST    R15,SV@BLKSZ             output blksize
         B     SV$BXLE                  CHECK NEXT OPTION
SV$NUM   CLI   8(R2),C' '               IS THERE AN OPERAND?
         BE    SV$NUMER                 NO - ERROR
         LA    R1,15(,R2)               -> LAST POSSIBLE DIGIT
         CLI   0(R1),C' '   <====+      IS THIS THE END OF THE NUMBER?
         BNE   *+12   >=======+  |      YES
         BCT   R1,*-8   >=====|==+      FIND END OF NUMBER
         EX    0,*            |
         LA    R15,8(,R2)  <==+         -> START OF NUMBER
         LR    R0,R15                   COPY ADDRESS
         SR    R1,R0                    COMPUTE EX LENGTH OF NUMBER
         BAL   R14,QNUMCONV             CONVERT THE NUMBER
         BER   R3                       RETURN TO CALLER IF NUMERIC
SV$NUMER MVC   QERRMSG(58),=C'*** INVALID OR MISSING OPERAND FOR KEYWOR$
               D - XXXXXXXX ***'
         MVC   QERRMSG+45(8),0(R2)      FILL IN KEYWORD
         QTILT *,OPTIONS=(REPROMPT,SET)
SV$ERR1  QTILT '*** CONFLICTING DISP KEYWORDS ***',                    $
               OPTIONS=(REPROMPT,SET)
SV$ERR2  QTILT '*** CONFLICTING SPACE KEYWORDS ***',                   $
               OPTIONS=(REPROMPT,SET)
         SPACE 1
SV$BXLE  BXLE  R2,R4,SV$KEYLP           CHECK ALL KEYWORDS
         SPACE 2
SV$KEYOK MVI   DAIRFLAG,X'08'           INDICATE ALLOC FUNCTION
         CLI   QPARM1,C''''             QUOTED DSN?
         BNE   *+8                      NO, CONTINUE
         NI    DA08CTL,X'FF'-X'20'      YES, TURN OFF DSN-PREFIX
         OI    QSCFLAG2,QSC2ALNA        TELL ALLOC NOT TO ABEND ME
*%%      MVC   DA08BLK,SV@BLKSZ         set max blksize
         CLI   DA08DSP1,X'04'           disp=new?
         BNE   SV$ALLOC                 no - don't compute space
         OC    SV@BLKSZ,SV@BLKSZ        output blksize specified yet?
         BNZ   *+10                     yes - don't set default
         MVC   SV@BLKSZ,=F'6233'        assume 1/3 3350
         MVC   DA08BLK,SV@BLKSZ         set max blksize
         TM    SV@FLAGS,SV@SPACE+SV@SPC#  any overrides?
         BNZ   SV$ALLOC                 yes, use them
         MVI   QGPFLAG,QPFDIR           req current pddb!
         QCALL GETPDDB                  go get it
         USNGX PDBDSECT,R1
         LH    R0,PDBLRECL              record len
         LTR   R0,R0                    any?
         BNZ   *+8                      yes - use it
         LA    R0,255                   no - pick a default
         XR    R14,R14                  clear
         L     R15,SV@BLKSZ             use user-specified max blksize
         SLDL  R14,8                    *256
         DR    R14,R0                   get blocking factor (*256)
*        LTR   R0,R15                   copy for later
*        BP    *+8                      skip if ok
*        LA    R0,1                     else use a default
         LR    R0,R15                   move answer for later divide
         XR    R14,R14                  clear again
         L     R15,PDBRECCT             get number of records
         DROPX R1
         TM    SV@FLAGS,SV@RANGE        range to copy?
         BZ    SV$ALL                   no, get record count
         ZAP   SV@DWD,SV@LAST           get end
         SP    SV@DWD,SV@FIRST          less beginning
         CVB   R15,SV@DWD               binary
SV$ALL   SLDL  R15-1,8                  *256
         DR    R15-1,R0                 get number of blocks
         LA    R15,1(,R15)              fudge just in case
         ST    R15,DA08PQTY             save primary
         LR    R14,R15                  copy
         SRA   R14,3                    /8 for secondary
         BP    *+8                      skip if >0
         LA    R14,1                    else force a secondary of one
         ST    R14,DA08SQTY             ditto secondary
SV$ALLOC QCALL ALLOCATE                 CALL DAIR
         MVI   QCSAVE,X'80'             INDICATE END OF LIST & purify
         L     R3,QCSAVE                -> HASPSAVE DCB
         USNGX IHADCB,R3                ADDRESS HASPSAVE DCB
         MVC   DCBDDNAM,DA08DDN         GET RETURNED DDNAME
         LA    R1,SAVE@DCB              -> DCB EXIT FOR SAVE DCB
         STCM  R1,B'0111',DCBEXLSA      SAVE IN DCB
         BAL   R14,QAMODE24             change to AMODE 24
         SPACE 1
         OPEN  (,OUTPUT),MF=(E,QCSAVE)  OPEN HASPSAVE DCB
         SPACE 1
         BAL   R14,QAMODE31             resume AMODE 31
         SR    R4,R4                    ASSUME START AT TOP
         SR    R7,R7                    ZERO RECORD COUNTER
         ZAP   QPREC,SV@FIRST           SET STARTING RECORD NUMBER
         TM    SV@FLAGS,SV@RANGE        WANT A RANGE?
         BNO   SV$LOOP                  NO - GO LOOP FOR WHOLE DATASET
         BCTR  R4,0                     R4 NEGATIVE ==> SPECIFIC START
         CP    SV@FIRST,SV@LAST         FIRST > LAST?
         BNH   SV$LOOP                  NO - GO LOOP FOR RANGE
         QTILT '*** REVERSE RANGE INVALID ***',OPTIONS=(REPROMPT,SET)
         SPACE 1
***********************************************************************
*                                                                     *
*   PROCESS RECORDS                                                   *
*                                                                     *
***********************************************************************
SV$LOOP  TM    QMISCFLG,QMFATTN         USER PRESS ATTN?
         BO    SAVE$END                 YES - CLOSE UP SHOP AND LEAVE
         QCALL GETREC                   GET THE NEXT REC FROM DATASET
         LTR   R4,R4                    HAVE WE HIT BOTTOM?
         BZ    SAVE$END                 YES - CLOSE UP SHOP AND LEAVE
         USNGX LRCDSECT,R4
         TM    LRCFLAG1,LRC1SPAN        SPANNED RECORD?
         BO    SV$NEXT                  YES - CAN'T HANDLE THESE YET
         LA    R0,LRCTEXT               -> DATA PORTION OF RECORD
         LR    R1,R6                    GET DATA LENGTH
         LA    R14,BUFFER               -> OUTPUT RECORD AREA
         LA    R15,L'BUFFER             GET DEST LENGTH
         ICM   R1,B'1000',QBLANK        INSERT PAD CHAR
         MVCL  R14,R0                   COPY RECORD TO OUTPUT BUFFER
         LA    R0,BUFFER                ASSUME RECFM=F
         TM    DCBRECFM,DCBRECV         IS IT RECFM V?
         BNO   SV$PUT                   NO - PUT IT OUT NOW
         LA    R1,4(,R6)                L'RECORD+RDW
         CH    R1,DCBLRECL              OVER LRECL?
         BNH   *+8                      NO, USE IT
         LH    R1,DCBLRECL              YES, GET MAX LEN
         SLL   R1,16                    ASSEMBLE RDW
         ST    R1,BUFFRDW               SAVE IN BUFFER
         LA    R0,BUFFRDW               -> OUTPUT RECORD
         SPACE 1
SV$PUT   BAL   R14,QAMODE24             change to AMODE 24
         PUT   IHADCB,(0)
         BAL   R14,QAMODE31             resume AMODE 31
SV$NEXT  LA    R7,1(,R7)                COUNT A RECORD
         CP    QCCREC,SV@LAST           REACH USER'S LIMIT?
         BL    SV$LOOP                  NO - CONTINUE
         DROPX R4                       LRCDSECT
***********************************************************************
*                                                                     *
*   CLOSE UP SHOP AND GO HOME                                         *
*                                                                     *
***********************************************************************
SAVE$END BAL   R14,QAMODE24             change to AMODE 24
         CLOSE MF=(E,QCSAVE)            CLOSE HASPSAVE DCB
         BAL   R14,QAMODE31             resume AMODE 31
         SPACE 1
         FREEPOOL  (R3)                 FREE ITS BUFFER POOL
         SPACE 1
         MVC   DA2CDDN,DCBDDNAM         GET THE DDNAME
         DROPX R3                       IHADCB (HASPSAVE)
         MVI   DAIRFLAG,X'2C'           INDICATE "NOT_IN_USE" FUNCTION
         QCALL ALLOCATE                 CALL DAIR
**LDW**  QCALL LISTDS                   GO BACK TO LISTDS
**LDW**  B     QSTOP
         TM    QMISCFLG,QMFATTN         USER PRESS ATTN?
         BO    SAVEATTN                 YES - USE ALTERNATE MESSAGE
         MVC   QERRMSG(3),=C'==='
         CVD   R7,QNUMWORK              CONVERT NUMBER OF RECORDS SAVED
         MVC   QERRMSG+4(11),=X'2020206B2020206B202120'
         LA    R1,QERRMSG+3+12-1        PRESET SIGNIFICANCE POINTER
         EDMK  QERRMSG+3(12),QNUMWORK+3 FILL IN NUMBER OF RECORDS SAVED
         MVC   QERRMSG+4(12),0(R1)      SHIFT THE NUMBER OVER
         LA    R15,QERRMSG+3+12         -> END OF NUMBER +1
         SR    R15,R1                   COMPUTE LENGTH OF NUMBER
         LA    R1,QERRMSG+4+1(R15)      -> LOC FOR NEXT PART OF MESSAGE
         MVC   0(16,R1),=C'RECORDS SAVED IN'
***      LA    R1,QERRMSG+13            -> WHERE DSNAME WILL GO
         LA    R1,17(,R1)               -> WHERE DSNAME WILL GO
         TM    DA08CTL,X'20'            WAS DSNAME ENTERED IN QUOTES?
         BO    *+12                     NO - SKIP
         MVI   0(R1),C''''              YES - PUT IN LEADING QUOTE
         LA    R1,1(,R1)                AND BUMP OUTPUT PTR
         MVC   0(44,R1),DSNAME+2        FILL IN DSNAME
         AH    R1,DSNAME                -> PAST END OF DSNAME
         TM    DA08CTL,X'20'            WAS DSNAME ENTERED IN QUOTES?
         BO    *+12                     NO - SKIP
         MVI   0(R1),C''''              YES - PUT IN LEADING QUOTE
         LA    R1,1(,R1)                AND BUMP OUTPUT PTR
         MVC   1(3,R1),=C'==='          END THE MESSAGE
         QTILT *
SAVEATTN NI    QMISCFLG,255-QMFATTN     RESET ATTN FLAG
         QTILT '=== SAVE INCOMPLETE DUE TO ATTENTION ==='
SV$TILT1 QTILT '=== YOU ARE NOT PROCESSING A VALID DATASET ==='
SV$TILT3 QTILT '*** OUTPUT DSNAME MUST BE SPECIFIED ***',              $
               OPTIONS=(REPROMPT,SET)
         EJECT ,
***********************************************************************
*                                                                     *
*   DCB EXIT FOR HASPSAVE DCB                                         *
*                                                                     *
***********************************************************************
         PUSH  USING
         DROP  ,
         USING SAVE$DCB,R15
         USING IHADCB,R1
         USING QCOMMON,R11
SAVE$DCB STM   R14,R12,12(R13)          SAVE OPEN'S REGS
         LA    R6,1                     ASSUME PRE ALLOC DSN
         CLI   DCBRECFM,0               ANY SPECIFIED?
         BNE   SV$DCBGR                 YES, GOT RECFM
         XR    R6,R6                    NEW DSN
         MVC   DCBRECFM,QCRECFM         MOVE IN RECORD FORMAT
         NI    DCBRECFM,DCBRECCC        TURN OFF EVERTHING BUT CCTL
         OI    DCBRECFM,DCBRECF+DCBRECBR  SPECIFY RECFM = FB??
SV$DCBGR LH    R2,DCBLRECL              ANY LRECL YET?
         LTR   R2,R2                    SPECIFIED?
         BNZ   SV$DCBGL                 YES, USE IT
         XR    R6,R6                    MUST BE NEW DSN
         LH    R2,QCLRECL               NO TRY RECORD LENGTH
         LTR   R2,R2                    IS THE LRECL ZERO?
         BNZ   SV$DCBGL                 NO. SKIP.
         LA    R2,133                   YES. USE LRECL 133.
SV$DCBGL STH   R2,DCBLRECL              STORE LRECL
         LH    R5,DCBBLKSI              ANY BLOCKSIZE?
         LTR   R5,R5                    IS IT A PREM DS?
         BNZ   SV$DCBHB                 YES, GOT BLOCKSIZE
         XR    R6,R6                    MUST BE NEW DSN
*%       LH    R5,=H'6233'              DEFAULT TO 1/3 TRACK 3350
         L     R5,DA08BLK               default to user-specified blksz
SV$DCBHB TM    DCBRECFM,DCBRECU         RECFM=U?
         BO    SV$DCBBL                 YES - JUST USE BLOCKSIZE
         TM    DCBRECFM,DCBRECV         RECFM=V?
         BO    SV$DCBRV                 YES, HANDLE IT
         SR    R4,R4                    CLEAR FOR DIVIDE
         DR    R4,R2                    DIVIDE BLKSIZE BY LRECL
         MR    R4,R2                    MULTIPLY RESULT BY LRECL
         B     SV$DCBBL
SV$DCBRV LA    R2,4(,R2)                ADD IN SPACE FOR BDW
         CR    R2,R5                    IS LRECL LARGER?
         BNH   SV$DCBBL                 NO, THEN WE ARE OK
         LTR   R6,R6                    WAS IT A PERM DS?
         BNZ   SV$DCBBL                 YES, LEAVE AS ALLOCATED
         OI    DCBRECFM,DCBRECSB        NO, MAKE SPANNED RECORDS
SV$DCBBL STH   R5,DCBBLKSI              STORE BLKSIZE
         LM    R14,R12,12(R13)          RESTORE OPEN'S REGS
         XR    R15,R15                  CLEAR RET CODE
         BR    R14                      RETURN TO CALLER
         POP   USING
         EJECT ,
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
SAVE$OC  OC    DSNAME+2(*-*),0(R2)      << EXECUTED >>
         SPACE 1
         LTORG ,
         DC    0F'0'
         SPACE 1
ALLOCNEW DC    XL8'00'                  DDNAME
         DC    2CL8' '                  UNIT & VOLSER
         DC    F'6233,10,50,0'          BLKSIZE / PRI / SEC / DIR
         DC    2CL8' '                  MEMBER & PASSWORD
         DC    X'04'                    STAT=NEW
         DC    2X'02'                   DISP=CATLG
*        DC    X'B0'                    TRKS + PREFIX_USERID + RLSE
         DC    X'70'                    blks + prefix_userid + rlse
         DC    F'0',CL8' '              RESERVED + DSORG + ATTRLISTNAME
         SPACE 1
ALLOCOLD DC    4F'0'                    BLKSIZE / PRI / SEC / DIR
         DC    2CL8' '                  MEMBER & PASSWORD
         DC    X'01'                    STAT=OLD
         DC    2X'08'                   DISP=KEEP
         DC    X'20'                    PREFIX_USERID TO DSNAME
         SPACE 1
SAVE@DCB DC    X'85',AL3(SAVE$DCB)      -> DCB EXIT FOR HASPSAVE
         SPACE 2
         ENTRY SV#KEYS
SV#KEYS  DC    CL8'OLD     '
         DC    CL8'MOD     '
         DC    CL8'NEW     '
         DC    CL8'NORLSE  '
         DC    CL8'UNIT    '
         DC    CL8'VOL     '
         DC    CL8'START   '
         DC    CL8'END     '
         DC    CL8'STOP    '
         DC    CL8'PRI     '
         DC    CL8'SEC     '
         DC    CL8'TRACK   ',CL8'TRK      '
         DC    CL8'CYL     '
         DC    CL8'BLK     '
*%       DC    CL8'DIR'
         DC    CL8'BLKSIZE ',CL8'BLKSZ    '
         DC    X'FF'
         SPACE 2
WORK     DSECT
         ORG   WORK+72                  SKIP OVER SAVE AREA
SV@DWD   DS    D                        misc workarea
SV@FIRST DS    PL4                      FIRST DS RECORD # TO SAVE
SV@LAST  DS    PL4                      LAST  DS RECORD # TO SAVE
SV@FLAGS DS    B
SV@DISP  EQU   X'80'                    DISPOSITION FOUND
SV@RANGE EQU   X'40'                    LINE NUMBER RANGE SPECIFIED
SV@SPACE EQU   X'20'                    unit of space specified
SV@SPC#  EQU   X'10'                    quantity of space specified
SV@BLKSZ DS    F                        max blksize for NEW output ds
DSNAME   DS    H
         DS    CL44
SV@WORKL EQU   *-WORK
         DS    0F
BUFFRDW  DS    2H                       OPTIONAL RDW
BUFFER   DS    CL256                    OUTPUT BUFFER
         SPACE 3
         DROPX R13                      WORK
         SPACE 3
         QCOMMON
         QDCB  ,                        ADDED FOR DCB EQUATES
         SPACE 3
         Q$LRC
         Q$PDDB
