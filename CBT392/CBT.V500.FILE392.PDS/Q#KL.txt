         TITLE '--- QUEUE--LISTKIT -- LIST ALL KIT CONTROL BLOCKS ---'
*---------------------------------------------------------------------*
*                                                                     *
*   LISTKIT - Display information from all KITs                       *
*                                                                     *
*   Note - This subcommand is restricted to privileged users          *
*                                                                     *
*   Updates:                                                          *
*      09Jan90  EMS  Updated for pre 311 JES2                         *
*      07Jan90  LDW  Written                                          *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
LISTKIT  QSTART  STACK=X'30'
         QCALL CKPT                     read checkpoint data
         QCALL DISP80                   switch to 3278-2 mode if 3278-5
          AIF   (&QLEVEL GE 7).KL01
         QTILT '*** LISTKIT not implemented for this level of JES2 ***'
          AGO   .KL99
.KL01     ANOP
          AIF  (&QLEVEL LT 9).KL02
         MVC   QDHLINE(78),=C'Id   Origin Ent1   Ctlb Ksiz   Klng R_n #$
               Elems F1 C# Vr | HDPsiz Vr CBln CBnum'
          AGO  .KL03
.KL02     ANOP ,
         MVC   QDHLINE(67),=C'Id   Origin Ent1   Ctlb Ksiz   Klng R_n #$
               Elems F1 C# Vr | HDPsiz Vr'
.KL03     ANOP
         L     R9,QCJQTL                -> master ckpt record
         USNGX $SAVEBEG,R9
*%%% should add fields in QCOMMON and change INITIAL to use them
*%%% instead of II@KITF and II@KITL.  Well, for now, just duplicate
*%%% the code
         LA    R3,$SAVEBEG+$SAVELEN     -> first QSE
         LA    R0,QSELEN                length of a QSE
*%%%     MH    R0,$QSENO                total for all systems
         MH    R0,$QSEMAX               number of QSEs
         AR    R3,R0                    -> JQE extension area
         AH    R3,$MAXEXSZ              -> first KIT
*%%% why doesn't INITIAL have the following 3 instructions???
         LA    R0,DASEXLEN              DAS extension length
         MH    R0,$SPOLNUM              length of all DAS extensions
         AR    R3,R0                    -> KIT area
         USNGX KIT,R3
         LR    R6,R3                    save addr of first KIT for disp
         LR    R4,R3
         AH    R4,=Y(KITLNGTH*(8-1))    -> last kit  *kludge*?
*%%% end of code duplicated from INITIAL
         LR    R5,R9                    copy addr of $SAVEBEG
         L     R14,$MASTERL             length of master record
         LA    R14,4095(,R14)           round like INITIAL does
         SRL   R14,12
         SLL   R14,12
         AR    R5,R14                   -> first change log block
         LH    R15,$CLRECN              number of change log blks
         SLL   R15,12                   * 4k
         AR    R5,R15                   -> first HDP
***********************************************************************
*                                                                     *
*        Loop through all KITs, displaying interesting info           *
*                                                                     *
***********************************************************************
KL$LOOP  EQU   *
         MVC   KL@ID,KITID
         HEX   KL@ORG,KITORG+1,LEN=3,HEXTAB=QHEXTAB
         HEX   KL@ENT1,KITENT1+1,LEN=3
         HEX   KL@CTLB,KITCTLB+2,LEN=2
         HEX   KL@KSIZ,KITKSIZ+1,LEN=3
         HEX   KL@KLNG,KITKLNG+2,LEN=2
         LH    R0,KITRECN
         CVD   R0,QNUMWORK
         OI    QNUMWORK+7,X'0F'         remove sign
         MVC   KL@RECN,=X'202120'
         ED    KL@RECN-1(L'KL@RECN+1),QNUMWORK+6
         HEX   KL@NUMEN,KITNUMEN+1,LEN=3
         HEX   KL@FLG1,KITFLG1,LEN=1
         HEX   KL@CTNUM,KITCTNUM,LEN=1
         HEX   KL@VER,KITVER,LEN=1
         MVI   KL@BAR,C'|'
         L     R7,KITORG                origin to pool
         AR    R7,R5                    -> HDP for this KIT
         USNGX HDP,R7
         HEX   KL@SIZE,HDPSIZE+1,LEN=3
         HEX   KL@HDPVR,HDPVRSN,LEN=1
          AIF  (&QLEVEL LT 9).KL04
         HEX   KL@CBLEN,HDPCBLEN,LEN=2
         LH    R0,HDPCBNUM
         CVD   R0,QNUMWORK
         OI    QNUMWORK+7,X'0F'         remove sign
         MVC   KL@CBNUM,=X'2020202120'
         ED    KL@CBNUM-1(L'KL@CBNUM+1),QNUMWORK+5
.KL04     ANOP ,
         BAL   R14,QADDLINE             display the line
         LA    R3,KITLNGTH(,R3)         -> next KIT
         CLR   R3,R4                    past last one?
         BNH   KL$LOOP                  no - display next
         BAL   R14,QADDLINE             add a blank line
*--- Show offset to first KIT
         BAL   R14,QADDLINE             add a blank line
         MVC   QDMSG(22),=C'Offset to first KIT = '
         SR    R6,R9                    (first KIT) - $SAVEBEG
         ST    R6,QNUMWORK
         HEX   QDMSG+22,QNUMWORK,LEN=4
         BAL   R14,QADDLINE             add line to screen
*--- Show offset to first HDP
         BAL   R14,QADDLINE             add a blank line
         MVC   QDMSG(22),=C'Offset to first HDP = '
         SR    R5,R9                    (first HDP) - $SAVEBEG
         ST    R5,QNUMWORK
         HEX   QDMSG+22,QNUMWORK,LEN=4
         BAL   R14,QADDLINE             add line to screen
         B     QSTOP                    all done
***********************************************************************
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
***********************************************************************
         LTORG ,
         SPACE 1
         DROPX R9,R3,R7                 $SAVEBEG, $KIT, $HDP
***********************************************************************
*                                                                     *
*        DESCRIBE ALL THE DSECTS NEEDED BY THIS MODULE                *
*                                                                     *
***********************************************************************
         QCOMMON
         ORG   QDMSG
KL@ID    DS    CL4,C
KL@ORG   DS    CL6,C
KL@ENT1  DS    CL6,C
KL@CTLB  DS    CL4,C
KL@KSIZ  DS    CL6,C
KL@KLNG  DS    CL4,C
KL@RECN  DS    CL3,C
KL@NUMEN DS    CL6,C
KL@FLG1  DS    CL2,C
KL@CTNUM DS    CL2,C
KL@VER   DS    CL2,C
KL@BAR   DS    C'|',C
KL@SIZE  DS    CL6,C                    HDPSIZE
KL@HDPVR DS    CL2,C                    HDPVRSN
          AIF  (&QLEVEL LT 9).KL05
KL@CBLEN DS    CL4,C                    HDPCBLEN
KL@CBNUM DS    CL5,C                    HDPCBNUM
.KL05     ANOP ,
         SPACE 3
         Q$HCT
         Q$QSE
         Q$DAS
         Q$KIT
         Q$HDP
.KL99     ANOP
