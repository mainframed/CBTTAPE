         TITLE 'ISPF/PDF / QUEUE INTERFACE MODULE'                 LDW1
ISRQ     QSTART TYPE=MAIN               START ISRQ CSECT ASSEMBLY
         USING WORK,R13                 OUR WORK AREA
         PRINT GEN
***********************************************************************
*                                                                     *
*  GET PARMS PASSED FROM ISPF                                         *
*                                                                     *
***********************************************************************
         L     R3,0(,R2)                GET TLD ADDRESS
         ST    R3,QPRM1                 SETUP PARM LIST
         L     R6,4(,R2)                GET PARM ADDR              LDW2
         SH    R6,=H'2'                 BACK UP TO PARM LENGTH     LDW2
         LOAD  EPLOC==CL8'ISPLINK'                                 LDW1
         ST    R0,QPRM2                 SAVE ADDRESS OF 'ISPLINK'  LDW1
***********************************************************************
*                                                                     *
*  GET ADDRESS OF ORIGINAL CPPL AND BUILD OUR OWN COPY                *
*                                                                     *
***********************************************************************
         USING PSA,R0
         L     R2,PSATOLD               GET MY TCB ADDR
         USING TCB,R2
         L     R2,TCBOTC                GET MOTHER TASK (ISPF ITSELF)
         L     R2,TCBFSA                GET ISPF FSA
         L     R2,12+4+4+4(,R2)         GET R1 ON ENTRY TO ISPF (CPPL)
         DROP  R2,R0
         MVC   QCPPL(CPPLLEN),0(R2)     COPY CPPL
         LA    R2,QCPPL                 POINT AT OURS
         ST    R2,QPRM3                 SAVE ADDRESS OF CPPL
***********************************************************************
*                                                                     *
*  RETRIEVE NAME OF QUEUE COMMMAND FROM SCREEN                        *
*                                                                     *
***********************************************************************
         MVC   LENG,=F'8'               SET MAX LENGTH
         L     R15,QPRM2                -> ISPLINK                 LDW1
         CALL  (15),(VCOPY,VNM1,LENG,QNAME,VMOVE),VL,              LDW1$
               MF=(E,VCALLST)
***********************************************************************
*                                                                     *
*  BUILD A COMMAND BUFFER                                             *
*                                                                     *
***********************************************************************
         USING CPPL,R2
         LA    R1,ISRCBUF               GET ADDRESS OF FAKE CMD BUFFER
         ST    R1,CPPLCBUF              POINT AT NEW BUFFER
         MVC   0(4,R1),=AL2(0,0)        SET HEADER
         MVC   4(72,R1),=CL72' '        SET BLANKS
         MVC   4(8,R1),QNAME            MOVE IN COMMAND NAME
         L     R3,LENG                  GET LENGTH OF NAME
         STH   R3,ISRCBUF+2             SET OFFSET USED
         LA    R3,4+1(R3,R1)            POINT TO FREE BYTE
         LH    R15,0(,R6)               GET PARM LENGTH            LDW2
         SH    R15,=H'1'                -1 FOR EX                  LDW2
         BM    *+8                      SKIP MOVE IF NOTHING       LDW2
         EX    R15,MVCPARM              MVC 0(*-*,R3),2(R6)        LDW2
         LA    R5,1(R15,R3)             POINT PAST LAST BYTE       LDW2
         SR    R5,R1                    GET LENGTH USED
         STH   R5,ISRCBUF               SET LENGTH USED
         DROP  R2
***********************************************************************
*                                                                     *
*  INVOKE QUEUE                                                       *
*                                                                     *
***********************************************************************
         XC    LINKSFL(LINKSFLL),LINKSFL  CLEAR LINK SF=L AREA     LDW2
         MVC   QPRM0,=F'-1'             SET PARAMETER TO IND NOT CPPL
         LA    R1,QPRM0                 POINT AT PARMS
         LINK  EPLOC=QNAME,SF=(E,LINKSFL)  GO DO COMMAND           LDW2
         DELETE  EPLOC==CL8'ISPLINK'                               LDW1
*LDW1    QSTOP RC=0                     EXIT
         L     R13,4(,R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
         SPACE 2
MVCPARM  MVC   0(*-*,R3),2(R6)          << EXECUTED >>             LDW2
***********************************************************************
*                                                                     *
*  MISCELLANY                                                         *
*                                                                     *
***********************************************************************
VCOPY    DC    CL8'VCOPY'               VARIABLE SERIVCE NAMES
VNM1     DC    CL8'QCMDNM'              ---
VNM2     DC    CL8'QPRFX'               |
VNM3     DC    CL8'QVLSR'               |   VARIABLE NAMES
VNM4     DC    CL8'QUNIT'               |
VNM5     DC    CL8'QSBS'                |
VNM6     DC    CL8'QICMD'               ---
VMOVE    DC    CL8'MOVE'
***********************************************************************
*                                                                     *
*  GENERATE CONTROL BLOCK DSECTS                                      *
*                                                                     *
***********************************************************************
SYMDEL   DSECT ,                        KILL SYM CARD GENERATION
ISRQ     CSECT ,
*LDW1    QPRBGEN BEGIN                  SET PRINT FOR CNTL BLOCK GEN
*LDW1    $HASPEQU
         IHAPSA ,
         IKJTCB ,
         IKJCPPL ,
CPPLLEN  EQU   *-CPPL                   LENGTH OF CPPL
*LDW1    QPRBGEN DONE                   RESTORE NORMAL PRINT STATUS
WORK     DSECT ,
QCOMMON  EQU   WORK                     TO AVOID ERRORS
         DS    CL72
LINKSFL  LINK  SF=L                                                LDW2
LINKSFLL EQU   *-LINKSFL                                           LDW2
LENG     DS    F                        LEN OF VARIABLE DATA RETURNED
QPRM0    DS    F       F'-1'            ***
QPRM1    DS    A       TLD ADDR         *
QPRM2    DS    A       V(ISPLINK)       * KEEP THESE RECORDS TOGETHER
QPRM3    DS    A       A(QPPL)          ***
QCPPL    DS    ((CPPLLEN+3)/4)F         CPPL SPACE
VCALLST  DS    5F                       CPPL PARM LIST
QNAME    DS    CL8                      ADDRESS OF Q LOAD MODULE NAME
         DS    0F
ISRCBUF  DS    CL80                     FAKE COMMAND BUFFER
SYMNODEL DSECT ,                        RESTORE SYM CARD GENERATION
         END
