PROC 0 ALT DEBUG
IF &DEBUG = DEBUG THEN CONTROL SYMLIST CONLIST LIST
/*
/* This clist suppresses the display of the invocation panel (QPENTRY)
/* for normal users.  Customize it appropriately for your site.
/* At USC, the systems staff have another panels library, which
/* contains a selection to invoke this clist with the ALT keyword.
/*
/* This allows the systems programming staff to use special operands,
/* while limiting the users to access only the current production
/* QUEUE, spool, and checkpoint.
/* */

SET &QPARMS =
ISPEXEC VGET (ZENVIR)
/* In ISPF V3R2, we no longer need the TLD for anything, so it is */
/* not necessary to use the interface module with prefix 'ISR'.   */
/* Otherwise, in order to get the TLD address, we must make ISPF  */
/* think we are part of it, hence the use of the ISRQ module.     */
IF &SUBSTR(6:8,&STR(&ZENVIR)) >= 3.2 THEN SET &PGM = QUEUE
                                     ELSE SET &PGM = ISRQ

/*IF &SUBSTR(1:1,&SYSUID) = S      THEN DO
IF &ALT = ALT                      THEN DO
   ISPEXEC DISPLAY PANEL(QPENTRY)
   IF &LASTCC ¬= 0 THEN EXIT
/* ISPEXEC VGET (QPRFX QVLSR QSBS QICMD)
   IF &QCMDNM = ISRQ THEN DO
      CONTROL ASIS
      WRITE That would be exceedingly stupid.
      EXIT CODE(20)
      END
   IF &SUBSTR(6:8,&STR(&ZENVIR)) >= 3.2 THEN SET &PGM = &QCMDNM
   IF &QPRFX ¬= &Z THEN SET &QPARMS = &STR(&QPARMS) DSN(&QPRFX)
   IF &QVLSR ¬= &Z THEN SET &QPARMS = &STR(&QPARMS) CKPT(&QVLSR)
   IF &QSBS  ¬= &Z THEN SET &QPARMS = &STR(&QPARMS) JES(&QSBS)
   IF &QICMD ¬= &Z THEN SET &QPARMS = &STR(&QPARMS) &STR(&QICMD)
   END
                                   ELSE DO
   SET &QCMDNM = QUEUE
   ISPEXEC VPUT (QCMDNM)
   END

ISPEXEC SELECT PGM(&PGM) PARM(&QPARMS)
