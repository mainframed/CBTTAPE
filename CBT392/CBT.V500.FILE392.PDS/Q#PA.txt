         TITLE '--- QUEUE--PARSE -- COMMAND LINE PARSE ROUTINES ---'
*---------------------------------------------------------------------*
*                                                                     *
*   PARSE - Parse the input subcommand                                *
*                                                                     *
*   Entry - New subcommand and operands in "QDREPLY"                  *
*                                                                     *
*   Exit - All kinds of fields filled in and bits set (in QCOMMON)    *
*                                                                     *
*   Updates:                                                          *
*      22Sep91  LDW  Add "ELIST/EL" subcommand                        *
*                    Add KEYS=LI#KEYS to LIST, JCL, JLOG, JMSG        *
*                    Add EL line command to PA#LC#DD                  *
*      26Mar91  LDW  Add "LI" as an alias for "LIST", because both    *
*                       "L" and "LIST" don't get to us while in       *
*                       BROWSE (BRIF) under ISPF                      *
*      30Jan91  LDW  Retrofit GLA fixes below:                        *
*     (22Jun90) GLA  Add new command "NJ".                            *
*     (17Apr90) GLA  Add second parm to CPU command.                  *
*     (06Jan90) GLA  Always generate MCS/CONS command                 *
*                    Add "?" as a line command "DD" or "S"            *
*     (15Nov89) GLA  Add SPIN command as part of PRINT.               *
*                    Change 'END' to ignore operands                  *
*     (20Sep89) GLA  Do not parse operator commands.                  *
*     (24Apr89) GLA  Disable REPOS commands if in ISPF                *
*      27Nov90  LDW  Change QCMN,  CKPT,  HCT,  JQE,  JOE,  JCT       *
*                       to XQCMN, XCKPT, XHCT, XJQE, XJOE, XJCT       *
*                       (X commands will be deleted once CBFMT is     *
*                       finished, since all will have /X operand)     *
*                    Add subcommands CB, QCMN, CKPT, HCT, JOT, JCT,   *
*                       JQE, JOE                                      *
*      05Jan90  LDW  Add LKIT and LDAS subcommands                    *
*      15May89  EMS  Add WORKLEN to QSTART macro just in case         *
*      25May89  LDW  Add DOWN command (just had D before!)            *
*      30May88  LDW  Add JQE and JOE subcommands (like XJ, but by     *
*                       offset)                                       *
*      10Apr88  LDW  Fix PA$TOKEN bug when scanning for subcmd name   *
*                    Correct PDDB entry                               *
*      17Jul87  EMS  Add 'PAGE' (alias 'P')                           *
*                    Add 'PDDB' command (per LDW)                     *
*      17Jan87  LDW  ADD 'SPSP' (alias 'TGPS')                        *
*      27AUG86  LDW  ADD 'ATTR'                                       *
*      04/07/86 LDW  ADD 'RETRIEVE' & 'RE' AS ALIASES FOR 'RECALL'    *
*                       FOR COMPATIBILITY WITH VM                     *
*                    REMOVE '*' COMMAND (OLD 'RECALL')                *
*                    ADD IN#KEYS                                      *
*      04/03/86 LDW  USE SPECIFIC BIT NAMES IN QDEBUG                 *
*      12/02/85 LDW  ADD J ALIAS FOR JS, ADD TO LINE COMMANDS TABLE   *
*      01/30/85 LDW  ADD SJ#KEYS REFERENCE                            *
*      08/30/84 LDW  SET &ZCMD INSTEAD OF &OPT IF '=AAA'              *
*      03/27/84 LDW  MOVE CALL TO 'DISPCHG' TO 'QUEUE' MAINLINE       *
*                    ADD 'JS' SUBCOMMAND                              *
*      01/27/84 LDW  ADD 'PS' SUBCOMMAND                              *
*      01/23/84 LDW  ADD 'SJ', 'NS', 'COLS' SUBCOMMANDS               *
*      12/20/83 LDW  ADD KEYS= TO 'DJ'                                *
*      11/29/83 LDW  ADD 'EJ' SUBCOMMAND (LINE CMD  'EJ')             *
*      10/30/83 LDW  ADD 'TJ' SUBCOMMAND (LINE CMDS 'TJ' AND 'T')     *
*      09/07/83 LDW  ADD 'S' AS LINE COMMAND ALIAS OF 'DD'            *
*      03/17/83 LDW  ADD MORE ALIASES FOR "STOP"                      *
*                    CHANGE "END" TO FUNC=04                          *
*                    ADD "RECALL" ("RC") AS PRIMARY NAME FOR "*",     *
*                       CHANGE REPROMPT, STARKEYS TO RECALL, RC#KEYS  *
*                    ADD KEYS=SV#KEYS, CHANGE MAX POSITS TO 1         *
*      03/09/83 LDW  ADD KEYS= TO "HELP"                              *
*      03/02/83 LDW  CHANGE 'QTILT *' TO USE QERRMSG                  *
*                    ENHANCED ERROR MESSAGES                          *
*      02/24/83 LDW  ADD LINE COMMAND TABLES                          *
*      02/09/83 LDW  MORE FIDDLING WITH =XXX CODE                     *
*      02/08/83 LDW  DELETE "DHX", CHANGE "DH" TO KEYS=DH#KEYS,       *
*                       CHANGE MAX POSIT OPERANDS FOR "DH" TO ZERO    *
*      02/07/83 LDW  IF INVOKED FROM WITHIN SPF AND '=COMMAND' IS     *
*                       ENTERED, SET &OPT AND FAKE OUT AN EXIT CMD    *
*                       NOTE:  SINCE THIS DOESN'T WORK YET, IT IS     *
*                       UNDER CONTROL OF A QDEBUG FLAG                *
*      02/03/83 LDW  CHANGE "RD" FROM LISTDS TO REPOS                 *
*                    CHANGE QDINQCMD TO QINQCMD, DELETE QDINQLEN      *
*                    MAKE USE OPTIONS=SET ON QTILT                    *
*                    FIX BUG WHICH CAUSED '/ABC      DEF=XYZ' TO FAIL *
*      02/02/83 LDW  ADD KEYS= TO "CHGS"                              *
*                    ADD "COPY"                                       *
*      01/31/83 LDW  FIX 3278-3 & -4 HANDLING                         *
*      01/19/83 LDW  SET QTPAGEX AND QTHALFX WHEN APPROPRIATE         *
*      01/12/83 LDW  MOVE STARKEYS TO "DISPLAY"                       *
*      01/06/83 LDW  ADD "LTXT"                                       *
*                    FIX BUG WHICH CAUSED "KEYWORDS NOT ALLOWED" MSG  *
*                       TO BE ISSUED INSTEAD OF "TOO MANY POSITIONAL  *
*                       OPERANDS" MSG                                 *
*      01/05/83 LDW  ALTER CHECK FOR LARGE SCREEN MODE                *
*                    IMPROVE CHECKING FOR MVS AND JES2 COMMANDS       *
*                    MORE FIXES TO "DON'T CHECK OPERANDS" CASE        *
*                    ADD KEYS= TO MODE, QINT, XB                      *
*                    CHANGE NBR OF POSITS ALLOWED TO 0 FOR MODE, QINT *
*                    SET Q2REPOS IF REPOSITIONING SUBCOMMAND          *
*                    ADD EDIT, BROWSE, REDISP, HCT, QCMN, CKPT        *
*                    CHANGE 'F' OPERAND OF "*" TO KEYWORD             *
*      12/21/82 LDW  ONLY GENERATE ENTRY FOR "CONS" IF &QTMPMAC=1     *
*                    ADD "QINT"                                       *
*                    IF NO POSITIONAL OPERANDS ALLOWED, MAKE SLASH    *
*                       IN FRONT OF FIRST RESERVED WORD OPTIONAL      *
*      12/17/82 LDW  CLEAR QDHLINE BEFORE MOVING IN ERROR MESSAGES    *
*                    CHANGE NUMBER OF POSITIONAL OPERANDS ALLOWED TO  *
*                       ZERO FOR DA, DS, DT (KEYWORDS NOW SUPPORTED)  *
*      12/15/82 LDW  ADD SUPPORT FOR RESERVED WORD AND KEYWORD=VALUE  *
*                       OPERANDS                                      *
*                    CHECK FOR TOO MANY POSITIONAL OPERANDS FOR       *
*                       GIVEN SUBCOMMAND                              *
*                    RE-ARRANGE OPERANDS ON QCMDE MACRO               *
*                    ADD "DN" AND "CHGS" SUBCOMMANDS                  *
*                    PUT IN CHECK FOR SUBCOMMAND NOT EXISTING         *
*      12/10/82 LDW  CHANGE "SAVE" TO "SAVEDS" TO AVOID ASSEMBLY      *
*                       PROBLEMS WITH NJE                             *
*      12/08/82 LDW  USE "QPARMEND" INSTEAD OF "QOFF4+12"             *
*      10/21/82 LDW  CHANGE "HO" TO "DH", "XHO" TO "DHX"              *
*      08/30/82 LDW  ADD "LBLK" SUBCOMMAND                            *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
PARSE    QSTART  Q16,WORKLEN=PA@WORKL
         USING WORK,R13                 BASE REG FOR LOCAL WORK AREA
***********************************************************************
*                                                                     *
*   PARSE SUBCOMMAND NAME                                             *
*                                                                     *
***********************************************************************
         LH    R2,QDRLNG                LENGTH OF REPLY LINE
         LTR   R2,R2                    ANY INPUT?
         BNP   QSTOP                    NO - SO LEAVE
         MVC   PA@REPLY(QDREPLYL),QDREPLY  COPY THE INPUT COMMAND
         OC    PA@REPLY(QDREPLYL),QBLANK   TRANSLATE TO UPPER CASE
         CLC   PA@REPLY(QDREPLYL),QBLANK   IS THE ENTIRE REPLY BLANK?
         BE    QSTOP                    YES - NOTHING TO DO HERE
         XC    QSCFLAGS,QSCFLAGS        CLEAR SUBCOMMAND FLAGS
         MVC   PA@FIELD,QBLANK          BLANK THE WORK FIELD
         MVC   P@OFFSET(4),=F'0'        ZERO THE OFFSET AND LENGTH
         MVC   QOFF0(12),P@OFFSET       INITIALIZE FIRST FIELD
         MVC   QOFF1(QPARMEND-QOFF1),QOFF0  INIT THE REST
         MVC   QKEYWORD+00(80),QBLANK   CLEAR KEYWORD/VALUE AREA
         MVC   QKEYWORD+80(80),QBLANK   CLEAR KEYWORD/VALUE AREA
         MVI   PA@FLAG,0                CLEAR MY FLAGS
         SR    R5,R5                    INDICATE PROCESSING SUBCMD NAME
         LA    R3,PA@REPLY              -> FIRST BYTE OF REPLY LINE
         BAL   R10,PA$TOKEN             GET SUBCOMMAND NAME
         EX    0,*                      THERE BETTER BE A SUBCMD NAME
         MVC   QOFF0(12),P@OFFSET       SAVE OFFSET, LEN, SUBCMD NAME
***********************************************************************
*                                                                     *
*   FIND THE PARSE TABLE ENTRY FOR THE SUB COMMAND                    *
*                                                                     *
***********************************************************************
         LA    R9,PA#TABLE              START OF SUBCOMMAND TABLE
         USING QCMDE,R9
PA$NEXT1 SR    R0,R0                    CLEAR FOR IC
         IC    R0,CMD@NUM               GET NUMBER OF SUBCMD NAMES
         LA    R1,CMD@CMD               -> FIRST SUBCMD NAME
PA$NEXT2 CLC   QSUBNAME(L'CMD@CMD),0(R1)  COMPARE COMMAND NAME
         BE    PA$FOUND                 THIS IS THE ONE
         LA    R1,L'CMD@CMD(,R1)        -> NEXT SUBCMD NAME
         BCT   R0,PA$NEXT2              CHECK ALL NAMES
*  INCREMENT TO NEXT SUBCMD ENTRY
         LR    R9,R1                    -> NEXT TABLE ENTRY
         CLI   0(R9),X'FF'              IS THIS THE END OF TABLE?
         BNE   PA$NEXT1                 NO. TRY NEXT ENTRY.
         LA    R9,PA#CMDJ               USE JES2 SUBCOMMAND ENTRY
         CLC   QSUBNAME(1),QCOMCHAR     COULD THIS BE A JES2 COMMAND?
         BE    PA$FOUND                 YES - CALL 'OPERCMD'
         LA    R9,PA#CMD                USE '/' SUBCOMMAND ENTRY
         CLI   QSUBNAME,C'/'            SHORT FORM FOR MVS COMMAND?
         BE    PA$FOUND                 YES - CALL 'OPERCMD'
         LA    R9,PA#CMDQ               USE '|' SUBCOMMAND ENTRY
         CLI   QSUBNAME,C'|'            QUIET FORM FOR MVS COMMAND?
         BE    PA$FOUND                 YES - CALL 'OPERCMD'
         CLI   QSUBNAME,C'='            DOES HE THINK HE'S IN SPF?
         BNE   PA$BOUNC                 NO - NORMAL ERROR MESSAGE
         TM    QFLAG2,Q2SPF             ARE WE REALLY IN SPF???
         BNO   PA$NOSPF                 NO - TELL HIM SORRY
         TM    QDEBUG1,QDEBUG12         IS THIS REQUESTED?    %%%
         BZ    PA$NOSPF                 NO - TELL HIM SORRY   %%%
*---  WE ARE IN SPF AND USER HAS ENTERED '=XXX'.  SET &ZCMD AND    ---*
*---  FAKE OUT THE COMMAND TO 'EXIT'.                              ---*
         SPACE 1
*        LINK  EPLOC=PA#ISPLK,MF=(E,PA@PARML),PARAM=(=CL8'VDEFINE',    $
               =CL8'ZCMD',PA@REPLY,=CL8'CHAR',=A(QDREPLYL),=CL8'COPY'),$
               VL=1
         SPACE 1
*        LTR   R15,R15
*        BZ    PA$DEFOK
         SPACE 1
*        QTILT '*** VDEFINE RC ¬= 0 ***'
*A$DEFOK LINK  EPLOC=PA#ISPLK,MF=(E,PA@PARML),PARAM=(=CL8'VPUT',       $
               =CL8'ZCMD',=CL8'PROFILE'),VL=1
         SPACE 1
*        LTR   R15,R15
*        BZ    PA$PUTOK
*        QTILT '*** VPUT RC ¬= 0 ***'
         SPACE 1
         LINK  EPLOC=PA#ISPLK,MF=(E,PA@PARML),PARAM=(=CL8'VREPLACE',   $
               =CL8'ZCMD',=A(QDREPLYL),PA@REPLY),VL=1
         SPACE 1
         LTR   R15,R15
         BZ    PA$REPOK
         QTILT '*** VREPLACE RC ¬= 0 ***'
         SPACE 1
PA$REPOK LINK  EPLOC=PA#ISPLK,MF=(E,PA@PARML),PARAM=(=CL8'VPUT',       $
               =CL8'ZCMD',=CL8'SHARED  '),VL=1
****???? SHOULD THIS BE THE FUNCTION POOL?
         SPACE 1
         LTR   R15,R15
         BZ    PA$PUTOK
         SH    R1,=H'8'                 BACK UP TO START OF TLD
         CLC   =C'TLD',0(R1)            DID WE GET THE TLD?
         BNE   PA$PUTNO                 NO - GIVE REGULAR ERROR MSG
         MVC   X'28C'(24,R1),QDREPLY+1  YES - SET JUMP DESTINATION
         B     PA$PUTOK                 AND LEAVE
PA$PUTNO QTILT '*** VPUT RC ¬= 0 ***'
PA$PUTOK LA    R9,PA#EXIT               USE 'EXIT' SUBCOMMAND ENTRY
         B     PA$FOUND                 GO PROCESS 'EXIT' SUBCOMMAND
PA$NOSPF QTILT '*** NOT IN SPF - SORRY...  TRY AGAIN ***'
PA$BOUNC MVC   QERRMSG(62),=C'*** INVALID SUBCOMMAND "XXXXXXXX" - FOR H$
               ELP, ENTER "HELP" ***'
         MVC   QERRMSG+24(8),QSUBNAME
         QTILT *,OPTIONS=(REPROMPT,SET)
PA$FOUND TM    CMD@FLAG,P@P             PRIVILEGED SUBCOMMAND?
         BNO   PA$ALL                   NO - CONTINUE
         TM    QXAUTH,QXAUTHX           YES - IS HE AUTHORIZED?
         BZ    PA$BOUNC                 NO - THE SUBCOMMAND IS INVALID
PA$ALL   MVC   QCODE(1),CMD@CODE        SUBCMD CODE
         MVC   QSUBCMD,CMD@ADDR         SUBCMD ADDRESS
         TM    QISFLAG1,QIS1ISPF        are we in ISPF?
         BZ    PA$ISPF                  nope - no concern
         CLC   QSUBCMD,=V(REPOS)        is this reposition?
         BE    PA$BOUNC                 yes - invalid with browse
PA$ISPF  DS    0H
         TM    CMD@FLAG,P@I             REPROMPTABLE INQUIRY COMMAND?
         BNO   *+10                     NO - SKIP
         MVC   QINQCMD,QDREPLY          YES - SAVE THE COMMAND LINE
         NI    QFLAG1,255-Q1VALJOB-Q1LSTCMD-Q1IMMED  RESET FLAGS
         TM    CMD@FLAG,P@X             EXECUTE THIS SUBCMD IMMED.?
         BNO   *+8                      NO - SKIP
         OI    QFLAG1,Q1IMMED           YES - SET FLAG
*%%%     TM    CMD@FLAG,P@P+P@X         OPERATOR COMMAND ???
*%%%     BO    PA$FINAL                 IF SO, IGNORE REST
         SPACE 2
***********************************************************************
*                                                                     *
*  PARSE OUT AND SAVE OPERANDS                                        *
*                                                                     *
***********************************************************************
         LA    R5,QOFF1                 -> FIRST OPERAND SLOT
         LA    R6,QPARMEND              -> PAST LAST OPERAND SLOT
         SR    R0,R0                    CLEAR FOR IC
         IC    R0,CMD@POS               # OF POSIT. OPERANDS ALLOWED
         SPACE 1
PA$AGAIN LTR   R2,R2                    ANY MORE INPUT?
         BNP   PA$VALCK                 NO - EXIT LOOP
         BAL   R10,PA$TOKEN             GET THE NEXT TOKEN
         B     PA$VALCK                 EXIT LOOP IF NO MORE OPERANDS
         TM    PA@FLAG,PA@KEYS          KEYWORD OR RESERVED WORD?
         BO    PA$KW$RW                 YES - SAVE IT
*                                       NO - POSITIONAL
         SH    R0,=H'1'                 COUNT 1 POSITIONAL OPERAND
         BM    PA$2MANY                 ABORT IF THIS IS ONE TOO MANY
         MVC   0(12,R5),P@OFFSET        MOVE POSITIONAL STUFF
         LA    R5,12(,R5)               BUMP TO NEXT POSITIONAL
         B     PA$MOVED                 CONTINUE
PA$KW$RW CR    R5,R6                    IS THERE ROOM FOR ANOTHER?
         BNL   PA$KMANY                 NO - GIVE ERROR MSG
         MVC   0(8,R5),PA@FIELD         MOVE KEYWORD OR VALUE
         LA    R5,8(,R5)                INCR TO NEXT FIELD
         CLI   PA@DELIM,C'='            WAS THIS A KEYWORD= ?
         BE    PA$KWEQ                  YES - SETUP FOR VALUE
         TM    PA@FLAG,PA@VALUE         NO - WAS THIS THE KW VALUE?
         BO    PA$KWOK                  YES - OK
         LA    R5,8(,R5)                NO - RSRVD WORD - BUMP PAST VAL
         B     PA$MOVED                 CONTINUE
PA$KWEQ  TM    PA@FLAG,PA@VALUE         WAS THIS VALUE?
         BO    PA$KWBAD                 YES - KEYWORD=VALUE= IS INVALID
         OI    PA@FLAG,PA@VALUE         NO - SET FLAG "EXPECTING VALUE"
         LA    R3,1(,R3)                SKIP OVER '='
         BCT   R2,PA$MOVED              CONTINUE IF MORE INPUT
         B     PA$VALCK                 VALIDITY CHECK KEYWORDS ENTERED
*---  HE ENTERED KEYWORD=VALUE=
PA$KWBAD MVC   QERRMSG(25),=C'*** SYNTAX ERROR -- VALUE'
         SH    R5,=H'16'                BACK UP TO KEYWORD
         MVC   QERRMSG+26(8),8(R5)
         MVC   QERRMSG+35(10),=C'OF KEYWORD'
         MVC   QERRMSG+46(8),0(R5)
         MVC   QERRMSG+55(22),=C'MAY NOT HAVE VALUE ***'
         QTILT *,OPTIONS=(REPROMPT,SET)
PA$KWOK  NI    PA@FLAG,X'FF'-PA@VALUE   RESET "VALUE" FLAG
PA$MOVED LTR   R2,R2                    IS THE REMAINING LENGTH ZERO?
         BZ    PA$VALCK                 YES - VAL. CHECK KEYWDS ENTERED
         MVC   PA@FIELD,QBLANK          BLANK THE WORK FIELD
         MVC   P@OFFSET(4),=F'0'        ZERO OUT OFFSET AND LENGTH
         CR    R5,R6                    WAS THAT THE LAST FIELD?
         BL    PA$AGAIN                 NO. PROCESS NEXT FIELD.
         CLI   CMD@POS,X'FF'            IGNORE ERRORS ON POSIT. FIELDS?
         BE    PA$EXIST                 YES - NOTHING MORE I CAN DO
         B     PA$AGAIN                 PROCESS NEXT OPERAND TOKEN
*A$2MANY TM    PA@FLAG,PA@KEYS          ALREADY PROCESSING KEYWORDS?
*%%%     BO    PA$PMANY                 YES - ERROR
PA$2MANY CLI   CMD@POS,0                ANY POSITIONALS ALLOWED?
         BNE   PA$PMANY                 YES - HE ENTERED TOO MANY
         OI    PA@FLAG,PA@KEYS          NO - SO PROCESS KEYWORDS!
         LA    R5,QKEYWORD              -> FIRST SLOT FOR KEYWORDS
         LA    R6,QKEYLAST              -> PAST LAST SLOT
         B     PA$KW$RW                 GO SAVE THIS KEYWORD
PA$PMANY QTILT '*** TOO MANY POSITIONAL OPERANDS FOR SUBCOMMAND ***',  $
               OPTIONS=(REPROMPT,SET)
PA$KMANY QTILT '*** TOO MANY KEYWORD AND/OR RESERVED WORD OPERANDS ***'$
               ,OPTIONS=(REPROMPT,SET)
***********************************************************************
*                                                                     *
*   SUBROUTINE TO GET THE NEXT TOKEN FROM THE INPUT LINE              *
*                                                                     *
***********************************************************************
PA$TOKEN MVC   PA@FIELD,QBLANK          CLEAR THE WORK FIELD
         LA    R4,PA@FIELD              FIRST BYTE OF WORK FIELD
         MVI   PA@DELIM,C' '            INIT IN CASE RUN OFF END OF INP
PA$BLANK CLI   0(R3),C' '               FIND A NON BLANK YET?
         BE    PA$SKIP                  NO - KEEP LOOKING
         LTR   R5,R5                    DOING SUBCOMMAND NAME?
         BZ    PA$FIRST                 YES - BYPASS '/' TEST
         CLI   CMD@POS,X'FF'            IGNORE ERRORS IN POSIT OPRNDS?
         BE    PA$FIRST                 YES - BYPASS '/' TEST
         CLI   0(R3),C'/'               START OF RESERVED WORDS INDIC?
         BNE   PA$FIRST                 NO. START OF FIELD.
         TM    PA@FLAG,PA@KEYS          "END OF POSITS" ALREADY SET?
         BO    PA$DBL                   YES - SYNTAX ERROR
         OI    PA@FLAG,PA@KEYS          NO - SET IT NOW
         LA    R5,QKEYWORD              -> FIRST SLOT FOR KEYWORDS
         LA    R6,QKEYLAST              -> PAST LAST SLOT
PA$SKIP  LA    R3,1(,R3)                YES. SKIP IT.
         BCT   R2,PA$BLANK              TRY NEXT BYTE
         BR    R10                      RETURN TO "NOTHING FOUND"
PA$DBL   QTILT '*** SYNTAX ERROR - TWO SLASHES FOUND, OR SLASH AFTER KE$
               YWORD ***',OPTIONS=(REPROMPT,SET)
PA$FIRST LH    R1,QDRLNG                REPLY LENGTH
         SR    R1,R2                    COMPUTE OFFSET TO FIELD START
         STH   R1,P@OFFSET              STORE OFFSET
         LR    R1,R2                    SAVE COUNT OF REMAINING BYTES
         LTR   R5,R5                    DOING SUBCOMMAND NAME?
         BZ    PA$CHAR                  YES - skip this other stuff
         CLI   CMD@POS,X'FF'            IGNORE ERRORS IN POSIT OPERNDS?
         BNE   PA$CHAR                  NO - CONTINUE
         OC    QOFF1,QOFF1              ARE WE WORKING ON FIRST OPERND?
         BNZ   PA$CHAR                  NO - SKIP
         MVC   QOFF1,P@OFFSET           YES - SAVE ITS OFFSET
         B     PA$CHAR                  CONTINUE
         SPACE 1
PA$LOOP  CLI   0(R3),C' '               IS THIS BYTE BLANK?
         BE    PA$LAST                  YES. END OF FIELD.
         CLI   0(R3),C'='               START OF KEYWORD VALUE?
         BNE   PA$CHAR                  NO - MOVE THIS CHAR
         TM    PA@FLAG,PA@KEYS          ALREADY PROCESSING KEYWORDS?
         BO    PA$LAST                  YES - OK
         CLI   CMD@POS,X'FF'            IGNORE ERRORS IN POSIT OPERNDS?
         BE    PA$IGNOR                 YES - BAIL OUT
         OI    PA@FLAG,PA@KEYS          INDICATE NOW PROCESSING KEYWDS
         LA    R5,QKEYWORD              -> FIRST SLOT FOR KEYWORDS
         LA    R6,QKEYLAST              -> PAST LAST SLOT
         B     PA$LAST
PA$IGNOR OC    QOFF1,QOFF1              ARE WE WORKING ON FIRST OPERND?
         BNZ   PA$VALCK                 NO
         MVC   QOFF1,P@OFFSET           YES - SAVE ITS OFFSET
         B     PA$VALCK                 NO FURTHER OPERAND PROCESSING
PA$CHAR  MVC   0(1,R4),0(R3)            MOVE ONE BYTE OF FIELD
         LA    R3,1(,R3)                INCREMENT 'FROM'
         LA    R4,1(,R4)                INCREMENT 'TO'
         BCT   R2,PA$LOOP               TRY NEXT BYTE
         B     *+10     >=======+       DON'T SAVE DELIM IF RUN OFF END
PA$LAST  MVC   PA@DELIM,0(R3)   |       SAVE THE DELIM (BLANK OR SLASH)
         SR    R1,R2    <=======+       COMPUTE FIELD LENGTH
         CH    R1,=H'8'                 IS LENGTH GREATER THAN 8?
         BNH   *+8                      NO. USE IT.
         LA    R1,8                     YES. USE LENGTH OF EIGHT.
         STH   R1,P@LENGTH              STORE FIELD LENGTH
         B     4(,R10)                  RETURN TO CALLER
***********************************************************************
*                                                                     *
*   VALIDITY CHECK KEYWORDS AND RESERVED WORDS ENTERED                *
*                                                                     *
***********************************************************************
PA$VALCK CL    R12,QSUBCMD              POINT TO ME???
         BE    PA$PSWD                  YES - MUST BE "XP" SUBCOMMAND
         L     R2,CMD@KEYS              SUBCMD'S VALID KEYWORD TABLE
         LTR   R2,R2                    ANY?
         BZ    PA$VALNO                 NO - NONE ALLOWED
         LA    R3,QKEYWORD              -> FIRST ONE ENTERED
         LA    R4,QKEYLAST              -> PAST LAST ONE POSSIBLE
PA$VALL1 CLI   0(R3),C' '               ANY WORD HERE?
         BE    PA$FINAL                 NO - DONE HERE
         CLC   0(8,R3),0(R2)            YES - IS IT VALID?
         BE    PA$VALL2                 YES - CHECK NEXT
         LA    R2,8(,R2)                -> NEXT VALID KEYWORD
         CLI   0(R2),X'FF'              END OF TABLE?
         BNE   PA$VALL1                 NO - SEE IF IT'S THIS ONE
         MVC   QERRMSG(21),=C'*** INVALID KEYWORD -'
         MVC   QERRMSG+22(8),0(R3)
         MVC   QERRMSG+31(18),=C'- FOR SUBCOMMAND -'
         MVC   QERRMSG+50(8),QSUBNAME
         MVC   QERRMSG+59(3),=C'***'
         QTILT *,OPTIONS=(REPROMPT,SET)
PA$VALL2 LA    R3,16(,R3)               -> NEXT WORD TO CHECK
         CR    R3,R4                    DONE CHECKING?
         BNL   PA$FINAL                 YES - DONE
         L     R2,CMD@KEYS              -> VALID KEYWORD TABLE
         B     PA$VALL1                 CHECK NEXT KEYWORD
PA$VALNO CLI   QKEYWORD,C' '            ANY KEYWORDS OR RESERVED WORDS?
         BE    PA$FINAL                 NO - OK, DONE HERE
         MVC   QERRMSG(54),=C'*** KEYWORDS NOT ALLOWED FOR "XXXXXXXX" S$
               UBCOMMAND ***'
         MVC   QERRMSG+30(8),QSUBNAME
         QTILT *,OPTIONS=(REPROMPT,SET)
***********************************************************************
*                                                                     *
*   DO FINAL PROCESSING OF OPERANDS IF REQUIRED                       *
*                                                                     *
***********************************************************************
PA$FINAL NI    QFLAG2,255-Q2REPOS       ASSUME NOT REPOSITIONING SUBCMD
         TM    CMD@FLAG,P@R             IS IT?
         BNO   *+8                      NO - OK
         OI    QFLAG2,Q2REPOS           YES - SET FLAG
         TM    CMD@FLAG,P@J+P@A         IS PARM1 A <JOBID> ?
         BZ    PA$EXIST                 NO - SKIP
         TM    CMD@FLAG,P@V             SUBCMD REQUIRE ACCESS TO JOB?
         BNO   *+8                      NO - SKIP
         OI    QFLAG1,Q1VALJOB          YES - SET FLAG
         TM    CMD@FLAG,P@L             LIST TYPE SUBCMD?
         BNO   *+8                      NO - SKIP
         OI    QFLAG1,Q1LSTCMD          YES - FLAG FOR LATER
         CLC   =C'* ',QPARM1            REQUEST PREVIOUS JOB?
         BE    USE$PREV                 YES - GO RESTORE IT
         TM    CMD@FLAG,P@A             ACTION COMMAND?
         BO    P$ACTION                 YES - DON'T ALLOW BLANK JOBNAME
         CLC   QPARM1,QBLANK            ANOTHER WAY TO SELECT PREVIOUS?
         BE    USE$PREV                 YES - GO RESTORE IT
P$ACTION CLI   QLNG1+1,1                PARM LENGTH = 1?
         BNE   PA$EXIST                 NO - DONE
         IC    R15,QPARM1               YES - SAVE THE CHARACTER
         MVC   QPARM1(7),QLOGON         MOVE IN USERID
         SR    R1,R1                    CLEAR FOR IC
         IC    R1,QLOGONL               GET LENGTH OF USERID
         STC   R15,QPARM1(R1)           SET CHARACTER AT END OF USERID
         LA    R1,1(,R1)                BUMP LENGTH
         STH   R1,QLNG1                 SAVE NEW LENGTH
         B     PA$EXIST                 DONE
USE$PREV MVC   QLNG1(2+8),QJOBID        RESTORE PREVIOUS JOBID & LENGTH
         SPACE 2
PA$EXIST CLC   =F'0',CMD@ADDR           IS SUBCOMMAND LINKED IN?
         BNE   QSTOP                    YES - OK
         MVC   QERRMSG(50),=C'*** "XXXXXXXX" SUBCOMMAND IS NOT YET AVAI$
               LABLE ***'
         MVC   QERRMSG+5(8),QSUBNAME
         QTILT *,OPTIONS=(REPROMPT,SET)
***********************************************************************
*                                                                     *
*   PROMPT FOR SECRET PASSWORD ("XP" SUBCOMMAND)                      *
*                                                                     *
***********************************************************************
PA$PSWD  CLC   QPARM1,=C'PASSWORD'      DID THE USER SAY PASSWORD?
         BNE   PA$BOUNC                 NO. REJECT.
         SPACE 1
         TPUT  WPASS,L'WPASS,FULLSCR
         SPACE 1
         LA    R1,RPASS                 REPLY ADDRESS
         SPACE 1
         TGET  (1),8
         SPACE 1
         CLC   RPASS,=C'YES SIR!'       IS THE PASSWORD CORRECT?
         BNE   PA$BOUNC                 NO. REJECT.
         XI    QXAUTH,QXPSWD            AUTHORIZE USER (OR UNDO IT)
         QTILT '=== PASSWORD ACCEPTED ==='
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
WPASS    DC    X'C1115D7F1140403C4040001D4C13'  *** SPF TCAM
PA#ISPLK DC    CL8'ISPLINK'
         LTORG
         SPACE 2
WORK     DSECT
         ORG   WORK+72                  SKIP OVER SAVE AREA
PA@FLAG  DS    X
PA@KEYS  EQU   X'01'                    NOW PROCESSING KEYWORDS/RSRVD
PA@VALUE EQU   X'02'                    NOW PROCESSING VALUE
PA@DELIM DS    C
P@OFFSET DS    H
P@LENGTH DS    H
PA@FIELD DS    CL8
*---  BE CAREFUL PUTTING THINGS AFTER PA@FIELD BECAUSE THE TOKENIZER
*---  DOESN'T STOP AT 8 CHARACTERS IF THE USER ENTERS MORE
RPASS    DS    CL8
*PA@REPLY DS   CL(QDREPLYL)
PA@REPLY DS    CL66                     STUPID ASMF
         SPACE 2
PA@PARML DS    0A                       PARM LIST FOR ISPLINK CALL
PA@SERVP DS    A                        -> SERVICE NAME
PA@PARM1 DS    A                        -> 1ST PARM
PA@PARM2 DS    A                        -> 2ND PARM
PA@PARM3 DS    A                        -> 3RD PARM
PA@PARM4 DS    A                        -> 3RD PARM
         SPACE 1
PA@OPTLN DS    F                        LENGTH OF CMD LINE INPUT
PA@WORKL EQU   *-WORK                   length of PARSE's workarea
         SPACE 2
         DROP  R13,R9
         EJECT
QCMDE    DSECT
CMD@ADDR DS    A                        V-CON OF SUBROUTINE
CMD@KEYS DS    A                        V-CON OF KEYWORD/RSRVD WORD TBL
CMD@FLAG DS    B                        FLAGS
P@J       EQU   X'80'                    FIRST OPERAND IS JOBID
P@A       EQU   X'40'                    SAME AS ABOVE FOR ACTION CMD
*                                        (DO NOT ALLOW BLNK FOR ACTION)
P@I       EQU   X'20'                    RE-EXECUTABLE INQUIRY COMMAND
P@P       EQU   X'10'                    SUBCOMMAND IS PRIVILEGED
P@V       EQU   X'08'                    VERIFY ACCESS TO JOB
P@L       EQU   X'04'                    LIST TYPE SUBCMD ("READ ONLY")
P@X       EQU   X'02'                    EXEC THIS SUBCMD IMMEDIATELY
*                                        IN "DISPLAY" & RETURN TO PREV
P@R       EQU   X'01'                    REPOSITIONING SUBCOMMAND
CMD@POS  DS    AL1                      MAXIMUM NUMBER OF POSITIONALS
*                                       X'FF' = DON'T CHECK
CMD@CODE DS    AL1                      INTERNAL FUNC CODE FOR ROUTINE
CMD@NUM  DS    AL1                      NUMBER OF SUBCMD NAMES HERE
CMD@CMD  DS    CL4                      SUBCOMMAND NAME
*---  CMD@CMD OCCURS CMD@NUM TIMES
CMD@ENTL EQU   *-QCMDE                  LENGTH OF TABLE ENTRY
         SPACE 3
*---  FORMAT OF QCMDE ENTRIES:
*  ROUTINE_NAME  QCMDE  FUNC_CODE,MAX_POSIT_OPRNDS,(CMD,ALIAS1,...),
*                       F=FLAG1+FLAG2+...,KEYS=KEY_TABLE_NAME
         SPACE 2
PARSE    CSECT                          RESUME AFTER DSECT
PA#TABLE DC    0D'0'
         SPACE 2
SEARCH   QCMDE 00,1,(STATUS,ST),F=P@I,KEYS=ST#KEYS
SEARCH   QCMDE 04,0,DA,F=P@I,KEYS=DA#KEYS
SEARCH   QCMDE 08,2,DI,F=P@I,KEYS=DI#KEYS
SEARCH   QCMDE 12,2,DO,F=P@I,KEYS=DO#KEYS
SEARCH   QCMDE 16,2,AI,F=P@I,KEYS=DI#KEYS
SEARCH   QCMDE 20,2,AO,F=P@I,KEYS=DO#KEYS
SEARCH   QCMDE 24,2,HI,F=P@I,KEYS=DI#KEYS
SEARCH   QCMDE 28,0,DH,F=P@I,KEYS=DH#KEYS
SEARCH   QCMDE 32,0,DT,F=P@I,KEYS=DA#KEYS
SEARCH   QCMDE 36,1,DJ,F=P@I+P@J,KEYS=DJ#KEYS
SEARCH   QCMDE 36,1,XJ,F=P@J+P@P
SEARCH   QCMDE 40,0,DS,F=P@I,KEYS=DA#KEYS
SEARCH   QCMDE 44,1,DQ,F=P@I,KEYS=DQ#KEYS
SEARCH   QCMDE 48,0,(DEVICES,DEV),F=P@I
SEARCH   QCMDE 52,0,DN,F=P@I,KEYS=DN#KEYS
SEARCH   QCMDE 56,0,(NJE,NJ),F=P@I
FORMAT   QCMDE 100,1,XJQE,F=P@P
FORMAT   QCMDE 104,1,XJOE,F=P@P
CPU      QCMDE   ,2,(CPU,DC),F=P@I
SYSOUT   QCMDE 00,1,(DELETE,DEL),F=P@A+P@V
SYSOUT   QCMDE 04,2,(REQUEUE,REQ),F=P@A+P@V
SYSOUT   QCMDE 08,2,(CANCEL,CAN,CJ),F=P@A+P@V
JOBINFO  QCMDE 00,1,(JOB,JD),F=P@J+P@I
JOBINFO  QCMDE 04,1,(JOBX,JDX),F=P@J+P@I+P@P
DDNAME   QCMDE 00,1,DD,F=P@J+P@V+P@L
DDNAME   QCMDE 04,1,(SJ,S),F=P@J+P@V+P@L,KEYS=SJ#KEYS
LISTTEXT QCMDE   ,1,LTXT,F=P@J+P@V+P@L+P@P
LIST     QCMDE 00,3,(LIST,L,LI),F=P@J+P@V+P@L,KEYS=LI#KEYS
LIST     QCMDE 04,1,(JCL,JC),F=P@J+P@V+P@L,KEYS=LI#KEYS
LIST     QCMDE 08,1,(JLOG,JL),F=P@J,KEYS=LI#KEYS
LIST     QCMDE 12,1,(JMSG,JM),F=P@J+P@V+P@L,KEYS=LI#KEYS
LIST     QCMDE 16,0,NS,KEYS=NS#KEYS
LIST     QCMDE 16,0,PS
*%%%LIST QCMDE 16,1,LL
LIST     QCMDE 20,3,(ELIST,EL),F=P@J+P@V+P@L
ATTR     QCMDE 00,2,(ATTR,AT),F=P@J,KEYS=AT#KEYS
ATTR     QCMDE 04,2,PDDB,F=P@J+P@P
JOBSUMM  QCMDE   ,1,(JS,J,JSUMM),F=P@J
JESNEWS  QCMDE 00,0,(JNEWS,JN)
SAVEDS   QCMDE 04,1,SAVE,KEYS=SV#KEYS
SYSLOG   QCMDE 00,2,(SLOG,SL),F=P@P
INITS    QCMDE   ,0,(INIT,IN),F=P@I,KEYS=IN#KEYS
RMTLIST  QCMDE   ,0,LRMT,F=P@I
SYSLIST  QCMDE   ,0,LSYS,F=P@I+P@P
BLKLIST  QCMDE   ,1,LBLK,F=P@I+P@P+P@J
LISTDAS  QCMDE   ,0,LDAS,F=P@P
LISTKIT  QCMDE   ,0,LKIT,F=P@P
HEXBLK   QCMDE 00,2,(XBLK,XB),F=P@P,KEYS=HB#KEYS
HEXBLK   QCMDE 04,2,XJCT,F=P@J+P@P
HEXBLK   QCMDE 08,1,XHCT,F=P@P
HEXBLK   QCMDE 12,1,XQCMN,F=P@P
HEXBLK   QCMDE 16,1,XCKPT,F=P@P
CBFMT    QCMDE 00,3,CB,F=P@P+P@J,KEYS=CB#KEYS
CBFMT    QCMDE 04,1,QCMN,F=P@P,KEYS=CB#KEYS
CBFMT    QCMDE 08,1,CKPT,F=P@P,KEYS=CB#KEYS
CBFMT    QCMDE 12,1,HCT,F=P@P,KEYS=CB#KEYS
CBFMT    QCMDE 16,0,JOT,F=P@P,KEYS=CB#KEYS
CBFMT    QCMDE 20,2,JCT,F=P@P,KEYS=CB#KEYS
CBFMT    QCMDE 24,1,JQE,F=P@P,KEYS=CB#KEYS
CBFMT    QCMDE 28,1,JOE,F=P@P,KEYS=CB#KEYS
REPOS    QCMDE 04,N,(FIND,F)
REPOS    QCMDE 04,N,(FALL,FA)
REPOS    QCMDE 08,1,(FTIME,FT)
REPOS    QCMDE 12,1,(COLUMN,COL,C),F=P@R
REPOS    QCMDE 16,1,(@,MD)
REPOS    QCMDE 20,1,(+,DOWN,D),F=P@R
REPOS    QCMDE 44,1,PF,F=P@R
REPOS    QCMDE 48,1,HF,F=P@R
REPOS    QCMDE 24,1,(-,UP,U),F=P@R
REPOS    QCMDE 52,1,PB,F=P@R
REPOS    QCMDE 56,1,HB,F=P@R
REPOS    QCMDE 28,0,(TOP,T),F=P@R
REPOS    QCMDE 32,0,(BOTTOM,BOT,B),F=P@R
REPOS    QCMDE 36,1,(LEFT,LE),F=P@R
REPOS    QCMDE 40,1,(RIGHT,RI),F=P@R
REPOS    QCMDE 60,0,(REDISP,RD)
REPOS    QCMDE 64,1,(PAGE,P),F=P@R
          AIF   (NOT &@@TMPSV).PARSE01
TSOCMD   QCMDE   ,N,TSO,F=P@X
.PARSE01  ANOP
MCS      QCMDE   ,1,(CONS,MCS),F=P@P+P@I
MODE     QCMDE 00,0,MODE,F=P@X,KEYS=MO#KEYS
MODE     QCMDE 04,0,COLS,KEYS=COL#KEYS
QINNARDS QCMDE   ,0,QINT,F=P@X,KEYS=QI#KEYS
PRINT    QCMDE 00,2,(PRINT,PR),F=P@X,KEYS=PR#KEYS
PRINT    QCMDE 04,2,(COPY,CY),F=P@X,KEYS=PR#KEYS
PRINT    QCMDE 08,2,SPIN,F=P@X,KEYS=PR#KEYS
SETUP    QCMDE   ,1,(SETUP,SU),F=P@J
OPERCMD  QCMDE 12,2,(ROUTE,ROU,RJ),F=P@V+P@A
OPERCMD  QCMDE 16,1,(HOLD,HJ),F=P@P+P@A
OPERCMD  QCMDE 20,1,(RELEASE,REL,AJ),F=P@P+P@A
OPERCMD  QCMDE 24,1,(PURGE,PUR,PJ),F=P@V+P@A
OPERCMD  QCMDE 32,1,TJ,F=P@P+P@A,KEYS=TJ#KEYS
OPERCMD  QCMDE 36,1,EJ,F=P@P+P@A
PA#CMDX  EQU   *
OPERCMD  QCMDE 28,N,//,F=P@P+P@X
OPERCMD  QCMDE 28,N,||,F=P@P+P@X
TGPS     QCMDE   ,0,(SPSP,TGPS),F=P@P
PFKSET   QCMDE   ,N,PFK,F=P@X
HELP     QCMDE   ,1,(HELP,H,?),KEYS=HE#KEYS
CHANGES  QCMDE   ,0,CHGS,KEYS=CH#KEYS
PA#EXIT  EQU   *
EXIT     QCMDE 04,N,(END,E)
EXIT     QCMDE 00,0,(EXIT,STOP,QUIT,RETURN,RET,=X)
RECALL   QCMDE   ,0,(RECALL,RC,RETRIEVE,RE),F=P@X,KEYS=RC#KEYS
SPFFUNC  QCMDE 00,1,(ED,EDIT),F=P@X,KEYS=SF#KEYS
SPFFUNC  QCMDE 04,1,(BR,BROWSE),F=P@X,KEYS=SF#KEYS
ABEND    QCMDE   ,0,ABEND
PARSE    QCMDE   ,1,XP
         DC    X'FF000000'              END OF COMMAND TABLE
         SPACE 2
*---  SPECIAL USE ENTRIES
PA#CMD   EQU   *
OPERCMD  QCMDE 00,N,/,F=P@P+P@X
PA#CMDQ  EQU   *
OPERCMD  QCMDE 04,N,|,F=P@P+P@X
PA#CMDJ  EQU   *
OPERCMD  QCMDE 08,N,$,F=P@P+P@X
         EJECT
*---  LINE COMMAND TABLES                                          ---*
*---  FORMAT IS DESCRIBED BY LCTABLE DSECT IN QCOMMON MACRO        ---*
         SPACE 1
*---  TABLE FOR 'DD' DISPLAY SCREEN                                ---*
         ENTRY PA#LC#DD
PA#LC#DD DC    C'L ',C'LIST',AL1(LCTFDD+LCTFDCMD)
         DC    C'S ',C'LIST',AL1(LCTFDD+LCTFDCMD)
**??%%%  DC    C'? ',C'LIST',AL1(LCTFDD+LCTFDCMD)
         DC    C'EL',C'ELIS',AL1(LCTFDD+LCTFDCMD)
         DC    C'A ',C'ATTR',AL1(LCTFDD+LCTFDCMD)
         DC    C'AT',C'ATTR',AL1(LCTFDD+LCTFDCMD)
         DC    X'FF'                    END OF TABLE
         SPACE 2
*---  TABLE FOR 'FORMAT' DISPLAY SCREEN                            ---*
         ENTRY PA#LC#FM
PA#LC#FM DC    C'DJ',C'DJ  ',AL1(LCTFDCMD)
         DC    C'XJ',C'XJ  ',AL1(LCTFDCMD+LCTFPRIV)
         DC    C'JS',C'JS  ',AL1(LCTFDCMD)
         DC    C'J ',C'JS  ',AL1(LCTFDCMD)
         DC    C'DE',C'DEL ',AL1(0)
         DC    C'DL',C'DEL ',AL1(0)
         DC    C'RE',C'REQ ',AL1(0)
         DC    C'RQ',C'REQ ',AL1(0)
         DC    C'CA',C'CAN ',AL1(0)
         DC    C'CJ',C'CAN ',AL1(0)
         DC    C'JD',C'JOB ',AL1(LCTFDCMD)
         DC    C'JX',C'JOBX',AL1(LCTFDCMD+LCTFPRIV)
         DC    C'SJ',C'SJ  ',AL1(LCTFDCMD)
         DC    C'S ',C'SJ  ',AL1(LCTFDCMD)
         DC    C'DD',C'DD  ',AL1(LCTFDCMD)
         DC    C'? ',C'DD  ',AL1(LCTFDCMD)
         DC    C'LT',C'LTXT',AL1(LCTFDCMD+LCTFPRIV)
         DC    C'L ',C'LIST',AL1(LCTFDCMD)
         DC    C'JC',C'JCL ',AL1(LCTFDCMD)
         DC    C'JL',C'JLOG',AL1(LCTFDCMD)
         DC    C'JM',C'JMSG',AL1(LCTFDCMD)
         DC    C'SL',C'SLOG',AL1(LCTFDCMD)
         DC    C'LB',C'LBLK',AL1(LCTFDCMD+LCTFPRIV)
         DC    C'JT',C'JCT ',AL1(LCTFDCMD+LCTFPRIV)
         DC    C'SU',C'SETU',AL1(LCTFDCMD)
         DC    C'R ',C'RJ  ',AL1(0)
         DC    C'RJ',C'RJ  ',AL1(0)
         DC    C'H ',C'HJ  ',AL1(0)
         DC    C'HJ',C'HJ  ',AL1(0)
         DC    C'A ',C'AJ  ',AL1(0)
         DC    C'AJ',C'AJ  ',AL1(0)
         DC    C'T ',C'TJ  ',AL1(0)
         DC    C'TJ',C'TJ  ',AL1(0)
         DC    C'PJ',C'PJ  ',AL1(0)
         DC    C'EJ',C'EJ  ',AL1(0)
         DC    X'FF'                    END OF TABLE
         SPACE 2
         QCOMMON
