         TITLE '--- QUEUE--FORMATING SUBROUTINES ---'
*---------------------------------------------------------------------*
*                                                                     *
*   QUEUE FORMATING SUBROUTINES                                       *
*                                                                     *
*   QSUBS - Processing interface for QITEM macro                      *
*                                                                     *
*   HEXFMT - GENERATE HEX DUMP OF ANY DATA                            *
*                                                                     *
*   ENTRY - R0 = DISPLAY OFFSET OF FIRST BYTE                         *
*         - R1 = ADDR OF DATA TO DUMP                                 *
*         - R2 = LENGTH TO DUMP                                       *
*                                                                     *
*   UPDATES:                                                          *
*      04Mar88  EMS  Fix turkey bug in leap-year calculation          *
*                    Integrate DATEFMT into QSUBS (remove epa)        *
*      15Jun87  EMS  rename to QSUBS from FMTSUBS                     *
*                    Add HEXFMT as entrypoint to QSUBS                *
*                    Update for new QITEM macro                       *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
QSUBS    QSTART  WORKLEN=QS@WORKL
         USNGX WORK,R13                 Base for local work area
         MVI   QS@FLAGS,0               clear flags
         MVC   QS@PMSG#,0(R14)          save id
         L     R7,QCITEMS               -> item table
         XR    R15,R15                  clear
         LH    R0,QS@PMSG#              get msg no
         SH    R0,=H'1'                 -1 (table origin 1)
         BZ    QS$O1SET                 if zero, at entry
QS$O1LP  IC    R15,0(,R7)               get entry len
         AR    R7,R15                   -> next entry
         BCT   R0,QS$O1LP               walk to our entry
QS$O1SET MVC   QS@PEC(QS@PL),1(R7)      copy parms to safe place
         SR    R4,R4                    clear for ic
         IC    R4,QS@PEC                get entry code
         CH    R4,=H'4'                 over max?
         BH    QS$INVC                  yes, error
         SLL   R4,2                     * 4 for branch table
         SR    R6,R6                    clear
         ICM   R6,B'0011',QS@POFF1      get offset 1
         LA    R2,QCOMMON(R6)           -> target
         ICM   R6,B'0011',QS@POFF2      get offset 2
         LA    R3,QCOMMON(R6)           -> target
         SR    R6,R6                    clear
         IC    R6,QS@PLEN               get data len
         TM    QS@PFLAG,QS@PFMSG        msg insert?
         BO    QS$FUNC                  no, just do conversion
         MVC   0(2,R2),=X'1DE8'         set first attr byte
         IC    R15,0(,R7)               get entry len
         SH    R15,=Y(QS@PL+2)          -8 (fixed len;-1 for ex)
         EX    R15,QS#OMVC              move in data
         LA    R2,2+1(R15,R2)           -> next attr bytes
         MVC   0(2,R2),=X'1D60'         move in second set
         LA    R5,2(,R5)                account for bytes
QS$FUNC  LR    R2,R1                    copy source addr
         B     *+4(R4)                  do function
         B     QS$CHAR                  + 00
         B     QS$HEX                   + 04
         B     QS$TIME                  + 08
         B     QS$DATE                  + 12
         B     QS$DEC                   + 16
         SPACE 1
QS$CHAR  BCTR  R6,0                     -1 for len to move
         EX    R6,QS#COC                anything here?
         BZ    QS$EXIT                  no, leave
         EX    R6,QS#CMVC               move data
         L     R1,=V(CHARTAB)           -> chartab
         EX    R6,QS#CTR                printable
         B     QS$EXIT                  -> exit
         SPACE 2
QS$HEX   LR    R14,R6                   copy len
         AR    R14,R14                  * 2
         SLL   R14,4                    shift
         OR    R14,R6                   combine for unpk
         EX    R14,QS#HUNPK             do it
         AR    R6,R6                    len * 2
         BCTR  R6,0                     -1 for ex
         EX    R6,QS#HTR                translate to char
         LA    R3,1(R6,R3)              -> last byte
         MVI   0(R3),C' '               clear
         B     QS$EXIT
         SPACE 2
QS$DEC   XR    R1,R1                    clear
         SLL   R6,2                     *2 for instruction offset
         EX    0,QS#DIC(R6)             (get correct load instr)
         CVD   R1,QS@TEMP               decimal
         LA    R1,QS@WORK+L'QS@WORK-1   -> end of string
         MVC   QS@WORK(15),QS#MASK      set edit mask
         MVC   QS@WORK+15(15),QBLANK    clear junk
         EDMK  QS@WORK(15),QS@TEMP+2    format; set r1
         MVC   0(13,R3),0(R1)           move to output
         B     QS$EXIT
         SPACE 2
QS$DATE  LTR   R2,R2                    any value?
         BNZ   QS$DGET                  yes, use it
         TIME  BIN                      get current time/date
         ST    R1,QS@PDATE              Save current Jdate
         B     QS$DGO                   call conv routine
QS$DGET  MVC   QS@PDATE,0(R2)           Save packed input Jdate
         OC    QS@PDATE,QS@PDATE        check for valid entry
         BZ    QS$EXIT                  if none, exit
         OI    QS@FLAGS,QS@FDATE        indicate valid date field
         SPACE 2
QS$DGO   MVC   QS@MNTHS(QS#DMTHL),QS#DMTHS  Copy days-in-month table
         OI    QS@PDATE+3,X'0F'         Remove sign
         UNPK  QS@WORK+1(5),QS@PDATE    Get ' yyddd'
         MVC   QS@WORK(2),QS@WORK+1     Get 'yyyddd'
         MVI   QS@WORK+2,C'.'           Make it 'yy.ddd'
         MVI   QS@WORK+6,C' '           Blank the last character
         TM    QMODE1,QM1JDATE          Want Julian format dates?
         BO    QS$DEXIT                 Yes - all done
         SPACE 3
         TM    QS@PDATE+1,X'01'         Leap year?
         BO    QS$DNLEP                 No (odd year) - not leap
         TM    QS@PDATE+1,X'12'         Divisible by 4?
         BM    QS$DNLEP                 No - not leap year
         MVI   QS@MNTHS+7,29            Feb has 29 days this year
         SPACE 1
QS$DNLEP MVC   QS@TEMP+4(4),QS@PDATE    Copy 00yydddf
         XC    QS@TEMP(6),QS@TEMP       Clear beginning, leave 0000dddf
         CVB   R0,QS@TEMP               Get day of year in binary
         LA    R15,QS@MNTHS             Point to table
         SPACE 1
QS$DLP   SH    R0,0(,R15)               Sub number of days this month
         BNP   QS$DHER                  Have month
         LA    R15,6(,R15)              Next table entry
         B     QS$DLP                   Keep looking
         SPACE 1
QS$DHER  MVC   QS@WORK+2(3),2(R15)      Move name of month
         AH    R0,0(,R15)               Get back correct residual
         CVD   R0,QS@TEMP
         OI    QS@TEMP+7,X'0F'          Remove sign
         UNPK  QS@WORK+5(2),QS@TEMP+6(2)  Day of month
         SPACE 1
QS$DEXIT MVC   0(7,R3),QS@WORK          move to output
         B     QS$EXIT
         SPACE 2
QS$TIME  L     R1,0(,R2)                get time
         XR    R0,R0
         D     R0,=F'100'               r0=hh;r1=secs
         SR    R0,R0
         D     R0,=F'60'                r0=secs;r1=min
         CVD   R0,QS@TEMP
         OI    QS@TEMP+7,X'0F'
         UNPK  QS@WORK+6(2),QS@TEMP     set secs
         MVI   QS@WORK+5,C':'
         SR    R0,R0
         D     R0,=F'60'                r0=min;r1=hours
         CVD   R0,QS@TEMP
         OI    QS@TEMP+7,X'0F'
         UNPK  QS@WORK+3(2),QS@TEMP
         MVI   QS@WORK+2,C':'
         CVD   R1,QS@TEMP               save hours
         OI    QS@TEMP+7,X'0F'
         UNPK  QS@WORK(2),QS@TEMP
         MVC   0(8,R3),QS@WORK
         B     QS$EXIT
         SPACE 3
QS$EXIT  L     R15,4(,R13)              -> prev regs
         L     R14,12(,R15)             restore r14
         LA    R14,2(,R14)              normal offset
         TM    QS@PFLAG,QS@PSKIP        trailing branch present?
         BZ    *+16                     no, leave it
         TM    QS@FLAGS,QS@FDATE        valid data?
         BZ    *+8                      no, leave it
         LA    R14,4(,R14)              yes, leapfrog it
         ST    R14,12(,R15)             save it for qstop
         ST    R5,40(,R15)              save new r5
         B     QSTOP
         EJECT ,
HEXFMT   QSTART  TYPE=ENTRY
         LA    R6,1(,R12)               PHONY UP A "NO MATCH" FOR PREV
         LR    R3,R1                    SAVE ADDR
         LR    R5,R0                    SAVE OFFSET
         LA    R4,16                    HANDY CONSTANT
         L     R8,=V(CHARTAB)           -> CHARACTER TRANSLATE TABLE
         QCALL DISP80                   SWITCH TO 3278-2 MODE IF 3278-5
         SPACE 2
HF$LOOP  CR    R2,R4                    > 16 BYTES LEFT?
         BNH   HF$SHORT                 NO
         CLC   0(16,R3),0(R6)           SAME AS PREVIOUS LINE?
         BE    HF$NEXT                  YES - SKIP IT
         LR    R6,R3                    SAVE ADDRESS OF THIS DATA
         MVC   HF@DATA(16),0(R3)        SAVE DATA
         B     HF$CONT                  CONTINUE
         SPACE 1
HF$MVC   MVC   HF@DATA(*-*),0(R3)       << EXECUTED >>
HF$SHORT LR    R14,R2                   COPY LENGTH
         BCTR  R14,0                    -1 FOR EX
         EX    R14,HF$MVC               SAVE THE DATA
         SPACE 1
HF$CONT  UNPK  HF@HEX+00(14+1),HF@DATA+00(7+1)
         UNPK  HF@HEX+14(14+1),HF@DATA+07(7+1)
         UNPK  HF@HEX+28(04+1),HF@DATA+14(2+1)
         TR    HF@HEX(32),QHEXTAB       CONVERT TO DISPLAYABLE HEX
         TR    HF@DATA(16),0(R8)        TRANSLATE OUT UNPRINTABLES
         CR    R2,R4                    < 16 BYTES ON THIS LINE?
         BNL   HF$DISP                  NO - GO DISPLAY IT
*---  MUST BLANK TAIL END
         LA    R15,HF@DATA(R2)          -> DATA TO BLANK
         MVC   0(16,R15),QBLANK         BLANK OUT NON-EXISTANT DATA
         LA    R15,HF@HEX(R2)
         AR    R15,R2                   -> CHARS TO BLANK
         MVC   0(32,R15),QBLANK         ...
         SPACE 1
HF$DISP  ST    R5,HF@WORK               STORE OFFSET
         UNPK  QDMSG(6+1),HF@WORK+1(3+1)
         TR    QDMSG(6),QHEXTAB         CONVERT OFFSET TO PRINTABLE
         MVI   QDMSG+6,C' '             REMOVE FLIP BYTE
         MVC   QDMSG+09(8),HF@HEX+00
         MVC   QDMSG+18(8),HF@HEX+08
         MVC   QDMSG+28(8),HF@HEX+16
         MVC   QDMSG+37(8),HF@HEX+24
         MVI   QDMSG+47,C'|'
         MVC   QDMSG+48(8),HF@DATA+0
         MVC   QDMSG+57(8),HF@DATA+8
         MVI   QDMSG+65,C'|'
         BAL   R14,QADDLINE
HF$NEXT  AR    R5,R4                    BUMP OFFSET
         AR    R3,R4                    BUMP ADDR
         SR    R2,R4                    DECR LENGTH
         BP    HF$LOOP                  CONTINUE IF MORE
         B     QSTOP
         SPACE 3
QS$INVC  QTILT '*** INVALID/UNSUPPORTED CALL ***'
         SPACE 3
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         SPACE 2
QS#OMVC  MVC   2(0,R2),QS@PL+1(R7)
QS#COC   OC    0(0,R2),0(R2)
QS#CMVC  MVC   0(0,R3),0(R2)
QS#CTR   TR    0(0,R3),0(R1)
QS#HUNPK UNPK  0(0,R3),0(0,R2)
QS#HTR   TR    0(0,R3),QHEXTAB
QS#DIC   IC    R1,0(,R2)   **ORDER**
         LH    R1,0(,R2)   **ORDER**
         L     R1,0(,R2)   **ORDER**
         SPACE 3
         LTORG ,
QS#MASK  DC    X'402020',C',',X'202020',C',',X'202020',C',',X'202120'
QS#MASKL EQU   *-QS#MASK
         SPACE 3
QS#DMTHS MONTHS   LC=YES
QS#DMTHL EQU   *-QS#DMTHS
         SPACE 3
WORK     DSECT
         ORG   WORK+72                  SKIP OVER SAVE AREA
         SPACE 1
QS@TEMP  DS    D
QS@PDATE DS    PL4
QS@MNTHS DS    0F,CL(QS#DMTHL)
QS@PMSG# DS    H
QS@PEC   DS    AL1                      entry code
QS@PFLAG DS    X'00'                    parm flags
QS@PFMSG EQU   X'80'                    no msg to insert
QS@PFDAT EQU   X'40'                    no data to move
QS@PSKIP EQU   X'20'                    branch at end of call
QS@PLEN  DS    AL1                      len of field 2
QS@POFF1 DS    AL2                      offset for field 1
QS@POFF2 DS    AL2                      offset for field 2
QS@PL    EQU   *-QS@PEC
QS@FLAGS DS    X'00'                    misc flags
QS@FDATE EQU   X'80'                    date present
QS@WORK  DS    2CL(QS#MASKL)
QS@WORKL EQU   *-WORK
HF@WORK  DS    F
HF@DATA  DS    CL16
         DS    CL16                     PAD FOR BLANKING PARTIAL DATA
HF@HEX   DS    CL32
         DS    CL32                     PAD FOR BLANKING PARTIAL DATA
         SPACE 3
         QCOMMON
         SPACE 3
         DROPX R13                      WORK
