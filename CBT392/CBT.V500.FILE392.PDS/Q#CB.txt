         TITLE '--- QUEUE--CBFMT -- FORMAT JES2 CONTROL BLOCKS ---'
*---------------------------------------------------------------------*
*                                                                     *
*   CBFMT -  Format a JES2 control block                              *
*                                                                     *
*   Note - All subcommands are restrict ed to privileged users        *
*                                                                     *
*   Updates:                                                          *
*      02Dec90  LDW  Created by copying "DUMP" routine from an old    *
*                       copy of a different version of QUEUE          *
*                       (Actually, this is a major rewrite, so *DMS*  *
*                       flags have been removed)                      *
*                    Use CHARTAB (in LISTDS) instead of TBL1,         *
*                       QHEXTAB instead of TBL                        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
CBFMT    QSTART  ,WORKLEN=CB@WORKL      ,stack=x'??'
         USNGX WORK,R13
         XC    DALLREG,DALLREG          initialize
         LH    R3,QCODEH                get subcommand code
         LTR   R3,R3                    is it CB subcommand?
         BZ    CB$CB                    yes - go handle it
         QCALL CKPT                     read checkpoint data
         B     *(R3)                    go to routine
         EX    0,*    B  CB$QCMN        04 - QCMN
         B     CB$CKPT                  08 - CKPT
         B     CB$HCT                   12 - HCT
         B     CB$JOT                   16 - JOT
         EX    0,*    B  DIRJCT         20 - JCT
         B     DIRJQE                   24 - JQE
         B     DIRJOE                   28 - JOE
***********************************************************************
*                                                                     *
*   Call FINDJOB to locate JQE, JCT, and IOT                          *
*                                                                     *
***********************************************************************
CB$CB    QCALL FINDJOB                  routine to find job
         OI    QSCFLAG1,QSC1JOB         put jobid in QDTOP
         EJECT
***********************************************************************
*                                                                     *
*   determine the control block to be formatted                       *
*                                                                     *
***********************************************************************
         CLC   =C'ALL ',QPARM2          format all CBs for a job?
         BE    CB$JALL                  yes. go process
         CLC   =C'JQE ',QPARM2          dump the jqe?
         BE    CB$JQE                   yes. go process
         CLC   =C'JCT ',QPARM2          dump the jct?
         BE    CB$JCT                   yes. go process
         CLC   =C'JOE ',QPARM2          dump the joe?
         BE    CB$JOE                   yes. go process
         CLC   =C'IOT ',QPARM2          dump the iot?
         BE    CB$IOT                   yes. go process
         CLC   =C'PDDB ',QPARM2         dump the pddb?
         BE    CB$PDDB                  yes. go process
          AIF   (&@@ACF2 EQ 0).CB010
         CLC   =C'ACF2 ',QPARM2         dump the acf2 njh?
         BE    CB$ACF2                  yes. go process
.CB010    ANOP
         MVC   QERRMSG(49),=C'*** "        " IS AN UNKNOWN CONTROL BLOC$
               K ID ***'
         MVC   QERRMSG+5(8),QPARM2      fill in cb id
         QTILT *,OPTIONS=REPROMPT
         B     QSTOP
***********************************************************************
*                                                                     *
*   dump all the control blocks for the job                           *
*                                                                     *
***********************************************************************
CB$JALL  LA    R2,CB$FIRST              -> first cb to be formatted
         ST    R2,DALLREG               save address
CB$FIRST B     CB$JQE                   dump JQE
         B     CB$JCT                   dump JCT
          AIF   (&@@ACF2 EQ 0).CB020
         B     CB$ACF2                  dump acf2 njh
.CB020    ANOP
         B     CB$JOE                   dump JOE
         B     CB$IOT                   dump IOT
         B     CB$PDDB                  dump PDDB
         B     QSTOP                    exit
***********************************************************************
*                                                                     *
*   format the jqe                                                    *
*                                                                     *
***********************************************************************
CB$JQE   MVC   QDHLINE,QBLANK           clear the header line
         MVC   QDHLINE(15),=C'===  J Q E  ==='
         L     R2,=A(CB#D#JQE)          a(jqe description)
         L     R4,QCJQEA                addr of input data
         BAL   R3,CB$FMT                go format the jqe
         B     CB$DONE                  exit
***********************************************************************
*                                                                     *
*   format the joe                                                    *
*                                                                     *
***********************************************************************
CB$JOE   MVC   QDHLINE,QBLANK           clear the header line
         MVC   QDHLINE(15),=C'===  J O E  ==='
         L     R1,QCJQEA                a(jqe)
*%       USNGX JQEDSECT,R1              base reg for jqe
         QNEXT R4,JQEJOE-JQE(R1)        joe offset
*%       DROPX R1
CB$JOELP LTR   R4,R4                    ?/joe present
         BZ    CB$DONE                  no. exit
         MVC   QDMSG(21),=C'***  Work  J O E  ***'
         ST    R4,QNUMWORK              set offset
         HEX   QDMSG+22,QNUMWORK+1,LEN=3,HEXTAB=QHEXTAB
         BAL   R14,QADDLINE             add line to screen buffer
         L     R1,QCJOTA                a(jot)
         AR    R4,R1                    a(joe)
         ST    R4,JOEADDR               save joe addr
         L     R2,=A(CB#DWJOE)          a(work joe description)
         BAL   R3,CB$FMT                branch to format routine
*%       MVC   QDMSG,QBLANK             blank out the message area
         BAL   R14,QADDLINE             add blank line to screen buffer
         L     R4,JOEADDR               restore joe addr
         USNGX JOEDSECT,R4              base reg for joe
         QNEXT R5,JOECHAR,NONE=CB$NCHAR get address of char joe
         MVC   QDMSG(21),=C'***  Char  J O E  ***'
         ST    R5,QNUMWORK              set offset
         HEX   QDMSG+22,QNUMWORK+1,LEN=3
         BAL   R14,QADDLINE             add line to screen buffer
         A     R5,QCJOTA                add offset
         LR    R4,R5                    r4 - char joe offset
         L     R2,=A(CB#DCJOE)          a(char joe description)
         BAL   R3,CB$FMT                branch to format routine
*%       MVC   QDMSG,QBLANK             blank out the message area
         BAL   R14,QADDLINE             add blank line to screen buffer
         L     R4,JOEADDR               restore joe addr
CB$NCHAR QNEXT R5,JOEJQNXT              get next joe offset
         LR    R4,R5                    r4 - work joe offset
         B     CB$JOELP                 go thru the cycle again
         DROPX R4
         EJECT
***********************************************************************
*                                                                     *
*   format the jct                                                    *
*                                                                     *
***********************************************************************
CB$JCT   MVC   QDHLINE,QBLANK           clear the header line
         MVC   QDHLINE(15),=C'===  J C T  ==='
         LA    R2,CB@WDJCT              load address of work desc
         L     R3,=A(CB#JCTDL)          load length  of jct  desc
         L     R4,=A(CB#D#JCT)          load address of jct  desc
         LR    R5,R3                    load length again
         MVCL  R2,R4                    move desc to working storage
         L     R4,QCJCTA                a(input data)
         USNGX JCT,R4                   temp addressability to jct
         CLI   JCTJOBID,C'J'            is this a job?
         BE    CB$J                     yes  no change necessary
         LA    R2,CB@WDJCT              get address
         NI    PRTYTYP(R2),X'FF'-X'80'  don't display as character
         DROPX R4                       drop addressability
CB$J     DS    0H
         LA    R2,CB@WDJCT              a(jct description)
         L     R4,QCJCTA                a(input data)
         BAL   R3,CB$FMT                go format the data
         B     CB$DONE                  exit
         EJECT
          AIF   (&@@ACF2 EQ 0).CB030
******************************************************************
*
*   format the acf2 jetwork job header.
*
******************************************************************
CB$ACF2  MVC   QDHLINE,QBLANK           clear the header line
         MVC   QDHLINE(27),=C'===  a c f 2     N J H  ==='
         L     R2,=A(CB#D#ACF)          a(njh description)
         L     R4,QCJCTA                a(input data)
         USNGX JCT,R4                   temp address to jct
*
*   find the acf2 nhd (job network header).
*   code taken from acf2 distributed source (acf2a1).
*
         LA    R8,JCTNJHDR              addr network job header
         LH    R15,0(,R8)               get length of block
         SH    R15,=H'4'                subtract the prefix
         LA    R8,4(,R8)                point to first section
CB$A2NJL CLC   2(6,R8),CB#A2ID          acf2 section?
         BE    CB$A2FND                 br if yes
         SH    R15,NJHGLEN-NJHG(,R8)    subtract length of sect
         BNP   CB$NACF2                 no more rec - not acf job
         AH    R8,NJHGLEN-NJHG(,R8)     bump to next section
         B     CB$A2NJL
         SPACE 1
CB#A2ID  DC    B'11100000'
         DC    B'00000001'
         DC    CL4'ACF2'
         SPACE 1
         DROPX R4
CB$A2FND DS    0H
         LA    R4,ACFNJSEC-ACFNJBLK(,R8)    point after garbage
         BAL   R3,CB$FMT                go format the data
CB$NACF2 DS    0H
         B     CB$DONE                  exit
.CB030    ANOP
***********************************************************************
*                                                                     *
*   format the iot                                                    *
*                                                                     *
***********************************************************************
CB$IOT   MVC   QDHLINE,QBLANK           clear the header line
         MVC   QDHLINE(15),=C'===  I O T  ==='
         MVI   CB@SW,0
         L     R4,QCIOTA                a(input data)
         LTR   R4,R4                    ?/iot valid
         BZ    CB$DONE                  no. exit
CB$IOTLP ST    R4,IOTADDR               yes. save for later use
         L     R2,=A(CB#D#IOT)          a(iot description)
         BAL   R3,CB$FMT                go format the data
         MVC   QDMSG,QBLANK             blank out the message area
         BAL   R8,CB$DISP1              go display the blank line
         L     R4,IOTADDR               restore iot addr
         L     R4,IOTIOTTR-IOT(,R4)     get addr of next iot
CB$IOT$2 LTR   R4,R4                    ?/next iot present
         BZ    CB$SPIOT                 no. check spin iot's
         BAL   R8,READ                  yes. read the iot in
         B     CB$IOTLP
CB$SPIOT TM    CB@SW,1                  ?/spin iot already searched
         BO    CB$DONE                  yes. exit
         OI    CB@SW,1                  no. indicate now being searched
         L     R4,QCJCTA                a(jct)
         L     R4,12(R4)                get addr of spin iot
         B     CB$IOT$2                 go verify the iot is valid
         EJECT
***********************************************************************
*                                                                     *
*   format the pddb                                                   *
*                                                                     *
***********************************************************************
CB$PDDB  MVC   QDHLINE,QBLANK           clear the header line
         MVC   QDHLINE(17),=C'===  P D D B  ==='
         MVI   CB@SW,0                  initialize switch
         MVC   PDDBKEY,=H'0'            initialize dsid key
         USNGX PDBDSECT,R2              base reg for pddb
         USNGX IOTDSECT,R3              base reg for iot
         CLI   QPARM3,C' '              was a specific pddb requested?
         BNE   PDDBS                    yes  go get it
SPDDB    L     R3,QCIOTA                load base reg
         LR    R4,R3                    base of iot
CB$PDDB2 A     R4,IOTPDDBP              offset beyond last pddb
         SH    R4,=Y(BUFSTART-BUFDSECT) adjust for hasp buffer prefix
         LR    R2,R3                    base of iot
         A     R2,IOTPDDB               offset to first pddb in iot
         SH    R2,=Y(BUFSTART-BUFDSECT) adjust for hasp buffer prefix
CB$PDDB3 ST    R2,PDDBADDR              save addr of pddb
         ST    R3,IOTADDR               save addr of iot
         ST    R4,LASTPDDB              save addr of last pddb
         LH    R4,PDDBKEY               load dsid key
         LTR   R4,R4                    was a specific one requested?
         BZ    CB$DPDDB                 no  skip test
         CH    R4,PDBDSKEY              it this the correct one
         BNE   CB$NPDDB                 no  skip to next one
CB$DPDDB LR    R4,R2                    addr of input dataet?
         L     R2,=A(CB#D#PDB)          a(pddb description)
         BAL   R3,CB$FMT                go format the pddb
         MVC   QDMSG,QBLANK             blank out the message area
         BAL   R8,CB$DISP1              go display the blank line
CB$NPDDB L     R2,PDDBADDR              restore addr of pddb
         L     R3,IOTADDR               restore addr of iot
         L     R4,LASTPDDB              restore addr of last pddb
         LA    R2,PDBLENG(,R2)          no. look at next pddb.
         CR    R2,R4                    gone past the last pddb?
         BNL   CB$PDDB4                 yes. get next pddb
         CLC   PDBDSKEY,=H'0'           ?/valid pddb
         BNE   CB$PDDB3                 yes. go process
CB$PDDB4 L     R4,IOTIOTTR              disk addr of next iot
CB$PDDB5 LTR   R4,R4                    is there another iot?
         BZ    CB$PDDB6                 no. try the spin iot?
         BAL   R8,READ                  read the iot
         B     CB$PDDB2                 search the next iot
         USNGX JCT,R1                   base reg for jct
CB$PDDB6 TM    CB@SW,1                  spin iot chain already done?
         BO    CB$DONE                  yes. exit.
         OI    CB@SW,1                  set switch
         L     R1,QCJCTA                load base reg
         L     R4,JCTSPIOT              disk addr of spin iot
         B     CB$PDDB5                 search the spin iot chain
         EJECT
***********************************************************************
*                                                                     *
*   check if requesting a specific pddb                               *
*                                                                     *
***********************************************************************
PDDBS    LH    R1,QLNG3                 length of dataset id field
         SH    R1,=H'1'                 is dsid field zero length?
         BM    PDBTILT                  yes. quit.
         LA    R15,QPARM3               -> parm to be converted
         BAL   R14,QNUMCONV             convert to decimal
         BNE   PDBTILT                  complain if not decimal
         STH   R15,PDDBKEY              store requested dsid key
         B     SPDDB                    and return to caller
PDBTILT  QTILT '*** illegal pddb number requested ***'
         DROPX R1,R2,R3
         EJECT
***********************************************************************
*                                                                     *
*   format the hct                                                    *
*                                                                     *
***********************************************************************
CB$HCT   MVC   QDHLINE,QBLANK           clear the header line
         MVC   QDHLINE(36),=C'===  HCT checkpointed variables  ==='
         L     R2,=A(CB#D#HCT)          a(hct description)
         L     R4,QCJQTL                addr of input data
         BAL   R3,CB$FMT                go format the hct
         B     CB$DONE                  exit
***********************************************************************
*                                                                     *
*   format the jot                                                    *
*                                                                     *
***********************************************************************
CB$JOT   MVC   QDHLINE,QBLANK           clear the header line
         MVC   QDHLINE(26),=C'===  Job Output Table  ==='
         L     R2,=A(CB#D#JOT)          a(jot description)
         L     R4,QCJOTA                addr of input data
         BAL   R3,CB$FMT                go format the jot
         B     CB$DONE                  exit
***********************************************************************
*                                                                     *
*   format the checkpoint                                             *
*                                                                     *
***********************************************************************
CB$CKPT  MVC   QDHLINE,QBLANK           clear the header line
         MVC   QDHLINE(27),=C'===  Checkpoint  record ==='
         L     R2,=F'4096'              length of ckpt records
         L     R4,QCJQTL                a(first checkpoint record)
         L     R6,QCJOTL                no. of checkpoint records
         LA    R6,0(,R6)                purify the reg
         SR    R5,R5                    clear offset register
         B     CKPT010                  go format the data
******************************************************************
*
*   format  jqe/joe for a given offset from jqt/jot
*
******************************************************************
DIRJQE   DS    0H
         BAL   R14,CB$COFF              convert offset from QPARM1
*
*  first of all, the offset should be a multiple of jqe length.
*
         LTR   R4,R4                    zero offset is invalid
         BZ    DIRTILT
         LH    R15,QCJQELEN             load jqe length.
         LR    R1,R4
         SLR   R0,R0
         DR    R0,R15
         LTR   R0,R0                    zero remainder ?
         BNZ   DIRTILT                  no - tilt.
*
*  then, the offset should be within the jqt.
*
         L     R1,QCJQTL                get ckpt record.
         LH    R1,$MAXJOBS-$SAVEBEG(,R1)   maxjobs from hct
         MR    R0,R15                   maximum avl offset..
         CLR   R4,R1                    what have you ?
         BH    DIRTILT                  to high an offset.
         AL    R4,QCJQTA                + jqt address is jqe.
         L     R2,=A(CB#D#JQE)          description of jqe
         BAL   R3,CB$FMT                format it.
         MVC   QDHLINE,QBLANK           clear the header line
         MVC   QDHLINE(15),=C'===  J Q E  ==='
         B     CB$DONE
*
DIRJOE   DS    0H
         BAL   R14,CB$COFF              convert offset from QPARM1
*
* validate divisability by joesize and being between jotjoes
* and end of joes.
*
         LA    R15,JOESIZE              load joe length.
         LR    R1,R4                    our offset.
         SLR   R0,R0                    clear for divide.
         DR    R0,R15
         LTR   R0,R0                    zero remainder ?
         BNZ   DIRTILT                  no - tilt.
         CH    R4,=Y(JOTJOES-JOT)       before start of joes ?
         BL    DIRTILT
         L     R1,QCJQTL                get ckpt record.
         LH    R1,$NUMJOES-$SAVEBEG(,R1)   numjoes from hct
         MR    R0,R15                   maximum avl offset..
         CLR   R4,R1                    what have you ?
         BH    DIRTILT                  to high an offset.
         AL    R4,QCJOTA                + jot address is joe.
         USNGX JOEDSECT,R4              validate some fields.
         L     R2,=A(CB#DCJOE)          load header+desc addr
         MVC   QDHLINE,QBLANK           clear the header line
         MVC   QDHLINE(21),=C'===  Char  J O E  ==='
*??      CLI   JOETYPE,$JOECHAR         is it char joe ?
         CLI   JOETYPE,JOECHARJ         is it char joe ?
         BE    DIRJOEOK                 can't validate much more.
         MVC   QDHLINE+5(4),=C'Work'    show type of joe
         L     R2,=A(CB#DWJOE)
*??      CLI   JOETYPE,$JOEWORK         is it ?
         CLI   JOETYPE,JOEWORK          is it ?
         BE    DIRJOEOK                 yes.
         MVC   QDHLINE+5(4),=C'Free'    show type of joe
*??      CLI   JOETYPE,$JOEFREE         free joe ?
         CLI   JOETYPE,JOEFREE          free joe ?
         BE    DIRJOEOK                 yes.
         MVC   QDHLINE+5(4),=C'????'    show type of joe
*                                       ... of w/joe
DIRJOEOK DS    0H
         BAL   R3,CB$FMT
         B     CB$DONE
*
*  Subroutine to convert hex offset for direct control block display
*
CB$COFF  LH    R1,QLNG1                 get length of parm
         LTR   R1,R1
         BZ    DIRTILTO                 offset is required.
         TR    QPARM1,TBLC2H            translate to binary.
         EX    R1,CB$PACK1              PACK QNUMWORK(4+1),QPARM1(*-*)
         L     R4,QNUMWORK              load translated offset.
*?       LA    R4,0(,R4)                clear highest
         BR    R14                      return to caller
*
DIRTILT  QTILT '*** requested offset is not a valid block ***'
DIRTILTO QTILT '*** direct jqe/joe must specify an offset ***'
         DROPX R4
         EJECT
***********************************************************************
*                                                                     *
*   format the data                                                   *
*                                                                     *
***********************************************************************
CB$FMT   ST    R3,RETURN                save link address
         MVC   CB@NMLEN,0(R2)           save field name length
*%       SR    R5,R5                    offset register
         LH    R5,2(,R2)                get initial offset
         LA    R2,2+2(,R2)              skip over table prefix
         B     *+8     >========+       skip around
*                               |
FMT000   BAL   R8,CB$DISP1      |       go display this line
         LA    R3,QDMSG     <===+       a(display line)
         LH    R6,QDCOLS                load length of line        mod5
         AR    R6,R3                    a(end of display)          mod5
         MVC   QDMSG,QBLANK             init the current line
         ST    R5,QNUMWORK              set down offset
         HEX   (0,R3),QNUMWORK+2,LEN=2,HEXTAB=QHEXTAB  convert to hex
         MVI   0(R3),C'+'               indicate relative
         LA    R3,5(,R3)                next location of display line
*
FMT010   CLI   0(R2),X'FF'              ?/all fields formatted
         BE    CB$DISP                  go display the line
         LH    R15,CB@NMLEN             get field name length
         LA    R0,1(R15,R3)             current disp in display line
         SR    R7,R7                    clear reg for insert
         IC    R7,0(,R2)                get length of field
         STH   R7,CB@FLEN               save for later
         TM    1(R2),X'80'              ?/character field
         BZ    DOUBLE                   ** no. double the length
*%%%%%%% SRL   R7,1                     ** yes. cut length in half
         B     NODOUBLE                 ** don't double
DOUBLE   AR    R0,R7                    add length to displacement
NODOUBLE AR    R0,R7                    double it
         CR    R0,R6                    ?/enough room on display
         BH    FMT000                   no - go display this line
         CLI   CB@NMLEN+1,5             short field names?
         BNH   NARROW                   yes - use narrow col width
         LA    R1,18(,R3)               default for next offset
         CH    R7,=H'4'                 ?/field length <= 4
         BNH   FMT015                   yes. continue
         LA    R1,18(,R1)               bump for next offset
         CH    R7,=H'13'                ?/field length <= 13
         BNH   FMT015                   yes. continue
         LA    R1,18(,R1)               bump for next offset
         CH    R7,=H'22'                ?/field length <= 22
         BNH   FMT015                   yes. continue
         LA    R1,18(,R1)               bump for next offset
         CH    R7,=H'32'                ?/field length <= 32
         BH    CB$FDERR                 no - error
         B     FMT015                   skip around normal code
NARROW   LA    R1,15(,R3)               default for next offset
         CH    R7,=H'4'                 ?/field length <= 4
         BNH   FMT015                   yes. continue
         LA    R1,15(,R1)               bump for next offset
         CH    R7,=H'11'                ?/field length <= 11
         BNH   FMT015                   yes. continue
         LA    R1,15(,R1)               bump for next offset
         CH    R7,=H'19'                ?/field length <= 19
         BNH   FMT015                   yes. continue
         LA    R1,15(,R1)               bump for next offset
         CH    R7,=H'32'                ?/field length <= 32
         BNH   FMT015                   yes. continue
*B$FDERR QTILT '** unable to format due to field description error **'
CB$FDERR BAL   R14,QADDLINE             add any partial line to display
         MVC   QDMSG(46),=C'** Formatting terminated due to QCBFE error$
                at'
         LH    R15,CB@NMLEN             get field name length
         BCTR  R15,0                    -1 for ex
         EX    R15,CB$FEMVC             show field name
         MVC   QDMSG+56(2),=C'**'
         BAL   R14,QADDLINE
         B     QSTOP
CB$FEMVC MVC   QDMSG+47(*-*),2(R2)      << executed >>
CB$FNMVC MVC   0(*-*,R3),2(R2)          << executed >>
FMT015   ST    R1,NEXTOSET              save next offset
*  With the old design of this routine, the following wasn't done for
*  wide field names.  Since I don't understand what the following
*  check is for in the first place, we'll do it for wide also.
*%       CLC   QDCOLS,=H'80'            is this a mod 2 screen
*%       BE    MVENAME                  yes  move field name
         CLI   2(R2),C' '               is the field name blank
         BE    FMT020                   yes  do not move field name
MVENAME  LH    R15,CB@NMLEN             get field name length
         BCTR  R15,0                    -1 for ex
         EX    R15,CB$FNMVC             move field name  0(R3),2(R2)
         LA    R3,1+1(R15,R3)           update displacement
FMT020   LH    R15,CB@FLEN              get field length
         TM    1(R2),X'80'              ?/character field
         BZ    FMT030                   no. go translate data to ebcdic
         BCTR  R15,0                    decrement length for execute
         EX    R15,MVECHAR              move data to display line
         L     R14,=V(CHARTAB)          -> translate table
         EX    R15,TRCHAR               insure data is printable
         LA    R15,1(,R15)              reset length
         AR    R3,R15                   add length to display line
         AR    R4,R15                   add length to input data offset
         AR    R5,R15                   add length to offset
         B     FMT040                   go prepare for next field
MVECHAR  MVC   0(*-*,R3),0(R4)          << executed >>
TRCHAR   TR    0(*-*,R3),0(R14)         << executed >>
FMT030   UNPK  0(2+1,R3),0(1+1,R4)
         TR    0(2,R3),QHEXTAB
         LA    R3,2(,R3)                update displacement
         LA    R4,1(,R4)                point to next byte of data
         LA    R5,1(,R5)                relative offset
         BCT   R7,FMT030                do for length of field
         MVI   0(R3),C' '               fix flip byte
FMT040   L     R3,NEXTOSET              get next offset
         AH    R2,CB@NMLEN              -> next field table ...
         LA    R2,1+1(,R2)                 ... entry
         B     FMT010                   process next field
CB$DISP  BAL   R8,CB$DISP1              go display the last line
         L     R3,RETURN                restore link address
         BR    R3                       return to caller
CB$DISP1 DS    0H
         CLI   QCODE,8                  ?/checkpoint being dumped
         BNE   CB$DISP2                 no. keep on trucking
         L     R1,STARTADR         starting addr of data for this line
         LR    R15,R4                ending addr of data for this line
         SR    R15,R1                   length of data
         BCTR  R15,0                    decrement by 1 for execute.
*?       EX    R15,MOVE0                move the data to the work area
*?       EX    R15,TRANSLTE             translate the data
*?       EX    R15,MOVE1             move the data to the display line
         EX    R15,CB$MVC1              copy data to the display line
         L     R14,=V(CHARTAB)          -> translate table
         EX    R15,CB$TR                translate data to printable
         MVI   QDMSG+52,C'*'            fill in the rest
         MVI   QDMSG+73,C'*'            of the data
CB$DISP2 EQU   *
         BAL   R14,QADDLINE             add line to screen buffer
         BR    R8
*?MOVE0  MVC   PLAY1(0),0(R1)           move data to work area
*?MOVE1  MVC   QDMSG+53(0),PLAY1        move data to display line
*?ANSLTE TR    PLAY1(0),TBL1            change the data to special fmt
CB$MVC1  MVC   QDMSG+53(*-*),0(R1)      << executed >>
CB$TR    TR    QDMSG+53(*-*),0(R14)     << executed >>
         EJECT
***********************************************************************
*                                                                     *
*   format the checkpoint data                                        *
*                                                                     *
***********************************************************************
CKPT010  ST    R4,STARTADR              save line data start addr
         LA    R3,QDMSG                 a(display line)
         MVC   QDMSG,QBLANK             blank the current line
         MVI   0(R3),C'+'               indicate relative
         STH   R5,QNUMWORK              move offset to work area
         HEX   (1,R3),QNUMWORK,LEN=2
         LA    R3,6(,R3)                -> next area in display line
         LA    R7,5                     no. of fields per display line
CKPT020  HEX   (0,R3),(0,R4),LEN=4
         LA    R5,4(,R5)                update the offset
         LA    R3,9(,R3)                update display line location
         LA    R4,4(,R4)                point at next 4 bytes of data
         SH    R2,=H'4'                 decrement length by 4
         BNP   CKPT030                  go display line if no data left
         BCT   R7,CKPT020               yes, do another field if room
         BAL   R8,CB$DISP1              no room. display this line
         B     CKPT010                  go get another line
CKPT030  BAL   R8,CB$DISP1              go display the line
         L     R2,=F'4096'              length of ckpt records
         BCT   R6,CKPT010               go format this buffer
         B     CB$DONE                  no more buffers. go home
         EJECT
***********************************************************************
*                                                                     *
*   prepare to read the spool pack                                    *
*                                                                     *
***********************************************************************
READ     ST    R4,QCTRAK
         L     R1,QCIOTA
         QCALL READSPC
         L     R4,QCIOTA                a(iot)
         BR    R8
         SPACE 4
***********************************************************************
*                                                                     *
*   return to the caller                                              *
*                                                                     *
***********************************************************************
CB$DONE  L     R2,DALLREG             restore reg to determine next cb
         LTR   R2,R2                    ?/is it valid
         BZ    QSTOP                    no. exit
         LA    R2,4(,R2)                yes. update the pointer
         ST    R2,DALLREG               save the pointer
         QCALL DISPLAY                  display the screen now
         BR    R2                       go to it
         EJECT ,
         LTORG ,
         SPACE 3
***********************************************************************
*                                                                     *
*   miscellaneous nuts, bolts, etc                                    *
*                                                                     *
***********************************************************************
TBLC2H   DC    256X'F0'
         ORG   TBLC2H+C'A'
         DC    X'FAFBFCFDFEFF'
         ORG   TBLC2H+C'0'
         DC    X'F0F1F2F3F4F5F6F7F8F9'
         ORG   ,
CB$PACK1 PACK  QNUMWORK(4+1),QPARM1(*-*)  << executed >>
         EJECT ,
***********************************************************************
*                                                                     *
*   format of tables for jqe, joe, iot, pddb, jct, jot, jcb.          *
*                                                                     *
*        offset    length         description                         *
*          0         5              field id tag.                     *
*          5         1              x... ....  if on indicates the    *
*                                              field is already in    *
*                                              ebcdic format.         *
*                                   ...x xxxx  length of field.       *
*                                              maximum length is x'1a'*
*                                                                     *
***********************************************************************
         COPY  Q#CB#JQE
         EJECT
         COPY  Q#CB#JOE
         EJECT
         COPY  Q#CB#JCT
         EJECT
          AIF   (&@@ACF2 EQ 0).NOACF2
         COPY  Q#CB#ACF
.NOACF2   ANOP
         EJECT
         COPY  Q#CB#IOT
         EJECT
         COPY  Q#CB#PDB
         EJECT
         COPY  Q#CB#JOT
         EJECT
***********************************************************************
*                                                                     *
*   format of table for hct variables.                                *
*                                                                     *
*        offset    length         description                         *
*          0         8              field id tag.                     *
*          8         1              x... ....  if on indicates the    *
*                                              field is already in    *
*                                              ebcdic format.         *
*                                   ...x xxxx  length of field.       *
*                                              maximum length is x'1e'*
*                                                                     *
***********************************************************************
         COPY  Q#CB#HCT
         EJECT
         QCOMMON
WORK     DSECT
         ORG   WORK+72                  skip over save area
DALLREG  DS    F
CB@NMLEN DS    H                        field name length for this C.B.
CB@FLEN  DS    H                        field length
CB@SW    DS    C
CB@WDJCT DS    CL(CB#JCTDL)             working copy of CB#D#JCT
PDDBKEY  DS    H
JOEADDR  DS    F
IOTADDR  DS    F
PDDBADDR DS    F
LASTPDDB DS    F
RETURN   DS    F
NEXTOSET DS    F
STARTADR DS    F
CB@WORKL EQU   *-WORK                   length of protected work area
         Q$HCT
         Q$JOT
         Q$JQE
         Q$JOE
         Q$JCT
         Q$IOT
         Q$PDDB
         Q$NHD
         QZACFNJB
