/* Rexx */
Address ISPEXEC
Arg arrgh
If Index(arrgh, 'DEBUG') ¬= 0 Then Trace I
/*
   This exec suppresses the display of the invocation panel (QPENTRY)
   for normal users.  Customize it appropriately for your site.
   At USC, the systems staff have another panels library, which
   contains a selection to invoke this clist with the ALT keyword.

   This allows the systems programming staff to use special operands,
   while limiting the users to access only the current production
   QUEUE, spool, and checkpoint.
*/

qparms = ''
"VGET (ZENVIR)"
/* In ISPF V3R2, we no longer need the TLD for anything, so it is
   not necessary to use the interface module with prefix 'ISR'.
   Otherwise, in order to get the TLD address, we must make ISPF
   think we are part of it, hence the use of the ISRQ module.     */
If Substr(zenvir,6,3) >= '3.2' Then pgm = 'QUEUE'
                               Else pgm = 'ISRQ'

/*If Substr(1:1,Sysvar(sysuid),1,1) = 'S' Then Do */
If Index(arrgh, 'ALT') ¬= 0 Then Do
   "DISPLAY PANEL(QPENTRY)"
   If Rc ¬= 0 Then Exit
/* "VGET (QPRFX QVLSR QSBS QICMD)" */
   If qcmdnm = 'ISRQ' Then Do
      Say "That would be exceedingly stupid."
      Exit 20
      End
   If Substr(zenvir,6,3) >= '3.2' Then pgm = qcmdnm
   If qprfx ¬= '' Then qparms = qparms "DSN("qprfx")"
   If qvlsr ¬= '' Then qparms = qparms "CKPT("qvlsr")"
   If qsbs  ¬= '' Then qparms = qparms "JES("qsbs")"
   If qicmd ¬= '' Then qparms = qparms qicmd
   End
                                 Else Do
   qcmdnm = 'QUEUE'
   "VPUT (QCMDNM)"
   End

/* Queue the command instead of executing it directly, so that the
   Rexx interpreter is gone.  Otherwise, it tends to get control
   for ATTNs.
*/
Queue "ISPEXEC SELECT PGM("pgm") PARM("qparms")"
