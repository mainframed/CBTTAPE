         TITLE '--- QUEUE--JOBSUMM -- SUMMARIZE JOB RUN RESULTS ---'
*---------------------------------------------------------------------*
*                                                                     *
*   JOBSUMM - Summarize job run results by reading job messages and   *
*             displaying selective information                        *
*                                                                     *
*   Updates:                                                          *
*      17Dec90  LDW  Get I/O counts from Komand accounting boxes      *
*      25Dec89  LDW  Q$KEYS -> QZKEYS, Q$TEXT -> QZTEXT               *
*      26Apr89  LDW  Remove unneeded Q$JCT, Q$IOT                     *
*      26Apr89  EMS  Update for SP311                                 *
*      06Jan88  LDW  Fix for job starting one year, ending next year  *
*       8Jul87  EMS  Update for GETREC, GETPDDB                       *
*      21May87  EMS  Update for GETREC enhancements                   *
*      21Mar87  LDW  Fix for DSID = 0 (from RSCS)                     *
*      25Jan87  LDW  Pass READSPC the JQE addr in case of CB ID error *
*      20Jan87  LDW  Fix for blank stepname                           *
*                    Change lots of character constants to lower case *
*      03Jan87  LDW  Fix problem of splitting summary lines when an   *
*                       exceptional message is displayed              *
*                    Don't use LC macro                               *
*      27AUG86  LDW  SET QDHDSID (J.I.C.)                             *
*      11JUL86  LDW  FIX BUG IN NON-HACSCG HEADER LINE                *
*      04/20/86 LDW  DISPLAY SRB TIME CORRECTLY                       *
*      04/08/86 LDW  DISPLAY ALL 'IEF' MESSAGES EXCEPT THOSE THAT     *
*                       ARE KNOWN TO BE UN-INTERESTING                *
*      04/07/86 LDW  FINISH WRITING FULL-BLOWN VERSION OF THIS CMD    *
*      03/31/86 LDW  RE-DESIGN 3278-5 SUPPORT (THIRD TRY!)            *
*      12/02/85 LDW  NEW ROUTINE                                      *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 3
JOBSUMM  QSTART  WORKLEN=JS@WORKL,STACK=X'40'
         USNGX WORK,R13                 LOCAL WORK AREA
         SPACE 1
***********************************************************************
*                                                                     *
*   CALL FINDJOB TO LOCATE THE JQE, JCT, AND IOT                      *
*                                                                     *
***********************************************************************
         QCALL FINDJOB                  FIND THE JOB
         OI    QSCFLAG1,QSC1JOB         PUT JOBID IN QDTOP
         QCALL DISP80                   SWITCH TO 3278-2 MODE IF 3278-5
***********************************************************************
*                                                                     *
*   LOCATE PDDB NUMBER 4                                              *
*                                                                     *
***********************************************************************
          AIF  (&QLEVEL GE 9).JS01
         MVC   QPDSID,=H'4'             set requested dsid
          AGO  .JS02
.JS01     ANOP ,
         MVC   QPDSID,=F'4'             set requested dsid
.JS02     ANOP ,
         MVI   QGPFLAG,QPFDIR+QPFNMSG   specific request
         QCALL GETPDDB                  get pddb
         LTR   R2,R1                    copy pddb addr
         BP    JS$FOUND                 and use it
         USNGX PDBDSECT,R2
         QTILT '*** JOB DOES NOT HAVE JOB MESSAGES DATASET ***'
         SPACE 2
JS$FOUND MVI   QCRFLAG,QRFINIT          req init processing
         MVC   QCGRMTTR,PDBMTTR         set starting mttr
*JS$FOUND L     R1,QCSTART               BEGINNING OF DISK ADDR TABLE
*        MVC   0(4,R1),PDBMTTR          SET STARTING DISK ADDR OF DS
         MVC   QDHDSID,=CL8'JOBMSG'     SET DISPLAYABLE DSID (J.I.C.)
*        SR    R4,R4                    TELL GETREC TO START AT TOP
         MVC   QDHLINE,JS#HDR1          MOVE IN HEADING
         MVC   JS@LINE,QBLANK           CLEAR THE OUTPUT LINE
         DROPX R2                       PDDB
         SPACE 1
***********************************************************************
*                                                                     *
*   READ JOB MESSAGES DATASET, EXTRACT AND DISPLAY INTERESTING STUFF  *
*                                                                     *
***********************************************************************
JS$NXT   QCALL GETREC                   GET THE NEXT RECORD
*  MUST NOT MODIFY R4,R5,R6 BETWEEN CALLS TO "GETREC"
         LTR   R4,R4                    HAVE WE HIT BOTTOM?
         BZ    QSTOP                    YES - ALL DONE
         USNGX LRCDSECT,R4              R4 SET BY "GETREC"
         TM    LRCFLAG1,LRC1SPAN        IS THIS A SPANNED RECORD?
         BO    JS$NXT                   YES - CAN'T HANDLE THESE YET
         LA    R2,LRCTEXT               -> START OF TEXT
         LR    R3,R6                    COPY LENGTH
         TM    LRCFLAG1,LRC1CCTL        IS CARRIAGE CONTROL SPECIFIED?
         BNO   JS$GO                    NO - OK
         LA    R2,1(,R2)                YES - SKIP OVER IT
         BCTR  R3,0                     DECREMENT LENGTH
         SPACE 2
JS$GO    LA    R1,JS#TABLE              -> MESSAGE ID TABLE
         SPACE 1
JS$MSGLP CLC   0(7,R2),2(R1)            IS THIS AN INTERESTING MESSAGE?
         BE    JS$LOOK                  YES - LOOK AT IT
*---  MESSAGES FROM THE CONVERTER HAVE THE JCL STMT NUMBER IN FRONT---*
         CLC   10(7,R2),2(R1)           IS THIS AN INTERESTING MESSAGE?
         BE    JS$LOOK                  YES - LOOK AT IT
          AIF   ('&@@ID' NE 'USC-UCS').JS03
         CLC   3(12,R2),=C'EXCP TOTALS:'  Komand summary line?
         BE    JS$KIO                   Yes - save I/O counts
.JS03     ANOP
         LA    R1,JS#TBLL(,R1)          -> NEXT TABLE ENTRY
         CLI   0(R1),X'FF'              END OF TABLE?
         BNE   JS$MSGLP                 NO - KEEP LOOKING
         CLC   =C'IEF',0(R2)            OTHER JOB SCHEDULER MESSAGE?
         BE    JS$CPY                   YES - DISPLAY IT
         CLC   =C'IEF',10(R2)           OTHER CONVERTER MESSAGE?
         BE    JS$CPY                   YES - DISPLAY IT
         B     JS$NXT                   YES - NOT INTERESTING - IGNORE
         DROPX R4                       LRCDSECT
***********************************************************************
*                                                                     *
*   PROCESS AN INTERESTING RECORD                                     *
*                                                                     *
***********************************************************************
JS$LOOK  LH    R15,0(,R1)               GET PROCESSING ROUTINE OFFSET
         B     0(R12,R15)               GO TO IT
         SPACE 2
*  IEF201I JJJ SSS - JOB TERMINATED BECAUSE OF CONDITION CODES
*  IEF212I JJJ SSS DDD - DATA SET NOT FOUND
*  IEF253I JJJ SSS DDD - DUPLICATE NAME ON DIRECT ACCESS VOLUME
*  IEF287I DSNAME DISP / VOL SER NOS =  ** ERROR DISP **
*  IEF606I MISPLACED DD STATEMENT
*  HAC220I (HAC JOB COST)
*JS$CPY  CLC   JS@LINE,QBLANK           ANY PARTIAL LINE?
*        BE    *+8                      NO - SKIP
*        BAL   R8,JS$BUILD              YES - FIX IT UP AND PUT IT OUT
JS$CPY   MVC   QDMSG(2),=X'1DE8'        SET OUTPUT, BRIGHT
         MVC   QDMSG+2(5),=C'Msg->'     FLAG IT
         MVC   QDMSG+7(2),=X'1D60'      SET OUTPUT, NORMAL
         CH    R3,=H'73'                MESSAGE LINE TOO LONG?
         BNH   *+8                      NO - USE LENGTH
         LA    R3,73                    YES - USE MAX
         BCTR  R3,0                     -1 FOR EX
         EX    R3,JS$MVC                MVC QDMSG+7(*-*),0(R2)
         MVI   QDATRNUM+1,2             SET # OF ATTR BYTES IN LINE
         BAL   R14,QADDLINE             ADD THIS LINE TO SCREEN
         B     JS$NXT                   PROCESS NEXT INPUT RECORD
JS$MVC   MVC   QDMSG+9(*-*),0(R2)       << EXECUTED >>
         SPACE 2
***********************************************************************
*   BUILD THE STANDARD OUTPUT LINE                                    *
***********************************************************************
JS$BUILD MVC   JS@ATTR1,=X'1DE8'        SET OUTPUT, BRIGHT
         CLC   =C'0000',JS@CODE         RETURN CODE 0?
         BNE   JS$NZERO                 NO - OK
         MVI   JS@ATTR1+1,X'60'         YES - SET OUTPUT, NORMAL
         MVC   JS@CODE,=C'zero'         AND MAKE IT SPECIAL
JS$NZERO MVC   JS@ATTR2,=X'1D60'        SET OUTPUT, NORMAL
         MVI   QDATRNUM+1,2             SET # OF ATTR BYTES IN LINE
*---  PUT PREVIOUSLY BUILT OUTPUT LINE TO SCREEN                   ---*
         MVC   QDMSG(JS@LINEL),JS@LINE  COPY TO LINE BUFFER AREA
         BAL   R14,QADDLINE             ADD THIS LINE TO SCREEN
         MVC   JS@LINE,QBLANK           CLEAR WORK LINE BUFFER
         BR    R8                       RETURN TO CALLER
         SPACE 3
***********************************************************************
*                                                                     *
*   PROCESSING ROUTINES                                               *
*                                                                     *
***********************************************************************
*  IEF142I JJJ SSS - STEP WAS EXECUTED - COND CODE NNNN
JS$142   BAL   R8,JS$GNAME              GET STEPNAMES
JS$142$L LA    R2,1(,R2)                -> BUMP INPUT PTR
         CLC   =C'COND CODE',0(R2)      IS THIS IT?
         BE    JS$142$S                 YES - SAVE IT
         BCT   R3,JS$142$L              NO - KEEP LOOKING
         B     JS$NXT                   %%%%%%%%%% LOGIC ERROR
JS$142$S MVC   JS@CODE,10(R2)           SAVE CONDITION CODE
         B     JS$NXT                   GO READ NEXT INPUT RECORD
         SPACE 1
*  IEF202I JJJ SSS - STEP WAS NOT RUN BECAUSE OF CONDITION CODES
JS$202   MVC   JS@DAYS+2(49),=C'<< Step was not run because of conditio$
               n codes >>'
         B     JS$NXT                   GO READ NEXT INPUT RECORD
         SPACE 1
*  IEF272I JJJ SSS - STEP WAS NOT EXECUTED
JS$272   BAL   R8,JS$GNAME              GET STEPNAMES
         MVC   JS@CODE(5),=C'flush'     INDICATE STEP NOT EXECUTED
         B     JS$NXT                   GO READ NEXT INPUT RECORD
         SPACE 1
*  IEF373I STEP START
*%JS$373 PACK  JS@SDATE,30(5,R2)        SAVE STEP START DATE
JS$373   PACK  JS@SYEAR,30(3,R2)        SAVE STEP START DATE (yyxx)
         PACK  JS@SDATE,32(3,R2)        SAVE STEP START DATE (ddd)
         PACK  JS@SHOUR,36(2,R2)        SAVE STEP START TIME (HH)
         PACK  JS@SMINS,38(2,R2)        SAVE STEP START TIME (MM)
         B     JS$NXT                   GO READ NEXT INPUT RECORD
         SPACE 1
*  IEF374I STEP STOP
*  IEF376I JOB  STOP
JS$374   CLC   JS@DAYS,QBLANK           ANY MESSAGE HERE ALREADY?
         BNE   JS$HAVE                  YES - DON'T OVERLAY IT
*%       PACK  JS@EDATE,30(5,R2)        SAVE STEP END DATE
         PACK  JS@EDATE,32(3,R2)        SAVE STEP END DATE (ddd)
         PACK  JS@EHOUR,36(2,R2)        SAVE STEP END TIME (HH)
         PACK  JS@EMINS,38(2,R2)        SAVE STEP END TIME (MM)
         SP    JS@EDATE,JS@SDATE        COMPUTE ELAPSED DAYS
         BNM   JS$374$0                 skip if ok
         AP    JS@EDATE,=P'365'         normalize 'ddd'
         TM    JS@SYEAR,X'01'           leap year?
         BO    JS$374$0                 no (odd year) - not leap
         TM    JS@SYEAR,X'12'           divisible by 4?
         BM    JS$374$0                 no - not leap year
         AP    JS@SDATE,=P'1'           yes - account for leap year
         SPACE 1
JS$374$0 SP    JS@EMINS,JS@SMINS        COMPUTE ELAPSED TIME (MM)
         BNM   JS$374$1                 SKIP IF OK
         AP    JS@EMINS,=P'60'          NORMALIZE 'MM'
         SP    JS@EHOUR,=P'1'           AND ADJUST NUMBER OF HOURS
         SPACE 1
JS$374$1 SP    JS@EHOUR,JS@SHOUR        COMPUTE ELAPSED TIME (HH)
         BNM   JS$374$2                 SKIP IF OK
         AP    JS@EHOUR,=P'24'          NORMALIZE 'HH'
         SP    JS@EDATE,=P'1'           AND ADJUST NUMBER OF DAYS
         SPACE 1
JS$374$2 CP    JS@EDATE,=P'0'           > 24 HOURS?
         BE    JS$374$3                 NO - SKIP DAYS DISPLAY
         UNPK  JS@DAYS(2),JS@EDATE      SHOW NUMBER OF DAYS
         OI    JS@DAYS+1,C'0'           FIX FLIP BYTE
         CLI   JS@DAYS,C'0'             CHEAPO EDIT
         BNE   *+8                      ...
         MVI   JS@DAYS,C' '             ... SUPPRESS LEADING BLANK
         MVC   JS@DAYS+3(4),=C'Days'    SHOW WHAT IT IS
         SPACE 1
JS$374$3 UNPK  JS@HOURS(2),JS@EHOUR     SHOW HH
         OI    JS@HOURS+1,C'0'          FIX FLIP BYTE
         MVI   JS@HOURS+2,C':'          PUT IN THE SEPARATOR
         UNPK  JS@HOURS+3(2),JS@EMINS   SHOW MM
         OI    JS@HOURS+4,C'0'          FIX FLIP BYTE
         SPACE 1
*---  TCB CPU TIME                                                 ---*
         MVC   JS@TEMP(4),45(R2)        FILL IN MINUTES
         MVI   JS@TEMP+4,C':'           SEPARATOR
         MVC   JS@TEMP+5(5),53(R2)      FILL IN SECONDS
         LA    R15,JS@TEMP+2            ASSUME < 100 MINUTES TCB TIME
         CLC   JS@TEMP(2),QBLANK        > 99 MINUTES TCB TIME?
         BE    *+12                     NO - KEEP FRACTIONAL SECONDS
         LA    R15,JS@TEMP              USE COMPLETE MINUTES VALUE
         MVI   JS@TEMP+7,C' '           AND KILL THE DOT
         MVC   JS@TCB,0(R15)            MOVE TCB TIME TO DISPLAY LINE
         SPACE 1
*---  SRB CPU TIME                                                 ---*
         MVC   JS@TEMP(4),66(R2)        FILL IN MINUTES
         MVI   JS@TEMP+4,C':'           SEPARATOR
         MVC   JS@TEMP+5(5),74(R2)      FILL IN SECONDS
         LA    R15,JS@TEMP+2            ASSUME < 100 MINUTES SRB TIME
         CLC   JS@TEMP(2),QBLANK        > 99 MINUTES SRB TIME?
         BE    *+12                     NO - KEEP FRACTIONAL SECONDS
         LA    R15,JS@TEMP              USE COMPLETE MINUTES VALUE
         MVI   JS@TEMP+7,C' '           AND KILL THE DOT
         MVC   JS@SRB,0(R15)            MOVE TCB TIME TO DISPLAY LINE
JS$HAVE  EQU   *
          AIF   ('&@@ID' EQ 'HAC SCG').JS04
         BAL   R8,JS$BUILD              GO DISPLAY COMPLETED MESSAGE
.JS04     ANOP
         B     JS$NXT                   GO READ NEXT RECORD
         SPACE 1
*  IEF375I JOB START
*%JS$375 PACK  JS@SDATE,30(5,R2)        SAVE JOB START DATE
JS$375   PACK  JS@SYEAR,30(3,R2)        SAVE JOB START DATE (yyxx)
         PACK  JS@SDATE,32(3,R2)        SAVE JOB START DATE (ddd)
         PACK  JS@SHOUR,36(2,R2)        SAVE JOB START TIME (HH)
         PACK  JS@SMINS,38(2,R2)        SAVE JOB START TIME (MM)
         MVC   JS@STEPN(17),=C'***Job Summary***'
         B     JS$NXT                   GO READ NEXT INPUT RECORD
         SPACE 1
*  IEF376I JOB STOP
JS$376   EQU   JS$374,4,C'I'            IDENTICAL CODE!
         SPACE 1
*  IEF472I JJJ SSS COMPLETION CODE - SYSTEM=XXX USER=NNNN
JS$472   BAL   R8,JS$GNAME              GET STEPNAMES
JS$472$L LA    R2,1(,R2)                -> BUMP INPUT PTR
         CLC   =C'SYSTEM=',0(R2)        IS THIS IT?
         BE    JS$472$S                 YES - SAVE IT
         BCT   R3,JS$472$L              NO - KEEP LOOKING
         B     JS$NXT                   %%%%%%%%%% LOGIC ERROR
JS$472$S MVI   JS@CODE,C'S'             INDICATE SYSTEM ABEND CODE
         MVC   JS@CODE+1(3),7(R2)       SAVE ABEND CODE
         CLC   =C'S000',JS@CODE         ANY SYSTEM ABEND CODE?
         BNE   JS$NXT                   YES - DISPLAY AS IS
         MVI   JS@CODE,C'U'             INDICATE USER ABEND CODE
         MVC   JS@CODE+1(4),16(R2)      SAVE ABEND CODE
         B     JS$NXT                   GO READ NEXT INPUT RECORD
         SPACE 1
          AIF   ('&@@ID' NE 'HAC SCG').JS05
*  HAC110I HAC STEP END
JS$110   CLC   JS@DISK,QBLANK           ANY MESSAGE HERE ALREADY?
         BNE   JS$110$S                 YES - DON'T OVERLAY IT
         LA    R2,39(,R2)               -> BEFORE DISK I/O FIELD
         SH    R3,=H'39'                ADJUST REMAINING LENGTH
         BAL   R14,JS$GWORD             GET DISK I/O COUNT
         BAL   R14,JS$RIGHT             RIGHT JUSTIFY IT
         MVC   JS@DISK,JS@WORD          MOVE INTO MESSAGE
         LA    R2,4(,R2)                SKIP OVER 'TAPE'
         SH    R3,=H'4'                 ADJUST REMAINING LENGTH
         BAL   R14,JS$GWORD             GET TAPE I/O COUNT
         BAL   R14,JS$RIGHT             RIGHT JUSTIFY IT
         MVC   JS@TAPE,JS@WORD          MOVE INTO MESSAGE
JS$110$S BAL   R8,JS$BUILD              GO DISPLAY COMPLETED MESSAGE
         B     JS$NXT                   GO READ NEXT RECORD
         SPACE 1
*  HAC210I HAC JOB END
JS$210   EQU   JS$110,4,C'I'            IDENTICAL CODE!
         SPACE 1
.JS05     ANOP
         SPACE 1
          AIF   ('&@@ID' NE 'USC-UCS').JS06
*  Komand step and job summary boxes (summary line is same in both)
JS$KIO   CLC   JS@DISK,QBLANK           ANY MESSAGE HERE ALREADY?
         BNE   JS$NXT                   YES - DON'T OVERLAY IT
         MVC   JS@DISK,33(R2)           MOVE INTO MESSAGE
         MVC   JS@TAPE,63(R2)           MOVE INTO MESSAGE
         B     JS$NXT                   GO READ NEXT RECORD
         SPACE 1
.JS06     ANOP
         SPACE 2
***********************************************************************
*   SUBROUTINE TO GET STEPNAME AND PROCSTEP                           *
***********************************************************************
JS$GNAME LA    R2,7(,R2)                -> PAST MSGID
         SH    R3,=H'7'                 ADJUST REMAINING LENGTH
         BAL   R14,JS$GWORD             GET JOBNAME
         MVC   JS@STEPN,QBLANK          CLEAR FIELD IN CASE NO STEPNAME
         MVC   JS@PROCS,QBLANK          CLEAR FIELD IN CASE NO STEPNAME
         CLI   1(R2),C'-'               ANY STEPNAME?
         BER   R8                       NO - RETURN
         BAL   R14,JS$GWORD             GET STEPNAME
         MVC   JS@STEPN,JS@WORD         STEPNAME TO MSG
         CLI   1(R2),C'-'               ANY PROCSTEP?
         BER   R8                       NO - RETURN
         BAL   R14,JS$GWORD             GET PROCSTEP
         MVC   JS@PROCS,JS@WORD         PROCSTEP TO MSG
         BR    R8                       RETURN TO CALLER
         SPACE 2
***********************************************************************
*   SUBROUTINE TO SCAN FOR NEXT WORD                                  *
***********************************************************************
JS$GWORD MVC   JS@WORD,QBLANK           CLEAR OUTPUT AREA
         LA    R1,JS@WORD               -> START OF OUTPUT AREA
         LA    R0,8                     MAX LENGTH
*  AT ENTRY, R2 -> BLANK IN FRONT OF THE WORD
         LA    R2,1(,R2)                -> START OF WORD
         BCT   R3,JS$GW$LP              DECR REMAINING LENGTH
         BR    R14                      RETURN TO CALLER IF NO MORE
         SPACE 1
JS$GW$LP MVC   0(1,R1),0(R2)            MOVE 1 CHAR
         LA    R1,1(,R1)                BUMP OUTPUT PTR
         LA    R2,1(,R2)                -> NEXT CHAR OF WORD
         BCT   R3,*+6   >======+        DECR REMAINING LENGTH
         BR    R14             |        RETURN TO CALLER IF NO MORE
         CLI   0(R2),C' '  <===+        END OF WORD?
         BER   R14                      YES - RETURN TO CALLER
         BCT   R0,JS$GW$LP              SAVE UP TO 8 CHARS
         L     R0,=C'JS1 '              SET LOGIC ERROR CODE
         L     R15,=V(LOGICERR)         -> ROUTINE
         BALR  R14,R15                  GO TO IT (NO RETURN)
         SPACE 2
***********************************************************************
*   SUBROUTINE TO RIGHT ADJUST A WORD                                 *
***********************************************************************
JS$RIGHT CLI   JS@WORD+7,C' '           IS IT ALL THE WAY TO THE RIGHT?
         BNER  R14                      YES - RETURN TO CALLER
         MVC   JS@DWD,JS@WORD           SAVE THE WORD
         MVC   JS@WORD+1(7),JS@DWD      MOVE IT BACK, ONE TO THE RIGHT
         MVI   JS@WORD,C' '             FILL IN THE SPACE OPENED UP
         B     JS$RIGHT                 AND TRY AGAIN
         SPACE 2
***********************************************************************
*                                                                     *
*   MISCELLANY                                                        *
*                                                                     *
***********************************************************************
         LTORG
          AIF   ('&@@ID' EQ 'USC-UCS').JS91
          AIF   ('&@@ID' EQ 'HAC SCG').JS91
JS#HDR1  DC    CL79'Stepname  Procstep  Code   Elapsed time  TCB time  $
               SRB time'
          AGO   .JS92
.JS91     ANOP
JS#HDR1  DC    CL79'Stepname  Procstep  Code   Elapsed time  TCB time  $
               SRB time  Disk I/O  Tape I/O'
.JS92     ANOP
         SPACE 2
JS#TABLE DC    Y(JS$142-JOBSUMM),CL8'IEF142I'  STEP WAS EXEC - COND ...
JS#TBLL  EQU   *-JS#TABLE
*>       DC    Y(JS$CPY-JOBSUMM),CL8'IEF201I'  JOB TERM DUE TO CC ...
         DC    Y(JS$202-JOBSUMM),CL8'IEF202I'  STEP NOT DUE BECAUSE CC
*>       DC    Y(JS$CPY-JOBSUMM),CL8'IEF212I'  DATA SET NOT FOUND
         DC    Y(JS$NXT-JOBSUMM),CL8'IEF236I'  ALLOC FOR JJJ SSS
         DC    Y(JS$NXT-JOBSUMM),CL8'IEF237I'  UUU ALLOCATED TO DDDDD
*>       DC    Y(JS$CPY-JOBSUMM),CL8'IEF253I'  DUPLICATE DSN ON DASD
         DC    Y(JS$272-JOBSUMM),CL8'IEF272I'  STEP WAS NOT EXECUTED
         DC    Y(JS$NXT-JOBSUMM),CL8'IEF285I'  DISPOSITION OF DATASET
*>       DC    Y(JS$CPY-JOBSUMM),CL8'IEF287I'  ERROR DISP OF DATASET
         DC    Y(JS$373-JOBSUMM),CL8'IEF373I'  STEP START
         DC    Y(JS$374-JOBSUMM),CL8'IEF374I'  STEP STOP
         DC    Y(JS$375-JOBSUMM),CL8'IEF375I'  JOB START
         DC    Y(JS$376-JOBSUMM),CL8'IEF376I'  JOB STOP
         DC    Y(JS$472-JOBSUMM),CL8'IEF472I'  COMPLETION CODE (ABEND)
         DC    Y(JS$NXT-JOBSUMM),CL8'IEF604I'  EXPDT ZERO DAYS VALUE
*>       DC    Y(JS$CPY-JOBSUMM),CL8'IEF606I'  MISPLACED DD STATEMENT
         DC    Y(JS$NXT-JOBSUMM),CL8'IEF648I'  INV. DISP - PASS SUB'D
         DC    Y(JS$NXT-JOBSUMM),CL8'IEF653I'  SUBSTITUTION JCL
         DC    Y(JS$NXT-JOBSUMM),CL8'IEF686I'  DDNAME NOT RESOLVED
          AIF   ('&@@ID' NE 'HAC SCG').JS93
         DC    Y(JS$110-JOBSUMM),CL8'HAC110I'  HAC STEP END
         DC    Y(JS$210-JOBSUMM),CL8'HAC210I'  HAC JOB  END
         DC    Y(JS$CPY-JOBSUMM),CL8'HAC220I'  HAC JOB COST
.JS93     ANOP
*> INDICATES MESSAGE WILL BE COPIED BY GENERAL 'IEF' SCAN
         DC    X'FF'                    END OF TABLE
         SPACE 3
         QCOMMON
         SPACE 3
WORK     DSECT
         ORG   WORK+72                  SKIP OVER SAVE AREA
JS@SYEAR DS    PL2                      START year (yy..)
JS@SDATE DS    PL3                      START DATE (ddd)
JS@SHOUR DS    PL2                      START TIME (HH)
JS@SMINS DS    PL2                      START TIME (MM)
JS@EDATE DS    PL3                      END   DATE (ddd)
JS@EHOUR DS    PL2                      END   TIME (HH)
JS@EMINS DS    PL2                      END   TIME (MM)
JS@LINE  DS    CL(79+2)
         ORG   JS@LINE
JS@STEPN DS    CL8,CL2
JS@PROCS DS    CL8,CL1
JS@ATTR1 DS    XL2
JS@CODE  DS    CL4,CL1
JS@ATTR2 DS    XL2
JS@DAYS  DS    CL7,CL1
JS@HOURS DS    CL5,CL2
JS@TCB   DS    CL8,CL2
JS@SRB   DS    CL8,CL2
JS@DISK  DS    CL8,CL2
JS@TAPE  DS    CL8
         ORG   JS@LINE+L'JS@LINE
JS@LINEL EQU   *-JS@LINE
         SPACE 1
JS@WORKL EQU   *-WORK
JS@DWD   DS    D
JS@WORD  DS    CL8
JS@TEMP  DS    CL10
         SPACE 3
         DROPX R13                      WORK
         SPACE 3
         Q$PDDB
         Q$LRC
         QZKEYS
         QZTEXT
