//XSDMNDMM JOB $$$$,DAVE,MSGCLASS=X,CLASS=Q,NOTIFY=C4SYSDM
//ASMHCL   PROC MAC1='SYS1.MACLIB',MAC2='SYS1.MODGEN'
//ASM    EXEC  PGM=IEV90,PARM='NODECK,OBJECT',REGION=1M
//SYSLIB   DD  DSN=&MAC1,DISP=SHR
//         DD  DSN=&MAC2,DISP=SHR
//SYSUT1   DD  UNIT=(3390,SEP=SYSLIB),SPACE=(CYL,(10,5)),DSN=&SYSUT1
//SYSPUNCH DD  SYSOUT=B,DCB=(BLKSIZE=800),SPACE=(CYL,(5,5,0))
//SYSPRINT DD SYSOUT=*,DCB=(BLKSIZE=3509),UNIT=(,SEP=(SYSUT1,SYSPUNCH))
//SYSLIN   DD  DISP=(,PASS),UNIT=3390,SPACE=(CYL,(5,5,0)),             *
//             DCB=(BLKSIZE=400),DSN=&&LOADSET
//LKED EXEC  PGM=IEWL,PARM='MAP,LET,LIST,NCAL,AMODE=24,RMODE=24',      *
//        REGION=1M,COND=(8,LT,ASM)
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLMOD DD DISP=(,PASS),UNIT=3390,                                   *
//        SPACE=(CYL,(2,1,2)),DSN=&&GOSET(GO)
//SYSUT1   DD  UNIT=3390,SPACE=(CYL,(3,2)),DSN=&SYSUT1
//SYSPRINT DD  SYSOUT=*
//  PEND
// EXEC ASMHCL
LSPACE   CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
CVTPTR   EQU   X'010'
CVTUCBSC EQU   X'434'
         USING *,R15
         STM   R14,R12,12(R13)
         LA    R12,SAVE
         ST    R12,8(R13)
         ST    R13,SAVE+4
         LR    R13,R12
         LR    R12,R15
         USING LSPACE,R12
         DROP  R15
         L     R2,0(R1)
         LA    R3,0
         LH    R3,0(R2)
         ST    R3,PFLAG
         LINK EP=KLEAR
         OPEN  (SYSPRINT,OUTPUT)
         PUT   SYSPRINT,HDR1
         PUT   SYSPRINT,HDR2
         PUT   SYSPRINT,HDR3
         PUT   SYSPRINT,HDR4
         PUT   SYSPRINT,HDR5
         PUT   SYSPRINT,HDR6
*EXTUCB  L     R15,CVTPTR
*        L     R15,CVTUCBSC(,R15)
*        CALL (15),(WORKAREA,DEVCLASS,ADDRUCB),VL
*        LTR   R15,R15
*        BNZ   ENDJC
NEXTUCB  DS    0H
U        UCBSCAN COPY,WORKAREA=WORKAREA,UCBAREA=UCBAREA,RANGE=ALL,     1
               DYNAMIC=YES,DEVCLASS=DASD,RETCODE=RET,RSNCODE=RSN
*        SR    R6,R6
         L     R6,RET
         LTR   R6,R6
         BNZ   ENDJC
*        MVC   0(48,R5),UCBAREA
*        B     CONT
         SR    R6,R6
         LA    R6,UCBAREA   UCB ADDRESSES
         B     FNDGBL
*
         LR    R6,R5
         LTR   R6,R6        CHECK FOR VALID ENTRIES
         BZ    INCR1        UCB HOLE
         C     R6,FMIN1     CHECK FOR END
         BE    ENDJC
         CLI   18(R6),X'20' MUST BE DIRECT ACCESS
         BNE   INCR1
         C     R6,LASTADR   UCB ADDRESSES MUST INCREASE
         BNH   INCR1        OTHERWISE THEY REPEAT.
         ST    R6,LASTADR   NEW ADDRESS
FNDGBL   TM    3(R6),X'80'  ONLINE BIT
         BZ    INCR1        NOPE
CHKRDY   TM    6(R6),X'40'  TEST READY BIT
         BO    INCR1        NOT READY
         TM    17(R6),X'08' VIRTUAL UCB
         BO    INCR1        YES
         TM    34(R6),X'08'
         BNO   INCR1
         B     FNDUCB
INCR1    DS    0H
         B     NEXTUCB      TRY NEXT UCB
*
**  MOVE UCB INFORMATION TO OUTPUT LINE
*
FNDUCB   MVC   VOLID,28(R6) MOVE VOLID
         MVC   ADDR,13(R6)  MOVE UNIT ADDRESS
         MVC   PG,=C'      '
         MVC   MTD,=C'    '
         TM    34(R6),X'40'    TEST PAGE PK
         BNO   TPBL            TO TEST PUBLIC
         MVC   PG,=C'PAGEPK'
TPBL     TM    34(R6),X'08'    TEST PUBLIC
         BNO   TSCR            TO TEST STORAGE
         MVC   MTD,=C'SCR'     WAS PUBLIC
         B     NPRIV
TSCR     TM    34(R6),X'04'    TEST STORAGE
         BNO   NPRIV           TO TEST SYSRES
         MVC   MTD,=C'SCR'     WAS STORAGE
NPRIV    MVC   SY,=C'      '
         TM    3(R6),X'02'     TEST SYSRES
         BNO   NDM             NOT SYSRES
         MVC   SY,=C'SYSRES'   WAS SYSRES
*
**  IF OFFLINE, DO NOT PROCESS
*
NDM      TM    3(R6),X'40'  PENDING BIT - SHOULD BE OFF
         BO    INCR1
         TM    3(R6),X'80'  ONLINE BIT - SHOULD BE ON
         BZ    INCR1
*
*
         LA    R1,LSP       POINT TO WORKAREA
         LR    R0,R6        POINT TO UCB
         SVC   78           ISSUE LSPACE SVC
         MVC   O1,ADDR
         MVC   O2,VOLID
         MVC   O3,I3
         MVC   O4,I4
         MVC   O5,I5
         MVC   O6,I6
         MVC   O7,I7
         MVC   O8,MTD
         MVC   O9,SY
         MVC   O10,PG
         MVC   O11,=C'          '
         TM    34(R6),X'04'    TEST STORAGE
         BNO   PUTNSCR
         CLC   VOLID(3),=C'C48'
         BE    C48
         CLC   I3,=C'0111'
         BH    PUTST
         MVC   O11,CMT
         B     PUTST
C48      CLC   I3,=C'0177'
         BH    PUTST
         MVC   O11,CMT
         B     PUTST
PUTNSCR  DS    0H
         LA    R1,1
         L     R2,PFLAG
         CR    R1,R2
         BE    INCR1
PUTST    PUT   SYSPRINT,OUT
         B     INCR1
ENDJC    DS    0H
         CLOSE (SYSPRINT)
         L     R13,4(R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
SYSPRINT DCB   DSORG=PS,MACRF=(PM),DDNAME=SYSPRINT,LRECL=79,RECFM=FB,  X
               BLKSIZE=790
SAVE     DC    19F'0'
ADDRUCB  DS    A
WORKAREA DC    XL100'00'
UCBAREA  DC    XL50'00'
RET      DC    F'0'
RSN      DC    F'0'
DEVCLASS DC    X'00'
         DS    0F
FMIN1    DC    X'0000FFFF'    END OF UCB LIST
LASTADR  DS    F       LAST UCB ADDRESS FOUND ( NO DUP'S )
PFLAG    DS    F       FLAG FOR PARM NEQ 0
VOLSET   XC    LASTADR,LASTADR CLEAR THE UCB COMPARE ADDRESS
ADDR     DS    CL3     UCB ADDRESS IN CHARACTERS
MTD      DS    CL3     VOLUME MOUNTED AS
PG       DS    CL6     VOLUME ? PAGING
SY       DS    CL6     ? SYSRES
CMT      DC    CL10'>=80% FULL'
         DC    CL1' '
VOLID    DS    CL6     CURRENT VOLUME SERIAL NUMBER TO PROCESS
         DC    CL1' '
LSP      DS    0C
         DS    CL6     'SPACE='
I3       DS    CL4     TOTAL NUMBER OF FREE CYLINDERS
         DS    CL1     ','
I4       DS    CL4     TOTAL NUMBER OF FREE TRACKS
         DS    CL1     ','
I5       DS    CL4     NUMBER OF EXTENTS
         DS    CL1     '/'
I6       DS    CL4     LARGEST EXTENT OF CONTIG CYLINDERS
         DS    CL1     ','
I7       DS    CL4     LARGEST EXTENT OF CONTIG TRACKS
         DS    39C' '
HDR1     DC    CL80'            TOTAL  TOTAL  NUMBER  LARGEST LARGEST'
HDR2     DC    CL80'            NUMBER NUMBER         CONTIG  CONTIG '
HDR3     DC    CL80'UCB VOLSER    OF     OF     OF    EXTENT  EXTENT '
HDR4     DC    CL80'             FREE   FREE           CYLS    TRKS  '
HDR5     DC    CL80'             CYLS   TRKS  EXTENTS                '
HDR6     DC    CL80'                                                 '
*                   XXX XXXXXX   XXXX   XXXX   XXXX    XXXX    XXXX
OUT      DS    0F
O1       DS    CL3  XXX
         DC    CL1' '
O2       DS    CL6      XXXXXX
         DC    CL3' '
O3       DS    CL4               XXXX
         DC    CL3' '
O4       DS    CL4                      XXXX
         DC    CL3' '
O5       DS    CL4                             XXXX
         DC    CL4' '
O6       DS    CL4                                     XXXX
         DC    CL4' '
O7       DS    CL4                                             XXXX
         DC    CL4' '
O8       DS    CL3
         DC    CL1' '
O9       DS    CL6
         DC    CL1' '
O10      DS    CL6
         DC    CL1' '
O11      DS    CL10
         DC    CL1' '
         LTORG
         END
//*KED.SYSLMOD DD DSN=SYS1.MISC.LINKLIB(LSPACE),DISP=SHR
//LKED.SYSLMOD DD DSN=C4SYSDM.LOADLIB(SCR),DISP=SHR
