//ASMI03AW JOB CLASS=Q,
//        NOTIFY=XSDMN,MSGCLASS=X
//*
//ASMHCL  PROC
//C      EXEC  PGM=IEV90,PARM=OBJECT,REGION=5M
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=(SYSDA,SEP=SYSLIB),SPACE=(CYL,(10,5)),DSN=&SYSUT1
//SYSPUNCH DD  SYSOUT=B,DCB=(BLKSIZE=800),SPACE=(CYL,(5,5,0))
//SYSPRINT DD SYSOUT=X,DCB=(BLKSIZE=3509),UNIT=(,SEP=(SYSUT1,SYSPUNCH))
//SYSLIN   DD  DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(5,5,0)),            *
//             DCB=(BLKSIZE=400),DSN=&&LOADSET
//L    EXEC  PGM=IEWL,PARM='MAP,LET,LIST,NCAL',REGION=2M,COND=(8,LT,C)
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLMOD DD DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(2,1,2)),DSN=&GOSET(GO)
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(3,2)),DSN=&SYSUT1
//SYSPRINT DD  SYSOUT=X,DCB=(RECFM=FB,BLKSIZE=3509)
//  PEND
//*
//*------------------------------------------------------------------*
//*        INSTALLATION OF TSO COMMAND  ' I O '                      *
//*------------------------------------------------------------------*
//IO       EXEC ASMHCL,PARM.L='AMODE=31,RMODE=24'
//SYSPRINT DD SYSOUT=*
*          DATA SET TSOIO      AT LEVEL 001 AS OF 12/03/79
*          DATA SET TSOIO      AT LEVEL 004 AS OF 05/05/73
         TITLE 'IO - SPNB TCT I/O DISPLAY COMMAND'
*
* DISPLAYS NON ZERO I/O COUNTS FROM CALLER'S TCTIOT
         MACRO
&N       EDIT  &T
&N       CVD   R1,DOUBLE
         MVC   &T,=X'40206B2020206B202120'
         ED    &T,DOUBLE+4
         CLC   =A(0),DOUBLE
         BE    *+10
         MVC   &T,=CL10' *********'
         MEND
         SPACE 2
         MACRO
&N       CLEAR &A
         LCLC  &L
&L       SETC  'L'''
&N       MVI   &A,C' '
         MVC   &A+1(&L&A-1),&A
         MEND
         EJECT
IO       CSECT
         SAVE  (14,12),T,IO-&SYSDATE-&SYSTIME
         LR    R12,R15
         USING IO,R12
         LA    R15,SAVEAREA
         ST    R13,4(R15)
         ST    R15,8(R13)
         LR    R13,R15
         LR    R2,R1                    SAVE PARAMETERS
         SR    R8,R8                    ZERO EXCP TOTAL COUNTER
         SPACE 1
* IN THE FOLLOWING CODE:
*    R2 = A(TCB)
*    R3 = A(TCT)
*    R4 = A(TIOT)
*    R8 = EXCP TOTAL COUNTER
         SPACE 1
ONLYME   L     R1,16                    FIND CVT
         L     R1,0(R1)                 FIND DOUBLE WORD
         L     R2,4(R1)                 FIND TCB
GOTTCB   L     R3,164(R2)               FIND TCT
         L     R4,12(R2)                FIND TIOT
         MVC   L1UID,0(R4)              MOVE IN USERID
         LTR   R3,R3                    NULL TCT POINTER?
         BZ    NOTCT
         L     R1,52(R3)                TPUT COUNT
         EDIT  L1TPUT
         L     R1,48(R3)                TGET COUNT
         EDIT  L1TGET
         TPUT  LINE1,LLINE1
         EJECT
* THIRD AND FOLLOWING LINE PROCESSING
         SPACE 1
* IN THE FOLLOWING, R2 = A(TCTIOT)
*                   R5 = A(DD LOOK UP ENTRY)
         SPACE 1
         L     R2,12(R3)                FIND TCTIOT
         LA    R5,8(R2)                 FIND FIRST LOOK UP ENTRY
         SPACE 1
DDLOOP   NI    FLAGS,255-TPUTREQ        TURN OFF TPUT REQUIRED FLG
         L     R6,0(R5)                 TIOT OFFSET
         L     R7,4(R5)                 TCT OFFSET
         LA    R6,0(R4,R6)              R6 = A(TIOT ENTRY)
         MVC   LNDDNAME,4(R6) MOVE IN DDNAME
         LA    R7,0(R2,R7)              R7 = A(TCTIOT ENTRY)
         SR    R6,R6                    COUNTER FOR UCB ENTRIES
         IC    R6,4(R7)                 SET COUNTER
         LTR   R6,R6                    ANY UCBS?
         BZ    SKIPDD                   NO, SKIP THIS ONE
         SPACE 1
DDLOOP1  LA    R10,14                   LINE INCREMENT
         LA    R11,LNAEND               COMPARAND
         LA    R9,LNAREA                INDEX
DDLOOP2  L     R1,8(R7)                 LOAD EXCPS
         LTR   R1,R1 .                  ZERO?
         BZ    SKIPUCB .                YA, SKIP THIS ONE
         AR    R8,R1                    ADD TO EXCP ACCUM.
         OI    FLAGS,TPUTREQ            TURN ON TPUT REQUIRED FLG
         EDIT  0(10,R9)
         MVI   10(R9),C'/'
         L     R1,0(R7)                 UCB ADDR
         MVC   11(3,R9),13(R1)          MOVE IN ADDRESS
SKIPUCB  BCT   R6,NEXTDEV .             ANOTHER UCB?
         B     NEXTDD .                 NO, TPUT LINE
NEXTDEV  LA    R7,8(R7)                 MOVE TO NEXT TCTIOT ENTRY
         BXLE  R9,R10,DDLOOP2 .         LOOP 4 NEXT DEV
         SPACE 1
         TM    FLAGS,LINE2PUT           WAS THE HEADER TPUT?
         BO    DONT1                    YA, DON'T DO IT AGAIN
         TPUT  LINE2,LLINE2             ELSE DO IT
         OI    FLAGS,LINE2PUT           AND SET FLAG
         SPACE 1
DONT1    TPUT  LINEN,LLINEN .           FOR TO  MANY FOR 1 LINE
         CLEAR LINEN
         B     DDLOOP1
         SPACE 1
NEXTDD   TM    FLAGS,TPUTREQ            SEE IF ANYTHING TO PUT
         BZ    SKIPDD
         SPACE 1
         TM    FLAGS,LINE2PUT           HEADER OUTPUT?
         BO    DONT2                    YA, DON'T REPEAT
         TPUT  LINE2,LLINE2             ELSE TPUT IT
         OI    FLAGS,LINE2PUT           AND SET FLAG
         SPACE 1
DONT2    TPUT  LINEN,LLINEN
SKIPDD   CLEAR LINEN
         LA    R5,8(R5) NEXT DD LOOK UP TAB ENTRY
         CLC   =A(0),0(R5)              E-O TABLE?
         BNE   DDLOOP
         MVC   LINEN(8),=CL8'TOTAL:'    LIST TOTALS
         LR    R1,R8
         EDIT  LNAREA(10)
         TPUT  LINEN,LLINEN
EXIT     L     R13,4(R13)               BACK UP ONE SA
         RETURN (14,12),T,RC=0
         SPACE 1
NOTCT    MVC   NOTCTM(8),L1UID          MOVE IN JOBNAME
         TPUT  NOTCTM,L'NOTCTM          TELL CALLER
         LA    R4,R4                    CHECK TIOT POINTER
         LTR   R4,R4
         BNZ   TIOTOK
         TPUT  NTIOTM,L'NTIOTM
         B     EXIT
TIOTOK   TPUT  NTHEADER,L'NTHEADER
         SPACE 1
* REGISTER USAGE IS AS FOLLOWS:
*   R3 = OUTPUT LINE SCAN POINTER
*   R4 = TIOT ENTRY POINTER
*   R5 = LENGTH OF CURRENT TIOT ENTRY
*   R6 = UCB COUNTER
*   R7 = UCB POINTER
*   R8 = OUTPUT LINE SCAN BXLE INCREMENT (4)
*   R9 = OUTPUT LINE SCAN BXLE COMPARAND
*
         SPACE 1
         LA    R4,24(R4)                MOVE TO FIRST ENTRY
ELOOP    SR    R5,R5                    ZERO COUNTERS
         SR    R6,R6
         IC    R5,0(R4) .               LENGTH OF TIOT ENTRY
         IC    R6,2(R4) .               NO. OF UCBS
         CLEAR NTLINE
         MVC   NTLINE(8),4(R4)          MOVE IN DDNAME
         LA    R8,4                     BXLE INCREMENT
         LA    R9,NTLINE+L'NTLINE-4     END OF NTLINE
         LA    R3,NTLINE+10             START OF UNIT LIST
         LTR   R6,R6                    ANY UCBS?
         BZ    NTPUT
         LA    R7,16(R4)                1ST UCB POINTER
         B     NTLOOP1
NTLOOP   MVI   3(R3),C','
         BXLE  R3,R8,NTLOOP1
         TPUT  NTLINE,L'NTLINE
         CLEAR NTLINE
         LA    R3,NTLINE+8
         SPACE 1
NTLOOP1  LH    R1,0(R7)                 FIND UCB
         MVC   0(3,R3),13(R1)           MOVE IN UNIT NAME
         LA    R7,4(R7)                 BUMP TO NEXT UCB ENTRY
         BCT   R6,NTLOOP
NTPUT   TPUT  NTLINE,L'NTLINE
         AR    R4,R5                    MOVE TO NEXT ENTRY
         CLI   0(R4),0                  END OF TIOT?
         BE    EXIT
         BNE   ELOOP
         TITLE 'IO - CONSTANTS AND WORK AREAS '
SAVEAREA DC    18F'0'
LINE1    DC    C'  I/O SUMMARY FOR USER: '
L1UID    DC    CL8' '
         DC    C'    TPUTS:'
L1TPUT   DC    CL10' '
         DC    C'    TGETS:'
L1TGET   DC    CL10' '
LLINE1   EQU   *-LINE1
         SPACE 1
LINE2    DC    C'DDNAME       EXCPS/UNIT             '
LLINE2   EQU   *-LINE2
         SPACE 2
LINEN    DS    0CL80
LNDDNAME DC    CL8' '
LNAREA   DC    CL72' '
LNAEND   EQU   *-14
LLINEN   EQU   *-LINEN
DOUBLE   DS    D
         SPACE 1
FLAGS    DC    F'0'                     WORK FLAGS
TPUTREQ  EQU   X'80'
LINE2PUT EQU   X'40'                    HEADER LINE WAS TPUT
NTLINE   DC    CL80' '
NTHEADER DC    C'DDNAME   UNIT(S)'
NOTCTM   DC    C'XXXXXXXX - HAS NO TASK TIMING TABLE.'
NTIOTM   DC    C'      NOR HAS IT A TASK INPUT OUTPUT TABLE.'
         SPACE
         LTORG
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END   IO
//L.SYSLMOD DD DSN=TEST.PGM.LOAD(IO),DISP=SHR
/*
