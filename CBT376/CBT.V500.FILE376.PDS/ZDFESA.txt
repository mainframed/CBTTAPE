//XSDMNZDF  JOB MSGCLASS=X,CLASS=Q,NOTIFY=XSDMN
//*
//*
//ASMHCL  PROC
//C      EXEC  PGM=IEV90,PARM=OBJECT,REGION=5M
//SYSLIB   DD  DSN=SYS1.MACLIB,DISP=SHR
//SYSUT1   DD  UNIT=(SYSDA,SEP=SYSLIB),SPACE=(CYL,(10,5)),DSN=&SYSUT1
//SYSPUNCH DD  SYSOUT=B,DCB=(BLKSIZE=800),SPACE=(CYL,(5,5,0))
//SYSPRINT DD SYSOUT=X,DCB=(BLKSIZE=3509),UNIT=(,SEP=(SYSUT1,SYSPUNCH))
//SYSLIN   DD  DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(5,5,0)),            *
//             DCB=(BLKSIZE=400),DSN=&&LOADSET
//L    EXEC  PGM=IEWL,PARM='MAP,LET,LIST,NCAL',REGION=2M,COND=(8,LT,C)
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  DDNAME=SYSIN
//SYSLMOD DD DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(2,1,2)),DSN=&GOSET(GO)
//SYSUT1   DD  UNIT=SYSDA,SPACE=(CYL,(3,2)),DSN=&SYSUT1
//SYSPRINT DD  SYSOUT=X,DCB=(RECFM=FB,BLKSIZE=3509)
//  PEND
//*
// EXEC ASMHCL,PARM.C='OBJECT,NODECK,XREF(FULL)',
// PARM.L=(XREF,LET,LIST,'AMODE=31','RMODE=24',RENT,REUS)
//C.SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//*          DD DSN=SYS1.AMACLIB,DISP=SHR
//           DD DSN=SYS1.MODGEN,DISP=SHR
//C.SYSIN DD  *
***********************************************************************
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* EXECUTION DISPLAY FACILITY (ZDF)  R8  R9   R11    WORKING REGS      *
*                                                                     *
* ABSTRACT: DISPLAY STATUS OF JES2 JOBS, LOGONS, & STARTED TASKS.     *
*                                                                     *
***********************************************************************
*
*    JOB       JOB NAME OF MEMORY
*    STEP      FOR BATCH JOBS WILL ALWAYS BE INIT.
*              FOR TSO JOBS AND STARTED TASKS WILL BE ACTUAL STEP NAME.
*    P         REASON CODE FOR SWAP OUT FROM OUCBPFL.
*              THE FOLLOWING ARE THE REASON CODES DISPLAYED:
*                U  0A MEMORY WAS UNILATERALLY SWAPPED
*                X  09 MEMORY WAS EXCHANGE SWAPPED
*                E  08 AN ENQHOLD HAS BEEN RECEIVED FOR THIS MEMORY
*                S  07 MEMORY REQUESTED SWAP OUT
*                D  06 INVOLUNTARY SWAPOUT DUE TO DETECTED WAIT
*                R  05 SWAPOUT CAUSED BY REAL STORAGE SHORTAGE
*                A  04 SWAPOUT CAUSED BY AUX. STORAGE SHORTAGE
*                L  03 MEMORY SWAPPED OUT BY MS6 LONG WAIT DETECTION
*                I  02 MEMORY IS IN INPUT TERMINAL WAIT
*                T  01 MEMORY IS IN OUTPUT TERMINAL WAIT
*                ?     UNABLE TO DETERMINE SWAPOUT REASON.
*    MEMORY    CURRENT AMOUNT OF MEMORY ALLOCATED TO THIS MEMORY.
*    WSS       SRM'S VIEW OF THE WORKING SET SIZE FOR THIS MEMORY.
*    T         TYPE OF MEMORY (S=STC OR BATCH, T=TSO USER)
*    LOC       CURRENT LOCATION OF THIS MEMORY:
*                IN  SWAPPED IN AND ELIGIBLE TO RUN.
*                OUT SWAPPED OUT BUT READY TO RUN.
*                WT  SWAPPED OUT AND NOT READY TO RUN.
*                N/S SWAPPED IN AND V=R OR NON-SWAPPABLE STATUS.
*                <-> TRANSITIONING BETWEEN STATES.
*    DP        DISPATCHING PRIORITY OF MEMORY (IN HEX).
*    PG        PERFORMANCE GROUP.
*    SC        NUMBER OF TIMES THE MEMORY HAS BEEN SWAPPED IN THE
*              CURRENT TRANSACTION.
*    WMR       CURRENT SRM WORKLOAD MANAGER RECOMMENDATION VALUE.
*              (ACTUALLY THE WORKLOAD LEVEL AT WHICH THIS MEMORY IS
*               OPERATING).
*    CPU       CPU TIME IN SECONDS USED BY THE MEMORY (SRB PLUS TASK).
*
***********************************************************************
*
*  CHANGE LOG
*
*  OCT 94   ESA UPDATE      - DM WA EDUC DEPT
*
***********************************************************************
ZDF      CSECT
         USING *,R12,R2
         SAVE  (14,12)
         LR    R12,R15
         LR    R2,R15
         A     R2,L4096       BASE 2
*
         LA    R0,4095
         GETMAIN R,LV=(0)
         LR    R5,R1
         LA    R0,4095
         GETMAIN R,LV=(0)
         ST    R1,8(R13)     LINK SAVE AREAS
         ST    R13,4(R1)     SAVE THE CALLER'S R13
         LR    R13,R1
         USING EDWA,R13
         ST    R5,CPUTABI
*
ZDFAGAIN XC    ISPARMS(40),ISPARMS
*
         L     R15,ISPLINK
         CALL  (15),(VLIBDEF,VISP,VDATASET,VPANELS),VL,MF=(E,ISPARMS)
*                                         INIT EDWA DSECT AREAS
         XC    MASTCPUS,MASTCPUS
         XC    HOLDTOD,HOLDTOD
         XC    OLDTOTL,OLDTOTL
         XC    OLDNO,OLDNO
         USING CPUTABL,R5
         L     R5,CPUTABI
         MVC   CPUTAB(26),SPACES
         MVI   CPUTABE,X'FF'
         DROP  R5
         MVC   TABAREA(80),SPACES
         MVC   ZDFTSO(18),SPACES
*
SHOWAGIN DS    0H
         MVI   HSMFLAG,C'0'
         BAL   R7,SHOWRMF     DISPLAY THE RESOURCE STUFF
*
         L     R15,ISPLINK
         CALL  (15),(VDELETE,ASTER),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(TBEND,ZDFTABLE),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VLIBDEF,VISP,VDATASET,VPANCLR),VL,MF=(E,ISPARMS)
*
         CLI   ZDFRPT,C'0'                BRANCHING OUT OF PGM
         BE    ZENDTEST
*
AUTCONT  MVC   VQWORK(32),SPACES
         MVC   VQWORK(12),VDELAY
         MVC   VQWORK+10(2),ZDFRPT
*
*        L     R15,ISPLINK
*        CALL (15),(VSELECT,L48,VADDALL),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL (15),(VSELECT,L64,VADDSYSP),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL (15),(VSELECT,L32,VQWORK),VL,MF=(E,ISPARMS)
*
         C     R15,FZEROS                WAS ATTN HIT ??
         BNE   ZENDTEST
*
         L     R15,ISPLINK
         CALL (15),(VSELECT,L48,VDELSYSP),VL,MF=(E,ISPARMS)
*
         LA    R11,0
         B     ZENDC
ZENDTEST LA    R11,8                      B ZDFEND COMING UP
ZENDC    DS    0H
*
         C     R11,FZEROS
         BNE   ZDFEND
*
         B     ZDFAGAIN
ZDFEND   DS    0H
         LA    R0,4095
         L     R1,CPUTABI
         FREEMAIN R,LV=(0),A=(1)
         LR    R1,R13
         L     R13,4(R13)     RESTORE THE CALLER'S R13
         FREEMAIN R,LV=(0),A=(1)
         RETURN (14,12),RC=0  RETURN TO OUR CALLER
*
L4096    DC    F'4096'
*****************************************************
*                                                   *
*     SHOWRMF ROUTINE START                         *
*                                                   *
*****************************************************
SHOWRMF  DS    0H
         ST    R7,ZDF7HOLD SAVE THE RETURN ADDRESS
*
         MVC   SELCODE(3),SPACES  SET SELECTION CODE TO SPACES
*
         L     R15,ISPLINK
         CALL  (15),(VDELETE,ASTER),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,SUP,SU,CHAR,L16),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,SERP,SER,CHAR,L25),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,ACTLINEP,TABAREA,CHAR,L37),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,STATP,STATUS,CHAR,L5),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,ACTLINBP,SIZE,CHAR,L34),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDLINEP,ZDFCMD,CHAR,L3),VL,MF=(E,ISPARMS)
         MVC   ZDFCMD(3),SPACES
*
*        L     R15,ISPLINK
*        CALL  (15),(VDEF,CMDWHERE,WHERE,CHAR,L3),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDRPT,ZDFRPT,CHAR,L2),VL,MF=(E,ISPARMS)
         MVC   ZDFRPT(2),SPACES
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDPREEP,ZDFPRE,CHAR,L8),VL,MF=(E,ISPARMS)
         MVC   ZDFPRE(8),SPACES
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDJES3,ZDFJS3,CHAR,L1),VL,MF=(E,ISPARMS)
         MVC   ZDFJS3(1),SPACES
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDCPU,ZDFCPU,CHAR,L4),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDAFC,ZDFAFC,CHAR,L4),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDPAG,ZDFPAG,CHAR,L4),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDTPR,ZDFTPR,CHAR,L4),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDUIC,ZDFUIC,CHAR,L4),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDMGB,ZDFMGB,CHAR,L4),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDTSO,ZDFTSO,CHAR,L18),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDJOBN,ZDFJBN,CHAR,L4),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDJOBC,ZDFJBC,CHAR,L1),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CMDJOBA,ZDFJBA,CHAR,L8),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,PIKP,SELCODE,CHAR,L1),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,HSMP,HSMINFO,CHAR,L80),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(TBCR,ZDFTABLE,NULL,DANAMLST,NOW),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(TBVCLEAR,ZDFTABLE),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL (15),(VGET,CMDPREEP,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL (15),(VGET,CMDJES3,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL (15),(VGET,CMDRPT,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL (15),(VGET,CMDLINEP,SHARED),VL,MF=(E,ISPARMS)
*
         CLC   ZDFCMD(3),SPACES
         BNE   FRPT
         MVC   ZDFCMD(3),BAT    INITIAL COMMAND - SHOW EVERYTHING
*
FRPT     CLC   ZDFRPT(2),SPACES
         BNE   FINDCVT
         MVI   ZDFRPT,C'0'
*
FINDCVT  DS    0H
*
**  LOOK THRU SRM CONTROL BLOCKS TO BUILD HEADER INFO
*
BUILDHDR DS    0H
         L     R4,16           ---> CVT...
         USING CVT,R4
         ST    R4,CVTPTRX
         L     R7,CVTPCCAT
         ST    R7,PCCATPTR
         L     R7,CVTHSM
         ST    R7,HSMACT
         L     R7,CVTASVT       TO ASVT
         ST    R7,ASVTPTR       TO ASVT
         L     R7,CVTRCEP
         ST    R7,RCEHOLD
         L     R7,CVTOPCTP     ---> RMCT... 1ST MODULE OF IRARMCNS
         ST    R7,RMCTPTR
         L     R7,CVTSMCA
         ST    R7,SMCAPTR
         L     R7,CVTCSD
         ST    R7,CSDPTR
         DROP  R4
*
         L     R4,RMCTPTR
         L     R5,4(R4)        ---> IRACCT IN RMCT
         LH    R6,102(R5)      **  CPU UTILIZATION **
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  ZDFCPU,WORK+6(2)     ...AND EDIT
*
         USING RMCT,R4            ADDRESS RMCT
         L     R7,RMCTRCT         LOAD RCT ADDRESS
         USING RCTDSECT,R7        ADDRESS RCT
*
         L     R9,M16
         LA    R8,0
         D     R8,RMCTADJC     **  CPU ADJUSTMENT                **
         CVD   R9,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  SU-1(5),WORK+5(3)  ...AND EDIT
         MVI   SU+4,C'/'
         L     R9,CSDPTR
         USING CSD,R9
         LH    R8,CSDCPUOL         R8 = CPUS ONLINE
         CVD   R8,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  SU+5(1),WORK+6(2)  ...AND EDIT
         MVC   SU+6(1),SPACES
         LA    R8,0
         IC    R8,CSDPLPN          R8 = PRSM #
         CVD   R8,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  SU+7(1),WORK+6(2)  ...AND EDIT
         MVC   SU+8(1),SPACES
         LH    R8,RCVCPUA      **  CPU UTILIZATION **
         CVD   R8,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  SU+9(4),WORK+6(2)    ...AND EDIT
         DROP  R9
*
         L     R9,CVTPTRX
         S     R9,=F'40'
         MVC   SER(8),0(R9)
         MVC   SER+8(1),SPACES
*
         L     R9,SMCAPTR
         USING SMCABASE,R9
         MVC   SER+9(4),SMCASID
         DROP  R9
         MVC   SER+13(1),SPACES
*
         L     R9,PCCATPTR
         L     R9,0(R9)
         MVC   SER+14(6),4+2(R9)
         MVC   SER+20(1),SPACES
         MVC   SER+21(4),10+2(R9)
*
         LH    R6,RCVAFQA      **  AVERAGE AVAILABLE FRAME COUNT **
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  ZDFAFC,WORK+6(2)     ...AND EDIT
*
         LH    R6,RCVPTR       ** PAGE FAULT RATE **
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  ZDFPAG,WORK+6(2)     ...AND EDIT
*
         LH    R6,RCVPAGRT     ** TPR **
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  ZDFTPR,WORK+6(2)     ...AND F ' DED'
*
         LH    R6,RCVUICA      ** UNREFERENCED INTERVAL COUNT **
         CVD   R6,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  ZDFUIC,WORK+6(2)     ...AND EDIT
*
         L     R6,RCEHOLD      ** MEGABYTES **
         L     R5,4(R6)
         SRL   R5,8                * 4096 / 1024 /1024
         CVD   R5,WORK             CONVERT TO DECIMAL...
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  ZDFMGB,WORK+6(2)     ...AND EDIT
*
         L     R7,RMCTDMDT     ** TSO P1/2 3 4 DOMAIN INFO **
         SR    R15,R15         CLEAR A REG
         LA    R5,13           ** PERFORM GROUP SKIP **
         LA    R14,ZDFTSO      POINT TO FIRST DOMAIN ENTRY
         USING DMDT,R7
DOTSO    LA    R7,DMDTLEN(R7)  BUMP PAST FIRST DOMAIN
         BCT   R5,DOTSO
         LA    R5,3            NUMBER OF DOMAIN ENTRIES TO PROCESS
DODMNS   DS    0H
         LH    R15,DMDTMPLT    GET MULTI-PROGRAMMING LEVEL TARGET
         CVD   R15,WORK        CHANGE IT TO DECIMAL
         OI    WORK+7,X'0F'    FIX THE SIGN
         UNPK  1(2,R14),WORK+6(2) MOVE IT TO THE OUTPUT AREA
         MVI   3(R14),C':'
         LH    R15,DMDTCMPL    GET CURRENT MPL
         CVD   R15,WORK        CHANGE IT TO DECIMAL
         OI    WORK+7,X'0F'    FIX THE SIGN
         UNPK  4(2,R14),WORK+6(2) MOVE IT TO THE OUTPUT AREA
         LA    R14,6(R14)     POINT TO NEXT DOMAIN ENTRY
         LA    R7,DMDTLEN(R7)  BUMP PAST THIS DOMAIN
         C     R5,=F'3'
         BNE   TOBCT
         LA    R7,DMDTLEN(R7)  BUMP PAST THIS DOMAIN
*    PAST DOMAIN EX TSO P2
         C     R7,RMCTDMDE
         BH    DOMFAIL         MIGHT DO THIS IF NOT #3 OR IPS CHANGE
TOBCT    BCT   R5,DODMNS       GO PROCESS ANOTHER
*
DOMFAIL  CLI   ZDFJS3,C'Q'
         BNE   HBAL
*
HD3      L     R15,ISPLINK                                       OPT Q
         CALL  (15),(VSELECT,L64,VALLC),VL,MF=(E,ISPARMS)        OPT Q
*                                                                OPT Q
         L     R0,=F'4096'           ** SET TO 4096              OPT Q
         GETMAIN R,LV=(0)            ** OBTAIN STORAGE AREA      OPT Q
         ST    R1,QSAVEUT                                        OPT Q
         LA    R8,MVPARM                                         OPT Q
         LA    R9,SYSUTF                                         OPT Q
         SR    R8,R9                                             OPT Q
         BCTR  R8,0                                              OPT Q
         EX    R8,MVPARM                                         OPT Q
*        MVC   0(102,R1),SYSUTF      MOVE DCB INTO GETMAIN AREA  OPT Q
         LR    R5,R1                                             OPT Q
         OPEN  ((R5),INPUT)                                      OPT Q
         L     R0,=F'4096'      ** ALLOCATE FLASHER JCT AND OPEN OPT Q
         GETMAIN R,LV=(0)       ** OBTAIN STORAGE AREA AND INIT  OPT Q
         ST    R1,R1QAREA                                        OPT Q
         ST    R1,QAREAOFF                                       OPT Q
         MVC   0(8,R1),SPACES                                    OPT Q
         LA    R1,0                                              OPT Q
         ST    R1,EOFMARK            ** PUT 0 IN EOF MARKER      OPT Q
*                                                                OPT Q
HBAL     DS    0H
         LA    R8,0
         ST    R8,CPUTOTL
         ST    R8,CPUNO
         L     R8,RMCTTOD        PICK UP TIME OF DAY FROM RMCT.  (SU)
         ST    R8,HTOD
         S     R8,HOLDTOD        CALC DELTA TOD
         SRDL  R8,32             MOVE INTO R9 FOR DIVIDE.
         D     R8,L1024          CONVERT TO SECONDS.
         ST    R9,DELTTOD        ST DELTA TOD HERE
         LTR   R9,R9
         BNZ   HBALC
         STIMER WAIT,BINTVL=SLEEP
         B     HBAL
HBALC    L     R8,HTOD
         ST    R8,HOLDTOD        HTOD --> HOLDTOD
*===============================================================
TOPASCB  DS    0H
         L     R5,ASVTPTR       TO ASVT
         USING ASVT,R5
         L     R3,ASVTMAXU      MAXIMUM NUMBER OF ENTRIES
         LA    R4,ASVTFRST      FIRST ENTRY MINUS 4.
         ST    R4,ASCBSAVE
*********************************************
*                        R2 IS EDWA DSECT   *
*                        R3 POINTS TO OUXB  *
*                        R4 POINTS TO RMCT  *
*   ASCB LOOP STARTS     R5 POINTS TO ASCB  *
*                        R6 POINTS TO OUCB  *
*                                           *
*********************************************
ASCBLOOP ST    R3,ASCOUNT
         L     R4,ASCBSAVE
         LA    R4,4(,R4)        BUMP BY FOUR.
         ST    R4,ASCBSAVE
         ICM   R5,B'1111',0(R4) LOAD THE ASCB POINTER
         BM    NEXTAS           BRANCH IF BAD PTR.
*
         L     R4,RMCTPTR
         USING ASCB,5
         L     R3,ASCBOUXB
         USING OUXB,R3
         L     R6,ASCBOUCB       LOAD PTR TO OUCB
         USING OUCB,6
*
*        LH    R1,ASCBASID      PICK UP ASID
*        CVD   R1,DOUBLEWD
*        MVC   WORK6(6),EDIT6
*        ED    WORK6(6),DOUBLEWD+5
*        MVC   ASID(4),WORK6+2  SET ASID
         LA    R7,0
         IC    R7,ASCBASID
         SRL   R7,4
         LTR   R7,R7
         BZ    DO2
         IC    R8,HEX(R7)
         STC   R8,ASID
DO2      IC    R7,ASCBASID
         N     R7,=F'15'
         IC    R8,HEX(R7)
         STC   R8,ASID+1
         IC    R7,ASCBASID+1
         SRL   R7,4
         IC    R8,HEX(R7)
         STC   R8,ASID+2
         IC    R7,ASCBASID+1
         N     R7,=F'15'
         IC    R8,HEX(R7)
         STC   R8,ASID+3
*
*
         LM    R8,R9,ASCBEJST   LOAD UP EJST (CPU TIME - TASK TYPE)
         SRDL  R8,12            TO GET MICROSEC
         D     R8,L10000        TO GET HUNDREDTHS AND MICROS
         LM    10,R11,ASCBSRBT  LOAD UP SRB TIME.
         SRDL  R10,12           TO GET MICROSEC
         D     R10,L10000       TO GET HUNDREDTHS AND MICROS
         AR    R9,R11           ADD TOGETHER.
         ST    R9,CPUHOLD
         L     R8,CPUTOTL
         AR    R8,R9
         ST    R8,CPUTOTL
         L     R8,CPUNO
         A     R8,L1
         ST    R8,CPUNO
*
         TM    OUCBYFL,OUCBLOG  CHECK FOR TSO.
         BO    ITISATSO         YES, IT IS TSO.
         CLC   ASCBJBNI(4),FZEROS  JOBNAME POINTER FOR BATCH JOBS
         BNE   ITISABAT         BRANCH IF ZERO POINTER.
         CLC   ASCBJBNS(4),FZEROS  JOBNAME POINTER FOR STARTED TASKS
         BNE   ITISASTC         BRANCH IF STARTED TASK.
         B     NEXTAS           NO, SO SKIP THIS.
*
ITISABAT DS    0H
         MVI   TYPE,C'B'
         CLC   ZDFCMD(3),ALL     DISPLAY EVERYTHING
         BE    DISPALL          YES
         CLC   ZDFCMD(3),BAT     DISPLAY BATCH
         BE    DISPALL          YES
         B     NEXTAS           NO, SO SKIP THIS.
*
ITISATSO DS    0H
         MVI   TYPE,C'T'
         CLC   ZDFCMD(3),ALL     DISPLAY EVERYTHING
         BE    DISPALL          YES
         CLC   ZDFCMD(3),TSU     DISPLAY TSO
         BE    DISPALL          YES
         B     NEXTAS           NO, SO SKIP THIS.
*
ITISASTC DS    0H
         MVI   TYPE,C'S'
         CLC   ZDFCMD(3),ALL     DISPLAY EVERYTHING
         BE    DISPALL          YES
         CLC   ZDFCMD(3),STC     DISPLAY STARTED TASKS
         BE    DISPALL          YES
         B     NEXTAS           NO, SO SKIP THIS.
*
DISPALL  MVC   PSTEP(8),SPACES
         MVC   STEP(8),SPACES
         MVC   TYPEA(1),TYPE
         MVC   JOB(8),=CL8'STARTING' TO BYPASS GARBAGE
         ICM   R8,B'1111',ASCBJBNI
         BZ    NOTJOB           BRANCH IF ZERO POINTER.
*************
*   JOB     *
*************
         MVC   JOB,0(R8)        OTHERWISE, MOVE IN JOBNAME.
         LA    R15,8
         LA    R11,ZDFPRE       R8 POINTS TO JOB
N0       CLC   0(1,R11),SPACES
         BE    GOJ
         CLC   0(1,R11),ASTER
         BE    N0PASS
         CLC   0(1,R11),0(R8)
         BNE   NEXTAS
N0PASS   LA    R11,1(R11)
         LA    R8,1(R8)
         BCT   R15,N0
*
GOJ      BAL   R10,FASTQ      **  CHECK OPTION Q J **
         ICM   R7,B'1111',ASCBCSCB    POINTER TO THE TOP CSCB.
         BZ    STEPOK           BRANCH IF CSCB POINTER IS ZERO.
         USING CHAIN,R7
         MVC   STEP(8),CHKEY
         CLI   ZDFJS3,C'Q'
         BNE   STEPOK
*
STEPLOOP L     R7,CHPTR           FIND PROC STEP X CSCB         OPT Q
         LTR   R7,R7                                            OPT Q
         BZ    STEPOK                                           OPT Q
         CLC   JOB(8),CHKEY                                     OPT Q
         BNE   STEPLOOP                                         OPT Q
         MVC   PSTEP(8),CHPROCSN                                OPT Q
         MVC   STEP(8),CHSTEP                                   OPT Q
         B     STEPOK                                           OPT Q
         DROP  R7
**************
* STC OR TSU *
**************
NOTJOB   ICM   R8,B'1111',ASCBJBNS
         BZ    NEXTAS
*
         CLC   MASTER(8),0(R8)    *MASTER* TASK
         BNE   MTESTS             NO - CHECK NEXT
*
         LA    R9,0             ** MASTER DETECTED , CLEAR FLDS **
         ST    R9,MASTFLG         CLEAR MASTFLG
         XC    MASTFMCT,MASTFMCT  CLEAR FRAME COUNT ACCUMULATOR
         XC    MASTWSS,MASTWSS    CLEAR WORKING SET SIZE ACCUMULATOR
         XC    MASTCPU,MASTCPU    CLEAR CPU ACCUMULATOR
         XC    MASTEXCP,MASTEXCP
         B     MPROCESS           PROCESS FOR SYSTEM TASK
*
MTESTS   CLC   ZDFPRE(1),SPACES   SEARCH SPECIFIC
         BNE   NOTMAST            YES SO NOT A SUB MAST
*
         LA    R11,MASTER+8
         LA    R9,16
MTEST    CLC   0(8,R11),0(R8)
         BE    MPROCESS
         LA    R11,8(R11)
         BCT   R9,MTEST
         B     NOTMAST
MPROCESS L     R9,MASTFLG         FOUND A MATCH WITH SUB MAST NAMES
         LA    R9,1(R9)
         ST    R9,MASTFLG
*                                                   *
*     MASTPROC ROUTINE START                        *
*                                                   *
MASTLOOP L     R9,OUXBFMCT       LOAD OUCBWSS
         SLL   R9,2             TIMES FOUR FOR K.
         A     R9,MASTFMCT       ADD TO SYSTEM ACCUMULATION
         ST    R9,MASTFMCT       STORE FOR ACCUMULATION
*
         L     R9,ASCBIOSC      LOAD IO COUNT
         A     R9,MASTEXCP       ADD TO SYSTEM ACCUMULATION
         ST    R9,MASTEXCP
*
         L     R9,CPUHOLD
         A     R9,MASTCPU       ADD TO SYSTEM ACCUMULATION
         ST    R9,MASTCPU       STORE FOR ACCUMULATION
*
         L     R9,MASTFLG       TEST ALL SYSTEM TASK BITS
         S     R9,=F'17'
         LTR   R9,R9
         BNZ   NEXTAS           PROCESS NEXT ASCB IF NOT ALL ON
*
*  ACCUMULATED ALLOCATED FRAME COUNT (MEMORY)
         MVC   ASID(4),SPACES   *** PRINT OUT MAST ACCUMULATION **
         MVC   WHERE(3),=CL3'***'
         L     R7,MASTFMCT      LOAD FRAME COUNT ACCUMULATION
         CVD   R7,WORK          CONVERT TO DECIMAL FORMAT.
         MVC   SIZE(6),=X'2020202120D2' GET READY FOR EDIT.
         ED    SIZE-1(6),WORK+5 EDIT THE RESULT.
*
         L     R7,MASTEXCP      LOAD FRAME COUNT ACCUMULATION
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   PSTEP(8),EDIT8
         ED    PSTEP(8),WORK+4 EDIT THE OUTPUT FIELD.
         L     R7,MASTCPU       LOAD CPU TIME ACCUMULATION
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   CPUTIME(8),=X'20202021204B2020' MOVE IN THE EDIT MASK.
         ED    CPUTIME-1(9),WORK+4 EDIT THE RESULT.
*
         L     R9,DELTTOD
         LTR   R9,R9             CHK TO SEE IF ANYTHING IS THERE
         BNP   SWAPG1            CHECK DIVIDE BY NON-ZERO
         SR    R8,R8
         L     R9,MASTCPU
         S     R9,MASTCPUS       DELTA CPU
         LTR   R9,R9
         BNP   SWAPG1            NO WRITE IF ZERO
         M     R8,=F'100'        PERCENT
         SR    R8,R8
         D     R8,DELTTOD        DELTA CPU / DELTA TOD
         CVD   R9,WORK           *
         MVC   LINEWSS,=X'2021204B20' *
         ED    LINEWSS-1(6),WORK+5 *
SWAPG1   MVC   JOB(8),MASTER  SIGNIFY *MASTER* TASKS
         BAL   R7,OUTMSG        MOVE THE LINE TO THE 3270 BUFFER.
         L     R8,MASTCPU
         ST    R8,MASTCPUS
*                                                   *
*     MASTPROC ROUTINE END                          *
*                                                   *
         B     NEXTAS           PROCESS NEXT ASCB
NOTMAST  EQU   *
*
         MVC   JOB,0(R8)        MOVE IN JOBNAME FOR S/M/L MEMORIES.
         LA    R15,8
         LA    R11,ZDFPRE       R8 POINTS TO JOB
M0       CLC   0(1,R11),SPACES
         BE    GOTSOSTC
         CLC   0(1,R11),ASTER
         BE    M0PASS
         CLC   0(1,R11),0(R8)
         BNE   NEXTAS
M0PASS   LA    R11,1(R11)
         LA    R8,1(R8)
         BCT   R15,M0
*
GOTSOSTC ICM   R7,B'1111',ASCBCSCB    POINTER TO THE TOP CSCB.
         BZ    STEPOK           BRANCH IF CSCB POINTER IS ZERO.
         BAL   R10,FASTQ     ** OPTION Q J PROCESSING  **
*
         CLI   4(R7),X'04'      CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    STORMT           JOB IS START OR MOUNT
         CLI   4(R7),X'0C'      CHECK TO SEE IF BATCH JOB OR S/M/L
         BE    STORMT           JOB IS START OR MOUNT
         MVC   STEP(8),16(R7)   PICK UP CHCLS.
         B     STEPOK           BYPASS FURTHER CSCB PROCESSING.
STORMT   MVC   STEP(8),8(R7)    PICK UP CHKEY.
**************
* ALL        *
**************
STEPOK   CLC   JOB(8),=CL8'INIT' IS IT A 'DRY' INIT??           KMT
         BE    NEXTAS           YEP...KISS OFF                  KMT
         CLI   ZDFJS3,C'Q'
         BNE   IOCONT
         CLI   ZDFCMD,C'B'
         BNE   IOCONT
         B     XIOC             BRANCH IF BATCH AND OPTION Q
*
IOCONT   L     R7,ASCBIOSC      LOAD IO COUNT           NOT BATCH AND Q
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   PSTEP(8),EDIT8
         ED    PSTEP(8),WORK+4 EDIT THE OUTPUT FIELD.
         CLC   PSTEP(3),SPACES
         BE    PSTCONT
         L     R7,ASCBIOSC      LOAD IO COUNT
         SRL   R7,10            DIV BY 1024
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         ED    PSTEP(8),WORK+4 EDIT THE OUTPUT FIELD.
         MVC   PSTEP+2(6),=X'2020202120D2' GET READY FOR EDIT.
         ED    PSTEP+1(6),WORK+5
PSTCONT  SR    R7,R7            PREPARE FOR THE INSERT CHARACTERS.
         IC    R7,ASCBIODP
         SRL   R7,4             SHIFT OUT LOW ORDER NIBBLE.
         IC    R8,HEX(R7)       PICK THE EBCDIC CHARACTER.
         STC   R8,PSTEP         STORE INTO OUTPUT FIELD.
         IC    R7,ASCBIODP
         N     R7,=F'15'        TURN OFF HIGH ORDER NIBBLE.
         IC    R8,HEX(R7)       LOAD THE EBCDIC CHARACTER.
         STC   R8,PSTEP+1       STORE INTO OUTPUT LINE.
*
*   FILL IN WORKING SET SIZE
*
XIOC     L     R9,OUXBFMCT       LOAD OUCBWSS
         SLL   R9,2             TIMES FOUR FOR K.
         CVD   R9,WORK
         MVC   SIZE(6),=X'2020202120D2' GET READY FOR EDIT.
         ED    SIZE-1(6),WORK+5
*
         MVC   WHERE,=CL3' IN'  DEFAULT MEMORY POSITION IS IN.
         TM    OUCBSFL,OUCBNSW   TEST FOR NON-SWAP
         BZ    TRYTRIV          BRANCH IF NOT NON-SWAP.
         MVC   WHERE,=CL3'N/S'  MOVE IN NON-SWAP INDICATION.
*        TM    ASCBRCTF,ASCBWAIT
*        BZ    WHEREOK
*        MVC   STATUS(4),=CL4'WAIT'
         B     WHEREOK
TRYTRIV  TM    OUCBQFL,OUCBLSW   CHECK FOR LOGICAL SWAP
         BZ    TRYOUT           IF NOT, EXIT POSITION PROCESSING.
         MVC   WHERE,=CL3'LSW'  MOVE IN LOGICAL SWAP
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYOUT   TM    OUCBQFL,OUCBOUT   TRY OUT QUEUE
         BZ    TRYWAIT          IF NOT, GO TRY TRIVIAL QUEUE STATUS.
         MVC   WHERE,=CL3'OUT'  MOVE IN OUT QUEUE POSITION.
         B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
TRYWAIT  TM    OUCBQFL,OUCBOFF   CHECK FOR WAIT
         BZ    WHEREOK          BRANCH IF NOT.
         MVC   WHERE,=CL3' WT'  MOVE IN WAIT POSITION INDICATION.
*        B     WHEREOK          BY-PASS REST OF POSITION PROCESSING.
WHEREOK  TM    OUCBQFL,OUCBGOO+OUCBGOI+OUCBGOB           TRANSITIONING
         BZ    NOTTRANS         BRANCH IF MEMORY NOT TRANSITIONING.
         MVC   WHERE(3),=CL3'<->' INDICATE TRANSITIONING.
*
NOTTRANS SR    R7,R7            PREPARE FOR THE INSERT CHARACTERS.
         IC    R7,ASCBDP        LOAD ASCBDP
         SRL   R7,4             SHIFT OUT LOW ORDER NIBBLE.
         IC    R8,HEX(R7)       PICK THE EBCDIC CHARACTER.
         STC   R8,DP            STORE INTO OUTPUT FIELD.
         IC    R7,ASCBDP        LOAD THE DISPATCHING PRIORITY AGN.
         N     R7,=F'15'        TURN OFF HIGH ORDER NIBBLE.
         IC    R8,HEX(R7)       LOAD THE EBCDIC CHARACTER.
         STC   R8,DP+1          STORE INTO OUTPUT LINE.
*                                                        *** PGN ***
         SR    R7,R7            PREPARE FOR THE IC.
         IC    R7,OUCBSPG+1     NEW PGN  ???
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   LINEPGN(3),=X'202120' MOVE IN THE EDIT MASK.
         ED    LINEPGN-1(4),WORK+6 EDIT THE OUTPUT FIELD.
*                                                        * SWAP CNT *
         CLI   ZDFJS3,C'Q'
         BE    PINR             OPT Q = LOAD RECENT PAGE IN RATE
         LH    R7,OUCBSWC       LOAD OUCBSWC (SWAP COUNT)
         CLI   TYPEA,C'T'
         BNE   SWPCONT
         L     R7,OUXBTRC       LOAD TRAN COUNT IF TSO
         B     SWPCONT
PINR     LH    R7,OUXBSIPR                                      OPT Q
SWPCONT  CVD   R7,WORK          CONVERT IT TO DECIMAL.
         LR    R8,R7
         S     R8,=F'999'
         BNP   SOK
         SRL   R7,10            DIV BY 1K
         CVD   R7,WORK          CONVERT IT TO DECIMAL.
         MVC   EDTEXT(5),=X'40202120D2' MOVE IN THE EDIT MASK.
         ED    EDTEXT(4),WORK+6 EDIT THE OUTPUT FIELD.
         MVC   LINESWC(3),EDTEXT+2
         B     SWAPRC
SOK      MVC   LINESWC(3),=X'202120' MOVE IN THE EDIT MASK.
         ED    LINESWC-1(4),WORK+6 EDIT THE OUTPUT FIELD.
*
*   FILL IN SWAP REASON CODE
*
SWAPRC   TM    OUCBQFL,OUCBLSW  SEE IF LOGICAL SWAP
         BNZ   CHKSRC
         TM    OUCBQFL,OUCBOUT  SEE IF OUT
         BNZ   CHKSRC
         B     PFLDONE          MUST BE IN.
CHKSRC   CLI   OUCBSRC,X'11'    OUCBSRC (SWAP REASON CODE)
         BH    UNKNOWN          TOO BIG REASON UNKNOWN
         SR    R7,R7            CLEAR OUT WORK REGISTER
         IC    R7,OUCBSRC       GET OUCBSRC (SWAP REASON CODE)
         SLL   R7,2             MULTIPLY BY 4 TO GET INDEX
         LA    R7,REASONS(R7)   GET ADDRESS OF REASON CODE
         MVC   STATUS+1(4),0(R7)     MOVE IT INTO THE MESSAGE
         B     PFLDONE
UNKNOWN  MVC   STATUS+1,=CL4'????'       SORRY..DONT KNOW
*
*   FILL IN SERVICE UNITS
*
PFLDONE  L     R7,OUCBWMS       OUCBWMS LOAD SERVICE UNITS THIS XACTION
         CVD   R7,WORK          CONVERT TO DECIMAL.
         MVC   LINEWMS(7),=X'20202020202120' MOVE IN THE EDIT MASK.
         ED    LINEWMS-1(8),WORK+4 EDIT THE RESULT.
*                                             **  FILL IN CPU TIME **
ELAPSED  L     R8,RMCTTOD        PICK UP TIME OF DAY FROM RMCT.  (SU)
         S     R8,OUCBTMO        SUBTRACT TRANSACTION START TIME.(SU)
         SRDL  R8,32             MOVE INTO R  FOR DIVIDE.
         D     R8,L1024          CONVERT TO SECONDS.
         SR    R8,R8             IGNORE REMAINDER.
         ST    R9,CALCELAP
         L     R9,CPUHOLD
         ST    R9,CALCCPU
         CVD   R9,WORK          CONVERT IT TO DECIMAL.
         MVC   CPUTIME(8),=X'20202021204B2020' MOVE IN THE EDIT MASK.
         ED    CPUTIME-1(9),WORK+4 EDIT THE RESULT.
*                                             **  FILL IN CPU % **
         SR    R8,R8
         MVC   LINEWSS,=X'2021204B20' *
         L     R9,CALCELAP
         LTR   R9,R9             CHK TO SEE IF ANYTHING IS THERE
         BNP   SWAPGO            BRANCH IF RESULT MAKES SENSE.
         SR    R8,R8
         L     R9,CALCCPU
         M     R8,=F'100'        PERCENT
         SR    R8,R8
         D     R8,CALCELAP
         CVD   R9,WORK           *
         ED    LINEWSS-1(6),WORK+5 *
         MVC   CPUI(1),ASTER
*
SWAPGO   MVI   CPUFLAG,C' '     DEFAULT
*
*  NOW LOOP, CHECKING FOR THE JOBS WHOSE CPU TIME HAS INCREASED SINCE
*            THE LAST ZDF DISPLAY
*
*        LA    R7,CPUTAB        START OF TABLE
         L     R7,CPUTABI
CPULOOP1 DS    0H
         CLI   0(R7),X'FF'      END OF TABLE
         BE    JUSTOUT1         YES, IGNORE
*
         CLC   0(16,R7),SPACES  UNUSED ENTRY?
         BE    CPUADD1          YES, ADD THIS JOB TO TABLE
*
         CLC   0(4,R7),ASID     MATCH ON ASID, JOBNAME
         BNE   CPUBUMP
         CLC   7(8,R7),JOB      MATCH ON ASID, JOBNAME
         BNE   CPUBUMP
         B     CPUMATCH         YES, COMPARE CURRENT CPU TIME VS LAST
*
CPUBUMP  LA    R7,26(,7)        BUMP TO NEXT ENTRY
         B     CPULOOP1         KEEP LOOPING
*
CPUMATCH DS    0H                       ** % CPU WHEN PREV ENTRY **
         CLC   16(2,R7),CPUTIME+3       CHECK CPU WHOLE SECONDS
         BE    UPDCPU           IF THEY ARE THE SAME, DON'T SET FLAG
         MVC   16(2,R7),CPUTIME+3       STORE CPU WHOLE SECONDS
         MVC   CPUFLAG(1),ASTER    INDICATE CPU TIME HAS INCREASED
*
UPDCPU   L     R9,CALCELAP
         S     R9,18(R7)
         ST    R9,DELTELAP
         LTR   R9,R9             CHK TO SEE IF ANYTHING IS THERE
         BNP   UPCPUN0
         MVC   LINEWSS,=X'2021204B20' *
         L     R9,CALCCPU
         S     R9,22(R7)
         LTR   R9,R9             CHK TO SEE IF ANYTHING IS THERE
         BNP   UPCPUN0
         M     R8,=F'100'        PERCENT
         SR    R8,R8
         D     R8,DELTELAP
         CVD   R9,WORK           *
         ED    LINEWSS-1(6),WORK+5 *
         MVI   CPUI,C' '
         B     JUSTOUT1
*
UPCPUN0  SR    R9,R9
         MVC   LINEWSS(6),SPACES
         B     JUSTOUT1
*
CPUADD1  MVC   0(16,R7),ASID    MOVE IN ASID, JOBNAME, STEPNAME, PSTEP
         MVC   16(2,R7),CPUTIME+3       STORE CPU WHOLE SECONDS
         MVC   26(33,R7),SPACES
*
JUSTOUT1 MVC   18(4,R7),CALCELAP
         MVC   22(4,R7),CALCCPU
GO       DS    0H
         CLC   STATUS+1(4),SPACES
         BE    GOGO             NOT WAITING
         CLC   STATUS+1(4),REASONS+4
         BE    GOGO             TGET
         CLC   STATUS+1(4),REASONS+8
         BE    GOGO             TPUT
*        CLI   TYPEA,C'S'               NOT STC WAIT
*        BNE   DECODE
*
         CLC   JOB(8),=C'EDBBLAS '
         BE    GOGO             JOBS USUALLY THIS
         CLC   JOB(8),=C'EDBBTAS '
         BE    GOGO             JOBS USUALLY THIS
         CLC   JOB(8),=C'EDGATE  '
         BE    GOGO             JOBS USUALLY THIS
         CLC   JOB(8),=C'EDSNALNK'
         BE    GOGO             JOBS USUALLY THIS
         CLC   JOB(8),=C'C4TSO   '
         BE    GOGO             JOBS USUALLY THIS
*                               ? CAN WE DECODE WAITS
DECODE   DS    0H
         MVI   STATUS,C'*'
         L     R9,HSMACT
         LTR   R9,R9
         BZ    NHSMWAIT
         CLI   HSMFLAG,C'0'
         BNE   HSMDONE
         MVI   HSMFLAG,C'1'
*                                           FIND HSM ACTIVITY
         B     HSMDONE                      TEMPORARY
         MVC   VQWORK(64),SPACES
         MVC   VQWORK(52),VXDF
         MVC   VQWORK+42(2),=C'1 '
         LA    R9,HSMINFO
         ST    R9,VQWORK+44
*
         L     R15,ISPLINK
         CALL (15),(VSELECT,L64,VQWORK),VL,MF=(E,ISPARMS)
*
         TPUT  HSMINFO,L'HSMINFO
         L     R15,ISPLINK
         CALL (15),(VGET,HSMP,SHARED),VL,MF=(E,ISPARMS)
*
HSMDONE  DS    0H
         LA    R9,10
         LA    R8,HSMINFO
QHSMINFO CLC   0(8,R8),JOB
         BNE   QHSMI
         MVC   STATUS+1(4),=C'HSM '
         B     GOGO
QHSMI    LA    R8,8(R8)
         BCT   R9,QHSMINFO
*
NHSMWAIT DS    0H
*
GOGO     DS    0H
         BAL   R7,OUTMSG        ADD THE LINE TO THE SPF TABLE.
*
NEXTAS   L     R3,ASCOUNT
         BCT   R3,ASCBLOOP      BRANCH BACK TO LOOK AT NEXT MEMORY.
****************************
*                          *
*   ASCB LOOP ENDS         *
*                          *
****************************
*===============================================================
*
GOTSOMEJ DS    0H
*
         MVC   SU+8(8),SPACES
         L     R8,DELTTOD
         LTR   R8,R8
         BZ    NOCENT
CENT     SR    R8,R8
         L     R9,CPUTOTL
         S     R9,OLDTOTL
         LTR   R9,R9             WENT -VE
         BNP   NOCENT
*        M     R8,=F'100'        PERCENT
*        SR    R8,R8
         D     R8,DELTTOD
         CVD   R9,WORK           *
*        ED    SU+8(4),WORK+6
         OI    WORK+7,X'0F'        FIX THE SIGN
         UNPK  SU+11(3),WORK+6(2)    ...AND EDIT
*
NOCENT   L     R9,CPUTOTL
         ST    R9,OLDTOTL
         MVI   SU+15,C'='
         L     R9,CPUNO
         S     R9,OLDNO
         BZ    NOCONT
         MVI   SU+15,C'+'
         L     R9,CPUNO
         C     R9,OLDNO
         BH    NOCONT
         MVI   SU+15,C'-'
NOCONT   L     R9,CPUNO
         ST    R9,OLDNO
*
         CLI   ZDFJS3,C'Q'
         BNE   TBTOPD
*
         L     R8,QSAVEUT                                   OPTN Q
         CLOSE ((R8))                                       OPTN Q
*                                                           OPTN Q
         L     R15,ISPLINK                                  OPTN Q
         CALL  (15),(VSELECT,L24,VFREE),VL,MF=(E,ISPARMS)   OPTN Q
*                                                           OPTN Q
         L     R0,=F'4096'                                  OPTN Q
         L     R1,QSAVEUT                                   OPTN Q
         FREEMAIN R,LV=(0),A=(1)                            OPTN Q
*                                                           OPTN Q
         L     R0,=F'4096'                                  OPTN Q
         L     R1,R1QAREA                                   OPTN Q
         FREEMAIN R,LV=(0),A=(1)                            OPTN Q
*
TBTOPD   L     R15,ISPLINK
         CALL  (15),(TBTOP,ZDFTABLE),VL,MF=(E,ISPARMS)
*
         CLI   ZDFRPT,C'0'
         BE   TABDIS
*
         L     R15,ISPLINK                                       RPT>0
         CALL  (15),(VCONTROL,VDISPLAY,VLOCK),VL,MF=(E,ISPARMS)  RPT>0
***********************************************************
*                                                         *
*        TABLE DISPLAY HERE                               *
*                                                         *
***********************************************************
TABDIS   L     R15,ISPLINK
         CALL  (15),(TBDISPL,ZDFTABLE,ZDFDATAB),VL,MF=(E,ISPARMS)
         LR    R8,R15
*
         L     R15,ISPLINK
         CALL (15),(VPUT,CMDRPT,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL (15),(VPUT,CMDJES3,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL (15),(VPUT,CMDPREEP,SHARED),VL,MF=(E,ISPARMS)
*
         C     R8,L8         END COMMAND ENTERED??
         BE    DUNWITIT       ALL DONE
*
         CLI   ZDFJS3,C'S'
         BE    SORTIT
*
         CLI   ZDFRPT,C'0'
         BE    NOTACONT
*
         L     R15,ISPLINK                                       RPT>0
         CALL  (15),(VCONTROL,VDISPLAY,VLINE),VL,MF=(E,ISPARMS)  RPT>0
         B     DUNWITIT       AUTO MODE EXIT ASAP                RPT>0
*
NOTACONT C     R8,FZEROS      COMMAND ENTERED   R15 WAS SET ON TABDIS
         BNE   GOTCMD         GO REPEAT THE DISPLAY
*
GOTSTUFF MVC   SELSAVE(3),SELCODE    SAVE SEL CODE
         OC    SELSAVE(3),SPACES   UPPER-CASE IT
         MVC   SELCODE(3),SPACES  SET SELECTION CODE TO SPACES
*
         L     R15,ISPLINK
         CALL  (15),(TBGET,ZDFTABLE),VL,MF=(E,ISPARMS)
*
         C     R15,L8         RC=8 MEANS CRP IS AT TOP
         BE    GOTCMD         THEN A COMMAND WAS ENTERED
*
* IF THE CRP IS NOT ZERO, THEN A LINE SELECTION CODE MUST HAVE BEEN
* ENTERED. I HAVE THE CURRENT LINE, SO EDIT THE SELECTION CODE.
*
         MVC   VQWORK(32),SPACES
*
         CLI   SELSAVE,C'C'   IS IT A JOB CANCEL
         BE    JS3CAN         GO DO IT
*
         CLI   SELSAVE,C'F'   IS IT FLASHER
         BE    JS3FLS         GO DO IT
*
         CLI   SELSAVE,C'Z'   IS IT FLASHER SHORTS STATUS
         BE    JS3FLZ         GO DO IT
*
         CLI   SELSAVE,C'S'   IS IT SHOW JES STATS
         BE    JS3            GO DO IT
*
         CLI   SELSAVE,C'N'   IS IT SHOW NQ
         BE    ENQ            GO DO IT
*
         B     GOTCMD         GO REPEAT THE DISPLAY
*
SORTIT   MVC   VQWORK(32),SPACES
         MVC   VQWORK(12),VQSRT
         B     EXECIT
*
JS3      MVC   VQWORK(16),VVQJ
         B     EXECVQW
*
JS3FLS   MVC   VQWORK(32),VVQF
         B     EXECVQW
*
JS3FLZ   MVC   VQWORK(32),VVQZ
         B     EXECVQW
*
ENQ      MVC   VQWORK(16),VVQN
         MVC   VQWORK+7(4),ASID
         CLI   VQWORK+7,C' '
         BNE   EXECIT
         MVI   VQWORK+7,C'0'
         B     EXECIT
*
JS3CAN   MVC   VQWORK(16),VVQC
*
EXECVQW  MVC   VQWORK+7(8),JOB
*
EXECIT   L     R15,ISPLINK
*        CALL (15),(VSELECT,L48,VADDALL),VL,MF=(E,ISPARMS)
*        L     R15,ISPLINK
         CALL (15),(VSELECT,L64,VADDSYSP),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL (15),(VSELECT,L32,VQWORK),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL (15),(VSELECT,L48,VDELSYSP),VL,MF=(E,ISPARMS)
*
         B     GOTCMD         GO REPEAT THE DISPLAY
*
DUNWITIT DS    0H
*
THEEND   L     R7,ZDF7HOLD     RESTORE  RETURN ADDRESS
         BR    R7             RETURN TO OUR CALLER
*****************************************************
*                                                   *
*     SHOWRMF ROUTINE END                           *
*                                                   *
*****************************************************
GOTCMD   L     R15,ISPLINK
         CALL (15),(VPUT,CMDLINEP,SHARED),VL,MF=(E,ISPARMS)
         MVC   TABAREA(80),SPACES
*
         L     R15,ISPLINK
         CALL  (15),(TBEND,ZDFTABLE),VL,MF=(E,ISPARMS)
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
*****************************************************
*                                                   *
*     OUTMSG   ROUTINE START                        *
*                                                   *
*****************************************************
OUTMSG   DS    0H
         CLI   ZDFJS3,C'0'
         BNE   OUTO1
         CLC   LINEWSS(5),SPACES
         BE    OUTEND
OUTO1    CLI   ZDFJS3,C'1'
         BNE   OUTON
         CLC   LINEWSS(2),SPACES
         BE    OUTEND
OUTON    L     R15,ISPLINK
*        MVI   TABAREA,C' '
         CALL  (15),(VPUT,DANAMLST),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(TBADD,ZDFTABLE),VL,MF=(E,ISPARMS)
*
OUTEND   MVC   TABAREA(80),SPACES
         BR    R7             RETURN TO OUR CALLER
         TITLE 'EXECUTED INSTRUCTIONS'
*****************************************************
*                                                   *
*     OUTMSG   ROUTINE END                          *
*                                                   *
*****************************************************
*****************************************************
*                                                   *
*     FASTQ    ROUTINE START                        *
*                                                   *
*****************************************************
FASTQ    DS    0H
         ST    R10,QSAVE
         CLI   ZDFJS3,C'J'
         BNE   NOTOPTJ
         MVC   ZDFJBA(8),JOB
*
         L     R15,ISPLINK
         CALL (15),(VPUT,CMDJOBA,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL (15),(VSELECT,L32,VQJ3),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL (15),(VGET,CMDJOBN,SHARED),VL,MF=(E,ISPARMS)
*
         MVC   ASID(4),ZDFJBN
NOTOPTJ  CLI   ZDFJS3,C'Q'
         BNE   FXX
*                                         OPT Q SPECIFIED
         MVC   ZDFJBC(1),ASTER
         L     R9,R1QAREA                 LOOP ROUND QAREA
RESCAN   CLI   0(R9),C' '                 LOOP ROUND QAREA
         BE    SCANREC                    LOOP ROUND QAREA  -> FILL IT
         CLC   0(8,R9),JOB                LOOP ROUND QAREA
         BE    FASTQOK                    LOOP ROUND QAREA
         LA    R9,9(R9)                   LOOP ROUND QAREA
         C     R9,QAREAOFF
         BH    QWRONG
         B     RESCAN                     LOOP ROUND QAREA
*
QWRONG   MVI   TYPE,C'?'        IF OPTN Q
         B     FXX
*
SCANREC  L     R11,EOFMARK                TEST EOF
         LTR   R11,R11                    TEST EOF
         BP    FXX                        TEST EOF
***********************************
         LA    1,LABEL15                  GO AMODE 24
         BSM   0,1                        GO AMODE 24
LABEL15  DS    0H                         GO AMODE 24
***********************************
         L     R11,QSAVEUT
         GET   (R11)                      TRY TO FILL NEXT TAB ENTRY
         LR    R11,R1
***********************************
         L     1,LABEL13                  GO AMODE 31
         BSM   0,1                        GO AMODE 31
LABEL13  DC    A(LABEL14+X'80000000')     GO AMODE 31
LABEL14  DS    0H                         GO AMODE 31
***********************************
         LR    R1,R11                     R1 POINTS TO RECORD READ
         L     R11,QSAVEUT
         USING IHADCB,R11
         LH    R8,DCBLRECL                GET LENGTH OF RECORD IN R8
NEXTJOB  CLI   0(R1),X'00'
         BNE   NOTEOF
         LA    R11,1                      NULL CHARS IS END OF DATA
         ST    R11,EOFMARK
         L     R11,QAREAOFF               UPDATE IN CORE DATA
         MVI   0(R11),C' '                NEXT ENTRY A SPACE
         B     RESCAN
NOTEOF   CLC   40(4,R1),SPACES            4 CHAR SYSID INDICATES ACTIVE
         BE    TRYNEXT
         L     R11,QAREAOFF               UPDATE IN CORE DATA
         MVC   0(8,R11),0(R1)             JOB NAME
         MVC   8(1,R11),32(R1)            JOB CLASS
         LA    R11,9(R11)
         MVI   0(R11),C' '                NEXT ENTRY A SPACE
         ST    R11,QAREAOFF               UPDATE IN CORE PTR
TRYNEXT  LA    R1,80(R1)
         SH    R8,EIGHTY
         LTR   R8,R8
         BP    NEXTJOB
         B     RESCAN
*
FASTQOK  MVC   ZDFJBC(1),8(R9)                  * FOUND IT ! *
         MVC   TYPE,ZDFJBC      IF OPTN Q
         B     FXX
*
FASTQEOF EQU   *                                              EOF
***********************************                           EOF
         L     1,LABEL11                  GO AMODE 31         EOF
         BSM   0,1                                            EOF
LABEL11  DC    A(LABEL12+X'80000000')                         EOF
LABEL12  DS    0H                                             EOF
***********************************                           EOF
         LA    R11,1                                          EOF
         ST    R11,EOFMARK                                    EOF
*
FXX      L     R10,QSAVE
         BR    R10
*****************************************************
*                                                   *
*     FASTQ    ROUTINE END                          *
*                                                   *
*****************************************************
*
         TITLE 'MISCELLANEOUS CONSTANTS'
*        PRINT NOGEN
FZEROS   DC    F'0'
L1       DC    F'1'
L2       DC    F'2'
L3       DC    F'3'
L4       DC    F'4'
L5       DC    F'5'
L6       DC    F'6'
L8       DC    F'8'
L12      DC    F'12'
L16      DC    F'16'
L18      DC    F'18'
L20      DC    F'20'
L24      DC    F'24'
L25      DC    F'25'
L32      DC    F'32'
L34      DC    F'34'
L37      DC    F'37'
L48      DC    F'48'
L64      DC    F'64'
L78      DC    F'78'
L80      DC    F'80'
L1024    DC    F'1024'
L10000   DC    F'10000'
*
EDIT6    DC    X'402020202121'
EDIT8    DC    X'4020202020202121'
TSU      DC    C'TSU'
ALL      DC    C'ALL'
STC      DC    C'STC'
BAT      DC    C'BAT'
ISPLINK  DC    V(ISPLINK)
*
ASTER    DC    CL1'*'
MASTER   DC    CL8'*MASTER*'
         DC    CL8'PCAUTH  '
         DC    CL8'GRS     '
         DC    CL8'DUMPSRV '
         DC    CL8'ALLOCAS '
         DC    CL8'RASP    '
         DC    CL8'XCFAS   '
         DC    CL8'CONSOLE '
         DC    CL8'SMXC    '
         DC    CL8'SYSBMAS '
         DC    CL8'TRACE   '
         DC    CL8'IOSAS   '
         DC    CL8'CATALOG '
         DC    CL8'SMF     '
         DC    CL8'LLA     '
         DC    CL8'TNF     '
         DC    CL8'VMCF    '
*
EIGHTY   DC    H'80'
SLEEP    DC    F'0010'      0.1 SECS
*
VVQJ     DC    C'CMD(QS SYSLOG  )'
VVQN     DC    C'CMD(QN 0000    )'
VDELAY   DC    C'CMD(ZWAIT   )'
VVQF     DC    C'CMD(QF SYSLOG   ) NEWAPPL(DISF) '
VVQZ     DC    C'CMD(QZ SYSLOG   ) NEWAPPL(DISF) '
VVQC     DC    C'CMD(QC SYSLOG  )'
VQJ3     DC    C'CMD(EXEC ''XSDMN.ZDF(OPTNJ)'')    '
VQSRT    DC    C'CMD(OPTNS  )'
VXDF     DC    C'CMD(CALL ''SYS1.PROD.'
*                1234567890 1234567890
         DC    C'EDED.LINKLIB(XDF)''   ''0 1234''  )'
*                123456789012345678 9012 345678 9012
VADDSYSP DC    C'CMD(ALTLIB ACTIVATE APPLICATION(CLIST) '
         DC    C'DATASET(''XSDMN.ZDF'')     '
*ADDALL  DC    C'CMD(ALLOCATE DD(SYSUPROC) DA(''XSDMNXX.ZDF'') SHR   '
VDELSYSP DC    C'CMD(ALTLIB DEACTIVATE APPLICATION(CLIST)          '
VALLC    DC    C'CMD(ALLOC DD(SYSUTF)    '
FJCTNAME DC    C'DA(''SYS1.JES3.FLSJCT'') SHR             )'
VPANELS  DC    C'(''XSDMN.ZDF'')     '
VPANCLR  DC    C'() '
VFREE    DC    C'CMD(FREE DD(SYSUTF)    )'
VSELECT  DC    C'SELECT '
*
TBADD    DC    C'TBADD '
TBCR     DC    C'TBCREATE '
TBDISPL  DC    C'TBDISPL '
TBEND    DC    C'TBEND '
TBGET    DC    C'TBGET '
TBTOP    DC    C'TBTOP '
TBVCLEAR DC    C'TBVCLEAR '
VDEF     DC    C'VDEFINE '
VDELETE  DC    C'VDELETE '
VGET     DC    C'VGET '
VPUT     DC    C'VPUT '
VCONTROL DC    C'CONTROL '
VDISPLAY DC    C'DISPLAY '
VLOCK    DC    C'LOCK '
VLINE    DC    C'LINE '
VLIBDEF  DC    C'LIBDEF '
VISP     DC    C'ISPPLIB '
VDATASET DC    C'DATASET '
*
NOW      DC    C'NOWRITE '
*
NULL     DC    C'()'
*
DANAMLST DC    C'('             DISPLAY ACTIVE NAME LIST
SEL      DC    C'PIKA     '     SELECTION CODE
ACTLINE  DC    C'ACTLINE  '     JOB INFO
STAT     DC    C'STAT     '     JOB INFO
ACTLINB  DC    C'ACTLINB )'     JOB INFO
ACTLINEP DC    C'(ACTLINE)'     JOB INFO
STATP    DC    C'(STAT)'        JOB INFO
ACTLINBP DC    C'(ACTLINB)'     JOB INFO
HSMP     DC    C'(HSM)'
SUP      DC    C'(SU)'
SERP     DC    C'(SER)'
CMDLINEP DC    C'(XCMD)'
*MDWHERE DC    C'(WHER)'
CMDRPT   DC    C'(XRPT)'
CMDPREEP DC    C'(ZPRE)'   PREFIX
CMDJES3  DC    C'(ZJS3)'    JES3
CMDCPU   DC    C'(CPU)'
CMDAFC   DC    C'(AFC)'
CMDPAG   DC    C'(PAG)'
CMDTPR   DC    C'(TPR)'
CMDUIC   DC    C'(UIC)'
CMDMGB   DC    C'(MGB)'
CMDTSO   DC    C'(TSO)'
CMDJOBN  DC    C'(ZJBN)'    JES3
CMDJOBC  DC    C'(ZJBC)'    JES3
CMDJOBA  DC    C'(ZJBA)'    JES3
PIKP     DC    C'(PIKA)'    SELECTION CODE
SHARED   DC    C'SHARED '
*
SPACES   DC    CL80' '
*
CHAR     DC    C'CHAR '
M16      DC    F'16000000'
*
ZDFDATAB DC    C'EDWADAT8'
ZDFTABLE DC    C'ZDFTABLE'
*
*
         TITLE 'REGISTER EQUATES'
***********************************************************************
*        REGISTER EQUATES                                             *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'WORKING STORAGE AREAS'
*
HEX      DC    C'0123456789ABCDEF'
* SEE RMCA FOR REASONS
REASONS  DC    C' ?? TPUTTGETLONGAUX<RS< WAITREQSENQ EXCHUNIL'
         DC    C'TRANCENSDPAG2LONAPPCOMVIOMVO'
*                |...|...|...|...|...|...|...|...|...|...|...
         LTORG
*        PRINT NOGEN
SYSUTF   DCB   DDNAME=SYSUTF,DSORG=PS,MACRF=(GL),EODAD=FASTQEOF,       X
               RECFM=F,BUFNO=7
MVPARM   MVC   0(0,R1),SYSUTF      MOVE DCB INTO GETMAIN AREA
         DCBD  DSORG=PS,DEVD=DA
         PRINT GEN
         CVT   DSECT=YES,LIST=NO
EDWA     DSECT
SAVEAREA DS    18F
CPUHOLD  DS    F
CPUTOTL  DS    F
OLDTOTL  DS    F
CPUNO    DS    F
OLDNO    DS    F
ASVTPTR  DS    F
SMCAPTR  DS    F
CSDPTR   DS    F
CVTPTRX  DS    F
HSMACT   DS    F
TYPEA    DS    CL1
R1QAREA  DS    F
QSAVEUT  DS    F
QSAVE    DS    F
QAREAOFF DS    F
ASCOUNT  DS    F
R4HOLD   DS    F
ASCBSAVE DS    F
EOFMARK  DS    F
RMCTPTR  DS    F
MASTFMCT DS    F                FRAME COUNT ACCUMULATOR
MASTWSS  DS    F                WORKING SET SIZE ACCUMULATOR
MASTCPU  DS    F                CPU TIME ACCUMULATOR
MASTCPUS DS    F                CPU TIME ACCUMULATOR
MASTEXCP DS    F                CPU TIME ACCUMULATOR
DELTELAP DS    F
CALCELAP DS    F
CALCCPU  DS    F
HTOD     DS    F
HOLDTOD  DS    F
DELTTOD  DS    F
         DS    0F
VQWORK   DS    CL64
ZDF7HOLD DS    F
RCEHOLD  DS    F
PCCATPTR DS    F
MASTFLG  DS    F                SYSTEM TASK PROCESSED FLAGS
ISPARMS  DS    10F            PARM LIST FOR ISPLINK
DOUBLEWD DS    D              PACKED DECIMAL WORK AREA
WORK     DS    D
         DS    CL2
SU       DS    CL16           SPEED CONST
         DS    CL1
SER      DS    CL25           SERIAL #
SELCODE  DS    CL3            LINE SELECTION CODE
SELSAVE  DS    CL3            LINE SELECTION CODE SAVE AREA
ZDFPRE   DS    CL8
ZDFCMD   DS    CL3
ZDFRPT   DS    CL2
ZDFJS3   DS    CL1
ZDFCPU   DS    CL4
ZDFAFC   DS    CL4
ZDFPAG   DS    CL4
ZDFTPR   DS    CL4
ZDFUIC   DS    CL4
ZDFMGB   DS    CL4
ZDFTSO   DS    CL18
ZDFJBN   DS    CL4
ZDFJBC   DS    CL1
ZDFJBA   DS    CL8
QAUT     DS    CL1
WORK6    DS    XL6
LINEWMS  DS    CL7
EDTEXT   DS    CL4            JOBTYPE SAVE AREA
         DS    CL1
TABAREA  DS    0CL80
ASID     DS    CL4
         DS    CL1
TYPE     DS    CL1
         DS    CL1
JOB      DS    CL8
         DS    CL1
PSTEP    DS    CL8
         DS    CL1
STEP     DS    CL8
         DS    CL1
WHERE    DS    CL3
         DS    CL1
STATUS   DS    CL5
         DS    CL1
SIZE     DS    CL5
         DS    CL2
LINESWC  DS    CL3
         DS    CL1
DP       DS    CL2
         DS    CL1
LINEPGN  DS    CL3
         DS    CL1
LINEWSS  DS    CL5
CPUI     DS    CL1
CPUTIME  DS    CL8
CPUFLAG  DS    CL1
         DS    CL4
         DS    CL2
HSMINFO  DS    CL80
HSMFLAG  DS    CL1
CPUTABI  DS    F
CPUTABL  DSECT ,
CPUTAB   DS    157CL26           CPUTABLE SAVE AREA
CPUTABE  DS    XL2               END OF CPUTABLE
***
*  THE FOLLOWING WAS ADDED TO PROVIDE ADDRESSABILITY TO MVS/SP1.3
*  SRM RESOURCE CONTROL TABLE VARIABLES (RCT)
***
RCTDSECT DSECT ,                   SRM RES CNTL TABLE             NERDC
RCTRCT   DS    CL4                 'RCT'                          NERDC
RCCUICTL DS    H                   UIC THRESHOLD LOW              NERDC
RCCUICTH DS    H                   UIC THRESHOLD HIGH             NERDC
RCCCPUTL DS    H                   CPU THRESHOLD LOW              NERDC
RCCCPUTH DS    H                   CPU THRESHOLD HIGH             NERDC
RCCPTRTL DS    H                   PAGING THRESHOLD LOW           NERDC
RCCPTRTH DS    H                   PAGING THRESHOLD HIGH          NERDC
RCCASMTL DS    H                   ASM Q REQUEST THRESHOLD LOW    NERDC
RCCASMTH DS    H                   ASM Q REQUEST THRESHOLD HIGH   NERDC
RCCDPRTL DS    H                   DEMAND PAGE RATE LOW THRESH    NERDC
RCCDPRTH DS    H                   DEMAND PAGE RATE HIGH THRESH   NERDC
RCCCPUPL DS    H                   CPU UTIL W/ DEMAND PAGING LOW  NERDC
RCCCPUPH DS    H                   CPU UTIL W/ DEMAND PAGING HI   NERDC
RCCMSPTL DS    H                   PAGE DELAY W/ DEMAND PAGING LO NERDC
RCCMSPTH DS    H                   PAGE DELAY W/ DEMAND PAGING HI NERDC
RCCPDLTL DS    H                   PAGE DELAY TIME (MS) LOW       NERDC
RCCPDLTH DS    H                   PAGE DELAY TIME (MS) HIGH      NERDC
         ORG   RCTDSECT+52                                        NERDC
RCVCTMC  DS    H                   SAMPLE INTERVALS COUNT   1     NERDC
RCVUICA  DS    H                   UIC AVERAGE              2     NERDC
RCVCPUA  DS    H                   CPU USAGE AVERAGE        3     NERDC
RCVAVQC  DS    H                   AVQ LOWW COUNT           4     NERDC
RCVAFQA  DS    H                   AVG AVAIL FRAMES         5     NERDC
RCVASMQA DS    H                   ASM Q LENGTH AVERAGE     6     NERDC
RCVPTR   DS    H                   PAGING RATE              7     NERDC
RCVDPR   DS    H                   DEMAND PAGING RATE ?     8     NERDC
RCVPAGRT DS    H                   TOTAL PAGING RATE        9     NERDC
RCVMSPP  DS    H                   PAGE DELAY TIME (MS)    10     NERDC
RCVUICC  DS    F                                            1
RCVCPUC  DS    F                                            2
RCVAVQP  DS    F                                            3
RCVAFQC  DS    F                                            4
*
*.....................................................................*
*        MAPPING DSECTS
*.....................................................................*
         IHAASVT
         IHAASCB
         IRAOUCB
         IHAOUXB
         IKJTCB
         IEECHAIN
         IEESMCA
         IHACSD
*.....................................................................*
*        MAPPING DSECT FOR DMDT (DOMAIN DESCRIPTOR TABLE)             *
*.....................................................................*
RMCT     DSECT
         ORG   RMCT+64
RMCTADJC DS    F
         ORG   RMCT+124
RMCTTOD  DS    F
         ORG   RMCT+180
RMCTDMDT DS    F
         ORG   RMCT+184
RMCTDMDE DS    F
         ORG   RMCT+228
RMCTRCT  DS    F
         ORG   ,
DMDT     DSECT
DMDTNO   DS    XL1             DOMAIN NUMBER
DMDTLO   DS    XL1             MIN  MPL LEVEL
DMDTHI   DS    XL1             MAX  MPL LEVEL
DMDTWT   DS    XL1             WEIGHTING FACTOR
DMDTMPLT DS    XL2             CURRENT MPL TARGET
DMDTRUA  DS    XL2             AVE # OF READY USERS
DMDTGOOU DS    XL2             CURRENT # USERS GOING OUT
DMDTCMPL DS    XL2             CURRENT MPL
DMDTOUTU DS    XL2             CURRENT # USERS ON OUT Q
DMDTINCU DS    XL2             CURRENT # SWAPPABLE INCORE USERS
DMDTRUC  DS    XL4             ACCUM FOR READY USER AVERAGE
DMDTEND  DS    0X              END OF DMT
         DS    11F             XA 2.1.7 REQUIRED 2 XTRA WORDS HERE
         DS    29F             ESA     REQUIRED 29 XTRA WORDS HERE
DMDTLEN  EQU   *-DMDT
         END
/*
//L.SYSLMOD  DD   DISP=SHR,DSN=TEST.PGM.LOAD
//L.SYSLIB   DD   DSN=ISP.V3R5M0.ISPLOAD,DISP=SHR
//L.SYSIN    DD   *
  NAME ZDF(R)
/*
