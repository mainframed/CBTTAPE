//ED115DMQ JOB MSGCLASS=X,CLASS=Q,REGION=4M,NOTIFY=XSDMN
//*
//ASM1    EXEC ASMHCL,PARM.L='XREF,LET,LIST,AMODE=31,RMODE=24'
//C.SYSLIB DD DISP=SHR,DSN=SYS1.MACLIB
//         DD DISP=SHR,DSN=SYS1.MODGEN
//SYSIN    DD  *
***********************************************************************
*                                                                     *
*                    UNIT DISPLAY FACILITY (UDF)                      *
*                                                                     *
* ABSTRACT: DISPLAY ONLINE DISK VTOC'S AND DATASETS.                  *
*                                                                     *
* EXTERNAL REFERENCE: ISPLINK                                         *
*                                                                     *
***********************************************************************
*
* NOTE THE FOLLOWING ENTRY POINT LOGIC IS NON-STANDARD. IN PARTICULAR,
*      IT WILL NOT WORK WITH CALLED PL/I PROGRAMS.
*
***********************************************************************
         MACRO
         DX    &HEXTYP,&CHAR
         DC    X'&HEXTYP'
         DC    CL7&CHAR
         MEND
         EJECT
UDF      CSECT
CVTPTX   EQU   X'010'
CVTUCBSX EQU   X'434'
         USING *,13,12,11,9
         B     72(,15)
         DC    17F'0'
         STM   14,12,12(13)   SAVE CALLING PGM'S REGISTERS
         LA    R2,0(,R15)     LOAD EPA IN R2 FOR WORK REGISTER
         ST    R2,8(R13)      LINK SAVE AREAS
         ST    R13,4(R2)      SAVE THE CALLER'S R13
*
SETBASES DS    0H
         LR    R13,R2         SET SAVE AREA AND BASE 1
         LR    R12,R2         SET
         A     R12,L4096          BASE 2
         LR    R11,R12        SET
         A     R11,L4096          BASE 3
         LR    R9,R11         SET
         A     R9,L4096           BASE 4
*
         LA    R0,4095
         GETMAIN R,LV=(0)
         ST    R1,GETAREA
*
INITFLDS DS    0H
         MVC   TABAREA(80),SPACES
         XC    ISPARMS(40),ISPARMS
         B     SHOWDASD
*
SHOWAGIN DS    0H
*
         L     R15,ISPLINK
         CALL  (15),(VDELETE,ASTER),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(TBEND,VDFTABLE),VL,MF=(E,ISPARMS)
*
         B     SHOWDASD    DISPLAY THE UNIT'S
*
SHOWDONE DS    0H
*                                  DELETE THE TABLE VARIABLES
         L     R15,ISPLINK
         CALL  (15),(VDELETE,ASTER),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(TBEND,VDFTABLE),VL,MF=(E,ISPARMS)
*
         LA    R0,4095
         L     R1,GETAREA
         FREEMAIN R,LV=(0),A=(1)
*
         L     R13,4(R13)     RESTORE THE CALLER'S R13
         LM    14,12,12(13)   RETURN TO OUR CALLER
         SR    15,15          SET RC=0
         BR    14             AND RETURN
         EJECT
**************************** END ***************************
CVTPRT   EQU   *
         UNPK  CVTA(9),ADDRUCB(5)
         TR    CVTA(8),HEXTAB
         LM    R14,R15,CVTA
         BR    R7
*
L4096    DC    F'4096'
*
************************************************************
*
SHOWDASD DS    0H
*
         MVC   SELCODE(1),SPACES  SET SELECTION CODE TO SPACES
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,VTOLINEP,TABAREA,CHAR,L77),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,VSLP,SELCODE,CHAR,L1),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,VCMD,CMDDATA,CHAR,L1),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,ONLI,ONLID,CHAR,L1),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,OFFL,OFFLD,CHAR,L1),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,DASD,DASDD,CHAR,L1),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,TAPE,TAPED,CHAR,L1),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,COMM,COMMD,CHAR,L1),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,TUBE,TUBED,CHAR,L1),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,UREC,URECD,CHAR,L1),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CTCC,CTCCD,CHAR,L1),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,OTHR,OTHRD,CHAR,L1),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VDEF,CH,CHD,CHAR,L2),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(TBCREATE,VDFTABLE,NULLENT,DANAMLST,NOWRITE),      X
               VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(TBVCLEAR,VDFTABLE),VL,MF=(E,ISPARMS)
*
         MVI   ONLID,C' '
         L     R15,ISPLINK
         CALL  (15),(VGET,ONLI,SHARED),VL,MF=(E,ISPARMS)
*
         MVI   OFFLD,C' '
         L     R15,ISPLINK
         CALL  (15),(VGET,OFFL,SHARED),VL,MF=(E,ISPARMS)
*
         MVI   DASDD,C' '
         L     R15,ISPLINK
         CALL  (15),(VGET,DASD,SHARED),VL,MF=(E,ISPARMS)
*
         MVI   TAPED,C' '
         L     R15,ISPLINK
         CALL  (15),(VGET,TAPE,SHARED),VL,MF=(E,ISPARMS)
*
         MVI   COMMD,C' '
         L     R15,ISPLINK
         CALL  (15),(VGET,COMM,SHARED),VL,MF=(E,ISPARMS)
*
         MVI   CTCCD,C' '
         L     R15,ISPLINK
         CALL  (15),(VGET,CTCC,SHARED),VL,MF=(E,ISPARMS)
*
         MVI   TUBED,C' '
         L     R15,ISPLINK
         CALL  (15),(VGET,TUBE,SHARED),VL,MF=(E,ISPARMS)
*
         MVI   URECD,C' '
         L     R15,ISPLINK
         CALL  (15),(VGET,UREC,SHARED),VL,MF=(E,ISPARMS)
*
         MVI   OTHRD,C' '
         L     R15,ISPLINK
         CALL  (15),(VGET,OTHR,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VGET,CH,SHARED),VL,MF=(E,ISPARMS)
*
         CLI   ONLID,C' '
         BNE   O1
         MVI   ONLID,C'Y'
O1       CLI   OFFLD,C' '
         BNE   O2
         MVI   OFFLD,C'Y'
O2       CLI   DASDD,C' '
         BNE   O3
         MVI   DASDD,C'N'
O3       CLI   TAPED,C' '
         BNE   O4
         MVI   TAPED,C'Y'
O4       CLI   COMMD,C' '
         BNE   O5
         MVI   COMMD,C'N'
O5       CLI   CTCCD,C' '
         BNE   O6
         MVI   CTCCD,C'N'
O6       CLI   TUBED,C' '
         BNE   O7
         MVI   TUBED,C'N'
O7       CLI   URECD,C' '
         BNE   O8
         MVI   URECD,C'N'
O8       CLI   OTHRD,C' '
         BNE   O9
         MVI   OTHRD,C'N'
O9       DS    0H
*
         TITLE 'EXTRACT UCB INFORMATION'
AGAIN    L     R1,16                        CVT ADDRESS
         USING CVT,R1
* CHPID START
         L     R5,CVTOPCTP        RMCT
         DROP  R1
         L     R6,280(R5)         CMCT
         L     R7,20(R6)          CMB
         LA    R7,32(R7)          PAST HDR
         ST    R7,CMB
* CHPID END
*XTUCB   L     R15,CVTPTX
NXTUCB   DS    0H
*        L     R15,CVTUCBSX(,R15)
*        CALL  (15),(WORKAREA,DEVCLASS,ADDRUCB),VL
*        LTR   R15,R15
         LA    R6,WORKAREA
U        UCBSCAN COPY,WORKAREA=WORKAREA,UCBAREA=UCBAREA,RANGE=ALL,     1
               DYNAMIC=YES,DEVCLASS=ALL,RETCODE=RET,RSNCODE=RSN
*        SR    R6,R6
         L     R6,RET
         LTR   R6,R6
         BNZ   SHOWVTAB
         L     R8,GETAREA
         MVC   0(48,R8),UCBAREA
         USING UCBCMSEG,R8
*
ONLTST   TM    UCBSTAT,UCBONLI              ONLINE?
         BNO   OFFTST
         CLI   ONLID,C'Y'
         BNE   NXTUCB
         B     USEUCB
*
OFFTST   CLI   OFFLD,C'Y'
         BNE   NXTUCB
         B     USEUCB
*
USEUCB   DS    0H                           GOT HERE THEN DO
         MVC   UNITADDR,UCBNAME
*
         LA    R10,0
         ST    R10,EXTPWORK
         OC    EXTPWORK+1(3),UCBEXTP
         USING UCBCMEXT,R10
         L     R10,EXTPWORK
         LA    R7,0
         LH    R7,UCBASID
         LTR   R7,R7
         BZ    NOTASID
*
         LA    R7,0
         IC    R7,UCBASID
         SRL   R7,4
         LTR   R7,R7
         BZ    DO2
         IC    R5,HEX(R7)
         STC   R5,ASID
DO2      IC    R7,UCBASID
         N     R7,=F'15'
         IC    R5,HEX(R7)
         STC   R5,ASID+1
         IC    R7,UCBASID+1
         SRL   R7,4
         IC    R5,HEX(R7)
         STC   R5,ASID+2
         IC    R7,UCBASID+1
         N     R7,=F'15'
         IC    R5,HEX(R7)
         STC   R5,ASID+3
*
NOTASID  CLI   UCBTBYT3,UCB3DACC            DIRECT ACCESS?
         BNE   NOTDASD
*
         CLI   DASDD,C'Y'
         BNE   NXTUCB
*
         MVC   VOLNAME,UCBVOLI
*
         SR    R3,R3                        CLEAR A REG
         LH    R3,UCBUSER                   GET USE COUNT
         CVD   R3,DBLW                      CONVERT TO PRINT
         OI    DBLW+7,X'0F'                 FIX SIGN BITS
         UNPK  USECNT,DBLW+6(2)             PLACE IT ON THE LINE
*
         MVC   UNITTYPE(4),=C'3350'
         CLI   UCBUNTYP,X'0B'
         BE    DA00
         MVC   UNITTYPE(4),=C'3380'
         CLI   UCBUNTYP,X'0E'
         BE    DA00
         MVC   UNITTYPE(4),=C'3390'
         CLI   UCBUNTYP,X'0F'
         BE    DA00
         MVC   UNITTYPE(4),=C'VIO?'
*
DA00     EQU   *
         LA    R3,0
         IC    R3,UCBTBYT1
         SRL   R3,4
         IC    R5,HEX(R3)
*        STC   R5,UNITTYPE+6
         IC    R3,UCBTBYT1
         N     R3,=F'15'
         IC    R5,HEX(R3)
*        STC   R5,UNITTYPE+7
*
         TM    UCBTBYT2,UCBRVDEV   IS THIS VIO
         BZ    DA10                NO..
         MVC   UNITTYPE(4),=C' VIO'
*        MVC   OUTVOL,VIOUNIT      INDICATE SO..
*
DA10     EQU   *
         TM    UCBSTAT,UCBONLI
         BZ    TSTCMB              IT IS OFFLINE
*
         TM    UCBSTAB,UCBPGFL     IS THERE PAGING ON THIS PACK
         BZ    DASYR
         MVC   OUTUSAGE(3),PAGEPACK TELL THE USER
*
DASYR    TM    UCBSTAT,UCBSYSR     IS THIS THE SYSRES ?
         BZ    DA2                 NO... PRESS ON
         MVC   OUTUSAGE(3),SYSRES INDICATE SO
*
DA2      TM    UCBSTAB,UCBBPRV     IS THIS VOL PRIVATE ?
         BZ    DA3                 NO..
*        MVC   OUTSTATE,PRIVATE    SHOW IT IS SO
*
DA3      TM    UCBSTAB,UCBBPUB     IS IT PUBLIC ?
         BZ    DA4                 NO..
         MVC   OUTUSAGE(3),PUBLIC
*
DA4      TM    UCBSTAB,UCBBSTR     STORAGE ?
         BZ    DA5                 NO...
         MVC   OUTUSAGE(3),STORAGE
*
DA5      TM    UCBSTAT,UCBRESV     IS IT RESERVED ?
         BZ    DA6                 NO..
         MVC   OUTUSAGE(3),RESERVED
*
DA6      TM    UCBSTAT,UCBPRES     PERMANENTLY RESIDENT
         BZ    DA7                 NO..
*        MVC   OUTSTATE,PRES
*
DA7      B     TSTCMB
*
NOTDASD  CLI   UCBTBYT3,UCB3TAPE            TAPE         ?
         BNE   NOTTAPE
*
         CLI   TAPED,C'Y'
         BNE   NXTUCB
*
         TM    UCBSTAT,UCBONLI              ONLINE?
         BNO   TDIS
*
         MVC   VOLNAME,UCBVOLI
         TM    UCBWGT,UCBREW       IS IT REWIND ?
         BZ    TAD1                NO.. MOVE ON
         MVC   OUTUSAGE(3),=C'REW'
         DROP  R10
TAD1     LA    R10,0
         ST    R10,EXTPWORK
         OC    EXTPWORK+1(3),UCBXTNB
         USING UCBMT,R10
         L     R10,EXTPWORK
         TM    UCBSTAT,UCBALOC
         BNO   TDIS
         LA    R5,0
         LH    R5,UCBSIO
         CVD   R5,WORK
         OI    WORK+7,X'0F'
         MVC   UNITTYPE+4(1),SPACES
         UNPK  UNITTYPE+5(5),WORK+5(3)
*
TDIS     LA    R3,TAPETYPS         GET DEVICE TYPES ADDRESSES
TA0      CLC   UCBUNTYP,0(R3)      IS IT THIS ?
         BE    TA1                 SHORE IS
         LA    R3,TYPESIZE(R3)     GET NEXT IF NOT
         CLI   0(R3),255           STOPPER ?
         BNE   TA0
TA1      MVC   UNITTYPE(4),1(R3)       MOVE THE DESCRIPTOR
*
* CHECK  OUT THE DENSITYS AVAILABLE
*
TA10     TM    UCBTBYT2,UCBDUDN1+UCB2OPT1 DUAL  800/1600 ?
         BZ    TA2                 NO..
         MVC   UNITTYPE(9),DD1600
         TM    UCBTBYT2,UCB2OPT1   SUPPORT ASCII ?
         BZ    TA7                 NO..
         MVC   OUTUSAGE(2),ASCII
         B     TA7
TA2      TM    UCBTBYT2,UCBDUDN2   1600/6250 ?
         BZ    TA3                 NO..
         MVC   UNITTYPE(9),DD6250
         B     TA7
TA3      TM    UCBTBYT2,UCB2OPT0   7-TRACK ?
         BZ    TA4                 NO..
         MVC   UNITTYPE(9),DD1600
         MVC   OUTUSAGE(2),TRACK7
         B     TA7
TA4      TM    UCBTBYT1,UCBD1600   1600 BPI ?
         BZ    TA5
         MVC   UNITTYPE(9),D1600
         B     TA7
TA5      TM    UCBTBYT1,UCBD6250   6250 BPI ?
         BZ    TA7                 NO..
         MVC   UNITTYPE(9),D6250
TA7      B     TSTCMB
*
NOTTAPE  CLI   UCBTBYT3,UCB3CTC             CTC          ?
         BNE   NOTCTC
*
         CLI   CTCCD,C'Y'
         BNE   NXTUCB
*
         MVC   UNITTYPE(4),=C' CTC'
         B     TSTACT
*
NOTCTC   CLI   UCBTBYT3,UCB3COMM            COMM         ?
         BNE   NOTCOMM
*
         CLI   COMMD,C'Y'
         BNE   NXTUCB
*
         LA    R3,COMMTYPS         GET DEVICE TYPES ADDRESSES
         MVC   CVTA,UCBTBYT1
         NI    CVTA,15             GET MODEL CODE
CO0      CLC   CVTA(1),0(R3)       IS IT THIS ?
         BE    CO1                 SHORE IS
         LA    R3,TYPESIZE(R3)     GET NEXT IF NOT
         CLI   0(R3),255           STOPPER ?
         BNE   CO0
*
         MVC   CVTA,UCBUNTYP
         NI    CVTA,15             GET CONTROL UNIT-TYPE
         LA    R3,COMMCUS
CO00     CLC   CVTA(1),0(R3)       IS IT THIS ?
         BE    CO1                 SHORE IS
         LA    R3,TYPESIZE(R3)     GET NEXT IF NOT
         CLI   0(R3),255           STOPPER ?
         BNE   CO00
CO1      MVC   UNITTYPE(4),1(R3)       MOVE THE DESCRIPTOR
         B     TSTACT
*
NOTCOMM  CLI   UCBTBYT3,UCB3DISP            TUBE         ?
         BNE   NOTDISP
*
         CLI   TUBED,C'Y'
         BNE   NXTUCB
*
         LA    R3,TUBETYPS         GET DEVICE TYPES ADDRESSES
TU0      CLC   UCBUNTYP,0(R3)      IS IT THIS ?
         BE    TU1                 SHORE IS
         LA    R3,TYPESIZE(R3)     GET NEXT IF NOT
         CLI   0(R3),255           STOPPER ?
         BNE   TU0
TU1      MVC   UNITTYPE(4),1(R3)       MOVE THE DESCRIPTOR
         B     TSTACT
*
NOTDISP  CLI   UCBTBYT3,UCB3UREC            UNIT RECORD  ?
         BNE   NOTUR
*
         CLI   URECD,C'Y'
         BNE   NXTUCB
*
         LA    R3,URTYPES          GET DEVICE TYPES ADDRESSES
UR0      CLC   UCBUNTYP,0(R3)      IS IT THIS ?
         BE    UR1                 SHORE IS
         LA    R3,TYPESIZE(R3)     GET NEXT IF NOT
         CLI   0(R3),255           STOPPER ?
         BNE   UR0
UR1      DS    0H
         MVC   UNITTYPE(4),1(R3)       MOVE THE DESCRIPTOR
         B     TSTACT
*
NOTUR    DS    0H
*
         CLI   OTHRD,C'Y'
         BNE   NXTUCB
*
         B     TSTACT
*
TSTCMB   DS    0H                                USE UCBCHAN ADD CMB
         LH    R3,UCBCHAN
CMBPROC  DS    0H        ** ALL TIMES IN 128 MICROS, DIV BY 8 TO MS **
         B     TSTACT    CANNOT DO ON HOW ?
         L     R4,ADDRUCB
         S     R4,=F'30'
         LH    R4,0(R4)
         S     R4,=F'1'
         LR    R1,R4                R1 IS INDEX TO IOCOUNTER
         SLL   R1,2                 * 4
         SLL   R4,5                 * 32
         L     R7,CMB
         LH    R6,2(R4,R7)          SSCH COUNT SAMPLES
         LR    R5,R6                     COUNT
         C     R6,IOCOUNT(R1)            COUNT DIFFERENCE
         BNM   SUBIO
         A     R6,=F'65536'
SUBIO    S     R6,IOCOUNT(R1)            COUNT DIFFERENCE
         CVD   R6,WORK
         SLL   R6,3                      * 8 SO TIMES IN MS
         ST    R6,COUNTER
         OI    WORK+7,X'0F'
         UNPK  OUTIO(5),WORK+5(3)
         ST    R5,IOCOUNT(R1)             SAVE COUNT
*
         L     R6,4(R4,R7)               CONN  SAMPLES
         LR    R5,R6                     CONN
         S     R6,IOCONN(R1)             CONN  DIFFERENCE
         LA    R2,0
         LR    R3,R6
         LTR   R3,R3
         BZ    NODIV
         D     R2,COUNTER
NODIV    CVD   R3,WORK
         OI    WORK+7,X'0F'
         UNPK  OUTCONN(3),WORK+6(2)
         ST    R5,IOCONN(R1)             SAVE CONN
*
         L     R6,12(R4,R7)              DISC  SAMPLES
         LR    R5,R6                     DISC
         S     R6,IODISC(R1)             DISC  DIFFERENCE
         LA    R2,0
         LR    R3,R6
         LTR   R3,R3
         BZ    NODIV3
         D     R2,COUNTER
NODIV3   CVD   R3,WORK
         OI    WORK+7,X'0F'
         UNPK  OUTDISC(3),WORK+6(2)
         ST    R5,IODISC(R1)             SAVE PEND
*
         L     R6,8(R4,R7)               PEND  SAMPLES
         LR    R5,R6                     PEND
         S     R6,IOPEND(R1)             PEND  DIFFERENCE
         LA    R2,0
         LR    R3,R6
         LTR   R3,R3
         BZ    NODIV2
         D     R2,COUNTER
NODIV2   CVD   R3,WORK
         OI    WORK+7,X'0F'
         UNPK  OUTPEND(3),WORK+6(2)
         ST    R5,IOPEND(R1)             SAVE PEND
*
         L     R6,16(R4,R7)              CUQ   SAMPLES
         LR    R5,R6                     CUQ
         S     R6,IOCUQ(R1)              CUQ   DIFFERENCE
         LA    R2,0
         LR    R3,R6
         LTR   R3,R3
         BZ    NODIV4
         D     R2,COUNTER
NODIV4   CVD   R3,WORK
         OI    WORK+7,X'0F'
         UNPK  OUTCUQ(3),WORK+6(2)
         ST    R5,IOCUQ(R1)              SAVE CUQ
*
TSTACT   MVC   USTATUS(3),=C'ACT'
         TM    UCBSTAT,UCBALOC
         BO    TSTTYPE
         MVC   USTATUS(3),=C'RES'
         TM    UCBSTAT,UCBRESV
         BO    TSTTYPE
         MVC   USTATUS(3),=C'OFP'
         TM    UCBSTAT,UCBUNLD
         BO    TSTTYPE
         MVC   USTATUS(3),=C'ONL'
         TM    UCBSTAT,UCBONLI
         BO    TSTTYPE
         MVC   USTATUS(3),=C'OFF'
*
TSTTYPE  CLC   OUTUSAGE(3),SPACES
         BNE   TST4
         MVC   OUTUSAGE(3),=C'NRY'
         TM    UCBFLA,UCBNRY
         BO    TST4
         MVC   OUTUSAGE(3),=C'DEF'
         TM    UCBFLA,UCBDEFER
         BO    TST4
         MVC   OUTUSAGE(3),=C'   '
*
TST4     DS    0H
         TM    UCBFLA,UCBNRY
         BZ    CPA
         B     FSTTYPE
*
CPA      DS    0H
         CLI   UCBTBYT3,UCB3DACC            DIRECT ACCESS?
         BNE   FSTTYPE
         TM    UCBFLA,UCBSTRT+UCBHALT+UCBCLEAR
         BZ    TSTPND
         B     TSTPND      CANNOT DO ON HOW ?
         L     R5,ADDRUCB
         S     R5,=F'4'
         L     R5,0(R5)
         LTR   R5,R5
         BZ    TSTPND
         MVC   CCHHR(5),=XL5'00'
         CLC   0(4,R5),=C'IOQ '
         BNE   NOIOSB
         L     R5,8(R5)                   GET ADDR OF IOSB FROM IOQ
         LTR   R5,R5
         BZ    NOIOSB
         MVC   CCHHR(5),103(R5)
NOIOSB   LA    R5,5
         LA    R4,UNITTYPE-2
         LA    R7,CCHHR-1
         LA    R6,0
IOSBL    LA    R4,2(R4)
         LA    R7,1(R7)
         IC    R6,0(R7)
         SRL   R6,4
         IC    R3,HEX(R6)
         STC   R3,0(R4)
         IC    R6,0(R7)
         N     R6,=F'15'
         IC    R3,HEX(R6)
         STC   R3,1(R4)
         BCT   R5,IOSBL
         B     FSTTYPE
*
TSTPND   TM    UCBFLA,UCBPSNS
         BZ    FSTTYPE
         MVC   UNITTYPE(10),PENDSNSE
         B     FSTTYPE
*
FSTTYPE  DS    0H
         BAL   R7,CVTPRT           CONVERT IT
*        STCM  R14,B'0011',OUTADDR MOVE THE FIRST PART OF THE ADDRESS
*        STCM  R15,B'1111',OUTADDR+2 AND THE REST ALSO
*
         UCBINFO PATHMAP,MAPAREA=INFOAREA,DEVN=UCBCHAN,RETCODE=RC
         L     R15,RC
         LTR   R15,R15
         BNZ   BADRC
         LA    R7,INFOAREA
         LA    R7,8(R7)
         LH    R6,0(R7)
         SRL   R6,8
         CVD   R6,WORK
         OI    WORK+7,X'0F'
         UNPK  OUTCHPID(2),WORK+6(2)
         LA    R7,5(R7)
         LA    R4,OUTCHPID
NXTCHP   LA    R7,3(R7)
         LA    R4,3(R4)
         LH    R5,0(R7)
         SRL   R5,8
         ST    R5,R5SAVE
*
         SRL   R5,4
         IC    R5,HEX(R5)
         STC   R5,0(R4)
         L     R5,R5SAVE
         N     R5,=F'15'
         IC    R5,HEX(R5)
         STC   R5,1(R4)
*        CVD   R5,WORK
*        OI    WORK+7,X'0F'
*        UNPK  0(2,R4),WORK+6(2)
         BCT   R6,NXTCHP
*        MVC   OUTCHPID(12),=CL12'OK  RC      '
         B     GOON
BADRC    DS    0H
         MVC   OUTCHPID(12),=CL12'BAD RC      '
*
GOON     DS    0H
         CLC   CHD(2),SPACES
         BE    GOONX
         CLC   OUTCHPID+3(2),CHD
         BE    GOONX
         CLC   OUTCHPID+6(2),CHD
         BE    GOONX
         CLC   OUTCHPID+9(2),CHD
         BE    GOONX
         CLC   OUTCHPID+12(2),CHD
         BE    GOONX
         MVC   TABAREA(80),SPACES
         B     NXTUCB
*
GOONX    DS    0H
         TM    UCBJBNR,UCBJES3
         BZ    JDONE
         MVI   USTATUS-1,C'*'
         B     JDONE2
JDONE    DS    0H
         TM    UCBJBNR,UCBJ3DV
         BZ    JDONE2
         MVI   USTATUS-1,C'.'
JDONE2   DS    0H
         L     R15,ISPLINK
         CALL  (15),(VPUT,DANAMLST),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(TBADD,VDFTABLE),VL,MF=(E,ISPARMS)
*
         MVC   TABAREA(80),SPACES
*
         B     NXTUCB                       NOW GO ON
         TITLE 'UDF-SHOW UCB  TABLE'
*
***********************************************************************
*
*   NOW SHOW THE ISPPLIB FORMAT AND THE TABLE ON THE SCREEN
*
***********************************************************************
*
SHOWVTAB DS    0H
*
         L     R15,ISPLINK
         CALL  (15),(TBTOP,VDFTABLE),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(TBDISPL,VDFTABLE,UDFDATAB),VL,MF=(E,ISPARMS)
         LR    R8,R15
*
         L     R15,ISPLINK
         CALL  (15),(VPUT,ONLI,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VPUT,OFFL,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VPUT,DASD,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VPUT,TAPE,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VPUT,COMM,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VPUT,CTCC,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VPUT,TUBE,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VPUT,UREC,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VPUT,OTHR,SHARED),VL,MF=(E,ISPARMS)
*
         L     R15,ISPLINK
         CALL  (15),(VPUT,CH,SHARED),VL,MF=(E,ISPARMS)
*
***********************************************************************
*   DETERMINE WHETHER ANYTHING WAS ENTERED ON THE SCREEN
***********************************************************************
*
         C     R8,FZEROS      COMMAND ENTERED/LINE MODIFIED??
         BNE   SHOWDONE       NOPE
*
GOTSTUFF DS    0H
         MVC   SELSAVE(1),SELCODE    SAVE SEL CODE
         OC    SELSAVE(1),SPACES   UPPER-CASE IT
         MVC   SELCODE(1),SPACES  SET SELECTION CODE TO SPACES
*
         CLI   CMDDATA,C' '   IF NOT SPACES
         BNE   GOTCMD         THEN A COMMAND WAS ENTERED
*
* IF THE CRP IS NOT ZERO, THEN A LINE SELECTION CODE MUST HAVE BEEN
* ENTERED. I HAVE THE CURRENT LINE, SO EDIT THE SELECTION CODE.
*
         CLI   SELSAVE,C'S'   IS IT A "SHOW VTOC" REQUEST
         BE    SHOWVTOC       GO DO IT
*
* ADD TESTS FOR OTHER LINE SELECTION CODES HERE
*
         B     SHOWAGIN       GO REPEAT THE DISPLAY
*
SHOWVTOC DS    0H
*
         B     SHOWAGIN       GO REPEAT THE DISPLAY
*
         EJECT
*
*    ONLY COMMAND SUPPORTED IS "REPEAT"
*
GOTCMD   DS    0H
*OTCMD   TR    CMDDATA,CAPSONLY  UPPER-CASE ALL INPUT
*
*  NOTE THE TR TABLE IS SET UP TO DELETE SEMI-COLONS, IN ORDER TO
*       PREVENT COMMAND CONCATENATION.
*
         CLI   CMDDATA,C' '   IF COMMAND AREA IS SPACES
         BE    SHOWAGIN       THEN A COMMAND WAS NOT ENTERED
*
         B     SHOWAGIN       GO REPEAT THE SCREEN
         TITLE 'MISCELLANEOUS CONSTANTS'
         PRINT   GEN
         LTORG
*
         DS    0F
*DIT4    DC    X'40202120'
*DIT6    DC    X'402020202121'
*DIT8    DC    X'4020202020202120'
*
ISPLINK  DC    V(ISPLINK)
CMDDATA  DC    CL1' '
*
*78      DC    F'78'
FZEROS   DC    F'0'
F8       DC    F'8'
L1       DC    F'1'
L2       DC    F'2'
L3       DC    F'3'
L77      DC    F'77'
L40      DC    F'40'
L72      DC    F'72'
*
SELCODE  DC    CL1' '
SELSAVE  DC    CL1' '
*
TBADD    DC    C'TBADD '
TBCREATE DC    C'TBCREATE '
TBDISPL  DC    C'TBDISPL '
TBEND    DC    C'TBEND '
TBGET    DC    C'TBGET '
TBQUERY  DC    C'TBQUERY '
TBSKIP   DC    C'TBSKIP '
TBTOP    DC    C'TBTOP '
TBVCLEAR DC    C'TBVCLEAR '
VDEF     DC    C'VDEFINE  '
VDELETE  DC    C'VDELETE '
VGET     DC    C'VGET '
VPUT     DC    C'VPUT '
VREPLACE DC    C'VREPLACE '
SHARED   DC    C'SHARED '
*
NOWRITE  DC    C'NOWRITE '
*
NULLENT  DC    C'()'
*
DANAMLST DC    C'('             DISPLAY ACTIVE NAME LIST
SEL      DC    C'VSLA     '     SELECTION CODE
VTOLINE  DC    C'VTOLINE )'     JOB INFO
VTOLINEP DC    C'(VTOLINE )'    JOB INFO
VSLP     DC    C'(VSLA)'    SELECTION CODE
VCMD     DC    C'(VCMD)'    SELECTION CODE
ONLI     DC    C'(ONLI)'
OFFL     DC    C'(OFFL)'
DASD     DC    C'(DASD)'
TAPE     DC    C'(TAPE)'
COMM     DC    C'(COMM)'
TUBE     DC    C'(TUBE)'
UREC     DC    C'(UREC)'
CTCC     DC    C'(CTCC)'
OTHR     DC    C'(OTHR)'
CH       DC    C'(CH)'
ONLID    DS    CL1
OFFLD    DS    CL1
DASDD    DS    CL1
TAPED    DS    CL1
COMMD    DS    CL1
TUBED    DS    CL1
URECD    DS    CL1
CTCCD    DS    CL1
OTHRD    DS    CL1
CHD      DS    CL2
*
SPACES   DC    80CL1' '
CZEROS   DC    CL8'00000000'
INFOAREA DS    CL40
RC       DS    F
*
CHAR     DC    C'CHAR '
*
UDFDATAB DC    C'UDFDATAB'
VDFTABLE DC    C'VDFTABLE'
*
*
         TITLE 'REGISTER EQUATES'
***********************************************************************
*                                                                     *
*        REGISTER EQUATES                                             *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         TITLE 'WORKING STORAGE AREAS'
DOUBLEWD DS    D              PACKED DECIMAL WORK AREA
WORK     DS    D              PACKED DECIMAL WORK AREA
CHIP     DS    CL6
CMB      DS    F
*
ISPARMS  DS    10F            PARM LIST FOR ISPLINK
*
*     THE ISP TABLE FLAGS INDICATE NO ACTIVE TABLE (0), OR ONE EXISTS
*
         TITLE 'UDF-WORK AREAS      '
         LTORG
*
PENDSNSE DC    CL10'SNSE-PNDNG'
CVTA     DC    D'0',X'0'               PLUS A GARBAGE BYTE
CVTB     DC    F'0'
HEXTAB   EQU   *-C'0'
         DC    C'0123456789ABCDEF'
DBLW     DS    D                           CVD WORK AREA
TYPESIZE EQU   8                   SIZE OF A 'DX' ENTRY
PUBLIC   DC    CL3'SCR'
STORAGE  DC    CL3'STG'
RESERVED DC    CL3'RES'
SYSRES   DC    CL3'SYR'
COMMCUS  EQU   *
         DX    1,'LOCL'
*        DX    2,'2701'
*        DX    3,'2703'
*        DX    4,'2955'
         DX    5,'NCP '
         DX    FF,'COMM'
TAPETYPS EQU   *
         DX    1,'2400'
         DX    2,'0002'
         DX    3,'TAPE'
         DX    80,'3480'
         DX    81,'3490'
         DX    FF,'????'
DASDTYPS EQU   *
         DX    6,'2305-1'
         DX    7,'2305-2'
         DX    8,'2314'
         DX    9,'3330'
         DX    A,'3340'
         DX    B,'3350'
         DX    D,'3330-1'
         DX    FF,'DASD'
TUBETYPS EQU   *
         DX    8,'3066'
         DX    9,'3277'
         DX    A,'3284'
         DX    B,'3286'
         DX    FF,'TUBE'
COMMTYPS EQU   *
*        DX    1,'TWX'
*        DX    3,'2740'
*        DX    4,'2740'
*        DX    5,'2740C'
*        DX    6,'2741P'
*        DX    7,'BSC3'
         DX    FF,'COMM'
URTYPES  EQU   *
*        DX    1,'2540-R'
*        DX    2,'2540-P'
*        DX    3,'1442-R'
*        DX    4,'2501'
*        DX    5,'2520'
*        DX    6,'3505'
*        DX    8,'1403'
         DX    9,'PRIN'
*        DX    A,'1443'
         DX    B,'PRIN'
*        DX    C,'3525'
         DX    E,'LASR'
*        DX    10,'2671'
*        DX    18,'2495'
*        DX    1B,'1287'
*        DX    1C,'1288'
*        DX    20,'1052'
*        DX    22,'3210'
*        DX    23,'3215'
*        DX    30,'3213'
*        DX    44,'3540'
         DX    FF,'U/R'
*
*APSONLY DC    CL64' '
*        DC    CL10' '
*        DC    C'¢.<(+|&&'
*        DC    CL9' '
*        DC    C'!$*)'
*        DC    C' '             NOTE SEMICOLON CHANGED TO BLANK
*        DC    C'¬-/'
*        DC    CL9' ',C',%_>?',CL10' ',C':#@''="'
*        DC    CL16' ABCDEFGHI      '
*        DC    CL16' JKLMNOPQR      '
*        DC    CL16'  STUVWXYZ      '
*        DC    CL16'                '
*        DC    CL16' ABCDEFGHI      '
*        DC    CL16' JKLMNOPQR      '
*        DC    CL16'  STUVWXYZ      '
*        DC    CL16'0123456789      '
         TITLE 'UDF-LSPACE WORK AREA'
*
HEX      DC    C'0123456789ABCDEF'
EXTPWORK DS    1F
R5SAVE   DS    1F
CCHHR    DS    XL5
ADDRUCB  DS    A
WORKAREA DC    XL100'00'
UCBAREA  DC    XL50'00'
DEVCLASS DC    X'00'
*IOUNIT  DC    CL7'V-I-O'
DD6250   DC    CL9'1600/6250'
DD1600   DC    CL9' 800/1600'
D1600    DC    CL9'1600     '
D6250    DC    CL9'6250     '
ASCII    DC    CL2'-A'
TRACK7   DC    CL2'-7'
PAGEPACK DC    CL3'PAG'
         TITLE 'UDF-UCB  LINE FORMAT'
TABAREA  DS   0CL79
VOLNAME  DS   CL6                           VOLUME NAME
         DS   C                             DIRF FLAG
UNITADDR DS   CL3                           UNIT ADDRESS
         DS   CL1
ASID     DS   CL4
         DS   CL1
UNITTYPE DS   CL11
         DS   CL1
USTATUS  DS   CL3
         DS   CL1
OUTUSAGE DS   CL3
         DS   CL1
USECNT   DS   CL3                           # OF CURRENT ALLOCATIONS
         DS   CL1                           TOTAL FREE TRACKS
OUTCHPID DS   CL14
         DS   CL1
OUTIO    DS   CL5
         DS   CL1
OUTCONN  DS   CL3
         DS   CL1
OUTDISC  DS   CL3
         DS   CL1
OUTPEND  DS   CL3
         DS   CL1
OUTCUQ   DS   CL3
         DS   CL5
COUNTER  DS   F
ASTER    DC   C'*'
GETAREA  DS   F
RET      DS   F
RSN      DS   F
IOCOUNT  DC   445F'0'
IOCONN   DC   445F'0'
IODISC   DC   445F'0'
IOPEND   DC   445F'0'
IOCUQ    DC   445F'0'
         EJECT
*
* EQUATES....
*
         PRINT GEN
         TITLE 'COMMUNICATION VECTOR TABLE'
         CVT   DSECT=YES,PREFIX=NO
         TITLE 'UNIT CONTROL BLOCK'
         IEFUCBOB LIST=NO
         IOSDMAP
         END
/*
//L.SYSLMOD  DD   DISP=SHR,DSN=TEST.PGM.LOAD(UDF)
//L.SYSLIB   DD   DISP=SHR,DSN=ISP.V3R5M0.ISPLOAD
