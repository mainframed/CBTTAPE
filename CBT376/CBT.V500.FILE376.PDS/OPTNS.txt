PROC 0 T
CONTROL NOMSG
IF &T ¬= &STR() THEN -
CONTROL LIST CONLIST SYMLIST MSG
ISPEXEC TBOPEN   ZDFDAT
ISPEXEC TBCREATE ZDFDAT NAMES(PIKA ASID T JOB PSTEP STEP WHERE ST1 ST2 -
           MEMORY SWP DP PG WSS CPU DEC) NOWRITE
ISPEXEC TBTOP ZDFTABLE
/* ISPEXEC TBSKIP ZDFTABLE */
/* ISPEXEC TBSKIP ZDFTABLE */
RETRY:-
ISPEXEC TBSKIP ZDFTABLE
ISPEXEC TBGET ZDFTABLE
IF &LASTCC ¬=0 THEN GOTO HCONT
SET &PIKA = &STR(   )
SET &ASID    = &SUBSTR(1:4,&ACTLINE)
SET &T       = &SUBSTR(6:6,&ACTLINE)
SET &JOB     = &SUBSTR(8:15,&ACTLINE)
SET &PSTEP   = &SUBSTR(17:24,&ACTLINE)
SET &STEP    = &SUBSTR(26:33,&ACTLINE)
SET &ST1     = &SUBSTR(35:37,&ACTLINE)
SET &MEMORY  = &SUBSTR(1:6,&ACTLINB)
SET &SWP     = &SUBSTR(9:10,&ACTLINB)
SET &DP      = &SUBSTR(12:13,&ACTLINB)
SET L EQ 0    /* RANGES OVER &DP FROM RIGHT TO LEFT
SET DEC EQ 0  /* DECIMAL VALUE ACCUMULATOR
SET P EQ 1    /* TAKES VALUES 1, 16, 256, ETC
SET D EQ '00010203040506070809101112131415'
SET X EQ 0123456789ABCDEF
DO WHILE &LENGTH(&DP)-&L GT 0
  SET M EQ 1  /* RANGES OVER &D
  SET N EQ 1  /* RANGES OVER &X
  DO WHILE &SUBSTR(&N,&X) NE &SUBSTR(&EVAL((&LENGTH(&DP)-&L)),&DP)
  SET M EQ &M+2
  SET N EQ &N+1
  IF &N GT 16 THEN DO
    WRITE INVALID HEXADECMAL VALUE &DP
    EXIT
    END
  END
  SET W EQ &SUBSTR(&M+1:&M+2,&D)
  SET W EQ &W*&P
  SET DEC EQ &DEC+&W
  SET L EQ &L+1
  SET P EQ &P*16
END
SET &PG      = &SUBSTR(15:17,&ACTLINB)
SET &WSS     = &SUBSTR(19:23,&ACTLINB)
SET &CPU     = &SUBSTR(25:32,&ACTLINB)
ISPEXEC TBADD ZDFDAT ORDER
GOTO RETRY
HCONT:-
ISPEXEC TBTOP ZDFDAT
REDIS:-
ISPEXEC TBDISPL ZDFDAT PANEL(ZDFDAT)
SET &LCC = &LASTCC
IF &ST ¬= &STR() THEN DO
      IF &ST = 1 THEN ISPEXEC TBSORT   ZDFDAT FIELDS(ASID,C,A)
      IF &ST = 2 THEN ISPEXEC TBSORT   ZDFDAT FIELDS(T,C,A)
      IF &ST = 3 THEN ISPEXEC TBSORT   ZDFDAT FIELDS(JOB,C,D)
      IF &ST = 4 THEN ISPEXEC TBSORT   ZDFDAT FIELDS(PSTEP,C,D)
      IF &ST = 5 THEN ISPEXEC TBSORT   ZDFDAT FIELDS(STEP,C,D)
      IF &ST = 6 THEN ISPEXEC TBSORT   ZDFDAT FIELDS(ST1,C,D)
      IF &ST = 7 THEN ISPEXEC TBSORT   ZDFDAT FIELDS(MEMORY,C,D)
      IF &ST = 8 THEN ISPEXEC TBSORT   ZDFDAT FIELDS(WSS,C,D)
      IF &ST = 9 THEN DO
                      ISPEXEC TBSORT   ZDFDAT FIELDS(DEC,N,D)
                      /* HAVE A LOOK AT HEXDEC */
                      END
      IF &ST =10 THEN ISPEXEC TBSORT   ZDFDAT FIELDS(PG,C,D)
      IF &ST =11 THEN ISPEXEC TBSORT   ZDFDAT FIELDS(SWP,C,D)
      IF &ST =12 THEN ISPEXEC TBSORT   ZDFDAT FIELDS(CPU,C,D)
                      GOTO REDIS
                      END
ISPEXEC TBCLOSE ZDFDAT
EXIT CODE(0)
