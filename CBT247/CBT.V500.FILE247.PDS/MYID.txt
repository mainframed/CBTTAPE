*      TSO COMMAND PROCESSOR TO DISPLAY THE USERID OF THE INVOKER.
*        REGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
MYID     CSECT
         STM   R14,R12,12(R13)     SAVE CALLER'S REGISTERS
         LR    R12,R15             LOAD ENTRY POINT INTO BASE REGISTER
         USING MYID,R12            TELL THE ASSEMBLER R12 IS BASE
         LR    R15,R13             SAVE CALLER'S SAVE ADDRESS
         LA    R13,SAVE            GET OUR SAVEAREA ADDRESS
         ST    R15,SAVE+4          STORE HIS SAVEAREA INTO MINE + 4
         ST    R13,8(,R15)         STORE MINE INTO HIS SAVEAREA + 8
RUNCHAIN L     R3,16               POINT TO CVT.
         L     R3,0(,R3)           POINT TO TCB/ASCB WORDS
         L     R3,4(,R3)           POINT TO TCB.
         L     R3,X'B4'(,R3)       POINT TO JSCB.
         L     R3,X'108'(,R3)      POINT TO PSCB.
         MVC   MSGLINE+13(7),0(R3)    MOVE USERID IN FROM PSCB
         CLC   MSGLINE+13(7),=C'>7BYTES'
         BNE   SEVENCHR
         MVC   MSGLINE+13(8),X'54'(R3)
SEVENCHR DS    0H
         TPUT  MSGLINE,L'MSGLINE      DISPLAY THE MESSAGE ON THE TUBE
RETURN   DS    0H
         L     R13,SAVE+4          RELOAD CALLER'S SAVEAREA POINTER
         LM    R14,R12,12(R13)     RELOAD REGISTERS
         BR    R14                 RETURN TO CALLER
SAVE     DC    18F'0'              MY SAVE AREA
MSGLINE  DC    C'MY USERID IS         '      LINE FOR MESSAGE
         LTORG
         END
