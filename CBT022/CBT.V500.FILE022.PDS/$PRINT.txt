/*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*/
/* DOC:  THIS IS AN ISPF V2.2 ISREDIT MACRO                          */
/* REQ:  USER MUST BE IN ISPF EDIT MODE                              */
/*                                                                   */
/* FUNCTION: PRINTS CURRENT MEMBER                                   */
/*                                                                   */
/*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*/
PROC 0 X() HEADING( )
CONTROL MAIN
/*CONTROL LIST SYM CON MSG NOFLUSH*/
ISREDIT MACRO (COPIES,NO)
SET &ID=&SYSUID
SET &RC=0
ISREDIT (DSN) =  DATASET
ISREDIT (MEM) =  MEMBER
IF &MEM¬=   THEN SET &DSN=&STR(&DSN.(&MEM))
IF &SYSDSN('&DSN')¬=OK THEN DO
   WRITE
   WRITE PLEASE 'SAVE' THIS FILE THEN RETRY COMMAND
   WRITE
   EXIT
  END
IF &LASTCC=0 AND &MEM¬=  THEN SET &MEM=&STR((&MEM))
IF &NO=NO OR &NO=NOHEADING OR &NO=NOHEADINGS THEN +
   SET &HEADING=NOHEADING
SET &DEST=&STR(DEST(MISPRT1))
IF &DATATYPE(&COPIES)=NUM THEN SET &COPIES=&STR(COPIES(&COPIES))
IF &COPIES¬=  AND &DATATYPE(&COPIES)=CHAR THEN DO
   SET &DEST=
   IF &COPIES=F THEN SET &DEST=&STR(DEST(FINANCE))
   IF &COPIES=A THEN SET &DEST=
   IF &COPIES=M THEN SET &DEST=&STR(DEST(MISPRT1))
   IF (&COPIES¬=F AND &COPIES¬=M) AND &COPIES¬=  THEN  +
      SET &DEST=&STR(CLASS(&COPIES))
   SET &COPIES=
  END

CONTROL LIST MSG NOFLUSH
PRINTOFF '&DSN' &DEST  &COPIES &HEADING
CONTROL NOLIST MSG NOFLUSH

IF &DEST¬=  THEN SET &DEST=&SUBSTR(6:&LENGTH(&DEST)-1,&DEST)

SET &ZEDSMSG = &STR(PRINTED &DEST)
SET &ZEDLMSG = &STR(&DSN ON PRINTER &DEST)

ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(&RC)
