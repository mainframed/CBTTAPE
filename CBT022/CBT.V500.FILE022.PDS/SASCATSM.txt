
  *--------------------------------------------------------------*
  *                                                              *
  *   LIB: SAS.MACLIB(SASCATSM)                                  *
  *   DOC: MACROS NEEDED IN THE SASCATS JOB STREAM               *
  *                                                              *
  *--------------------------------------------------------------*;

    %MACRO _REPORT;
    %*-------------------------------------------------------*
     * MACRO: _REPORT                                        *
     * FUNCT: PRINTS A LISTING OF ALL POSSIBLE REPORTS       *
     *-------------------------------------------------------*;
     DATA _NULL_ ;
          SET CAT.REPORT ;
          REP=REPEAT('*',131);
          RDATE=DATE;
          FORMAT RDATE DATE7. ;
          PUT _PAGE_
            /// @2  REP
              / @1  '*'                                       @132 '*'
              / @1  '*' @40 'GENERATE THE FOLLOWING REPORTS '
                       'USING THE SPECFIED MACROS CALLED AS SHOWN'
                @132 '*'
              / @1  '*'                                       @132 '*'
              / @1  '*' @48 'DATE SAS FILE WAS CREATED = '
                             DATE WEEKDATE23.
                @132 '*'
              / @1  '*'                                       @132 '*'
              / @1  '*' @5  'REPORT_NAME' @30 'F_U_N_C_T_I_O_N'
                        @100 'PARMS'       @122 'DEPEND.'
                @132 '*'
              / @1  '*'                                       @132 '*'
              / @1  '*' @6  '%_TITLE   '
                @25 'THIS IS THE MAIN TITLE ON ALL REPORTS            '
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_VARDASD'
                @25 'THE VARAIBLES USED IN A PROC PRINT NON-VSAM DATA '
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_VARVSAM'
                @25 'THE VARAIBLES USED IN A PROC PRINT FOR VSAM DATA '
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_CATALOG'
                @25 'REPORT ON ALL CATALOG ENTRIES                    '
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_ALIAS  '
                @25 'REPORT ALL ALIASES BY ASSOCIATION                '
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_NVSAMD '
                @25 'REPORT ON ALL NON-VSAM ON DASD VIA VTOC+CATALOG  '
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_TAPE   '
                @25 'REPORT ON ALL TAPE, DEVT 3420 BY DSNAME + VOLUME'
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_CART   '
                @25 'REPORT ON ALL CARTS, DEVT 3480 BY DSNAME + VOLUME'
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_UCAT   '
                @25 'REPORT ON ALL USERCATALOGS                       '
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_GDGBASE'
                @25 'REPORT ON ALL GDG_BASES, EXCEPTION ON NOSCRATCH  '
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_GDG    '
                @25 'REPORT ON ALL GDG"S                              '
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_VSAM   '
                @25 'REPORT ON ALL VSAM CLUSTERS                      '
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_VSAMRPT'
                @25 'REPORT OF VSAM USAGE                             '
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_CISPLIT'
                @25 'REPORT ON CONTROL INTERVAL SPLITS > 10           '
                @98 'SPLITS=10'
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_CASPLIT'
                @25 'REPORT ON CONTROL AREA SPLITS > 2                '
                @98 'SPLITS=2'
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_NEWVSAM'
                @25 'REPORT ON ALL NEW VSAM CREATED 7 OR LESS DAYS AGO'
                @98 'DAYS=7'
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_NWNVSAM'
                @25 'REPORT ON ALL NEW NON-VSAM CREATED 7 OR LESS DAYS'
                @98 'DAYS=7'
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_SUMMARY'
               @25 'PRODUCE A SUMMARY BASED ON FIRST A NODE OF THE DSN'
                @98 'NODE=1'
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_JALIAS '
                @25 'CREATE DEFINE STATEMENTS FOR ALL ALIASES         '
                @98 'DDNAME=JCL'
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_JNVSAM '
                @25 'CREATE DEFINE STATEMENTS FOR ALL NON-VSAM        '
                @98  'DDNAME=JCL'
                @121 '%_NVSAMD'
                @132 '*'
              / @1  '*' @6  '%_JUCAT  '
                @25 'CREATE DEFINE STATEMENTS FOR ALL USERCATS        '
                @98 'DDNAME=JCL'
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_JGDG   '
                @25 'CREATE DEFINE STATEMENTS FOR ALL GDG-BASES       '
                @98 'DDNAME=JCL'
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_JPAGE  '
                @25 'CREATE DEFINE STATEMENTS FOR ALL PAGE SPACES     '
                @98 'DDNAME=JCL'
                @124 'N/A'
                @132 '*'
              / @1  '*' @6  '%_DEVTYPE'
                @25 'EVALUATE DEVICE SPECIFICATIONS                   '
                @124 'N/A'
                @132 '*'
              / @1  '*'                                       @132 '*'
              / @1  REP
              / _PAGE_
              ;
   %MEND;

    %MACRO _TITLE;
    %*-------------------------------------------------------*
     * MACRO: _TITLE                                         *
     * FUNCT: MAIN TITLE FOR ALL REPORTS                     *
     *-------------------------------------------------------*;
     TITLE 'CATALOG REPORT  FOR  RIVERSIDE METHODIST HOSPITAL';
     %* CANCEL FOOTNOTE IF ANY ;
     FOOTNOTE;
   %MEND;

    %MACRO _VARDASD;
    %*-------------------------------------------------------*
     * MACRO: _VARDASD                                       *
     * FUNCT: CREATE 'VAR' STATEMENTS FOR PROC PRINTS        *
     *        THROUGHOUT THIS REPORT                         *
     *-------------------------------------------------------*;
        VAR NONVSAM VOLSER  DEVT  JDATE
            DSORG   EXTENTS REFDT ALLOC UNUSED OWNERID
            ;
     %MEND;

    %MACRO _VARVSAM;
    %*-------------------------------------------------------*
     * MACRO: _VARVSAM                                       *
     * FUNCT: CREATE 'VAR' STATEMENTS FOR PROC PRINTS        *
     *        THROUGHOUT THIS REPORT                         *
     *-------------------------------------------------------*;
          VAR CLUSTER VOLSER    JDATE    DEVT    RACF
              CISZ    CI_SPLTS  CA_SPLTS
              ALLOC   PRI       SEC      EXTENTS
              RECTOT  RECDEL    RECINSRT RECUPDT RECRET EXCPS
              KEYLEN  AVGLRECL  MAXLRECL BUFSPACE
              CI_FREE CA_FREE   FREEBYTE
              REC_CI  MAX_RECS
              ;
     %MEND;

    %MACRO _CATALOG;
    %*-------------------------------------------------------*
     * MACRO: _CATALOG                                       *
     * FUNCT: REPORT ON MASTER AND USER CATALOGS             *
     *-------------------------------------------------------*;
     PROC PRINT DATA=CAT.CATALOG LABEL SPLIT='*' NOOBS;
     %_TITLE
     TITLE2 '              C A T A L O G    E N T R Y (S) ';
     %MEND;

    %MACRO _ALIAS;
    %*-------------------------------------------------------*
     * MACRO: _ALIAS                                         *
     * FUNCT: REPORT ON ALL ALIASES                          *
     *-------------------------------------------------------*;
     PROC SORT DATA=CAT.VTOCS OUT=VTOCS;
          BY NONVSAM;   * MUST-MUST-MUST BE SORTED BY NONVSAM ;
  %* RETURN # OF ENTRIES ON DISK PER  ALIAS ;
     DATA PREFIX (KEEP=ALIAS N);
          SET VTOCS;
          IF _N_=1 THEN DO; DOT='.';FORMAT ALIAS $8.;ALIAS='        ';
             RETAIN DOT ALIAS;END;
          N+1;
          X=INDEX(NONVSAM,DOT);
          X=X-1;
          IF X > 0 THEN DO;
             IF SUBSTR(NONVSAM,1,X)¬=ALIAS THEN DO;
                IF _N_¬=1 THEN OUTPUT;
                N=0;
                ALIAS=SUBSTR(NONVSAM,1,X);
                RETAIN ALIAS;
               END;
            END;
  %* READY FOR MERGE ;
     PROC SORT DATA=CAT.ALIAS OUT=ALIAS;     BY ALIAS;
     PROC SORT DATA=PREFIX;                  BY ALIAS;
     DATA ALIASES;   MERGE ALIAS PREFIX;     BY ALIAS;
          IF LENGTH(ASSOC) < 5 THEN DELETE;
          AL=ALIAS||'_'||N;   AL=COMPRESS(AL);
     PROC SORT DATA=ALIASES;  BY ASSOC ALIAS;
     PROC FORMS DATA=ALIASES W=13 L=1 D=0 NA=12 B=4;
          LINE 1 AL;
          BY ASSOC;
          %_TITLE
          TITLE2 'ALL ALIASES' ;
          TITLE3 'BY ASSOCIATION';
          FOOTNOTE1
       'ALL ALIASES HAVE AND ASSOCIATED NUMBER OF ENTRIES: ALIAS_###';
    %MEND;

    %MACRO _NVSAMD;
    %*-------------------------------------------------------*
     * MACRO: _NVSAMD                                        *
     * FUNCT: REPORT ON THE NONVSAM DISK DATASETS            *
     *        THOSE CATALOGED AND NOT CATALOGED              *
     *-------------------------------------------------------*;
     DATA VTOCNVSM;
          SET CAT.VTOCS;
       %* DELETE ALL VSAM;
          IF DSORG='VS' THEN DELETE;
     PROC SORT DATA=VTOCNVSM;                  BY VOLSER NONVSAM ;
     PROC SORT DATA=CAT.NVSAMD OUT=NVSAMD;     BY VOLSER NONVSAM ;
  %* MERGE CATALOG AND VTOC;
     DATA TOTNVSAM;   MERGE VTOCNVSM NVSAMD;   BY VOLSER NONVSAM ;
       %* CALC PERCENT USED;
          PC_T=(UNUSED/ALLOC)*100;PCT=ROUND(PC_T,.01);DROP PC_T;
          LABEL PCT='PERCENT*TRACKS*UNUSED';
     PROC SORT DATA=TOTNVSAM;     BY VOLSER;
     PROC PRINT DATA=TOTNVSAM LABEL SPLIT='*' NOOBS;
          BY VOLSER;
          %_VARDASD
          %_TITLE
          TITLE2 'ALL NONVSAM' ;
          TITLE3 'DISK';
          FOOTNOTE1
          'IF DEVICE-TYPE AND CREATE-DATE ARE BLANK, NOT CATALOGED';
    %MEND;

    %MACRO _SUMMARY(NODE=1);
    %*-------------------------------------------------------*
     * MACRO: _SUMMARY                                       *
     * FUNCT: SUMMARIZE DATA ON VTOC FILE BY NODE            *
     *-------------------------------------------------------*;
     PROC SUMMARY DATA=CAT.VTOCS NWAY ;
          VAR ALLOC UNUSED ;
          CLASS NODE&NODE ;
          OUTPUT  OUT=NODE
                  MEAN=MEAN_AL MEAN_UN
                  SUM=SUM_AL SUM_UN
                  N=NUM_AL NUM_UN
                  ;
     PROC SORT  DATA=NODE;  BY NODE&NODE ;
     PROC PRINT DATA=NODE LABEL SPLIT='*' NOOBS;
          VAR NODE&NODE SUM_AL SUM_UN  NUM_AL NUM_UN
             MEAN_AL MEAN_UN
             ;
        LABEL SUM_AL=SUM*ALLOC'     SUM_UN='SUM*UNUSED'
              NUM_AL=NUMBER*ALLOC'  NUM_UN='NUMBER*UNUSED'
              MEAN_AL='MEAN*ALLOC'  MEAN_UN='MEAN*UNUSED'
             ;
          %_TITLE
          TITLE2 'SPACE USED BY NODE';
    %MEND;

    %MACRO _TAPE;
    %*-------------------------------------------------------*
     * MACRO: _TAPE;                                         *
     * FUNCT: REPORT ON THE TAPE, DEVICE TYPE 3420           *
     *        DATASETS BY DATASET NAME AND VOLSER            *
     *-------------------------------------------------------*;
     PROC SORT DATA=CAT.NVSAMT OUT=NVSAMT;    BY NONVSAM;
     PROC PRINT DATA=NVSAMT  NOOBS SPLIT='*' LABEL;
          VAR NONVSAM VOLSER DEVT JDATE OWNERID;
          %_TITLE
          TITLE2 'TAPE - 3420';
          TITLE3 'BY DATASET NAME';
     PROC SORT DATA=NVSAMT;     BY VOLSER ;
     PROC PRINT DATA=NVSAMT  NOOBS SPLIT='*' LABEL;
          VAR VOLSER NONVSAM DEVT JDATE OWNERID;
          TITLE4 'BY VOLSER';
    %MEND;

    %MACRO _CART;
    %*-------------------------------------------------------*
     * MACRO: _CART;                                         *
     * FUNCT: REPORT ON THE CART, DEVICE TYPE 3480           *
     *        DATASETS BY DATASET NAME AND VOLSER            *
     *-------------------------------------------------------*;
     PROC SORT DATA=CAT.NVSAMC OUT=NVSAMC;    BY NONVSAM;
     PROC PRINT DATA=NVSAMC  NOOBS SPLIT='*' LABEL;
          VAR NONVSAM VOLSER DEVT JDATE OWNERID;
          %_TITLE
          TITLE2 'TAPE - 3480';
          TITLE3 'BY DATASET NAME';
     PROC SORT DATA=NVSAMC;    BY VOLSER ;
     PROC PRINT DATA=NVSAMC  NOOBS SPLIT='*' LABEL;
          VAR VOLSER NONVSAM DEVT JDATE OWNERID;
          TITLE3 'BY VOLSER';
    %MEND;

    %MACRO _UCAT;
    %*-------------------------------------------------------*
     * MACRO: _UCAT                                          *
     * FUNCT: REPORT ON USERCATALOGS                         *
     *-------------------------------------------------------*;
     PROC SORT DATA=CAT.USERCAT OUT=USERCAT;   BY UCATNAME VOLSER;
     PROC PRINT DATA=USERCAT  NOOBS SPLIT='*' LABEL;
          %_TITLE
          TITLE2 'ALL USERCATS';
    %MEND;

    %MACRO _GDGBASE;
    %*-------------------------------------------------------*
     * MACRO: _GDGBSAE                                       *
     * FUNCT: PROCESS AND REPORT ON GDGBASES                 *
     *        INCLUDING GDGS  WITH THE OPTION OF NO-SCRATCH  *
     *        SPECIFIED                                      *
     *-------------------------------------------------------*;
     PROC SORT DATA=CAT.GDGS OUT=GDGS;     BY GDGNAME ;
     PROC PRINT DATA=GDGS  NOOBS SPLIT='*' LABEL;
          VAR GDGNAME JDATE LIMIT OPTIONS;
          %_TITLE
          TITLE2 'ALL GDG ENTRIES';
  %* NOTE ALL GDG BASES WITH NOTE OF "NOSCRATCH" - PROBLEM !! ;
     DATA GDGNOSCR;  SET GDGS;  IF SUBSTR(OPTIONS,1,9)='NOSCRATCH';
     PROC PRINT DATA=GDGNOSCR  NOOBS SPLIT='*' LABEL;
          VAR GDGNAME JDATE LIMIT OPTIONS;
          TITLE2 'GDG ENTRIES';
          TITLE3 '                  OPTIONS:  NOSCRATCH';
    %MEND;

    %MACRO _GDG;
    %*-------------------------------------------------------*
     * MACRO: _GDG                                           *
     * FUNCT: REPORT ON ALL GDGS                             *
     *-------------------------------------------------------*;
     PROC SORT DATA=CAT.GDGNVSAM OUT=GDGNVSAM;  BY NONVSAM ;
     PROC PRINT DATA=GDGNVSAM  NOOBS SPLIT='*' LABEL;
          VAR VOLSER NONVSAM DEVT JDATE OWNERID;
          %_TITLE
          TITLE2 'ALL CATALOGED GDG LEVELS';
    %MEND;

    %MACRO _VSAM;
    %*-------------------------------------------------------*
     * MACRO: _VSAM                                          *
     * FUNCT: REPORT ON ALL VSAM                             *
     *-------------------------------------------------------*;
     PROC SORT DATA=CAT.VSAM OUT=VSAM;     BY CLUSTER;
     PROC PRINT DATA=VSAM  SPLIT='*' LABEL;
          %_VARVSAM
          %_TITLE
          TITLE2 'ALL VSAM CLUSTERS';
    %MEND;

    %MACRO _VSAMRPT;
    %*-------------------------------------------------------*
     * MACRO: _VSAMRPT                                       *
     * FUNCT: CREATE VSAM USAGE REPORTS                      *
     *-------------------------------------------------------*;
     DATA SP_VSAM  UN_VSAM;
          SET CAT.VSAM;
          IF _N_ = 1  THEN DO;  X='.';  RETAIN X;  END;
          NM=INDEX(CLUSTER,X);  NM=NM-1;
          IF SUBSTR(CLUSTER,1,NM)='SYS1' THEN DELETE;
        * CALCULATE ALLOCATIONS BY CONVERTING EVERYTHING TO TRACKS ;
          IF ALLOC='CYLINDER' THEN DO;
             IF DEVT='3380' THEN AMT=15;  ELSE AMT=30;
             PRI=PRI*AMT;
             SEC=SEC*AMT;
             DROP AMT;
            END;
          ALLOC='TRK';
        * DATASET NOT IN USE - NO RECORDS ;
          IF RECTOT = 0  THEN DO;
             UNUSED=PRI+SEC;
             LABEL UNUSED='UNUSED*TRACKS';
             OUTPUT UN_VSAM;
            END;
        * CALC % USED SPACE  ;
          USEDSPC=0;
          IF AVGLRECL=0 THEN AVGLRECL=MAXLRECL;
          IF FREEBYTE ¬= 0 AND RECTOT ¬= 0  THEN DO;
             USEDSPC=(FREEBYTE/AVGLRECL)/(RECTOT*AVGLRECL) ;
             USEDSPC=ROUND(USEDSPC,.01);
             LABEL USEDSPC='UNUSED*RECORDS*  %' ;
             OUTPUT SP_VSAM ;
            END;
     PROC SORT DATA=UN_VSAM;  BY DESCENDING UNUSED;
     PROC PRINT LABEL SPLIT='*'  DATA=UN_VSAM;
          VAR CLUSTER VOLSER    JDATE    UNUSED
              ;
          SUM UNUSED;
     %_TITLE
     TITLE2 'UNUSED VSAM DATASETS';
     TITLE3 'SORTED BY UNUSED SPACE';
     TITLE4 'IN TRACKS';
     PROC PRINT LABEL SPLIT='*'  DATA=SP_VSAM;
          VAR CLUSTER VOLSER    JDATE
              RECTOT  FREEBYTE  USEDSPC
              ALLOC   PRI       SEC      AVGLRECL
              ;
    %MEND;

    %MACRO _CISPLIT(SPLITS=10);
    %*-------------------------------------------------------*
     * MACRO: _CISPLIT                                       *
     * FUNCT: REPORT ON CI SPLITS BASED ON CRITERIA          *
     *        SPECIFIED IN THE MACRO, DEFAULT 10             *
     *-------------------------------------------------------*;
     DATA CISPLITS; SET CAT.VSAM; IF CI_SPLTS >= &SPLITS THEN OUTPUT;
     PROC PRINT DATA=CISPLITS SPLIT='*' LABEL;
        %_VARVSAM
        %_TITLE
        TITLE2 'ALL VSAM CLUSTERS';
        TITLE3 'WITH CI-SPLITS GREATER THAN  &SPLITS';
   %MEND;

    %MACRO _CASPLIT(SPLITS=2);
    %*-------------------------------------------------------*
     * MACRO: _CASPLIT                                       *
     * FUNCT: REPORT ON CA SPLITS BASED ON CRITERIA          *
     *        SPECIFIED IN THE MACRO, DEFAULT 2              *
     *-------------------------------------------------------*;
     DATA CASPLITS; SET CAT.VSAM; IF CA_SPLTS >= &SPLITS THEN OUTPUT;
     PROC PRINT DATA=CASPLITS SPLIT='*' LABEL;
        %_VARVSAM
        %_TITLE
        TITLE2 'ALL VSAM CLUSTERS';
        TITLE3 'WITH CA-SPLITS GREATER THAN  &SPLITS';
   %MEND;

    %MACRO _NEWVSAM(DAYS=7);
    %*-------------------------------------------------------*
     * MACRO: _NEWVSAM                                       *
     * FUNCT: REPORT ON NEW VSAM DATASETS CREATED WITHIN     *
     *        X NUMBER OF DAYS, DEFAULT IS 7 DAYS            *
     *-------------------------------------------------------*;
     DATA NEWVSAM;  SET CAT.VSAM;
          TDAY = TODAY()- &DAYS ;
          IF JDATE < TDAY THEN DELETE;
          DROP TDAY;
        * CALCULATE ALLOCATIONS BY CONVERTING EVERYTHING TO TRACKS ;
          IF ALLOC='CYLINDER' THEN DO;
             IF DEVT='3380' THEN AMT=15;
                            ELSE AMT=30;
             PRI=PRI*AMT;
             SEC=SEC*AMT;
            END;
          ALLOC='TRK';
          OUTPUT;
     PROC SORT  DATA=NEWVSAM;    BY CLUSTER VOLSER;
     PROC PRINT DATA=NEWVSAM  LABEL SPLIT='*' NOOBS;
          %_VARVSAM
          SUM PRI SEC;
          %_TITLE
          TITLE2 'WITHIN THE LAST &DAYS DAYS';
          TITLE3 'BY CLUSTER';
   %MEND;

    %MACRO _NWNVSAM(DAYS=7);
    %*-------------------------------------------------------*
     * MACRO: _NWNVSAM                                       *
     * FUNCT: REPORT ON NEW NON-VSAM DATASETS CREATED WITHIN *
     *        X NUMBER OF DAYS, DEFAULT IS 7 DAYS            *
     *-------------------------------------------------------*;
     DATA NEWNVSAM;  SET TOTNVSAM;
          TDAY = TODAY() - &DAYS;
          IF JDATE>=TDAY THEN OUTPUT;
          DROP TDAY;
     PROC SORT DATA=NEWNVSAM;    BY NONVSAM VOLSER;
     PROC PRINT DATA=NEWNVSAM LABEL SPLIT='*' NOOBS;
          %_VARDASD
          %_TITLE
          TITLE2 'NONVSAM DATASETS' ;
          TITLE3 'WITHIN THE LAST &DAYS DAYS';
   %MEND;

    %MACRO _JALIAS(DDNAME=JCL);
    %*-------------------------------------------------------*
     * MACRO: _JALIAS                                        *
     * FUNCT: CREATE JCL FOR ALIAS REDEFINITIONS             *
     *-------------------------------------------------------*;
     PROC SORT DATA=CAT.ALIAS OUT=ALIAS;    BY ASSOC ALIAS;
     DATA _NULL_; SET ALIAS;
          FILE &DDNAME   LS=80   NOPRINT   NOTITLE;
          IF _N_=1 THEN
             PUT @2  '/*'  @69 '*/'
               / @2  '/*'  @30 'ALIAS DEFINITIONS'   @69 '*/'
               / @2  '/*'  @69 '*/'
              ;
          PUT @2  'DEF ALIAS (NAME ( ' ALIAS $8.
                  ' )  REL( ' ASSOC  ' ) ) -'
            / @6 'CAT ( MASTER.CATALOG ) '
            ;
      %MEND;

    %MACRO _JNVSAM(DDNAME=JCL);
    %*-------------------------------------------------------*
     * MACRO: _JNVSAM                                        *
     * FUNCT: CREATE JCL FOR NONVSAM REDEFINITIONS           *
     *-------------------------------------------------------*;
     PROC SORT DATA=CAT.NVSAMD OUT=NVSAMD; BY VOLSER NONVSAM;
     DATA _NULL_; SET NVSAMD;
          FILE &DDNAME   LS=80   NOPRINT   NOTITLE;
          IF _N_=1 THEN
             PUT @2  '/*'    @69 '*/'
               / @2  '/*'    @30 'NONVSAM DEFINITIONS'  @69 '*/'
               / @2  '/*'    @69 '*/'
                 ;
       PUT / @2 'DELETE  ' NONVSAM  ' NONVSAM  NOSCRATCH    -'
               / @10 'CAT ( MASTER.CATALOG ) '
            ;
       IF SUBSTR(NONVSAM,1,4)='SYS1' AND VOLSER='******' THEN
          PUT @2  'DEF NONVSAM                   - '
            / @14 '( NAME ( ' NONVSAM ' ) -'
            / @14 '  VOL  ( ****** )      -'
            / @14 '  DEVT ( 0000 )        -'
            / @14 ')                       - '
            / @10 'CAT ( MASTER.CATALOG ) '
            ;
      ELSE  IF OWNERID=. THEN
          PUT @2  'DEF NONVSAM                   - '
            / @14 '( NAME ( ' NONVSAM ' ) -'
            / @14 '  VOL  ( ' VOLSER ' )      -'
            / @14 '  DEVT ( ' DEVT ' )      -'
            / @14 ')                       - '
            / @10 'CAT ( ' CATNAME ' )'
            ;
     ELSE PUT @2  'DEF NONVSAM                   - '
            / @14 '( NAME ( ' NONVSAM ' ) -'
            / @14 '  VOL  ( ' VOLSER ' )      -'
            / @14 '  DEVT ( ' DEVT ' )      -'
            / @14 '  OWNER ( ' OWNERID ' )    -'
            / @14 ')                        - '
            / @10 'CAT ( MASTER.CATALOG ) '
            ;
   %MEND;

    %MACRO _JUCAT(DDNAME=JCL);
    %*-------------------------------------------------------*
     * MACRO: _JUCAT                                         *
     * FUNCT: REDEFINE ALL USERCAT ENTRIES                   *
     *-------------------------------------------------------*;
     PROC SORT DATA=CAT.USERCAT OUT=USERCAT;   BY UCATNAME VOLSER;
     DATA _NULL_; SET USERCAT;
          FILE &DDNAME   LS=80   NOPRINT   NOTITLE;
          IF _N_=1 THEN
             PUT @2  '/*'       @69 '*/'
               / @2  '/*'
                 @20 'USERCATALOG DEFINITIONS - NEED MODIFICATION'
                 @69 '*/'
               / @2  '/*'       @69 '*/'
                 ;
          PUT @2  'DEFINE USERCATALOG            - '
            / @21 '(  NAME ( ' UCATNAME ' )  - '
            / @21 '   VOL  ( ' VOLSER ' )         - '
            / @21 '   DEVT ( ' DEVT ' )         - '
            / @21 '   FILE ( DDNAME )          - '
            / @21 '   CYL  ( PRI SEC )         - '
            / @21 '   CISZ ( 512 )             - '
            / @21 '   FSPC ( 10 5 )            - '
            / @21 '   ICFCATALOG               - /* NOTE THIS */ '
            / @21 '   RECSZ ( 4086 32400 )     - '
            / @21 '   SHR ( 3 4 )              - '
            / @21 ')                          - '
            / @18 'CAT ( MASTER.CATALOG ) '
            ;
     DATA _NULL_; SET USERCAT;
          FILE JCL LS=80 NOPRINT NOTITLE;
          IF _N_=1 THEN
             PUT @2  '/*'          @69 '*/'
               / @2  '/*'
                 @20 'USERCATALOG DEFINITIONS - CONNECTIONS'
                 @69 '*/'
               / @2  '/*'           @69 '*/'
                 ;
          PUT @2  'IMPORT CONNECT OBJECTS (      - '
            / @27 '( ' UCATNAME ' -'
            / @27 '  DEVT ( ' DEVT ' )           -'
            / @27 '  VOL  ( ' VOLSER ' )  )        -'
            / @27 ')                               -'
            / @9 'CAT ( MASTER.CATALOG )'
            ;
   %MEND;

    %MACRO _JGDG(DDNAME=JCL);
    %*-------------------------------------------------------*
     * MACRO: _JGDG                                          *
     * FUNCT: REDEFINE ALL GDG-BASES                         *
     *-------------------------------------------------------*;
     PROC SORT DATA=CAT.GDGS OUT=GDGS; BY GDGNAME ;
     DATA _NULL_; SET GDGS;
          FILE &DDNAME   LS=80   NOPRINT   NOTITLE;
          IF _N_=1 THEN
             PUT @2  '/*'      @69 '*/'
               / @2  '/*'  @30 'GDG DEFINITIONS' @69 '*/'
               / @2  '/*'      @69 '*/'
                 ;
          PUT @2  'DEFINE GENERATIONDATAGROUP    - '
            / @21 '( NAME ( '  GDGNAME ' )     - '
            / @21 '  NOEMPTY                   - '
            / @21 '  LIMIT( ' LIMIT ' )        - '
            / @21 ' ' OPTIONS '               - '
            / @21 ')                            '
            ;
   %MEND;

    %MACRO _JPAGE(DDNAME=JCL);
    %*-------------------------------------------------------*
     * MACRO: _JUCAT                                         *
     * FUNCT: REDEFINE ALL PAGE SPACES                       *
     *-------------------------------------------------------*;
     DATA _NULL_; SET CAT.PAGESPC OUT=PAGESPC;
          FILE &DDNAME   LS=80   NOPRINT   NOTITLE;
          IF _N_=1 THEN
             PUT @2  '/*'            @69 '*/'
               / @2  '/*'  @30 'PAGESPACE DEFINITIONS '  @69 '*/'
               / @2  '/*'            @69 '*/'
                 ;
          PUT @2  'DEFINE PAGESPACE              - '
            / @21 '( NAME ( ' PAGENAME ' )     - '
            / @21 '  FILE ( FILE1 )            - '
            / @22 ALLOC '( ' PRI ' ' SEC ' )   - '
            / @21 '  VOL ( ' VOLSER ' )       - '
            / @21 ')                          - '
            / @18 'CAT ( MASTER.CATALOG ) '
            ;
   %MEND;

    %MACRO _DEVTYPE;
    %*-------------------------------------------------------*
     * MACRO: _VARVSAM                                       *
     * FUNCT: CREATE 'VAR' STATEMENTS FOR PROC PRINTS        *
     *        THROUGHOUT THIS REPORT                         *
     *-------------------------------------------------------*;
         IF DEVICE = 3010200EX THEN DEVT='3380';
         IF DEVICE = 3050200BX THEN DEVT='3350';
         IF DEVICE = 32008003X THEN DEVT='3420';
         IF DEVICE = 78008080X THEN DEVT='3480';
         IF DEVICE = 00022000X THEN DEVT='?22?';
         IF DEVICE = 00012000X THEN DEVT='?12?';
         IF DEVICE = 32108003X THEN DEVT='?TP?';
         JDATE=CYY||CDDD;  JDATE=COMPRESS(JDATE);
         JDATE=DATEJUL(JDATE);
      %MEND;
