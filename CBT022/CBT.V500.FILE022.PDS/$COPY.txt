/*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*/
/* DOC:  THIS IS AN ISPF V2.2 ISREDIT MACRO                          */
/* REQ:  USER MUST BE IN ISPF EDIT MODE.                             */
/*                                                                   */
/* FUNCTION: COPY LIBARY AND MEMBER AFTER LINE POSITION              */
/*                                                                   */
/*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*/
PROC 0 X() RC(0)
CONTROL MAIN
ISREDIT MACRO NOPROCESS (INDSN INMEM)
ISPEXEC CONTROL ERRORS RETURN
ISREDIT (DSN) =  DATASET
ISREDIT (MEM) =  MEMBER
SET &PNT=0
SET &ID=&SYSUID

IF &INDSN=CARDLIB  THEN SET &INDSN=&STR(SYS1.CARDLIB)
IF &INDSN=JOBLIB   THEN SET &INDSN=&STR(SYS1.JOBLIB)
IF &INDSN=PROCLIB  THEN SET &INDSN=&STR(SYS1.PROCLIB)
IF &INDSN=PARMLIB  THEN SET &INDSN=&STR(SYS1.PARMLIB)
IF &INDSN=PROC     THEN SET &INDSN=&STR(SYS1.PROC)
IF &SYSDSN('&INDSN')¬=OK THEN DO
   IF &SYSDSN('&SYSUID..&INDSN')¬=OK THEN DO
      WRITE ... CAN NOT FIND '&INDSN'   OR '&SYSUID..&INDSN'
      EXIT
     END
   SET &INDSN=&STR(&SYSUID..&INDSN)
  END
LISTDSI '&INDSN'
IF &SYSDSORG=PS THEN GOTO DSORGPS

/* FOR PDS'S */
IF &SYSDSN('&INDSN.(&INMEM)')¬=OK THEN DO
   WRITE DSN = '&INDSN.(&INMEM)' NOT FOUND ...
   WRITE ... WILL TRY &MEM
   IF &SYSDSN('&INDSN.(&MEM)')¬=OK THEN DO
      WRITE DSN = '&INDSN.(&MEM)' NOT FOUND ...
      EXIT
     END
   SET &INMEM=&MEM
  END
/* COPY REQUESTED MEMBER TO LIBRARY */
SET &PW=
IF &INDSN=&STR(SYS1.PROC) THEN SET &PW=&STR(PASSWORD(PASSWORD))

ISPEXEC LMINIT   DATAID(INDD) DATASET('&INDSN') ENQ(SHR) &PW
SET &RC=&LASTCC
IF &RC¬=0 THEN DO
   WRITE UNABLE TO ALLOCATE '&INDSN'
   EXIT
  END
ISPEXEC LMINIT   DATAID(OUTDD) DATASET('&DSN.') ENQ(SHRW) &PW
SET &RC=&LASTCC
IF &RC¬=0 THEN DO
   WRITE UNABLE TO ALLOCATE '&OUTDSN'
   ISPEXEC LMFREE DATAID(&INDD)
   EXIT
  END
ISPEXEC LMCOPY FROMID(&INDD) FROMMEM(&INMEM) +
               TODATAID(&OUTDD) TOMEM($$##$$##)
SET &RC=&LASTCC
IF &RC¬=0 THEN DO
   WRITE COPY UNSUCCESSFUL ....
   GOTO FREEM
  END
GOTO DELIT

/* FOR DSORG=PS */
DSORGPS: +
ISPEXEC LMINIT   DATAID(INDD) DATASET('&INDSN') ENQ(SHR)
SET &RC=&LASTCC
IF &RC¬=0 THEN DO
   WRITE UNABLE TO ALLOCATE '&INDSN'
   EXIT
  END
ISPEXEC LMINIT   DATAID(OUTDD) DATASET('&DSN.') ENQ(SHRW) &PW
SET &RC=&LASTCC
IF &RC¬=0 THEN DO
   WRITE UNABLE TO ALLOCATE '&OUTDSN'
   ISPEXEC LMFREE DATAID(&INDD)
   EXIT
  END
ISPEXEC LMCOPY FROMID(&INDD) +
               TODATAID(&OUTDD) TOMEM($$##$$##)
SET &RC=&LASTCC
IF &RC¬=0 THEN DO
   WRITE COPY UNSUCCESSFUL ....
   GOTO FREEM
  END

DELIT: +
ISREDIT PROCESS DEST

ISREDIT COPY $$##$$## AFTER .ZDEST

ISPEXEC LMOPEN   DATAID(&OUTDD) OPTION(OUTPUT)
ISPEXEC LMMDEL   DATAID(&OUTDD) MEMBER($$##$$##)
ISPEXEC LMCLOSE  DATAID(&OUTDD)

FREEM: +
ISPEXEC LMFREE   DATAID(&INDD)
ISPEXEC LMFREE   DATAID(&OUTDD)

SET &ZEDSMSG = &STR(&INMEM WAS COPIED)
SET &ZEDLMSG = &STR(&INDSN.(&INMEM) WAS COPIED AFTER SPECIFIED LINE)

ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(&RC)
