//SASCATS JOB
//*-------------------------------------------------------------------*
//*   LIB: SAS.MACLIB(SASCATS )                                       *
//*   DOC: PRINT ALL CATALOG INFORMATION                              *
//*                                                                   *
//*-------------------------------------------------------------------*
//*
//LISCAT   EXEC PGM=IDCAMS,REGION=3M
//SYSPRINT  DD  DSN=&&Q,DISP=(,PASS),UNIT=SYSDA,
//          SPACE=(CYL,(10,1),RLSE),
//          DCB=(LRECL=125,BLKSIZE=629,RECFM=VBA,DSORG=PS)
//SYSIN     DD  *
  LISTCAT  ALL CAT(CATALOG.MVSICFM.VMASTER)
  LISTCAT  ALL CAT(CATALOG.MVSICFU.VUSER01)
  LISTCAT  ALL CAT(CATALOG.MVSICFU.VUSER02)
//CATSTRIP EXEC PGM=CATSTRIP
//STEPLIB   DD DSN=TEST.LINKLIB,DISP=SHR
//LIST      DD DSN=&&Q,DISP=(OLD,DELETE)
//SYSOUT    DD DSN=&&X,DISP=(,PASS),UNIT=SYSDA,SPACE=(CYL,(10,1))
//SYSUDUMP  DD SYSOUT=(,)
//*
//TSOBATCH PROC SOUT='SYSOUT=(,)',RGN=2M
//IEFPROC  EXEC PGM=IKJEFT01,   ** TSO LOGON PGM
//         DYNAMNBR=30,         ** NUMBER OF DYNAMIC ALLOC/SESION
//         REGION=&RGN          ** REGION
//SYSUDUMP DD SYSOUT=A,HOLD=YES
//SYSHELP  DD DSN=SYS1.HELP,DISP=SHR
//SYSUADS  DD DSN=SYS1.UADS,DISP=SHR
//SYSLBC   DD DSN=SYS1.BRODCAST,DISP=SHR
//SYSPRINT DD &SOUT
//SYSTSPRT DD &SOUT
//SYSTERM  DD &SOUT
//SYSTSPRT DD &SOUT
//SYSIN    DD DUMMY
//SYSTSIN  DD DDNAME=INPUT
// PEND
//TSOBATCH EXEC TSOBATCH
//VTOCOUT   DD  DSN=&&VTOC,DISP=(,PASS),UNIT=SYSDA,
//          SPACE=(CYL,(2,1))
//INPUT     DD  *
VTOC ALL TRK NOHEADING +
PRINT(NEW (DSNAME VOLUME DSO EX BLKSZ LRECL REFDT ALLOC UNUSED CDATE))
//*
//SCRATCH  EXEC PGM=IEHPROGM
//SYSPRINT DD  SYSOUT=Z
//DD1      DD  UNIT=SYSDA,VOL=SER=PACK01,DISP=OLD
//SYSIN    DD  *
  SCRATCH DSNAME=SAS.WEEKLY.STATS,VOL=SYSDA=PACK01
  UNCATLG DSNAME=SAS.WEEKLY.STATS
//*
//SASCATS  EXEC SAS
//SYSPRINT  DD  DSN=&&X,DISP=(OLD,DELETE)
//OUTPUT    DD  DSN=&&OUT,DISP=(,DELETE),
//          DCB=(LRECL=121,BLKSIZE=23474,RECFM=FB),
//          UNIT=SYSDA,SPACE=(CYL,(2,1))
//SYSOUT   DD DUMMY
//VTOCOUT   DD   DSN=&&VTOC,DISP=(OLD,DELETE)
//MACROS    DD   DSN=SAS.MACLIB,DISP=SHR
//CAT       DD  DSN=SAS.WEEKLY.STATS,
//          DISP=(,CATLG),UNIT=SYSDA,SPACE=(CYL,(7,1),RLSE),
//          DCB=(RECFM=U,BLKSIZE=18148,LRECL=9072,DSORG=DA),
//          VOL=SER=PACK01
//SYSIN     DD  *
  OPTIONS NOMACROGEN NOSOURCE2 DATE SOURCE ERRORS=0 ;

  *--------------------------------------------------------------*
  *      THIS ROUTINE STRIPS THE LISTCAT INTO A MORE READABLE    *
  *                   FORMAT FOR PROCESSING                      *
  *--------------------------------------------------------------*;

   DATA _NULL_;
      INFILE SYSPRINT LENGTH=LEN ;
      INPUT DATA $VARYING121. LEN  ;
      IF SUBSTR(DATA,1,4)  = '1IDC'   THEN DELETE ;
      IF SUBSTR(DATA,1,15) = '0              ' THEN DELETE ;
      IF SUBSTR(DATA,1,1)  = '0' THEN   DATA=SUBSTR(DATA,2,LEN);
      FILE OUTPUT LS=121 NOPRINT NOTITLE;
           PUT @1 DATA;

  * INCLUDE MACROS - NECESSARY FOR MACRO %_DEVTYPE ;

    %INCLUDE MACROS(SASCATSM) ;

PAGE;
  *--------------------------------------------------------------*
  *                                                              *
  *       ALL FILES USED IN PROCESSING THE OUTPUT OF THE         *
  *                       LISTCAT ALL                            *
  *                                                              *
  *--------------------------------------------------------------*;

 DATA CAT.REPORT  (KEEP=DATE)
      CAT.ALIAS   (KEEP=ALIAS ASSOC)
      CAT.NVSAMD  (KEEP=NONVSAM VOLSER DEVT JDATE OWNERID)
      CAT.NVSAMC  (KEEP=NONVSAM VOLSER DEVT JDATE OWNERID)
      CAT.NVSAMT  (KEEP=NONVSAM VOLSER DEVT JDATE OWNERID)
      CAT.VSAM    (KEEP=CLUSTER JDATE RACF KEYLEN AVGLRECL
                   BUFSPACE CISZ MAXLRECL RECTOT CI_SPLTS CA_SPLTS
                   CI_FREE CA_FREE ALLOC PRI SEC VOLSER DEVT
                   REC_CI MAX_RECS DATE  EXCPS
                   RECDEL EXTENTS RECINSRT RECUPDT RECRET FREEBYTE)
      CAT.USERCAT  (KEEP=UCATNAME VOLSER DEVT)
      CAT.PAGESPC  (KEEP=PAGENAME JDATE RACF DEVT ALLOC PRI SEC VOLSER)
      CAT.CATALOG  (KEEP=CATNAME JDATE RACF PSWD)
      CAT.GDGS     (KEEP=GDGNAME LIMIT OPTIONS JDATE)
      CAT.GDGNVSAM (KEEP=NONVSAM VOLSER DEVT JDATE OWNERID)
      ;

      INFILE OUTPUT
      ;
 PAGE;
  *--------------------------------------------------------------*
  *                                                              *
  *       SAS  LABEL STATEMENTS FOR ALL FILES                    *
  *                                                              *
  *--------------------------------------------------------------*;
         LABEL
               ALIAS='ALIAS'
               ALLOC='ALLOCATION*TYPE'
               ASSOC='ASSOCIATIONS'
               AVGLRECL='AVERAGE*RECORD*LENGTH'
               BUFSPACE='BUFFER*SPACE'
               CA_SPLTS='CONTROL*AREA*SPLITS'
               CA_FREE='PERCENT*CA*FREE'
               CI_SPLTS='CONTROL*INTERVAL*SPLITS'
               CI_FREE='PERCENT*CI*FREE'
               CISZ='CONTROL*INTERVAL*SIZE'
               CLUSTER='CLUSTER*NAME'
               DATE='CURRENT*DATE'
               DEVT='DEVICE*TYPE'
               EXCPS='EXCPS'
               EXTENTS='EXTENTS'
               FREEBYTE='FREE*BYTES'
               GDGNAME='GDG*DATA*SET'
               INCAT='CATALOG*ENTRY'
               JDATE='CREATE*DATE'
               KEYLEN='KEY*LENGTH'
               LIMIT='NUMBER*OF*LEVELS'
               MAXLRECL='MAXIMUM*RECORD*LENGTH'
               NONVSAM='NON*VSAM*DSNAME'
               OPTIONS='GENERATION*OPTIONS'
               OWNERID='OWNER*ID'
               PAGENAME='PAGE*SPACE*NAME'
               PRI='PRIMARY*ALLOCATION'
               RACF='RACF*PROTECTION'
               RECTOT='TOTAL*RECORDS'
               RECDEL='RECS*DELETED'
               RECINSRT='RECS*INSERTED'
               RECUPDT='RECS*UPDATED'
               RECRET='RECS*RETREIVED'
               SEC='SECONDARY*ALLOCATION'
               UCATNAME='USER*CATALOG*NAME'
               VOLSER='VOLUME*SERIAL'
               ;


    FORMAT  DEVT $4.  JDATE DATE DATE7. ;

 PAGE;
  *--------------------------------------------------------------*
  *                                                              *
  *   PROCESSING OF THE FIRST FIELD TO DETERMINE TYPE OF ENTRY   *
  *                                                              *
  *--------------------------------------------------------------*;

  INPUT @1 TYPE $11.
        @;

  IF _N_ = 1 THEN DO;
     DATE=TODAY(); FORMAT DATE DATE7.; OUTPUT CAT.REPORT;
    END;

    *--------- ALIAS --------;
      IF TYPE='ALIAS' THEN DO;
         INPUT  #1  @17  ALIAS    $8.
                #5  @10  ASSOC    $44.
                ;
         IF LENGTH(ALIAS)=1 THEN DELETE;
         OUTPUT CAT.ALIAS;
        END;

    *-------- NONVSAM --------;
      IF SUBSTR(TYPE,1,7)='NONVSAM' THEN DO;
         INPUT #1  @10 GNONVSAM  $44.    /* GDG NAME ???     */
                   @17 NONVSAM  $44.     /* DATA SET NAME    */
               #2  @1  HIST     $7.      /* CHECK FOR HISTORY */
                   @;
               IF HIST='HISTORY' THEN DO;
         INPUT    #3  @17 OWNERID   $8.     /* OWNER            */
                      @48 CYY       $2.     /* CREATE YEAR      */
                      @51 CDDD      $3.     /*CREATE DATE       */
                  #6  @19 VOLSER    $6.     /* VOLUME SERIAL    */
                      @45 DEVICE  HEX8.     /* DEVICE TYPE      */
                      ;
                  END;
               IF HIST='IN-CAT'  THEN DO;
         INPUT        @12 INCAT     $44.    /* CATALOG NAME     */
                  #3  @17 OWNERID   $8.     /* OWNER            */
                      @48 CYY       $2.     /* CREATE YEAR      */
                      @51 CDDD      $3.     /*CREATE DATE       */
                  #7  @19 VOLSER    $6.     /* VOLUME SERIAL    */
                      @45 DEVICE  HEX8.     /* DEVICE TYPE      */
                      ;
                  END;
       * INDIRECT ADDRESSING FOR SYSRES VOLUME ;
         IF VOLSER='******' THEN VOLSER='SYSRES';
         IF VOLSER=' '      THEN DELETE;
         %_DEVTYPE
         RNVSAM=REVERSE(NONVSAM);           /* REVERSE THE DSN   */
         RGDG='00V';                        /* OPPOSITE OF 'V00' */
         X=INDEX(RNVSAM,RGDG);              /* OPPOSITE OF 'V00' */
         IF X¬=0 THEN DO;                   /* FOUND A GDG FILE  */
            NONVSAM=GNONVSAM;
            OUTPUT CAT.GDGNVSAM;
           END;
       * DIVIDE OUT TAPE AND DISK DATASET NAMES;
         IF DEVT='3420' OR DEVT='3480' THEN DO;
             IF DEVT='3420' THEN OUTPUT CAT.NVSAMT;
                            ELSE OUTPUT CAT.NVSAMC;
            END;
          ELSE DO;
             OUTPUT CAT.NVSAMD;
            END;
      END;


    *------- USERCATALOG -------;
      IF TYPE='USERCATALOG' THEN DO;
         INPUT #1  @17 UCATNAME $44.     /* DATA SET NAME       */
               #5  @19 VOLSER    $6.     /* VOLSER              */
                   @45 DEVICE  HEX8.     /* DEVICE TYPE         */
               #6  @22 NULL      $4.     /* CHECK ASSOCIATIONS  */
               ;
         IF LENGTH(UCATNAME)=1 THEN DELETE;
         %_DEVTYPE
         OUTPUT CAT.USERCAT;
        END;

    *------- GDG BASE --------;
      IF SUBSTR(TYPE,1,8)='GDG BASE' THEN DO;
         INPUT #1  @17 GDGNAME  $44.     /* DATA SET NAME       */
               #3  @48 CYY      $2.      /* CREATE YEAR         */
                   @51 CDDD     $3.      /* CREATE DAY          */
               #6  @24 LIMIT     3.      /* NUMBER OF GENRATIONS*/
                   @30 OPTIONS  $40.     /* GDG OPTIONS         */
               ;
         %_DEVTYPE
         OUTPUT CAT.GDGS;
        END;

    *------- PAGE SPACE ------;
      IF SUBSTR(TYPE,1,9)='PAGESPACE'  THEN DO;
         INPUT #1  @17 PAGENAME $44.     /* PAGE SPACE NAME    */
               #3  @48 CYY       $2.     /* CREATE YEAR         */
                   @51 CDDD      $3.     /* CREATE DATE         */
               #5  @53 RACF      $1.     /* RACF: Y/N           */
              #17  @49 MAXLRECL   5.     /* MAX RECORD LENGTH   */
              #26  @1  QQQ       $10.    /* LOOKING FOR ALLOC   */
               @;
               IF QQQ='ALLOCATION' THEN DO;
                   INPUT #27      @15 ALLOC    $10.
                         #28      @18 PRI        7.
                         #29      @20 SEC        5.
                         #31      @19 VOLSER     $6.
                         #32      @16 DEVICE   HEX8.
                         ;
                  END;
             %_DEVTYPE
             OUTPUT CAT.PAGESPC;
           END;
PAGE;
    *------- CLUSTER ---------;

      IF TYPE='CLUSTER'  THEN DO;
         DATE=TODAY();                   /* SAVE CURRENT DATE   */
         INPUT #1  @17 CLUSTER  $44.     /* DATA SET NAME       */
               @;
         IF SUBSTR(CLUSTER,1,4)='0000' THEN DO; /* ICF-CATALOG */
            INPUT #7  @14 CATNAME  $44.  /* CATALOG NAME       */
                  #9  @48 CYY      $2.   /* CREATE YEAR        */
                      @51 CDDD     $3.   /* CREATE DAY         */
                  #11 @22 PSWD     $4.   /* PASSWORD           */
                      @53 RACF     $1.   /* RACF (Y/N)         */
                      ;
            %_DEVTYPE
            OUTPUT CAT.CATALOG;
            RETURN;
           END;
         INPUT #3  @48 CYY       $2.     /* CREATE YEAR         */
                   @51 CDDD      $3.     /* CREATE DATE         */
               #5  @22 PSWD      $4.     /* PASSWORD OR NULL    */
                   @53 RACF      $1.     /* RACF: Y/N           */
                   @;
         IF RACF=' ' THEN RACF='N' ;
         IF PSWD ='    '  THEN DO;         /* A PASSWORD EXISTS   */
             INPUT #6   @17 MSTRPW     $8. /* MASTER PASSWORD */
                        @46 UPDTPW     $8. /* UPDATE PASSWORD */
                   #11  @22 DATAPSWD   $4. /* DATA PASSWORD ? */
                   @;
                   IF DATAPSWD='    ' THEN DO;
                       INPUT #18  @22 KEYLEN     3.
                                 @49 AVGLRECL   5.
                                 @77 BUFSPACE   6.
                                 @107 CISZ      5.
                             #19 @49 MAXLRECL   5.
                                 @98 CI_CA      4.
                             #17 @1  SHROPT    $7.  @; /* BACK TRACK */
                           END;
                       ELSE DO; GOTO LINK1; END;
                    END;
                   ELSE DO;
      LINK1: INPUT #15  @22 KEYLEN     3.
                        @49 AVGLRECL   5.
                        @77 BUFSPACE   6.
                        @107 CISZ      5.
                    #16 @49 MAXLRECL   5.
                        @98 CI_CA      4.
                    #17 @1  SHROPT    $7.     @;
                    END;
               IF SHROPT='RECORDS' THEN DO;
                 INPUT
                   #20      @18 REC_CI    7.    /* ALLOC RECORDS */
                            @45 MAX_RECS  9.
                   #21      @11 RECTOT    14.
                            @50 CI_SPLTS   4.
                   #22      @51 CA_SPLTS   4.
                   #23      @52 CI_FREE    2.
                   #24      @52 CA_FREE    2.
                   #27      @15 ALLOC    $10.
                   #28      @18 PRI        7.
                   #29      @20 SEC        5.
                   #31      @19 VOLSER     $6.
                   #32      @16 DEVICE   HEX8.
                   ;END;
               ELSE DO;
                   IF SHROPT='ATTRIBU' THEN DO;
                    INPUT
                       #23      @11 RECTOT    14.
                                @50 CI_SPLTS   4.
                                @70 EXCPS     13.
                       #24      @14 RECDEL    11.
                                @51 CA_SPLTS   4.
                                @79 EXTENTS    4.
                       #25      @14 RECINSRT  11.
                                @52 CI_FREE    2.
                       #26      @14 RECUPDT   11.
                                @52 CA_FREE    2.
                       #27      @14 RECRET    11.
                                @44 FREEBYTE  10.
                       #29      @15 ALLOC    $10.
                       #30      @18 PRI        7.
                       #31      @20 SEC        5.
                       #33      @19 VOLSER     $6.
                       #34      @16 DEVICE   HEX8.
                       ;END;
                   ELSE DO;
                    INPUT
                       #20      @11 RECTOT    14.
                                @50 CI_SPLTS   4.
                                @70 EXCPS     13.
                       #21      @14 RECDEL    11.
                                @51 CA_SPLTS   4.
                                @79 EXTENTS    4.
                       #22      @14 RECINSRT  11.
                                @52 CI_FREE    2.
                       #23      @14 RECUPDT   11.
                                @52 CA_FREE    2.
                       #24      @14 RECRET    11.
                                @44 FREEBYTE  10.
                       #26      @15 ALLOC    $10.
                       #27      @18 PRI        7.
                       #28      @20 SEC        5.
                       #30      @19 VOLSER     $6.
                       #31      @16 DEVICE   HEX8.
                       ;END;
                 END;
         %_DEVTYPE
         OUTPUT CAT.VSAM;
        END;
PAGE;
    *---------------------------------------------*
    *         NON-VSAM  FROM VTOC PROGRAM         *
    *                                             *
    *  NOTE: VTOC COMMAND WAS ...                 *
    *                                             *
    *        VTOC ALL TRK              +          *
    *             NOHEADING            +          *
    *             PRINT(NEW (DSNAME VOLUME DSO +  *
    *                        EX BLKSZ LRECL    +  *
    *                        REFDT ALLOC UNUSED + *
    *                        CDATE))              *
    *---------------------------------------------*;
     DATA CAT.VTOCS
                    (KEEP=NONVSAM VOLSER DSORG EXTENTS
                          REFDT ALLOC UNUSED DSNAME VOL
                          NODE1 NODE2 NODE3 )
                     ;
          LABEL ALLOC='ALLOC*IN*TRACKS'
                CDATE='CREATE*DATE'
                DSORG='DATA*SET*ORG'
                EXTENTS='EXTENTS'
                NODE1='DATA*SET*NODE1'
                NODE2='DATA*SET*NODE2'
                NODE3='DATA*SET*NODE3'
                NONVSAM='DATE*SET*NAME'
                REFDTE='LAST*REF.*DATE'
                UNUSED='UNUSED*IN*TRACKS'
                VOLSER='VOLUME*SERIAL'
                ;
          LENGTH  NODE1  NODE2  NODE3  $9. ;

          INFILE VTOCOUT JFCB=JFCB VOLUME=IVOL;
          INPUT @1  CC       $1.
                @2  NONVSAM  $44.
                @47 VOLSER   $6.
                @54 DSORG    $2.
                @58 EXTENTS   2.
                @61 BLKSIZE   5.
                @67 LRECL     5.
                @72 R_DATE   $5.
                @79 ALLOC     5.
                @86 UNUSED    5.
                @92 C_DATE   $5.
                ;

          IF _N_=1 THEN DO;
             J_D=TODAY();  JD=DATEJUL(J_D);
             TEMPDS=SYS||JD; TEMPDS=COMPRESS(TEMPDS); DROP J_D JD;
             RETAIN TEMPDS;
            END;

        * KILL ANY RECORD WITH CARRIDGE CONTROL ;
          IF CC='0' THEN DELETE;

        * NOTE: ALL SPACE ALLOCAION IS IN TRACKS ...
                AND BLKSIZE AND LRECL VARIABLES NOT KEPT   ;

        * ELIMINATE ALL TEMP DATASETS;
          IF SUBSTR(NONVSAM,1,8)=TEMPDS  THEN DELETE;


          IF R_DATE ¬= ' 0000' AND R_DATE ¬= '     '
             THEN REFDT=DATEJUL(R_DATE);
          IF C_DATE ¬= ' 0000' AND C_DATE ¬= '     '
             THEN CDATE=DATEJUL(C_DATE);
          FORMAT REFDT CDATE DATE7.;


    *--------------------------------------*
    * CREATE UNIQUE VARIABLES FROM DSNAME  *
    *--------------------------------------* ;

      LDSN=LENGTH(NONVSAM);
    * CREATE FIRST NODE OF DATASET NAME ;
      X=INDEX(NONVSAM,'.');
      X=X-1;                         * ONE BACK FROM '.' ;
      NODE1=SUBSTR(NONVSAM,1,X);
      X=X+2;                         * ONE UPTO '.' AND ONE PAST '.';
      SUBDSN=SUBSTR(NONVSAM,X,LDSN);
      LDSN=LDSN-X;
    * CREATE SECOND NODE OF DATASET NAME ;
      X=INDEX(SUBDSN,'.');
      X=X-1;
      NODE2=SUBSTR(SUBDSN,1,X);
      X=X+2;
      SUBDSN=SUBSTR(SUBDSN,X,LDSN);
      LDSN=LDSN-X;
    * CREATE THIRD NODE OF DATASET NAME ;
      X=INDEX(SUBDSN,'.');
      X=X-1;
      IF X>0 THEN  NODE3=SUBSTR(SUBDSN,1,X);
             ELSE  NODE3=SUBSTR(SUBDSN,1,LDSN);
      DROP X LDSN SUBDSN;


    * ELIMINATE CATALOGS, VVDSS'S, INDEXED VTOCS ;
      IF NODE2='VVDS' OR NODE2='VTOCIX' OR NODE1='CATALOG'
         THEN DELETE;


* LIST ALL POSSIBLE REPORTS ;

  %_REPORT

  ENDSAS;

/*
//
