//  EXEC SMPE
//SMPLIST  DD DSN=&&SMPE,UNIT=SYSDA,DISP=(,PASS),
// SPACE=(CYL,(10,1)),DCB=BLKSIZE=12100
//SMPCNTL  DD  *
       SET BDY(TARGET_ZONE).
          LIST DDDEF .
//*
//SAS   EXEC SAS
//SMPELIST DD  DSN=&&SMPE,DISP=(OLD,DELETE)
//SMPOUT   DD  UNIT=SYSDA,SPACE=(CYL,(10,1)),DSN=&&SMPOUT,
//         DISP=(,PASS),DCB=(LRECL=120,BLKSIZE=12000,RECFM=FB)
//UCLIN    DD  SYSOUT=*
//SYSIN    DD  *
  OPTIONS NOMACROGEN MPRINT DATE  ERRORS=0 ;

  /*-------------------------------------------------------------*
   * FUNCTION: THIS SAS PROGRAM PROCESSES THE OUTPUT OF A        *
   *           'LIST DDDEF' COMMAND FROM SMP/E, REPORTING        *
   *           ON THE RESULTS AND OPTIONALLY CREATING            *
   *           STATEMENTS TO REDEFINE ALL DDDEF'S VIA THE        *
   *           UCLIN DD CARD.                                    *
   *                                                             *
   * NOTE:     THIS PROGRAM ASSUMES THE FOLLOWING FORMAT         *
   *           FOR ALL DDDEF ENTRIES, EXCEPT THE CONCATENATIONS  *
   *       1   DDNAME  DSN                                       *
   *       2           VOL                                       *
   *       3           UNIT                                      *
   *       4           DISP                                      *
   *                                                             *
   *                                                             *
   *-------------------------------------------------------------*/

   DATA _NULL_;
      INFILE SMPELIST;
      INPUT @1 CC   $1.
            @2 DATA $120.;
      IF SUBSTR(CC,1,1)='1'         THEN DELETE;
      IF SUBSTR(DATA,1,6)='  NAME'  THEN DELETE;
      IF SUBSTR(DATA,1,5)=' NAME'   THEN DELETE;
      IF SUBSTR(DATA,1,4)='NAME'   THEN DELETE;
      IF SUBSTR(DATA,1,6)='MVSTGT'  THEN DELETE;
      IF SUBSTR(DATA,1,20)='                     ' THEN DELETE;
      DROP CC;
      FILE SMPOUT LS=120 NOPRINT NOTITLE;
           PUT @1 DATA;

   DATA DDDEFS (KEEP=DDNAME DSN VOLUME UNIT DISP)
        CONCATS (KEEP=DDNAME CONCAT)
        ;
        INFILE SMPOUT;
        INPUT @1   DDNAME   $8.
              @11  DATASET  $7.
              @;

        IF DATASET='CONCAT ' THEN DO ;
           INPUT #1 @29 CONCAT $102.   ;
           OUTPUT CONCATS;
           RETURN;
          END;

        IF DATASET='DATASET' THEN DO ;
           INPUT #1   @29  DSN      $44.
                 #2   @1   V_OL     $6.
                 @;
           IF V_OL='VOLUME' THEN DO;
              INPUT #2   @19  VOLUME   $6.
                    #3   @1   U_NIT    $4.
                 @;
             END;
           IF V_OL='SHR' | V_OL='MOD' | V_OL='OLD' THEN DO;
              INPUT #2   @19  DISP     $4. ;
              GOTO PUTM;
             END;
              IF U_NIT='UNIT' THEN DO;
                 INPUT #3   @19  UNIT     $4.
                       #4   @1   DISP     $3.
                       ;
                END;
        PUTM:    OUTPUT DDDEFS;
          END;


   PROC PRINT DATA=CONCATS;

   PROC SORT DATA=DDDEFS; BY DDNAME;

   PROC PRINT DATA=DDDEFS;

   DATA _NULL_;
        SET DDDEFS END=EOF;
        FILE UCLIN LS=80 NOTITLES NOPRINT LRECL=80 ;
        IF _N_=1 THEN DO;
           PUT @1 ' SET BDY(MVSTGT).' / @6 'UCLIN.' /
               @6 'REP TZONE(MVSTGT) OPTIONS(OPTMVST).'
           ;
          END;

      *------------------------------------;
      * CHANGE VOLSER REFERENCE FOR SYSRES ;
      *------------------------------------;

    /*  IF VOLUME='MVSRES' THEN VOLUME='MVSDEV' ;   */


        DA='DA('||DSN||')'; DA=COMPRESS(DA);
        DD='DDDEF('||DDNAME||')'; DD=COMPRESS(DD);
        VS='VOLUME('||VOLUME||')'; VS=COMPRESS(VS);
        PUT @6  'REP ' DD ' ' DA
         /  @10 VS ' UNIT(3380) '  DISP '.'
        ;
        IF EOF THEN PUT @3 'ENDUCL.' ;



/*
//
