PROC 0 A(0) X( ) P( ) OFLAG( ) +
     DS('TS.SYSLOG.DATA') +  /* DISK BASE GDG */
     VOL(MVSDLB) +
     GDG(0) NHASP CMD( ) CMDA( ) +
     SDATE( ) STIME( ) SNUM( ) SNAME( ) SMSG( ) +
     OTHER( ) L K DEST(LOCAL)
CONTROL NOMSG NOLIST NOFLUSH
SET &ID=&SYSUID
FREE DD(SORTIN,SORTOUT,SORTWK01,SYSOUT)
FREE ATTRLIST(SYSLOG)
IF &SYSDSN('&ID..SORTIN')=OK THEN DELETE '&ID..SORTIN'
IF &SYSDSN('&ID..SORTOUT')=OK THEN DELETE '&ID..SORTOUT'
CONTROL MAIN MSG

ATTR SYSLOG LRECL(133) BLKSIZE(23408) RECFM(F B) DSORG(PS)

IF &SYSISPF=ACTIVE THEN +
ISPEXEC VGET (GDG,NHASP,CMD,CMDA,SDATE,STIME,SNUM,SNAME,SMSG,OTHER,+
L,K,DEST,P,JOB) SHARED
IF &DEST=  THEN SET &DEST=LOCAL
/*   CONVERSION  SECTION   */
IF &L=Y OR &L=L THEN DO
   FREE DD(SYSIN,W)
   ATTR W  LRECL(125) BLKSIZE(629) RECFM(V B A) DSORG(PS)
   ALLOC DD(SYSPRINT) SP(1,1) CYL NEW USING(W) DELETE
   OPENFILE SYSIN OUTPUT
   SET SYSIN=&STR( LISTCAT LVL('&DS') ALL)
   PUTFILE SYSIN
   CLOSFILE SYSIN
   CALL 'SYS1.LINKLIB(IDCAMS)'
   ISPEXEC  LMINIT  DATAID(ID1) DDNAME(SYSPRINT)
   ISPEXEC  BROWSE  DATAID(&ID1)
   IF &P=Y  THEN  DO
      ISPEXEC LMPRINT DATAID(&ID1) FORMAT(NO)
      SET &ZEDSMSG = &STR(LISTED)
      SET &ZEDLMSG = &STR(PRINT SENT TO ISPF/LIST FILE)
      ISPEXEC SETMSG MSG(ISRZ001)
     END
   ISPEXEC  LMFREE  DATAID(&ID1)
   FREE DD(SYSPRINT,SYSIN)
   ALLOC DD(SYSPRINT) DS(*)
   ALLOC DD(SYSIN) DS(*)
   ALLOC DD(VTOCOUT)  NEW DELETE
   /* USE OF VTOC COMMAND PROCESSOR FROM CBT TAPE */
   VTOC &VOL LEVEL(&DS) PRINT(NEW +
        (DSNAME VOL LRECL BLKSZ RFM DSO EX ALLOC PCT CDATE REFDT))
   ISPEXEC  LMINIT  DATAID(ID1) DDNAME(VTOCOUT)
   ISPEXEC  BROWSE  DATAID(&ID1)
   IF &P=Y  THEN  DO
      ISPEXEC LMPRINT DATAID(&ID1) FORMAT(NO)
      SET &ZEDSMSG = &STR(LISTED)
      SET &ZEDLMSG = &STR(PRINT SENT TO ISPF/LIST FILE)
      ISPEXEC SETMSG MSG(ISRZ001)
     END
   ISPEXEC  LMFREE  DATAID(&ID1)
   ALLOC DD(SYSPRINT) DS(*)
   FREE DD(VTOCOUT)
   EXIT
  END


FREE DD(DSN,W)
ATTR W)   DSORG(PS) LRECL(80) BLKSIZE(3120) RECFM(F B)
ALLOC DD(DSN) NEW DELETE USING(W)
CALL 'LINKLIB(SYSLOC)' '&DS(-&GDG)'
IF &LASTCC¬=0 THEN DO
   WRITE DATASET '&DS(-&GDG)' NOT FOUND
   WRITE
   WRITE CLIST WILL ALLOCATE THE (0) GENERATION
   CALL 'LINKLIB(SYSLOC)' '&DS(0)'
   IF &LASTCC¬=0 THEN GOTO BADEND
  END
OPENFILE DSN INPUT
GETFILE DSN
CLOSFILE DSN
SET &L=&SUBSTR(1:2,&DSN)
SET &L=&L+2
ALLOC DD(SYSLOG) DS('&SUBSTR(3:&L,&DSN)') SHR
IF &LASTCC¬=0 THEN GOTO BADEND

ALLOC DD(SORTIN) DS('&ID..SORTIN') NEW DELETE SPACE(7,1) CYL +
      USING(SYSLOG) UNIT(DISK)

/*=========>  CHECK PARMS  <==========*/
IF &NHASP = Y OR &NHASP=NOHASP THEN SET &X=&STR(NOHASP)
IF &CMD  ¬=   THEN SET &PARM=&STR(S /&CMD/)
IF &CMDA ¬=   THEN SET &PARM=&STR(S ALL /&CMDA/)
IF &SDATE¬=   THEN SET &PARM=&STR(S D/&SDATE/)
IF &STIME¬=   THEN SET &PARM=&STR(S T/&STIME/)
IF &SNUM ¬=   THEN SET &PARM=&STR(S J/&SNUM/)
IF &SNAME¬=   THEN SET &PARM=&STR(S J/&SNAME/)
IF &SMSG ¬=   THEN SET &PARM=&STR(S M/&SMSG/)
IF &JOB  ¬=   THEN DO
   ALLOC DD(SORTOUT) DS('&ID..SORTOUT') NEW DELETE +
         SPACE(4,1) CYL USING(SYSLOG) UNIT(DISK)
   ALLOC DD(SORTWK01)  UNIT(SYSDA) SPACE(1,1) CYL NEW DELETE
   FREE DD(SYSIN)
   OPENFILE SYSIN OUTPUT
   SET SYSIN=&STR( SORT FIELDS=(39,4,CH,A,17,17,CH,A))
   PUTFILE SYSIN
   CLOSFILE SYSIN
   CALL 'LINKLIB(SYSLOG)' 'S JOB/&JOB/'
   ALLOC DD(SYSOUT) DUMMY
   CALL 'SYS1.LPALIB(ICEMAN)'
   FREE DD(SYSOUT,SORTIN,SORTWK01,SYSIN,DSN)
   ISPEXEC  LMINIT  DATAID(ID1) DDNAME(SORTOUT)
   IF &LASTCC>0 THEN DO
      WRITE UNABLE TO BROWSE DATA
      FREE DD(SORTOUT,SYSLOG)
      EXIT
     END
   ISPEXEC  BROWSE  DATAID(&ID1)
   WRITENR TO PRINT TO &DEST PRINTER (Y):&STR( )
   READ &A
   IF &A=Y THEN DO
      /* ISPEXEC LMPRINT DATAID(&ID1) FORMAT(NO) */
      PRINTOFF '&ID..SORTOUT'  DEST(&DEST)
      SET &ZEDSMSG = &STR(PRINTED)
      SET &ZEDLMSG = &STR(PRINT SENT TO &DEST)
      ISPEXEC SETMSG MSG(ISRZ001)
     END
   ISPEXEC  LMFREE  DATAID(&ID1)
   IF &K=Y THEN DO
      IF &SYSDSN('&ID..SYSLOG.FILE')=OK THEN DELETE '&ID..SYSLOG.FILE'
      ALLOC DD(KEEP) DS('&ID..SYSLOG.FILE') NEW SP(1,2) CYL CATALOG +
               USING(SYSLOG) UNIT(DISK)
      COPYSDS SORTIN KEEP FILE
      SET &ZEDSMSG = &STR(FILE KEPT)
      SET &ZEDLMSG = &STR(FILE KEPT ON '&ID..SYSLOG.FILE')
      ISPEXEC SETMSG MSG(ISRZ001)
  END
   FREE DD(SORTOUT,SYSLOG,SYSIN,SYSPRINT)
   ALLOC DD(SYSPRINT) DS(*)
   ALLOC DD(SYSIN) DS(*)
   EXIT
  END

IF &X=  THEN  CALL 'LINKLIB(SYSLOG)' '&PARM'
        ELSE  CALL 'HEDGESM.LINKLIB(SYSLOG)' 'NOHASP'

ISPEXEC  LMINIT  DATAID(ID1) DDNAME(SORTIN)
IF &LASTCC>0 THEN DO
   WRITE UNABLE TO BROWSE DATA
   GOTO FREEDDS
   EXIT
  END
IF &P=Y  THEN  DO
   PRINTOFF '&ID..SORTIN' DEST(&DEST)
   /*ISPEXEC LMPRINT DATAID(&ID1) FORMAT(NO)*/
   SET &ZEDSMSG = &STR(PRINTED)
   SET &ZEDLMSG = &STR(PRINT SENT TO &DEST)
   ISPEXEC SETMSG MSG(ISRZ001)
   GOTO LMFRE
  END
ISPEXEC  BROWSE  DATAID(&ID1)

LMFRE: ISPEXEC  LMFREE  DATAID(&ID1)

IF &K=Y THEN DO
   IF &SYSDSN('&ID..SYSLOG.FILE')=OK THEN DELETE '&ID..SYSLOG.FILE'
      ALLOC DD(KEEP) DS('&ID..SYSLOG.FILE') NEW SP(1,2) CYL CATALOG +
               USING(SYSLOG) UNIT(DISK)
   COPYSDS SORTIN KEEP FILE
   SET &ZEDSMSG = &STR(FILE KEPT)
   SET &ZEDLMSG = &STR(FILE KEPT ON '&ID..SYSLOG.FILE')
   ISPEXEC SETMSG MSG(ISRZ001)
  END

FREEDDS: +
FREE DD(DSN,SORTIN,SYSIN,SYSPRINT)
FREE ATTRLIST(SYSLOG)
ALLOC DD(SYSPRINT) DS(*)
ALLOC DD(SYSIN) DS(*)
BADEND: EXIT
