//SASCSI  JOB
//*
//CSI EXEC SAS
//SMPCSI     DD  DSN=SYS1.MVSSMPE.CSI,DISP=SHR
//ZONERPT  DD       SYSOUT=(,)
//DDEFRPT  DD DUMMY,SYSOUT=(,)
//DSNRPT   DD DUMMY,SYSOUT=(,)
//PGMRPT   DD DUMMY,SYSOUT=(,)
//FMIDRPT  DD       SYSOUT=(,)
//ASMRPT   DD DUMMY,SYSOUT=(,)
//SYSMOD   DD DUMMY,SYSOUT=(,)
//LMODRPT  DD DUMMY,SYSOUT=(,)
//MACRPT   DD DUMMY,SYSOUT=(,)
//MODRPT   DD DUMMY,SYSOUT=(,)
//SRCRPT   DD DUMMY,SYSOUT=(,)
//DLIBRPT  DD DUMMY,SYSOUT=(,)
//SRCLTRPT DD DUMMY,SYSOUT=(,)
//LMODCRPT DD DUMMY,SYSOUT=(,)
//HOLDRPT  DD DUMMY,SYSOUT=(,)
//SYSIN    DD *
* SAS PROGRAM TO READ AND ANALYZE A PTF OUTPUT FROM SMPE ;

OPTIONS MISSING=' ' TIME=MAX LINESIZE=132 ;

DATA CSI(KEEP=IRBRELAX CCACZIDX IRBCATGX IRBOCCRX);

LENGTH TYPE $ 8 SYSMOD $ 8 SYSENT $ 50  SYMTYPE $ 8 CSINAME $ 8 ;
LENGTH SRCLST $ 8 DLIB $ 8 ATTRX $ 8 LMOD $ 8 MOD $ 8 MAC $ 8 SRC $ 8;
LENGTH ZONEI $ 8 ZONE $ 8 DDEF $ 8 DSN $ 8 PGM $ 8 FMID $ 8 ASM $ 8;
LENGTH PARM $ 50 CARD $ 80 HOLD $ 8 LMODCD $ 8;
* ---------- DEFINE INFILE ---------------;
  INFILE SMPCSI VSAM LENGTH=KEYLGTH ;

*------------------------------------------------------------;
* IRBCATG    DESCRIPTION
   01        ZPOOL1
   02        ZONE ASSIGNMENT (ZONEINDEX)
   03
   04        CATG=CAT
   05        CATG=REL
   10        ZONE
   11        OPTIONS
   12        DDDEF
   13        UTILITY
   14        FMIDSET
   20        ASSEM
   21        LMOD
   22        MAC
   23        MOD
   24        SRC
   25        DLIB
   26        SYSMOD
   30        ASM SRC
   31        LMOD SRC
   32        HOLD DATA ;
*------------------------------------------------------------;
* IRBRELA    DESCRIPTION           IRBRELA       DESCRIPTION
* -------    ------------          ---------     --------------;
* X'0101'      MAIN CSI             X'2001'      ASM SECURITY
* X'0201'      ZONE INDEX CODE      X'2010'      ASM BACKUP
* X'0301'      RDE  CODE            X'2100'      LMOD SECURITY
* X'0401'           CODE            X'2101'      LMOD ATTR
* X'0501'           CODE            X'2110'      LMOD BACKUP
* X'1000'      ZONE SECURITY        X'2113'      LOADLIB
* X'1001'      ZONE INDEX           X'2200'      MAC SECURITY
* X'1020'      ZONE RELATED         X'2201'      MAC  FMID
* X'1021'      ZONE SREL            X'2202'      MAC RMID
* X'1022'      ZONE FMID            X'2210'      MAC BACKUP
* X'1027'      ZONE OPTIONS         X'2211'      MAC DLIB
* X'1172'      AMS IDCAMS UCLIN     X'2213'      MAC LIB
* X'1173'      ASSEMBLER            X'2230'      GENASM
* X'1174'      COMP                 X'2240'      ALIAS
* X'1175'      IEBCOPY              X'2250'      UMID
* X'1177'      LINKAGE EDITOR       X'2300'      MOD SECURITY
* X'1178'      RETRY (IEBCOPY)      X'2301'      MOD FMID
* X'1179'      UPDATE (IEBUPDTE)    X'2302'      MOD RMID
* X'117A'      SZAP (IMASPZAP)      X'2310'      MOD BACKUP
* X'117B'      NUCID                X'2311'      MODDLIB
* X'1180'      UCLIN PAGELEN        X'2313'      MOD ATTR
* X'1181'      UCLIN PREMAX         X'2340'      CSECT
* X'1182'      PURGE/NOPURGE        X'2341'      CSECT LMODE
* X'1183'      REQUEST/NOREQUEST    X'2342'      TALIAS
* X'1188'      RETRY DDNAME         X'2343'      DALIAS
* X'1190'      SAVEMTS              X'2350'      UPDATE ID (UMID)
* X'1191'      SAVESTS              X'2401'      SRC FMID
* X'1192'      DSSPACE              X'2402'      SRC RMID
* X'1193'      DSPREFIX             X'2410'      SRC BACKUP
* X'1201'      DSN=                 X'2411'      SRC DLIB
* X'1202'      VOL=SER=             X'2413'      SRC LIB
* X'1203'      UNIT=                X'2450'      SRC UMID
* X'1209'      CONCAT DD            X'2500'      DLIB SECURITY
* X'1210'      DISP=                X'2501'      DLIB BACKUP
* X'1301'      PGM=                 X'2513'      COPY LIB
* X'1302'      PARM=
* X'1303'      DDN= (PRINT)
* X'1304'      RETURN CODE
* X'1401'      SETFMID ;
* X'2600'      SYSMOD SECURITY      X'26A9'      VERSION
* X'2601'      SYSMOD TYPE          X'26B1'      MOD
* X'2602'      SYSMOD ATTRIBUTE     X'26B2'      ZAP
* X'2611'      DELBY                X'26B3'      XZAP
* X'2612'      LSUBBY               X'26B4'      MAC
* X'2630'      BACKUP               X'26B5'      MACU
* X'2631'      FMID                 X'26B6'      SRC
* X'2632'      RECDATE              X'26B7'      SRCU
* X'2633'      INSDATE              X'26C1'      ASM
* X'2635'      RSTDATE              X'26C2'      APPLY ID
* X'2636'      UCLDATE              X'26C3'      ACCEPT ID
* X'2637'      DSN=                 X'26D1'      RMOD
* X'2638'      # OF REL FILE        X'26D2'      RZAP
* X'2639'      SECID                X'26D3'      RXZAP
* X'263A'      FESN                 X'26D4'      RMAC
* X'26A1'      VSREC                X'26D5'      RMACU
* X'26A2'      VFMID                X'26D6'      RSRC
* X'26A3'      VPRE                 X'26D7'      RSRCU
* X'26A4'      VNPRE                X'26F0'      SUPBY
* X'26A5'      VSUP                 X'26F1'      CIFREQ
* X'26A6'      VREQ                 X'26F4'      HOLDERR
* X'26A7'      VIREQ                X'26F5'      HOLDSYS
* X'26A8'      VDEL                 X'26F6'      HOLDUSR  ;
* X'3001'      SOURCE LISTING       X'3210'      ++HOLD CARD;
* X'3101'      LMOD CARDS           X'3220'      ++HOLD COMMENT;
*                                   X'3230'      ++HOLD FULLGEN;
RETAIN PTR 13 I 1 TYPE SYSMOD SYSENT SYMTYPE ' ';
RETAIN SRCLST DLIB LMOD MOD MAC SRC LMODCD HOLD ' ';
RETAIN ZONEI ZONE DDEF DSN PGM FMID ASM ' ';
RETAIN INDEX ' ';
* CSI KEY FORMAT;
  INPUT CCACZID PIB2.
  @3    IRBCATG PIB1.
  @4    IRBNAME1 $8.
  @12   IRBRELA PIB2.
  @12   IRBRELAC $CHAR2.
  @14   IRBOCCR PIB2. @;
* @16   IRBNAM2F $8. ;
 FORMAT  CCACZIDX IRBOCCRX HEX4. ;
 FORMAT IRBCATGX                   HEX2. ;
 IRBCATGX=IRBCATG;
 CCACZIDX=CCACZID;
 IRBOCCRX=IRBOCCR;
* IF KEYLGTH > 23 THEN LIST;
  NAMELEN=KEYLGTH-16;
 *--------  MAIN CSI  -----------------------------------------;
 IF IRBCATG = 1     THEN DELETE;
 *-----    ZONEINDEX ------------------------------------------;
 IF IRBCATG = 2 THEN DO;
    FILE ZONERPT HEADER=ZONEHDR NOTITLES PRINT;
ZONEIX: IF ZONEI = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4.
            @4  (ZINDEX1 ZINDEX2) (PIB2.) @;
      FORMAT ATTRX  $HEX8.;
      FORMAT ZID1 ZID2   HEX4.;
      ZID1=ZINDEX1; ZID2=ZINDEX2;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      PUT @PTR ' ZONENAME-' CSINAME @; PTR+20;
      PUT @PTR ' PRIMARY INDEX-' ZID1 @; PTR+20;
      PUT @PTR ' SECONDARY INDEX-' ZID2 ; PTR+20;
      DELETE;
END;* END OF ZONEI=IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    ZONEI=IRBNAME1;
    PUT ' ZONEINDEX '@;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO ZONEIX;
    END;
    DELETE;
    END;
 *-----    ZONE   ---------------------------------------------;
 IF  IRBCATG = 3 | IRBCATG = 4 | IRBCATG = 5  THEN DO;
    FILE ZONERPT HEADER=ZONEHDR NOTITLES PRINT;
ZONEX: IF ZONE = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      LIST;
END;* END OF LMOD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    ZONE=IRBNAME1;
    PUT '**@ ' ZONE @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO ZONEX;
    END;
    DELETE;
    END;
 *-------   ZONE DEFINITION -----------------------------------;
 IF IRBCATG = 16    THEN DO; * ZONE;
    FILE ZONERPT HEADER=ZONEHDR NOTITLES PRINT;
ZONEY: IF ZONE = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC = '1000'X
         THEN DO; DELETE; RETURN; END;
 ELSE IF IRBRELAC = '1001'X THEN DO;
         INDEX=CSINAME;
         PUT @PTR  ' INDEX-' CSINAME @; END;
 ELSE IF IRBRELAC = '1010'X THEN PUT @PTR  ' RELATED-' CSINAME @;
 ELSE IF IRBRELAC = '1021'X THEN PUT @PTR  ' SREL-' CSINAME @;
 ELSE IF IRBRELAC = '1022'X THEN PUT @PTR  ' FMID-' CSINAME @;
 ELSE IF IRBRELAC = '1026'X THEN PUT @PTR  ' OPTS-' CSINAME @;
  PTR+15;
  IF PTR > 118 THEN DO; PUT; PTR=13; END;
END;* END OF LMOD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    ZONE=IRBNAME1;
    PUT '** ' ZONE @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO ZONEY;
    END;
    DELETE;
    END;
 *-------   OTHER DEFINITION -----------------------------------;
 IF IRBCATG = 17    THEN DO; * ZONE;
    FILE DDEFRPT HEADER=DDEFHDR NOTITLES PRINT;
DDEFX:    IF DDEF = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC = '1100'X
         THEN DO; DELETE; RETURN; END;
 ELSE IF IRBRELAC = '1172'X THEN PUT @PTR  ' AMS-' CSINAME @;
 ELSE IF IRBRELAC = '1173'X THEN PUT @PTR  ' ASMBLR-' CSINAME @;
 ELSE IF IRBRELAC = '1174'X THEN PUT @PTR  ' COMPRS-' CSINAME @;
 ELSE IF IRBRELAC = '1175'X THEN PUT @PTR  ' COPY-' CSINAME @;
 ELSE IF IRBRELAC = '1177'X THEN PUT @PTR  ' LKED-' CSINAME @;
 ELSE IF IRBRELAC = '1178'X THEN PUT @PTR  ' RETRY-' CSINAME @;
 ELSE IF IRBRELAC = '1179'X THEN PUT @PTR  ' UPDTE-' CSINAME @;
 ELSE IF IRBRELAC = '117A'X THEN PUT @PTR  ' SZAP-' CSINAME @;
 ELSE IF IRBRELAC = '117B'X THEN PUT @PTR  ' NUCID-' CSINAME @;
 ELSE IF IRBRELAC = '1180'X THEN PUT @PTR  ' PGELEN-' CSINAME @;
 ELSE IF IRBRELAC = '1181'X THEN PUT @PTR  ' PREMAX-' CSINAME @;
 ELSE IF IRBRELAC = '1182'X THEN PUT @PTR  ' PURGE-' CSINAME @;
 ELSE IF IRBRELAC = '1183'X THEN PUT @PTR  ' REQST-' CSINAME @;
 ELSE IF IRBRELAC = '1188'X THEN PUT @PTR  ' RTYDD-' CSINAME @;
 ELSE IF IRBRELAC = '1190'X THEN PUT @PTR  ' SAVEMTS-' CSINAME @;
 ELSE IF IRBRELAC = '1191'X THEN PUT @PTR  ' SAVESTS-' CSINAME @;
 ELSE IF IRBRELAC = '1192'X THEN DO;
        INPUT @16 PRIM PIB2. @18 SECS PIB2. @20 DIRECTY PIB2. @;
        PUT @PTR  ' DSSPACE-' PRIM ',' SECS ',' DIRECTY @;
        PTR+10; END;
 ELSE IF IRBRELAC = '1193'X THEN DO;
        INPUT @16 PARM &$50.@;
        PUT @PTR  ' DSPREFIX-' PARM @;
        PTR+(NAMELEN-9); END;
  PTR+15;
  IF PTR > 118 THEN DO; PUT; PTR=13; END;
END;* END OF LMOD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    DDEF=IRBNAME1;
    PUT '** ' DDEF @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO DDEFX;
    END;
    DELETE;
    END;
 *-----DSN  ---------------------------------------------------;
 IF IRBCATG = 18    THEN DO; * LMOD;
    FILE DSNRPT HEADER=DSNHDR NOTITLES PRINT;
DSNX:  IF DSN = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC = '1200'X
         THEN DO; DELETE; RETURN; END;
 ELSE IF IRBRELAC = '1201'X THEN DO;
        INPUT @16 PARM &$50.@;
        PUT @PTR  ' DSN-' PARM @;
        IF NAMELEN-9 < 20 THEN PTR+20;
                          ELSE PTR+40;
        END;
 ELSE IF IRBRELAC = '1202'X THEN PUT @PTR  ' VOL-' CSINAME @;
 ELSE IF IRBRELAC = '1203'X THEN PUT @PTR  ' UNIT-' CSINAME @;
 ELSE IF IRBRELAC = '1209'X THEN PUT @PTR  ' CONCAT-' CSINAME @;
 ELSE IF IRBRELAC = '1210'X THEN PUT @PTR  ' DISP-' CSINAME @;
  PTR+15;
  IF PTR > 118 THEN DO; PUT; PTR=13; END;
END;* END OF LMOD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    DSN=IRBNAME1;
    PUT '** ' DSN @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO DSNX;
    END;
    DELETE;
    END;
 *-----PGM NAME -----------------------------------------------;
 IF IRBCATG = 19    THEN DO; * LMOD;
    FILE PGMRPT HEADER=PGMHDR NOTITLES PRINT;
 PGMX:   IF PGM = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4.
            @16 PARM &50. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC = '1300'X
         THEN DO; DELETE; RETURN; END;
 ELSE IF IRBRELAC = '1301'X THEN PUT @PTR  ' PGMNME-' CSINAME @;
 ELSE IF IRBRELAC = '1302'X THEN DO;
          PUT @PTR  ' PARM-' PARM @; PTR+(NAMELEN-6); END;
 ELSE IF IRBRELAC = '1303'X THEN DO;
          PUT @PTR  ' SYSOUT-' CSINAME @; PTR+5; END;
 ELSE IF IRBRELAC = '1304'X THEN PUT @PTR  ' RETCODE-' CSINAME @;
  PTR+15;
  IF PTR > 118 THEN DO; PUT; PTR=13; END;
END;* END OF LMOD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    PGM=IRBNAME1;
    PUT '** ' PGM @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO PGMX;
    END;
    DELETE;
    END;
 *-----SET FMID -----------------------------------------------;
 IF IRBCATG = 20    THEN DO; * LMOD;
    FILE FMIDRPT HEADER=FMIDHDR NOTITLES PRINT;
FMIDX: IF FMID = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC = '1400'X
         THEN DO; DELETE; RETURN; END;
 ELSE IF IRBRELAC = '1401'X THEN PUT @PTR  ' FMID-' CSINAME @;
  PTR+15;
  IF PTR > 118 THEN DO; PUT; PTR=13; END;
END;* END OF LMOD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    FMID=IRBNAME1;
    PUT '** ' FMID @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO FMIDX;
    END;
    DELETE;
    END;
 *-----ASM  -----------------------------------------------;
 IF IRBCATG = 32    THEN DO; * LMOD;
    FILE ASMRPT HEADER=ASMHDR NOTITLES PRINT;
ASMX: IF ASM = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC = '2000'X
         THEN DO; DELETE; RETURN; END;
 ELSE IF IRBRELAC = '2010'X THEN PUT @PTR  ' BKUP-' CSINAME @;
  PTR+15;
  IF PTR > 118 THEN DO; PUT; PTR=13; END;
END;* END OF LMOD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    ASM=IRBNAME1;
    PUT '** ' ASM @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO ASMX;
    END;
    DELETE;
    END;
 *-------------------------------------------------------------;
 IF IRBCATG = 33    THEN DO; * LMOD;
    FILE LMODRPT HEADER=LMODHDR NOTITLES PRINT;
LMODX: IF LMOD = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC = '2100'X
         THEN DO; DELETE; RETURN; END;
 ELSE IF IRBRELAC = '2101'X THEN PUT @PTR  ' ATTR-' ATTRX @;
 *  ATTR ANALYSIS CAN BE DONE ON IOPLMLEP SEE GIMMPIOP;
 ELSE IF IRBRELAC = '2110'X THEN PUT @PTR  ' BKUP-' CSINAME @;
 ELSE IF IRBRELAC = '2113'X THEN PUT @PTR  ' LLIB-' CSINAME @;
  PTR+15;
  IF PTR > 118 THEN DO; PUT; PTR=13; END;
END;* END OF LMOD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    LMOD=IRBNAME1;
    PUT '** ' LMOD @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO LMODX;
    END;
    DELETE;
    END;
 *-------------------------------------------------------------;
 IF IRBCATG = 34    THEN DO; * MAC ;
    FILE MACRPT  HEADER=MACHDR NOTITLES PRINT;
MACX: IF MAC = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC = '2200'X
         THEN DO; DELETE; RETURN; END;
 ELSE IF IRBRELAC = '2201'X THEN PUT @PTR  ' FMID-' CSINAME @;
 ELSE IF IRBRELAC = '2202'X THEN PUT @PTR  ' RMID-' CSINAME @;
 ELSE IF IRBRELAC = '2210'X THEN PUT @PTR  ' BKUP-' CSINAME @;
 ELSE IF IRBRELAC = '2211'X THEN PUT @PTR  ' DLIB-' CSINAME @;
 ELSE IF IRBRELAC = '2213'X THEN PUT @PTR  ' LLIB-' CSINAME @;
 ELSE IF IRBRELAC = '2230'X THEN PUT @PTR  ' GENASM-' CSINAME @;
 ELSE IF IRBRELAC = '2240'X THEN PUT @PTR  ' ALIAS-' CSINAME @;
 ELSE IF IRBRELAC = '2250'X THEN PUT @PTR  ' UMID-' CSINAME @;
  PTR+15;
  IF PTR > 118 THEN DO; PUT; PTR=13; END;
END;* END OF MAC =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    MAC=IRBNAME1;
    PUT '** ' MAC @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO MACX;
    END;
  DELETE;
    END;
 *-------------------------------------------------------------;
 IF IRBCATG = 35    THEN DO; * MOD ;
    FILE MODRPT  HEADER=MODHDR NOTITLES PRINT;
MODX:    IF MOD = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC = '2300'X
         THEN DO; DELETE; RETURN; END;
 ELSE IF IRBRELAC = '2301'X THEN PUT @PTR  ' FMID-' CSINAME @;
 ELSE IF IRBRELAC = '2302'X THEN PUT @PTR  ' RMID-' CSINAME @;
 ELSE IF IRBRELAC = '2310'X THEN PUT @PTR  ' BKUP-' CSINAME @;
 ELSE IF IRBRELAC = '2311'X THEN PUT @PTR  ' DLIB-' CSINAME @;
 ELSE IF IRBRELAC = '2313'X THEN PUT @PTR  ' ATTR-' ATTRX @;
 *  ATTR ANALYSIS CAN BE DONE ON IOPMDLEP SEE GIMMPIOP;
 ELSE IF IRBRELAC = '2340'X THEN PUT @PTR  ' CSECT-' CSINAME @;
 ELSE IF IRBRELAC = '2341'X THEN PUT @PTR  ' CSECTL-' CSINAME @;
 ELSE IF IRBRELAC = '2342'X THEN PUT @PTR  ' TALIAS-' CSINAME @;
 ELSE IF IRBRELAC = '2343'X THEN PUT @PTR  ' DALIAS-' CSINAME @;
 ELSE IF IRBRELAC = '2350'X THEN PUT @PTR  ' UMID-' CSINAME @;
  PTR+15;
  IF PTR > 118 THEN DO; PUT; PTR=13; END;
END;* END OF SYSMOD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    MOD=IRBNAME1;
    PUT '** ' MOD @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO MODX;
    END;
  DELETE;
    END;
 *-------------------------------------------------------------;
 IF IRBCATG = 36    THEN DO; * SRC ;
    FILE SRCRPT  HEADER=SRCHDR NOTITLES PRINT;
SRCX: IF SRC = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC = '2400'X
         THEN DO; DELETE; RETURN; END;
 ELSE IF IRBRELAC = '2401'X THEN PUT @PTR  ' FMID-' CSINAME @;
 ELSE IF IRBRELAC = '2402'X THEN PUT @PTR  ' RMID-' CSINAME @;
 ELSE IF IRBRELAC = '2410'X THEN PUT @PTR  ' BKUP-' CSINAME @;
 ELSE IF IRBRELAC = '2411'X THEN PUT @PTR  ' DLIB-' CSINAME @;
 ELSE IF IRBRELAC = '2413'X THEN PUT @PTR  ' LIB-' CSINAME @;
 ELSE IF IRBRELAC = '2450'X THEN PUT @PTR  ' UMID-' CSINAME @;
  PTR+15;
  IF PTR > 118 THEN DO; PUT; PTR=13; END;
END;* END OF SYSMOD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    SRC=IRBNAME1;
    PUT '** ' SRC @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO SRCX;
    END;
  DELETE;
    END;
 *-------------------------------------------------------------;
 IF IRBCATG = 37    THEN DO; * DLIB;
    FILE DLIBRPT  HEADER=DLIBHDR NOTITLES PRINT;
DLIBX: IF DLIB = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC = '2500'X
         THEN DO; DELETE; RETURN; END;
 ELSE IF IRBRELAC = '2510'X THEN PUT @PTR  ' BKUP-' CSINAME @;
 ELSE IF IRBRELAC = '2513'X THEN PUT @PTR  ' CPYLIB-' CSINAME @;
  PTR+15;
  IF PTR > 118 THEN DO; PUT; PTR=13; END;
END;* END OF SYSMOD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    DLIB=IRBNAME1;
    PUT '** ' DLIB @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO DLIBX;
    END;
  DELETE;
    END;
 *-------------------------------------------------------------;
 IF IRBCATG = 38    THEN DO;    *SYSMOD RECORD;
    FILE SYSMOD HEADER=SMODHDR NOTITLES PRINT;
SYSMODX: IF SYSMOD = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4.
            @16 ATTRB1 PIB1. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC = '2637'X | IRBRELAC = '2638'X
         THEN DO; DELETE; RETURN; END;
 ELSE IF IRBRELAC = '2601'X THEN DO;
         TYPE= CSINAME;
         PUT @PTR  ' TYPE-' TYPE @ ; END;
 ELSE IF IRBRELAC = '2600'X THEN PUT @PTR  ' SMDSEC-' CSINAME @;
 ELSE IF IRBRELAC = '2602'X THEN DO;
    PUT @PTR  ' ATTR-' ATTRX @;
    PTR+15;
    IF PTR > 118 THEN DO; PUT; PTR=13; END;
    * IOPPSTAT ANALYSIS;
    IF ATTRB1 = '1.......'B THEN DO;
       PUT @PTR 'PTF APPLIED' @; PTR+15;
       IF PTR > 118 THEN DO; PUT; PTR=13; END;  END;
    IF ATTRB1 = '.1......'B THEN DO;
       PUT @PTR 'RESTORE ATTEMPTED' @; PTR+20;
       IF PTR > 118 THEN DO; PUT; PTR=13; END;  END;
    IF ATTRB1 = '..1.....'B THEN DO;
       PUT @PTR 'PTF ACCEPTED' @; PTR+15;
       IF PTR > 118 THEN DO; PUT; PTR=13; END;  END;
    IF ATTRB1 = '...1....'B THEN DO;
       PUT @PTR 'SYS ERR ENCOUNTERED' @; PTR+20;
       IF PTR > 118 THEN DO; PUT; PTR=13; END;  END;
    IF ATTRB1 = '.....1..'B THEN DO;
       PUT @PTR 'PTF IS SUPED/DELETED' @; PTR+20;
       IF PTR > 118 THEN DO; PUT; PTR=13; END;  END;
    IF ATTRB1 = '......1.'B THEN DO;
       PUT @PTR 'BYPASS USED TO PROCESS' @; PTR+25;
       IF PTR > 118 THEN DO; PUT; PTR=13; END;  END;
    IF ATTRB1 = '.......1'B THEN DO;
       PUT @PTR 'PTF DEFAULT APPLIED VIA SYSGEN' @; PTR+30;
       IF PTR > 118 THEN DO; PUT; PTR=13; END;  END;
   END;
 ELSE IF IRBRELAC = '2611'X THEN PUT @PTR  ' DELBY-' CSINAME @;
 ELSE IF IRBRELAC = '2612'X THEN PUT @PTR  ' LSUBBY-' CSINAME @;
 ELSE IF IRBRELAC = '26A1'X THEN PUT @PTR  ' SREL-' CSINAME @;
 ELSE IF IRBRELAC = '26C2'X THEN PUT @PTR  ' APP-' CSINAME @;
 ELSE IF IRBRELAC = '26C3'X THEN PUT @PTR  ' ACC-' CSINAME @;
 ELSE IF IRBRELAC = '26F0'X THEN PUT @PTR  ' SUPBY-' CSINAME @;
 ELSE IF IRBRELAC = '26F1'X THEN DO;
        PUT @PTR  ' CIREQ-' CSINAME @;
      INPUT @24 IRBNAM2F $CHAR8. @;
      CSINAME=IRBNAM2F; PTR+15; IF PTR >118 THEN  PUT;
        PUT @PTR  ' CIREQ-' CSINAME @;
        END;
 ELSE IF IRBRELAC = '26F4'X THEN PUT @PTR  ' HERR-' CSINAME @;
 ELSE IF IRBRELAC = '26F5'X THEN PUT @PTR  ' HSYS-' CSINAME @;
 ELSE IF IRBRELAC = '26F6'X THEN PUT @PTR  ' HUSER-' CSINAME @;
 ELSE IF IRBRELAC = '2630'X THEN PUT @PTR  ' BKUP-' CSINAME @;
 ELSE IF IRBRELAC = '2631'X THEN PUT @PTR  ' FMID-' CSINAME @;
 ELSE IF IRBRELAC = '2639'X THEN PUT @PTR  ' SRCEID-' CSINAME @;
 ELSE IF IRBRELAC = '263A'X THEN PUT @PTR  ' FESD-' CSINAME @;
 ELSE IF IRBRELAC = '26A2'X THEN PUT @PTR  ' FMID-' CSINAME @;
 ELSE IF IRBRELAC = '26A3'X THEN PUT @PTR  ' PRE-' CSINAME @;
 ELSE IF IRBRELAC = '26A4'X THEN PUT @PTR  ' NPRE-' CSINAME @;
 ELSE IF IRBRELAC = '26A5'X THEN PUT @PTR  ' SUP-' CSINAME @;
 ELSE IF IRBRELAC = '26A6'X THEN PUT @PTR  ' REQ-' CSINAME @;
 ELSE IF IRBRELAC = '26A7'X THEN PUT @PTR  ' IREQ-' CSINAME @;
 ELSE IF IRBRELAC = '26A8'X THEN PUT @PTR  ' DEL-' CSINAME @;
 ELSE IF IRBRELAC = '26A9'X THEN PUT @PTR  ' VERSION-' CSINAME @;
 ELSE IF IRBRELAC = '26B1'X THEN PUT @PTR  ' MOD-' CSINAME @;
 ELSE IF IRBRELAC = '26B2'X THEN PUT @PTR  ' ZAP-' CSINAME @;
 ELSE IF IRBRELAC = '26B3'X THEN PUT @PTR  ' XZAP-' CSINAME @;
 ELSE IF IRBRELAC = '26B4'X THEN PUT @PTR  ' MAC-' CSINAME @;
 ELSE IF IRBRELAC = '26B5'X THEN PUT @PTR  ' MACU-' CSINAME @;
 ELSE IF IRBRELAC = '26B6'X THEN PUT @PTR  ' SRC-' CSINAME @;
 ELSE IF IRBRELAC = '26B7'X THEN PUT @PTR  ' SRCU-' CSINAME @;
 ELSE IF IRBRELAC = '26C1'X THEN PUT @PTR  ' ASM-' CSINAME @;
 ELSE DO; INPUT  @16 IRBDATE PD3. @;
    IF IRBRELAC = '2632'X THEN PUT @PTR  ' RECDATE-' IRBDATE @;
 ELSE IF IRBRELAC = '2633'X THEN PUT @PTR  ' INSDATE-' IRBDATE @;
 ELSE IF IRBRELAC = '2635'X THEN PUT @PTR  ' RSTDATE-' IRBDATE @;
 ELSE IF IRBRELAC = '2636'X THEN PUT @PTR  ' UCLDATE-' IRBDATE @ ;
  END; * END OF IRBDATE;
  PTR+15;
  IF PTR > 118 THEN DO; PUT; PTR=13; END;
END;* END OF SYSMOD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    SYSMOD=IRBNAME1;
    PUT '** ' SYSMOD @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO SYSMODX;
    END;
  DELETE;
    END;
 *------------  SOURCE CARDS ------------------------------------;
 IF IRBCATG = 48    THEN DO;    *SRCLST RECORD;
    FILE SRCLTRPT HEADER=SRCLTHDR NOTITLES PRINT;
SRCLTX: IF SRCLST = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4.
            @16 ATTRB1 PIB1. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC = '3001'X THEN DO;
         INPUT @16 CARD $80. @;
         PUT @PTR  ' SRCLIST-' CARD ;
        PTR=28 ; END;
END;* END OF SRCLST =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    SRCLST=IRBNAME1;
    PUT '** ' SRCLST @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO SRCLTX;
    END;
  DELETE;
    END;
 *------------  LMOD CARDS ------------------------------------;
 IF IRBCATG = 49    THEN DO;    *LMOD RECORD;
    FILE LMODCRPT HEADER=LMODCHDR NOTITLES PRINT;
LMODCX: IF LMODCD = IRBNAME1 THEN DO;
      INPUT @16 IRBNAM2F $CHAR8. @16 IRBATTRX $CHAR4.
            @16 ATTRB1 PIB1. @;
      FORMAT ATTRX  $HEX8.;
      ATTRX=IRBATTRX;
      CSINAME=IRBNAM2F;
      IF IRBRELAC Â¬= '3101'X
         THEN DO; DELETE; RETURN; END;
 ELSE IF IRBRELAC = '3101'X THEN DO;
         INPUT @16 CARD $80. @;
         PUT @PTR  ' LMODCARD-' CARD ;
        PTR=28 ; END;
END;* END OF LMODCD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    LMODCD=IRBNAME1;
    PUT '** ' LMODCD @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+15;
    GO TO LMODCX;
    END;
  DELETE;
    END;
 *------------  HOLD CARDS ------------------------------------;
 IF IRBCATG = 50    THEN DO;    *HOLD RECORD;
    FILE HOLDRPT HEADER=HOLDHDR NOTITLES PRINT;
HOLDX: IF HOLD = IRBNAME1 THEN DO;
         INPUT @16 CARD $80. @;
      IF IRBRELAC = '3210'X
         THEN DO;
         PUT @PTR  ' ++HOLD-' CARD ;
        PTR=26 ; END;
 ELSE IF IRBRELAC = '3220'X THEN DO;
         PUT @PTR  ' ++COMMENT-' CARD ;
        PTR=26 ; END;
 ELSE IF IRBRELAC = '3230'X THEN DO;
         PUT @PTR  ' ++FULLGEN-' CARD ;
        PTR=26 ; END;
        ELSE LIST;
END;* END OF HOLD =IRBNAME1;
ELSE DO;
    PUT; PTR=13;
    HOLD=IRBNAME1;
    PUT '** ' HOLD @;
    PUT @PTR ' ZONE-' CCACZIDX @; PTR+13;
    GO TO HOLDX;
    END;
  DELETE;
    END;
 LIST;DELETE;
RETURN;
SMODHDR: PUT ' CDSI -    SYSMOD LISTING FROM CSI FILE ';
         RETURN;
HOLDHDR: PUT ' CDSI -    ++HOLD LISTING FROM CSI FILE ';
         RETURN;
LMODHDR: PUT ' CDSI -    LMOD LISTING FROM CSI FILE ';
         RETURN;
LMODCHDR: PUT ' CDSI -    LMOD CARDS LISTING FROM CSI FILE ';
         RETURN;
SRCLTHDR: PUT ' CDSI -    SOURCE CARDS LISTING FROM CSI FILE ';
         RETURN;
MODHDR: PUT ' CDSI -    MOD LISTING FROM CSI FILE ';
         RETURN;
MACHDR: PUT ' CDSI -    MAC LISTING FROM CSI FILE ';
         RETURN;
SRCHDR: PUT ' CDSI -    SRC LISTING FROM CSI FILE ';
         RETURN;
DLIBHDR: PUT ' CDSI -    DLIB LISTING FROM CSI FILE ';
         RETURN;
ZONEHDR: PUT ' CDSI -    ZONE LISTING FROM CSI FILE ';
         RETURN;
DDEFHDR: PUT ' CDSI -    DDDEF LISTING FROM CSI FILE ';
         RETURN;
DSNHDR: PUT ' CDSI -    DSN LISTING FROM CSI FILE ';
         RETURN;
PGMHDR: PUT ' CDSI -    PGM LISTING FROM CSI FILE ';
         RETURN;
FMIDHDR: PUT ' CDSI -   FMID LISTING FROM CSI FILE ';
         RETURN;
ASMHDR: PUT ' CDSI -    ASM LISTING FROM CSI FILE ';
         RETURN;
//
