ETPS     TITLE '--- E T P S -- EMERGENCY TELE-PROCESSING SYSTEM ---'
         PRINT OFF
         COPY  ETPS$O2A            "ETPS" SYSPARM SELECTION
         MACRO
        @ULT   &DUMMY
         GBLA  &ESSN
         GBLC  &ESSO,&ESSE(1)
         LCLA  &N
*------- USERS TABLE ------------------------------------------------*
USERTBL  DS    0CL7                START USERS TABLE
         DC    CL7'&ESSO'          MASTER USER
&N       SETA  1
.A       AIF   (&N GT &ESSN).D
         AIF   ('&ESSE(&N)' EQ '').B
         DC    CL7'&ESSE(&N)'
         AGO   .C
.B       AIF   (&N EQ &ESSN).D
         DC    CL7' '
.C       ANOP
&N       SETA  &N+1
         AGO   .A
.D       DC    XL1'0'              END OF TABLE
         MEND
         MACRO
        @MPW   &DUMMY
         GBLC  &ESSM
         AIF   ('&ESSM' EQ '').A
MASTPASS DC    CL8'&ESSM'
         AGO   .B
.A       ANOP
MASTPASS DC    CL8' '
.B       MEND
         COPY  ETPSMACS            "ETPS" MACROS
         COPY  ETPSDEFS            "ETPS" DEFINITIONS
         PRINT ON
ETPS     START 0
         SPACE 1
ETPS     AMODE 24
ETPS     RMODE 24
         SPACE 1
**********************************************************************
** --------------------------  E  T  P  S  ------------------------ **
**********************************************************************
**                                                                  **
**  FUNCTION : THIS PROGRAM IS INTENDED TO PROVIDE EMERGENCY        **
**             TELE-PROCESSING SERVICES WHEN THE NORMAL SUBSYSTEMS  **
**             SUCH AS JES2, VTAM, TCAM, ETC... ARE NOT AVAILABLE.  **
**             IT IS INTENDED TO BE INVOKED AS A STARTED TASK OR A  **
**             BATCH JOB. IT CAN ALSO BE INVOKED AS A TSO COMMAND   **
**             PROCESSOR USING A FRONT-END MODULE, "ETPSTSO".       **
**                                                                  **
**             IF YOU WANT TO INVOKE IT AS A SUB-SYSTEM WHEN JES2   **
**             IS NOT UP, YOU MUST ADD A SUB-SYSTEM DEFINITION.     **
**                                                                  **
**             REMARK : WITH RACF, MODULE "ICHRIN03" CONTAINS THE   **
**                      DEFAULT USER-ID AND GROUP FOR SUB-SYSTEM    **
**                      AND STARTED TASK.                           **
**                                                                  **
**      PARM : THIS PROGRAM MUST BE INVOKED WITH A STANDARD         **
**             EXECUTE-CARD FORMAT PARAMETER, THE LENGTH MUST BE    **
**             BYTES, AS FOLLOWS :                                  **
**             1. THE FIRST 4 CHARACTERS MUST BE "ETPS" OR "OPER"   **
**                (THIS LAST BYPASS THE LOGON PANEL).               **
**             2. THE 5TH BYTE IS TERMINAL TYPE. VALID TERMINAL     **
**                TYPES ARE :                                       **
**                A = 3278-2A OR 3279-2C (20-LINE TERMINAL)         **
**                2 = 3278 MODEL 2                                  **
**                3 = 3279 MODEL 3B                                 **
**                4 = 3278 MODEL 4                                  **
**             3. THE LAST 3 BYTES MUST BE THE UNIT ADDRESS OF THE  **
**                TERMINAL. AS AN EXAMPLE,                          **
**                //STEP1  EXEC  PGM=ETPS,PARM='ETPS49C4'           **
**                STARTS TIME-SHARING ON A LOCAL 3278 MODEL 4 AT    **
**                ADDRESS 9C4.                                      **
**             TO INVOKE AS A TSO COMMAND PROCESSOR, THE PARAMETER  **
**             MUST BE :  DC  H'8',CL8'TSO'  (SEE MODULE ETPSTSO).  **
**       DOC : REFER TO ETPS@ FOR FURTHER DOCUMENTATION.            **
**                                                                  **
**********************************************************************
         EJECT
        MIDENT
         SPACE 1
**********************************************************************
**                                                                  **
**  REMARKS : (ONLY IN THIS MODULE)                                 **
**  ========= 1. ALL CARDS MARKED <X> IN COLUMNS 68-70 INDICATE THE **
**               CODED INSTRUCTIONS EXECUTED ONLY WHEN TERMINAL I/O **
**               IS DONE THROUGH EXCP'S, IN OTHER WORDS, WHEN ETPS  **
**               RUN AS SUB-SYSTEM OR STARTED-TASK.                 **
**            2. ALL CARDS MARKED <T> IN COLUMNS 68-70 INDICATE THE **
**               CODED INSTRUCTIONS EXECUTED WHEN TSO INVOCATION    **
**               (ETPSTSO) IS DONE WITH THE TEST OPERAND.           **
**                                                                  **
**********************************************************************
         EJECT
*======= SECTIONS INFORMATION =======================================*
*   NAME      SAVE-AREA  OTHER SECTIONS USED OR CALLED               *
*   ========  =========  ========================================    *
*   ETPS      SAVE1      ETPSXAI                                 <X> *
*                        ETPSCOMM  ETPSMESS  ETPSFDTE  ETPSLIBM      *
*                        ETPSCOMP  ETPSIDCA  ETPSZAP   ETPSIZAP      *
*                        ETPSDSST                                    *
*   --------  ---------  ----------------------------------------    *
*   ETPSXAI   SAVE2      XATTN     XABEND                        <X> *
*   --------  ---------  ----------------------------------------    *
*   ETPSLIBM  SAVE2      ETPSCOMM  ETPSMESS  ETPSDYNA                *
*                        ETPSBROW  ETPSEDIT  ETPSMEMC                *
*   --------  ---------  ----------------------------------------    *
*   ETPSCOMP  SAVE2      ETPSCOMM  ETPSMESS  ETPSDYNA                *
*   --------  ---------  ----------------------------------------    *
*   ETPSIDCA  SAVE2      ETPSCOMM  ETPSMESS                          *
*                        ETPSBROW                                    *
*   --------  ---------  ----------------------------------------    *
*   ETPSZAP   SAVE2      ETPSCOMM  ETPSMESS  ETPSDYNA                *
*                        ETPSXZAP  ETPSBROW                          *
*   --------  ---------  ----------------------------------------    *
*   ETPSIZAP  SAVE2      ETPSCOMM  ETPSMESS  ETPSDYNA                *
*                        ETPSINCZ  ETPSBROW                          *
*   --------  ---------  ----------------------------------------    *
*   ETPSDSST  SAVE2      ETPSCOMM  ETPSMESS  ETPSDYNA                *
*                        ETPSXDST  ETPSBROW                          *
*   --------  ---------  ----------------------------------------    *
*   ETPSBROW  SAVE3      ETPSCOMM                                    *
*   --------  ---------  ----------------------------------------    *
*   ETPSEDIT  SAVE3      ETPSCOMM  ETPSMESS                          *
*   --------  ---------  ----------------------------------------    *
*   ETPSXZAP  SAVE3      ETPSCOMM  ETPSMESS  ETPSDYNA                *
*   --------  ---------  ----------------------------------------    *
*   ETPSINCZ  SAVE3      ETPSDYNA  ETPSFDTE                          *
*   --------  ---------  ----------------------------------------    *
*   ETPSXDST  SAVE3      ETPSDYNA  ETPSFDTE                          *
*   --------  ---------  ----------------------------------------    *
*   ETPSCOMM  SAVE4      ETPSMESS                                    *
*   --------  ---------  ----------------------------------------    *
*   ETPSMEMC  LOCAL-SA                                               *
*   --------  ---------  ----------------------------------------    *
*   ETPSDYNA  LOCAL-SA                                               *
*   --------  ---------  ----------------------------------------    *
*   ETPSFDTE  LOCAL-SA                                               *
*   --------  ---------  ----------------------------------------    *
*   ETPSMESS  LOCAL-SA                                               *
*====================================================================*
         EJECT
         PRINT GEN
         USING *,R12,R11
         B     32(,R15)
         DC    CL28'  ETPS     &SYSDATE &SYSTIME'
         PRINT &PRF
         STM   R14,R12,12(R13)     SAVE INPUT REGISTERS
         LR    R12,R15             SET PROGRAM BASE REGISTER 1
         LA    R3,1
         LA    R11,4*KB-1(R3,R12)  SET PROGRAM BASE REGISTER 2
         L     R2,0(,R1)           SAVE PARM POINTER
         LR    R4,R0               SAVE "SNAP" ADDRESS IF TSO
         L     R9,GETSIZE          GET WORK-AREA LENGTH
        GETMAIN R,LV=(R9)          GET "MYSAVE" WORK-AREA (UNCOND)
         LR    R8,R1               SAVE GETMAIN POINTER
         ST    R8,8(R13)           STORE FORWARD POINTER
         LR    R5,R13              SAVE BACKWARD POINTER
         LR    R13,R8              SET DSECT BASE 1
         LA    R10,4*KB-1(R3,R13)  SET DSECT BASE 2
         USING MYSAVE,R13,R10 ************************** R13 ==> SAVE1
         LA    R14,*
         XR    R15,R15
         MVCL  R8,R14              CLEAR ALL TO BINARY ZEROS
         ST    R5,4(R13)           STORE BACKWARD POINTER
         ST    R13,SAVETOP         SAVE THE START ADDRESS (A.SAVE1)
         SPACE 1
*------- INITIALIZATION ---------------------------------------------*
         LA    R1,1
         ST    R1,ARSNAP
        LOAD   EP=ETPSUTIL,ERRET=LUTLERR PLUG EPA'S ADDRESSES
         LR    R1,R0
         MVC   ARDYNAM(2*4),0(R1)  SET ADDRESSES
        LOAD   EP=ETPSMESS,ERRET=LMSGERR PLUG EPA ADDRESS
         ST    R0,ARMESS           SET ADDRESS
         MVC   ARPOPS(3*4),=A(ETPSPOPS,ETPSCOMM,ETPSLIBM)
         MVC   SUPRMOD(SMODLEN),SMOD INITIALIZE MODESETS
         MVC   PROBMOD(PMODLEN),PMOD
         MVC   SPLIT1(18*4),0(R13) INITIALIZE SAVE-AREAS FOR
         MVC   SPLIT2(18*4),0(R13)   SPLIT-SCREEN
         MVC   SPLOFF1,=A(SPLIT1-SAVE4) 1ST OFFSET TO SPLIT AREA
         MVC   SPLOFF2,=A(SPLIT2-SAVE4) 2ND OFFSET TO SPLIT AREA
         MVC   XQSNAP(L'XESNAP),XESNAP INITIALIZE SNAP PROCESSING
         LA    R1,8                PICK UP PARM = DDNAME
         CH    R1,0(,R2)           PARM LENGTH = 8?
         BE    OKPRML              YES
        XMESS  1001                ELSE SHUT IT DOWN
         B     RTRNEND
LUTLERR  XR    R2,R2               SAY "ETPSUTIL" (+0)
         B     *+L'*+4
LMSGERR  LA    R2,4                SAY "ETPSMESS" (+4)
         LR    R4,R1               SAVE R1 AND R15
         LR    R5,R15
         MVC   SPLITWRK+4(EXTLSTL),EXTLST
        EXTRACT SPLITWRK,'S',FIELDS=(TSO),MF=(E,SPLITWRK+4)
         L     R3,SPLITWRK
         MVC   SPLITWRK(LOADMSGL),LOADMSG
         B     *+L'*(R2)
         B     *+L'*+4             +0
         B     *+L'*+10            +4
         MVC   SPLITWRK+LDMS1(L'LDMS1),=CL4'UTIL'
         B     *+L'*+6
         MVC   SPLITWRK+LDMS1(L'LDMS1),=CL4'MESS'
         TM    0(R3),X'80'         TSO FOREGROUND RUNNING?
         BZ    *+L'*+6             NO, OK
         MVC   SPLITWRK+LOADMSGL-L'ALTRDF(L'ALTRDF),ALTRD+ALTRDF
         CVD   R4,DBLWRD
         MVC   DBLWRD(4),=XL4'40202120'
         ED    DBLWRD(4),DBLWRD+6
         MVC   SPLITWRK+LDMS2(L'LDMS2),DBLWRD+1
         L     R0,L'LDMS3
         LA    R15,SPLITWRK+LDMS3+L'LDMS3-1
         STC   R5,0(R15)
         BCTR  R15,0
         SRL   R5,4
         BCT   R0,*-10
         NC    SPLITWRK+LDMS3(L'LDMS3),=XL8'0F0F0F0F0F0F0F0F'
         TR    SPLITWRK+LDMS3(L'LDMS3),=CL16'0123456789ABCDEF'
         TM    0(R3),X'80'         TSO FOREGROUND RUNNING?
         BZ    DOWTOM              NO, GO DO WTO
        TPUT   SPLITWRK+4,L'LOADMSG-8
         B     WTGEX
DOWTOM  WTO    MF=(E,SPLITWRK)
WTGEX    B     *+L'*(R2)
         B     QUIT                +0
         B     LEAVE               +4
OKPRML   CLC   2(4,R2),=CL4'OPER'  OPERATOR TERMINAL?
         BE    *+L'*+10            YES
         CLC   2(4,R2),=CL4'ETPS'  USER TERMINAL?
         BNE   CHKTSO              NO
         MVC   DYNRB(DYNLENG),CONRB ALLOCATE TUBE                  <X>
         MVC   TUBEDDNM(L'TUBEDDNM),2(R2) MOVE ENVIRONMENT         <X>
         LA    R1,DYNDDNAM                                         <X>
         ST    R1,DYNTEXT1                                         <X>
         LA    R1,DYNDISP                                          <X>
         ST    R1,DYNTEXT2                                         <X>
         LA    R1,DYNUNIT                                          <X>
         ST    R1,DYNTEXT3                                         <X>
         OI    DYNTEXT3,S99TUPLN                                   <X>
         LA    R1,DYNTEXT1                                         <X>
         ST    R1,TEXTPTRS                                         <X>
         LA    R1,DYNRB                                            <X>
         ST    R1,DYNPARM                                          <X>
         OI    DYNPARM,S99RBPND                                    <X>
         MVC   UNITADDR,TERMADDR   MOVE IN UNIT ADDRESS            <X>
         LA    R1,DYNPARM                                          <X>
        DYNALLOC ,                                                 <X>
         LTR   R15,R15             HOW COMPLETE?                   <X>
         BZ    OKDYNA              OK                              <X>
        XMESS  1003,R15                                            <X>
         B     RTRNEND                                             <X>
OKDYNA   NI    COMMSW,255-PERMERR-INTERR-PFKFLAG 1ST TIME THROUGH  <X>
         MVI   WRITECC,X'0D'       SET ERASE/WRITE ALTERNATE       <X>
         CLI   TERMTYPE,C'3'       IS IT A MOD3?                   <X>
         BE    ITSMOD3             GO SET IT                       <X>
         CLI   TERMTYPE,C'4'       IS IT A MOD4?                   <X>
         BE    ITSMOD4             GO SET IT                       <X>
         MVI   WRITECC,X'05'       SET DEFAULT COMMAND CODE  W.A.M.<X>
         CLI   TERMTYPE,C'A'       IS IT A 3278-2A?          W.A.M.<X>
         BE    ITSMOD2A            SET 20 LINES              W.A.M.<X>
         LA    R1,24*80            ASSUME 3278-2, DEF. BUFF.SIZE   <X>
         ST    R1,SCREENSZ         SET IT                          <X>
         LA    R15,24              DEFAULT NUMBER OF ROWS          <X>
         ST    R15,SCROWS          SET IT                          <X>
         B     SETSCSZ             USE DEFAULT                     <X>
ITSMOD3  LA    R1,28*80            MOD 3 BUFFER SIZE               <X>
         ST    R1,SCREENSZ         SET IT                          <X>
         LA    R15,28              DEFAULT NUMBER OF ROWS          <X>
         ST    R15,SCROWS          SET IT                          <X>
         B     SETSCSZ             ALL SET                         <X>
ITSMOD4  LA    R1,43*80            MOD 4 BUFFER SIZE               <X>
         ST    R1,SCREENSZ         SET IT                          <X>
         LA    R15,43              DEFAULT NUMBER OF ROWS          <X>
         ST    R15,SCROWS          SET IT                          <X>
         B     SETSCSZ             ALL SET                         <X>
ITSMOD2A LA    R1,20*80            MOD 2A BUFFER SIZE        W.A.M.<X>
         ST    R1,SCREENSZ         SET IT                    W.A.M.<X>
         LA    R15,20              DEFAULT NUMBER OF ROWS    W.A.M.<X>
         ST    R15,SCROWS          SET IT                    W.A.M.<X>
SETSCSZ  MVC   SCROWS1,SCROWS                                      <X>
         XC    SCROWS2,SCROWS2                                     <X>
         MVC   TUBE(TUBEL),TINIT   SCREEN DCB                      <X>
         LA    R2,TUBE             GET DCB ADDRESS                 <X>
         USING IHADCB,R2                                           <X>
         MVC   DCBDDNAM,TUBEDDNM   SET DD-NAME                     <X>
         LA    R1,TUBEIOB          SET IOB ADDRESS                 <X>
         STCM  R1,B'0111',DCBIOBAA IN THE DCB                      <X>
         MVI   OPCLPL,VLB          SET "VL" BIT                    <X>
        OPEN   ((R2)),MF=(E,OPCLPL) OPEN TUBE                      <X>
         TM    DCBOFLGS,DCBOFOPN   GOOD OPEN?                      <X>
         BO    OKOPEN              YES                             <X>
        XMESS  1004                NOPE                            <X>
         B     FRTUBE                                              <X>
OKOPEN   XC    RECB(4),RECB        CLEAR ECB                       <X>
         MVC   TUBEIOB(IOBLEN),IOBINIT INITIALIZE IOB              <X>
         STCM  R2,B'0111',TUBEIOB+(IOBDCBPB-IOBSTDRD) SET A(DCB)   <X>
         AIF   ('&EATL' EQ '').SKX1                                <X>
         L     R15,=A(ETPSXAI)     GO TO EXCP-ATTENTION INIT.      <X>
*- - - - ETPSXAI PARM.LIST : NONE (R2 = DCB ADDRESS)               <X>
        CALL   (15)                                                <X>
.SKX1    B     INITBUF                                             <X>
         DROP  R2                                                  <X>
CHKTSO   CLC   2(3,R2),=CL3'TSO'   TSO TERMINAL?
         BE    ITSTSO              YES
        XMESS  1002                NO
         B     RTRNEND
ITSTSO   MVC   TUBEDDNM(L'TUBEDDNM),2(R2) YES, MOVE ENVIRONMENT
         CLI   TUBEDDNM+L'TUBEDDNM-1,C'T' WANT TEST LOGON PANEL?   <T>
         BNE   *+L'*+4             NO                              <T>
         OI    STATSW,TESTFLG      YES, SET REQUEST                <T>
         LTR   R4,R4               SNAP ACTIVE?
         BZ    *+L'*+4             NO, SKIP
         ST    R4,ARSNAP           YES, SET "SNAP" ADDRESS
        GTSIZE
         LTR   R15,R15             HOW COMPLETE?
         BZ    OKSIZE              WELL
        XMESS  1005,R15
         B     RTRNEND
OKSIZE   LTR   R0,R0               ROWS?
         BNZ   OKROWS
        XMESS  1006
         B     RTRNEND
OKROWS   ST    R0,SCROWS           SET IT
         ST    R0,SCROWS1          SET IT
         MH    R1,SCROWS+2
         ST    R1,SCREENSZ         SET IT
         XC    SCROWS2,SCROWS2
        STFSMODE ON,INITIAL=YES    TURN ON VTAM FULL-SCREEN MODE
         LTR   R15,R15             HOW COMPLETE?
         BZ    OKSTFS              WELL
        XMESS  1007,R15
         B     RTRNEND
OKSTFS  TPUT   ERSSCR,L'ERSSCR,FULLSCR
INITBUF  L     R0,=A(STBIGWA-MYSAVE) SET BIG WORK AREAS ADDRESSES
         AR    R0,R13
         ST    R0,BUFF
         AL    R0,=A(4*KB)
         ST    R0,REPLY
         AL    R0,=A(4*KB)
         ST    R0,TERMINPT
         AL    R0,=A(4*KB)
         ST    R0,TERMOUT
         AL    R0,=A(4*KB)
         LA    R1,DYNRCODE
         STM   R0,R1,DYNWORKP
         OI    DYNWORKP+4,VLB      SET "VL" BIT
         MVI   SPLIT,0             NOT IN SPLIT SCREEN
*- - - - "TERMOUT" DESCRIPTION : LINES ARE LOCATED IN "OUTPTBUF" AREA
BUFNOL   EQU   43             MAXIMUM NUMBER OF LINES
BUFOLL   EQU   93             ONE LINE LENGTH IS 93 BYTES
*                             (SO, TOTAL LINES LENGTH IS 3999 BYTES)
*                +0 = ADDRESS OF "WCCFLG"
*                +4 = ADDRESS OF OUPUT LINE 1
*                +8 = ADDRESS OF OUTPUT LINE 2
*               ...
*              +172 = ADDRESS OF OUTPUT LINE 43
*              NOTE - "EOS" (END-OF-SCREEN) MUST BE SET ON FIRST BYTE
*                     OF ADDRESS TO INDICATE THE LAST LINE OF THE
*                     SCREEN IMAGE
*              EACH OUTPUT LINE CONTAINS :
*                +0 = LENGTH OF THE LINE SCREEN IMAGE
*                +1 ... = THE LINE SCREEN IMAGE
         L     R15,TERMOUT         FIRST LINE ADDRESS
         LA    R14,WCCFLAG         "WCC" IS USED TO SET THE ALARM
         ST    R14,TERMOUT         NOW BUILD OUT-ADDRESSES
         LA    R14,TERMOUT+4
         LA    R1,BUFNOL           LOOP CONTROL
         ST    R15,0(R14)          STORE THE ADDRESS
         LA    R15,BUFOLL(R15)     BUMP TO NEXT OUTPUT LINE
         LA    R14,4(R14)          BUMP TO NEXT OUTADDR
         BCT   R1,*-12             LOOP 43 TIMES
         MVI   WCCFLAG,C'N'        NO ALARM REQUESTED
*- - - - "TERMINPT" DESCRIPTION : LINES ARE LOCATED IN "INPTBUF" AREA
BUFNIL   EQU   BUFNOL         SAME AS FOR "TERMOUT"
BUFILL   EQU   BUFOLL          " "
*                +0 = ADDRESS OF AID AND CURSOR POSITION (BYTES 0-2)
*                +4 = ADDRESS OF INPUT LINE 1
*                +8 = ADDRESS OF INPUT LINE 2
*               ...
*              +172 = ADDRESS OF INPUT LINE 43
*              EACH INPUT LINE CONTAINS AN UPDATED COPY OF THE
*              CORRESPONDING OUTPUT LINE SCREEN IMAGE IF THERE IS
*              AT LEAST ONE INPUT FIELD ENTERED :
*                +0 = LENGTH OF THE LINE SCREEN IMAGE OR ZERO IF NONE
*                     HAS BEEN ENTERED IN THIS LINE
*                +1 ... = THE UPDATED LINE SCREEN IMAGE
         LA    R14,TERMINPT+4      NOW BUILD IN-ADDRESSES
         LA    R1,BUFNIL           LOOP CONTROL
         L     R15,TERMINPT        FIRST ADDRESS
         LA    R15,BUFILL(R15)     BUMP TO NEXT INPUT LINE
         ST    R15,0(,R14)         STORE THE ADDRESS
         LA    R15,BUFILL(R15)     BUMP TO NEXT INPUT LINE
         LA    R14,4(R14)          BUMP TO NEXT INADDR
         BCT   R1,*-12             LOOP 43 TIMES
         MVC   LIB1DCB+(DCBDDNAM-IHADCB)(L'DCBDDNAM),=CL8'LIB1DD'
         MVC   LIB2DCB+(DCBDDNAM-IHADCB)(L'DCBDDNAM),=CL8'LIB2DD'
         MVC   MEM1DCB+(DCBDDNAM-IHADCB)(L'DCBDDNAM),=CL8'MEM1DD'
         MVC   MEM2DCB+(DCBDDNAM-IHADCB)(L'DCBDDNAM),=CL8'MEM2DD'
         MVC   COP1DCB+(DCBDDNAM-IHADCB)(L'DCBDDNAM),=CL8'COP1DD'
         MVC   COP2DCB+(DCBDDNAM-IHADCB)(L'DCBDDNAM),=CL8'COP2DD'
         LA    R0,LIB1DCB
         LA    R1,MEM1DCB
         LA    R2,COP1DCB
         LA    R3,LIB2DCB
         LA    R4,MEM2DCB
         LA    R5,COP2DCB
         DROP  R10
         LA    R10,SPLITWRK        SET DSECT BASE FOR SPLIT-SCREEN
         USING SPLTAREA,R10
         ST    R0,ALIBDCB
         ST    R1,AMEMDCB
         ST    R2,ACOPDCB
         MVC   N#IN,=CL8'MYIN$1'
         MVC   N#PRINT,=CL8'MYPRT$1'
         MVC   N#UT1,=CL8'MYUT1$1'
         MVC   N#UT2,=CL8'MYUT2$1'
         MVC   N#UT3,=CL8'MYUT3$1'
         MVC   N#UT4,=CL8'MYUT4$1'
         LR    R14,R13             SAVE THE SAVE-AREA ADDRESS
         AL    R13,=A(SPLIT2-MYSAVE) OFFSET OF SPLIT2 AREA
         LA    R10,SPLITWRK
         ST    R3,ALIBDCB
         ST    R4,AMEMDCB
         ST    R5,ACOPDCB
         MVC   N#IN,=CL8'MYIN$2'
         MVC   N#PRINT,=CL8'MYPRT$2'
         MVC   N#UT1,=CL8'MYUT1$2'
         MVC   N#UT2,=CL8'MYUT2$2'
         MVC   N#UT3,=CL8'MYUT3$2'
         MVC   N#UT4,=CL8'MYUT4$2'
         LR    R13,R14             RESTORE THE SAVE-AREA ADDRESS
         LA    R10,SPLITWRK
         MVI   PRIMEFLG,0          NOT IN PRIMARY OPTION MENU
         USING PSA,R0
         L     R2,CVTPTR           CVT
         DROP  R0
         USING CVTMAP,R2
         L     R1,CVTJESCT         JES COMM TABLE
         DROP  R2
         USING JESCT,R1
         ICM   R2,B'1111',JESSSCT  SSCT POINTER
         BZ    STLOGON             NONE
         USING SSCT,R2
SJ2LOOP  CLC   SSCTID,=CL4'SSCT'   RIGHT ID?
         BNE   STLOGON             NO, JUMP
         CLC   SSCTSNAM,=CL4'JES2' JES2 SSCT?
         BE    SJ2OK               YES
         ICM   R2,B'1111',SSCTSCTA NO, GET NEXT SSCT
         BNZ   SJ2LOOP             AND LOOP IF MORE
         B     STLOGON             ELSE JES2 NOT FOUND
         DROP  R1,R2
SJ2OK    MVC   SUBRB(SUBLEN),SUBRBSK INITIALIZE SUBMIT
         LA    R1,SUBTXP
         ST    R1,SUBRB+(S99TXTPP-S99RB)
         LA    R1,SUBRB
         ST    R1,SUBSUBP
         OI    SUBSUBP,S99RBPND    INITIALIZATION DONE
         SPACE 1
*------- LOGON PROCESSING -------------------------------------------*
*              (SIMULATE PROMPT FOR LOGON AND PASSWORD)
STLOGON  TM    STATSW,TESTFLG      TSO TEST OF LOGON PANEL?        <T>
         BO    DOLOGON             YES                             <T>
         CLI   TUBEDDNM,C'O'       OPERATOR TERMINAL?
         BE    OPRLOGON            YES, NO LOGON
         CLI   TUBEDDNM,C'T'       TSO TERMINAL?
         BE    TSOLOGON            YES, NO LOGON
DOLOGON  XC    MSGADD(4),MSGADD    NO MESSAGE AT UPPER-RIGHT
RELOGON  MVI   SPLIT,0             NOT IN SPLIT SCREEN
         MVI   PRIMEFLG,0          NOT IN PRIMARY OPTION MENU
         MVC   USERID,=CL8' '      CLEAR USER-ID
         MVC   USERPASS,=CL8' '    CLEAR PASSWORD
         MVC   RACGROUP,=CL8' '    CLEAR RACF GROUP-ID
         MVI   WCCFLAG,C'A'        ALARM REQUESTED
RSHWLOG  MVI   CURROW,CRLUSC       CURSOR ROW
         MVI   CURCOL,CCLUSC       CURSOR COLUMN
RLPRMPT  L     R15,=A(LOGHELP)     HELP SCREEN
         ST    R15,HELPADD
         LA    R5,SCRNLOG
         L     R6,=A(LOGSCR)
         BAL   R14,SCRNBLD
         L     R15,ARCOMM          GO COMMUNICATE
*- - - - ETPSCOMM PARM.LIST : NONE
        CALL   (15)
         MVI   WCCFLAG,C'N'        NO ALARM REQUESTED
         L     R2,TERMINPT         PICK UP AID ADDRESS
         LM    R4,R6,TERMINPT+ZIL  PICK UP RESPONSES LINES
         LTR   R15,R15             IS IT A BAD RETURN CODE?
         BNZ   OUTEX               YUP, GO EOJ
         TM    COMMSW,PFKFLAG      IS IT A PF KEY/ATTN?
         BZ    CHECKIF1            NOPE, IT'S JUST ENTER
         CLI   0(R2),X'F3'         IS IT PF3?
         BE    OUTEX               YUP, GO CLOSE THE TUBE AND EOJ
         CLI   0(R2),X'C3'         IS IT PF15?
         BE    OUTEX               YUP, GO CLOSE THE TUBE AND EOJ
         CLI   0(R2),X'6C'         IS IT PA1?
         BE    OUTEX               YUP, GO CLOSE THE TUBE AND EOJ
         CLI   0(R2),X'6D'         IS IT "CLEAR"?
         BE    RELOGON             YUP, RESHOW SCREEN (FIELDS CLEARED)
         CLI   0(R2),X'6E'         IS IT PA2?
         BE    RSHWLOG             YUP, RESHOW SCREEN AS BEFORE
CHECKIF1 CLI   0(R4),0             USER-ID ENTERED?
         BE    CHECKIF2            NO
         LA    R0,L'LGSCF1-1       YES, LEFT JUSTIFY FIELD IF NEEDED
         CLI   LGSCF1(R4),C' '
         BNE   *+L'*+14
         MVC   LGSCF1(L'LGSCF1-1,R4),LGSCF1+1(R4)
         MVI   LGSCF1+L'LGSCF1-1(R4),C' '
         BCT   R0,*-18
         MVC   USERID(L'USERTBL),LGSCF1(R4) MOVE USER-ID FIELD
CHECKIF2 CLI   0(R5),0             PASSWORD ENTERED?
         BE    CHECKIF3            NO
         LA    R0,L'LGSCF2-1       YES, LEFT JUSTIFY FIELD IF NEEDED
         CLI   LGSCF2(R5),C' '
         BNE   *+L'*+14
         MVC   LGSCF2(L'LGSCF2-1,R5),LGSCF2+1(R5)
         MVI   LGSCF2+L'LGSCF2-1(R5),C' '
         BCT   R0,*-18
         MVC   USERPASS(L'USERPASS),LGSCF2(R5) MOVE PASSWORD FIELD
CHECKIF3 CLI   0(R6),0             RACF GROUP ENTERED?
         BE    CHECKPFK            NO
         LA    R0,L'LGSCF3-1       YES, LEFT JUSTIFY FIELD IF NEEDED
         CLI   LGSCF3(R6),C' '
         BNE   *+L'*+14
         MVC   LGSCF3(L'LGSCF3-1,R6),LGSCF3+1(R6)
         MVI   LGSCF3+L'LGSCF3-1(R6),C' '
         BCT   R0,*-18
         MVC   RACGROUP(L'RACGROUP),LGSCF3(R6) MOVE GROUP-ID FIELD
CHECKPFK TM    COMMSW,PFKFLAG      WAS IT A PF KEY/ATTN?
         BZ    *+L'*+12            NO, SEE IF IT IS EOJ
         LA    R1,INVPFK           YES, IT'S AN INVALID KEY
         ST    R1,MSGADD
         B     RSHWLOG             RESHOW SCREEN
         CLI   USERID,C' '         USER-ID SUPPLIED?
         BNE   *+L'*+12            YES
         LA    R1,MNUSER           NO, IT'S MANDATORY, REPROMPT
         ST    R1,MSGADD
         B     RSHWLOG             RESHOW SCREEN
         CLC   USERID(3),=CL4'EOJ ' ALL DONE?
         BNE   CHECKUSR            NO
         MVC   USERID,=CL8' '      YES, CLEAR USER-ID
         MVC   USERPASS,=CL8' '    CLEAR PASSWORD
         MVC   RACGROUP,=CL8' '    CLEAR RACF GROUP-ID
OUTEX    TM    STATSW,TESTFLG      TSO TEST OF LOGON PANEL?        <T>
         BZ    CLOSETB             NO, GO CLOSE THE TUBE AND EOJ
         MVI   TUBEDDNM+L'TUBEDDNM-1,C' ' YES, RESET TEST LOGON    <T>
         NI    STATSW,255-TESTFLG  CLEAR REQUEST                   <T>
         B     TSOEND              TERMINATE TSO RUN               <T>
CHECKUSR TM    STATSW,TESTFLG      TSO TEST OF LOGON PANEL?        <T>
         BZ    SKMSTS              NO, SKIP MESSAGE'S TEST         <T>
         CLI   USERID,C'0'         YES, TEST MESSAGE NUMBER CHECK  <T>
         BL    SKMSTS                                              <T>
         CLI   USERID,C'9'                                         <T>
         BH    SKMSTS                                              <T>
         XR    R15,R15             GET THE MESSAGE NUMBER          <T>
         LR    R14,R15                                             <T>
         LA    R0,L'USERID                                         <T>
         LA    R1,X'0F'                                            <T>
         LA    R2,USERID                                           <T>
NXMSTS   IC    R14,0(R2)                                           <T>
         NR    R14,R1                                              <T>
         LTR   R15,R15                                             <T>
         BNP   *+L'*+4                                             <T>
         MH    R15,=H'10'                                          <T>
         AR    R15,R14                                             <T>
         CH    R15,=H'9999'                                        <T>
         BNH   *+L'*+12                                            <T>
         LH    R15,=H'9999'        ANY NUMBER OVER 9999 IS         <T>
         LA    R15,1(R15)            DISPLAYED AS 10000            <T>
         B     DOMSTS                                              <T>
         LA    R2,1(R2)                                            <T>
         BCT   R0,*+L'*+4                                          <T>
         B     DOMSTS                                              <T>
         CLI   0(R2),C' '                                          <T>
         BE    DOMSTS                                              <T>
         CLI   0(R2),C'0'                                          <T>
         BL    *+L'*+8                                             <T>
         CLI   0(R2),C'9'                                          <T>
         BNH   NXMSTS                                              <T>
         LA    R1,INVMSN           INVALID MESSAGE NUMBER          <T>
         ST    R1,MSGADD                                           <T>
         B     RSHWLOG             RESHOW SCREEN                   <T>
DOMSTS   STH   R15,MESSNO          AND NOW JUST TEST IT            <T>
         MVC   USERID,=CL8' '      CLEAR USER-ID                   <T>
        XMESS  ,                                                   <T>
         B     RELOGON             RESHOW SCREEN (FIELDS CLEARED)  <T>
SKMSTS   CLC   USERID(5),=CL5'DUMP ' TAKE A DUMP?
         BNE   *+L'*+2             NO, GO CHECK THE USER-ID
         DC    H'0'                YUP, TAKE AN S0C1 <------------ DUMP
         L     R1,=A(USERTBL)      VALID USERS TABLE
USTLOOP  CLI   0(R1),0             END OF TABLE?
         BNE   *+L'*+12            NO
         LA    R1,REJUSER          YES, INVALID USER
         ST    R1,MSGADD
         B     RSHWLOG             RESHOW SCREEN (REPROMPT)
         CLC   USERID(L'USERTBL),0(R1) AUTHORIZED USER-ID?
         BE    *+L'*+8             YUP, OK
         LA    R1,L'USERTBL(R1)    BUMP TO NEXT USER IN TABLE
         B     USTLOOP             LOOP
*- - - - - - - NOTE : THE PASSWORD IS HANDLED AND USED AS SHIPPED, BUT
*                     IF YOU NEED TO CHECK IT, DO IT HERE BELOW.
*                     I.E., YOU MAY DO AS FOLLOWS :
*                     SCAN "USERPASS" AGAINST YOUR PASSWORDS-TABLE
*                     IF OK, CONTINUE
*                     ELSE, SET MSG "PASSWORD REJECTED"
*                           SET CURSOR ROW TO "CRLPSC"
*                           SET CURSOR COLUMN TO "CCLPSC"
*                           AND GO BACK TO "RLPRMPT"
         CLI   RACGROUP,C' '       RACF GROUP-ID ENTERED?
         BE    CHKWHERE            NO
         CLI   RACGROUP,C'A'       YES, TEST IF IT STARTS BY ALPHA
         BL    *+L'*+8
         CLI   RACGROUP,C'Z'
         BNH   CHKWHERE            OK
         LA    R1,INVGROUP         NO, INVALID RACF-GROUP
         ST    R1,MSGADD
         MVI   CURROW,CRLGSC       CURSOR ROW
         MVI   CURCOL,CCLGSC       CURSOR COLUMN
         B     RLPRMPT             RESHOW SCREEN (REPROMPT)
CHKWHERE TM    STATSW,TESTFLG      TSO TEST OF LOGON PANEL?        <T>
         BZ    VERUSGRP            NO
         MVI   TUBEDDNM+L'TUBEDDNM-1,C' ' YES, RESET TEST LOGON    <T>
         NI    STATSW,255-TESTFLG  CLEAR REQUEST                   <T>
         B     TSOLOGON                                            <T>
OPRLOGON MVC   USERID(L'USERTBL),USERTBL SET USER-ID = FIRST IN TABLE
         MVI   USERID+L'USERTBL,C' '
         MVC   USERPASS,MASTPASS   PASSWORD IS MASTER PASSWORD
         B     SETRGRP
         USING PSA,R0
TSOLOGON L     R1,PSATOLD          PICK UP TCB
         DROP  R0
         USING TCB,R1
         L     R1,TCBTIO           TCB TIOT
         DROP  R1
         USING TIOT,R1
         MVC   USERID(L'TIOCNJOB),TIOCNJOB SET USER-ID = JOBNAME
         DROP  R1
         MVC   USERPASS,=CL8' '    CLEAR PASSWORD
         USING PSA,R0
         L     R1,PSAAOLD          PICK UP ASCB
         DROP  R0
         USING ASCB,R1
         L     R2,ASCBTSB          PICK UP TSB
         DROP  R1
         LTR   R2,R2
         BNZ   OKTSB
        XMESS  1008
         B     RTRNEND
OKTSB   MODESET MF=(E,SUPRMOD)
         USING TSB,R2
         MVC   TERMSNME,TSBTRMID   TERMINAL SYMBOLIC NAME
         DROP  R2
        MODESET MF=(E,PROBMOD)
SETRGRP  MVC   RACGROUP,=CL8' '    CLEAR RACF GROUP-ID
*- - - - VERIFY USER AND VERIFY OR SET RACF GROUP
VERUSGRP RACSTAT ,                 IS RACF ACTIVE?
         LTR   R15,R15             ACTIVE?
         BNZ   STNORACF            NO
         USING PSA,R0
         L     R15,PSAAOLD         PICK UP ASCB
         DROP  R0
         USING ASCB,R15
         L     R14,ASCBASXB        PICK UP ASXB
         DROP  R15
         USING ASXB,R14
         L     R3,ASXBSENV         PICK UP ACEE
         DROP  R14
         LA    R3,0(,R3)
         LTR   R3,R3               IS THERE AN ACEE?
         BZ    STNORACF            NO
         USING ACEE,R3
         CLC   ACEEACEE,=CL4'ACEE' IS ACEE VALID?
         BNE   STNORACF            NO
         TM    ACEEFLG1,ACEERACF   IS USER RACF DEFINED?
         BZ    STNORACF            NO
         CLC   USERID,ACEEUSRI     IS USER OK?
         BE    OKUSER              YES
        XMESS  1009      (THIS WAY EXECUTED ONLY WHEN NOT OPER NOR TSO)
         B     CLOSETB
OKUSER   TM    ACEEFLG1,ACEESPEC   SPECIAL ATTRIBUTE?
         BZ    *+L'*+4             NO
         OI    STATSW,SPCLFLG      YES, RETAIN IT
         CLC   RACGROUP,=CL8' '    GROUP-ID?
         BNE   *+L'*+10            YES, GO CHECK IT
         MVC   RACGROUP,ACEEGRPN   NONE, SET IT
         B     ENDLOGON
         CLC   RACGROUP,ACEEGRPN   IS GROUP-ID OK?
         BE    ENDLOGON            YES
*                                  NO, TRY TO SWITCH IT (THIS WAY IS
*                                  EXECUTED ONLY WHEN NOT OPER NOR TSO)
         MVC   VUSWA(RINITSKL),RINITSK
        MODESET MF=(E,SUPRMOD)
        RACINIT GROUP=RACGROUP,ENVIR=CHANGE,MF=(E,VUSWA)
         LR    R2,R15              SAVE RETURN CODE
        MODESET MF=(E,PROBMOD)
         LTR   R15,R2              SUCCESSFUL?
         BZ    OKRACI              YES
        XMESS  1010,R15  (THIS WAY EXECUTED ONLY WHEN NOT OPER NOR TSO)
         B     CLOSETB
         USING PSA,R0              GO PROPAGATE ADSP TO GET AROUND
OKRACI   L     R1,PSATOLD            IBM BUG / PICK UP TCB
         DROP  R0
         USING TCB,R1
         L     R2,TCBJSCB          GET JSCB ADDRESS
         DROP  R1
         USING IEZJSCB,R2
        MODESET MF=(E,SUPRMOD)
         NI    JSCBFBYT,X'FF'-JSCBADSP  TURN OFF ADSP IN JSCB
         TM    ACEEFLG1,ACEEADSP   DOES USER NOW HAVE ADSP?
         BZ    *+L'*+4             NO, LEAVE JSCBADSP OFF
         OI    JSCBFBYT,JSCBADSP   YES, TURN JSCBADSP ON
        MODESET MF=(E,PROBMOD)
         DROP  R2,R3
         XC    VUSWA(RINITSKL),VUSWA CLEAR WORK-AREA
         B     ENDLOGON
STNORACF MVC   RACGROUP,=CL8'(-NONE-)' SIGNAL IT
         L     R1,=A(USERTBL)      VALID USERS TABLE
USSLOOP  CLI   0(R1),0             END OF TABLE?
         BE    ENDLOGON            YES
         CLC   USERID(L'USERTBL),0(R1) AUTHORIZED USER-ID?
         BE    *+L'*+8             YUP
         LA    R1,L'USERTBL(R1)    BUMP TO NEXT USER IN TABLE
         B     USSLOOP             LOOP
         OI    STATSW,SPCLFLG      GIVE HIM AUTHORITY
ENDLOGON TM    STATSW,INITFLG      SPLIT-SCREEN REGISTERS SET?
         BO    *+L'*+8             YUP
         STM   R10,R13,PRISPLIT    SAVE REGISTERS
         OI    STATSW,INITFLG      SAY DONE
         EJECT
*------- MAINLINE - DISPLAY PRIMARY OPTION MENU ---------------------*
         CNOP  0,4
ETPSPOPS OI    LIBPASS,C' '
         CLI   LIBPASS,C' '        IS THERE A PASSWORD SPECIFIED?
         BNE   PRIOPT              YES
         MVC   LIBPASS,USERPASS    SET DSN PASSWORD TO LOGON PASSWORD
PRIOPT   CLI   TUBEDDNM,C'O'       OPERATOR TERMINAL?
         BE    *+L'*+8             YES
         CLI   TUBEDDNM,C'T'       TSO TERMINAL?
         BNE   *+L'*+8             NO
         L     R15,=A(POPHELPA)    HELP SCREEN
         B     *+L'*+4
         L     R15,=A(POPHELP)     HELP SCREEN
         ST    R15,HELPADD
         MVI   PRIMEFLG,1          ON SCREEN 1
         LA    R5,SCRNPRM
         L     R6,=A(PRIMOPT)
         BAL   R14,SCRNBLD
         MVI   CURROW,CRPOSC       CURSOR ROW
         MVI   CURCOL,CCPOSC       CURSOR COLUMN
         L     R15,ARCOMM          GO COMMUNICATE
*- - - - ETPSCOMM PARM.LIST : NONE
        CALL   (15)
         LTR   R15,R15             HOW COMPLETE?
         BNZ   PRIEND              YUP, GET OUT
         MVI   PRIMEFLG,0          ON SCREEN 1
         L     R2,TERMINPT         PICK UP AID ADDRESS
         L     R4,TERMINPT+ZIP     PICK UP OPTION FIELD LINE
         CLI   SPLIT,2             AM I ON SCREEN 2?
         BNE   PRIBLD              NOPE
         L     R3,SCROWS1          PICK UP SIZE OF SCREEN 1
         SLL   R3,2                MULTIPLY BY 4
         L     R4,TERMINPT+ZIP(R3) PICK UP OPTION FIELD LINE
PRIBLD   TM    COMMSW,PFKFLAG      IS IT A PF KEY/ATTN?
         BZ    PRICHK              NO
         CLI   0(R2),X'F3'         IS IT PF3?
         BE    PRIEND              YUP, GO RESHOW THE LOGON PANEL
         CLI   0(R2),X'C3'         IS IT PF15?
         BE    PRIEND              YUP, GO RESHOW THE LOGON PANEL
         CLI   0(R2),X'6C'         IS IT PA1?
         BE    PRIEND              YUP, GO RESHOW THE LOGON PANEL
         CLI   0(R2),X'6D'         IS IT "CLEAR"?
         BE    PRIOPT              YUP, GO RESHOW THE PRIMOPT
         CLI   0(R2),X'6E'         IS IT PA2?
         BE    PRIOPT              YUP, GO RESHOW THE PRIMOPT
         LA    R1,INVPFK
         ST    R1,MSGADD
         B     PRIOPT              RESHOW SCREEN
PRIEND   CLI   TUBEDDNM,C'T'       TSO TERMINAL?
         BE    TSOEND              YES
         CLI   TUBEDDNM,C'O'       OPERATOR TERMINAL?
         BNE   RELOGON             NO
         B     CLOSETB             YES, NO LOGON, GO EOJ
PRICHK   CLI   0(R4),0             OPTION ENTERED?
         BE    PRIOPT              NO, GO RESHOW PRIMARY OPTION MENU
         LA    R0,L'POSCF1-1       LEFT JUSTIFY FIELD IF NEEDED
         CLI   POSCF1(R4),C' '
         BNE   *+L'*+18
         MVC   POSCF1(L'POSCF1-1,R4),POSCF1+1(R4)
         MVI   POSCF1+L'POSCF1-1(R4),C' '
         BCT   R0,*-18
         B     PRIOPT              GO RESHOW THE PRIMARY OPTION MENU
         CLI   POSCF1+1(R4),C' '   VALID?
         BNE   PRINVS              NO, INVALID SELECTION
         CLI   POSCF1(R4),C'X'     OK, IS IT END?
         BE    PRIEND              YUP, GO RESHOW THE LOGON PANEL
         SPACE 1
*- - - - DETERMINE PRIMARY OPTION PROCESSOR
         CLI   POSCF1(R4),C'1'     BROWSE?
         BE    BRWOPT              GO DO IT
         CLI   POSCF1(R4),C'2'     EDIT?
         BE    EDTOPT              GO DO IT
         CLI   POSCF1(R4),C'3'     UTILITIES?
         BE    UTLOPT              GO DO IT
         CLI   POSCF1(R4),C'4'     IDCAMS INTERFACE?
         BE    IDOPT               GO DO IT
         CLI   POSCF1(R4),C'5'     ZAP?
         BE    ZAOPT               GO DO IT
         CLI   POSCF1(R4),C'6'     INCORZAP?
         BE    IZOPT               GO DO IT
         CLC   POSCF1(5,R4),=CL5'DUMP ' TAKE A DUMP?
         BNE   *+L'*+2             NO
         DC    H'0'                YUP, TAKE AN S0C1 <------------ DUMP
PRINVS   LA    R1,INVOPT
         ST    R1,MSGADD
         B     PRIOPT              GO RESHOW THE PRIMARY OPTION MENU
         SPACE 1
*- - - - BROWSE PROCESSING
BRWOPT   MVI   LIBFUNC,C'B'        INDICATE BROWSE FUNCTION
REBROPT  L     R15,=A(BROHELP)     HELP SCREEN
         ST    R15,HELPADD
         LA    R5,SCRNBES
         L     R6,=A(BROWSCR)
         BAL   R14,SCRNBLD
         MVI   CURROW,CRBRSC       CURSOR ROW
         MVI   CURCOL,CCBRSC       CURSOR COLUMN
         L     R15,ARCOMM          GO COMMUNICATE
*- - - - ETPSCOMM PARM.LIST : NONE
        CALL   (15)
         LTR   R15,R15             BAD RC?
         BNZ   PRIOPT              YUP
         TM    COMMSW,PFKFLAG      IS IT A PF KEY/ATTN?
         BZ    EDCKINPT            NOPE, GO BROWSE THE DATA-SET
         L     R2,TERMINPT         PICK UP AID ADDRESS
         CLI   0(R2),X'F3'         IS IT PF3?
         BE    PRIOPT              YUP, I'M DONE
         CLI   0(R2),X'C3'         IS IT PF15?
         BE    PRIOPT              YUP, I'M DONE
         CLI   0(R2),X'6C'         IS IT PA1?
         BE    PRIOPT              YUP, I'M DONE
         CLI   0(R2),X'6D'         IS IT "CLEAR"?
         BE    REBROPT             YUP, RESHOW SCREEN
         CLI   0(R2),X'6E'         IS IT PA2?
         BE    REBROPT             YUP, RESHOW SCREEN
         LA    R1,INVPFK
         ST    R1,MSGADD
         B     REBROPT             RESHOW SCREEN
         SPACE 1
*- - - - EDIT PROCESSING
EDTOPT   MVI   LIBFUNC,C'E'        INDICATE EDIT FUNCTION
REEDOPT  L     R15,=A(EDTHELP)     HELP SCREEN
         ST    R15,HELPADD
         LA    R5,SCRNBES
         L     R6,=A(EDITSCR)
         BAL   R14,SCRNBLD
         MVI   CURROW,CREDSC       CURSOR ROW
         MVI   CURCOL,CCEDSC       CURSOR COLUMN
         L     R15,ARCOMM          GO COMMUNICATE
*- - - - ETPSCOMM PARM.LIST : NONE
        CALL   (15)
         LTR   R15,R15             BAD RC?
         BNZ   PRIOPT              YUP, GO BACK
         TM    COMMSW,PFKFLAG      IS IT A PF KEY/ATTN?
         BZ    EDCKINPT            NOPE, GO EDIT THE DATA-SET
         L     R2,TERMINPT         PICK UP AID ADDRESS
         CLI   0(R2),X'F3'         IS IT PF3?
         BE    PRIOPT              YUP, I'M DONE
         CLI   0(R2),X'C3'         IS IT PF15?
         BE    PRIOPT              YUP, I'M DONE
         CLI   0(R2),X'6C'         IS IT PA1?
         BE    PRIOPT              YUP, I'M DONE
         CLI   0(R2),X'6D'         IS IT "CLEAR"?
         BE    REEDOPT             YUP, RESHOW SCREEN
         CLI   0(R2),X'6E'         IS IT PA2?
         BE    REEDOPT             YUP, RESHOW SCREEN
         LA    R1,INVPFK
         ST    R1,MSGADD
         B     REEDOPT             RESHOW SCREEN
EDCKINPT L     R5,TERMINPT+ZIB1    PICK UP DS-NAME FIELD
         L     R6,TERMINPT+ZIB2    PICK UP VOL-SER FIELD
         L     R7,TERMINPT+ZIB3    PICK UP PASSWORD FIELD
         CLI   SPLIT,2             AM I ON SCREEN 2?
         BNE   EDCKINPC            NOPE
         L     R3,SCROWS1          PICK UP SIZE OF SCREEN 1
         SLL   R3,2                MULTIPLY BY 4
         L     R5,TERMINPT+ZIB1(R3) PICK UP DS-NAME FIELD
         L     R6,TERMINPT+ZIB2(R3) PICK UP VOL-SER FIELD
         L     R7,TERMINPT+ZIB3(R3) PICK UP PASSWORD FIELD
EDCKINPC CLI   0(R5),0             DS-NAME ENTERED (OR CHANGED)?
         BNE   *+L'*+16            YES
         OI    LIBDSNM,C' '
         CLI   LIBDSNM,C' '        DS-NAME ALREADY IN?
         BNE   CHKVLSR             YES
         B     REEDSCN             ELSE RETRY THE SCREEN
         LA    R0,L'EDSCF1-1       LEFT JUSTIFY FIELD IF NEEDED
         CLI   EDSCF1(R5),C' '
         BNE   EDDSNMV
         MVC   EDSCF1(L'EDSCF1-1,R5),EDSCF1+1(R5)
         MVI   EDSCF1+L'EDSCF1-1(R5),C' '
         BCT   R0,*-18
         MVI   LIBDSNM,C' '        RESET DS-NAME FIELD
         MVC   LIBDSNM+1(L'LIBDSNM-1),LIBDSNM
         B     REEDSCN             RETRY THE SCREEN
EDDSNMV  CLC   EDSCF1(4,R5),=CL4'END ' END OF BROWSE/EDIT?
         BE    PRIOPT              YES, GO BACK
         MVI   LIBDSNM,C' '        RESET DS-NAME FIELD
         MVC   LIBDSNM+1(L'LIBDSNM-1),LIBDSNM
         LA    R0,L'LIBDSNM        MAX. LENGTH FOR A DS-NAME
         LA    R14,EDSCF1(R5)      START ADDRESS FOR DSN LENGTH CALC
         XR    R15,R15             SET COUNTER
EDDSNLOP CLI   0(R14),C'('         LEFT PAREN INDICATES MEMBER?
         BE    EDDSNEND            GOT END, IGNORE MEMBER
         CLI   0(R14),C' '         BLANK IS END OF DSN?
         BE    EDDSNEND            GOT END
         LA    R14,1(R14)          BUMP ADDRESS
         LA    R15,1(R15)          BUMP COUNTER
         BCT   R0,EDDSNLOP         TEST NEXT BYTE
         LA    R1,BADEDDSN
         B     EDSTMSG
EDMOVDSN MVC   LIBDSNM(*-*),EDSCF1(R5) <<EXECUTED>>
EDDSNEND BCTR  R15,0               SUBTRACT 1
         EX    R15,EDMOVDSN
CHKVLSR  MVC   LIBVOL(L'LIBVOL),=CL8' '
         CLI   0(R6),0             VOL-SER ENTERED?
         BE    CHKPSWD             NO
         LA    R0,L'EDSCF2-1       LEFT JUSTIFY FIELD IF NEEDED
         CLI   EDSCF2(R6),C' '
         BNE   *+L'*+14
         MVC   EDSCF2(L'EDSCF2-1,R6),EDSCF2+1(R6)
         MVI   EDSCF2+L'EDSCF2-1(R6),C' '
         BCT   R0,*-18
         MVC   LIBVOL(L'LIBVOL),EDSCF2(R6) MOVE IN THE VOL-SER
CHKPSWD  CLI   0(R7),0             PASSWORD ENTERED?
         BE    EDSETVOL            NO
         LA    R0,L'EDSCF3-1       LEFT JUSTIFY FIELD IF NEEDED
         CLI   EDSCF3(R7),C' '
         BNE   *+L'*+14
         MVC   EDSCF3(L'EDSCF3-1,R7),EDSCF3+1(R7)
         MVI   EDSCF3+L'EDSCF3-1(R7),C' '
         BCT   R0,*-18
         MVC   LIBPASS(L'LIBPASS),EDSCF3(R7) MOVE IN THE PASSWORD
EDSETVOL BAL   R14,CTVTRTN
         B     EDSTMSG             ERROR RETURN
         USING DS1FMTID,R1
         MVC   LIBORG,=CL3'SEQ'    SET DEFAULT DSORG
         CLC   DS1DSORG(L'DS1DSORG),=AL1(DS1DSGPS,0) IS IT SEQUENTIAL?
         BE    EDTSTLRC            YUP
         MVC   LIBORG,=CL3'PDS'    SET DEFAULT DSORG
         CLC   DS1DSORG(L'DS1DSORG),=AL1(DS1DSGPO,0) IS IT PO?
         BE    EDTSTLRC            YUP
         LA    R1,BADEDORG         ELSE NO GOOD, SET MESSAGE
         B     EDSTMSG
EDTSTLRC CLI   LIBFUNC,C'E'        EDIT FUNCTION REQUESTED?
         BNE   EDFMTDIR            NO, SKIP LRECL CHECK
         CLC   DS1LRECL(L'DS1LRECL),=H'80' IS LRECL=80?
         BE    EDFMTDIR            YUP, OK
         DROP  R1
         LA    R1,BADED80          ELSE NO GOOD, SET MESSAGE
EDSTMSG  ST    R1,MSGADD
         B     REEDSCN             RETRY THE SCREEN
EDFMTDIR L     R15,ARLIBM          GO TO LIBRARY MANAGEMENT
*- - - - ETPSLIBM PARM.LIST : DS-NAME, VOL-SER, DS-ORG, PASSWORD
        CALL   (15),(LIBDSNM,LIBVOL,LIBORG,LIBPASS),VL,MF=(E,PARMVL)
REEDSCN  CLI   LIBFUNC,C'B'        BROWSE FUNCTION REQUESTED?
         BE    REBROPT             YES, RESHOW THE SCREEN
         B     REEDOPT             ELSE EDIT, RESHOW THE SCREEN
         SPACE 1
*- - - - LIBRARY UTILITY PROCESSING
UTLOPT   MVI   LIBFUNC,C'U'        INDICATE LIBRARY UTILITY FUNCTION
REUTOPT  L     R15,=A(UTLHELP)     HELP SCREEN
         ST    R15,HELPADD
         LA    R5,SCRNUTL
         L     R6,=A(UTLMENU)
         BAL   R14,SCRNBLD
         CLI   LIBDSNM,C' '        IS THERE A DATA-SET NAME SPECIFIED?
         BNE   *+L'*+12            BRANCH IF YES
         MVI   CURROW,CRUTSN       CURSOR ROW
         MVI   CURCOL,CCUTSN       CURSOR COLUMN
         B     *+L'*+8
         MVI   CURROW,CRUTSC       CURSOR ROW
         MVI   CURCOL,CCUTSC       CURSOR COLUMN
         L     R15,ARCOMM          GO COMMUNICATE
*- - - - ETPSCOMM PARM.LIST : NONE
        CALL   (15)
         LTR   R15,R15             BAD RC?
         BNZ   PRIOPT              OUT
         TM    COMMSW,PFKFLAG      IS IT A PF KEY/ATTN?
         BZ    UTLCMD              NONE
         L     R2,TERMINPT         PICK UP AID ADDRESS
         CLI   0(R2),X'F3'         IS IT PF3?
         BE    PRIOPT              YUP, I'M DONE
         CLI   0(R2),X'C3'         IS IT PF15?
         BE    PRIOPT              YUP, I'M DONE
         CLI   0(R2),X'6C'         IS IT PA1?
         BE    PRIOPT              YUP, I'M DONE
         CLI   0(R2),X'6D'         IS IT "CLEAR"?
         BE    REUTOPT             YUP
         CLI   0(R2),X'6E'         IS IT PA2?
         BE    REUTOPT             YUP
         LA    R1,INVPFK
         ST    R1,MSGADD
         B     REUTOPT
UTLCMD   L     R3,TERMINPT+ZIUE    PICK UP COMMAND INPUT LINE
         L     R5,TERMINPT+ZIU1    PICK UP DS-NAME FIELD
         L     R6,TERMINPT+ZIU2    PICK UP VOL-SER FIELD
         L     R7,TERMINPT+ZIU3    PICK UP PASSWORD FIELD
         CLI   SPLIT,2             AM I ON SCREEN 2?
         BNE   UTLSEL              NOPE
         L     R3,SCROWS1          PICK UP SIZE OF SCREEN 1
         SLL   R3,2                MULTIPLY BY 4
         L     R5,TERMINPT+ZIU1(R3) PICK UP DS-NAME FIELD
         L     R6,TERMINPT+ZIU2(R3) PICK UP VOL-SER FIELD
         L     R7,TERMINPT+ZIU3(R3) PICK UP PASSWORD FIELD
         L     R3,TERMINPT+ZIUE(R3) PICK UP COMMAND INPUT LINE
UTLSEL   CLI   0(R3),0             COMMAND ENTERED?
         BE    UTCHKDSN            NO
         LA    R0,L'UTSCF1-1       LEFT JUSTIFY FIELD IF NEEDED
         CLI   UTSCF1(R3),C' '
         BNE   *+L'*+14
         MVC   UTSCF1(L'UTSCF1-1,R3),UTSCF1+1(R3)
         MVI   UTSCF1+L'UTSCF1-1(R3),C' '
         BCT   R0,*-18
         CLC   UTSCF1(4,R3),=CL4'END ' END OF THIS SCREEN?
         BE    PRIOPT              YES, GO BACK
UTCHKDSN CLI   0(R5),0             DS-NAME ENTERED (OR CHANGED)?
         BNE   *+L'*+16            YES
         OI    LIBDSNM,C' '
         CLI   LIBDSNM,C' '        DS-NAME ALREADY IN?
         BNE   UTCHKVOL            YES
         B     REUTOPT             ELSE RESHOW THE SCREEN
         LA    R0,L'UTSCF2-1       LEFT JUSTIFY FIELD IF NEEDED
         CLI   UTSCF2(R5),C' '
         BNE   UTDSNMV
         MVC   UTSCF2(L'UTSCF2-1,R5),UTSCF2+1(R5)
         MVI   UTSCF2+L'UTSCF2-1(R5),C' '
         BCT   R0,*-18
         MVC   LIBDSNM(L'LIBDSNM),UTSCF2(R5)
         B     REUTOPT             RESHOW THE SCREEN
UTDSNMV  MVI   LIBDSNM,C' '        RESET DS-NAME FIELD
         MVC   LIBDSNM+1(L'LIBDSNM-1),LIBDSNM
         MVC   LIBORG,=CL3'PDS'    SET IT PDS, BY DEFAULT
         LA    R0,L'LIBDSNM        MAX. LENGTH FOR A DS-NAME
         LA    R14,UTSCF2(R5)      START ADDRESS FOR DSN LENGTH CALC
         XR    R15,R15             SET COUNTER
UTDSNLOP CLI   0(R14),C' '         BLANK IS END OF DSN?
         BE    UTDSNEND            GOT END
         CLI   0(R14),C'('         LEFT PAREN?
         BE    UTDSNEND            GOT END
         LA    R14,1(R14)          BUMP ADDRESS
         LA    R15,1(R15)          BUMP COUNTER
         BCT   R0,UTDSNLOP         TEST NEXT BYTE
         LA    R1,UTBADDSN
         B     UTSTMSG
UTDSNEND BCT   R15,*+L'*+6         SUBTRACT 1
         MVC   LIBDSNM(*-*),UTSCF2(R5) <<EXECUTED>>
         EX    R15,*-6
UTCHKVOL MVC   LIBVOL(L'LIBVOL),=CL8' '
         CLI   0(R6),0             VOL-SER ENTERED?
         BE    UTCHKPSW            NO
         LA    R0,L'UTSCF3-1       LEFT JUSTIFY FIELD IF NEEDED
         CLI   UTSCF3(R6),C' '
         BNE   *+L'*+14
         MVC   UTSCF3(L'UTSCF3-1,R6),UTSCF3+1(R6)
         MVI   UTSCF3+L'UTSCF3-1(R6),C' '
         BCT   R0,*-18
         MVC   LIBVOL(L'LIBVOL),UTSCF3(R6) MOVE IN THE VOL-SER
UTCHKPSW CLI   0(R7),0             PASSWORD ENTERED?
         BE    UTCHKCMD            NO
         LA    R0,L'UTSCF4-1       LEFT JUSTIFY FIELD IF NEEDED
         CLI   UTSCF4(R7),C' '
         BNE   *+L'*+14
         MVC   UTSCF4(L'UTSCF4-1,R7),UTSCF4+1(R7)
         MVI   UTSCF4+L'UTSCF4-1(R7),C' '
         BCT   R0,*-18
         MVC   LIBPASS(L'LIBPASS),UTSCF4(R7) MOVE IN THE PASSWORD
UTCHKCMD CLI   0(R3),0             IF NO COMMAND ENTERED
         BE    UTLLIST             GO LIST THE LIBRARY
         CLI   UTSCF1(R3),C' '     IS IT PDS MEMBERS LIST?
         BE    UTLLIST             YUP
         CLI   UTSCF1(R3),C'C'     IS IT PDS COMPRESS?
         BE    UTLCPRS             YUP
         CLI   UTSCF1(R3),C'S'     IS IT DS STATISTICS?
         BE    UTLSTAT             YUP
         LA    R1,INVOPT
         B     UTSTMSG
UTLCPRS  ICM   R15,B'1111',ARCOMP
         BNZ   CALLCM
        LOAD   EP=ETPSCOMP,ERRET=LDCMERR
         ST    R0,ARCOMP
         LR    R15,R0
*- - - - ETPSCOMP PARM.LIST : DS-NAME, VOL-SER, PASSWORD
CALLCM  CALL   (15),(LIBDSNM,LIBVOL,LIBPASS),VL,MF=(E,PARMVL)
         B     REUTOPT
LDCMERR  STM   R0,R1,MESSPL        COMPRESS IS UNAVAILABLE
        XMESS  99,R15
         LA    R1,UTLLDCME
         B     UTSTMSG
UTLSTAT  ICM   R15,B'1111',ARSTAT
         BNZ   CALLST
        LOAD   EP=ETPSDSST,ERRET=LDSMERR
         ST    R0,ARSTAT
         LR    R15,R0
*- - - - ETPSDSST PARM.LIST : DS-NAME, VOL-SER, PASSWORD, USER-ID
CALLST  CALL   (15),(LIBDSNM,LIBVOL,LIBPASS,USERID),VL,MF=(E,PARMVL)
         B     REUTOPT
LDSMERR  STM   R0,R1,MESSPL        STATDS IS UNAVAILABLE
        XMESS  99,R15
         LA    R1,UTLLDSME
         B     UTSTMSG
UTLLIST  BAL   R14,CTVTRTN
         B     UTSTMSG             ERROR RETURN
         USING DS1FMTID,R1
         CLC   DS1DSORG(L'DS1DSORG),=AL1(DS1DSGPO,0) IS IT PO?
         BNE   UTLORBAD            NOPE
         CLC   DS1LRECL(L'DS1LRECL),=H'80' IS LRECL=80?
         BNE   UTLLRBAD            IF NO GOOD, RETRY THE SCREEN
         DROP  R1
         L     R15,ARLIBM          GO TO LIBRARY MANAGEMENT
*- - - - ETPSLIBM PARM.LIST : DS-NAME, VOL-SER, DS-ORG, PASSWORD
        CALL   (15),(LIBDSNM,LIBVOL,LIBORG,LIBPASS),VL,MF=(E,PARMVL)
         B     REUTOPT             RESHOW THE SCREEN
UTLORBAD LA    R1,UTBADORG
         B     UTSTMSG
UTLLRBAD LA    R1,UTBADLRE
UTSTMSG  ST    R1,MSGADD
         B     REUTOPT
         SPACE 1
*- - - - CATALOG/VTOC INFO ROUTINE
*              R14 = LINK REGISTER / RETURN +0 = ERROR (R1 = A.MSG)
*                                           +4 = OK (R1 = A.VTOC-INFO)
CTVTRTN  ST    R14,SVRTR14         SAVE RETURN
         OI    LIBVOL,C' '
         CLI   LIBVOL,C' '         WAS VOL-SER PROVIDED?
         BNE   CTVTRTN1            YUP
         MVC   VCMLST(CTCMLSL),CTCMLST CLEAR CAMLST AREA
         XC    VUSWA(L'VUSWA),VUSWA
         XC    VUSWA+L'VUSWA(16),VUSWA+L'VUSWA
         LA    R15,LIBDSNM         SET DSN
         XR    R0,R0
         LA    R1,VUSWA            SET OUTPUT AREA
         STM   R15,R1,VCMLST+4
        LOCATE VCMLST
         LTR   R15,R15             CHECK RETURN CODE
         BZ    *+L'*+8             OK
         LA    R1,BADCTLOC         ELSE SET MESSAGE
         B     CTVTRTNX
         MVC   LIBVOL(L'LIBVOL),VUSWA+6 SET THE VOL-SER
         OI    PRCSSW,VSRFC        SAY RETRIEVED FROM CATALOG
CTVTRTN1 MVC   VCMLST(VTCMLSL),VTCMLST CLEAR CAMLST AREA
         XC    VUSWA(DSCBLGTH),VUSWA
         LA    R15,LIBDSNM         SET DSN
         LA    R0,LIBVOL           SET VOL-SER
         LA    R1,VUSWA            SET OUTPUT AREA ADDRESS
         STM   R15,R1,VCMLST+4
        OBTAIN VCMLST
         LTR   R15,R15             CHECK RETURN CODE
         BZ    *+L'*+8             OK
         LA    R1,BADVTOBT         ELSE SET MESSAGE
         B     CTVTRTNX
         LA    R1,VUSWA            PASS BACK VTOC-INFO ADDRESS
CTVTRTNX L     R14,SVRTR14         RETURN
         LTR   R15,R15             TEST RETURN CODE
         BZ    4(R14)              OK
         BR    R14                 ERROR
         SPACE 1
*- - - - IDCAMS FOREGROUND PROCESSING
IDOPT    ICM   R15,B'1111',ARIDCA
         BNZ   CALLID
        LOAD   EP=ETPSIDCA,ERRET=LDIDERR
         ST    R0,ARIDCA
         LR    R15,R0
*- - - - ETPSIDCA PARM.LIST : NONE
CALLID  CALL   (15)
         B     PRIOPT
LDIDERR  STM   R0,R1,MESSPL        IDCAMS IS UNAVAILABLE
        XMESS  99,R15
         LA    R1,UTLLDIDE
         ST    R1,MSGADD
         B     PRIOPT
         SPACE 1
*- - - - ZAP (DISPLAY/MODIFY DATA-SET)
ZAOPT    ICM   R15,B'1111',ARZAP
         BNZ   ZACALL
        LOAD   EP=ETPSZAP,ERRET=LDZAERR
         ST    R0,ARZAP
         LR    R15,R0
*- - - - ETPSZAP PARM.LIST : NONE
ZACALL  CALL   (15)                GO TO ZAP SELECT DATA-SET PROCESS
         B     PRIOPT              THEN LOOK FOR ANOTHER SELECTION
LDZAERR  STM   R0,R1,MESSPL        ZAP IS UNAVAILABLE
        XMESS  99,R15
         LA    R1,ZAUMSG
         ST    R1,MSGADD
         B     PRIOPT
         SPACE 1
*- - - - INCORZAP (MAIN STORAGE SUPERZAP FOR MVS)
IZOPT    TM    STATSW,SPCLFLG      USER HAS SPECIAL ATTRIBUTE?
         BO    IZGODO              YES, OK
         LA    R1,IZRUMSG          ELSE USER IS UNAUTHORIZED
         ST    R1,MSGADD
         B     PRIOPT
IZGODO   ICM   R15,B'1111',ARIZAP
         BNZ   IZCALL
        LOAD   EP=ETPSIZAP,ERRET=LDIZERR
         ST    R0,ARIZAP
         LR    R15,R0
*- - - - ETPSIZAP PARM.LIST : NONE
IZCALL  CALL   (15)                GO TO INCORZAP PROCESS
         B     PRIOPT              THEN LOOK FOR ANOTHER SELECTION
LDIZERR  STM   R0,R1,MESSPL        INCORZAP IS UNAVAILABLE
        XMESS  99,R15
         LA    R1,IZUMSG
         ST    R1,MSGADD
         B     PRIOPT
         EJECT
*------- PROCESSING TERMINATION -------------------------------------*
         AIF   ('&EATL' EQ '').SKX2                                <X>
CLOSETB  TM    ATTNSW,ATTRSET      IS ATTENTION ROUTINE ACTIVE?    <X>
         BZ    NOATTR              NO                              <X>
         L     R1,XWAPTR           YES, GET WORK-AREA POINTER      <X>
         USING XWAREA,R1                                           <X>
         LA    R15,XWABND          SET RESET ALL ENTRY ADDRESS     <X>
        CALL   (15)                AND GO DO IT                    <X>
         DROP  R1                                                  <X>
         L     R0,=A(XWAREAL)      NOW FREE WORK-AREA              <X>
        FREEMAIN R,LV=(0),A=(1)                                    <X>
NOATTR   L     R4,BUFF             ADDRESS OF WRITE BUFFER         <X>
         AGO   .SKX3                                               <X>
.SKX2    ANOP  ,                                                   <X>
CLOSETB  L     R4,BUFF             ADDRESS OF WRITE BUFFER         <X>
.SKX3    MVI   0(R4),X'7E'         ERASE-WRITE-ALTERNATE           <X>
         MVC   1(L'ERSSCR,R4),ERSSCR ERASE SCREEN SEQUENCE         <X>
         MVI   1(R4),X'C3'         RESET KEYBOARD + RESET MDT      <X>
         LA    R5,L'ERSSCR+1       COMPUTE LENGTH                  <X>
         XC    RECB,RECB           RESET ECB                       <X>
         MVC   CCW1(8),SLCCW                                       <X>
         MVC   CCW2(1),WRITECC     COMMAND CODE SET BASED ON THE   <X>
         MVC   CCW2+1(7),WRTCCW+1    TERMINAL TYPE                 <X>
         STCM  R4,B'0111',CCW2+1   SET THE DATA ADDRESS            <X>
         STCM  R5,B'0011',CCW2+6   SET THE LENGTH                  <X>
         LA    R1,CCW1                                             <X>
         STCM  R1,B'0111',TUBEIOB+(IOBSTRTB-IOBSTDRD)              <X>
         LA    R2,RECB             SET ECB ADDRESS IN THE IOB      <X>
         STCM  R2,B'0111',TUBEIOB+(IOBECBPB-IOBSTDRD)              <X>
        EXCP   TUBEIOB                                             <X>
        WAIT   ECB=RECB                                            <X>
         LA    R2,TUBE                                             <X>
         MVI   OPCLPL,VLB          SET "VL" BIT                    <X>
        CLOSE  ((R2)),MF=(E,OPCLPL)                                <X>
FRTUBE   MVI   VERBCODE,S99VRBUN   SET TO INDICATE UNALLOCATE      <X>
         OI    DYNTEXT1,S99TUPLN   DDNAME ONLY                     <X>
         LA    R1,DYNPARM                                          <X>
        DYNALLOC ,                 DE-ALLOCATE TUBE                <X>
         LTR   R15,R15             HOW COMPLETE?                   <X>
         BZ    DELMODS             OK                              <X>
        XMESS  1101,R15                                            <X>
         B     DELMODS                                             <X>
TSOEND  TPUT   ERSSCR,L'ERSSCR,FULLSCR
        STFSMODE OFF               TURN OFF FULL-SCREEN MODE
         LTR   R15,R15             HOW COMPLETE?
         BZ    DELMODS             OK
        XMESS  1102,R15
DELMODS  ICM   R0,B'1111',ARBROWSE
         BZ    DELMOD1
        DELETE EP=ETPSBROW
DELMOD1  ICM   R0,B'1111',AREDIT
         BZ    DELMOD2
        DELETE EP=ETPSEDIT
DELMOD2  ICM   R0,B'1111',ARMEMC
         BZ    DELMOD3
        DELETE EP=ETPSMEMC
DELMOD3  ICM   R0,B'1111',ARCOMP
         BZ    DELMOD4
        DELETE EP=ETPSCOMP
DELMOD4  ICM   R0,B'1111',ARIDCA
         BZ    DELMOD5
        DELETE EP=ETPSIDCA
DELMOD5  ICM   R0,B'1111',ARZAP
         BZ    DELMOD6
        DELETE EP=ETPSZAP
DELMOD6  ICM   R0,B'1111',ARIZAP
         BZ    DELMOD7
        DELETE EP=ETPSIZAP
DELMOD7  ICM   R0,B'1111',ARSTAT
         BZ    RTRNEND
        DELETE EP=ETPSDSST
RTRNEND DELETE EP=ETPSMESS
LEAVE   DELETE EP=ETPSUTIL
QUIT     L     R0,GETSIZE
         LR    R1,R13
         L     R13,4(R13)          PICK UP CALLING SAVE-AREA
        FREEMAIN R,LV=(0),A=(1)
         LM    R14,R12,12(R13)     RESTORE CALLING REGISTERS
         XR    R15,R15             SET OK RETURN CODE
         BR    R14                   AND RETURN
         EJECT
*------- GENERAL SCREEN BUILD SUB-ROUTINE ---------------------------*
*              AT ENTRY, R5 IS THE ADDRESS OF THE SCREEN COMPLETION
*                            PROCESS TO USE,
*                        R6 POINTS TO THE BEGINNING OF THE VARIABLE
*                           LENGTH VECTOR OF THE SCREEN LINES IMAGES
*                           ADDRESSES, AND
*                        R14 IS THE RETURN ADDRESS.
SCRNBLD  L     R4,SCROWS1          NUMBER OF ROWS IN SCREEN 1
         LA    R3,TERMOUT+4        POINT TO FIRST LINE OF SCREEN 1
         CLI   SPLIT,2             AM I ON SCREEN 2
         BNE   SCRNBLD1            NOPE, I'M ALL SET
         SLL   R4,2                MULTIPLY OFFSET BY 4
         LA    R3,TERMOUT+4(R4)    POINT TO ROW 1 OF SCREEN 2
         L     R4,SCROWS2          NUMBER OF ROWS IN SCREEN 2
SCRNBLD1 L     R2,0(,R6)           ADDRESS OF FROM FIELD
         L     R7,0(,R3)           ADDRESS OF "TO" FIELD
         XR    R1,R1
         IC    R1,0(,R2)           PICK UP LENGTH
         EX    R1,*+L'*+2          MOVE IT
         BR    R5
         MVC   0(*-*,R7),0(R2)     <<EXECUTED>>
*- - - - LOGON SCREEN
SCRNLOG  CLC   LUOFF(13,R7),=CL13'ENTER USER-ID' USER-ID?
         BNE   SCRNLOGN            NO
         TM    STATSW,TESTFLG      YES, TSO TEST OF LOGON PANEL?   <T>
         BZ    *+L'*+6             NO                              <T>
         MVC   LTOFF(20,R7),=CL20'(OR MSG-NUMBER TEST)'            <T>
         MVC   LGSCF1(L'LGSCF1,R7),USERID MOVE IT
         B     SCRNSKP             DO THE REST
SCRNLOGN CLC   LPOFF(13,R7),=CL13'     PASSWORD' PASSWORD?
         BNE   *+L'*+10            NO
         MVC   LGSCF2(L'LGSCF2,R7),USERPASS MOVE IT
         B     SCRNSKP             DO THE REST
         CLC   LGOFF(13,R7),=CL13'   RACF GROUP' RACF-GROUP?
         BNE   SCRNSKP             NOPE, SKIP THIS
         MVC   LGSCF3(L'LGSCF3,R7),RACGROUP MOVE IT
         B     SCRNSKP             DO THE REST
*- - - - PRIMARY OPTION SCREEN
SCRNPRM  CLC   PROFF1+1(9,R7),=CL9'USER-ID :' USER-ID?
         BNE   SCRNPRMD            NO
         MVC   UOFF(L'UOFF,R7),USERID YES, SET USER-ID
         MVC   GROFF(L'GROFF,R7),RACGROUP SET GROUP-ID
         CLI   TUBEDDNM,C'T'       TSO TERMINAL?
         BNE   *+L'*+10            NO
         MVC   TERMOFF(L'TERMOFF,R7),TERMSNME
         B     *+L'*+12
         MVC   TERMOFF(1,R7),TERMTYPE
         MVC   TERMOFF+2(3,R7),TERMADDR
         B     SCRNSKP             DO THE REST
SCRNPRMD CLC   PROFF2+1(14,R7),=CL14'SELECT OPTION ' USER-ID?
         BNE   SCRNPRMT            NO
         ST    R14,VUSWA           SAVE R14 AROUND ROUTINE
         LA    R1,VUSWA+4          -> FORMAT DATE AND TIME WORK-AREA
         XC    0(FDTWAL,R1),0(R1)  CLEAR
         L     R15,ARFDTE          GET CURRENT DATE/TIME
*- - - - ETPSFDTE PARM.LIST : "FDTWA" ADDRESS
        CALL   (15)
         USING FDTWA,R1
         LTR   R15,R15             HOW COMPLETE?
         BNZ   *+L'*+12            ERROR, SKIP
         AIF   ('&TXV' NE 'EUROPE').SK1
         MVC   DATEOFF(L'FXDD,R7),FXDD
         MVC   DATEOFF+L'FXDD+1(L'FXMM,R7),FXMM
         MVC   DATEOFF+L'FXDD+L'FXMM+2(L'FXYY,R7),FXYY
         AGO   .SK2
.SK1     MVC   DATEOFF+L'FXYY+L'FXMM+2(L'FXDD,R7),FXDD
         MVC   DATEOFF+L'FXYY+1(L'FXMM,R7),FXMM
         MVC   DATEOFF(L'FXYY,R7),FXYY
.SK2     MVC   TIMEOFF(L'FTHH,R7),FTHH
         MVC   TIMEOFF+L'FTHH+1(L'FTMM,R7),FTMM
         L     R14,VUSWA           RESTORE R14
         B     SCRNSKP             DO THE REST
         DROP  R1
SCRNPRMT CLI   TUBEDDNM,C'O'       OPERATOR TERMINAL?
         BE    *+L'*+8             YES
         CLI   TUBEDDNM,C'T'       TSO TERMINAL?
         BNE   SCRNSKP             NO, DO THE REST
         CLC   MSGOFF1(11,R7),=CL11'AND RETURN ' EXIT MESSAGE?
         BE    SCRNPRMX            YES
         CLC   MSGOFF2(11,R7),=CL11'AND RETURN ' EXIT MESSAGE?
         BNE   SCRNSKP             NO, DO THE REST
         MVC   MSGOFF2(L'MSGOFF2,R7),MSGOFF2-1(R7) NO LOGON PANEL
         MVI   MSGOFF2-1(R7),C'.'
         B     SCRNSKP             DO THE REST
SCRNPRMX MVC   MSGOFF1(L'MSGOFF1,R7),MSGOFF1-1(R7) NO LOGON PANEL
         MVI   MSGOFF1-1(R7),C'.'
         B     SCRNSKP             DO THE REST
*- - - - BROWSE/EDIT ENTRY SCREENS
SCRNBES  TM    PRCSSW,VSRFC        VOL-SER WAS RETRIEVED FROM CATALOG?
         BZ    *+L'*+10            NO
         NI    PRCSSW,255-VSRFC
         MVC   LIBVOL(L'LIBVOL),=CL8' '
         CLC   EDOFF1(10,R7),=CL10'FULLY-QUAL' DS-NAME?
         BNE   SCRNBES1            NO
         OI    LIBDSNM,C' '
         CLI   LIBDSNM,C' '        IS THERE A DATA-SET NAME SPECIFIED?
         BE    SCRNSKP             NO
         MVC   EDSCF1(L'LIBDSNM,R7),LIBDSNM COPY CURRENT DATA-SET NAME
         B     SCRNSKP             DO THE REST
SCRNBES1 CLC   EDOFF2(13,R7),=CL13'VOLUME SERIAL' VOL-SER?
         BNE   SCRNBES2            NO
         OI    LIBVOL,C' '
         CLI   LIBVOL,C' '         IS THERE A VOL-SER SPECIFIED?
         BE    SCRNSKP             NO
         MVC   EDSCF2(L'EDSCF2,R7),LIBVOL  COPY CURRENT VOL-SER
         B     SCRNSKP             DO THE REST
SCRNBES2 CLC   EDOFF3(17,R7),=CL17'DATA-SET PASSWORD' PASSWORD?
         BNE   SCRNSKP             NOPE, SKIP THIS
         MVC   EDSCF3(L'EDSCF3,R7),LIBPASS COPY CURRENT PASSWORD
         CLI   LIBPASS,C' '        IS THERE A PASSWORD SPECIFIED?
         BE    SCRNSKP             NO
         MVC   EDOFF4(L'EDOFF4,R7),=CL3'(I)' SAY IN
         MVI   EDOFF4-1(R7),X'05'  SET INTENS
         B     SCRNSKP             DO THE REST
*- - - - LIBRARY UTILITY ENTRY SCREENS
SCRNUTL  TM    PRCSSW,VSRFC        VOL-SER WAS RETRIEVED FROM CATALOG?
         BZ    *+L'*+10            NO
         NI    PRCSSW,255-VSRFC
         MVC   LIBVOL(L'LIBVOL),=CL8' '
         CLC   UTOFF1(10,R7),=CL10'FULLY-QUAL' DS-NAME?
         BNE   SCRNUTL1            NO
         OI    LIBDSNM,C' '
         CLI   LIBDSNM,C' '        IS THERE A DATA-SET NAME SPECIFIED?
         BE    SCRNSKP             NO
         MVC   UTSCF2(L'UTSCF2,R7),LIBDSNM COPY CURRENT DATA-SET NAME
         B     SCRNSKP             DO THE REST
SCRNUTL1 CLC   UTOFF2(13,R7),=CL13'VOLUME SERIAL' VOL-SER?
         BNE   SCRUTL2             NO
         OI    LIBVOL,C' '
         CLI   LIBVOL,C' '         IS THERE A VOL-SER SPECIFIED?
         BE    SCRNSKP             NO
         MVC   UTSCF3(L'UTSCF3,R7),LIBVOL  COPY CURRENT VOL-SER
         B     SCRNSKP             DO THE REST
SCRUTL2  CLC   UTOFF3(17,R7),=CL17'DATA-SET PASSWORD' PASSWORD?
         BNE   SCRNSKP             NOPE, SKIP THIS
         MVC   UTSCF4(L'UTSCF4,R7),LIBPASS COPY CURRENT PASSWORD
         CLI   LIBPASS,C' '        IS THERE A PASSWORD SPECIFIED?
         BE    SCRNSKP             NO
         MVC   UTOFF4(L'UTOFF4,R7),=CL3'(I)' SAY IN
         MVI   UTOFF4-1(R7),X'05'  SET INTENS
SCRNSKP  TM    0(R6),EOL           END-OF-LIST?
         BO    SCRNBLD2            YUP
         LA    R6,4(R6)            BUMP TO NEXT PARM
         LA    R3,4(R3)            BUMP TO NEXT PARM
         NI    0(R3),255-EOS       CLEAR END-OF-SCREEN
         BCT   R4,SCRNBLD1         KEEP LOOPING
SCRNBLD2 LTR   R4,R4               END OF SCREEN ROWS?
         BZ    SCRNBLD4            YES
         BCTR  R4,0                SUBTRACT 1 FOR LUCK
         LTR   R4,R4               END OF SCREEN ROWS?
         BZ    SCRNBLD4            YES
         LA    R3,4(R3)            BUMP TO NEXT PARM
         L     R7,0(,R3)           ADDRESS OF "TO" FIELD
SCRNBLD3 MVC   0(DUMMYRWL,R7),DUMMYROW MOVE IN A DUMMY ROW
         NI    0(R3),255-EOS       CLEAR END-OF-SCREEN
         LA    R3,4(R3)            BUMP TO NEXT PARM
         L     R7,0(,R3)           ADDRESS OF "TO" FIELD
         BCT   R4,SCRNBLD3         KEEP LOOPING
         CLI   SPLIT,1             AM I IN SPLIT-SCREEN, SCREEN 1?
         BE    SCRNBLD4            YUP, DON'T MOVE THIS IN
         MVC   0(DUMMYRWL,R7),DUMMYROW MOVE IN A DUMMY ROW
SCRNBLD4 CLI   SPLIT,1             AM I IN SPLIT-SCREEN, SCREEN 1?
         BER   R14                 YES
         OI    0(R3),EOS           SET END-OF-SCREEN
         BR    R14
         EJECT
*------- DATA AREA'S AND CONSTANTS ----------------------------------*
         SPACE 1
XESNAP   EQU   *,8
         BALR  R15,0
         L     R15,6(,R15)
         BR    R15
        @MPW   ,                   MASTER PASSWORD
RINITSK  RACINIT ENVIR=CHANGE,MF=L
RINITSKL EQU   *-RINITSK
CTCMLST CAMLST NAME,*-*,,*-*
CTCMLSL  EQU   *-CTCMLST
VTCMLST CAMLST SEARCH,*-*,*-*,*-*
VTCMLSL  EQU   *-VTCMLST
         SPACE 1
GETSIZE  DC    A(GMSIZE)
SMOD    MODESET KEY=ZERO,MODE=SUP,MF=L       ENTER IN SUP STATE
SMODLEN  EQU   *-SMOD
PMOD    MODESET KEY=NZERO,MODE=PROB,MF=L     GET BACK IN PROB STATE
PMODLEN  EQU   *-PMOD
*- - - - SUBMIT DYNAMIC ALLOCATION PARAMETERS, ETC...
SUBRBSK  DC    AL1(S99RBEND-S99RB) LENGTH
         DC    AL1(S99VRBAL)       VERB CODE = DS-NAME ALLOCATION
         DC    AL1(0,0)            FLAGS (ALLOW OFFLINE UNITS)
         DC    2XL2'0'             ERROR-INFORMATION REASON CODES
         DC    A(*-*)              POINTER TO TEXT POINTERS
SUBLENI  EQU   (S99RBEND-S99RB)-(*-SUBRBSK)
         DC    (SUBLENI)X'0'       RESERVED + FLAGS
         DC    A(SUBDDNAM)         POINTER TO DDNAME
         DC    A(SUBCLS)           POINTER TO SYSOUT CLASS
         DC    A(SUBPGM)           POINTER TO INTRDR
         DC    AL1(S99TUPLN),AL3(SUBFRE) POINTER TO FREE=CLOSE
SUBLEN   EQU   *-SUBRBSK
         DS    0H
SUBDDNAM DC    AL2(DALDDNAM)       DDNAME=  PARAMETER
         DC    AL2(1)              NUMBER OF TEXT UNITS
         DC    AL2(6)              LENGTH OF PARM
         DC    CL8'SUBJOB'         DDNAME
         DS    0H
SUBCLS   DC    AL2(DALSYSOU)       SYSOUT= PARAMETER
         DC    AL2(1)              NUMBER OF TEXT UNITS
         DC    AL2(1)              LENGTH OF PARM
         DC    CL1'A'              SYSOUT CLASS
         DS    0H
SUBPGM   DC    AL2(DALSPGNM)       INTRDR PARAMETER
         DC    AL2(1)              NUMBER OF TEXT UNITS
         DC    AL2(6)              LENGTH OF PARM
         DC    CL8'INTRDR'         PGM NAME
         DS    0H
SUBFRE   DC    AL2(DALCLOSE)       FREE=CLOSE
         DC    AL2(0)              NUMBER OF TEXT UNITS
*- - - - DISPLAY TERMINAL ALLOCATION, ETC...                       <X>
SLCCW    CCW   X'0B',*-*,X'60',1                                   <X>
WRTCCW   CCW   X'0D',*-*,X'20',*-*                                 <X>
IOBINIT  DC    AL1(IOBCMDCH+IOBUNREL,0)                            <X>
         DC    XL(IOBLEN-(IOBSENS0-IOBSTDRD))'0' REST OF IOB       <X>
TINIT   DCB    DSORG=PS,MACRF=E,DDNAME=SCREEN,BUFL=4*KB,RECFM=U    <X>
         DS    0F                                                  <X>
CONRB    DC    AL1(S99RBEND-S99RB) LENGTH                          <X>
         DC    AL1(S99VRBAL)       VERB CODE = DS-NAME ALLOCATION  <X>
         DC    AL1(0,0)            FLAGS (ALLOW OFFLINE UNITS)     <X>
         DC    2XL2'0'             ERROR-INFORMATION REASON CODES  <X>
         DC    F'0'                POINTER TO TEXT POINTERS        <X>
         DC    F'0'                RESERVED                        <X>
         DC    AL1(S99OFFLN,0,0,0) FLAGS (CONSIDER OFFLINE UNITS)  <X>
         DS    0H                                                  <X>
         DC    AL2(DALDDNAM)       DDNAME=  PARAMETER              <X>
         DC    AL2(1)              NUMBER OF TEXT UNITS            <X>
         DC    AL2(8)              LENGTH OF PARM                  <X>
         DC    CL8'TUBE'           DDNAME                          <X>
         DS    0H                                                  <X>
         DC    AL2(DALSTATS)       DISP= PARAMETER                 <X>
         DC    AL2(1)              NUMBER OF TEXT UNITS            <X>
         DC    AL2(1)              LENGTH OF PARM                  <X>
         DC    XL1'01'             OLD                             <X>
         DS    0H                                                  <X>
         DC    AL2(DALUNIT)        UNIT= PARAMETER                 <X>
         DC    AL2(1)              NUMBER OF TEXT UNITS            <X>
         DC    AL2(3)              LENGTH OF PARM                  <X>
         DC    CL3' '              UNIT ADDRESS                    <X>
CONRBLG  EQU   *-CONRB             VALUE MUST MATCH "DYNLENG" VALUE<X>
         SPACE 1
DUMMYROW DC    AL1(DUMMYRWL-1),X'04',CL9' '
DUMMYRWL EQU   *-DUMMYROW
ERSSCR   DC    X'C1115D7E1140403C4040001DC813'
         SPACE 1
BADEDORG MSG   'BAD DSORG'
BADED80  MSG   'LRECL NOT 80'
BADEDDSN MSG   'DS-NAME ERROR'
BADCTLOC MSG   'LOCATE FAILED'
BADVTOBT MSG   'OBTAIN FAILED'
UTBADORG MSG   'INVALID DSORG'
INVPFK   MSG   'INVALID PF-KEY'
INVOPT   MSG   'INVALID OPTION'
UTBADDSN MSG   'INVALID DS-NAME'
ZAUMSG   MSG   'ZAP UNAVAILABLE'
REJUSER  MSG   'USER-ID REJECTED'
MNUSER   MSG   'USER-ID MANDATORY'
INVMSN   MSG   'INVALID MSG-NUMBER'
INVGROUP MSG   'INVALID RACF-GROUP'
UTLLDIDE MSG   'IDCAMS UNAVAILABLE'
UTLLDSME MSG   'STATDS UNAVAILABLE'
IZRUMSG  MSG   'RESTRICTED USE ONLY'
UTBADLRE MSG   'INVALID LRECL-NOT 80'
UTLLDCME MSG   'COMPRESS UNAVAILABLE'
IZUMSG   MSG   'INCORZAP UNAVAILABLE'
         SPACE 1
EXTLST  EXTRACT ,'S',,MF=L
EXTLSTL  EQU   *-EXTLST
LOADMSG WTO    'ETPS0099 : LOAD "ETPS...." FAILED / RC = .. / ABEND CODX
               E = ........ ',ROUTCDE=2,DESC=3,MF=L
LOADMSGL EQU   *-LOADMSG
LDMS1    EQU   25,4
LDMS2    EQU   45,2
LDMS3    EQU   63,8
ALTRD   WTO    '  ',ROUTCDE=11,DESC=7,MF=L
ALTRDF   EQU   6,4
         PRINT &PRS
        LTORG
         PRINT &PRF
         DROP  R10,R11,R12,R13
        @ULT   ,                   USERS TABLE
         EJECT
*------- LIBRARY MANAGEMENT -----------------------------------------*
*        AT ENTRY, R1 = A(PARM.LIST) -> (DS-NAME,
*                                        VOL-SER,
*                                        DS-ORG,
*                                        PASSWORD)
         CNOP  0,4
         USING *,R12,R11
         USING SAVE2,R13      ************************** R13 ==> SAVE2
         USING SPLTAREA,R10   AT ENTRY : R10 MUST POINT TO "SPLTAREA"
ETPSLIBM STM   R14,R12,12(R13)     SAVE INPUT REGISTERS
         LR    R12,R15             SET PROGRAM BASE REGISTER 1
         LA    R3,1
         LA    R11,4*KB-1(R3,R12)  SET PROGRAM BASE REGISTER 2
         LR    R3,R10              SPLIT-SCREEN DSECT
         SH    R3,=Y(SPLITWRK-SAVE2) POINT TO SAVE2
         ST    R13,4(,R3)          STORE BACKWARD POINTER
         ST    R3,8(R13)           STORE FORWARD POINTER
         LR    R13,R3              SET DSECT BASE
         L     R7,DYNWORKP         INITIALIZE DYNAMIC ALLOCATION WORK
         LR    R2,R7
         LH    R3,=Y(PARMLLEN)     LENGTH TO BE CLEARED
         LA    R14,*
         XR    R15,R15
         ICM   R15,B'1000',=CL8' '
         MVCL  R2,R14              CLEAR TO ALL BLANKS
         LM    R3,R6,0(R1)         PICK UP 4 PARMS
*                                    R3=DS-NAME
*                                    R4=VOL-SER
*                                    R5=DS-ORG
*                                    R6=PASSWORD (IF ANY)
         MVC   DIRDSNM(L'DIRDSNM),0(R3) DS-NAME
         MVC   DIRVOL(L'DIRVOL),0(R4) VOL-SER
         MVC   DIRORG(L'DIRORG),0(R5) DS-ORG
         MVC   DIRPASS(L'DIRPASS),0(R6) PASSWORD
         USING PARMLIST,R7
*- - - - ALLOCATE THE FILE
         MVC   DSNAME,DIRDSNM      DS-NAME
         MVC   DSVOLSER,DIRVOL     VOL-SER
         MVC   PASSWORD,DIRPASS    PASSWORD
         MVC   DSSTATUS,=CL8'SHR'  SET DISP=SHR
         MVC   LOCNAME,=CL8' '     NOT DOING LOCATE
         MVC   DSUNIT,=CL8'SYSALLDA'
         CLC   DIRORG,=CL3'SEQ'    IS THIS A SEQUENTIAL FILE?
         BE    LIBDYN              YUP
*- - - - DIRECTORY MEMBERS SELECTION TO PROCESS
         L     R2,ALIBDCB          PICK UP LIBRARY ADDRESS
         MVC   DDNAME(L'DDNAME),DCBDDNAM-IHADCB(R2) DD-NAME
         MVC   0(DIRDCBL,R2),DIRDCB MOVE DCB SKELETON
         MVC   DCBDDNAM-IHADCB(L'DCBDDNAM,R2),DDNAME SET BACK DD-NAME
         L     R15,ARDYNAM
*- - - - ETPSDYNA PARM.LIST : "DYNWORKP" ADDRESS
        CALL   (15),MF=(E,DYNWORKP)
         LTR   R15,R15             HOW COMPLETE?
         BZ    OKALLD              OK
         CLC   DYNRCODE(2),=XL2'0210'
         BE    ERRINUSE
        XMESS  1301
         LA    R1,*+L'*+4
         B     EDIRSMS             YUP - ALL DONE
         MSG   'ALLOC "L" FAILED',H=Y
OKALLD   MVI   DIRNOTE,0           INITIALIZE
         MVI   DRFUSW,0
         XC    DIRADDR,DIRADDR
         MVI   SELFUNC,C' '
         MVI   NWNMFLD,C' '
         L     R0,SCROWS1          LINES PER PAGE SCREEN 1
         CLI   SPLIT,2             AM I ON SCREEN 2?
         BNE   *+L'*+4             NO
         L     R0,SCROWS2          LINES PER PAGE SCREEN 2
         SH    R0,=Y(CRDLST)
         STH   R0,DRSCROLL
APDSDCB  TM    DCBOFLGS-IHADCB(R2),DCBOFOPN IS IT OPEN ALREADY?
         BO    APDSOPND            YUP
         LA    R1,DRREOF
         STCM  R1,B'0111',DCBEODA-IHADCB(R2)
         LA    R1,DIRSYNAD
         STCM  R1,B'0111',DCBSYNA-IHADCB(R2)
         NI    DRFLSW,255-SYNADFLG
         MVI   OPCLPL,VLB          SET "VL" BIT
        OPEN   ((R2),(UPDAT)),MF=(E,OPCLPL) OPEN FILE
         TM    DCBOFLGS-IHADCB(R2),DCBOFOPN TEST FOR GOOD OPEN?
         BO    OKLBOP              OK
        XMESS  1302
         LA    R1,*+L'*+4
         B     EDIRSMS             YUP - ALL DONE
         MSG   'OPEN "L" FAILED',H=Y
OKLBOP   MVC   DIRDECBW(DIRDECBL),DIRDECB
APDSOPND NI    DRFLSW,255-DIREOFLG INDICATE NOT AT EOF
         XC    DIRTOP,DIRTOP
         LA    R4,TERMOUT+4        POINT TO START
         CLI   SPLIT,2             AM I ON SCREEN 2?
         BNE   *+L'*+12            NOPE, JUST GO DO IT
         L     R1,SCROWS1          PICK UP NUMBER OF ROWS ON SCREEN 1
         SLL   R1,2                MULTIPLY BY 4
         LA    R4,TERMOUT+4(R1)    POINT TO START
         NI    0(R4),255-EOS       CLEAR END-OF-SCREEN
         L     R14,0(,R4)          PICK UP ADDRESS OF FIRST OUTPUT LINE
         LA    R15,DIRROW1         PICK UP FIRST LINE OF INPUT
         XR    R1,R1
         IC    R1,0(,R15)          PICK UP LENGTH
         EX    R1,MVEDR            MOVE INPUT INTO OUTADDR WORK AREA
         LA    R15,DIRDSNAM(R14)
         MVC   0(L'DIRDSNM,R15),DIRDSNM
         LA    R1,L'DIRDSNM
         CLI   0(R15),C' '
         BNE   *+L'*+4
         MVI   0(R15),C'-'
         LA    R15,1(R15)
         BCT   R1,*-16
         CLI   LIBFUNC,C'E'        EDIT FUNCTION REQUESTED?
         BE    *+L'*+20
         MVC   DIRTITLE(7,R14),=CL7' BROWSE'
         CLI   LIBFUNC,C'B'        BROWSE FUNCTION REQUESTED?
         BE    *+L'*+6
         MVC   DIRTITLE(7,R14),=CL7'UTILITY'
         L     R14,4(,R4)          BUMP
         NI    4(R4),255-EOS       CLEAR END-OF-SCREEN
         LA    R15,DIRROW2         PICK UP NEXT LINE OF INPUT
         IC    R1,0(,R15)          PICK UP LENGTH
         EX    R1,MVEDR            MOVE INPUT INTO OUTADDR WORK AREA
         TM    DRFUSW,DRCRSW
         BZ    *+L'*+10
         MVC   DRSCF2(L'DRSCF2,R14),=CL4'CSR'
         B     *+L'*+24
         TM    DRFUSW,DRHLSW
         BZ    *+L'*+10
         MVC   DRSCF2(L'DRSCF2,R14),=CL4'HALF'
         B     *+L'*+6
         MVC   DRSCF2(L'DRSCF2,R14),=CL4'PAGE'
         L     R14,8(,R4)          BUMP
         NI    8(R4),255-EOS       CLEAR END-OF-SCREEN
         LA    R15,DIRROW3         PICK UP NEXT LINE OF INPUT
         IC    R1,0(,R15)          PICK UP LENGTH
         EX    R1,MVEDR            MOVE INPUT INTO OUTADDR WORK AREA
         L     R14,12(,R4)         BUMP
         NI    12(R4),255-EOS      CLEAR END-OF-SCREEN
         LA    R15,DIRROW4         PICK UP NEXT LINE OF INPUT
         IC    R1,0(,R15)          PICK UP LENGTH
         EX    R1,MVEDR            MOVE INPUT INTO OUTADDR WORK AREA
         LA    R4,16(,R4)          SET R4 TO START OF BUILD AREA
         MVC   FIRSTMEM,=CL8' '    RESET FIRST NAME (TOP)
         LH    R8,DRSCROLL         MAX DIRECTORY MEMBERS ON SCREEN
         TM    DRFUSW,DRRCSW       DIR. RECORD STILL IN?
         BZ    DRREAD              NO
         NI    DRFUSW,255-DRRCSW   YES
         MVC   DIRBOT,DIRPOINT     COPY CURRENT TTR (BOTTOM)
         LA    R9,PDSKEY+L'PDSKEY+2 SET FIRST MEMBER ADDRESS
         B     DRNEXT
DRREAD   BAL   R14,DRRTN
         ST    R1,DIRBOT           KEEP CURRENT TTR (BOTTOM)
         USING PDS2,R9
DRNEXT   CLI   PDS2NAME,X'FF'      END OF DIRECTORY?
         BE    DIRENDOF            YES, LAST USED BLOCK
         TM    DRFLSW,DIREOFLG     HAVE I HIT EOF?
         BO    DIRENDOF            YUP
         NI    0(R4),255-EOS       CLEAR END-OF-SCREEN
         L     R1,0(,R4)
         USING DIRMASK,R1
         MVC   DIRLGTH(DIRELEN),DIRLIST MOVE DIR-ENTRY IN
*- - - - AM I DOING LOCATE FUNCTION?
         CLI   LOCNAME,C' '        LOCATE?
         BE    GOTDLOCF            NOPE
         CLC   LOCNAME(1),PDS2NAME MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP             IF HIGH, KEEP READING DIRECTORY
         CLI   LOCNAME+1,C' '      END OF STRING?
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+1(1),PDS2NAME+1 MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP             IF HIGH, KEEP READING DIRECTORY
         CLI   LOCNAME+2,C' '      END OF STRING?
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+2(1),PDS2NAME+2 MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP             IF HIGH, KEEP READING DIRECTORY
         CLI   LOCNAME+3,C' '      END OF STRING?
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+3(1),PDS2NAME+3 MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP             IF HIGH, KEEP READING DIRECTORY
         CLI   LOCNAME+4,C' '      END OF STRING?
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+4(1),PDS2NAME+4 MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP             IF HIGH, KEEP READING DIRECTORY
         CLI   LOCNAME+5,C' '      END OF STRING?
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+5(1),PDS2NAME+5 MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP             IF HIGH, KEEP READING DIRECTORY
         CLI   LOCNAME+6,C' '      END OF STRING?
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+6(1),PDS2NAME+6 MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP             IF HIGH, KEEP READING DIRECTORY
         CLI   LOCNAME+7,C' '      END OF STRING?
         BE    GOTDLOCF            YES, MATCHED UP TO HERE, SO OK
         CLC   LOCNAME+7(1),PDS2NAME+7 MATCH?
         BL    GOTDLOCF            GONE PAST IT
         BH    DIRBUMP             NOPE
GOTDLOCF OC    DIRTOP,DIRTOP       TOP OF SCREEN DISPLAY UNKNOWN?
         BNZ   *+L'*+6             NO, SKIP
         MVC   DIRTOP(L'DIRTOP),DIRBOT YES, RETAIN TTR (TOP)
*- - - - FORMAT THE PDS MEMBER
         MVC   DIRNAME(L'PDS2NAME),PDS2NAME MOVE MEMBER NAME TO PRINT
         CLI   SELFUNC,C' '        SELECT CODE?
         BE    NOSLCD              BRANCH IF NONE
         MVC   DIRCODE,SELFUNC     ELSE COPY SELECT CODE
         CLI   DIRADDR+1,CCDLF1    IS THIS FIELD IN ERROR?
         BNE   *+L'*+4             NO, SKIP
         MVI   DIRSUN1,X'02'       YES, SET IT AS INTENS
         OI    DIRSUN1,INFMOD      AND THEN SET IT AS MDT ON
NOSLCD   CLI   LIBFUNC,C'U'        LIBRARY UTILITY FUNCTION REQUESTED?
         BE    *+L'*+8             YES, SKIP
         MVI   DIRSUN3,X'04'       ELSE INHIBIT USER INPUT
         B     NONWNM
         CLI   SELFUNC,C' '        WAS THERE A SELECT CODE?
         BE    NONWNM              BRANCH IF NONE
         CLI   NWNMFLD,C' '        NEW MEMBER NAME?
         BE    NONWNM              BRANCH IF NONE
         MVC   DIRNWNM,NWNMFLD     ELSE COPY NEW MEMBER NAME
         CLI   DIRADDR+1,CCDLF2    IS THIS IN ERROR?
         BNE   *+L'*+4             NO, SKIP
         MVI   DIRSUN3,X'02'       YES, SET IT AS INTENS
         OI    DIRSUN3,INFMOD      AND THEN SET IT AS MDT ON
NONWNM   MVI   SELFUNC,C' '        NOW RESET
         MVI   NWNMFLD,C' '
         NI    PDS2INDC,PDS2LUSR
         CLI   PDS2INDC,X'0F'
         BNE   NOTSPF
         LA    R14,PDS2USRD        START  OF USER AREA
         USING SPFMT,R14
         XR    R15,R15
         IC    R15,SPFVER
         CVD   R15,DBLWRD
         OI    DBLWRD+7,X'0F'
         UNPK  DIRLEVEL(3),DBLWRD+6(2)
         MVI   DIRLEVEL,C' '
         IC    R15,SPFMOD
         CVD   R15,DBLWRD
         OI    DBLWRD+7,X'0F'
         UNPK  DIRLEVEL+3(3),DBLWRD+6(2)
         MVI   DIRLEVEL+3,C'.'
         MVC   DIRLEVEL(5),DIRLEVEL+1
         MVI   DIRLEVEL+5,C' '
         UNPK  DIRCREAT(5),SPFCRDT(3) CREATION DATE
         UNPK  DIRMOD(5),SPFMODT(3) MODIFIED DATE
         XR    R15,R15
         ICM   R15,B'0011',SPFSIZE
         CVD   R15,DBLWRD
         OI    DBLWRD+7,X'0F'
         UNPK  DIRLINES(5),DBLWRD+5(3)
         MVC   DIRUSER(L'SPFUID),SPFUID
         DROP  R1,R14
NOTSPF   CLI   FIRSTMEM,C' '
         BNE   *+L'*+6
         MVC   FIRSTMEM,PDS2NAME   SET TOP OF LIST
         BCT   R8,*+L'*+10         KEEP GOING
         MVC   LASTMEM,PDS2NAME    SET IT
         B     DIRFMT
         LA    R4,4(R4)            LOAD ADDRESS OF NEXT MEMBER
DIRBUMP  MVC   LASTMEM,PDS2NAME    SET IT
         CLC   PDSKEY,PDS2NAME     TEST FOR LAST MEMBER IN BLOCK
         BNH   DRREAD
         XR    R14,R14
         NI    PDS2INDC,PDS2LUSR   CLEAR ALIAS AND TTR FLAGS
         IC    R14,PDS2INDC        PICK UP USER DATA LENGTH IN H-WORDS
         SLL   R14,1               MULTIPLY BY 2
         LA    R9,PDS2USRD(R14)    POINT TO NEXT MEMBER
         B     DRNEXT
         DROP  R9
MVEDR    MVC   0(*-*,R14),0(R15)   <<EXECUTED>>
         SPACE 1
*- - - - READ A DIRECTORY RECORD ROUTINE
*              R9 = AT EXIT, POINTER TO FIRST MEMBER NAME IN BLOCK
*              R14 = LINK REGISTER
DRRTN    ST    R14,DIRSR14
         L     R2,ALIBDCB
         LA    R3,PDSKEY
        READ   DIRDECBW,SF,(R2),(R3),'S',MF=E
        CHECK  DIRDECBW
         TM    DRFLSW,SYNADFLG     WAS SYNAD EXIT TAKEN?
         BZ    *+L'*+8             NO, BRANCH
         LA    R1,DRLBMSG          YES, ERROR
         B     EDIRSMS
        NOTE   (R2)
         CLM   R1,B'0010',DIRNOTE  KEEP HIGHEST R OF TTR
         BNH   *+L'*+4
         STCM  R1,B'0010',DIRNOTE
         LA    R9,L'PDSKEY+2(R3)   FIRST MEMBER ADDRESS (+KEY+LENGTH)
         L     R14,DIRSR14
         BR    R14                 BACK TO CALLER
         CNOP  0,4
DRREOF  XMESS  1309
         LA    R1,*+L'*+4
         B     EDIRSMS
         MSG   'DIR.EOF READ',H=Y
*- - - - FILL REST OF SCREEN (END OF DIRECTORY)
DIRENDOF NI    0(R4),255-EOS       CLEAR END-OF-SCREEN
         L     R1,0(,R4)
         USING DIRMASK,R1
         MVC   DIRLGTH(DIRELEN),DIRLIST MOVE DIR-ENTRY IN
         MVI   DIRSUN1,X'04'       INHIBIT USER TO DO ANY
         MVI   DIRSUN3,X'04'         SELECTION ON THESE LINES
         TM    DRFLSW,DIREOFLG     FIRST TIME?
         BO    *+L'*+14            NO
         MVI   DIRSUN2,X'05'
         MVC   DIRNAME,=CL9'** END **'
         OI    DRFLSW,DIREOFLG     INDICATE I HAVE HIT EOF
         BCT   R8,*+L'*+4          KEEP GOING
         B     *+L'*+8
         LA    R4,4(,4)            LOAD ADDRESS OF NEXT MEMBER
         B     DIRENDOF
         DROP  R1
         OC    DIRTOP,DIRTOP       TOP OF SCREEN DISPLAY UNKNOWN?
         BNZ   DIRFMT              NO, SKIP
         MVC   DIRTOP(L'DIRTOP),DIRBOT YES, RETAIN TTR (TOP)
DIRFMT   CLI   SPLIT,1             AM I ON TOP SCREEN?
         BE    *+L'*+4             YUP
         OI    0(R4),EOS           SET END-OF-SCREEN
         L     R15,=A(LIBHELP)     HELP SCREEN
         ST    R15,HELPADD
         XC    CURADDR,CURADDR
         OC    CURADDR,DIRADDR     ANY CURSOR POSITION REQUESTED?
         BNZ   *+L'*+8             YES
         MVI   CURROW,CRDRSC       NO, SET DEFAULT CURSOR ROW
         MVI   CURCOL,CCDRSC       AND CURSOR COLUMN
         L     R15,ARCOMM          GO COMMUNICATE
*- - - - ETPSCOMM PARM.LIST : NONE
        CALL   (15)
         LTR   R15,R15             BAD RC?
         BNZ   FREELIB             OUT
         XC    DIRADDR,DIRADDR
*- - - - SCREEN MAY HAVE CHANGED
         L     R0,SCROWS1          LINES PER PAGE SCREEN 1
         CLI   SPLIT,2             AM I ON SCREEN 2?
         BNE   *+L'*+4             NO
         L     R0,SCROWS2          LINES PER PAGE SCREEN 2
         SH    R0,=Y(CRDLST)
         STH   R0,DRSCROLL
         L     R2,TERMINPT         PICK UP AID ADDRESS
         L     R4,TERMINPT+ZIME    PICK UP COMMAND INPUT LINE
         CLI   SPLIT,2             AM I ON SECOND SCREEN?
         BNE   DIRSCRL             NO, GO TEST PFKEY
         L     R3,SCROWS1          PICK UP NUMBER OF ROWS ON SCREEN 1
         SLL   R3,2                MULTIPLY BY 4
         L     R4,TERMINPT+ZIME(R3) PICK UP COMMAND INPUT LINE
*- - - - CHECK IF SCROLL ENTERED
DIRSCRL  CLI   0(R4),0             COMMAND OR SCROLL ENTERED?
         BE    DIRCKPF             NOPE
         TM    DRSCF2-1(R4),INFMOD YES, SCROLL ENRERED?
         BZ    DIRCKPF             NO
         LA    R14,DRSCF2(,R4)
         LA    R0,L'DRSCF2
         CLI   0(R14),C' '
         BNE   *+L'*+12
         LA    R14,1(,R14)
         BCT   R0,*-12
         B     *+L'*+18            NONE, CLEARED
         MVI   DRDBLW,C' '
         OC    DRDBLW(1),0(R14)
         NI    DRFUSW,255-DRHLSW-DRCRSW RESET TO PAGE
         CLI   DRDBLW,C'H'
         BNE   *+L'*+8
         OI    DRFUSW,DRHLSW       SET HALF
         B     DIRCKPF
         CLI   DRDBLW,C'C'
         BNE   DIRCKPF
         OI    DRFUSW,DRCRSW       SET CURSOR
DIRCKPF  TM    COMMSW,PFKFLAG      IS IT A PF KEY/ATTN?
         BZ    DIRTCMD             NONE
*- - - - TEST PF KEYS (PF KEY WAS ENTERED, SEE WHAT IT WAS)
         CLI   0(R2),X'F3'         IS IT PF3?
         BE    FREELIB             YUP - ALL DONE
         CLI   0(R2),X'C3'         IS IT PF15?
         BE    FREELIB             YUP - ALL DONE
         CLI   0(R2),X'F8'         IS IT PF8?
         BE    DRMORDIR            YUP - SHOW NEXT SCREEN OF DIR
         CLI   0(R2),X'C8'         IS IT PF20?
         BE    DRMORDIR            YUP - SHOW NEXT SCREEN OF DIR
         CLI   0(R2),X'F7'         IS IT PF7?
         BE    DRPREDIR            YUP - SHOW PREV SCREEN OF DIR
         CLI   0(R2),X'C7'         IS IT PF19?
         BE    DRPREDIR            YUP - SHOW PREV SCREEN OF DIR
         CLI   0(R2),X'6C'         IS IT PA1?
         BE    FREELIB             YUP - ALL DONE
         CLI   0(R2),X'6D'         IS IT "CLEAR"?
         BE    DIRRSHW             YUP - GO RESHOW THE SCREEN
         CLI   0(R2),X'6E'         IS IT PA2?
         BE    DIRRSHW             YUP - GO RESHOW THE SCREEN
         LA    R1,*+L'*+14
         ST    R1,MSGADD
DIRRSHW  MVC   LOCNAME,FIRSTMEM    RE-LOCATE THE TOP OF DISPLAY
         B     DIRLC2
         MSG   'INVALID PF-KEY',H=Y
*- - - - CHECK IF A COMMAND ENTERED
DIRTCMD  CLI   0(R4),0             COMMAND OR SCROLL ENTERED?
         BE    DIRSEL              NOPE
         TM    DRSCF1-1(R4),INFMOD YES, COMMAND ENRERED?
         BZ    DIRSEL              NO
         LA    R0,L'DRSCF1-1       YES, LEFT JUSTIFY FIELD IF NEEDED
         CLI   DRSCF1(R4),C' '
         BNE   *+L'*+18
         MVC   DRSCF1(L'DRSCF1-1,R4),DRSCF1+1(R4)
         MVI   DRSCF1+L'DRSCF1-1(R4),C' '
         BCT   R0,*-18
         B     DIRSEL              NONE
         CLC   DRSCF1(4,R4),=CL4'END ' END OF WORK?
         BE    FREELIB             YES
         CLC   DRSCF1(4,R4),=CL4'DUMP' TAKE A DUMP?
         BNE   *+L'*+2             NOPE
         DC    H'0'                YUP, TAKE AN S0C1 <------------ DUMP
         CLI   DRSCF1(R4),C'L'     LOCATE?
         BE    DIRLOC              YUP, GO LOCATE
         CLI   DRSCF1(R4),C'F'     ALLOW "F" FOR FIND AS WELL
         BE    DIRLOC              GO LOCATE
         CLC   DRSCF1(5,R4),=CL5'DOWN' DOWN?
         BE    DRMORDIR            GO DO IT
         CLC   DRSCF1(2,R4),=CL2'D' DOWN?
         BE    DRMORDIR            GO DO IT
         CLC   DRSCF1(2,R4),=CL2'+' DOWN?
         BE    DRMORDIR            GO DO IT
         CLC   DRSCF1(3,R4),=CL3'UP' UP?
         BE    DRPREDIR            GO DO IT
         CLC   DRSCF1(2,R4),=CL2'U' UP?
         BE    DRPREDIR            GO DO IT
         CLC   DRSCF1(2,R4),=CL2'-' UP?
         BE    DRPREDIR            GO DO IT
*- - - - PUT OTHER TESTS FOR COMMAND LINE INPUT HERE
DIRINVC  LA    R1,*+L'*+4
         B     EDIRSTM
         MSG   'INVALID COMMAND',H=Y
DIRSEL   LA    R3,TERMINPT+ZIMN    POINT TO LINE 5 (1ST SELECT LINE)
         L     R4,SCROWS1          PICK UP NUMBER OF ROWS ON SCREEN 1
         CLI   SPLIT,2             AM I ON SECOND SCREEN?
         BNE   DIRSEL1             NO, GO ROLL
         LR    R3,R4               NUMBER OF ROWS ON SCREEN 1
         SLL   R3,2                MULTIPLY BY 4
         LA    R3,TERMINPT+ZIMN(R3) SET ADDRESS OF 1ST SELECT LINE
         L     R4,SCROWS2          PICK UP NUMBER OF ROWS ON SCREEN 2
DIRSEL1  SH    R4,=Y(CRDLST)       MAX DIRECTORY MEMBERS FOR ONE SCREEN
DIRSEL2  L     R5,0(,R3)           POINT TO EACH LINE IN TURN
         CLI   0(R5),0             ANY INPUT?
         BE    *+L'*+8             NO, SKIP
         CLI   DRSLF1(R5),C' '     YES, BUT IS IT JUST CLEAR FIELD?
         BNE   DIRSEL4             NO, GO PROCESS IT
DIRSEL3  LA    R3,4(,R3)           POINT TO NEXT LINE
         BCT   R4,DIRSEL2
         B     AMEMEOFG            GO SHOW MORE
DIRSEL4  CLC   DRSLF2(8,R5),=CL8' '
         BE    *+L'*+10
         CLC   DRSLF2(9,R5),=CL9'** END **'
         BNE   DIRSEL5
         MVI   DRSLF1(R5),C' '     ERROR, CLEAR IT
         B     DIRINVS+L'DIRINVS   INVALID SELECTION
DIRSEL5  MVC   LOCNAME(L'LOCNAME),DRSLF2(R5) SET IT
         MVC   SELFUNC(L'SELFUNC),DRSLF1(R5) SET SELECT CODE
         MVI   DRSLF1(R5),C' '     CLEAR IT
         CLI   SELFUNC,C'S'        SELECT?
         BE    DIRMEMB             YES
         CLI   SELFUNC,C'B'        BROWSE?
         BE    DIRMEMB             YES
         CLI   LIBFUNC,C'E'        EDIT FUNCTION REQUESTED?
         BNE   *+L'*+8             NO, SKIP
         CLI   SELFUNC,C'E'        EDIT?
         BE    DIRMEMB             YES
         CLI   LIBFUNC,C'U'        LIBRARY UTILITY FUNCTION REQUESTED?
         BNE   DIRINVS             NO, SKIP
         CLI   SELFUNC,C'D'        DELETE?
         BE    MEMDEL              YES
         CLI   SELFUNC,C'R'        RENAME?
         BE    MEMREN              YES
         CLI   SELFUNC,C'C'        COPY?
         BE    MEMCPY              YES
*- - - - PUT TESTS FOR OTHER SELECTION CODES HERE
DIRINVS  MVC   DIRADDR,=AL1(CRDLST,CCDLF1) PROCESSING AT THIS POINT
         LA    R1,INVSEL           SET MESSAGE AND TRUNCATE
         B     EDIRSTM
DIRMEMB  LR    R0,R7               INITIALIZE DYNAMIC ALLOCATION WORK
         LH    R1,=Y(PARMLLEN)     LENGTH TO BE CLEARED
         LA    R14,*
         XR    R15,R15
         ICM   R15,B'1000',=CL8' '
         MVCL  R0,R14              CLEAR TO ALL BLANKS
         MVC   DSMEMBER(L'DSMEMBER),DRSLF2(R5) MEMBER SELECTED
         L     R4,AMEMDCB
         MVC   DDNAME(L'DDNAME),DCBDDNAM-IHADCB(R4) DD-NAME
         MVC   0(SEQDCBL,R4),SEQDCB MOVE DCB SKELETON
         MVC   DCBDDNAM-IHADCB(L'DCBDDNAM,R4),DDNAME SET BACK DD-NAME
         MVC   DSNAME,DIRDSNM      SET DS-NAME OF LIBRARY
         MVC   DSSTATUS,=CL8'SHR'  STATUS, DISP=SHR
         MVC   DSVOLSER,DIRVOL
         MVC   PASSWORD,DIRPASS
         MVC   DSUNIT,=CL8'SYSALLDA'
         L     R15,ARDYNAM
*- - - - ETPSDYNA PARM.LIST : "DYNWORKP" ADDRESS
        CALL   (15),MF=(E,DYNWORKP)
         LTR   R15,R15             HOW COMPLETE?
         BZ    OKDYNL              OK
        XMESS  1303
         LA    R1,*+L'*+4
         B     EDIRSTM             SKIP IT
         MSG   'ALLOC "L(M)" FAILED',H=Y
OKDYNL   CLI   LIBFUNC,C'E'        EDIT FUNCTION REQUESTED?
         BNE   MEMBRO              NO
         CLI   SELFUNC,C'E'        EDIT?
         BE    *+L'*+8             YES
         CLI   SELFUNC,C'S'        SELECT?
         BNE   MEMBRO              NO
         MVI   SELFUNC,C' '        NOW RESET
         LA    R1,AMEMEOF1
         STCM  R1,B'0111',DCBEODA-IHADCB(R4)
         LA    R1,SEQSYNAD
         STCM  R1,B'0111',DCBSYNA-IHADCB(R4)
         NI    DRFLSW,255-SYNADFLG
         XR    R3,R3               CLEAR RECORD COUNTER
         MVI   OPCLPL,VLB          SET "VL" BIT
        OPEN   ((R4),(INPUT)),MF=(E,OPCLPL)
         TM    DCBOFLGS-IHADCB(R4),DCBOFOPN GOOD OPEN?
         BO    AMEMDCNT            YES
        XMESS  1304
         LA    R1,*+L'*+8
         ST    R1,MSGADD
         B     AMEMEOFR            SKIP IT
         MSG   'OPEN "L(M)" FAILED',H=Y
AMEMDCNT GET   (R4),CRDWA
         TM    DRFLSW,SYNADFLG     WAS SYNAD EXIT TAKEN?
         BO    AMEMERR             YES, BRANCH
         LA    R3,1(R3)            BUMP COUNTER
         B     AMEMDCNT
*- - - - BROWSE MEMBER
MEMBRO   MVI   SELFUNC,C' '        NOW RESET
         ICM   R15,B'1111',ARBROWSE
         BNZ   CALLBR1
        LOAD   EP=ETPSBROW,ERRET=LDB1ERR
         ST    R0,ARBROWSE
         LR    R15,R0
*- - - - ETPSBROW PARM.LIST : DCB, DS-NAME, MEMBER-NAME
CALLBR1 CALL   (15),((R4),DSNAME,DSMEMBER),VL,MF=(E,PARMVL)
         B     AMEMEOFR
LDB1ERR  LA    R2,BRUMSG           BROWSE IS UNAVAILABLE
         B     LDE1ERR+L'LDE1ERR
LDE1ERR  LA    R2,EDUMSG           EDIT IS UNAVAILABLE
         STM   R0,R1,MESSPL
        XMESS  99,R15
         ST    R2,MSGADD
         B     AMEMEOFR
AMEMERR  LA    R1,DRLBMSG
         ST    R1,MSGADD
AMEMEOF1 MVI   OPCLPL,VLB          SET "VL" BIT
        CLOSE  ((R4)),MF=(E,OPCLPL)
         TM    DRFLSW,SYNADFLG     WAS SYNAD EXIT TAKEN?
         BO    AMEMEOFR            YES, BRANCH
         ST    R3,SEQRNUM
         ICM   R15,B'1111',AREDIT
         BNZ   CALLED1
        LOAD   EP=ETPSEDIT,ERRET=LDE1ERR
         ST    R0,AREDIT
         LR    R15,R0
*- - - ETPSEDIT PARM.LIST : DCB, DS-NAME, MEMBER-NAME, DS-ORG,
*                           NUM-RECS, VOL-SER, PASSWORD
CALLED1 CALL   (15),((R4),DSNAME,DSMEMBER,DIRORG,SEQRNUM,              X
               DIRVOL,DIRPASS),VL,MF=(E,PARMVL)
AMEMEOFR LR    R0,R7               INITIALIZE DYNAMIC ALLOCATION WORK
         LH    R1,=Y(PARMLLEN)     LENGTH TO BE CLEARED
         LA    R14,*
         XR    R15,R15
         ICM   R15,B'1000',=CL8' '
         MVCL  R0,R14              CLEAR TO ALL BLANKS
         MVI   DSNAME,0            INDICATE FREE REQUEST
         MVC   DSNAME+1(L'DSNAME-1),DSNAME
         MVC   DDNAME(L'DDNAME),DCBDDNAM-IHADCB(R4) DD-NAME
         L     R15,ARDYNAM
*- - - - ETPSDYNA PARM.LIST : "DYNWORKP" ADDRESS
        CALL   (15),MF=(E,DYNWORKP)
         LTR   R15,R15             HOW COMPLETE?
         BZ    AMEMEOFG            OK
        XMESS  1305
         TM    DRFLSW,SYNADFLG     WAS SYNAD EXIT TAKEN?
         BO    AMEMEOFG            YES, BRANCH
         LA    R1,FRLBMS
EDIRSTM  ST    R1,MSGADD
AMEMEOFG MVC   DSMEMBER,=CL8' '    CLEAR MEMBER NAME FIELD
         NI    DRFLSW,255-SYNADFLG
         B     DIRLC2
FRLBMS   MSG   'FREE "L(M)" FAILED',H=Y
*- - - - LOCATE MEMBER IN LIBRARY
DIRLOC   CLI   DRSCF1+1(R4),C' '
         BNE   *+L'*+12
         LA    R0,L'DRSCF1-2
         LA    R1,DRSCF1+2(R4)
         B     DIRLC1
         CLC   DRSCF1+1(6,R4),=CL6'OCATE '
         BNE   *+L'*+12
         LA    R0,L'DRSCF1-7
         LA    R1,DRSCF1+7(R4)
         B     DIRLC1
         CLC   DRSCF1+1(4,R4),=CL4'IND '
         BNE   DIRINVC
         LA    R0,L'DRSCF1-5
         LA    R1,DRSCF1+5(R4)
DIRLC1   CLI   0(R1),C' '
         BNE   *+L'*+12
         LA    R1,1(R1)
         BCT   R0,DIRLC1
         B     DIRLC2
         MVC   LOCNAME(L'LOCNAME),0(R1) MOVE IN "LOCATE" NAME
DIRLC2   XC    DIRPOINT,DIRPOINT   POINT TO START OF DIRECTORY
         MVI   DIRPOINT+2,1
         L     R2,ALIBDCB
        POINT  (R2),DIRPOINT
         B     APDSDCB
*- - - - DELETE MEMBER
MEMDEL   LA    R0,DRSLF2(,R5)      POINT TO MEMBER NAME
         L     R1,ALIBDCB          POINT TO LIBRARY
        STOW   (1),(0),D
         LTR   R15,R15             HOW COMPLETE?
         BNZ   *+L'*+8             BRANCH IF ERROR
         MVI   SELFUNC,C' '        NOW RESET
         B     DIRSEL3             LOOK FOR ANOTHER SELECTION
         LA    R1,=CL6'DELETE'     ERROR, SET MESSAGE AND TRUNCATE
         B     DOSTWM                PROCESSING AT THIS POINT
*- - - - RENAME MEMBER
MEMREN   BAL   R14,CHKNWNM         DO CHECK
         MVC   VUSWA(8),DRSLF2(R5) MOVE CURRENT NAME
         MVC   VUSWA+8(8),DRSLF3(R5) MOVE NEW NAME
         LA    R0,VUSWA            POINT TO MEMBER NAMES
         L     R1,ALIBDCB          POINT TO LIBRARY
        STOW   (1),(0),C
         LTR   R15,R15             HOW COMPLETE?
         BNZ   *+L'*+18            BRANCH IF ERROR
         MVC   LOCNAME(L'LOCNAME),DRSLF3(R5) AFTER POINT TO NEW NAME
         MVI   SELFUNC,C' '        AND NOW RESET
         MVI   NWNMFLD,C' '
         B     DIRSEL3             LOOK FOR ANOTHER SELECTION
         LA    R1,=CL6'RENAME'     SET MESSAGE AND TRUNCATE
DOSTWM   XR    R14,R14               PROCESSING AT THIS POINT
         LA    R2,OPFAIL
         CH    R15,=H'16'
         BNL   DOSTWMX               BRANCH IF R15 GREATER THAN 12
         EX    0,*+L'*(R15)          R15 IS LESS THAN 16 AND NEVER 0
         B     DOSTWMX
         LA    R2,OPDUPL             = 4
         LA    R2,OPNFND             = 8
         LA    R2,OPFULL             = 12
DOSTWMX  IC    R14,0(R2)
         EX    R14,MVSTWM
         CH    R15,=H'16'
         BL    DOSTWMZ
         MVC   DRLBMSG+1(6),0(R1)
         CVD   R15,DBLWRD
         MVC   DBLWRD(4),=XL4'40202120'
         ED    DBLWRD(4),DBLWRD+L'DBLWRD-2
         MVC   DRLBMSG+18(3),DBLWRD+1
DOSTWMZ  LA    R1,DRLBMSG
         MVC   DIRADDR,=AL1(CRDLST,CCDLF1)
         B     EDIRSTM
MVSTWM   MVC   DRLBMSG(*-*),0(R2)  <<EXECUTED>>
OPFAIL   MSG   '...... FAILED RC=...'
OPDUPL   MSG   'DUPLICATE NEW NAME'
OPNFND   MSG   'NAME NOT FOUND'
OPFULL   MSG   'NO SPACE IN DIR.',H=Y
         SPACE 1
*- - - - CHECK MEMBER NEW NAME ROUTINE
*              R14 = LINK REGISTER
CHKNWNM  LA    R0,L'DRSLF3-1       LEFT JUSTIFY FIELD IF NEEDED
         CLI   DRSLF3(R5),C' '
         BNE   CHKNWNM1
         MVC   DRSLF3(L'DRSLF3-1,R5),DRSLF3+1(R5)
         MVI   DRSLF3+L'DRSLF3-1(R5),C' '
         BCT   R0,*-18
         LA    R1,=CL8'MISSING'    NONE, ERROR, SET MESSAGE AND
         LA    R0,1                  TRUNCATE PROCESSING AT THIS POINT
         B     CHKNWNM3
CHKNWNM1 MVC   NWNMFLD(L'NWNMFLD),DRSLF3(R5)
         CLC   DRSLF3+L'DRSLF3-(L'DRSLF3-8)(L'DRSLF3-8,R5),=CL8' '
         BE    CHKNWNM2            OK
         LA    R1,=CL8'ERROR'      YES, ERROR, SET MESSAGE AND
         LA    R0,3                  TRUNCATE PROCESSING AT THIS POINT
         B     CHKNWNM3
CHKNWNM2 CLC   DRSLF2(8,R5),DRSLF3(R5) CURRENT MEMBER = NEW MEMBER?
         BNER  R14                 NO, OK
         LA    R1,=CL8'CONFLICT'   YES, ERROR, SET MESSAGE AND
         XR    R0,R0                 TRUNCATE PROCESSING AT THIS POINT
CHKNWNM3 XR    R14,R14
         IC    R14,NWMBMS
         EX    R14,MVNWMM
         SR    R14,R0
         STC   R14,DRLBMSG
         MVC   DRLBMSG+12(8),0(R1)
         LA    R1,DRLBMSG
         MVC   DIRADDR,=AL1(CRDLST,CCDLF2)
         B     EDIRSTM
MVNWMM   MVC   DRLBMSG(*-*),NWMBMS <<EXECUTED>>
NWMBMS   MSG   'NEW MEMBER ........',H=Y
*- - - - COPY MEMBER
MEMCPY   BAL   R14,CHKNWNM         DO CHECK
         MVC   VUSWA(8),=AL2(1,58) MOVE BLDL LIST
         MVC   VUSWA+4(8),DRSLF3(R5) MOVE NEW NAME
         LA    R0,VUSWA            POINT TO MEMBER NAMES
         L     R1,ALIBDCB          POINT TO LIBRARY
        BLDL   (1),(0)
         CH    R15,=H'4'           HOW COMPLETE?
         BNE   MEMCPY1             ERROR
         MVI   SELFUNC,C' '        4 = NOT FOUND, OK, NOW RESET
         MVI   NWNMFLD,C' '
         ICM   R15,B'1111',ARMEMC
         BNZ   CALLMM
        LOAD   EP=ETPSMEMC,ERRET=LDMCERR
         ST    R0,ARMEMC
         LR    R15,R0
*- - - ETPSMEMC PARM.LIST : FROM = DS-NAME, MEMBER-NAME, VOL-SER,
*                                  PASSWORD
*                             TO = DS-NAME, MEMBER-NAME, VOL-SER,
*                                  PASSWORD
CALLMM  CALL   (15),(DIRDSNM,DRSLF2(,R5),DIRVOL,DIRPASS,               X
               DIRDSNM,DRSLF3(,R5),DIRVOL,DIRPASS,                     X
               DRLBMSG),VL,MF=(E,VUSWA)
         LTR   R15,R15             HOW COMPLETE?
         BNZ   *+L'*+10            BRANCH IF ERROR
         MVC   LOCNAME(L'LOCNAME),DRSLF3(R5) AFTER POINT TO NEW NAME
         B     DIRSEL3             LOOK FOR ANOTHER SELECTION
         LA    R1,DRLBMSG
         STM   R0,R1,MESSPL
        XMESS  1307,R15
         B     EDIRSTM             TRUNCATE PROCESSING AT THIS POINT
LDMCERR  STM   R0,R1,MESSPL        COPY IS UNAVAILABLE
        XMESS  99,R15
         LA    R1,CPUMSG
         B     EDIRSTM
MEMCPY1  BH    MEMCPY2             GREATER THAN 4 = ERROR
         LA    R1,OPDUPL           0 = NEW NAME ALREADY EXIST
         MVC   DIRADDR,=AL1(CRDLST,CCDLF2)
         B     EDIRSTM
MEMCPY2  XR    R14,R14
         IC    R14,CPYFAIL
         EX    R14,MVCPYM
         CVD   R15,DBLWRD
         MVC   DBLWRD(4),=XL4'40202120'
         ED    DBLWRD(4),DBLWRD+L'DBLWRD-2
         MVC   DRLBMSG+16(3),DBLWRD+1
         LA    R1,DRLBMSG
         MVC   DIRADDR,=AL1(CRDLST,CCDLF1)
         B     EDIRSTM
MVCPYM   MVC   DRLBMSG(*-*),CPYFAIL <<EXECUTED>>
CPYFAIL  MSG   'COPY FAILED RC=...',H=Y
*- - - - SHOW MORE DIRECTORY INFO
DRMORDIR NI    DRFUSW,255-DRUPSW   SAY DOWN CONTROL
         BAL   R14,DRCSAR          GET SCROLL AMOUNT
         B     DRMORMAX            RETURN +0
         MVI   DIRPOINT+3,0         " "   +4
         MVC   DIRPOINT(L'DIRTOP),DIRTOP SET FIRST BLOCK READ (TOP)
DRMRDR1  L     R2,ALIBDCB
        POINT  (R2),DIRPOINT
         BAL   R14,DRRTN
         ST    R1,DIRPOINT         KEEP CURRENT TTR
         LR    R3,R9               RETAIN START ADDRESS
         USING PDS2,R9
         CLC   FIRSTMEM,PDS2NAME   IS THIS MEMBER NAME?
         BE    DRMRDR6             YES, IT IS JUST THIS ONE
         BH    DRMRDR2             NO
         BAL   R14,DRSUBBLK        ELSE GO BACK
         B     DRPREMAX            RETURN +0
         B     DRMRDR1              " "   +4
DRMRDR2  CLC   FIRSTMEM,PDSKEY     IS MEMBER IN THIS BLOCK?
         BNH   DRMRDR3             YES
         BAL   R14,DRRTN           NO, GO AHEAD
         ST    R1,DIRPOINT         KEEP CURRENT TTR
         LR    R3,R9               RETAIN START ADDRESS
         CLC   FIRSTMEM,PDS2NAME   IS THIS MEMBER NAME?
         BE    DRMRDR6             YES, IT IS JUST THIS ONE
         BL    DRMRDR9             IT HAS BEEN DELETED, STOP HERE
         B     DRMRDR2             ELSE
DRMRDR3  CLC   PDS2NAME,PDSKEY     LAST MEMBER IN BLOCK?
         BL    DRMRDR4             NO
         CLI   PDSKEY,X'FF'        YES, END OF DIRECTORY?
         BE    DRMORMX1            YES
         BAL   R14,DRRTN           NO, GO AHEAD
         ST    R1,DIRPOINT         KEEP CURRENT TTR
         LR    R3,R9               RETAIN START ADDRESS
         B     DRMRDR5
DRMRDR4  XR    R14,R14
         NI    PDS2INDC,PDS2LUSR   CLEAR ALIAS AND TTR FLAGS
         IC    R14,PDS2INDC        PICK UP USER DATA LENGTH IN H-WORDS
         SLL   R14,1               MULTIPLY BY 2
         LA    R9,PDS2USRD(R14)    POINT TO NEXT MEMBER NAME
DRMRDR5  CLC   FIRSTMEM,PDS2NAME   IS THIS MEMBER NAME?
         BL    DRMRDR9             IT HAS BEEN DELETED, STOP HERE
         BH    DRMRDR3             NO
DRMRDR6  CLC   PDS2NAME,PDSKEY     YES, LAST MEMBER IN BLOCK?
         BNL   DRMRDR8             YES
DRMRDR7  XR    R14,R14             NO
         NI    PDS2INDC,PDS2LUSR   CLEAR ALIAS AND TTR FLAGS
         IC    R14,PDS2INDC        PICK UP USER DATA LENGTH IN H-WORDS
         SLL   R14,1               MULTIPLY BY 2
         LA    R9,PDS2USRD(R14)    LOAD ADDRESS OF NEXT MEMBER NAME
         B     DRMRDR9
DRMRDR8  CLI   PDSKEY,X'FF'        END OF DIRECTORY?
         BE    DRMORMX1            YES
         BAL   R14,DRRTN
         ST    R1,DIRPOINT         KEEP CURRENT TTR
         CLI   PDS2NAME,X'FF'      END OF DIRECTORY?
         BE    DRMORMX2            YES
         LR    R3,R9               RETAIN START ADDRESS
DRMRDR9  BCT   R8,DRMRDR10
         CLI   PDS2NAME,X'FF'      END OF DIRECTORY?
         BE    DRMORMX1            YES
         MVC   LOCNAME,PDS2NAME    NO, LOOKING FOR PARTICULAR MEMBER
         OI    DRFUSW,DRRCSW       SET DIR. RECORD STILL IN
         B     APDSDCB
DRMRDR10 CLC   PDS2NAME,PDSKEY     LAST MEMBER IN BLOCK?
         BL    DRMRDR7             NO, CONTINUE
         B     DRMRDR8             YES
DRMORMAX MVC   DIRPOINT,DIRBOT     SET LAST BLOCK READ (BOTTOM)
         L     R2,ALIBDCB
        POINT  (R2),DIRPOINT
         BAL   R14,DRRTN
         ST    R1,DIRPOINT         KEEP CURRENT TTR
         CLI   PDSKEY,X'FF'        END OF DIRECTORY?
         BNE   *-12                NO
         B     DRMORMX2            YES
DRMORMX1 LR    R9,R3               RESTORE START ADDRESS
DRMORMX2 LH    R8,DRSCROLL         NOW MOVE DOWN THE SCREEN
         TM    DRFUSW,DRHLSW       HALF PAGE
         BZ    *+L'*+4             NO
         SRL   R8,1                YES
         LTR   R8,R8
         BP    DRMRPR
         LA    R8,1
         B     DRMRPR
         DROP  R9
*- - - - SHOW PREVIOUS DIRECTORY INFO
DRPREDIR OI    DRFUSW,DRUPSW       SAY UP CONTROL
         BAL   R14,DRCSAR          GET SCROLL AMOUNT
         B     DRPREMAX            RETURN +0
         MVI   DIRPOINT+3,0         " "   +4
         MVC   DIRPOINT(L'DIRTOP),DIRTOP SET FIRST BLOCK READ (TOP)
DRPRDR1  L     R2,ALIBDCB
        POINT  (R2),DIRPOINT
DRPRDR2  BAL   R14,DRRTN
         ST    R1,DIRPOINT         KEEP CURRENT TTR
         USING PDS2,R9
         CLI   PDS2NAME,X'FF'      END OF DIRECTORY?
         BNE   DRPRDR4             NO
DRPRDR3  BAL   R14,DRSUBBLK        YES, BACK 1 BLOCK
         B     DRPREMAX            RETURN +0
         B     DRPRDR1              " "   +4
DRPRDR4  LR    R3,R9               RETAIN START ADDRESS
         CLC   FIRSTMEM,PDS2NAME   HOW WITH TOP SCREEN MEMBER NAME?
         BL    DRPRDR3             MUST GO BACK
         BE    DRPRDR7             IT IS JUST THIS ONE
DRPRDR5  CLC   FIRSTMEM,PDSKEY     IS MEMBER IN THIS BLOCK?
         BNH   DRPRDR6             YES
         BAL   R14,DRRTN           NO, ELSE GO AHEAD
         ST    R1,DIRPOINT         KEEP CURRENT TTR
         LR    R3,R9               RETAIN START ADDRESS
         B     DRPRDR5
DRPRDR6  CLC   FIRSTMEM,PDS2NAME   IS THIS MEMBER NAME?
         BNH   DRPRDR7             YES OR IT HAS BEEN DELETED
         CLC   PDS2NAME,PDSKEY     NO, LAST MEMBER IN BLOCK?
         BNL   DRPRDR7             IT HAS BEEN DELETED, STOP HERE
         XR    R14,R14
         NI    PDS2INDC,PDS2LUSR   CLEAR ALIAS AND TTR FLAGS
         IC    R14,PDS2INDC        PICK UP USER DATA LENGTH IN H-WORDS
         SLL   R14,1               MULTIPLY BY 2
         LA    R9,PDS2USRD(R14)    LOAD ADDRESS OF NEXT MEMBER NAME
         B     DRPRDR6
DRPRDR7  LR    R4,R9               NOW TRY TO BACKWARD THE SCREEN
         LR    R9,R3               RESTORE START ADDRESS
         XR    R15,R15             ZERO COUNT
DRPRDR8  CLR   R9,R4               THIS MEMBER NAME?
         BE    DRPRDR9             YES
         CLC   PDS2NAME,PDSKEY     NO, LAST MEMBER IN BLOCK?
         BNL   DRPRDR9             YES
         LA    R15,1(R15)          NO, UPDATE COUNT
         XR    R14,R14
         NI    PDS2INDC,PDS2LUSR   CLEAR ALIAS AND TTR FLAGS
         IC    R14,PDS2INDC        PICK UP USER DATA LENGTH IN H-WORDS
         SLL   R14,1               MULTIPLY BY 2
         LA    R9,PDS2USRD(R14)    LOAD ADDRESS OF NEXT MEMBER NAME
         B     DRPRDR8
DRPRDR9  SR    R8,R15              IS IT ENOUGH?
         BZ    DRPRDR12            YES, EXACTLY THE AMOUNT
         BM    DRPRDR10            A FEW TOO MUCH
         BAL   R14,DRSUBBLK        NO, BACK 1 BLOCK
         B     DRPREMAX            RETURN +0
         L     R2,ALIBDCB           " "   +4
        POINT  (R2),DIRPOINT
         BAL   R14,DRRTN
         ST    R1,DIRPOINT         KEEP CURRENT TTR
DRMRPR   LR    R3,R9               RETAIN START ADDRESS
         XR    R4,R4
         LA    R15,1               COUNT START AT 1
         B     DRPRDR8
         DROP  R9
DRPRDR10 LPR   R8,R8               NUMBER OF NAMES FORWARD IN BLOCK
         USING PDS2,R3
         XR    R14,R14
DRPRDR11 NI    PDS2INDC,PDS2LUSR   CLEAR ALIAS AND TTR FLAGS
         IC    R14,PDS2INDC        PICK UP USER DATA LENGTH IN H-WORDS
         SLL   R14,1               MULTIPLY BY 2
         LA    R3,PDS2USRD(R14)    LOAD ADDRESS OF NEXT MEMBER NAME
         BCT   R8,DRPRDR11
DRPRDR12 MVC   LOCNAME,PDS2NAME    LOOKING FOR PARTICULAR MEMBER
         OI    DRFUSW,DRRCSW       SET DIR. RECORD STILL IN
         B     APDSDCB
         DROP  R3
DRPREMAX MVC   LOCNAME,=CL8' '     BACK TO TOP OF DIRECTORY
         B     DIRLC2
         SPACE 1
*- - - - COMPUTE SCROLL AMOUNT ROUTINE
*              R14 = LINK REGSITER / RETURN +0 = MAX.
*                                           +4 = OK (R8 = AMOUNT)
DRCSAR   ST    R14,DIRSR14
         CLI   0(R4),0             COMMAND LINE ENTERED?
         BE    DRCSARDF            NOPE
         TM    DRSCF1-1(R4),INFMOD YES, COMMAND ENRERED?
         BZ    DRCSARDF            NO, JUST BUMP DEFAULT
         LA    R0,L'DRSCF1-1       LEFT JUSTIFY FIELD IF NEEDED
         CLI   DRSCF1(R4),C' '
         BNE   *+L'*+14
         MVC   DRSCF1(L'DRSCF1-1,R4),DRSCF1+1(R4)
         MVI   DRSCF1+L'DRSCF1-1(R4),C' '
         BCT   R0,*-18
         CLI   DRSCF1(R4),C' '     COMMAND ENTERED?
         BE    DRCSARDF            NO, JUST BUMP DEFAULT
         L     R2,TERMINPT         PICK UP AID ADDRESS
         TM    DRFUSW,DRUPSW       UP REQUESTED?
         BO    DRCSARUP            YES
         CLI   0(R2),X'F8'         IS IT PF8?
         BE    DRCSARSC            YES
         CLI   0(R2),X'C8'         IS IT PF20?
         BE    DRCSARSC            YES
         MVC   DRDBLW(7),=CL8' '
         OC    DRDBLW(7),DRSCF1(R4)
         CLC   DRDBLW(5),=CL5'DOWN' IS IT "DOWN"?
         BNE   *+L'*+12            NO
         LA    R4,5(R4)            YES, SEE FORWARD
         SH    R0,=H'5'
         B     DRCSARTV
         CLC   DRDBLW(2),=CL2'D'   IS IT "DOWN"?
         BE    *+L'*+10            YES
         CLC   DRDBLW(2),=CL2'+'   IS IT "DOWN"?
         BNE   DIRINVC             NO, INVALID COMMAND
         LA    R4,2(R4)            YES, SEE FORWARD
         SH    R0,=H'2'
         B     DRCSARTV
DRCSARUP CLI   0(R2),X'F7'         IS IT PF7?
         BE    DRCSARSC            YES
         CLI   0(R2),X'C7'         IS IT PF19?
         BE    DRCSARSC            YES
         MVC   DRDBLW(7),=CL8' '
         OC    DRDBLW(7),DRSCF1(R4)
         CLC   DRDBLW(3),=CL3'UP'  IS IT "UP"?
         BNE   *+L'*+12            NO
         LA    R4,3(R4)            YES, SEE FORWARD
         SH    R0,=H'3'
         B     DRCSARTV
         CLC   DRDBLW(2),=CL2'U'   IS IT "UP"?
         BE    *+L'*+10            YES
         CLC   DRDBLW(2),=CL2'-'   IS IT "UP"?
         BNE   DIRINVC             NO, INVALID COMMAND
         LA    R4,2(R4)            YES, SEE FORWARD
         SH    R0,=H'2'
DRCSARTV BNP   DRCSARDF            NO MORE, JUST BUMP DEFAULT
         CLI   DRSCF1(R4),C' '
         BNE   DRCSARSC
         LA    R4,1(R4)
         BCT   R0,*-12
         B     DRCSARDF            NONE, JUST BUMP DEFAULT
DRCSARSC MVI   DRDBLW,C' '
         OC    DRDBLW(1),DRSCF1(R4)
         CLI   DRDBLW,C'M'         SCROLL "MAX"?
         BNE   *+L'*+6             NO
         L     R14,DIRSR14         YES
         BR    R14
         CLI   DRSCF1(R4),C'0'     IF NOT NUMERIC?
         BL    DRCSARDF            JUST BUMP DEFAULT
         LA    R14,DRSCF1(,R4)     SET START OF FIELD
         LA    R15,6               MAXIMUM LENGTH
         CLR   R15,R0
         BNL   *+L'*+2
         LR    R15,R0
         XR    R1,R1               COUNTER
         CLI   0(R14),C'0'         IF NOT NUMERIC
         BL    *+L'*+12
         LA    R1,1(,R1)           BUMP COUNTER
         LA    R14,1(,R14)         BUMP POINTER
         BCT   R15,*-16
         LTR   R1,R1               IF LENGTH IS ZERO?
         BZ    DRCSARDF            JUST BUMP DEFAULT
         BCT   R1,*+L'*+6          SUBTRACT 1 FOR EXECUTED PACK
         PACK  DRDBLW,DRSCF1(*-*,R4) <<EXECUTED>>
         EX    R1,*-6              PACK IT IN DRDBLW
         CVB   R0,DRDBLW           PUT VALUE IN R0
         B     DRCSARXQ
DRCSARDF LH    R0,DRSCROLL         MOVE DOWN THE SCREEN
         TM    DRFUSW,DRCRSW       CURSOR?
         BZ    DRCSARHF            NO
         L     R14,TERMINPT        PICK UP AID/ROW/COL ADDRESS
         XR    R15,R15
         IC    R15,1(R14)          GET ROW NUMBER
         SH    R15,=Y(CRDLST)
         BNP   DRCSARXQ
         TM    DRFUSW,DRUPSW       UP REQUESTED?
         BO    *+L'*+6             YES
         LR    R0,R15              NO, DOWN
         B     DRCSARXQ
         BCTR  R0,0
         SR    R0,R15
         B     DRCSARXQ
DRCSARHF TM    DRFUSW,DRHLSW       HALF PAGE
         BZ    DRCSARXQ            NO
         SRL   R0,1                YES
DRCSARXQ LTR   R8,R0
         BNP   AMEMEOFG            NONE, RE-DISPLAY
         L     R14,DIRSR14
         B     4(R14)
         SPACE 1
*- - - - BACK 1 BLOCK TTR OF DIRECTORY ADDRESS COMPUTATION ROUTINE
*              R14 = LINK REGISTER / RETURN +0 = TOP OF DIRECTORY
*                                           +4 = OK, DONE
DRSUBBLK XR    R1,R1
         CLI   DIRPOINT+2,1        AM I ON BLOCK 1?
         BE    DRSUBTRK            YES, JUST SUBTRACT FROM TRACK
         IC    R1,DIRPOINT+2       NO, PICK UP TRACK NUMBER
         BCTR  R1,0                SUBTRACT 1
         STC   R1,DIRPOINT+2
         B     4(R14)
DRSUBTRK ICM   R1,B'0011',DIRPOINT AM I ON TRACK 0?
         BZR   R14                 GO POINT TO TOP OF DIRECTORY
         BCTR  R1,0                SUBTRACT 1
         STCM  R1,B'0011',DIRPOINT PREVIOUS TRACK
         MVC   DIRPOINT+2(1),DIRNOTE POINT TO LAST BLOCK OF TRACK
         B     4(R14)
*- - - - SEQUENTIAL PROCESS
LIBDYN   L     R4,ACOPDCB          PICK UP LIBRARY ADDRESS
         MVC   DDNAME(L'DDNAME),DCBDDNAM-IHADCB(R4) DD-NAME
         MVC   0(SEQDCBL,R4),SEQDCB MOVE DCB SKELETON
         MVC   DCBDDNAM-IHADCB(L'DCBDDNAM,R4),DDNAME SET BACK DD-NAME
         L     R15,ARDYNAM
*- - - - ETPSDYNA PARM.LIST : "DYNWORKP" ADDRESS
        CALL   (15),MF=(E,DYNWORKP)
         LTR   R15,R15             HOW COMPLETE?
         BZ    OKALLS              OK
         CLC   DYNRCODE(2),=XL2'0210'
         BE    ERRINUSE
        XMESS  1301
         LA    R1,*+L'*+4
         B     EDIRSMS             YUP - ALL DONE
         MSG   'ALLOC "LIB" FAILED',H=Y
OKALLS   CLI   LIBFUNC,C'E'        EDIT FUNCTION REQUESTED?
         BE    SEQEDIT             YES
         CLI   LIBFUNC,C'B'        BROWSE FUNCTION REQUESTED?
         BE    SEQBROWS            YES
         B     SEQFREE
SEQEDIT  LA    R1,AMEMEOF2
         STCM  R1,B'0111',DCBEODA-IHADCB(R4)
         LA    R1,SEQSYNAD
         STCM  R1,B'0111',DCBSYNA-IHADCB(R4)
         NI    DRFLSW,255-SYNADFLG
         XR    R3,R3               CLEAR RECORD COUNTER
         MVI   OPCLPL,VLB          SET "VL" BIT
        OPEN   ((R4),(INPUT)),MF=(E,OPCLPL)
         TM    DCBOFLGS-IHADCB(R4),DCBOFOPN GOOD OPEN?
         BO    BMEMCOUN            YES
        XMESS  1302
         LA    R1,*+L'*+8
         ST    R1,MSGADD
         B     SEQFREE             SKIP IT
         MSG   'OPEN "LIB" FAILED',H=Y
BMEMCOUN GET   (R4),CRDWA
         TM    DRFLSW,SYNADFLG     WAS SYNAD EXIT TAKEN?
         BO    *+L'*+8             YES, BRANCH
         LA    R3,1(R3)            BUMP COUNTER
         B     BMEMCOUN
         LA    R1,DRLBMSG
         ST    R1,MSGADD
AMEMEOF2 MVI   OPCLPL,VLB          SET "VL" BIT
        CLOSE  ((R4)),MF=(E,OPCLPL)
         TM    DRFLSW,SYNADFLG     WAS SYNAD EXIT TAKEN?
         BO    SEQFREE             YES, BRANCH
         ST    R3,SEQRNUM
         ICM   R15,B'1111',AREDIT
         BNZ   CALLED2
        LOAD   EP=ETPSEDIT,ERRET=LDE2ERR
         ST    R0,AREDIT
         LR    R15,R0
*- - - ETPSEDIT PARM.LIST : DCB, DS-NAME, MEMBER-NAME, DS-ORG,
*                           NUM-RECS, VOL-SER, PASSWORD
CALLED2 CALL   (15),((R4),DSNAME,DSMEMBER,DIRORG,SEQRNUM,              X
               DIRVOL,DIRPASS),VL,MF=(E,PARMVL)
         B     SEQFREE
LDB2ERR  LA    R2,BRUMSG           BROWSE IS UNAVAILABLE
         B     LDE2ERR+L'LDE2ERR
LDE2ERR  LA    R2,EDUMSG           EDIT IS UNAVAILABLE
         STM   R0,R1,MESSPL
        XMESS  99,R15
         ST    R2,MSGADD
         B     SEQFREE
SEQBROWS ICM   R15,B'1111',ARBROWSE
         BNZ   CALLBR2
        LOAD   EP=ETPSBROW,ERRET=LDB2ERR
         ST    R0,ARBROWSE
         LR    R15,R0
*- - - - ETPSBROW PARM.LIST : DCB, DS-NAME, MEMBER-NAME
CALLBR2 CALL   (15),((R4),DSNAME,DSMEMBER),VL,MF=(E,PARMVL)
SEQFREE  LR    R0,R7               INITIALIZE DYNAMIC ALLOCATION WORK
         LH    R1,=Y(PARMLLEN)     LENGTH TO BE CLEARED
         LA    R14,*
         XR    R15,R15
         ICM   R15,B'1000',=CL8' '
         MVCL  R0,R14              CLEAR TO ALL BLANKS
         MVI   DSNAME,0            INDICATE FREE REQUEST
         MVC   DSNAME+1(L'DSNAME-1),DSNAME
         MVC   DDNAME(L'DDNAME),DCBDDNAM-IHADCB(R4) DD-NAME
         L     R15,ARDYNAM
*- - - - ETPSDYNA PARM.LIST : "DYNWORKP" ADDRESS
        CALL   (15),MF=(E,DYNWORKP)
         LTR   R15,R15             HOW COMPLETE?
         BZ    DIRRETRN            OK
        XMESS  1308
         TM    DRFLSW,SYNADFLG     WAS SYNAD EXIT TAKEN?
         BO    DIRRETRN            YES, BRANCH
         LA    R1,*+L'*+8
         ST    R1,MSGADD
         B     DIRRETRN
         MSG   'FREE "LIB" FAILED',H=Y
ERRINUSE LA    R1,ALL0210
EDIRSMS  ST    R1,MSGADD
*- - - - DONE EDITING THIS FILE, SO FREE IT
FREELIB  LA    R4,ALIBDCB
         LA    R3,NLDCB
BUMPLIB1 L     R2,0(,R4)
         TM    DCBOFLGS-IHADCB(R2),DCBOFOPN
         BZ    BUMPLIB2
         MVI   OPCLPL,VLB          SET "VL" BIT
        CLOSE  ((R2)),MF=(E,OPCLPL)
BUMPLIB2 LR    R0,R7               INITIALIZE DYNAMIC ALLOCATION WORK
         LH    R1,=Y(PARMLLEN)     LENGTH TO BE CLEARED
         LA    R14,*
         XR    R15,R15
         ICM   R15,B'1000',=CL8' '
         MVCL  R0,R14              CLEAR TO ALL BLANKS
         MVI   DSNAME,0
         MVC   DSNAME+1(L'DSNAME-1),DSNAME
         MVC   DDNAME(L'DDNAME),DCBDDNAM-IHADCB(R2) DD-NAME
         L     R15,ARDYNAM
*- - - - ETPSDYNA PARM.LIST : "DYNWORKP" ADDRESS
        CALL   (15),MF=(E,DYNWORKP)
         LTR   R15,R15
         BZ    NXTLIB
         CH    R15,=H'4'
         BNE   *+L'*+8
         CL    R0,=A(X'04380000')
         BE    NXTLIB              DD-NAME NOT FOUND (NOT ALLOCATED)
         ST    R3,MESSPL
        XMESS  1306
         OC    MSGADD,MSGADD
         BNZ   NXTLIB
         LA    R1,FRFMSG
         ST    R1,MSGADD
NXTLIB   LA    R4,4(R4)
         BCT   R3,BUMPLIB1         LOOP IF MORE
DIRRETRN NI    DRFLSW,255-SYNADFLG
         L     R13,4(R13)          PICK UP CALLING SAVE-AREA
         LM    R14,R12,12(R13)     RESTORE CALLING REGISTERS
         BR    R14                 NORMAL RETURN
         DROP  R7
         SPACE 1
*------- SYNAD EXITS - ENTERED DURING THE I/O IF AN ERROR OCCURS.
         CNOP  0,4
DIRSYNAD SYNADAF ACSMETH=BSAM
         MVI   DRLBMSG,15
         MVC   DRLBMSG+1(15),91(R1) ERROR DESCRIPTION ONLY
         OI    DRFLSW,SYNADFLG
        SYNADRLS
         BR    R14
         CNOP  0,4
SEQSYNAD SYNADAF ACSMETH=QSAM
         MVI   DRLBMSG,15
         MVC   DRLBMSG+1(15),91(R1) ERROR DESCRIPTION ONLY
         OI    DRFLSW,SYNADFLG
        SYNADRLS
         BR    R14
         SPACE 1
CPUMSG   MSG   'EDIT UNAVAILABLE'
EDUMSG   MSG   'EDIT UNAVAILABLE'
FRFMSG   MSG   'FREE FILE FAILED'
INVSEL   MSG   'INVALID SELECTION'
BRUMSG   MSG   'BROWSE UNAVAILABLE'
ALL0210  MSG   '0210/DATA-SET IN USE'
         SPACE 1
DIRDCB  DCB    DSORG=PS,MACRF=(RP,WP),DDNAME=DUMMY,RECFM=F,            X
               LRECL=256,BLKSIZE=256,KEYLEN=8,EODAD=*-*,SYNAD=*-*
DIRDCBL  EQU   *-DIRDCB
        READ   DIRDECB,SF,*-*,*-*,'S',MF=L
SEQDCB  DCB    DSORG=PS,MACRF=(GM,PM),DDNAME=DUMMY,EODAD=*-*,SYNAD=*-*
SEQDCBL  EQU   *-SEQDCB
         SPACE 1
*- - - - DIRECTORY LIST FORMAT
*        THIS IS THE FORMAT FOR A PDS LIBRARY DIRECTORY LISTING
DIRROW1  DC    AL1(DIRROW1L-1),XL1'05'
DIRTITLE EQU   *-DIRROW1,7
         DC    CL7'   EDIT',CL3' - '
DIRDSNAM EQU   *-DIRROW1
         DC    69CL1'-'
DIRROW1L EQU   *-DIRROW1
CRDRSC   EQU   01                  CURSOR ROW ON LINE BELOW
ZIME     EQU   (CRDRSC+1)*4        "TERMINPT" DISPLACEMENT
DIRROW2  DC    AL1(DIRROW2L-1)
         DC    XL1'05',CL12'COMMAND ===>'
CCDRSC   EQU   *-DIRROW2           CURSOR COLUMN ON FIELD BELOW
DRSCF1   EQU   (*-DIRROW2)+1,46    INPUT FIELD 1
         DC    XL1'01',CL46' '
         DC    XL1'05',CL11'< SCROLL =>'
DRSCF2   EQU   (*-DIRROW2)+1,4     INPUT FIELD 2
         DC    XL1'01',CL4' ',XL1'05',CL1'<',XL1'04'
DIRROW2L EQU   *-DIRROW2
DIRROW3  DC    AL1(DIRROW3L-1)
         DC    XL1'05',CL26'    NAME       NEWNAME    '
         DC    C'VV.MM CREATED   CHANGED         SIZE  ID '
DIRROW3L EQU   *-DIRROW3
DIRROW4  DC    AL1(DIRROW4L-1)
         DC    XL1'05',CL26'    --------   --------   '
         DC    C'----- -------   -------         ----  ------- '
DIRROW4L EQU   *-DIRROW4
CRDLST   EQU   04                  DIRECTORY LIST LINE NUMBER
ZIMN     EQU   (CRDLST+1)*4        "TERMINPT" DISPLACEMENT
DIRLIST  DC    AL1(DIRELEN-1)
CCDLF1   EQU   *-DIRLIST           CURSOR COLUMN ON FIELD BELOW
DRSLF1   EQU   (*-DIRLIST)+1,1     INPUT FIELD 1 - SEL.CODE
         DC    XL1'01',CL1' '
DRSLF2   EQU   (*-DIRLIST)+3,8     DISPLAY FIELD 2 - NAME
         DC    XL1'04',CL12' '
CCDLF2   EQU   *-DIRLIST           CURSOR COLUMN ON FIELD BELOW
DRSLF3   EQU   (*-DIRLIST)+1,10    INPUT FIELD 3 - NEW NAME OR RENAME
         DC    XL1'01',CL10' ',XL1'04',CL50' '
DIRELEN  EQU   *-DIRLIST
         DROP  R10,R11,R12,R13
         PRINT &PRS
        LTORG
         PRINT &PRF
         EJECT
*------- TERMINAL COMMUNICATIONS SUBROUTINE -------------------------*
*              (CONSOLE I/O SUBROUTINE)
*- - - - COMMUNICATIONS SUBROUTINE - EXCP INTERFACE
*              ON RETURN : R15 =  0 - OK WRITE (TPUT) AND READ (TGET)
*                              = 16 - WRITE (TPUT) OR READ (TGET) ERROR
         CNOP  0,4
         USING *,R12,R11
         USING SAVE4,R13      ************************** R13 ==> SAVE4
         USING SPLTAREA,R10   AT ENTRY : R10 MUST POINT TO "SPLTAREA"
ETPSCOMM STM   R14,R12,12(R13)     SAVE INPUT REGISTERS
         LR    R12,R15             SET PROGRAM BASE REGISTER 1
         LA    R3,1
         LA    R11,4*KB-1(R3,R12)  SET PROGRAM BASE REGISTER 2
         LR    R3,R10              SPLIT-SCREEN DSECT
         SH    R3,=Y(SPLITWRK-SAVE4) POINT TO SAVE4
         ST    R13,4(,R3)          STORE BACKWARD POINTER
         ST    R3,8(R13)           STORE FORWARD POINTER
         LR    R13,R3              SET DSECT BASE
         TM    COMMSW,EXTSBP       EXTERNAL PROCESS?
         BZ    RESCREEN            NO
*- - - - WHEN EXTERNAL SCREEN BUFFER PROCESS, AT ENTRY, REGISTERS
*              ARE PASSED AS BELOW :
*              R0 = WRITE/TPUT BUFFER ADDRESS
*              R1 = WRITE/TPUT LENGTH
         LR    R4,R0               YES, POSITION REGISTERS
         LR    R5,R1
         B     GOTOX1
RESCREEN NI    COMMSW,255-HELPFLG  INDICATE "NOT DOING HELP"
RESCRHLP L     R9,SCROWS           PICK UP TERMINAL SCREEN SIZE
         XR    R5,R5               LENGTH COUNTER
         L     R7,BUFF             ADDRESS OF WRITE BUFFER
         LR    R0,R7
         L     R1,=A(4*KB)
         LA    R14,*
         XR    R15,R15
         MVCL  R0,R14              CLEAR BUFFER TO BINARY ZEROS
         CLI   TUBEDDNM,C'T'       TSO TERMINAL?
         BNE   *+L'*+12            NO
         MVI   0(R7),X'27'         YES, SET TSO WRITE SEQUENCE
         LA    R7,1(R7)            BUMP ADDRESS
         LA    R5,1(R5)            BUMP COUNTER
         MVI   0(R7),X'7E'         ERASE-WRITE-ALTERNATE
         TM    COMMSW,HELPFLG      AM I DOING HELP?
         BO    BLDBUFFL            YES
         L     R6,TERMOUT          PICK UP WCC INDICATOR
         CLI   0(R6),C'A'          ALARM REQUESTED?
         BNE   BLDBUFFL            NO
         MVI   1(R7),X'C7'         ALARM + RESET KEYBOARD + RESET MDT
         B     BLDBUFFL+L'BLDBUFFL
BLDBUFFL MVI   1(R7),X'C3'         RESET KEYBOARD + RESET MDT
         LA    R7,2(R7)            BUMP ADDRESS
         LA    R5,2(R5)            BUMP COUNTER
         XR    R8,R8               SCREEN ROW 1
         LA    R6,TERMOUT+4        PICK UP LINE 1
         TM    COMMSW,HELPFLG      AM I DOING HELP?
         BZ    BLDBUFFS            NOPE
         L     R6,HELPADD          YUP, HELP SCREEN
*- - - - BUILD THE SCREEN I/O OUTPUT BUFFER LOOPING THROUGH ALL THE
*              "TERMOUT" DESCRIPTIONS UNTIL THE TERMINAL SCREEN SIZE
*              OR THE END-OF-LIST REACHED
BLDBUFFS XR    R4,R4               SET TO COL 1
         MVI   0(R7),X'11'         SET SBA CODE
         STC   R8,SBAROW           GET ROW
         STC   R4,SBACOL           GET COL
         BAL   R14,SBASLATE        TRANSLATE TO SBA
         MVC   1(2,R7),SBAWORK     SET ADDRESS OF FROM FIELD
         LA    R7,3(R7)            BUMP ADDRESS
         LA    R5,3(R5)            BUMP COUNTER
         L     R2,0(R6)            ADDRESS OF FROM FIELD
         CLI   0(R2),0             IS LENGTH ZERO?
         BNE   *+L'*+8             NOPE
         LA    R2,ERRROWZ          SAY ROW LENGTH IS ZERO
         B     *+L'*+12
         CLI   0(R2),BUFOLL-1      IS LENGTH TOO HIGH?
         BNH   *+L'*+4             NOPE
         LA    R2,ERRROWL          SAY ROW LENGTH IS TOO LONG
         XR    R1,R1               CLEAR REGISTER
         IC    R1,0(R2)            PICK UP LENGTH
         LA    R2,1(R2)            POINT TO FIRST INPUT BYTE
         XR    R3,R3               SET NO INPUT FIELD PROCESSED
BUFFMVL  CLI   0(R2),INFMOD+ATTMAX "SF" CODE?
         BNH   BUFFSF              YES, GO PROCESS IT
         MVC   0(1,R7),0(R2)       NO, MOVE BYTE OF DATA
         LA    R7,1(R7)            BUMP BUFFER POINTER
         LA    R5,1(R5)            BUMP BUFFER COUNTER
         B     BUFFMVT             CONTINUE
BUFFSF   LTR   R3,R3               HAVE WE PROCESSED AN INPUT FIELD?
         BZ    BUFFGSF             NO, GO AHEAD
         BAL   R0,ADJINF           YES, GO LOOK FOR TRAILING BLANKS
         LTR   R14,R14             ANY TRAILING BLANKS?
         BZ    BUFFGSF             NONE
         SR    R7,R14              BACK BUFFER POINTER
         SR    R5,R14              BACK BUFFER COUNTER
         MVI   0(R7),X'11'         SET SBA CODE
         STC   R8,SBAROW           GET ROW
         STC   R4,SBACOL           GET COL
         BAL   R14,SBASLATE        TRANSLATE TO SBA
         MVC   1(2,R7),SBAWORK     SET ADDRESS OF FROM FIELD
         LA    R7,3(R7)            BUMP ADDRESS
         LA    R5,3(R5)            BUMP COUNTER
BUFFGSF  XR    R14,R14
         IC    R14,0(R2)           PICK UP ATTRIBUTE BYTE
         LA    R0,ATTMAX
         NR    R14,R0
         AR    R14,R14
         LA    R14,ATTRIBS(R14)    POINT TO ATTRIBUTE
         TM    0(R14),FPROT        CHECK IF PROTECTED FIELD
         BZ    *+L'*+6             NO
         XR    R3,R3               YES, SET NO INPUT FIELD STARTS
         B     *+L'*+4
         LA    R3,2(R7)            SET INPUT FIELD START POINT
         MVI   0(R7),X'1D'         SET SF CODE
         TM    0(R2),INFMOD        CHECK IF INPUT FIELD MODIFIED?
         BO    *+L'*+10
         MVC   1(1,R7),0(R14)      MOVE ATTRIBUTE
         B     *+L'*+6
         MVC   1(1,R7),1(R14)      MOVE ATTRIBUTE (+MDT)
         LA    R7,2(R7)            BUMP BUFFER POINTER
         LA    R5,2(R5)            BUMP BUFFER COUNTER
BUFFMVT  LA    R2,1(R2)            BUMP INPUT  POINTER
         LA    R4,1(R4)            BUMP COL NUMBER
         BCT   R1,BUFFMVL          MOVE EVERY BYTE
         LTR   R3,R3               HAVE WE PROCESSED AN INPUT FIELD?
         BZ    TSIMSG              NO, GO AHEAD
         BAL   R0,ADJINF           YES, GO LOOK FOR TRAILING BLANKS
         LTR   R14,R14             ANY TRAILING BLANKS?
         BZ    TSIMSG              NONE
         SR    R7,R14              BACK BUFFER POINTER
         SR    R5,R14              BACK BUFFER COUNTER
TSIMSG   XR    R14,R14
         CL    R14,MSGADD          IS THERE A MESSAGE?
         BE    BLDBUMPT            NO, GO BUMP
         CLI   SPLIT,2             AM I IN SPLIT-SCREEN 2?
         BE    CHKMSG              YES, SKIP THIS
         LA    R14,TERMOUT+4       SET LINE 1 ADDRESS
         CR    R6,R14              AM I ON LINE1?
         BE    BLDBUMPS            YES I AM
         B     BLDBUMPT            NO, GO BUMP
CHKMSG   L     R14,SCROWS1
         SLL   R14,2
         LA    R14,TERMOUT+4(R14)  SET LINE 1 ADDRESS (SCREEN 2)
         CR    R6,R14              AM I ON LINE1?
         BNE   BLDBUMPT            NO, GO BUMP
BLDBUMPS LR    R15,R7
         L     R14,MSGADD          MESSAGE ADDRESS
         XR    R2,R2
         IC    R2,0(R14)           MESSAGE LENGTH
         LA    R0,20
         CLR   R2,R0
         BNH   *+L'*+2
         LR    R2,R0               TRUNCATE MESSAGE TOO LONG
         SR    R15,R2
         AR    R15,R1
         L     R14,MSGADD          MESSAGE ADDRESS
         BCT   R2,*+L'*+6
         MVC   0(*-*,R15),1(R14)   <<EXECUTED>>
         EX    R2,*-6              PLACE MESSAGE
         XC    MSGADD(4),MSGADD    CLEAR THE MESSAGE
         L     R14,BUFF            BUFFER ADDRESS
         CLI   0(R14),X'27'        IS IT TSO WRITE SEQUENCE?
         BNE   *+L'*+4             NO
         LA    R14,1(R14)          YES, ONE MORE AHEAD
         MVI   1(R14),X'C7'        ALARM + RESET KEYBOARD + RESET MDT
BLDBUMPT LA    R8,1(R8)            BUMP SCREEN ROW NUMBER
         TM    0(R6),EOS           END-OF-SCREEN LINES?
         BO    *+L'*+8             LET 'ER RIP
         LA    R6,4(R6)            BUMP TO NEXT PARM
         BCT   R9,BLDBUFFS         GO DO IT
         MVI   0(R7),X'11'
         XR    R0,R0
         IC    R0,CURROW           ROW
         TM    COMMSW,HELPFLG      AM I DOING HELP?
         BO    *+L'*+12            YES, DON'T APPLY SPLIT SCREEN
         CLI   SPLIT,2             AM I IN SPLIT-SCREEN 2?
         BNE   *+L'*+4             NO
         A     R0,SCROWS1          YES, ADD ROWS ON SPLIT SCREEN
         STC   R0,SBAROW
         MVC   SBACOL,CURCOL       COL
         BAL   R14,SBASLATE        TRANSLATE TO SBA
         MVC   1(2,R7),SBAWORK     SET ADDRESS OF CURSOR
         MVI   3(R7),X'13'
         LA    R5,4(R5)
         ST    R5,BUFFL1           MAKE SURE THE LENGTH IS SET
         L     R4,BUFF             ADDRESS OF WRITE BUFFER
         L     R0,REPLY            ADDRESS IF INPUT BUFFER
         L     R1,SCREENSZ
         LA    R14,*
         XR    R15,R15
         MVCL  R0,R14              CLEAR BUFFER TO BINARY ZEROS
*- - - - WHEN EXTERNAL SCREEN BUFFER PROCESS, AT RETURN, CONDITIONS
*              OF WRITE-READ/TPUT-TGET ARE PASSED AS BELOW :
*              R2 = ALWAYS THE "ECB" ADDRESS
*              R15 =  0 - WRITE-READ/TPUT-TGET OK
*                         R0 = "REPLY" LENGTH
*                         R1 = "REPLY" ADDRESS
*                     4 - WRITE ERROR (R0 = R1 = 0)
*                     8 - READ ERROR (R0 = R1 = 0)
*                    12 - WRITE ERROR (R0 = TPUT RC, R1 = 0)
*                    16 - READ ERROR (R0 = TGET RC, R1 = 0)
GOTOX1   CLI   0(R4),X'27'         IS IT TSO WRITE SEQUENCE?
         BE    WRTUBET             YES
         XC    RECB,RECB           RESET ECB                       <X>
         MVC   CCW1(8),SELCCW                                      <X>
         MVC   CCW2(1),WRITECC     COMMAND CODE SET BASED ON       <X>
         MVC   CCW2+1(7),WRITECCW+1 TERMINAL TYPE                  <X>
         STCM  R4,B'0111',CCW2+1   SET THE DATA ADDRESS            <X>
         STCM  R5,B'0011',CCW2+6   SET THE LENGTH                  <X>
         LA    R1,CCW1                                             <X>
         STCM  R1,B'0111',TUBEIOB+(IOBSTRTB-IOBSTDRD)              <X>
         LA    R2,RECB             SET ECB ADDRESS                 <X>
         STCM  R2,B'0111',TUBEIOB+(IOBECBPB-IOBSTDRD) IN THE IOB   <X>
        EXCP   TUBEIOB                                             <X>
        WAIT   ECB=RECB                                            <X>
         CLI   RECB,ECBNORM        CHECK FOR OK?                   <X>
         BE    OKWRITE             YES                             <X>
         TM    COMMSW,EXTSBP       NO, EXTERNAL PROCESS?           <X>
         BZ    WRFAIL              NO                              <X>
         LA    R15,4               YES, SET RC=4                   <X>
         XR    R0,R0                                               <X>
         B     GOTOX2                                              <X>
WRFAIL   CLI   RECB,ECBINCPT       CHECK FOR INTERCEPT?            <X>
         BE    INTIOER             IF YES, I STOP HERE             <X>
         CLI   RECB,ECBPERR        CHECK FOR PERMANENT I/O ERROR?  <X>
         BE    PERMIOER            TRY TO RECOVER                  <X>
        XMESS  1201                                                <X>
         B     TRETURNX                                            <X>
OKWRITE  NI    COMMSW,255-PERMERR-INTERR RESET FIRST TIME THROUGH  <X>
         L     R4,REPLY            PICK UP BUFFER ADDRESS          <X>
         L     R5,SCREENSZ         PICK UP BUFFER LENGTH           <X>
         MVC   CCW1(8),SELCCW                                      <X>
         MVC   CCW2(8),READCCW                                     <X>
         STCM  R4,B'0111',CCW2+1   SET THE DATA ADDRESS            <X>
         STCM  R5,B'0011',CCW2+6   SET THE LENGTH                  <X>
         LA    R1,CCW1                                             <X>
         STCM  R1,B'0111',TUBEIOB+(IOBSTRTB-IOBSTDRD)              <X>
         LA    R1,RECB             SET ECB ADDRESS                 <X>
         STCM  R1,B'0111',TUBEIOB+(IOBECBPB-IOBSTDRD) IN THE IOB   <X>
         AIF   ('&EATL' EQ '').SKX4                                <X>
         TM    ATTNSW,ATTRSET      IS ATTENTION ROUTINE ACTIVE?    <X>
         BZ    TURREAD             NO                              <X>
         OI    ATTNSW,ATTWAIT      YES, SAY WAITING ATTENTION      <X>
         XC    RECB,RECB           RESET ECB                       <X>
        WAIT   ECB=RECB            WAIT ATTENTION                  <X>
.SKX4    ANOP  ,                                                   <X>
TURREAD  XC    RECB,RECB           RESET ECB                       <X>
        EXCP   TUBEIOB                                             <X>
        WAIT   ECB=RECB                                            <X>
         CLI   0(R4),X'60'         GOT ANY DATA?                   <X>
         BNE   READEND             YES, CONTINUE                   <X>
        STIMER WAIT,BINTVL=TWOSEC  NO AID GENERATED, WAIT-A-BIT    <X>
         B     TURREAD             AND GO READ AGAIN               <X>
READEND  TM    COMMSW,EXTSBP       NO, EXTERNAL PROCESS?           <X>
         BZ    TESTREAD            NO, GO CHECK READ               <X>
         CLI   RECB,ECBNORM        CHECK FOR OK?                   <X>
         BE    TESTREAD            YES                             <X>
         LA    R15,8               NO, SET RC=8                    <X>
         XR    R0,R0                                               <X>
         B     GOTOX2                                              <X>
WRTUBET TPUT   (R4),(R5),FULLSCR
         LTR   R15,R15             HOW COMPLETE?
         BZ    OKTPUT              OK
         TM    COMMSW,EXTSBP       NO, EXTERNAL PROCESS?
         BZ    TPFAIL              NO
         LR    R0,R15              YES, PASS TPUT RC
         LA    R15,12              SET RC=12
         B     GOTOX2
TPFAIL  XMESS  1207,R15            TPUT FAILED, JUST ABEND
         DC    H'0'                YUP, TAKE AN S0C1 <------------ DUMP
OKTPUT   L     R1,REPLY            PICK UP BUFFER ADDRESS
         L     R0,SCREENSZ         PICK UP BUFFER LENGTH
        TGET   (1),(0),ASIS
         LTR   R15,R15             HOW COMPLETE?
         BZ    OKTGET              OK
         CH    R15,=H'8'           INTERRUPT?
         BNE   *+L'*+12            NO
         L     R14,REPLY           PICK UP BUFFER ADDRESS
         MVI   0(R14),X'6C'        YES, SET PA1
         B     OKTGET
         TM    COMMSW,EXTSBP       NO, EXTERNAL PROCESS?
         BZ    TGFAIL              NO
         LR    R0,R15              YES, PASS TGET RC
         LA    R15,16              YES, SET RC=16
GOTOX2   XR    R1,R1
         B     TRETURNZ
TGFAIL  XMESS  1208,R15            TGET FAILED, JUST ABEND
         DC    H'0'                YUP, TAKE AN S0C1 <------------ DUMP
OKTGET   L     R0,SCREENSZ         PICK UP BUFFER LENGTH
         SR    R0,R1               SET R0 = RESIDUAL LENGTH
         STH   R0,TUBEIOB+(IOBCSW-IOBSTDRD)+5 TUCK IT AWAY IN IOB
         MVI   RECB,ECBNORM        SET READ OK
*- - - - EXAMINE READ RESULT AND SET POINTERS TO INPUT FIELDS
TESTREAD TM    COMMSW,EXTSBP       EXTERNAL PROCESS?
         BZ    DOPINPT             NO
         L     R0,SCREENSZ         BUFFER INPUT LENGTH
         SH    R0,TUBEIOB+(IOBCSW-IOBSTDRD)+5 (RESIDUAL COUNT)
         L     R1,REPLY            ADDRESS OF INPUT RESPONSE
         XR    R15,R15             SET RC=0
TRETURNZ LA    R2,RECB             PASS ADDRESS OF ECB
         L     R13,4(R13)          PICK UP CALLING SAVE-AREA
         LM    R3,R12,20+4*R3(R13) RESTORE CALLER REGISTERS
         L     R14,12(R13)         RESTORE RETURN REGISTER
         BR    R14                 AND GO BACK TO CALLER
DOPINPT  LA    R14,TERMINPT+4      NOW CLEAR ALL INPUT LINES
         L     R1,SCROWS           PICK UP SIZE OF SCREEN
CLRINPT  L     R15,0(R14)          GET THE ADDRESS
         MVI   0(R15),0            CLEAR INPUT LINE
         MVC   1(BUFILL-1,R15),0(R15)
         LA    R14,4(R14)          BUMP TO NEXT INADDR
         BCT   R1,CLRINPT          LOOP
         LA    R4,TERMINPT         GET ADDRESS OF INPUT LINES
         L     R3,REPLY            GET ADDRESS OF INPUT RESPONSE
         CLI   RECB,ECBNORM        CHECK FOR OK?
         BE    OKREAD              YES
         CLI   RECB,ECBINCPT       CHECK IF READ INTERCEPTED?
         BE    INTIOER             IF YES, I STOP HERE
         CLI   RECB,ECBPERR        PERMANENT IO ERROR?
         BE    PERMIOER            RECOVER IF YES
        XMESS  1202
         B     TRETURNX
OKREAD   NI    COMMSW,255-PERMERR-INTERR-PFKFLAG 1ST TIME THROUGH
         CLC   0(4,R3),=XL4'016C6102' CHECK FOR TEST-REQ?
         BE    RESCREEN            RESET SCREEN IF YES
         CLI   0(R3),X'7D'         IS IT ENTER?
         BE    *+L'*+4             YES
         OI    COMMSW,PFKFLAG      IF NOT ENTER, MUST BE PFK
         TM    COMMSW,HELPFLG      AM I DOING HELP ALREADY?
         BZ    NOTHLPP             NO
         TM    COMMSW,PFKFLAG      YES, IS IT A PFK ENTERED?
         BZ    RESCRHLP            NO, RE-DISPLAY HELP
         CLI   0(R3),X'F3'         IS IT PF3 (END PFK)?
         BE    *+L'*+16            YUP
         CLI   0(R3),X'C3'         IS IT PF15 (END PFK)?
         BE    *+L'*+8             YUP
         CLI   0(R3),X'6C'         IS IT PA1?
         BNE   RESCRHLP            OTHERWISE, GO RE-DISPLAY HELP
         NI    COMMSW,255-HELPFLG  SHUT IT OFF
         MVC   CURADDR,HOMEADDR    RESTORE CURSOR ADDRESS
         B     RESCREEN            GO REDISPLAY THE SCREEN
NOTHLPP  XC    SBAWORK,SBAWORK
         OC    SBAWORK(L'SBAWORK),1(R3) GET CURSOR POSITION
         BZ    SREAD               NONE
         BAL   R14,SBAXLATE        TRANSLATE TO ROW AND COLUMN
         CLI   SPLIT,0             AM I IN SPLIT-SCREEN?
         BE    SREAD               NO
         CLI   SPLIT,2             YES, AM I IN SPLIT-SCREEN 2?
         BE    VRFSCT              YES
         CLC   SBAROW,SCROWS1+3    NO, IS CURSOR IN SCREEN 1?
         BL    SREAD               YES
         B     SKPST               ELSE SKIP SET
VRFSCT   XR    R0,R0               COMPUTE ROW RELATIVE TO SCREEN 2
         IC    R0,SBAROW
         S     R0,SCROWS1
         BNP   SKPST               CURSOR IN SCREEN 1, SKIP SET
         CL    R0,SCROWS2          IS CURSOR IN SCREEN 2?
         BNL   SKPST               NO, SKIP SET
         STC   R0,SBAROW           OK
SREAD    MVC   CURADDR,SBAWORK     SET CURSOR POSITION
SKPST    L     R14,0(R4)           PICK UP ADDRESS FOR AID AND CURSOR
         MVC   0(1,R14),0(R3)      MOVE AID
         MVC   1(L'CURADDR,R14),CURADDR MOVE CURSOR POSITION
         TM    COMMSW,PFKFLAG      IS IT A PFK ENTERED?
         BZ    LREAD               NO, GO PROCESS INPUT FIELDS IF ANY
*- - - - PF-KEY 2 / 14 : SPLIT SCREEN PROCESS
         CLI   0(R3),X'F2'         IS IT SPLIT PFKEY?
         BE    *+L'*+8             YUP
         CLI   0(R3),X'C2'         IS IT SPLIT PFKEY?
         BNE   TSWITCH             OTHERWISE, TEST FOR SWITCH
         CLI   SPLIT,0             SPLIT-SCREEN ALREADY?
         BE    *+L'*+12            NO, SPLIT IT
         L     R2,TERMINPT         PICK UP AID ADDRESS
         MVI   0(R2),X'6E'         RESET IT TO PA2
         B     TRETURN             JUST EXECUTE PA2
         L     R1,SCROWS           SET IT
         SRL   R1,1                DIVIDE BY 2
         ST    R1,SCROWS1          STORE IT
         L     R0,SCROWS           PICK IT UP
         SR    R0,R1               SUBTRACT
         ST    R0,SCROWS2          STORE THE REMAINDER
         MVI   SPLIT,2             INDICATE SCREEN 2
         L     R14,SAVETOP         PICK UP ADDRESS OF SAVE1
         L     R15,=A(AREALEN)     LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLOFF1
         AR    R0,R13
         MVCL  R0,R14              COPY TO SPLIT1
         L     R14,SAVETOP         PICK UP ADDRESS OF SAVE1
         L     R15,=A(AREALEN)     LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLOFF2
         AR    R0,R13
         MVCL  R14,R0              COPY FROM SPLIT2
         L     R14,ARPOPS          PICK UP RETURN ADDRESS
         LM    R10,R13,PRISPLIT
         XR    R15,R15             RESET (CLEAR)
         LR    R0,R15
         LR    R1,R15
         LR    R2,R15
         LR    R3,R15
         LR    R4,R15
         LR    R5,R15
         LR    R6,R15
         LR    R7,R15
         LR    R8,R15
         LR    R9,R15
         BR    R14                 SHOW PRIMARY OPTION MENU
*- - - - PF-KEY 9 / 21 : SWITCH SCREEN PROCESS
TSWITCH  CLI   0(R3),X'F9'         IS IT SWITCH SCREEN?
         BE    *+L'*+8             YUP
         CLI   0(R3),X'C9'         IS IT SWITCH SCREEN?
         BNE   TENDPFK             OTHERWISE, TEST FOR END PFK
         L     R2,TERMINPT         PICK UP AID ADDRESS
         MVI   0(R2),X'6E'         RESET IT TO PA2
         CLI   SPLIT,0             IN SPLIT-SCREEN MODE?
         BE    TRETURN             NO, JUST EXECUTE PA2
         CLI   SPLIT,1             AM I ON TOP SCREEN?
         BNE   SWBOTTOP            NOPE, GO FROM BOTTOM TO TOP
         L     R14,SAVETOP         PICK UP ADDRESS OF SAVE1
         L     R15,=A(AREALEN)     LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLOFF1
         AR    R0,R13
         MVCL  R0,R14              COPY TO SPLIT1
         MVI   SPLIT,2             INDICATE SCREEN 2
         L     R14,SAVETOP         PICK UP ADDRESS OF SAVE1
         L     R15,=A(AREALEN)     LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLOFF2
         AR    R0,R13
         MVCL  R14,R0              COPY FROM SPLIT2
         B     RESCREEN            SET UP FOR SWITCH
SWBOTTOP L     R14,SAVETOP         PICK UP ADDRESS OF SAVE1
         L     R15,=A(AREALEN)     LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLOFF2
         AR    R0,R13
         MVCL  R0,R14              COPY TO SPLIT2
         L     R14,SAVETOP         PICK UP ADDRESS OF SAVE1
         L     R15,=A(AREALEN)     LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLOFF1
         AR    R0,R13
         MVCL  R14,R0              COPY FROM SPLIT1
         MVI   SPLIT,1             TURN IT ON
         B     RESCREEN
*- - - - PF-KEY 3 / 15 : END PROCESS
TENDPFK  CLI   0(R3),X'F3'         IS IT END PFK?
         BE    *+L'*+8             YUP
         CLI   0(R3),X'C3'         IS IT END PFKEY?
         BNE   LREAD               NO, GO PROCESS INPUT FIELDS IF ANY
         CLI   SPLIT,0             SPLIT-SCREEN?
         BE    TRETURN             NOPE, JUST GO PROCESS
         CLI   PRIMEFLG,0          PRIMARY OPTION MENU ON SCREEN 1?
         BE    TRETURN             NO, JUST RETURN
         CLI   SPLIT,2             AM I ON BOTTOM SCREEN?
         BE    SWBOTEOF            YES, GO END THE BOTTOM SCREEN
         LM    R0,R1,SPLOFF1       NO, EXCHANGE SPLIT1-SPLIT2
         ST    R0,SPLOFF2
         ST    R1,SPLOFF1
SWBOTEOF L     R2,TERMINPT         PICK UP AID ADDRESS
         MVI   0(R2),X'6E'         RESET IT TO PA2
         L     R14,SAVETOP         PICK UP ADDRESS OF SAVE1
         L     R15,=A(AREALEN)     LENGTH OF SPLIT AREA
         LR    R1,R15
         L     R0,SPLOFF1
         AR    R0,R13
         MVCL  R14,R0              COPY FROM SPLIT1
         MVC   SCROWS1,SCROWS      RESET NUMBER OF ROWS
         XC    SCROWS2,SCROWS2     CLEAR THIS
         MVI   SPLIT,0             INDICATE NOT IN SPLIT-SCREEN
         B     TRETURN             GO PROCESS PA2
*- - - - INPUT FIELDS DATA PROCESS
LREAD    L     R1,SCREENSZ         START EXAMINING BUFFER FOR DATA
         SH    R1,TUBEIOB+(IOBCSW-IOBSTDRD)+5 (RESIDUAL COUNT)
         LA    R9,0(R1,R3)         LAST BYTE OF INPUT READ
         BCTR  R9,0
         CL    R1,=F'3'            IS IT ENTER WITH NO DATA?
         BNH   CHKHLP              YES
         LA    R4,4(R4)            BUMP TO NEXT ADDRESS
         LA    R3,3(R3)            POINT PAST AID AND CURSOR
*- - - - NOW I HAVE TO PICK UP THE SBA AND LOCATE THE CORRESPONDING
*        OUTPUT LINE (FIRST TRANSLATE SBA CODES TO ROW AND COLUMN)
GOTSBAL  XR    R6,R6               LENGTH OF DATA
         LR    R8,R3               START OF DATA
         CLI   0(R8),X'11'         DO I HAVE AN SBA?
         BE    OKSBA               YES
        XMESS  1203
         B     TRETURNX
OKSBA    LA    R1,2(R8)            SBA-ADDRESS CHECK
         CLR   R1,R9               HOW IS IT?
         BNH   OKCHK               OK
        XMESS  1204
         B     TRETURNX
OKCHK    MVC   SBAWORK(L'SBAWORK),1(R8) SET ADDRESS OF SBA
         BAL   R14,SBAXLATE        TRANSLATE TO ROW AND COLUMN
*- - - - CHECK IF WE ARE IN THE ACTIVE PART OF THE SCREEN
         CLI   SPLIT,0             SPLIT-SCREEN?
         BE    GTIFLD              NOPE, OK, PROCEED
         CLI   SPLIT,2             AM I ON BOTTOM SCREEN?
         BE    *+L'*+14            YES
         CLC   SBAROW,SCROWS1+3    NO, IS FIELD IN SCREEN 1?
         BL    GTIFLD              YES, OK, PROCEED
         B     SKPFLD              ELSE SKIP FIELD
         XR    R0,R0               COMPUTE ROW RELATIVE TO SCREEN 2
         IC    R0,SBAROW
         S     R0,SCROWS1
         BNP   SKPFLD              FIELD IN SCREEN 1, SKIP FIELD
         CL    R0,SCROWS2          IS FIELD IN SCREEN 2?
         BL    GTIFLD              YES, OK, PROCEED
SKPFLD   LA    R3,3(R3)            BUMP ADDRESS
         CLR   R3,R9               COMPARE VS LAST BYTE READ IN
         BH    CHKHLP              OUT LOOPING
         CLI   0(R3),X'11'         LOOK FOR NEXT SBA
         BE    GOTSBAL             GOT IT, KEEP LOOPING
         LA    R3,1(R3)            BUMP ADDRESS
         B     SKPFLD+L'SKPFLD     CONTINUE
*- - - - NOW MOVE THE IMAGE OF THE ROW THAT WAS TRANSMITTED TO THE
*        TERMINAL INTO MY INPUT BUFFER SO THAT I CAN SIMULATE A
*        FULL-LINE READ (MARK INPUT FIELDS AS ENTERED)
GTIFLD   LA    R14,TERMOUT+4       LINE 1 THAT WAS DISPLAYED
         XR    R15,R15             CLEAR REGISTER
         IC    R15,SBAROW          PICK UP LINE NUMBER (REL 0)
         SLL   R15,2               MULTIPLY BY 4
         L     R2,0(R15,R14)       POINT TO ACTUAL OUTPUT LINE
         LA    R14,TERMINPT+4      POINT TO INPUT LINE SLOT
         L     R7,0(R15,R14)       POINT TO ACTUAL INPUT LINE
         LA    R4,0(R15,R14)       SET ADDRESS POINTER
         CLI   0(R7),0             IS THIS A BLANK LINE?
         BNE   *+L'*+10            NO, SECOND FIELD ON THIS LINE
         XR    R14,R14             CLEAR REGISTER
         IC    R14,0(R2)           PICK UP LENGTH
         EX    R14,MOVINADD        MOVE LINE IMAGE TO INPUT BUFFER
         XR    R14,R14             CLEAR REGISTER
         IC    R14,SBACOL          PICK UP COLUMN (REL 0)
         LA    R7,0(R7,R14)        POINT TO ACTUAL INPUT ATTRIBUTE
         OI    0(R7),INFMOD        SET INPUT FIELD MODIFIED
         LA    R7,1(R7)            POINT TO ACTUAL INPUT LOCATION
*- - - - NOW I HAVE TO FIND THE LENGTH OF INPUT THAT WAS ENTERED
*        AT THE TERMINAL SO I CAN MOVE IT INTO THE LINE IMAGE.
         LA    R3,3(R3)            BUMP ADDRESS
SBABUMP  CLR   R3,R9               COMPARE VS LAST BYTE READ IN
         BH    GOTSBAM             OUT LOOPING
         CLI   0(R3),X'11'         LOOK FOR NEXT SBA
         BE    GOTSBAM             GOT IT
         LA    R3,1(R3)            BUMP ADDRESS
         LA    R6,1(R6)            BUMP LENGTH
         B     SBABUMP             KEEP LOOPING
GOTSBAM  LTR   R6,R6               CHECK IF JUST FIELD CANCELED?
         BZ    GOTSBAF             YES
         BCTR  R6,0
         TM    PRCSSW,ASISFLG      ASIS REQUESTED?
         BO    *+L'*+4             YES
         EX    R6,REPLYCAP         NO, UPPER-CASE THE INPUT
         EX    R6,REPLYMVC         MOVE IT INTO THE LINE
         LA    R7,1(R6,R7)
GOTSBAF  TM    0(R7),X'F0'         CLEAR FIELD (ALL OR REST)
         BZ    GOTSBAT
         MVI   0(R7),C' '
         LA    R7,1(R7)
         B     GOTSBAF
MOVINADD MVC   0(*-*,R7),0(R2)     <<EXECUTED>>
REPLYCAP TR    3(*-*,R8),CAPSONLY  <<EXECUTED>>
REPLYMVC MVC   0(*-*,R7),3(R8)     <<EXECUTED>>
GOTSBAT  CLR   R3,R9               COMPARE VS LAST BYTE READ IN
         BNH   GOTSBAL             KEEP LOOPING
*- - - - PF-KEY 1 / 13 : HELP PROCESS
*              NOTE THAT ALL HELP PANELS HAVE THE CURSOR ON THE FIRST
*              BLANK LINE (ROW 2, COLUMN 2)
CHKHLP   TM    COMMSW,PFKFLAG      IS IT A PFK ENTERED?
         BZ    TRETURN             NO, THAT'S ALL, RETURN
         L     R3,REPLY            YES, GET ADDRESS OF INPUT RESPONSE
         CLI   0(R3),X'F1'         IS IT HELP PFKEY?
         BE    *+L'*+8             YUP
         CLI   0(R3),X'C1'         IS IT HELP PFKEY?
         BNE   TRETURN             NO, THAT'S ALL, RETURN
         OI    COMMSW,HELPFLG      TURN HELP ON
         MVC   HOMEADDR,CURADDR    SAVE CURSOR ADDRESS
         MVC   CURADDR,=AL1(1,1)   SET CURSOR OF HELP PANEL
         L     R1,SCROWS           COPY ALL MODIFIED LINES
         LA    R2,TERMINPT+4
         LA    R3,TERMOUT+4
         XR    R15,R15
CPYLOOP  L     R4,0(R2)
         L     R5,0(R3)
         CLI   0(R4),0
         BE    *+L'*+8
         IC    R15,0(R4)
         EX    R15,COPYOUT
         LA    R2,4(R2)
         LA    R3,4(R3)
         BCT   R1,CPYLOOP
         B     RESCRHLP            AND GO DISPLAY HELP
COPYOUT  MVC   0(*-*,R5),0(R4)     <<EXECUTED>>
TRETURN  XR    R15,R15             SET RC=0
         L     R13,4(R13)          PICK UP CALLING SAVE-AREA
         LM    R0,R12,20(R13)      RESTORE CALLING REGISTERS
         L     R14,12(R13)         PRESERVE R15
         BR    R14                 NORMAL RETURN
PERMIOER TM    COMMSW,PERMERR      FIRST TIME THROUGH?
         BO    *+L'*+8             NOPE
         OI    COMMSW,PERMERR      SET NOT FIRST TIME THROUGH
         B     RESCREEN            TRY TO RESHOW THE SCREEN
        XMESS  1205
         B     TRETURNX
INTIOER  TM    COMMSW,INTERR       FIRST TIME THROUGH?
         BO    *+L'*+8             NOPE
         OI    COMMSW,INTERR       SET NOT FIRST TIME THROUGH
         B     RESCREEN            TRY TO RESHOW THE SCREEN
        XMESS  1206
TRETURNX LA    R15,16              SET RC=16
         B     TRETURN+L'TRETURN
         SPACE 1
*- - - - ROUTINE WHICH LOOK TO CUT OUT TRAILING BLANKS
*              OF THE JUST MOVED INPUT FIELD (THIS DO "NULLS ON")
*              R0 = LINK REGISTER
ADJINF   LR    R15,R7
         XR    R14,R14
ADJINFL  BCTR  R15,0
         CLR   R15,R3
         BL    ADJINFX
         CLI   0(R15),C' '
         BNE   ADJINFX
         MVI   0(R15),0            CLEAR IT
         LA    R14,1(R14)          BUMP BLANKS COUNT
         B     ADJINFL
ADJINFX  LR    R15,R0
         BR    R15
         SPACE 1
*- - - - SBA TRANSLATE TO ROW AND COLUMN ROUTINE
*              R14 = LINK REGISTER
SBAXLATE STM   R14,R1,12(R13)      SAVE SBA'S-WORK REGISTERS
         LA    R15,SBAWORK
         TR    0(2,R15),EBCTOBIN
         NC    0(2,R15),=XL2'3F3F'
         XR    R0,R0
         LR    R1,R0
         IC    R0,0(R15)
         IC    R1,1(R15)
         SLL   R0,6
         AR    R1,R0
         XR    R0,R0
         CL    R1,SCRWIDTH
         BNL   *+L'*+12
         STC   R0,SBAROW           0 (1)
         STC   R1,SBACOL           0-79 (1-80)
         B     *+L'*+12
         D     R0,SCRWIDTH
         STC   R1,SBAROW           0-42 (1-43)
         STC   R0,SBACOL           0-79 (1-80)
         LM    R14,R1,12(R13)      RESTORE SBA'S-WORK REGISTERS
         BR    R14
         SPACE 1
*- - - - ROW AND COLUMN TRANSLATE TO SBA ROUTINE
*              R14 = LINK REGISTER
SBASLATE STM   R14,R1,12(R13)      SAVE SBA'S-WORK REGISTERS
         XR    R0,R0               POS = ((ROW-1) * MAXCOL) + (COL-1)
         LR    R1,R0
         IC    R1,SBAROW           0-42 (1-43)
         LTR   R1,R1
         BZ    *+L'*+4
         MH    R1,SCRWIDTH+2
         IC    R0,SBACOL           0-79 (1-80)
         AR    R1,R0
         LR    R15,R1
         SRL   R15,6
         IC    R0,BINTOEBC(R15)
         STC   R0,SBAROW
         N     R1,=A(X'0000003F')
         IC    R0,BINTOEBC(R1)
         STC   R0,SBACOL
         LM    R14,R1,12(R13)      RESTORE SBA'S-WORK REGISTERS
         BR    R14
         SPACE 1
SCRWIDTH DC    F'80'               SCREEN WIDTH
TWOSEC   DC    A(2*60)             TWO SECONDS TIMER BIN VALUE     <X>
SELCCW   CCW   X'0B',*-*,X'60',1                                   <X>
WRITECCW CCW   X'0D',*-*,X'20',*-*                                 <X>
READCCW  CCW   X'06',*-*,X'20',*-*                                 <X>
         SPACE 1
BINTOEBC DS    0F
*                    0 1 2 3 4 5 6 7 8 9 A B C D E F
         DC    XL16'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'  0
         DC    XL16'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'  1
         DC    XL16'6061E2E3E4E5E6E7E8E96A6B6C6D6E6F'  2
         DC    XL16'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'  3
*                    0 1 2 3 4 5 6 7 8 9 A B C D E F
         SPACE 1
*- - - - FIELD ATTRIBUTE BYTES TABLE
*                             (2ND BYTE = + MDT WHEN UNPROTECTED)
ATTRIBS  DC    XL2'40C1'      X'00' - OFFSET ZERO (DEFAULT, NOT USED)
         DC    XL2'40C1'      X'01' - UNPROTECTED, DISPLAY <=== DEFAULT
         DC    XL2'C8C9'      X'02' - UNPROTECTED, DISPLAY, INTENSIFIED
         DC    XL2'4C4D'      X'03' - UNPROTECTED, NONDISPLAY
         DC    XL2'F0F0'      X'04' - SKIP, DISPLAY
         DC    XL2'F8F8'      X'05' - SKIP, DISPLAY, INTENSIFIED
         DC    XL2'7C7C'      X'06' - SKIP, NONDISPLAY
         DC    XL2'40C1'      X'07' - OFFSET SEVEN (DEFAULT, NOT USED)
*        BELOW IS THE MAXIMUM VALUE OF AN ATTRIBUTE (MUST BE X'07')
ATTMAX   EQU   ((*-ATTRIBS)-L'ATTRIBS)/L'ATTRIBS
FPROT    EQU   X'20'               "PROTECTED-FIELD" BIT
         SPACE 1
EBCTOBIN DC    256AL1(*-EBCTOBIN)
         ORG   EBCTOBIN+X'40'
         DC    X'C0'
         ORG   EBCTOBIN+X'4A'
         DC    X'CACBCCCDCECF'
         ORG   EBCTOBIN+X'50'
         DC    X'D0'
         ORG   EBCTOBIN+X'5A'
         DC    X'DADBDCDDDEDF'
         ORG   EBCTOBIN+X'60'
         DC    X'E0E1'
         ORG   EBCTOBIN+X'6A'
         DC    X'EAEBECEDEEEF'
         ORG   EBCTOBIN+X'7A'
         DC    X'FAFBFCFDFEFF'
         ORG
         SPACE 1
*- - - - CAPS ONLY TRANSLATE TABLE
CAPSONLY DC    CL16' '
         DC    XL2'1111',CL14' '   SBA
         DC    CL16' '
         DC    CL16' '
         DC    CL10' ',CL6'.<(+|'
         DC    CL1'&&',CL9' ',CL6'!$*);'
         DC    CL2'-/',CL8' ',CL6',%_>?'
         DC    CL9' ',CL7'`:#@''="'
         DC    CL10' ABCDEFGHI',CL6' '
         DC    CL10' JKLMNOPQR',CL6' '
         DC    CL10'  STUVWXYZ',CL6' '
         DC    CL16' '
         DC    CL10'{ABCDEFGHI',CL6' '
         DC    CL10'}JKLMNOPQR',CL6' '
         DC    CL10'\ STUVWXYZ',CL6' '
         DC    CL10'0123456789',CL6' '
         SPACE 1
ERRROWZ  DC    AL1(ERRROWZL-1)
         DC    XL1'05',C'===> ROW IS LENGTH 0 <===',XL1'04'
ERRROWZL EQU   *-ERRROWZ
ERRROWL  DC    AL1(ERRROWLL-1)
         DC    XL1'05',C'===> ROW IS TOO LONG <===',XL1'04'
ERRROWLL EQU   *-ERRROWL
         PRINT &PRS
        LTORG
         PRINT &PRF
         DROP  R10,R11,R12,R13
         AIF   ('&EATL' EQ '').SKX5                                <X>
         EJECT ,                                                   <X>
*------- EXCP ATTENTION INITIALIZATION --------------------------* <X>
*        HOW DONE : AN ENTRY IS DYNAMICALLY SET IN THE ATTENTION   <X>
*              TABLE (IOSVATTN) IN ORDER TO CAPTURE THE ATTENTION  <X>
*              SIGNAL FROM THE ALLOCATED TERMINAL (UCB).           <X>
*        NOTE : IN CASE THE ENTRY IN THE ATTENTION TABLE CANNOT BE <X>
*              INSTALLED, "ETPSCOMM" WORKS LOOPING ON THE I/O READ <X>
*              EXCP EVERY TWO SECONDS.                             <X>
         CNOP  0,4                                                 <X>
         USING *,R12                                               <X>
         USING SAVE2,R13      ********************** R13 ==> SAVE2 <X>
ETPSXAI  STM   R14,R12,12(R13)     SAVE INPUT REGISTERS            <X>
         LR    R12,R15             SET PROGRAM BASE REGISTER       <X>
         LR    R3,R13              SAVE1 POINTER                   <X>
         AH    R3,=Y(SAVE2-SAVE1)  POINT TO SAVE2                  <X>
         ST    R13,4(,R3)          STORE BACKWARD POINTER          <X>
         ST    R3,8(R13)           STORE FORWARD POINTER           <X>
         LR    R13,R3              SET DSECT BASE                  <X>
         USING IHADCB,R2           AT ENTRY R2 = DCB ADDRESS       <X>
         XR    R1,R1                                               <X>
         LR    R4,R1                                               <X>
         ICM   R1,B'0111',DCBDEBA  DEB ADDRESS                     <X>
         DROP  R2                                                  <X>
DUPTR    EQU   (DEBBASND-DEBBASIC)+(DEBUCBA-DEBDASD)               <X>
         ICM   R4,B'0111',DUPTR(R1) UCB ADDRESS                    <X>
         L     R3,=A(XWAREAL)      GET WORK-AREA                   <X>
        GETMAIN RC,LV=(R3)                                         <X>
         LTR   R15,R15             HOW COMPLETE?                   <X>
         BZ    XANAL               OK                              <X>
        XMESS  1011,R15            ERROR                           <X>
         B     XINIT               GO BACK                         <X>
XANAL    LR    R10,R1              SAVE GETMAIN POINTER            <X>
         LR    R2,R1                                               <X>
         LA    R14,*                                               <X>
         XR    R15,R15                                             <X>
         MVCL  R2,R14              CLEAR ALL TO BINARY ZEROS       <X>
         USING XWAREA,R10                                          <X>
         ST    R4,XWUCB                                            <X>
         L     R2,CVTPTR           CVT ADDRESS                     <X>
         USING CVT,R2                                              <X>
         L     R2,CVTIXAVL         IOCOM ADDRESS                   <X>
         DROP  R2                                                  <X>
         USING IOCOM,R2                                            <X>
         L     R2,IOCATTBL         ATTENTION TABLE ADDRESS         <X>
         DROP  R2                                                  <X>
         USING ATB,R2                                              <X>
         LR    R1,R2               RETAIN ADDRESS FOR LATER        <X>
         LA    R6,X'&EATL'(R2)     SET MAXIMUM ADDRESS FOR SEARCH  <X>
         LA    R3,56+4             SET UCBATI PAST SS1/DSM ENTRY   <X>
         SLL   R3,ATBMP2           APPLY POWER OF 2 MULTIPLIER     <X>
         LA    R3,ATBFLA(R3)       POINT TO ENTRY                  <X>
         DROP  R2                                                  <X>
         USING ATB,R3                                              <X>
         LR    R0,R3               RETAIN ADDRESS FOR LATER        <X>
         LA    R4,ATBELL           ONE ENTRY LENGTH                <X>
XLOOP1   CLC   ATBFLA(XDUML),XDUM  SCAN TABLE FORWARD              <X>
         BE    XFOUND1             DUMMY ROUTINE FOUND             <X>
         ALR   R3,R4               FORWARD TO NEXT                 <X>
         CLR   R3,R6               TOO FAR?                        <X>
         BL    XLOOP1              NO, LOOP                        <X>
        XMESS  1012                YES                             <X>
         B     XNONE                                               <X>
XFOUND1  L     R2,XCHK             NOW SCAN TABLE BACKWARD         <X>
         LR    R5,R3                                               <X>
         DROP  R3                                                  <X>
         USING ATB,R5                                              <X>
XLOOP2   SLR   R5,R4               BACKWARD TO PREVIOUS            <X>
         CL    R3,ATBRTN           TRY TO FIND A FREE/DUMMY ENTRY  <X>
         BNE   *+L'*+8             DON'T MATCH                     <X>
         CL    R2,ATBFLA                                           <X>
         BE    XFOUND2             MATCH                           <X>
         CLR   R5,R0               TOO FAR?                        <X>
         BH    XLOOP2              NO, LOOP                        <X>
         DROP  R5                                                  <X>
        XMESS  1013                                                <X>
XNONE    L     R0,=A(XWAREAL)      NO ENTRY, FREE WORK-AREA        <X>
         LR    R1,R10                                              <X>
        FREEMAIN R,LV=(0),A=(1)                                    <X>
         B     XINIT               AND GO BACK                     <X>
XFOUND2  ST    R10,XWAPTR          SET FOR TERMINATION FREEMAIN    <X>
         STM   R2,R3,XWRST         RESTORE VALUES                  <X>
         ST    R5,XWATP            ATTENTION TABLE ENTRY ADDRESS   <X>
         SLR   R5,R1               COMPUTE ATTENTION INDEX         <X>
         SRL   R5,ATBMP2           APPLY POWER OF 2 MULTIPLIER     <X>
         ST    R5,XWAIX            ATTENTION INDEX                 <X>
         USING PSA,R0                                              <X>
         L     R1,PSAAOLD          ASCB ADDRESS                    <X>
         DROP  R0                                                  <X>
         USING ASCB,R1                                             <X>
         XR    R2,R2                                               <X>
         ICM   R2,B'0011',ASCBASID MY AS-ID                        <X>
         DROP  R1                                                  <X>
         ICM   R2,B'1000',=AL1(ATBLLKR) LOCAL LOCK REQUIRED        <X>
         LA    R3,XWATTN           ATTENTION ENTRY ADDRESS         <X>
         MVC   0(XASKLL,R3),XASKL  MOVE ATTENTION ENTRY CODE       <X>
         STM   R2,R3,XWSET         SET VALUES                      <X>
         MVC   XWAREP,=A(XATTN)    ATTENTION ROUTINE ADDRESS       <X>
         ST    R10,XWARPT          "XWAREA" ADDRESS                <X>
         ST    R13,XWMYSV          "MYSAVE" ADDRESS                <X>
         LA    R3,XWABND           ABEND ENTRY ADDRESS             <X>
         MVC   0(XXSKLL,R3),XXSKL  MOVE ABEND ENTRY CODE           <X>
         MVC   XWXAEP,=A(XABEND)   ABEND EXIT ADDRESS              <X>
         ST    R10,XWXAPT          "XWAREA" ADDRESS                <X>
         MVC   XWLIST(XSKELL),XSKEL ACTIVE ABEND EXIT              <X>
        ESTAE  (R3),TOKEN=XWTOKEN,MF=(E,XWLIST)                    <X>
         LM    R2,R3,XWSET         NOW DO THE CHANGES              <X>
         L     R4,XWATP                                            <X>
         L     R5,XWUCB                                            <X>
         L     R6,XWAIX                                            <X>
         OI    ATTNSW,ATTRSET      SAY ATTENTION ROUTINE ACTIVE    <X>
        MODESET MF=(E,SUPRMOD)                                     <X>
         STM   R2,R3,0(R4)         SET ATTENTION TABLE ENTRY       <X>
        IOSGEN TP,UCB=(R5),VAR=ATNMOD,TABLE=(R6) SET ATTN INDEX    <X>
        MODESET MF=(E,PROBMOD)                                     <X>
XINIT    L     R13,4(R13)          PICK UP CALLING SAVE-AREA       <X>
         LM    R14,R12,12(R13)     RESTORE CALLING REGISTERS       <X>
         XR    R15,R15             SET RC=0                        <X>
         BR    R14                 RETURN                          <X>
         DROP  R10                                                 <X>
         SPACE 1                                                   <X>
*- - - - - - - - - - - - - - - - - DUMMY ROUTINE CODE              <X>
XDUM     SLR   R15,R15             R15 (RETURN CODE) = 0           <X>
         BSM   0,R14               BRANCH RETURN TO CALLER (IOS)   <X>
XDUML    EQU   *-XDUM              ROUTINE CODE LENGTH             <X>
*- - - - - - - - - - - - - - - - - ATTENTION ENTRY CODE            <X>
         USING XWATTN,R15                                          <X>
XASKL    STM   R0,R15,0(R13)       SAVE ALL REGISTERS              <X>
         L     R9,XWARPT           "XWAREA" ADDRESS                <X>
         L     R15,XWAREP          "XATTN" ADDRESS                 <X>
         BR    R15                 GO TO ATTENTION ROUTINE         <X>
         CNOP  0,4                                                 <X>
XASKLL   EQU   *-XASKL             ENTRY CODE LENGTH               <X>
         DROP  R15                                                 <X>
         SPACE 1                                                   <X>
*- - - - - - - - - - - - - - - - - ABEND ENTRY CODE                <X>
         USING XWABND,R15                                          <X>
XXSKL    STM   R0,R15,XWXSVR       SAVE ALL REGISTERS              <X>
         L     R9,XWXAPT           "XWAREA" ADDRESS                <X>
         L     R12,XWXAEP          "XATTN" ADDRESS                 <X>
         BR    R12                 GO TO ABEND EXIT                <X>
         CNOP  0,4                                                 <X>
XXSKLL   EQU   *-XXSKL             ENTRY CODE LENGTH               <X>
         DROP  R15                                                 <X>
         SPACE 1                                                   <X>
XCHK     DC    0F'0',AL1(ATBSEP+ATBLLKR,0),AL2(1)                  <X>
XSKEL   ESTAE  *-*,TERM=YES,MF=L                                   <X>
XSKELL   EQU   *-XSKEL             ESTAE LIST LENGTH               <X>
         PRINT &PRS                                                <X>
        LTORG  ,                                                   <X>
         PRINT &PRF                                                <X>
         DROP  R12,R13                                             <X>
         EJECT ,                                                   <X>
*------- EXCP ATTENTION ROUTINE ---------------------------------* <X>
         CNOP  0,4                                                 <X>
         USING *,R15               ESTABLISH LOCAL ADDRESSABILITY  <X>
         USING IOSB,R1                                             <X>
         USING XWAREA,R9                                           <X>
XATTN    CLC   XWUCB,IOSUCB        UCB ADDRESS MATCH?              <X>
         BNE   XATTNX              NO, IGNORE                      <X>
         L     R8,XWMYSV           "MYSAVE" ADDRESS                <X>
         USING SAVE2,R8                                            <X>
         TM    ATTNSW,ATTWAIT      WAITING ATTENTION?              <X>
         BZ    XATTNX              NO, IGNORE                      <X>
         NI    ATTNSW,255-ATTWAIT  YES, RESET WAITING ATTENTION    <X>
         L     R10,=A(X'40000000') GET POST COMPLETION CODE        <X>
         LA    R11,RECB            ECB ADDRESS                     <X>
        POST   (11),(10),BRANCH=YES DO POST ECB                    <X>
         USING PSA,R0                                              <X>
XATTNX   LM    R0,R15,0(R13)       RESTORE ALL REGISTERS           <X>
         XR    R15,R15             SET RETURN CODE                 <X>
         B     PSARET              AND RETURN                      <X>
         PRINT &PRS                                                <X>
        LTORG  ,                                                   <X>
         PRINT &PRF                                                <X>
         DROP  R0,R1,R8,R9,R15                                     <X>
         EJECT ,                                                   <X>
*------- ESTAE EXIT (ABEND EXIT) --------------------------------* <X>
         CNOP  0,8                                                 <X>
         USING *,R12               ESTABLISH LOCAL ADDRESSABILITY  <X>
         USING XWAREA,R9                                           <X>
XABEND   L     R8,XWMYSV           "MYSAVE" ADDRESS                <X>
         USING SAVE2,R8                                            <X>
         TM    ATTNSW,ATTRSET      IS ATTENTION ROUTINE ACTIVE?    <X>
         BZ    XABNDX              NO                              <X>
         NI    ATTNSW,255-ATTRSET-ATTWAIT YES, RESET ALL           <X>
         LM    R2,R3,XWRST         RESET ATTENTION ROUTINE ENTRY   <X>
         L     R4,XWATP                                            <X>
         L     R5,XWUCB                                            <X>
         XR    R6,R6               NULLIFY ATTENTION INDEX         <X>
        MODESET MF=(E,SUPRMOD)                                     <X>
         STM   R2,R3,0(R4)         RESET ATTENTION TABLE ENTRY     <X>
        IOSGEN TP,UCB=(R5),VAR=ATNMOD,TABLE=(R6) RESET ATTN        <X>
        MODESET MF=(E,PROBMOD)                                     <X>
XABNDX  ESTAE  0,TOKEN=XWTOKEN     CANCEL ETPS ESTAE LEVEL         <X>
         LM    R0,R15,XWXSVR       RESTORE ALL REGISTERS           <X>
         XR    R15,R15             SET RETURN CODE                 <X>
         BR    R14                 AND RETURN                      <X>
         SPACE 1                                                   <X>
         DROP  R8,R9,R12                                           <X>
.SKX5    EJECT
*------- SCREEN FORMATS ---------------------------------------------*
*  THE BUFFER ALWAYS STARTS WITH A WRITE CONTROL CHARACTER (WCC) :
*        X'C7' = SOUND ALARM, RESET KEYBOARD, RESET MDT (MODIFIED
*                DATA TAG) BITS
*        X'C3' = RESET KEYBOARD, RESET MDT BITS
*  AFTER THE WCC, FOLLOWS ORDER CODES AND DATA SEQUENCES.
*  USED ORDERS ARE :
*        X'11' = SET BUFFER ADDRESS (SBA), FOLLOWED BY 2-BYTE BUFFER
*                ADDRESS
*        X'13' = INSERT CURSOR (IC)
*        X'1D' = START FIELD (SF), FOLLOWED BY 1-BYTE ATTRIBUTE CHAR.
         SPACE 1
*- - - - LOGON SCREEN
         CNOP  0,4
LOGSCR   DC    A(LOGSC1)
         DC    A(LNEBLK)
         DC    A(LOGSC2)
         DC    A(LOGSC3)
         DC    A(LOGSC4)
         DC    A(LOGSC5)
         DC    A(LOGSC6)
         DC    A(LOGSC7)
         DC    A(LOGSC8)
         DC    A(LOGSC9)
         DC    A(LOGSC10)
         DC    A(LOGSC11)
         DC    A(LOGSC12)
         DC    A(LOGSC13)
         DC    A(LOGSC14)
         DC    A(LNEBLK)
CRLUSC   EQU   (*-LOGSCR)/4        CURSOR ROW ON LINE BELOW
ZIL      EQU   (*-LOGSCR)+4        "TERMINPT" DISPLACEMENT
         DC    A(LOGSC15)
CRLPSC   EQU   (*-LOGSCR)/4        CURSOR ROW ON LINE BELOW
         DC    A(LOGSC16)
CRLGSC   EQU   (*-LOGSCR)/4        CURSOR ROW ON LINE BELOW
         DC    A(LOGSC17)
         DC    A(LOGSC18)
         DC    A(LNEBLK)
         DC    A(EML+HLPLNE)
         SPACE 1
LOGSC1   DC    AL1(LOGSC1L-1)
         DC    XL1'05',10CL1'-'    PROTECT, HIGH-LIGHT
         DC    CL20' ETPS - LOGON PANEL ',49CL1'-'
LOGSC1L  EQU   *-LOGSC1
LOGSC2   DC    AL1(LOGSC2L-1),XL1'04',CL16'    E  T  P  S  '
*     ----+----1----+----2----+----3----+----4----+----5----+----6
 DC C'                    (((-----)))'
LOGSC2L  EQU   *-LOGSC2
LOGSC3   DC    AL1(LOGSC3L-1),XL1'04',CL16'    M  E  R  Y  '
 DC C'                  ** (       )**'
LOGSC3L  EQU   *-LOGSC3
LOGSC4   DC    AL1(LOGSC4L-1),XL1'04',CL16'    E  L  O  S  '
 DC C'                 *( (        ) )*'
LOGSC4L  EQU   *-LOGSC4
LOGSC5   DC    AL1(LOGSC5L-1),XL1'04',CL16'    R  E  C  T  '
 DC C'               ((                )'
LOGSC5L  EQU   *-LOGSC5
LOGSC6   DC    AL1(LOGSC6L-1),XL1'04',CL16'    G     E  E  '
*     ----+----1----+----2----+----3----+----4----+----5----+----6
* ===>              (  < (O) > < (O) >   )                        <===
 DC C'              (  <',X'05'
 DC C'(O)',XL1'04'
 DC C'> <',XL1'05'
 DC C'(O)',XL1'04'
 DC C'>   )'
LOGSC6L  EQU   *-LOGSC6
LOGSC7   DC    AL1(LOGSC7L-1),XL1'04',CL16'    E     S  M  '
 DC C'               ((       (       )))'
LOGSC7L  EQU   *-LOGSC7
LOGSC8   DC    AL1(LOGSC8L-1),XL1'04',CL16'    N     S     '
 DC C'               ((               )))'
LOGSC8L  EQU   *-LOGSC8
LOGSC9   DC    AL1(LOGSC9L-1),XL1'04',CL16'    C     I     '
 DC C'                ((((    ___   ))))'
LOGSC9L  EQU   *-LOGSC9
LOGSC10  DC    AL1(LOGSC10L-1),XL1'04',CL16'    Y     N     '
 DC C'                 ((((        ))))'
LOGSC10L EQU   *-LOGSC10
LOGSC11  DC    AL1(LOGSC11L-1),XL1'04',CL16'          G     '
 DC C'                    ((      ))'
LOGSC11L EQU   *-LOGSC11
LOGSC12  DC    AL1(LOGSC12L-1),XL1'04',CL16'                '
 DC C'                      )    / '
LOGSC12L EQU   *-LOGSC12
LOGSC13  DC    AL1(LOGSC13L-1),XL1'04',CL16'                '
         AIF   ('&TXV' NE 'EUROPE').SK3
 DC C'                      |( (|'
         AGO   .SK4
.SK3     ANOP
 DC C'                      !( (!'
.SK4     ANOP
LOGSC13L EQU   *-LOGSC13
LOGSC14  DC    AL1(LOGSC14L-1),XL1'04',CL16'                '
         AIF   ('&TXV' NE 'EUROPE').SK5
 DC C'                      |) )|'
         AGO   .SK6
.SK5     ANOP
 DC C'                      !) )!'
.SK6     ANOP
LOGSC14L EQU   *-LOGSC14
LOGSC15  DC    AL1(LOGSC15L-1),XL1'04',CL16'                '
LUOFF    EQU   *-LOGSC15
         DC    CL13'ENTER USER-ID',XL1'05',CL4'===>'
CCLUSC   EQU   *-LOGSC15           CURSOR COLUMN ON FIELD BELOW
LGSCF1   EQU   (*-LOGSC15)+1,7     INPUT FIELD 1
         DC    XL1'01',CL7' '      SF = UNPROT
LTOFF    EQU   (*-LOGSC15)+1
         DC    XL1'04',CL20' '
LOGSC15L EQU   *-LOGSC15
LOGSC16  DC    AL1(LOGSC16L-1),XL1'04',CL16'                '
LPOFF    EQU   *-LOGSC16
         DC    CL13'     PASSWORD',XL1'05',CL4'===>'
CCLPSC   EQU   *-LOGSC16           CURSOR COLUMN ON FIELD BELOW
LGSCF2   EQU   (*-LOGSC16)+1,8     INPUT FIELD 2
         DC    XL1'03',CL8' '      SF = UNPROT, NO DISPLAY
         DC    XL1'04',CL1' '
LOGSC16L EQU   *-LOGSC16
LOGSC17  DC    AL1(LOGSC17L-1),XL1'04',CL16'                '
LGOFF    EQU   *-LOGSC17
         DC    CL13'   RACF GROUP',XL1'05',CL4'===>'
CCLGSC   EQU   *-LOGSC17           CURSOR COLUMN ON FIELD BELOW
LGSCF3   EQU   (*-LOGSC17)+1,8     INPUT FIELD 3
         DC    XL1'01',CL8' '      SF = UNPROT
         DC    XL1'04',CL13' '
         DC    X'04',CL9'VERSION :'
         DC    X'05',CL8'&VNUM..&VMOD (&VIDT)',XL1'04'
LOGSC17L EQU   *-LOGSC17
LOGSC18  DC    AL1(LOGSC18L-1),XL1'05',79CL1'-'
LOGSC18L EQU   *-LOGSC18
LNEBLK   DC    AL1(LNEBLKL-1),XL1'04',CL9' ' SF = PROT
LNEBLKL  EQU   *-LNEBLK
         EJECT
*- - - - LOGON HELP SCREEN
         CNOP  0,4
LOGHELP  DC    A(LOGHL1)
         DC    A(HLPENT)
         DC    A(LOGHL2)
         DC    A(LOGHL3)
         DC    A(LOGHL4)
         DC    A(LOGHL5)
         DC    A(LNEBLK)
         DC    A(EML+LOGHL6)
         SPACE 1
LOGHL1   DC    AL1(LOGHL1L-1)
         DC    XL1'05',10CL1'-'    PROTECT, HIGH-LIGHT
         DC    CL20' ETPS - LOGON PANEL ',49CL1'-'
LOGHL1L  EQU   *-LOGHL1
*                ----+----1----+----2----+----3----+----4----+----5----
*              +----6----+----7----+----
LOGHL2   DC    AL1(LOGHL2L-1),XL1'05'
         DC    C'THE ETPS LOGON PANEL IS PROVIDED TO ALLOW A USERID TO X
               BE ENTERED AND VERIFIED'
LOGHL2L  EQU   *-LOGHL2
LOGHL3   DC    AL1(LOGHL3L-1),XL1'05'
         DC    C'    TO LIMIT THE POSSIBILITY OF UNAUTHORIZED USE.'
LOGHL3L  EQU   *-LOGHL3
LOGHL4   DC    AL1(LOGHL4L-1),XL1'05'
         DC    C'    THE PASSWORD FIELD IS NOT VERIFIED BY ETPS, AS SHIX
               PPED.'                                      '
LOGHL4L  EQU   *-LOGHL4
LOGHL5   DC    AL1(LOGHL5L-1),XL1'05'
         DC    C'    IF RACF IS INSTALLED, THE RACF GROUP IS THE DEFAULX
               T ONE IF NOT SUPPLIED.'
LOGHL5L  EQU   *-LOGHL5
LOGHL6   DC    AL1(LOGHL6L-1),XL1'05'
         DC    C'PF3, OR ENTERING "EOJ" IN THE USERID FIELD LOGS YOU OFX
               F.'
LOGHL6L  EQU   *-LOGHL6
HLPENT   DC    AL1(HLPENTL-1),XL1'01',CL1' ',XL1'05',CL7' '
HLPENTL  EQU   *-HLPENT
HLPLNE   DC    AL1(HLPLNEL-1)
         DC    XL1'05',C'PF1'
         DC    XL1'04',C'IS ALWAYS'
         DC    XL1'05',C'HELP'
HLPLNEL  EQU   *-HLPLNE
         EJECT
*- - - - PRIMARY OPTION MENU SCREEN
         CNOP  0,4
PRIMOPT  DC    A(PRIML1)
         DC    A(PRIML2)
CRPOSC   EQU   (*-PRIMOPT)/4       CURSOR ROW ON LINE BELOW
ZIP      EQU   (*-PRIMOPT)+4       "TERMINPT" DISPLACEMENT
         DC    A(PRIML3)
         DC    A(PRIML4)
         DC    A(PRIML5)
         DC    A(PRIML6)
         DC    A(PRIML7)
         DC    A(PRIML8)
         DC    A(PRIML9)
         DC    A(LNEBLK)
         DC    A(PRIML10)
         DC    A(EML+HLPLNE)
         SPACE 1
PRIML1   DC    AL1(PRIML1L-1)
         DC    XL1'05',10CL1'-'    SF PROTECTED, HIGH-LIGHT
         DC    CL28' ETPS - PRIMARY OPTION MENU ',41CL1'-'
PRIML1L  EQU   *-PRIML1
PRIML2   DC    AL1(PRIML2L-1)
PROFF1   EQU   *-PRIML2
         DC    XL1'04',CL9'USER-ID :' SF PROTECTED
UOFF     EQU   (*-PRIML2)+1,7
         DC    XL1'05',CL7' '      SF PROTECTED, HIGH-LIGHT
         DC    XL1'04',CL1' '      SF PROTECTED
         DC    XL1'04',CL10'GROUP-ID :' SF PROTECTED
GROFF    EQU   (*-PRIML2)+1,8
         DC    XL1'05',CL8' '      SF PROTECTED, HIGH-LIGHT
         DC    XL1'04',CL10'TERMINAL :' SF PROTECTED
TERMOFF  EQU   (*-PRIML2)+1,8
         DC    XL1'05',CL8' '      SF PROTECTED, HIGH-LIGHT
         DC    XL1'04',CL10' VERSION :' SF PROTECTED
         DC    XL1'05',CL8'&VNUM..&VMOD (&VIDT)' SF PROTECTED, HIGH-L.
PRIML2L  EQU   *-PRIML2
PRIML3   DC    AL1(PRIML3L-1)
PROFF2   EQU   *-PRIML3
         DC    XL1'05'             SF PROTECTED, HIGH-LIGHT
         DC    CL18'SELECT OPTION ===>'
CCPOSC   EQU   (*-PRIML3)          CURSOR COLUMN ON FIELD BELOW
POSCF1   EQU   (*-PRIML3)+1,6      INPUT FIELD 1
         DC    XL1'01',CL6' '      SF UNPROTECTED
         DC    XL1'04',CL18' ',CL6'TIME =' SF PROTECTED
TIMEOFF  EQU   (*-PRIML3)+1,5
         DC    XL1'05',CL5'  :  '  SF PROTECTED, HIGH-LIGHT
         DC    XL1'04',CL7' ',CL6'DATE =' SF PROTECTED
DATEOFF  EQU   (*-PRIML3)+1,8
         DC    XL1'05',CL8'  /  /  ' SF PROTECTED, HIGH-LIGHT
PRIML3L  EQU   *-PRIML3
PRIML4   DC    AL1(PRIML4L-1)
         DC    XL1'05',CL2' 1'     SF PROTECTED, HIGH-LIGHT
         DC    XL1'04'             SF PROTECTED
         DC    CL36'BROWSE DATA '
         DC    XL1'05',CL2' 2'     SF PROTECTED, HIGH-LIGHT
         DC    XL1'04'             SF PROTECTED
         DC    CL36'EDIT SOURCE DATA '
PRIML4L  EQU   *-PRIML4
PRIML5   DC    AL1(PRIML5L-1)
         DC    XL1'05',CL2' 3'     SF PROTECTED, HIGH-LIGHT
         DC    XL1'04'             SF PROTECTED
         DC    CL36'LIBRARY UTILITY '
         DC    XL1'05',CL2' 4'     SF PROTECTED, HIGH-LIGHT
         DC    XL1'04'             SF   PROTECTED
         DC    CL36'IDCAMS (VSAM) INTERFACE '
PRIML5L  EQU   *-PRIML5
PRIML6   DC    AL1(PRIML6L-1)
         DC    XL1'05',CL2' 5'     SF PROTECTED, HIGH-LIGHT
         DC    XL1'04'             SF   PROTECTED
         DC    CL36'ZAP (DISPLAY/MODIFY DATA-SET) '
         DC    XL1'05',CL2' 6'     SF PROTECTED, HIGH-LIGHT
         DC    XL1'04'             SF   PROTECTED
         DC    CL36'INCORZAP (IN-CORE SUPERZAP FOR MVS) '
PRIML6L  EQU   *-PRIML6
PRIML7   DC    AL1(PRIML7L-1)
         DC    XL1'05',CL2' '      SF PROTECTED, HIGH-LIGHT
         DC    XL1'04'             SF   PROTECTED
         DC    CL36' '
         DC    XL1'05',CL2' '      SF PROTECTED, HIGH-LIGHT
         DC    XL1'04'             SF   PROTECTED
         DC    CL36' '
PRIML7L  EQU   *-PRIML7
PRIML8   DC    AL1(PRIML8L-1)
         DC    XL1'05',CL2' '      SF PROTECTED, HIGH-LIGHT
         DC    XL1'04'             SF   PROTECTED
         DC    CL36' '
         DC    XL1'05',CL2' '      SF PROTECTED, HIGH-LIGHT
         DC    XL1'04'             SF   PROTECTED
         DC    CL36' '
PRIML8L  EQU   *-PRIML8
PRIML9   DC    AL1(PRIML9L-1)
         DC    XL1'05',CL2' X'     SF PROTECTED, HIGH-LIGHT
         DC    XL1'04'             SF PROTECTED
         DC    CL22'EXIT - TERMINATE ETPS '
MSGOFF1  EQU   *-PRIML9,31
         DC    CL31'AND RETURN TO THE LOGON PANEL.'
PRIML9L  EQU   *-PRIML9
PRIML10  DC    AL1(PRIML10L-1)
         DC    XL1'04'             SF PROTECTED
         DC    CL5'PRESS '
         DC    XL1'05'             SF PROTECTED, HIGH-LIGHT
         DC    CL3'PF3'
         DC    XL1'04'             SF PROTECTED
         DC    CL18'TO TERMINATE ETPS '
MSGOFF2  EQU   *-PRIML10,34
         DC    CL34'AND RETURN TO THE LOGON PANEL.'
PRIML10L EQU   *-PRIML10
         EJECT
*- - - - PRIMARY OPTION MENU HELP SCREEN
         CNOP  0,4
POPHELP  DC    A(POPHL1)
         DC    A(HLPENT)
         DC    A(POPHL2)
         DC    A(POPHL3)
         DC    A(LNEBLK)
         DC    A(POPHX1)
         DC    A(LNEBLK)
         DC    A(POPHL4)
         DC    A(POPHL5)
         DC    A(EML+POPHL6)
POPHELPA DC    A(POPHL1)
         DC    A(HLPENT)
         DC    A(POPHL2)
         DC    A(POPHL3)
         DC    A(LNEBLK)
         DC    A(POPHX2)
         DC    A(LNEBLK)
         DC    A(POPHL4)
         DC    A(POPHL5)
         DC    A(EML+POPHL6)
         SPACE 1
POPHL1   DC    AL1(POPHL1L-1)
         DC    XL1'05',10CL1'-'    PROTECT, HIGH-LIGHT
         DC    CL28' ETPS - PRIMARY OPTION MENU ',41CL1'-'
POPHL1L  EQU   *-POPHL1
*                ----+----1----+----2----+----3----+----4----+----5----
*              +----6----+----7----+----
POPHL2   DC    AL1(POPHL2L-1),XL1'05'
         DC    C'THE PRIMARY OPTION MENU SHOWS WHAT ETPS FUNCTIONS AVAIX
               LABLE.'
POPHL2L  EQU   *-POPHL2
POPHL3   DC    AL1(POPHL3L-1),XL1'05'
         DC    C'    THE OPTIONS ARE INVOKED BY ENTERING THE OPTION NUMX
               BER IN THE COMMAND LINE.'
POPHL3L  EQU   *-POPHL3
POPHL4   DC    AL1(POPHL4L-1),XL1'05'
         DC    C'GENERAL USE OF SOME PF-PA KEYS :'
POPHL4L  EQU   *-POPHL4
POPHL5   DC    AL1(POPHL5L-1),XL1'05'
         DC    C'        PF2=SPLIT SCREEN (HALF)   PF9=SWITCH SCREEN'
POPHL5L  EQU   *-POPHL5
POPHL6   DC    AL1(POPHL6L-1),XL1'05'
         DC    C'        PA1=CANCEL PROCESS        PA2=RESHOW SCREEN'
POPHL6L  EQU   *-POPHL6
POPHX1   DC    AL1(POPHX1L-1),XL1'05'
         DC    C'PF3, OR ENTERING "X" IN THE COMMAND LINE RETURNS TO THX
               E LOGON PANEL.'
POPHX1L  EQU   *-POPHX1
POPHX2   DC    AL1(POPHX2L-1),XL1'05'
         DC    C'PF3, OR ENTERING "X" IN THE COMMAND LINE TERMINATES ETX
               PS.'
POPHX2L  EQU   *-POPHX2
         EJECT
*- - - - BROWSE ENTRY SCREEN
         CNOP  0,4
BROWSCR  DC    A(BRROW1)
         DC    A(BRROW2)
CRBRSC   EQU   (*-BROWSCR)/4       CURSOR ROW ON LINE BELOW
ZIB1     EQU   (*-BROWSCR)+4       "TERMINPT" DISPLACEMENT
         DC    A(BRROW3)
ZIB2     EQU   (*-BROWSCR)+4       "TERMINPT" DISPLACEMENT
         DC    A(BRROW4)
         DC    A(LNEBLK)
ZIB3     EQU   (*-BROWSCR)+4       "TERMINPT" DISPLACEMENT
         DC    A(EML+BRROW5)
         SPACE 1
BRROW1   DC    AL1(BRROW1L-1)
         DC    XL1'05',10CL1'-'    PROTECT, HIGH-LIGHT
         DC    CL22' BROWSE - ENTRY PANEL ',47CL1'-'
BRROW1L  EQU   *-BRROW1
BRROW2   DC    AL1(BRROW2L-1)
         DC    XL1'05',CL30'ENTER/VERIFY PARAMETERS BELOW:'
         DC    XL1'04',CL1' '
BRROW2L  EQU   *-BRROW2
BRROW3   DC    AL1(BRROW3L-1)
BROFF1   EQU   (*-BRROW3)+1,10
         DC    XL1'04',CL20'FULLY-QUALIFIED NAME',XL1'05',CL3'==>'
CCBRSC   EQU   (*-BRROW3)          CURSOR COLUMN ON FIELD BELOW
BRSCF1   EQU   (*-BRROW3)+1,54     INPUT FIELD 1
         DC    XL1'02',CL54' '
BRROW3L  EQU   *-BRROW3
BRROW4   DC    AL1(BRROW4L-1)
BROFF2   EQU   (*-BRROW4)+10,13
         DC    XL1'04',CL22'         VOLUME SERIAL'
         DC    XL1'05',CL3'==>'
BRSCF2   EQU   (*-BRROW4)+1,6      INPUT FIELD 2
         DC    XL1'02',CL6' '
         DC    XL1'04',CL45'          (IF NOT CATALOGED)'
BRROW4L  EQU   *-BRROW4
BRROW5   DC    AL1(BRROW5L-1)
BROFF3   EQU   (*-BRROW5)+6,17
         DC    XL1'04',CL22'     DATA-SET PASSWORD'
         DC    XL1'05',CL3'==>'
BRSCF3   EQU   (*-BRROW5)+1,8      INPUT FIELD 3
         DC    XL1'03',CL8' '      NO DISPLAY
         DC    XL1'04',CL37'        (IF PASSWORD PROTECTED)'
BROFF4   EQU   (*-BRROW5)+1,3
         DC    XL1'04',CL3'(N)',XL1'04',CL1' '
BRROW5L  EQU   *-BRROW5
         EJECT
*- - - - BROWSE ENTRY HELP SCREEN
         CNOP  0,4
BROHELP  DC    A(BROHL1)
         DC    A(HLPENT)
         DC    A(BROHL2)
         DC    A(BROHL3)
         DC    A(BROHL4)
         DC    A(LNEBLK)
         DC    A(EML+BROHL5)
         SPACE 1
BROHL1   DC    AL1(BROHL1L-1)
         DC    XL1'05',10CL1'-'    PROTECT, HIGH-LIGHT
         DC    CL22' BROWSE - ENTRY PANEL ',47CL1'-'
BROHL1L  EQU   *-BROHL1
*                ----+----1----+----2----+----3----+----4----+----5----
*              +----6----+----7----+----
BROHL2   DC    AL1(BROHL2L-1),XL1'05'
         DC    C'THE BROWSE PANEL PROVIDES FOR ENTERING THE DATA-SET NAX
               ME, WHICH MUST BE A'
BROHL2L  EQU   *-BROHL2
BROHL3   DC    AL1(BROHL3L-1),XL1'05'
         DC    C'    FULLY-QUALIFIED DS-NAME (NEVER DELIMITED BY QUOTESX
               ).'
BROHL3L  EQU   *-BROHL3
BROHL4   DC    AL1(BROHL4L-1),XL1'05'
         DC    C'    A SPECIFIC VOL-SER MAY ALSO BE ENTERED, AND A PASSX
               WORD.'
BROHL4L  EQU   *-BROHL4
BROHL5   DC    AL1(BROHL5L-1),XL1'05'
         DC    C'PF3 RETURNS TO THE PRIMARY OPTION MENU PANEL.'
BROHL5L  EQU   *-BROHL5
         EJECT
*- - - - EDIT ENTRY SCREEN
         CNOP  0,4
EDITSCR  DC    A(EDROW1)
         DC    A(EDROW2)
CREDSC   EQU   (*-EDITSCR)/4       CURSOR ROW ON LINE BELOW
         DC    A(EDROW3)
         DC    A(EDROW4)
         DC    A(LNEBLK)
         DC    A(EML+EDROW5)
         SPACE 1
EDROW1   DC    AL1(EDROW1L-1)
         DC    XL1'05',10CL1'-'    PROTECT, HIGH-LIGHT
         DC    CL20' EDIT - ENTRY PANEL ',49CL1'-'
EDROW1L  EQU   *-EDROW1
EDROW2   DC    AL1(EDROW2L-1)
         DC    XL1'05',CL30'ENTER/VERIFY PARAMETERS BELOW:'
         DC    XL1'04',CL1' '
EDROW2L  EQU   *-EDROW2
EDROW3   DC    AL1(EDROW3L-1)
EDOFF1   EQU   (*-EDROW3)+1,10
         DC    XL1'04',CL20'FULLY-QUALIFIED NAME',XL1'05',CL3'==>'
CCEDSC   EQU   (*-EDROW3)          CURSOR COLUMN ON FIELD BELOW
EDSCF1   EQU   (*-EDROW3)+1,54     INPUT FIELD 1
         DC    XL1'02',CL54' '
EDROW3L  EQU   *-EDROW3
EDROW4   DC    AL1(EDROW4L-1)
EDOFF2   EQU   (*-EDROW4)+10,13
         DC    XL1'04',CL22'         VOLUME SERIAL'
         DC    XL1'05',CL3'==>'
EDSCF2   EQU   (*-EDROW4)+1,6      INPUT FIELD 2
         DC    XL1'02',CL6' '
         DC    XL1'04',CL45'          (IF NOT CATALOGED)'
EDROW4L  EQU   *-EDROW4
EDROW5   DC    AL1(EDROW5L-1)
EDOFF3   EQU   (*-EDROW5)+6,17
         DC    XL1'04',CL22'     DATA-SET PASSWORD'
         DC    XL1'05',CL3'==>'
EDSCF3   EQU   (*-EDROW5)+1,8      INPUT FIELD 3
         DC    XL1'03',CL8' '      NO DISPLAY
         DC    XL1'04',CL37'        (IF PASSWORD PROTECTED)'
EDOFF4   EQU   (*-EDROW5)+1,3
         DC    XL1'04',CL3'(N)',XL1'04',CL1' '
EDROW5L  EQU   *-EDROW5
         SPACE 1
        CKSCF  BRSCF1,EDSCF1       CHECK : THESE
        CKSCF  BRSCF2,EDSCF2               VALUES
        CKSCF  BRSCF3,EDSCF3               MUST BE
        CKSCF  BROFF1,EDOFF1               EQUALS
        CKSCF  BROFF2,EDOFF2
        CKSCF  BROFF3,EDOFF3
        CKSCF  BROFF4,EDOFF4
         EJECT
*- - - - EDIT ENTRY HELP SCREEN
         CNOP  0,4
EDTHELP  DC    A(EDTHL1)
         DC    A(HLPENT)
         DC    A(EDTHL2)
         DC    A(EDTHL3)
         DC    A(EDTHL4)
         DC    A(LNEBLK)
         DC    A(EML+EDTHL5)
         SPACE 1
EDTHL1   DC    AL1(EDTHL1L-1)
         DC    XL1'05',10CL1'-'    PROTECT, HIGH-LIGHT
         DC    CL20' EDIT - ENTRY PANEL ',49CL1'-'
EDTHL1L  EQU   *-EDTHL1
*                ----+----1----+----2----+----3----+----4----+----5----
*              +----6----+----7----+----
EDTHL2   DC    AL1(EDTHL2L-1),XL1'05'
         DC    C'THE EDIT PANEL PROVIDES FOR ENTERING THE DATA-SET NAMEX
               , WHICH MUST BE A'
EDTHL2L  EQU   *-EDTHL2
EDTHL3   DC    AL1(EDTHL3L-1),XL1'05'
         DC    C'    FULLY-QUALIFIED DS-NAME (NEVER DELIMITED BY QUOTESX
               ).'
EDTHL3L  EQU   *-EDTHL3
EDTHL4   DC    AL1(EDTHL4L-1),XL1'05'
         DC    C'    A SPECIFIC VOL-SER MAY ALSO BE ENTERED, AND A PASSX
               WORD.'
EDTHL4L  EQU   *-EDTHL4
EDTHL5   DC    AL1(EDTHL5L-1),XL1'05'
         DC    C'PF3 RETURNS TO THE PRIMARY OPTION MENU PANEL.'
EDTHL5L  EQU   *-EDTHL5
         EJECT
*- - - - LIBRARY UTILITY ENTRY SCREEN
         CNOP  0,4
UTLMENU  DC    A(UTROW1)
CRUTSC   EQU   (*-UTLMENU)/4       CURSOR ROW ON LINE BELOW
ZIUE     EQU   (*-UTLMENU)+4       "TERMINPT" DISPLACEMENT
         DC    A(UTROW2)
         DC    A(UTROW3)
         DC    A(UTROW4)
         DC    A(LNEBLK)
         DC    A(UTROW5)
CRUTSN   EQU   (*-UTLMENU)/4       CURSOR ROW ON LINE BELOW
ZIU1     EQU   (*-UTLMENU)+4       "TERMINPT" DISPLACEMENT
         DC    A(UTROW6)
ZIU2     EQU   (*-UTLMENU)+4       "TERMINPT" DISPLACEMENT
         DC    A(UTROW7)
         DC    A(LNEBLK)
ZIU3     EQU   (*-UTLMENU)+4       "TERMINPT" DISPLACEMENT
         DC    A(EML+UTROW8)
         SPACE 1
UTROW1   DC    AL1(UTROW1L-1)
         DC    XL1'05',10CL1'-'    PROTECT, HIGH-LIGHT
         DC    CL23' UTILITY - ENTRY PANEL ',46CL1'-'
UTROW1L  EQU   *-UTROW1
UTROW2   DC    AL1(UTROW2L-1)
         DC    XL1'05'             SF PROTECTED, HIGH-LIGHT
         DC    CL18'SELECT OPTION ===>'
CCUTSC   EQU   (*-UTROW2)          CURSOR COLUMN ON FIELD BELOW
UTSCF1   EQU   (*-UTROW2)+1,6      INPUT FIELD 1
         DC    XL1'01',CL6' '      SF UNPROTECTED
         DC    XL1'04',CL1' '
UTROW2L  EQU   *-UTROW2
UTROW3   DC    AL1(UTROW3L-1)
         DC    XL1'04',CL3' ',XL1'05',CL5'BLANK'
         DC    XL1'04',CL25' - PDS MEMBERS LIST'
         DC    XL1'05',CL5'    C'
         DC    XL1'04',CL26' - PDS COMPRESS'
UTROW3L  EQU   *-UTROW3
UTROW4   DC    AL1(UTROW4L-1)
         DC    XL1'04',CL3' ',XL1'05',CL5' '
         DC    XL1'04',CL25' '
         DC    XL1'05',CL5'    S'
         DC    XL1'04',CL26' - DS STATISTICS'
UTROW4L  EQU   *-UTROW4
UTROW5   DC    AL1(UTROW5L-1)
         DC    XL1'05',CL30'ENTER/VERIFY PARAMETERS BELOW:'
         DC    XL1'04',CL48' '
UTROW5L  EQU   *-UTROW5
UTROW6   DC    AL1(UTROW6L-1)
UTOFF1   EQU   (*-UTROW6)+1,10
         DC    XL1'04',CL20'FULLY-QUALIFIED NAME',XL1'05',CL3'==>'
CCUTSN   EQU   (*-UTROW6)          CURSOR COLUMN ON FIELD BELOW
UTSCF2   EQU   (*-UTROW6)+1,44     INPUT FIELD 2
         DC    XL1'02',CL44' '
         DC    XL1'04',CL1' '
UTROW6L  EQU   *-UTROW6
UTROW7   DC    AL1(UTROW7L-1)
UTOFF2   EQU   (*-UTROW7)+10,13
         DC    XL1'04',CL22'         VOLUME SERIAL'
         DC    XL1'05',CL3'==>'
UTSCF3   EQU   (*-UTROW7)+1,6      INPUT FIELD 3
         DC    XL1'02',CL6' '
         DC    XL1'04',CL45'          (IF NOT CATALOGED)'
UTROW7L  EQU   *-UTROW7
UTROW8   DC    AL1(UTROW8L-1)
UTOFF3   EQU   (*-UTROW8)+6,17
         DC    XL1'04',CL22'     DATA-SET PASSWORD'
         DC    XL1'05',CL3'==>'
UTSCF4   EQU   (*-UTROW8)+1,8      INPUT FIELD 4
         DC    XL1'03',CL8' '      NO DISPLAY
         DC    XL1'04',CL37'        (IF PASSWORD PROTECTED)'
UTOFF4   EQU   (*-UTROW8)+1,3
         DC    XL1'04',CL3'(N)',XL1'04',CL1' '
UTROW8L  EQU   *-UTROW8
         EJECT
*- - - - LIBRARY UTILITY ENTRY HELP SCREEN
         CNOP  0,4
UTLHELP  DC    A(UTLHL1)
         DC    A(HLPENT)
         DC    A(UTLHL2)
         DC    A(UTLHL3)
         DC    A(UTLHL4)
         DC    A(LNEBLK)
         DC    A(UTLHL5)
         DC    A(UTLHL6)
         DC    A(UTLHL7)
         DC    A(LNEBLK)
         DC    A(EML+UTLHL8)
         SPACE 1
UTLHL1   DC    AL1(UTLHL1L-1)
         DC    XL1'05',10CL1'-'    PROTECT, HIGH-LIGHT
         DC    CL23' UTILITY - ENTRY PANEL ',46CL1'-'
UTLHL1L  EQU   *-UTLHL1
*                ----+----1----+----2----+----3----+----4----+----5----
*              +----6----+----7----+----
UTLHL2   DC    AL1(UTLHL2L-1),XL1'05'
         DC    C'THE UTILITY PANEL PROVIDES FOR ENTERING THE DATA-SET NX
               AME, WHICH MUST BE A'
UTLHL2L  EQU   *-UTLHL2
UTLHL3   DC    AL1(UTLHL3L-1),XL1'05'
         DC    C'    FULLY-QUALIFIED DS-NAME (NEVER DELIMITED BY QUOTESX
               ).'
UTLHL3L  EQU   *-UTLHL3
UTLHL4   DC    AL1(UTLHL4L-1),XL1'05'
         DC    C'    A SPECIFIC VOL-SER MAY ALSO BE ENTERED, AND A PASSX
               WORD.'
UTLHL4L  EQU   *-UTLHL4
UTLHL5   DC    AL1(UTLHL5L-1),XL1'05'
         DC    C'THE OPTIONS ARE : " " FOR PDS MEMBERS LIST,'
UTLHL5L  EQU   *-UTLHL5
UTLHL6   DC    AL1(UTLHL6L-1),XL1'05'
         DC    C'                  "C" FOR PDS COMPRESS,'
UTLHL6L  EQU   *-UTLHL6
UTLHL7   DC    AL1(UTLHL7L-1),XL1'05'
         DC    C'                  "S" FOR DS STATISTICS.'
UTLHL7L  EQU   *-UTLHL7
UTLHL8   DC    AL1(UTLHL8L-1),XL1'05'
         DC    C'PF3 RETURNS TO THE PRIMARY OPTION MENU PANEL.'
UTLHL8L  EQU   *-UTLHL8
         EJECT
*- - - - LIBRARY MEMBERS DISPLAY HELP SCREEN
         CNOP  0,4
LIBHELP  DC    A(LIBHL1)
         DC    A(HLPENT)
         DC    A(LIBHL2)
         DC    A(LIBHL3)
         DC    A(LIBHL4)
         DC    A(LIBHL5)
         DC    A(LIBHL6)
         DC    A(LIBHL7)
         DC    A(LIBHL8)
         DC    A(LNEBLK)
         DC    A(LIBHL9)
         DC    A(LIBHL10)
         DC    A(LIBHL11)
         DC    A(LIBHL12)
         DC    A(LNEBLK)
         DC    A(LIBHL13)
         DC    A(LIBHL14)
         DC    A(EML+LIBHL15)
         SPACE 1
LIBHL1   DC    AL1(LIBHL1L-1)
         DC    XL1'05',10CL1'-'    PROTECT, HIGH-LIGHT
         DC    CL20' PDS MEMBER OPTIONS ',49CL1'-'
LIBHL1L  EQU   *-LIBHL1
LIBHL2   DC    AL1(LIBHL2L-1),XL1'05'
*                ----+----1----+----2----+----3----+----4----+----5----
*              +----6----+----7----+----
         DC    C'EXECUTING FUNCTIONS: "B"(BROWSE), "E"(EDIT) AND "U"(UTX
               ILITY)'
LIBHL2L  EQU   *-LIBHL2
LIBHL3   DC    AL1(LIBHL3L-1),XL1'05'
         DC    C'     S ---  BROWSE MEMBER (WHEN "B" AND "U"), EDIT MEMX
               BER (WHEN "E")'
LIBHL3L  EQU   *-LIBHL3
LIBHL4   DC    AL1(LIBHL4L-1),XL1'05'
         DC    C'     B ---  BROWSE MEMBER (WHEN "B")'
LIBHL4L  EQU   *-LIBHL4
LIBHL5   DC    AL1(LIBHL5L-1),XL1'05'
         DC    C'     E ---  EDIT MEMBER (WHEN "E")'
LIBHL5L  EQU   *-LIBHL5
LIBHL6   DC    AL1(LIBHL6L-1),XL1'05'
         DC    C'     D ---  DELETE MEMBER (WHEN "U")'
LIBHL6L  EQU   *-LIBHL6
LIBHL7   DC    AL1(LIBHL7L-1),XL1'05'
         DC    C'     R ---  RENAME MEMBER, REQUIRE "NEWNAME" (WHEN "U"X
               )'
LIBHL7L  EQU   *-LIBHL7
LIBHL8   DC    AL1(LIBHL8L-1),XL1'05'
         DC    C'     C ---  COPY MEMBER, REQUIRE "NEWNAME" (WHEN "U")'
LIBHL8L  EQU   *-LIBHL8
LIBHL9   DC    AL1(LIBHL9L-1),XL1'05'
         DC    C'COMMANDS ARE :'
LIBHL9L  EQU   *-LIBHL9
LIBHL10  DC    AL1(LIBHL10L-1),XL1'05'
         DC    C' L(OCATE) OR F(IND) ... (MEMBER NAME OR MEMBER NAMES SX
               TARTING BY THE CHARACTER'
LIBHL10L EQU    *-LIBHL10
LIBHL11  DC    AL1(LIBHL11L-1),XL1'05'
         DC    CL24' ',C'STRING ...)'
LIBHL11L EQU   *-LIBHL11
LIBHL12  DC    AL1(LIBHL12L-1),XL1'05'
         DC    C' UP, U OR -           / DOWN, D OR +         FOLLOWED X
               BY "M" OR A NUMBER'
LIBHL12L EQU   *-LIBHL12
LIBHL13  DC    AL1(LIBHL13L-1),XL1'05'
         DC    C'SCROLL : P(AGE) BY DEFAULT, H(ALF) OR C(SR)'
LIBHL13L EQU   *-LIBHL13
LIBHL14  DC    AL1(LIBHL14L-1),XL1'05'
         DC    C'PFK''S : 7=UP AND 8=DOWN'
LIBHL14L EQU   *-LIBHL14
LIBHL15  DC    AL1(LIBHL15L-1),XL1'05'
         DC    C'        PF3 RETURNS TO THE PREVIOUS BROWSE, EDIT OR UTX
               ILITY ENTRY PANEL.'
LIBHL15L EQU   *-LIBHL15
         EJECT
         PRINT &PRS
        SPLTAREA
         SPACE 1
        MYSAVE
         SPACE 1
        DIRMASK
         SPACE 1
        SPFMT
         SPACE 1
        PARMLIST
         SPACE 1
        FDATE
         AIF   ('&EATL' EQ '').SKX6                                <X>
        XAREA  ,                                                   <X>
.SKX6    PRINT &PRF
        CVT    DSECT=YES,LIST=YES
        IHAPSA DSECT=YES,LIST=YES
        IEFJESCT ,                 JES VECTOR TABLE
        IEFJSCVT ,                 SUB-SYSTEM COMM. VECTOR TABLE
        IHAACEE
        IHAASCB DSECT=YES,LIST=YES
        IHAASXB DSECT=YES,LIST=YES
        IKJTCB
TIOT     DSECT
        IEFTIOT1
        IKJTSB
        IEZJSCB
        IEFZB4D0
        IEFZB4D2
        IHAPDS PDSBLDL=NO
        DCBD   DSORG=(PS,PO),DEVD=DA
        IECSDSL1 (1)
DSCBLGTH EQU   DS1END-IECSDSL1
        IEZIOB
        IHAECB
         AIF   ('&EATL' EQ '').SKX7                                <X>
        IEZDEB LIST=YES                                            <X>
        IECDATB ,                                                  <X>
ATBELL   EQU   *-ATB               ATTENTION TABLE ENTRY LENGTH    <X>
        IECDIOCM ,                                                 <X>
UCB      DSECT ,                                                   <X>
        IEFUCBOB ,                                                 <X>
        IECDIOSB ,                                                 <X>
.SKX7    PRINT GEN
         END
