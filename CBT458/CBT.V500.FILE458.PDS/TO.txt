TOCP     TITLE '-   T O   - TSO-COMMAND.'
TO       START 0
         SPACE 1
* AUTHOR : BILL GODFREY, PLANNING RESEARCH CORPORATION.
* -------- INSTALLATION : PRC COMPUTER CENTER INC, MCLEAN VA.
*          DATE WRITTEN : MARCH 5 1976.
*          DATE UPDATED : DECEMBER 11 1980.
*                         11DEC80 - CHANGE SS01 TO TSSO.
*                         11DEC80 - 3-BYTE USERID NO LONGER ASSUMED
*                                   IN MSG05, MSG07.
*          ATTRIBUTES : RE-ENTRANT.
*
* DESCRIPTION : THIS TSO COMMAND SENDS A MESSAGE TO A TSO USER.
* ------------- IT DIFFERS FROM THE 'SEND' COMMAND IN THAT THE
*               MESSAGE IS NOT IN QUOTES, SO YOU DON'T HAVE TO
*               THINK ABOUT QUOTES WITHIN YOUR MESSAGE.
*
*               THE SYNTAX IS : TO USERID MESSAGE-TEXT
*
*               EXAMPLES : TO JONES WHERE'S THAT REPORT
*                          TO (JONES SMITH) OK, WE'LL TEST IT.
*
*               THIS COMMAND WILL NOT PUT A MESSAGE IN A USER'S
*               BROADCAST MAIL.
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=R12,LV=@DATAL,TYPE=RENT
         USING @DATA,R13
        $TEW$EC NOTTSO=(,ONLY),MSG=PUTMSG,ERR=SETRC,PUT=PUTLNE
        $TEW$PP PARM=PARMLST,REG=R3,ATT=SETRC
         LA    R4,USERID
         LA    R5,SPACE
         LA    R6,STRING
         TM    STRING+6,X'80'      TEXT SPECIFIED?
         BZ    ERRNOTXT            NO, BRANCH
         MVI   MYTEXT-1,C' '
         MVC   MYTEXT,MYTEXT-1     BLANK THE TEXT
         LH    R1,STRING+4         GET LENGTH OF TEXT
         LTR   R1,R1               LENGTH ZERO?
         BNP   ERRNOTXT            YES, BRANCH
         BCTR  R1,0                LENGTH MINUS 1
         L     R14,STRING          GET ADDRESS OF TEXT
         B     *+L'*+6
         MVC   MYTEXT(0),0(R14)    MOVE TEXT
         EX    R1,*-6              EXECUTE THE ABOVE MVC
         LA    R14,MYTEXT+1(R1)    POINT PAST LAST BYTE OF TEXT
         MVC   0(2,R14),=CL2' '
         LA    R7,1+2+8(,R1)       TOTAL LENGTH IN R7
         LA    R6,MYTEXT
         L     R1,CVTPTR           ADDRESS OF CVT
         USING CVT,R1
         L     R1,CVTTCBP          TCB WORDS
         DROP  R1
         L     R1,4(,R1)           CURRENT TCB
         USING TCB,R1
         L     R1,TCBTIO           TIOT ADDRESS
         DROP  R1
         SPACE 1
*------- IF THIS IS ISSUED FROM A BATCH JOB, THE LENGTH OF
*        THE TEXT INCLUDES SOME TRAILING BLANKS, SO BACK UP
*        TO THE LAST NON-BLANK TO APPEND THE JOB-NAME.
         SPACE 1
         MVC   2(8,R14),0(R1)      APPEND JOBNAME/USERID
*         CLC   0(8,R1),=CL8'TSSO'  IS THIS &TO FROM CONSOLE?      -EU-
*         BNE   USERLOOP            NO                             | S
*         MVC   2(8,R14),=CL8' '    YES, ERASE THAT APPENDED NAME  | U
*         LA    R0,12               BACK UP AS MANY AS 12 BYTES    | P
*TRAIL    BCTR  R14,0               BACK UP 1 BYTE                 | P
*         CLI   0(R14),C' '         IS IT A BLANK?                 | R
*         BNE   TRAILX              NO, GO APPEND ID               | E
*         BCT   R0,TRAIL            KEEP BACKING UP                | S
*TRAILX   LA    R14,1(,R14)         POINT PAST NON-BLANK           | S
*         MVC   2(8,R14),=CL8'OPER' APPEND OPER                    -EU-
USERLOOP TM    6(R4),X'80'         USERID SPECIFIED?
         BZ    ERRNOUSR            NO, BRANCH
         LH    R1,4(,R4)           GET LENGTH OF USERID
         LTR   R1,R1               LENGTH ZERO?
         BNP   ERRNOUSR            YES, BRANCH
         MVC   MYUSER,=CL8' '      BLANK USERID
         BCTR  R1,0                LENGTH MINUS 1
         L     R14,0(R4)           GET ADDRESS OF USERID
         B     *+L'*+6
         MVC   MYUSER(0),0(R14)    MOVE TEXT
         EX    R1,*-6              EXECUTE THE ABOVE MOVE
         SPACE 1
         L     R1,CVTPTR           CVTPTR
         USING CVT,R1
         L     R1,CVTASVT          POINT TO ADDRESS SPACE VECTOR
         DROP  R1
         USING ASVT,R1
         L     R0,ASVTMAXU         MAX. NO. OF ADDRESS SPACES
         LA    R1,ASVTENTY         FIRST ASID
         DROP  R1
ASIDLOOP L     R15,0(,R1)          GET AN ASCB ADDRESS
         LTR   R15,R15             IS THIS AN ACTIVE ADDRESS SPACE
         BM    ASIDNEXT            NO, BRANCH
         USING ASCB,R15
         L     R14,ASCBTSB         ADDRESS OF TSB
         LTR   R14,R14             IS THIS A TSO SESSION?
         BZ    ASIDNEXT            NO, BRANCH
         L     R14,ASCBJBNS        GET JOBNAME (USERID)
         CLC   MYUSER,0(R14)       DOES USERID MATCH
         BE    ASIDX               YES, BRANCH
ASIDNEXT LA    R1,4(,R1)           POINT TO NEXT ASVTENTY ENTRY
         BCT   R0,ASIDLOOP         BRANCH UNTIL ASVTMAXU EXCEEDED
         B     TPTL20
ASIDX    LH    R15,ASCBASID        GET ASID FROM ASCB
         LR    R8,R15              COPY ASID
         DROP  R15
        TPUT   (R6),(R7),ASID=(R8)
         B     *+L'*(R15)          HOW COMPLETE?
         B     NEXTUSER            +0 - OK
         B     TPTL04              +4 -
         B     TPTL08              +8 -
         B     TPTL12              +12 -
         B     TPTL16              +16 -
         B     TPTL20              +20 -
         SPACE 1
TPTL04   MVC   MYMSG(MSG03L),MSG03
         MVC   MYMSG+(MSG03A-MSG03)(7),MYUSER
         LA    R1,MYMSG
         LA    R0,MSG03L
         B     TPTLMSG
TPTL08   MVC   MYMSG(MSG04L),MSG04
         MVC   MYMSG+(MSG04A-MSG04)(7),MYUSER
         LA    R1,MYMSG
         LA    R0,MSG04L
         B     TPTLMSG
TPTL12   MVC   MYMSG(MSG05L),MSG05
         MVC   MYMSG+(MSG05A-MSG05)(7),MYUSER
         LA    R1,MYMSG
         LA    R0,MSG05L
         B     TPTLMSG
TPTL16   MVC   MYMSG(MSG06L),MSG06
         MVC   MYMSG+(MSG06A-MSG06)(7),MYUSER
         LA    R1,MYMSG
         LA    R0,MSG06L
         B     TPTLMSG
TPTL20   MVC   MYMSG(MSG07L),MSG07
         MVC   MYMSG+(MSG07A-MSG07)(7),MYUSER
         LA    R1,MYMSG
         LA    R0,MSG07L
TPTLMSG  BAS   R14,PUTLNE
         B     *+L'*(R15)          HOW COMPLETE?
         B     NEXTUSER            +0 - OK
         B     SETRC               +4 - ERROR
         SPACE 1
NEXTUSER CLI   8(R4),X'FF'
         BE    LEAVE
         L     R4,8(,R4)
         B     USERLOOP
ERRNOTXT LA    R1,MSG01
         LA    R0,L'MSG01
         B     SNDMSG
ERRNOUSR LA    R1,MSG02
         LA    R0,L'MSG02
SNDMSG   BAS   R14,PUTLNE
SETRC    LA    R2,16               RC=16 ------------------------------
         B     QUIT
LEAVE    XR    R2,R2               RC=0 -------------------------------
QUIT    IKJRLSA #TSANSW
        $XRET  CC=(R2),LV=@DATAL,TYPE=RENT
         EJECT
*------- CONSTANTS.
         SPACE 1
MSG01    DC    C'NO TEXT SPECIFIED.'
MSG02    DC    C'NO USERID SPECIFIED.'
MSG03    DC    C'MESSAGE NOT SENT TO '
MSG03A   DC    C'        - NO BUFFERS AVAILABLE.'
MSG03L   EQU   *-MSG03
MSG04    DC    C'MESSAGE NOT SENT TO '
MSG04A   DC    C'        - ATTN INTERRUPT.'
MSG04L   EQU   *-MSG04
MSG05    DC    C'MESSAGE NOT SENT TO '
MSG05A   DC    C'        - PROFILE NOINTERCOM.'
MSG05L   EQU   *-MSG05
MSG06    DC    C'MESSAGE NOT SENT TO '
MSG06A   DC    C'        - LOGIC ERROR IN COMMAND.'
MSG06L   EQU   *-MSG06
MSG07    DC    C'MESSAGE NOT SENT TO '
MSG07A   DC    C'        - NOT LOGGED ON.'
MSG07L   EQU   *-MSG07
         SPACE 1
         LTORG
         EJECT
*------- PARSE PCL.
         SPACE 1
         PRINT NOGEN
PARMLST  IKJPARM
USERID   IKJIDENT 'USERID',LIST,                                       X
               FIRST=ANY,OTHER=ALPHANUM,MAXLNTH=7,                     X
               VALIDCK=USEVC,                                          X
               PROMPT='USERID TO WHOM MESSAGE IS TO BE SENT'
SPACE    IKJPOSIT SPACE
STRING   IKJPOSIT STRING,ASIS,PROMPT='TEXT OF MESSAGE TO BE SENT'
         IKJENDP
         PRINT GEN
         SPACE 1
*------- THIS VALIDITY CHECK ROUTINE CHECKS THE FIRST CHARACTER
*        OF EACH USERID SPECIFIED. THE IKJIDENT MACRO SPECIFIES
*        FIRST=ANY SO THAT PARSE IS FORCED TO TREAT THE FIRST
*        OPERAND ENTERED AS THE USERID. THEN THIS ROUTINE REJECTS
*        IT AS IF IT WERE FIRST=ALPHA. SPECIFYING FIRST=ALPHA AND
*        ENTERING SOMETHING LIKE " TO 'XXX' " WOULD CAUSE PARSE
*        TO TAKE XXX' AS THE STRING AND PROMPT FOR USERID, WHICH
*        IS NOT DESIRED HERE.
         SPACE 1
         DC    0H'0'
         USING *,R10
USEVC    STM   R14,R12,12(R13)
         USING VCPL,R1
         LR    R10,R15
         L     R2,PDEADR           POINT R2 TO PDE
         DROP  R1
         XR    R15,R15
         L     R1,0(,R2)           POINT TO USERID
         CLI   0(R1),C'A'          LESS THAN 'A'?
         BL    *+L'*+8             YES
         CLI   0(R1),C'Z'          GREATER THAN Z?
         BNH   *+L'*+4             NO
         LA    R15,4
         L     R14,R12(,R13)
         LM    R0,R12,20(R13)
         BR    R14
         DROP  R10
         EJECT
*------- DSECTS.
         SPACE 1
@DATA    DSECT
         DS    18F
        $TEW$WA
MYUSER   DS    CL8
MYMSG    DS    CL80
MYTEXTB  DS    C                   MYTEXT MINUS 1
MYTEXT   DS    256C
@DATAL   EQU   (((*-@DATA)+7)/8)*8
         SPACE 1
         PRINT NOGEN
        $TEW$DS VCPL=YES
         IHAASVT
         IHAASCB
         IKJTCB
         PRINT GEN
         SPACE 1
         END
