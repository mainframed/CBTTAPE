DIDOCS   TITLE 'D I D O C S - SIMULATE OS CONSOLE ON TSO USER TUBE.'
*        DATA-SET DIDOCS : AT LEVEL 002 AS OF 04/05/82.
         SPACE 1
         PRINT OFF
         MACRO
&LAB    PUTIT  &MESS,&EXIT=SETRC
         LCLC  &L
&L       SETC  'L'''
&LAB    TPUT   M&SYSNDX,&L.M&SYSNDX
         B     &EXIT
M&SYSNDX DC    C&MESS
         MEND
         MACRO
&LAB    @SNAP  &ID,&HEADER
         AIF   ('&ID' EQ '').A
&LAB     LA    R0,&ID
         AGO   .B
.A       ANOP
&LAB     XR    R0,R0
.B       AIF   ('&HEADER' EQ '').C
         L     R1,=A(&HEADER)
         AGO   .D
.C       XR    R1,R1
.D       L     R15,=A(SNAP)
         BASR  R14,R15
         LTR   R15,R15
         MEND
         COPY  DIDOCSM
         PRINT ON
         SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * *        D   I   D   O   C   S        * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1
***********************************************************************
*                                                                     *
* TSO COMMAND : D I D O C S                                           *
* -------------                                                       *
*                                                                     *
*    (DEVICE INDEPENDENT DISPLAY OPERATOR CONSOLE SUPPORT)            *
*                                                                     *
*                                                                     *
* PURPOSE :                                                           *
* ---------                                                           *
*                                                                     *
*    FOR TSO OPER PRIVLEDGE USERS, SIMULATE AN OS 3277 CONSOLE        *
*    AT THEIR TSO TUBE BY PEEKING AT THE DIDOCS CONSOLE MESSAGE       *
*    BUFFERS IN CSA. DIDOCS ACCEPTS A REPLY FROM THE USER.            *
*    NON-BLANK REPLIES ARE FORMATTED INTO A CMD AND ISSUED VIA        *
*    SVC34, BLANK REPLIES JUST CAUSE THE SCREEN TO BE UPDATED.        *
*    ANY COMMANDS ISSUED VIA SVC34 ARE ALSO LOGGED VIA A WTO SO       *
*    THAT YOUR AUDITORS AND OPERATIONS KNOW WHAT IS GOING ON.         *
*                                                                     *
*    THIS CODE IS BASED ON THE DIDOCS COMMAND FROM THE SHARE DCMS     *
*    SYSTEM. ORIGINAL CODE GENERATED BY FRED LUDDY OF AMDAHL CORP.    *
*                                                                     *
***********************************************************************
*                                                                     *
* TO INVOKE : TYPE DIDOCS                                             *
*             EXIT FROM DIDOCS IS VIA PFK-3                           *
*                                                                     *
* ERROR MESSAGE : NO ACCEPTABLE CONSOLES ACTIVE.                      *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
* MOD 1 - KEN TRUE - FAIRCHILD CAMERA AND INSTRUMENT CORP.  18 MAR 80 *
*         A) ADDED MODESETS TO FIX FETCH PROT. ON MCS BUFFERS WITH    *
*            MVS 3.8B. INTERNALIZED ALL MACROS.                       *
*         B) REMOVED DCMS SCREEN HANDLING AND CONVERTED TO SINGLE     *
*            CSECT USING DIRECT SCREEN FORMATTING.                    *
*                                                                     *
* MOD 2 - ARNOLD CASINGHINO - CONNECTICUT BANK AND TRUST.   18 SEP 82 *
*         GET THE MASTER CONSOLE ONLY AND BYPASS THE REST.            *
*                                                                     *
*  NOTE : LINK WITH 'AC(1)', AND AUTHORIZE COMMAND VIA ENTRY          *
*         IN IKJEFTE8 (MODULE IKJEFT02).                              *
*                                                                     *
***********************************************************************
         SPACE 1
* MODIFIED BY :  MOINIL P.A.
* -------------  COMPUTING CENTRE (TP 361)
*                J.R.C. - ISPRA ESTABLISHMENT
*                21020 ISPRA (VA), ITALY
*
* COMMAND SYNTAX : DIDOCS OPMODE
*
*        WHERE OPMODE : IF OMITTED, DEFAULT IS DISPLAY COMMANDS,
*                              WITH LOG, BUT DEPENDING ON USER
*                              AUTHORITY LEVEL.
*                       SYSTEM OR S - ALL COMMANDS (OS, JES2, CJS,
*                              ...), WITH LOG, BUT DEPENDING ON USER
*                              AUTHORITY LEVEL.
*                       CASPER OR GHOST - ALL COMMANDS WITH NO LOG.
*                       DEBUG OR D - DEBUG AID (PFK'S 1-8-9-10-11).
*
* ADDS/CHANGES :
*      18 OCT 88 - DETERMINE AT ENTRY IF TSO COMMAND OR SUB-COMMAND.
*                - CHECK USER AUTHORITY.
*                - CONTROL SCREEN SIZE (24 * 80).
*                - DISPLAY PANEL TO SELECT ANY EXISTING ACTIVE GRAPHIC
*                  OPERATOR'S CONSOLE ON USER TSO CRT (PFK-2).
*                - DISPLAY A PANEL WITH THE OPERATOR'S REPLIES PENDING
*                  INFORMATION IN THE SYSTEM (PFK-5).
*                - DEBUG (SNAPS) FACILITIES (FILE SYSSNAP NEEDED).
*                - MODULE CODED RE-ENTERABLE.
*                - RUN ON MVS/XA VERSIONS 2.2.0 AND 2.2.3.
*      19 MAR 91 - RESHOW LAST COMMAND (PFK-12).
         EJECT
DIDOCS   START 0
         SPACE 1
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        AUTHORITY LEVELS DEFINITIONS.                          *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
LV15     EQU   15        SYSTEM (AND EVENTUAL SHADOW) DIDOCS LEVEL.
LV10     EQU   10 - 14   ALL COMMANDS AND LOG LEVEL.
LV9      EQU   9         DISPLAY COMMANDS AND REPLY TO JES328X LEVEL.
LV8      EQU   8 - 2     ONLY DISPLAY COMMANDS LEVEL.
LV1      EQU   1         ONLY REPLY TO JES328X LEVEL.
LV0      EQU   0         REJECT LEVEL.
         SPACE 1
        $MDL@IX
         SPACE 1
        $DEFREG
         EJECT
        $XENT  BASE=(R11,R12),LV=WRKL,TYPE=RENT
         LA    R10,2048(,R13)      SET WORK AREAS ADDRESSABILITY.
         LA    R10,2048(,R10)
         USING WRKDSECT,R13,R10
        $TEW$EC TIOT=YES,NOTTSO=(,ONLY),MSG=PUTMSG,ERR=SETRC
         MVC   WTOLOG(WTOLOGL),WTOLOGP INITIALIZE WTO-LOG.
         USING CPPL,R2
         L     R1,CPPLPSCB
         USING PSCB,R1
         MVC   LOGUSER,PSCBUSER    SET UP TO LOG THE COMMAND.
         DROP  R1,R2
         EJECT
*------- CHECK USER AUTHORITY : SYSTEM, OPERATOR, ...
         SPACE 1
         MVI   SWITCH,0            INITIALIZE.
         MVI   SWTRC,0
         MVI   AUTH,LV0
         XC    REQAUT(REQAUTL),REQAUT
         MVC   REQAUT+1(3),=AL3(MDL@IX)
         LA    R1,AUTH
         ST    R1,REQAUT+4
        $EACM  REQAUT
         CLI   AUTH,LV0            AUTHORIZED?
         BE    NOTAUT              NO, REJECT.
         SPACE 1
*------- CHECK CALLER TERMINAL.
         SPACE 1
        GTSIZE ,                   GET ORIGINAL SCREEN SIZE.
         LTR   R0,R0               IS IT A SCREEN?
         BZ    NOTSCR              IF ZERO, NOT A SCREEN.
         CH    R0,=H'24'           STANDARD NUMBER OF LINES?
         BL    NOTSTD              BRANCH IF LESS.
         BE    TSTLNL              BRANCH IF YES.
         CH    R1,=H'80'           STANDARD LINE LENGTH?
         BL    NOTSTD              BRANCH IF LESS.
SETSSZ   ST    R0,SZSCRN           SAVE SCREEN SIZE.
         ST    R1,SZLINE           SAVE LINE SIZE.
         OI    SWITCH,SCRST        INDICATE SCREEN SIZE TO BE SET.
         B     GOPARS
TSTLNL   CH    R1,=H'80'           STANDARD LINE LENGTH?
         BL    NOTSTD              BRANCH IF LESS.
         BH    SETSSZ              BRANCH IF MORE.
         EJECT
GOPARS  $TEW$PP PARM=PARMLST,REG=R3
         SPACE 1
         MVI   CMDALLOW,C' '       SET DEFAULT.
         CLI   AUTH,LV1            IS IT COMMON AUTHORITY LEVEL?
         BE    JONLY               YES.
         CLI   AUTH,LV9            CHECK AUTHORITY.
         BL    INITIAL
         LH    R1,OPMODE
         LTR   R1,R1
         BZ    INITIAL
         BCT   R1,SYSINIT
         SPACE 1
*------- LOGGED DIDOCS USER : ACCEPT ALL COMMANDS AND LOG.
         SPACE 1
         CLI   AUTH,LV9
         BE    JONLY
NORMAL   MVI   CMDALLOW,C'L'
         B     INITIAL
         SPACE 1
*------- SHADOW DIDOCS USER : ACCEPT ALL COMMANDS AND NO-LOG.
         SPACE 1
SYSINIT  CLI   AUTH,LV15           CHECK AUTHORITY.
         BL    NORMAL
         BCT   R1,TSDBG
         MVI   CMDALLOW,C'S'
         B     INITIAL
         SPACE 1
*------- DEBUG REQUEST : PFK'S 1-8-9-10-11.
         SPACE 1
TSDBG    OI    SWTRC,TRALL         SET TRACE ON.
         B     INITIAL
         SPACE 1
*------- COMMON DIDOCS USER : ACCEPT ONLY DISPLAY COMMANDS AND LOG.
         SPACE 1
JONLY    OI    SWITCH,J328X        ACCEPT REPLY TO JES328X.
         DROP  R3
         EJECT
INITIAL IKJRLSA #TSANSW
         SPACE 1
*------- CHECK TRACE ALLOWABLE.
         SPACE 1
CHECKTR  TM    SWTRC,TRALL         IS TRACE REQUESTED?
         BZ    INITWA              NO.
         CLI   AUTH,LV15           AUTHORIZED?
         BL    CLTRC               NO.
         XR    R1,R1
         L     R2,#TSTIOT
         USING TIODSECT,R2
         L     R3,=A(SNAPCS)
         USING IHADCB,R3
SEARCH   CLC   TIOELNGH(4),=F'0'
         BE    NOSNAP
         CLC   TIOEDDNM(L'TIOEDDNM),DCBDDNAM
         BE    TRREQ
         IC    R1,TIOELNGH
         ALR   R2,R1
         B     SEARCH
         DROP  R2,R3
TRREQ    OI    SWTRC,TRALL
         XC    SVSNAP(18*4),SVSNAP
         B     INITWA
CLTRC    MVI   SWTRC,0
         EJECT
*------- INITIALIZE WORK AREAS.
         SPACE 1
INITWA   L     R1,=A(DCMREND-DCMTSRT)
         ST    R1,RDCML
         LA    R1,7(R1)
         SRL   R1,3
         SLL   R1,3
         ST    R1,RDCMLG
        GETMAIN R,LV=(1),SP=1
         ST    R1,LOCRDCM
         XC    LOCTDCM,LOCTDCM
         XC    LOCBUF,LOCBUF
         XC    LOCORE,LOCORE
         MVC   UNITMSG(UNITLEN),UNITMSGP
         MVC   CMND(L'CMNDP),CMNDP
         MVC   CMNDBUF(L'CMNDBUF),LOTSBLNK
         MVC   PFREPLY(L'PFREPLY),LOTSBLNK
         MVC   SELSCRN(FULLSCRL),FULLSCRP
         MVC   SELT1A(SELT1L),SELT1
         MVC   SELT1B(SELT1L),SELT1
         MVC   SELT2A(SELT2L),SELT2
         MVC   SELT2B(SELT2L),SELT2
         MVC   SELSP(SELSPL),SELSPP
         L     R1,=A(FAKEWARN)
         MVC   INFLINE(FAKEWRNL),0(R1)
         CLI   AUTH,LV1            IS IT COMMON AUTHORITY LEVEL?
         BNE   *+L'*+6             NO.
         MVC   INFLINE+FAKEWRNR(11),=CL11'INACTIVE.'
         MVC   SETICT(SETICL),SETIC
         MVC   FULLSCRN(FULLSCRL),FULLSCRP
         MVC   OPRPSCRN(OPRPSCRL),OPRPSCRP
         MVC   OPLST(OPLSTPL),OPLSTP
         L     R1,=A(HELPSCRP)
         MVC   HELPSCRN(256),0(R1)
         MVC   HELPSCRN+256(HELPLEN-256),256(R1)
         LA    R2,SCREEN
         LA    R3,SCREENL
         LA    R4,*
         XR    R5,R5
         MVCL  R2,R4
         MVC   WSPT(WSPL),WSP
         MVC   MSGLINE(L'MSGLINE),LOTSBLNK
         MVC   WSIT(WSIL),WSI
         L     R1,=A(FKWARN)
         MVC   INFOLINE(FKWRNL),0(R1)
         CLI   AUTH,LV1            IS IT COMMON AUTHORITY LEVEL?
         BNE   *+L'*+6             NO.
         MVC   INFOLINE+FKWRNR(10),=CL11'INACTIVE.'
         MVC   WSFT(WSFL),WSF
         MVC   SETCOT(SETCOL),SETCO
         L     R2,CVTPTR           POINT TO CVT.
         USING CVT,R2
         L     R3,CVTSMCA          GET SMF CONTROL AREA ADDRESS.
         USING SMCABASE,R3
LSYSID   EQU   L'SMCASID           SYSTEM ID LENGTH.
         MVC   INFLINE+FAKEWRNL-LSYSID-L'LSF(LSYSID),SMCASID
         MVC   INFOLINE+FKWRNL-LSYSID-L'LSF(LSYSID),SMCASID
         MVC   OPSYS(LSYSID),SMCASID
         DROP  R2,R3
         TM    SWITCH,J328X        ACCEPT REPLY TO JES328X?
         BZ    *+L'*+6             NO.
         MVC   RNJ328X(L'RNJ328X),LOTSBLNK
         EJECT
*------- START PROCESSING.
         SPACE 1
        STFSMODE ON,INITIAL=YES    TURN ON VTAM FULL SCREEN MODE.
         TM    SWITCH,SCRST        SCREEN SIZE TO BE SET?
         BZ    NOSCRS              BRANCH IF NOT.
        STSIZE SIZE=80,LINE=24     SET STANDARD SCREEN SIZE.
         OI    SWITCH,SCRRS        INDICATE SCREEN SIZE TO BE RESET.
NOSCRS  ZEROKEY ,
         L     R1,CVTPTR           CVT POINTER.
         L     R1,CVTCUCB-CVT(R1)  UCM BASE POINTER.
         USING UCM,R1
         XC    CONASID,CONASID
         MVC   CONASID+2(2),UCMCTID     GET COMMTASK ASID.
         DROP  R1
        ESAR   R5                  GET SECONDARY ASID.
         ST    R5,SASID            KEEP IT.
         MVC   AXPL(4),=XL4'00010000'
        AXRES  AXLIST=AXPL         RESERVE AX.
         L     R5,CONASID          GET COMMTASK ASID.
        AXEXT  ASID=(5)            EXTRACT AX OF CONSOLE IN R0.
        AXSET  AX=(0)              SET AX TO ALLOW CONSOLE AS 2ND.
        RESETKEY ,
         EJECT
*------- DISPLAY USEFULL CONSOLES INFORMATION.
         SPACE 1
RESTART  MVC   SELCUU,SELCUUP
         MVC   SELWHAT,SELWHATP
RESET    ICM   R1,B'1111',LOCTDCM  TDCM AREA TO BE FREED?
         BZ    NOFREET             BRANCH IF NOT.
         L     R14,TDCMLG          LENGTH OF AREA.
        FREEMAIN R,A=(1),LV=(R14),SP=1
         XC    LOCTDCM,LOCTDCM     NULLIFY.
NOFREET  ICM   R1,B'1111',LOCBUF   TDCM BUFFER AREA TO BE FREED?
         BZ    NOFREEB             BRANCH IF NOT.
         L     R14,LOCBUFL         LENGTH OF AREA.
        FREEMAIN R,A=(1),LV=(R14),SP=1
         XC    LOCBUF,LOCBUF       NULLIFY.
NOFREEB  LA    R2,SELDT
         LR    R6,R2
         LA    R3,SELDTN
         LR    R7,R3
         MVC   0(SELDTL,R2),SELDTP
         LA    R2,SELDTL(,R2)
         BCT   R3,*-10
         L     R1,CVTPTR           BEGINNING...
         L     R1,CVTCUCB-CVT(R1)  ...
         USING UCM,R1
         LM    R3,R5,UCMVEA        R3 -> TO THE FIRST UCME,
*                                   R4 CONTAINS UCME LENGTH,
*                                    R5 -> TO THE LAST UCME.
         DROP  R1
         USING UCMLIST,R3
NEXTUCM  TM    SWTRC,PF9+TRALL     TRACE REQUESTED AND ALLOWED?
         BNO   GETUCM              NO.
         ST    R3,SNAPLST          UCME START.
         L     R15,=A(UCMESIZE-1)  UCME LENGTH.
         ALR   R15,R3
         ST    R15,SNAPLST+4       UCME END.
         OI    SNAPLST+4,X'80'
        @SNAP  1,HD1               DUMP UCME.
         BZ    GETUCM
SNPERR   MVC   SCREEN(SNPMSGL),SNPMSG
         TM    SWTRC,SNOPEN
         BO    *+L'*+6
         MVC   SCREEN+SNPMSGO(4),=CL4'OPEN'
         BAS   R9,CVTPRT
         MVC   SCREEN+SNPRCL(L'CVTA),CVTA
         LA    R1,FULLSCRN
         LA    R0,FULLSCRL+SNPMSGL
         OI    SWITCH,QUIT
         BAS   R9,STERM            SEND THE SCREEN.
         B     ENDIT               END.
GETUCM   TM    UCMATR,UCMUF        IS THIS CONSOLE ACTIVE?
         BZ    TESTUCM             NO... UNUSABLE.
         TM    UCMDISP1,UCMDISPC+UCMDISPE    GRAPHICS AND FULL I/O?
         BNO   TESTUCM             NO... UNACCEPTABLE.
         ICM   R1,B'1111',UCMXB    IS IT GRAPHICS?
         BZ    TESTUCM             NO... USELESS.
         L     R1,UCMUCB           GET THE UCB ADDRESS.
         USING UCBDSECT,R1
         MVC   6(L'UCBNAME,R6),UCBNAME
         DROP  R1
         XR    R15,R15             GET THE CONSOLE ID.
         IC    R15,UCMID
         CVD   R15,CVTA
         UNPK  12(2,R6),CVTA+6(2)
         OI    13(R6),C'0'
         LA    R15,16(R6)          GET AUTHORITY.
         TM    UCMAUTHA,UCMAUTH1
         BZ    TAIO
         MVC   0(4,R15),=CL4'SYS.'
         LA    R15,3(R15)
TAIO     TM    UCMAUTHA,UCMAUTH2
         BZ    TACONS
         CLI   0(R15),C' '
         BE    TAIOA
         MVC   0(5,R15),=CL5', IO.'
         LA    R15,4(R15)
         B     TACONS
TAIOA    MVC   0(3,R15),=CL3'IO.'
         LA    R15,2(R15)
TACONS   TM    UCMAUTHA,UCMAUTH3
         BZ    TAINFO
         CLI   0(R15),C' '
         BE    TACONSA
         MVC   0(7,R15),=CL7', CONS.'
         B     TAINFO
TACONSA  MVC   0(5,R15),=CL5'CONS.'
TAINFO   CLI   16(R6),C' '
         BNE   *+L'*+6
         MVC   16(5,R6),=CL5'INFO.'
         TM    UCMDISP1,UCMDISPA   MASTER CONSOLE?
         BZ    NOTMS               NO.
         MVC   34(2,R6),=CL2'MS'
         MVC   SELCUU(L'UCBNAME),6(R6)
NOTMS    LA    R6,SELDTL(R6)
         BCT   R7,TESTUCM
         B     GOASK
         DROP  R3
TESTUCM  BXLE  R3,R4,NEXTUCM       GET THE NEXT ENTRY.
         CLI   SELDT+6,C' '        ANYTHING RETAINED?
         BNE   GOASK               YES.
         MVC   SCREEN(ERMSGL),ERMSG NO ACCEPTABLE CONSOLES ACTIVE.
         LA    R1,FULLSCRN
         LA    R0,FULLSCRL+ERMSGL
         OI    SWITCH,QUIT
         BAS   R9,STERM            SEND THE SCREEN.
         B     ENDIT               END.
GOASK    CLI   AUTH,LV1            IS IT COMMON AUTHORITY LEVEL?
         BE    GETMS               YES, ONLY MASTER CONSOLE ALLOWED.
         LA    R1,SELSCRN
         LA    R0,SELSCRNL
         NI    SWITCH,255-ADSDV-RLCMD
         BAS   R9,STERM
         OC    TXREPLY,LOTSBLNK    CONVERT TO UPPER CASE.
         LA    R1,L'TXREPLY-1
         LA    R15,TXREPLY
SCANREP  CLI   0(R15),C' '         NULL INPUT?
         BNE   GETCUU              NO.
         LA    R15,1(R15)
         BCT   R1,SCANREP
         CLC   SELCUU,SELCUUP
         BNE   GETMS
         MVC   SELCUU,SELCUUP
         MVC   SELWHAT,=CL10'? MISSING.'
         B     GOASK
         EJECT
*------- SETUP TO COPY PAGEABLE DCM TO OUR ADDRESS SPACE.
         SPACE 1
GETCUU   MVC   SELCUU(L'SELCUU),0(R15)
GETMS    L     R1,CVTPTR           BEGINNING...
         L     R1,CVTCUCB-CVT(R1)  ...
         USING UCM,R1
         LM    R3,R5,UCMVEA        R3 -> TO THE FIRST UCME,
*                                   R4 CONTAINS UCME LENGTH,
*                                    R5 -> TO THE LAST UCME.
         DROP  R1
         USING UCMLIST,R3
DIDL     L     R1,UCMUCB           SEARCH FOR SOMETHING USEFULL.
         USING UCBDSECT,R1
         CLC   SELCUU(L'UCBNAME),UCBNAME     IS CONSOLE THIS ONE?
         BNE   DIDT                BRANCH IF NOT.
         MVC   UNITXXX(L'UCBNAME),UCBNAME    DEBUGGING ...
         DROP  R1
         TM    UCMATR,UCMUF        IS IT ACTIVE?
         BZ    DIDT                NO... UNUSABLE.
         ICM   R6,B'1111',UCMXB    IS IT GRAPHICS?
         BZ    DIDT                NO... USELESS.
         STM   R3,R5,MOVEBSAV      SAVE SCAN REGISTERS.
        ZEROKEY ,
         L     R7,CONASID          GET COMMTASK ASID.
        SSAR   R7                  SET CONSOLE AS SECONDARY.
         L     R7,LOCRDCM          THE 'TO' ADDRESS.
         L     R3,RDCML            LENGTH OF THE 'TO'.
         XR    R15,R15             ZERO R15 FOR 'FROM' KEY.
MOVERD  MVCP   0(R3,R7),0(R6),R15  MOVE IT.
         BZ    MOVEDRD
         AL    R6,=F'256'          BUMP 'FROM' ADDRESS BY 256.
         AL    R7,=F'256'          BUMP 'TO' ADDRESS BY 256.
         SL    R3,=F'256'          DECREMENT LENGTH OF MOVE LEFT.
         B     MOVERD              GO BACK AGAIN.
MOVEDRD  L     R5,SASID            RESTORE PREVIOUS SECONDARY.
        SSAR   R5                  SHOULD BE AS PRIMARY.
        RESETKEY ,
         L     R9,LOCRDCM          ADDRESS OF COPY OF DCM.
         USING DCMTSRT,R9
         ICM   R6,B'1111',DCMADTRN FIND THE PAGEABLE DCM (TDCM).
         BZ    DIDTRR              DOES IT EXIST? NO...
         LH    R1,DCMLEN           LENGTH OF PAGEABLE DCM.
         ST    R1,TDCML
         LA    R1,7(R1)            ROUND UP TO 8 BYTE BOUNDARY.
         SRL   R1,3
         SLL   R1,3
         ST    R1,TDCMLG
         TM    SWTRC,PF9+TRALL     TRACE REQUESTED AND ALLOWED?
         BNO   GETTDCM             NO.
         DROP  R9
         ST    R9,SNAPLST          DCM START.
         L     R15,RDCML           DCM LENGTH.
         ALR   R15,R9
         BCTR  R15,0
         ST    R15,SNAPLST+4       DCM END.
         OI    SNAPLST+4,X'80'
        @SNAP  2,HD2               DUMP DCM.
         BNZ   SNPERR
GETTDCM  L     R3,MOVEBSAV         RESTORE UCM POINTER.
         MVC   XUCMID,UCMID        SAVE THIS CONSOLE ID.
         DROP  R3
         L     R1,TDCMLG           LENGTH OF PAGEABLE DCM.
        GETMAIN R,LV=(1),SP=1
         ST    R1,LOCTDCM          STORE AREA ADDRESS.
        ZEROKEY ,
         L     R7,CONASID          GET COMMTASK ASID.
        SSAR   R7                  SET CONSOLE AS SECONDARY.
         L     R7,LOCTDCM          THE 'TO' ADDRESS.
         L     R3,TDCML            LENGTH OF THE 'TO'.
         XR    R15,R15             ZERO R15 FOR 'FROM' KEY.
MOVETD  MVCP   0(R3,R7),0(R6),R15  MOVE IT.
         BZ    MOVEDTD
         AL    R6,=F'256'          BUMP 'FROM' ADDRESS BY 256.
         AL    R7,=F'256'          BUMP 'TO' ADDRESS BY 256.
         SL    R3,=F'256'          DECREMENT LENGTH OF MOVE LEFT.
         B     MOVETD              GO BACK AGAIN.
MOVEDTD  L     R5,SASID            RESTORE PREVIOUS SECONDARY.
        SSAR   R5                  SHOULD BE AS PRIMARY.
        RESETKEY ,
         L     R6,LOCTDCM          ADDRESS OF COPY OF TDCM.
         USING DCMSTRT,R6
         TM    SWTRC,PF9+TRALL     TRACE REQUESTED AND ALLOWED?
         BNO   NOTTDCM             NO.
         ST    R6,SNAPLST          TDCM START.
         L     R15,TDCMLG          LENGTH OF PAGEABLE DCM.
         BCTR  R15,0
         ALR   R15,R6
         ST    R15,SNAPLST+4       TDCM END.
         OI    SNAPLST+4,X'80'
        @SNAP  3,HD3               DUMP TDCM.
         BNZ   SNPERR
NOTTDCM  CLI   DCMIONDX,X'10'      IS IT A 3277?
         BE    GETBUF              YES, BY DINGLES.
* --->>> DEVICE 3066 (M/165) IS NO MORE SUPPORTED.
*        CLI   DCMIONDX,X'04'      IS IT A M/165 (3066)?
*        BE    GETBUF              YES, CAN BE SUPPORTED.
DIDTRR   LM    R3,R5,MOVEBSAV      RESTORE SCAN REGISTERS.
DIDT     BXLE  R3,R4,DIDL          GET THE NEXT ENTRY.
         MVC   SELWHAT,=CL10'? INVALID.'
         B     RESET
GETBUF   L     R1,DCMLSCRN         POINT TO LAST BUFFER LINE.
         AH    R1,DCMCORLN         LENGTH OF DCM LINE IN CORE.
         L     R0,DCMASCRN         POINT TO FIRST INPUT LINE.
         SR    R1,R0               COMPUTE AND
         ST    R1,BUFLGTH               STORE BUFFER LENGTH.
         LA    R1,7(R1)            ROUND UP TO 8 BYTE BOUNDARY.
         SRL   R1,3
         SLL   R1,3
         ST    R1,LOCBUFL          STORE LENGTH FOR FREE.
        GETMAIN R,LV=(1),SP=1
         ST    R1,LOCBUF           STORE BUFFER AREA ADDRESS.
         EJECT
*------- A CONSOLE HAS BEEN FOUND COPY THE SCREEN
*        AND DISPLAY IT FOR THE USER.
*
*              R6 -> TDCM
         SPACE 1
GOTONE  ZEROKEY ,
         L     R4,CONASID          GET COMMTASK ASID.
        SSAR   R4                  SET CONSOLE AS SECONDARY.
         L     R2,BUFLGTH          LENGTH OF STUFF TO MOVE.
         L     R7,DCMASCRN         POINT TO THE FIRST INPUT LINE.
* --->>> DEVICE 3066 (M/165) IS NO MORE SUPPORTED.
*        CLI   DCMIONDX,X'10'      IS IT A 3277?
*        BNE   *+L'*+4             BRANCH IF NOT.
         SL    R7,=F'6'            ADJUST OFFSET.
         L     R8,LOCBUF           BUFFER ADDRESS.
         XR    R15,R15             'FROM' KEY.
MOVEBUF MVCP   0(R2,R8),0(R7),R15  MOVE IT.
         BZ    ENDMOVE
         AL    R7,=F'256'          BUMP 'FROM' ADDRESS.
         AL    R8,=F'256'          BUMP 'TO' ADDRESS.
         SL    R2,=F'256'          DECREMENT THE LENGTH FIELD.
         B     MOVEBUF             GO BACK AND GET MORE.
ENDMOVE  L     R4,SASID            RESTORE PREVIOUS SECONDARY.
        SSAR   R4                  SHOULD BE AS PRIMARY.
        RESETKEY ,
         L     R5,LOCBUF           GET BUFFER ADDRESS.
         TM    SWTRC,PF10+TRALL    TRACE REQUESTED AND ALLOWED?
         BNO   NOTTBUF             NO.
         ST    R5,SNAPLST          DCM BUFFER START.
         L     R15,BUFLGTH         LENGTH OF DCM BUFFER.
         BCTR  R15,0
         ALR   R15,R5
         ST    R15,SNAPLST+4       DCM BUFFER END.
         OI    SNAPLST+4,X'80'
        @SNAP  10,HD10             DUMP TDCM BUFFER.
         BNZ   SNPERR
NOTTBUF  LH    R4,DCMCORLN         COMPUTE NO. OF LINES IN
         XR    R2,R2                    CONSOLE BUFFER.
         L     R3,BUFLGTH
         DR    R2,R4
         LA    R0,L'SCREEN-L'TABSET     DETERMINE MOVE LENGTH.
         CLR   R4,R0
         BNH   *+L'*+2
         LR    R4,R0
         BCTR  R4,0
NEXTPAGE NI    SWITCH,255-NXTPG
         LA    R8,SCREEN
         LA    R9,SCREENL
         LA    R14,*
         XR    R15,R15
         ICM   R15,B'1000',LOTSBLNK     SET UP A BLANK.
         MVCL  R8,R14              PAD WITH SPACES.
         LA    R0,NDL              COMPLETE SCREEN INITIALIZATION.
         L     R1,=A(TABSET)
         LA    R2,SCREEN
INITTB   MVC   0(L'TABSET,R2),0(R1)
         LA    R1,L'TABSET(R1)
         LA    R2,L'SCREEN(R2)
         BCT   R0,INITTB
         LA    R0,NDL              SCREEN LINES NUMBER.
         LA    R2,SCREEN           SCREEN BUFFER.
         CLI   DCMIONDX,X'10'      IS IT A 3277?
         BE    T3277               BRANCH IF YES.
* --->>> DEVICE 3066 (M/165) IS NO MORE SUPPORTED.
*        CLI   DCMIONDX,X'04'      IS IT A 3066?
*        BE    T3066               BRANCH IF YES.
         MVC   SCREEN(WCOMSGL),WCOMSG   CANNOT OCCURS.
         LA    R1,FULLSCRN
         LA    R0,FULLSCRL+WCOMSGL
         OI    SWITCH,QUIT
         BAS   R9,STERM            SEND THE SCREEN.
         B     ENDIT               END.
         SPACE 1
T3277    BAS   R9,*+L'*+6
         MVC   L'TABSET(*-*,R2),5(R5)
         EX    R4,0(R9)            MOVE A LINE.
         CLC   3(L'SFINT,R5),SFINT
         BNE   INCRSB
         MVC   4(1,R2),SFINT+1     ADJUST ATTRIBUTE BYTE.
* --->>> DEVICE 3066 (M/165) IS NO MORE SUPPORTED.
*        B     INCRSB
         SPACE 1
*T3066   BAS   R9,*+L'*+6
*        MVC   L'TABSET(*-*,R2),0(R5)
*        EX    R4,0(R9)            MOVE A LINE.
         SPACE 1
INCRSB   CLC   L'TABSET(L'SCREEN-L'TABSET,R2),LOTSBLNK
         BE    NOMORE              NOTHING MOVED, STOP HERE.
         LA    R2,L'SCREEN(R2)     BUMP SCREEN BUFFER POINTER.
         AH    R5,DCMCORLN         BUMP BUFFER POINTER.
         BCT   R3,TESTSB           LOOP TILL BUFFER FILLED OUT.
         B     NOMORE
TESTSB   BCT   R0,6(R9)            LOOP TILL SCREEN BUFFER FILLED IN.
         STM   R3,R5,MOVEBSAV      SAVE BUFFER POINTERS.
         LA    R2,SELDT            LOOK IF REALLY ANYTHING MORE.
         EX    R4,0(R9)
         EX    R4,CTRLSB
         BE    NOMORE
         OI    SWITCH,NXTPG        MORE DATA TO BE DISPLAYED.
         MVC   NXTPGI,NXPAGE
         B     SENDPG
CTRLSB   CLC   L'TABSET(*-*,R2),LOTSBLNK
NOMORE   MVC   NXTPGI,NOPAGE
SENDPG   MVC   MSGLINE(UNITLEN),UNITMSG MOVE IN TITLES.
         LA    R1,FULLSCRN
         LA    R0,SCRLEN
SRSHW    STM   R0,R1,SVPARM
RDSPL    OI    SWITCH,ADSDV+RLCMD
         BAS   R9,STERM            DO IT JACK.
         MVC   SVCRCM,LOTSBLNK
         MVC   NOTR15,LOTSBLNK
         MVC   RPLYNO(L'RPLYNO),LOTSBLNK
         MVC   SETCOT(SETCOL),SETCO
        $FS    WCC=(KBR,RMDT),MF=(I,FULLSWCC)
        $FS    SF=NORMAL,MF=(I,WSFM)
         OC    TXREPLY,LOTSBLNK    CONVERT TO UPPER CASE.
         LA    R1,L'TXREPLY-1
         LA    R15,TXREPLY
SCANIN   CLI   0(R15),C' '         NULL INPUT?
         BNE   GETIN               NO.
         LA    R15,1(R15)
         BCT   R1,SCANIN
         TM    SWTRC,PF11+TRALL    TRACE REQUESTED AND ALLOWED?
         BO    TRCRQ               YES.
         TM    SWITCH,NXTPG        NO COMMAND... GO PEEK AND SHOW SIGH
         BZ    GOTONE                   ... GOT TO ISSUE THE COMMAND.
         LM    R3,R5,MOVEBSAV      RESTORE BUFFER POINTERS.
         B     NEXTPAGE
TRCRQ    OI    SWTRC,NXTPGT
         XR    R2,R2
         B     TSTRC
GETIN    MVC   CMNDBUF,LOTSBLNK
         NI    SWITCH,255-NXTPG
         BCT   R1,*+L'*+6
         MVC   CMNDBUF(*-*),0(R15)
         EX    R1,*-6
         MVC   TXREPLY,LOTSBLNK
         MVC   LOGCMD,CMNDBUF      SAVE COMMAND AS EXECUTED.
         CLI   CMNDBUF,C'K'        IS IT A CONTROL COMMAND?
         BE    DONTDOIT            YES, REJECT IT.
         SPACE 1
*------- IF AUTHORITY IS NOT BLANK THEN DO IT.
         SPACE 1
         CLI   CMDALLOW,C' '
         BNE   DOIT
         SPACE 1
*------- IF AUTHORITY IS BLANK AND COMMAND IS NOT
*        A DISPLAY THEN SEE AUTHORITY TO DO IT.
         SPACE 1
         CLI   AUTH,LV1            IS IT COMMON AUTHORITY LEVEL?
         BE    LOOKJ               YES.
         CLI   CMNDBUF,C'D'        OS?
         BE    DOIT                YES.
         CLI   CMNDBUF,C'$'        CJS?
         BE    LOOKD               YES.
         CLI   CMNDBUF,C'.'        JES2?
         BNE   LOOKJ               NO.
LOOKD    CLI   CMNDBUF+1,C'D'      DISPLAY?
         BE    DOIT                YES.
LOOKJ    TM    SWITCH,J328X        ACCEPT REPLY TO JES328X ALSO?
         BZ    DONTDOIT            NO.
         CLC   RNJ328X(L'RNJ328X),CMNDBUF    REPLY-ID OK?
         MVC   RNJ328X(L'RNJ328X),LOTSBLNK   CLEAR REPLY-ID.
         BNE   DONTDOIT            NO.
         SPACE 1
*------- IF AUTHORITY LEVEL IS 'S' THEN DO NOT LOG.
         SPACE 1
DOIT     CLI   CMDALLOW,C'S'
         BE    NOLOG
         XR    R0,R0
         IC    R0,XUCMID           QUEUE TO CONSOLE ID.
        WTO    MF=(E,WTOLOG)       DO THE WTO FOR POSTERITY.
NOLOG   ZEROKEY ,
         XR    R0,R0
         IC    R0,XUCMID           OUR MAN IN THE COMM TASK.
         XR    R15,R15
        MGCR   CMND                FIRE HIM THRU.
         LR    R2,R15              SAVE RETURN CODE.
        RESETKEY ,
         B     TSTRC
DONTDOIT XR    R2,R2
         MVC   SVCRCM(L'SVCRCM+L'NOTR15),=CL17' COMMAND REJECTED'
TSTRC    TM    SWTRC,TRALL         TRACE ALLOWED?
         BZ    PRCDNM              NO.
         TM    SWTRC,PF11
         BZ    NODBG
         LA    R15,SVA             TRACE WORK AREAS.
         ST    R15,SNAPLST
         AL    R15,=A(WRKL-1)
         ST    R15,SNAPLST+4
         OI    SNAPLST+4,X'80'
        @SNAP  100,HD100           DUMP WORK AREAS.
         BZ    NODBG
         MVC   SVCRCM(L'SVCRCM),=CL9'SNP RC = '
         TM    SWTRC,SNOPEN
         BO    TRDBG
         MVC   SVCRCM(3),=CL3'OPN'
         B     TRDBG
NODBG    LTR   R15,R2
         BNZ   SDERR               NON-ZERO RC.
         TM    SWTRC,NXTPGT
         BZ    GOTONE
         NI    SWTRC,255-NXTPGT
         TM    SWITCH,NXTPG
         BZ    GOTONE
         LM    R3,R5,MOVEBSAV      RESTORE BUFFER POINTERS.
         B     NEXTPAGE
         SPACE 1
*------- SEE IF WE ARE HERE BY A PREVIOUS MARRIAGE ...
*              (WITH A NON-ZERO RC)
         SPACE 1
PRCDNM   LTR   R15,R2
         BZ    GOTONE              FIND OUT WUTS HAPPENIN.
SDERR    MVC   SVCRCM(L'SVCRCM),=CL9'CMD RC = '
TRDBG    BAS   R9,CVTPRT           DO IT.
         MVC   NOTR15,CVTA         MOVE IN RC.
         MVC   MSGLINE(UNITLEN),UNITMSG MOVE IN ERROR LINE.
         LA    R1,FULLSCRN
         LA    R0,SCRLEN
         MVC   RPLYNO(L'RPLYNO),LOTSBLNK
         MVC   SETCOT(SETCOL),SETCO
         NI    SWITCH,255-ADSDV-RLCMD
         BAS   R9,STERM
         MVC   TXREPLY,LOTSBLNK
         MVC   SVCRCM,LOTSBLNK
         MVC   NOTR15,LOTSBLNK
         B     GOTONE
         DROP  R6
         EJECT
*------- RESTORE/REPEAT THE LAST COMMAND TEXT.
         SPACE 1
LCMDR    LA    R3,L'RPTCMD
         LA    R2,CMNDBUF+L'RPTCMD-1
         CLI   0(R2),C' '
         BNE   *+L'*+10
         BCTR  R2,0
         BCT   R3,*-10
         B     RDSPL               BACK TO RE-DISPLAY.
         XC    RPTCMD(RPTLEN),RPTCMD
         LA    R0,SCRLENF(R3)
         LA    R2,RPTCMD(R3)
         BCT   R3,*+L'*+6
         MVC   RPTCMD(*-*),CMNDBUF
         EX    R3,*-6
         MVC   0(SETCOL,R2),SETCO
        $FS    WCC=(KBR),MF=(I,FULLSWCC)
        $FS    SF=(MDT),MF=(I,WSFM)
         L     R1,SVPARM+4         RESTORE ONLY R1 PARM.
         B     SRSHW               BACK TO RE-DISPLAY.
         EJECT
*------- DISPLAY OPERATOR'S REPLIES PENDING.
         SPACE 1
DWTOR    ST    R6,SVTDCM           SAVE TDCM ADDRESS.
         XC    SVOREPT,SVOREPT
         OC    LOCORE,LOCORE
         BNZ   DWTORST
         L     R1,LGTORE
         AL    R1,LGTWQE
        GETMAIN R,LV=(1),SP=1
         ST    R1,LOCORE           SAVE ORE AREA ADDRESS.
         AL    R1,LGTORE
         ST    R1,LOCWQE           SAVE WQE AREA ADDRESS.
DWTORST  LA    R2,OPWTRS
         LA    R3,OPWTRSL
         XR    R4,R4
         L     R5,=A(X'40000000')
         MVCL  R2,R4
         ICM   R2,B'1111',SVOREPT
         BNZ   DWTORPT             ORE CHAIN CONTINUE...
         L     R1,CVTPTR           BEGINNING...
         L     R1,CVTCUCB-CVT(R1)
         USING UCM,R1
         L     R2,UCMRPYQ          ADDRESS OF FIRST ORE (REPLY-Q).
         DROP  R1
DWTORPT  LA    R3,OPWTRS
         LA    R4,20
         LTR   R2,R2
         BZ    DWTOR2              NO ORE CHAIN.
DWTOR1   L     R7,LOCORE           THE 'TO' ADDRESS.
         LA    R5,ORESIZE          LENGTH OF THE 'TO'.
        ZEROKEY ,
         L     R6,CONASID          GET COMMTASK ASID
        SSAR   R6                  SET CONSOLE AS SECONDARY.
         XR    R15,R15             ZERO R15 FOR 'FROM' KEY.
DWTORM1 MVCP   0(R5,R7),0(R2),R15  MOVE IT.
         BZ    DWTORM2
         AL    R2,=F'256'          BUMP 'FROM' ADDRESS BY 256.
         AL    R7,=F'256'          BUMP 'TO' ADDRESS BY 256.
         SL    R5,=F'256'          DECREMENT LENGTH OF MOVE LEFT.
         B     DWTORM1             GO BACK AGAIN.
DWTORM2  L     R6,SASID            RESTORE PREVIOUS SECONDARY.
        SSAR   R6                  SHOULD BE AS PRIMARY.
        RESETKEY ,
         L     R7,LOCORE           ORE AREA ADDRESS.
         USING OREF,R7
         CLC   ORECBID,=CL4'ORE '  CONTROL BLOCK ID?
         BNE   DWTOR3              NO... STOP HERE.
         MVC   1(L'OREID,R3),OREID REPLY IDENTIFICATION.
         ICM   R2,B'1111',ORERWQE  WQE ADDRESS.
         BZ    DWTOR4              ANY ONE? NO... STOP HERE.
         L     R8,LOCWQE           THE 'TO' ADDRESS.
         LA    R5,WQETXTL-WQE      LENGTH OF THE 'TO'.
        ZEROKEY ,
         L     R6,CONASID          GET COMMTASK ASID
        SSAR   R6                  SET CONSOLE AS SECONDARY.
         XR    R15,R15             ZERO R15 FOR 'FROM' KEY.
DWTORM3 MVCP   0(R5,R8),0(R2),R15  MOVE IT.
         BZ    DWTORM4
         AL    R2,=F'256'          BUMP 'FROM' ADDRESS BY 256.
         AL    R8,=F'256'          BUMP 'TO' ADDRESS BY 256.
         SL    R5,=F'256'          DECREMENT LENGTH OF MOVE LEFT.
         B     DWTORM3             GO BACK AGAIN.
DWTORM4  L     R6,SASID            RESTORE PREVIOUS SECONDARY.
        SSAR   R6                  SHOULD BE AS PRIMARY.
        RESETKEY ,
         TM    SWTRC,PF8+TRALL     TRACE REQUESTED AND ALLOWED?
         BNO   DWTORDS             NO.
         ST    R7,SNAPLST          ORE-WQE START.
         L     R15,LGTORE          ORE AREA LENGTH.
         AL    R15,LGTWQE          ADD WQE AREA LENGTH.
         ALR   R15,R7
         ST    R15,SNAPLST+4       ORE-WQE END.
         OI    SNAPLST+4,X'80'
        @SNAP  20,HD20             DUMP ORE-WQE.
         BNZ   SNPERR
DWTORDS  L     R8,LOCWQE           WQE AREA ADDRESS.
         USING WQE,R8
         CLC   OREID(L'OREID),WQETXT+1  REPLY-ID MATCH?
         BNE   DWTORID             NO, SAY IT.
         MVC   5(L'WQEJOBNM,R3),WQEJOBNM     JOB-NAME.
         L     R1,WQENBR           MESSAGE TEXT LENGTH.
         S     R1,=F'4'
         BNP   DWTORML             WRONG LENGTH.
         CH    R1,=H'64'
         BNH   DWTORMV
         LA    R1,60               TRUNCATE MESSAGE TEXT.
         LA    R15,WQETXT+62
         CLI   0(R15),C' '
         BE    *+L'*+6
         BCTR  R15,0
         BCT   R1,*-10
         LA    R15,16(R1,R3)
         MVC   0(3,R15),=CL3'...'
DWTORMV  EX    R1,DWTORMT          SET MESSAGE TEXT
         TM    SWITCH,J328X        REPLY TO JES328X ACTIVE?
         BZ    DWTORCN             NO
         CLC   15(8,R3),=CL8'JES328X '  JOB-NAME MATCH?
         BNE   DWTORCN             NO
         MVC   RNJ328X,OREID       GET REPLY-ID.
         B     DWTORCN
DWTORMT  MVC   15(*-*,R3),WQETXT+4 << EXECUTED >>
DWTORID  MVC   15(25,R3),=CL25'... NO MATCH REPLY-ID ...'
         B     DWTORCN
DWTORML  MVC   15(25,R3),=CL25'... TEXT LENGTH ERROR ...'
DWTORCN  ICM   R2,B'1111',ORELKP   LINKAGE POINTER.
         DROP  R7,R8
         BZ    DWTOR5              END OF CHAIN.
         LA    R3,80(R3)
         BCT   R4,DWTOR1           LOOP WITH NEXT.
         ST    R2,SVOREPT
         MVC   OPNXT,NXPAGE
         B     DWTOR6
DWTOR2   MVC   15(25,R3),=CL25'... NO OPERATOR REPLY ...'
         B     DWTOR5
DWTOR3   MVC   15(22,R3),=CL22'... ORE-ID MISSING ...'
         B     DWTOR5
DWTOR4   MVC   15(22,R3),=CL22'... NO WQE POINTER ...'
DWTOR5   MVC   OPNXT,=CL9'---------'
DWTOR6   MVC   OPRNM,OPRNMP
         LA    R1,OPRPSCRN
         LA    R0,OPRPLEN
         NI    SWITCH,255-ADSDV-RLCMD
         BAS   R9,STERM            WRITE SCREEN.
         OC    TXREPLY(2),LOTSBLNK CONVERT TO UPPER CASE.
         CLC   TXREPLY(2),LOTSBLNK ANY REPLY NUMBER ENTERED?
         BNE   DWTORTS             YES, ANALYZE IT.
         CLC   OPNXT,NXPAGE        ANY CONTINUE?
         BE    DWTORST             YES.
         B     DWTORRT             NO.
DWTORTS  CLC   TXREPLY(2),=CL2'00' VALID INPUT REPLY NUMBER?
         BL    DWTORST             NO, IGNORE.
         CLC   TXREPLY(2),=CL2'99'
         BH    DWTORST             NO, IGNORE.
         MVC   RPLYNO(L'RPLYNO),TXREPLY DISPLAY REPLY NUMBER.
DWTORRT  L     R6,SVTDCM           RESTORE TDCM ADDRESS.
         LM    R0,R1,SVPARM        RESTORE R0-R1 PARM.
         B     RDSPL               BACK TO RE-DISPLAY.
         EJECT
*------- TERMINATE PROCESSING.
         SPACE 1
ENDIT    LR    R2,R15
         TM    SWTRC,SNOPEN
         BZ    NOSNCL
        CLOSE  (SNAPDCB),MF=(E,SNAPCL)
NOSNCL   ICM   R1,B'1111',LOCRDCM  DCM AREA TO BE FREED?
         BZ    NOARR               BRANCH IF NOT.
         L     R14,RDCMLG          LENGTH OF AREA.
        FREEMAIN R,A=(1),LV=(R14),SP=1
         XC    LOCRDCM,LOCRDCM     NULLIFY.
NOARR    ICM   R1,B'1111',LOCTDCM  TDCM AREA TO BE FREED?
         BZ    NOART               BRANCH IF NOT.
         L     R14,TDCMLG          LENGTH OF AREA.
        FREEMAIN R,A=(1),LV=(R14),SP=1
         XC    LOCTDCM,LOCTDCM     NULLIFY.
NOART    ICM   R1,B'1111',LOCBUF   TDCM BUFFER AREA TO BE FREED?
         BZ    NOARB               BRANCH IF NOT.
         L     R14,LOCBUFL         LENGTH OF AREA.
        FREEMAIN R,A=(1),LV=(R14),SP=1
         XC    LOCBUF,LOCBUF       NULLIFY.
NOARB    ICM   R1,B'1111',LOCORE   ORE AREA TO BE FREED?
         BZ    NOORE               BRANCH IF NOT.
         L     R14,LGTORE          LENGTH OF ORE AREA.
         AL    R14,LGTWQE          LENGTH OF WQE AREA.
        FREEMAIN R,A=(1),LV=(R14),SP=1
         XC    LOCORE,LOCORE       NULLIFY.
NOORE   ZEROKEY ,
        AXFRE  AXLIST=AXPL         FREE AX.
        RESETKEY ,
         MVC   FULLSCRN(CLEARL),CLEAR   SET CLEAR SEQUENCE.
         TM    SWITCH,SCRRS        WAS SCREEN SIZE CHANGED?
         BZ    ENDCL               BRANCH IF NOT.
        STSIZE SIZELOC=SZLINE,LINELOC=SZSCRN RESTORE ORIGINAL SIZE.
        $FS    CC=EWA,MF=(I,FULLSCRN)
ENDCL    LA    R1,FULLSCRN
         LA    R0,CLEARL
         ICM   R1,B'1000',TPUTFLG  INDICATE FULL SCREEN.
        TPUT   (1),(0),R
        STFSMODE OFF               TURN OFF FULL SCREEN MODE.
         LR    R15,R2
         CH    R15,=H'16'          IS RETURN CODE HIGHER THAN 16?
         BNH   EXIT                NO, LET STAY R15.
         EJECT
*------- THAT'S ALL FOLKS.
         SPACE 1
         XR    R15,R15             ZERO OUT REGISTER 15.
EXIT    $XRET  CC=(R15),LV=WRKL,TYPE=RENT
SETRC    LA    R15,16              SET ERROR RETURN CODE.
         B     EXIT
         EJECT
*------- LOCAL SUBROUTINE TO CONVERT R15 TO PRINTABLE HEX (EBCDIC).
         SPACE 1
CVTPRT   ST    R15,CVTB
         UNPK  CVTA(9),CVTB(5)
         NC    CVTA(8),HEXMASK
         TR    CVTA(8),HEXTAB
         BR    R9
         SPACE 1
*------- LOCAL SUBROUTINE TO WRITE SCREEN VIA TPUT,
*        AND TO GET REPLY VIA TGET.
         SPACE 1
STERM    TM    SWITCH,SCRST+SCRRS  FIRST TPUT DONE?
         BNO   SNDWR               BRANCH IF YES.
         ST    R1,ASCRN
        $FS    CC=EW,MF=(I,(R1))   SET ERASE/WRITE.
SNDWR    ICM   R1,B'1000',TPUTFLG  INDICATE FULL SCREEN.
        TPUT   (1),(0),R
         TM    SWITCH,SCRST+SCRRS  FIRST TPUT DONE?
         BNO   SNDGT               BRANCH IF YES.
         L     R1,ASCRN
        $FS    CC=W,MF=(I,(R1))    RESTORE WRITE.
         NI    SWITCH,255-SCRST    SET FIRST TPUT DONE.
SNDGT    MVC   PFREPLY(L'PFREPLY),LOTSBLNK
        TGET   PFREPLY,L'PFREPLY-1,ASIS
         TM    SWITCH,QUIT         TERMINATING PROCESS?
         BOR   R9                  YES.
         CH    R15,=H'20'          TERMINAL DISCONNECTED?
         BE    ENDIT               YES,... SPLIT.
         CH    R15,=H'8'           ATTENTION?
         BE    RESTART             YES,... START OVER.
         CH    R15,=H'12'          IS INPUT LONGER THAN BUFFER?
         BNE   SKIPCLRQ            NO.
        TCLEARQ INPUT              FLUSH THE TRASH.
SKIPCLRQ CLI   PFREPLY,X'F1'       PF 1 ENTERED?
         BE    DHELP               YES,... HELP.
         CLI   PFREPLY,X'C1'       PF 13 ENTERED (ALT. PF 1)?
         BE    DHELP               YES,... HELP.
         CLI   PFREPLY,X'F2'       PF 2 ENTERED?
         BE    RESTART             YES,... START OVER.
         CLI   PFREPLY,X'C2'       PF 14 ENTERED (ALT. PF 2)?
         BE    RESTART             YES,... START OVER.
         CLI   PFREPLY,X'F3'       PF 3 ENTERED?
         BE    ENDIT               YES,... SPLIT.
         CLI   PFREPLY,X'C3'       PF 15 ENTERED (ALT. PF 3)?
         BE    ENDIT               YES,... SPLIT.
         TM    SWITCH,ADSDV        WTOR'S MAY BE ACCEPTED?
         BZ    SKWTR               NO.
         CLI   PFREPLY,X'F5'       PF 5 ENTERED?
         BE    DWTOR               YES,... WTOR'S.
         CLI   PFREPLY,X'C5'       PF 17 ENTERED (ALT. PF 5)?
         BE    DWTOR               YES,... WTOR'S.
SKWTR    CLI   PFREPLY,X'F8'       PF 8 ENTERED?
         BE    SWPF8               YES,... SWITCH TRACE OPTION.
         CLI   PFREPLY,X'C8'       PF 20 ENTERED (ALT. PF 8)?
         BE    SWPF8               YES,... SWITCH TRACE OPTION.
         CLI   PFREPLY,X'F9'       PF 9 ENTERED?
         BE    SWPF9               YES,... SWITCH TRACE OPTION.
         CLI   PFREPLY,X'C9'       PF 21 ENTERED (ALT. PF 9)?
         BE    SWPF9               YES,... SWITCH TRACE OPTION.
         CLI   PFREPLY,X'7A'       PF 10 ENTERED?
         BE    SWPF10              YES,... SWITCH TRACE OPTION.
         CLI   PFREPLY,X'4A'       PF 22 ENTERED (ALT. PF 10)?
         BE    SWPF10              YES,... SWITCH TRACE OPTION.
         CLI   PFREPLY,X'7B'       PF 11 ENTERED?
         BE    SWPF11              YES,... SWITCH TRACE OPTION.
         CLI   PFREPLY,X'4B'       PF 23 ENTERED (ALT. PF 11)?
         BE    SWPF11              YES,... SWITCH TRACE OPTION.
         TM    SWITCH,RLCMD        LAST CMD RESTORE MAY BE ACCEPTED?
         BZR   R9                  NO.
         CLI   PFREPLY,X'7C'       PF 12 ENTERED?
         BE    LCMDR               YES,... SPLIT.
         CLI   PFREPLY,X'4C'       PF 24 ENTERED (ALT. PF 12)?
         BE    LCMDR               YES,... SPLIT.
         BR    R9
         EJECT
*------- DEBUG FACILITIES (SYSSNAP).
         SPACE 1
SWPF8    LA    R0,PF8
         B     SWTRT
SWPF9    LA    R0,PF9
         B     SWTRT
SWPF10   LA    R0,PF10
         B     SWTRT
SWPF11   LA    R0,PF11
SWTRT    TM    SWTRC,TRALL         TRACE ALLOWED?
         BZR   R9                  NO.
         LR    R1,R0
         EX    R1,TSTM             TEST TRACE OPTION (FLIP-FLOP).
         BO    SOPON
         EX    R1,TSOI             TRACE ON.
         BR    R9
SOPON    LA    R1,255
         XR    R1,R0
         EX    R1,TSNI             TRACE OFF.
         BR    R9
TSTM     TM    SWTRC,*-*           TEST OPTION.
TSOI     OI    SWTRC,*-*           SET OPTION.
TSNI     NI    SWTRC,*-*           CLEAR OPTION.
DHELP    TM    SWTRC,TRALL         TRACE ALLOWED?
         BZR   R9                  NO, ... DEBUG IS INACTIVE.
         MVC   PF8S,=CL8'INACTIVE'
         MVC   PF9S,=CL8'INACTIVE'
         MVC   PF10S,=CL8'INACTIVE'
         MVC   PF11S,=CL8'INACTIVE'
         TM    SWTRC,PF8
         BZ    *+L'*+6
         MVC   PF8S,=CL8'ACTIVE'
         TM    SWTRC,PF9
         BZ    *+L'*+6
         MVC   PF9S,=CL8'ACTIVE'
         TM    SWTRC,PF10
         BZ    *+L'*+6
         MVC   PF10S,=CL8'ACTIVE'
         TM    SWTRC,PF11
         BZ    *+L'*+6
         MVC   PF11S,=CL8'ACTIVE'
         LA    R1,HELPSCRN
         LA    R0,HELPLEN
         B     STERM
         EJECT
*------- DIAGNOSTIC'S MESSAGES.
         SPACE 1
        PRINT  NOGEN
NOTAUT  PUTIT  ' -> UNAUTHORIZED COMMAND.'
NOTSCR  PUTIT  ' -> DIDOCS RUN ONLY ON A SCREEN TERMINAL.'
NOTSTD  PUTIT  ' -> DIDOCS WANT TO BE SCREEN SIZED 24 * 80.'
NOSNAP  PUTIT  ' -> DIDOCS DEBUG NEEDS FILE SYSSNAP ALLOCATED.'
        PRINT  GEN
         EJECT
***********************************************************************
*        MESSAGES.                                                    *
***********************************************************************
         SPACE 1
WTOLOGP  WTO   'XXXXXXX CMD :                                          C
                                                      ',               C
               MCSFLAG=(REG0),MF=L
WTOLOGL  EQU   *-WTOLOGP
         SPACE 1
UNITMSGP $FS   SBA=(22,1),SF=(PROT),MF=L
         $FS   TEXT='SIMULATED OPER CONSOLE USING DIDOCS CONSOLE AT',  C
               MF=L
SFINT    $FS   SF=(PROT,INT),MF=L
         $FS   TEXT=(' ',4),MF=L
         $FS   TEXT=(' ',9),MF=L
         $FS   TEXT=(' ',8),MF=L
         SPACE 1
ERMSG    $FS   SBA=(1,1),SF=(PROT),MF=L
         $FS   TEXT='>>> NO ACCEPTABLE CONSOLES ACTIVE <<<',MF=L
         $FS   SBA=(1,1),SF=(IC),MF=L
ERMSGL   EQU   *-ERMSG
         SPACE 1
SNPMSG   $FS   SBA=(1,1),SF=(PROT),MF=L
SNPMSGO  EQU   (*-SNPMSG)+4
         $FS   TEXT='--- SNAP RC = ',MF=L
SNPRCL   EQU   *-SNPMSG
         $FS   TEXT='         ---',MF=L
         $FS   SBA=(1,1),SF=(IC),MF=L
SNPMSGL  EQU   *-SNPMSG
         SPACE 1
WCOMSG   $FS   SBA=(1,1),SF=(PROT),MF=L
         $FS   TEXT='*** UNEXPECTED ERROR ***',MF=L
         $FS   SBA=(1,1),SF=(IC),MF=L
WCOMSGL  EQU   *-WCOMSG
         EJECT
***********************************************************************
*        CONSTANTS.                                                   *
***********************************************************************
         SPACE 1
LGTORE   DC    A(((ORESIZE+7)/8)*8)
LGTWQE   DC    A((((WQEXA-WQE)+7)/8)*8)
CMNDP    DC    0F'0',AL2(79,0)
LOTSBLNK DC    CL90' '
HEXMASK  DC    XL8'0F0F0F0F0F0F0F0F'
HEXTAB   DC    C'0123456789ABCDEF'
BLANKTAB DC    CL256' '
         ORG   BLANKTAB+C'¢'
         DC    C'¢.<(+|',X'50'
         ORG   BLANKTAB+C'!'
         DC    C'!$*);¬-/'
         ORG   BLANKTAB+C','
         DC    C',%_>?'
         ORG   BLANKTAB+C':'
         DC    C':#@''="'
         ORG   BLANKTAB+X'81'      TRANSLATE LOWER CASE TO UPPER.
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+X'91'      ...
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+X'A2'      ...
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'A'
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+C'J'
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+C'S'
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'0'
         DC    C'0123456789'
         ORG
         EJECT
* TURN OFF FULL SCREEN (SPF), CLEAR AND RESET CURSOR TO ROW1, COL1.
         SPACE 1
CLEAR    $FS   CC=EW,WCC=(AL,KBR,RMDT),SBA=(24,79),MF=L
         $FS   SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(1,1),SF=(IC),MF=L
CLEARL   EQU   *-CLEAR
TPUTFLG  DC    X'03'               ITS FULL SCREEN TIME.
         SPACE 1
NOPAGE   DC    XL9'6D6D6D6D6D6D6D6D6D'
NXPAGE   DC    CL9' * MORE *'
         EJECT
***********************************************************************
*        SCREEN FORMATS.                                              *
***********************************************************************
         SPACE 1
FULLSCRP $FS   CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L SET BUFFER/CLEAR.
         $FS   SBA=(1,1),RA=(1,1,00),MF=L
FULLSCRL EQU   *-FULLSCRP
SELSPP   $FS   SF=(PROT),TEXT='FROM THIS LIST, ',MF=L
         $FS   TEXT='ENTER THE CONSOLE ADDRESS OF YOUR CHOICE :',MF=L
         $FS   SF=(INT),MF=L       START OF INPUT LINE.
SELCUUP  $FS   TEXT='CUU',MF=L     INPUT LINE : CONSOLE ADDRESS.
         $FS   SF=(PROT,INT),MF=L
SELWHATP $FS   TEXT=(' ',10),MF=L  RESERVED FOR DIAGNOSE.
         $FS   SF=(PROT),MF=L
         $FS   TEXT=(' ',6),MF=L   RESERVED.
SELSPL   EQU   *-SELSPP
SELT1    $FS   TEXT=(' ',4),MF=L
         $FS   SF=(PROT,INT),TEXT='CUU',MF=L
         $FS   SF=(PROT),TEXT=(' ID  AUTHORITY',31),MF=L
SELT1L   EQU   *-SELT1
SELT2    $FS   TEXT=(' ',4),MF=L
         $FS   SF=(PROT,INT),TEXT='---',MF=L
         $FS   SF=(PROT),TEXT=(' --  ---------------',31),MF=L
SELT2L   EQU   *-SELT2
SELDTP   $FS   TEXT=(' ',4),MF=L
         $FS   SF=(PROT,INT),TEXT=(' ',3),MF=L
         $FS   SF=(PROT),TEXT=(' ',21),MF=L
         $FS   SF=(PROT,INT),TEXT=(' ',2),MF=L
         $FS   SF=(PROT),TEXT=(' ',6),MF=L
SELDTL   EQU   *-SELDTP
SETIC    $FS   SBA=(1,1),SF=(PT,IC),MF=L
SETICL   EQU   *-SETIC
WSP      $FS   SBA=(21,1),SF=(PROT,INT),RA=(21,71,6D),MF=L
         $FS   TEXT=(' ',9),MF=L   NEXT PAGE INDICATOR.
WSPL     EQU   *-WSP
WSI      $FS   SBA=(24,1),SF=(PROT),MF=L     LAST LINE.
WSIL     EQU   *-WSI
WSF      $FS   SBA=(23,1),SF=NORMAL,MF=L     START OF INPUT LINE.
         $FS   TEXT=(' ',2),MF=L             REPLY NUMBER.
WSFL     EQU   *-WSF
SETCO    $FS   SBA=(1,1),SF=(PT,IC),MF=L
SETCOL   EQU   *-SETCO
OPRPSCRP $FS   CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L SET BUFFER/CLEAR.
         $FS   SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(1,1),SF=(PROT,INT),MF=L
         $FS   TEXT='D I D O C S - REPLIES PENDING IN SYSTEM : ',MF=L
         DC    CL(LSYSID)' '
         $FS   SF=(PROT),MF=L
         $FS   SBA=(2,1),MF=L
         $FS   TEXT='ENTER HERE THE REPLY-ID NUMBER',MF=L
         $FS   TEXT=' YOU WANT TO USE :',MF=L
         $FS   SF=(INT),MF=L
OPRNMP   $FS   TEXT='NN',MF=L      INPUT LINE : REPLY NUMBER.
         $FS   SF=(PROT),MF=L
         $FS   SBA=(3,1),TEXT=' ID  JOB-NAME  MESSAGE ...',MF=L
         $FS   SBA=(4,1),SF=(PROT,INT),RA=(4,4,-),MF=L
         $FS   TEXT=(' ',2),RA=(4,14,-),MF=L
         $FS   TEXT=(' ',2),RA=(4,71,-),MF=L
         $FS   TEXT=(' ',9),MF=L   CONTINUE...
         $FS   SF=(PROT),MF=L
OPRPSCRL EQU   *-OPRPSCRP
OPLSTP   $FS   SBA=(1,1),SF=(PT,IC),MF=L
OPLSTPL  EQU   *-OPLSTP
         EJECT
***********************************************************************
*        LITERAL POOL.                                                *
***********************************************************************
         SPACE 1
         LTORG
         SPACE 1
         DROP  R10,R11,R12,R13
         EJECT
***********************************************************************
*        TRACE ROUTINE (DEBUG AID / PF'S 1-8-9-10-11).                *
*              R0      = SNAP ID NUMBER (0-255).                      *
*              R1      = SNAP HEADER ADDRESS.                         *
*              R9-R10  = WORK AREAS POINTERS.                         *
*              R11     = BASE REGISTER.                               *
*              R13     = NEW SAVE AREA.                               *
*              R14     = LINK REGISTER.                               *
*              R15     = ENTRY ADDRESS.                               *
*              NOTE - THE FOLLOWING ALLOCATION IS NEEDED TO OBTAIN    *
*                     THE SNAP DUMPS : ALLOC F(SYSSNAP) SYSOUT(A)     *
***********************************************************************
         SPACE 1
         DS    0D
         USING *,R11
SNAP     STM   R14,R12,12(R13)     ENTRY.
         LR    R11,R15
         LR    R9,R13
         USING WRKDSECT,R9,R10
         LA    R13,SVSNAP
         ST    R9,4(R13)
         ST    R13,8(R9)
         LR    R2,R0
         ST    R1,SNAPHD
         OI    SNAPHD,X'80'
         LA    R3,SNAPDCB
         USING IHADCB,R3
         TM    SWTRC,SNOPEN        SNAP ALREADY OPENED?
         BO    SNS                 YES.
         MVC   SNAPDCB(SNAPLL),SNAPCS
         OPEN  ((R3),OUTPUT),MF=(E,SNAPOP)
         TM    DCBOFLGS,DCBOFOPN   OPENED?
         BO    SNO                 YES.
         LA    R15,32              SET OPEN ERROR.
         B     SNC
SNO      OI    SWTRC,SNOPEN
SNS      SNAP  DCB=(R3),TCB='S',ID=(R2),LIST=SNAPLST,                  C
               STRHDR=SNAPHD,MF=(E,SNAPLIST)
         LTR   R15,R15             ALL OK?
         BZ    SNR                 YES.
SNC      NI    SWTRC,255-TRALL     KILL ALLOWABLE.
SNR      L     R13,4(R13)          EXIT.
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         MVI   12(R13),X'FF'
         BR    R14
         SPACE 1
         DROP  R3,R9,R10,R11
         SPACE 1
         LTORG ,                   LITERAL POOL.
         EJECT
***********************************************************************
*        SKELETONS AND CONSTANTS.                                     *
***********************************************************************
         SPACE 1
         PRINT NOGEN
SNAPCS   DCB   DSORG=PS,RECFM=VBA,MACRF=(W),BLKSIZE=1632,LRECL=125,    C
               DDNAME=SYSSNAP
         OPEN  (*-*),MF=L
         CLOSE (*-*),MF=L
         SNAP  MF=L
SNAPLL   EQU   *-SNAPCS
         PRINT GEN
         SPACE 1
HD1      DC    AL1(L'HD1T)
HD1T     DC    C'D I D O C S - DUMP OF UCME.'
HD2      DC    AL1(L'HD2T)
HD2T     DC    C'D I D O C S - DUMP OF DCM.'
HD3      DC    AL1(L'HD3T)
HD3T     DC    C'D I D O C S - DUMP OF TDCM.'
HD10     DC    AL1(L'HD10T)
HD10T    DC    C'D I D O C S - DUMP OF TDCM BUFFER.'
HD20     DC    AL1(L'HD20T)
HD20T    DC    C'D I D O C S - DUMP OF ORE-WQE.'
HD100    DC    AL1(L'HD100T)
HD100T   DC    C'D I D O C S - DUMP OF WORK AREAS.'
         EJECT
***********************************************************************
*        VARIABLES.                                                   *
***********************************************************************
         SPACE 1
TABSET   DS    0XL5
         $FS   SBA=(1,1),SF=(PROT),MF=L
         $FS   SBA=(2,1),SF=(PROT),MF=L
         $FS   SBA=(3,1),SF=(PROT),MF=L
         $FS   SBA=(4,1),SF=(PROT),MF=L
         $FS   SBA=(5,1),SF=(PROT),MF=L
         $FS   SBA=(6,1),SF=(PROT),MF=L
         $FS   SBA=(7,1),SF=(PROT),MF=L
         $FS   SBA=(8,1),SF=(PROT),MF=L
         $FS   SBA=(9,1),SF=(PROT),MF=L
         $FS   SBA=(10,1),SF=(PROT),MF=L
         $FS   SBA=(11,1),SF=(PROT),MF=L
         $FS   SBA=(12,1),SF=(PROT),MF=L
         $FS   SBA=(13,1),SF=(PROT),MF=L
         $FS   SBA=(14,1),SF=(PROT),MF=L
         $FS   SBA=(15,1),SF=(PROT),MF=L
         $FS   SBA=(16,1),SF=(PROT),MF=L
         $FS   SBA=(17,1),SF=(PROT),MF=L
         $FS   SBA=(18,1),SF=(PROT),MF=L
         $FS   SBA=(19,1),SF=(PROT),MF=L
         $FS   SBA=(20,1),SF=(PROT),MF=L
         SPACE 1
FAKEWARN $FS   TEXT='PRESS',MF=L
         $FS   SF=(PROT,INT),TEXT='PF-3',MF=L
         $FS   SF=(PROT),TEXT='TO TERMINATE DIDOCS,',MF=L
         $FS   SF=(PROT,INT),TEXT='PF-2',MF=L
         $FS   SF=(PROT),MF=L
FAKEWRNR EQU   *-FAKEWARN
         $FS   TEXT='TO RESTART.                SYSTEM :',MF=L
         $FS   SF=(PROT,INT),MF=L
         DC    CL(LSYSID)' '
LSF      $FS   SF=(PROT),MF=L
FAKEWRNL EQU   *-FAKEWARN
         SPACE 1
FKWARN   $FS   TEXT='KEYS',MF=L
         $FS   SF=(PROT,INT),TEXT='PF-3',MF=L
         $FS   SF=(PROT),TEXT=': END,',MF=L
         $FS   SF=(PROT,INT),TEXT='PF-12',MF=L
         $FS   SF=(PROT),TEXT=': L-CMD,',MF=L
         $FS   SF=(PROT,INT),TEXT='PF-5',MF=L
         $FS   SF=(PROT),TEXT=': WTOR-S,',MF=L
         $FS   SF=(PROT,INT),TEXT='PF-2',MF=L
         $FS   SF=(PROT),MF=L
FKWRNR   EQU   *-FKWARN
         $FS   TEXT=': RESTART.  SYSTEM :',MF=L
         $FS   SF=(PROT,INT),MF=L
         DC    CL(LSYSID)' '
         $FS   SF=(PROT),MF=L
FKWRNL   EQU   *-FKWARN
         SPACE 1
HELPSCRP $FS   CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L SET BUFFER/CLEAR.
         $FS   SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(2,1),SF=(PROT,INT),MF=L
         $FS   TEXT='D I D O C S - DEBUG AID SERVICE :',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(5,21),TEXT='PF 1 OR 13 :',MF=L
         $FS   SF=(PROT,INT),TEXT='TRACE STATUS',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(6,21),TEXT='PF 8 OR 20 :',MF=L
         $FS   SF=(PROT,INT),MF=L
         $FS   TEXT=(' ',8),MF=L
         $FS   SF=(PROT),TEXT='(DUMP ORE-WQE)',MF=L
         $FS   SBA=(7,21),TEXT='PF 9 OR 21 :',MF=L
         $FS   SF=(PROT,INT),MF=L
         $FS   TEXT=(' ',8),MF=L
         $FS   SF=(PROT),TEXT='(DUMP UCME-DCM-TDCM)',MF=L
         $FS   SBA=(8,20),TEXT='PF 10 OR 22 :',MF=L
         $FS   SF=(PROT,INT),MF=L
         $FS   TEXT=(' ',8),MF=L
         $FS   SF=(PROT),TEXT='(DUMP TDCM BUFFER)',MF=L
         $FS   SBA=(9,20),TEXT='PF 11 OR 23 :',MF=L
         $FS   SF=(PROT,INT),MF=L
         $FS   TEXT=(' ',8),MF=L
         $FS   SF=(PROT),TEXT='(DUMP WORK AREAS)',MF=L
         $FS   SBA=(12,21),TEXT='PF 2 OR 14 :',MF=L
         $FS   SF=(PROT,INT),TEXT='SELECT CONSOLE',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(13,21),TEXT='PF 3 OR 15 :',MF=L
         $FS   SF=(PROT,INT),TEXT='TERMINATE DIDOCS',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(14,21),TEXT='PF 5 OR 17 :',MF=L
         $FS   SF=(PROT,INT),TEXT='PENDING REPLIES',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(15,20),TEXT='PF 12 OR 24 :',MF=L
         $FS   SF=(PROT,INT),TEXT='LAST COMMAND',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(16,27),TEXT='PA 1 :',MF=L
         $FS   SF=(PROT,INT),TEXT='INTERRUPT DIDOCS',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(1,1),SF=(IC),MF=L
         EJECT
***********************************************************************
*        PARSE CONTROL LIST.                                          *
***********************************************************************
         SPACE 1
         PRINT NOGEN
PARMLST IKJPARM
OPMODE  IKJKEYWD
        IKJNAME 'SYSTEM',ALIAS=('S')
        IKJNAME 'CASPER',ALIAS=('GHOST')
        IKJNAME 'DEBUG',ALIAS=('D')
        IKJENDP
         PRINT GEN
         EJECT
***********************************************************************
*        DATA - WORK AREAS DESCRIPTION.                               *
***********************************************************************
         SPACE 1
WRKDSECT DSECT                     WORK AREAS DESCRIPTION.
         SPACE 1
SVA      DS    18F                 SAVE AREA.
SVSNAP   DS    18F                 SNAP SAVE AREA.
CVTA     DC    D'0',X'0'           PLUS A GARBAGE BYTE.
XUCMID   DC    X'0'
CMDALLOW DC    C' '
AUTH     DC    X'0'
         SPACE 1
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(*-*)
REQAUTL  EQU   *-REQAUT
         SPACE 1
MOVEBSAV DS    3F                  REGISTERS SAVE AREA.
CVTB     DC    F'0'
SVTDCM   DC    F'0'
SVPARM   DC    2F'0'
SVOREPT  DC    F'0'
SZLINE   DC    F'0'                LINE SIZE (LINE LENGTH).
SZSCRN   DC    F'0'                SCREEN SIZE (NUMBER OF LINES).
ASCRN    DC    F'0'                SCREEN IMAGE ADDRESS.
SASID    DS    F                   PREVIOUS SECONDARY ASID.
CONASID  DS    F                   'CONSOLE' ASID (COMMTASK).
AXPL     DC    H'1',H'0'           AX PARM LIST.
         SPACE 1
        $TEW$WA
         SPACE 1
LOCRDCM  DC    A(0)                ADDRESS OF RESIDENT DCM AREA.
RDCML    DC    A(0)                LENGTH OF RESIDENT DCM AREA.
RDCMLG   DC    A(0)                LENGTH OF RESIDENT DCM IN CORE.
LOCTDCM  DC    A(0)                ADDRESS OF PAGEABLE DCM AREA.
TDCML    DC    A(0)                LENGTH OF PAGEABLE DCM AREA.
TDCMLG   DC    A(0)                LENGTH OF PAGEABLE DCM IN CORE.
LOCBUF   DC    A(0)                ADDRESS OF DCM BUFFER AREA.
LOCBUFL  DC    F'0'                LENGTH OF DCM BUFFER AREA.
BUFLGTH  DC    F'0'                LENGTH OF DCM BUFFER IN CORE.
LOCORE   DC    A(0)                ADDRESS OF ORE AREA.
LOCWQE   DC    A(0)                ADDRESS OF WQE AREA.
         SPACE 1
         PRINT NOGEN
WTOLOG   WTO   'XXXXXXX CMD :                                          C
                                                      ',               C
               MCSFLAG=(REG0),MF=L
         PRINT GEN
         ORG   WTOLOG+4
LOGUSER  DS    CL7
         ORG   WTOLOG+18
LOGCMD   DS    CL80
RNJ328X  DS    CL2
         ORG
         SPACE 1
         PRINT NOGEN
SNAPDCB  DCB   DSORG=PS,RECFM=VBA,MACRF=(W),BLKSIZE=1632,LRECL=125,    C
               DDNAME=SYSSNAP
SNAPOP   OPEN  (*-*),MF=L
SNAPCL   CLOSE (*-*),MF=L
SNAPLIST SNAP  MF=L
         PRINT GEN
SNAPLST  DS    2F
SNAPHD   DS    F
         SPACE 1
CMND     DC    0F'0',AL2(79,0)     SEND COMMAND AREA.
CMNDBUF  DC    CL80' '
PFREPLY  DS    0CL86               AREA TO HOLD TGET REPLY.
         DC    CL6' '              AID + JUNK.
TXREPLY  DC    CL80' '             REAL REPLY.
         SPACE 1
UNITMSG  $FS   SBA=(22,1),SF=(PROT),MF=L
         $FS   TEXT='SIMULATED OPER CONSOLE USING DIDOCS CONSOLE AT',  C
               MF=L
         $FS   SF=(PROT,INT),MF=L
UNITXXX  $FS   TEXT=(' ',4),MF=L
SVCRCM   $FS   TEXT=(' ',9),MF=L
NOTR15   $FS   TEXT=(' ',8),MF=L
UNITLEN  EQU   *-UNITMSG
         EJECT
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        CONSOLES SELECTION PANEL.                                    *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
SELSCRN  $FS   CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L SET BUFFER/CLEAR.
         $FS   SBA=(1,1),RA=(1,1,00),MF=L
SELT1A   DC    CL(SELT1L)' '
SELT1B   DC    CL(SELT1L)' '
SELT2A   DC    CL(SELT2L)' '
SELT2B   DC    CL(SELT2L)' '
SELDT    DC    40CL(SELDTL)' '     DATA LINES.
SELDTN   EQU   ((*-SELDT)/SELDTL)
SELSP    $FS   SF=(PROT),TEXT='FROM THIS LIST, ',MF=L
         $FS   TEXT='ENTER THE CONSOLE ADDRESS OF YOUR CHOICE :',MF=L
         $FS   SF=(INT),MF=L       START OF INPUT LINE.
SELCUU   $FS   TEXT='CUU',MF=L     INPUT LINE : CONSOLE ADDRESS.
         $FS   SF=(PROT,INT),MF=L
SELWHAT  $FS   TEXT=(' ',10),MF=L  RESERVED FOR DIAGNOSE.
         $FS   SF=(PROT),MF=L
         $FS   TEXT=(' ',6),MF=L   RESERVED.
INFLINE  DC    CL(FAKEWRNL)' '     INFORMATIONAL.
SETICT   $FS   SBA=(1,1),SF=(PT,IC),MF=L
SELSCRNL EQU   *-SELSCRN
         EJECT
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        CONSOLE DISPLAY PANEL.                                       *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
FULLSCRN $FS   CC=W,MF=L           SET BUFFER/CLEAR.
FULLSWCC $FS   WCC=(KBR,RMDT),SBA=(24,80),MF=L
         $FS   SBA=(1,1),RA=(1,1,00),MF=L
SCREEN   DC    20CL84' '           DATA LINES.
SCREENL  EQU   *-SCREEN            DATA LINES TOTAL LENGTH.
NDL      EQU   (SCREENL)/L'SCREEN  NUMBER OF LINES.
WSPT     $FS   SBA=(21,1),SF=(PROT,INT),RA=(21,71,6D),MF=L
NXTPGI   $FS   TEXT=(' ',9),MF=L   NEXT PAGE INDICATOR.
MSGLINE  DC    CL80' '
WSIT     $FS   SBA=(24,1),SF=(PROT),MF=L     LAST LINE.
INFOLINE DC    CL(FKWRNL)' '       INFORMATIONAL.
WSFT     $FS   SBA=(23,1),MF=L     START OF INPUT LINE.
WSFM     $FS   SF=NORMAL,MF=L
RPLYNO   $FS   TEXT=(' ',2),MF=L   REPLY NUMBER.
SETCOT   $FS   SBA=(1,1),SF=(PT,IC),MF=L
SCRLEN   EQU   *-FULLSCRN
         ORG   RPLYNO
RPTCMD   DC    CL79' '             REPEAT COMMAND.
         DC    XL(SETCOL)'0'
RPTLEN   EQU   *-RPTCMD
SCRLENF  EQU   SCRLEN-L'RPLYNO
         EJECT
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        OPERATORS REPLIES PANEL.                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
OPRPSCRN $FS   CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L SET BUFFER/CLEAR.
         $FS   SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(1,1),SF=(PROT,INT),MF=L
         $FS   TEXT='D I D O C S - REPLIES PENDING IN SYSTEM : ',MF=L
OPSYS    DC    CL(LSYSID)' '
         $FS   SF=(PROT),MF=L
         $FS   SBA=(2,1),MF=L
         $FS   TEXT='ENTER HERE THE REPLY-ID NUMBER',MF=L
         $FS   TEXT=' YOU WANT TO USE :',MF=L
         $FS   SF=(INT),MF=L
OPRNM    $FS   TEXT='NN',MF=L      INPUT LINE : REPLY NUMBER.
         $FS   SF=(PROT),MF=L
         $FS   SBA=(3,1),TEXT=' ID  JOB-NAME  MESSAGE ...',MF=L
         $FS   SBA=(4,1),SF=(PROT,INT),RA=(4,4,-),MF=L
         $FS   TEXT=(' ',2),RA=(4,14,-),MF=L
         $FS   TEXT=(' ',2),RA=(4,71,-),MF=L
OPNXT    $FS   TEXT=(' ',9),MF=L   CONTINUE...
         $FS   SF=(PROT),MF=L
OPWTRS   DC    20CL80' '           WTOR'S INFO LINES.
OPWTRSL  EQU   *-OPWTRS
OPLST    $FS   SBA=(1,1),SF=(PT,IC),MF=L
OPRPLEN  EQU   *-OPRPSCRN
         EJECT
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        DEBUG HELP PANEL.                                            *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
HELPSCRN $FS   CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L SET BUFFER/CLEAR.
         $FS   SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(2,1),SF=(PROT,INT),MF=L
         $FS   TEXT='D I D O C S - DEBUG AID SERVICE :',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(5,21),TEXT='PF 1 OR 13 :',MF=L
         $FS   SF=(PROT,INT),TEXT='TRACE STATUS',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(6,21),TEXT='PF 8 OR 20 :',MF=L
         $FS   SF=(PROT,INT),MF=L
PF8S     $FS   TEXT=(' ',8),MF=L
         $FS   SF=(PROT),TEXT='(DUMP ORE-WQE)',MF=L
         $FS   SBA=(7,21),TEXT='PF 9 OR 21 :',MF=L
         $FS   SF=(PROT,INT),MF=L
PF9S     $FS   TEXT=(' ',8),MF=L
         $FS   SF=(PROT),TEXT='(DUMP UCME-DCM-TDCM)',MF=L
         $FS   SBA=(8,20),TEXT='PF 10 OR 22 :',MF=L
         $FS   SF=(PROT,INT),MF=L
PF10S    $FS   TEXT=(' ',8),MF=L
         $FS   SF=(PROT),TEXT='(DUMP TDCM BUFFER)',MF=L
         $FS   SBA=(9,20),TEXT='PF 11 OR 23 :',MF=L
         $FS   SF=(PROT,INT),MF=L
PF11S    $FS   TEXT=(' ',8),MF=L
         $FS   SF=(PROT),TEXT='(DUMP WORK AREAS)',MF=L
         $FS   SBA=(12,21),TEXT='PF 2 OR 14 :',MF=L
         $FS   SF=(PROT,INT),TEXT='SELECT CONSOLE',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(13,21),TEXT='PF 3 OR 15 :',MF=L
         $FS   SF=(PROT,INT),TEXT='TERMINATE DIDOCS',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(14,21),TEXT='PF 5 OR 17 :',MF=L
         $FS   SF=(PROT,INT),TEXT='PENDING REPLIES',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(15,20),TEXT='PF 12 OR 24 :',MF=L
         $FS   SF=(PROT,INT),TEXT='LAST COMMAND',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(16,27),TEXT='PA 1 :',MF=L
         $FS   SF=(PROT,INT),TEXT='INTERRUPT DIDOCS',MF=L
         $FS   SF=(PROT),MF=L
         $FS   SBA=(1,1),SF=(IC),MF=L
HELPLEN  EQU   *-HELPSCRN
         EJECT
*------- PROCESSING SWITCHES.
         SPACE 1
SWITCH   DC    X'0'
NXTPG    EQU   X'01'               MORE DATA TO BE DISPLAYED.
SCRST    EQU   X'02'               SCREEN SIZE TO BE SET.
SCRRS    EQU   X'04'               SCREEN SIZE TO BE RESET.
QUIT     EQU   X'08'               TERMINATING PROCESS.
ADSDV    EQU   X'10'               ACCEPT WTOR REQUEST.
J328X    EQU   X'20'               ACCEPT REPLY TO JES328X.
RLCMD    EQU   X'40'               ACCEPT REPEAT LAST COMMAND.
         SPACE 1
SWTRC    DC    X'0'
TRALL    EQU   X'01'               TRACE ALLOWED.
SNOPEN   EQU   X'02'               SNAP OPENED.
NXTPGT   EQU   X'04'               NEXT PAGE PROCESS.
PF8      EQU   X'10'               PF8 SNAP ACTIVE.
PF9      EQU   X'20'               PF9 SNAP ACTIVE.
PF10     EQU   X'40'               PF10 SNAP ACTIVE.
PF11     EQU   X'80'               PF11 SNAP ACTIVE.
         SPACE 1
WRKL     EQU   (((*-SVA)+7)/8)*8   WORK AREAS LENGTH.
         EJECT
*------- DSECTS DEFINITIONS.
         SPACE 1
        PRINT  NOGEN
        IEESMCA
        IEECUCM ,                  MVS UCM DEFINITION.
DCMTSRT  DSECT DCMTSPTR                                            -EU-
        IEECRDCM DSECT=YES         MVS RESIDENT DCM DEFINITION (RDCM).
DCMREND  DS    0C                  END OF DCM                      -EU-
DCMSTRT  DSECT DCMSTPTR                                            -EU-
        IEECDCM ,                  MVS TRANSIENT DCM DEFINITION (TDCM).
UCBDSECT DSECT
        IEFUCBOB
        IHAORE ,                                                   -EU-
        IHAWQE ,                                                   -EU-
        DCBD   DSORG=PS,DEVD=DA
        $TEW$DS ,
        PRINT  GEN
         SPACE 1
         END
