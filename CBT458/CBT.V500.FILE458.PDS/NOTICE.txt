NTCS     TITLE 'NOTICE - COMMAND PROCESSOR FOR NOTICES.'
         PRINT OFF                                                 -EU-
         COPY  GTEMACS                                             -EU-
         PRINT ON                                                  -EU-
         SPACE 1                                                   -EU-
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -EU-*
* Origin : Extracted from CBT tape 86 FEB, file 160.              -EU-*
*   Note : See original material for more details (BULLETIN).     -EU-*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -EU-*
         SPACE 1                                                   -EU-
*******************************************************************-EU-
***                                                                -EU-
*** EU modifications : MOINIL P.A. - Computing Centre              -EU-
***                    J.R.C. - Ispra establishment                -EU-
***                    21020 Ispra (VA), Italy                     -EU-
***      1. Change the name BULLETIN by NOTICE to override a       -EU-
***         confusion at our installation.                         -EU-
***      2. Use of our local authorization SVC.                    -EU-
***      3. Insert of our authority check routine to determine     -EU-
***         which process may be allowed to the caller.            -EU-
***      4. Reduce to minimum the instructions running supervisor  -EU-
***         mode, key 0.                                           -EU-
***      5. When NOCLEAR is specified, use SE '...',LOGON instead  -EU-
***         of SE '...',SAVE command.                              -EU-
***                                                                -EU-
*******************************************************************-EU-
         SPACE 1
**********************************************************************
***                                                                ***
***  Set the following global variables before program assembly :  ***
***                                                                ***
***    &SYSBC   - The name of the system notices data-set, which   ***
***               will be the default used with the BROADCAST      ***
***               operand.                                         ***
***                                                                ***
***    &WAIT    - The number of hundredths of seconds to wait      ***
***               between succeeding SEND commands.                ***
***                                                                ***
**********************************************************************
         EJECT ,                                                   -EU-
NOTICE   START 0                                                   -EU-
         SPACE 1
**********************************************************************
***                                                                ***
***  Syntax -                                                      ***
***                                                                ***
***  NOTICE  CLEAR(NN)  BROADCAST(dsname)                          ***
***          CL(NN)     BC(DSNAME)                                 ***
***          NOCLEAR    NOBROADCAST                                ***
***          NOCL       NOBC                                       ***
***          LIST                                                  ***
***          TEST                                                  ***
***                                                                ***
**********************************************************************
         SPACE 1
**********************************************************************
***                                                                ***
***  The NOTICE command is an interface between TSO and SVC-34,    ***
***  the console operator SVC.                                     ***
***                                                                ***
***  NOTICE reads text from a data-set, then builds and executes   ***
***  operator SEND 'text',SAVE commands. When reading entries from ***
***  the input data-set, NOTICE will process start/stop dates      ***
***  for individual items.                                         ***
***                                                                ***
***  The CLEAR and BROADCAST functions of NOTICE require special   ***
***  privilege (OPERATOR). Only a scan of the input data-set is    ***
***  allowed to all other users of 'NOTICE' (LIST or TEST).        ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  ASM options :  RENT                                           ***
***                                                                ***
***  LKED options : RENT                                           ***
***                                                                ***
***  Return codes : 04 - A. Error in parsing operands.             ***
***                      B. Error in dynamic allocation.           ***
***                      C. ABEND was trapped by the command.      ***
***                 00 - Anything else.                            ***
***                                                                ***
***  Register Usage :                                              ***
***      R0-R1 - Linkage conventions, macros, work registers.      ***
***      R2-R3 - Macros, work registers.                           ***
***      R4-R6 - Work registers.                                   ***
***         R7 - (unused)                                          ***
***         R8 - (unused)                                          ***
***         R9 - Base of PARMPDL, returned by IKJPARS.             ***
***        R10 - BAS register.                                     ***
***        R11 - Second base register (NOTICE)                     ***
***        R12 - First base register (NOTICE+4096)                 ***
***        R13 - Linkage conventions, base of WORKD work area.     ***
***    R14-R15 - Linkage conventions, macros, work registers.      ***
***                                                                ***
***                                                                ***
***  Non-IBM macros used :                                         ***
***    GTEDADAT - Creates dynamic allocation control blocks.       ***
***    GTEDASET - Links together control blocks created by         ***
***               GTEDADAT.                                        ***
***    GTEDAALC - Executes dynamic allocation using control        ***
***               blocks created by GTEDADAT and linked together   ***
***               by GTEDASET.                                     ***
***    GTEDADOC - Not actually used.  This macro is documentation  ***
***               for the other GTEDAxxx macros.                   ***
***                                                                ***
**********************************************************************
         EJECT ,                                                   -EU-
         LCLC  &SYSBC,&WAIT        DEFINE LOCAL CONSTANTS
         SPACE 1                                                   -EU-
&SYSBC   SETC  '->.OPER.NOTICES.DATA'   SYSTEM NOTICES DSN (DSORG=PS)
&WAIT    SETC  '020'               NO OF 100THS OF SECONDS WAIT
         SPACE 2
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *  -EU-
*        AUTHORITY LEVELS DEFINITIONS.                          *  -EU-
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *  -EU-
         SPACE 1                                                   -EU-
LVL10    EQU   10        OPERATORS LEVEL                           -EU-
         SPACE 1                                                   -EU-
        $MDL@IX ,                                                  -EU-
         SPACE 2                                                   -EU-
        $DEFREG ,                                                  -EU-
         EJECT
**********************************************************************
***                                                                ***
***   PROLOG                                                       ***
***                                                                ***
***     1.  Linkage conventions and addressability.                ***
***     2.  Clear the completion code                              ***
***     3.  Save the pointer to the CPPL.                          ***
***     4.  Get caller authority value.                        -EU-***
***                                                                ***
**********************************************************************
         SPACE 1
        $XENT  BASE=(R11,R12),LV=WORKDLEN,TYPE=RENT                -EU-
         EJECT ,                                                   -EU-
         LR    R8,R13              ADDRESSABILITY                  -EU-
         USING WORKD,R8              OF WORK AREA                  -EU-
         USING PARMPDL,R9          ADDRESSABILITY OF PDE LIST
         XC    COMPCODE,COMPCODE   CLEAR COMPLETION CODE TO ZERO
         ST    R1,CPPLPTR          SAVE CPPL POINTER
         XC    AUTH(REQAUTL),AUTH  get caller authority            -EU-
         LA    R1,AUTH                                             -EU-
         ST    R1,REQAUT+4                                         -EU-
         MVC   REQAUT+1(3),MOD@IX                                  -EU-
        $EACM  REQAUT                                              -EU-
         EJECT
**********************************************************************
***                                                                ***
***   MAIN LINE ROUTINE                                            ***
***                                                                ***
**********************************************************************
         SPACE 1
         BAS   R10,ABEND           SET UP ABEND EXIT
         BAS   R10,PPLSETUP        SET UP PARSE PARM LIST
         BAS   R10,PARSE           PARSE THE INPUT PARAMETERS
         CLC   RETCDE(4),F0        IF RETURN CODE IS BAD
         BNE   ENDING04              GO TO CC=4 ENDING
         BAS   R10,DSNALLOC        ALLOCATE THE INPUT DSNAME
         CLC   RETCDE(4),F0        IF RETURN CODE IS BAD
         BE    MACLEAR               THEN
         MVC   COMPCODE(4),F4          SET CC=4
         B     MAUNALC             GO TO UNALLOCATION
MACLEAR  BAS   R10,CLEAR           CLEAR THE OLD MESSAGES
         BAS   R10,BC              SEND BROADCAST MESSAGES
MAUNALC  BAS   R10,UNALLOC         UNALLOCATE THE FILE NAMES
         B     ENDING              BRANCH TO ENDING
         EJECT
**********************************************************************
***                                                                ***
***   EPILOG                                                       ***
***                                                                ***
***     1.  Release storage used by IKJPARS.                       ***
***     2.  Linkage conventions.                                   ***
***                                                                ***
**********************************************************************
         SPACE 1
ENDING04 LA       R15,X'04'(0,0)   RETURN CODE TO R15
         ST       R15,COMPCODE     SAVE IN COMPLETION CODE
         SPACE 1
ENDING   LA       R2,MYPPL+(PPLANS-PPL) ADDRESS OF PTR TO PDL
         L        R2,0(0,R2)       R4 POINTS TO PDL
         IKJRLSA  (R2)             FREE STORAGE OF PDL
         ESTAE    0                CANCEL THE ABEND EXIT
         L        R2,COMPCODE      R2 TEMPORARILY HAS COMP CODE
        $XRET     CC=(R2),LV=WORKDLEN,TYPE=RENT   RETURN           -EU-
         EJECT
**********************************************************************
***                                                                ***
***   SET UP EXIT FOR ABENDS                                       ***
***                                                                ***
***  Use ESTAE to set up an environment to trap ABENDs.            ***
***                                                                ***
***  1.  The exit is ESTXIT.                                       ***
***  2.  The return point is at label ENDING04 in main routine.    ***
***  3.  The parm list passed to the exit will be:                 ***
***                                                                ***
***         ESTUPL                                                 ***
***         +-----------+                                          ***
***         |           |==> Return point                          ***
***         +-----------+                                          ***
***         |           |==> User work area                        ***
***         +-----------+    +-----------+                         ***
***                          | Register  |                         ***
***                          | save area |                         ***
***                         ---         ---                        ***
***                         ---         ---                        ***
***                          |           |                         ***
***                          +-----------+                         ***
***                          | Other data|                         ***
***                          +-----------+                         ***
***                                                                ***
**********************************************************************
         SPACE 1
ABEND    ST    R10,ABRBALSV        SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE       SET ERROR CODE TO ZERO
         MVC   ESTAEL(ESTAELLN),ESTAED  INITIALIZE ESTAE PARM LIST
         LA    R15,ENDING04        ADDRESS OF RETURN POINT
         ST    R15,ESTUPL            INTO USER PARM LIST
         LA    R15,ESTUWK          ADDRESS OF USER WORK AREA
         ST    R15,ESTUPL+4          INTO USER PARM LIST
         LA    R4,ESTUPL           R4 HAS ADDRESS OF USER PARM LST
         L     R5,VESTXIT          R5 HAS ADDRESS OF ESTAE EXIT
         ESTAE (R5),PARAM=(R4),MF=(E,ESTAEL) EXECUTE ESTAE
         L     R10,ABRBALSV        RESTORE RETURN ADDRESS
         BR    R10                 RETURN
         EJECT
**********************************************************************
***                                                                ***
***   CREATE PARSE PARAMETER LIST AND I/O PARM LIST                ***
***                                                                ***
***        PARSE                                                   ***
***          1.  Copy addresses of UPT, ECT and CBUF from CPPL.    ***
***          2.  Add addresses of own ECB, ANS and UWA, and        ***
***              address of PCE List (PCL) created by macros.      ***
***                                                                ***
***        I/O                                                     ***
***          1.  Copy addresses of UPT and ECT from CPPL.          ***
***          2.  Add address of own I/O ECB.                       ***
***          3.  Zero the IOPLIOPB pointer.  This will point to    ***
***              the parm block for the appropriate I/O routine.   ***
***              It will be filled in by the execute form of the   ***
***              STACK, GETLINE, PUTLINE or PUTGET macro when      ***
***              executed.                                         ***
**********************************************************************
         SPACE 1
PPLSETUP ST    R10,PPRBALSV        SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE       SET ERROR CODE TO ZERO
         L     R4,CPPLPTR          ADDRESS OF CMD PROC PARM LIST
         USING CPPL,R4               ADDRESSABILITY
         LA    R6,MYPPL            ADDRESS OF MY PARSE PARM LIST
         USING PPL,R6                ADDRESSABILITY
         MVC   PPLUPT(4),CPPLUPT   UPT  (CPPL)
         MVC   PPLECT(4),CPPLECT   ECT  (CPPL)
         LA    R5,MYECB
         ST    R5,PPLECB           ECB  (MINE)
         MVC   PPLPCL(4),VPARMPCL  PCL  (CSECT)
         LA    R5,MYANS
         ST    R5,PPLANS           ANS  (MINE)
         MVC   PPLCBUF(4),CPPLCBUF      CBUF (CPPL)
         LA    R5,MYUWA
         ST    R5,PPLUWA           UWA  (MINE)
         DROP  R6                  DROP ADDRESSABILITY
         LA    R6,MYIOPL           ADDRESS OF MY IO PARM LIST
         USING IOPL,R6               ADDRESSABILITY
         MVC   IOPLUPT(4),CPPLUPT  UPT  (CPPL)
         MVC   IOPLECT(4),CPPLECT  ECT  (CPPL)
         LA    R15,MYIOECB
         ST    R15,IOPLECB         ECB  (MINE)
         XC    IOPLIOPB(4),IOPLIOPB     IOPB (0000)
         DROP  R4,R6
         L     R10,PPRBALSV        RESTORE RETURN ADDRESS
         BR    R10                 RETURN
         EJECT
**********************************************************************
***                                                                ***
***   PARSE THE INPUT PARAMETER STRING                             ***
***                                                                ***
***      1.  Execute IKJPARS with CALLTSSR using own PPL.          ***
***      2.  Load address of Parm Descriptor Element List (PDL)    ***
***          from own ANS word into R9 for addressability.         ***
***                                                                ***
**********************************************************************
         SPACE 1
PARSE    ST    R10,PARBALSV        SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE       SET ERROR CODE TO ZERO
         XC    MYECB,MYECB         ZERO THE ECB FOR PARSE
         CALLTSSR EP=IKJPARS,MF=(E,MYPPL)    PARSE THE PARMS
         L     R9,MYPPL+(PPLANS-PPL)    POINTER TO PDL ADDRESS
         L     R9,0(0,R9)          ADDRESSABILITY OF PDL
         L     R10,PARBALSV        RESTORE RETURN ADDRESS
         BR    R10                 RETURN
         EJECT
**********************************************************************
***                                                                ***
***   ALLOCATE THE INPUT DSNAME WITH GTEDAxxx                      ***
***                                                                ***
**********************************************************************
         SPACE 1
DSNALLOC ST    R10,DSRBALSV        SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE       SET ERROR CODE TO ZERO
         TM    KEYBC+1,X'01'       IF NOT USING BC KEYWORD
         BNO   DSEND                 GO TO NEXT ROUTINE
         MVC   DSDDNAM1(8),DSDDNAM DUMMY DDNAME TO SAVE AREA
         MVC   MY1(MY1LEN),@M1     INITIALIZE SVC-99 CONTROL BLOCKS
         GTEDASET MY1,CPPLPTR=CPPLPTR   LINK BLOCKS TOGETHER
         L     R4,SBCDSN           R4 POINTS TO BC-DSN
         LH    R5,SBCDSN+4         R5 HAS LENGTH
         BCTR  R5,0                  MINUS 1 FOR EXEC
         B     *+L'*+6             BRANCH AROUND MOVE
         MVC   MY1DSNAM(*-*),0(R4) MOVE DSNAME TO T.U.
         EX    R5,*-6              EXECUTE THE MOVE
         TM    SBCDSN+14,X'80'     IF MEMBER NAME NOT USED
         BNO   DSDSNBCX              BRANCH AROUND
         L     R4,SBCDSN+8         R4 POINTS TO BC-MEMBNAME
         LH    R5,SBCDSN+12        R5 HAS LENGTH
         BCTR  R5,0                  MINUS 1 FOR EXEC
         B     *+L'*+6             BRANCH AROUND MOVE
         MVC   MY1MEMBR(*-*),0(R4) MOVE MEMBER NAME TO T.U.
         EX    R5,*-6              EXECUTE THE MOVE
         B     DSNALC1             BRANCH TO ALLOCATE
DSDSNBCX XC    MY1MEMKY(2),MY1MEMKY     NO MEMBER, ZERO THE KEY
DSNALC1  GTEDAALC MY1,VERB=AL,ERRMSG=YES     PERFORM SVC-99/DAIRFAIL
         MVC   DSDDNAM1(8),MY1RTDDN     SAVE THE RETURNED DDNAME
         CLC   MY1S99RC,F0         IF SVC-99 R/C ZERO
         BE    DSEND                 GO TO END
         LA    R15,X'04'(0,0)      SET ERROR VALUE
         ST    R15,RETCDE            INTO INTERNAL RETURN CODE
DSEND    L     R10,DSRBALSV        RESTORE RETURN ADDRESS
         BR    R10                 RETURN
         EJECT
**********************************************************************
***                                                                ***
***   If the CLEAR keyword has been used, clear old messages.      ***
***                                                                ***
**********************************************************************
         SPACE 1
CLEAR    ST    R10,CLRBALSV        SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE       SET ERROR CODE TO ZERO
         LH    R1,KEYC             IF CLEAR/... KEYWORD            -EU-
         LTR   R1,R1                 IS NOT USED                   -EU-
         BZ    CLEND                   GO TO END OF ROUTINE        -EU-
         BCT   R1,CLEND            IF NOT CLEAR, GO TO END         -EU-
         CLI   AUTH,LVL10          OPERATOR LEVEL ?                -EU-
         BNL   CLOPERXN              YES, BRANCH TO CONTINUE       -EU-
         LA    R15,PU0003              ELSE SELECT MESSAGE #3      -EU-
         BAS   R10,PUTLPROC              WRITE IT
         B     CLRET04             GO TO R/C=4 ENDING
CLOPERXN L     R3,SC               R3 POINTS TO CLEAR COUNT
         L     R3,0(0,R3)          R3 CONTAINS BINARY COUNT
         LTR   R3,R3               IF COUNT NOT POSITIVE
         BP    CLMSGNO               THEN
         LA    R15,PU0007              SELECT MESSAGE #7           -EU-
         BAS   R10,PUTLPROC            WRITE THE MESSAGE
         LA    R15,PU0000              SELECT BLANK LINE           -EU-
         BAS   R10,PUTLPROC            WRITE THE MESSAGE
         B     CLRET04                 GO TO RC=4 ENDING
CLMSGNO  XR    R4,R4               CLEAR R4 MSG NUMBER
CLLOOP1  LA    R4,1(0,R4)          INCR MSG NUMBER BY 1, BINARY
         CVD   R4,CLDOUBLE         CLDOUBLE HAS MSG NUMBER PACK
         OI    CLDOUBLE+7,X'0F'    KILL THE SIGN
         UNPK  CLZONE(3),CLDOUBLE+6(2)  CLZONE HAS MSG NUMBER ZD
         MVC   CLCMD(18),@CLCMD    INITIALIZE THE COMMAND
         MVC   CLCMD+9(2),CLZONE+1 INSERT THE MSG NUMBER
        ZEROKEY ,                                                  -EU-
         LA    R1,CLCMD            POINT TO THE COMMAND FOR SVC-34
         XR    R0,R0               CLEAR R0 FOR SVC-34
         SVC   34                  OPERATOR
        RESETKEY ,                                                 -EU-
         STIMER WAIT,BINTVL=BIWAITIM    WAIT A LITTLE BIT
         BCT   R3,CLLOOP1          LOOP BACK UP
         LA    R15,PU0002          SELECT MESSAGE #2               -EU-
         BAS   R10,PUTLPROC        WRITE THE MESSAGE
         LA    R15,PU0000          SELECT BLANK LINE               -EU-
         BAS   R10,PUTLPROC        WRITE THE MESSAGE
         B     CLEND
CLRET04  LA    R15,X'04'           RETURN CODE TO R15
         ST    R15,RETCDE          SAVE IT
CLEND    L     R10,CLRBALSV        RESTORE RETURN ADDRESS
         BR    R10                 RETURN
         EJECT
**********************************************************************
***                                                                ***
***   Execute the operator SEND command.                           ***
***                                                                ***
**********************************************************************
         SPACE 1
BC       ST    R10,BCRBALSV        SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE       SET ERROR CODE TO ZERO
         TM    KEYBC+1,X'01'       IF BROADCAST KEYWORD NOT USED
         BNO   BCEND                 BYPASS THE WHOLE ROUTINE
         TIME  ,                   R1 GETS 00YYDDDF
         ST    R1,BCYYDDD          SAVE IT
         NI    BCYYDDD+3,X'FC'     SET POSITIVE SIGN
         MVC   LOYYDDD,P0          INITIALIZE LOW YYDDD
         MVC   HIYYDDD,P99365      INITIALIZE HIGH YYDDD
         XC    BITEXTWK(8),BITEXTWK     INITIALIZE TEXT WORK SAVE AREA
         MVC   BCDCB(BCDCBL),@BCDCB     INITIALIZE DCB
         MVC   BCDCB+(DCBDDNAM-IHADCB)(8),DSDDNAM1 INITIALIZE DDN
         MVC   OPENL(OPENLLEN),@OPENL   INITIALIZE OPEN PARM LIST
         LA    R4,BCDCB            POINT TO DCB
         OPEN  ((R4),INPUT),MF=(E,OPENL)     OPEN THE FILE
         TM    (DCBRECFM-IHADCB)(R4),DCBRECF IF RECFM NOT FIXED
         BNO   BCBADFIL                        OR
         CLC   (DCBLRECL-IHADCB)(2,R4),H80   IF LRECL NOT 80
         BNE   BCBADFIL                        GO TO ERROR ROUTINE
BCIO     BAS   R10,BIO             PERFORM IO ROUTINE
         TM    BISWITCH,X'80'      END OF FILE ?
         BZ    BCIO                NO, LOOP UP TILL END OF FILE
         MVC   CLOSL(CLOSLLEN),@CLOSL   INITIALIZE CLOSE PARM LIST
         LA    R4,BCDCB            POINT TO DCB
         CLOSE ((R4)),MF=(E,CLOSL) CLOSE THE FILE
         B     BCEND
BCBADFIL LA    R15,PU0004          SELECT MESSAGE #4               -EU-
         BAS   R10,PUTLPROC        WRITE THE MESSAGE
BCRET04  LA    R15,X'04'           RETURN CODE TO R15
         ST    R15,RETCDE          SAVE IT
BCEND    L     R10,BCRBALSV        RESTORE RETURN ADDRESS
         BR    R10                 RETURN
         EJECT
**********************************************************************
***                                                                ***
***   UNALLOCATE FILE NAME                                         ***
***                                                                ***
**********************************************************************
         SPACE 1
UNALLOC  ST    R10,UNRBALSV        SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE       SET ERROR CODE TO ZERO
         TM    KEYBC+1,X'01'       IF BROADCAST KEYWORD NOT USED  -EU-
         BNO   UNEND                 BYPASS THE WHOLE ROUTINE     -EU-
         MVC   MY0(MY0LEN),@M0     INITIALIZE SVC-99 CONTROL BLOCKS
         GTEDASET MY0,CPPLPTR=CPPLPTR   LINK BLOCKS TOGETHER
         MVC   MY0DDNAM(8),DSDDNAM1     DDNAME TO T.U.
         GTEDAALC MY0,VERB=UN,ERRMSG=NO PERFORM SVC-99/DAIRFAIL
UNEND    L     R10,UNRBALSV        RESTORE RETURN ADDRESS
         BR    R10                 RETURN
         EJECT
**********************************************************************
***                                                                ***
***   Transfer BROADCAST input to output.                          ***
***                                                                ***
**********************************************************************
         SPACE 1
BIO      ST    R10,BIRBALSV        SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE       SET ERROR CODE TO ZERO
         LA    R4,BCDCB            POINT TO DCB
         NI    BISWITCH,X'7F'      INITIALIZE EOF SWITCH OFF
         GET   (R4),BITEXT         GET INPUT
         BAS   R10,ETEXT           EDIT THE INPUT
         CLC   RETCDE(4),F0        IF NOT PRINT RETURN CODE
         BNE   BIEND                 GO TO ENDING
         B     BIMSG               BRANCH AROUND EOF ROUTINE
BIEOF    OI    BISWITCH,X'80'      SET EOF SWITCH ON
         CLC   BITEXTWK(5),ETENDE  IF PREV USED CARD WAS '-END '
         BE    BIEND                 GO TO ENDING
         CLI   BITEXTWK,X'00'      IF NO PREV CARD WAS USED
         BE    BIEND                 GO TO ENDING
         MVC   BIMAIN(85),@BIHYPH  INITIALIZE COMMAND
         LA    R1,BIMAIN+81                                        -EU-
         ST    R1,APPOS                                            -EU-
BIMSG    MVC   MSG0001B(2),H73     SET MESSAGE SEGMENT LENGTH
         MVC   MSG0001B+2(2),H9    SET MESSAGE SEGMENT OFFSET
         CLC   ETTEXTWK(5),ETENDE  IF IT IS '-END' CARD            -EU-
         BNE   *+L'*+10                                            -EU-
         MVC   MSG0001B+4(69),BIDATA-2  SET HYPHENS                -EU-
         B     *+L'*+18                                            -EU-
         MVC   MSG0001B+4(2),BIDATA-2   OTHERWISE ORIGINAL TEXT    -EU-
         MVC   MSG0001B+6(65),BITEXT+1    (BIDATA IS EXPANDED BY   -EU-
         MVC   MSG0001B+71(2),APSENDSV      APOST IN ETEXT)        -EU-
         LA    R15,PU0001          SELECT MESSAGE #1               -EU-
         BAS   R10,PUTLPROC        WRITE THE MESSAGE
         LH    R4,BIMAIN           R4 HAS LENGTH OF SEND COMMAND
         C     R4,F133             IF LENGTH NOT GREATER THAN 133
         BNH   BICMD                 GO EXECUTE THE COMMAND
         LA    R15,PU0006          ELSE SELECT MESSAGE #6          -EU-
         BAS   R10,PUTLPROC          WRITE IT
         LA    R15,PU0000            SELECT BLANK LINE             -EU-
         BAS   R10,PUTLPROC          WRITE IT
         B     BIRET04               GO TO HIGH RETURN
BICMD    CLI   AUTH,LVL10          OPERATOR LEVEL ?                -EU-
         BL    BILIST                NO, JUST LIST                 -EU-
         LH    R1,KEYC             IF CLEAR/... KEYWORD            -EU-
         LTR   R1,R1                 IS NOT USED                   -EU-
         BNZ   BITCMD                  YES, GO TO SEE WHAT         -EU-
BILIST   MVC   BITEXTWK(8),ETTEXTWK     SAVE THE TEXT WORK AREA    -EU-
         XC    BIRC,BIRC           SET ZERO RETURN CODE            -EU-
         B     BIEND               GO TO ENDING                    -EU-
BITCMD   BCT   R1,*+L'*+4          IS IT CLEAR REQUEST ?           -EU-
         B     BISCMD                YES, GO TO SEND COMMAND       -EU-
         BCT   R1,BILIST           GO TO JUST LIST IF NOT NOCLEAR  -EU-
         L     R1,APPOS            WHEN NOCLEAR, CHANGE SAVE       -EU-
         MVC   0(5,R1),APLOGON       BY LOGON TO SEND COMMAND      -EU-
         LH    R1,BIMAIN           COMMAND LENGTH IS NOW +1        -EU-
         LA    R1,1(R1)                                            -EU-
         STH   R1,BIMAIN                                           -EU-
BISCMD  ZEROKEY ,                                                  -EU-
         LA    R1,BIMAIN           POINT TO COMMAND
         XR    R0,R0               CLEAR R0 FOR SVC-34
         SVC   34                  EXECUTE CONSOLE SVC
         ST    R15,BIRC            SAVE THE RETURN CODE
        RESETKEY ,                                                 -EU-
         MVC   BITEXTWK(8),ETTEXTWK     SAVE THE TEXT WORK AREA
         STIMER WAIT,BINTVL=BIWAITIM    WAIT FOR A LITTLE
         B     BIEND               GO TO ENDING
BIRET04  LA    R15,X'04'           RETURN CODE TO R15
         ST    R15,RETCDE          SAVE IT
BIEND    L     R10,BIRBALSV        RESTORE RETURN ADDRESS
         BR    R10                 RETURN
         EJECT
**********************************************************************
***                                                                ***
***   Edit the text.                                               ***
***                                                                ***
***   1. Process control cards.                                    ***
***                                                                ***
***     The card type is designated by characters in the beginning ***
***     of the card image. Types can be in lower/UPPER case.       ***
***                                                                ***
***     '*'      = Comment.                                        ***
***     '-com'   = Comment.                                        ***
***     '-dates' = DD/MM/YY dates for message to begin and end -EU-***
***                or set the word 'current' to indicate the   -EU-***
***                message(s) permanent status.                -EU-***
***                (Default is the previous -dates card.)          ***
***     blank    = Message text (65-bytes, beginning with col. 2). ***
***     '-end'   = End of message text (causes line of hyphens).   ***
***                                                                ***
***   2. Expand apostrophes to double apostrophes in text.         ***
***   3. Set RETCDE :                                              ***
***        00 - Convert this to a SEND command.                    ***
***        04 - Don't convert this to a SEND command.              ***
***                                                                ***
**********************************************************************
         SPACE 1
ETEXT    ST    R10,ETRBALSV        SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE       SET ERROR CODE TO ZERO
         MVC   ETTEXTWK(8),BITEXT  GET FIRST 8 CHARACTERS TO WORK
         OC    ETTEXTWK(8),BLANKS  SHIFT TO UPPER CASE
         CLI   ETTEXTWK,C' '       IF IT BEGINS WITH A BLANK
         BE    ETTEXT                GO PROCESS THE TEXT CARD
         CLC   ETTEXTWK(7),ETDATES IF IT IS A '-DATES ' CARD
         BE    ETDATE                GO PROCESS THE DATE CARD
         CLC   ETTEXTWK(5),ETENDE  IF IT IS A '-END' CARD
         BE    ETEOD                 GO PROCESS THE EOD CARD
         CLI   ETTEXTWK,C'*'       IF IT IS A '*' CARD
         BE    ETRET04               OR
         CLC   ETTEXTWK(5),ETCOM   IF IT IS A '-COM ' CARD
         BE    ETRET04               GO TO NO-SEND RETURN
*                                  IF NO MATCH ABOVE
         B     ETRET04               GO TO NO-SEND RETURN
ETTEXT   CP    LOYYDDD(4),BCYYDDD  IF LOW DATE HIGHER THAN CURRENT
         BH    ETRET04               GO TO NO-SEND RETURN
         CP    HIYYDDD(4),BCYYDDD  IF HIGH DATE LESS THAN CURRENT
         BL    ETRET04               GO TO NO-SEND RETURN
         MVC   BIMAIN(85),@BIMAIN  INITIALIZE COMMAND
         MVC   BIDATA(65),BITEXT+1 MOVE TEXT TO OUTPUT AREA
         BAS   R10,APOST           EXPAND TEXT APOSTROPHES
         B     ETEND               ELSE GO TO SEND RETURN
ETEOD    CP    LOYYDDD(4),BCYYDDD  IF LOW DATE HIGHER THAN CURRENT
         BH    ETRET04               GO TO NO-SEND RETURN
         CP    HIYYDDD(4),BCYYDDD  IF HIGH DATE LESS THAN CURRENT
         BL    ETRET04               GO TO NO-SEND RETURN
         MVC   BIMAIN(85),@BIHYPH  INITIALIZE COMMAND
         LA    R1,BIMAIN+81                                        -EU-
         ST    R1,APPOS                                            -EU-
         B     ETEND               GO TO SEND RETURN
ETDATE   LA    R5,BITEXT+50        MAX. RANGE FOR SEARCH DATES     -EU-
         LA    R4,BITEXT+7         SEARCH START OF BEGIN DATE      -EU-
ETDATEBS CLI   0(R4),C' '                                          -EU-
         BNE   ETDATEB                                             -EU-
         LA    R4,1(R4)                                            -EU-
         CLR   R4,R5                                               -EU-
         BNH   ETDATEBS                                            -EU-
ETDATERS LA    R15,PU0000          SELECT BLANK LINE               -EU-
         BAS   R10,PUTLPROC          WRITE IT                      -EU-
         LA    R15,PU0008          SELECT MESSAGE #8               -EU-
         BAS   R10,PUTLPROC          WRITE IT                      -EU-
         LA    R15,PU0000          SELECT BLANK LINE               -EU-
         BAS   R10,PUTLPROC          WRITE IT                      -EU-
         B     ETRET04             BRANCH TO NO-SEND RETURN        -EU-
ETDATEB  MVC   DATEWORK(8),0(R4)   MOVE BEGIN DATE TO WORK AREA    -EU-
         OC    DATEWORK(8),BLANKS  UPPERCASE                       -EU-
         CLC   DATEWORK(8),DACURR  CURRENT DATE REQUESTED ?        -EU-
         BNE   ETDATEBP            NO                              -EU-
         MVC   LOYYDDD(4),BCYYDDD  MOVE THE CURRENT DATE           -EU-
         MVC   HIYYDDD(4),BCYYDDD                                  -EU-
         B     ETRET04             BRANCH TO NO-SEND RETURN        -EU-
ETDATEBP MVC   DATE(8),0(R4)       MOVE BEGIN DATE TO WORK AREA    -EU-
         MVC   DADD(2),0(R4)         (BUT WE USE THE               -EU-
         MVC   DAMM(2),3(R4)           EUROPEAN FORMAT - DD/MM/YY) -EU-
         BAS   R10,DATECONV        CONVERT TO 00YYDDDC
         CLC   RETCDE(4),F0        IF BAD RETURN
         BNE   ETDATERR              GO TO ERROR ROUTINE
         MVC   LOYYDDD(4),DATEPACK ELSE MOVE TO STORAGE
         LA    R4,8(R4)            LOOK TO FIND ENDING DATE        -EU-
         CLR   R4,R5                 (AT LEAST ONE BLANK           -EU-
         BH    ETDATERS                MUST BE PRESENT AS          -EU-
         CLI   0(R4),C' '                SEPARATOR)                -EU-
         BNE   ETDATERS                                            -EU-
         LA    R4,1(R4)                                            -EU-
ETDATEES CLI   0(R4),C' '                                          -EU-
         BNE   ETDATEE                                             -EU-
         LA    R4,1(R4)                                            -EU-
         CLR   R4,R5                                               -EU-
         BNH   ETDATEES                                            -EU-
         B     ETDATERS                                            -EU-
ETDATEE  MVC   DATE(8),0(R4)       MOVE END DATE TO WORK AREA      -EU-
         MVC   DADD(2),0(R4)         (BUT WE USE THE               -EU-
         MVC   DAMM(2),3(R4)           EUROPEAN FORMAT - DD/MM/YY) -EU-
         BAS   R10,DATECONV        CONVERT TO 00YYDDDC
         CLC   RETCDE(4),F0        IF BAD RETURN
         BNE   ETDATERR              GO TO ERROR ROUTINE
         MVC   HIYYDDD(4),DATEPACK ELSE MOVE TO STORAGE
         B     ETRET04             BRANCH TO NO-SEND RETURN
ETDATERR MVC   MSG0005B(13),WRK0005B    INITIALIZE MESSAGE SEGMENT
         MVC   MSG0005B+4(8),DATE  MOVE BAD DATE INTO SEGMENT
         MVC   MSG0005B+4(2),DADD    (BUT WE USE THE               -EU-
         MVC   MSG0005B+7(2),DAMM      EUROPEAN FORMAT - DD/MM/YY) -EU-
         MVC   LOYYDDD(4),BCYYDDD  MOVE CURRENT DATE TO START DATE
         MVC   HIYYDDD(4),BCYYDDD    AND TO END DATE
         LA    R15,PU0000          SELECT BLANK LINE               -EU-
         BAS   R10,PUTLPROC          WRITE IT
         LA    R15,PU0005          SELECT MESSAGE #5               -EU-
         BAS   R10,PUTLPROC          WRITE IT
         LA    R15,PU0000          SELECT BLANK LINE               -EU-
         BAS   R10,PUTLPROC          WRITE IT
ETRET04  LA    R15,X'04'           RETURN CODE TO R15
         ST    R15,RETCDE          SAVE IT
ETEND    L     R10,ETRBALSV        RESTORE RETURN ADDRESS
         BR    R10                 RETURN
         EJECT
**********************************************************************
***                                                                ***
***   Expand single apostrophes to double apostrophes.             ***
***                                                                ***
**********************************************************************
         SPACE 1
APOST    ST    R10,APRBALSV        SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE       SET ERROR CODE TO ZERO
         LA    R3,BIDATA-1         R3 POINTS TO TEXT-1
         LA    R4,65(0,R3)         R4 POINTS TO LAST TEXT CHAR
         LA    R1,65(0,0)          R1 IS COUNTER FOR OUTER LOOP
APLOOP1  LA    R3,1(0,R3)          R3 POINTS TO NEXT CHAR
         CLI   0(R3),X'7D'         IF NOT APOSTROPHE
         BNE   APLOOP1X              GO TO OUTER LOOP END
         LR    R2,R1               R2 IS INNER LOOP COUNTER
         LR    R5,R4               R5 IS INNER LOOP POINTER
         LA    R4,1(0,R4)          R4 POINTS TO NEW LAST CHAR
APLOOP2  MVC   1(1,R5),0(R5)       COPY FROM CHAR TO CHAR+1
         BCTR  R5,0                DECR POINTER BY ONE
         BCT   R2,APLOOP2          LOOP BACK UP TO INNER LOOP
         LA    R3,1(0,R3)          MOVE POINTER TO 'NEW' APOST
APLOOP1X BCT   R1,APLOOP1          LOOP BACK UP TO OUTER LOOP
         LA    R1,5(R4)                                            -EU-
         ST    R1,APPOS                                            -EU-
         MVC   1(8,R4),APSENDSV    MOVE B-,'SAVE TO SEND COMMAND
         LA    R4,9(0,R4)          R4 POINTS JUST BEYOND LAST CHAR
         LA    R1,BIMAIN           R1 POINTS TO SEND COMMAND LEN
         SR    R4,R1               R4 IS LENGTH OF COMMAND
         STH   R4,BIMAIN           SAVE LEN IN COMMAND BUFFER
         L     R10,APRBALSV        RESTORE RETURN ADDRESS
         BR    R10                 RETURN
         EJECT
**********************************************************************
***                                                                ***
***   Convert MM/DD/YY (ZD) to 00YYDDDC (packed)                   ***
***                                                                ***
**********************************************************************
         SPACE 1
DATECONV ST    R10,DARBALSV        SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE       SET ERROR CODE TO ZERO
         XC    DATEPACK,DATEPACK   ZERO THE RETURN DATE
         MVC   DATEWORK(8),DATE    COPY DATE TO TEST AREA
         NC    DATEWORK(8),DATSTPAT     'AND' IT WITH TEST PATTERN
         CLC   DATEWORK(8),DARESPAT     IF RESULT IS NOT '00/00/00'
         BNE   DARET04                    GO TO HIGH RETURN
         NI    DASWITCH,X'7F'      TURN OFF LEAP YEAR SWITCH
         PACK  DADOUBLE(8),DAYY(2) CONVERT TO 00000000 00000YYF
         MVC   DAYYPACK(4),DATEPACK     SAVE IT
         CVB   R3,DADOUBLE         R3 HAS BINARY YEAR
         XR    R2,R2               CLEAR R2 FOR DIVIDE
         D     R2,F4               CHECK FOR LEAP YEAR
         LTR   R2,R2               IF LEAP YEAR
         BNZ   *+L'*+4               THEN
         OI    DASWITCH,X'80'          TURN ON LEAP YEAR SWITCH
         CLC   DAMM(2),ZD12        IF MM GREATER THAN 12
         BNH   *+L'*+10              THEN
         XC    DATEPACK,DATEPACK       ZERO THE RETURN DATE
         B     DARET04                   AND GO TO HIGH RETURN
         PACK  DADOUBLE(8),DAMM(2) CONVERT TO 00000000 00000MMF
         CVB   R3,DADOUBLE         R3 HAS BINARY MONTH 1 TO 12
         BCTR  R3,0                R3 IS BINARY 0 TO 11
         SLL   R3,1                R3 IS OFFSET INTO TABLE
         TM    DASWITCH,X'80'      IF LEAP YEAR
         BNO   *+L'*+8               THEN
         LA    R2,DALEAPTB             R2 HAS ADDR OF LEAP TABLE
         B     *+L'*+4             ELSE
         LA    R2,DAREGTB            R2 HAS ADDR OF REGULAR TBL
         LH    R2,0(R3,R2)         R2 HAS DDD FOR DAY 0 OF MONTH
         CLC   DADD(2),ZD31        IF DD GREATER THAN 31
         BNH   *+L'*+10              THEN
         XC    DATEPACK,DATEPACK       ZERO THE RETURN DATE
         B     DARET04                   AND GO TO HIGH RETURN
         PACK  DADOUBLE(8),DADD(2) CONVERT TO 00000000 00000DDF
         CVB   R3,DADOUBLE         R3 HAS BINARY DAY OF MONTH
         AR    R3,R2               R3 HAS BINARY DAY OF YEAR
         CVD   R3,DADOUBLE         DADOUBLE HAS 00000000 0000DDDC
         L     R3,DATEPACK         R3 HAS 0000DDDC
         L     R2,DAYYPACK         R2 HAS 00000YYF
         SRL   R2,4                R2 HAS 000000YY
         SLL   R2,16               R2 HAS 00YY0000
         OR    R2,R3               R2 HAS 00YYDDDC
         ST    R2,DATEPACK         SAVE IT
         B     DAEND
DARET04  LA    R15,X'04'           RETURN CODE TO R15
         ST    R15,RETCDE          SAVE IT
DAEND    L     R10,DARBALSV        RESTORE RETURN ADDRESS
         BR    R10                 RETURN
         EJECT
**********************************************************************
***                                                                ***
***   WRITE MESSAGE WITH PUTLINE                                   ***
***                                                                ***
***    1.  IOPL was initialized at beginning of program, IOPLIOPB  ***
***        field will be filled in by PUTLINE execute macro.       ***
***    2.  PTPB will be filled in by PUTLINE execute macro.        ***
***    3.  OLD will be filled in by this procedure.                ***
***                                                                ***
***    R15 contains branch address for processing appropriate  -EU-***
***    message.                                                -EU-***
**********************************************************************
         SPACE 1
PUTLPROC ST    R10,PURBALSV        SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE       SET ERROR CODE TO ZERO
         BR    R15                 GO TO APPROPRIATE PROCESSING
         SPACE 1
PU0000   LA    R15,1(0,0)               SEGMENTS = 1
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0000              ADDR OF BLANK LINE
         ST    R15,MYOLD+4                INTO O.L.D.
         B     PUOUTPUT                 GO WRITE IT
PU0001   LA    R15,2(0,0)               SEGMENTS = 2
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0001A             ADDR OF BUL0001I, SEG #1
         ST    R15,MYOLD+4                INTO O.L.D.
         LA    R15,MSG0001B             ADDR OF BUL0001I, SEG #2
         ST    R15,MYOLD+8                INTO OLD
         B     PUOUTPUT                 GO WRITE IT
PU0002   LA    R15,1(0,0)               SEGMENTS = 1
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0002              ADDR OF BUL0002I
         ST    R15,MYOLD+4                INTO O.L.D.
         B     PUOUTPUT                 GO WRITE IT
PU0003   LA    R15,1(0,0)               SEGMENTS = 1
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0003              ADDR OF BUL0003E
         ST    R15,MYOLD+4                INTO O.L.D.
         B     PUOUTPUT                 GO WRITE IT
PU0004   LA    R15,1(0,0)               SEGMENTS = 1
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0004              ADDR OF BUL0004E
         ST    R15,MYOLD+4                INTO O.L.D.
         B     PUOUTPUT                 GO WRITE IT
PU0005   LA    R15,2(0,0)               SEGMENTS = 2
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0005A             ADDR OF BUL0005C, SEG #1
         ST    R15,MYOLD+4                INTO O.L.D.
         LA    R15,MSG0005B             ADDR OF BUL0005C, SEG #2
         ST    R15,MYOLD+8                INTO OLD
         B     PUOUTPUT                 GO WRITE IT
PU0006   LA    R15,1(0,0)               SEGMENTS = 1
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0006              ADDR OF BUL0006E
         ST    R15,MYOLD+4                INTO O.L.D.
         B     PUOUTPUT                 GO WRITE IT
PU0007   LA    R15,1(0,0)               SEGMENTS = 1
         ST    R15,MYOLD                  INTO O.L.D.
         LA    R15,MSG0007              ADDR OF BUL0007C
         ST    R15,MYOLD+4                INTO O.L.D.
         B     PUOUTPUT                 GO WRITE IT
PU0008   LA    R15,1(0,0)               SEGMENTS = 1               -EU-
         ST    R15,MYOLD                  INTO O.L.D.              -EU-
         LA    R15,MSG0008              ADDR OF BUL0008C           -EU-
         ST    R15,MYOLD+4                INTO O.L.D.              -EU-
         SPACE 1                                                   -EU-
PUOUTPUT XC    MYPTPB(MYPTPBLN),MYPTPB  ZERO THE PUTLINE PARM BLOCK
         XC    MYIOECB,MYIOECB     ZERO THE ECB
         PUTLINE PARM=MYPTPB,OUTPUT=MYOLD,MF=(E,MYIOPL)
         LTR   R15,R15             IF RETURN IS ZERO
         BZ    PUEND                 GO TO END
         LA    R15,X'04'(0,0)      SET ERROR VALUE
         ST    R15,RETCDE            INTO INTERNAL RETURN CODE
PUEND    L     R10,PURBALSV        RESTORE RETURN ADDRESS
         BR    R10                 RETURN
         EJECT
**********************************************************************
***                                                                ***
***   DATA CONSTANTS                                               ***
***                                                                ***
**********************************************************************
         SPACE 1
         DS    0D
VPARMPCL DC    V(PARMPCL)               ADDR OF PARM CONTROL LIST
VESTXIT  DC    V(ESTXIT)                ADDR OF ABEND EXIT
F0       DC    F'0'                     CONSTANT
F4       DC    F'4'                     CONSTANT
F133     DC    F'133'                   CONSTANT
H2       DC    H'2'                     CONSTANT
H9       DC    H'9'                     CONSTANT
H73      DC    H'73'                    CONSTANT
H80      DC    H'80'                    CONSTANT
P0       DC    PL4'+0'                  CONSTANT
P99365   DC    PL4'+99365'              CONSTANT
BLANKS   DC    CL80' '                  BLANK LINE
DSDDNAM  DC    CL8'SYS99999'            DUMMY DDNAME
ZDZEROES DC    XL8'F0F0F0F0F0F0F0F0'    ZONE DECIMAL ZEROES
ZD12     DC    CL2'12'                  CONSTANT
ZD31     DC    CL2'31'                  CONSTANT
         SPACE 1
*                              0....+....1....+....2....+....3....+....
*              4....+....5....+....6....+
MSG0000  DC    H'06',H'00',CL02'  '
MSG0001A DC    H'78',H'00',CL74'BUL0001I '
MSG0002  DC    H'38',H'00',CL34'BUL0002I CLEAR operation complete.'
MSG0003  DC    H'67',H'00',CL63'BUL0003E OPERATOR authority is requiredx
                for CLEAR or BROADCAST.'
MSG0004  DC    H'64',H'00',CL60'BUL0004E Input file must have 80-byte, X
               fixed length records.'
MSG0005A DC    H'60',H'00',CL56'BUL0005C Unable to process date :  CurrX
               ent date assumed.'
WRK0005B DC    H'13',H'34',CL09'        .'
MSG0006  DC    H'58',H'00',CL54'BUL0006E Line ignored. Too many apostroX
               phes (46, max).'
MSG0007  DC    H'62',H'00',CL58'BUL0007C CLEAR function ignored. QuantiX
               ty was less than 1.'
MSG0008  DC    H'62',H'00',CL58'BUL0008C Invalid "-dates " : format errX
               or or missing date.'                                -EU-
         SPACE 1
BIWAITIM DC    F'&WAIT'                 WAIT TIME (100THS OF SECONDS)
         SPACE 1
*                0-   --       -+--- -1----+----2----+----3----+----4--
*              --+----5----+----6----+----7----+--- -8----+
@BIMAIN  DC    H'85',H'0',CL81'SEND ''- ....+....1....+....2....+....3.X
               ...+....4....+....5....+....6....+ -'',SAVE '
@BIHYPH  DC    H'85',H'0',CL81'SEND ''---------------------------------X
               ------------------------------------'',SAVE '
@CLCMD   DC    H'18',H'0',CL14'SEND NN,DELETE'
         SPACE 1
@M0      GTEDADAT DDNAM=X,UNALC=YES
@M1      GTEDADAT DSNAM=X,MEMBR=X,STATS=SHR,RTDDN=X
         SPACE 1
ETDATES  DC    CL7'-DATES '             LITERAL
ETCOM    DC    CL5'-COM '               LITERAL
ETENDE   DC    CL5'-END '               LITERAL
DACURR   DC    CL8'CURRENT'             LITERAL                    -EU-
APLOGON  DC    CL5'LOGON'               SUFFIX TO SEND TEXT (NOCL) -EU-
APSENDSV DC    CL8' -'',SAVE'           SUFFIX OF SEND TEXT
         SPACE 1
DAREGTB  DC    H'000,031,059,090,120,151,181,212,243,273,304,334'
DALEAPTB DC    H'000,031,060,091,121,152,182,213,244,274,305,335'
DATSTPAT DC    XL8'F0F0FFF0F0FFF0F0'    'AND' TEST FOR 'NN/NN/NN'
DARESPAT DC    CL8'00/00/00'            RESULT OF GOOD TEST
         SPACE 1
ESTAED   ESTAE MF=L
@OPENL   OPEN  (),MF=L                  OPEN PARM LIST
@CLOSL   CLOSE (),MF=L                  CLOSE PARM LIST
         SPACE 1
         PRINT NOGEN                                               -EU-
@BCDCB   DCB   DSORG=PS,MACRF=(GM),EODAD=BIEOF,DDNAME=XXXXXXXX
         PRINT GEN                                                 -EU-
         SPACE 1                                                   -EU-
MOD@IX   DC    AL3(MDL@IX)                                         -EU-
         SPACE 1
         DS    0D
         EJECT
**********************************************************************
***                                                                ***
***   COMMAND OPERANDS                                             ***
***                                                                ***
***    See syntax description at beginning of program.             ***
***                                                                ***
**********************************************************************
         SPACE 1
         PRINT NOGEN                                               -EU-
PARMPCL  IKJPARM  DSECT=PARMPDL
         SPACE 1
KEYC     IKJKEYWD DEFAULT='LIST'                                   -EU-
         IKJNAME  'CLEAR',ALIAS=('CL'),SUBFLD=SUBC
         IKJNAME  'NOCLEAR',ALIAS=('NOCL')
         IKJNAME  'LIST',ALIAS=('TEST')                            -EU-
         SPACE 1
KEYBC    IKJKEYWD DEFAULT='NOBROADCAST'
         IKJNAME  'BROADCAST',ALIAS=('BC'),SUBFLD=SUBBCDSN
         IKJNAME  'NOBROADCAST',ALIAS=('NOBC')
         SPACE 1
SUBBCDSN IKJSUBF
SBCDSN   IKJPOSIT DSNAME,                                              X
               USID,                                                   X
               DEFAULT='''&SYSBC''',                                   X
               HELP=('1-44 CHARACTER DSNAME AND 1-8 CHARACTER MEMBER')
         SPACE 1
SUBC     IKJSUBF
SC       IKJIDENT 'NUMBER OF MESSAGES',                                X
               MAXLNTH=2,                                              X
               FIRST=NUMERIC,OTHER=NUMERIC,                            X
               INTEG,                                                  X
               DEFAULT='30',                                           X
               HELP=('NUMBER OF MESSAGES TO DELETE')
         SPACE 1
         IKJENDP
         PRINT GEN                                                 -EU-
         EJECT
**********************************************************************
***                                                                ***
***   DATA AREA IN SUBPOOL 0                                       ***
***                                                                ***
**********************************************************************
         SPACE 1
WORKD    DSECT                          AREA FOR VARIABLES
         DS    18F                      REGISTER SAVE AREA
CPPLPTR  DS    F                        INITIAL VALUE OF R1 (CPPLADDR)
RETCDE   DS    F                        INTERNAL RETURN CODE
COMPCODE DS    F                        PROGRAM COMPLETION CODE
         SPACE 1
ABRBALSV DS    F                        RETURN ADDRESS SAVE AREA
PPRBALSV DS    F                        RETURN ADDRESS SAVE AREA
PARBALSV DS    F                        RETURN ADDRESS SAVE AREA
UNRBALSV DS    F                        RETURN ADDRESS SAVE AREA
DSRBALSV DS    F                        RETURN ADDRESS SAVE AREA
BCRBALSV DS    F                        RETURN ADDRESS SAVE AREA
CLRBALSV DS    F                        RETURN ADDRESS SAVE AREA
ETRBALSV DS    F                        RETURN ADDRESS SAVE AREA
APRBALSV DS    F                        RETURN ADDRESS SAVE AREA
DARBALSV DS    F                        RETURN ADDRESS SAVE AREA
BIRBALSV DS    F                        RETURN ADDRESS SAVE AREA
PURBALSV DS    F                        RETURN ADDRESS SAVE AREA
         SPACE 1
MYPPL    DS    7F                       PARSE PARAMETER LIST
MYECB    DS    F                        ECB FOR PARSE
MYANS    DS    F                        POINTER TO THE PDL
         SPACE 1
MYUWA    DS    0D                       WORK/SAVE AREA FOR PARSE EXITS
MYUWASV  DS    18F                      SAVE AREA FOR EXITS
MYUWAWRK DS    14F                      WORK AREA FOR EXITS
         SPACE 1
ETTEXTWK DS    CL8                      WORK AREA FOR TEXT
BCYYDDD  DS    PL4                      CURRENT 00YYDDDC
LOYYDDD  DS    PL4                      LOW 00YYDDDC
HIYYDDD  DS    PL4                      HIGH 00YYDDDC
         SPACE 1
DASWITCH DS    B'00000000'              SWITCHES
*                1.......                 X'80' - IT IS A LEAPYEAR
         SPACE 1
DADOUBLE DS    0D
         DS    PL4                      00YYDDDF
DATEPACK DS    PL4                      WORK AREA FOR PACKED VALUES
DAYYPACK DS    PL4                      PACKED YEAR
         SPACE 1
DATEWORK DS    CL8
         SPACE 1
DATE     DS    0CL8
DAMM     DS    CL2
         DS    CL1
DADD     DS    CL2
         DS    CL1
DAYY     DS    CL2
         SPACE 1
MY0      GTEDADAT MAP=ONLY,DDNAM=X,UNALC=YES
MY1      GTEDADAT MAP=ONLY,DSNAM=X,MEMBR=X,STATS=SHR,RTDDN=X
         SPACE 1
MSG0001B DS    H,H,CL69                 SEGMENT TWO OF MESSAGE
MSG0005B DS    H,H,CL9                  SEGMENT TWO OF MESSAGE
         SPACE 1
MYIOPL   DS    4F                       IO PARM LIST
MYIOECB  DS    F                        ECB FOR I/O ROUTINES
MYPTPB   PUTLINE MF=L                   PUTLINE PARM BLOCK (PTPB)
MYPTPBLN EQU   *-MYPTPB                   LENGTH OF PTPB
         SPACE 1
*                                       OUTPUT LINE DESCRIPTOR, 1 LEVEL
MYOLD    DS    F                          NUMBER OF SEGMENTS
         DS    5A                         ADDRS OF UP TO FIVE SEGMENTS
         SPACE 1
BIMAIN   DC    H'85',H'0'               LENGTH, OFFSET
         DC    CL8'SEND ''- '           SEND '-B
BIDATA   DS    CL65                     TEXT
         DC    CL9' -'',SAVE'           B-',SAVE OR B-',LOGON      -EU-
         DS    CL65                     EXPANSION AREA DUE TO APOST'S
         SPACE 1
BIRC     DS    F
BISWITCH DS    B '00000000'             SWITCH AREA
*                 1.......                X'80' - EOF
*                 .1111111                      - NOT USED
BITEXTWK DS    CL8                      SAVE WORK AREA FOR TEXT
         SPACE 1
CLCMD    DS    2H,CL14                  SEND DELETE COMMAND
CLZONE   DS    CL3                      MESSAGE NUMBER, ZD
CLDOUBLE DS    D                        MESSAGE NUMBER, PACKED
         SPACE 1
ESTAEL   ESTAE MF=L
ESTAELLN EQU   *-ESTAEL
ESTUPL   DS    0D                       ESTAE USER PARM LIST
         DS    A                          ADDRESS OF RETURN POINT
         DS    A                          ADDRESS OF USER WORK AREA
ESTUWK   DS    0D                       ESTAE USER WORK AREA
         DS    18F                        REGISTER SAVE AREA
         DS    D                          DOUBLE WORD FOR UNPACK
         DS    F                          BAS REGISTER SAVE AREA
         WTO   '.+....1....+....2....+....3....+....4....+....5',      X
               ROUTCDE=(11),DESC=(7),MF=L
         SPACE 1
DSDDNAM1 DS    CL8                      SAVE AREA FOR RETURNED DDNAME
DSDDNAM2 DS    CL8                      SAVE AREA FOR RETURNED DDNAME
         SPACE 1
OPENL    OPEN  (),MF=L                  OPEN PARM LIST
OPENLLEN EQU   *-OPENL
CLOSL    CLOSE (),MF=L                  CLOSE PARM LIST
CLOSLLEN EQU   *-CLOSL
         SPACE 1
         PRINT NOGEN                                               -EU-
BCDCB    DCB   DSORG=PS,MACRF=(GM),EODAD=BIEOF,DDNAME=XXXXXXXX
         PRINT GEN                                                 -EU-
BCDCBL   EQU   *-BCDCB
         SPACE 1
BITEXT   DS    CL255                    BROADCAST TEXT WORK AREA
         SPACE 1
AUTH     DC    XL1'0'                                              -EU-
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(*-*)            -EU-
REQAUTL  EQU   *-AUTH                                              -EU-
APPOS    DS    F                                                   -EU-
         SPACE 1                                                   -EU-
WORKDEND DS    0D
WORKDLEN EQU   *-WORKD                  TOTAL LENGTH OF WORK AREA
         EJECT
**********************************************************************
***                                                                ***
***   MAPPING DSECTS                                               ***
***                                                                ***
**********************************************************************
         SPACE 1
         PRINT NOGEN
         SPACE 1
         CVT   DSECT=YES,LIST=YES     , CVTMAP FOR IKJPARS
         IKJCPPL                        COMMAND PROCESSOR PARM LIST
         IKJPPL                         PARSE PARM LIST
         IKJIOPL                        I/O PARM LIST
         SPACE 1
         IKJEFFDF DFDSECT=YES,DFDSEC2=YES  MAPS DAIRFAIL CONTROL BLOCKS
         IEFZB4D0                       MAPS SVC99 CONTROL BLOCKS
         IEFZB4D2 ,                     SVC99 KEYS TABLE           -EU-
         SPACE 1
         IHASDWA                      , SDWA FOR ESTAE EXIT
         SPACE 1
         DCBD  DSORG=PS,DEVD=DA         MAPS DCB
         SPACE 1
         PRINT GEN
         EJECT
**********************************************************************
***                                                                ***
***   ESTAE (ABEND) EXIT                                           ***
***                                                                ***
***   This exit will receive control from RTM during ABEND         ***
***   processing.  Data areas available to this exit will be       ***
***   the System Diagnostic Work Area (SDWA) provided by the       ***
***   RTM, and the user parm list and the areas it points to       ***
***   which are provided by the main routine.                      ***
***                                                                ***
***   This exit will format an ABEND message, and write it         ***
***   with a WTO.  It will then return to RTM.                     ***
***                                                                ***
***   The register save area used by this exit has already been    ***
***   obtained by a GETMAIN by the main program, prior to the      ***
***   execution of the ESTAE macro.                                ***
***                                                                ***
***   This exit is entered by RTM with standard MVS linkage        ***
***   conventions.  Upon entry, the following relationships        ***
***   exist:                                                       ***
***                                                                ***
*** R1              SDWA (IHASDWA)   ESTUSRPL                      ***
*** +-----------+   +------------+   +-----------+                 ***
*** |           |==>| SDWAPARM   |==>| ESURETA   |==> Return point ***
*** +-----------+   +------------+   +-----------+                 ***
***                 | SDWAABCC   |   | ESUWRKA   |==> ESUWKD       ***
***                 +------------+   +-----------+   +-----------+ ***
***                 |            |                   | Save area | ***
***                ---          ---                 ---         ---***
***                ---          ---                 ---         ---***
***                 |            |                   |           | ***
***                 +------------+                   +-----------+ ***
***                 | SDWAGR15   |                   | Other work| ***
***                 +------------+                   |   areas   | ***
***                 |            |                   +-----------+ ***
***                ---          ---                                ***
***                ---          ---                                ***
***                 |            |                                 ***
***                 +------------+                                 ***
***                                                                ***
**********************************************************************
         EJECT
ESTXIT   CSECT
         STM   R14,R12,12(R13)     SAVE REGS INTO CALLER'S S.A.
         LR    R12,R15             R12 HAS BASE ADDRESS
         USING ESTXIT,R12            ADDRESSABILITY
         LR    R11,R1              R11 POINTS TO SDWA
         USING SDWA,R11              ADDRESSABILITY
         L     R10,SDWAPARM        R10 POINTS TO USER PARM LIST
         USING ESTUSRPL,R10          ADDRESSABILITY
         L     R15,ESUSRWKA        R15 POINTS TO USER WORK AREA
         ST    R13,4(0,R15)        CALLER'S S.A. ADDR TO OWN S.A.
         ST    R15,8(0,R13)        OWN S.A. ADDR TO CALLER'S S.A.
         LR    R13,R15             R13 POINTS TO OWN S.A.
         USING ESUWKD,R13            ADDRESSABILITY
         BAS   R9,ESMSG            FORMAT AND WRITE A MESSAGE
         L     R4,ESURETA          R4 CONTAINS RETURN ADDRESS
         SETRP WKAREA=(R11),RC=4,RETADDR=(R4),RETREGS=YES,FRESDWA=YES
         XR    R15,R15             CLEAR RETURN CODE
         L     R13,4(0,R13)        RESTORE R13, PNT TO CALLER'S SA
         LM    R0,R12,20(R13)      RESTORE R0-R12 FROM CALLER'S SA
         L     R14,12(0,R13)       RESTORE R14 FROM CALLER'S S.A.
         BR    R14                 RETURN
         EJECT
**********************************************************************
***                                                                ***
***   WRITE ABEND MESSAGE                                          ***
***                                                                ***
**********************************************************************
         SPACE 1
ESMSG    ST    R9,ESUBALS1         SAVE BAS REGISTER
         MVC   WTOL(WTOLLEN),WTOD  INITIALIZE WTO PARM LIST
         L     R3,SDWAABCC         R3 HAS ABEND CODES
         SLL   R3,8                LEFT JUSTIFY SYSTEM CODE
         LA    R4,WTOL+26          R4 POINTS INTO MESSAGE
         LA    R5,3                R5 COUNTER SET TO 3
         BAS   R9,ESHEX            CONVERT TO DISPLAYABLE HEX
         L     R3,SDWAGR15         R3 HAS ABEND R15 CONTENTS
         LA    R4,WTOL+42          R4 POINTS INTO MESSAGE
         LA    R5,8                R5 COUNTER SET TO 8
         BAS   R9,ESHEX            CONVERT TO DISPLAYABLE HEX
         L     R3,SDWAABCC         R3 HAS ABEND CODES
         N     R3,ESXFFF           R3 HAS ONLY USER CODE
         CVD   R3,ESUWKDBL         CONVERT TO DECIMAL
         OI    ESUWKDBL+7,X'0F'    KILL THE SIGN
         UNPK  WTOL+33(4),ESUWKDBL(8)   UNPACK INTO MESSAGE
         WTO   MF=(E,WTOL)         WRITE THE MESSAGE
         L     R9,ESUBALS1         RESTORE THE BAS REGISTER
         BR    R9                  RETURN
         SPACE 1
ESHEX    XR    R2,R2               CLEAR EVEN REG OF EVEN/ODD
         SLDL  R2,4                R2 RECEIVES ONE HEX DIGIT
         LA    R4,1(0,R4)          R4 POINTS TO NEXT MESSAGE CHAR
         LA    R2,ESHEXTBL(R2)     R2 POINTS TO CHAR IN TABLE
         MVC   0(1,R4),0(R2)       MOVE CHAR FROM TABLE TO MESSAGE
         BCT   R5,ESHEX            LOOP BACK UP
         BR    R9                  RETURN
         EJECT
**********************************************************************
***                                                                ***
***   DATA AREAS AND DSECTS FOR ESTAE EXIT                         ***
***                                                                ***
**********************************************************************
         SPACE 1
ESHEXTBL DC    CL16'0123456789ABCDEF'   TRANSLATE TABLE
         DS    0F                       ALIGN
ESXFFF   DC    X'00000FFF'              MASK TO CLEAR BITS 0-19
WTOD     WTO   '*** ABEND ***  Codes: SXXX, U9999, R15=XXXXXXXX',      X
               ROUTCDE=(11),DESC=(7),MF=L
         SPACE 1
ESTUSRPL DSECT                          USER PARM LIST
ESURETA  DS    A                          RETURN ADDRESS FOR RTM
ESUSRWKA DS    A                          WORK AREA ADDRESS FOR ME
         SPACE 1
ESUWKD   DSECT                          USER WORK AREA
ESUWKSAV DS    18F                        REGISTER SAVE AREA
ESUWKDBL DS    D                          DOUBLE WORD FOR UNPACK
ESUBALS1 DS    F                          BAS REGISTER SAVE AREA
WTOL     WTO   '.+....1....+....2....+....3....+....4....+....5',      X
               ROUTCDE=(11),DESC=(7),MF=L
WTOLLEN  EQU   *-WTOL                     LENGTH OF WTO PARM LIST
         SPACE 1
         END
