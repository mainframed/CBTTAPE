AU30     TITLE '-- TSO-CP AUT30 --'
AUT30    START 0
         SPACE 1
**********************************************************************
*        THIS CP PERMITS TO UPDATE IN A TRANSACTIONNAL               *
*        WAY THE ACCOUNTING FILE.                                    *
*                                             M.SACRE  01 OCTOBRE 84 *
*                                          P.A.MOINIL  03 OCTOBRE 89 *
* REMARK : THE SCREEN DISPLAY IS ORGANIZED ON 25 LINES OF DATA FOR   *
*          25 NUMBERS OF PROGRAMMERS NO. FIELDS.                     *
NPL      EQU   25 = AUTNPNF IN AUTDSECT                              *
*          IF AUTNPNF IS REDEFINED HIGHER, TAKE CARE THAT THEN YOU   *
*          MUST CHANGE THE SCREEN DISPLAY LOGIC AND ORGANIZATION.    *
**********************************************************************
         SPACE 1
*---------------------------------------------------------------------*
*        AUTHORITY LEVELS DEFINITIONS.                                *
*---------------------------------------------------------------------*
         SPACE 1
LV10     EQU   10        REDUCED SERVICES LEVEL
LV5      EQU   5         DISPLAY SERVICES LEVEL
         SPACE 1
         PRINT NOGEN
        $DEFREG
        $XENT  BASE=(R10,R11,R12)
         SPACE 1
*------- CONTROL OF CALLER (USERID)
         L     R15,=V(AUT98T)
         MVI   1(R15),LV5              SET REQUESTED AUTHORITY LEVEL
         BALR  R0,0                    SAY HARDCOPY
         L     R15,=V(AUT98)
         BALR  R14,R15
         LTR   R15,R15
         BNZ   LEAVE                   BRANCH TO END OF CP
         SPACE 1
*--------------------------------------------------------------------*
*        USERID AUTORISE A UTILISER CETTE CP.                        *
*--------------------------------------------------------------------*
         SPACE 1
*------- ALLOCATION DYNAMIQUE DU FICHIER D'ACCOUNTING
*        QUI SE TROUVE SOUS LA DD-NAME SYSACC
         L     R1,=V(AUT98A)
         MVC   DYNDSN(44),0(R1)        SET ACCOUNT FILE DS-NAME
        $DYNALL DYNLIST                DYNAMIC ALLOC OF SYSACC
         LTR   R15,R15                 TEST RETURN CODE
         BNZ   ERALL                   B ON ERROR ALLOC IF NOT ZERO
        CALL   ZOPEN,(RETCD),VL        OPEN FILE UNDER SYSACC DD
         LTR   R15,R15                 IS RETURN CODE 0?
         BNZ   ERRZOC                  IF NOT, ERROR AT OPEN
         L     R1,=V(AUT99I)
         MVC   0(4,R1),=V(AUT95)       SET PF-2 KEY ACTIVE
         EJECT
*--------------------------------------------------------------------*
*        FICHIER D'ACCOUNTING BIEN OUVERT.                           *
*--------------------------------------------------------------------*
         SPACE 1
DEMINIT  LA    R1,PECRIN               INITIAL DEMAND
         L     R15,=V(AUT99)
         BALR  R14,R15
         NOP   *+L'*+16                SWITCH ADVICE
         OI    *-3,X'F0'               THIS WAY DONE JUST ONCE
         MVC   OVRR(LPTIC),PTIC        CUT OUT THE ADVICE
         MVC   PECRIN,=A(LECRIN)
         MVI   ECRINM,C' '
         MVC   ECRINM+1(L'ECRINM-1),ECRINM
         MVC   ECRINS,ECRINM
         LTR   R15,R15                 NORMAL COMPLETION ?
         BNZ   SEEWHAT                 NO
         L     R15,=V(AUT99R)          GET RESPONSE
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    FININER                 IF YES, EXIT
         LTR   R1,R1                   NO TEXT ?
         BZ    FININER                 YES, EXIT
         MVC   RESPONSE(1),3(R15)
         OI    RESPONSE,C' '           TRANSLATE LOWER CASE IN UPPER
         CLI   RESPONSE,C'A'           IS RESPONSE = A ?
         BE    AUTHOR                  IF YES, GO TO AUTHOR
         CLI   RESPONSE,C'P'           IS RESPONSE = P ?
         BE    INPROG                  IF YES, ACCESS BY PROG NBR
         CLI   RESPONSE,C' '           IS RESPONSE = BLANK ?
         BE    FININER                 IF YES, CLOSE FILE, END
         MVC   ECRINM(L'MSGINV),MSGINV
         MVC   ECRINM+L'MSGINV-5(1),RESPONSE
         B     DEMINIT                 BACK TO DEMAND INITIAL
SEEWHAT  CH    R15,=H'4'
         BE    FININER                 END OF CP REQUESTED
         B     CLSZR                   ELSE EXIT
         EJECT
*--------------------------------------------------------------------*
*        ACCES DU FICHIER PAR LE NUMERO D'AUTORISATION               *
*--------------------------------------------------------------------*
         SPACE 1
AUTHOR   MVI   ECRAUM,C' '
         MVC   ECRAUM+1(L'ECRAUM-1),ECRAUM
REQAUT   LA    R1,PECRAU               ASK AUTHOR. NBRE
         L     R15,=V(AUT99)
         BALR  R14,R15
         LTR   R15,R15                 NORMAL COMPLETION ?
         BNZ   FININER                 NO, CLOSE FILE, END (ONLY 4)
         L     R15,=V(AUT99R)          GET RESPONSE
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    DEMINIT                 IF YES, ESCAPE
         LTR   R1,R1                   NO TEXT ?
         BZ    DEMINIT                 YES, ESCAPE
         MVC   WCL4(4),=CL25' '
         BCT   R1,*+L'*+6
         MVC   WCL4(*-*),3(R15)
         EX    R1,*-6
TESTAUT  CLC   WCL4(4),=CL25' '        IS RESPONSE = BLANK ?
         BE    DEMINIT                 YES, ESCAPE
         MVI   ECRAUM,C' '
         MVC   ECRAUM+1(L'ECRAUM-1),ECRAUM
         LA    R0,L'WCL4
         LA    R1,WCL4+L'WCL4
         BCTR  R1,0
         CLI   0(R1),C'0'              TEST IF NUMERIC
         BL    WRAUTH
         CLI   0(R1),C'9'
         BH    WRAUTH
         BCT   R0,*-18
*------- ICI, NOUS SAVONS QUE L'INPUT EST NUMERIQUE
         PACK  DSD,WCL4                PACK AUTHOR. NUMBER
         CVB   R5,DSD                  CONVERT IN BINARY
         STH   R5,ARGUMAUT             STORE IN ARGUM. AUTHORIZ.
        CALL   ZREAD,(RETCD,ARGUMAUT,AREAR),VL  READ FILE
         LTR   R15,R15                 TEST RETURN CODE
         BZ    FNDRC                   IF ZERO, RECORD FOUND
         CH    R15,=H'15'              IS RETURN CODE 15 ?
         BE    ASKCREA                 IF YES, GO CREATE IT
         B     ERRZR                   IF NOT, GO DISPLAY Z... MESSAGE
WRAUTH   MVC   ECRAUM(L'MSGAN1),MSGAN1 AUTHORIZATION NOT NUMERIC
         B     REQAUT                  BRANCH TO ASK AUTHOR.
         EJECT
*--------------------------------------------------------------------*
*        AUTH. NOT FOUND, ASK IF HE WANTS TO CREATE IT               *
*--------------------------------------------------------------------*
         SPACE 1
ASKCREA  LA    R1,PECRAW               ASK IF CREATING AUTHOR. NBRE
         L     R15,=V(AUT99)
         BALR  R14,R15
         LTR   R15,R15                 NORMAL COMPLETION ?
         BNZ   FININER                 NO, CLOSE FILE, END (ONLY 4)
         MVI   ECRAWM,C' '
         MVC   ECRAWM+1(L'ECRAWM-1),ECRAWM
         L     R15,=V(AUT99R)          GET RESPONSE
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    REQAUT                  IF YES, ESCAPE
         LTR   R1,R1                   NO TEXT ?
         BZ    REQAUT                  YES, ESCAPE
         MVC   RESPONSE(1),3(R15)
         OI    RESPONSE,C' '           TRANSLATE LOWER CASE IN UPPER
         CLI   RESPONSE,C'Y'           IS RESPONSE = Y ?
         BE    EXCREA                  IF YES, CREATE NEW AUTH.
         CLI   RESPONSE,C' '           IS RESPONSE BLANK ?
         BE    REQAUT                  IF YES, ESCAPE
         MVC   ECRAWM(L'MSGRSP),MSGRSP IF NOT, RE-ASK
         MVC   ECRAWM+L'MSGRSP-5(1),RESPONSE
         B     ASKCREA
         EJECT
*--------------------------------------------------------------------*
*        CALL SUBROUTINE TO CREATE A NEW AUTHORIZATION.              *
*--------------------------------------------------------------------*
         SPACE 1
EXCREA  CALL   AUT31,(WCL4,ARGUMAUT),VL
         LTR   R15,R15
         BNZ   WRCREA                  ABNORMAL COMPLETION
         BCTR  R1,0                    NORMAL COMPLETION, SET MSG
         IC    R15,0(R1)
         BCT   R15,*+L'*+6
         MVC   ECRAUM(*-*),1(R1)
         EX    R15,*-6
         B     REQAUT
WRCREA   CH    R15,=H'4'
         BE    FININER                 END OF CP REQUESTED
         B     CLSZR                   SEVERE ERROR
         SPACE 1
*--------------------------------------------------------------------*
*        ACCES DU FICHIER PAR LE NUMERO DE PROGRAMMEUR.              *
*--------------------------------------------------------------------*
         SPACE 1
INPROG  CALL   AUT32                   ACCESS FILE BY PROGR. NUMBER
         LTR   R15,R15
         BNZ   WRINPROG                ABNORMAL COMPLETION
         LTR   R1,R1                   NORMAL COMPLETION
         BNP   DEMINIT
         BCTR  R1,0                    SET MESSAGE
         IC    R15,0(R1)
         BCT   R15,*+L'*+6
         MVC   ECRINM(*-*),1(R1)
         EX    R15,*-6
         B     DEMINIT
WRINPROG CH    R15,=H'4'               ABNORMAL COMPLETION
         BE    FININER                 END OF CP REQUESTED
         B     CLSZR                   SEVERE ERROR
         EJECT
*--------------------------------------------------------------------*
*        TROUVE L'AUTORISATION DANS LE FICHIER                       *
*--------------------------------------------------------------------*
         SPACE 1
*------- PREPARATION DE L'ECRAN D'OUTPUT
FNDRC    MVC   AREARO(AUTLGTH),AREAR   SET FOR HARDCOPY
         OI    SWINU+1,X'F0'           SET SWITCH TO NO UPDATE(S)
         LA    R2,AREAR                SET ADRESSABILITY OF AUTH DSECT
         USING AUTDSECT,R2
         LA    R3,AREAP                SET ADRESSABILITY OF PROG DSECT
         USING PRGDSECT,R3
         LH    R5,AUTANO               LOAD AUTHORIZATION NUMBER
         MVI   SCRSI,C' '              CLEAR OUTPUT FIELD
         LTR   R5,R5                   SET CONDITION CODE
         BNM   *+L'*+4                 IF NOT MINUS, RECORD OK
*------- ICI, LE RECORD TROUVE A ETE SUPPRIME. ON L'A FAIT
*        EN RENDANT LE NUMERO D'AUTHORISATION NEGATIF.
*        CELA VEUT DIRE QU'IL A ETE INVALIDE MAIS NON SUPPRIME |||
         MVI   SCRSI,C'I'              PUT I IN OUTPUT FIELD
         MVC   INSI,SCRSI              RETAIN INITIAL VALUE
         MVC   SCRAUT,WCL4             MOVE AUTH NUMBER FOR DISPLAY
         MVC   SCRFICH,AUTACNO         MOVE FICHE PROGRAMME
         MVC   INFICH,SCRFICH          RETAIN INITIAL VALUE
*------- CONVERT NUMBER OF PROGR. RESPONSABLE
         LH    R5,AUTPMNO              PROGRAMMER RESPONS.
         CVD   R5,DSD                  CONVERT IN DECIMAL
         UNPK  SCRPR,DSD+5(3)          CONVERT IN EBCDIC
         OI    SCRPR+L'SCRPR-1,C'0'    SET LAST ZONING
         MVC   INPR,SCRPR              RETAIN INITIAL VALUE
*------- CONVERT DATE DE DEBUT FOR DISPLAY
         XR    R5,R5
         ICM   R5,B'0111',AUTVSD
         CL    R5,=F'99999'            WHICH FORMAT?
         BH    *+L'*+4                 NEW FORMAT
         AL    R5,=F'1900000'          OLD FORMAT, ADJUST IT
         CVD   R5,DSD                  CONVERT IN PACKED
         UNPK  WCL7,DSD+4(4)           CONVERT IN CHAR YYYYDDD
         OI    WCL7+L'WCL7-1,C'0'      SET LAST ZONING AFTER UNPK
         MVC   INDTEI,WCL7             RETAIN INITIAL VALUE
CALLDT  $GFDATE ,,MDY,WCL7
         LTR   R15,R15                 TEST RETURN CODE
         BZ    NORMC
         CLC   WCL7+4(3),=CL3'001'     IS DATE LOWER 001 ?
         BNL   *+L'*+16                IF NOT, DO NEXT TEST
         MVC   MSG1+30(7),WCL7         SAVE INVALID DATE
         MVC   WCL7+4(3),=CL3'001'     FORCE DATE
         B     SETDT
         CLC   WCL7+4(3),=CL3'365'     IS DATE GREATER 365 ?
         BNH   SDERR                   IF NOT, GO TO ERROR
         MVC   MSG1+30(7),WCL7         SAVE INVALID DATE
         MVC   WCL7+4(3),=CL3'365'     FORCE DATE
SETDT    MVC   MSG1(30),=CL30'Note : DATE de DEBUT invalide'
         NI    SWINU+1,X'0F'           SET SWITCH TO UPDATE(S)
         B     CALLDT
*------- DISPLAY DU RETURN CODE DE GFDATE IF NOT ZERO
SDERR    CVD   R15,DSD
         UNPK  WCL4,DSD+5(3)
         OI    WCL4+L'WCL4-1,C'0'
         MVC   MSGGDTE+30(4),WCL4
         MVC   MSGGDTE+19(7),WCL7      DATE TO TRANSFORM YYYYDDD
         LA    R1,MSGGDTE
         LM    R2,R3,NGDT
         L     R15,=V(AUT97)
         BALR  R14,R15
         B     CLSZR
NORMC    MVC   OUDTEI,WCL7             GFDATE RC = 0, SET SERIAL DATE
         MVC   SCRDTEI(2),MDY+2        MOVE DAYS
         MVI   SCRDTEI+2,C'/'          SEPARATOR
         MVC   SCRDTEI+3(2),MDY        MOVE MONTH
         MVI   SCRDTEI+5,C'/'          SEPARATOR
         MVC   SCRDTEI+6(4),MDY+4      MOVE YEAR
*------- CONVERT DATE DE FIN FOR DISPLAY
         XR    R5,R5
         ICM   R5,B'0111',AUTVED
         CL    R5,=F'99999'            WHICH FORMAT?
         BH    *+L'*+4                 NEW FORMAT
         AL    R5,=F'1900000'          OLD FORMAT, ADJUST IT
         CVD   R5,DSD                  CONVERT IN PACKED
         UNPK  WCL7,DSD+4(4)           CONVERT IN CHAR YYYYDDD
         OI    WCL7+L'WCL7-1,C'0'      SET LAST ZONING AFTER UNPK
         MVC   INDTEO,WCL7             RETAIN INITIAL VALUE
CALLGD  $GFDATE ,,MDY,WCL7
         LTR   R15,R15                 TEST RETURN CODE
         BZ    NORMR
         CLC   WCL7+2(3),=CL3'001'     IS DATE LOWER 001 ?
         BNL   *+L'*+16                IF NOT, DO NEXT TEST
         MVC   MSG2+30(7),WCL7         SAVE INVALID DATE
         MVC   WCL7+2(3),=CL3'001'     FORCE DATE
         B     SETGD
         CLC   WCL7+2(3),=CL3'365'     IS DATE GREATER 365 ?
         BNH   SDERR                   IF NOT, GO TO ERROR
         MVC   MSG2+30(7),WCL7         SAVE INVALID DATE
         MVC   WCL7+2(3),=CL3'365'     FORCE DATE
SETGD    MVC   MSG2(30),=CL30'Note : DATE de FIN invalide'
         NI    SWINU+1,X'0F'           SET SWITCH TO UPDATE(S)
         B     CALLGD
NORMR    MVC   OUDTEO,WCL7             GFDATE RC = 0, SET SERIAL DATE
         MVC   SCRDTEO(2),MDY+2        MOVE DAYS
         MVI   SCRDTEO+2,C'/'          SEPARATOR
         MVC   SCRDTEO+3(2),MDY        MOVE MONTH
         MVI   SCRDTEO+5,C'/'          SEPARATOR
         MVC   SCRDTEO+6(4),MDY+4      MOVE YEAR
         CLC   MSG1(4),=CL25' '
         BNE   CNVNPG
         CLC   MSG2(4),=CL25' '
         BNE   CNVNPG
         MVC   WCL16(4),SCRDTEI+6      CONTROL START-END DATE
         MVC   WCL16+4(2),SCRDTEI+3
         MVC   WCL16+6(2),SCRDTEI
         MVC   WCL16+8(4),SCRDTEO+6
         MVC   WCL16+12(2),SCRDTEO+3
         MVC   WCL16+14(2),SCRDTEO
         CLC   WCL16(8),WCL16+8
         BNH   CNVNPG
         MVC   MSG1(35),=CL35'DATE de DEBUT d!passe DATE de FIN'
         NI    SWINU+1,X'0F'           SET SWITCH TO UPDATE(S)
*------- CONVERT NUMBERS OF PROGR. FOR THIS AUTHORIZATION.
CNVNPG   LA    R5,AUTPNO               ADDRESS OF BEGINNING OF LIST
         LA    R6,NPL                  NBR OF PROGRAMMER IN LIST
         LA    R8,VWORK                WORKING VECTOR
         LA    R9,NAME1                PROGRAMMER'S NAME CORRESPONDING
*------- IF PGM NBR < 0 PUT A 'I' IN CORRESPONDING FIELD IN VECTOR
LOOP     LH    R7,0(R5)                LOAD PROGR. NUMBER
         LTR   R7,R7                   TEST SIGN
         BZ    INCRMN                  IF ZERO, JUMP THIS FIELD
         BM    *+L'*+6
         LNR   R14,R7                  IF POSITIVE, CREATE NEGATIVE
         B     *+L'*+2
         LPR   R14,R7                  IF NEGATIVE, CREATE POSITIVE
         LA    R15,AUTPNO              SCAN TO ELIMINE DOUBLE
SCDBLE   CLR   R5,R15                  SCAN ENDED ?
         BE    HNDLIT                  YES
         CH    R7,0(R15)               NO, SEE IF MATCH
         BE    *+L'*+16
         CH    R14,0(R15)
         BE    *+L'*+8
         LA    R15,L'AUTPNO(R15)
         B     SCDBLE
         NI    SWINU+1,X'0F'           SET SWITCH TO UPDATE(S)
         B     INCRMN
HNDLIT   LTR   R14,R7                  TEST SIGN AGAIN
         BM    INVLD                   IF NEGATIF, HAS BEEN DELETED
         AH    R14,=Y(NAUTNO+1)        ADD NO. OF AUT. AT KEY
         STH   R14,ARGUMPRG            STORE KEY IN ARGUM.PROGRAM.
        CALL   ZREAD,(RETCD,ARGUMPRG,AREAP),VL  READ FILE
         LTR   R15,R15                 TEST RETURN CODE
         BZ    TSTPRGN                 IF ZERO, FOUND PROG'S NAME
         CH    R15,=H'15'              IS RETURN CODE 15 ?
         BNE   ERRZR                   IF NOT, GO DISPLAY Z... MESSAGE
         MVC   0(25,R9),MSGERR1        MSG INSTEAD OF NAME
         MVI   4(R8),C'M'              SAY MSG INSTEAD OF NAME
         B     DOCNV
TSTPRGN  LH    R15,PRGPNO              LOAD PROGRAMMER NUMBER
         LTR   R15,R15                 TEST SIGN
         BP    OKPRGNM                 IF POSITIVE, FOUND PROG'S NAME
*------- CAS OU LE PROGRAMMER NUMBER EST NEGATIF.
*        CELA VEUT DIRE QU'IL A ETE SUPPRIME.
*        ON MET 'I' DANS LA CASE CORRESPONDANTE DU VECTEUR
         MVC   0(25,R9),MSGERR2        MSG INSTEAD OF NAME
         B     SETINV
INVLD    LPR   R7,R7                   CONVERT IN POSITIVE
         MVC   0(25,R9),MSGERR3        MSG INSTEAD OF NAME
SETINV   MVI   4(R8),C'I'              BECAUSE WAS NEGATIVE
         B     DOCNV
OKPRGNM  MVC   0(25,R9),PRGPNME        MOVE NAME IN VECTOR
*------- JUMP TO NEXT RECORD IN VECTORS
DOCNV    CVD   R7,DSD                  CONVERT IN PACKED
         UNPK  0(4,R8),DSD+5(3)        CONVERT IN CHAR IN VECTOR
         OI    3(R8),C'0'              SET LAST ZONING AFTER UNPK
         LA    R9,DX(R9)               JUMP TO NEXT FREE NAME
         LA    R8,L'VWORK(R8)          JUMP TO NEXT FREE FIELD
INCRMN   LA    R5,L'AUTPNO(R5)         JUMP TO NEXT PROGRAMMER
         BCT   R6,LOOP                 LOOP MAX N... TIMES
*------- MOVE ALL PGM NUMBERS AND FLAGS FOR DISPLAY
         LA    R1,DX
         LA    R4,NPL                  PROGRAMMERS BY AUTH.
         LA    R8,VWORK                WORKING VECTOR
         LA    R9,NUM1                 FIELD IN ECRAN
         LA    R14,ASTER1              FIELD IN ECRAN
         LA    R15,NATT1               FIELD IN ECRAN
LVNU     MVC   0(4,R9),0(R8)           MOVE NUM FROM VECTOR IN ECRAN
         CLI   4(R8),C' '              MSG IN NAME?
         BE    LVINS                   NO
         MVC   0(L'NATT1,R15),ATTNI    SET INTENSIVE DISPLAY
         CLI   4(R8),C'M'              WAS MSG MARKER?
         BNE   LVINS                   NO
         MVI   4(R8),C' '              YES, RESET MSG MARKER
         NI    SWINU+1,X'0F'           SET SWITCH TO UPDATE(S)
LVINS    MVC   0(1,R14),4(R8)          MOVE FLAG FROM VECTOR IN ECRAN
         LA    R8,L'VWORK(R8)          JUMP TO NEXT IN VECTOR
         ALR   R9,R1                   JUMP TO NEXT NAME IN ECRAN
         ALR   R14,R1                  JUMP TO NEXT NAME IN ECRAN
         ALR   R15,R1
         BCT   R4,LVNU                 LOOP N... TIMES
*------- RETAIN ALL INITIAL VALUES FOR CONTROL
         LA    R4,NPL                  PROGRAMMERS BY AUTH.
         LA    R8,VWORK                WORKING VECTOR
         LA    R9,VINIT                INITIAL VECTOR
LVCPY    MVC   0(L'VINIT,R9),0(R8)
         LA    R8,L'VWORK(R8)          JUMP TO NEXT
         LA    R9,L'VINIT(R9)
         BCT   R4,LVCPY                LOOP N... TIMES
        $SORT  VINIT,NPL,L'VINIT,0,L'VINIT-1
         EJECT
*--------------------------------------------------------------------*
*        DISPLAY PRIORITY INCREMENT/DECREMENT.                       *
*--------------------------------------------------------------------*
         SPACE 1
         CLI   AUTPI,0
         BE    *+L'*+10
         MVC   SCRPI,AUTPI
         B     *+L'*+4
         MVI   SCRPI,C'4'
         MVC   INPI,SCRPI              RETAIN INITIAL VALUE
         SPACE 1
*--------------------------------------------------------------------*
*        RECHERCHE DU NOM DU PROG. RESPONSABLE DE L'AUTORISATION.    *
*--------------------------------------------------------------------*
*    NB. ALL RECORDS OF PROGRAMMER HAVE A KEY .GE. NO. OF AUT. + 1
*        SO YOU MUST ADD NO.OF AUT. AT THE NBR YOU HAVE FOUND
*        IN RECORD OF AUTHORIZATION TO GET THE RIGHT ADDRESS.
         SPACE 1
         LH    R5,AUTPMNO              LOAD NUMBER OF PROG RESP.
         AH    R5,=Y(NAUTNO+1)         ADD NO. OF AUT. AT KEY
         STH   R5,ARGUMRSP             STORE KEY IN ARGUMENT
        CALL   ZREAD,(RETCD,ARGUMRSP,AREAP),VL  READ FILE
         LTR   R15,R15                 TEST RETURN CODE
         BZ    TSTPGRES                IF ZERO, FOUND PROG. RESP
         CH    R15,=H'15'              IS RETURN CODE 15 ?
         BNE   ERRZR                   IF NOT, GO DISPLAY Z... MESSAGE
         MVC   SCRNOM,MSGPN1
         B     SETMSG
TSTPGRES LH    R15,PRGPNO              LOAD PROGRAMMER NUMBER
         LTR   R15,R15                 TEST SIGN
         BP    OKPRGRES                IF ZERO, FOUND PROG. RESP
         MVC   SCRNOM,MSGPN2
SETMSG   MVC   SCRNOA,ATTNI
         NI    SWINU+1,X'0F'           SET SWITCH TO UPDATE(S)
         B     SWINU
*------- ICI, NOUS AVONS LE RECORD DU PROGRAMMEUR RESPONSABLE
OKPRGRES MVC   SCRNOM,PRGPNME          MOVE RESP. NAME FOR DISPLAY
         MVC   SCRNOA,ATTNP
         EJECT
*--------------------------------------------------------------------*
*        DISPLAY ALL ABOUT THE AUTHORIZATION.                        *
*--------------------------------------------------------------------*
         SPACE 1
SWINU    B     *+L'*+8
         NI    SWUPD+1,X'0F'           SET SWITCH TO UPDATE(S)
         B     DSPLAY
         OI    SWUPD+1,X'F0'           SET SWITCH TO NO UPDATE(S)
DSPLAY   OI    SWCTL+1,X'F0'           SET SWITCH TO NO UPDATE(S)
         LA    R1,PECRAN               DISPLAY SCREEN
         L     R15,=V(AUT99)
         BALR  R14,R15
         LTR   R15,R15                 NORMAL COMPLETION ?
         BNZ   FININER                 NO, END CP (ONLY 4)
         MVI   MSG1,C' '               CLEAR MESSAGE FIELDS
         MVC   MSG1+1(L'MSG1-1),MSG1
         MVC   MSG2,MSG1
         MVI   ECRANM,C' '
         MVC   ECRANM+1(L'ECRANM-1),ECRANM
         L     R15,=V(AUT99R)          GET RESPONSE(S)
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BNZ   *+L'*+12                IF NOT, GO PROCESS
SWUPD    B     NOUPDT                  ESCAPE IF NO UPDATE(S)
         NI    SWCTL+1,X'0F'           SET SWITCH TO DO UPDATE(S)
         B     STCTRL                  GO DO A FINAL CONTROL
         NI    SWUPD+1,X'0F'           SET SWITCH TO UPDATE(S)
         EJECT
*--------------------------------------------------------------------*
*        LECTURE DES CHAMPS ACCESSIBLES A LA MISE A JOUR.            *
*--------------------------------------------------------------------*
*
*        DESCRIPTION DU BUFFER D'INPUT :
*        -------------------------------
*        LES CHAMPS SONT DANS L'INPUT AREA ET SONT DANS L'ORDRE
*        SUIVANT S'ILS SONT TOUS PRESENTS (MODIFIES) :
*         1 = FLAG DE SUPPRESSION, INVALIDATION DE L'AUTORISATION :
*                  D OU I (CL1)
*         2 = FICHE PROGRAMME (CL10)
*         3 = DATE DE DEBUT DE VALIDITE DE L'AUTORISATION (CL8)
*         4 = NUMERO DU PROGR. RESPONSABLE DE L'AUTORISATION (CL4)
*         5 = INCREMENT/DECREMENT DE PRIORITE DE L'AUTORISATION (CL1)
*         6 = DATE DE FIN DE VALIDITE DE L'AUTORISATION (CL8)
*
* (1)     7 = NUMERO DE PROGRAMMEUR POUR CETTE AUTORISATION (CL4)
*         8 = FLAG SI D VEUT DIRE PROGR. SUPPRIME (CL1)
*                  SI I VEUT DIRE PROGR. INVALIDE
*
* (2)     9 = NUMERO DE PROGRAMMEUR POUR CETTE AUTORISATION (CL4)
*        10 = FLAG SI D VEUT DIRE PROGR. SUPPRIME (CL1)
*                  SI I VEUT DIRE PROGR. INVALIDE
*
* ..... ETC POUR LES NUMEROS DE PROGRAMMEUR POSSIBLES.
         SPACE 1
*------- STORE AND TRANSLATE IN UPPER CASE
*        ALL DATA INPUT FROM DISPLAY (SAVE)
         CLC   0(3,R15),SBASI
         BNE   NOTSI
         MVI   SCRSI,C' '
         LTR   R1,R1
         BZ    *+L'*+6
         OC    SCRSI(1),3(R15)         TRANSLATE LOWER CASE IN UPPER
         CLI   SCRSI,C'D'              IS IT 'D' ?
         BE    RDELET                  IF YES, GO TO DELETE RECORD
         CLI   SCRSI,C'X'              IS IT 'X' ?
         BNE   *+L'*+10                IF NOT, CONTINUE
         MVC   ECRAUM(L'MSGUBYP),MSGUBYP  IF YES, ESCAPE
         B     SETUPD
         L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    STCTRL                  IF YES, ALL CHANGE RECEIVED
NOTSI    CLC   0(3,R15),SBAFICH
         BNE   NOTFICH
         LTR   R1,R1
         BZ    FICHOK                  IGNORE IF CLEAR
         MVC   SCRFICH(L'SCRFICH),=CL25' '
         LA    R0,L'SCRFICH
         CLR   R1,R0
         BNH   *+L'*+2
         LR    R1,R0
         BCT   R1,*+L'*+6
         OC    SCRFICH(*-*),3(R15)     TRANSLATE LOWER IN UPPER
         EX    R1,*-6
FICHOK   L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    STCTRL                  IF YES, ALL CHANGE RECEIVED
NOTFICH  CLC   0(3,R15),SBADTEI
         BNE   NOTDTEI
         LTR   R1,R1
         BZ    DTEIOK                  IGNORE IF CLEAR
         MVC   SCRDTEI(L'SCRDTEI),=CL25' '
         LA    R0,L'SCRDTEI
         CLR   R1,R0
         BNH   *+L'*+2
         LR    R1,R0
         BCT   R1,*+L'*+6
         OC    SCRDTEI(*-*),3(R15)     TRANSLATE LOWER IN UPPER
         EX    R1,*-6
DTEIOK   L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    STCTRL                  IF YES, ALL CHANGE RECEIVED
NOTDTEI  CLC   0(3,R15),SBAPR
         BNE   NOTPR
         LTR   R1,R1
         BZ    PROK                    IGNORE IF CLEAR
         MVC   SCRPR(L'SCRPR),=CL25' '
         LA    R0,L'SCRPR
         CLR   R1,R0
         BNH   *+L'*+2
         LR    R1,R0
         BCT   R1,*+L'*+6
         OC    SCRPR(*-*),3(R15)       TRANSLATE LOWER IN UPPER
         EX    R1,*-6
PROK     L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    STCTRL                  IF YES, ALL CHANGE RECEIVED
NOTPR    CLC   0(3,R15),SBAPI
         BNE   NOTPI
         L     R4,=V(AUT98T)
         CLI   0(R4),LV10
         BNH   NOTALL
         LTR   R1,R1
         BNZ   *+L'*+8
         MVI   SCRPI,C'4'              SET DEFAULT
         B     PIOK
         MVC   SCRPI(L'SCRPI),3(R15)
         CLI   SCRPI,C'0'              TEST IF NUMERIC
         BL    *+L'*+8
         CLI   SCRPI,C'9'
         BNH   PIOK
         MVC   MSG2(24),=CL24'Invalid Priority Number|'
         B     NOTALL+L'NOTALL
NOTALL   MVC   MSG2(33),=CL33'Sorry, you can''t change Priority|'
         OI    SWCTL+1,X'F0'           SET SWITCH TO NO UPDATE(S)
PIOK     L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    STCTRL                  IF YES, ALL CHANGE RECEIVED
NOTPI    CLC   0(3,R15),SBADTEO
         BNE   NOTDTEO
         LTR   R1,R1
         BZ    DTEOOK                  IGNORE IF CLEAR
         MVC   SCRDTEO(L'SCRDTEO),=CL25' '
         LA    R0,L'SCRDTEO
         CLR   R1,R0
         BNH   *+L'*+2
         LR    R1,R0
         BCT   R1,*+L'*+6
         OC    SCRDTEO(*-*),3(R15)     TRANSLATE LOWER IN UPPER
         EX    R1,*-6
DTEOOK   L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    STCTRL                  IF YES, ALL CHANGE RECEIVED
NOTDTEO  LA    R4,SBAN1
         LA    R5,NUM1
         LA    R6,ASTER1
         LA    R7,DX
         LA    R8,NPL
         LA    R9,NATT1
SCANVT   MVC   DSD(L'NUM1),0(R5)       RETAIN IT IN CASE BLANKED
         CLC   0(3,R15),0(R4)
         BNE   NOTNUM
         LTR   R1,R1
         BZ    NUMOK                   IGNORE IF CLEAR
         MVC   0(L'NUM1,R5),=CL25' '
         MVC   0(L'NATT1,R9),ATTNP
         LA    R0,L'NUM1
         CLR   R1,R0
         BNH   *+L'*+2
         LR    R1,R0
         BCT   R1,*+L'*+6
         OC    0(*-*,R5),3(R15)        TRANSLATE LOWER IN UPPER
         EX    R1,*-6
NUMOK    L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BNZ   WHATNX                  IF NOT, GO SEE WHAT NEXT
         BAL   R14,CHKBLK              IF YES, ALL CHANGE RECEIVED
         B     STCTRL
WHATNX   CLC   0(3,R15),SBAA1(R4)
         BE    GETAST
         BAL   R14,CHKBLK
         B     NOTAST
NOTNUM   CLC   0(3,R15),SBAA1(R4)
         BNE   NOTAST
GETAST   MVI   0(R6),C' '
         MVC   0(L'NATT1,R9),ATTNP
         LTR   R1,R1
         BZ    *+L'*+6
         OC    0(1,R6),3(R15)          TRANSLATE LOWER IN UPPER
         BAL   R14,CHKBLK
         L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    STCTRL                  IF YES, ALL CHANGE RECEIVED
NOTAST   ALR   R4,R7
         ALR   R5,R7
         ALR   R6,R7
         ALR   R9,R7
         BCT   R8,SCANVT
         MVC   ECRANM(L'ERRCTL0),ERRCTL0  ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------- LOCAL CHECK BLANKED ROUTINE (LINK = R14)
CHKBLK   CLC   0(L'NUM1,R5),=CL25' '   IS IT BLANKED ?
         BNER  R14                     OK IF NOT BLANK
         CLI   0(R6),C'D'              IF BLANK, SEE IF DELETED
         BE    CHKBLK1                 YES
         MVC   0(L'NUM1,R5),DSD        NO, RESTORE IT
         CLC   0(L'NUM1,R5),=CL25' '   CHECK AGAIN IF BLANK ?
         BNER  R14                     OK IF NOT BLANK
CHKBLK1  MVI   0(R6),C' '              FORCE RESET
         MVC   0(L'NATT1,R9),ATTNP
         BR    R14
         EJECT
*--------------------------------------------------------------------*
*        CONTROLE ET VALIDATION DES DONNEES LUES SUR LE DISPLAY.     *
*--------------------------------------------------------------------*
         SPACE 1
*------- CONTROLE DU PREMIER CARACTERE.
*        VALIDE SI BLANC  OU 'I'
STCTRL   CLI   SCRSI,C' '              IS IT BLANK ?
         BE    VALSI                   IF YES, IT IS VALID
         CLI   SCRSI,C'I'              IF NO, IS IT 'I' ?
         BE    VALSI                   IF YES, IT IS VALID
*------- ICI, NOUS SAVONS QUE LE 1ER CARACTERE EST INVALIDE
         MVC   ECRANM(L'ERRCTL1),ERRCTL1  ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------ ICI, NOUS SAVONS QUE LE NUMERO DE FICHE PROGR. EST INVALIDE
WRFICH   EQU   *
*        MVC   ECRANM(L'ERRCTL2),ERRCTL2  ERROR MESSAGE
*        B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------- CONTROLE DU NUMERO DE LA FICHE PROGRAMME.
*        VALIDE SI NUMERIQUE.
VALSI    CLC   SCRFICH(L'SCRFICH),=CL25' '
         BNE   *+L'*+10                OK, NOT BLANK
         MVC   ECRANM(L'ERRCTLA),ERRCTLA  ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*        LA    R0,L'SCRFICH
*        LA    R1,SCRFICH+L'SCRFICH
*        BCTR  R1,0
*        CLI   0(R1),C'0'              TEST IF NUMERIC
*        BL    WRFICH
*        CLI   0(R1),C'9'
*        BH    WRFICH
*        BCT   R0,*-18
         EJECT
*--------------------------------------------------------------------*
*        VALIDATION DE LA DATE DE DEBUT DE L'AUTORISATION.           *
*--------------------------------------------------------------------*
         SPACE 1
         MVC   MDY(2),SCRDTEI+3        MOVE MM IN WORK FIELD
         MVC   MDY+2(2),SCRDTEI        MOVE DD IN WORK FIELD
         MVC   MDY+4(4),SCRDTEI+6      MOVE YYYY IN WORK FIELD
         LA    R0,L'MDY
         LA    R1,MDY+L'MDY
         BCTR  R1,0
         CLI   0(R1),C'0'              TEST IF NUMERIC
         BL    WRDTEI
         CLI   0(R1),C'9'
         BH    WRDTEI
         BCT   R0,*-18
*------- ICI NOUS SAVONS QUE LA DATE EST NUMERIQUE
*        CONTROLE QU'ELLE EST BIEN DU FORMAT DD/MM/YYYY
         LA    R15,SCRDTEI
         BAL   R14,CTDELIM             GO CONTROL DELIMITERS
         B     VALDTEI                 +0 - GO TO DATE IS VALID
         B     ERRDTEI                 +4 - GO TO ERROR DATE
*------- FORMAT DE LA DATE NON VALIDE. SLASH NON EN BONNE PLACE.
         MVC   ECRANM(L'ERRCTL4),ERRCTL4  ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------- TRANSFORMATION A SERIAL DATE.
VALDTEI $GSDATE MDY,WCL7
         LTR   R15,R15
         BZ    STDTEI
*------- ICI, LA DATE DE DEBUT EST INVALIDE. VALEURS INVALIDES.
ERRDTEI  MVC   ECRANM(L'ERRCTL5),ERRCTL5  ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------ ICI, NOUS SAVONS QUE LA DATE DE DEBUT EST INVALIDE. NON NUM.
WRDTEI   MVC   ECRANM(L'ERRCTL3),ERRCTL3  ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------- ICI NOUS SAVONS QUE LA DATE DE DEBUT DE L'AUTORISATION
*        EST VALIDE.
STDTEI   MVC   OUDTEI,WCL7             RETAIN SERIAL DATE
         EJECT
*--------------------------------------------------------------------*
*        CONTROLE DU NUMERO DE PROGRAMMEUR RESPONSABLE.              *
*--------------------------------------------------------------------*
         SPACE 1
         LA    R0,L'SCRPR
         LA    R1,SCRPR+L'SCRPR
         BCTR  R1,0
         CLI   0(R1),C'0'              TEST IF NUMERIC
         BL    WRSCRPR
         CLI   0(R1),C'9'
         BH    WRSCRPR
         BCT   R0,*-18
*------- ICI, NOUS SAVONS QUE LE NUM DU PGM RESPONSABLE
*        EST BIEN NUMERIQUE. TEST S'IL EXISTE DANS LE
*        FICHIER DES PROGRAMMEURS AUTORISES.
         PACK  WPL3,SCRPR              PACK NBR OF PGM RESP
         ZAP   DSD,WPL3                ALIGN IN ADDRESS * 8
         CVB   R5,DSD                  CONVERT PGM NBR IN BINARY
         AH    R5,=Y(NAUTNO+1)         ADD NO. OF AUT. AT KEY
         CH    R5,ARGUMRSP             IS IT CHANGED ?
         BE    VALFO                   NO
         STH   R5,ARGUMRSP             STORE KEY IN ARGUMENT
        CALL   ZREAD,(RETCD,ARGUMRSP,AREAP),VL  READ FILE
         LTR   R15,R15                 TEST RETURN CODE
         BZ    TSTEXPR                 IF ZERO, PROG. RESP EXIST
         CH    R15,=H'15'              IS RETURN CODE 15 ?
         BNE   ERRZR                   IF NOT, GO DISPLAY Z... MESSAGE
         MVC   SCRNOM,MSGPN1           IF NOT, ERROR MESSAGE
         B     SETMPR
TSTEXPR  LH    R15,PRGPNO              LOAD PROGRAMMER NUMBER
         LTR   R15,R15                 TEST SIGN
         BP    EXISTPR                 IF ZERO, FOUND PROG. RESP
         MVC   SCRNOM,MSGPN2
SETMPR   MVC   SCRNOA,ATTNI
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------- ICI, NOUS SAVONS QUE LE NUMERO N'EST PAS NUMERIQUE
WRSCRPR  MVC   ECRANM(L'ERRCTL6),ERRCTL6  ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
EXISTPR  MVC   SCRNOM,PRGPNME
         MVC   SCRNOA,ATTNP
         EJECT
*--------------------------------------------------------------------*
*        VALIDATION DE LA DATE DE FIN DE L'AUTORISATION.             *
*--------------------------------------------------------------------*
         SPACE 1
VALFO    MVC   MDY(2),SCRDTEO+3        MOVE MM IN WORK FIELD
         MVC   MDY+2(2),SCRDTEO        MOVE DD IN WORK FIELD
         MVC   MDY+4(4),SCRDTEO+6      MOVE YYYY IN WORK FIELD
         LA    R0,L'MDY
         LA    R1,MDY+L'MDY
         BCTR  R1,0
         CLI   0(R1),C'0'              TEST IF NUMERIC
         BL    WRDTEO
         CLI   0(R1),C'9'
         BH    WRDTEO
         BCT   R0,*-18
*------- ICI NOUS SAVONS QUE LA DATE EST NUMERIQUE
*        CONTROLE QU'ELLE EST BIEN DU FORMAT DD/MM/YYYY
         LA    R15,SCRDTEO
         BAL   R14,CTDELIM             GO CONTROL DELIMITERS
         B     VALDTEO                 +0 - GO TO DATE IS VALID
         B     ERRDTEO                 +4 - GO TO ERROR DATE
*------- FORMAT DE LA DATE NON VALIDE. SLASH NON EN BONNE PLACE.
         MVC   ECRANM(L'ERRCTL8),ERRCTL8  ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------- TRANSFORMATION A SERIAL DATE.
VALDTEO $GSDATE MDY,WCL7
         LTR   R15,R15
         BZ    STDTEO
*------- ICI, LA DATE DE FIN EST INVALIDE. VALEURS INVALIDES.
ERRDTEO  MVC   ECRANM(L'ERRCTL9),ERRCTL9  ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------- ICI, NOUS SAVONS QUE LA DATE DE FIN EST INVALIDE. NON NUM.
WRDTEO   MVC   ECRANM(L'ERRCTL7),ERRCTL7  ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------- ICI NOUS SAVONS QUE LA DATE DE FIN DE L'AUTORISATION
*        EST VALIDE.
STDTEO   MVC   OUDTEO,WCL7             RETAIN SERIAL DATE
         MVC   WCL16(4),SCRDTEI+6      CONTROL START-END DATE
         MVC   WCL16+4(2),SCRDTEI+3
         MVC   WCL16+6(2),SCRDTEI
         MVC   WCL16+8(4),SCRDTEO+6
         MVC   WCL16+12(2),SCRDTEO+3
         MVC   WCL16+14(2),SCRDTEO
         CLC   WCL16(8),WCL16+8
         BNH   *+L'*+10
         MVC   ECRANM(35),=CL35'DATE de DEBUT d!passe DATE de FIN'
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
         EJECT
*--------------------------------------------------------------------*
*        CONTROLE DES NUMEROS DE PROGR. ENTRES ET DES                *
*        CODES D, I OU BLANC TROUVES AU NIVEAU PROGR.                *
*--------------------------------------------------------------------*
         SPACE 1
         LA    R9,NPL                  LOOP N... TIMES
         LA    R4,NATT1
         LA    R5,NUM1
         LA    R6,ASTER1
         LA    R7,NAME1
         LA    R8,VWORK                WORKING VECTOR
LOOPCTL  CLC   0(4,R5),=CL25' '        IS PGM NBR PRESENT ?
         BNE   CHKPGN                  YES
         MVC   0(L'VWORK,R8),=CL25' '
         CLC   0(25,R7),=CL25' '       CLEAR IF NEEDED
         BE    INCRCTL
         MVC   0(25,R7),=CL25' '
         B     SETSWC
CHKPGN   LA    R0,4
         LA    R1,4(R5)
         BCTR  R1,0
         CLI   0(R1),C'0'              TEST IF NUMERIC
         BL    WRPGMN
         CLI   0(R1),C'9'
         BH    WRPGMN
         BCT   R0,*-18
         CLC   0(4,R5),0(R8)           PGM NUMBER CHANGED ?
         BE    NOTCHG                  NO
         MVI   0(R6),C' '              YES, FORCE CODE BLANK
         LA    R15,NUM1                SCAN TO ELIMINE DOUBLE
DBLSCN   CLR   R5,R15                  SCAN ENDED ?
         BE    PRCSIT                  YES, PROCESS IT
         CLC   0(4,R5),0(R15)          NO, SEE IF DEFINED PREVIOUSLY
         BE    *+L'*+8
         LA    R15,DX(R15)
         B     DBLSCN
         MVC   0(4,R5),=CL25' '        CANCEL ALL ABOUT THIS ONE
         MVC   0(25,R7),=CL25' '
         MVC   0(L'VWORK,R8),=CL25' '
         B     SETSWC
WRPGMN   MVC   0(25,R7),MSGERR4        NOT NUMERIC MESSAGE
         B     SETSWC
PRCSIT   MVC   0(4,R8),0(R5)           RETAIN IT
         OI    SWCTL+1,X'F0'           SET SWITCH TO NO UPDATE(S)
         B     GETNME
NOTCHG   CLC   0(1,R6),4(R8)           STATUS CHANGED ?
         BE    INCRCTL                 NO
         CLI   0(R6),C'D'              IS CODE 'D' ?
         BE    SETSPP                  YES
         CLI   0(R6),C'I'              IS CODE 'I'?
         BNE   *+L'*+10                NO
         MVC   0(25,R7),MSGERR3
         B     CPYST
         CLI   0(R6),C' '              IS CODE BLANK ?
         BE    GETNME                  YES
         MVC   0(25,R7),MSGERR5        CODE ERROR MESSAGE
         B     SETINT
GETNME   MVC   4(1,R8),0(R6)
         PACK  DSD,0(4,R5)
         CVB   R0,DSD
         AH    R0,=Y(NAUTNO+1)         ADD NO. OF AUT. AT KEY
         STH   R0,ARGUMPRG             STORE KEY IN ARGUM.PROGRAM.
        CALL   ZREAD,(RETCD,ARGUMPRG,AREAP),VL  READ FILE
         LTR   R15,R15                 TEST RETURN CODE
         BZ    TSTNME                  IF ZERO, FOUND PROG'S NAME
         CH    R15,=H'15'              IS RETURN CODE 15 ?
         BNE   ERRZR                   IF NOT, GO DISPLAY Z... MESSAGE
         MVC   0(25,R7),MSGERR1        MSG NOT FOUND
         B     SETINT
TSTNME   LH    R15,PRGPNO              LOAD PROGRAMMER NUMBER
         LTR   R15,R15                 TEST SIGN
         BP    SETNME                  IF POSITIVE, FOUND PROG'S NAME
         MVC   0(25,R7),MSGERR2        MSG INVALIDATED
         MVI   0(R6),C'I'              FORCE INVALID
CPYST    MVC   4(1,R8),0(R6)           COPY STATUS
SETINT   MVC   0(L'NATT1,R4),ATTNI     SET INTENSIVE DISPLAY
         B     SETSWC
SETSPP   MVC   0(25,R7),MSGERR6        SET DELETED
         B     CPYST
SETNME   MVC   0(25,R7),PRGPNME        SET NAME
SETSWC   OI    SWCTL+1,X'F0'           SET SWITCH TO NO UPDATE(S)
INCRCTL  LA    R4,DX(R4)
         LA    R5,DX(R5)
         LA    R6,DX(R6)
         LA    R7,DX(R7)
         LA    R8,L'VWORK(R8)
         BCT   R9,LOOPCTL
SWCTL    B     DSPLAY
         EJECT
*--------ICI, NOUS SOMMES SUR DE LA VALIDITE DU RECORD LU SUR LE
*        DISPLAY, SUIT LE TRAITEMENT DE CE RECORD.
*        SEE IF SOME INITIAL VALUES CHANGED.
         NI    CMSWW+1,X'0F'
         CLC   INSI,SCRSI
         BE    CMSRT
         CLI   INSI,C' '               VALID AUT.?
         BNE   EXWRT                   NO
         L     R15,=A(EXTCTL)          * * * EXTENDED CONTROL * * *
         BALR  R14,R15                 * * * EXTENDED CONTROL * * *
         DC    Y(CHKINA-EXTCTL)        * * * EXTENDED CONTROL * * *
         OI    CMSWW+1,X'F0'
CMSRT   $SORT  VWORK,NPL,L'VWORK,0,L'VWORK-1
         LA    R4,NPL                  PROGRAMMERS BY AUTH.
         LA    R8,VWORK                WORKING VECTOR
         LA    R9,VINIT                INITIAL VECTOR
CMCTL    CLC   0(L'VINIT,R9),0(R8)     HAS CHANGED ?
         BE    CMNXT                   NO
         CLI   4(R8),C' '              YES, IS IT AN ADDITION ?
         BE    CMSET                   YES
         CLI   4(R8),C'D'              NO, IS IT A DELETION ?
         BNE   CMCHK                   NO
         CLC   0(L'VINIT,R9),=CL25' '  YES, DELETION OF SOME ADDED ?
         BE    CMNXT                   YES
CMCHK    L     R15,=A(EXTCTL)          * * * EXTENDED CONTROL * * *
         BALR  R14,R15                 * * * EXTENDED CONTROL * * *
         DC    Y(CHKAPN-EXTCTL)        * * * EXTENDED CONTROL * * *
CMSET    OI    CMSWW+1,X'F0'
CMNXT    LA    R8,L'VWORK(R8)          JUMP TO NEXT
         LA    R9,L'VINIT(R9)
         BCT   R4,CMCTL                LOOP N... TIMES
CMSWW    NOP   EXWRT
         CLC   INFICH,SCRFICH
         BNE   EXWRT
         CLC   INDTEI,OUDTEI
         BNE   EXWRT
         CLC   INDTEO,OUDTEO
         BNE   EXWRT
         CLC   INPR,SCRPR
         BNE   EXWRT
         CLC   INPI,SCRPI
         BNE   EXWRT
NOUPDT   MVC   ECRAUM(L'MSGUPDN),MSGUPDN
         B     SETUPD
EXWRT    MVC   INSI,SCRSI
         MVC   INFICH,SCRFICH
         MVC   INDTEI,OUDTEI
         MVC   INDTEO,OUDTEO
         MVC   INPR,SCRPR
         MVC   INPI,SCRPI
*------- BUILD AUTHORIZATION UPDATE RECORD.
         XC    AREAR(AUTLGTH),AREAR    CLEAR RECORD AREA
         LH    R1,ARGUMAUT
         CLI   INSI,C'I'
         BNE   *+L'*+2
         LNR   R1,R1
         STH   R1,AUTANO               SET AUTH. NUMBER AS DESIRED
         MVC   AUTACNO,INFICH          SET FICHE PROGRAMME
         CLI   INPI,C'4'
         BE    *+L'*+6
         MVC   AUTPI,INPI              SET PRIORITY INCR/DECR
         PACK  DSD,INDTEI              TRANFORM EBCDIC IN PACKED
         CVB   R1,DSD                  CONVERT PACKED IN FIXED
         STCM  R1,B'0111',AUTVSD       SET START DATE
         PACK  DSD,INDTEO              TRANFORM EBCDIC IN PACKED
         CVB   R1,DSD                  CONVERT PACKED IN FIXED
         STCM  R1,B'0111',AUTVED       SET END DATE
         PACK  DSD,INPR                PACK PGM RESP NBR.
         CVB   R1,DSD                  CONVERT PACKED IN FIXED
         STH   R1,AUTPMNO              STORE PROG RESP NBR
         LA    R4,NPL                  PROGRAMMERS BY AUTH.
         LA    R5,AUTPNO               ADDRESS OF BEGINNING OF LIST
         LA    R6,VWORK                WORKING VECTOR
BLDSCN   CLC   0(4,R6),=CL25' '        ANY PGM NBR PRESENT ?
         BE    BLDNXT                  NO, SKIP TO NEXT
         CLI   4(R6),C'D'              YES, IS IT TO DELETE ?
         BE    BLDNXT                  YES, SKIP TO NEXT
         PACK  DSD,0(4,R6)             PACK PGM NBR.
         CVB   R1,DSD                  CONVERT PACKED IN FIXED
         CLI   4(R6),C'I'              YES, IS IT TO INVALIDATE ?
         BNE   *+L'*+2                 NO
         LNR   R1,R1                   YES, SET IT NEGATIVE
         STH   R1,0(R5)                STORE IN RECORD
         LA    R5,L'AUTPNO(R5)         JUMP TO NEXT IN RECORD
BLDNXT   LA    R6,L'VWORK(R6)          JUMP TO NEXT IN VECTOR
         BCT   R4,BLDSCN               LOOP N... TIMES
*------- WRITE BACK AUTHORIZATION IN FILE
        CALL   ZWRITE,(RETCD,ARGUMAUT,AREAR),VL
         LTR   R15,R15
         BNZ   UPFAIL
         LA    R1,RQHCPY1              HARDCOPY OF OPERATION
         L     R15,=V(AUT96)
         BALR  R14,R15
         LTR   R15,R15                 TEST RETURN CODE
         BZ    EXSUCC
         XR    R2,R2                   PASS HARDCOPY ERROR
         LR    R3,R0
         B     SCRAMBLE                SEVERE ERROR
EXSUCC   MVC   ECRAUM(L'MSGDONE),MSGDONE
         B     SETUPD
UPFAIL   CH    R15,=H'15'              IS RETURN CODE 15 ?
         BNE   ERRZW                   IF NOT, GO DISPLAY Z... MESSAGE
         MVC   ECRAUM(L'MSGFAIL),MSGFAIL
SETUPD   MVC   ECRAUM(6),=CL6'UPDATE'
         B     SETMA
*------- DELETE AUTHORIZATION FROM FILE
RDELET   CLI   INSI,C'I'               INVALID AUT.?
         BE    DLAOK                   YES
         L     R15,=A(EXTCTL)          * * * EXTENDED CONTROL * * *
         BALR  R14,R15                 * * * EXTENDED CONTROL * * *
         DC    Y(CHKDLA-EXTCTL)        * * * EXTENDED CONTROL * * *
DLAOK    MVC   ECRADA,SCRAUT           SET AUT. NR. IN DISPLAY
         LA    R1,PECRAD               DISPLAY SCREEN TO CONFIRM
         L     R15,=V(AUT99)
         BALR  R14,R15
         LTR   R15,R15                 NORMAL COMPLETION ?
         BNZ   FININER                 NO, END CP (ONLY 4)
         MVI   ECRADM,C' '
         MVC   ECRADM+1(L'ECRADM-1),ECRADM
         L     R15,=V(AUT99R)          GET RESPONSE(S)
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    DSPLAY                  IF YES, ESCAPE
         LTR   R1,R1                   NO TEXT ?
         BZ    DSPLAY                  YES, ESCAPE
         MVC   RESPONSE(1),3(R15)
         OI    RESPONSE,C' '           TRANSLATE LOWER CASE IN UPPER
         CLI   RESPONSE,C'Y'           IS RESPONSE = Y ?
         BE    XDELET                  IF YES, GO TO DELETE
         CLI   RESPONSE,C'N'           IS RESPONSE = N ?
         BE    DSPLAY                  IF YES, ESCAPE
         CLI   RESPONSE,C' '           IS RESPONSE = BLANK ?
         BE    DSPLAY                  IF YES, ESCAPE
         MVC   ECRADM(L'MSGRSP),MSGRSP
         MVC   ECRADM+L'MSGRSP-5(1),RESPONSE
         B     RDELET+L'RDELET         BACK TO CONFIRM AGAIN
XDELET  CALL   ZDELET,(RETCD,ARGUMAUT),VL
         LTR   R15,R15
         BNZ   DLFAIL
         LA    R1,RQHCPY2              HARDCOPY OF OPERATION
         L     R15,=V(AUT96)
         BALR  R14,R15
         LTR   R15,R15                 TEST RETURN CODE
         BZ    SETDLT
         XR    R2,R2                   PASS HARDCOPY ERROR
         LR    R3,R0
         B     SCRAMBLE                SEVERE ERROR
DLFAIL   CH    R15,=H'15'              IS RETURN CODE 15 ?
         BNE   ERRZD                   IF NOT, GO DISPLAY Z... MESSAGE
         MVC   ECRAUM(L'MSGFAIL),MSGFAIL
         B     SETDLT+L'SETDLT
SETDLT   MVC   ECRAUM(L'MSGDONE),MSGDONE
         MVC   ECRAUM(6),=CL6'DELETE'
SETMA    LH    R0,ARGUMAUT
         CVD   R0,DSD
         MVC   DSD(5),=XL5'2120202020'
         ED    DSD(5),DSD+5
         MVC   ECRAUM+L'MSGDONE-5(4),DSD+1
         DROP  R2,R3
         SPACE 1
FINCP    MVI   MSG1,C' '               CLEAR MESSAGE FIELDS
         MVC   MSG1+1(L'MSG1-1),MSG1
         MVC   MSG2,MSG1
*------- INITIALIZE OUTPUT SCREEN FIELDS
         MVI   SCRSI,C' '              INIT FIELD WITH BLANKS
         MVC   SCRFICH(L'SCRFICH),=CL25' '
         MVC   SCRDTEI(L'SCRDTEI),=CL25' '
         MVC   SCRPR(L'SCRPR),=CL25' '
         MVI   SCRPI,C' '
         MVC   SCRDTEO(L'SCRDTEO),=CL25' '
         LA    R0,NPL                  MAX.NO OF FIELDS
         LA    R1,DX
         LA    R5,NUM1
         LA    R6,ASTER1
         LA    R7,NATT1
         LA    R8,NAME1
REINIT   MVC   0(L'NUM1,R5),=CL25' '   RESET ALL FIELDS
         MVI   0(R6),C' '
         MVC   0(L'NATT1,R7),ATTNP
         MVC   0(L'NAME1,R8),=CL25' '
         ALR   R5,R1
         ALR   R6,R1
         ALR   R7,R1
         ALR   R8,R1
         BCT   R0,REINIT
         LA    R0,NPL
         LA    R5,VWORK                WORKING VECTOR
         LA    R6,VINIT                INITIAL VECTOR
REINVT   MVC   0(L'VWORK,R5),=CL25' '  INITIALIZE VECTORS
         MVC   0(L'VINIT,R6),=CL25' '
         LA    R5,L'VWORK(R5)
         LA    R6,L'VINIT(R6)
         BCT   R0,REINVT
         B     REQAUT
         EJECT
*--------------------------------------------------------------------*
*        ERREUR AU MOMENT DE L'ALLOCATION DU FICHIER D'ACCOUNTING.   *
*--------------------------------------------------------------------*
         SPACE 1
ERALL    STM   R15,R0,DSD
        TPUT   ERROR1,L'ERROR1
         UNPK  ERROR1C+8(9),DSD(5)
         TR    ERROR1C+8(8),TRTAB-X'F0'
         MVI   ERROR1C+16,C' '
         UNPK  ERROR1C+22(9),DSD+4(5)
         TR    ERROR1C+22(8),TRTAB-X'F0'
         MVI   ERROR1C+30,C' '
        TPUT   ERROR1C,L'ERROR1C
         B     LEAVE
         SPACE 1
*--------------------------------------------------------------------*
*        FIN DU TRAVAIL - SORTIE.                                    *
*--------------------------------------------------------------------*
         SPACE 1
FININER  XR    R1,R1                   EXIT FULLSCREEN
         L     R15,=V(AUT99)
         BALR  R14,R15
CLSZR    XR    R1,R1                   CLOSE HARDCOPY
         L     R15,=V(AUT96)
         BALR  R14,R15
        CALL   ZCLOSE,(RETCD),VL       CLOSE FILE UNDER SYSACC DD
         LTR   R15,R15                 IS RETURN CODE 0?
         BNZ   ERRZOC                  IF NOT, ERROR AT CLOSE
FREACC   XC    DYNDSN,DYNDSN
        $DYNALL DYNLIST                DYNAMIC DE-ALLOC OF SYSACC
LEAVE   $XRET  CC=0
         EJECT
*--------------------------------------------------------------------*
*        ERREUR A L'UTILISATION DU FICHIER D'ACCOUNTING              *
*--------------------------------------------------------------------*
         SPACE 1
ERRZOC   LR    R0,R15
         XR    R1,R1
        CALL   ZYDIAG
         LA    R8,1(R15)
         XR    R9,R9
         BCTR  R15,0
         IC    R9,0(R15)
         BCTR  R9,0
        TPUT   (R8),(R9)
         B     FREACC
         SPACE 1
ERRZW    LM    R2,R3,NZWR
         B     ERRZX
ERRZD    LM    R2,R3,NZDL
         B     ERRZX
ERRZR    LM    R2,R3,NZRD
ERRZX    LR    R0,R15
         XR    R1,R1
SCRAMBLE L     R15,=V(AUT97)
         BALR  R14,R15
         B     CLSZR
         EJECT
*--------------------------------------------------------------------*
*        CONTROLE FORMAT ET VALIDITE D'UNE DATE (ROUTINE)            *
*--------------------------------------------------------------------*
         SPACE 1
*        MDY = DATE IN FORMAT MMDDYYYY
*        R15 = ADDRESS OF DATE (DD/MM/YYYY)
*        R14 = LINK - RETURN = +0 DATE IS VALID
*                              +4 ERROR DATE
*                              +8 DATE IS INVALID
*        CONTROLE QU'ELLE EST BIEN DU FORMAT DD/MM/YYYY
CTDELIM  CLI   2(R15),C'/'             IS / PRESENT ?
         BE    CTDLM2                  YES
         CLI   2(R15),C' '             IS BLANK PRESENT ?
         BE    CTDLM1                  YES
         CLI   2(R15),C'.'             IS PERIOD PRESENT ?
         BE    CTDLM1                  YES
         CLI   2(R15),C'-'             IS - PRESENT ?
         BNE   8(R14)                  IF NOT, GO TO INVALID DATE
CTDLM1   MVI   2(R15),C'/'             SET / IN PLACE
CTDLM2   CLI   5(R15),C'/'             IS PRESENT SECOND / ?
         BE    CTDLM4                  YES
         CLI   5(R15),C' '             IS PRESENT SECOND BLANK ?
         BE    CTDLM3                  YES
         CLI   5(R15),C'.'             IS PRESENT SECOND PERIOD ?
         BE    CTDLM3                  YES
         CLI   5(R15),C'-'             IS PRESENT SECOND - ?
         BNE   8(R14)                  IF NOT, GO TO INVALID DATE
CTDLM3   MVI   5(R15),C'/'             SET / IN PLACE
*------- ICI NOUS SOMMES CERTAINS QUE LE FORMAT DE LA DATE EST BON
*        ET QU'ELLE EST NUMERIQUE.
*        CONTROLE DE LA VRAISEMBLANCE DE LA DATE
*    NB. LA DATE EGALE A 99/99/9999 EST VALIDE (SIGNIFIE L'INFINI).
CTDLM4   CLC   MDY,=C'99999999'        COMPARE WITH 99/99/9999
         BER   R14                     IF EQUAL, DATE IS VALID
*------- ICI LES JOURS ET MOIS NE SONT PAS EGAUX A 99
         CLC   MDY+2(2),=C'31'         IS DAY > 31 ?
         BH    4(R14)                  IF YES, GO TO ERROR DATE
         CLC   MDY+2(2),=C'01'         IS DAY < 01 ?
         BL    4(R14)                  IF YES, GO TO ERROR DATE
         CLC   MDY(2),=C'12'           IS MONTH > 12 ?
         BH    4(R14)                  IF YES, GO TO ERROR DATE
         CLC   MDY(2),=C'01'           IS MONTH < 01 ?
         BL    4(R14)                  IF YES, GO TO ERROR DATE
*------- ICI, LA DATE EST VRAISEMBLABLE
         BR    R14                     BACK, DATE IS VALID
         EJECT
*--------------------------------------------------------------------*
*        VARIABLES DE TRAVAIL                                        *
*--------------------------------------------------------------------*
         SPACE 1
DSD      DC    D'0'
PECRIN   DC    A(LECRIF,ECRIN)
PECRAU   DC    A(LECRAU,ECRAU)
PECRAW   DC    A(LECRAW,ECRAW)
PECRAN   DC    A(LECRAN,ECRAN)
PECRAD   DC    A(LECRAD,ECRAD)
RETCD    DC    F'0'          RETURN CODE FROM Z... ROUTINES
NZRD     DC    0F'0',CL8'ZREAD'
NZDL     DC    0F'0',CL8'ZDELET'
NZWR     DC    0F'0',CL8'ZWRITE'
NGDT     DC    0F'0',CL8'GFDATE'
AREAR    DC    (AUTLGTH)X'0' WORKA FOR READ FILE. RECORD AUTHOR.
AREARO   DC    (AUTLGTH)X'0' WORKA FOR READ FILE. RECORD AUTHOR.
AREAP    DC    (PRGLGTH)X'0' WORKA FOR READ FILE. RECORD PROGRAMMER
ARGUMAUT DC    H'0'          AUTORIZATION NUMBER TO CHAIN IN FILE
ARGUMPRG DC    H'0'          PROGRAMMER NUMBER TO CHAIN IN FILE
ARGUMRSP DC    H'0'          RESP. PGR. NUMBER TO CHAIN IN FILE
MDY      DC    CL8' '        MONTH, DAY, YEAR FROM GFDATE
OUDTEI   DC    CL7' '        SERIAL DATE FROM GSDATE
OUDTEO   DC    CL7' '        SERIAL DATE FROM GSDATE
RESPONSE DC    C' '
WPL3     DC    PL3'0'        WORK FIELD PACKED LENGTH 3
WCL4     DC    CL4' '        WORK FIELD CHAR LENGTH 4
WCL7     DC    CL7' '        WORK FIELD CHAR LENGTH 7
WCL16    DC    CL16' '       WORK FIELD CHAR LENGTH 16
ATTNP   $FS    SF=(PROT),MF=L
ATTNI   $FS    SF=(PROT,INT),MF=L
         EJECT
*--------------------------------------------------------------------*
*        DYNAMIC ALLOCATION PARAMETER LIST                           *
*--------------------------------------------------------------------*
         SPACE 1
DYNLIST  DC    C'SYSACC  '
DYNDSN   DC    XL44'0'
         DC    CL8' '        MEMBER NAME
         DC    CL8' '        PASSWORD
         DC    CL8'SHR'      STATUS
         DC    CL256' '      FILLER
         DC    CL85' '       FILLER
         SPACE 1
*--------------------------------------------------------------------*
*        VECTORS DEFINITION                                          *
*--------------------------------------------------------------------*
         SPACE 1
*                            - - - - - - - - - - - - - - - - - - - - -
INSI     DC    CL1' '        AUT. CODE
INFICH   DC    CL10' '       FICHE PROGRAMME
INDTEI   DC    CL7' '        STARTING DATE
INDTEO   DC    CL7' '        ENDING DATE
INPR     DC    CL4' '        RESPONSIBLE PROGR. NUMBER
INPI     DC    CL4' '        PRIORITY INCREMENT/DECREMENT
*                            - - - - - - - - - - - - - - - - - - - - -
VWORK    DS    0CL5          WORKING VECTOR
*                            +0 = PGM NBR IN EBCDIC
*                            +4 = 'I' IF PGM NBR < 0
*                                 'D' IF PGM NBR TO DELETE
         DC    (NPL)CL5' '
*                            - - - - - - - - - - - - - - - - - - - - -
VINIT    DS    0CL5          INITIAL VECTOR
*                            +0 = PGM NBR IN EBCDIC
*                            +4 = 'I' IF PGM NBR < 0
         DC    (NPL)CL5' '
         EJECT
*--------------------------------------------------------------------*
*        HARDCOPY REQUESTS.                                          *
*--------------------------------------------------------------------*
         SPACE 1
RQHCPY1  DC    AL1(3),AL4(ARGUMAUT,AREARO,AREAR)   UPDATE AUTH.
RQHCPY2  DC    AL1(5),AL4(ARGUMAUT,AREARO)   DELETE AUTHORIZATION.
         SPACE 1
*--------------------------------------------------------------------*
*        MESSAGES D'ERREURS                                          *
*--------------------------------------------------------------------*
         SPACE 1
ERROR1   DC    C'*** ACCOUNT file ALLOC error *** '
ERROR1C  DC    C'    R15=........ - R0=........ - AUT30 '
         DC    AL1(L'MSGGDTE)
MSGGDTE  DC    C' GFDATE error (Day=.......,RC=....)'
MSGINV   DC    C'Unknown option selected :   ???'
MSGRSP   DC    C'Unknown response received :   ???'
MSGAN1   DC    C'Authorization NOT numeric or NOT 4 digits.'
MSGPN1   DC    CL40'RESP. Progr. Number NOT found'
MSGPN2   DC    CL40'RESP. Progr. Nr. REC invalid.'
MSGDONE  DC    C'...... is successfully done - Authorization :     .'
MSGUPDN  DC    C'...... bypassed - No change - Authorization :     .'
MSGUBYP  DC    C'...... bypassed - Escape... - Authorization :     .'
MSGFAIL  DC    C'...... has failed - Authorization NOT found :     .'
ERRCTL0  DC    C'Input scan SBA order not found ???'
ERRCTL1  DC    C'Auth. action error (set I, D or null)'
ERRCTL2  DC    C'Numero de Fiche Progr. NON num!rique|'
ERRCTL3  DC    C'DATE de DEBUT non  N U M E R I Q U E |'
ERRCTL4  DC    C'DATE de DEBUT mal format!e (JJ/MM/AAAA)'
ERRCTL5  DC    C'DATE de DEBUT avec valeurs invalides.'
ERRCTL6  DC    C'Numero de PGM. RESP. NON num!rique|'
ERRCTL7  DC    C'DATE de FIN non  N U M E R I Q U E |'
ERRCTL8  DC    C'DATE de FIN mal format!e (JJ/MM/AAAA)'
ERRCTL9  DC    C'DATE de FIN avec valeurs invalides.'
ERRCTLA  DC    C'D!finissez la Fiche Programme S.V.P. |||'
MSGERR1  DC    CL25'Progr. Number NOT found'
MSGERR2  DC    CL25'Progr. Nr.REC Invalidated'
MSGERR3  DC    CL25'Progr. Number Invalidated'
MSGERR4  DC    CL25'Progr. Number NOT numeric'
MSGERR5  DC    CL25'Code error (D, I or null)'
MSGERR6  DC    CL25'Progr. Number deleted'
TRTAB    DC    C'0123456789ABCDEF'
         EJECT
*--------------------------------------------------------------------*
*        ECRANS D'OUTPUT                                             *
*--------------------------------------------------------------------*
         SPACE 1
ECRIN   $FS    CC=W,WCC=(AL,KBR,RMDT),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,25),SF=(PROT,INT),MF=L
        $FS    TEXT='AUTHORIZATION and/or PROGRAMMER',MF=L
        $FS    SBA=(2,25),SF=(PROT,INT),MF=L
        $FS    TEXT='     U   P   D   A   T   E     ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(3,25),SF=(PROT,INT),MF=L
        $FS    TEXT='===============================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(5,10),SF=(PROT),MF=L
        $FS    TEXT='You can update information for ',MF=L
        $FS    TEXT='an Authorization Number',MF=L
        $FS    SBA=(6,39),SF=(PROT),MF=L
        $FS    TEXT='or a Programmer Number',MF=L
        $FS    SBA=(9,11),TEXT='Enter A or P',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),TEXT=' ',MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(10,16),SF=(PROT),TEXT='or you may Hit',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SF=(PROT),TEXT='or',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-3',MF=L
        $FS    SF=(PROT),TEXT='to exit (emergency)',MF=L
        $FS    SBA=(11,10),SF=(PROT),TEXT='Hit',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-1',MF=L
        $FS    SF=(PROT),TEXT='to view ',MF=L
        $FS    TEXT='Authorizations/Programmers ',MF=L
        $FS    TEXT='MANAGEMENT Rules Summary',MF=L
        $FS    SBA=(12,10),SF=(PROT),TEXT='Hit',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-2',MF=L
        $FS    SF=(PROT),TEXT='to view ',MF=L
        $FS    TEXT='the current Accounting data-set status',MF=L
        $FS    SBA=(14,1),SF=(PROT,INT),MF=L
ECRINM  $FS    TEXT=(' ',78),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(15,1),SF=(PROT,INT),MF=L
ECRINS  $FS    TEXT=(' ',78),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(17,1),SF=(PROT,INT),TEXT='Advice :',MF=L
        $FS    SF=(PROT),TEXT='from now until you exit this ',MF=L
        $FS    TEXT='process, you have the',MF=L
        $FS    SF=(PROT,INT),TEXT='exclusive',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(18,11),TEXT='use of the accounting data-set.',MF=L
LECRIN   EQU   (*-ECRIN)+LPTIC
OVRR    $FS    SBA=(20,1),SF=(PROT,INT),RA=(20,80,-),MF=L
        $FS    SBA=(21,2),TEXT='Remark :',MF=L
        $FS    SF=(PROT),TEXT='if desired, you can also enter ',MF=L
        $FS    TEXT='directly in this process typing',MF=L
        $FS    SBA=(22,11),TEXT='the TSO-command',MF=L
        $FS    SF=(PROT,INT),TEXT='AUT30',MF=L
        $FS    SF=(PROT),TEXT='at any time in your session.',MF=L
        $FS    SBA=(23,1),SF=(PROT,INT),RA=(23,80,-),MF=L
        PRINT  OFF
        $FS    SBA=(24,1),SF=(PROT,INT),TEXT='WELCOME',MF=L
        $FS    SF=(PROT),MF=L
        $FS    TEXT='to this up-grade CP implemented by ',MF=L
        $FS    TEXT='MOINIL P.A. ... your wizard |',MF=L
        PRINT  ON
PTIC    $FS    SBA=(1,1),SF=(PT,IC),MF=L
LPTIC    EQU   *-PTIC
LECRIF   EQU   *-ECRIN
         SPACE 1
ECRAU   $FS    CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,25),SF=(PROT,INT),MF=L
        $FS    TEXT='AUTHORIZATION     N U M B E R',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,25),SF=(PROT,INT),MF=L
        $FS    TEXT='=============================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(5,10),SF=(PROT),MF=L
        $FS    TEXT='Enter Authorization Number (4 numerics)',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(6,10),SF=(PROT),TEXT='or you may Hit',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SF=(PROT),TEXT='to escape or',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-3',MF=L
        $FS    SF=(PROT),TEXT='to exit (emergency)',MF=L
        $FS    SBA=(17,1),SF=(PROT,INT),TEXT='Mandatory :',MF=L
        $FS    SF=(PROT),TEXT='when creating or updating ',MF=L
        $FS    TEXT='an Authorization be carefull the',MF=L
        $FS    SBA=(18,14),TEXT='Programmers Numbers you set ',MF=L
        $FS    TEXT='in are already defined. This means',MF=L
        $FS    SBA=(19,14),TEXT='you must first be sure ',MF=L
        $FS    TEXT='these exist, and then, you may work',MF=L
        $FS    SBA=(20,14),TEXT='on the Authorization.',MF=L
        $FS    SBA=(23,1),SF=(PROT,INT),MF=L
ECRAUM  $FS    TEXT=(' ',78),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
LECRAU   EQU   *-ECRAU
         SPACE 1
ECRAW   $FS    CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,25),SF=(PROT,INT),MF=L
        $FS    TEXT='AUTHORIZATION     N U M B E R',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,25),SF=(PROT,INT),MF=L
        $FS    TEXT='=============================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(5,10),SF=(PROT),MF=L
        $FS    TEXT='Authorization NOT FOUND in file.',MF=L
        $FS    SBA=(6,10),SF=(PROT),TEXT='Enter',MF=L
        $FS    SF=(PROT,INT),TEXT='Y',MF=L
        $FS    SF=(PROT),TEXT='if you are creating a new one',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),TEXT=' ',MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(7,10),SF=(PROT),TEXT='or you may Hit',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SF=(PROT),TEXT='to escape or',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-3',MF=L
        $FS    SF=(PROT),TEXT='to exit (emergency)',MF=L
        $FS    SBA=(23,1),SF=(PROT,INT),MF=L
ECRAWM  $FS    TEXT=(' ',78),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
LECRAW   EQU   *-ECRAW
         SPACE 1
ECRAN   $FS    CC=W,WCC=(KBR),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,25),SF=(PROT,INT),MF=L
        $FS    TEXT='AUTHORIZATION     U P D A T E',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,25),SF=(PROT,INT),MF=L
        $FS    TEXT='=============================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(3,1),SF=(PROT),MF=L
        $FS    TEXT='Authorization state/action :',MF=L
        $FS    SF=(PROT,INT),TEXT='I',MF=L
        $FS    SF=(PROT),TEXT='= invalidate,',MF=L
        $FS    SF=(PROT,INT),TEXT='D',MF=L
        $FS    SF=(PROT),TEXT='= delete or',MF=L
        $FS    SF=(PROT,INT),TEXT='X',MF=L
        $FS    SF=(PROT),TEXT='to escape',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),MF=L
SBASI   $FS    SBA=(3,78),MF=L
SCRSI   $FS    TEXT=' ',MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(4,1),SF=(PROT),TEXT='Authorization Number :',MF=L
        $FS    SF=(PROT,INT),MF=L
SCRAUT  $FS    TEXT=(' ',4),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(4,41),SF=(PROT),TEXT='Fiche Programme :',MF=L
        $FS    SF=(INT),MF=L
SBAFICH $FS    SBA=(4,60),MF=L
SCRFICH $FS    TEXT=(' ',10),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(5,1),SF=(PROT),TEXT='DATE start :',MF=L
        $FS    SF=(INT),MF=L
SBADTEI $FS    SBA=(5,15),MF=L
SCRDTEI $FS    TEXT='  /  /    ',MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(5,25),SF=(PROT),MF=L
        $FS    TEXT='Responsible Programmer :',MF=L
        $FS    SF=(INT),MF=L
SBAPR   $FS    SBA=(5,51),MF=L
SCRPR   $FS    TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(5,60),SF=(PROT),MF=L
        $FS    TEXT='Priority (0-9) :',MF=L
        $FS    SF=(INT),MF=L
SBAPI   $FS    SBA=(5,78),MF=L
SCRPI   $FS    TEXT=' ',MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(6,6),SF=(PROT),TEXT='  end :',MF=L
        $FS    SF=(INT),MF=L
SBADTEO $FS    SBA=(6,15),MF=L
SCRDTEO $FS    TEXT='  /  /    ',MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(6,25),SF=(PROT),TEXT='Resp. Name :',MF=L
SCRNOA  $FS    SF=(PROT),MF=L
SCRNOM  $FS    TEXT=(' ',40),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(7,1),SF=(PROT,INT),MF=L
MSG1    $FS    TEXT=(' ',37),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(7,41),SF=(PROT,INT),MF=L
MSG2    $FS    TEXT=(' ',37),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(8,1),SF=(PROT),MF=L
        $FS    TEXT='Programmers for this Authorization :',MF=L
        $FS    SBA=(9,3),SF=(PROT),TEXT='----> ',MF=L
        $FS    SF=(PROT,INT),TEXT='I',MF=L
        $FS    SF=(PROT),TEXT='= invalidate,',MF=L
        $FS    SF=(PROT,INT),TEXT='D',MF=L
        $FS    SF=(PROT),TEXT='= delete',MF=L
        $FS    SBA=(9,43),SF=(PROT),TEXT='----> ',MF=L
        $FS    SF=(PROT,INT),TEXT='I',MF=L
        $FS    SF=(PROT),TEXT='= invalidate,',MF=L
        $FS    SF=(PROT,INT),TEXT='D',MF=L
        $FS    SF=(PROT),TEXT='= delete',MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 01 -*
SX       EQU   *
        $FS    SBA=(10,1),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
SBAN1   $FS    SBA=(10,4),MF=L
NUM1    $FS    TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
SBAA1    EQU   *-SBAN1
        $FS    SBA=(10,11),MF=L
ASTER1  $FS    TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
NATT1   $FS    SF=(PROT),MF=L
NAME1   $FS    TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
DX       EQU   *-SX
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 02 -*
        $FS    SBA=(10,41),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(10,44),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(10,51),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 03 -*
        $FS    SBA=(11,1),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(11,4),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(11,11),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 04 -*
        $FS    SBA=(11,41),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(11,44),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(11,51),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 05 -*
        $FS    SBA=(12,1),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(12,4),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(12,11),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 06 -*
        $FS    SBA=(12,41),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(12,44),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(12,51),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 07 -*
        $FS    SBA=(13,1),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(13,4),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(13,11),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 08 -*
        $FS    SBA=(13,41),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(13,44),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(13,51),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 09 -*
        $FS    SBA=(14,1),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(14,4),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(14,11),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 10 -*
        $FS    SBA=(14,41),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(14,44),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(14,51),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 11 -*
        $FS    SBA=(15,1),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(15,4),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(15,11),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 12 -*
        $FS    SBA=(15,41),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(15,44),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(15,51),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 13 -*
        $FS    SBA=(16,1),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(16,4),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(16,11),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 14 -*
        $FS    SBA=(16,41),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(16,44),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(16,51),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 15 -*
        $FS    SBA=(17,1),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(17,4),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(17,11),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 16 -*
        $FS    SBA=(17,41),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(17,44),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(17,51),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 17 -*
        $FS    SBA=(18,1),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(18,4),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(18,11),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 18 -*
        $FS    SBA=(18,41),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(18,44),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(18,51),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 19 -*
        $FS    SBA=(19,1),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(19,4),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(19,11),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 20 -*
        $FS    SBA=(19,41),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(19,44),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(19,51),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 21 -*
        $FS    SBA=(20,1),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(20,4),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(20,11),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 22 -*
        $FS    SBA=(20,41),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(20,44),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(20,51),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 23 -*
        $FS    SBA=(21,1),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(21,4),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(21,11),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 24 -*
        $FS    SBA=(21,41),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(21,44),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(21,51),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
*- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 25 -*
        $FS    SBA=(22,1),SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(22,4),TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='>',MF=L
        $FS    SF=(INT),MF=L
        $FS    SBA=(22,11),TEXT=' ',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SF=(PROT),TEXT=(' ',25),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(22,42),SF=(PROT,INT),MF=L
ECRANM  $FS    TEXT=(' ',38),MF=L
        $FS    SBA=(23,1),SF=(PROT),MF=L
        $FS    TEXT='Type in your changes and hit',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SF=(PROT),TEXT='until they are correct, ',MF=L
        $FS    TEXT='then hit only',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SBA=(24,1),SF=(PROT),MF=L
        $FS    TEXT='to execute the update, or use at any time',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-3',MF=L
        $FS    SF=(PROT),TEXT='to exit (emergency)',MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
LECRAN   EQU   *-ECRAN
         SPACE 1
ECRAD   $FS    CC=W,WCC=(KBR),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,25),SF=(PROT,INT),MF=L
        $FS    TEXT='AUTHORIZATION     D E L E T E',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,25),SF=(PROT,INT),MF=L
        $FS    TEXT='=============================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(10,10),SF=(PROT),TEXT='Confirm',MF=L
        $FS    SF=(PROT,INT),TEXT='DELETE',MF=L
        $FS    SF=(PROT),TEXT='action on Authorization number :',MF=L
        $FS    SF=(PROT,INT),,MF=L
ECRADA  $FS    TEXT=(' ',4),MF=L
        $FS    SBA=(15,10),SF=(PROT),TEXT='Enter',MF=L
        $FS    SF=(PROT,INT),TEXT='Y',MF=L
        $FS    SF=(PROT),TEXT='or',MF=L
        $FS    SF=(PROT,INT),TEXT='N ===>',MF=L
        $FS    SF=(INT),TEXT=' ',MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(16,10),SF=(PROT),TEXT='or you may Hit',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SF=(PROT),TEXT='to escape or',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-3',MF=L
        $FS    SF=(PROT),TEXT='to exit (emergency)',MF=L
        $FS    SBA=(23,1),SF=(PROT,INT),MF=L
ECRADM  $FS    TEXT=(' ',78),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
LECRAD   EQU   *-ECRAD
         EJECT
*--------------------------------------------------------------------*
*        LITERAL POOL                                                *
*--------------------------------------------------------------------*
         SPACE 1
        LTORG
         EJECT
*--------------------------------------------------------------------*
*        UJVTABLE (IEFUJV) EXTENDED CONTROLS                         *
*--------------------------------------------------------------------*
         SPACE 1
         CNOP  0,8
         USING *,R15
EXTCTL   STM   R8,R9,SVEXT
         LR    R9,R15
         DROP  R15
         USING EXTCTL,R9
         LA    R8,2(R14)
         XR    R1,R1
         ICM   R1,B'0011',0(R14)
         B     0(R1,R9)
CHKINA   XC    DSD,DSD
        CALL   AUT33,(ARGUMAUT,DSD,DSD),VL
         LTR   R15,R15
         BNZ   *+L'*+8
         LR    R14,R8                  ACCEPTED
         LM    R8,R9,SVEXT
         BR    R14
         CH    R15,=H'8'
         BE    CHKERR
         MVC   ECRANM(27),=CL27'Aut. INVALIDATION rejected.'
         MVC   SCRSI,INSI
         B     CHKMSG
CHKAPN   L     R1,SVEXT
         PACK  DSD,0(4,R1)
         CVB   R0,DSD
         XC    DSD,DSD
         STH   R0,DSD
        CALL   AUT33,(ARGUMAUT,DSD,DSD+2),VL
         LTR   R15,R15
         BNZ   *+L'*+8
         LR    R14,R8                  ACCEPTED
         LM    R8,R9,SVEXT
         BR    R14
         CH    R15,=H'8'
         BE    CHKERR
         L     R1,SVEXT
         CLI   4(R1),C'D'
         BE    *+L'*+10
         MVC   ECRANM(33),=CL33'Programmer INVALIDATION rejected.'
         B     *+L'*+6
         MVC   ECRANM(29),=CL29'Programmer DELETION rejected.'
         MVI   4(R1),C' '
         B     CHKMSG
CHKDLA   XC    DSD,DSD
        CALL   AUT33,(ARGUMAUT,DSD,DSD),VL
         LTR   R15,R15
         BNZ   *+L'*+8
         LR    R14,R8                  ACCEPTED
         LM    R8,R9,SVEXT
         BR    R14
         CH    R15,=H'8'
         BE    CHKERR
         MVC   ECRANM(23),=CL23'Aut. DELETION rejected.'
         MVC   SCRSI,INSI
CHKMSG   MVC   MSG1(34),=CL34'UJVTABLE (IEFUJV) control detected'
         MVC   MSG2(33),=CL33'* * * System HIGH-SENSITIVE * * *'
         B     DSPLAY
CHKERR   LM    R2,R3,NCALL             SEVERE ERROR
         B     SCRAMBLE
         DROP  R9
         SPACE 1
*--------------------------------------------------------------------*
*        CONSTANTS AND WORK-AREAS                                    *
*--------------------------------------------------------------------*
         SPACE 1
SVEXT    DC    2F'0'
NCALL    DC    0F'0',CL8'AUT30'
         SPACE 1
*--------------------------------------------------------------------*
*        LITERAL POOL                                                *
*--------------------------------------------------------------------*
         SPACE 1
        LTORG
         EJECT
*--------------------------------------------------------------------*
*        DUMMY SECTIONS                                              *
*--------------------------------------------------------------------*
         SPACE 1
         PRINT GEN
        $AIRD  SEC=YES,R=NO
         SPACE 2
         END
