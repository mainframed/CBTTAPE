AU98     TITLE '-- ROUTINE AUT98 - CONTROL CALLER SERVICE --'
AUT98    START 0
         SPACE 1
**********************************************************************
*        THIS SUBROUTINE CONTROL USERID CALLER UNAUTHORIZED          *
*        ACCESS TO THE CP.                                           *
*        THEN ALLOWS SPECIAL USERS TO ALLOCATE THE ACCOUNTING        *
*        AND TSO-UADS DATA-SETS, AND EVENTUALLY SET THE HARDCOPY     *
*        DESTINATION.                                                *
*                                  (MOINIL P.A. - 21 SEPTEMBER 1989) *
**********************************************************************
*        CALL AUT98,(PARM),VL                                        *
*        FUNCTION : CONTROL USER ACCESS.                             *
*                   NOTE - IF A LEVEL OF AUTHORITY IS REQUESTED BY   *
*                          THE CALLING MODULE, SET IT BEFORE THE     *
*                          CALL IN THE BYTE LOCATED AT 'AUT98T+1'.   *
*        AT ENTRY : R1  = TSO COMMAND OR SUB-COMMAND ENTRY REGISTER  *
*                   R0  = 0 MEANS NO HARDCOPY TO BE DONE             *
*                         ANYTHING ELSE = REQUEST HARDCOPY           *
*         AT EXIT : R15 = 0 - NORMAL COMPLETION                      *
*                         4 - UNAUTHORIZED USERID OR ERROR DETECTED  *
*                             (MESSAGE SENT)                         *
*--------------------------------------------------------------------*
         SPACE 1
*---------------------------------------------------------------------*
*        AUTHORITY LEVELS DEFINITIONS.                                *
*---------------------------------------------------------------------*
         SPACE 1
LV15     EQU   15        PRIVILEGED LEVEL - WIZARD
LV12     EQU   12        ADMINISTRATOR CONTROL LEVEL
LV10     EQU   10        REDUCED SERVICES LEVEL
LV5      EQU   5         DISPLAY SERVICES LEVEL
LV0      EQU   0         REJECT LEVEL
         SPACE 1
        $MDL@IX
         EJECT
        $UJVT
         EJECT
         PRINT NOGEN
        $DEFREG
        $XENT  BASE=R10
         SPACE 1
         LR    R2,R1                   RETAIN R1 ENTRY ADDRESS
         LR    R5,R0                   RETAIN R0 VALUE
         SPACE 1
*--------------------------------------------------------------------*
*        CHECK USER AUTHORITY : SYSTEM, OPERATOR, ...                *
*              THESE TESTS DON'T INCLUDE RACF PROTECTION ON          *
*              DATA-SETS, WHICH IS CONTROLED AT OPEN TIME IF         *
*              INSTALLED IN THE SYSTEM.                              *
*--------------------------------------------------------------------*
         SPACE 1
        EXTRACT ATIOT,'S',FIELDS=(TIOT,TSO,PSB)
         L     R1,ATSO             CHECK FOR PRIVILEDGE
         TM    0(R1),X'80'
         BZ    NOTTSO              NOT TSO ADDRESS SPACE
         L     R1,APSCB            PSCB ADDRESS
         LTR   R1,R1
         BZ    NOTTSO              WE ARE'NT IN TSO
         USING CPPL,R2             SET CPPL ADDRESSABILITY
         USING PSCB,R1
         CLC   PSCBUPT,CPPLUPT     CPPL+4 = UPT ADDRESS?
         BNE   *+L'*+10            NO
         DROP  R1
         CLC   APSCB(L'APSCB),CPPLPSCB  CPPL+8 = PSCB ADDRESS?
         BE    OK                  YES, TSO COMMAND
         LR    R1,R2
         USING IKJEBECA,R1
         L     R2,CAPTTMP          CPPL ADDRESS
         DROP  R1
         CLC   APSCB(L'APSCB),CPPLPSCB  CPPL+8 = PSCB ADDRESS?
         BNE   NOTCMD              NO, NOT TSO SUB-COMMAND
         DROP  R2
OK      $EACM  REQAUT
         CLI   AUT98T,LV0
         BE    NOTAUT
         CLC   AUT98T,CRQAUT       REQUESTED AUTHORITY LEVEL ?
         BH    TTEST               IF HIGH, OK
*------- UNAUTHORIZED USER FOR THIS COMMAND PROCESSOR.
NOTAUT  TPUT   ERRORA,L'ERRORA         UNAUTHORIZED TO USE CP
XBACK    LA    R15,4                   SET RC TO END OF CP
         B     LEAVE
*------- ONLY TSO COMMAND OR SUB-COMMAND PROCESSOR.
NOTCMD  TPUT   ERRORB,L'ERRORB
         B     XBACK
NOTTSO  WTO    '*** ONLY "TSO" COMMAND PROCESSOR ALLOWED *** ',        1
               ROUTCDE=11,DESC=7
         B     XBACK
*------- UJVTABLE (IEFUJV) NOT FOUND.
UJVTERR TPUT   ERRORC,L'ERRORC
         B     XBACK
TTEST    L     R4,APSCB                ADDRESS OF PSCB
         USING PSCB,R4
         MVC   USNME(L'PSCBUSER),PSCBUSER
         CLI   AUT98T,LV10             REDUCED SERVICES ?
         BNH   NOSNAP+L'NOSNAP         YES
         CLI   AUT98T,LV12             WIZARD LEVEL ?
         BNH   NOSNAP                  NO
*------- ACTIVE INPUT BUFFER SNAP IF 'SYSSNAP' DD PRESENT.
         XR    R1,R1
         L     R2,ATIOT
         USING TIODSECT,R2
SEARCH   CLC   TIOELNGH(4),=F'0'
         BE    NOSNAP
         CLC   TIOEDDNM(L'TIOEDDNM),=CL8'SYSSNAP'
         BE    *+L'*+10
         IC    R1,TIOELNGH
         ALR   R2,R1
         B     SEARCH
         DROP  R2
         L     R1,=V(AUT99S)
         MVI   0(R1),X'FF'             OPEN SNAP WAY
*------- ALLOW ALTERNATE DATA-SETS NAMES FOR TEST.
NOSNAP   NI    SWRDS+1,X'0F'
         L     R15,=V(SUJVTA)
         BASR  R14,R15
         LTR   R15,R15
         BZ    GETNME
         TM    SWRDS+1,X'F0'
         BO    UJVTERR
         MVC   ECRACC(L'ERRORC),ERRORC
         MVC   ECRANM(L'ERROR8),ERROR8
         B     FILLSC
         USING UVTDSECT,R1
GETNME   L     R2,ACCNME
         DROP  R1
         MVC   AUT98A(L'AUT98A),0(R2)   GET ACC. DATA-SET DEFAULT NAME
         L     R15,=V(DUJVTA)
         BASR  R14,R15
SWRDS    B     QUIT
         MVC   ECRACC,AUT98A
FILLSC   MVC   ECRTSO,AUT98U
         MVC   ACCNPR(L'PSCBUSER),PSCBUSER
         MVC   UADSPR(L'PSCBUSER),PSCBUSER
         LTR   R5,R5
         BNZ   DSPSDF
         MVC   PECRAN,=A(LECRAS)
         B     DSPLAY
DSPSDF   MVC   DFDEST,HCDFDN
         MVC   DFCLSS,HCDFCL
DSPLAY   LTR   R5,R5
         BNZ   *+L'*+6
         MVC   ECRANS(LECRAT),ECRANT
         LA    R1,PECRAN               DISPLAY SCREEN
         L     R15,=V(AUT99)
         BALR  R14,R15
         LTR   R15,R15                 NORMAL COMPLETION ?
         BNZ   LEAVE                   NO, END CP (ONLY 4)
         MVI   ECRANM,C' '
         MVC   ECRANM+1(L'ECRANM-1),ECRANM
NXTFLD   L     R15,=V(AUT99R)          ASK REPLY TEXT
         BALR  R14,R15
         LTR   R15,R15                 NONE OR END?
         BZ    SWPRC                   YES
         NI    SWPRC+1,X'0F'           NO, SOMETHING ENTERED
         CLC   0(L'ASBAPR,R15),ASBAPR
         BNE   *+L'*+8
         LA    R2,ACCNPR
         B     SETFLD
         CLC   0(L'ASBAGR,R15),ASBAGR
         BNE   *+L'*+8
         LA    R2,ACCNGR
         B     SETFLD
         CLC   0(L'ASBATY,R15),ASBATY
         BNE   *+L'*+8
         LA    R2,ACCNTY
         B     SETFLD
         CLC   0(L'USBAPR,R15),USBAPR
         BNE   *+L'*+8
         LA    R2,UADSPR
         B     SETFLD
         CLC   0(L'USBAGR,R15),USBAGR
         BNE   *+L'*+8
         LA    R2,UADSGR
         B     SETFLD
         CLC   0(L'USBATY,R15),USBATY
         BNE   *+L'*+8
         LA    R2,UADSTY
         B     SETFLD
         CLC   0(L'DSBADS,R15),DSBADS
         BNE   *+L'*+8
         LA    R2,DESTNM
         B     SETFLD
         CLC   0(L'DSBASY,R15),DSBASY
         BE    SETCLSS
         MVC   ECRANM(L'ERROR1),ERROR1
         B     DSPLAY
SETCLSS  MVI   DESTSY,C' '
         LTR   R1,R1
         BZ    NXTFLD                  NONE, FIELD HAS BEEN ERASED
         OC    DESTSY(1),3(R15)
         CLI   DESTSY,C' '
         BE    NXTFLD
         CLI   DESTSY,C'A'
         BL    ERRCLSS
         CLI   DESTSY,C'I'
         BNH   NXTFLD
         CLI   DESTSY,C'J'
         BL    ERRCLSS
         CLI   DESTSY,C'R'
         BNH   NXTFLD
         CLI   DESTSY,C'S'
         BL    ERRCLSS
         CLI   DESTSY,C'Z'
         BNH   NXTFLD
         CLI   DESTSY,C'0'
         BL    ERRCLSS
         CLI   DESTSY,C'9'
         BNH   NXTFLD
ERRCLSS  MVC   ECRANM(L'ERROR7),ERROR7
         B     DSPLAY
SETFLD   MVC   0(8,R2),=CL8' '         CLEAR FIELD
         LTR   R1,R1
         BZ    NXTFLD                  NONE, FIELD HAS BEEN ERASED
         LA    R0,8                    PROCESS FIELD
         CLR   R1,R0
         BNH   *+L'*+2
         LR    R1,R0
         BCT   R1,*+L'*+6
         OC    0(*-*,R2),3(R15)        TRANSLATE LOWER CASE IN UPPER
         EX    R1,*-6
         B     NXTFLD
SWPRC    B     CHKDFA
         CLI   ACCNPR,C' '
         BNE   PRCSSA
         CLI   ACCNGR,C' '
         BNE   *+L'*+8
         CLI   ACCNTY,C' '
         BE    PRCSST
         MVC   ECRANM(L'ERROR2),ERROR2
         B     DSPLAY
PRCSSA   CLI   ACCNGR,C' '
         BNE   PRCSST
         CLI   ACCNTY,C' '
         BE    *+L'*+10
         MVC   ECRANM(L'ERROR3),ERROR3
         B     DSPLAY
         CLC   ACCNPR(L'PSCBUSER),PSCBUSER
         BNE   PRCSST
         MVC   ACCNPR(L'ACCNPR),=CL8' '
PRCSST   CLI   UADSPR,C' '
         BNE   PRCSSU
         CLI   UADSGR,C' '
         BNE   *+L'*+8
         CLI   UADSTY,C' '
         BE    PRCSSX
         MVC   ECRANM(L'ERROR4),ERROR4
         B     DSPLAY
PRCSSU   CLI   UADSGR,C' '
         BNE   PRCSSX
         CLI   UADSTY,C' '
         BE    *+L'*+10
         MVC   ECRANM(L'ERROR5),ERROR5
         B     DSPLAY
         CLC   UADSPR(L'PSCBUSER),PSCBUSER
         BNE   PRCSSX
         MVC   UADSPR(L'UADSPR),=CL8' '
PRCSSX   CLC   DESTNM(L'DESTNM),=CL8' '
         BE    PRCSSY
         CLI   DESTNM,C'A'
         BL    DSTERR
         CLI   DESTNM,C'I'
         BNH   PRCSSY
         CLI   DESTNM,C'J'
         BL    DSTERR
         CLI   DESTNM,C'R'
         BNH   PRCSSY
         CLI   DESTNM,C'S'
         BL    DSTERR
         CLI   DESTNM,C'Z'
         BNH   PRCSSY
DSTERR   MVC   ECRANM(L'ERROR6),ERROR6
         B     DSPLAY
         DROP  R4
PRCSSY   MVC   AUT98D,DESTNM
         MVC   AUT98D+L'AUT98D(L'DESTSY),DESTSY
         CLI   ACCNPR,C' '
         BE    PRCSSZ
         LA    R2,WCL44                BUILD NEW ACC. DS
         MVI   0(R2),C' '
         MVC   1(L'WCL44-1,R2),0(R2)
         MVC   0(L'ACCNPR,R2),ACCNPR
         CLI   ACCNGR,C' '
         BE    MVACC
         LA    R2,L'ACCNPR-1(R2)
         CLI   0(R2),C' '
         BNE   *+L'*+4
         BCT   R2,*-8
         MVI   1(R2),C'.'
         MVC   2(L'ACCNGR,R2),ACCNGR
         CLI   ACCNTY,C' '
         BE    MVACC
         LA    R2,L'ACCNGR+1(R2)
         CLI   0(R2),C' '
         BNE   *+L'*+4
         BCT   R2,*-8
         MVI   1(R2),C'.'
         MVC   2(L'ACCNTY,R2),ACCNTY
MVACC    CLC   AUT98A,WCL44
         BE    PRCSSZ
         MVC   AUT98A,WCL44
         OI    DSSWST,FLACC
PRCSSZ   CLI   UADSPR,C' '
         BE    CHKDFA
         LA    R2,WCL44                BUILD NEW TSO-UADS DS
         MVI   0(R2),C' '
         MVC   1(L'WCL44-1,R2),0(R2)
         MVC   0(L'UADSPR,R2),UADSPR
         CLI   UADSGR,C' '
         BE    MVTSO
         LA    R2,L'UADSPR-1(R2)
         CLI   0(R2),C' '
         BNE   *+L'*+4
         BCT   R2,*-8
         MVI   1(R2),C'.'
         MVC   2(L'UADSGR,R2),UADSGR
         CLI   UADSTY,C' '
         BE    MVTSO
         LA    R2,L'UADSGR+1(R2)
         CLI   0(R2),C' '
         BNE   *+L'*+4
         BCT   R2,*-8
         MVI   1(R2),C'.'
         MVC   2(L'UADSTY,R2),UADSTY
MVTSO    CLC   AUT98U,WCL44
         BE    CHKDFA
         MVC   AUT98U,WCL44
         OI    DSSWST,FLTSO
CHKDFA   CLI   AUT98A,C' '
         BNE   QUIT
         MVC   ECRANM(L'ERROR9),ERROR9
         B     DSPLAY
QUIT     XR    R15,R15
LEAVE   $XRET  CC=(R15)                RETURN
         EJECT
*--------------------------------------------------------------------*
*        ALTERNATE DATA-SETS NAMES                                   *
*--------------------------------------------------------------------*
         SPACE 1
         DS    0F                      ALIGNMENT
         ENTRY AUT98A
AUT98A   DC    CL44' '                 ACC. DATA-SET DEFAULT NAME
         ENTRY AUT98U
AUT98U   DC    CL44'SYS1.UADS'         TSO-UADS DATA-SET DEFAULT NAME
         SPACE 1
*--------------------------------------------------------------------*
*        HARDCOPY INFORMATION.                                       *
*--------------------------------------------------------------------*
         SPACE 1
         DS    0F                      ALIGNMENT
         ENTRY AUT98D
AUT98D   DC    CL8' ',CL1' '           DESTINATION/CLASS
USNME    DC    CL8' '                  USER
         SPACE 1
*--------------------------------------------------------------------*
*        USER-ID AUTHORITY LEVEL, FLAGS, HARDCOPY DEFAULTS.          *
*--------------------------------------------------------------------*
         SPACE 1
         DS    0F                      ALIGNMENT
         ENTRY AUT98T
AUT98T   DC    XL1'0'                  AUTHORITY LEVEL (0-15)
CRQAUT   DC    XL1'0'                  REQUEST AUTHORITY LEVEL (0-15)
DSSWST   DC    XL1'0'                  DATA-SET(S) SWITCH STATUS
FLACC    EQU   X'80'                   ACCOUNT D.S. CHANGED
FLTSO    EQU   X'40'                   TSO-UADS D.S. CHANGED
HCDFCL   DC    CL1'A'                  DEFAULT SYSOUT CLASS
HCDFDN   DC    CL8'R41'                DEFAULT DESTINATION NAME
         EJECT
*--------------------------------------------------------------------*
*        VARIABLES DE TRAVAIL                                        *
*--------------------------------------------------------------------*
         SPACE 1
PECRAN   DC    A(LECRAN,ECRAN)
ATIOT    DC    A(*-*)
ATSO     DC    A(*-*)
APSCB    DC    A(*-*)
REQAUT   DC    0F'0',BL1'00000000',AL3(MDL@IX),AL4(AUT98T)
WCL44    DC    CL44' '
         SPACE 1
*--------------------------------------------------------------------*
*        MESSAGES D'ERREURS                                          *
*--------------------------------------------------------------------*
         SPACE 1
ERRORA   DC    C'*** Unauthorized access to this command *** '
ERRORB   DC    C'*** Only TSO command or sub-command allowed *** '
ERRORC   DC    C'*** UJVTABLE (IEFUJV) not found *** '
ERROR1   DC    C'Input scan SBA order not found ???'
ERROR2   DC    C'Accounting data-set name - PROJECT missing |'
ERROR3   DC    C'Accounting data-set name - GROUP missing |'
ERROR4   DC    C'TSO-UADS data-set name - PROJECT missing |'
ERROR5   DC    C'TSO-UADS data-set name - GROUP missing |'
ERROR6   DC    C'Hardcopy destination name error |'
ERROR7   DC    C'Hardcopy Sysout class error |'
ERROR8   DC    C'Unable to get Acc. data-set default name, try using Pr1
               oject/Group/Type'
ERROR9   DC    C'Define the Acc. data-set name using Project/Group/Type1
                please, or exit'
         EJECT
*--------------------------------------------------------------------*
*        ECRAN D'OUTPUT                                              *
*--------------------------------------------------------------------*
         SPACE 1
ECRAN   $FS    CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,25),SF=(PROT,INT),MF=L
        $FS    TEXT='TEST WITH ALTERNATE DATA-SETS',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,25),SF=(PROT,INT),MF=L
        $FS    TEXT='=============================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(4,1),SF=(PROT,INT),TEXT='Accounting',MF=L
        $FS    SF=(PROT),TEXT='data-set name :',MF=L
        $FS    SBA=(5,10),SF=(PROT),TEXT='Project',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),MF=L
ASBAPR  $FS    SBA=(5,24),MF=L
ACCNPR  $FS    TEXT=(' ',8),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(6,10),SF=(PROT),TEXT='Group  ',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),MF=L
ASBAGR  $FS    SBA=(6,24),MF=L
ACCNGR  $FS    TEXT=(' ',8),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(7,10),SF=(PROT),TEXT='Type   ',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),MF=L
ASBATY  $FS    SBA=(7,24),MF=L
ACCNTY  $FS    TEXT=(' ',8),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(9,3),SF=(PROT,INT),TEXT='TSO-UADS',MF=L
        $FS    SF=(PROT),TEXT='data-set name :',MF=L
        $FS    SBA=(10,10),SF=(PROT),TEXT='Project',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),MF=L
USBAPR  $FS    SBA=(10,24),MF=L
UADSPR  $FS    TEXT=(' ',8),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(11,10),SF=(PROT),TEXT='Group  ',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),MF=L
USBAGR  $FS    SBA=(11,24),MF=L
UADSGR  $FS    TEXT=(' ',8),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(12,10),SF=(PROT),TEXT='Type   ',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),MF=L
USBATY  $FS    SBA=(12,24),MF=L
UADSTY  $FS    TEXT=(' ',8),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(14,1),SF=(PROT,INT),TEXT='Change :',MF=L
        $FS    SF=(PROT),MF=L
        $FS    TEXT='type in any alternate data-sets names you ',MF=L
        $FS    TEXT='want to use or just ENTER.',MF=L
        $FS    SBA=(15,1),SF=(PROT),MF=L
        $FS    TEXT='By default the data-sets names are :',MF=L
        $FS    SBA=(16,10),SF=(PROT),TEXT='Accounting =',MF=L
        $FS    SF=(PROT,INT),MF=L
ECRACC  $FS    TEXT=(' ',44),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(17,12),SF=(PROT),TEXT='TSO-UADS =',MF=L
        $FS    SF=(PROT,INT),MF=L
ECRTSO  $FS    TEXT=(' ',44),MF=L
        $FS    SF=(PROT),MF=L
LECRAS   EQU   (*-ECRAN)+LECRAT
ECRANS  $FS    SBA=(19,1),SF=(PROT,INT),TEXT='Hardcopy :',MF=L
        $FS    SF=(PROT),TEXT='Destination name',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),MF=L
DSBADS  $FS    SBA=(19,35),MF=L
DESTNM  $FS    TEXT=(' ',8),MF=L
        $FS    SF=(PROT),TEXT='and Sysout class',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),MF=L
DSBASY  $FS    SBA=(19,66),MF=L
DESTSY  $FS    TEXT=' ',MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(20,23),SF=(PROT),TEXT='Default is ',MF=L
DFDEST  $FS    TEXT=(' ',8),MF=L
        $FS    SBA=(20,54),SF=(PROT),TEXT='Default is ',MF=L
DFCLSS  $FS    TEXT=' ',MF=L
        $FS    SBA=(21,1),SF=(PROT,INT),TEXT='Note :',MF=L
        $FS    SF=(PROT),TEXT='any change to the Hardcopy is ',MF=L
        $FS    TEXT='handled only if the Accounting data-set',MF=L
        $FS    SBA=(22,8),SF=(PROT),MF=L
        $FS    TEXT='is an alternate data-set.',MF=L
ECRANT  $FS    SBA=(23,1),SF=(PROT,INT),MF=L
ECRANM  $FS    TEXT=(' ',78),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
LECRAT   EQU   *-ECRANT
LECRAN   EQU   *-ECRAN
         EJECT
*--------------------------------------------------------------------*
*        LITERAL POOL                                                *
*--------------------------------------------------------------------*
         SPACE 1
        LTORG
         SPACE 1
*--------------------------------------------------------------------*
*        DUMMY SECTIONS                                              *
*--------------------------------------------------------------------*
         SPACE 1
        IKJCPPL
        IKJPSCB
        IKJEBECA
TIODSECT DSECT
        IEFTIOT1
         SPACE 2
         END
