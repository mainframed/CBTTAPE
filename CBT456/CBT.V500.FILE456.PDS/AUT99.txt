AU99     TITLE '-- ROUTINE AUT99 - FULLSCREEN SERVICE --'
AUT99    START 0
         SPACE 1
**********************************************************************
*        A ROUTINE TO ALLOW A MORE EASIER WAY OF FULLSCREEN          *
*        DISPLAY MANAGEMENT.                                         *
*                                  (MOINIL P.A. - 21 SEPTEMBER 1989) *
**********************************************************************
*     1. CALL AUT99                                                  *
*        FUNCTION : ROUTINE TO WRITE SCREEN VIA TPUT,                *
*                   AND TO GET REPLY VIA TGET.                       *
*        AT ENTRY : R1  = PARM. ADDRESS                              *
*                         PARM. --->   A(SCREEN IMAGE LENGTH)        *
*                                      A(SCREEN IMAGE ADDRESS)       *
*                   IF R1  = A(X'80000000'+PARM.ADDRESS) THEN THE    *
*                              GET REPLY IS SKIPPED (ONLY TPUT DONE) *
*              OR : R1 = 0 TO TERMINATE/EXIT FULLSCREEN MODE.        *
*         AT EXIT : R15 =  0 - NORMAL COMPLETION (TEXT AT DISPOSAL)  *
*                              (R1 = ADDRESS OF AID-BYTE FOLLOWED    *
*                              BY THE 2-BYTES CURSOR ADDRESS)        *
*                          4 - END REQUESTED                         *
*                          8 - NOT SCREEN TERMINAL OR NOT SCREEN     *
*                              SIZED 24 * 80 (MESSAGE SENT).         *
*     2. CALL AUT99R                                                 *
*        FUNCTION : ROUTINE TO PROCESS THE REPLY TEXT                *
*                   OBTAINED VIA TGET.                               *
*        AT ENTRY : NONE                                             *
*         AT EXIT : R15 =  0 - COMPLETION OF REPLY TEXT (NO MORE)    *
*              OR : R15 =  ADDRESS OF SBA (SET BUFFER ADDRESS ORDER, *
*                          1-BYTE) FOLLOWED BY THE BUFFER ADDRESS    *
*                          (2-BYTES), FOLLOWED BY THE TEXT READ      *
*                          (R1 = LENGTH OF TEXT, 0 = NULL, NO TEXT)  *
*--------------------------------------------------------------------*
         SPACE 1
         PRINT NOGEN
        $DEFREG
        $XENT  BASE=R10
         SPACE 1
         NI    SWITCH,255-SKGET       INIT REQUEST
         LTR   R2,R1                   TEST AND RETAIN PARM ADDRESS
         BZ    QUIT                    BRANCH IF EXIT FULLSCREEN REQ.
         BP    *+L'*+8
         LA    R2,0(R2)
         OI    SWITCH,SKGET            SAY NO REPLY REQUESTED
         TM    SWITCH,SCRFT            WAS FIRST TIME ENTRY DONE?
         BO    GODONE                  BRANCH IF YES
         SPACE 1
*------- CHECK CALLER TERMINAL.
         SPACE 1
        GTSIZE ,                       GET ORIGINAL SCREEN SIZE
         LTR   R0,R0                   IS IT A SCREEN?
         BZ    NOTSCR                  IF ZERO, NOT A SCREEN
         CH    R0,=H'24'               STANDARD NUMBER OF LINES?
         BL    NOTSTD                  BRANCH IF LESS
         BE    TSTLNL                  BRANCH IF YES
         CH    R1,=H'80'               STANDARD LINE LENGTH?
         BL    NOTSTD                  BRANCH IF LESS
SETSSZ   ST    R0,SZSCRN               SAVE SCREEN SIZE
         ST    R1,SZLINE               SAVE LINE SIZE
         OI    SWITCH,SCRST            INDICATE SCREEN SIZE TO BE SET
         B     STRFS
TSTLNL   CH    R1,=H'80'               STANDARD LINE LENGTH?
         BL    NOTSTD                  BRANCH IF LESS
         BH    SETSSZ                  BRANCH IF MORE
STRFS   STFSMODE ON,INITIAL=YES        TURN ON VTAM FULL SCREEN MODE
         TM    SWITCH,SCRST            SCREEN SIZE TO BE SET?
         BZ    NOCHNG                  BRANCH IF NOT
        STSIZE SIZE=80,LINE=24         SET STANDARD SCREEN SIZE
         OI    SWITCH,SCRRS            INDICATE SCREEN SIZE TO BE RESET
NOCHNG   OI    SWITCH,SCRFT            SET FIRST TIME ENTRY DONE
         SPACE 1
*------- EXECUTE FULLSCREEN PROCESSING.
         SPACE 1
GODONE   LM    R0,R1,0(R2)             GET IMAGE INFO'S
         TM    SWITCH,SCRST+SCRRS      FIRST TPUT DONE?
         BNO   SNDWR                   BRANCH IF YES
        $FS    CC=EW,MF=(I,(R1))       SET ERASE/WRITE
SNDWR    ICM   R1,B'1000',TPUTFLG      INDICATE FULL SCREEN
        TPUT   (1),(0),R
         TM    SWITCH,SCRST+SCRRS      FIRST TPUT DONE?
         BNO   SNDGT                   BRANCH IF YES
         L     R1,4(R2)
        $FS    CC=W,MF=(I,(R1))        RESTORE WRITE
         NI    SWITCH,255-SCRST        SET FIRST TPUT DONE
SNDGT    TM    SWITCH,SKGET            NO REPLY REQUESTED ?
         BO    RETOK                   YES, EXIT
         LM    R4,R7,CLRRP             NO, PROCESS REPLY
         MVCL  R4,R6
         L     R4,CLRRP
         LA    R5,L'PFREPLY
         ALR   R4,R5
         ST    R4,AREPLY               REPLY START ADDRESS
        TGET   PFREPLY,REPLYL-1,ASIS
         SR    R1,R5
         BNM   *+L'*+2
         XR    R1,R1
         ST    R1,LREPLY               TRUE LENGTH REPLY
         CH    R15,=H'20'              TERMINAL DISCONNECTED?
         BE    ENDIT                   YES,... SPLIT
         CH    R15,=H'8'               ATTENTION?
         BE    ENDIT                   YES,... SPLIT
         CH    R15,=H'12'              IS INPUT LONGER THAN BUFFER?
         BNE   SKIPCLRQ                NO
        TCLEARQ INPUT                  FLUSH THE TRASH
SKIPCLRQ CLI   PFREPLY,X'F1'           PF 1 ENTERED?
         BE    HELP                    YES,... HELP
         CLI   PFREPLY,X'C1'           PF 13 ENTERED (ALT. PF 1)?
         BE    HELP                    YES,... HELP
         CLI   PFREPLY,X'F2'           PF 2 ENTERED?
         BE    STATS                   YES,... STATUS
         CLI   PFREPLY,X'C2'           PF 14 ENTERED (ALT. PF 2)?
         BE    STATS                   YES,... STATUS
         CLI   PFREPLY,X'F3'           PF 3 ENTERED?
         BE    ENDIT                   YES,... SPLIT
         CLI   PFREPLY,X'C3'           PF 15 ENTERED (ALT. PF 3)?
         BE    ENDIT                   YES,... SPLIT
         CLI   PFREPLY,X'6C'           PA 1 ENTERED?
         BE    ENDIT                   YES,... SPLIT
         CLI   PFREPLY,X'6E'           PA 2 ENTERED?
         BE    ENDIT                   YES,... SPLIT
         CLI   PFREPLY,X'6B'           PA 3 ENTERED?
         BE    ENDIT                   YES,... SPLIT
         CLI   PFREPLY,X'7D'           ENTER KEY DEPRESSED?
         BNE   GODONE                  NO,... IGNORE,... RE-DISPLAY
         CLI   AUT99S,X'FF'            YES,... BUFFER SNAP?
         BNE   NOSNAP                  NO
         STM   R0,R9,SVSNAP
SWSNAP   NOP   DOSNAP
         OI    SWSNAP+1,X'F0'          CLOSE THIS WAY
        OPEN   (SNAPDCB,OUTPUT)
         TM    SNAPDCB+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    OSERR                   SNAP OPEN ERROR
DOSNAP  SNAP   TCB='S',MF=(E,SNAPLST)
         LTR   R15,R15
         BZ    EXSNAP
        CLOSE  (SNAPDCB)               SNAP ERROR
OSERR    MVI   AUT99S,0                CLOSE SNAP WAY FOR EVER
EXSNAP   LM    R0,R9,SVSNAP
NOSNAP   L     R1,4(R13)
         MVC   24(4,R1),CLRRP          PASS ADDRESS OF AID ...
         B     RETOK
ENDIT    LA    R15,4                   END REQUESTED
         B     LEAVE
         EJECT
*------- DISPLAY STATUS OR HELP INFORMATION.
         SPACE 1
STATS    ICM   R15,B'1111',AUT99I      STATUS MODULE ?
         BZ    GODONE                  NO,... IGNORE,... RE-DISPLAY
         BASR  R14,R15                 YES, GO DO IT
         LTR   R15,R15                 COMPLETION ?
         BNZ   ENDIT                   ERROR, ...SPLIT
         STM   R0,R1,STPNL             OK, STORE STATUS PANEL INFO
         BAS   R8,DISPNL
         LM    R0,R1,STPNL
         B     DISPNL
         SPACE 1
HELP     ICM   R15,B'1111',AUT99H      HELP PANEL INFO'S TO BE SET ?
         BZ    GODONE                  NO,... BACK
         LM    R0,R1,0(R15)            YES
         LTR   R0,R0                   IS RESET TO DEFAULT REQUESTED ?
         BNZ   *+L'*+10                NO
         MVC   AUT99H,=A(DFHPNL)       YES, SET DEFAULT
         LM    R0,R1,DFHPNL
         STM   R0,R1,HLPNL             STORE HELP PANEL INFO'S
         BASR  R8,0                    DISPLAY HELP PANEL
         LM    R0,R1,HLPNL
DISPNL   ICM   R1,B'1000',TPUTFLG      INDICATE FULL SCREEN
        TPUT   (1),(0),R
         XC    RESPONSE,RESPONSE
        TGET   RESPONSE,L'RESPONSE,ASIS
         CH    R15,=H'20'              TERMINAL DISCONNECTED?
         BE    ENDIT                   YES,... SPLIT
         CH    R15,=H'8'               ATTENTION?
         BE    ENDIT                   YES,... SPLIT
         CH    R15,=H'12'              IS INPUT LONGER THAN BUFFER?
         BNE   SKPCLRQ                 NO
        TCLEARQ INPUT                  FLUSH THE TRASH
SKPCLRQ  CLI   RESPONSE,X'F3'          PF 3 ENTERED?
         BE    GODONE                  YES,... BACK
         CLI   RESPONSE,X'C3'          PF 15 ENTERED (ALT. PF 3)?
         BE    GODONE                  YES,... BACK
         CLI   RESPONSE,X'7D'          ENTER KEY DEPRESSED?
         BNER  R8                      NO,... IGNORE,... RE-DISPLAY
         B     GODONE                  YES,... BACK
         SPACE 1
*------- UNACCEPTABLE TERMINAL.
         SPACE 1
NOTSCR  TPUT   ERROR0,L'ERROR0         NOT SCREEN TERMINAL
         B     SEVERR
NOTSTD  TPUT   ERROR1,L'ERROR1         NOT SIZED 24 * 80
SEVERR   LA    R15,8                   SEVERE ERROR
         B     LEAVE
         EJECT
*------- PASS INPUT TO CALLER (FIELD BY FIELD).
         ENTRY AUT99R
        CNOP   0,8
         DROP  R10
         USING *,R15
AUT99R   STM   R14,R12,12(R13)         SAVE REGISTERS
         L     R10,=A(AUT99)           BASE ADDRESS
         DROP  R15
         USING AUT99,R10               RESTORE FULL ADDRESSABILITY
         LR    R2,R13                  PREVIOUS SAVE AREA
         LA    R13,32(R10)             CURRENT SAVE AREA
         ST    R13,8(R2)               LINK SAVE AREAS
         ST    R2,4(R13)
         LM    R15,R0,AREPLY
         LTR   R0,R0                   ANYTHING TO PASS?
         BNP   RETOK                   NO MORE ...
         CLI   0(R15),X'11'            SBA ORDER?
         BE    *+L'*+10                YES
         XC    LREPLY,LREPLY           NO, RESET ALL ...
         B     RETOK                   SAY END ...
         SH    R0,=H'3'                MINUS SBA + BUFFER ADDR.
         BP    *+L'*+10                BRANCH IF NOT END
         XC    24(4,R2),24(R2)         SET R1 = 0 FOR RETURN
         B     RESALL
         LA    R14,3(R15)              POINT TO TEXT
         XR    R1,R1
SRCSBA   CLI   0(R14),X'11'            SEARCH END OR NEXT SBA
         BE    NXTSBA
         LA    R1,1(R1)
         LA    R14,1(R14)
         BCT   R0,SRCSBA
         ST    R1,24(R2)               SET R1 = LENGTH FOR RETURN
RESALL   XC    LREPLY,LREPLY           RESET ALL ...
         B     LEAVE
NXTSBA   ST    R14,AREPLY
         ST    R0,LREPLY
         ST    R1,24(R2)               SET R1 = LENGTH FOR RETURN
         B     LEAVE
         EJECT
*------- TURN OFF FULL SCREEN (SPF),
*        CLEAR AND RESET CURSOR TO ROW1, COL1.
         SPACE 1
QUIT     TM    SWITCH,SCRFT            WAS FIRST TIME ENTRY DONE?
         BZ    GOEXIT                  BRANCH IF NOT
         TM    SWITCH,SCRRS            WAS SCREEN SIZE CHANGED?
         BZ    ENDCL                   BRANCH IF NOT
        STSIZE SIZELOC=SZLINE,LINELOC=SZSCRN RESTORE ORIGINAL SIZE
        $FS    CC=EWA,MF=(I,CLEAR)
ENDCL    LA    R1,CLEAR
         LA    R0,LCLEAR
         ICM   R1,B'1000',TPUTFLG      INDICATE FULL SCREEN
        TPUT   (1),(0),R
        STFSMODE OFF                   TURN OFF FULL SCREEN MODE
         SPACE 1
*------- THAT'S ALL FOLKS.
         SPACE 1
GOEXIT   MVI   SWITCH,0                RESET ALL
         XC    AUT99I,AUT99I
         MVC   AUT99H,=A(DFHPNL)
         CLI   AUT99S,X'FF'            YES,... BUFFER SNAP?
         BNE   RETOK                   NO
        CLOSE  (SNAPDCB)               CLOSE SNAP
         MVI   AUT99S,0                CLOSE SNAP WAY FOR EVER
RETOK    XR    R15,R15
LEAVE   $XRET  CC=(R15)
         SPACE 1
         DROP  R10
         EJECT
*--------------------------------------------------------------------*
*        CONSTANTS AND WORK-AREAS.                                   *
*--------------------------------------------------------------------*
         SPACE 1
SVSNAP   DC    10F'0'
         ENTRY AUT99H
AUT99H   DC    A(DFHPNL)     PF-1 KEY ADDRESS OF HELP PANEL INFO'S
HLPNL    DC    A(*-*,*-*)
         ENTRY AUT99I
AUT99I   DC    A(*-*)        PF-2 KEY ADDRESS OF STATUS PROCESS
STPNL    DC    A(*-*,*-*)
DFHPNL   DC    A(LECRAN,ECRAN)
SZLINE   DC    F'0'          LINE SIZE (LINE LENGTH)
SZSCRN   DC    F'0'          SCREEN SIZE (NUMBER OF LINES)
AREPLY   DC    F'0'          TGET - REPLY CURRENT ADDRESS
LREPLY   DC    F'0'          TGET - REPLY LENGTH
CLRRP    DC    A(PFREPLY,REPLYL,*+4,0)
         ENTRY AUT99S
AUT99S   DC    XL1'0'        INPUT BUFFER SNAP SWITCH (SET BY AUT98)
TPUTFLG  DC    X'03'         ITS FULL SCREEN TIME
SWITCH   DC    XL1'0'
SCRFT    EQU   X'80'         FIRST TIME ENTRY DONE
SKGET    EQU   X'40'         SKIP GET REPLY REQUESTED
SCRST    EQU   X'08'         SCREEN SIZE TO BE SET
SCRRS    EQU   X'04'         SCREEN SIZE TO BE RESET
RESPONSE DC    XL8'0'        USED WITH HELP OR STATUS DISPLAY
ERROR0   DC    C'*** I RUN ONLY ON A SCREEN TERMINAL ***'
ERROR1   DC    C'*** I WANT TO BE SCREEN SIZED 24X80 ***'
         SPACE 1
CLEAR    $FS   CC=EW,WCC=(AL,KBR,RMDT),SBA=(24,79),MF=L
         $FS   SBA=(1,1),RA=(1,1,00),MF=L
         $FS   SBA=(1,1),SF=(IC),MF=L
LCLEAR   EQU   *-CLEAR
         SPACE 1
*--------------------------------------------------------------------*
*        SNAP CONTROL BLOCKS.                                        *
*--------------------------------------------------------------------*
         SPACE 1
SNAPDCB DCB    DSORG=PS,RECFM=VBA,MACRF=(W),BLKSIZE=1632,LRECL=125,    X
               DDNAME=SYSSNAP
SNAPLST SNAP   DCB=SNAPDCB,ID=1,STORAGE=(SBSNAP,EBSNAP),               X
               STRHDR=SNAPHD,MF=L
SNAPHD   DC    AL1(L'HDT)
HDT      DC    C'A U T 9 9 - DUMP OF TERMINAL INPUT BUFFER.'
         EJECT
*--------------------------------------------------------------------*
*        LITERAL POOL.                                               *
*--------------------------------------------------------------------*
         SPACE 1
        LTORG
         SPACE 1
*--------------------------------------------------------------------*
*        INPUT BUFFER.                                               *
*--------------------------------------------------------------------*
         SPACE 1
*------- AREA TO HOLD TGET REPLY
        CNOP   0,8           FILLER FOR SNAP START ALIGNMENT
SBSNAP   DS    0F            SNAP DUMP START
PFREPLY  DC    XL3'0'        AID + JUNK (CURSOR ADDRESS)
REPLY    DS    1024X'0'      REAL REPLY
REPLYL   EQU   *-PFREPLY
EBSNAP   DC    XL8'0'        FILLER FOR SNAP DUMP
         EJECT
*--------------------------------------------------------------------*
*        HELP PANEL.                                                 *
*--------------------------------------------------------------------*
         SPACE 1
ECRAN   $FS    CC=W,WCC=(KBR),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,10),SF=(PROT,INT),MF=L
        $FS    TEXT='Authorizations/Programmers ',MF=L
        $FS    TEXT='MANAGEMENT Rules Summary',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,10),SF=(PROT,INT),MF=L
        $FS    TEXT='===========================',MF=L
        $FS    TEXT='========================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(4,1),SF=(PROT,INT),TEXT='1',MF=L
        $FS    SF=(PROT),TEXT='An Authorization N¢ defines a Pro',MF=L
        $FS    TEXT='ject Manager (Resp. Progr. N¢) and the',MF=L
        $FS    SBA=(5,3),MF=L
        $FS    SF=(PROT),TEXT='Programmers N¢s allowed to work o',MF=L
        $FS    TEXT='n it. A Progr. N¢ in the Authorization can',MF=L
        $FS    SBA=(6,3),MF=L
        $FS    SF=(PROT),TEXT='be momentaneously invalidated or ',MF=L
        $FS    TEXT='deleted, but when you add or reactivate it,',MF=L
        $FS    SBA=(7,3),MF=L
        $FS    SF=(PROT),TEXT='it will be forced to the state of',MF=L
        $FS    TEXT=' the Progr. N¢ definition. The Resp. Progr.',MF=L
        $FS    SBA=(8,3),MF=L
        $FS    SF=(PROT),TEXT='must always be a defined active P',MF=L
        $FS    TEXT='rogr. N¢ and does not allow to work on the',MF=L
        $FS    SBA=(9,3),MF=L
        $FS    SF=(PROT),TEXT='Authorization (to do it, you must',MF=L
        $FS    TEXT=' also define it in the Progr. N¢s list).',MF=L
        $FS    SBA=(11,1),SF=(PROT,INT),TEXT='2',MF=L
        $FS    SF=(PROT),TEXT='A Programmer N¢ is always created',MF=L
        $FS    TEXT=' as active, then it may be invalidated or',MF=L
        $FS    SBA=(12,3),MF=L
        $FS    SF=(PROT),TEXT='deleted, but these last two actio',MF=L
        $FS    TEXT='ns are automatically reflected in all the',MF=L
        $FS    SBA=(13,3),MF=L
        $FS    SF=(PROT),TEXT='Authorizations where he is define',MF=L
        $FS    TEXT='d as work-programmer. The invalidation or',MF=L
        $FS    SBA=(14,3),MF=L
        $FS    SF=(PROT),TEXT='deletion is rejected until this P',MF=L
        $FS    TEXT='rogrammer is indicated as a Resp. Progr. in',MF=L
        $FS    SBA=(15,3),MF=L
        $FS    SF=(PROT),TEXT='any Authorization(s).',MF=L
        $FS    SBA=(17,1),SF=(PROT,INT),TEXT='3',MF=L
        $FS    SF=(PROT),TEXT='The Priority defined in the Autho',MF=L
        $FS    TEXT='rizations is used by IEFUJV SMFexit routine',MF=L
        $FS    SBA=(18,3),MF=L
        $FS    SF=(PROT),TEXT='to increment or decrement the job',MF=L
        $FS    TEXT='''s scheduling priority for background/batch',MF=L
        $FS    SBA=(19,3),MF=L
        $FS    SF=(PROT),TEXT='execution. So all jobs using this',MF=L
        $FS    TEXT=' Authorization N¢ will have more or less',MF=L
        $FS    SBA=(20,3),MF=L
        $FS    SF=(PROT),TEXT='opportunities to be executed with',MF=L
        $FS    TEXT=' respect to the others. The standard level',MF=L
        $FS    SBA=(21,3),MF=L
        $FS    SF=(PROT),TEXT='0 is indicated by the digit 4 (th',MF=L
        $FS    TEXT='us 0 results in a decrement of 4 (-4) and 9',MF=L
        $FS    SBA=(22,3),MF=L
        $FS    SF=(PROT),TEXT='results in an increment of 5 (+5)',MF=L
        $FS    TEXT=' at the execution time job scheduling).',MF=L
        $FS    SBA=(24,1),SF=(PROT),TEXT='Hit',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SF=(PROT),TEXT='or',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-3',MF=L
        $FS    SF=(PROT),TEXT='to escape',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),TEXT=' ',MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
LECRAN   EQU   *-ECRAN
         SPACE 1
*--------------------------------------------------------------------*
*        DUMMY SECTIONS.                                             *
*--------------------------------------------------------------------*
         SPACE 1
        DCBD   DSORG=PS,DEVD=DA
         SPACE 2
         END
