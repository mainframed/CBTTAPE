AU40     TITLE '-- TSO-CP AUT40 --'
AUT40    START 0
         SPACE 1
**********************************************************************
*        THIS CP ALLOWS TO KNOW ALL USER-IDS DEFINED WITH A          *
*        SPECIFIC AUTHORIZATION NUMBER OR PROGRAMMER NUMBER.         *
*                                         P.A.MOINIL  05 DECEMBRE 89 *
**********************************************************************
         PRINT NOGEN
         SPACE 1
NMB      EQU   2000                    MAX. NO. OF MEMBERS NAMES IN
*                                      THE DIRECTORY OF TSO-UADS.
NAN      EQU   3000                    MAX. NO. OF ACCOUNT NUMBERS IN
*                                      THE TSO-UADS DATA-SET.
MXMU     EQU   3                       MAX. NO. OF MEMBERS FOR A USERID
*                                      IN THE TSO-UADS DATA-SET
*                                      (ACCEPTABLE RANGE IS 1 UP TO 9).
         SPACE 1
        $DEFREG
        $XENT  BASE=(R10,R11,R12)
         SPACE 1
*------- CONTROL OF CALLER (USERID)
         L     R15,=V(AUT99H)
         XC    0(4,R15),0(R15)         SUPPRESS HELP KEY
         XR    R0,R0                   NO HARDCOPY
         L     R15,=V(AUT98)
         BALR  R14,R15
         LTR   R15,R15
         BNZ   LEAVE                   BRANCH TO END OF CP
         L     R1,=A(X'80000000'+PECRIN)
         L     R15,=V(AUT99)           INITIAL PANEL DISPLAY
         BALR  R14,R15
         EJECT
*--------------------------------------------------------------------*
*        USERID AUTORISE A UTILISER CETTE CP.                        *
*--------------------------------------------------------------------*
         SPACE 1
*------- ALLOCATION DYNAMIC DU FICHIER TSO-UADS (DD-NAME = UADSSYS).
         XR    R4,R4                   SET SWITCH TO PROCESS
         L     R1,=V(AUT98U)
         MVC   DYNDSN(44),0(R1)        SET TSO-UADS DS-NAME
        $DYNALL DYNLIST                DYNAMIC ALLOC OF TSO-UADS
         LTR   R15,R15                 TEST RETURN CODE
         BZ    OKALL                   BRANCH IF OK
         STM   R15,R0,DSD
         UNPK  ERROR1C+9(9),DSD(5)
         TR    ERROR1C+9(8),TRTAB-X'F0'
         MVI   ERROR1C+17,C' '
         UNPK  ERROR1C+23(9),DSD+4(5)
         TR    ERROR1C+23(8),TRTAB-X'F0'
         MVI   ERROR1C+31,C' '
         LA    R1,ERROR1
         XR    R2,R2
         LA    R3,ERROR1C
         L     R15,=V(AUT97)
         BALR  R14,R15
         BCTR  R4,0                    SET SWITCH TO NO PROCESS
         B     WHTOGO
OKALL   OPEN   (RDDIR,INPUT)
         TM    RDDIR+(DCBOFLGS-IHADCB),DCBOFOPN
         BO    OKOPN
         LA    R1,ERROR2
SNDMS    XR    R2,R2
         LR    R3,R2
         L     R15,=V(AUT97)
         BALR  R14,R15
         BCTR  R4,0                    SET SWITCH TO NO PROCESS
         B     FRUADS
*------- RETAIN ALL MEMBERS NAMES OF DIRECTORY.
OKOPN    LM    R5,R7,DIRPTR
DIRRD   GET    RDDIR,DIRWA
         LH    R2,DIRWA                USEFULL LENGTH
         LA    R3,DIRWA+2              START ADDRESS
         LA    R15,2                   STARTING COUNT LENGTH
DIRNX    SR    R2,R15                  REDUCE USEFULL LENGTH
         BNP   DIRRD                   BRANCH IF NO MORE
         CLC   0(4,R3),=XL4'0'         END OF DIRECTORY DATA ?
         BE    DIRRD                   YES
         CLC   0(8,R3),=XL8'FFFFFFFFFFFFFFFF' NO, END OF DIRECTORIES ?
         BE    DIREOD                  YES
         BXLE  R5,R6,DIRMR             NO, BUMP MEMBER TABLE
        CLOSE  RDDIR
         LA    R1,ERROR3               DIRECTORY TABLE OVERFLOW
         B     SNDMS
DIRMR    MVC   0(8,R5),0(R3)           RETAIN MEMBER NAME
         IC    R15,11(R3)              NO. OF HALFWORDS DATA LENGTH
         N     R15,=A(X'0000001F')
         SLL   R15,1
         LA    R15,12(R15)             TOTAL DIR. ENTRY LENGTH
         ALR   R3,R15                  POINT TO NEXT DIR. ENTRY
         B     DIRNX
DIREOD   ST    R5,DIRPTR+8             SET TRUE END
         L     R5,DIRPTR               COMPUTE AND SET TRUE START
         AL    R5,DIRPTR+4
         ST    R5,DIRPTR
        CLOSE  RDDIR
         CLC   DIRPTR,DIRPTR+8
         BNE   STMOP
         LA    R1,ERROR4               DATA-SET ERROR
         B     SNDMS
*------- FILL IN ACCOUNT TABLE ALL USERIDS.
STMOP   $MOPEN URECL,DYNLIST           OPEN FILE UNDER UADSSYS DD
         LTR   R15,R15
         BZ    OKMOP
         CVD   R15,DSD
         UNPK  DSD(5),DSD+6(3)
         MVC   ERROR5+L'ERROR5-2(2),DSD+1
         LA    R1,ERROR5               OPEN DATA-SET ERROR
         B     SNDMS
OKMOP    CLC   URECL,=F'80'            CHECK LOGICAL RECORD LENGTH
         BE    STUSID
         LA    R1,ERROR6               LOGICAL RECORD LENGTH ERROR
         B     SNDMS
STUSID   L     R8,DIRPTR
         LM    R5,R7,ACCPTR
RDUSID   L     R2,=A(INPUT)
         LA    R3,NMU
         MVC   USRNME(8),0(R8)
         LA    R1,USRNME+L'USRNME-1
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         TM    0(R1),C'0'
         BO    *+L'*+14
         MVC   ERROR13+12(8),0(R8)
         LA    R1,ERROR13              MEMBER NAME ERROR
         B     SNDMG
         MVI   0(R1),C' '
NXUSID   MVC   MBNME(8),0(R8)
        $MFIND MBNME,DYNLIST           FIND USERID MEMBER
         LTR   R15,R15
         BZ    OKFIND
         CVD   R15,DSD
         UNPK  DSD(5),DSD+6(3)
         MVC   ERROR7+L'ERROR7-2(2),DSD+1
         MVC   ERROR7+12(L'MBNME),MBNME
         LA    R1,ERROR7               FIND MEMBER ERROR
SNDMG    XR    R2,R2
         LR    R3,R2
         L     R15,=V(AUT97)
         BALR  R14,R15
         BCTR  R4,0                    SET SWITCH TO NO PROCESS
         B     CLUADS
OKFIND  $MREAD (R2),DYNLIST
         LTR   R15,R15
         BM    EODUADS
         BNZ   ERREAD
         AL    R2,URECL
         BCT   R3,OKFIND
         MVC   ERROR8+L'ERROR8-L'MBNME(L'MBNME),MBNME
         LA    R1,ERROR8               INPUT BUFFER TOO SMALL
         B     SNDMG
ERREAD   CVD   R15,DSD
         UNPK  DSD(5),DSD+6(3)
         MVC   ERROR9+L'ERROR9-2(2),DSD+1
         MVC   ERROR9+12(L'MBNME),MBNME
         LA    R1,ERROR9               MREAD ERROR
         B     SNDMG
EODUADS  CL    R8,DIRPTR+8             CHECK IF USER-ID CONTINUE
         BNL   PRCSUS
         LR    R9,R8
         AL    R9,DIRPTR+4
         MVC   DSD(8),0(R8)
         LA    R1,DSD+7
         CLI   0(R1),C' '
         BNE   *+L'*+4
         BCT   R1,*-8
         NI    0(R1),X'0F'
         TR    0(1,R1),=CL16'123456789*******'
         CLC   DSD(8),0(R9)
         BNE   PRCSUS
         LR    R8,R9
         B     NXUSID
PRCSUS   L     R2,=A(INPUT)
         USING DHED,R2
         L     R9,UADSPWD1
         LA    R9,UADSMHDR(R9)
         USING DPOB,R9
NXTPSW   L     R14,UADSPSUB
         LA    R14,UADSMHDR(R14)
         USING DNOB,R14
NXTACC   ICM   R15,B'1111',UADSADAT
         BZ    NOACCT
         LA    R15,UADSMHDR(R15)
         USING DNOBD,R15
         CLI   UADSALEN,8              STANDARD EURATOM AUT/PGM ?
         BNE   NOACCT                  NO, BYPASS IT
         BXLE  R5,R6,OKACCT
         LA    R1,ERROR10              DIRECTORY TABLE OVERFLOW
         B     SNDMG
OKACCT   MVC   0(8,R5),UADSANUM        MOVE ACCOUNT (AUT/PGM)
         DROP  R15
         MVC   8(8,R5),USRNME          SET USERID NAME
NOACCT   TM    UADSAFLG,AFLG01         ANY MORE ACCOUNT ?
         BO    UPTOPS                  NO, GO UP ONE LEVEL
         XR    R1,R1
         ICM   R1,B'0111',UADSANEX
         LA    R14,UADSMHDR(R1)
         BNZ   NXTACC
         DROP  R14
UPTOPS   TM    UADSPFLG,PFLG01         ANY MORE PASSWORDS ?
         BO    ENDUSR                  NO, END OF THIS USER-ID
         XR    R1,R1
         ICM   R1,B'0111',UADSPNEX
         LA    R9,UADSMHDR(R1)
         BNZ   NXTPSW
         DROP  R2
ENDUSR   AL    R8,DIRPTR+4
         CL    R8,DIRPTR+8
         BNH   RDUSID
         ST    R5,ACCPTR+8             SET TRUE END
         L     R5,ACCPTR               COMPUTE AND SET TRUE START
         AL    R5,ACCPTR+4
         ST    R5,ACCPTR
CLUADS  $MCLOSE DYNLIST                CLOSE FILE BEFORE FREE
         LTR   R15,R15
         BZ    FRUADS
         CVD   R15,DSD
         UNPK  DSD(5),DSD+6(3)
         MVC   ERROR11+L'ERROR11-2(2),DSD+1
         LTR   R4,R4
         BZ    CLUADSX
        TPUT   ERROR11,L'ERROR11       CLOSE DATA-SET ERROR
         B     FRUADS
CLUADSX  LA    R1,ERROR11              CLOSE DATA-SET ERROR
         XR    R2,R2
         LR    R3,R2
         L     R15,=V(AUT97)
         BALR  R14,R15
         BCTR  R4,0                    SET SWITCH TO NO PROCESS
FRUADS   XC    DYNDSN,DYNDSN
        $DYNALL DYNLIST                DYNAMIC DE-ALLOC OF TSO-UADS
         LTR   R15,R15                 TEST RETURN CODE
         BZ    WHTOGO                  BRANCH IF OK
         STM   R15,R0,DSD
         UNPK  ERROR1C+9(9),DSD(5)
         TR    ERROR1C+9(8),TRTAB-X'F0'
         MVI   ERROR1C+17,C' '
         UNPK  ERROR1C+23(9),DSD+4(5)
         TR    ERROR1C+23(8),TRTAB-X'F0'
         MVI   ERROR1C+31,C' '
         LTR   R4,R4
         BZ    FRUADSX
        TPUT   ERROR0+1,L'ERROR0-1
        TPUT   ERROR1C+1,L'ERROR1C-1
         B     WHTOGO
FRUADSX  LA    R1,ERROR0
         XR    R2,R2
         LA    R3,ERROR1C
         L     R15,=V(AUT97)
         BALR  R14,R15
         BCTR  R4,0                    SET SWITCH TO NO PROCESS
WHTOGO   LTR   R4,R4                   ANY ERROR ENCOUNTERED ?
         BNZ   LEAVE                   YES, DON'T CONTINUE, BUT EXIT
         CLC   ACCPTR,ACCPTR+8
         BNE   DEMASKC
         LA    R1,ERROR12              NO VALID ACCOUNT IN DATA-SET
         XR    R2,R2
         LR    R3,R2
         L     R15,=V(AUT97)
         BALR  R14,R15
         B     LEAVE
DEMASKC TCLEARQ INPUT                  RESET ANY INPUT
         SPACE 1
*------- ALLOCATION DYNAMIQUE DU FICHIER D'ACCOUNTING
*        QUI SE TROUVE SOUS LA DD-NAME SYSACC.
         L     R1,=V(AUT98A)
         MVC   DYADSN(44),0(R1)        SET ACCOUNT FILE DS-NAME
        $DYNALL DYALIST                DYNAMIC ALLOC OF SYSACC
         LTR   R15,R15                 TEST RETURN CODE
         BZ    ALLOK                   B ON ALLOC DONE IF ZERO
         STM   R15,R0,DSD
         UNPK  ERROR1C+9(9),DSD(5)
         TR    ERROR1C+9(8),TRTAB-X'F0'
         MVI   ERROR1C+17,C' '
         UNPK  ERROR1C+23(9),DSD+4(5)
         TR    ERROR1C+23(8),TRTAB-X'F0'
         MVI   ERROR1C+31,C' '
         LA    R1,ERROR1A
         XR    R2,R2
         LA    R3,ERROR1C
         L     R15,=V(AUT97)
         BALR  R14,R15
         B     LEAVE
ALLOK   CALL   ZOPEN,(RETCD,XOPT),VL   OPEN FILE UNDER SYSACC DD
         LTR   R0,R15                  IS RETURN CODE 0?
         BZ    OPNOK                   BRANCH IF YES
         XR    R1,R1
         LM    R2,R3,NZOP
         L     R15,=V(AUT97)
         BALR  R14,R15
         B     FREACC
OPNOK    L     R1,=V(AUT99I)
         MVC   0(4,R1),=V(AUT95)       SET PF-2 KEY ACTIVE
        TIME   BIN
         ST    R1,DATE                 FORMAT = 0CYYDDDF
         AP    DATE,=PL4'+1900000'
         EJECT
*--------------------------------------------------------------------*
*        HANDLE A REQUEST AND PROCESS IT.                            *
*--------------------------------------------------------------------*
         SPACE 1
DEMINIT  LA    R1,PECRAN
         L     R15,=V(AUT99)
         BALR  R14,R15
         LTR   R15,R15                 NORMAL COMPLETION ?
         BNZ   SEEWHAT                 NO
         L     R15,=V(AUT99H)
         XC    0(4,R15),0(R15)         SUPPRESS HELP KEY
         MVC   ECRANM(L'ECRANM),=CL80' '
         MVC   ECDATA(79),=CL80' '
         LA    R15,NLDATA
         LA    R14,ECDATA+79
         MVC   0(80,R14),=CL80' '
         LA    R14,80(R14)
         BCT   R15,*-10
        $FS    SF=(PROT),MF=(I,MSGNTF)
         MVC   MSGNTE(L'MSGNTE),=CL80' '
         MVC   SBACUR,SBAAUT
         L     R15,=V(AUT99R)          YES, GET REQUEST
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BNZ   ANLTXT                  NO, GO SEE WHAT ENTERED
         CLI   SCRCNT,C' '             YES, IS THERE ANY MORE PENDING ?
         BE    CHKRPT                  NO
         MVC   SCRCNT(L'SCRCNT),=CL80' '  YES, CONTINUE
         LA    R4,ECDATA
         LA    R3,NDATA
         B     SETIT
CHKRPT   CLI   SCRAUT,C' '             CHECK TO REPEAT LAST REQUEST
         BNE   STPRC
         CLI   SCRPRG,C' '
         BNE   STPRC
         CLI   SCRUID,C' '
         BNE   STPRC
REASK    MVC   ECRANM(L'MSGASK),MSGASK RE-ASK
         B     DEMINIT
ANLTXT   CLC   0(3,R15),SBAAUT
         BNE   NOTAUT
         MVC   SCRAUT(L'SCRAUT),=CL80' '
         LTR   R1,R1                   TEXT RECEIVED ?
         BZ    AUTOK                   NONE (JUST CLEARED)
         BCT   R1,*+L'*+6
         MVC   SCRAUT(*-*),3(R15)
         EX    R1,*-6
         MVI   CURPOS,C'A'
AUTOK    L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    SCCTRL                  YES, ALL RECEIVED
NOTAUT   CLC   0(3,R15),SBAPRG
         BNE   NOTPRG
         MVC   SCRPRG(L'SCRPRG),=CL80' '
         LTR   R1,R1                   TEXT RECEIVED ?
         BZ    PRGOK                   NONE (JUST CLEARED)
         BCT   R1,*+L'*+6
         MVC   SCRPRG(*-*),3(R15)
         EX    R1,*-6
         MVI   CURPOS,C'P'
PRGOK    L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    SCCTRL                  YES, ALL RECEIVED
NOTPRG   CLC   0(3,R15),SBAUID
         BNE   NOTUID
         MVC   SCRUID(L'SCRUID),=CL80' '
         LTR   R1,R1                   TEXT RECEIVED ?
         BZ    UIDOK                   NONE (JUST CLEARED)
         BCT   R1,*+L'*+6
         OC    SCRUID(*-*),3(R15)
         EX    R1,*-6
         MVI   CURPOS,C'U'
UIDOK    L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    SCCTRL                  YES, ALL RECEIVED
NOTUID   MVC   ECRANM(L'MSGSBA),MSGSBA NO, ERROR MESSAGE
         B     DEMINIT
SCCTRL   CLI   SCRAUT,C' '             CHECK REQUEST
         BE    CHKPRG
         CLI   SCRPRG,C' '
         BNE   CHKMSG
         CLI   SCRUID,C' '
         BNE   CHKMSG
         XC    DSD(L'SCRAUT),DSD
         MVZ   DSD(L'SCRAUT),SCRAUT
         CLC   DSD(L'SCRAUT),=CL4'0000'
         BE    STPRC
         MVC   ECRANM(L'MSGINVA),MSGINVA  AUT. INVALID
         B     DEMINIT
CHKMSG   MVC   ECRANM(L'MSGDBL),MSGDBL ERROR MESSAGE
         CLI   CURPOS,C'A'
         BNE   LKFWD
         CLI   SCRPRG,C' '
         BE    *+L'*+10
         MVC   SBACUR,SBAPRG
         B     DEMINIT
         MVC   SBACUR,SBAUID
         B     DEMINIT
LKFWD    CLI   CURPOS,C'P'
         BNE   FWDLK
         CLI   SCRAUT,C' '
         BNE   DEMINIT
         MVC   SBACUR,SBAUID
         B     DEMINIT
FWDLK    CLI   CURPOS,C'U'
         BNE   DEMINIT
         CLI   SCRAUT,C' '
         BNE   DEMINIT
         MVC   SBACUR,SBAPRG
         B     DEMINIT
CHKPRG   CLI   SCRPRG,C' '
         BE    CHKUID
         CLI   SCRUID,C' '
         BNE   CHKMSG
         XC    DSD(L'SCRPRG),DSD
         MVZ   DSD(L'SCRPRG),SCRPRG
         CLC   DSD(L'SCRPRG),=CL4'0000'
         BE    STPRC
         MVC   ECRANM(L'MSGINVP),MSGINVP  PRG. INVALID
         MVC   SBACUR,SBAPRG
         B     DEMINIT
CHKUID   CLI   SCRUID,C' '
         BE    REASK
         CLI   SCRUID,C'A'
         BL    *+L'*+8
         CLI   SCRUID,C'Z'
         BNH   *+L'*+16
         MVC   ECRANM(L'MSGINVU),MSGINVU  USER-ID INVALID
         MVC   SBACUR,SBAUID
         B     DEMINIT
         LA    R14,SCRUID+L'SCRUID-1
         LA    R15,L'SCRUID-1
         STC   R15,KEYLG
         CLI   0(R14),C' '             LOOK IF KEY/ SEARCH
         BNE   *+L'*+10
         BCTR  R14,0
         BCT   R15,*-10
         B     STPRC
         CLI   0(R14),C'/'
         BNE   STPRC
         BCTR  R15,0                   COMPUTE LENGTH OF KEY
         STC   R15,KEYLG
*------- PROCESS THE REQUEST.
STPRC    LM    R5,R7,ACCPTR            FILL PANEL WITH ALL MATCHES
         LA    R4,ECDATA
         LA    R3,NDATA
SRAUT    CLI   SCRAUT,C' '
         BE    SRPRG
LPAUT    CLC   0(4,R5),SCRAUT
         BNE   *+L'*+4
         BAL   R9,SETIT
         BXLE  R5,R6,LPAUT
         B     TSTPR
SRPRG    CLI   SCRPRG,C' '
         BE    SRUID
LPPRG    CLC   4(4,R5),SCRPRG
         BNE   *+L'*+4
         BAL   R9,SETIT
         BXLE  R5,R6,LPPRG
         B     TSTPR
SRUIDCK  CLC   8(*-*,R5),SCRUID
SRUID    XR    R15,R15
         IC    R15,KEYLG
         EX    R15,SRUIDCK
         BNE   *+L'*+4
         BAL   R9,SETIT
         BXLE  R5,R6,SRUID
TSTPR    CLC   ECDATA(20),=CL80' '
         BNE   TCRPS
         MVC   ECRANM(L'MSGNNM),MSGNNM NONE MESSAGE
TCRPS    CLI   CURPOS,C'P'
         BNE   *+L'*+10
         MVC   SBACUR,SBAPRG
         B     DEMINIT
         CLI   CURPOS,C'U'
         BNE   DEMINIT
         MVC   SBACUR,SBAUID
         B     DEMINIT
SETIT    BCT   R3,*+L'*+10
         MVC   SCRCNT,=CL6'*MORE*'
         B     TCRPS
         MVC   2(7,R4),8(R5)
         MVC   10(4,R4),0(R5)
         MVC   15(4,R4),4(R5)
         PACK  DSD,4(4,R5)
         CVB   R0,DSD
         STH   R0,PRGNO
         LNR   R0,R0
         STH   R0,PRGNM
         PACK  DSD,0(4,R5)             CHECK ACCOUNTING NUMBER
         CVB   R0,DSD
         STH   R0,ARGUMAUT
        CALL   ZREAD,(RETCD,ARGUMAUT,AREAR),VL
         LTR   R15,R15                 TEST RETURN CODE 0
         BZ    SETFND                  IF YES, RECORD FOUND
         CH    R15,=H'15'              TEST RETURN CODE 15
         BE    SETNFD                  IF YES, AUT. NOT FOUND
         XR    R1,R1
         LM    R2,R3,NZRD
         L     R15,=V(AUT97)
         BALR  R14,R15
         B     CLSZR
SETFND   LA    R15,AREAR
         USING AUTDSECT,R15
         LH    R1,AUTANO
         LTR   R1,R1
         BNP   SETCAN                  AUT. CANCELED
         XR    R1,R1
         ICM   R1,B'0111',AUTVSD
         CL    R1,=F'99999'            WHICH FORMAT?
         BH    *+L'*+4                 NEW FORMAT
         AL    R1,=F'1900000'          OLD FORMAT, ADJUST IT
         CVD   R1,DSD
         CP    DATE,DSD+4(4)
         BL    SETNYA                  AUT. NOT YET ACTIVE
         XR    R1,R1
         ICM   R1,B'0111',AUTVED
         CL    R1,=F'99999'            WHICH FORMAT?
         BH    *+L'*+4                 NEW FORMAT
         AL    R1,=F'1900000'          OLD FORMAT, ADJUST IT
         CVD   R1,DSD
         CP    DATE,DSD+4(4)
         BH    SETEXP                  AUT. EXPIRED
         LA    R0,AUTNPNF
         LA    R1,AUTPNO
SETLOOP  CLC   0(L'PRGNO,R1),PRGNO     CHECK PRG. ALLOWED
         BE    SETOK
         CLC   0(L'PRGNM,R1),PRGNM     CHECK PRG. INVALIDATED
         BE    SETINV
         LA    R1,L'AUTPNO(R1)
         BCT   R0,SETLOOP
         DROP  R15
         MVI   0(R4),C'P'              PRG. NOT DEFINED
         B     SETMNT
SETNFD   MVI   0(R4),C'N'
         B     SETMNT
SETCAN   MVI   0(R4),C'C'
         B     SETMNT
SETNYA   MVI   0(R4),C'S'
         B     SETMNT
SETEXP   MVI   0(R4),C'E'
         B     SETMNT
SETINV   MVI   0(R4),C'I'
SETMNT   CLI   MSGNTE,C' '
         BNE   SETOK
        $FS    SF=(PROT,INT),MF=(I,MSGNTF)
        $FS    TEXT='Note :',MF=(I,MSGNTE)
        $FS    SF=(PROT),MF=I
        $FS    TEXT='hit',MF=I
        $FS    SF=(PROT,INT),MF=I
        $FS    TEXT='PF-1',MF=I
        $FS    SF=(PROT),MF=I
        $FS    TEXT='to get flag meanings.',MF=I
         L     R15,=V(AUT99H)
         MVC   0(4,R15),=A(PECRAH)     REPLACE HELP KEY
SETOK    LA    R4,20(R4)
         BR    R9
         SPACE 1
*--------------------------------------------------------------------*
*        FIN DU TRAVAIL - SORTIE.                                    *
*--------------------------------------------------------------------*
         SPACE 1
SEEWHAT  CH    R15,=H'4'
         BNE   CLSZR                   ERROR, EXIT
         XR    R1,R1                   END OF CP REQUESTED
         L     R15,=V(AUT99)           EXIT FULLSCREEN
         BALR  R14,R15
CLSZR   CALL   ZCLOSE,(RETCD),VL       CLOSE ACCOUNT FILE
         LTR   R0,R15                  IS RETURN CODE 0?
         BZ    FREACC                  BRANCH IF YES
         XR    R1,R1
        CALL   ZYDIAG
         LA    R8,1(R15)
         XR    R9,R9
         BCTR  R15,0
         IC    R9,0(R15)
         BCTR  R9,0
        TPUT   (R8),(R9)
FREACC   XC    DYADSN,DYADSN
        $DYNALL DYALIST                DYNAMIC DE-ALLOC OF SYSACC
         LTR   R15,R15                 TEST RETURN CODE
         BZ    LEAVE                   BRANCH IF OK
         STM   R15,R0,DSD
        TPUT   ERROR0A,L'ERROR0A
         UNPK  ERROR1C+9(9),DSD(5)
         TR    ERROR1C+9(8),TRTAB-X'F0'
         MVI   ERROR1C+17,C' '
         UNPK  ERROR1C+23(9),DSD+4(5)
         TR    ERROR1C+23(8),TRTAB-X'F0'
         MVI   ERROR1C+31,C' '
        TPUT   ERROR1C+1,L'ERROR1C-1
LEAVE   $XRET  CC=0
         EJECT
*--------------------------------------------------------------------*
*        VARIABLES DE TRAVAIL.                                       *
*--------------------------------------------------------------------*
         SPACE 1
DSD      DC    D'0'
DIRPTR   DC    A(DIRTB-L'DIRTB,L'DIRTB,EDIRTB)
ACCPTR   DC    A(ACCTB-L'ACCTB,L'ACCTB,EACCTB)
PECRIN   DC    A(LECRIN,ECRIN)
PECRAN   DC    A(LECRAN,ECRAN)
PECRAH   DC    A(LECRAH,ECRAH)
URECL    DC    F'0'
RETCD    DC    F'0'          RETURN CODE FROM Z... ROUTINES
NZOP     DC    0F'0',CL8'ZOPEN'
NZRD     DC    0F'0',CL8'ZREAD'
TRTAB    DC    C'0123456789ABCDEF'
MBNME    DC    CL8' '
USRNME   DC    CL8' '
XOPT     DC    C'ONLY'       OPTION FOR Z... ROUTINES
DATE     DC    PL4'+0'
ARGUMAUT DC    H'0'
PRGNO    DC    H'0'
PRGNM    DC    H'0'
KEYLG    DC    AL1(*-*)
CURPOS   DC    CL1'A'
DIRWA    DC    0F'0',XL256'0'
AREAR    DC    0F'0',(AUTLGTH)X'0'
         SPACE 1
*--------------------------------------------------------------------*
*        DYNAMIC ALLOCATION PARAMETER LISTS                          *
*--------------------------------------------------------------------*
         SPACE 1
         DS    0F
DYNLIST  DC    C'UADSSYS '
DYNDSN   DC    XL44'0'
         DC    CL8' '        MEMBER NAME
         DC    CL8' '        PASSWORD
         DC    CL8'SHR'      STATUS
         DC    CL256' '      FILLER
         DC    CL88' '       FILLER
         SPACE 1
         DS    0F
DYALIST  DC    C'SYSACC  '
DYADSN   DC    XL44'0'
         DC    CL8' '        MEMBER NAME
         DC    CL8' '        PASSWORD
         DC    CL8'SHR'      STATUS
         DC    CL256' '      FILLER
         DC    CL85' '       FILLER
         EJECT
*--------------------------------------------------------------------*
*        MESSAGES D'ERREURS                                          *
*--------------------------------------------------------------------*
         SPACE 1
         DC    AL1(L'ERROR0)
ERROR0   DC    C' TSO-UADS : dynamic de-allocation error.'
         DC    AL1(L'ERROR0A)
ERROR0A  DC    C'*** ACCOUNT file FREE error ***'
         DC    AL1(L'ERROR1)
ERROR1   DC    C' TSO-UADS : dynamic allocation error.'
         DC    AL1(L'ERROR1A)
ERROR1A  DC    C' *** ACCOUNT file ALLOC error ***'
         DC    AL1(L'ERROR1C)
ERROR1C  DC    C'     R15=........ - R0=........ - AUT40'
         DC    AL1(L'ERROR2)
ERROR2   DC    C' TSO-UADS : open directory failed.'
         DC    AL1(L'ERROR3)
ERROR3   DC    C' TSO-UADS : directory table overflow.'
         DC    AL1(L'ERROR4)
ERROR4   DC    C' TSO-UADS : data-set empty (no members).'
         DC    AL1(L'ERROR5)
ERROR5   DC    C' TSO-UADS : open data-set error - MOPEN RC=  '
         DC    AL1(L'ERROR6)
ERROR6   DC    C' TSO-UADS : unexpected logical record length (not 80)'
         DC    AL1(L'ERROR7)
ERROR7   DC    C' TSO-UADS :          not found - MFIND RC=  '
         DC    AL1(L'ERROR8)
ERROR8   DC    C' TSO-UADS : input buffer overflow reading         '
         DC    AL1(L'ERROR9)
ERROR9   DC    C' TSO-UADS :          read error - MREAD RC=  '
         DC    AL1(L'ERROR10)
ERROR10  DC    C' TSO-UADS : account numbers table overflow.'
         DC    AL1(L'ERROR11)
ERROR11  DC    C' TSO-UADS : close data-set error - MCLOSE RC=  '
         DC    AL1(L'ERROR12)
ERROR12  DC    C' TSO-UADS : data-set empty (no valid account numbers)'
         DC    AL1(L'ERROR13)
ERROR13  DC    C' TSO-UADS :          = erroneous member name.'
MSGASK   DC    C'Please ENTER an Authorization or a Programmer number o1
               r a User-ID.'
MSGSBA   DC    C'Input scan SBA order not found ???'
MSGDBL   DC    C'More selections done. Please choice only one.'
MSGINVA  DC    C'Invalid Authorization Number (not 4 numerics).'
MSGINVP  DC    C'Invalid Programmer Number (not 4 numerics).'
MSGINVU  DC    C'Invalid User-ID (first character not alpha).'
MSGNNM   DC    C'There are no matches to your request.'
         EJECT
*--------------------------------------------------------------------*
*        ECRANS D'OUTPUT.                                            *
*--------------------------------------------------------------------*
         SPACE 1
ECRIN   $FS    CC=W,WCC=(AL,KBR,RMDT),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,27),SF=(PROT,INT),MF=L
        $FS    TEXT='USER-ID''S   D I S P L A Y',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,27),SF=(PROT,INT),MF=L
        $FS    TEXT='=========================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(4,1),SF=(PROT,INT),MF=L
        $FS    TEXT='Purpose :',MF=L
        $FS    SF=(PROT),TEXT='this Commmand Processor (CP) ',MF=L
        $FS    TEXT='allows you to display the Accounting',MF=L
        $FS    SBA=(5,12),TEXT='information and TSO User-IDs ',MF=L
        $FS    TEXT='for a specific Authorization Number',MF=L
        $FS    SBA=(6,12),TEXT='or Programmer Number as well ',MF=L
        $FS    TEXT='as the Accounting information for a',MF=L
        $FS    SBA=(7,12),TEXT='specific TSO User-ID.',MF=L
        $FS    SBA=(9,1),SF=(PROT,INT),RA=(9,80,-),MF=L
        $FS    SBA=(10,2),TEXT='Remarks :',MF=L
        $FS    SF=(PROT),TEXT='1. If desired, you can also ',MF=L
        $FS    TEXT='enter directly in this process typing',MF=L
        $FS    SBA=(11,15),TEXT='the TSO-command',MF=L
        $FS    SF=(PROT,INT),TEXT='AUT40',MF=L
        $FS    SF=(PROT),TEXT='at any time in your session.',MF=L
        $FS    SBA=(12,11),MF=L
        $FS    SF=(PROT),TEXT='2. Any changes in the TSO-UADS ',MF=L
        $FS    TEXT='data-set during this CP is running',MF=L
        $FS    SBA=(13,15),TEXT='are never immediately ',MF=L
        $FS    TEXT='displayable. The reason is because once',MF=L
        $FS    SBA=(14,15),TEXT='at the beginning, all the ',MF=L
        $FS    TEXT='requested data are read and memorized',MF=L
        $FS    SBA=(15,15),TEXT='in an internal core storage ',MF=L
        $FS    TEXT='table.',MF=L
        $FS    SBA=(16,1),SF=(PROT,INT),RA=(16,80,-),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(19,10),SF=(PROT,INT),RA=(19,70,*),MF=L
        $FS    SBA=(20,11),TEXT='* Now, sorry if you must wait a ',MF=L
        $FS    TEXT='couple of minutes ... but *',MF=L
        $FS    SBA=(21,11),TEXT='* the internal core storage ',MF=L
        $FS    TEXT='table takes time to be build. *',MF=L
        $FS    SBA=(22,11),RA=(22,70,*),MF=L
        $FS    SF=(PROT),MF=L
        PRINT  OFF
        $FS    SBA=(24,1),SF=(PROT,INT),TEXT='WELCOME',MF=L
        $FS    SF=(PROT),MF=L
        $FS    TEXT='to this up-grade CP implemented by ',MF=L
        $FS    TEXT='MOINIL P.A. ... your wizard |',MF=L
        PRINT  ON
        $FS    SF=(PROT),MF=L
        $FS    SF=(IC),TEXT=(' ',1),MF=L
        $FS    SF=(PROT),MF=L
LECRIN   EQU   *-ECRIN
         SPACE 1
ECRAN   $FS    CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,27),SF=(PROT,INT),MF=L
        $FS    TEXT='USER-ID''S   D I S P L A Y',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,27),SF=(PROT,INT),MF=L
        $FS    TEXT='=========================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(4,1),SF=(PROT),MF=L
        $FS    TEXT='Enter Authorization Number',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT,NUM),MF=L
SBAAUT  $FS    SBA=(4,34),MF=L
SCRAUT  $FS    TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='or Programmer Number',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT,NUM),MF=L
SBAPRG  $FS    SBA=(4,65),MF=L
SCRPRG  $FS    TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='or',MF=L
        $FS    SBA=(5,7),SF=(PROT),TEXT='User-ID (or key/)',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),MF=L
SBAUID  $FS    SBA=(5,31),MF=L
SCRUID  $FS    TEXT=(' ',7),MF=L
        $FS    SF=(PROT),TEXT='or Hit',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-3',MF=L
        $FS    SF=(PROT),TEXT='to exit (end or emergency).',MF=L
        $FS    SBA=(6,1),SF=(PROT,INT),MF=L
ECRANM  $FS    TEXT=(' ',78),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(7,1),SF=(PROT),MF=L
        $FS    TEXT='F User-ID Aut. Prg. ',MF=L
        $FS    TEXT='F User-ID Aut. Prg. ',MF=L
        $FS    TEXT='F User-ID Aut. Prg. ',MF=L
        $FS    TEXT='F User-ID Aut. Prg.',MF=L
        $FS    SBA=(8,1),SF=(PROT),MF=L
        $FS    TEXT='- ------- ---- ---- ',MF=L
        $FS    TEXT='- ------- ---- ---- ',MF=L
        $FS    TEXT='- ------- ---- ---- ',MF=L
        $FS    TEXT='- ------- ---- ----',MF=L
        $FS    SBA=(9,1),SF=(PROT,INT),MF=L
ECDATA  $FS    TEXT=(' ',79),MF=L      LINE  9 -  1
        $FS    TEXT=(' ',80),MF=L      LINE 10 -  2
        $FS    TEXT=(' ',80),MF=L      LINE 11 -  3
        $FS    TEXT=(' ',80),MF=L      LINE 12 -  4
        $FS    TEXT=(' ',80),MF=L      LINE 13 -  5
        $FS    TEXT=(' ',80),MF=L      LINE 14 -  6
        $FS    TEXT=(' ',80),MF=L      LINE 15 -  7
        $FS    TEXT=(' ',80),MF=L      LINE 16 -  8
        $FS    TEXT=(' ',80),MF=L      LINE 17 -  9
        $FS    TEXT=(' ',80),MF=L      LINE 18 - 10
        $FS    TEXT=(' ',80),MF=L      LINE 19 - 11
        $FS    TEXT=(' ',80),MF=L      LINE 20 - 12
        $FS    TEXT=(' ',80),MF=L      LINE 21 - 13
        $FS    TEXT=(' ',80),MF=L      LINE 22 - 14
        $FS    TEXT=(' ',80),MF=L      LINE 23 - 15
LDATA    EQU   *-ECDATA
NDATA    EQU   ((LDATA+1)/20)+1
NLDATA   EQU   ((LDATA+1)/80)-1
        $FS    SBA=(24,1),MF=L
MSGNTF  $FS    SF=(PROT),MF=L
MSGNTE  $FS    TEXT=(' ',70),MF=L
        $FS    SBA=(24,72),SF=(PROT,INT),MF=L
SCRCNT  $FS    TEXT=(' ',6),MF=L
        $FS    SF=(PROT),MF=L
SBACUR  $FS    SBA=(4,34),SF=(IC),MF=L
LECRAN   EQU   *-ECRAN
         EJECT
*--------------------------------------------------------------------*
*        LITERAL POOL.                                               *
*--------------------------------------------------------------------*
         SPACE 1
        LTORG
         SPACE 1
*--------------------------------------------------------------------*
*        DATA CONTROL BLOCK FOR TSO-UADS DIRECTORY.                  *
*--------------------------------------------------------------------*
         SPACE 1
RDDIR   DCB    DDNAME=UADSSYS,DSORG=PS,MACRF=GM,EODAD=DIREOD,          1
               RECFM=F,BLKSIZE=256,LRECL=256
         EJECT
*--------------------------------------------------------------------*
*        ECRAN LOCAL POUR HELP.                                      *
*--------------------------------------------------------------------*
         SPACE 1
ECRAH   $FS    CC=W,WCC=(KBR,RMDT),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,27),SF=(PROT,INT),MF=L
        $FS    TEXT='USER-ID''S   D I S P L A Y',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,27),SF=(PROT,INT),MF=L
        $FS    TEXT='=========================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(4,1),SF=(PROT),MF=L
        $FS    TEXT='Flag values description :',MF=L
        $FS    SBA=(5,1),MF=L
        $FS    TEXT='-------------------------',MF=L
        $FS    SBA=(7,10),SF=(PROT,INT),TEXT='N',MF=L
        $FS    SF=(PROT),MF=L
        $FS    TEXT='= Authorization not found.',MF=L
        $FS    SBA=(9,10),SF=(PROT,INT),TEXT='C',MF=L
        $FS    SF=(PROT),MF=L
        $FS    TEXT='= Authorization has been invalidated.',MF=L
        $FS    SBA=(11,10),SF=(PROT,INT),TEXT='S',MF=L
        $FS    SF=(PROT),MF=L
        $FS    TEXT='= Authorization not yet active.',MF=L
        $FS    SBA=(13,10),SF=(PROT,INT),TEXT='E',MF=L
        $FS    SF=(PROT),MF=L
        $FS    TEXT='= Authorization is expired.',MF=L
        $FS    SBA=(15,10),SF=(PROT,INT),TEXT='I',MF=L
        $FS    SF=(PROT),MF=L
        $FS    TEXT='= Programmer has been invalidated in this ',MF=L
        $FS    TEXT='Authorization.',MF=L
        $FS    SBA=(17,10),SF=(PROT,INT),TEXT='P',MF=L
        $FS    SF=(PROT),MF=L
        $FS    TEXT='= Programmer is not defined in this ',MF=L
        $FS    TEXT='Authorization.',MF=L
        $FS    SBA=(21,1),SF=(PROT,INT),TEXT='Note :',MF=L
        $FS    SF=(PROT),MF=L
        $FS    TEXT='these checks are those done by the System ',MF=L
        $FS    TEXT='on the Accounting',MF=L
        $FS    SBA=(22,9),MF=L
        $FS    TEXT='field of any JOB or LOGON.',MF=L
        $FS    SBA=(24,1),SF=(PROT),TEXT='Hit',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SF=(PROT),TEXT='or',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-3',MF=L
        $FS    SF=(PROT),TEXT='to escape',MF=L
        $FS    SF=(PROT,INT),TEXT='===>',MF=L
        $FS    SF=(INT),TEXT=' ',MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
LECRAH   EQU   *-ECRAH
         EJECT
*--------------------------------------------------------------------*
*        WORKING TABLES.                                             *
*--------------------------------------------------------------------*
         SPACE 1
         DS    0D                 FORCE DOUBLE-WORD ALIGNMENT
NMU      EQU   MXMU*10
INPUT    DS    (NMU)CL80          READ MEMBERS INPUT BUFFER
         SPACE 1
DIRTB    DS    (NMB)XL8           DIRECTORY MEMBERS NAMES
EDIRTB   EQU   *-L'DIRTB
         SPACE 1
ACCTB    DS    (NAN)XL16          ACCOUNT/USER-ID'S
EACCTB   EQU   *-L'ACCTB
         SPACE 1
*--------------------------------------------------------------------*
*        DUMMY SECTIONS.                                             *
*--------------------------------------------------------------------*
         SPACE 1
        DCBD   DEVD=DA,DSORG=PS
        IKJEFUAD DSPL,ALL
         SPACE 1
         PRINT GEN
        $AIRD  R=NO
         SPACE 2
         END
