VSMA     TITLE 'VSMANAL - VSM ANALYSIS.'
        PRINT  OFF
         MACRO           SAVE AREA'S TANK MANAGEMENT
&NAME   @ENTER &DUMMY
        CNOP   0,4
         USING &NAME,R11           SET LOCAL ADDRESSABILITY
&NAME    STM   R14,R12,12(R13)     SAVE CALLER'S REGISTERS
         LR    R11,R15             SET LOCAL BASE REGISTER
         CP    TANKNO,TANKMX       CONTROL TANK AVAILABILITY
         BNL   STOVER              OVERFLOW
         AP    TANKNO,TANKUP       UP TANKS NUMBER
         LA    R15,18*4(R13)       NEXT SAVE AREA
         XC    0(18*4,R15),0(R15)  CLEAR IT
         ST    R15,8(R13)          LINK SAVE AREAS
         ST    R13,4(R15)
         LR    R13,R15             ACTIVATE NEW SAVE AREA
         MEND
         MACRO           SAVE AREA'S TANK MANAGEMENT
&NAME   @EXIT  &DUMMY
&NAME    CP    TANKNO,TANKUP       CONTROL TANK AVAILABILITY
         BL    STUNDER             UNDERFLOW
         SP    TANKNO,TANKUP       DOWN TANKS NUMBER
         L     R13,4(R13)          PREVIOUS SAVE AREA
         ST    R15,16(R13)         SET REGISTER 15 (RC)
         LM    R14,R12,12(R13)     RESTORE CALLER'S REGISTERS
         MVI   12(R13),X'FF'       INDICATE RETURN EXECUTED
         BR    R14                 RETURN
         DROP  R11                 KILL LOCAL ADDRESSABILITY
         MEND
         MACRO           CALL ROUTINE
&NAME   @CALL  &WHERE
         AIF   ('&WHERE' EQ '').CO
&NAME    L     R15,=A(&WHERE)      GET ENTRY POINT
         BASR  R14,R15             GO TO ROUTINE
         MEXIT
.CO      ANOP
&NAME    BASR  R14,R15             GO TO ROUTINE
         MEND
         MACRO --- $ABDF --- ALLOCATED BLOCK DESCRIPTOR FORMAT
         $AFUF
AFUF     DSECT           ALLOCATED/FREE/UNALLOCATED SPACE DESCRIPTOR
AFUFVA   DS    A              VIRTUAL ADDRESS OF THE ALLOCATED BLOCK
*                             BIT 0 : THE CALLER SPECIFIED THE REAL
*                                     OPTION AND THIS ALLOCATED BLOCK
*                                     COULD BE BACKED IN REAL STORAGE
*                                     ABOVE (BIT 0 = 1) OR BELOW (BIT
*                                     0 = 0) 16 MEGABYTES
AFUFLG   DS    F              LENGTH OF THE ALLOCATED BLOCK
LAFUF    EQU   *-AFUF         LENGTH OF DSECT
         MEND
         MACRO --- $SDF --- SUBPOOL DESCRIPTOR FORMAT
         $SDF
SDF      DSECT           SUBPOOL DESCRIPTOR
SDFIS    DS    XL1            X'00' TO IDENTIFY A SUBPOOL DESCRIPTOR
SDFLG    DS    XL1            LENGTH OF SUBPOOL DESCRIPTOR
SDFID    DS    XL1            SUBPOOL ID
SDFKF    DS    XL1            MISCELLANEOUS FLAGS AND STORAGE KEY
*                             BITS 0-3 : STORAGE KEY
FGTCB    EQU   X'08'          BIT 4 : THE TCB WITH WHICH THIS DESCRIP-
*                                     TOR IS ASSOCIATED OWNS THE STORA-
*                                     GE DESCRIBED BY THIS DESCRIPTOR
*                                     (THIS IS MEANINGFUL FOR PRIVATE
*                                     AREA STORAGE ONLY)
FGSHR    EQU   X'04'          BIT 5 : THE STORAGE DESCRIBED BY THIS
*                                     DESCRIPTOR IS SHARED (THIS IS
*                                     MEANINGFUL FOR PRIVATE AREA
*                                     STORAGE ONLY)
*                             BITS 6-7 : RESERVED
SDFTCB   DS    A              OWNING TCB ADDRESS (IF PVT SUBPOOL),
*                                     OTHERWISE ZERO
         MEND
        PRINT  ON
         SPACE 1
VSMANAL  START 0
         SPACE 1
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * *            VSM  ANALYSIS            * * * * * * * * *
* * * * * * * * *                                     * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
* AUTHOR :     P.A. MOINIL                                            *
* --------     COMPUTING CENTRE                                       *
*              J.R.C. - ISPRA ESTABLISHMENT                           *
*              21020 ISPRA (VA), ITALY                                *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*                                                                     *
* PURPOSE :    THIS PROGRAM PRODUCES A LISTING OF THE ENTIRE VSM      *
* ---------    BLOCKS (ALL SUBPOOLS) FOR SPECIFIED STORAGE AREAS.     *
*                                                                     *
* CALL OF THE PROGRAM :                                               *
* ---------------------                                               *
*                                                                     *
*        //LIST   EXEC  PGM=VSMANAL,PARM='...'                        *
*        //SYSPRINT DD  SYSOUT=A                                      *
*        //SYSSNAP  DD  SYSOUT=A (ONLY IF DEBUG NEEDED)               *
*                                                                     *
*        WHERE ... IS USED TO SPECIFY THE DESIRED STORAGE AREAS,      *
*              AND MAY BE ANY ONE OF THE FOLLOWING KEYWORDS :         *
*                   SQA  - SYSTEM QUEUE AREA                          *
*                   CSA  - COMMON SERVICE AREA                        *
*                   LSQA - LOCAL SYSTEM QUEUE AREA                    *
*                   PVT  - PRIVATE (CURRENT ADDRESS SPACE)            *
*                   AE   - ALLOCATED ELEMENT (CURRENT ADDRESS SPACE)  *
*              OR, IF MORE THAN ONE DESIRED, THEY MUST BE SEPARATED   *
*              BY A COMMA.                                            *
*                   I.E. : PARM='SQA,CSA,LSQA'                        *
*        ALSO ALTERNATE DD-NAMES MAY BE SPECIFIED FOR THE LIST OR     *
*              SNAP OUTPUT, AS SHOWN BELOW :                          *
*                   PARM='L=MYPRINT,S=MYSNAP'                         *
*        IF NO PARM= IS GIVEN, DEFAULT IS ASSUMED LIKE :              *
*                   PARM='CSA,L=SYSPRINT,S=SYSSNAP'                   *
*        SPECIAL PARM= ENTRIES FOR TEST AN SQA SPACE (SUBPOOL 226) :  *
*              T=ALL TO GETMAIN/FREEMAIN THE SQA SPACE                *
*              T=GET TO GETMAIN ONLY AND NOTIFY THE SQA SPACE         *
*              T=HHHHHHHH TO FREEMAIN ONLY THE SQA SPACE GETMAIN'D    *
*                   BY A PREVIOUS EXECUTION OF T=GET                  *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
MXSTN    EQU   10                  MAXIMUM NUMBER OF SAVE-AREAS USE
*                                    TO BE GENERATED AS TANKS
WKLEN    EQU   (MXSTN*18)*4        SAVE AREAS TANKS
KB       EQU   1024                1K BYTES
         EJECT
        $DEFREG ,
        $XENT  BASE=R12,LV=WKLEN,TYPE=RENT
         EJECT
*------- INITIALIZATION
         SPACE 1
         ST    R13,SVFSVA          SAVE ADDRESS OF FIRST S.A.
         LA    R2,18*4(R13)        TANKS INITIALIZATION
         L     R3,=A(WKLEN-(18*4))
         LR    R4,R2
         XR    R5,R5
         MVCL  R2,R4
         LA    R2,MXSTN
         CVD   R2,DBLWRD
         MVC   TANKMX(L'TANKMX),DBLWRD+L'DBLWRD-L'TANKMX
         SPACE 1
*------- START PROCESSING
         SPACE 1
        @CALL  GETPARM             R1 = ADDRESS OF PARM=
         LM    R6,R7,WAADD         SET WORK AREA POINTER/SIZE
         CLI   SWSEL,0
         BE    AEREQ
        @CALL  GETVSM              GET VSM DATA
AEREQ    TM    SWITCH,PFAEL
         BZ    SETRC
        @CALL  AELIST              AE'S LIST
         SPACE 1
*------- TERMINATION
         SPACE 1
SETRC    XR    R10,R10
LEAVE   $PCLOSE
         ICM   R1,B'1111',WAADD    WORK AREA ADDRESS
         BZ    XFSQA
         L     R0,WASIZ            WORK AREA SIZE
        FREEMAIN R,A=(1),LV=(0)    FREE IT
XFSQA    TM    SWITCH,PFALL
         BO    *+L'*+8
         TM    SWITCH,PFGET
         BO    QUIT
         ICM   R1,B'1111',ACSQA
         BZ    QUIT
        $TSWXA 31,EXPAND=ONLY
        ZEROKEY ,
         L     R1,ACSQA
        FREEMAIN R,A=(1),LV=CSQAL,SP=226
        RESETKEY ,
        $TSWXA 24,EXPAND=ONLY
         TM    SWITCH,PFALL
         BO    QUIT
         UNPK  MSGFRA,ACSQA(5)
         TR    MSGFRA(L'MSGFRA-1),TRTAB-C'0'
         MVI   MSGFRA+L'MSGFRA-1,C' '
        WTO    MF=(E,MSGSQF)       LOG ADDRESS MESSAGE
QUIT    $XRET  CC=(R10),LV=WKLEN,TYPE=RENT
         SPACE 1
*------- ERRORS DIAGNOSE
         SPACE 1
STOVER   XR    R0,R0
         B     STUNDER+L'STUNDER
STUNDER  LR    R0,R13
         MVC   MSGERRT(18),=CL18'SAVE AREA''S TANKS '
         LTR   R0,R0
         BNZ   *+L'*+10
         MVC   MSGERRT+18(33),=CL33'OVERFLOW (CHECK VALUE OF "MXSTN")'
         B     *+L'*+6
         MVC   MSGERRT+18(33),=CL33'UNDERFLOW *** S C R A M B L E ***'
         B     ERRMSG
ERR1     CH    R15,=H'4'           ALL INFORMATION NOT RECEIVED R.C. ?
         BNE   *+L'*+10            NO
         MVC   MSGERRT(19),=CL19'WORK-AREA TOO SMALL'
         B     ERRMSG
         MVC   MSGERRT(26),=CL26'GET WORK-AREA ERROR / RC ='
         CVD   R15,DBLWRD          DISPLAY R.C.
         MVC   MSGERRT+26(4),=XL4'40202120'
         ED    MSGERRT+26(4),DBLWRD+6
         B     ERRMSG
ERR2     MVC   MSGERRT(20),=CL20'NO SUBPOOLS RECIEVED'
         B     ERRMSG
ERR3     MVC   MSGERRT(35),=CL35'WORK-AREA OVERFLOW AT SUBPOOL LEVEL'
         B     ERRMSG
ERR4     MVC   MSGERRT(35),=CL35'SUBPOOL DESCRIPTOR IDENTIFIER ERROR'
         B     ERRMSG
ERR5     MVC   MSGERRT(48),=CL48'WORK-AREA OVERFLOW AT NUMBER OF ALLOCAX
               TED BLOCKS'
         B     ERRMSG
ERR6     MVC   MSGERRT(48),=CL48'WORK-AREA OVERFLOW AT ALLOCATED BLOCK X
               DESCRIPTOR'
         B     ERRMSG
ERR7     MVC   MSGERRT(53),=CL53'WORK-AREA OVERFLOW AT NUMBER OF FREE SX
               PACE DESCRIPTOR'
         B     ERRMSG
ERR8     MVC   MSGERRT(43),=CL43'WORK-AREA OVERFLOW AT FREE SPACE DESCRX
               IPTOR'
         B     ERRMSG
ERR9     MVC   MSGERRT(28),=CL28'WORK-AREA DATA NOT EXHAUSTED'
         B     ERRMSG
ERRA     MVC   MSGERRT(28),=CL28'NO TCB''S IN THE PRIVATE AREA'
         B     ERRMSG
ERRB     MVC   MSGERRT(31),=CL31'WORK-AREA OVERFLOW AT TCB LEVEL'
         B     ERRMSG
ERRC     MVC   MSGERRT(46),=CL46'WORK-AREA OVERFLOW AT NUMBER OF SUBPOOX
               LS LEVEL'
         B     ERRMSG
ERRG     MVC   MSGERRT(19),=CL19'GFDATE ERROR / RC ='
         CVD   R15,DBLWRD          DISPLAY R.C.
         MVC   MSGERRT+19(4),=XL4'40202120'
         ED    MSGERRT+19(4),DBLWRD+6
         B     ERRMSG
ERRS     MVC   MSGERRT(17),=CL17'SNAP ERROR / RC ='
         CVD   R15,DBLWRD          DISPLAY R.C.
         MVC   MSGERRT+17(4),=XL4'40202120'
         ED    MSGERRT+17(4),DBLWRD+6
         B     ERRMSG
ERRX     MVC   MSGERRT(18),=CL18'PRINT ERROR / RC ='
         CVD   R15,DBLWRD          DISPLAY R.C.
         MVC   MSGERRT+18(4),=XL4'40202120'
         ED    MSGERRT+18(4),DBLWRD+6
         B     ERRMSG
ERRY     MVC   MSGERRT(26),=CL26'UNEXPECTED SELECTION ERROR'
         B     ERRMSG
ERRZ     MVC   MSGERRT(27),=CL27'INVALID PARAMETER SPECIFIED'
ERRMSG   L     R13,SVFSVA          BACK TO FIRST S.A.
        WTO    MF=(E,MSGERR)       LOG ERROR MESSAGE
         LA    R10,16
         B     LEAVE
         SPACE 1
*------- PRINT DATA ROUTINE
*              R2 = ADDRESS OF LINE TO BE PRINTED
*              R10 = LINK REGISTER
         SPACE 1
PLNE     CP    NLN,=PL2'+55'
         BL    PLNEN
         AP    NPG,=PL1'+1'
         MVC   TIT1E,=XL6'402020202120'
         ED    TIT1E,NPG
        $PRINT TIT1,,DDLIST
         LTR   R15,R15
         BNZ   ERRX
         ZAP   NLN,=PL1'+1'
         CLI   0(R2),C'0'
         BE    PLNEN
         MVI   0(R2),C'0'
         OI    SWIND,SIDSP
PLNEN   $PRINT (R2),,DDLIST
         LTR   R15,R15
         BNZ   ERRX
         AP    NLN,=PL1'+1'
         CLI   0(R2),C'0'
         BNER  R10
         AP    NLN,=PL1'+1'
         TM    SWIND,SIDSP
         BZR   R10
         MVI   0(R2),C' '
         NI    SWIND,255-SIDSP
         BR    R10
         SPACE 1
*------- SNAP WORK-AREA ROUTINE
*              R2 = WORK REGISTER
*              R3 = WORK REGISTER
*              R6 = WORK AREA ADDRESS ("WASIZ" = LENGTH)
*              R10 = LINK REGISTER
         SPACE 1
WASNAP   TM    SWIND,SISNP
         BO    WASNF
         OC    ATIOT,ATIOT
         BNZR  R10
        EXTRACT ATIOT,'S',FIELDS=(TIOT)
         L     R2,ATIOT
         USING TIODSECT,R2
         XR    R1,R1
WASNL    CLC   TIOELNGH(4),=F'0'
         BER   R10
         CLC   TIOEDDNM(L'TIOEDDNM),DDSNAP
         BE    WASNN
         IC    R1,TIOELNGH
         ALR   R2,R1
         B     WASNL
         DROP  R2
WASNN    OI    SWIND,SISNP
WASNF    LR    R2,R6
         LR    R3,R2
         AL    R3,WASIZ
         ZAP   NLS,=PL2'+99'       FORCE NEW PAGE
WASNP    LA    R0,8
         LA    R1,LNTA
WASND    UNPK  0(9,R1),0(5,R2)
         TR    0(8,R1),TRTAB-C'0'
         MVI   8(R1),C' '
         LA    R2,4(R2)
         LA    R1,L'LNTA(R1)
         BCT   R0,WASND
         CP    NLS,=PL2'+55'
         BL    WASNE
         AP    LPG,=PL1'+1'
         MVC   TIT1E,=XL6'402020202120'
         ED    TIT1E,LPG
        $PRINT TIT1,,DDSNAP
         LTR   R15,R15
         BNZ   ERRS
         ZAP   NLS,=PL1'+1'
         MVI   LNT,C'0'
WASNE   $PRINT LNT,,DDSNAP
         LTR   R15,R15
         BNZ   ERRS
         AP    NLS,=PL1'+1'
         CLR   R2,R3
         BNLR  R10
         CLC   0(32,R2),=XL32'0'
         BER   R10
         MVI   LNT,C' '
         B     WASNP
         EJECT
DBLWRD   DC    D'0'                DOUBLE WORD WORK AREA
SVFSVA   DC    A(*-*)              ADDRESS OF FIRST S.A.
ATIOT    DC    F'0'
ACSQA    DC    A(*-*)
CSQAL    EQU   64
WAADD    DC    F'0'                WORK AREA ADDRESS
WASIZ    DC    A(20*KB)            WORK AREA SIZE
DDLIST   DC    CL8'SYSPRINT'
DDSNAP   DC    CL8'SYSSNAP'
TRTAB    DC    CL16'0123456789ABCDEF'
TANKMX   DC    PL2'+0'
TANKNO   DC    PL2'+0'
TANKUP   DC    PL1'+1'
NPG      DC    PL3'+0'
NLN      DC    PL2'+0'
LPG      DC    PL3'+0'
NLS      DC    PL2'+0'
SWSEL    DC    AL1(SCSA)
SCSA     EQU   X'80'
SSQA     EQU   X'40'
SLSQA    EQU   X'20'
SPVT     EQU   X'10'
SWITCH   DC    XL1'0'
PFPVT    EQU   X'80'
PFAEL    EQU   X'40'
PFGET    EQU   X'02'
PFALL    EQU   X'01'
SWIND    DC    XL1'0'
SISNP    EQU   X'80'
SIDSP    EQU   X'08'
SILSI    EQU   X'04'
STSEL    DS    0XL5
         DC    CL4'CSA',AL1(SCSA)
         DC    CL4'SQA',AL1(SSQA)
         DC    CL4'LSQA',AL1(SLSQA)
         DC    CL4'PVT',AL1(SPVT)
NTSEL    EQU   (*-STSEL)/L'STSEL
         SPACE 1
         DC    AL1(TIT1L)
TIT1     DC    CL11'1 -- VSM : '
TIT1A    DC    CL12'analysis of '
TIT1B    DC    CL4' ',CL4' -- '
TIT1C    DC    CL20' ',CL2'/ '
TIT1D    DC    CL8'  :  :  ',CL9' --- Page'
TIT1E    DC    CL6' ',CL3' --'
TIT1L    EQU   *-TIT1
         SPACE 1
         DC    AL1(LNTL)
LNT      DC    CL7' '
LNTA     DC    8CL9' '
LNTL     EQU   *-LNT
         SPACE 1
         DC    AL1(LNPL)
LNP      DC    CL10'0 a.TCB : '
LNPA     DC    CL9' ',C'('
LNPB     DC    CL8' ',C'/'
LNPC     DC    CL8' ',C'/'
LNPD     DC    CL8' ',C')'
LNPL     EQU   *-LNP
         SPACE 1
         DC    AL1(LNDL)
LND      DC    CL10' ',CL8'address '
LNDA     DC    CL9' ',CL7'length '
LNDB     DC    CL9' '
LNDC     DC    CL8' '
LNDL     EQU   *-LND
         SPACE 1
MSGSQF  WTO    '--- VSMANAL - SQA (SP=226) ADDRESS :          - FREEMAIX
               N DONE ---',ROUTCDE=11,DESC=7,MF=L
MSGFRA   EQU   MSGSQF+41,9
         SPACE 1
MSGERR  WTO    '--- VSMANAL -                                          X
                              ',ROUTCDE=11,DESC=7,MF=L
MSGERRT  EQU   MSGERR+18,56
         EJECT
         LTORG
         EJECT
*------- HANDLE REQUEST(S)
         SPACE 1
GETPARM @ENTER ,
         L     R2,0(R1)            GET PARM= IF ANY
         LH    R1,0(R2)
         LTR   R1,R1
         BNP   NOPARM
         NI    SWSEL,255-SCSA
         LA    R4,2(R2)
         LA    R3,1(R2,R1)
         LA    R2,1
SPNXT    LA    R5,DBLWRD
         MVC   DBLWRD(8),=CL8' '
         CLC   0(2,R4),=CL2'T='
         BE    TSTST
         CLC   0(2,R4),=CL2'L='
         BNE   TSSNP
         LA    R6,DDLIST
         B     GTPRM
TSSNP    CLC   0(2,R4),=CL2'S='
         BNE   TSREQ
         LA    R6,DDSNAP
GTPRM    ALR   R4,R2
         BXLE  R4,R2,*+L'*+4
         B     ERRZ
         LA    R0,8+1
         BAS   R14,SCPRM
         MVC   0(8,R6),DBLWRD
         B     SPSKP
SCPRM    CLI   0(R4),C','
         BNE   *+L'*+12
         CLC   DBLWRD(4),=CL8' '
         BE    SPSKP
         BR    R14
         BCT   R0,*+L'*+4
         B     ERRZ
         MVC   0(1,R5),0(R4)
         LA    R5,1(R5)
         BXLE  R4,R2,SCPRM
         BR    R14
TSREQ    LA    R0,4+1
         BAS   R14,SCPRM
         LA    R0,NTSEL
         LA    R1,STSEL
         CLC   DBLWRD(4),0(R1)
         BE    STREQ
         LA    R1,L'STSEL(R1)
         BCT   R0,*-14
         CLC   DBLWRD(4),=CL4'AE'
         BNE   ERRZ
         OI    SWITCH,PFAEL
         B     SPSKP
TSTST    ALR   R4,R2
         BXLE  R4,R2,*+L'*+4
         B     ERRZ
         LA    R0,8+1
         BAS   R14,SCPRM
         CLC   DBLWRD(8),=CL8'ALL'
         BNE   *+L'*+8
         OI    SWITCH,PFALL
         B     SPSKP
         CLC   DBLWRD(8),=CL8'GET'
         BNE   *+L'*+8
         OI    SWITCH,PFGET
         B     SPSKP
         CLC   DBLWRD(8),=CL8' '
         BE    SPSKP
         LA    R0,L'DBLWRD
         LA    R5,DBLWRD
         XR    R14,R14
         XR    R15,R15
TSTST1   CLI   0(R5),C'9'
         BH    ERRZ
         CLI   0(R5),C'0'
         BL    TSTST2
         ICM   R15,B'1000',0(R5)
         SLL   R15,4
         SLDL  R14,4
         B     TSTST3
TSTST2   CLI   0(R5),C'F'
         BH    ERRZ
         CLI   0(R5),C'A'
         BL    ERRZ
         IC    R15,0(R5)
         AH    R15,=H'9'
         SLL   R15,28
         SLDL  R14,4
TSTST3   LA    R5,1(R5)
         BCT   R0,TSTST1
         ST    R14,ACSQA
         B     SPSKP
STREQ    OC    SWSEL(1),4(R1)
SPSKP    BXLE  R4,R2,SPNXT
         CLI   SWSEL,0
         BNE   NOPARM
         TM    SWITCH,PFAEL
         BNZ   XFRSQA
         B     ERRZ
NOPARM   L     R0,WASIZ            WORK AREA SIZE
        GETMAIN R,LV=(0)           OBTAIN IT
         ST    R1,WAADD            WORK AREA ADDRESS
XFRSQA   ICM   R1,B'1111',ACSQA
         BZ    XGPARM
        $TSWXA 31,EXPAND=ONLY
        ZEROKEY ,
         L     R1,ACSQA
        FREEMAIN R,A=(1),LV=CSQAL,SP=226
        RESETKEY ,
        $TSWXA 24,EXPAND=ONLY
         UNPK  MSGFRA,ACSQA(5)
         TR    MSGFRA(L'MSGFRA-1),TRTAB-C'0'
         MVI   MSGFRA+L'MSGFRA-1,C' '
        WTO    MF=(E,MSGSQF)       LOG ADDRESS MESSAGE
         XC    ACSQA,ACSQA
XGPARM  @EXIT  ,
         SPACE 1
        LTORG
         EJECT
*------- GET VSM DATA
         SPACE 1
GETVSM  @ENTER ,
         TM    SWITCH,PFALL+PFGET
         BZ    NXTRQ
        $TSWXA 31,EXPAND=ONLY
        ZEROKEY ,
        GETMAIN R,LV=CSQAL,SP=226
         ST    R1,ACSQA
        RESETKEY ,
        $TSWXA 24,EXPAND=ONLY
         TM    SWITCH,PFGET
         BZ    NXTRQ
         UNPK  MSGADD,ACSQA(5)
         TR    MSGADD(L'MSGADD-1),TRTAB-C'0'
         MVI   MSGADD+L'MSGADD-1,C' '
        WTO    MF=(E,MSGSQA)       LOG ADDRESS MESSAGE
NXTRQ    LR    R2,R6               CLEAR
         LR    R3,R7                 VSMLIST
         LR    R4,R6                   WORK AREA
         XR    R5,R5                     WITH
         MVCL  R2,R4                       ZEROS
         NI    SWITCH,255-PFPVT
        $TSWXA 31,EXPAND=ONLY      ENTER 31-BIT MODE
         TM    SWSEL,SCSA
         BZ    TFSQA
        VSMLIST SP=CSA,SPACE=FREE,WKAREA=((R6),(R7))
         B     PRCSS
TFSQA    TM    SWSEL,SSQA
         BZ    TFLSQA
        VSMLIST SP=SQA,SPACE=FREE,WKAREA=((R6),(R7))
         B     PRCSS
TFLSQA   TM    SWSEL,SLSQA
         BZ    RQPVT
        VSMLIST SP=LSQA,SPACE=FREE,WKAREA=((R6),(R7))
         B     PRCSS
         USING PSA,R0              PSA ADDRESSABILITY.
RQPVT    L     R2,PSAAOLD          GET ASCB ADDRESS
         DROP  R0
         USING ASCB,R2
         L     R1,ASCBASXB         GET ASXB ADDRESS
         DROP  R2
         USING ASXB,R1
         L     R2,ASXBFTCB         GET FIRST TCB ADDRESS ON QUEUE
         DROP  R1
        VSMLIST SP=PVT,SPACE=FREE,WKAREA=((R6),(R7)),TCB=((R2),ALL)
         OI    SWITCH,PFPVT
PRCSS   $TSWXA 24,EXPAND=ONLY      BACK TO 24-BIT MODE
         LTR   R15,R15             SUCCESSFULLY EXECUTED ?
         BNZ   ERR1                NO
         SPACE 1
*------- LIST DATA FROM VSM
         SPACE 1
        $GFDATE TIT1C,DBLWRD
         LTR   R15,R15
         BNZ   ERRG
         MVC   TIT1D(2),DBLWRD
         MVC   TIT1D+3(2),DBLWRD+2
         MVC   TIT1D+6(2),DBLWRD+4
         TM    SWIND,SILSI
         BO    NOINF
        @CALL  LSINFO              LIST INFORMATION
NOINF    ZAP   NLN,=PL2'+99'       FORCE NEW PAGE
         LA    R0,NTSEL
         LA    R1,STSEL
         XR    R2,R2
         IC    R2,4(R1)
         EX    R2,*+L'*+16
         BO    *+L'*+20
         LA    R1,L'STSEL(R1)
         BCT   R0,*-16
         B     ERRY
         TM    SWSEL,*-*
         NI    SWSEL,*-*
         LA    R0,255
         XR    R2,R0
         EX    R2,*-10
         MVC   TIT1B(4),0(R1)
         BAS   R10,WASNAP          SNAP DATA IF REQUESTED
         L     R3,4(R6)            GET ADDRESS OF DATA
         L     R5,8(R6)            GET LENGTH OF DATA
         AR    R5,R3               END OF DATA+1 ADDRESS
         TM    SWITCH,PFPVT
         BZ    NPVT1
         XC    NTCBS,NTCBS
         OC    NTCBS(L'NTCBS),0(R3) GET NUMBER OF TCB'S
         BZ    ERRA                NONE ? YES
         LA    R3,4(R3)            BUMP CURRENT POINTER
NXTCB    CLR   R3,R5               OVERFLOW ?
         BNL   ERRB                YES
         UNPK  LNPA,0(5,R3)        TCB ADDRESS
         TR    LNPA(8),TRTAB-C'0'
         MVI   LNPA+L'LNPA-1,C' '
         ICM   R1,B'1111',0(R3)
         USING TCB,R1
         L     R1,TCBTIO
         DROP  R1
         USING TIODSECT,R1
         MVC   LNPB,TIOCNJOB
         MVC   LNPC(8),TIOCSTEP
         MVC   LNPD(8),TIOCSTEP+8
         DROP  R1
         LA    R2,LNP
         BAS   R10,PLNE
         LA    R3,4(R3)            BUMP CURRENT POINTER
         CLR   R3,R5               OVERFLOW ?
         BNL   ERRC                YES
NPVT1    L     R8,0(R3)            GET NUMBER OF SUBPOOLS
         LTR   R8,R8               NONE ?
         BNP   ERR2                YES
         LA    R3,4(R3)            BUMP CURRENT POINTER
LOOP1    CLR   R3,R5               OVERFLOW ?
         BNL   ERR3                YES
         USING SDF,R3
         CLI   SDFIS,X'00'         SUBPOOL DESCRIPTOR IDENTIFIER ?
         BNE   ERR4
         XR    R0,R0               GET SUBPOOL ID NUMBER
         IC    R0,SDFID
         CVD   R0,DBLWRD
         MVC   LNSA,=XL4'40202120'
         ED    LNSA,DBLWRD+6
         XR    R0,R0               GET STORAGE KEY
         IC    R0,SDFKF
         SRL   R0,4
         CVD   R0,DBLWRD
         MVC   DBLWRD(4),=XL4'40202120'
         ED    DBLWRD(4),DBLWRD+6
         MVI   LNSB,C' '
         MVC   LNSB+1(L'LNSB-1),LNSB
         CLI   DBLWRD+2,C' '
         BNE   *+L'*+14
         MVC   LNSB(2),DBLWRD+2
         LA    R2,LNSB+2
         B     *+L'*+10
         MVC   LNSB(3),DBLWRD+1
         LA    R2,LNSB+3
         TM    SDFKF,FGTCB
         BZ    *+L'*+10
         MVC   0(5,R2),=CL5', TCB'
         LA    R2,5(R2)
         TM    SDFKF,FGSHR
         BZ    *+L'*+10
         MVC   0(5,R2),=CL5', SHR'
         LA    R2,5(R2)
         MVC   0(9,R2),=CL9') a.TCB ='
         UNPK  10(9,R2),SDFTCB(5)
         TR    10(8,R2),TRTAB-C'0'
         MVI   18(R2),C' '
         ICM   R1,B'1111',SDFTCB
         BZ    NOTTCB
         USING TCB,R1
         L     R1,TCBTIO
         DROP  R1
         USING TIODSECT,R1
         MVI   19(R2),C'('
         MVC   20(L'TIOCNJOB,R2),TIOCNJOB
         MVI   20+L'TIOCNJOB(R2),C'/'
         MVC   21+L'TIOCNJOB(8,R2),TIOCSTEP
         MVI   29+L'TIOCNJOB(R2),C'/'
         MVC   30+L'TIOCNJOB(8,R2),TIOCSTEP+8
         MVI   38+L'TIOCNJOB(R2),C')'
         DROP  R1
NOTTCB   LA    R2,LNS
         BAS   R10,PLNE
         XR    R0,R0
         IC    R0,SDFLG            LENGTH OF SUBPOOL DESCRIPTOR
         DROP  R3
         AR    R3,R0               BUMP CURRENT PTR
         CLR   R3,R5               TEST IF DATA OVERFLOW
         BNL   ERR5                YES
         ICM   R9,B'1111',0(R3)    GET NUMBER OF ALLOCATED BLOCKS
         LA    R3,4(R3)            BUMP CURRENT POINTER
         LTR   R9,R9               HOW MANY ALLOCATED BLOCKS ?
         BNP   NOBLKS              BRANCH IF NONE
LOOP2    CLR   R3,R5               TEST IF DATA OVERFLOW
         BNL   ERR6                YES
         USING AFUF,R3
         MVC   DBLWRD(4),AFUFVA    VIRTUAL ADDRESS OF ALLOC. BLOCK
         NI    DBLWRD,X'7F'
         UNPK  LNDA(9),DBLWRD(5)
         TR    LNDA(8),TRTAB-C'0'
         MVI   LNDA+L'LNDA-1,C' '
         MVC   DBLWRD(4),AFUFLG    LENGTH OF ALLOCATED BLOCK
         UNPK  LNDB(9),DBLWRD(5)
         TR    LNDB(8),TRTAB-C'0'
         MVI   LNDB+L'LNDB-1,C' '
         TM    AFUFVA,X'80'
         BZ    *+L'*+10
         MVC   LNDC,=CL8'Real>16M'
         BZ    *+L'*+6
         MVC   LNDC,=CL8' '
         DROP  R3
         LA    R2,LND
         BAS   R10,PLNE
         LA    R3,LAFUF(R3)        BUMP CURRENT POINTER,
         CLR   R3,R5               TEST IF OVERFLOW
         BNL   ERR7                YES
         ICM   R4,B'1111',0(R3)    GET NUMBER OF FREE SPACE DESC.
         LA    R3,4(R3)            BUMP CURRENT POINTER
         LTR   R4,R4               HOW MANY FREE SPACE DESC. ?
         BNP   NOFREE              BRANCH IF NONE
         MVC   LNFA,=CL6'Free :'
LOOP3    CLR   R3,R5               DATA OVERFLOW ?
         BNL   ERR8                YES
         USING AFUF,R3
         MVC   DBLWRD(4),AFUFVA    VIRTUAL ADDRESS OF FREE SPACE
         UNPK  LNFB(9),DBLWRD(5)
         TR    LNFB(8),TRTAB-C'0'
         MVI   LNFB+L'LNFB-1,C' '
         MVC   DBLWRD(4),AFUFLG    LENGTH OF FREE SPACE
         UNPK  LNFC(9),DBLWRD(5)
         TR    LNFC(8),TRTAB-C'0'
         MVI   LNFC+L'LNFC-1,C' '
         DROP  R3
         LA    R2,LNF
         BAS   R10,PLNE
         MVC   LNFA(L'LNFA),=CL8' '
         LA    R3,LAFUF(R3)        BUMP CURRENT POINTER
         BCT   R4,LOOP3            LOOP IF MORE FREE SPACE DESC.
NOFREE   BCT   R9,LOOP2            LOOP IF MORE ALLOCATED BLOCKS
NOBLKS   BCT   R8,LOOP1            LOOP IF MORE SUBPOOLS
         TM    SWITCH,PFPVT
         BZ    NPVT2
         L     R1,NTCBS
         BCT   R1,*+L'*+4
         B     NPVT2
         ST    R1,NTCBS
         B     NXTCB
NPVT2    CLR   R3,R5               DATA EXHAUSTED ?
         BNE   ERR9                NO, ERROR
         CLI   SWSEL,0             YES, ALL SPECIFIED REQUESTS DONE ?
         BNE   NXTRQ               NO, CONTINUE
        @EXIT  ,
         SPACE 1
NTCBS    DC    F'0'
         SPACE 1
MSGSQA  WTO    '--- VSMANAL - SQA (SP=226) ADDRESS :          - GETMAINX
                DONE ---',ROUTCDE=11,DESC=7,MF=L
MSGADD   EQU   MSGSQA+41,9
         SPACE 1
         DC    AL1(LNSL)
LNS      DC    CL17'0    Subpool ID :'
LNSA     DC    CL4' ',CL7' (key ='
LNSB     DC    CL60' '
LNSL     EQU   *-LNS
         SPACE 1
         DC    AL1(LNFL)
LNF      DC    CL15' '
LNFA     DC    CL6' ',CL9' address '
LNFB     DC    CL9' ',CL7'length '
LNFC     DC    CL9' '
LNFL     EQU   *-LNF
         SPACE 1
        LTORG
         EJECT
*------- LIST ALLOCATED ELEMENTS
         SPACE 1
AELIST  @ENTER ,
        $GFDATE TIT1C,DBLWRD
         LTR   R15,R15
         BNZ   ERRG
         MVC   TIT1D(2),DBLWRD
         MVC   TIT1D+3(2),DBLWRD+2
         MVC   TIT1D+6(2),DBLWRD+4
         MVC   TIT1B(4),=CL4'AE'
         ZAP   NLN,=PL2'+99'       FORCE NEW PAGE
        $TSWXA 31,EXPAND=ONLY      ENTER 31-BIT MODE
         USING PSA,R0              PSA ADDRESSABILITY.
         L     R1,PSAAOLD          GET ASCB ADDRESS
         DROP  R0
         USING ASCB,R1
         L     R2,ASCBASXB         GET ASXB ADDRESS
         DROP  R1
         USING ASXB,R2
         L     R3,ASXBFTCB         GET FIRST TCB ADDRESS ON QUEUE
         L     R4,ASXBLTCB         GET LAST TCB ADDRESS ON QUEUE
         DROP  R2
         USING TCB,R3
NXTTCB   ST    R3,DBLWRD
         UNPK  LNPA,DBLWRD(5)      TCB ADDRESS
         TR    LNPA(8),TRTAB-C'0'
         MVI   LNPA+L'LNPA-1,C' '
         L     R1,TCBTIO
         USING TIODSECT,R1
         MVC   LNPB,TIOCNJOB
         MVC   LNPC(8),TIOCSTEP
         MVC   LNPD(8),TIOCSTEP+8
         DROP  R1
        $TSWXA 24,EXPAND=ONLY      BACK TO 24-BIT MODE
         LA    R2,LNP
         BAS   R10,PLNE
        $TSWXA 31,EXPAND=ONLY      ENTER 31-BIT MODE
         ICM   R5,B'1111',TCBAE
         BZ    *+L'*+10
         MVC   LNDC,=CL8'<--- AE '
         BAS   R9,NXTAE
         ICM   R5,B'1111',TCBEAE
         BZ    *+L'*+10
         MVC   LNDC,=CL8'<--- EAE'
         BAS   R9,NXTAE
         CLR   R3,R4
         BE    AEEND
         L     R3,TCBTCB
         B     NXTTCB
         DROP  R3
         USING AE,R5
NXTAE    MVC   DBLWRD(4),AEAREA    ADDRESS OF ALLOC. BLOCK
         UNPK  LNDA(9),DBLWRD(5)
         TR    LNDA(8),TRTAB-C'0'
         MVI   LNDA+L'LNDA-1,C' '
         MVC   DBLWRD(4),AESIZE    LENGTH OF ALLOCATED BLOCK
         UNPK  LNDB(9),DBLWRD(5)
         TR    LNDB(8),TRTAB-C'0'
         MVI   LNDB+L'LNDB-1,C' '
         L     R5,AENEXT
         DROP  R5
        $TSWXA 24,EXPAND=ONLY      BACK TO 24-BIT MODE
         LA    R2,LND
         BAS   R10,PLNE
         MVC   LNDC,=CL8' '
        $TSWXA 31,EXPAND=ONLY      ENTER 31-BIT MODE
         LTR   R5,R5
         BNZ   NXTAE
         BR    R9
AEEND   $TSWXA 24,EXPAND=ONLY      BACK TO 24-BIT MODE
        @EXIT  ,
         SPACE 1
        LTORG
         EJECT
*------- LIST INFORMATION
         SPACE 1
LSINFO  @ENTER ,
         OI    SWIND,SILSI
         ZAP   NLN,=PL2'+99'       FORCE NEW PAGE
         MVC   TIT1A(L'TIT1A+L'TIT1B),=CL16'some information'
         LA    R3,LNI-1
         XR    R4,R4
         LA    R2,1(R3)
         BAS   R10,PLNE
         IC    R4,0(R3)
         LA    R3,1(R3,R4)
         CLI   0(R3),0
         BNE   *-20
         MVC   TIT1A,=CL12'analysis of '
        @EXIT  ,
         SPACE 1
        LTORG
         SPACE 1
         DC    AL1(L'LNI)
LNI      DC    C'0 MVS/XA storage protection keys assignment :'
         DC    AL1(L'LNI0)
LNI0     DC    C'  ==========================================='
         DC    AL1(L'LNI1)
LNI1     DC    C'0     Key :     0 Supervisor and other System functionX
               s'
         DC    AL1(L'LNI2)
LNI2     DC    C'      -----     1 Job Scheduler and Job Entry sub-systX
               em'
         DC    AL1(L'LNI3)
LNI3     DC    C'                2 VSPC'
         DC    AL1(L'LNI4)
LNI4     DC    C'              3-4 Reserved'
         DC    AL1(L'LNI5)
LNI5     DC    C'                5 Data Management, including OPEN/CLOSX
               E/EOV'
         DC    AL1(L'LNI6)
LNI6     DC    C'                6 TCAM and VTAM'
         DC    AL1(L'LNI7)
LNI7     DC    C'                7 IMS'
         DC    AL1(L'LNI8)
LNI8     DC    C'                8 All V=V problem programs'
         DC    AL1(L'LNI9)
LNI9     DC    C'             9-15 V=R problem programs'
         DC    AL1(L'LNI10)
LNI10    DC    C'0 MVS/XA storage subpools numbers types :'
         DC    AL1(L'LNI11)
LNI11    DC    C'  ======================================='
         DC    AL1(L'LNI1A+L'LNI1B)
LNI1A    DC    CL40'0      0 - 127 : PRIVATE - LOW'
LNI1B    DC    CL40'|          238 : Reserved (error)'
         DC    AL1(L'LNI1C+L'LNI1D)
LNI1C    DC    CL40'           128 : Reserved (error)'
LNI1D    DC    CL40'|          239 : COMMON - SQA/ESQA'
         DC    AL1(L'LNI1E+L'LNI1F)
LNI1E    DC    CL40'     129 - 225 : Undefined'
LNI1F    DC    CL40'|          240 : PRIVATE - LOW'
         DC    AL1(L'LNI1G+L'LNI1H)
LNI1G    DC    CL40'           226 : COMMON - SQA'
LNI1H    DC    CL40'|          241 : COMMON - CSA/ECSA'
         DC    AL1(L'LNI1I+L'LNI1J)
LNI1I    DC    CL40'     227 - 228 : COMMON - CSA/ECSA'
LNI1J    DC    CL40'|    242 - 244 : Reserved (error)'
         DC    AL1(L'LNI1K+L'LNI1L)
LNI1K    DC    CL40'     229 - 230 : PRIVATE - HIGH'
LNI1L    DC    CL40'|          245 : COMMON - SQA/ESQA'
         DC    AL1(L'LNI1M+L'LNI1N)
LNI1M    DC    CL40'           231 : COMMON - CSA/ECSA'
LNI1N    DC    CL40'|    246 - 249 : Reserved (error)'
         DC    AL1(L'LNI1O+L'LNI1P)
LNI1O    DC    CL40'           232 : Reserved (error)'
LNI1P    DC    CL40'|    250 - 252 : PRIVATE - LOW'
         DC    AL1(L'LNI1Q+L'LNI1R)
LNI1Q    DC    CL40'     233 - 235 : PRIVATE - LSQA/ELSQA'
LNI1R    DC    CL40'|    253 - 255 : PRIVATE - LSQA/ELSQA'
         DC    AL1(L'LNI1S+L'LNI1T)
LNI1S    DC    CL40'     236 - 237 : PRIVATE - HIGH'
LNI1T    DC    CL40'| '
         SPACE 1
         DC    AL1(0)              END OF LINES INDICATOR
         EJECT
        $SDF
         SPACE 1
        $AFUF
         SPACE 2
        PRINT  NOGEN
        IHAPSA
        IHAASCB
        IHAASXB
        IKJTCB LIST=YES
        IHAAE
AETLEN   EQU   *-AE
TIODSECT DSECT
        IEFTIOT1
         SPACE 2
         END
