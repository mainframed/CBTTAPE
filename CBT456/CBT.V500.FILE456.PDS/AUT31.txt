AU31     TITLE '-- TSO-CP AUT30 - ROUTINE AUT31 --'
AUT31    START 0
         SPACE 1
**********************************************************************
*        THIS SUBROUTINE IS CALLED BY PROGRAM 'AUT30'.               *
*        I CREATE A NEW AUTHORIZATION.                               *
*                                             M.SACRE  01 OCTOBRE 84 *
*                                          P.A.MOINIL  03 OCTOBRE 89 *
**********************************************************************
*        CALL AUT31,(PARM1,PARM2),VL                                 *
*        FUNCTION : SUBROUTINE TO CREATE A NEW AUTHORIZATION.        *
*        AT ENTRY : R1  = PARM. ADDRESS                              *
*                         PARM. --->   A(PARM1,PARM2)                *
*                 WHERE : PARM1 = AUTHOR. NUMBER IN EBCDIC (CL4)     *
*                         PARM2 = AUTHOR. NUMBER IN BINARY (H)       *
*         AT EXIT : R15 = 0 - NORMAL COMPLETION                      *
*                         WITH R1 = ADDRESS OF TERMINATION MESSAGE   *
*                         TEXT, IT POINTS AS BELOW :                 *
*                                    DC  AL1(L'TEXT)                 *
*                         ---> TEXT  DC  C'...MESSAGE...'            *
*                         4 - END REQUESTED                          *
*                         8 - SEVERE ERROR.                          *
*--------------------------------------------------------------------*
         SPACE 1
*---------------------------------------------------------------------*
*        AUTHORITY LEVELS DEFINITIONS.                                *
*---------------------------------------------------------------------*
         SPACE 1
LV10     EQU   10        REDUCED SERVICES LEVEL
         SPACE 1
         PRINT NOGEN
        $DEFREG
        $XENT  BASE=R10
         SPACE 1
         LM    R3,R4,0(R1)             LOAD ADDRESS OF PARAMETERS
         MVC   SCRAUT(L'SCRAUT),0(R3)  GET AUT NBR EBCDIC
         MVC   ARGUMAUT(L'ARGUMAUT),0(R4)  GET AUT NBR BINARY
         LA    R2,AREAR                SET ADRESSABILITY OF DSECT
         USING AUTDSECT,R2
         LA    R3,AREAP                SET ADRESSABILITY OF DSECT
         USING PRGDSECT,R3
         EJECT
*------- INITIALIZE/RESET ALL DISPLAY INPUT FIELDS
         OI    SWCRT+1,X'F0'           SET SWITCH TO NO CREATE
         MVC   SCRFICH(L'SCRFICH),=CL10' '
         MVC   SCRDTEI(L'SCRDTEI),=CL10' '
         MVC   SCRDTEO(L'SCRDTEO),=CL10' '
         MVC   SCRPR(L'SCRPR),=CL10' '
         MVI   SCRPI,C'4'
         MVI   SCRNOM,C' '
         MVC   SCRNOM+1(L'SCRNOM-1),SCRNOM
         MVI   ECRANM,C' '
         MVC   ECRANM+1(L'ECRANM-1),ECRANM
         SPACE 1
*--------------------------------------------------------------------*
*        DISPLAY SCREEN ET LECTURE DES INFORMATIONS                  *
*--------------------------------------------------------------------*
*        DESCRIPTION DU BUFFER D'INPUT :
*        -------------------------------
*        LES CHAMPS SONT DANS L'INPUT AREA ET SONT DANS L'ORDRE
*        SUIVANT S'ILS SONT TOUS PRESENTS (MODIFIES) :
*         1 = FICHE PROGRAMME (CL10)
*         2 = DEBUT VALIDATION (CL10) JJ/MM/AAAA
*         3 = FIN VALIDATION (CL10) JJ/MM/AAAA
*         4 = NUMERO PROG. RESP (CL4)
*         5 = INCR./DECR. PRIORITE (CL1)
         SPACE 1
DSPLAY   OI    SWCTL+1,X'F0'           SET SWITCH TO DO CONTROL
         LA    R1,PECRAN               DISPLAY SCREEN
         L     R15,=V(AUT99)
         BALR  R14,R15
         LTR   R15,R15                 NORMAL COMPLETION ?
         BNZ   LEAVE                   NO, END CP (ONLY 4)
         MVI   ECRANM,C' '
         MVC   ECRANM+1(L'ECRANM-1),ECRANM
         L     R15,=V(AUT99R)          GET RESPONSE(S)
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BNZ   *+L'*+12                IF NOT, GO PROCESS
SWCRT    B     EXNONE                  ESCAPE IF NONE FIRST TIME
         NI    SWCTL+1,X'0F'           SET SWITCH TO ALLOW WRITE
         B     STCTRL                  GO DO A FINAL CONTROL
         NI    SWCRT+1,X'0F'           SET SWITCH TO CREATE
         EJECT
*------- STORE AND TRANSLATE ALL INPUT FROM DISPLAY IN UPPER CASE
         CLC   0(3,R15),SBAFICH
         BNE   NOTFICH
         LTR   R1,R1
         BZ    FICHOK                  IGNORE IF CLEAR
         MVC   SCRFICH(L'SCRFICH),=CL10' '
         LA    R0,L'SCRFICH
         CLR   R1,R0
         BNH   *+L'*+2
         LR    R1,R0
         BCT   R1,*+L'*+6
         OC    SCRFICH(*-*),3(R15)     TRANSLATE LOWER IN UPPER
         EX    R1,*-6
         CLC   SCRFICH+1(L'SCRFICH-1),=CL10' '
         BNE   FICHOK
         CLI   SCRFICH,C'X'            ESCAPE REQUESTED ?
         BE    EXREQ                   YES
FICHOK   L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    STCTRL                  IF YES, ALL CHANGE RECEIVED
NOTFICH  CLC   0(3,R15),SBADTEI
         BNE   NOTDTEI
         LTR   R1,R1
         BZ    DTEIOK                  IGNORE IF CLEAR
         MVC   SCRDTEI(L'SCRDTEI),=CL10' '
         LA    R0,L'SCRDTEI
         CLR   R1,R0
         BNH   *+L'*+2
         LR    R1,R0
         BCT   R1,*+L'*+6
         OC    SCRDTEI(*-*),3(R15)     TRANSLATE LOWER IN UPPER
         EX    R1,*-6
DTEIOK   L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    STCTRL                  IF YES, ALL CHANGE RECEIVED
NOTDTEI  CLC   0(3,R15),SBADTEO
         BNE   NOTDTEO
         LTR   R1,R1
         BZ    DTEOOK                  IGNORE IF CLEAR
         MVC   SCRDTEO(L'SCRDTEO),=CL10' '
         LA    R0,L'SCRDTEO
         CLR   R1,R0
         BNH   *+L'*+2
         LR    R1,R0
         BCT   R1,*+L'*+6
         OC    SCRDTEO(*-*),3(R15)     TRANSLATE LOWER IN UPPER
         EX    R1,*-6
DTEOOK   L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    STCTRL                  IF YES, ALL CHANGE RECEIVED
NOTDTEO  CLC   0(3,R15),SBAPR
         BNE   NOTPR
         LTR   R1,R1
         BZ    PROK                    IGNORE IF CLEAR
         MVC   SCRPR(L'SCRPR),=CL10' '
         LA    R0,L'SCRPR
         CLR   R1,R0
         BNH   *+L'*+2
         LR    R1,R0
         BCT   R1,*+L'*+6
         OC    SCRPR(*-*),3(R15)       TRANSLATE LOWER IN UPPER
         EX    R1,*-6
PROK     L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    STCTRL                  IF YES, ALL CHANGE RECEIVED
NOTPR    CLC   0(3,R15),SBAPI
         BNE   NOTPI
         L     R4,=V(AUT98T)
         CLI   0(R4),LV10
         BNH   NOTALL
         LTR   R1,R1
         BNZ   *+L'*+8
         MVI   SCRPI,C'4'              SET DEFAULT
         B     PIOK
         MVC   SCRPI(L'SCRPI),3(R15)
         CLI   SCRPI,C'0'              TEST IF NUMERIC
         BL    *+L'*+8
         CLI   SCRPI,C'9'
         BNH   PIOK
         MVC   ECRANM(L'ERROR11),ERROR11  SET ERROR MESSAGE
         B     DSPLAY                  GO TO DISPLAY SCREEN
NOTALL   MVC   ECRANM(L'ERROR12),ERROR12  SET ERROR MESSAGE
         B     DSPLAY                  GO TO DISPLAY SCREEN
PIOK     L     R15,=V(AUT99R)          GET NEXT FIELD
         BALR  R14,R15
         LTR   R15,R15                 ONLY END OF TEXT ?
         BZ    STCTRL                  IF YES, ALL CHANGE RECEIVED
NOTPI    MVC   ECRANM(L'ERROR0),ERROR0 SET ERROR MESSAGE
         B     DSPLAY                  GO TO DISPLAY SCREEN
         EJECT
*--------------------------------------------------------------------*
*        CONTROLE ET VALIDATION DES DONNEES LUES SUR LE DISPLAY.     *
*--------------------------------------------------------------------*
         SPACE 1
STCTRL   CLC   SCRFICH(L'SCRFICH),=CL10' '  FICHE PROGR.
         BNE   *+L'*+10                OK, NOT BLANK
         MVC   ECRANM(L'ERROR14),ERROR14  SET ERROR MESSAGE
         B     DSPLAY                  GO TO DISPLAY SCREEN
*        LA    R0,L'SCRFICH
*        LA    R1,SCRFICH+L'SCRFICH
*        BCTR  R1,0
*        CLI   0(R1),C'0'              TEST IF NUMERIC
*        BL    WRAUTH
*        CLI   0(R1),C'9'
*        BH    WRAUTH
*        BCT   R0,*-18
*        B     TESTDV
WRAUTH   EQU   *
*        MVC   ECRANM(L'ERROR1),ERROR1 SET ERROR MESSAGE
*        B     DSPLAY                  GO TO DISPLAY SCREEN
         EJECT
*--------------------------------------------------------------------*
*        CONTROLE DE LA DATE DE VALIDATION DE L'AUTORISATION.        *
*        (DATE DE DEBUT)                                             *
*--------------------------------------------------------------------*
         SPACE 1
TESTDV   MVC   MDY(2),SCRDTEI+3        MOVE MM IN WORK FIELD
         MVC   MDY+2(2),SCRDTEI        MOVE DD IN WORK FIELD
         MVC   MDY+4(4),SCRDTEI+6      MOVE YYYY IN WORK FIELD
         LA    R0,L'MDY
         LA    R1,MDY+L'MDY
         BCTR  R1,0
         CLI   0(R1),C'0'              TEST IF NUMERIC
         BL    WRDTEI
         CLI   0(R1),C'9'
         BH    WRDTEI
         BCT   R0,*-18
*------- ICI NOUS SAVONS QUE LA DATE EST NUMERIQUE
*        CONTROLE QU'ELLE EST BIEN DU FORMAT NN/NN/NN
         LA    R15,SCRDTEI
         BAL   R14,CTDELIM             GO CONTROL DELIMITERS
         B     VALDTEI                 +0 - GO TO DATE IS VALID
         B     ERRDTEI                 +4 - GO TO ERROR DATE
*------- TRANSFORMATION A SERIAL DATE.
VALDTEI $GSDATE MDY,WCL7
         LTR   R15,R15
         BZ    STDTEI
*------- FORMAT DE LA DATE NON VALIDE. SLASH NON EN BONNE PLACE.
         MVC   ECRANM(L'ERROR3),ERROR3 SET ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------- ICI, LA DATE DE DEBUT EST INVALIDE. VALEURS INVALIDES.
ERRDTEI  MVC   ECRANM(L'ERROR4),ERROR4 SET ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------ ICI, NOUS SAVONS QUE LA DATE D'ENTREE EST INVALIDE. NON NUM.
WRDTEI   MVC   ECRANM(L'ERROR2),ERROR2 SET ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------- ICI NOUS SAVONS QUE LA DATE D'ENTREE DE L'AUTORISATION
*        EST VALIDE.
STDTEI   MVC   OUDTEI,WCL7             RETAIN SERIAL DATE
         EJECT
*--------------------------------------------------------------------*
*        CONTROLE DE LA DATE DE VALIDATION DE L'AUTORISATION.        *
*        (DATE DE FIN)                                               *
*--------------------------------------------------------------------*
         SPACE 1
         MVC   MDY(2),SCRDTEO+3        MOVE MM IN WORK FIELD
         MVC   MDY+2(2),SCRDTEO        MOVE DD IN WORK FIELD
         MVC   MDY+4(4),SCRDTEO+6      MOVE YYYY IN WORK FIELD
         LA    R0,L'MDY
         LA    R1,MDY+L'MDY
         BCTR  R1,0
         CLI   0(R1),C'0'              TEST IF NUMERIC
         BL    WRDTEO
         CLI   0(R1),C'9'
         BH    WRDTEO
         BCT   R0,*-18
*------- ICI NOUS SAVONS QUE LA DATE EST NUMERIQUE
*        CONTROLE QU'ELLE EST BIEN DU FORMAT NN/NN/NN
         LA    R15,SCRDTEO
         BAL   R14,CTDELIM             GO CONTROL DELIMITERS
         B     VALDTEO                 +0 - GO TO DATE IS VALID
         B     ERRDTEO                 +4 - GO TO ERROR DATE
*------- TRANSFORMATION A SERIAL DATE.
VALDTEO $GSDATE MDY,WCL7
         LTR   R15,R15
         BZ    STDTEO
*------- FORMAT DE LA DATE NON VALIDE. SLASH NON EN BONNE PLACE.
         MVC   ECRANM(L'ERROR6),ERROR6 SET ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------- ICI, LA DATE DE SORTIE EST INVALIDE. VALEURS INVALIDES.
ERRDTEO  MVC   ECRANM(L'ERROR7),ERROR7 SET ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------ ICI, NOUS SAVONS QUE LA DATE D'ENTREE EST INVALIDE. NON NUM.
WRDTEO   MVC   ECRANM(L'ERROR5),ERROR5 SET ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
*------- ICI NOUS SAVONS QUE LA DATE DE SORTIE DE L'AUTORISATION
*        EST VALIDE.
STDTEO   MVC   OUDTEO,WCL7             RETAIN SERIAL DATE
         MVC   WCL16(4),SCRDTEI+6      CONTROL START-END DATE
         MVC   WCL16+4(2),SCRDTEI+3
         MVC   WCL16+6(2),SCRDTEI
         MVC   WCL16+8(4),SCRDTEO+6
         MVC   WCL16+12(2),SCRDTEO+3
         MVC   WCL16+14(2),SCRDTEO
         CLC   WCL16(8),WCL16+8
         BNH   *+L'*+10
         MVC   ECRANM(L'ERROR13),ERROR13  SET ERROR MESSAGE
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
         EJECT
*--------------------------------------------------------------------*
*        CONTROLE DU NUMERO DE PROGRAMMEUR RESPONSABLE.              *
*--------------------------------------------------------------------*
         SPACE 1
         LA    R0,L'SCRPR
         LA    R1,SCRPR+L'SCRPR
         BCTR  R1,0
         CLI   0(R1),C'0'              TEST IF NUMERIC
         BL    WRSCRPR
         CLI   0(R1),C'9'
         BH    WRSCRPR
         BCT   R0,*-18
*------- ICI, NOUS SAVONS QUE LE NUM DU PGM RESP. EST BIEN NUMERIQUE
*        TEST S'IL EXISTE DANS LE FICHIER DES PROGRAMMEURS AUTORISES.
         PACK  WPL3,SCRPR              PACK NBR OF PGM RESP
         ZAP   DSD,WPL3                ALIGN IN ADDRESS * 8
         CVB   R5,DSD                  CONVERT PGM NBR IN BINARY
         AH    R5,=Y(NAUTNO+1)         ADD NO. OF AUT. AT KEY
         CH    R5,ARGUMPRG             IS IT CHANGED ?
         BE    SWCTL                   NO
         STH   R5,ARGUMPRG             STORE KEY IN ARGUMENT
        CALL   ZREAD,(RETCD,ARGUMPRG,AREAP),VL  READ FILE
         LTR   R15,R15                 TEST RETURN CODE
         BZ    TSTEXPR                 IF ZERO, PROG. RESP EXIST
         CH    R15,=H'15'              IS RETURN CODE 15 ?
         BE    *+L'*+8                 BRANCH IF YES
         LM    R2,R3,NZRD              SAY ZREAD ERROR
         B     ERRZR
         MVC   ECRANM(L'ERROR10),ERROR10  SET ERROR MSG
         B     CLRNOM
TSTEXPR  LH    R15,PRGPNO              LOAD PROGRAMMER NUMBER
         LTR   R15,R15                 TEST SIGN
         BP    EXISTPR                 IF ZERO, OK
         MVC   ECRANM(L'ERROR9),ERROR9 IF YES, ERROR MSG
         B     CLRNOM
*------ ICI, NOUS SAVONS QUE LE NUMERO N'EST PAS NUMERIQUE
WRSCRPR  MVC   ECRANM(L'ERROR8),ERROR8 SET ERROR MESSAGE
CLRNOM   MVI   SCRNOM,C' '
         MVC   SCRNOM+1(L'SCRNOM-1),SCRNOM
         B     DSPLAY                  DISPLAY SCREEN FOR CORRECTION
EXISTPR  MVC   SCRNOM,PRGPNME          SET PROGR. NAME
SWCTL    B     DSPLAY
         EJECT
*--------------------------------------------------------------------*
*        CREATION DE LA NOUVELLE AUTORISATION. WRITE DU RECORD.      *
*--------------------------------------------------------------------*
         XC    AREAR(AUTLGTH),AREAR    CLEAR RECORD AREA
         MVC   AUTANO,ARGUMAUT         SET ARGUMENT IN RECORD
         MVC   AUTACNO,SCRFICH         MOVE FICHE PROGRAMME
         CLI   SCRPI,C'4'
         BE    *+L'*+6
         MVC   AUTPI,SCRPI             SET PRIORITY INCR/DECR
         PACK  DSD,OUDTEI              TRANFORM EBCDIC IN PACKED
         CVB   R1,DSD                  CONVERT PACKED IN FIXED
         STCM  R1,B'0111',AUTVSD       REPLACE DATE
         PACK  DSD,OUDTEO              TRANFORM EBCDIC IN PACKED
         CVB   R1,DSD                  CONVERT PACKED IN FIXED
         STCM  R1,B'0111',AUTVED       REPLACE DATE
         PACK  WPL3,SCRPR              PACK NBR OF PGM RESP
         ZAP   DSD,WPL3                ALIGN IN ADDRESS * 8
         CVB   R1,DSD                  CONVERT PGM NBR IN BINARY
         STH   R1,AUTPMNO              STORE IN RECORD
         DROP  R2,R3
        CALL  ZWRITE,(RETCD,ARGUMAUT,AREAR),VL  WRITE RECORD
         LTR   R15,R15                 TEST RETURN CODE
         BZ    *+L'*+8
         LM    R2,R3,NZWR              SAY ZWRITE ERROR
         B     ERRZR
         LA    R1,RQHCPY               HARDCOPY OF OPERATION
         L     R15,=V(AUT96)
         BALR  R14,R15
         LTR   R15,R15                 TEST RETURN CODE
         BZ    EXSUCC
         XR    R2,R2                   PASS HARDCOPY ERROR
         LR    R3,R0
         B     ERRHC
*------- ERREUR AU MOMENT DE LA LECTURE OU DE L'ECRITURE DU RECORD.
ERRZR    LR    R0,R15
         XR    R1,R1
ERRHC    L     R15,=V(AUT97)
         BALR  R14,R15
         LA    R15,8                   SAY SEVERE ERROR
         B     LEAVE
EXNONE   MVC   MSGSKIP+L'MSGSKIP-5(4),SCRAUT
         LA    R1,MSGSKIP
         B     EXSMSG
EXREQ    MVC   MSGCBYP+L'MSGCBYP-5(4),SCRAUT
         LA    R1,MSGCBYP
         XR    R15,R15
         B     EXSMSG
EXSUCC   MVC   MSGDONE+L'MSGDONE-5(4),SCRAUT
         LA    R1,MSGDONE
EXSMSG   L     R2,4(R13)
         ST    R1,24(R2)               PASS ADDRESS OF MESSAGE
LEAVE   $XRET  CC=(R15)                RETURN
         EJECT
*--------------------------------------------------------------------*
*        CONTROLE FORMAT ET VALIDITE D'UNE DATE (ROUTINE)            *
*--------------------------------------------------------------------*
         SPACE 1
*        MDY = DATE IN FORMAT MMDDYYYY
*        R15 = ADDRESS OF DATE (DD/MM/YYYY)
*        R14 = LINK - RETURN = +0 DATE IS VALID
*                              +4 ERROR DATE
*                              +8 DATE IS INVALID
*        CONTROLE QU'ELLE EST BIEN DU FORMAT DD/MM/YYYY
CTDELIM  CLI   2(R15),C'/'             IS / PRESENT ?
         BE    CTDLM2                  YES
         CLI   2(R15),C' '             IS BLANK PRESENT ?
         BE    CTDLM1                  YES
         CLI   2(R15),C'.'             IS PERIOD PRESENT ?
         BE    CTDLM1                  YES
         CLI   2(R15),C'-'             IS - PRESENT ?
         BNE   8(R14)                  IF NOT, GO TO INVALID DATE
CTDLM1   MVI   2(R15),C'/'             SET / IN PLACE
CTDLM2   CLI   5(R15),C'/'             IS PRESENT SECOND / ?
         BE    CTDLM4                  YES
         CLI   5(R15),C' '             IS PRESENT SECOND BLANK ?
         BE    CTDLM3                  YES
         CLI   5(R15),C'.'             IS PRESENT SECOND PERIOD ?
         BE    CTDLM3                  YES
         CLI   5(R15),C'-'             IS PRESENT SECOND - ?
         BNE   8(R14)                  IF NOT, GO TO INVALID DATE
CTDLM3   MVI   5(R15),C'/'             SET / IN PLACE
*------- ICI NOUS SOMMES CERTAINS QUE LE FORMAT DE LA DATE EST BON
*        ET QU'ELLE EST NUMERIQUE.
*        CONTROLE DE LA VRAISEMBLANCE DE LA DATE
*    NB. LA DATE EGALE A 99/99/9999 EST VALIDE (SIGNIFIE L'INFINI).
CTDLM4   CLC   MDY,=C'99999999'        COMPARE WITH 99/99/9999
         BER   R14                     IF EQUAL, DATE IS VALID
*------- ICI LES JOURS ET MOIS NE SONT PAS EGAUX A 99
         CLC   MDY+2(2),=C'31'         IS DAY > 31 ?
         BH    4(R14)                  IF YES, GO TO ERROR DATE
         CLC   MDY+2(2),=C'01'         IS DAY < 01 ?
         BL    4(R14)                  IF YES, GO TO ERROR DATE
         CLC   MDY(2),=C'12'           IS MONTH > 12 ?
         BH    4(R14)                  IF YES, GO TO ERROR DATE
         CLC   MDY(2),=C'01'           IS MONTH < 01 ?
         BL    4(R14)                  IF YES, GO TO ERROR DATE
*------- ICI, LA DATE EST VRAISEMBLABLE
         BR    R14                     BACK, DATE IS VALID
         EJECT
*--------------------------------------------------------------------*
*        VARIABLES DE TRAVAIL                                        *
*--------------------------------------------------------------------*
         SPACE 1
DSD      DC    D'0'
PECRAN   DC    A(LECRAN,ECRAN)
RETCD    DC    F'0'          RETURN CODE
NZRD     DC    0F'0',CL8'ZREAD'
NZWR     DC    0F'0',CL8'ZWRITE'
AREAR    DC    (AUTLGTH)X'0'
AREAP    DC    (PRGLGTH)X'0'
MDY      DC    CL8' '        MMDDYYYY
ARGUMAUT DC    H'0'          AUTH NBR
ARGUMPRG DC    H'0'          RESP. PROGR NBR
OUDTEI   DC    CL7' '
OUDTEO   DC    CL7' '
WPL3     DC    PL3'+0'
WCL7     DC    CL7' '
WCL16    DC    CL16' '
         SPACE 1
*--------------------------------------------------------------------*
*        HARDCOPY REQUESTS.                                          *
*--------------------------------------------------------------------*
         SPACE 1
RQHCPY   DC    AL1(1),AL4(ARGUMAUT,AREAR)   CREATE AUTHORIZATION.
         EJECT
*--------------------------------------------------------------------*
*        MESSAGES D'ERREURS                                          *
*--------------------------------------------------------------------*
         SPACE 1
ERROR0   DC    C'Input scan SBA order not found ???'
ERROR1   DC    C'Fiche Programme NON Num!rique.'
ERROR2   DC    C'DATE de DEBUT non  N U M E R I Q U E |'
ERROR3   DC    C'DATE de DEBUT mal format!e (JJ/MM/AAAA)'
ERROR4   DC    C'DATE de DEBUT avec valeurs invalides.'
ERROR5   DC    C'DATE de FIN non  N U M E R I Q U E |'
ERROR6   DC    C'DATE de FIN mal format!e (JJ/MM/AAAA)'
ERROR7   DC    C'DATE de FIN avec valeurs invalides.'
ERROR8   DC    C'Num!ro du Progr. RESP. NON Num!rique |'
ERROR9   DC    C'Le Num!ro du PROG. RESP. est INVALIDE |'
ERROR10  DC    C'Le Num!ro du PROG. RESP. n''existe pas |'
ERROR11  DC    C'Invalid Priority Number|'
ERROR12  DC    C'Sorry, you can''t change Priority|'
ERROR13  DC    C'DATE de DEBUT d!passe DATE de FIN'
ERROR14  DC    C'D!finissez la Fiche Programme S.V.P. |||'
         DC    AL1(L'MSGDONE)
MSGDONE  DC    C'CREATE is successfully done - Authorization :     .'
         DC    AL1(L'MSGSKIP)
MSGSKIP  DC    C'CREATE bypassed - None set - Authorization :     .'
         DC    AL1(L'MSGCBYP)
MSGCBYP  DC    C'CREATE bypassed - Escape... - Authorization :     .'
         EJECT
*--------------------------------------------------------------------*
*        ECRAN D'INTRODUCTION D'UNE NOUVELLE AUTORISATION.           *
*--------------------------------------------------------------------*
         SPACE 1
ECRAN   $FS    CC=W,WCC=(KBR),SBA=(24,80),MF=L
        $FS    SBA=(1,1),RA=(1,1,00),MF=L        SET BUFFER/CLEAR.
        $FS    SBA=(1,1),SF=(PROT),MF=L
        $FS    SBA=(1,25),SF=(PROT,INT),MF=L
        $FS    TEXT='AUTHORIZATION     C R E A T E',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(2,25),SF=(PROT,INT),MF=L
        $FS    TEXT='=============================',MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(5,1),SF=(PROT),TEXT='Authorization Number :',MF=L
        $FS    SF=(PROT,INT),MF=L
SCRAUT  $FS    TEXT=(' ',4),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(7,1),SF=(PROT),TEXT='Fiche Programme :',MF=L
        $FS    SF=(INT),MF=L
SBAFICH $FS    SBA=(7,20),MF=L
SCRFICH $FS    TEXT=(' ',10),MF=L
        $FS    SF=(PROT),TEXT='< (or enter',MF=L
        $FS    SF=(PROT,INT),TEXT='X',MF=L
        $FS    SF=(PROT),TEXT='to escape)',MF=L
        $FS    SBA=(9,1),SF=(PROT),MF=L
        $FS    TEXT='DATE de DEBUT de validation (JJ/MM/AAAA) :',MF=L
        $FS    SF=(INT),MF=L
SBADTEI $FS    SBA=(9,45),MF=L
SCRDTEI $FS    TEXT=(' ',10),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(10,3),SF=(PROT),MF=L
        $FS    TEXT='DATE de FIN de validation (JJ/MM/AAAA) :',MF=L
        $FS    SF=(INT),MF=L
SBADTEO $FS    SBA=(10,45),MF=L
SCRDTEO $FS    TEXT=(' ',10),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(12,1),SF=(PROT),MF=L
        $FS    TEXT='Responsible Programmer :',MF=L
        $FS    SF=(INT),MF=L
SBAPR   $FS    SBA=(12,27),MF=L
SCRPR   $FS    TEXT=(' ',4),MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(13,1),SF=(PROT),TEXT='Resp. Name :',MF=L
        $FS    SF=(PROT,INT),MF=L
SCRNOM  $FS    TEXT=(' ',40),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(15,1),SF=(PROT),MF=L
        $FS    TEXT='Priority Increment/Decrement (0-9) :',MF=L
        $FS    SF=(INT),MF=L
SBAPI   $FS    SBA=(15,39),MF=L
SCRPI   $FS    TEXT=' ',MF=L
        $FS    SF=(PROT),TEXT='<',MF=L
        $FS    SBA=(16,1),SF=(PROT,INT),TEXT='Note :',MF=L
        $FS    SF=(PROT),TEXT='a priority of 4=+0, 0=-4 and ',MF=L
        $FS    TEXT='9=+5 at Jobs execution scheduling time.',MF=L
        $FS    SBA=(18,1),SF=(PROT,INT),MF=L
ECRANM  $FS    TEXT=(' ',78),MF=L
        $FS    SF=(PROT),MF=L
        $FS    SBA=(20,1),SF=(PROT),MF=L
        $FS    TEXT='Type in needed data and hit',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SF=(PROT),TEXT='until they are correct, ',MF=L
        $FS    TEXT='then hit only',MF=L
        $FS    SF=(PROT,INT),TEXT='ENTER',MF=L
        $FS    SBA=(21,1),SF=(PROT),MF=L
        $FS    TEXT='to create the Authorization, ',MF=L
        $FS    TEXT='or use at any time',MF=L
        $FS    SF=(PROT,INT),TEXT='PF-3',MF=L
        $FS    SF=(PROT),TEXT='to exit (emergency)',MF=L
        $FS    SBA=(22,1),SF=(PROT),MF=L
        $FS    TEXT='As soon as you have created the ',MF=L
        $FS    TEXT='Authorization, you may then define the',MF=L
        $FS    SBA=(23,1),SF=(PROT),MF=L
        $FS    TEXT='Programmers Numbers through the',MF=L
        $FS    SF=(PROT,INT),TEXT='UPDATE',MF=L
        $FS    SF=(PROT),TEXT='process.',MF=L
        $FS    SBA=(1,1),SF=(PT,IC),MF=L
LECRAN   EQU   *-ECRAN
         EJECT
*--------------------------------------------------------------------*
*        LITERAL POOL                                                *
*--------------------------------------------------------------------*
         SPACE 1
        LTORG
         EJECT
*--------------------------------------------------------------------*
*        DUMMY SECTIONS                                              *
*--------------------------------------------------------------------*
         SPACE 1
         PRINT GEN
        $AIRD  SEC=YES,R=NO
         SPACE 2
         END
