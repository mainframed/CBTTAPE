//C4SYSDM7 JOB EDPCSBS1,'RMF77',MSGCLASS=X,CLASS=1,NOTIFY=ED115DM
//** XA FORMAT
//*
//SAS     EXEC EDSAS607,OPTIONS=NODATE
//DATLIB  DD DSN=ED115DM.PROD.SMFDUMPW,DISP=SHR
//DATLIB  DD DSN=SYS1.MAN2,DISP=SHR
//SYSIN   DD *
TITLE 'SMF 77';
DATA T77 (KEEP = TIMEOUT SMFXXSID
        SMF77SSI          SMF77STY        SMF77TRN
        SMF77RV1          SMF77PRS        SMF77PRL
        SMF77PRN          SMF77EQS        SMF77EQL
        SMF77EQN          SMF77EDS        SMF77EDL
        SMF77EDN)
     PRODUCT (KEEP = TIMEOUT
             SMF77MFV          SMF77PRD           SMF77IST
             SMF77DAT          SMF77INT           SMF77MFL
             SMF77SAM          SMF77RV2           SMF77FLA
             SMF77RLS          SMF77CYC           SMF77MVS
             SMF77IML          SMF77RV9
             )
     ECONTROL (KEEP = TIMEOUT
             SMF77FG1          SMF77RF2
             )
     ENQD     (KEEP = TIMEOUT
             SMF77QNM          SMF77RNM           SMF77WTM
             SMF77WTX          SMF77WTT           SMF77RV3
             SMF77QL1          SMF77QL2           SMF77QL3
             SMF77QL4          SMF77QLT           SMF77EXM
             SMF77EXX          SMF77SHM           SMF77SHX
             SMF77EVT          SMF77RLN           SMF77DFG
             SMF77DOW          SMF77DWR           SMF77DO1
             SMF77DO2          SMF77DW1           SMF77DW2
             SMF77SY1          SMF77SY2           SMF77SY3
             SMF77SY4
             )
          ;
  RETAIN V;
  INFILE DATLIB JFCB=JF MISSOVER;
  IF _N_ = 1 THEN DO;
                  INPUT @;
                  IF INDEX(JF,'SYS1.MAN') Â¬=0 THEN V=4; ELSE V=0;
                  END;
  IF V=4 THEN
      INPUT    SMFXXLEN PIB2. SMFXXSEG PIB2. @ ;
  INPUT    SMFXXFLG PIB1. SMFXXRTY PIB1.
                       @;
  IF SMFXXRTY = 212 THEN DO;
                        *PUT '212 DETECTED - SKIPPING';
                         DELETE;
                         END;
  IF SMFXXRTY = 77;
  INPUT    TIMEOUT SMFSTAMP8.
      SMFXXSID $CHAR4. @;
  INPUT SMF77SSI $CHAR4.  SMF77STY PIB2.  SMF77TRN PIB2.
        SMF77RV1 PIB2.    SMF77PRS PIB4.  SMF77PRL PIB2.
        SMF77PRN PIB2.    SMF77EQS PIB4.  SMF77EQL PIB2.
        SMF77EQN PIB2.    SMF77EDS PIB4.  SMF77EDL PIB2.
        SMF77EDN PIB2. @;
  OUTPUT T77;
  DO I=1 TO SMF77PRN;
  OFF = V -3 + SMF77PRS + (I-1)*SMF77PRL;
  INPUT @OFF SMF77MFV $CHAR2.  SMF77PRD $CHAR8.   SMF77IST PD4.
             SMF77DAT PD4.     SMF77INT RMFDUR.   SMF77MFL PIB2.
             SMF77SAM PIB4.    SMF77RV2 PIB2.     SMF77FLA PIB2.
             SMF77RLS $CHAR4.  SMF77CYC PD4.      SMF77MVS $CHAR8.
             SMF77IML PIB1.    SMF77RV9 PIB3.
  @;
  OUTPUT PRODUCT;
  END;
  DO I=1 TO SMF77EQN;
  OFF = V -3 + SMF77EQS + (I-1)*SMF77EQL;
  INPUT @OFF SMF77FG1 PIB1.    SMF77RF2 PIB1.
  @;
  OUTPUT ECONTROL;
  END;
  DO I=1 TO SMF77EDN;
  OFF = V -3 + SMF77EDS + (I-1)*SMF77EDL;
  INPUT @OFF SMF77QNM $CHAR8.  SMF77RNM $CHAR44.  SMF77WTM PIB4.
             SMF77WTX PIB4.    SMF77WTT PIB4.     SMF77RV3 PIB2.
             SMF77QL1 PIB2.    SMF77QL2 PIB2.     SMF77QL3 PIB2.
             SMF77QL4 PIB2.    SMF77QLT PIB2.     SMF77EXM PIB2.
             SMF77EXX PIB2.    SMF77SHM PIB2.     SMF77SHX PIB2.
             SMF77EVT PIB2.    SMF77RLN PIB1.     SMF77DFG PIB1.
             SMF77DOW PIB2.    SMF77DWR PIB2.     SMF77DO1 $CHAR8.
             SMF77DO2 $CHAR8.  SMF77DW1 $CHAR8.   SMF77DW2 $CHAR8.
             SMF77SY1 $CHAR8.  SMF77SY2 $CHAR8.   SMF77SY3 $CHAR8.
             SMF77SY4 $CHAR8.
  @;
  OUTPUT ENQD;
  END;
RETURN;
PROC PRINT DATA=T77;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=PRODUCT;
FORMAT TIMEOUT DATETIME.;
FORMAT SMF77INT TIME.;
PROC PRINT DATA=ECONTROL;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=ENQD;
FORMAT TIMEOUT DATETIME.;
