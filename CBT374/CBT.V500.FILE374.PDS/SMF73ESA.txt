//C4SYSDM3 JOB EDPCSBS1,'RMF73',MSGCLASS=X,CLASS=1,NOTIFY=ED115DM
//** ESA FORMAT
//*
//SAS     EXEC SAS,OPTIONS=NODATE
//*ATLIB  DD DSN=C4H1.PROD.SMFDUMPW.HOLD,DISP=SHR
//DATLIB  DD DSN=SYS1.MAN4,DISP=SHR
//SYSIN   DD *
TITLE 'SMF 73';
DATA T73 (KEEP = TIMEOUT SMFXXSID
        SMF73SSI          SMF73STY        SMF73TRN
        SMF73RV1          SMF73PRS        SMF73PRL
        SMF73PRN          SMF73HIS        SMF73HIL
        SMF73HIN          SMF73HPS        SMF73HPL
        SMF73HPN)
     PRODUCT (KEEP = TIMEOUT
             SMF73MFV          SMF73PRD           SMF73IST
             SMF73DAT          SMF73INT           SMF73MFL
             SMF73SAM          SMF73RV2           SMF73FLA
             SMF73RLS          SMF73CYC           SMF73MVS
             SMF73IML          SMF73PRF
             SMF73PTN          SMF73SRL           SMF73IET
             )
     PCONTROL (KEEP = TIMEOUT
             SMF73SMP          SMF73CFL          SMF73RS3
             SMF73TNM          SMF73TSF          SMF73RS4
             SMF73TOK          SMF73TDT          SMF73TTM
             SMF73CRC          SMF73CSC
             )
     CHPID    (KEEP = TIMEOUT
             SMF73PID          SMF73FG2
             SMF73FG3
             SMF73FG4          SMF73BSY           SMF73PBY
             SMF73PTI
             )
          ;
  RETAIN V;
  INFILE DATLIB JFCB=JF MISSOVER;
  IF _N_ = 1 THEN DO;
                  INPUT @;
                  IF INDEX(JF,'SYS1.MAN') Â¬=0 THEN V=4; ELSE V=0;
                  END;
  IF V=4 THEN
      INPUT    SMFXXLEN PIB2. SMFXXSEG PIB2. @ ;
  INPUT    SMFXXFLG PIB1. SMFXXRTY PIB1.
                       @;
  IF SMFXXRTY = 212 THEN DO;
                        *PUT '212 DETECTED - SKIPPING';
                         DELETE;
                         END;
  IF SMFXXRTY = 73;
  INPUT    TIMEOUT SMFSTAMP8.
      SMFXXSID $CHAR4. @;
  INPUT SMF73SSI $CHAR4.  SMF73STY PIB2.  SMF73TRN PIB2.
        SMF73RV1 PIB2.    SMF73PRS PIB4.  SMF73PRL PIB2.
        SMF73PRN PIB2.    SMF73HIS PIB4.  SMF73HIL PIB2.
        SMF73HIN PIB2.    SMF73HPS PIB4.  SMF73HPL PIB2.
        SMF73HPN PIB2. @;
  OUTPUT T73;
  DO I=1 TO SMF73PRN;
  OFF = V -3 + SMF73PRS + (I-1)*SMF73PRL;
  INPUT @OFF SMF73MFV $CHAR2.  SMF73PRD $CHAR8.   SMF73IST PD4.
             SMF73DAT PD4.     SMF73INT RMFDUR.   SMF73MFL PIB2.
             SMF73SAM PIB4.    SMF73RV2 PIB2.     SMF73FLA PIB2.
             SMF73RLS $CHAR4.  SMF73CYC PD4.      SMF73MVS $CHAR8.
             SMF73IML PIB1.    SMF73PRF PIB1.
             SMF73PTN PIB1.    SMF73SRL PIB1.     SMF73IET $CHAR8.
  @;
  OUTPUT PRODUCT;
  END;
  DO I=1 TO SMF73HIN;
  OFF = V -3 + SMF73HIS + (I-1)*SMF73HIL;
  INPUT @OFF SMF73SMP PIB4.    SMF73CFL  PIB1.   SMF73RS3 PIB3.
             SMF73TNM $CHAR44. SMF73TSF  $CHAR2. SMF73RS4 PIB2.
             SMF73TOK $CHAR16. SMF73TDT  $CHAR8. SMF73TTM  $CHAR8.
             SMF73CRC PIB4.    SMF73CSC  PIB4.
  @;
  OUTPUT PCONTROL;
  END;
  DO I=1 TO SMF73HPN;
  OFF = V -3 + SMF73HPS + (I-1)*SMF73HPL;
  INPUT @OFF SMF73PID PIB1.    SMF73FG2 PIB1.     SMF73FG3 PIB1.
             SMF73FG4 PIB1.    SMF73BSY PIB4.     SMF73PBY PIB4.
             SMF73PTI PIB4.
  @;
  OUTPUT CHPID;
  END;
RETURN;
PROC PRINT DATA=T73;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=PRODUCT;
FORMAT TIMEOUT DATETIME.;
FORMAT SMF73INT TIME.;
PROC PRINT DATA=PCONTROL;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=CHPID;
FORMAT TIMEOUT DATETIME.;
