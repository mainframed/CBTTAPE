//C4SYSDMA JOB (EDPCSBS1),MSGCLASS=X,CLASS=L,NOTIFY=ED115DM
//* XA FORMAT
//* THERE SEEMS TO BE SOMETHING FUNNY ABOUT EXCPS IN PARTICULAR
//* WHEN INTERVAL RECORDS ARE PUT OUT. TYPE 5 SMF30 MAY BE ZERO FOR
//* CICS ETC. - MAYBE INTERVAL DATA IS NOT SUMMED IN TYPE 30.
//*
//SAS     EXEC SAS606,OPTIONS='NOSOURCE NODATE'
//*ATLIB  DD DSN=SYS1.MAN1,DISP=SHR
//DATLIB  DD DSN=EDH1.PROD.SMFDUMPW.HOLD,DISP=OLD
//*ATLIB  DD DSN=ED115DM.PROD.SMFDUMPW,DISP=OLD
//*ATLIB  DD DSN=EDH1.PROD.SMFDUMPW,DISP=OLD
//*ATLIB  DD DSN=EDH1.PROD.C4#3.SMFDAILY.COPY1,DISP=OLD
//*       VOL=SER=702786
//*ATLIB  DD DSN=C4H1.PROD.SMFDUMPW.CPY,DISP=OLD
//*ATLIB  DD DSN=C4P2.PROD.JOBS.OCT86,DISP=OLD
//FT13F001 DD SYSOUT=X
//FT14F001 DD SYSOUT=X
//FT15F001 DD SYSOUT=X
//FT16F001 DD SYSOUT=X
//FT17F001 DD SYSOUT=X
//FT18F001 DD SYSOUT=X
//FT19F001 DD SYSOUT=X
//FT20F001 DD SYSOUT=X
//FT21F001 DD SYSOUT=X
//FT22F001 DD SYSOUT=X
//FT23F001 DD SYSOUT=X
//SYSIN   DD *
TITLE SMF 30;
  DATA
 STARTER (KEEP = SMF30JBN TIMEOUT  SMFXXSID
                 SMF30WID SMF30STP  SMF30SOF  SMF30SLN
                 SMF30SON SMF30IOF  SMF30ILN  SMF30ION
                 SMF30UOF SMF30ULN  SMF30UON  SMF30TOF
                 SMF30TIN SMF30TON  SMF30COF  SMF30CLN
                 SMF30CON SMF30AOF  SMF30ALN  SMF30AON
                 SMF30ROF SMF30RLN  SMF30RON  SMF30POF
                 SMF30PLN SMF30PON  SMF30OOF  SMF30OLN
                 SMF30OON SMF30EOF  SMF30ELN  SMF30EON
                 SMF30EOR
        )
  SUBSYS (KEEP = SMF30JBN I TIMEOUT
                            SMF30TYP SMF30RS1 SMF30RVN SMF30PNM)
  IDENT (KEEP = SMF30JBN I TIMEOUT
                           SMF30PGM SMF30STM SMF30UIF SMF30JNM SMF30STN
                SMF30CLS SMF30IF1 SMF30PGN SMF30JPT SMF30AST SMF30PPS
                SMF30IDT SMF30SDT SMF30EDT SMF30USR SMF30GRP SMF30RUD
                SMF30TID READTIME WAITTIME RUNTIME )
  IOACT (KEEP = SMF30JBN I TIMEOUT
                           SMF30INP SMF30TEP SMF30TPT SMF30TGT SMF30RDR
                SMF30RDT SMF30TCN SMF30MFL)
  COMPL (KEEP = SMF30JBN I TIMEOUT SMF30SCC SMF30ST1 )
  PRACC (KEEP = SMF30JBN I TIMEOUT
                SMF30PTY SMF30RV3 SMF30CPT SMF30CPS SMF30ICU SMF30ISB)
  ACCNT (KEEP = SMF30JBN I TIMEOUT SMF30ACL SMF30ACT )
  STOR  (KEEP = SMF30JBN I TIMEOUT
                SMF30RSV SMF30SFL SMF30SPK SMF30PRV SMF30SYS SMF30PGI
                SMF30PGO SMF30REC SMF30NSW SMF30PSI SMF30PSO SMF30VPI
                SMF30VPO SMF30VPR SMF30CPI SMF30CPR SMF30LPI SMF30LPR
                SMF30PST SMF30PSC SMF30RGB SMF30ERG SMF30ARB SMF30EAR
                SMF30URB SMF30EUR SMF30RGN SMFRTPGI SMFRTPGO SMFRTREC
                SMFRTNSW SMFRTPSI SMFRTPSO SMFRTVPI SMFRTVPO SMFRTVPR
                SMFRTCPI SMFRTCPR SMFRTLPI SMFRTLPR SMFRTPST SMFRTPSC
                )
  PERFM (KEEP = SMF30JBN I TIMEOUT
                SMF30SRV SMF30CSU SMF30SRB SMF30IO  SMF30MSO SMF30TAT
                SMFRTSRV SMFRTCSU SMFRTSRB SMFRTIO  SMFRTMSO
                SMF30TET SMF30RES SMF30TRS WSET)
  OPER  (KEEP = SMF30JBN I TIMEOUT
                SMF30PDM SMF30PRD SMF30PTM SMF30TPR SMF30MTM SMF30MSR)
     EXCP  (KEEP = SMF30JBN I TIMEOUT CUMEXCP
                SMF30DEV SMF30UTP SMF30CUA SMF30DDN SMF30BLK SMF30BSZ
                SMF30DCT )
      ;
  CUMEXCP=0;
  WW=0;
  W=4;
  RETAIN V;
  INFILE DATLIB JFCB=JF;
  IF _N_ = 1 THEN DO;
                  INPUT @;
                  IF INDEX(JF,'SYS1.MAN') ¬=0 THEN V=4; ELSE V=0;
                  END;
  IF V=4 THEN
      INPUT    SMFXXLEN PIB2. SMFXXSEG PIB2. @ ;
  INPUT    SMFXXFLG PIB1. SMFXXRTY PIB1.
                       @;
  IF SMFXXRTY = 212 THEN DO;
                        *PUT '212 DETECTED - SKIPPING';
                         DELETE;
                         END;
  IF SMFXXRTY = 30;
  INPUT TIMEOUT SMFSTAMP8.
        SMFXXSID $CHAR4. @;
  INPUT SMF30WID $CHAR4. SMF30STP PIB2. SMF30SOF PIB4.  SMF30SLN PIB2.
        SMF30SON PIB2.   SMF30IOF PIB4. SMF30ILN PIB2.  SMF30ION PIB2.
        SMF30UOF PIB4.   SMF30ULN PIB2. SMF30UON PIB2.  SMF30TOF PIB4.
        SMF30TIN PIB2.   SMF30TON PIB2. SMF30COF PIB4.  SMF30CLN PIB2.
        SMF30CON PIB2.   SMF30AOF PIB4. SMF30ALN PIB2.  SMF30AON PIB2.
        SMF30ROF PIB4.   SMF30RLN PIB2. SMF30RON PIB2.  SMF30POF PIB4.
        SMF30PLN PIB2.   SMF30PON PIB2. SMF30OOF PIB4.  SMF30OLN PIB2.
        SMF30OON PIB2.   SMF30EOF PIB4. SMF30ELN PIB2.  SMF30EON PIB2.
        SMF30EOR PIB2.
        @;
   IF SMF30SON > 0 THEN DO;
      DO I = 0 TO (SMF30SON - 1);
        OFF = SMF30SOF -3+V + I*SMF30SLN;
        INPUT @OFF SMF30TYP PIB2. SMF30RS1 PIB2. SMF30RVN $CHAR2.
        SMF30PNM $CHAR8.
        @ ;
      END;
   END;
   IF SMF30ION > 0 THEN DO;
      DO I = 0 TO (SMF30ION - 1);
        OFF = SMF30IOF -3+V + I*SMF30ILN;
        INPUT @OFF SMF30JBN $CHAR8. SMF30PGM $CHAR8. SMF30STM $CHAR8.
                   SMF30UIF $CHAR8. SMF30JNM $CHAR8. SMF30STN PIB2.
                   SMF30CLS $CHAR1. SMF30IF1 PIB1.   SMF30PGN PIB2.
                   SMF30JPT PIB2.   SMF30AST PIB4.2  SMF30PPS PIB4.2
                   SMF30IDT SMFSTAMP8.
                   SMF30SDT SMFSTAMP8.
                   SMF30EDT SMFSTAMP8.
                  SMF30USR $CHAR20. SMF30GRP $CHAR8. SMF30RUD $CHAR8.
                   SMF30TID $CHAR8.
        @ ;
      END;
   END;
   IF SUBSTR(SMF30JBN,1,8) = 'EDSPDY92';
   QDATE = DATEPART(TIMEOUT);
   QTIME = TIMEPART(TIMEOUT);
   * IF QDATE <= '01JAN00'D OR QDATE >= '31DEC99'D THEN DELETE;
   * IF QTIME <= '00:00:00'T OR QTIME >= '24:00:00'T THEN DELETE;
   OUTPUT STARTER;
   IF SMF30SON > 0 THEN DO;
      DO I = 0 TO (SMF30SON - 1);
        OFF = SMF30SOF -3+V + I*SMF30SLN;
        INPUT @OFF SMF30TYP PIB2. SMF30RS1 PIB2. SMF30RVN $CHAR2.
        SMF30PNM $CHAR8.
        @ ;
        OUTPUT SUBSYS;
        IF W=SMF30TYP THEN WW+1;
      END;
   END;
   IF WW=0 THEN DELETE;
   IF SMF30ION > 0 THEN DO;
      DO I = 0 TO (SMF30ION - 1);
        OFF = SMF30IOF -3+V + I*SMF30ILN;
        INPUT @OFF SMF30JBN $CHAR8. SMF30PGM $CHAR8. SMF30STM $CHAR8.
                   SMF30UIF $CHAR8. SMF30JNM $CHAR8. SMF30STN PIB2.
                   SMF30CLS $CHAR1. SMF30IF1 PIB1.   SMF30PGN PIB2.
                   SMF30JPT PIB2.   SMF30AST PIB4.2  SMF30PPS PIB4.2
                   SMF30IDT SMFSTAMP8.
                   SMF30SDT SMFSTAMP8.
                   SMF30EDT SMFSTAMP8.
                  SMF30USR $CHAR20. SMF30GRP $CHAR8. SMF30RUD $CHAR8.
                   SMF30TID $CHAR8.
        @ ;
             READTIME = SMF30EDT - SMF30SDT;
             WAITTIME = SMF30IDT - SMF30SDT;
              RUNTIME = TIMEOUT  - SMF30IDT;
        OUTPUT IDENT;
      END;
   END;
   IF SMF30UON > 0 THEN DO;
      DO I = 0 TO (SMF30UON - 1);
        OFF = SMF30UOF -3+V + I*SMF30ULN;
        INPUT @OFF SMF30INP PIB4.   SMF30TEP PIB4.   SMF30TPT PIB4.
                   SMF30TGT PIB4.   SMF30RDR PIB1.   SMF30RDT PIB1.
                   SMF30TCN PIB4.   SMF30MFL PIB4.
        @ ;
        OUTPUT IOACT;
      END;
   END;
   IF SMF30TON > 0 THEN DO;
      DO I = 0 TO (SMF30TON - 1);
        OFF = SMF30TOF -3+V + I*SMF30TIN;
        INPUT @OFF SMF30SCC PIB2.   SMF30ST1 PIB2.
        @ ;
        OUTPUT COMPL;
      END;
   END;
   IF SMF30CON > 0 THEN DO;
      DO I = 0 TO (SMF30CON - 1);
        OFF = SMF30COF -3+V + I*SMF30CLN;
        INPUT @OFF SMF30PTY PIB2.   SMF30RV3 PIB2.    SMF30CPT PIB4.2
                   SMF30CPS PIB4.2  SMF30ICU PIB4.2   SMF30ISB PIB4.2
        @ ;
        OUTPUT PRACC;
      END;
   END;
   IF SMF30AON > 0 THEN DO;
      DO I = 0 TO (SMF30AON - 1);
        OFF = SMF30AOF -3+V + I*SMF30ALN;
        INPUT @OFF SMF30ACL PIB1.
        @ ;
       *SMF30ACT = '';
       *IF SMF30ACL > 0 THEN DO J = 0 TO (SMF30ACL-1);
       *                     INPUT SMFXXACT $CHAR1. @;
       *                     SMF30ACT = SMF30ACT || SMFXXACT  ;
       *                     END;
        OUTPUT ACCNT;
      END;
   END;
   IF SMF30RON > 0 THEN DO;
      DO I = 0 TO (SMF30RON - 1);
        OFF = SMF30ROF -3+V + I*SMF30RLN;
        INPUT @OFF SMF30RSV PIB2.   SMF30SFL PIB1.    SMF30SPK PIB1.
                   SMF30PRV PIB2.
                   SMF30SYS PIB2.   SMF30PGI PIB4.    SMF30PGO PIB4.
                   SMF30REC PIB4.   SMF30NSW PIB4.    SMF30PSI PIB4.
                   SMF30PSO PIB4.   SMF30VPI PIB4.    SMF30VPO PIB4.
                   SMF30VPR PIB4.   SMF30CPI PIB4.    SMF30CPR PIB4.
                   SMF30LPI PIB4.   SMF30LPR PIB4.    SMF30PST PIB4.
                   SMF30PSC PIB8.   SMF30RGB PIB4.    SMF30ERG PIB4.
                   SMF30ARB PIB4.   SMF30EAR PIB4.    SMF30URB PIB4.
                   SMF30EUR PIB4.   SMF30RGN PIB4.
        @ ;
        IF RUNTIME ¬=0 THEN DO;
        SMFRTPGI = SMF30PGI / RUNTIME;
        SMFRTPGO = SMF30PGO / RUNTIME;
        SMFRTREC = SMF30REC / RUNTIME;
        SMFRTNSW = SMF30NSW / RUNTIME;
        SMFRTPSI = SMF30PSI / RUNTIME;
        SMFRTPSO = SMF30PSO / RUNTIME;
        SMFRTVPI = SMF30VPI / RUNTIME;
        SMFRTVPO = SMF30VPO / RUNTIME;
        SMFRTVPR = SMF30VPR / RUNTIME;
        SMFRTCPI = SMF30CPI / RUNTIME;
        SMFRTCPR = SMF30CPR / RUNTIME;
        SMFRTLPI = SMF30LPI / RUNTIME;
        SMFRTLPR = SMF30LPR / RUNTIME;
        SMFRTPST = SMF30PST / RUNTIME;
        SMFRTPSC = SMF30PSC / RUNTIME;
                            END;
        OUTPUT STOR;
      END;
   END;
   IF SMF30PON > 0 THEN DO;
      DO I = 0 TO (SMF30PON - 1);
        OFF = SMF30POF -3+V + I*SMF30PLN;
        INPUT @OFF SMF30SRV PIB4.   SMF30CSU PIB4.    SMF30SRB PIB4.
                   SMF30IO  PIB4.   SMF30MSO PIB4.    SMF30TAT PIB4.
                   SMF30TET PIB4.   SMF30RES PIB4.    SMF30TRS PIB4.
        @ ;
        SMF30TAT=SMF30TAT/977 ;
        SMF30RES=SMF30RES/977 ;
        SMFRTSRV=SMF30SRV / RUNTIME;
        SMFRTCSU=SMF30CSU / RUNTIME;
        SMFRTSRB=SMF30SRB / RUNTIME;
        SMFRTIO =SMF30IO  / RUNTIME;
        SMFRTMSO=SMF30MSO / RUNTIME;
        IF SMF30CSU > 0 THEN
        WSET = (SMF30MSO / SMF30CSU) * 500/3; ELSE WSET=0;
        OUTPUT PERFM;
      END;
   END;
   IF SMF30OON > 0 THEN DO;
      DO I = 0 TO (SMF30OON - 1);
        OFF = SMF30OOF -3+V + I*SMF30OLN;
        INPUT @OFF SMF30PDM PIB4.   SMF30PRD PIB4.    SMF30PTM PIB4.
                   SMF30TPR PIB4.   SMF30MTM PIB4.    SMF30MSR PIB4.
        @ ;
        OUTPUT OPER;
      END;
   END;
   IF SMF30EON > 0 THEN DO;
   IF SMF30EOR ¬= 0 THEN PUT '***** EOR NEQ 0 ***** JOB WAS ' SMF30JBN;
      DO I = 0 TO (SMF30EON - 1);
        OFF = SMF30EOF -3+V + I*SMF30ELN;
        INPUT @OFF SMF30DEV PIB1.   SMF30UTP PIB1.    SMF30CUA PIB2.
                   SMF30DDN $CHAR8. SMF30BLK PIB4.    SMF30BSZ PIB2.
                   SMF30DCT PIB2.
        @ ;
        SMF30DCT = SMF30DCT * 128 / 1000000;
        CUMEXCP + SMF30BLK;
        OUTPUT    EXCP;
      END;
   END;
FORMAT TIMEOUT SMF30IDT SMF30SDT SMF30EDT DATETIME.
       READTIME WAITTIME RUNTIME
       SMF30AST SMF30PPS SMF30TAT SMF30RES
       SMF30CPT SMF30CPS SMF30ICU SMF30ISB TIME.
       SMF30CUA SMF30SCC HEX4.
       ;
PROC PRINTTO UNIT=13;
PROC PRINT DATA=STARTER;
TITLE STARTER SECTION;
PROC PRINTTO UNIT=14;
PROC PRINT DATA=SUBSYS;
TITLE SUBSYSTEM SECTION;
PROC PRINTTO UNIT=15;
PROC PRINT DATA=IDENT;
TITLE IDENTIFICATION SECTION;
PROC PRINTTO UNIT=16;
PROC PRINT DATA=IOACT;
TITLE I/O ACTIVITY SECTION;
PROC PRINTTO UNIT=17;
PROC PRINT DATA=COMPL;
TITLE COMPLETION SECTION;
PROC PRINTTO UNIT=18;
PROC PRINT DATA=PRACC;
TITLE PROCESSOR ACCOUNTING SECTION;
PROC PRINTTO UNIT=19;
PROC PRINT DATA=ACCNT;
TITLE ACCOUNTING SECTION;
PROC PRINTTO UNIT=20;
PROC PRINT DATA=STOR;
TITLE STORAGE AND PAGING SECTION;
PROC PRINTTO UNIT=21;
PROC PRINT DATA=PERFM;
TITLE PERFORMANCE SECTION;
PROC PRINTTO UNIT=22;
PROC PRINT DATA=OPER;
TITLE OPERATOR SECTION;
PROC PRINTTO UNIT=23;
PROC PRINT DATA=EXCP;
TITLE EXCP SECTION;
