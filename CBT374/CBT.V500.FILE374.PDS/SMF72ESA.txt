//C4SYSDM2 JOB EDPCSBS1,'RMF72',MSGCLASS=X,CLASS=L,NOTIFY=ED115DM
//** ESA FORMAT
//*
//SAS     EXEC EDSAS608,OPTIONS=NODATE
//*ATLIB  DD DSN=EDH1.PROD.RMFDEC94,DISP=SHR
//*ATLIB  DD DSN=ED115DM.PROD.SMFDUMPW,DISP=SHR
//DATLIB  DD DSN=SYS1.MAN4,DISP=SHR
//SYSIN   DD *
TITLE SMF 72;
  DATA T72 (KEEP =
      TIMEOUT
      SMFXXSID SMF72SSI SMF72STY SMF72TRN SMF72PRS SMF72PRL
      SMF72PRN SMF72WLS SMF72WLL SMF72WLN SMF72PGS SMF72PGL SMF72PGN)
      PRODUCT (KEEP = TIMEOUT SMF72MFV      SMF72PRD
      SMF72IST         SMF72DAT
      SMF72INT         SMF72MFL       SMF72SAM
      SMF72RV2         SMF72FLA       SMF72RLS
      SMF72CYC         SMF72MVS
             SMF72IML          SMF72PRF           SMF72PTN
             SMF72SRL          SMF72IET
             )
      WORKLOAD (KEEP = TIMEOUT
           SMF72FGI         SMF72RV3         SMF72SUB
           SMF72HGP         SMF72IPS         SMF72IRF
           SMF72CRF         SMF72ERF         SMF72ISD
           SMF72CSD         SMF72RV4         SMF72SSD
           SMF72OPT         SMF72ICS         SMF72SYS
         SMF72CLS          SMF72USR          SMF72NAM
         SMF72ADJ          SM72MSD
             )
      WRKLOAD2 (KEEP = TIMEOUT
                R722TSR          R7222RV3         R722TOT
           R722NXT          R722IPS
             )
         PG (KEEP = TIMEOUT
             SMF72TTX       SMF72ACT       SMF72SER
           SMF72TTM         SMF72LEV         SMF72MTS
           SMF72ITS       SMF72CTS       SMF72TAT
           SMF72SPP       SMF72CDN       SMF72PON
           SMF72TSG       SMF72STS       SMF72ET1
           SMF72ET2       SMF72PIN       SMF72FT1
           SMF72FT2       SMF72HIN       SMF72HRM
           SMF72BPI       SMF72PIE       SMF72BPE
           SMF72BKA       SMF72BKE       SMF72ER1
           SMF72ER2       SMF72RCT       SMF72IIT
           SMF72HST       SMF72TST
  )
         PG2 (KEEP = TIMEOUT
                 R722PG         R722DMN        R722USER
           R722ACTV         R722PAGE         R722SWAP
           R722OUTR       R722ACTF       R722ACTS
           R722IDLE       R722IDLS       R722PGIN
           R722SLOT       R722DIV        R722DIVS
           R722FIX        R722ET         R722QT
           R722END        R722UPRO       R722UDEV
           R722DPRO       R722DDEV       R722DSTO
           R722DJES       R722DHSM       R722DXCF
           R722DENQ       R722DMNT       R722DMSG
           R722UNKN       R722VALD       R722LSCT
           R722ESCT       R722PSCT       R722LSCF
           R722LSEF       R722LSSA       R722PSEF
           R722PSSA       R722VECT       R722ACFF
           R722IDLF       R722SLTF       R722DIVF
           R722FIXF
  )
  ;
  RETAIN V;
  INFILE DATLIB JFCB=JF MISSOVER;
  IF _N_ = 1 THEN DO;
                  INPUT @;
                  IF INDEX(JF,'SYS1.MAN') Â¬=0 THEN V=4; ELSE V=0;
                  END;
  IF V=4 THEN
      INPUT    SMFXXLEN PIB2. SMFXXSEG PIB2. @ ;
  INPUT    SMFXXFLG PIB1. SMFXXRTY PIB1.
                       @;
  IF SMFXXRTY = 212 THEN DO;
                        *PUT '212 DETECTED - SKIPPING';
                         DELETE;
                         END;
  IF SMFXXRTY = 72;
  INPUT    TIMEOUT SMFSTAMP8.
      SMFXXSID $CHAR4. @;
  INPUT SMF72SSI $CHAR4.  SMF72STY PIB2.  SMF72TRN PIB2.
        SMF72RV1 PIB2.    SMF72PRS PIB4.  SMF72PRL PIB2.
        SMF72PRN PIB2.    SMF72WLS PIB4.  SMF72WLL PIB2.
        SMF72WLN PIB2.    SMF72PGS PIB4.  SMF72PGL PIB2.
        SMF72PGN PIB2. @;
  OUTPUT T72;
  DO I=1 TO SMF72PRN;
  OFF = V -3 + SMF72PRS + (I-1)*SMF72PRL;
  INPUT @OFF SMF72MFV $CHAR2.  SMF72PRD $CHAR8.   SMF72IST PD4.
             SMF72DAT PD4.     SMF72INT RMFDUR.   SMF72MFL PIB2.
             SMF72SAM PIB4.    SMF72RV2 PIB2.     SMF72FLA PIB2.
             SMF72RLS $CHAR4.  SMF72CYC PD4.      SMF72MVS $CHAR8.
             SMF72IML PIB1.    SMF72PRF PIB1.     SMF72PTN PIB1.
             SMF72SRL PIB1.    SMF72IET $CHAR8.
  @;
  OUTPUT PRODUCT;
  END;
  IF SMF72STY = 1 THEN DO;   * TYPE 1;
   DO I = 0 TO (SMF72WLN-1);
     OFF = V - 3 + SMF72WLS + I * SMF72WLL;
     INPUT @OFF SMF72FGI PIB1.   SMF72RV3 PIB1.   SMF72SUB PIB2.
           SMF72HGP PIB2.   SMF72IPS $CHAR8. SMF72IRF $CHAR3.
           SMF72CRF $CHAR3. SMF72ERF $CHAR6. SMF72ISD $CHAR4.
           SMF72CSD $CHAR4. SMF72RV4 $CHAR4. SMF72SSD $CHAR4.
           SMF72OPT $CHAR8. SMF72ICS $CHAR8. SMF72SYS $CHAR4.
         SMF72CLS $CHAR10. SMF72USR $CHAR10. SMF72NAM $CHAR10.
         SMF72ADJ PIB4.    SM72MSD  $CHAR8.
      @ ;
       OUTPUT WORKLOAD;
   END;
   DO I = 0 TO (SMF72PGN-1);
     OFF = V - 3 + SMF72PGS + I * SMF72PGL;
      INPUT @OFF SMF72TTX PIB4. SMF72ACT PIB4. SMF72SER PIB4.
           SMF72TTM PIB4.   SMF72LEV PIB4.   SMF72MTS PIB4.
           SMF72ITS PIB4. SMF72CTS PIB4. SMF72TAT PIB4.
           SMF72SPP PIB4. SMF72CDN PIB2. SMF72PON PIB1.
           SMF72TSG PIB1. SMF72STS PIB4. SMF72ET1 PIB4.
           SMF72ET2 PIB4. SMF72PIN PIB4. SMF72FT1 PIB4.
           SMF72FT2 PIB4. SMF72HIN PIB4. SMF72HRM PIB4.
           SMF72BPI PIB4. SMF72PIE PIB4. SMF72BPE PIB4.
           SMF72BKA PIB4. SMF72BKE PIB4. SMF72ER1 PIB4.
           SMF72ER2 PIB4. SMF72RCT PIB4. SMF72IIT PIB4.
           SMF72HST PIB4. SMF72TST PIB4.
       @ ;
       OUTPUT PG;
   END;
   END;  * TYPE 1;
  IF SMF72STY = 2 THEN DO;   * TYPE 2;
   DO I = 0 TO (SMF72WLN-1);
     OFF = V - 3 + SMF72WLS + I * SMF72WLL;
     INPUT @OFF R722TSR  PIB2.   R7222RV3 PIB2.   R722TOT  PIB4.
           R722NXT  PIB4.   R722IPS $CHAR8.
      @ ;
       OUTPUT WRKLOAD2;
   END;
   DO I = 0 TO (SMF72PGN-1);
     OFF = V - 3 + SMF72PGS + I * SMF72PGL;
      INPUT @OFF R722PG   PIB2. R722DMN  PIB2. R722USER PIB4.
           R722ACTV PIB4.   R722PAGE PIB4.   R722SWAP PIB4.
           R722OUTR PIB4. R722ACTF PIB4. R722ACTS PIB4.
           R722IDLE PIB4. R722IDLS PIB4. R722PGIN PIB4.
           R722SLOT PIB4. R722DIV  PIB4. R722DIVS PIB4.
           R722FIX  PIB4. R722ET   PIB8. R722QT   PIB8.
           R722END  PIB4. R722UPRO PIB4. R722UDEV PIB4.
           R722DPRO PIB4. R722DDEV PIB4. R722DSTO PIB4.
           R722DJES PIB4. R722DHSM PIB4. R722DXCF PIB4.
           R722DENQ PIB4. R722DMNT PIB4. R722DMSG PIB4.
           R722UNKN PIB4. R722VALD PIB4. R722LSCT PIB4.
           R722ESCT PIB4. R722PSCT PIB4. R722LSCF PIB4.
           R722LSEF PIB4. R722LSSA PIB4. R722PSEF PIB4.
           R722PSSA PIB4. R722VECT PIB4. R722ACFF PIB4.
           R722IDLF PIB4. R722SLTF PIB4. R722DIVF PIB4.
           R722FIXF PIB4.
       @ ;
       OUTPUT PG2;
   END;
   END;  * TYPE 2;
RETURN;
PROC PRINT DATA=T72;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=PRODUCT;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=WORKLOAD;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=WRKLOAD2;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=PG;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=PG2;
FORMAT TIMEOUT DATETIME.;
