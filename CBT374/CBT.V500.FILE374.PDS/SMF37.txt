//C4SYSDMS JOB EDPCSBS1,'SCRD',MSGCLASS=X,CLASS=2,NOTIFY=ED115DM
//*
//SAS     EXEC SAS,OPTIONS=NODATE
//DATLIB  DD DSN=EDH1.PROD.SMFDUMPW,DISP=SHR
//*ATLIB  DD DSN=C4H1.PROD.SMFDUMPW,DISP=SHR
//*ATLIB  DD DSN=C4H1.PROD.SMFHIST.NOV86.COPY1,DISP=SHR
//*ATLIB  DD DSN=EDH1.PROD.C4#3.SMFDAILY.COPY1,DISP=SHR
//*ATLIB  DD DSN=SYS1.MAN1,DISP=SHR
//SYSIN   DD *
TITLE SMF 37;
  DATA I (KEEP =
           TIMEOUT
      SMFXXSID
      BRFRWID           BRFRSUBT
      BRFRPRDI          BRFRPRLN         BRFRPRNO
      BRFALLDI          BRFALLLN         BRFALLNO
      BRFEVTDI          BRFEVLN          BRFEVNO
      BRFSTADI          BRFSTALN         BRFSTANO
      BRFMODDI          BRFMODLN         BRFMODNO
      BRFLPDDI          BRFLPDLN         BRFLPDNO
      BRFLANDI          BRFLANLN         BRFLANNO
         )
      PRODUCT ( KEEP =
                   BRFSUBTY         BRFREVLT          BRFPRONM
                   BRFTIMS1 BRFTIMS2
         )
      COMMON  ( KEEP =
                   BRFDOMNM         BRFLRNM           BRFLRTY
                   BRFHINM1         BRFHITY1
                   BRFHINM2         BRFHITY2
                   BRFHINM3         BRFHITY3
                   BRFHINM4         BRFHITY4
                   BRFHINM5         BRFHITY5
                   BRFCPL           BRFALT
         )
      EVENT   ( KEEP =
                   BRFALRTT         BRFGENCA          BRFSPECA
                   BRFBLKID         BRFUACD
                   BRFUAQL1         BRFUAQL2
                   BRFUAQL3         BRF48TXT
                   BRFDBKID         BRFDUACD
                   BRFNMVTY
         )
      STATS   ( KEEP =
                   BRFTRFCC         BRFTEMPS
         )
      MODEM   ( KEEP =
                   BRFFAIL          BRFLNKTY
                   BRFLMSIN         BRFRMSIN
                   BRFMODOD         BRFMODTP
                   BRFMODSP         BRFLNQUL
                   BRFHTCTL         BRFLDBIN
                   BRFLDBNO         BRFLNQUR
                   BRFHTCTR         BRFRDBIN
                   BRFRDBNO
         )
         ;
  RETAIN V;
  INFILE DATLIB JFCB=JF MISSOVER;
  IF _N_ = 1 THEN DO;
                  INPUT @;
                  IF INDEX(JF,'SYS1.MAN') Â¬=0 THEN V=4; ELSE V=0;
                  END;
  IF V=4 THEN
      INPUT    SMFXXLEN PIB2. SMFXXSEG PIB2. @ ;
  INPUT    SMFXXFLG PIB1. SMFXXRTY PIB1.
                       @;
  IF SMFXXRTY = 212 THEN DO;
                        *PUT '212 DETECTED - SKIPPING';
                         DELETE;
                         END;
  INPUT    TIMEOUT SMFSTAMP8.
      SMFXXSID $CHAR4. @;
  IF SMFXXRTY = 37;
INPUT BRFRWID  $CHAR4.  BRFRSUBT $CHAR2.
      BRFRPRDI PIB4.    BRFRPRLN PIB2.   BRFRPRNO PIB2.
      BRFALLDI PIB4.    BRFALLLN PIB2.   BRFALLNO PIB2.
      BRFEVTDI PIB4.    BRFEVLN  PIB2.   BRFEVNO  PIB2.
      BRFSTADI PIB4.    BRFSTALN PIB2.   BRFSTANO PIB2.
      BRFMODDI PIB4.    BRFMODLN PIB2.   BRFMODNO PIB2.
      BRFLPDDI PIB4.    BRFLPDLN PIB2.   BRFLPDNO PIB2.
      BRFLANDI PIB4.    BRFLANLN PIB2.   BRFLANNO PIB2.
      @ ;
      OUTPUT I;
   IF BRFRPRNO > 0 THEN DO;
      DO I = 0 TO (BRFRPRNO - 1);
        OFF = BRFRPRDI -3+V + I*BRFRPRLN;
        INPUT @OFF BRFSUBTY PIB2.   BRFREVLT PIB1.    BRFPRONM $CHAR4.
                   BRFTIMS1 PD4.    BRFTIMS2 PD4.
        @ ;
        OUTPUT PRODUCT;
      END;
   END;
   IF BRFALLNO > 0 THEN DO;
      DO I = 0 TO (BRFALLNO - 1);
        OFF = BRFALLDI -3+V + I*BRFALLLN;
        INPUT @OFF BRFDOMNM $CHAR8. BRFLRNM  $CHAR8.  BRFLRTY  $CHAR4.
                   BRFHINM1 $CHAR8. BRFHITY1 $CHAR4.
                   BRFHINM2 $CHAR8. BRFHITY2 $CHAR4.
                   BRFHINM3 $CHAR8. BRFHITY3 $CHAR4.
                   BRFHINM4 $CHAR8. BRFHITY4 $CHAR4.
                   BRFHINM5 $CHAR8. BRFHITY5 $CHAR4.
                   BRFCPL   PIB1.   BRFALT   PIB1.
        @ ;
        OUTPUT COMMON ;
      END;
   END;
   IF BRFEVNO > 0 THEN DO;
      DO I = 0 TO (BRFEVNO - 1);
        OFF = BRFEVTDI -3+V + I*BRFEVLN;
        INPUT @OFF BRFALRTT PIB1.   BRFGENCA PIB1.    BRFSPECA PIB1.
                   BRFBLKID PIB2.   BRFUACD  PIB1.
                   BRFUAQL1 $CHAR8. BRFUAQL2 $CHAR8.
                   BRFUAQL3 $CHAR8. BRF48TXT $CHAR48.
                   BRFDBKID PIB2.   BRFDUACD PIB1.
                   BRFNMVTY PIB1.
        @ ;
        OUTPUT EVENT  ;
      END;
   END;
   IF BRFSTANO > 0 THEN DO;
      DO I = 0 TO (BRFSTANO - 1);
        OFF = BRFSTADI -3+V + I*BRFSTALN;
        INPUT @OFF BRFTRFCC PIB4.   BRFTEMPS PIB2.
        @ ;
        OUTPUT STATS  ;
      END;
   END;
   IF BRFMODNO > 0 THEN DO;
      DO I = 0 TO (BRFMODNO - 1);
        OFF = BRFMODDI -3+V + I*BRFMODLN;
        INPUT @OFF BRFFAIL  PIB1.   BRFLNKTY PIB1.
                   BRFLMSIN PIB1.   BRFRMSIN PIB1.
                   BRFMODOD $CHAR2. BRFMODTP $CHAR6.
                   BRFMODSP $CHAR4. BRFLNQUL PIB2.
                   BRFHTCTL PIB2.   BRFLDBIN PIB2.
                   BRFLDBNO PIB2.   BRFLNQUR PIB2.
                   BRFHTCTR PIB2.   BRFRDBIN PIB2.
                   BRFRDBNO PIB2.
        @ ;
        OUTPUT MODEM  ;
      END;
   END;
RETURN;
PROC PRINT DATA=I;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=PRODUCT;
PROC PRINT DATA=COMMON;
PROC PRINT DATA=EVENT;
PROC PRINT DATA=STATS;
PROC PRINT DATA=MODEM;
