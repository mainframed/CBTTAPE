//C4SYSDM6 JOB EDPCSBS1,'RMF76',MSGCLASS=X,CLASS=1,NOTIFY=ED115DM
//** XA FORMAT
//*
//SAS     EXEC SAS,OPTIONS=NODATE
//*ATLIB  DD DSN=C4H1.PROD.SMFDUMPW.HOLD,DISP=SHR
//DATLIB  DD DSN=SYS1.MAN2,DISP=SHR
//SYSIN   DD *
TITLE 'SMF 76';
DATA T76 (KEEP = TIMEOUT SMFXXSID
        SMF76SSI          SMF76STY        SMF76TRN
        SMF76RV1          SMF76PRS        SMF76PRL
        SMF76PRN          SMF76TCS        SMF76TCL
        SMF76TCN          SMF76TDS        SMF76TDL
        SMF76TDN          SMF76VFS        SMF76VFL
        SMF76VFN)
     PRODUCT (KEEP = TIMEOUT
             SMF76MFV          SMF76PRD           SMF76IST
             SMF76DAT          SMF76INT           SMF76MFL
             SMF76SAM          SMF76RV2           SMF76FLA
             SMF76RLS          SMF76CYC           SMF76MVS
             SMF76IML          SMF76RV9
             )
     TCONTROL (KEEP = TIMEOUT
             SMF76NUM          SMF76RV4
             )
     TDATA    (KEEP = TIMEOUT
             SMF76NAM          SMF76OPT         SMF76OP1
             SMF76SLN          SMF76DLN         SMF76SSS
             SMF76SSL          SMF76MIN         SMF76MAX
             SMF76AVG          S1F76STD         S2F76STD
                               S3F76STD         SMF76ENV
             )
     TDATAV   (KEEP = TIMEOUT  SMF76C
             )
          ;
  RETAIN V;
  INFILE DATLIB JFCB=JF MISSOVER;
  IF _N_ = 1 THEN DO;
                  INPUT @;
                  IF INDEX(JF,'SYS1.MAN') Â¬=0 THEN V=4; ELSE V=0;
                  END;
  IF V=4 THEN
      INPUT    SMFXXLEN PIB2. SMFXXSEG PIB2. @ ;
  INPUT    SMFXXFLG PIB1. SMFXXRTY PIB1.
                       @;
  IF SMFXXRTY = 212 THEN DO;
                        *PUT '212 DETECTED - SKIPPING';
                         DELETE;
                         END;
  IF SMFXXRTY = 76;
  INPUT    TIMEOUT SMFSTAMP8.
      SMFXXSID $CHAR4. @;
  INPUT SMF76SSI $CHAR4.  SMF76STY PIB2.  SMF76TRN PIB2.
        SMF76RV1 PIB2.    SMF76PRS PIB4.  SMF76PRL PIB2.
        SMF76PRN PIB2.    SMF76TCS PIB4.  SMF76TCL PIB2.
        SMF76TCN PIB2.    SMF76TDS PIB4.  SMF76TDL PIB2.
        SMF76TDN PIB2.    SMF76VFS PIB4.  SMF76VFL PIB2.
        SMF76VFN PIB2. @;
  OUTPUT T76;
  DO I=1 TO SMF76PRN;
  OFF = V -3 + SMF76PRS + (I-1)*SMF76PRL;
  INPUT @OFF SMF76MFV $CHAR2.  SMF76PRD $CHAR8.   SMF76IST PD4.
             SMF76DAT PD4.     SMF76INT RMFDUR.   SMF76MFL PIB2.
             SMF76SAM PIB4.    SMF76RV2 PIB2.     SMF76FLA PIB2.
             SMF76RLS $CHAR4.  SMF76CYC PD4.      SMF76MVS $CHAR8.
             SMF76IML PIB1.    SMF76RV9 PIB3.
  @;
  OUTPUT PRODUCT;
  END;
  DO I=1 TO SMF76TCN;
  OFF = V -3 + SMF76TCS + (I-1)*SMF76TCL;
  INPUT @OFF SMF76NUM PIB2.    SMF76RV4 PIB2.
  @;
  OUTPUT TCONTROL;
  END;
  DO I=1 TO SMF76TDN;
  OFF = V -3 + SMF76TDS + (I-1)*SMF76TDL;
  INPUT @OFF SMF76NAM $CHAR6.  SMF76OPT PIB1.   SMF76OP1 PIB1.
             SMF76SLN PIB1.    SMF76DLN PIB1.   SMF76SSS PIB2.
             SMF76SSL PIB2.    SMF76MIN PIB4.   SMF76MAX PIB4.
             SMF76AVG PIB8.    S1F76STD PIB4. S2F76STD PIB4.
                               S3F76STD PIB4.   SMF76ENV PIB4.
  @;
  OUTPUT TDATA;
  END;
  DO I=1 TO SMF76VFN;
  OFF = V -3 + SMF76VFS + (I-1)*SMF76VFL;
  INPUT @OFF SMF76C   PIB4.
  @;
  OUTPUT TDATAV;
  END;
RETURN;
PROC PRINT DATA=T76;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=PRODUCT;
FORMAT TIMEOUT DATETIME.;
FORMAT SMF76INT TIME.;
PROC PRINT DATA=TCONTROL;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=TDATA;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=TDATAV;
FORMAT TIMEOUT DATETIME.;
