//C4SYSDM5 JOB EDPCSBS1,'RMF75',MSGCLASS=X,CLASS=1,NOTIFY=ED115DM
//** XA FORMAT
//*
//SAS     EXEC SAS,OPTIONS=NODATE
//*ATLIB  DD DSN=C4H1.PROD.SMFDUMPW.HOLD,DISP=SHR
//DATLIB  DD DSN=SYS1.MAN1,DISP=SHR
//SYSIN   DD *
TITLE 'SMF 75';
DATA T75 (KEEP = TIMEOUT SMFXXSID
        SMF75SSI          SMF75STY        SMF75TRN
        SMF75PRS          SMF75PRL        SMF75PRN
        SMF75PSS          SMF75PSL        SMF75PSN
        )
     PRODUCT (KEEP = TIMEOUT
             SMF75MFV          SMF75PRD           SMF75IST
             SMF75DAT          SMF75INT           SMF75MFL
             SMF75SAM          SMF75RV2           SMF75FLA
             SMF75RLS          SMF75CYC           SMF75MVS
             SMF75IML          SMF75RV9
             )
     PGSPC    (KEEP = TIMEOUT
             SMF75DSN          SMF75PST           SMF75FL2
             SMF75RV5          SMF75TYP           SMF75CHA
             SMF75VOL          SMF75RV3           SMF75SLA
             SMF75MXU          SMF75MNU           SMF75AVU
             SMF75BDS          SMF75USE           SMF75REQ
             SMF75SIO          SMF75PGX           SMF75RV8
             )
          ;
  RETAIN V;
  INFILE DATLIB JFCB=JF MISSOVER;
  IF _N_ = 1 THEN DO;
                  INPUT @;
                  IF INDEX(JF,'SYS1.MAN') Â¬=0 THEN V=4; ELSE V=0;
                  END;
  IF V=4 THEN
      INPUT    SMFXXLEN PIB2. SMFXXSEG PIB2. @ ;
  INPUT    SMFXXFLG PIB1. SMFXXRTY PIB1.
                       @;
  IF SMFXXRTY = 212 THEN DO;
                        *PUT '212 DETECTED - SKIPPING';
                         DELETE;
                         END;
  IF SMFXXRTY = 75;
  INPUT    TIMEOUT SMFSTAMP8.
      SMFXXSID $CHAR4. @;
  INPUT SMF75SSI $CHAR4.  SMF75STY PIB2.  SMF75TRN PIB2.
        SMF75RV  PIB2.    SMF75PRS PIB4.  SMF75PRL PIB2.
        SMF75PRN PIB2.    SMF75PSS PIB4.  SMF75PSL PIB2.
        SMF75PSN PIB2.
         @;
  OUTPUT T75;
  DO I=1 TO SMF75PRN;
  OFF = V -3 + SMF75PRS + (I-1)*SMF75PRL;
  INPUT @OFF SMF75MFV $CHAR2.  SMF75PRD $CHAR8.   SMF75IST PD4.
             SMF75DAT PD4.     SMF75INT RMFDUR.   SMF75MFL PIB2.
             SMF75SAM PIB4.    SMF75RV2 PIB2.     SMF75FLA PIB2.
             SMF75RLS $CHAR4.  SMF75CYC PD4.      SMF75MVS $CHAR8.
             SMF75IML PIB1.    SMF75RV9 PIB3.
  @;
  OUTPUT PRODUCT;
  END;
  DO I=1 TO SMF75PSN;
  OFF = V -3 + SMF75PSS + (I-1)*SMF75PSL;
  INPUT @OFF SMF75DSN $CHAR44. SMF75PST PIB1.     SMF75FL2 PIB1.
             SMF75RV5 PIB1.    SMF75TYP PIB4.     SMF75CHA PIB2.
             SMF75VOL $CHAR6.  SMF75RV3 PIB5.     SMF75SLA PIB4.
             SMF75MXU PIB4.    SMF75MNU PIB4.     SMF75AVU PIB4.
             SMF75BDS PIB4.    SMF75USE PIB4.     SMF75REQ PIB4.
             SMF75SIO PIB4.    SMF75PGX PIB4.     SMF75RV8 PIB4.
  @;
  OUTPUT PGSPC;
  END;
RETURN;
PROC PRINT DATA=T75;
FORMAT TIMEOUT DATETIME.;
PROC PRINT DATA=PRODUCT;
FORMAT TIMEOUT DATETIME.;
FORMAT SMF75INT TIME.;
PROC PRINT DATA=PGSPC;
FORMAT TIMEOUT DATETIME.;
