         PRINT GEN
         MACRO
&LABEL   TEXTBILD &AREA=,&TEXT=
.*+-----------------------------------------------------------------+
.*|  MACRO TO BUILD TEXT LINES FROM INDIVIDUAL WORDS AND PHRASES.   |
.*|   E.G.                                                          |
.*|           TEXTBILD  AREA=OUTAREA,TEXT=(AVALUE,BVALUE,CVALUE)    |
.*|                                                                 |
.*|  AVALUE   DC  H'5',C'ALPHA'                                     |
.*|  BVALUE   DC  H'7',C'-BRAVO-'                                   |
.*|  CVALUE   DC  H'7',C'CHARLIE'                                   |
.*|                                                                 |
.*|  OUTAREA  DS  CL80                                              |
.*|                                                                 |
.*|  CLEARS THE OUTAREA DATA ITEM WITH BLANKS, THEN                 |
.*|  PRODUCES THE FOLLOWING IN OUTAREA: 'ALPHA-BRAVO-CHARLIE'       |
.*|                                                                 |
.*|  UPON COMPLETION:  R1  POINTS TO OUTAREA                        |
.*|                    R15 HAS TOTAL LENGTH OF TEXT (19)            |
.*|                                                                 |
.*|  NOTES 1.  THE LENGTH OF THE CLEAR OPERATION IS TAKEN FROM      |
.*|            THE DEFINED LENGTH OF THE 'AREA' DATA ITEM.          |
.*|        2.  THE CODE PRODUCED BY THIS MACRO IS REENTERABLE.      |
.*+-----------------------------------------------------------------+
         LCLA  &N1,&N2
&N1      SETA  N'&TEXT                  NUMBER OF TEXT OPERANDS
&N2      SETA  1                        INDEX OF INITIAL TEXT OPERAND
.*
&LABEL   MVI   &AREA,X'40'              CLEAR THE TEXT LINE
         MVC   &AREA+1(L'&AREA-1),&AREA   WITH BLANKS
.*
         LA    15,&AREA                 R15 POINTS TO TEXT BEGIN
.LOOP1   ANOP
         LH    1,&TEXT(&N2)             R1 HAS LENGTH OF TEXT
         BCTR  1,0                        MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND MOVE
         MVC   0(*-*,15),&TEXT(&N2)+2     MOVE THE DATA
         EX    1,*-6                    EXECUTE THE MOVE
         LA    15,1(1,15)               R15 POINTS JUST BEYOND CHARS
&N2      SETA  &N2+1                    POINT TO NEXT TEXT OPERAND
         AIF   (&N2 LE &N1).LOOP1       LOOP BACK UP
.*
         LA    1,&AREA                  R1 HAS ADDR OF AREA
         SR    15,1                     R15 HAS TOTAL LENGTH OF TEXT
.*
         MEND
*
         EJECT
*
         LCLC  &MYNAME                  LOCAL CONSTANT
*
&MYNAME  SETC  'GTEUA60'                CSECT NAME
RBASE    EQU   12                       BASE REGISTER FOR CSECT
RBAL     EQU   11                       BAL REGISTER
*
         TITLE '&MYNAME'                LISTING TITLE
**********************************************************************
***                                                                ***
***                   GTEUA60                                      ***
***                                                                ***
***  DESCRIPTION:                                                  ***
***                                                                ***
***    The UPDATE function of this program is to copy a member of  ***
***    a PDS into a Panvalet library.  This program receives a     ***
***    3-10 character Panvalet member name as an input parm, and   ***
***    also a 52 character Panvalet comment. The program truncates ***
***    the last two characters from the Panvalet name to create    ***
***    the PDS member name.                                        ***
***                                                                ***
***    The RESTORE function of this program is similar to UPDATE,  ***
***    except that it copies in the other direction, from the      ***
***    Panvalet library to the PDS.  For the RESTORE function, the ***
***    52-character comment parm is optional, and is ignored.      ***
***    The RESTORE function also copies the RESTOREd Panvalet      ***
***    member to the next Panvalet name in the sequence.           ***
***                                                                ***
***  The following code will invoke this program:                  ***
***                                                                ***
*** *------------------------------------------------------------- ***
*** MACALL   LA    R15,MAFUNCT              ADDRESS OF PARM        ***
***          ST    R15,PARM60+00              INTO PARM LIST       ***
***          LA    R15,NEXTNAME             ADDRESS OF PARM        ***
***          ST    R15,PARM60+04              INTO PARM LIST       ***
***          LA    R15,PANCOM               ADDRESS OF PARM        ***
***          ST    R15,PARM60+08              INTO PARM LIST       ***
***          LA    R15,COPYNAME             ADDRESS OF PARM        ***
***          ST    R15,PARM60+12              INTO PARM LIST       ***
***          OI    PARM60+12,X'80'          MARK END OF LIST       ***
***          LA    R1,PARM60                R1 POINTS TO LIST      ***
***          L     R15,=V(GTEUA60)          ADDRESS OF SUBROUTINE  ***
***          BALR  R14,R15                  CALL THE SUBROUTINE    ***
*** *------------------------------------------------------------- ***
*** PARM60   DS    4A                       PARMLIST FOR SUBROUTNE ***
*** NEXTNAME DS    CL10                     PAN NAME TO BE ADDED   ***
*** MAFUNCT  DS    CL12                    'RESTORE' 'UPDATE' ETC. ***
*** COPYNAME DS    CL10                     PAN NAME TO BE COPIED  ***
*** PANCOM   GTEUM03  DSECT=NO              PANVALET COMMENT AREA  ***
*** *------------------------------------------------------------- ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  FUNCTION:                                                     ***
***                                                                ***
***  1.  Create input text names for the PDS member and PAN member ***
***      from the input parm.                                      ***
***  2.  Initialize the DCB and Panvalet parm list, and open the   ***
***      output file which will be used as SYSIN input by PAN#1.   ***
***      The DDNAME is in the macro at label CNTLDCBD.             ***
***      The PDS DDNAME is in the data at label PDSDDN.            ***
***  3.  Create and write the ++OPTION, ++ADD, and ++COMMENT       ***
***      card images.                                              ***
***  4.  Close the file.                                           ***
***  5.  LINK to the PAN#1 program.                                ***
***                                                                ***
***                                                                ***
***  NOTES:                                                        ***
***                                                                ***
***  1. Do not hard link the PAN#1 program.  It is not reenterable ***
***     or reusable, and it is huge.                               ***
***                                                                ***
***  2. Additionally, when PAN#1 is invoked, it will require the   ***
***     following files:                                           ***
***        PANDD1   - The Panvalet library.                        ***
***        SYSPRINT - Output listing.                              ***
***                                                                ***
***                                                                ***
***  RETURN CODES:                                                 ***
***     00 - OKAY                                                  ***
***     12 - Error returned from PAN#1.                            ***
***     16 - Input name was shorter than 3 characters.             ***
***                                                                ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  REGISTER USAGE:                                               ***
***                                                                ***
***     R0-R3   - Linkage conventions, work, macros.               ***
***     R4      - (unused)                                         ***
***     R5      - (unused)                                         ***
***     R6      - (unused)                                         ***
***     R7      - (unused)                                         ***
***     R8      - Base of COPYNAME parm.                           ***
***     R9      - Base of NEXTNAME parm.                           ***
***     R10     - Base of PANCOM parm.                             ***
***     R11     - Linkage register used with BAL.                  ***
***     R12     - Base register of program code.                   ***
***     R13     - Linkage conventions, base of WORKD area.         ***
***     R14-R15 - Linkage conventions, work, macros.               ***
***                                                                ***
***                                                                ***
***  NON-IBM MACROS USED:                                          ***
***                                                                ***
***     GTEUM03  - Description of Panvalet comment area.           ***
***                                                                ***
***  ASSEMBLER OPTIONS:       RENT                                 ***
***  LINKAGE EDITOR OPTIONS:  RENT, REUS, REFR                     ***
***                                                                ***
**********************************************************************
         EJECT
**********************************************************************
***                                                                ***
***  CULPRIT:                                                      ***
***                                                                ***
***     Chuck Hoffman, IBM Systems Programming                     ***
***     GTE Laboratories, Technical Computation Center             ***
***     40 Sylvan Road                                             ***
***     Waltham, Massachusetts  02254                              ***
***                                                                ***
***                     617/466-2131    V/N 679-2131               ***
***                                                                ***
**********************************************************************
         SPACE 3
**********************************************************************
***                                                                ***
***  MAINTENANCE HISTORY:                                          ***
***                                                                ***
***  1.  A.  Initial version.                                      ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
***                                                                ***
**********************************************************************
         EJECT
***********************************************************************
***                                                                 ***
***      LINKAGE CONVENTIONS ENTERING PROGRAM                       ***
***                                                                 ***
***********************************************************************
&MYNAME  CSECT ,
         STM   R14,R12,12(R13)          SAVE REGS TO CALLER S.A.
         B     (BEGIN-&MYNAME)(R15)     BRANCH AROUND EYECATCHER
         DC    A(L'NAME)                LENGTH OF CSECT NAME
NAME     DC    C'&MYNAME'               CSECT NAME
         DC    C' &SYSDATE &SYSTIME '   ASSEMBLY DATE/TIME STAMP
BEGIN    LR    RBASE,R15                LOAD BASE REGISTER
         USING &MYNAME,RBASE              ADDRESSABILITY
         PRINT NOGEN
         GETMAIN R,LV=WORKDLEN          GET SAVE/WORK AREA
         ST    R1,8(0,R13)              MY S.A. ADDR INTO CALLER S.A.
         ST    R13,4(0,R1)              CALLER S.A. ADDR INTO MY S.A.
         LR    R13,R1                   R13 POINTS TO MY S.A.
         USING WORKD,R13                ADDRESSABILITY OF SAVE AREA
         L     R1,4(0,R13)              R1 POINTS TO CALLER S.A.
         LM    R15,R1,16(R1)            R15 R0 AND R1 ARE RESTORED
*
         EJECT
***********************************************************************
***                                                                 ***
***      MAINLINE ROUTINE                                           ***
***                                                                 ***
***********************************************************************
MAIN     EQU   *                        BEGIN MAINLINE ROUTINE
         ST    R1,R1SAVE                SAVE INITIAL R1
         XC    COMPCODE,COMPCODE        CLEAR COMPLETION CODE
         MVI   SWITCH0,B'00000000'      INITIALIZE SWITCH AREA
*
MAPARMS  L     R2,0(0,R1)               R2 POINTS TO FUNCTION PARM
         MVC   FUNCTION,0(R2)             MOVE INTO STORAGE
         LM    R9,R10,4(R1)             LOAD OTHER PARM ADDRESSES
         USING NEXTNAME,R9                ADDRESSABILITY
         USING PANCOM,R10                 ADDRESSABILITY
         L     R8,12(0,R1)              LOAD ADDR OF COPY PARM
         USING COPYNAME,R8                ADDRESSABILITY
*
MANAMES  BAL   RBAL,NAMES               PROCESS THE NAME PARM
         CLC   RETCDE,F0                IF BAD RETURN
         BNE   END16                      GO TO ERROR ENDING
*
MAOPEN   BAL   RBAL,OPEN                OPEN THE OUTPUT FILE
MAPUT    BAL   RBAL,PUT                 WRITE THE OUTPUT RECORDS
MACLOSE  BAL   RBAL,CLOSE               CLOSE THE OUTPUT FILE
*
MAPAN#1  BAL   RBAL,PAN#1               EXECUTE THE PANVALET PROGRAM
         CLC   RETCDE,F0                IF BAD RETURN
         BNE   END12                      GO TO ERROR ENDING
*
         B     ENDING                   GO TO ENDING
*
         EJECT
***********************************************************************
***                                                                 ***
***      LINKAGE CONVENTIONS EXITING PROGRAM                        ***
***                                                                 ***
***********************************************************************
*
END00    LA    R15,00                   IF PROPOSED COMPLETION CODE
         C     R15,COMPCODE               IS GREATER THAN STORED CODE
         BNH   *+8                          THEN
         ST    R15,COMPCODE                   STORE THE PROPOSED CODE
         B     ENDING                   GO TO ENDING
*
END16    LA    R15,16                   IF PROPOSED COMPLETION CODE
         C     R15,COMPCODE               IS GREATER THAN STORED CODE
         BNH   *+8                          THEN
         ST    R15,COMPCODE                   STORE THE PROPOSED CODE
         B     ENDING                   GO TO ENDING
*
END12    LA    R15,12                   IF PROPOSED COMPLETION CODE
         C     R15,COMPCODE               IS GREATER THAN STORED CODE
         BNH   *+8                          THEN
         ST    R15,COMPCODE                   STORE THE PROPOSED CODE
         B     ENDING                   GO TO ENDING
*
ENDING   L     R14,COMPCODE             R14 SAVES COMP CODE
         LR    R1,R13                   R1 SAVES ADDR OF MY S.A.
         L     R13,4(0,R1)              R13 RESTORED, PTR CALLER S.A.
         FREEMAIN R,LV=WORKDLEN,A=(R1)  FREE MY SAVE/WORK AREA
         LR    R15,R14                  R15 SET TO COMP CODE
         LM    R0,R12,20(R13)           R0-R12 RESTORED
         L     R14,12(0,R13)            R14 RESTORED
         MVI   12(R13),X'FF'            SET COMPLETION SIGNAL
         BR    R14                      RETURN TO CALLER
*
         EJECT
**********************************************************************
***                                                                ***
***  INITIALIZE THE DCB AND OPEN THE OUTPUT FILE.                  ***
***  INITIALIZE THE PANVALET PARM WITH THE DDNAME.                 ***
***                                                                ***
**********************************************************************
OPEN     ST    RBAL,OPRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         MVC   CNTLDCB(CNTLDCBL),CNTLDCBD  INITIALIZE THE DCB
         MVC   OPENL(OPENLLN),OPENLD    INITIALIZE THE OPEN PARM LIST
*
         MVC   PANPARM,=H'8'            SET PARM LENGTH
         MVC   PANPARM+2(8),CNTLDCB+(DCBDDNAM-IHADCB)  COPY THE DDNAME
         LA    R1,PANPARM               ADDR OF PARM
         ST    R1,PANPARML                INTO ONE-MEMBER LIST
         OI    PANPARML+00,X'80'        MARK END OF ONE-MEMBER LIST
*
         LA    R3,OPENL                 R3 POINTS TO THE PARM LIST
         LA    R2,CNTLDCB               R2 POINTS TO THE DCB
         OPEN  ((R2),(OUTPUT)),MF=(E,(R3))  OPEN THE FILE
*
         B     OPEND
*
OPRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     OPEND                    BRANCH TO ENDING
*
OPEND    L     RBAL,OPRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***  CLOSE THE OUTPUT FILE.                                        ***
***                                                                ***
**********************************************************************
CLOSE    ST    RBAL,CLRBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         MVC   CLOSL(CLOSLLN),CLOSD     INITIALIZE THE CLOSE PARM LIST
*
         LA    R3,CLOSL                 R3 POINTS TO THE PARM LIST
         LA    R2,CNTLDCB               R2 POINTS TO THE DCB
         CLOSE ((R2)),MF=(E,(R3))       CLOSE THE FILE
*
         B     CLEND
*
CLRET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     CLEND                    BRANCH TO ENDING
*
CLEND    L     RBAL,CLRBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***  CREATE THE PDS AND PAN NAME TEXT UNITS FROM THE INPUT NAME    ***
***                                                                ***
**********************************************************************
NAMES    ST    RBAL,NARBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
NASUFF   LA    R2,NEXTNAME              R2 POINTS TO NAME
         BCTR  R2,0                     R2 POINTS JUST BEFORE NAME
         LA    R3,10                    R3 IS COUNTER
*
NALOOP1  LA    R2,1(0,R2)               R2 POINTS TO NEXT CHARACTER
         CLI   0(R2),X'40'              IF BLANK
         BE    NALENGTH                   BRANCH OUT
         BCT   R3,NALOOP1               LOOP BACK UP
*
         LA    R2,1(0,R2)               FALL THRU: POINT TO NEXT CHAR
*
*                                       R2 NOW POINTS JUST BEYOND NAME
NALENGTH LA    R3,NEXTNAME              R3 POINTS TO NAME
         SR    R2,R3                    R2 IS LENGTH OF PAN NAME
         C     R2,=F'3'                 IF LENGTH LT 3
         BL    NARET04                    GO TO ERROR
         STH   R2,NAPAN                 SAVE THE LENGTH OF PAN NAME
         STH   R2,NAPANCOP              SAVE SAME LENGTH OF COPY NAME
         S     R2,=F'2'                 R2 IS LENGTH OF PDS NAME
         STH   R2,NAPDS                 SAVE THE LENGTH OF PDS NAME
*
NANAMES  LH    R2,NAPAN                 R2 IS LENGTH OF PAN NAME
         BCTR  R2,0                       MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND MOVE
         MVC   NAPAN+2(0),NEXTNAME        MOVE NAME TO STORAGE
         EX    R2,*-6                   EXECUTE THE MOVE
         B     *+10                     BRANCH AROUND MOVE
         MVC   NAPANCOP+2(0),COPYNAME     MOVE COPY NAME TO STORAGE
         EX    R2,*-6                   EXECUTE THE MOVE
         LH    R2,NAPDS                 R2 IS LENGTH OF PDS NAME
         BCTR  R2,0                       MINUS 1 FOR EXEC
         B     *+10                     BRANCH AROUND MOVE
         MVC   NAPDS+2(0),NEXTNAME        MOVE NAME TO STORAGE
         EX    R2,*-6                   EXECUTE THE MOVE
*
         B     NAEND
*
NARET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     NAEND                    BRANCH TO ENDING
*
NAEND    L     RBAL,NARBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***  CREATE AND WRITE PANVALET CONTROL CARDS TO OUTPUT FILE        ***
***                                                                ***
**********************************************************************
PUT      ST    RBAL,PURBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
PUFUNCT  CLC   FUNCTION,RESTORE         IF IT IS A RESTORE
         BE    PURESTOR                   GENERATE ++WRITE CARDS
         B     PUOTHER                  ELSE GENERATE ++ADD CARDS
*
PURESTOR TEXTBILD  AREA=PULINE1,TEXT=(OPTOUT,PDSDDN,COMMA,NAPDS)
         TEXTBILD  AREA=PULINE2,TEXT=(WRITE,NAPAN)
         TEXTBILD  AREA=PULINE3,TEXT=(COPY,NAPAN,COMMA,NAPANCOP)
         LA    R3,3                     R3 IS COUNTER
         B     PUTOUT                   GO TO WRITE IT
*
PUOTHER  MVC   PUCOM(52),PANCOM         MOVE THE TEXT TO WORK AREA
         MVC   PUCOM(2),=H'50'          REPLACE '* ' WITH LENGTH
         TEXTBILD  AREA=PULINE1,TEXT=(OPTION,PDSDDN,COMMA,NAPDS)
         TEXTBILD  AREA=PULINE2,TEXT=(ADD,NAPAN,DATA)
         TEXTBILD  AREA=PULINE3,TEXT=(COMMENT,NAPAN,COMMA,PUCOM)
         LA    R3,3                     R3 IS COUNTER
         B     PUTOUT                   GO TO WRITE IT
*
PUTOUT   LA    R2,PULINE1-80            R2 POINTS TO -1 LINE
PULOOP1  LA    R2,80(0,R2)              R2 POINTS TO NEXT LINE
         PUT   CNTLDCB,(R2)             WRITE THE LINE
         BCT   R3,PULOOP1               LOOP BACK UP
*
         B     PUEND
*
PURET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     PUEND                    BRANCH TO ENDING
*
PUEND    L     RBAL,PURBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
**********************************************************************
***                                                                ***
***  EXECUTE THE PANVALET PROGRAM.                                 ***
***                                                                ***
**********************************************************************
PAN#1    ST    RBAL,PARBALSV            SAVE RETURN ADDRESS
         XC    RETCDE,RETCDE            SET ERROR CODE TO ZERO
*
         LA    R1,PANPARML              POINT TO THE PARM LIST
         LINK  EP=PAN#1                 EXECUTE PANVALET
         LTR   R15,R15                  IF NOT RC=00
         BNE   PARET04                    GO TO HIGH RETURN
*
         B     PAEND
*
PARET04  LA    R15,X'04'                RETURN CODE TO R15
         ST    R15,RETCDE               SAVE IT
         B     PAEND                    BRANCH TO ENDING
*
PAEND    L     RBAL,PARBALSV            RESTORE RETURN ADDRESS
         BR    RBAL                     RETURN
*
         EJECT
***********************************************************************
***                                                                 ***
***      FIXED DATA AREA                                            ***
***                                                                 ***
***********************************************************************
F0       DC    F'0'                     CONSTANT
BLANKS   DC    CL80' '                  LITERAL
X00S     DC    XL40'00'                 LITERAL
*
CNTLDCBD DCB   DDNAME=WRK002,DSORG=PS,LRECL=80,BLKSIZE=80,RECFM=F,     X
               MACRF=PM
OPENLD   OPEN  (,),MF=L
CLOSD    CLOSE (),MF=L
*
OPTION   DC    H'15',C'--OPTION INPUT,'
OPTOUT   DC    H'16',C'--OPTION OUTPUT,'
ADD      DC    H'06',C'--ADD '
DATA     DC    H'05',C',DATA'
COMMENT  DC    H'10',C'--COMMENT '
COMMA    DC    H'01',C','
WRITE    DC    H'13',C'--WRITE WORK,'
COPY     DC    H'07',C'--COPY '
*
RESTORE  DC    CL12'RESTORE'            FUNCTION LITERAL
*
PDSDDN   DC    H'03',CL8'PDS'
*
         LTORG
*
         EJECT
***********************************************************************
***                                                                 ***
***      DSECT FOR MY SAVE AREA AND VARIABLES.                      ***
***                                                                 ***
***********************************************************************
WORKD    DSECT
MYSAVE   DS    18F                      MY REGISTER SAVE AREA
COMPCODE DS    F                        PROGRAM COMPLETION CODE
RETCDE   DS    F                        INTERNAL RETURN CODE
R1SAVE   DS    F                        INITIAL VALUE IN R1
*
SWITCH0  DS    B'00000000'              SWITCH AREA
*                1.......               X'80' - (NOT USED)
*                .1111111               (NOT USED)
*
OPRBALSV DS    F                        RETURN ADDRESS SAVE AREA
CLRBALSV DS    F                        RETURN ADDRESS SAVE AREA
NARBALSV DS    F                        RETURN ADDRESS SAVE AREA
PURBALSV DS    F                        RETURN ADDRESS SAVE AREA
PARBALSV DS    F                        RETURN ADDRESS SAVE AREA
*
FUNCTION DS    CL12                     FUNCTION NAME
NAPDS    DS    H,CL10                   TEXT OF PDS MEMBER NAME
NAPAN    DS    H,CL10                   TEXT OF PAN MEMBER NAME
NAPANCOP DS    H,CL10                   TEXT OF PAN COPY MEMBER NAME
LINEOUT  DS    CL80                     OUTPUT TEXT LINE
*
PUCOM    DS    H,CL50                   TEXT MADE FROM PAN COMMENT
*
PULINE1  DS    CL80                     OUTPUT TEXT TO BE BUILT
PULINE2  DS    CL80                     OUTPUT TEXT TO BE BUILT
PULINE3  DS    CL80                     OUTPUT TEXT TO BE BUILT
PULINES  EQU   (*-PULINE1)/80           NUMBER OF OUTPUT LINES
*
PANPARML DS    A                        PARM LIST FOR PANVALET
PANPARM  DS    H,CL100                  LENGTH, TEXT OF PARM
*
CNTLDCB  DCB   DDNAME=WRK002,DSORG=PS,LRECL=80,BLKSIZE=80,RECFM=F,     X
               MACRF=PM
CNTLDCBL EQU   *-CNTLDCB
*
OPENL    OPEN  (,),MF=L
OPENLLN  EQU   *-OPENL
*
CLOSL    CLOSE (),MF=L
CLOSLLN  EQU   *-CLOSL
*
         DS    0D
WORKDLEN EQU   *-WORKD
*
         EJECT
***********************************************************************
***                                                                 ***
***      MAPPING DSECTS                                             ***
***                                                                 ***
***********************************************************************
PANCOM   GTEUM03  DSECT=YES             PANVALET COMMENT AREA
*
NEXTNAME DSECT
         DS       CL10                  PANVALET NAME TO BE ADDED
COPYNAME DSECT
         DS       CL10                  PANVALET NAME TO BE COPIED
*
         IHADCB  DSORG=PS,DEVD=DA
*
         EJECT
***********************************************************************
***                                                                 ***
***      REGISTER EQUATES                                           ***
***                                                                 ***
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
         END
