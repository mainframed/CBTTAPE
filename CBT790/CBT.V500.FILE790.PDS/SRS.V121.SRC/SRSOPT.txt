*/*********************************************************************
*/* SRSOPT - Specify SRS installtion-wide defaults                    *
*/*********************************************************************
*/* Change log:                                                      */
*/*   DWD 12/04: New for V121                                    V121*/
*/*********************************************************************
*
         MACRO
         SRSOPT &DSECT=NO,                                             X
               &CLASS=,                                                X
               &DEST=LOCAL,                                            X
               &DISP=DELETE,                                           X
               &FORMS=STD,                                             X
               &JOBNAME=,                                              X
               &LIM=,                                                  X
               &PPARM=,                                                X
               &PROG=SRSGENER,                                         X
               &SEP=NO,                                                X
               &SEPID=*SRS>,                                           X
               &STATUS=NONHELD,                                        X
               &WAIT=,                                                 X
               &WRITER=
*
         AIF    ('&DSECT' NE 'YES').NODSECT
SRSOPT   DSECT
&NEYE    SETC  'SRSOEYE'
&NLVL    SETC  'SRSOLVL'
&NCLS    SETC  'SRSOCLAS'
&NDST    SETC  'SRSODEST'
&NDISP   SETC  'SRSODISP'
&NFORM   SETC  'SRSOFORM'
&NJOBN   SETC  'SRSOJOBN'
&NLIM    SETC  'SRSOLIMT'
&NPPA    SETC  'SRSOPPRM'
&NPGM    SETC  'SRSOPGMN'
&NSEP    SETC  'SRSOSEPO'
&NSEPI   SETC  'SRSOSEPI'
&NSTAT   SETC  'SRSOSTAT'
&NWAIT   SETC  'SRSOWAIT'
&NWTR    SETC  'SRSOWTRN'
         AGO   .GENCODE
.NODSECT ANOP
SRSOPT   CSECT
SRSOPT   AMODE  31
SRSOPT   RMODE  ANY
&NEYE    SETC  ''
&NLVL    SETC  ''
&NCLS    SETC  ''
&NDST    SETC  ''
&NDISP   SETC  ''
&NFORM   SETC  ''
&NJOBN   SETC  ''
&NLIM    SETC  ''
&NPPA    SETC  ''
&NPGM    SETC  ''
&NSEP    SETC  ''
&NSEPI   SETC  ''
&NSTAT   SETC  ''
&NWAIT   SETC  ''
&NWTR    SETC  ''
.GENCODE ANOP
         AIF   ('&DISP' NE 'KEEP' AND '&DISP' NE 'DELETE').DISPERR
         AIF   ('&SEP' NE 'YES' AND '&SEP' NE 'NO' AND                 X
                '&SEP' NE 'JOB' AND '&SEP' NE 'OUTGRP' AND             X
                '&SEP' NE 'DATASET').SEPERR
         AIF   ('&STATUS' NE 'HELD' AND '&STATUS' NE 'NONHELD' AND     X
                '&STATUS' NE 'ALL').STATERR
.* Check for &CLASS= specified
&DCLS    SETC  'DC'
         AIF    ('&CLASS' NE '').DSET1
&DCLS    SETC  'DS'
.DSET1   ANOP
.* Check if &DEST= specified
&DDST    SETC  'DC'
         AIF    ('&DEST' NE '').DSET2
&DDST    SETC  'DS'
.DSET2   ANOP
.* Check if &FORMS= specified
&DFORM   SETC  'DC'
         AIF    ('&FORMS' NE '').DSET3
&DFORM   SETC  'DS'
.DSET3   ANOP
.* Check if &JOBNAME= specified
&DJOBN   SETC  'DC'
         AIF    ('&JOBNAME' NE '').DSET4
&DJOBN   SETC  'DS'
.DSET4   ANOP
.* Check if &PPARM= specified
&DPPA    SETC  'DC'
         AIF    ('&PPARM' NE '').DSET5
&DPPA    SETC  'DS'
.DSET5   ANOP
.* Check if &SEPID= specified
&DSEPI   SETC  'DC'
         AIF    ('&SEPID' NE '').DSET6
&DSEPI   SETC  'DS'
.DSET6   ANOP
.* Check if &WAIT= specified
&DWAIT   SETC  'DC'
         AIF    ('&WAIT' NE '').DSET7
&DWAIT   SETC  'DS'
.DSET7   ANOP
.* Check if &WRITER= specified
&DWTR    SETC  'DC'
         AIF    ('&WRITER' NE '').DSET8
&DWTR    SETC  'DS'
.DSET8   ANOP
SRSOMAC  EQU    2                       Level of SRSOPT         @A12101
.*=====================================================================
.* NOTE: Whenever this macro is updated, SRSOMAC *MUST* be incremented.
.* This will force a re-assembly of any down-level options modules.
.*=====================================================================
&NEYE    DC     C'SRSOPT_&SYSDATE._&SYSTIME. '
&NLVL    DC     AL1(SRSOMAC)            Options module level
&NCLS    &DCLS  CL1'&CLASS'             SYSOUT Class
&NDST    &DDST  CL18'&DEST'             Destination ID
&NDISP   DC     CL6'&DISP'              Processed SYSOUT disposition
&NFORM   &DFORM CL8'&FORMS'             Forms ID
&NJOBN   &DJOBN CL8'&JOBNAME'           Jobname
&NLIM    DC     CL4'&LIM'               SYSOUT limit
&NPPA    &DPPA  CL100'&PPARM'           Despooling program parms
&NPGM    DC     CL8'&PROG'              Despooling program name
&NSEP    DC     CL7'&SEP'               Separator record option
&NSEPI   &DSEPI CL8'&SEPID'             Separator record ID
&NSTAT   DC     CL7'&STATUS'            SYSOUT status
&NWAIT   &DWAIT CL3'&WAIT'              WAIT-for-SYSOUT option
&NWTR    &DWTR  CL8'&WRITER'            WRITER ID
         DS     0D
         AIF    ('&DSECT' NE 'YES').NODLEN
SRSOPTL  EQU    *-SRSOPT
         AGO   .NOEND
.NODLEN  ANOP
         END
.NOEND   ANOP
         MEXIT
.DISPERR MNOTE 8,'ERROR: DISP= must be KEEP or DELETE'
         MEXIT
.SEPERR  MNOTE 8,'ERROR: SEP= must be YES NO JOB OUTGRP or DATASET'
         MEXIT
.STATERR MNOTE 8,'ERROR: STATUS= must be HELD NONHELD or ALL'
         MEXIT
         MEND
