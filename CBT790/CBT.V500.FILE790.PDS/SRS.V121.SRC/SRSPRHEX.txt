*PROCESS RENT
SRSPRHEX TITLE '- HEX to EBCDIC conversion routine'
SRSPRHEX CSECT , ATTR=RENT,REUS,REFR
SRSPRHEX AMODE 31
SRSPRHEX RMODE ANY
***********************************************************************
* SRS general purpose hexadecimal-to-ebcdic conversion routine        *
*                                                                     *
* This program converts hexadecimal characters to printable ebcdic    *
* characters.                                                         *
*                                                                     *
* On entry to program,                                                *
*   R1 - contains the four-byte hex field to be converted             *
*   R13 - contains the address of an eighteen-byte save area          *
*   R14 - contains the return address                                 *
*   R15 - contains the program's entry point                          *
* On return to caller,                                                *
*   R0 & R1 - contains the converted string                           *
***********************************************************************
         SAVE  (14,12),,SRSPRHEX_&SYSDATE._&SYSTIME.
         LR    R12,R15             Set up base reg
         USING SRSPRHEX,R12        Establish addressability
         LR    R5,R1               Save data
         GETMAIN R,LV=WORKLEN      Get workarea core
         LR    R11,R1              Save address
         USING WORKAREA,R11
         XC    WORKAREA(WORKLEN),WORKAREA      Clear work area
         ST    R5,FIELD1           Move data to work area
         UNPK  FIELD2(9),FIELD1(5)
         NC    FIELD2(8),=X'0F0F0F0F0F0F0F0F'  Turn off high order bits
         TR    FIELD2(8),=C'0123456789ABCDEF'  Convert to EBCDIC
         LM    R3,R4,FIELD2        Load translated data
         FREEMAIN R,A=(R11),LV=WORKLEN         Free work area
         LR    R0,R3
         LR    R1,R4
         L     R14,12(R13)         Restore R14
         RETURN (3,12),,RC=0       Return
WORKAREA DSECT
FIELD1   DS    F,CL1
FIELD2   DS    2F,CL1
WORKLEN  EQU   *-WORKAREA
         YREGS
         END
