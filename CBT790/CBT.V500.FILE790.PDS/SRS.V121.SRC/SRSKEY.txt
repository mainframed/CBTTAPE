*/*********************************************************************
*/* SRSKEY - Define SRS keywords                                      *
*/*********************************************************************
*/* Change log:                                                      */
*/*   DWD 12/04: New for V121                                    V121*/
*/*********************************************************************
*
         MACRO
         SRSKEY &KEYWORD=,                                             X
               &LOC=,                                                  X
               &ROUTINE=0,                                             X
               &TBLK=0,                                                X
               &DEFAULT=0,                                             X
               &ABBREV=0
*
         LA    R15,&LOC                Addr of loc to store value
         ST    R15,SRSKWLOC            Save value addr
         LA    R15,L'&LOC              Get length of storage loc
         STC   R15,SRSKWLOL            Save length
         LA    R15,&ROUTINE            Processing routine addr
         ST    R15,SRSKWRTN            Save routine addr
         AIF    ('&TBLK' EQ '0').NOTBLK
         LA    R15,&TBLK-SRSTBLK       Offest of value into TBLK
         STH   R15,SRSKWTBK            Save TBLK offset
.NOTBLK  ANOP
         AIF    ('&DEFAULT' EQ '0').NODEFA
         LA    R15,&DEFAULT-SRSOPT     Offest of default into OPTS
         STH   R15,SRSKWDEF            Save default offset
.NODEFA  ANOP
         LA    R15,=C'&KEYWORD'        Addr of keyword name
         ST    R15,SRSKWNAM            Save name addr
         LA    R15,L'=C'&KEYWORD'      Length of keyword name
         STC   R15,SRSKWNAL            Save keyword name length
         AIF    ('&ABBREV' EQ '0').NOABBREV
         MVI   SRSKWSIG,&ABBREV        Set significant chars
         AGO   .ABBREVX
.NOABBREV ANOP
         STC   R15,SRSKWSIG            All chars are significant
.ABBREVX  ANOP
         LA    R1,SRSKWL(R1)           Point to next entry
         MEND
