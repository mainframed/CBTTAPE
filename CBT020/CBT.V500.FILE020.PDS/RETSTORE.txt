/***** REXX ******************** REXX ********************************/
/*                                                                  V*/
/*                       DASD UCB TABLE LOOKUP                       */
/*  THIS REXX PROGRAM SCANS ALL DASD UCBS AND REPORTS FOR THOSE THAT */
/*  ARE MOUNTED WITH THE STORAGE ATTRIBUTE.                          */
/*                                                                  V*/
/*===================================================================*/
/*               UCBTBYT WORD AT X'10' OFF INTO UCB                  */
/*  LOCATE UCB:  CVT+X'4E4'(CVTUCBA) POINTS TO THE 1ST UCB ENTRY     */
/*  UCBSTAT   -> UCB+X'03'- X'80'/ONLINE, X'08'/ALLOCATED            */
/*  UCBTBYT   -> UCB+X'10' 4 BYTESLONG                               */
/*   XX...... -> THE 1ST BYTE IS THE MODEL FLAG    (UCBTBYT1)        */
/*   ..XX.... -> THE 2ND BYTE IS THE FEATURE FLAG  (UCBTBYT2)        */
/*               X'0E' TYPE DEVICE TYPE 3380                         */
/*               X'0F' TYPE DEVICE TYPE 3390                         */
/*   ....XX.. -> THE 3RD BYTE MUST BE 20 FOR DASD  (UCBTBYT3)        */
/*               X'80' FOR TAPE DEVICE                               */
/*               X'40' FOR COMMUNICATION DEVICE                      */
/*               X'41' FOR CTC DEVICE                                */
/*               X'20' FOR DASD DEVICE                               */
/*               X'10' FOR DISPLAY DEVICE                            */
/*               X'08' FOR UNIT RECORD DEVICE                        */
/*               X'24' FOR CHARACTER READER DEVICE                   */
/*   ......XX -> THE 4TH BYTE IS THE TYPE TYPE     (UCBTBYT4)        */
/*  UCBSTAB   -> UCB+X'22' 1 BYTE LONG                               */
/*               X'40' FOR PACK BEING USED AS PAGE PACK              */
/*               X'10' FOR PRIVATE PACK ATTRIBUTE                    */
/*               X'08' FOR PUBLIC PACK ATTRIBUTE                     */
/*               X'04' FOR STORAGE PACK ATTRIBUTE                    */
/*               X'01' FOR CONTROL VOLUME (CATALOG RESIDES)          */
/*********************************************************************/
CVT      = STORAGE(10,4)                       /* LOAD A(CVT)        */
DCVT     = C2D(CVT)                            /* CONVERT TO DECIMAL */
UCB    = STORAGE(D2X(DCVT+X2D('4E4')),4)       /* LOAD A(CVTUCBA)    */
DUCB   = C2D(UCB)                              /* CONVERT TO DECIMAL */

RETC = 0                                       /* INIT RETURN COUNT  */
TOTAL  = 0                                     /* INIT TOTAL COUNTER */
LINEC  = 0                                     /* INIT LINE CONTROL  */
OFFCNT = 0                                     /* INIT OFFLINE COUNT */
ONCNT  = 0                                     /* INIT ON LINE COUNT */

DO WHILE DUCB \= 0
  UCBN   = STORAGE(D2X(DUCB+X2D('04')),2)      /* LOAD UCBNUM ADDR   */
  UCBNX  = STORAGE(D2X(DUCB+X2D('08')),4)      /* LOAD A(UCBNEXT)    */
  UCBSTAT= STORAGE(D2X(DUCB+X2D('03')),1)      /* LOAD UCB STATUS    */
  UCBTYP = STORAGE(D2X(DUCB+X2D('11')),1)      /* LOAD UCBTBYT2 TYPE */
  UCBDEV = STORAGE(D2X(DUCB+X2D('12')),1)      /* LOAD UCBTBYT3 TYPE */
  UCBTYP = STORAGE(D2X(DUCB+X2D('13')),1)      /* LOAD UCBTBYT4 TYPE */
  UCBSTAB= STORAGE(D2X(DUCB+X2D('22')),1)      /* LOAD UCB ATTRIBUTE */
  IF UCBDEV = '20'X THEN DO                    /* IS THIS A DASD UCB */
     UCBN = SUBSTR(C2X(UCBN),2)                /* CONV UCBNUM TO CHAR*/
     VOL = STORAGE(D2X(DUCB+X2D('1C')),6)      /* LOAD UCBVOLI       */
     IF UCBSTAB = '04'X THEN DO                /* IS IT A STO-PACK?  */
        ATTB = STO                             /* YES, USE IT        */
        SAY UCBN VOL ATTB
        RETC = RETC + 1                        /* COUNT              */
      END
  END
  UCB   =  UCBNX                               /* GET NEXT UCB ADDR. */
  DUCB  =  C2D(UCB)                            /* CONVERT TO DECIMAL */
  TOTAL = TOTAL + 1                            /* COUNT VOLUME       */
  ITERATE                                      /* GO GET NEXT UCB    */
END                                            /*                    */
