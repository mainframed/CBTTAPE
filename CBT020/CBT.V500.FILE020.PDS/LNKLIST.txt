/*  THIS REXX LISTS ALL THE LLT LIBRARIES BY BROWSING              */
/*  THE LINK LIST TABLE ADDRESSED FROM OFFSET 1244                 */
/*  IN THE CVT.                                                    */
/*  THE LAYOUT IS:                                                 */
/*         CONSTANT             3 'LLT'                            */
/*         NUMBER OF ENTRIES    4                                  */
/*         LEN OF ENTRY         1        )   TIMES NUM             */
/*         DSN                  44       )   OF ENTRIES            */
/*  THE DSN IS PADDED WITH BLANKS SO THE LENGTH                    */
/*  ISN'T USED.                                                    */
ARG     FLNAME
CLEAR                             /* CLEAR SCREEN BEFORE STARTING  */

CVT     = STORAGE(10,4)           /* MEMORY @ 16 POINTS TO CVT     */
CCVT    = C2X(CVT)                /* CHARACTERIZE CVT ADDRESS      */
DCVT    = C2D(CVT)                /* DECIMAL CVT ADDR              */

SMCA    = DCVT + 196              /* ADD JESCT OFFSET (C4)         */
SMCA    = D2X(SMCA)               /* CONVERT TO HEX                */
SMCA    = STORAGE(SMCA,4)         /* GET SMCA ADDRESS FROM CVT     */
SMCA    = C2X(SMCA)               /* CHARACTERIZE SMCA ADDRESS     */
IF LEFT(SMCA,1) > 7 THEN DO       /* CHECK FOR 31 BIT ADDRESS      */
   CHAR = LEFT(SMCA,1) - 8        /* REMOVE HIGH BIT FROM NIBBLE   */
   SMCA = CHAR || RIGHT(SMCA,7)   /* REMAKE ADDRESS MINUS HIGH BIT */
END
SMCA    = X2D(SMCA)               /* DECIMALIZE ADDRESS            */
SMFID   = SMCA + 16               /* POINT TO SMFID ( OFFSET 16 )  */
SMFID   = D2X(SMFID)              /* HEXIFY ADDRESS OF SMFID       */
SMFID   = STORAGE(SMFID,4)        /* GET SMFID FROM MEMORY         */

DLLT    = DCVT + 1244             /* DECIMAL LLT ADDR              */
LLT     = D2C(DLLT,4)             /* LLT  TAB ADDRESS              */
CLLT    = C2X(LLT)                /* CHAR LLT ADDR                 */
ADDR1   = STORAGE(CLLT,4)         /* ADDRESS OF AUTH TAB           */
CADDR1  = C2X(ADDR1)              /* CHAR ADDR OF LLT              */
DATA    = STORAGE(CADDR1,8)       /* GET LUMP OF DATA              */
NUMENT  = SUBSTR(DATA,5,4)        /* NUMBER OF ENTRIES             */
DNUMENT = C2D(NUMENT)             /* CONVERT TO DECIMAL            */
LEN     = (DNUMENT * 45) + 8      /* MAX SIZE REQ                  */
DATA    = STORAGE(CADDR1,LEN)     /* GET LUMP OF DATA              */

DLINK   = DCVT + 8                /* DECIMAL LINK ADDR             */
HLINK   = D2X(DLINK)              /* HEX LINK ADDRESS              */
LADDR   = STORAGE(HLINK,4)        /* TABLE ADDRESS                 */
CLADDR  = C2X(LADDR)              /* CHAR ADDR OF LINK             */
DLADDR  = X2D(CLADDR)             /* DECIMALIZE ADDR               */
DLADDR  = DLADDR + 44             /* ADD OFFSET OF 44(2C)          */
HLADDR  = D2X(DLADDR)             /* RE-HEXIFY ADDR                */
LDCB    = STORAGE(HLADDR,4)       /* GET DCB ADDRESS               */
CLDCB   = C2X(LDCB)               /* CONVERT TO CHARS              */
DLDCB   = X2D(CLDCB)              /* DECIMALIZE ADDRESS            */
HLDCB   = DLDCB + 32              /* ADD OFFSET FOR #1             */

NEWSTACK
IF  FLNAME \= '' THEN
    "ALLOC FI(TEMP) DA("FLNAME") SHR REU"
SAY '       LINKLIST LIBRARIES ('SMFID')'
SAY '   ' DATE(U) '     ' TIME()
SAY 'BEG TTR  END TTR    LIBRARY NAME'
IF FLNAME ¬= ' ' THEN DO
   QUEUE '       LINKLIST LIBRARIES ('SMFID')'
   QUEUE '   ' DATE(U) '     ' TIME()
   QUEUE 'BEG TTR  END TTR    LIBRARY NAME'
END
START   = 10                      /* START OF 1ST ENTRY            */
DO I = 1 TO DNUMENT
   DSN   = SUBSTR(DATA,START,44)
   START = START + 45
   HLDCB = D2X(HLDCB)
   TTRS = STORAGE(HLDCB,16)
   CYL1 = SUBSTR(TTRS,7,2)
   CYL2 = SUBSTR(TTRS,11,2)
   HEAD1 = SUBSTR(TTRS,13,1)
   HEAD2 = SUBSTR(TTRS,14,1)
   CYL1 = C2X(CYL1)
   CYL2 = C2X(CYL2)
   HEAD1 = C2X(HEAD1)
   HEAD2 = C2X(HEAD2)
   DSN = CYL1 HEAD1' ' CYL2 HEAD2' ' DSN
   SAY DSN
   IF FLNAME ¬= ' ' THEN QUEUE DSN
   HLDCB = X2D(HLDCB)
   HLDCB = HLDCB + 16
END
SAY
SAY 'THE NUMBER OF LIBRARIES IN THE LNKLST IS ' DNUMENT
QUEUE ' '
QUEUE 'THE NUMBER OF LIBRARIES IN THE LNKLST IS ' DNUMENT
IF  FLNAME \= '' THEN DO
   SAY '** SAVING LIST TO' FLNAME' STARTED **'
   "EXECIO * DISKW TEMP (FINIS"
   "FREE F(TEMP)"
END
DELSTACK
