/******************************  REXX  *******************************/
/*     THIS REXX PROGRAM LISTS ALL THE APF LIBRARIES BY BROWSING     */
/*     THE AUTHORIZED LIBRARY TABLE AT OFFSET 484 IN THE CVT.        */
/*     THE LAYOUT IS: NUMBER OF ENTRIES    2                         */
/*                    LEN OF ENTRY         1        )   TIMES        */
/*                    VOLUME               6        )   NUMBER       */
/*                    DSN                  VARIABLE )   OF ENTRIES   */
/*********************************************************************/
ARG     NAME
CLEAR                                        /* CLEAR SCREEN        */
CVT     = STORAGE(10,4)                      /* DECIMAL 16 ==> CVT  */
CCVT    = C2X(CVT)                           /* CHAR CVT ADDRESS    */
DCVT    = C2D(CVT)                           /* DECIMAL CVT ADDR    */
DAUTH   = DCVT + 484                         /* DECIMAL AUTH ADDR   */
AUTH    = D2C(DAUTH,4)                       /* AUTH TAB ADDRESS    */
CAUTH   = C2X(AUTH)                          /* CHAR AUTH TAB ADDR  */
ADDR1   = STORAGE(CAUTH,4)                   /* ADDRESS OF AUTH TAB */
CADDR1  = C2X(ADDR1)                         /* CHAR ADDR OF AUTH   */
DATA    = STORAGE(CADDR1,2)                  /* GET LUMP OF DATA    */
NUMENT  = SUBSTR(DATA,1,2)                   /* NUMBER OF ENTRIES   */
DNUMENT = C2D(NUMENT)                        /* CONVERT TO DECIMAL  */
LEN     = (DNUMENT * 51) + 2                 /* MAX STORAGE POSS    */
DATA    = STORAGE(CADDR1,LEN)                /* GET LUMP OF DATA    */
START   = 3                                  /* START OF 1ST ENTRY  */
HEADER  = 'AUTHORIZED LIBRARY LIST'          /* INIT HEADER LINES   */
HEADER  = CENTER(HEADER,80)                  /* CENTERS IT          */
SAY     HEADER                               /* PUT IT OUT          */
HEADER  = 'SEQ  VOLUME  DATASET NAME'        /* INIT HEADER LINES   */
HEADER  = LEFT(HEADER,56)                    /* FORMAT INTO 56 BYTE */
HEADER  = CENTER(HEADER,79)                  /* CENTERS IT          */
SAY     HEADER                               /* PUT IT OUT          */
HEADER  = '===  ======  ===============' ||, /* INIT HEADER LINES   */
          '============================='    /*   "     "     "     */
HEADLEN = LENGTH(HEADER)                     /* GET HEADER LENGTH   */
HEADER  = CENTER(HEADER,79)                  /* CENTERS THE HEADER  */
SAY     HEADER                               /* PUT IT OUT          */
DO I = 1 TO DNUMENT                          /* PROCESS ALL ENTRY   */
   LEN     = SUBSTR(DATA,START,1)            /* GET 1ST ENTRY LENGTH*/
   DLEN1   = C2D(LEN)                        /* CONVERT TO DECIMAL  */
   DLEN2   = DLEN1 - 6                       /* DECREASE BY 6       */
   VOL     = SUBSTR(DATA,START+1,6)          /* GET VOLUME SERIAL   */
   DSN     = SUBSTR(DATA,START+7,DLEN2)      /* GET DATASET NAME    */
   START   = START + 1 + DLEN1               /* BUMP TO NEXT ENTRY  */
   SEQ     = RIGHT(I,3)                      /* FORMAT SEQ. NUMBER  */
   HEADER  = SEQ || ' ' VOL || '  ' ||,      /* FORMAT DATA LINE    */
             DSN                             /*    "        "       */
   HEADER  = LEFT(HEADER,HEADLEN)            /* JUSTIFY TO 56 BYTES */
   HEADER  = CENTER(HEADER,79)               /* CENTERS IT          */
   IF   LENGTH(NAME) > 0 THEN                /* ANY PARM ENTERED?   */
        DO                                   /* YES, CHECK IT OUT   */
           LEN = LENGTH(NAME)                /* GET PARM LENGTH     */
           IF  SUBSTR(VOL,1,LEN) = NAME |,   /* IS IT MATCH VOLSER  */
               SUBSTR(DSN,1,LEN) = NAME THEN /* IS IT MATCH DSNAME  */
               SAY HEADER                    /* YES, PUT IT OUT     */
        END                                  /*                     */
   ELSE SAY    HEADER                        /* NO PARM, PUT ALL OUT*/
END                                          /* END OF DO WHILE     */
SAY                                          /* PUT OUT A BLANK LINE*/
HEADER  = 'THE NUMBER OF APF LIBRARIES',     /* INIT TOTAL LINE     */
          'IS ' DNUMENT                      /*                     */
HEADER  = CENTER(HEADER,79)                  /* CENTERS IT          */
SAY     HEADER                               /* PUT IT OUT          */
