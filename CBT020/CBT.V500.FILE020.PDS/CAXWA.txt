/***** REXX **********************************************************/
/*                                                                   */
/*                    SYSTEM CAXWA TABLE LOOKUP                      */
/*  THIS REXX PROGRAM IS USED TO MAP ALL OPEN USER CATAOLGS AND      */
/*  SHOW RELATED UCB INFORMATION FROM THE DASD THEY RESIDE UPON.     */
/*                                                                   */
/*===================================================================*/
/*                      CONTROL BLOCK CHAINING                       */
/*  AMCBS      : CVT+X'100' ACCESS METHOD CONTROL BLOCK  (CVTCBSP)   */
/*  CAXWA     -> AMCBS+X'14' CATALOG WORK AREA                       */
/*  CAXNEXT   -> CAXWA+X'04' NEXT CAXWA ENTRY                        */
/*  CATFLAG1  -> CAXWA+X'08' CATALOG FLAG BYTE 1                     */
/*   X....... -> BUILD REQUESTED                                     */
/*   .X...... -> OPEN IN CONTROL                                     */
/*   ..X..... -> CLOSE IN CONTROL                                    */
/*   ...X.... -> END OF VOLUME IN CONTROL                            */
/*   ....X... -> I/O SUPPORT COMPLETED                               */
/*   .....X.. -> 1:MASTER CATALOG, 0:USER CATALOG                    */
/*   ......X. -> CATALOG MANAGEMENT IN CONTROL                       */
/*   .......X -> SCHEDULER IN CONTROL                                */
/*  CATFLAG2  -> CAXWA+X'09' CATALOG FLAG BYTE 2                     */
/*   X....... -> CATALOG DELETED                                     */
/*   .X...... -> NO DDNAME FOUND                                     */
/*   ..X..... -> UNABLE TO GET CORE                                  */
/*   ...X.... -> I/O ERROR                                           */
/*   ....X... -> RPL CLEAN UP REQUESTED                              */
/*   .....X.. -> FREE CAXWA IF ERROR                                 */
/*   ......X. -> RECOVERABLE CATALOG                                 */
/*   .......X -> VOLUME TIME STAMP UPDATED                           */
/*  CATFLAG3  -> CAXWA+X'0A' CATALOG FLAG BYTE 3                     */
/*   X....... -> CRA ALT TIOT EXIST                                  */
/*   .X...... -> CRA NOT EXIST                                       */
/*   ..X..... -> RESERVED                                            */
/*   ...X.... -> CATALOG IS RAC DEFINED                              */
/*   ....X... -> NO CATALOG PSWD DEFINED                             */
/*   .....XX. -> 01 NO UPDATE 10 UPDATE                              */
/*   .......X -> RESERVED                                            */
/*   CATACT   -> CAXWA+X'0B' CATALOG ACTIVITY                        */
/*   X....... -> IN STORAGE                                          */
/*   .X...... -> ALLOCATED                                           */
/*  CATUCBP   -> CAXWA+X'1C' ADDRESS OF CATALOG UCB                  */
/*  CATNAME   -> CAXWA+X'34' CATALOG NAME FIELD 44 BYTES             */
/*                                                                   */
/*********************************************************************/
ARG     OPTION                                /* PULL IN OPTION PARM */
CLEAR                                         /* CLEAR SCREEN        */
CVT     = STORAGE(10,4)                       /* ADDR CVT            */
DCVT    = C2D(CVT)                            /* CONVERT TO DECIMAL  */
AMCB    = STORAGE(C2X(D2C(DCVT+X2D(100))),4)  /* ADDR AMCB FROM CVT  */
DAMCB   = C2D(AMCB)                           /* CONVERT TO DECIMAL  */
CAXWA   = STORAGE(C2X(D2C(DAMCB+X2D(14))),4)  /* ADDR CAXWA FROM AMCB*/
HEADER  = 'ACTIVE CATALOG TABLE LOOKUP'       /* INIT HEADER LINE    */
HEADER  = CENTER(HEADER,80)                   /* CENTERS IT          */
IF   SYSVAR(SYSNEST) = NO THEN                /* CALL BY PROGRAM?    */
     SAY     HEADER                           /* NO, PUT OUT HEADER  */
ELSE DO                                       /* YES, INIT RETURN    */
        RETC = 0                              /* YES, INIT RETURN CNT*/
        RETD = ''                             /* YES, INIT RETURN    */
     END                                      /* YES, INIT RETURN    */
HEADER  = '=============================' ||, /* INIT HEADER LINE    */
          '=================================' /*   "    "     "   "  */
HEADER  = CENTER(HEADER,79)                   /* CENTERS IT          */
IF   SYSVAR(SYSNEST) = NO THEN                /* CALL BY PROGRAM?    */
     SAY     HEADER                           /* NO, PUT OUT HEADER  */
DO   I  = 1 BY 1 UNTIL CAXWA = '00000000'X        /* SCAN CAXWA      */
     DCAX    = C2D(CAXWA)                         /* CONVERT TO DEC. */
     CAXNEXT = STORAGE(C2X(D2C(DCAX+X2D(04))),4)  /* ADDR NEXT CAXWA */
     CATFLAG = STORAGE(C2X(D2C(DCAX+X2D(08))),1)  /* MAP CATALOG TYPE*/
     CATUCBP = STORAGE(C2X(D2C(DCAX+X2D(1C))),4)  /* MAP CATALOG UCBP*/
     CATNAME = STORAGE(C2X(D2C(DCAX+X2D(34))),44) /* MAP CATALOG NAME*/
     CATNAME = STRIP(CATNAME,T,' ')               /* STRIP TRAILER SP*/
     DUCB    = C2D(CATUCBP)                       /* CONVERT TO DEC. */
     CATADDR = STORAGE(C2X(D2C(DUCB+X2D(0D))),3)  /* MAP CATALOG ADDR*/
     CATVOLS = STORAGE(C2X(D2C(DUCB+X2D(1C))),6)  /* MAP CATALOG VOLS*/
     IF    BITAND(CATFLAG,'04'X) = '04'X THEN     /* IS IT MASTER CAT*/
           TYPE = MCAT                            /* YES SAY IT      */
     ELSE  TYPE = UCAT                            /* MAP UCB TYPE 2  */
     LINE = '|  UCB=' || CATADDR ' VOL=' ||,      /* INIT DATA LINE  */
            CATVOLS ' DSN=' || CATNAME ||,        /*   "   "    "    */
            '  TYPE=' || TYPE || '  |'            /*   "   "    "    */
     LINE = CENTER(LINE,79)                   /* CENTERS IT          */
     IF    SYSVAR(SYSNEST) = NO  &,           /* CALL BY TERMINAL    */
           (LENGTH(OPTION) = 0   |,           /* AND NO PARM ENTERED */
            OPTION = TYPE)    THEN            /* OR A MATCH WAS FOUND*/
            SAY  LINE                         /* NO, PUT OUT THE LINE*/
     ELSE  IF   OPTION = TYPE |,              /* NO, IS TYPE MATCH   */
                LENGTH(OPTION) = 0 THEN       /* OR ALL REQUESTED    */
                DO                            /* YES, DO RETURN PROC */
                   LINE =  CATVOLS CATADDR,   /* YES, PUT OUT LINE   */
                           CATNAME            /*         "           */
                   RETC = RETC + 1            /* INCREASE RETC BY 1  */
                   RETD = RETD || LINE        /* SAVE RETURN DATA    */
                END                           /* END OF PROCESS      */
     CAXWA   = CAXNEXT                        /* BUMP TO NEXT CAXWA  */
END                                           /* END OF PROCESS      */
HEADER  = '=============================' ||, /* INIT HEADER LINE    */
          '=================================' /*   "    "     "   "  */
HEADER  = CENTER(HEADER,79)                   /* CENTERS IT          */
IF   SYSVAR(SYSNEST) = NO THEN                /* ANY OPTION ENTERED  */
     DO                                       /* DO NOPARM PROCESS   */
        SAY     HEADER                        /* PUT OUT HEADER 1    */
        RETURN(0)                             /* RETURN COND=0       */
     END                                      /* END OF NOPARM PROC  */
ELSE DO                                       /* YES, RETURN MATCHED */
        RETC = LEFT(RETC,4)                   /* FORMAT RETURN COUNT */
        RETD = RETC || RETD                   /* CONCAT COUNT & DATA */
        IF   SYSVAR(SYSNEST) = NO THEN        /* CALLED BY OTHER PGM */
             RETURN(0)                        /* NO RETURN 0         */
        ELSE RETURN(RETD)                     /* RETURN THE REQ-DATA */
     END
