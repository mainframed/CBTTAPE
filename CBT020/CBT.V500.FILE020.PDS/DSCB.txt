/****************************** REXX ********************************/
/*                                                                  */
/*                          NAME: DSCB                              */
/*  THIS REXX PROGRAM IS USED TO DISPLAY THE FORMAT-1 DSCB INFO FOR */
/*  A CATALOGUED NON-VSAM DATASET.  ALL INFORMATION WILL BE MAPPED  */
/*  AS PER THE MACRO IECPDSCB.                                      */
/*                                                                  */
/********************************************************************/
                                                 /*                  */
ARG INDSN                                        /* ALLOW DSNAME IN  */
IF   LENGTH(INDSN) = 0 THEN                      /* ANY IN DSNAME?   */
     DO                                          /* NO, GO PROMPT    */
        I = 1                                    /*     FOR INPUT    */
        SAY 'ENTER DATASET NAME YOU WANT TO',    /* PROMPT MESSAGE   */
            'LOOK AT:'                           /*                  */
        DO UNTIL I > 3 |,                        /* GIVING 3 TRIES   */
                 LENGTH(INDSN) > 0               /*                  */
           PARSE PULL INDSN                      /* READ DSNAME      */
           IF    LENGTH(INDSN) = 0 THEN          /* ANY DATA ENTERED */
                 SAY 'TRY AGAIN?'                /* NO, TRY AGAIN    */
           I = I + 1                             /* INCREASE COUNT   */
        END                                      /*                  */
     END                                         /*                  */
IF   SYSDSN(INDSN) \= OK THEN                    /* IS DSNAME EXIST  */
     DO                                          /*                  */
        SAY INDSN 'NOT FOUND OR NO DATASET,',    /* NO, SHOW ERROR   */
            'REQUEST BYPASSED'                   /* NO, SHOW ERROR   */
        RETURN                                   /*     ABORT        */
     END                                         /*                  */
X = OUTTRAP("TRAP.","*")                         /* ENABLE TRAPING   */
LISTD INDSN LABEL                                /* TRAP ALL USERCAT */
X = OUTTRAP("OFF")                               /* DISABLE TRAPING  */
IF   TRAP.0 = 0 THEN RETURN                      /* ANY DATA TRAPED? */
MEM   = ''                                       /* INIT MEMBER NAME */
DSNAME= TRAP.1                                   /*    FORM DSNAME   */
RECFM = WORD(TRAP.3,1)                           /*     FORM RECFM   */
LRECL = WORD(TRAP.3,2)                           /*     FORM LRECL   */
BLKSZ = WORD(TRAP.3,3)                           /*     FORM BLKSZ   */
DSORG = WORD(TRAP.3,4)                           /*     FORM DSORG   */
VOLSER= STRIP(TRAP.5,L,' ')                      /*    FORM VOLSER   */
PROC  = SUBSTR(TRAP.6,1,6)                       /* PROCESSTRAP DATA */
IF    LRECL = VSAM THEN                          /* IS IT A VSAM FILE*/
      DO                                         /* YES, ABORT WITH  */
         SAY 'UNABLE TO PROCESS A VSAM',         /*      ERROR       */
             'DATASET, REQUEST ABORTED'          /*      MESSAGE     */
         RETURN                                  /*                  */
      END                                        /*                  */
IF    PROC = '--MEMB' THEN                       /* SELECT FOLLOWING */
      DO                                         /*                  */
         I = 10                                  /*                  */
         MEM   = WORD(TRAP.7,1)                  /*                  */
         TTR   = WORD(TRAP.7,2)                  /*                  */
         ALIAS = WORD(TRAP.7,3)                  /*                  */
         DIR   = SUBWORD(TRAP.7,6,4)             /*                  */
         DIR   = DIR TRAP.8                      /*                  */
      END                                        /*                  */
 ELSE I = 7                                      /*                  */
 DSCB = TRAP.I                                   /* TRAP DSCB OUTPUT */
 I = I + 1                                       /*                  */
 DSCB = DSCB TRAP.I                              /*  2ND LINE OF DSCB*/
 I = I + 1                                       /*                  */
 DSCB = DSCB TRAP.I                              /*  3RD LINE OF DSCB*/
IF   LENGTH(MEM) = 0 THEN                        /* ANY MEM-NAME?    */
     SIGNAL FMTDSCB                              /* NO, GO GET DSCB  */
CLEAR                                            /* CLEAR SCREEN     */
TITLE = 'DATASET FORMAT-1 DSCB LOOKUP'           /* PREPARE HEADER   */
SAY     CENTER(TITLE,79)                         /* CENTER & SAY IT  */
DATA = 'DSNAME:' DSNAME '   '  ||,               /* FORM HEADER LINE */
       'VOLSER:' VOLSER                          /*                  */
SAY CENTER(DATA,79)                              /* PUT OUT HEADER   */
DATA = 'RECFM:' || RECFM '  DSORG:' || DSORG ,   /* FORM HEADER INFO */
       '  LRECL:' || LRECL '  BLKSZ:' || BLKSZ   /*                  */
SAY CENTER(DATA,79)                              /* PUT OUT INFO LINE*/
DATA = '*' || LEFT('-',71,'-') || '*'            /* FORM DATA SCREEN */
SAY CENTER(DATA,79)                              /* PUT IT OUT       */
MUSER= X2C(WORD(DIR,6)) ||,                      /* FORM MEMBER USER */
       X2C(WORD(DIR,7))                          /*                  */
CDATE= SUBSTR(WORD(DIR,2),3,5)                   /*      CREATE DATE */
MDATE= SUBSTR(WORD(DIR,3),3,5)                   /*      MODIFY DATE */
MTIME= SUBSTR(WORD(DIR,4),1,4)                   /*      MODIFY TIME */
MTIME= INSERT(':',MTIME,2)                       /*                  */
VERNO= SUBSTR(WORD(DIR,1),1,4)                   /*      VERSION NO. */
VERNO= INSERT('.',VERNO,2)                       /*                  */
CSIZE= X2D(SUBSTR(WORD(DIR,4),5,4))              /*      CURRENT SIZE*/
ISIZE= X2D(SUBSTR(WORD(DIR,5),1,4))              /*      INITIAL SIZE*/
DATA = '| ' || 'MEMBER NAME: ' || LEFT(MEM,8) ,  /* FORM DATA LINE   */
       '| ' || 'MODIFIED BY: ' || LEFT(MUSER,8), /*                  */
       '| ' || 'VERSION/MOD: ' || LEFT(VERNO,8), /*                  */
       '|'                                       /*                  */
SAY CENTER(DATA,79)                              /*                  */
DATA = '| ' || 'MODIFY TIME: ' || LEFT(MTIME,8), /*                  */
       '| ' || 'MODIFY DATE: ' || LEFT(MDATE,8), /*                  */
       '| ' || 'CREATE DATE: ' || LEFT(CDATE,8), /*                  */
       '|'                                       /*                  */
SAY CENTER(DATA,79)                              /*                  */
DATA = '| ' || 'TTR ADDRESS: ' || LEFT(TTR,8) ,  /*                  */
       '| ' || 'CURRENT SZ : ' || LEFT(CSIZE,8), /*                  */
       '| ' || 'INITIAL SZ : ' || LEFT(ISIZE,8), /*                  */
       '|'                                       /*                  */
SAY CENTER(DATA,79)                              /*                  */
DATA = '| ' || 'ALIAS  NAME: ' || LEFT(ALIAS,8), /*                  */
       '| ' || '             ' || LEFT(' ',8) ,  /*                  */
       '| ' || '             ' || LEFT(' ',8) ,  /*                  */
       '|'                                       /*                  */
SAY CENTER(DATA,79)                              /*                  */
DATA = '*' || LEFT('-',71,'-') || '*'            /*                  */
SAY CENTER(DATA,79)                              /*                  */
PULL A                                           /* BREAK HERE       */
                                                 /*                  */
FMTDSCB:                                         /* FORMAT DSCB      */
CLEAR                                            /* CLEAR SCREEN     */
TITLE = 'DATASET FORMAT-1 DSCB LOOKUP'           /* FORM HEADER      */
SAY     CENTER(TITLE,79)                         /* PUT IT OUT       */
DATA = 'DSNAME:' DSNAME '   '  ||,               /*                  */
       'VOLSER:' VOLSER                          /*                  */
SAY CENTER(DATA,79)                              /*                  */
DATA = 'RECFM:' || RECFM '  DSORG:' || DSORG,    /*                  */
       '  LRECL:' || LRECL '  BLKSZ:' || BLKSZ   /*                  */
SAY CENTER(DATA,79)                              /*                  */
DATA = '*' || LEFT('-',71,'-') || '*'            /*                  */
SAY CENTER(DATA,79)                              /*                  */
DATA = '|  NAME  |OFFSET|TYPE|DATA CONTENT|',
       '       D E S C R I P T I O N        |'
SAY CENTER(DATA,79)
DATA = '|--------+------+----+------------+' ||,
       '-------------------------------------|'
SAY CENTER(DATA,79)
DATA = '|DS1FMTID| 002C |CL1 |' || LEFT(WORD(DSCB,1),12) || '|',
       'FORMAT IDENTIFIDER                  |'
SAY CENTER(DATA,79)
DATA = '|DS1DSSN | 002D |CL6 |' || LEFT(WORD(DSCB,2),12) || '|',
       'DATASET SERIAL NUMBER (THE 1ST ONE) |'
SAY CENTER(DATA,79)
DATA = '|DS1VOLSQ| 0033 |XL2 |' || LEFT(WORD(DSCB,3),12) || '|',
       'VOLUME SEQUENCE NUMBER (1-9999)     |'
SAY CENTER(DATA,79)
CREDT= RIGHT(X2D(SUBSTR(WORD(DSCB,4),1,2)),2,'0') || '.' ||,
       RIGHT(X2D(SUBSTR(WORD(DSCB,4),3,4)),3,'0')
DATA = '|DS1CREDT| 0035 |XL3 |' ||,
       LEFT(WORD(DSCB,4),12) || '|',
       'CREATION DATE (' || CREDT ||,
       ')              |'
SAY CENTER(DATA,79)
DATA = '|DS1EXPDT| 0038 |XL3 |' || LEFT(WORD(DSCB,5),12) || '|',
       'EXPIRATION DATE                     |'
SAY CENTER(DATA,79)
DATA = '|DS1NOEPV| 003B |XL1 |' || LEFT(WORD(DSCB,6),12) || '|',
       'NUMBER OF DATA EXTENTS ON VOLUME    |'
SAY CENTER(DATA,79)
DATA = '|DS1NOBDB| 003C |XL1 |' || LEFT(WORD(DSCB,7),12) || '|',
       'NO OF BYTES USED IN LAST DIR-BLOCK  |'
SAY CENTER(DATA,79)
DATA = '|        | 003D |XL1 |' || LEFT(WORD(DSCB,8),12) || '|',
       'RESERVED                            |'
SAY CENTER(DATA,79)
DATA = '|DS1SYSCD| 003E |CL13|' ||,
       LEFT(SUBSTR(WORD(DSCB,9),1,8),12) || '|',
       'SYSTEM CODE                         |'
SAY CENTER(DATA,79)
DATA = '|        |      |    |' ||,
       LEFT(SUBSTR(WORD(DSCB,9),9,8),12) || '|',
       '                                    |'
SAY CENTER(DATA,79)
DATA = '|        |      |    |' ||,
       LEFT(RIGHT(WORD(DSCB,9),10),12) || '|',
       '                                    |'
SAY CENTER(DATA,79)
REFDT= RIGHT(X2D(SUBSTR(WORD(DSCB,10),1,2)),2,'0') || '.' ||,
       RIGHT(X2D(SUBSTR(WORD(DSCB,10),3,4)),3,'0')
DATA = '|DS1REFDT| 004B |XL3 |' ||,
       LEFT(SUBSTR(WORD(DSCB,10),1,6),12) || '|',
       'DATE LAST REFERENCED (' || REFDT ||,
       ')       |'
SAY CENTER(DATA,79)
DATA = '|DS1SMSFG| 004E |XL1 |' ||,
       LEFT(SUBSTR(WORD(DSCB,10),7,2),12) || '|',
       'SYSTEM MANAGED DATASET FLAG         |'
SAY CENTER(DATA,79)
DATA = '|DS1SCEXT| 004F |XL3 |' ||,
       LEFT(SUBSTR(WORD(DSCB,10),9,6),12) || '|',
       'SECONDARY EXTENSION FLAG(1)/VALUE(2)|'
SAY CENTER(DATA,79)
DATA = '|DS1DSORG| 0052 |XL2 |' || LEFT(WORD(DSCB,11),12) || '|',
       'DATASET DSORG FLAG (ORG,TCAM/VSAM)  |'
SAY CENTER(DATA,79)
DATA = '|DS1RECFM| 0054 |XL1 |' || LEFT(WORD(DSCB,12),12) || '|',
       'DATASET RECORD FORMAT               |'
SAY CENTER(DATA,79)
DATA = '|DS1OPTCD| 0055 |XL1 |' || LEFT(WORD(DSCB,13),12) || '|',
       'DATASET OPTION CODE                 |'
SAY CENTER(DATA,79)
DATA = '|DS1BLKL | 0056 |XL2 |' || LEFT(WORD(DSCB,14),12) || '|',
       'DATASET BLOCK LENGTH                |'
SAY CENTER(DATA,79)
DATA = '|DS1LRECL| 0058 |XL2 |' || LEFT(WORD(DSCB,15),12) || '|',
       'LOGICAL RECORD LENGTH               |'
SAY CENTER(DATA,79)
DATA = '|DS1KEYL | 005A |XL1 |' || LEFT(WORD(DSCB,16),12) || '|',
       'KEY LENGTH                          |'
SAY CENTER(DATA,79)
DATA = '|DS1RKP  | 005B |XL2 |' || LEFT(WORD(DSCB,17),12) || '|',
       'RELATIVE KEY POSITION               |'
SAY CENTER(DATA,79)
DATA = '|DS1DSIND| 005D |XL1 |' || LEFT(WORD(DSCB,18),12) || '|',
       'DATASET INDICATORS                  |'
SAY CENTER(DATA,79)
DATA = '|DS1SCAL | 005E |XL4 |' || LEFT(WORD(DSCB,19),12) || '|',
       'SPACE PARAMETERS TYPE(1)/QUANTITY(3)|'
SAY CENTER(DATA,79)
DATA = '|DS1STAR | 0062 |XL3 |' || LEFT(WORD(DSCB,20),12) || '|',
       'LAST USED TRACK AND BLOCK (TTR)     |'
SAY CENTER(DATA,79)
DATA = '|DS1TRBAL| 0065 |XL3 |' || LEFT(WORD(DSCB,21),12) || '|',
       'BYTES REMAINING ON LAST TRACK USED  |'
SAY CENTER(DATA,79)
DATA = '|        | 0067 |XL2 |' || LEFT(WORD(DSCB,22),12) || '|',
       'RESERVED (DMS FLAG)                 |'
SAY CENTER(DATA,79)
J = 1
DO I = 23 TO WORDS(DSCB)-1
   OFSET= D2X(X2D(69)+(J-1)*10)
   DATA = '|DS1EXT' || LEFT(J,2) || '|',
          RIGHT(OFSET,4,'0') '|XL1 |' ||,
          LEFT(SUBSTR(WORD(DSCB,I),1,2),12) || '|',
          'EXTENT TYPE INDICATOR               |'
   SAY CENTER(DATA,79)
   DATA = '|        |      |XL1 |' ||,
          LEFT(SUBSTR(WORD(DSCB,I),3,2),12) || '|',
          'EXTENT SEQUENCE NUMBER              |'
   SAY CENTER(DATA,79)
   DATA = '|        |      |XL4 |' ||,
          LEFT(SUBSTR(WORD(DSCB,I),5,8),12) || '|',
          'LOWER LIMIT (CCHH)                  |'
   SAY CENTER(DATA,79)
   DATA = '|        |      |XL4 |' ||,
          LEFT(SUBSTR(WORD(DSCB,I),13,8),12) || '|',
          'UPPER LIMIT (CCHH)                  |'
   SAY CENTER(DATA,79)
   J = J + 1
END
OFSET= D2X(X2D(69)+(J-1)*10)
DATA = '|DS1PTRDS|'  RIGHT(OFSET,4,'0') '|XL5 |' ||,
       LEFT(WORD(DSCB,I),12) || '|',
       'CCHHR OF ANY ASSOCIATED FMT-2/3 DSCB|'
SAY CENTER(DATA,79)
DATA = '*' || LEFT('-',71,'-') || '*'
SAY CENTER(DATA,79)
RETURN(0)
