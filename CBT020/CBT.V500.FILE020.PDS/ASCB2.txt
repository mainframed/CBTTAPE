/****************************** REXX *********************************/
/*   THE REXX IS USED TO PULL OUT ALL JOB RELATED INFORMATION FROM   */
/*   ADDRESS CONTROL BLOCK (ASCB) AND OTHER RELATED CONTROL BLOCKS   */
/*   LISTED BELOW:                                                   */
/*           CVT, ASVT, ASCB, ASXB, TSB, LWA, PSCB, CSCB             */
/*********************************************************************/
ARG      TAR OPT                               /* PULL IN PARMS      */
CVT    = STORAGE(10,4)                         /* ADDR CVT           */
DCVT   = C2D(CVT)                              /* CONVERT TO DECIMAL */
ASVT   = STORAGE(C2X(D2C(DCVT+556)),4)         /* ADDR ASVT          */
DASVT  = C2D(ASVT)                             /* CONVERT TO DECIMAL */
ASCNT  = C2D(STORAGE(C2X(D2C(DASVT+516)),4))   /* ADDR ASCBCNT       */
COUNT  = 0                                     /* INIT THE SEQ-COUNT */
CLEAR                                          /* CLEAR SCREEN       */
HEADER = 'A C T I V E    J O B    L I S T'     /*INIT HEADER LINE    */
HEADER = CENTER(HEADER,80)                     /* CENTERS IT         */
SAY      HEADER                                /* PUT OUT HEADER LINE*/
HEADER = 'SEQ JOB NAME   PROC     ' ||,        /* INIT TITLE LINE 1  */
         'ASCB   ASID TYPE  ST  ATTB'          /*                    */
HEADER = CENTER(HEADER,79)                     /* CENTERS IT         */
SAY      HEADER                                /* PUT OUT TITLE LINE */
HEADER = '=== ======== ======== ==' ||,        /* INIT TITLE LINE 2  */
         '====== ==== ==== ==== ===='          /*                    */
HEADER = CENTER(HEADER,79)                     /* CENTERS IT         */
SAY      HEADER                                /* PUT OUT TITLE LINE */
DO I     = 1 TO ASCNT
   ASCB  = STORAGE(C2X(D2C(DASVT+524+I*4)),4)  /* ADDR ASCB FROM ASVT*/
   IF   ABBREV(C2X(ASCB),'80')  = 1            /* IS IT VALID?       */
        THEN ITERATE                           /* NO, LEAVE FOR NEXT */
   ELSE IF   STORAGE(C2X(ASCB),4) \= 'ASCB'    /* IS IT ALLOCATED?   */
             THEN ITERATE                      /* NO, LEAVE FOR NEXT */
   DASCB = C2D(ASCB)                           /* CONVERT TO DECIMAL */
   COUNT = COUNT + 1                           /* INCREASE COUNTER   */
   TSB   = STORAGE(C2X(D2C(DASCB+60)),4)       /* LOAD TSB ADDRESS   */
   IF    TSB \= '00000000'X  THEN              /* ANY ADDR IN THERE  */
         TYPE = TSO                            /* YES, ITS A TSO     */
   ELSE  TYPE = STC                            /* NO, ASSUME STC     */
   UID   = STORAGE(C2X(D2C(DASCB+172)),4)      /* LOAD ASCBJOBI ADDR */
   IF    C2X(UID) = 00000000 THEN              /* IS THERE AN ADDR?  */
         UID = STORAGE(C2X(D2C(DASCB+176)),4)  /* NO, MUST BE A STC  */
   ELSE  TYPE = JOB                            /* YES, MAKE TYPE JOB */
   DUID  = C2D(UID)                            /*                    */
   IF    TYPE = JOB THEN                       /* IS IT A JOB?       */
         CHKEY= STORAGE(C2X(D2C(DUID+24)),8)   /*                    */
   ELSE  IF    TYPE = STC THEN                 /* IS IT A STC?       */
               CHKEY= STORAGE(C2X(D2C(DUID+16)),8)
         ELSE  CHKEY= STORAGE(C2X(D2C(DUID+08)),8)
   CHKEY = LEFT(CHKEY,8)                       /* MAKE IT PRINTABLE  */
   UID   = STORAGE(C2X(UID),8)                 /* GET JOB/STC NAME   */
   STAT  = STORAGE(C2X(D2C(DASCB+X2D(66))),1)  /*                    */
   STAT  = C2X(STAT)                           /*                    */
   IF    LENGTH(TAR) > 0 &,                    /* ANY TARGET ENTERED */
         TAR \= '*'   THEN                     /* DO USERID CHECKING */
         DO                                    /* DO USERID CHECKING */
            LN  = LENGTH(TAR)                  /* GET PARM LENGTH    */
            IF  TAR \= SUBSTR(UID,1,LN) THEN   /* IS IT MATCH UID?   */
                ITERATE                        /* NO, GO GET NEXT ONE*/
         END                                   /*                    */
   IF    LENGTH(OPT) > 0 &,                    /* ANY OPTION ENTERED */
         OPT \= TYPE   THEN                    /* IS IT MATCH TYPE?  */
         ITERATE                               /* NO, GO GET NEXT ONE*/
   ASID  = STORAGE(C2X(D2C(DASCB+X2D(24))),2)  /* LOAD ASID IN HEX   */
   ASID  = C2X(ASID)                           /* CONVERT TO CHAR.   */
   ASXB  = STORAGE(C2X(D2C(DASCB+108)),4)      /* LOAD ASXB ADDRESS  */
   DASXB = C2D(ASXB)                           /* CONVERT TO DECIMAL */
   LTCB  = STORAGE(C2X(D2C(DASXB+8)),4)        /* LOAD LAST TCB ADDR */
   DLTCB = C2D(LTCB)                           /* CONVERT TO DECIMAL */
   CSCB  = STORAGE(C2X(D2C(DASCB+X2D(38))),4)  /* LOAD CSCB ADDRESS  */
   DCSCB = C2D(CSCB)                           /* CONVERT TO DECIMAL */
/* CHKEY = STORAGE(C2X(D2C(DCSCB+8)),8)         * GET PROC NAME      */
/* CHCLS = STORAGE(C2X(D2C(DCSCB+16)),8)        * GET STEP NAME      */
   LWA   = STORAGE(C2X(D2C(DASXB+20)),4)       /* LOAD LWA ADDRESS   */
   DLWA  = C2D(LWA)                            /* CONVERT TO DECIMAL */
   PSCB  = STORAGE(C2X(D2C(DLWA+24)),4)        /* LOAD PSCB ADDRESS  */
   DPSCB = C2D(PSCB)                           /* CONVERT TO DECIMAL */
   XFLAG = C2X(STORAGE(C2X(D2C(DPSCB+16)),1))  /* GET PROFILE ATTRIB */
   OTCNT = RIGHT(COUNT,3)                      /* FORMAT COUNT FIELD */
   LINE  = OTCNT UID CHKEY C2X(ASCB) ASID,     /* FORMAT OUTPUT LINE */
           TYPE ' ' STAT ' ' XFLAG ' '         /*                    */
   LINE  = CENTER(LINE,79)                     /* CENTERS THE LINE   */
   SAY     LINE                                /* PUT OUT TITLE LINE */
END
RETURN(0)
