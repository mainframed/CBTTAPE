/*====REXX===========================================================*/
/*                                                                   */
/*                       TAPE UCB TABLE LOOKUP                       */
/*  THIS REXX WILL SHOW TAPE DEVICE INFORMATION FROM THE UCB TABLE   */
/*  MAINTAINED BY MVS.                                               */
/*                                                                   */
/*===================================================================*/
/*               UCBTBYT WORD AT X'10' OFF INTO UCB                  */
/*  LOCATE UCB:  CVT+X'4E4'(CVTUCBA) POINTS TO THE 1ST UCB ENTRY     */
/*  UCBSTAT   -> UCB+X'03'- X'80'/ONLINE, X'08'/ALLOCATED            */
/*  UCBTBYT   -> UCB+X'10" 4 BYTESLONG                               */
/*   XX...... -> THE 1ST BYTE IS THE MODEL FLAG    (UCBTBYT1)        */
/*   ..XX.... -> THE 2ND BYTE IS THE FEATURE FLAG  (UCBTBYT2)        */
/*               X'04' DEVICE WITH IDRC (COMPACT) SUPPORT            */
/*               X'00' DEVICE WITHOUT IDRC (COMPACT) SUPPORT         */
/*   ....XX.. -> THE 3RD BYTE MUST BE 80 FOR TAPE  (UCBTBYT3)        */
/*               X'80' FOR TAPE DEVICE                               */
/*               X'40' FOR COMMUNICATION DEVICE                      */
/*               X'41' FOR CTC DEVICE                                */
/*               X'20' FOR DASD DEVICE                               */
/*               X'10' FOR DISPLAY DEVICE                            */
/*               X'08' FOR UNIT RECORD DEVICE                        */
/*               X'24' FOR CHARACTER READER DEVICE                   */
/*   ......XX -> THE 4TH BYTE IS THE DEVICE TYPE   (UCBTBYT4)        */
/*               X'80' FOR DEVICE 3480                               */
/*               X'81' FOR DEVICE 3490 OR 9348                       */
/*                                                                   */
/*********************************************************************/
CLEAR                                         /* CLEAR SCREEN        */
ALLOCX. =                                     /* INIT ALLOC STACKER  */
VOLSER. =                                     /* INIT VOLSER STACKER */
UCBJOB. =                                     /* INIT JOBNM STACKER  */
NREDYX. =                                     /* INIT FREE STACKER   */
ALC     = 0                                   /* INIT ALLOC COUNT    */
NRD     = 0                                   /* INIT FREE COUNT     */
CVT     = STORAGE(10,4)                       /* ADDR CVT            */
DCVT    = C2D(CVT)                            /* CONVERT TO DECIMAL  */
UCB     = STORAGE(C2X(D2C(DCVT+X2D(4E4))),4)  /* ADDR UCB FROM CVT   */
DUCB    = C2D(UCB)                            /* CONVERT TO DECIMAL  */
SAY     '         A  L  L  O  C  A  T  E  D          ', /* HEADER 1  */
        '    A  V  A  I  L  A  B  L  E'
SAY     'DEV STATUS TYPE ASID JOB NAME VOLSER LAB ACL', /* HEADER 2  */
        ' DEV STATUS TYPE DEV STATUS TYPE'
SAY     '=== ====== ==== ==== ======== ====== === ===', /* HEADER 3  */
        ' === ====== ==== === ====== ===='
DO   I  = 1 BY 1 UNTIL UCBNM  = 4CF           /* SCAN UCB UNTIL 4CF  */
     UCBNM   = STORAGE(C2X(D2C(DUCB+X2D(04))),2)  /* SAVE DEVNUM     */
     UCBNM   = SUBSTR(C2X(UCBNM),2,3) ||' '       /* CONVERT TO CHAR */
     UCBNXUCB= STORAGE(C2X(D2C(DUCB+X2D(08))),4)  /* MAP NEXT UCB/A  */
     UCBSTAT = STORAGE(C2X(D2C(DUCB+X2D(03))),1)  /* MAP UCB STATUS  */
     UCBTBYT1= STORAGE(C2X(D2C(DUCB+X2D(10))),1)  /* MAP UCB TYPE 1  */
     UCBTBYT2= STORAGE(C2X(D2C(DUCB+X2D(11))),1)  /* MAP UCB TYPE 2  */
     UCBTBYT3= STORAGE(C2X(D2C(DUCB+X2D(12))),1)  /* MAP UCB TYPE 3  */
     UCBTBYT4= STORAGE(C2X(D2C(DUCB+X2D(13))),1)  /* MAP UCB TYPE 4  */
     UCBDMCT = STORAGE(C2X(D2C(DUCB+X2D(23))),1)  /* MAP UCB MOUNT   */
     UCBTFL1 = STORAGE(C2X(D2C(DUCB+X2D(2B))),1)  /* MAP UCB FLAG 1  */
     UCBEXT  = STORAGE(C2X(D2C(DUCB+X2D(15))),3)  /* MAP UCB EXT     */
     DUCBEXT = X2D(C2X(UCBEXT))                   /* CONVERT TO DEC. */
     UCBASID = STORAGE(C2X(D2C(14+DUCBEXT)),2)    /* MAP UCBASID     */
     UCBASID = C2X(UCBASID)                   /* CONVERT TO CHARACTER*/
     IF   UCBSTAT \= '00'X  &,                /* IS DEVICE ONLINED   */
          UCBTBYT3 = '80'X  THEN              /* OR DEVICE = TAPE ?  */
          CALL DOTAPE                         /* YES, COLLECT INFO.  */
     UCB       = UCBNXUCB                     /* ELSE ADDR NEXT UCB  */
     DUCB      = C2D(UCB)                     /*      CONVERT TO DEC */
END                                           /* END DO HERE         */
/*********************************************************************/
/*   MAP THE JOB NAME USING THE TAPE BY GOING THROUGH THE ASVT       */
/*   AND COMPARE THE ASID WITH UCBASID SAVED                         */
/*********************************************************************/
ASVT    = STORAGE(C2X(D2C(DCVT+556)),4)       /* ADDR ASVT (CVT+556) */
DASVT   = C2D(ASVT)                           /* CONVERT TO DECIMAL  */
ASCNT   = C2D(STORAGE(C2X(D2C(DASVT+516)),4)) /* ADDR ASVT ASCBCNT   */
DO J    = 1 TO ASCNT                          /* SCAN ASVT TABLE     */
   ASCB = STORAGE(C2X(D2C(DASVT+524+J*4)),4)
   IF   ABBREV(ASCB,'00'X) \= 1 THEN ITERATE  /* IS ASCB IN USED     */
   DASCB= C2D(ASCB)                           /* YES CONVERT TO DEC. */
   ASID = STORAGE(C2X(D2C(DASCB+X2D(24))),2)  /* MAP ASCBASID        */
   ASID = C2X(ASID)                           /* CONVERT TO CHAR.    */
   JOBNM= STORAGE(C2X(D2C(DASCB+172)),4)      /* MAP ASCBJBNI (JOB)  */
   IF   C2X(JOBNM) = 00000000 THEN            /* IS IT ALL ZEROS     */
        JOBNM= STORAGE(C2X(D2C(DASCB+176)),4) /* MAP ASCBJBNS (STC)  */
   JOBNM.ASID = STORAGE(C2X(JOBNM),8)         /* SAVE IT IN STACKER  */
END                                           /* END DO ASVT HERE    */
I  = ALC
IF   FORMAT(NRD/2,,0) > I  THEN               /* FREE COUNT > ALLOC  */
     I  = FORMAT(NRD/2,,0)                    /* YES USE FREE COUNT  */
DO   J  = 1 BY 1 UNTIL J > I                  /* SCAN ALL STACKER    */
     IF   ALLOCX.J  = '' THEN                 /* CLEAR LEFT HALF     */
          ALLOCX.J  = '                                          '
     X  = SUBSTR(ALLOCX.J,17,4)               /* FORMAT SHR TAPE JOB */
     IF   SUBSTR(VOLSER.J,1,2) = 'A ' THEN    /* IS CONTROL BY MIM   */
          JOBNM = '**GTAF**'                  /* YES, PUT JOBNM MIM  */
     ELSE IF   X = '' THEN JOBNM = ''         /* IS THERE A JOBNM    */
     ELSE                  JOBNM = JOBNM.X    /* ELSE SAVE ASIS      */
     IF   JOBNM = 'JOBNM.0000' THEN           /* IS THIS A NULL NAME */
          JOBNM = 'UN-KNOWN'                  /* YES, SET TO UNKNOWN */
     LST = ALLOCX.J JOBNM VOLSER.J            /* PREPARE FOR OUTPUT  */
     K = J * 2 - 1                            /* 2 PART PER LINE     */
     IF   LENGTH(NREDYX.K) > 0 THEN           /* IS STACKER EMPTY    */
          LST = LST || ' ' NREDYX.K           /* NO, FORMAT IT       */
     K = K + 1                                /* PART 2 OF THE LINE  */
     IF   LENGTH(NREDYX.K) > 0 THEN           /* IS IT EMPTY         */
          LST = LST NREDYX.K                  /* NO, FORMAT IT       */
     SAY LST                                  /* OUT PUT TO SCREEN   */
END                                           /* END RETURN          */
RETURN(0)
/*********************************************************************/
/*   FORMAT TAPE INFORMATION FROM UCB FIELD                          */
/*********************************************************************/
DOTAPE:
     VOLSER    = '      '                     /* CLEAR VOLSER FIELD  */
     IF   BITAND(UCBSTAT,'88'X) = '88'X  THEN /* IS TAPE ALLOCATED   */
          STATUS  = ALLOC                     /* YES, SHOW ALLOC     */
     ELSE STATUS  = FREE' '                   /* OTHERWISE SHOW FREE */
     IF   UCBTBYT4 = '81'X THEN               /* IS IT A 3490E TAPE  */
          DEVTYP  = 349E                      /* YES, SHOW 349E      */
     ELSE IF   UCBTBYT4 = '80'X &,            /* NO, IS IT A 3480X   */
               UCBTBYT2 = '04'X THEN          /* WITH COMP. FEATURE  */
               DEVTYP = 3490                  /* YES, SHOW 3490      */
     ELSE IF   UCBTBYT4 = '80'X &,            /* NO, IS IT A 3480    */
               UCBTBYT2 = '00'X THEN          /* W/OUT COMP. FEATURE */
               DEVTYP = 3480                  /* YES, SHOW 3480      */
     ELSE IF   UCBTBYT4 = '03'X &,            /* NO, IS IT A 3400    */
               UCBTBYT1 = '32'X &,            /* SERIOUS AND IT IS   */
               UCBTBYT2 = '10'X THEN          /* 3420 TAPE           */
               DEVTYP = 3420                  /* YES, SHOW 3420      */
          ELSE DEVTYP = 3400                  /* OTHERWISE SHOW 3400 */
     IF   UCBDMCT = '80'X  THEN               /* IS MOUNT PENDING    */
          STATUS = MOUNT                      /* YES, SHOW MOUNT     */
     IF   STATUS = ALLOC |,                   /* IS TAPE IN USED     */
          STATUS = MOUNT   THEN               /* ALLOC OR MOUNT      */
          DO                                  /* YES, DO FORMAT LIST */
               ALC = ALC + 1
               VOL = STORAGE(C2X(D2C(DUCB+X2D(1C))),6) /* GET VOLSER */
               IF   VOL \= BITOR(VOL,'404040404040'X) THEN
                    VOL  = '      '
               IF   BITAND(UCBTFL1,'80'X) = '80'X THEN /* LABEL = NL */
                    LABEL  = NL' '
               ELSE IF    BITAND(UCBTFL1,'40'X) = '40'X THEN /*   NS */
                          LABEL  = NS' '
               ELSE       LABEL  = SL' '                     /*   SL */
               IF   BITAND(UCBTFL1,'0C'X) = '0C'X THEN       /*ACL=A */
                    ACL = ACT
               ELSE IF    BITAND(UCBTFL1,'08'X) = '08'X THEN /*ACL=Y */
                          ACL = YES
                     ELSE ACL = '   '                        /*NO ACL*/
               IF   VOL = '      ' THEN
                    LABEL = '   '
               UCBJOB.ALC  = C2X(UCBASID)
               ALLOCX.ALC  = UCBNM||STATUS ' 'DEVTYP UCBASID /*SAVE IT*/
               VOLSER.ALC  = VOL LABEL ACL
          END
     ELSE DO
               NRD         = NRD + 1
               NREDYX.NRD  = UCBNM STATUS DEVTYP  /* SAVE IT AS FREE */

          END
RETURN(0)
