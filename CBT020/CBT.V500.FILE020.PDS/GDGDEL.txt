/*==== REXX ==========================================================*/
/*                                                                    */
/*        THIS REXX WILL SCAN THE OUTPUT OF A LISTCAT DISPLAYING ALL  */
/*   THE GDGS IN A CATALOG AND DELETE THOSE WHICH HAVE NO GENERATIONS */
/*   IN THEM.  IT FIRST ASKS WHETHER OR NOT THE GDG'S SHOULD REALLY   */
/*   BE DELETED.  ANSWERING 'NO' MAKES THE REXX RUN IN TEST MODE.     */
/*                                                                    */
/*====================================================================*/

FILENAME: SAY 'ENTER INPUT FILE NAME :'
PARSE UPPER PULL FNAME
"ALLOC DA(" || FNAME || ") F(TEST) SHR REU"
IF RC ¬= 0 THEN DO
  SAY 'UNABLE TO ALLOCATE THAT FILE.  PLEASE TRY AGAIN.'
  SIGNAL FILENAME
  END
O = '0'
NREC = 0
NGDG = 0
NDEL = 0
SAY "SHOULD GDG'S REALLY BE DELETED ( YES/NO ) : "
PARSE UPPER PULL ANSWR
IF ( LEFT(ANSWR,1) = 'Y' ) THEN DELSW = ' '
ELSE DELSW = ' NOT '
SAY 'DELETES WILL REALLY' || DELSW || 'BE PERFORMED.'

DO FOREVER
  'EXECIO 1 DISKR TEST'                  /* QUEUE FILE INPUT LINE     */
  IF RC = 2 THEN LEAVE                   /* EXIT ON EOF               */
  NREC = NREC + 1                        /* COUNT ANOTHER RECORD READ */
  PARSE PULL A1 A2 A3 A4 A5 A6 A7 A8 A9 A10 A11 A12 A13 A14 A15
  IF ( A2 = 'BASE' ) & ( RIGHT(A1,3) = 'GDG' ) THEN DO
    NGDG = NGDG + 1                      /* COUNT ANOTHER GDG        */
    IF ( O ¬= '0' ) THEN DO              /* CHECK LAST ONE EXISTS    */
      IF DEL ¬= 'NO' THEN DO             /* WERE THWNW GWNWRATIONS ? */
        IF DELSW = ' ' THEN "DELETE '" || O || "' GDG"
        ELSE SAY 'GDG' O 'WAS DELETED.'
        NDEL = NDEL + 1
      END
      ELSE SAY '          GDG' O 'WAS RETAINED.'
    END
    O = A4                               /* MAKE NEW ONE OLD ONE     */
    DEL = 'YES'                          /* AND NO GENERATIONS       */
  END
  ELSE IF ( LEFT(A1,5) = 'NONVS' ) | ( LEFT(A2,5) = 'NONVS' ) THEN DO
    DEL = 'NO'                           /* NONVSAM GENS MEAN NO DEL */
  END
END
IF ( O ¬= '0' ) THEN DO                  /* CHECK LAST IN INPUT      */
  IF DEL ¬= 'NO' THEN DO
    IF DELSW = ' ' THEN "DELETE '" || O || "' GDG"
    ELSE SAY 'A GDG' O 'WAS DELETED.'
  END
  ELSE SAY 'GDG' O 'WAS RETAINED.'
END                                      /* REPORT ON RESULTS        */
SAY NREC 'RECORDS WERE READ BEFORE AN EOF WAS DETECTED.'
SAY NGDG "GDG'S WERE FOUND AND" NDEL "WERE DELETED."
