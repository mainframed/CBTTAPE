/*==== REXX ==========================================================*/
/*                                                                    */
/*        THIS ROUTINE GOES THROUGH ALL THE ASCB'S IN THE SYSTEM AND  */
/*   PICKS OUT THOSE RELATING TO TSO USERS CURRENTLY ON LINE.  IT     */
/*   DOES THIS BY LOOKING FOR A NON-ZERO TSB IN AN ACTIVE ASCB AND    */
/*   IF ONE IS FOUND, IT USES THE JBNS POINTER TO GET THE JOB NAME.   */
/*   THE SUBROUTINE SENDER MAY BE EASILY MODIFIED TO SENT THE WHAT-   */
/*   EVER MESSAGES ARE NECESSARY.                                     */
/*                                                                    */
/*====================================================================*/

CVT = 10                              /* THE CVT POINTER IS AT HEX 10 */
CVT = C2D(STORAGE(CVT,4))             /* GET THE CVT ADDRESS          */
ASVT = CVT + 556                      /* ASVT IS IN THE CVT           */
ASVT = C2D(STORAGE(D2X(ASVT),4))      /* GET THE ASVT ADDRESS         */
NUM = ASVT + 516                      /* POINT TO MAX NUMBER ASIDS    */
NUM = C2D(STORAGE(D2X(NUM),4))        /* GET THE NUMBER OF ASIDS      */
ASCBPTR = ASVT + 528                  /* POINT TO THE VECTOR TABLE    */
ACTIVE = 0                            /* START WITH 0 ACTIVE JOBS     */
NTSO = 0                              /* AND 0 ACTIVE TSO USERS       */
DO I = 1 TO NUM                       /* LOOP THROUGH ALL ASIDS       */
  ASCB = C2X(STORAGE(D2X(ASCBPTR),4)) /* GET NEXT ASCB POINTER        */
  X = LEFT(ASCB,1)                    /* HIGH BIT SAYS FREE ASCB      */
  IF X < 8 THEN DO                    /* ACTIVE ASCB                  */
    ACTIVE = ACTIVE + 1               /* COUNT ACTIVE ASCB'S          */
    TSB = X2D(ASCB) + 60              /* GET TSB OFFSET IN ASCB       */
    TSB = C2X(STORAGE(D2X(TSB),4))    /* GET TSB                      */
    IF TSB Â¬= 0 THEN DO               /* ZERO TSB MEANS NOT TSO USER  */
      ASID = X2D(ASCB) + 36           /* POINT TO ASID FOR KICKS      */
      ASID = C2X(STORAGE(D2X(ASID),2))/* GET ASID FOR LATER USE       */
      JOBID = X2D(ASCB) + 176         /* OFFSET TO JOB NAME POINTER   */
      JOBID = C2X(STORAGE(D2X(JOBID),4)) /* PICK UP JOB NAME POINTER  */
      JOBID = STORAGE(JOBID,8)        /* PICK UP JOB NAME             */
      NTSO = NTSO + 1                 /* COUNT ANOTHER ACTIVE TSO USER*/
      CALL SENDER(JOBID ASID)         /* CALL PROCESSING ROUTINE      */
      END
    END
  ASCBPTR = ASCBPTR + 4               /* POINT TO NEXT ASCB ADDRESS   */
  END

                                      /* SHOW RESULTS OF OUR LABOR    */
SAY 'THERE WERE' NUM 'ASCB SLOTS IN THE/TABLE.'
SAY 'THERE WERE' ACTIVE 'ACTIVE ASCB''S/FOUND.'
SAY 'THERE WERE' NTSO 'ACTIVE TSO USERS/FOUND.'
RETURN

/*                 E N D   O F   M A I N   R O U T I N E              */

/*====================================================================*/
/*                                                                    */
/*        THIS ROUTINE RECIEVES A CALL FOR EACH CURRENTLY ACTIVE TSO  */
/*   USER ON THE SYSTEM.  IT CAN EASILY BE MADE TO SEND THE USER ONE  */
/*   OR MORE LINES OF MESSAGE TEXT.  THE PROGRAM TO DO THIS GREW OUT  */
/*   OF A DESIRE TO SEND MULTIPLE LINE MESSAGES TO ALL USERS ON THE   */
/*   SYSTEM WITHOUT THE LONG WAIT BETWEEN LINES EXPERIENCED WHEN THE  */
/*   SEND WAS USED FOR EACH LINE TO ALL USERS.                        */
/*        IT APPEARS THAT THERE IS A SYSTEM LIMITATION OF 3 LINES TO  */
/*   BE QUEUED FOR EACH USER.  ANY MORE THAT THAT SEEMS TO BE LOST    */
/*   EVEN ON A TRANSMISSION TO A SINGLE USER.                         */
/*                                                                    */
/*====================================================================*/

SENDER: PROCEDURE                     /* THIS IS A PROCEDURE          */

PARSE UPPER ARG USERID ASID .         /* PULL SUPPLIED ARGS           */

SAY USERID 'IN ADDRESS SPACE' ASID 'IS RECEIVING THE MESSAGES NOW.'

"SEND " ||,
"'ANDY CAPALOFF FOUND A RING NEAR THE FOURTH FLOOR ELEVATORS.'"||,
     ",USER(" || USERID || "),LOGON"
"SEND " ||,
"'PLEASE CONTACT HIM AT EXTENSION 3308 IF IT IS YOURS'"||,
     ",USER(" || USERID || "),LOGON"
"SEND " ||,
"'OR, IF YOU HAD ANY VISITORS, PLEASE INFORM THEM.'"||,
     ",USER(" || USERID || "),LOGON"

RETURN

/*          E N D   O F   S E N D E R   R O U T I N E                 */
