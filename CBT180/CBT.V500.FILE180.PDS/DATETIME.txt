***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1979 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.  (Please see reason mentioned       *
*        below.)                                                      *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.  (Please see reason        *
*        mentioned below.)                                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1.3, w/DFP 3.2),   *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc!ldw                                                *
*                                                                     *
*          (213) 740-2875 (direct -- 11 am to 7 pm Pacific time)      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, 714, or 310.  Sorry.                                  *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
         TITLE 'IGCSV000  ---  SSVC 0   ---  NOWSVC:  DATE AND TIME'
**********************************************************************
*                                                                    *
*  IGCSV000                                                          *
*                                                                    *
*  PROGRAMMER:      VIC TOLOMEI                                      *
*                                                                    *
*  WRITTEN:         7/10/73                                          *
*  UPDATES: 08/17/77 - *KDR* - REMOVE DECIMAL INSTRUCTIONS           *
*           02/02/79 - JCJ1  - KLUDGE FIX FOR SECURITY HOLE (SPIE    *
*                              EXIT GETS CONTROL IF DATA EXCEPTIONS) *
*           05/08/79 - LDW1  - CHANGE TO A LINKED-TO MODULE FOR MVS  *
*                                                                    *
*  MACROS USED:     REGEQU, GETMAIN, TIME                            *
*                                                                    *
*  MODULE ATTRIBUTES:    FULLY RE-ENTRANT AND SERIALLY RE-USABLE     *
*                                                                    *
**********************************************************************
         SPACE 9
**********************************************************************
*                                                                    *
*  IGCSV000  ---  THE NOW SVC  ---  SSVC 0                           *
*                                                                    *
*  THIS LOAD OF THE SERVICE SVC  (SVC 245) WILL RETURN THE DATE AND  *
*  IN THE MANY FORMS USEFUL TO THE ASSEMBLER PROGRAMMER.  THESE      *
*  FORMS MAY BE SEEN IN THE URSA PROCESSOR 'NOW'.  THESE FORMS       *
*  COME COMPLETE WITH RELEVANT PUNCTUATION, SO THAT THEY ARE READY   *
*  TO USE IMMEDIATELY.   THE CALLER                                  *
*  MAY EITHER REQUEST THE CURRENT DATE BY ZEROING R1 BEFORE THE CALL *
*  OR MAY HAVE THE TIME AND DATE (IN R0 AND R1 RESPECTIVELY)         *
*  INTERPRETED BY THE SVC.  THE SVC IS REFERENCED BY CODING THE      *
*  FOLLOWING (ASSUMING R1 IS ZERO, OR R0 AND R1 ARE CORRECTLY        *
*  INITIALIZED):                                                     *
*                                                                    *
*        SSVC  0                        FOR CCN/MVT                  *
*        LINK  EP=DATETIME              FOR MVS                  LDW1*
*                                                                    *
*  ON RETURN, R1 WILL POINT TO A PARMLIST IN WHICH THE               *
*  SVC WILL PLACE THE FORMS OF THE DATE AND TIME.  THIS PARMLIST     *
*  WILL BE 72 BYTES LONG, AND WILL BE ALIGNED ON A DOUBLEWORD,       *
*  RESIDENT IN THE CALLER'S SUBPOOL 0.  THE FOLLOWING CHARACTER      *
*  INFORMATION IS FILLED IN BY THE SVC:   THE DATE (WITH A CHARACTER *
*  MONTH AND A NUMERIC MONTH), THE JULIAN DATE, AND THE TIME (IN     *
*  24-HOUR CLOCK AND 12-HOUR CLOCK).  THE SSVC ALSO DETERMINES       *
*  WHETHER THE TIME IS 'AM' OR 'PM', AND WHETHER WE ARE IN           *
*  PACIFIC STANDARD TIME (PST) OR PACIFIC DAYLIGHT TIME (PDT).       *
*                                                                    *
*  OFFSET   FORM                 DESCRIPTION                         *
*   0-11  HH:MM:SS ZZZ       24-HOUR TIME, WHERE ZZZ IS PST OR PDT   *
*  14-21  (M)M/(D)D/YY       THE DATE, RIGHT JUSTIFIED               *
*  24-32  WWWWWWWWW          THE DAY OF THE WEEK, LEFT JUSTIFIED     *
*  34-46  MMM(M) (D)D, 19YY  THE DATE, WHERE MMM(M) IS THE STANDARD  *
*                            ABBREVIATIONS FOR MONTHS (JAN/FEB/      *
*                            MAR/APR/MAY/JUNE/JULY/AUG/SEPT/OCT/     *
*                            NOV/DEC) LEFT JUSTIFIED                 *
*  49-63  HH:MM:SS TT ZZZ    12-HOUR TIME, WHERE TT IS AM OR PM,     *
*                            AND ZZZ IS PST OR PDT                   *
*  66-71  YY-DDD             THE JULIAN DATE                         *
*                                                                    *
*                                                                    *
*  RETURN CODES (R15):   0 ..... SUCCESSFUL COMPLETION               *
*                        4 ..... NO CORE AVAILABLE FOR PARMLIST      *
*                                                                    *
*                                                                    *
*  FOR EXAMPLE, IF SSVC 0  WAS CALLED ON SEPT. 26, 1973 AT 3:16:17PM *
*  THE PARAMETER LIST RETURNED WOULD LOOK LIKE:                      *
*                                                                    *
*15:16:17 PDT   9/26/73  WEDNESDAY SEPT 26, 1973  03:16:17 PM PDT  73*
*.269                                                                *
*                                   OR                               *
*                                                                    *
*00:35:17 PST    1/1/73  MONDAY    JAN 1, 1973    12:35:17 AM PST  73*
*.001                                                                *
*                                                                    *
*  NOTE:  FOR CURRENT TIME, R1 MAY EITHER BE ZEROED BY THE USER      *
*         BEFORE THE CALL, OR THE MACRO MAY DO IT, AS FOLLOWS:       *
*                                                                    *
*               SSVC 0,R1=0             FOR CCN/MVT                  *
*                                                                    *
*               SR   R1,R1              FOR MVS                  LDW1*
*               LINK EP=DATETIME        FOR MVS                  LDW1*
*                                                                    *
*  NOTE:  OFFSET 0-21 GIVES A SHORT FORM OF THE TIME AND DATE FOR    *
*         USE IN TSO COMMANDS WHERE IT SHOULD BE SHORT.  EG:         *
*              0-21...15:16:17 PDT   9/26/73                         *
*              0-26...15:16:17 PDT   9/26/73  WED                    *
*             0-32...15:16:17 PDT   9/26/73  WEDNESDAY               *
*         THE 40-CHARACTER STRING AT 24063 GIVES DATE AND TIME IN A  *
*         'NICE' FORM - APPROPRIATE FOR URSA LIKE 'NOW' ETC.         *
*                                                                    *
**********************************************************************
         EJECT
         REGEQU
         SPACE 8
**********************************************************************
*                                                                    *
*  REGISTER USAGE:  R0 ..... TIME RETURN FROM SVC 11 OR FROM CALLER  *
*                   R1 ..... DATE RET FROM SVC 11, PARM RET PTR      *
*                   R2 ..... WORKAREA PTR AND BASE FROM SSVC         *
*                   R3 ..... BASE FOR CALER PARMLIST                 *
*                   R4 ..... R0 SAVE AND WORK REG                    *
*                   R5 ..... R1 SAVE AND WORK REG                    *
*                   R6 ..... *** FREE ***                            *
*                   R7 ..... *** FREE ***                            *
*                   R8 ..... *** FREE ***                            *
*                   R9 ..... *** FREE ***                            *
*                   R10 .... *** FREE ***                            *
*                   R11 .... *** FREE ***                            *
*                   R12 .... CSECT BASE                              *
*                   R13 .... *** FREE ***                            *
*                   R14 .... WORK REGISTER                           *
*                   R15 .... ENTRY POINT, RETURN CODE, AND WORK REG  *
*                                                                    *
**********************************************************************
         TITLE 'IGCSV000  ---  SSVC 0   ---  NOWSVC:  MAIN ENTRY'
IGCSV000 CSECT
         STM   R14,R12,12(R13)          SAVE CALLER'S REGS         LDW1
         LR    R12,R15                  GET ADDRESSABILITY
         USING IGCSV000,R12
*LDW1    USING NOWWORK,R2
*LDW1    LM    R4,R5,32(R5)             GET R0 & R1 FROM SVRB (SSVC R5)
         LR    R4,R0                    GET R0                     LDW1
         LR    R5,R1                    GET R1                     LDW1
*
*  GET THE CORE FOR THE PARMLIST IN THE CALLER'S SUBPOOL 0 (SP=250)
*
*JCJ1    XC    GETLIST(10),GETLIST      ZERO OUT GETMAIN LIST
*LDW1    GETMAIN EC,LV=PARMLEN,A=PARMADDR,MF=(E,GETLIST),SP=250
         GETMAIN RU,LV=PARMLEN                                     LDW1
*LDW1    LTR   R15,R15                  DID IT GET THE CORE?
*LDW1    BZ    *+6                      YES - GO ON
*LDW1    SVC   3                        NO - RET WITH RC=4, IGNORE CALL
         SPACE 2
*  ----  SET US AS A PEON. NOTE THAT R15 = 0.                      JCJ1
         SPACE 1
*LDW1    SVC   254                      ENABLED,PROBLEM STATE/KEY  JCJ1
         SPACE 2
*LDW1    L     R3,PARMADDR              SAVE THE PTR FOR ADDRBLTY & RET
         LR    R3,R1                    SAVE THE PTR ...           LDW1
         USING PARMLIST,R3
         MVI   PARMLIST,C' '            INITIALIZE THE PARMLIST...
         MVC   PARMLIST+1(PARMLEN-1),PARMLIST      TO BLANKS
         LR    R0,R4                    RESTORE R0 (TO FOOL TIME DEC)
         LR    R1,R5                    RESTORE R1 (TO FOOL TIME DEC)
         SPACE 2
*LDW1    DROP  R2                       DONE WITH EXT RB WKAREA    JCJ1
         EJECT
**********************************************************************
*                                                                    *
*  GET THE CURRENT TIME (OR USER-SPECIFIED TIME)                     *
*                                                                    *
**********************************************************************
         SPACE 2
*
*  24-HOUR TIME
*
         SPACE 1
         LTR   R1,R1                    DOES HE WANT THE CURRENT TIME?
         BNZ   NOCURR                   NO - HE'S SUPPLYING IT
         TIME  DEC                      GET THE DATE AND THE TIME
NOCURR   ST    R0,WORD                  SAVE THE TIME
         UNPK  DBLW(8),WORD(4)          GET THE TIME IN CHAR
         MVC   TIME24HR(2),DBLW+1       MOVE 24-HR HOUR TO PARMLIST
         MVI   TIME24HR+2,C':'          MOVE IN PUNCTUATION
         MVC   TIME24MN(2),DBLW+1+2     MOVE MINUTES TO PARMLIST
         MVI   TIME24MN+2,C':'          MOVE IN PUNCTUATION
         MVC   TIME24SC(2),DBLW+1+4     MOVE SECONDS TO PARMLIST
         SPACE 2
*
*  12-HOUR TIME
*
         SPACE 1
         SRL   R0,20                    JUST GET THE HOURS (+ SIGN NIB)
         XC    DBLW(8),DBLW             CLEAR OUT WORK AREA
         ST    R0,DBLW+4                SAVE THE HOURS AWAY
         OI    DBLW+7,X'0F'             MOVE IN SIGN
         CVB   R0,DBLW                  GET BINARY HOURS BACK IN REG
         C     R0,=F'12'                ARE WE AM?
         BL    AM                       YES - GO ON
         BE    NOON                     NO - IT'S IN BETWEEN (NOON)
         S     R0,=F'12'                NO - IT'S PM, SO CONVERT TO AM
NOON     MVC   AMPM(2),=C'PM'           SAY IT IS PM
         B     GETCHAR                  AND WORK ON THE HOUR
AM       MVC   AMPM(2),=C'AM'           SAY IT IS AM
         LTR   R0,R0                    ARE WE MIDNIGHT THOUGH (00:00)?
         BNZ   GETCHAR                  NO - NOTHING TO WORRY ABOUT
         LA    R0,12                    YES - IT'S 12 O'CLOCK AM, NOT 0
GETCHAR  CVD   R0,DBLW                  GET THE 12-HOUR TIME IN DEC
         UNPK  WORD(3),DBLW+6(2)        GET IT IN CHAR
         OI    WORD+2,X'F0'             GET THE FLIP BYTE
         MVC   TIME12HR(2),WORD+1       MOVE THE 12-HOUR HR TO PARMLIST
         MVC   TIME12HR+2(6),TIME24HR+2 MOVE IN :MM:SS FROM 24-HR
         EJECT
**********************************************************************
*                                                                    *
*  GET THE CURRENT DATE (OR THE USER-SPECIFIED DATE)                 *
*                                                                    *
**********************************************************************
         SPACE 2
*
*  THE YEAR (REGULAR AND JULIAN)
*
         SPACE 1
         ST    R1,WORD                  GET THE DATE
         MVO   DBLW(8),WORD+1(1)        PICK OFF THE YEAR
         OI    DBLW+7,X'0F'             FLIP BYTE
         UNPK  YEAR(2),DBLW+6(2)        MOVE IT TO PARMLIST
         MVC   JULYEAR(2),YEAR          MOVE IT FOR JULIAN DATE TOO
         CVB   R1,DBLW                  GET YEAR IN BINARY
         STH   R1,WORKYEAR              SAVE THE YEAR FOR WEEKDAY RTN
*
*  DETERMINE NOW, WITH THE JULIAN DATE HANDY, WHETHER WE ARE IN
*  PACIFIC STANDARD OR PACIFIC DAYLIGHT TIME (PST OR PDT).
*  THIS IS DONE AS FOLLOWS:  DAYLIGHT SAVINGS (PDT) RUNS FROM 2AM
*  ON THE LAST SUNDAY OF APRIL TO 2AM ON THE LAST SUNDAY OF OCTOBER.
*  STANDARD TIME (PST) RUNS FOR THE OTHER 6 MONTHS.  A TABLE WAS MADE
*  ('PSTPDT') OF THE PAIR OF JULIAN DATES CORRESPONDING TO THE
*  THOSE SUNDAYS FOR EACH YEAR FROM 1973 (YEAR OF THE SOURCE) TO
*  1982 (10 YEARS FROM NOW, WHEN IT MIGHT NOT EVEN MATTER ANY MORE.)
*  THE JULIAN DATE TO BE INTERPRETED BY THE SSVC IS LOCATED WITHIN
*  WITHIN THAT TABLE BY YEAR AND BY DAY, AND THUS EITHER 'PST' OR
*  'PDT' IS MOVED TO THE RETURN PARMLIST.
*  AT THIS POINT, R1 CONTAINS THE BINARY YEAR (TO BE USED FOR AN
*  OFFSET INTO THE TABLE, AND 'WORD' CONTAINS THE FULL JULIAN DATE
*  (THE LOW ORDER HALFWORD OF THE DAY WILL BE USED ONLY.  NOTE THAT THE
*  TABLE CONTAINS THE JULIAN DAYS IN PACKED DECIMAL, SO ALL COMPARISONS
*  WILL BE DONE IN PACKED DECIMAL.
*  NOTE:  DAYLIGHT SAVINGS IS IN EFFECT BETWEEN THE LAST APRIL SUNDAY
*         AND THE LAST OCTOBER SUNDAY (APR INCLUSIVE, OCT EXCLUSIVE) AS
*         FOLLOWS:
*              APR SUNDAY <= DAY < OCT SUNDAY.
*         SO STANDARD TIME IS IN EFFECT IN THE FOLLOWING PERIODS:
*              JAN 1 <= DAY < APR SUNDAY AND OCT SUNDAY <= DAY < DEC 31
*         THE CROSSOVERS IN THIS ROUTINE WILL NOT OCCUR AT 2AM,
*         BUT AT 12AM OF THOSE TWO SUNDAYS (FOR EASE IN PROGRAMMING).
*  FOR YEARS BEYOND 1982 (BEYOND THE TABLE), THE FIELDS FOR PST/PDT
*  ARE LEFT BLANK.
*
         CH    R1,=H'82'                IS THE YEAR BEYOND THE TABLE?
         BH    DONEPSDT                 YES - FORGET THIS, LEAVE BLANK
*
         LR    R15,R1                   SAVE R1 FOR LATER WORK
         SH    R15,=H'73'               GET OFFSET INTO PSTPDT TABLE
         SLL   R15,2                    *4 FOR FULLWORDS
         LA    R15,PSTPDT(R15)          PT TO JULIAN DAY PAIR FOR YEAR
         MVC   TIME24TZ(3),=C'PST'      ASSUME STANDARD TIME
         MVC   TIME12TZ(3),=C'PST'      ASSUME STANDARD TIME FOR NOW
         NI    WORD+3,X'FC'             MAKE SURE CLC'S ARE VALID *KDR*
         CLC   WORD+2(2),0(R15)         CURR DAY < LAST APR SUN?  *KDR*
         BL    DONEPSDT                 YES - THEN IT HAS TO BE PST
         CLC   WORD+2(2),2(R15)         CURR DAY < LAST OCT SUN?  *KDR*
         BNL   DONEPSDT                 NO - IT'S PST, SO DEFAULT IS OK
*
         MVC   TIME24TZ(3),=C'PDT'      APR SUNDAY  < DAY < OCT SUNDAY
         MVC   TIME12TZ(3),=C'PDT'      SO IT MUST BE PDT
*
*  GET THE MONTH, JULIAN DAY, AND DAY
*
         SPACE 1
DONEPSDT MVC   MOTAB(26),DAYSINMO       GET THE DAYS IN EACH MO
         N     R1,=F'3'                 TEST FOR LEAP YEAR
         BNZ   *+10                     NO - LEAVE IT ALONE
         MVC   MOTAB+2(2),=H'29'        YES - 29 DAYS IN FEBRUARY
*
         UNPK  JULDAY(3),WORD+2(2)      GET JULIAN DAY WHILE AT IT
         OI    JULDAY+2,C'0'        HOW COULD HE BE THAT STUPID?   LDW1
         MVI   JULYEAR+2,C'.'           MOVE IN DOT
*
         XR    R1,R1                    MONTH COUNTER
         XR    R15,R15                  INDEX REGISTER
         XC    DBLW(8),DBLW             CLEAR WORK AREA
         MVC   DBLW+6(2),WORD+2         GET THE DAY OF THE YEAR
         CVB   R14,DBLW                 IN BINARY
MOLOOP   SH    R14,MOTAB(R15)           SUBTRACT A MONTH WORTH OF DAYS
         BNP   GOTMO                    WE HAVE THE MONTH
         LA    R1,1(R1)                 NOT YET, UPDATE MONTH COUNT
         LA    R15,2(R15)               UP THE TABLE PTR
         B     MOLOOP                   KEEP IT UP
*
GOTMO    AH    R14,MOTAB(R15)           CORRECT FOR 1 TOO MANY SUBTRACT
         STH   R14,WORKDAY              SAVE THE DAY FOR WEEKDAY RTN
         LR    R15,R1                   PROTECT THE MONTH
         LA    R15,1(R15)               MONTH=MONTH+1 (WEEKDAY ROUTINE)
         STH   R15,WORKMO               SAVE THE MONTH FOR WEEKDAY RTN
*
*  MAKE THE DATE MM/DD/YY BY RIGHT JUSTIFYING AND SQUISHING OUT THE
*  LEADING INSIGNIFICANT ZEROES.
*
         MVI   DAY+2,C'/'               MOVE IN SLASH FOR /YY
         CVD   R14,DBLW                 GET THE DAY
         OI    DBLW+7,X'0F'             MAKE IT PACKED DEC
         UNPK  DAY(2),DBLW+6(2)         MOVE DAY TO PARMLIST
         LA    R4,MO+1                  ASSUME THERE IS A LEADING 0
         LA    R5,DAY                   HERE ALSO (SET PTRS UP)
         CLI   DAY,C'0'                 IS THERE ONE?
         BE    MVISLASH                 YES - SO CLOBBER IT WITH '/'
         BCTR  R4,0                     NO - MOVE ALL PTRS 1 COL LEFT
         BCTR  R5,0                     THIS ONE TOO
MVISLASH MVI   0(R5),C'/'               MOVE '/' FOR /DD (OR /D)
         CVD   R15,DBLW                 GET THE MONTH IN DECIMAL
         OI    DBLW+7,X'0F'             FLIP BYTE
         UNPK  0(2,R4),DBLW+6(2)        MOVE IT TO THE PARMLIST
         CLI   0(R4),C'0'               DOES MONTH HAVE A LEADING 0?
         BNE   OKMO                     NO - LEAVE IT ALONE
         MVI   0(R4),C' '               YES - KILL IT, BUT NO SQUASH
OKMO     SLL   R1,2                     INDEX FACTOR INTO MO TAB (4)
         LA    R1,MONTHS(R1)            PICK IT UP
         MVC   CHARDATE(4),0(R1)        MOVE THE MONTH TO THE PARMLIST
*
*  SQUASH THE REST OF THE FIELDS OVER AGAINST A POSSIBLY SHORT MONTH
*  (MONTH MAY BE EITHER MMMM OR MMM)
*
         LA    R5,CHARDATE+3            ASSUME IT IS MMMM
BLANKLP  CLI   CHARDATE+3,C' '          ARE WE RIGHT?
         BNE   GOTNON                   YES - WE HAVE IT, GET OUT
         BCTR  R5,0                     NO - POINT TO THE NON-BLANK
         SPACE 1
*  CLEAN UP DAY IF IT HAS AN INSIGNIFICANT 0 AND NEEDS TO BE EDITED.
*  DO THIS EDITING BY CLEARING OUT THAT 0 AND SQUASHING THE REST OF
*  THE DATE OVER SO THAT IT LOOKS GOOD.
         SPACE 1
GOTNON   MVC   WORD(2),DAY              SAVE THE DAY FOR THE WORK
         CLI   WORD,C'/'                DID DAY HAVE INSIG 0 (SQUASHED)
         BNE   NOCHEAT                  NO - LEAVE IT ALONE
         MVI   WORD,C' '                YES - WE MUST EDIT NASTILY
         BCTR  R5,0                     SQUASH THE STRING UP ONE BYTE
NOCHEAT  MVC   0+2(2,R5),WORD           MOVE IN DAY, W/ INTERVEN BLANK
         MVC   2+2(4,R5),=C', 19'       MOVE IN MASK
         MVC   6+2(2,R5),YEAR           MOVE IN YEAR AFTER
         SPACE 3
**********************************************************************
*                                                                    *
*  GET THE DAY OF THE WEEK                                           *
*                                                                    *
*  ALGORITHM        MONTH=MOD(MONTH,12)+1                            *
*                   IF(MONTH=11 | MONTH=12) YEAR=YEAR-1              *
*                   A=(13*MONTH-1)/5                                 *
*                   B=YEAR/4                                         *
*                   C=CENTURY/4                                      *
*                   D=A+B+C+DAY+YEAR-2*CENTURY                       *
*                   Z=MOD(D,7)+1                                     *
*                                                                    *
*  WHERE Z = DAY OF THE WEEK, 1=SUNDAY, ..., 7=SATURDAY              *
*                                                                    *
**********************************************************************
         SPACE 2
*  MONTH=MOD(MONTH+9,12)+1
         SPACE 1
         XR    R4,R4                    CLEAR REM REG
         LH    R5,WORKMO                GET MONTH
         LA    R5,9(R5)                 MONTH+9
         D     R4,=F'12'                /12
         LA    R4,1(R4)                 R4=MONTH
         STH   R4,WORKMO                SAVE IT
         SPACE 1
*  IF(MONTH=11 | MONTH=12) YEAR=YEAR-1
         SPACE 1
         CH    R4,=H'11'                11?
         BE    SUBYEAR                  YES - SUBTRACT
         C     R4,=F'12'                12?
         BNE   GETOUT                   NO - LEAVE THIS
SUBYEAR  LH    R5,WORKYEAR              GET THE YEAR
         BCTR  R5,0                     -1
         STH   R5,WORKYEAR              SAVE IT AWAY
         SPACE 1
*  A=(13*MONTH-1)/5
         SPACE 1
GETOUT   XR    R4,R4                    CLEAR REM REG
         LH    R5,WORKMO                MONTH
         MH    R5,=H'13'                *13
         BCTR  R5,0                     -1
         D     R4,=F'5'                 ALL OVER 5 (R5=A)
         SPACE 1
*  B=YEAR/4
         SPACE 1
         LH    R4,WORKYEAR              GET YEAR
         SRL   R4,2                     YEAR/4
         AR    R5,R4                    D=A+B SO FAR
         SPACE 1
*  C=CENTURY/4
         SPACE 1
         LH    R4,=H'19'                GET THE CENTURY
         SLL   R4,1                     GET CENTURY*2 FOR LATER
         SR    R5,R4                    D=A+B-CENT*2 SO FAR
         SRL   R4,3                     CENTURY/4
         AR    R5,R4                    D=A+B+C-2*CENT NOW
         SPACE 1
*  D=A+B+C+DAY+YEAR-2*CENTURY
         SPACE 1
         AH    R5,WORKDAY               ADD DAY
         AH    R5,WORKYEAR              ADD YEAR
         SPACE 1
*  Z=MOD(D,7)+1
         SPACE 1
         XR    R4,R4                    CLEAR REM REG
         D     R4,=F'7'                 GET MOD(D,7) IN R4 = Z
*  NOTE:  Z-1 FOR OFFSET INTO TABLE
         MH    R4,=H'9'                 GET OFFSET INTO TABLE
         LA    R4,DAYS(R4)              POINT TO THE DAY IN TABLE
         MVC   WEEKDAY(9),0(R4)         MOVE DAY TO PARMLIST
         SPACE 2
*  RETURN TO THE CALLER WITH R1 AND R15
         SPACE 1
         LR    R1,R3                    GET ADDR OF SVC WORKAREA   JCJ1
         LA    R0,SVC#END-PARMLIST      LENGTH OF AREA TO FREE     JCJ1
         FREEMAIN R,LV=(0),A=(1)        FREE WORKAREA              JCJ1
         LA    R1,SVC#END               POINT TO RETURN AREA       JCJ1
         SPACE 1
*LDW1    XR    R15,R15                  RC=0 FOR SUCCESSFUL OPERATION
*LDW1    SVC   3                        ALL DONE - RETURN TO CALLER
         ST    R1,24(,R13)              SET R1 TO RETURN           LDW1
         LM    R14,R12,12(R13)          RESTORE CALLER'S REGS      LDW1
         XR    R15,R15                  RC=0 FOR SUCCESSFUL OPER.  LDW1
         BR    R14                      RETURN TO CALLER           LDW1
         TITLE 'IGCSV000  ---  SSVC 0   ---  NOWSVC:  CONSTANTS'
DAYSINMO DC    H'31',H'28',H'31',H'30'
         DC    H'31',H'30',H'31',H'31'
         DC    H'30',H'31',H'30',H'31',H'9999'
         SPACE 3
DAYS     DC    CL9'SUNDAY   '
         DC    CL9'MONDAY   '
         DC    CL9'TUESDAY  '
         DC    CL9'WEDNESDAY'
         DC    CL9'THURSDAY '
         DC    CL9'FRIDAY   '
         DC    CL9'SATURDAY '
         SPACE 3
MONTHS   DC    CL4'JAN '
         DC    CL4'FEB '
         DC    CL4'MAR '
         DC    CL4'APR '
         DC    CL4'MAY '
         DC    CL4'JUNE'
         DC    CL4'JULY'
         DC    CL4'AUG '
         DC    CL4'SEPT'
         DC    CL4'OCT '
         DC    CL4'NOV '
         DC    CL9'DEC '
         SPACE 3
*  TABLE OF PST/PDT CROSSOVER JULIAN DAYS FROM 1973 TO 1982.
*  (LAST SUNDAY IN APRIL AND LAST SUNDAY IN OCTOBER FOR EACH YEAR)
         SPACE 1
PSTPDT   DS    0F
         DC    PL2'119,301'             APR 29, OCT 28    1973
         DC    PL2'118,300'             APR 28, OCT 27    1974
         DC    PL2'117,299'             APR 27, OCT 26    1975
         DC    PL2'116,305'             APR 25, OCT 31    1976  (LEAP)
         DC    PL2'114,303'             APR 24, OCT 30    1977
         DC    PL2'120,302'             APR 30, OCT 29    1978
         DC    PL2'119,301'             APR 29, OCT 28    1979
         DC    PL2'118,300'             APR 27, OCT 26    1980  (LEAP)
         DC    PL2'116,298'             APR 26, OCT 25    1981
         DC    PL2'115,304'             APR 25, OCT 31    1982
         SPACE 3
*  LITERALS
         SPACE 1
         LTORG
         TITLE 'IGCSV000  ---  SSVC 0   ---  NOWSVC:  DUMMY SECTIONS'
NOWWORK  DSECT                          WORKAREA AREA SPACE FROM SSVC
         SPACE 1
PARMADDR DS    A                        ADDRESS FROM GETMAIN       JCJ1
*GETLIST GETMAIN EC,A=PARMADDR,LV=PARMLEN,SP=250,MF=L     LDW1     JCJ1
         TITLE 'IGCSV000  ---  SSVC 0   ---  NOWSVC:  PARMLIST DSECT'
PARMLIST DSECT                          PARMLIST DSECT MASK
         SPACE 2
*  ----  SVC WORK VARIABLES MOVED TO PROBLEM KEY CORE.             JCJ1
         SPACE 2
DBLW     DS    D                        SCRATCH DBLW               JCJ1
WORD     DS    F                        SCRATCH WORD               JCJ1
WORKYEAR DS    H                        SAVE THE YEAR              JCJ1
WORKDAY  DS    H                        SAVE THE DAY               JCJ1
WORKMO   DS    H                        SAVE THE MONTH             JCJ1
MOTAB    DS    13H                      DAYS IN EACH MONTH         JCJ1
SVC#END  DS    0D                       ALIGN/FLAG END OF WORKAREA JCJ1
         SPACE 2
*  ----  END OF SVC WORKAREA VARIABLES.                            JCJ1
         SPACE 2
TIME24   DS    0CL12                    TIME IN 24-HR.....HH:MM:SS PST
TIME24HR DS    2C,C                     HH:
TIME24MN DS    2C,C                     MM:
TIME24SC DS    2C                       SS
         DS    C                        BLANK
TIME24TZ DS    3C                       PST OR PDT
         DS    2C                       BLANKS
         SPACE 1
NUMDATE  DS    0CL8                     THE DATE...(M)M/(D)D/YY
MO       DS    2C,C                     (M)M/
DAY      DS    2C,C                     (D)D/
YEAR     DS    2C                       YY
         DS    2C                       BLANKS
         SPACE 1
WEEKDAY  DS    9C                       THE WEEKDAY
         DS    C                        BLANK
         SPACE 1
CHARDATE DS    13C                      THE DATE:  MMM(M) (D)D, 19YY
         DS    2C                       BLANKS
         SPACE 1
TIME12   DS    0CL15                    TIME IN 12-HR:  HH:MM:SS AM PST
TIME12HR DS    2C,C                     HH:
TIME12MN DS    2C,C                     MM:
TIME12SC DS    2C,C                     SS + BLANK
AMPM     DS    2C                       AM OR PM
         DS    C                        BLANK
TIME12TZ DS    3C                       PDT OR PST
         DS    2C                       BLANKS
         SPACE 1
JULDATE  DS    0CL6                     THE DATE:  YY.DDD
JULYEAR  DS    2C,C                     YY.
JULDAY   DS    3C                       DDD
         SPACE 2
         DS    0D                       ALIGN FOR BOUNDARY         JCJ1
PARMLEN  EQU   *-PARMLIST               LENGTH OF THE PARMLIST
         END
