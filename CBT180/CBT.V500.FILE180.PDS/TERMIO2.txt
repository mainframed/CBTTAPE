***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1987 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.  (Please see reason mentioned       *
*        below.)                                                      *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.  (Please see reason        *
*        mentioned below.)                                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1.3, w/DFP 3.2),   *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc!ldw                                                *
*                                                                     *
*          (213) 740-2875 (direct -- 11 am to 7 pm Pacific time)      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, 714, or 310.  Sorry.                                  *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
TERMIO2  TITLE 'TERMIO2 -- 3270 TO USER INTERFACE ROUTINE UNDER TSO'
*---------------------------------------------------------------------*
*                                                                     *
* UPDATES:                                                            *
*    23MAR87  U013 EMS  YET ANOTHER VERSION OF U009 FOR VTAM V3/3272  *
*    11/12/81 U012 SDM  ADD 3270 ATTR AT END OF UPDATE TIME FIELD     *
*    10/22/81 U011 LDW  INCREASE SIZE OF 'BUFFER' FROM 2100 TO 2700   *
*                          (FOR PROCESSORS WITH MANY FIELDS)          *
*    09/08/81 U010 SDM  CHANGE TO USE 3270 ERASE WRITE FOR 3278-5'S   *
*    06/15/81 U009 LDW  TEMP PATCH FOR STUPID 3277'S OR VTAM???       *
*    04/20/81 U008 SDM  CHANGE 'ERASE INPUT' FIELD TO ZERO LENGTH     *
*                       ELIMINATE 'PFK DATA ONLY' LOGIC (SO 'MM'      *
*                          FIX IN U005 CAN WORK)                      *
*    02/26/81 U007 SDM  ALLOW NULLS TO SURVIVE UPPERCASE              *
*                          TRANSLATION                                *
*    02/24/81 U006 SDM  ALLOW CALLER TO TRANSMIT NULLS                *
*                       IMPROVE ATTN VS. STFSMODE LOGIC               *
*    07/24/80 U005 LDW  CHANGE SO TRANSFER ONLY WORKS IF USED WITH    *
*                          PF3 (THIS ALLOWS A PROCESSOR TO RECEIVE    *
*                          AN INPUT LINE STARTING WITH '='.)          *
*    06/25/80 U004 LDW  CHANGE CSECT NAMES:   TERMIO#  -> TERMIO1     *
*                                             TERMIO$  -> TERMIO2     *
*                                             TERMIO@  -> TERMIO3     *
*                                             TERMIOWT -> TERMIO4     *
*                                             TERMIOHC -> TERMIO5     *
*    02/11/80 U003 LDW  SUPPORT IOCBCATR FLAG                         *
*    10/26/79 U002 SDM  FIX THIS STUPID THING FOR NO KEYBOARD         *
*                          RESET!!!                                   *
*    10/02/79 U001 LDW  INSTALL STFSMODE MACRO CALL                   *
*                       TRANSLATE PF 13-24 TO PF 1-12                 *
*                       CHANGE TRANSFER CHAR TO '='                   *
*                                                                     *
*---------------------------------------------------------------------*
TERMIO2  CSECT
         SPACE 3
* * * * *   U S E R   A B E N D   C O D E S   * * * * *
*
*  101 - STAX MACRO FAILED
*  102 - ATTEMPT TO ISSUE TWO READS IN A ROW
*  103 - UNKNOWN RETURN CODE FROM TGET
*  104 - OVERLAPPING FIELDS IN FORMAT TABLE
*  105 - GENERATED DATA EXCEEDS BUFFER SIZE
*  106 - INVALID CURSOR POSITION
*  107 - UNKNOWN RETURN CODE FROM TPUT
*  108 - INVALID INPUT FORMAT
*  109 - INVALID AID IN INPUT BUFFER
*  110 - ATTEMPT TO READ W/ UNFORMATTED SCREEN
*
         SPACE 3
*        CONTROL RECEIVED FROM   T E R M I O 1  -
*        AN OPEN-ENDED CSECT WHOSE ONLY FUNCTION IS TO
*        DETECT WHICH SERVICE I/O ROUTINE TO USE -
*        BY DETERMINING WHETHER OR NOT THIS JOBSTEP
*        IS   T S O    OR   D C M S  "START"
         EJECT
         DCMSREGS
         EJECT
         LR    R11,R15
         USING TERMIO2,R11
         LR    R10,R1                       SAVE IOCB ADDRESS
         USING IOCB,R10
         OI    IOCBFLGT,IOCBTTSO            TERMIO FOR TSO RESPONDS
         MVI   IOCBAID,X'00'                SET OFF INPUT INDICATION
         CLI   IOCBTERM,X'7F'
         BE    CKDSPLY
         GTSIZE
         STM   0,1,IOCBTERM
         MVI   IOCBTERM,X'7F'
CKDSPLY  EQU   *
         CLC   IOCBTERM+2(2),=XL2'0000'                            U001
         BNE   TSDISPLY
         LR    R1,R10
         L     R15,=V(TERMIO5)
         BR    R15
*
*        HARDCOPY ALTERNATE FOR TSO INVOKED HERE
*
TSDISPLY EQU   *
         CLI   IOCBTERM+1,X'7F'         DONE STFSMODE?             U001
         BE    OKSTFSMD                                            U001
         STFSMODE  ON,INITIAL=YES                                  U001
         MVI   IOCBTERM+1,X'7F'         SAY DONE                   U001
         SPACE 2                                                   U001
OKSTFSMD EQU   *
         OI    IOCBFLGT,IOCBTDSP .      FLAG AS DISPLAY DEVICE
         SPACE 3
         BAL   R14,GETFSA         GET FIRST SAVE AREA IN REG 2
*
*   AT THIS POINT WE HAVE LOCATED THE FIRST SAVE AREA PROVIDED TO THIS
*   TASK WHEN IT WAS ATTACHED. NOBODY CARES ABOUT THE REGISTERS THAT
*   IT CONTAINS, SO TERMIO USES IT AS THE ANCHOR FOR ITS TABLE.
*   USAGE IS AS FOLLOWS:
*
*        24    ORIGINAL PARM SAVED BY INITIAL ROUTINE (DCB PTR)
*        28    'TRIO' IF TERMIO HAS EVER BEEN INVOKED
*        32    POINTER TO PREVIOUS FORMAT TABLE USED.
*        36    POINTER TO CURRENT TERMIO WORK AREA
*        40    POINTER TO MASTER MONITOR (MM) CMD AREA, IF ANY
*        44    CHARACTER 'MM' IF MASTER MONITOR IN CONTROL.
*
         CLC   28(4,R2),=C'#TSO'  IS THIS OUR FIRST TIME THRU
         BE    CONTINUE           NO, PICK UP WORK AREA ADDRESS
         MVC   28(4,R2),=C'#TSO'            MARK FSA AS INITIALISED
         B     NEWWORK            GO GET A WORK AREA
         SPACE 2
CONTINUE L     R4,36(,R2)         GET OLD WORK AREA
         USING WORK,R4
         TM    IOCBFLGC,IOCBCCLR
         BO    CHGFMT
         CLC   IOCBFMT,32(R2)     HAS HE CHANGED FORMATS
         BNE   CHGFMT             YES, NEED TO CHANGE WORK AREAS
         L     R15,IOCBFMT        POINT AT THE NEW FORMAT
         CLC   FMTHEAD,0(R15)     IS HEADER UNCHANGED
         BNE   CHGFMT             NO, GET NEW WORK AREA
         TM    IOCBFLGS,IOCBFW8+IOCBFNWT    IS IT NEW FORMAT REQUEST
         BNO   RESTART            NO, JUST CONTINUE
NEWFMTOK MVI   FLAGS,CLEARED      REQUEST REFORMATTING
         RETURN (14,12)
         SPACE 2
CHGFMT   DS    0H
         LR    R1,R4              LOAD WORD AREA ADDRESS
         L     R0,DSECSIZE        GET SP AND LENGTH
*        FREEMAIN R,LV=(0),ADDR=(1),SP=(0)
         SVC   10                 ISSUE FREEMAIN SVC
         DROP  R4
         SPACE 2
NEWWORK  L     R15,IOCBFMT        POINT AT THE NEW FORMAT
         LH    R3,0(,R15)         GET LENGTH OF IMAGE
         LA    R3,WORKLEN(,R3)    ADD SIZE OF REST OF WORK AREA
         ICM   R3,8,=AL1(16)      ASK FOR SP=16
         LR    R0,R3
         LNR   R1,R0              SET FLAG FOR GETMAIN
*        GETMAIN R,LV=(0),SP=(0)
         SVC   10                 ISSUE GETMAIN SVC
         ST    R1,36(,R2)         SET POINTER TO THE NEW WORK AREA
         LR    R4,R1
         USING WORK,R4
         ST    R3,DSECSIZE        SAVE FREEMAIN ARGUMENT
         L     R15,IOCBFMT        POINT AT THE NEW FORMAT TABLE
         ST    R15,32(,R2)        PLACE IT IN THE FSA
         MVC   FMTHEAD,0(R15)     SAVE THE HEADER FOR VALIDATION
         MVI   FLAGS,CLEARED      REQUEST A SCREEN RE-FORMAT
         LA    R1,ATTNECB
         ST    R1,WAITLIST             BUILD LIST OF ECB'S FOR WAIT
         LA    R1,TIMERECB             STIMER ECB
         ST    R1,WAITLIST+4
         OI    WAITLIST+4,X'80'        'END OF LIST' FLAG
         XC    ATTNECB(4),ATTNECB      CLEAR ECB AREA
         XC    TIMERECB(4),TIMERECB    CLEAR ECB AREA
*
         TM    IOCBFLGS,IOCBFW8+IOCBFNWT  IS IT A NEW FORMAT REQUEST
         BO    NEWFMTOK
         TM    IOCBFLGS,IOCBFNWR       MUST HAVE A WRITE TO FORMAT TUBE
         BZ    RESTART
         ABEND 110     ATTEMPT TO READ WITH UNFORMATTED SCREEN
         DROP  R4
         SPACE 4
RESTART  ST    R4,8(,R13)         FORWARD CHAIN
         ST    R13,4(,R4)         BACK CHAIN
         LR    R13,R4             ESTABLISH WORK BASE
         USING WORK,R13
*                                 SET ATTENTION EXIT
         STAX  ATTNEXIT,IBUF=0,OBUF=0,USADDR=(13),MF=(E,STAXLIST)
         LTR   R15,R15            TEST STAX RETURN
         BZ    STAXOK             ...GOOD
         ABEND 101                ...NO GOOD
*
STAXOK   DS    0H
         L     R15,IOCBFMT        POINT AT THE USERS FORMAT
         LA    R6,8               SET FMT ENTRY LENGTH FOR BXLE
         LH    R7,2(,R15)         GET LENGTH OF FORMAT
         AR    R7,R15             POINT PAST END OF FORMAT
         SR    R7,R6              POINT AT LAST ENTRY
XGOTBUF  EQU   *
READWILL EQU   *
         TM    IOCBFLGS,IOCBFNWR  IS A WRITE REQUIRED
         BO    NOWRITE            NO, CHECK FOR OTHER OPTIONS
*U010    LA    R8,BUFFER+4        POINT PAST WCC AND SPF FLAG
*U010    MVC   BUFFER(1),4(R15)   MOVE THE DEFAULT WCC IN
         MVC   BUFFPFX(INITSCRL),INITSCR  INIT BUFFER PREFIX       U010
         MVC   BUFFWCC,4(R15)           MOVE THE DEFAULT WCC IN    U010
         TM    IOCBFLGS,IOCBFNBP+IOCBFNRR+IOCBFNRS  WERE THERE O'RIDES
         BO    WCCOK              NO, USE WCC FROM FORMAT
*U010    NC    BUFFER(1),IOCBFLGS           REMOVE OVERRIDDEN BITS
*U010    OC    BUFFER(1),IOCBWCC            SET THE OVERRIDE VALUES
*CCOK    OI    BUFFER,X'C0'       MAKE THE WCC PRINTABLE
*U010    MVC   BUFFER+1(3),=X'115D7F'  SET SPF FLAG FIELD
         NC    BUFFWCC,IOCBFLGS         REMOVE OVERRIDDEN BITS     U010
         OC    BUFFWCC,IOCBWCC          SET THE OVERRIDE VALUES    U010
WCCOK    OI    BUFFWCC,X'C0'            MAKE THE WCC PRINTABLE     U010
         LA    R8,BUFFDATA              -> BUFFER DATA AREA        U010
         LA    R4,6(,R15)         POINT AT FIRST ENTRY
         TM    FLAGS,CLEARED      IS RE-FORMAT NECESSARY?
*U003    BZ    UPDATE             NO - JUST THE CHANGED FIELDS
         BO    CLEARIT                  YES - GO DO IT             U003
         TM    IOCBFLGC,IOCBCATR        REFRESH ATTRIBUTE BYTES?   U003
         BNO   UPDATE                   NO - JUST CHANGED FIELDS   U003
         B     NXTATR                   YES - GO BUILD ATTRIBUTES  U003
         SPACE 2
*CLEARIT MVC   0(L'CLRORD,R8),CLRORD    MOVE IN ORDERS TO CLEAR SCREEN
*U010    LA    R8,L'CLRORD(,R8)         BUMP POINTER OF CLEAR ORDERS
CLEARIT  MVI   BUFFOPCD,X'F5'           3270 ERASE WRITE COMMAND   U010
         LH    R3,0(,R15)         GET LENGTH OF LOCAL AREA
         LA    R2,LOCAL           POINT AT LOCAL AREA
         SR    R1,R1              SET UP TO CLEAR THE AREA
         MVCL  R2,R0              ZERO THE WHOLE THING
*
*   BUILD THE ORDERS TO INSERT THE ATTRIBUTE BYTES
*
NXTATR   MVI   0(R8),X'11'        SBA
         LA    R8,1(,R8)
         LH    R1,2(,R4)          GET THE BUFFER ADDRESS
         BCTR  R1,0               BACK UP TO THE ATTRIBUTE BYTE
         BAL   R14,ENCODE         GO SET UP AN ADDRESS
         MVI   0(R8),X'1D'        START FIELD
         MVC   1(1,R8),1(R4)      MOVE IN THE ATTRIBUTE BYTE
         LA    R8,2(,R8)
         TM    1(R4),X'30'        IS THIS A DISPLAY FIELD
         BO    NOFILL             YES, IT CAN BE EXTENDED TO COVER FILL
         LH    R1,6(,R4)          GET THE FIELD LENGTH
         AH    R1,2(,R4)          COMPUTE THE NEXT ATTRIBUTE BYTE
         LA    R1,1(,R1)          POINT AT THE NEXT DATA BYTE
         CR    R4,R7              IS THERE ANOTHER FIELD
         BNL   DOFILL             NO, MUST LIMIT THIS ONE
         CH    R1,10(,R4)         IS NEXT FIELD ADJACENT
         BE    NOFILL             YES, IT WILL PROVIDE A LIMIT
         BL    DOFILL             OTHERWISE CREATE A DUMMY FIELD
         ABEND 104                OVERLAPPING FIELDS IN FORMAT TABLE
         SPACE 3
DOFILL   MVI   0(R8),X'11'        SBA
         LA    R8,1(,R8)          POINT TO NEXT BUFFER BYTE
         BCTR  R1,0               POINT AT THE ATTRIBUTE BYTE
         BAL   R14,ENCODE
         MVC   0(2,R8),=X'1D7C'   BLANK PROTECTED FIELD
         LA    R8,2(,R8)
         SPACE 2
NOFILL   BXLE  R4,R6,NXTATR       GO DO THE NEXT FIELD
         EJECT
*
*   AT THIS POINT R8 POINTS TO A BUFFER POSITION AT WHICH TO PLACE A
*   SERIES OF ORDERS TO UPDATE THE SCREEN TO LOOK LIKE THE USERS IMAGE.
*   THE ACTUAL CONTENTS ARE IN THE LOCAL IMAGE.
*
UPDATE   SR    R9,R9              INDICATE NO CURRENT BUFFER POSITION
         LA    R4,6(,R15)         POINT AT THE FIRST FORMAT ENTRY
NXTFIELD LH    R2,4(R4)           GET IMAGE OFFSET
         LA    R0,LOCAL(R2)       GET POSITION IN LOCAL IMAGE
         A     R2,IOCBIMG         GET POSITION IN USER IMAGE
         LH    R3,6(,R4)          GET THE FIELD LENGTH
         LR    R1,R3
         LR    R5,R2              SAVE START OF FIELD
         CLCL  R0,R2              LOOK FOR DIFFERENCES
         BE    MATCH              THEY ARE THE SAME
         SH    R1,6(,R4)          NUMBER OF BYTES THAT ARE THE SAME
         LCR   R1,R1
         CH    R1,=H'3'           IS IT WORTH A SBA
         BH    LNGMATCH           YES, GO DO ONE
         LR    R2,R5              NO, BACK UP TO START OF FIELD
         AR    R3,R1              RESTORE COUNT
         CH    R9,2(R4)           DID A PREVIOUS COMMAND PLACE US THERE
         BE    NOSBA1             YES, NO NEED TO DO A SBA
         MVI   0(R8),X'11'        SBA
         LA    R8,1(,R8)
         LH    R1,2(,R4)          POINT AT FIRST DATA BYTE
         BAL   R14,ENCODE
NOSBA1   TM    0(R4),X'C0'        IS A CURSOR NEEDED AT START OF FIELD
         BNM   NOCUR1             NO
         TM    IOCBFLGS,IOCBFCUR  HAS HE OVERRIDDEN THE CURSOR POSITION
         BO    NOCUR1             YES, DON'T NEED ONE HERE
         MVI   0(R8),X'13'        INSERT THE CURSOR
         LA    R8,1(,R8)
NOCUR1   B     DATAMOVE           GO PLACE THE ACTUAL DATA IN STREAM
         SPACE 2
LNGMATCH LR    R5,R1              SAVE BYTE COUNT REMAINING
         TM    0(R4),X'C0'        DID WE NEED A CURSOR
         BNM   NOCUR2             NO, JUST GO DO THE DATA
         TM    IOCBFLGS,IOCBFCUR  HAS HE OVERRIDDEN THE CURSOR POSITION
         BO    NOCUR2             YES, DON'T HONOR THE FORMAT BYTE
         CH    R9,2(R4)           MAYBE WE ARE ALREADY THERE
         BE    NOSBA2             YES, HOW LUCKY
         MVI   0(R8),X'11'        WHAT A LOT OF WORK FOR A MEASLY
         LA    R8,1(,R8)             LITTLE CURSOR.
         LH    R1,2(,R4)          POINT AT THE DATA ADDRESS
         BAL   R14,ENCODE
NOSBA2   MVI   0(R8),X'13'        INSERT CURSOR
         LA    R8,1(,R8)
NOCUR2   MVI   0(R8),X'11'
         LA    R8,1(,R8)
         LH    R1,2(,R4)          GET FIELD ADDRESS IN 3270
         AR    R1,R5              MOVE TO POINT OF FIRST DIFFERENCE
         BAL   R14,ENCODE
*
*   R8=BUFFER LOCATION  R2=DATA ADDRESS   R3=DATA LENGTH
*
DATAMOVE STM   R2,R3,28(R13)      SAVE INPUT POINTERS
         LH    R1,4(,R4)          GET IMAGE OFFSET
         LA    R0,LOCAL(R1)       COMPUTE LOCAL FIELD START
         LR    R1,R3              GET DATA LENGTH
         SH    R1,6(,R4)          GET COMPLEMENT OF NUMBER OF MATCHES
         SR    R0,R1              BUMP LOCAL FIELD ADDRESS
         LR    R1,R3              GET NUMBER OF BYTES TO MOVE
         MVCL  R0,R2              UPDATE LOCAL IMAGE
         LM    R2,R3,28(R13)      RESTORE THE DATA POINTERS
         BCTR  R2,0               ADJUST FOR REVERSE SCAN
BLNKOUT  LA    R5,0(R2,R3)        POINT AT TRAILING BYTE
         CLI   0(R5),C' '         IS IT A BLANK
         BNE   SHORTND            NO, END OF TRUNCATION
         BCT   R3,BLNKOUT         THROW AWAY THE BLANKS
         BCTR  R5,0               ALLOW BLANKING OF 1-BYTE FIELDS
         B     INSPT              JUST WIPE OUT REST OF FIELD
         SPACE 2
SHORTND  LA    R2,1(,R2)          POINT BACK AT FIRST BYTE
         LA    R9,BUFFER+L'BUFFER-16        LAST ALLOWABLE ADDRESS
         SR    R9,R8
         CR    R9,R3              WILL IT FIT
         BH    LENOK
         ABEND 105                GENERATED DATA EXCEEDS BUFFER LENGTH
LENOK    LR    R9,R3
         LR    R1,R9              SAVE OUTPUT LENGTH
         MVCL  R8,R2
         LR    R3,R1              RETRIEVE OUTPUT LENGTH
         LR    R2,R8
         SR    R2,R3              COMPUTE OUTPUT ADDRESS
         BAL   R14,OKCHAR         KEEP INVALID CHARS OFF THE LINE
         SPACE 2
INSPT    TM    0(R4),X'C0'        DOES HE WANT A CURSOR HERE
         BNO   NOCUR3
         TM    IOCBFLGS,IOCBFCUR  HAS HE OVERRIDDEN THE CURSOR POSITION
         BO    NOCUR3             YES, THEN HE DOESN'T WANT ONE HERE
         MVI   0(R8),X'13'        INSERT CURSOR
         LA    R8,1(,R8)
NOCUR3   S     R5,28(,R13)        COMPUTE DISTANCE TO END OF FIELD
         S     R5,32(,R13)        GET -1-REMAINING BYTES
         LCR   R5,R5              THAT MAKES IT REMAINING BYTES + 1
         BCT   R5,*+8             CHECK FOR FULL FIELD
         B     NONULL             IF SO, DO NOT PUT THE NULL IN
         MVI   0(R8),0            INSERT A NULL
         LA    R8,1(,R8)
NONULL   MVI   0(R8),X'05'        INSERT A PT ORDER
         LA    R8,1(,R8)
         LR    R9,R4              POINT AT CURRENT FMT ENTRY
PTSCAN   BXH   R9,R6,NOUNP        POINT TO NEXT FORMAT ENTRY
         TM    1(R9),X'20'        IS THE FIELD UNPROTECTED
         BO    PTSCAN             NO. PT ORDER WILL SKIP IT
         LH    R9,2(,R9)          GET STOPPING POINT FOR PT ORDER
         B     ENDFIELD
         SPACE 3
MATCH    TM    IOCBFLGS,IOCBFCUR  HAS HE OVERRIDDEN THE CURSOR POSITION
         BO    ENDFIELD           YES, THEN WE ARE THROUGH HERE
         TM    0(R4),X'C0'        DID THE FORMAT CALL FOR A CURSOR
         BZ    ENDFIELD           NO, GO TO NEXT FIELD
         BO    MATSCAN            YES, AFTER ALL THE DATA
         CH    R9,2(,R4)          IS THE BUFF ADD NOW AT RIGHT POINT
         BE    NOSBA4             YES, HOW FORTUTITOUS
         LH    R1,2(,R4)          GET THE DATA ADDRESS
MATSBA   MVI   0(R8),X'11'        SBA
         LA    R8,1(,R8)
         BAL   R14,ENCODE
NOSBA4   MVI   0(R8),X'13'        INSERT CURSOR
         LA    R8,1(,R8)
         SR    R9,R9              INDICATE UNKNOWN BUFFER ADDRESS
         B     ENDFIELD
         SPACE 2
MATSCAN  LH    R1,6(,R4)          GET FIELD LENGTH
MATSCANL BCTR  R2,0               MOVE POINTER BACK ONE
         CLI   0(R2),C' '         IS THIS A BLANK
         BNE   MATSCANE           YES, PLACE THE CURSOR HERE
         BCT   R1,MATSCANL
MATSCANE AH    R1,2(,R4)          COMPUTE CURSOR ADDRESS
         B     MATSBA
         SPACE
NOUNP    SR    R9,R9              INDICATE NO FOLLOWING UNPROTECTED
ENDFIELD BXLE  R4,R6,NXTFIELD     GO DO NEXT FIELD
         SPACE 5
*                             PUT TIMESTAMP IN BOTTOM LH CORNER
*
         TM    IOCBFLGC,IOCBCTIM
         BO    NOTIMSTP
         MVC   0(STAMPLEN,R8),IOCBCTRL
*U012    LA    R8,STAMPLEN(R8)
         MVC   STAMPLEN(2,R8),=X'1D60'  PROT FIELD AFTER STAMP     U012
         LA    R8,STAMPLEN+2(,R8)       POINT PAST THIS DATA       U012
NOTIMSTP EQU   *
*
*  NOW THAT THE SCREEN HAS BEEN FORMATTED WE CAN PLACE THE SPECIAL
*  FIELD IN THE LOWER RIGHT CORNER FOR ERASE INPUT DETECTION.
*
         MVC   0(7,R8),=X'115D7E1DC11D60'   ROW 24 COL 79          U008
         LA    R8,7(,R8)          BUMP THE FIELD POINTER
         TM    IOCBFLGS,IOCBFCUR  HAS HE OVERRIDDEN THE CURSOR POSITION
         BZ    NOCUR4             NO, THEN CURSOR IS ALREADY IN
         LH    R1,IOCBCUR         GET IMAGE OFFSET OF CURSOR
         L     R4,IOCBFMT         GET FIRST FORMAT ENTRY
         LA    R4,6(,R4)          SKIP THE HEADER
CURSCAN  LH    R2,4(,R4)          GET THE IMAGE OFFSET OF THE FIELD
         AH    R2,6(,R4)          COMPUTE END OF FIELD
         CR    R1,R2              SEE IF CURSOR IS IN FIELD
         BL    CURFND             YES, GO PLACE IT THERE
         BXLE  R4,R6,CURSCAN      GO CHECK THE NEXT ENTRY
CURSBAD  ABEND 106                CURSOR IS BEYOND END OF IMAGE
         SPACE 2
CURFND   SH    R1,4(,R4)          COMPUTE OFFSET IN FIELD
         BNM   CUROK              CHECK FOR INCLUSION
         B     CURSBAD            CURSOR IS IN IMAGE FILL AREA
CUROK    AH    R1,2(,R4)          COMPUTE TUBE ADDRESS
         MVI   0(R8),X'11'        SBA
         LA    R8,1(,R8)
         BAL   R14,ENCODE
         MVI   0(R8),X'13'        INSERT CURSOR
         LA    R8,1(,R8)
         SPACE 3
NOCUR4   NI    BUFFWCC,255-X'02'        RESET 'KEYBOARD RESET' BIT U010
         LA    R9,BUFFER          POINT AT FRONT OF BUFFER
         SR    R8,R9              COMPUTE DATA LENGTH
         TPUT  (R9),(R8),FULLSCR,WAIT,NOHOLD,NOBREAK
         CH    R15,=H'8'
         BE    SIMPA1        RC = 8 (ATTENTION DURING TPUT)
         LTR   R15,R15
         BZ    TPUTOK
         ABEND 107                     ERROR FROM TPUT
TPUTOK   MVI   FLAGS,LASTOPW                REMEMBER THE STATUS
         TM    IOCBFLGS,IOCBFNRD  WAS A READ REQUESTED
         BZ    READ               YES, GO ISSUE IT
         TM    IOCBFLGS,IOCBFNWT  DID HE WANT ME TO DO THE WAIT
         BZ    SETAID             ...YES, GO DO IT (MAYBE)
         B     EXIT               ...NO, SO EXIT.
*
SETAID   EQU   *
         MVI   IOCBAID,AIDENT     INIT RETURN AID
*
*                          NO  READ REQUESTED
*
         TM    IOCBFLGD,IOCBDYON      USE IOCB WAIT TIME?
         BZ    EXIT                   ...NO, SO EXIT.
         TM    IOCBFLGD,IOCBDFRC      FORCE DELAY OF TSO USER?
         BZ    EGETS                  ...NO, SO DO TGET INSTEAD.
         STIMER REAL,TIMEXIT,BINTVL=IOCBWAIT  ...YES, DO STIMER
*
         WAIT  ECBLIST=WAITLIST      THEN WAIT TIL ATTN OR TIMER POST
*
         TM    ATTNECB,X'40'            ATTN INTERRUPT?            U006
         BO    WAIT1                    YES, PROCESS IT            U006
         MVI   TIMERECB,X'00'           NO, RESET TIMER ECB        U006
         B     EXIT                         AND GO HOME            U006
*
WAIT1    MVI   ATTNECB,X'00'            RESET ATTN ECB             U006
         TTIMER CANCEL                  CANCEL STIMER
         MVI   TIMERECB,X'00'           RESET TIMER ECB            U006
         B     SIMPA1                   AND SIMULATE ATTENTION
*
EGETS    EQU   *
         L     R1,IOCBTERM+8
         LTR   R1,R1
         BNZ   EGOTBUF
         GETMAIN R,LV=4096,SP=1
         ST    R1,IOCBTERM+8 .       KEEP RETRY BUFFER
EGOTBUF  EQU   *
         L     R9,IOCBTERM+8
         LA    R8,2148
         B     TGET
*
         SPACE 2
NOWRITE  TM    IOCBFLGS,IOCBFW8   WAS A WAIT ONLY REQUESTED
         BO    BADREQST           YES, WE CAN'T DO THAT
         TM    FLAGS,LASTOPW      WAS LAST OPERATION A WRITE
         BO    READ               YES, THEN GO DO THE READ
BADREQST ABEND 102                NO, ATTEMPT TO READ TWICE IN A ROW
         SPACE 2
READ     LA    R9,BUFFER
         LA    R8,L'BUFFER
TGET     EQU   *
         ST    R9,CURRBUF
         TGET  (R9),(R8),ASIS
         ST    R1,INLEN                SAVE INPUT LENGTH
         CH    R15,=H'8'               CHECK RESULTS
         BL    TGETOK
         BE    SIMPA1                  ATTN WAS PROCESSED
         ABEND 103                     INVALID RC FROM TGET
         SPACE 2
SIMPA1   MVI   0(R9),AIDPA1            SET ATTENTION AID
         CLI   IOCBTERM+1,X'7F'         DONE STFSMODE?             U006
         BNE   TGETOK                   NO, DON'T DO IT AGAIN      U006
         STFSMODE  ON,INITIAL=NO        YES, MAKE SURE ALL IS OKAY U006
         SPACE 2
TGETOK   MVI   FLAGS,LASTOPR      INDICATE ALL I/O COMPLETE
         MVC   IOCBAID,0(R9)      SAVE THE ATTENTION DESCRIPTION
         TR    IOCBAID,AIDTABLE         CHANGE PF13-24 TO PF1-12   U001
*
         BAL   R14,CHKAID               CHK FOR PFK'S OR PA'S
*
         LR    R5,R1                    GET LENGTH OF INPUT DATA
         LA    R5,BUFFER-3(R5)          POINT AT LAST 3 BYTES      U008
         CLC   0(3,R5),=X'115D7F'       IS LAST FIELD EXPECTED ONE U008
         BE    CNVTCUR            YES, USER DID NOT HIT ERASE INPUT
* LOCAL 3272'S SEEM TO CRUNCH THE LAST BYTE OF THE SBA SEQUENCE
* RETURNED BY OUR SPECIAL ZERO-LENGTH FIELD.  HENCE THE KLUGE HERE.
         CLC   0(2,R5),=X'115D7F'       IS LAST FIELD SORT OF      U013
         BE    CNVTCUR                  YES, HARDWARE SCREWED UP   U013
* LOCAL 3272'S SEEM TO LOSE THE LAST BYTE OF THE SBA SEQUENCE
* RETURNED BY OUR SPECIAL ZERO-LENGTH FIELD.  HENCE THE KLUGE HERE.
         LA    R5,1(,R5)                POINT AT LAST 2 BYTES      U009
         CLC   0(2,R5),=X'115D7F'       IS LAST FIELD SORT OF      U009
         BE    CNVTCUR                  YES, HARDWARE SCREWED UP   U009
         LA    R5,2(,R5)                SET LIMIT TO END OF BUFFER U009
*
*  THE SPECIAL FIELD THAT WAS PLACED IN THE LOWER RIGHT HAND CORNER
*  OF THE SCREEN WAS RESET. THE USER HIT THE ERASE INPUT KEY.
*
         L     R4,IOCBFMT
         LA    R4,6(,R4)          POINT AT THE FIRST FORMAT ENTRY
ERSINP   TM    1(R4),X'20'        IS THIS A PROTECTED FIELD
         BO    ERSPRO             YES, THEN DON'T ERASE IT
         LH    R2,4(,R4)          GET THE IMAGE OFFSET
         LA    R2,LOCAL(R2)       POINT AT THE LOCAL COPY
         LH    R3,6(,R4)          GET THE FIELD LENGTH
         SR    R1,R1              SET UP FOR FIELD ZEROING
         ICM   R1,8,=CL128' '     MAKE THE FILL CHARACTER A BLANK
         MVCL  R2,R0              BLANK OUT THE FIELD
ERSPRO   BXLE  R4,R6,ERSINP       GO DO THE NEXT FIELD
*U008    CLC   0(3,R5),=X'115D7E6A6A'       IS THE FIELD STILL THER
*U008    BE    CNVTCUR            FIELD IS STILL THERE, BUT DIFFERENT
*U008    LA    R5,1(,R5)          MOVE TO LAST POSSIBLE POSITION
*U008    CLC   0(3,R5),=X'115D7E6A6A'       DID HE ONLY DO IN ONE BYTE
*U008    BE    CNVTCUR            YES, IGNORE THAT FIELD
*U008    LA    R5,4(,R5)          FIELD IS GONE COMPLETELY
*U009    LA    R5,3(,R5)                SET LIMIT TO END OF BUFFER U008
         SPACE 2
CNVTCUR  EQU   *
         SR    R9,R9              COUNT OF # OF FIELDS MOVED
         LA    R8,BUFFER+1        POINT AT BYTE AFTER THE AID
         BAL   R14,DECODE         GET THE IMAGE OFFSET FOR IT
         STH   R1,IOCBCUR         FEED IT BACK TO THE USER
         CR    R8,R5              IS THE BUFFER EXHAUSTED
         BNL   EOB                IF SO, COPY THE UPDATED BUFFER
         CLI   0(R8),X'11'        VERIFY THAT IT IS A SBA ORDER
         BNE   CLEAR              BUFFER WAS NOT FORMATTED, IGNORE IT
INPFIELD LA    R8,1(,R8)          POINT PAST THE SBA
*
         CLC   0(2,R8),=X'5DE5'   CHECK FOR INTERVAL FIELD
         BE    NEWTIME            USER CHANGED TIME INTERVAL VALUE
*
         BAL   R14,DECODE         GET THE IMAGE OFFSET
         CH    R1,4(,R4)          DOES IT POINT TO FIELD START
         BNE   BADDATA            NO...INVALID INPUT LOC
*
         LA    R9,1(R9)           INCREMENT COUNT OF FLDS MOVED
         LR    R2,R8              GET DATA ADDRESS
         LR    R3,R5              GET EOB ADDRESS
         SR    R3,R2              COMPUTE REMAINING BYTES
         BP    SBALOOP            CHECK FOR LEGAL DATA FORMAT
         LR    R1,R8              USER ERASED THE FIELD
         BZ    FNDSBA             GO COPY NO BYTES TO THE IMAGE
*
BADDATA  ABEND 108                INPUT DATA WAS TOO SHORT
SBALOOP  CH    R3,=H'256'
         BNH   SHRTRT             IS LENGTH DOWN BELOW 257
         TRT   0(256,R2),TRTSBA   LOOK FOR AN SBA BYTE
         BNZ   FNDSBA             WAS ONE FOUND
         AH    R2,=H'256'         NO, ADVANCE ONE SWATCH
         SH    R3,=H'256'         REDUCE THE COUNT
         B     SBALOOP            GO TRY SOME MORE
SHRTRT   BCTR  R3,0               REDUCE COUNT FOR EX
         EX    R3,EXTRT           ONE LAST SCAN
         BNZ   FNDSBA             WAS ONE FOUND
         LA    R1,1(R2,R3)        NO, POINT PAST END OF BUFFER
FNDSBA   SR    R1,R8              GET INPUT FIELD LENGTH
         LR    R0,R8              GET FIELD ADDRESS
         AR    R8,R1              POINT AT THE LOCATED BYTE
         LR    R3,R1              SET UP FOR UPCASE ROUTINE
         LH    R2,4(,R4)          GET IMAGE OFFSET
         LA    R2,LOCAL(R2)       POINT AT DESTINATION
         LR    R14,R2
         LH    R15,6(,R4)         GET DESTINATION LENGTH
         ICM   R1,8,=CL128' '     SET UP THE PAD CHARACTER
         MVCL  R14,R0             COPY INPUT INTO IMAGE
CHKEND   CR    R8,R5              IS THAT END OF BUFFER
         BL    INPFIELD           NO, GO DO THE NEXT ONE
EOB      BE    CANCEL             MAKE SURE EOB HIT RIGHT ON
         B     BADDATA            END OF INPUT BUFFER WAS MISSED
         SPACE 2
*                   GET UPDATED TIME INTERVAL FROM INPUT
*
NEWTIME  DS    0H
         MVC   4(2,R8),5(R8)              COMPRESS OUT '.'
         XC    DOUBLE(8),DOUBLE           CLEAR WORK AREA
         PACK  DOUBLE+5(3),2(4,R8)
         CVB   R1,DOUBLE                  INTO BINARY
         ST    R1,IOCBWAIT                AND STORE FOR NEXT TIME
*
         LA    R8,7(R8)                   MOVE POINTER PAST TIME
         B     CHKEND                     AND RETURN INTO LOOP
         EJECT
CANCEL   L     R2,IOCBIMG         POINT AT USER IMAGE AREA
         LA    R0,LOCAL           POINT AT OUR IMAGE AREA
         L     R14,IOCBFMT        POINT AT USER FORMAT
         LH    R1,0(,R14)         GET IMAGE LENGTH
         LR    R3,R1
         MVCL  R2,R0              COPY LOCAL IMAGE TO USER IMAGE
         LH    R3,0(,R14)              GET LENGTH AGAIN
         SR    R2,R3
         BAL   R14,UPCASE              MAKE THE USERS COPY UPPER CASE
         CLI   IOCBAID,AIDCAN     DID WE GET HERE FROM CANCEL KEY
         BNE   CHECKSIM           NO, CHECK FOR SIMULATED PFK
         SPACE 2
PA1      EQU   *
PA3      EQU   *
PF12     EQU   *
*U010    TPUT  CLEARSCR,11,FULLSCR,,HOLD   CLEAR SCREEN            U001
         TPUT  CLRSCR,CLRSCRL,FULLSCR                              U010
*
CLEAR    MVI   FLAGS,CLEARED      INDICATE THAT TUBE IS NOT FORMATTED
         B     EXIT               RETURN TO USER
         SPACE 4
         EJECT
CHECKSIM DS    0H     CHECK FOR SIMULATED PFK, OR MM COMMAND
*                     ...DENOTED BY SINGLE INPUT FIELD WITH
*                     LEADING '/'.
*   CHANGED TO '='                                                 U001
         CH    R9,=H'1'          SINGLE FIELD ENTERED?
         BNE   EXIT              ...NO, LEAVE
         L     R1,CURRBUF        WHERE INPUT SHOULD BE
         LA    R1,6(R1)          START OF ACTUAL DATA
         CLI   0(R1),C'='        START WITH ESCAPE CHAR?           U001
         BNE   EXIT              ...NO, LEAVE
*
         MVC   TESTPFK(1),1(R1)
         TR    TESTPFK(1),PFKTRT  TRANSLATE SIMULATORS TO PFK AIDS
         CLI   TESTPFK,X'00'     WAS IT VALID PFK SIMULATOR?
         BE    CKSIM2            ...NO, ASSUME MM COMMAND
         MVC   IOCBAID,TESTPFK   MOVE TO AID FIELD
         BAL   R14,CHKAID        TEST AGAIN FOR PA1,3, OR PF12
         B     EXIT              THEN LEAVE.
*
CKSIM2   EQU   *                 HAVE MM COMMAND.
         BAL   R14,GETFSA        GET FSA IN REG2
         CLC   44(2,R2),=C'MM'   IS MM IN CONTROL?
         BNE   EXIT              ...NO, TOO BAD.
         CLI   IOCBAID,AIDPF3           IS IT END KEY?             U005
         BNE   EXIT                     NO - PASS TO PROCESSOR     U005
*
         LA    R0,1(,R1)         SOURCE ADDR (INPUT BUFFER)
         L     R1,INLEN          USER INPUT LEN
         SH    R1,=H'12'         LESS LEADING AND TRAILING STUFF
         ICM   R1,8,=C' '        PAD CHARACTER
*
         SR    R3,R3             CLEAR FOR ICM
         ICM   R3,1,40(R2)       MM WORK AREA LENGTH
         L     R2,40(R2)         MM WORK AREA ADDR
*
         MVCL  R2,R0             DO MOVE TO MM'S AREA
*
         MVI   IOCBAID,AIDPA1    SIMULATE ATTN TO TERMINATE TASK
         B     CLEAR             CLEAR FLAGS
*
EXIT     EQU   *
         BAL   R14,GETFSA        GET FSA IN REG2
*
         CLI   44(R2),X'7F'
         BNE   OOPS
         L     R3,48(R2)
         USING TDCB,R3
         MVC   TDCBAID,IOCBAID
         DROP  R3
*
*
*
OOPS     EQU   *
         STAX  ,                  CANCEL ATTENTION EXIT
         L     R13,4(,R13)        POINT TO CALLERS REGISTERS
         RETURN (14,12)
         EJECT
*
*   CONVERT A HALF WORD BUFFER ADDRESS TO 3270 FORMAT
*
*        R1 = 12 BIT BUFFER ADDRESS
*        R8 = OUTPUT AREA ADDRESS
*
ENCODE   SLDL  R0,26              GET TWO 6-BIT QUANTITIES
         SRL   R1,2               MAKE THEM 8-BIT QUANTITIES
         SRDL  R0,8               IN THE SAME REGISTER
         STCM  R1,12,0(R8)        PLACE THEM IN THE DATA STREAM
         TR    0(2,R8),TABLENC    MAKE THEM PRINTABLE
         LA    R8,2(,R8)          BUMP THE OUTPUT ADDRESS
         BR    R14                RETURN TO CALLER
         SPACE 2
TABLENC  DC    C' ABCDEFGHI¢.<(+|&&JKLMNOPQR!$*);¬-/STUVWXYZ¦,%_>?01234X
               56789:#@''="'      TABLE TO MAKE ADDRESSES PRINTABLE
         SPACE 4
*
*   ROUTINE TO CONVERT A FIELD TO UPPER CASE
*        R2=ADDRESS,  R3=LENGTH,  R14=RETURN
*
OKCHAR   LA    R1,OKCTBL          USE OUTPUT TRANSLATE TABLE
         B     *+8
UPCASE   LA    R1,UPCTBL          USE INPUT (UPPER CASE) TABLE
         CH    R3,=H'256'         IS IT TOO SHORT FOR A LONG TR ?
         BL    LASTPASS           YES, GO DO AN EXECUTED ONE
         TR    0(256,R2),0(R1)      CONVERT A SWATCH
         AH    R2,=H'256'         MOVE TO NEXT SWATCH
         SH    R3,=H'256'         DECREASE REMAINING COUNT
         BNZ   UPCASE             GO DO IT AGAIN
LASTPASS SH    R3,=H'1'           REDUCE FOR EXECUTE
         BMR   R14                YES, JUST RETURN
         EX    R3,TRUPC           CONVERT TAIL END TO U.C.
         BR    R14
         SPACE 2
TRUPC    TR    0(0,R2),0(R1)           << EXECUTED >>
         SPACE 2
OKCTBL   DC    256C'?'                 ALL INVALIDS GO TO QUESTION
         ORG   OKCTBL+X'00'                                        U006
         DC    X'00'                                               U006
         ORG   OKCTBL+C' '
         DC    C' '
         ORG   OKCTBL+C'¢'
         DC    C'¢.<(+|&&'
         ORG   OKCTBL+C'!'
         DC    C'!$*);¬-/'
         ORG   OKCTBL+C','
         DC    C',%_>?'
         ORG   OKCTBL+C':'
         DC    C':#@''="'
         ORG   OKCTBL+X'81'
         DC    X'818283848586878889'   RETAIN LOWER CASE FOR OUTPUT
         ORG   OKCTBL+X'91'
         DC    X'919293949596979899'
         ORG   OKCTBL+X'A2'
         DC    X'A2A3A4A5A6A7A8A9'
         ORG   OKCTBL+C'A'
         DC    C'ABCDEFGHI'
         ORG   OKCTBL+C'J'
         DC    C'JKLMNOPQR'
         ORG   OKCTBL+C'S'
         DC    C'STUVWXYZ'
         ORG   OKCTBL+C'0'
         DC    C'0123456789'
         ORG
         SPACE 2
UPCTBL   DC    256C'?'                 ALL INVALIDS GO TO QUESTION
         ORG   UPCTBL+X'00'                                        U007
         DC    X'00'                                               U007
         ORG   UPCTBL+C' '
         DC    C' '
         ORG   UPCTBL+C'¢'
         DC    C'¢.<(+|&&'
         ORG   UPCTBL+C'!'
         DC    C'!$*);¬-/'
         ORG   UPCTBL+C','
         DC    C',%_>?'
         ORG   UPCTBL+C':'
         DC    C':#@''="'
         ORG   UPCTBL+X'81'
         DC    C'ABCDEFGHI'            LOWER GOES TO UPPER FOR INPUT
         ORG   UPCTBL+X'91'
         DC    C'JKLMNOPQR'
         ORG   UPCTBL+X'A2'
         DC    C'STUVWXYZ'
         ORG   UPCTBL+C'A'
         DC    C'ABCDEFGHI'
         ORG   UPCTBL+C'J'
         DC    C'JKLMNOPQR'
         ORG   UPCTBL+C'S'
         DC    C'STUVWXYZ'
         ORG   UPCTBL+C'0'
         DC    C'0123456789'
         ORG
         SPACE 4
*
*   ROUTINE TO FIND THE IMAGE OFFSET FOR A BUFFER ADDRESS
*   INPUT:  R8=ADDRESS OF INPUT,  R6 AND R7 SET UP FOR BXLE THROUGH FMT
*   OUTPUT:  R1= IMAGE OFFSET, R4 POINTS AT FORMAT ENTRY
*      R1 = -1 IF BUFFER ADDRESS IS OUTSIDE OF THE IMAGE
*
DECODE   L     R4,IOCBFMT
         LA    R4,6(,R4)          POINT AT FIRST FORMAT
         IC    R0,1(,R8)          GET THE SECOND BYTE
         SLL   R0,2               REMOVE HIGH TWO BITS
         ICM   R0,2,0(R8)         TACK ON THE FIRST BYTE
         SRL   R0,2               PUT IT IN LOW 12 BITS
         LA    R1,4095            SET MASK FOR 12 BITS
         NR    R1,R0              GET THE BINARY BUFFER OFFSET
         LA    R8,2(,R8)          BUMP THE SCAN POINTER
DECDL    LH    R0,2(,R4)          GET FIELD START
         CR    R0,R1              IS THIS IT
         BE    DECDF              YES, RIGHT ON THE DOT
         BH    DECDERR            FIELD IS NOT DEFINED
         AH    R0,6(,R4)          POINT AT END OF FIELD
         CR    R0,R1              CHECK FOR INCLUSION
         BH    DECDF              YES, GOT IT
         BXLE  R4,R6,DECDL        GO TRY NEXT FIELD
DECDERR  LH    R1,=H'-1'          SET ERROR INDICATION
         BR    R14
DECDF    SH    R1,2(,R4)          GET RELATIVE POSITION IN FIELD
         AH    R1,4(,R4)          POINT INTO IMAGE INSTEAD
         BR    R14                RETURN TO CALLER
         SPACE 4
GETFSA   DS    0H        FIND FSA AND RETURN IN REG 2
         L     R2,16                        POINT AT THE CVT
         L     R2,0(,R2)                    NEW/OLD
         L     R2,4(,R2)                    OUR TCB
         L     R2,112(,R2)                  FSA
         BR    R14
CHKAID   DS    0H
*        TM    IOCBFLGC,IOCBCFRA  IF FORCE
*        BO    EXIT                  THEN EXIT
*U008
*U008    TM    IOCBFLGC,IOCBCRAO  IF
*U008    BNO   OTHER2               REQUESTED
*U008    CLI   IOCBAID,AIDPF1                TRANSMIT
*U008    BL    OTHER1                                ONLY
*U008    CLI   IOCBAID,AIDPF9                            PFK
*U008    BH    OTHER2                                       AID
*U008    B     EXIT                                            BACK
*OTHER1  EQU   *
*U008    CLI   IOCBAID,AIDPF10     OTHERWISE,
*U008    BL    OTHER2                        PERFORM
*U008    CLI   IOCBAID,AIDPF12                      NORMAL
*U008    BH    OTHER2                                     READ
*U008    BE    PF12                                           REPLACE
*U008    B     EXIT
*
*OTHER2  EQU   *
         CLI   IOCBAID,AIDCLR     DID HE HIT THE CLEAR KEY
         BE    CLEAR              YES, GO INDICATE ZEROED BUFFER
         CLI   IOCBAID,AIDCAN     DID HE HIT CANCEL (PA2)
         BE    CANCEL             YES, GIVE BACK ORIGINAL SCREEN
         CLI   IOCBAID,AIDPA1     IS IT SOME OTHER FUNCTION
         BE    PA1
         CLI   IOCBAID,AIDPA3
         BE    PA3
         CLI   IOCBAID,AIDPF12
         BE    PF12
         TM    IOCBAID,X'40'      IS IT A VALID AID
         BOR   R14                YES, GO LOOK AT MANUAL INPUT
         ABEND 109                AID BYTE WAS NOT VALID
         EJECT
*---------------------------------------------------------------------*
*
*    ATTENTION EXIT - ENTERED FROM SYSTEM IF USER HITS ATTENTION
*            KEY.  IT WILL POST AN ECB WHICH THE MAIN LINE ROUTINE
*            IS WAITING ON.
*
*---------------------------------------------------------------------*
         SPACE 2
         DROP  R13
*
ATTNEXIT DS    0H
         STM   R14,R12,12(R13)          SAVE REGS
         L     R4,8(R1)                 WORK AREA ADDR
         USING WORK,R4
         LR    R12,R15                  ENTRY ADDRESS
         USING ATTNEXIT,R12
*
         POST  ATTNECB                  ISSUE POST
*
         RETURN (14,12),RC=0
*
         DROP  R4,R12
         USING WORK,R13
         EJECT
*---------------------------------------------------------------------*
*
*    STIMER EXIT - ENTERED FROM SYSTEM IF STIMER (FOR IOCB VALUE)
*            EXPIRES.  IT WILL POST AN ECB WHICH THE MAIN LINE
*            ROUTINE IS WAITING ON.
*
*---------------------------------------------------------------------*
         SPACE 2
TIMEXIT  DS    0H
         STM   R14,R12,12(R13)          SAVE REGS
         LR    R12,R15                  ENTRY ADDRESS
         USING TIMEXIT,R12
*
*                                       FIND WORK AREA ADDRESS
         L     R2,16                        POINT AT THE CVT
         L     R2,0(,R2)                    NEW/OLD
         L     R2,4(,R2)                    OUR TCB
         L     R2,112(,R2)                  FSA (FIRST SAVE AREA)
         L     R4,36(R2)                    WORK AREA ADDRESS
         DROP  R13                      DROP PREVIOUS WORK BASE
         USING WORK,R4
*
         POST  TIMERECB                 ISSUE POST
*
         RETURN (14,12),RC=0            RETURN TO SYSTEM
*
         DROP  R4,R12
         USING WORK,R13                 RESTORE PREVIOUS WORK BASE
         EJECT
*   CONSTANTS DSECTS AND OTHER POOR TRASH
NOT      EQU   X'FF'
EXTRT    TRT   0(0,R2),TRTSBA     SCAN FOR SBA
         SPACE 4
         LTORG
         SPACE 1
INITSCR  DC    X'27'                    ESC: 3270 COMMAND FOLLOWS  U010
         DC    X'F1'                    3270 NORMAL WRITE COMMAND  U010
         DC    X'C0'                    WCC: SKELETON VALUE        U010
         DC    X'115D7F'                SPF TCAM PREFIX: INIT      U010
INITSCRL EQU   *-INITSCR                                           U010
         SPACE 1
*LEARSCR DC    XL11'C41140403C4040001D4013'  FOR CLEAR TPUT        U002
         SPACE 1
CLRSCR   DC    X'27'                    ESC: 3270 COMMAND FOLLOWS  U010
         DC    X'F5'                    3270 ERASE WRITE COMMAND   U010
         DC    X'C4'                    WCC: ALARM                 U010
         DC    X'115D7E'                SPF TCAM PREFIX: END       U010
         DC    X'114040'                REQUIRED FOR SPF TCAM PFX  U010
CLRSCRL  EQU   *-CLRSCR                                            U010
         SPACE 1
*U010  CLRORD  DC  X'1140403C404000'         ORDERS TO CLEAR SCREEN
TRTSBA   DC    XL17'0',X'01',XL238'0'       TRT TABLE FOR SBA ORDER
         SPACE 1
PFKTRT   DC    240X'00',X'7A',C'123456789',6X'00'
         ORG   PFKTRT+C'-'
         DC    X'7B'        - IS PFK 11 SIMULATOR
         ORG   PFKTRT+C'='
         DC    X'7C'        = IS PFK 12 SIMULATOR
         ORG
         SPACE 1
AIDTABLE DC    256AL1(*-AIDTABLE)       EVERYTHING GOES TO ITSELF  U001
         ORG   AIDTABLE+X'C1'           EXCEPT PF13-PF21           U001
         DC    X'F1F2F3F4F5F6F7F8F9'    CHANGE TO PF1-PF9          U001
         ORG   AIDTABLE+X'4A'           AND PF22-PF24              U001
         DC    X'7A7B7C'                CHANGE TO PF10-PF12        U001
         ORG   ,                        THAT'S IT                  U001
         SPACE 1                                                   U001
PATCH    DS    XL50'0'
IOCB     DSECT
         IOCB
         EJECT
*---------------------------------------------------------------------*
*
*  WORK DSECT ... SAVE AREAS, SCREEN BUFFER AND MISC.
*
*---------------------------------------------------------------------*
         SPACE 2
WORK     DSECT
         DS    18F                SAVE AREA
DOUBLE   DS    D                  WORK AREA FOR NEWTIME - PACK
TIMERECB DS    F                  ECB FOR STIMER
ATTNECB  DS    F                  ECB FOR ATTENTION EXIT
WAITLIST DS    2A                 LIST OF ECB'S FOR WAIT
*
STAXLIST STAX  ATTNEXIT,MF=L      LIST FOR OF STAX FOR EXECUTE FORM
*
FLAGS    DS    H
CLEARED  EQU   X'20'
LASTOPR  EQU   X'10'
LASTOPW  EQU   X'08'
*
CURRBUF  DS    F             LOCATION OF CURRENT INPUT BUFFER
INLEN    DS    F             LAST TGET INPUT LENGTH
TESTPFK  DS    X             FOR CHECKING FOR SIMULATED PFK
*
FMTHEAD  DS    CL6
DSECSIZE DS    F
BUFFER   DS    CL2700                                              U011
         ORG   BUFFER                                              U010
BUFFPFX  DS    0XL6                     ENTIRE BUFFER PREFIX       U010
         DS    X                        "ESC": COMMAND FOLLOWS     U010
BUFFOPCD DS    X                        3270 COMMAND CODE          U010
BUFFWCC  DS    X                        3270 WRITE CONTROL CODE    U010
BUFFSPF  DS    XL3                      SPF TCAM PREFIX            U010
BUFFDATA DS    0C                       START OF BUFFER DATA       U010
         ORG   ,                                                   U010
LOCAL    DS    0F
WORKLEN  EQU   *-WORK
         EJECT
         TDCB
         END
