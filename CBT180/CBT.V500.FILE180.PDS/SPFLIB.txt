SPFLIB   TITLE '-----  S P F L I B -- Allocate SPF libraries  -----'
***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1988 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.                                     *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*                                                                     *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1, with DFP 3.1),  *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc-oberon!LDW (last resort)                           *
*                                                                     *
*          (213) 743-5391 (direct -- 11 am to 7 pm Pacific time)      *
*          (213) 743-2957 (msgs)                                      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, or 714.  Sorry.                                       *
*                                                                     *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*   Program name:  SPFLIB                                             *
*                                                                     *
*   Purpose:       Allow users to change the concatenation of their   *
*                  SPF libraries without needing to re-specify (or    *
*                  even know) the system library names.               *
*                                                                     *
*   Author:        Leonard D. Woren                                   *
*                                                                     *
*   Date:          January 17, 1985                                   *
*                                                                     *
*   Updates:                                                          *
*                                                                     *
*      28Nov86  U005 LDW - Change concept:  make the standard oper-   *
*                             ation to keep all existing libraries.   *
*                             This function was previouslky provided  *
*                             via the "ADD" keyword, which is now     *
*                             obsolute, and will produce a warning    *
*                             warning message to that effect.         *
*                                                                     *
*      01/09/86 U004 LDW - Don't attempt re-allocation if there are   *
*                             no datasets in the new list (this will  *
*                             prevent the ABEND when LOAD(*) is       *
*                             specified and there are no system libs) *
*                                                                     *
*      12/09/85 U003 LDW - Change concatenation for ISPTLIB to keep   *
*                             the first library first, due to design  *
*                             stupidity in the way that ISPF does the *
*                             ENQ when updating a table (we always    *
*                             have the same dummy library first in    *
*                             the concatenation in the logon proc)    *
*                        - Change all DDname checks to 8 characters   *
*                                                                     *
*      05/07/85 U002 LDW - Add support for HELP  to re-alloc SYSHELP  *
*                        - Check for duplicate specification of       *
*                             datasets, and use only first            *
*                             occurrence                              *
*                        - Add "ADD" keyword to cause all existing    *
*                             dataset allocations to be retained,     *
*                             not just those on system index levels   *
*                        - Fix bug that was causing all ddnames to    *
*                             be checked for SYSxxx and TSOx, not     *
*                             just SYSPROC                            *
*                        - Add "DEBUG" keyword to bypass actually     *
*                             freeing and reallocating anything       *
*                             ("DEBUG" forces "LIST")                 *
*                                                                     *
*      01/23/85 U001 LDW - Add support for CLIST to re-alloc SYSPROC  *
*                        - Add LIST keyword to list all dsnames info  *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
SPFLIB   OSENTER  PL=(PARSE),EXIT=LEAVE,RC=(R15),                      $
               PARMREG=R5,ENV=(CP,SIMULATE),ADDRREG=R14,LENREG=R15,    $
               GETMAIN=(WORKLEN,WORKD)
SPFLIB   CSECT                          Resume
         LA    R14,ZEROVARS             -> Start of area to clear
         L     R15,=A(ZEROLEN)          Length to clear
         SR    R1,R1                    No "from" length
         MVCL  R14,R0                   Clear the whole thing
         LR    R15,R13                  Copy addr of WORKD
         AH    R15,=H'4096'             Temp second base
         USNGX WORKD+4096,R15
*
         LA    R0,AR@CLIST              -> first CLIST dsn slot    U001
         ST    R0,P@CLIST               Save for later use         U001
         ST    R0,A@CLIST               Save for PARSE valck exit  U001
         MVC   L@CLIST,=CL8'CLIST'      Set lib type for clists    U001
         MVC   D@CLIST,=CL8'SYSPROC'    Set sys DDname for clists  U001
*
         LA    R0,AR@HELP               -> first HELP dsn slot     U002
         ST    R0,P@HELP                Save for later use         U002
         ST    R0,A@HELP                Save for PARSE valck exit  U002
         MVC   L@HELP,=CL8'HELP'        Set lib type for clists    U002
         MVC   D@HELP,=CL8'SYSHELP'     Set sys DDname for clists  U002
*
         LA    R0,AR@LOAD               -> first slot for LOAD dsnames
         ST    R0,P@LOAD                Save for later use
         ST    R0,A@LOAD                Save for PARSE valck exit
         MVC   L@LOAD,=CL8'LOAD'        Set library type for messages
         MVC   D@LOAD,=CL8'ISPLLIB'     Set SPF DDname for messages
*
         LA    R0,AR@MSGS               -> first slot for MSGS dsnames
         ST    R0,P@MSGS                Save for later use
         ST    R0,A@MSGS                Save for PARSE valck exit
         MVC   L@MSGS,=CL8'MSGS'        Set library type for messages
         MVC   D@MSGS,=CL8'ISPMLIB'     Set SPF DDname for messages
*
         LA    R0,AR@PNLS               -> first slot for PNLS dsnames
         ST    R0,P@PANELS              Save for later use
         ST    R0,A@PANELS              Save for PARSE valck exit
         MVC   L@PANELS,=CL8'PANELS'    Set library type for panels
         MVC   D@PANELS,=CL8'ISPPLIB'   Set SPF DDname for panels
*
         LA    R0,AR@SKLS               -> first slot for SKLS dsnames
         ST    R0,P@SKELS               Save for later use
         ST    R0,A@SKELS               Save for PARSE valck exit
         MVC   L@SKELS,=CL8'SKELS'      Set library type for skels
         MVC   D@SKELS,=CL8'ISPSLIB'    Set SPF DDname for skels
*
         LA    R0,AR@TBLS               -> first slot for TBLS dsnames
         ST    R0,P@TABLES              Save for later use
         ST    R0,A@TABLES              Save for PARSE valck exit
         MVC   L@TABLES,=CL8'TABLES'    Set library type for tables
         MVC   D@TABLES,=CL8'ISPTLIB'   Set SPF DDname for tables
*
         DROPX R15                      WORKD+4096
         SPACE 2
         TSPARSE  PCL=(=V(PCL),I),CBUF=(0(,R5),I),USER=(R13)
*??                                     SYNAD=XXX
         SPACE 2
         LTR   R15,R15                  Did PARSE work?
         BZ    PARSEOK                  Yes - continue
         TPUTX 'IKJPARS Couldn''t'
         LA    R15,16                   Set return code
         B     LEAVE                    Get out
         SPACE 3
*---------------------------------------------------------------------*
*  Check LIST/NOLIST/DEBUG/ADD keywords --                            *
*  All other operands have been saved by validity check exits         *
*---------------------------------------------------------------------*
PARSEOK  L     R2,TSPARANS              -> PARSE output area       U001
         USNGX PDL,R2                                              U001
         CLI   P#LIST+1,1               "LIST" requested?          U001
         BNE   *+8                      No - skip                  U001
         OI    FLAGS,F@LIST             Yes - remember             U001
         CLI   P#DEBUG+1,1              "DEBUG" requested?         U002
         BNE   *+8                      No - skip                  U002
         OI    FLAGS,F@DEBUG+F@LIST     Yes - remember             U002
         CLI   P#ADD+1,1                "ADD" requested?           U002
         BNE   *+8                      No - skip                  U002
         OI    FLAGS,F@ADD              Yes - remember             U002
         DROPX R2                       PDL                        U002
*---------------------------------------------------------------------*
*  Free PARSE's storage                                               *
*---------------------------------------------------------------------*
         IKJRLSA  TSPARANS              Free PARSE's storage
         SPACE 3
*---------------------------------------------------------------------*
*  Issue warning message if obsolete keyword was used             U005*
*---------------------------------------------------------------------*
         TM    FLAGS,F@ADD              "ADD" specified?           U005
         BZ    TIOTSCAN                 No - continue              U005
         TPUTX 'SPFLIB - "ADD" is obsolete -- ignored'             U005
         SPACE 3
*---------------------------------------------------------------------*
*  Scan the TIOT and save all the dsnames, volsers, and unit types    *
*  of currently allocated SPF datasets                                *
*---------------------------------------------------------------------*
TIOTSCAN L     R1,PSATNEW-PSA           -> My TCB
         L     R1,TCBTIO-TCB(,R1)       -> My TIOT
         LA    R1,TIOENTRY-TIOT1(,R1)   -> First device entry
         USNGX TIOENTRY,R1
         SR    R0,R0                    Clear for IC
         SPACE 2
TIOT$LP1 CLC   =F'0',0(R1)              End of TIOT?
         BE    GO$ALLOC                 Yes - go start allocating
*
         LA    R2,@@CLIST               -> CLIST DSN list descript U001
         CLC   =C'SYSPROC ',TIOEDDNM    Start of LOAD libraries?   U003
         BE    TIOT$LP2                 Yes - save system ds info  U001
*
         LA    R2,@@HELP                -> HELP DSN list descript  U002
         CLC   =C'SYSHELP ',TIOEDDNM    Start of LOAD libraries?   U003
         BE    TIOT$LP2                 Yes - save system ds info  U002
*
         LA    R2,@@LOAD                -> LOAD DSN list descriptor
         CLC   =C'ISPLLIB ',TIOEDDNM    Start of LOAD libraries?   U003
         BE    TIOT$LP2                 Yes - save system ds info
*
         LA    R2,@@MSGS                -> MSGS DSN list descriptor
         CLC   =C'ISPMLIB ',TIOEDDNM    Start of MSGS libraries?   U003
         BE    TIOT$LP2                 Yes - save system ds info
*
         LA    R2,@@PANELS              -> PANELS DSN list descriptor
         CLC   =C'ISPPLIB ',TIOEDDNM    Start of PANELS libraries? U003
         BE    TIOT$LP2                 Yes - save system ds info
*
         LA    R2,@@SKELS               -> SKELS DSN list descriptor
         CLC   =C'ISPSLIB ',TIOEDDNM    Start of SKELS libraries?  U003
         BE    TIOT$LP2                 Yes - save system ds info
*
         LA    R2,@@TABLES              -> TABLES DSN list descriptor
         CLC   =C'ISPTLIB ',TIOEDDNM    Start of TABLES libraries? U003
         BE    TIOT$LP2                 Yes - save system ds info
*
IGNORE   IC    R0,TIOELNGH              Get length of this TIOT entry
         AR    R1,R0                    -> next TIOT entry
         B     TIOT$LP1                 Scan whole TIOT
         SPACE 3
         USNGX DSDESC,R2
TIOT$LP2 CLC   DS@NBR,=F'0'             Any user specified datasets?
         BE    IGNORE                   No - don't bother reallocating
*                                            this DDname
         SR    R14,R14                  Clear for ICM
         ICM   R14,B'0111',TIOEJFCB     -> JFCB Prefix
         L     R3,DS@NBR                Get number of dsnames      U002
         L     R4,DS@TOP                -> first dsname element    U002
         USNGX DSENTRY,R4                                          U002
         SPACE 1
DUP$LOOP CLC   DSE@DSN,16(R14)          Already have this dsname?  U002
         BE    NEXTTIOT                 Yes - don't add again      U002
         LA    R4,DSE@@LEN(,R4)         -> next array element      U002
         BCT   R3,DUP$LOOP              Keep looking for this dsn  U002
         DROPX R4                       DSENTRY                    U002
*---  This dsname not previously specifed                          ---*
*U005    TM    FLAGS,F@ADD              "ADD" requested?           U002
*U005    BO    SAVE$DSN                 Yes - bypass check for     U002
*                                          system dsnames          U002
         SPACE 2
*U005    CLC   =C'SPF',16(R14)          SPF system dataset?
*U005    BE    SAVE$DSN                 Yes - save dsn info        U001
*U005    CLC   DS@NAME,=CL8'CLIST'      Is this special?           U002
*U005    BE    OK$SYS                   Yes - allow TSOx and SYSx  U002
*U005    CLC   DS@NAME,=CL8'HELP'       Is this special?           U002
*U005    BNE   NEXTTIOT                 No - skip it
*OK$SYS  CLC   =C'TSO',16(R14)          System CLIST library?      U001
*U005    BE    SAVE$DSN                 Yes - save dsn info        U001
*U005    CLC   =C'SYS',16(R14)          System CLIST library?      U001
*U005    BNE   NEXTTIOT                 No - skip it               U001
         SPACE 1
SAVE$DSN L     R3,DS@NEXT               Get addr for next dsname
         USNGX DSENTRY,R3
         L     R4,DS@NBR                Get number of dsnames
         CH    R4,=H'16'                Are we full here?
         BNL   TOOMANY                  Yes - say "no can do"
         MVC   DSE@DSN,16(R14)          Save this dsname
         SR    R14,R14                  Clear for ICM
         ICM   R14,B'0111',TIOEFSRT     -> UCB
         USNGX UCBOB,R14
         MVC   DSE@VOL,UCBVOLI          Save VOLSER
         SR    R15,R15                  Clear for IC
         IC    R15,UCBTBYT4             Get DASD device type code
         SLL   R15,3                    * 8
         LA    R15,DEVTYPES-8(R15)      -> Character form of devtype
         MVC   DSE@UNIT,0(R15)          Save it in dsn table
         LA    R3,DSE@@LEN(,R3)         Bump table pointer
         LA    R4,1(,R4)                Increment number of dsnames
         ST    R3,DS@NEXT               Save updated addr
         ST    R4,DS@NBR                Save updated number
         DROPX R14,R3                   UCBOB, DSENTRY
         SPACE 2
NEXTTIOT IC    R0,TIOELNGH              Get length of this TIOT entry
         AR    R1,R0                    -> next TIOT entry
         CLC   =F'0',0(R1)              End of TIOT?
         BE    GO$ALLOC                 Yes - go start allocating
         CLC   TIOEDDNM,BLANKS          Concatenation?
         BE    TIOT$LP2                 Yes - continue current DDname
         B     TIOT$LP1                 No - go check for new DDname
         DROPX R1                       TIOENTRY
         SPACE 2
TOOMANY  MVC   BUFFER,BLANKS
         MVC   BUFFER(75),=C'*** Maximum of 16 dsnames exceeded for lib$
               rary xxxxxxxx DDname xxxxxxxx ***'
         MVC   BUFFER+47(8),DS@NAME
         MVC   BUFFER+63(8),DS@DDN
         TPUTX BUFFER,75
         ABEND 1,DUMP
         DROPX R2                       DSDESC
         SPACE 3
*---------------------------------------------------------------------*
*  Initialize SVC 99 Request Block and Text Units                     *
*---------------------------------------------------------------------*
GO$ALLOC LA    R1,$99RBLEN              -> Request block
         ST    R1,$99RBPTR
         MVI   $99RBPTR,S99RBPND        Set end of list indicator
         MVI   $99RBLEN,20              Set length of Request Block
         SPACE 2
         LA    R14,T1#DSN
         LA    R15,T1#SHR
         LA    R0,T1#PERM
         LA    R1,T1#DDN
         LA    R2,T1#VOL
         LA    R3,T1#UNIT
         STM   R14,R3,TEXTLST1          Set Text Unit pointers
         OI    TEXTLST1+5*4,X'80'       Set end of list indicator
         MVC   T1#DSN(6),=Y(DALDSNAM,1,44)
         MVC   T1#SHR(6),=Y(DALSTATS,1,1)
         MVC   T1#PERM(4),=Y(DALPERMA,0)
         MVI   T1#SHR+6,X'08'           SHR
         MVC   T1#VOL(6),=Y(DALVLSER,1,6)
         MVC   T1#UNIT(6),=Y(DALUNIT,1,8)
         SPACE 2
         LA    R14,T2#DDN
         LA    R15,T2#PERM
         STM   R14,R15,TEXTLST2         Set Text Unit pointers
         OI    TEXTLST2+4*1,X'80'       Set end of list indicator
         MVC   T2#DDN(2),=Y(DCCDDNAM)
         MVC   T2#PERM(4),=Y(DCCPERMC,0)
         SPACE 2
         LA    R14,T3#DDN
         LA    R15,T3#UNALC
         STM   R14,R15,TEXTLST3         Set Text Unit pointer
         OI    TEXTLST3+4*1,X'80'       Set end of list indicator
         MVC   T3#DDN(6),=Y(DUNDDNAM,1,8)
         MVC   T3#UNALC(4),=Y(DUNUNALC,0)
         SPACE 3
         TM    FLAGS,F@LIST             Want list of dsnames?      U001
         BNO   NO$LIST                  No - skip                  U001
         SPACE 1                                                   U001
         LA    R1,=C'--- SYSPROC INFO ---'                         U001
         L     R2,P@CLIST                                          U001
         L     R3,N@CLIST                                          U001
         BAL   LINK,TEMP$PRT                                       U001
         SPACE 1
         LA    R1,=C'--- SYSHELP INFO ---'                         U002
         L     R2,P@HELP                                           U002
         L     R3,N@HELP                                           U002
         BAL   LINK,TEMP$PRT                                       U002
         SPACE 1
         LA    R1,=C'--- ISPLLIB INFO ---'                         U001
         L     R2,P@LOAD
         L     R3,N@LOAD
         BAL   LINK,TEMP$PRT
         SPACE 1
         LA    R1,=C'--- ISPMLIB INFO ---'                         U001
         L     R2,P@MSGS
         L     R3,N@MSGS
         BAL   LINK,TEMP$PRT
         SPACE 1
         LA    R1,=C'--- ISPPLIB INFO ---'                         U001
         L     R2,P@PANELS
         L     R3,N@PANELS
         BAL   LINK,TEMP$PRT
         SPACE 1
         LA    R1,=C'--- ISPSLIB INFO ---'                         U001
         L     R2,P@SKELS
         L     R3,N@SKELS
         BAL   LINK,TEMP$PRT
         SPACE 1
         LA    R1,=C'--- ISPTLIB INFO ---'                         U001
         L     R2,P@TABLES
         L     R3,N@TABLES
         BAL   LINK,TEMP$PRT
         B     NO$LIST                                             U001
         SPACE 2
TEMP$PRT LTR   R3,R3
         BZR   LINK
         TPUTX (1),20                   Put out the header line    U001
TEMP$PRL TPUTX (R2),DSE@@LEN
         LA    R2,DSE@@LEN(,R2)
         BCT   R3,TEMP$PRL
         BR    LINK
         SPACE 3
*---------------------------------------------------------------------*
*  Free and rebuild each SPF library concatenation                    *
*---------------------------------------------------------------------*
NO$LIST  TM    FLAGS,F@DEBUG            In debug mode?             U002
         BO    LEAVE                    Yes - skip all FREE/ALLOC  U002
         LA    R2,@@CLIST               -> First array descriptor  U001
         LA    R3,7                     Nbr of arrays to process   U002
         USNGX DSDESC,R2
         SPACE 3
*---  Free the current allocation, if this operand specified       ---*
MAINLOOP CLC   DS@NBR,=F'0'             Anything here?
         BE    CONCATOK                 No - skip to next DDN
         MVC   T3#DDN+6(8),DS@DDN       Get DDName to FREE
         LA    R0,TEXTLST3              -> Text Unit address list
         ST    R0,$99TXTPP              Set in Request Block
         XC    $99FLAG1,$99FLAG1
         XC    $99ERROR(L'$99ERROR+L'$99INFO),$99ERROR
         MVI   $99VERB,S99VRBUN         Indicate "UNALLOCATION"
         LA    R1,$99RBPTR
         SVC   99                       Free the DDname
         LTR   R15,R15                  OK?
         BZ    FREE$OK                  Yes - continue
         CLC   =X'0438',$99ERROR        DDname not allocated?
         BNE   FREE$ERR                 No - print error message
         CLC   =C'ISPLLIB ',T3#DDN+6    Special case?              U003
         BE    FREE$OK                  Yes - continue
FREE$ERR MVC   BUFFER,BLANKS            Clear message buffer
         MVC   BUFFER(35),=C'*** De-allocation failed for DDname'
         MVC   BUFFER+36(8),T3#DDN+6    Fill in DDname
         LA    R2,BUFFER+45             -> Area for return codes
         BAL   LINK,DYN$ERR             Format R15, Error & info
         CLC   =X'0420',$99ERROR        Not freed because ds is open?
         BNE   FREE$AB                  Don't say anything more
         TPUTX '*** Dataset is open -- SPFLIB command cannot be execute$
               d from within SPF   ***'
FREE$AB  TPUTX '*******************************************************$
               ***********************'
         ABEND 2,DUMP
         SPACE 3
*---  Allocate each dataset and save system generated DDname       ---*
FREE$OK  LA    R4,T2#DDN+4              -> First DDname slot for CONCAT
         L     R5,DS@NBR                Get number of dsnames
         L     R6,DS@TOP                -> First dsname
         SR    R7,R7                    Init number of DDnames
         USNGX DSENTRY,R6
         CH    R5,=H'1'                 Anything to ALLOC?         U004
         BH    ALLOC$LP                 Yes - do it                U004
         CLC   =C'* ',DSE@DSN           Is this "RESET" function?  U004
         BNE   ALLOC$LP                 No - go do alloc           U004
         MVC   BUFFER,BLANKS            ... nothing to alloc...    U004
         MVC   BUFFER(8),DS@DDN         DDname                     U004
         MVC   BUFFER+9(66),=C' not re-allocated -- no system libraries$
                were previously allocated'                         U004
         TPUTX BUFFER,75                                           U004
         B     CONCATOK                 And pretend we did it      U004
         SPACE 2
ALLOC$LP CLC   =C'* ',DSE@DSN           Is this "RESET" function?
         BE    SKIPDSN                  Yes - don't alloc anything yet
         LA    R7,1(,R7)                Increment DDname number
         MVC   T1#DSN+6(44),DSE@DSN     Get DSNAME to ALLOC
         OI    TEXTLST1+3*4,X'80'       Assume no VOL and UNIT
         CLC   DSE@VOL,BLANKS           Any?
         BE    NO$VOL                   No - skip
         MVC   T1#VOL+6(6),DSE@VOL      VOLSER to SVC99 parmlist
         MVC   T1#UNIT+6(8),DSE@UNIT    UNIT to SVC99 parmlist
         NI    TEXTLST1+3*4,X'FF'-X'80' And include their text units
NO$VOL   LA    R0,TEXTLST1              -> Text Unit address list
         ST    R0,$99TXTPP              Set in Request Block
         XC    $99FLAG1,$99FLAG1
         XC    $99ERROR(L'$99ERROR+L'$99INFO),$99ERROR
         MVI   $99VERB,S99VRBAL         Indicate ALLOC by DSNAME
         MVC   T1#DDN(6),=Y(DALRTDDN,1,8)  Assume not first in concat
         CH    R7,=H'1'                 Is this first in this concat?
         BNE   NOTFIRST                 No - OK as is
         MVC   T1#DDN(2),=Y(DALDDNAM)   Yes - we will supply the DDname
         MVC   T1#DDN+6(8),DS@DDN       Fill it in
         SPACE 1
NOTFIRST LA    R1,$99RBPTR
         SVC   99                       Alloc the input dataset
         LTR   R15,R15                  OK?
         BZ    ALLOC$OK                 Yes
         CLC   =X'1708',$99ERROR        Dataset not found?
         BE    DSNOTFND                 Yes - print better msg
         SPACE 1
         MVC   BUFFER,BLANKS            Clear message buffer
         MVC   BUFFER(32),=C'*** Allocation failed for DSName'
         MVC   BUFFER+33(44),T1#DSN+6   Fill in DSName
         LA    R2,BUFFER+33             -> Start of dsname
         CLI   0(R2),C' '  <====+       Find end yet?
         LA    R2,1(,R2)        |       -> Next char of dsname
         BNE   *-8     >========+       No - keep looking
         BAL   LINK,DYN$ERR             Format R15, Error & info
         TPUTX '*******************************************************$
               ***********************'
         ABEND 3,DUMP
         SPACE 2
DSNOTFND TPUTX '*******************************************************$
               ***********************'
         MVC   BUFFER(24),=C'*** Dataset not found:  '
         MVC   BUFFER+24(44),T1#DSN+6
         TPUTX BUFFER,24+44
         TPUTX '*******************************************************$
               ***********************'
         ABEND 4
         SPACE 3
ALLOC$OK MVC   0(2,R4),=H'8'            Set length of this DDname
         MVC   2(8,R4),T1#DDN+6         Move in this DDname
         LA    R4,10(,R4)               Increment to next DDname slot
         SPACE 2
SKIPDSN  LA    R6,DSE@@LEN(,R6)         Increment to next DSNAME slot
         BCT   R5,ALLOC$LP              Alloc all dsnames for this lib
         SPACE 1
         DROPX R6                       DSENTRY
         SPACE 3
*---  Concatenate all the DDnames                                  ---*
         CH    R7,=H'1'                 More than 1 to CONCAT?
         BE    CONCATOK                 No - so skip the CONCAT
         STH   R7,T2#DDN+2              Save # of DDnames for CONCAT
         LA    R0,TEXTLST2              -> Text Unit address list
         ST    R0,$99TXTPP              Set in Request Block
         XC    $99FLAG1,$99FLAG1
         XC    $99ERROR(L'$99ERROR+L'$99INFO),$99ERROR
         MVI   $99VERB,S99VRBCC         Indicate CONCATENATION
         LA    R1,$99RBPTR
         SVC   99                       Concat all datasets
         LTR   R15,R15                  OK?
         BZ    CONCATOK                 Yes
         SPACE 1
         MVC   BUFFER,BLANKS            Clear message buffer
         MVC   BUFFER(35),=C'*** Concatenation failed for DDname'
         MVC   BUFFER+36(8),DS@DDN      Fill in DDname
         LA    R2,BUFFER+45             -> Area for return codes
         BAL   LINK,DYN$ERR             Format R15, Error & info
         TPUTX '*******************************************************$
               ***********************'
         ABEND 5,DUMP
         SPACE 2
CONCATOK LA    R2,DS@@LEN(,R2)          -> Next Library Descriptor
         BCT   R3,MAINLOOP              Process all 5 libraries
         B     LEAVE                    All done!
         SPACE 2
         DROPX R2                       DSDESC
         SPACE 3
DYN$ERR  TPUTX '*******************************************************$
               ***********************'
         MVC   0(33,R2),=C'(R15=XXXX,ERR=XXXX,INFO=XXXX) ***'
         ST    R15,TEMP1                Set down SVC99's return code
         HEX   (5,R2),TEMP1+2,LEN=2,BYTE=C','  FORMAT IT INTO MESSAGE
         HEX   (14,R2),$99ERROR,LEN=2,BYTE=C','
         HEX   (24,R2),$99INFO,LEN=2,BYTE=C')'
         TPUTX BUFFER,L'BUFFER          Display the message
         BR    LINK                     Return to caller
         SPACE 3
         DROPX ,
         EJECT
*---------------------------------------------------------------------*
*  PARSE validify check exit to save a dsname for "CLIST()"       U001*
*---------------------------------------------------------------------*
VAL#C    STM   R14,R12,12(R13)          Save PARSE'S registers
         LA    R15,VAL#DSN-VAL#C(,R15)  Adjust base for common routine
         LA    R2,@@CLIST-WORKD         Offset of CLIST dsn descriptor
         BR    R15                      Go to common routine
         SPACE 2
*---------------------------------------------------------------------*
*  PARSE validify check exit to save a dsname for "HELP()"        U002*
*---------------------------------------------------------------------*
VAL#H    STM   R14,R12,12(R13)          Save PARSE'S registers
         LA    R15,VAL#DSN-VAL#H(,R15)  Adjust base for common routine
         LA    R2,@@HELP-WORKD          Offset of HELP dsn descriptor
         BR    R15                      Go to common routine
         SPACE 2
*---------------------------------------------------------------------*
*  PARSE validify check exit to save a dsname for "LOAD()"            *
*---------------------------------------------------------------------*
VAL#L    STM   R14,R12,12(R13)          Save PARSE'S registers
         LA    R15,VAL#DSN-VAL#L(,R15)  Adjust base for common routine
         LA    R2,@@LOAD-WORKD          Offset of LOAD dsn descriptor
         BR    R15                      Go to common routine
         SPACE 2
*---------------------------------------------------------------------*
*  PARSE validify check exit to save a dsname for "MSGS()"            *
*---------------------------------------------------------------------*
VAL#M    STM   R14,R12,12(R13)          Save PARSE'S registers
         LA    R15,VAL#DSN-VAL#M(,R15)  Adjust base for common routine
         LA    R2,@@MSGS-WORKD          Offset of MSGS dsn descriptor
         BR    R15                      Go to common routine
         SPACE 2
*---------------------------------------------------------------------*
*  PARSE validify check exit to save a dsname for "PANELS()"          *
*---------------------------------------------------------------------*
VAL#P    STM   R14,R12,12(R13)          Save PARSE'S registers
         LA    R15,VAL#DSN-VAL#P(,R15)  Adjust base for common routine
         LA    R2,@@PANELS-WORKD        Offset of PANELS dsn descriptor
         BR    R15                      Go to common routine
         SPACE 2
*---------------------------------------------------------------------*
*  PARSE validify check exit to save a dsname for "SKELS()"           *
*---------------------------------------------------------------------*
VAL#S    STM   R14,R12,12(R13)          Save PARSE'S registers
         LA    R15,VAL#DSN-VAL#S(,R15)  Adjust base for common routine
         LA    R2,@@SKELS-WORKD         Offset of SKELS dsn descriptor
         BR    R15                      Go to common routine
         SPACE 2
*---------------------------------------------------------------------*
*  PARSE validify check exit to save a dsname for "TABLES()"          *
*---------------------------------------------------------------------*
VAL#T    STM   R14,R12,12(R13)          Save PARSE'S registers
         LR    R12,R15                  Get a base register        U003
         USNGX VAL#T,R12                                           U003
*---  If this is the first table specification, find and save the  ---*
*---  current first library in the TIOT, so that it remains first  ---*
         L     R11,4(,R1)               Get addr of WORKD          U003
         USNGX WORKD,R11                                           U003
         LA    R2,@@TABLES              -> ISPTLIB descriptor      U003
         USNGX DSDESC,R2                                           U003
         CLC   DS@NBR,=F'0'             Any here yet?              U003
         BNE   VAL#T$OK                 Yes - continue normally    U003
         L     R14,DS@NEXT              -> Next dsname slot        U003
         USNGX DSENTRY,R14                                         U003
         MVC   0(DSE@@LEN,R14),BLANKS   Clear it                   U003
         L     R1,PSATNEW-PSA           -> My TCB                  U003
         L     R1,TCBTIO-TCB(,R1)       -> My TIOT                 U003
         LA    R1,TIOENTRY-TIOT1(,R1)   -> First device entry      U003
         USNGX TIOENTRY,R1                                         U003
         SR    R0,R0                    Clear for IC               U003
         SPACE 2
VAL#T$LP CLC   =F'0',0(R1)              End of TIOT?               U003
         BE    VAL#T$OK                 Yes - should never occur   U003
         CLC   =C'ISPTLIB ',TIOEDDNM    Tables library?            U003
         BE    VAL#T$SV                 Yes - save first ds info   U003
         IC    R0,TIOELNGH              Get len of this TIOT entry U003
         AR    R1,R0                    -> next TIOT entry         U003
         B     VAL#T$LP                 Scan whole TIOT            U003
         SPACE 1
VAL#T$SV SR    R15,R15                  Clear for ICM              U003
         ICM   R15,B'0111',TIOEJFCB     -> JFCB Prefix             U003
         MVC   0(44,R14),16(R15)        Save the dsname            U003
         SR    R4,R4                    Clear for ICM              U003
         ICM   R4,B'0111',TIOEFSRT      -> UCB                     U003
         DROPX R1                       TIOENTRY                   U003
         USNGX UCBOB,R4                                            U003
         MVC   DSE@VOL,UCBVOLI          Save VOLSER                U003
         SR    R1,R1                    Clear for IC               U003
         IC    R1,UCBTBYT4              Get DASD device type code  U003
         SLL   R1,3                     * 8                        U003
         LA    R1,DEVTYPES-8(R1)        -> Char form of devtype    U003
         MVC   DSE@UNIT,0(R1)           Save it in dsn table       U003
         DROPX R4                       UCBOB                      U003
         MVC   DS@NBR,=F'1'             Set count for this one     U003
         LA    R14,DSE@@LEN(,R14)       -> Next dsname slot        U003
         ST    R14,DS@NEXT              Store updated pointer      U003
         DROPX R14,R2                   DSENTRY, DSDESC            U003
         SPACE 1
VAL#T$OK LM    R14,R12,12(R13)          Restore entry registers    U003
*---  And now the standard code for these routines                 ---*
         LA    R15,VAL#DSN-VAL#T(,R15)  Adjust base for common routine
         LA    R2,@@TABLES-WORKD        Offset of TABLES dsn descriptor
         BR    R15                      Go to common routine
         SPACE 1
         DROPX R12,R11                  VAL#T, WORKD               U003
         SPACE 3
*---------------------------------------------------------------------*
*  PARSE validify check exit -- common code                           *
*---------------------------------------------------------------------*
VAL#DSN  LR    R12,R15                  Get my base
         USNGX VAL#DSN,R12
         LM    R10,R11,0(R1)            Get addr of PDE & WORKD
         USNGX DSNPDE,R10
         USNGX WORKD,R11
         AR    R2,R11                   Relocate dsname descriptor ptr
         USNGX DSDESC,R2
         L     R15,DS@NBR               Get current count
         LA    R15,1(,R15)              Increment
         ST    R15,DS@NBR               Store updated count
         L     R14,DS@NEXT              -> Next dsname slot
         USNGX DSENTRY,R14
         MVC   0(DSE@@LEN,R14),BLANKS   Clear it
         LH    R1,PDEDSNL               Get length of dsn
         BCTR  R1,0                     -1 for EX
         L     R15,PDEDSNA              -> dsn
         EX    R1,MVC#DSN               MVC 0(*-*,R14),0(R15)
         LA    R14,DSE@@LEN(,R14)       -> Next dsname slot
         ST    R14,DS@NEXT              Store updated pointer
         LM    R14,R12,12(R13)          Restore caller's registers
         SR    R15,R15                  Indicate "Parameter is valid"
         BR    R14                      Return to PARSE
         DROPX R2                       DSDESC
MVC#DSN  MVC   DSE@DSN(*-*),0(R15)      << Executed >>
         DROPX R12,R11,R10,R14          VAL#DSN, WORKD, DSNPDE, DSENTRY
         SPACE 3
         LTORG
         EJECT
WORKD    DSECT                          RESUME
TEMP1    DS    F                        For hex convert
ZEROVARS EQU   *                        Start of area to be cleared
         SPACE 2
DSDESC   DSECT                          Format of DSN array descriptors
DS@TOP   DS    A                        -> First dsn
DS@NEXT  DS    A                        -> Next available dsn slot
DS@NBR   DS    F                        Current number of dataset names
DS@NAME  DS    CL8                      External id of this array
DS@DDN   DS    CL8                      SPF DDname for this array
DS@@LEN  EQU   *-DSDESC                 Length of 1 array descriptor
         SPACE 2
WORKD    DSECT                          RESUME
*
@@CLIST  DS    0A                                                  U001
P@CLIST  DS    A                        -> First CLIST dsname slot U001
A@CLIST  DS    A                        -> Next CLIST dsname slot  U001
N@CLIST  DS    F                        Current nbr of CLIST dsns  U001
L@CLIST  DS    CL8                      'CLIST'                    U001
D@CLIST  DS    CL8                      'SYSPROC'                  U001
*
@@HELP   DS    0A                                                  U002
P@HELP   DS    A                        -> First HELP dsname slot  U002
A@HELP   DS    A                        -> Next HELP dsname slot   U002
N@HELP   DS    F                        Current nbr of HELP dsns   U002
L@HELP   DS    CL8                      'HELP'                     U002
D@HELP   DS    CL8                      'SYSHELP'                  U002
*
@@LOAD   DS    0A
P@LOAD   DS    A                        -> First LOAD dsname slot
A@LOAD   DS    A                        -> Next LOAD dsname slot
N@LOAD   DS    F                        Current number of LOAD dsns
L@LOAD   DS    CL8                      'LOAD'
D@LOAD   DS    CL8                      'ISPLLIB'
*
@@MSGS   DS    0A
P@MSGS   DS    A                        -> First MSGS dsname slot
A@MSGS   DS    A                        -> Next MSGS dsname slot
N@MSGS   DS    F                        Current number of MSGS dsns
L@MSGS   DS    CL8                      'MSGS'
D@MSGS   DS    CL8                      'ISPMLIB'
*
@@PANELS DS    0A
P@PANELS DS    A                        -> First PANELS dsname slot
A@PANELS DS    A                        -> Next PANELS dsname slot
N@PANELS DS    F                        Current number of PANELS dsns
L@PANELS DS    CL8                      'PANELS'
D@PANELS DS    CL8                      'ISPPLIB'
*
@@SKELS  DS    0A
P@SKELS  DS    A                        -> First SKELS dsname slot
A@SKELS  DS    A                        -> Next SKELS dsname slot
N@SKELS  DS    F                        Current number of SKELS dsns
L@SKELS  DS    CL8                      'SKELS'
D@SKELS  DS    CL8                      'ISPSLIB'
*
@@TABLES DS    0A
P@TABLES DS    A                        -> First TABLES dsname slot
A@TABLES DS    A                        -> Next TABLES dsname slot
N@TABLES DS    F                        Current number of TABLES dsns
L@TABLES DS    CL8                      'TABLES'
D@TABLES DS    CL8                      'ISPTLIB'
*
FLAGS    DS    B                                                   U001
F@LIST    EQU   B'10000000'             "LIST" option requested    U001
F@DEBUG   EQU   B'01000000'             "DEBUG" option requested   U002
F@ADD     EQU   B'00100000'             "ADD" option requested     U002
*
BUFFER   DS    CL120
         SPACE 3
*---------------------------------------------------------------------*
*  Parmlist for SVC 99                                                *
*---------------------------------------------------------------------*
         DC    0A(0)                    Alignment
$99RBPTR DC    X'80',AL3($99RBLEN)
         SPACE 1
$99RBLEN DC    AL1(20)                  Length
$99VERB  DC    AL1(*-*)                 Verb code
$99FLAG1 DC    X'0000'
$99ERROR DC    H'0'                     Error reason code
$99INFO  DC    H'0'                     Information reason code
$99TXTPP DC    A(*-*)                   -> Text units
         DC    A(0),F'0'                Reserved;  FLAGS2
         SPACE 2
*---------------------------------------------------------------------*
*  Text elements to ALLOC input dataset                               *
*---------------------------------------------------------------------*
TEXTLST1 DC    A(T1#DSN,T1#SHR,T1#PERM,T1#DDN,T1#VOL,T1#UNIT)
         SPACE 1
T1#DSN   DC    Y(DALDSNAM,1,44),CL44' ' DSNAME
T1#SHR   DC    Y(DALSTATS,1,1),X'08'    DISP=SHR
T1#PERM  DC    Y(DALPERMA,0)            Permanently allocated
T1#DDN   DC    Y(DALRTDDN,1,8),CL8' '   Return DDname
T1#VOL   DC    Y(DALVLSER,1,6),CL6' '   VOLSER
T1#UNIT  DC    Y(DALUNIT,1,8),CL8' '    UNIT type
         SPACE 2
*---------------------------------------------------------------------*
*  Text elements for CONCATENATION function                           *
*---------------------------------------------------------------------*
TEXTLST2 DC    A(T2#DDN,T2#PERM)
         SPACE 1
T2#DDN   DC    Y(DCCDDNAM,*-*),16XL(2+8)'00'  DDnames
T2#PERM  DC    Y(DCCPERMC,0)            Permanently concatenated
         SPACE 2
*---------------------------------------------------------------------*
*  Text elements for UNALLOCATION                                     *
*---------------------------------------------------------------------*
TEXTLST3 DC    A(T3#DDN,T3#UNALC)
         SPACE 1
T3#DDN   DC    Y(DUNDDNAM,1,8),CL8' '   DDname
T3#UNALC DC    Y(DUNUNALC,0)            Unalloc even if PERM alloc
         SPACE 3
*---------------------------------------------------------------------*
*  DSNAME arrays for each DDname                                      *
*---------------------------------------------------------------------*
         SPACE 2
DSENTRY  DSECT
DSE@DSN  DS    CL44                     Dsname
DSE@VOL  DS    CL6                      Volser or blank
DSE@UNIT DS    CL8                      Unit or blank
DSE@@LEN EQU   *-DSENTRY                Length of one entry
         SPACE 2
WORKD    DSECT                          Resume
*---  DSNAME/VOLUME/UNIT Arrays                                    ---*
AR@CLIST DS    16XL(DSE@@LEN)                                      U001
AR@HELP  DS    16XL(DSE@@LEN)                                      U002
AR@LOAD  DS    16XL(DSE@@LEN)
AR@MSGS  DS    16XL(DSE@@LEN)
AR@PNLS  DS    16XL(DSE@@LEN)
AR@SKLS  DS    16XL(DSE@@LEN)
AR@TBLS  DS    16XL(DSE@@LEN)
         SPACE 1
ZEROLEN  EQU   *-ZEROVARS               Length to clear
         SPACE 1
WORKLEN  EQU   *-WORKD                  For OSENTER macro
         SPACE 3
SPFLIB   CSECT                          Resume
         EJECT
HEXTAB   HEXTAB  DUAL=NO
BLANKS   DC    CL(L'BUFFER)' '
DEVTYPES DC    C'2311    2301    2303    2302    2321    2305-1  2305-2$
                 2314    3330    3340    3350    3375    3330-1  3380  $
                 ??0F    '
         SPACE 1
LINK     EQU   R10                      Primary linkage register
         EJECT
***********************************************************************
*                                                                     *
*   SYNTAX:                                                           *
*                                                                     *
*      SPFLIB LOAD(mdsn1 mdsn2 ...) MSGS(pdsn1 pdsn2 ...)             *
*                                                                     *
*           PANELS(sdsn1 sdsn2 ...) SKELS(tdsn1 tdsn2 ...)            *
*                                                                     *
*           TABLES(ldsn1 ldsn2 ...) CLIST(cdsn1 cdsn2 ...)            *
*                                                                     *
*           HELP(hdsn1 hdsn2 ...)                                     *
*                                                                     *
*           LIST/NOLIST/DEBUG                                         *
*                                                                     *
***********************************************************************
         SPACE 3
         PRINT NOGEN                    Save 10 pages
         SPACE 2
PCL      IKJPARM  DSECT=PDL
*---------------------------------------------------------------------*
P#LIST   IKJKEYWD
*---------------------------------------------------------------------*
         IKJNAME  'LIST'                                           U001
         IKJNAME  'NOLIST'                                         U001
*---------------------------------------------------------------------*
P#DEBUG  IKJKEYWD
*---------------------------------------------------------------------*
         IKJNAME  'DEBUG'                                          U002
*---------------------------------------------------------------------*
P#ADD    IKJKEYWD
*---------------------------------------------------------------------*
         IKJNAME  'ADD'                                            U002
*---------------------------------------------------------------------*
P#CLIST  IKJKEYWD
*---------------------------------------------------------------------*
         IKJNAME  'CLIST',SUBFLD=S#CLIST                           U001
*---------------------------------------------------------------------*
P#HELP   IKJKEYWD
*---------------------------------------------------------------------*
         IKJNAME  'HELP',SUBFLD=S#HELP                             U002
*---------------------------------------------------------------------*
P#LOAD   IKJKEYWD
*---------------------------------------------------------------------*
         IKJNAME  'LOAD',SUBFLD=S#LOAD
*---------------------------------------------------------------------*
P#MSGS   IKJKEYWD
*---------------------------------------------------------------------*
         IKJNAME  'MSGS',SUBFLD=S#MSGS
*---------------------------------------------------------------------*
P#PANELS IKJKEYWD
*---------------------------------------------------------------------*
         IKJNAME  'PANELS',SUBFLD=S#PANELS
*---------------------------------------------------------------------*
P#SKELS  IKJKEYWD
*---------------------------------------------------------------------*
         IKJNAME  'SKELS',SUBFLD=S#SKELS
*---------------------------------------------------------------------*
P#TABLES IKJKEYWD
*---------------------------------------------------------------------*
         IKJNAME  'TABLES',SUBFLD=S#TABLES
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
S#CLIST  IKJSUBF  ,                                                U001
*---------------------------------------------------------------------*
I#CLIST  IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C,                     U001$
               PROMPT='Dataset name',                              U001$
               HELP='Name(s) of CLIST libraries'                   U001
*---------------------------------------------------------------------*
I#CLST02 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
I#CLST03 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
I#CLST04 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
I#CLST05 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
I#CLST06 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
I#CLST07 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
I#CLST08 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
I#CLST09 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
I#CLST10 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
I#CLST11 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
I#CLST12 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
I#CLST13 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
I#CLST14 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
I#CLST15 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#C                      U001
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
S#HELP   IKJSUBF  ,                                                U002
*---------------------------------------------------------------------*
I#HELP   IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H,                     U002$
               PROMPT='Dataset name',                              U002$
               HELP='Name(s) of HELP libraries'                    U002
*---------------------------------------------------------------------*
I#HELP02 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
I#HELP03 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
I#HELP04 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
I#HELP05 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
I#HELP06 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
I#HELP07 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
I#HELP08 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
I#HELP09 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
I#HELP10 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
I#HELP11 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
I#HELP12 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
I#HELP13 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
I#HELP14 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
I#HELP15 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#H                      U002
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
S#LOAD   IKJSUBF
*---------------------------------------------------------------------*
I#LOAD   IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L,                         $
               PROMPT='Dataset name',                                  $
               HELP='Name(s) of SPF LOAD libraries'
*---------------------------------------------------------------------*
I#LOAD02 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
I#LOAD03 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
I#LOAD04 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
I#LOAD05 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
I#LOAD06 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
I#LOAD07 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
I#LOAD08 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
I#LOAD09 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
I#LOAD10 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
I#LOAD11 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
I#LOAD12 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
I#LOAD13 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
I#LOAD14 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
I#LOAD15 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#L
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
S#MSGS   IKJSUBF
*---------------------------------------------------------------------*
I#MSGS   IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M,                         $
               PROMPT='Dataset name',                                  $
               HELP='Name(s) of SPF MSGS libraries'
*---------------------------------------------------------------------*
I#MSGS02 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
I#MSGS03 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
I#MSGS04 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
I#MSGS05 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
I#MSGS06 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
I#MSGS07 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
I#MSGS08 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
I#MSGS09 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
I#MSGS10 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
I#MSGS11 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
I#MSGS12 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
I#MSGS13 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
I#MSGS14 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
I#MSGS15 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#M
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
S#PANELS IKJSUBF
*---------------------------------------------------------------------*
I#PANELS IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P,                         $
               PROMPT='Dataset name',                                  $
               HELP='Name(s) of SPF PANELS libraries'
*---------------------------------------------------------------------*
I#PNLS02 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
I#PNLS03 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
I#PNLS04 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
I#PNLS05 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
I#PNLS06 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
I#PNLS07 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
I#PNLS08 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
I#PNLS09 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
I#PNLS10 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
I#PNLS11 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
I#PNLS12 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
I#PNLS13 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
I#PNLS14 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
I#PNLS15 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#P
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
S#SKELS  IKJSUBF
*---------------------------------------------------------------------*
I#SKELS  IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S,                         $
               PROMPT='Dataset name',                                  $
               HELP='Name(s) of SPF SKELS libraries'
*---------------------------------------------------------------------*
I#SKLS02 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
I#SKLS03 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
I#SKLS04 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
I#SKLS05 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
I#SKLS06 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
I#SKLS07 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
I#SKLS08 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
I#SKLS09 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
I#SKLS10 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
I#SKLS11 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
I#SKLS12 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
I#SKLS13 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
I#SKLS14 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
I#SKLS15 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#S
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
S#TABLES IKJSUBF
*---------------------------------------------------------------------*
I#TABLES IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T,                         $
               PROMPT='Dataset name',                                  $
               HELP='Name(s) of SPF TABLES libraries'
*---------------------------------------------------------------------*
I#TBLS02 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
I#TBLS03 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
I#TBLS04 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
I#TBLS05 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
I#TBLS06 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
I#TBLS07 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
I#TBLS08 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
I#TBLS09 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
I#TBLS10 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
I#TBLS11 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
I#TBLS12 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
I#TBLS13 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
I#TBLS14 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
I#TBLS15 IKJPOSIT  DSTHING,USID,VALIDCK=VAL#T
*---------------------------------------------------------------------*
*---------------------------------------------------------------------*
         IKJENDP
*---------------------------------------------------------------------*
         EJECT ,
         IEFZB4D0 ,
         EJECT ,
         IEFZB4D2 ,
         SPACE 2
         IKJCVT  ,
         SPACE 2
DSNPDE   DSECT
PDEDSNA  DS    A                        -> dsn
PDEDSNL  DS    H                        L'dsn
PDEFLG1  DS    X
PDEF1PRS  EQU   X'80'                   dsn present
         DS    X                        reserved
         SPACE 1
PDEMBRA  DS    A                        -> member name
PDEMBRL  DS    H                        L'member name
PDEFLG2  DS    X
PDEF2PRS  EQU   X'80'                   member name present
         DS    X                        reserved
         SPACE 1
PDEPSWDA DS    A                        -> password
PDEPSWDL DS    H                        L'password
PDEFLG3  DS    X
PDEF3PRS  EQU   X'80'                   password present
         DS    X                        reserved
         SPACE 2
         PRINT NOGEN
         IHAPSA
         IKJTCB
TIOT     DSECT
         IEFTIOT1
UCB      DSECT
         IEFUCBOB
         SPACE 2
         END
