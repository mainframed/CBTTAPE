***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1980 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.  (Please see reason mentioned       *
*        below.)                                                      *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.  (Please see reason        *
*        mentioned below.)                                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1.3, w/DFP 3.2),   *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc!ldw                                                *
*                                                                     *
*          (213) 740-2875 (direct -- 11 am to 7 pm Pacific time)      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, 714, or 310.  Sorry.                                  *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
TERMIO3  TITLE 'TERMIO3 -- 3270 TO USER INTERFACE ROUTINE BTAM LOCAL'
         DCMSREGS
TERMIO3  CSECT
*        SAVE  (14,12),,*
*        CONTROL RECEIVED FROM   T E R M I O #  -
*        AN OPEN-ENDED CSECT WHOSE ONLY FUNCTION IS TO
*        DETECT WHICH SERVICE I/O ROUTINE TO USE -
*        BY DETERMINING WHETHER OR NOT THIS JOBSTEP
*        IS   T S O    OR   D C M S  "START"
*
*  UPDATES:                                                           *
*    06-25-80      LDW  CHANGE CSECT NAMES:   TERMIO#  -> TERMIO1     *
*                                             TERMIO$  -> TERMIO2     *
*                                             TERMIO@  -> TERMIO3     *
*                                             TERMIOWT -> TERMIO4     *
*                                             TERMIOHC -> TERMIO5     *
*                                                                     *
         LR    R11,R15
         USING TERMIO3,R11
         LR    R10,R1                       SAVE IOCB ADDRESS
         USING IOCB,R10
         OI    IOCBFLGT,IOCBTBSC
         L     R2,16                        POINT AT THE CVT
         L     R2,0(,R2)                    NEW/OLD
         L     R2,4(,R2)                    OUR TCB
         L     R2,112(,R2)                  FSA
*
*   AT THIS POINT WE HAVE LOCATED THE FIRST SAVE AREA PROVIDED TO THIS
*   TASK WHEN IT WAS ATTACHED. NOBODY CARES ABOUT THE REGISTERS THAT
*   IT CONTAINS, SO TERMIO USES IT AS THE ANCHOR FOR ITS TABLE.
*   USAGE IS AS FOLLOWS:
*
*        24    ORIGINAL PARM SAVED BY INITIAL ROUTINE (DCB PTR)
*        28    'TRIO' IF TERMIO HAS EVER BEEN INVOKED
*        32    POINTER TO PREVIOUS FORMAT TABLE USED.
*        36    POINTER TO CURRENT TERMIO WORK AREA
*
         CLC   28(4,R2),=C'#BSC'  IS THIS OUR FIRST TIME THRU
         BE    CONTINUE           NO, PICK UP WORK AREA ADDRESS
         MVC   28(4,R2),=C'#BSC'            MARK FSA AS INITIALISEDK
         B     NEWWORK            GO GET A WORK AREA
         EJECT
CONTINUE EQU   *
         L     R1,36(,R2)         GET OLD WORK AREA
         USING WORK,R1
         TM    IOCBFLGC,IOCBCCLR  REFRESH REQUESTED ?
         BO    CHGFMT                     REFORMAT IF YES
         CLC   IOCBFMT,32(R2)     HAS HE CHANGED FORMATS
         BNE   CHGFMT             YES, NEED TO CHANGE WORK AREAS
         L     R15,IOCBFMT        POINT AT THE NEW FORMAT
         CLC   FMTHEAD,0(R15)     IS HEADER UNCHANGED
         BNE   CHGFMT             NO, GET NEW WORK AREA
         TM    IOCBFLGS,IOCBFW8+IOCBFNWT    IS IT NEW FORMAT REQUEST
         BNO   RESTART            NO, JUST CONTINUE
         TM    FLAGS,READING+WRITING        IS I/O IN PROGRESS
         BZ    NEWFMTOK           NO, MAKE ADJUSTMENT
         EX    0,BOMB             YES, CANNOT CHANGE FORMAT WITH I/O
NEWFMTOK MVI   FLAGS,CLEARED      REQUEST REFORMATTING
         RETURN (14,12)
         SPACE 2
CHGFMT   TM    FLAGS,READING+WRITING        WAS I/O IN PROGRESS
         BZ    FREEOK
         EX    0,BOMB             CANNOT FREE THE I/O AREA WHILE IN USE
FREEOK   L     R0,DSECSIZE        GET SP AND LENGTH
*        FREEMAIN R,LV=(0),ADDR=(1),SP=(0)
         SVC   10                 ISSUE FREEMAIN SVC
         DROP  R1
         SPACE 2
NEWWORK  L     R15,IOCBFMT        POINT AT THE NEW FORMAT
         LH    R3,0(,R15)         GET LENGTH OF IMAGE
         LA    R3,WORKLEN(,R3)    ADD SIZE OF REST OF WORK AREA
         ICM   R3,8,=AL1(16)      ASK FOR SP=16
         LR    R0,R3
         LNR   R1,R0              SET FLAG FOR GETMAIN
*        GETMAIN R,LV=(0),SP=(0)
         SVC   10                 ISSUE GETMAIN SVC
         ST    R1,36(,R2)         SET POINTER TO THE NEW WORK AREA
         USING WORK,R1
         ST    R3,DSECSIZE        SAVE FREEMAIN ARGUMENT
         L     R3,24(,R2)         RETRIEVE PARMLIST ADDRESS
         L     R3,0(,R3)          GET DCB ADDRESS
         ST    R3,DECBR+8         INITIALIZE THE DECB
         ST    R3,DECBW+8            TO POINT TO THE DCB
         LA    R3,L'BUFFER        GET LENGTH OF INPUT AREA
         STH   R3,DECBR+6         SET IT FOR THE I/O TASK
         LA    R3,BUFFER          POINT AT THE I/O AREA
         ST    R3,DECBR+12        PLACE IT IN THE DECBS
         ST    R3,DECBW+12           ALSO
         L     R15,IOCBFMT        POINT AT THE NEW FORMAT TABLE
         ST    R15,32(,R2)        PLACE IT IN THE FSA
         MVC   FMTHEAD,0(R15)     SAVE THE HEADER FOR VALIDATION
         MVI   FLAGS,CLEARED      REQUEST A SCREEN RE-FORMAT
         TM    IOCBFLGS,IOCBFW8+IOCBFNWT  IS IT A NEW FORMAT REQUEST
         BO    NEWFMTOK
         TM    IOCBFLGS,IOCBFNWR       MUST HAVE A WRITE TO FORMAT TUBE
         BZ    RESTART
         EX    0,BOMB  ATTEMPT TO READ WITH UNFORMATTED SCREEN
         DROP  R1
         SPACE 4
RESTART  ST    R1,8(,R13)         FORWARD CHAIN
         ST    R13,4(,R1)         BACK CHAIN
         LR    R13,R1             ESTABLISH WORK BASE
         USING WORK,R13
*
*        A I D   P R O P O G A T I O N   S U P P O R T
*
         TM    IOCBFLGS,IOCBFNRD
         BNO   NOAIDCPY
         L     R14,IOCBTDCB
         LA    R14,0(R14)
         LTR   R14,R14
         BZ    NOAIDCPY                COPY AID ACROSS ?????????
         MVC   IOCBAID(1),TDCBAID-TDCB(R14)
         TM    IOCBFLGD,IOCBDPK2  EXIT HERE IF KEY 2 IS TO
         BO    NOAIDCPY                BE PASSED TO USER DISPLAY
         CLI   IOCBAID,AIDPF2    WAS LAST KEY 2 ?
         BNE   NOAIDCPY              EXIT IF NOT
         LA    R1,IOCBTDCB           INVOKE PROFILE PROMPTER
         XC    WAITPROF,WAITPROF
         ATTACH EP=DCMSPROF,SF=(E,ATCHLST),ECB=WAITPROF
         ST    R1,PROFTCBA
         WAIT  ECB=WAITPROF
         MVI   IOCBAID,AIDENT .         FORCE DEFAULT AID AFTER PROF
         DETACH  PROFTCBA,STAE=NO
         MVI   FLAGS,CLEARED
         L     R14,IOCBTDCB
         MVI   TDCBAID-TDCB(R14),AIDENT .
NOAIDCPY EQU   *
         EJECT
         L     R15,IOCBFMT        POINT AT THE USERS FORMAT
         LA    R6,8               SET FMT ENTRY LENGTH FOR BXLE
         LH    R7,2(,R15)         GET LENGTH OF FORMAT
         AR    R7,R15             POINT PAST END OF FORMAT
         SR    R7,R6              POINT AT LAST ENTRY
         TM    IOCBFLGS,IOCBFNWR  IS A WRITE REQUIRED
         BO    NOWRITE            NO, CHECK FOR OTHER OPTIONS
         TM    FLAGS,READING+WRITING        IS I/O IN PROGRESS
         BZ    WRITEOK            NO, PROCEED
         EX    0,BOMB             WRITE REQUESTED WHEN I/O IN PROGRESS
WRITEOK  LA    R8,BUFFER+5        POINT PAST RDW AND WCC
         MVC   BUFFER+4(1),4(R15) MOVE THE DEFAULT WCC IN
         TM    IOCBFLGS,IOCBFNBP+IOCBFNRR+IOCBFNRS  WERE THERE O'RIDES
         BO    WCCOK              NO, USE WCC FROM FORMAT
         NC    BUFFER+4(1),IOCBFLGS         REMOVE OVERRIDDEN BITS
         OC    BUFFER+4(1),IOCBWCC          SET THE OVERRIDE VALUES
WCCOK    OI    BUFFER+4,X'C0'     MAKE THE WCC PRINTABLE
         LA    R4,6(,R15)         POINT AT FIRST ENTRY
         TM    FLAGS,CLEARED      IS RE-FORMAT NECESSARY
         BZ    UPDATE             NO, JUST THE CHANGED FIELDS
         LH    R3,0(,R15)         GET LENGTH OF LOCAL AREA
         LA    R2,LOCAL           POINT AT LOCAL AREA
         SR    R1,R1              SET UP TO CLEAR THE AREA
         MVCL  R2,R0              ZERO THE WHOLE THING
*
*   BUILD THE ORDERS TO INSERT THE ATTRIBUTE BYTES
*
NXTATR   MVI   0(R8),X'11'        SBA
         LA    R8,1(,R8)
         LH    R1,2(,R4)          GET THE BUFFER ADDRESS
         BCTR  R1,0               BACK UP TO THE ATTRIBUTE BYTE
         BAL   R14,ENCODE         GO SET UP AN ADDRESS
         MVI   0(R8),X'1D'        START FIELD
         MVC   1(1,R8),1(R4)      MOVE IN THE ATTRIBUTE BYTE
         LA    R8,2(,R8)
         TM    1(R4),X'30'        IS THIS A DISPLAY FIELD
         BO    NOFILL             YES, IT CAN BE EXTENDED TO COVER FILL
         LH    R1,6(,R4)          GET THE FIELD LENGTH
         AH    R1,2(,R4)          COMPUTE THE NEXT ATTRIBUTE BYTE
         LA    R1,1(,R1)          POINT AT THE NEXT DATA BYTE
         CR    R4,R7              IS THERE ANOTHER FIELD
         BNL   DOFILL             NO, MUST LIMIT THIS ONE
         CH    R1,10(,R4)         IS NEXT FIELD ADJACENT
         BE    NOFILL             YES, IT WILL PROVIDE A LIMIT
         BL    DOFILL             OTHERWISE CREATE A DUMMY FIELD
         EX    0,BOMB             OVERLAPPING FIELDS IN FORMAT TABLE
         SPACE 3
DOFILL   MVI   0(R8),X'11'        SBA
         LA    R8,1(,R8)          POINT TO NEXT BUFFER BYTE
         BCTR  R1,0               POINT AT THE ATTRIBUTE BYTE
         BAL   R14,ENCODE
         MVC   0(2,R8),=X'1D7C'   BLANK PROTECTED FIELD
         LA    R8,2(,R8)
         SPACE 2
NOFILL   BXLE  R4,R6,NXTATR       GO DO THE NEXT FIELD
         EJECT
*
*   AT THIS POINT R8 POINTS TO A BUFFER POSITION AT WHICH TO PLACE A
*   SERIES OF ORDERS TO UPDATE THE SCREEN TO LOOK LIKE THE USERS IMAGE.
*   THE ACTUAL CONTENTS ARE IN THE LOCAL IMAGE.
*
UPDATE   SR    R9,R9              INDICATE NO CURRENT BUFFER POSITION
         LA    R4,6(,R15)         POINT AT THE FIRST FORMAT ENTRY
NXTFIELD LH    R2,4(R4)           GET IMAGE OFFSET
         LA    R0,LOCAL(R2)       GET POSITION IN LOCAL IMAGE
         A     R2,IOCBIMG         GET POSITION IN USER IMAGE
         LH    R3,6(,R4)          GET THE FIELD LENGTH
         LR    R1,R3
         LR    R5,R2              SAVE START OF FIELD
         CLCL  R0,R2              LOOK FOR DIFFERENCES
         BE    MATCH              THEY ARE THE SAME
         SH    R1,6(,R4)          NUMBER OF BYTES THAT ARE THE SAME
         LCR   R1,R1
         CH    R1,=H'3'           IS IT WORTH A SBA
         BH    LNGMATCH           YES, GO DO ONE
         LR    R2,R5              NO, BACK UP TO START OF FIELD
         AR    R3,R1              RESTORE COUNT
         CH    R9,2(R4)           DID A PREVIOUS COMMAND PLACE US THERE
         BE    NOSBA1             YES, NO NEED TO DO A SBA
         MVI   0(R8),X'11'        SBA
         LA    R8,1(,R8)
         LH    R1,2(,R4)          POINT AT FIRST DATA BYTE
         BAL   R14,ENCODE
NOSBA1   TM    0(R4),X'C0'        IS A CURSOR NEEDED AT START OF FIELD
         BNM   NOCUR1             NO
         TM    IOCBFLGS,IOCBFCUR  HAS HE OVERRIDDEN THE CURSOR POSITION
         BO    NOCUR1             YES, DON'T NEED ONE HERE
         MVI   0(R8),X'13'        INSERT THE CURSOR
         LA    R8,1(,R8)
NOCUR1   B     DATAMOVE           GO PLACE THE ACTUAL DATA IN STREAM
         SPACE 2
LNGMATCH LR    R5,R1              SAVE BYTE COUNT REMAINING
         TM    0(R4),X'C0'        DID WE NEED A CURSOR
         BNM   NOCUR2             NO, JUST GO DO THE DATA
         TM    IOCBFLGS,IOCBFCUR  HAS HE OVERRIDDEN THE CURSOR POSITION
         BO    NOCUR2             YES, DON'T HONOR THE FORMAT BYTE
         CH    R9,2(R4)           MAYBE WE ARE ALREADY THERE
         BE    NOSBA2             YES, HOW LUCKY
         MVI   0(R8),X'11'        WHAT A LOT OF WORK FOR A MEASLY
         LA    R8,1(,R8)             LITTLE CURSOR.
         LH    R1,2(,R4)          POINT AT THE DATA ADDRESS
         BAL   R14,ENCODE
NOSBA2   MVI   0(R8),X'13'        INSERT CURSOR
         LA    R8,1(,R8)
NOCUR2   MVI   0(R8),X'11'
         LA    R8,1(,R8)
         LH    R1,2(,R4)          GET FIELD ADDRESS IN 3270
         AR    R1,R5              MOVE TO POINT OF FIRST DIFFERENCE
         BAL   R14,ENCODE
*
*   R8=BUFFER LOCATION  R2=DATA ADDRESS   R3=DATA LENGTH
*
DATAMOVE STM   R2,R3,28(R13)      SAVE INPUT POINTERS
         LH    R1,4(,R4)          GET IMAGE OFFSET
         LA    R0,LOCAL(R1)       COMPUTE LOCAL FIELD START
         LR    R1,R3              GET DATA LENGTH
         SH    R1,6(,R4)          GET COMPLEMENT OF NUMBER OF MATCHES
         SR    R0,R1              BUMP LOCAL FIELD ADDRESS
         LR    R1,R3              GET NUMBER OF BYTES TO MOVE
         MVCL  R0,R2              UPDATE LOCAL IMAGE
         LM    R2,R3,28(R13)      RESTORE THE DATA POINTERS
         BCTR  R2,0               ADJUST FOR REVERSE SCAN
BLNKOUT  LA    R5,0(R2,R3)        POINT AT TRAILING BYTE
         CLI   0(R5),C' '         IS IT A BLANK
         BNE   SHORTND            NO, END OF TRUNCATION
         BCT   R3,BLNKOUT         THROW AWAY THE BLANKS
         BCTR  R5,0               ALLOW BLANKING OF 1-BYTE FIELDS
         B     INSPT              JUST WIPE OUT REST OF FIELD
         SPACE 2
SHORTND  LA    R2,1(,R2)          POINT BACK AT FIRST BYTE
         LA    R9,BUFFER+L'BUFFER-16        LAST ALLOWABLE ADDRESS
         SR    R9,R8
         CR    R9,R3              WILL IT FIT
         BH    LENOK
         EX    0,BOMB             GENERATED DATA EXCEEDS BUFFER LENGTH
LENOK    LR    R9,R3
         LR    R1,R9              SAVE OUTPUT LENGTH
         MVCL  R8,R2
         LR    R3,R1              RETRIEVE OUTPUT LENGTH
         LR    R2,R8
         SR    R2,R3              COMPUTE OUTPUT ADDRESS
         BAL   R14,UPCASE              KEEP INVALID CHARS OFF THE LINE
         SPACE 2
INSPT    TM    0(R4),X'C0'        DOES HE WANT A CURSOR HERE
         BNO   NOCUR3
         TM    IOCBFLGS,IOCBFCUR  HAS HE OVERRIDDEN THE CURSOR POSITION
         BO    NOCUR3             YES, THEN HE DOESN'T WANT ONE HERE
         MVI   0(R8),X'13'        INSERT CURSOR
         LA    R8,1(,R8)
NOCUR3   S     R5,28(,R13)        COMPUTE DISTANCE TO END OF FIELD
         S     R5,32(,R13)        GET -1-REMAINING BYTES
         LCR   R5,R5              THAT MAKES IT REMAINING BYTES + 1
         BCT   R5,*+8             CHECK FOR FULL FIELD
         B     NONULL             IF SO, DO NOT PUT THE NULL IN
         MVI   0(R8),0            INSERT A NULL
         LA    R8,1(,R8)
NONULL   MVI   0(R8),X'05'        INSERT A PT ORDER
         LA    R8,1(,R8)
         LR    R9,R4              POINT AT CURRENT FMT ENTRY
PTSCAN   BXH   R9,R6,NOUNP        POINT TO NEXT FORMAT ENTRY
         TM    1(R9),X'20'        IS THE FIELD UNPROTECTED
         BO    PTSCAN             NO. PT ORDER WILL SKIP IT
         LH    R9,2(,R9)          GET STOPPING POINT FOR PT ORDER
         B     ENDFIELD
         SPACE 3
MATCH    TM    IOCBFLGS,IOCBFCUR  HAS HE OVERRIDDEN THE CURSOR POSITION
         BO    ENDFIELD           YES, THEN WE ARE THROUGH HERE
         TM    0(R4),X'C0'        DID THE FORMAT CALL FOR A CURSOR
         BZ    ENDFIELD           NO, GO TO NEXT FIELD
         BO    MATSCAN            YES, AFTER ALL THE DATA
         CH    R9,2(,R4)          IS THE BUFF ADD NOW AT RIGHT POINT
         BE    NOSBA4             YES, HOW FORTUTITOUS
         LH    R1,2(,R4)          GET THE DATA ADDRESS
MATSBA   MVI   0(R8),X'11'        SBA
         LA    R8,1(,R8)
         BAL   R14,ENCODE
NOSBA4   MVI   0(R8),X'13'        INSERT CURSOR
         LA    R8,1(,R8)
         SR    R9,R9              INDICATE UNKNOWN BUFFER ADDRESS
         B     ENDFIELD
         SPACE 2
MATSCAN  LH    R1,6(,R4)          GET FIELD LENGTH
MATSCANL BCTR  R2,0               MOVE POINTER BACK ONE
         CLI   0(R2),C' '         IS THIS A BLANK
         BNE   MATSCANE           YES, PLACE THE CURSOR HERE
         BCT   R1,MATSCANL
MATSCANE AH    R1,2(,R4)          COMPUTE CURSOR ADDRESS
         B     MATSBA
         SPACE
NOUNP    SR    R9,R9              INDICATE NO FOLLOWING UNPROTECTED
ENDFIELD BXLE  R4,R6,NXTFIELD     GO DO NEXT FIELD
         SPACE 5
         TM    IOCBFLGC,IOCBCTIM
         BO    NOTIMSTP
*        TIMESTAMP SCREEN
*
         MVC   0(STAMPLEN,R8),IOCBCTRL    COPY TIMESTAMP AND INTERVAL
         LA    R8,STAMPLEN(R8)
NOTIMSTP EQU   *
*
*  NOW THAT THE SCREEN HAS BEEN FORMATTED WE CAN PLACE THE SPECIAL
*  FIELD IN THE LOWER RIGHT CORNER FOR ERASE INPUT DETECTION.
*
         MVC   0(7,R8),=X'115D7D1D5D6A6A'  ROW 24 COL  78
         LA    R8,7(,R8)          BUMP THE FIELD POINTER
         TM    IOCBFLGS,IOCBFCUR  HAS HE OVERRIDDEN THE CURSOR POSITION
         BZ    NOCUR4             NO, THEN CURSOR IS ALREADY IN
         LH    R1,IOCBCUR         GET IMAGE OFFSET OF CURSOR
         L     R4,IOCBFMT         GET FIRST FORMAT ENTRY
         LA    R4,6(,R4)          SKIP THE HEADER
CURSCAN  LH    R2,4(,R4)          GET THE IMAGE OFFSET OF THE FIELD
         AH    R2,6(,R4)          COMPUTE END OF FIELD
         CR    R1,R2              SEE IF CURSOR IS IN FIELD
         BL    CURFND             YES, GO PLACE IT THERE
         BXLE  R4,R6,CURSCAN      GO CHECK THE NEXT ENTRY
         EX    0,BOMB             CURSOR IS BEYOND END OF IMAGE
         SPACE 2
CURFND   SH    R1,4(,R4)          COMPUTE OFFSET IN FIELD
         BNM   CUROK              CHECK FOR INCLUSION
         EX    0,BOMB             CURSOR IS IN IMAGE FILL AREA
CUROK    AH    R1,2(,R4)          COMPUTE TUBE ADDRESS
         MVI   0(R8),X'11'        SBA
         LA    R8,1(,R8)
         BAL   R14,ENCODE
         MVI   0(R8),X'13'        INSERT CURSOR
         LA    R8,1(,R8)
         SPACE 3
NOCUR4   LA    R9,BUFFER          POINT AT FRONT OF BUFFER
         SR    R8,R9              COMPUTE DATA LENGTH
         STH   R8,BUFFER
         XC    BUFFER+2(2),BUFFER+2  CLEAR OUT REST OF RDW
         LA    R1,DECBW           POINT AT THE OUTPUT DECB
         ST    R1,IOCBDECB        PUT IT WHERE CALLER CAN WAIT FOR IT
         TM    FLAGS,CLEARED      WAS AN ERASE/WRITE NEEDED
         BO    WRITEER            YES, GO GET IT
         WRITE (1),SF,MF=E
         B     TESTCHK
WRITEER  WRITE (1),SZ,MF=E
TESTCHK  MVI   FLAGS,WRITING+LASTOPW        REMEMBER THE STATUS
         TM    IOCBFLGS,IOCBFNRD  WAS A READ REQUESTED
         BZ    READ               YES, GO ISSUE IT
         L     R15,IOCBTDCB
*KLUGE
         MVC   TDCBPECB-TDCB(4,R15),=F'0'  TDECB POINTER
*KLUGE
*        BLUSH BLUSH STAMMER
         TM    IOCBFLGS,IOCBFNWT  DID HE WANT ME TO DO THE WAIT
         BO    EXIT               NO, GET OUT OF HERE
WRTCHK   CHECK DECBW              WAIT FOR THE WRITE TO COMPLETE
         NI    FLAGS,NOT-WRITING  INDICATE I/O NOT OUTSTANDING
         TM    IOCBFLGS,IOCBFNRD  WAS A READ REQUESTED
         BO    EXIT               NO, NOW WE ARE DONE
         B     RDWAIT             YES, GO CHECK IT OUT
         SPACE 2
NOWRITE  TM    IOCBFLGS,IOCBFW8   WAS A WAIT ONLY REQUESTED
         BO    WAITONLY           YES, GO SEE WHAT'S COOKING
         TM    FLAGS,LASTOPW      WAS LAST OPERATION A WRITE
         BO    READ               YES, THEN GO DO THE READ
         EX    0,BOMB             NO, ATTEMPT TO READ TWICE IN A ROW
         SPACE 2
WAITONLY TM    IOCBFLGS,IOCBFNWT  DOES HE REALLY WANT A WAIT
         BO    EXIT               NO, JUST GETTING SET UP
         TM    FLAGS,WRITING      WAS A WRITE IN PROGRESS
         BO    WRTCHK             YES, THAT MUST BE WHAT HE WANTS
         TM    FLAGS,READING      WAS A READ IN PROGRESS
         BO    RDCHK              YES, GO CHECK THAT
         EX    0,BOMB             WAIT REQUESTED WHEN NO I/O RUNNING
         SPACE 4
READ     TM    FLAGS,READING      IS READ ALREADY GOING
         BZ    READOK             NO, THEN WE CAN DO ONE
         EX    0,BOMB             TWO READS ATTEMPTED
READOK   LA    R1,DECBR           POINT AT INPUT DECB
         ST    R1,IOCBDECB        PUT IT WHERE HE CAN WAIT ON IT
         READ  (1),SF,MF=E
         OI    FLAGS,READING
         TM    IOCBFLGS,IOCBFNWR  WAS A WRITE REQUESTED
         BZ    WRTCHK             YES, GO CHECK IT
         SPACE 2
RDWAIT   TM    IOCBFLGS,IOCBFNWT  DID HE WANT ME TO DO THE CHECK
         BO    EXIT               NO, GO BACK TO HIM
RDCHK    CHECK DECBR
         MVI   FLAGS,LASTOPR      INDICATE ALL I/O COMPLETE
         MVC   IOCBAID,BUFFER+4   SAVE THE ATTENTION DESCRIPTION
         TM    IOCBFLGD,IOCBDPK2     RETURN KEY 2
         BO    NOPROFKY                     IF REQUESTED
         CLI   IOCBAID,AIDPF2          ELSE
         BNE   NOPROFKY                    INVOKE
         LA    R1,IOCBTDCB                        PROFILE  AND
         XC    WAITPROF,WAITPROF
         ATTACH EP=DCMSPROF,SF=(E,ATCHLST),ECB=WAITPROF
         ST    R1,PROFTCBA
         WAIT  ECB=WAITPROF
         MVI   IOCBAID,AIDENT .         FORCE DEFAULT AID AFTERWARDS
         DETACH  PROFTCBA,STAE=NO
NOPROFKY EQU   *
         TM    IOCBFLGC,IOCBCFRA  IF FORCE
         BO    EXIT                  THEN EXIT
         TM    IOCBFLGC,IOCBCRAO  IF
         BNO   OTHER2               REQUESTED
         CLI   IOCBAID,AIDPF1                TRANSMIT
         BL    OTHER1                                ONLY
         CLI   IOCBAID,AIDPF9                            PFK
         BH    OTHER2                                       AID
         B     EXIT                                            BACK
OTHER1   EQU   *
         CLI   IOCBAID,AIDPF10     OTHERWISE,
         BL    OTHER2                        PERFORM
         CLI   IOCBAID,AIDPF12                      NORMAL
         BH    OTHER2                                     READ
         B     EXIT                                           REPLACE
OTHER2   EQU   *
         CLI   IOCBAID,AIDCLR     DID HE HIT THE CLEAR KEY
         BE    CLEAR              YES, GO INDICATE ZEROED BUFFER
         CLI   IOCBAID,AIDCAN     DID HE HIT CANCEL (PA2)
         BE    CANCEL             YES, GIVE BACK ORIGINAL SCREEN
         CLI   IOCBAID,AIDPA1     IS IT SOME OTHER FUNCTION
         BE    PA1
         CLI   IOCBAID,AIDPA3
         BE    PA3
         TM    IOCBAID,X'40'      IS IT A VALID AID
         BO    AIDVALID           YES, GO LOOK AT MANUAL INPUT
         EX    0,BOMB             AID BYTE WAS NOT VALID
AIDVALID LH    R5,BUFFER          GET LENGTH OF INPUT DATA
         LA    R5,BUFFER-5(R5)    POINT AT LAST 5 BYTES
         CLC   0(5,R5),=X'115D7E6A6A'       IS LAST FIELD EXPECTED ONE
         BE    CNVTCUR            YES, USER DID NOT HIT ERASE INPUT
*
*   THE SPECIAL FIELD THAT WAS PLACED IN THE LOWER RIGHT HAND CORNER
*  OF THE SCREEN WAS TAMPERED WITH. THE ASSUMPTION IS MADE THAT THE
*   USER HIT THE ERASE INPUT FIELD. IF HE WAS JUST FUTZING AROUND,
*   TOO BAD FOR HIM. THE FIELD IS NON-DISPLAY ANAYWAY.
*
         L     R4,IOCBFMT
         LA    R4,6(,R4)          POINT AT HTE FIRST FORMAT ENTRY
ERSINP   TM    1(R4),X'20'        IS THIS A PROTECTED FIELD
         BO    ERSPRO             YES, THEN DON'T ERASE IT
         LH    R2,4(,R4)          GET THE IMAGE OFFSET
         LA    R2,LOCAL(R2)       POINT AT THE LOCAL COPY
         LH    R3,6(,R4)          GET THE FIELD LENGTH
         SR    R1,R1              SET UP FOR FIELD ZEROING
         ICM   R1,8,=CL128' '     MAKE THE FILL CHARACTER A BLANK
         MVCL  R2,R0              BLANK OUT THE FIELD
ERSPRO   BXLE  R4,R6,ERSINP       GO DO THE NEXT FIELD
         CLC   0(3,R5),=X'115D7E6A6A'       IS THE FIELD STILL THER
         BE    CNVTCUR            FIELD IS STILL THERE, BUT DIFFERENT
         LA    R5,1(,R5)          MOVE TO LAST POSSIBLE POSITION
         CLC   0(3,R5),=X'115D7E6A6A'       DID HE ONLY DO IN ONE BYTE
         BE    CNVTCUR            YES, IGNORE THAT FIELD
         LA    R5,4(,R5)          FIELD IS GONE COMPLETELY
         SPACE 2
CNVTCUR  LA    R8,BUFFER+5        POINT AT BYTE AFTER THE AID
         BAL   R14,DECODE         GET THE IMAGE OFFSET FOR IT
         STH   R1,IOCBCUR         FEED IT BACK TO THE USER
         CR    R8,R5              IS THE BUFFER EXHAUSTED
         BNL   EOB                IF SO, COPY THE UPDATED BUFFER
         CLI   0(R8),X'11'        VERIFY THAT IT IS A SBA ORDER
         BNE   CLEAR              BUFFER WAS NOT FORMATTED, IGNORE IT
INPFIELD LA    R8,1(,R8)          POINT PAST THE SBA
         BAL   R14,DECODE         GET THE IMAGE OFFSET
         CH    R1,4(,R4)          DOES IT POINT TO FIELD START
         BE    ADDROK
         EX    0,BOMB             RETURNED ATTR ADDR+1 WAS NOT VALID
ADDROK   LR    R2,R8              GET DATA ADDRESS
         LR    R3,R5              GET EOB ADDRESS
         SR    R3,R2              COMPUTE REMAINING BYTES
         BP    SBALOOP            CHECK FOR LEGAL DATA FORMAT
         LR    R1,R8              USER ERASED THE FIELD
         BZ    FNDSBA             GO COPY NO BYTES TO THE IMAGE
         EX    0,BOMB             INPUT DATA WAS TOO SHORT
SBALOOP  CH    R3,=H'256'
         BNH   SHRTRT             IS LENGTH DOWN BELOW 257
         TRT   0(256,R2),TRTSBA   LOOK FOR AN SBA BYTE
         BNZ   FNDSBA             WAS ONE FOUND
         AH    R2,=H'256'         NO, ADVANCE ONE SWATCH
         SH    R3,=H'256'         REDUCE THE COUNT
         B     SBALOOP            GO TRY SOME MORE
SHRTRT   BCTR  R3,0               REDUCE COUNT FOR EX
         EX    R3,EXTRT           ONE LAST SCAN
         BNZ   FNDSBA             WAS ONE FOUND
         LA    R1,1(R2,R3)        NO, POINT PAST END OF BUFFER
FNDSBA   SR    R1,R8              GET INPUT FIELD LENGTH
         LR    R0,R8              GET FIELD ADDRESS
         AR    R8,R1              POINT AT THE LOCATED BYTE
         LR    R3,R1              SET UP FOR UPCASE ROUTINE
         LH    R2,4(,R4)          GET IMAGE OFFSET
         LA    R2,LOCAL(R2)       POINT AT DESTINATION
         LR    R14,R2
         LH    R15,6(,R4)         GET DESTINATION LENGTH
         ICM   R1,8,=CL128' '     SET UP THE PAD CHARACTER
         MVCL  R14,R0             COPY INPUT INTO IMAGE
         CR    R8,R5              IS THAT END OF BUFFER
         BL    INPFIELD           NO, GO DO THE NEXT ONE
EOB      BE    CANCEL             MAKE SURE EOB HIT RIGHT ON
         EX    0,BOMB             END OF INPUT BUFFER WAS MISSED
         SPACE 2
CANCEL   L     R2,IOCBIMG         POINT AT USER IMAGE AREA
         LA    R0,LOCAL           POINT AT OUR IMAGE AREA
         L     R14,IOCBFMT        POINT AT USER FORMAT
         LH    R1,0(,R14)         GET IMAGE LENGTH
         LR    R3,R1
         MVCL  R2,R0              COPY LOCAL IMAGE TO USER IMAGE
         LH    R3,0(,R14)              GET LENGTH AGAIN
         SR    R2,R3
         BAL   R14,UPCASE              MAKE THE USERS COPY UPPER CASE
         CLI   IOCBAID,AIDCAN     DID WE GET HERE FROM CANCEL KEY
         BNE   EXIT               NO,RETURN TO USER
         SPACE 2
PA1      EQU   *
PA3      EQU   *
CLEAR    MVI   FLAGS,CLEARED      INDICATE THAT TUBE IS NOT FORMATTED
         B     EXIT               RETURN TO USER
         EJECT
EXIT     EQU   *
         TM    IOCBFLGD,IOCBDYON . SUPPRESS DELAY IF:
         BNO   REALEXIT .
         CLC   IOCBWAIT,=F'0' .      - DELAY IS ZERO
         BE    REALEXIT .
         BL    REALEXIT .
         CLI   IOCBAID,AIDENT .       O R -
         BE    RTIMER .                     ANY
         CLI   IOCBAID,AIDCAN .                PF
         BE    RTIMER .                           KEY
         CLI   IOCBAID,AIDNULL .                     WAS
         BE    RTIMER .                                 ENTERED
         B     REALEXIT .
RTIMER   EQU   *
         STIMER WAIT,BINTVL=IOCBWAIT
REALEXIT EQU   *
         L     R13,4(,R13)        POINT TO CALLERS REGISTERS
         RETURN (14,12)
         EJECT
*
*   CONVERT A HALF WORD BUFFER ADDRESS TO 3270 FORMAT
*
*        R1 = 12 BIT BUFFER ADDRESS
*        R8 = OUTPUT AREA ADDRESS
*
ENCODE   SLDL  R0,26              GET TWO 6-BIT QUANTITIES
         SRL   R1,2               MAKE THEM 8-BIT QUANTITIES
         SRDL  R0,8               IN THE SAME REGISTER
         STCM  R1,12,0(R8)        PLACE THEM IN THE DATA STREAM
         TR    0(2,R8),TABLENC    MAKE THEM PRINTABLE
         LA    R8,2(,R8)          BUMP THE OUTPUT ADDRESS
         BR    R14                RETURN TO CALLER
         SPACE 2
TABLENC  DC    C' ABCDEFGHI¢.<(+|&&JKLMNOPQR!$*);¬-/STUVWXYZ¦,%_>?01234X
               56789:#@''="'      TABLE TO MAKE ADDRESSES PRINTABLE
         SPACE 4
BOMB     EX    0,BOMB
         SPACE 4
*
*   ROUTINE TO CONVERT A FIELD TO UPPER CASE
*        R2=ADDRESS,  R3=LENGTH,  R14=RETURN
*
UPCASE   CH    R3,=H'256'         IS IT TOO SHORT FOR A LONG TR
         BL    LASTPASS           YES, GO DO AN EXECUTED ONE
         TR    0(256,R2),UPCTBL     CONVERT A SWATCH
         AH    R2,=H'256'         MOVE TO NEXT SWATCH
         SH    R3,=H'256'         DECREASE REMAINING COUNT
         BNZ   UPCASE             GO DO IT AGAIN
LASTPASS SH    R3,=H'1'           REDUCE FOR EXECUTE
         BMR   R14                YES, JUST RETURN
         EX    R3,TRUPC           CONVERT TAIL END TO U.C.
         BR    R14
         SPACE 2
TRUPC    TR    0(0,R2),UPCTBL
         SPACE 2
UPCTBL   DC    256C'?'                 ALL INVALIDS GO TO QUESTION
         ORG   UPCTBL+C' '
         DC    C' '
         ORG   UPCTBL+C'¢'
         DC    C'¢.<(+|&&'
         ORG   UPCTBL+C'!'
         DC    C'!$*);¬-/'
         ORG   UPCTBL+C','
         DC    C',%_>?'
         ORG   UPCTBL+C':'
         DC    C':#@''="'
         ORG   UPCTBL+X'81'
         DC    C'ABCDEFGHI'
         ORG   UPCTBL+X'91'
         DC    C'JKLMNOPQR'
         ORG   UPCTBL+X'A2'
         DC    C'STUVWXYZ'
         ORG   UPCTBL+C'A'
         DC    C'ABCDEFGHI'
         ORG   UPCTBL+C'J'
         DC    C'JKLMNOPQR'
         ORG   UPCTBL+C'S'
         DC    C'STUVWXYZ'
         ORG   UPCTBL+C'0'
         DC    C'0123456789'
         ORG
         SPACE 4
*
*   ROUTINE TO FIND THE IMAGE OFFSET FOR A BUFFER ADDRESS
*   INPUT:  R8=ADDRESS OF INPUT,  R6 AND R7 SET UP FOR BXLE THROUGH FMT
*   OUTPUT:  R1= IMAGE OFFSET, R4 POINTS AT FORMAT ENTRY
*      R1 = -1 IF BUFFER ADDRESS IS OUTSIDE OF THE IMAGE
*
DECODE   L     R4,IOCBFMT
         LA    R4,6(,R4)          POINT AT FIRST FORMAT
         IC    R0,1(,R8)          GET THE SECOND BYTE
         SLL   R0,2               REMOVE HIGH TWO BITS
         ICM   R0,2,0(R8)         TACK ON THE FIRST BYTE
         SRL   R0,2               PUT IT IN LOW 12 BITS
         LA    R1,4095            SET MASK FOR 12 BITS
         NR    R1,R0              GET THE BINARY BUFFER OFFSET
         LA    R8,2(,R8)          BUMP THE SCAN POINTER
DECDL    LH    R0,2(,R4)          GET FIELD START
         CR    R0,R1              IS THIS IT
         BE    DECDF              YES, RIGHT ON THE DOT
         BH    DECDERR            FIELD IS NOT DEFINED
         AH    R0,6(,R4)          POINT AT END OF FIELD
         CR    R0,R1              CHECK FOR INCLUSION
         BH    DECDF              YES, GOT IT
         BXLE  R4,R6,DECDL        GO TRY NEXT FIELD
DECDERR  LH    R1,=H'-1'          SET ERROR INDICATION
         BR    R14
DECDF    SH    R1,2(,R4)          GET RELATIVE POSITION IN FIELD
         AH    R1,4(,R4)          POINT INTO IMAGE INSTEAD
         BR    R14                RETURN TO CALLER
         EJECT
*   CONSTANTS DSECTS AND OTHER POOR TRASH
NOT      EQU   X'FF'
EXTRT    TRT   0(0,R2),TRTSBA     SCAN FOR SBA
         SPACE 4
         LTORG
TRTSBA   DC    XL17'0',X'01',XL238'0'       TRT TABLE FOR SBA ORDER
PATCH    DS    XL50'0'
IOCB     DSECT
         IOCB
WORK     DSECT
         DS    18F                SAVE AREA
PROFTCBA DS    F .
WAITPROF DS    F .
ATCHLST  ATTACH EP=DCMSPROF,ECB=WAITPROF,SF=L
         WRITE DECBW,SF,MF=L
         READ  DECBR,SF,MF=L
FLAGS    DS    H
READING  EQU   X'80'
WRITING  EQU   X'40'
CLEARED  EQU   X'20'
LASTOPR  EQU   X'10'
LASTOPW  EQU   X'08'
FMTHEAD  DS    CL6
DSECSIZE DS    F
BUFFER   DS    CL2100
LOCAL    DS    0F
WORKLEN  EQU   *-WORK
         TDCB
         END
