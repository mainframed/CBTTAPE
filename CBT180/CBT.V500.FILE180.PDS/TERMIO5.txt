***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1980 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.  (Please see reason mentioned       *
*        below.)                                                      *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.  (Please see reason        *
*        mentioned below.)                                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1.3, w/DFP 3.2),   *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc!ldw                                                *
*                                                                     *
*          (213) 740-2875 (direct -- 11 am to 7 pm Pacific time)      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, 714, or 310.  Sorry.                                  *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
TERMIO5  TITLE 'HARDCOPY TERMIO SUPPORT FOR TSO '
*  UPDATES:                                                           *
*    06-25-80      LDW  CHANGE CSECT NAMES:   TERMIO#  -> TERMIO1     *
*                                             TERMIO$  -> TERMIO2     *
*                                             TERMIO@  -> TERMIO3     *
*                                             TERMIOWT -> TERMIO4     *
*                                             TERMIOHC -> TERMIO5     *
*                                                                     *
         EJECT
         DCMSREGS
         EJECT
*
*  USER ABEND CODES :
*     120  - INVALID RETURN FROM TGET SVC
*
TERMIO5  CSECT
         USING TERMIO5,R11
         LR    R11,R15
*        TERMIO1 SAVED THE REGISTERS
         LR    R10,R1              POINT TO IOCB
         USING IOCB,R10
         GETMAIN R,LV=WORKLEN,SP=2
         ST    R13,4(R1)           CHAIN
         ST    R1,8(R13)           THE
         LR    R13,R1              SAVE AREAS
         USING DCMSWORK,R13
         SPACE 3
         OI    IOCBFLGT,IOCBTHCY
         TM    IOCBFLGS,IOCBFNWR+IOCBFNRD
         BO    QUITLOG
         TM    IOCBFLGS,IOCBFNWR
         BO    NOWRITE
         CLI   IOCBTERM,X'7F'
         BE    NOCHECK
         GTSIZE
         STM   0,1,IOCBTERM
         MVI   IOCBTERM,X'7F'
NOCHECK  EQU   *
         LH    R2,IOCBTERM+6
         CH    R2,=H'80'
         BNL   CONTINU
         TPUT  CANTDO,L'CANTDO
         LA    R15,16
         MVI   IOCBAID,AIDPA1
         B     QUITLOG
CONTINU  EQU   *
         SR    R0,R0           . SOURCE LOC = 0
         SR    R15,R15         .
         SR    R1,R1           . SOURCE LEN = 0
         LA    R14,LOGBUF      . TARGET LOC
         ICM   R1,8,=C' '      . FILL CHARACTER
         L     R15,=A(28*80) . . TARGET LENGTH
         MVCL  R14,R0 .        .   CLEAR BUFFER OUT
         L     R9,IOCBFMT
         L     R8,IOCBIMG
         LH    R7,0(R9)
         LH    R6,2(R9)
         AR    R6,R9
         LA    R9,6(R9)
         LA    R1,LOGBUF
FORMLOOP EQU   *
         LH    R2,2(R9)            TO OFFSET
         AR    R2,R1
         LH    R4,4(R9)            FROM OFFSET
         AR    R4,R8
         LH    R3,6(R9)            GET LENGTH
         LR    R5,R3               IN = OUT (LENGTH
         LA    R5,0(R5)            PADCHAR = X'00'
*
         MVCL  R2,R4
*
         LA    R9,8(R9)            NEXT FORMAT ENTRY
         CR    R9,R6
         BL    FORMLOOP
*
*        LOGOUT VERSION OF SCREEN COMPLETED HERE
*
         LA    R2,LOGBUF
         LA    R3,23
         LA    R4,80
         LA    R5,158         FOR EXECUTES
         TPUT  SLASH,80
LOGLOOP  EQU   *
         MVI   0(R2),C'>'
*                          FOR 2 BLANK LINES IN A ROW, SKIP ONE.
         EX    R5,CMPBLANK           TWO BLANK LINES?
         BE    LOGNOPUT              ...YES, SKIP PUTTING THIS ONE.
*
         TPUT  (R2),(R4)
LOGNOPUT AR    R2,R4
         BCT   R3,LOGLOOP
*
         TPUT  SPACEZ,1
*
         LA    R1,IOCBTIME+1
         LA    R0,STAMPLEN
         TPUT  (1),(0)
*
         TPUT  SPACEZ,1
*
NOWRITE  EQU   *
         MVI   IOCBAID,AIDENT
         TM    IOCBFLGS,IOCBFNRD
*        BO    QUITLOG
         LA    R1,OUTPUTL
         L     R0,=A(136)
         TGET  (1),(0)
         LTR   R15,R15
         BZ    GETLGTH
         CH    R15,=H'8'
         BE    DUMATTN
         BL    GETLGTH
         ABEND 120
*
GETLGTH  LA    R1,0(R1)
         MVI   IOCBAID,AIDENT        DEFAULT = ENTER
         LTR   R3,R1                 INPUT LEN = ZERO?
         BZ    QUITLOG               ...YES, NO MORE TO DO.
*
         BCTR  R3,0                  FOR EXECUTE.
         EX    R3,TRANS          TRANSLATE INPUT TO UPPER CASE
*
         MVC   IOCBEND(L'IOCBEND),OUTPUTL
         CH    R1,=H'1'
         BE    DUMPFK
         CH    R1,=H'2'
         BNE   INPUT
         CLI   OUTPUTL,C'1'
         BNE   INPUT
         CLI   OUTPUTL+1,C'0'
         BNE   KEY11
         MVI   IOCBAID,AIDPF10
         B     QUITLOG
TRANS    TR    OUTPUTL(0),UPCTBL      FORCE UPPERCASE ON INPUT
KEY11    CLI   OUTPUTL+1,C'1'
         BNE   KEY12
         MVI   IOCBAID,AIDPF11
         B     QUITLOG
KEY12    CLI   OUTPUTL+1,C'2'
         BNE   QUITLOG
         MVI   IOCBAID,AIDPF12
         B     QUITLOG
DUMPFK   EQU   *
         CLI   OUTPUTL,C'!'
         BNE   DUMPFK2
         MVI   OUTPUTL,AIDPA1
DUMPFK2  CLI   OUTPUTL,AIDPF1
         BL    PFK02
         CLI   OUTPUTL,AIDPF9
         BH    QUITLOG
         MVC   IOCBAID(1),OUTPUTL
         B     QUITLOG
PFK02    CLI   OUTPUTL,AIDPA3
         BL    QUITLOG
         CLI   OUTPUTL,AIDPF12
         BH    QUITLOG
         MVC   IOCBAID(1),OUTPUTL
         B     QUITLOG
DUMATTN  EQU   *
         MVI   IOCBAID,AIDPA1
         B     QUITLOG
*
INPUT    EQU   *
         L     R9,IOCBFMT
         L     R8,IOCBIMG
         LH    R7,0(R9)
         LH    R6,2(R9)
         AR    R6,R9
         LA    R9,6(R9)
LOOKLOOP EQU   *
         TM    0(R9),X'80'  CURSOR IN FIELD?
         BO    GOTCUR
         LA    R9,8(R9)
         CR    R9,R6
         BNH   LOOKLOOP
         B     QUITLOG
GOTCUR   EQU   *
         LH    R4,4(R9)   GET OFFSET
         AR    R4,R8      OFFSET + START
         AR    R1,R4
         CR    R1,R9       ? END
         BNH   MOVEIN
         LR    R1,R9
         SR    R1,R4
         LR    R3,R1
MOVEIN   EX    R3,MOVEXX
         B     QUITLOG
MOVEXX   MVC   0(0,R4),OUTPUTL
         TITLE 'CONSTANTS'
CMPBLANK CLC   1(0,R2),SPACEZ           EXECUTED - COMPARE TO BLANKS
SLASH    DC    80C'-'
SPACEZ   DC    CL160' '
         SPACE 2
UPCTBL   DC    256C'?'                 ALL INVALIDS GO TO QUESTION
         ORG   UPCTBL+C' '
         DC    C' '
         ORG   UPCTBL+C'¢'
         DC    C'¢.<(+|&&'
         ORG   UPCTBL+C'!'
         DC    C'!$*);¬-/'
         ORG   UPCTBL+C','
         DC    C',%_>?'
         ORG   UPCTBL+C':'
         DC    C':#@''="'
         ORG   UPCTBL+X'81'
         DC    C'ABCDEFGHI'
         ORG   UPCTBL+X'91'
         DC    C'JKLMNOPQR'
         ORG   UPCTBL+X'A2'
         DC    C'STUVWXYZ'
         ORG   UPCTBL+C'A'
         DC    C'ABCDEFGHI'
         ORG   UPCTBL+C'J'
         DC    C'JKLMNOPQR'
         ORG   UPCTBL+C'S'
         DC    C'STUVWXYZ'
         ORG   UPCTBL+C'0'
         DC    C'0123456789'
         ORG
         SPACE 4
CANTDO   DC    C'LINESIZE OF TERMINAL MUST BE 80 OR GREATER'
         DS    0F .
QUITLOG  DS     0H .
         SR    R15,R15
DCMSRCD  EQU   *
         LR    R2,R13
         L     R1,4(R13)
         ST    R15,16(R1)
         LR    R13,R1
         FREEMAIN R,LV=WORKLEN,A=(R2),SP=2
         LM    14,12,12(13)
         BR    R14
         LTORG
         EJECT
IOCB DSECT
         IOCB
         EJECT
DCMSWORK DSECT
DCMSAVE  DS    18F .
PARMADDR DS    F
OUTPUTL  DS    3F .
LOGBUF   DS    28CL80 .
WORKLEN  EQU   *-DCMSWORK
         END
