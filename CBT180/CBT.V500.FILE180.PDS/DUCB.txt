DUCB     TITLE 'D U C B - DISPLAY UNITS COMMAND FROM TSO/DCMS'
***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1988 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.                                     *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*                                                                     *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1, with DFP 3.1),  *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc-oberon!LDW (last resort)                           *
*                                                                     *
*          (213) 743-5391 (direct -- 11 am to 7 pm Pacific time)      *
*          (213) 743-2957 (msgs)                                      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, or 714.  Sorry.                                       *
*                                                                     *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*  Updates:                                                           *
*    22Nov90  U027 LDW  Show if device is O-BOX                       *
*    27Aug90  U026 LDW  Add U024 new commands to HELP displays        *
*                       Change HELP panels to mixed case              *
*    15Apr90  U025 LDW  Add UCBREW display                            *
*    30Mar90  U024 LDW  Add 3380K recognition                         *
*                       Reduce file= to MVS max of 4 digits           *
*                       Add support for COMM-only, DISP-only, and     *
*                          U/R-only displays                          *
*                       Fix bugs in Mode= display                     *
*    20Feb90  U023 EMS  Add 2 digits to File= disp.                   *
*                       Add 3390 recognition                          *
*                       Fix a few literal length errors               *
*    20Feb88  U022 LDW  Fix for MVS/XA                                *
*                       Correct the VIO test                          *
*                       Display 'F-Box' for boxed devices             *
*                       Change some things to lower case (not flagged)*
*                       Change some DC's to literals     (not flagged)*
*    23JUN86  U021 LDW  CHECK FOR AND DISPLAY 3380E                   *
*    22JUN86  U020 LDW  FIX INFINITE LOOP PROBLEM WHEN NO DEVICES     *
*                          MEET DISPLAY CRITERIA                      *
*    02/28/86 U019 LDW  FIX SO THAT IF THE NUMBER OF DEVICES TO BE    *
*                          DISPLAYED IS AN EXACT MULTIPLE OF THE      *
*                          SCREEN SIZE, SCROLLING PAST THE END        *
*                          DOESN'T DISPLAY A BLANK SCREEN             *
*                       ALLOW OPERATOR COMMANDS TO BE ISSUED VIA      *
*                          "/XXX"                                     *
*                       ADD DEVICE TYPES                              *
*    02/23/86 U018 LDW  USE UCB LOOKUP ROUTINE IF SP1.3 OR SP2        *
*                       USE @GLOBALS                                  *
*                       ADD SMFID SYSTEM ID TO MODE LINE              *
*    04/12/83 U017 LDW  INDICATE SUSPENDED CH PGM VIA 'A-SPD'         *
*    03/24/83 U016 LDW  ADD CHECK FOR SUSPENDED CHANNEL PROGRAM       *
*    10/25/82 U015 LDW  ADD NEW DEVICE TYPES                          *
*                       FIX TAPE DENSITY LOGIC                        *
*                       REMOVE DEF=NO FOR NEW PFKEY MACRO             *
*    07/31/81 U014 SDM  FIX TO MAKE INPUT PARM WORK AGAIN             *
*    07/23/81 U013 LDW  DISPLAY 'SCRTCH' IF MOUNT PENDING VOLSER IS   *
*                          BAD                                        *
*    07/10/81 U012 SDM  CHANGE POSITION LOGIC TO UCBNAME FROM UCBCHAN *
*                       FIX PARM LENGTH SENSITIVE BUG                 *
*                       FIX HELP TEXT FOR UNPRIVILEGED USER (&GIS)    *
*    07/08/81 U011 SDM  COMPLETELY REORGANIZE SCROLLING LOGIC         *
*                       FIX 0C4 ABENDS WITH 'ON' OR 'OFF' OPERANDS    *
*                       DISPLAY QUEUED I/O MODE                       *
*    06/25/81 U010 SDM  CHANGE DEFAULT MODE TO ONLINE TAPES           *
*                       IMPLEMENT GIBRALTAR COND ASSEMBLY (&GIS)      *
*                       FORCE NORMAL USERS TO ONLINE TAPES (&GIS)     *
*    06/09/81 U009 SDM  FIX MOUNT PENDING LOGIC SOME MORE             *
*                       RECOMPUTE FIRST AUTO UPDATE SCREEN            *
*                       FIX BUG BACKING UP PAST FIRST UCB             *
*    03/23/81 U008 SDM  FIX MOUNT PENDING LOGIC                       *
*    01/26/81 U007 LDW  BUILD TABLE OF DEVICE ADDRESSES DISPLAYED, SO *
*                          THAT WE CAN SHOW ALL DEVICES EVEN IF A     *
*                          SWAP HAS OCCURRED, WHICH GETS THE UCB'S    *
*                          OUT OF ORDER, (THE OLD OPTCHAN CODE        *
*                          CODE SKIPPED THEM)                         *
*    01/12/81 U006 LDW  CHANGE 3277 TO 3278                           *
*                       ADD DEVICE TYPES: 3203, 3290, 3800,           *
*                                         3838, 3851, 3886            *
*                       DISPLAY 'C' INSTEAD OF 'A' FOR MCS CONSOLES   *
*    01/08/81 U005 SDM  RECOGNIZE MOUNT PENDING CONDITION             *
*                       MOVE CVTA TO DSECT (FIX REENTERABILITY)       *
*    08/12/80 U004 LDW  STANDARDIZE PF KEY USAGE TO CONFORM TO SPF    *
*                       CHANGE ENTER KEY TO REDISPLAY RATHER THAN     *
*                          SCROLL DOWN                                *
*                       ADD "+", "-", "." COMMANDS IN CASE NO PFK'S   *
*                       ADD HELP COMMAND                              *
*                       ACCEPT "END" TO EXIT                          *
*    04/29/80 U003 LDW  SHOW FILE SEQUENCE NUMBER FOR TAPES           *
*    04/14/80 U002 LDW  SHOW OWNER FOR TAPE DRIVES                    *
*                       DEFINE PF3 TO BE EXIT                         *
*                       MISCELLANEOUS CHANGES FOR NEW TERMIO$         *
*    10/12/79 U001 LDW  ADD SOME UNIT TYPES                           *
*                       CHANGE DEFAULT TO 'ONLINE' FROM 'ALL'         *
*---------------------------------------------------------------------*
         SPACE 4
         MACRO
         DX    &HEXTYP,&CHAR
         DC    X'&HEXTYP'
         DC    CL7&CHAR
         MEND
         SPACE 3
         MACRO
&NFS     $     &TEXT
&NFS     DC    CL79&TEXT
         MEND
         EJECT
         COPY  @GLOBALS                                            U018
         EJECT
         SPACE 3
*.
* DUCB  -
*   DISPLAY UNITS COMMAND FROM TSO/DCMS
*   ALL SYSGENNED UNITS ARE DISPLAYED STARTING FROM THE WORD ZERO
*
*   INPUT PARAMETERS ALLOWED -
*      D - DISPLAY DASD ONLY
*      T - DISPLAY TAPE ONLY
*      AL - DISPLAY ALL DEVICES
*      Q - DISPLAY ONLY DEVICES WITH QUEUE LENGTH > 0
*      CUU - DISPLAY BEGINNING AT UNIT 'CUU'
*
* PF KEY DEFINITIONS -
*
*    -----
*   | PF2 |   AUTO UPDATE MODE - REDISPLAY CURRENT SCREEN          U004
*   |     |
*    -----
*
*    -----
*   | PF3 |   EXIT                                                 U002
*   |     |
*    -----
*
*    -----
*   | PF4 |   SWITCH MODES - (A) DISPLAY ALL SYSGEN'D UNITS        U004
*   |     |                  (B) DISPLAY ONLY ONLINE UNITS
*    -----
*
*    -----
*   | PF7 |   SCROLL UP 20 UNITS
*   |     |
*    -----
*
*    -----
*   | PF8 |   SCROLL DOWN 20 UNITS
*   |     |
*    -----
*
*    -----
*   | PF9 |   RESTART AT FIRST UCB, CLEAR ALL FLAGS
*   |     |
*    -----
*
*
* REGISTER USAGE
*
*    R2 - POINTER TO CURRENT UCB LOOKUP TABLE ENTRY
*    R3 - POINTER TO CURRENT UCB NAME TABLE ENTRY
*    R4 - POINTER TO CURRENT SCREEN LINE ENTRY
*    R5 - POINTER TO THE CURRENT UCB
*    R7 - POINTER TO LAST UCB DISPLAYED
*    R9 - SUBROUTINE LINKAGE REGISTER
*   R11 - BASE+4096
*   R12 - BASE
*
*  THIS PROGRAM DOES NOT ATTEMPT TO FIND THE 'OPTCHAN' UNITS SO YOU
*  MAY SEE SOME UNITS APPEARING MORE THAN ONCE.
*
*  *SDC* CODE ATTEMPTS TO SKIP OPTCHAN DEVICE UCB'S, BUT MAY SKIP
*  OTHER DEVICES IF THERE HAS BEEN A 'SWAP' SINCE THE LAST IPL.
*
* IF YOU HAVE ANY QUESTIONS PLEASE CALL:
*
*     FRED LUDDY
*     AMDAHL CORP.
*     1250 E ARQUES
*     SUNNYVALE CA.
*   PHONE:
*     (408) 735 4011
*
*.
         EJECT
DUCB     DCMSTART RENT,LV=DCMSWLEN
         SPACE 1
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         USING DUCB+4096,R11
         SPACE 2
* INITIALIZE AREAS IN DSECT
         SPACE 1
         NI    IOCBFLGD,255-IOCBDYON+IOCBDFRC                      U002
         OI    IOCBFLGC,IOCBCRAO+IOCBCFRA                          U002
         SPACE 1
         MVC   DEVCLAS,=C'Tape'         DEFAULT = TAPE             U010
         MVI   FLAG1,F1ON+F1TAPE        SET TO ONLINE TAPES ONLY   U010
         MVI   FLAG2,0                  INIT OTHER FLAGS           U019
         SPACE 1
         L     R2,CVTPTR                AND IN THE BEGINNING...    U011
         USING CVT,R2                                              U018
         L     R15,CVTSMCA              -> SMF CONTROL BLOCK       U018
         MVC   SYSID,SMCASID-SMCABASE(R15)  SAVE SMF ID            U018
          AIF   (&@@SPLVL GE 130).SP01                             U018
         L     R2,CVTILK2               -> UCB WORDS               U011
         ST    R2,UCBLTBL               SAVE POINTER TO THE BEGIN  U011
         SR    R5,R5                    CLEAR FOR ICM (LATER)
          AGO   .SP02                                              U018
.SP01     ANOP                                                     U018
         MVC   IOSVSUCB,CVTUCBSC        SAVE IOSVSUCB ADDR         U018
         XC    UCBWORK,UCBWORK          INIT IOSVSUCB WORK AREA    U018
         LA    R14,UCBWORK              -> 100 BYTE WORK AREA      U018
         LA    R15,=X'00'               -> CLASS BYTE              U018
         LA    R0,UCBADDR               -> ANSWER AREA             U018
         STM   R14,R0,UCBPARM           SET UP PARMS FOR IOSVSUCB  U018
.SP02     ANOP                                                     U018
         DROP  R2                       CVT                        U018
         SPACE 1
         L     R1,0(,R1)                ADDRESS OF COMMAND STRING
         LH    R2,0(,R1)                LOAD COMMAND STRING LENGTH
         LH    R3,2(,R1)                LOAD OFFSET
         LA    R6,4(R1,R3)              BEGINNING OF SUBCOMMAND DATA
         SPACE 1
         L     R1,PSATNEW-PSA           -> MY TCB                  U018
         L     R1,TCBJSCB-TCB(,R1)      -> JSCB                    U018
         L     R1,JSCBPSCB-IEZJSCB(,R1) -> PSCB                    U018
         USING PSCB,R1                                             U018
         TM    &@@STAFF                 DATA CENTER STAFF?         U018
         BZ    *+8                      NO - SKIP                  U018
         OI    FLAG2,F2PRIV             INDICATE PRIVILEGED USER   U012
         TM    PSCBATR1,PSCBCTRL        OPERATOR AUTHORITY?        U019
         BZ    *+8                      NO - SKIP                  U019
         OI    FLAG2,F2OPER             YES - REMEMBER             U019
         DROP  R1                       IKJPSCB                    U018
         SPACE 1
INIT1    SR    R2,R3                    SUBTRACT OFFSET OF 'SUBCMDS'
         SH    R2,=H'4'                 SUB LEN OF HEADER          U012
         BNP   RESTART                  NO SUBCOMMANDS             U012
         SPACE 1
INSHIFT  EQU   *
         LR    R1,R6                    -> FIRST BYTE              U012
IS01     CLI   0(R1),C' '               ENDING BLANK?              U012
         BE    IS02                     YES, END OF DATA           U012
         LA    R1,1(,R1)                NO, INCREMENT POINTER      U012
         BCT   R2,IS01                  REDUCE COUNT AND LOOP      U012
IS02     LR    R2,R1                    -> END OF SUBCMD + 1       U012
         SR    R2,R6                    LENGTH OF SUBCMD           U012
         BNP   RESTART                  NO SUBCMD, IGNORE          U012
         BCTR  R2,0                     REDUCE FOR EX              U012
         SPACE 1
         EX    R2,UPSHIFT               SHIFT TO UPPERCASE
         SPACE 1
         CLC   =C'END',0(R6)            WANT OUT?                  U012
         BE    END1                     YES - LET HIM OUT          U004
         CLI   0(R6),C'H'               NEED HELP?                 U012
         BE    HELP                     YES - POOR GUY             U004
         CLI   0(R6),C'?'               REALLY NEED HELP?          U012
         BE    HELP                     YES - POOR GUY             U004
         CLC   =C'DA',0(R6)             DASD ONLY?                 U011
         BE    PARMDA                   YES - SET IT               U011
         CLI   0(R6),C'T'               TAPE ONLY?                 U011
         BE    PARMTA                   YES - SET IT               U011
         CLC   0(2,R6),=C'CO'           comm only?                 U024
         BE    PARMCOMM                 yes - set it               U024
         CLC   0(2,R6),=C'DI'           disp only?                 U024
         BE    PARMDISP                 yes - set it               U024
         CLI   0(R6),C'U'               u/r only?                  U024
         BE    PARMUR                   yes - set it               U024
         CLI   0(R6),C'Q'               DEVICES WITH I/O QUEUED?   U011
         BE    PARMQ                    YES - SET IT               U011
         CLC   =C'ON',0(R6)             ONLINE DEVICES ONLY?       U004
         BE    PARMON                   YES - SET IT               U011
         CLC   =C'OF',0(R6)             INCLUDE OFFLINE DEVICES?   U004
         BE    PARMOF                   YES - SET IT               U011
         CLC   0(2,R6),=C'AL'           ALL DEVICES?
         BE    DOALL                                               U011
         B     SETFIRST                                            U011
         SPACE 2
DOALL    MVC   DEVCLAS,=C'All '         SET CLASS MESSAGE
         NI    FLAG1,255-F1DEVTYP-F1QD  CLEAR types + Q FLAGS      U024
         B     RESTART
         SPACE 1
PARMDA   NI    FLAG1,255-F1DEVTYP       CLEAR types                U024
         OI    FLAG1,F1DASD             ...YES, SET FLAG
         MVC   DEVCLAS,=C'Dasd'         SET CLASS MESSAGE
         B     RESTART
         SPACE 1
PARMTA   NI    FLAG1,255-F1DEVTYP       CLEAR types                U024
         OI    FLAG1,F1TAPE             ...YES, SET FLAG
         MVC   DEVCLAS,=C'Tape'         SET CLASS MESSAGE
         B     RESTART                  AND PROCEED
         SPACE 1
PARMCOMM NI    FLAG1,255-F1DEVTYP       CLEAR types                U024
         OI    FLAG1,F1COMM             ...YES, SET FLAG           U024
         MVC   DEVCLAS,=C'Comm'         SET CLASS MESSAGE          U024
         B     RESTART                  AND PROCEED                U024
         SPACE 1
PARMDISP NI    FLAG1,255-F1DEVTYP       CLEAR types                U024
         OI    FLAG1,F1DISP             ...YES, SET FLAG           U024
         MVC   DEVCLAS,=C'Disp'         SET CLASS MESSAGE          U024
         B     RESTART                  AND PROCEED                U024
         SPACE 1
PARMUR   NI    FLAG1,255-F1DEVTYP       CLEAR types                U024
         OI    FLAG1,F1UR               ...YES, SET FLAG           U024
         MVC   DEVCLAS,=C'U/R '         SET CLASS MESSAGE          U024
         B     RESTART                  AND PROCEED                U024
         SPACE 1
PARMQ    OI    FLAG1,F1QD+F1ON          SET ONLINE AND QUEUED I/O
         B     RESTART
         SPACE 1
PARMON   OI    FLAG1,F1ON               SET ONLINE ONLY            U011
         NI    FLAG1,255-F1QD           RESET QUEUED I/O FLAG      U011
         B     RESTART                                             U011
         SPACE 1
PARMOF   NI    FLAG1,255-F1ON-F1QD      SET OFFLINE AND ONLINE     U011
         B     RESTART                                             U011
         SPACE 1
SETFIRST EQU   *
         MVC   FIRSTUCB,=3C'0'          CLEAR WORK AREA            U012
         CH    R2,=H'2'                 TOO LONG?                  U012
         BNH   *+8       >=====+        NO, USE IT AS IS           U012
         LH    R2,=H'2'        |        YES, CUT IT OFF            U012
         EX    R2,MOVEUCB  <===+        MOVE INPUT PARM TO PACK W.A.
         LA    R0,L'FIRSTUCB            LOOP COUNT                 U012
         LA    R1,FIRSTUCB              FIRST BYTE TO CHECK        U012
SF01     CLI   0(R1),C'A'               IS IT HIGH ENOUGH FOR HEX? U012
         BL    RESTART                  NO, IGNORE INPUT           U012
         CLI   0(R1),C'F'               IS IT A-F ?                U012
         BNH   SF02                     YES, IT'S OKAY             U012
         CLI   0(R1),C'0'               IS IT HIGH ENOUGH FOR NUM? U012
         BL    RESTART                  NO, IGNORE INPUT           U012
         CLI   0(R1),C'9'               IS IT 0-9 ?                U012
         BH    RESTART                  NO, IGNORE INPUT           U012
SF02     LA    R1,1(,R1)                -> NEXT BYTE               U012
         BCT   R0,SF01                  DECREMENT COUNT AND LOOP   U012
         XC    FIRSTUCB,=3C'0'          FIX COLLATING SEQUENCE     U012
         OI    FLAG2,F2POS              INDICATE POSITION REQUEST  U011
         B     RESTART                  AND PROCEED
         SPACE 1
MOVEUCB  MVC   FIRSTUCB(0),0(R6)        EXECUTED                   U012
UPSHIFT  OC    0(0,R6),BLANKS           EXECUTED
         SPACE 2
*   START DISPLAY WITH FIRST UCB
         SPACE 1
RESTART  MVC   UCBNBRT,=H'1'            DISPLAY FIRST UCB FOUND    U011
         NI    FLAG2,255-F2DISP         ASSUME NOTHING DISPLAYED   U020
         SPACE 1
*   START DISPLAY WITH 'UCBNBRT' UCB
         SPACE 1
REDISP   XC    UCBNBR,UCBNBR            NO UCB'S FOUND YET         U011
         NI    FLAG2,255-F2FOUND        ASSUME NOTHING FOUND       U019
          AIF   (&@@SPLVL GE 130).SP03                             U018
         L     R2,UCBLTBL               INIT UCB LOOKUP POINTER    U011
          AGO   .SP04                                              U018
.SP03     ANOP                                                     U018
         XC    UCBWORK,UCBWORK          START SCAN AT BEGINNING    U018
.SP04     ANOP                                                     U018
         LA    R3,NAMELIST              INIT NAME TABLE POINTER    U011
DU01     EQU   *
         TM    FLAG2,F2PRIV             PRIVILEGED USER?           U012
         BO    DU02                     YES, SKIP RESTRICTIONS     U012
*                                       NORMAL TSO USERS GET ONLY  U010
*                                          ONLINE TAPES WITHOUT    U010
*                                          AUTO UPDATE CLASS       U010
         NI    FLAG1,255-F1DEVTYP       CLEAR FLAGS                U024
         NI    FLAG2,255-F2AUTO         CLEAR FLAG                 U024
         NI    IOCBFLGS,255-IOCBFNRD           CLEAR AUTO FLAGS    U010
         NI    IOCBFLGD,255-IOCBDYON-IOCBDFRC  CLEAR AUTO FLAGS    U010
         OI    FLAG1,F1ON+F1TAPE        FORCE ONLINE TAPES ONLY    U010
         MVC   DEVCLAS,=C'Tape'         SET TAPES MESSAGE          U010
DU02     EQU   *                                                   U010
*                                       STARTING AT UCB ENTRY POINTED
*                                       TO BY R2
         LA    R8,20                    NUMBER OF UNITS TO DISPLAY
         LA    R0,FORMAT1               CLEAR SCREEN
         LA    R1,L1                        WORK AREA
         BAL   R14,@CLEAR                       USING DCMS SUBROUTINE
         MVC   L1,TITLE                 INITIALIZE TITLE LINE
         MVC   MODECLH,=C'Class ='         AND LAST LINE           U011
         MVC   MODECLAS,DEVCLAS                                    U011
         MVC   MODEMH,=C'Mode ='                                   U011
         MVC   MODEMODE,=CL19'Queued I/O only'                     U011
         TM    FLAG1,F1QD               QUEUED I/O ONLY?           U011
         BO    DU03                     YES, CONTINUE WITH DISPLAY U011
         MVC   MODEMODE,=CL19'Online only'                         U011
         TM    FLAG1,F1ON               ONLINE ONLY?               U011
         BO    DU03                     YES, CONTINUE WITH DISPLAY U011
         MVC   MODEMODE,=CL19'Online and Offline'                  U011
         SPACE 1
DU03     EQU   *
         MVC   MODESYS,=C'SYS = '                                  U018
         MVC   MODEID,SYSID             FILL IN SMF SYSTEM ID      U018
         LA    R4,L1                    GET ADDRESS OF CURRENT ENTRY
         USING OUTLINE,R4
         SPACE 1
DU04     EQU   *                        BCT LOOP HERE...
*                                       ...FOR EACH LINE OF OUTPUT.
         LA    R4,79(,R4)               GET NEXT LINE ADDRESS
         SPACE 1
DU05     EQU   *                        LOOP TO HERE FOR NEXT UCB.
          AIF   (&@@SPLVL GE 130).SP05                             U018
         CLC   0(2,R2),=X'FFFF'         END OF UCB TABLE?
         BE    PUTOUT                   ...YES, STOP HERE.
         ICM   R5,3,0(R2)               SEE IF WE HAVE A LIVE ONE
         BNZ   DU07                     THIS UNIT DOES EXIST
         USING UCBOB,R5                                            U011
         SPACE 1
DU06     EQU   *
         LA    R2,2(,R2)                GET NEXT
         B     DU05
         SPACE 1
          AGO   .SP06                                              U018
.SP05     ANOP                                                     U018
         USING UCBOB,R5                                            U018
         SPACE 1
DU06     EQU   *                                                   U018
         LA    R1,UCBPARM               -> PARMLIST FOR IOSVSUCB   U018
         L     R15,IOSVSUCB             -> LOOKUP ROUTINE          U018
         BALR  R14,R15                  INVOKE IOSVSUCB            U018
         CH    R15,=H'4'                DID IT WORK?               U018
         BE    PUTOUT                   YES - NO MORE UCBS         U018
         BL    *+8                      YES - WE GOT A UCB         U018
         EX    0,*                      NO - INVALID RC            U018
         L     R5,UCBADDR               GET RETURNED UCB ADDR      U018
.SP06     ANOP                                                     U018
DU07     EQU   *
         SPACE 1
*    HAVE A POSSIBLE CANDIDATE.  CHECK FLAGS FOR RESTRICTIONS
*    INDICATED BY USER, AND VERIFY THAT DEVICE MATCHES REQUEST.
         SPACE 1
*  BUT FIRST LET'S SEE IF WE ALREADY DID THIS ONE                  U007
         LA    R14,NAMELIST             -> FIRST                   U007
         LA    R0,3                     INCREMENT                  U007
PREVLOOP CR    R14,R3                   END OF LIST?               U011
         BNL   SHOWNOW                  YES, DISPLAY UNIT          U011
         CLC   UCBNAME(3),0(R14)        THIS IT?                   U007
         BE    DU06                     YES - DON'T DISPLAY AGAIN  U007
         AR    R14,R0                   -> NEXT SLOT               U007
         B     PREVLOOP                 KEEP LOOKING               U007
*
*
SHOWNOW  MVC   0(3,R14),UCBNAME         SAVE DEVICE ADDRESS        U007
         AR    R3,R0                    -> NEXT SLOT               U011
*
         TM    FLAG1,F1ON               ONLINE ONLY ?
         BZ    DU08                     NO..
         TM    UCBSTAT,UCBONLI
         BZ    DU06                     NOT ONLINE, IGNORE
*
DU08     EQU   *
         TM    FLAG1,F1DASD             LIST DASD ONLY?
         BZ    CHKTP                    ...NO
         TM    UCBTBYT3,UCB3DACC        ...YES, CHECK FOR DASD
         BZ    DU06                     NOT DASD, SKIP
         B     GOTONE                   ...OK, FORMAT IT
*
CHKTP    TM    FLAG1,F1TAPE             LIST TAPE ONLY?
         BZ    CHKCOMM                  ...NO, PROCEED
         TM    UCBTBYT3,UCB3TAPE        ...YES, CHECK FOR TAPE
         BZ    DU06                     NOT TAPE, SKIP
         B     GOTONE                   ...OK, FORMAT IT           U024
*
CHKCOMM  TM    FLAG1,F1COMM             LIST comm ONLY?            U024
         BZ    CHKDISP                  ...NO, PROCEED             U024
         TM    UCBTBYT3,UCB3COMM        ...YES, CHECK FOR comm     U024
         BZ    DU06                     NOT comm, SKIP             U024
         B     GOTONE                   ...OK, FORMAT IT           U024
*
CHKDISP  TM    FLAG1,F1DISP             LIST disp ONLY?            U024
         BZ    CHKUR                    ...NO, PROCEED             U024
         TM    UCBTBYT3,UCB3DISP        ...YES, CHECK FOR disp     U024
         BZ    DU06                     NOT disp, SKIP             U024
         B     GOTONE                   ...OK, FORMAT IT           U024
*
CHKUR    TM    FLAG1,F1UR               LIST u/r  ONLY?            U024
         BZ    GOTONE                   ...NO, PROCEED             U024
         TM    UCBTBYT3,UCB3UREC        ...YES, CHECK FOR u/r      U024
         BZ    DU06                     NOT u/r , SKIP             U024
*
GOTONE   EQU   *                        PASSED DASD/TAPE CRITERIA
*   NOW CONTROL PAGING BY BYPASSING ACCEPTABLE UCB'S UNTIL THE
*   CONTROL COUNTER IS EQUAL TO THE TOP OF SCREEN TARGET.
         LA    R1,1                                                U011
         AH    R1,UCBNBR                                           U011
         STH   R1,UCBNBR                                           U011
         CH    R1,UCBNBRT               HAVE WE HIT TOP OF SCREEN? U011
         BL    DU06                     NO, SKIP THIS ONE          U011
         SPACE 1
*   TEST FOR POSITION BY NAME
CKPOS    TM    FLAG2,F2POS              POSITIONING BY NAME?       U011
         BNO   TESTQ                    NO, SKIP THIS TEST         U011
         MVC   DWORK(3),UCBNAME         GET UCB IDENTIFIER         U012
         XC    DWORK(3),=3C'0'          FIX COLLATING SEQUENCE     U012
         CLC   DWORK(3),FIRSTUCB        IS IT PAST FIRST REQ'D UCB?U012
         BL    DU06                     ...NO, SKIP IT.
         NI    FLAG2,255-F2POS          RESET POSITIONING FLAG     U011
         MVC   UCBNBRT,UCBNBR           SET TOP OF SCREEN NUMBER   U011
         SPACE 1
*   NOW TEST FOR I/O QUEUED RESTRICTIONS
TESTQ    TM    FLAG1,F1QD               ONLY DEVICES W/ I/O QUEUED?
         BZ    MOVEIT                   ...NO, THEN THIS ONE IS OK.
          AIF   (&@@SPLVL GE 200).SPX1                             U022
         CLI   UCBCNT,X'00'             ANY QUEUE?
         BZ    DU06                     ...NO, SKIP THIS ONE.
.SPX1     ANOP                                                     U018
*
*  THIS UCB HAS PASSED ALL THE SELECTION CRITERIA.
*  BEGIN FORMATTING OUTPUT INFORMATION.
*
MOVEIT   EQU   *
         OI    FLAG2,F2FOUND+F2DISP     WE FOUND SOMETHING         U020
         MVC   OUTUNIT(3),UCBNAME       MOVE THE EBCDIC NAME
*
* DETERMINE UNIT TYPE..
*
         CLC   UCBTBYT1(4),=F'0'        DUMMY?                     U015
         BNE   DU09A                    NO - TRY NEXT              U015
         MVC   OUTTYPE,=CL8'Dummy'      YES - SHOW IT              U015
         B     DU14                     AND PRESS ON               U015
DU09A    EQU   *                                                   U015
         CLI   UCBTBYT3,UCB3TAPE        IS THIS A TAPE
         BNE   DU09                     NO, TRY NEXT
         BAL   R9,ITSATAPE              GO DO TAPE UNIT STUFF
         B     DU14                     AND PRESS ON
DU09     EQU   *
         CLI   UCBTBYT3,UCB3COMM        IS THIS A TERMINAL
         BNE   DU10                     NO, TRY NEXT
         BAL   R9,ITSACOMM              GO DO TERMINAL
         B     DU14                     AND PRESS ON
DU10     EQU   *
         CLI   UCBTBYT3,UCB3CTC         IS THIS A CTC
         BNE   DU11                     NO, THANK GOD IF THIS A 470
         BAL   R9,ITSACTC               GO DO CTC STUFF
         B     DU14                     AND PRESS ON
DU11     EQU   *
         CLI   UCBTBYT3,UCB3DACC        IS THIS DASD ?
         BNE   DU12                     NO, WHATS UP DOC ?
         BAL   R9,ITSADASD              GO DO DASDSTUFF
         B     DU14                     AND PRESS ON
DU12     EQU   *
         CLI   UCBTBYT3,UCB3DISP        IS THIS GRAPHICS
         BNE   DU13                     NO.. HOPE FOR UR
         BAL   R9,ITSATUBE              GO DOWN THE TUBES
         B     DU14                     AND PRESS ON
DU13     EQU   *
         CLI   UCBTBYT3,UCB3UREC        IS THIS UNIT RECORD ?
         BNE   DU14                     NO.. LEAVE THE DAMN THING BLANK
         BAL   R9,ITSAUR                GO DO THE UNIT RECORD STUFF
*
DU14     EQU   *
*
* ANALYZE THE DEVICES CURRENT STATUS, ONLINE OR OFF, ALLOCATED, OR NOT
* BUSY OR NOT, ETC...
*
         MVC   OUTSTAT(7),=C'Offline'   DEFAULT TO OFFLINE
         TM    UCBFLA,UCBBOX            is it boxed?               U022
         BZ    *+10                     no - skip                  U022
         MVC   OUTSTAT(7),=CL7'F-Box'   show boxed                 U022
         TM    UCBSTAT,UCBONLI          IS THIS DEVICE ONLINE ?
         BZ    DU15                     NO, PRESS ON
         MVC   OUTSTAT(7),BLANKS        RESET A MINUTE
         MVI   OUTSTAT,C'O'             SHOW IT'S ONLINE
*??      TM    UCBIOSF1,UCBRESVH        is it reserved?            U022
*??      BZ    *+10                     no - skip                  U022
*??      MVC   OUTSTAT+5(2),=C'-R'      show reserved              U022
*
* SEE WHATS HAPPENING WITH ONLINE DEVICES
*
         TM    UCBSTAT,UCBALOC          IS IT ALLOCATED
         BZ    *+8                      NO.. SKIP MVI
         MVI   OUTSTAT,C'A'             SHOW ALLOCATED
         CLI   UCBTBYT3,UCB3DACC        IS IT A DISK?              U006
         BE    NOTCONS                  YES.. SKIP NEXT TEST       U006
         TM    UCBSTAT,UCBSYSR          IS IT A CONSOLE?           U006
         BZ    NOTCONS                  NO.. SKIP MVI              U006
         MVI   OUTSTAT,C'C'             SHOW CONSOLE               U006
NOTCONS  TM    UCBFLA,UCBNRY            NOT READY ?
         BZ    NOTMTP                   NO, AND SKIP MTP LOGIC TOO U009
         MVC   OUTSTAT+1(5),=C'-Nrdy'
         CLI   UCBTBYT3,UCB3DACC        DIRECT ACCESS DEVICE?      U008
         BNE   MP01                     NO, SKIP DISK MTP TEST     U008
         TM    UCBDMCT,UCBMOUNT         MOUNT PENDING?             U008
         BO    MP02                     YES, GO SAY SO             U008
         B     NOTMTP                   LEAVE QUIETLY              U008
MP01     CLI   UCBTBYT3,UCB3TAPE        TAPE DEVICE?               U008
         BNE   NOTMTP                   NO, SKIP TAPE MTP TEST     U008
         TM    UCBSTAB,UCBBNUL          MESSAGES OUTSTANDING?      U008
         BNO   NOTMTP                   NO, ASSUME NO MOUNT        U008
MP02     MVC   OUTSTAT+1(5),=C'-MTP '   OVERLAY NOTREADY IN OUTPUT U008
         CLC   OUTVOL(6),UCBVOLI        WAS VOLSER READABLE?       U013
         BE    *+10                     YES - OK                   U013
         MVC   OUTVOL(6),=C'Scrtch'     NO - MTP MUST BE 'SCRTCH'  U013
NOTMTP   TM    UCBFLA,UCBBOX            is it boxed?               U027
         BZ    *+10                     no - skip                  U027
         MVC   OUTSTAT(7),=CL7'O-Box'   show boxed                 U027
          AIF   (&@@SPLVL GE 200).SPX4                             U022
         TM    UCBFLA,UCBBSY            IS IT BUSY ?
          AGO   .SPX5                                              U022
.SPX4     ANOP                                                     U022
         TM    UCBFLA,UCBSTRT           start subchannel issued?   U022
.SPX5     ANOP                                                     U022
         BZ    DU17                     NO..
         MVC   OUTSTAT+1(5),=C'-Busy'   SHOW BUSY
*
* ATTEMPT TO FIND OUT WHY
*
         TM    UCBFLA,UCBPSNS           PENDING SENSE ?
         BZ    DU15                     NO
         MVC   OUTNOTE(13),=C'Sense pending'  SHOW REASON          U003
         B     DU17                     GET OUT
DU15     EQU   *
          AIF   (&@@SPLVL GE 200).SPX6                             U022
         TM    UCBFLA,UCBCUB            IS THE CONTROL UNIT BUSY ?
         BZ    DU16                     NO, SEE IF CHANNEL PGM ACTIVE
         MVC   OUTNOTE(17),=C'Control Unit busy'                   U003
         B     DU17                     AND SPLIT
.SPX6     ANOP                                                     U022
DU16     EQU   *
         L     R1,UCBEXTPT              -> COMMON EXTENSION        U017
         USING UCBCMEXT,R1                                         U017
         TM    UCBFL7,UCBSSPND          CHANNEL PGM SUSPENDED?     U017
         BZ    DU16A                    NO - SEE IF CH PGM ACTIVE  U017
         MVC   OUTSTAT+2(4),=C'SPD '    INDICATE SUSPENDED         U017
         B     DU17                     GET OUT                    U017
         DROP  R1                                                  U017
DU16A    EQU   *
          AIF   (&@@SPLVL GE 200).SPX2                             U022
         TM    UCBFLA,UCBACTV           CHAN PGM ACTIVE
         BZ    DU17                     SORRY.. I GIVE UP
         MVC   OUTNOTE(22),=C'Channel program active'              U002
.SPX2     ANOP                                                     U022
DU17     EQU   *
         LR    R1,R5                    GET THE ADDRESS OF THE UCB
          AIF   (&@@SPLVL GE 200).SPX3                             U022
         SR    R6,R6                    CLEAR FOR IC
         IC    R6,UCBCNT                GET CURRENT QUEUE LENGTH
         CVD   R6,DWORK                 TO DECIMAL
         UNPK  DWORK(3),DWORK+6(2)
         OI    DWORK+2,X'F0'            SET ZONE
         MVC   OUTQLEN+2(3),DWORK       MOVE TO OUTPUT FIELD
.SPX3     ANOP                                                     U022
          AIF   (&@@SPLVL LT 130).SP07                             U018
         LA    R2,2(,R2)                GET PTR TO NEXT LOOK UP
.SP07     ANOP                                                     U018
         BCT   R8,DU04                  LOOP FOR THE NEXT
         TITLE 'WRITE SCREEN AND (OPTIONALLY) READ INPUT'
PUTOUT   EQU   *
         NI    FLAG2,255-F2POS          CLEAR POSITION REQUEST     U011
         TM    FLAG2,F2DISP             ANYTHING DISPLAYED?        U020
         BZ    NOTHING                  NOPE - SPECIAL MESSAGE     U020
         TM    FLAG2,F2FOUND            DID WE FIND ANYTHING?      U019
         BZ    RESTART                  NO - START OVER            U019
*
PUTBAL   EQU   *                        DOIT JACK !!!
         NI    FLAG2,255-F2FOUND        RESET "SOMETHING FOUND"    U020
         TM    FLAG2,F2AUTO             AUTO UPDATE?               U024
         BO    WRITE                    ...YES, DO WRITE ONLY
*
PUTHELP  TRMIO IOCB,FORMAT=FORMAT1,IMAGE=L1    WRITE/READ
*
*
         PFKEY DEF=REDISP,ATTN=END1,ENTER=ENTER,RESHOW=PUTHELP,    U011$
               K1=HELP,K2=AUTO,K3=END1,K4=FLIPMODE,                U011$
               K7=BACK,K8=FORWARD,K9=DOALL,K12=END1                U004
*
WRITE    EQU   *                            WRITE ONLY
         TRMIO IOCB,WRITE,FORMAT=FORMAT1,IMAGE=L1
         SPACE 1
         PFKEY ATTN=AUTOOFF                                        U015
         SPACE 1
         B     AUTO                     GO UPDATE THE DISPLAY      U020
         SPACE 3
NOTHING  MVC OUTSTAT+79*2(31),=C'No devices meet search criteria'  U020
         B     PUTBAL                   DISPLAY ERROR MESSAGE      U020
         TITLE 'INPUT HANDLING SUBROUTINES'
AUTO     EQU   *                        AUTO UPDATE CLASS REQUESTED
         OI    FLAG2,F2AUTO                                        U024
         OI    IOCBFLGS,IOCBFNRD        NO READ
         OI    IOCBFLGD,IOCBDYON+IOCBDFRC
         B     REDISP                                              U009
         SPACE 1
AUTOOFF  EQU   *                        TURN OFF AUTO UPDATE
         NI    FLAG2,255-F2AUTO                                    U024
         NI    IOCBFLGS,255-IOCBFNRD
         NI    IOCBFLGD,255-IOCBDYON-IOCBDFRC
         B     PUTBAL
         SPACE 1
FORWARD  EQU   *                        GET NEXT 20 UNITS
          AIF   (&@@SPLVL GE 130).SP09                             U018
         CLC   0(2,R2),=X'FFFF'         ARE WE AT END ALREADY?
         BE    RESTART                  ...YES, SO GO BACK TO ZERO.U011
          AGO   .SP10                                              U018
.SP09     ANOP                                                     U018
         OC    UCBWORK,UCBWORK          ARE WE AT END ALREADY?     U018
         BZ    RESTART                  ...YES, SO GO BACK TO ZERO.U018
.SP10     ANOP                                                     U018
         LA    R1,20                                               U011
         AH    R1,UCBNBRT               NEW CURRENT UCB            U011
         STH   R1,UCBNBRT               SET NEW TOP OF SCREEN      U011
         B     DU01                     GO GET NEXT SET OF UCBS.
         SPACE 1
ENTER    EQU   *                        HIT ENTER KEY
         LA    R0,L'ENTRLN              LENGTH OF DATA             U012
         LA    R6,ENTRLN                FIRST INPUT BYTE           U012
ENTER1   CLI   0(R6),C' '               LEADING BLANK?             U012
         BNE   ENTER2                   NO, TEST THE DATA          U012
         LA    R6,1(,R6)                -> NEXT BYTE               U012
         BCT   R0,ENTER1                DECREMENT LENGTH AND LOOP  U012
         B     REDISP                   NO DATA, GO REDISPLAY      U012
ENTER2   CLI   0(R6),C'.'               PAGE FORWARD?              U012
         BE    FORWARD                  YES - DO IT                U004
         CLI   0(R6),C'+'               PAGE FORWARD?              U012
         BE    FORWARD                  YES - DO IT                U004
         CLI   0(R6),C'-'               PAGE BACKWARD?             U012
         BE    BACK                     YES - DO IT                U004
         CLI   0(R6),C'/'               MVS OPERATOR COMMAND?      U019
         BE    MVS$CMD                  YES - DO IT                U019
         LR    R2,R0                    DATA LENGTH FOR 'INSHIFT'  U012
         B     INSHIFT                  JUST GO TO PARM ANALYSIS
         SPACE 1
MVS$CMD  TM    FLAG2,F2OPER             PRIVILEGED USER?           U019
         BZ    REDISP                   NO - IGNORE IT             U019
         MVI   0(R6),C' '               REMOVE THE '/'             U019
         CLC   ENTRLN+1(L'ENTRLN-1),BLANKS  ANY COMMAND HERE?      U019
         BE    REDISP                   NO - IGNORE IT             U019
         LA    R15,CMDPARM              -> COMMAND                 U019
         ST    R15,CMDADDR              SET IN PARMLIST            U019
         OI    CMDADDR,X'80'            MARK END OF LIST           U019
         BCTR  R0,0                     -1 FOR '/'                 U019
         STH   R0,CMDPARM               STORE CMD LENGTH IN PARM   U019
         LR    R14,R0                   COPY LENGTH                U019
         MVC   CMDPARM+2(L'ENTRLN),BLANKS  CLEAR RECEIVING AREA    U019
         BCTR  R14,0                    -1 FOR EX                  U019
         EX    R14,CMD$MVC              COPY TEXT TO PARM AREA     U019
         LA    R1,CMDADDR               -> PARMLIST                U019
         LINK  EP=OPERCMD               ISSUE THE COMMAND & WTO    U019
         B     REDISP                   ALL DONE                   U019
         SPACE 1
CMD$MVC  MVC   CMDPARM+2(*-*),1(R6)     << EXECUTED >>             U019
         SPACE 2
FLIPMODE EQU   *                        SWITCH ONLINE/OFFLINE MODES
         TM    FLAG1,F1QD               QUEUED I/O MODE?           U011
         BO    PARMON                   YES, GO SET ONLINE MODE    U011
         TM    FLAG1,F1ON               ONLINE MODE?               U011
         BO    PARMOF                   YES, GO SET OFFLINE MODE   U011
         B     PARMON                   OTEHRWISE, SET ONLINE      U011
         SPACE 1
*
* COUNT BACK UP 20 UNITS PRIOR TO *FIRST* ONE ON SCREEN
*
BACK     EQU   *
         LH    R1,UCBNBRT               TOP OF SCREEN UCB NUMBER   U011
         SH    R1,=H'20'                REDUCE BY TWENTY           U011
         BNP   RESTART                  NOT OKAY, START AT TOP     U011
         STH   R1,UCBNBRT               SAVE NEW TOP OF SCREEN     U011
         B     REDISP                   AND GO FORMAT DISPLAY      U011
         SPACE 3
HELP     LA    R14,L1                   DEST ADDR                  U004
         LH    R15,FORMAT1              DEST LEN                   U004
         LA    R0,HELPIMG               SOURCE ADDR                U004
         LA    R1,HELPLEN               SOURCE LEN                 U004
         TM    FLAG2,F2PRIV             PRIVILEGED USER?           U012
         BO    HELP1                    YES, WE HAVE RIGHT IMAGE   U012
         LA    R0,HELPIMG1              NO, POINT TO NEW IMAGE     U004
         LA    R1,HELPLEN1                 AND NEW IMAGE LENGTH    U004
HELP1    ICM   R1,B'1000',BLANKS        PAD CHAR                   U004
         MVCL  R14,R0                   COPY THE HELP SCREEN       U004
         B     PUTHELP                  GO WRITE IT                U004
         TITLE 'DEVICE TYPE SPECIFIC SUBROUTINES'
ITSACTC  EQU   *                        UCBTYPE3=X'41'
         MVC   OUTTYPE,=CL7'CTC'                                   U023
         BR    R9
         SPACE 2
ITSAUR   EQU   *                        UCBTYPE3=X'08'
         LA    R1,URTYPES               GET DEVICE TYPES ADDRESSES
UR0      EQU   *
         CLC   UCBUNTYP,0(R1)           IS IT THIS ?
         BE    UR1                      SHORE IS
         LA    R1,TYPESIZE(R1)          GET NEXT IF NOT
         CLI   0(R1),255                STOPPER ?
         BNE   UR0
UR1      EQU   *
         MVC   OUTTYPE,1(R1)            MOVE THE DESCRIPTOR
         BR    R9
         SPACE 2
ITSACOMM EQU   *                        UCBTYPE3=X'40'
         LA    R1,COMMTYPS              GET DEVICE TYPES ADDRESSES
         MVC   DWORK(1),UCBTBYT1
         NI    DWORK,15                 GET MODEL CODE
CO0      EQU   *
         CLC   DWORK(1),0(R1)           IS IT THIS ?
         BE    CO1                      SHORE IS
         LA    R1,TYPESIZE(,R1)         GET NEXT IF NOT
         CLI   0(R1),255                STOPPER ?
         BNE   CO0
         MVC   DWORK(1),UCBUNTYP
         NI    DWORK,15                 GET CONTROL UNIT-TYPE
         LA    R1,COMMCUS
CO00     EQU   *
         CLC   DWORK(1),0(R1)           IS IT THIS ?
         BE    CO1                      SHORE IS
         LA    R1,TYPESIZE(,R1)         GET NEXT IF NOT
         CLI   0(R1),255                STOPPER ?
         BNE   CO00
CO1      EQU   *
         MVC   OUTTYPE,1(R1)            MOVE THE DESCRIPTOR
         BR    R9
         SPACE 2
ITSATUBE EQU   *                        UCBTYPE3=X'10'
         LA    R1,TUBETYPS              GET DEVICE TYPES ADDRESSES
TU0      EQU   *
         CLC   UCBUNTYP,0(R1)           IS IT THIS ?
         BE    TU1                      SHORE IS
         LA    R1,TYPESIZE(,R1)         GET NEXT IF NOT
         CLI   0(R1),255                STOPPER ?
         BNE   TU0
TU1      EQU   *
         MVC   OUTTYPE,1(R1)            MOVE THE DESCRIPTOR
         BR    R9
         SPACE 2
ITSATAPE EQU   *                        UCBTYPE3=X'80'
         LA    R1,TAPETYPS              GET DEVICE TYPES ADDRESSES
TA0      EQU   *
         CLC   UCBUNTYP,0(R1)           IS IT THIS ?
         BE    TA1                      SHORE IS
         LA    R1,TYPESIZE(,R1)         GET NEXT IF NOT
         CLI   0(R1),255                STOPPER ?
         BNE   TA0
TA1      EQU   *
         MVC   OUTTYPE,1(R1)            MOVE THE DESCRIPTOR
         TM    UCBSTAT,UCBONLI          IS IT ONLINE ?
         BZ    TA10                     NO.. MOVE ON
         MVC   OUTVOL(6),UCBVOLI        MOVE THE VOL-SER
         TR    OUTVOL(6),BLANKTAB       MAKE SURE IT IS READABLE
*
* CHECK  OUT THE DENSITIES AVAILABLE
*
TA10     EQU   *
         TM    UCBTBYT2,UCBDUDN1        DUAL  800/1600 ?           U015
         BZ    TA2                      NO..
         MVC   OUTNOTE(9),=CL9'800/1600'
         MVC   OUTTYPE+4(2),=C'-4'      3400-4                     U004
         TM    UCBTBYT2,UCB2OPT1        SUPPORT ASCII ?
         BZ    TA7                      NO..
         MVC   OUTNOTE+9(5),=C'ASCII'
         B     TA7
TA2      EQU   *
         TM    UCBTBYT2,UCBDUDN2        1600/6250 ?
         BZ    TA3                      NO..
         MVC   OUTNOTE(9),=C'1600/6250'
         MVC   OUTTYPE+4(2),=C'-6'      3400-6                     U004
         B     TA7
TA3      EQU   *
         TM    UCBTBYT2,UCB2OPT0        7-TRACK ?
         BZ    TA4                      NO..
         MVC   OUTNOTE(7),=C'7-Track'                              U023
         MVC   OUTTYPE+4(2),=C'-2'      3400-2                     U004
         B     TA7
TA4      EQU   *
         TM    UCBTBYT1,UCBD1600        1600 BPI ?
         BZ    TA5
         MVC   OUTNOTE(4),=C'1600'
         MVC   OUTTYPE+4(2),=C'-3'      3400-3                     U004
         B     TA7
TA5      EQU   *
         TM    UCBTBYT1,UCBD6250        6250 BPI ?
         BZ    TA7                      NO..
         MVC   OUTNOTE(4),=C'6250'
         MVC   OUTTYPE+4(2),=C'-5'      3400-5                     U004
TA7      EQU   *
         TM    UCBSTAT,UCBALOC          IS IT ALLOCATED?           U002
         BNO   TA9                      NO - SKIP                  U002
*U024    MVC   OUTNOTE+26(5),=C'file='                             U003
         LH    R0,UCBFSEQ               GET FILE SEQUENCE NUMBER   U003
         CVD   R0,DWORK                                            U003
         OI    DWORK+7,X'0F'            REMOVE SIGN                U003
         UNPK  OUTNOTE+31(5),DWORK+5(3) PUT ON SCREEN              U023
         MVC   OUTNOTE+27(5),=C'file='                             U024
         L     R1,UCBEXTPT              -> COMMON EXTENSION        U002
         LH    R15,UCBASID-UCBCMEXT(,R1)  GET ASID OF OWNER        U002
         BCTR  R15,0                    APPLY FUDGE FACTOR         U002
         SLL   R15,2                    * 4 BYTES / ENTRY          U002
         L     R1,16                    -> CVT                     U002
         L     R1,CVTASVT-CVT(,R1)      -> ASVT                    U002
         L     R1,ASVTENTY-ASVT(R15,R1) -> ASCB                    U002
         MVC   OUTSTATE(3),=C'-->'      FLAG IT                    U002
         L     R15,ASCBJBNI-ASCB(,R1)   -> JOBNAME                 U002
         LTR   R15,R15                  OK?                        U002
         BNZ   TA8                      YES - CONTINUE             U002
         L     R15,ASCBJBNS-ASCB(,R1)   -> JOBNAME FOR S/M/L       U002
         LTR   R15,R15                  OK?                        U002
         BZ    TA9                      NO - BYPASS                U002
TA8      EQU   *                                                   U002
         MVC   OUTSTATE+3(8),0(R15)     MOVE IN JOBNAME            U002
TA9      EQU   *                                                   U002
         TM    UCBWGT,UCBREW            is it rewinding?           U025
         BZ    *+10                     no                         U025
         MVC   OUTNOTE(15),=C'>> rewinding <<'                     U025
         BR    R9
         SPACE 2
ITSADASD EQU   *                        UCBTYPE3=X'20'
         TM    UCBWGT,UCBMTPXP          IS THIS THE DOLDRUMS ?
         BZ    DA00                     NO..
         TM    UCBCHAN+1,15             IS THIS THE BASE EXPOSURE? U022
         BNZ   DU06                     NOPE.. GOTO NEXT
DA00     EQU   *
         LA    R1,DASDTYPS              GET DEVICE TYPES ADDRESSES
DA0      EQU   *
         CLC   UCBUNTYP,0(R1)           IS IT THIS ?
         BE    DA1                      SHORE IS
         LA    R1,TYPESIZE(,R1)         GET NEXT IF NOT
         CLI   0(R1),255                STOPPER ?
         BNE   DA0
DA1      EQU   *
         MVC   OUTTYPE,1(R1)            MOVE THE DESCRIPTOR
*U022    TM    UCBTBYT2,UCBRVDEV        IS THIS VIO
         TM    UCBJBNR,UCBVRDEV         IS THIS VIO                U022
         BZ    DA10                     NO..
         MVC   OUTVOL,=CL7'V-I-O'       INDICATE SO..
DA10     EQU   *
         CLC   =C'3380',OUTTYPE         IS IT SOMETHING STUPID?    U021
         BNE   DA11                     NO - NORMAL DEVICE         U021
         L     R1,UCBEXTPT              -> COMMON EXTENSION        U021
         ICM   R1,B'1111',DCEUCBEX(R1)  -> DCE                     U021
         BZ    DA11                     NONE - CAN'T BE 3380E      U021
         USING DCE,R1                   DASD CLASS EXTENSION       U021
         TM    DCEFLAG1,DCEVALFS        DEVICE FIELDS VALID?       U021
         BZ    DA11                     NO - CAN'T BE 3380E/K      U021
         MVI   OUTTYPE+4,C'K'           assume 3380K               U024
         CLC   =H'2656',DCEUDCYL        # OF DATA+ALT CYL ==> "K"  U024
         BE    DA11                     yes - continue             U024
         MVI   OUTTYPE+4,C'E'           assume 3380E               U024
         CLC   =H'1771',DCEUDCYL        # OF DATA+ALT CYL ==> "E"  U021
         BE    DA11                     yes - continue             U024
         MVI   OUTTYPE+4,C' '           assume standard 3380       U024
         DROP  R1                       DCE                        U021
DA11     EQU   *                                                   U021
         TM    UCBSTAT,UCBONLI          IS IT ONLINE ?
         BZR   R9                       NO.. RETURN
         TM    UCBSTAB,UCBPGFL          IS THERE PAGING ON THIS PACK
         BZ    *+10
         MVC   OUTNOTE(13),=CL13'<Paging-pack>'  TELL THE USER
         MVC   OUTVOL(6),UCBVOLI        MOVE THE VOL-SER
         TR    OUTVOL(6),BLANKTAB       MAKE SURE IT IS READABLE
         TM    UCBSTAT,UCBSYSR          IS THIS THE SYSRES ?
         BZ    DA2                      NO... PRESS ON
         MVC   OUTSTATE(10),=CL10'<-SYSRES->'  INDICATE SO
         BR    R9                       AND RETURN
         SPACE
DA2      EQU   *
         TM    UCBSTAB,UCBBPRV          IS THIS VOL PRIVATE ?
         BZ    DA3                      NO..
         MVC   OUTSTATE,=C'Priv'        SHOW IT IS SO
DA3      EQU   *
         TM    UCBSTAB,UCBBPUB          IS IT PUBLIC ?
         BZ    DA4                      NO..
         MVC   OUTSTATE,=C'Publ'
DA4      EQU   *
         TM    UCBSTAB,UCBBSTR          STORAGE ?
         BZ    DA5                      NO...
         MVC   OUTSTATE,=C'Strg'
DA5      EQU   *
         TM    UCBSTAT,UCBRESV          IS IT RESERVED ?
         BZ    DA6                      NO..
         MVC   OUTSTAT2,=C'/Rserv'
DA6      EQU   *
         TM    UCBSTAT,UCBPRES          PERMANENTLY RESIDENT
         BZ    DA7                      NO..
         MVC   OUTSTAT2,=CL7'/Rsdnt'
DA7      EQU   *
         BR    R9
         TITLE 'D U - DATA AREAS AND THE LIKE'
FORMAT1  DCMSFMT
L1       DCMSFLD  79
L2       DCMSFLD  79,INTEN=LO
L3       DCMSFLD  79,INTEN=LO
L4       DCMSFLD  79,INTEN=LO
L5       DCMSFLD  79,INTEN=LO
L6       DCMSFLD  79,INTEN=LO
L7       DCMSFLD  79,INTEN=LO
L8       DCMSFLD  79,INTEN=LO
L9       DCMSFLD  79,INTEN=LO
L10      DCMSFLD  79,INTEN=LO
L11      DCMSFLD  79,INTEN=LO
L12      DCMSFLD  79,INTEN=LO
L13      DCMSFLD  79,INTEN=LO
L14      DCMSFLD  79,INTEN=LO
L15      DCMSFLD  79,INTEN=LO
L16      DCMSFLD  79,INTEN=LO
L17      DCMSFLD  79,INTEN=LO
L18      DCMSFLD  79,INTEN=LO
L19      DCMSFLD  79,INTEN=LO
L20      DCMSFLD  79,INTEN=LO
L21      DCMSFLD  79,INTEN=LO
ENTRLN   DCMSFLD  79,ALPHA,CURSOR=AFTER
MODELN   DCMSFLD  79
         DCMSFEND
*
          AIF   (&@@SPLVL GE 200).SPX7                             U022
TITLE    DC    CL79'UNIT TYPE   STATUS  VOLSER  VOLSTATE  Q-LEN  ______$
               ____NOTES_____________'
          AGO  .SPX8                                               U022
.SPX7     ANOP                                                     U022
TITLE    DC    CL79'UNIT TYPE   STATUS  VOLSER  VOLSTATE  __________NOT$
               ES_____________'                                    U022
.SPX8     ANOP                                                     U022
         LTORG
BLANKTAB DC    CL256' '
         ORG   BLANKTAB+C''
         DC    C'.<(+|',X'50'
         ORG   BLANKTAB+C'!'
         DC    C'!$*);-/'
         ORG   BLANKTAB+C','
         DC    C',%_>?'
         ORG   BLANKTAB+C':'
         DC    C':#@''="'
         ORG   BLANKTAB+C'A'
         DC    C'ABCDEFGHI'
         ORG   BLANKTAB+C'J'
         DC    C'JKLMNOPQR'
         ORG   BLANKTAB+C'S'
         DC    C'STUVWXYZ '
         ORG   BLANKTAB+C'0'
         DC    C'0123456789'
         ORG
TYPESIZE EQU   8                        SIZE OF A 'DX' ENTRY
         PRINT NOGEN                                               U002
*  THIS TABLE IS BY LOW ORDER NIBBLE OF 4TH UCBTYPE BYTE           U002
*  (UCB+X'13')                                                     U002
         SPACE 2
COMMCUS  EQU   *              XXXX40XN  UCBTYP3=X'40' (IF NOT COMMTYP)
         DX    1,'2702'
         DX    2,'2701'
         DX    3,'2703'
         DX    4,'2955'                 FEDAU                      U001
         DX    5,'3704/5'
         DX    FF,'COMM'
         SPACE 3
TAPETYPS EQU   *              XXXX80NN  UCBTYP3=X'80': TAPE DEVICES
         DX    1,'2400'
         DX    3,'3400'
         DX    80,'3480'                                           U019
         DX    FF,'TAPE'
         SPACE 3
DASDTYPS EQU   *              XXXX20NN  UCBTYP3=X'20': DIRECT ACCESS
***      DX    1,'2311'                 DISK                       U019
***      DX    2,'2301'                 DISK                       U019
***      DX    3,'2303'                 DISK                       U019
***      DX    4,'2302'                 DISK                       U019
***      DX    5,'2321'                 DISK                       U019
         DX    6,'2305-1'               FIXED HEAD
         DX    7,'2305-2'               FIXED HEAD
         DX    8,'2314'                 DISK
         DX    9,'3330'                 DISK
         DX    A,'3340'                 DISK
         DX    B,'3350'                 DISK
         DX    C,'3375'                 DISK                       U015
         DX    D,'3330-1'               DISK
         DX    E,'3380'                 DISK                       U015
         DX    F,'3390'                 disk                       U023
         DX    FF,'DASD'
         SPACE 3
TUBETYPS EQU   *              XXXX10NN  UCBTYP3=X'10': GRAPHICS
         DX    2,'2250'                 DISPLAY UNIT               U015
***      DX    3,'2260'                 DISPLAY UNIT               U019
***      DX    4,'1053'                 PRINTER                    U019
***      DX    5,'2280'                 DISPLAY UNIT               U019
***      DX    6,'2282'                 DISPLAY UNIT               U019
***      DX    7,'2282'                 MODEL 85 CONSOLE           U019
         DX    8,'3066'                 168 SYSTEM CONSOLE
         DX    9,'3278'                 DISPLAY STATION            U006
         DX    A,'3284'                 PRINTER
         DX    B,'3286'                 PRINTER
         DX    C,'3158'                 158 CONSOLE                U001
         DX    D,'3036'                 3033 CONSOLE               U001
***      DX    E,'3138'                 138 CONSOLE                U001
***      DX    F,'3148'                 148 CONSOLE                U001
         DX    FF,'TUBE'
         SPACE 3
COMMTYPS EQU   *              XNXX40XX  UCBTYP3=X'40': COMMUNICATIONS
         DX    1,'TWX'
         DX    3,'2740'
         DX    4,'2740'
         DX    5,'2740C'
         DX    6,'2741P'
         DX    7,'BSC3'
         DX    8,'2740X'
         DX    9,'2740B'
         DX    F1,'3791'                LOCAL CONTROL UNIT
         DX    FF,'COMM'
         SPACE 3
URTYPES  EQU   *              XXXX08NN  UCBTYP3=X'08':  UNIT RECORD
         DX    1,'2540-R'               CARD READER
         DX    2,'2540-P'               CARD PUNCH
         DX    3,'1442-R'
         DX    4,'2501'                 CARD READER
         DX    5,'2520'                 CARD READ PUNCH
         DX    6,'3505'                 CARD READER
         DX    8,'1403'                 PRINTER
         DX    9,'3211'                 PRINTER
         DX    A,'1443'                 PRINTER
         DX    B,'3203'                 PRINTER                    U006
         DX    C,'3525'                 CARD PUNCH
         DX    E,'3800'                 LASER PRINTER              U006
         DX    10,'2671'                PAPER TAPE READER
         DX    11,'4245'                PRINTER                    U019
         DX    13,'4248'                PRINTER                    U019
         DX    16,'3890'                DOCUMENT PROCESSOR         U006
         DX    17,'3886'                OPTICAL CHARACTER READER   U006
         DX    18,'2495'                TAPE CARTRIDGE READER
         DX    19,'3895'                DOCUMENT READER/INSCRIBER  U019
         DX    1A,'1285'                OPTICAL READER             U019
         DX    1B,'1287'                OPTICAL READER
         DX    1C,'1288'                OPTICAL PAGE READER
         DX    1D,'1419'                MAG CHAR RDR, PRIMARY C.U. U019
         DX    1E,'2ND CU'              SECONDARY CU FOR 1419,1275 U019
         DX    1F,'1275'                OPTICAL RDR SORTER, PRI CU U019
         DX    20,'1052'                CONSOLE PRINTER-KEYBOARD
         DX    21,'2150'                CONSOLE                    U019
         DX    22,'3210'                CONSOLE PRINTER-KEYBOARD
         DX    23,'3215'                CONSOLE PRINTER-KEYBOARD
         DX    30,'3213'                PRINTER
         DX    3D,'7443'                SRF                        U002
         DX    42,'3851'                MSF                        U006
         DX    44,'3540'                DISKETTE
         DX    4C,'3838'                ARRAY PROCESSOR            U006
         DX    FF,'U/R'
         PRINT GEN                                                 U004
         TITLE 'D U - TERMINATION AND REENTRANT DATA AREAS'
END1     DCMSTOP
         SPACE 2
DUCB     CSECT                          RESUME                     U004
         PRINT NOGEN                                               U006
HELPIMG  $     '       * * *  D U C B  * * *           HELP'       U026
         $     ' '
 $ 'PF key definitions:                    Commands:'
 $ ' '
 $ '____________________________    HELP - this screen'
 $ '|PF1   13|PF2   14|PF3   15|    END  - terminate DUCB'
 $ '| Help   | Update | End    |    ON   - select online devices only'
 $ '|________|________|________|    OFF  - include offline devices'
 $ '|PF4   16|PF5   17|PF6   18|    DA   - select dasd only'
 $ '| On/Off |        |        |    T    - select tape only'
 $ '|________|________|________|    CO   - select comm only'       U026
 $ '|PF7   19|PF8   20|PF9   21|    DI   - select display only'    U026
 $ '| Down   | Up     | Restart|    U    - select U/R only'        U026
 $ '|________|________|________|    Q    - select only devices with I/O$
                queued'                                            U026
 $ '|PF10  22|PF11  23|PF12  24|    ALL  - reset DA/T/CO/DI/U/Q'   U026
 $ '|        |        |        |     '
 $ '|________|________|________|    +    - scroll down'
 $ '                                .    - scroll down'
 $ '                                -    - scroll up'
 $ '                                /xxx - issue MVS operator command "$
               xxx"'
HELPLEN  EQU   *-HELPIMG                                           U004
         SPACE 2
HELPIMG1 $     '       * * *  D U C B  * * *           HELP'       U026
         $     ' '
 $ 'PF key definitions:                    Commands:'
 $ ' '
 $ '____________________________    HELP - this screen'
 $ '|PF1   13|PF2   14|PF3   15|    END  - terminate DUCB'
 $ '| Help   | Update | End    |    '
 $ '|________|________|________|    '
 $ '|PF4   16|PF5   17|PF6   18|    '
 $ '| On/Off |        |        |    '
 $ '|________|________|________|    '                              U026
 $ '|PF7   19|PF8   20|PF9   21|    '                              U026
 $ '| Down   | Up     | Restart|    '                              U026
 $ '|________|________|________|    Q    - select only devices with I/O$
                queued'                                            U026
 $ '|PF10  22|PF11  23|PF12  24|    ALL  - reset Q'                U026
 $ '|        |        |        |     '
 $ '|________|________|________|    +    - scroll down'
 $ '                                .    - scroll down'
 $ '                                -    - scroll up'
HELPLEN1 EQU   *-HELPIMG1
         SPACE 1
         PRINT GEN                                                 U006
         SPACE 2
DCMSWORK DSECT                          RESUME                     U004
DWORK    DS    D
         DSGEN FORMAT1                  GENERATE SCREEN WORK AREA
         SPACE 1
         ORG   MODELN                   REDEFINE MODELN AREA       U011
MODECLH  DS    C'Class ='                                          U011
         DS    CL1                                                 U011
MODECLAS DS    C'ALL '                                             U024
         DS    CL2                                                 U024
MODEMH   DS    C'Mode ='                                           U011
         DS    CL1                                                 U011
MODEMODE DS    CL19'Online and Offline'                            U011
         ORG   MODELN+(L'MODELN-6-4-1)                             U018
MODESYS  DS    C'SYS = '                                           U018
MODEID   DS    C'SCG3'                                             U018
         ORG   ,                                                   U019
         SPACE 1
CMDADDR  DS    A                        -> PARMLIST FOR "OPERCMD"  U019
CMDPARM  DS    H,CL(L'ENTRLN)                                      U019
         ORG
         SPACE 2
          AIF   (&@@SPLVL GE 130).SP91                             U018
UCBLTBL  DS    A                        START OF UCB LOOKUP TABLE  U011
          AGO   .SP92                                              U018
.SP91     ANOP                                                     U018
IOSVSUCB DS    A                        COPY OF CVTUCBSC           U018
UCBPARM  DS    3A                       PARMLIST FOR IOSVSUCB      U018
UCBADDR  DS    A                        ANSWER AREA                U018
         DS    0D                       ALIGNMENT                  U018
UCBWORK  DS    CL100                    WORK AREA FOR IOSVSUCB     U018
.SP92     ANOP                                                     U018
UCBNBRT  DS    H                        NUMBER OF FIRST DSPLY UCB  U011
UCBNBR   DS    H                        NUMBER OF CUR DSPLY UCB    U011
         SPACE 1
FLAG1    DS    X                        DEFAULT 'ALL'
F1ON     EQU   X'80'                    USE ONLY ONLINE PEOPLE
F1DASD   EQU   X'40'                    ONLY DASD DEVICES
F1TAPE   EQU   X'20'                    ONLY TAPE DEVICES
F1COMM   EQU   X'10'                    only comm devices          U024
F1DISP   EQU   X'08'                    only disp devices          U024
F1UR     EQU   X'04'                    only u/r  devices          U024
F1DEVTYP EQU   F1DASD+F1TAPE+F1COMM+F1DISP+F1UR  dev cls mask bits U024
F1QD     EQU   X'01'                    ONLY DEVICES WITH I/O QUEUED
*
FLAG2    DS    X                                                   U019
F2PRIV   EQU   X'80'                    FULL PRIVILEGED USER       U012
F2OPER   EQU   X'40'                    OPERATOR AUTHORITY         U019
F2AUTO   EQU   X'10'                    AUTO UPDATE                U024
F2POS    EQU   X'04'                    POSITION TO UCB BY NAME    U011
F2DISP   EQU   X'02'                    AT LEAST 1 DEV DISPLAYED   U020
F2FOUND  EQU   X'01'                    WE FOUND SOMETHING         U019
*
FIRSTUCB DS    CL3                      DEFAULT FIRST UCB TO PRINT U012
*
DEVCLAS  DS    CL4                      curr class (ALL/DASD/TAPE) U024
*
SYSID    DS    CL4                      SMF SYSTEM ID              U018
*
NAMELIST DS    (65535/40)CL3            NAMES OF UCB'S DONE        U007
         DS    0D
DCMSWLEN EQU   *-DCMSWORK               FOR GETMAIN
         SPACE 4
OUTLINE  DSECT ,
OUTUNIT  DS    CL5
OUTTYPE  DS    CL7
OUTSTAT  DS    CL8
OUTVOL   DS    CL7
OUTSTATE DS    CL4
OUTSTAT2 DS    CL7
          AIF   (&@@SPLVL GE 200).SPX9                             U022
OUTQLEN  DS    CL7
.SPX9     ANOP                                                     U022
OUTNOTE  DS    CL36                                                U023
OUTLINE# EQU   *-OUTLINE                len of outline             U023
         DS    0XL(L'L1-OUTLINE#)       if to long, get asm error  U023
         TITLE 'MVS UCB DEFINITION '
UCB      DSECT ,                                                   U018
         IEFUCBOB  ,
         TITLE 'DASD CLASS EXTENSION'                              U021
         IECDDCE  ,                                                U021
         EJECT ,                                                   U018
*U018    TITLE 'ASVT DEFINITION'
         PRINT NOGEN                                               U018
         IHAASVT  ,                                                U002
*U018    TITLE 'ASCB DEFINITION'
         IHAASCB  ,                                                U002
*U018    TITLE 'MVS CVT DEFINITION'
         CVT   DSECT=YES
         IHAPSA  ,                                                 U018
         IKJTCB  ,                                                 U018
         IEZJSCB  ,                                                U018
         IKJPSCB  ,                                                U018
         IEESMCA  ,                                                U018
*  Things to add:                                                  U022
*    ucbfl5,ucbnaloc  oltep(etc)                                   U022
*    ucbflc,ucbattp   attention pending                            U022
*    ucbflc,ucbddrsw  ddr sw pending                               U022
         END
