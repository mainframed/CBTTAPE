LVO      TITLE 'LISTVO - COMMAND PROCESSOR'
***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1988 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.                                     *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*                                                                     *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1, with DFP 3.1),  *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc-oberon!LDW (last resort)                           *
*                                                                     *
*          (213) 743-5391 (direct -- 11 am to 7 pm Pacific time)      *
*          (213) 743-2957 (msgs)                                      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, or 714.  Sorry.                                       *
*                                                                     *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*  Attributes:  Reenterable, Authorized                               *
*               (will work non-authorized, but not get free DSCB      *
*               count from Indexed VTOCs).                            *
*                                                                     *
*  Updates:                                                           *
*    12Sep89 U029 LDW  Check for parse failure to avoid S0C4 later    *
*    05May89 U028 LDW  Fix off-by-one bug in U025 which caused        *
*                        zero length line to be PUTLINE'd             *
*    13Dec87 U027 LDW  Move NAMELIST to fix addressibility            *
*    06Dec87 U026 SDM  Restore 'No volumes' message removed by U024.  *
*                      Fix 'ALL' security hole in U025.               *
*    25Nov87 U025 SDM  Add sort keywords (default BYVOLSER):          *
*                        BYVOLSER,BYVOLUME,BYVOLID                    *
*                        BYADDRESS,BYDEVADDRESS,BYDEVADR              *
*                        BYFREE                                       *
*                        BYCONTIGFREE                                 *
*                      Implement special vol index flags:             *
*                        $xxx - use xxx as vol index (suppress        *
*                               unitname search).                     *
*                        = - use TSO default unitname.                *
*                        * or ALL - list all DASD (sysprog only).     *
*                        -xxx or ¬xxx - suppress xxx volumes.         *
*    09Nov87 U024 SDM  Use @GLOBALC to create volumes index lists.    *
*                      Change logic to scan UCB's exactly once.       *
*                      First try each name against unit names table,  *
*                         unless name starts with "$" flag character. *
*    09Sep87 U023 LDW  Make all numeric fields blank filled           *
*    18Aug87 U022 LDW  Rework 12Jun86 mod expanding various fields    *
*                         to support 3380E                            *
*                      Eliminate use of "LC" macro                    *
*                      Change some messages to lower case             *
*                      Make it assemble with XA MACLIB                *
*    05JUN87 U021 SDM  Use TESTAUTH before getting available DSCB     *
*                         count from Indexed VTOC.                    *
*    01JUN87 U020 SDM  GET UNITNAMES FROM @GLOBALS                    *
*            U019 SDM  DO UCB SCAN WITH MVS/SP SERVICE 'IOSVSUCB'     *
*                      USE @GLOBALS TO DETERMINE OPERATING SYS LEVEL  *
*    05DEC83 U018 LDW  ADD TOTALS LINE IF MORE THAN ONE VOLUME        *
*                         LISTED ("NOTOTALS" KEYWORD WILL SUPPRESS)   *
*                      FIX CHECK FOR 'ALL'                            *
*    30NOV83 U017 LDW  USE GQSCAN TO CHECK FOR ENQ ON SYSVTOC         *
*                      MISC OTHER CLEANUP                             *
*    26JUL83 U016 LDW  IF VTOC INDEX EXISTS, CALL CVAF TO GET         *
*                         NUMBER OF AVAILABLE DSCBS                   *
*                      UPDATE UNITNAME TABLE                          *
*    14MAR83 U015 LDW  DISPLAY 4 DIGITS OF # OF FREE EXTENTS          *
*    29NOV82 U014 EMS  FIX BUG IN U013                                *
*    19NOV82 U013 LDW  SHORTEN LINE LENGTH FOR STUPID OLD OLD TCAM    *
*                      CHANGE HEADER LINES TO LOWER CASE              *
*                      PUT IN 'REMOV' INSTEAD OF LEAVING IT BLANK     *
*    15NOV82 U012 LDW  CHANGE UNIT DEFINITIONS FOR HUGHES AIRCRAFT    *
*                      DON'T DEFAULT TO 'ALL' IF GUN NOT FOUND        *
*                      USE "PUTLINE" INSTEAD OF "TPUT"/"TPUTX"        *
*    30AUG82 U011 LDW  INDICATE INDEXED VTOC FOR PRIVILEGED USERS     *
*    09AUG82 U010 LDW  DEFAULT TO "STATUS" IF TUBE                    *
*                      ADD "NOHEADER" KEYWORD                         *
*                      UPDATE VOLUMES TABLE                           *
*    16FEB82 U009 LDW  RESTRICT 'ALL' TO SYSTEMS PROGRAMMERS          *
*                      CREATE 'UNITS' MACRO, INCREASE MAX TO 5        *
*                      SET FOR TITLE INSURANCE UNIT TYPES             *
*                      HANDLE BLANK GUN IN UNITNAME TABLE             *
*                      IMPROVE "NO VOLUMES FOUND ..." ERROR MSG       *
*                      USE 'UNITS' FOR PHONY GENERICS                 *
*                      ADD PHONY GENERIC "PC"                         *
*    23JUN81 U008 SDM  SIMPLIFY UNIT TABLE LOGIC (SLIGHTLY)           *
*    08JUN81 U007 SDM  CONVERT 'MACRO=YES' TO 'GETMAIN='              *
*                      ADD THREE INDEX SUPPORT FOR 'SYS' REQUEST      *
*                      MAKE GENERIC NAME LENGTH LOGIC MORE FLEXIBLE   *
*    06JAN81 U006 LDW  SCAN QCB'S TO CHECK FOR ENQ ON SYSVTOC         *
*    07MAY80 U005 LDW  ADD PHONY GENERIC 'SYS'                        *
*                      CHANGE TO LOCAL MACROS                         *
*    04DEC79 U004 LDW  INSERT MORE SPACES                             *
*                      CHANGE UNIT TABLE                              *
*                      ADD STATUS INFO                                *
*                      DO BOTH USE COUNTS                             *
*    16NOV79 U003 SDM  REPLACE 'SVC 3' WITH REAL EXIT CODE            *
*    01OCT79 U002 LDW  REPAIR FMT4 OBTAIN CODE                        *
*         77 U001 LDW  OVERHAUL TITLE LOGIC                           *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         COPY  @GLOBALS                 INSTALLATION DEFINITIONS   U019
         EJECT ,
LISTVO   OSENTER  ENV=(CP,SIM),PL=(PARSE,IO),PARMREG=R2,           U012+
               EXIT=RET,RC=(R15),GETMAIN=(WORKLEN,WORKD)           U017
         EJECT
         MVC   LINERDW(4),=Y(4+LINEL,0)   INITIALIZE PUTLINE RDW   U012
         LA    R0,PUTLNMFL              -> PUTLINE MF=L            U012
         ST    R0,IOPLIOPB              SET IN IOPL                U012
         SPACE 2
*        IF NO OPERAND GIVEN DEFAULT TO SEARCH INDEX BASED ON USER'S
*        GENERIC UNIT NAME
         SPACE 2
         MVI   FLAG,F@TOTALS+F@DFLT     Initialize all flags.      U025
         XC    CLEAR(CLEARL),CLEAR      CLEAR SOME VARIABLES       U018
         ZAP   TOTCYL,=P'0'             ...                        U018
         ZAP   TOTTRK,=P'0'             ...                        U018
         ZAP   TOTNUM,=P'0'             ...                        U018
         LA    R0,SORTVOL               -> Volser sort compare     U025
         ST    R0,SORTRTN               Initialize to volser sort  U025
         SPACE 1
         GTSIZE  ,                      GET SCREEN SIZES           U010
         LTR   R0,R0                    TUBE?                      U010
         BNP   *+8                      NO - SKIP                  U010
         OI    FLAG,F@STAT              YES - DEFAULT TO STATUS    U010
         SPACE 1
         MVC   XHDR1,HEADER1            COPY HEADER LINE 1         U011
         MVC   XHDR2,HEADER2            COPY HEADER LINE 2         U011
         TESTAUTH  FCTN=1               Test running authorized    U024
         LTR   R15,R15                  Well?                      U024
         BNZ   *+8                      No, forget about it        U024
         OI    FLAG,F@AUTH              Yes, remember it           U024
         L     R1,CPPLPSCB              -> PSCB                    U011
         USNGX PSCB,R1                                             U025
         TM    &@@STAFF                 System operator?           U025
         BO    OKIXVTOC                 YES - HE SEES IXVTOC IND.  U011
         DROPX R1                       PSCB                       U025
*                                       NO - CLEAR IT FROM ...     U011
         MVC   XHDR1+OLIXVTOC-1-LINE(4),BLANKS  ... HEADER ...     U011
         MVC   XHDR2+OLIXVTOC-1-LINE(4),BLANKS  ... LINES          U011
         SPACE 1
OKIXVTOC LA    R1,INDXLIST              -> Target index list       U025
         ST    R1,INDXLADR              Save for later             U025
         ST    R1,INDXLPTR              Save for later             U025
         LA    R1,INDXLL-1(,R1)         -> last byte in list       U025
         ST    R1,INDXLLIM              Save for limit test        U025
         LA    R1,SPRSLIST              -> Suppress index list     U025
         ST    R1,SPRSLADR              Save for later             U025
         ST    R1,SPRSLPTR              Save for later             U025
         LA    R1,SPRSLL-1(,R1)         -> last byte in list       U025
         ST    R1,SPRSLLIM              Save for limit test        U025
         L     R1,CPPLECT               -> ECT                     U024
         TM    ECTSWS-ECT(R1),ECTNOPD   NO OPERAND?
         BO    UNIT                     NO, LEAVE IT BLANK
         SPACE 1
         TSPARSE CBUF=(CPPLCBUF,I),PCL=(=V(PCL),I)
         SPACE 1
         LTR   R2,R15                   parse ok?                  U029
         BZ    PARSE$OK                 yes - continue             U029
         LA    R0,MSGPARSE              -> error message           U029
         BAL   R14,PUTLINE              display it                 U029
         LA    R2,20                    set return code            U029
         B     RET                      and leave                  U029
         SPACE 2
RET0     SR    R2,R2                    set return code            U029
         B     RET                      and leave                  U029
         SPACE 2
PARSE$OK L     R15,TSPARANS             POINT TO DESCRIPTOR LIST   U004
         CLI   STAT+1-PDL(R15),1        STATUS SPECIFIED?          U004
         BNE   *+8                      NO                         U004
         OI    FLAG,F@STAT              YES - SET FLAG             U004
         CLI   STAT+1-PDL(R15),2        NOSTATUS SPECIFIED?        U010
         BNE   *+8                      NO                         U010
         NI    FLAG,255-F@STAT          YES - UNSET FLAG           U010
         CLI   HEAD+1-PDL(R15),2        NOHEADER SPECIFIED?        U010
         BNE   *+8                      NO                         U010
         OI    FLAG,F@NOHEAD            YES - SET FLAG             U010
         CLI   TOTALS+1-PDL(R15),2      NOTOTALS SPECIFIED?        U018
         BNE   *+8                      NO - SKIP                  U018
         NI    FLAG,255-F@TOTALS        YES - UNSET FLAG           U018
         CLI   SORTS+1-PDL(R15),BYVOL   Volser sort requested?     U025
         BE    GOTSORT                  Yes, sort is set           U025
         LA    R0,SORTADDR              Set devaddr sort           U025
         CLI   SORTS+1-PDL(R15),BYADDR  Devaddr sort requested?    U025
         BE    SETSORT                  Yes, set the sort          U025
         LA    R0,SORTFREE              Set freespace sort         U025
         CLI   SORTS+1-PDL(R15),BYFREE  Freespace sort requested?  U025
         BE    SETSORT                  Yes, set the sort          U025
         LA    R0,SORTCNTG              Set contigfree sort        U025
SETSORT  ST    R0,SORTRTN               Set sort compare addr      U025
GOTSORT  DC    0H'0'                                               U025
         LA    R3,VOL-PDL(,R15)         -> vol index descriptor    U024
         L     R14,0(,R3)               -> vol index data          U024
LST$NAME MVC   LISTNAME,BLANKS          Clear name of list         U024
         LH    R1,4(,R3)                Get length of name         U024
         BCTR  R1,0                     Decrement for execute      U024
         EX    R1,NAMVC                 Move name to fixed field   U024
         LA    R1,LISTNAME              -> List name               U024
         CLI   0(R1),C'-'               Suppressed vol index?      U025
         BE    NOT$LIST                 Yes, use as volume index   U025
         CLI   0(R1),C'¬'               Suppressed vol index?      U025
         BE    NOT$LIST                 Yes, use as volume index   U025
         NI    FLAG,255-F@DFLT          Turn off default flag      U025
         CLC   =C'* ',LISTNAME          Request for 'ALL' volumes? U026
         BE    LST$ALL                  Yes, use 'ALL' index       U026
         CLC   =C'ALL ',LISTNAME        Request for 'ALL' volumes? U026
         BNE   NOT$ALL                  No, skip 'ALL' logic       U026
         L     R15,CPPLPSCB             -> PSCB                    U026
         USNGX PSCB,R15                                            U026
         TM    &@@SYSP                  Systems programmer?        U026
         BZ    NOT$ALL                  No, skip 'ALL' logic       U026
         DROPX R15                      PSCB                       U026
LST$ALL  LA    R1,BLANKS                Set 'ALL' index            U026
         B     NOT$LIST                 Go set the vol index       U026
NOT$ALL  CLI   0(R1),C'$'               Forced vol index?          U026
         BNE   TRY$LIST                 No, try it as a unit name  U024
         LA    R1,1(,R1)                -> desired vol index       U024
         B     NOT$LIST                 Process vol index          U024
TRY$LIST BAL   R14,SET$UNIT             Set the unit name          U024
         BZ    NXT$NAME                 Found it, get next name    U024
NOT$LIST BAL   R14,SET$INDX             Set the vol index          U024
NXT$NAME CLC   =X'FF000000',8(R3)       End of vol index list?     U024
         BE    CONTINUE                 Yes, end of processing     U024
         L     R3,8(,R3)                -> next vol index PDE      U024
         L     R14,0(,R3)               -> vol index data          U024
         B     LST$NAME                 Go process next name       U024
         SPACE 1
NAMVC    MVC   LISTNAME(0),0(R14)       << Executed >>             U024
         TITLE 'S P A C E -- PRODUCE VOLUME LIST'
*---------------------------------------------------------------------*
*
* NAME         UNIT
*
* FUNCTION     SELECT A DEFAULT SEARCH INDEX BASED ON USER'S GENERIC
*              UNIT NAME IN PSCB.
*
*---------------------------------------------------------------------*
         SPACE 2
UNIT     LA    R1,=C'='                 Request default unit       U025
         BAL   R14,SET$UNIT             Set the unit name          U024
         BZ    CONTUNIT                 Found it, go finish up     U025
         SPACE 1
         MVC   LINE(LINEL),BLANKS       Clear the print line       U024
         MVC  LINE(44),=C'No table entry found for generic unit name -'
         MVC   LINE+45(8),0(R1)                                    U024
         LA    R0,LINERDW               -> message line            U024
         BAL   R14,PUTLINE              Tell him about our problem U024
         LA    R2,12                    Set return code            U029
         B     RET                      End the program            U024
         SPACE 2
SET$UNIT ST    R14,LINKUNIT             Save return address        U024
         LA    R2,NAMELIST              -> unit name table         U024
         CLI   0(R1),C'='               Special request?           U025
         BNE   UNITSCAN                 No, look up name as-is     U025
         L     R1,CPPLPSCB              Yes, look up default       U025
         LA    R1,PSCBGPNM-PSCB(,R1)    Generic unit for TSO ID    U025
         SPACE 1
UNITSCAN CLI   0(R2),C' '               End of table ?             U024
         BE    UNITNONE                 YES, UNIT NAME NOT FOUND   U012
         CLC   0(8,R2),0(R1)            SEARCH TABLE FOR UNITNAME  U020
         BE    UNITYES                  FOUND
         LA    R2,UNITNLEN(,R2)         -> NEXT TABLE ENTRY        U008
         B     UNITSCAN
         SPACE 1
UNITYES  LA    R2,8(,R2)                -> first vol index         U024
         SPACE 1
UNIT$NXT CLI   0(R2),C' '               End of list?               U024
         BE    UNITDONE                 Yes, end of vol index list U024
         LR    R1,R2                    -> desired vol index       U024
         BAL   R14,SET$INDX             List the vol index         U024
         LA    R2,6(,R2)                -> next vol index          U024
         B     UNIT$NXT                 Go list it                 U024
         SPACE 1
UNITNONE LA    R15,8                    Indicate name not in table U024
         B     UNITEXIT                 Go return to caller        U024
         SPACE 1
UNITDONE SLR   R15,R15                  Indicate that it worked    U024
         SPACE 1
UNITEXIT L     R14,LINKUNIT             Restore return address     U024
         LTR   R15,R15                  Set CC according to retcd  U024
         BR    R14                      Return to caller           U024
         SPACE 2
SET$INDX ST    R14,LINKINDX             Save return address        U025
         LA    R14,SPRSLADR             -> suppress list           U025
         CLI   0(R1),C'-'               Suppress index?            U025
         BE    LEN01                    Yes, process suppress      U025
         CLI   0(R1),C'¬'               Suppress index?            U025
         BNE   LEN02                    No, process normal index   U025
LEN01    LA    R1,1(,R1)                Skip flag character        U025
         B     LEN03                    Process name               U025
LEN02    LA    R14,INDXLADR             -> target list             U025
         LH    R15,=H'-1'               Set for possible "ALL"     U025
         CLI   0(R1),C' '               Request for 'ALL' volumes? U026
         BE    LEN06                    Yes                        U025
LEN03    LA    R15,5(,R1)               -> end of index            U025
LEN04    CLI   0(R15),C' '              Trailing blank?            U025
         BNE   LEN05                    No, found real end         U025
         BCT   R15,LEN04                Decrement and loop         U025
         SPACE 1
LEN05    SR    R15,R1                   Length -1 for EX           U025
LEN06    L     R4,4(,R14)               -> current available entry U025
         LA    R0,LISTEL(,R4)           -> next available entry    U026
         C     R0,8(,R14)               Entry fit in list?         U025
         BH    ERRCAP                   No, capacity error         U024
         MVI   0(R4),X'00'              Init flags byte            U026
         STC   R15,1(,R4)               Save length in list        U026
         MVC   2(6,R4),BLANKS           Clear index field          U026
         LTR   R15,R15                  Really any index?          U025
         BM    LEN07                    No, skip move              U025
         EX    R15,SIMVC                Move data to list          U024
LEN07    ST    R0,4(,R14)               -> Next available entry    U025
         L     R14,LINKINDX             Restore return address     U025
         BR    R14                      Return to caller           U024
         SPACE 1
SIMVC    MVC   2(0,R4),0(R1)            << Executed >>             U026
         SPACE 2
ERRCAP   MVC   LINE(LINEL),BLANKS       Clear the print line       U024
         MVC   LINE(36),=C'Volume index table capacity exceeded'   U024
         LA    R0,LINERDW               -> message line            U024
         BAL   R14,PUTLINE              Tell him about our problem U024
         SPACE 2
CONTINUE DS    0H
         IKJRLSA TSPARANS               Release parse storage      U025
         TM    FLAG,F@DFLT              Still need default unit?   U025
         BO    UNIT                     Yes, go get the default    U025
CONTUNIT DC    0H'0'                                               U025
         LA    R0,WORKD                 -> program work area       U025
         AH    R0,=Y(OUTLINES-WORKD)    -> output lines area       U025
         ST    R0,AOUTLINA              -> output lines area       U025
         ST    R0,AOUTLINN              -> next output line        U025
         A     R0,=A(OUTLINEL-1)        -> end of area             U025
         ST    R0,AOUTLINL              -> end of area             U025
         LA    R1,HEADERL1              Get length of short title  U025
         TM    FLAG,F@STAT              Status wanted?             U025
         BNO   *+8                      No                         U025
         LA    R1,HEADERL2              Yes, use longer line       U025
         LA    R1,4(,R1)                Length of RDW + data       U025
         SLL   R1,16                    Format for RDW             U025
         STCM  R1,B'1111',LINERDW       Set line RDW               U025
         STCM  R1,B'1111',XHDR1RDW      Set header1 RDW            U025
         STCM  R1,B'1111',XHDR2RDW      Set header2 RDW            U025
         SPACE 3
*        SCAN UCB TABLE
         SPACE 1
FINDUCB  XR    R4,R4                    Clear UCB pointer          U024
          AIF   (&@@SPLVL GE 130).UCB010                           U019
         L     R1,CVTPTR                GET CVT ADDRESS            U019
         L     R3,CVTILK2-CVT(,R1)      -> UCB lookup table        U024
         B     LOOP                     ENTER THE SCAN LOOP        U019
         SPACE 1
NXTUCB   LA    R3,2(,R3)                -> next UCB address        U024
         SPACE 1
LOOP     CLC   =X'FFFF',0(R3)           Is it end of table?        U024
         BE    RETURN                   ...YES, RETURN
         SR    R2,R2                    Clear for ICM              U024
         ICM   R2,B'0011',0(R3)         UCB offset                 U024
         LTR   R2,R2                                               U024
         BZ    NXTUCB                   IF NON-ZERO GO ON          U019
         USNGX UCB,R2                                              U024
         CLI   UCBTBYT3,UCB3DACC        DIRECT ACCESS?             U019
         BNE   NXTUCB                   NO                         U019
         DROPX R2                       UCB                        U024
          AGO   .UCB020                                            U019
.UCB010   ANOP                                                     U019
         LA    R1,IOSWORK               -> IOSVSUCB WORK AREA      U019
         ST    R1,IOSPARM               -> IOSVSUCB WORK AREA      U019
         LA    R1,IOSDVCLS              -> IOSVSUCB DEVICE CLASS   U019
         ST    R1,IOSPARM+4             -> IOSVSUCB DEVICE CLASS   U019
         LA    R1,IOSAUCB               -> IOSVSUCB RETURN -> UCB  U019
         ST    R1,IOSPARM+8             -> IOSVSUCB RETURN -> UCB  U019
         OI    IOSPARM+8,X'80'          FLAG END OF PARMS          U019
         XC    IOSWORK,IOSWORK          CLEAR IOSVSUCB WORK AREA   U019
         MVI   IOSDVCLS,UCB3DACC        LIMIT SCAN TO DASD         U019
NXTUCB   LA    R1,IOSPARM               -> IOSVSUCB PARAMETERS     U019
         L     R15,CVTPTR               -> CVT                     U019
         L     R15,CVTUCBSC-CVT(,R15)   -> IOSVSUCB ROUTINE        U019
         BALR  R14,R15                  LOCATE NEXT UCB            U019
         LTR   R15,R15                  ANY UCB FOUND?             U019
         BNZ   RETURN                   NO, END OF SCAN            U019
         L     R2,IOSAUCB               -> UCB                     U024
.UCB020   ANOP                                                     U019
         CR    R2,R4                    Already done this one?     U024
         BNH   NXTUCB                   IF SO SKIP IT NOW
         LR    R4,R2                    -> new UCB                 U024
         USNGX UCB,R4                                              U024
         TM    UCBSTAT,UCBONLI          ONLINE?
         BNO   NXTUCB                   NO, IGNORE DEVICE
         CLC   UCBTBYT3(2),=X'2005'     DATA CELL?
         BE    NXTUCB                   IF SO SKIP IT
         TM    UCBVOLI,X'FF'            ANY NAME?
         BZ    NXTUCB                   NO, IGNORE IT
         LA    R1,SPRSLADR              -> suppress list           U025
         LA    R15,UCBVOLI              -> current volume serial   U026
         BAL   R14,LISTFIND             Find entry in list         U025
         BZ    NXTUCB                   Found, skip this UCB       U025
         LA    R1,INDXLADR              -> target list             U025
         LA    R15,UCBVOLI              -> current volume serial   U026
         BAL   R14,LISTFIND             Find entry in list         U025
         BNZ   NXTUCB                   Not found, skip this UCB   U025
         SPACE 1
*        FORMAT UCB INFORMATION FIRST
         SPACE 1
USEUCB   MVC   LINE(LINEL),BLANKS       CLEAR THE LINE             U001
         MVC   OLVOLSER,UCBVOLI         PRINT VOLUME NAME
         MVC   OLDEVADR,UCBNAME         GET UNIT ADDRESS
         SPACE 1
         MVI   OLSTAT1+4,C'/'           PUT IN DELIMITER           U004
         MVC   OLSTAT2,=C'Remov'        ASSUME REMOVABLE           U022
         TM    UCBSTAT,UCBRESV          RESERVED?                  U004
         BNO   *+10                     NO - SKIP                  U004
         MVC   OLSTAT2,=C'Rsrvd'        YES                        U022
         TM    UCBSTAT,UCBPRES          RESIDENT?                  U004
         BNO   *+10                     NO - SKIP                  U004
         MVC   OLSTAT2,=C'Rsdnt'        YES                        U022
         SPACE 1
         TM    UCBSTAB,UCBBPRV          PRIVATE?                   U004
         BNO   *+10                     NO - SKIP                  U004
         MVC   OLSTAT1,=C'Priv'         YES                        U022
         TM    UCBSTAB,UCBBPUB          PUBLIC?                    U004
         BNO   *+10                     NO - SKIP                  U004
         MVC   OLSTAT1,=C'Publ'         YES                        U022
         TM    UCBSTAB,UCBBSTR          STORAGE?                   U004
         BNO   *+10                     NO - SKIP                  U004
         MVC   OLSTAT1,=C'Stor'         YES                        U022
         SPACE 1
          AIF   (&@@SPLVL GE 200).UCB030                           U019
         XR    R1,R1                    CLEAR A REG                U004
         IC    R1,UCBUSER               GET USE COUNT              U004
          AGO  .UCB040                                             U019
.UCB030  LH    R1,UCBUSER               GET USE COUNT              U019
.UCB040   ANOP                                                     U019
         CVD   R1,DBLW                  CONVERT TO PRINT           U004
         MVC   OLUALLOC-1(6),=X'402020202020'                      U023
         ED    OLUALLOC-1(6),DBLW+5                                U023
         SPACE 1
         IC    R1,UCBDMCT               GET OPEN DCB COUNT         U002
         N     R1,=XL4'7F'              REPAIR IT                  U002
         CVD   R1,DBLW                  CONVERT TO PRINT
         MVC   OLUOPEN-1(4),=X'40202020'                           U023
         ED    OLUOPEN-1(4),DBLW+6                                 U023
         SPACE 1
         TM    UCBFLA,UCBNRY            NOT READY?                 U004
         BNO   CHECKENQ                 NO - CONTINUE              U017
         MVC   OLMSG(9),=C'Not Ready'   SAY WHAT THE PROBLEM IS    U022
         B     PUTIT                    AND DON'T HANG UP HERE     U004
         SPACE 2
LISTFIND LA    R0,4                     Assume a miss              U026
         L     R5,0(,R1)                -> First entry in list     U026
         SLR   R6,R6                    Clear for IC               U026
LISTF1   C     R5,4(,R1)                Reached end of list?       U026
         BNL   LISTF9                   Yes, end of loop           U026
         ICM   R6,B'0001',1(R5)         EX length for CLC          U026
         BM    LISTF2                   Hit an 'ALL' request.      U026
         EX    R6,EXCLC                 Does volume name match?    U026
         BNE   LISTF3                   No, Pick up next entry     U026
LISTF2   SLR   R0,R0                    Indicate a hit             U026
         OI    0(R5),LF@FND             Indicate volume found      U026
LISTF3   LA    R5,LISTEL(,R5)           -> next vol index          U026
         B     LISTF1                   Check next index           U025
LISTF9   LTR   R15,R0                   Set CC for caller          U026
         BR    R14                      Return to caller           U025
         SPACE 1
EXCLC    CLC   2(0,R5),0(R15)           << Executed >>             U026
         SPACE 3
*        SCAN THE ENQ STRUCTURE TO SEE IF ANYONE IS ENQUEUED ON    U006
*        'SYSVTOC' 'VOLSER'.  IF SO, DON'T DO THE LSPACE SVC,      U006
*        SINCE IT WILL HANG UP AND CAN'T BE ATTENTIONED OUT OF.    U006
CHECKENQ EQU   *                                                   U022
          AIF   (&@@SPLVL GE 200).GRS01                            U022
         L     R1,16                    -> CVT                     U017
         CLC   =F'0',640(R1)            ANY QCB'S?                 U017
         BNZ   QCBSCAN                  YES - PRE SP1.2            U017
          AIF   (&@@SPLVL GE 120).GRS01                            U019
         B     LSPACE                   SP1.2, BUT NOT SUPPORTED   U017
          AGO   .GRS02                                             U017
.GRS01    ANOP                                                     U017
         MVC   GQLIST(GQSCANXL),GQSCANX INIT PARM BLOCK            U017
         XC    TOKEN(4),TOKEN           ZERO TOKEN                 U017
         L     R2,=A(AREAL)             GET LENGTH OF 'AREA'       U017
         SPACE 1                                                   U017
         GQSCAN  AREA=(AREA,(R2)),TOKEN=TOKEN,MF=(E,GQLIST),       U017$
               RESNAME=(=CL8'SYSVTOC',UCBVOLI,6)                   U017
         SPACE 1                                                   U017
         CH    R15,=H'16'               RC IN RANGE?               U017
         BNH   *+8(R15)                 YES - DECODE IT            U017
         EX    0,*                      NO - DIE                   U017
         B     SCAN1                    00 - SCAN COMPLETE         U017
         B     LSPACE                   04 - NOTHING FOUND         U017
         B     SCAN1                    08 - SCAN INCOMPLETE       U017
         EX    0,*                      12 - FOUND ABNORMAL COND.  U017
         EX    0,*                      16 - INVALID SYSNAME       U017
         SPACE 1                                                   U017
SCAN1    ST    R0,RIBLS                 SAVE LENGTH OF RIB & RIBE  U017
         SR    R15,R15                  CLEAR RETURN REGISTER      U017
         LA    R1,AREA                  POINT TO RIB               U017
         USNGX RIB,R1                                              U017
         AH    R1,RIBVLEN               + LEN OF VARIABLE PORTION  U017
         AH    R1,RIBL                  + RIBLENGTH RETURN BY SCAN U017
*                                       R1 -> FIRST RIBE           U017
         DROPX R1                       RIB                        U017
         USNGX RIBE,R1                                             U017
         MVC   OLMSG(39),=C'Try later - VTOC in use by job ????????' 22
         MVC   OLMSG+31(8),RIBEJBNM     PUT JOBNAME INTO MESSAGE   U017
         B     PUTIT                    AND PUT OUT MESSAGE        U017
         DROPX R1                       RIBE                       U017
          AIF   (&@@SPLVL GE 200).GRS03                            U022
.GRS02    ANOP                                                     U019
         SPACE 3
*---  SEARCH OLD STYLE QCB'S IN SQA                                ---*
QCBSCAN  L     R1,16                    -> CVT                     U006
         LA    R2,640(,R1)              -> QCB ORIGIN              U006
         USNGX MAJ,R2                   MAJOR QCB                  U006
         SPACE 1
MAJLOOP  L     R2,MAJNMAJ               -> NEXT MAJOR QCB          U006
         LTR   R2,R2                    END OF CHAIN?              U006
         BZ    LSPACE                   YES - NO MAJOR QCB FOR     U006
*                                       'SYSVTOC', SO IT'S SAFE.   U006
         CLC   =C'SYSVTOC ',MAJNAME     IS THIS WHAT WE WANT?      U006
         BNE   MAJLOOP                  NO - CHECK NEXT MAJOR QCB. U006
         L     R2,MAJFMIN               -> FIRST MINOR QCB         U006
         DROPX R2                       MAJOR QCB                  U006
         USNGX MIN,R2                                              U006
         B     MINLOOP1                 SKIP AROUND FIRST TIME     U006
         SPACE 1                                                   U006
MINLOOP  L     R2,MINNMIN               -> NEXT MINOR QCB.         U006
MINLOOP1 LTR   R2,R2                    ANY?                       U006
         BZ    LSPACE                   NO - IT'S SAFE.            U006
         CLC   OLVOLSER,MINNAME         IS IT FOR OUR VOLUME?      U006
         BNE   MINLOOP                  NO - CHECK NEXT            U006
*  DRAT.  SOMEONE IS ENQUEUED ON THE VTOC OF THE VOLUME I WANT TO  U006
*  LOOK AT.  SO, SAY WE'RE SORRY, AND SKIP THE LSPACE SVC.         U006
         L     R2,MINFQEL               -> FIRST QEL               U006
         DROPX R2                       MINOR QCB                  U006
         USNGX QEL,R2                                              U006
         MVC   OLMSG(39),=C'Try later - VTOC in use by job ????????' 22
         LH    R15,QELASID              GET OWNER'S ASID           U006
         DROPX R2                       QEL                        U017
         LTR   R15,R15                  ANY?                       U006
         BZ    PUTIT                    NO - LEAVE MSG AS IS       U006
         L     R1,16                    -> CVT                     U006
         L     R1,CVTASVT-CVT(,R1)      -> ASVT                    U017
         SLL   R15,2                    MULTIPLY ASID BY 4         U006
         LA    R1,ASVTENTY-4-ASVT(R1,R15)  -> THIS AS'S ASVT ENT   U017
         L     R1,0(,R1)                -> ASCB                    U006
         USNGX ASCB,R1                                             U017
         ICM   R15,B'1111',ASCBJBNI     -> JOBNAME                 U006
         BNZ   GOTJOBNM                 IT'S A JOB                 U006
         ICM   R15,B'1111',ASCBJBNS     -> S/M/L JOBNAME           U006
         BZ    PUTIT                    NO POINTER HERE EITHER?    U006
         DROPX R1                       ASCB                       U017
         SPACE 1
GOTJOBNM MVC   OLMSG+31(8),0(R15)       MOVE JOBNAME TO MSG        U006
         B     PUTIT                    AND WE'RE DONE.            U006
.GRS03    ANOP                                                     U019
         SPACE 2                                                   U006
*        GET MORE INFORMATION FROM LSPACE
         SPACE 1
LSPACE   LR    R0,R4                    Point to UCB               U024
         LA    R1,DBLW                  WORKSPACE
         SVC   78                       GET SPACE INFO
         LTR   R15,R15                  DID IT WORK?
         BZ    SPCOK                    YES
* THE 'DOS' BIT (AT LEAST) WILL GET US HERE.
* APPARENTLY, HOWEVER, THE DIRF BIT WON'T.
VTOCERR  MVC   OLMSG(25),=CL25'Error in VTOC'                      U022
         SPACE 2
PUTIT    L     R1,AOUTLINN              -> next output line        U025
         C     R1,AOUTLINL              Still within the table?    U025
         BH    ERRSORT                  No, go out with error      U025
         LA    R0,L'OUTLINES(,R1)       -> new next output line    U025
         ST    R0,AOUTLINN              Save for later             U025
         L     R15,SORTRTN              -> sort compare routine    U025
SORT1    SH    R1,=Y(L'OUTLINES)        -> previous output line    U025
         C     R1,AOUTLINA              Still in the table?        U025
         BL    SORT2                    No, insert line at start   U025
         MVC   L'OUTLINES(L'OUTLINES,R1),0(R1)  Move line back JIC U025
         BR    R15                      Select compare routine     U025
SORTFREE CLC   OLFCYL-LINERDW(OLFTRK+L'OLFTRK-OLFCYL,R1),OLFCYL    U025
         BL    SORT1                    No, loop                   U025
         B     SORT2                    Yes, go insert line        U025
SORTCNTG CLC   OLCCYL-LINERDW(OLCTRK+L'OLCTRK-OLCCYL,R1),OLCCYL    U025
         BL    SORT1                    No, loop                   U025
         B     SORT2                    Yes, go insert line        U025
SORTVOL  CLC   OLVOLSER-LINERDW(,R1),OLVOLSER  Found place?        U025
         BH    SORT1                    No, loop                   U025
SORTADDR DC    0H'0'                    Already in devaddr order   U025
SORT2    MVC   L'OUTLINES(L'OUTLINES,R1),LINERDW  Yes, insert line U025
         LH    R15,NUMVOLS              GET # OF VOLUMES FOUND     U018
         LA    R15,1(,R15)              INCREMENT                  U018
         STH   R15,NUMVOLS              SAVE UPDATED COUNT         U018
         B     NXTUCB                   NOW GO ON
         SPACE 2                                                   U004
SPCOK    MVC   OLFCYL,DBLW+6            CYL (TOTAL)                U022
         LA    R15,OLFCYL               -> field                   U023
         BAL   R14,UNZERO               blank out leading zeroes   U023
         MVC   OLFTRK,DBLW+11           TRK (TOTAL)
         LA    R15,OLFTRK               -> field                   U023
         BAL   R14,UNZERO               blank out leading zeroes   U023
         MVC   OLFNUM,DBLW+16           NUM (OF AREAS)             U015
         LA    R15,OLFNUM               -> field                   U023
         BAL   R14,UNZERO               blank out leading zeroes   U023
         MVC   OLCCYL,DBLW+21           CYL (LARGEST)              U022
         LA    R15,OLCCYL               -> field                   U023
         BAL   R14,UNZERO               blank out leading zeroes   U023
         LA    R15,DBLW+26              -> field                   U023
         BAL   R14,UNZERO               blank out leading zeroes   U023
         MVC   OLCTRK,DBLW+27           TRK (LARGEST)
         SPACE 1
*---  UPDATE TOTALS/MAX COUNTERS                                   ---*
         PACK  PWORK,DBLW+6(4)          GET NUMBER OF FREE CYL     U022
         AP    TOTCYL,PWORK             ACCUMULATE IT              U018
         PACK  PWORK,DBLW+11(4)         GET NUMBER OF FREE TRK     U018
         AP    TOTTRK,PWORK             ACCUMULATE IT              U018
         PACK  PWORK,DBLW+16(4)         GET NUMBER OF FREE EXTENTS U018
         AP    TOTNUM,PWORK             ACCUMULATE IT              U018
         CLC   OLCCYL,MAXCYL            THIS LARGEST CONTIG EXT?   U018
         BL    GET$FMT0                 NO - SKIP                  U018
         BE    CYLSAME                  MAYBE                      U018
         MVC   MAXCYL,OLCCYL            YES - SAVE NEW LARGEST CYL U018
         MVC   MAXTRK,OLCTRK            ..AND SAVE NEW LARGEST TRK U018
         B     GET$FMT0                 DONE HERE                  U018
         SPACE 1
CYLSAME  CLC   OLCTRK,MAXTRK            THIS LARGEST CONTIG TRK?   U018
         BNH   *+10                     NO - SKIP                  U018
         MVC   MAXTRK,OLCTRK            YES - SAVE NEW LARGEST TRK U018
         SPACE 1
*        NOW FIND THE NUMBER OF DSCBS LEFT (FMT0 COUNT)
         SPACE 1
GET$FMT0 MVC   CAML(4),=AL1(193,0,0,0)  MOVE IN DSNAME OPTIONS     U002
         MVC   CAMV(6),UCBVOLI          VOLUME NAME
         LA    R0,=44X'04'              FORMAT 4 DSCB'S DSNAME     U002
         LA    R1,CAMV                  VOLUME LIST
         LA    R2,CAMW                  WORKAREA
         STM   R0,R2,CAML+4             SET UP CAMLIST
         MVC   OLFDSCB+L'OLFDSCB-3(3),=C'n/a'   Assume not avail.  U021
         MVC   OLIXVTOC,=C'??'          SET DEFAULT                U021
         L     R1,CPPLPSCB              -> PSCB                    U017
         USNGX PSCB,R1                                             U025
         TM    &@@STAFF                 System operator?           U025
         BO    *+10                     YES - SKIP                 U017
         MVC   OLIXVTOC,BLANKS          NO - REMOVE '??'           U017
         DROPX R1                       PSCB                       U025
         OBTAIN CAML                    GET FORMAT 4 DSCB
         LTR   R15,R15                  DID IT WORK?
         BNZ   PUTIT                    THEN GIVE UP               U002
******** TM    DS4VTOCI,DS4IVTOC        INDEXED VTOC?              U016
         TM    CAMW+14,1                INDEXED VTOC?              U016
         BO    IXVTOC                   YES - GO CALL CVAF         U016
         MVC   OLIXVTOC,BLANKS          CLEAR INDEXED INDICATOR    U017
         LH    R1,CAMW+6   DS4DSREC     GET NUMBER OF FMT0S        U002
         SPACE 2                                                   U016
FMT$FMT0 CVD   R1,DBLW                  CONVERT IT
         MVC   OLFDSCB-1(6),=X'402020202020'                       U023
         ED    OLFDSCB-1(6),DBLW+5                                 U023
         A     R1,TOTDSCB               TALLY TOTAL NUMBER OF ...  U018
         ST    R1,TOTDSCB               ... FREE DSCB'S            U018
         B     PUTIT                    NOW GO ON                  U016
         SPACE 2                                                   U016
IXVTOC   L     R1,CPPLPSCB              -> PSCB                    U011
         USNGX PSCB,R1                                             U025
         TM    &@@STAFF                 System operator?           U025
         BNO   *+10                     NO - SKIP                  U016
         MVC   OLIXVTOC,=C'IX'          YES - SHOW IT'S INDEXED    U011
         DROPX R1                       PSCB                       U025
         MVC   CVPL(CVPLLEN),CVPLX      COPY MODEL CVPL            U016
         SPACE 1
*---  NEED TO OPEN THE VTOC AND PASS THE DEB ADDR IF NOT APF AUTH  ---*
*        CVAFDSM  ACCESS=MAPDATA,MAP=VTOC,COUNT=YES,DEB=(R??),     U016$
               CTAREA=DBLW,MF=(E,CVPL)                             U016
*---  SO, FOR NOW, WE'LL BE APF AUTHORIZED AND USE THE UCB ADDR    ---*
         TM    FLAG,F@AUTH              Running authorixed?        U024
         BZ    PUTIT                    No, forget about it        U024
         CVAFDSM  ACCESS=MAPDATA,MAP=VTOC,COUNT=YES,UCB=(R4),      U024$
               CTAREA=DBLW,MF=(E,CVPL),BRANCH=(YES,PGM)            U016
         SPACE 1
         LTR   R15,R15                  DID CVAFDSM WORK?          U021
         BNZ   PUTIT                    I FIGURED AS MUCH          U016
         L     R1,DBLW                  GET THE FORMAT ZERO COUNT  U016
         B     FMT$FMT0                 AND GO FORMAT IT           U016
         DROPX R4                       UCB                        U024
         SPACE 2
*        Print the list now
         SPACE 2
RETURN   LA    R2,INDXLIST              -> volume index list       U026
RETLST   C     R2,INDXLPTR              Still in the list?         U026
         BNL   RETURN1                  No, end of scan            U026
         TM    0(R2),LF@FND             Any found here?            U026
         BO    RETLST1                  Yes, try next one          U026
         LA    R1,SPRSLADR              -> suppress list           U026
         LA    R15,2(,R2)               -> index                   U026
         BAL   R14,LISTFIND             Was it suppressed?         U026
         BZ    RETLST1                  Yes, no error              U026
         MVC   LINE(LINEL),BLANKS       Clear the line             U026
         MVC   LINE(28),=C'No volumes found for index -'           U026
         MVC   LINE+29(6),2(R2)         Show what we didn't find   U026
         LA    R0,LINERDW               -> data line               U026
         BAL   R14,PUTLINE              Put it out                 U026
RETLST1  LA    R2,LISTEL(,R2)           -> next list entry         U026
         B     RETLST                   Go check next entry        U026
         SPACE 2
ERRSORT  MVC   LINE(LINEL),BLANKS       Clear the print line       U025
         MVC   LINE(35),=C'Output sort table capacity exceeded'    U025
         LA    R0,LINERDW               -> message line            U025
         BAL   R14,PUTLINE              Tell him about our problem U025
         SPACE 1
RETURN1  CLC   NUMVOLS,=H'1'            Any volumes found?         U026
         BL    RET0                     No, skip                   U029
         TM    FLAG,F@NOHEAD            Need headers?              U026
         BNZ   RETPRT                   No, skip                   U025
         LA    R0,XHDR1RDW              -> Title line              U025
         BAL   R14,PUTLINE              Write first title line     U025
         LA    R0,XHDR2RDW              -> Title line              U025
         BAL   R14,PUTLINE              Write second title line    U025
         SPACE 1
RETPRT   L     R2,AOUTLINA              -> first data line         U025
         SPACE 1
RETPRT1  C     R2,AOUTLINN              Any data left?             U025
         BNL   RETPRT2                  No, finished with data     U028
         LR    R0,R2                    -> data line               U025
         BAL   R14,PUTLINE              Write detail line          U025
         LA    R2,L'OUTLINES(,R2)       -> Next output line        U025
         B     RETPRT1                  Loop                       U025
         SPACE 2
RETPRT2  TM    FLAG,F@TOTALS            Want totals?               U025
         BNO   RET0                     NO - END THE PROGRAM       U029
         CLC   NUMVOLS,=H'2'            MORE THAN ONE VOL FOUND?   U018
         BL    RET0                     NO - SKIP TOTALS LINE      U029
         SPACE 2
*---  FORMAT THE TOTALS LINE                                       U018
         MVC   LINE(LINEL),BLANKS       CLEAR THE DISPLAY LINE     U018
         MVC   LINE+3(14),=C'*** Totals ***'                       U022
         MVC   OLIXVTOC+2(11),=C'*** Max ***'                      U022
         MVC   DBLW(6),=X'402020202020'                            U023
         ED    DBLW(6),TOTCYL+1                                    U023
         MVC   OLFCYL-1(5),DBLW+1                                  U023
         MVC   DBLW(6),=X'402020202020'                            U023
         ED    DBLW(6),TOTTRK+1                                    U023
         MVC   OLFTRK-1(5),DBLW+1                                  U023
         MVC   OLFNUM-2(6),=X'402020202020'                        U023
         ED    OLFNUM-2(6),TOTNUM+1                                U023
FNUM5    MVC   OLCCYL,MAXCYL            MOVE IN LARGEST CONTIG CYL U022
         MVC   OLCTRK,MAXTRK            MOVE IN LARGEST CONTIG TRK U018
         L     R1,TOTDSCB               GET TOTAL # OF FREE DSCBS  U018
         CVD   R1,DBLW                  CONVERT IT                 U018
         MVC   OLFDSCB-1(6),=X'402020202020'                       U023
         ED    OLFDSCB-1(6),DBLW+5                                 U023
         LA    R0,LINERDW               -> DATA LINE               U012
         BAL   R14,PUTLINE              Write the line             U024
         B     RET0                     END THE PROGRAM            U029
         SPACE 2
*--- At entry:  R15 -> 4 char field to blank leading zeroes on     U023
UNZERO   CLI   0(R15),C'0'              leading zero?              U023
         BNER  R14                      no - all done              U023
         MVI   0(R15),C' '              yes - blank it             U023
         CLI   1(R15),C'0'              leading zero?              U023
         BNER  R14                      no - all done              U023
         MVI   1(R15),C' '              yes - blank it             U023
         CLI   2(R15),C'0'              leading zero?              U023
         BNER  R14                      no - all done              U023
         MVI   2(R15),C' '              yes - blank it             U023
         BR    R14                      all done                   U023
         SPACE 2
*---  AT ENTRY:  R0 -> RDW FOR PUTLINE                             U012
PUTLINE  ST    R14,LINKLINE             Save routine linkage       U024
         PUTLINE  OUTPUT=((0),,,DATA),MF=(E,IOPL)                  U024
         SPACE 1
         L     R14,LINKLINE             Restore routine linkage    U024
         BR    R14                      Return to caller           U024
         DROPX R12                      LISTVO                     U025
         EJECT                                                     U025
*
*        Parse exit to validity check volume index entries
*
VALVOL   SAVE  (14,2)                                              U025
         LR    R2,R15                   Routine base address       U025
         USNGX VALVOL,R2                                           U025
         SLR   R15,R15                  Set normal retcode         U025
         L     R14,0(,R1)               -> vol index PDE           U025
         L     R1,0(,R14)               -> vol index data          U025
         CLI   0(R1),C'*'               Special request?           U026
         BE    VALVOL3                  Yes, check validity        U025
         CLI   0(R1),C'='               Special request?           U025
         BE    VALVOL2                  Yes, check length          U025
         CLI   0(R1),C'$'               Special request?           U025
         BE    VALVOL5                  Yes, check length          U025
         CLI   0(R1),C'¬'               Special request?           U025
         BE    VALVOL5                  Yes, check length          U025
         CLI   0(R1),C'-'               Special request?           U025
         BE    VALVOL5                  Yes, check length          U025
         CLI   0(R1),C'#'               National char?             U025
         BE    VALVOL9                  Yes, valid                 U025
         CLI   0(R1),C'@'               National char?             U025
         BE    VALVOL9                  Yes, valid                 U025
         CLI   0(R1),C'A'               Alphabetic?                U025
         BL    VALVOL8                  Invalid, error             U025
         CLI   0(R1),C'I'               Alphabetic?                U025
         BNH   VALVOL9                  Yes, valid                 U025
         CLI   0(R1),C'J'               Alphabetic?                U025
         BL    VALVOL8                  No, error                  U025
         CLI   0(R1),C'R'               Alphabetic?                U025
         BNH   VALVOL9                  Yes, valid                 U025
         CLI   0(R1),C'S'               Alphabetic?                U025
         BL    VALVOL8                  No, error                  U025
         CLI   0(R1),C'Z'               Alphabetic?                U025
         BNH   VALVOL9                  Yes, valid                 U025
         CLI   0(R1),C'0'               Numeric?                   U025
         BL    VALVOL8                  No, error                  U025
         CLI   0(R1),C'9'               Numeric?                   U025
         BNH   VALVOL9                  Yes, valid                 U025
         B     VALVOL8                  No, error                  U025
VALVOL2  CLC   4(2,R14),=H'1'           Exactly one char?          U025
         BNE   VALVOL8                  No, error                  U025
         B     VALVOL9                  Yes, valid                 U025
VALVOL3  CLC   4(2,R14),=H'1'           Exactly one char?          U025
         BNE   VALVOL8                  No, error                  U025
         L     R1,540          PSATOLD  -> current TCB             U026
         L     R1,180(,R1)     TCBJSCB  -> job step CB             U025
         L     R1,264(,R1)     JSCBPSCB -> TSO protected step CB   U025
         USNGX PSCB,R1                                             U025
         TM    &@@SYSP                  Systems programmer?        U025
         BZ    VALVOL8                  No, error                  U025
         B     VALVOL9                  Yes, valid                 U025
         DROPX R1                       PSCB                       U025
VALVOL5  CLC   4(2,R14),=H'1'           More than one char?        U025
         BNH   VALVOL8                  No, error                  U025
         CLC   4(2,R14),=H'7'           More than seven chars?     U025
         BNH   VALVOL9                  No, valid                  U025
VALVOL8  LA    R15,4                    Indicate invalid parm      U025
VALVOL9  RETURN (14,2),RC=(15)          Return to caller (IKJPARS) U025
         DROPX R2                       VALVOL                     U025
         TITLE 'LISTVO    -- CONSTANTS'
         LTORG                                                     U025
         SPACE 2
BLANKS   DC    CL80' '                                             U007
HEADER1  DC    C'Volume  Dev  Use Count  ---------- Free ---------   Co$
               ntiguous  IX '                                      U022
HEADERL1 EQU   *-HEADER1                                           U004
         DC    C' Unit Mount'                                      U022
HEADERL2 EQU   *-HEADER1                                           U004
         SPACE 1
HEADER2  DC    C' Name   Adr   Alc Opn   Cyl   Trk   Extents  DSCB     $
               Cyl Trk  VTOC'                                      U022
         DC    C'   Status  '                                      U022
         SPACE 3                                                   U007
*---  MESSAGES USE 'WTO' MACRO ONLY FOR THE PURPOSE OF GENERATING
*---  A VARIABLE LENGTH MESSAGE.  THESE ARE *NOT* REAL WTO'S.      U012
         SPACE 1
MSGPARSE WTO   'PARSE FAILED',MF=L                                 U029
         SPACE 2
MSGNALL  WTO   'RESTRICTED OPERAND - "ALL"',MF=L                   U014
         SPACE 2
CVPLX    CVAFDSM  ACCESS=MAPDATA,MAP=VTOC,COUNT=YES,MF=L           U016
         SPACE 1
CVPLLEN  EQU   *-CVPLX                                             U016
         SPACE 2
GQSCANX  GQSCAN  AREA=(,AREAL),REQLIM=MAX,SCOPE=ALL,REQCNT=1,MF=L  U017
         SPACE 1
GQSCANXL EQU   *-GQSCANX                                           U017
         SPACE 2
*DCBX    DCB   DDNAME=*-*,???           FOR VTOC FOR CVAF          U017
         SPACE 1
*DCBXL   EQU   *-DCBX                                              U017
         SPACE 2
NAMELIST DC    0C' '                                               U024
         @GLOBALC LISTVO,NAMELIST                                  U024
         DC    5CL(UNITNLEN)' '         Patch space for 5 lists    U024
         DC    C' '                     End flag for NAMELIST      U024
         SPACE 2
         PRINT NOGEN
PCL      IKJPARM DSECT=PDL
VOL      IKJIDENT 'VOLUME SEARCH LIST NAME',LIST,                  U025$
               FIRST=ANY,OTHER=ALPHANUM,VALIDCK=VALVOL,            U025$
               MAXLNTH=8,PROMPT='VOLUME SEARCH LIST NAME'          U024
STAT     IKJKEYWD
         IKJNAME  'STATUS'                                         U010
         IKJNAME  'NOSTATUS'                                       U010
HEAD     IKJKEYWD
         IKJNAME  'HEADER'                                         U010
         IKJNAME  'NOHEADER'                                       U010
TOTALS   IKJKEYWD
         IKJNAME  'TOTALS'                                         U018
         IKJNAME  'NOTOTALS'                                       U018
SORTS    IKJKEYWD DEFAULT='BYVOLSER'                               U025
         IKJNAME  'BYVOLSER',ALIAS=('BYVOLUME','BYVOLID')          U025
         IKJNAME  'BYADDRESS',ALIAS=('BYDEVADDRESS','BYDEVADR')    U025
         IKJNAME  'BYFREE'                                         U025
         IKJNAME  'BYCONTIGFREE'                                   U025
BYVOL    EQU   1                                                   U025
BYADDR   EQU   2                                                   U025
BYFREE   EQU   3                                                   U025
BYCNTG   EQU   4                                                   U025
         IKJENDP
         PRINT GEN
         TITLE 'LISTVO  - DYNAMIC STORAGE DEFINITIONS'
WORKD    DSECT                                                     U017
         SPACE 1
LISTNAME DS    CL8                                                 U024
LINKUNIT DS    A                                                   U024
LINKINDX DS    A                                                   U025
LINKLINE DS    A                                                   U024
         SPACE 1
* The following three lines MUST remain in order
INDXLADR DS    A                        -> first byte in list area U025
INDXLPTR DS    A                        -> next byte in list area  U025
INDXLLIM DS    A                        -> last byte in list area  U025
         SPACE 1
* The following three lines MUST remain in order
SPRSLADR DS    A                        -> first byte in list area U025
SPRSLPTR DS    A                        -> next byte in list area  U025
SPRSLLIM DS    A                        -> last byte in list area  U025
         SPACE 1
AOUTLINA DS    A                        -> output lines area       U025
AOUTLINL DS    A                        -> end of area +1          U025
AOUTLINN DS    A                        -> next output line        U025
SORTRTN  DS    A                        -> sort compare routine    U025
PUTLNMFL PUTLINE  MF=L                                             U012
         SPACE 1
CVPL     CVAFDSM  ACCESS=MAPDATA,MAP=VTOC,COUNT=YES,MF=L           U016
         SPACE 2
IOSPARM  DC    A(IOSWORK,IOSDVCLS,IOSAUCB+X'80000000')             U019
IOSAUCB  DC    A(0)                     IOSVSUCB RETURNED UCB ADDR U019
IOSWORK  DC    XL100'00'                IOSVSUCB SERVICE WORK AREA U019
         SPACE 2
LINERDW  DS    2H                       PUTLINE RECORD DESC WORD   U012
LINE     DS    0C
OLVOLSER DS    CL6
         DS    2C                                                  U004
OLDEVADR DS    CL3
         DS    C                                                   U023
OLUALLOC DS    CL5                                                 U023
         DS    C                                                   U004
OLUOPEN  DS    CL3                                                 U004
         DS    2C                                                  U022
OLMSG    EQU   *+1                      MESSAGES GO HERE           U022
OLFCYL   DS    CL4                                                 U022
         DS    2C                                                  U018
OLFTRK   DS    CL4
         DS    4C                                                  U015
OLFNUM   DS    CL4                                                 U015
         DS    3C                                                  U022
OLFDSCB  DS    CL5                                                 U022
         DS    4C                                                  U022
OLCCYL   DS    CL4                                                 U022
         DS    C
OLCTRK   DS    CL3
         DS    3C                                                  U011
OLIXVTOC DS    CL2                                                 U011
OLMSGL   EQU   *-OLMSG                  MAX LENGTH AVAIL FOR MSGS  U006
         DS    2C                                                  U011
OLSTAT1  DS    CL4                                                 U004
         DS    C'/'                                                U004
OLSTAT2  DS    CL5                                                 U004
LINEL    EQU   *-LINE
         SPACE 2
IOSDVCLS DC    AL1(UCB3DACC)            IOSVSUCB DASD DEVICE CLASS U019
         SPACE 1
FLAG     DS    X                                                   U001
F@DFLT   EQU   X'80'                    Need to use default unit   U025
F@STAT   EQU   X'40'                    WANTS STATUS ALSO          U004
F@NOHEAD EQU   X'10'                    DOESN'T WANT HEADER LINES  U007
F@AUTH   EQU   X'08'                    Running APF authorized     U024
F@TOTALS EQU   X'04'                    WANT TOTALS LINE           U018
         SPACE 2
CLEAR    EQU   *                        START OF AREA TO ZERO      U018
NUMVOLS  DS    H                        NUMBER OF VOLUMES FOUND    U018
TOTCYL   DS    PL4                      TOTAL NUMBER OF FREE CYL   U018
TOTTRK   DS    PL4                      TOTAL NUMBER OF FREE TRK   U018
TOTNUM   DS    PL4                      TOTAL NUMBER OF FREE EXT   U018
TOTDSCB  DS    F                        TOTAL                      U018
MAXCYL   DS    CL4                      MAX CONTIG CYL             U018
MAXTRK   DS    CL4                      MAX CONTIG TRK             U018
CLEARL   EQU   *-CLEAR                  LENGTH TO ZERO             U018
PWORK    DS    PL4                      WORK AREA FOR PACK         U018
DBLW     DS    4D
XHDR1RDW DS    2H                       RDW FOR PUTLINE            U012
XHDR1    DS    CL(HEADERL2)                                        U011
XHDR2RDW DS    2H                       RDW FOR PUTLINE            U012
XHDR2    DS    CL(HEADERL2)                                        U011
CAM      DS    0D
CAML     DS    4F                       OBTAIN LIST
CAMV     DS    2F                       VOL NAME
         DS    0D
CAMW     DS    140X                     CAMLST WORK AREA
TOKEN    DS    F                        TOKEN FOR GQSCAN           U017
RIBLS    DS    0F                       RIB LENGTHS...             U017
RIBL     DS    H                        LENGTH OF RIB              U017
RIBEL    DS    H                        LENGTH OF RIBE             U017
GQLIST   GQSCAN  MF=L                                              U017
LISTEL   EQU   1+1+6                    Index list entry length    U026
LF@FND   EQU   X'80'                    Volume was found           U026
INDXLIST DS    64XL(LISTEL)             Target vol index list      U026
INDXLL   EQU   *-INDXLIST               Max length of target list  U025
SPRSLIST DS    16XL(LISTEL)             Suppressed vol index list  U026
SPRSLL   EQU   *-SPRSLIST               Max length of suppress lst U025
AREA     DS    CL4096                   GQSCAN ANSWER AREA         U017
AREAL    EQU   *-AREA                                              U017
         DS    0H                       Align the following field  U025
OUTLINES DS    500XL(LINEL+4)           Room for 500 output lines  U025
OUTLINEL EQU   *-OUTLINES               Length of output area      U025
         DS    H                        OUTLINES ending flag       U025
WORKLEN  EQU   *-WORKD                                             U017
         TITLE 'LISTVO    -- DSECTS'
         PRINT NOGEN                                               U025
         CVT   DSECT=YES                                           U019
         SPACE 2
UCB      DSECT
         IEFUCBOB
         SPACE 2
         IKJPSCB
         SPACE 2
         IKJECT
         SPACE 2
          AIF   (&@@SPLVL GE 200).GRS91                            U022
         IHAQCB
         SPACE 2
         IHAQEL
.GRS91    ANOP                                                     U022
         SPACE 2
         ISGRIB  ,                                                 U017
         SPACE 2
         IHAASVT  ,                                                U017
         SPACE 2
         IHAASCB  ,                                                U017
         SPACE 2
         END
