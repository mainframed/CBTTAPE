***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1988 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.  (Please see reason mentioned       *
*        below.)                                                      *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.  (Please see reason        *
*        mentioned below.)                                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1.3, w/DFP 3.2),   *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc!ldw                                                *
*                                                                     *
*          (213) 740-2875 (direct -- 11 am to 7 pm Pacific time)      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, 714, or 310.  Sorry.                                  *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
DPAGE  TITLE '-----  DISPLAY NAMES OF ACTIVE PAGE/SWAP DATASETS  -----'
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM NAME:  DPAGE                                              *
*                                                                     *
*   AUTHOR:        LEONARD D. WOREN                                   *
*                                                                     *
*   DATE:          APRIL 10, 1980                                     *
*                                                                     *
*   PURPOSE:       DISPLAY INFORMATION ON CURRENTLY ACTIVE PAGE AND   *
*                  SWAP DATASETS.                                     *
*                                                                     *
*   Updates:                                                          *
*      24Sep88  LDW   - Update for XA 2.2                             *
*                     - Use macros instead of offsets                 *
*                     - Change some things to lower case              *
*      12/01/82 LDW   - ADD PFK6 (ADR) TO DISPLAY CB ADDRESSES        *
*      05/06/82 LDW   - MAKE IT WORK ON EMS'S MOD 5                   *
*                     - FIX NON-FULLSCR SUPPORT                       *
*      04/21/81 LDW   - CHANGE ENTR TO OSENTER                        *
*                     - ADD 'ATTR' COMMAND TO DISPLAY DS ATTRIBUTES   *
*                     - MAKE PROGRAM RE-ENTRANT                       *
*      11/05/80 LDW   - EXPAND PAGE DS TYPE FROM 1 TO 4 CHARS         *
*                     - ACCEPT "END" TO LEAVE                         *
*                     - NEW INTERFACE TO DCMS TERMIO                  *
*      05/22/80 LDW   CONVERT TO DCMS (FULLSCREEN)                    *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
DPAGE    OSENTER  EXIT=LEAVE,RC=0,GETMAIN=(WORKLEN,WORKD),AMODE=31
REPEAT   MVC   LASTCMD,BLANKS
         MVC   ERRMSG,BLANKS
         MVI   FLAG,0                   INIT FLAG
         SPACE 2
         GTSIZE  ,                      GET THE TERMINAL LINE SIZE
         SPACE 1
         CH    R1,=H'80'                80 OR MORE COLUMNS WIDE?
         BL    LOOP                     NO - SKIP
         CH    R0,=H'24'                24 OR MORE LINES?
         BL    LOOP                     NO - SKIP
         OI    FLAG,F3270               YES - SET 3270 FLAG
         MVC   IOCBNAME(8),=CL8'DPAGE'
         SPACE 1
         LOAD  EPLOC==CL8'TERMIO'       LOAD DCMS TERMINAL I/O ROUTINE
         ST    R0,IOCBATIO              SAVE ADDRESS
         SPACE 1
         STFSMODE  ON,INITIAL=YES
         SPACE 2
LOOP     LA    R14,HEADER               -> START
         LH    R15,SCR1                 LENGTH OF SCREEN BUFFER
         L     R1,=X'40000000'          GET PAD CHAR & LEN=0
         MVCL  R14,R0                   CLEAR THE SCREEN BUFFER
         MVC   HEADER(40),=C'Type   Size  Inuse  Avail  Error  Dsname'
         TM    FLAG,FADDR               DISPLAY ADDRESSES?
         BNO   *+10                     NO - SKIP
         MVC   HEADER+73(6),=C'@PARTE'
         TM    FLAG,FATTR               DISPLAY ATTRIBUTES?
         BNO   *+10                     NO - SKIP
         MVC   HEADER+34(41),=C'Volume adr type   PGWT  PCTRQTIM  PARER$
               QTM'
         MVC   REPLY,LASTCMD            MOVE IN LAST CMD (ONLY IF BAD)
         MVC   MODEMSG+10(20),ERRMSG    AND ERROR MSG IF ANY
         MVC   LASTCMD,BLANKS           CLEAR OUT THE BAD COMMAND
         MVC   ERRMSG,BLANKS            AND THE ERROR MSG
         LA    R10,LINEONE-79           -> FIRST SCREEN LINE (SORT OF)
         LA    R9,20+1                  MAX LINES (SORT OF)
         USING LINE,R10
         TM    FLAG,F3270               FULLSCREEN MODE?
         BO    NO$HDR                   YES - SKIP
         LA    R10,REPLY+79             -> LINE BUFFER TO USE
         TPUTX HEADER,79                PUT OUT THE HEADER
         TPUTX '-------------------------------------------------------$
               ------------------------'
         SPACE 3
NO$HDR   L     R1,16                    -> CVT
         L     R1,X'2C0'(,R1)           -> ASMVT
         L     R2,8(,R1)                -> PART
         USNGX PART,R2
         L     R3,PARTDSNL  24          -> DSN LIST IN CSA
         L     R4,PARTSIZE   4          NUMBER OF PARTE'S IN PART
         L     R5,PARTEUSE   8          NUMBER OF PARTE'S INUSE
         LA    R6,PARTENTS  80          -> FIRST PARTE
         NI    FLAG,255-FSWAP           NOT SWAP DATASETS YET
         BAL   R11,SPEW
         SPACE 3
         L     R1,16                    -> CVT
         L     R1,X'2C0'(,R1)           -> ASMVT
         L     R2,4(,R1)                -> SART
*  Should have a SART dsect, but we'll use PART for now...
         L     R3,24(,R2)               -> DSN LIST IN CSA
         L     R4,4(,R2)                NUMBER OF SARTE'S IN SART
         L     R5,8(,R2)                NUMBER OF SARTE'S INUSE
         LA    R6,80(,R2)               -> FIRST SARTE
         OI    FLAG,FSWAP               SWAP DATASETS FROM HERE ON
         BAL   R11,SPEW
         DROPX R2                       PART
         TM    FLAG,F3270               FULLSCREEN MODE?
         BO    WRITE                    YES - DO DCMS WRITE
         SPACE 2
TGET     TPUTX 'DPAGE:',ASIS            PROMPT HIM
         SPACE 1
*        TGET  REPLY,L'REPLY            LET HIM SAY SOMETHING
         LA    R1,REPLY
         LA    R0,L'REPLY
         O     R1,=X'80000000'          SET TGET FLAG
         SVC   93                       TGET
         SPACE 1
         OC    REPLY,BLANKS             FOLD IT
         B     ENTER                    DECODE COMMAND
         EJECT
WRITE    TM    FLAG,FAUTO               AUTO UPDATE?
         BO    WRT$AUTO                 YES - GO DO IT
         MVI   IOCBFLGD,0               CLEAR FLAGS
         SPACE 1
         TRMIO IOCB,FORMAT=SCR1,IMAGE=HEADER
         SPACE 2
PFKEY    PFKEY ATTN=ATTN,DEF=BADKEY,ENTER=ENTER,K1=CMD$HELP,K2=UPDATE, $
               K3=QUIT,K4=FLIP$ACT,K5=FLIP$ATR,K6=FLIP$ADR,            $
               K9=REPEAT,K12=QUIT
         SPACE 2
ENTER    CLI   REPLY,C' '               ANY REPLY?
         BE    LOOP                     NO
         CLI   REPLY,C'E'               WANT OUT?
         BE    QUIT
         CLC   =C'AL',REPLY             ALL PAGE DATASETS?
         BE    CMD$ALL
         CLC   =C'AC',REPLY             ONLY ACTIVE PAGE DATASETS?
         BE    CMD$ACT
         CLC   =C'AT',REPLY             DISPLAY ATTRIBUTES?
         BE    CMD$ATTR
         CLC   =C'DS',REPLY             DISPLAY DSNAMES?
         BE    CMD$DSN
         CLC   =C'AD',REPLY             DISPLAY CTL BLOCK ADDRESSES?
         BE    CMD$ADDR
         CLI   REPLY,C'H'               NEED HELP?
         BE    CMD$HELP
         CLI   REPLY,C'?'               NEED HELP?
         BE    CMD$HELP
         MVC   LASTCMD,REPLY
         MVC   ERRMSG(19),=C'* INVALID COMMAND *'
         B     LOOP
         SPACE 3
BADKEY   MVC   ERRMSG+1(17),=C'* UNKNOWN PFKEY *'
         B     LOOP
         SPACE 3
ATTN     TM    FLAG,FAUTO               IN AUTO UPDATE MODE?
         BNO   QUIT                     NO - GET OUT OF HERE
         SPACE 2
UPDATE   XI    FLAG,FAUTO               FLIP FLAG
         B     LOOP                     AND DO IT AGAIN
         SPACE 3
FLIP$ACT XI    FLAG,FACTIVE             FLIP FLAG
         B     LOOP                     AND DO IT AGAIN
         SPACE 3
FLIP$ATR XI    FLAG,FATTR               FLIP FLAG
         NI    FLAG,255-FADDR           FORCE OFF THIS FLAG
         B     LOOP                     AND DO IT AGAIN
         SPACE 3
FLIP$ADR XI    FLAG,FADDR               FLIP FLAG
         NI    FLAG,255-FATTR           FORCE OFF THIS FLAG
         B     LOOP                     AND DO IT AGAIN
         SPACE 3
CMD$ALL  NI    FLAG,255-FACTIVE         RESET FLAG
         B     LOOP                     AND DO IT AGAIN
         SPACE 3
CMD$ACT  OI    FLAG,FACTIVE             SET FLAG
         B     LOOP                     AND DO IT AGAIN
         SPACE 3
CMD$ATTR OI    FLAG,FATTR               SET FLAG
         NI    FLAG,255-FADDR           RESET OTHER FLAG
         B     LOOP                     AND DO IT AGAIN
         SPACE 3
CMD$DSN  NI    FLAG,255-FATTR-FADDR     RESET FLAGS
         B     LOOP                     AND DO IT AGAIN
         SPACE 3
CMD$ADDR OI    FLAG,FADDR               SET FLAG
         NI    FLAG,255-FATTR           FORCE OFF THIS FLAG
         B     LOOP                     AND DO IT AGAIN
         SPACE 3
CMD$HELP TM    FLAG,F3270               FULLSCREEN?
         BNO   HELPTPUT                 NO - DO IT LINE BY SLOW LINE
         MVC   HELPHDR,HELPIMG          MOVE IN HELP HEADER LINE
         MVC   REPLYH,BLANKS            CLEAR REPLY
         MVC   MODEMSGX,BLANKS
         LA    R14,HELPONE              -> START
         LA    R15,HELPLEN              LENGTH OF SCREEN BUFFER
         LA    R0,HELPLNE1              -> SCREEN IMAGE
         LR    R1,R15                   GET PAD CHAR & LENGTH
         MVCL  R14,R0                   COPY THE HELP SCREEN IMAGE
         MVI   IOCBFLGD,0               CLEAR FLAGS
         OI    IOCBFLGC,IOCBCTIM        DO NOT TIMESTAMP HELP SCREEN
         SPACE 1
         TRMIO IOCB,FORMAT=HELPSCR,IMAGE=HELPHDR
         SPACE 2
         NI    IOCBFLGC,255-IOCBCTIM    RESET:  TIMESTAMP NEXT SCREEN
         MVC   REPLY,REPLYH             MOVE REPLY
         B     PFKEY                    DECODE PF KEYS
         SPACE 2
HELPTPUT TPUTX HELPIMG,79               PUT OUT HELP HEADER
         SPACE 1
         LA    R10,HELPLNE1             -> FIRST HELP LINE
         LA    R9,HELPLEN/79            NUMBER OF HELP LINES
         SPACE 1
HELPLOOP TPUTX (R10),79
         LA    R10,79(,R10)             -> NEXT HELP LINE
         BCT   R9,HELPLOOP              DO ALL LINES
         B     TGET                     LET HIM ASK FOR SOMETHING
         SPACE 3
WRT$AUTO OI    IOCBFLGD,IOCBDYON+IOCBDFRC
         SPACE 1
         TRMIO IOCB,WRITE,FORMAT=SCR1,IMAGE=HEADER
         SPACE 2
         PFKEY ATTN=ATTN,DEF=LOOP
         SPACE 3
QUIT     STFSMODE  OFF
         SPACE 1
         B     LEAVE
         EJECT
         USNGX PARTENT,R6
SPEW     TM    FLAG,FACTIVE             ONLY DISPLAY ACTIVE DATASETS?
         BNO   SPEW$ALL                 NO - SKIP
         SH    R5,=H'1'                 DECREMENT IN-USE COUNT
         BM    SPEW$NXT                 SKIP IF NO MORE IN USE
         TM    PAREFLG1,X'80'           NOT IN USE?
         BO    SPEW$NXT                 YES - SKIP
         SPACE 1
SPEW$ALL TM    FLAG,F3270               FULLSCREEN MODE?
         BNO   SPEW$GO                  NO - SKIP THIS STUFF
         LA    R10,79(,R10)             -> NEXT SCREEN LINE
         BCT   R9,SPEW$GOF              CONTINUE IF NOT FULL
         MVC   HEADER+50(28),=C'** TOO MANY PAGE DATASETS **'
         B     WRITE
         SPACE 2
SPEW$GO  MVC   LINE(79),BLANKS          CLEAR PRINT LINE
SPEW$GOF TM    FLAG,FATTR               WANT ATTRIBUTES?
         BNO   SPEW$DSN                 NO - GIVE DSNAME
         TM    PAREFLG1,X'80'           PARTE/SARTE IN USE?
         BO    SPEW$NUM                 NO - SKIP THIS SECTION OF CODE
         CLC   BLANKS(44),0(R3)         ANY DSNAME HERE?
         BE    SPEW$NUM                 NO - SKIP THIS SECTION OF CODE
         L     R15,PAREUCBP             -> UCB FOR THIS PAGE DATASET
         MVC   VOLUME,28(R15)           MOVE IN VOLSER
         MVC   UNIT,13(R15)             AND UNIT ADDRESS
         L     R15,PAREPCTP             -> PCT OR SDCTE
         TM    FLAG,FSWAP               SWAP DATASET?
         BO    SPEWSWAP                 YES - GO FORMAT SDCTE
*                                       NO - FORMAT PCT
         MVC   UNITTYPE,4(R15)          MOVE DEVICE TYPE
         HEX   PAGEWGHT,(28,R15),LEN=2  PAGING WEIGHT FOR THIS DEV TYPE
         HEX   MINTIME,(32,R15),LEN=4   MIN SLOT READ OR WRITE TIME
         HEX   CORRVAL,PARERQTM,LEN=4   CORRECTION VALUE FOR SRV BURST
         B     SPEW$NUM                 SKIP OTHER MVC
         SPACE 2
SPEWSWAP MVC   UNITTYPE,0(R15)          MOVE DEVICE TYPE
         B     SPEW$NUM                 SKIP OTHER MVC
         SPACE 2
SPEW$DSN MVC   DSN,0(R3)                COPY THE DATASET NAME
         TM    FLAG,FADDR               WANT PARTE/SARTE ADDRESS?
         BNO   SPEW$NUM                 NO - SKIP
         MVI   ADDR-1,C' '              BLANK OUT IN CASE LONG DSN
         ST    R6,DWD                   SET DOWN ADDRESS FOR CONVERT
         HEX   ADDR,DWD,LEN=4
         SPACE 2
SPEW$NUM TM    PAREFLG1,X'40'           ASM DETECTED ERRORS?
         BNO   *+10                     NO - SKIP
         MVC   LINE+79-5-6(5),=C'*BAD*' YES - SAY SO
         MVC   TYPE,=C'SWAP '           ASSUME SWAP
         TM    FLAG,FSWAP               SWAP?
         BO    GOT$TYPE                 YES - SKIP
         MVC   TYPE,=C'PLPA '           ASSUME PLPA
         TM    PARETYPE,X'80'           IS IT?
         BO    GOT$TYPE                 YES
         MVC   TYPE,=C'Commn'           ASSUME COMMON
         TM    PARETYPE,X'40'           IS IT?
         BO    GOT$TYPE                 YES
         MVC   TYPE,=C'Duplx'           ASSUME DUPLEX
         TM    PARETYPE,X'20'           IS IT?
         BO    GOT$TYPE                 YES
         MVC   TYPE,=C'Local'           ASSUME LOCAL
         TM    PARETYPE,X'10'           IS IT?
         BO    GOT$TYPE                 YES
         MVC   TYPE,=C'  ?  '           HUH?
         SPACE 2
GOT$TYPE L     R0,PARESZSL              GET SIZE
         CVD   R0,DWD                   CONVERT
         OI    DWD+7,X'0F'              REMOVE SIGN
         MVC   SIZE(5),=X'2020202120'   MOVE IN MASK
         ED    SIZE-1(6),DWD+5
         SPACE 1
         S     R0,PARESLTA              COMPUTE INUSE=SIZE-AVAIL
         CVD   R0,DWD                   CONVERT
         OI    DWD+7,X'0F'              REMOVE SIGN
         MVC   INUSE(5),=X'2020202120'  MOVE IN MASK
         ED    INUSE-1(6),DWD+5
         SPACE 1
         L     R0,PARESLTA              GET AVAIL
         CVD   R0,DWD                   CONVERT
         OI    DWD+7,X'0F'              REMOVE SIGN
         MVC   AVAIL(5),=X'2020202120'  MOVE IN MASK
         ED    AVAIL-1(6),DWD+5
         SPACE 1
         L     R0,PARERRCT              GET ERROR COUNT
         CVD   R0,DWD                   CONVERT
         OI    DWD+7,X'0F'              REMOVE SIGN
         MVC   ERROR(5),=X'2020202120'  MOVE IN MASK
         ED    ERROR-1(6),DWD+5
         SPACE 2
         TM    FLAG,F3270               FULLSCREEN MODE?
         BO    SPEW$NXT                 YES - SKIP
         TPUTX LINE,79                  NO - PRINT OUT THE LINE NOW
         SPACE 2
SPEW$NXT LA    R3,44(,R3)               -> NEXT DSN
         LA    R6,PARTELEN(,R6)         -> NEXT PARTE
         TM    FLAG,FSWAP               IS THIS REALLY A SWAP?
         BNO   *+8                      NO - SKIP
         SH    R6,=Y(64-48)             ADJUST TO SIZE OF SARTE
         BCT   R4,SPEW                  LOOP
         BR    R11                      RETURN TO CALLER
         SPACE 2
         DROPX R6                       PARTENT
         SPACE 3
         LTORG
         SPACE 3
BLANKS   DC    CL79' '
HEXTAB   HEXTAB  DUAL=NO
         SPACE 2
         SPACE 2
HELPIMG  DC    CL79'                           << D P A G E  --  HELP >$
               >'
         DC    CL79'                                                   $
                                           '
HELPLNE1 DC    CL79'    PF KEY DEFINITIONS               COMMANDS      $
                                           '
         DC    CL79'                                                   $
                                           '
         DC    CL79'_________________________         ACT - SELECT ACTI$
               VE DATASETS ONLY            '
         DC    CL79'|PF1 /13|PF2 /14|PF3 /15|         ALL - DISPLAY ALL$
                PARTES/SARTES              '
         DC    CL79'| HELP  | AUTO  |  END  |         ATR - DISPLAY DS $
               ATTRIBUTES                  '
         DC    CL79'|_______________________|         DSN - DISPLAY DSN$
               AMES                        '
         DC    CL79'|PF4 /16|PF5 /17|PF6 /18|         ADR - DISPLAY PAR$
               TE/SARTE ADDRESSES          '
         DC    CL79'|ACT/ALL|ATR/DSN|ADR    |         END - EXIT FROM P$
               ROGRAM                      '
         DC    CL79'|_______________________|                          $
                                           '
         DC    CL79'|PF7 /19|PF8 /20|PF9 /21|                          $
                                           '
         DC    CL79'|       |       |RESTART|                          $
                                           '
         DC    CL79'|_______________________|                          $
                                           '
         DC    CL79'|PF10/22|PF11/23|PF12/24|                          $
                                           '
         DC    CL79'|       |       |  END  |                          $
                                           '
         DC    CL79'|_______________________|                          $
                                           '
HELPLEN  EQU   *-HELPLNE1               LENGTH OF HELP DATA
         SPACE 2
LINE     DSECT
TYPE     DS    CL5,CL2
SIZE     DS    CL5,CL2
INUSE    DS    CL5,CL2
AVAIL    DS    CL5,C
ERROR    DS    CL5,CL2
DSN      DS    CL44,C
         ORG   LINE+79-8
ADDR     DS    CL8
         ORG   DSN
VOLUME   DS    CL6,C
UNIT     DS    CL3,C
UNITTYPE DS    CL6,C
PAGEWGHT DS    CL4,CL2
MINTIME  DS    CL8,CL2
CORRVAL  DS    CL8,C
         ORG   ,
         SPACE 3
DPAGE    CSECT                          RESUME
         EJECT
         PRINT NOGEN
         SPACE 1
SCR1     DCMSFMT
HEADER   DCMSFLD  79
REPLY    DCMSFLD  10,ALPHA,INTEN=HI,CURSOR=YES
MODEMSG  DCMSFLD  68,INTEN=HI
LINEONE  DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFEND
         SPACE 3
HELPSCR  DCMSFMT
HELPHDR  DCMSFLD  79
REPLYH   DCMSFLD  10,ALPHA,INTEN=HI,CURSOR=YES
MODEMSGX DCMSFLD  68,INTEN=HI
HELPONE  DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFLD  79,INTEN=LO
         DCMSFEND
         SPACE 3
WORKD    DSECT                          RESUME
DWD      DS    D
LASTCMD  DS    CL10
ERRMSG   DS    CL20
         SPACE 1
FLAG     DS    B
F3270    EQU   X'80'                    TERMINAL IS A 3270
FAUTO    EQU   X'40'                    IN AUTO UPDATE MODE
FSWAP    EQU   X'20'                    NOW FORMATTING SWAP DATASETS
FACTIVE  EQU   X'10'                    ONLY DISPLAY ACTIVE DATASETS
FATTR    EQU   X'08'                    DISPLAY DS ATTRIBUTES
FADDR    EQU   X'04'                    DISPLAY PARTE/SARTE ADDRESSES
         SPACE 2
IOCB     IOCB
         EJECT
         DSGEN SCR1                     SCREEN BUFFER
         SPACE 2
         DSGEN HELPSCR                  HELP SCREEN BUFFER
         SPACE 2
WORKLEN  EQU   *-WORKD
*        ILRPART
PART     DSECT
PARTIDEN DC    CL4'PART'
PARTSIZE DS    F    +4
PARTEUSE DS    F    +8
PARTCIR0 DS    A    +C
PARTCIR1 DS    A    +10
PARTCIR2 DS    A    +14
PARTTPAR DS    0A   +18
PARTDSNL DS    A    +18
PARTPCTQ DS    A    +1C
PARTLCNT DS    H    +20
PARTFLG1 DS    B    +22
         DS    X    +23
PARTNPCF DS    A    +24
PARTNPCL DS    A    +28
PARTTIME DS    F    +2C
         DS    CL32 +30
PARTENTS EQU   *
         SPACE 2
PARTENT  DSECT
PAREPARE DS    A    +0
PAREBRST DS    AL1  +4
PAREIORN DS    AL1  +5
         DS    XL2  +6
PARETYPE DS    B    +8
PAREFLG1 DS    B    +9
PARENN   DS    H    +A
PAREDEIB DS    A    +C
PARESZSL DS    F    +10
PARESLTA DS    F    +14
PARERRCT DS    F    +18
PAREIORB DS    A    +1C
PAREPATP DS    A    +20
PAREPCTP DS    A    +24
PAREEDBP DS    A    +28
PAREUCBP DS    A    +2C
*  Following are unique for the PARTE
PARETIOR DS    A    +30
PARETIME DS    F    +34
PARERQTM DS    F    +38
PARELSLT DS    H    +3C
PAREREQS DS    H    +3E
         DS    XL32 +40                 maybe something new for 2.2???
PARTELEN EQU   *-PARTENT
         SPACE 2
         END
