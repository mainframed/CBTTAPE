***********************************************************************
*                                                                     *
*                *===================================*                *
*                *  COPYRIGHT NOTICE AND DISCLAIMER  *                *
*                *===================================*                *
*                                                                     *
*   Copyright (c) 1991 by Leonard D. Woren.                           *
*   All rights reserved, except as explicitly noted herein.  This     *
*   program may be used, modified, and distributed, provided that     *
*   all of the following conditions are met:                          *
*                                                                     *
*   (1)  This notice and all references to the original author(s)     *
*        are retained forever in all copies and versions of the       *
*        source.                                                      *
*                                                                     *
*   (2)  This program may be distributed via "public domain" mods     *
*        tapes, such as the SHARE mods tape, the CBT (Connecticut     *
*        Bank and Trust) tape, the LA MVSUG (Los Angeles MVS User's   *
*        Group) tape, etc, etc.  Only versions of this program        *
*        authorized by Leonard D. Woren may be placed on these        *
*        tapes.  Distribution of modified versions of this program,   *
*        via the above named tapes or via any method, is              *
*        specifically prohibited.  (Please see reason mentioned       *
*        below.)                                                      *
*                                                                     *
*   (3)  Permission is specifically NOT given to distribute MODIFIED  *
*        versions of this program.  Modified versions may be used     *
*        only at the site making the mods.  (Please see reason        *
*        mentioned below.)                                            *
*                                                                     *
*   (4)  The only charge which may be made for distribution is to     *
*        recover real costs, such as postage, or creating a tape.     *
*                                                                     *
*   (5)  The only charge for running the program which may be made    *
*        is your normal charge for computer time.                     *
*                                                                     *
*                                                                     *
*   The reason for the restrictions on distribution of modified       *
*   versions is to try to prevent circulation of many different       *
*   versions of the program, each with a few features that aren't     *
*   in any other version.                                             *
*                                                                     *
*   Since everyone will benefit from this, please send all updates    *
*   to me.  (Address below.)  I will try merge them in, if they       *
*   have been made to a reasonably current version of the source,     *
*   and if they are in keeping with the general design of the rest    *
*   of the program.  Mods may be altered by me for this purpose.      *
*   Any such mods which are incorporated into the program will then   *
*   become governed by the restrictions specified here for the whole  *
*   program, with appropriate credit to the contributor.              *
*                                                                     *
*   Although this program has been extensively tested, and is in use  *
*   in a production environment (MVS/ESA release 3.1.3, w/DFP 3.2),   *
*   no guarantee is made of (or responsibility assumed for) correct   *
*   or reliable operation.  I may try to help with problems.  I do    *
*   not assume any responsibility to distribute updates.              *
*                                                                     *
*   CONTACT:                                                          *
*          Leonard D. Woren                                           *
*        snail-mail:                                                  *
*          University of Southern California                          *
*          University Park  Mail Code 0251                            *
*          Los Angeles, CA   90089-0251                               *
*                                                                     *
*        e-mail:                                                      *
*          LDW@USCMVSA.BITNET                                         *
*          LDW@MVSA.USC.EDU                                           *
*          ...!usc!ldw                                                *
*                                                                     *
*          (213) 740-2875 (direct -- 11 am to 7 pm Pacific time)      *
*                                                                     *
*          I prefer electronic mail.  If you call me long distance    *
*          and leave a msg because you didn't get me, there's a good  *
*          chance that I won't call back.  Moral:  don't leave a      *
*          message for me to call you back unless you're in 213,      *
*          818, 714, or 310.  Sorry.                                  *
*                                                                     *
*          SHARE installation code:  USC                              *
*          GUIDE installation code:  OUY                              *
*                                                                     *
***********************************************************************
         EJECT ,
CORE     TITLE 'CORE --  DCMS STORAGE ALTER/DISPLAY'
*---------------------------------------------------------------------*
*                                                                     *
*  Updates:                                                           *
*                                                                     *
*    26May91  U072 LDW  Fix bug in LPA search code                    *
*                                                                     *
*    21May91  U071 LDW  Fix UCB command for MVS/XA                    *
*                                                                     *
*    18Sep90  U070 LDW  Add WHERE command (from Mike Stein's LOOKXA)  *
*             U069 LDW  Add "LPA" command (like "M", skips MLPA)      *
*                       Change help screens to lower case             *
*                                                                     *
*    14Jun90  U068 LDW  Fix bug in cross memory mode                  *
*                                                                     *
*    07Jan90  U067 LDW  Fix bug in "M" for minor LPDEs                *
*                                                                     *
*    01Aug88  U066 LDW  Add IS command for short (24-bit) indirect    *
*                                                                     *
*    22May88  U065 LDW  Insert relative offset into dump line         *
*             U064 LDW  Fix bug in control block formatting           *
*                                                                     *
*    21Feb88  U063 LDW  Update for 31 bit addresses                   *
*                          (Includes running AMODE=31)                *
*                                                                     *
*    02Aug87  U062 LDW  Fix for MVS/XA                                *
*                                                                     *
*    10/05/84 U061 LDW  SUPPORT CROSS MEMORY FETCH                    *
*             U060 LDW  FIXES FOR ADDRESSABILITY                      *
*             U059 LDW  DON'T DO 'LRA' FOR PAGE 0, TO FIX FOR         *
*                          RUNNING MVS UNDER VM WITH 'STBYPASS VR'    *
*                                                                     *
*    03/12/84 U058 SDM  ADD SSCVT COMMAND                             *
*                                                                     *
*    02/17/84 U057 SDM  ADD SVC COMMAND                               *
*                                                                     *
*    06/07/83 U056 SDM  FIX KEY BUG INSERTED IN U054                  *
*                                                                     *
*    05/23/83 U055 SDM  CHANGE ALL 2K PAGE REFERENCES TO 4K           *
*             U054 SDM  FIX BIG HOLES IN FIND COMMAND                 *
*             U053 SDM  USE EP ADDR FROM MINOR CDE                    *
*             U052 SDM  IMPROVE LPA MODULE LOOKUP CODE                *
*                                                                     *
*    02/07/83 U051 SDM  EXPAND 'UNDEFINED SYMBOL' MESSAGE             *
*             U050 SDM  FIX SO INDIRECT WORKS FROM PROTECTED MEMORY   *
*             U049 SDM  WRITE EVAL ROUTINE, REPLACE NUMSCAN LOGIC     *
*                                                                     *
*    11/16/81 U048 LDW  ALLOW BLANKS BEFORE CMD AND BEFORE OPERAND    *
*             U047 LDW  REWRITE STORAGE KEY DISPLAY LOGIC             *
*             U046 LDW  MOVE CODE AROUND FOR SIMPLICITY               *
*             U045 LDW  ADD "ASID" AND "JOB" COMMANDS                 *
*             U044 LDW  INCREASE SIZE OF HISTORY AND SYMBOL TABLES    *
*             U043 LDW  REWRITE HELP ROUTINE                          *
*                                                                     *
*    11/05/81 U042 LDW  FIX BUGS INTRODUCED ON 11/02/81               *
*                                                                     *
*    11/02/81 U041 LDW  REMOVE SPIE MACRO FROM INNER "FIND" LOOP      *
*             U040 LDW  MINOR ADJUSTMENTS TO SCREEN LAYOUT            *
*             U039 LDW  FIX "FIND" NO-STOP-AT-END-OF-STORAGE BUG      *
*             U038 LDW  EXPAND ERROR MESSAGES FROM "LINK"             *
*                                                                     *
*    10/30/81 U037 LDW  RE-WRITE "M" COMMAND TO ALSO FIND FLPA, MLPA  *
*             U036 LDW  ADD "*" COMMAND TO RE-DISP PREV COMMAND       *
*             U035 LDW  MISC CLEANUP                                  *
*                                                                     *
*    10/26/81 U034 EMS  FIX GETSTOR ROUTINE CROSSING 2K BOUNDARY      *
*                                                                     *
*    06/12/81 U033 LDW  ADD "M" COMMAND TO LOCATE LPA MODULES         *
*                                                                     *
*    02/25/81 U032 LDW  ADD "FIND" COMMAND, AND RE-PROMPT FEATURE     *
*             U031 LDW  RE-ARRANGE SCREEN FORMAT                      *
*             U030 LDW  CHANGE CB LOAD MODULE NAME: CBMACS -> CORECBS *
*             U029 LDW  FIX '=' CMD - NO DUPLICATE OR INVALID SYMBOLS *
*             U028 LDW  ADD NEW COMMANDS:  "D", "U", "HISTORY",       *
*                        '=' WITH NO OPERAND DISPLAYS DEFINED SYMBOLS *
*             U027 LDW  ADD "BLIST" TO DISPLAY NAMES OF KNOWN CB MAPS *
*             U026 LDW  FINISH "GETSTOR"                              *
*             U025 LDW  SPLIT HELP SCREEN INTO 3 SCREENS              *
*             U024 LDW  MISC OTHER CLEANUP                            *
*             U023 LDW  ALLOW VARIABLE LENGTH COMMAND NAMES           *
*             U022 LDW  CHANGE SO SPIE IS ONLY ACTIVE WHEN NEEDED     *
*             U021 LDW  TOTAL RE-WRITE OF DUMP SCREEN FORMAT ROUTINES *
*             U020 LDW  ADD PAGEFIX AND PAGEFREE COMMANDS             *
*             U019 LDW  CHANGE OVERLAY SUBCMD FROM "O" TO "B", ALLOW  *
*                        "B" TO MEAN THE SAME THING AS "BNULL"        *
*                                                                     *
*    08/18/80 U018 LDW  CHANGE "ENTR" TO "OSENTER"                    *
*             U017 LDW  ADD STORAGE ALTERATION CAPABILITIES           *
*             U016 LDW  MAKE PF12 END ALSO                            *
*             U015 LDW  USE "PSCBACCT" BIT FOR "SYSTEMS PROGRAMMER"   *
*             U014 LDW  DISP KEYMODE ("USER" OR "ZERO") IF AUTH USER  *
*             U013 LDW  DISPLAY STORAGE KEY IF AUTH USER              *
*                                                                     *
*    07/29/80 U012 LDW  LOAD "TERMIO"                                 *
*                                                                     *
*    05/23/80 U011 LDW  USE "ENTR" MACRO                              *
*             U010 LDW  CHANGE PROGRAM NAME FROM "LOOK" TO "CORE"     *
*             U009 LDW  "UCB" COMMAND TO LOCATE A SPECIFIC UCB        *
*             U008 LDW  "Z" COMMAND TO SET/RESET KEY ZERO MODE        *
*             U007 LDW  IMPLEMENT AUTO UPDATE (PF2)                   *
*             U006 LDW  PROVIDE BETTER PROGRAM CHECK INFORMATION      *
*             U005 LDW  USE "STFSMODE"                                *
*                                                                     *
*    04/21/80 U004 LDW  MAKE "CBMACS"  EXTERNALLY LOADED              *
*             U003 LDW  IMPLEMENT HELP SCREEN                         *
*             U002 LDW  USE "PFKEY" MACRO                             *
*             U001 LDW  MISC CLEANUP                                  *
*                                                                     *
*                                                                     *
*  TO DO:                                                             *
*                                                                     *
*    REPLACE TRACE LOGIC                                              *
*    ADD '#' COMMAND                                                  *
*    ADD 'TSO' COMMAND                                                *
*    ADD 'ENABLESRB' COMMAND                                          *
*    FIX PAGEFIX/PAGEFREE                                             *
*    IF SCAN STRING CROSSES A FETCH BOUNDARY, IT WON'T BE FOUND!! %%  *
*                                                                     *
*---------------------------------------------------------------------*
         EJECT
         MACRO                                                     U063
&NFS     SCMDE &CMD,&ADDR                                          U063
         LCLA  &L                                                  U063
&L       SETA  K'&CMD-2                                            U063
&NFS     DC    A(&ADDR),Y(0),AL1(0,&L-1),CL8&CMD                   U070
         SPACE 1                                                   U063
         MEND                                                      U063
         EJECT
         COPY  @GLOBALS                                            U063
         EJECT
CORE     OSENTER  BASE=R10,BASE2=R11,GETMAIN=(WORKLEN,WORKAREA),   U024$
               EXIT=LEAVE,LOAD=CORECBS,AMODE=31                    U063
         SPACE 3
$        EQU   CORE                     shorthand for offsets      U063
RLINK    EQU   R9                       PRIMARY LINKAGE REGISTER   U024
NUMHIST  EQU   16*4                     SIZE OF HISTORY TABLE      U044
NUMNAME  EQU   16*4                     SIZE OF SYMBOL TABLE       U044
         EJECT
         STFSMODE  ON,INITIAL=YES       TELL VTAM WE'RE HERE       U005
         SPACE 2
         LA    R2,CLRSTART              -> START OF AREA TO CLEAR  U011
         LH    R3,=Y(CLRLEN)            LENGTH TO CLEAR            U044
         SR    R5,R5                    ZERO PAD BYTE, LENGTH 2
         MVCL  R2,R4                    CLEAR THE WORK AREA
         MVC   IOCBNAME,=CL8'CORE'      SET DCMS PROCESSOR NAME    U010
         SPACE 2
         LOAD  EPLOC==CL8'TERMIO'                                  U012
         ST    R0,IOCBATIO              SAVE ADDRESS               U012
         SPACE 2
         L     R1,X'218'                -> TCB                     U008
         L     R1,180(,R1)              -> JSCB                    U008
         L     R1,264(,R1)              -> PSCB                    U008
         TM    PSCBATR1-PSCB(R1),PSCBACCT  SYSTEMS PROGRAMMER?     U015
         BNO   TURKEY                   NO - SKIP                  U008
         OI    FLAGS,AUTHUSER           YES - REMEMBER             U008
         SPACE 2
         TESTAUTH  FCTN=1               ARE WE APF AUTHORIZED?     U008
         SPACE 1
         LTR   R15,R15                  WELL?                      U008
*U068    BNZ   *+8                      NO - SKIP                  U008
         BNZ   TURKEY                   NO - SKIP                  U068
         OI    FLAGS,AUTHAPF            YES - REMEMBER             U008
         IPK   0                        get my current key         U068
         STC   R2,MYKEY                 save for MS$KUSER          U068
         SPACE 2
TURKEY   LA    R2,IOAREA                START OF IO BUFFER
         LA    R3,IOLEN                 SIZE OF IO BUFFER
         LA    R5,C' '                  PAD BYTE
         SLL   R5,24                    MOVE TO HIGH ORDER BYTE
         MVCL  R2,R4                    CLEAR THE IO BUFFER
          AIF   (&@@SPLVL GE 200).XA01                             U063
         MVC   HEADER,=CL79'CORE'                                  U063
         SPACE 2
         SPIE  MF=(E,SPIEMFL)           SET UP PGM CHECK HANDLER   U041
          AGO   .XA02                                              U063
.XA01     ANOP                                                     U063
         MVC   HEADER,=CL79'CORE/XA'                               U063
         SPACE 2
         ESPIE SET,SPIEEXIT,(4,5)       SET UP PGM CHECK HANDLER   U063
.XA02     ANOP                                                     U063
         SPACE 1
         ST    R1,OLDPIE                SAVE ADDR OF PREV PIE      U041
         EJECT
*---------------------------------------------------------------------*
*
*   INPUT SCAN
*
*---------------------------------------------------------------------*
         SPACE 2
ISCAN    CLC   =C'* ',INPLINE           IS THIS THE SPECIAL CMD?   U036
         BNE   ISCAN010                 NO - CONTINUE              U036
         XC    INPLINE,INPSAVE          YES - SWITCH...            U036
         XC    INPSAVE,INPLINE          ... CURRENT AND ...        U036
         XC    INPLINE,INPSAVE          ... PREVIOUS COMMANDS      U036
         B     DISPLAY                  AND GO RE-DISPLAY          U036
         SPACE 2
ISCAN010 MVC   MSGLINE,BLANKS           CLEAR MESSAGE              U048
         CLC   INPLINE,BLANKS           ANYTHING HERE AT ALL?      U048
         BE    FORMAT                   NO - JUST GO FORMAT SCREEN U048
         SPACE 2
ISCAN020 CLI   INPLINE,C' '             LEADING BLANK?             U048
         BNE   ISCAN030                 NO - EXIT THIS LOOP        U048
         MVC   INPLINE(L'INPLINE-1),INPLINE+1  YES - SHIFT OVER    U048
         MVI   INPLINE+L'INPLINE-1,C' ' FIX LAST BYTE              U048
         B     ISCAN020                 AND KEEP SCANNING          U048
         SPACE 2
ISCAN030 MVC   INPSAVE,INPLINE          SAVE LAST USER INPUT
         SPACE 2
ISCAN2   LA    R3,OPERAND               POINT TO START OF INPUT    U048
         MVC   OPERAND,INPLINE          ASSUME NO COMMAND          U048
         L     R1,=A(SCANTAB)           COMMAND TABLE              U070
         NI    FLAGS,255-INDFLAG-INDFLAGS  TURN OFF INDIRECT FLAG  U066
         MVC   MSGLINE,BLANKS           CLEAR ERROR MESSAGE        U031
         MVC   PGMCKMSG,BLANKS          CLEAR PGMCHK MESSAGE       U042
         SPACE 2
SCANLOOP L     R12,0(,R1)               get address of routine     U070
         SR    R15,R15                  CLEAR FOR IC               U048
         IC    R15,7(,R1)               GET EX LENGTH OF CMD NAME  U070
         EX    R15,SCANCLC              IS THIS THE COMMAND?       U023
         BE    SCANGOT                  YES - GO FIND OPERAND      U048
         LA    R1,16(,R1)               -> NEXT ENTRY              U070
         CLI   0(R1),X'FF'              END OF TABLE?              U023
         BE    NUMSCAN                  YES - ASSUME NUMERIC INPUT
         B     SCANLOOP                 KEEP LOOKING
         SPACE 2
SCANGOT  LA    R1,INPLINE+1(R15)        -> PAST END OF COMMAND     U048
*U070    AR    R12,R10                  relocate routine address   U070
         LA    R14,INPLINE+L'INPLINE-1  -> END OF BUFFER           U048
         MVC   OPERAND,BLANKS           PRE-CLEAR FIELD            U048
         SPACE 1
FINDOPER CLI   0(R1),C' '               BLANK?                     U048
         BNE   GOTOPER                  YES - GO MOVE OPERAND      U048
         LA    R1,1(,R1)                -> NEXT POSITION           U048
         CR    R1,R14                   PAST END?                  U048
         BNH   FINDOPER                 NO - KEEP LOOKING          U048
         BR    R12                      NO OPERAND - GO TO COMMAND U070
         SPACE 1
GOTOPER  SR    R14,R1                   COMPUTE LENGTH OF OPERAND  U048
         EX    R14,SCANMVC              MOVE OPERAND               U048
         BR    R12                      GO TO COMMAND ROUTINE      U070
         SPACE 2
SCANCLC  CLC   INPLINE(*-*),8(R1)       << EXECUTED >>             U070
SCANMVC  MVC   OPERAND(*-*),0(R1)       << EXECUTED >>             U048
         EJECT
*---------------------------------------------------------------------*
*
*     FORMAT THE OUTPUT SCREEN
*
*---------------------------------------------------------------------*
         SPACE 2
*---  BLANK INPUT COMMAND
         SPACE 1
FORMAT   MVC   INPLINE,BLANKS           CLEAR INPUT LINE
FORMAT1  LA    R3,OPERAND               CURSOR ADDRESS             U048
FORMAT2  LH    R2,CURRENT               HISTORY TABLE POINTER
         L     R4,HISTORY(R2)           GET ADDRESS TO DISPLAY
         ST    R4,CURRLOC               SAVE FOR LATER             U040
         MVC   LOCLINE,BLANKS           CLEAR THE WHOLE THING      U045
         BAL   R14,ERASE                CLEAR THE SCREEN
         LA    R0,IOAREA                START OF SCREEN IMAGE
         SR    R3,R0                    CURSOR OFFSET
         SR    R5,R5                    CLEAR FOR IC
         IC    R5,HISTORY+4(R2)         GET DISPLAY FMT FROM HIST TAB
         B     *+4(R5)                  GO TO DISPLAY ROUTINE
         B     DUMP                     0: DUMP FORMAT IS STANDARD
         B     CBHIT                    4: CB MAP IN USE
         B     NOCB                     8: SUPPRESS CB USAGE
* NO CODE ABOVE 8 IN USE NOW
         SPACE 2
* TRY TO LOCATE A CONTROL BLOCK FORMAT
DUMP     L     R5,CORECBS               GET START OF INDEX         U030
DUMPCB   CLI   0(R5),X'FF'              SEE IF END OF INDEX
         BE    NOCB                     IF YES, NO CB FOUND
         CLI   4(R5),X'FF'              ENTRY ONLY FOR "B" CMDS?   U001
         BE    NEXTCB                   YES - SKIP IT              U001
         L     R2,4(,R5)                GET CB IDENTIFIER OFFSET
         AR    R2,R4                    ADD DISPLAY ADDRESS
         LA    R0,4                     LENGTH TO FETCH
         BAL   RLINK,GETSTOR            FETCH THE DATA             U024
         BNZ   NOCB                     BR IF NOT ALL DATA FOUND   U054
         CLC   0(4,R5),DUMPDATA         SEE IF IDENTIFIER MATCH
         BE    CBHIT                    BR IF YES, CB WAS FOUND
         SPACE 1
NEXTCB   LA    R5,12(,R5)               NEXT INDEX ENTRY
         B     DUMPCB                   AND CHECK FOR MORE CB'S
         SPACE 2
* IF NO CONTROL BLOCK TO BE SHOWN, JUST USE DUMP FORMAT
NOCB     LR    R2,R4                    COPY ADDR TO DISPLAY
         N     R2,=XL4'FFFFFFF0'        ROUND DOWN FOR DISPLAY     U054
         LR    R5,R2                    SAVE ADDR OF CARET LINE    U021
         TM    MODEFLAG,MIDDUMP         PUT ADDR INCENTER OF SCREEN?
         BZ    *+8                      BR IF NOT
         SH    R2,=H'64'                IF YES, BACK UP            U021
         LA    R2,0(,R2)                CLEAR HIGH BYTE            U021
* R2 NOW HAS FIRST ADDRESS TO DISPLAY
         OI    FLAGS,DISPKEY            FORMAT KEYS FOR DISPLAY    U054
         LA    R0,256                   LENGTH TO FETCH
         BAL   RLINK,GETSTOR            GO FETCH THE DATA          U024
         NI    FLAGS,255-DISPKEY        RESET DISPLAY FORMAT FLAG  U054
         SPACE 1
         LA    R6,DUMPDATA              POINT TO DATA TO DISPLAY
         LA    R4,OUT1                  FIRST OUTPUT LINE ADDR
         LA    R0,16                    NO. OF LINES TO DISPLAY    U021
         SPACE 2
DUMPLOOP ST    R2,TEMP1                 SET DOWN ADDRESS           U021
         MVC   0(79,R4),BLANKS          CLEAR THE DISPLAY LINE     U021
         HEX   (0,R4),TEMP1,LEN=4       FORMAT THE ADDRESS         U063
         LR    R15,R2                   copy current line addr     U065
         SL    R15,CURRLOC              offset of this line        U065
         MVI   10(R4),C'+'              assume positive offset     U065
         BNM   *+10                     skip if correct            U065
         MVI   10(R4),C'-'              make it negative           U065
         LCR   R15,R15                  and flip sign of offset    U065
         ST    R15,TEMP1                set down for unpk          U065
         HEX   (11,R4),TEMP1+3,LEN=1    and show the offset        U065
         LR    R15,R2                   COPY CURRENT LINE ADDR     U021
         AL    R15,=F'15'               -> LAST BYTE FOR THIS LINE U021
         CL    R2,HIGOOD                LEFT > HI ?                U054
         BH    DL$CARET                 YES - NOTHING TO DISPLAY   U021
         CL    R15,LOGOOD               RIGHT < LO ?               U054
         BL    DL$CARET                 YES - NOTHING TO DISPLAY   U021
         UNPK  HEXWORK+00(14+1),00(7+1,R6)      BYTES 01-07        U021
         UNPK  HEXWORK+14(14+1),07(7+1,R6)      BYTES 08-14        U021
         UNPK  HEXWORK+28(04+1),14(2+1,R6)      BYTES 15-16        U021
         TR    HEXWORK(32),HEXTAB       MAKE IT ALL DISPLAYABLE    U021
*U065    MVI   58(R4),C'|'              LEFT CHAR DELIM            U063
*U065    MVC   59(8,R4),0(R6)           FIRST 8 BYTES OF CHAR DATA U063
*U065    MVC   68(8,R4),8(R6)           LAST 8 BYTES OF CHAR DATA  U063
*U065    TR    59(17,R4),VALCHAR        MAKE IT ALL DISPLAYABLE    U063
*U065    MVI   76(R4),C'|'              RIGHT CHAR DELIM           U063
*U065    MVC   11(4,R4),HEXWORK+00      WORD                       U063
*U065    MVC   16(4,R4),HEXWORK+04           1                     U063
*U065    MVC   22(4,R4),HEXWORK+08      WORD                       U063
*U065    MVC   27(4,R4),HEXWORK+12           2                     U063
*U065    MVC   34(4,R4),HEXWORK+16      WORD                       U063
*U065    MVC   39(4,R4),HEXWORK+20           3                     U063
*U065    MVC   45(4,R4),HEXWORK+24      WORD                       U063
*U065    MVC   50(4,R4),HEXWORK+28           4                     U063
*?       MVI   61(R4),C'|'              LEFT CHAR DELIM            U065
         MVC   62(8,R4),0(R6)           FIRST 8 BYTES OF CHAR DATA U065
         MVC   71(8,R4),8(R6)           LAST 8 BYTES OF CHAR DATA  U065
         TR    62(17,R4),VALCHAR        MAKE IT ALL DISPLAYABLE    U065
*?       MVI   ??(R4),C'|'              RIGHT CHAR DELIM           U065
         MVC   15(4,R4),HEXWORK+00      WORD                       U065
         MVC   20(4,R4),HEXWORK+04           1                     U065
         MVC   26(4,R4),HEXWORK+08      WORD                       U065
         MVC   31(4,R4),HEXWORK+12           2                     U065
         MVC   38(4,R4),HEXWORK+16      WORD                       U065
         MVC   43(4,R4),HEXWORK+20           3                     U065
         MVC   49(4,R4),HEXWORK+24      WORD                       U065
         MVC   54(4,R4),HEXWORK+28           4                     U065
         SPACE 2
DL$CARET CR    R2,R5                    IS THIS THE CARET LINE?    U021
         BNE   DL$NEXT                  NO - SKIP                  U021
         LA    R1,15                    MASK FOR LOW 4 BITS        U021
         N     R1,CURRLOC               GET LOW 4 BITS OF ADDR     U040
         IC    R1,DUMPOFFS(R1)          GET CARET OFFSET           U021
         LA    R1,14(R1,R4)             -> CARET POS               U065
         MVI   0(R1),C'>'               ASSUME EVEN                U021
         TM    CURRLOC+3,1              EVEN OR ODD?               U042
         BNO   *+8                      EVEN - OK                  U021
         MVI   0(R1),C'<'               ODD                        U021
         SPACE 1
DL$NEXT  LA    R2,16(,R2)               -> NEXT SOURCE ADDRESS
         LA    R6,16(,R6)               -> NEXT SOURCE DATA
         LA    R4,79(,R4)               -> NEXT SCREEN LINE
         BCT   R0,DUMPLOOP              FILL THE SCREEN            U021
         EJECT
FMT$LOC  MVC   LOCLINE(4),=C'ADDR'      DISPLAY CURRENT...         U047
         HEX   LOCLINE+5,CURRLOC,LEN=4   ... LOCATION              U063
         TM    FLAGS,AUTHAPF            CAN WE DO THIS?            U047
         BNO   DISPLAY                  NO - SO SKIP ALL THE REST  U047
         SPACE 2
         MVC   LOCLINE+18(4),=C'KEY='                              U063
         LA    R15,KEYSAVE1             -> FIRST SAVED KEY         U047
         LA    R1,LOCLINE+22            -> DESTINATION             U063
         BAL   RLINK,FMT$KEY            FORMAT THIS KEY            U047
         SPACE 1
         CLC   KEYSAVE1,KEYSAVE2        BOTH KEYS IDENTICAL?       U047
         BE    SAME$KEY                 YES - DON'T FORMAT SECOND  U047
         SPACE 1
         MVC   LOCLINE+30(5),=C'KEY2='                             U063
         LA    R15,KEYSAVE2             -> SECOND SAVED KEY        U047
         LA    R1,LOCLINE+35            -> DESTINATION             U063
         BAL   RLINK,FMT$KEY            FORMAT THIS KEY            U047
         SPACE 2
SAME$KEY TM    FLAGS,AUTHUSER           AUTHORIZED USER?           U047
         BNO   DISPLAY                  NO - DON'T TELL MODE/ASID  U047
         MVC   LOCLINE+43(12),=C'MODE=USERKEY' ASSUME NOT KEY ZERO U047
         TM    MODEFLAG,KEYZERO         ARE WE RUNNING KEY ZERO?   U047
         BNO   *+10                     NO - SKIP                  U047
         MVC   LOCLINE+48(7),=C'KEYZERO' YES - CHANGE MESSAGE      U047
         SPACE 2
         OC    ASID,ASID                DIFFERENT A.S.?            U045
         BZ    DISPLAY                  NO - SKIP                  U045
         MVC   LOCLINE+59(4),=C'ASID'   YES...                     U045
         HEX   LOCLINE+64,ASID,LEN=2    ...DISPLAY ASID...         U045
         MVI   LOCLINE+69,C'('          ...AND...                  U045
         MVC   LOCLINE+70(8),JOBNAME    ...JOBNAME...              U045
         MVI   LOCLINE+78,C')'          ...                        U045
         B     DISPLAY                  SHOW THE SCREEN            U047
         SPACE 2
FMT$KEY  MVI   0(R1),C'?'               ASSUME LRA FAILED          U047
         CLI   0(R15),X'FF'             UNDEFINED KEY?             U054
         BER   RLINK                    JUST RETURN IF SO          U054
         UNPK  TEMP2(2+1),0(1+1,R15)    UNPACK THE KEY BYTE        U047
         TR    TEMP2(1),HEXTAB          MAKE IT DISPLAYABLE        U047
         MVC   0(1,R1),TEMP2            MOVE TO 'LOCLINE'          U047
         TM    0(R15),X'08'             FETCH PROTECTED?           U047
         BNO   *+8                      NO - SKIP                  U047
         MVI   2(R1),C'F'               YES - INDICATE IT          U047
         TM    0(R15),X'02'             CHANGED?                   U047
         BNO   *+8                      NO - SKIP                  U047
         MVI   4(R1),C'C'               YES - INDICATE IT          U047
         BR    RLINK                    RETURN TO MAINLINE         U047
         EJECT
* DISPLAY THE PAGE AND CHECK FOR END REQUEST
DISPLAY  TM    FLAGS,AUTOFLAG           IN AUTO UPDATE MODE?       U007
         BO    DISPAUTO                 YES - GO WRITE ONLY        U007
         SPACE 1
         NI    IOCBFLGD,255-IOCBDYON-IOCBDFRC                      U007
         SPACE 1
         L     R2,=A(SCR1)              -> SCREEN FORMAT DESC      U060
         TRMIO IOCB,FORMAT=(R2),IMAGE=IOAREA                       U060
         SPACE 1
         B     CHKPFKEY                                            U007
         SPACE 3
DISPAUTO OI    IOCBFLGD,IOCBDYON+IOCBDFRC                          U007
         SPACE 1
         L     R2,=A(SCR1)              -> SCREEN FORMAT DESC      U060
         TRMIO IOCB,WRITE,FORMAT=(R2),IMAGE=IOAREA                 U060
         SPACE 3
CHKPFKEY PFKEY K1=HELPKEY,              PF1 IS HELP                U043$
               K2=UPDATE,               PF2 IS START AUTO UPDATE   U007$
               K3=ENDIT,                PF3 IS END                 U002$
               K5=FINDKEY,              FIND PREVIOUS STRING AGAIN U032$
               K6=REPEAT,               REPEAT LAST INPUT LINE     U024$
               K7=UP,                   SCROLL UP OR PREV CB SEG   U002$
               K8=DOWN,                 SCROLL DOWN OR NEXT CB SEG U002$
               K10=BACK,                BACK THRU HISTORY          U002$
               K11=FWD,                 FWD THRU HISTORY           U002$
               K12=ENDIT,               PF12 IS END ALSO           U016$
               ATTN=ATTN,               ATTN IS STOP AUTO UPDATE   U007$
               ENTER=ISCAN,                                        U023$
               DEF=DISPLAY              ALL OTHERS IGNORED         U002
         SPACE 2
ENDIT    STFSMODE OFF                   RESET FULLSCREEN MODE      U005
         SPACE 1
          AIF   (&@@SPLVL GE 200).XA03                             U063
         L     R1,OLDPIE                -> PREVIOUS PIE            U041
         SPACE 1
         SPIE  MF=(E,(1))               RESTORE PREVIOUS SPIE      U041
          AGO   .XA04                                              U063
.XA03     ANOP                                                     U063
         ESPIE RESET,OLDPIE             restore previous ESPIE     U063
.XA04     ANOP                                                     U063
         SPACE 1
*  RESTORE AX IF NECESSARY                                         U061
         OC    ASID,ASID                FETCHING FROM ALT A.S.?    U061
         BZ    LEAVE                    NO - SKIP                  U061
         BAL   R14,MS$SUPR              GET SUPERVISOR STATE       U061
         SPACE 1
         AXSET AX=OLDAXVAL              RESTORE PREVIOUS AUTH INDX U061
         SPACE 1
         BAL   R14,MS$PROB              RESTORE PROBLEM STATE      U061
         B     LEAVE                    ALL DONE                   U006
         SPACE 2
ATTN     TM    FLAGS,AUTOFLAG           IN AUTO UPDATE MODE?       U007
         BNO   ENDIT                    NO - LEAVE                 U007
         NI    FLAGS,255-AUTOFLAG       YES - TURN IT OFF          U007
         B     FORMAT                   AND RE-DISPLAY             U007
         EJECT
*---------------------------------------------------------------------*
*
* CBHIT  -- CONTROL BLOCK FORMATTING ROUTINE
* AT ENTRY:    R3 HAS CURSOR OFFSET
*              R4 HAS ADDRESS TO DISPLAY
*              R5 HAS CBINDEX ENTRY ADDRESS
*
*---------------------------------------------------------------------*
         SPACE 2
CBHIT    LH    R2,CURRENT               GET HISTORY TABLE POINTER
         LA    R2,HISTORY+4(R2)         GET CURRENT SEG FROM HIST TABLE
         SR    R6,R6                    clear for icm              U064
         ICM   R6,7,1(R2)               LAST SEGMENT DISPLAYED
         BNZ   CBHIT2                   BR IF A CONTINUING DISPLAY
         L     R6,8(,R5)                ELSE GET FIRST SEG ADDRESS
         STCM  R6,B'0111',1(R2)         AND SAVE FOR LATER
         MVI   0(R2),4                  NOTE CB FORMAT ROUTINE IN USE
         USNGX CBS,R6
         SPACE 1
CBHIT2   LH    R0,CBSSIZE               LENGTH TO FETCH
         LH    R2,CBSFIRST              OFFSET TO FETCH
         AR    R2,R4                    ADD START OF CB ADDRESS
         BAL   RLINK,GETSTOR            GO FETCH THE DATA          U024
         BNZ   NOCB                     BR IF NOT ALL DATA FETCHED U054
         SPACE 1
         LA    R5,OUT1                  START OF SCREEN
         LA    R2,DUMPDATA              POINT TO DATA BUFFER
         SPACE 2
CBLOOP   LH    R15,CBSTYPE              GET FORMAT ITEM TYPE
         B     *+4(R15)                 GO TO FORMAT ROUTINE
         B     FMT$LOC                  TYPE 0 END OF SEGMENT      U047
         B     CBHEX                    TYPE 4 HEX
         B     CBCONST                  TYPE 8 EBCDIC CONSTANT
         B     CBEBCD                   TYPE 12 EBCDIC DATA
         SPACE 2
* EBCDIC CONSTANT ITEM
CBCONST  LA    R15,CBSDATA              ADDR OF DATA ITEM
         LH    R14,CBSLEN               LENGTH FOR MVC
         LH    R1,CBSOFF                SCREEN OFFSET
         AR    R1,R5                    ADD START OF SCREEM
         EX    R14,CBEMVC               MOVE EBCDIC DATA
         LA    R6,7(R14,R6)             POINT TO NEXT FOMAT ITEM
         B     CBLOOP
         SPACE 1
CBEMVC   MVC   0(*-*,R1),0(R15)         MOVE EBCDIC DATA
         SPACE 2
* EBCDIC DATA ITEM
CBEBCD   LH    R15,CBSDATA              OFFSET OF DATA TO SHOW
         AR    R15,R2                   ADD START OF BUFFER
         LH    R14,CBSLEN               LENGTH TO MOVE
         LH    R1,CBSOFF                SCREEN OFFSET
         AR    R1,R5                    ADD START OF SCREEN
         EX    R14,CBEMVC               MOVE THE DATA
         LA    R6,8(,R6)                POINT TO NEXT FORMAT ITEM
         B     CBLOOP
         SPACE 2
* HEX DATA ITEM
CBHEX    LH    R15,CBSDATA              DATA OFFSET
         AR    R15,R2                   ADD START OF BUFFER
         UNPK  TEMP1(9),0(5,R15)        UNPACK THE DATA
         TR    TEMP1(8),HEXTAB          AND CONVERT TO HEX
         IC    R14,CBSLEN+1             GET MVC LENGTH
         LH    R1,CBSOFF                GET SCREEN OFFSET
         AR    R1,R5                    ADD START OF SCREEN
         LA    R15,TEMP1                FIRST DIGIT UNPACKED
         TM    CBSLEN,1                 SEE IF NEXT DIGIT WANTED
         BZ    CBHEX2                   BR IF NOT
         LA    R15,TEMP1+1              ELSE POINT TO NEXT HEX DIGIT
         SPACE 1
CBHEX2   EX    R14,CBEMVC               MOVE THE DATA
         LA    R6,8(,R6)                POINT TO NEXT FORMAT ITEM
         B     CBLOOP
         SPACE 2
         DROPX R6
         SPACE 5
*---------------------------------------------------------------------*
*
* OVERLAY  --  MANIPULATE CONTROL BLOCK FORMATTING
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "B" COMMAND
         SPACE 1
OVERLAY  CSECT ,                                                   U070
         USNGX OVERLAY,R12                                         U070
         CLC   =C'NULL',OPERAND         SEE IF "NO FMTTING" WANTED U048
         BE    OVERNULL                 BR IF YES
         CLC   BLANKS(5),OPERAND        ANOTHER WAY TO SAY THAT    U048
         BE    OVERNULL                 BR IF YES                  U019
         CLC   =C'LIST',OPERAND         SEE IF LIST WANTED         U048
         BE    OVERLIST                 BR IF YES                  U027
         L     R5,CORECBS               GET CB MAP INDEX           U030
         SPACE 2
OVERLOOP CLI   0(R5),X'FF'              SEE IF END OF INDEX
         BE    FORMAT                   BR IF YES,IGNORE COMMAND
         CLC   OPERAND(4),0(R5)         SEE IF CB MATCH            U048
         BE    OVERHIT                  BR IF YES
         LA    R5,12(,R5)               ELSE POINT TO NEXT ENTRY
         B     OVERLOOP                 AND KEEP LOOKING
         SPACE 2
OVERHIT  LH    R2,CURRENT               CURRENT HIST TABLE ENTRY OFFSET
         L     R6,8(,R5)                START SEG ADDR FOR CB FOUND
         ST    R6,HISTORY+4(R2)         PUT IN HISTORY TABLE
         LA    R2,HISTORY+4(R2)         -> FLAG BYTE IN HIST TABLE
         MVI   0(R2),4                  NOTE CB FORMAT IN USE
         B     FORMAT                   GO SHOW IT
         SPACE 2
OVERNULL LH    R2,CURRENT               CURRENT HIST TABLE ENTRY OFFSET
         LA    R2,HISTORY+4(R2)         -> FLAG BYTE IN HIST TABLE
         MVI   0(R2),8                  SUPPRESS CB OVERLAY
         B     FORMAT                   GO SHOW STORAGE
         SPACE 2
OVERLIST L     R5,CORECBS               -> MAP INDEX               U030
         BAL   R14,ERASE                CLEAR THE SCREEN           U027
         MVC   INPLINE,BLANKS           CLEAR INPUT LINE           U027
       MVC MSGLINE+22(36),=C'*** AVAILABLE CONTROL BLOCK MAPS ***' U044
         SR    R15,R15                  INITIAL COLUMN OFFSET      U027
         SPACE 1
OVRLST10 LA    R1,OUT1(R15)             -> FIRST SLOT              U044
         LA    R0,16                    NUMBER OF LINES            U044
         SPACE 1
OVRLST20 CLI   0(R5),X'FF'              END OF INDEX?              U027
         BE    DISPLAY                  YES - DISPLAY THE RESULTS  U027
         MVC   0(4,R1),0(R5)            NO - MOVE CB NAME TO SCRN  U027
         MVC   TEMP1(4),0(R5)           COPY CB NAME               U027
         TR    TEMP1(4),VALCHAR         GET RID OF GARBAGE CHARS   U027
         CLC   TEMP1(4),0(R5)           DID IT CHANGE?             U027
         BE    OVRLST30                 NO - OK                    U027
         HEX   (0,R1),(0,R5),LEN=4      YES - SHOW IT IN HEX       U027
         SPACE 1
OVRLST30 LH    R14,6(,R5)               GET OFFSET OF IDENTIFIER   U027
         MVI   10(R1),C'+'              ASSUME POSITIVE            U027
         LTR   R14,R14                  IS IT?                     U027
         BNM   OVRLST40                 YES - SKIP                 U027
         LPR   R14,R14                  CONVERT OFFSET TO POSITIVE U027
         MVI   10(R1),C'-'              INDICATE NEGATIVE          U027
OVRLST40 STH   R14,TEMP1                SET DOWN OFFSET            U027
         HEX   (11,R1),TEMP1,LEN=2      PUT IT ON THE SCREEN       U027
         LA    R1,79(,R1)               -> NEXT SCREEN LINE        U027
         LA    R5,12(,R5)               -> NEXT ENTRY              U027
         BCT   R0,OVRLST20              FILL THIS COLUMN           U027
         LA    R15,20(,R15)             NEXT COLUMN OFFSET         U027
         B     OVRLST10                 GO TO TOP OF NEXT COLUMN   U027
         SPACE 1
         DROPX R12                      OVERLAY (local base)       U070
         EJECT
*---------------------------------------------------------------------*
*
* DOWN -- SCROLL DOWN 256 BYTES OR NEXT CTRL BLK SEGMENT
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "D" COMMAND
*---  PF8, PF20
         SPACE 1
CORE     CSECT ,                                                   U070
DOWN     LH    R2,CURRENT               CURRENT HIST TABLE POINTER
         LA    R2,HISTORY(R2)           POINT TO HIST TABLE ENTRY
         CLI   4(R2),4                  SEE IF THIS IS A CB DISPLAY
         BE    DOWNCB                   BR IF YES
         L     R1,0(,R2)                ELSE GET ADDR TO SHOW
         AH    R1,=H'256'               AND MOVE DOWN ONE SCREEN
         ST    R1,0(,R2)                AND REPLACE IN HIST TABLE  U063
*%%% might need to zero high byte if not XA                        U063
         B     FORMAT                   GO DISPLAY IT
         SPACE 2
DOWNCB   L     R1,4(,R2)                GET CB SEGMENT ADDR
         L     R1,4(,R1)                GET NEXT SEG ADDR
         STCM  R1,7,5(R2)               AND PUT IN HIST TABLE
         B     FORMAT                   GO SHOW IT
         SPACE 4
*---------------------------------------------------------------------*
*
* UP   -- SCROLL UP   256 BYTES OR PREV CTRL BLK SEGMENT
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "U" COMMAND
*---  PF7, PF19
         SPACE 1
UP       LH    R2,CURRENT               CURRENT HIST TABLE POINTER
         LA    R2,HISTORY(R2)           POINT TO HIST TABLE ENTRY
         CLI   4(R2),4                  SEE IF THIS IS A CB DISPLAY
         BE    UPCB                     BR IF YES
         L     R1,0(,R2)                ELSE GET ADDR TO SHOW
         SH    R1,=H'256'               AND MOVE UP ONE SCREEN
         ST    R1,0(,R2)                AND REPLACE IN HIST TABLE  U063
*%%% might need to zero high byte if not XA                        U063
         B     FORMAT                   GO DISPLAY IT
         SPACE 2
UPCB     L     R1,4(,R2)                GET CB SEGMENT ADDR
         L     R1,0(,R1)                GET PREV SEG ADDR
         STCM  R1,7,5(R2)               AND PUT IN HIST TABLE
         B     FORMAT                   GO SHOW IT
         EJECT
*---------------------------------------------------------------------*
*
* AUTO -- START AUTO UPDATING
*
*---------------------------------------------------------------------*
         SPACE 2
*---  PF2
         SPACE 1
UPDATE   XI    FLAGS,AUTOFLAG           FLIP AUTO UPDATE MODE      U007
         B     FORMAT                   AND GO FORMAT THE SCREEN   U007
         SPACE 5
*---------------------------------------------------------------------*
*
* REPEAT -- REPEAT LAST INPUT LINE
*
*---------------------------------------------------------------------*
         SPACE 2
*---  PF6
         SPACE 1
REPEAT   MVC   INPLINE,INPSAVE          COPY SAVED INPUT
         B     ISCAN2                   AND PROCESS IT
         SPACE 5
*---------------------------------------------------------------------*
*
*  ESTABLISH SCREEN MODE
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "MODE" COMMAND
         SPACE 1
MODE1    OI    MODEFLAG,MIDDUMP         MODE 1 MEANS BACK UP 4 LINES
         B     FORMAT
         SPACE 2
MODE2    NI    MODEFLAG,255-MIDDUMP     MODE 2 MEANS BACKUP 0
         B     FORMAT
         EJECT
*---------------------------------------------------------------------*
*
* LINK -- LINK TO LABELED CONTROL BLOCK FIELD
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "L" COMMAND
         SPACE 1
LINK     CSECT ,                                                   U070
         USNGX LINK,R12                                            U070
         LH    R5,CURRENT               CURRENT HIST TABLE POINTER
         LA    R5,HISTORY(R5)           POINT TO HIST TABLE ENTRY
         CLI   4(R5),4                  SEE IF CB FORMATTING IN USE
         BE    LINK2                    BR IF YES
         MVC   MSGLINE(64),=C'***** LINK NOT POSSIBLE - CONTROL BLOCK F$
               ORMATTING NOT IN EFFECT'                            U038
         B     FORMAT                   AND RESHOW SCREEN
         SPACE 1
LINK2    XR    R3,R3                    clear for ICM              U064
         ICM   R3,B'0111',5(R5)         GET CURRENT CB SEG ADDRESS U064
         LR    R4,R3                    SAVE THE START OF SEG ADDR
         USNGX CBS,R3
         SPACE 1
LINK3    LH    R15,CBSTYPE              GET ITEM TYPE
         B     *+4(R15)                 GO HANDLE ITEM TYPE
         B     LINKEND                  END OF SEGMENT
         B     LINKHEX                  HEX ITEM
         B     LINKCON                  CONSTANT ITEM
         B     LINKHEX                  EBCD ITEM
         SPACE 2
* HEX    ITEM
LINKHEX  LA    R3,8(,R3)                POINT TO NEXT ITEM
         B     LINK3                    CHECK NEXT ITEM
         SPACE 2
* CONST  ITEM  (A LABEL)
LINKCON  LH    R14,CBSLEN               GET LABEL LENGTH
         LA    R15,CBSDATA              POINT TO THE LABEL
         EX    R14,LINKEX               SEE IF ITS THE SAME
         LA    R3,7(R3,R14)             POINT TO NEXT ITEM
         BNE   LINK3                    BR IF NOT A MATCH, IGNORE IT
         LA    R1,OPERAND+1(R14)        -> BYTE AT END OF COMMAND  U048
         CLI   0(R1),C' '               SEE IF FOLLOWED BY BLANK
         BNE   LINK3                    BR IF NOT A MATCH, IGNORE IT
         CLI   CBSTYPE+1,4              SEE IF NEXT ITEM IS HEX
         BNE   LINK3                    IF NOT, ANALYZE IT FOR A LABEL
         SPACE 1
* A MATCH HAS  BEEN FOUND. FETCH THE  ADDRESS TO LINK TO
         SR    R1,R1                    CLEAR FOR IC
         IC    R1,CBSLEN+1              LENGTH OF ITEM  MINUS 1
         LA    R1,2(,R1)                TRUE LENGTH PLUS 1 FOR ROUNDING
         SRL   R1,1                     GET NUMBER OF BYTES TO FETCH
         LR    R0,R1                    COPY TO LENGTH REG
         LH    R2,CURRENT               HIST TABLE POINTER
         L     R2,HISTORY(R2)           CCURRENT DISPLAY ADDRESS
         AH    R2,CBSDATA               OFFSET OF ITEM TO FETCH
         AH    R2,CBSFIRST-CBS(R4)      AND REMEMBER THE SEGMENT OFFSET
         BAL   RLINK,GETSTOR            FETCH THE  ADDRESS         U024
         BNZ   LINK3                    BR IF DATA CANT BE FETCHED U054
         BAL   R15,FWDSUB               UPDATE HIST TABLE TO NEXT
         SR    R15,R15                  CLEAR FOR IC
         IC    R15,CBSLEN+1             GET LENGTH MINUS 1 AGAIN
         LA    R15,1(,R15)              GET TRUE LENGTH
         SLL   R15,2                    4 BITS PER NIBBLE
         SR    R0,R0                    CLEAR RESULT REG
         L     R1,DUMPDATA              LOAD ADDRESS THAT WAS FETCHED
         TM    CBSLEN,1                 SEE IF ODD DIGIT WANTED
         BZ    *+8                      BR IF NOT
         SLL   R1,4                     ELSE SHIFT OUT UNWANTED BITS
         SLDL  R0,0(R15)                ISOLATE DESIRED BITS
         LH    R2,CURRENT               NEW HIST TABLE POINTER
         ST    R0,HISTORY(R2)           SAVE IN HIST TAB
         SR    R0,R0                    CLEAR CBSEGMENT INDICATOR
         ST    R0,HISTORY+4(R2)         SAVE IN HIST TABLE
         B     FORMAT                   GO FORMAT THE NEW CB
         DROPX R3
         SPACE 2
         USNGX CBS,R4
LINKEND  L     R3,CBSNEXT               NEXT SEG ADDR
         LR    R4,R3                    SAVE IT
         CLM   R4,7,5(R5)               SEE IF SAME AS FIRST ONE
         BNE   LINK3                    NO, SEARCH NEW SEG         U038
         MVC   MSGLINE(35),=C'***** LINK FAILED - LABEL NOT FOUND' U038
         B     FORMAT                   AND RESHOW SCREEN          U038
         DROPX R4
         SPACE 1
LINKEX   CLC   OPERAND(*-*),0(R15)      CHECK FOR LABEL MATCH      U048
         SPACE 1
         DROPX R12                      LINK (local base)          U070
         SPACE 2
*---------------------------------------------------------------------*
*
*  SET INDIRECT FLAG
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "IS" COMMAND                                                 U066
         SPACE 1
CORE     CSECT ,                                                   U070
INDIR$S  OI    FLAGS,INDFLAGS           Remember "Short Indirect"  U066
         SPACE 2
*---  "I" COMMAND
         SPACE 1
INDIR    OI    FLAGS,INDFLAG            SET FLAG
         CLI   OPERAND,C' '             USE CURRENT ADDRESS        U048
         BNE   NUMSCAN                  IF NO, GO SCAN INPUT ADDRESS
         LH    R4,CURRENT               GET LAST ADDRESS DISPLAYED
         L     R4,HISTORY(R4)           AND USE AS BASE FOR INDIRECT
         B     ENDNUM
         EJECT
*---------------------------------------------------------------------*
*
*  FIND MODULE IN LPA (PLPA, MLPA, FLPA)
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "M" COMMAND
         SPACE 1
MODULE   L     R1,16           CVTPTR   -> CVT                     U033
         L     R3,X'80'(,R1)   CVTNUCB  -> TOP OF NUCLEUS          U037
         L     R4,X'168'(,R1)  CVTLPDIR -> FIRST LPDE (BEGIN PLPA?)U033
         L     R5,X'BC'(,R1)   CVTQLPAQ -> -> FIRST LPAQ           U037
         L     R5,0(,R5)                -> RECENT LPA ACTIVE CDE   U052
         USNGX CDENTRY,R5                                          U037
         SPACE 2
MODLOOP1 CLC   CDNAME,OPERAND           IS THIS THE MOD WE WANT?   U052
         BE    MODHERE1                 YES - DISPLAY THIS LOC     U037
         ICM   R5,B'1111',CDCHAIN       -> NEXT CDE                U052
         BNZ   MODLOOP1                 NO - KEEP LOOKING          U037
         SPACE 2
*---  MODULE NOT FOUND ON ACTIVE LPA QUEUE.  SCAN ALL LPDE'S NOW   U037
MOD$LPA  L     R3,16          CVTPTR    -> CVT                     U052
         L     R6,X'160'(,R3) CVTLPDSR  -> LPDE SEARCH ROUTINE     U052
         LM    R0,R1,OPERAND            MODULE NAME                U052
         BALR  R14,R6                   CALL LPDE SEARCH ROUTINE   U052
* R0, R1, R3, AND R14 MUST BE SET.
* R0 RETURNS LPDE ADDRESS IF FOUND. R6, R8 AND R9 ARE ALTERED.
* RETURN TO R14+0 IF FOUND, R14+4 IF NOT.
         B     MODHERE2                 FOUND MODULE, USE IT       U052
         SPACE 1
         MVC   MSGLINE(32),=C'MODULE XXXXXXXX NOT FOUND IN LPA'    U037
         MVC   MSGLINE+7(8),OPERAND     MODULE NAME TO MESSAGE     U048
         B     FORMAT                   GO DISPLAY ERROR MSG       U033
         SPACE 2
MODHERE1 ST    R5,TEMP1                 SAVE CDE ADDRESS           U037
         L     R1,CDENTPT               GET EPA                    U053
         MVC   MSGLINE+15(4),=C'CDE='   SET TITLE                  U063
         TM    CDATTR,CDMIN             MINOR CDE?                 U037
         BNO   *+8                      NO - SKIP                  U037
         L     R5,CDXLMJP               YES - GET PTR TO MAJOR     U037
         MVC   MSGLINE+33(8),CDNAME     TRUE MODULE NAME TO MSG    U063
         SPACE 1
         MVC   MSGLINE+43(8),=C'LOC=PLPA'   ASSUME PLPA            U063
         L     R5,CDXLMJP               -> EXTENT LIST             U037
         DROPX R5                                                  U037
         USNGX XTLST,R5                                            U037
         L     R0,XTLMSBAA              ADDR OF MODULE (NOT EPA)   U062
         SPACE 1
         CL    R3,XTLMSBAA              NUCLEUS VS. ADDR           U062
         BNH   MODNFLPA                 NOT IN FIXED LPA           U037
         MVI   MSGLINE+47,C'F'          INDICATE FLPA              U063
         B     MODHERE                  GO SET DISPLAY ADDRESS     U037
         SPACE 1
MODHERE2 LR    R4,R0                    -> RETURNED LPDE           U052
         USNGX LPDE,R4                                             U052
         ST    R4,TEMP1                 SAVE LPDE ADDRESS          U052
         L     R1,LPDENTP               EPA                        U053
         MVC   MSGLINE+14(5),=C'LPDE='  SET TITLE                  U063
         MVC   MSGLINE+33(8),LPDENAME   MOVE MODULE NAME TO MSG    U063
         TM    LPDEATTR,LPDEMIN         MINOR LPDE?                U037
*U067    BNO   *+10                     NO - OK                    U037
*U067    MVC   MSGLINE+33(8),LPDEMJNM   YES - MOVE TRUE MOD NAME   U063
         BNO   *+8                      no - ok                    U067
         L     R4,LPDEMJP               yes - get ptr to major     U067
         MVC   MSGLINE+33(8),LPDENAME   and move true mod name     U067
         L     R0,LPDEXTAD              ADDR OF MODULE             U037
         MVC   MSGLINE+43(8),=C'LOC=PLPA'   IDENTIFY PLPA          U063
         B     MODHERE                  GO SET DISPLAY ADDRESS     U037
         DROPX R4                       LPDE                       U052
         SPACE 1
MODNFLPA CL    R4,XTLMSBAA              TOP OF MLPA VS. ADDR       U062
         BNH   MODHERE                  NOT IN MLPA - GO SET ADDR  U037
         MVI   MSGLINE+47,C'M'          INDICATE MLPA              U063
         SPACE 1
MODHERE  HEX   MSGLINE+19,TEMP1,LEN=4   CDE OR LPDE ADDRESS        U063
         ST    R1,TEMP1                 GET EPA                    U037
         MVC   MSGLINE(4),=C'EPA='      MOVE TITLE                 U037
         HEX   MSGLINE+4,TEMP1,LEN=4    FILL IN ENTRY POINT ADDR   U063
         MVC   MSGLINE+29(4),=C'MOD='   ...                        U063
         LH    R2,CURRENT               GET HISTORY TABLE PTR      U033
         ST    R0,HISTORY(R2)           SET ADDR TO DISPLAY        U033
         B     FORMAT                   GO DISPLAY IT              U033
         SPACE 1
         DROPX R5                                                  U037
         EJECT
*---------------------------------------------------------------------*
*
*  FIGURE OUT WHERE WE ARE                                         U070
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "WHERE" COMMAND
         SPACE 1
WHERE    CSECT ,                                                   U070
         USNGX WHERE,R12                                           U070
         L     R8,CURRLOC               address to process         U070
         N     R8,=X'7FFFFFFF'          sure high bit zero         U070
         SPACE 1                                                   U070
         L     R4,PSATOLD-PSA           @ current TCB              U070
         USNGX TCB,R4                                              U070
WH$JPQ1  SR    R7,R7                                               U070
         ICM   R7,B'0111',TCBJPQB       top CDE on JOB PACK queue  U070
         BNZ   WH$JPQ2                                             U070
         ICM   R4,15,TCBOTC                                        U070
         BNZ   WH$JPQ1                                             U070
         USNGX CDENTRY,R7                                          U070
WH$JPQ2  BAL   R14,CK$CDE                                          U070
         B     WH$FNDJ                                             U070
         ICM   R7,15,CDCHAIN                                       U070
         BNZ   WH$JPQ2                                             U070
         DROPX R4                       TCB                        U070
         SPACE 1                                                   U070
* search active LPA queue                                          U070
         LA    R6,=CL16'active LPA'                                U070
         L     R3,CVTPTR                                           U070
         USNGX CVTMAP,R3                                           U070
         L     R7,CVTQLPAQ                                         U070
* need FRR incase queue changes?                                   U070
WH$LPA1  ICM   R7,15,CDCHAIN                                       U070
         BZ    WH$LPA2                                             U070
         BAL   R14,CK$CDE                                          U070
         B     WH$FND                                              U070
         B     WH$LPA1                                             U070
         DROPX R7                       CDENTRY                    U070
         SPACE 1                                                   U070
* search lpa directory                                             U070
WH$LPA2  LA    R6,=CL16'LPA directory'                             U070
         SR    R7,R7                                               U070
         ICM   R7,B'0111',CVTLPDIR                                 U070
         USNGX LPDE,R7                                             U070
WH$LPA3  BAL   R14,CK$CDE                                          U070
         B     WH$FND                                              U070
         LA    R7,LPDEMJNM+L'LPDEMJNM   @ NEXT LPDE ENTRY          U070
         CLI   LPDENAME,X'FF'                                      U070
         BNE   WH$LPA3                                             U070
         DROPX R7                       LPDE                       U070
         SPACE 1                                                   U070
* try nuclkup; note RC 4 if not found (rc 8 if loc zero)           U070
* returns csect addr in r0 too!                                    U070
         LR    R0,R8                    don't have nuclkup kill r8 U070
         NUCLKUP  BYADDR,NAME=TEMP2,ADDR=(0)                       U070
         LTR   R15,R15                                             U070
         BNZ   WH$NFND                                             U070
         N     R0,=X'7FFFFFFF'          clear amode bit            U070
         LR    R1,R0                                               U070
         LA    R7,TEMP2-(CDNAME-CDENTRY)  fake a cde               U070
         LA    R6,=CL16'in nucleus'                                U070
         B     WH$FND                                              U070
         SPACE 1                                                   U070
* not found, try VSM list?                                         U070
WH$NFND  MVC   MSGLINE(30),=CL30'not found'                        U070
         B     FORMAT                                              U070
         SPACE 1                                                   U070
WH$FNDJ  MVC   MSGLINE+60(16),=CL16'tcb '                          U070
         ST    R4,TEMP1                                            U070
         HEX   MSGLINE+60+4,TEMP1,LEN=4                            U070
         LA    R6,MSGLINE+60                                       U070
         SPACE 1                                                   U070
WH$FND   SR    R8,R1                    get offset into module     U070
         MVI   MSGLINE,C'+'                                        U070
         ST    R8,TEMP1                                            U070
         HEX   MSGLINE+1,TEMP1,LEN=4                               U070
         MVC   MSGLINE+9(4),=C' in '                               U070
         MVC   MSGLINE+13(8),CDNAME-CDENTRY(R7)                    U070
         MVC   MSGLINE+22(16),0(R6)                                U070
         B     FORMAT                                              U070
         SPACE 1                                                   U070
* Entry: R7  = @ CDE (or LPDE) to check                            U070
*        R8  = address to "where"                                  U070
*        R14 = return addr  (+ 4 if not found, +0 if found)        U070
*                                                                  U070
* Exit:  R0  = @ past end of module                                U070
*        R1  = @ begining of module                                U070
*                                                                  U070
* (only supports single extent XL's).                              U070
         SPACE 1                                                   U070
         USNGX CDENTRY,R7                                          U070
CK$CDE   TM    CDATTR,CDMIN             minor?                     U070
         BO    4(,R14)                  rif minor, not a match     U070
         LA    R1,LPDEXTLN-LPDE+CDENTRY-8  assume LPDE             U070
         TM    CDATTRB,CDELPDE          this LPDE?                 U070
         BO    CK$CDE1                  bif LPDE                   U070
         ICM   R1,15,CDXLMJP            @ extent list              U070
         BZ    4(,R14)                  skip if no XL              U070
CK$CDE1  LM    R0,R1,8(R1)              len/adr of module          U070
         N     R0,=X'7FFFFFFF'          clear high bit in len      U070
         N     R1,=X'7FFFFFFF'           and addr                  U070
         AR    R0,R1                    @ past end of module       U070
         CLR   R8,R0                    where :: end addr          U070
*U072    BH    4(,R14)                  rif adr past end           U070
         BNL   4(,R14)                  rif adr past end           U072
         CLR   R8,R1                    where :: begin addr        U070
         BL    4(,R14)                  rif adr before begining    U070
         BR    R14                                                 U070
         DROPX R7                       CDENTRY                    U070
         DROPX R12                      WHERE (local base)         U070
         EJECT
*---------------------------------------------------------------------*
*
*  MANIPULATE HISTORY TABLE
*
*---------------------------------------------------------------------*
         SPACE 2
*---  ">" COMMAND
         SPACE 1
CORE     CSECT ,                                                   U070
FWD      BAL   R15,FWDSUB               FORWARD 1 IN HIST TABLE
         B     FORMAT
         SPACE
FWDSUB   LH    R1,CURRENT               CURRENT ENTRY INDEX
         LA    R1,8(,R1)                ADVANCE 1
         CH    R1,=Y(NUMHIST*8)         SEE IF WRAP NEEDED         U044
         BL    *+6                      BR IF NOT
         SR    R1,R1                    WRAP TO BEGINNING
         STH   R1,CURRENT               STORE NEW OFFSET
         BR    R15                      RETURN TO CALLER
         SPACE 2
*---  "<" COMMAND
         SPACE 1
BACK     LH    R1,CURRENT
         SH    R1,=H'8'                 BACK UP ONE ENTRU
         BNM   *+8                      BR IF NO BACKWARD WRAP
         LA    R1,(NUMHIST-1)*8         ELSE GO TO END OF TABLE    U044
         STH   R1,CURRENT               SAVE RESULT
         B     FORMAT
         EJECT
*---------------------------------------------------------------------*
*
*  DISPLAY HISTORY TABLE                                           U028
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "HISTORY" COMMAND
         SPACE 1
SHOWHIST CSECT ,                                                   U070
         USNGX SHOWHIST,R12                                        U070
         BAL   R14,ERASE                CLEAR THE SCREEN BUFFER    U028
         MVC   INPLINE,BLANKS           CLEAR INPUT LINE           U028
         MVC   MSGLINE+29(21),=C'*** HISTORY TABLE ***'            U044
         LA    R4,HISTORY               -> FIRST ENTRY             U028
         LA    R14,NUMHIST*8(,R4)       -> PAST LAST ENTRY         U044
         SR    R15,R15                  INITIAL LINE OFFSET        U028
         LH    R2,CURRENT               OFFSET OF CURRENT ENTRY    U028
         AR    R2,R4                    -> CURRENT ENTRY           U028
         SPACE 1
SHSTLP1  LA    R1,OUT1(R15)             -> FIRST OUTPUT LINE       U044
         LA    R0,16                    NUMBER OF OUTPUT LINES     U044
         SPACE 1
SHSTLP2  HEX   (1,R1),(0,R4),LEN=4      LOCATION                   U063
         CR    R4,R2                    IS THIS THE CURRENT ENTRY? U028
         BNE   *+8                      NO - SKIP                  U028
         MVI   0(R1),C'>'               YES - FLAG IT              U028
         SPACE 2
*---  SEE IF THIS LOCATION HAS HAD A SYMBOL DEFINED HERE.          U028
         LA    R3,NAMES                 -> FIRST                   U028
         LA    R5,NUMNAME               MAX NUMBER                 U044
         SPACE 1
SHSTLP3  CLI   0(R3),0                  UNUSED ENTRY?              U028
         BE    SHWHST20                 YES - END THIS LOOP        U028
         CLC   0(4,R4),8(R3)            THIS SYMBOL AT THIS LOC?   U063
         BE    SHWHST79                 YES - SHOW ITS NAME        U028
         LA    R3,12(,R3)               -> NEXT SYMBOL ENTRY       U028
         BCT   R5,SHSTLP3               KEEP SEARCHING             U028
         SPACE 2
*---  SEE IF THIS IS A CONTROL BLOCK THAT WE KNOW OF               U028
SHWHST20 CLI   4(R4),4                  CB DISPLAY HERE?           U028
         BNE   SHWHST90                 NO - SKIP THIS SECTION     U028
         SR    R3,R3                    clear for ICM              U063
         ICM   R3,B'0111',5(R4)         -> CB SEGMENT              U028
         SPACE 1
SHWHST30 L     R5,CORECBS               -> CB INDEX                U028
         MVC   TEMP1(4),0(R5)           COPY CB NAME               U028
         TR    TEMP1(4),VALCHAR         SEE IF IT'S ANY GOOD       U028
         CLC   TEMP1(4),0(R5)           DID IT CHANGE?             U028
         BNE   SHWHST50                 YES - SKIP FUNNY ENTRIES   U028
         SPACE 2
SHWHST40 CLM   R3,B'0111',9(R5)         IS THIS IT?                U028
         BE    SHWHST80                 YES - SHOW NAME            U028
         ICM   R3,B'0111',1(R3)         -> previous segment        U063
         CLM   R3,B'0111',5(R4)         HAVE WE WRAPPED?           U063
         BNE   SHWHST40                 NO - KEEP TESTING THIS CB  U028
         SPACE 1
SHWHST50 LA    R5,12(,R5)               -> NEXT INDEX ENTRY        U028
         CLI   0(R5),X'FF'              END OF INDEX?              U028
         BNE   SHWHST40                 NO - KEEP LOOKING          U028
         B     SHWHST90                 YES - DON'T DISPLAY IT     U028
         SPACE 1
SHWHST79 MVC   11(8,R1),0(R3)           MOVE IN SYMBOL NAME        U063
         B     SHWHST90                 NEXT HISTORY TABLE ENTRY   U028
         SPACE 1
SHWHST80 MVC   11(3,R1),=C'CB='                                    U063
         MVC   14(4,R1),0(R5)           MOVE CB NAME               U063
         SPACE 1
SHWHST90 LA    R4,8(,R4)                -> NEXT TABLE ENTRY        U028
         LA    R1,79(,R1)               -> NEXT SCREEN LINE        U028
         CR    R4,R14                   END OF HISTORY TABLE?      U028
         BNL   DISPLAY                  YES - GO DISPLAY IT        U028
         BCT   R0,SHSTLP2               FILL THIS COLUMN           U028
         LA    R15,20(,R15)             -> NEXT COLUMN             U028
         B     SHSTLP1                  GO TO TOP OF NEXT COLUMN   U028
         SPACE 1
         DROPX R12                      SHOWHIST (local base)      U070
         EJECT
*---------------------------------------------------------------------*
*
*   ADD ENTRY TO SYMBOL TABLE
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "=" COMMAND
         SPACE 1
*  MUCH OF THIS SECTION OF CODE WAS RE-ARRANGED, SO THAT           U029
*  DUPLICATE AND INVALID SYMBOLS COULD BE DETECTED.                U029
*  CODE WHICH WAS ONLY MOVED WAS NOT FLAGGED.                      U029
DEFINE   CSECT ,                                                   U070
         USNGX DEFINE,R12                                          U070
         LA    R3,NAMES                 START OF TABLE
         CLI   OPERAND,C' '             ANY OPERAND?               U048
         BE    DEFLIST                  NO - SHOW HIM KNOWN NAMES  U028
         MVC   TEMP1(8),BLANKS          CLEAR AN AREA              U029
         LA    R15,9                    MAX LENGTH + 1
         LA    R0,OPERAND               -> START OF SYMBOL         U048
         LR    R1,R0                    COPY SYMBOL ADDR
         SPACE 1
DEFLOOP1 CLI   0(R1),C' '               SEE IF END OF SYM
         BE    DEFEND                   BR IF YES
         LA    R1,1(,R1)                -> NEXT BYTE OF SYMBOL
         BCT   R15,DEFLOOP1             CONTINUE FOR 9 BYTES
         MVC   MSGLINE(21),=C'***** SYMBOL TOO LONG'               U031
         B     FORMAT2                  DISPLAY WITH CURSOR AT ERROR
         SPACE 1
DEFBAD   MVC   MSGLINE(50),=C'***** INVALID SYMBOL (SYMBOL ENTERED IS V$
               ALID HEX)'                                          U046
         B     FORMAT2                  DISPLAY WITH CURSOR AT ERROR
         SPACE 1
DEFEND   SR    R1,R0                    SYMBOL LENGTH
         BCTR  R1,0                     FOR EXECUTE
         EX    R1,DEFMOVE               MOVE SYMBOL
         EX    R1,HEXTRT                DOES IT LOOK LIKE HEX?     U029
         BZ    DEFBAD                   YES - DON'T ALLOW IT       U029
         SPACE 1
         LA    R15,NUMNAME              NUMBER OF TABLE ENTRIES    U044
         SPACE 1
DEFLOOP2 CLC   TEMP1(8),0(R3)           SYMBOL HERE?               U029
         BE    DEFREPL                  YES - REDEFINE IT          U029
         CLI   0(R3),X'0'               SEE IF UNUSED ENTRY
         BE    DEFNEW                   BR IF SPACE FOUND          U029
         LA    R3,12(,R3)               NEXT ENTRY ADDR
         BCT   R15,DEFLOOP2             TRY ALL ENTRIES
         MVC   MSGLINE(26),=C'***** SYMBOL TABLE IS FULL'          U031
         B     FORMAT
         SPACE 1
DEFREPL  MVC   MSGLINE(16),=C'SYMBOL REDEFINED'                    U031
DEFNEW   MVC   0(8,R3),TEMP1            PUT SYMBOL NAME IN TABLE   U029
         LH    R1,CURRENT               GET CURRENT ADDRESS
         L     R1,HISTORY(R1)
         ST    R1,8(,R3)                STORE WITH SYMBOL
         B     FORMAT
         SPACE 1
DEFMOVE  MVC   TEMP1(*-*),OPERAND       EXECUTED TO MOVE SYMBOL    U048
         SPACE 2
DEFLIST  BAL   R14,ERASE                CLEAR THE SCREEN BUFFER    U028
         MVC   INPLINE,BLANKS           CLEAR INPUT LINE           U028
         MVC   MSGLINE+28(23),=C'*** DEFINED SYMBOLS ***'          U044
         SR    R15,R15                  INITIAL DISP LINE OFFSET   U028
         SPACE 1
DEF710   LA    R1,OUT1(R15)             -> FIRST OUTPUT LINE       U044
         LA    R0,16                    # OF LINES TO USE          U044
         SPACE 1
DEF720   CLI   0(R3),0                  UNUSED ENTRY?              U028
         BE    DISPLAY                  YES - DISPLAY THE RESULTS  U028
         MVC   0(8,R1),0(R3)            MOVE SYMBOL NAME TO SCREEN U028
         HEX   (10,R1),(8,R3),LEN=4     AND ITS VALUE              U063
         LA    R1,79(,R1)               -> NEXT LINE               U028
         LA    R3,12(,R3)               -> NEXT SYMBOL ENTRY       U028
         BCT   R0,DEF720                FILL THIS COLUMN           U028
         LA    R15,20(,R15)             GET NEXT COLUMN OFFSET     U028
         B     DEF710                   START AT TOP OF NEXT COL   U028
         SPACE 1
         DROPX R12                      DEFINE (local base)        U070
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  SCAN THE ADDRESS EXPRESSION                                        *
*                                                                     *
* REGISTER USAGE:                                                     *
*   R0  - WORK                                                        *
*   R1  - WORK                                                        *
*   R2  - WORK                                                        *
*   R3  - SCAN POINTER                                                *
*   R4  - VALUE                                                       *
*   R14 - WORK                                                        *
*   R15 - WORK                                                        *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
CORE     CSECT ,                                                   U070
         SPACE 1
NUMSCANX MVC   OPERAND,INPLINE          RESTORE COMMAND LINE       U049
         SPACE 1
*---  "," COMMAND
NUMSCAN  L     R2,CURRLOC               INPUT VALUE                U057
         BAL   R14,CALLEVAL             CALL EVAULATE ROUTINE      U057
         CH    R15,=H'4'                SUCCESSFUL?                U049
         BH    FORMAT2                  NO, GO DISPLAY MESSAGE     U057
         LR    R4,R2                    SET NEW VALUE              U049
         SPACE 1
ENDNUM   TM    FLAGS,INDFLAG            IS THIS AN INDIRECT REQ
         BZ    EN01                     BR IF NOT                  U050
*%%%% THE FOLLOWING CODE IS INVALID ON THE SRB!!!!!
         BAL   R14,MS$KZERO             ENTER KEY ZERO             U050
         L     R4,0(,R4)                DO THE INDIRECT
         BAL   R14,MS$KUSER             ENTER USER KEY             U050
EN01     LA    R4,0(,R4)                CLEAR HIGH BYTE            U021
         TM    FLAGS,INDFLAGS           Short Indirect?            U066
         BZ    *+8                      no - continue              U066
         N     R4,=X'00FFFFFF'          yes - strip off garbage    U066
         BAL   R15,FWDSUB               UPDATE HISTORY POINTR
         LH    R15,CURRENT              NEW HIST TABLE OFFSET
         ST    R4,HISTORY(R15)          SAVE VALUE IN HISTORY TABLE
         SR    R0,R0                    INDICATE DISPLAY FORMAT IS DUMP
         ST    R0,HISTORY+4(R15)        AND SAVE WITH HISTORY
         B     FORMAT
         EJECT
*---------------------------------------------------------------------*
*
*  FIND A UCB                                                      U009
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "UCB" COMMAND                                                U024
         SPACE 1
UCB      TRT   OPERAND(3),NUMTAB        IS IT HEX?                 U048
         BNZ   NUMSCANX                 NO                         U049
          AIF   (&@@SPLVL GE 200).XA15                             U071
         L     R14,16                   -> CVT                     U009
         L     R14,40(,R14)             -> ILK2                    U009
         SPACE 1
UCBLOOP  CLC   =X'FFFF',0(R14)          END OF TABLE?              U009
         BE    NUMSCANX                 YES - IT'S NOT HERE        U049
         SR    R4,R4                    PREPARE FOR ICM            U009
         ICM   R4,B'0011',0(R14)        LOAD UCB ADDRESS           U009
         BZ    UCBNEXT                  NO - SKIP                  U009
         CLC   OPERAND(3),13(R4)        IS THIS THE RIGHT ONE?     U048
         BNE   UCBNEXT                  NO - TRY NEXT              U009
         LH    R2,CURRENT               GET HISTORY TABLE PTR      U009
         ST    R4,HISTORY(R2)           SET ADDR TO DISPLAY        U009
         B     FORMAT                   GO DISPLAY IT              U009
         SPACE 1
UCBNEXT  LA    R14,2(,R14)              -> NEXT UCB ADDRESS        U009
         B     UCBLOOP                  GO ON SCANNING             U009
          AGO   .XA16                                              U071
.XA15     ANOP                                                     U071
         XC    IOSWORK,IOSWORK          init IOSVSUCB work area    U071
         LA    R14,IOSWORK              -> 100 byte work area      U071
         LA    R15,=X'00'               -> class byte              U071
         LA    R0,IOSANSW               -> answer area             U071
         STM   R14,R0,IOSPARM           set up parms for IOSVSUCB  U071
         L     R15,16                   -> CVT                     U071
         L     R3,CVTUCBSC-CVT(,R15)    -> LOOKUP ROUTINE          U071
         SPACE 1
UCBLOOP  LA    R1,IOSPARM               -> PARMLIST FOR IOSVSUCB   U071
         LR    R15,R3                   -> IOSVSUCB                U071
         BALR  R14,R15                  invoke IOSVSUCB            U071
         CH    R15,=H'4'                did it work?               U071
         BE    NUMSCANX                 yes - no more UCBs         U071
         BL    *+8                      yes - we got a UCB         U071
         EX    0,*                      no - invalid rc            U071
         L     R4,IOSANSW               get returned UCB addr      U071
         CLC   OPERAND(3),13(R4)        is this the right one?     U071
         BNE   UCBLOOP                  no - try next              U071
         LH    R2,CURRENT               get history table ptr      U071
         ST    R4,HISTORY(R2)           set addr to display        U071
         B     FORMAT                   go display it              U071
.XA16     ANOP                                                     U071
         SPACE 3
*---------------------------------------------------------------------*
*
*  PAGEFIX AND PAGEFREE COMMANDS                                   U020
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "PAGEFREE" COMMAND
         SPACE 1
PAGEFREE TM    FLAGS,AUTHUSER           CAN HE DO THIS?            U020
         BNO   NUMSCANX                 NO - IGNORE COMMAND        U049
         TM    FLAGS,AUTHAPF            CAN I DO THIS?             U020
         BNO   ZOTNOT                   NO - TELL HIM SORRY        U020
         OC    ASID,ASID                IN ANOTHER A.S.?           U047
         BNZ   PAGENOPE                 YES - COMMAND INVALID      U047
         LH    R1,CURRENT               HISTORY TABLE POINTER      U020
         L     R1,HISTORY(R1)           GET CURRENT ADDRESS        U020
         N     R1,=X'FFFFF000'          MAKE IT A 4K BOUNDARY      U055
         SPACE 1
         PGFREE  R,A=(1)                                           U020
         SPACE 1
         LTR   R15,R15                  OK?                        U020
         BZ    *+8                      YES - CONTINUE             U020
         EX    0,*                      NO - DIE                   U020
         MVC   MSGLINE(10),=C'PAGE FREED'                          U020
         B     FORMAT                   RE-DISPLAY SCREEN          U020
         SPACE 2
*---  "PAGEFIX" COMMAND
         SPACE 1
PAGEFIX  TM    FLAGS,AUTHUSER           CAN HE DO THIS?            U020
         BNO   NUMSCANX                 NO - IGNORE COMMAND        U049
         TM    FLAGS,AUTHAPF            CAN I DO THIS?             U020
         BNO   ZOTNOT                   NO - TELL HIM SORRY        U020
         OC    ASID,ASID                IN ANOTHER A.S.?           U047
         BNZ   PAGENOPE                 YES - COMMAND INVALID      U047
         LH    R1,CURRENT               HISTORY TABLE POINTER      U020
         L     R1,HISTORY(R1)           GET CURRENT ADDRESS        U020
         N     R1,=X'FFFFF000'          MAKE IT A 4K BOUNDARY      U055
         LA    R0,FIXECB                -> ECB                     U020
         SPACE 1
         PGFIX R,A=(1),LONG=Y,ECB=(0)   FIX IT                     U020
         SPACE 1
         CH    R15,=H'8'                WAIT FOR IT?               U020
         BE    FIXWAIT                  YES                        U020
         LTR   R15,R15                  OK?                        U020
         BZ    FIXDONE                  YES - CONTINUE             U020
         EX    0,*                      NO - DIE                   U020
         SPACE 2
FIXWAIT  WAIT  ECB=FIXECB               WAIT FOR THE PAGE          U020
         SPACE 1
         CLI   FIXECB+3,0               OK?                        U020
         BE    FIXDONE                  YES                        U020
         MVC   MSGLINE(13),=C'BAD POST CODE'                       U020
         B     FORMAT                   RE-DISPLAY SCREEN          U020
         SPACE 2
FIXDONE  MVC   MSGLINE(37),=C'PAGE FIXED (WHILE YOU ARE SWAPPED IN)'
         B     FORMAT                   RE-DISPLAY SCREEN          U020
         SPACE 3
PAGENOPE MVC   MSGLINE(30),=C'***** COMMAND INVALID WITH SRB'      U047
         B     FORMAT                   RE-DISPLAY SCREEN          U047
         EJECT
*---------------------------------------------------------------------*
*
*  STORAGE ALTERATION ROUTINES                                     U017
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "S", "X", "O", "N" COMMANDS
         SPACE 1
STORES   BAL   R1,STORE                 GO TO ROUTINE              U017
         MVC   0(*-*,R4),ALTRDATA       << EXECUTED >>             U017
         SPACE 1
STOREX   BAL   R1,STORE                 GO TO ROUTINE              U017
         XC    0(*-*,R4),ALTRDATA       << EXECUTED >>             U017
         SPACE 1
STOREO   BAL   R1,STORE                 GO TO ROUTINE              U017
         OC    0(*-*,R4),ALTRDATA       << EXECUTED >>             U017
         SPACE 1
STOREN   BAL   R1,STORE                 GO TO ROUTINE              U017
         NC    0(*-*,R4),ALTRDATA       << EXECUTED >>             U017
         SPACE 3
STORE    OC    ASID,ASID                IN ANOTHER A.S.?           U047
         BNZ   INVSTORE                 YES - COMMAND INVALID      U047
         ST    R1,ALTERPTR              SAVE PTR TO ALTER INSTR    U017
         LA    R6,ALTERLEN              -> MY AREA                 U048
         BAL   RLINK,GETSTRNG           CONVERT THE OPERAND        U032
         LH    R2,CURRENT               HISTORY TABLE POINTER      U017
         L     R4,HISTORY(R2)           GET ADDRESS TO ALTER       U017
         L     R3,ALTERPTR              GET ADDRESS OF INSTRUCTION U017
         LH    R5,ALTERLEN              GET LENGTH TO ALTER        U048
         BCTR  R5,0                     -1 FOR EX                  U017
         OI    FLAGS,PGMCHKOK           ALLOW PGM CHECKS           U041
         BAL   R14,MS$KZERO             YES - SO DO IT             U024
         LA    R14,BADSTORE             SET SPIE RETURN ADDR       U022
         EX    R5,0(,R3)                DO THE ALTER               U017
         SPACE 1
BADSTORE BAL   R14,MS$KUSER             YES - SO UN-DO IT          U024
         NI    FLAGS,255-PGMCHKOK       DISALLOW PGM CHECKS        U041
         B     FORMAT                   DISPLAY MY HANDYWORK       U017
         SPACE 2
INVSTORE MVC   MSGLINE(30),=C'***** ALTER INVALID IN XM MODE'      U061
         B     FORMAT                                              U047
         EJECT
*---------------------------------------------------------------------*
*
*  SEARCH FOR SOME DATA                                            U032
*
*---------------------------------------------------------------------*
         SPACE 2
*---  PF5
         SPACE 1
FINDKEY  MVC   INPLINE,BLANKS           CLEAR THE INPUT            U032
         MVI   INPLINE,C'F'             SIMULATE COMMAND           U032
         MVC   OPERAND,BLANKS           PF5 HAS NO OPERAND         U048
         L     R12,=A(FIND)             set local base             U070
         BR    R12                      go to it                   U070
         SPACE 2
*---  "F" COMMAND
         SPACE 1
FIND     CSECT ,                                                   U070
         USNGX FIND,R12                                            U070
         MVC   MSGLINE,BLANKS           CLEAR MESSAGE AREA         U048
         LA    R6,FINDLEN               -> MY AREA                 U048
         BAL   RLINK,GETSTRNG           CONVERT THE OPERAND        U032
         SPACE 1
         STIMER  TASK,BINTVL==A(10000*100)  SET FOR 10,000 SECS    U032
         SPACE 1
         XC    FINDCNT1(4*2),FINDCNT1   CLEAR COUNTERS             U032
         LH    R6,FINDLEN               GET LENGTH TO FIND         U048
         SH    R6,=H'2'                 GET EX LENGTH OF REMAINDER U032
         SR    R1,R1                    CLEAR FOR IC               U032
         IC    R1,FINDDATA              GET FIRST CHARACTER        U032
         LA    R1,TRTAB(R1)             -> LOC IN TRTAB            U032
         XC    TRTAB,TRTAB              SET TO STOP ON ...         U032
         MVI   0(R1),X'FF'              ... FIRST CHAR TO FIND     U032
         LH    R1,CURRENT               GET OFFSET INTO HIST TBL   U032
         L     R3,HISTORY(R1)           -> LOCATION TO FETCH       U032
         CLI   OPERAND,C' '             WAS THERE AN OPERAND?      U048
         BNE   *+8                      YES - START AT CURRENT LOC U032
         LA    R3,1(,R3)                NO - START AT NEXT BYTE    U032
         SPACE 2
FIND$LP1 LR    R2,R3                    GET ADDRESS TO FETCH       U032
         LA    R1,256(,R2)              END OF DATA + 1            U054
         CLR   R1,R2                    DID IT WRAP?               U054
         BH    *+6                      NO, USE AS IS              U054
         XR    R1,R1                    YES, TRUNCATE TO END       U054
         SR    R1,R2                    ACTUAL LENGTH TO FETCH     U054
         LA    R0,0(,R1)                CLEAR NOISE BITS           U054
         BAL   RLINK,GETSTOR            RETRIEVE DATA              U054
         C     R2,LOGOOD                IS FIRST PART AVAILABLE?   U054
         BNE   FINDFAIL                 NO, SKIP IMMEDIATE         U054
         L     R4,HIGOOD                -> HIGH VALID BYTE         U054
         SR    R4,R2                    VALID LENGTH - 1           U054
         SR    R4,R6                    REAL SCAN LENGTH (TRICKY)  U054
         BNP   FINDFAIL                 COULDN'T WORK ANYWAY       U054
         SPACE 1
FINDGET  L     R1,FINDCNT1              GET BYTES SCANNED          U032
         AR    R1,R4                    INCREMENT                  U032
         ST    R1,FINDCNT1              SAVE NEW COUNT             U032
         LA    R1,DUMPDATA              -> SCAN DATA               U054
         LR    R0,R1                    -> SCAN DATA               U054
         AR    R0,R4                    -> END OF SCAN DATA + 1    U054
         BCTR  R0,0                     -> END OF SCAN DATA        U054
         SPACE 1
FIND$LP2 LR    R2,R0                    -> END OF SCAN DATA + 1    U054
         SR    R2,R1                    LENGTH TO SCAN             U054
         BM    FINDNEXT                 RAN OUT OF ROOM            U054
         EX    R2,FIND$TRT              FIND THE FIRST CHAR        U054
         BZ    FINDNEXT                 NOT FOUND, GET NEXT DATA   U032
         LTR   R6,R6                    MORE THAN 1 BYTE?          U032
         BM    FINDDISP                 NO - FOUND IT - GO DISPLAY U032
         EX    R6,FIND$CLC              DOES THE REST MATCH?       U032
         BE    FINDDISP                 YES - DISPLAY IT           U032
         LA    R1,1(,R1)                NO - SKIP A BYTE           U054
         B     FIND$LP2                 AND TRY AGAIN              U054
         SPACE 1
FINDNEXT LA    R3,0(R3,R4)              -> NEXT CHUNK              U054
         LTR   R3,R3                    DID IT WRAP?               U054
         BNZ   FIND$LP1                 NO, PROCESS NEW DATA       U054
         BCTR  R3,0                     ALL ONES                   U054
         LA    R2,0(,R3)                ARCHITECTURE END OF MEMORY U054
         B     FINDNOT                  EXIT                       U032
         SPACE 2
FINDFAIL XR    R4,R4                    CLEAR SCAN LENGTH          U054
         AL    R3,=F'4096'              -> NEXT 4K                 U055
         N     R3,=X'FFFFF000'          -> START OF PAGE           U055
         LR    R1,R3                    -> NEXT PAGE               U054
         SLR   R1,R2                    LENGTH SKIPPED             U054
         A     R1,FINDCNT2              TOTAL LENGTH SKIPPED       U054
         ST    R1,FINDCNT2              STORE BACK                 U032
         B     FINDNEXT                 TRY NEXT PAGE              U054
         SPACE 1
FINDDISP LR    R2,R1                    SAVE DISPLAY ADDRESS       U032
         LA    R0,DUMPDATA              -> START OF FETCH AREA     U032
         SR    R2,R0                    COMPUTE OFFSET             U032
         AR    R2,R3                    GET TRUE ADDRESS           U032
         SPACE 1
FINDNOT  BAL   R15,FWDSUB               UPDATE HISTORY POINTER     U032
         LH    R15,CURRENT              GET NEW HIST TABLE OFFSET  U032
         ST    R2,HISTORY(R15)          SAVE LOCATION IN HIST TBL  U032
         SR    R0,R0                    INDICATE DISPLAY FMT=DUMP  U032
         ST    R0,HISTORY+4(R15)        AND SAVE WITH HISTORY      U032
         SPACE 2
         TTIMER  CANCEL,MIC,TEMP1       SEE HOW MUCH TIME IS LEFT  U032
         SPACE 1
         MVC   MSGLINE,BLANKS           CLEAR MESSAGE AREA         U032
         LM    R0,R1,TEMP1              REMAINING MUSEC * 2 **12   U032
         SRDL  R0,12                    CONVERT TO MICROSECONDS    U032
         D     R0,=F'1000'              CONVERT TO MILLISECS       U032
         S     R1,=A(10000*1000)        SEE HOW MUCH WE USED       U032
         MVC   MSGLINE+1(8),=X'202021204B202020' PUT IN EDIT MASK  U032
         CVD   R1,TEMP1                 GET READY TO DISPLAY       U032
         OI    TEMP1+7,X'0F'            REMOVE SIGN (WAS NEGATIVE) U032
         ED    MSGLINE(9),TEMP1+4       SHOW HOW MUCH TIME USED    U032
         MVC   MSGLINE(9),MSGLINE+2     SHIFT IT LEFT              U032
         MVC   MSGLINE+8(7),=C'SECONDS'                            U032
         SPACE 1
         L     R1,FINDCNT1              GET # BYTES SCANNED        U032
         SRL   R1,10                    CONVERT TO K BYTES SCANNED U032
         BAL   RLINK,FINDCONV           FORMAT FOR PRINTING        U032
         MVC   MSGLINE+17(5),TEMP2+1    MOVE THE CONVERTED NUMBER  U054
         MVC   MSGLINE+22(9),=C'K SCANNED'                         U054
         SPACE 1
         L     R1,FINDCNT2              GET SKIPPED COUNT          U032
         SRL   R1,10                    CONVERT TO KBYTES SKIPPED  U054
         BAL   RLINK,FINDCONV           FORMAT FOR PRINTING        U032
         MVC   MSGLINE+33(5),TEMP2+1    MOVE THE CONVERTED NUMBER  U054
         MVC   MSGLINE+38(9),=C'K SKIPPED'                         U054
         MVC   PGMCKMSG,BLANKS          CLEAR ANY PREVIOUS PGMCHK  U047
         B     FORMAT                   AND GO DISPLAY             U032
         SPACE 1
FINDCONV CVD   R1,TEMP1                 CONVERT                    U032
         MVC   TEMP2(6),=X'402020202120'  MOVE EDIT MASK           U032
         ED    TEMP2(6),TEMP1+5         UNPACK IT                  U032
         BR    RLINK                    RETURN TO MAINLINE         U032
         SPACE 1
FIND$TRT TRT   0(*-*,R1),TRTAB          << EXECUTED >>             U032
FIND$CLC CLC   1(*-*,R1),FINDDATA+1     << EXECUTED >>             U032
         SPACE 1
         DROPX R12                      FIND (local base)          U070
         EJECT
*---------------------------------------------------------------------*
*
*  CONVERT CHARACTER OR HEX OPERAND                                U032
*
*---------------------------------------------------------------------*
         SPACE 2
CORE     CSECT ,                                                   U070
         SPACE 1
         USNGX STRWORK,R6                                          U032
GETSTRNG CLI   OPERAND,C'*'             WANT RE-PROMPT?            U048
         BE    SHOWSTR                  YES - DO IT                U032
         CLI   OPERAND,C' '             ANY OPERAND?               U048
         BE    SAMESTR                  NO - USE PREVIOUS          U017
         XC    TRTAB(256),TRTAB         PREPARE TRT TABLE          U032
         CLI   OPERAND,C'A'             DELIMITER?                 U048
         BL    STRNGDLM                 YES - SPECIAL HANDLING     U017
         MVI   TRTAB+C' ',255           SET TO STOP ON BLANKS      U032
         LA    R1,OPERAND+78            PRESET STOP ADDRESS        U048
         TRT   OPERAND(78),TRTAB        FIND A BLANK               U048
         LR    R15,R1                   COPY STOP ADDRESS          U017
         LA    R14,OPERAND              -> START                   U048
         SR    R15,R14                  COMPUTE LENGTH OF HEX      U017
         STC   R15,TEMP1                STORE FOR TEST             U017
         TM    TEMP1,1                  EVEN NUMBER OF DIGITS?     U017
         BO    BADHEX                   NO - BLOW OUT              U017
         BCTR  R15,0                    -1 FOR EX                  U017
         EX    R15,HEXTRT               SEE IF ALL GOOD HEX        U017
         BNZ   BADHEX                   NOPE                       U017
*  DO THIS BY BRUTE FORCE, SINCE IT'S EASIER                       U017
         MVI   STRDLM,X'EF'             INDICATE NO DELIMITER      U032
         LA    R2,STRDATA               -> DEST                    U032
         LA    R3,OPERAND               -> SOURCE                  U048
         SPACE 1
HEXLOOP  MVC   TEMP1(2),0(R3)           GET 1 BYTE'S WORTH         U017
         TR    TEMP1(2),HEXTAB          CONVERT TO PSUEDO HEX      U017
         PACK  0(1+1,R2),TEMP1(2+1)     CONVERT TO INTERNAL HEX    U017
         LA    R2,1(,R2)                BUMP DEST PTR              U017
         LA    R3,2(,R3)                BUMP SOURCE PTR            U017
         SH    R15,=H'2'                DESC SOURCE LENGTH         U017
         BNM   HEXLOOP                  CONTINUE FOR ALL INPUT     U017
         LA    R1,STRDATA+1-1           -> START OF ALTER DATA     U032
*                                       (FUDGED FOR INCR ABOVE)    U017
         SR    R2,R1                    COMPUTE LENGTH OF DATA     U017
         STH   R2,STRLEN                SAVE LENGTH OF ALTER DATA  U048
         BR    RLINK                    RETURN TO CALLER           U032
         SPACE 1
STRNGDLM CLC   OPERAND(1),OPERAND+1     NULL STRING?               U048
         BE    STRNGNUL                 YES - COMPLAIN             U032
         SR    R1,R1                    CLEAR FOR IC               U017
         IC    R1,OPERAND               GET DELIMITER              U048
         STC   R1,STRDLM                SAVE FOR RE-PROMPT         U032
         LA    R1,TRTAB(R1)             -> SPOT IN TRT TABLE       U032
         MVI   0(R1),255                SET TO STOP THE TRT        U017
         TRT   OPERAND+1(77),TRTAB      FIND ENDING DELIM          U048
         BZ    NODELIM                  NONE?                      U017
         LA    R0,OPERAND+1             -> START OF DATA           U048
         SR    R1,R0                    COMPUTE LENGTH OF DATA     U017
         STH   R1,STRLEN                SAVE LENGTH OF ALTER DATA  U048
         MVC   STRDATA,OPERAND+1        AND SAVE THE DATA          U048
         BR    RLINK                    RETURN TO CALLER           U032
         SPACE 2
SAMESTR  OC    STRLEN,STRLEN            ANY PREVIOUS DATA?         U048
         BNZR  RLINK                    YES - RETURN TO CALLER     U032
         MVC   MSGLINE(22),=C'***** OPERAND REQUIRED'              U024
         B     FORMAT                   GO FORMAT THE SCREEN       U017
         SPACE 2
SHOWSTR  OC    STRLEN,STRLEN            ANY PREVIOUS DATA?         U048
         BZ    STARNONE                 NO - BLOW                  U032
         CLI   STRDLM,X'EF'             HEX OR CHAR?               U032
         BE    STARHEX                  HEX                        U032
         MVC   OPERAND(1),STRDLM        PUT IN LEADING DELIM       U048
         MVC   OPERAND+1(76),STRDATA    MOVE DATA & TRAILING DELIM U048
         B     FORMAT1                  DISPLAY IT                 U032
         SPACE 2
STARHEX  LH    R1,STRLEN                GET LENGTH                 U048
         LA    R3,OPERAND               -> OUTPUT AREA             U048
         LA    R2,STRDATA               -> SOURCE                  U032
         SPACE 1
STARHEX1 HEX   (0,R3),(0,R2),LEN=1      UN-DO A BYTE               U032
         LA    R3,2(,R3)                -> NEXT OUTPUT PLACE       U032
         LA    R2,1(,R2)                -> NEXT SOURCE             U032
         BCT   R1,STARHEX1              UN-DO ALL                  U032
         B     FORMAT1                  DISPLAY MY HANDIWORK       U032
         SPACE 2
STARNONE MVC   MSGLINE(25),=C'***** NO PREVIOUS OPERAND'           U032
         B     FORMAT1                                             U032
         SPACE 2
BADHEX   MVC   MSGLINE(17),=C'***** INVALID HEX'                   U031
         B     FORMAT2                  DISP WITH CURSOR AT ERROR  U017
         SPACE 2
STRNGNUL MVC   MSGLINE(25),=C'***** NULL STRING INVALID'           U032
         B     FORMAT2                  DISP WITH CURSOR AT ERROR  U032
         SPACE 2
NODELIM  MVC   MSGLINE(25),=C'***** NO ENDING DELIMITER'           U031
         B     FORMAT2                  DISP WITH CURSOR AT ERROR  U017
         SPACE 1
HEXTRT   TRT   OPERAND(*-*),NUMTAB      << EXECUTED >>             U048
         SPACE 1
         DROPX R6                                                  U032
         EJECT
*---------------------------------------------------------------------*
*
*  DISPLAY HELP ROUTINE
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "H", "?", AND "HELP" COMMANDS                                U043
         SPACE 1
HELPKEY  MVC   OPERAND,BLANKS           NO OPERAND FOR HELP KEY    U043
         L     R12,=A(HELP)             set local base             U070
         BR    R12                      go to it                   U070
         SPACE 1
HELP     CSECT ,                                                   U070
         USNGX HELP,R12                                            U070
         L     R4,=V(COREHELP)          -> HELP DESCRIPTORS        U043
         MVC   INPLINE,BLANKS           CLEAR INPUT LINE           U025
         MVI   INPLINE,C'?'             SET TO GO TO NEXT          U043
         CLI   OPERAND,C' '             ANY OPERAND?               U043
         BNE   *+8                      YES - KEEP IT              U043
         MVI   OPERAND,C'1'             NO - ASSUME '1'            U043
         CLI   OPERAND+1,C' '           OPERAND TOO LONG?          U043
         BNE   HELPBAD                  YES - DISPLAY ERROR MSG    U043
         CLI   OPERAND,C'0'             DIGIT?                     U043
         BL    HELPBAD                  NO - DISPLAY ERROR MSG     U043
         IC    R15,OPERAND              GET OPERAND DIGIT          U043
         LA    R14,1(,R15)              SET DIGIT FOR...           U043
         STC   R14,INPLINE+1            ... NEXT HELP SCREEN       U043
         N     R15,=F'15'               STRIP IT                   U043
         BZ    HELPBAD                  NO GOOD IF ZERO            U043
         C     R15,0(,R4)               OUT OF RANGE?              U043
         BH    HELPHIGH                 YES - DISPLAY ERROR MSG    U043
         BNE   *+10                     SKIP IF NOT LAST           U043
         MVC   INPLINE,BLANKS           TERMINATE AT THIS SCREEN   U043
         BCTR  R15,0                    ADD IN V.F.F.              U043
         SLL   R15,3                    *8                         U043
         LA    R15,4(R4,R15)            -> DESCRIPTORS             U043
         LM    R4,R5,0(R15)             GET (ADDR,LEN)             U043
         LA    R2,MSGLINE               -> START OF OUTPUT AREA    U043
         LA    R3,79*(16+1)             LENGTH                     U043
         ICM   R5,B'1000',BLANKS        PAD CHAR                   U003
         MVCL  R2,R4                    COPY IMAGE TO SCREEN BUF   U003
         B     DISPLAY                  GO SHOW IT                 U003
         SPACE 2                                                   U043
HELPBAD  MVC   MSGLINE(28),=C'***** HELP - OPERAND INVALID'        U043
         MVC   INPLINE(2),BLANKS        KILL REBUILT PROMPT        U043
         B     FORMAT2                                             U043
         SPACE 2                                                   U043
HELPHIGH MVC   MSGLINE(33),=C'***** HELP - OPERAND OUT OF RANGE'   U043
         MVC   INPLINE(2),BLANKS        KILL REBUILT PROMPT        U043
         B     FORMAT2                                             U043
         SPACE 1
         DROPX R12                      HELP (local base)          U070
         EJECT
*---------------------------------------------------------------------*
*
*  FLIP KEY 0 FLAG                                                 U008
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "Z" COMMAND
         SPACE 1
CORE     CSECT ,                                                   U070
         SPACE 1
ZOT      TM    FLAGS,AUTHUSER           CAN HE DO THIS?            U008
         BNO   NUMSCANX                 NO - PRETEND LIKE BAD CMD  U049
         TM    FLAGS,AUTHAPF            CAN WE DO THIS?            U008
         BNO   ZOTNOT                   NOPE                       U008
         XI    MODEFLAG,KEYZERO         FLIP FLAG                  U008
         MVC   LOCLINE+48(7),=C'USERKEY'  ASSUME NOT KEY ZERO      U040
         TM    MODEFLAG,KEYZERO         ARE WE RUNNING KEY ZERO?   U014
         BNO   FORMAT                   NO - SKIP                  U014
         MVC   LOCLINE+48(7),=C'KEYZERO'  YES - CHANGE MESSAGE     U040
         B     FORMAT                   RE-DISPLAY                 U008
         SPACE 1
ZOTNOT   MVC   MSGLINE(24),=C'***** NOT APF AUTHORIZED'            U031
         B     FORMAT                   RE-DISPLAY                 U008
         EJECT
*---------------------------------------------------------------------*
*
*  SET JOBNAME                                                     U045
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "JOB" COMMAND
         SPACE 1
JOB      BAL   RLINK,JOBCHECK           CAN HE DO THIS?            U045
         L     R1,16                    -> CVT                     U045
         L     R1,X'22C'(,R1)           CVTASVT                    U045
         L     R0,X'204'(,R1)           ASVTMAXU = MAXUSERS        U045
         LA    R1,X'210'(,R1)           ASVTFRST -> FIRST ENTRY    U045
         SPACE 2
JOBLOOP  ICM   R14,B'1111',0(R1)        GET AN ASCB ADDR           U045
         BM    JOBNEXT                  SKIP UNUSED ENTRIES        U045
         USNGX ASCB,R14                                            U045
         ICM   R15,B'1111',ASCBJBNI     -> INIT JOBNAME            U045
         BNZ   *+12                     SKIP IF PRESENT            U045
         ICM   R15,B'1111',ASCBJBNS     -> S/M/L JOBNAME           U045
         BZ    JOBNEXT                  SKIP IF NO JOBNAME PTR     U045
         CLC   OPERAND(8),0(R15)        THIS THE JOB?              U045
         BE    JOBFOUND                 YES                        U045
         SPACE 1
JOBNEXT  LA    R1,4(,R1)                -> NEXT ASCB PTR           U045
         BCT   R0,JOBLOOP               SEARCH ALL ASID'S          U045
         MVC   MSGLINE(28),=C'***** JOB XXXXXXXX NOT FOUND'        U045
         MVC   MSGLINE+10(8),OPERAND    PUT JOBNAME IN MESSAGE     U045
         B     FORMAT2                                             U045
         SPACE 2                                                   U045
JOBFOUND MVC   JOBNAME,OPERAND          SAVE JOBNAME               U045
         MVC   ASID,ASCBASID            SAVE ASID                  U045
         ST    R1,ADDRASCB              SAVE OTHER ASCB ADDRESS    U045
         B     ASIDBIND                 GO DO AXSET                U061
         DROPX R14                                                 U045
         SPACE 3
JOBCHECK TM    FLAGS,AUTHUSER           CAN HE DO THIS?            U045
         BNO   NUMSCANX                 NO - FAKE IT               U049
         TM    FLAGS,AUTHAPF            CAN I DO THIS?             U045
         BNO   ZOTNOT                   NO - TELL HIM              U045
         XC    ASID,ASID                CLEAR ASID                 U045
         CLI   OPERAND,C' '             ANY OPERAND?               U045
         BE    ASIDBIND                 NO - GO DO AXSET           U061
         CLC   OPERAND(8),=CL8'*'       RESET TO SELF?             U045
         BE    ASIDBIND                 YES - GO DO AXSET          U061
         BR    RLINK                    RETURN TO SUBCOMMAND       U045
         EJECT
*---------------------------------------------------------------------*
*
*  LOCATE SVC MODULE                                               U057
*
*---------------------------------------------------------------------*
         SPACE 2
SVC      CSECT ,                                                   U070
         USNGX SVC,R12                                             U070
         BAL   R14,CALLEVAL             EVALUATE SVC NUMBER        U057
         CH    R15,=H'4'                IS THERE AN ERROR MESSAGE? U057
         BH    FORMAT2                  YES, SKIP OUT QUICK        U057
         BE    SVC99                    RELATIVE EXPR -- ERROR     U057
         LTR   R2,R2                    SVC NUMBER NEGATIVE?       U057
         BM    SVC99                    YES, ERROR                 U057
         CH    R2,=H'255'               SVC NUMBER TOO HIGH?       U057
         BH    SVC99                    YES, ERROR                 U057
         SLL   R2,3                     (X8) OFFSET INTO SVC TABLE U057
         L     R1,16           CVTPTR   -> CVT                     U057
         L     R1,X'C8'(,R1)   CVTABEND -> SCVT                    U057
         L     R1,X'84'(,R1)   SCVTSVCT -> SVCTABLE                U057
         AR    R1,R2                    -> SVCTABLE ENTRY          U057
         MVC   MSGLINE(10),=C'TYPE 1 SVC'                          U057
         TM    4(R1),X'E0'              ANY TYPE BITS SET?         U057
         BZ    SVC01                    NO, TYPE ONE SVC           U057
         TM    4(R1),X'C0'              TYPE TWO OR THREE SET?     U057
         MVI   MSGLINE+5,C'2'                                      U057
         BM    SVC01                    MIXED, TYPE TWO            U057
         MVI   MSGLINE+5,C'3'                                      U057
         BO    SVC01                    BOTH, TYPE THREE           U057
         MVI   MSGLINE+5,C'6'           NEITHER, TYPE SIX          U057
SVC01    LA    R2,MSGLINE+10                                       U057
         TM    4(R1),X'08'              AUTHORIZED?                U057
         BZ    SVC02                    NO                         U057
         MVC   0(5,R2),=C', APF'        YES, SAY SO                U057
         LA    R2,5(,R2)                POSITION PAST MSG          U057
SVC02    TM    4(R1),X'04'              PART OF ESR?               U057
         BZ    SVC03                    NO                         U057
         MVC   0(5,R2),=C', ESR'        YES, SAY SO                U057
         LA    R2,5(,R2)                POSITION PAST MSG          U057
SVC03    TM    4(R1),X'02'              NON-PREEMPTABLE            U057
         BZ    SVC04                    NO                         U057
         MVC   0(4,R2),=C', NP'         YES, SAY SO                U057
         LA    R2,4(,R2)                POSITION PAST MSG          U057
SVC04    CLI   6(R1),X'00'              ANY LOCKS?                 U057
         BE    SVC50                    NO, EXIT                   U057
         MVC   0(8,R2),=C', LOCKS:'                                U057
         LA    R2,7(,R2)                POSITION PAST MSG          U057
         TM    6(R1),X'80'              LOCAL LOCK?                U057
         BZ    SVC05                    NO                         U057
         MVC   2(6,R2),=C'LOCAL,'       YES, SAY SO                U057
         LA    R2,7(,R2)                POSITION PAST MSG          U057
SVC05    TM    6(R1),X'40'              CMS LOCK?                  U057
         BZ    SVC06                    NO                         U057
         MVC   2(4,R2),=C'CMS,'         YES, SAY SO                U057
         LA    R2,5(,R2)                POSITION PAST MSG          U057
SVC06    TM    6(R1),X'20'              OPT LOCK?                  U057
         BZ    SVC07                    NO                         U057
         MVC   2(4,R2),=C'OPT,'         YES, SAY SO                U057
         LA    R2,5(,R2)                POSITION PAST MSG          U057
SVC07    TM    6(R1),X'10'              SALLOC LOCK?               U057
         BZ    SVC08                    NO                         U057
         MVC   2(7,R2),=C'SALLOC,'      YES, SAY SO                U057
         LA    R2,8(,R2)                POSITION PAST MSG          U057
SVC08    TM    6(R1),X'08'              DISP LOCK?                 U057
         BZ    SVC09                    NO                         U057
         MVC   2(5,R2),=C'DISP,'        YES, SAY SO                U057
         LA    R2,6(,R2)                POSITION PAST MSG          U057
SVC09    MVI   0(R2),C' '               CLEAR TRAILING COMMA       U057
SVC50    LH    R2,CURRENT               HISTORY TABLE OFFSET       U057
         LA    R2,HISTORY(R2)           -> HISTORY TABLE ENTRY     U057
         MVC   0(4,R2),0(R1)            SET ADDRESS TO DISPLAY     U057
         B     FORMAT                   GO DISPLAY IT              U057
SVC99    MVC   MSGLINE(24),=C'***** INVALID SVC NUMBER'            U057
         B     FORMAT2                  GO WRITE MESSAGE           U057
         SPACE 1
         DROPX R12                      SVC (local base)           U070
         EJECT
*---------------------------------------------------------------------*
*
*  LOCATE SUBSYSTEM CONTROL VECTOR TABLE                           U058
*
*---------------------------------------------------------------------*
         SPACE 2
CORE     CSECT ,                                                   U070
         SPACE 1
SSCVT    L     R1,16           CVTPTR   -> CVT                     U058
         L     R1,X'128'(,R1)  CVTJESCT -> JESCT                   U058
         L     R1,X'18'(,R1)   JESSSCT  -> SSCVT                   U058
SSCVT01  CLC   8(4,R1),OPERAND SSCTSNAM DESIRED SUBSYSTEM?         U058
         BE    SSCVT02                  YES, GO DISPLAY IT         U058
         ICM   R1,B'1111',4(R1)         NO, -> NEXT SSCVT          U058
         BNZ   SSCVT01                  GOT ONE, TEST IT           U058
         MVC   MSGLINE(30),=C'***** UNDEFINED SUBSYSTEM NAME'      U058
         B     FORMAT2                  GO WRITE MESSAGE           U058
SSCVT02  LH    R15,CURRENT              HISTORY TABLE OFFSET       U058
         LA    R15,HISTORY(R15)         -> HISTORY TABLE ENTRY     U058
         ST    R1,0(,R15)               SET ADDRESS TO DISPLAY     U058
         B     FORMAT                   GO DISPLAY IT              U058
         EJECT
*---------------------------------------------------------------------*
*
*  SET ASID                                                        U045
*
*---------------------------------------------------------------------*
         SPACE 2
*---  "ASID" COMMAND
         SPACE 1
ASIDGET  CSECT ,                                                   U070
         USNGX ASIDGET,R12                                         U070
         BAL   RLINK,JOBCHECK           CAN HE DO THIS?            U045
         TR    OPERAND,HEXTAB           CONVERT TO HEX             U045
         LA    R1,OPERAND               -> START OF OPERAND        U045
         SR    R0,R0                    CLEAR ACCUMULATOR          U045
         LA    R14,15                   MASK                       U045
         MVI   OPERAND+L'OPERAND-1,C' ' INSURE STOP                U045
         SPACE 1
ASIDLOOP CLI   0(R1),C' '               END OF OPERAND?            U045
         BE    ASIDEND                  YES                        U045
         IC    R15,0(,R1)               GET A CHAR                 U045
         NR    R15,R14                  STRIP IT                   U045
         SLL   R0,4                     SHIFT PREVIOUS             U045
         OR    R0,R15                   ADD IN THIS HEX DIGIT      U045
         LA    R1,1(,R1)                -> NEXT INPUT CHAR         U045
         B     ASIDLOOP                 CONTINUE                   U045
         SPACE 1
ASIDEND  L     R1,16                    CVTPTR                     U045
         L     R1,X'22C'(,R1)           CVTASVT                    U045
         C     R0,X'204'(,R1)           ASVTMAXU                   U045
         BH    ASIDBAD                  SPECIFIED GT MAX           U045
         LTR   R0,R0                    POSITIVE?                  U045
         BNP   ASIDBAD                  NO - ERROR                 U045
         SLL   R0,2                     *4                         U045
         LA    R1,X'20C'(,R1)           ASVTFRST -> FIRST ENT-4    U045
         AR    R1,R0                    -> ASCB ADDRESS            U045
         TM    0(R1),X'80'              UNUSED ENTRY?              U045
         BO    ASIDFREE                 YES                        U045
         L     R1,0(,R1)                -> ASCB                    U045
         USNGX ASCB,R1                                             U045
         MVC   ASID,ASCBASID            SAVE ASID                  U045
         ST    R1,ADDRASCB              SAVE OTHER ASCB ADDRESS    U045
         MVC   JOBNAME,=C'????????'     INITIALIZE JOBNAME         U045
         ICM   R14,B'1111',ASCBJBNI     INITIATED JOBNAME          U045
         BNZ   *+12                     SKIP IF GOT IT             U045
         ICM   R14,B'1111',ASCBJBNS     -> S/M/L JOBNAME           U045
         BZ    ASIDBIND                 NO JOBNM PTR - GO DO AXSET U061
         MVC   JOBNAME,0(R14)           SAVE JOBNAME               U045
         B     ASIDBIND                 GO DO AXSET                U061
         DROPX R1                                                  U045
         SPACE 2
ASIDBAD  MVC   MSGLINE(28),=C'***** ASID - INVALID OPERAND'        U045
         B     FORMAT2                                             U045
         SPACE 2
ASIDFREE MVC   MSGLINE(26),=C'***** ASID XXXX NOT IN USE'          U045
         SRL   R0,2                     DIVIDE BY 4 (RESTORE ASID) U045
         STH   R0,TEMP1                 PUT INTO WORK AREA         U045
         HEX   MSGLINE+11,TEMP1,LEN=2   PUT ASID INTO MESSAGE      U045
         B     FORMAT2                                             U045
         SPACE 1
         DROPX R12                      ASIDGET (local base)       U070
         EJECT
*---------------------------------------------------------------------*
*
*  SET AX TO ALLOW ACCESS TO ANOTHER ADDRESS SPACE                 U061
*
*---------------------------------------------------------------------*
         SPACE 2
CORE     CSECT ,                                                   U070
         SPACE 1
*%  SHOULD PREVENT OTHER A.S. FROM TERMINATING WHILE WE HAVE IT    U061
*%  BOUND.  SOME OTHER DAY.                                        U061
ASIDBIND L     R1,ADDRASCB              -> ASCB                    U061
         L     R1,ASCBOUCB-ASCB(,R1)    -> OUCB                    U061
         TM    OUCBSFL-OUCB(R1),OUCBNSW NON-SWAPPABLE?             U061
         BO    BIND$OK                  YES - CONTINUE             U061
         MVC   MSGLINE(50),=C'***** XXXXXXXX (ASID XXXX) NOT NON-SWAPPA$
               BLE *****'                                          U061
         MVC   MSGLINE+6(8),JOBNAME     PUT JOBNAME INTO MESSAGE   U061
         HEX   MSGLINE+21,ASID,LEN=2,BYTE=C')'  ALSO ASID          U061
         XC    ASID,ASID                CLEAR ASID                 U061
         B     FORMAT2                                             U061
BIND$OK  TM    FLAGS,AXSAVED            ALREADY SAVED AXVAL?       U061
         BO    FORMAT                   YES - DON'T SAVE IT AGAIN  U061
         BAL   R14,MS$SUPR              GET SUPERVISOR STATE       U061
         SPACE 1
*---  THE STUPID MACRO FOLLOWING DOES NOT ALLOW COMMENTS           U061
*---  EXTRACT AUTHORIZATION INDEX FOR CURRENT PASID                U061
         AXEXT
         SPACE 1
         STH   R0,OLDAXVAL              SAVE FOR RESTORE           U061
         SPACE 1
         ESAR  R1                       GET SECONDARY ASID         U061
         SPACE 1
         STH   R1,OLDSASID              SAVE FOR RESTORE           U061
         SPACE 1
         LA    R0,1                     GET AX VALUE               U061
         SPACE 1
         AXSET AX=(0)                   SET AUTHORIZATION INDEX    U061
         SPACE 1
         BAL   R14,MS$PROB              RESTORE PROBLEM STATE      U061
         B     FORMAT                                              U061
         EJECT                                                     U045
*---------------------------------------------------------------------*
*
*  PROGRAM CHECK EXIT
*
*---------------------------------------------------------------------*
         SPACE 2
          AIF   (&@@SPLVL LT 200).XA05                             U063
         USNGX EPIE,R1                                             U063
.XA05     ANOP                                                     U063
SPIEEXIT TM    FLAGS,PGMCHKOK           PGM CHK ALLOWED?           U041
         BO    OKPGMCHK                 YES - CONTINUE             U041
         SPACE 2
         MVC   PGMCKMSG,BLANKS                                     U063
         MVC   PGMCKMSG(22),=C'ADDR=xxxxxxxx  OFFSET='             U063
          AIF   (&@@SPLVL GE 200).XA13                             U063
         L     R0,8(,R1)                PSW IN PIE                 U063
         HEX   PGMCKMSG+5,(8,R1),LEN=4  show addr of pgm check     U063
          AGO   .XA14                                              U063
.XA13     ANOP                                                     U063
         L     R0,EPIEPSW+4             PSW IN PIE                 U063
         HEX   PGMCKMSG+5,EPIEPSW+4,LEN=4  show addr of pgm check  U063
.XA14     ANOP                                                     U063
         SR    R0,R10                   compute offset             U063
         ST    R0,TEMP1                                            U063
         HEX   PGMCKMSG+22,TEMP1+1,LEN=3  show offset of pgm check U063
         SPACE 1
         TPUTX '*** UNEXPECTED PROGRAM CHECK ***'                  U041
         SPACE 1
         TPUTX PGMCKMSG,28                                         U063
         SPACE 1
         ABEND 1                                                   U041
         SPACE 2
OKPGMCHK MVC   PGMCKMSG,=C' ****** PROGRAM CHECK ******'           U046
          AIF   (&@@SPLVL GE 200).XA06                             U063
         CLI   4+2+1(R1),4              PROTECTION?                U006
          AGO   .XA07                                              U063
.XA06     ANOP                                                     U063
         CLI   EPIEINT+2+1,4            PROTECTION?                U063
.XA07     ANOP                                                     U063
         BNE   *+10                     NO - SKIP                  U006
         MVC   PGMCKMSG,=C'*** PROTECTION EXCEPTION ***'           U046
          AIF   (&@@SPLVL GE 200).XA08                             U063
         CLI   4+2+1(R1),5              ADDRESSING?                U006
          AGO   .XA09                                              U063
.XA08     ANOP                                                     U063
         CLI   EPIEINT+2+1,5            ADDRESSING?                U063
.XA09     ANOP                                                     U063
         BNE   *+10                     NO - SKIP                  U006
         MVC   PGMCKMSG,=C'*** ADDRESSING EXCEPTION ***'           U046
         LA    R0,SPIERET               SET RETURN FROM GETSTOR
          AIF   (&@@SPLVL GE 200).XA10                             U063
         ST    R0,8(,R1)                PSW IN PIE
          AGO   .XA11                                              U063
.XA10     ANOP                                                     U063
         ST    R0,EPIEPSW+4             PSW IN PIE                 U063
         OI    EPIEPSW+4,X'80'          and run in 31 bit mode     U063
.XA11     ANOP                                                     U063
SPIERET  BR    R14                      RETURN TO SUPV
          AIF   (&@@SPLVL LT 200).XA12                             U063
         DROPX R1                       EPIE                       U063
.XA12     ANOP                                                     U063
         SPACE 5
*---------------------------------------------------------------------*
*
* ERASE  --  CLEAR THE SCREEN BUFFER
*
*---------------------------------------------------------------------*
         SPACE 2
ERASE    SAVE  (2,5)                    NEED SOME WORK REGS
         LA    R2,OUT1                  START OF AREA TO CLEAR
         LA    R3,79*16                 LENGTH TO CLEAR
         LA    R5,C' '                  FILL CHAR
         SLL   R5,24                    MAKE PAD BYTE AND CLEAR SOURCE
         MVCL  R2,R4                    PROPAGATE BLANKS
         RETURN (2,5)                   RESTORE REGS AND RETURN
         SPACE 5
*---------------------------------------------------------------------*
*
* CALLEVAL -- CALL EXPRESSION EVALUATOR                            U057
*
*---------------------------------------------------------------------*
         SPACE 2
CALLEVAL ST    R14,CELINK               SAVE ROUTINE LINKAGE       U057
         LA    R1,OPERAND               -> INPUT DATA              U057
         LA    R0,OPERAND+L'OPERAND     -> END OF DATA + 1         U057
         SR    R0,R1                    LENGTH OF DATA             U057
         L     R15,=A(EVAL)             -> EXPRESSION EVALUATOR    U057
         BALR  R14,R15                  EVALUATE EXPRESSION        U057
         CH    R15,=H'4'                DID IT WORK?               U057
         BNH   CE01                     YES, RETURN TO CALLER      U057
         MVC   MSGLINE(6),=CL6'*****'                              U057
         LH    R14,0(,R15)              MESSAGE LENGTH             U057
         BCTR  R14,0                    MINUS ONE FOR EX           U057
         EX    R14,CEMVC                MVC MSGLINE+6(0),2(R15)    U057
         B     CE02                     GO RETURN TO CALLER        U057
CE01     LR    R3,R1                    SET SCAN POINTER           U057
CE02     L     R14,CELINK               RESTORE ROUTINE LINKAGE    U057
         BR    R14                      RETURN TO CALLER           U057
         SPACE 2                                                   U057
CEMVC    MVC   MSGLINE+6(0),2(R15)      << EXECUTED >>             U057
         EJECT
*---------------------------------------------------------------------*
*
*  MODE CHANGE SUBROUTINES
*
*---------------------------------------------------------------------*
         SPACE 1
MS$SUPR  MODESET  MODE=SUP              GET SUPERVISOR STATE       U024
         SPACE 1
         BR    R14                      RETURN TO CALLER           U024
         SPACE 3
MS$PROB  MODESET  MODE=PROB             GET PROBLEM STATE          U024
         SPACE 1
         BR    R14                      RETURN TO CALLER           U024
         SPACE 3
MS$KZERO TM    MODEFLAG,KEYZERO         ARE WE RUNNING KEY ZERO?   U050
         BZR   R14                      NO - SKIP                  U050
         SPACE 1
         MODESET  KEY=ZERO              GET KEY ZERO               U024
         SPACE 1
         BR    R14                      RETURN TO CALLER           U024
         SPACE 3
MS$KUSER TM    MODEFLAG,KEYZERO         ARE WE RUNNING KEY ZERO?   U050
         BZR   R14                      NO - SKIP                  U050
         OC    ASID,ASID                FETCH FROM ANOTHER A.S.?   U068
         BNZ   MS_KU_XM                 yes - can't use SVC type   U068
         SPACE 1
         MODESET  KEY=NZERO             GET PROBLEM KEY            U024
         SPACE 1
         BR    R14                      RETURN TO CALLER           U024
         SPACE 2
MS_KU_XM IC    R15,MYKEY                get my original key        U068
         SPKA  0(R15)                                              U068
         BR    R14                      RETURN TO CALLER           U068
         EJECT
*---------------------------------------------------------------------*
*
*  STORAGE RETRIEVAL SUBROUTINE
*
*---------------------------------------------------------------------*
*
*  INPUT:
*     R0 =  LENGTH TO FETCH
*     R2 -> SOURCE LOCATION
*
*  OUTPUT:
*     "DUMPDATA" WILL CONTAIN THE REQUESTED DATA
*
*  RETURNS:
*     ALL DATA FOUND:             R15 = 0, CC = 0                  U054
*     SOME OR ALL DATA NOT FOUND: R15 = 4, CC = 2                  U054
*
GETSTOR  STM   R14,R7,12(R13)           SAVE ALL REGISTERS         U054
         MVI   LOGOOD,X'FF'             INVALIDATE                 U054
         MVI   HIGOOD,X'00'               STORAGE RANGE            U054
         LR    R3,R0                    DATA LENGTH                U054
         LR    R6,R3                    SAVE DATA LENGTH           U054
         AR    R0,R2                    END OF DATA + 1            U054
         BCTR  R0,0                     ACTUAL END OF DATA         U054
         N     R0,=X'FFFFF000'          GET START OF PAGE          U055
         CR    R0,R2                    ALL DATA IN SAME PAGE?     U054
         BL    GETS01                   YES, SETUP IS OKAY         U054
         SR    R0,R2                    GET LENGTH IN PAGE         U054
         LR    R3,R0                    AND USE THAT FOR MOVE      U054
         SPACE 1
GETS01   LR    R7,R3                    SAVE MOVED DATA LENGTH     U054
         LA    R4,DUMPDATA              DESTINATION ADDRESS        U054
         BAL   R5,GOFETCH               GET DATA IN FIRST PAGE     U054
         MVC   KEYSAVE1,KEYSAVE         SAVE THE KEY               U054
         MVC   KEYSAVE2,KEYSAVE         SAVE IT AGAIN              U054
         LTR   R15,R15                  DID FETCH WORK?            U054
         BNZ   GETS02                   NO, TRY NEXT ONE           U054
         ST    R2,LOGOOD                YES, SET LOW ADDRESS       U054
         LR    R0,R2                    START OF DATA              U054
         AR    R0,R3                    END OF DATA                U054
         ST    R0,HIGOOD                SET HIGH ADDRESS           U054
         SPACE 1                                                   U054
GETS02   SR    R6,R7                    RESIDUAL DATA LENGTH       U054
         BNP   GETS04                   NONE, EXIT                 U054
         ALR   R2,R7                    DATA RESTART ADDRESS       U054
         LR    R3,R6                    LENGTH TO MOVE             U054
         AR    R4,R7                    DEST RESTART ADDRESS       U054
         BAL   R5,GOFETCH               FETCH THE SECOND PART      U054
         MVC   KEYSAVE2,KEYSAVE         SAVE THE KEY               U054
         LTR   R15,R15                  DID SECOND FETCH WORK?     U054
         BNZ   GETS04                   NO, EXIT WITH ERROR        U054
         CLI   LOGOOD,X'FF'             PREVIOUS FETCH WORK?       U054
         BNE   GETS03                   YES, DON'T UPDATE LOGOOD   U054
         ST    R2,LOGOOD                NO, SET NEW LOW ADDRESS    U054
         LA    R15,4                    INDICATE PREVIOUS FAILURE  U054
GETS03   LR    R0,R2                    START OF DATA              U054
         AR    R0,R3                    END OF DATA                U054
         ST    R0,HIGOOD                SET HIGH ADDRESS           U054
         SPACE 1
GETS04   L     R14,12(R13)              RESTORE CALLER'S R14       U054
         LM    R0,R7,20(R13)            RESTORE CALLER'S R0-R7     U054
         LTR   R15,R15                  SET CC FOR CALLER          U054
         BR    RLINK                    RETURN TO CALLER           U054
         EJECT
*
*  INPUT:
*     R2 -> SOURCE LOCATION
*     R3 =  LENGTH TO FETCH
*     R4 -> DESTINATION
*     R5 -> RETURN ADDRESS                                         U054
*
*  OUTPUT:
*     DATA MOVED TO REQUESTED LOCATION
*
*  RETURNS:
*     ALL DATA FOUND:             R15 = 0                          U054
*     SOME OR ALL DATA NOT FOUND: R15 = 4                          U054
*
GOFETCH  OI    FLAGS,PGMCHKOK           INDICATE PGMCHK'S ALLOWED  U041
         MVI   KEYSAVE,X'FF'            ASSUME LRA FAILED          U056
         BAL   R14,MS$KZERO             GET KEY ZERO               U024
         OC    ASID,ASID                FETCH FROM ANOTHER A.S.?   U061
         BNZ   GF$CMS                   YES - DO IT                U061
         LA    R14,BADFETCH             SET SPIE RETURN ADDR       U022
         BCTR  R3,0                     -1 FOR EX                  U026
         EX    R3,FETCHMVC              FETCH THE DATA             U026
         BAL   R14,MS$KUSER             RESTORE MY KEY             U024
         NI    FLAGS,255-PGMCHKOK       DISALLOW PGMCHK'S          U041
         XR    R15,R15                  INDICATE FETCH WORKED      U054
         TM    FLAGS,AUTHAPF+DISPKEY    CAN WE DO THIS?            U047
         BNOR  R5                       NO - RETURN NOW TO GETSTOR U054
         BAL   R14,MS$SUPR              GET SUPERVISOR MODE        U047
         LTR   R15,R2                   VIRTUAL PAGE 0?            U059
         BZ    GF$ISK                   YES - SKIP LRA             U059
         LRA   R15,0(,R2)               GET REAL STORAGE ADDRESS   U056
         BNZ   GF$01                    RETURN IF FAILED           U054
         N     R15,=X'FFFFF000'         GET DOWN TO 4K BOUNDARY    U055
*GF$ISK  ISK   R14,R15                  GET THE STORAGE KEY        U047
GF$ISK   ISKE  R14,R15                  GET THE STORAGE KEY        U062
         STC   R14,KEYSAVE              SAVE IT                    U047
GF$01    BAL   R14,MS$PROB              GET OUT OF SUPERVISOR      U054
         XR    R15,R15                  INDICATE FETCH WORKED      U054
         BR    R5                       RETURN TO GETSTOR          U054
         SPACE 1
GF$CMS   BAL   R14,MS$SUPR              GET SUPERVISOR             U061
         LH    R14,ASID                 GET DESIRED ASID           U061
         SSAR  R14                      SET AS SECONDARY ASN       U061
         SR    R15,R15                  CLEAR KEY                  U061
         LA    R14,BADFETCH             SET SPIE RETURN ADDR       U061
         MVCP  0(R3,R4),0(R2),R15       FETCH THE DATA             U061
         BAL   R14,MS$KUSER             RESTORE MY KEY             U061
         LH    R14,OLDSASID             GET OLD SECONDARY ASID     U061
         SSAR  R14                      RESTORE SECONDARY ASN      U061
         BAL   R14,MS$PROB              RESTORE PROBLEM STATE      U061
         NI    FLAGS,255-PGMCHKOK       DISALLOW PGMCHK'S          U061
*%%  WHAT ABOUT THE STORAGE KEY???                                 U061
*%%      XR    R15,R15                  INDICATE FETCH WORKED      U061
*%%      TM    FLAGS,AUTHAPF+DISPKEY    CAN WE DO THIS?            U061
*%%      BNOR  R5                       NO - RETURN NOW TO GETSTOR U061
*%%      BAL   R14,MS$SUPR              GET SUPERVISOR MODE        U061
*%%      LRA   R15,0(,R2)               GET REAL STORAGE ADDRESS   U061
*%%      BNZ   GF$03                    RETURN IF FAILED           U061
*%%      N     R15,=X'FFFFF000'         GET DOWN TO 4K BOUNDARY    U061
*%%      ISK   R14,R15                  GET THE STORAGE KEY        U061
*%%      STC   R14,KEYSAVE              SAVE IT                    U061
*%%GF$03 BAL   R14,MS$PROB              GET OUT OF SUPERVISOR      U061
         XR    R15,R15                  INDICATE FETCH WORKED      U061
         BR    R5                       RETURN TO GETSTOR          U061
         SPACE 1
BADFETCH BAL   R14,MS$KUSER             RESTORE MY KEY             U054
         NI    FLAGS,255-PGMCHKOK       DISALLOW PGMCHK'S          U054
         LA    R15,4                    INDICATE FAILURE           U054
         OC    ASID,ASID                IN ALTERNATE ADDR MODE?    U061
         BZR   R5                       NO - RETURN NOW TO GETSTOR U061
         LH    R14,OLDSASID             GET OLD SECONDARY ASID     U061
         SSAR  R14                      RESTORE SECONDARY ASN      U061
         BAL   R14,MS$PROB              RESTORE PROBLEM STATE      U061
         LA    R15,4                    INDICATE FAILURE           U054
         BR    R5                       RETURN NOW TO GETSTOR      U054
         SPACE 2
FETCHMVC MVC   0(*-*,R4),0(R2)          << EXECUTED >>             U026
         EJECT
BLANKS   DC    CL79' '
         SPACE 1
         LTORG ,                                                   U070
         SPACE 1
          AIF   (&@@SPLVL GE 200).XA91                             U063
SPIEMFL  SPIE  SPIEEXIT,(4,5),MF=L      ROOM FOR SPIE PARM LIST    U022
.XA91     ANOP                                                     U063
         SPACE 3
HEXTAB   DC    256C' '                  GENERAL HEX CONVERSION TABLE
         ORG   HEXTAB+C'A'              INPUT CONVERSION
         DC    X'0A0B0C0D0E0F'
         ORG   HEXTAB+C'0'              OUTPUT CONVERSION
         DC    C'0123456789ABCDEF'
         ORG
         SPACE 2
* HEX INPUT TRT TABLE
NUMTAB   DC    256X'1'
         ORG   NUMTAB+C'A'
         DC    6X'0'
         ORG   NUMTAB+C'0'
         DC    10X'0'
         ORG
         SPACE 2
VALCHAR  CHARTAB  FILL='.'              TRANSLATE TO VALID CHARS   U046
         SPACE 2
*  CARET OFFSETS INTO HEX PART OF SCREEN LINE (DUMP FORMAT)        U063
*UMPOFFS DC    AL1(8,13,13,18,19,24,24,29,31,36,36,41,42,47,47,52) U021
DUMPOFFS DC    AL1(0,5,5,10,11,16,16,21,23,28,28,33,34,39,39,44)   U063
         EJECT
         LTORG ,                                                   U024
         EJECT
SCANTAB  DC    0A(0)                    COMMAND TABLE
*---  Table format changed                                  --->   U063
         SCMDE 'UCB',UCB                                           U009
         SCMDE ' ',FORMAT
         SCMDE 'D',DOWN                                            U028
         SCMDE 'U',UP                                              U028
         SCMDE 'IS',INDIR$S                                        U066
         SCMDE 'I',INDIR
         SCMDE '>',FWD
         SCMDE '<',BACK
         SCMDE 'F',FIND
         SCMDE 'LPA',MOD$LPA                                       U069
         SCMDE 'WHERE',WHERE                                       U069
         SCMDE 'L',LINK
         SCMDE '=',DEFINE
         SCMDE ',',NUMSCAN                                         U049
         SCMDE 'END',ENDIT                                         U023
         SCMDE 'HIST',SHOWHIST                                     U028
         SCMDE 'MODE1',MODE1                                       U023
         SCMDE 'MODE2',MODE2                                       U023
         SCMDE 'B',OVERLAY                                         U019
         SCMDE 'Z',ZOT                                             U008
         SCMDE 'SVC',SVC                                           U057
         SCMDE 'SSCVT',SSCVT                                       U058
         SCMDE 'S',STORES                                          U017
         SCMDE 'X',STOREX                                          U017
         SCMDE 'O',STOREO                                          U017
         SCMDE 'N',STOREN                                          U017
         SCMDE 'M',MODULE                                          U033
         SCMDE 'PAGEFIX',PAGEFIX                                   U020
         SCMDE 'PAGEFREE',PAGEFREE                                 U020
         SCMDE '?',HELP                                            U003
         SCMDE 'HELP',HELP                                         U043
         SCMDE 'H',HELP                                            U003
         SCMDE 'ASID',ASIDGET                                      U045
         SCMDE 'JOB',JOB                                           U045
         DC    X'FF'                    END OF TABLE INDICATOR
         EJECT
*  SCREEN FORMAT
         SPACE 1
SCR1     DCMSFMT
HEADER   DCMSFLD 79                                                U031
INPLINE  DCMSFLD 79,ALPHA,CURSOR=YES                               U031
INPSAVE  DCMSFLD 79,INTEN=LO                                       U031
***  NOTE:  "HELP" DEPENDS ON MSGLINE BEING IMMEDIATELY IN FRONT   U043
***         OF OUT1.                                               U043
MSGLINE  DCMSFLD 79                                                U031
OUT1     DCMSFLD 79,INTEN=LO                                       U031
         PRINT NOGEN
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         DCMSFLD 79,INTEN=LO
         PRINT GEN
LOCLINE  DCMSFLD 79,ROW=22,COLUMN=2                                U040
         DCMSFEND
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*  EVALUATE AN EXPRESSION                                             *
*                                                                     *
*  EXPRESSIONS MAY CONSIST OF ADD, SUBTRACT, MULTIPLY, AND DIVIDE     *
*  OPERATIONS INVOLVING HEXADECIMAL CONSTANTS, DECIMAL CONSTANTS      *
*  (NUMERICS FOLLOWED BY A PERIOD), AND SYMBOLS.  THE DOLLAR          *
*  CHARACTER IS A SPECIAL SYMBOL THAT HAS THE VALUE OF THE CURRENT    *
*  LOCATION COUNTER.                                                  *
*                                                                     *
*  MULTIPLY AND DIVIDE OPERATIONS WILL BE PERFORMED FROM LEFT TO      *
*  RIGHT BEFORE ADD AND SUBTRACT OPERATIONS ARE PERFORMED FROM LEFT   *
*  TO RIGHT.  EXPRESSIONS IN PARENTHESES WILL BE EVALUATED FIRST.     *
*                                                                     *
*  AN EXPRESSION THAT STARTS WITH AN ADD OR SUBTRACT OPERATOR (A      *
*  'RELATIVE' EXPRESSION) WILL BE ADDED TO OR SUBTRACTED FROM THE     *
*  INITIAL VALUE SUPPLIED BY THE CALLER.                              *
*                                                                     *
*  REGISTER USAGE:                                                    *
*    R0  - INPUT, LENGTH OF EXPRESSION STRING                         *
*        - WORK                                                       *
*    R1  - INPUT, ADDRESS OF EXPRESSION STRING                        *
*        - OUTPUT, ADDRESS OF END OF STRING + 1                       *
*    R2  - INPUT, INITIAL VALUE (IF EXPRESSION IS RELATIVE)           *
*        - OUTPUT, RESULTING VALUE OF EVALUATION                      *
*    R13 - INPUT, WORK AREA ADDRESS                                   *
*    R14 - INPUT, RETURN ADDRESS                                      *
*    R15 - OUTPUT, RETURN CODE                                        *
*          0 - SUCCESS, ABSOLUTE EXPRESSION                           *
*          4 - SUCCESS, RELATIVE EXPRESSION                           *
*          >4 - FAILURE, POINTER TO ERROR MESSAGE AND LENGTH          *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 2
         DROPX R10,R11                  DROP BASE REGISTERS
         INUSE
         SPACE 2
EVAL#    CSECT ,                                                   U070
EVAL     SAVE  (14,12),,*               SAVE CALLER'S REGS
         LR    R11,R15                  SET EVAL BASE
         USNGX EVAL,R11                 ADDRESS EVAL
         L     R10,=A(EVALWK-WORKAREA)  EVAL work area offset      U071
         AR    R10,R13                  -> EVALWK                  U071
         USNGX EVALWK,R10               ADDRESS EVAL WORD
         ST    R2,EVNSTAK               SAVE INITIAL VALUE
         LA    R2,EVNSTAK               -> CURR STACK ENTRY
         ST    R2,EVNSTAKP              SAVE STACK POINTER
         MVC   EVOSTAK(2),=AL1(EV$STRT,EVP$STRT)  INIT OP STACK
         LA    R2,EVOSTAK               -> CURR STACK ENTRY
         ST    R2,EVOSTAKP              SAVE STACK POINTER
         BAL   R14,INITCHAR             INITIALIZE STRING ROUTINES
         MVI   EVFLAGS,EVFINIT          SET FLAGS TO INITIAL NUMBER
         B     *+8                      GO GET TOKEN
EVLOOP   NI    EVFLAGS,255-EVFINIT      PAST FIRST TOKEN
         BAL   R14,GETTOKN              GET NEXT TOKEN
         B     *+4(R15)                 BRANCH ON RETURN CODE
         B     EVNUM                 0  NUMBER
         B     EVOP                  4  OPERATOR
         B     EVSYM                 8  SYMBOL
         LR    R15,R1               12  -> TOKEN ERROR MESSAGE
EVEXIT   LM    R0,R12,12+2*4(R13)       RESTORE REGS 0-12
         L     R14,12(,R13)             RESTORE REG 14
         BR    R14                      RETURN TO CALLER
         SPACE 2
* PROCESS SYMBOL TOKEN
EVSYM    BAL   R14,SYMLOC               GO LOCATE SYMBOL TABLE ENTRY
         LTR   R15,R15                  TEST RETURN CODE
         BZ    EVS01                    FOUND SYMBOL
         MVC   EVERRWK+2(7),=C'SYMBOL ' BUILD ERROR MESSAGE        U051
         MVC   EVERRWK+2+7(8),WORD      SYMBOL NAME                U051
         LA    R15,EVERRWK+2+7          -> START OF SYMBOL NAME    U051
         AH    R15,WORDLEN              -> END OF NAME PLUS ONE    U051
         MVC   0(12,R15),=C' NOT DEFINED'  END OF MESSAGE TEXT     U051
         LA    R15,7+12                 LENGTH OF FIXED TEXT       U051
         AH    R15,WORDLEN              FULL MESSAGE LENGTH        U051
         STH   R15,EVERRWK              SET MESSAGE LENGTH         U051
         LA    R15,EVERRWK              PASS MESSAGE BACK          U051
         B     EVEXIT                   RETURN WITH ERROR
EVS01    L     R1,8(,R1)                SYMBOL VALUE
*        B     EVNUM                    PROCESS AS NUMBER TOKEN
         SPACE 2
* PROCESS NUMBER TOKEN
EVNUM    TM    EVFLAGS,EVFNUM           TWO NUMBERS IN A ROW?
         BO    EVOPSEQ                  YES, OPERAND SEQUENCE ERROR
         OI    EVFLAGS,EVFNUM           REMEMBER NUMBER
         L     R14,EVNSTAKP             -> CURR STACK ENTRY
         LA    R14,4(,R14)              -> NEXT STACK ENTRY
         LA    R15,EVNSTAKE             -> STACK LIMIT
         CR    R14,R15                  ROOM IN STACK?
         BH    EVCOMP                   NO, EXPRESSION TOO COMPLEX
         ST    R14,EVNSTAKP             SAVE STACK POINTER
         ST    R1,0(,R14)               STACK THE NUMBER
         B     EVLOOP                   GO GET ANOTHER TOKEN
         SPACE 2
* PROCESS OPERATOR TOKEN
EVOP     CLI   OPERATOR,EV$LPAR         OPEN PARENTHESIS?
         BNE   EVO01                    NO, CONTINUE
         TM    EVFLAGS,EVFNUM           PREVIOUS NUMBER?
         BNO   EVO05                    NO, GO STACK PAREN
EVOPSEQ  LA    R15,EVEMOS               -> OPERATOR SEQUENCE MSG
         B     EVEXIT                   RETURN WITH ERROR
EVO01    TM    EVFLAGS,EVFINIT          LEADING OPERATOR?
         BNO   EVO02                    NO, CONTINUE
         OI    EVFLAGS,EVFREL           YES, INDICATE RELATIVE EXPR
         CLI   OPPRIOR,EVP$MUL          MULT OR DIV PRIORITY?
         BE    EVOPSEQ                  YES, INVALID OPERATOR SEQ
EVO02    TM    EVFLAGS,EVFNUM+EVFINIT   PREVIOUS NUMBER?
         BZ    EVOPSEQ                  NO, OPERATOR SEQUENCE ERROR
         CLI   OPERATOR,EV$RPAR         CLOSE PARENTHESIS?
         BNE   EVO03                    NO, CONTINUE
         OI    EVFLAGS,EVFNUM           YES, ACT LIKE PREV NUMBER
         B     EVO04                    CONTINUE
EVO03    NI    EVFLAGS,255-EVFNUM       REMEMBER NOT NUMBER
EVOLOOP  EQU   *
EVO04    L     R14,EVOSTAKP             -> CURR OPERATOR STACK ENTRY
         CLC   OPPRIOR,1(R14)           COMPARE PRIORITIES
         BNH   EVO07                    NOT HIGHER - POP STACK
         CLI   OPERATOR,EV$RPAR         HIGHER - STACKING CLOSE PAREN?
         BE    EVNBPAR                  YES, EXCESS RIGHT PARENS
EVO05    L     R14,EVOSTAKP             -> CURR OPERATOR STACK ENTRY
         LA    R14,2(,R14)              -> NEXT OPERATOR STACK ENTRY
         LA    R15,EVOSTAKE             -> OPERATOR STACK LIMIT
         CR    R14,R15                  IS THERE ROOM IN STACK?
         BNH   EVO06                    YES, GO STACK IT
EVCOMP   LA    R15,EVEMCE               -> COMPLEX EXPR MSG
         B     EVEXIT                   RETURN WITH ERROR
EVO06    ST    R14,EVOSTAKP             SAVE OPERATOR STACK POINTER
         MVC   0(2,R14),OPERATOR        STACK NEW OPERATOR
         B     EVLOOP                   GO GET NEXT TOKEN
EVO07    SR    R15,R15
         IC    R15,0(,R14)              OPERATOR VALUE
         SLL   R15,2                    OPERATOR TIMES 4
         SH    R14,=H'2'                -> PREV OPERATOR STACK ENTRY
         ST    R14,EVOSTAKP             SAVE OPERATOR STACK POINTER
         L     R1,EVNSTAKP              -> CURR NUMBER STACK ENTRY
         SH    R1,=H'4'                 -> PREV NUMBER STACK ENTRY
*   R1 IS PASSED TO THE OPERATION ROUTINES
         B     *+4(R15)                 BRANCH ON OPERATOR
         B     EVOSTRT               0  START OF EXPR
         B     EVOLPAR               1  LEFT PAREN
         B     EVORPAR               2  RIGHT PAREN
         B     EVOADD                3  ADD
         B     EVOSUB                4  SUBTRACT
         B     EVOMULT               5  MULTIPLY
* DIVIDE OPERATOR
         OC    4(4,R1),4(R1)         6  ATTEMPT DIVIDE BY ZERO?
         BNZ   EVOD01                   NO, CONTINUE
         LA    R15,EVEMDZ               -> DIVIDE BY ZERO MSG
         B     EVEXIT                   RETURN WITH ERROR
EVOD01   ST    R1,EVNSTAKP              SAVE NUMBER STACK POINTER
         L     R15,0(,R1)               PREVIOUS NUMBER
         SR    R14,R14
         D     R14,4(,R1)               OPERATION
         ST    R15,0(,R1)               SAVE RESULT
         B     EVOLOOP                  GET NEXT OPERATOR
         SPACE 2
* MULTIPLY OPERATOR
EVOMULT  ST    R1,EVNSTAKP              SAVE NUMBER STACK POINTER
         L     R15,0(,R1)               PREVIOUS NUMBER
         M     R14,4(,R1)               OPERATION
         ST    R15,0(,R1)               SAVE RESULT
         B     EVOLOOP                  GET NEXT OPERATOR
         SPACE 2
* SUBTRACT OPERATOR
EVOSUB   ST    R1,EVNSTAKP              SAVE NUMBER STACK POINTER
         L     R15,0(,R1)               PREVIOUS NUMBER
         S     R15,4(,R1)               OPERATION
         ST    R15,0(,R1)               SAVE RESULT
         B     EVOLOOP                  GET NEXT OPERATOR
         SPACE 2
* ADD OPERATOR
EVOADD   ST    R1,EVNSTAKP              SAVE NUMBER STACK POINTER
         L     R15,0(,R1)               PREVIOUS NUMBER
         A     R15,4(,R1)               OPERATION
         ST    R15,0(,R1)               SAVE RESULT
         B     EVOLOOP                  GET NEXT OPERATOR
         SPACE 2
* RIGHT PAREN - THIS SHOULDN'T HAPPEN
EVORPAR  LA    R15,EVEMLE               -> LOGIC ERROR MESSAGE
         B     EVEXIT                   RETURN WITH ERROR
         SPACE 2
* LEFT PAREN - PARENTHESIZED EXPRESSION IS COMPLETE
EVOLPAR  CLI   OPERATOR,EV$RPAR         MATCHED PARENTHESES?
         BE    EVLOOP                   YES, ANNIHILATE OPERATOR
EVNBPAR  LA    R15,EVEMUP               -> UNBAL PAREN MSG
         B     EVEXIT                   RETURN WITH ERROR
         SPACE 2
* START OF STRING - EXPRESSION IS COMPLETE
EVOSTRT  L     R2,4(,R1)                CURRENT RESULT
         L     R1,CHARADR               END OF CHAR SCAN
         STM   R1,R2,12+2*4+R1*4(R13)   STASH R1 AND R2 IN SAVE AREA
         SR    R15,R15                  ASSUME ABSOLUTE EXPR
         TM    EVFLAGS,EVFREL           RELATIVE EXPRESSION?
         BNO   EVEXIT                   NO, RETURN
         LA    R15,4                    INDICATE RELATIVE EXPR
         B     EVEXIT                   RETURN TO CALLER
         SPACE 1
EVEMOS   DC    Y(L'EVETOS)
EVETOS   DC    C'OPERATOR SEQUENCE ERROR'
         SPACE 1
EVEMCE   DC    Y(L'EVETCE)
EVETCE   DC    C'EXPRESSION IS TOO COMPLEX'
         SPACE 1
EVEMDZ   DC    Y(L'EVETDZ)
EVETDZ   DC    C'ATTEMPTED DIVIDE BY ZERO'
         SPACE 1
EVEMLE   DC    Y(L'EVETLE)
EVETLE   DC    C'LOGIC ERROR - CLOSE PAREN IS ON STACK'
         SPACE 1
EVEMUP   DC    Y(L'EVETUP)
EVETUP   DC    C'UNBALANCED PARENTHESES'
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*    LOOK UP SYMBOL IN SYMBOL TABLE                                   *
*                                                                     *
* REGISTER USAGE:                                                     *
*   R0  - WORK                                                        *
*   R1  - INPUT, POINTER TO SYMBOL NAME                               *
*       - OUTPUT, POINTER TO SYMBOL IN TABLE (OR AVAILABLE SLOT)      *
*   R14 - INPUT, RETURN ADDRESS                                       *
*   R15 - OUTPUT, RETURN CODE                                         *
*                  0 - SYMBOL FOUND                                   *
*                  4 - SYMBOL NOT FOUND, SPACE IS AVAILABLE           *
*                  8 - SYMBOL NOT FOUND, TABLE IS FULL                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
SYMLOC   LA    R0,NUMNAME               NUMBER OF TABLE ENTRIES
         LA    R15,NAMES                -> START OF SYMBOL TABLE
SL01     CLI   0(R15),X'00'             EMPTY ENTRY?
         BE    SL02                     YES, RETURN AVAILABLE SLOT
         CLC   0(8,R15),0(R1)           NO, IS IT THE ONE WE WANT?
         BE    SL03                     YES, RETURN IT TO USER
         LA    R15,12(,R15)             -> NEXT TABLE ENTRY
         BCT   R0,SL01                  COUNT AND LOOP
         LA    R15,8                    TABLE IS FULL
         BR    R14                      RETURN TO CALLER
SL02     LR    R1,R15                   PASS BACK ADDRESS OF SLOT
         LA    R15,4                    INDICATE AVAILABLE SLOT
         BR    R14                      RETURN TO CALLER
SL03     LR    R1,R15                   PASS BACK ADDRESS OF SYMBOL
         SR    R15,R15                  INDICATE SYMBOL FOUND
         BR    R14                      RETURN TO CALLER
         EJECT
*---------------------------------------------------------------------*
*                                                                     *
*    GET A TOKEN FROM THE INPUT STRING                                *
*                                                                     *
* REGISTER USAGE:                                                     *
*   R1  - OUTPUT, NUMBER - VALUE OF NUMBER                            *
*                 OPERATOR - VALUE IN BYTE 2, AND                     *
*                            PRIORITY IN BYTE 3                       *
*                 SYMBOL - POINTER TO EIGHT CHARACTER SYMBOL NAME     *
*                 INVTOK - POINTER TO ERROR MESSAGE                   *
*   R2  - WORK                                                        *
*   R14 - INPUT, RETURN ADDRESS                                       *
*   R15 - OUTPUT, RETURN CODE                                         *
*                  0 - NUMBER                                         *
*                  4 - OPERATOR                                       *
*                  8 - SYMBOL                                         *
*                 12 - INVALID TOKEN                                  *
*---------------------------------------------------------------------*
         SPACE 1
GETTOKN  ST    R14,GTLINK               SAVE ROUTINE LINKAGE
         BAL   R14,GETWORD              GET WORD (IF POSSIBLE)
         CLM   R2,B'0001',=X'FF'        INVALID DELIMITER?
         BE    GTINVD                   YES, ERROR
         LTR   R15,R15                  TEST WORD LENGTH
         BM    GTINVW                   TOO LONG, INVALID WORD
         CLM   R2,B'0001',=AL1(EV$DEC)  DECIMAL NUMBER?
         BE    GTDEC                    YES, PROCESS IT
         LTR   R15,R15                  TEST WORD LENGTH
         BNZ   GTWORD                   GOT WORD, PROCESS IT
         CLM   R2,B'0001',=AL1(EV$END)  END OF EVERYTHING?
         BE    GTOEND                   YES, PROCESS IT
         BAL   R14,GETCHAR              CONSUME SYMBOL
         CLM   R2,B'0001',=AL1(EV$CURR) CURRENT VALUE SYMBOL?
         BE    GTOCURR                  YES, PROCESS IT
         STC   R2,OPERATOR              SAVE OPERATOR VALUE
         STC   R2,OPPRIOR               SAVE VALUE FOR TRANS.
         TR    OPPRIOR,GTOPT            TRANSLATE VALUE TO PRIOR.
         B     GTOEXIT                  RETURN OPERATOR TO CALLER
GTOCURR  L     R1,CURRLOC               RETURN CURRENT LOCATION
         B     GTNEXIT                  RETURN WITH NUMBER
GTOEND   MVC   OPERATOR,=AL1(EV$STRT,EVP$STRT)   SET END OPERATOR
*        B     GTOEXIT                  RETURN OPERATOR TO CALLER
GTOEXIT  LH    R1,OPERATOR              RETURN OPERATOR
         LA    R15,4                    INDICATE OPERATOR
         B     GTEXIT                   RETURN TO CALLER
GTDEC    BAL   R14,GETCHAR              CONSUME PERIOD
         BAL   R14,CVDECIN              CONVERT DECIMAL FOR INPUT
         LTR   R1,R15                   TEST RESULT
         BNM   GTNEXIT                  RETURN NUMBER TO CALLER
         LA    R1,GTEMID                -> INVALID DECIMAL MSG
         B     GTEEXIT                  RETURN ERROR TO CALLER
GTWORD   BAL   R14,CVHEXIN              ASSUME IT IS HEX
         LTR   R15,R15                  DID IT WORK?
         BZ    GTNEXIT                  YES, RETURN NUMBER TO CALLER
         CLI   WORD,C'0'                NO, LEADING NUMERIC?
         BNE   GTSEXIT                  NO, RETURN SYMBOL TO CALLER
         LA    R1,GTEMIH                -> INVALID HEX NUMBER MSG
         B     GTEEXIT                  RETURN ERROR TO CALLER
GTINVW   LA    R1,GTEMTL                -> WORD TOO LONG MSG
         B     GTEEXIT                  RETURN ERROR TO CALLER
GTINVD   LA    R1,GTEMIC                -> INVALID CHAR MSG
*        B     GTEEXIT                  RETURN ERROR TO CALLER
GTEEXIT  LA    R15,12                   INDICATE ERROR RETURN
         B     GTEXIT                   RETURN TO CALLER
GTNEXIT  SR    R15,R15                  INDICATE NUMBER RETURN
         B     GTEXIT                   RETURN TO CALLER
GTSEXIT  LA    R1,WORD                  -> SYMBOL NAME
         LA    R15,8                    INDICATE SYMBOL RETURN
*        B     GTEXIT                   RETURN TO CALLER
GTEXIT   L     R14,GTLINK               RESTORE ROUTINE LINKAGE
         BR    R14                      RETURN TO CALLER
         SPACE 2
* OPERATOR VALUES FOR STACK
EV$STRT  EQU   0                        EXPR START OR END
EV$LPAR  EQU   1                        LEFT PAREN
EV$RPAR  EQU   2                        RIGHT PAREN
EV$ADD   EQU   3                        ADD
EV$SUB   EQU   4                        SUBTRACT
EV$MULT  EQU   5                        MULTIPLY
EV$DIV   EQU   6                        DIVIDE
* SPECIAL OPERATOR VALUES FOR 'GETTOKN'
EV$CURR  EQU   7                        CURRENT ADDRESS
EV$DEC   EQU   8                        DECIMAL NUMBER
EV$END   EQU   9                        BLANK CHARACTER
* OPERATOR PRIORITIES FOR STACK
EVP$STRT EQU   0                        EXPR START AND END
EVP$PAR  EQU   1                        PARENTHESES
EVP$ADD  EQU   2                        ADD AND SUBTRACT
EVP$MUL  EQU   3                        MULTIPLY AND DIVIDE
         SPACE 2
* OPERATOR VALUE VS. OPERATOR PRIORITY TRANSLATION TABLE
GTOPT    DC    7X'00'
         ORG   GTOPT+EV$LPAR
         DC    AL1(EVP$PAR)
         ORG   GTOPT+EV$RPAR
         DC    AL1(EVP$PAR)
         ORG   GTOPT+EV$ADD
         DC    AL1(EVP$ADD)
         ORG   GTOPT+EV$SUB
         DC    AL1(EVP$ADD)
         ORG   GTOPT+EV$MULT
         DC    AL1(EVP$MUL)
         ORG   GTOPT+EV$DIV
         DC    AL1(EVP$MUL)
         ORG   ,
         SPACE 2
GTEMID   DC    Y(L'GTETID)
GTETID   DC    C'INVALID DECIMAL NUMBER'
         SPACE 1
GTEMIH   DC    Y(L'GTETIH)
GTETIH   DC    C'INVALID HEX NUMBER'
         SPACE 1
GTEMTL   DC    Y(L'GTETTL)
GTETTL   DC    C'SYMBOL OR NUMBER IS TOO LONG'
         SPACE 1
GTEMIC   DC    Y(L'GTETIC)
GTETIC   DC    C'INVALID CHARACTER'
         EJECT
**************************************************************
* STEVE MCGINTY HANDY-DANDY SCANNER ROUTINES (V19M5R2 OR SO) *
**************************************************************
         SPACE 1
* INITIALIZE SCANNER WORK AREAS
* REG 'R1' POINTS AT BEGINNING OF SOURCE STRING
* REG 'R0' CONTAINS LENGTH OF SOURCE STRING
INITCHAR BCTR  R1,0                     INITIALIZE ADDRESSES
         ST    R1,CHARADR               SAVE STRING START (-1)
         AR    R1,R0                    POINT TO END OF STRING
         ST    R1,CHARLIM               SAVE STRING END
*              NOW WE FALL INTO THE GETCHAR ROUTINE TO GET THE
*              FIRST CHARACTER.
         SPACE 1
* GET THE NEXT CHARACTER FROM THE SOURCE STRING INTO 'CHAR'
* THE CONDITION CODE WILL BE ZERO IF SUCCESSFUL, NONZERO IF NOT
GETCHAR  MVI   CHAR,C' '                SET INITIAL VALUE
         L     R1,CHARADR               POINT TO PREV CHAR
         C     R1,CHARLIM               ARE WE TOO FAR?
         BHR   R14                      YES, RETURN (NONZERO CC)
         LA    R1,1(,R1)                POINT TO NEXT CHAR
         ST    R1,CHARADR               SAVE NEW POINTER
         C     R1,CHARLIM               ARE WE TOO FAR?
         BHR   R14                      YES, RETURN (NONZERO CC)
         OC    CHAR,0(R1)               GET CHARACTER (UPPER CASE)
         CR    0,0                      SET COND CODE ZERO
         BR    R14                      EXIT FOR NOW
         SPACE 2
* SKIP PAST CONSECUTIVE BLANKS IN THE SOURCE STRING.
SPANSPA  ST    R14,SSLINK               SAVE THE LINK REGISTER
SS01     CLI   CHAR,C' '                ARE WE AT A SPACE?
         BNE   SS03                     NO, EXIT
         BAL   R14,GETCHAR              GET THE NEXT CHARACTER
         BZ    SS01                     GOT IT, SO GO TEST IT.
SS03     L     R14,SSLINK               RESTORE THE LINK REGISTER
         BR    R14                      GO BACK TO MOMMA
         SPACE 2
* GET A WORD (8 CHAR MAX) FROM THE SOURCE STRING.
* HALFWORD 'WORDLEN' AND REG 'R15' WILL CONTAIN THE LENGTH, AND
* 'WORD' WILL CONTAIN THE WORD (RIGHT FILLED WITH BLANKS).
* IF THE SOURCE WORD WAS LONGER THAN 8 CHARACTERS, 'R15' WILL
* BE COMPLEMENTED, AND 'WORD' WILL CONTAIN THE TRUNCATED WORD.
* THE CONDITION CODE ALWAYS REFLECTS THE STATE OF 'R15'.
* REGISTER USAGE:
*   R1  - WORK
*   R2  - OUTPUT, TRT VALUE FOR DELIMITER
*   R14 - INPUT, RETURN ADDRESS
*   R15 - OUTPUT, WORD LENGTH (COMPLEMENTED IF OVER 8)
GETWORD  ST    R14,GWLINK               SAVE LINK REGISTER
         BAL   R14,SPANSPA              SKIP LEADING SPACES
         MVC   WORD,=8C' '              INITIALIZE THE WORD
         MVI   CHAR,C' '                INITIALIZE CHARACTER
         L     R15,CHARLIM              -> END OF INPUT STRING
         LA    R1,1(,R15)               INITIALIZE FOR TRT
         L     R14,CHARADR              -> INPUT STRING
         SR    R2,R2                    INITIALIZE FOR TRT
         SR    R15,R14                  LENGTH FOR TRT
         BM    GW01                     NO DATA
         EX    R15,GWTRT                TRT 0(0,R14),GWTBL
         BZ    GW01                     NO DELIMITER BEFORE END
         MVC   CHAR,0(R1)               SAVE DELIMITER CHARACTER
GW01     ST    R1,CHARADR               SAVE DELIMITER ADDRESS
         LR    R15,R1                   -> DELIMITER
         SR    R15,R14                  WORD LENGTH
         STH   R15,WORDLEN              SAVE WORD LENGTH
         BNP   GW03                     NO DATA
         CH    R15,=H'8'                HOW MUCH DATA?
         BNH   GW02                     NOT TOO MUCH
         LH    R15,=H'8'                TOO MUCH, TRUNCATE IT
GW02     BCTR  R15,0                    MINUS 1 FOR EX
         EX    R15,GWMVC                MVC WORD(0),0(R14)
GW03     L     R14,GWLINK               RESTORE LINK REGISTER
         LH    R15,WORDLEN              GET LENGTH
         CH    R15,=H'8'                HOW'ED WE DO?
         BH    GW04                     NOT SO HOT
         LTR   R15,R15                  SET CONDITION CODE
         BR    R14                      EXIT TO CALLER
GW04     LCR   R15,R15                  COMPLEMENT LENGTH
*                                       (AND SET COND CODE)
         BR    R14                      EXIT
         SPACE 2
GWTRT    TRT   0(0,R14),GWTBL           << EXECUTED >>
GWMVC    MVC   WORD(0),0(R14)           << EXECUTED >>
         SPACE 2
GWTBL    DC    256X'FF'
         ORG   GWTBL+C' '
         DC    AL1(EV$END)
         ORG   GWTBL+C'('
         DC    AL1(EV$LPAR)
         ORG   GWTBL+C')'
         DC    AL1(EV$RPAR)
         ORG   GWTBL+C'+'
         DC    AL1(EV$ADD)
         ORG   GWTBL+C'-'
         DC    AL1(EV$SUB)
         ORG   GWTBL+C'*'
         DC    AL1(EV$MULT)
         ORG   GWTBL+C'/'
         DC    AL1(EV$DIV)
         ORG   GWTBL+C'$'
         DC    AL1(EV$CURR)
         ORG   GWTBL+C'.'
         DC    AL1(EV$DEC)
         ORG   GWTBL+C'A'
         DC    9X'00'
         ORG   GWTBL+C'J'
         DC    9X'00'
         ORG   GWTBL+C'S'
         DC    8X'00'
         ORG   GWTBL+C'0'
         DC    10X'00'
         ORG   ,
         SPACE 2
* TEST FOR ALL NUMERIC WORD AND LEAVE THE VALUE IN REG 'R15'.
* IF IT IS NOT NUMERIC, THEN LEAVE -1.  THE CONDITION CODE WILL
* REFLECT THE STATE OF 'R15'.
CVDECIN  LH    R15,WORDLEN
         LTR   R15,R15
         BZ    CDIBAD
         LA    R1,WORD
         CH    R15,=H'8'
         BH    CDIBAD
CDI01    CLI   0(R1),C'0'
         BL    CDIBAD
         CLI   0(R1),C'9'
         BH    CDIBAD
         LA    R1,1(,R1)
         BCT   R15,CDI01
         LH    R15,WORDLEN
         BCTR  R15,0
         EX    R15,CDIPACK
         CVB   R15,EVTEMP
         LTR   R15,R15
         BR    R14
CDIPACK  PACK  EVTEMP(8),WORD(0)        << EXECUTED >>
CDIBAD   LA    R15,1
         LCR   R15,R15
         BR    R14
         SPACE 2
* CONVERT HEXADECIMAL INPUT IN WORD INTO INTERNAL REPRESENTATION.
* REGISTER USAGE:
*   R1  - WORK
*       - OUTPUT, RESULT OF CONVERSION
*   R14 - INPUT, RETURN ADDRESS
*   R15 - OUTPUT, RETURN CODE
*                  0 - SUCCESS
*                  8 - FAILURE, INVALID HEX
CVHEXIN  LH    R1,WORDLEN               LENGTH OF HEX DATA
         LTR   R1,R1                    IS THERE ANY?
         BNP   CHIINV                   NO, TOO BAD
         CH    R1,=H'8'                 IS THERE TOO MUCH?
         BH    CHIINV                   YES, ALSO TOO BAD
         L     R15,=A(NUMTAB)           -> TRANSLATE TABLE
         BCTR  R1,0                     LENGTH MINUS ONE FOR EX
         EX    R1,CHITRT                TRT WORD(0),NUMTAB
         BNZ   CHIINV                   NOT VALID HEX
         MVC   EVTEMP,=8C'0'            INITIALIZE WORK AREA
         LA    R15,EVTEMP+7             -> END OF DEST
         SR    R15,R1                   -> DESTINATION
         EX    R1,CHIMVC                MVC 0(0,R15),WORD
         L     R15,=A(HEXTAB)           -> TRANSLATE TABLE
         TR    EVTEMP(8),0(R15)         CONVERT TO PSEUDO-HEX
         PACK  EVTEMP+8(4+1),EVTEMP(8+1)  PACK INTO REAL HEX
         L     R1,EVTEMP+8              RETURN NUMBER TO CALLER
         SR    R15,R15                  INDICATE GOOD NUMBER
         BR    R14                      RETURN TO CALLER
CHIINV   LA    R15,8                    INDICATE FAILURE
         BR    R14                      RETURN TO CALLER
         SPACE 1
CHITRT   TRT   WORD(0),0(R15)           << EXECUTED >>
CHIMVC   MVC   0(0,R15),WORD            << EXECUTED >>
         SPACE 2
         DROPX R10,R11                  DROP BASE REGISTERS
         SPACE 2
         LTORG
         EJECT
COREHELP CSECT                                                     U043
         DC    F'3'                     NUMBER OF HELP SCREENS     U043
         DC    A(HELP001,HELP001L)      ADDRESS, LENGTH            U043
         DC    A(HELP002,HELP002L)      ADDRESS, LENGTH            U043
         DC    A(HELP003,HELP003L)      ADDRESS, LENGTH            U043
*  FIRST LINE OF EACH SCREEN IS TITLE AND GOES ON "MSGLINE"        U043
         SPACE 2
HELP001  DC    CL79'                        *********** COMMANDS ******$
               ****'                                               U025
         DC    CL79' '                                         1   U025
         DC    CL79'B<name>  - Display current location as control bloc$
               k <name>'                                       2   U025
         DC    CL79'B        - Turn off control block mapping for this $
               location this time'                             3   U025
         DC    CL79'I<exp>   - Indirect from location <exp>  IS<exp> - $
               Indirect Short (24 bit)'                        4   U066
         DC    CL79'L<name>  - Link using <name> field from current con$
               trol block mapping'                             5   U025
         DC    CL79',<exp>   - Display location <exp>'         6   U025
         DC    CL40'HISTORY  - Display history table        '  7   U057
         DC    CL39'                                       '       U058
         DC    CL40'=<name>  - Define symbol                '  8   U025
         DC    CL39'UCB<cuu> - Display UCB for device <cuu>'       U025
         DC    CL40'=        - Display symbol table         '  9   U025
         DC    CL39'MODE1    - Set mode to disp+0          '       U023
         DC    CL40'>        - Forward in history table     '  10  U025
         DC    CL39'MODE2    - Set mode to disp-64         '       U023
         DC    CL40'<        - Backward in history table    '  11  U025
         DC    CL39'?2       - Display more commands       '       U025
         DC    CL40'D        - Scroll down (+x''100'')      '  12  U025
         DC    CL39'?3       - Display pf key definitions  '       U025
         DC    CL40'U        - Scroll up   (-x''100'')      '  13  U025
         DC    CL39'*        - Re-prompt previous input cmd'       U036
         DC    CL40'M<name>  - Find module in LPA/MLPA      '  14  U069
         DC    CL39'SVC<exp> - Find SVC routine            '       U057
         DC    CL40'LPA<name> - Find module in LPA          ' 15   U069
         DC    CL39'SSCVT    - Locate subsys control block '       U058
         DC    CL40'END      - Exit from this processor    '   16  U025
         DC    CL39'                                       '       U069
HELP001L EQU   *-HELP001                                           U025
         SPACE 2
HELP002  DC    CL79'                  *********** COMMANDS (continued) $
               **********                 '                        U033
         DC    CL79' '                  1                          U025
         DC    CL79'PAGEFIX  - Page fix current location until LOGOFF  $
                      (privileged command)'  2                     U025
         DC    CL79'PAGEFREE - Un-fix page at current location         $
                      (privileged command)'  3                     U025
         DC    CL79'Z        - Set/reset keyzero                       $
                      (privileged command)'  4                     U025
         DC    CL79'S<data>  - Store <data> at current loc'  5     U025
         DC    CL79'X<data>  - Exclusive or at current loc'  6     U025
         DC    CL79'O<data>  - "Or"  <data> at current loc'  7     U025
         DC    CL79'N<data>  - "And" <data> at current loc'  8     U025
         DC    CL79'F<data>  - Search for <data>'            9     U032
         DC    CL79'--- <data> is a character string enclosed in delimi$
               ters, or a hex string    ---'  10                   U025
         DC    CL79'--- Examples:  N7f     turn off high order bit in b$
               yte at current location  ---'  11                   U025
         DC    CL79'---     S/hi there/    store ''hi there'' at curren$
               t location                 ---'  12                 U025
         DC    CL79'--- Note:  Searches and alters are done in current $
               key (set by "Z" command) ---'  13                   U032
         DC    CL79'--- Note:  An operand of "*" for cmds S,X,O,N,F wil$
               l re-prompt the last oprnd -'  14                   U032
HELP002L EQU   *-HELP002                                           U025
         SPACE 2
HELP003  DC    CL79'                        ****** PF KEY DEFINITIONS *$
               *****'                                              U025
         DC    CL79' '                                             U025
         DC    CL79'PF1 / PF13   -  Help (this screen)'            U025
         DC    CL79'PF2 / PF14   -  Start auto updating'           U025
         DC    CL79'PF3 / PF15   -  End (exit from processor)'     U025
         DC    CL79'PF4 / PF16   -  (unused)'                      U025
         DC    CL79'PF5 / PF17   -  Repeat last find command'      U025
         DC    CL79'PF6 / PF18   -  Repeat last input line'        U025
         DC    CL79'PF7 / PF19   -  Scroll up or go to previous control$
                block segment              '                       U025
         DC    CL79'PF8 / PF20   -  Scroll down or go to next control b$
               lock segment                '                       U025
         DC    CL79'PF9 / PF21   -  (unused)'                      U025
         DC    CL79'PF10/ PF22   -  Back thru history'             U025
         DC    CL79'PF11/ PF23   -  Forward thru history'          U025
         DC    CL79'PF12/ PF24   -  End (exit from processor)'     U025
         DC    CL79' '                                             U025
         DC    CL79'PA1 / ATTN   -  Stop auto updating'            U025
HELP003L EQU   *-HELP003                                           U025
         EJECT
WORKAREA DSECT
CLRSTART EQU   *                                                   U011
OLDPIE   DS    A                        ADDRESS OF PREVIOUS PIE    U006
LOGOOD   DS    A                        LOWEST ADDRESS FETCHED     U026
HIGOOD   DS    A                        HIGHEST ADDRESS FETCHED    U026
CURRLOC  DS    A                        CURRENT DISPLAY LOCATION   U040
CELINK   DS    A                        LINKAGE SAVED BY CALLEVAL  U057
FIXECB   DS    F                        ECB FOR PGFIX              U020
ALTERPTR DS    A                        -> ALTER INSTRUCTION       U017
FINDCNT1 DS    F                        BYTES SCANNED              U032
FINDCNT2 DS    F                        K SKIPPED                  U032
         SPACE 1
ALTERLEN DS    H                        LENGTH OF DATA TO STORE    U048
ALTERDLM DS    C                        DELIMITER FOR ALTER        U048
ALTRDATA DS    CL76,C                   DATA TO STORE              U048
         SPACE 1
FINDLEN  DS    H                        LENGTH OF DATA TO FIND     U048
FINDDLM  DS    C                        DELIMITER FOR FIND         U048
FINDDATA DS    CL76,C                   DATA TO FIND               U048
         SPACE 1
STRWORK  DSECT                                                     U048
STRLEN   DS    H                        LENGTH OF DATA             U048
STRDLM   DS    C                        DELIMITER                  U048
STRDATA  DS    CL76,C                   DATA                       U048
         SPACE 1
WORKAREA DSECT                          RESUME                     U032
         DS    0D                       ALIGNMENT                  U046
DUMPDATA DS    XL256                    DATA RETURNED BY GETSTOR
TRTAB    DS    XL256                    TRT TABLE TO WORK WITH     U032
HEXWORK  DS    CL32,C                                              U021
KEYSAVE  DS    X                        "GOFETCH" SAVES KEY HERE   U047
KEYSAVE1 DS    X                        KEY FOR FIRST PART         U047
KEYSAVE2 DS    X                        KEY FOR SECOND PART        U047
CURRENT  DS    H                        HIST TABLE OFFSET
ASID     DS    H                        OTHER ASID, OR ZERO        U045
ASIDPREV DS    H                        PREVIOUS ASID FOR UNBIND   U061
OLDSASID DS    H                        PREVIOUS SECONDARY ASID    U061
OLDAXVAL DS    H                        PREVIOUS AUTH INDEX        U061
MYKEY    DS    X                        my normal execution key    U068
         DS    X                        currently wasted           U068
         DS    0F                       ALIGNMENT FOR 'OPERAND'    U061
OPERAND  DS    CL78                     OPERAND OF THIS COMMAND    U048
JOBNAME  DS    CL8                      OTHER JOBNAME              U045
ADDRASCB DS    A                        ADDR OF OTHER ASCB         U045
         SPACE 1
IOAREA   EQU   *
         DSGEN SCR1
IOLEN    EQU   *-IOAREA
         ORG   HEADER+79-28                                        U046
PGMCKMSG DS    CL28                                                U046
         ORG   ,                                                   U046
TEMP1    DS    D,C                      CONVERSION WORK AREA       U021
TEMP2    DS    CL9                      CONVERSION WORK AREA
         SPACE 1
FLAGS    DS    X                        ASSORTED FLAGS
INDFLAG  EQU   X'80'                    INDIRECT IN PROGRESS
AUTOFLAG EQU   X'40'                    WE ARE AUTO UPDATING       U007
AUTHAPF  EQU   X'20'                    WE ARE APF AUTHORIZED      U008
AUTHUSER EQU   X'10'                    THIS USER IS A GOOD GUY    U008
DISPKEY  EQU   X'08'                    FORMATTING FOR DISPLAY     U054
AXSAVED  EQU   X'04'                    OLD AX SAVED IN OLDAXVAL   U061
INDFLAGS EQU   X'02'                    Short Indirect             U066
PGMCHKOK EQU   X'01'                    PGM CHECKS ARE ALLOWED     U041
         SPACE 1
MODEFLAG DS    X                        USER OPTION FLAGS
MIDDUMP  EQU   X'80'                    ON TO FORCE DISP BACK 4 LINES
KEYZERO  EQU   X'08'                    FETCH SHOULD BE DONE KEY0  U008
         SPACE 1
          AIF   (&@@SPLVL LT 200).XA94                             U071
IOSPARM  DS    3A                       parmlist for IOSVSUCB      U071
IOSANSW  DS    A                        answer area                U071
         DS    0D                       alignment                  U071
IOSWORK  DS    CL100                    work area for IOSVSUCB     U071
.XA94     ANOP                                                     U071
         EJECT
IOCB     IOCB
         EJECT ,
HISTORY  DS    (NUMHIST*8/4)F           HISTORY TABLE              U044
         DS    X                        END OF LIST INDICATOR      U044
         DS    0D                       ALIGN THE FOLLOWING        U044
NAMES    DS    (NUMNAME*12/4)F          SYMBOL TABLE               U044
         SPACE 1
CLRLEN   EQU   *-CLRSTART               LENGTH TO CLEAR            U044
*
*  ROUTINE "EVAL" WORK AREA
*
EVALWK   DC    0D'0'
EVTEMP   DC    2D'0'                    CONVERSION WORK AREA
WORD     DC    CL8' '
GTLINK   DC    A(0)                     TOKEN ROUTINE RETURN ADDRESS
SSLINK   DC    A(0)
GWLINK   DC    A(0)
CHARADR  DC    A(0)
CHARLIM  DC    A(0)
         SPACE 1
EVNSTAKP DC    A(0)                     EVNSTACK POINTER
EVNSTAK  DC    10F'0'                   EVAL NUMBER STACK
EVNSTAKE EQU   *-4                      EVNSTAK LIMIT
         SPACE 1
EVOSTAKP DC    A(0)                     EVOSTACK POINTER
EVOSTAK  DC    10H'0'                   EVAL OPERATOR STACK
EVOSTAKE EQU   *-2                      EVOSTAK LIMIT
         SPACE 1
OPERATOR DC    H'0'                     OPERATOR AND PRIORITY
OPPRIOR  EQU   OPERATOR+1,1             PRIORITY
WORDLEN  DC    H'0'
EVERRWK  DC    Y(0),CL40' '             ERROR MESSAGE BUILD AREA
CHAR     DC    C' '
         SPACE 1
EVFLAGS  DC    X'00'                    EVAL ROUTINE FLAGS
EVFINIT  EQU   X'80'                    FIRST TOKEN IN STRING
EVFNUM   EQU   X'40'                    PREV TOKEN NUMERIC
EVFREL   EQU   X'20'                    EXPR IS RELATIVE
         SPACE 2
WORKLEN  EQU   *-WORKAREA
         SPACE 5
*---------------------------------------------------------------------*
*
* CBS  -- CONTROL BLOCK SEGMENT
*
*---------------------------------------------------------------------*
         SPACE 2
CBS      DSECT
CBSPREV  DS    A                        ADDR OF PREVIOUS SEGMENT
CBSNEXT  DS    A                        ADDR OF NEXT SEGMENT
CBSFIRST DS    H                        OFFSET OF FIRST DATA TO FETCH
CBSSIZE  DS    H                        LENGTH OF DATA TO FETCH
CBSNAME  DS    CL8                      SEGMENT IDENTIFIER
* THE FOLLOWING FIELDS DESCRIBE A FORMAT ITEM
CBSTYPE  DS    H                        FORMAT TYPE
*                                         4=HEX
*                                         8=LABEL
*                                        12=CHAR
CBSLEN   DS    H                        SCREEN LENGTH OF ITEM AS REQ
*                                       BY FORMAT ROUTINE
CBSOFF   DS    H                        SCREEN OFFSET OF ITEM
CBSDATA  DS    H                        DATA OFFSET FROM START OF THIS
*                                       SEGMENTS DATA AREA OR A
*                                       VARIABLE LENGTH CONSTANT
         EJECT
         IKJPSCB
         EJECT
         IHALPDE ,                                                 U033
         SPACE 1
LPDELEN  EQU   *-LPDE                   LENGTH OF LPDE             U033
         EJECT
         IHACDE ,                                                  U037
         EJECT
         IHAXTLST ,                                                U037
         SPACE 2
          AIF   (&@@SPLVL LT 200).XA92                             U063
         IHAEPIE  ,                                                U063
.XA92     ANOP                                                     U063
         SPACE 2
         PRINT NOGEN
         SPACE 2
         IHAPSA  ,                                                 U070
         SPACE 2
         CVT   DSECT=YES                                           U070
         SPACE 2
         IHAASCB  ,                                                U045
         SPACE 2
         IRAOUCB  ,                                                U059
         SPACE 2
         IKJTCB   ,                                                U070
         SPACE 2
         END
