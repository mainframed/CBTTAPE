//AIMS31B4 JOB 'IMSGROUP  SH28205610*110A044         999  3000007037',
//  REGION=999K,TIME=5,MSGLEVEL=1,MSGCLASS=H,NOTIFY=$A1016
//*MAIN ORG=LOCAL,CLASS=L5
//NARBGN40  EXEC IMSUPDTE,MEMBER=NARBGN40,
//             P=IMA,LOAD='PVT.MODS',DISPR=SHR,
//  SYMI=SOURCETM,SYMO=SOURCETM,RECORD=NO,ACCT=1,SOUT='*'
//C.SYSIN  DD *
         TITLE 'LOCAL ROCKWELL INITIALIZATION MODULE   N A R B G N 4 0'
NARBGN40 CSECT
         PROLOGUE SAVE=*PRECHN
         REQUATE
         CEQUATE
         PRINT   GEN
R#BALC   EQU   R0
B#S      EQU   R11              *** IDENTITY FROM CALLER. **
R#W      EQU   R15
**********************************************************************
 SPACE 3
         MVC   PARMSIN,0(R1)        COPY INPUT PARMS.
         ST    B#S,ADDRSCD          STO IBM'S SCD PTR.
 SPACE 3
***********************************************************
         L       R#W,ADDRSCD
         NARSCDF R#W,NARSCFRX,KNOWN=SCD   FIND THE RESTART FLAGS.
         MVC   RSTFLAGS,0(R#W)            SAVE THEM LOCALLY.
**********************************************************************
   TITLE 'N A R B G N 4 0  ---  INITIALIZE SOME FLAGS AND FEATURES'
**********************************************************************
**  THIS CODE IS TO READ-IN THE FLAGA STANDARD VALUES.
NARBGN40 CSECT
*
*
         USING NARBGN40,R12
         PRINT NOGEN
A#A      EQU   R2
A#B      EQU   R3
A#C      EQU   R4
         RDJFCB DCBPO
         MVC   BLDLMBR1(8),JFCB00+44
         NIF               JFCB00+86,X'01'   NO MBR PRESENT.
         OI                JFCB00+52,X'08'   NO WRITEBACK.
         MVC               JFCB00+44(8),=CL8' '
         OPEN  (DCBPO,INPUT),TYPE=J
         TM     DCBPO+48,X'10'
         BZ    SKIPRJ
         BLDL   DCBPO,BLDLIST1
         LTR   R15,R15
         BNZ   SKIPRK
         CLOSE  DCBPO
         MVC               JFCB00+44(8),BLDLMBR1 MBR NAME.
         OI                JFCB00+86,X'01'       MBR IS PRESENT.
         OPEN  (DCBPS,INPUT)
         TM    DCBPS+48,X'10'
         BZ    SKIPRL
         GET   DCBPS,GETFLAGA
         L       A#A,ADDRSCD
         NARSCDF A#A,NARSCFAX,KNOWN=SCD FIND WHERE TO MOVE THE DATA.
         MVC   0(16,A#A),GETFLAGA                     MOVE THE DATA.
******************************
**  STORE THE FLAG $A VALUES INTO ALL PSTS.
         L       A#B,ADDRSCD
         NARSCDF A#B,NARPSTSL,KNOWN=SCD PTR TO TABLE OF PST ADCONPAIRS.
         S     A#B,=F'8'                            ( PRE - DECRMT )
*        ***********
LOOP12   LA    A#B,8(A#B)               PTR TO 1ST/NEXT     ADCONPAIR.
         L     A#C,0(A#B)                               AUX PST.
         LTR   A#C,A#C                  Q. IS IT REAL & PRESENT.
         BNP   P12XX                         NO, EXIT THE LOOP.
         L     A#C,NARPSTW0-NARPST#0+12(A#C) YES, PTR TO 4TH WORK AREA.
         MVC   NARCFLGA-NARCMOV4(16,A#C),0(A#A)  STO ORIG FLAGS IN WA4.
         B     LOOP12                            LOOP FOR NEXT PST.
*        ***********
P12XX    EQU   *
********************
         AP    SKIPR,=P'1'
SKIPRJ   AP    SKIPR,=P'1'
SKIPRK   AP    SKIPR,=P'1'
SKIPRL   AP    SKIPR,=P'1'
         CLOSE  DCBPO
         CLOSE  DCBPS
****************************************
         B     ENDPRT1A
**************************************************
PSEUDOBF DC    Y(2+2+72,0) THESE FIELDS MUST REMAIN IN THIS ORDER 1.
GETFLAGA DC    XL80'0'     THESE FIELDS MUST REMAIN IN THIS ORDER 2.
SKIPR    DC    PL4'0'
DCBPO    DCB   DDNAME=NARFLAGA,DSORG=PO,EXLST=EXLST,MACRF=R
DCBPS    DCB   DDNAME=NARFLAGA,DSORG=PS,EXLST=EXLST,MACRF=GM
EXLST    DC    0A(0),XL1'87',AL3(JFCB00)
JFCB00   DC    XL176'0'
BLDLIST1 DC    H'1',H'076'
BLDLMBR1 DC    CL8' ',XL76'0'
RSTFLAGS DC    CL16' '
RSTFLAGA EQU   RSTFLAGS+0
RSTFLAGB EQU   RSTFLAGS+1
RSTFLAGC EQU   RSTFLAGS+2
RSTFLAGD EQU   RSTFLAGS+3
ADDRSCD  DC    A(0)
PARMSIN  DC    XL16'0'
         LTORG
**************************************************
ENDPRT1A #
         DROP
***********************************************************
   EJECT
***********************************************************
         USING NARBGN40,R12
         RDJFCB LDCBPO
         MVC   BLDLMBR1(8),JFCB00+44
         NIF               JFCB00+86,X'01'   NO MBR PRESENT.
         OI                JFCB00+52,X'08'   NO WRITEBACK.
         MVC               JFCB00+44(8),=CL8' '
         OPEN  (LDCBPO,INPUT),TYPE=J
         TM     LDCBPO+48,X'10'
         BZ    LSKIPRJ
         BLDL   LDCBPO,BLDLIST1
         LTR   R15,R15
         BNZ   LSKIPRK
         CLOSE  LDCBPO
         MVC               JFCB00+44(8),BLDLMBR1 MBR NAME.
         OI                JFCB00+86,X'01'       MBR IS PRESENT.
         OPEN  (LDCBPS,INPUT)
         TM    LDCBPS+48,X'10'
         BZ    LSKIPRL
         L     A#A,ADDRSCD
LGET     GET   LDCBPS,GETFLAGA
         CLC   =C'/LOG',GETFLAGA
         BNE   LGET
         CALL  NARAOUF0,((A#A),LUEHPSUD)
         B     LGET
LSKIPRJ  AP    LSKIPR,=P'1'
LSKIPRK  AP    LSKIPR,=P'1'
LSKIPRL  AP    LSKIPR,=P'1'
LEODAD   #
         CLOSE  LDCBPO
         CLOSE  LDCBPS
         B     ENDPRT1L
*******************************
LSKIPR   DC    PL4'0'
LDCBPO   DCB   DDNAME=NARLINIT,DSORG=PO,EXLST=EXLST,MACRF=R
LDCBPS   DCB   DDNAME=NARLINIT,DSORG=PS,EXLST=EXLST,MACRF=GM,          *
               EODAD=LEODAD
         LTORG
LUEHPSUD DC    A(0)             0  PSEUDO
         DC    A(0)             4    UEHB
         DC    A(0)             8      CONTROL
         DC    A(PSEUDOBF)      C        BLOCK
         DC  50A(0)               ZEROES
*******************************
ENDPRT1L #
***********************************************************
   EJECT
***********************************************************
**  FIND A SUB-TASK PORT  AND  ATTACH THE  1-MINUTE TIMER ROUTINE.
NARBGN40 CSECT
*
PART2    #
*
         USING NARBGN40,R12
B#A      EQU   R2
B#B      EQU   R3
B#C      EQU   R4
B#D      EQU   R5
B#E      EQU   R6
B#F      EQU   R8
         TM    RSTFLAGB,X'02' Q. SHALL WE DO THE ATTACH.
         BZ    ENDPART2         NO, SKIP.
         NARSCDF B#A,NARSUBTX           PTR TO TOP OF LIST.
*                                     = PTR TO PTR TO NEXT AVAIL ENTRY.
         L     B#B,0(B#A)                      PTR TO NEXT AVAIL ENTRY.
         CLI   0(B#B),X'FF'        Q. IS IT TRULY AVAILABLE.
         BALC  EQ,INVALID            NO, PITY.
         LA    R#W,4(B#B)            YES, CALC NEW NEXT
         ST    R#W,0(B#A)                 STO  FOR NEXT USER.
         USING NARSUBTC,B#B        ESTABLISH ADDRESSABILITY.
         L     B#B,0(B#B)          BASE FOR ADCONS FOR THIS SUBTASK.
         LA    B#F,NARSUBE2   PTR TO ECB ON WHICH S/TASK IS TO WAIT.
         XC    NARSUBE1(8),NARSUBE1
         STM   R0,R12,SAVESUB+20   SAVE REGS.
         LA    B#C,SUBTCBB         PTR TO WHERE TO EXECUTE AS A SUBTSK.
*
         LA    B#D,NARSUBE1        PTR TO ECB FOR OS TO POST AT TERM.
         LA    B#E,RSTFLAGS
         TM    RSTFLAGB,X'04' Q. SHALL WE OFFER AN ECB TO DAUGHTER.
         BZ    P22J             YES, LET IT STAND.
         XR    B#D,B#D ZERO-OUT ECB PTR 'CAUSE OS/VS MAY  S/102.
P22J     EQU   *
*        ***
         ATTACH EP=SUBTASK1,ECB=(B#D),SZERO=NO,STAI=(STAIXB,STAIPB),   *
               PARAM=((B#C),SAVESUB)
         ST   R1,NARSUBTC          STO TCB PTR.
         CHAP +1,NARSUBTC
         L     R1,NARSUBTC
         L     B#E,112(R1)         GET FSA ADDR.
         ST    B#E,NARSUBSA        STO FSA ADDR.
         CHAP -2,NARSUBTC
         MVI      NARSUBTC,X'01'   SHOW THAT WE ARE USING THIS ONE.
         B     ENDPART2
***************************************
 SPACE 3
****************************************
SAVESUB  DC    18A(0)
         LTORG
         DROP
*************************************************
 SPACE 3
*************************************************
         USING *,R15               HERE WE ARE.
STAIXB   DC    0H'0'
         AP    APQ,=P'1'           COUNT TRIPS.
         XR    R15,R15             CLEAR.
         BR    R14                 EXIT THE STAE EXIT.
STAIPB   DC    A(0)
APQ      DC    PL4'0'
         LTORG
         DROP  R15
***************************************
 SPACE 3
***************************************
         USING NARBGN40,R12
SUBTCBB  DC    0H'0'
         L     R0,SUBTNAME
         STIMER REAL,(0),BINTVL=BINTVL1
         WAIT  ECB=(B#F)
         LA    R15,259
         BR    R10
***********************
         WXTRN   NARMINUT
SUBTNAME DC    V(NARMINUT)
BINTVL1  DC    A(100*1)  ONE SECOND IN HUNDREDTHS OF A SECOND.
***************************************
 SPACE 3
***************************************
         LTORG
 SPACE 3
**************************************************
ENDPART2 DC    0H'0'
         DROP
***********************************************************
   EJECT
***********************************************************
**  FRAGMENT  SUB-POOL #2 TO AVOID FREED CORE FROM GOING TO PK#0.
NARBGN40 CSECT
         USING NARBGN40,R12
     B  ENDPART3  NO LONGER USED...
         LA    R6,7
FRAGIT GETMAIN R,LV=4096,SP=2
      FREEMAIN R,LV=4080,A=(1),SP=2
         BCT   R6,FRAGIT
****************************************
         SPACE 3
****************************************
         B     ENDPART3
 SPACE 3
         LTORG
 SPACE 3
**************************************************
ENDPART3 DC    0H'0'
         DROP
***********************************************************
   EJECT
***********************************************************
**  CALL  -NARBGNP0-  FOR SOME WORK.
**
NARBGN40 CSECT
*
PART4    #
*
D#D      EQU   R6
         USING NARBGN40,R12
****************************************
**  FORCE  TRANSACTION  CLASS  PER  CONCATENATION LEVEL IN DD=STEPLMSG.
         TM    RSTFLAGD,X'01'           Q. DO THIS PART.
         BZ    P43XX
         B     P43XX
         AGO   .P43XX
         NARSCDF D#D,NARSCD10,NARSCDCX  PTR TO NARSCDC0
         L       D#D,00(D#D)            PTR TO NARSCDC1 = RESTART AREA.
         TM    08+01(D#D),SCDRSCLD      Q. COLD START.
         BZ    P43XX                      NO, SKIP.
         TIME  DEC
         ST    R0,CTIM1
         L     R1,ADDRSCD                 NO, LOAD SCDPTR.
         LA    R2,=X'FE02C100'
         LINK  EP=NARBGNP0                          LINK  FOR CLASSSET.
         TIME  DEC
         ST    R0,CTIM2
.P43XX   ANOP
P43XX    EQU   *
******************************
         SPACE 3
******************************
**  WIPEOUT TCBTCT POINTER.
         TM    RSTFLAGD,X'02'           Q. DO THIS PART.
         BZ    P44XX                      NO, SKIP.
         LINK  EP=NARAO200,PARAM=((B#S),P4TCTPM)
         B     P44XX
P4TCTPM  DC    CL8'B0'
P44XX    EQU   *
********************
 SPACE 3
********************
**  RIG TO RUN NUC IN SUPVR STATE
**      SO THAT DISPATCHER CAN WAIT IN PK=0 FOR BETTER PERFORMANCE.
         TM    RSTFLAGD,X'04'           Q. DO THIS PART.
         BZ    P45XX                      NO, SKIP.
         B     P45XX                      NO, SKIP.
         L     R1,ADDRSCD
         LA    R2,=X'FE02C3F0'
         LINK  EP=NARBGNP0
P45XX    #
******************************
 SPACE 3
******************************
         B     ENDPART4
******************************
 SPACE 3
****************************************
CTIM1    DC    A(0)
CTIM2    DC    A(0)
         LTORG
 SPACE 3
**************************************************
ENDPART4 DC    0H'0'
         DROP
***********************************************************
   EJECT
***********************************************************
**  INITIALIZE  AUX/EXTRA/NAR CLB'S.
NARBGN40 CSECT
*
PART5    #
*
         USING NARBGN40,R12
         B     ENDPART5
         AGO   .ENDPT5
E#A      EQU   8
E#B      EQU   1
E#C      EQU   2
E#D      EQU   3
E#E      EQU   4
E#F      EQU   14
CCLBA    EQU   5
CLBLEN   EQU   6
LASTCLB  EQU   7
CNRCLBA  EQU   9
NRCLBL   EQU   10
LNRCLB   EQU   11
**************************************************
         L     R1,16               CVT
         L     R1,00(R1)           DBL
         L     R1,00(R1)           TCB
         L     R1,12(R1)           TIOT
         ST    R1,BGNTIOT          TIOT
********************
         L     E#A,ADDRSCD
         USING     SCD,E#A
***************************************
         LH    LASTCLB,SCDCLBN          NO. CLB'S
         BCTR  LASTCLB,0                -1
         MH    LASTCLB,SCDCLBL          * LENGTH
         L     CCLBA,SCDCLB             ADDRESS OF 1ST CLB
         LA    CCLBA,0(,CCLBA)
         AR    LASTCLB,CCLBA            ADDRESS OF LAST CLB
         LH    CLBLEN,SCDCLBL           LENGTH OF CLB
         USING CLBDECB,CCLBA
******************************
         NARSCDF E#C,NARSCD10,NARCLBXX,TOFIND=PTR
         USING       NARCLBXX,E#C
         LH    LNRCLB,NARCLBXX+4+0      NO. NR CLB'S
         BCTR  LNRCLB,0                 -1
         MH    LNRCLB,NARCLBXX+4+2      *  LENGTH
         L     CNRCLBA,NARCLBXX         ADDRESS OF 1ST NR CLB
         LA    CNRCLBA,0(,CNRCLBA)
         AR    LNRCLB,CNRCLBA           ADDRESS OF LAST NR CLB
         LH    NRCLBL,NARCLBXX+4+2      LENGTH OF NR CLB
         BCTR  NRCLBL,0                 -1
         STC   NRCLBL,CPYNR+1           STORE LENGTH TO MOVE
         LA    NRCLBL,1(,NRCLBL)        RESTORE ACTUAL LENGTH
         USING NARCLB$X,CNRCLBA
****************************************
NXTCLB   AP    DLIN,=P'1'
*        ***********
******************************
***     LINE NUMBER.
         MVI   NARCLBNO+3,C' '
         UNPK  NARCLBNO,DLIN
         OI    NARCLBNO+L'NARCLBNO-1,X'F0'
********************
***     CLB/DECB ADDRESS.
         ST    CCLBA,NARCLBDE           DECB PTR FOR CURRENT CLB
         MVC   NARCLBDE(1),DECRLN  STO RLN FOR THIS DECB.
********************
***********************************************************************
* FIND NO. PTERMS FOR THIS CLB & DEVTYPE
*
         L     E#B,CLBCTB
         LA    E#B,0(,E#B)
         USING CTB,E#B
         L     E#D,CTBCTT
         MVC   NARCLBIO(1),0(E#D)       MOVE IN DEVTYPE FROM CTT
         SR    E#D,E#D
         SH    E#B,SCDCTBL              BACK UP ONE CTB BECAUSE-
NXTCTB   AH    E#B,SCDCTBL              BUMP TO NEXT CTB
         LA    E#D,1(,E#D)              COUNT ANOTHER PTERM
         TM    CTBFLAG3,CTB3LAST        IS THIS THE LAST ONE?
         BNO   NXTCTB                   NO
         STC   E#D,NARCLBDC             YES
***     DCB ADDRESS.
         L     E#E,CLBDCB          DCB  PTR.
         LA    E#E,0(E#E)
         LTR   E#E,E#E
         BZ    INCRBOTH
         STCM  E#E,7,NARCLBDC+1
         USING     IHADCB,E#E
*        ***********
***     DDNAME.
         MVC   NARCLBUC,=CL4'NOP ' IN CASE NOT OPEN.
         MVC   NARCLBDD,DCBDDNAM   MOVE DDNAME IF IT IS CLOSED.
         TM    DCBOFLGS,X'10'      Q. IS IT OPEN.
         BZ    INCRBOTH             NO, SKIP  REMAINING FIELDS.
         LH    R#W,DCBTIOT
         A     R#W,BGNTIOT
         MVC   NARCLBDD,4(R#W)     MOVE DDNAME IF IT IS OPEN.
********************
***     DEB ADDRESS.
NXTUADD  L     E#F,DCBDEBAD
         ST    E#F,NARCLBDB             DEBPTR
         MVC   NARCLBDB(1),16(E#F)      NBR OF UCB'S ON THIS DCB
***     UNIT ADDRESS IN EBCDIC.
         MVC   NARCLBUC,=CL4'INC ' IN CASE NOT ENUF UNITS.
         CLC   NARCLBDE(1),NARCLBDB     Q. ENUF UNIT TO GET HERE?
         BH    INCRBOTH                 NO,SKIP REMAINING FIELDS.
         XR    R#W,R#W                  GET THIS
         IC    R#W,NARCLBDE             RELATIVE LINE NO.
         BCTR  R#W,0                     (RELATIVE TO ZERO)
         SLA   R#W,2                * 4 = OFFSET TO UCB PTR.
         L     R#W,32(R#W,E#F)     UCB PTR.
         MVI   NARCLBUC+3,C' '
         MVC   NARCLBUC+0(3),X'0D'(R#W)      UCB NAME.
***     IOB ADDRESS.
         L     E#F,DCBIOBAD        IOB BASE.
         XR    R1,R1               RELATIVE
         IC    R1,NARCLBDE                LINE NBR,
         XR    R0,R0                * SIZE OF
         IC    R0,DCBEIOBX                  EACH IOB,
         MR    R0,R0                = OFFSET FROM BASE.
         AR    E#F,R1               +  BASE =  IOB PTR.
         STCM  E#F,7,NARCLBIO+1         IOB PTR.
         CLI   DECRLN,X'01'             IS THIS THE FIRST CLB THIS LN
         BNE   INCRBOTH                 NO
         CLC   NARCLBDE(1),NARCLBDB     IS THIS THE LAST UCB THIS LN
         BE    RESET                    YES
NOP1     NOP   SKIPTEST
         BXH   CCLBA,CLBLEN,ENDPART5    BUMP TO NEXT CLB OR QUIT
         CLC   DECDCBAD+1(3),NARCLBDC+1 NEXTCLB HAVE SAME DCB?
         BE    INCRNR                   YES
         SR    CCLBA,CLBLEN             REST CLB POINTER
         MVI   NOP1+1,X'F0'             MAKE NOP A BRANCH
SKIPTEST LR    E#C,CNRCLBA              NO, MAKE A DUMMY ENTRY
         BXH   CNRCLBA,NRCLBL,ENDPART5  IF ANY ROOM LEFT
CPYNR    MVC   NARCLB$X,0(E#C)
         XR    E#C,E#C
         IC    E#C,NARCLBDE
         LA    E#C,1(,E#C)
         STC   E#C,NARCLBDE
         B     NXTUADD
RESET    MVI   NOP1+1,X'00'             MAKE BRANCH A NOP
INCRBOTH BXH   CCLBA,CLBLEN,ENDPART5
INCRNR   BXLE  CNRCLBA,NRCLBL,NXTCLB
         B     ENDPART5
**************************************************
BGNTIOT  DC    A(0)
DLIN     DC    PL4'0'
**************************************************
    SPACE 3
.ENDPT5  ANOP
*****************************************
         LTORG
*****************************************
    SPACE 3
*****************************************
ENDPART5 DC    0Y(0)
         DROP
***********************************************************
   EJECT
**********************************************************************
*** MAKE UP OUR OWN OS/MVS START COMMAND PREFIX FOR /STA REGION CMDS
*    TO SUFFIX WITH THE REGION JOB MEMBER NAME SUFFIX.
*   IF PARMS AVAILABLE FROM START-UP OF THIS NUC, THEN ADD THEM IN.
PART6    #
         USING NARBGN40,R12
         USING SCD,B#S
         L     B#S,ADDRSCD
         DLLINK (P6CMND00,4),P6CMNDPF      INITIALIZE WITH PREFIX.
         OPEN (PARMPROP,INPUT)             OPEN PARMS PROPAGATED.
         TM    PARMPROP+48,X'10'           Q. DID IT OPEN.
         BZ    P6CONT1                       NO; SKIP.
         GET   PARMPROP,P6CMNDPT             YES; GET PARM CARDIMAGE.
         CLOSE PARMPROP                           CLOSE IT.
         LA    R#W,P6CMNDPT+72   (COL73)   END OF RECORD PRE-INCREMENT.
P6LOOP1  BCTR  R#W,0                       DECREMENT. (COL#72,71,70,..)
         CLI   0(R#W),X'40'                Q. IS THIS A BLANK.
         BNE   P6GOTEXT                      NO, NON-BLANK; GOT IT.
         C     R#W,=A(P6CMNDPT)              YES; Q. ALL BLANK.
         BC    GT,P6LOOP1                           NO, KEEP LOOKING.
         B     P6CONT1                              YES; SKIP.
P6GOTEXT MVC   P6LAST,0(R#W)               ADDR OF LAST NON-BLANK
         S     R#W,=A(P6CMNDPT)             - ADDR 1ST CHAR = (LEN-1)
         LA    R#W,5(R#W)                   + 5
         STH   R#W,P6CMNDPL                 = LENGTH_INCLUDING_LL00.
         DLLINK P6CMND00,P6CMNDPL          ADD-ON THE PROPAGATED PARMS.
         CLI    P6LAST,C','                Q. WAS THERE A TRALNG COMMA.
         BE    P6CONT1                       YES; SKIP.
         DLLINK P6CMND00,','                 NO; ADD ONE.
P6CONT1  DLLINK P6CMND00,P6CMNDSF          ADD-ON THE SUFFIX.
         MVC   P6SCRDR0,SCDSRDR            COPY ORIGINAL VALUES
         MVC   P6SCRMBR,SCDSRMBR               FROM THE SCD.
         LA    R#W,P6CMND00                PTR TO OUR COMMAND
         ST    R#W,SCDSRDR                    STO INTO SCD;
         AH    R#W,P6CMND00                 + LEN = MEMBER NAME SPOT
         ST    R#W,SCDSRMBR                   STO INTO SCD.
         B     ENDPART6
**********************************************************************
    SPACE 3
*****************************************
P6LAST   DC    C' '
P6SCRDR0 DC    A(0)
P6SCRMBR DC    A(0)
P6CMNDPF DLTEXT 'S SSS,'
P6CMNDSF DLTEXT 'M=IMSMSG'
P6CMNDPL DC    Y(0,0)
P6CMNDPT DC    CL80' '
P6CMND00 DC    XL128'0'
PARMPROP DCB   DDNAME=PARMPROP,MACRF=GM,DSORG=PS,RECFM=FB,LRECL=80
         LTORG
*****************************************
    SPACE 3
*****************************************
ENDPART6 DC    0Y(0)
         DROP
         PRINT   GEN
**********************************************************************
   EJECT
**********************************************************************
NARBGN40 CSECT
         USING NARBGN40,R12
EPILOGUE EPILOGUE  RC=000
INVALID  ABEND 1,DUMP
**************************************************
         LTORG
**************************************************
         DSECT
         EJECT
         NARSCD
         EJECT
         NARCPY
         EJECT
*&Ã«%$    AIF   ('&SYSPARM'(1,1) NE 'S').OVNOGEN
         PRINT NOGEN
.OVNOGEN ANOP
         ISCD  SCDBASE=0
         IDLI  PSTBASE=0
         ICLI  CLBBASE=0,CTBBASE=0
         DCBD  DSORG=(CX,PS)
         END
