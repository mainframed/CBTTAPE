//YA3324UE JOB 'EACARBONE.SH28205610*110A044         015  1001007037',
//  MSGLEVEL=1,MSGCLASS=H,
//  REGION=1024K,TIME=010,
//  NOTIFY=YA3324
//*MAIN ORG=RMY24,CLASS=L
//*********************************  (YA3324.IMA.RECORD(ADCTR))  ******
//ADCTR    EXEC IMSUPDTE,P=IMA,DISPR=SHR,MEMBER=ADCTR,
//   SYMI=GENLIB,SYMO=MACLIB,USYSUT2=PERM,NEW=NEW,
//   SOUT='*',CONDL=0
//SYSIN    DD  *
./  ADD     NAME=ADCTR
         MACRO                                                           0010000
&NAME    ADCTR &CTR,&REG1=R15,&REG2=R14,&SCD=R9,&PST=
.**********************************************************************
.*                                                                    *
.*       THIS MACRO IS USED TO INCREASE BY ONE THE VALUE OF A         *
.*       SYSTEM COUNTER DESIGNATED BY &CTR.  &CTR IS A NUMERIC        *
.*       SPECIFICATION STARTING FROM 1 (OR 01) AND GOES UP TO 64.     *
.*       THE VALUES FOR &REG1 AND &REG2 ARE REGISTER NOTATIONS        *
.*       AND ALWAYS TAKE THE FORM "R_" OR "R__".  THE VALUES FOR      *
.*       &SCD CAN BE REGISTER NOTATION OR THE VALUE "PSTSCDAD".       *
.*       IF NO VALUE IS GIVEN FOR &PST, THEN ADDRESSABILITY TO A      *
.*       PST IS ASSUMED.  IF A "R_" OR "R__" VALUE IS GIVEN, THEN     *
.*       ADDRESSABILITY IS NOT ASSUMED AND THE GIVEN REGISTER IS      *
.*       USED.  THE INNER MACRO "NARSCDF" IS USED TO FIND THE         *
.*       LOCATION OF THE ROCKWELL EXTENTION OF THE SCD AND THEN       *
.*       THE FIELD OF COUNTERS IS FOUND USING THE ADDRESS RETURNED.   *
.*                                                                    *
.**********************************************************************
         LCLC  &I
         LCLA  &N
&I       SETC  '&CTR'(1,K'&CTR)
&N       SETA  &CTR
         AIF   (&N GE 1 AND &N LE 64).CONT
         MNOTE 8,'COUNTER VALUE &CTR INVALID'
         AGO   .MEND
.*
.CONT    ANOP
         AIF   ('&PST' NE '').NOPST
&NAME    TM    PSTCODE1,PSTP3RGN
         AGO   .CONT1
.NOPST   ANOP
&NAME    TM    PSTCODE1-PST(&PST),PSTP3RGN
.CONT1   ANOP
         BO    OVRCTR&I
         AIF   ('&SCD' EQ 'PSTSCDAD').NOTREG
         LR    &REG1,&SCD
         AGO   .CONT2
.NOTREG  ANOP
         L     &REG1,&SCD
.CONT2   ANOP
*
         NARSCDF &REG1,NARSWAT0,KNOWN=SCD,FAILX=OVRCTR&I
*
         LA    &REG2,1
         A     &REG2,(&N-1)*4(,&REG1)
         ST    &REG2,(&N-1)*4(,&REG1)
OVRCTR&I EQU   *
.MEND    MEND
