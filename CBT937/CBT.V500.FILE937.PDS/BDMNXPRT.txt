BDMNXPRT TITLE 'BDMNXPRT - Export BRODCAST Notices to VB-255 File.'
         SPACE 1
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*  DOCUMENTATION:   THIS PROGRAM MAY BE RUN AS A TSO-IN-BATCH         *
*                   COMMAND.   NEED NOT BE AUTHORIZED.                *
*                                                                     *
*       Copyright (C) 1995-2016 Sam Golob Systems Programming LLC     *
*                 All Rights Reserved                                 *
*                                                                     *
*         SYNTAX:   BDMNXPRT                                          *
*                                                                     *
*       FUNCTION:   To export BRODCAST Notices to an external VB-255  *
*                   format file, in BDMNOTFY format, so they can be   *
*                   reloaded into a new copy of the BRODCAST Dataset. *
*                                                                     *
*          AUTHOR:  SAM GOLOB               VERSION:   1.4.0B         *
*                                                                     *
*            DATE:  May 15, 2007                                      *
*                                                                     *
*       EXECUTION:  ALLOC F(BRODCAST) DA('SYS1.BRODCAST') SH REUSE    *
*                                                                     *
*                   ALLOC F(EXPORT) DA(EXPORT.DATA) SPACE(5 5)        *
*                     TRACKS RECFM(V B) LRECL(255) UNIT(SYSDA) REUSE  *
*                                                                     *
*       NOTE:       BDMNXPRT READS THE BRODCAST DATASET DIRECTLY,     *
*                   AND REPORTS THE NOTICES ONLY FROM IT.  IT         *
*                   DOES NOT READ THE INCORE COPY OF THE NOTICES      *
*                   POINTED TO BY CVTTVT (THE TSO VECTOR TABLE)       *
*                   AND THE NCT WHICH IS DESCRIBED IN MACRO           *
*                   IKJTSVT.  (THE INCORE NOTICES TABLE)              *
*                                                                     *
*       COMMENTS:   This program produces an output file in VB-255    *
*                   format, which dumps all Global Notices in the     *
*                   BRODCAST Dataset.  The EXPORT file contains       *
*                   records that are in the format:                   *
*                                                                     *
*                   BDMNOTFY nnnnn Message Text                       *
*                                                                     *
*                   which are suitable for reloading into another     *
*                   BRODCAST Dataset.                                 *
*                                                                     *
*       CHANGES:    07/25/01  -  CLEANED OUT A LOT OF THE BDMLIST     *
*                   (VER 1.1)    CODE, AND ISSUED A MESSAGE IF A      *
*                                "NOTICES INDEX RECORD" IS CORRUPTED. *
*                                                                     *
*                   04/10/05  -  REPRESENT MESSAGE NUMBERS IN DECIMAL *
*                                INSTEAD OF IN HEX - LIKE LISTBC.     *
*                                                                     *
*                   04/26/05  -  FIX SITUATION IN WHICH INDEX RECORD  *
*                   (VER 1.4)    MARKS A MESSAGE AS USED, BUT THE     *
*                                MESSAGE LENGTH IS ZERO.              *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*       I N S T R E A M    M A C R O S                                *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         MACRO
         IKJZT301  &DSECT=YES
* COPIED FROM OPTIONAL MATERIAL SYM1-1(1) 29MAY80 LDW.  PL/S DELETED.
*        UPDATED                          31MAY95 SBG.
*
*        IKJZT301       RELEASE=OS/VS2.2  LEVEL=01
***********************************************************************
***********************************************************************
*                                                                     *
*                         SYS1.BRODCAST DATA SET                      *
*                                RECORD 1                             *
*                                                                     *
*        THE FIRST RECORD OF THE SYS1.BRODCAST DATA SET (R1BC) HAS    *
*        POINTERS TO THE NOTICES AND MAIL SECTIONS, AND OTHER INFO    *
*        CONCERNING THE DATA SET.                                     *
*                                           10/1/72    LEVEL=1        *
***********************************************************************
***********************************************************************
         AIF   ('&DSECT' EQ 'NO').NODSECT
R1BC     DSECT , -            RECORD 1 OF SYS1.BRODCAST DATA SET
         AGO   .SKIP
.NODSECT ANOP
R1BC     DS    0F -           RECORD 1 OF SYS1.BRODCAST DATA SET
.SKIP    ANOP
R1BCPTRP DS    0A -           SAME AS R1BCPTR BELOW
R1BCFLGS DS    B -            NOTICES FLAGS - NOT USED
R1BCPTR  DS    AL3 -          RELATIVE BLOCK ADDRESS (RBA) OF FIRST
*                               NOTICES DIRECTORY RECORD
R1USPTRP DS    0A -           SAME AS R1USPTR BELOW
R1USFLGS DS    B -            USER MAIL FLAGS - NOT USED
R1USPTR  DS    AL3 -          RBA OF FIRST USER MAIL DIRECTORY RECORD
R1RECNUM DS    F -            TOTAL NO. OF RECORDS IN SYS1.BRODCAST DS
R1BCMAX  DS    H -            MAXIMUM BRODCAST MSG NO. -
*                               FROM MASTER SCHEDULER BASEA, BABCMAX
*                                                                     *
R1DSN    DS    CL24 -         DATA SET NAME IN EBCDIC =
*                               ' SYS1.BRODCAST DATA SET '
R1LEVEL  DS    CL7 -          LEVEL NO. = 'LEVEL N', WHERE 'N' IS
*                               A 1-DIGIT NUMBER
         DS    CL1 -          RESERVED
R1FRESRH DS    CL3 -          RBA OF FREE SEARCH RECORD
R1GENNUM DS    F -            GENERATION NUMBER FOR IN-STORAGE NOTICE
*                               TABLE
         DS    CL76 -         RESERVED
         MEND
         MACRO
         IKJZT302  &DSECT=YES
*        IKJZT302      RELEASE=OS/VS2.2   LEVEL=01
***********************************************************************
***********************************************************************
*                                                                     *
*                         SYS1.BRODCAST DATA SET                      *
*                        NOTICES DIRECTORY RECORD                     *
*                                                                     *
*        THE BROADCAST NOTICES DIRECTORY RECORD (BCDIR) HAS BROADCAST *
*        MESSAGE NUMBERS AND A POINTER TO THE MESSAGE RECORD          *
*        ASSOCIATED WITH EACH MSG. NUMBER.                            *
*                                                                     *
*        NOTE: THIS MACRO USES EQUATES GENERATED BY 'IEZBITS' MACRO.  *
*              THEREFORE, THE IEZBITS MACRO MUST BE CODED             *
*              PREVIOUS TO CALLING THIS MACRO.                        *
*                                           10/1/72    LEVEL=1        *
***********************************************************************
***********************************************************************
         AIF   ('&DSECT' EQ 'NO').NODSECT
BCDIR    DSECT , -
         AGO   .SKIP
.NODSECT ANOP
BCDIR    DS    0F
.SKIP    ANOP
BCDNENT  EQU   25             NUMBER OF ENTRIES
BCDENTRY DS    0CL5 -         ENTRY FOR 1 BROADCAST MSG NO.
BCDMFLG  DS    0BL1 -         BROADCAST DIRECTORY MSG. FLAG:
BCDNOMSG EQU   BIT0
*                               '1' = NO NOTICES MSG ASSIGNED
*                                     TO THIS MSG NUMBER
*                               '0' = NOTICES MSG FOR THIS NUMBER
*                                     IS ASSIGNED
BCDMSGNO DS    FL2 -          BROADCAST NOTICES MSG NO. IN HEX
BCDMRBA  DS    AL3 -          RELATIVE BLOCK ADDR OF NOTICE MSG RCD
         DS    24CL5 -        RESERVE SPACE FOR 24 MORE ENTRIES
*                               IDENTICAL IN FORMAT TO 'BCDENTRY'
BCDREND  DS    CL1 -          END-OF-RECORD INDICATOR = X'7F'
BCDNEXT  DS    AL3 -          CHAIN PTR TO NEXT NOTICE DIRECTORY RCD
*                               (ZERO IF LAST)
         MEND  , */
         MACRO
         IKJZT303   &DSECT=YES
*        IKJZT303      RELEASE=OS/VS2.2  LEVEL=01
***********************************************************************
***********************************************************************
*                                                                     *
*                         SYS1.BRODCAST DATA SET                      *
*                         NOTICES MESSAGE RECORD                      *
*                                                                     *
*          THE BRODCAST NOTICES MESSAGE RECORD (BCMSG) IS A           *
*          RECORD IN THE SYS1.BRODCAST DATA SET WHICH CONTAINS A      *
*          NOTICES MESSAGE INTENDED FOR ALL USERS.                    *
*                                           10/1/72     LEVEL=1       *
***********************************************************************
***********************************************************************
         AIF   ('&DSECT' EQ 'NO').NODSECT
BCMSG    DSECT , -            BRODCAST NOTICES MESSAGE RECORD
         AGO   .SKIP
.NODSECT ANOP
BCMSG    DS    0F -           BRODCAST NOTICES MESSAGE RECORD
.SKIP    ANOP
BCMLNG   DS    FL1 -          LENGTH OF BRODCAST NOTICES MSG TEXT
BCMTEXT  DS    CL125 -        MESSAGE TEXT (PADDED WITH BLANKS)
         DS    XL3 -          RESERVED
         MEND  , */
         SPACE
         MACRO
&NAME    HEX   &TO,&LEN,&FROM
&NAME    STM   R15,R1,HEXSAVE
         LA    R1,&FROM
         LA    R0,&LEN
         LA    R15,&TO
         BAL   R9,HEX
         LM    R15,R1,HEXSAVE
         MEND
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
*        REGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 1
SP000    EQU   0
         SPACE 1
         GBLC  &VERSNUM,&PKGNAME,&PKGTITL,&VERTITL,&COPYRGT,&COPYRSV
         LCLC  &PGMNAME
&VERSNUM SETC  ' 1.4.0B'
&PKGNAME SETC  'BRODMSTR'
&PKGTITL SETC  'Broadcast Master'
&VERTITL SETC  ' Version '
&COPYRGT SETC  'Copyright (c) 1995-2016 Sam Golob Systems Programming'
&COPYRSV SETC  ' LLC - All Rights Reserved  '
&PGMNAME SETC  'BDMNXPRT'
&PGMNAME AMODE 24
&PGMNAME RMODE 24
&PGMNAME CSECT
         USING &PGMNAME,R12,R11,R10
         B     BEGINN-*(,R15)
         DC    CL8'&PGMNAME'
         DC    C' '
         DC    C'&PKGTITL'
         DC    C'&VERTITL'
         DC    C'&VERSNUM'
         DC    C' '
         DC    C'&COPYRGT'
         DC    C'&COPYRSV'
         DC    CL16' &SYSDATE &SYSTIME '
         DC    CL6'HLASM '
         DC    C'&SYSVER'
         DC    C' '
BEGINN   DS    0H
         STM   R14,R12,12(R13)
         LR    R12,R15
         LA    R11,4095(,R12)      LOAD SECOND BASE
         LA    R11,1(,R11)
         LA    R10,4095(,R11)      LOAD THIRD BASE
         LA    R10,1(,R10)
         LR    R4,R1               PRESERVE CPPL POINTER.
         GETMAIN RU,LV=DATALEN,SP=SP000,LOC=BELOW
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING DATAAREA,R13
         BAL   R9,INITCON          INITIALIZE CONSTANTS IN DATA AREA
         SPACE 1
         BAL   R9,GETPSCB          GET PSCB IF ANY (IN R2)
         LTR   R2,R2               ANY PSCB?
         BZ    EOJ8                GET OUT.  NOT TSO.
         BAL   R9,CPPLPTRS         STORE STUFF OUT OF THE CPPL
         BAL   R9,SETIOPL          SET UP IOPL FOR PUTLINE
*        BAL   R9,CHEKAUTH         ARE WE OPER OR ACCT ?
CONT0    LTR   R4,R4               ANY CPPL?
         BZ    EOJ8                NO.  GET OUT
         L     R1,0(,R4)           PT TO CMD BUFFER.
         LTR   R1,R1               ANY CMD BUFFER?
         BZ    EOJ4                GET OUT IF NONE
         LH    R3,0(,R1)           LOAD LENGTH.
*        CH    R3,=H'4'            ANYTHING?
*        BNH   EOJ4                NO.  GET OUT.
         LR    R5,R3               LENGTH OF COMMAND + 4
         ICM   R5,14,FULLZERO      CLEAR HI ORDER 3 BYTES
         LH    R3,2(,R1)           LOAD OFFSET.
         SR    R5,R3               SUBTRACT OFFSET FROM FULL LENGTH+4
         SH    R5,=H'4'            CORRECT FOR LENGTH OF CMDBUFR HEADER
*        BC    8,WMESS3            ZERO, THIS PROGRAM IS RUN W/O PARAMS
*        BC    5,EOJ8              LESS, OR OVERFLOW, OUT.
* -- >  Title message - below
         MVC   LINE,LINE-1                  WRITE MESSAGE WITH
         MVC   LINE(MSGTLEN),MESSAGET         THE COUNTS OF RECORDS
         LA    R0,MSGTLEN                       THAT WERE ZEROED.
         BAL   R9,MSG
         MVC   LINE,LINE-1
         MVC   LINE(MSGCLEN),MESSAGEC
         LA    R0,MSGCLEN
         BAL   R9,MSG
         MVC   LINE,LINE-1
         MVC   LINE(L'REPTLN01),REPTLN01
         LA    R0,133
         BAL   R9,MSG
         MVC   LINE,LINE-1
         LA    R0,20
         BAL   R9,MSG
* -- >  Title message - above
DOBROD   OPEN  (BRODCAST,(INPUT))
         TM    BRODCAST+48,X'10'
         BNO   WMESS1
         OPEN  (EXPORT,(OUTPUT))
         TM    EXPORT+48,X'10'
         BNO   WMESS2
         BAL   R9,HEADREAD         READ HEADER RECORD AND EXTRACT
*                                  RBA OF FIRST NOTICES RECORD, AND
*                                  ALSO THE MAXIMUM NUMBER OF NOTICES
*                                  IN THIS BROADCAST DATASET.
PRMSGHED DS    0H
         MVC   LINE,LINE-1
         MVC   LINET,MESSAG20
         LA    R0,L'MESSAG20+7
         BAL   R9,MSG
         MVC   LINE,LINE-1
         MVC   LINET,MESSAG21
         LA    R0,L'MESSAG21+7
         BAL   R9,MSG
         MVC   LINE,LINE-1
         MVC   LINET,MESSAG22
         LA    R0,L'MESSAG22+7
         BAL   R9,MSG
         MVC   LINE,LINE-1
         LA    R0,133
         BAL   R9,MSG
         BAL   R9,PROCOUT
*
* ------------------------------------------------------------------ *
* ---    MAIN LOOP TO READ EACH NOTICE RECORD.                   --- *
* ---     FIRST, WE HAVE TO GO THRU THE INDEX RECORDS            --- *
* ------------------------------------------------------------------ *
INDEXBC  DS    0H                  READ THROUGH NOTICE INDEX RECORDS
         MVC   SAVERBA(3),FSTNOTI    GET THE BALL ROLLING
INDXLOOP DS    0H
         CLC   SAVERBA(3),ZEROS    ARE WE AT THE END OF NOTICE INDEXES
         BE    ENDINDEX            END OF NOTICE INDEX RECORDS. FINISH.
*   PLEASE MAKE SURE THAT THIS REGISTER R7 ISN'T MESSED WITH, OR
*    IS PROPERLY SAVED, SO THE BCT INSTRUCTION IS PROPER, AND
*    NOTICES AREN'T MISSED.
         LA    R7,25               COUNT OF INDEXES PER RECORD
         MVC   BLOCKNO,SAVERBA     GET RBA FOR READ
         READ  MDIDECB,            ECB ADDRESS TO POST                 X
               DI,                 USE DATA AND KEY                    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               BLOCKNO,MF=E        RBA OF THE RECORD
         SPACE
         CHECK MDIDECB
         L     R3,MDIDECB+20       ADDRESS OF RECORD
         CLI   0(R3),X'00'         NOTICE INDEX RECORD?
         BNE   BADEND              NO. ERROR.
         L     R3,MDIDECB+12       ADDRESS OF RECORD
         USING BCDIR,R3
         MVC   LASTRBA(3),SAVERBA  SAVE CURRENT RBA
         MVC   SAVERBA(3),BCDNEXT  GET NEXT RECORD'S RBA
         LA    R8,0                INITIALIZE INDEX SLOT COUNTER
         DROP  R3
*                       R3 IS STILL POINTING AT THE FIRST INDEX ENTRY
BCILOOP  DS    0H
         USING BCDENTRY,R3
* --->
*        MVC   LINE,LINE-1
*        HEX   LINE,5,BCDENTRY
*        LA    R0,20
*        BAL   R9,MSG
         MVC   LINE,LINE-1
* --->
         TM    0(R3),X'80'                   VALID NOTICE ?
         BO    TRYNEXT                       NO. BUMP TO NEXT ONE.
         CLC   BCDENTRY(5),=X'0000000000'    BLANK ENTRY?
         BE    TRYNEXT                       IF SO, JUST BUMP.
         MVC   SHOWNUMB(2),BCDMSGNO          SAVE NOTICE NUMBER
         MVC   SHOWRBA(3),BCDMRBA            SAVE NOTICE RBA
         BAL   R9,SHOWNOTE                   WRITE NOTICE OUT
         AP    BMSGCNT,=P'1'
TRYNEXT  LA    R3,5(,R3)           BUMP LOCATION TO NEXT INDEX SLOT
         LA    R8,1(,R8)           BUMP COUNTER WITHIN RECORD
         BCT   R7,BCILOOP          TRY FOR ANOTHER NOTICE SLOT
*                           FREE BUFFERS BEFORE GETTING ANOTHER RECORD.
         FREEDBUF MDIDECB,         DECB ADDR USED TO READ              X
               D,                  USED BDAM READ                      X
               BRODCAST            DCB FOR PROCESS DATA SET
         B     INDXLOOP            END OF RECORD? GET ANOTHER.
         SPACE 3
* ------------------------------------------------------------------ *
* ---    END OF MAIN LOOP FOR NOTICES                            --- *
* ------------------------------------------------------------------ *
BADEND   MVC   LINE,LINE-1                COME HERE IF ID KEY NOT X'01'
         LA    R0,L'MESPREND               WRITE MESSAGE THAT
         MVC   LINE(L'MESPREND),MESPREND      THINGS ARE NOT
         BAL   R9,MSG                            SO GOOD.
*                                         GET HERE AT END OF USER RCDS
ENDINDEX DS    0H                            ARE WE DOING ALL IDS ?
         CP    BMSGCNT,=P'0'
         BE    ENDNONE
         B     ZEROUT
ENDNONE  DS    0H                            ARE WE DOING ALL IDS ?
         MVC   LINE,LINE-1
         LA    R0,L'MESDONE
         MVC   LINE(L'MESDONE),MESDONE
         BAL   R9,MSG
         B     EOJ0                           GET THE HECK OUT
ZEROUT   B     EOJ0                WE'RE DONE.
         SPACE 3
WMESS1   MVC   LINE,LINE-1                  WRITE MESSAGE THAT
         MVC   LINET,MESSAGE1                 SYS1.BRODCAST NOT ALLOC'D
         LA    R0,L'MESSAGE1+7                  TO DDNAME BRODCAST.
         BAL   R9,MSG
         B     EOJ4
WMESS2   MVC   LINE,LINE-1                  WRITE MESSAGE THAT
         MVC   LINET,MESSAGE2                 EXPORT.DATA NOT ALLOC'D
         LA    R0,L'MESSAGE2+7                  TO DDNAME BRODCAST.
         BAL   R9,MSG
         B     EOJ4
WMESS3   MVC   LINE,LINE-1                  WRITE MESSAGE THAT
         MVC   LINET,MESSAGE3                 YOU HAVE TO ENTER THE
         LA    R0,L'MESSAGE3+7                  USERID NAME.
         BAL   R9,MSG
         B     EOJ4                                          PARS 06/99
EOJ0     SR    R5,R5               SET CC = 0.
         B     EOJ                 CONT.
EOJ4     LA    R5,4                SET CC = 4.
         B     EOJ                 CONT.
EOJ8     LA    R5,8                SET CC = 8.
         B     EOJ                 CONT
EOJ12    LA    R5,12               SET CC = 12.
         B     EOJ                 CONT
EOJ16    LA    R5,16               SET CC = 16.
         B     EOJ                 CONT
EOJ      DS    0H                  BYE.
         CLOSE (BRODCAST)
         CLOSE (EXPORT)
         LR    R1,R13
         L     R13,4(,R13)
         FREEMAIN RU,LV=DATALEN,A=(R1),SP=SP000
         LR    R15,R5              Restore RC into corrupted R15
         ST    R15,16(,R13)        PUT RC INTO PASSED SAVE AREA
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14
         EJECT
*-----------------------------------------------------------------*
*---*                S U B R O U T I N E S                    *---*
*-----------------------------------------------------------------*
         SPACE 2
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
GETPSCB  DS    0H
         ST    R9,SAVER9A
         L     R1,16               POINT TO CVT.
         L     R1,0(,R1)           POINT TO TCB/ASCB WORDS
         L     R1,4(,R1)           POINT TO TCB.
         L     R1,X'B4'(,R1)       POINT TO JSCB.
         L     R1,X'108'(,R1)      POINT TO PSCB.
         LA    R2,0(,R1)           CLEAR HIGH BYTE.  DUMP INTO R2
         L     R9,SAVER9A
         BR    R9
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        READ THE SYS1.BRODCAST HEADER RECORD AND COPY FIELDS.    *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
HEADREAD DS    0H
         ST    R9,SAVER9B
         READ  MDIDECB,            ECB ADDRESS TO POST                 X
               DI,                 USE DATA AND KEY                    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               BLOKZERO,MF=E       RBA OF THE RECORD
         SPACE
         CHECK MDIDECB
         L     R3,MDIDECB+12       ADDRESS OF RECORD
         USING R1BC,R3             ADDRESSABILITY TO HEADER RECORD MAP
         MVC   FSTNOTI(3),R1BCPTR  SAVE RBA OF FIRST NOTICES INDEX
         MVC   NUMNOTES(2),R1BCMAX  SAVE MAXIMUM NUMBER OF NOTES
         DROP  R3
         FREEDBUF MDIDECB,         DECB ADDR USED TO READ              X
               D,                  USED BDAM READ                      X
               BRODCAST            DCB FOR PROCESS DATA SET
         L     R9,SAVER9B
         BR    R9
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
SHOWNOTE DS    0H
         ST    R9,SAVER9C
         MVC   MESSRBA(3),SHOWRBA
         READ  MDLDECB,            ECB ADDRESS TO POST                 X
               DI,                 USE DATA AND KEY, EXCLUSIVE CNTL    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               MESSRBA,MF=E        RBA OF THE RECORD
         SPACE
         CHECK MDLDECB
         L     R2,MDLDECB+20       ADDRESS OF KEY
         CLI   0(R2),X'02'         IS THIS A GOOD NOTICE ?
         BNE   SHOWNOTN            NO. WE HAVE NO BUSINESS BEING HERE
         L     R2,MDLDECB+12       ADDRESS OF RECORD
         USING BCMSG,R2
         XR    R0,R0
         IC    R0,BCMLNG           LENGTH FOR PUTLINE
         LR    R6,R0               LOAD IT INTO R6
         ST    R6,MSGLNGXX         Store true Message Length
         A     R0,=F'29'
         ST    R0,OUTLSTOR
         BCTR  R6,0                SUBTRACT ONE FOR EXECUTE
*>BELOW<-COVER CASE IN WHICH MSG LENGTH WAS INCORRECTLY MARKED AS ZERO.
         C     R6,=X'FFFFFFFF'     IS R6 EQUAL TO MINUS ONE?
         BNE   SHOWNMIN            NO. THERE WON'T BE A PROBLEM.
         LA    R6,0                YES. SET IT EQUAL TO ZERO.
         MVC   LINE,LINE-1         DUPLICATE LINE BELOW.
         B     SHOWNMWR            SKIP MESSAGE MOVE BUT REPORT NUMBER.
SHOWNMIN DS    0H
*>ABOVE<-COVER CASE IN WHICH MSG LENGTH WAS INCORRECTLY MARKED AS ZERO.
         MVC   LINE,LINE-1         CLEAR MESSAGE LINE.
SHOWNMWR DS    0H
         MVI   OUTDATA,X'40'
         MVC   OUTDATA+1(250),OUTDATA    CLEAR AREA
         EX    R6,MOVEMSG          MOVE MESSAGE TEXT TO BRODCAST BUFFER
         EX    R6,MOVEEXP
         L     R0,MSGLNGXX         Load true Message Length back
         A     R0,=F'10'           TSO MESSAGE LENGTH OF 10
*        HEX   LINE+5,2,SHOWNUMB
         LH    R1,SHOWNUMB         LOAD REGISTER WITH HEX NUMBER
         CVD   R1,DWORD            CONVERT BLOCK COUNT TO DECIMAL
* ->
         MVC   WORK10,MASK10            Move in mask.
         ED    WORK10,DWORD+3           Make 10 digit number.
         MVC   LINE+5(4),BLANKMSG       INITIALIZE AREA TO BLANKS
         MVC   LINE(5),=C'MSGNO'        APPEND TITLE TO NUMBER
         MVC   LINE+5(4),WORK10+6       Take last 4 digits.
         CLI   LINE+6,C' '
         BNE   ZEROA
         MVI   LINE+6,C'0'
ZEROA    DS    0H
         CLI   LINE+7,C' '
         BNE   ZEROB
         MVI   LINE+7,C'0'
ZEROB    DS    0H
* ->
***----->  Diagnostic  -  below
*        L     R0,MSGLNGXX         Load true Message Length back
*        A     R0,=F'30'           TSO MESSAGE LENGTH OF 10
*        HEX   LINE+68,4,OUTLSTOR
***----->  Diagnostic  -  above
         BAL   R9,MSG                   AND SHOW THE MESSAGE ON SCREEN
         BAL   R9,EXPROUT
SHOWNOTN DS    0H
         FREEDBUF MDLDECB,         DECB ADDR USED TO READ              X
               D,                  USED BDAM READ                      X
               BRODCAST            DCB FOR PROCESS DATA SET
         L     R9,SAVER9C
         BR    R9
MOVEMSG  MVC   LINE+10(*-*),BCMTEXT   EXECUTED
MOVEEXP  MVC   OUTMESSG(*-*),BCMTEXT   EXECUTED
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
MSG      DS    0H
         ST    R9,SAVER9D
         LA    R1,LINE             WRITE A LINE USING PUTLINE FACILITY
*        LOAD LENGTH OF MESSAGE INTO R0 BEFORE CALLING THIS ROUTINE.
         BAL   R14,PUTLINE
         MVC   LINE,LINE-1
         L     R9,SAVER9D
         BR    R9
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
HEX      DS    0H
         ST    R9,SAVER9E
         MVC   1(1,R15),0(R1)      MOVE BYTE
         UNPK  0(3,R15),1(2,R15)   UNPACK
         TR    0(2,R15),HEXTAB-240
         LA    R15,2(,R15)         INCREMENT OUTPUT PTR
         LA    R1,1(,R1)           INCREMENT INPUT PTR
         BCT   R0,HEX              DECREMENT LENGTH, THEN LOOP
         MVI   0(R15),C' '         BLANK THE TRAILING BYTE
         L     R9,SAVER9E
         BR    R9                  RETURN TO CALLER
HEXTAB   DC    C'0123456789ABCDEF' TRANSLATE TABLE
HEXSAVE  DS    3F                  SAVE FOR HEX MACRO
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        DATA AREAS THAT NEED TO BE INITIALIZED AT THE BEGINNING  *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INITCON  DS    0H
         ST    R9,SAVER9F
         MVI   LINE-1,C' '         INITIALIZE BLANK
         MVC   LINE,LINE-1         BLANK THE MESSAGE LINE
         MVI   OUTSTCH1,C'{'
         MVI   OUTENCH1,C'}'
         MVC   WORK10(10),BLANKMSG
         ZAP   BMSGCNT,=P'0'
         MVC   WK1NOUSR,MESNOUSR
         MVC   WK1ALL,MESALL
         MVI   ALLFLAG,X'00'
         MVC   WKLBLNK(2),BLANKS
         MVI   OUTFILL1,X'40'
         MVC   OUTNUMBR,BLANKS
         MVI   OUTFILL2,X'40'
         MVI   OUTDATA,X'40'
         MVC   OUTDATA+1(250),OUTDATA    CLEAR AREA
         XC    OUTLSTOR,OUTLSTOR
         MVC   OUTLENG,=H'0'
         MVC   OUTZERO,=H'0'
         L     R9,SAVER9F
         BR    R9
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         USING CPPL,R4
CPPLPTRS DS    0H
         ST    R9,SAVER9G
         L     R6,CPPLUPT          SAVE CPPL POINTERS, SUCH AS:
         ST    R6,SAVEUPT             THE UPT
         L     R6,CPPLPSCB
         ST    R6,SAVEPSCB            THE PSCB
         L     R6,CPPLECT
         ST    R6,SAVEECT             THE ECT
         L     R6,CPPLCBUF
         ST    R6,CMBUFAD             THE COMMAND BUFFER ADDRESS
         ST    R4,SAVECPPL            AND THE CPPL ADDRESS ITSELF
         LA    R6,0
         L     R9,SAVER9G
         BR    R9
         DROP  R4
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*              TEST IF USER HAS PSCB AUTHORIZATION.               *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         USING PSCB,R2
CHEKAUTH DS    0H
         ST    R9,SAVER9H
         TM    PSCBATR1,PSCBACCT   ACCT USER?
         BO    CONT0               YES.  CONTINUE.
         TM    PSCBATR1,PSCBCTRL   OPER USER?
         BZ    EOJ4                NO.  GET OUT.
         DROP  R2
         L     R9,SAVER9H
         BR    R9
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
************************************************************
*        SET UP IOPL FOR PUTLINE                           *
************************************************************
         SPACE
         USING CPPL,R4
SETIOPL  DS    0H
         ST    R9,SAVER9I
         LA    R15,MYIOPL
         USING IOPL,R15
         MVC   IOPLUPT(4),CPPLUPT
         MVC   IOPLECT(4),CPPLECT
         DROP  R4
         LA    R0,MYECB
         ST    R0,IOPLECB
         XC    MYECB,MYECB
         LA    R0,MYPTPB
         ST    R0,IOPLIOPB
         DROP  R15
         SPACE
         L     R15,CVTPTR          POINT TO CVT                  JDM1
         USING CVTMAP,R15          GET ADDRESSABILITY            JDM1
         TM    CVTPUTL,X'80'       IS PUTLINE LOADED? (VS2)      JDM1
         BNO   PUTLOAD             NO - BRANCH TO LOAD
         L     R15,CVTPUTL         YES - USE CVTPUTL             JDM1
         DROP  R15                                               JDM1
         B     PUTLOADX            BRANCH AROUND LOAD
PUTLOAD  LA    R0,=CL8'IKJPUTL '
         LOAD  EPLOC=(0)
         LR    R15,R0              GET ENTRY ADDRESS
         LA    R15,0(,R15)         CLEAR HI BYTE FOR DELETE ROUTINE
PUTLOADX ST    R15,MYPUTLEP        SAVE PUTLINE ENTRY ADDRESS
         L     R9,SAVER9I
         BR    R9
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
EXPROUT  DS    0H
         ST    R9,SAVER9J
         ST    R3,SAVER3A
         L     R3,OUTLSTOR
         LA    R3,1(,R3)
         ST    R3,OUTLSTOR
         L     R3,SAVER3A
         MVC   OUTLENG(2),OUTLSTOR+2
         MVC   OUTZERO,=H'0'
         MVC   OUTLIT(8),=C'BDMNOTFY'
         MVC   OUTNUMBR(5),WORK10+5
         MVI   OUTSTCH1,C'{'
         ST    R3,SAVER3A
         LA    R3,OUTMESSG
         A     R3,MSGLNGXX
         MVI   0(R3),C'}'
         L     R3,SAVER3A
         PUT   EXPORT,OUTLINE
         L     R9,SAVER9J
         BR    R9                  RETURN TO CALLER
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
PROCOUT  DS    0H
         ST    R9,SAVER9K
         MVI   OUTDATA,X'40'
         MVC   OUTDATA+1(250),OUTDATA    CLEAR AREA
         MVC   OUTLIT(6),=C'PROC 0'
         MVC   OUTLENG,=H'21'
         MVC   OUTZERO,=H'0'
         PUT   EXPORT,OUTLINE
         MVI   OUTDATA,X'40'
         MVC   OUTDATA+1(250),OUTDATA    CLEAR AREA
         MVC   OUTLIT(12),=C'CONTROL ASIS'
         MVC   OUTLENG,=H'27'
         MVC   OUTZERO,=H'0'
         PUT   EXPORT,OUTLINE
         MVI   OUTDATA,X'40'
         MVC   OUTDATA+1(250),OUTDATA    CLEAR AREA
         MVC   OUTLIT(8),=C'BDMNOTFY'
         XC    OUTLSTOR,OUTLSTOR
         MVC   OUTLENG,=H'0'
         MVC   OUTZERO,=H'0'
         L     R9,SAVER9K
         BR    R9
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
************************************************************
*        PUTMSG ROUTINE                                    *
************************************************************
         SPACE
PUTMSG   STM   R14,R1,PUTSAVE
         XC    MYOLD(8),MYOLD
         XC    MYSEG1(4),MYSEG1
         MVC   MYPTPB(12),MODLPTPM
         LA    R14,1               NO. OF MESSAGE SEGMENTS
         ST    R14,MYOLD
         LA    R14,MYSEG1          POINT TO 1ST SEGMENT
         ST    R14,MYOLD+4
         LR    R14,R0              LENGTH IN R0
         LA    R14,4(,R14)         ADD 4
         LA    R15,MYSEG1+4
         CLC   0(3,R1),=C'IKJ'     IS DATA PRECEEDED BY MESSAGE ID?
         BE    *+16                YES - BRANCH
         LA    R14,1(,R14)         ADD 1 TO LENGTH
         MVI   0(R15),C' '         INSERT LEADING BLANK
         LA    R15,1(,R15)         BUMP POINTER
         STH   R14,MYSEG1
         LR    R14,R0
         BCTR  R14,0
         B     *+10
         MVC   0(0,R15),0(R1)      MOVE MESSAGE IN
         EX    R14,*-6
         LA    R1,MYIOPL
         L     R15,MYPUTLEP
         SPACE
         PUTLINE PARM=MYPTPB,OUTPUT=(MYOLD),ENTRY=(15),MF=(E,(1))
         SPACE
         LM    R14,R1,PUTSAVE
         BR    R14
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
************************************************************
*        PUTLINE ROUTINE                                   *
************************************************************
         SPACE
PUTLINE  STM   R14,R1,PUTSAVE
         XC    MYSEG1(4),MYSEG1
         MVC   MYPTPB(12),MODLPTPB
         LR    R14,R0              LENGTH IN R0
         LA    R14,4(,R14)         ADD 4
         STH   R14,MYSEG1
         LR    R14,R0
         BCTR  R14,0
         B     *+10
         MVC   MYSEG1+4(0),0(R1)   MOVE TEXT IN
         EX    R14,*-6
         LA    R1,MYIOPL
         L     R15,MYPUTLEP
         SPACE
         PUTLINE PARM=MYPTPB,OUTPUT=(MYSEG1,DATA),ENTRY=(15),MF=(E,(1))
         SPACE
         LM    R14,R1,PUTSAVE
         BR    R14
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         EJECT
*-----------------------------------------------------------------*
*----         FIXED DATA AREAS THAT DON'T CHANGE.             ----*
*-----------------------------------------------------------------*
BLANKMSG DC    CL125' '
DUBLZERO DC    D'0'
FULLZERO DC    F'0'
EDPAT    DC    X'40202020202020202120'
* - - - - - - - - - - - - - - - - - - - - - - - - *
BLANKS   DC    CL10'     '         10 BLANKS
BLOKZERO DC    X'000000'           BLOCK NUMBER OF BROADCAST HEADER
ZEROS    DS    0CL8
ZERENTRY DC    X'000000000000'     SHOW USERID HAS NO MESSAGES WAITING
         DC    X'0000'
MESSAGET DC    C'&PGMNAME PROGRAM  -  &PKGTITL &VERTITL &VERSNUM '
MSGTLEN  EQU   *-MESSAGET
MESSAGEC DC    C'&COPYRGT &COPYRSV'
MSGCLEN  EQU   *-MESSAGEC
*--------------------------------------------------------------------*
         SPACE
REPTLN01 DS    0CL133
         DC    C' '
         DC    CL3' '
         DC    CL6'---'
         DC    CL51'&PGMNAME   ---   BRODCAST Notices Export Program  '
         DC    C' ---'
         DC    CL68' '
*--------------------------------------------------------------------*
MESSAGE1 DC    C'BAD OPEN - ALLOC F(BRODCAST) DA(SYS1.BRODCAST) SH REU'
MESSAGE2 DC    C'BAD OPEN - ALLOC F(EXPORT) DA(EXPORT.DATA) SPACE(5 45)X
                TRACKS RECFM(V B) LRECL(255) UNIT(SYSDA) REUSE'
MESSAGE3 DC    C'PLEASE ENTER THE USERID NAME AFTER COMMAND NAME'
MESSEND  DC    C'* - - - - NO BRODCAST NOTICES - - - - *'
MESSAG20 DC    CL58'   &PGMNAME &PKGTITL &VERTITL &VERSNUM      '
MESSAG21 DC    CL58'   -------  --------- ------  -------   ----- '
MESSAG22 DC    CL58'&COPYRGT      '
MESSAG30 DC    CL58' /*  Messages for USERID:                */ '
MESTOTAL DC    C'USER         HAS           DEFERRED TSO MESSAGES'
MESALL DC C'TOTAL DEFERRED TSO MESSAGES IN SYS1.BRODCAST              '
MESNOUSR DC    C'* - - NO USERID RECORD FOR USER          - - *'
MESPREND DC    C'?????  PRELIMINARY END OF NOTICES  - - - - - *'
MESDONE  DC    C'BDM576I   NO BROADCAST MESSAGE '
MASK6    DC    XL6'402020202120'
MASK10   DC    XL10'40202020202020202120'
BPARM    DC    AL2(BPARME-BPARM,0),C'NON'
BPARME   EQU   *
         SPACE 3
BRODCAST DCB   DDNAME=BRODCAST,    JCL CONNECTION                      X
               BLKSIZE=129,        LENGTH OF FILES BLOCK               X
               DSORG=DA,           IS DIRECT ACCESS FILE               X
               MACRF=(RISXC,WIC),  3 BYTE RBA, DYNAMIC BUF, READ/CHECK X
               OPTCD=R,            3 BYTE RBA WILL BE USED             X
               RECFM=F,            FIXED LENGTH                        X
               BUFNO=2,            # OF BUFS TO GET DYNAMICALLY        X
               KEYLEN=1,           SIZE OF KEY                         X
               BUFL=130            KEYLEN + BLKSIZE
         SPACE 3
EXPORT   DCB   DSORG=PS,MACRF=PM,DDNAME=EXPORT,RECFM=VB,LRECL=255       00400
         SPACE 3
         LTORG
         SPACE
MODLPTPM PUTLINE OUTPUT=(1,TERM,SINGLE,INFOR),                         X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
         SPACE
MODLPTPB PUTLINE OUTPUT=(1,TERM,SINGLE,DATA),                          X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
         DS    0F
         SPACE 3
*-----------------------------------------------------------------*
*----         GETMAINED DATA AREA FIELDS                      ----*
*-----------------------------------------------------------------*
DATAAREA DSECT
SAVE     DS    9D
DWORD    DC    D'0'
SAVER0R1 DS    D
SAVE4    DS    F
SAVERG4  DS    F
SAVERG5  DS    F
SAVERG7  DS    F
SAVERG9  DS    F
SAVER3A  DS    F                   BAL Register Save Area
SAVER9A  DS    F                   BAL Register Save Area
SAVER9B  DS    F                   BAL Register Save Area
SAVER9C  DS    F                   BAL Register Save Area
SAVER9D  DS    F                   BAL Register Save Area
SAVER9E  DS    F                   BAL Register Save Area
SAVER9F  DS    F                   BAL Register Save Area
SAVER9G  DS    F                   BAL Register Save Area
SAVER9H  DS    F                   BAL Register Save Area
SAVER9I  DS    F                   BAL Register Save Area
SAVER9J  DS    F                   BAL Register Save Area
SAVER9K  DS    F                   BAL Register Save Area
SAVER9L  DS    F                   BAL Register Save Area
SAVER9M  DS    F                   BAL Register Save Area
SAVER9N  DS    F                   BAL Register Save Area
SAVER9O  DS    F                   BAL Register Save Area
SAVER9P  DS    F                   BAL Register Save Area
SAVER9Q  DS    F                   BAL Register Save Area
SAVER9R  DS    F                   BAL Register Save Area
SAVER9S  DS    F                   BAL Register Save Area
SAVER9T  DS    F                   BAL Register Save Area
CMBUFAD  DS    F                   COMMAND BUFFER ADDRESS
WKLBLNK  DS    CL2
         DS    CL72                PADDING
BMSGCNT  DC    PL3'0'              COUNT OF MESSAGES FOR EACH NOTICE
NEXT     DS    F                   FOR IKJIFRIF ADD ADDRESS
NUMNOTES DC    X'0000'             MAX NUMBER OF NOTICE RECORDS
SAVEPTRS DC    X'000000000000'     SAVE MSG POINTERS FOR NEW USERID
BLOCKNO  DC    X'000000'           STORE BLOCK NUMBER FOR READ
SHOWRBA  DC    X'000000'           STORE RBA OF NOTICE
SHOWNUMB DC    X'0000'             STORE NUMBER OF NOTICE
LASTRBA  DC    X'000000'           STORE CURRENT RBA
SAVERBA  DC    X'000000'           STORE RBA FOR LATER READ
FSTNOTI  DC    X'000000'           STORE RBA OF FIRST NOTICE INDEX
NMSGRBA  DC    X'000000'           RBA OF NEXT MESSAGE
MESSRBA  DC    X'000000'           RBA OF CURRENT MESSAGE
         DS    0F                  ALIGN ON FULLWORD
SAVECPPL DS    F                   SAVE THE CPPL
SAVEUPT  DS    F                   SAVE FOR IKJIFRIF
SAVEPSCB DS    F                   SAVE FOR IKJIFRIF
SAVEECT  DS    F                   SAVE FOR IKJIFRIF
SAVER7   DS    F                   SAVE COUNT FOR USERID SCAN
MYPPL    DS    7F
MYANS    DS    F
MYECB    DS    F                   USED BY PUTLINE ROUTINE
MYIOPL   DS    4F                  USED BY PUTLINE ROUTINE
MYPTPB   DS    3F                  USED BY PUTLINE ROUTINE
MYPUTLEP DS    F                   USED BY PUTLINE ROUTINE
MYOLD    DS    2F                  USED BY PUTLINE ROUTINE
MYSEG1   DS    2H,CL130            USED BY PUTLINE ROUTINE
PUTSAVE  DS    4F                  USED BY PUTLINE ROUTINE
LINEB    DS    C                   LINE-1
LINE     DS    CL133
LINET    EQU   LINE+7
         DS    0F
RDWR     READ  MDLDECB,DI,MF=L     NOTICE READ
RDWREND  DS    0F
RDWRLEN  EQU   *-RDWR
RDWI     READ  MDIDECB,DI,MF=L     INDEX READ
RDWIEND  DS    0F
RDWILEN  EQU   *-RDWI
WK1TOTAL DC    C'                                                '
WK1ALL DC C'                                                          '
WK1NOUSR DC    C'                                              '
ALLFLAG  DS    X
WORK10   DC    XL10'00'
         DC    CL16' '
OUTENCH1 DC    CL1'}'
         DS    F                   OUTLINE MUST BE ALIGNED.
OUTLINE  DS    0CL255
OUTLENG  DC    H'0'
OUTZERO  DC    H'0'
OUTDATA  DS    0CL251
OUTBLANK DC    CL8' '
OUTLIT   DC    CL8' '
OUTFILL1 DC    CL1' '
OUTNUMBR DC    CL5' '
OUTFILL2 DC    CL1' '
OUTSTCH1 DC    CL1'{'
OUTMESSG DS    CL227
         DC    C'   '
OUTLSTOR DS    F
MSGLNGXX DS    F
         SPACE 1
DATALEN  EQU   *-DATAAREA          LENGTH OF DATA AREA
         IKJCPPL
         IKJUPT
         IKJPSCB
         IKJECT
         IKJIOPL
         IKJZT301 DSECT=YES
         IEZBITS
         IKJZT302 DSECT=YES
         IKJZT303 DSECT=YES
         CVT  DSECT=YES
         END
