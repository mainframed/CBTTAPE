BDMCLEAN TITLE 'BDMCLEAN - CLEAN BROADCAST NOTICES AND USERID MESSAGES'
         SPACE 1
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*  DOCUMENTATION:   THIS PROGRAM MAY BE RUN AS A TSO COMMAND          *
*                   OR USING TSO-IN-BATCH.                            *
*                                                                     *
*       Copyright (C) 1995-2016 Sam Golob Systems Programming LLC     *
*                 All Rights Reserved                                 *
*                                                                     *
*                                                                     *
*         SYNTAX:   BDMCLEAN                                          *
*                                                                     *
*       FUNCTION:   TO CLEAN BRODCAST NOTICES AND RID THEM OF         *
*                   GARBAGE LEFT BY PREVIOUS USE OF THE SLOT.         *
*                   ALSO TO CLEAN DELETED USER MESSAGES FROM THE      *
*                   BRODCAST DATASET.                                 *
*                                                                     *
*          AUTHOR:  SAM GOLOB               VERSION:   1.4.0B         *
*                                                                     *
*            DATE:  NOV 24, 2006                                      *
*                                                                     *
*       EXECUTION:  ALLOC F(BRODCAST) DA('SYS1.BRODCAST') SH REUSE    *
*                                                                     *
*       BATCH EXECUTION:                                              *
*                                                                     *
*       //XXXXXXXX JOB ,'BDMCLEAN',CLASS=B,                           *
*       //    MSGCLASS=X,NOTIFY=&SYSUID                               *
*       //*                                                           *
*       //*      INSTRUCTIONS FOR BDMCLEAN TSO COMMAND                *
*       //*      ------------ --- -------- --- -------                *
*       //*   BDMCLEAN N - CLEAN NOTICES ONLY                         *
*       //*   BDMCLEAN U - CLEAN USER MESSAGES ONLY                   *
*       //*   BDMCLEAN M - CLEAN USER MESSAGES ONLY                   *
*       //*   BDMCLEAN   - CLEAN BOTH NOTICES AND USERID MESSAGES     *
*       //*                                                           *
*       //TSOBATCH EXEC PGM=IKJEFT01                                  *
*       //STEPLIB  DD DISP=SHR,DSN=yourid.BRODMSTR.LOADLIB            *
*       //SYSTSPRT DD SYSOUT=*                                        *
*       //BRODCAST DD DISP=SHR,DSN=name.of.brodcast.dataset           *
*       //SYSTSIN DD *                                                *
*       BDMCLEAN                                                      *
*       /*                                                            *
*                                                                     *
*       NOTE:       BDMCLEAN READS THE BRODCAST DATASET DIRECTLY,     *
*                   AND CLEANS OUT ALL UNUSED NOTICES TO HEXZEROS,    *
*                   WHILE ZEROING OUT ALL UNUSED BYTES FROM USED      *
*                   NOTICES TOO.  CLEANS OUT DELETED USER MESSAGES    *
*                   AS WELL.                                          *
*                                                                     *
*       CHANGES:  05/09/05  -  ADD ENQUEUE PROTECTION.  ENQ ON        *
*                              HEADER RECORD FOR THE DURATION,        *
*                              AND ON THE RECORD BEING WRITTEN,       *
*                              DURING THE WRITE OPERATION.            *
*                                                                     *
*                 06/11/24  -  SIMILAR ENQUEUE PROTECTION WHEN        *
*                              CLEARING USER MESSAGES.                *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*       I N S T R E A M    M A C R O S                                *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         MACRO
         IKJZT301  &DSECT=YES
* COPIED FROM OPTIONAL MATERIAL SYM1-1(1) 29MAY80 LDW.  PL/S DELETED.
*        UPDATED                          31MAY95 SBG.
*
*        IKJZT301       RELEASE=OS/VS2.2  LEVEL=01
***********************************************************************
***********************************************************************
*                                                                     *
*                         SYS1.BRODCAST DATA SET                      *
*                                RECORD 1                             *
*                                                                     *
*        THE FIRST RECORD OF THE SYS1.BRODCAST DATA SET (R1BC) HAS    *
*        POINTERS TO THE NOTICES AND MAIL SECTIONS, AND OTHER INFO    *
*        CONCERNING THE DATA SET.                                     *
*                                           10/1/72    LEVEL=1        *
***********************************************************************
***********************************************************************
         AIF   ('&DSECT' EQ 'NO').NODSECT
R1BC     DSECT , -            RECORD 1 OF SYS1.BRODCAST DATA SET
         AGO   .SKIP
.NODSECT ANOP
R1BC     DS    0F -           RECORD 1 OF SYS1.BRODCAST DATA SET
.SKIP    ANOP
R1BCPTRP DS    0A -           SAME AS R1BCPTR BELOW
R1BCFLGS DS    B -            NOTICES FLAGS - NOT USED
R1BCPTR  DS    AL3 -          RELATIVE BLOCK ADDRESS (RBA) OF FIRST
*                               NOTICES DIRECTORY RECORD
R1USPTRP DS    0A -           SAME AS R1USPTR BELOW
R1USFLGS DS    B -            USER MAIL FLAGS - NOT USED
R1USPTR  DS    AL3 -          RBA OF FIRST USER MAIL DIRECTORY RECORD
R1RECNUM DS    F -            TOTAL NO. OF RECORDS IN SYS1.BRODCAST DS
R1BCMAX  DS    H -            MAXIMUM BRODCAST MSG NO. -
*                               FROM MASTER SCHEDULER BASEA, BABCMAX
*                                                                     *
R1DSN    DS    CL24 -         DATA SET NAME IN EBCDIC =
*                               ' SYS1.BRODCAST DATA SET '
R1LEVEL  DS    CL7 -          LEVEL NO. = 'LEVEL N', WHERE 'N' IS
*                               A 1-DIGIT NUMBER
         DS    CL1 -          RESERVED
R1FRESRH DS    CL3 -          RBA OF FREE SEARCH RECORD
R1GENNUM DS    F -            GENERATION NUMBER FOR IN-STORAGE NOTICE
*                               TABLE
         DS    CL76 -         RESERVED
         MEND
         MACRO
         IKJZT302  &DSECT=YES
*        IKJZT302      RELEASE=OS/VS2.2   LEVEL=01
***********************************************************************
***********************************************************************
*                                                                     *
*                         SYS1.BRODCAST DATA SET                      *
*                        NOTICES DIRECTORY RECORD                     *
*                                                                     *
*        THE BROADCAST NOTICES DIRECTORY RECORD (BCDIR) HAS BROADCAST *
*        MESSAGE NUMBERS AND A POINTER TO THE MESSAGE RECORD          *
*        ASSOCIATED WITH EACH MSG. NUMBER.                            *
*                                                                     *
*        NOTE: THIS MACRO USES EQUATES GENERATED BY 'IEZBITS' MACRO.  *
*              THEREFORE, THE IEZBITS MACRO MUST BE CODED             *
*              PREVIOUS TO CALLING THIS MACRO.                        *
*                                           10/1/72    LEVEL=1        *
***********************************************************************
***********************************************************************
         AIF   ('&DSECT' EQ 'NO').NODSECT
BCDIR    DSECT , -
         AGO   .SKIP
.NODSECT ANOP
BCDIR    DS    0F
.SKIP    ANOP
BCDNENT  EQU   25             NUMBER OF ENTRIES
BCDENTRY DS    0CL5 -         ENTRY FOR 1 BROADCAST MSG NO.
BCDMFLG  DS    0BL1 -         BROADCAST DIRECTORY MSG. FLAG:
BCDNOMSG EQU   BIT0
*                               '1' = NO NOTICES MSG ASSIGNED
*                                     TO THIS MSG NUMBER
*                               '0' = NOTICES MSG FOR THIS NUMBER
*                                     IS ASSIGNED
BCDMSGNO DS    FL2 -          BROADCAST NOTICES MSG NO. IN HEX
BCDMRBA  DS    AL3 -          RELATIVE BLOCK ADDR OF NOTICE MSG RCD
         DS    24CL5 -        RESERVE SPACE FOR 24 MORE ENTRIES
*                               IDENTICAL IN FORMAT TO 'BCDENTRY'
BCDREND  DS    CL1 -          END-OF-RECORD INDICATOR = X'7F'
BCDNEXT  DS    AL3 -          CHAIN PTR TO NEXT NOTICE DIRECTORY RCD
*                               (ZERO IF LAST)
         MEND  , */
         MACRO
         IKJZT303   &DSECT=YES
*        IKJZT303      RELEASE=OS/VS2.2  LEVEL=01
***********************************************************************
***********************************************************************
*                                                                     *
*                         SYS1.BRODCAST DATA SET                      *
*                         NOTICES MESSAGE RECORD                      *
*                                                                     *
*          THE BRODCAST NOTICES MESSAGE RECORD (BCMSG) IS A           *
*          RECORD IN THE SYS1.BRODCAST DATA SET WHICH CONTAINS A      *
*          NOTICES MESSAGE INTENDED FOR ALL USERS.                    *
*                                           10/1/72     LEVEL=1       *
***********************************************************************
***********************************************************************
         AIF   ('&DSECT' EQ 'NO').NODSECT
BCMSG    DSECT , -            BRODCAST NOTICES MESSAGE RECORD
         AGO   .SKIP
.NODSECT ANOP
BCMSG    DS    0F -           BRODCAST NOTICES MESSAGE RECORD
.SKIP    ANOP
BCMLNG   DS    FL1 -          LENGTH OF BRODCAST NOTICES MSG TEXT
BCMTEXT  DS    CL125 -        MESSAGE TEXT (PADDED WITH BLANKS)
         DS    XL3 -          RESERVED
         MEND  , */
         SPACE
         MACRO
&NAME    HEX   &TO,&LEN,&FROM
&NAME    STM   R15,R1,HEXSAVE
         LA    R1,&FROM
         LA    R0,&LEN
         LA    R15,&TO
         BAL   R9,HEX
         LM    R15,R1,HEXSAVE
         MEND
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
*        REGS
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 1
SP000    EQU   0
         SPACE 1
         GBLC  &VERSNUM,&PKGNAME,&PKGTITL,&VERTITL,&COPYRGT,&COPYRSV
         LCLC  &PGMNAME
&VERSNUM SETC  ' 1.4.0B'
&PKGNAME SETC  'BRODMSTR'
&PKGTITL SETC  'Broadcast Master'
&VERTITL SETC  ' Version '
&COPYRGT SETC  'Copyright (c) 1995-2016 Sam Golob Systems Programming'
&COPYRSV SETC  'LLC - All Rights Reserved  '
&PGMNAME SETC  'BDMCLEAN'
BDMCLEAN CSECT
         USING BDMCLEAN,R12,R11,R10
         B     BEGINN-*(,R15)
         DC    CL8'&PGMNAME'
         DC    C' '
         DC    C'&PKGTITL'
         DC    C'&VERTITL'
         DC    C'&VERSNUM'
         DC    C' '
         DC    C'&COPYRGT'
         DC    C' '
         DC    C'&COPYRSV'
         DC    CL16' &SYSDATE &SYSTIME '
         DC    CL6'HLASM '
         DC    C'&SYSVER'
         DC    C' '
BEGINN   DS    0H
         STM   R14,R12,12(R13)
         LR    R12,R15
         LA    R11,4095(,R12)      LOAD SECOND BASE
         LA    R11,1(,R11)
         LA    R10,4095(,R11)      LOAD THIRD BASE
         LA    R10,1(,R10)
         LR    R4,R1               PRESERVE CPPL POINTER.
         GETMAIN RU,LV=DATALEN,SP=SP000,LOC=BELOW
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING DATAAREA,R13
         BAL   R9,INITCON          INITIALIZE CONSTANTS IN DATA AREA
         ZAP   BMSGCNT,=P'0'
         ZAP   GMSGCNT,=P'0'
         ZAP   EMSGCNT,=P'0'
         ZAP   FREEREC,=P'0'
         MVI   PARMFLAG,X'00'
         SPACE 1
         BAL   R9,GETPSCB          GET PSCB IF ANY (IN R2)
         LTR   R2,R2               ANY PSCB?
         BZ    EOJ8                GET OUT.  NOT TSO.
         BAL   R9,CPPLPTRS         STORE STUFF OUT OF THE CPPL
         BAL   R9,SETIOPL          SET UP IOPL FOR PUTLINE
*        BAL   R9,CHEKAUTH         ARE WE OPER OR ACCT ?
CONT0    LTR   R4,R4               ANY CPPL?
         BZ    EOJ8                NO.  GET OUT
         L     R1,0(,R4)           PT TO CMD BUFFER.
         LTR   R1,R1               ANY CMD BUFFER?
         BZ    EOJ4                GET OUT IF NONE
         LH    R3,0(,R1)           LOAD LENGTH.
         LR    R5,R3               LENGTH OF COMMAND + 4
         ICM   R5,14,FULLZERO      CLEAR HI ORDER 3 BYTES
         LH    R3,2(,R1)           LOAD OFFSET.
         SR    R5,R3               SUBTRACT OFFSET FROM FULL LENGTH+4
         SH    R5,=H'4'            CORRECT FOR LENGTH OF CMDBUFR HEADER
         BC    8,NOPARM            ZERO, MESSAGE
*        BC    8,EOJ               ZERO, EOJ
         BC    5,EOJ4              LESS, OR OVERFLOW, OUT.
         AR    R1,R3               POINT ...
         LA    R1,4(,R1)           ... TO CHAR.
         LA    R6,7                LOAD REG FOR BCT LOOP
         LR    R7,R1               INITIALIZE POINTER
         LA    R7,0(,R7)                TO FIRST CHARACTER OF ID
         LA    R8,0                INITIALIZE CHARACTER COUNT
CUTIDLEN CLI   0(R7),X'40'         IS THIS CHARACTER A BLANK?
         BE    NOMORCUT            YES, WE ARE AT THE END OF THE ID.
         LA    R7,1(,R7)           BUMP ANOTHER CHARACTER
         LA    R8,1(,R8)           COUNT ANOTHER CHARACTER
         CR    R8,R5               END OF COMMAND BUFFER ?
         BNL   NOMORCUT            YES. GET OUT NOW.
         BCT   R6,CUTIDLEN         TRY FOR BLANK AGAIN
NOMORCUT DS    0H
         MVC   CMDPRM(8),BLANKS    CLEAR FIELD
         BCTR  R8,0                SUBTRACT 1 FOR EXECUTE
         EX    R8,MOVEPRM          MAKE SUBCOMMAND INTO NEW ID
         LA    R8,1(,R8)           BUMP REG BACK TO WHERE IT WAS
         STC   R8,PARMLEN          SAVE NEW USER'S LENGTH
         OC    CMDPRM(8),BLANKS    UPPERCASE NEW USERID
*---> HANDLE PARM IF ANY - BELOW
         CLI   CMDPRM,C'U'
         BE    UMSGONLY
         CLI   CMDPRM,C'M'
         BE    UMSGONLY
         CLI   CMDPRM,C'N'
         BE    NOTEONLY
         B     NOPARM
NOTEONLY DS    0H
         OI    PARMFLAG,X'01'
         B     YESPARM
UMSGONLY DS    0H
         OI    PARMFLAG,X'02'
         B     YESPARM
*---> HANDLE PARM IF ANY - ABOVE
MOVEPRM  MVC   CMDPRM(*-*),0(R1)   EXECUTED
NOPARM   DS    0H
         MVI   PARMFLAG,X'00'      NO PARMS-CLEAR NOTICES AND USERMSGS
         B     DOBROD
YESPARM  DS    0H
DOBROD   OPEN  (BRODCAST,(INOUT))
         TM    BRODCAST+48,X'10'
         BNO   WMESS1
         MVC   RNAM(3),ZEROS       ENQUEUE ON HEADER RECORD
         ENQ   (QNAM,RNAM,E,,SYSTEM),MF=(E,ENQUSER)
         BAL   R9,HEADREAD         READ HEADER RECORD AND EXTRACT
*                                  RBA OF FIRST NOTICES RECORD, AND
*                                  ALSO THE MAXIMUM NUMBER OF NOTICES
*                                  IN THIS BROADCAST DATASET.
* ------------------------------------------------------------------ *
* ---    MAIN LOOP TO READ EACH NOTICE RECORD.                   --- *
* ---     FIRST, WE HAVE TO GO THRU THE INDEX RECORDS            --- *
* ------------------------------------------------------------------ *
         TM    PARMFLAG,X'02'             USER ONLY INDICATED?
         BO    WMESS2                     SKIP NOTICES CLEANING
INDEXBC  DS    0H                  READ THROUGH NOTICE INDEX RECORDS
         MVC   SAVERBA(3),FSTNOTI    GET THE BALL ROLLING
INDXLOOP DS    0H
         CLC   SAVERBA(3),ZEROS    ARE WE AT THE END OF NOTICE INDEXES
         BE    ENDINDEX            END OF NOTICE INDEX RECORDS. FINISH.
*   PLEASE MAKE SURE THAT THIS REGISTER R7 ISN'T MESSED WITH, OR
*    IS PROPERLY SAVED, SO THE BCT INSTRUCTION IS PROPER, AND
*    NOTICES AREN'T MISSED.
         LA    R7,25               COUNT OF INDEXES PER RECORD
         MVC   BLOCKNO,SAVERBA     GET RBA FOR READ
         READ  MDIDECB,            ECB ADDRESS TO POST                 X
               DI,                 USE DATA AND KEY                    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               BLOCKNO,MF=E        RBA OF THE RECORD
         SPACE
         CHECK MDIDECB
         L     R3,MDIDECB+20       ADDRESS OF RECORD
         CLI   0(R3),X'00'         NOTICE INDEX RECORD?
         BNE   BADEND              NO. ERROR.
         L     R3,MDIDECB+12       ADDRESS OF RECORD
         USING BCDIR,R3
         MVC   LASTRBA(3),SAVERBA  SAVE CURRENT RBA
         MVC   SAVERBA(3),BCDNEXT  GET NEXT RECORD'S RBA
         LA    R8,0                INITIALIZE INDEX SLOT COUNTER
         DROP  R3
*                       R3 IS STILL POINTING AT THE FIRST INDEX ENTRY
BCILOOP  DS    0H
         USING BCDENTRY,R3
* --->
*        MVC   LINE,LINE-1
*        HEX   LINE,5,BCDENTRY
*        LA    R0,20
*        BAL   R9,MSG
         MVC   LINE,LINE-1
* --->
         TM    0(R3),X'80'                   VALID NOTICE ?
         BZ    YESNMSG                       YES. SKIP COMPLETE CLEAN.
         OI    CLNALFLG,X'01'                NO. CLEAN ENTIRE RECORD.
YESNMSG  DS    0H
         CLC   BCDENTRY(5),=X'0000000000'    BLANK ENTRY?
         BE    TRYNEXT                       IF SO, JUST BUMP.
         MVC   CLENNUMB(2),BCDMSGNO          SAVE NOTICE NUMBER
         MVC   CLENRBA(3),BCDMRBA            SAVE NOTICE RBA
         ST    R9,SAVERG9
         BAL   R9,CLENNOTE                   WRITE NOTICE OUT
         AP    BMSGCNT,=P'1'
         L     R9,SAVERG9
TRYNEXT  LA    R3,5(,R3)           BUMP LOCATION TO NEXT INDEX SLOT
         LA    R8,1(,R8)           BUMP COUNTER WITHIN RECORD
         BCT   R7,BCILOOP          TRY FOR ANOTHER NOTICE SLOT
*                           FREE BUFFERS BEFORE GETTING ANOTHER RECORD.
         FREEDBUF MDIDECB,         DECB ADDR USED TO READ              X
               D,                  USED BDAM READ                      X
               BRODCAST            DCB FOR PROCESS DATA SET
         B     INDXLOOP            END OF RECORD? GET ANOTHER.
         SPACE 3
* ------------------------------------------------------------------ *
* ---    END OF MAIN LOOP FOR NOTICES                            --- *
* ------------------------------------------------------------------ *
BADEND   MVC   LINE,LINE-1                COME HERE IF ID KEY NOT X'00'
         LA    R0,L'MESPREND               WRITE MESSAGE THAT
         MVC   LINE(L'MESPREND),MESPREND      THINGS ARE NOT
         BAL   R9,MSG                            SO GOOD.
*                                         GET HERE AT END OF INDX RCDS
ENDINDEX DS    0H                            END OF NOTICE INDEX RCDS.
WMESS2   DS    0H
         MVC   LINE,LINE-1                  WRITE MESSAGE WITH
         MVC   LINE(MSGTLEN),MESSAGET         THE COUNTS OF RECORDS
         LA    R0,MSGTLEN                       THAT WERE ZEROED.
         BAL   R9,MSG
         MVC   LINE,LINE-1
         MVC   LINE(MSGCLEN),MESSAGEC
         LA    R0,MSGCLEN
         BAL   R9,MSG
         MVC   LINE,LINE-1
         LA    R0,L'MESSBLNK
         BAL   R9,MSG
*
         TM    PARMFLAG,X'02'             USER ONLY INDICATED?
         BO    NOCLINDX                   YES. SKIP NOTICE MESSAGES.
*
         MVC   LINE,LINE-1
         MVC   LINE(MSG3LEN),MESSAGE3
         MVC   LINE+34(10),MASK10
         ED    LINE+34(10),GMSGCNT
         LA    R0,MSG3LEN
         BAL   R9,MSG
         MVC   LINE,LINE-1
         MVC   LINE(MSG4LEN),MESSAGE4
         MVC   LINE+34(10),MASK10
         ED    LINE+34(10),EMSGCNT
         LA    R0,MSG4LEN
         BAL   R9,MSG
         MVC   LINE,LINE-1
         MVC   LINE(MSG5LEN),MESSAGE5
         MVC   LINE+34(10),MASK10
         ED    LINE+34(10),BMSGCNT
         LA    R0,MSG5LEN
         BAL   R9,MSG
         MVC   LINE,LINE-1
         LA    R0,L'MESSBLNK
         BAL   R9,MSG
* ---- >
NOCLINDX DS    0H
         TM    PARMFLAG,X'01'
         BO    NOCLUSER
         BAL   R9,CLEANUSR
         MVC   LINE,LINE-1
         MVC   LINE(MSG6LEN),MESSAGE6
         MVC   LINE+34(10),MASK10
         ED    LINE+34(10),FREEREC
         LA    R0,MSG6LEN
         BAL   R9,MSG
         MVC   LINE,LINE-1
         LA    R0,L'MESSBLNK
         BAL   R9,MSG
NOCLUSER DS    0H
* ---- >
         B     EOJ0
WMESS1   MVC   LINE,LINE-1                  WRITE MESSAGE THAT
         MVC   LINE,MESSAGE1                  SYS1.BRODCAST NOT ALLOC'D
         LA    R0,L'MESSAGE1                    TO DDNAME BRODCAST.
         BAL   R9,MSG
         B     EOJ4                SET CC = 4.
EOJ      DS    0H
         MVC   RNAM(3),ZEROS       DEQ ON HEADER RECORD
         DEQ   (QNAM,RNAM,,SYSTEM),RET=HAVE,MF=(E,ENQUSER)
*
         CLOSE (BRODCAST)
         LR    R1,R13
         L     R13,4(,R13)
         FREEMAIN RU,LV=DATALEN,A=(R1),SP=SP000
         LR    R15,R5              Load return code into R15.
         ST    R15,16(,R13)        Put RC into passed save area.
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14                 Go back to system.
         SPACE
EOJ0     DS    0H
         LA    R5,0                    Zero Return Code
         B     EOJ
EOJ4     DS    0H
         LA    R5,4                    RC = 4
         B     EOJ
EOJ8     DS    0H
         LA    R5,8                    RC = 8
         B     EOJ
EOJ12    DS    0H
         LA    R5,12                   RC = 12
         B     EOJ
EOJ16    DS    0H
         LA    R5,16                   RC = 16
         B     EOJ
         EJECT
*-----------------------------------------------------------------*
*---*                S U B R O U T I N E S                    *---*
*-----------------------------------------------------------------*
         SPACE 2
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
GETPSCB  DS    0H
         ST    R9,SAVER9B
         L     R1,16               POINT TO CVT.
         L     R1,0(,R1)           POINT TO TCB/ASCB WORDS
         L     R1,4(,R1)           POINT TO TCB.
         L     R1,X'B4'(,R1)       POINT TO JSCB.
         L     R1,X'108'(,R1)      POINT TO PSCB.
         LA    R2,0(,R1)           CLEAR HIGH BYTE.  DUMP INTO R2
         L     R9,SAVER9B
         BR    R9
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        READ THE SYS1.BRODCAST HEADER RECORD AND COPY FIELDS.    *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
HEADREAD DS    0H
         ST    R9,SAVER9C
         READ  MDIDECB,            ECB ADDRESS TO POST                 X
               DI,                 USE DATA AND KEY                    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               BLOKZERO,MF=E       RBA OF THE RECORD
         SPACE
         CHECK MDIDECB
         L     R3,MDIDECB+12       ADDRESS OF RECORD
         USING R1BC,R3             ADDRESSABILITY TO HEADER RECORD MAP
         MVC   FSTNOTI(3),R1BCPTR  SAVE RBA OF FIRST NOTICES INDEX
         MVC   NUMNOTES(2),R1BCMAX  SAVE MAXIMUM NUMBER OF NOTES
         MVC   HIGHVAL(4),R1RECNUM  SAVE NUMBER OF RECORDS IN BRODCAST
         DROP  R3
         FREEDBUF MDIDECB,         DECB ADDR USED TO READ              X
               D,                  USED BDAM READ                      X
               BRODCAST            DCB FOR PROCESS DATA SET
         L     R9,SAVER9C
         BR    R9
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
CLENNOTE DS    0H
         ST    R9,SAVER9D
         MVC   MESSRBA(3),CLENRBA
         READ  MDLDECB,            ECB ADDRESS TO POST                 X
               DI,                 USE DATA AND KEY, EXCLUSIVE CNTL    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               MESSRBA,MF=E        RBA OF THE RECORD
         SPACE
         CHECK MDLDECB
         L     R2,MDLDECB+20       ADDRESS OF KEY
         CLI   0(R2),X'02'         IS THIS A GOOD NOTICE ?
         BNE   CLENNOTN            NO. WE HAVE NO BUSINESS BEING HERE
         L     R2,MDLDECB+12       ADDRESS OF RECORD
         USING BCMSG,R2
         TM    CLNALFLG,X'01'
         BO    CLEANALL
         AP    GMSGCNT,=P'1'
         XR    R0,R0
         IC    R0,BCMLNG           LENGTH FOR MESSAGE MOVE
         LR    R6,R0               LOAD IT INTO R6
         BCTR  R6,0                SUBTRACT ONE FOR EXECUTE
         B     CLENGOON
CLEANMSG MVC   GOODOUT(*-*),BCMTEXT   EXECUTED
CLENGOON DS    0H
*>BELOW<-COVER CASE IN WHICH MSG LENGTH WAS INCORRECTLY MARKED AS ZERO.
         C     R6,=X'FFFFFFFF'     IS R6 EQUAL TO MINUS ONE?
         BNE   CLENNMIN            NO. THERE WON'T BE A PROBLEM.
         LA    R6,0                YES. SET IT EQUAL TO ZERO.
         B     CLENNOTN            SKIP MESSAGE MOVE AND GET OUT.
CLENNMIN DS    0H
*>ABOVE<-COVER CASE IN WHICH MSG LENGTH WAS INCORRECTLY MARKED AS ZERO.
         MVI   GOODOUT,X'00'
         MVC   GOODOUT+1(124),GOODOUT
         EX    R6,CLEANMSG         MOVE MESSAGE TEXT TO WORK AREA
         DROP  R2
         ST    R8,SAVERG8
         L     R8,MDLDECB+12
         MVC   1(125,R8),GOODOUT
         L     R8,SAVERG8
         B     CLENNMWR
CLEANALL DS    0H
         AP    EMSGCNT,=P'1'
         MVI   GOODOUT,X'00'
         MVC   GOODOUT+1(127),GOODOUT
         ST    R8,SAVERG8
         L     R8,MDLDECB+12
         MVC   0(128,R8),GOODOUT
         NI    CLNALFLG,X'FF'-X'01'
         L     R8,SAVERG8
CLENNMWR DS    0H
         MVC   RNAM(3),MESSRBA         USE RBA AS RNAME FOR ENQ/DEQ
         ENQ   (QNAM,RNAM,E,,SYSTEM),MF=(E,ENQUSER)
*
         WRITE MDLDECB,            ECB ADDRESS TO POST                 X
               DI,                 USE DATA AND KEY, EXCLUSIVE CNTL    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               MESSRBA,MF=E        RBA OF THE RECORD
         SPACE
         CHECK MDLDECB
         MVC   RNAM(3),MESSRBA         USE RBA AS RNAME FOR ENQ/DEQ
         DEQ   (QNAM,RNAM,,SYSTEM),RET=HAVE,MF=(E,ENQUSER)
CLENNOTN DS    0H
         FREEDBUF MDLDECB,         DECB ADDR USED TO READ              X
               D,                  USED BDAM READ                      X
               BRODCAST            DCB FOR PROCESS DATA SET
         L     R9,SAVER9D
         BR    R9
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
MSG      DS    0H
         ST    R9,SAVER9E
         LA    R1,LINE             WRITE A LINE USING PUTLINE FACILITY
*        LOAD LENGTH OF MESSAGE INTO R0 BEFORE CALLING THIS ROUTINE.
         BAL   R14,PUTLINE
         MVC   LINE,LINE-1
         L     R9,SAVER9E
         BR    R9
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
HEX      DS    0H
         ST    R9,SAVER9F
         MVC   1(1,R15),0(R1)      MOVE BYTE
         UNPK  0(3,R15),1(2,R15)   UNPACK
         TR    0(2,R15),HEXTAB-240
         LA    R15,2(,R15)         INCREMENT OUTPUT PTR
         LA    R1,1(,R1)           INCREMENT INPUT PTR
         BCT   R0,HEX              DECREMENT LENGTH, THEN LOOP
         MVI   0(R15),C' '         BLANK THE TRAILING BYTE
         L     R9,SAVER9F
         BR    R9                  RETURN TO CALLER
HEXTAB   DC    C'0123456789ABCDEF' TRANSLATE TABLE
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        DATA AREAS THAT NEED TO BE INITIALIZED AT THE BEGINNING  *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INITCON  DS    0H
         ST    R9,SAVER9G
         MVI   LINE-1,C' '         INITIALIZE BLANK
         MVC   LINE,LINE-1         BLANK THE MESSAGE LINE
         ZAP   BMSGCNT,=P'0'
         MVC   WKLBLNK(2),BLANKS
         MVI   ZERREC-1,X'00'
         MVC   ZERREC(129),ZERREC-1
         MVI   CLNALFLG,X'00'
         XC    HIGHVAL,HIGHVAL
         MVC   CMDPRM(8),BLANKS
         MVI   PARMFLAG,X'00'
         L     R9,SAVER9G
         BR    R9
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         USING CPPL,R4
CPPLPTRS DS    0H
         ST    R9,SAVER9H
         L     R6,CPPLUPT          SAVE CPPL POINTERS, SUCH AS:
         ST    R6,SAVEUPT             THE UPT
         L     R6,CPPLPSCB
         ST    R6,SAVEPSCB            THE PSCB
         L     R6,CPPLECT
         ST    R6,SAVEECT             THE ECT
         L     R6,CPPLCBUF
         ST    R6,CMBUFAD             THE COMMAND BUFFER ADDRESS
         ST    R4,SAVECPPL            AND THE CPPL ADDRESS ITSELF
         LA    R6,0
         L     R9,SAVER9H
         BR    R9
         DROP  R4
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*              TEST IF USER HAS PSCB AUTHORIZATION.               *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         USING PSCB,R2
CHEKAUTH DS    0H
         ST    R9,SAVER9I
         TM    PSCBATR1,PSCBACCT   ACCT USER?
         BO    CONT0               YES.  CONTINUE.
         TM    PSCBATR1,PSCBCTRL   OPER USER?
         BZ    EOJ4                NO.  GET OUT.
         DROP  R2
         L     R9,SAVER9I
         BR    R9
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
************************************************************
*        SET UP IOPL FOR PUTLINE                           *
************************************************************
         SPACE
         USING CPPL,R4
SETIOPL  DS    0H
         ST    R9,SAVER9J
         LA    R15,MYIOPL
         USING IOPL,R15
         MVC   IOPLUPT(4),CPPLUPT
         MVC   IOPLECT(4),CPPLECT
         DROP  R4
         LA    R0,MYECB
         ST    R0,IOPLECB
         XC    MYECB,MYECB
         LA    R0,MYPTPB
         ST    R0,IOPLIOPB
         DROP  R15
         SPACE
         L     R15,CVTPTR          POINT TO CVT                  JDM1
         USING CVTMAP,R15          GET ADDRESSABILITY            JDM1
         TM    CVTPUTL,X'80'       IS PUTLINE LOADED? (VS2)      JDM1
         BNO   PUTLOAD             NO - BRANCH TO LOAD
         L     R15,CVTPUTL         YES - USE CVTPUTL             JDM1
         DROP  R15                                               JDM1
         B     PUTLOADX            BRANCH AROUND LOAD
PUTLOAD  LA    R0,=CL8'IKJPUTL '
         LOAD  EPLOC=(0)
         LR    R15,R0              GET ENTRY ADDRESS
         LA    R15,0(,R15)         CLEAR HI BYTE FOR DELETE ROUTINE
PUTLOADX ST    R15,MYPUTLEP        SAVE PUTLINE ENTRY ADDRESS
         L     R9,SAVER9J
         BR    R9
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
************************************************************
*        PUTMSG ROUTINE                                    *
************************************************************
         SPACE
PUTMSG   STM   R14,R1,PUTSAVE
         XC    MYOLD(8),MYOLD
         XC    MYSEG1(4),MYSEG1
         MVC   MYPTPB(12),MODLPTPM
         LA    R14,1               NO. OF MESSAGE SEGMENTS
         ST    R14,MYOLD
         LA    R14,MYSEG1          POINT TO 1ST SEGMENT
         ST    R14,MYOLD+4
         LR    R14,R0              LENGTH IN R0
         LA    R14,4(,R14)         ADD 4
         LA    R15,MYSEG1+4
         CLC   0(3,R1),=C'IKJ'     IS DATA PRECEEDED BY MESSAGE ID?
         BE    *+16                YES - BRANCH
         LA    R14,1(,R14)         ADD 1 TO LENGTH
         MVI   0(R15),C' '         INSERT LEADING BLANK
         LA    R15,1(,R15)         BUMP POINTER
         STH   R14,MYSEG1
         LR    R14,R0
         BCTR  R14,0
         B     *+10
         MVC   0(0,R15),0(R1)      MOVE MESSAGE IN
         EX    R14,*-6
         LA    R1,MYIOPL
         L     R15,MYPUTLEP
         SPACE
         PUTLINE PARM=MYPTPB,OUTPUT=(MYOLD),ENTRY=(15),MF=(E,(1))
         SPACE
         LM    R14,R1,PUTSAVE
         BR    R14
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
************************************************************
*        PUTLINE ROUTINE                                   *
************************************************************
         SPACE
PUTLINE  STM   R14,R1,PUTSAVE
         XC    MYSEG1(4),MYSEG1
         MVC   MYPTPB(12),MODLPTPB
         LR    R14,R0              LENGTH IN R0
         LA    R14,4(,R14)         ADD 4
         STH   R14,MYSEG1
         LR    R14,R0
         BCTR  R14,0
         B     *+10
         MVC   MYSEG1+4(0),0(R1)   MOVE TEXT IN
         EX    R14,*-6
         LA    R1,MYIOPL
         L     R15,MYPUTLEP
         SPACE
         PUTLINE PARM=MYPTPB,OUTPUT=(MYSEG1,DATA),ENTRY=(15),MF=(E,(1))
         SPACE
         LM    R14,R1,PUTSAVE
         BR    R14
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*--------------------------------------------------------------------*
*        THIS IS NOW OUR READ LOOP AS WE DO A SUCESSIVE READ
*        INCREMENTING THE RBA NUMBER UNTIL WE REACH THE BCNUMREC
*        VALUE SAVED IN 'HIGHVAL'.  WHEN WE ENCOUNTER THE X'FF'
*        RECORD TYPE, WE MAKE SURE IT IS COMPLETELY ZEROED OUT.
*--------------------------------------------------------------------*
         SPACE
CLEANUSR DS    0H
         ST    R9,SAVER9A
         MVC   BLOCKNO(3),ZERREC   START BLOCK NUMBER AT ZERO.
CLEALOOP DS    0H
         ICM   R1,7,BLOCKNO        GET PROCESSED RBA
         LA    R1,1(R1)            INCREMENT BY ONE
         STCM  R1,7,BLOCKNO        STORE IT BACK FOR READ
         CLC   BLOCKNO(3),HIGHVAL+1  DID WE REACH MAX?           JDM0
         BE    EXITLOOP            IF SO, GO ANALYZE THEM ALL
         SPACE
         READ  REPECB,             DECB TO USE IN MACRO EXPANSION      X
               DI,                 USE DATA AND KEY                    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               BLOCKNO,MF=E        RBA TO HOLD THE KEY
         SPACE
         CHECK REPECB
         SPACE
         L     R2,REPECB+20        GET KEY ADDRESS
         CLI   0(R2),X'FF'         IS IT DELETED RECORD ?      SG 05/99
         BNE   LOOPGO1             NO. DON'T DO WRITES.        SG 05/99
         ST    R8,SAVER8                                       SG 05/99
         L     R8,REPECB+12        GET RECORD ADDRESS TO ZERO. SG 05/99
         MVC   1(128,R8),ZERREC    AND ZERO IT OUT             SG 05/99
         AP    FREEREC,=P'1'       ADD 1 TO TOTAL
         L     R8,SAVER8                                       SG 05/99
         MVC   RNAM(3),BLOCKNO         USE RBA AS RNAME FOR ENQ/DEQ
         ENQ   (QNAM,RNAM,E,,SYSTEM),MF=(E,ENQUSER)
         WRITE REPECB,             ECB ADDRESS TO POST                 X
               DI,                 USE DATA AND KEY, EXCLUSIVE CNTL    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               BLOCKNO,MF=E        RBA OF THE RECORD
         SPACE
         CHECK REPECB
         MVC   RNAM(3),BLOCKNO         USE RBA AS RNAME FOR ENQ/DEQ
         DEQ   (QNAM,RNAM,,SYSTEM),RET=HAVE,MF=(E,ENQUSER)
LOOPGO1  DS    0H                                              SG 05/99
         SPACE
FREEB    DS    0H                                                       009
         FREEDBUF REPECB,          DECB ADDR USED TO READ              X
               D,                  USED BDAM READ                      X
               BRODCAST            DCB FOR PROCESS DATA SET
         SPACE
         B     CLEALOOP
EXITLOOP DS    0H
         L     R9,SAVER9A
         BR    R9
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         EJECT
*-----------------------------------------------------------------*
*----         FIXED DATA AREAS THAT DON'T CHANGE.             ----*
*-----------------------------------------------------------------*
FULLZERO DC    F'0'
* - - - - - - - - - - - - - - - - - - - - - - - - *
QNAM     DS    CL8'SYSIKJBC'
BLANKS   DC    C'        '         8 BLANKS
BLOKZERO DC    X'000000'           BLOCK NUMBER OF BROADCAST HEADER
RNAME    DC    X'000000'
ZEROS    DS    0CL8
ZERENTRY DC    X'000000000000'     SHOW USERID HAS NO MESSAGES WAITING
         DC    X'0000'
MESSAGET DC    C'&PGMNAME PROGRAM  -  &PKGTITL &VERTITL &VERSNUM '
MSGTLEN  EQU   *-MESSAGET
MESSAGEC DC    C'&COPYRGT &COPYRSV'
MSGCLEN  EQU   *-MESSAGEC
MESSAGE1 DC    C'BAD OPEN - ALLOC F(BRODCAST) DA(SYS1.BRODCAST) SH REU'
MESSAGE3 DC    C' COUNT OF USED  NOTICES CLEANED:             '
MSG3LEN  EQU   *-MESSAGE3
MESSAGE4 DC    C' COUNT OF EMPTY NOTICES CLEANED:             '
MSG4LEN  EQU   *-MESSAGE4
MESSAGE5 DC    C' TOTAL COUNT OF NOTICES CLEANED:             '
MSG5LEN  EQU   *-MESSAGE5
MESSAGE6 DC    C' COUNT OF USER MESSAGES CLEANED:             '
MSG6LEN  EQU   *-MESSAGE5
MESSBLNK DC    C'                                             '
MESPREND DC    C'?????  PRELIMINARY END OF NOTICES  - - - - - *'
MASK6    DC    XL6'402020202120'
MASK10   DC    XL10'40202020202020202120'
         SPACE 3
BRODCAST DCB   DDNAME=BRODCAST,    JCL CONNECTION                      X
               BLKSIZE=129,        LENGTH OF FILES BLOCK               X
               DSORG=DA,           IS DIRECT ACCESS FILE               X
               MACRF=(RISXC,WIC),  3 BYTE RBA, DYNAMIC BUF, READ/CHECK X
               OPTCD=R,            3 BYTE RBA WILL BE USED             X
               RECFM=F,            FIXED LENGTH                        X
               BUFNO=2,            # OF BUFS TO GET DYNAMICALLY        X
               KEYLEN=1,           SIZE OF KEY                         X
               BUFL=130            KEYLEN + BLKSIZE
         SPACE 3
         LTORG
         SPACE
MODLPTPM PUTLINE OUTPUT=(1,TERM,SINGLE,INFOR),                         X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
         SPACE
MODLPTPB PUTLINE OUTPUT=(1,TERM,SINGLE,DATA),                          X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
         DS    0F
ENQUSER  ENQ   (QNAM,RNAME,E,,SYSTEM),MF=L
         SPACE 3
*-----------------------------------------------------------------*
*----         GETMAINED DATA AREA FIELDS                      ----*
*-----------------------------------------------------------------*
DATAAREA DSECT
SAVE     DS    9D
DWORD    DC    D'0'
*
SAVERG8  DS    F
SAVERG9  DS    F
HIGHVAL  DS    F
CMBUFAD  DS    F                   COMMAND BUFFER ADDRESS
         DS    CL1
ZERREC   DS    CL129
WKLBLNK  DS    CL2
         DS    CL72                PADDING
BMSGCNT  DC    PL5'0'              COUNT OF MESSAGES FOR EACH NOTICE
GMSGCNT  DC    PL5'0'              COUNT OF MESSAGES FOR EACH NOTICE
EMSGCNT  DC    PL5'0'              COUNT OF MESSAGES FOR EACH NOTICE
FREEREC  DC    PL5'0'              COUNT OF MESSAGES FOR EACH NOTICE
PARMFLAG DS    X
PARMLEN  DS    C
CMDPRM   DS    CL8
NUMNOTES DC    X'0000'             MAX NUMBER OF NOTICE RECORDS
BLOCKNO  DC    X'000000'           STORE BLOCK NUMBER FOR READ
CLENRBA  DC    X'000000'           STORE RBA OF NOTICE
CLENNUMB DC    X'0000'             STORE NUMBER OF NOTICE
LASTRBA  DC    X'000000'           STORE CURRENT RBA
SAVERBA  DC    X'000000'           STORE RBA FOR LATER READ
FSTNOTI  DC    X'000000'           STORE RBA OF FIRST NOTICE INDEX
MESSRBA  DC    X'000000'           RBA OF CURRENT MESSAGE
RNAM     DC    X'000000'           RBA FOR RNAME IN ENQ-DEQ
         DS    0F                  ALIGN ON FULLWORD
SAVECPPL DS    F                   SAVE THE CPPL
SAVEUPT  DS    F                   SAVE FOR IKJIFRIF
SAVEPSCB DS    F                   SAVE FOR IKJIFRIF
SAVEECT  DS    F                   SAVE FOR IKJIFRIF
SAVER7   DS    F                   SAVE COUNT FOR USERID SCAN
SAVER8   DS    F                   SAVE WORK REGISTER
SAVER9A  DS    F                   BAL Register Save Area
SAVER9B  DS    F                   BAL Register Save Area
SAVER9C  DS    F                   BAL Register Save Area
SAVER9D  DS    F                   BAL Register Save Area
SAVER9E  DS    F                   BAL Register Save Area
SAVER9F  DS    F                   BAL Register Save Area
SAVER9G  DS    F                   BAL Register Save Area
SAVER9H  DS    F                   BAL Register Save Area
SAVER9I  DS    F                   BAL Register Save Area
SAVER9J  DS    F                   BAL Register Save Area
SAVER9K  DS    F                   BAL Register Save Area
SAVER9L  DS    F                   BAL Register Save Area
SAVER9M  DS    F                   BAL Register Save Area
SAVER9N  DS    F                   BAL Register Save Area
SAVER9O  DS    F                   BAL Register Save Area
SAVER9P  DS    F                   BAL Register Save Area
SAVER9Q  DS    F                   BAL Register Save Area
SAVER9R  DS    F                   BAL Register Save Area
SAVER9S  DS    F                   BAL Register Save Area
HEXSAVE  DS    3F                  SAVE FOR HEX MACRO
MYPPL    DS    7F
MYANS    DS    F
MYECB    DS    F                   USED BY PUTLINE ROUTINE
MYIOPL   DS    4F                  USED BY PUTLINE ROUTINE
MYPTPB   DS    3F                  USED BY PUTLINE ROUTINE
MYPUTLEP DS    F                   USED BY PUTLINE ROUTINE
MYOLD    DS    2F                  USED BY PUTLINE ROUTINE
MYSEG1   DS    2H,CL130            USED BY PUTLINE ROUTINE
PUTSAVE  DS    4F                  USED BY PUTLINE ROUTINE
LINEB    DS    C                   LINE-1
LINE     DS    CL133
LINET    EQU   LINE+7
         DS    0F
RDWR     READ  MDLDECB,DI,MF=L     NOTICE READ
RDWREND  DS    0F
RDWRLEN  EQU   *-RDWR
RDWI     READ  MDIDECB,DI,MF=L     INDEX READ
RDWIEND  DS    0F
RDWILEN  EQU   *-RDWI
REPE     READ  REPECB,DI,MF=L      SEQUENTIAL READ
REPEEND  DS    0F
REPELEN  EQU   *-REPE
CLNALFLG DS    X
GOODOUT  DS    CL129
         SPACE 1
DATALEN  EQU   *-DATAAREA          LENGTH OF DATA AREA
         IKJCPPL
         IKJUPT
         IKJPSCB
         IKJECT
         IKJIOPL
         IKJZT301 DSECT=YES
         IEZBITS
         IKJZT302 DSECT=YES
         IKJZT303 DSECT=YES
         IKJZT304 DSECT=YES
         IKJZT305 DSECT=YES
         CVT  DSECT=YES
         END
