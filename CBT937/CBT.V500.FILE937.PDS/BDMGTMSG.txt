BDMGTMSG TITLE 'Extract User Messages from BRODCAST for Reloading'
*------------------------------------------------------------------*
*
*          BDMGTMSG (Extract User Messages from BRODCAST)
*
*
*       Copyright (C) 1995-2016 Sam Golob Systems Programming LLC
*                 All Rights Reserved
*
*
*        This program extracts user messages from the BRODCAST
*        Dataset, for later bulk reloading with the BDMMLOAD
*        program.
*
*------------------------------------------------------------------*
*
*
*        JCL TO INVOKE:
*
*            //BRODCAST EXEC PGM=BDMGTMSG,REGION=4M
*            //STEPLIB  DD  -- NONAUTHORIZED LIBRARY IF YOU CHOOSE --
*            //SYSPRINT DD  SYSOUT=A
*            //BRODCAST DD  DSN=SYS1.BRODCAST,DISP=SHR
*            //BDMMDUMP DD  DISP=(NEW,CATLG,DELETE),
*            //   DSN=yourid.usermsg.dump.dataset,
*            //   DCB=(RECFM=FB,DSORG=PS,LRECL=130,BLKSIZE=27950),
*            //   SPACE=(TRK,(60,60),RLSE),UNIT=SYSALLDA,
*            //   VOL=SER=WORK04
*
* INSTALLED:  20 Dec 2006
*
* CHANGE LOG:
*    20 Dec 2006 -
*
*
*------------------------------------------------------------------*
         EJECT
         MACRO
&NAME    HEX   &TO,&LEN,&FROM
&NAME    DS    0H
         ST    R4,SAV4HEX
         STM   R15,R1,HEXSAVE
         LA    R1,&FROM
         LA    R0,&LEN
         LA    R15,&TO
         BAL   R4,HEX
         L     R4,SAV4HEX
         LM    R15,R1,HEXSAVE
         MEND
         SPACE
R0       EQU   0                  USED BY O.S.
R1       EQU   1                  ADDR OF PARM LIST
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9                   PTR TO OUTPUT LINE FOR PUT
R10      EQU   10                  BAL REGISTER
R11      EQU   11
R12      EQU   12                  BASE REGISTER
R13      EQU   13                  SAVE-AREA ADDRESS
R14      EQU   14                  ULTIMATE RETURN ADDRESS
R15      EQU   15                  ENTRY-PT ADDR, WORK REG, RET CODE
         SPACE
         GBLC  &VERSNUM,&PKGNAME,&PKGTITL,&VERTITL,&COPYRGT,&COPYRSV
         LCLC  &PGMNAME
&VERSNUM SETC  ' 1.4.0B'
&PKGNAME SETC  'BRODMSTR'
&PKGTITL SETC  'Broadcast Master'
&VERTITL SETC  ' Version '
&COPYRGT SETC  'Copyright (c) 1995-2016 Sam Golob Systems Programming'
&COPYRSV SETC  ' LLC - All Rights Reserved  '
&PGMNAME SETC  'BDMGTMSG '
&PGMNAME AMODE 24
&PGMNAME RMODE 24
&PGMNAME CSECT
         B     BEGINN-*(,R15)
         DC    CL8'&PGMNAME'
         DC    C' '
         DC    C'&PKGTITL'
         DC    C'&VERTITL'
         DC    C'&VERSNUM'
         DC    C' '
         DC    C'&COPYRGT'
         DC    C'&COPYRSV'
         DC    CL16' &SYSDATE &SYSTIME '
         DC    CL6'HLASM '
         DC    C'&SYSVER'
         DC    C' '
BEGINN   DS    0H
         STM   R14,R12,12(R13)
         LR    R5,R1                  Save PARM address
         LR    R12,R15                Load entry address
         USING BDMGTMSG,R12,R11,R8    Establish addressability
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LA    R8,4095(,R11)
         LA    R8,1(,R8)
         LR    R15,R13             Save caller's save address
         LA    R13,SAVE            Get our savearea address
         ST    R15,SAVE+4          Save off into low save
         ST    R13,8(,R15)         Chain mine back
         ST    R5,PARMSAVE         Store PARM address for reloading
INITPGM  DS    0H
         MVI   LINE-1,X'40'
         MVI   BLANK44-1,X'40'     Initialize Blanks area
         MVC   BLANK44(44),BLANK44-1
         SPACE
* ----------------------------------------------------------------- *    06223
TIOTSCAN DS    0H             Determine the presence of Optional         06224
*                             DDNAMES:  SYSPRINT, BRODUSER, SYSIN        06225
* ----->                      OPEN them if there is JCL for them.        06226
* ----------------------------------------------------------------- *    06227
         ST    R3,SAVER3
         ST    R5,SAVER5
         L     R5,16          R5=CVT                                     06228
         L     R5,0(,R5)      R5=TCB WORDS                               06229
         L     R5,4(,R5)      R5=MY TCB                                  06230
         L     R5,12(,R5)     R5=TIOT                                    06231
         LA    R5,24(,R5)     R5=FIRST DD ENTRY                          06232
TIOTLOOP CLC   0(4,R5),=XL4'00' END OF TIOT?                             06233
         BE    TIOTSEND       YES, CHECK FOR OUTPUT VOL OVERRIDES        06234
         CLC   4(8,R5),SYSPRINC     SYSPRINT DDNAME THERE?               06235
         BNE   TIOTPRTN       NO                                         06236
         OI    DDNFLAG,X'01'        SHOW PRESENCE OF SYSPRINT DD         06237
         B     TIOTNEXT       CONTINUE                                   06238
TIOTPRTN CLC   4(8,R5),BDMMDUMC                                          06239
         BNE   TIOTUSRN                                                  06240
         OI    DDNFLAG,X'02'        SHOW PRESENCE OF BDMMDUMP DD         06241
         B     TIOTNEXT                                                  06242
TIOTUSRN CLC   4(8,R5),SYSINC                                            06239
         BNE   TIOTBROD                                                  06240
         OI    DDNFLAG,X'04'        SHOW PRESENCE OF SYSIN    DD         06241
         B     TIOTNEXT                                                  06242
TIOTBROD CLC   4(8,R5),BRODCASC                                          06239
         BNE   TIOTNEXT                                                  06240
         OI    DDNFLAG,X'08'        SHOW PRESENCE OF BRODCAST DD         06241
         B     TIOTNEXT                                                  06242
TIOTNEXT SR    R3,R3                                                     06243
         IC    R3,0(,R5)      R3=LENGTH OF DD ENTRY                      06244
         AR    R5,R3          R5=NEXT DD ENTRY                           06245
         B     TIOTLOOP       GO CHECK IT                                06246
TIOTSEND DS    0H                                                        06247
         L     R3,SAVER3
         L     R5,SAVER5
         B     OPEN
SYSPRINC DC    CL8'SYSPRINT'
BDMMDUMC DC    CL8'BDMMDUMP'
SYSINC   DC    CL8'SYSIN   '
BRODCASC DC    CL8'BRODCAST'
* -------------------------------------------------------- *
*   OPEN files and print title lines in SYSPRINT report.   *
* -------------------------------------------------------- *
OPEN     DS    0H              .... TEST ALL THE DD NAMES .....
         TM    DDNFLAG,X'01'        SYSPRINT there?
         BO    OPENSYSP             Yes. Go OPEN it.
         WTO   'BDMGM00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMGM10E     SYSPRINT DD Name is missing...',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMGM00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         TM    DDNFLAG,X'02'        BDMMDUMP there? Then it's allocated
         BZ    NODELMSG             Not there, no need to delete.
         WTO   'BDMGM03E     BDMMDUMP not created properly.',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMGM02E     Delete BDMMDUMP and rerun job.',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMGM00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
NODELMSG DS    0H
         B     EOJ16                End right away.  Code 16.
*
OPENSYSP DS    0H
         OPEN  (SYSPRINT,(OUTPUT))
         TM    DDNFLAG,X'08'        BRODCAST missing?
         BZ    NODDNAME             Put out msgs and end with Code 12.
         OPEN  (BRODCAST,(INPUT))
         TM    DDNFLAG,X'02'        BDMMDUMP missing?
         BZ    NODDNAME             Put out msgs and end with Code 12.
         OPEN  (BDMMDUMP,(OUTPUT))
         RDJFCB BRODCAST
*
* ------------- Get the EESCB in a program -------------*  below
* -----------------  This needs AMODE 31  --------------*
GETEESCB DS    0H
         CNOP  0,4
         L     R15,CONST            Amode 31
         BASSM R0,R15
CONST    DC    X'80',AL3(CONST+4)
         ST    R5,SAVER5A           R5 is temporary.
         L     R5,X'10'             CVT
         L     R5,X'9C'(,R5)        Point to TSVT
         L     R5,X'4C'(,R5)        Point to TPVT
         ST    R5,SAVETPVT          Save the TPVT for reference.
         L     R5,X'20'(,R5)        Point to EESCB - R5
         ST    R5,SAVEESCB          Store away the IKJEESCB address
         L     R5,SAVER5A           Restore R5 quickly.
         L     R2,SAVEESCB          Use work register here.
         LTR   R2,R2                EESCB supported for this release?
         BZ    ENDEESCB             No. Bypass EESCB stuff.
         USING IKJEESCB,R2
USEEESCB DS    0H                   Save EESCB quantities.
         CLC   EESCB_IDENTIFIER(8),LITEESCB      Is this the EESCB?
         BNE   ENDEESCB             No. Can't do anything if not it.
         MVC   EESIDENT(8),EESCB_IDENTIFIER
         MVC   EESVERS(1),EESCB_VERSION  Save EESCB Version Number
         CLI   EESCB_VERSION,X'03'  At least EESCB version 3 ?
         BL    ENDEESCB             No. Don't support fancy stuff.
         MVC   EESDSN(44),EESCB_BROADCAST_DSNAME BRODCAST dataset name
         MVC   EESVOL(6),EESCB_BROADCAST_VOLSER  BRODCAST dataset volsr
         MVC   EESUNT(8),EESCB_BROADCAST_UNIT    BRODCAST dataset unit
         DROP  R2
         CNOP  0,4
         L     R15,CONSTX          Amode 24
         BASSM R0,R15
CONSTX   DC    A(CONSTX+4)
         B     ENDEESCB
SAVETPVT DC    F'0'                Needed for Get EESCB routine
SAVEESCB DC    F'0'                Needed for Get EESCB routine
EESDSN   DC    CL44' '             BRODCAST Dsname from EESCB
EESVOL   DC    CL6' '              BRODCAST Volser from EESCB
EESUNT   DC    CL8' '              BRODCAST Unit Name from EESCB
EESVERS  DC    X'00'               EESCB version number
EESIDENT DC    CL8' '              EESCB Identifier, copied.
LITEESCB DC    CL8'IKJEESCB'
ENDEESCB DS    0H
* ------------- Get the EESCB in a program -------------*  above
         SPACE
*--------------------------------------------------------------------*
*        GET THE HEADER RECORD.  THIS IS A SPECIAL RECORD 1.
*--------------------------------------------------------------------*
         SPACE
         READ  HDRECB,             ECB ADDRESS TO POST                 X
               DI,                 USE DATA AND KEY                    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               BLOCKNO             RBA TO HOLD THE KEY
         SPACE
         CHECK HDRECB
         L     R2,HDRECB+20        Address of Key
         MVC   KEYHOLD(1),0(R2)    Move the key data where we can see
         L     R3,HDRECB+12        Address of Data
         MVC   RECHOLD(129),0(R3)  Move the data where it is accessible
         USING R1BC,R3             Addressability to Record 1 Mapping
         SPACE
         MVC   SAVERBA(3),R1USPTR  Save RBA 1ST Mail Directory Record
         MVC   HIGHVAL(4),R1RECNUM Save get out value
         MVC   SAVHDADR(4),R1RECNUM Save get out value
         MVC   FRSRCRBA(3),R1FRESRH Save Free Search Record RBA
         LA    R9,TITLINE          Title line
         BAL   R10,PRINT           Print it
         LA    R9,CPYRLINE         Copyright line
         BAL   R10,PRINT           Print it
         LA    R9,REPTLN01         Line with name of report
         BAL   R10,PRINT           Print it
         MVC   DSNAMEN(44),JFCBDSNM
         LA    R9,DSNAMET
         BAL   R10,PRINT
         MVC   DSVOLSN(6),JFCBVOLS
         LA    R9,DSVOLST
         BAL   R10,PRINT
*  EESCB stuff  -  below
         CLC   EESIDENT(8),=C'IKJEESCB'
         BNE   NOEESCB1
         CLI   EESVERS,X'03'
         BL    NOEESCB1
         CLC   JFCBDSNM(44),EESDSN
         BNE   NOTOFFIC
         CLC   JFCBVOLS(6),EESVOL
         BNE   NOTOFFIC
         B     YESOFFIC
NOTOFFIC DS    0H
         MVC   LINE,LINE-1
         MVC   LINE+10(L'MESNOFFL),MESNOFFL
         LA    R9,LINE
         BAL   R10,PRINT
         B     EESCBEND
YESOFFIC DS    0H
         MVC   LINE,LINE-1
         MVC   LINE+10(L'MESYOFFL),MESYOFFL
         LA    R9,LINE
         BAL   R10,PRINT
         B     EESCBEND
EESCBEND DS    0H
NOEESCB1 DS    0H
*  EESCB stuff  -  above
         LA    R9,UIDTITLE
         BAL   R10,PRINT
         LA    R9,USERDASH
         BAL   R10,PRINT
         SPACE
         FREEDBUF HDRECB,          DECB address used to READ           X
               D,                  USED BDAM READ                      X
               BRODCAST            DCB for PROCESS Dataset
         SPACE
* ------------------------------------------------------------------- *
INDEXLP  DS    0H               Loop through Userids and run          *
*                               the chains of messages, to see        *
*                               which Userids have messages           *
*                               outstanding, and how many.            *
* ------------------------------------------------------------------- *
         LA    R6,9                COUNT OF USERS PER RECORD
         MVC   BLOCKNO,SAVERBA
         SPACE
         READ  SCNECB,             DECB TO USE IN MACRO EXPANSION      X
               DI,                 USE DATA AND KEY                    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               BLOCKNO             RBA TO HOLD THE KEY
         SPACE
         CHECK SCNECB
         SPACE
         L     R3,SCNECB+12        ADDRESS OF RECORD
         USING USDIR,R3
         MVC   SAVERBA,USDNEXT     COPY NEXT INDEX RECORD NUMBER
         SPACE
*--------------------------------------------------------------------*
*        YOU WILL GO THROUGH THIS LOOP 9 TIMES, BECAUSE 9 ENTRIES
*        ARE HELD IN EVERY RECORD.
*--------------------------------------------------------------------*
         SPACE
USRLOOP  DS    0H
         MVC   USERID(8),BLANK44
         MVC   USERID(7),USDID     Copy who this is
         CLC   USERID(7),ZEROS     Is this a real user?
         BE    NOMSG               Nope, must have been deleted.
         AP    USERS,=P'1'         Count how many users.
         XR    R4,R4               Clear upper byte
         ICM   R4,7,USDRBA         Get block position
         BZ    NOMSG               NO.. JUST BUMP
         MVC   ADDUSUSR(7),BLANK44
         MVC   ADDUSUSR(7),USERIDI
         PUT   BDMMDUMP,ADDUSLNE
* --- >    Printing title of user with messages  -  below
* --- >    Printing title of user with messages  -  above
BUMPUSR  DS    0H
         AP    MSGCOUNT,=P'1'      INCREMENT HOW MANY MESSAGES
         STCM  R4,7,MSGNUM
         SPACE
         READ  MSGECB,             DECB TO USE IN MACRO EXPANSION      X
               DI,                 USE DATA AND KEY                    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               MSGNUM              RBA TO HOLD THE KEY
         SPACE
         CHECK MSGECB
         SPACE
         L     R4,MSGECB+20        ADDRESS OF KEY
         MVC   KEYHOLD(1),0(R4)    Move the key data where we can see
         L     R4,MSGECB+12        Address of Data
         MVC   RECHOLD(129),0(R4)  Move the data where it is accessible
         AP    USRMSG,=P'1'
         PUT   BDMMDUMP,REC130
*
* --- >    Printing message number for this USERID  above
         USING USMSG,R4
         ICM   R4,7,USMNEXT        RRN OF NEXT MESSAGE
         DROP  R4
         SPACE
         FREEDBUF MSGECB,          DECB ADDR USED TO READ              X
               D,                  USED BDAM READ                      X
               BRODCAST            DCB FOR PROCESS DATA SET
         LTR   R4,R4
         BNZ   BUMPUSR
*--------------------------------------------------------------------*
         AP    UWMCNT,=P'1'
         AP    RECCNT,=P'1'
         CP    RECCNT,=P'2'        TEST MID RANGE
         BL    LINE1               LOWER
         BH    LINE3               HIGHER
         BE    LINE2               .MIDRANGE
LINE1    DS    0H
         MVC   MSGCNT1,MASK10
         MVC   USERID1,USERIDI
         ED    MSGCNT1,MSGCOUNT
         B     ZAPCNT
         SPACE
LINE2    DS    0H
         MVC   MSGCNT2,MASK10
         MVC   USERID2,USERIDI
         ED    MSGCNT2,MSGCOUNT
         B     ZAPCNT
         SPACE
LINE3    DS    0H
         MVC   MSGCNT3,MASK10
         MVC   USERID3,USERIDI
         ED    MSGCNT3,MSGCOUNT
         LA    R9,USERLINE
         BAL   R10,PRINT
         MVC   MSGCNT1,=CL10' '
         MVC   MSGCNT2,=CL10' '
         MVC   MSGCNT3,=CL10' '
         MVC   USERID2,=CL7' '
         MVC   USERID3,=CL7' '
         ZAP   RECCNT,=P'0'
         SPACE
ZAPCNT   DS    0H
         ZAP   MSGCOUNT,=P'0'
NOMSG    DS    0H
         LA    R3,13(R3)           BUMP TO NEXT ENTRY
         BCT   R6,USRLOOP          REPEAT FOR NINE TIMES
         SPACE
*--------------------------------------------------------------------*
*        END OF INNER LOOP
*--------------------------------------------------------------------*
         SPACE
         FREEDBUF SCNECB,          DECB ADDR USED TO READ              X
               D,                  USED BDAM READ                      X
               BRODCAST            DCB FOR PROCESS DATA SET
         SPACE
         CLC   SAVERBA,=3X'00'     IS THIS LAST
         BNE   INDEXLP             GO DO NEXT ONE
         CP    RECCNT,=P'0'
         BE    PUTUCNT
         LA    R9,USERLINE         POINT TO LINE TO PRINT
         BAL   R10,PRINT
PUTUCNT  DS    0H
         LA    R9,USERDASH
         BAL   R10,PRINT
         MVC   UWMSCNT(10),MASK10
         ED    UWMSCNT(10),UWMCNT  HOW MANY MESSAGES ARE THERE
         LA    R9,UWMSTOTL         POINT TO LINE TO PRINT
         BAL   R10,PRINT
         MVC   UMSGCNT,MASK10
         ED    UMSGCNT,USRMSG      HOW MANY MESSAGES ARE THERE
         LA    R9,UMSGTOTL         POINT TO LINE TO PRINT
         BAL   R10,PRINT
         MVC   USERCNT,MASK10
         ED    USERCNT,USERS       HOW MANY USERS ARE THERE
         LA    R9,UTOTLINE         POINT TO LINE TO PRINT
         BAL   R10,PRINT
         RDJFCB BDMMDUMP
         LA    R9,USERDASH
         BAL   R10,PRINT
         MVC   LINE,LINE-1
         MVC   LINE+3(61),=C'The User Messages Have Been Dumped to the x
               Following Dataset: '
         PUT   SYSPRINT,LINE
         MVC   DSNAMEN(44),JFCBDSNM
         MVC   DSNAMET+1(8),=C'BDMMDUMP'
         MVI   DSNAMET,C'0'
         LA    R9,DSNAMET
         BAL   R10,PRINT
         MVC   DSVOLSN(6),JFCBVOLS
         MVC   DSVOLST+1(8),=C'BDMMDUMP'
         LA    R9,DSVOLST
         BAL   R10,PRINT
         SPACE
*--------------------------------------------------------------------*
*        END OF OUTER LOOP
*        TIME TO CLOSE AND END THE PROGRAM, BYE.
*--------------------------------------------------------------------*
         SPACE
CLOSE    DS    0H
         CLOSE BRODCAST
         CLOSE SYSPRINT
         TM    DDNFLAG,X'02'
         BZ    EOJ8
         CLOSE BDMMDUMP
EOJ0     DS    0H
         LA    R5,0                    Zero Return Code
         B     EOJ
EOJ4     DS    0H
         LA    R5,4                    RC = 4
         B     EOJ
EOJ8     DS    0H
         LA    R5,8                    RC = 8
         B     EOJ
EOJ12    DS    0H
         LA    R5,12                   RC = 12
         B     EOJ
EOJ16    DS    0H
         LA    R5,16                   RC = 16
         B     EOJ
EOJ      DS    0H
         L     R13,SAVE+4
         LR    R15,R5              Load return code into R15.
         ST    R15,16(,R13)        Put RC into passed save area.
         LM    R14,R12,12(R13)
         BR    R14                 Go back to system.
         SPACE
*--------------------------------------------------------------------*
*        ERROR IF NO BDMMDUMP OR BRODCAST DD NAME                    *
*--------------------------------------------------------------------*
NODDNAME DS    0H
         LA    R9,TITLINE          Title line
         BAL   R10,PRINT           Print it
         LA    R9,CPYRLINE         Copyright line
         BAL   R10,PRINT           Print it
         LA    R9,REPTLN01         Line with name of report
         BAL   R10,PRINT           Print it
         MVC   LINE,LINE-1
         PUT   SYSPRINT,LINE
         TM    DDNFLAG,X'02'       BDMMDUMP DD there?
         BO    BYPBDMMD            Yes. Bypass msg that it's not.
         MVC   LINE,LINE-1
         MVC   LINEM(47),=C'BDMGM01E   ---  BDMMDUMP DD Name Missing  -X
               --  '
         PUT   SYSPRINT,LINE
BYPBDMMD DS    0H
         TM    DDNFLAG,X'08'       BRODCAST DD there?
         BO    BYPMDMSG            Yes. Bypass msg that it's not.
         MVC   LINE,LINE-1
         MVC   LINEM(47),=C'BDMGM01E   ---  BRODCAST DD Name Missing  -X
               --  '
         PUT   SYSPRINT,LINE
BYPMDMSG DS    0H
         TM    DDNFLAG,X'02'       BDMMDUMP DD there?
         BZ    BYPBRODC            Yes. It needs to be deleted.
         MVC   LINE,LINE-1
         MVC   LINEM(47),=C'BDMGM03E   ---  BDMMDUMP not created properX
               ly..'
         PUT   SYSPRINT,LINE
         MVC   LINE,LINE-1
         MVC   LINEM(47),=C'BDMGM02E   ---  Delete BDMMDUMP and Rerun -X
               --  '
         PUT   SYSPRINT,LINE
BYPBRODC DS    0H
         MVC   LINE,LINE-1
         PUT   SYSPRINT,LINE
         TM    DDNFLAG,X'08'       BRODCAST DD there?
         BZ    NOCLSBRC            No. Don't try to CLOSE it.
         CLOSE BRODCAST
NOCLSBRC DS    0H
         MVC   LINE,LINE-1
         MVC   LINEM(47),=C'BDMGM05E   ***  Please correct your JCL   *x
               **  '
         PUT   SYSPRINT,LINE
         MVC   LINE,LINE-1
         PUT   SYSPRINT,LINE
         CLOSE SYSPRINT
         B     EOJ12
*--------------------------------------------------------------------*
*        PRINT OUTPUT SUBROUTINE.
*--------------------------------------------------------------------*
         SPACE
PRINT    DS    0H
         PUT   SYSPRINT,(R9)       WRITE FROM AREA ADDRESS IN R9
         BR    R10                 RETURN TO WHENCE WE CAME
         SPACE
*--------------------------------------------------------------------*
*        DATA CONTROL BLOCKS FOLLOW.
*--------------------------------------------------------------------*
         SPACE
BRODCAST DCB   DDNAME=BRODCAST,    JCL CONNECTION                      X
               BLKSIZE=129,        LENGTH OF FILES BLOCK               X
               DSORG=DA,           IS DIRECT ACCESS FILE               X
               MACRF=RISC,         3 BYTE RBA, DYNAMIC BUF, READ/CHECK X
               OPTCD=R,            3 BYTE RBA WILL BE USED             X
               RECFM=F,            FIXED LENGTH                        X
               BUFNO=2,            # OF BUFS TO GET DYNAMICALLY        X
               KEYLEN=1,           SIZE OF KEY                         X
               BUFL=130,           KEYLEN + BLKSIZE                    X
               EXLST=JFCB1
         SPACE
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,LRECL=133,MACRF=PM,RECFM=FBA
         SPACE
BDMMDUMP DCB   DSORG=PS,DDNAME=BDMMDUMP,MACRF=PM,                      X
               LRECL=130,RECFM=FB,EXLST=JFCB1
         DS    0F                  NEEDED FOR CORRECT EXLST
JFCB1    DC    X'87'
         DC    AL3(INFMJFCB)
         IEFJFCBN LIST=YES
         LTORG
         SPACE
*--------------------------------------------------------------------*
*        DATA AREAS FOLLOW.
*--------------------------------------------------------------------*
         SPACE
CVDAREA  DS    D
SAVE     DC    18F'0'
TYPETBLA DS    F       Address of GETMAINED Type Table         HD
NOTETBLA DS    F       Address of GETMAINED Notice Directory Table
NOTGTSIZ DS    F
CURNOTBL DS    F       Address of beginning of data in Note dir table
HIGHVAL  DS    F
SAVHDADR DS    F                   SAVE HEADER ADDRESS         SG MAY99
BDMPRPGM DS    F                   BDMPRINT entry point
BRODPDCB DS    F                   BRODPRNT DCB in BDMPRINT program
BRODUDCB DS    F                   BRODUSER DCB in BDMPRINT program
SAVRG45  DC    2F'0'               Save Regs 4 and 5 for NDTABLE build
SAVER1   DS    F                   Save Register 1
SAVER3   DS    F                   Save Register 3
SAVER5   DS    F                   Save Register 5
SAVER5A  DS    F                   Save Register 5
SAVER6   DS    F                   Preserve R6 during BRODPRNT handling
SAVER6A  DS    F                   Save Register 6
SAVER10  DS    F                   Save Register 10
PARMSAVE DS    F                   Save passed PARM address if any
SAV14TO2 DC    6F'0'
SAVERBA  DS    CL3                 SAVE LOCAL OF USER INDEX
NOTEDRBA DS    CL3                 Save RBA of Notify Directory f/headr
FRSRCRBA DS    CL3                 Save RBA of Free Search Rcd f/header
NUMNOTCS DS    CL2                 Number of Notices slots in BRODCAST
         DC    C' '                Initialized at beginning of program
BLANK44  DC    CL44' '             General Blanks if needed
PWKAREA  DC    CL3' '              For uppercasing the PARM
ADDUSLNE DS    0CL130
ADDUSHDR DC    CL6'./--- '
ADDUSUSR DC    CL7'       '
ADDUSEND DC    CL117' '
ALLPRINT DC    X'00'
* - - - - - - - - - - -  - - - - - <<<<<<<<<<< >>>>>>>>>>>   below
*                   Keep DDNFLAG and EXECMODE together!
DDNFLAG  DC    X'00'               Results of TIOT scan
*              X'01'               BRODPRNT DD is coded in JCL
*              X'02'               BRODUSER DD is coded in JCL
*              X'04'               SYSIN    DD is coded in JCL
EXECMODE DC    X'00'               Execution mode of BDMPRINT program
*              X'01'               Header Record only
*              X'02'               Notice Messages Directory Mode
*              X'04'               BRODUSER mode
* - - - - - - - - - - -  - - - - - <<<<<<<<<<< >>>>>>>>>>>   above
REC130   DS    0CL130              Dump Record made with Key+Data
KEYHOLD  DC    XL1'00'             Key slot
RECHOLD  DC    XL129'00'           Data slot
INCORGEN DS    F
ZEROS    DC    XL7'00'             Zero Userid Entry
*EXTAB   DC    CL16'0123456789ABCDEF'
         DS    0F
BLOCKNO  DC    X'000000'           BLOCK NUMBERS
MSGNUM   DS    CL3                 SAVE LOCAL OF USER INDEX
MASK3    DC    XL3'202120'
MASK4    DC    XL4'40202120'
MASK46   DC    XL6'402020202120'
MASK46A  DC    XL6'404020202120'
MASK7    DC    XL7'4020206B202120'
MASK10   DC    XL10'40202020202020202120'
RECCNT   DC    PL1'0'
MSGCOUNT DC    PL5'0'
UWMCNT   DC    PL5'0'
NOTDIR   DC    PL5'0'              CT FOR NOTICE DIRECTORY RECS
USRIND   DC    PL5'0'              CT FOR USER MAIL DIRECTORY RECS
NOTMSG   DC    PL5'0'              CT FOR NOTICE MSG RECS
USRMSG   DC    PL5'0'              CT FOR USER MAIL MSG RECS
PTRMSG   DC    PL5'0'              CT FOR POINTER REC (ALWAYS 1) JDM1
REC04    DC    PL5'0'              CT FOR MASTER REC (ALWAYS 1)  JDM0
FREEREC  DC    PL5'0'              CT FOR INACTIVE MAIL MSG RECS
UNUSABL  DC    PL5'0'
USERS    DC    PL5'0'
ACTNOT   DC    PL5'0'              Number of active Notify messages
INANOT   DC    PL5'0'              Number of inactive Nofify messages
TABLERR  DC    CL133'  ** Keys have been updated during this run **'
USERSOUT DC    CL133'0< The Following Users Have Messages Waiting  >'
USERDASH DC    CL133'    ----------------------------------------------X
               --------------------'
NDCREERR DC    CL133'   Notify Directory Table Read Error  -  1'
MESNOFFL DC    C'This BRODCAST Dataset IS NOT IN USE by the system.'
MESYOFFL DC    C'This BRODCAST Dataset IS IN USE by the system.'
*
USERID   DS    0CL8
USERIDI  DS    CL7
USERIDB  DS    CL1
         SPACE
*--------------------------------------------------------------------*
*--              M E S S A G E   L A Y O U T S                     --*
*--------------------------------------------------------------------*
         SPACE
USERIMSG DS    0CL133
         DC    CL8'-   *** '
USERITXT DC    CL31'Chain of Messages for USERID:  '
USERIDM  DC    CL7'       '
         DC    CL5'  -- '
         DC    CL8' Start: '
USERISTH DC    CL7'       '
         DC    CL6' End: '
USERIENH DC    CL7'       '
         DC    CL54' '
         SPACE
*--------------------------------------------------------------------*
USERMSNM DS    0CL133
         DC    CL1' '
USERMTXT DC    CL15'Message Number '
USERMSNO DC    CL7'       '
         DC    CL14' for Userid:  '
USERIDX  DC    CL7'       '
         DC    CL89' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
ACTNOMSG DS    0CL133
         DC    CL10' '
ACTNMSG  DC    CL50'Number of Active BRODCAST Notices Messages    '
ACTNNUM  DC    CL10'       '
         DC    CL63' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
INANOMSG DS    0CL133
         DC    CL10' '
INANMSG  DC    CL50'Number of Inactive BRODCAST Notices Messages  '
INANNUM  DC    CL10'       '
         DC    CL63' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
KEYCOUNT DS    0CL133
         DC    CL10' '
KEYTYPE  DC    CL50'Header Records'
KEYCNT   DC    CL10'         1'
         DC    CL63' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
MAXIMUM  DS    0CL133
         DC    CL60'0Total Available Blocks in Dataset'
MAXCNT   DS    CL10
         DC    CL63' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
DSNAMET  DS    0CL133
         DC    CL30'-BRODCAST Dataset Name  : '
DSNAMEN  DS    CL44
         DC    CL59' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
DSVOLST  DS    0CL133
         DC    CL30' BRODCAST Dataset Volser: '
DSVOLSN  DS    CL6
         DC    CL97' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
NOTICES  DS    0CL133
         DC    CL60' Blocks Required for Notify Service (Directory+MessX
               ages)'
NOTCNT   DS    CL10
         DC    CL63' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
USERLINE DS    0CL133
         DC    CL4' '
USERID1  DS    CL7
         DC    CL1' '
MSGCNT1  DS    CL10
         DC    CL6'  |   '
USERID2  DS    CL7
         DC    CL1' '
MSGCNT2  DS    CL10
         DC    CL6'  |   '
USERID3  DS    CL7
         DC    CL1' '
MSGCNT3  DS    CL10
         DC    CL63' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
UTOTLINE DS    0CL133
         DC    CL60' Number of TSO Users Defined in this BRODCAST datasX
               et:'
USERCNT  DS    CL10
         DC    CL63' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
UIDTITLE DS    0CL133
         DC    CL60'-   USERIDS with Outstanding User Messages: '
         DC    CL73' '
         SPACE
*--------------------------------------------------------------------*
UMSGTOTL DS    0CL133
         DC    CL60' Total number of Outstanding User Messages Extractex
               d :'
UMSGCNT  DS    CL10
         DC    CL63' '
         SPACE
*--------------------------------------------------------------------*
UWMSTOTL DS    0CL133
         DC    CL60' Total number of Userids Having Outstanding Messagex
               s :'
UWMSCNT  DS    CL10
         DC    CL63' '
         SPACE
*--------------------------------------------------------------------*
TITLINE  DS    0CL133
         DC    C'1'
         DC    CL3' '
         DC    CL6'---'
         DC    CL79'&PGMNAME PROGRAM  -  &PKGTITL &VERTITL &VERSNUM   -X
               --'
         DC    CL46' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
CPYRLINE DS    0CL133
         DC    C'0'
         DC    CL3' '
* -->                               Currently 77 characters
         DC    C'&COPYRGT'
         DC    C'&COPYRSV'
* -->
         DC    CL52' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
REPTLN01 DS    0CL133
         DC    C'-'
         DC    CL3' '
         DC    CL6'---'
         DC    CL52'&PGMNAME   ---   BRODCAST Bulk Message Extraction '
         DC    C' ---'
         DC    CL67' '
*--------------------------------------------------------------------*
         SPACE
REPTLN02 DS    0CL133
         DC    C'-'
         DC    CL2' '
         DC    CL6'---'
         DC    CL43'&PGMNAME   ---   BRODCAST Dataset - Record '
         DC    CL17'Display Report   '
         DC    C'--- '
         DC    CL60' '
*--------------------------------------------------------------------*
         SPACE
REPTUL02 DS    0CL133
         DC    C'-'
         DC    CL2' '
         DC    CL6'---'
         DC    CL42'&PGMNAME   ---   BRODCAST Userid Messages '
         DC    CL17'Display Report   '
         DC    C'--- '
         DC    CL61' '
*--------------------------------------------------------------------*
         SPACE
JUNKLINE DS    0CL133
         DC    CL60'0Blocks found to be Orphaned (not connected to a UsX
               erid):'
UNUSDCNT DC    CL10' '
         DC    CL63' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
JUNKLIN2 DS    0CL133
         DC    CL65' (Use the BDMDIAG TSO command to display and/or del-
               ete these.)'
         DC    CL2' '
         DC    CL66' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
JUNKLIN3 DS    0CL133
         DC    CL75' (BDMDIAG REPORT reports orphaned blocks, and BDMDI-
               AG DELBAD deletes them.)'
         DC    CL2' '
         DC    CL56' '
         SPACE
PRMPRINT DS    0F
PRMDRCNO DS    F                   RBA number of record in BRODCAST
PRMDRCAD DS    F                   Address of 130-byte record to be
*                                   printed and optionally formatted.
PRMNDTBL DS    F                   Pointer to incore Notices Directory
*                                   Table, zero if it isn't there.
         DC    C' '                                                      05250
LINE     DC    CL133' '                                                  05251
LINET    EQU   LINE+7
LINEM    EQU   LINE+1
*--------------------------------------------------------------------*
* --------------------------------------------------------- *
*    BAL REGISTER IS R4.
* --------------------------------------------------------- *
HEX      DS    0H
         MVC   1(1,R15),0(R1)        Move byte
         UNPK  0(3,R15),1(2,R15)     Unpack
         TR    0(2,R15),HEXTAB-240
         LA    R15,2(,R15)           Increment output pointer
         LA    R1,1(,R1)             Increment input pointer
         BCT   R0,HEX                Decrement length, then loop
         MVI   0(R15),C' '           Blank the trailing byte
         BR    R4                    Return to caller
HEXTAB   DC    C'0123456789ABCDEF'   Translate table
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
HEXSAVE  DS    3F                    Register save for HEX macro
SAV4HEX  DC    F'0'
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
*--------------------------------------------------------------------*
         SPACE
         EJECT
*----------------------------------------------------------------*
*        THE FOLLOWING IS TAKEN FROM AN OLD MVS MANUAL CALLED
*        "OS/VS2 MVS CONVERSION NOTEBOOK" (GC28-0689-6) AND
*        CONTAINS THE FOLLOWING REFERENCE TO HOW THE BROADCAST
*        HAS CHANGED FROM OS/MVT AND SVS TO MVS:
*
*        'MODIFIED TO INCLUDE A CHANGE LEVEL INDICATOR IN THE
*        HEADER RECORD AND A NEW FIELD FOR EACH USER ID IN THE
*        MAIL DIRECTORY RECORDS. THIS FIELD CONTAINS THE
*        ADDRESS OF THE LAST MESSAGE IN THE CHAIN OF MESSAGES
*        ASSOCIATED WITH EACH NEW USER.'
*
*        NOTE: UNTIL I CAN DUMP RECORDS AND FIGURE OUT WHERE THE
*              ABOVE STATEMENTS FIT INTO THE DSECTS BELOW, WILL
*              LET IT TO THE READER TO HELP AND GET IT FIGURED
*              OUT BEFORE I DO.
*
*----------------------------------------------------------------*
         SPACE
         IEZBITS
         SPACE
         IKJZT301 DSECT=YES
         SPACE
         IKJZT302
         SPACE
         IKJZT303
         SPACE
         IKJZT304
         SPACE
         IKJZT305
         SPACE
         IKJZT306
         SPACE
         IKJEESCB
         END
