BDMUSORT TITLE 'BDMUSORT - Sort userids and delete duplicates'
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*  Documentation:   This program is a batch program to feed userid    *
*                   names into a BRODCAST dataset, in bulk.  The      *
*                   lists of userids which are input to the program   *
*                   are sorted, and all duplicates deleted, before    *
*                   they are loaded into the BRODCAST dataset.        *
*                                                                     *
*    Copyright (C) 1995-2016 Sam Golob Systems Programming LLC        *
*              All Rights Reserved                                    *
*                                                                     *
*    Function:   To load a new BRODCAST dataset with userids from     *
*                a file, in bulk.  The program gets all the userid    *
*                inputs, sorts them, and deletes duplicates.          *
*                                                                     *
*                Your SORT program is LINKed to and invoked, to sort  *
*                all inputted 8-character Userid records in ascending *
*                order.  Afterwards, duplicates are deleted and the   *
*                remaining records are put out to the USRIDOUT DD.    *
*                                                                     *
*                Input to this program can be created by the BDMGTUID *
*                program.                                             *
*                                                                     *
*                Output from this program can be fed into the         *
*                BDMULOAD program.                                    *
*                                                                     *
*    Sample JCL:                                                      *
*                                                                     *
*  ALL of the following DD Names must be present:                     *
*                                                                     *
*   USERIDS, USRIDOUT, SORTIN, SORTOUT, SYSOUT, SYSPRINT              *
*                                                                     *
*  If any of these DD Names is missing, see the Diagnosis section     *
*  below.                                                             *
*                                                                     *
*  //BDMUSORT EXEC PGM=BDMUSORT,REGION=3000K                          *
*  //STEPLIB  DD  DISP=SHR,DSN=SBGOLOB.BRODMSTR.LOADLIB               *
*  //USERIDS  DD  DISP=SHR,DSN=SBGOLOB.NEW.USROUT                     *
*  //         DD  DISP=SHR,DSN=SBGOLOB.SYS1.USROUT                    *
*  //USRIDOUT DD  DISP=(NEW,CATLG,DELETE),DSN=SBGOLOB.NEW.USRSORT,    *
*  //             DCB=(RECFM=FB,DSORG=PS,LRECL=8,BLKSIZE=8000),       *
*  //             SPACE=(TRK,(5,5)),UNIT=SYSALLDA,VOL=SER=WORK04      *
*  //SORTIN   DD  DSN=&&SORT,DISP=(NEW,PASS),UNIT=SYSALLDA,           *
*  //             DCB=(RECFM=FB,LRECL=8,BLKSIZE=8000),                *
*  //             SPACE=(TRK,(5,5))                                   *
*  //SORTOUT  DD  DSN=&&SORT,DISP=(SHR,PASS),VOL=REF=*.SORTIN         *
*  //SYSOUT   DD  SYSOUT=*                                            *
*  //SYSPRINT DD  SYSOUT=*                                            *
*                                                                     *
*    Diagnosis:                                                       *
*                                                                     *
*  All DD Names must be present, otherwise a "write to the JES LOG"   *
*  is written that a DD Name is missing, and the Return Code is 16.   *
*                                                                     *
*  A "write to the JES LOG" is always written with a Diagnosis        *
*  message.  If correct, the following appears:                       *
*                                                                     *
*  BDMUS01I     DDNFLAG = 3F     *  - OK -   *                        *
*  IEF142I SBGOLOBC BDMUSORT - STEP WAS EXECUTED - COND CODE 0000     *
*                                                                     *
*  If incorrect, something like the following appears instead:        *
*                                                                     *
*  BDMUS01I     DDNFLAG = 36     * diagnosis *                        *
*  BDMUS00I     ----------------------------                          *
*  BDMUS10E     USERIDS  DD Name is missing...                        *
*  BDMUS00I     ----------------------------                          *
*  BDMUS00I     ----------------------------                          *
*  BDMUS10E     USRIDOUT DD Name is missing...                        *
*  BDMUS00I     ----------------------------                          *
*  BDMUS00I     ----------------------------                          *
*  BDMUS09E     Please correct your JCL.......                        *
*  BDMUS00I     ----------------------------                          *
*  IEF142I SBGOLOBC BDMUSORT - STEP WAS EXECUTED - COND CODE 0016     *
*                                                                     *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*                                                                     *
*    Date Written:  Dec. 16, 2006       Release:  1.4.0B              *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         MACRO
&NAME    HEX   &TO,&LEN,&FROM
&NAME    STM   R15,R1,HEXSAVE
         LA    R1,&FROM
         LA    R0,&LEN
         LA    R15,&TO
         BAL   R4,HEX
         LM    R15,R1,HEXSAVE
         MEND
         YREGS
         SPACE 1
         GBLC  &VERSNUM,&PKGNAME,&PKGTITL,&VERTITL,&COPYRGT,&COPYRSV
         LCLC  &PGMNAME
&VERSNUM SETC  ' 1.4.0B'
&PKGNAME SETC  'BRODMSTR'
&PKGTITL SETC  'Broadcast Master'
&VERTITL SETC  ' Version '
&COPYRGT SETC  'Copyright (c) 1995-2016 Sam Golob Systems Programming'
&COPYRSV SETC  ' LLC - All Rights Reserved  '
&PGMNAME SETC  'BDMUSORT'
&PGMNAME AMODE 24
&PGMNAME RMODE 24
&PGMNAME CSECT
         USING &PGMNAME,R15
         B     BEGINN-*(,R15)
         DC    CL8'&PGMNAME'
         DC    C' '
         DC    C'&PKGTITL'
         DC    C'&VERTITL'
         DC    C'&VERSNUM'
         DC    C' '
         DC    C'&COPYRGT'
         DC    C'&COPYRSV'
         DC    CL16' &SYSDATE &SYSTIME '
         DC    CL6'HLASM '
         DC    C'&SYSVER'
         DC    C' '
SAVE     DC    18F'0'                                                   00406
BEGINN   DS    0H
* ------
         STM   R14,R12,12(R13)                                          00102
         LR    R5,R1                     SAVE PARM POINTER
         LA    R1,SAVE                   NEW SAVE-AREA ADDR
         ST    R1,8(,R13)                FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)                BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1                    NEW SAVE AREA ADDRESS
         LR    R1,R5                     RESTORE PARM POINTER
         DROP  R15
* ------
         LR    R12,R15                                                  00103
         USING BDMUSORT,R12,R11,R10,R9   SET UP FOUR BASE REGISTERS     00104
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LA    R10,4095(,R11)
         LA    R10,1(,R10)
         LA    R9,4095(,R10)
         LA    R9,1(,R9)
***********************************************************************
INITEM   DS    0H
         MVI   BLANK-1,X'40'
         MVI   LINE-1,X'40'
         MVC   BLANK,BLANK-1
         MVC   LINE,LINE-1
         MVC   BIRECIN,=F'0'
         ZAP   DERECIN,=P'0'
         MVC   BIRECSR,=F'0'
         ZAP   DERECSR,=P'0'
         MVC   BIRECOU,=F'0'
         ZAP   DERECOU,=P'0'
         MVI   DDNFLAG,X'00'
* ----------------------------------------------------------------- *    06223
TIOTSCAN DS    0H             Determine the presence of Optional         06224
*                             DDNAMES:  BRODPRNT, BRODUSER, SYSIN        06225
* ----->                      OPEN them if there is JCL for them.        06226
* ----------------------------------------------------------------- *    06227
* --   DD NAME  -  IF THIS DD NAME IS PRESENT, THEN AS FOLLOWS   -- *    06227
* --                                                             -- *    06227
* --                  USERIDS   -  DDNFLAG = X'01'               -- *    06227
* --                  SORTIN    -  DDNFLAG = X'02'               -- *    06227
* --                  SYSPRINT  -  DDNFLAG = X'04'               -- *    06227
* --                  USRIDOUT  -  DDNFLAG = X'08'               -- *    06227
* --                  SORTOUT   -  DDNFLAG = X'10'               -- *    06227
* --                  SYSOUT    -  DDNFLAG = X'20'               -- *    06227
* ----------------------------------------------------------------- *    06227
         ST    R3,SAVER3A
         ST    R5,SAVER5A
         L     R5,16          R5=CVT                                     06228
         L     R5,0(,R5)      R5=TCB WORDS                               06229
         L     R5,4(,R5)      R5=MY TCB                                  06230
         L     R5,12(,R5)     R5=TIOT                                    06231
         LA    R5,24(,R5)     R5=FIRST DD ENTRY                          06232
TIOTLOOP CLC   0(4,R5),=XL4'00' END OF TIOT?                             06233
         BE    TIOTSEND       YES, CHECK FOR OUTPUT VOL OVERRIDES        06234
         CLC   4(8,R5),USERIDSC     USERIDS DDNAME THERE?                06235
         BNE   TIOTUSER       NO                                         06236
         OI    DDNFLAG,X'01'        SHOW PRESENCE OF USERIDS DD          06237
         B     TIOTNEXT       CONTINUE                                   06238
TIOTUSER CLC   4(8,R5),SORTINC                                           06239
         BNE   TIOTSRTI                                                  06240
         OI    DDNFLAG,X'02'        SHOW PRESENCE OF SORTIN DD           06241
         B     TIOTNEXT                                                  06242
TIOTSRTI CLC   4(8,R5),SYSPRINC                                          06239
         BNE   TIOTSYSP                                                  06240
         OI    DDNFLAG,X'04'        SHOW PRESENCE OF SYSPRINT DD         06241
         B     TIOTNEXT                                                  06242
TIOTSYSP CLC   4(8,R5),USRIDOUC                                          06239
         BNE   TIOTSRTO                                                  06240
         OI    DDNFLAG,X'08'        SHOW PRESENCE OF USRIDOUT DD         06241
         B     TIOTNEXT                                                  06242
TIOTSRTO CLC   4(8,R5),SORTOUTC                                          06239
         BNE   TIOTSYSO                                                  06240
         OI    DDNFLAG,X'10'        SHOW PRESENCE OF SORTOUT  DD         06241
         B     TIOTNEXT                                                  06242
TIOTSYSO CLC   4(8,R5),SYSOUTC                                           06239
         BNE   TIOTNEXT                                                  06240
         OI    DDNFLAG,X'20'        SHOW PRESENCE OF SYSOUT   DD         06241
         B     TIOTNEXT                                                  06242
TIOTNEXT SR    R3,R3                                                     06243
         IC    R3,0(,R5)      R3=LENGTH OF DD ENTRY                      06244
         AR    R5,R3          R5=NEXT DD ENTRY                           06245
         B     TIOTLOOP       GO CHECK IT                                06246
TIOTSEND DS    0H                                                        06247
         L     R3,SAVER3A
         L     R5,SAVER5A
         B     OPENDDS              BRANCH PAST CONSTANTS
* ----------------------------------------------------------------- *
USERIDSC DC    CL8'USERIDS '
SORTINC  DC    CL8'SORTIN  '
SYSPRINC DC    CL8'SYSPRINT'
USRIDOUC DC    CL8'USRIDOUT'
SORTOUTC DC    CL8'SORTOUT '
SYSOUTC  DC    CL8'SYSOUT  '
*---------------------------------------------------------------------* 00112
OPENDDS  DS    0H
         HEX   DDNREPR,1,DDNFLAG
         MVC   DIAGWTO+8(8),=C'BDMUS01I'
         MVC   DIAGWTO+21(7),=C'DDNFLAG'
         MVI   DIAGWTO+29,C'='
         MVC   DIAGWTO+31(2),DDNREPR
         TM    DDNFLAG,X'3F'
         BNO   DIAGGOON
         MVC   DIAGWTO+40(9),=C' - OK -  '
DIAGGOON DS    0H
DIAGWTO  WTO   '                              * diagnosis *',ROUTCDE=11X
               ,DESC=7
         TM    DDNFLAG,X'01'
         BO    OPUSERID
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS10E     USERIDS  DD Name is missing...',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         B     NOUSERID
OPUSERID DS    0H
         OPEN  (USERIDS,INPUT)                                          00109
NOUSERID DS    0H
         TM    DDNFLAG,X'02'
         BO    OPSORTIN
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS10E     SORTIN   DD Name is missing...',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         B     NOSORTIN
OPSORTIN DS    0H
         OPEN  (SORT,OUTPUT)                                            00110
NOSORTIN DS    0H
         TM    DDNFLAG,X'04'
         BO    OPSYSPRI
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS10E     SYSPRINT DD Name is missing...',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         B     NOSYSPRI
OPSYSPRI DS    0H
         OPEN  (SYSPRINT,OUTPUT)                                        00110
NOSYSPRI DS    0H
         TM    DDNFLAG,X'08'
         BO    GOUSRIDO
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS10E     USRIDOUT DD Name is missing...',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
GOUSRIDO DS    0H
         TM    DDNFLAG,X'10'
         BO    GOSORTOU
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS10E     SORTOUT  DD Name is missing...',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
GOSORTOU DS    0H
         TM    DDNFLAG,X'20'
         BO    GOSYSOUT
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS10E     SYSOUT   DD Name is missing...',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
GOSYSOUT DS    0H
TSTEOJ16 DS    0H
         TM    DDNFLAG,X'3F'    Change if testing more DD Names
         BO    TITLPRNT
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS09E     Please correct your JCL.......',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMUS00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         B     EOJ16
*---------------------------------------------------------------------* 00112
TITLPRNT DS    0H
         MVC   LINE(133),LINE-1            BLANK OUTPUT LINE
         PUT   SYSPRINT,TITLINE            Print Title and
         PUT   SYSPRINT,CPYRLINE             Copyright notice.
         PUT   SYSPRINT,REPTLN01           And name of which Report.
         MVC   LINE(133),LINE-1            BLANK OUTPUT LINE
         PUT   SYSPRINT,LINE
         MVC   LINE(133),LINE-1            BLANK OUTPUT LINE
         PUT   SYSPRINT,LINE
*---------------------------------------------------------------------* 00112
GETINPUT EQU   *                                                        00117
         GET   USERIDS,INAREA                                           00367
         OC    INAREA(8),BLANKS                                         00119
         MVC   OUTUSID(8),INAREA
         L     R2,BIRECIN
         LA    R2,1(,R2)
         ST    R2,BIRECIN
         PUT   SORT,OUTUSID        WRITE THE RECORD                     00212
         B     GETINPUT            BACK AND START AGAIN                 00213
               EJECT                                                    00214
*---------------------------------------------------------------------* 00215
*        END OF DATA ROUTINE FOR THE PTF INPUT FILE  - DDNAME         * 00216
*        IS INPUT.                                                    * 00217
*---------------------------------------------------------------------* 00218
EOF      EQU   *                                                        00219
         CLOSE USERIDS                                                  00220
         CLOSE SORT                                                     00221
         SPACE 3                                                        00222
*---------------------------------------------------------------------* 00223
*        LINK TO SORT (THIS WAS DEVELOPED USING SYNCSORT)             * 00224
*---------------------------------------------------------------------* 00225
         LA    R1,SORTPARM         GET ADDRESS OF SORT PARMS            00226
         LINK  EP=SORT             CALL THE SORT UTILITY                00227
         LTR   R15,R15             CHECK RETURN CODE                    00228
         BNZ   SORTERR             NO GOOD - ABEND                      00229
         EJECT                                                          00230
*---------------------------------------------------------------------* 00231
*        THIS IS THE BEGINNING OF PHASE 2 - SETS UP THE               * 00232
*        FILES, VERIFIES THE EXSISTANCE OF THE TITLES FILE, AND       * 00233
*        WRITES THE FIRST SET OF HEADERS                              * 00234
*---------------------------------------------------------------------* 00235
         OPEN  (SORT,INPUT)                                             00236
         OPEN  (USRIDOUT,OUTPUT)                                        00238
         EJECT                                                          00247
*---------------------------------------------------------------------* 00251
*                                      - Delete duplicate Userids -
         MVC   HOLDUSID(8),BLANKS        Prime the hold area.
PUTOUTPT DS    0H                        Loop to write out Userids
         GET   SORT,OUTSORT              GET a sorted record.           00252
         L     R2,BIRECSR                Count records out from SORT.
         LA    R2,1(,R2)
         ST    R2,BIRECSR
         CLC   HOLDUSID(8),OUTSORT       Is this record a duplicate?
         BE    PUTOUTPT                  Yes. Go read another one.
         MVC   HOLDUSID(8),OUTSORT       No. Move for next compare.
         L     R2,BIRECOU                Count records that remain.
         LA    R2,1(,R2)
         ST    R2,BIRECOU
         PUT   USRIDOUT,OUTSORT          Put out all non-duplicates.
         B     PUTOUTPT
*---------------------------------------------------------------------* 00365
PXGET    EQU   *                   COMMON READ ROUTINE FOR USERIDS      00366
         GET   USERIDS,INAREA                                           00367
         LA    R4,INAREA           POINT TO DATA                        00369
         BR    R2                  RETURN TO CALLER                     00370
         SPACE 2                                                        00371
*---------------------------------------------------------------------* 00385
ENDSRT   DS    0H                                                       00353
         XR    R2,R2                     ZERO OUT WORK REGISTER
         L     R2,BIRECIN                SAVE RECORDS PER TRACK COUNT
         CVD   R2,CVDWORK                CONVERT TO DECIMAL
         MVC   DERECIN,CVDWORK           STORE AWAY FOR REPORTING
         XR    R2,R2                     ZERO OUT WORK REGISTER
         L     R2,BIRECSR                SAVE RECORDS PER TRACK COUNT
         CVD   R2,CVDWORK                CONVERT TO DECIMAL
         MVC   DERECSR,CVDWORK           STORE AWAY FOR REPORTING
         XR    R2,R2                     ZERO OUT WORK REGISTER
         L     R2,BIRECOU                SAVE RECORDS PER TRACK COUNT
         CVD   R2,CVDWORK                CONVERT TO DECIMAL
         MVC   DERECOU,CVDWORK           STORE AWAY FOR REPORTING
         MVC   LINE,LINE-1
         MVC   LINET(L'MSGRECIN),MSGRECIN
         MVC   LINET+34(10),MASK10
         ED    LINET+34(10),DERECIN+3
         PUT   SYSPRINT,LINE
         MVC   LINE,LINE-1
         MVC   LINET(L'MSGRECSR),MSGRECSR
         MVC   LINET+34(10),MASK10
         ED    LINET+34(10),DERECSR+3
         PUT   SYSPRINT,LINE
         MVC   LINE,LINE-1
         PUT   SYSPRINT,LINE
         MVC   LINE,LINE-1
         MVC   LINET(L'MSGRECOU),MSGRECOU
         MVC   LINET+34(10),MASK10
         ED    LINET+34(10),DERECOU+3
         PUT   SYSPRINT,LINE
         MVC   LINE,LINE-1
         PUT   SYSPRINT,LINE
         CLOSE SORT                                                     00355
         CLOSE SYSPRINT                                                 00356
         CLOSE USRIDOUT                                                 00356
         B     EOJ0
*---------------------------------------------------------------------* 00385
EOJ0     DS    0H
         LA    R5,0                    Zero Return Code
         B     EOJ
EOJ4     DS    0H
         LA    R5,4                    RC = 4
         B     EOJ
EOJ8     DS    0H
         LA    R5,8                    RC = 8
         B     EOJ
EOJ12    DS    0H
         LA    R5,12                   RC = 12
         B     EOJ
EOJ16    DS    0H
         LA    R5,16                   RC = 16
         B     EOJ
EOJ      DS    0H
         L     R13,SAVE+4
         LR    R15,R5              Load return code into R15.
         ST    R15,16(,R13)        Put RC into passed save area.
         LM    R14,R12,12(R13)
         BR    R14                 Go back to system.
         SPACE
         EJECT                                                          00361
*---------------------------------------------------------------------* 00385
* --------------------------------------------------------- *
HEX      DS    0H
         MVC   1(1,R15),0(R1)        Move byte
         UNPK  0(3,R15),1(2,R15)     Unpack
         TR    0(2,R15),HEXTAB-240
         LA    R15,2(,R15)           Increment output pointer
         LA    R1,1(,R1)             Increment input pointer
         BCT   R0,HEX                Decrement length, then loop
         MVI   0(R15),C' '           Blank the trailing byte
         BR    R4                    Return to caller
HEXTAB   DC    C'0123456789ABCDEF'   Translate table
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
HEXSAVE  DS    3F                    Register save for HEX macro
SAV4HEX  DC    F'0'
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*---------------------------------------------------------------------* 00385
*        IF THE SORT RETURNS A NON-ZERO CONDITION CODE,               * 00386
*        CONTROL IS PASSED TO THIS ABEND MACRO.                       * 00387
*---------------------------------------------------------------------* 00388
SORTERR  EQU   *                                                        00389
         ABEND 128,DUMP                                                 00390
         EJECT                                                          00391
         LTORG                                                          00392
         EJECT                                                          00393
USERIDS  DCB   DSORG=PS,MACRF=GM,EODAD=EOF,DDNAME=USERIDS,RECFM=FB,    X00394
               LRECL=8                                                  00395
         SPACE 3                                                        00396
SORT     DCB   DSORG=PS,MACRF=(PM,GM),DDNAME=SORTIN,RECFM=FB,LRECL=8,  X00397
               BLKSIZE=8000,EODAD=ENDSRT                                00398
         SPACE 3                                                        00399
USRIDOUT DCB   DSORG=PS,MACRF=PM,EODAD=EOF,DDNAME=USRIDOUT,RECFM=FB,   X00394
               LRECL=8                                                  00395
         SPACE 3                                                        00396
SYSPRINT DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=FBA,LRECL=133    00400
         SPACE 3                                                        00402
HOLDOUTS DS      0CL15            HOLD AREA FOR PREVIOUS SORT OUTPUT
HOLDUSID DC      CL8' '           PREVIOUS PTF NAME
         DC      CL8' '
BLANKS   DC      CL8' '                                                 00418
         DC      C' '
BLANK    DC      CL8' '                                                 00418
*
INAREA   DS      CL8                                                    00416
         DC      CL8' '
OUTSORT  DS      0CL8             INPUT AREA FOR SORT OUTPUT RECORD
OUTUSID  DC      CL8' '           SORT PTF NAME - 1ST 7 CHARACTERS      00417
         DS      0F                                                     00419
SORTPARM DC      X'80'                                                  00420
         DC      AL3(PARMLIST)                                          00421
         DC      H'0'                                                   00422
PARMLIST DC      AL2(PARMEND-PARMLIST)                                  00423
         DC      A(SORTCARD)                                            00424
         DC      A(SORTEND)                                             00425
         DC      A(RECCARD)                                             00426
         DC      A(RECEND)                                              00427
         DC      A(0)                                                   00428
         DC      A(0)                                                   00429
PARMEND  EQU   *                                                        00430
SORTCARD DC    C' SORT FIELDS=(1,7,CH,A)'                               00431
SORTEND  DC    C' '                                                     00432
RECCARD  DC    C' RECORD LENGTH=8,TYPE=F'                               00433
RECEND   DC    C' '                                                     00434
         DC    CL4'    '                                                00442
         DC    C' '
LINE     DC    CL133' '
LINET    EQU   LINE+7
LINEM    EQU   LINE+1
         DC    CL8' '
*--------------------------------------------------------------------*
TITLINE  DS    0CL133
         DC    C'1'
         DC    CL3' '
         DC    CL6'---'
         DC    CL79'&PGMNAME PROGRAM  -  &PKGTITL &VERTITL &VERSNUM   -X
               --'
         DC    CL46' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
CPYRLINE DS    0CL133
         DC    C'0'
         DC    CL3' '
* -->                               Currently 77 characters
         DC    C'&COPYRGT'
         DC    C'&COPYRSV'
* -->
         DC    CL52' '
         SPACE
*--------------------------------------------------------------------*
         SPACE
REPTLN01 DS    0CL133
         DC    C'-'
         DC    CL3' '
         DC    CL6'---'
         DC    CL50'&PGMNAME   ---   Userid Sort and Delete Duplicates'
         DC    C' ---'
         DC    CL69' '
*--------------------------------------------------------------------*
         DC    CL8' '
MSGRECIN DC    C'Total Userids Read Into Program :'
MSGRECSR DC    C'Total Userids Read Out from Sort:'
MSGRECOU DC    C'Total Userids Without Duplicates:'
MASK10   DC    XL10'40202020202020202120'
DDNREPR  DS    2C
CVDWORK  DS    D
DDNFLAG  DS    X
*--------------------------------------------------------------------*
BIRECIN  DS    F                 FULLWORD BINARY NUMBER
DERECIN  DS    PL8
BIRECSR  DS    F                 FULLWORD BINARY NUMBER
DERECSR  DS    PL8
BIRECOU  DS    F                 FULLWORD BINARY NUMBER
DERECOU  DS    PL8
SAVER3A  DS    F
SAVER5A  DS    F
         END                                                            00478
