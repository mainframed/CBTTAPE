BDMGTUID TITLE 'BDMGTUID - EXTRACT USERIDS DEFINED TO SYS1.BRODCAST'
         SPACE 1
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*  DOCUMENTATION:   THIS PROGRAM IS A BATCH PROGRAM WHICH EXTRACTS    *
*                   ALL USERID NAMES FROM A BROADCAST DATASET.        *
*                                                                     *
*       Copyright (C) 1995-2016 Sam Golob Systems Programming LLC     *
*                 All Rights Reserved                                 *
*                                                                     *
*       Batch Program - Execution JCL                                 *
*                                                                     *
*       // JOB CARD                                                   *
*       //*                                                           *
*       //GETUSERS EXEC PGM=BDMGTUID                                  *
*       //STEPLIB  DD DISP=SHR,DSN=your.BRODMSTR.LOADLIB              *
*       //BRODCAST DD DISP=SHR,DSN=SYS1.BRODCAST                      *
*       //SYSPRINT DD SYSOUT=*                                        *
*       //UIDDIAG  DD SYSOUT=*                           Optional     *
*       //*             UIDDIAG without BDMUSOUT will produce a       *
*       //*             diagnostic report only, on defined USERIDs.   *
*       //BDMUSOUT DD DISP=(NEW,CATLG,DELETE),                        *
*       //            DSN=your.userid.file,                           *
*       //            DCB=(LRECL=8,BLKSIZE=8000,DSORG=PS),            *
*       //            SPACE=(TRK,(15,15),RLSE),                       *
*       //            UNIT=SYSALLDA,VOL=SER=volser                    *
*       //EXPORT   DD DISP=(NEW,CATLG,DELETE),      (Optional)        *
*       //            DSN=your.EXPORT.file,                           *
*       //            DCB=(LRECL=80,BLKSIZE=8000,DSORG=PS),           *
*       //            SPACE=(TRK,(15,15),RLSE),                       *
*       //            UNIT=SYSALLDA,VOL=SER=volser                    *
*       //                                                            *
*                                                                     *
*  FUNCTION:   TO EXTRACT ALL USERIDS FROM AN EXISTING BROADCAST      *
*              DATASET.                                               *
*                                                                     *
*  AUTHOR:     SAM GOLOB               VERSION:   1.4.0B              *
*                                                                     *
*  DATE:       OCT 23, 2006                                           *
*                                                                     *
*  CHANGES:    06/11/21  -  Introduced UIDDIAG DD name and report,    *
*                             which is an optional DD name in JCL.    *
*                           Allowed UIDDIAG without BDMUSOUT for      *
*                             diagnosis of userids defined in the     *
*                             Broadcast dataset.                      *
*                           RDJFCB done for BDMUSOUT to show the      *
*                             dataset that the userids were written   *
*                             to.                                     *
*                                                                     *
*              06/11/22  -  Cleaned up errors when there are missing  *
*                             DD names.                               *
*                                                                     *
*                                                                     *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*       I N S T R E A M    M A C R O S                                *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         MACRO
         IKJZT301  &DSECT=YES
* COPIED FROM OPTIONAL MATERIAL SYM1-1(1) 29MAY80 LDW.  PL/S DELETED.
*        UPDATED                          31MAY95 SBG.
*
*        IKJZT301       RELEASE=OS/VS2.2  LEVEL=01
***********************************************************************
***********************************************************************
*                                                                     *
*                         SYS1.BRODCAST DATA SET                      *
*                                RECORD 1                             *
*                                                                     *
*        THE FIRST RECORD OF THE SYS1.BRODCAST DATA SET (R1BC) HAS    *
*        POINTERS TO THE NOTICES AND MAIL SECTIONS, AND OTHER INFO    *
*        CONCERNING THE DATA SET.                                     *
*                                           10/1/72    LEVEL=1        *
***********************************************************************
***********************************************************************
         AIF   ('&DSECT' EQ 'NO').NODSECT
R1BC     DSECT , -            RECORD 1 OF SYS1.BRODCAST DATA SET
         AGO   .SKIP
.NODSECT ANOP
R1BC     DS    0F -           RECORD 1 OF SYS1.BRODCAST DATA SET
.SKIP    ANOP
R1BCPTRP DS    0A -           SAME AS R1BCPTR BELOW
R1BCFLGS DS    B -            NOTICES FLAGS - NOT USED
R1BCPTR  DS    AL3 -          RELATIVE BLOCK ADDRESS (RBA) OF FIRST
*                               NOTICES DIRECTORY RECORD
R1USPTRP DS    0A -           SAME AS R1USPTR BELOW
R1USFLGS DS    B -            USER MAIL FLAGS - NOT USED
R1USPTR  DS    AL3 -          RBA OF FIRST USER MAIL DIRECTORY RECORD
R1RECNUM DS    F -            TOTAL NO. OF RECORDS IN SYS1.BRODCAST DS
R1BCMAX  DS    H -            MAXIMUM BRODCAST MSG NO. -
*                               FROM MASTER SCHEDULER BASEA, BABCMAX
*                                                                     *
R1DSN    DS    CL24 -         DATA SET NAME IN EBCDIC =
*                               ' SYS1.BRODCAST DATA SET '
R1LEVEL  DS    CL7 -          LEVEL NO. = 'LEVEL N', WHERE 'N' IS
*                               A 1-DIGIT NUMBER
         DS    CL1 -          RESERVED
R1FRESRH DS    CL3 -          RBA OF FREE SEARCH RECORD
R1GENNUM DS    F -            GENERATION NUMBER FOR IN-STORAGE NOTICE
*                               TABLE
         DS    CL76 -         RESERVED
         MEND
         MACRO
         IKJZT304  &DSECT=YES
* COPIED FROM OPTIONAL MATERIAL SYM1-1(1) 29MAY80 LDW.  PL/S DELETED.
*
*        IKJZT304       RELEASE=OS/VS2.2  LEVEL=01
***********************************************************************
***********************************************************************
*                                                                     *
*                         SYS1.BRODCAST DATA SET                      *
*                       USER MAIL DIRECTORY RECORD                    *
*                                                                     *
*        THE USER MAIL DIRECTORY RECORD (USDIR) IS A LOGICAL          *
*        DIRECTORY RECORD IN THE SYS1.BRODCAST DATA SET.              *
*        THE DIRECTORY CONTAINS USERIDS AND PTRS TO THE FIRST AND     *
*        LAST MESSAGE FOR EACH USERID.                                *
*                                           10/1/72    LEVEL=1        *
***********************************************************************
***********************************************************************
         AIF   ('&DSECT' EQ 'NO').NODSECT
USDIR    DSECT , -            USER MAIL DIRECTORY RECORD
         AGO   .SKIP
.NODSECT ANOP
USDIR    DS    0F -           USER MAIL DIRECTORY RECORD
.SKIP    ANOP
USDENTRY DS    0CL13 -        DIRECTORY ENTRY FOR 1 USERID
USDID    DS    CL7 -          USERID (LEFT JUSTIFIED, PADDED W/ BLANKS)
USDRBA   DS    AL3 -          RELATIVE BLOCK ADDRESS (RBA) OF FIRST
*                               MESSAGE FOR THIS USERID (ZERO IF NONE)
USDEND   DS    AL3 -          RBA OF LAST MESSAGE FOR THIS USERID
*                               (ZERO IF NONE)
         DS    8CL13 -        RESERVE SPACE FOR 8 MORE DIRECTORY
*                               ENTRIES IDENTICAL IN FORMAT TO THE
*                               PRECEDING 'USDENTRY'
         DS    XL8 -          RESERVED
USDREND  DS    CL1 -          END-OF-RECORD INDICATOR = X'7F'
USDNEXT  DS    AL3 -          CHAIN PTR TO NEXT USER MAIL DIRECTORY
*                               RECORD (ZERO IF LAST)
         MEND
         MACRO
         IKJZT305  &DSECT=YES
* COPIED FROM OPTIONAL MATERIAL SYM1-1(1) 29MAY80 LDW.  PL/S DELETED.
*
*        IKJZT305       RELEASE=OS/VS2.2  LEVEL=01
***********************************************************************
***********************************************************************
*                                                                     *
*                         SYS1.BRODCAST DATA SET                      *
*                        USER MAIL MESSAGE RECORD                     *
*                                                                     *
*        THE USER MAIL MESSAGE RECORD (USMSG) IS A RECORD IN          *
*        THE SYS1.BRODCAST DATA SET WHICH CONTAINS A MAIL MESSAGE     *
*        INTENDED FOR A PARTICULAR USERID.                            *
*                                           10/1/72    LEVEL=1        *
***********************************************************************
***********************************************************************
         AIF   ('&DSECT' EQ 'NO').NODSECT
USMSG    DSECT , -            USER MAIL MESSAGE RECORD
         AGO   .SKIP
.NODSECT ANOP
USMSG    DS    0F -           USER MAIL MESSAGE RECORD
.SKIP    ANOP
USMLNG   DS    FL1 -          LENGTH OF MAIL MSG TEXT
USMTEXT  DS    CL125 -        MESSAGE TEXT (PADDED WITH BLANKS)
USMNEXT  DS    AL3 -          CHAIN PTR TO NEXT MAIL MESSAGE RECORD
*                               FOR THIS USERID (ZERO IF LAST)
         MEND
         MACRO
&NAME    HEX   &TO,&LEN,&FROM
&NAME    STM   15,1,HEXSAVE
         LA    1,&FROM
         LA    0,&LEN
         LA    15,&TO
         BAL   4,HEX
         LM    15,1,HEXSAVE
         MEND
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         SPACE 1
*        Register Equates
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 1
SP000    EQU   0
         SPACE 1
         GBLC  &VERSNUM,&PKGNAME,&PKGTITL,&VERTITL,&COPYRGT,&COPYRSV
         LCLC  &PGMNAME
&VERSNUM SETC  ' 1.4.0B'
&PKGNAME SETC  'BRODMSTR'
&PKGTITL SETC  'Broadcast Master'
&VERTITL SETC  ' Version '
&COPYRGT SETC  'Copyright (c) 1995-2016 Sam Golob Systems Programming'
&COPYRSV SETC  ' LLC - All Rights Reserved  '
&PGMNAME SETC  'BDMGTUID'
&PGMNAME AMODE 24
&PGMNAME RMODE 24
&PGMNAME CSECT
         B     BEGINN-*(,R15)       Branch over Eyecatchers
         DC    CL8'&PGMNAME'
         DC    C' '
         DC    C'&PKGTITL'
         DC    C'&VERTITL'
         DC    C'&VERSNUM'
         DC    C' '
         DC    C'&COPYRGT'
         DC    C'&COPYRSV'
         DC    CL16' &SYSDATE &SYSTIME '
         DC    CL6'HLASM '
         DC    C'&SYSVER'
         DC    C' '
BEGINN   DS    0H
         STM   R14,R12,12(R13)
         USING BDMGTUID,R12,R11,R10
         LR    R12,R15              Set up base registers
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LA    R10,4095(,R11)
         LA    R10,1(,R10)
         LR    R5,R1                Preserve PARM Pointer.
         GETMAIN RU,LV=DATALEN,SP=SP000,LOC=BELOW
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING DATAAREA,R13
         ST    R5,SAVEPRM           PARM Pointer to Data Area Field
         L     R1,SAVEPRM           Restore PARM Pointer
         BAL   R9,INITCON           Initialize Constants in Data Area
         SPACE 1
* ----------------------------------------------------------------- *    06223
TIOTSCAN DS    0H             Determine the presence of DD names in      06224
*                             the JCL:  BRODCAST, BDMUSOUT, UIDDIAG.     06225
* ----->                      Then OPEN them if there is JCL for them.   06226
* ----->                      Then test the OPEN if it was good.         06226
* ----->                      Set DDNFLAG on if the appropriate DD       06226
* ----->                       was in the JCL.                           06226
* ----->                      Set BDMNOFLG on if the appropriate OPEN    06226
* ----->                       for the DD DIDN'T WORK!!!                 06226
* ----------------------------------------------------------------- *    06227
         MVI   DDNFLAG,X'00'        ZERO OUT DDNFLAG - Nothing OPEN
         ST    R3,SAVER3            Save work registers
         ST    R5,SAVER5
         L     R5,16                R5=CVT                               06228
         L     R5,0(,R5)            R5=TCB WORDS                         06229
         L     R5,4(,R5)            R5=MY TCB                            06230
         L     R5,12(,R5)           R5=TIOT                              06231
         LA    R5,24(,R5)           R5=FIRST DD ENTRY                    06232
TIOTLOOP CLC   0(4,R5),=XL4'00'     END OF TIOT?                         06233
         BE    TIOTSEND             Yes. Go on with OPENs.               06234
         CLC   4(8,R5),BRODCASN     BRODPRNT DDNAME there?               06235
         BNE   TIOTPRTN             No                                   06236
         OI    DDNFLAG,X'01'        Show presence of BRODCAST DD         06237
         B     TIOTNEXT             Try next entry                       06238
TIOTPRTN CLC   4(8,R5),BDMUSOUN     BDMUSOUT DDNAME there?               06239
         BNE   TIOTUSRN             No                                   06240
         OI    DDNFLAG,X'02'        Show presence of BDMUSOUT DD         06241
         B     TIOTNEXT             Try next entry                       06242
TIOTUSRN CLC   4(8,R5),UIDDIAGN     UIDDIAG DDNAME there?                06239
         BNE   TIOTSYSP             No                                   06240
         OI    DDNFLAG,X'04'        Show presence of UIDDIAG  DD         06241
         B     TIOTNEXT             Try next entry                       06242
TIOTSYSP CLC   4(8,R5),SYSPRINN     SYSPRINT DDNAME there?               06239
         BNE   TIOTEXPT             No                                   06240
         OI    DDNFLAG,X'08'        Show presence of SYSPRINT DD         06241
         B     TIOTNEXT             Try next entry                       06242
TIOTEXPT CLC   4(8,R5),EXPORTN      EXPORT   DDNAME there?               06239
         BNE   TIOTNEXT             No                                   06240
         OI    DDNFLAG,X'10'        Show presence of SYSPRINT DD         06241
         B     TIOTNEXT             Try next entry                       06242
TIOTNEXT SR    R3,R3                                                     06243
         IC    R3,0(,R5)            R3=LENGTH OF DD ENTRY                06244
         AR    R5,R3                R5=NEXT DD ENTRY                     06245
         B     TIOTLOOP             GO CHECK IT                          06246
TIOTSEND DS    0H                                                        06247
         L     R3,SAVER3            Restore work registers
         L     R5,SAVER5
         B     CHEKOPEN             Jump over Constants
* ---- >     - - - - - - - - - - - -
BRODCASN DC    CL8'BRODCAST'        Literals for the DD names.
BDMUSOUN DC    CL8'BDMUSOUT'
UIDDIAGN DC    CL8'UIDDIAG '
SYSPRINN DC    CL8'SYSPRINT'
EXPORTN  DC    CL8'EXPORT  '
* ---- >     - - - - - - - - - - - -
* ---------------------------------------------------------------- *
*                                                                  *
*   OPEN FILES -                                                   *
*                                                                  *
*    When we're finished here, we should have a good idea about    *
*    how the DD names were coded in the JCL, and which files were  *
*    actually opened.  We use 2 flags:  DDNFLAG (if on) shows      *
*    that the DD name was actually coded in the JCL.  BDMNOFLG     *
*    (if on) shows that the actual file is NOT OPEN.  In either    *
*    flag:                                                         *
*                                                                  *
*    BRODCAST DD  -  flag = X'01'                                  *
*    BDMUSOUT DD  -  flag = X'02'                                  *
*    UIDDIAG  DD  -  flag = X'04'                                  *
*    SYSPRINT DD  -  flag = X'08'                                  *
*    EXPORT   DD  -  flag = X'10'                                  *
*                                                                  *
* ---------------------------------------------------------------- *
CHEKOPEN DS    0H
         TM    DDNFLAG,X'08'        SYSPRINT there?
         BO    DOOPENS              Yes. Go OPEN it.
         WTO   'BDMGU00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMGU10E     SYSPRINT DD Name is missing...',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMGU00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
         TM    DDNFLAG,X'02'        BDMUSOUT there? Then it's allocated
         BZ    NODELUSO             Not there, no need to delete.
         WTO   'BDMGU03E     BDMUSOUT not created properly.',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMGU02E     Delete BDMUSOUT and rerun job.',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMGU00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
NODELUSO DS    0H
         TM    DDNFLAG,X'10'        EXPORT   there? Then it's allocated
         BZ    NODELMSG             Not there, no need to delete.
         WTO   'BDMGU03E     Optional EXPORT DD not proper.',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMGU02E     Delete EXPORT DSN and rerun.  ',ROUTCDE=11X
               ,DESC=7
         WTO   'BDMGU00I     ----------------------------  ',ROUTCDE=11X
               ,DESC=7
NODELMSG DS    0H
         B     EOJ16                End right away.  Code 16.
DOOPENS  DS    0H                   OPEN SYSPRINT and print titles
         OPEN  (SYSPRINT,(OUTPUT))
         PUT   SYSPRINT,TITLINE
         PUT   SYSPRINT,CPYRLINE
         PUT   SYSPRINT,BLNKLINE
         TM    DDNFLAG,X'01'        Is BRODCAST DD in JCL?
         BZ    NOBRODDD             No. Don't OPEN. Flag as Bad OPEN.
         OPEN  (BRODCAST,(INPUT))
         TM    BRODCAST+48,X'10'    Did the OPEN work?
         BO    TRYOOPN1             Yes. Go OPEN the other files.
NOBRODDD DS    0H
         OI    BDMNOFLG,X'01'       Flag that BRODCAST DD is not OPEN
*        B     TRYOOPNO
TRYOOPN1 DS    0H
         TM    DDNFLAG,X'02'        Is BDMUSOUT DD in JCL?
         BZ    NOUOUTDD             No. Don't OPEN. Flag as Bad OPEN.
         TM    DDNFLAG,X'01'        BROADCAST DATASET IN JCL?
         BZ    WMESS                No. Get out and do nothing.
         TM    BDMNOFLG,X'01'       BRODCAST DATASET NOT OPEN?
         BO    WMESS                If so, don't OPEN others.
         OPEN  (BDMUSOUT,(OUTPUT))
         TM    BDMUSOUT+48,X'10'    Did the OPEN work?
         BO    TRYOOPNA             Yes. Don't flag that it didn't.
NOUOUTDD DS    0H
         OI    BDMNOFLG,X'02'       Flag that BDMUSOUT DD is not OPEN
         B     TRYOOPNA             Go to the next DD name.
TRYOOPNA DS    0H
         TM    DDNFLAG,X'10'        Is EXPORT   DD in JCL?
         BZ    NOUOUTEX             No. Don't OPEN. Flag as Bad OPEN.
         OPEN  (EXPORT,(OUTPUT))
         TM    EXPORT+48,X'10'      Did the OPEN work?
         BO    USOUTOPN             Yes. Don't flag that it didn't.
NOUOUTEX DS    0H
         OI    BDMNOFLG,X'10'       Flag that EXPORT DD is not OPEN
         TM    BDMNOFLG,X'02'       BDMUSOUT OPENed?
         BZ    USOUTOPN             Yes. Go to the RDJFCB.
         B     TRYOOPN2             Go to the next DD name.
USOUTOPN DS    0H                   Report on BDMUSOUT Characteristics
         TM    BDMNOFLG,X'02'       BDMUSOUT OPENed?
         BO    TRYOOPN2             No. Bypass the RDJFCB.
* ----------------------------------------------------------------- *
*   BDMUSOUT has to be OPEN here, because we branched if not OPEN.  *
* ----------------------------------------------------------------- *
         RDJFCB BDMUSOUT
         MVC   OUTLINE,OUTLINE-1    Clear work area for output line
         MVC   OUTLINE,MSGDSN       Move in message skeleton
         TM    DDNFLAG,X'07'        3 DD'S are OPEN?
         BO    TRYOOPN2             Yes-then don't print here yet.
         BAL   R6,PRTJFCB           Has to be Diagnostics Only.
* ---- PRTJFCB OF EXPORT DD NAME -  BELOW
         TM    BDMNOFLG,X'10'       EXPORT OPENed?
         BO    USOUTOPW             No. Don't print RDJFCB info for it.
         RDJFCB EXPORT
         MVC   OUTLINE,OUTLINE-1    Clear work area for output line
         MVC   OUTLINE,MSGEXP       Move in message skeleton
         BAL   R6,PRTJFCB           Print the EXPORT info if there.
USOUTOPW DS    0H                   Report on EXPORT Characteristics
* ---- PRTJFCB OF EXPORT DD NAME -  ABOVE
TRYOOPN2 DS    0H
         TM    DDNFLAG,X'04'        UIDDIAG DD present?
         BZ    NOUIDDDD             No. So don't try to OPEN it.
         TM    BDMNOFLG,X'01'       BRODCAST Dataset not OPEN?
         BO    NOUIDDDD             Then flag UIDDIAG not OPEN too.
         OPEN  (UIDDIAG,(OUTPUT))
         TM    UIDDIAG+48,X'10'     Did the OPEN work?
         BO    PUTUIDTL             Yes. So put out title information.
NOUIDDDD DS    0H
         OI    BDMNOFLG,X'04'       Flag that UIDDIAG DD is not OPEN.
         B     TRYOOPNO             If so, no UID report. But go on.
PUTUIDTL DS    0H                   Begin printing UID Diagnostics.
         PUT   UIDDIAG,TITLINE
         PUT   UIDDIAG,CPYRLINE
         PUT   UIDDIAG,UIDTITLE
         PUT   UIDDIAG,BLNKLINE
         TM    DDNFLAG,X'07'        3 DD'S are OPEN?
         BNO   OUTNDIAG             Not all OPEN. Don't print JFCB here
         BAL   R6,PRTJFCB           Print characteristics of BDMUSOUT
* ---- PRTJFCB OF EXPORT DD NAME -  BELOW
         TM    BDMNOFLG,X'10'       EXPORT OPENed?
         BO    USOUTOPX             No. Don't print RDJFCB info for it.
         RDJFCB EXPORT
         MVC   OUTLINE,OUTLINE-1    Clear work area for output line
         MVC   OUTLINE,MSGEXP       Move in message skeleton
         BAL   R6,PRTJFCB           Print the EXPORT info if there.
USOUTOPX DS    0H                   Report on EXPORT Characteristics
* ---- PRTJFCB OF EXPORT DD NAME -  ABOVE
OUTNDIAG DS    0H                   Diagnostics only message.
         TM    DDNFLAG,X'02'        BDMUSOUT there?
         BO    NOMSG3               Yes. No Diagnosis Only report.
         TM    DDNFLAG,X'05'        BRODCAST and UIDDIAG only?
         BNO   NOMSG3               Not this combination. Omit message.
         BAL   R6,DIAGONLY          Diagnostics report only, for UIDs.
NOMSG3   DS    0H                   Finish up UID report Header.
         PUT   UIDDIAG,UIDDESCL
         PUT   UIDDIAG,UIDDASH
         PUT   UIDDIAG,BLNKLINE
TRYOOPNO DS    0H
         TM    DDNFLAG,X'03'        BRODCAST and BDMUSOUT OPEN?
         BO    HREAD                They are OPEN, so go on.
         TM    DDNFLAG,X'05'        BRODCAST and UIDDIAG OPEN?
         BO    HREAD                They are OPEN, so go on.
         B     WMESS                They are not OPEN - Error & Exit.
*
*
HREAD    DS    0H
         BAL   R9,HEADREAD          Read Header Record and extract
*                                   RBA of first UID record to search.
*                                   This value is stored in SAVERBA(3).
* ------------------------------------------------------------------ *
* ---    MAIN LOOP TO READ EACH USERID'S MESSAGES.               --- *
* ------------------------------------------------------------------ *
INDEXUS  DS    0H                  READ THROUGH USERID RECORDS
         CLC   SAVERBA(3),ZEROS    ARE WE AT THE END OF USERID RECORDS
         BE    ENDUSER             NO USERID LIKE THIS. DEAL WITH IT.
         ZAP   SLOTCNT,=P'0'       NEW SLOT COUNT FOR NEW READ
         LA    R7,9                COUNT OF USERS PER RECORD
         MVC   BLOCKNO,SAVERBA     GET RBA FOR READ
         READ  MDLDECB,            ECB ADDRESS TO POST                 X
               DI,                 USE DATA AND KEY                    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               BLOCKNO,MF=E        RBA OF THE RECORD
         SPACE
         CHECK MDLDECB
         L     R3,MDLDECB+12       ADDRESS OF RECORD
         USING USDIR,R3
         MVC   LASTRBA(3),SAVERBA  SAVE CURRENT RBA
         MVC   UIDPRBA(3),SAVERBA  SAVE CURRENT RBA FOR UID REPORT
         MVC   SAVERBA(3),USDNEXT  GET NEXT RECORD'S RBA
         LA    R8,0                INITIALIZE USERID SLOT COUNTER
USRLOOP  DS    0H
         AP    SLOTCNT,=P'1'       COUNT WHICH SLOT THIS IS
         MVC   USERID(8),BLANKS    ENSURE BLANK IN 8TH PLACE
         MVC   USERIDI(7),USDID    COPY WHO THIS IS
         CLC   USERIDI(7),ZEROS    IS THIS A REAL USER?
         BE    TRYNEXT             NOPE. JUST GO TO NEXT ENTRY
         TM    BDMNOFLG,X'02'      Is BDMUSOUT not OPEN?
         BO    NOBDMUSO            Yes. Can't PUT anything to it.
         PUT   BDMUSOUT,USERID     PUT OUT THIS ID TO OUTPUT FILE
NOBDMUSO DS    0H
         TM    BDMNOFLG,X'10'
         BO    NOEXPORT
         MVC   EXPOUT,EXPOUT-1
         MVC   EXPOUT+8(9),=C'BDMUSADD '
         MVC   EXPOUT+17(8),USERID
         PUT   EXPORT,EXPOUT
NOEXPORT DS    0H
         AP    USERCNT,=P'1'       INCREMENT USER COUNTER      SG 05/99
         TM    BDMNOFLG,X'04'        UIDDIAG NOT OPEN?
         BO    NOUIDPRT               THEN NO UID PRINT
*
* -- > This is the guts of the UID diagnostics report - below
*
         MVC   SLOTLINE(133),SLOTLINE-1    CLEAR SLOT LINE
         MVC   SLOTLINE+5(10),MASK10       Move in mask
         ED    SLOTLINE+5(10),USERCNT      Count of all Userids
         MVC   SLOTLINE+20(8),USERID       Name of this Userid
         HEX   SLOTLINE+30,3,UIDPRBA       Hex display of record's RBA
         MVC   SLOTLINE+40(10),BLNKLINE    Prepare for Decimal display
         L     R5,UIDPRB                   Get RBA number
         CVD   R5,DWORK                    Convert it to decimal
         MVC   SLOTLINE+40(7),MASK7A       Move in edit mask
         ED    SLOTLINE+40(7),DWORK+5      Edit the Decimal number in
         MVC   SLOTLINE+50(6),MASK6        Report which slot this is
         ED    SLOTLINE+50(6),SLOTCNT+4    Edit the slot number
         MVC   SLOTLINE+54(30),BLNKLINE    Clean out rest of the line.
         PUT   UIDDIAG,SLOTLINE
         MVC   SLOTLINE(133),SLOTLINE-1    Clear the entire SLOT LINE.
NOUIDPRT DS    0H
*
* -- > This is the guts of the UID diagnostics report - above
*
TRYNEXT  LA    R3,13(,R3)          BUMP LOCATION TO NEXT USERID
         LA    R8,1(,R8)           BUMP COUNTER WITHIN RECORD
         BCT   R7,USRLOOP          TRY FOR ANOTHER USER ENTRY
*                           FREE BUFFERS BEFORE GETTING ANOTHER RECORD.
         FREEDBUF MDLDECB,         DECB ADDR USED TO READ              X
               D,                  USED BDAM READ                      X
               BRODCAST            DCB FOR PROCESS DATA SET
         B     INDEXUS             END OF RECORD? GET ANOTHER.
         SPACE 3
* ------------------------------------------------------------------ *
* ---    END OF MAIN LOOP FOR USERID'S.                          --- *
* ------------------------------------------------------------------ *
BADEND   MVC   LINE,LINE-1                COME HERE IF ID KEY NOT X'01'
*        LA    R0,L'MESPREND+7             WRITE MESSAGE THAT
         MVC   LINET(L'MESPREND),MESPREND     THINGS ARE NOT
         PUT   SYSPRINT,LINE                     SO GOOD.
*
*        *--------------------------------*
*        *  GET HERE AT END OF USER RCDS  *
*        *--------------------------------*
* -- >      Put out the Summary Report line....
*
ENDUSER  DS    0H
         MVC   LINE,LINE-1                   YES. WE TOTAL 'EM ALL.
         PUT   SYSPRINT,LINE                   LINES AT THE END
         MVC   WK1ALL,USRALL                      WITH THE TOTAL COUNT
         MVC   WK1ALL+46(10),MASK10               AND ENDING MESSAGE
         ED    WK1ALL+46(10),USERCNT
         MVC   LINET(L'WK1ALL),WK1ALL
         PUT   SYSPRINT,LINE                THEN
         TM    BDMNOFLG,X'04'               Also doing for UIDDIAG?
         BO    ENDUEOJ                      Not there - don't do.
         PUT   UIDDIAG,BLNKLINE
         PUT   UIDDIAG,LINE
ENDUEOJ  B     EOJ                          Now get the heck out.
         SPACE 3
WMESS    DS    0H                           Crucial DD names missing.
         TM    BDMNOFLG,X'03'               BRODCAST and BDMUSOUT OPEN?
         BZ    EOJ                          Both are, just get out.
         TM    BDMNOFLG,X'01'               Is BRODCAST DD not OPEN?
         BZ    WMESS2                       OPEN--Don't write message
         MVC   LINE,LINE-1                  Clear Line
         MVC   LINEM(L'MESSAGE1),MESSAGE1   Write OPEN Error message
         PUT   SYSPRINT,LINE
         TM    DDNFLAG,X'02'       BDMUSOUT DD there?
         BZ    BYPBRODC            Yes. It needs to be deleted.
         MVC   LINE,LINE-1
         MVC   LINEM(47),=C'BDMGU03E   ---  BDMUSOUT not created properX
               ly..'
         PUT   SYSPRINT,LINE
         MVC   LINE,LINE-1
         MVC   LINEM(47),=C'BDMGU02E   ---  Delete BDMUSOUT and rerun jX
               ob. '
         PUT   SYSPRINT,LINE
BYPBRODC DS    0H
         TM    DDNFLAG,X'10'       EXPORT DD there?
         BZ    BYPRUEXP            Yes. It needs to be deleted.
         MVC   LINE,LINE-1
         MVC   LINEM(47),=C'BDMGU03E   ---  EXPORT DD not created propeX
               rly.'
         PUT   SYSPRINT,LINE
         MVC   LINE,LINE-1
         MVC   LINEM(48),=C'BDMGU02E   ---  Delete EXPORT DSN and rerunX
                job.'
         PUT   SYSPRINT,LINE
BYPRUEXP DS    0H
WMESS2   DS    0H
         TM    BDMNOFLG,X'02'               Is BDMUSOUT DD not OPEN?
         BZ    EXIT12                       No message--just bad code
         MVC   LINE,LINE-1                  Clear Line
         MVC   LINEM(L'MESSAGE2),MESSAGE2   Write OPEN Error message
         PUT   SYSPRINT,LINE
         B     EXIT12              SYSPRINT there, do Code 12.
EXIT12   DS    0H
         MVC   LINE,LINE-1
         PUT   SYSPRINT,LINE
         MVC   LINE,LINE-1
         MVC   LINEM(47),=C'BDMGM05E   ***  Please correct your JCL   *x
               **  '
         PUT   SYSPRINT,LINE
         MVC   LINE,LINE-1
         PUT   SYSPRINT,LINE
         B     EOJ12
EOJ      DS    0H                  BYE. CC = 0.
         LA    R5,0                Set return code in R5 to load later.
         B     EOJOUT
EOJ0     DS    0H
         LA    R5,0                    Zero Return Code
         B     EOJOUT
EOJ4     DS    0H
         LA    R5,4                    RC = 4
         B     EOJOUT
EOJ8     DS    0H
         LA    R5,8                    RC = 8
         B     EOJOUT
EOJ12    DS    0H
         LA    R5,12                   RC = 12
         B     EOJOUT
EOJ16    DS    0H
         LA    R5,16                   RC = 16
         B     EOJOUT
EOJOUT   DS    0H
         TM    BDMNOFLG,X'01'      Was BRODCAST DD never opened?
         BO    EOJBCCLS            Then don't try to close it.
         CLOSE (BRODCAST)
EOJBCCLS DS    0H
         TM    BDMNOFLG,X'02'      Was BDMUSOUT DD never opened?
         BO    EOJBUCLS            Then don't try to close it.
         TM    DDNFLAG,X'02'       Was BDMUSOUT DDname never coded?
         BZ    EOJBUCLS            Then don't try to close it.
         CLOSE (BDMUSOUT)
EOJBUCLS DS    0H
         TM    BDMNOFLG,X'04'      Was UIDDIAG DD never opened?
         BO    EOJUSCLS            Then don't try to close it.
         TM    DDNFLAG,X'04'       Was UIDDIAG DDname never coded?
         BO    EOJUSCLS            Then don't try to close it.
         CLOSE (UIDDIAG)
EOJUSCLS DS    0H                  Close EXPORT conditionally
         TM    BDMNOFLG,X'10'      Was EXPORT DD never opened?
         BO    EOJEXCLS            Then don't try to close it.
         TM    DDNFLAG,X'10'       Was EXPORT DDname never coded?
         BO    EOJEXCLS            Then don't try to close it.
         CLOSE (EXPORT)
EOJEXCLS DS    0H                  Close SYSPRINT after everything.
         TM    DDNFLAG,X'08'       Was SYSPRINT DD there?
         BZ    EOJNCSYS            No. Then don't CLOSE it.
         CLOSE (SYSPRINT)
EOJNCSYS DS    0H                  Bypass CLOSE if SYSPRINT missing.
         LR    R1,R13              Restore address of GETMAINed area.
         FREEMAIN RU,LV=DATALEN,A=(R1),SP=SP000
         LR    R15,R5              Restore corrupted RC register
         L     R13,4(,R13)         Prepare return to the system.
         L     R14,12(,R13)        Restore R12
         ST    R15,16(,R13)        Put RC into passed savearea
         LM    R0,R12,20(R13)      Other registers
         BR    R14                 Back to system
         EJECT
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
* - -           S  U  B  R  O  U  T  I  N  E  S               - - *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
HEX      MVC   1(1,R15),0(R1)      MOVE BYTE
         UNPK  0(3,R15),1(2,R15)   UNPACK
         TR    0(2,R15),HEXTAB-240
         LA    R15,2(,R15)         INCREMENT OUTPUT PTR
         LA    R1,1(,R1)           INCREMENT INPUT PTR
         BCT   R0,HEX              DECREMENT LENGTH, THEN LOOP
         MVI   0(R15),C' '         BLANK THE TRAILING BYTE
         BR    R4                  RETURN TO CALLER
HEXTAB   DC    C'0123456789ABCDEF' TRANSLATE TABLE
         SPACE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        Initialize Data Areas that have been GETMAINed.          *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INITCON  MVI   LINE-1,C' '                INITIALIZE BLANKER
         MVI   OUTLINE-1,X'40'            INITIALIZE BLANKER
         MVC   OUTLINE(133),OUTLINE-1     CLEAR LINE AREA
         MVC   USERCNT(5),PACKZER5        INITIALIZE COUNTER
         MVI   BDMNOFLG,X'00'             MARK ALL FILES OPENABLE
         MVI   SLOTLINE-1,X'40'           INITIALIZE BLANKER
         MVC   SLOTLINE(133),SLOTLINE-1   CLEAR SLOT LINE
         XC    DWORK,DWORK                CLEAR WORK AREA
         MVI   JFCB1,X'87'                INITIALIZE JFCB HEADER
         MVI   EXPOUT-1,X'40'
         MVC   EXPOUT,EXPOUT-1
         BR    R9
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*        READ THE SYS1.BRODCAST HEADER RECORD AND COPY FIELDS.    *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
HEADREAD DS    0H
         READ  MDLDECB,            ECB ADDRESS TO POST                 X
               DI,                 USE DATA AND KEY                    X
               BRODCAST,           DCB TO USE FOR READ                 X
               'S',                DO DYNAMIC BUFFERING                X
               'S',                GET LENGTH FROM DCB                 X
               'S',                KEY + DATA ARE READ SEQUENTIALLY    X
               BLOKZERO,MF=E       RBA OF THE RECORD
         SPACE
         CHECK MDLDECB
         L     R3,MDLDECB+12       ADDRESS OF RECORD
         USING R1BC,R3             ADDRESSABILITY TO HEADER RECORD MAP
         MVC   SAVERBA(3),R1USPTR  SAVE RBA OF FIRST USER DIR RECORD
         DROP  R3
         FREEDBUF MDLDECB,         DECB ADDR USED TO READ              X
               D,                  USED BDAM READ                      X
               BRODCAST            DCB FOR PROCESS DATA SET
         BR    R9
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
* -- BELOW ---------------  SUBROUTINE TO PRINT JFCB INFORMATION ---- *
*
PRTJFCB  DS    0H                         Print JFCB info for BDMUSOUT
         ST    R6,SAVER6                  Save the BAL register
         MVC   OUTLINE+19(44),JFCBDSNM    DSNAME
         PUT   SYSPRINT,OUTLINE
         TM    DDNFLAG,X'04'              Is UIDDIAG DD in the JCL?
         BZ    NOPRTU01                   No. Bypass printing to it.
         TM    BDMNOFLG,X'04'             UIDDIAG not OPEN?
         BO    NOPRTU01                   No. Don't print info to it.
         PUT   UIDDIAG,OUTLINE
NOPRTU01 DS    0H                         SKIP OVER UIDDIAG PRINT.
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGMBR
         MVC   OUTLINE+19(8),JFCBELNM     MEMBER NAME
         PUT   SYSPRINT,OUTLINE
         TM    DDNFLAG,X'04'              Is UIDDIAG DD in the JCL?
         BZ    NOPRTU02                   No. Bypass printing to it.
         TM    BDMNOFLG,X'04'             UIDDIAG not OPEN?
         BO    NOPRTU02                   No. Don't print info to it.
         PUT   UIDDIAG,OUTLINE
NOPRTU02 DS    0H                         SKIP OVER UIDDIAG PRINT.
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGVOLSR           VOLSER
         MVC   OUTLINE+19(6),JFCBVOLS
         MVC   OUTLINE+26(6),JFCBVOLS+6
         MVC   OUTLINE+33(6),JFCBVOLS+12
         MVC   OUTLINE+40(6),JFCBVOLS+18
         MVC   OUTLINE+47(6),JFCBVOLS+24
         PUT   SYSPRINT,OUTLINE
         TM    DDNFLAG,X'04'              Is UIDDIAG DD in the JCL?
         BZ    NOPRTU03                   No. Bypass printing to it.
         TM    BDMNOFLG,X'04'             UIDDIAG not OPEN?
         BO    NOPRTU03                   No. Don't print info to it.
         PUT   UIDDIAG,OUTLINE
NOPRTU03 DS    0H                         SKIP OVER UIDDIAG PRINT.
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGDSORG           DSORG
         MVC   OUTLINE+19(3),=C'IS '
         CLI   JFCDSRG1,JFCORGIS
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'ISU'
         CLI   JFCDSRG1,JFCORGIS+JFCORGU
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'PS '
         CLI   JFCDSRG1,JFCORGPS
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'PSU'
         CLI   JFCDSRG1,JFCORGPS+JFCORGU
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'DA '
         CLI   JFCDSRG1,JFCORGDA
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'DAU'
         CLI   JFCDSRG1,JFCORGDA+JFCORGU
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'PO '
         CLI   JFCDSRG1,JFCORGPO
         BE    PUTDSRG
         MVC   OUTLINE+19(3),=C'POU'
         CLI   JFCDSRG1,JFCORGPO+JFCORGU
         BE    PUTDSRG
         OI    FLAG1,X'01'                FLAG DSORG MISSING
         MVC   OUTLINE+19(15),=C'MISSING OR VSAM'
PUTDSRG  PUT   SYSPRINT,OUTLINE
         TM    DDNFLAG,X'04'              Is UIDDIAG DD in the JCL?
         BZ    NOPRTU04                   No. Bypass printing to it.
         TM    BDMNOFLG,X'04'             UIDDIAG not OPEN?
         BO    NOPRTU04                   No. Don't print info to it.
         PUT   UIDDIAG,OUTLINE
NOPRTU04 DS    0H                         SKIP OVER UIDDIAG PRINT.
RECFM    DS    0H
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGRECFM           RECFM
         LA    R4,OUTLINE+19
         TM    JFCRECFM,JFCUND
         BNO   NOTUND
         MVI   0(R4),C'U'
         OI    FLAG2,X'01'                FLAG RECFM=U
         A     R4,ONE
         B     CHKB
NOTUND   TM    JFCRECFM,JFCFIX
         BNO   NOTFIX
         MVI   0(R4),C'F'
         A     R4,ONE
NOTFIX   TM    JFCRECFM,JFCVAR
         BNO   CHKB
         MVI   0(R4),C'V'
         A     R4,ONE
CHKB     TM    JFCRECFM,JFCRFB
         BNO   NOTBLK
         MVI   0(R4),C'B'
         A     R4,ONE
NOTBLK   TM    JFCRECFM,JFCRFS
         BNO   NOTSCN
         MVI   0(R4),C'S'
         A     R4,ONE
NOTSCN   TM    JFCRECFM,JFCASA
         BNO   NOTASA
         MVI   0(R4),C'A'
         A     R4,ONE
NOTASA   TM    JFCRECFM,JFCMAC
         BNO   PUTRCFM
         MVI   0(R4),C'M'
PUTRCFM  PUT   SYSPRINT,OUTLINE
         TM    DDNFLAG,X'04'              Is UIDDIAG DD in the JCL?
         BZ    NOPRTU05                   No. Bypass printing to it.
         TM    BDMNOFLG,X'04'             UIDDIAG not OPEN?
         BO    NOPRTU05                   No. Don't print info to it.
         PUT   UIDDIAG,OUTLINE
NOPRTU05 DS    0H                         Skip over UIDDIAG print.
LRECL    DS    0H
         MVC   OUTLINE,OUTLINE-1          Clear line.
         MVC   OUTLINE,MSGLRECL           Put LRECL skeleton into it.
         SR    R4,R4                      Clear work register
         LH    R4,JFCLRECL                Load LRECL in from JFCB
         C     R4,=F'0'                   Is it zero?
         BNE   LRECPRT                    No. Don't check for RECFM=U.
         TM    FLAG2,X'01'                Is RECFM=U?
         BO    LRECPRT                    Yes-0 is legitimate.
         OI    FLAG1,X'04'
         MVC   OUTLINE+19(7),=C'MISSING'
         B     LRECPUT
LRECPRT  DS    0H
         CVD   R4,DECVALUE
         MVC   OUTLINE+19(12),EDPAT       LRECL
         ED    OUTLINE+19(12),DECVALUE+3
LRECPUT  PUT   SYSPRINT,OUTLINE
         TM    DDNFLAG,X'04'              Is UIDDIAG DD in the JCL?
         BZ    NOPRTU06                   No. Bypass printing to it.
         TM    BDMNOFLG,X'04'             UIDDIAG not OPEN?
         BO    NOPRTU06                   No. Don't print info to it.
         PUT   UIDDIAG,OUTLINE
NOPRTU06 DS    0H                         SKIP OVER UIDDIAG PRINT.
BLKSIZE  MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE,MSGBLKSZ
         SR    R4,R4
         LH    R4,JFCBLKSI
         C     R4,=F'0'
         BNE   BLKSIZEG                   BLOCK SIZE IS GOOD
         OI    FLAG1,X'02'                FLAG BLKSIZE MISSING
         MVC   OUTLINE+19(15),=C'ZERO OR MISSING'
         B     BLKSZPUT
BLKSIZEG CVD   R4,DECVALUE
         MVC   OUTLINE+19(12),EDPAT       BLKSIZE
         ED    OUTLINE+19(12),DECVALUE+3
BLKSZPUT PUT   SYSPRINT,OUTLINE
         TM    DDNFLAG,X'04'              Is UIDDIAG DD in the JCL?
         BZ    NOPRTU07                   No. Bypass printing to it.
         TM    BDMNOFLG,X'04'             UIDDIAG not OPEN?
         BO    NOPRTU07                   No. Don't print info to it.
         PUT   UIDDIAG,OUTLINE
         PUT   UIDDIAG,BLNKLINE
NOPRTU07 DS    0H                         SKIP OVER UIDDIAG PRINT.
         L     R6,SAVER6
         BR    R6
* -- ABOVE ---------------  SUBROUTINE TO PRINT JFCB INFORMATION ---- *
DIAGONLY DS    0H                  Diagnostics Only
* - - - >
         ST    R6,SAVER6
         MVC   LINE,LINE-1                  Clear line.
         MVC   LINET(L'MESSAGE3),MESSAGE3   Write no BDMUSOUT output.
         PUT   SYSPRINT,LINE
         PUT   SYSPRINT,BLNKLINE
         PUT   UIDDIAG,LINE                 Also write to UIDDIAG reprt
         PUT   UIDDIAG,BLNKLINE
         L     R6,SAVER6                    Restore BAL register
         BR    R6                           Go back to caller.
* - - - >
         EJECT
*-----------------------------------------------------------------*
*----         FIXED DATA AREAS THAT DON'T CHANGE.             ----*
*-----------------------------------------------------------------*
*--------------------------------------------------------------------*
TITLINE  DS    0CL133              Program Title on Report
         DC    C'1'
         DC    CL3' '
         DC    CL6'---'
         DC    CL79'&PGMNAME PROGRAM  -  &PKGTITL &VERTITL &VERSNUM   -X
               --'
         DC    CL46' '
         SPACE
*--------------------------------------------------------------------*
UIDTITLE DS    0CL133              Userid Diagnostics Report Title
         DC    C'0'
         DC    CL11' '
         DC    CL3'-  '
         DC    CL79'&PGMNAME Program  -  Userid Diagnosis Report  -    X
                 '
         DC    CL41' '
*--------------------------------------------------------------------*
UIDDESCL DS    0CL133              Userid Diagnostics Report Header
         DC    C'0'
         DC    CL6' '
         DC    CL9'UID Count'
         DC    CL4' '
         DC    CL8'Userid  '
         DC    CL1' '
         DC    CL8'RBA Hex '
         DC    CL2' '
         DC    CL8'RBA Dec '
         DC    CL2' '
         DC    CL8'Slot #'
         DC    CL76' '
*--------------------------------------------------------------------*
UIDDASH  DS    0CL133              Dashes under Report Heading
         DC    C' '
         DC    CL6' '
         DC    CL9'---------'
         DC    CL4' '
         DC    CL8'------  '
         DC    CL1' '
         DC    CL8'------- '
         DC    CL2' '
         DC    CL8'------- '
         DC    CL2' '
         DC    CL8'------'
         DC    CL76' '
*--------------------------------------------------------------------*
         SPACE
CPYRLINE DS    0CL133              Copyright information for program
         DC    C'0'
         DC    CL3' '
* -->                              Currently 77 characters
         DC    C'&COPYRGT'
         DC    C'&COPYRSV'
* -->
         DC    CL48' '
         SPACE
*--------------------------------------------------------------------*
BLNKLINE DC    CL133' '            Blank Line sample
*-----------------------------------------------------------------*
FLAG1    DC    X'00'
*              X'01'               DSORG IS MISSING
FLAG2    DC    X'00'
ONE      DC    F'1'
DECVALUE DS    CL8
EDPAT    DC    X'402020206B2020206B202120'
PACKZERO DC    PL3'0'
PACKZER5 DC    PL5'0'
FULLZERO DC    F'0'
* - - - - - - - - - - - - - - - - - - - - - - - - *
FF       EQU   X'FF'
F16      DC    F'16'
F00      DC    F'0'
BLANKS   DC    C'        '         8 BLANKS
BLOKZERO DC    X'000000'           BLOCK NUMBER OF BROADCAST HEADER
ZEROS    DS    0CL8
ZERENTRY DC    X'000000000000'     SHOW USERID HAS NO MESSAGES WAITING
         DC    X'0000'
MESSAGE1 DC    C'BDMGU01E   ---  BRODCAST DD Name Missing  ---  '
MESSAGE2 DC    C'BDMGU01E   ---  BDMUSOUT DD Name Missing  ---  '
MESSAGE3 DC    C'Diagnostic Report Only - No BDMUSOUT was produced.   '
USRALL DC C'Total # of Userids Dumped to BDMUSOUT DD Name             '
MESPREND DC    C'?????  Preliminary end of USERID Records - - *'
MSGEXP   DC    CL133'0   EXPORT   DSN ='
MSGDSN   DC    CL133'0   BDMUSOUT DSN ='
MSGVOLSR DC    CL133'         VOLSER  ='
MSGMBR   DC    CL133'         MEMBER  ='
MSGDSORG DC    CL133'         DSORG   ='
MSGBLKSZ DC    CL133'         BLKSIZE ='
MSGLRECL DC    CL133'         LRECL   ='
MSGRECFM DC    CL133'         RECFM   ='
         DC    CL133' '
MASK6    DC    XL6'402020202120'
MASK7A   DC    XL7'40202020212020'
MASK10   DC    XL10'40202020202020202120'
         DS    0F                  NEEDED FOR CORRECT EXLST
JFCB1    DC    X'87'
         DC    AL3(INFMJFCB)
         IEFJFCBN LIST=YES
         SPACE 3
BRODCAST DCB   DDNAME=BRODCAST,    JCL CONNECTION                      X
               BLKSIZE=129,        LENGTH OF FILES BLOCK               X
               DSORG=DA,           IS DIRECT ACCESS FILE               X
               MACRF=(RISXC,WIC),  3 BYTE RBA, DYNAMIC BUF, READ/CHECK X
               OPTCD=R,            3 BYTE RBA WILL BE USED             X
               RECFM=F,            FIXED LENGTH                        X
               BUFNO=2,            # OF BUFS TO GET DYNAMICALLY        X
               KEYLEN=1,           SIZE OF KEY                         X
               BUFL=130            KEYLEN + BLKSIZE
         SPACE 3
SYSPRINT DCB   MACRF=PM,DDNAME=SYSPRINT,DSORG=PS,RECFM=FA,LRECL=133,   X 05426
               BLKSIZE=133                                               05427
UIDDIAG  DCB   MACRF=PM,DDNAME=UIDDIAG,DSORG=PS,RECFM=FA,LRECL=133,    X 05426
               BLKSIZE=133                                               05427
BDMUSOUT DCB   DDNAME=BDMUSOUT,MACRF=PM,DSORG=PS,RECFM=FB,             X
               LRECL=8,EXLST=JFCB1
EXPORT   DCB   DDNAME=EXPORT,MACRF=PM,DSORG=PS,RECFM=FB,EXLST=JFCB1,   X
               LRECL=80
         LTORG
         SPACE
         DS    0F
         SPACE 3
*-----------------------------------------------------------------*
*----         GETMAINED DATA AREA FIELDS                      ----*
*-----------------------------------------------------------------*
DATAAREA DSECT
SAVE     DS    9D
DWORK    DC    D'0'
CMBUFAD  DS    F                   COMMAND BUFFER ADDRESS
NEWID    DC    C'        '         STORE PARM USERID HERE
         DS    CL72                PADDING
ADDADR   DS    0CL12
USERCNT  DC    PL5'0'              COUNT OF ALL MESSAGES DISPLAYED
SLOTCNT  DC    PL5'0'              COUNT OF SLOT NUMBER IN UID RECORD
BLOCKNO  DC    X'000000'           STORE BLOCK NUMBER FOR READ
LASTRBA  DC    X'000000'           STORE CURRENT RBA
UIDPRB   DS    0F
UIDPRBAX DC    X'00'
UIDPRBA  DC    X'000000'           STORE CURRENT RBA FOR DIAG PRINT
SAVERBA  DC    X'000000'           STORE RBA FOR LATER READ
MYUSSLOT DC    X'00'               STORE SLOT OF MY USERID IN RECORD
NWUSRBA  DC    X'000000'           STORE RBA OF NEW USERID FOR LATER
NWUSSLOT DC    X'00'               STORE SLOT OF NEW USERID IN RECORD
USERID   DS    0CL8                STORE FOUND USERID HERE
USERIDI  DS    CL7                 STORE FOUND USERID HERE
         DS    0F                  ALIGN ON FULLWORD
SAVER3   DS    F                   WORK REGS FOR TIOT SCAN
SAVER5   DS    F                   WORK REGS FOR TIOT SCAN
SAVEPRM  DS    F                   TEMP SAVE FOR PARM POINTER
SAVER6   DS    F                   BAL REGISTER SAVE AREA
SAVER9A  DS    F                   BAL REGISTER SAVE AREA
SAVER9B  DS    F                   BAL REGISTER SAVE AREA
HEXSAVE  DS    3F                  SAVE FOR HEX MACRO
LINEB    DS    C                   LINE-1
LINE     DS    CL133
LINET    EQU   LINE+7
LINEM    EQU   LINE+1
OUTLINEB DS    C                   OUTLINE-1
OUTLINE  DS    CL133
         DS    0F
RDWR     READ  MDLDECB,DI,MF=L
RDWREND  DS    0F
RDWRLEN  EQU   *-RDWR
WK1ALL DC C'                                                          '
BDMNOFLG DS    X
*        EQU   X'01'               BRODCAST DD MISSING
*        EQU   X'02'               BDMUSOUT DD MISSING
*        EQU   X'04'               UIDDIAG  DD MISSING
         SPACE 1
DDNFLAG  DS    X
         DS    CL6                 FILLER
         DS    C
SLOTLINE DS    CL133
         DS    CL6
         DS    X
EXPOUT   DS    CL80
DATALEN  EQU   *-DATAAREA          LENGTH OF DATA AREA
         IKJZT301 DSECT=YES
         IKJZT304 DSECT=YES
         IKJZT305 DSECT=YES
         CVT  DSECT=YES,LIST=YES
         END
