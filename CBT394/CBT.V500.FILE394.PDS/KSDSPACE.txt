//*.......  JOB  ....your jobcard......
//*
//*
//ASM      EXEC PGM=ASMA90,PARM='OBJECT,NODECK,NOALIGN,LINECOUNT(64)'
//SYSPRINT DD SYSOUT=*
//SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.MODGEN,DISP=SHR
//SYSPUNCH DD DUMMY
//SYSUT1   DD UNIT=SYSALLDA,SPACE=(1024,(120,120))
//SYSLIN   DD UNIT=SYSALLDA,SPACE=(3040,(40,40),RLSE),
//            DCB=(RECFM=FBS,LRECL=80,BLKSIZE=0),
//            DSN=&&SYSLIN,DISP=(NEW,PASS)
//SYSIN    DD *
         TITLE 'KSDS FILE SPACE DISTRIBUTION ANALYSIS ROUTINE'
*
* "COPYRIGHT (C) 2002 by Jan (Janek) Jakubek.
* ALL RIGHTS  RESERVED EXCEPT:  PARTICULAR LICENSE IS GRANTED TO
* DISTRIBUTE THIS PROGRAM FREE OF CHARGE, BUT IT MUST NOT BE SOLD."
*
* KSDS FILE SPACE DISTRIBUTION ANALYSIS ROUTINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* THIS VERSION DOES A SEQUENTIAL PROCESSING OF THE DATA COMPONENT     *
* RESULTING IN BETTER PERFORMANCE (VSAM BUFFER READ AHEAD) IF EXTRA   *
* DATA BUFFERS ARE SPECIFIED.                                         *
*                                                                     *
* This version has been tested on OS/390 2.10. I assume it should work*
* with DFSMS 1.3 and up, although I do not have older versions of     *
* DFSMS to test under.                                                *
* If you are running DFSMS 1.2 or older version of DFP (unsupported) -*
* you may want to use the previous version of this program: KSDSPACO  *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* SPECIFICATIONS:
*
* MODULE NAME:       KSDSPACE
*
* ATTRIBUTES :       STANDARD (NOT AUTHORIZED) *** SEE NOTE #1 ***
*                    RMODE 24
*                    AMODE 24
*
* ASSEMBLER OPTIONS: NOALIGN
*
* Notes:
* 1. If KSDSPACE is linkedited with AC=1 into an APF authorized
*    LOADLIB - it can process BCS of an ICF catalog as well.
*    RACF ALTER authority to the catalog is also required in order
*    to open it as a dataset.
*
* FUNCTION = SEE BELOW
*
*     THE PURPOSE OF THIS ROUTINE IT TO READ THE INDEX (SEQUENCE SET
*  ONLY) AND DATA COMPONENTS OF A KSDS CLUSTER AND PRODUCE SPACE
*  UTILIZATION REPORTS FOR:
*  .EACH CONTROL AREA (DATA COMPONENT ONLY)
*  .INDEX COMPONENT
*  .THE WHOLE DATA COMPONENT
*  TWO REPORTS ARE PRINTED: ONE WITH NUMERIC VALUES (TABLE LIKE FORMAT)
*  AND ANOTHER WHERE SPACE DISTRIBUTION IS PRESENTED IN A GRAPHICAL
*  FORMAT: A LINE IS PRINTED FOR EACH CONTROL AREA. IT REPRESENTS
*  100% OF THE SPACE. DIFFERENT CHARACTERS REPRESENT BREAKDOWN OF
*  THAT SPACE INTO THE FOLLOWING COMPONENTS:
*
*  * - SPACE OCCUPIED BY DATA RECORDS.
*  - - UNUSED CONTROL INTERVALS RESULTING FROM CA FREE SPACE
*      SPECIFICATION AND/OR CA SPLITS (OR RECORD DELETES).
*  X - UNUSED SPACE IN CONTROL INTERVALS CONTAINING DATA RECORDS
*      RESULTING FROM CI FREE SPACE SPECIFICATION AND/OR CI SPLITS
*      (OR RECORD DELETES).
*  + - UNUSED CONTROL INTERVALS RESULTING FROM LACK OF SPACE IN THE
*      INDEX CI (INDICATION THAT INDEX CI size IS TOO SMALL).
*      THIS IS AN ESTIMATE ONLY - NOT A PRECISE VALUE AND IT CAN ONLY
*      SHOW UP WHEN A CA IS FULLY LOADED (NO CA FREE SPACE).
*      Usually it is an undrestimate.
*
*  "-", "+" WILL NEVER BE SHOWN FOR AN INDEX COMPONENT. ALSO, "X"
*  HAS A DIFFERENT MEANING: EITHER INDEX CI IS TWO LARGE OR IT RESULTS
*  FROM "-" TYPE FREE SPACE IN THE DATA COMPONENT.
*
*  THE REPORTS CAN BE USEFUL FOR TUNNING OF A KSDS FILE: CI SIZE
*  SELECTION (BOTH INDEX AND DATA COMPONENTS), FREE SPACE
*  SPECIFICATION, CA SIZE SPECIFICATION, AND TO MAKE A DECISION TO
*  REORGANIZE A FILE.
*
*
*
*
* RETURN CODES FROM THE PROGRAM:
*     0 - NORMAL SUCCESSFUL COMPLETION
*    16 - ERROR. CHECK MESSAGES IN SYSPRINT AND/OR SYSTEM LOG.
*
*
*    THE FOLLOWING JCL PROCEDURE CAN BE CATALOGED & INVOKED
*    TO RUN THIS PROGRAM:
*
*        //KSDSPACE PROC DISP=SHR,RPTT='SYSOUT=*',RPTG='SYSOUT=*',
*        //              LHKR='SYSOUT=*'
*        //KSDSPACE EXEC PGM=KSDSPACE
*        //STEPLIB    DD DSN=SYS1.UTIL.LINKLIB,DISP=SHR
*        //KSDS       DD DSN=&KSDS,DISP=&DISP
*        //SYSPRINT   DD SYSOUT=*           Diagnostic MESSAGE FILE
*        //RPTT       DD &RPTT              TABLE FORMAT REPORT
*        //RPTG       DD &RPTG              GRAPHICAL FORMAT REPORT
*        //LHKR       DD &LHKR              Low/High key     REPORT
*    (LHKR can be written to a DASD data set for viewing/editing,
*     particulary if the key is non-printable/binary/numeric).
*
*    "KSDS" SYMBOLIC PARAMETER HAS TO SPECIFY THE NAME OF THE KSDS
*    CLUSTER.
*    A REPORT CAN BE SUPRESSED BY SPECIFYING RPTT/RPTG/LHKR=DUMMY.
*
* WRITTEN BY:
*            Jan (Janek) Jakubek
*     TEL  : 416-255-8807 (Canada, Ontario)
*     EMAIL: Jan.Jakubek@CGI.COM
*
* Date first version written: MARCH-APRIL 1989
*
* Known restrictions:
* 1. Since the program uses Control Interval Access (OPTCD=CNV) -
*    RLS and compressed datasets are not supported.
*
* KNOWN PROBLEMS, NOT YET FIXED:
* 1. None known to me (JJ)
*
* FUTURE ENHANCEMENTS IDEAS (in no particular order):
* 1. KEY-RANGE DATASET SUPPORT (LOW PRIORITY, SUPPORT FOR KEY-RANGE
*    will BE REMOVED FROM VSAM IN THE FUTURE).
* 2. The current Index Record Short free space is an estimate only.
*    If you see any CA with this - it is a sure sign that there is
*    a problem. However there may be a problem and yet KSDSPACE
*    will report the free CIs as CA FREESPACE.
*    I'm thinking on improving the estimate (and avoid rising false
*    alarms), however I wish I could find out how VSAM does it
*    (decides that it cannot address more CIs in a CA, given a free
*     space in an Index Sequence Set CI).
* 3. This may not be a problem, however: I get the first SS IX record
*    from AMDSSRBA in AMDSB (OCO only CB). I know that AMDSSRBA is
*    expressed in bytes for IMBED index KSDS. I do not know if this
*    always is the case however (like for EF/EA datasets).
*    In all other KSDS examples available to me (not IMBED IX) - the
*    first SS record is always the first CI in the index component
*    so its RBA is always zero. Apparently VSAM doesn't build the
*    2nd level index until there are at least 2 sequence set records,
*    meaning at least two data CAs containing data. Thus the 1st index
*    record in a non-imbedded cluster will aways be the first sequence
*    set record, hence RBA 0. For RBA=0 it is impossible to discern
*    if AMDSSRBA represents bytes or relative CI number.
*    If a contrary case is ever found, the commented out code above
*    label IXCIPROC may be helpful in fixing a problem (likely
*    a GETIX read error).
* 4. Convert the use of OCO control blocks to CSI search. I did not
*    researched (yet) if all data used by KSDSPACE can be retrieved via
*    CSI though.
* 5. For empty CAs (low/high key per CA report) add a second line
*    with high key value.
* 6. Add an average compressed key length per CA column in one of the
*    reports (to be decided which one) a la Mainstar VSAM Manager.
*    This may help with enh. # 2 above ("dead" CIs estimate).
*
* CHANGE ACTIVITY:
*
*  April 5/2004, JJ:
*    .On a suggestion from Gerhard Postpischil added a local
*     customization equate:
*     RPTLPP EQU 65 (report lines per page)
*     to make it easier to change in case the default is not
*     appropriate in a particular (print) environment.
*
*  May   7/2003, JJ:
*    .Changed Extended Addressability determination to a CSI call
*     (from OBTAIN/format 1 DSCB). Is seems DSCB1 extended format
*     flags may not mean the Extended Addressability.
*
*  Dec. 11/2002, JJ:
*    .Added Extended Addressability KSDS support. In the EA KSDS, RBA
*     in index records is expressed as relative CI number: this means
*     that it needs to be multiplied by CI length to calculate a true
*     RBA value.
*
*    .Converted RPL OPTCD from RBA to XRBA (this is required if
*     Ext-Addr KSDS with size above 4GB is being processed: RBA
*     should work until 4GB RBA is reached. It would fail from 4GB and
*     above). Although, apparently, with PTF for APAR OW43595 applied -
*     XRBA is required for Extended Addressability datasets regardless
*     of dataset size.
*
*    .Removed check for DFSMS 1.3 or above. This means that lower
*     (unsupported) levels of DFSMS are not supported. Use the old
*     version of this program: KSDSPACO if running DFSMS 1.2 or older
*     version of DFP, or a version that does not support XRBA
*     (I do not know when XRBA was introduced).
*
*    .Replaced CI RBA in LOW/HIGH key report by CI# (relative CI
*     number). This is due to difficulty in handling large (8 bytes)
*     binary numbers (arithmetic and conversion to printable).
*
*    .Moved some control blocks/macros and report header lines to
*     KSDSPACD csect to alleviate addressability constraints of
*     the mainline CSECT.
*
*    .Removed bytes column from RPPT FREESPACE-CA and IX REC SHORT
*     columns. This is related to restructuring of SUS (Space
*     Utilization Summary) control block.
*
*    .Converted all bytes counts from 4 bytes binary to 8 bytes.
*     This is in support of files larger than 4GB.
*     If data component size is > 4 GiB - the grand total byte counts
*     are rounded to 1KiB.
*
*    .For empty control areas added message in the key column:
*     "***** EMPTY Control Area *****"
*     (an empty CA has all logical data records deleted).
*     The previous version would incorrectly print a line with high
*     key from the previous CA.
*
*    .Separated all data fields/constants from code (this
*     apparently impacts performance/ CPU time on z hardware).
*
*  Sept  4/01,JJ:
*
*    .Added Low/High key report: for every CA two lines are printed/
*     written to LHKR (ddname) datasets. Line/data format is:
*     CA#          - control area number
*     CI RBA       - RBA of CI in which the Low/High key has occurred
*     Low/High key - Low (first line for a CA)/ High (second line for
*                    a CA) key as found in data CIs of a CA.
*     The Low/High key are as found in data records (every data record
*     is processed) and not the values from index Sequence Set CI.
*
*    .Fixed a serious error: RBA of data CI being read was always
*     within the first CA. As a result the CI free/used space was
*     always incorrect (except of the first CA). This was due to
*     my missinterpretation of the vertical pointer (IBFLP3).
*     IXHBRBA (CA base RBA) has to be added to vertical pointer
*     to get a data CI RBA.
*
*  Aug  24/01,JJ:
*    .Added report on index structure: # of IX records per IX level.
*     This gets printed on RPTT only after IX stats. If index is
*     IMBED - the count of records for IX level 1 (Sequence Set)
*     is always 0 and is not valid. I do not bother to fix this
*     (a sequential read of IMBED index will not read SS - SEOF
*     occurs before SS) since the SS IX record count is the
*     same Used Conrol Areas count printed at the end.
*
*  Aug  20/01,JJ:
*    .Eliminated second header line on page #1 of RPTT (table format
*     report).
*
*  DEC  03/99,JJ:
*    .MINOR CHANGE TO ALLOW ICF BCS PROCESSING (ONLY IF KSDSPACE IS
*     LINKEDITED WITH AC(1), INTO AN APF AUTHORIZED LOADLIB).
*     TO OPEN A CATALOG AS A DATASET, YOU MUST HAVE ALTER AUTHORITY TO
*     THE CATALOG AND APF AUTHORIZATION.
*
*  JUNE 24/99,JJ:
*    .FIXED ABEND 0C4/ INVALID SECOND LINE ON SYSPRINT REPORT
*     (DIAGNOSTIC MSGS). SECOND PAGE HEADER LINE ADDRESS WAS
*     MISSING RESULTING IN EITHER GARBAGE LINE OR 0C4.
*    .FIXED A MINOR LOGIC ERROR. NUMBER OF FREE (UNUSED) CA'S
*     AT EOF WAS ALWAYS 0 (DFSMS 1.3 OR UP).
*
*  SEPT. 2/98,JJ:
*     CALCULATION OF % OF USED/ FREE SPACE FAILED  (RPTTSDLP LABEL)
*     0C7 ABEND IN "MP    D,=PL3'1000'". REPLACED THIS WITH A SHIFT OF
*     2 GPRS (12 BITS, 3 ZONES)
*
*  OCT.  7/97,JJ:
*     ITEM #1 BELOW IS NOT ADDRESSED YET. I DO NOT HAVE TIME AND
*     A KEY RANGE KSDS TO WORK WITH.
*     ITEM #2 BELOW IS FIXED - ADDED CODE TO DETERMINE THE
*     DFP LEVEL AND INTERPRET RBA AS # OF CI'S ON DFSMS 1.3.0 AND UP.
*
*  OCT.  7/97,JJ:
*     FOUND OUT TWO PROBLEMS THAT I INTEND TO FIX:
*     1.I USE ONLY FIRST ARDB (ADDRESS RANGE DEFINITION BLOCK).
*       THIS IS FINE FOR A REGULAR KSDS FILE. IT MIGHT BE INCORRECT
*       THOUGH FOR A KEYRANGE DATA SET (THERE IS AN ARDB FOR EVERY
*       KEYRANGE). WILL PROCESS ALL ARDB'S AND SELECT THE ONE WITH
*       HIGHEST RBA.
*     ??? AS OF TODAY, I DO NOT UNDERSTAND THE KEYRANGE DATA SET
*     ??? PROCESSING. THE # OF FREE CA'S AT THE END OF A DATASET MAY
*     ??? BE INCORECT. ALSO, IS EVERY KEYRANGE PROCESSED AS A SEPARATE
*     ??? DATASET ?
*     2.AS OF DFSMS 1.3 RBA'S IN ARDB ARE EXPRESSED IN NUMBER OF CI'S
*       RATHER THAN BYTES. AS A RESULT - # OF CA'S AT THE END OF
*       A DATA SET IS ALWAYS REPORTED AS 0.
*
*    APR. 10/97,JJ:
*       FREE CA CI'S DUE TO IX RECORD TOO SHORT CALCULATION WAS
*       INCORRECT. MADE A CORRECTION AND SEEMS TO BE OK NOW.
*
*    AUG. 27/92,JJ: A HEADER LINE WAS MISSING FOR RPTT REPORT ON ALL
*       LINES EXCEPT OF LINE ONE. CORRECTED THIS.
*
*    JUNE 23/92,JJ: ADDED EXTRA LINE IN THE COMPONENT STATISTICS PART
*       OF THE REPORTS.
*       ADDED NUMBER OF CI'S (#CI HEADER) COLUMN FOR THE CA FREE SPACE,
*       UNUSED CI'S AND USED SPACE.
*
         SPACE 2
*  Acknowledgment:
*  SUBENT and SUBRET macro are of an unknown origin.
*  I borrowed them from some public domain (I think) code an use
*  them here and in other programs.
*
*  "SUBENT" - SUBROUTINE ENTER MACRO
*  "SUBENT" IS USED AT ENTRY TO INTERNAL SUBROUTINE
*  LINK REGISTER IS ALWAYS R14
         MACRO
&LAB     SUBENT
         SPACE
&LAB     DS    0F
         B     *+16                     AROUND EYE CATCHER AND R14 SAVE
         DC    CL8'&LAB'                EYE CATCHER
         DC    F'0'                     R14 SAVE
         ST    R14,*-4                  STORE R14
         SPACE
         MEND
*  "SUBRET" - SUBROUTINE RETURN MACRO
*  "SUBRET" IS USED TO EXIT FROM AN INTERNAL SUBROUTINE
         SPACE
         MACRO
&LAB1    SUBRET &LAB
         SPACE
&LAB1    L     R14,&LAB+12              RESTORE R14
         BR    R14                      RETURN
         SPACE
         MEND
         SPACE
         PRINT NOGEN
KSDSPACE CSECT                    CSECT NAME
KSDSPACE AMODE 24
KSDSPACE RMODE 24
         USING KSDSPACE,R15
         SAVE  (14,12)            SAVE REGISTERS
         CNOP  0,4                ALIGNEMENT ON A FULLWORD BOUNDARY
         BAL   R2,SAE             SAVE AREA ADDRESS
         DROP  R15
SA       DS    18F                SAVE AREA
         USING SA,R2
SAE      ST    R13,SA+4           CHAIN
         ST    R2,8(R13)          SAVE AREAS
         LR    R13,R2             CURRENT SAVE AREA
         DROP  R2
         USING SA,R13,R12         PROGRAM BASE REGISTER
         LA    R12,2048(,R13)     SECOND BASE REGISTER
         LA    R12,2048(,R12)
         SPACE
* REGISTERS EQUATES
         YREGS ,
         SPACE
TCHR     EQU   R2                 HEX TRANSLATED CHARACTER REGISTER
HEXLEN   EQU   R3                 CONVHEX INPUT FIELD LENGTH
DATACSR  EQU   R5                 Data CSECT addressability register
HEXOUTAR EQU   R14                CONVHEX OUTPUT FIELD ADDRESS
HEXINAR  EQU   R15                CONVHEX INPUT FIELD ADDRESS
LNKREG1  EQU   R9                 LINK REGISTER 1
RPTDBAR  EQU   R10                REPORT DEFINITION BLOCK ADDRESS
MSGADDR  EQU   R11                ERROR MESSAGE ADDRESS
         SPACE
* Local customization equates
RPTLPP   EQU   65                 Report lines per page
         SPACE
* OFFSETS OF A FEW FIELDS IN AMBL
AMBLQ    EQU   23                 QUALIFIER           (IN AMBL)
AMBLKSDS EQU   X'04'              CLUSTER OPEND AS KSDS FLAG (AMBLQ)
AMBLDTA  EQU   52                 POINTER TO DATA AMB (IN AMBL)
AMBLIX   EQU   56                 POINTER TO INDEX AMB (IN AMBL)
         SPACE
         OPEN  (VSAMF)
         LTR   R15,R15            OPEN OK.............................?
         BNZ   OPENERR            NO, OPEN ERROR---------------------->
*  CHECK IF IT IS A KSDS FILE
         L     R2,ACBAMBL         AMB LIST ADDRESS
         TM    AMBLQ(R2),AMBLKSDS A KSDS CLUSTER......................?
         BZ    NOTKSDS            NO, IT IS NOT A KSDS FILE----------->
* PRINT OPEN TIME STATISTIC FOR DATA AND INDEX COMPONENTS AVAILABLE
* IN THE AMDSB
         L     R3,AMBLDTA(,R2)    LOAD DATA COMP. AMB ADDRESS
         USING AMB,R3             AMB ADDRESSABILITY
         L     R4,AMBDSB          LOAD DATA COMP. AMBSB ADDRESS
         ST    R4,DATADSBA        SAVE DATA COMPONENT AMDSB ADDRESS
         USING AMDSB,R4           AMBSB ADDRESSABLITY
* We will adjust record length of the LHKR file/DCB
         LA    R0,LHKL1KEY-LHKL1  lenght of fixed part of the record
         AH    R0,AMDKEYLN        + key length = record length
         STH   R0,LHKL1           store it in RDW
* If LRECL < 137 will make it 137 (due to header line)
         CH    R0,=H'137'
         BNL   LHKRLA             L'LHKL1 >= 137
         LH    R0,=H'137'         make is 137 (minimum LRECL)
LHKRLA   L     R1,LHKRDCBA        DCB address
         USING IHADCB,R1          DCB ADDRESSABILITY
         STH   R0,DCBLRECL        store LRECL in DCB
         LH    R0,AMDKEYLN        Key length
         DROP  R1                 Drop addressability
         CVD   R0,D               CONVERT TO DECIMAL
         L     R1,LHKRHTA         address of report header line
         USING LHKRHLT,R1         header line addressibility
         ED    LHKRHLKL,D+4       EDIT/convert key length
*                                 to printable on LHKR header line
         DROP  R1
* ONLY A LOADED KSDS (NON-EMPTY) CAN BE PROCESSED BY THIS PROGRAM
* FOR A BCS, AMDNLR FIELD IS ALWAYS ZERO:
* WE NEED TO DETERMINE HERE IF THIS IS A BCS. WILL READ FMT 1 DSCB
* OF DATA COMPONENT TO CHECK IF THIS IS A BCS.
         L     DATACSR,=V(KSDSPACD) Data CSECT address
         USING KSDSPACD,DATACSR   KSDSPACD addressability
         MVC   DS1DSNAM,AMBDSNM   MOVE DATA COMPONENT DSNAME FOR
*                                 CMALST/OBTAIN USE
* NOW WE NEED TO GET DATA COMPONEMT 1-ST VOLSER FROM ARDB
         L     R15,AMDPARDB       LOAD ARDB ADDRESS
         USING ARDB,R15           ARDB ADDRESSABILITY
         MVC   DS1DSSN,ARDVOLSR   MOVE DATA COMPONENT VOLSER FOR
*                                 CMALST/OBTAIN USE
         DROP  R15
         OBTAIN CAMLSTO           GET FMT1 DSCB
         LTR   R15,R15            OBTAIN SHOULD NEVER FAIL SINCE
*                                 DATASET IS ALREADY OPEN, HOWEVER....?
         BNZ   NOTBCS             WILL IGNORE IT DOES ---------------->
         CLI   DS1FMTID,C'1'      DO WE HAVE FMT1 DSCB ...............?
         BNE   NOTBCS             THIS IS UNEXPECTED, LET'S IGNORE IT->
         TM    DS1OPTCD,DS1OPTBC  IS THIS AN ICF BCS .................?
         BO    BCS1               YES, BYPASS EMPTY KSDS TEST--------->
*
*  Call Catalog Search to determine
*  if this is an extended addressing KSDS
NOTBCS   MVC   CSIFILTK,AMBDSNM   MOVE DATA COMPONENT DSN to CSI filter
         L     R15,IGGCSIMA       IGGCSI00  EP ADDRESS into R15
         LA    R1,CSIPARML        CSI call parm list
         BASR  R14,R15            CSI call
         LTR   R15,R15            Is RC=0 from CSI call ..............?
         BNZ   CSICERR1           CSI call error---------------------->
         TM    CSIEFLAG,CSIEDATAR Data returned for this entry........?
         BZ    NOTBCS1            No, assume no Extended Addressability
         TM    CSIFEAFLG,CSIFEA4GB Extended Addressability KSDS ......?
         BZ    NOTBCS1            No
         OI    CAFLAGS,EAKSDS     Yes, set Extended Addressability flag
NOTBCS1  L     R14,AMDNLR         NO.OF RECORDS IN THE DATA COMPONENT
         LTR   R14,R14            ANY RECORDS IN THE FILE.............?
         BZ    KSDSNL             NO, DATA SET IS NOT LOADED---------->
         DROP  DATACSR
BCS1     MVC   CISIZED,AMDCINV    SAVE THE DATA COMPONENT CISIZE
         BAL   R14,PRNTSTAT       PRINT DATA  COMPONENT STATISTICS
         L     R3,AMBLIX(,R2)     LOAD INDEX COMP. AMB ADDRESS
         L     R4,AMBDSB          LOAD INDEX COMP. AMBSB ADDRESS
         ST    R4,INDXDSBA        SAVE INDEX COMPONENT AMDSB ADDRESS
         MVC   CISIZEI,AMDCINV    SAVE THE INDEX COMPONENT CISIZE
         BAL   R14,PRNTSTAT       PRINT INDEX COMPONENT STATISTICS
* Find out # of records at each index level and print that like:
* IX level   # of IX records
         STM   R2,R15,SAVEA1      SAVE REGISTERS BEFORE CALLING
         BAL   R14,IXRPT          Index analyses/ report
         LM    R2,R15,SAVEA1      RESTORE THE REGISTERS
         LA    R11,SGL1           ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTGDB         ADDRESS OF THE REPORT DEFINITION BLK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         LA    R11,SGL2           ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTGDB         ADDRESS OF THE REPORT DEFINITION BLK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         LA    R11,SGL3           ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTGDB         ADDRESS OF THE REPORT DEFINITION BLK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         LA    R11,SGL4           ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTGDB         ADDRESS OF THE REPORT DEFINITION BLK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         L     R11,RPTTHTA1       ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTTDB         ADDRESS OF THE REPORT DEFINITION BLK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         USING RPTTL1H,R11        addressability to header line
         MVI   RPTTL1HA,C'0' 1 SPACE NEXT TIME THIS HEADER LINE PRINTED
         DROP  R11
         BAL   R14,BLANKL         PRINT A BLANK LINE AFTER STATISTICS
* AT THIS POINT WE HAVE INDEX AMDSB ADDRESS IN R4
* From what I know  - this is a real RBA (not a relative CI#)
* Besides, the KSDSes that I ever looked at - RBA of the first IX SS CI
* is always zero (except of IMBED IX KSDS, and in this case, it is
* expressed in bytes).
         MVC   IXCIRBA,AMDSSRBA   MOVE RBA OF THE FIRST SEQUENCE SET
         XC    IXCIRBAX,IXCIRBAX  Clear HO XRBA
*        TM    AMDATTR,AMDSDT     IMBED INDEX.........................?
*        BO    IXCIPROC           Yes, bypass multiply by CISIZEI----->
*        L     R1,AMDSSRBA        RBA of the first IX SS record
*                                 (expressed as relative CI#)
*        M     R0,CISIZEI         * IX CI size = XRBA
*        STM   R0,R1,IXCIRBAX     Store XRBA into work field
         SPACE
* THIS IS THE MAINLINE PROCESSING CODE.
* AN INDEX SEQUENCE SET CI (RECORD) GOVERNING ONE CA IS READ.
* FOR EVERY SS RECORD, ALL USED DATA CI FROM THAT CA ARE ALSO READ
* AND PROCESSED. A REPORT LINE IS PRINTED FOR EACH CA.
IXCIPROC DS    0H                 INDEX RECORD (SS) PROCESSING
* Edit current CA number to printable (in LHKL1 for LHKR report)
         MVC   LHKL1CA#,=X'4020202020202120' CA number edit mask
         L     R15,DC#CA          this control area number (-1)
         AL    R15,=F'1'          + 1 since DC#CA is updated at the
*                                 of a CA processing
         CVD   R15,D              CONVERT TO DECIMAL
         ED    LHKL1CA#,D+4       EDIT/convert CA # to printable
*
         XC    CASUS,CASUS        RESET THE CA SUS TO ZEROS
         XC    CAIXRLSL,CAIXRLSL  RESET LAST SS IX RECORD SECTION LEN
         OI    CAFLAGS,CAFFDRP    set first CA data CI flag
         L     R1,=A(RPLIX)       RPL address (index read)
         GETIX RPL=(1)            GET AN INDEX (SS) RECORD
         LTR   R15,R15            GET SEQUENCE SET RECORD OK..........?
         BNZ   IXRERROR           NO, INDEX READ ERROR---------------->
         L     R5,IXCIADDR        LOAD THE ADDRESS OF THE INDEX RECORD
         USING IXHDR,R5           INDEX RECORD (HEADER) ADDRESSABILITY
         CLI   IXHLV,X'01'        IS IT A SEQUENCE SET CI.............?
         BNE   ABEND0             NO, SOMETHING WRONG, ABEND --------->
* CHECK IF THIS IS SEOF (SOFTWARE END-OF-FILE) CI
         LH    R14,IXHLL          LENGTH OF INDEX RECORD
         LA    R15,IXHDR+3(R14)   ADDRESS OF THE CIDF FIELD
         USING CIDF,R15           CIDF ADDRESSABLITY
         OC    CIDF(4),CIDF       ALL ZEROS IN CIDF...................?
         BZ    SEOF               YES, SOFTWARE END-OF-FILE----------->
* CALCULATE THE VERTICAL POINTER LENGTH AND SAVE IF IN A FULLWORD
* FOR EASE OF USE LATER ON
         XR    R1,R1              CLEAR R1
         IC    R1,IXHFLPLN        INDEX ENTRY CONTROL INFO.LENGTH
         ST    R1,IXECILEN        SAVE IT IN A FULLWORD FOR EASE OF USE
         BCTR  R1,0
         BCTR  R1,0               - 2 = VERTICAL POINTER LENGTH
         ST    R1,VPLENGTH        VERTICAL POINTER LENGTH
* CHECK IF THERE ARE ANY UNUSED CI'S IN THE CA AND UPDATE THE
* CAFSP FREE SPACE VALUE
         XR    R0,R0              CLEAR R0 (PREPARE FOR A DIVIDE)
         LH    R1,IXHFSO          OFFSET OF UNUSED SPACE IN THE CI
         SH    R1,=H'24'          - LENGTH OF THE HEADER
         BNP   IXCIP2             = 0, NO VERTICAL POINTERS TO FREE CI
         D     R0,VPLENGTH        FREE CI ENTRIES LEN/VPL=NO.OF FREE CI
         ST    R1,CA#FCI          KEEP RECORD OF NO.OF FREE CI'S
         L     R4,DATADSBA        DATA AMDSB ADDRESS
* AT THIS POINT WE HAVE DATA COMPONENT AMDSB ADDRESS IN R4
* FOR THIS CA, WE ARE DONE HERE WITH THE CA FREE SPACE (# of free CIs)
IXCIP2   DS    0H
* NOW WE WILL CHECK IF THERE IS ANY UNUSED SPACE IN THIS INDEX
* SEQUENCE SET RECORD
*              (AMDCIPCA is 2 bytes, CA#FCI is an F)
         CLC   CA#FCI+2(2),AMDCIPCA IF NO.OF FREE CI'S = NO.OF CI'S/CA
*                                 IT MEANS THAT THE WHOLE CA IS FREE
         BL    IXCIP4             NO, FEWER FREE CI'S THAN CI'S/CA
* ALL CI'S IN THIS CA ARE FREE, IT MEANS THERE AREN'T ANY INDEX ENTRIES
* (This case likely doesnt happen. An empty CA should have CI/CA - 1
*  free data CI pointers + an entry for CA high key).
         LH    R15,IXHLL          SS RECORD LENGTH
         SH    R15,IXHFSO         - FREE SPACE OFFSET = LENGTH OF FSPC
         BAL   LNKREG1,IXCIPFSU   Update IX SUS
         B     IXCIPEX            EXIT PROCESSING FOR THIS CA
* The common routine/code below is entered via LNKREG1
* UPDATE THE USED SPACE FOR THE INDEX (SEQUENCE SET)
*        R15 has this IX CI free space
* We are doing 8 bytes arithmetic here
* Update IX CI free space
IXCIPFSU ST    R15,CAIXRFSP       save THIS IX REC FSPC in work field
         L     R0,IX#CISS         Update # of IX SS records
         AL    R0,=F'1'           + 1
         ST    R0,IX#CISS         Updated
* Update IX CI free space
         LM    R0,R1,IXCIFSP      IX (SS) CI free space
         ALR   R1,R15             + this IX record free space
         BC    12,IXCIP2F         no carry in add logical
         AL    R0,=F'1'           Carry: add 1 to HO 4 bytes of FSPC
IXCIP2F  STM   R0,R1,IXCIFSP      Updated IX (SS) CI free space
* Update IX CI used space
         L     R14,CISIZEI        Index CI size
         SLR   R14,R15            - this IX record free space =
*                                 this CI used space
         LM    R0,R1,IXUSPC       IX (SS) CI used space
         ALR   R1,R14             + this IX record used space
         BC    12,IXCIP2U         no carry in add logical
         AL    R0,=F'1'           Carry: add 1 to HO 4 bytes of USPC
IXCIP2U  STM   R0,R1,IXUSPC       Updated IX (SS) CI used space
         BR    LNKREG1            return to caller
* AT THIS POINT WE ARE DONE WITH AN IX SS RECORD AND THE DATA CA
* CONTROLLED BY THIS SS RECORD
         SPACE
IXCIP4   DS    0H
* THERE ARE INDEX ENTRIES (AND CORRESPONDING DATA CI'S) IN THIS
* SEQUENCE SET RECORD. WE WILL GO THROUGH ALL INDEX ENTRIES, AND THEN
* READ AND PROCCESS ALL DATA CI'S.
* WE WILL PROCESS DATA CI'S IN THE KEY SEQUENCE (INDEX ENTRIES FROM
* RIGHT TO LEFT).
* PLEASE NOTE THAT OFFSETS TO INDEX ENTRIES ARE IN FACT OFFSETS TO THE
* CONTROL INFORMATION IN AN ENTRY.
*
IXELEOR  EQU   R11                THIS REGISTER IS DESIGNATED TO HOLD
*                                 OFFSET OF THE LAST INDEX ENTRY IN
*                                 THE CURRENTLY PROCESSED SECTION OF
*                                 THE INDEX RECORD
IXEOR    EQU   R10                THIS REGISTER WILL HOLD OFFSET OF
*                                 THE CURRENTLY PROCESSED INDEX ENTRY
*
* FIRST WE WILL CALCULATE THE FREE SPACE IN THIS INDEX RECORD
* AND UPDATE THE IXSUS
*
         LH    IXELEOR,IXHLEO     LAST ENTRY OFFSET IN THE INDEX REC.
         LA    R15,IXHDR(IXELEOR) ADDRESS OF THE CONTROL INFORMATION
         USING IXECIFMT,R15       CONTROL INFO. DSECT ADDRESSABILITY
         XR    R1,R1              CLEAR R1
         IC    R1,IBFLPL          LENGTH OF KEY IN THE LAST ENTRY
         LR    R15,IXELEOR        LAST ENTRY OFFSET IN THE INDEX REC.
         SLR   R15,R1             - COMPRESSED KEY LENGTH
         SH    R15,IXHFSO         - OFFSET OF THE FREE SPACE
*                                 = FREE SPACE IN THIS IX RECORD
         BAL   LNKREG1,IXCIPFSU   Update IX SUS
*
* NOW WE WILL PROCESS THE INDEX ENTRIES FROM RIGHT (LOWEST KEY) TO
* THE LEFT (HIGHER KEYS)
         LH    IXELEOR,IXHSEO     LAST ENTRY OFFSET IN THE FIRST SECT.
         LH    IXEOR,IXHLL        INDEX RECORD LENGTH
         SL    IXEOR,IXECILEN     - CONTROL INFO.LENGTH = OFFSET OF
*                                 THE FIRST ENTRY
* AT IXCIP5 LABEL WE HAVE AN INDEX ENTRY PROCESSING LOGIC.
* FOR EACH ENTRY THE "DCCIP" SUBROUTINE IS CALLED TO READ AND PROCESS
* A DATA CI CONTROLLED BY THE INDEX ENTRY.
IXCIP5   LA    R15,IXHDR(IXEOR)   ADDRESS OF THE CONTROL INFORMATION
         IC    R14,IXHPTLS        INSERT VERTICAL POINTER INDICATOR
         XR    R1,R1              CLEAR R1
         EX    R14,ICMVP          INSERT VERTICAL POINTER INTO R1
         L     R4,DATADSBA        LOAD DATA AMDSB ADDRESS INTO R4
         TM    CAFLAGS,EAKSDS     Extended Addressability KSDS .......?
*  For EF IXHBRBA has CA base relative CI# rather than a "true" RBA
         BNO   IXCIP6             No, we have a "true" RBA in IXHBRBA
*  EF KSDS: CI# = VP + CA base CI#
         AL    R1,IXHBRBA         VP + CA base (beginning) CI#
IXCIP6   M     R0,CISIZED         relative CI# * DATA CI SIZE = XRBA
         STM   R0,R1,DCCIRBAX     save DATA CI XRBA
         TM    CAFLAGS,EAKSDS     Extended Addressability KSDS .......?
         BO    IXCIP7             Yes, we have a "true" RBA in IXHBRBA
* non-EF KSDS: RBA = VP * CIsize + CA base RBA
         AL    R1,IXHBRBA         + CA base (beginning) RBA =
         ST    R1,DCCIRBA         RBA OF THE DATA CI
IXCIP7   STM   R2,R15,SAVEA1      SAVE REGISTERS BEFORE CALLING
* (WE ARE SHORT OF GPR'S HERE)    DATA CI PROCESSING ROUTINE
         BAL   R14,DCCIP          CALL DATA CI PROCESSING ROUTINE
         LM    R2,R15,SAVEA1      RESTORE THE REGISTERS
         XR    R1,R1              CLEAR R1
         IC    R1,IBFLPL          LENGTH OF THE IXKEY FIELD IN IX ENTRY
         CLR   IXELEOR,IXEOR      IS THIS THE LAST ENTRY IN THIS SECT.?
         BNL   IXCIPNS            YES, LAST ENTRY IN THIS SECTION----->
* WE STILL HAVE MORE ENTRIES IN THIS INDEX SECTION
         SLR   IXEOR,R1           OFFSET OF CURRENT ENTRY -IXKEY LENGTH
         SL    IXEOR,IXECILEN     - CONTROL INFO.LENGTH= NEXT HIGHER
*                                 ENTRY OFFSET
         B     IXCIP5             PROCESS THE NEXT ENTRY-------------->
* WE'VE REACHED THE END OF CURRENT INDEX SECTION
IXCIPNS  DS    0H                 NEW SECTION PROCESSING
         CH    IXEOR,IXHLEO       IS IT THE LAST ENTRY IN THIS RECORD.?
         BNH   IXCIPEX            YES, END OF CA PROCESSING----------->
         SLR   IXEOR,R1           OFFSET OF CURRENT ENTRY -IXKEY LENGTH
         BCTR  IXEOR,0            ENTRY OFFSET - 2 (LENGTH OF SECTION
         BCTR  IXEOR,0            CONTROL FIELD)
         LH    R1,IXHDR(IXEOR)    LOAD THIS SECTION CONTROL FIELD
         STH   R1,CAIXRLSL        SAVE IT IN THE WORK FIELD
*                          (USED  LATER FOR IX RECORD SHORT TEST)
         SLR   IXELEOR,R1         = LAST ENTRY OFFSET IN THE NEXT SECT.
         SL    IXEOR,IXECILEN     - CONTROL INFO.LENGTH= FIRST ENTRY
*                                 OFFSET IN THE NEXT SECTION
         B     IXCIP5             PROCESS THE NEXT ENTRY-------------->
         SPACE
ICMVP    ICM   R1,X'00',IBFLP3    INSERT VERTICAL POINTER INTO R1
SAVEA1   DS    16F                INTERNAL REGISTER SAVE AREA
         SPACE
* WE'VE GONNE THROUGH THE WHOLE SEQUENCE SET IX RECORD AND READ ALL
* DATA CI'S UNDER ITS CONTROL (THE CONTROL AREA).
* NOW WE WILL CHECK IF THIS SS IX RECORD DESCRIBES ALL CI'S IN THIS CA.
* (EG. IF THERE ARE ANY UNUSED DATA CI'S BECAUSE OF LACK OF SPACE IN
*  THIS INDEX RECORD).
* THEN WILL WE WILL PRINT REPORT LINES FOR THIS CONTROL AREA.
IXCIPEX  DS    0H                 END OF CONTROL AREA PROCESSING
* THE LOGIC BELOW IS PROBABLY USELESS, BUT DOES NOT DO ANY HARM
* (NORMALLY, A SS IX RECORD SHOULD ALWAYS ACCOUNT FOR ALL CI'S IN A CA)
         L     R0,CA#FCI          NO.OF FREE CI'S IN THE CA
         AL    R0,CA#UCI          + NO.OF USED CI'S = NO.OF DATA CI'S
*                                 CONTROLLED BY THIS SS RECORD
         CH    R0,AMDCIPCA        SAME AS NO.OF CI'S PER CA...........?
         BNL   IXCIPEX1           ANOTHER CHECK FOR SS IX RECORD SHORT>
* THERE ARE UNUSED CI'S IN THIS CA BECAUSE SEQUENCE SET INDEX RECORD
* IS TOO SHORT
         LH    R1,AMDCIPCA        NO.OF CI'S PER CA
         SLR   R1,R0              - NO.OF CI'S CONTROLLED BY IX SS REC.
*                                 = NO.OF UNUSED CI'S DUE TO IX RECLEN
         ST    R1,CA#IXRSC        STORE IT IN THE CA SUS
IXCIPEX1 L     R1,CA#FCI          ANY FREE CI'S IN THE CA
         LTR   R1,R1              ?
         BNP   IXCIPEXR           NO, NO POSSIBILITY OF IX REC SHORT
*        CAIXRFSP is a fullword, CAIXRLSL is a halfword , since + 2
         CLC   CAIXRLSL,CAIXRFSP+2  FREE SPACE IS LESS THAN LAST INDEX
*                                 SECTION LENGTH                      ?
         BL    IXCIPEXR           NO, ENOUGH OF FREE SPACE FOR ANOTHER
*                                 INDEX SECTION
* WE ARE ASSUMING THAT ALL THESE FREE CI'S ARE DUE TO INDEX RECORD
* BEING TOO SHORT SINCE THERE IS NO FREE SPACE TO CREATE ANOTHER
* SECTION OF THE INDEX (WE ARE WORKING WITH SS IX RECORD HERE)
         AL    R1,CA#IXRSC        UPDATE THE IX rec short # of CIs
         ST    R1,CA#IXRSC        STORE IT IN THE CA SUS
* All free CIs are assumed to be due to IX record short
* We need to nullify CA#FCI
         XC    CA#FCI,CA#FCI      clear CA FSPC # of CIs
* WE ARE GOING TO PRINT THE REPORT LINES FOR THE CA HERE
IXCIPEXR DS    0H
         STM   R2,R11,SAVEA1      SAVE REGISTERS (2 TO THE LAST BASE)
         MVC   CISIZE,CISIZED     DATA COMPONENT CISIZE
         BAL   R14,CARPTT         RPTT CA REPORT LINE(S)
         BAL   R14,CARPTG         RPTG CA REPORT LINE(S)
* Print HIGH key line in LHKR report
         MVC   EMPTYCAS,LHKL1     save the record length
         TM    CAFLAGS,LHKFOUND   a key/data record found in this CA..?
         BO    IXCIPXR1           Yes, print it ---------------------->
* We seem to have an empty CA (no data records) here
         MVC   LHKL1KEY(L'EMPTYCA),EMPTYCA Empty CA message/text
         LA    R10,(LHKL1KEY-LHKL1)+L'EMPTYCA this message length
         STH   R10,LHKL1          store in record header
IXCIPXR1 LA    R11,LHKL1          ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,LHKRDB         ADDRESS OF THE LHKR DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         NI    CAFLAGS,X'FF'-LHKFOUND Reset a key found in CA flag
         MVC   LHKL1(2),EMPTYCAS  restore the record length
         LM    R2,R11,SAVEA1      RESTORE REGISTERS
         L     R4,INDXDSBA        RELOAD THE INDEX AMDSB ADDRESS
*                                 (IT HAS TO BE AT THE IXCIPROC LABEL)
         XR    R0,R0              clear R0
         L     R1,IXHHP           RBA OF THE NEXT SEQUENCE SET RECORD
         TM    CAFLAGS,EAKSDS     Extended Addressability KSDS .......?
*  For EF: HP  has relative IX SS CI# rather than a "true" RBA
         BNO   IXCIPXRB           No, we have a "true" RBA in IXHBRBA
         M     R0,CISIZEI         relative CI# * IX CI SIZE = XRBA
IXCIPXRB STM   R0,R1,IXCIRBAX     XRBA OF THE NEXT SEQUENCE SET RECORD
         LA    R0,1               UPDATE THE NUMBER OF CA'S PROCESSED
         AL    R0,DC#CA           NO.OF CA'S + 1
         ST    R0,DC#CA           UPDATED
* UPDATE THE DATA COMPONENT SPACE UTILIZATION SUMMARY
* BY ADDING VALUES FROM THE CURRENT CONTROL AREA
* 8 bytes binary counts are used for data component byte count totals
         LA    R15,1              In case of carry: put 1 in R15
* Data component used space
         LM    R0,R1,DCUSPC       DC used space (8 bytes count)
         AL    R0,CAUSPC          + CA used space (HO part)
*                        (We assume no overflow in HO part)
         AL    R1,CAUSPC+4        + CA used space (LO part)
         BC    12,IXCIDCS1        no carry in add logical
         ALR   R0,R15             Carry: add 1 to HO 4 bytes
IXCIDCS1 STM   R0,R1,DCUSPC       Updated Data Comp. used space/bytes
* Data component CI free space
         LM    R0,R1,DCCIFSP      DC CI free space (8 bytes count)
         AL    R0,CACIFSP         + CA CI free space (HO part)
*                        (We assume no overflow in HO part)
         AL    R1,CACIFSP+4       + CA CI free space (LO part)
         BC    12,IXCIDCS2        no carry in add logical
         ALR   R0,R15             Carry: add 1 to HO 4 bytes
IXCIDCS2 STM   R0,R1,DCCIFSP      Updated DComp. CI free space/bytes
* Data component # of used CIs
         L     R0,DC#UCI          DC # of used CIs
         AL    R0,CA#UCI          + current CA used CI count
         ST    R0,DC#UCI          Updated DC used CI count
* Data component CA free space # of free CIs
         L     R0,DC#CAFCI        CA free space # of free CIs
         AL    R0,CA#FCI          + current CA free CI count
         ST    R0,DC#CAFCI        Updated DC CA freespce CI count
* Data component unused CI's due to IX CI short
         L     R0,DC#IXRSC        IX rec short  # of free CIs
         AL    R0,CA#IXRSC        + current CA IX short free CIs
         ST    R0,DC#IXRSC        Updated DC IX short free CI count
* We are done Data Component SUS update here
         OC    IXCIRBAX(8),IXCIRBAX      RBA OF NEXT SS RECORD = 0....?
         BZ    SEOF               YES,END-OF-FILE,LAST SS CI PROCESSED>
         B     IXCIPROC           PROCESS NEXT SS RECORD AND DATA CA
         SPACE
* SOFTWARE END-OF-FILE RECORD FOUND IN THE SEQUENCE SET
* OR, LAST PROCESSED SS RECORD HORIZONTAL POINTER WAS 0 (EOF)
SEOF     DS    0H
* WE NEED TO CHECK IF THERE ARE ANY CA'S ALLOCATED AFTER THE LAST
* PROCESSED.
* LET'S CALCULATE HOW MANY CA'S AFTER EOF ARE UNUSED.
* (WE HAVE TO FIND ARDB WITH THE HIGHEST DATA RBA: THIS ARDB CONTAINS
*  THE HIGEST ALLOCATED RBA)
         L     R4,DATADSBA        RELOAD THE DATA AMDSB ADDRESS
         L     R15,AMDPARDB       LOAD ARDB ADDRESS
         USING ARDB,R15           ARDB ADDRESSABILITY
*  RBA MEANS # OF CI'S (DFSMS 1.3 AND UP)
         L     R1,ARDERBA         HIGHEST RBA ALLOCATED IN DATA COMP.
         SL    R1,ARDHRBA         - FIRST FREE CI RBA AT THE EOF
         LTR   R1,R1              ANY FREE SPACE AFTER EOF............?
         BZ    SEOF1              NO FREE CA'S AFTER EOF-------------->
         LH    R14,AMDCIPCA       NO.OF CI'S PER CONTROL AREA
         XR    R0,R0              PREPARE FOR DIVIDE ( R0=0 )
         DR    R0,R14             FREE CI'S / CI PER CA = # FREE CA'S
         ST    R1,DC#CAFR         NO.OF FREE CA'S AT THE END OF DATASET
         DROP  R15
SEOF1    DS    0H                 PRINT SUMMARY FOR DATA AND INDEX
         BAL   R14,DCSUSPR        PRINT SUS FOR DATA COMPONENT
         BAL   R14,IXSUSPR        PRINT SUS FOR INDEX COMPONENT
         B     CLOSE              CLOSE VSAM ACB AND REPORTS' DCB'S
* HERE IS THE END OF THE MAINLINE PROCESSING LOGIC. THE FOLLOWING ARE
* VARIOUS PROCEDURES CALLED FROM THE MAINLINE.
         SPACE
*---------------------------------------------------------------------*
* DATA CI PROCESSING ROUTINE                                          *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
DCCIP    SUBENT
* This code ensures that there are no idle data CI reads
* (checks if we are positioned correctly, will however do an
* unnecessary POINT for the first read of the data component CI)
* Also, this prevents EODAD error if by a chance the last CI read
* was the last CI of the data component.
         LM    R0,R1,DCCIRBAR     XRBA of the last read data CI
         AL    R1,CISIZED         + CI size = RBA of next to be read CI
         BC    12,DCCIP0          no carry in add logical
         AL    R0,=F'1'           Carry: add 1 to HO 4 bytes of XRBA
DCCIP0   C     R0,DCCIRBAX        Next CI RBA = required RBA LO bytes.?
         BNE   DCCIPP             No, position/POINT first------------>
         C     R1,DCCIRBA         Next CI RBA = required RBA HO bytes.?
         BNE   DCCIPP             No, position/POINT first------------>
DCCIPG   L     R1,=A(RPLDC)       RPL address (data CI read)
         GET   RPL=(1)            GET A DATA COMPONENT CI
         LTR   R15,R15            GET DATA CI OK......................?
         BNZ   DCIRERR            NO, DATA CI READ ERROR-------------->
         SHOWCB RPL=RPLDC,        GET THE RBA OF JUST READ DATA CI     +
               AREA=DCCIRBAR,     CURRENTLY READ CI RBA                +
               LENGTH=8,                                               +
               FIELDS=(XRBA)      XRBA WILL BE SHOWN
         CLC   DCCIRBAR(8),DCCIRBAX  SAME READ AS REQUIRED FROM IX REC?
         BE    DCCIP1             YES, W'VE GOT THE RIGHT DATA CI----->
* NEXT SEQUENTIALLY READ DATA CI ISN'T THE RIGHT ONE. WE HAVE TO ISSUE
* A POINT MACRO AND REREAD THE DATA CI
DCCIPP   MODCB RPL=RPLDC,         MOVE ARGUMENT ADDRESS INTO RPL       +
               ARG=DCCIRBAX       XRBA OF THE REQUIRED DATA CI
         L     R1,=A(RPLDC)       RPL address (data CI read)
         POINT RPL=(1)            POINT ON REQUIRED DATA CI
         LTR   R15,R15            POINT OK............................?
         BNZ   POINTERR           NO, ERROR--------------------------->
         B     DCCIPG             GET THE REQUIRED DATA CI------------>
DCCIP1   L     R2,DCCIADDR        ADDRESS OF THE CI IN A BUFFER
         LM    R0,R1,DCCIRBAR     This data CI RBA
         OC    DCCIRBAR(8),DCCIRBAR      RBA = 0......................?
         BZ    DCCIP10            Yes, DIVIDE not required/invalid---->
         D     R0,CISIZED         / DATA CIsize = relative CI#
DCCIP10  CVD   R1,D               Convert to decimal
         MVC   LHKL1CI#,=X'402020202020202020202120'
         ED    LHKL1CI#,D+2       EDIT/convert CI# to printable
* WE EXPECT DATA AMDSB ADDRESS TO BE IN R4
* Register use:
* R2  CI   address
* R3  CIDF address
* R4  AMDSB address
* R3  1st/right RDF  address (if there is a pair)
CIDFR    EQU   R3                 CIDF address register
RDFR     EQU   R3                 RDF address register
         USING AMDSB,R4           AMBSB ADDRESSABLITY
         L     R15,CISIZED        DATA CI SIZE
         LR    CIDFR,R15
         SL    CIDFR,=F'4'        DATA CI SIZE - 4 = CIDF OFFSET
         LA    CIDFR,0(R2,CIDFR)  CIDF ADDRESS
         USING CIDF,CIDFR         CIDF  ADDRESSABLITY
         ICM   R0,B'0011',CIDF11  LOAD LENGTH OF UNUSED SPACE INTO R0
         SLL   R0,17              CLEAR THE CI BUSY BIT
         SRL   R0,17
         SLR   R15,R0             CI SIZE - UNSUED SPACE = USED SPACE
* We need to 8 bytes arithmetic here: R15 used bytes, R0 - free bytes
         AL    R15,CAUSPC+4       CA USED SPACE - LO 4 bytes
         ST    R15,CAUSPC+4
         BC    12,DCCIPFS         no carry in add logical
         LA    R15,1              Carry: add 1 to HO 4 bytes
         AL    R15,CAUSPC         CA USED SPACE - HO 4 bytes
         ST    R15,CAUSPC         Update HO 4 bytes of count
DCCIPFS  AL    R0,CACIFSP+4       UPDATE  CI FREE SPACE
         ST    R0,CACIFSP+4       LO 4 bytes
         BC    12,DCCIPFS1        no carry in add logical
         LA    R0,1               Carry: add 1 to HO 4 bytes
         AL    R0,CACIFSP         CA free SPACE - HO 4 bytes
         ST    R0,CACIFSP         Update HO 4 bytes of count
DCCIPFS1 LA    R0,1               UPDATED NO.OF USED DATA CI'S
         AL    R0,CA#UCI          (+ 1)
         ST    R0,CA#UCI
* At this point we will be extracting keys from data records
* in order to find LOW/HIGH keys in this data area
         ICM   R1,B'0011',CIDFOSET LENGTH OF data records in this CI
         SLL   R1,17              CLEAR THE CI BUSY BIT
         SRL   R1,17
         LTR   R1,R1              No data records (a free CI).........?
*                                 (this should not happen, we check
*                                  just in case)
         BZ    DCCIPEX            empty CI, exit DCCIP
*        CI address + lenght of data records = free space address
         LA    R1,0(R2,R1)        Free space address in thi CI
         ST    R1,DCCIPFSP        Save it for later checking if we
*                                 went through all data records
* (in case of spanned records this is somewhat simpler: after last
*  or intermediate segment - there should not be any more records in
*  this data CI).
*
* In case of a spanned record file - we have to find the first segment
* (or the whole first record in this CI)
* R2 points to a record or segment
* We will proceed form left to right (in the CI) and extract key from
* every record.
* For the first record found - if this is the first CI in the CA
* (CAFFDRP flag is on) - will write the LOW key line.
* At the end of CA processing - will write the last/HIGH key line.
* At this point RDFR points to CIDF
         SH    RDFR,=H'6'         - 6 = left RDF address (if paired,
*                                 if any)
* ( we are getting view of left and right RDF even though there can
*   be only the right one)
         USING RDF,RDFR           RDF addressability
DCCIPRP  TM    RRDFF,RRDFFS+RRDFLS a spanned record ..................?
         BZ    DCCIPF             No, a fixed length record ---------->
* We seem to have a spanned record here
         TM    RRDFF,RRDFFS+RRDFLS intermediate segment...............?
         BO    DCCIPEX            Yes, no more segments in this CI
         TM    RRDFF,RRDFLS       last segment         ...............?
* (in case of spanned records CI with last or intermediate segment
*  cannot contain any other records)
         BO    DCCIPEX            Yes, no more segments in this CI
* We seem to have the first segment of a spanned record here
         BAL   LNKREG1,DCCIPEK    move key from data record to LHKL1KEY
         TM    CAFLAGS,CAFFDRP    first data CI in this CA ...........?
         BNO   DCCIPEX            No, Exit this CI processing--------->
         LA    R11,LHKL1          ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,LHKRDB         ADDRESS OF THE LHKR DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
* We printed the LOW key, let's reset the CAFFDRP flag
         NI    CAFLAGS,X'FF'-CAFFDRP reset first CA data record flag
         B     DCCIPEX            Exit this CI processing
*                                 (a spanned segment should always be
*                                 the last record in a data CI)
         SPACE
* We have a fixed record in this CI (pointed by R2)
DCCIPF   TM    CAFLAGS,CAFFDRP    first data CI in this CA ...........?
         BNO   DCCIPF1            No, Exit this CI processing--------->
         BAL   LNKREG1,DCCIPEK    move key from data record to LHKL1KEY
         LA    R11,LHKL1          ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,LHKRDB         ADDRESS OF THE LHKR DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         MVI   LHKL1CA#,X'40'     Ensure next line is single spaced
* We printed the LOW key, let's reset the CAFFDRP flag
         NI    CAFLAGS,X'FF'-CAFFDRP reset first CA data record flag
DCCIPF1  TM    RRDFF,RRDFP        is it a paired RDF..................?
         BO    DCCIPFP            Yes, paired RDF, get last rec   ---->
* Just single record, extract key, position on next record
DCCIPF2  BAL   LNKREG1,DCCIPEK    move key from data record to LHKL1KEY
         AH    R2,RRDF#           Next record address in the CI
         C     R2,DCCIPFSP        = free space address ...............?
         BNL   DCCIPEX            yes, we are done with this CI ------>
* Since we have un-paired RDF, we need to decrement by 3 to get next
         SH    RDFR,=H'3'         - 3 = next RDF address
         B     DCCIPRP            Look at the next RDF/ data record
* Multiple fixed length records described by paired RDF
DCCIPFP  LH    R15,LRDF#          number of records described by this
*                                 paired RDF
         BCTR  R15,0              - 1
         MH    R15,RRDF#          * record length = offste of last rec
         LA    R2,0(R2,R15)       last record address
         BAL   LNKREG1,DCCIPEK    move key from data record to LHKL1KEY
         AH    R2,RRDF#           Next record address in the CI
         C     R2,DCCIPFSP        = free space address ...............?
         BNL   DCCIPEX            yes, we are done with this CI ------>
* In case of a paired RDF we need to subtract 6 bytes to position at
* next RDF pair (if any).
         SH    RDFR,=H'6'         - 6 = next RDF address
         B     DCCIPRP            Look at the next RDF/ data record
         DROP  CIDFR              Drop addressability of CIDF
*        DROP  RDFR               Drop addressability of RDF
DCCIPEX  SUBRET DCCIP             RETURN TO CALLER
MVCKEY   MVC   LHKL1KEY(1),0(R15) move record key to LHKL1KEY
* Extract key from this data record (pointed by R2)
DCCIPEK  OI    CAFLAGS,LHKFOUND   We found a key/data record in this CA
         LH    R15,AMDRKP         RELATIVE KEY POSITION
         LH    R1,AMDKEYLN        KEY LENGTH
         BCTR  R1,0               KEY LENGTH - 1
         LA    R15,0(R2,R15)      Key address in the first record
         EX    R1,MVCKEY          move load key to LHKL1KEY
*********test***************************
*        LA    R11,LHKL1          ADDRESS OF THE LINE TO BE PRINTED
*        LA    R10,LHKRDB         ADDRESS OF THE LHKR DEFINITION BLOCK
*        ST    R9,DCCIPR2S        save R2 ******* test ***************
*        BAL   R14,RPTPP          REPORT PRINT PROCEDURE
*        L     R9,DCCIPR2S        restore R2 ******* test ***********
*********test***************************
         BR    LNKREG1            Return to caller
         SPACE
*---------------------------------------------------------------------*
* PRINT REPORT LINE FOR A CONTROL AREA - GRAPHICAL FORMAT             *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
CARPTG   SUBENT
         LA    R6,CASUS           CONTROL AREA SUS ADDRESS
         BAL   R14,RPTGSDL        PRINT THE SPACE DISTRIBUTION LINE
         SUBRET CARPTG            RETURN TO CALLER
         SPACE
*---------------------------------------------------------------------*
* PRINT REPORT LINE FOR A CONTROL AREA - TABLE FORMAT                 *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
CARPTT   SUBENT
         LA    R6,CASUS           CONTROL AREA SUS ADDRESS
         BAL   R14,RPTTSDL        PRINT THE SPACE DISTRIBUTION LINE
         SUBRET CARPTT            RETURN TO CALLER
         SPACE
*---------------------------------------------------------------------*
* PRINT THE SPACE DISTRIBUTION LINE - GRAPHICAL FORMAT                *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
* Round byte count to 1KiB if total > 4GiB
* R0/R1 has 8 bytes count
RPPGRKiB TM    CAFLAGS,GT4GiB     Is rounding required ...............?
         BNOR  R14                No, return
         D     R0,=F'1024'        Round to 1KiB
         C     R0,=F'512'         If reminder >  512 will add 1 to
*                                 the result
         BNHR  R14                reminder <= 512  return
         AL    R1,=F'1'           reminder >  512  add 1 to result
         BR    R14                Return
*
RPTGSDL  SUBENT
* R6 HAS THE SPACE UTILIZATION SUMMARY ADDRESS ( SUS )
         USING SUS,R6             SUS ADDRESSABILITY
         L     R15,SUSTOTB        LOAD total bytes or KiB saved by
*                                 RPTTSDL
* R15 HAS NOW TOTAL SPACE IN BYTES
         LR    R11,R15            SAVE TOTAL SPACE IN THE R11
         XR    R14,R14            CLEAR R14 (PREPARE FOR DIVIDE)
         LA    R10,L'RPTGL1A      SPACE DISTRIBUTION AREA LENGTH
         DR    R14,R10            TOTAL SPACE/PRINT LINE LENGTH
* R15 (QUOTIENT) HAS NO.OF BYTES PER ONE CHARACTER OF THE PRINT LINE
         LR    R8,R15             NO.OF BYTES PER ONE PRINT POSITION
         SRL   R8,1               DIVIDE BY 2
         LA    R9,RPTGL1A         ADDRESS OF THE AREA IN THE PRINT
*                                 RECORD
         MVI   RPTGL1C,C' '       ONE LINE SPACING
         MVI   RPTGL1A,C' '       FILL PRINT AREA WITH BLANKS
         MVC   RPTGL1A+1(L'RPTGL1A-1),RPTGL1A
* USED SPACE PROCESSING: IT WILL BE REPRESENTED BY * CHARACTERS
* ON THE PRINTOUT
         LM    R0,R1,USPC         Used space
         BAL   R14,RPPGRKiB       Round to KiB is required
         LTR   R1,R1              IS THERE ANY USED SPACE.............?
         BNP   RPTGSDLB           NO, CHECK CA FREE SPACE------------->
* USED SPACE IS  IN R1
         IC    R7,USPCID          CHARACTER REPRESENTING USED SPACE
*                                 INTO R7
         BAL   R14,RPTGSDP        CONVERT USED SPACE INTO * CHARS
*                                 IN THE PRINT LINE
* CI FREE SPACE PROCESSING: IT WILL BE REPRESENTED BY - CHARACTERS
* ON THE PRINTOUT
RPTGSDLB LM    R0,R1,CIFSP        CI free space
         BAL   R14,RPPGRKiB       Round to KiB if required
         LTR   R1,R1              IS THERE ANY CI FREE SPACE..........?
         BNP   RPTGSDLC           NO, CHECK IX REC.SHORT FREE SPC----->
         IC    R7,CIFSPCID        CHARACTER REPRESENTING CI FREE SPACE
*                                 INTO R7
         BAL   R14,RPTGSDP        CONVERT CI FREE SPACE INTO - CHARS
*                                 ON THE PRINT LINE
* CA FREE SPACE PROCESSING: IT WILL BE REPRESENTED BY + CHARACTERS
* ON THE PRINTOUT
RPTGSDLC L     R1,CAFSPCI#        # of CA free space CIs
         LTR   R1,R1              IS THERE ANY CA FREE SPACE..........?
         BNP   RPTGSDLD           NO, CHECK CI FREE SPACE------------->
         M     R0,CISIZE          * CISIZE = # of bytes
         BAL   R14,RPPGRKiB       Round to KiB if required
         LTR   R1,R1              any free space after rounding ......?
         BNP   RPTGSDLD           NO, CHECK CI FREE SPACE------------->
         IC    R7,CAFSPCID        CHARACTER REPRESENTING CA FREE SPACE
*                                 INTO R7
         BAL   R14,RPTGSDP        CONVERT CA FREE SPACE INTO + CHARS
*                                 IN THE PRINT LINE
* CA FREE SPACE RESULTING FROM IX RECORD BEING TOO SHORT - IT WILL BE
* REPRESENTED BY X CHARACTERS ON THE PRINTOUT
RPTGSDLD L     R1,IX#SFCI         # of UNUSED CI'S due to IX CI SHORT
         LTR   R1,R1              IS THERE ANY IX REC.SHORT FSPC......?
         BNP   RPTGSDLP           NO, PRINT THE LINE OF THE RPTG------>
         M     R0,CISIZE          * CISIZE = # of bytes
         BAL   R14,RPPGRKiB       Round to KiB if required
         LTR   R1,R1              any free space after rounding ......?
         BNP   RPTGSDLP           NO, PRINT THE LINE OF THE RPTG------>
         IC    R7,IXSFSPID        CHARACTER REPRESENTING IX RECORD
*                                 SHORT CAUSED FREE SPACE INTO R7
         BAL   R14,RPTGSDP        CONVERT IX REC.SHORT FSPC INTO X
*                                 CHARS ON THE PRINT LINE
* PRINT THE SPACE DISTRIBUTION REPRESENTATION ON RPTG
RPTGSDLP LA    R11,RPTGL1         ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTGDB         ADDRESS OF THE REPORT DEFINITION BLK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         SUBRET RPTGSDL           RETURN TO CALLER
         SPACE
*---------------------------------------------------------------------*
* THIS PROC FILLS THE RPTG PRINT LINE WITH REQUESTED CHARACTERS       *
* REPRESENTING KSDS FILE SPACE DISTRIBUTION (CA IN PARTICULAR)        *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
RPTGSDP  SUBENT
* REGISTER CONTENTS:
* R1 - SPACE IN BYTES TO BE REPRESENTED BY SPECIFIED CHARS
* R7 - CHARACTER REPRESENTIG GIVEN SPACE ON THE PRINT LINE (L/O BYTE)
* R9 - ADDRESS OF AREA TO BE FILLED WITH THE ABOVE CHARACTERS
* R10- LENGTH OF THE ABOVE AREA
* R15- NO.OF BYTES REPRESENTED BY ONE PRINT LINE POSITION
* R8 - CONTENTS OF R15 DIVIDED BY TWO (2)
         XR    R4,R4              CLEAR R4 (PREPARE FOR DIVIDE)
         LR    R5,R1              USED SPACE IN BYTES
         DR    R4,R15             DIVIDE BY NO.OF CHARS/PRINT POSITION
         CLR   R4,R8              REMAINDER >= HALF OF R15............?
         BL    RPTGSDP1           NO, FILL PRINT AREA WITH CHARS------>
         LA    R5,1(,R5)          YES, ONE POSITION EXTRA
RPTGSDP1 LTR   R5,R5              ANY POSITIONS TO BE FILLED..........?
         BNP   RPTGSDPE           NO, EXIT---------------------------->
         CLR   R5,R10             # OF POSITION > AREA LENGTH.........?
         BNH   RPTGSDP2           NO, WE ARE OK
         LR    R5,R10             YES,CHANGE IT TO NO.OF POSITIONS LEFT
RPTGSDP2 LR    R0,R5              SAVE NO.OF POSITIONS FILLED IN R0
         STC   R7,0(,R9)          STORE SPECIFIED CHAR IN THE OUTAREA
         SH    R5,=H'2'           NO.OF POSITIONS FILLED - 2
         BM    RPTGSDP3           ONLY ONE CHARACTER WAS REQUIRED----->
         EX    R5,MVCRPTG         FILL THE ARE WITH REQUIRED # OF CHARS
RPTGSDP3 ALR   R9,R0              NEXT AVAILABLE BYTE ADDRESS IN THE
*                                 PRINT AREA
         SR    R10,R0             NO.OF CHARS LEFT IN THE PRINT AREA
         BNP   RPTGSDLP           NONE LEFT, BYPASS ANY LEFT SPACE
*                                 DISTRIBUTION COMPONENTS PROCESSING
RPTGSDPE SUBRET RPTGSDP           RETURN TO CALLER
         SPACE
MVCRPTG  MVC   1(1,R9),0(R9) MOVE SPACE ID CHARACTERS INTO PRINT AREA
         SPACE
*---------------------------------------------------------------------*
* REPORT PRINT PROCEDURE                                              *
* REGISTERS CONTENTS AT ENTRY:                                        *
* R11 HAS ADDRESS OF THE V FROMAT RECORD TO BE PRINTED                *
* R10 HAS ADDRESS OF THE REPORT DEFINITION BLOCK                      *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
RPTPP    SUBENT
         USING RPTDB,R10          RPTDB ADDRESSABILITY
* CHECK IF REPORT DCB IS OPEN
         L     R9,RPTDCBA         REPORT DCB ADDRESS INTO R9
         TM    48(R9),DCBOFOPN    IS REPORT FILE OPENED...............?
         BO    RPTPPA             YES, ALREADY OPENED    ------------->
         OPEN  ((9),(OUTPUT))
         TM    48(R9),DCBOFOPN    OPEN SUCCESSFUL        .............?
         BNO   RPTOPNER           NO, OPEN ERROR         ------------->
* CHECK NUMBER OF LINES RESULTING FROM ANSI PRINT CONTROL CHARACTER
RPTPPA   CLI   4(R11),C'+'        PRINT IN THE SAME LINE..............?
         BE    RPTPP0             YES--------------------------------->
         CLI   4(R11),C' '        ONE LINE SPACING....................?
         BE    RPTPP1             YES--------------------------------->
         CLI   4(R11),C'0'        TWO LINES SPACING...................?
         BE    RPTPP2             YES--------------------------------->
         CLI   4(R11),C'-'        THREE LINES SPACING.................?
         BE    RPTPP3             YES--------------------------------->
         MVI   4(R11),C' '        INVALID PRINT CNTRL CHAR, CHANGE IT
         B     RPTPP1             TO BLANK (1 LINE SPACING)
RPTPP0   SLR   R0,R0              SAME LINE PRINTING
         B     RPTPPB             CHECK LINE COUNT-------------------->
RPTPP1   LA    R0,1               ONE LINE SPACING
         B     RPTPPB             CHECK LINE COUNT-------------------->
RPTPP2   LA    R0,2               TWO LINES SPACING
         B     RPTPPB             CHECK LINE COUNT-------------------->
RPTPP3   LA    R0,3               THREE LINES SPACING
RPTPPB   AL    R0,RPTLCNT         NEW LINE COUNT
         CL    R0,RPTLPL          FITS ON CURRENT PAGE................?
         BH    RPTPPN             NO, WE NEED TO START A NEW PAGE----->
         ST    R0,RPTLCNT         YES, NEW PAGE COUNT
         LR    R0,R11             PRINT LINE ADDRESS INTO R0
         L     R1,RPTDCBA         REPORT DCB ADDRESS INTO R1
         PUT   (1),(0)            PRINT THE REPORT LINE
         SUBRET RPTPP
         SPACE
RPTPPN   BAL   R14,RPTNP          NEW PAGE OF THE REPORT PROC
         MVI   4(R11),C'0'        TWO LINES SPACING AFTER NEW PAGE
         B     RPTPP2             PRINT THE LINE ON NEW PAGE
         SPACE
*---------------------------------------------------------------------*
* REPORT HEADER LINE PRINT PROCEDURE                                  *
* REGISTERS CONTENTS AT ENTRY:                                        *
* R10 HAS ADDRESS OF THE REPORT DEFINITION BLOCK                      *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
RPTNP    SUBENT
         MVC   RPTHDRP,=X'402020202120' EDIT MASK INTO HEADER LINE
         L     R14,RPTPGN         LOAD PAGE NUMBER
         CVD   R14,D              CONVERT TO DECIMAL
         ED    RPTHDRP,D+5        EDIT THE PAGE NUMBER
         LA    R14,1(,R14)        PAGE NUMBER + 1
         ST    R14,RPTPGN         NEXT PAGE NUMBER
         MVI   RPTHDRT,C' '       BALNK OUT THE HEADER LINE TEXT
         MVC   RPTHDRT+1(L'RPTHDRT-1),RPTHDRT
         L     R14,RPTHTA         REPORT HERADER TEXT ADDRESS
         XR    R1,R1              CLEAR R1
         IC    R1,0(,R14)         REPORT HEADER TEXT LENGTH
         LA    R0,L'RPTHDRT       MAX.HEADER TEXT LENGTH
         CLR   R1,R0              HEADER TEXT DOES NOT EXCEED MAX.....?
         BNH   RPTNP1             NO, MOVE IT INTO HEADER LINE
         LR    R1,R0              TEXT TOO LONG, TRUNCATE TO MAXLEN
RPTNP1   BCTR  R1,0               TEXT LENGTH - 1 (FOR EXECUTE)
         EX    R1,MVCHDRT         MOVE TEXT INTO HEADER LINE
         L     R1,RPTDCBA         REPORT DCB ADDRESS INTO R1
         PUT   (1),RPTHDR         PRINT THE HEADER LINE
         LA    R1,1               NEW PAGE LINE COUNT
         ST    R1,RPTLCNT
* CHECK IF THERE IS A SECOND HEADER LINE TO BE PRINTED
         L     R0,RPTHTA1         SECOND HEADER LINE ADDRESS
         LTR   R0,R0              = 0 ................................?
         BZ    RPTNPR             YES, NO SECOND LINE TO BE PRINTED--->
         L     R1,RPTDCBA         REPORT DCB ADDRESS INTO R1
         PUT   (1),(0)            PRINT THE SECOND HEADER LINE
         L     R1,RPTLCNT
* WE ARE ASSUMING THE SECOND HEADER LINE HAS ALWAYS '0' CTL CHAR
         LA    R1,2(,R1)          NEW PAGE LINE COUNT + 2
         ST    R1,RPTLCNT
RPTNPR   SUBRET RPTNP             RETURN TO CALLER
         SPACE
MVCHDRT  MVC   RPTHDRT(1),1(R14)  MOVE TEXT INTO HEADER LINE
         SPACE
*---------------------------------------------------------------------*
* PRINT SUMMARY REPORTS FOR THE WHOLE DATA COMPONENT                  *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
DCSUSPR  SUBENT
* SUMMARY LINE 1
         MVI   DCSL1C,C'-'        3 LINES SPACING
         LA    R11,DCSL1          ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTGDB         ADDRESS OF THE RPTG DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         LA    R10,RPTTDB         ADDRESS OF THE RPTT DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
* SUMMARY LINE 2
         MVC   DCSL2N,=X'4020202020202120' EDIT MASK INTO SUMMARY LINE2
         L     R14,DC#CA          NUMBER OF PROCESSED CA'S
         CVD   R14,D              CONVERT TO DECIMAL
         ED    DCSL2N,D+4         EDIT THE NUMBER OF CA'S
         MVI   DCSL2C,C'0'        2 LINES SPACING
         LA    R11,DCSL2          ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTGDB         ADDRESS OF THE RPTG DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         LA    R10,RPTTDB         ADDRESS OF THE RPTT DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
* SUMMARY LINE 3
         MVC   DCSL3N,=X'4020202020202120' EDIT MASK INTO SUMMARY LINE3
         L     R14,DC#CAFR        NUMBER OF FREE CA'S AT EOF
         CVD   R14,D              CONVERT TO DECIMAL
         ED    DCSL3N,D+4         EDIT THE NUMBER OF FREE CA'S
         MVI   DCSL3C,C'0'        2 LINES SPACING
         LA    R11,DCSL3          ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTGDB         ADDRESS OF THE RPTG DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         LA    R10,RPTTDB         ADDRESS OF THE RPTT DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         BAL   R14,BLANKL         PRINT A BLANK LINE ON BOTH REPORTS
* SUMMARY LINE 3 (SPACE DISTRIBUTION)
         LA    R6,DCSUS           DATA COMPONENT SUS ADDRESS
         MVC   CISIZE,CISIZED     DATA COMPONENT CISIZE
         BAL   R14,RPTTSDL        PRINT THE SPACE DISTRIBUTION LINE
         BAL   R14,RPTGSDL        PRINT THE SPACE DISTRIBUTION LINE
         SUBRET DCSUSPR           RETURN TO CALLER
         SPACE
*---------------------------------------------------------------------*
* PRINT SUMMARY REPORTS FOR THE WHOLE INDEX COMPONENT                 *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
IXSUSPR  SUBENT
* SUMMARY LINE 1
         MVI   IXSL1C,C'-'        3 LINES SPACING
         LA    R11,IXSL1          ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTGDB         ADDRESS OF THE RPTG DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         LA    R10,RPTTDB         ADDRESS OF THE RPTT DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         BAL   R14,BLANKL         PRINT A BLANK LINE
* SUMMARY LINE 2 (SPACE DISTRIBUTION)
         LA    R6,IXSUS           INDEX COMPONENT SUS ADDRESS
         MVC   CISIZE,CISIZEI     INDEX COMPONENT CISIZE
         BAL   R14,RPTTSDL        PRINT THE SPACE DISTRIBUTION LINE
         BAL   R14,RPTGSDL        PRINT THE SPACE DISTRIBUTION LINE
         SUBRET IXSUSPR           RETURN TO CALLER
         SPACE
*---------------------------------------------------------------------*
* PRINT DATA AND/OR INDEX COMPONENT STATISTICS                        *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
PRNTSTAT SUBENT
         USING AMB,R3             AMB ADDRESSABILITY
* STATISTICS LINE 1
         MVI   STL1C,C'-'         3 LINES SPACING
         MVC   STL1N,AMBDSNM      COMPONENT'S NAME
         LA    R11,STL1           ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTGDB         ADDRESS OF THE RPTG DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         LA    R10,RPTTDB         ADDRESS OF THE RPTT DEFINITION BLOCK
* PREVENT PRINTING OF SECOND HEADER LINE ON FIRST PAGE OF RPTT
         MVC   RPTGHTA1,RPTTHTA1  SAVE 2ND HDR LINE ADDRESS (IN RPTGDB)
         XC    RPTTHTA1,RPTTHTA1  CLEAR 2ND HDR ADDRESS (NO PRINT)
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         MVC   RPTTHTA1,RPTGHTA1  REST 2ND HDR LINE ADDRESS
         XC    RPTGHTA1,RPTGHTA1  CLEAR 2ND HDR ADDRESS (NO PRINT)
* STATISTICS LINE 2
         MVC   STL2CI,=X'6020202020202120' EDIT MASK (CI SIZE)
         L     R14,AMDCINV        CI SIZE
         CVD   R14,D              CONVERT TO DECIMAL
         ED    STL2CI,D+4         EDIT THE CI SIZE
         MVC   STL2CICA,=X'6020202020202120' EDIT MASK (CI/CA)
         LH    R14,AMDCIPCA       NO.OF CI'S PER CA
         CVD   R14,D              CONVERT TO DECIMAL
         ED    STL2CICA,D+4       EDIT THE CI/CA
         XR    R14,R14            CLEAR R14 (BEFORE IC)
         IC    R14,AMDPCTCI       % OF FREE BYTES IN A CI
         CVD   R14,D              CONVERT TO DECIMAL
         MVC   STL2FSCI,=X'602020202120' EDIT MASK (%CI FREESPACE)
         ED    STL2FSCI,D+5       EDIT THE FREESPACE-%CI
         IC    R14,AMDPCTCA       % OF FREE CI'S IN THE CA
         CVD   R14,D              CONVERT TO DECIMAL
         MVC   STL2FSCA,=X'602020202120' EDIT MASK (%CA FREESPACE)
         ED    STL2FSCA,D+5       EDIT THE FREESPACE-%CA
         MVC   STL2CISP,=X'6020202020202120' EDIT MASK (SPLITS-CI)
         L     R14,AMDNCIS        NO.OF CI SPLITS
         CVD   R14,D              CONVERT TO DECIMAL
         ED    STL2CISP,D+4       EDIT THE SPLITS-CI
         MVC   STL2CASP,=X'602020202120' EDIT MASK (SPLITS-CA)
         L     R14,AMDNCAS        NO.OF CA SPLITS
         CVD   R14,D              CONVERT TO DECIMAL
         ED    STL2CASP,D+5       EDIT THE SPLITS-CA
         MVI   STL2C,C'0'         2 LINES SPACING
         LA    R11,STL2           ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTGDB         ADDRESS OF THE RPTG DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         LA    R10,RPTTDB         ADDRESS OF THE RPTT DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
* STATISTICS LINE 3
         MVC   STL3IXL,=X'60202120' EDIT MASK (IX LVLS)
         LH    R14,AMDNIL         NUMBER OF INDEX LEVELS
         CVD   R14,D              CONVERT TO DECIMAL
         ED    STL3IXL,D+6        EDIT THE IX LEVELS
         MVC   STL3EXT,=X'602020202120' EDIT MASK (EXTENTS)
         LH    R14,AMDNEXT        NO.OF EXTENTS IN THE DATA SET
         CVD   R14,D              CONVERT TO DECIMAL
         ED    STL3EXT,D+5        EDIT THE EXTENTS #
         MVC   STL3EXCP,=X'60202020202020202120' EDIT MASK (EXCPS)
         L     R14,AMDEXCP        NO.OF EXCPS ISSUED BY VSAM
         CVD   R14,D              CONVERT TO DECIMAL
         ED    STL3EXCP,D+3       EDIT THE EXCP #
         MVI   STL3EXTF,C' '      CLEAR the field
         MVC   STL3EXTF+1(L'STL3EXTF-1),STL3EXTF
         TM    AMDATTR,AMDSDT     IMBED INDEX.........................?
         BNO   PRNTST3E           NO---------------------------------->
         MVC   STL3IMBD,=C'IMBED IX' YES, INDICATE IMBED INDEX
PRNTST3E TM    CAFLAGS,EAKSDS     Extended Addressability KSDS .......?
         BNO   PRNTST3X           NO---------------------------------->
         MVC   STL3EXTF,=C'Ext-Addr' Indicate extended address. KSDS
PRNTST3X MVI   STL3C,C'0'         2 LINES SPACING
         LA    R11,STL3           ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTGDB         ADDRESS OF THE RPTG DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         LA    R10,RPTTDB         ADDRESS OF THE RPTT DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         DROP  R3                 drop AMB addressability
         SUBRET PRNTSTAT          RETURN TO CALLER
         SPACE
*---------------------------------------------------------------------*
* PRINT THE SPACE DISTRIBUTION LINE - TABLE FORMAT                    *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
* A little subroutine entered via LNKREG1
* Add R0/R1 (8 byte counts) to R14/R15
ADD8BC   ALR   R15,R1             + CI freespace LO 4 bytes
         BC    12,ADD8BC1         no carry in add logical
         AL    R14,=F'1'          Carry: add 1 to HO 4 bytes of FSPC
ADD8BC1  ALR   R14,R0             + CI freespace HO 4 bytes
         BR    LNKREG1            Return
*
* Round all byte counts to 1KiB if total > 4GiB
* R14/R15 has 8 bytes count
RPPTRKiB TM    CAFLAGS,GT4GiB     Is rounding required ...............?
         BNOR  LNKREG1            No, return
         D     R14,=F'1024'       Round to 1KiB
         C     R14,=F'512'        If reminder >  512 will add 1 to
*                                 the result
         BNHR  LNKREG1            reminder <= 512  return
         AL    R15,=F'1'          reminder >  512  add 1 to result
         BR    LNKREG1            Return
*
RPTTSDL  SUBENT
         NI    CAFLAGS,X'FF'-GT4GiB      reset > 4GiB flag
* R6 HAS THE SPACE UTILIZATION SUMMARY ADDRESS ( SUS )
         USING SUS,R6             SUS ADDRESSABILITY
* Add counters to get total space in bytes
* If total space > 4GiB - will round it to KiB
* We are struggling with pre-z hardware 8 bytes counters here
         LM    R14,R15,USPC       LOAD used space in bytes
         LM    R0,R1,CIFSP        LOAD CI free space
         BAL   LNKREG1,ADD8BC     Add them
         L     R1,CAFSPCI#        # of CA free space CIs
         AL    R1,IX#SFCI         + # of UNUSED CI'S due to IX CI SHORT
*                                 = total free CIs
         M     R0,CISIZED         x DataCIsize = unused bytes
         BAL   LNKREG1,ADD8BC     Add them
* R14/R15 has total bytes
         LTR   R14,R14            Total bytes > 4GiB .................?
         BZ    RPPTSPD            No, convert to decimal ------------->
         OI    CAFLAGS,GT4GiB     Set total > 4GiB flag
* Round all byte counts to 1KiB if total > 4GiB
         BAL   LNKREG1,RPPTRKiB   Round result to 1KB
* R15 HAS NOW TOTAL SPACE IN BYTES or KiB
RPPTSPD  ST    R15,SUSTOTB        Save this for RPTG use
*                                 (since it needs to the same above
*                                  calculation)
         CVD   R15,D1             CONVERT TOTAL SPACE TO DECIMAL
*        USED SPACE NOW
         LM    R14,R15,USPC       LOAD used space in bytes
         BAL   LNKREG1,RPPTRKiB   Round result to 1KB if required
         CVD   R15,D              CONVERT used TO DECIMAL
         MVC   RPTTL1U,RPPTMSK1   EDIT MASK
         ED    RPTTL1U,D+2        EDIT THE USED SPACE
         MVI   RPTTL1U+L'RPTTL1U,C' ' Blank KiB units indicator
         TM    CAFLAGS,GT4GiB     Is this rounded to 1KiB.............?
         BNO   RPPTSPDK           NO---------------------------------->
         MVI   RPTTL1U+L'RPTTL1U,C'K' Yes, mark KiB units
RPPTSPDK BAL   R14,RPTTSDLP       CALCULATE AND EDIT THE % OF TOTAL
         MVC   RPTTL1UP,RPTTPCT+2 MOVE % VALUE INTO THE LINE
* CI FREE SPACE
         LM    R14,R15,CIFSP      LOAD CI free space in bytes
         BAL   LNKREG1,RPPTRKiB   Round result to 1KB if required
         CVD   R15,D              CONVERT TO BINARY
         MVC   RPTTL1I,RPPTMSK1   EDIT MASK
         ED    RPTTL1I,D+2        EDIT THE FREESPACE-CI
         MVI   RPTTL1I+L'RPTTL1I,C' ' Blank KiB units indicator
         TM    CAFLAGS,GT4GiB     Is this rounded to 1KiB.............?
         BNO   *+8                No---------------------------------->
         MVI   RPTTL1I+L'RPTTL1I,C'K' Yes, mark KiB units
         BAL   R14,RPTTSDLP       CALCULATE AND EDIT THE % OF TOTAL
         MVC   RPTTL1IP,RPTTPCT+2 MOVE % VALUE INTO THE LINE
* # OF USED CI'S IN THIS SUS
         L     R15,#UCIS          # of used CIs is this SUS
         CVD   R15,D              = # CI'S, CONVERT TO BINARY
         MVC   RPTTL1#O,RPPTMSK1   EDIT MASK
         ED    RPTTL1#O,D+2        EDIT # OF CI'S
* Will put total # of CIs in D1 work field (for % calucalation)
         L     R1,CAFSPCI#        # of CA free space CIs
         AL    R1,IX#SFCI         + # of UNUSED CI'S due to IX CI SHORT
         AL    R1,#UCIS           + # of used  CI'S
*                                 = total number of CIs
         CVD   R1,D1              CONVERT TOTAL CI# TO DECIMAL
* CA FREE SPACE (# of CIs)
         L     R1,CAFSPCI#        # of CA free space CIs
         CVD   R1,D               CONVERT TO BINARY
         MVC   RPTTL1#C,RPPTMSK1  EDIT MASK
         ED    RPTTL1#C,D+2       EDIT THE FREESPACE-CA
         BAL   R14,RPTTSDLP       CALCULATE AND EDIT THE % OF TOTAL
         MVC   RPTTL1CP,RPTTPCT+2 MOVE % VALUE INTO THE LINE
* FREE SPACE RESULTING FROM INDEX CI BEING TOO SHORT (# of CIs)
         L     R1,IX#SFCI         + # of UNUSED CI'S due to IX CI SHORT
         CVD   R1,D               CONVERT TO BINARY
         MVC   RPTTL1#U,RPPTMSK1   EDIT MASK
         ED    RPTTL1#U,D+2        EDIT THE FREESPACE-CI
         BAL   R14,RPTTSDLP       CALCULATE AND EDIT THE % OF TOTAL
         MVC   RPTTL1SP,RPTTPCT+2 MOVE % VALUE INTO THE LINE
* PRINT THE RPTT SPACE DISTRIBUTION LINE
         MVI   RPTTL1A,C' '       ENSURE ONE LINE SPACING
         LA    R11,RPTTL1         ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTTDB         ADDRESS OF THE RPTT DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         SUBRET RPTTSDL
         SPACE
* CALCULATE AND EDIT PERCENTAGE OF TOTAL SPACE (RPTT LINE RPTTL1)
RPTTSDLP DS    0H
* D1 CONTAINS TOTAL SPACE IN BYTES or KiB or # of CI's
* D  CONTAINS A SPACE DISTRIBUTION COMPONENT IN BYTES (DECIMAL)
*    or KiB or # of CIs
         MP    D,=PL2'100'        MULTIPLY BY A 100
         DP    D,D1+2(6)          DIVIDE BY TOTAL SPACE = %
         LH    R15,D              LOAD THE QUOTIENT INTO R4 (3 DECIMAL
*                                 DIGITS PLUS A SIGN)
         SRL   R15,4              ELIMINATE THE SIGN (SHIFTED OUT)
         XC    D(2),D             CLEAR TWO H/O BYTES
* A FIX TO ORIGINAL MULTILPY, REPLACED BY A SHIFT + MVC OF ZEROS/SIGN
* AT THE END OF THE OPERAND (0C7 EVEN THOUGH ENOUGH OF LEADING ZEROS)
*        MP    D,=PL3'1000'       MULTIPLY REMAINDER BY A 1000
         STM   R0,R1,R0R1SA       SAVE R0,R1
         LM    R0,R1,D            LOAD "D" INTO R0,R1
         SLDL  R0,12              SHIFT LEFT BY 12 BITS/3 ZONES
*                                 = MULTIPLY BY 1000
         STM   R0,R1,D            STORE RESULT INTO D
         MVC   D+6(2),=X'000C'    INSERT TRAILING ZEROS + DECIMAL SIGN
         LM    R0,R1,R0R1SA       RESTORE R0,R1
* END OF FIX, SEPT. 2/98
         DP    D,D1+2(6)          DIVIDE BY TOTAL SPACE = %
*                                 3 DIGITS AFTER DECIMAL POINT
         MVC   D+6(2),D           MOVE THEM TO THE END OF D
         STH   R15,D+4            PUT THE 3 DEC DIGITS BACK (SHIFTED
*                                 ONE POSITION TO THE RIGHT)
         XC    D(4),D             CLEAR 4 H/O BYTES
* SHIFT AND ROUND THE PERCENTAGE: 2 DEC.DIGITS TO THE RIGHT. ONE
* ROUNDED DECIMAL DIGITS AFTER DECIMAL POINT WILL BE LEFT
         SRP   D,64-2,5
         MVC   RPTTPCT,=X'40202021204B20' EDIT MASK INTO PERCENTAGE FLD
         ED    RPTTPCT,D+5        EDIT THE ROUNDED PERCENTAGE
         BR    R14                RETURN
         SPACE
*---------------------------------------------------------------------*
* PRINT A BLANK LINE ON RPTG AND RPTT                                 *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
BLANKL   SUBENT
         MVI   BLANKLC,C' '       ONE LINE SPACING
         LA    R11,BLANKLN        ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,RPTGDB         ADDRESS OF THE RPTG DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         LA    R10,RPTTDB         ADDRESS OF THE RPTT DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         SUBRET BLANKL
         SPACE
*---------------------------------------------------------------------*
* INPUT KSDS FILE IS EMPTY                                            *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
KSDSNL   DS    0H
         L     R11,=A(EMPTM1)     ADDRESS OF THE LINE TO BE PRINTED
         LA    R10,DMSGDB         ADDRESS OF THE DMSG DEFINITION BLOCK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         B     RC16               TERMINATE WITH RC=16
         SPACE
* CLOSE ALL FILES AND TERMINATE
CLOSE    BAL   R14,CLOSEP         CLOSE ALL FILES
         SPACE
RC0      SR    R15,R15            RC = 0 ( NORMAL COMPLETION )
         SPACE
RETURN   DS    0H
         L     R13,SA+4           PREVIOUS SAVE AREA ADDRESS
         RETURN (14,12),RC=(15)   RETURN
         SPACE
RC16     DS    0H                 RC = 16 ( ERROR )
         BAL   R14,CLOSEP         CLOSE ALL FILES
         LA    R15,16             RC = 16 ( ERROR )
         B     RETURN
         SPACE
*---------------------------------------------------------------------*
* CLOSE FILES PROC                                                    *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
CLOSEP   SUBENT
         CLOSE (VSAMF)            CLOSE THE KSDS
         CLOSE (MSGFILE,,RPTG,,RPTT,,LHKR)
         SUBRET CLOSEP
         SPACE
*---------------------------------------------------------------------*
* ABEND - INDEX RECORD JUST READ IS NOT A SEQUENCE SET RECORD         *
*         AS EXPECTED                                                 *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
ABEND0   ABEND 0,DUMP
         SPACE
*---------------------------------------------------------------------*
* OPEN/CLOSE ERROR ROUTINE                                            *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
OPENERR  DS    0H                 OPEN/CLOSE ERROR ROUTINE
         ST    R15,ACBERROR       SAVE RC FROM R15
         LA    HEXINAR,ACBERROR   INPUT ADDRESS FOR CONVHEX
         LA    HEXOUTAR,OCR15     OUTPUT ADDRESS FOR CONVHEX
         LA    HEXLEN,L'ACBERROR  LENGTH OF INPUT FIELD TO BE CONVERTED
         BAL   LNKREG1,CONVHEX    CONVERT RC (R15) TO HEXADECIMAL
         SHOWCB ACB=VSAMF,AREA=ACBERROR,LENGTH=4,                      +
               FIELDS=(ERROR)     GET ERROR FIELD FROM ACB
         LTR   R15,R15            SHOWCB OK...........................?
         BNZ   SHOWCBER           NO, SHOWCB ERROR-------------------->
         LA    HEXINAR,ACBERROR   INPUT ADDRESS FOR CONVHEX
         LA    HEXOUTAR,ACBERF    OUTPUT ADDRESS FOR CONVHEX
         LA    HEXLEN,L'ACBERROR  LENGTH OF INPUT FIELD TO BE CONVERTED
         BAL   LNKREG1,CONVHEX   CONVERT ACB ERROR FIELD TO HEXADECIMAL
         LA    MSGADDR,OPECLOEM   OPEN/CLOSE ERROR MESSAGE ADDRESS
         LA    RPTDBAR,DMSGDB     DIAGNOSTIC MSGS DEFINITION BLOCK
         BAL   R14,RPTPP          PRINT ERROR MESSAGE
         B     RC16               RETURN WITH RC=16
         SPACE
*---------------------------------------------------------------------*
* VSAM REQUEST ERROR ROUTINE                                          *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
IXRERROR DS    0H                 GETIX READ ERROR
         MVC   REQTYPE,=CL11'GETIX' REQUEST TYPE ID
         L     R8,=A(RPLIX)       RPL address (index read)
         B     ERROR              GO TO ERROR PROC-------------------->
POINTERR DS    0H                 POINT REQUEST ERROR
         MVC   REQTYPE,=CL11'POINT'  REQUEST TYPE ID
         B     DCIRERR1           LOAD ADDRESS OF THE RPL INTO R8----->
DCIRERR  DS    0H                 DATA CI READ ERROR
         MVC   REQTYPE,=CL11'GET DATA CI' REQUEST TYPE ID
DCIRERR1 L     R8,=A(RPLDC)       RPL ADDRESS
         SPACE
ERROR    DS    0H                 VSAM REQUEST ERROR: GETIX/GET DATA CI
         ST    R15,ACBERROR       SAVE RC FROM R15
         SHOWCB RPL=(8),AREA=FTNCD,LENGTH=8,                           +
               FIELDS=(FTNCD,FDBK)  GET FTNCD AND FDBK FIELD FROM RPL
         LTR   R15,R15            SHOWCB OK...........................?
         BNZ   SHOWCBER           NO, SHOWCB ERROR-------------------->
         LA    HEXINAR,ACBERROR   INPUT ADDRESS FOR CONVHEX
         LA    HEXOUTAR,REQR15    OUTPUT ADDRESS FOR CONVHEX
         LA    HEXLEN,L'ACBERROR  LENGTH OF INPUT FIELD TO BE CONVERTED
         BAL   LNKREG1,CONVHEX    CONVERT RC (R15) TO HEXADECIMAL
         SPACE
         LA    HEXINAR,FTNCD      INPUT ADDRESS FOR CONVHEX
         LA    HEXOUTAR,REQFTNCD  OUTPUT ADDRESS FOR CONVHEX
         LA    HEXLEN,L'FTNCD     LENGTH OF INPUT FIELD TO BE CONVERTED
         BAL   LNKREG1,CONVHEX    CONVERT FTNCD TO HEXADECIMAL
         SPACE
         LA    HEXINAR,FDBK       INPUT ADDRESS FOR CONVHEX
         LA    HEXOUTAR,REQFDBK   OUTPUT ADDRESS FOR CONVHEX
         LA    HEXLEN,L'FDBK      LENGTH OF INPUT FIELD TO BE CONVERTED
         BAL   LNKREG1,CONVHEX    CONVERT FDBK  TO HEXADECIMAL
         LA    MSGADDR,REQERRM    VSAM REQUEST ERROR MESSAGE ADDRESS
         LA    RPTDBAR,DMSGDB     DIAGNOSTIC MSGS DEFINITION BLOCK
         BAL   R14,RPTPP          PRINT ERROR MESSAGE
*********THESE ARE TEMPORARY, DIAGNOSTIC STMTS*************************
*        CLOSE (MSGFILE,,RPTG,,RPTT) **********************************
*        ABEND 1,DUMP ************A TEMPORARY DIAGNOSTIC DUMP**********
*********THESE ARE TEMPORARY, DIAGNOSTIC STMTS*************************
         B     RC16               TERMINATE THE PROGRAM--------------->
         SPACE
*---------------------------------------------------------------------*
* SHOWCB ERROR ROUTINE                                                *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
SHOWCBER DS    0H                 SHOWCB MACRO ERROR
         STM   R15,R0,ACBERROR    SAVE R15 AND R0 (ERROR CODE)
         LA    HEXINAR,ACBERROR   INPUT ADDRESS FOR CONVHEX
         LA    HEXOUTAR,SCBR15    OUTPUT ADDRESS FOR CONVHEX
         LA    HEXLEN,L'ACBERROR  LENGTH OF INPUT FIELD TO BE CONVERTED
         BAL   LNKREG1,CONVHEX    CONVERT RC (R15) TO HEXADECIMAL
         SPACE
         LA    HEXINAR,ACBERROR+4 INPUT ADDRESS FOR CONVHEX
         LA    HEXOUTAR,SCBR0     OUTPUT ADDRESS FOR CONVHEX
         LA    HEXLEN,L'ACBERROR  LENGTH OF INPUT FIELD TO BE CONVERTED
         BAL   LNKREG1,CONVHEX    CONVERT ERROR CODE TO HEXADECIMAL
         SPACE
         LA    MSGADDR,SCBERRM    SHOWCB ERROR MESSAGE ADDRESS
         LA    RPTDBAR,DMSGDB     DIAGNOSTIC MSGS DEFINITION BLOCK
         BAL   R14,RPTPP          PRINT ERROR MESSAGE
         B     RC16               TERMINATE THE PROGRAM WITH RC=16
         SPACE
*---------------------------------------------------------------------*
* MODCB ERROR ROUTINE                                                 *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
MODCBER  DS    0H                 MODCB MACRO ERROR
         STM   R15,R0,ACBERROR    SAVE R15 AND R0 (ERROR CODE)
         LA    HEXINAR,ACBERROR   INPUT ADDRESS FOR CONVHEX
         LA    HEXOUTAR,MCBR15    OUTPUT ADDRESS FOR CONVHEX
         LA    HEXLEN,L'ACBERROR  LENGTH OF INPUT FIELD TO BE CONVERTED
         BAL   LNKREG1,CONVHEX    CONVERT RC (R15) TO HEXADECIMAL
         SPACE
         LA    HEXINAR,ACBERROR+4 INPUT ADDRESS FOR CONVHEX
         LA    HEXOUTAR,MCBR0     OUTPUT ADDRESS FOR CONVHEX
         LA    HEXLEN,L'ACBERROR  LENGTH OF INPUT FIELD TO BE CONVERTED
         BAL   LNKREG1,CONVHEX    CONVERT ERROR CODE TO HEXADECIMAL
         SPACE
         LA    MSGADDR,MCBERRM    SHOWCB ERROR MESSAGE ADDRESS
         LA    RPTDBAR,DMSGDB     DIAGNOSTIC MSGS DEFINITION BLOCK
         BAL   R14,RPTPP          PRINT ERROR MESSAGE
         B     RC16               RETURN WITH RC=16
         SPACE
*---------------------------------------------------------------------*
* INPUT FILE IS NOT A KSDS                                            *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
NOTKSDS  LA    MSGADDR,NKSDSEM    NON-KSDS DATA SET ON INPUT
         LA    RPTDBAR,DMSGDB     DIAGNOSTIC MSGS DEFINITION BLOCK
         BAL   R14,RPTPP          PRINT ERROR MESSAGE
         B     RC16               RETURN WITH RC=16
         SPACE
*---------------------------------------------------------------------*
* CSI call error                                                      *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
         USING KSDSPACD,DATACSR   KSDSPACD addressability
CSICERR1 ST    R15,D              SAVE R15 in a work field
         LA    HEXINAR,D          INPUT ADDRESS FOR CONVHEX
         LA    HEXOUTAR,CSIEMR15  OUTPUT ADDRESS FOR CONVHEX
         LA    HEXLEN,4           LENGTH OF INPUT FIELD TO BE CONVERTED
         BAL   LNKREG1,CONVHEX    CONVERT RC (R15) TO HEXADECIMAL
         SPACE
         LA    HEXINAR,CSIRSNCODE INPUT ADDRESS FOR CONVHEX
         LA    HEXOUTAR,CSIERSNC  OUTPUT ADDRESS FOR CONVHEX
         LA    HEXLEN,1           LENGTH OF INPUT FIELD TO BE CONVERTED
         BAL   LNKREG1,CONVHEX    CONVERT RC (R15) TO HEXADECIMAL
         SPACE
         LA    HEXINAR,CSIRTNCODE INPUT ADDRESS FOR CONVHEX
         LA    HEXOUTAR,CSIEMRETC OUTPUT ADDRESS FOR CONVHEX
         LA    HEXLEN,1           LENGTH OF INPUT FIELD TO BE CONVERTED
         BAL   LNKREG1,CONVHEX    CONVERT RC (R15) TO HEXADECIMAL
         SPACE
         MVC   CSIEMID,CSIMODID   CSI call catalog module id
         LA    MSGADDR,CSIERRM    CSI call error message address
         LA    RPTDBAR,DMSGDB     DIAGNOSTIC MSGS DEFINITION BLOCK
         BAL   R14,RPTPP          PRINT ERROR MESSAGE
         LA    MSGADDR,CSIERRM1   CSI call error follow up message
         LA    RPTDBAR,DMSGDB     DIAGNOSTIC MSGS DEFINITION BLOCK
         BAL   R14,RPTPP          PRINT ERROR MESSAGE
         B     NOTBCS1            Will assume this is not an
*                                 extended addressability KSDS
         DROP  DATACSR
         SPACE
*---------------------------------------------------------------------*
*   TRANSLATION OF ERROR CODES INTO HEXADECIMAL SUBROUTINE            *
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
* HEXINAR  - INPUT FIELD ADDRESS
* HEXOUTAR - OUTPUT FIELD ADDRESS
* HEXLEN   - INPUT FIELD LENGTH
         SPACE
CONVHEX  DS    0H
         IC    TCHR,0(0,HEXINAR)  TRANSLATED CHARACTER
         SLL   TCHR,24            EXTRACT H/O HEX DIGIT
         SRL   TCHR,28
         LA    TCHR,HEXCH(TCHR)   HEX DIGIT ADDRESS
         MVC   0(1,HEXOUTAR),0(TCHR) MOVE HEX DIGIT INTO OUTPUT STRING
         IC    TCHR,0(0,HEXINAR)  TRANSLATED CHARACTER
         SLL   TCHR,28            EXTRACT L/O HEX DIGIT
         SRL   TCHR,28
         LA    TCHR,HEXCH(TCHR)   HEX DIGIT ADDRESS
         MVC   1(1,HEXOUTAR),0(TCHR) MOVE HEX DIGIT INTO OUTPUT STRING
         LA    HEXINAR,1(0,HEXINAR) NEXT TRANSLATED CHAR ADDRESS
         LA    HEXOUTAR,2(0,HEXOUTAR) NEXT OUTPUT ADDRESS
         BCT   HEXLEN,CONVHEX     TRANSLATE NEXT CHARACTER
         BR    LNKREG1            RETURN TO CALLER
         SPACE
IXRPT    SUBENT
* REPORT ON INDEX STRUCTURE: Print # OF IX RECORDS PER IX LEVEL
* Read index sequentially until SEOF.
* Print # of index record in each IX level.
* R4 - Index AMDSB address/pointer (saved in INDXDSBA)
         USING AMDSB,R4           Index AMDSB ADDRESSABLITY
* We need to get storage for the IXLVLT table
         LH    R0,AMDNIL          # of IX levels
         SLL   R0,1               times 2 = IXLVLT table size
*                                 (one word for each IX level)
         GETMAIN R,LV=(0),LOC=(24,31) Get storage for IXLVLT
         LR    R2,R1              IXLVLT address into R2
* We need to init the IXLVLT entries to 0
         USING IXLVLT,R2          IXLVLT addressability
         LH    R1,AMDNIL          # of IX levels
         XR    R0,R0              clear R0 (0 value)
         XR    R3,R3              clear R3 (IXLVLT index reg)
IXRPT00  STH   R0,IXLVLR#(R3)     clear IXLVLT entry
         LA    R3,2(,R3)          next IXLVLT entry address
         BCT   R1,IXRPT00         Contine for all IXLVL entries
* Now we will start reading index records sequentially until SEOF
         XC    IXCIRBAX(8),IXCIRBAX   Start from begining (RBA=0)
IXRPTR   DS    0H                 Read of the index record
         L     R1,=A(RPLIX)       RPL address (index read)
         GETIX RPL=(1)            GET AN INDEX (SS) RECORD
         LTR   R15,R15            GET of index RECORD OK..............?
         BNZ   IXRERROR           NO, INDEX READ ERROR---------------->
         L     R5,IXCIADDR        LOAD THE ADDRESS OF THE INDEX RECORD
         USING IXHDR,R5           INDEX RECORD (HEADER) ADDRESSABILITY
* CHECK IF THIS IS SEOF (SOFTWARE END-OF-FILE) CI
         LH    R14,IXHLL          LENGTH OF INDEX RECORD
         LA    R15,IXHDR+3(R14)   ADDRESS OF THE CIDF FIELD
         USING CIDF,R15           CIDF ADDRESSABLITY
         OC    CIDF(4),CIDF       ALL ZEROS IN CIDF...................?
         BZ    IXRPTEOF           YES, SOFTWARE END-OF-FILE----------->
* Lets validate the IXHLV field and update IXLVLR# in IXLVLT
         CLI   IXHLV,X'00'        A valid IX level should be from 1 to
*                                 AMDNIL
         BE    IXRPTE1            NO, SOMETHING WRONG, IX level=0----->
         XR    R3,R3              clear R3
         IC    R3,IXHLV           This IX recod index level
         CH    R3,AMDNIL          IX level <= AMDNIL .................?
         BH    IXRPTE1            NO, SOMETHING WRONG, IX level>max--->
         BCTR  R3,0               IX level - 1
         SLL   R3,1               * 2 = IXLVLR# offset in IXLVLT
* Update IX record cound on this index level
         LH    R1,IXLVLR#(R3)     current value
         LA    R1,1(,R1)          + 1
         STH   R1,IXLVLR#(R3)     current value
* XRBA of next IX record = current record XRBA + IX CISIZE
* This is a correct (I hope) 8 bytes binary arithmetic
IXRPTNR  LM    R0,R1,IXCIRBAX     current record XRBA
         AL    R1,CISIZEI         + IX CISIZE
         BC    12,IXRPTNR1        no carry in add logical IX CIsize
         LA    R15,1              Carry: put 1 in R15
         ALR   R0,R15             Carry: add 1 to HO 4 bytes of XRBA
IXRPTNR1 STM   R0,R1,IXCIRBAX     next record XRBA
         B     IXRPTR             Read next record
* We read all index record at this point
* Let's now print the counts (on RPTT only)
IXRPTEOF DS    0H
         LA    R11,IXRPTL1        ADDRESS OF headr line
         LA    R10,RPTTDB         ADDRESS OF THE REPORT DEFINITION BLK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         TM    AMDATTR,AMDSDT     IMBED INDEX.........................?
         BO    IXRPTPR0           Yes, will print the comment 1st lne->
         MVC   IXRPTL2C+1(L'IXRPTL2C-1),IXRPTL2C clear comment for
*                                 IMBED index (SS IX count not valid)
* Print IX level/ # of records lines
IXRPTPR0 LH    R3,AMDNIL          # of IX levels
         XR    R5,R5              clear R5 (IXLVLT index reg)
IXRPTPR1 MVC   IXRPTL2L,=X'402020202120' edit mask into # IX level
         ED    IXRPTL2L,IXRPTL#    edit to character format
         LH    R0,IXLVLR#(R5)     number of IX records at this level
         CVD   R0,D               convert to decimal
         MVC   IXRPTL2R,=X'4020202020202120' edit mask into # of recs
         ED    IXRPTL2R,D+4        edit to character format
         LA    R11,IXRPTL2        ADDRESS OF headr line
         LA    R10,RPTTDB         ADDRESS OF THE REPORT DEFINITION BLK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         MVC   IXRPTL2C+1(L'IXRPTL2C-1),IXRPTL2C clear comment for
*                                 IMBED index (SS IX count not valid)
         LA    R5,2(,R5)          next IXLVLT entry address
         AP    IXRPTL#,=PL1'1'    Index level +1
         BCT   R3,IXRPTPR1        Contine for all IXLVL entries
* We are done with IX levels report, let's free the IXLVLT
         LH    R0,AMDNIL          # of IX levels
         SLL   R0,1               times 2 = IXLVLT table size
*                                 (one word for each IX level)
         LR    R1,R2              IXLVLT address into R2
         FREEMAIN R,LV=(0),A=(1)  Free IXLVLT
IXRPTEX  SUBRET IXRPT             Return back to caller
         SPACE
IXRPTE1  DS    0H                 Index level invalid (0 or > max)
         MVC   IXRPTL3R,=X'4020206B2020206B2020206B202120'
         L     R0,IXCIRBA         RBA of this index record
         CVD   R0,D               convert to decimal
         ED    IXRPTL3R,D+2       edit to character format
         LA    R11,IXRPTL3        ADDRESS OF error msg line
         LA    R10,RPTTDB         ADDRESS OF THE REPORT DEFINITION BLK
         BAL   R14,RPTPP          REPORT PRINT PROCEDURE
         B     IXRPTNR            Read next IX record
         DROP  R2,R4,R5,R15       drop addressability for IXRPT
         SPACE
IXRPTL#  DC    PL3'1'             IX level
IXRPTL1  DC    AL2(IXRPTL1E-IXRPTL1) IXRPT header line
         DC    AL2(0)
         DC    C'0Index level    # of records'
IXRPTL1E EQU   *
         SPACE
IXRPTL2  DC    AL2(IXRPTL2E-IXRPTL2) IXRPT line 2
         DC    AL2(0)
         DC    C' '               ONE LINE SPACING
IXRPTL2L DC    X'402020202120'    Index level
         DC    C'          '
IXRPTL2R DC    X'4020202020202120'  # OF IX CIs at this level
IXRPTL2C DC    C' <-- SS IX count invalid when IMBED'
IXRPTL2E EQU   *
         SPACE
IXRPTL3  DC    AL2(IXRPTL3E-IXRPTL3) IXRPT index error line
         DC    AL2(0)
         DC    C'0Index level invalid in IX record, RBA='
IXRPTL3R DC    X'4020206B2020206B2020206B202120' RBA of IX record
         DC    C' record ignored'
IXRPTL3E EQU   *
         SPACE
USPCID   DC    C'*'               CHARACTER REPRESENTING USED SPACE
CAFSPCID DC    C'-'               CHARACTER REPRESENTING CA FREE SPACE
CIFSPCID DC    C'X'               CHARACTER REPRESENTING CI FREE SPACE
IXSFSPID DC    C'+'               CHARACTER REPRESENTING FREE SPACE
*                                 RESULTING FROM INDEX CI BEING TOO
*                                 SHORT
RPTGL1   DC    AL2(RPTGL1E-RPTGL1) RPTG PRINT LINE 1 (RDW - RECLEN)
         DC    AL2(0)
RPTGL1C  DC    C' '               ANSI PRINT CONTROL CHAR (1 LINES SPC)
RPTGL1A  DS    CL132              TEXT TO BE PRINTED
RPTGL1E  EQU   *
         SPACE
RPTHDR   DC    AL2(RPTHDRE-RPTHDR) REPORT HEADER LINE RDW - RECLEN)
         DC    AL2(0)
RPTHDRC  DC    C'1'               ANSI PRINT CONTROL - NEW PAGE
RPTHDRT  DS    CL122              HEADET TEXT
         DC    C'PAGE'
RPTHDRP  DC    X'402020202120'    PAGE NUMBER
RPTHDRE  EQU   *
         SPACE
STL1     DC    AL2(STL1E-STL1) COMPONENT STATISTICS LINE 1
         DC    AL2(0)
STL1C    DC    C'-'               ANSI PRINT CONTROL - SPACE 3 LINES
STL1N    DS    CL44               COMPONENT NAME
STL1T    DC    C'STATISTICS:'
STL1E    EQU   *
         SPACE
STL2     DC    AL2(STL2E-STL2) DC STATISTICS LINE 2
         DC    AL2(0)
STL2C    DC    C'0'               ANSI PRINT CONTROL - SPACE 2 LINES
         DC    C'CISIZE'
STL2CI   DC    X'6020202020202120'
         DC    C'    CI/CA'
STL2CICA DC    X'6020202020202120'
         DC    C'    FREESPACE-%CA'
STL2FSCA DC    X'602020202120'
         DC    C'    FREESPACE-%CI'
STL2FSCI DC    X'602020202120'
         DC    C'    SPLITS-CA'
STL2CASP DC    X'602020202120'
         DC    C'    SPLITS-CI'
STL2CISP DC    X'6020202020202120'
STL2E    EQU   *
         SPACE
STL3     DC    AL2(STL3E-STL3) DC STATISTICS LINE 3
         DC    AL2(0)
STL3C    DC    C'0'               ANSI PRINT CONTROL - SPACE 2 LINES
         DC    C'IX LEVELS-'
STL3IXL  DC    X'60202120'
         DC    C'    EXTENTS'
STL3EXT  DC    X'602020202120'
         DC    C'    EXCPS'
STL3EXCP DC    X'60202020202020202120'
         DC    C'        '
STL3IMBD DS    C'IMBED IX'
         ORG   STL3IMBD
STL3EXTF DS    C'Ext-Addr'
STL3E    EQU   *
         SPACE
IXSL1    DC    AL2(IXSL1E-IXSL1)  IX SUMMARY LINE 1
         DC    AL2(0)
IXSL1C   DC    C'-'               ANSI PRINT CONTROL - SPACE 3 LINES
IXSL1T   DC    C'SPACE UTILIZATION SUMMARY FOR THE INDEX COMPONENT:'
IXSL1E   EQU   *
         SPACE
DCSL1    DC    AL2(DCSL1E-DCSL1)  DC SUMMARY LINE 1
         DC    AL2(0)
DCSL1C   DC    C'-'               ANSI PRINT CONTROL - SPACE 3 LINES
DCSL1T   DC    C'SPACE UTILIZATION SUMMARY FOR THE DATA COMPONENT:'
DCSL1E   EQU   *
         SPACE
DCSL2    DC    AL2(DCSL2E-DCSL2)  DC SUMMARY LINE 2
         DC    AL2(0)
DCSL2C   DC    C'0'               ANSI PRINT CONTROL - SPACE 2 LINES
DCSL2T   DC    C'NUMBER OF PROCESSED (USED) CONTROL AREAS: '
DCSL2N   DC    X'4020202020202120'
DCSL2E   EQU   *
         SPACE
DCSL3    DC    AL2(DCSL3E-DCSL3)  DC SUMMARY LINE 3
         DC    AL2(0)
DCSL3C   DC    C'0'               ANSI PRINT CONTROL - SPACE 2 LINES
DCSL3T   DC    C'NUMBER OF FREE (UNUSED) CONTROL AREAS AT THE END OF TH+
               E DATA SET: '
DCSL3N   DC    X'4020202020202120'
DCSL3E   EQU   *
         SPACE
         SPACE
HEXCH    DC    C'0123456789ABCDEF' HEXADECIMAL CHARACTERS
         SPACE
BLANKLN  DC    AL2(BLANKLNE-BLANKLN)
         DC    AL2(0)
BLANKLC  DC    C'  '              ANSI PRINT CONTROL - SPACE 3 LINES
BLANKLNE EQU   *
         SPACE
R0R1SA   DS    2F                 SAVE AREA FOR R0,R1
RPTTPCT  DC    X'40202021204B20'  PERCENTAGE WORK FIELD
         SPACE
RPTTL1   DC    AL2(RPTTL1E-RPTTL1)  RPPT LINE 1
         DC    AL2(0)
RPTTL1A  DC    C' '               ONE LINE SPACING
RPTTL1U  DC    X'4020206B2020206B2020206B202120' USED SPACE
         DC    C'  ('
RPTTL1UP DC    CL5' '             PERCENTAGE OF TOTAL SPACE
         DC    C')'
RPTTL1#O DC    X'4020206B2020206B2020206B202120'   # OF used CI'S
         DC    C'  '
RPTTL1I  DC    X'4020206B2020206B2020206B202120' CI FREE SPACE
         DC    C'  ('
RPTTL1IP DC    CL5' '  CI FSPC    PERCENTAGE OF TOTAL SPACE
         DC    C')   '
         DC    C' ('
RPTTL1CP DC    CL5' ' CA free space: PERCENTAGE OF TOTAL SPACE
         DC    C')'
RPTTL1#C DC    X'4020206B2020206B2020206B202120'   # OF FREEE CI'S
         DC    C'   ('
RPTTL1SP DC    CL5' ' IX record short: PERCENTAGE OF TOTAL SPACE
         DC    C') '
RPTTL1#U DC    X'4020206B2020206B2020206B202120'    # OF UNUSED CI'S
RPTTL1E  EQU   *
         SPACE
         SPACE
RPTTDB   DS    6F                 RPTT DEFINITION BLOCK
         ORG   RPTTDB
*/////////////////////////////////////////////////////////////////////*
RPTTDCBA DC    A(RPTT)            DCB ADDRESS
RPTTLPL  DC    A(RPTLPP)          RPTT LINES PER PAGE LIMIT
RPTTLCNT DC    A(RPTLPP)          RPTT LINE COUNT (FORCE FIRST HEADER)
RPTTPGN  DC    A(1)               RPTT PAGE NUMBER
RPTTHTA  DC    A(RPTTHLT)         PAGE HEADER LINE TEXT ADDRESS
RPTTHTA1 DC    A(RPTTL1H)         SECOND PAGE HEADER LINE ADDRESS
*/////////////////////////////////////////////////////////////////////*
         SPACE
DMSGDB   DS    6F                 DIAGNOSTIC MSGS DEFINITION BLOCK
         ORG   DMSGDB
*/////////////////////////////////////////////////////////////////////*
DMSGDCBA DC    A(MSGFILE)         DCB ADDRESS
DMSGLPL  DC    A(RPTLPP)          DMSG LINES PER PAGE LIMIT
DMSGLCNT DC    A(RPTLPP)          DMSG LINE COUNT (FORCE FIRST HEADER)
DMSGPGN  DC    A(1)               DMSG PAGE NUMBER
DMSGHTA  DC    A(DMSGHLT)         PAGE HEADER LINE TEXT ADDRESS
DMSGHTA1 DC    A(0)               NO SECOND HEADER LINE FOR THIS RPT
*/////////////////////////////////////////////////////////////////////*
         SPACE
RPTGDB   DS    6F                 RPTG DEFINITION BLOCK
         ORG   RPTGDB
*/////////////////////////////////////////////////////////////////////*
RPTGDCBA DC    A(RPTG)            DCB ADDRESS
RPTGLPL  DC    A(RPTLPP)          RPTG LINES PER PAGE LIMIT
RPTGLCNT DC    A(RPTLPP)          RPTG LINE COUNT (FORCE FIRST HEADER)
RPTGPGN  DC    A(1)               RPTG PAGE NUMBER
RPTGHTA  DC    A(RPTGHLT)         PAGE HEADER LINE TEXT ADDRESS
RPTGHTA1 DC    A(0)               NO SECOND HEADER LINE FOR THIS RPT
*/////////////////////////////////////////////////////////////////////*
         SPACE
*/////////////////////////////////////////////////////////////////////*
*        THE FOLLOWING FULLWORDS ARE LOCATION DEPENDENT               *
DISPLAY  DS    0F                 FIELDS OBTAINED BY SHOWCB           *
LRECL    DS    F                  MAX. LRECL                          *
RKP      DS    F                  RELATIVE KEY POSITION               *
KEYLEN   DS    F                  KEY LENGTH                          *
ACBERROR DS    F                  ACB ERROR FIELD                     *
FTNCD    DS    F                  RPL FUNCTION CODE                   *
FDBK     DS    F                  RPL FEEDBACK FIELD                  *
*/////////////////////////////////////////////////////////////////////*
IXCIADDR DS    CL4                     INDEX CI ADDRESS IN A BUFFER
DCCIADDR DS    CL4                     DATA  CI ADDRESS IN A BUFFER
         SPACE
         IFGACB DSECT=NO,AM=VSAM       ACB LAYOUT
         ORG   IFGACB
VSAMF    ACB   AM=VSAM,DDNAME=KSDS,MACRF=(CNV,DIR,SEQ),STRNO=2
         SPACE
RPTOPNER DS    0H                 MSG DCB OPEN ERROR
         WTL   'KSDSPACE  - A report dataset could not be opened'
         B     RC16
         SPACE
OPECLOEM DC    AL2(OPECLOME-OPECLOEM) OPEN/CLOSE ERROR MESSAGE
         DC    AL2(0)
         DC    C'0OPEN/CLOSE ERROR  R15='
OCR15    DS    CL8                RC CODE IN R15
         DC    C' ACB ERROR FIELD='
ACBERF   DS    CL8                ACB ERROR FILED
OPECLOME EQU   *
         SPACE
REQERRM  DC    AL2(REQERRME-REQERRM) GET | PUT | ERASE ERROR MSG
         DC    AL2(0)
         DC    C'0VSAM '
REQTYPE  DC    CL11'GET DATA CI'  REQUEST TYPE
         DC    C' REQUEST ERROR  R15='
REQR15   DS    CL8                RC CODE IN R15
         DC    C' FTNCD='
REQFTNCD DS    CL8                REQUEST FUNCTION CODE
         DC    C' FDBK='
REQFDBK  DS    CL8                REQUEST FEEDBACK FIELD
REQERRME EQU   *
         SPACE
SCBERRM  DC    AL2(SCBERRME-SCBERRM) SHOWCB MACRO ERROR
         DC    AL2(0)
         DC    C'0SHOWCB MACRO ERROR  R15='
SCBR15   DS    CL8                RC CODE IN R15
         DC    C' R0='
SCBR0    DS    CL8                REQUEST FUNCTION CODE
SCBERRME EQU   *
         SPACE
MCBERRM  DC    AL2(MCBERRME-MCBERRM) MODCB MACRO ERROR MSG
         DC    AL2(0)
         DC    C'0MODECB MACRO ERROR  R15='
MCBR15   DS    CL8                RC CODE IN R15
         DC    C' R0='
MCBR0    DS    CL8                REQUEST FUNCTION CODE
MCBERRME EQU   *
         SPACE
NKSDSEM  DC    AL2(NKSDSEME-NKSDSEM) NOT KSDS FILE ERROR MESSAGE
         DC    AL2(0)
         DC    C'0NON-KSDS DATA SET IS NOT ALLOWED AS INPUT TO THIS PRO+
               GRAM'
NKSDSEME EQU   *
         SPACE
SGL1     DC    AL2(SGL1E-SGL1) LEGEND LINE FOR RPTG
         DC    AL2(0)
         DC    C'-*  REPRESENTS SPACE USED BY DATA RECORDS'
SGL1E    EQU   *
SGL2     DC    AL2(SGL2E-SGL2) LEGEND LINE FOR RPTG
         DC    AL2(0)
         DC    C'0-  REPRESENTS UNUSED CONTROL INTERVALS RESULTING FROM+
                CA-FREESPACE SPECIFICATION AND/OR CA SPLITS'
SGL2E    EQU   *
SGL3     DC    AL2(SGL3E-SGL3) LEGEND LINE FOR RPTG
         DC    AL2(0)
         DC    C'0X  REPRESENTS UNUSED CI SPACE RESULTING FROM CI-FREES+
               PACE SPECIFICATION AND/OR CI SPLITS'
SGL3E    EQU   *
SGL4     DC    AL2(SGL4E-SGL4) LEGEND LINE FOR RPTG
         DC    AL2(0)
         DC    C'0+  REPRESENTS UNUSED CA SPACE RESULTING FROM INDEX CI+
                BEING TOO SHORT'
SGL4E    EQU   *
         SPACE
* WORK AREAS/FILEDS
         DS    0D
D        DS    D                  A DOUBLEWORD
D1       DS    D                  A DOUBLEWORD
DATADSBA DS    A                  DATA COMPONENT STATISTICS BLOCK ADDR
INDXDSBA DS    A                  INDEX COMPONENT STATISTICS BLOCK ADDR
DCCIPFSP DS    A                  Address of free space in this data CI
*/////////////////////////////////////////////////////////////////////*
* 8 bytes binary RBA due to Extended Format and OPTCD=(XRBA)
IXCIRBAX DS    F                  HO part of XRBA
IXCIRBA  DS    F                  RBA OF THE NEXT INDEX (SS) CI TO BE
*                                 READ
*/////////////////////////////////////////////////////////////////////*
         SPACE
*/////////////////////////////////////////////////////////////////////*
* 8 bytes binary RBA due to Extended Format and OPTCD=(XRBA)
DCCIRBAX DS    F                  HO part of XRBA
DCCIRBA  DS    F                  RBA OF THE DATA CI TO BE READ
*/////////////////////////////////////////////////////////////////////*
         SPACE
*/////////////////////////////////////////////////////////////////////*
* 8 bytes binary RBA due to Extended Format and OPTCD=(XRBA)
DCCIRBAR DS    F                  HO part of XRBA
DCCIRBAC DS    F                  RBA OF THE RETRIEVED DATA CI
*/////////////////////////////////////////////////////////////////////*
VPLENGTH DS    F                  VERTICAL POINTER LENGTH IN AN INDEX
*                                 ENTRY
IXECILEN DS    F                  INDEX ENTRY CONTROL INFO.LENGTH
CISIZE   DS    F                  CISIZE FOR THE RPTTSDL SUBROUTINE
CISIZED  DS    F                  DATA COMPONENT CISIZE
CISIZEI  DS    F                  INDEX COMPONENT CISIZE
         SPACE
* INDEX (SEQUENCE SET) SPACE UTILIZATION SUMMARY
*/////////////////////////////////////////////////////////////////////*
IXSUS    DS    0CL16              4 FULLWORDS (LOCATION DEPENDENT)
IXUSPC   DC    2F'0'              USED SPACE IN IX SS in bytes
IXCIFSP  DC    2F'0'              CI FREE SPACE in IX SS
         DC    F'0'               n/a (CA free CIs)
         DC    F'0'               n/a
IX#CISS  DC    F'0'               # of IX SS CIs
*/////////////////////////////////////////////////////////////////////*
         SPACE
* DATA COMPONENT SPACE UTILIZATION SUMMARY
* has now 8 bytes binary counts (used space and CI free space only)
* This is to support EF datasets larger than 4GB.
*/////////////////////////////////////////////////////////////////////*
DCSUS    DS    0CL36              9  FULLWORDS (LOCATION DEPENDENT)
DCUSPC   DC    2F'0'              Used space DATA COMPONENT in bytes
DCCIFSP  DC    2F'0'              CI FREE SPACE in bytes
DC#CAFCI DC    F'0'               # of CA free CIs
DC#IXRSC DC    F'0'               # of UNUSED CI'S due to IX CI SHORT
DC#UCI   DC    F'0'               # of USED CI'S in DATA component
*                                 The below is not a part of SUS
DC#CA    DC    F'0'               NUMBER OF PROCESSED (USED CA'S)
*                                 IN THE DATA COMPONENT
DC#CAFR  DC    F'0'               NUMBER OF FREE (UNUSED) CA'S AT
*                                 THE END OF THE DATA SET
*/////////////////////////////////////////////////////////////////////*
         SPACE
* DATA COMPONENT CONTROL AREA SPACE UTILIZATION SUMMARY
* has now 8 bytes binary counts (used space and CI free space only)
* This is to support EF datasets larger than 4GB.
* (8 bytes are not required for a CA, however it has to match
*  the SUS DSECT).
*/////////////////////////////////////////////////////////////////////*
CASUS    DS    0CL(CASUSE-CAUSPC) 7 FULLWORDS (LOCATION DEPENDENT)
CAUSPC   DC    2F'0'              Used space DATA COMPONENT in bytes
CACIFSP  DC    2F'0'              CI FREE SPACE in bytes
CA#FCI   DC    F'0'               NUMBER OF FREE CI'S IN THE CA
CA#IXRSC DC    F'0'               # of UNUSED CI'S due to IX CI SHORT
CA#UCI   DC    F'0'               NUMBER OF USED CI'S IN THE CA
CASUSE   EQU   *
* The CI counts above are split into two halfwords becasue
* that is how they are used by the code (as halfwords)
*/////////////////////////////////////////////////////////////////////*
*
* WORK FIELDS FOR THE CURRENT CA: USED TO MAKE AN ASSUMPTION THAT
* NEXT SECTION OF INDEX COULD NOT BE CREATED AND THAT ALL CI'S
* CANNOT BE ADDRESSED BY THIS SS IX RECORD
*
SUSTOTB  DS    F             A work field, total bytes or KiB in SUS
CAIXRFSP DC    F'0'              IX SS RECORD: FREE SPACE IN THIS REC
CAIXRLSL DC    H'0'              IX SS RECORD: LAST IX SECTION LENGTH
*                                 (LAST SECTION CONTROL FIELD VALUE)
*
* Control Area processing flags
CAFLAGS  DC    X'00'              Control Area processing flags
CAFFDRP  EQU   X'01'              First data CI in this CA has
*                                 been processed (used for low/high
*                                 key report processing)
EAKSDS   EQU   X'02'              Extended Addressability KSDS flag
* (means it can be > 4GB)         (the flag is NOT CA related)
GT4GiB   EQU   X'04'              Total files size > 4GB
*                                 (this will result in rounding of
*                                 byte counts to 1KiB)
LHKFOUND EQU   X'08'              A LOW/HIGH key found in this CA
*                                 (this is to detect and empty CA)
         SPACE
LHKRDB   DS    6F                 LHKR DEFINITION BLOCK
         ORG   LHKRDB
*/////////////////////////////////////////////////////////////////////*
LHKRDCBA DC    A(LHKR)            DCB ADDRESS
LHKRLPL  DC    A(RPTLPP)          LHKR LINES PER PAGE LIMIT
LHKRLCNT DC    A(RPTLPP)          LHKR LINE COUNT (FORCE FIRST HEADER)
LHKRPGN  DC    A(1)               LHKR PAGE NUMBER
LHKRHTA  DC    A(LHKRHLT)         PAGE HEADER LINE TEXT ADDRESS
LHKRHTA1 DC    A(0)               NO SECOND HEADER LINE FOR THIS RPT
*/////////////////////////////////////////////////////////////////////*
         SPACE
* Low/high key report print line
* The record length will be set to (LHKL1KEY-LHKL1) + key length
* After KSDS ACB open
LHKL1    DC    AL2(LHKL1E-LHKL1)  Low/high key print line
         DC    AL2(0)
LHKL1CA# DC    X'4020202020202120' Control Area number
LHKL1CI# DC    X'402020202020202020202120' relative CI# of this data CI
         DC    X'40'
LHKL1KEY DS    CL255              Low/High key (max 255 chars)
LHKL1E   EQU   *
EMPTYCA  DC    C'***** EMPTY Control Area *****'
EMPTYCAS DS    CL2 work field to save the above record length
         SPACE
RPPTMSK1 DC    X'4020206B2020206B2020206B202120' edit mask for RPPT
* Should be the same as RPTTL1U field            count fields
         LTORG
* This is a CSECT with some control blocks/ data in order to relieve
* somewhat addressability constraints (we are approaching the 2 base
* registers limit).
KSDSPACD CSECT
*
* CAMLST FOR OBTAIN MACRO. OBTAIN READS 96 BYTES DATA PORTION OF FMT1
* DSCB (STARTING ON DS1FMTID, THAT'S WHY DS1FMTID IS CODED IN WORKAREA
* PARM OF CAMLST). OBTAIN WILL READ DATA INTO THE AREA GENERATED BY
* IECSDSL1 MACRO.
CAMLSTO  CAMLST SEARCH,DS1DSNAM,DS1DSSN,DS1FMTID
         IECSDSL1 (1)           FORMAT 1 DSCB DATA SET NAME AND BUFFER
         DS    CL(140-(DS1END-DS1FMTID)) OBTAIN WORKAREA HAS TO BE 140
*              BYTES. THIS PADDS THE REQUIRED BYTES
         SPACE
RPTGHLT  DC    AL1(L'RPTGHLTT)    LENGTH OF THE HEADER LINE TEXT
RPTGHLTT DC    C'KSDSPACE - RPTG: SPACE DISTRIBUTION IS SHOWN IN GRAPHI+
               CAL FORM'
         SPACE
RPTTHLT  DC    AL1(L'RPTTHLTT)    LENGTH OF THE HEADER LINE TEXT
RPTTHLTT DC    C'KSDSPACE - RPTT: SPACE DISTRIBUTION IS SHOWN IN BYTES/+
               CI# AND PRECENTAGES OF TOTAL'
         SPACE
RPTTL1H  DC    AL2(RPTTL1HE-RPTTL1H)  RPPT LINE 1 HEADER
         DC    AL2(0)
RPTTL1HA DC    C'-'               THREE LINES SPACING
         DC    CL15'     USED SPACE'
         DC    C'  ('
         DC    CL5'   % '         PERCENTAGE OF TOTAL SPACE
         DC    C')            #CI   '
         DC    CL15'   FREESPACE-CI'       CI FREE SPACE
         DC    C' ('
         DC    CL5'   % '         PERCENTAGE OF TOTAL SPACE
         DC    C')   '
         DC    C' FREESPACE-CA%'     CA FREE SPACE % of total
         DC    C'      #CI'
         DC    C'   IX REC SHORT%'  INDEX RECORD SHORT % of total
         DC    C'       #CI'
RPTTL1HE EQU   *
         SPACE
EMPTM1   DC    AL2(EMPTM1E-EMPTM1) EMPTY KSDSFILE ERROR MESSAGE
         DC    AL2(0)
EMPTM1C  DC    C'0'               ANSI PRINT CONTROL - SPACE 2 LINES
EMPTM1T  DC    C'INPUT KSDS DATA SET IS EMPTY. PROCESSING TERMINATED'
EMPTM1E  EQU   *
         SPACE
DMSGHLT  DC    AL1(L'DMSGHLTT)    LENGTH OF THE HEADER LINE TEXT
DMSGHLTT DC    C'KSDSPACE - DIAGNOSTIC MESSAGES'
         SPACE
LHKRHLT  DC    AL1(LHKRHLTE-LHKRHLTT)  LENGTH OF THE HEADER LINE TEXT
LHKRHLTT DC    C'     CA#     CI#    Low/High data record key '
         DC    C'in CA, KeyLength=' +
LHKRHLKL DC    X'4020202020202120' Key length
LHKRHLTE EQU   *
         SPACE
*  LHKR (LO/High key per CA) report output file
*  Max possible key lenght is specified. It will be adjusted/changed
*  after VSAM KSDS ACB open
LHKR     DCB   DSORG=PS,MACRF=PM,DDNAME=LHKR,RECFM=VBA,                +
               EXLST=MSGEXLST,LRECL=(LHKL1E-LHKL1)
         SPACE
*  MESSAGE SEQL DATA SET
MSGFILE  DCB   DSORG=PS,MACRF=PM,DDNAME=SYSPRINT,RECFM=VBA,            +
               LRECL=137,EXLST=MSGEXLST
MSGEXLST DS    0F
         DC    X'85',AL3(MSGDCBEX) EXIT FOR BLKSIZE NOT SPECIFIED
         SPACE
*  RPTG SEQL DATA SET
RPTG     DCB   DSORG=PS,MACRF=PM,DDNAME=RPTG,RECFM=VBA,                +
               LRECL=137,EXLST=MSGEXLST
         SPACE
*  RPTT SEQL DATA SET
RPTT     DCB   DSORG=PS,MACRF=PM,DDNAME=RPTT,RECFM=VBA,                +
               LRECL=137,EXLST=MSGEXLST
         SPACE
MSGDCBEX DS    0H                 EXIT IF BLKSIZE NOT SPECIFIED        +
                                  MSG AND REPORT DATA SETS
         USING IHADCB,R1          DCB ADDRESSABILITY
         LH    R2,DCBBLKSI        LOAD BLKSIZE
         LTR   R2,R2              BLKSIZE=0 ..........................?
         BNZR  R14                NO,RETURN <----------------
         LH    R2,DCBLRECL        LRECL
         LA    R2,4(,R2)          BLKSIZE = LRECL + 4 (RECFM=VB)
         STH   R2,DCBBLKSI        BLKSIZE=LRECL+4======================
         BR    R14                RETURN    <----------------
         DROP  R1
         SPACE
RPLIX    RPL   ACB=VSAMF,              RPL TO READ AN INDEX SS CI      +
               AREA=IXCIADDR,AREALEN=4,  FIELD FOR CI ADDRESS          +
               ARG=IXCIRBAX,             FIELD FOR XRBA                +
               OPTCD=(CNV,DIR,NUP,LOC,XRBA)
         SPACE
RPLDC    RPL   ACB=VSAMF,              RPL TO READ DATA COMPONENT      +
               AREA=DCCIADDR,AREALEN=4,  FIELD FOR CI ADDRESS          +
               OPTCD=(CNV,SEQ,NUP,LOC,XRBA)
         SPACE
CSIERRM  DC    AL2(CSIERRME-CSIERRM) CSI call ERROR MSG
         DC    AL2(0)
         DC    C'0CSI call error occurred: R15=x'
CSIEMR15 DS    CL8                RC CODE IN R15
         DC    C' ModID='
CSIEMID  DS    CL2                Catalog module id
         DC    C' RsnCode=x'
CSIERSNC DS    CL2                CSI REQUEST reason code
         DC    C' RetCode=x'
CSIEMRETC DS   CL2                CSI REQUEST return code
CSIERRME EQU   *
         SPACE
CSIERRM1 DC    AL2(CSIERRME1-CSIERRM1) CSI call ERROR MSG
         DC    AL2(0)
         DC    C'0Will assume this KSDS does not have Extended'
         DC    C' Addressability'
CSIERRME1 EQU   *
         SPACE
IGGCSIMA DC    V(IGGCSI00)        IGGCSI00 SUBROUTINE address
**********************************************************************
*                                                                    *
* PARAMETER LIST FOR IGGCSI00 INVOCATION                             *
*                                                                    *
**********************************************************************
*
CSIPARML DS    0D
         DC    A(CSIMRSRT)        MODULE/REASON/RETURN
         DC    A(CSIFIELD)        CSI selection criteria fields
         DC    A(CSIRWORK)        data returned from CSI
*
**********************************************************************
*                                                                    *
* MODULE ID/REASON CODE/RETURN CODE from CSI                         *
*                                                                    *
**********************************************************************
*
CSIMRSRT    DS 0F
CSIMRC      DS 0CL4
CSIMODID    DC XL2'0000'     MODULE ID
CSIRSNCODE  DC XL1'00'       REASON CODE
CSIRTNCODE  DC XL1'00'       RETURN CODE
**********************************************************************
*                                                                    *
* PARAMETER FIELDS FOR CATALOG SEARCH INTERFACE (CSI)                *
*                                                                    *
**********************************************************************
*
CSIFIELD    DS 0F
CSIFILTK    DC CL44'**'  FILTER   KEY
CSICATNM    DC CL44' '        CATALOG NAME OR BLANKS
CSIRESNM    DC CL44' '        RESUME NAME OR BLANKS
CSIDTYPD    DS 0CL16          ENTRY TYPES
CSIDTYPS    DC CL16'C               ' Cluster only
CSIOPTS     DS 0CL4           CSI OPTIONS
CSICLDI     DC CL1' '         RETURN D&I IF C A MATCH Y OR BLNK
CSIRESUM    DC CL1' '         RESUME FLAG         Y OR BLANK
CSIS1CAT    DC CL1'Y'         SEARCH CATALOG      Y OR BLANK
CSIOPTNS    DC CL1' '         halfword lengths entries
CSINUMEN    DC AL2((CSIENTSE-CSIENTS)/8)  NUMBER OF ENTRIES FOLLOWING
CSIENTS     DS 0CL8           VARIABLE NUMBER OF ENTRIES FOLLOW
CSIFLDNM1   DC CL8'XACIFLAG'  Extended attribute flags
CSIENTSE    DS 0CL1           end of ENTRIES label
*
CSIRWORK    DS 0F             CSI Return Work Area
CSIUSRLN    DC A(CSIRWORKE-CSIRWORK)     Work Area length
CSIREQLN    DS F              Minimum work area length (1 entry
*                             for catalog + 1 entry)
CSIUSDLN    DS F              length of work area used
CSINUMFD    DS H              Number of field names plus 1
* INFORMATION RETURNED FOR EACH CATALOG
CSICFLG     DS CL1            Catalog flag information
CSICTYPE    DS CL1            Catalog type. X'F0'
CSICNAME    DS CL44           Catalog name
CSICRETN    DS 0CL4           Return information for Catalog
CSICRETM    DS CL2            Catalog module id
CSICRETR    DS CL1            Catalog return reason code
CSICRETC    DS CL1            Catalog return code
* INFORMATION RETURNED FOR EACH ENTRY
CSIEFLAG    DS XL1            Entry flag information
CSIENTER    EQU B'01000000'   Error indication is set for this entry
CSIEDATAR   EQU B'00100000'   Data is returned for this entry
CSIETYPE    DS CL1            Entry type
CSIENAME    DS CL44           Entry name
CSIERETN    DS 0CL4           Error return information if CSIENTER=1
* CSIERETN exists only if CSIENTER=1, hence the ORG below
CSIERETM    DS CL2            Entry return module ID
CSIERETR    DS CL1            Entry return reason code
CSIERETC    DS CL1            Entry return code
*
            ORG CSIERETN
* CSIOPTNS IS NOT F
CSIEDATA    DS 0CL1           Returned data for entry if CSIENTER=0
CSITOTLN    DS AL2            Total length of returned information
            DS CL2            reserved
CSILENFD    DS 0CL1           Length of fields
CSILENF1    DS AL2            First length field
*
* INFORMATION RETURNED FOR EACH FIELD NAME
CSIFDDAT    DS 0CL1           Field data
CSIFEAFLG   DS CL1            Extended attribute flags
CSIFEA4GB   EQU B'01000000'   Extended Addressability flag
*                             (Data set can be greater than 4GB)
*
            DS CL(1024-(*-CSIRWORK)) minimum work area length=1024
CSIRWORKE   DS 0CL1           CSI Return Work Area end label
*
         SPACE
*
 DC C'"COPYRIGHT (C) 2002 by Jan (Janek) Jakubek. '
 DC C'ALL RIGHTS  RESERVED EXCEPT:  PARTICULAR LICENSE IS GRANTED TO '
 DC C'DISTRIBUTE THIS PROGRAM FREE OF CHARGE, '
 DC C'BUT IT MUST NOT BE SOLD."'
*
CODENDS  EQU   *
         SPACE
* VSAM ACCESS METHOD DATA SET STATISTICS BLOCK
AMDSB    DSECT
AMDSBID  DS    XL1                CONTROL BLOCK IDENTIFIER, X'60'
AMDATTR  DS    XL1                ATTRIBUTES OF THE DATA SET:
AMDDST   EQU   X'80'              KEY-SEQUENCED DATA SET
AMDWCK   EQU   X'40'              CHECK EACH RECORD WHEN IT IS WRITTEN
AMDSDT   EQU   X'20'              SEQUENCE SET IS STORED WHITH THE
*                                 DATA AND REPLICATED
AMDREPL  EQU   X'10'              ALL INDEX RECORDS ARE REPLICATED
AMDORDER EQU   X'08'              USE THE VOLUMES IN THE SAME ORDER
*                                 AS IN THE VOLUME LIST
AMDRANGE EQU   X'04'              THE DATA SET IS DIVIDED INTO KEY
*                                 RANGES
AMDRRDS  EQU   X'02'              RELATIVE RECORD DATA SET
AMDSPAN  EQU   X'01'              THE DATA SET CONTAINS SPANNED RECORDS
AMDLEN   DS    XL2                LENGTH OF THE AMDSB
AMDNEST  DS    XL2                NUMBER OF INDEX ENTRIES IN THE INDEX
*                                 SECTION
AMDRKP   DS    XL2                RELATIVE KEY POSITION
AMDKEYLN DS    XL2                KEY LENGTH
AMDPCTCA DS    XL1                % OF FREE CI IN THE CA
AMDPCTCI DS    XL1                % OF FREE BYTES IN THE CI
AMDCIPCA DS    XL2                NO. OF CONTROL INTERVALS IN A CA
AMDFSCA  DS    XL2                NO. OF FREE CONTROL INTERVALS IN A CA
AMDFSCI  DS    XL4                NO. OF FREE BYTES IN A CI
AMDCINV  DS    XL4                CONTROL INTERVAL SIZE
AMDLRECL DS    XL4                MAXIMUM RECORD LENGTH
AMDHLRBA DS    XL4                RBA OF THE HIGH-LEVEL INDEX RECORD
AMDNSLOT EQU   AMDHLRBA           NO. OF RECORD SLOTS PER CI
AMDSSRBA DS    XL4                RBA OF THE FIRST SEQUENCE-SET RECORD
AMDPARDB DS    XL4                ADDRESS OF THE FIRST ARDB
AMDSTAT  DS    0CL56              DATA SET STATISTICS
AMDATTR3 DS    XL1                ATTRIBUTES OF THE DATA SET
AMDUNQ   EQU   X'80'              THE DATA SET HAS NONUNIQUE KEYS
AMDLM    EQU   X'08'              DATA SET IS LOADED
AMDSTRNO DS    XL1                NO.OF CONCURRENT REQUESTS
AMDDUI   DS    XL4                IMS DBRC USAGE INDICATOR
AMDBFNO  DS    XL2                NUMBER OF BUFFERS
AMDSTSP  DS    XL8                SYSTEM TIME STAMP
AMDNIL   DS    XL2                NUMBER OF INDEX LEVELS
AMDNEXT  DS    XL2                NUMBER OF EXTENTS IN THE DATA SET
AMDNLR   DS    XL4                NUMBER OF USER SUPPLIED RECORDS
AMDDELR  DS    XL4                NUMBER OF DELETED RECORDS
AMDIREC  DS    XL4                NUMBER OF INSERTED RECORDS
AMDUPR   DS    XL4                NUMBER OF UPDATED RECORDS
AMDRETR  DS    XL4                NUMBER OF RETRIEVED RECORDS
AMDASPA  DS    XL4                NO.OF BYTES OF FREE SPACE
AMDNCIS  DS    XL4                NO.OF CI SPLITS
AMDNCAS  DS    XL4                NO.OF CA SPLITS
AMDEXCP  DS    XL4                NO.OF EXCPS ISSUED BY VSAM ROUTINES
         SPACE
* ACCESS METHOD BLOCK
AMB      DSECT
AMBID    DS    XL1                CONTROL BLOCK IDENTIFIER, X'40'
AMBRSC   DS    XL1                RESOURCE TS BYTE
AMBLEN   DS    XL2                LENGTH OF THE AMB
AMBLINK  DS    XL4                ADDRESS OF THE NEXT AMB IN THE CHAIN
AMBBUFC  DS    XL4                ADDRESS OF THE BUFC HEADER
AMBPH    DS    XL4                ADDRESS OF PLH HEADER
AMBCACB  DS    XL4                ADDRESS OF THE VSAM CATALOG'S ACB
AMBDSB   DS    XL4                ADDRESS OF THE AMDSB
AMBEOVR  DS    XL1                END-OF-VOLUME REQUEST TYPE
AMBFLG0  EQU   AMBEOVR            MVM AMB FLAGS:
AMBPSDS  EQU   X'80'              PAGE SPACE
AMBSWSP  EQU   X'40'              SWAP SPACE
AMBSHR1  EQU   X'20'              SHARE OPTION
AMBFLG1  DS    XL1                INDICATOR FLAGS:
AMBCREAT EQU   X'80'              THE OBJECT IS BEING CREATED (LOADED)
AMBTYPE  EQU   X'40'              THE AMB DESCRIBES THE KSDS INDEX
AMBMCAT  EQU   X'20'              THE AMB DESCRIBES THE MASTER CATALOG
AMBUCAT  EQU   X'10'              THE AMB DESCRIBES A USER CATALOG
AMBSPEED EQU   X'08'              SPEED OPTION
AMBUBUF  EQU   X'04'              USER BUFFERING HAS BEEN SPECIFIED
AMBJRN   EQU   X'02'              EXLST HAS A JOURNALING EXIT RTN ADDR
AMBINBUF EQU   X'01'              THE DATA SET IS SHARED - DIRECT BUF
AMBDSORG DS    XL2                DATA SET ORGANIZATION INDICATORS
AMBIOMB  DS    XL4                ADDRESS OF THE IOMB
AMBCDSN  DS    XL3                DSN OF THE CATALOG
AMBDDSN  DS    XL3                DSN OF OBJECT ASSOC.WITH THE AMB
         DS    XL2                RESERVED
AMBTIOT  DS    XL2                OFFSET TO TIOT
AMBINFL  DS    XL1                INDICATOR FLAGS
AMBCAT   EQU   X'10'              THE AMB DESCRIBES A CATALOG
AMBSCRA  EQU   X'08'              CRA IS IN SYSTEM STORAGE
AMBUCRA  EQU   X'04'              CRA IS IN USER'S STORAGE
AMBUPX   EQU   X'02'              AN UPGRADE TABLE (UPT) EXISTS
AMBSDS   EQU   X'01'              SYSTEM DATA SET
AMBAMETH DS    XL1                VSAM ACCESS METHOD INDICATOR
AMBDEBPT DS    0XL4               DEB ADDRESS
AMBIFLGS DS    XL1                ERROR FLAGS
AMBDEBAD DS    XL3                DEB ADDRESS
AMBOFLGS DS    XL1                OPEN STATUS FLAGS:
AMBOPEN  EQU   X'10'              THE AMB IS OPEN
AMBEXFG  EQU   X'02'              USER EXIT ROUTINES ARE ACTIVE
AMBBUSY  EQU   X'01'              BUSY BIT
AMBFLG2  DS    XL1                FLAG BYTE 2:
AMBPUG   EQU   X'80'              DATA SET IS AN AIX IN AN UPGRADE SET
AMBSHR   EQU   X'40'              DATA SET USING SHARE OPTIONS 3 OR 4
AMBUP    EQU   X'20'              UPAD IS PRESENT
AMBRPT   DS    XL2
AMBEDB   DS    XL4                ADDRESS OF THE EDB
AMBEOVPT DS    XL4                ADDRESS OF THE AMBXN FOR AN EOV REQ.
AMBAMBXN EQU   AMBEOVPT           POINTER TO AMB EXTENTION IN MVB
AMBWKA   DS    XL4                ADDRESS OF THE AMB WORK AREA
AMBIWA   DS    XL4                ADDRESS OF THE DIWA
AMBIOBA  DS    XL4                ADDRESS OF THE IOB
AMBSVI   EQU   AMBIOBA            OBJECT VSI POINTER
AMBPIXP  DS    XL4                ADDRESS OF THE INDEX'S AMB
AMBPAMBL DS    XL4                ADDRESS OF THE PRIMARY AMBL
AMBUPLH  DS    XL4                ADDRESS OF UPGRADE PLACEHOLDER
AMBAFLG  DS    XL1                FLAG BYTE:
AMBLSR   EQU   X'40'              LOCAL SHARED RESOURCES
AMBGSR   EQU   X'20'              GLOBAL SHARED RESOURCES
AMBICI   EQU   X'10'              IMPROVED CONTROL-INTERVAL ACCESS
AMBDFR   EQU   X'08'              DEFER WRITE OPERATIONS
AMBSIS   EQU   X'04'              SEQUENTIAL INSERT STRATEGY
AMBCFX   EQU   X'02'              CONTROL BLOCKS FIXED IN REAL STORAGE
         DS    XL1                RESERVED
AMBRDCNT DS    XL2                RESERVED
AMBBM2SH DS    XL4                RESERVED
AMBCPA   DS    XL4                LSR - ADDRESS OF THE WSHD
*                                 NSR - ADDR OF THE FIRST CPA IN CHAIN
AMBWSHD  DS    XL4                ADDR OF WORKING STORAGE HEADER
AMBEXEX  DS    XL8                NAME OF USER EXCPETION EXIT ROUTINE
AMBSZRD  DS    XL2                SIZE OF THE CHANNEL PROG. FOR READ
AMBSZWR  DS    XL2                SIZE OF THE CHANNEL PROG. FOR WRITE
AMBSZFW  DS    XL2               SIZE OF THE CHAN.PROG.FOR FORMAT WRITE
AMBSZCP  DS    XL2                SIZE OF THE CPA BASE
AMBVIOT  DS    XL4                ADDR OF THE VALID-IOMB TABLE
AMBTRACE DS    XL4                ADDRESS OF TRACE TABLE
AMBPAL   DS    XL3                PRIMARY ALLOCATION QUANTITY
         DS    XL1                RESERVED
AMBBSAL  DS    XL3                SECONDARY ALLOCATION QUANTITY
         DS    XL1                RESERVED
AMBSOPT  DS    XL1                ALLOCATION OPTION
AMBRNLEN DS    0XL3               RNAME LENGTHS
AMBRLN   DS    XL1                LENGTH OF RNAME
AMBDSLN  DS    XL1                LENGTH OF DSNAME
AMBCATLN DS    XL1                LENGTH OF CATALOG DSNAME
AMBDSNM  DS    CL44               COMPONENT NAME
AMBUPAD  DS    XL4                POINER TO UPAD ROUTINE
AMBJRNAD DS    XL4                POINER TO JRNAD ROUTINE
         SPACE
* ADDRESS RANGE DEFINITION BLOCK
ARDB     DSECT
ARDID    DS    XL1                CONTROL BLOCK ID, X'61'
ARDTYPE  DS    XL1                TYPE OF SPACE DEFINED BY THE ARDB:
ARDKR    EQU   X'80'              ONE KEY RANGE OF A KEY-RANGE DATA SET
ARDHLI   EQU   X'40'              THE TOTAL INDEX OF A KSDS, OR
*                                 NON SS INDEX PART IF SS IS IMBEDED
ARDSS    EQU   X'20'              THE SS OF A KSDS IF SS IS IMBEDED
ARDUOVFL EQU   X'10'              OVERFLOW MAY BE USED FOR THIS KEY RNG
ARDEOD   EQU   X'08'              KEY RANGE WITH HIGHEST DATA RBA
ARDUSED  EQU   X'04'              THE ARDHRBA INITIAL VALUE HAS CHANGED
ARDUPD   EQU   X'02'              ARDB MODIFIED
ARDLEN   DS    XL2                LENGTH OF THE ARDB
ARDNPTR  DS    XL4                POINTER TO NEXT ARDB IN THE CHAIN
ARDHKRBA DS    XL4                RBA OF DATA CI WITH HIGHEST KEY
ARDHRBA  DS    XL4                RBA OF NEXT FREE SPACE CI AT THE EOF
ARDERBA  DS    XL4                RBA OF THE HIGHEST CI ALLOCATED
ARDVOLSR DS    CL6                VOLSER WITH HIGHEST RBA ALLOCATED
ARDRELNO DS    XL2                SEQ.NO.OF THE DATA SET GROUP SET
ARDPRF   DS    XL1                PREFORMAT FLAGS:
ARDPRSS  EQU   X'80'              THE SS IS STORED WITH DATA
ARDPRFMT EQU   X'40'              THE KEY RANGE'S EXTENTS HAS BEEN
*                                 PREFORMATTED
ARDKEYS  DS    0XL1               LOW AN HIGH KEY VALUES (2 * KEYLEN)
         SPACE
* INDEX RECORD HEADER DSECT
IXHDR    DSECT
IXHLL    DS    XL2                IX RECORD LENGTH (CI SIZE - 7)
IXHFLPLN DS    XL1                LENGTH OF CONTROL INFO. IN EACH
*                                 INDEX ENTRY (IBFLPF, IBFLPL, IBFLP3)
IXHPTLS  DS    XL1                VERTICAL POINTERS LENGTH
IXHBRBA  DS    XL4                SS - RBA OF THE DATA CA
IXHHP    DS    XL4                HORIZONTAL POINTER (RBA) TO NEXT REC.
IXHXX    DS    XL4                RESERVED
IXHLV    DS    XL1                INDEX LEVEL NUMBER ( = 1 FOR SS )
IXHFLGS  DS    XL1                RESERVED
IXHFSO   DS    XL2                FREE SPACE OFFSET (IN THIS CI)
IXHLEO   DS    XL2                LAST INDEX ENTRY OFFSET
IXHSEO   DS    XL2                OFFSET OF THE LAST ENTRY IN THE FIRST
*                                 SECTION
         SPACE
* SPACE UTILIZATION SUMMARY (FOR CA, SS, DATA SET)
SUS      DSECT
USPC     DC    2F'0'              Used space DATA COMPONENT in bytes
CIFSP    DC    2F'0'              CI FREE SPACE in bytes
CAFSPCI# DC    F'0'               # of CA free space CIs
IX#SFCI  DC    F'0'               # of UNUSED CI'S due to IX CI SHORT
#UCIS    DC    F'0'               # of USED CI'S in DATA component
         SPACE
* CONTROL INTERVAL DEFINITION FIELD FORMAT
CIDF     DSECT
CIDFOSET DS    XL2                OFFSET OF UNUSED SPACE OR CNTRL INFO.
*                                 (= RECL (CI-7) FOR IX)
CIDF11   DS    XL2                LENGTH OF UNUSED SPACE (=0 FOR IX)
         SPACE
* INDEX ENTRY CONTROL INFORMATION FORMAT
IXECIFMT DSECT
IBFLPF   DS    XL1                FRONT-KEY COMPRESSION COUNT
IBFLPL   DS    XL1                LENGTH OF IXKEY FIELD IN THIS ENTRY
IBFLP3   DS    XL3                POINTER TO DATA CI (1 TO 3 BYTES)
*                                 This is data CI number in this CA
*                                 (beginning at 0)
         SPACE
* DATA CI Record Definition Field pair (Left and Right)
* If RDF is not paired - there is only the one (Right) RDF
RDF      DSECT
* Left  RDF
LRDFF    DS    XL1                Control field (flags)
*                                 For spanned records:
LRDFFS   EQU   X'10'              - first segment
LRDFLS   EQU   X'20'              - last  segment
LRDFIS   EQU   X'30'              - intermediate segment
LRDF#R   EQU   X'08'              Non spanned: # of recs of same length
*                                 Spanned recs: segment update #
LRDF#    DS    XL2          Fixed length non spanned: # of records
*                           Spanned : segment update number
* Right RDF
RRDFF    DS    XL1                Control field (flags)
RRDFP    EQU   X'40'              a paired RDF to the left of this one
*                                 For spanned records:
RRDFFS   EQU   X'10'              - first segment
RRDFLS   EQU   X'20'              - last  segment
RRDFIS   EQU   X'30'              - intermediate segment
RRDF#    DS    XL2                record/segment/slot length
         SPACE
* REPORT DEFINITION BLOCK
* ALLOWS TO USE ONE PROCEDURE TO PRINT RPTG, RPTT, AND MESSAGES
RPTDB    DSECT
*/////////////////////////////////////////////////////////////////////*
RPTDCBA  DS    A                  DCB ADDRESS
RPTLPL   DS    A(RPTLPP)          RPTG LINES PER PAGE LIMIT
RPTLCNT  DS    A(RPTLPP)          RPTG LINE COUNT (FORCE FIRST HEADER)
RPTPGN   DS    A(1)               RPTG PAGE NUMBER
RPTHTA   DS    A                  PAGE HEADER LINE TEXT ADDRESS
RPTHTA1  DS    A                  SECOND HEADER LINE ADDRESS
*/////////////////////////////////////////////////////////////////////*
         SPACE
* This is table for IXRPT proc (print # of Index records per IX level)
IXLVLT   DSECT
IXLVLR#  DS    H                  # of IX records per level (as many
*                                 entries as index levels)
         SPACE
         IFGRPL DSECT=YES,AM=VSAM       RPL LAYOUT
         SPACE
         DCBD  DSORG=PS
         SPACE
         CVT   DSECT=YES                CVT MAPPING MACRO
         SPACE
         END   KSDSPACE
/*
//*
//* IGGCSI00 is being linked due to KSDSPACE being AMODE=24
//LKED     EXEC PGM=IEWL,
//         PARM='NCAL,LET,LIST,XREF,AC=1'
//SYSLMOD  DD DISP=SHR,DSN=...your.loadlib....
//LINKLIB  DD DISP=SHR,DSN=SYS1.LINKLIB
//SYSUT1   DD UNIT=SYSALLDA,SPACE=(TRK,(10,10))
//SYSPRINT DD SYSOUT=*
//SYSLIN   DD DSN=&&SYSLIN,DISP=(OLD,DELETE)
//         DD DDNAME=SYSIN
//SYSIN    DD *
 INCLUDE LINKLIB(IGGCSI00)
 ENTRY KSDSPACE
 NAME  KSDSPACE(R)
/*
