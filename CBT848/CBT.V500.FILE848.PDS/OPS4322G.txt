LIBPRT   CSECT
*/****************************************************************** /*
*/*                                                                  */
*/*       WRITTEN BY :             DONALD HARRIS                     */
*/*                                OCCIDENTAL CHEMICAL CORPORATION   */
*/*                                360 RAINBOW BLVD. SOUTH           */
*/*                                NIAGARA FALLS , NEW YORK 14303    */
*/*                                (716) 286-3252                    */
*/*                                                                  */
*/*      12/10/83 SJB              SKIP BAD MEMBER CREATED BY        */
*/*                                MASTER FILE BEING FULL            */
*/*                                                                  */
*/****************************************************************** /*
         PRINT NOGEN
         STM   14,12,12(13)
         BALR  10,0
         USING *,10
HERE     B     BEGIN
         DC    CL8'LIBPRT'
SAVE     DS    18F
BEGIN    EQU   *
         ST    13,SAVE+4
         LA    7,SAVE
         ST    7,8(13)
         LA    13,SAVE
         EJECT
         L     1,0(1)                        ----> TO PARAMTER LIST
         CLI   2(1),C'W'                     WEEKLY RUN ?
         BNE   OPENMAST                      NO, LEAVE DAILY IN RPT HDR
         MVC   RPTPERID(6),WEEKLY            MOVE WEEKLY TO RPT PERIOD
         MVC   RPTDSNME(6),WEEKLY            MOVE WEEKLY TO RPT DSNAME
OPENMAST OPEN  (MASTER,(INPUT))
         OPEN  (REPORT,(OUTPUT))
LIBRDJF  RDJFCB MF=(E,EX1A)              GET THE TAPE DSN + VOLSER
         L     3,=A(LIBJFCB)             ---> TO JFCB
         USING JFCB,3                    IBM JFCB DSECT
         MVC   ARCHDSN(44),JFCBDSNM      MOVE DATASET NAME TO HEADING
         MVC   ARCHVOL(6),JFCBVOLS            VOLSER NAME TO HEADING
         DROP  3
         TIME  DEC                       GET CURRENT TIME/DATE
         ST    1,JDAYSVE                 STORE JULIAN DATE
         ST    0,TOD                     STORE TIME
         UNPK  TODUP(7),TOD              UNPACK TIME
         MVC   HEADHH(2),TODUP           MOVE HOURS TO HEADING
         MVC   HEADMIN(2),TODUP+2        MOVE MINUTES TO HEADING
         UNPK  JDAYUNPK(7),JDAYSVE+1     UNPACK JULIAN DATE
         MVC   YRUNPACK+3(5),JDAYUNPK    MOVE INTO CONVERT AREA
         LA    5,LIBE15                  ESTABLISH ADDRESSABLITY
         BAL   6,CNVRTDTE                CONVERT JULIAN TO GREGORIAN
         MVC   HEADYY(2),GREGDTE         MOVE DATE TO HEADING
         MVC   HEADMM(2),GREGDTE+2
         MVC   HEADDD(2),GREGDTE+4
         LA    1,SORTLST                 1 ---> SORT PARAMETER LIST
         LINK  EP=SORT                   GO SORT
         B     LIBPR002                  RETURN FROM SORT END PROGRAM
         CNOP  0,8
LIBE15   EQU   *                         SORT E15 EXIT PASS DATA TO
         USING *,15                      THE SORT
         STM   14,12,LIBE15SV            SAVE REGISTERS
         B     E15GET
LIBE15SV DC    18F'0'
LIBSVE15 DC    F'0'
         DS    0H
E15GET   EQU   *                         ESTABLISH ADDRESSABILITY
         LR    5,15
         LA    4,DIROUT                  PUT ---> TO SORT RECORD
         ST    4,LIBE15SV+12             IN REGISTER 13 PASS TO SORT
         DROP  15
         USING LIBE15,5
GET15    GET   MASTER,MASTIN             GET A RECORD
         LA    4,MASTIN
         USING MASTDS,4                  USE MASTER DSECT
         MVC   DIRJNM(8),MASJNM          MOVE MASTER FIELD TO MAKE
         CLC   GET15YY(2),MASJD          A SORT DIRECTORY RECORD
         BE    GET15CZ
         CLC   =CL9' ',MASJD       IS THIS A BAD ENTRY         12/10/83
         BE    GET15               YES, THEN SKIP              12/10/83
         TM    MASJNO,X'F0'              CHECK IF NUMERIC DATA IN FIELD
         BNO   GET15                     NO THEN GET ANOTHER
         MVC   YRUNPACK+3(5),MASJD       GET JULIAN RUN DATE
         B     GET15C
GET15CZ  MVC   DIRJD(2),MASYY
         MVC   DIRJD+2(2),MASMM
         MVC   DIRJD+4(2),MASDD
         B     GET15N
GET15C   BAL   6,CNVRTDTE                CONVERT JULIAN TO GREGORIAN
         MVC   DIRJD(6),GREGDTE          MOVE FIELDS TO MAKE RECORD
GET15N   MVC   DIRSTH(2),MASSTH          FOR DIRECTORY
         MVC   DIRSTM(2),MASSTM
         MVC   DIRPTH(2),MASPTH
         MVC   DIRPTM(2),MASPTM
         MVC   DIRMID(8),MASMID
         MVC   DIRUID(7),MASUID
         MVC   DIROUT(40),DIROUTD
         DROP  4
GET15P   LM    14,12,LIBE15SV             RESTORE REGS
         LA    15,12                      RETURN CODE 12 INSERT RECORD
         BR    14                         RETURN TO SORT
LIBPRCLS EQU   *
         LM    14,12,LIBE15SV             NO MORE RECORDS
         LA    15,8                       DON'T RETURN TO THIS EXIT
         BR    14                         RETURN TO SORT
         EJECT
CNVRTDTE PACK YRPACK,YRUNPACK          PACK IT
         L    1,YRPACK                PUT IT IN R1
*
         SR   2,2
         PACK DATEP+6(2),YRUNPACK+3(2)  CHECK IF LEAP YEAR
         CVB  3,DATEP                   CONVERT YR TO BINARY
         D    2,DIV4                    DIVIDE BY 4
         LTR  2,2                       CK FOR REMANDER
         BZ   CHANGFEB                  NO THEN LEAP
         B    FINDDAY
CHANGFEB MVI  FEB,X'1D'
*
FINDDAY  SLA   1,16                    REMOVE YEAR DDDF0000
         SRA   1,16                    MOVE BACK 0000DDDF
         XC    DUBLWD,DUBLWD            CLEAR DUBLWD
         ST    1,DUBLWD+4
         CVB   1,DUBLWD                PACKED TO BINARY
         LA    7,1                     SET MONTH CTR TO 1
         LA    8,CALENDAR
         SR    9,9
GETDYMTH IC    9,0(8)                 INSERT DAYS IN MONTH IN REG 9
         CR    1,9                    DAY OF YEAR - DAYS IN MONTH
         BNH   GOTDYMTH
         SR    1,9                    SUBTRACT MONTHS DAYS FROM R1
         LA    7,1(7)                 INCREMENT TO NEXT MONTH
         LA    8,1(8)                 INCREMENT TO NEXT MONTH
         B     GETDYMTH
GOTDYMTH BAL   11,CNVRTX
         MVC   GREGDTE+2(2),DWORD+2     MOVE MONTH
         LR    7,1
         BAL   11,CNVRTX
         MVC   GREGDTE+4(2),DWORD+2     MOVE DAY
         MVC   GREGDTE+0(2),YRUNPACK+3  MOVE YEAR
XITDTECV BR    6
*
CNVRTX   CVD   7,DWORD
         UNPK  DWORD(4),DWORD+5(3)
         OI    DWORD+3,X'F0'
         BR    11
*
GREGDTE  DC    D'0'
DATEP    DS    D
DIV4     DC    X'00000004'
YRUNPACK DC    X'F0F0F0F0F0F0F0F0'
DUBLWD   DC    D'0'
DWORD    DC    D'0'
DBLTIME  DC    D'0'
YRPACK   DC    F'0'
FULLTIME DC    F'0'
JULDATE  DC    F'0'
CALENDAR DC    X'1F'            NUMBER OF DAYS IN JAN.
FEB      DC    X'1C'            NUMBER OF DAYS IN FEB.
         DC    X'1F'            NUMBER OF DAYS IN MAR.
         DC    X'1E'            NUMBER OF DAYS IN APR.
         DC    X'1F'            NUMBER OF DAYS IN MAY
         DC    X'1E'            NUMBER OF DAYS IN JUNE
         DC    X'1F'            NUMBER OF DAYS IN JULY
         DC    X'1F'            NUMBER OF DAYS IN AUG.
         DC    X'1E'            NUMBER OF DAYS IN SEPT
         DC    X'1F'            NUMBER OF DAYS IN OCT.
         DC    X'1E'            NUMBER OF DAYS IN NOV.
         DC    X'1F'            NUMBER OF DAYS IN DEC.
*
GET15Z   DC    XL5'F0F0F0F0F0'
GET15YY  DC    CL2'YY'
         EJECT
         SPACE
MASTIN   DS    CL124
         SPACE
         DS    0F
DIROUT   DS    CL40
DIROUTD  DS    0CL40           SORT RECORD LAYOUT FOR DIRECTORY
DIRJD    DC    CL6' '
DIRJNM   DC    CL8' '
DIRMID   DC    CL8' '
DIRSTH   DC    CL2' '
         DC    CL1'.'
DIRSTM   DC    CL2' '
DIRPTH   DC    CL2' '
         DC    CL1'.'
DIRPTM   DC    CL2' '
DIRUID   DC    CL7' '
         DC    C' '
         SPACE
         DS    0F
         EJECT
         SPACE
         DSECT               MASTER ARCHIVE INDEX LAYOUT
MASTDS   DS    0CL133
         DS    CL1
MASMID   DS    0CL8
         DS    CL4
MASJNO   DS    CL4
         DS    CL16
MASJD    DS    CL5
MASSTH   DS    CL2
MASSTM   DS    CL2
MASPTH   DS    CL2
MASPTM   DS    CL2
         DS    CL10
MASMM    DS    CL2
         DS    CL1
MASDD    DS    CL2
         DS    CL1
MASYY    DS    CL2
         DS    CL50
MASJNM   DS    CL8
MASUID   DS    CL7
         DS    CL13
         EJECT
LIBPRT   CSECT                      SORT PARAMTER LIST
         DS    0F
SORTLST  DC    X'80'
         DC    AL3(SORTALST)
         DC    X'0000'
SORTALST DC    X'0020'
         DC    A(SORTCD)
         DC    A(STCDED)
         DC    A(RCDCD)
         DC    A(RDCDED)
         DC    A(LIBE15)
         DC    A(LIBE35)
         DC    X'00'
         DC    X'020000'
         DC    X'03'
         DC    AL3(MSGNAME)
SORTCD   DC    C'SORT  FIELDS=(7,8,CH,A,1,6,CH,A,23,5,CH,A)'
STCDED   DC    C' '
RCDCD    DC    C'RECORD  LENGTH=(40,40,40),TYPE=F'
RDCDED   DC    C' '
MSGNAME  DC    CL8'MESSAGES'
         DROP  5
         EJECT
         PRINT GEN
JFCB     DSECT
         IEFJFCBN LIST=YES
         PRINT NOGEN
         SPACE 3
LIBPRT   CSECT
EX1A     RDJFCB (TAPE),MF=L         LIST FOR RDJFCB MACRO
LIBJFCB  DS    176C
         EJECT
         CNOP  0,8                   E35 SORT EXIT AFTER PHASE 3
LIBE35   EQU   *
         USING *,15
         STM   14,12,LIBE35SV              SAVE REGISTERS
         B     E35STRT
LIBE35SV DC    18F'0'
         DS    0H
E35STRT  EQU   *                           ESTABLISH ADDRESSABILITY
         LR    5,15
         DROP  15
         USING LIBE35,5
         L     1,0(1)                      ----> TO PARAMETER LIST
         LTR   1,1                         IF ZERO ALL DONE
         BZ    E35END
         MVC   DETOUT(133),HEAD5           CLEAR OUTPUT AREA
         SR    4,4                         CLEAR LINE COUNT
         LH    4,LINECT                    UPDATE LINE COUNTER
         AH    4,=H'1'
         STH   4,LINECT
         USING SORTDS,1                    USE SORT RECORD FOR DSECT
         MVC   JOBNAME(8),DIRJNMD          MOVE FIELD TO OUTPUT
         MVC   JOBNUM(4),DIRMIDD+4
         MVC   DETMM(2),DIRJDD
         MVC   DETDD(2),DIRJDD+2
         MVC   DETYY(2),DIRJDD+4
         MVC   DETSTHH(2),DIRSTHD
         MVC   DETSTMM(2),DIRSTMD
         MVC   DETETHH(2),DIRPTHD
         MVC   DETETMM(2),DIRPTMD
         MVC   USERID(7),DIRUIDD
         MVC   MODULE(8),DIRMIDD
         DROP  1
         CLC   PAGEPK(2),=PL2'1'           FIRST TIME THRU ?
         BL    HEADRT                      YES PUT OUT HEADINGS
         CH    4,=H'56'                    MAX LINES ?
         BH    HEADRT                      YES PUT OUT HEADINGS
E35PUT   MVC   DETOUT(133),DETAIL          OUTPUT RECORD TO OUT AREA
         PUT   REPORT,DETOUT               WRITE USING PM
         LM    14,12,LIBE35SV              RESTORE REGS
         LA    15,4                        DELETE RECORD FROM SORT
         BR    14                          RETURN TO SORT
E35END   EQU   *                           NO MORE RECORDS
         LM    14,12,LIBE35SV              RESTORE REGS
         LA    15,8                        DON'T RETURN TO THIS EXIT
         BR    14                          GET BACK
HEADRT   EQU   *
         XC    LINECT(2),LINECT            ZERO LINE COUNTER
         AP    PAGEPK,=P'1'                UPDATE PAGE COUNTER
         UNPK  PAGECT(3),PAGEPK
         OI    PAGECT+2,X'F0'             MAKE IT NICE
         PUT   REPORT,HEAD1
         PUT   REPORT,HEAD2
         PUT   REPORT,HEAD3
         PUT   REPORT,HEAD4
         PUT   REPORT,HEAD5
         B     E35PUT
         EJECT
PAGEPK   DC    PL2'0'
LINECT   DC    PL2'0'
WEEKLY   DC    CL6'WEEKLY'
DETOUT   DS    CL133
         SPACE
HEAD1    DS    0CL133
         DC    C'1'
         DC    CL5'DATE='
HEADMM   DS    CL2
         DC    CL1'/'
HEADDD   DS    CL2
         DC    CL1'/'
HEADYY   DS    CL2
         DC    CL2' '
         DC    CL5'TIME='
HEADHH   DS    CL2
         DC    CL1':'
HEADMIN  DS    CL2
         DC    CL8' '
RPTPERID DC    CL7' DAILY '
         DC    CL39'LIBSPOOL ARCHIVE TAPE MASTER FILE INDEX'
         DC    CL33' '
         DC    CL5'PAGE'
PAGECT   DS    CL3
         DC    CL12' '
         SPACE
HEAD2    DS    0CL133
         DC    C' '
         DC    CL13' '
RPTDSNME DC    CL7' DAILY '
         DC    CL20'ARCHIVE TAPE DSNAME='
ARCHDSN  DS    CL44
         DC    CL2' '
         DC    CL7'VOLSER='
ARCHVOL  DS    CL6
         DC    CL33' '
         SPACE
HEAD3    DS    0CL133
         DC    C'0'
         DC    CL10' '
         DC    CL13'JOB NAME'
         DC    CL15'JOB NUMBER'
         DC    CL13'RUN DATE'
         DC    CL15'START TIME'
         DC    CL13'END TIME'
         DC    CL13'USER ID'
         DC    CL32'LIBRARIAN'
         SPACE
HEAD4    DS    0CL133
         DC    C' '
         DC    CL91' '
         DC    CL41'MODULE NAME'
         SPACE
HEAD5    DC    CL133' '
DETAIL   DS    0CL133
         DC    C' '
         DC    CL10' '
JOBNAME  DS    CL8
         DC    CL8' '
JOBNUM   DS    CL4
         DC    CL8' '
DETMM    DS    CL2
         DC    CL1'/'
DETDD    DS    CL2
         DC    CL1'/'
DETYY    DS    CL2
         DC    CL7' '
DETSTHH  DS    CL2
         DC    CL1':'
DETSTMM  DS    CL2
         DC    CL9' '
DETETHH  DS    CL2
         DC    CL1':'
DETETMM  DS    CL2
         DC    CL7' '
USERID   DS    CL7
         DC    CL6' '
MODULE   DS    CL8
         DC    CL33' '
         DSECT
SORTDS   DS    0CL40
DIRJDD   DC    CL6' '
DIRJNMD  DC    CL8' '
DIRMIDD  DC    CL8' '
DIRSTHD  DC    CL2' '
         DC    CL1'.'
DIRSTMD  DC    CL2' '
DIRPTHD  DC    CL2' '
         DC    CL1'.'
DIRPTMD  DC    CL2' '
DIRUIDD  DC    CL7' '
         DC    C' '
         SPACE
LIBPRT   CSECT
         DROP  5
         EJECT
         CNOP  0,8
LIBPR002 EQU   *
LIBPREND EQU   *
         CLOSE (MASTER)                   CLOSE MASTER INPUT
         CLOSE (REPORT)                   CLOSE SYSOUT REPORT
         L     13,SAVE+4
         LM    14,12,12(13)
         LA    15,0
         BR    14
         SPACE 3
         DS    F
TAPEEXIT DC    X'11'                 TAPE JFCB ABEND EXIT
         DC    AL3(TAPEAB)           ---> ABEND EXIT
         DC    X'87'                 LAST IN LIST
         DC    AL3(LIBJFCB)          ---> JFCB OUTPUT
TAPEAB   EQU   *
         USING *,15                  ADDRESSABILITY
         MVI   3(1),X'04'            IGNORE ABEND
         BR    14                    GET BACK
         DROP  15
         SPACE
JDAYSVE  DC    F'0'
JDAYUNPK DS    XL10
TOD      DS    F
TODUP    DC    XL8'00'
         SPACE 3
MASTER   DCB   MACRF=(GM),DSORG=PS,EODAD=LIBPRCLS,DDNAME=MASTDD
TAPE     DCB   MACRF=(R,W),DSORG=PS,DDNAME=TAPEDD,EXLST=TAPEEXIT
REPORT   DCB   MACRF=(PM),DSORG=PS,DDNAME=REPORTDD
         SPACE
         LTORG
         END
