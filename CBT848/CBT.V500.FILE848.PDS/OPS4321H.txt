LIBUP    CSECT
*/****************************************************************** /*
*/*                                                                  */
*/*       WRITTEN BY :             DONALD HARRIS                     */
*/*                                OCCIDENTAL CHEMICAL CORPORATION   */
*/*                                360 RAINBOW BLVD. SOUTH           */
*/*                                NIAGARA FALLS , NEW YORK 14303    */
*/*                                (716) 286-3252                    */
*/*                                                                  */
*/*      12/10/83 SJB              SKIP BAD MEMBER CREATED BY        */
*/*                                MASTER FILE BEING FULL            */
*/*                                                                  */
*/****************************************************************** /*
         PRINT NOGEN
         STM   14,12,12(13)
         BALR  10,0
         USING *,10
HERE     B     BEGIN
         DC    CL8'LIBUP'
SAVE     DS    18F
BEGIN    EQU   *
         ST    13,SAVE+4
         LA    7,SAVE
         ST    7,8(13)
         LA    13,SAVE
         EJECT
         L     1,0(1)                        ----> TO PARAMTER LIST
*
         LH    3,0(1)                      GET LENGTH OF PARM INPUT
         CH    3,=H'1'                     IS LENGTH = 1
         BE    CKRUNTYP                    CHECK RUN TYPE
         CLI   2(1),C'W'                     WEEKLY RUN ?
         BNE   CKRUNTYP                    CHECK RUN TYPE
         CH    3,=H'7'                     IS LENGTH = 7
         BNE   CKRUNTYP                    CHK IF 6 CHAR. DATE PARM.
         MVC   STOWNEWN+2(6),3(1)          SAVE WEEKLY MEMBER NAME
*
CKRUNTYP CLI   2(1),C'W'                     WEEKLY RUN ?
         BNE   LIBRDJF                       NO
         WTO   'WEEKLY RUN EXECUTING'        DOC TO OPERATOR
         OI    WEEKSW,X'01'                  WEEKLY RUN INDICATOR
         OPEN  (DIRECTA,(UPDAT))             OPEN DIRECTORY TO
         STOW  DIRECTA,STOWLST3,D            DELETE #DAILY MEMBER
         CLOSE (DIRECTA)                  CLOSE DIRECTORY
LIBRDJF  RDJFCB MF=(E,EX1A)               READ TAPE JFCB
         L     4,=A(MASTER)               OPEN MASTER INPUT
         OPEN  ((4),(INPUT))
         L     3,=A(LIBJFCB)
         USING JFCB,3
         MVC   DIRDSN(40),JFCBDSNM        MOVE DSN TO HEADER AREA
         MVC   DIROUT(40),LABEL1          INIT FIRST RECORD TO ZEROES
         DROP  3
         LA    1,SORTLST                  ---> TO SORT PARAMETER LIST
         LINK  EP=SORT                    GO SORT
         B     LIBUP002                   GOTO END OF PROGRAM
         EJECT
         CNOP  0,8
         PUSH  USING                                                SJB
LIBE15   EQU   *                          E15 SORT EXIT
         USING *,15                       PASSES RECORD FROM MASTER
         STM   14,12,LIBE15SV             TO SORT
         B     E15GET                     SAVE REGS
LIBE15SV DC    18F'0'
LIBSVE15 DC    F'0'
         DS    0H
E15GET   EQU   *
         LR    5,15                       ESTABLISH ADDRESSABILITY
         LA    4,DIROUT                   POINTER TO RECORD IN REG 13
         ST    4,LIBE15SV+12
         DROP  15
         USING LIBE15,5
         TM    LABELSW,X'01'              CHECK FOR FIRST TIME THRU
         BO    GET15                      NO GET RECORD PASS TO SORT
         OI    LABELSW,X'01'              YES PASS ZERO RECORD TO SORT
         B     GET15P
GET15    GET   MASTER,MASTIN              GET A RECORD
         LA    4,MASTIN                   SET UP AND MOVE DATA
         USING MASTDS,4
         MVC   DIRJNM(8),MASJNM    SAVE JOBNAME
         CLC   GET15YY(2),MASJD    IS JULIAN YEAR = 'YY'
         BE    GET15CZ             YES
         CLC   MASJD,=CL9' '       IS THIS A BAD RECORD        12/10/83
         BE    GET15               YES, THEN SKIP              12/10/83
         TM    MASJNO,X'F0'        IS JOB NUMBER NUMERIC
         BNO   GET15               NOPE, THEN SKIP
         MVC   YRUNPACK+3(5),MASJD     GET JULIAN RUN DATE
         B     GET15C
GET15CZ  MVC   DIRJD(2),MASYY
         MVC   DIRJD+2(2),MASMM
         MVC   DIRJD+4(2),MASDD
         B     GET15N
GET15C   BAL   6,CNVRTDTE                  CONVERT JULIAN TO GREGORIAN
         MVC   DIRJD(6),GREGDTE            MOVE REST OF FIELDS
GET15N   MVC   DIRSTH(2),MASSTH
         MVC   DIRSTM(2),MASSTM
         MVC   DIRPTH(2),MASPTH
         MVC   DIRPTM(2),MASPTM
         MVC   DIRMID(8),MASMID
         MVC   DIRUID(7),MASUID
         MVC   DIROUT(40),DIROUTD
         DROP  4
         CLC   GETDTH(6),DIRJD              CHECK FOR HIGHEST DATE
         BH    GET15CKL                     NO CHECK FOR LOWEST
         MVC   GETDTH(6),DIRJD              YES SAVE IT FOR STOW USER
GET15CKL CLC   GETDTL(6),DIRJD               DATA
         BL    GET15P                        LOWEST ? NO DON'T SAVE
         MVC   GETDTL(6),DIRJD              YES SAVE IT
GET15P   LM    14,12,LIBE15SV               RESTORE REGS
         LA    15,12                        INSERT RECORD RC
         BR    14                           GET BACK
LIBUPCLS EQU   *
         LM    14,12,LIBE15SV         RESTORE REGS
         LA    15,8                   DON'T EVER RETURN TO E15 RC
         BR    14
         DROP  5
         POP   USING                                                SJB
         EJECT
LIBUP    CSECT
CNVRTDTE PACK YRPACK,YRUNPACK          PACK IT
         L    1,YRPACK                PUT IT IN R1
*
         SR   2,2
         PACK DATEP+6(2),YRUNPACK+3(2)  CHECK IF LEAP YEAR
         CVB  3,DATEP                   CONVERT YR TO BINARY
         D    2,DIV4                    DIVIDE BY 4
         LTR  2,2                       CK FOR REMANDER
         BZ   CHANGFEB                  NO THEN LEAP
         B    FINDDAY
CHANGFEB MVI  FEB,X'1D'
*
FINDDAY  SLA   1,16                    REMOVE YEAR DDDF0000
         SRA   1,16                    MOVE BACK 0000DDDF
         XC    DUBLWD,DUBLWD            CLEAR DUBLWD
         ST    1,DUBLWD+4
         CVB   1,DUBLWD                PACKED TO BINARY
         LA    7,1                     SET MONTH CTR TO 1
         LA    8,CALENDAR
         SR    9,9
GETDYMTH IC    9,0(8)                 INSERT DAYS IN MONTH IN REG 9
         CR    1,9                    DAY OF YEAR - DAYS IN MONTH
         BNH   GOTDYMTH
         SR    1,9                    SUBTRACT MONTHS DAYS FROM R1
         LA    7,1(7)                 INCREMENT TO NEXT MONTH
         LA    8,1(8)                 INCREMENT TO NEXT MONTH
         B     GETDYMTH
GOTDYMTH BAL   11,CNVRTX
         MVC   GREGDTE+2(2),DWORD+2     MOVE MONTH
         LR    7,1
         BAL   11,CNVRTX
         MVC   GREGDTE+4(2),DWORD+2     MOVE DAY
         MVC   GREGDTE+0(2),YRUNPACK+3  MOVE YEAR
XITDTECV BR    6
*
CNVRTX   CVD   7,DWORD
         UNPK  DWORD(4),DWORD+5(3)
         OI    DWORD+3,X'F0'
         BR    11
*
GREGDTE  DC    D'0'
DATEP    DS    D
DIV4     DC    X'00000004'
YRUNPACK DC    X'F0F0F0F0F0F0F0F0'
DUBLWD   DC    D'0'
DWORD    DC    D'0'
DBLTIME  DC    D'0'
YRPACK   DC    F'0'
FULLTIME DC    F'0'
JULDATE  DC    F'0'
CALENDAR DC    X'1F'            NUMBER OF DAYS IN JAN.
FEB      DC    X'1C'            NUMBER OF DAYS IN FEB.
         DC    X'1F'            NUMBER OF DAYS IN MAR.
         DC    X'1E'            NUMBER OF DAYS IN APR.
         DC    X'1F'            NUMBER OF DAYS IN MAY
         DC    X'1E'            NUMBER OF DAYS IN JUNE
         DC    X'1F'            NUMBER OF DAYS IN JULY
         DC    X'1F'            NUMBER OF DAYS IN AUG.
         DC    X'1E'            NUMBER OF DAYS IN SEPT
         DC    X'1F'            NUMBER OF DAYS IN OCT.
         DC    X'1E'            NUMBER OF DAYS IN NOV.
         DC    X'1F'            NUMBER OF DAYS IN DEC.
*
GET15Z   DC    XL5'F0F0F0F0F0'
GET15YY  DC    CL2'YY'
GETDTH   DC    CL6'000000'
GETDTL   DC    CL6'999999'
         EJECT
         SPACE
LABELSW  DC    X'00'
LABEL1   DC    XL40'00'
LIBREC   DC    F'0'
         SPACE
MASTIN   DS    CL133
         SPACE
         DS    0F
DIROUT   DS    CL40
DIROUTD  DS    0CL40
DIRJD    DC    CL6' '              JULIAN DATE
DIRJNM   DC    CL8' '              JOBNAME
DIRMID   DC    CL8' '              MEMBER NAME
DIRSTH   DC    CL2' '              START HOUR
         DC    CL1'.'
DIRSTM   DC    CL2' '              START MINUTE
DIRPTH   DC    CL2' '              STOP HOUR
         DC    CL1'.'
DIRPTM   DC    CL2' '              STOP MINUTE
DIRUID   DC    CL7' '              USERID
         DC    C' '
         SPACE
         EJECT
LIBUP    CSECT
         DS    0F
SORTLST  DC    X'80'
         DC    AL3(SORTALST)
         DC    X'0000'
SORTALST DC    X'0020'
         DC    A(SORTCD)
         DC    A(STCDED)
         DC    A(RCDCD)
         DC    A(RDCDED)
         DC    A(LIBE15)
         DC    A(0)
         DC    X'00'
         DC    X'020000'
         DC    X'03'
         DC    AL3(MSGNAME)
SORTCD   DC    C'SORT  FIELDS=(1,6,CH,A,7,8,CH,A,18,4,CH,A)'
STCDED   DC    C' '
RCDCD    DC    C'RECORD  LENGTH=(40,40,40),TYPE=F'
RDCDED   DC    C' '
MSGNAME  DC    CL8'MESSAGES'
         SPACE 3
EX1A     RDJFCB (TAPE),MF=L
LIBJFCB  DS    176C
         EJECT
         CNOP  0,8
LIBUP002 EQU   *
         OPEN  (DIRECT,(UPDAT))        OPEN DIRECT FOR HEADING UPDATE
         LA    2,DECB                  READ FIRST RECORD
         READ  (2),SF,MF=E
         CHECK (2)                     WAIT
         MVC   DIRRDIN(40),DIRLABEL    REPLACE ZEROES WITH HEADING
         WRITE (2),SF,MF=E
         CHECK (2)                     WAIT
         CLOSE (DIRECT)                CLOSE
         CLOSE (MASTER)
         MVC   STOWFD(6),GETDTL        MOVE LOW DATE TO USER DATA STOW
         MVC   STOWLD(6),GETDTH        MOVE HIGH DATE """"""""""""""""
         OPEN  (DIRECTA,(UPDAT))
         STOW  DIRECTA,STOWLST1,R       REPLACE USER DATA
         TM    WEEKSW,X'01'            WEEKLY RUN ????
         BNO   LIBUPEND                NO END MEMBER IS #DAILY
         CLC   STOWNEWN+2(6),=C'      ' MEMBER DATE IN THRU PARM ?
         BNE   STOWIT                   STOW WITH PARM DATE
         TIME  DEC                     USE CURRENT GREG. DATE FOR NAME
         ST    1,JDAYSVE               NAME TO INCLUDE DATE
         UNPK  JDAYUNPK(7),JDAYSVE+1    GET THE DATE
         MVC   YRUNPACK+3(5),JDAYUNPK   SET FRO CONVERT TO GREG
         LA    5,LIBE15                 ESTABLISH ADDRESSABILITY
         BAL   6,CNVRTDTE               CONVERT JULIAN TO GREORIAN
         MVC   STOWNEWN+2(6),GREGDTE    MOVE GREG DATE TO STOW LIST
STOWIT   STOW  DIRECTA,STOWLST2,C       STOW IT
LIBUPEND EQU   *
         CLOSE (DIRECTA)                CLOSE DIRECTORY
         L     13,SAVE+4                RESTORE REGS
         LM    14,12,12(13)
         LA    15,0                     RC = 0
         BR    14                       BYE
         SPACE 3
         DS    F
MASTEXIT DC    X'11'                     DCB ABEND
         DC    AL3(MASTABEN)             ABEND EXIT ADDRESS
         DC    X'87'                     LAST IN LIST
         DC    AL3(LIBJFCB)              ---> JFCB OUTPUT AREA
MASTABEN EQU   *
         USING *,15                      ADDRESSABILITY
         MVI   3(1),X'04'                IGNORE THE ABEND
         BR    14                        GET BACK
         DROP  15
         SPACE
         READ  DECB,SF,DIRECT,DIRRDIN,MF=L
         SPACE 3
WEEKSW   DC    X'00'
JDAYSVE  DC    F'0'
JDAYUNPK DS    XL10
         DS    0F
DIRLABEL DS    0CL40
DIRDSN   DC    CL40' '
         SPACE 3
         DS    0F
STOWLST1 DS    0CL24
STOWNAME DC    CL8'#DAILY  '
STOWTTR  DC    XL3'00'
         DC    XL1'06'
STOWFD   DC    CL6'YYMMDD'
STOWLD   DC    CL6'YYMMDD'
         SPACE
         DS    0F
STOWLST2 DS    0CL16
STOWNM   DC    CL8'#DAILY  '
STOWNEWN DC    CL8'$W      '
         DS    0F
STOWLST3 DS    0CL8
STOWNMD  DC    CL8'#DAILY  '
MASTER   DCB   MACRF=(GM),DSORG=PS,EODAD=LIBUPCLS,DDNAME=MASTDD
DIRECT   DCB   MACRF=(R,W),DSORG=PS,DDNAME=DIRDD,RECFM=FB
         SPACE
DIRECTA  DCB   MACRF=(R,W),DSORG=PO,DDNAME=DIRDDA
         SPACE
TAPE     DCB   MACRF=(R,W),DSORG=PS,DDNAME=TAPEDD,EXLST=MASTEXIT
         SPACE
         LTORG
DIRRDIN  DS    CL6200
         EJECT
         SPACE
         DS    0F
         DSECT
MASTDS   DS    0CL133
         DS    CL1
MASMID   DS    0CL8                MEMBER ID
         DS    CL4                 'A' || JULIAN DAY
MASJNO   DS    CL4                 JOB NUMBER
         DS    CL16
MASJD    DS    CL5                 JULIAN DATE
MASSTH   DS    CL2                 START HOUR
MASSTM   DS    CL2                 START MINUTE
MASPTH   DS    CL2                 STOP HOUR
MASPTM   DS    CL2                 STOP MINUTE
         DS    CL10
MASMM    DS    CL2                 ADDED MONTH
         DS    CL1
MASDD    DS    CL2                 ADDED DAY
         DS    CL1
MASYY    DS    CL2                 ADDED YEAR
         DS    CL50
MASJNM   DS    CL8                 JOBNAME
MASUID   DS    CL7                 USERID
         DS    CL13
JFCB     DSECT
         PRINT GEN
         IEFJFCBN LIST=YES
         PRINT NOGEN
         END
