         TITLE 'LIBSPOOL: OPS4319G - ARCHIVE DIRECTORY FILE INQUIRY'
*/****************************************************************** /*
*/*                                                                  */
*/*       WRITTEN BY :             ROBERT P DZIALAK                  */
*/*                                OCCIDENTAL CHEMICAL CORPORATION   */
*/*                                360 RAINBOW BLVD. SOUTH           */
*/*                                NIAGARA FALLS , NEW YORK 14303    */
*/*                                (716) 286-3251                    */
*/*                                                                  */
*/****************************************************************** /*
OPS4319G CSECT
         PRINT NOGEN
*
STARTDMP EQU   *
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING OPS4319G,R12
         B     NAMEED
         DC    CL8'OPS4319G'
NAMEED   EQU   *
         ST    R13,SAVEAREA+4
         LR    R2,R13
         LA    R13,SAVEAREA
         ST    R13,8(R2)
*
*   THIS PROGRAM IS PASSED A PARAMETER LIST AT ENTRY REG1 POINTS TO
*   AN ADDRESS THAT POINTS TO THE FOLLOWING
*
*        1) A HALF WORD THAT HAS THE LENGTH OF THE PARAMETER LIST.
*           MAXIMUM LENGTH IS 100 BYTES.
*        2) PARAMETER VALUE (A-E) - 1 BYTE
*        3) USER-ID - 7 BYTES (ASSUMED TO ALWAYS BE 7 BYTES).
*        4) START SCAN DATE YYMMDD - 6 BYTES
*        5) END SCAN DATE YYMMDD - 6 BYTES
*        6) THE NAME BASED ON THE PARMETER VALUE PASSED (0-8 BYTES).
*
         L     R2,0(R1)                R2 = ADDR. OF PASSED PARMS
         MVC   PARM(20),2(R2)      LOAD PARM,TSOUID,START/END SCAN DATE
         CLI   PARM,C'E'               ARE ALL MODULES TO BE DISPLAYED?
         BE    OPENPRT                 NO NAME ON PARM = E
         SR    R3,R3                   CLEAR R3
         IC    R3,1(R2)                PUT PARM LENGTH IN R3
         SH    R3,=H'21'               SUBTRACT 1 FOR PARM
*                                      SUBTRACT 7 FOR USER ID
*                                      SUBTRACT 6 FOR START SCAN DATE
*                                      SUBTRACT 6 FOR END SCAN DATE
*                                      SUBTRACT 1 FOR MVC AND CLC
         CH    R3,=H'7'                COMPARE R3 TO 7
         BNH   SETMVELN                OK AS IS TO SET MOVE LENGTH
         LH    R3,=H'7'                USE 7 IF IT IS TOO BIG
SETMVELN STC   R3,MVENAME+1            SET UP MVC FOR NAME PARM
MVENAME  MVC   NAME(8),22(R2)          MOVE NAME FIELD OF PROPER LENGTH
*ABENDIT  DC    D'0'
OPENPRT  OPEN  (TSODCB,(OUTPUT))       OPEN OUTPUT FILE TO TERMINAL
         OPEN  (PDSDRECT,(INPUT))  OPEN DIRECTORY OF ARCHIVE DIR. DSN
         OPEN  (ARCHDCB,(INPUT))       OPEN ARCHIVE DIR. DSN
*        OPEN  (SNAPDCB,(OUTPUT))
         BAL   R4,CHKPARMS            SET UP COMPARES AND R11 BR ADDDR.
         USING MBRENTRY,R3
RDIRCTRY GET   PDSDRECT,DIRECTRY
         LA    R3,DIRECTRY
         LH    R10,DIRECTRY           NUMBER OF BYTES USED IN DIRECTORY
         AR    R10,R3                 ADDRESS OF END OF DIRECTORY = R10
         LA    R3,2(R3)               START ADDRESS OF MEMBER DATA
*        SNAP  DCB=SNAPDCB,ID=3,STORAGE=(STARTDMP,ENDBUF),PDATA=(REGS)
CHKMBRNM CLC   MBRNAME(2),=X'FFFF'     TEST IF END OF DIRECTORY
         BE    ENDOFDTA                ENTIRE DIRECTORY HAS BEEN SCANED
         CLC   MBRNAME(2),=X'0000'     TEST IF END OF THIS DIRECTRY BLK
         BE    RDIRCTRY                GO READ NEXT DIRECTORY BLOCK
         CLC   MBRENDTE(6),SCANBGN     LAST RECORD DATE < SCAN BEGIN
         BL    INCRMEMB                YES, GET NEXT MEMBER
         CLC   MBRBGNDT(6),SCANEND     FIRST MEMBER DATE > SCAN END
         BH    ENDOFDTA                YES, ALL REMAINING MEMBERS HAVE
         B     FINDMBR                 MEMBER WITHIN SCAN DATE INTERVAL
INCRMEMB LA    R3,24(R3)               ADDRESS OF NEXT MEMBER NAME
         CR    R3,R10                  END OF DIRECTORY DATA
         BL    CHKMBRNM                NO, CHECK THIS MEMBER
         B     RDIRCTRY                GO READ NEXT DIRECTORY BLOCK
FINDMBR  MVC   MBRTTRK(3),8(R3)        SET UP RELATIVE ADDRESS
         LA    R2,MBRTTRK
         FIND  ARCHDCB,(2),C           POSITION TO MEMBER FOR READ
         MVI   PRINTIT+1,X'00'         PRINT TAPEINFO AND HEADER
         MVI   SAVEDSN+1,X'00'         GET DSNAME FOR THIS MEMBER
READMBR  READ  DECB1,SF,ARCHDCB,BUFAREA,'S'
         CHECK DECB1
         LA    R8,BUFAREA                 ADDRESS OF BUFFER
         USING ARCHVREC,R8
         LA    R6,ARCHDCB                 ADDRESS OF ARCHIVE DSN DCB
         USING IHADCB,R6
         LH    R9,DCBBLKSI               BLKSIZE AT TIME OF READ
         L     R1,DECB1+16               IOB ADDR (BLK RESIDUAL AT +14)
         SH    R9,14(R1)                 LENGTH OF BLOCK READ
         SH    R9,DCBLRECL               BACK OFF TO LAST RECORD
         AR    R9,R8                   ADDRESS OF LAST RECORD IN BLK-R9
SAVEDSN  NOP   BEGINBLK
         MVC   ARCHDSN(40),ARCHVREC     SAVE ARCHIVE TAPE DSN
         MVI   SAVEDSN+1,X'F0'          SKIP FOR REST OF THIS MEMBER
*
*        SNAP  DCB=SNAPDCB,ID=2,STORAGE=(STARTDMP,ENDBUF),PDATA=(REGS)
*
GETNXTRC AH    R8,DCBLRECL              INCREMENT TO NEXT ARCHIVE REC
BEGINBLK CR    R8,R9                    COMPARE TO ADDR. OF LAST REC.
         BH    READMBR                  GO LOOK AT NEXT BLOCK
         CLC   ARCRUNYY(6),SCANBGN      COMPARE RUN DATE TO SCAN BEGIN
         BL    GETNXTRC                 DATE LOW, BUMP TO NEXT RECORD
         CLC   ARCRUNYY(6),SCANEND      COMPARE RUN DATE TO SCAN END
         BH    ENDOFDTA                 EXHAUSTED ALL RECORDS WITHIN
*
         BR    R11                      BRANCH TO SELECTION DESIRED
*
PARMA    EQU   *               ********* JOB NAME SELECTION ***********
CHKNAME  CLC   ARCJNAME(8),NAME          IS IT THE NAME WANTED
*        SNAP  DCB=SNAPDCB,ID=4,STORAGE=(STARTDMP,ENDBUF),PDATA=(REGS)
         BNE   GETNXTRC                  NO,GET NEXT RECORD
         BAL   R5,PRINTIT                YES, SEND DATA TO THE TERMINAL
         B     GETNXTRC                  NO,GET NEXT RECORD
*
PARMB    EQU   *               ********* USER ID SELECTION ************
         CLC   ARCUID(7),NAME
         BNE   GETNXTRC                  NO,GET NEXT RECORD
         BAL   R5,PRINTIT                YES, SEND DATA TO THE TERMINAL
         B     GETNXTRC                  NO,GET NEXT RECORD
*
PARMC    EQU   *                 ******* JOB NUMBER SELECTION *********
         CLC   ARCJNUMB(4),NAME
         BNE   GETNXTRC                  NO,GET NEXT RECORD
         BAL   R5,PRINTIT                YES, SEND DATA TO THE TERMINAL
         B     GETNXTRC                  NO,GET NEXT RECORD
*
PARMD    EQU   *                 ******* MODULE NAME SELECTION ********
         CLC   ARCMODNM(8),NAME
         BNE   GETNXTRC                  NO,GET NEXT RECORD
         BAL   R5,PRINTIT                YES, SEND DATA TO THE TERMINAL
         B     GETNXTRC                  NO,GET NEXT RECORD
*
PARME    EQU   *                  ****** ALL MODULE SELECTION *********
         BAL   R5,PRINTIT
         B     GETNXTRC                  NO,GET NEXT RECORD
*
ENDOFDTA XC    ENDLINE,ENDLINE           CLEAR LINE
         PUT   TSODCB,ENDLINE            WRITE BLANK LINE
         MVC   ENDLINE(11),ENDATAMS      INDICATE END OF DATA
         PUT   TSODCB,ENDLINE            WRITE TO TERMINAL
         L     R7,HITCOUNT               LOAD COUNT OF HITS
         LTR   R7,R7                     CHECK IF ZERO
         BNZ   CLOSEUP                   EXIT IF NAMES HAVE BEEN FOUND
         XC    ENDLINE,ENDLINE           CLEAR LINE
         PUT   TSODCB,ENDLINE            WRITE BLANK LINE
         MVC   ENDLINE(16),NOMATCH       INDICATE NO MATCHES
         PUT   TSODCB,ENDLINE            WRITE TO TERMINAL
         MVI   RETURNCD,X'04'            INDICATE NO HITS RETURN CODE
*        SNAP  DCB=SNAPDCB,ID=5,STORAGE=(STARTDMP,ENDBUF),PDATA=(REGS)
*
CLOSEUP  EQU   *
         CLOSE ARCHDCB
         CLOSE PDSDRECT
*        CLOSE SNAPDCB
         CLOSE TSODCB
         SR    R15,R15
         IC    R15,RETURNCD
         L     R13,SAVEAREA+4
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14
*
PRINTIT  NOP   SKIPHDR
         MVI   PRINTIT+1,X'F0'
         PUT   TSODCB,SPACES
         PUT   TSODCB,ARCHDSNM
         PUT   TSODCB,SPACES
         PUT   TSODCB,HDRLINE1
         PUT   TSODCB,HDRLINE2
SKIPHDR  MVC   PRNTLINE+00(8),ARCJNAME       PREPARE PRINT LINE TO BE
         MVC   PRNTLINE+14(4),ARCJNUMB       SENT TO THE TERMINAL
         MVC   PRNTLINE+24(7),ARCUID
         MVC   PRNTLINE+35(8),ARCMODNM
         MVC   PRNTLINE+48(2),ARCRUNMM
         MVI   PRNTLINE+50,C'/'
         MVC   PRNTLINE+51(2),ARCRUNDD
         MVI   PRNTLINE+53,C'/'
         MVC   PRNTLINE+54(2),ARCRUNYY
         CLC   ARCRUNST(5),=C'HH.MM'
         BNE   SHOWTIME
         MVC   PRNTLINE+58(21),NOTSCHED
         B     PUT2TERM
SHOWTIME MVC   PRNTLINE+58(21),SPACES
         MVC   PRNTLINE+60(5),ARCRUNST
         MVC   PRNTLINE+72(5),ARCRUNEN
PUT2TERM PUT   TSODCB,PRNTLINE
         L     R7,HITCOUNT               LOAD COUNT OF HIT SO FAR
         LA    R7,1(R7)                  INCREMENT BY 1
         ST    R7,HITCOUNT               SAVE IT
         BR    R5
*
CHKPARMS CLI   PARM,C'A'
         BNE   CHK4PRMB
         LA    R11,PARMA                LOAD ADDRESS FOR BRANCH
         MVC   CHKNAME+1(1),MVENAME+1    SET UP COMPARE LENGTH
         BR    R4
CHK4PRMB CLI   PARM,C'B'
         BNE   CHK4PRMC
         LA    R11,PARMB                LOAD ADDRESS FOR BRANCH
         BR    R4
CHK4PRMC CLI   PARM,C'C'
         BNE   CHK4PRMD
         LA    R11,PARMC                LOAD ADDRESS FOR BRANCH
         BR    R4
CHK4PRMD CLI   PARM,C'D'
         BNE   CHK4PRME
         LA    R11,PARMD                LOAD ADDRESS FOR BRANCH
         BR    R4
CHK4PRME CLI   PARM,C'E'
         BNE   BADPARM
         LA    R11,PARME                LOAD ADDRESS FOR BRANCH
         BR    R4
*
BADPARM  XC    ENDLINE,ENDLINE
         PUT   TSODCB,ENDLINE          WRITE BLANK LINE
         MVC   ENDLINE(11),BADPRMES
         MVC   ENDLINE+12(1),PARM
         PUT   TSODCB,ENDLINE          WRITE TO TERMINAL BAD PARM
         B     CLOSEUP
*
BADTTR   XC    ENDLINE,ENDLINE
         PUT   TSODCB,ENDLINE          WRITE BLANK LINE
         MVC   ENDLINE(11),BADTTRMS
         PUT   TSODCB,ENDLINE          WRITE TO TERMINAL BAD PARM
*        SNAP  DCB=SNAPDCB,ID=1,STORAGE=(STARTDMP,ENDBUF),PDATA=(REGS)
         B     CLOSEUP
*
TSODCB  DCB   DSORG=PS,MACRF=(PM),DDNAME=SCREEN,RECFM=FB,              X
               BLKSIZE=1580,LRECL=79
SNAPDCB  DCB   DSORG=PS,MACRF=(W),DDNAME=SNAPDUMP,RECFM=VBA,           X
               BLKSIZE=1632,LRECL=125
PDSDRECT DCB   DSORG=PS,MACRF=(GM),DDNAME=DIRECTRY,RECFM=U,BLKSIZE=256,X
               EODAD=CLOSEUP
ARCHDCB  DCB   DSORG=PO,MACRF=(R),DDNAME=DIRECTRY,                     X
               EODAD=INCRMEMB,SYNAD=BADTTR
*
MBRTTRK  DC    F'0'
HITCOUNT DC    F'0'
SAVEAREA DC    18F'0'
PARMEQU  DC    CL5'PARM='
PARM     DC    C'0'
TSOUID   DC    CL7' '
SCANBGN  DC    CL6' '
SCANEND  DC    CL6' '
NAME     DC    CL8' '
RETURNCD DC    X'00'
SPACES   DC    CL79' '
TAPEINFO DC    CL79' '
ENDLINE  DC    CL79' '
PRNTLINE DC    CL79' '
HDRLINE1 DC    CL79' '
         ORG   HDRLINE1
         DC    CL10'JOB NAME  '
         DC    CL10' JOB NUMBE'
         DC    CL10'R   USER I'
         DC    CL10'D    LIBRA'
         DC    CL10'RIAN    RU'
         DC    CL10'N DATE  ST'
         DC    CL10'ART TIME  '
         DC    CL09' END TIME'
HDRLINE2 DC    CL79' '
         ORG   HDRLINE2
         DC    CL10'________  '
         DC    CL10'    ____  '
         DC    CL10'    ______'
         DC    CL10'_   MODULE'
         DC    CL10' NAME  (MM'
         DC    CL10'/DD/YY)  ('
         DC    CL10'HH.MM)    '
         DC    CL09' (HH.MM) '
ARCHDSNM DC    CL30'        ARCHIVE TAPE DSNAME = '
ARCHDSN  DC    CL40' '
         DC    CL9' '
ENDATAMS DC    CL11'END OF DATA'
BADPRMES DC    CL11'BAD PARM = '
BADTTRMS DC    CL11'BAD TTR    '
NOMATCH  DC    CL16'NO MATCHES FOUND'
NOTSCHED DC    CL21'  JOB NOT SCHEDULED  '
         LTORG
*
DIRALIGN DS    0F
DIRECTRY DC    CL256' '
*
*
ENDBUF   EQU   BUFAREA+4000
BUFALIGN DS    0F
BUFAREA  DS    6200C
*
MBRENTRY DSECT
MBRNAME  DS    CL8
MBRTTR   DS    CL3
MBRCOUNT DS    CL1
MBRBGNDT DS    CL6
MBRENDTE DS    CL6
*
ARCHVREC DSECT
ARCRUNYY DS    CL2
ARCRUNMM DS    CL2
ARCRUNDD DS    CL2
ARCJNAME DS    CL8
ARCMODNM DS    CL8
ARCRUNST DS    CL5
ARCRUNEN DS    CL5
ARCUID   DS    CL7
         DS    CL1
ARCJNUMB EQU   ARCMODNM+4
*
         DCBD DSORG=PO
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         END
