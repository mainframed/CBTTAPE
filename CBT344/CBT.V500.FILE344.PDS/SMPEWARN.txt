         /*Copyright 28 Oct 1999 by JBG REXX SMPEWarn                */
         /*----------------------------------------------------------*/
         /* Syntax: SMPEWarn                                         */
         /*----------------------------------------------------------*/
         /* The author can be reached at joerg.berning(at)gmx.net    */
         /*----------------------------------------------------------*/
         trace off
         myVolume = "TART01"
         "alloc da('JBG."myVolume".DATASETS.CATALOG') f(in) shr"
         "execio * diskr in(fini stem myLine."
         "free f(in)"
         "dropbuf"
         queue left("Datasetname",44),
           right("RC",2),
           right("SR",2),
           myVolume,
           left("DSO",3),
           left("RecFM",6),
           right("LRecl",5),
           right("BlkSiz",6),
           right("Alloc",5),
           right("Used",5),
           right("Free",5),
           right("PrimE",5),
           right("SecoE",5),
           left("Units",8),
           right("Ext",3),
           right("AlDi",4),
           right("UsDi",4),
           right("FrDi",4),
           right("Membe",5)
         do i = 1 to myLine.0
           myDSN = subword(myLine.i,1,1)
           rc = listdsi("'"myDSN"' volume("myVolume") directory")
           select
             when sysunits = "CYLINDER" then
               do
                 mySysAlloc = sysalloc * 15
                 mySysUsed = sysused * 15
                 mySysPrimary = sysprimary * 15
                 mySysSeconds = sysseconds * 15
               end /* of when */
             when sysunits = "BLOCK" then
               do
                 mySysAlloc = sysalloc / sysblkstrk
                 mySysUsed = sysused / sysblkstrk
                 mySysPrimary = sysprimary / sysblkstrk
                 mySysSeconds = sysseconds / sysblkstrk
               end /* of when */
             when sysunits = "TRACK" then
               do
                 mySysAlloc = sysalloc
                 mySysUsed = sysused
                 mySysPrimary = sysprimary
                 mySysSeconds = sysseconds
               end /* of when */
             otherwise
               do
                 mySysAlloc = sysalloc
                 mySysUsed = sysused
                 mySysPrimary = sysprimary
                 mySysSeconds = sysseconds
               end /* of when */
           end /* of select */
           mySysFree = mySysAlloc - mySysUsed
           if sysdsorg = "PO" | sysdsorg = "POU" then
             mySysFreeDirBlk = sysadirblk - sysudirblk
           else
             mySysFreeDirBlk = "-"
           queue left(myDSN,44),
             right(rc,2),
             right(sysreason,2),
             myVolume,
             left(sysdsorg,3),
             left(sysrecfm,6),
             right(syslrecl,5),
             right(sysblksize,6),
             right(mySysAlloc,5),
             right(mySysUsed,5),
             right(mySysFree,5),
             right(mySysPrimary,5),
             right(mySysSeconds,5),
             left(sysunits,8),
             right(sysextents,3),
             right(sysadirblk,4),
             right(sysudirblk,4),
             right(mySysFreeDirBlk,4),
             right(sysmembers,5)
         end /* of do i */
         queue ""
         myFi = "F"||time(s)
         "alloc f("myFi") recfm(v b) lrecl(255) new unit(vio)",
           "delete cylinders space(1,1) reuse dsorg(PS) "
         queue ""
         "execio * diskW "myFi"(fini"
         address ispexec
         "LMINIT DATAID(MYDATAID) DDNAME("myFi")"
         "EDIT DATAID("myDataID") MACRO(MACRORES)"
         "LMFREE DATAID("myDataID")"
         address tso
         "free f("myFi")"
