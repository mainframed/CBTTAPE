         /* REXX Add Exec by UIS0070                                 */
         /*----------------------------------------------------------*/
         /* The author can be reached at joerg.berning(at)gmx.net    */
         /*----------------------------------------------------------*/
         trace off
         address isredit
         "MACRO NOPROCESS(MYINPUT)"
         "PROCESS RANGE C"
         select
           when rc = 0 then
             do
               "(CMD) = RANGE_CMD"
               "(MYFIRST) = LINENUM .ZFRANGE"
               "(MYLAST) = LINENUM .ZLRANGE"
             end /* of do */
           when rc = 4 then                      /* no line selected */
             do
               "(MYFIRST) = LINENUM .ZFIRST"
               "(MYLAST) = LINENUM .ZLAST"
             end /* of do */
           otherwise
             do
               zErrSM = "RC="rc
               zErrLM = "Add failed, RC:"rc
               zErrAlrm = "YES"
               zErrHM = "ISR2MACR"
               address ispexec
               "VPUT (ZERRSM ZERRLM ZERRALRM ZERRHM)"
               "SETMSG MSG(ISRZ002)"
               exit
             end /* of otherwise */
         end /* of select */
         "RECOVERY ON"
         "(CHGST) = DATA_CHANGED"
         if chgst = yes then
           "BUILTIN SAVE"
         select
           when words(myInput) = 0 then
             do
               myFromCol = 1
               "(MYWIDTH) = DATA_WIDTH"
               myToCol = myWidth
             end /* of do */
           when words(myInput) = 2 then
             do
               myFromCol = subword(myInput,1,1)
               myToCol = subword(myInput,2,1)
             end /* of do */
           otherwise
             do
               zErrSM = "Syntax|"
               zErrLM = "Syntax: Add (<FromCol ToCol>)"
               zErrAlrm = "YES"
               zErrHM = "ISR2MACR"
               address ispexec
               "VPUT (ZERRSM ZERRLM ZERRALRM ZERRHM)"
               "SETMSG MSG(ISRZ002)"
               exit 12
             end
         end /* of select */
         myFirst = Strip(myFirst,l,0)
         myLast = Strip(myLast,l,0)
         mySum = 0
         error = 0
         do i = myFirst to myLast
           "(MYCURR) = LINE "i
           myLength = myToCol - myFromCol +1
           myAdd = substr(myCurr,myFromCol,myLength)
           myPos = pos(",",myAdd)
           if myPos <> 0 then
             myAdd = substr(myAdd,1,myPos -1)||substr(myAdd,myPos +1)
           if datatype(myAdd) <> "NUM" then
             do
               error = error + 1
               say "Not added:" myAdd
               myAdd = 0
             end
           mySum = mySum + myAdd
         end
         if error = 0 then
           zErrSM = mySum
         else
           zErrSM = mySum"("error"|||)"
         if error = 0 then
           zErrLM = "Add from "myFromCol" to "myToCol" = "mySum
         else
           zErrLM = "Add from "myFromCol" to "myToCol" = "mySum,
             ", # of rows not selected:"error
         zErrAlrm = "YES"
         zErrHM = "ISR2MACR"
         address ispexec
         "VPUT (ZERRSM ZERRLM ZERRALRM ZERRHM)"
         "SETMSG MSG(ISRZ002)"
         address isredit
         myWidthOfSum = length(mySum)
         my#OfBlanks = myToCol - myWidthOfSum
         "line_after "myLast" = INFOLINE '"||,
           copies(" ",my#OfBlanks)mySum"'"
