         /*Copyright 25 Aug 1999 by JBG REXX HLQInfo                 */
         /*----------------------------------------------------------*/
         /* The author can be reached at joerg.berning(at)gmx.net    */
         /*----------------------------------------------------------*/
         trace off
         rc = outtrap(myCatalog.)
         queue "CATALOG.MVSICFM.VBKP620"
         queue "CATALOG.MVSICFM.VOS3CAT"
         queue "CATALOG.MVSICF1.CA"
         queue "CATALOG.MVSICF1.LANDMARK"
         queue "CATALOG.MVSICF1.SAG"
         queue "CATALOG.MVSICF1.SMEP"
         queue "CATALOG.MVSICF1.TSOUSER"
         queue "CATALOG.MVSICF1.VOS3SMP"
         queue "CATALOG.MVSICF1.VTAPES"
         queue "CATALOG.SYS"
         j = 0
         say "Processing the following catalogs:"
         do queued()
           pull line
           say line
           queue line
         end /* of do */
         do queued()
           pull line
           j = j + 1
           outLine.j = line
           myUserCat = subword(line,1,1)
           rc = outtrap(myUserCatLine.)
           "listc ent('"myUserCat"') all"
           do i = 1 to myUserCatLine.0
             mySubword1 = subword(myUserCatLine.i,1,1)
             if substr(mySubword1,1,5) = "ALIAS" then
               do
                 myAlias = substr(mySubword1,lastpos("-",mySubword1) + 1)
                 j = j + 1
                 outLine.j = myAlias
               end
           end
         end
         "dropbuf"
         say " "
         say j" HLQs found."
         say " "
         do i = 1 to j
           myAlias = subword(outLine.i,1,1)
           if pos(".",myAlias) <> 0 then           /* Catalog-DSName */
             do
               say myAlias
               iterate
             end /* of if */
           rc = outtrap(myListC.)
           "listc lvl('"myAlias"')"
           rc = outtrap(mySR.)
           "sr mask("myAlias".)"
           rc = outtrap("OFF")
           select
             when subword(mySR.1,1,1) = "ICH31005I" then
               my#OfRACFProfiles = "no RACF-Profiles"
             when mySR.0 = 1 then
               do
                 rc = outtrap("myLD.")
                 "ld dataset('"subword(mySR.1,1,1)"')"
                 myUACC = subword(myLD.5,3,1)
                 rc = outtrap("OFF")
                 my#OfRACFProfiles = mySR.1 "UACC:"myUACC
               end
             otherwise
               my#OfRACFProfiles = mySR.0
           end /* of select */
           if subword(myListC.1,1,1) = "IDC3012I" then
             say left(myAlias,8) "no Datasets" my#OfRACFProfiles
           else
             do
               my#OfDS = myListC.0 / 2
               say left(myAlias,8) right(my#OfDS,11) my#OfRACFProfiles
             end
         end
