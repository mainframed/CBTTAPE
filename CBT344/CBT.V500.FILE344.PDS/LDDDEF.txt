         /*COPYRIGHT 3 NOV 1999 BY JBG REXX LDDDEF                   */
         /*----------------------------------------------------------*/
         /* Syntax: LDDDef <dddef-entry>                             */
         /*----------------------------------------------------------*/
         /* The author can be reached at joerg.berning(at)gmx.net    */
         /*----------------------------------------------------------*/
         trace off
         arg myDDDef myCSI
         myCSI = strip(myCSI,,"'")
         if myDDDef = "" then
           myDDDef = ""
         if myCSI = "" then
           myCSI = "SMPE.GLOBAL.CSI"
         "alloc f(SMPCSI) da('"myCSI"') shr reuse"
         "alloc f(SMPLIST) recfm(v b) lrecl(255) new unit(vio)",
           "delete cylinders space(1,1) reuse dsorg(PS) "
         "alloc f(SMPLOG) recfm(v b) lrecl(255) new unit(vio)",
           "delete cylinders space(1,1) reuse dsorg(PS) "
         "alloc f(SMPOUT) recfm(v b) lrecl(255) new unit(vio)",
           "delete cylinders space(1,1) reuse dsorg(PS) "
         "alloc f(SMPRPT) recfm(v b) lrecl(255) new unit(vio)",
           "delete cylinders space(1,1) reuse dsorg(PS) "
         "alloc f(SMPCNTL) recfm(f b) lrecl(80) new unit(vio)",
           "delete tracks space(1) reuse dsorg(PS) "
         "dropbuf"
         if myDDDef <> "" then
           do
             queue "SET BOUNDARY(GLOBAL)."
             queue "LIST ALLZONES DDDEF("myDDDef")."
           end /* of if */
         else
           do
             queue "SET BOUNDARY(GLOBAL)."
             queue "LIST ALLZONES DDDEF."
           end /* of else */
         "execio 2 diskW smpcntl(fini"
         address linkmvs "GIMSMP"
         if rc <> 0 then
           do
             zErrSM = "RC:"rc
             zErrLM = "GIMSMP ended with rc:"rc
             zErrAlrm = "YES"
             zErrHM = "ISR2MACR"
             address ispexec
             "VPUT (ZERRSM ZERRLM ZERRALRM ZERRHM)"
             "SETMSG MSG(ISRZ002)"
             address tso
           end /* of if */
         "dropbuf"
         queue "SMPList:"
         "execio * diskR SMPLIST(fini"
         queue "SMPLog:"
         "execio * diskR SMPLOG(fini"
         queue "SMPOut:"
         "execio * diskR SMPOUT(fini"
         queue "SMPRpt:"
         "execio * diskR SMPRPT(fini"
         queue ""
         "free f(SMPCSI SMPLIST SMPLOG SMPOUT SMPRPT)"
         myFi = "F"||time(s)
         "alloc f("myFi") recfm(v b) lrecl(255) new unit(vio)",
           "delete cylinders space(1,1) reuse dsorg(PS) "
         "execio * diskW "myFi"(fini"
         address ispexec
         "LMINIT DATAID(MYDATAID) DDNAME("myFi")"
         "EDIT DATAID("myDataID") MACRO(MACRORES)"
         "LMFREE DATAID("myDataID")"
         address tso
         "free f("myFi")"
