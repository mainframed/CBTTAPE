./ ADD NAME=SCH0P000                               13AUG93 18.34.21
PROC 0 DEBUG

/* ----------------------------------------------------------------- */
/*                                                                   */
/* History...  Changes...  Modifications...                          */
/* ----------------------------------------                          */
/*                                                                   */
/* . Written 11Jan93, William J. Smith, OSVS Technical Services      */
/*   - This CLIST is provided to establish the parallel testing      */
/*     environment which will allow two concurrent versions of the   */
/*     ISPF Script dialog (S.14.1) to execute.                       */
/*   - This will activate option "0" on panel SCH0P000.              */
/*                                                                   */
/* ----------------------------------------------------------------- */

IF &DEBUG = DEBUG THEN +
  CONTROL MAIN MSG LIST CONLIST SYMLIST
ELSE +
  CONTROL MAIN NOMSG NOLIST NOCONLIST NOSYMLIST

ISPEXEC CONTROL ERRORS RETURN

ISPEXEC LIBDEF ISPMLIB DATASET ID('SYS0.SCRIPT.MLIB')
ISPEXEC LIBDEF ISPPLIB DATASET ID('SYS0.SCRIPT.PLIB')
ALTLIB  ACT APPLICATION(CLIST) DATASET('SYS0.SCRIPT.CLIST')

ISPEXEC SELECT PGM(ISRFPR) PARM((SCH0P100) 0) NEWPOOL

ISPEXEC LIBDEF ISPMLIB DATASET
ISPEXEC LIBDEF ISPPLIB DATASET
ALTLIB  DEACT APPLICATION(CLIST)
EXIT
./ ADD NAME=SCH0P110                               13AUG93 18.34.21
PROC 0 DEBUG

/*-------------------------------------------------------------------*/
/*                                                                   */
/* Application Design Considerations                                 */
/* ---------------------------------                                 */
/*                                                                   */
/* The summer-fall, 1992, rewrite of the Schwab S.14.1 Script/VS     */
/* foreground ISPF front-end was primarily undertaken to incorporate */
/* new CLIST function into a modularly structured and supportable    */
/* application.  Consistency of panel design, variable usage, and    */
/* top-down processing were primary considerations.  The SCRIPTIT    */
/* EDIT macro was also updated to function correctly with the up-    */
/* dated CLISTs, panels, and messages.                               */
/*                                                                   */
/* The systems programmer who supports and updates this application  */
/* should have a thorough understanding of Script/VS syntax, keyword */
/* operand usage, and the current PSF in-house configuration.        */
/* Changes to this application can have dramatic impact on the       */
/* manner in which the application operates (or fails to operate).   */
/*                                                                   */
/* History...  Changes...  Modifications...                          */
/* ----------------------------------------                          */
/*                                                                   */
/*   . Updated 20Jan93, William Smith, OSVS Technical Services       */
/*     - Panel SCH0P100 changed:  Beginner's mode is now "Basic"     */
/*       mode.  Changed message SCHP010 to reflect the new usage.    */
/*                                                                   */
/*   . Updated 20Jan93, William Smith, OSVS Technical Services       */
/*     - Reviewed handling of all VGET and VPUT service calls to     */
/*       guarantee that variable handling is correct.  I was picking */
/*       up certain options that had been VPUT into the profile      */
/*       pool from older copies of the application because I was     */
/*       allowing the VGETs to default to ASIS rather than SHARED.   */
/*       I only want to VPUT variables from the Print Distribution   */
/*       and Separator Page Defaults panel into the profile pool.    */
/*       Everything else should go into the shared pool.             */
/*                                                                   */
/*   . Updated 11Jan93, William Smith, OSVS Technical Services       */
/*     - Another duplex logic improvement:  instead of setting a     */
/*       &DUPLEX to a null value and then issuing a VPUT into the    */
/*       SHARED pool, I used the VERASE service to simply remove     */
/*       the FORMDEF variable from the SHARED pool.  Consequently,   */
/*       the value of FORMDEF will always be null for a continuous   */
/*       forms device or if changed back to "NO".                    */
/*                                                                   */
/*   . Updated 08Jan93, William Smith, OSVS Technical Services       */
/*     - Improved the logic for duplex processing across all real    */
/*       device families.  I was inadvertently propagating the local */
/*       FORMDEF name, DSCRPT, into the Print Distribution and       */
/*       Separator Page Defaults panel for continuous forms devices. */
/*       DUP&SVAR doesn't appear on any of the continuous forms      */
/*       panels.  Consequently, if DUP&SVAR is null or "NO", I       */
/*       should set the FORMDEF name to null, too.  This change      */
/*       causes the FORMDEF name to be set correctly when a          */
/*       document is reformatted using a different intended target   */
/*       device.                                                     */
/*                                                                   */
/*   . Updated 06Jan93, William Smith, OSVS Technical Services       */
/*     - Updated all advanced device panels and SCHPNOVC to add      */
/*       .HELP fields in the )INIT section.  Also added a )HELP      */
/*       section after the )PROC section even though field level     */
/*       help panels have not been written.                          */
/*                                                                   */
/*   . Updated 05Jan93, William Smith, OSVS Technical Services       */
/*     - An IF statement was added just before the section of code   */
/*       that maps an intended real device (&RDEV) to a specific     */
/*       logical device for a new generic printer class "ISI".       */
/*       The ISI 7028 and 7817 can be configured from the front      */
/*       panel to emulate a variety of IBM printers (e.g. 3812,      */
/*       3816, 4028 (among others).  This field should be changed as */
/*       appropriate if the front panel setting is changed.  The     */
/*       intent of this change:  eliminate possible confusion with   */
/*       users who are unfamiliar with the IBM product model no.s.   */
/*       and the current settings on the printer front panel.        */
/*     - Updated SCH0P100 to add support for &RDEV = ISI.            */
/*                                                                   */
/*   . Updated 04Jan93, William Smith, OSVS Technical Services       */
/*     - Updated all advanced device panels, SCHPNOVC, and message   */
/*       members to add support for ALL logical device types for     */
/*       each intended output device.  The "ROTx" panel variable     */
/*       now verifies all possible logical device types for each     */
/*       intended output device.  This change adds significant new   */
/*       function when formatting; every possible dimension and      */
/*       orientation of output media for all supported and intended  */
/*       output devices are now supported.  The panel field name     */
/*       text was changed accordingly for the intended output (real) */
/*       device.                                                     */
/*                                                                   */
/*       This change was suggested by Lauretta Ongaro Zerga in the   */
/*       Printing Technology Group.  It is unlikely that the feature */
/*       will have widespread use; it is intended for the more       */
/*       sophisticated Script "power" user.                          */
/*                                                                   */
/*     - The SCRIPTIT PDF EDIT macro was converted to use only the   */
/*       AFP Core Interchange Fonts logical device (AFPA) rather     */
/*       than use PG2A (IBM 3820).                                   */
/*                                                                   */
/*   . Updated 15Dec92, William Smith, OSVS Technical Services       */
/*     - Ref: WSC 9231 and APAR PN21493 - New device support for     */
/*       Core Interchange Fonts;  PTF UN24230 is superceded by       */
/*       UN29299                                                     */
/*     - I added support for logical device "AFPxxxx" in CLIST       */
/*       SCH0P110, updated SCH0P100, and created panel SCHPAFP       */
/*     - New SVAR = 9 for AFP using either 240 or 300 pel output     */
/*                                                                   */
/*   . Completely re-written during the fall of 1992 by William J.   */
/*     Smith, Staff Systems Programmer.  Largely completed by early  */
/*     November, 1992.  All panels & nested CLISTs were re-written   */
/*     or re-designed to exploit MVS/ESA 4.2.2, JES/2 4.2, PSF/2.1,  */
/*     TSO/E 2.3.1, and DCF Script/VS 1.4.0.  Made use of the new    */
/*     Script/VS TSO Enhancements SPE allowing use of a DDNAME       */
/*     rather than use of the Script/VS keyword operands.            */
/*                                                                   */
/*   . Modified by William J. Smith, Staff Systems Programmer, in    */
/*     the fall of 1990.  Added support for GDDM CDPU, eliminated    */
/*     support for the IBM 6670 and 4250, updated to use TSO/E 2.1   */
/*     CLIST and ISPF 3.1 dialog management services enhancements.   */
/*     Corrected numerous logic errors and reduced complexity for    */
/*     use with DCF Script/VS 1.3.2 and 1.4.0.                       */
/*                                                                   */
/*   . Updated by Roger Murphy, a summer intern, 1986, to add        */
/*     support for IBM 3800 and 3820 page printers.                  */
/*                                                                   */
/*   . Original idea:  Jim Dickson, Systems Programmer, circa 1985.  */
/*                                                                   */
/*-------------------------------------------------------------------*/

/*-------------------------------------------------------------------*/
/*                                                                   */
/* Debug Code                                                        */
/*                                                                   */
/*-------------------------------------------------------------------*/

ISPEXEC VGET (DEBUG) SHARED
IF &DEBUG = DEBUG THEN +
  CONTROL MAIN MSG LIST CONLIST SYMLIST
ELSE +
  CONTROL MAIN NOMSG NOLIST NOCONLIST NOSYMLIST

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Following variables initialized from PGM(ISRFPR) and SCH0P100:    */
/* PRJ1, LIB1, TYP1, MEM, DSN, ZDSQMEM, ZDSQ, ZDS, ZMEM, ZORG.       */
/*                                                                   */
/* Ref:  "ISPF and ISPF/PDF Planning and Customization", Version 3.4 */
/*       Chapter 3, 'ISPF/PDF Foreground and Batch Customizing',     */
/*       p. 103-114, SC34-4257-02.                                   */
/*                                                                   */
/* ----------------------------------------------------------------- */

ISPEXEC VGET (PRJ1,LIB1,TYP1,DSN,ZDSQMEM,ZDSQ,ZDS,ZMEM,ZORG,+
              ZUSER,ZPREFIX,SVAR,SKILL,RDEV,DEBUG,CDPU) ASIS

/* ----------------------------------------------------------------- */

SET &ZFPRFC = 0                          /* Set ISRFPA TSO CP RC     */

/* ----------------------------------------------------------------- */
/*                                                                   */
/* The following NGLOBAL statement names variables that all inline   */
/* subprocedures can use.  Note:  When defining an NGLOBAL variable  */
/* any subprocedure in the same CLIST can refer to it by name and    */
/* modify its value.                                                 */
/*                                                                   */
/* ----------------------------------------------------------------- */

NGLOBAL PRJ1,LIB1,TYP1,DSN,ZDSQMEM,ZDSQ,ZDS,ZMEM,ZORG,ZUSER,ZPREFIX,+
        SVAR,SKILL,RDEV,LISTID

/* ----------------------------------------------------------------- */
/*                                                                   */
/* The following code establishes a unique set of device-dependent   */
/* variables for each logical device family.                         */
/*                                                                   */
/* VGET only those variables that are required on a panel-by-panel   */
/* basis.  The variable names are ordered in the VGET calling        */
/* sequence as the .ZVARS are ordered in each processing section     */
/* of each device-dependent processing panel.                        */
/*                                                                   */
/* The following "IF" statement checking for use of real device type */
/* ISI, was added to allow specification of "ISI" as a generic       */
/* printer class for Interface Systems Printers (ISI 7028 and ISI    */
/* 7817).  Both of these printers can be configured from the front   */
/* panel to emulate a 3812, 3816, 4028 (among others).  This field   */
/* should be changed as appropriate if the front panel setting is    */
/* changed.  This feature should eliminate possible confusion with   */
/* users who are unfamiliar with the IBM product model no.s and the  */
/* current settings on the printer front panel.                      */
/*                                                                   */
/* ----------------------------------------------------------------- */

IF &RDEV = &STR(ISI) THEN +
  SET &RDEV = 3816

SELECT (&RDEV)
  WHEN (TERM | 3270) +
    DO
      SET &SVAR = 1                  /* Logical Device = TERMINAL    */
      SYSCALL VGET_DIALOG_VARIABLES
    END
  WHEN (1403) +
    DO                               /* Logical Device = 1403XXX     */
      SET &SVAR = 2
      SYSCALL VGET_DIALOG_VARIABLES
    END
  WHEN (PS) +
    DO
      SET &SVAR = 3                  /* Logical Device = POSTSCRIPT  */
      SYSCALL VGET_DIALOG_VARIABLES
    END
  WHEN (3800) +
    DO
      SET &SVAR = 4                  /* Logical Device = 38PPXXXX    */
      SYSCALL VGET_DIALOG_VARIABLES
    END
  WHEN (3820) +
    DO
      SET &SVAR = 5                  /* Logical Device = PG1XXXX     */
      SYSCALL VGET_DIALOG_VARIABLES
    END
  WHEN (3812 | 3816) +
    DO
      SET &SVAR = 6                  /* Logical Device = PG2XXXX     */
      SYSCALL VGET_DIALOG_VARIABLES
    END
  WHEN (4224) +
    DO
      SET &SVAR = 7                  /* Logical Device = PG3XXXX     */
      SYSCALL VGET_DIALOG_VARIABLES
    END
  WHEN (4028) +
    DO
      SET &SVAR = 8                  /* Logical Device = PG4XXXX     */
      SYSCALL VGET_DIALOG_VARIABLES
    END
  WHEN (AFP) +
    DO
      SET &SVAR = 9                  /* Logical Device = AFPXXXX     */
      SYSCALL VGET_DIALOG_VARIABLES
    END
END

VGET_DIALOG_VARIABLES: PROC 0
  SELECT (&RDEV)
    WHEN (TERM | 3270) ISPEXEC VGET +
         (BDO&SVAR,BDE&SVAR,CH1&SVAR,CH2&SVAR,+
          CH3&SVAR,CH4&SVAR,MS1&SVAR,MS2&SVAR,ROT&SVAR,+
          SPL&SVAR,UPC&SVAR,PRO&SVAR,SLB&SVAR,OSO&SVAR,DEVT) SHARED

    WHEN (1403) ISPEXEC VGET +
         (BDO&SVAR,BDE&SVAR,SEP&SVAR,CH1&SVAR,CH2&SVAR,+
          CH3&SVAR,CH4&SVAR,MS1&SVAR,MS2&SVAR,ROT&SVAR,+
          SPL&SVAR,UPC&SVAR,PRO&SVAR,SLB&SVAR,OSO&SVAR,DEVT) SHARED

    WHEN (PS) ISPEXEC VGET +
         (PSO,BDO&SVAR,BDE&SVAR,SEP&SVAR,CH1&SVAR,CH2&SVAR,+
          CH3&SVAR,CH4&SVAR,DUP&SVAR,MS1&SVAR,MS2&SVAR,+
          ROT&SVAR,SPL&SVAR,UPC&SVAR,GDDM&SVAR,PSG&SVAR,+
          SPM&SVAR,PIL&SVAR,PRO&SVAR,SLB&SVAR,OSO&SVAR,DEVT) SHARED

    WHEN (3800 | 4224) ISPEXEC VGET +
         (BDO&SVAR,BDE&SVAR,SEP&SVAR,CH1&SVAR,CH2&SVAR,+
          CH3&SVAR,CH4&SVAR,MS1&SVAR,MS2&SVAR,ROT&SVAR,SPL&SVAR,+
          UPC&SVAR,GDDM&SVAR,PSG&SVAR,SPM&SVAR,SGL&SVAR,PRO&SVAR,+
          SLB&SVAR,OSO&SVAR,DEVT) SHARED

    WHEN (3812 | 3816 | 3820 | 4028 | AFP) ISPEXEC VGET +
         (BDO&SVAR,BDE&SVAR,SEP&SVAR,CH1&SVAR,CH2&SVAR,+
          CH3&SVAR,CH4&SVAR,DUP&SVAR,MS1&SVAR,MS2&SVAR,+
          ROT&SVAR,SPL&SVAR,UPC&SVAR,GDDM&SVAR,PSG&SVAR,+
          SPM&SVAR,SGL&SVAR,PRO&SVAR,SLB&SVAR,OSO&SVAR,DEVT) SHARED

  END
END

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Display the device-specific options panel for each of the device  */
/* types supported in the Script environment.                        */
/*                                                                   */
/* SUPPORTED DEVICES:                                                */
/*                                                                   */
/* DEVICE = TERM OR 3270 - output displayed at the terminal          */
/*          1403         - high-speed, line-printer output           */
/*          3800-3       - Siemens 2300, 2140 and IBM 3900           */
/*          PSA          - Adobe PostScript or compatible printers   */
/*          PG1A         - IBM 3820 page printer or compatible       */
/*          PG2A         - IBM 3812 or 3816 printers or compatibles  */
/*          PG3A         - IBM 4224                                  */
/*          PG4A         - IBM 4028 page printer                     */
/*          AFPA         - Core Interchange Fonts (scalable) with    */
/*                         APAR PN21493 @ 240 or 300 pel resolution  */
/*                                                                   */
/* 3800-USAGE NOTES:                                                 */
/* -----------------                                                 */
/* -*-      The default orientation for output printed on the 2300   */
/*          will be portrait using DEVICE(38PPNS90).  The standard   */
/*          stock form mounted in the printer is 12x8.5".  Output    */
/*          must be rotated 90 degrees (cost savings) to obtain a    */
/*          portrait orientation.                                    */
/*                                                                   */
/* -*-      In order to print in landscape mode using the standard   */
/*          12x8.5" stock use DEVICE(38PPNS) when printing on the    */
/*          2300, 2140, or 3900.                                     */
/*                                                                   */
/* -*-      In order to print on the 2140 use DEVICE(38PPN) to       */
/*          print in portrait mode with a non-rotated appearance on  */
/*          9.5x11" stock.                                           */
/*                                                                   */
/* 4224-USAGE NOTES:                                                 */
/* -----------------                                                 */
/* -*-      The IBM 4224 (PG3) doesn't follow the same convention    */
/*          for specifying logical device types as its PGx counter-  */
/*          parts.  A 4224 doesn't really "rotate" since it is a     */
/*          continuous forms rather than cut-sheet printer.          */
/*                                                                   */
/* ----------------------------------------------------------------- */

DISPLAY_DEVICE_OPTIONS_PANELS: +
  IF &SKILL = B THEN +
    DO
      ISPEXEC CONTROL NONDISPL ENTER
      ISPEXEC DISPLAY PANEL(SCHPNOVC)
      SET &RC = &LASTCC
    END
  ELSE +
    DO
      SELECT (&RDEV)
        WHEN (TERM | 3270) +
          DO
            ISPEXEC DISPLAY PANEL(SCHP3270)
            SET &RC = &LASTCC
          END
        WHEN (1403) +
          DO
            ISPEXEC DISPLAY PANEL(SCHP1403)
            SET &RC = &LASTCC
          END
        WHEN (PS) +
          DO
            ISPEXEC DISPLAY PANEL(SCHPPS)
            SET &RC = &LASTCC
          END
        WHEN (3800) +
          DO
            ISPEXEC DISPLAY PANEL(SCHP3800)
            SET &RC = &LASTCC
          END
        WHEN (3820) +
          DO
            ISPEXEC DISPLAY PANEL(SCHP3820)
            SET &RC = &LASTCC
          END
        WHEN (3812 | 3816) +
          DO
            ISPEXEC DISPLAY PANEL(SCHP3816)
            SET &RC = &LASTCC
          END
        WHEN (4224) +
          DO
            ISPEXEC DISPLAY PANEL(SCHP4224)
            SET &RC = &LASTCC
          END
        WHEN (4028) +
          DO
            ISPEXEC DISPLAY PANEL(SCHP4028)
            SET &RC = &LASTCC
          END
        WHEN (AFP) +
          DO
            ISPEXEC DISPLAY PANEL(SCHPAFP)
            SET &RC = &LASTCC
          END
      END
    END

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Determine the return code after displaying the device-specific    */
/* DCF options panel for each of the above-supported printers.       */
/*                                                                   */
/* ----------------------------------------------------------------- */

  SELECT (&RC)
    WHEN (0) +
      GOTO BIND_SPELL_UPCASE_PROCESSING
    WHEN (8) +
      DO
        SET &MSG = SCHP000
        GOTO CLEANUP_AND_EXIT_CLIST
      END
    WHEN (12:20) +
      DO
        SET &MSG = SCHP001
        GOTO CLEANUP_AND_EXIT_CLIST
      END
  END

/* ------------------------------------------------------------------ */
/*                                                                    */
/* BIND, SPELLCHK, and UPCASE are processed in the )PROC section of   */
/* each device panel.                                                 */
/*                                                                    */
/* Duplex processing (printing) is not a function that is native to   */
/* Script/VS.  Duplex printing is a function of an AFP FORMDEF.  The  */
/* "Duplex hardcopy output" option only appears on those panels where */
/* the printer supports duplexed printing (Note: The IBM 1403, 3800,  */
/* 3900, 4224, and 4028 do NOT support duplexed printing).            */
/*                                                                    */
/* However, the Interface Systems ISI 7028 and 7817 emulate an IBM    */
/* 4028 and offer the additional feature of duplexed printing.        */
/* Consequently, the 4028 data entry panel has been modified to add   */
/* support for duplexed printing.                                     */
/*                                                                    */
/* Duplexed hardcopy output support was added here for lack of a      */
/* better place in the CLIST to support it.  The F1DSCRPT FORMDEF is  */
/* a locally developed FORMDEF.  Any comparable FORMDEF can be        */
/* substituted in its place and must be a maximum of 6 characters in  */
/* length.  The FORMDEF name, DSCRPT, will be referenced in the       */
/* dynamic output descriptor DCFOUDES later in this CLIST and will    */
/* appear on the "Print Distribution & Separator Page Defaults"       */
/* panel.                                                             */
/*                                                                    */
/* ------------------------------------------------------------------ */
/*                                                                    */
/* DUP&SVAR is null for continuous forms printers... consequently,    */
/* when DUP&SVAR is null, you should always set the FORMDEF value to  */
/* a null value, too.  This code is necessary when changing between   */
/* different intended real device types.  It guarantees that the      */
/* SHARED pool is clear of any FORMDEF value if you change from a     */
/* duplexed cut-sheet printer to a continuous forms printer (which    */
/* doesn't have DUP&SVAR on its panel).                               */
/*                                                                    */
/* ------------------------------------------------------------------ */

BIND_SPELL_UPCASE_PROCESSING: +
  SET &DUPLEX = &&DUP&SVAR
  SELECT (&DUPLEX)
    WHEN (&STR() | N) +
      ISPEXEC VERASE (FORMDEF) ASIS
    WHEN (Y) +
      DO
        SET &FORMDEF = &STR(DSCRPT)
        ISPEXEC VPUT (FORMDEF) SHARED
      END
  END

/* ------------------------------------------------------------------ */
/*                                                                    */
/* CHARS:  Specify Fonts                                              */
/* ------  -------------                                              */
/*                                                                    */
/* The CHARS option identifies the fonts to be used.                  */
/*                                                                    */
/* For page devices, you specify an 8-character font identifier for   */
/* the coded font.  For PostScript devices, you specify a 1- to       */
/* 8-character AFM (Adobe Font Metrics) filename.                     */
/*                                                                    */
/* If you specify the CHARS option, you must specify at least one     */
/* font.  You can specify a maximum of 31 fonts.                      */
/*                                                                    */
/* Ref:   "DCF: SCRIPT/VS Language Reference", Release 4.0,           */
/*        SH35-0070-06, P. 24-25.                                     */
/*                                                                    */
/* ------------------------------------------------------------------ */

CHARS_PROCESSING: +
  SET &CHAR1 = &&CH1&SVAR
  SET &CHAR2 = &&CH2&SVAR
  SET &CHAR3 = &&CH3&SVAR
  SET &CHAR4 = &&CH4&SVAR
  IF (&CHAR1 ¬= &STR() | &CHAR2 ¬= &STR() | &CHAR3 ¬= &STR() | +
      &CHAR4 ¬= &STR()) THEN +
    SET &CHAR = +
        &STR(CHARS(&&CH1&SVAR &&CH2&SVAR &&CH3&SVAR &&CH4&SVAR))

/* ------------------------------------------------------------------ */
/*                                                                    */
/* DEVICE:  Specify a Logical Output Device                           */
/* -------  -------------------------------                           */
/*                                                                    */
/* SCRIPT/VS logical device support allows a single physical device   */
/* to be defined as many different logical device types, each having  */
/* different characteristics.                                         */
/*                                                                    */
/* Ref:   "DCF: SCRIPT/VS Language Reference", Release 4.0,           */
/*        SH35-0070-06, P. 26-29.                                     */
/*                                                                    */
/* The logical device type (&DEVT) will be set within the )PROC       */
/* portion of each real device type processing panel.  It is largely  */
/* dependent upon the choice of whether to rotate and the physical    */
/* characteristics (size) of the output medium.                       */
/*                                                                    */
/* ------------------------------------------------------------------ */

DEVICE_PROCESSING: +
  GOTO FILE_PROCESSING_ORGANIZATION

/* ------------------------------------------------------------------ */
/*                                                                    */
/* FILE:  Name a Disk File for Output                                 */
/* -----  ---------------------------                                 */
/*                                                                    */
/* Pre-allocate formatted output file with a DDNAME of SCRPTFIL       */
/* ------------------------------------------------------------       */
/*                                                                    */
/* Note:  An alternative method for specifying the direct-access      */
/*        file for the formatted output in TSO is to pre-allocate     */
/*        the desired direct-access file with a DDNAME of SCRPTFIL.   */
/*                                                                    */
/* Ref:   "DCF: SCRIPT/VS Language Reference", Release 4.0,           */
/*        SH35-0070-06, P. 32.                                        */
/*                                                                    */
/* PDS/E's are allocated as output files in both of the inline sub-   */
/* procedures.  No directory blocks are specified and a DSNTYPE of    */
/* LIBRARY is used in each ALLOCATE statement.                        */
/*                                                                    */
/* ------------------------------------------------------------------ */
/*                                                                    */
/* Only partitioned(PO) or physical sequential(PS) input is allowed.  */
/*                                                                    */
/* ------------------------------------------------------------------ */

FILE_PROCESSING_ORGANIZATION: +
  IF (&ZORG = PO) | (&ZORG = PS) THEN +
    IF (&ZORG = PS) THEN +
      DO
        SET &TIME = &SYSTIME
        SET &LISTID = +
        &STR($&SUBSTR(1:2,&TIME)&SUBSTR(4:5,&TIME)&SUBSTR(7:8,&TIME))
        SET &ZMEM = &LISTID
        ISPEXEC VPUT (LISTID,ZMEM) SHARED
      END
    ELSE +
      IF (&ZORG = PO && &ZMEM = ) THEN +
        SET &MSG = SCHP002
      ELSE +
        DO
          SET &LISTID = &ZMEM
          SET &MSG =
        END
  ELSE +
    DO
      SET &MSG = SCHP003
      IF &MSG ¬= THEN +
        GOTO CLEANUP_AND_EXIT_CLIST
    END

FILE_PROCESSING_ALLOCATION: +
  SELECT (&RDEV)
    WHEN (1403) +
      DO
        SET &RECFM_1403 = &STR(V B M)
        SYSCALL ALLOCATE_MISC_FILE_FORMATS LIST &STR(RECFM_1403) 255 3156
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            SET MSG = SCHP004
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END
    WHEN (PS) +
      DO
        SET &RECFM_PS = &STR(V B)
        SYSCALL ALLOCATE_MISC_FILE_FORMATS LISTPS &STR(RECFM_PS) 260 264
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            SET MSG = SCHP004
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END
    WHEN (3800) +
      DO
        SYSCALL ALLOCATE_FILE_VBM_8205_8209 LIST38PP
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            SET MSG = SCHP004
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END
    WHEN (3820 | 3812 | 3816) +
      DO
        SYSCALL ALLOCATE_FILE_VBM_8205_8209 LIST3820
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            SET MSG = SCHP004
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END
    WHEN (4224) +
      DO
        SYSCALL ALLOCATE_FILE_VBM_8205_8209 LIST4224
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            SET MSG = SCHP004
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END
    WHEN (4028) +
      DO
        SYSCALL ALLOCATE_FILE_VBM_8205_8209 LIST4028
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            SET MSG = SCHP004
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END
    WHEN (AFP) +
      DO
        SYSCALL ALLOCATE_FILE_VBM_8205_8209 LISTAFP
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            SET MSG = SCHP004
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END
  END

ALLOCATE_MISC_FILE_FORMATS: PROC 4 SUFFIX RECFM LRECL BLKSIZE
  NGLOBAL SCRIPT_OUTPUT_FILE
  SYSREF &RECFM
  SET &SCRIPT_OUTPUT_FILE = &ZPREFIX..&LISTID..&SUFFIX
  IF &SYSDSN('&SCRIPT_OUTPUT_FILE') = &STR(OK) THEN +
    DEL '&SCRIPT_OUTPUT_FILE'
  ALLOCATE F(SCRPTFIL) DA('&SCRIPT_OUTPUT_FILE(&LISTID)') +
    UNIT(TSODA) SP(5,2) TRACKS DSORG(PO) NEW REUSE RELEASE +
    RECFM(&RECFM) LRECL(&LRECL) BLKSIZE(&BLKSIZE) DSNTYPE(LIBRARY)
  SET &RC = &LASTCC
  IF &RC > 0 THEN +
    DO
      ISPEXEC DISPLAY PANEL(SCH0P160)
      RETURN CODE(&RC)
    END
END

ALLOCATE_FILE_VBM_8205_8209: PROC 1 SUFFIX
  NGLOBAL SCRIPT_OUTPUT_FILE
  SET &SCRIPT_OUTPUT_FILE = &ZPREFIX..&LISTID..&SUFFIX
  IF &SYSDSN('&SCRIPT_OUTPUT_FILE') = &STR(OK) THEN +
    DEL '&SCRIPT_OUTPUT_FILE'
  ALLOCATE F(SCRPTFIL) DA('&SCRIPT_OUTPUT_FILE(&LISTID)') +
    UNIT(TSODA) SP(5,2) TRACKS DSORG(PO) NEW REUSE RELEASE +
    RECFM(V B M) LRECL(8205) BLKSIZE(8209) DSNTYPE(LIBRARY)
  SET &RC = &LASTCC
  IF &RC > 0 THEN +
    DO
      ISPEXEC DISPLAY PANEL(SCH0P160)
      RETURN CODE(&RC)
    END
END

/* ------------------------------------------------------------------ */
/*                                                                    */
/* FONTLIB:  Specify a Font Library                                   */
/* --------  ----------------------                                   */
/*                                                                    */
/* Pre-allocate required font libraries with a DDNAME of SCRPTFNT     */
/* --------------------------------------------------------------     */
/*                                                                    */
/* Note:  When you have a DDNAME of SCRPTFNT allocated and you don't  */
/*        specify the FONTLIB command option, SCRIPT/VS searches all  */
/*        of the font libraries concatenated in SCRPTFNT.             */
/*                                                                    */
/* ===>   The first DCFINDEX found in the concatenation must          */
/*        accurately represent all of the font members in the         */
/*        SCRPTFNT concatenation.                                     */
/*                                                                    */
/* Ref:   "DCF: SCRIPT/VS Language Reference", Release 4.0,           */
/*        SH35-0070-06, P. 33.                                        */
/*                                                                    */
/* ------------------------------------------------------------------ */

FONTLIB_PROCESSING: +
  SELECT (&RDEV)
    WHEN (PS) +
      DO
        ALLOC F(SCRPTFNT) DA('SYS0.SCRIPT.FONTPS') SHR REUSE
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            ISPEXEC DISPLAY PANEL(SCH0P161)
            SET MSG = SCHP005
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END

    WHEN (3800) +
      DO
        ALLOC F(SCRPTFNT) DA('SYS0.FONTLIB' 'SYS1.FONTLIB') SHR REUSE
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            ISPEXEC DISPLAY PANEL(SCH0P161)
            SET MSG = SCHP005
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END

    WHEN (3820 | 3812 | 3816 | 4224) +
      DO
        ALLOC F(SCRPTFNT) DA('SYS0.FONTLIBB' 'SYS1.FONTLIBB') SHR REUSE
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            ISPEXEC DISPLAY PANEL(SCH0P161)
            SET MSG = SCHP005
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END

    WHEN (4028 | AFP) +
      DO
        ALLOC F(SCRPTFNT) DA('SYS0.FONT300' 'SYS1.FONT300' +
                             'SYS0.FONTLIBB' 'SYS1.FONTLIBB') SHR REUSE
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            ISPEXEC DISPLAY PANEL(SCH0P161)
            SET MSG = SCHP005
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END
  END

/* ------------------------------------------------------------------ */
/*                                                                    */
/* LIB:  Specify Symbol and Macro Libraries                           */
/* ----  ----------------------------------                           */
/*                                                                    */
/* Pre-allocate symbol/macro libraries with a DDNAME of SCRPTLIB      */
/* -------------------------------------------------------------      */
/*                                                                    */
/* Note:  An alternative method for specifying symbol and macro       */
/*        libraries in TSO is to pre-allocate the desired libraries   */
/*        with a DDNAME of SCRPTLIB.  If this ddname is found,        */
/*        SCRIPT/VS searches all of the libraries linked to this      */
/*        DDNAME.                                                     */
/*                                                                    */
/* 1. If user specifies a "user macro library", concatenate the       */
/*    symbol and macro libraries as follows:                          */
/*       "USER MACRO LIBRARY"                                         */
/*       SYS0.SCRIPT.SCHWAB.MACLIB                                    */
/*       SYS0.SCRIPT.MACLIB                                           */
/*                                                                    */
/* 2. If user does not specify a "user macro library", concatenate    */
/*    symbol and macro libraries as follows:                          */
/*       SYS0.SCRIPT.SCHWAB.MACLIB                                    */
/*       SYS0.SCRIPT.MACLIB                                           */
/*                                                                    */
/* Ref:  "DCF: SCRIPT/VS Language Reference", Release 4.0,            */
/*       SH35-0070-06, P. 33.                                         */
/*                                                                    */
/* ------------------------------------------------------------------ */

LIB_PROCESSING: +
  SET &USER_SCRIPT_MACLIB = &&SLB&SVAR
  SELECT
    WHEN (&USER_SCRIPT_MACLIB = &STR()) +
      DO
        ALLOC F(SCRPTLIB) DA('SYS0.SCRIPT.SCHWAB.MACLIB' +
                             'SYS0.SCRIPT.MACLIB') SHR REUSE
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            ISPEXEC DISPLAY PANEL(SCH0P162)
            SET MSG = SCHP006
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END
    OTHERWISE +
      DO
        DO WHILE &SYSDSN(&USER_SCRIPT_MACLIB) ¬= &STR(OK)
          ISPEXEC DISPLAY MSG(SCHP000A) CURSOR(SLB&SVAR)
          SET &RC = &LASTCC
          SELECT
            WHEN (&USER_SCRIPT_MACLIB = &STR()) +
              DO
                ALLOC F(SCRPTLIB) DA('SYS0.SCRIPT.SCHWAB.MACLIB' +
                                     'SYS0.SCRIPT.MACLIB') SHR REUSE
                SET &RC = &LASTCC
                IF &RC = 0 THEN +
                  GOTO MESSAGE_PROCESSING
                ELSE +
                  DO
                    ISPEXEC DISPLAY PANEL(SCH0P162)
                    SET MSG = SCHP006
                    GOTO CLEANUP_AND_EXIT_CLIST
                  END
              END
            WHEN (&RC > 0) +
              DO
                SET MSG = SCHP000
                GOTO CLEANUP_AND_EXIT_CLIST
              END
          END
        END
        ALLOC F(SCRPTLIB) DA(&USER_SCRIPT_MACLIB +
                             'SYS0.SCRIPT.SCHWAB.MACLIB' +
                              'SYS0.SCRIPT.MACLIB') SHR REUSE
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            ISPEXEC DISPLAY PANEL(SCH0P162)
            SET MSG = SCHP006
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END
  END

/* ------------------------------------------------------------------ */
/*                                                                    */
/* MESSAGE:  Control Message Printing                                 */
/* --------  ------------------------                                 */
/*                                                                    */
/* ID causes SCRIPT/VS to include the error message identifier along  */
/* with the error message.                                            */
/*                                                                    */
/* TRACE causes SCRIPT/VS to list, whenever appropriate, the          */
/* sequence of imbedded files, from the file that includes the next   */
/* line afte the error input line, backward to the primary input      */
/* file.                                                              */
/*                                                                    */
/* Message should always be specified before any other command        */
/* option.                                                            */
/*                                                                    */
/* ------------------------------------------------------------------ */

MESSAGE_PROCESSING: +
  SET &MS1 = &&MS1&SVAR
  IF &MS1 = T THEN +
    SET &MESS1 =
  ELSE +
    SET &MESS1 = DELAY
  SET &MS2 = &&MS2&SVAR
  IF &MS2 = N THEN +
    SET &MESS2 =
  ELSE +
    SET &MESS2 = TRACE

/* ------------------------------------------------------------------ */
/*                                                                    */
/* PROFILE:  Specify a Profile                                        */
/* --------  -----------------                                        */
/*                                                                    */
/* Pre-allocate files containing macros with a DDNAME of SCRPTPRO     */
/* --------------------------------------------------------------     */
/*                                                                    */
/* Note:  An alternative method for specifying the profile that you   */
/*        want SCRIPT/VS to use for processing in TSO is to pre-      */
/*        allocate the desired profile with the DDNAME of SCRPTPRO.   */
/*                                                                    */
/* If the user specifies a profile, then allocate their data set as   */
/* the "user profile", otherwise allocate IBM's DSMPROF4 GML Starter  */
/* Set Profile as the default profile.                                */
/*                                                                    */
/* Ref:   "DCF: SCRIPT/VS Language Reference", Release 4.0,           */
/*        SH35-0070-06, P. 40.                                        */
/*                                                                    */
/* ------------------------------------------------------------------ */

PROFILE_PROCESSING: +
  SET &USER_PROFILE_LIBRARY = &&PRO&SVAR
  SELECT
    WHEN (&USER_PROFILE_LIBRARY = &STR()) +
      DO
        ALLOC F(SCRPTPRO) DA('SYS0.SCRIPT.MACLIB(DSMPROF4)') SHR REUSE
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            ISPEXEC DISPLAY PANEL(SCH0P163)
            SET MSG = SCHP007
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END
    OTHERWISE +
      DO
        DO WHILE &SYSDSN(&USER_PROFILE_LIBRARY) ¬= &STR(OK)
          ISPEXEC DISPLAY MSG(SCHP000A) CURSOR(PRO&SVAR)
          SET &RC = &LASTCC
          SELECT
            WHEN (&USER_PROFILE_LIBRARY = &STR()) +
              DO
                ALLOC F(SCRPTPRO) DA('SYS0.SCRIPT.MACLIB(DSMPROF4)') +
                      SHR REUSE
                SET &RC = &LASTCC
                IF &RC = 0 THEN +
                  GOTO SEARCH_PROCESSING
                ELSE +
                  DO
                    ISPEXEC DISPLAY PANEL(SCH0P163)
                    SET MSG = SCHP007
                    GOTO CLEANUP_AND_EXIT_CLIST
                  END
              END
            WHEN (&RC > 0) +
              DO
                SET MSG = SCHP000
                GOTO CLEANUP_AND_EXIT_CLIST
              END
          END
        END
        ALLOC F(SCRPTPRO) DA(&USER_PROFILE_LIBRARY) SHR REUSE
        SET &RC = &LASTCC
        IF &RC > 0 THEN +
          DO
            ISPEXEC DISPLAY PANEL(SCH0P163)
            SET MSG = SCHP007
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END
  END
/* ------------------------------------------------------------------ */
/*                                                                    */
/* SEARCH: Specify a Library for Imbedded or Appended Files           */
/* ------- ------------------------------------------------           */
/*                                                                    */
/* If the TEXTLIB DDNAME has been allocated for the TSO session, the  */
/* allocated libraries are searched for a member of the given name.   */
/* The same restrictions that apply to SEARCH libraries also apply to */
/* macro libraries.                                                   */
/*                                                                    */
/* This option is being included to allow a user to include an ASCII  */
/* file that contains PostScript commands in a SCRIPT/VS file.        */
/*                                                                    */
/* Ref:  "DCF: SCRIPT/VS Language Reference", Release 4.0,            */
/*       SH35-0070-06, P. 42, AND P. 260 (.PO/PostScript control      */
/*       word)                                                        */
/*                                                                    */
/* ------------------------------------------------------------------ */

SEARCH_PROCESSING: +
  SET &USER_SEARCH_LIBRARY = &&PIL&SVAR
  IF (&RDEV = PS) THEN +
    DO
      IF &USER_SEARCH_LIBRARY ¬= &STR() THEN +
        DO
          DO WHILE &SYSDSN(&USER_SEARCH_LIBRARY) ¬= &STR(OK)
            ISPEXEC DISPLAY MSG(SCHP000A) CURSOR(PIL&SVAR)
            SET &RC = &LASTCC
            SELECT
              WHEN (&USER_SEARCH_LIBRARY = &STR()) +
                GOTO SEGLIB_PROCESSING
              WHEN (&RC > 0) +
                DO
                  SET MSG = SCHP000
                  GOTO CLEANUP_AND_EXIT_CLIST
                END
            END
          END
          ALLOC F(TEXTLIB) DA(&USER_SEARCH_LIBRARY) SHR REUSE
          IF &LASTCC > 0 THEN +
            DO
              ISPEXEC DISPLAY PANEL(SCH0P164)
              SET &MSG = SCHP008
              GOTO CLEANUP_AND_EXIT_CLIST
            END
        END
    END

/* ------------------------------------------------------------------ */
/*                                                                    */
/* SEGLIB: Specify a Segment Library                                  */
/* ------- -------------------------                                  */
/*                                                                    */
/* Pre-allocate segment libraries with a DDNAME of SCRPTSEG           */
/* --------------------------------------------------------           */
/*                                                                    */
/* Note:  An alternative method for specifying segment libraries in   */
/*        TSO is to pre-allocate the desired libraries with a DDNAME  */
/*        of SCRPTSEG.  When you have a DDNAME of SCRPTSEG allocated  */
/*        the default segment library is not added to the list of     */
/*        concatenated libaries.  The SEGLIB option is ignored for    */
/*        line printers and PostScript printers.                      */
/*                                                                    */
/* 1. If user specifies a "user page segment library", concatenate    */
/*    the page segment libraries as follows:                          */
/*       "USER_PAGE SEGMENT_LIBRARY"                                  */
/*       SYS0.PSEGLIB                                                 */
/*       SYS1.PSEGLIB                                                 */
/*                                                                    */
/* 2. If user does not specify a "user page segment library",         */
/*    concatenate the in-house and IBM-supplied page segment          */
/*    libraries as follows:                                           */
/*       SYS0.PSEGLIB                                                 */
/*       SYS1.PSEGLIB                                                 */
/*                                                                    */
/* Ref:  "DCF: SCRIPT/VS Language Reference", Release 4.0,            */
/*       SH35-0070-06, P. 42.                                         */
/*                                                                    */
/* ------------------------------------------------------------------ */

SEGLIB_PROCESSING: +
  IF (&RDEV ¬= TERM | &RDEV ¬= 3270 | &RDEV ¬= 1403) THEN +
    DO
      SET &USER_SEGMENT_LIBRARY = &&SGL&SVAR
      SELECT
        WHEN (&USER_SEGMENT_LIBRARY = &STR()) +
          DO
            ALLOC F(SCRPTSEG) DA('SYS0.PSEGLIB' 'SYS1.PSEGLIB') +
                              SHR REUSE
            SET &RC = &LASTCC
            IF &RC > 0 THEN +
              DO
                ISPEXEC DISPLAY PANEL(SCH0P165)
                SET MSG = SCHP009
                GOTO CLEANUP_AND_EXIT_CLIST
              END
          END
        OTHERWISE +
          DO
            DO WHILE &SYSDSN(&USER_SEGMENT_LIBRARY) ¬= &STR(OK)
              ISPEXEC DISPLAY MSG(SCHP000A) CURSOR(SGL&SVAR)
              SET &RC = &LASTCC
              SELECT
                WHEN (&USER_SEGMENT_LIBRARY = &STR()) +
                  DO
                    ALLOC F(SCRPTSEG) DA('SYS0.PSEGLIB' 'SYS1.PSEGLIB') +
                                      SHR REUSE
                    SET &RC = &LASTCC
                    IF &RC = 0 THEN +
                      GOTO SEPMASTR_PROCESSING
                    ELSE +
                      DO
                        ISPEXEC DISPLAY PANEL(SCH0P165)
                        SET MSG = SCHP009
                        GOTO CLEANUP_AND_EXIT_CLIST
                      END
                  END
                WHEN (&RC > 0) +
                  DO
                    SET MSG = SCHP000
                    GOTO CLEANUP_AND_EXIT_CLIST
                  END
              END
            END
            ALLOC F(SCRPTSEG) DA(&USER_SEGMENT_LIBRARY +
                                 'SYS0.PSEGLIB' 'SYS1.PSEGLIB') +
                              SHR REUSE
            SET &RC = &LASTCC
            IF &RC > 0 THEN +
              DO
                ISPEXEC DISPLAY PANEL(SCH0P165)
                SET MSG = SCHP009
                GOTO CLEANUP_AND_EXIT_CLIST
              END
          END
      END

/* ------------------------------------------------------------------ */
/*                                                                    */
/* SEPMASTR:  Specify Output Separation Masters                       */
/* ---------  ---------------------------------                       */
/*                                                                    */
/* Separation masters are used in the printing process to produce     */
/* multi-part forms or multiple-color documents.                      */
/*                                                                    */
/* The SEPMASTR option allows you to separate a page into multiple    */
/* masters that can then be used in an offset printing process to     */
/* produce color documents or as multi-part forms.  Each output       */
/* master is produced 'n' times where 'n' is the number of masters    */
/* selected for that page.  The 'n' masters specified with the        */
/* SEPMASTR command option correspond to the numbers given on the     */
/* .SM (Separation Master) control word.  The SEPMASTR command option */
/* is specified as follows:                                           */
/*                                                                    */
/* SEPMASTR(n0 ... n16)     where n0 through n16 represent the        */
/*                          specified suboptions                      */
/*                                                                    */
/* Ref:  "DCF: SCRIPT/VS Language Reference", Release 4.0,            */
/*       SH35-0070-06, P. 43.                                         */
/*                                                                    */
/* SEPMASTR is processed in the )PROC section of all composed-page    */
/* device panels.                                                     */
/*                                                                    */
/* ------------------------------------------------------------------ */

SEPMASTR_PROCESSING: +
  GOTO GDDM_PGF_PROCESSING

/* ----------------------------------------------------------------- */
/*                                                                   */
/* GDDM:  Graphical Data Display Manager                             */
/* -----  ------------------------------                             */
/*                                                                   */
/* The first panel displayed, SCH0P110, informs the user that a      */
/* graphics capable display device must be in use before GDDM host   */
/* graphics can be used.                                             */
/*                                                                   */
/* GDDM may be used on a personal computer that is using a 3270      */
/* emulator (e.g. Attachmate EXTRA! for Windows, OS/2 2.0 with       */
/* Extended Services and GDDM OS/2 LINK, or Rumba with the Host      */
/* Graphics Option).                                                 */
/*                                                                   */
/* ----------------------------------------------------------------- */

GDDM_PGF_PROCESSING: +
  SET &GDDM = &&GDDM&SVAR
  IF &GDDM = Y THEN +
    DO
      ISPEXEC DISPLAY PANEL(SCH0P110)
      SET &RC = &LASTCC
      SELECT (&RC)
        WHEN (0) +
          DO

/* ----------------------------------------------------------------- */
/*                                                                   */
/* The second panel displayed, SCH0P300, provides a selection panel  */
/* to allow choices of various PGF components (ICU, ISE, VSE),       */
/* additional IBM/OEM products, or in-house developed GDDM-based     */
/* applications.  This option is provided only as a "bridge" between */
/* DCF and GDDM.                                                     */
/*                                                                   */
/* Note:  The return codes from the SELECT service are -not- the     */
/*        same as the DISPLAY service !  Check carefully before you  */
/*        make any changes.                                          */
/*                                                                   */
/* ----------------------------------------------------------------- */

            ISPEXEC SELECT PANEL(SCH0P300)
            SET &RC = &LASTCC
            SELECT (&RC)
              WHEN (0:4) +
                DO
                  SET &PAGESEG = &&PSG&SVAR
                  IF &PAGESEG = Y THEN +
                    DO
                      ISPEXEC SETMSG MSG(SCHP000B)
                      GOTO PAGE_SEGMENT_PROCESSING
                    END
                END
              WHEN (12:20) +
                DO
                  SET &MSG = SCHP001
                  GOTO CLEANUP_AND_EXIT_CLIST
                END
            END
          END
        WHEN (4:8) +
          DO
            SET &PAGESEG = &&PSG&SVAR
            IF &PAGESEG = Y THEN +
              DO
                ISPEXEC SETMSG MSG(SCHP000C)
                GOTO PAGE_SEGMENT_PROCESSING
              END
          END
        WHEN (12:20) +
          DO
            SET &MSG = SCHP001
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END


    END

/* ----------------------------------------------------------------- */
/*                                                                   */
/* AFP Page Segment Creation and Conversion                          */
/* ----------------------------------------                          */
/*                                                                   */
/* The third panel displayed, SCH0P120, is a locally-written panel   */
/* which front-ends a modified version of the IBM-supplied CLIST,    */
/* ADMUCIMT.                                                         */
/*                                                                   */
/* ADMUCIMT drives module ADMUCDSO allowing a user to output charts  */
/* or GDF on a composed-page, family-4, printer.  It provides the    */
/* SCRIPT user with a means by which to convert ICU chart and data   */
/* components to page segments.                                      */
/*                                                                   */
/* ----------------------------------------------------------------- */

PAGE_SEGMENT_PROCESSING: +
  SET &PAGESEG = &&PSG&SVAR
  IF &PAGESEG = Y THEN +
    ISPEXEC SELECT CMD(%SCH0P120 &DEBUG)

/* ------------------------------------------------------------------ */
/*                                                                    */
/* Process "other" user-specified command options.                    */
/*                                                                    */
/* ------------------------------------------------------------------ */

OTHER_OPTIONS_PROCESSING: +
  SET &OSO = &STR(&&OSO&SVAR)

/* ------------------------------------------------------------------ */
/*                                                                    */
/* Initiate Script/VS Processing                                      */
/* -----------------------------                                      */
/*                                                                    */
/* The following SCRIPT/VS messages and return codes are documented   */
/* in the DCF "Messages" manual, Release 4.0 (SH35-0048-07), p. 5-6.  */
/*                                                                    */
/* Severity Code    Description                     Return Code       */
/*   R              Reply required                      0             */
/*   I              Informational                       0             */
/*   W              Warning                             4             */
/*   E              Error                               8             */
/*   S              Severe Error                        12            */
/*   T              Terminal Error                      16            */
/*                  Incorrect parameters on             24            */
/*                  Script command line                               */
/*                                                                    */
/* ------------------------------------------------------------------ */
/*                                                                    */
/* The variable ZFPRFC is documented in the ISPF "Planning and        */
/* Customizing" manual, SC34-4257-04, topic "Foreground and Batch     */
/* Customizing".  Do not set &ZFPRFC = &RC just before the CLIST      */
/* WRITE statement below.  Otherwise, you will display messages       */
/* generated by ISRFPA rather than use your own messages.             */
/*                                                                    */
/* ------------------------------------------------------------------ */
/*                                                                    */
/* Due to limitations within the CLIST language, a REXX EXEC will be  */
/* called to calculate the CPU and SRM time necessary to proccess the */
/* SCRIPT command.  The CLIST language cannot perform arithmetic op-  */
/* erations upon the &SYSCPU and &SYSSRV built-in functions.  The     */
/* calculated value is slightly skewed due to the execution of the    */
/* intermediate SET statement which stores the &LASTCC value from     */
/* the SCRIPT command execution.                                      */
/*                                                                    */
/* ------------------------------------------------------------------ */
/*                                                                    */
/* Test invocation (i.e. pseudo "STEPLIB") within ISPF/PDF can be     */
/* accomplished by using the following format of the XEQ command to   */
/* point to another version of the SCRIPT/VS interpreter:             */
/*                                                                    */
/* XEQ SCRIPT TASKLIB('SYS0.SCRIPT.LINKLIB') CP                       */
/*                                                                    */
/* XEQ is a public domain command that can be found on the Share MVS  */
/* or CBT modifications tape.  It uses the ATTACH macro to establish  */
/* a "task" library environment.                                      */
/*                                                                    */
/* ------------------------------------------------------------------ */

SCRIPT_PROCESSING: +
  SET &SCRIPT_INPUT_FILE = &ZDSQMEM
  ISPEXEC VGET (ZSM) PROFILE
  IF &ZSM = YES THEN +
    ISPEXEC CONTROL DISPLAY SM START(1)
  ELSE +
    ISPEXEC CONTROL DISPLAY LINE START (1)

  SET &CPU_BEFORE= &SYSCPU
  SET &SRV_BEFORE = &SYSSRV
  SCRIPT '&SCRIPT_INPUT_FILE' +
                    M(&MESS1 ID &MESS2) &BIND &CHAR +
                    DEVICE(&DEVT) &SPL &UPC &SEPM &PSOUT &OSO
  SET &RC = &LASTCC
  SET &CPU_AFTER = &SYSCPU
  SET &SRV_AFTER = &SYSSRV
  %SCH0P140 &CPU_AFTER &CPU_BEFORE &SRV_AFTER &SRV_BEFORE
/*SET &ZFPRFC = &RC (Only used for debugging messages from ISRFPA...) */
  WRITE  The SCRIPT/VS text formatter has ended with return code &RC..
  IF &RC >= 12 THEN +
    DO
      SET &MSG = SCHP001
      GOTO CLEANUP_AND_EXIT_CLIST
    END

/* ------------------------------------------------------------------ */
/*                                                                    */
/* ISPF BROWSE SERVICE VS. THE COMPOSITE DOCUMENT PRINTING UTILITY    */
/* ---------------------------------------------------------------    */
/*                                                                    */
/* Depending upon the logical device and the options, the following   */
/* code will allow the user to view a formatted document upon a       */
/* supported display device.                                          */
/*                                                                    */
/* If a document is formatted for a line printer (e.g. 1403),         */
/* use the ISPF BROWSE service to view the formatted document.        */
/*                                                                    */
/* If a document is formatted for a PostScript and the PSOUT option   */
/* specifies "EBCDIC" (Note:  the default is ASCII), use the ISPF     */
/* BROWSE service to view the generated PostScript document code.     */
/* ASCII PostScript output will not be displayed using the BROWSE     */
/* service.                                                           */
/*                                                                    */
/* If a document is formatted for a page printer, invoke the GDDM     */
/* Composite Document Printing Utility to view the formatted          */
/* document.                                                          */
/*                                                                    */
/* The BROWSE service sets a return code of 0 when PF3 (END) is       */
/* depressed indicating a normal completion of the viewing process.   */
/*                                                                    */
/* ------------------------------------------------------------------ */

GDDM_CDPU_PROCESSING: +
  SELECT
    WHEN (&RDEV = TERM | &RDEV = 3270) +
      GOTO CLEANUP_AND_EXIT_CLIST
    WHEN (&RDEV = 1403) +
      DO
        ISPEXEC BROWSE DATASET('&SCRIPT_OUTPUT_FILE(&LISTID)')
        IF &LASTCC >0 THEN +
          DO
            SET MSG = SCHP000G
            GOTO CLEANUP_AND_EXIT_CLIST
          END
      END
    WHEN (&RDEV = PS) +
      DO
        IF &PSOUT = &STR(PSOUT(EBCDIC)) THEN +
          DO
            ISPEXEC BROWSE DATASET('&SCRIPT_OUTPUT_FILE(&LISTID)')
            IF &LASTCC >0 THEN +
              DO
                SET MSG = SCHP000G
                GOTO CLEANUP_AND_EXIT_CLIST
              END
          END
        ELSE +
          GOTO OUTPUT_PROCESSING
      END
    OTHERWISE +
      DO
        ISPEXEC VGET (CDPU) SHARED
        IF &CDPU ¬= NO THEN +
          DO
            ISPEXEC CONTROL ERRORS RETURN
            ISPEXEC SELECT CMD(%SCH0P130 +
              '&SCRIPT_OUTPUT_FILE(&LISTID)' &DEBUG)
            SET &RC = &LASTCC
/* ----------------------------------------------------------------- */
/*                                                                   */
/* NORMAL TERMINATION FROM THE COMPOSITE DOCUMENT PRINTING UTILITY ? */
/*                                                                   */
/* Be aware that ADM4CDUT will generate a non-zero return code if    */
/* the document cannot be displayed.  Refer to the GDDM manual and   */
/* the documentation supplied by IBM in CLIST "SCH0P130".  This      */
/* CLIST is a GDDM-supplied CLIST that has been slightly modified    */
/* to support allocation by data set name rather than by DDNAME.     */
/*                                                                   */
/* Note:  The return codes from the SELECT service are -not- the     */
/*        same as the DISPLAY service !  Check carefully before you  */
/*        make any changes.                                          */
/*                                                                   */
/* ----------------------------------------------------------------- */
            IF &RC >4 THEN +
              ISPEXEC SETMSG MSG(SCHP000H)
          END
      END
  END

/* ----------------------------------------------------------------- */
/*                                                                   */
/* SCRIPT Output File Foreground Print Processing                    */
/* ----------------------------------------------                    */
/*                                                                   */
/* If the END command is entered, the SCRIPT/VS output file is kept  */
/* without printing (END command RC=8).                              */
/*                                                                   */
/* ----------------------------------------------------------------- */
/*                                                                   */
/* LISTDSI is used to obtain data set characteristics of the SCRIPT  */
/* output data set (FILE) after SCRIPT processing has completed.     */
/* The data set attributes will be displayed along with the SRM and  */
/* CPU processing times.                                             */
/*                                                                   */
/* ----------------------------------------------------------------- */

OUTPUT_PROCESSING: +
  SET &SCRPTIN = &SCRIPT_INPUT_FILE
  SET &SCRPTOUT = &SCRIPT_OUTPUT_FILE(&LISTID)

  LISTDSI '&SCRPTOUT'
  SET &UNIT = &SYSUNIT
  SET &VOL = &SYSVOLUME
  SET &DSORG = &SYSDSORG
  SET &RECFM = &SYSRECFM
  SET &LRECL = &SYSLRECL
  SET &BLKSIZE = &SYSBLKSIZE
  SET &SPACE = &SYSUNITS
  SET &ALLOC = &SYSALLOC
  SET &USED = &SYSUSED

  ISPEXEC DISPLAY PANEL(SCH0P140)
  SET &RC = &LASTCC
  SELECT (&RC)
    WHEN (0) +
      DO
        SET &PAGE_SEPARATOR = &&SEP&SVAR
        SELECT (&PRINTOPT)
          WHEN (PK) +
            DO
              SYSCALL OUTDES_PRINTDS_SUBROUTINE &PAGE_SEPARATOR
              SET &RC= &LASTCC
              IF &RC = 0 THEN +
                SET &MSG = SCHP000I
              ELSE +
                SET &MSG = SCHP000M
              GOTO CLEANUP_AND_EXIT_CLIST
            END
          WHEN (PD) +
            DO
              SYSCALL OUTDES_PRINTDS_SUBROUTINE &PAGE_SEPARATOR
              SET &RC= &LASTCC
              IF &RC = 0 THEN +
                DO
                  DELETE '&SCRIPT_OUTPUT_FILE'
                  SET &MSG = SCHP000J
                END
              ELSE +
                SET &MSG = SCHP000M
              GOTO CLEANUP_AND_EXIT_CLIST
            END
          WHEN (K) +
            DO
              SET &MSG = SCHP000K
              GOTO CLEANUP_AND_EXIT_CLIST
            END
          WHEN (D) +
            DO
              DELETE '&SCRIPT_OUTPUT_FILE'
              SET &MSG = SCHP000L
              GOTO CLEANUP_AND_EXIT_CLIST
            END
        END
      END
    WHEN (4:8) +
      DO
        SET &MSG = SCHP000M
        GOTO CLEANUP_AND_EXIT_CLIST
      END
    WHEN (12:20) +
      DO
        SET &MSG = SCHP000N
        GOTO CLEANUP_AND_EXIT_CLIST
      END
  END

/* ----------------------------------------------------------------- */
/*                                                                   */
/* CONSTRUCT OUTPUT DESCRIPTORS FOR USE IN SEPARATOR PAGE FORMATTING */
/* ----------------------------------------------------------------- */
/*                                                                   */
/* The output descriptor "DCFOUDES" has each of its keywords in the  */
/* the same order as the keywords appear in the body of panel        */
/* SCH0P150.  &NRSTR is used with OUTDES to prevent errors that can  */
/* arise if any of the text entered on SCH0P150 contains an amper-   */
/* sand.                                                             */
/*                                                                   */
/* Return Codes for the OUTDES Command                               */
/* -----------------------------------                               */
/*                                                                   */
/* The return codes are:                                             */
/*                                                                   */
/* RC = 0  - Processing successful.                                  */
/* RC = 12 - The installation exit requested termination.            */
/* RC = 16 - Processing unsuccessful.                                */
/*                                                                   */
/* Return Codes for the PRINTDS Command                              */
/* ------------------------------------                              */
/*                                                                   */
/* The return codes are:                                             */
/*                                                                   */
/* RC = 0  - Processing successful.                                  */
/* RC = 4  - Processing completed, but a warning message has been    */
/*           issued.                                                 */
/* RC = 8  - The input, output, or sysout data set could not be      */
/*           used.                                                   */
/* RC = 12 - An error occurred during the processing of the PRINTDS  */
/*           command.                                                */
/* RC = 16 - The installation exit requested termination of the      */
/*           PRINTDS command.                                        */
/*                                                                   */
/* ----------------------------------------------------------------- */

OUTDES_PRINTDS_SUBROUTINE: PROC 1 PAGE_SEPARATOR
  ISPEXEC VGET (CPYCOUNT,FORMDEF,FORMNAME,PAGEDEF,CLASS,XWTR,+
                TITLE,DESTID,NAME,ROOM,BLDG,DEPT,+
                ADDR1,ADDR2,ADDR3,ADDR4,PRTMSG) ASIS

  ISPEXEC CONTROL ERRORS RETURN
  SET &ERRORSW = OFF

  IF &PRTMSG = &STR() THEN +
    SET &PRTMSG = &STR(&SYSXSMF..&SYSUID)

  SELECT (&PAGE_SEPARATOR)
    WHEN (N) +
      DO
        ISPEXEC CONTROL NONDISPL ENTER
        ISPEXEC DISPLAY PANEL(SCH0P150)
        SET &RC = &LASTCC
      END
    OTHERWISE +
      DO
        ISPEXEC DISPLAY PANEL(SCH0P150)
        SET &RC = &LASTCC
      END
  END

  IF &RC = 0 THEN +
    DO WHILE (&OUTDES_PRINTDS_RC ¬= 0)
      CONTROL ASIS MSG

      ERROR +
        DO
          SET &OUTDES_PRINTDS_RC = &LASTCC
          IF &OUTDES_PRINTDS_RC > 0 THEN +
            SET &ERRORSW = ON
          RETURN
        END
      &NRSTR(OUTDES DCFOUDES REUSE NODEFAULT +
             &SCHCOPY +
             &SCHFORMD +
             &SCHFORMS +
             &SCHPAGED +
             &SCHCLASS +
             &SCHXWTR +
             &SCHTITLE +
             &SCHDEST +
             &SCHNAME +
             &SCHROOM +
             &SCHBLDG +
             &SCHDEPT +
             &SCHADDR +
             &SCHNOTFY)

      PRINTDS DSN('&SCRIPT_OUTPUT_FILE(&LISTID)') OUTDES(DCFOUDES)
      ERROR OFF

      IF &ERRORSW = ON THEN +
        DO
          ISPEXEC DISPLAY CURSOR(CPYCOUNT) MSG(SCHP000O)
          SET &RC = &LASTCC
          IF &RC >= 8 THEN +
            DO
              CONTROL CAPS
              RETURN CODE(&RC)
            END
          ELSE +
            SET &ERRORSW = OFF
        END
      ELSE +
        DO
          CONTROL CAPS NOMSG
          RETURN CODE(&RC)
        END
    END
  RETURN CODE(&RC)
END

/* ----------------------------------------------------------------- */
/* EXIT CLIST with appropriate message...                            */
/* ----------------------------------------------------------------- */

CLEANUP_AND_EXIT_CLIST: +
  CONTROL NOMSG
  FREE FI(SCRPTFIL,SCRPTFNT,SCRPTLIB,SCRPTPRO,SCRPTSEG,TEXTLIB)
  IF &MSG = &STR() THEN +
    SET &MSG = SCHP000
  ISPEXEC VPUT (ZFPRFC) SHARED
  ISPEXEC SETMSG MSG(&MSG)
  END

./ ADD NAME=SCH0P120                               13AUG93 18.34.21
PROC 0 DEBUG

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Rewritten 14Oct92, William J. Smith, OSVS Technical Services      */
/*                                                                   */
/* ----------------------------------------------------------------- */
/*                                                                   */
/* The following CLIST is a modified version of ADMUCIMT as shipped  */
/* by IBM with GDDM.                                                 */
/*                                                                   */
/* Ref:  "GDDM Installation and System Management", GC33-0321-04,    */
/*       Appendix O, "Listings of user CLISTs and JCL", pages 383-   */
/*       386.                                                        */
/*                                                                   */
/* Rev:  GDDM Version 2.3                                            */
/*                                                                   */
/* ----------------------------------------------------------------- */

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Debug Code                                                        */
/*                                                                   */
/* ----------------------------------------------------------------- */

ISPEXEC VGET (DEBUG) ASIS
IF &DEBUG = DEBUG THEN +
  CONTROL MAIN MSG LIST CONLIST SYMLIST
ELSE +
  CONTROL MAIN NOMSG NOLIST NOCONLIST NOSYMLIST

/*-------------------------------------------------------------------*/

ISPEXEC VGET (CHDNAME,CHFNAME,ICUDATA,ICUFORM,CHWIDTH,CHDEPTH) SHARED

ISPEXEC CONTROL ERRORS RETURN
SET &ERRORSW = OFF

ISPEXEC DISPLAY PANEL(SCH0P120)
SET &RC= &LASTCC

/*-------------------------------------------------------------------*/
/*                                                                   */
/* Verify that the FORMAT and DATA ICU component data set names      */
/* specified by the user on the conversion panel exist.              */
/*                                                                   */
/* If either of the data set names do not exist, redisplay the       */
/* panel with an appropriate error message.                          */
/*                                                                   */
/*-------------------------------------------------------------------*/

DO WHILE (&RC = 0)

  DO WHILE &SYSDSN(&CHDNAME) ¬= &STR(OK)
    ISPEXEC DISPLAY MSG(SCHP000A) CURSOR(CHDNAME)
    SET &RC = &LASTCC
    IF &RC > 0 THEN +
      EXIT
  END

  DO WHILE &SYSDSN(&CHFNAME) ¬= &STR(OK)
    ISPEXEC DISPLAY MSG(SCHP000A) CURSOR(CHFNAME)
    SET &RC = &LASTCC
    IF &RC > 0 THEN +
      EXIT
  END

  DO WHILE &SYSDSN(&CHDNAME(&ICUDATA)) ¬= &STR(OK)
    ISPEXEC DISPLAY MSG(SCHP000A) CURSOR(ICUDATA)
    SET &RC = &LASTCC
    IF &RC > 0 THEN +
      EXIT
  END

  DO WHILE &SYSDSN(&CHFNAME(&ICUFORM)) ¬= &STR(OK)
    ISPEXEC DISPLAY MSG(SCHP000A) CURSOR(ICUFORM)
    SET &RC = &LASTCC
    IF &RC > 0 THEN +
      EXIT
  END

/*-------------------------------------------------------------------*/
/*                                                                   */
/* Allocate both of the user's libraries only if their existence is  */
/* assured.  Don't waste processing time allocating one data set     */
/* only to find that the other doesn't exist.                        */
/*                                                                   */
/*-------------------------------------------------------------------*/

  ALLOC F(ADMCDATA) DA(&CHDNAME) SHR REUSE
  SET &RC = &LASTCC
  IF &RC > 0 THEN +
    DO
      ISPEXEC SETMSG MSG(SCHP000D)
      ISPEXEC DISPLAY PANEL(SCH0P166)
    END

  ALLOC F(ADMCFORM) DA(&CHFNAME) SHR REUSE
  SET &RC = &LASTCC
  IF &RC > 0 THEN +
    DO
      ISPEXEC SETMSG MSG(SCHP000D)
      ISPEXEC DISPLAY PANEL(SCH0P166)
    END

/*-------------------------------------------------------------------*/
/*                                                                   */
/* Allocate the IBM-supplied GDDM symbol sets if the allocation of   */
/* requested user's GDDM data sets was successful.                   */
/*                                                                   */
/*-------------------------------------------------------------------*/

  ALLOC F(ADMSYMBL) DA('SYS0.GDDM.GDDMSYM') SHR REUSE

/*-------------------------------------------------------------------*/
/*                                                                   */
/* The following parameters are in order of appearance as passed to  */
/* ADMUCDSO.  Options DT and DS have been changed from the values    */
/* specified within the IBM-supplied version of ADMUCIMT.            */
/*                                                                   */
/* The use of an asterisk in parameter F will cause a file of the    */
/* following format to be generated:  userid.&CHARTDATA.ADMIMAGE     */
/*                                                                   */
/*-------------------------------------------------------------------*/

                                        /* ------------------------- */
  SET &CHARTDATA = &ICUDATA             /* Chart & Format Name as    */
  SET &FORMAT = &ICUFORM                /*   passed by the user for  */
                                        /*   GDDM conversion.        */
                                        /* ------------------------- */
  SET &CD = &STR(&CHARTDATA)            /* ICU Chart Data Name       */
  SET &CF = &STR(&FORMAT)               /* ICU Chart Format Name     */
  SET &DP = 6                           /* Chart Display Option      */
  SET &DT = IMG240X                     /* 3800-3/3820 device token  */
  SET &DS = 1                           /* Data stream type = PSEG   */
  SET &SP = 0                           /* Internal data in spill    */
  SET &N  = 8                           /* No. of swathes for image  */
  SET &W  = &CHWIDTH                    /* Image Width (0) Tenths/In */
  SET &D  = &CHDEPTH                    /* Image Depth  "  "     /"  */
  SET &U  = 0                           /* Units tenths of an inch   */
  SET &FO = 1                           /* Image Format 1 is for PSF */
  SET &F  = &STR(&CHARTDATA..*)         /* Output File Identifier    */
                                        /* ------------------------- */

  CONTROL MSG
  ERROR +
    DO
      SET &ADMUCDSO_RC = &LASTCC
      IF &ADMUCDSO_RC > 0 THEN +
        SET &ERRORSW = ON
      RETURN
    END

  CALL 'SYS2.GDDM.LINKLIB(ADMUCDSO)' +
       '&CD &CF &DP 4 &DT (5 &DS 6 &SP 7 &N 8 &W &D &U 9 &FO ) ( &F )'

  ERROR OFF

  IF &ERRORSW = ON THEN +
    DO
      ISPEXEC DISPLAY CURSOR(CHDNAME) MSG(SCHP000E)
      SET &RC = &LASTCC
      IF &RC >0 THEN +
        EXIT
      ELSE +
        SET &ERRORSW = OFF
    END
  ELSE +
    DO
      CONTROL NOMSG
      ISPEXEC DISPLAY CURSOR(CHDNAME) MSG(SCHP000F)
      SET &RC = &LASTCC
      IF &RC >0 THEN +
        EXIT
    END

END
./ ADD NAME=SCH0P130                               13AUG93 18.34.21
 PROC 1 CDPUDATA DEBUG

/**********************************************************************/
/*                                                                    */
/*  GDDM/MVS (2.2)                                                    */
/*  5665-356   (C) COPYRIGHT IBM CORP.  1979, 1988                    */
/*  ALL RIGHTS RESERVED                                               */
/*  LICENSED MATERIAL - PROGRAM PROPERTY OF IBM                       */
/*                                                                    */
/**********************************************************************/
/*                                                                    */
/*  Updated 12Sep89, William Smith, Charles Schwab Technical Services */
/*    . Ref:  "GDDM 2.2 Base Programming Reference Volume 2",         */
/*      SC33-0332-1, p. 57-58.                                        */
/*    . The original source for this CLIST was the IBM-supplied       */
/*      version in the GDDMSAM library (ADMUBCDT).                    */
/*    . Eliminated the requirement for external allocation of an OS   */
/*      DDNAME to a data set to be browsed.                           */
/*    . This change allows a data set name to be specified rather     */
/*      than a DDNAME as the documentation indicates below.           */
/*                                                                    */
/**********************************************************************/
/*                                                                    */
/* PROGRAM NAME - ADMUBCDT CLIST                                      */
/*                                                                    */
/* DESCRIPTIVE NAME - Browse Composite Document files on a terminal.  */
/*                                                                    */
/* FUNCTION - To browse LIST38xx, PSEG38xx, OVLY38xx, LIST4250,       */
/*            PSEG4250, OVLY4250, LISTAPA, LISTCDP and ADMIMAGE       */
/*            files on a terminal.                                    */
/*                                                                    */
/* ENTRY CONDITIONS - This CLIST is invoked with CDFILE assumed to    */
/*                    be the DDNAME of the Composed Document file     */
/*                                                                    */
/* EXIT-NORMAL RETURN CODES -                                         */
/*                                                                    */
/*     0   When the file has been displayed correctly                 */
/*                                                                    */
/* EXIT-ERROR RETURN CODES -                                          */
/*                                                                    */
/*     Any return code other than 0 would indicate an error           */
/*     which will be explained in an accompanying message.            */
/*                                                                    */
/* NOTES - None                                                       */
/*                                                                    */
/**********************************************************************/

/*-------------------------------------------------------------------*/
/* Debug Code                                                        */
/*-------------------------------------------------------------------*/

 ISPEXEC VGET (DEBUG) ASIS
 IF &DEBUG = DEBUG THEN +
   CONTROL MAIN MSG LIST CONLIST SYMLIST
 ELSE +
   CONTROL MAIN NOMSG NOLIST NOCONLIST NOSYMLIST

/* ----------------------------------------------------------------- */
/* Set default parameters for ADM4CDUT                               */
/* ----------------------------------------------------------------- */

 SET &COPIES = 1                   /* Number of copies               */
 SET &DUPLEX = 1                   /* 1 = Simplex                    */
                                   /* 2 = Normal Duplex              */
                                   /* 3 = Tumble Duplex              */
 SET &PROCOPTS =                   /* Null Procopts                  */
 SET &DEVTOK = &STR(*)             /* Device Token                   */
 SET &FAMILY = 1                   /* Family                         */
 SET &NAMELIST =                   /* Null Namelist                  */

 SET &CDFILE = CDFILE              /* DDNAME of allocated CDPU file  */

 ALLOC F(&CDFILE) DA(&CDPUDATA) SHR REUSE

/* ----------------------------------------------------------------- */
/* Start the GDDM Composite Document Print Utility                   */
/* ----------------------------------------------------------------- */

 CALL 'SYS2.GDDM.LINKLIB(ADM4CDUT)' +
     '(&CDFILE)(&COPIES &DUPLEX)(&NAMELIST)(&PROCOPTS) +
      (&DEVTOK)(&FAMILY)'
 FREE F(CDFILE)
 EXIT
./ ADD NAME=SCH0P140                               13AUG93 18.34.21
/* ----------------------------- REXX ------------------------------ */
/*                                                                   */
/* Written 29Oct92, Willliam J. Smith, OSVS Technical Services       */
/*                                                                   */
/* The following REXX EXEC was written to overcome an architectural  */
/* shortcoming in the CLIST language which doesn't allow arithmetic  */
/* operations on the &SYSCPU and &SYSSRV builtin functions.          */
/*                                                                   */
/* This EXEC will calculate the CPU and Service Units time consumed  */
/* during the execution of the SCRIPT TSO command processor while    */
/* formatting a document.                                            */
/*                                                                   */
/* The results of the operations are VPUT in the SHARED pool and are */
/* displayed on SCH0P140 in fields &C and &S on the panel.           */
/*                                                                   */
/* ----------------------------------------------------------------- */
arg cpu_after cpu_before srv_after srv_before
dcfcpu = cpu_after - cpu_before
dcfsrv = srv_after - srv_before
address ispexec 'VPUT (DCFCPU DCFSRV) SHARED'
./ ADD NAME=SCRIPTIT                               13AUG93 18.34.21
ISREDIT MACRO (SKILL CDPU DEBUG) PROCESS
/* ----------------------------------------------------------------- */
/*                                                                   */
/* "SCRIPTIT" ISPF EDIT MACRO                                        */
/*                                                                   */
/*  - Updated 18Dec92, William Smith, Charles Schwab & Co., Inc.     */
/*    . With the installation of APAR PN21493 and its superceding    */
/*      PTF UN29299, IBM formally supports use of the Core Inter-    */
/*      change Fonts by Script/VS with the AFPxxxx logical device    */
/*      family.                                                      */
/*    . This application has been changed to use device AFP rather   */
/*      than device 3820;  SCRIPTIT will have a "virtual" device     */
/*      independence across multiple printer resolutions using the   */
/*      scalable Core Interchange Font technology.                   */
/*    . Corrected the logic by which SKILL and CDPU (both are posi-  */
/*      tional NOT keyword parameters !) to properly handle the case */
/*      where SKILL was specified as something other than A or B and */
/*      CDPU was specified as something other than YES or NO.        */
/*                                                                   */
/*  - Updated 19Sep92, William Smith, Charles Schwab & Co., Inc.     */
/*    . Updated to support a complete re-write of the DCF Script/VS  */
/*      dialog.  Virtually every panel was changed for uniformity,   */
/*      consistency, and correct mode of operation.                  */
/*    . Major new function was added in CLIST SCH0P110 to exploit    */
/*      MVS/ESA 4.2.2, JES2/4.2, PSF/2.1, DCF Script/VS 1.4.0,       */
/*      and TSO/E 2.3.1.                                             */
/*                                                                   */
/*  - Updated 12Sep90, William Smith, Charles Schwab & Co., Inc.     */
/*    . Rewritten - corrected use of all set statements to use       */
/*      variables that match the .ZVARS parameters in the 3820       */
/*      panel SCHP3820.                                              */
/*    . Added a macro parameter which allows the user to chose       */
/*      skill level (beginner or advanced).                          */
/*    . Added a macro parameter which allows the user to invoke      */
/*      GDDM's "CDPU" to browse DCF output on a graphics terminal.   */
/*                                                                   */
/*  - Updated 11Sep90, William Smith, Charles Schwab & Co., Inc.     */
/*    . Original author unknown; written around 1987 (?).            */
/*    . Substantially updated and organized for ease of maintenance. */
/*    . This EDIT macro provides the DCF Script/VS user a means      */
/*      by which to format the data set currently being edited for   */
/*      output processing on a 3820.                                 */
/*    . The macro drive the in-house developed DCF Script/VS         */
/*      foreground ISPF dialog.                                      */
/*                                                                   */
/* ----------------------------------------------------------------- */

IF &DEBUG = DEBUG THEN +
  CONTROL MSG FLUSH CONLIST SYMLIST LIST
ELSE +
  CONTROL PROMPT MSG NOFLUSH NOCONLIST NOSYMLIST

ISREDIT (STATUS) = USER_STATE          /* Save state of EDIT VARS    */
ISREDIT SAVE                           /* Save data set being edited */
ISREDIT (ZDS) = DATASET                /* Get name of in-use dsname  */
ISREDIT (ZMEM) = MEMBER                /* Get in-use member name     */

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Provide a means to specify the level of expertise for fast-path   */
/* (beginner) or experienced (advanced) user of Script/VS.           */
/*                                                                   */
/* Any positional parameter other than B or a NULL value will force  */
/* use of ADVANCED mode of the S.14 device-specific processing       */
/* panel (SCHPAFP/SVAR=9/logical device=AFPA).                       */
/*                                                                   */
/* ----------------------------------------------------------------- */

SELECT (&SKILL)
  WHEN (&STR() OR B) +
    SET &SKILL = B
  OTHERWISE +
    SET &SKILL = A
END

/* ----------------------------------------------------------------- */
/*                                                                   */
/* For the AFP user who has a graphics capable display device,       */
/* provide a parameter that allows the user to format a document     */
/* that is being edited and view it "WYSIWYG" on the terminal.       */
/*                                                                   */
/* Any positional parameter other than NO or a NULL value will force */
/* a WYSWIG display of the document upon the terminal.               */
/*                                                                   */
/* ----------------------------------------------------------------- */

SELECT (&CDPU)
  WHEN (&STR() OR NO) +
    SET &CDPU = NO
  OTHERWISE +
    SET &CDPU = YES
END

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Refer to the ISPF and ISPF/PDF Planning and Customization Guide   */
/* under topic "Foreground and Batch Customizing" for details on     */
/* the variables associated with the use of ISRFPR.                  */
/*                                                                   */
/* ----------------------------------------------------------------- */

SET &PRJ1    =                         /* Explicitly req'd by ISRFPR */
SET &LIB1    =                         /* "          "     "  "      */
SET &TYP1    =                         /* "          "     "  "      */
SET &ZORG    = PO                      /* Input data set organizat'n */
SET &ZDSQ    = &ZDS                    /* Used by ISRFPR             */
SET &ZDSQMEM = &ZDSQ(&ZMEM)            /* Explicitly req'd by ISRFPR */

/* ----------------------------------------------------------------- */
/*                                                                   */
/* Refer to the ".ZVARS" parameter in the )INIT section of SCHP3820  */
/* and CLIST SCH0P110 to obtain details on the following variables   */
/* and their usage.                                                  */
/*                                                                   */
/* With additional logic changes, it would be very easy to allow the */
/* user to specify the output device type.  The CLIST would then set */
/* default values for each of the variables and invoke DCF Script/VS */
/* processing accordingly.  For example:  SCRIPTIT 3816 A            */
/*                                                                   */
/* ----------------------------------------------------------------- */

SET &SVAR    = 9                       /* Output device type setting */
SET &RDEV    = AFP                     /* "      "      "    is AFP  */


SET &BDO9    =                         /* BIND odd-page offset       */
SET &BDE9    =                         /* BIND even-page offset      */
SET &SEP9    = Y                       /* Change output separators   */
SET &CH19    =                         /* CHARS parameter # 1        */
SET &CH29    =                         /* "     "         # 2        */
SET &CH39    =                         /* "     "         # 3        */
SET &CH49    =                         /* "     "         # 4        */
SET &DUP9    = N                       /* Duplex output = NO         */
SET &MS19    = T                       /* Messages 1 = Terminal      */
SET &MS29    = N                       /* Messages 2 = No trace      */
SET &ROT9    =                         /* Page size alternatives     */
SET &SPL9    = N                       /* Spelling check = NO        */
SET &UPC9    = N                       /* Upper case only = NO       */
SET &GDDM9   = N                       /* Invoke GDDM graphics = NO  */
SET &PSG9    = N                       /* Create page segments = NO  */
SET &SPM9    =                         /* Separation master numbers  */
SET &SGL9    =                         /* Page segment lib dsname    */
SET &PRO9    =                         /* Profile data set name      */
SET &SLB9    =                         /* Script MACLIB dsname       */
SET &OSO9    =                         /* Other SCRIPT options       */

ISPEXEC VPUT (PRJ1,LIB1,TYP1,ZORG,ZDS,ZDSQ,ZDSQMEM,ZMEM,+
              SVAR,RDEV,SKILL,+
              BDO9,BDE9,SEP9,CH19,CH29,CH39,CH49,+
              DUP9,MS19,MS29,ROT9,SPL9,UPC9,GDDM9,+
              PSG9,SPM9,SGL9,PRO9,SLB9,OSO9,+
              CDPU,DEBUG) SHARED

ISPEXEC LIBDEF ISPMLIB DATASET ID('SYS0.SCRIPT.MLIB')
ISPEXEC LIBDEF ISPPLIB DATASET ID('SYS0.SCRIPT.PLIB')
ALTLIB  ACT APPLICATION(CLIST) DATASET('SYS0.SCRIPT.CLIST')

%SCH0P110

ISPEXEC LIBDEF ISPMLIB DATASET
ISPEXEC LIBDEF ISPPLIB DATASET
ALTLIB  DEACT APPLICATION(CLIST)

ISREDIT USER_STATE = (STATUS)
EXIT
