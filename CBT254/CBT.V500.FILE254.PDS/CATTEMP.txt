/* REXX EXEC to build CATALOG entries */
true=1; false=0;
"FREE F(SYSIN SYSPRINT IAMPRINT OUTPUT)";
"ALLOC F(SYSIN) NEW DELETE TRACK SPACE(1 1)";
"ALLOC F(SYSPRINT) NEW DELETE TRACK SPACE(10 5)";
"ALLOC F(IAMPRINT) DUMMY";
"ALLOC F(OUTPUT) DA('TECH110.USER.CNTLLIB(CATOUT)') SHR";
SYSIN.1 = " LISTC UCAT ALL";
"EXECIO * DISKW SYSIN (FINIS STEM SYSIN.";
"CALL 'SYS1.LINKLIB(IDCAMS)'";
"EXECIO * DISKR SYSPRINT (FINIS STEM OUTPUT.";
inputctr = 0;
do index = 1 to output.0;
     parse var output.index cc 2 ty 3 trail ;
     select;
       when cc=" " then ,
        input.inputctr = input.inputctr strip(substr(output.index,2));
       when cc="0" then do;
           if ty=" " & trail¬=" " then ,
        input.inputctr = input.inputctr strip(substr(output.index,2));
        else do;
          if substr(output.index,2,5)¬="ALIAS" & ,
             substr(output.index,2,7)¬="CLUSTER" & ,
             substr(output.index,2,11)¬="USERCATALOG" & ,
             substr(output.index,2,7)¬="NONVSAM" then iterate index;
          inputctr = inputctr+1;
          input.inputctr = strip(substr(output.index,2));
          end;
       end;
       otherwise nop;
     end;
end;
idcctr = 1;
hlqlist="";
uctlist="";
do index = 1 to inputctr;
     type = word(input.index,1); /* alias, nvsam , cl */
     select;
        when type="ALIAS" then do;
           typename=word(input.index,3);
           aucat = pos(" USERCAT--",input.index);
           anvsm = pos(" NONVSAM--",input.index);
           if aucat¬=0 then do;
                assocname=word(substr(input.index,aucat+10),1);
                if wordpos(assocname,uctlist)=0 then ,
                   uctlist = uctlist assocname;
                end;
            else ,
                assocname=word(substr(input.index,anvsm+10),1);
           idc.idcctr=" DEFINE ALIAS (NAME("typename") -";
           idcctr = idcctr +1;
           idc.idcctr="                REL("assocname"))";
           idcctr = idcctr +1;
           end;
        when type="NONVSAM" then do;
           typename=word(input.index,3);
           parse var typename hlq "." . ;
           hlq=strip(hlq);
           if wordpos(hlq,hlqlist)¬=0 then iterate index;
             else hlqlist = hlqlist hlq;
           end;
        when type="USERCATALOG" then do;
           typename=word(input.index,3);
           aucat = pos(" VOLSER--",input.index);
           if aucat¬=0 then do;
                volname=word(substr(input.index,aucat+18),1);
                end;
           idc.idcctr=" IMPORT CONNECT OBJECTS -";
           idcctr = idcctr +1;
           idc.idcctr=" (("typename "DEVT(3390) -";
           idcctr = idcctr +1;
           idc.idcctr=" VOLUMES("volname"))) -";
           idcctr = idcctr +1;
           idc.idcctr=" CAT(SYS1.MCATSA.CLUSTER)";
           idcctr = idcctr +1;
           idc.idcctr=" IMPORT CONNECT OBJECTS -";
           idcctr = idcctr +1;
           idc.idcctr=" (("typename "DEVT(3390) -";
           idcctr = idcctr +1;
           idc.idcctr=" VOLUMES("volname"))) -";
           idcctr = idcctr +1;
           idc.idcctr=" CAT(SYS1.MCATSB.CLUSTER)";
           idcctr = idcctr +1;
           end;
        otherwise nop;
     end;
end;
hlqctr = words(hlqlist);
do index=1 to hlqctr;
     hlqname=word(hlqlist,index);
     idc.idcctr=" /* NONVSAM HLQ:" left(hlqname,9) "*/";
     idcctr = idcctr + 1;
end;
uctctr = words(uctlist);
do index=1 to uctctr;
     uctname=word(uctlist,index);
     idc.idcctr=" /* Usercatalog:" left(uctname,45) "*/";
     idcctr = idcctr + 1;
end;
"EXECIO * DISKW OUTPUT (FINIS STEM IDC.";
"FREE F(SYSIN SYSPRINT IAMPRINT OUTPUT)";
"ALLOC F(SYSIN) DA(*)";
"ALLOC F(SYSPRINT) DA(*)";
