/* REXX RockRoll roll groups of datasets and archive oldest gener */
address "TSO";
true = "1"; false = "0";
/* Acquire ddnames and datasetnames that we are to rock and roll */
dummy = outtrap("listalc."); /* trap lista output */
"LISTALC STATUS"; /* find dsnames per ddname */
response = "";
ddnamelist = "";
do  i = 2 to listalc.0;
       response = response strip(listalc.i); /* remove filler */
end;
counter = words(response); /* loop */
do i = 1 to counter;
     element = word(response,i);
     if element = "TERMFILE" then do;
           ddname = word(response,i+1);
           ddnamelist = ddnamelist ddname;
           ddname.ddname = "TERMFILE";
           lastddname = ddname; /* save ddname for concat */
           i = i + 1; /* skip */
           end;
     else if element = "NULLFILE" then do;
           ddname = word(response,i+1);
           ddnamelist = ddnamelist ddname;
           ddname.ddname = "NULLFILE";
           lastddname = ddname; /* save ddname for concat */
           i = i + 1; /* skip */
           end;
        else do;
           dsname = element;
           if substr(dsname,1,1)="*" then ,
                 dsname = substr(dsname,2); /* remove notinuse flag*/
           ddname = word(response,i+1);
           if substr(ddname,1,4)="KEEP" | substr(ddname,1,6)="DELETE" ,
               then do;
                    ddname.lastddname = ddname.lastddname dsname;
                    i = i + 1;
               end;
               else do;
                    ddnamelist = ddnamelist ddname;
                    ddname.ddname = dsname;
                    lastddname = ddname; /* save ddname for concat */
                    i = i + 2;
               end;
        end;
end;
ddname# = words(ddnamelist); /* sort ddnames found */
do i = 1 to ddname#;
    ddname.i = word(ddnamelist,i);
end;
swapped = true;
do while(swapped);
swapped = false;
do i = 1 to ddname#-1;
   this = i;
   next = i+1;
   if ddname.this > ddname.next then do;
        temp = ddname.next; /* swap */
        ddname.next = ddname.this;
        ddname.this = temp;
        swapped = true;
        end;
end;
end;
do i = 1 to ddname#;
  trueddn = ddname.i;
  say left(trueddn,8) word(ddname.trueddn,1);
  dsncount = words(ddname.trueddn);
  if dsncount>1 then do j = 2 to dsncount;
     say "         "word(ddname.trueddn,j);
     end;
end;
exit 0;
