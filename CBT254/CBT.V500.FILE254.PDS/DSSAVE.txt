//JEFFKZ   JOB (530000000000),'TECH.JEFF.KAPLAN',
// CLASS=S,MSGCLASS=G,MSGLEVEL=(1,1),NOTIFY=JEFFK,REGION=6M
//S EXEC ASMHCL,PARM.L='XREF,LIST,RENT,REUS'
//C.SYSLIB DD DSN=SYS1.MACLIB,DISP=SHR
//         DD DSN=SYS1.MODGEN,DISP=SHR
//         DD DSN=SYS1.AMACLIB.SMPE5,DISP=SHR
//C.SYSIN  DD *
Dsserv   TITLE 'Dataset Services TSO/E Command'
DSSERV   CSECT , /* Dataset Services */
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
K0       EQU   0
K1       EQU   1
K2       EQU   2
K3       EQU   3
K4       EQU   4
K5       EQU   5
K6       EQU   6
K7       EQU   7
K8       EQU   8
K9       EQU   9
K10      EQU   10
K11      EQU   11
K12      EQU   12
K13      EQU   13
K14      EQU   14
K15      EQU   15
K16      EQU   16
K17      EQU   17
K18      EQU   18
K19      EQU   19
K20      EQU   20
K24      EQU   24
K28      EQU   28
K32      EQU   32
K36      EQU   36
K40      EQU   40
K44      EQU   44
K48      EQU   48
K52      EQU   52
K56      EQU   56
K59      EQU   59
K60      EQU   60
K64      EQU   64
K68      EQU   68
K72      EQU   72
K76      EQU   76
K80      EQU   80
K84      EQU   84
K88      EQU   88
K92      EQU   92
K96      EQU   96
K100     EQU   100
K128     EQU   128
K255     EQU   255
K256     EQU   256
K512     EQU   512
K4095    EQU   4095
K32760   EQU   32760
X80      EQU   X'80'
X40      EQU   X'40'
X20      EQU   X'20'
X10      EQU   X'10'
X08      EQU   X'08'
X04      EQU   X'04'
X02      EQU   X'02'
X01      EQU   X'01'
X7F      EQU   X'7F'
XFF      EQU   X'FF'
DSSERV   AMODE 31
DSSERV   RMODE 24
         SAVE  (14,12),,DSSERV*&SYSDATE*&SYSTIME
         LR    R3,R1 /* Cppl */
         USING CPPL,R3
         LR    RA,RF /* Addressability */
         USING DSSERV,RA,RB,RC
         LA    RB,K4095(,RA)
         LA    RB,K1(,RB)
         LA    RC,K4095(,RB)
         LA    RC,K1(,RC)
         LR    R2,RD /*SAVEAREA HOUSEKEEPING*/
         SPLEVEL SET=2 , /* MVS/XA EXPANSION */
         LA    R0,DWORDS
         GETMAIN R,LV=(0),LOC=BELOW
         LR    RD,R1
         ST    RD,K8(,R2)
         ST    R2,K4(,RD)
         USING DSRVWMAP,RD
         MVC   IOSVPARM(K12),IOSVSKEL /* IOSVSUCB parameter */
         LA    RF,VSUCBRET /* Position in parm list */
         ST    RF,IOSVPARM ... /* result ptr */
         LA    RF,IOSVUCBR /* Position in parm list */
         STCM  RF,K7,IOSVPARM+K9 ... /* UCB addr return ptr */
         XC    VSUCBRET(K100),VSUCBRET /* Clear IOSVSUCB area */
         MVC   ISPLINKL(CALLISTL),CALLIST /* Reentrant call list */
         CALL  ISPQRY , /* Verify ISPF active */
         LTR   RF,RF ...
         BNZ   ISPINACT /* ISPF inactive */
         LA    R1,ISPLINKL /* Call list */
         CALL  ISPLINK,(VDEFINE,TABLCOLS,VARIABLE,VFORMAT,VLENGTH,LIST)X
               ,MF=(E,(1)),VL , /* Function variable pool table cols */
         LTR   RF,RF ...
         BNZ   FVDEF /* Variable define failed */
         LA    R1,ISPLINKL /* Call list */
         CALL  ISPLINK,(VDEFINE,SYSNAMES,SYSVARS,ZFORMAT,ZLENGTH,LIST),X
               MF=(E,(1)),VL , /* Function variable pool sys z vars */
         LTR   RF,RF ...
         BNZ   FVDEF /* Variable define failed */
         CALL  ISPLINK,(TBCREATE,TABLNAME,NULL,TABLCOLS,NOWRITE,REPLACEX
               ),VL
         CLM   RF,K1,=AL1(K4) /* Rc = 0 or 4 is normal */
         BH    FTBCREAT /* Table creation failed */
         CALL  ISPLINK,(TBSORT,TABLNAME,SORTLIST),VL /* Sort table */
         LTR   RF,RF ...
         BNZ   FTBSORT /* Table sort failed */
         USING PSA,R0 /* Map PSA */
         L     R4,FLCCVT /* CVT pointer */
         DROP  R0
         USING CVT,R4 /* Map CVT */
         L     RF,CVTUCBSC /* IOSVSUCB entry pointer */
         ST    RF,VSUCBEP /* Save */
         L     R4,CVTCBSP /* AMCBS pointer */
         DROP  R4
         USING AMCBS,R4 /* Map SP 245 key 0 */
         L     R4,CBSCAXCN /* Pointer to CAXWA chain */
         DROP  R4
         USING IGGCAXWA,R4 /* Map Catalog Work Area */
LOOKMCAT TM    CAXFLGS,CAXMCT /* Master catalog entry */
         BO    LOCMCAT /* Yes */
         ICM   R4,K15,CAXCHN /* Scan active catalog chain */
         BNZ   LOOKMCAT /* Loop */
         B     LMCATERR /* Master catalog not located ? */
LOCMCAT  MVC   MCATDSN(L'MCATDSN),CAXCNAM /* Save catalog name */
         MVC   MCATACB,CAXACB /* Save master catalog ACB ptr */
         L     R4,CAXUCB /* Map UCB of mcat volume */
         DROP  R4
         USING UCBOB,R4
         MVC   MCATVSER(L'MCATVSER),UCBVOLI /* Save DASD volume ser */
         DROP  R4
         MVC   VCMD(K9),=CL59' ' /* Init */
         MVC   VENTRY(K9),=CL9'Mstrcatlg' /* Build mcat line */
         MVC   VNAME(K59),=CL59' ' /* Init */
         XC    VDATA,VDATA /* Init */
         MVC   VNAME(L'MCATDSN),MCATDSN ...
         MVI   VKEY,C'1'     /* Sort code */
         MVC   VKEY+K1(L'MCATDSN),MCATDSN ...
         MVC   VKEY+K1+L'MCATDSN(K3),=CL3'000'
         CALL  ISPLINK,(TBADD,TABLNAME,NULL,ORDER,MULTROWS),VL
         LTR   RF,RF ...
         BNZ   FTBADD /* Table addition failed */
IOSVSCAN LA    R1,IOSVPARM /* Input param for IOSVSUCB */
         L     RF,VSUCBEP /* Routine ptr */
         BALR  RE,RF ... /* DASD scan */
         LTR   RF,RF /* Scan end */
         BNZ   ENDIOSV /* Yes */
         L     RF,IOSVUCBR /* Pickup UCB address */
         USING UCBOB,RF /* Map UCB */
         TM    UCBSTAT,UCBONLI /* DASD unit online */
         BZ    IOSVSCAN /* No */
         MVC   VCMD(K9),=CL59' ' /* Init */
         MVC   VENTRY(K9),=CL9'Volume' /* Build DASD line */
         MVC   VNAME(K59),=CL59' ' /* Init */
         XC    VDATA,VDATA /* Init */
         STCM  RF,K15,VDATA /* Save UCB address in hidden data */
         MVC   VNAME(L'UCBVOLI),UCBVOLI ...
         MVI   VKEY,C'3'     /* Sort code */
         MVC   VKEY+K1(L'MCATDSN),VNAME ...
         MVC   VKEY+K1+L'MCATDSN(K3),=CL3'000'
         DROP  RF
         CALL  ISPLINK,(TBADD,TABLNAME,NULL,ORDER,MULTROWS),VL
         LTR   RF,RF ...
         BNZ   FTBADD /* Table addition failed */
         B     IOSVSCAN /* Continue */
ENDIOSV  EQU   * *
         XC    SVC26PRM(CTGPLEN),SVC26PRM /* Clear cat parm */
         LA    R9,SVC26PRM /* Map parm */
         USING CTGPL,R9
         OI    CTGOPTN1,CTGNAME /* Ref dsect descript */
         OI    CTGOPTN2,CTGGTNXT+CTGBOTH
         OI    CTGOPTN3,CTGCMS+CTGAM0
         OI    CTGOPTNS,CTGLTCAT
         MVI   CTGTYPE,CTGTUCAT /* Ucat code */
         LA    R0,CTGENTP /* Req dsn area */
         ST    R0,CTGENT ...
         MVI   CTGENTP,C' ' /* Blank out entry dsn for first call */
         MVC   CTGENTP+K1(L'CTGENTP-K1),CTGENTP ...
         LA    R0,MCATACB /* Req cat area */
         ST    R0,CTGCAT ...
         XC    CATRESP,CATRESP /* Clear cat response area */
         LA    R0,L'CATRESP /* Tell sys len */
         STCM  R0,K3,CATRESP ...
         LA    R0,CATRESP /* Tell sys address */
         ST    R0,CTGWKA ...
         MVC   CTGFLS(CTGFLSPL),CTGFLSP /* Build needed field entries*/
         LA    RF,K4 /* Max # CTGFL entries */
         STC   RF,CTGNOFLD /* Tell sys */
         LA    RE,CTGFIELD /* CTGFL pointers */
         LA    R1,CTGFLS /* Actual CTGFLs */
         USING CTGFL,R1 /* Map */
CTGSETL  ST    R1,K0(,RE) /* Set a CTGFL ptr */
         SLR   R0,R0 /* Calc size of this CTGFL */
         IC    R0,CTGFLDNO /* # of variable len fields */
         SLL   R0,K3 /* Each var field is 8 bytes */
         ALR   R1,R0 /* Ptr to next CTGFL in stor */
         LA    R1,CTGFLL(,R1) /* Factor in fixed len */
         LA    RE,K4(,RE) /* Next CTGFL ptr loc */
         BCT   RF,CTGSETL /* Loop for all fields */
         DROP  R1
CATSCANU LA    R1,SVC26PRM /* Cat serv param */
         SVC   26 , /* Service */
         CLM   RF,K1,=AL1(K40) /* RC 40 return */
         BE    URC40 /* Ignore it */
         LTR   RF,RF /* Scan until end */
         BNZ   CUEND ...
         USING CTGFL,R4
URC40    LA    R4,RESPENTN /* Map entry name response */
         LM    R6,R7,CTGFLDAT /* Acquire len and address */
         MVC   VCMD(K9),=CL59' ' /* Init */
         MVC   VENTRY(K9),=CL9'Usercatlg' /* Build ucat line */
         MVC   VNAME(K59),=CL59' ' /* Init */
         XC    VDATA,VDATA /* Init */
         MVC   VNAME(K3),SEPR
         MVC   VNAME+K3(L'MCATDSN),K0(R7)  /* DSN for panel display */
         MVI   VKEY,C'2'     /* Sort code */
         MVC   VKEY+K1(L'MCATDSN),VNAME+K3 ...
         MVC   VKEY+K1+L'MCATDSN(K3),=CL3'000'
         MVC   CTGENTP(L'MCATDSN),K0(R7)  /* DSN for cat scan;up key*/
         SLR   RF,RF /* Add binary 1 to cat scan key */
         IC    RF,CTGENTP+L'MCATDSN-K1
         LA    RF,K1(,RF) ...
         STC   RF,CTGENTP+L'MCATDSN-K1
         CALL  ISPLINK,(TBADD,TABLNAME,NULL,ORDER,MULTROWS),VL
         LTR   RF,RF ...
         BNZ   FTBADD /* Table addition failed */
         B     CATSCANU /* Loop for all ucats */
         DROP  R4
CUEND    MVI   CTGENTP,C' ' /* Blank out entry dsn for first call */
         MVC   CTGENTP+K1(L'CTGENTP-K1),CTGENTP ...
         MVI   CTGTYPE,CTGTANM /* Alias code */
CATSCANX LA    R1,SVC26PRM /* Cat serv param */
         SVC   26 , /* Service */
         CLM   RF,K1,=AL1(K40) /* RC 40 return */
         BE    XRC40 /* Ignore it */
         LTR   RF,RF /* Scan until end */
         BNZ   CXEND ...
         USING CTGFL,R4
XRC40    LA    R4,RESPENTN /* Map entry name response */
         LM    R6,R7,CTGFLDAT /* Acquire len and address */
         MVC   CTGENTP(L'MCATDSN),K0(R7)  /* Als for cat scan;up key*/
         SLR   RF,RF /* Add binary 1 to cat scan key */
         IC    RF,CTGENTP+L'MCATDSN-K1
         LA    RF,K1(,RF) ...
         STC   RF,CTGENTP+L'MCATDSN-K1
         MVC   VCMD(K9),=CL59' ' /* Init */
         MVC   VENTRY(K9),=CL9'Alias' /* Build alias line */
         MVC   VNAME(K59),=CL59' ' /* Init */
         XC    VDATA,VDATA /* Init */
         MVC   VNAME+K3(K3),SEPR
         MVC   VNAME+K6(L'MCATDSN),K0(R7)  /*Als for panel display */
         TRT   VNAME+K6(L'MCATDSN+K1),LOOKBLNK /* Append .* */
         MVC   K0(K2,R1),=C'.*' ...
         MVI   VKEY,C'2'     /* Sort code */
         LA    R4,RESPNMDS /* Map true name response */
         LM    R6,R7,CTGFLDAT /* Acquire len and address */
         CLI   K0(R7),CTGTUCAT /* Ucat code */
         BNE   CATSCANX /* Skip over non-ucat alias assoc */
         MVC   VKEY+K1(L'MCATDSN),K1(R7) ...
         MVC   VKEY+K1+L'MCATDSN(K3),=CL3'001'
         CALL  ISPLINK,(TBADD,TABLNAME,NULL,ORDER,MULTROWS),VL
         LTR   RF,RF ...
         BNZ   FTBADD /* Table addition failed */
         B     CATSCANX /* Loop for all alias */
         DROP  R4
         DROP  R9
CXEND    CALL  ISPLINK,(TBTOP,TABLNAME),VL
         LTR   RF,RF ...
         BNZ   FTBTOP /* Table top post failed */
REDISPLY CALL  ISPLINK,(TBDISPL,TABLNAME,PANELID),VL
         CNOP  0,4
         B     *+K4(RF) /* index on return code */
         B     ZEROSEL /* Zero or one sel row and scroll may be pend */
         B     MULTSEL /* Multiple selected rows */
         B     DISPRET /* End selected; rows may have been selected */
         B     FTBDISPL /* Error */
         B     FTBDISPL /* Error */
         B     FTBDISPL /* Error */
ZEROSEL  EQU   * *
MULTSEL  EQU   * *
         ICM   R4,K15,UTDSELS /* How many rows selected */
         BZ    REDISPLY /* None */
PROCESS  EQU   * *
         CLI   VENTRY,C'M' /* Master cat */
         BE    PROCM ...
         CLI   VENTRY,C'U' /* User cat */
         BE    PROCU ...
         CLI   VENTRY,C'A' /* Alias entry */
         BE    PROCA ...
         CLI   VENTRY,C'V' /* Vol entry */
         BE    PROCV ...
PROCV    ICM   R7,K15,VDATA /* Volume row saved UCB ptr */
         MVC   CVPLPARM(CVPLNGTH),CVPL /* CVAF services prep */
         LA    R5,CVPLPARM /* Map CVPL */
         USING CVPLMAP,R5
         LA    R3,CVPLPBUF /* Buffer list */
         XC    CVPLPBUF(CVPLPBFL),CVPLPBUF /* Clear */
         LA    R6,IECSDSL1 /* F1 dscb */
         OI    BFLHFL,BFLHDSCB /* DSCBs to be read */
         MVI   BFLHNOE,X'01' /* Buflist entry count */
         LA    R0,DS1FMTID /* Addr DSCB buffer */
         ST    R0,BFLEBUF ...
         MVI   BFLELTH,DS1END-DS1FMTID /* 96 byte len */
         XC    DS1DSNAM,DS1DSNAM /* Start key */
         CVAFSEQ DSN=(R6),BUFLIST=(R3),UCB=(R7),MF=(E,CVPLPARM)
         CLI   DS1DSNAM,X'04' /* VTOC ? */
         BNE   VTOCNORM /* No */
  TPUT ERRORS,16
VTOCNORM EQU   * *
  TPUT ERRORS,15
         B     PROCEND /* Continue */
         DROP  R5
PROCM    EQU   * *
PROCU    EQU   * *
PROCA    EQU   * *
PROCEND  BCT   R4,PROCNXTR /* Process next selected row */
         B     REDISPLY /* Continue */
PROCNXTR CALL  ISPLINK,(TBDISPL,TABLNAME),VL
         B     PROCESS /* Loop processing all selected rows */
DISPRET  EQU   * *
         CALL  ISPLINK,(TBCLOSE,TABLNAME),VL /* Close open table */
         B     MAINEXIT
ERRORS   DC    C'ABCDEFGHIJKLMNOP'
FTBTOP   EQU   * *
         TPUT  ERRORS,1
         B     MAINEXIT
FTBSORT  EQU   * *
         TPUT  ERRORS,2
         B     MAINEXIT
FTBDISPL EQU   * *
         TPUT  ERRORS,3
         B     MAINEXIT
FVDEF    EQU   * *
         TPUT  ERRORS,4
         B     MAINEXIT
FTBADD   EQU   * *
         TPUT  ERRORS,5
         B     MAINEXIT
FTBCREAT EQU   * *
         TPUT  ERRORS,6
         B     MAINEXIT
ISPINACT EQU   * *
         TPUT  ERRORS,7
         B     MAINEXIT
LMCATERR EQU   * *
         TPUT  ERRORS,8
MAINEXIT L     R2,K4(,RD)
         LR    R1,RD
         LA    R0,DWORDS
         FREEMAIN R,A=(1),LV=(0)
         LR    RD,R2
         RETURN (14,12),RC=0
VLENGTH  DC    A(K9,K9,K59,K48,K4) /* Variable lens */
ZLENGTH  DC    A(K8,K3),(K9)A(K4) /* Variable lens */
CALLIST  CALL  ,(,,,,,,,,,,,,,,),MF=L,VL
CALLISTL EQU   *-CALLIST
CVPL     CVAFSEQ ACCESS=GTEQ,IXRCDS=KEEP,MF=L
CVAFDIRL CVAFDIR ACCESS=READ,MF=L
IOSVSKEL DC    A(*-*,IOSVDEVT,X'80000000') /* IOSVSUCB parm */
VFORMAT  DC    (K5)CL8'CHAR' /* All charac formats */
ZFORMAT  DC    (K2)CL8'CHAR',(K9)CL8'FIXED' /* Mixed formats */
MULTROWS DC    A(K60) /* Assume TBADD of 60 rows at a time */
CTGFLSP  DC    AL1(K1),(K3)AL1(K0),AL4(K0,ENTNAME,K0),(K2)AL4(K0)
         DC    AL1(K1),(K3)AL1(K0),AL4(K0,ENTYPE,K0),(K2)AL4(K0)
         DC    AL1(K1),(K3)AL1(K0),AL4(K0,CATVOL,K0),(K2)AL4(K0)
         DC    AL1(K1),(K3)AL1(K0),AL4(K0,NAMEDS,K0),(K2)AL4(K0)
CTGFLSPL EQU   *-CTGFLSP /* Len */
MULTITYP DC    CL8'MULTITYP'   /* CTGFL field name */
ENTNAME  DC    CL8'ENTNAME'    /* CTGFL field name */
ENTYPE   DC    CL8'ENTYPE'     /* CTGFL field name */
CATVOL   DC    CL8'CATVOL'     /* CTGFL field name */
NAMEDS   DC    CL8'NAMEDS'     /* CTGFL field name */
CATACB   DC    CL8'CATACB'     /* CTGFL field name */
TBCREATE DC    CL8'TBCREATE' /* ISPF service */
TBSORT   DC    CL8'TBSORT' /* ISPF service */
TBADD    DC    CL8'TBADD' /* ISPF service */
TBTOP    DC    CL8'TBTOP' /* ISPF service */
VDEFINE  DC    CL8'VDEFINE' /* ISPF service */
TBDISPL  DC    CL8'TBDISPL' /* ISPF service */
TBCLOSE  DC    CL8'TBCLOSE' /* ISPF service */
TABLNAME DC    CL8'DSSERVT' /* ISPF tablename */
PANELID  DC    CL8'DSSERVP' /* ISPF panelname */
TABLCOLS DC    C'(COMMAND,ENTRYTYP,NAME,SORTKEY,DATA)'
SYSNAMES DC    C'(ZTDRET,ZTDADD,ZTDSCRP,ZTDSRID,ZTDAMT,ZTDSIZE,ZTDLTOP,X
               ZTDLROWS,ZTDROWS,ZTDSELS,ZTDTOP)'
SORTLIST DC    C'(SORTKEY,C,A,NAME,C,A)'
NOWRITE  DC    CL8'NOWRITE' /* Temporary ISPF table */
ORDER    DC    CL8'ORDER' /* Sort ISPF table */
REPLACE  DC    CL8'REPLACE' /* Replace existing table */
LIST     DC    CL8'LIST' /* VDEFINE list */
NULL     DC    C' ' /* Dummy positional call parameter */
IOSVDEVT DC    AL1(UCB3DACC)  /* DASD class */
LOOKBLNK DC    (K256)AL1(K0) /* Look for a blank */
         ORG   LOOKBLNK+C' '
         DC    AL1(K255)
         ORG
SEPR     DC    C'|__'  /* PC-like line sep */
         LTORG
DSRVWMAP DSECT
SAVEAREA DC    (K18)A(K0)
VSUCBRET DC    (K100)AL1(K0) /* IOSVSUCB results */
IOSVPARM DC    A(*-*,IOSVDEVT,X'80000000') /* IOSVSUCB parm */
VSUCBEP  DC    A(*-*) /* IOSVSUCB ep */
IOSVUCBR DC    A(*-*) /* Return UCB ptr */
ISPLINKL CALL  ,(,,,,,,,,,,,,,,),MF=L,VL
MCATACB  DC    A(*-*) /* Master catalog ACB ptr */
MCATDSN  DC    CL44' ' /* Master catalog dsname */
MCATVSER DC    CL6' ' /* Master catalog volume serial */
*********************** Contiguous storage required *******************
ALIGN    DC    0D'0' /* Alignment for variable pool */
VARIABLE EQU   * * /* Begin ISPF function variables */
VCMD     DC    CL9' '   /* Command col */
VENTRY   DC    CL9' '   /* Entrytyp col */
VNAME    DC    CL59' '  /* Name col */
VKEY     DC    CL48' '  /* Sort key hidden col */
VDATA    DC    CL4' '  /* Hidden data per row type */
SYSVARS  EQU   * * /* Begin ISPF function variables */
UTDRET   DC    CL8' ' /* ISPF system variables */
UTDADD   DC    CL3' '
UTDSCRP  DC    F'0'
UTDSRID  DC    F'0'
UTDAMT   DC    F'0'
UTDSIZE  DC    F'0'
UTDLTOP  DC    F'0'
UTDLROWS DC    F'0'
UTDROWS  DC    F'0'
UTDSELS  DC    F'0'
UTDTOP   DC    F'0'
*********************** Contiguous storage required *******************
CVPLPARM DC    (CVPLNGTH)AL1(K0) /* CVPL services */
CVPLPBUF ICVAFBFL DSECT=NO
CVPLPBFL EQU   *-CVPLPBUF
         IECSDSL1 (1)
CTGFLS   EQU   *
RESPENTN DC    AL1(K1),(K3)AL1(K0),AL4(K0,ENTNAME,K0),(K2)AL4(K0)
         DC    AL1(K1),(K3)AL1(K0),AL4(K0,ENTYPE,K0),(K2)AL4(K0)
         DC    AL1(K1),(K3)AL1(K0),AL4(K0,CATVOL,K0),(K2)AL4(K0)
RESPNMDS DC    AL1(K1),(K3)AL1(K0),AL4(K0,NAMEDS,K0),(K2)AL4(K0)
SVC26PRM DC    (CTGPLEN)AL1(K0) /* Param for catalog services */
CTGENTP  DC    CL44' ' /* Requested dsname for cat searches */
CATRESP  DC    CL256' ' /* Cat serv response area */
DWORDS   EQU   ((((*-SAVEAREA)+K7)/K8)*K8)
         PRINT NOGEN
         IKJPPL
         IKJCPPL
         DSECT
         IEFUCBOB
         IHAPSA
         CVT   DSECT=YES
AMCBS    DSECT , /* Access Method Control Block Structure */
         ORG   AMCBS+K20
CBSCAXCN DC    A(*-*) /* Pointer CAXWA chain */
IGGCAXWA DSECT , /* Catalog Auxiliary Work Area */
         ORG   IGGCAXWA+K4 /* CAXWA chain ptr */
CAXCHN   DC    A(*-*) ...
CAXFLGS  DC    AL1(*-*) /* Flags */
CAXMCT   EQU   X'04' /* 1=Master Catalog, 0=User Catalog */
         ORG   IGGCAXWA+K24
CAXACB   DC    A(*-*)   /* Catalog acb */
         ORG   IGGCAXWA+K28
CAXUCB   DC    A(*-*) /* UCB pointer */
         ORG   IGGCAXWA+K52
CAXCNAM  DC    CL44' ' /* Catalog name */
CTGPL    DSECT ,      /* Catalog parameter list */
CTGOPTN1 DS    X      /* First option byte */
CTGNAME  EQU   X'04'  /* CTGENT holds a(dsname) or a(ci#)
CTGCNAME EQU   X'02'  /* CTGCAT hold a(cat dsname */
CTGOPTN2 DS    X      /* Second option byte */
CTGGTNXT EQU   X'10'  /* LISTCAT get next option */
CTGBOTH  EQU   X'01'  /* Caller accepts ICF arch */
CTGOPTN3 DS    X      /* Third option byte */
CTGCMS   EQU   X'80'  /* Catalog mgmt services req */
CTGAM0   EQU   X'01'  /* ICF/VSAM request */
CTGOPTN4 DS    X      /* Fourth option byte */
CTGENT   DS    A      /* Address of the catalog record id */
CTGCAT   DS    A      /* Address cat dsname or address cat acb */
CTGWKA   DS    A      /* Address callers workarea */
CTGDSORG DS    XL2    /* Superlocate DSORG */
         ORG   CTGDSORG
CTGOPTNS DS    XL2    /* Cat management services options */
CTGLTCAT EQU   X'20'  /* LISTCAT */
CTGF2SEN EQU   X'01'  /* Can handle 40-2 return code */
CTGTYPE  DS    C      /* Cat record type */
CTGTUCAT EQU   C'U'   /* Usercat code */
CTGTANM  EQU   C'X'   /* Alias name */
CTGNOFLD DS    X      /* CTGFIELD entry count */
CTGNEWNM DS    A      /* Alter new dsname ptr */
         ORG   CTGNEWNM
CTGFDBK  DS    XL2    /* Feedback area */
CTGFBFLG DS    XL2    /* Superlocate flags */
CTGJSCB  DS    A      /* JSCB  ptr */
CTGFIELD EQU   *      /* 4 byte ptr to each CTGFL; len=4*CTGNOFLD */
CTGPLEN  EQU   *-CTGPL+(K4*K4) /* Base len w/max4 4byte ctgfl ptrs */
CTGFL    DSECT , /* Filed entry map */
CTGFLDNO DS    X /* # entries in CTGFLDAT */
CTGFLDCD DS    X /* Fl test condition */
CTGFLDGC DS    X /* Type group code */
CTGFLDRE DS    X /* Test results */
CTGFLDWA DS    A /* Workarea; cat rcd field name info */
CTGFLDNM DS    A /* Address of the field name */
CTGFLCHN DS    A /* Address of CTGFL test or 0 */
CTGFLDAT EQU   * /* Start of len/address pairs */
CTGFLL   EQU   *-CTGFL
         PRINT GEN
CVPLMAP  ICVAFPL
         PRINT NOGEN
         END
/*
//L.SYSLMOD DD DSN=IPO1.LINKLIB(DSS),DISP=SHR,
// UNIT=3390,VOL=SER=MCSRES
//*UNIT=,SPACE=
//L.SYSLIB DD DSN=ISP.V2R3M0.ISPLOAD,DISP=SHR
