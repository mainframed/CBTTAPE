/* REXX EXEC TO CONSTRUCT COMDISCO DASD FDR RESTORE JOBS */
"ALLOC F(INPUT) DA('TECH110.COMDISCO.CNTLLIB(STEP4SKL)') SHR";
"ALLOC F(OUTPUT) DA('TECH110.COMDISCO.CNTLLIB(STEP4)') SHR";
"EXECIO * DISKR INPUT (FINIS STEM INPUT.";
OUTPTR = 1;
CURDSN = "";
DO INDEX = 1 TO INPUT.0;
     IF SUBSTR(INPUT.INDEX,1,8)¬="//TAPE1 " & ,
        SUBSTR(INPUT.INDEX,1,13)¬="// UNIT=3480," THEN DO;
          OUTPUT.OUTPTR=INPUT.INDEX;
          OUTPTR=OUTPTR+1;
          END;
      ELSE DO;
            IF SUBSTR(INPUT.INDEX,3,1)="T" THEN DO;
              PARSE VAR INPUT.INDEX . "DSN=" CURDSN ".GXXXXV00" . ;
              SAY "PROCESSING DATASET" CURDSN;
              DUMMY = OUTTRAP("LISTCOUT.");
              "LISTC ENTRY('"CURDSN"') VOLUME";
              DUMMY = OUTTRAP("OFF");
              VOLLIST = "";
              DO LISTIDX = LISTCOUT.0 TO 1 BY -1;
                  IF  POS("VOLSER---",LISTCOUT.LISTIDX)¬=0 THEN DO;
                        PARSE VAR LISTCOUT.LISTIDX ,
                        . "VOLSER------------" VOLSER "DEVTYPE" . ;
                        IF VOLLIST="" THEN VOLLIST = STRIP(VOLSER);
                          ELSE VOLLIST=STRIP(VOLSER)","VOLLIST;
                      END;
                  ELSE IF POS("NONVSAM ",LISTCOUT.LISTIDX)¬=0 ,
                     THEN DO;
                     PARSE VAR LISTCOUT.LISTIDX ,
                       . "NONVSAM ---- " CURDSN . ;
                      CURDSN=STRIP(CURDSN);
                     LEAVE LISTIDX;
                     END;
              END;
              VOLLIST="("VOLLIST")";
          OUTPUT.OUTPTR="//TAPE1 DD DISP=OLD,DSN="CURDSN",UNIT=3480,";
              OUTPTR=OUTPTR+1;
              OUTPUT.OUTPTR="// VOL=SER="VOLLIST;
              OUTPTR=OUTPTR+1;
            END;
          END;
END;
"EXECIO * DISKW OUTPUT (FINIS STEM OUTPUT.";
EXIT 0;
