/* REXX BUILD A COPY JOB                                     */
ADDRESS "TSO";
"ALLOC F(INPUT) DA('TECH110.USER.CNTLLIB(BLDCINP)') SHR";
"EXECIO * DISKR INPUT (FINIS STEM INPUT.";
"FREE F(INPUT)";
OP = 1;
OUT.OP="//COPYSYS JOB 5304010530000000,'M MCCARTY',NOTIFY=TECH105,";
OP=OP+1;
OUT.OP="// CLASS=S,MSGCLASS=G,REGION=8M";
OP=OP+1;
OUT.OP="//COPY EXEC PGM=IDCAMS,DYNAMNBR=255";
OP=OP+1;
OUT.OP="//SYSPRINT DD SYSOUT=*";
OP=OP+1;
OUT.OP="//SYSIN DD *";
OP=OP+1;
VOLLIST="SYS016 SYS017 SYS018 SYS019 SYS020 SYS021 SYS022 SYS023";
DO I = 1 TO INPUT.0 BY 1;
  PARSE VAR INPUT.I IDSN;
  IDSN=STRIP(IDSN);
  PARSE VAR IDSN "SYS1." REMDSN;
  CDSN="SYSC."REMDSN;
  OUT.OP=" ALLOC DA('"CDSN"') -";
  OP=OP+1;
  OUT.OP="    NEW CATALOG DSNTYPE(PDS) DSORG(PO) -";
  OP=OP+1;
  X=LISTDSI("'"IDSN"'" DIRECTORY);
  DIR=SYSADIRBLK;
  SELECT;
  WHEN DIR="45" THEN DIR="44";
  WHEN DIR="90" THEN DIR="89";
  WHEN DIR="135" THEN DIR="134";
  WHEN DIR="180" THEN DIR="179";
  WHEN DIR="225" THEN DIR="224";
  WHEN DIR="270" THEN DIR="269";
  WHEN DIR="315" THEN DIR="314";
  WHEN DIR="360" THEN DIR="359";
  WHEN DIR="405" THEN DIR="404";
  OTHERWISE NOP;
  END;
  PARSE VAR VOLLIST VOLSER VOLLIST;
  OUT.OP="    STORCLAS(SYSSHARE) VOL("VOLSER") DIR("DIR") -";
  OP=OP+1;
  VOLLIST = VOLLIST VOLSER;
  IF SYSRECFM="U" THEN RECFM="U";
  IF SYSRECFM="F" THEN RECFM="F";
  IF SYSRECFM="FB" THEN RECFM="F,B";
  IF SYSRECFM="VB" THEN RECFM="V,B";
  IF SYSRECFM="V" THEN RECFM="V";
  IF RECFM="U" THEN DO;
  OUT.OP="    RECFM("RECFM") BLKSIZE("SYSBLKSIZE") -";
  OP=OP+1; END;
  ELSE DO;
  OUT.OP="    RECFM("RECFM") LRECL("SYSLRECL") -";
  OP=OP+1; END;
  S1=SYSUSED; S2=SYSUDIRBLK; S3=SYSEXTENTS;
  OUT.OP=" /*ALLOCUSED="S1 "DIRUSED="S2 "EXT="S3 "*/";
  OP=OP+1;
  IF SYSUNITS="CYLINDER" THEN SU="CYL";
  ELSE IF SYSUNITS="TRACK" THEN SU="TRK";
  ELSE SU="???";
  OUT.OP="   "SU "SPACE("SYSPRIMARY","SYSSECONDS") -";
  OP=OP+1;
  DUMMY=OUTTRAP("LISTC.");
  "LISTC ENT('"IDSN"') ALL";
  DUMMY = OUTTRAP("OFF");
  DO X=1 TO LISTC.0;
     X1=POS("MANAGEMENTCLASS",LISTC.X);
     X2=POS("DATACLASS",LISTC.X);
     IF X1=0 & X2=0 THEN ITERATE;
     IF X1¬=0 THEN DO;
         PARSE VAR LISTC.X . "MANAGEMENTCLASS" MCLASS;
         MCLASS=TRANSLATE(MCLASS," ","-");
         MCLASS=STRIP(MCLASS);
         END;
     IF X2¬=0 THEN DO;
         PARSE VAR LISTC.X "DATACLASS" DCLASS . ;
         DCLASS=TRANSLATE(DCLASS," ","-");
         DCLASS=STRIP(DCLASS);
         LEAVE X;
         END;
  END;
  OUT.OP=" DATACLAS("DCLASS") MGMTCLAS("MCLASS")";
  OP=OP+1;
  DCLASS="";MCLASS="";
  OUT.OP=" ALTER" CDSN "-";
  OP=OP+1;
  OUT.OP="     NEWNM("IDSN") -";
  OP=OP+1;
  OUT.OP="     CAT(SYS1.MCATSA.CLUSTER)";
  OP=OP+1;
  OUT.OP=" DEF NVSAM (NAME("IDSN") -";
  OP=OP+1;
  OUT.OP="     DEVT(3390) VOLUMES("VOLSER") RECATALOG) -";
  OP=OP+1;
  OUT.OP="     CAT(SYS1.MCATSB.CLUSTER)";
  OP=OP+1;
END;
"ALLOC F(OUTPT) DA('TECH110.USER.CNTLLIB(BLDCOUT)') SHR";
"EXECIO * DISKW OUTPT (FINIS STEM OUT.";
"FREE F(OUTPT)";
EXIT 0;
