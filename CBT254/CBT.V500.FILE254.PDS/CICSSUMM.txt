/* CICS SUMMARY REXX */
REPORTS="JAN92 FEB92 MARCH92 APRIL92 MAY92 JUNE92 JULY92 AUG92 " || ,
  "SEPT92 OCT92 NOV92 DEC92 JAN93 FEB93 MARCH93 APRIL93 MAY93 JUNE93";
RPTCTR=WORDS(REPORTS);
TRANSIDLIST="";
DO I = 1 TO RPTCTR;
   RPTMONTH=WORD(REPORTS,I);
   SAY "PROCESSING" RPTMONTH;
   "FREE F(RPTIN)";
   "ALLOC F(RPTIN) DA('TECH102.MONTHLY.TRAN.DATA("RPTMONTH")') SHR";
   "EXECIO * DISKR RPTIN (FINIS STEM DATAIN.";
   SDATALINE=5;
   EDATALINE=DATAIN.0-1;
   DO J = SDATALINE TO EDATALINE;
        PARSE VAR DATAIN.J TRANSID TRANSCOUNT FILEIOCOUNT;
        TRANSID=STRIP(TRANSID);
        TRANSCOUNT=SPACE(TRANSLATE(STRIP(TRANSCOUNT),"",","),0);
        FILEIOCOUNT=SPACE(TRANSLATE(STRIP(FILEIOCOUNT),"",","),0);
        XCOUNT.RPTMONTH.TRANSID = TRANSCOUNT;
        FCOUNT.RPTMONTH.TRANSID = FILEIOCOUNT;
        IF WORDPOS(TRANSID,TRANSIDLIST)=0 THEN ,
            TRANSIDLIST=TRANSIDLIST TRANSID;
   END;
END;
RPTCTR=WORDS(REPORTS);
XCTR = WORDS(TRANSIDLIST);
"FREE F(COUT)";
"FREE F(FOUT)";
"ALLOC F(COUT) DA('TECH110.XCOUNT.DATA') SHR";
"ALLOC F(FOUT) DA('TECH110.FCOUNT.DATA') SHR";
XOCTR=2;FOCTR=2;
XXOUT.1="TRANSACTION,JAN92,FEB92,MAR92,APR92,MAY92,JUN92,JUL92,"|| ,
 "AUG92,SEP92,OCT92,NOV92,DEC92,JAN93,FEB93,MAR93,APR93,MAY93,JUN93";
FFOUT.1="TRANSACTION,JAN92,FEB92,MAR92,APR92,MAY92,JUN92,JUL92,"|| ,
 "AUG92,SEP92,OCT92,NOV92,DEC92,JAN93,FEB93,MAR93,APR93,MAY93,JUN93";
SAY "FORMATTING FINAL REPORTS";
DO K = 1 TO XCTR;
TRANSID=WORD(TRANSIDLIST,K);
XOUTLINE=STRIP(TRANSID);
FOUTLINE=STRIP(TRANSID);
X92SUM=0;X93SUM=0;
F92SUM=0;F93SUM=0;
  DO L = 1 TO RPTCTR;
      RPTMONTH=WORD(REPORTS,L);
      IF SYMBOL("XCOUNT.RPTMONTH.TRANSID")="VAR" THEN DO;
          XOUTLINE=XOUTLINE","XCOUNT.RPTMONTH.TRANSID;
          FOUTLINE=FOUTLINE","FCOUNT.RPTMONTH.TRANSID;
          YEAR=SUBSTR(RPTMONTH,LENGTH(RPTMONTH)-1,2);
          IF YEAR="92" THEN DO;
               X92SUM=X92SUM+XCOUNT.RPTMONTH.TRANSID;
               F92SUM=F92SUM+FCOUNT.RPTMONTH.TRANSID;
               END;
            ELSE DO;
               X93SUM=X93SUM+XCOUNT.RPTMONTH.TRANSID;
               F93SUM=F93SUM+FCOUNT.RPTMONTH.TRANSID;
               END;
          END;
       ELSE DO;
          XOUTLINE=XOUTLINE",0";
          FOUTLINE=FOUTLINE",0";
          END;
  END;
XXOUT.XOCTR= ,
   LEFT(XOUTLINE,200)RIGHT(X92SUM,12,"0") RIGHT(X93SUM,12,"0");
  XOCTR=XOCTR+1;
FFOUT.FOCTR= ,
   LEFT(FOUTLINE,200)RIGHT(F92SUM,12,"0") RIGHT(F93SUM,12,"0");
  FOCTR=FOCTR+1;
END;
"EXECIO * DISKW COUT (FINIS STEM XXOUT.";
"EXECIO * DISKW FOUT (FINIS STEM FFOUT.";
