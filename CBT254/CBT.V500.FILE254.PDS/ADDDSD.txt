/* REXX EXEC to build ADDDSD RACF profiles */
true=1; false=0;
uaccflag=false;
uaccflag2=false;
accflag=false;
accflag2=false;
olinectr=1;
groups="TECHSUPT STORSYS POLLING";
"ALLOC F(INP) DA('SRTY001.ESA.ACCESS') SHR UNIT(3390) VOL(MCS001)";
"ALLOC F(IN2) DA('SRTY001.ESARACF.CONVERT') SHR" ,
         "UNIT(3390) VOL(MCS001)";
"ALLOC F(OUT) DA('TECH110.USER.CNTLLIB(RACFDSD)') SHR";
"EXECIO * DISKR INP (FINIS STEM ILINE.";
"EXECIO * DISKR IN2 (FINIS STEM ILINE2.";
"FREE F(INP)";
"FREE F(IN2)";
cnvlist="";
do index=1 to iline2.0; /* build conversion list */
    parse var iline2.index p1 p2;
    p1=strip(p1);p2=strip(p2);
    if p2="" then p2=p1;
    cnvlist=cnvlist p1 p2;
end;
do index=1 to iline.0;
     iline.index=substr(iline.index,2); /* skip column 1 */
     if ¬uaccflag & ¬accflag then do;
          if wordpos("INFORMATION FOR DATASET",iline.index)¬=0 then do;
                parse var iline.index . "DATASET" profname . ;
                parse var profname hlq "." dsnrem ;
                dsnrem=strip(dsnrem);
                if substr(hlq,1,3)="SYS" | hlq="BUILDING" then ,
                     iterate index;
                x= wordpos(hlq,cnvlist);
                if x¬=0 then do;
                     y=word(cnvlist,x+1);
                     if y="DELETE" then iterate index; /* skipdsd*/
                     if y="TECHSUPT" then iterate index;
                     if hlq¬=y & wordpos(y,groups)=0 then ,
                            profname=y"."dsnrem;
                     end;
                  else iterate index; /* not found */
                uaccflag=true; /* looking for uacc */
                if substr(profname,length(profname)-1,2)=".*" then ,
                     profname=profname"*";
                 else if substr(profname,length(profname),1)="*" then ,
                     profname=profname".**";
                iterate index;
                end;
          else iterate index;
          end;
     else if uaccflag then do;
          if ¬uaccflag2 then do;
                if wordpos("UNIVERSAL ACCESS",iline.index)¬=0 then do;
                     uaccflag2=true;
                     iterate index;
                     end;
             iterate index;
             end;
          else do;
             if word(iline.index,1)="-----" then iterate index;
             uacc = word(iline.index,3);
             uaccflag=false;uaccflag2=false;accflag=true;
             pelist="";
             oline.olinectr="AD '"profname"' GENERIC -";
             olinectr = olinectr+1;
             oline.olinectr="   OWNER(SECURITY) UACC("uacc")";
             olinectr = olinectr+1;
             iterate index;
             end;
       end;
     else if accflag then do;
          if ¬accflag2 then do;
                if word(iline.index,1)="ID" & ,
                   word(iline.index,2)="ACCESS" then do;
                     accflag2=true;
                     iterate index;
                     end;
             iterate index;
             end;
          else do;
             if word(iline.index,1)="--------" then iterate index;
             if iline.index=" " then iterate index;
             if word(iline.index,1)="PROGRAM" then do;
                 accflag=false;accflag2=false;
                 if wordpos("TESTSYS",pelist)¬=0 then do;
                      tstr=wordpos("TESTSYS",pelist);
                      tacc=tstr+1;
                      tacc=word(pelist,tacc);
                      pelist=delword(pelist,tstr,2);
                      pelist=pelist "FASHNBUG" tacc ,
                           "CORPSVC1" tacc,
                           "CORPSVC2" tacc,
                           "STORSYS" tacc,
                           "DEVCNTR" tacc,
                           "INFOCNTR" tacc;
                      end;
                 pelist = pelist "TEMPUSER NONE";
                 pectr=words(pelist);
                 do peptr = 1 to pectr by 2;
                   peid=word(pelist,peptr);
                   peac=word(pelist,peptr+1);
                   oline.olinectr="      PE '"profname"' GENERIC -";
                   olinectr = olinectr+1;
                   oline.olinectr="         ID("peid") ACCESS("peac")";
                   olinectr = olinectr+1;
                 end;
                 iterate index;
                 end;
             peid=word(iline.index,1);
             if peid="SUPTEST" then peid="TESTSYS"; /* sub */
             else do;
                x=wordpos(peid,cnvlist); /* convert */
                if x=0 then iterate index;
                y=word(cnvlist,x+1);
                if y="DELETE" then iterate index;
                peid=y; /* reset */
                end;
             peac=word(iline.index,2);
             if wordpos(peid,pelist)¬=0 then iterate index;
             pelist=pelist peid peac; /* add to permit list */
             end;
       end;
end;
"EXECIO * DISKW OUT (FINIS STEM OLINE.";
"FREE F(OUT)";
