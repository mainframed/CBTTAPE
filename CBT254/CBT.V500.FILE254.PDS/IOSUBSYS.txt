/* REXX I/O Subsystem display */
okay = 0;
true=1;
devlist="";
"CONSPROF SOLDISP(NO)";
"CONSOLE ACTIVATE";
address "CONSOLE";
"CART CHPSTAT";
do i = 0 to 255;
chpid = d2x(i,2);
chpd.chpid="";
"D M=CHP("chpid")";
response = GETMSG("resp.","SOL","CHPSTAT",,30);
if response = okay then do j =3 to (resp.0 - 3);
     if j=3 & ,
         pos("PART OF ANOTHER",resp.j)>0 then iterate i;
     if j=3 & ,
         pos("DOES NOT EXIST",resp.j)>0 then iterate i;
      else if j=3 then nop;
      else do;
          pdev=word(resp.j,1);
          do k = 0 to 15;
              kdev=right(pdev||d2x(k,1),4,"0");
              if wordpos(kdev,devlist)=0 then do;
                   devlist = devlist kdev;
                   dev.kdev = word(resp.j,k+2) chpid;
                   end;
                else ,
                   dev.kdev = dev.kdev word(resp.j,k+2) chpid;
          end k;
        end;
   end j;
  else do;
     say "Error processing CHPID" chpid;
     exit 16;
     end;
end i;
"DEACTIVATE";
address "TSO";
"CONSPROF SOLDISP(YES)";
cvtptr = storage("00000010",4); /* locate llt/smca from cvt */
cvtsmca = storage(d2x(c2d(cvtptr)+196),4); /* mapping smca */
savehi=substr(c2x(cvtsmca),1,2); /* hi order bit is on */
smcasid = savehi||d2x(x2d(substr(c2x(cvtsmca),3))+16);
smcasid = storage(smcasid,4); /* SMF id */
outptr=1;
outp.outptr="System" smcasid "I/O Configuration as of" date("U")".";
do j = 0 to 4095; /* process list */
   if pos(d2x(j,4),devlist)=0 then iterate j;
   z = d2x(j,4); /* note works only for 3 digit devno with left 0 */
   l = words(dev.z);
   outptr=outptr+1;
   outp.outptr="Devno" z
   do k = 1 to l by 2;
       str="";
       status = word(dev.z,k);
       chpid = word(dev.z,k+1);
       if pos("+",status)>0 then str=str "and Online";
       if pos("@",status)>0 then str=str "and Path not validated";
       if pos("-",status)>0 then str=str "and Offline";
       if pos(".",status)>0 then str=str "and Device does not exist";
       if pos("*",status)>0 then str=str "and Physically online";
       if pos("$",status)>0 then str=str "and Path not operational";
       str=substr(space(str),5)".";
       outptr=outptr+1;
       outp.outptr="    " "via CHPID" chpid "status is" str;
       chpd.chpid=chpd.chpid z translate(str,"$"," ");
   end k;
end;
do i = 0 to 255;
  chpid = d2x(i,2);
  outptr=outptr+1;
  outp.outptr="CHPID" chpid;
  xword=words(chpd.chpid);
  do j = 1 to xword by 2;
      devno=word(chpd.chpid,j);
      str=translate(word(chpd.chpid,j+1)," ","$");
      outptr=outptr+1;
      outp.outptr="    " "Devno" devno "status is" str;
  end;
end;
"ALLOC F(OUTPUT) DA('TECH110.USER.CNTLLIB(IOSTATS)') SHR";
"EXECIO * DISKW OUTPUT (STEM outp. FINIS";
"FREE F(OUTPUT)";
exit 0;
