         TITLE 'LISTDD - LIST DATASETS ALLOCATED TO TSO SESSION'        00001
         PRINT GEN                                                      00002
*********************************************************************** 00003
*TSO DD ALLOCATION LIST -- AN ALTERNATIVE TO THE TSO LISTALC COMMAND  * 00004
*                                                                     * 00005
*AUTHOR: ANTHONY M. HECHT                                             * 00006
*        FIREMAN'S FUND INSURANCE COMPANY -- JULY 1988                * 00007
*        (415) 492-5026                                               * 00008
*********************************************************************** 00009
* FEATURES: ALLOCATION INFO SORTED BY DDNAME                          * 00010
*           SPEED GREATLY IMPROVED (OVER LISTALC)                     * 00011
*           ALLOCATED PDS MEMBERNAMES DISPLAYED                       * 00012
*           INPUT PARAMETER FLEXIBILITY                               * 00013
*           OUTPUT WITH PUTLINE FOR &SYSOUTTRAP COMPATIBILITY         * 00014
*           DATASET VOLSER AND DISPOSTION INFO INCLUDED               * 00015
*                                                                     * 00016
* FORMAT:   LISTDD  {DDNAME}                                          * 00017
*           IF DDNAME SUPPLIED, LISTS DSN INFO FOR THAT DD. WILDCARD  * 00018
*           CHARACTER '*' PERMITTED AT END OF SUPPLIED DDNAME.  IF    * 00019
*           NO DDNAME SUPPLIED, EXTRACTS AND SORTS ALL DD'S ALLOCATED * 00020
*           FOR SESSION.                                              * 00021
*                                                                     * 00022
* TSO                                                                 * 00023
* EXAMPLES: LISTDD SYSPROC                                            * 00024
*            DISPLAYS INFO FOR ALL DATASETS ALLOCATED TO DDNAME       * 00025
*            "SYSPROC".                                               * 00026
*           LISTDD ISP*                                               * 00027
*            DISPLAYS INFO FOR ALL DATASETS ALLOCATED TO ALL DDNAMES  * 00028
*            BEGINNING WITH THE CHARACTERS "ISP", SORTED BY DDNAME.   * 00029
*           LISTDD                                                    * 00030
*            DISPLAYS INFO FOR ALL DATASETS ALLOCATED TO ALL DDNAMES, * 00031
*            SORTED BY DDNAME.                                        * 00032
*                                                                     * 00033
*                                                                     * 00034
* RETURN CODES:   0 -- ALL OKAY                                       * 00035
*                11_ -- INPUT PARM ERRORS:                            * 00036
*                  113 -- DDNAME > 8 CHARACTERS                       * 00037
*                130 -- DDNAME NOT FOUND                              * 00038
*                140 -- TABLE SIZE EXCEEDED                           * 00039
*                                                                     * 00040
* REGISTER USAGE:                                                     * 00041
*                 R9  - ADDRESS LISTDD TABLE                          * 00042
*                 R10 - ADDRESS TIOT                                  * 00043
*                 R11 - BRANCH-AND-LINK REGISTER                      * 00044
*                 R12 - BASE REGISTER                                 * 00045
*                                                                     * 00046
* ASSEMBLY/LINK INSTRUCTIONS:                                         * 00047
*                 ASSEMBLED WITH IEV90 (ASM-H), LINK-EDITED WITH IEWL.* 00048
*                 NO SPECIAL ATTRIBUTES REQUIRED.                     * 00049
*                 //SYSLIB DD FOR ASM STEP MUST INCLUDE LIBRARIES     * 00050
*                 CONTAINING THE FOLLOWING SYSTEM MACROS:             * 00051
*                 IHAPSA IKJTCB IEFTIOT1 IEFJFCBN IKJCPPL AND IKJIOPL * 00052
*                                                                     * 00053
*********************************************************************** 00054
LISTDD   CSECT 0                                                        00055
R0       EQU   0                                                        00056
R1       EQU   1                                                        00057
R2       EQU   2                                                        00058
R3       EQU   3                                                        00059
R4       EQU   4                                                        00060
R5       EQU   5                                                        00061
R6       EQU   6                                                        00062
R7       EQU   7                                                        00063
R8       EQU   8                                                        00064
R9       EQU   9                                                        00065
R10      EQU   10                                                       00066
R11      EQU   11                                                       00067
R12      EQU   12                                                       00068
R13      EQU   13                                                       00069
R14      EQU   14                                                       00070
R15      EQU   15                                                       00071
*                                                                       00072
         SAVE  (14,12)             SAVE REGISTERS                       00073
         USING LISTDD,R12          OS                                   00074
         LR    R12,R15              STAN-                               00075
         ST    R13,SAVEAREA+4        DARD                               00076
         LA    R13,SAVEAREA           LINK-                             00077
         ST    R13,SAVEAREA+8          AGE                              00078
         B     BEGIN                                                    00079
SAVEAREA DS    18F                                                      00080
         DC    C'&SYSDATE AT &SYSTIME FROM LISTDD'                      00081
         USING SORTLINE,R9             ADDRESS LISTDD TABLE             00082
         USING TIOENTRY,R10            ADDRESS TIOT                     00083
         USING INFMJFCB,R5             ADDRESS JFCB                     00084
BEGIN    DS    0H                                                       00085
         ST    R1,PARMADDR        SAVE REGISTER 1 FOR AWHILE            00086
*                                                                       00087
         BAL   R11,INIT1          INITIALIZATION                        00088
         CLI   ERRFLAG1,C'Y'        ERRORS FOUND?                       00089
         BE    EOJ                  YES: QUIT                           00090
*                                                                       00091
         BAL   R11,SCAN1          SCAN INPUT REQUEST                    00092
         CLI   ERRFLAG1,C'Y'        ERRORS FOUND?                       00093
         BE    EOJ                  YES: QUIT                           00094
*                                                                       00095
         BAL   R11,GETM1          ALLOCATE LISTDD TABLE                 00096
*                                                                       00097
         BAL   R11,SCTT1          SCAN TASK I/O TABLE FOR DDNAMES       00098
         CLI   ERRFLAG1,C'Y'        ERRORS FOUND?                       00099
         BE    FREEMAIN             YES: FREE TABLE AND QUIT            00100
*                                                                       00101
         BAL   R11,SORT1          SORT ACQUIRED LISTDD TABLE            00102
*                                                                       00103
         BAL   R11,DISP1          DISPLAY RESULTS TABLE                 00104
*                                                                       00105
FREEMAIN DS    0H                                                       00106
         BAL   R11,FREE1          FREE LISTDD TABLE                     00107
         B     EOJ                                                      00108
*                                                                       00109
***************************                                             00110
** EOJ                   **                                             00111
***************************                                             00112
EOJ      DS    0H                                                       00113
         L     R15,RETCODE                                              00114
         L     R13,4(,R13)                                              00115
         RETURN (14,12),RC=(15)                                         00116
*                                                                       00117
**********************************************************              00118
**** PUTLINE CONTROL BLOCKS INITIALIZATION ROUTINE      **              00119
**********************************************************              00120
         DS    F                                                        00121
INIT1    ST    R11,INIT1-4        SAVE RETURN ADDRESS                   00122
         L     R2,PARMADDR        RETRIEVE PARM ADDRESS                 00123
         TM    0(R2),X'80'        IS THIS A CMD PROCESSOR?              00124
         BZ    INIT1$01           YES, CONTINUE                         00125
         TPUT  ERRMSG00,L'ERRMSG00  NO: SEND ERROR MSG                  00126
         MVI   ERRFLAG1,C'Y'      SET ERROR FLAG                        00127
         B     INIT1END           AND GO TO RETURN                      00128
*                                                                       00129
INIT1$01 DS    0H                                                       00130
         MVC   MYIOPL+IOPLUPT-IOPL,CPPLUPT-CPPL(R2)                     00131
         MVC   MYIOPL+IOPLECT-IOPL,CPPLECT-CPPL(R2)                     00132
         LA    R1,MYECB                EVENT CONTROL BLOCK ADDRESS      00133
         ST    R1,MYIOPL+IOPLECB-IOPL                                   00134
         LA    R1,PUTLISTF             PUTLINE PARAMETER BLOCK ADDRESS  00135
         ST    R1,MYIOPL+IOPLIOPB-IOPL                                  00136
*                                                                       00137
INIT1END L     R11,INIT1-4        GET RETURN ADDRESS                    00138
         BR    R11                AND RETURN                            00139
*                                                                       00140
************************************                                    00141
**** INPUT BUFFER SCAN ROUTINE    **                                    00142
************************************                                    00143
         DS    F                                                        00144
SCAN1    ST    R11,SCAN1-4        SAVE RETURN ADDRESS                   00145
         L     R2,PARMADDR        RETRIEVE PARM ADDRESS                 00146
         L     R2,0(R2)           ADDRESS OF CP INPUT STRING            00147
         LH    R3,0(R2)           OVERALL LENGTH OF INPUT STRING        00148
         LH    R4,2(R2)           LENGTH OF COMMAND NAME                00149
         SR    R3,R4              GET LENGTH OF PARM: SUBTRACT          00150
         SH    R3,=H'4'             4 (FOR LENGTH OF FIRST FULLWORD)    00151
         STH   R3,PARMLEN         SAVE PARM LENGTH                      00152
         BNZ   SCAN1$01           PARM GIVEN: GO CHECK IT               00153
         MVI   INPUTDD,C'*'       NO PARMS GIVEN: LIST ALL DD'S         00154
         B     SCAN1END           ALL DONE SCANNING                     00155
*                                                                       00156
SCAN1$01 DS    0H                                                       00157
         CH    R3,=H'8'           DDNAME > 8 CHARACTERS?                00158
         BNH   SCAN1$02           NO: OK CONTINUE                       00159
         MVC   RETCODE,=F'113'    SET BAD RETURN CODE                   00160
         MVC   PLDATA(L'ERRMSG13),ERRMSG13 SEND                         00161
         BAL   R11,PUTLINE                   MESSAGE                    00162
         MVI   ERRFLAG1,C'Y'      SET ERROR FLAG                        00163
         B     SCAN1END           AND GO TO RETURN                      00164
*                                                                       00165
SCAN1$02 DS    0H                                                       00166
         LA    R2,4(R2,R4)        POINT R2 AT START OF REAL PARM        00167
         BCTR  R3,0               SUBTRACT 1 FOR EXECUTE                00168
         EX    R3,MVCIT1          MOVE INPUT PARMS TO HOLD AREA         00169
         OC    INPUTDD,=8C' '     FOLD TO UPPERCASE                     00170
*                                                                       00171
SCAN1$03 DS    0H                                                       00172
         LA    R2,0(R2,R3)        POINT TO LAST CHARACTER               00173
         CLI   0(R2),C'*'         IS IT THE WILDCARD CHARACTER?         00174
         BNE   SCAN1END           NO: NON-GENERIC REQUEST; DONE         00175
         BCTR  R3,0               YES: REQUEST IS GENERIC;              00176
         STH   R3,COMPLEN          SAVE LENGTH FOR EXECUTED COMPARE     00177
*                                                                       00178
SCAN1END L     R11,SCAN1-4        GET RETURN ADDRESS                    00179
         BR    R11                AND RETURN                            00180
*                                                                       00181
***************************                                             00182
** GETMAIN ROUTINE       **                                             00183
***************************                                             00184
         DS    F                                                        00185
GETM1    ST    R11,GETM1-4          SAVE RETURN ADDRESS                 00186
         GETMAIN RC,LV=20000          GET AREA FOR TABLE                00187
         LR    R9,R1                  SAVE TABLE BEGINNING ADDRESS      00188
         ST    R9,TABLBEG             STORE TABLE BEGINNING ADDRESS     00189
         A     R1,=F'19999'           CALCULATE ENDING TABLE ADDRESS    00190
         ST    R1,TABLEND             STORE TABLE ENDING ADDRESS        00191
         L     R11,GETM1-4          GET RETURN ADDRESS                  00192
         BR    R11                  AND RETURN                          00193
*                                                                       00194
*************************************                                   00195
** SCAN TASK I/O TABLE ROUTINE     **                                   00196
*************************************                                   00197
         DS    F                                                        00198
SCTT1    ST    R11,SCTT1-4          SAVE RETURN ADDRESS                 00199
         L     R10,PSATOLD-PSA         CURRENT TCB ADDR                 00200
         L     R10,TCBTIO-TCB(,R10)    OUR TIOT ADDR                    00201
         LA    R10,TIOENTRY-TIOT(,R10) 1ST DD SEGMENT IN TIOT           00202
         SR    R4,R4                   CLEAR FOR WORK (TIO ENT LENGTH)  00203
         SR    R5,R5                   CLEAR FOR WORK                   00204
         SR    R6,R6                   CLEAR FOR WORK (SEQ COUNTER)     00205
*                                                                       00206
SCTT1$01 DS    0H                                                       00207
         MVI   SORTLINE,C' '           CLEAR CURRENT TABLE LINE         00208
         MVC   SORTLINE+1(L'SORTLINE-1),SORTLINE      TO BLANKS         00209
         LA    R10,TIOENTRY(R4)        POINT TO NEXT TIOT ENTRY         00210
         CLI   TIOELNGH,0              END OF TIOT?                     00211
         BE    SCTT1$18                YES: DONE SCANNING TIOT          00212
         IC    R4,TIOELNGH             LENGTH OF THIS ENTRY             00213
         CLI   TIOEDDNM,C'A'           DDNAME FIRST CHARACTER VALID?    00214
         BNL   SCTT1$02                YES: USE AS IS                   00215
         CLC   TIOEDDNM,=8C' '         ALL BLANKS?                      00216
         BNE   SCTT1$01                NO: DEALLOCATED; SKIP IT.        00217
         MVC   SORTDDN,PREVDDN         YES: CONCAT DD'S; USE PREV DDN   00218
         B     SCTT1$03                GO CHECK FOR DDNAME REQUEST      00219
*                                                                       00220
SCTT1$02 DS    0H                                                       00221
         MVC   SORTDDN,TIOEDDNM        MOVE DDNAME FOR SORT             00222
         MVC   PREVDDN,SORTDDN         AND SAVE IT FOR POSSIBLE CONCATS 00223
*                                                                       00224
SCTT1$03 DS    0H                                                       00225
         CLI   INPUTDD,C'*'            IS THIS AN 'ALL' REQUEST?        00226
         BE    SCTT1$04                YES: CONTINUE                    00227
         LH    R2,COMPLEN              GET COMPARE LENGTH FOR EXECUTE   00228
         EX    R2,COMPIT1              IS THIS THE DD WE WANT?          00229
         BE    SCTT1$04                YES: CONTINUE                    00230
         B     SCTT1$01                NO: CONTINUE LOOKING             00231
*                                                                       00232
SCTT1$04 DS    0H                                                       00233
         STCM  R6,B'0011',SORTTAG      TO KEEP CONCAT SEQUENCE W/ SORT  00234
         LA    R6,1(,R6)               INCREMENT SEQ COUNTER            00235
         ICM   R5,7,TIOEJFCB           JFCB POINTER                     00236
         LA    R5,16(R5)               FIX IT                           00237
         MVC   SORTDSN,JFCBDSNM        MOVE DSNAME FOR DISPLAY          00238
         MVC   SORTVOLS,JFCBVOLS       MOVE VOLS FOR DISPLAY            00239
         TM    JFCFLGS1,JFCVRDS        VIO DATASET?                     00240
         BZ    SCTT1$05                NO: CONTINUE CHECKING            00241
         MVC   SORTVOLS,=CL6'(VIO)'    YES: MOVE LITERAL                00242
         B     SCTT1$06                CONTINUE                         00243
*                                                                       00244
SCTT1$05 DS    0H                                                       00245
         TM    TIOELINK,TIOTTERM       IS DEVICE A TERMINAL?            00246
         BZ    SCTT1$06                NO: USE VOLSER AND CONTINUE      00247
         MVC   SORTVOLS,=C'(TERM)'     YES: MOVE LITERAL                00248
*                                                                       00249
SCTT1$06 DS    0H                                                       00250
         TM    JFCBIND1,JFCPDS         IS ALLOC TO A PDS MEMBER?        00251
         BZ    SCTT1$11                NO: GO GET DISPOSITION           00252
         LA    R2,SORTDSN              FIND                             00253
         LA    R3,45                    END                             00254
*                                                                       00255
SCTT1$07 DS    0H                        OF                             00256
         CLI   0(R2),C' '                 DSN                           00257
         BE    SCTT1$08                    FIELD                        00258
         LA    R2,1(,R2)                    TO                          00259
         BCT   R3,SCTT1$07                   PLACE                      00260
*                                                                       00261
SCTT1$08 DS    0H                             MEMBER NAME               00262
         MVI   0(R2),C'('              MOVE LITERAL                     00263
         MVC   1(8,R2),JFCBELNM        MOVE MEMBER NAME                 00264
         LA    R3,9                    FIND                             00265
*                                                                       00266
SCTT1$09 DS    0H                       END                             00267
         CLI   0(R2),C' '                OF                             00268
         BE    SCTT1$10                   MEMBER                        00269
         LA    R2,1(,R2)                   NAME                         00270
         BCT   R3,SCTT1$09                  TO                          00271
*                                                                       00272
SCTT1$10 DS    0H                            PLACE ')'                  00273
         MVI   0(R2),C')'              MOVE LITERAL                     00274
*                                                                       00275
SCTT1$11 DS    0H                                                       00276
         TM    JFCBIND2,JFCTEMP        TEMP DATASET?                    00277
         BZ    SCTT1$12                NO: KEEP TRYING                  00278
         MVC   SORTDISP,=C'TMP'        YES: MOVE LITERAL                00279
         B     SCTT1$17                AND CONTINUE                     00280
*                                                                       00281
SCTT1$12 DS    0H                                                       00282
         TM    JFCBIND2,JFCSHARE       SHR DATASET?                     00283
         BZ    SCTT1$13                NO: KEEP TRYING                  00284
         MVC   SORTDISP,=C'SHR'        YES: MOVE LITERAL                00285
         B     SCTT1$17                AND CONTINUE                     00286
*                                                                       00287
SCTT1$13 DS    0H                                                       00288
         TM    JFCBIND2,JFCOLD         OLD DATASET?                     00289
         BZ    SCTT1$14                NO: KEEP TRYING                  00290
         MVC   SORTDISP,=C'OLD'        YES: MOVE LITERAL                00291
         B     SCTT1$17                AND CONTINUE                     00292
*                                                                       00293
SCTT1$14 DS    0H                                                       00294
         TM    JFCBIND2,JFCMOD         MOD DATASET?                     00295
         BZ    SCTT1$15                NO: KEEP TRYING                  00296
         MVC   SORTDISP,=C'MOD'        YES: MOVE LITERAL                00297
         B     SCTT1$17                AND CONTINUE                     00298
*                                                                       00299
SCTT1$15 DS    0H                                                       00300
         TM    JFCBIND2,JFCNEW         NEW DATASET?                     00301
         BZ    SCTT1$16                NO: LEAVE BLANK                  00302
         MVC   SORTDISP,=C'NEW'        YES: MOVE LITERAL                00303
         B     SCTT1$17                AND CONTINUE                     00304
*                                                                       00305
SCTT1$16 DS    0H                                                       00306
         MVC   SORTDISP,=C'   '        INSERT BLANKS                    00307
*                                                                       00308
SCTT1$17 DS    0H                                                       00309
         CLI   ERRFLAG2,C'E'           TABLE SIZE ALREADY EXCEEDED?     00310
         BE    SCTT1$01                YES: JUST KEEP COUNTING          00311
         LA    R9,80(,R9)              BUMP TABLE POINTER               00312
         C     R9,TABLEND              PAST TABLE END?                  00313
         BNH   SCTT1$01                NO: ITERATE                      00314
         MVI   ERRFLAG2,C'E'           YES: FLAG TABLE SIZE EXCEEDED    00315
         B     SCTT1$01                BUT CONTINUE COUNTING            00316
*                                                                       00317
SCTT1$18 DS    0H                                                       00318
         CLI   ERRFLAG2,C'E'           TABLE SIZE EXCEEDED?             00319
         BNE   SCTT1$19                NO: CONTINUE                     00320
         MVC   PLDATA(ERR40LEN),ERRMSG40  NOTIFY USER,                  00321
         BAL   R11,PUTLINE                 TABLE SIZE EXCEEDED          00322
         MVC   RETCODE,=F'140'         SET BAD RETURN CODE              00323
         MVI   ERRFLAG1,C'Y'           SET ERROR FLAG                   00324
         B     SCTT1END                DONE PROCESSING                  00325
*                                                                       00326
SCTT1$19 DS    0H                                                       00327
         STH   R6,DSNSAVE              SAVE NUMBER OF DATASETS          00328
         LTR   R6,R6                   DID WE FIND ANY?                 00329
         BNZ   SCTT1END                YES: DONE                        00330
         MVC   ERR30DDN,INPUTDD        NO: FILL IN ERROR PARAMETER      00331
         LH    R10,PARMLEN             GET LENGTH OF PARM               00332
         LA    R4,ERR30DDN(R10)         TO BUILD ERROR MSG              00333
         MVC   1(L'ERRMSG31,R4),ERRMSG31 AND JUSTIFY                    00334
         MVC   PLDATA(ERR30LEN),ERRMSG30  SEND                          00335
         BAL   R11,PUTLINE                 MSG                          00336
         MVC   RETCODE,=F'130'    SET BAD RETURN CODE                   00337
         MVI   ERRFLAG1,C'Y'           SET ERROR FLAG                   00338
*                                                                       00339
SCTT1END L     R11,SCTT1-4          GET RETURN ADDRESS                  00340
         BR    R11                  AND RETURN                          00341
*                                                                       00342
****************************************************                    00343
** SORT ROUTINE FOR ACQUIRED LISTDD TABLE         **                    00344
****************************************************                    00345
         DS    F                                                        00346
SORT1    ST    R11,SORT1-4          SAVE RETURN ADDRESS                 00347
         L     R9,TABLBEG              RESET TO BEGINNING OF TABLE      00348
         LH    R6,DSNSAVE              GET NUMBER OF DATASETS           00349
         BCTR  R6,0                    R6=# OF SORT SCANS REMAINING     00350
         LTR   R6,R6                   ONLY 1 RECORD?                   00351
         BZ    SORT1END                YES: DON'T BOTHER TO SORT        00352
SORT1$01 LR    R7,R6                   R7=# OF COMPARES FOR THIS SCAN   00353
         LA    R8,80(,R9)              POINT TO TEST RECORD FOR COMPARE 00354
*                                                                       00355
SORT1$02 DS    0H                                                       00356
         CLC   0(L'SORTKEY,R8),SORTKEY IS THIS RECORD < CURRENT?        00357
         BNL   SORT1$03                NO: TRY NEXT RECORD              00358
         XC    SORTLINE,0(R8)  ----+                                    00359
         XC    0(80,R8),SORTLINE   +-- YES: SWAP 'EM                    00360
         XC    SORTLINE,0(R8)  ----+                                    00361
*                                                                       00362
SORT1$03 LA    R8,80(,R8)              BUMP COMPARE POINTER             00363
         BCT   R7,SORT1$02             ITERATE R7 TIMES                 00364
         LA    R9,80(,R9)              BUMP BASE POINTER                00365
         BCT   R6,SORT1$01             ITERATE R6 TIMES                 00366
*                                                                       00367
SORT1END L     R11,SORT1-4          GET RETURN ADDRESS                  00368
         BR    R11                  AND RETURN                          00369
*                                                                       00370
******************************                                          00371
** TABLE DISPLAY ROUTINE    **                                          00372
******************************                                          00373
         DS    F                                                        00374
DISP1    ST    R11,DISP1-4          SAVE RETURN ADDRESS                 00375
         LH    R6,DSNSAVE              RETRIEVE NUMBER OF DATASETS      00376
         CVD   R6,DBLWORK              CONVERT NUMBER                   00377
         UNPK  DSNCOUNT,DBLWORK+6(2)    OF DATASETS                     00378
         OI    DSNCOUNT+2,X'F0'          TO DISPLAY FORMAT              00379
         CLI   DSNCOUNT,C'0'           ONLY TWO SIGNIFICANT DIGITS?     00380
         BNE   DISP1$01                NO: GO DISPLAY LINE              00381
         MVC   HDR1(HDR1LEN-1),HDR1+1  YES: SHIFT AND DESTROY           00382
         CLI   DSNCOUNT,C'0'           ONLY ONE SIGNIFICANT DIGIT??     00383
         BNE   DISP1$01                NO: GO DISPLAY LINE              00384
         MVC   HDR1(HDR1LEN-1),HDR1+1  YES: SHIFT AND DESTROY           00385
*                                                                       00386
DISP1$01 DS    0H                                                       00387
         MVC   PLDATA(HDR1LEN),HDR1    AND DISPLAY                      00388
         BAL   R11,PUTLINE              MSG LINE                        00389
*                                                                       00390
DISP1$02 DS    0H                                                       00391
         MVI   PLDATA,C' '             DISPLAY                          00392
         BAL   R11,PUTLINE              BLANK LINE                      00393
         MVC   PLDATA(HDR2LEN),HDR2    DISPLAY                          00394
         BAL   R11,PUTLINE              MSG LINE                        00395
         MVC   PLDATA(HDR3LEN),HDR3    DISPLAY                          00396
         BAL   R11,PUTLINE              MSG LINE                        00397
*                                                                       00398
         L     R9,TABLBEG              START OF TABLE                   00399
         MVC   PREVDDN,=8C' '          INITIALIZE DDNAME HOLD AREA      00400
DISP1$03 DS    0H                                                       00401
         CLC   SORTDDN,PREVDDN         SAME DDNAME?                     00402
         BNE   DISP1$04                NO: USE IT                       00403
         MVC   SORTDDN,=8C' '          YES: BLANK IT OUT                00404
         B     DISP1$05                AND GO DISPLAY                   00405
DISP1$04 DS    0H                                                       00406
         MVC   PREVDDN,SORTDDN         SET NEW DDNAME HOLD AREA         00407
DISP1$05 DS    0H                                                       00408
         MVC   SORTTAG,=C'  '          ERASE TAG CHARACTER              00409
         MVC   SORTLTRL,=C'DD'         FILL IN 'DD'                     00410
         MVC   PLDATA(L'SORTLINE-1),SORTLINE  DISPLAY                   00411
         BAL   R11,PUTLINE                     DATALINE                 00412
         LA    R9,80(,R9)              BUMP BASE POINTER                00413
         BCT   R6,DISP1$03             ITERATE                          00414
DISP1END L     R11,DISP1-4          GET RETURN ADDRESS                  00415
         BR    R11                  AND RETURN                          00416
*                                                                       00417
***************************                                             00418
** FREEMAIN ROUTINE      **                                             00419
***************************                                             00420
         DS    F                                                        00421
FREE1    ST    R11,FREE1-4          SAVE RETURN ADDRESS                 00422
         L     R1,TABLBEG                                               00423
         FREEMAIN RC,LV=20000,A=(R1)                                    00424
         L     R11,FREE1-4          GET RETURN ADDRESS                  00425
         BR    R11                  AND RETURN                          00426
*                                                                       00427
***************************                                             00428
** PUTLINE ROUTINE       **                                             00429
***************************                                             00430
         DS    F                                                        00431
PUTLINE  ST    R11,PUTLINE-4        SAVE RETURN ADDRESS                 00432
         PUTLINE OUTPUT=(PLLINE,,,DATA),MF=(E,MYIOPL)                   00433
         MVI   PLDATA,C' '                 BLANK OUT                    00434
         MVC   PLDATA+1(L'PLDATA-1),PLDATA   DATA LINE                  00435
         L     R11,PUTLINE-4        GET RETURN ADDRESS                  00436
         BR    R11                  AND RETURN                          00437
*                                                                       00438
***************************                                             00439
** DATA AREAS            **                                             00440
***************************                                             00441
MVCIT1   MVC   INPUTDD(0),0(R2)        EXECUTED                         00442
COMPIT1  CLC   SORTDDN(0),INPUTDD      EXECUTED                         00443
PUTLISTF PUTLINE MF=L                                                   00444
*                                                                       00445
DBLWORK  DS    D                                                        00446
TABLBEG  DS    F                                                        00447
TABLEND  DS    F                                                        00448
PARMADDR DS    F                                                        00449
RETCODE  DC    F'0'                                                     00450
MYECB    DC    F'0'       FOR PUTLINE                                   00451
MYIOPL   DC    4F'0'      FOR PUTLINE                                   00452
PARMLEN  DC    H'0'                                                     00453
COMPLEN  DC    H'7'       ASSUME NON-GENERIC (IE, FULL-LENGTH) REQUEST  00454
DSNSAVE  DC    H'0'                                                     00455
INPUTDD  DC    CL8' '                                                   00456
PREVDDN  DS    CL8' '                                                   00457
ERRFLAG1 DC    C'N'                                                     00458
ERRFLAG2 DC    C'N'                                                     00459
*                                                                       00460
PLLINE   DC    H'84',H'0'   DATA LENGTH + 4 FOR THESE 2 HALFWORDS       00461
PLDATA   DC    CL80' '                                                  00462
*                                                                       00463
HDR1     DS    0C                                                       00464
DSNCOUNT DS    PL3                                                      00465
         DC    C' DATASET(S) CURRENTLY ALLOCATED. '                     00466
HDR1LEN  EQU   *-HDR1                                                   00467
HDR2     DS    0C                                                       00468
         DC    CL7'DDNAME:'                                             00469
         DC    5C' '                                                    00470
         DC    CL13'DATASET NAME:'                                      00471
         DC    42C' '                                                   00472
         DC    CL11'VOLSR: DSP:'                                        00473
HDR2LEN  EQU   *-HDR2                                                   00474
HDR3     DS    0C                                                       00475
         DC    CL7'------ '                                             00476
         DC    5C' '                                                    00477
         DC    CL13'------- ---- '                                      00478
         DC    42C' '                                                   00479
         DC    CL11'-----  --- '                                        00480
HDR3LEN  EQU   *-HDR3                                                   00481
*                                                                       00482
ERRMSG00 DC    C'ERROR: LISTDD MUST BE INVOKED AS A TSO CMD PROCESSOR.' 00483
ERRMSG13 DC    C'DDNAME MUST BE 8 CHARACTERS OR LESS IN LENGTH.'        00484
ERRMSG30 DC    C'DDNAME '                                               00485
ERR30DDN DC    CL8' '                                                   00486
ERR30LN1 EQU   *-ERRMSG30                                               00487
         DC    CL30' '                                                  00488
ERRMSG31 DC    C'IS NOT CURRENTLY ALLOCATED.'                           00489
ERR31LN1 EQU   *-ERRMSG31                                               00490
ERR30LEN EQU   ERR30LN1+ERR31LN1+1                                      00491
ERRMSG40 DC    C'UNFORTUNATELY, THIS IS TOO MANY FOR LISTDD TO SORT '   00492
         DC    C'AND DISPLAY.  TERMINATING.'                            00493
ERR40LEN EQU   *-ERRMSG40                                               00494
         DSECT                                                          00495
SORTLINE DS    0CL80                                                    00496
SORTKEY  DS    0CL10                                                    00497
SORTDDN  DS    CL8                                                      00498
SORTTAG  DS    XL2                                                      00499
         ORG   SORTTAG+1                                                00500
SORTLTRL DS    CL2                                                      00501
         DS    CL1                                                      00502
SORTDSN  DS    CL44                                                     00503
         DS    CL11     SPACE FOR MEMBER NAME IF FOUND                  00504
SORTVOLS DS    CL6        (VOLUME LIST TRUNCATED TO 1 VOLSER)           00505
         DS    CL1                                                      00506
SORTDISP DS    CL3                                                      00507
         ORG   SORTLINE+80                                              00508
         EJECT                                                          00509
*                                                                       00510
*  MVS MAPPING MACROS                                                   00511
*                                                                       00512
         CSECT                                                          00513
         IHAPSA                                                         00514
         IKJTCB                                                         00515
TIOT     DSECT                                                          00516
         IEFTIOT1                                                       00517
JFCB     DSECT                                                          00518
         IEFJFCBN                                                       00519
         IKJCPPL                                                        00520
         IKJIOPL                                                        00521
         END   LISTDD                                                   00522
