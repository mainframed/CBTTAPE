The Display Volume command

      %GETDEVNM

GETUADDR ASSEMBLER ROUTINE

         TITLE 'DISPLAY VOLUME COMMAND'
ZERO     EQU   X'00'                   ZERO
SPACE    EQU   C' '                    SPACE
SIGNF    EQU   X'F0'                   POSITIVE SIGN
GETUADDR CSECT
GETUADDR AMODE 24
GETUADDR RMODE 24
         BAKR  R14,0                   SAVE CALLER'S ARS + GPRS
*                                      IN THE LINKAGE STACK
         USING GETUADDR,R12            SET UP PROGRAM BASE REGISTER
         LAE   R12,0(R15,0)            SET UP PROGRAM BASE REGISTER
         LM    R3,R8,0(R1)             LOAD PARM @
         USING RVOLUME,R3              INFORM THE ASSEMBLER
         USING DEVNUM,R4               INFORM THE ASSEMBLER
         USING DSTATUS,R5              INFORM THE ASSEMBLER
         USING PATHAREA,R6             INFORM THE ASSEMBLER
         USING RETCODE,R7              INFORM THE ASSEMBLER
         USING REASONC,R8              INFORM THE ASSEMBLER
GETWORK  EQU   *
         L     R9,=AL4(WORKALEN)       WORK AREA LENGTH
         STORAGE OBTAIN,LENGTH=(R9),ADDR=(R10),SP=0,KEY=8,             X
               LOC=BELOW,COND=NO,RELATED=(FREEWORK,'FREE WORK AREA')
         LR    R13,R1                  @ THE WORKAREA
         USING SAVEAREA,R13            INFORM THE ASSEMBLER
         MVC   PREVSA,=C'F1SA'         PUT ACRONYM INTO SAVEAREA
*                                      TO INDICATE STATUS SAVED ON
*                                      THE LINKAGE STACK
MAINLINE EQU   *
         MVC   RC,=X'00000000'         SET THE RETURN CODE
         MVC   RSCODE,=X'00000000'     SET THE REASON CODE
         BAL   R2,UCBSCAN              GET DEVICE NUM FOR VOLSER
RETURN   EQU   *
         LAE   R1,0(R13,0)             ADDRESS TO FREE
         L     R9,=AL4(WORKALEN)       WORK AREA LENGTH
         STORAGE RELEASE,ADDR=(R1),LENGTH=(R9),SP=0,KEY=8,             X
               COND=NO,RELATED=(GETWORK,'OBTAIN WORK AREA')
         SLR   R15,R15                 SET RC TO ZERO
         PR                            RESTORE CALLER'S ARS
*                                      GPRS 2-14 AND RETURN
*                                      TO CALLER
         TITLE 'SCAN THE UCBS TO OBTAIN THE UCB ADDRESSES'
UCBSCAN  EQU   *
         XC    IOTOKEN,IOTOKEN         ZEROIZE
         XC    UCBSWA,UCBSWA           ZEROIZE
         MVC   VOLUME,VOL              VOLUME TO CHECK
UCBCHECK EQU   *
         MVC   UCBLPARM,UCBPARM        MOVE UCBSCAN PARM AREA
         UCBSCAN COPY,                                                 X
               WORKAREA=UCBSWA,                                        X
               UCBAREA=UCBCS,                                          X
               CMXTAREA=UCBES,                                         X
               UCBPAREA=UCBPES,                                        X
               DCEAREA=DCEES,                                          X
               DCELEN=DCEESLEN,                                        X
               VOLSER=VOLUME,                                          X
               DYNAMIC=YES,                                            X
               RANGE=ALL,                                              X
               IOCTOKEN=IOTOKEN,                                       X
               DEVCLASS=DASD,                                          X
               DEVNCHAR=DNUM,                                          X
               LINKAGE=SYSTEM,                                         X
               RETCODE=UCBRTCDE,                                       X
               RSNCODE=UCBRSCDE,                                       X
               MF=(E,UCBLPARM,COMPLETE)
         LTR   R15,R15                 UCB RETURNED?
         BNZ   UCBCERR                 UCB SCAN ERROR
         LA    R9,UCBCS                @ UCB
         USING UCB,R9                  INFORM THE ASSEMBLER
DVCODE   EQU   *
         MVC   DEVCODE,UCBTBYT4        DEVICE CODE
ONOROFF  EQU   *
         MVC   STATUS,=CL7'ONLINE '    DEVICE ON-LINE
         TM    UCBSTAT,UCBONLI         DEVICE ON-LINE?
         BO    PENDING                 YES-
         MVC   STATUS,=CL7'OFFLINE'    DEVICE ON-LINE
PENDING  EQU   *
         TM    UCBSTAT,UCBCHGS         CHANGING STAT?
         BNO   ALLOC                   NO
         MVC   STATUS,=CL7'PENDING'    DEVICE ON-LINE
ALLOC    EQU   *
         MVI   VALLOC,C'Y'             DEVICE IS ALLOCATED
         TM    UCBSTAT,UCBALOC         DEVICE ALLOCATED?
         BO    SMSINDC                 YES-
         MVI   VALLOC,C'N'             DEVICE NOT ALLOCATED
SMSINDC  EQU   *
         MVI   SMSIND,C'Y'             DEFAULT
         TM    UCBFL5,UCBSMS           SMS DEVICE?
         BO    CHKUSE                  YES-
         MVI   SMSIND,C'N'             NO-
CHKUSE   EQU   *
         MVI   VOLUSE,C'1'             PRIVATE
         TM    UCBSTAB,UCBBPRV         PRIVATE?
         BO    CHKMOUNT                YES-
         MVI   VOLUSE,C'2'             PUBLIC
         TM    UCBSTAB,UCBBPUB         PUBLIC?
         BO    CHKMOUNT                YES-
         MVI   VOLUSE,C'3'             STORAGE
         TM    UCBSTAB,UCBBSTR         STORAGE?
         BO    CHKMOUNT                YES-
         MVI   VOLUSE,C'0'             ?
CHKMOUNT EQU   *
         MVI   VOLMOUNT,C'1'           DEFAULT
         TM    UCBSTAT,UCBRESV         RESERVED?
         BO    SHARED                  YES-
         MVI   VOLMOUNT,C'2'           DEFAULT
         TM    UCBSTAT,UCBPRES         PERMANENTLY RESERVED?
         BO    SHARED                  NO-
         MVI   VOLMOUNT,C'0'           ?  -
SHARED   EQU   *
         MVI   VOLSHARE,C'Y'           DEFAULT
         TM    UCBTBYT2,UCBRR          SHARED VOLUME?
         BO    SYSRSVOL                YES-
         MVI   VOLSHARE,C'N'           DEFAULT
SYSRSVOL EQU   *
         MVI   SYSRES,C'N'             DEFAULT
         TM    UCBSTAT,UCBSYSR         SYSRES VOLUME?
         BNO   CHKUCBX                 NO-
         MVI   SYSRES,C'Y'             YES-
CHKUCBX  EQU   *
         LA    R11,UCBPES              @ UCB PREFIX AREA
         USING UCBPDATA,R11            INFORM THE ASSEMBLER
         MVI   PAGEDEV,C'N'            SET DEFAULT
         TM    UCBMIHTI,UCBPGDEV       PAGING DEVICE?
         BNO   DASDRSV                 NO-
         MVI   PAGEDEV,C'Y'            SET DEFAULT
DASDRSV  EQU   *
         MVI   RESERVED,C'N'           DEFAULT
         TM    UCBIOSF1,UCBRESVH       DEVICE RESERVED?
         BNO   MOVEISC                 NO-
         MVI   RESERVED,C'Y'           YES-
MOVEISC  EQU   *
         XR    R10,R10                 CLEAR
         ICM   R10,B'0011',UCBPMCW1    PICK UP INTERRUPTION SUBCLASS
         SRL   R10,11(0)               LASTBYTE= 00000ISC
         N     R10,=X'00000007'        TURN OFF ALL BITS EXCEPT LAST
*                                      THREE BITS
         STC   R10,ISC                 STORE FOR CALLER
SCPROG   EQU   *
         LA    R11,UCBES               @ UCB COMMON EXTENSION
         USING UCBCMEXT,R11            INFORM THE ASSEMBLER
         MVI   SUSPCP,C'N'             DEFAULT
         TM    UCBFL7,UCBSSPND         SUSPENDED CHANNEL PROGRAM?
         BNO   INFOUCB                 NO-
         MVI   SUSPCP,C'Y'             DEFAULT
INFOUCB  EQU   *
         MVC   UCBINFP,UCBINF          MOVE UCBINF PARM AREA
         UCBINFO PATHINFO,                                             X
               PATHAREA=PATHINFO,                                      X
               DEVN=UCBCHAN,                                           X
               LINKAGE=SYSTEM,                                         X
               RETCODE=UCBIRTCD,                                       X
               RSNCODE=UCBIRSCD,                                       X
               MF=(E,UCBINFP,COMPLETE)
         LTR   R15,R15                  UCBINFO OKAY?
         BNZ   UCBIERR                  NO-
         DROP  R9                       INFORM THE ASSEMBLER
         LA    R9,PATHINFO              @ THE PATH INFO
         USING PATH,R9                  INFORM THE ASSEMBLER
         MVC   PATHCNT,PATH#CHPIDS      NO OF INSTALLED PATHS
         LA    R6,L'PATHCNT(,R6)        RE-POSITION
         ICM   R10,B'1111',PATH#CHPIDS  NO OF INSTALLED PATHS
         BZR   R2                       RETURN TO CALLER
PATHMOVE EQU   *
         MVC   CHPID,PATHCHPID          CHPID
         MVC   PBITS,PATHBITS           PATH BITS
         MVC   PTYPE,PATHINTTYPE        PATH TYPE
         LA    R6,PCHPLEN(,R6)          NEXT TABLE ENTRY
         LA    R9,L'PATHCHPIDARRAY(,R9) NEXT TABLE ENTRY
         BCT   R10,PATHMOVE             DO WHILE R10 > 0?
EXIT     EQU   *
         BR    R2                       RETURN TO CALLER
         TITLE 'ERROR SECTION'
UCBIERR  EQU   *
         STCM  R15,B'1111',RC           RETURN CODE
         MVC   RSCODE,UCBIRSCD          REASON CODE
         MVI   PATHCNT,C'I'             UCBINFO ERROR
         BR    R2                       RETURN TO CALLER
UCBCERR  EQU   *
         STCM  R15,B'1111',RC           RETURN CODE
         MVC   RSCODE,UCBRSCDE          REASON CODE
         MVI   PATHCNT,C'C'             UCBCOPY ERROR
         BR    R2                       RETURN TO CALLER
         LTORG
UCBPARMA DS    CL1                      UCBSCAN AREA
         UCBSCAN MF=(L,UCBPARM)
         UCBINFO MF=(L,UCBINF)
UCBLSIZE EQU   1                        NO OF UCB ADDRESSES
DEVSIZE  EQU   36                       RETURNED INFO LENGTH
DCEESLEN DC    AL2(128)                 DCE EXTENSION SEGMENT LENGTH
WORKAREA DSECT
SAVEAREA DS    CL72                     SAVEAREA
PREVSA   EQU   SAVEAREA+4,4             @ OF PREVIOUS SAVEAREA
UCB@     DS    AL4                      UCB ADDRESS
UCBPFX@  DS    AL4                      UCB PREFIX ADDRESS
UCBSWA   DS    CL100                    UCBSCAN WORK AREA
UCBCS    DS    CL48                     UCB COMMON SEGMENT
UCBES    DS    CL32                     UCB EXTENSION SEGMENT
UCBPES   DS    CL32                     UCB PREFIX EXTENSION SEGMENT
DCEES    DS    CL128                    DCE EXTENSION SEGMENT
VOLUME   DS    CL6                      VOLUME
DEVNO    DS    CL4                      CURRENT DEVICE NO
IOTOKEN  DS    CL48                     DYNAMIC I/O TOKEN
UCBRTCDE DS    F                        RETURN CODE
UCBRSCDE DS    F                        REASON CODE
UCBLPARM DS    CL(UCBPARML)             UCBSCAN PARAMETER AREA
         DS    0D
UCBINFP  DS    CL(UCBINFL)              UCBINF PARAMETER AREA
PATHINFO DS    XL256                    UCBINFO PATH INFORMATION
         DS    0F
UCBIRTCD DS    XL4                      UCBINFO RETURN CODE
UCBIRSCD DS    XL4                      UCBINFO REASON CODE
WORKALEN EQU   *-WORKAREA               WORK AREA LENGTH
RVOLUME  DSECT
VOL      DS    CL6                      VOLUME SERIAL NO
DEVNUM   DSECT
DNUM     DS    CL4                      DEVICE NUMBER
DSTATUS  DSECT
STATUS   DS    CL7                      STATUS
PATHAREA DSECT
SMSIND   DS    C                        SMS INDICATOR
VOLMOUNT DS    C                        MOUNT ATTRIBUTE
VOLUSE   DS    C                        USE ATTRIBUTE
VOLSHARE DS    C                        SHARED DASD
VALLOC   DS    C                        DEVICE ALLOCATED
PAGEDEV  DS    C                        PAGING DEVICE
ISC      DS    X                        INTERRUPT SUBCLASS MASK
SUSPCP   DS    C                        SUSPENDED CHANNEL PROGRAM
DEVCODE  DS    X                        DEVICE CODE FROM UCBTBYT4
RESERVED DS    C                        DASD RESERVED BYTE
SYSRES   DS    C                        SYSRES DEVICE
PATHCNT  DS    XL4                      NO OF CHANNEL PATHS
         ORG   PATHCNT
CHPID    DS    XL2                      CHANNEL PATHS
PBITS    DS    X                        PATH BITS
PTYPE    DS    X                        PATH TYPE
PCHPLEN  EQU   *-CHPID                  ENTRY LENGTH
RETCODE  DSECT
RC       DS    AL4                      RETURN CODE
REASONC  DSECT
RSCODE   DS    AL4                      REASON CODE
UCB      DSECT
         IEFUCBOB                       UCB MACRO ID
         IOSDUPI
         IOSDPATH
         CVT   DSECT=YES,LIST=NO
         END   GETUADDR

GETDEVNM REXX EXEC

/**rexx**/
/*  PROGRAM-ID           GETDEVNM                                  */
/*  Remarks              This REXX exec will return the unit       */
/*                       number for a volume.                      */
/*trace i*/
save_prompt = prompt("off")
dnum= '    '                                /* device number       */
status= '       '                           /* device status       */
retcode= '00000000'x                        /* ucbscan return code */
pathinfo= '                                                '
reason_code= '00000000'x                    /* ucbscan reason code */
address "ISPEXEC" "LIBDEF ISPPLIB DATASET ID('XXXXXX.PANELS')"
main_processing_loop:
do forever
   call display_the_volume_panel            /* display vol panel   */
   ADDRESS "ISPEXEC" "REMPOP"               /* remove pop-up       */
end
display_the_volume_panel:
ADDRESS "ISPEXEC" "ADDPOP ROW(1) COLUMN(2)" /* pop-up position  */
ADDRESS "ISPEXEC" "DISPLAY PANEL(GETDEVP1)"
if (rc > 8) then do                         /* error?              */
   say 'Display error    rc = 'rc''         /* yes- output message */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"       /* remove allocation   */
   exit(0)                               /* and quit               */
end
if (rc = 8) then do                      /* return?                */
   ADDRESS "ISPEXEC" "REMPOP"            /* remove pop-up          */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(0)                               /* and quit               */
end
call Lets_Lock_The_Terminal              /* lock the terminal      */
ADDRESS "LINKPGM" "GETUADDR  volser dnum status pathinfo retcode reason_code"
if (retcode > '00000000'x) then do
   ADDRESS "ISPEXEC" "REMPOP"            /* remove pop-up          */
   call display_error_panel              /* display the error panel*/
   return                                /* return to caller       */
end
ADDRESS "ISPEXEC" "TBCREATE DEVNTAB NOWRITE REPLACE"
if (rc >  4) then do                     /* call okay?             */
   say 'tbcreate error    rc = 'rc''     /* no- inform the user    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(0)                               /* let's quit             */
end
ztdmark= ' '
ADDRESS  "ISPEXEC" "VPUT    (ztdmark) SHARED"
tabrows=  9999                           /* max table rows         */
x= 16                                    /* starting position      */
no_of_chpids= x2d(c2x(substr(pathinfo,12,4)))
if (no_of_chpids = 0) then do
   ADDRESS "ISPEXEC" "REMPOP"             /* remove pop-up         */
   pinfo= '***  No Paths Defined ***'     /* inform user           */
   ADDRESS "ISPEXEC" "ADDPOP ROW(1) COLUMN(5)" /* pop-up position  */
   ADDRESS "ISPEXEC" "DISPLAY PANEL(getdevp3)"
   if (rc > 8) then do                    /* error?                */
      say 'Display error    rc = 'rc''    /* yes- output message   */
      ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"  /* remove allocation     */
      exit(0)                             /* and quit              */
   end
   ADDRESS "ISPEXEC" "REMPOP"             /* remove pop-up         */
   return                                 /* return to caller      */
end
sms= 'YES'                                /* set sms to yes        */
if  substr(pathinfo,1,1) = 'N' then       /* sms device?           */
    sms= 'NO '                            /* no-                   */
vstatus= ' '                              /* init                  */
select                                    /* determine vol status  */
    when status = 'ONLINE'  then
          vstatus= 'Online  '
    when status = 'OFFLINE'  then
          vstatus= 'Offline  '
    when status = 'PENDING'  then
          vstatus= 'Pending Offline  '
   otherwise
          nop
end
select                                   /* volume allocation      */
    when substr(pathinfo,5,1) = 'Y' then
          vstatus= vstatus||'Alloc  '
    when substr(pathinfo,5,1) = 'N' then
          vstatus= vstatus||'Not-Alloc  '
   otherwise
          nop
end
select                                   /* sysres                 */
    when substr(pathinfo,11,1) = 'Y' then
          vstatus= vstatus||'SYSRES  '
   otherwise
          nop
end
select                                   /* dasd reserve           */
    when substr(pathinfo,10,1) = 'Y' then
          vstatus= vstatus||'Reserved'
   otherwise
          nop
end
mntstat= ' '                             /* init                   */
select                                   /* determine vol res      */
    when substr(pathinfo,2,1) = 1 then
          mntstat= 'Resident  '
    when substr(pathinfo,2,1) = 2 then
          mntstat= 'Perm Res  '
   otherwise
          nop
end
select                                   /* determine vol status   */
    when substr(pathinfo,3,1) = 1 then
          mntstat= mntstat||'Private  '
    when substr(pathinfo,3,1) = 2 then
          mntstat= mntstat||'Public  '
    when substr(pathinfo,3,1) = 3 then
          mntstat= mntstat||'Storage  '
   otherwise
          nop
end
select                                   /* shared dasd            */
    when substr(pathinfo,4,1) = 'Y' then
          mntstat= mntstat||'Shared'
   otherwise
          nop
end
devstat=  ' '                            /* init                   */
select                                   /* device type            */
    when substr(pathinfo,9,1) = '0B'x then
          devstat= '3350  '
    when substr(pathinfo,9,1) = '0C'x then
          devstat= '3375  '
    when substr(pathinfo,9,1) = '0E'x then
          devstat= '3380  '
    when substr(pathinfo,9,1) = '0F'x then
          devstat= '3390  '
   otherwise
          nop
end
if substr(pathinfo,8,1) = 'Y' then       /* suspended channel prog?*/
   devstat= devstat||'Suspended Channel Program  '
if substr(pathinfo,6,1) = 'Y' then       /* paging device?         */
   devstat= devstat||'<Paging Device>'
ISC=  x2d(c2x(substr(pathinfo,7,1)))     /* Interrupt Subclass     */
do i= 1 to no_of_chpids                  /* do no of table entries */
   path=  c2x(substr(pathinfo,x+1,1))    /* path                   */
   select                                /* Determine Path Type    */
         when (substr(pathinfo,x+3,1)) = '01'x then
              pathtyp= 'Parallel Block Multiplexor'
         when (substr(pathinfo,x+3,1)) = '02'x then
              pathtyp= 'Parallel Byte Multiplexor'
         when (substr(pathinfo,x+3,1)) = '03'x then
              pathtyp= 'ESCON Point To Point'
         when (substr(pathinfo,x+3,1)) = '04'x then
              pathtyp= 'ESCON '
         when (substr(pathinfo,x+3,1)) = '05'x then
              pathtyp= 'ESCON Switch Point To Point'
         when (substr(pathinfo,x+3,1)) = '06'x then
              pathtyp= 'FIBER Extended'
         when (substr(pathinfo,x+3,1)) = '07'x then
              pathtyp= 'ESCON Native'
   otherwise
              pathtyp= 'UNKNOWN'
   end
   if substr(pathinfo,x+2,1) = '06'x then do /* path on-line?      */
      pathstat= 'Path ONLINE'              /* yes                  */
      call create_table_entry              /* create a table entry */
   end
   path_status= substr(pathinfo,x+2,1)     /* path status byte     */
   path_status= bitand(path_status,'20'x)  /* isolate bit required */
   if (path_status = '20'x) then do      /* path off-line due escm?*/
      pathstat= 'Path OFFLINE Due To ESCM' /* yes                  */
      call create_table_entry              /* create a table entry */
   end
   path_status= substr(pathinfo,x+2,1)     /* path status byte     */
   path_status= bitand(path_status,'10'x)  /* isolate bit required */
   if (path_status = '10'x) then do        /* path off-line due op?*/
      pathstat= 'Path OFFLINE Due To Operator'
      call create_table_entry              /* create a table entry */
   end
   path_status= substr(pathinfo,x+2,1)     /* path status byte     */
   path_status= bitand(path_status,'08'x)  /* isolate bit required */
   if (path_status = '08'x) then do    /* path off-line due cu rcv?*/
      pathstat= 'Path OFFLINE Due CU Recovery'
      call create_table_entry            /* create a table entry   */
   end
   path_status= substr(pathinfo,x+2,1)   /* path status byte       */
   path_status= bitand(path_status,'01'x) /* isolate bit required  */
   if (path_status = '01'x) then do      /* vary off-line in prog? */
      pathstat= 'Path OFFLINE In Progress'
      call create_table_entry            /* create a table entry   */
   end
   x= (x + 4)                            /* next table entry       */
end
ADDRESS "ISPEXEC" "REMPOP"               /* remove pop-up          */
ADDRESS "ISPEXEC" "ADDPOP ROW(1) COLUMN(2)" /* pop-up position     */
ADDRESS "ISPEXEC" "TBTOP DEVNTAB"        /* position to top of tab */
if (rc ^= 0) then do                     /* call okay?             */
   say 'tbtop error    rc = 'rc''        /* no- inform the user    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(0)                               /* let's quit             */
end
ADDRESS "ISPEXEC" "TBDISPL DEVNTAB PANEL(GETDEVP3)"
if (rc > 8) then do                      /* error?                 */
   say 'tbdispl error    rc = 'rc''      /* yes- output message    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(0)                               /* and quit               */
end
ADDRESS "ISPEXEC" "TBCLOSE  DEVNTAB"     /* close the table        */
if (rc ^= 0) then do                     /* call okay?             */
   say 'tbtop error    rc = 'rc''        /* no- inform the user    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(0)                               /* lets quit              */
end
ADDRESS "ISPEXEC" "REMPOP"               /* remove pop-up          */
return
Lets_Lock_The_Terminal:
ADDRESS "ISPEXEC" "CONTROL DISPLAY LOCK" /* lock the terminal      */
if (rc > 8) then do                      /* error?                 */
   say 'Lock error    rc = 'rc''         /* yes- output message    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(0)                               /* and quit               */
end
ADDRESS "ISPEXEC" "ADDPOP ROW(3) COLUMN(7)" /* pop-up position     */
ADDRESS "ISPEXEC" "DISPLAY PANEL(GETDEVP2)"
if (rc > 8) then do                      /* error?                 */
   say 'Display error    rc = 'rc''      /* yes- output message    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(0)                               /* and quit               */
end
return
display_error_panel:
select
  when (substr(pathinfo,12,1)) = 'C' then do /* UCBCOPY error?     */
       if (retcode = '00000004'x) & (reason_code = '00000001'x) then
           merr= 'VOLSER Not ONLINE'
       else
           merr= 'Error Has Occurred during call to UCBCOPY'
       end
  when (substr(pathinfo,12,1)) = 'I' then do /* UCBINFO Error?     */
       merr= 'Error Has Occurred during call to UCBINFO'
       end
otherwise
      nop                                /* nop                    */
end
urtc=  c2x(retcode)                      /* convert to character   */
ursc=  c2x(reason_code)                  /* convert to character   */
ADDRESS "ISPEXEC" "ADDPOP ROW(2) COLUMN(7)"  /* pop-up position    */
ADDRESS "ISPEXEC" "DISPLAY PANEL(GETDEVP4)"  /* display error panel*/
if (rc > 8) then do                      /* error?                 */
   say 'Display error    rc = 'rc''      /* yes- output message    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(0)                               /* and quit               */
end
ADDRESS "ISPEXEC" "REMPOP"               /* remove pop-up          */
return
create_table_entry:
ADDRESS "ISPEXEC" "TBADD DEVNTAB
         SAVE(path,pathtyp,pathstat)
         MULT("TABROWS")"
if (rc ^= 0) then do                     /* call okay?             */
   say 'tbadd error    rc = 'rc''        /* no- inform the user    */
   ADDRESS "ISPEXEC" "LIBDEF ISPPLIB"    /* remove allocation      */
   exit(0)                               /* let's quit             */
end
path= ' '                                /* reset                  */
pathtyp= ' '                             /* reset                  */
return

GETDEVP1 PANEL

)ATTR
_ TYPE(INPUT)   INTENS(HIGH) COLOR(YELLOW)
% TYPE(TEXT)    COLOR(RED)
+ TYPE(TEXT)    COLOR(WHITE)
# TYPE(TEXT)    INTENS(HIGH) COLOR(BLUE)
? TYPE(OUTPUT)  INTENS(HIGH) COLOR(RED)
@ TYPE(OUTPUT)  COLOR(RED)
)BODY WINDOW(33,7)
%
%COMMAND ===>_ZCMD      %
%
%
+   Volume Serial No:  _z     %
%
)INIT
.ZVARS= '(volser)'
&ZCMD= ' '
&volser= ' '
.CURSOR= volser
&ZWINTTL=  'Return Device Number'
)REINIT
&ZCMD= ' '
&volser= ' '
.CURSOR= volser
&ZWINTTL=  'Return Device Number'
)PROC
ver (&volser,nb)
)END

GETDEVP2 PANEL

)ATTR
_ TYPE(INPUT)   INTENS(HIGH) COLOR(YELLOW)
% TYPE(TEXT)    COLOR(RED)
* TYPE(TEXT)    COLOR(YELLOW)
+ TYPE(TEXT)    COLOR(WHITE)
# TYPE(OUTPUT)  INTENS(HIGH) COLOR(BLUE)
? TYPE(OUTPUT)  INTENS(HIGH) COLOR(RED)
@ TYPE(OUTPUT)  COLOR(RED)
)BODY WINDOW(54,7)
%
%COMMAND ===>_ZCMD      %         SCROLL ===>_AMT   +
%
%   ------------------------------------------------
%   | Your Request Is Being Processed. Please Wait |
%   ------------------------------------------------
%
)INIT
&ZCMD= ' '
&ZWINTTL=  'Progress Panel'
)REINIT
&ZCMD= ' '
)PROC
)END

GETDEVP3 PANEL

)ATTR
_ TYPE(INPUT)   INTENS(HIGH) COLOR(YELLOW)
% TYPE(TEXT)    COLOR(RED)
* TYPE(TEXT)    COLOR(YELLOW)
+ TYPE(TEXT)    COLOR(WHITE)
# TYPE(OUTPUT)  INTENS(HIGH) COLOR(YELLOW) CAPS(OFF)
? TYPE(OUTPUT)  INTENS(HIGH) COLOR(RED)
@ TYPE(OUTPUT)  COLOR(RED)
)BODY WINDOW(71,17)
%COMMAND ===>_ZCMD      %
%
+Volume Ser Number: #volser+ Device Number:#dnum+ SMS:#sms+
+Status:            #vstatus                          +
+Mount Status:      #mntstat                  +
+Device Status:     #devstat                                         *
+Interrupt Subclass:#z*
%
+?pinfo                    %
+
+ Path Path                           Status
+      Type
+
)MODEL
*#z * #z                          *  #z                              *
)INIT
.ZVARS= '(isc,path,pathtyp,pathstat)'
&ZCMD= ' '
&ZWINTTL=  'Volume Information'
)REINIT
&ZCMD= ' '
)PROC
)END

GETDEVP4 PANEL

)ATTR
_ TYPE(INPUT)   INTENS(HIGH) COLOR(YELLOW)
% TYPE(TEXT)    COLOR(RED)
* TYPE(TEXT)    COLOR(BLUE)
+ TYPE(TEXT)    COLOR(WHITE)
# TYPE(OUTPUT)  INTENS(HIGH) COLOR(BLUE)
? TYPE(OUTPUT)  INTENS(HIGH) COLOR(RED)
@ TYPE(OUTPUT)  COLOR(YELLOW)
)BODY WINDOW(47,9)
%
%COMMAND ===>_ZCMD      %
%
%  ?merr                                     %
%
*   Return Code=@urtc    %
*   Reason Code=@ursc    %
%
)INIT
&ZCMD= ' '
&ZWINTTL=  'Error Panel'
)REINIT
&ZCMD= ' '
)PROC
)END


R F Perretta
Senior Systems Programmer (UK)                                     c Xephon 1995

