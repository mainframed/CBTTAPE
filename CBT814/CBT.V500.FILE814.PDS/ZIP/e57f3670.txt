A 'super scratch' utility

Unfortunately, IBM still steadfastly refuses to acknowledge
that there is a requirement to have more than one dataset of the
same name on different volumes - 'SYS1.' datasets in
particular. This causes problems when it comes to renaming or
deleting old datasets because they are deemed to be 'in use',
which, of course, they are but on different volumes.

This little program gets around this problem by using an
undocumented facility of the SCRATCH macro function - that
is, telling it that the 'SYSDSN/dataset name' ENQ has already
been done. The program is otherwise perfectly standard and
does not use any 'dodgy' techniques. The dataset to be deleted
is specified using a SCRATCH DD statement, prefixing the
dataset name with an 'X.' and specifying the UNIT and up to
20 volumes. Here is some sample JCL:

      //jobname  JOB   ...
      //SCRATCH  EXEC  PGM=SCRATCH
      //STEPLIB  DD DSN=authorized.loadlib,DISP=SHR
      //SCRATCH  DD DSN=X.SYS1.DAE,UNIT=SYSDA,DISP=SHR,
      //      VOL=SER=(vol001,vol002,vol003,...)

The program must be authorized and, therefore, live in an
authorized library: I would recommend that its use is
controlled.

It should be noted that this technique does not work for
renaming datasets.


SCRATCH SOURCE

 TITLE 'SCRATCH, "DATASET" SCRATCH ROUTINE'
**LINK   SETCODE AC(1)
*** THIS PROGRAM WILL "SCRATCH" THE DATASET ASSOCIATED WITH THE
*   "SCRATCH DD" EVEN THOUGH THERE MAY BE ANOTHER DATASET IN USE
*   WITH THE SAME NAME (ON ANOTHER VOLUME).  THIS IT DOES QUITE
*   LEGALLY VIA THE "SCRATCH/CAMLST" MACROS BUT IT TURNS ON THE
*   "BYPASS RACF PROTECTION BIT" WHICH ALSO SKIPS THE "SYSDSN ENQ"
*     THE DATASET NAME MAY BE PRECEDED WITH 'X.' TO AVOID THE
*   "SYSDSN ENQ" DONE BY THE INITIATOR FROM CLASHING.
         MACRO ,
         $ASMDATE ,
         B     4+8+8+8(,R15)
         DC    CL8'&SYSECT'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
         MEND  ,
SCRATCH  CSECT ,
        $ASMDATE ,
         STM   R14,R12,12(R13)
         LR    R12,R15             FIRST BASE
         LR    R9,R1               SAVE OS_PARM POINTER
         USING SCRATCH,R12
         LA    R1,SAVEAREA
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         L     R4,PSATOLD-PSA      "TCB"
         L     R4,TCBJSTCB-TCB(,R4) "JOB-STEP-TCB"
         L     R4,TCBTIO-TCB(,R4)  "TIOT"
         LA    R4,24(,R4)          FIRST "DDNAME"
         SLR   R5,R5
         BALR  R14,0               SET LOOP POINT
         AR    R4,R5               MOVE ONTO NEXT "TIOT" ENTRY
         IC    R5,TIOELNGH-TIOENTRY(,R4) LENGTH OF "TIOT" ENTRY
         LTR   R5,R5               END OF "TIOT"?
         BZ    *+1   *** "SCRATCH" DD STATEMENT MISSING
         CLC   =CL8'SCRATCH',TIOEDDNM-TIOENTRY(R4) THIS "DDNAME"?
         BNER  R14                   NO, THEN MOVE ON
         AR    R5,R4               R5 = ADDRESS AFTER END OF TIOT
         LA    R2,VOLIST+2         GET FIRST "VOLIST" ENTRY
         SLR   R3,R3               RESET "VOLUME" COUNT
         LA    R4,TIOESTTB-TIOENTRY(,R4) GET FIRST "UCB" ADDRESS
TPNXUCB  EQU   *
         SLR   R6,R6
         ICM   R6,B'0111',TIOEFSRT-TIOESTTB(R4) GET UCB ADDRESS
         BZ    *+1                 YES UNIT IS A DUMMY
         USING UCBOB,R6
         MVC   0(4,R2),UCBTYP      MOVE DEVICE TYPE TO "VOLIST"
         MVC   4(6,R2),UCBVOLI     MOVE VOLSER TO "VOLIST"
         MVC   4+6(2,R2),=A(0)     RESET "STATUS_CODE"
         LA    R4,4(,R4)           MOVE ON TO NEXT "UCB" POINTER
         LA    R2,4+6+2(,R2)       MOVE ON TO NEXT "VOLIST" ENTRY
         LA    R3,1(,R3)           INDICATE ANOTHER VOLUME
         CR    R4,R5               Q - LAST UCB DONE?
         BL    TPNXUCB             NO- GO AND DO NEXT
         BH    *+1  *** LOGIC ERROR SCANNING "TIOT"
         STH   R3,VOLIST           SAVE NUMBER OF VOLUMES
        RDJFCB (SCRDCB,)           GET "DATASET_NAME"
         MVC   WTODSNM,JFCBDSNM    COPY "DSNAME"
         CLC   =C'X.',WTODSNM      PREFIXED BY 'X.'?
         BNE   *+1                   NO, THEN ERROR
         MVC   WTODSNM,WTODSNM+2   REMOVE 'X.' PREFIX
         MVC   WTODSNM+L'WTODSNM-2(2),=CL2' ' AND BLANK END
*   "BYPASS RACF PROTECTION BIT" WHICH ALSO SKIPS THE "SYSDSN-ENQ"
         OI    SCRCAMLS+2,X'08'
        MODESET KEY=NZERO,MODE=SUP GET AUTHORITY TO ISSUE COMMAND
         SLR   R0,R0               NO "UCB" SUPPLIED
        SCRATCH SCRCAMLS
         LR    R7,R15              COPY RETURN_CODE
        MODESET KEY=NZERO,MODE=PROB LOSE AUTHORITY
         LTR   R15,R7              COPY AND TEST RETURN_CODE
         NOP   RETALLOK "BZ"         NOT ZERO, ABEND
         BNZ   *+4+6                 NOT ZERO, THEN WAS AN ERROR
           MVC   WTOERR,=CL5' OKAY'  ZERO, THEN PUT "OKAY"
*    RETURN CODE X'00000004' MEANS VOLUME NOT MOUNTED
*                X'00000008'       UNUSUAL CONDITION
*                X'0000000C'       INVALID VOLUME LIST
*    STATUS CODE X'00000000' MEANS ALL "DSCB" DELETED OKAY
*                X'00000001'       "FORMAT-1_DSCB" NOT FOUND
*                X'00000002'       INCORRECT "PASSWORD" SUPPLIED
*                X'00000003'       NOT EXPIRED AND NOT "OVRD"
*                X'00000004'       PERM I/O ERROR
*                X'00000005'       UNABLE TO MOUNT "VOLUME"
*                X'00000006'       UNABLE TO MOUNT "VOLUME"
*                X'00000007'       "DATASET" IS IN USE
*                X'00000008'       "RACF" NOT AUTHORIZED
         ST    R15,WORK            RETURN CODE
         LA    R2,VOLIST+2         GET FIRST "VOLIST" ENTRY
         LH    R3,VOLIST           RESET "VOLUME" COUNT
ERRVOLLP EQU   *
         MVC   WTOVOL,4(R2)        "VOLUME_SERIAL"
         UNPK  WTORC(L'WTORC+1),WORK+2(2+1) CONVERT
         TR    WTORC,HEXTAB-C'0'              TO PRINTABLE HEX
         MVI   WTORC+L'WTORC,C'/'
         UNPK  WTOST(L'WTOST+1),4+6(2+1,R2) "STATUS_CODE"
         TR    WTOST,HEXTAB-C'0'              TO PRINTABLE HEX
         MVI   WTOST+L'WTOST,C''''
         SLR   R14,R14
         ICM   R14,B'0011',4+6(R2) "STATUS_CODE"
         SLL   R14,5
         LA    R14,LOCERRTB(R14)   POINT AT REASON MESSAGE
         MVC   WTOMSG,0(R14)       REASON MESSAGE TO WTO
ERRWTOA WTO    'SCRATCH - "SCRATCH" ERROR FOR DSN "12345678901234567890*
               123456789012345678901234", VOL "VVVVVV",',              *
               ROUTCDE=11
WTOACTN  EQU   ERRWTOA+8+11,7,C'C'  ACTION "SCRATCH" OR "RENAME"
WTOERR   EQU   ERRWTOA+8+20,5,C'C'  'ERROR' OR 'OKAY'
WTODSNM  EQU   ERRWTOA+8+35,44,C'C' DATASET NAME
WTOVOL   EQU   ERRWTOA+8+87,6,C'C'  VOLUME
ERRWTOB WTO    'SCRATCH - "SCRATCH" R15/STATUS=X''9999/0000'' - 1234567*
               890123456789012345678901',ROUTCDE=11
WTORC    EQU   ERRWTOB+8+33,4,C'C'  RETURN CODE IN "R15"
WTOST    EQU   ERRWTOB+8+38,4,C'C'  STATUS CODE IN "VOL_LIST"
WTOMSG   EQU   ERRWTOB+8+46,31,C'C' REASON MESSAGE
         LA    R2,4+6+2(,R2)       MOVE ON TO NEXT "VOLIST" ENTRY
         BCT   R3,ERRVOLLP          AND LOOP BACK FOR NEXT VOLUME
RETALLOK EQU   *
         LR    R15,R7              COPY RETURN_CODE
         L     R13,4(,R13)
        RETURN (14,12),RC=(15)
LOCERRTB DC    CL32'ALL "DSCB" DELETED OKAY'          STAT=X00
         DC    CL32'"FORMAT-1_DSCB" NOT FOUND'        STAT=X01
         DC    CL32'INCORRECT "PASSWORD" SUPPLIED'    STAT=X02
         DC    CL32'NOT EXPIRED AND NOT "OVRD"'       STAT=X03
         DC    CL32'PERM I/O ERROR'                   STAT=X04
         DC    CL32'UNABLE TO MOUNT "VOLUME"'         STAT=X05
         DC    CL32'UNABLE TO MOUNT "VOLUME"'         STAT=X06
         DC    CL32'"DATA-SET" IS IN USE'             STAT=X07
         DC    CL32'"RACF" NOT AUTHORIZED'            STAT=X08
         DC    CL32'?'                                STAT=X09
         DC    CL32'?'                                STAT=X0A
         DC    CL32'?'                                STAT=X0B
         LTORG ,
         DC    0D'0'
SCRCAMLS CAMLST SCRATCH,WTODSNM,,VOLIST,,OVRD
SAVEAREA DC    18F'0'
SCRDCB   DCB   DDNAME=SCRATCH,DSORG=PS,MACRF=E,EXLST=EXLST
         DC    0D'0'
EXLST    DC    0F'0',AL1(7+X'80'),AL3(INFMJFCB)
        IEFJFCBN ,
         DC    0D'0',H'0'
VOLIST   DC    H'0',20XL(4+6+2)'00' ENOUGH FOR 20 VOLUMES
WORK     DC    F'0'                WORK AREA FOR CONVERT
HEXTAB   DC    C'0123456789ABCDEF'
IEFTIOT  DSECT ,
        IEFTIOT1 ,
UCB      DSECT ,
        IEFUCBOB ,
        IKJTCB SYS=AOS2,DSECT=YES,LIST=YES
        IHAPSA ,
SCRATCH  CSECT ,
        REGEQU  ,
         END   SCRATCH


Martin Hamilton
Technical Consultant
Thomson Tour Operations Ltd (UK)          c Thomson Tour Operations 1993

