A program to search concatenated libraries

I am sure that all you systems programmers out there know
exactly where all your home-grown and third-party library
members are.  In that case you won't be interested in this little
utility.  Give it a member name and a DDNAME and it will
search through the libraries concatenated to the DDNAME and
tell you which library the member is to be found in.  If you
omit the DDNAME, it will search LINKLIST for you.

To use the program you must allocate a SYSOUT file called
SYSPRINT.  Input comes either from a SYSIN file called
SYSIN, or via a PARM, and is of fixed format.

The JCL required to run FINDER is as follows:

    //FINDER    EXEC PGM=FINDER
    //SYSPRINT  DD  SYSOUT=C
    //LIBRARY   DD  DSN=.......
    //          DD  DSN=.......
    ..          ..  ...........
    //SYSIN     DD  *
    MEMBER    LIBRARY
    /*

Note that the DDNAME must agree with control card and that
the format of the control card is:

¥   Bytes 1-8 Ð member name padded with blanks
¥   Bytes 9-10 Ð blanks
¥   Bytes 11-18 Ð DDNAME padded with blanks.

The member name must be supplied but the DDNAME is
optional.  If it is omitted the LINKLIST will be scanned and if
LINKLIST is specified as a DDNAME then, again, the
LINKLIST will be scanned, and no library DD is required.

Alternatively, the control card information can be supplied as a
PARM.  The format of the PARM is:

¥   Bytes 1-8 Ð member name padded with blanks
¥   Bytes 9-16 Ð DDNAME padded with blanks.

The DDNAME information may be omitted (implying a
LINKLIST search), hence the PARM length must be 8 or 16
bytes.

Under TSO the program could be used to scan ISPF libraries,
for example.  Assuming the concatenation is pre-allocated, the
following commands can be used to run FINDER.

    ALLOC FI(SYSPRINT) DA(*)
    CALL 'finder.load.lib(FINDER)' 'MEMBER  LIBRARY '

Here MEMBER has the same meaning as above, and
LIBRARY is the DDNAME of the set of concatenated
libraries.  Notice that both parts of the PARM have been
padded to 8 characters in length.

FINDER reports the relative position of the dataset in the
concatenation which contains the member.  In other words, if
the member is in the third dataset in the concatenation,
FINDER will report that it is in library 002.

FINDER is a very simple program that issues a BLDL macro,
and analyses the response.  It has proved extremely useful,
particularly when used against LINKLIST.

FINDER   CSECT
         STM   14,12,12(13)       SAVE REGISTERS
         LR    12,15              SET UP
         USING FINDER,12
         ST    13,SAVEAREA+4      SAVE CALLERS SA ADDR IN OUR SA
         LR    2,13               CALLERS SA ADDR TO ANOTHER REG
         LA    13,SAVEAREA        ADDRESS OF OUR SA
         ST    13,8(2)            ADDRESS OF OUR SA TO CALLERS SA
         L     R3,0(R1)           POINT TO PARM
         OPEN  (SYSPRINT,(OUTPUT))
         LTR   R15,R15
         BNE   CANTOPEN
         USING IHADCB,R10
         LA    R10,SYSPRINT
         TM    DCBOFLGS,X'10'
         BZ    ERRSOUT
*        CHECK THE PARMS
         LH    R5,0(R3)
         LTR   R5,R5
         BZ    OPENSYSN
         MVI   PARMTHER,C'Y'
         CH    R5,=H'8'
         BE    NOLIB
         CH    R5,=H'16'
         BNE   BADPARM
         MVC   MODULE,2(R3)
         MVC   LIBRARY,10(R3)
         B     GOTCARD
NOLIB    EQU   *
         MVC   MODULE,2(R3)
         MVC   LIBRARY,=CL8'        '
         B     GOTCARD
OPENSYSN EQU   *
         OPEN  SYSIN
         LTR   R15,R15
         BNE   CANTOPEN
         LA    R10,SYSIN
         TM    DCBOFLGS,X'10'
         BZ    ERRSIN
*        READ THE CONTROL CARD, AND WRITE IT TO SYSPRINT            *
         GET   SYSIN,INCARD
GOTCARD  EQU   *
         MVC   OUTRECM,MODULE
         CLC   LIBRARY(8),=CL8'        '
         BNE   LIBGIVEN
         MVC   LIBRARY(8),=CL8'OMITTED '
LIBGIVEN EQU   *
         MVC   OUTRECL,LIBRARY
         PUT   SYSPRINT,OUTCARD
*        GET THE LIBRARY AND SEE IF IT IS LINKLIST                 *
         CLC   LIBRARY(8),=CL8'OMITTED '
         BE    NODCB
         CLC   LIBRARY(8),=CL8'LINKLIST'
         BNE   NOTLINK
NODCB    EQU   *
         MVC   BLDLNAME,MODULE
         BLDL  0,BLDLLIST
AFTRBLDL EQU   *
         LTR   R15,R15
         BNZ   BLDLERR
         CLI   BLDLZ,X'00'
         BE    PRIVATE
         CLI   BLDLZ,X'01'
         BE    LINKLIB
         CLI   BLDLZ,X'02'
         BE    STEPLIB
* JOB, TASK, OR STEP LIBRARY OF PARENT TASK N, WHERE N = Z-2
         WTO   'JOB, TASK, OR STEP LIBRARY OF PARENT TASK N.....',     X
               ROUTCDE=11
         B     CLOSSHOP
PRIVATE  EQU   *
         SR    R2,R2
         IC    R2,BLDLK
         CVD   R2,WRK8
         UNPK  WRK8(3),WRK8+6(2)
         OI    WRK8+2,X'F0'
         MVI   OUTREC,C' '
         MVC   OUTREC+1(132),OUTREC
         MVC   OUTREC+2(25),=CL25'FOUND IN PRIVATE LIBRARY '
         MVC   OUTREC+27(3),WRK8
         PUT   SYSPRINT,OUTCARD
         B     CLOSSHOP
LINKLIB  EQU   *
         SR    R2,R2
         IC    R2,BLDLK
         CVD   R2,WRK8
         UNPK  WRK8(3),WRK8+6(2)
         OI    WRK8+2,X'F0'
         MVI   OUTREC,C' '
         MVC   OUTREC+1(132),OUTREC
         MVC   OUTREC+2(26),=CL26'FOUND IN LINKLIST LIBRARY '
         MVC   OUTREC+28(3),WRK8
         PUT   SYSPRINT,OUTCARD
         B     CLOSSHOP
STEPLIB  EQU   *
         SR    R2,R2
         IC    R2,BLDLK
         CVD   R2,WRK8
         UNPK  WRK8(3),WRK8+6(2)
         OI    WRK8+2,X'F0'
         MVI   OUTREC,C' '
         MVC   OUTREC+1(132),OUTREC
         MVC  OUTREC+2(36),=CL36'FOUND IN JOB, TASK, OR STEP LIBRARY '
         MVC   OUTREC+38(3),WRK8
         PUT   SYSPRINT,OUTCARD
         B     CLOSSHOP
NOTLINK  EQU   *
         LA    R10,MYDCB
         MVC   DCBDDNAM(8),LIBRARY
         OPEN  MYDCB
         LTR   R15,R15
         BNZ   BADOPEN
         LA    R10,MYDCB
         TM    DCBOFLGS,X'10'
         BZ    ERRMINE
         MVC   BLDLNAME,MODULE
         BLDL  MYDCB,BLDLLIST
         B     AFTRBLDL
*        CLOSE FILES AND EXIT                                      *
CLOSSHOP EQU   *
         CLOSE SYSPRINT
         CLI   PARMTHER,C'Y'
         BE    STOPIT
         CLOSE SYSIN
         B     STOPIT
*        ERROR ROUTINES                                            *
BADPARM  EQU   *
         LR    R5,R15
         WTO   'PARM SHOULD BE 8 OR 16 BYTES, (PAD EACH HALF WITH SPACEX
               S)',ROUTCDE=11
         ABEND 106,DUMP
ERRMINE  EQU   *
         LR    R5,R15
         WTO   'OPEN OF LIBRARY DCB FAILED, OPEN UNSUCCESSFUL',        X
               ROUTCDE=11
         ABEND 105,DUMP
BADOPEN  EQU   *
         LR    R5,R15
         WTO   'OPEN OF LIBRARY DCB FAILED, RETURN CODE IN R5',        X
               ROUTCDE=11
         ABEND 104,DUMP
BLDLERR  EQU   *
         LR    R5,R15
         CH    R5,=H'04'
         BE    NOTFOUND
         LR    R4,R0
         WTO   'BLDL FAILED, RETURN CODE IN R5, REASON CODE IN R4',    X
               ROUTCDE=11
         ABEND 103,DUMP
NOTFOUND EQU   *
         MVI   OUTREC,C' '
         MVC   OUTREC+1(132),OUTREC
         MVC   OUTREC+2(20),=CL20'MODULE NOT FOUND'
         PUT   SYSPRINT,OUTCARD
         B     CLOSSHOP
ERRSIN   EQU   *
         WTO   'SYSIN OPEN ERROR',ROUTCDE=11
         ABEND 102,DUMP
ERRSOUT  EQU   *
         CLOSE SYSIN
         WTO   'SYSPRINT OPEN ERROR',ROUTCDE=11
         ABEND 101,DUMP
CANTOPEN EQU   *
         WTO   'CAN''T OPEN SYSIN OR SYSPRINT',ROUTCDE=11
         ABEND 100,DUMP
STOPIT   EQU   *
         L     13,SAVEAREA+4       RESTORE R13
*        LR    15,0                RETURN CODE TO R15
         L     14,12(13)           RESTORE R14
         LM    0,12,20(13)         RESTORE R0 THRU R12
         BR    14                  RETURN TO CONTROL PROGRAM
*        STORAGE DEFINITIONS
PARMTHER DC    C'N'
INCARD   DS    0CL80
MODULE   DS    CL8
         DS    CL2
LIBRARY  DS    CL8
SPAREIN  DS    CL62
OUTCARD  DC    AL2(137),AL2(0)
OUTREC   DS    0CL133
         DC    C'  FIND MODULE '
OUTRECM  DS    CL8
         DC    C', DDNAME IS '
OUTRECL  DS    CL8
         DC    CL91' '
         ORG   OUTREC+133
DCBADDR  DC    A(0)
BLDLLIST DS    0H
BLDLNENT DC    H'1'
BLDLLENG DC    H'14'
BLDLNAME DC    CL8' '
BLDLTTR  DC    CL3' '
BLDLK    DC    C' '
BLDLZ    DC    C' '
BLDLC    DC    C' '
WRK8     DS    D
SAVEAREA DS    18F
MYDCB    DCB   DDNAME=MYDCB,DSORG=PO,MACRF=R
SYSIN    DCB   DDNAME=SYSIN,DOSRG=PS,MACRF=GM
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,                      X
               LRECL=137,BLKSIZE=1370,RECFM=VB
DCBDSECT DCBD
         END


Cliff Pratt
Systems Programming Co-ordinator
New Zealand Dairy Board (New Zealand) © New Zealand Dairy Board 1990

