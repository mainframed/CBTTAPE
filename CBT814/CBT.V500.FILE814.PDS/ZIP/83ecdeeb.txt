Removing a user-id from all RACF access lists

Often it is necessary to remove a group-id or a user-id from all
RACF access lists. RACF offers no standard utility for this
task. Usually the output from ICHUT100/IRRUT100 is used
and all the permits are done manually.

The edit macro listed below simplifies the task. It converts
ICHUT100 output (see Figure 1) to a series of CLIST
statements (PERMITs and REMOVEs - see Figure 2). It works
with the ICHUT100 output from RACF 1.8.1 and 1.9. Release
dependent statements are marked 1.8.1 or 1.9 as appropriate.
The CLIST can easily be modified to process IRRUT100
output, so it is not necessary to unload a RACF database in its
entirety with IRRDBU00 (available March 1992, APAR
OY50228) and load it into a DB2 database.

/**********************************************************************/
/*   EDIT MACRO R3  CONVERTS RACF ICHUT100 OUTPUT TO A TSO CLIST      */
/*   IN ORDER TO REMOVE A USERID/GROUPID FROM ALL ACCESS LISTS.       */
/**********************************************************************/
PROC 0
  ISREDIT MACRO
  CONTROL MSG CAPS NOFLUSH  /*  LIST SYMLIST CONLIST  */
  /* ISREDIT REC OFF  */ /* OPTIONAL */
  ISREDIT EXCLUDE ALL
  ISREDIT FIND P'^' ALL 2 80
  ISREDIT EXCLUDE ALL '(G) - ENTITY NAME IS GENERIC'
  ISREDIT DELETE X ALL
  ISREDIT (VAR) = LINE 1
  SET &VAR = &VAR
  SET &VAR = &SUBSTR(16:23,&STR(&VAR        ))
/***  PROCESS GENERIC DATASET PROFILES           **********************/
  ISREDIT EXCLUDE ALL
  ISREDIT CHANGE  +
  /* '               IN STANDARD ACCESS LIST OF PROFILE ' 1.8.1 */ +
     '               IN STANDARD ACCESS LIST OF DATASET PROFILE ' +
     "PERMIT '" ALL
  ISREDIT CHANGE ' (G)' "' ID(&VAR) DELETE GENERIC /*#*/" NX ALL
  ISREDIT EXCLUDE ALL
  ISREDIT CHANGE  +
     '               IN ACCESS LIST OF GROUP' +
     'REMOVE (&VAR) GROUP( ' ALL
  ISREDIT CHANGE ' ' ') /*#*/' 40 40 NX ALL
  ISREDIT EXCLUDE ALL
  ISREDIT CHANGE  +
     '               FIRST QUALIFIER OF PROFILE ' +
     "DELDSD '" ALL
  ISREDIT CHANGE ' (G)' "' GENERIC /*#*/" NX  ALL
/***  PROCESS USER PROFILE                       **********************/
  ISREDIT EXCLUDE ALL
  ISREDIT CHANGE  +
     '               USER ENTRY EXISTS ' +
     'DELUSER (&VAR) /*#*/'
/***  PROCESS GENERAL RESOURCE PROFILES          **********************/
  ISREDIT CHANGE  '(G)' ' ' ALL
  ISREDIT EXCLUDE ALL
/*ISREDIT CHANGE 'IN ACCESS LIST OF '  '#################?' ALL 1.8.1*/
  ISREDIT CHANGE +
  'IN STANDARD ACCESS LIST OF GENERAL RESOURCE PROFILE '  /* 1.9.0 */ +
  '#################?' ALL
  ISREDIT CHANGE  ' ' '>' 44 80 ALL NX   /* 1.9.0 */
  ISREDIT CHANGE  '>' ''  44 80 ALL NX   /* 1.9.0 */
  ISREDIT (ZL) = LINENUM .ZLAST
  ISREDIT CURSOR = 1 1
  ISREDIT FIND  '?' NEXT
  SET &RC = &LASTCC
  DO  WHILE &RC = 0
    ISREDIT (ROW,COL) = CURSOR
    ISREDIT (V2) = LINE &ROW
    SET &V2  = &NRSTR(&V2)
    SET &COL1 = &COL + 1
    SET &COL2 = &COL + 8
    SET &V3  = &SUBSTR(&COL1:&COL2,&NRSTR(&V2        ))
    ISREDIT CHANGE '?&V3' 'PE ' .ZCSR .ZCSR
    ISREDIT CHANGE ' ' '?CLASS(&V3) >' 72 72  .ZCSR .ZCSR
         IF  &ROW < &ZL  THEN DO
             SET &ROW = &ROW + 1
             ISREDIT CURSOR = &ROW  1
             ISREDIT FIND  '?' NEXT
             SET &RC = &LASTCC
         END
         ELSE  SET &RC = 99
     END
  ISREDIT CHANGE '               #################PE  ' 'PERMIT ' ALL NX
  ISREDIT CHANGE '                   ?' ' ?' ALL NX  /* STRIP        */
  ISREDIT CHANGE '                   ?' ' ?' ALL NX  /* LEADING      */
  ISREDIT CHANGE '          ?' ' ?' ALL NX           /* BLANKS       */
  ISREDIT CHANGE '     ?' ' ?' ALL NX
  ISREDIT CHANGE '                   )' ' )' ALL NX
  ISREDIT CHANGE '                   )' ' )' ALL NX
  ISREDIT CHANGE '          )' ' )' ALL NX
  ISREDIT CHANGE '     )' ' )' ALL NX
  ISREDIT CHANGE '  )' ')' ALL  NX
  ISREDIT CHANGE '  )' ')' ALL NX
  ISREDIT CHANGE '  )' ')' ALL NX
  ISREDIT CHANGE ' )' ')' ALL NX
  ISREDIT CHANGE '                   >' ' >' ALL NX
  ISREDIT CHANGE '                   >' ' >' ALL NX
  ISREDIT CHANGE ' >' ' ID(&VAR) DELETE GENERIC /*#*/' ALL NX
  ISREDIT CHANGE '  )' ')' ALL  NX
  ISREDIT CHANGE '  )' ')' ALL  NX
  ISREDIT CHANGE ' ?CLASS' ' CLASS' ALL NX
  ISREDIT EXCLUDE ALL
/***  REMOVE ALL NOT PROCESSED LINES             **********************/
  ISREDIT FIND '/*#*/'     ALL
  ISREDIT DELETE X ALL
  ISREDIT C    '/*#*/' ''  ALL
  ISREDIT LINE_BEFORE .ZF = +
  '  CONTROL MSG CAPS NOFLUSH LIST SYMLIST CONLIST'
  ISREDIT RESET
EXIT CODE(1)


Karl Reinhard Blatt
Systems Programmer
Leonberger Bausparkasse AG (Germany)       c Leonberger Bausparkasse 1992

