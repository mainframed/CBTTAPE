MVS to VM spool offload facility

Our installation is in a fast growth environment, and currently
we are installing MVS/ESA 3.1.0 under a well-tuned VM/XA
SP 2.1 as a V=R machine.  We keep VM since there are many
interactive and auto-operated applications in VM.  Therefore,
all equipment is managed by VM except MVS-dedicated
DASD and consoles.  Based on the above constraints, we have
problems on printing and punching subsystems since MVS
doesn't care whether it uses virtual printer and punch.  If
somebody offloads listings, he or she must reconnect the MVS
CP console and issue the command:

      #CP CLOSE PRT

Sometimes unexpected results occur.  All listings being
spooled are truncated and transferred into VM spool and the
user(s) must split them manually to select his or her own
output.  Unfortunately sometimes this becomes a problem to
other users that don't expect unfinished output to be printed
accidentally.

To solve it, I have coded a simple MVS program to provide a
handshaking interface between MVS and VM spool and install
it as JES2 exit 1.  As an additional function we use the
'programmer name' field in the JOB card as spooling
information.  If the submitter specifies 'VM=MYCMS', the
offloaded output will be transferred to the printer spool of VM
user-id MYCMS.  If the submitter specifies
'REMOTE=YOURCMS', the output is transferred to a remote
VM system in the reader spool of VM user-id YOURCMS via
RSCS.  Otherwise the output is offloaded into the VM system
operator printer spool.

To set this program up as JES2 exit 1, code the following
statement in the JES2 parameter dataset:

      LOADMOD(DERHAS01),STORAGE=CSA
      EXIT1 ROUTINE=DERJ2X01,STATUS=ENABLED,TRACE=NO

Use SYS1.AMODGEN, SYS1.MACLIB, and
SYS1.HASPSRC in SYSLIB DD statement for the assembly of
the exit.

DERU TITLE 'JES2 EXIT-1 '
****************************************************************
**   GROUP         :  JES2 EXIT ROUTINE                       **
**   MODULE NAME   :  DERHAS01                                **
**   FUNCTION      :  MVS-TO-VM SPOOL OFFLOAD FACILITY        **
**                 :  CLOSE SPOOL PRINTER OR PUNCH DYNAMICALLY**
**                    EVERY OFFLOAD PROCESSING COMPLETE       **
**   TYPE          :  AUTHORIZED SYSTEM PROGRAM (AC = 1)      **
****************************************************************
         COPY  $HASPGBL
DERHAS01 $MODULE ENVIRON=JES2,NOTICE=NONE,                             X
               $BUFFER,$CAT,$DCT,$HASPEQU,$HCT,$JCT,$JOE,              X
               $JQE,$MIT,$PCE,$PDDB,UCB
         USING JCT,R10             MAP JOB CONTROL TABLE
         USING DCT,R9              MAP DEVICE CONTROL TABLE
         USING UCB,R7              MAP UNIT CONTROL BLOCK
DERJ2X01 $ENTRY CSECT=YES,BASE=R12 ENTRYPOINT AND BASE
        $SAVE                      SAVE JES2 REGISTERS
         LR    R9,R1               COPY DCT POINTER
         LR    R12,R15             SETUP BASE
         USING VMDSECT,R8          ESTABLISH COMMAND MAPPING
         L     R7,DCTUCB           POINT TO UCB
         LR    R4,R0               PICK UP PARM
         CH    R4,=H'8'            IS IT THE END TRAILER
         BNE   QUIT                QUIT TO JES2
        $GETBUF TYPE=HASP,FIX=YES,WAIT=YES   OBTAIN BUFFER
         ST    R1,PCEUSER0         SAVE BUFFER
         LA    R8,BUFSTART-BFPDSECT(R1)       SKIP BUFFER PREFIX
         B     CLOSE                          GO TO CLOSE ROUTINE
EXIT     EQU   *
         LA    R15,0               SET NORMAL RETURN
EXITRC   EQU   *
         LR    R4,R15              SAVE RC
         L     R5,PCEUSER0         RESTORE BUFFER ADDRESS
        $FREEBUF (R5)                        CLEAR BUFFER
         LR    R15,R4              RESTORE RC
         B     RETURN
QUIT     DS    0H
         LA    R15,0               SET NORMAL RETURN
RETURN   DS    0H
         CH    R15,=H'12'          RC EXCEED ?
         BNH   ENDING              GO HERE IF SO
         LA    R15,8               OTHERWISE SET R15 = 8
ENDING   DS    0H
        $RETURN RC=(R15)          BACK TO JES2
*-----------------------------------------*
*    CP CLOSE COMMAND ISSUER              *
*    THE COMMAND PASSED TO VM/CP          *
*    VIA DIAGNOSE CODE X'08' INTERFACE    *
*-----------------------------------------*
CLOSE    DS    0H
         MVC   VMDSECT(VMDSECTL),CPTEXT  put cp text into buffer
         MVC   VMUNIT(4),DCTUNIT   PUT DCT UNIT HERE
         OC    VMUNIT(4),=CL4' '   MAKE UPPCASES/BLANKS
         TR    VMUNIT(4),EBCDIC    clean unknown text
         MVC   PRTCUU(4),VMUNIT    PUT DCT UNIT HERE
         MVC   VMTAGDEV(4),VMUNIT    PUT DCT UNIT HERE
         MVC   VMFNAME,JCTJNAME    JOB CARD NAME AS FNAME of SPOOL
         MVC   VMFN,JCTJNAME       JOB CARD NAME AS FNAME of MSG
         MVC   VMFTYPE,JCTJOBID    HASP JOB ID AS FTYPE of SPOOL
         MVC   VMFT,JCTJOBID       HASP JOB ID AS FTYPE in MSG
         MVC   VMREMOTE,=CL8' '    CLEAN RECEIVER ID
         CLC   =C'VM=',JCTPNAME    HAS Local VM-ID ASSIGNED ?
         BE    CLOSE1              GO HERE IF SO
         CLC   =C'ROUTE=',JCTPNAME HAS Remote VM-ID ASSIGNED ?
         BE    CLOSE2              GO HERE IF SO
CLOSE0   DS    0H
         MVC   VMNODE,=C'OPERATOR'   DEFAULT USER IS OPERATOR
         MVC   VMUSER,=C'OPERATOR'
         MVC   VMRCVER,=C'OPERATOR'  DEFAULT MSG RECEIVER IS OPERATOR
         MVC   VMREMOTE,=C'OPERATOR' DATA RECEIVER IS OPERATOR
         MVC   VMDEVICE,C'PRT'       LOCAL USER USING PRINTER SPOOL
         MVC   VMSITE(6),=CL6'LOCAL'
         B     CLOSECP
CLOSE1   DS    0H
         MVC   VMNODE,JCTPNAME+3     Userid of SPOOL destination
         MVC   VMUSER,JCTPNAME+3
         MVC   VMRCVER,JCTPNAME+3    Specify also in MSG text
         MVC   VMREMOTE(6),=CL6'LOCAL' Set RECEIVER as local userid
         MVC   VMDEVICE,=C'PRT'      Use SPOOL printer
         MVC   VMSITE(6),=CL6'LOCAL'
         B     CLOSECP
CLOSE2   DS    0H
         MVC   VMNODE,JCTPNAME+6     Userid of SPOOL destination
         MVC   VMUSER,=CL8'RSCS'     Use RSCS as transmitter
*        MVC   VMRCVER,JCTPNAME+6    Specify destination in MSG text
         MVC   VMRCVER,=C'OPERATOR'  Drive MSG to OPERATOR
         MVC   VMREMOTE,JCTPNAME+6   Specify destination in TAG text
         MVC   VMDEVICE,=C'RDR'      Device is SPOOL reader
         MVC   VMSITE(6),=CL6'REMOTE'  ..of remote VM system
CLOSECP  DS    0H
         MVC   VMDIST,JCTTSUID     TSO userid as distribution
         MVC   TSOID,JCTTSUID      TSO userid as sender
         OC    VMDIST,=CL7' '      MAKE UPPCASES/BLANKS
         CLC   VMDIST,=CL7' '      ANY TSU FOR NOTIFY ?
         BNE   CLOSEGO             GO IF SO
         MVC   VMDIST,=C'MVS/ESA'  Otherwise use "MVS/ESA" as DIST
         MVC   TSOID,=C'MVS/ESA'   and sender mark
CLOSEGO  DS    0H
         OC    VMDSECT(VMDSECTL),CPTEXT  get preprinted CP text
         MVC   VMCLASS,JCTMCLAS    CLASS = MSGCLASS IN JCL
*------------------------------------------*
* ISSUE CP MSG TO VM SYSTEM OPERATOR       *
*------------------------------------------*
         SR    R6,R6               CLEAR FOR LENGTH
         IC    R6,MSGLEN           Get LENGTH OF CP MSG command
         LRA   R5,VMMSG            Point to CP MSG text
         DS    0H
         DC    X'83',X'56',X'0008' Issue CP MSG via DIAG X'08'
*------------------------------------------*
* ISSUE CP TAG DEV CCUU FOR RSCS           *
*------------------------------------------*
         SR    R6,R6               CLEAR FOR LENGTH
         IC    R6,TAGLEN           GET LENGTH OF CP TAG command
         LRA   R5,VMTAG            Point to CP TAG text
         DS    0H
         DC    X'83',X'56',X'0008' Issue CP TAG via DIAG X'08'
*------------------------------------------*
* ISSUE CP CLOSE CCUU TO RECEIVER/OPERATOR *
*------------------------------------------*
         SR    R6,R6               CLEAR FOR LENGTH
         IC    R6,COMLEN           GET LENGTH OF CP CLOSE cmd
         LRA   R5,VMCLOSE          Point to CP CLOSE text
         DS    0H
         DC    X'83',X'56',X'0008' Issue CP CLOSE via DIAG X'08'
*------------------------------------------*
* ISSUE WTO TO MVS/ESA OPERATOR CONSOLE    *
*------------------------------------------*
         LA    R2,VMCLOSE          Point CP CLOSE text
         SR    R3,R3               clear R3 ..
         IC    R3,COMLEN           .. for command length
        $WTO  (R2),(R3)            send text to MVS console
         LTR   R6,R6               WAS COMMAND PROCESSED OK?
         BNZ   COMERR              NO, TELL OF ERROR
*------------------------------------------*
* ISSUE CP MSGNOH TO RECEIVER/OPERATOR     *
*------------------------------------------*
CLOSEOKE DS    0H
         SR    R6,R6               CLEAR FOR LENGTH
         IC    R6,TELLLEN          GET LENGTH OF CP MSG text
         LRA   R5,VMTELL           POINT TO CP MSG text
         DS    0H
         DC    X'83',X'56',X'0008' Issue CP MSG via DIAG X'08'
         LA    R15,8               SET NO TRAILER
         B     EXITRC              exit
COMERR   EQU   *
*------------------------------------------*
* ISSUE WTO ERROR MSG TO MVS/ESA CONSOLE   *
*------------------------------------------*
         LA    R2,MSG3             Point to MSG text
         SR    R3,R3               clear R3 ..
         IC    R3,MSG3LEN          ... for MSG length
        $WTO  (R2),(R3)            send MSG to MVS console
         B     EXIT
MSG3LEN  DC    AL1(L'MSG3)
MSG3     DC    C'SPOOL CLOSE COMMAND RETURNED ERROR'
COMLEN   DC    AL1(COMMANDL)
MSGLEN   DC    AL1(CPMSGL)
TAGLEN   DC    AL1(CPTAGL)
TELLLEN  DC    AL1(CPTELLL)
CPTEXT   DS    0H
*------------------------------------------------*
* PREPRINTED CP MSG COMMAND TEXT FOR VM OPERATOR *
*------------------------------------------------*
CPMSG    DC    CL30'CP MSG OPERATOR issue command'
*------------------------------------------*
* PREPRINTED CP TAG   COMMAND TEXT         *
*------------------------------------------*
CPTAG   DC     CL22'TAG DEV      RSCSSD1 '
        DC     CL8' '
CPTAGL  EQU    *-CPTAG
*------------------------------------------*
* PREPRINTED CP CLOSE COMMAND TEXT         *
*------------------------------------------*
COMMAND  DC    CL9'CP CLOSE '
         DC    CL4' '           CP CLOSE .... TO ........ -
         DC    C' TO '          CLASS .. DIST ........    -
         DC    CL8' '           NAME ........ ........
         DC    C' '
         DC    CL3' '
         DC    C' CLASS '
         DC    C' '
         DC    C' DIST '
         DC    CL7' '
         DC    C' NAME '
         DC    CL8' '               VM SPOOL FILE NAME
         DC    C' '
         DC    CL8' '               VM SPOOL FILE TYPE
COMMANDL EQU   *-COMMAND
CPMSGL   EQU   *-CPMSG
*---------------------------------------------*
* PREPRINTED CP MSG COMMAND TEXT FOR RECEIVER *
*---------------------------------------------*
CPTELL   DS    0H
         DC    CL10'CP MSGNOH '
         DC    CL8' '
         DC    CL25' DERJES001I MVS/ESA Unit='
         DC    CL4' '
         DC    CL23' has offloaded as file '
         DC    CL8' '
         DC    C' '
         DC    CL8' '
         DC    CL15' by TSO='
         DC    CL19' in'
CPTELLL  EQU   *-CPTELL
CPTEXTL  EQU   *-CPTEXT
*------------------------------------------*
* EBCDIC-TEXT TRANSLATE TABLE              *
*------------------------------------------*
         DS    0H
EBCDIC   DC    CL256' '
         ORG   EBCDIC+C'0'
         DC    C'0123456789'
         ORG   EBCDIC+C'A'
         DC    C'ABCDEF'
         ORG   EBCDIC+C'a'
         DC    C'abcdef'
         ORG
*------------------------------------------*
* LITERAL POOL                             *
*------------------------------------------*
         LTORG
*------------------------------------------*
* DSECT FOR CP COMMAND BUFFER MAP          *
*------------------------------------------*
VMDSECT  DSECT
VMMSG    EQU   *
         DS    CL30'CP MSG OPERATOR issue command'
VMTAG    EQU   *
         DS    CL8'TAG DEV '
VMTAGDEV DS    CL4
         DS    CL10' RSCSSD1 '
VMREMOTE DS    CL8
VMTAGL   EQU   *-VMTAG
VMCLOSE  EQU   *
         DS    CL9 -CP CLOSE-
PRTCUU   DS    CL4           PRT DEVICE NUMBER
         DS    CL4 -TO-
VMUSER   DS    CL8           VM USERID
         DS    C -
VMDEVICE DS    CL3           VM SPOOL DEVICE
         DS    CL7 -CLASS-
VMCLASS  DS    C             VM SPOOL CLASS
         DS    CL6 -DIST-
VMDIST   DS    CL7           VM SPOOL DISTRIBUTION ATTRIBUTE
         DS    CL6 -NAME-
VMFNAME  DS    CL8           VM SPOOL FILENAME
         DS    C -
VMFTYPE  DS    CL8           VM SPOOL FILETYPE
VMMSGL   EQU   *-VMMSG
VMCLOSEL EQU   *-VMCLOSE
VMTELL   DS    0H
         DS    CL10'CP MSGNOH '
VMRCVER  DS    CL8
VMNOTE   DS    CL25' DERJES001I MVS/ESA UNIT='
VMUNIT   DS    CL4
         DS    CL23' HAS OFFLOADED AS FILE '
VMFN     DS    CL8
         DS    C
VMFT     DS    CL8
         DS    CL8' BY TSO='
TSOID    DS    CL7
         DS    CL4' IN '
VMSITE   DS    CL6'remote'
         DS    C
VMNODE   DS    CL8
VMNOTEL  EQU   *-VMNOTE
VMTELLL  EQU   *-VMTELL
VMDSECTL EQU   *-VMDSECT
DERHAS01 CSECT
         LTORG
        $MODEND
         END


Deru Sudibyo
Senior Systems Designer
PT SDD/BNI (Indonesia)                          c Xephon 1991

