Refreshing RACF profiles - an update

Since the publication of my article A TSO command to refresh
RACF profiles in the July 1991 issue of MVS Update, the IBM
RACF design team has contacted me to explain some possible
problems with the program. It seems that I used control blocks
that are intended for IBM diagnostic purposes only, and as
such these control blocks are liable to change without customer
notification. After further conversations with IBMers and
following their suggestions, I have rewritten the program and
now use intended RACF interfaces.

The new version of the program is a lot safer than before.
RACF keeps the four most recently used sets of generic
profiles in storage in the user's address space. So, if the
program issues four RACROUTE REQUEST=AUTH requests,
each for a non-existent dataset and each with a different high-
level qualifier, RACF will flush out all the previous sets of
generic profiles. This will force RACF to extract any
subsequent profile from the database and the effect of
refreshing the profiles will have been achieved.

There is a small overhead in I/O to the RACF database, but it
should not be too much if the high-level qualifiers are selected
to be similar to each other. This overhead is reduced by
choosing 'SYS0DUM*' since all the index blocks required to
find names that start with 'SYS1' are probably in storage and a
name beginning with 'SYS0' would probably fall into the same
blocks. APF-authorization is still needed to suppress any
failure and audit records produced by the program.

Many thanks to Walt Farrell from the IBM RACF design team
in the USA for his help.

*********************************************************************
*
* PROGRAM TO REFRESH RACF PROFILES WITHIN                            *
* ADDRESS SPACE WITHOUT HAVING TO RE-LOGON TO TSO.                   *
* NEEDS TO BE IN AN AUTHORIZED LIBRARY AND IN THE TABLE OF           *
* AUTHORIZED COMMANDS FOR TSO.                                       *
**********************************************************************
REFRESH   CSECT                         *
          USING  REFRESH,R15            * USE R15 AS TEMPORARY BASE
          STM    R14,R12,12(R13)        * STORE REGS
          LA     R2,SAVEAREA            * POINT TO SAVEAREA
          ST     R2,8(R13)              * STORE R2
          ST     R13,4(R2)              * STORE R13
          LR     R13,R2                 * COPY R2
          DROP   R15                    * DROP TEMPORARY BASE
          LR     R12,R15                * LOAD NEW BASE
          USING  REFRESH,R12            * ESTABLISH ADDRESSABILITY
          MODESET KEY=ZERO,MODE=SUP     * SET TO KEY0 AND SUPERVISOR
**********************************************************************
* NOW ISSUE FOUR RACROUTE REQUESTS TO FLUSH THE GENERIC PROFILES.    *
* THE DATASETS DO NOT EXIST, SO SUPPRESS ANY ERROR RECORDS.          *
**********************************************************************
          RACROUTE REQUEST=AUTH,                                       C
               CLASS='DATASET',                                        C
               ENTITY=(DSN1),                                          C
               VOLSER=VOL1,                                            C
               LOG=NOSTAT,                                             C
               WORKA=WORKAREA
          RACROUTE REQUEST=AUTH,                                       C
               CLASS='DATASET',                                        C
               ENTITY=(DSN2),                                          C
               VOLSER=VOL1,                                            C
               LOG=NOSTAT,                                             C
               WORKA=WORKAREA
          RACROUTE REQUEST=AUTH,                                       C
               CLASS='DATASET',                                        C
               ENTITY=(DSN3),                                          C
               VOLSER=VOL1,                                            C
               LOG=NOSTAT,                                             C
               WORKA=WORKAREA
          RACROUTE REQUEST=AUTH,                                       C
               CLASS='DATASET',                                        C
               ENTITY=(DSN4),                                          C
               VOLSER=VOL1,                                            C
               LOG=NOSTAT,                                             C
               WORKA=WORKAREA
          MODESET KEY=NZERO,MODE=PROB   * RESET AUTH
RETURN    EQU   *                       *
          TPUT  MSG1,34                 * TELL USER ITS OK
          L     R13,SAVEAREA+4          * LOAD UP SAVEAREA ADDRESS
          LM    R14,R12,12(R13)         * LOAD REGS
          B     R14                     * BRANCH BACK
MSG1      DC    C'REFRESH OF RACF PROFILES COMPLETED '
          DS    0F
SAVEAREA  DC    18F'0'                  * REGISTER SAVEAREA
WORKAREA  DC    512C'40'                * WORKAREA FOR RACROUTE
DSN1      DC    CL44'SYS0DUM1.DUMMY1'   * DUMMY DATASETS
DSN2      DC    CL44'SYS0DUM2.DUMMY2'   *
DSN3      DC    CL44'SYS0DUM3.DUMMY3'   *
DSN4      DC    CL44'SYS0DUM4.DUMMY4'   *
VOL1      DC    CL6'SRES31'             * VOLUME -NEEDED FOR RACROUTE
          END REFRESH


Rob Scott
MVS Systems Programmer
BOC Limited (UK)                               c Xephon 1992

