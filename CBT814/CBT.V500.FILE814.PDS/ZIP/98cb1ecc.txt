Using the OAM OSREQ API

         MACRO
&NAME    MODE   &MODE
&GNAME   SETC  'MOD'.'&SYSNDX'
         AIF   ('&MODE' EQ '').ERR1
         AIF   ('&MODE' EQ '24').SET24
         AIF   ('&MODE' EQ '31').SET31
         AGO   .ERR2
.SET24   ANOP
&NAME    SLR   1,1
         LA    1,&GNAME.A
         BSM   0,1
&GNAME.A EQU   *
         AGO   .EXIT
.SET31   ANOP
&NAME    LA    1,&GNAME.B
         O     1,=X'80000000'
         BSM   0,1
&GNAME.B EQU   *
         AGO   .EXIT
.ERR1    ANOP
         MNOTE 12,'ADDRESSING MODE REQUESTED NOT SPECIFIED'
         AGO   .EXIT
.ERR2    ANOP
         MNOTE 12,'MODE MUST BE 24 OR 31'
         AGO   .EXIT
.EXIT    MEXIT
         MEND
OBJ1 CSECT
OBJ1 AMODE 31
OBJ1 RMODE 24
         BAKR  14,0               Save regs, ARs, etc on Linkage Stack
         LR    12,15              Base register #1
         LA    11,4095(,12)       Base register #2
         LA    11,1(,11)
         USING OBJ1,12,11
         STORAGE OBTAIN,LENGTH=WORKL  Get work area storage
         LR    10,1
         USING WORK,10
         LAE   13,SAVEAREA        Set up save area addr in AR/R13
         MVC   4(4,13),=C'F1SA'   LS save indicator
         OPEN  (SYSPRINT,(OUTPUT)),MODE=31
         LTR   15,15
         BZ    ACCESS
         WTO   'Open SYSPRINT failed'
         B     RETURN
ACCESS   DS    0H
* Connect to OAM services                                             *
* A token is returned which must be supplied on subsequent calls to   *
* OSREQ.                                                              *
         OSREQ ACCESS,                                                 X
               TOKEN=TOKEN,                                            X
               RETCODE=XRC,                                            X
               REACODE=XREAS,                                          X
               MF=(E,OSREQL)
         LTR   15,15
         BZ    QUERY1
         L     1,XRC
         L     2,XREAS
         MVC   XLINE,=CL133' '
         MVC   XLINE(30),=CL30' OSREQ ACCESS failed'
         MODE  24
         PUT   SYSPRINT,XLINE
         MODE  31
         DC H'0'
QUERY1   DS    0H
         BAS   14,QUERY                Do query on object
         LTR   15,15
         BNZ   STORE
RETRIEVE DS    0H
* Create an Object Buffer List which will point to a buffer           *
* to contain the retrieved object                                     *
         LA    3,XOBL
         USING OBL,3
         XC    OBL(LOBL),OBL           Clear OAM buffer list
         MVC   OBLID,=C'OBL '          eyecatcher
         MVC   OBLLSTL,=A(L'XOBL+L'XODESC) OBL length
         MVI   OBLVERSN,X'02'          OBL Version id
         MVC   OBLNUMBF,=A(1)          Number of buffer descriptors
         DROP  3
* Create a buffer descriptor for the buffer we are passing to OAM     *
         LA    3,XODESC
         USING OBLBDESC,3
         LA    1,BUFF
         ST    1,OBLBUFP               Store pointer to buffer
         MVC   OBLBBLTH,=A(L'BUFF)     Size of the buffer
         MVC   OBLBUSED,=A(0)          buffer is unused
* Retrieve object in  collection   QM6M7V8.OAM.COLLECT2               *
*                     object name  DAN.COLLECT1.OBJECT2               *
*   VIEW=PRIMARY - indicates that we want the primary copy of the     *
*                  object.  The application could try to retrieve     *
*                  the primary and, if that fails, try VIEW=BACKUP    *
*                  to retrieve the back-up copy if one exists.        *
         OSREQ RETRIEVE,                                               X
               COLLECTN=COLLECTN,                                      X
               NAME=OBJECT,                                            X
               BUFLIST=XOBL,                                           X
               VIEW=PRIMARY,                                           X
               TOKEN=TOKEN,                                            X
               MF=(E,OSREQL)
         LTR   15,15
         BZ    QUERY2
         L     1,XRC
         L     2,XREAS
         MVC   XLINE,=CL133' '
         MVC   XLINE(30),=CL30' OSREQ RETRIEVE failed'
         MODE  24
         PUT   SYSPRINT,XLINE
         MODE  31
         DC H'0'
QUERY2   DS    0H
* Last reference date should be changed.                              *
         BAS   14,QUERY
* Change retention period on object.                                  *
         OSREQ CHANGE,                                                 X
               COLLECTN=COLLECTN,                                      X
               NAME=OBJECT,                                            X
               MGMTCLAS=NEWMC,                                         X
               TOKEN=TOKEN,                                            X
               MF=(E,OSREQL)
         LTR   15,15
         BZ    QUERY3
         L     1,XRC
         L     2,XREAS
         MVC   XLINE,=CL133' '
         MVC   XLINE(30),=CL30' OSREQ CHANGE failed'
         MODE  24
         PUT   SYSPRINT,XLINE
         MODE  31
* continue even if error
QUERY3   DS    0H
* Query again - expiry date should be changed.                        *
         BAS   14,QUERY
* Delete object                                                       *
         OSREQ DELETE,                                                 X
               COLLECTN=COLLECTN,                                      X
               NAME=OBJECT,                                            X
               QEL=XQEL,                                               X
               TOKEN=TOKEN,                                            X
               MF=(E,OSREQL)
         LTR   15,15
         BZ    STORE                   If query is OK, then continue
         MVC   XLINE,=CL133' '
         MVC   XLINE(30),=CL30' OSREQ DELETE failed'
         MODE  24
         PUT   SYSPRINT,XLINE
         MODE  31
         DC    H'0'
STORE    DS    0H
* Create an Object Buffer List which will point to a buffer           *
* containing the object data to be stored.                            *
         LA    3,XOBL
         USING OBL,3
         XC    OBL(LOBL),OBL           Clear OAM buffer list
         MVC   OBLID,=C'OBL '          eyecatcher
         MVC   OBLLSTL,=A(L'XOBL+L'XODESC) OBL length
         MVI   OBLVERSN,X'02'          OBL version-id
         MVC   OBLNUMBF,=A(1)          Number of buffer descriptors
         DROP  3
* Create a buffer descriptor for the buffer we are passing to OAM     *
         LA    3,XODESC
         USING OBLBDESC,3
         LA    1,BUFF
         ST    1,OBLBUFP               Store pointer to buffer
         MVC   OBLBBLTH,=A(L'BUFF)     Size of the buffer
         MVC   OBLBUSED,SIZE           Space used within the buffer
* Store the object in collection   QM6M7V8.OAM.COLLECT2               *
*                     object name  DAN.COLLECT1.OBJECT2               *
         OSREQ STORE,                                                  X
               COLLECTN=COLLECTN,                                      X
               NAME=OBJECT,                                            X
               SIZE=SIZE,                                              X
               BUFLIST=XOBL,                                           X
               TOKEN=TOKEN,                                            X
               MF=(E,OSREQL)
         C     15,=F'4'
         BNH   UNACCESS
         L     1,XRC
         L     2,XREAS
         MVC   XLINE,=CL133' '
         MVC   XLINE(30),=CL30' OSREQ STORE failed'
         MODE  24
         PUT   SYSPRINT,XLINE
         MODE  31
         DC H'0'
UNACCESS DS    0H
         CLOSE (SYSPRINT),MODE=31
         OSREQ UNACCESS,                                               X
               TOKEN=TOKEN,                                            X
               MF=(E,OSREQL)
         LTR   15,15
         BZ    RETURN
         WTO   'Error in OSREQ UNACCESS'
         DC H'0'
RETURN   DS    0H
         LR    1,10               Release storage
         STORAGE RELEASE,ADDR=(1),LENGTH=WORKL
         SLR   15,15              rc = 0
         PR                       Return
* Query subroutine to print out information about an object           *
QUERY    DS    0H
         BAKR  14,0
         SLR   15,15
* Create an Query Buffer List which will point to a buffer            *
* to contain returned query elements.                                 *
         LA    3,XQEL
         USING QEL,3
         XC    QEL(LQEL),QEL           Clear OAM buffer list
         MVC   QELID,=C'QEL '          eyecatcher
         MVC   QELLSTL,=A(L'XQEL+L'XQDESC) QEL length
         MVI   QELVERSN,X'03'          QEL Version id
         MVC   QELNUMBF,=A(1)          Number of buffer descriptors
         DROP  3
* Create a buffer descriptor for the buffer we are passing to OAM     *
* to place query elements in.                                         *
         LA    3,XQDESC
         USING QELBDESC,3
         LA    1,BUFF
         ST    1,QELBUFP               Store pointer to buffer
         MVC   QELBBLTH,=A(L'BUFF)     Size of the buffer
         MVC   QELBUSED,=A(0)          Query buffer is unused
         OSREQ QUERY,                                                  X
               COLLECTN=COLLECTN,                                      X
               NAME=OBJECT,                                            X
               QEL=XQEL,                                               X
               TOKEN=TOKEN,                                            X
               MF=(E,OSREQL)
         LTR   15,15
         BZ    QUERYOK                 If query is okay, then continue
         MVC   XLINE,=CL133' '
         MVC   XLINE(30),=CL30' OSREQ QUERY failed'
         MODE  24
         PUT   SYSPRINT,XLINE
         MODE  31
         LA    15,8
         B     QUERYFL
QUERYOK  DS    0H
         DROP  3
         LA    3,BUFF
         USING QELQ,3
         MVC   XLINE,=CL133' '
         MVC   XLINE(25),=CL25' Collection   '
         MVC   XLINE+25(44),QELQECNN
         MODE 24
         PUT   SYSPRINT,XLINE
         MODE 31
         MVC   XLINE,=CL133' '
         MVC   XLINE(25),=CL25' Object Name  '
         MVC   XLINE+25(44),QELQEONN
         MODE 24
         PUT   SYSPRINT,XLINE
         MODE 31
         MVC   XLINE,=CL133' '
         MVC   XLINE(25),=CL25' Creation Date'
         MVC   XLINE+25(10),QELQECD
         MODE 24
         PUT   SYSPRINT,XLINE
         MODE 31
         MVC   XLINE,=CL133' '
         MVC   XLINE(25),=CL25' Creation Time'
         MVC   XLINE+25(15),QELQECT
         MODE 24
         PUT   SYSPRINT,XLINE
         MODE 31
         MVC   XLINE,=CL133' '
         MVC   XLINE(25),=CL25' Last Reference Date'
         MVC   XLINE+25(10),QELQELD
         MODE 24
         PUT   SYSPRINT,XLINE
         MODE 31
         MVC   XLINE,=CL133' '
         MVC   XLINE(25),=CL25' Expiration Date'
         MVC   XLINE+25(10),QELQEED
         MODE 24
         PUT   SYSPRINT,XLINE
         MODE 31
         MVC   XLINE,=CL133' '
         MVC   XLINE(25),=CL25' Storage Class'
         MVC   XLINE+25(8),QELQESCN
         MODE 24
         PUT   SYSPRINT,XLINE
         MODE 31
         MVC   XLINE,=CL133' '
         MVC   XLINE(25),=CL25' Management Class'
         MVC   XLINE+25(8),QELQEMCN
         MODE 24
         PUT   SYSPRINT,XLINE
         MVC   XLINE,=CL133' '
         PUT   SYSPRINT,XLINE
         MODE 31
         SLR   15,15
QUERYFL  DS    0H
         PR
SYSPRINT DCB   LRECL=133,RECFM=FBA,MACRF=(PM),DSORG=PS,DDNAME=SYSPRINT
* Collection name is specified with 2-byte length header followed     *
* by text containing name of the collection.                          *
COLLECTN DC    AL2(L'COLLECT1)
COLLECT1 DC    C'QM6M7V8.OAM.COLLECT2'
* Object name is specified with 2-byte length header followed         *
* by text containing name of the object.                              *
OBJECT   DC    AL2(L'OBJECT1)
OBJECT1  DC    C'DAN.COLLECT1.OBJECT2'
NEWMC    DC    AL2(L'NEWMC1)
NEWMC1   DC    CL8'ODASD'
TOKEN    DC    CL8' '
OBJ      DC    250C'JUNK1 '
SIZE     DC    A(*-OBJ)
BUFLIST  DC    A(X'80000000'+OBJ)
OSREQL   OSREQ STORE,                                                  X
               COLLECTN=COLLECTN,                                      X
               NAME=OBJECT,                                            X
               SIZE=L'OBJ,                                             X
               BUFLIST=BUFLIST,                                        X
               TOKEN=TOKEN,                                            X
               MF=L
         LTORG
WORK     DSECT
SAVEAREA DS    18F
XOBL     DS    XL(LOBL)
XODESC   DS    XL(LODESC)
XQEL     DS    XL(LOBL)
XQDESC   DS    XL(LODESC)
XRC      DS    F
XREAS    DS    F
XLINE    DS    CL133
BUFF     DS    CL4096
WORKL    EQU   *-WORK
         YREGS
         CBRIBUFL
LOBL     EQU   OBLBUFL-OBL
LODESC   EQU   OBLBRSV1+4-OBLBDESC
         CBRIQEL
LQEL     EQU   QELBUFL-QEL
LQDESC   EQU   QELBRSV1+4-QELBDESC
         END


Dan Little
Royal Bank (Canada)                                                                c Xephon 1994

