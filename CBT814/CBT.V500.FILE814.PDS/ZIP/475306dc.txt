Issuing operator commands through JCL

It is possible to issue operator commands and JES commands via JCL, although at
most installations this ability is disabled by JES parameters because users
should not be allowed to do it.  In addition, it is impossible to issue an
operator command depending on step condition codes, nor do it under TSO.

The following program, OPER$, overcomes these restrictions.  If it is called in
batch the command is passed as a parameter, if it is called from TSO the user is
prompted and multiple commands may be given.  Use of command OPER$ is restricted
to certain jobs and TSO users defined in the module.  Therefore make sure that
unauthorised users are not allowed to update the module.

// EXEC ASMFCL,PARM.LKED='XREF,REUS,AC=1',COND.LKED=(0,LT)
//ASM.SYSPRINT DD SYSOUT=*
***********************************************************************
*        BATCH: OPERATOR COMMAND VIA JCL EXEC PARAMETER.
*        E.G. // EXEC PGM=OPER$,PARM='D A,IBMUSER'
*        TSO:  OPERATOR COMMAND VIA TSO SCREEN.
* PROGRAM MUST BE AUTHORIZED, THEREFORE INSERT 'OPER$' INTO IKJEFTE2.
***********************************************************************
OPER$    CSECT                               SERIALLY REUSABLE
         SAVE  (14,12),,               SAVE CALLER'S REGISTERS         X
               OPER$_&SYSDATE          IDENTIFY MODULE
         LR    R2,R15                  LOAD BASE REGISTER
         USING OPER$,R2                TELL IT ASSEMBLER
         ST    R13,SAVEAREA+4          CHAIN
         LR    R3,R13                    SAVEAREAS
         LA    R13,SAVEAREA
         ST    R13,8(R3)
         LR    R4,R1                   SAVE ADDR. OF PARAMETER
         EXTRACT FADDRS,'S',           GET ADDR. OF TIOT & TSO BIT     X
               FIELDS=(TIOT,TSO)
         LM    R5,R6,FADDRS            ADDR. TIOT & ADDR. TSO BIT
         CLC   =C'IBMUSER ',0(R5)      ADDR. TIOT = ADDR JOBNAME/USERID
         BE    AUTH
*        CLC   =C'XXXXXXXX',0(R5)      REPEAT FOR ALL JOBNAMES/USERIDS
*        BE    AUTH                    ALLOWED TO ISSUE OPERATOR CMDS
NOTAUTH  TM    0(R6),X'80'             TSO ?
         BO    TSOMSG                    YES
         CNOP  0,4
WTO      WTO   '******* YOU ARE NOT ALLOWED TO ISSUE OPERATOR COMMANDS'X
               ,ROUTCDE=11             BATCH: MESSAGE TO JOB LOG
WTOL     EQU   *-WTO-14                LENGTH OF MESSAGE
         B     KO
TSOMSG   TPUT  WT0+8,WTOL              TSO: MESSAGE TO SCREEN
KO       LA    R15,12
         B     PROGEND
AUTH     TM    0(R6),X'80'             TSO ?
         BO    TSOENV                    YES
BATCHENV L     R4,0(R4)                ADDR. OF JCL EXEC PARAMETER
         LH    R3,0(R4)                LENGTH OF PARAMETER
         LTR   R3,R3                   LENGTH = 0 ?
         BZ    OK                        YES, FINISH
         BCTR  R3,0                    - 1 BECAUSE OF EX
         EX    R3,EXMVC                COMMAND TO COMMAND BUFFER
         LA    R3,5(R3)                LENGTH + 4
         STH   R3,CMD                  SET LENGTH PREFIX OF COMMAND
         B     DOIT
TSOENV   TPUT  PROMPT,L'PROMPT         PROMPT FOR COMMAND
GET      TGET  CMDTEXT,L'CMDTEXT       GET COMMAND
         LTR   R1,R1                   LENGTH = 0 ?
         BZ    OK                        YES, FINISH
         LA    R1,4(R1)                LENGTH + 4
         STH   R1,CMD                  SET LENGTH PREFIX OF COMMAND
DOIT     MODESET KEY=ZERO,MODE=SUP     INTO SUPERVISOR STATE
         LA    R1,CMD                  ADDR. OF COMMAND
         SLR   R0,R0
         SVC   34                      ISSUE COMMAND
         MODESET KEY=NZERO,MODE=PROB   RETURN TO PROBLEM STATE
         TM    0(R6),X'80'             TSO ?
         BO    GET                       YES, MAYBE ANOTHER COMMAND
OK       SLR   R15,R15
PROGEND  L     R13,4(R13)
         RETURN (14,12),RC=(15)        GO BACK
EXMVC    MVC   CMDTEXT(1),2(R4)
PROMPT   DC    C'******* ENTER OPERATOR COMMAND -'
         LTORG
SAVEAREA DS    18F
FADDRS   DS    2A
CMD      DC    Y(L'CMDTEXT+4),Y(0)     COMMAND BUFFER PREFIX
CMDTEXT  DS    CL100                   COMMAND BUFFER
         END   OPER$


Walter Wiedemann
Consultant (West Germany)     © Walter Wiedemann 1987













































