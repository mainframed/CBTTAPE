Executing a list of commands


EXCMD REXX EXEC

/*-REXX/EXCMD---------------------------------------------------------*/
/* Rexx macro to execute several TSO commands simply by selecting the */
/* lines with the prefix command "C" (like copying lines).            */
/*--------------------------------------------------------------------*/
    address 'ISREDIT'
    'MACRO NOPROCESS'
    'PROCESS RANGE C'
    if rc <> 0 then exit 1
/*------------- Find out the initial and final lines -----------------*/
    '(CMD) = RANGE_CMD'
    '(FIRST) = LINENUM .ZFRANGE'
    '(LAST) = LINENUM .ZLRANGE'
/*-------------------- Loop executing each command -------------------*/
    CmdOld = ''
    do Ind = First to Last
       '(CMD) = LINE' Ind            /* Extract line, ie, the command */
       if right(strip(Cmd,'T'),1) = '-'      /* If it's continuation, */
          then do                            /*  save the command     */
               CmdOld = strip(Cmd,'B')
               iterate
               end
       Cmd = strip(CmdOld,'T','-') strip(Cmd,'B') /* "Append" last cmd*/
       CmdOld = ''
/*----- Execute the command and sets a msg line with return code ----*/
       address TSO Cmd
       say 'Rc='Rc',Cmd='Cmd
       'LINE_AFTER' Ind '= MSGLINE "===> RC='Rc' ('Date(E)'-'Time()')"'
    end
    exit


Patrick Leroy
Systems Analyst
Banco Fonsecas & Burnay (Portugal)                      c Xephon 1996

