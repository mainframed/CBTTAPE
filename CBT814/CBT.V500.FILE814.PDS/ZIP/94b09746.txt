Validating MVS account codes and job classes

Most sites have a need to validate the use of account codes and,
possibly, job classes.  Ideally this should be done using standard
RACF or ACF2 resource profiles.  It is possible to use the IBM SMF
exit, IEFUJI, to perform this validation.  This exit is passed the
necessary information in a formatted control block and is much
simpler to write than an equivalent JES2 or JES3 exit.

This code should operate under both JES2 and JES3 and, providing
IBM maintains the SMF exit parameters as they are, should not be
affected by MVS or JES2/3 release changes.  Since the job will
have been initiated when this exit gets control, there is no need
for code to create RACF or ACF2 control blocks as there would be if
the exit were running in the JES2/3 address space.  The SMF
member of SYS1.PARMLIB should specify no invocation of IEFUJI
for started tasks, or the code should check the type of address
space since no accounting information is normally available for
started tasks.  To check the type of address space, use the ASCB as
the ASCBTSB field is set for TSO users and the name of the job or
task is found via either ASCBJBNI or ASCBJBNS according to
whether it was initiated (job) or a started task or a TSO user.  The
current ASCB address is found in the PSA field PSAAOLD, the PSA
being addressed from absolute zero.  If the need is only to
distinguish foreground users the JMRINDC field can be tested
against the mask JMRFIND (foreground initiated) as seen below.

* SMF EXIT IEFUJI, MUST BE CODED AND LINKED REENTRANTLY
* THIS VERIFIES FOR TSO AND BATCH THAT THE ACCOUNT CODE IS PERMITTED
* TO THE USER. THE EXIT IS NOT DESIGNED TO BE CALLED BY STARTED TASKS.
* THIS CODE CAN EXECUTE IN AMODE 24 OR 31 DUE TO THE USE OF RACROUTE
* RATHER THAN RACHECK. THIS MEANS THAT THE CLASS OF ACCOUNT MUST BE
* ADDED TO THE RACROUTE CLASS TABLE. ALTERNATIVELY CHANGE THE CODE TO
* USE RACHECK AND ACCEPT THE AMODE 24 RESTRICTION. THE CLASS ACCOUNT
* IS DEFINED TO RACF IN THE CLASS DESCRIPTOR TABLE WITH A LENGTH OF 8.
IEFUJI   CSECT    , REENTRANT
         SAVE  (14,12),,*              SAVE REGISTERS
         LR    R12,R15                 COPY ENTRY PT
         USING IEFUJI,R12              ADDRESSABILITY
         LR    R11,R1                  COPY PARM LIST ADDR
         GETMAIN R,LV=WORKLEN          GET WORKING STORAGE
         ST    R13,4(,R1)              CHAIN SAVE AREAS
         ST    R1,8(,R13)
         LR    R13,R1                  NEW SAVE AREA
         USING WORKAREA,R13            GET ADDRESSABILITY
* SEE IF THIS CALLER IS A TSO USER USING SMF CEPA PARMS
         L     R5,0(R11)               LOAD SMF CEPA
         USING JMR,R5                  ADDR
         MVI   TSOUSER,0               SET FLAG DEFAULT
         TM    JMRINDC,JMRFIND         FOREGROUND INDICATED
         BNO   CONT                    NO, CONTINUE FOR BATCH
         MVI   TSOUSER,1               SET FLAG FOR TSO
* GET ACCOUNT CODE FROM SMF FIELD PASSED
CONT     L     R6,12(R11)              GET ACCOUNT ADDR
         CLI   0(R6),0                 IS THERE ANY ACCOUNT
         BE    FORMERR                 NO, ERROR
         XR    R7,R7                   CLEAR FOR ICM
         ICM   R7,1,1(R6)              GET LENGTH OF ACCOUNT CODE
         BZ    FORMERR                 ERROR, NONE
         C     R7,=F'8'                CHECK LENGTH
         BH    FORMERR                 MORE THAN 8 BYTES
         MVC   COPYACCT,BLANKS         CLEAR COPY
         BCTR  R7,0                    LESS ONE FOR EX
         EX    R7,MOVEACCT             COPY ACCOUNT CODE
         MVC   RACROUTE(RACHTMPL),RACHTMP COPY TEMPLATE
         LA    R1,RACROUTE                ADDR RACROUTE
         LA    R7,COPYACCT                ADDR ACCOUNT CODE
         RACROUTE REQUEST=AUTH,WORKA=RACWORK,                          X
               CLASS=CLACC,ENTITY=((7)),MF=(E,(1)),ATTR=READ,LOG=ASIS
         LTR   R15,R15                    TEST RACF RESULT
         BNZ   NOTALLOW                   VERIFICATION FAILURE
ALLOW    XR    R2,R2                   ZERO RETURN CODE
         B     RETURN                  AND ENTER RETURN SEQUENCE
* INVALID ACCOUNT FORMAT
FORMERR  WTO   MF=(E,FORMMSG)          WTO FOR FORMAT ERROR
         CLI   TSOUSER,1               IS IT TSO USER
         BNE   SKIP1                   NO
         TPUT  DASHES,L'DASHES         TSO DASHES
         TPUT  FORMMSG+4,LFORMMSG-8    TSO MESSAGE
         TPUT  DASHES,L'DASHES         TSO DASHES
SKIP1    LA    R2,4                    SET RC - CANCEL
         B     RETURN                  AND RETURN
* RACF FAILED ACCOUNT AUTHORISATION ERROR
NOTALLOW MVC   MESSAGE(LINVALID),INVALID  MOVE MESSAGE TO WORK
         MVC   MESSAGE+19(8),COPYACCT  INSERT ACCOUNT CODE
         WTO   MF=(E,MESSAGE)          ISSUE IT
         CLI   TSOUSER,1               IS IT TSO USER
         BNE   SKIP2                   NO
         TPUT  DASHES,L'DASHES         TSO DASHES
         TPUT  MESSAGE+4,LINVALID-8    TSO INFORM BY TPUT
         TPUT  DASHES,L'DASHES         TSO DASHES
SKIP2    LA    R2,4                    SET RC - CANCEL
         B     RETURN                  AND RETURN
RETURN   LR    R1,R13                  COPY WORKAREA ADDR
         L     R13,4(,R13)             GET OLD SAVE AREA ADDR
         FREEMAIN R,LV=WORKLEN,A=(1)   FREE STORAGE
         LR    R15,R2                  COPY RETURN CODE
         RETURN (14,12),RC=(15)        AND RETURN
MOVEACCT MVC   COPYACCT(0),2(R6)       EXECUTED MOVE
INVALID  WTO   '- ACCOUNT CODE ???????? IS NOT AUTHORISED BY RACF -',  X
               ROUTCDE=(1,9,11),MF=L
LINVALID EQU   *-INVALID               MESSAGE LENGTH
FORMMSG  WTO   '- ACCOUNT CODE IS MISSING OR LENGTH IS TOO LONG   -',  X
               ROUTCDE=(1,9,11),MF=L
LFORMMSG EQU   *-FORMMSG               MESSAGE LENGTH
DASHES   DC    C'---------------------------------------------------'
BLANKS   DC    CL8' '
CLACC    DC    XL1'07',CL8'ACCOUNT'    RACF RESOURCE CLASS
RACHTMP  RACROUTE REQUEST=AUTH,MF=L    RACF TEMPLATE
RACHTMPL EQU   *-RACHTMP               LEN
         LTORG
WORKAREA DSECT
SAVEAREA DS    18F                     SAVE AREA
TSOUSER  DS    X                       TSO USER FLAG
RACWORK  DS    512X                    RACROUTE WORK
MESSAGE  DS    CL80                    WTO MESSAGE WORK
COPYACCT DS    CL8                     ACCOUNT CODE COPY
RACROUTE RACROUTE REQUEST=AUTH,MF=L    LAYOUT RACROUTE
WORKLEN  EQU   *-WORKAREA              WORK AREA LENGTH
         IEFJMR                        SMF EXIT CEPA
         END


    © Reserved 1989

