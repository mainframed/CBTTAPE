Restricting the MVS RESET command

USERMOD FOR MVS/ESA 3.1.3

++USERMOD(LM00034)        /* USERMOD FOR MVS/ESA V3.1.3 */.
++VER(Z038) FMID(JBB3311).
++ZAP(IEEMB810) /* THIS ZAP HOOKS IN IEEMB810 BEFORE A SYSEVENT
                   RESETPG IS ISSUED.  IT CALLS PROGRAM RESETICS
                   TO VALIDATE THE PERFORMANCE GROUP ENTERED ON
                   THE RESET COMMAND.  IEEMB810 IS MARKED AS
                   REENTRANT/REUSABLE AND THIS ZAP WILL VIOLATE
                   THAT REENTRANCY (SEE COMMENTS MARKED WITH AN
                   ASTERISK).  AN ALTERNATIVE METHOD IS TO
                   REWRITE RESETICS AS AN SVC TO REPLACE THE
                   IEEMB810 SYSEVENT RESETPG     */.
 NAME IEEMB810
* START ZAP VERIFICATION
 VER 03B2 47F0C39A                  B     SVC95         VF BRANCH
 VER 03B6 001F                      DC    AL2(31)       VFY SYSEVENT CODE
 VER 03B8 0A5F             SVC95    SVC   95            VFY SYSEVENT SVC
 VER 0488 0000000000000000          DC    4F'0'         VERIFY PATCH AREA
 VER 0490 0000000000000000
 VER 0498 0000000000000000          DC    4F'0'         VERIFY PATCH AREA
 VER 04A0 0000000000000000
 VER 04A8 0000000000000000          DC    4F'0'         VERIFY PATCH AREA
 VER 04B0 0000000000000000
* START ZAP REPLACE
 REP 03B2 47F0C46A                  B     NEWCODE       REPLACE BRANCH
 REP 0488 41F0C482         NEWCODE  LA    15,PGMNAME   LOAD R15 W/A(PGMNAME)
 REP 048C 50F0C47A                  ST    15,PGMPTR   *STORE A(PGM) IN PARM*
 REP 0490 50E0C48A                  ST    14,R14SAVE   *SAVE R14 FOR RETURN*
 REP 0494 45F0C48E                  BAL   15,SVC        BRANCH TO LINK SVC
 REP 0498 00000000         PGMPTR   DC    F'0'          A(MODULE NAME)--|
 REP 049C 00000000         DCBPTR   DC    F'0'          DCB POINTER     |
 REP 04A0 D9C5E2C5E3C9C3E2 PGMNAME  DC    CL8'RESETICS' PROGRAM NAME----|
 REP 04A8 00000000         R14SAVE  DC    F'0'          R14 SAVEAREA
 REP 04AC 0A06             SVC      SVC   6             LINK TO RESETICS
 REP 04AE 58E0C48A                  L     14,R14SAVE    RELOAD R14
 REP 04B2 47F0C39A                  B     SVC95         BACK TO MAINLINE

USERMOD FOR MVS/ESA 4.3

++USERMOD(LM00034)        /* USERMOD FOR MVS/ESA V4.3   */.
++VER(Z038) FMID(HBB4430).
++ZAP(IEEMB810) /* THIS ZAP HOOKS IN IEEMB810 BEFORE A SYSEVENT
                   RESETPG IS ISSUED.  IT CALLS PROGRAM RESETICS
                   TO VALIDATE THE PERFORMANCE GROUP ENTERED ON
                   THE RESET COMMAND.  IEEMB810 IS MARKED AS
                   REENTRANT/REUSABLE AND THIS ZAP WILL VIOLATE
                   THAT REENTRANCY (SEE COMMENTS MARKED WITH AN
                   ASTERISK).  AN ALTERNATIVE METHOD IS TO
                   REWRITE RESETICS AS AN SVC TO REPLACE THE
                   IEEMB810 SYSEVENT RESETPG     */.
 NAME IEEMB810              TWO BLANKS NEEDED BEFORE NAME
* START ZAP VERIFICATION
 VER 06A4 47F0C6AA                  B     SVC95         VFY BRANCH
 VER 06A8 001F                      DC    AL2(31)       VFY SYSEVENT CODE
 VER 06AA 0A5F             SVC95    SVC   95            VFY SYSEVENT SVC
 VER 075C C75CC75EC760C762          DC    23S(*)    VFY PATCH AREA (23 HALFW
 VER 0764 C764C766C768C76A
 VER 076C C76CC76EC770C772
 VER 0774 C774C776C778C77A
 VER 077C C77CC77EC780C782
 VER 0784 C784C786C788
* START ZAP REPLACE
 REP 06A4 47F0C75C                  B     NEWCODE       REPLACE BRANCH
 REP 075C 41F0C774         NEWCODE  LA    15,PGMNAME   LOAD R15 W/A(PGMNAME)
 REP 0760 50F0C76C                  ST    15,PGMPTR   *STORE A(PGM) IN PARM*
 REP 0764 50E0C77C                  ST    14,R14SAVE   *SAVE R14 FOR RETURN*
 REP 0768 45F0C780                  BAL   15,SVC        BRANCH TO LINK SVC
 REP 076C 00000000         PGMPTR   DC    F'0'          A(MODULE NAME)--|
 REP 0770 00000000         DCBPTR   DC    F'0'          DCB POINTER     |
 REP 0774 D9C5E2C5E3C9C3E2 PGMNAME  DC    CL8'RESETICS' PROGRAM NAME----|
 REP 077C 00000000         R14SAVE  DC    F'0'          R14 SAVEAREA
 REP 0780 0A06             SVC      SVC   6             LINK TO RESETICS
 REP 0782 58E0C77C                  L     14,R14SAVE    RELOAD R14
 REP 0786 47F0C6AA                  B     SVC95         BACK TO MAINLINE

RESETICS SOURCE CODE

         TITLE 'RESETICS-ENFORCE IEAICS CONTROL FOR MVS RESET(E) CMD'
RESETICS AMODE 31
RESETICS RMODE ANY
RESETICS CSECT                         ESTABLISH CSECT
         SAVE  (14,12),,RESETICS-&SYSDATE
         YREGS
         LR    R12,R15                 LOAD R12 W/EPA ADDRESS
         USING RESETICS,R12            ESTABLISH ADDRESSABLITY TO CSECT
         GETMAIN RU,LV=WORKLEN         GETMAIN WORKAREA
         LR    R2,R1                   LOAD R2 W/A(AREA) FOR MVCL
         LA    R3,WORKLEN              LOAD R3 W/WORKAREA LENGTH
         SR    R5,R5                   CLEAR R5 FOR MVCL PAD + FROM LEN
         MVCL  R2,R4                   CLEAR WORK AREA
         ST    R13,4(,R1)              ST CALLER'S S/A ADDR IN MY S/A
         ST    R1,8(,R13)              ST MY S/A ADDR IN CALLER'S S/A
         LR    R13,R1                  LOAD ADDR OF MY S/A IN R13
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY
         L     R1,4(,R13)              POINT TO CALLER'S SAVEAREA
         LM    R0,R1,20(R1)            RELOAD R0/R1 FROM CALLER
         STCM  R0,12,ASID              SAVE ASID NUMBER
         STCM  R1,3,RESETPGN           SAVE PERFORMANCE GROUP
         MVC   EYECATCH,EYEVALUE       MOVE EYECATCHER TO GETMAIN AREA
         L     R15,CVTPTR              LOAD R15 W/A(CVT)
         USING CVTMAP,R15              ESTABLISH ADDRESSABLITY
         L     R14,CVTOPCTP            LOAD R14 W/A(RMCT)
         USING RMCT,R14                ESTABLISH ADDRESSABLITY
         ST    R14,RMCTADDR            SAVE A(RMCT)
         ICM   R11,15,RMCTICST         LOAD R11 W/A(ICSC) IF ANY
         BZ    RETURN                  NONE, THEN ALLOW RESET AS IS
         L     R15,CVTASVT             LOAD R15 W/A(ASVT)
         USING ASVT,R15                ESTABLISH ADDRESSABLITY
         LA    R15,ASVTENTY-4          LOAD R15 W/A(1ST ASVT ENTRY)
         SR    R4,R4                   CLEAR R4
         LH    R4,ASID                 LOAD R4 WITH ASID
         SLL   R4,2                    MULTIPLY R4 BY 4 FOR INDEXING
         LA    R15,0(R4,R15)           LOAD R15 W/A(REQ. ASVT ENTRY)
         TM    0(R15),ASVTAVAL         IS ENTRY AVAILABLE
         BO    RETURN                  YES, THEN ALLOW RESET AS IS
         L     R15,0(,R15)             LOAD R15 W/A(ASCB IN QUESTION)
         USING ASCB,R15                ESTABLISH ADDRESSABLITY
         ST    R15,ASCBADDR            SAVE A(ASCB)
         L     R15,ASCBOUCB            LOAD R15 W/A(OUCB)
         USING OUCB,R15                ESTABLISH ADDRESSABLITY
         ST    R15,OUCBADDR            SAVE A(OUCB)
         MVC   SAVEYFL,OUCBYFL         SAVE OUCBYFL FLAG FOR LATER
         LA    R15,FPGOAREA            LOAD R15 W/A(PSEUDO FPGO AREA)
         ST    R15,FPGOADDR            SAVE A(PSEUDO FPGO AREA)
         LA    R1,ICSPAREA             LOAD R15 W/A(PSEUDO ICSP PLIST)
         ST    R1,ICSPADDR             SAVE A(PSEUDO ICSP PLIST)
         OI    ICSPADDR,X'80'          TURN ON FULL ICSP INDICATOR
         USING ICSP,R1                 ESTABLISH ADDRESSABILITY
         L     R15,OUCBADDR            RELOAD A(OUCB)
         MVI   ICSPSUBN,C' '           CLEAR SUBSYSTEM NAME
         MVC   ICSPSUBN+1(L'ICSPSUBN-1),ICSPSUBN  CLEAR SUBSYSTEM NAME
         MVC   ICSPSUB0,OUCBSUBN       MOVE SUBSYTEM NAME
         MVC   ICSPTRXN,OUCBTRXN       MOVE TRANSACTION NAME
         MVC   ICSPUSRD,OUCBUSRD       MOVE USERID
         MVC   ICSPCLS,OUCBCLS         MOVE CLASS
         MVI   ICSPACTL,0              INDICATE NO ACCOUNTING INFO
* THIS CODE DOES NOT SUPPORT ACCOUNTING INFO VALIDITY CHECKING
         MVC   ICSPPGN,RESETPGN        MOVE REQUESTED PGN TO CHECK
         L     R15,RMCTRMSB            LOAD A(RMSB)
         USING RMSB,R15                ESTABLISH ADDRESSABILITY
         L     R15,RMSBFPG             LOAD A(FIND PGN ROUTINE)
         LM    R0,R5,FPGOADDR          LOAD R0-R5 WITH PARMS FOR FPG
         LR    R6,R13                  SAVE A(SAVEAREA)
         LA    R13,SAVEADJ             SKIP SAVEAREA PL/I WORD(FOR FPG)
         BALR  R14,R15                 INVOKE FIND PGN ROUTINE
         LR    R13,R6                  RESTORE A(SAVEAREA)
         CLC   EYECATCH,EYEVALUE       IS EYECATCHER CLOBBERED
         BNE   ERROR                   YES, ERROR
         C     R15,FOUR                IS RETURN CODE GOOD
         BH    SETDEF                  NO, GO SET DEFAULT PGN
         CLC   RESETPGN,FPGONPG        WAS REQUESTED PGN RETURNED
         BE    RETURN                  YES, ALLOW IT
SETDEF   MVC   WTOPGN(MODLWTOL),MODLWTO MOVE MODEL WTO TO GETMAIN AREA
         TM    SAVEYFL,OUCBLOG         IS THIS A TSO USER
         BZ    BATSTC                  NO, MUST BE BATCH/STC(USE TRXN)
         MVC   WTOPGN+MNAME(L'MNAME),ICSPUSRD MOVE USERID TO MESSAGE
         B     CNVTPGN                 GO MAKE NEW PGN PRINTABLE
BATSTC   MVC   WTOPGN+MNAME(L'MNAME),ICSPTRXN MOVE TRX NAME TO MESSAGE
CNVTPGN  LH    R1,FPGONPG              LOAD R1 W/NEW PGN VALUE RETURNED
         CVD   R1,CNVTAREA             CONVERT PGN VALUE TO DECIMAL
         OI    CNVTAREA+7,X'0F'        MAKE SIGN PRINTABLE
         UNPK  WTOPGN+MPGNN(3),CNVTAREA+6(2) MAKE IT PRINTABLE
         LH    R1,RESETPGN             LOAD R1 W/PGN VALUE REQUESTED
         CVD   R1,CNVTAREA             CONVERT PGN VALUE TO DECIMAL
         OI    CNVTAREA+7,X'0F'        MAKE SIGN PRINTABLE
         UNPK  WTOPGN+MPGNO(3),CNVTAREA+6(2) MAKE IT PRINTABLE
         WTO   MF=(E,WTOPGN)           ISSUE MESSAGE
         L     R15,SAVEAREA+4          LOAD R15 W/A(CALLER'S SAVE AREA)
         MVC   26(2,R15),FPGONPG       PUT NEW PGN IN HIS SAVEAREA R1
RETURN   LR    R1,R13                  LOAD R1 W/A(SAVEAREA)
         L     R13,4(,R13)             LOAD R13 W/ADDR OF CALLER'S S/A
         FREEMAIN RU,LV=WORKLEN,A=(1)  FREEMAIN WORKAREA
         RETURN (14,12),RC=0           RETURN TO OS WITH RETCODE=0
ERROR    WTO   'ICS002E STACK AREA HAS BEEN OVERLAYED DURING A RESET COX
               MMAND',DESC=3,ROUTCDE=2
         B     RETURN
         TITLE 'RESETICS-CONSTANTS AND DATA AREAS'
FOUR     DC    F'4'                    HIGHEST RETURN CODE TO ACCEPT
MODLWTO  WTO   'ICS001I PERFORM=*** CHANGED TO *** FOR ********',      X
               DESC=5,ROUTCDE=2,MF=L MODEL WTO
MODLWTOL EQU   *-MODLWTO               MODEL WTO LENGTH
MPGNO    EQU   20,3                    OFFSET FOR OLD PERFORMANCE GROUP
MPGNN    EQU   35,3                    OFFSET FOR NEW PERFORMANCE GROUP
MNAME    EQU   43,8                    OFFSET FOR TRXNAME/USERID
EYEVALUE DC    CL8'EYE**EYE'           EYECATCHER INFO FOR GETMAIN AREA
WORKAREA DSECT
SAVEAREA DC    18F'0'                  SHOULD BE FIRST IN WORKAREA ---|
SAVEADJ  EQU   SAVEAREA+4              ADJUSTED SAVEAREA FOR FPG      |
         DC    33F'0'                  STACK FRAME 1                  |
         DC    33F'0'                  STACK FRAME 2                  |
         DC    33F'0'                  STACK FRAME 3 -----------------|
* THE ABOVE BRACKETED AREAS ARE STACK FRAMES USED BY IRARMFPG.  THE
* EYECATCHER BELOW IS USED TO DETERMINE IF IRARMFPG HAS CHANGED TO USE
* MORE STACK FRAMES.  IF SO, ADD MORE STACK FRAMES ABOVE.
EYECATCH DC    CL8' '                  EYECATCHER
FPGOADDR DC    F'0'                    A(FPGO) -------(R0)------------|
ICSPADDR DC    F'0'                    A(ICSP)        (R1)            |
RMCTADDR DC    F'0'                    A(RMCT)        (R2)            |
RRPAADDR DC    F'0'                    A(RRPA)-ZEROES (R3)            |
OUCBADDR DC    F'0'                    A(OUCB)        (R4)            |
ASCBADDR DC    F'0'                    A(ASCB) -------(R5)------------|
* THE ABOVE BRACKETED AREA R0 THROUGH R5 VALUES PASSED TO IRARMFPG.
* THE RRPAADDR IS NOT USED AND IS LEFT AS ZERO-ESPECIALLY SINCE I DON'T
* KNOW HOW TO BUILD IT.  THE ASCBADDR IS NOT USED BUT IS FILLED IN
* ANYWAY FOR COMPATIBILITY.
CNVTAREA DS    D                       AREA TO MAKE NEW PGN PRINTABLE
ASID     DC    H'0'                    ASID OF JOB BEING RESET
RESETPGN DC    H'0'                    PGN REQUESTED FROM RESET CMD
WTOPGN   DS    CL(MODLWTOL)            AREA FOR MODEL WTO
ICSPAREA DS    CL(ICSPLNG)             PSEUDO ICSP PLIST
         DS    0F                      ALIGN TO FULLWORD
FPGOAREA DS    CL(OUCBDRFP-OUCBFPGO)   PSEUDO FPGO AREA
FPGONPG  EQU   FPGOAREA,4              PSEUDO OUCBNPG FROM IRARMFPG
SAVEYFL  DC    X'00'                   OUCBYFL
         DS    0F                      ALIGN TO FULLWORD
WORKLEN  EQU   *-WORKAREA              WORKAREA LENGTH
         CVT   DSECT=YES,LIST=NO       CVT
         IHAASVT                       ASVT
         IHAASCB                       ASCB
         IRAOUCB                       OUCB
         TITLE 'ICSPRINT-IRA DSECTS FROM PVTMACS'
* THE EQUATES BELOW ARE TO RESOLVE REFERENCES IN THE IRARMCT MACRO.
* IF YOU GET ASSEMBLY ERRORS, ADD THE NAMES HERE WITH ' EQU   0'
* OR YOU CAN HARD-CODE OFFSETS INTO RMCT.
CCT      EQU   0                       RESOLVE IRARMCT REFERENCE
ICT      EQU   0                       RESOLVE IRARMCT REFERENCE
MCT      EQU   0                       RESOLVE IRARMCT REFERENCE
RMPT     EQU   0                       RESOLVE IRARMCT REFERENCE
RMCA     EQU   0                       RESOLVE IRARMCT REFERENCE
RMEX     EQU   0                       RESOLVE IRARMCT REFERENCE
IRAEPPA  EQU   0                       RESOLVE IRARMCT REFERENCE
EPDTSCN  EQU   0                       RESOLVE IRARMCT REFERENCE
EPATSCN  EQU   0                       RESOLVE IRARMCT REFERENCE
LSCT     EQU   0                       RESOLVE IRARMCT REFERENCE
IRAEPPR  EQU   0                       RESOLVE IRARMCT REFERENCE
IRACTMQE EQU   0                       RESOLVE IRARMCT REFERENCE
IRAOUCB  EQU   0                       RESOLVE IRARMCT REFERENCE
IRAWTQE  EQU   0                       RESOLVE IRARMCT REFERENCE
IRALSQE  EQU   0                       RESOLVE IRARMCT REFERENCE
IRAOTQE  EQU   0                       RESOLVE IRARMCT REFERENCE
IRAINQE  EQU   0                       RESOLVE IRARMCT REFERENCE
RQSV     EQU   0                       RESOLVE IRARMCT REFERENCE
IRAOUXB  EQU   0                       RESOLVE IRARMCT REFERENCE
IRASRBT  EQU   0                       RESOLVE IRARMCT REFERENCE
RCT      EQU   0                       RESOLVE IRARMCT REFERENCE
RSPL     EQU   0                       RESOLVE IRARMCT REFERENCE
CMCT     EQU   0                       RESOLVE IRARMCT REFERENCE
ESCT     EQU   0                       RESOLVE IRARMCT REFERENCE
EPAT     EQU   0                       RESOLVE IRARMCT REFERENCE
         IRARMCT                       SRM CONTROL TABLE
*RMCT     DSECT                         UNCOMMENT IF MACRO NOT FOUND--|
*RMCTRMSB EQU   RMCT+44                 POINTER TO RMSB               |
*RMCTICST EQU   RMCT+220                POINTER TO ICSC TABLE---------|
         IRARMSB                        SRM VECTOR TABLE
*RMSB     DSECT                         UNCOMMENT IF MACRO NOT FOUND--|
*RMSBFPG  EQU   RMSB+92                 POINTER TO FIND PGN ROUTINE---|
         IRAICSP                        ICS PLIST
*ICSP     DSECT                         UNCOMMENT IF MACRO NOT FOUND--|
*         DS    CL180                   SIZE OF ICSP                  |
*ICSPSUBN EQU   ICSP,8                  SUBSYSTEM NAME                |
*ICSPTRXN EQU   ICSP+8,8                TRANSACTION NAME              |
*ICSPUSRD EQU   ICSP+16,8               USERID                        |
*ICSPCLS  EQU   ICSP+24,8               CLASS                         |
*ICSPPGN  EQU   ICSP+32,2               PERFORMANCE GROUP IN HEX      |
*ICSPFLAG EQU   ICSP+34,1               STATUS FLAGS                  |
*ICSPDP   EQU   ICSP+35,1               DISPATCHING PRTY IN HEX       |
*ICSPACTL EQU   ICSP+36,1               LENGTH OF ACCOUNT NUMBER      |
*ICSPACTN EQU   ICSP+37,143             ACCOUNT NUMBER FROM JCL       |
*ICSPLNG  EQU   *-ICSP                  LENGTH OF ICSP----------------|
         END


                                                                              c Xephon 1995

