Automated TSO log-on limit control
I have been in a few shops where TSO log-ons have been at a premium.  That is,
there were always more people who wanted to log-on to TSO than there were users
allowed.  This situation inevitably led to departmental fighting as to just how
many users from each department were logged on to TSO.  Given a finite TSO user
maximum, all sorts of contrivances were tried to maintain some equity, including
reduction of SMF job wait time, regular submission of started tasks to cancel
TSO users who were inactive for more than a specified length of time, and the
periodic monitoring and cancellation of TSO users by computer operators.
In order to automate the process as well as to have it more evenly administered,
we devised a scheme, based on some sample code distributed with the CA-TOP
SECRET security package, to execute at TSO user initialisation and termination
time to perform log-on checking.  This code depends on a subsystem
communications vector table (SSCVT) entry defined in the IEFSSNxx PARMLIB member
to act as an anchor for a table which it builds and maintains.  Since there is a
log-on validation exit in TSO but no log-off exit, we elected to implement our
idea via code in SMF exits IEFUSI and IEFACTRT.
The logic of IEFUSI is to check if the SSCTSUSE reserved field in the SSCT entry
we set up is zero.  If not, we load a table into CSA and anchor it through the
SSCTSUSE field.  The table is a separately-assembled load module consisting of
some header information and multiple 16-byte entries for each group or
department whose number of logged on users we wish to restrict.  The table
entries contain a TSO user-id prefix, a group user limit, and an accumulator for
the number of currently logged on users in the group.  If the SSCTSUSE is non-
zero, we use it as the address of a previously-loaded table.  Every time a user
logs on, IEFUSI will check his prefix against the table for a match and if
found, it will see if his group is already at their user maximum.  If so, we
fail the log-on with a polite message; if not we allow the user in and increment
the count for that group.  The one exception is that between 5 pm and 8 am we
will increment counters but will not fail the log-on.  In IEFACTRT at log-off
time, we check for the same criteria as above and decrement the count if
necessary.  We also check that IEFACTRT is entered with one type of entry code,
so we do not incorrectly decrement the count multiple times because IEFACTRT is
called repeatedly for any one TSO termination.
In our implementation we used TSO user-id prefixes to indicate group
affiliations.  The length of the prefix may be changed by changing the value at
label IDLEN.  Those installations that have a security package such as ACF2 or
CA-TOP SECRET could use the TSO users UID string or ACID department,
respectively.  This would eliminate the need for TSO user-ids to begin with a
standard prefix.  Another thing to consider is not allowing anyone who does not
match a table entry to log on at all.  We also left some patch areas in the
table so that we could dynamically add table entries with the storage alteration
commands available through OMEGAMON or RESOLVE.  If either of these packages are
available, you could also use them to zero out the SSCTUSE field to cause the
exit to load an entirely new table.  Code would have to be added to the IEFUSI
exit to count up the number of users logged on per group, however, to re-
establish the counters.  If you never see a need for this, you may wish to code
the required subsystem name entry in the IEFSSNxx PARMLIB member to include a
subsystem initialisaion routine to initially load the table and anchor it in the
SSCT, thus removing some of the code required in the IEFUSI exit.
We have found that these exits, when coupled with the TIMEOUT command published
in issue 8 (which we invoke selectively via a CLIST executed in the log-on
procedure parameter field), enable us to administer the TSO environment more
effectively and equitably.
IEFACTRT SOURCE
IEFACTRT CSECT                         ESTABLISH CSECT
         SAVE  (14,12),,IEFACTRT-&SYSDATE
         LR    R12,R15                 LOAD R12 W/EPA ADDRESS
         USING IEFACTRT,R12            ESTABLISH ADDRESSABLITY
         C     R0,TWELVE               IS THIS A STEP TERM ENTRY
         BNE   QUICKXIT                NO, GO EXIT
         GETMAIN RU,LV=WORKLEN         GETMAIN WORKAREA
         LTR   R15,R15                 WAS GETMAIN GOOD
         BNZ    QUICKXIT               NO, GO EXIT
         ST    R13,4(R1)               ST CALLERS S/A ADDR IN MY S/A
         ST    R1,8(R13)               ST MY S/A ADDR IN CALLERS S/A
         LR    R13,R1                  LOAD ADDR OF MY S/A IN R13
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY
         L     R2,CVTPTR               LOAD R2 W/A(CVT)
         USING CVT,R2                  ESTABLISH ADDRESSABILITY
         L     R15,CVTTCBP             LOAD R15 W/A(TCB WORDS)
         L     R15,12(R15)             LOAD R15 W/A(CURRENT ASCB)
         USING ASCB,R15                ESTABLISH ADDRESSABILITY
         L     R14,ASCBJBNS            LOAD R1 W/A(TSO JOBNAME)
         MVC   JOBNAME,0(R14)          SAVE JOBNAME FOR LATER COMPARE
         LA    R3,=A(IN31BIT+X'80000000') LOAD R3 W/31 BIT ADDRESS
         BSM   R0,R3                   BRANCH TO ADDRESS IN 31 BIT MODE
IN31BIT  L     R15,ASCBOUCB            LOAD R15 W/A(OUCB)
         USING OUCB,R15                ESTABLISH ADDRESSABILITY
         MVC   SAVEYFL,OUCBYFL         SAVE OUCBYFL BYTE
         LA    R3,IN24BIT              LOAD R3 W/24 BIT ADDRESS
         BSM   R0,R3                   BRANCH TO ADDRESS IN 24 BIT MODE
IN24BIT  TM    SAVEYFL,OUCBLOG         IS THIS A LOGON (TSO USER)
         BZ    EXIT0                   NO, GO RETURN
         L     R2,CVTJESCT             LOAD R2 W/A(JESCT)
         USING JESCT,R2                ESTABLISH ADDRESSABILITY
         LA    R2,JESSSCT-(SSCTSCTA-SSCT) LOAD R2 W/A(SSCT HEAD)
         USING SSCT,R2                 ESTABLISH ADDRESSABILITY
NEXTSSCT ICM   R2,15,SSCTSCTA          LOAD R2 W/A(SSCT ENTRY)
         BZ    EXIT0                   DIDN'T FIND REQUIRED SSCT
         CLC   SSCTSNAM,SUBNAME        IS THIS THE REQUIRED SSCT NAME
         BNE   NEXTSSCT                NO, GO CHECK NEXT SSCT
         B     TERM                    ELSE GO CHECK TABLE
*        COMMON EXIT                                                  *
EXIT0    LR    R1,R13                  LOAD R1 W/A(SAVEAREA)
         L     R13,4(R13)              LOAD R13 W/ADDR OF CALLERS S/A
         FREEMAIN RU,LV=WORKLEN,A=(1)  FREEMAIN WORKAREA
QUICKXIT RETURN (14,12),RC=0           RETURN TO OS
* PROCESS JOB TERMINATION                                             *
* THIS CODE UNDOES WHAT WAS DONE PREVIOUSLY FOR TSO USERS BY DECREMENT-
* ING THIS TSO USER'S DEPARTMENT COUNTER SO THAT OTHER USERS WAITING
* TO LOGON MAY DO SO WITHOUT ANY FURTHER DELAY.
TERM     ICM   R8,15,SSCTSUSE          R8 = A(DEPARTMENT TABLE)
         BZ    EXIT0                   NO TABLE, GO RETURN
         USING TABLMAP,R8              ESTABLISH ADDRESSABILITY
         LA    R8,HDRLEN(R8)           SKIP PAST HEADER WORD
         USING TABLID,R8               ESTABLISH ADDRESSABILITY
TERMLOOP CLI   TABLID,X'00'            ARE WE AT END OF TABLE
         BE    EXIT0                   YES, GO RETURN
         CLC   TABLID(IDLEN),JOBNAME   DOES DEPT PREFIX = USER PREFIX
         BE    TERMCNT                 YES, GO DECREMENT COUNT
         LA    R8,ENTLEN(R8)           ELSE POINT TO NEXT TABLE ENTRY
         B     TERMLOOP                GO CHECK NEXT ENTRY
TERMCNT  L     R3,TABLCNT              LOAD R3 W/(# OF CURRENT USERS)
         LR    R4,R3                   COPY CURRENT COUNT
         BCTR  R4,0                    DECREMENT BY 1
         CS    R3,R4,TABLCNT           UPDATE COUNT IF SAFE
         BZ    TERMCNT                 NO, GO TRY AGAIN
         B     EXIT0                   ELSE GO RETURN
*        DATA CONSTANTS
         LTORG
TWELVE   DC    F'12'                   CONSTANT 12
SUBNAME  DC    CL4'LMON'               SUBSYSTEM NAME TO SCAN FOR
TABLMAP  DSECT                         MAP DEPARTMENT ENTRIES --------|
TABLHDR  DS    A                       TABLE LENGTH FOR FREEMAIN      |
TABLDATE DS    A                       DATE OF TABLE LOAD             |
TABLTIME DS    A                       TIME OF TABLE LOAD             |
HDRLEN   EQU   *-TABLHDR               LENGTH OF TABLE HEADER         |
IDLEN    EQU   2                       LENGTH OF ID PREFIX            |
TABLID   DS    CL(IDLEN)               DEPARTMENT ID                  |
         DS    CL(8-IDLEN)             FILLER                         |
TABLIMIT DS    F                       MAX USERS FOR THIS DEPARTMENT  |
TABLCNT  DS    F                       CURRENT NUM OF USERS SIGNED ON |
ENTLEN   EQU   *-TABLID                LENGTH OF A TABLE ENTRY -------|
* MACROS/DSECTS WE DON'T NEED TO SEE..
         CVT   DSECT=YES
         IEFJESCT
         IEFJSCVT
         IHAASCB
         IRAOUCB
WORKAREA DSECT                         WORK AREA
SAVEAREA DC    18F'0'                  SHOULD BE FIRST IN WORKAREA
JOBNAME  DS    CL8                     SAVE AREA FOR TSO JOB NAME
SAVEYFL  DS    X                       SAVE AREA FOR OUCBYFL
WORKLEN  EQU   *-WORKAREA              WORK AREA LENGTH
         END
IEFUSI SOURCE
IEFUSI   CSECT                         ESTABLISH CSECT
         SAVE  (14,12),,IEFUSI=&SYSDATE
         LR    R12,R15                 LOAD R12 W/EPA ADDRESS
         USING IEFUSI,R12              ESTABLISH ADDRESSABILITY
         GETMAIN RU,LV=WORKLEN         GETMAIN WORKAREA
         LTR   R15,R15                 WAS GETMAIN GOOD
         BZ    GETGOOD                 YES, SKIP
         LA    R15,0                   ELSE LOAD R15 W/RETURN CODE
         B     QUICKXIT                GO EXIT
GETGOOD  ST    R13,4(R1)               ST CALLERS S/A ADDR IN MY S/A
         ST    R1,8(R13)               ST MY S/A ADDR IN CALLERS S/A
         LR    R13,R1                  LOAD ADDR OF MY S/A IN R13
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY
         L     R2,CVTPTR               LOAD R2 W/A(CVT)
         USING CVT,R2                  ESTABLISH ADDRESSABILITY
         L     R15,CVTTCBP             LOAD R15 W/A(TCB WORDS)
         L     R15,12(R15)             LOAD R15 W/A(CURRENT ASCB)
         USING ASCB,R15                ESTABLISH ADDRESSABILITY
         L     R14,ASCBJBNS            LOAD R1 W/A(TSO JOBNAME)
         MVC   JOBNAME,0(R14)          SAVE JOBNAME FOR LATER COMPARE
         LA    R3,=A(IN31BIT+X'80000000') LOAD R3 W/31 BIT ADDRESS
         BSM   R0,R3                   BRANCH TO ADDRESS IN 31 BIT MODE
IN31BIT  L     R15,ASCBOUCB            LOAD R15 W/A(OUCB)
         USING OUCB,R15                ESTABLISH ADDRESSABILITY
         MVC   SAVEYFL,OUCBYFL         SAVE OUCBYFL BYTE
         LA    R3,IN24BIT              LOAD R3 W/24 BIT ADDRESS
         BSM   R0,R3                   BRANCH TO ADDRESS IN 24 BIT MODE
IN24BIT  TM    SAVEYFL,OUCBLOG         IS THIS A LOGON (TSO USER)
         BZ    EXIT0                   NO, GO RETURN
         L     R2,CVTJESCT             LOAD R2 W/A(JESCT)
         USING JESCT,R2                ESTABLISH ADDRESSABILITY
         LA    R2,JESSSCT-(SSCTSCTA-SSCT) LOAD R2 W/A(SSCT HEAD)
         USING SSCT,R2                 ESTABLISH ADDRESSABILITY
NEXTSSCT ICM   R2,15,SSCTSCTA          LOAD R2 W/A(SSCT ENTRY)
         BZ    EXIT0                   DIDN'T FIND REQUIRED SSCT
         CLC   SSCTSNAM,SUBNAME        IS THIS THE REQUIRED SSCT NAME
         BNE   NEXTSSCT                NO, GO CHECK NEXT SSCT
         B     INIT                    ELSE GO CHECK TABLE
*        COMMON EXIT                                                  *
EXIT     LR    R1,R13                  LOAD R1 W/A(SAVEAREA)
         L     R13,4(R13)              LOAD R13 W/ADDR OF CALLERS S/A
         FREEMAIN RU,LV=WORKLEN,A=(1)  FREEMAIN WORKAREA
QUICKXIT RETURN (14,12),RC=(15)        RETURN TO OS
*        PROCESS JOB/SESSION START                                    *
INIT     DS    0H
*        CHECK IF DEPT LIMIT FOR THIS TSO USER HAS BEEN REACHED.
         CLC   SSCTSUSE,ZEROS          IS RESERVED FIELD ZERO
         BNZ   INITTABL                NO, GO USE EXISTING TABLE
         MVC   PDS2NAME,TABLNAME       MOVE LOAD MODULE NAME
         MVC   BLDLPARM(BLDLLEN),BLDLLIST MOVE BLDL HEADER TO DSECT
         BLDL  0,BLDLPARM              LOCATE IN STEPLIB, LNKLST, ETC.
         LTR   R15,R15                 WAS BLDL GOOD
         BZ    LOADTABL                YES, GO LOAD TO CSA
EXIT0    LA    R15,0                   ELSE SET RETURN CODE
         B     EXIT                    GO EXIT
LOADTABL LA    R0,BLDLLIST             LOAD R0 W/A(BLDL PARM LIST)
         LOAD  DE=(0),GLOBAL=YES,ERRET=EXIT0 LOAD TABLE TO CSA
         SR    R4,R4                   CLEAR R4 FOR COMPARE/SWAP
         CS    R4,R1,SSCTSUSE          UPDATE FIELD IF SAFE
         BZ    DELETABL                NO, GO DELETE AND TRY AGAIN
         LR    R8,R1                   SAVE ADDRESS OF TABLE
         USING TABLHDR,R8              ESTABLISH ADDRESSABILITY
         TIME  DEC                     GET DATE AND TIME
         ST    R0,TABLTIME             STORE TIME OF TABLE LOAD
         ST    R1,TABLDATE             STORE DATE OF TABLE LOAD
INITTABL LA    R8,HDRLEN(R8)           SKIP PAST TABLE HEADER
         USING TABLID,R8               ESTABLISH ADDRESSABILITY
INITLOOP CLI   TABLID,X'00'            ARE WE AT END OF TABLE
         BE    EXIT0                   YES, GO RETURN
         CLC   TABLID(IDLEN),JOBNAME   DOES DEPT PREFIX = USER PREFIX
         BE    INITCNT                 YES, GO CHECK FURTHER
         LA    R8,ENTLEN(R8)           ELSE POINT TO NEXT TABLE ENTRY
         B     INITLOOP                GO CHECK NEXT ENTRY
INITCNT  L     R3,TABLCNT              LOAD R3 W/(# CURRENT USERS)
         C     R3,TABLIMIT             IS DEPT AT THEIR LIMIT
         BNL   TIMECHK                 YES, GO CHECK TIME RANGE
INITINCR LR    R4,R3                   COPY CURRENT COUNT
         LA    R4,1(R4)                ADD 1 TO CURRENT COUNT
         CS    R3,R4,TABLCNT           UPDATE COUNT IF SAFE
         BZ    INITCNT                 ELSE GO TRY AGAIN
         B     EXIT0                   GO RETURN
TIMECHK  TIME  DEC                     GET TIME
         C     R0,T0800                IS IT BEFORE 8 AM
         BL    INITINCR                YES, LET USER LOG ON
         C     R0,T1700                IS IT AFTER 5 PM
         BH    INITINCR                YES, LET USER LOG ON
         TPUT  MSG001,MSG001L          ISSUE MESSAGE TO TERMINAL
         LA    R15,4                   LOAD R15 W(AILURE RETURN CODE)
         B     EXIT0                   GO TO COMMON EXIT
DELETABL DELETE DE=BLDLPARM            DELETE TABLE FROM CSA
         B     INIT                    GO START AGAIN
*        DATA CONSTANTS
         LTORG
ZEROS    DC    F'0'                    ZERO CONSTANT
T0800    DC    X'08000000'             TIME CONSTANT FOR 8 AM
T1700    DC    X'17000000'             TIME CONSTANT FOR 5 PM
BLDLLIST DC    H'1',H'60'              BLDL PARMLIST HEADER  --------|
BLDLLEN  EQU   *-BLDLLIST              LENGTH OF BLDL HEADER --------|
MSG001   DC    C'MAX001I DEPARTMENT SIGNON LIMIT HAS BEEN REACHED, PLEAX
               SE TRY AGAIN LATER'                                   |
MSG001L  EQU   *-MSG001 ---------------------------------------------|
SUBNAME  DC    CL4'LMON'               SUBSYSTEM NAME TO SCAN FOR
TABLNAME DC    CL8'TSOTABL'            NAME OF LOAD MODULE TABLE
TABLMAP  DSECT                         MAP DEPARTMENT ENTRIES --------|
TABLHDR  DS    A                       TABLE LENGTH FOR FREEMAIN      |
TABLDATE DS    A                       DATE OF TABLE LOAD             |
TABLTIME DS    A                       TIME OF TABLE LOAD             |
HDRLEN   EQU   *-TABLHDR               LENGTH OF TABLE HEADER         |
IDLEN    EQU   2                       LENGTH OF ID PREFIX            |
TABLID   DS    CL(IDLEN)               DEPARTMENT ID                  |
         DS    CL(8-IDLEN)             FILLER                         |
TABLIMIT DS    F                       MAX USERS FOR THIS DEPARTMENT  |
TABLCNT  DS    F                       CURRENT NUM OF USERS SIGNED ON |
ENTLEN   EQU   *-TABLMAP               LENGTH OF A TABLE ENTRY -------|
* MACROS/DSECTS WE DON'T NEED TO SEE..
         CVT   DSECT=YES
         IEFJESCT
         IEFJSCVT
         IHAASCB
         IRAOUCB
WORKAREA DSECT                         WORK AREA
SAVEAREA DC    18F'0'                  SHOULD BE FIRST IN WORKAREA
BLDLPARM DC    H'1',H'60'              BLDL PARMLIST ---|
         IHAPDS PDSBLDL=YES,DSECT=NO  ------------------|
JOBNAME  DS    CL8                     SAVE AREA FOR TSO JOB NAME
SAVEYFL  DS    X                       SAVE AREA FOR OUCBYFL
WORKLEN  EQU   *-WORKAREA              WORK ARE LENGTH
         END
TSOTABL SOURCE
TSOTABL  CSECT
*        TSO DEPARTMENT SIGNON-LIMITING TABLE
*    THIS TABLE CONTAINS A LIST OF DEPARTMENTS WHOSE TSO USAGE IS
*  TO BE RESTRICTED, DEPENDING ON THE NUMBER OF SIGNED-ON USERS IN
*  THE DEPARTMENT.  WE MAKE ONE ASSUMPTION HERE -- SINCE THE USER'S
*  ACTUAL DEPARTMENT IS NOT READILY AVAILABLE TO THIS USER EXIT, WE
*  WILL ASSUME THAT THE FIRST THREE CHARACTERS OF THE USERID REPRESENT
*  THE USER'S DEPARTMENT CODE.
*  THE TABLE BELOW IS ACTUALLY A SKELETON TABLE.  THE ACTIVE TABLE
*  IS LOADED INTO CSA THE FIRST TIME SOMEONE TRIES TO UPDATE THE
*  TABLE BUT FINDS THAT THERE REALLY IS NONE YET. THE ACTIVE TABLE
*  IS POINTED TO BY THE SSCTSUSE FIELD OF THE SSVT.  THE TABLE HAS A
*  12 BYTE HEADER CONSISTING OF:
* OFFSET                    DESCRIPTION
*        *-------------------------------------------------------*
*  + 0   |  TOTAL TABLE LENGTH                          (BINARY) |
*        +-------------------------------------------------------+
*  + 4   |  DATE TABLE WAS LOADED TO CSA              (TIME DEC) |
*        +-------------------------------------------------------+
*  + 8   |  TIME TABLE WAS LOADED TO CSA              (TIME DEC) |
*        *-------------------------------------------------------*
*          TABLE ENTRIES (16 BYTES) HAVE THE FOLLOWING FORMAT:
* OFFSET                    DESCRIPTION
*        *-------------------------------------------------------*
*  + 0   |  DEPARTMENT ID                       (LEFT-JUSTIFIED) |
*        +-------------------------------------------------------+
*  + 8   |  # OF CONCURRENT TSO USERS ALLOWED           (BINARY) |
*        +-------------------------------------------------------+
*  +12   |  # OF CURRENTLY LOGGED-ON USERS              (BINARY) |
*        *-------------------------------------------------------*
**
TABLSTRT DS    0F                      SKELETON DEPARTMENT ID TABLE --|
TABLHDR  DC    A(TABLENG)              TABLE LENGTH FOR FREEMAIN      |
TABLDATE DC    A(0)                    DATE OF TABLE LOAD             |
TABLTIME DC    A(0)                    TIME OF TABLE LOAD             |
HDRLEN   EQU   *-TABLHDR               LENGTH OF TABLE HEADER         |
IDLEN    EQU   2                       LENGTH OF ID PREFIX            |
TABLID   DS    0H                      START OF TABLE ENTRIES         |
         DC    CL8'AP ',F'04',F'0'     ACCOUNTS PAYABLE               |
         DC    CL8'AR ',F'04',F'0'     ACCOUNT RECEIVABLE             |
         DC    CL8'CS ',F'04',F'0'     CORPORATE SECURITY             |
         DC    CL8'GL ',F'04',F'0'     GENERAL LEDGER                 |
         DC    CL8'MR ',F'06',F'0'     MARKET RESEARCH                |
         DC    CL8'OP ',F'08',F'0'     OPERATIONS                     |
         DC    CL8'PY ',F'08',F'0'     PAYROLL                        |
         DC    CL8'SC ',F'08',F'0'     SCHEDULING                     |
         DC    CL8'SY ',F'99',F'0'     SYSTEMS PROGRAMMING(OF COURSE) |
         DC    CL8'TS ',F'04',F'0'     TECHNICAL SUPPORT              |
         DC    4F'0'                   TABLE PATCH AREA               |
         DC    4F'0'                   TABLE PATCH AREA               |
         DC    4F'0'                   TABLE PATCH AREA               |
         DC    4F'0'                   END OF TABLE INDICATOR         |
TABLENG  EQU   *-TABLSTRT              LENGTH OF TABLE ---------------|
         END


Bruce Bordonaro
System Software Manager (USA) © Xephon 1988






























































