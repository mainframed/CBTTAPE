Dynamic concatenation under TSO

The following CLIST provides the user with the ability to concatenate any
dataset ahead of any DDNAME or to remove a dataset from a concatenation.  It is
currently running under TSO/E Release 1.2.

/*-------------------------------------------------------------------*/
/* PARM 1:  OFF TO TURN OFF;  DEFAULT IS TO TURN ON                  */
/* PARM 2:  DDNAME;  DEFAULTS TO ISPPLIB (ISPF PANELS)               */
/* PARM 3:  DSNAME;  DEFAULTS TO USERID.TEST.CNTL IF ISPPLIB         */
/*                   DEFAULTS TO USERID.TEST.CLIST IF SYSPROC        */
/*                   NO DEFAULT IF OTHER DDNAME                      */
/*-------------------------------------------------------------------*/
PROC 0 OFF DDNAME(ISPPLIB) DSNAME()
CONTROL NOMSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ERROR DO
 SET &RET = &LASTCC
 RETURN
END
IF &DDNAME.. = . THEN DO
 SET &DDNAME - ISPPLIB
END
IF &DDNAME = ISPPLIB AND &DSNAME.. = . THEN DO
 SET &DSNAME = &STR(&SYSUID..TEST.CNTL)
END
IF &DDNAME = SYSPROC AND &DSNAME.. = . THEN DO
 SET &DSNAME = &STR(&SYSUID..TEST.CLIST)
END
IF &SYSDSN('&DSNAME') ^= &STR(OK) THEN DO
 WRITE &DSNAME CANNOT BE ALLOCATED
 EXIT
END
SET &RET = 0
SET &SYSOUTTRAP = 2000
LISTA STATUS
SET &Q = 0
DO WHILE &Q < &SYSOUTLINE
 SET &Q = &Q + 1
 SET &SYSDVAL = &STR(&&SYSOUTLINE&Q)
 SET &SYSDVAL = &STR(&SYSDVAL)
 READDVAL &A1 &A2
 SET P = 1
 DO WHILE &P < 3
  SET &A = A
  SET &B = &STR(&&A&P)
  SET &B1 = &STR(&&A&P)
  IF &STR(&B) = &STR(&DDNAME) THEN DO
   SET &Q = &Q - 1
   SET &R = 0
   DO WHILE &Q LE &SYSOUTLINE
    SET &SYSDVAL = &STR(&&SYSOUTLINE&Q)
    SET &SYSDVAL = &STR(&SYSDVAL)
    READDVAL &C1 &C2
    SET &Q1 = &Q + 1
    IF &Q1 LE &SYSOUTLINE THEN DO
     SET &SYSDVAL = &STR(&&SYSOUTLINE&Q1)
     SET &SYSDVAL = &STR(&SYSDVAL)
     READDVAL &E1 &E2
    END
    ELSO DO
     SET &E2 =
    END
    IF (&E2.. = . AND &B. ^= &E1.) OR &B1 = &E1 THEN DO
     SET &B1 =
     SET &R = &R + 1
     SET &LIBR = LIBR
     SET &&LIBR&R = &C1
     SET &Q = &Q + 2
    END
    ELSE DO
     SET &Q = &SYSOUTLINE + 1
     SET &P = 3
    END
   END
  END
  SET &P = &P + 1
 END
END
SET &SYSOUTTRAP = 0
SET &Q = 0
SET &CON = &STR(ALLOC FI(&DDNAME) SHR REUSE DA('&DSNAME'&STR( )
SET &DECON = &STR(ALLOC FI(&DDNAME) SHR REUSE DA(
DO WHILE &Q < &R
 SET &Q = &Q + 1
 SET &LIBR = LIBR
 SET &B = &STR(&&LIBR&Q)
 SET &L = L
 SET &&L&Q = &STR('&B')
 IF &DSNAME ^= &B ! &Q > 1 THEN DO
  SET &CON = &STR(&CON '&B'&STR( ))
 END
 IF &DSNAME ^= &B THEN DO
  SET &DECON = &STR(&DECON '&B'&STR( ))
 END
 ELSE DO
  SET &DECONNO = &Q
 END
END
SET &RPAR = )
SET &CON = &STR(&CON&STR(&RPAR))
SET &DECON = &STR(&DECON&STR(&RPAR))
IF &OFF. ^= OFF THEN DO
 IF &LIBR1 = &STR(&DSNAME) THEN DO
  WRITE &DSNAME ALREADY CONCATENATED AS FIRST DATASET IN DDNAME &DDNAME
  EXIT
 END
 ELSE DO
  SET &RET = 0
  &CON
  IF &RET > 0 THEN DO
   WRITE ERROR RET CODE = &RET AT CONCATENATE OF &DSNAME IN &DDNAME
   EXIT
  END
 /* WRITE &DSNAME TURNED ON AS FIRST LIBRARY IN DDNAME &DDNAME
END
END
ELSE DO
 IF &DECONNO.. = . THEN DO
  WRITE &DSNAME DID NOT EXIST IN CONCATENATION OF DDNAME &DDNAME
  EXIT
 END
 SET &RET = 0
 &DECON
 IF &RET > 0 THEN DO
  WRITE ERROR RET CODE = &RET AT DECONCATENATE OF &DSNAME IN &DDNAME
  EXIT
 END
 WRITE &DSNAME REMOVED FROM CONCATENATION OF &DDNAME AT SEQ &DECONNO
END
EXIT


Nils Plum
Systems Programmer (Denmark)  © Nils Plum 1987
































































