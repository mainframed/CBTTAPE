Converting PDS to sequential
There does not seem to be an existing facility to process all members in a PDS
other than to concatenate all members in the execution JCL.  This is not very
convenient when the PDS is changed as members are added and deleted.  The
following progam was written in response to this need.
PROGRAM OUTPUT
1    Each and every member of an input PDS is copied to an output physical
sequential dataset.
2    The DCB information for the output dataset is copied from the input
dataset.
3    There are two DCBs which point to the input dataset: one for the PDS
directory and one for processing the members.
4    BSAM read and write is used for the PDS members and the sequential output
dataset.
5    Members which are alias names are ignored.
6    The program produces a report which identifies:
     •    The input and output dataset names
     •    The member names which were successfully copied
     •    The total number of members that were copied.
Some safeguards that have been built into the program are:
•    A SYNAD exit to handle the situation of the directory DD (INDIR) pointing
to a non-PDS.
•    An ESTAE routine to handle a 213 abend (which would result if directory
DD(INDIR) was a VSAM cluster).
•    A DCB abend exit for write errors on DD OUTDATA.
* DESCRIPTION: THIS PROGRAM COPIES MEMBERS FROM A PDS INPUT TO     *
*              A SEQUENTIAL OUTPUT. ANY (LEGAL) LRECL AND BLKSIZE  *
*              ARE PERMITTED.                                      *
*              DCB FOR OUTPUT FILE COPIED FROM INPUT FILE.         *
* FILES:       SYSPRINT - MESSAGES                                 *
*              INDIR    - INPUT PDS DIRECTORY                      *
*              INDATA   - INPUT PDS MEMBERS                        *
*              OUTDATA  - OUTPUT SEQUENTIAL                        *
* NOTES:       ESTAE ROUTINE HANDLES 213 ABEND WHICH WOULD RESULT  *
*              IF DD INDIR WERE A VSAM FILE. A SYNAD EXIT HANDLES  *
*              DD INDIR POINTING TO A NON-PDS. THERE IS ALSO A     *
*              DCB ABEND EXIT FOR WRITE ERRORS ON DD OUTDATA.      *
PDSCONV1 CSECT
         STM   R14,R12,12(R13)          SAVE REGISTERS
         LR    R12,R15                  LOAD BASE REGISTER
         USING PDSCONV1,R12
         ST    R13,SAVEA+4              CHAIN SAVE AREA
         LR    R9,R13
         LA    R13,SAVEA
         ST    R13,8(R9)
* ESTABLISH ESTAE ENVIRONMENT TO HANDLE 213 ABEND FOR DD IDIR
ML00000  LR    R3,R13                  REG SAVE AREA IS ESTAE PARM
         ESTAE ESTAX,CT,PARAM=(R3),XCTL=NO,                            *
               PURGE=NONE,ASYNCH=YES,TERM=NO
         OPEN  (INDIR,(INPUT))
         LA    R9,INDIR                ESTABLISH ADDRESSABILITY
         USING IHADCB,R9
         TM    DCBOFLGS,X'10'          OPEN OK?
         BZ    IOPNERR                 NO, BYE
* CANCEL ESTAE AND OPEN SYSPRINT
         ESTAE 0                       CANCEL ESTAE ENVIRONMENT
         OPEN  (SYSPRINT,(OUTPUT))
* SET UP REPORT HEADER
         TIME  ,                       GET TIME FROM SYSTEM
         ST    R1,TIME1
         UNPK  TIME2,TIME1(4)
         MVC   HDR1DATE,TIME2+3        SET DATE IN REPORT HEADER
         AP    CTRPAG,ONEDEC           INCREMENT PAGE COUNT FOR REPORT
         MVC   HDR1PAGE,EDMASK1        SET UP PAGE NUMBER
         ED    HDR1PAGE,CTRPAG         SET UP PAGE NUMBER
         PUT   SYSPRINT,HDR1           PRINT HEADER
         CLEAR OUTLINE
         PUT   SYSPRINT,HDR2           PRINT MESSAGE
         CLEAR OUTLINE
         LH    R7,HDRLINE              SET LINE COUNTER FOR HEADER
         LA    R11,JFCBO               ESTABLISH ADDRESSABILITY
         USING INFMJFCB,R11
         RDJFCB (OUTDATA)              READ JFCB
         LTR   R15,R15                 READ OK?
         BZ    ML00110                 YES, CONTINUE
         MVI   OUTCC,SKIP1             NO, PREPARE ERROR MSG
         MVC   OUTAREA(47),ERRMSG3     BAD READ ON DD OUTDATA
         PUT   SYSPRINT,OUTLINE        PRINT MESSAGE
         B     END9010                 RETURN TO SYSTEM
ML00110  MVC   OUTDSN(44),JFCBDSNM     GET OUTPUT DSN FOR REPORT
         LA    R11,JFCBI               ESTABLISH ADDRESSABILITY
         RDJFCB (INDATA)
         LTR   R15,R15                 READ OK?
         BZ    ML00120                 YES, CONTINUE
         MVI   OUTCC,SKIP1             NO, PREPARE ERROR MSG
         MVC   OUTAREA(47),ERRMSG2     BAD READ ON DD INDATA
         PUT   SYSPRINT,OUTLINE        PRINT MESSAGE
         B     END9010                 RETURN TO SYSTEM
ML00120  MVC   INDSN(44),JFCBDSNM      GET INPUT DSN FOR REPORT
         PUT   SYSPRINT,CPYMSG1        PRINT REPORT HEADERS
         MVI   OUTCC,SKIP1             CARRAGE CNTL FOR SKIP
         LA    R7,1(,R7)               INCREMENT LINE COUNT FOR SKIP
* DIRECTORY PROCESSING - SET-UP JFCB FOR PDS INPUT MEMBER
DIR0110  LA    R9,INDIR                ESTABLISH ADDRESSABILITY
         GET   INDIR,DIRREC            READ DIRECTORY RECORD
         LA    R3,DIRDATA              LOAD START OF DIRECTORY INFO
         ST    R3,R3SAV                SAVE ADDR
DIR0120  CLC   0(8,R3),HIVALUES        END OF DIRECTORY?
         BE    END9000                 YES
         TM    11(R3),X'80'            MEMBER AN ALIAS?
         BNO   DIR0140                 NO, SET UP THIS MEMBER FOR USE
DIR0130  EQU   *                       IGNORE ALIAS SKIP TO NEXT MODULE
         LA    R3,11(R3)               INCR TO CTL FIELD
         NI    0(R3),X'1F'             CLEAR BITS NOT WANTED
         MVC   CTLWRK+1(1),0(R3)       GET NUM OF HALF WORDS USER DATA
         LA    R3,1(R3)                SKIP OVER COUNT FIELD
         AH    R3,CTLWRK               SKIP OVER USER DATA -
         AH    R3,CTLWRK               ADD TWICE AS COUNT IN HALFWDS
         LA    R5,DIRREC               GET NUM ACTIVE BYTES IN BLOCK
         AH    R5,DIRREC               GET ENDING ADDR OF BLOCK
         CR    R3,R5                   END OF DIRECTORY BLOCK?
         BL    DIR0120                 NO, CHECK BALANCE OF BLOCK
         B     DIR0110                 YES, GET NEXT DIRECTORY BLOCK
* MODIFY JFCB OF INPUT DATASET TO INCLUDE PROPER MEMBER NAME
* THEN OPEN INPUT MEMBER AS A SEQUENTIAL DATASET
DIR0140  LA    R11,JFCBI               ESTABLISH ADDRESSABILITY
         USING INFMJFCB,R11
         RDJFCB (INDATA)               READ JBFB
         LTR   R15,R15                 READ OK?
         BZ    DIR0150                 YES, CONTINUE
         MVI   OUTCC,SKIP1             NO, PREPARE ERROR MSG
         MVC   OUTAREA(47),ERRMSG2     BAD READ ON DD INDATA
         PUT   SYSPRINT,OUTLINE        PRINT MESSAGE
         B     END9000                 RETURN TO SYSTEM
DIR0150  MVC   MEMNAME,0(R3)           SAVE MEMBER NAME
         MVC   JFCBELNM,MEMNAME        MOVE MEMBER TO JFCB
         OI    JFCBMASK+4,X'80'        INDICATE JFCB TO BE REWRITTEN
*                                   TO SCHEDULER WORK AREA (SWA)
*                                   AFTER OPEN PROCESSING
         OI    JFCBIND1,JFCPDS         DS IS MEMBER OF PDS
         LA    R9,INDATA               ESTABLISH ADDRESSABILITY
         USING IHADCB,R9
         OPEN  (INDATA,INPUT),TYPE=J   OPEN MODULE FOR PROCESSING
*                                   USING JFCB WE SUPPLY
         TM    DCBOFLGS,X'10'          OPEN OK?
         BZ    IOPNERR                 NO, BYE
         CLI   SWFRST,HIVALU           FIRST TIME PROCESSING?
         BE    DIR0170                 YES, SET UP DCB FOR OUTPUT
DIR0160  MVC   OUTAREA(8),MEMNAME      SAVE MEMBER NAME
         PUT   SYSPRINT,OUTLINE        PRINT MESSAGE
         LA    R7,1(,R7)               INCREMENT LINE COUNTER
         CH    R7,MAXLINE              START NEW PAGE?
         BNH   DIR0180                 NO
         AP    CTRPAG,ONEDEC           INCREMENT PAGE COUNT FOR REPORT
         MVC   HDR1PAGE,EDMASK1        SET UP PAGE NUMBER
         ED    HDR1PAGE,CTRPAG         SET UP PAGE NUMBER
         PUT   SYSPRINT,HDR1           PRINT HEADER
         LH    R7,HDRLINE              SET LINE COUNTER FOR HEADER
         PUT   SYSPRINT,CPYMSG1        PRINT REPORT HEADERS
         MVI   OUTCC,SKIP1             CARRIAGE CNTL - SKIP
         B     DIR0190
DIR0180  MVI   OUTCC,BLNK              CARRIAGE CNTL - SINGLE SPACE
DIR0190  BAL   R10,PM00100             PROCESS MEMBER
         CLOSE (INDATA)                CLOSE MEMBER
         AP    CTRMEM,ONEDEC           INCREMENT COUNTER FOR MEMBERS
         B     DIR0130                 SET UP FOR NEXT MEMBER
DIR0170  IC    R2,DCBRECFM             GET INPUT RECORD FORMAT
         LH    R5,DCBLRECL             GET INPUT LRECL
         LH    R6,DCBBLKSI             GET INPUT BLKSIZE
         STH   R6,STGAMT               SAVE BLKSIZE FOR BUFFER SIZE
         LA    R9,OUTDATA              ESTABLISH ADDRESSABILITY
         USING IHADCB,R9
         STC   R2,DCBRECFM             USE INPUT RECORD FORMAT
         STH   R5,DCBLRECL             USE INPUT LRECL FOR OUTPUT
         STH   R6,DCBBLKSI             USE INPUT BLKSIZE FOR OUTPUT
         OPEN  (OUTDATA,(OUTPUT))
         TM    DCBOFLGS,X'10'          OPEN OK?
         BZ    IOPNERR                 NO, BYE
         XR    R0,R0                   CLEAR R0
         LH    R0,STGAMT               GET BUFFER SIZE
         GETMAIN R,LV=(0)
         ST    R1,STGADDR              SAVE BUFFER ADDRESS
         LR    R4,R1                   CLEAR STG OBTAINED
         XR    R5,R5                     "
         LH    R5,STGAMT                 "
         XR    R2,R2                     "
         XR    R3,R3                     "
         MVCL  R4,R2                     "
         L     R3,R3SAV                RELOAD DIRECTORY ADDR
         MVI   SWFRST,LOVALU           SET SWITCH OFF
         B     DIR0160                 CONTINUE WITH PROCESSING
* PROCESS MEMBER - COPY INPUT TO OUTPUT
PM00100  LA    R9,INDATA               ESTABLISH ADDRESSABILITY
         USING IHADCB,R9
         L     R4,STGADDR              GET BUFFER ADDRESS
         READ  INDEB1,SF,INDATA,(R4),'S'
         CHECK INDEB1,DSORG=ALL
         LH    R6,STGAMT               GET ORIGINAL BLKSIZE
         L     R1,INDEB1+16            ADJUST FOR POSSIBLE SHORT BLK
         SH    R6,14(R1)                "
         LA    R9,OUTDATA              ESTABLISH ADDRESSABILITY
         USING IHADCB,R9
         STH   R6,DCBBLKSI             USE ADJUSTED BLKSIZE FOR WRITE
         WRITE OUTDEB1,SF,OUTDATA,(R4),'S'
         CHECK OUTDEB1,DSORG=ALL
         CLI   ERRABE,LOVALU           WRITE OK?
         BE    PM00100                 YES, READ NEXT BLOCK
         MVI   OUTCC,SKIP1             NO, PRINT ERROR MESSAGE
         MVC   OUTAREA(37),ERRMSG4
         PUT   SYSPRINT,OUTLINE
         B     END9000                 BYE
* PM00110 IS THE EODAD FOR INDATA
PM00110  BR    R10                     RETURN
* END OF JOB PROCESSING, END9000 IS EODAD FOR DIRECTORY
END9000  CLI   ERRABE,LOVALU           ALL WRITES OK?
         BNE   END9030                 NO, BYE
         CLEAR OUTLINE
         MVI   OUTCC,SKIP1             CARRIAGE CNTL FOR SKIP
         MVC   OUTAREA(24),MEMMSG2     PRINT MEMBER COUNT MSG
         MVC   PRNTLEN,EDMASK
         ED    PRNTLEN,CTRMEM
         MVC   OUTLINE+25(10),PRNTLEN
         PUT   SYSPRINT,OUTLINE
END9030  CLI   SWFRST,HIVALU           FIRST TIME PROCESSING?
         BE    END9040                 YES, SKIP FREEMAIN
         XR    R0,R0                   CLEAR R0
         LH    R0,STGAMT               GET STORAGE AMOUNT
         FREEMAIN R,LV=(0),A=STGADDR
END9010  CLOSE (INDIR,,SYSPRINT,,OUTDATA)
         CLI   ERRABE,LOVALU           ALL WRITES OK?
         BNE   END9040                 NO, BYE
         CLI   ERRIO,HIVALU            I/O ERROR ON INDIR DD?
         BE    END9040                 YES, BYE
         L     R13,SAVEAREA+4          RESTORE REGISTER CONTENTS
         LM    R14,R12,12(R13)
         XR    R15,R15                 CLEAR REG 15 FOR ZERO RET CODE
         BR    R14                     RETURN
* FOLLOWING IS TO PROCESS BAD RETURN CODE FOR WRITE ERROR
END9040  L     R13,SAVEAREA+4          RESTORE REGISTER CONTENTS
         LM    R14,R12,12(R13)
         LA    R15,16                  BAD RETURN CODE
         BR    R14                     RETURN
* FOLLOWING IS TO PROCESS FILE OPEN ERROR
IOPNERR  WTO   'FILE OPEN ERROR, PROCESSING ENDED',ROUTCDE=11
         B     END9010
* FOLLOWING IS SYNAD ERROR ROUTINE
IOCHK    MVI   ERRIO,HIVALU            SET ERROR SWITCH
         USING IHADCB,R9
         TM    DCBDSRG1,DCBDSGPO       IS THIS DATASET PDS?
         BO    IOCHK100                YES, UNDETERMINED ERROR
         WTO   'DD INDIR - DATASET NOT PDS, PROCESSING ENDED',         *
               ROUTCDE=11
         MVC   OUTAREA(57),ERRMSG1     NO, PRINT MESSAGE AND END
         PUT   SYSPRINT,OUTLINE        PRINT MESSAGE
         B     IOCHK110
IOCHK100 WTO   'UNDETERMINED ERROR ON DD INDIR, PROCESSING ENDED',     *
               ROUTCDE=11
         MVC   OUTAREA(50),ERRMSG5
         PUT   SYSPRINT,OUTLINE        PRINT MESSAGE
IOCHK110 B     END9010
* DCB EXIT FOR INDATA
EXLIST1  DS    0F
         DC    X'87'                   LAST EXIT IS JFCB EXIT
         DC    AL3(JFCBI)              JFDB EXIT ADDRESS
JFCBI    DS    CL176
* DCB EXITS FOR OUTDATA
EXLISTO  DS    0F
         DC    X'07'                   JFCB EXIT
         DC    AL3(JFCBO)              JFCB EXIT ADDRESS
         DC    X'91'                   LAST EXIT IS DCB ABEND EXIT
         DC    AL3(ABEXIT)             DCB ABEND EXIT ADDRESS
JFCBO    DS    CL176
ABEXIT   ST    R14,R14SAV              SAVE RETURN ADDR
         LA    R2,3(R1)                GET OPTION MASK BYTE ADDR
         MVI   ERRABE,HIVALU           SET ERROR SWITCH
         WTO   'ABEND ON DD OUTDATA',ROUTCDE=11
         TM    0(R2),X'04'             OPTION MASK - IGNORE OK?
         BNO   ABE0010                 NO, LET ABEND CONTINUE
         MVI   0(R2),X'04'             SET OPTION MASK TO IGNORE
ABE0010  L     R14,R14SAV              RESTORE RETURN ADDR
         BR    R14                     END SPECIAL ROUTINE
BLNK     EQU   C' '
SKIP1    EQU   C'0'
HIVALU   EQU   X'FF'
LOVALU   EQU   X'00'
* SWITCHES, CONSTANTS AND WORK AREAS
         DS    0H                      HALFWORD ALIGNMENT
DIRREC   DS    0CL256
         DS    CL2                     NUMBER BYTES USED IN BLOCK
DIRDATA  DS    CL254
HIVALUES DC    8X'FF'                  MARK END OF DIRECTORY
STGAMT   DS    H                       BLOCKSIZE BUFFER VALUE
STGADDR  DS    F                       ADDRESS OF STORAGE OBTAINED
R14SAV   DS    F                       R14 SAVE
R3SAV    DS    F                       R3 SAVE
TIME1    DS    F                       WORK AREA FOR SYSTEM TIME
TIME2    DS    D                       WORK AREA FOR SYSTEM TIME
MEMNAME  DC    CL8' '                  WORK AREA FOR MEMBER NAME
HDRLINE  DC    H'3'                    CONSTANT FOR HEADER LINES
MAXLINE  DC    H'57'                   CONSTANT FOR LINES ON PAGE
CTRMEM   DC    PL4'0'                  COUNTER FOR MEMBERS PROC
CTRPAG   DC    PL3'0'                  COUNTER FOR REPORT PAGE
ONEDEC   DC    PL1'1'                  CONSTANT: ONE DECIMAL
SWFRST   DC    XL1'FF'                 SWITCH FOR FIRST TIME
ERRABE   DC    XL1'00'                 SWITCH FOR WRITE ABEND
ERRIO    DC    XL1'00'                 SWITCH FOR IO ERROR
PRNTLEN  DC    CL10' '                 PRINT AREA FOR COUNTER
MEMMSG1  DC    CL33'FOLLOWING MEMBERS WERE PROCESSED:'
MEMMSG2  DC    CL24'TOTAL MEMBERS PROCESSED:'
EDMASK   DC    XL10'40206B2020206B202120'                EDIT MASK
EDMASK1  DC    XL7'4020206B202120'                           "
OUTLINE  DC    CL133' '                OUTPUT REPORT LINE
         ORG   OUTLINE
OUTCC    DS    CL1                     CARRIAGE CONTROL CHARACTER
OUTAREA  DS    CL132                   GENERAL DATA AREA
CTLWRK   DC    H'0000'                 WORK FIELD FOR DIRECTORY
HDR1     DS    0CL133
HDR1CC   DC    CL1'1'
         DC    CL50'CONVERT ALL MEMBERS OF PDS TO SEQUENTIAL '
         DC    CL15' '
         DC    CL10'RUN DATE: '
HDR1DATE DS    CL5
         DC    CL4' '
         DC    CL5'PAGE '
HDR1PAGE DS    CL7
         DC    CL46' '
HDR2     DS    0CL133
         DC    CL1' '
         DC    CL132'MEMBERS PROCESSED LISTED BELOW.    OUTPUT DATASET *
               DCB COPIED FROM INPUT DATASET DCB.'
ERRMSG1  DC    CL57'INDIR DD - DATASET NOT PDS ORGANISATION, PROCESSING*
                ENDED'
ERRMSG2  DC    CL50'UNDETERMINED ERROR ON DD INDATA, PROCESSING ENDED'
ERRMSG3  DC    CL50'UNDETERMINED ERROR ON DD OUTDATA, PROCESSING ENDED'
ERRMSG4  DC    CL37'ABEND ON DD OUTDATA, PROCESSING ENDED'
ERRMSG5  DC    CL50'UNDETERMINED ERROR ON DD INDIR, PROCESSING ENDED'
CPYMSG1  DS    0CL133
         DC    CL1' '
         DC    CL16'INPUT DATASET: '
INDSN    DC    CL44' '
         DC    CL5' '
         DC    CL16'OUTPUT DATASET: '
OUTDSN   DC    CL44' '
         DC    CL7' '
* DCB'S
INDATA   DCB   DSORG=PS,MACRF=R,DDNAME=INDATA,EODAD=PM00110,           X
               EXLST=EXLISTI
OUTDATA  DCB   DSORG=PS,MACRF=W,DDNAME=OUTDATA,EXLST=EXLISTO
INDIR    DCB   DDNAME=INDIR,BLKSIZE=256,LRECL=256,RECFM=F,             X
               DSORG=PS,EODAD=END9000,MACRF=(GM),SYNAD=IOCHK
SYSPRINT DCB   DSORG=PS,MACRF=(PM),DDNAME=SYSPRINT,                    X
               BLKSIZE=15428,LRECL=133,RECFM=FBA
         LTORG
         IEFJFCBN
         DCBD  DSORG=PS,DEVD=DA
* ESTAE ROUTINE FOR HANDLING 213 ABEND                             *
ESTAX    CSECT
         LR    R11,R15                 LOAD BASE REGISTER
         USING ESTAX,R11
         ST    R14,12(R13)             SAVE R14 FOR RETURN
         CH    R0,SDWAOK               DO WE HAVE AN SDWA?
         BE    ESTAX70                 NO, USE INFO IN REG'S
         USING SDWA,R1                 R1 HAS SDWA ADDR ON ENTRY
         L     R4,SDWAABCC             SYSTEM COMPLETION CODE
         N     R4,CLRABCD              CLEAR UNWANTED BITS
         C     R4,AB213                213 ABEND?
         BE    ESTAX50                 YES, RETRY
* NOT 213 ABEND, INDICATE TO LET ABEND CONTINUE AND RETURN         *
ESTAX20  SETRP RC=0,REGS=(14)
* THIS IS 213 ABEND, SET RETRY WANTED AND RETURN TO SUPERVISOR     *
ESTAX50  LA    R5,ESTAX60              RETRY ROUTINE ADDRESS
         SETRP WKAREA=(1),             R1 HAS SDWA ADDRESS             *
               RC=4,                   INDICATE RETRY                  *
               DUMP=NO,                SUPPRESS ANY SYSTEM DUMP        *
               FRESDWA=YES,            FREE THE SDWA BEFORE RETRY      *
               RETREGS=NO,             DO NOT RESTORE REGS             *
               REGS=(14),              USE R14 FOR RETURN              *
               RETADDR=(5)             ADDRESS FOR THE RETRY
* RETRY ROUTINE                                                    *
ESTAX60  LR    R10,R15                 LOAD BASE REGISTER
         USING ESTAX60,R10
         LR    R13,R1                  GET PARM-REG SAVE AREA ADDR
         WTO   'ERROR ON DD INDIR, VERIFY THAT DATASET IS A PDS',      *
               ROUTCDE=11
         L     R13,4(R13)              RESTORE REGISTERS
         LM    R14,R12,12(R13)
         LA    R15,16                  LOAD RETURN CODE = 16
         BR    R14                     END
* NO SDWA, CHECK ABEND REASON AND SET UP FOR RETRY IF 213          *
ESTAX70  LR    R4,R1                   GET ABEND REASON
         N     R4,CLRABCD              CLEAR UNWANTED BITS
         C     R4,AB213                213 ABEND?
         DS    0F                      FULLWORD ALIGNMENT
AB213    DC    XL4'00213000'           MASK FOR 213 ABEND CODE
CLRABCD  DC    XL4'00FFF000'           MASK TO CLEAR UNWANTED BITS
SDWAOK   DC    H'12'                   SDWA INDICATOR (12=NONE)
         IHASDWA DSECT=YES,VRAMAP=NO
         END
CLEAR macro definition
         MACRO
&NAME    CLEAR &AREA
         LCLC  &LNGTH
&LNGTH   SETC  'L'''
         MVI   &AREA,C' '              CLEAR AREA
         MVC   &AREA+1(&LNGTH&AREA-1),&AREA
         MEND


Harold Dunlap
Senior Technical Analyst
Hudson’s Bay Company (Canada) © Harold Dunlap 1989





























































