Communicating with the SRM

         TITLE 'RSTPG37 - RESET PERFORMANCE GROUP NUMBER VIA SYSEVENT'
*----+----+----+----+----+----+----+----+----+----+----+----+----+----*
* CSECT   : RSTPG37                                                   *
* MODULE  : RSTPG37                                                   *
* AUTHOR  : ENTERPRISE DATA TECHNOLOGIES                              *
* DESC    : RSTPG37 IS A SPECIAL PURPOSE ROUTINE THAT IS INTENDED TO  *
*           BE INVOKED UNDER TSO.  IT WILL UTILIZE THE SYSEVENT SER-  *
*           VICE INTERFACE TO RESET THE PERFORMANCE GROUP OF THE      *
*           ADDRESS SPACE.  IN THIS CASE, PERFORMANCE GROUP 37.       *
* MACROS  : MODESET EXTRACT SETLOCK STORAGE $ESAEPI $ESAPRO $ESASTG   *
* DSECTS  : CVT IHAPSA                                                *
* INPUT   : NONE                                                      *
* OUTPUT  : NONE                                                      *
* PLIST   : INFORMATION IS IN REGISTERS 0 AND 1                       *
*           REG 0 : BYTES 0-1 CONTAIN THE ASID                        *
*                 : BYTE  2   UNUSED                                  *
*                 : BYTE  3   SYSEVENT CODE, 31 IN THIS CASE          *
*           REG 1 : BYTES 2-3 PERFORMANCE GROUP NUMBER                *
* NOTE    : THIS PLIST INFORMATION WAS OBTAINED FROM LY28-1592-3      *
*----+----+----+----+----+----+----+----+----+----+----+----+----+----*
RSTPG37  $ESAPRO
         L     R5,16                   GET @(CVT)
         L     R5,CVTTCBP-CVTMAP(R5)   GET @(DISPATCH TCB'S)
         L     R5,12(R5)               GET @(MY ASCB)
         L     R6,ASCBTSB-ASCB(R5)     PICK UP THE TSB POINTER
         LTR   R6,R6                   Q. IS THERE A TSB PRESENT
         BZ    EXITEXIT                A. NO, ADIOS
* GET IN THE CORRECT STATE FOR THE EXTRACT AND SETLOCK
         MODESET MODE=SUP,KEY=ZERO
         LA    R2,EXTRASID             GET @(DATA FIELD)
* ASK SYSTEM TO SUPPLY MY ASID
         EXTRACT (R2),'S',FIELDS=(ASID)
         MVC   ASID95,EXTRASID+2       PUT IT IN PARM LIST
* SEE IF WE ALREADY HAVE THE LOCAL LOCK (WE SHOULD NEVER HAVE IT!!!)
         SETLOCK TEST,TYPE=LOCAL,BRANCH=(HELD,HAVELOCK)
* OBTAIN THE LOCAL LOCK
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=STDSAVE
         B     LOCK_ACQ                BRANCH TO SYSEVENT CODE
* WE SHOULD NEVER TAKE THIS PATH
HAVELOCK DS    0H
         MVC   LOCKFLAG,LOCK           INDICATE LOCK ALREADY HELD
LOCK_ACQ DS    0H
         MVI   EVNTCODE,RESETPGV       PUT REQUEST VAL IN PLIST
* BUILD THE INFO INTO R0 AND R1, THEN BRANCH TO SRM FOR SERVICE
         L     R0,SYSEVT95             LOAD INFO TO REGISTER 0
         LA    R1,PGN_37               GET NEW PERFORMANCE GRP NUMBER
         L     15,CVTPTR(,0)           GET @(CVT)
         L     15,CVTOPTE-CVT(,15)     GET @(SRM ENTRY POINT)
         BALR  14,15                   BRANCH INTO SRM CODE
         CLC   LOCKFLAG,LOCK           Q. CHECK THE LOCK
         BE    EXIT_PGM                A. OK TO MODESET
* RELEASE THE LOCAL LOCK
         SETLOCK RELEASE,TYPE=LOCAL,REGS=STDSAVE
EXIT_PGM DS    0H
         MODESET MODE=SUP,KEY=NZERO
EXITEXIT DS    0H
         $ESAEPI
LOCK     DC    CL4'LOCK'               USED FOR LOCK TESTING-SETTING
RESETPGV EQU   31                      INDICATE RESET PGN REQUEST
PGN_37   EQU   37                      NEW PERFORMANCE GROUP
         $ESASTG
LOCKFLAG DS    F                       USED AS FLAG
EXTRASID DS    F
SYSEVT95 DS    0F
ASID95   DS    H                       CURRENT ASID
         DS    XL1                     FILLER
EVNTCODE DS    XL1                     SYSEVENT REQUEST BYTE
         CVT DSECT=YES
         IHAPSA
         IHAASCB
         END   RSTPG37                 INDICATE ENTRY POINT


Enterprise Data Technologies (USA)                                 c Xephon 1995

