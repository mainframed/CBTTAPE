An improved TTOC listing

When a LIST TTOC is produced for an HSM back-up or spill
volume, the dataset names that are printed are the HSM-
generated back-up names, not the original dataset names. In the
past this has caused us problems when we have had read errors
on the back-up or spill tape volume and needed to know what
datasets were contained on the tape, so that we could take the
appropriate action. The introduction of TAPECOPY and
TAPEREPL has alleviated some of the problems in that you
can now have a copy of the original tape.

The program shown below (HSM05) will re-create the TTOC
listing showing the real dataset name instead of the HSM-
generated name.  Any back-up datasets that are no longer valid
will be printed with the HSM-generated name and three
asterisks. Figure 1 shows some typical output. Input to HSM05
is the TTOC listing and the HSM BCDS or a copy of the
BCDS.

The JCL to invoke HSM05 is as follows:

//HSM05    EXEC  PGM=HSM05
//HSMBCDS  DD    DSN=HSMP.BCDS,DISP=SHR,
//         AMP=('BUFNI=2,BUFND=21')
//TTOC     DD    DSN=BIE007.TTOC.LISTING,DISP=SHR
//SYSUDUMP DD    SYSOUT=*
//SYSOUT   DD    SYSOUT=*
//PRINTER  DD    SYSOUT=*

Use appropriate values for BUFNI and BUFND on the
HSMBCDS DD card.


HSM05 SOURCE CODE

HSM05    TITLE 'OBTAIN ORIGINAL DATASET NAME'
         MACRO
         BALS  &REG,&BRTO
         BAL   &REG,&BRTO
         DC    S(*+2)
         MEND
HSM05    CSECT
***********************************************************************
*        MODULE NAME.   HSM05.                                        *
*        REMARKS.       THIS PROGRAM WILL OBTAIN THE REAL DSN AND     *
*                       OVERLAY THIS ONTO THE TTOC LISTING DATA SET   *
*                       NAME. THIS WILL BE USEFUL WHEN A VALID        *
*                       BACKUP/SPILL TAPE HAS READ ERRORS, AND THE    *
*                       DATASETS THAT WERE ON THAT TAPE MUST BE       *
*                       RE-CREATED.                                   *
*        REGISTER CONVENTIONS = STANDARD CONVENTIONS.                 *
*                    REGISTER  1       = PARAMETER LIST               *
*                    REGISTER  4       = WORK REGISTER                *
*                    REGISTER  5       = WORK REGISTER                *
*                    REGISTER  9       = ACB ADDRESS                  *
*                    REGISTER  10      = RPL ADDRESS                  *
*                    REGISTER  11      = DCB ADDRESSES                *
*                    REGISTER  12      = BASE REGISTER                *
*                    REGISTER  13      = SAVE AREA ADDRESS            *
*                    REGISTER  14      = RETURN ADDRESS               *
*                    REGISTER  15      = INITIAL ENTRY ADDRESS        *
***********************************************************************
EOF      EQU   X'04'                   END OF FILE
RNF      EQU   X'10'                   RECORD NOT FOUND
MAXSCAN  EQU   9                       MAX NO OF CHARACTERS TO SCAN
         TITLE 'MAIN PROCESSING LOGIC'
         SAVE  (14,12),,HSM05_&SYSDATE-&SYSTIME
         USING HSM05,R12                SET UP BASE ADDRESSABILITY
         LR    R12,R15                  ENTRY POINT ADDRESS
         ST    R13,PREVSA               PREVIOUS SAVEAREA ADDRESS
         LR    R15,R13                  SWITCH
         LA    R13,SAVEAREA             LENGTH OF WORKAREA
         ST    R13,8(,R15)              STORE IN CALLERS NEXT SA
         SR    R3,R3                    ZEROIZE
         SR    R8,R8                    ZEROIZE
         LA    R0,L'BUFFER(0,0)         BUFFER LENGTH
GETBUF   EQU   *
         GETMAIN R,LV=(0),RELATED=(FREEBUF,'FREE BUFFER STORAGE')
         LR    R4,R1                    BUFFER ADDRESS
         USING VSAMBUF,R4               INFORM THE ASSEMBLER
* OBTAIN ACB AND RPL
ACBGET   EQU   *
         GENCB BLK=ACB,                                                X
               AM=VSAM,                                                X
               DDNAME=HSMBCDS,          OBTAIN ADDRESSABILITY          X
               MACRF=(KEY,AMODE31,DIR,SEQ,IN)
         LTR   R15,R15                  ACB OBTAINED?
         BNZ   GENERR                   NO- ABEND
* R0 = ACB LENGTH  R1 = ACB ADDRESS
         LR    R9,R1                    ACB ADDRESS
         USING IFGACB,R9                OBTAIN DSECT ADDRESSABILITY
GETRPL   EQU   *
         GENCB BLK=RPL,                                                X
               AM=VSAM,                                                X
               ACB=(R9),                                               X
               OPTCD=(KEY,DIR,FWD,SYN,MVE),                            X
               KEYLEN=(*,KEYLEN),                                      X
               ARG=TTOCKEY,                                            X
               AREA=(R4),                                              X
               AREALEN=(*,RECLEN)
         LTR   R15,R15                  RPL OBTAINED?
         BNZ   GENERR                   NO- ABEND
* R0 = ACB LENGTH  R1 = ACB ADDRESS
         LR    R10,R1                   RPL ADDRESS
         USING IFGRPL,R10               OBTAIN DSECT ADDRESSABILITY
* OPEN FILES
OPEN     EQU   *
         OPEN  (PRINT,OUTPUT,TTOCLST,INPUT,(R9))  OPEN FILES
         LA    R11,PRINT                PRINT DCB
         USING IHADCB,R11               OBTAIN DSECT ADDRESSABILITY
         TM    DCBOFLGS,X'10'           PRINT DCB OPENED?
         BZ    OPENERR                  NO - ERROR
         LA    R11,TTOCLST              TTOC LISTING DCB
         USING IHADCB,R11               OBTAIN DSECT ADDRESSABILITY
         TM    DCBOFLGS,X'10'           TTOC LISTING DCB OPENED?
         BZ    OPENERR                  NO - ERROR
         LTR   R15,R15                  BCDS OPENED?
         BNZ   OPENERR                  NO - ERROR
* READ THE BCDS
READBCDS EQU   *
         GET   TTOCLST,TTOC             READ A TTOC RECORD
         LA    R5,TTOCDSN               @ TTOC DSN
         LA    R6,MAXSCAN               MAX NO OF CHARACTERS TO SCAN
LOCBACK  EQU   *
         CLC   0(L'BACK,R5),BACK        .BACK. FOUND?
         BE    DIRREAD                  YES-
         LA    R5,1(,R5)                MOVE ONTO NEXT CHARACTER
         BCT   R6,LOCBACK               DO WHILE R6 > 0?
         B     PUTREC                   OUTPUT PRINT LINE
DIRREAD  EQU   *
         GET   RPL=(R10)                DIRECT GEN READ?
         B     DIRCHK(R15)              READ OKAY?
DIRCHK   B     RDOKAY                   YES-
         B     INVREQ                   REQUEST NOT ACCEPTED
         B     SHOW                     LOGICAL ERROR
         B     PHYERROR                 PHYSICAL ERROR
RDOKAY   EQU   *
         MVC   TTOCDSN(L'MCCDSN),MCCDSN  ORIGINAL DSN
PUTREC   EQU   *
         PUT   PRINT,TTOC               OUTPUT RECORD
         B     READBCDS                 DO WHILE NOT EOF?
SHOW     EQU   *
         SHOWCB AREA=FDBKCODE,          OBTAIN THE                     X
               FIELDS=FDBK,             RPL FEEDBACK                   X
               LENGTH=4,                LENGTH                         X
               RPL=(R10)                RPL ADDRESS
         LTR   R15,R15                  SHOWCB OKAY?
         BNZ   SHOWERR                  NO- ABEND
         CLI   REASON,RNF               RECORD NOT FOUND?
         BE    RECNOTFD                 YES-
         B     LOGERROR                 UNKNOWN ERROR
RECNOTFD EQU   *
         MVC   TTOCRNF,=C'***'          INDICATE RECORD NOT FOUND
         B     PUTREC                   OUTPUT THE RECORD
CLOSE    EQU  *
         CLOSE (PRINT,,TTOCLST,,(R9))     CLOSE THE FILES
         LA    R0,L'BUFFER(0,0)         BUFFER LENGTH
         LR    R1,R4                    BUFFER ADDRESS
FREEBUF  EQU   *
         FREEMAIN R,LV=(0),A=(1),RELATED=(GETBUF,'FREE BUFFER STORAGE')
EXIT     EQU   *
         L     R13,PREVSA               ADDRESS OF THE PREVIOUS SA
         RETURN (14,12),,RC=0           RETURN TO THE INITIATOR
* ABENDS
GENERR   EQU   *
         ABEND 001,DUMP                 GENCB ERROR
OPENERR  EQU   *
         ABEND 002,DUMP                 OPEN ERROR
SHOWERR  EQU   *
         ABEND 004,DUMP                 SHOWCB ERROR
LOGERROR EQU   *
         ABEND 005,DUMP                 LOGICAL ERROR
PHYERROR EQU   *
         ABEND 006,DUMP                 PHYSICAL ERROR
INVREQ   EQU   *
         ABEND 007,DUMP                 PHYSICAL ERROR
         LTORG
SAVEAREA DC    18F'0'                  REGISTER SAVEAREA
PREVSA   EQU   SAVEAREA+4,4            PREVIOUS SAVEAREA
BACK     DC    CL6'.BACK.'             LOCATE VARIABLE
PRINT    DCB   DSORG=PS,DEVD=DA,MACRF=(PM),DDNAME=PRINTER
TTOCLST  DCB   DSORG=PS,DEVD=DA,MACRF=(GM),DDNAME=TTOC,EODAD=CLOSE
TTOC     DS    CL121                   TTOC LISTING
TTOCDSN  EQU   TTOC+4,11               BACKUP DSN
TTOCKEY  EQU   TTOC+4,44               MCC KEY
TTOCRNF  EQU   TTOC+49,3               RNF INDICATION
KEYLEN   DC    F'44'                   KEY LENGTH
RECLEN   DC    F'2040'                 MAX RECORD LENGTH
FDBKCODE DS    F                       RPL FEEDBACK CODE
REASON   EQU   FDBKCODE+L'FDBKCODE-1   REASON CODE
VSAMBUF  DSECT
BUFFER   DS    CL2040
MCCDSN   EQU   BUFFER+64,44            ORGINAL DATASET NAME
         IFGACB
         IFGRPL
         DCBD  DSORG=(QS),DEVD=DA
         END   HSM05


R F Perretta
Systems Consultant (UK)                                     c Xephon 1992

