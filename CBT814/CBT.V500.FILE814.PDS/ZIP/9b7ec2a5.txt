REXX delay timer program


    ADDRESS TSO "DELAY 300"
Source code for the TSO DELAY program
         TITLE 'TIMER DELAY PROGRAM'
DELAY    CSECT
         SAVE  (14,12)
         LR    R12,R15
         USING DELAY,R12
         ST    R13,SAVEAREA+4
         LA    R2,SAVEAREA
         ST    R2,8(,R13)
         LR    R13,R2
         L     R6,0(,R1)
         LH    R5,2(,R6)
         LA    R4,4(R5,R6)
         LH    R3,0(,R6)
         SR    R3,R5
         SH    R3,=H'4'
         BZ    STAX
         CH    R3,=H'15'
         BH    ERRMSG1
         LR    R2,R3
         LR    R7,R4
VALLOOP  EQU   *
         CLI   0(R7),C'0'
         BL    ERRMSG2
         CLI   0(R7),C'9'
         BH    ERRMSG2
         LA    R7,1(,R7)
         BCT   R2,VALLOOP
         BCTR  R3,*-*
         EX    R3,PACK
         CVB   R2,DEC
         MH    R2,=H'100'
         ST    R2,TIME
STAX     EQU   *
         STAX  WAKEUP,OBUF=(ATTNMSG,L'ATTNMSG),TOPLEVL=YES
         LTR   R15,R15
         BZ    STIMER
         TPUT  MSG3,L'MSG3
STIMER   EQU   *
         STIMER REAL,WAKEUP,BINTVL=TIME
         WAIT  1,ECB=WECB,LONG=YES
RETURN   EQU   *
         L     R13,4(R13)
         RETURN (14,12),RC=0
ERRMSG1  EQU   *
         TPUT  MSG1,L'MSG1
         B     ERRRETN
ERRMSG2  EQU   *
         TPUT  MSG2,L'MSG2
         B     ERRRETN
ERRRETN  EQU   *
         TPUT  MSGX,L'MSGX
         L     R13,4(R13)
         RETURN (14,12),RC=12
WAKEUP   EQU   *
         SAVE  (14,12)
         LR    R10,R15
         DROP  R12
         USING WAKEUP,R10
         LA    R3,EXITSAVE
         ST    R13,4(,R3)
         ST    R3,8(,R13)
         LR    R13,R3
         POST  WECB
         L     R13,4(R13)
         RETURN (14,12),RC=0
         DROP  R10
         USING DELAY,R12
ATTNMSG  DC    C'DELAY TERMINATED DUE TO ATTN'
MSG1     DC    C'ARGUMENT LONGER THAN 16 CHARACTERS'
MSG2     DC    C'ARGUMENT NOT NUMERIC'
MSG3     DC    C'STAX MACRO FAILURE, DELAY CONTINUES'
MSGX     DC    C'DELAY TERMINATED DUE TO ERROR'
PACK     PACK  DEC,0(*-*,R4)
SAVEAREA DS    18F
EXITSAVE DS    18F
WECB     DC    F'0'
TIME     DC    F'6000'
DEC      DS    D
         LTORG
         REGEQUS
         END   DELAY


Source code for the batch DELAYB program
         TITLE 'DELAYB ----- GENERAL BATCH TIMER PROGRAM.'
DELAYB   CSECT
         REGEQUS
         SAVE  (14,12)
         LR    R12,R15
         USING DELAYB,R12
         ST    R13,SAVEAREA+4
         LA    R2,SAVEAREA
         ST    R15,8(R13)
         LR    R13,R2
MAINSEC  EQU   *
         L     R9,0(,R1)
         LH    R2,0(,R9)
         CH    R2,=H'0'
         BE    ERROR01
         CH    R2,=H'8'
         BNE   ERROR04
         LA    R9,2(R9)
HOURS    EQU   *
         LA    R8,0(R9)
         BAL   R7,CHECKNUM
         LA    R8,1(R9)
         BAL   R7,CHECKNUM
         CLC   =C'23',0(R9)
         BNH   ERROR02
         MVC   PAUSE(2),0(R9)
MINS     EQU   *
         LA    R8,3(R9)
         BAL   R7,CHECKNUM
         LA    R8,4(R9)
         BAL   R7,CHECKNUM
         CLC   =C'60',3(R9)
         BNH   ERROR02
         MVC   PAUSE+2(2),3(R9)
SECS     EQU   *
         LA    R8,6(R9)
         BAL   R7,CHECKNUM
         LA    R8,7(R9)
         BAL   R7,CHECKNUM
         CLC   =C'60',6(R9)
         BNH   ERROR02
         MVC   PAUSE+4(2),6(R9)
         STIMER WAIT,DINTVL=PAUSE
         B     EXIT
ERROR01  EQU   *
         WTO   MF=(E,TIMWTO3)
         LA    R15,8
         B     EXIT2
ERROR02  EQU   *
         WTO   MF=(E,TIMWTO2)
         LA    R15,9
         B     EXIT2
ERROR03  EQU   *
         WTO   MF=(E,TIMWTO4)
         LA    R15,10
         B     EXIT2
ERROR04  EQU   *
         WTO   MF=(E,TIMWTO5)
         LA    R15,11
         B     EXIT2
EXIT     EQU   *
         SR    R15,R15
EXIT2    EQU   *
         L     R13,SAVEAREA+4
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14
CHECKNUM EQU   *
         CLI   0(R8),C'0'
         BL    ERROR03
         CLI   0(R8),C'9'
         BH    ERROR03
         BR    R7
TIMWTO2  WTO   '* DELAYB : INVALID WAIT TIME ENTERED  *',MF=L
TIMWTO3  WTO   '* DELAYB : NO WAIT TIME ENTERED       *',MF=L
TIMWTO4  WTO   '* DELAYB : NON-NUMERIC INPUT          *',MF=L
TIMWTO5  WTO   '* DELAYB : INVALID PARM LENGTH        *',MF=L
SAVEAREA DS    18F
PAUSE    DS    CL8
         END


Example REXX EXEC to Execute DELAYB

/* REXX */
ARG V
SAY "ASLEEP"
ADDRESS ATTCHMVS 'DELAYB V'
SAY "AWAKE AGAIN"


Sample JCL to Execute DELAYB

//STS01A   JOB  CLASS=A,MSGCLASS=Q,MSGLEVEL=(1,1),NOTIFY=STS01
//*
//*   THIS JOB IS USED TO RUN EXEC DELAYBT FROM FIGURE THREE.
//*
//STEP1    EXEC PGM=IRXJCL,PARM='DELAYBT 00:00:59'
//SYSTSPRT DD   SYSOUT=*
//SYSEXEC  DD   DSN=STS01.BD.REXX,DISP=SHR
//SYSTSIN  DD   DUMMY


ASSEMBLY AND LINK JCL

//STS01A   JOB  CLASS=A,MSGCLASS=Q,MSGLEVEL=(1,1)
//*
//ASM1     EXEC PGM=IEV90,PARM='OBJ,NODECK,XREF(SHORT)'
//SYSLIB   DD   DSN=SYS1.MACLIB,DISP=SHR
//         DD   DSN=SYS1.MODGEN,DISP=SHR
//         DD   DSN=SYS1.TSOMAC,DISP=SHR
//SYSUT1   DD   UNIT=SYSDA,SPACE=(1700,(600,100))
//SYSUT2   DD   UNIT=SYSDA,SPACE=(1700,(300,50))
//SYSUT3   DD   UNIT=SYSDA,SPACE=(1700,(300,50))
//SYSPRINT DD   SYSOUT=*
//SYSLIN   DD   UNIT=SYSDA,SPACE=(80,(200,50)),DISP=(NEW,PASS)
//SYSIN    DD   DSN=STS01.BD.SOURCE(DELAYB),DISP=SHR
//*
//LKED1    EXEC PGM=IEWL,PARM=('MAP,LET,LIST,NCAL'),
//         COND=(0,NE,ASM1)
//SYSUT1   DD   UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSPRINT DD   SYSOUT=*
//SYSLMOD  DD   DSN=SYS3.LINKLIB,DISP=SHR
//SYSLIB   DD   DSN=*.ASM1.SYSLIN,DISP=(OLD,DELETE)
//SYSLIN   DD   *
  INCLUDE  SYSLIB
  NAME DELAYB(R)
/*

John Bradley
Senior Systems Support Engineer (UK)         c Xephon 1996

