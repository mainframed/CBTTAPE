Modifying VTOC records from ISPF

Zapping VTOC records using AMASPZAP can be dangerous
and very tedious.  ISPFDSCB is a program that performs full-
screen VTOC record display and alteration.  ISPFDSCB allows
for the display and alteration of format-4, format-1, and
format-3 Dataset Control Blocks (DSCBs).  New values for
fields are simply 'typed-over' and then the DSCB can be written
back.  ISPFDSCB provides ACF2 security and an audit trial
via SYSLOG.  ISPFDSCB makes VTOC alteration fast,
simple, and safe.  Figure 1 shows a sample format-1 DSCB as
displayed by ISPFDSCB.

ISPFDSCB makes use of five panel members (DSCBPNL,
DSCB1PNL, DSCB3PNL, DSCB4PNL, and DSCBCNFR) and
one message member (DSCB00).  ISPFDSCB is invoked by
placing a statement like DSCB,'PGM(ISPFDSCB)' in the
primary panel.

ISPFDSCB SOURCE CODE

         MACRO
&LABEL   VDEFINE &VARNAME,&LEN
&LABEL   LA    R15,=CL8'&VARNAME'
         ST    R15,DEFVAR
         LA    R15,&VARNAME
         ST    R15,DEFSTOR
         LA    R15,=F'&LEN'
         ST    R15,DEFLEN
         OI    DEFLEN,X'80'
         LA    R1,DEFPARM
         L     R15,LINKADDR
         BALR  R14,R15
         MEND
         MACRO
&LABEL   DISPLAY &PANEL
&LABEL   LA    R15,=CL8'&PANEL'
         ST    R15,DISPANEL
         OI    DISPANEL,X'80'
         LA    R1,DISPARM
         L     R15,LINKADDR
         BALR  R14,R15
         MEND
         MACRO
&LABEL   MESSAGE &PANEL,&MSG
&LABEL   LA    R15,=CL8'&PANEL'
         ST    R15,DISPANEL
         LA    R15,=CL8'&MSG'
         ST    R15,DISMSG
         OI    DISMSG,X'80'
         LA    R1,DISPARM
         L     R15,LINKADDR
         BALR  R14,R15
         MEND
ISPFDSCB CSECT SCD
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING ISPFDSCB,R12,R2,R3
         LA    R2,4095(,R12)
         LA    R2,1(,R2)
         LA    R3,4095(,R2)
         LA    R3,1(,R3)
         ST    R13,SAVEAREA+4
         LA    R13,SAVEAREA
         L     R5,CVTPTR               CVT ADDRESS
         USING CVTMAP,R5               ADDRESS CVT
         L     R4,CVTUCBSC             GET UCB SEARCH RTN ADDRESS
         ST    R4,UCBSRCH              AND STORE
         L     R4,CVTTCBP              TCB POINTER
         DROP  R5
         L     R4,4(,R4)               OUR TCB
         USING TCB,R4                  ADDRESS TCB
         L     R4,TCBJSCB              FIND JOB STEP CONTROL BACCT
         DROP  R4                      DROP USING
         USING IEZJSCB,R4              JSCB CHAIN
         L     R4,JSCBJCT              JOB CONTROL TABLE
         DROP  R4                      DROP USING
         USING JCT-16,R4               GO TO ACCOUNT AREA
         MVC   USER(8),JCTJNAME        GET JOB NAME
         DROP  R4                      DROP USING
         GETMAIN RU,LV=SWKLEN          GET SAVEAREA AND ACF2 WORKAREA
         USING ACF2AREA,R11            ADDRESS SAVEAREA AND WORKAREA
         LR    R11,R1                  ADDRESSABILITY
         LA    R6,ACF2AREA             ADDRESS OF AREA
         LA    R7,WORKLEN              LENGTH OF AREA
         LR    R8,R12                  ANY ADDRESS
         XR    R9,R9                   CLEAR REG 9
         MVCL  R6,R8                   SET WORKAREA TO X'00'
         XC    ACFSPARM(ACFSPRML),ACFSPARM CLEAR PARM LIST
         MVI   ACFSPREQ,ACFSPRDS       REQ DSN VALIDATION ONLY
         MVI   ACFSPID1,ACFSPIUR       USER CALL
         MVI   ACFSPAC1,ACFSPAOU       WRITE ACCESS
         MVI   ACFSPAC2,0
         LA    R5,DSNAREA              GET ADDRESS OF DSNHEAD
         ST    R5,ACFSPDSN             GIVE ACF ADDRESS OF DSN
         ACFGACVT R10,NONE=NOACF2      -> ACF'S CVT
         USING ACCVT,R10               ADDRESS IT
         STM   R10,R11,ACF2SAVE        SAVE ADDRESS
         MVC   DSNAREA(44),=CL44'FORMAT4.DSCB' VTOC NAME
         ACFSVC ACFSPARM,TYPE=S,NONE=NOACF2,CVT=HAVE
         LTR   R15,R15                 IS USER AUTHORIZED ?
         BZ    SPFDEF                  YES...CONTINUE
         OI    FLAG,X'01'              SET NO ACF2 FLAG ON
         MVC   PNLMSG,NOACFMSG         INDICATE NO ACCEPT COMMAND
SPFDEF   BAL   R9,DEFINE               DEFINE VARIABLES TO ISPF
MAIN     XC    DS1DSNAM(145),DS1DSNAM  CLEAR DSCB AREA
         XC    OLDVOL,OLDVOL           CLEAR OLD VOLSER
         XC    OLDDSN,OLDDSN           CLEAR OLD DSN
         MVC   DSNSCRN,=CL46' '        BLANK DSN
         MVC   VOL,=CL46' '            BLANK VOLSER
         MVI   DSCBTYPE,C' '           BLANK TYPE SELECTION
         DISPLAY DSCBPNL               DISPLAY INITIAL PANEL
         LTR   R15,R15
         BNZ   RETURN
MAINCHK  CLC   DSCBTYPE,=C'1'          TYPE 1 DSCB ?
         BE    TYPE1
         CLC   DSCBTYPE,=C'4'          TYPE 4 DSCB ?
         BE    TYPE4
         MESSAGE DSCBPNL,DSCB001       DISPLAY ERROR MESSAGE
         LTR   R15,R15
         BNZ   RETURN
         B     MAINCHK
TYPE1    NI    FLAG,X'0F'              TURN OFF PREV DSCB INDICATORS
         OI    FLAG,X'80'              INDICATE TYPE 1
         DISPLAY DSCB1PNL              DISPLAY DSCB 1 PANEL
TYPE1BCK MVI   DSCBTYPE,C'1'           TYPE 1 DSCB
         LTR   R15,R15
         BNZ   MAIN
         MVC   DSN,=CL46' '            CLEAR RESULT NAME
         CLI   DSNSCRN,C''''           START WITH QUOTE ?
         BNE   NOQUOTE                 NO - SKIP
         LA    R14,DSN                 SET RESULT NAME
         LA    R15,DSNSCRN             AND BEGIN NAME
         LA    R15,1(,R15)             BYPASS QUOTE
ENDFIND  CLI   0(R15),C''''            END QUOTE ?
         BE    DSNFIN                  YES - EXIT
         CLI   0(R15),C' '             HOW ABOUT A BLANK ?
         BE    DSNFIN                  YES - GET OUT
         MVC   0(1,R14),0(R15)         NO - GET A CHARACTER
         LA    R14,1(,R14)             NEXT BYTES
         LA    R15,1(,R15)             .
         C     R15,=A(DSNSCRN+46)      ALL CHECKED
         BNL   DSNFIN                  YES - EXIT
         B     ENDFIND                 NO - CONTINUE
NOQUOTE  MVC   DSN(5),USER             GET USERID
         MVI   DSN+5,C'.'              AND DELIMITER
         MVC   DSN+6(38),DSNSCRN       REST OF DATASET NAME
         MVI   DSNSCRN,C''''           SET START QUOTE
         LA    R14,DSN                 START OF NAME
         LA    R15,DSNSCRN+1           AND OF RESULT
         LA    R1,44                   SET MAX. LOOP
BACKOUT  CLI   0(R14),C' '             END REACHED ?
         BE    LASTQ                   YES - SKIP
         MVC   0(1,R15),0(R14)         NO - GET A CHARACTER
         LA    R14,1(,R14)             NEXT BYTES
         LA    R15,1(,R15)             .
         BCT   R1,BACKOUT              AND LOOP
LASTQ    MVI   0(R15),C''''            SET LAST QUOTE
DSNFIN   CLC   DSN,OLDDSN              SAME DATASET ?
         BNE   TYPE1GET                NO...GO GET NEW
         CLC   OLDVOL,VOL              SAME VOLSER ?
         BNE   TYPE1GET                NO...GO GET NEW
         CLC   CMD,=CL46' '            ANY COMMAND ?
         BE    TYPE1                   NO...THEN NO CHANGE
         CLC   CMD(5),=C'DSCB3'        ASKING FOR DSCB3 ?
         BE    TYPE3
         CLC   CMD(5),=C'ACCEPT'       ASKING TO ACCEPT ?
         BNE   BADCMD1                 NO...INVALID COMMAND
         BAL   R9,ACCEPT               YES...GO TO IT
         LTR   R15,R15                 OK ?
         BZ    UPT1OK                  YES...GO INDICATE
         C     R15,=F'4'               NOT AUTHORIZED ?
         BE    NOAUTH1                 YES...GO INDICATE
         C     R15,=F'8'               NO UCB FOUND ?
         BE    NOUCB1                  YES...GO INDICATE
         C     R15,=F'12'              CANCELLED ?
         BE    CAN1                    YES...GO INDICATE
         MESSAGE DSCB1PNL,DSCB000B     NO...THEN ERROR
         B     TYPE1BCK
UPT1OK   MESSAGE DSCB1PNL,DSCB009      DISPLAY MESSAGE
         B     TYPE1BCK
NOAUTH1  MESSAGE DSCB1PNL,DSCB008      DISPLAY MESSAGE
         B     TYPE1BCK
NOUCB1   MESSAGE DSCB1PNL,DSCB000C     DISPLAY MESSAGE
         B     TYPE1BCK
CAN1     MESSAGE DSCB1PNL,DSCB000E     DISPLAY MESSAGE
         B     TYPE1BCK
BADCMD1  MESSAGE DSCB1PNL,DSCB000A     DISPLAY MESSAGE
         B     TYPE1BCK
TYPE1GET MVC   OLDDSN,DSN              SAVE COPY OF DSN
         XC    DS1DSNAM(145),DS1DSNAM  CLEAR DSCB AREA
         MVC   DS1DSNAM,OLDDSN         MOVE DSN IN
         CLC   VOL,=CL46' '            VOLSER SPECIFIED ?
         BNE   OBTAIN1                 NO...ISSUE OBTAIN
         LOCATE CATLST                 GET VOLSER IF CATALOGUED
         LTR   R15,R15                 WAS IT SUCCESSFUL?
         BNZ   CATERR                  NO - ERROR
         CLI   CATDEV+2,DASD           IS IT CATALOGUED TO DISK?
         BNE   INVDEV                  NO - INVALID DEVICE TYPE
         MVC   VOL(6),CATVOL           MOVE VOLSER FOR OBTAIN
OBTAIN1  MVC   OLDVOL,VOL              COPY OF OLD VOLSER
         OBTAIN DSCBIN1                GET FORMAT 1 DSCB
         LTR   R15,R15                 CHECK RETURN CODE
         BZ    TYPE1                   OK...DISPLAY
OBT1RC4  ST    R15,RCSTORE
         C     R15,=F'4'               NOT MOUNTED ?
         BNE   OBT1RC8                 NO...
         MESSAGE DSCB1PNL,DSCB005      DISPLAY MESSAGE
         B     TYPE1BCK
OBT1RC8  C     R15,=F'8'               NOT FOUND ?
         BNE   OBT1RC12                NO...
         MESSAGE DSCB1PNL,DSCB004      DISPLAY MESSAGE
         B     TYPE1BCK
OBT1RC12 MESSAGE DSCB1PNL,DSCB006      DISPLAY MESSAGE
         B     TYPE1BCK
CATERR   MESSAGE DSCB1PNL,DSCB003      DISPLAY MESSAGE
         B     TYPE1BCK
INVDEV   MESSAGE DSCB1PNL,DSCB002      DISPLAY MESSAGE
         B     TYPE1BCK
TYPE3    MVC   CMD,=CL46' '            RESET COMMAND
         MVI   DSCBTYPE,C'3'           TYPE 3 DSCB ?
         NI    FLAG,X'0F'              TURN OFF PREV DSCB INDICATORS
         OI    FLAG,X'40'              INDICATE TYPE 3
         CLC   DS1PTRDS,=X'0000000000' POINTER TO DSCB3 ?
         BE    NODSCB3                 NO...ERROR
         MVC   DS3CCHHR,DS1PTRDS       SAVE DSCB3 ADDRESS
         OBTAIN DSCBIN3                GET DSCB2 OR DSCB3
         LTR   R15,R15
         BNZ   OBT3FAIL
         CLI   DS3FMTID,C'3'           IS IT A DSCB3 ?
         BE    TYPE3DIS                YES...GO DISPLAY
         CLC   DS1PTRDS,=X'0000000000' POINTER TO DSCB3 ?
         BE    NODSCB3                 NO...ERROR
         MVC   DS3CCHHR,DS1PTRDS       SAVE DSCB3 ADDRESS
         OBTAIN DSCBIN3                GET DSCB3
         LTR   R15,R15
         BNZ   OBT3FAIL
TYPE3DIS DISPLAY DSCB3PNL              DISPLAY DSCB3 PANEL
TYPE3BCK LTR   R15,R15
         BNZ   TYPE1GET
         CLC   CMD,=CL46' '            ANY COMMAND ?
         BE    TYPE3DIS                NO...THEN NO CHANGE
         CLC   CMD(5),=C'ACCEPT'       ASKING TO ACCEPT ?
         BNE   BADCMD3                 NO...INVALID COMMAND
         BAL   R9,ACCEPT               YES...GO TO IT
         LTR   R15,R15                 OK ?
         BZ    UPT3OK                  YES...GO INDICATE
         C     R15,=F'4'               NOT AUTHORIZED ?
         BE    NOAUTH3                 YES...GO INDICATE
         C     R15,=F'8'               UCB NOT FOUND ?
         BE    NOUCB3                  YES...GO INDICATE
         C     R15,=F'12'              CANCELLED ?
         BE    CAN3                    YES...GO INDICATE
         MESSAGE DSCB3PNL,DSCB000B     NO...THEN ERROR
         B     TYPE3BCK
UPT3OK   MESSAGE DSCB3PNL,DSCB009      DISPLAY MESSAGE
         B     TYPE3BCK
NOAUTH3  MESSAGE DSCB3PNL,DSCB008      DISPLAY MESSAGE
         B     TYPE3BCK
NOUCB3   MESSAGE DSCB3PNL,DSCB000C     DISPLAY MESSAGE
         B     TYPE3BCK
CAN3     MESSAGE DSCB3PNL,DSCB000E     DISPLAY MESSAGE
         B     TYPE3BCK
BADCMD3  MESSAGE DSCB3PNL,DSCB000A     DISPLAY MESSAGE
         B     TYPE3BCK
NODSCB3  MESSAGE DSCB1PNL,DSCB007      DISPLAY MESSAGE
         B     TYPE1BCK
OBT3FAIL ST    R15,RCSTORE
         MESSAGE DSCB3PNL,DSCB000D     DISPLAY MESSAGE
         B     TYPE3BCK
TYPE4    NI    FLAG,X'0F'              TURN OFF PREV DSCB INDICATORS
         OI    FLAG,X'20'              INDICATE TYPE 4
         DISPLAY DSCB4PNL              DISPLAY DSCB 4 PANEL
TYPE4BCK LTR   R15,R15
         BNZ   MAIN
         CLC   OLDVOL,VOL              SAME VOLSER ?
         BNE   TYPE4GET                NO...GO GET NEW
         CLC   CMD,=CL46' '            ANY COMMAND ?
         BE    TYPE4                   NO...THEN NO CHANGE
         CLC   CMD(5),=C'ACCEPT'       ASKING TO ACCEPT ?
         BNE   BADCMD4                 NO...INVALID COMMAND
         BAL   R9,ACCEPT               YES...GO TO IT
         LTR   R15,R15                 OK ?
         BZ    UPT4OK                  YES...GO INDICATE
         C     R15,=F'4'               NOT AUTHORIZED ?
         BE    NOAUTH4                 YES...GO INDICATE
         C     R15,=F'8'               NO UCB FOUND ?
         BE    NOUCB4                  YES...GO INDICATE
         C     R15,=F'12'              CANCELLED ?
         BE    CAN4                    YES...GO INDICATE
         MESSAGE DSCB4PNL,DSCB000B     NO...THEN ERROR
         B     TYPE4BCK
UPT4OK   MESSAGE DSCB4PNL,DSCB009      DISPLAY MESSAGE
         B     TYPE4BCK
NOAUTH4  MESSAGE DSCB4PNL,DSCB008      DISPLAY MESSAGE
         B     TYPE4BCK
NOUCB4   MESSAGE DSCB4PNL,DSCB000C     DISPLAY MESSAGE
         B     TYPE4BCK
CAN4     MESSAGE DSCB4PNL,DSCB000E     DISPLAY MESSAGE
         B     TYPE4BCK
BADCMD4  MESSAGE DSCB4PNL,DSCB000A     DISPLAY MESSAGE
         B     TYPE4BCK
TYPE4GET MVC   OLDVOL,VOL              SAVE VOLSER
         XC    DS1DSNAM(145),DS1DSNAM  CLEAR DSCB AREA
         MVC   DS1DSNAM,DSCB4NAM       MOVE IN DSCB4 NAME
         OBTAIN DSCBIN4                GET FORMAT 4 DSCB
         LTR   R15,R15                 CHECK RETURN CODE
         BZ    TYPE4                   OK...DISPLAY
OBT4RC4  ST    R15,RCSTORE
         C     R15,=F'4'               NOT MOUNTED ?
         BNE   OBT4RC8                 NO...
         MESSAGE DSCB4PNL,DSCB005      DISPLAY MESSAGE
         B     TYPE4BCK
OBT4RC8  C     R15,=F'8'               NOT FOUND ?
         BNE   OBT4RC12                NO...
         MESSAGE DSCB4PNL,DSCB004      DISPLAY MESSAGE
         B     TYPE4BCK
OBT4RC12 MESSAGE DSCB4PNL,DSCB006      DISPLAY MESSAGE
         B     TYPE4BCK
ACCEPT   MVC   CMD,=CL46' '            RESET COMMAND
         TM    FLAG,X'01'              AUTHORIZED FOR WRITE ?
         BNO   ACCOK                   YES...CONTINUE
         L     R15,=F'4'               SET RETURN CODE
         BR    R9                      AND RETURN
ACCOK    STM   R10,R11,R10SAVE
         LM    R10,R11,ACF2SAVE        SAVE ADDRESS
         MVI   ACFSPAC1,ACFSPAOT       ALLOC/SCR/RENAME/CAT
         MVC   DSNAREA(44),DSN         MOVE DSN IN
         TM    FLAG,X'20'              DSCB4 ?
         BNO   ACF2CHK                 NO...
         MVC   DSNAREA(44),=CL44'FORMAT4.DSCB'
ACF2CHK  ACFSVC ACFSPARM,TYPE=S,NONE=NOACF2,CVT=HAVE
         LM    R10,R11,R10SAVE
         LTR   R15,R15                 IS USER AUTHORIZED ?
         BZ    UCBFIND                 YES...CONTINUE
         L     R15,=F'4'               SET RETURN CODE
         BR    R9                      AND RETURN
UCBFIND  XC    UCBWORK,UCBWORK         CLEAR WORK AREA
UCBNEXT  LA    R1,UCBPARM              GET PARM ADDRESS
         L     R15,UCBSRCH             GET ADDRESS OF UCB SEARCH RTN
         BALR  R14,R15                 GO FIND UCB
         LTR   R15,R15                 UCB ADDR FOUND ?
         BZ    VOLCHK                  YES...GO CHECK
         L     R15,=F'8'               NO...ERROR..SET RETURN CODE
         BR    R9                      AND RETURN
VOLCHK   L     R7,UCBADDR              LOAD UCB ADDRESS
         USING UCBOB,R7
         CLC   UCBVOLI,VOL             VOLSER MATCH?
         BNE   UCBNEXT                 NO...CHECK NEXT ONE
         XC    BUFLIST(BFLHLN+BFLELN),BUFLIST CLEAR OUT BUFFER LIST
         OI    BFLHFL,BFLHDSCB             INDICATE DSCB ACCESS
         MVI   BFLHNOE,1                   NO. OF ENTRIES IS ONE
*96      MVC   BFLEBUF,=A(DS1FMTID)        ADDR OF DSCB1 TO BUFFER
         MVC   BFLEBUF,=A(DS1DSNAM)        ADDR OF DSCB1 TO BUFFER
         MVI   BFLELTH,DSCBLTH         LENGTH OF DSCB1
         OI    BFLEFL,BFLEMOD          MODIFY DSCB
         OI    BFLEFL,BFLECHR          INDICATE CCHHR
         TM    FLAG,X'80'              TYPE 1 DSCB
         BNO   SETCCHH3                NO...CHECK FOR TYPE 3
         MVC   BFLEARG,DS1CCHHR        MOVE CCHHR OUT
         B     SWTCH1                  AND CONTINUE
SETCCHH3 TM    FLAG,X'40'              TYPE 3 DSCB
         BNO   SETCCHH4                NO...CHECK FOR TYPE 4
         MVC   BFLEARG,DS3CCHHR        MOVE CCHHR OUT
         B     SWTCH1                  AND CONTINUE
SETCCHH4 TM    FLAG,X'20'              IS IT DSCB4
         BNO   ABEND
         MVC   BFLEARG,DS4CCHHR        MOVE CCHHR OUT
SWTCH1   EQU   *
         MVC   UNITADDR,UCBNAME        CONFIRM UNIT ADDRESS
         DISPLAY DSCBCNFR
         LTR   R15,R15
         BZ    ALTER
         L     R15,=F'12'
         BR    R9

This article will be concluded next month when we shall be
publishing the remainder of the source code for ISPFDSCB
plus the panel members DSCBPNL, DSCB1PNL,
DSCB3PNL, DSCB4PNL, and DSCBCNFR, along with the
message member DSCB00.

    © Xephon 1990

