REXX CLIST to display the APF and LNKLST

INTRODUCTION

With ESA came the new (to MVS) language of REXX,
although it has been available on VM for several years.  The
MVS implementation has a few commands that are unique to
MVS and one of these commands is the storage command.
This command allows storage to be retrieved from specified
addresses (and replaced if necessary).

REXX has several commands for changing the format of data
(eg from hexadecimal to character, character to decimal etc)
that make it ideal for manipulating control block data.  The two
REXXs here display the APF table and LNKLST table from
their respective locations in storage.

It should be noted that the initialization routines have added the
default libraries to the lists, which may make them slightly
longer than the PARMLIB equivalents.

DISPLAYING THE APF LIBRARIES

/*  THIS REXX LISTS ALL THE APF LIBRARIES BY BROWSING    */
/*  THE AUTHORIZED LIBRARY TABLE AT OFFSET 484 IN THE    */
/*  CVT.                                                 */
/*  THE LAYOUT IS:                                       */
/*         NUMBER OF ENTRIES    2                        */
/*         LEN OF ENTRY         1        )   TIMES       */
/*         VOLUME               6        )   NUMBER      */
/*         DSN                  VARIABLE )   OF ENTRIES  */
CLEAR                             /* CLEAR SCREEN        */
CVT     = STORAGE(10,4)           /* DECIMAL 16 ==> CVT  */
CCVT    = C2X(CVT)                /* CHAR CVT ADDRESS    */
DCVT    = C2D(CVT)                /* DECIMAL CVT ADDR    */
DAUTH   = DCVT + 484              /* DECIMAL AUTH ADDR   */
AUTH    = D2C(DAUTH,4)            /* AUTH TAB ADDRESS    */
CAUTH   = C2X(AUTH)               /* CHAR AUTH TAB ADDR  */
ADDR1   = STORAGE(CAUTH,4)        /* ADDRESS OF AUTH TAB */
CADDR1  = C2X(ADDR1)              /* CHAR ADDR OF AUTH   */
DATA    = STORAGE(CADDR1,2)       /* GET LUMP OF DATA    */
NUMENT  = SUBSTR(DATA,1,2)        /* NUMBER OF ENTRIES   */
DNUMENT = C2D(NUMENT)             /* CONVERT TO DECIMAL  */
LEN     = (DNUMENT * 51) + 2      /* MAX STORAGE POSS    */
DATA    = STORAGE(CADDR1,LEN)     /* GET LUMP OF DATA    */
START   = 3                       /* START OF 1ST ENTRY  */
SAY '           APF LIBRARIES'
SAY
SAY 'VOLUME     DSN'
SAY
DO I = 1 TO DNUMENT
   LEN     = SUBSTR(DATA,START,1)
   DLEN1   = C2D(LEN)
   DLEN2   = DLEN1 - 6
   VOL     = SUBSTR(DATA,START+1,6)
   DSN     = SUBSTR(DATA,START+7,DLEN2)
   START = START + 1 + DLEN1
   SAY VOL '   ' DSN
   END                            /*  OF DO WHILE        */
SAY
SAY 'THE NUMBER OF APF LIBRARIES IS ' DNUMENT

DISPLAYING THE LNKLST LIBRARIES

/*  THIS REXX LISTS ALL THE LLT LIBRARIES BY BROWSING    */
/*  THE LINK LIST TABLE ADDRESSED FROM OFFSET 1244       */
/*  IN THE CVT.                                          */
/*  THE LAYOUT IS:                                       */
/*         CONSTANT             3 'LLT'                  */
/*         NUMBER OF ENTRIES    4                        */
/*         LEN OF ENTRY         1        )   TIMES NUM   */
/*         DSN                  44       )   OF ENTRIES  */
/*  THE DSN IS PADDED WITH BLANKS SO THE LENGTH          */
/*  ISN'T USED.                                          */
CLEAR                             /* CLEAR SCREEN        */
CVT     = STORAGE(10,4)           /* DECIMAL 16 ==> CVT  */
CCVT    = C2X(CVT)                /* CHAR CVT ADDRESS    */
DCVT    = C2D(CVT)                /* DECIMAL CVT ADDR    */
DLLT    = DCVT + 1244             /* DECIMAL LLT ADDR    */
LLT     = D2C(DLLT,4)             /* LLT  TAB ADDRESS    */
CLLT    = C2X(LLT)                /* CHAR LLT ADDR       */
ADDR1   = STORAGE(CLLT,4)         /* ADDRESS OF AUTH TAB */
CADDR1  = C2X(ADDR1)              /* CHAR ADDR OF LLT    */
DATA    = STORAGE(CADDR1,8)       /* GET LUMP OF DATA    */
NUMENT  = SUBSTR(DATA,5,4)        /* NUMBER OF ENTRIES   */
DNUMENT = C2D(NUMENT)             /* CONVERT TO DECIMAL  */
LEN     = (DNUMENT * 45) + 8      /* MAX SIZE REQ        */
DATA    = STORAGE(CADDR1,LEN)     /* GET LUMP OF DATA    */
START   = 10                      /* START OF 1ST ENTRY  */
SAY '       LINKLIST LIBRARIES'
SAY
DO I = 1 TO DNUMENT
   DSN     = SUBSTR(DATA,START,44)
   START = START + 45
   SAY  DSN
   END                            /*  OF DO WHILE        */
 SAY
SAY 'THE NUMBER OF LIBRARIES IN THE LNKLST IS ' DNUMENT


Michael Bell
Systems  Programmer (UK)                    © Xephon 1990

