Edit macro to invoke a CLIST against edited file

I've written a little ISPF edit macro to invoke a CLIST or TSO
command against the file currently being edited.  This is for
situations where you don't want to bother re-typing the
complete file name of whatever it is you have just finished
editing, eg for an R90AFP, PRINTDS, DSPRINT, PRINTL,
PRINTC, or SCRIPT invocation.  This is also handy when you
just can't squeeze all those extra operands onto that all-too-
short ISPF command line.

To invoke the macro, which I have called CL, you must be in
ISPF edit on the file or member you wish to process with a
CLIST.  At the command line, simply enter CL.  If you have
edited your file but not yet saved it, CL will terminate with an
error message asking you to save the file and try again.  This is
a safety feature, to guard against operating on old data.  You
will be prompted for the name of the CLIST to be invoked, and
for three lines of operands to be used by the CLIST after your
dataset name.  The CLIST name and operands are saved for
subsequent invocations of CL.  CL then invokes the requested
CLIST, supplying the dataset name of the file currently being
edited as the first operand of the CLIST, and your other
operands following the dataset name.  CL then returns to edit
mode.

You can also optionally supply CL with the CLIST name and
up to eight operands, all on the command line.  For example,
CL DSPRINT T075 would immediately request a local print of
the file on printer T075, without displaying any operand
request panel.

CL MACRO

ISREDIT MACRO (COMMAND P2 P3 P4 P5 P6 P7 P8 P9)
/*  MAINLINE CL MACRO                                               */
/*  EXECUTE COMMAND &COMMAND ON THIS DATASET, WITH OPERANDS P2-P9.  */
/* CONTROL LIST CONLIST SYMLIST         /*                          */
ISREDIT (DSN) = DATASET
SET MEMBER = .
ISREDIT (MEMBER) = MEMBER
IF &MEMBER >= . THEN +
  SET DSN = &DSN(&MEMBER)
ISREDIT (CHGST) = DATA_CHANGED
IF &CHGST = YES THEN +
  DO
    SET ZEDSMSG = DATA WAS NOT YET SAVED
    SET ZEDLMSG = PLEASE SAVE DATA FIRST.
    ISPEXEC SETMSG MSG(ISRZ001)
    EXIT CODE(8)
  END
IF &COMMAND.. = . THEN +
  DO
    SET DNAME = &DSN
    ISPEXEC VGET (CLCLIST,CLOPER1,CLOPER2,CLOPER3) PROFILE
    SET CLIST = &CLCLIST
    SET OPER1 = &CLOPER1
    SET OPER2 = &CLOPER2
    SET OPER3 = &CLOPER3
    ISPEXEC VPUT (CLIST,OPER1,OPER2,OPER3) SHARED
    ISPEXEC DISPLAY PANEL(DFQDCL01)
    SET COMMAND = &CLIST
    SET P2 = &OPER1
    SET P3 = &OPER2
    SET P4 = &OPER3
    SET CLCLIST = &CLIST
    SET CLOPER1 = &OPER1
    SET CLOPER2 = &OPER2
    SET CLOPER3 = &OPER3
    ISPEXEC VPUT (CLCLIST,CLOPER1,CLOPER2,CLOPER3) PROFILE
  END
&COMMAND '&DSN' &P2 &P3 &P4 &P5 &P6 &P7 &P8 &P9
EXIT

PANEL DFQDCL01

)BODY
+(DFQDCL01)------------------- CLIST PARAMETER ENTRY --------------------------
 +COMMAND ===>_LECMDIN                                            +
+                                                                TIME - &ZTIME
+DATA SET NAME:%&DNAME                                      +    DATE - &ZDATE
+                                                                      (&ZJDAT)
+ENTER COMMAND PARAMETERS BELOW:
+
+CLIST NAME   ===>_CLIST   +           (1 to 8 character name)
+
+OPERANDS     ===>_OPER1
+             ===>_OPER2
+             ===>_OPER3
+                                      (Enter as many operands as
+                                       required separated by commas or
+                                       blanks. Continue as required)
+
+
+
+
+
+
+
+
+PRESS%ENTER+TO CONTINUE; PRESS%HELP+KEY FOR HELP; PRESS%END+TO EXIT
)INIT
 .HELP = DFQTUT25
 &LECMDIN = '                                                   '
 .CURSOR = LECMDIN
 &ZJDAT = &ZJDATE
)PROC
 VER (&CLIST,NONBLANK,MSG=DFQ036)
)END


Abe Getzler
Senior Technical Analyst
Brooklyn Union Gas (USA)                        © Brooklyn Union Gas 1990

