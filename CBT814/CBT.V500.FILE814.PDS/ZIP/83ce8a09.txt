ISPF commands - a simpler invocation

INTRODUCTION

On reading the title of this article one would be forgiven for
thinking that executing an ISPF command could not be simpler.
Correct - however, a problem we have encountered is not so
much in actually invoking the command but, rather, remembering
which command to invoke. The base-supplied ISPCMDS table
contains about 34 commands. In our shop we now have 90 to 100
commands available, many copied or cloned from MVS or VSAM
Update over the years. The following panel and CLIST were built
to help in deciding which command is required.

Basically, using the ISPF command DCX, the user executes the
CLIST DISPCMDX. This in turn displays the ISPCMDS table,
with the first 27 characters of the description (variable
ZCTDESC) on a scrollable window with a selection field in front
of each line. The user simply places an 'S' against the required
command and hits ENTER. The command is then executed, and
after exiting the invoked function the user is returned to the ISPF
panel from which he or she invoked DCX.

ISPFCMDS TABLE ENTRY FOR DCX COMMAND

DCX       0  SELECT CMD(%DISPCMDX)
                SELECT ISPF COMMANDS DISPLAY

DISPCMDX CLIST

PROC 0
     CONTROL NOFLUSH
ISPEXEC ADDPOP COLUMN(33)
SET &CRP = 1
ISPEXEC TBTOP ISPCMDS
DO WHILE &RC < 8
   ISPEXEC TBDISPL ISPCMDS PANEL(DISPCMDX) CURSOR(SCMD) -
           AUTOSEL(NO) POSITION(CRP)
   SET &RC = &LASTCC
   DO WHILE &ZTDSELS > 0
      IF &SCMD = S THEN GOTO SELECT
      IF &ZTDSELS > 1 THEN -
         DO
           ISPEXEC TBDISPL ISPCMDS POSITION(CRP)
         END
      ELSE -
         DO
           SET &ZTDSELS = 0
         END
    END
END
ISPEXEC REMPOP
IF &RC = 8 THEN GOTO RUN999
SELECT:-
ISPEXEC REMPOP
ISPEXEC VGET (ZDEL)      /* note ZDEL = Command stacking delimiter */
SET &XEND = END
SET &ZCMD = &STR(&XEND&ZDEL&ZCTVERB)
ISPEXEC CONTROL NONDISPL ENTER
ISPEXEC DISPLAY PANEL(DUMMY)
RUN999:-
EXIT

DISPCMDX PANEL

)ATTR
 % TYPE(TEXT) INTENS(HIGH) COLOR(BLUE)
 _ TYPE(INPUT) INTENS(HIGH) COLOR(RED) CAPS(ON)
 # TYPE(OUTPUT) INTENS(HIGH) COLOR(GREEN)
 + TYPE(TEXT) INTENS(LOW) COLOR(TURQ)
 $ TYPE(TEXT) INTENS(HIGH) COLOR(RED)
 | TYPE(TEXT) INTENS(HIGH) COLOR(WHITE) HILITE(USCORE)
)BODY WINDOW(41,20)
%------------+ISPF COMMANDS%------------+
+_ZCMD                                  +
+ Scroll Fwd/Bwd and $S+elect reqd. CMD +
|     CMD      DESCRIPTION              +
)MODEL CLEAR(SCMD ZCTVERB ZCTDESC)
+_Z#Z       #Z                          +
)INIT
  &SCR = CSR
  .ZVARS = '(SCMD ZCTVERB ZCTDESC)'
  .CURSOR = SCMD
  &ZSCROLLD = &SCR
)PROC
)END

DUMMY PANEL

)ATTR
 _ TYPE(INPUT) COLOR(WHITE)
)BODY
+_ZCMD
)PROC
)END


D Matthews (UK)                                                                         c Xephon 1994

