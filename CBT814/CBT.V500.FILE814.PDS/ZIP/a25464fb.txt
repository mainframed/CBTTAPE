Help menus



HELPMENU REXX EXEC

/* REXX */
address "TSO"
x=outtrap(line.)
"LISTALC STAT"
x=outtrap("OFF")
tab_help=""
no_good="SYS1 IPO1"
do  a=2 to line.0
    if  find(line.a,"SYSHELP")>0 then
        do
            parse value value("line."a-1) with q1"."
            if  wordpos(q1,no_good)=0 then
                tab_help=value("line."a-1)
            do  w=a+1 while words(line.w)=1 &,
                    left(line.w,4)\="LINE"
                if  pos(".",line.w)>0 then
                    do
                        parse value line.w with q1"."
                        if  wordpos(q1,no_good)=0 then
                            tab_help=tab_help line.w
                    end
            end
            leave a
        end
end
if  words(tab_help)>0 then
    do
        topicos=""
        do  w=1 to words(tab_help)
            x=outtrap(line.,,"NOCONCAT")
            "listds '"word(tab_help,w)"' members"
            x=outtrap("OFF")
            do  a=7 to line.0
                topicos=topicos word(line.a,1)
            end
        end
        call show_help
    end
else
    say "DD SYSHELP not found"
exit
/* - - - - - - - - - - - - */
show_help:
topicos=@ordtab(topicos)
tab_name="T"time(s)
tab='M N O P Q R S'
address "ISPEXEC"
"tbcreate "tab_name" names("tab") nowrite"
do  a=1 by 7 to words(topicos)
    c=0
    do  w=a to min(a+6,words(topicos))
        c=c+1
        interpret word(tab,c)"=left(word(topicos,w),8)"
    end
    if  w>words(topicos) & w <= a+6 then
        do  w=w to a+6
            c=c+1
            interpret word(tab,c)"=' '"
        end
    "tbadd "tab_name
end
do  forever
    "tbtop "tab_name
    "tbdispl "tab_name" panel(phmenu) csrrow(&csrrow) cursor(&cursor)"
    select
        when rc=8 then
            leave;
        when csrrow=0 | cursor='' then
            nop
        otherwise
            p=(csrrow-1) * 7 + wordpos(cursor,tab)
            call get_help word(topicos,p)
    end
end
"tbend "tab_name
return
/* - - - - - - - - - - - - */
@ordtab: procedure
parse arg tab_sort
tab_sort='* 'tab_sort' *'
do  a=2 to words(tab_sort)-2
    do  b=a+1 to words(tab_sort)-1
        if  word(tab_sort,a)>word(tab_sort,b) then
            tab_sort=subword(tab_sort,1,a-1),
            word(tab_sort,b),
            subword(tab_sort,a+1,b-a-1),
            word(tab_sort,a),
            subword(tab_sort,b+1)
    end
end
return space(subword(tab_sort,2,words(tab_sort)-2),1)
/* - - - - - - - - - - - - */
get_help: procedure
arg nome
tabkey='103 115 117'
x=outtrap(h_line.)
address "TSO" "HELP "nome
x=outtrap(off)
tab_name='t'||time(s)
address "ISPEXEC"
"tbcreate "tab_name" names(linha) nowrite"
do  a=1 to h_line.0
    linha=h_line.a
    "tbadd "tab_name
end
"tbtop "tab_name
do  a=101 to 124
    if  wordpos(a,tabkey)=0 then
        iterate
    "vget zpf"right(a,2)" profile"
    pkey.a=value('zpf'right(a,2))
    interpret "zpf"right(a,2)"=' '"
    "vput zpf"right(a,2)" profile"
end
signal on syntax
do  forever
    "tbdispl "tab_name" panel(pgen05)"
    zedsmsg=""
    select
        when idvpfkey=' ' then
            nop
        when idvpfkey='PF03' then
            leave
        when idvpfkey='PF17' then
            call print_text
        otherwise
            zedsmsg="Undefined PF/PA key"
    end
    if  zedsmsg\="" then
        address "ISPEXEC" "setmsg msg(isrz001)"
end
syntax:
address "ISPEXEC"
do  a=101 to 124
    if  wordpos(a,tabkey)=0 then
        iterate
    interpret "zpf"right(a,2)"=pkey.a"
    "vput zpf"right(a,2)" profile"
end
"tbend "tab_name
return
print_text:
dest="LSW1"
address "TSO"
"outdes laser new",
   "class(J)",
   "chars(LRE1)",     /* 12 cpi coded font */
   "copies(1)",
   "dest("dest")",
   "formdef(SWE09B)", /* formdef for portrait presentation printing */
   "pagedef(SWE09B)", /* pagedef for 6 lpi, 60 lpp */
   "prmode(PAGE)",
   "notify("userid()")"
"alloc f(prtx) outdes(laser) recfm(f b) lrecl(80) sysout"
"execio * diskw prtx (finis stem h_line."
"free file(prtx)"
"free outdes(laser)"
address "ISPEXEC"
zedsmsg = "Print sent to "dest
zedlmsg = "HELP Text sent for printing at device "dest
"SETMSG MSG(ISRZ001)"
return


PGEN05 PANEL

)ATTR DEFAULT(%+_)
/*
| type(text)    color(turquoise)
@ type(output)  color(yellow) caps(off)
)BODY
+
|COMMAND ===>_ZCMD                                            |SCROLL ===>_AMT +
+
)MODEL
+  @Z
)INIT
 .HELP  = ISP00005
 &ZPRIM = NO
 &ZTDMARK = '------------------------------------------------------------------+
             --------------'
 .ZVARS = '(LINHA)'
)PROC
&IDVPFKEY = .PFKEY
)END


PHMENU PANEL

)ATTR DEFAULT(%+_)
/*
| type(text)    color(turquoise)
@ type(input)   color(yellow) caps(off)
)BODY
+
|COMMAND ===>_ZCMD                                            |SCROLL ===>_AMT +
+
)MODEL
   +@Z       +@Z       +@Z       +@Z       +@Z        +@Z       +@Z       +
)INIT
 .HELP  = ISP00005
 &ZPRIM = NO
 .ZVARS = '(M N O P Q R S)'
 &ZTDMARK = '------------------------------------------------------------------+
             --------------'
)REINIT
 .CSRROW = &CSRROW
 .CSRPOS = &CSRPOS
)PROC
 &CURSOR = .CURSOR
 &CSRROW = .CSRROW
 &CSRPOS = .CSRPOS
)END


João Bentes de Jesus
Systems Programmer
Mundial-Confiança SA (Portugal)               c Xephon 1996

