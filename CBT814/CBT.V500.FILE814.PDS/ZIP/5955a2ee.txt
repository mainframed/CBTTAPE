How to use a subsystem console

Previous articles have shown how to develop an MVS
subsystem.  One of the uses for this could be to intercept the
function code 9 calls which MVS issues to all active
subsystems for each WTO.  WTOs can, however, be
intercepted in other ways with MVS exits.

There may be a requirement to issue an MVS command and
then receive and process the response.  The problem with this
is choosing with which console-id to issue the command.
Using a real console-id means interfering with normal operator
usage of the console.

The other difficulty is to disentangle the reply to your
command from the other message traffic on a real console.
One solution is to set up a dedicated hardware console for
software use only.  Route codes should be set to 'none' so only
command replies appear.

A far better method is to use a subsystem console.  This in fact
need not involve the use of an MVS subsystem.  By invoking
the IBM module IEAVG700, a unique console-id can be
assigned.  The operator commands can then be issued with this
console-id and the WTO intercept can look for this id in the
WTO Queue Element (WQE).

Before acquiring the subsystem console, there must be a
number of these defined to be available.  This is done in the
SYS1.PARMLIB member CONSOLxx.  Issuing the command
'D C,N' will display which ones have been defined and which
are currently in use.

Any operator commands can be issued in this way and they are
not limited to MVS commands.  Intercepting the replies and
processing them can be complex but this is dependent on the
installation's requirements.  The appropriate WTO exits are
well documented by IBM.

The advantage of using a subsystem function 9 intercept is that
the subsystem control blocks can be used to pass across the
console-id to be used so that the appropriate WQEs can be
selected.  Using a WTO exit means that this id must be passed
in some other way.

The following code extracts illustrate how to acquire the
subsystem console and issue a command with that console-id.
Refer to previous issues of MVS Update for details on
subsystem function routines.  Note that subsystem consoles can
be released with another call to IEAVG700 but only from the
same address space that was used to acquire it.


Obtaining a subsystem console and ISSUING A COMMAND

          .
          .
          MODESET MODE=SUP        SUPERVISOR STATE
          MODESET EXTKEY=ZERO,SAVEKEY=(2)  SAVE LAST KEY IN R2
          XC    SCSRPLST(SCSLEN),SCSRPLST   CLEAR PARMS
          MVC   SCSACRO,=CL4'SCSR'   NAME OF CONTROL BLOCK
          MVI   SCSVER,SCSVERSN      VERSION
          MVC   SCSNAME,=CL8'ABCD'   SUBSYS NAME (ANYTHING)
 * this function byte determines the operation(s) required
          OI    SCSFUNC1,SCSOBTAN+SCSRTCDF  OBTAIN/SET ROUTE CODE
          LA    R1,SCSRTCD        ROUTE CODE SECTION (SET TO NONE)
          ST    R1,SCSRTCDP       SET PTR
          LA    R1,SCSRPLST       LOAD PARM ADDR
          ST    R1,PARMADDR       SAVE IN WORD
          LA    R1,PARMADDR       POINT TO PARM LIST
          LINK  EP=IEAVG700       INVOKE IBM MODULE
          LTR   R15,R15           TEST RESULT
          BNZ   BADCON            BAD CALL
 * this completion byte indicates if the operation(s) were done
          TM    SCSCOMP1,SCSOBTNC+SCSRTCDC  TEST COMPLETION
          BNO   BADCON            BAD CONSOLE OBTAIN
 * console obtained, id is in SCSCNID
          MVC   CONSID,SCSCNID    SAVE CONSOLE ID
          MODESET KEYADDR=(2)     RESTORE PREVIOUS KEY (IN R2)
          MODESET MODE=PROB       PROBLEM STATE
          .
          .
 * data area contains
 CONSID   DS    H
 PARMADDR DS    F                 ADDR OF PARM LIST
          IEZVG100 DSECT=NO       IEAVG100 PARMS FOR VG700


Issuing aN MVS command with this subsystem console-id

          .
          .
          MVC   CAREA+4(40),COMMAND  OPERATOR COMMAND ITSELF
          LH    R0,CONSID         SET OUR SUBSYSTEM CONSOLE ID
          LA    R1,CAREA          ADDR COMMAND AREA
          MGCR  (R1)              ISSUE COMMAND VIA SVC34
          LTR   R15,R15           TEST
          BNZ   BADCMD            BAD, OUT
          .
          .
 * data area contains
 CAREA    DC    AL2(40),AL2(0),CL40' '


Selecting a WTO queue element for this console-id

          .
          .
          USING WQEMAJ,R6         ADDR MAJOR WQE
          CLC   CONSID,WMJMCNID+2 OUR CONSOLE? (FULLWORD IN WQE)
          BNE   OUT               NO, DONT PROCESS
 * process the WTO for our console
          .
          .
 * data area contains
          IHAWQE FORMAT=NEW       WQE MAP


                                                            c Xephon 1991

