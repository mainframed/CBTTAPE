Checking for RACF group special authority

The IBM-supplied RACF commands which allow updates to
the various RACF profiles grant authority if the issuer is a
'system special', a 'group special' for the owning group of the
target profile, or the owner of the profile. It can be useful to
employ the same logic in programs written in-house that update
RACF user-ids or any other profiles.

To detect 'system special' is very easy, you simply test the
ACEE field in question. To detect 'group special' is more
involved and requires the use of RACXTRT macros to retrieve
the owner (or author) field from the target profile and see if the
issuer is a 'special' for that group. This has to be repeated up
the group structure by following the superior group chain until
special authority is found or group SYS1 is reached.

The code shown below will enable this standard authority
checking to be performed thereby keeping your own commands
in-line with IBM's. After making the update it is also a good
practice to write an SMF type-80 record, which is now easy to
do using the new RACROUTE AUDIT macro as shown below.
A string of log data is specified and will appear on RACF
report writer output if you issue the request EVENT
GENERAL in RACFRW.


standard system/group special/owner checking

* THIS EXAMPLE CHECKS AUTHORITY TO UPDATE A RACF USER-ID PROFILE.
* IT ASSUMES THAT TARGET USERID VALUE IS SET IN THE FIELD 'USERID'.
* FIRST CHECK ISSUING USER'S AUTHORITY THROUGH SYSTEM SPECIAL
         USING PSA,R0            ADDRESSABILITY FOR PSA
         L     R1,PSATOLD        ADDR OUR TCB
         USING TCB,R1
         ICM   R1,15,TCBSENV     ADDR THE ACEE
         BNZ   GOTACEE           BRANCH IF PRESENT
         L     R1,PSAAOLD        ADDR OUR ASCB
         USING ASCB,R1
         L     R1,ASCBASXB       ADDR THE ASXB
         USING ASXB,R1
         L     R1,ASXBSENV       ADDR THE ACEE
* IF ACEE FOUND, TEST ISSUER FOR SYSTEM SPECIAL ATTRIBUTE
GOTACEE  EQU   *
         LTR   R1,R1             TEST ACEE FIRST
         BZ    NOTAUTH           NOT THERE
         USING ACEE,R1           ADDR IT
         MVC   ISSUER,ACEEUSRI   SAVE ISSUER'S ID
         ST    R1,ADDRACEE       SAVE ACEE ADDR
         TM    ACEEFLG1,ACEESPEC TEST FOR SPECIAL USER
         BO    AUTHOK            YES, THEN AUTHORIZED
         DROP  R1                DROP ACEE
* NOW, GROUP SPECIAL CHECK, OBTAIN OWNING GROUP OF TARGET USERID
         LA    R8,USERID         ADDR TARGET USERID FIELD
         LA    R6,RFLD1A         ADDR FIELDS TO BE OBTAINED
         LA    R9,RACWORK        RACROUTE WORK AREA
         MVC   RX2(RX2L),RX1     INITIALIZE REENTRANT AREA
         RACROUTE REQUEST=EXTRACT,WORKA=(9),RELEASE=1.8.1,             X
               TYPE=EXTRACT,FIELDS=(6),ENTITY=(8),MF=(E,RX2)
         LTR   R15,R15           TEST RACROUTE RETURN CODE
         BNZ   BADRXRT           RACXTRT FAILED
         LR    R7,R1             RETURN AREA ADDR
* PROCESS THE RETURNED INFOMATION, SEG AREA IS LEN(4), DATA(8)
         XR    R4,R4             CLEAR
         IC    R4,0(R7)          SUBPOOL OF GETMAINED AREA
         XR    R5,R5             CLEAR
         ICM   R5,B'0111',1(R7)  LENGTH OF GETMAINED AREA
         LH    R6,4(R7)          OFFSET TO SEGMENT AREA
         AR    R6,R7             ADD BASE ADDR FOR SEGMENT AREA
         MVC   OWNERN,4(R6)      COPY USER PROFILE OWNER (8)
         FREEMAIN R,LV=(5),A=(7),SP=(4) FREE RACXTRT AREA
         LA    R11,200           PREVENT INFINITE LOOP IF BUG
* CHECK IF ISSUER USERID IS THE ACTUAL OWNER OF THE PROFILE
         CLC   ISSUER,OWNERN     IS PROFILE OWNER THE ISSUER
         BE    AUTHOK            COMMAND AUTHORIZED
* CHECK IF ISSUER HAS GROUP SPECIAL FOR THE USER'S OWNING GROUP
CHECKG   EQU   *
         L     R4,ADDRACEE       LOAD ACEE ADDR OF ISSUER
         USING ACEE,R4           ADDR
         L     R5,ACEECGRP       ADDR OF CONNECT TABLE, 1
         LTR   R5,R5             TEST
         BNZ   OKCG              OK, THERE
         L     R5,ACEEFCGP       ADDR OF CONNECT TABLE, 2
         LTR   R5,R5             TEST
         BZ    NOTAUTH           NOT THERE
OKCG     EQU   *
         USING CGRP,R5           ADDR CONNECT TABLE
         LA    R6,CGRPENT        ADDR FIRST ENTRY
         LH    R7,CGRPNUM        NUMBER OF ENTRIES
         USING CGRPENTD,R6       ADDR IT
LOOPG    EQU   *
         CLC   OWNERN,CGRPNAME   IS THIS THE GROUP
         BE    MATCHG            YES
         LA    R6,L'CGRPENT(R6)  INCREMENT ENTRY
         BCT   R7,LOOPG          CHECK NEXT
         B     NEXTG             NOT AUTHORIZED
MATCHG   EQU   *
         TM    CGRPAUTH,CGRPSPEC  GROUP SPECIAL FLAG
         BO    AUTHOK            ON, AUTH
NEXTG    EQU   *
         CLC   OWNERN,=CL8'SYS1' IS IT THE FINAL GROUP
         BE    NOTAUTH           YES, NOT AUTHORIZED
* GET SUPERIOR GROUP TO LAST GROUP AND CHECK AGAIN UNTIL SYS1 (TOP)
         LA    R8,OWNERN         ADDR GROUP FIELD
         LA    R6,RFLD2A         ADDR FIELDS TO BE OBTAINED
         LA    R9,RACWORK        RACROUTE WORK AREA
         MVC   RX4(RX4L),RX3     INITIALIZE REENTRANT AREA
         RACROUTE REQUEST=EXTRACT,WORKA=(9),RELEASE=1.8.1,             X
               TYPE=EXTRACT,FIELDS=(6),ENTITY=(8),MF=(E,RX4)
         LTR   R15,R15           TEST RACROUTE RETURN CODE
         BNZ   BADRXRT           RACXTRT FAILED
         LR    R7,R1             RETURN AREA ADDR
* PROCESS THE RETURNED INFOMATION, SEG AREA IS LEN(4), DATA(8)
         XR    R4,R4             CLEAR
         IC    R4,0(R7)          SUBPOOL OF GETMAINED AREA
         XR    R5,R5             CLEAR
         ICM   R5,B'0111',1(R7)  LENGTH OF GETMAINED AREA
         LH    R6,4(R7)          OFFSET TO SEGMENT AREA
         AR    R6,R7             ADD BASE ADDR FOR SEGMENT AREA
         MVC   OWNERN,4(R6)      COPY GROUP SUPERIOR GROUP
         FREEMAIN R,LV=(5),A=(7),SP=(4) FREE RACXTRT AREA
         BCT   R11,CHECKG        CHECK FOR GROUP SPECIAL AGAIN
         B     NOTAUTH           NOT AUTHORIZED AFTER 200 LOOPS MAX
         DROP  R4,R5,R6
* THE ISSUER IS AUTHORIZED TO UPDATE THE TARGET USERID PROFILE
AUTHOK   EQU   *
* THE ISSUER IS NOT AUTHORIZED TO UPDATE THIS USERID PROFILE
NOTAUTH  EQU   *
* BAD RETURN CODE FROM RACXTRT
BADRXRT  EQU   *
* CONSTANTS, NON-MODIFIABLE IN REENTRANT CODE
ZEROS    DC    256XL1'00'        ZEROS
BLANKS   DC    CL255' '          BLANKS
RFLD1A   DC    A(1)              NUMBER OF FIELDS FOLLOWING
RFLD1B   DC    CL8'AUTHOR'       NAME OF FIELD (OWNER)
RFLD2A   DC    A(1)              NUMBER OF FIELDS FOLLOWING
RFLD2B   DC    CL8'SUPGROUP'     NAME OF FIELD (SUPERIOR GROUP)
RX1      RACROUTE REQUEST=EXTRACT,WORKA=ZEROS,RELEASE=1.8.1,           X
               TYPE=EXTRACT,FIELDS=ZEROS,ENTITY=ZEROS,CLASS='USER',MF=L
RX3      RACROUTE REQUEST=EXTRACT,WORKA=ZEROS,RELEASE=1.8.1,           X
               TYPE=EXTRACT,FIELDS=ZEROS,ENTITY=ZEROS,                 X
               CLASS='GROUP',MF=L
* PROGRAM DATA AREA (GETMAINED)
WORKAREA DSECT
SAVEAREA DS    18F               SAVE AREA
ADDRACEE DS    F                 ACEE ADDR OF ISSUER
ISSUER   DS    CL8               ISSUER USERID
USERID   DS    CL8               TARGET USERID
OWNERN   DS    CL8               TARGET USERID OWNING GROUP/ID
RX2      RACROUTE REQUEST=EXTRACT,WORKA=ZEROS,RELEASE=1.8.1,           X
               TYPE=EXTRACT,FIELDS=ZEROS,ENTITY=ZEROS,CLASS='USER',MF=L
RX2L     EQU   *-RX2             RACXTRT LENGTH
RX4      RACROUTE REQUEST=EXTRACT,WORKA=ZEROS,RELEASE=1.8.1,           X
               TYPE=EXTRACT,FIELDS=ZEROS,ENTITY=ZEROS,                 X
               CLASS='GROUP',MF=L
RX4L     EQU   *-RX4             RACXTRT LENGTH
RACWORK  DS    CL512             RACROUTE WORK AREA
WORKLEN  EQU   *-WORKAREA
* DSECTS
         YREGS
         IHAACEE                 ACEE
         ICHPCGRP                CGRP
         IHAASCB                 ASCB
         IHAASXB                 ASXB
         IHAPSA                  PSA
         IKJTCB                  TCB
         END


logGING an update to SMF with a type-80 record

* FIRST SET LOG DATA TO CONTAIN A RECORD OF THE EVENT THAT TOOK PLACE
         LA    R2,LOGDATA        ADDR LOG DATA, LEN (1) DATA (n)
         LA    R9,RACWORK        ADDR WORK AREA
         RACROUTE REQUEST=AUDIT,EVENT='GENERAL',LOGSTR=(2),EVQUAL=0,   X
               RELEASE=1.9,WORKA=(9)
LOGDATA  DC   AL1(80),CL80'INSTALLATION COMMAND UPDATED USER XXXXXX'


                                                                        c Xephon 1993

