REXX load module generator

      pgmname[/size]

Sample JCL

//S0 EXEC PGM=IEFBR14
//SYSRXOUT DD DSN=userid.TEMP(TEMP),UNIT=work,SPACE=(TRK,(10,10,1)),
// DISP=(MOD,CATLG)
//S1 EXEC PGM=REXXLMG,PARM='RXPGM/11'
//STEPLIB  DD DSN=libname,DISP=SHR
//SYSRXOUT DD DSN=userid.TEMP(TEMP),UNIT=work,DISP=(OLD,DELETE)
//* SORT
//SYSOUT   DD SYSOUT=*
//SORTIN   DD DSN=*.SYSRXOUT,DISP=(OLD,KEEP)
//SORTOUT  DD DSN=*.SORTIN,DISP=SHR
//* LOAD MODULE
//SYSLIN   DD DSN=*.SORTOUT,DISP=(OLD,PASS)
//         DD *
 INCLUDE SYSLIB(REXXLDR)
 ORDER REXXLDR
//SYSLMOD  DD DISP=SHR,DSN=libname
//SYSUT1   DD UNIT=work,SPACE=(TRK,(10,10))
//SYSLIB   DD DSN=libname,DISP=SHR     (REXXLDR)
//SYSPRINT DD SYSOUT=*
//* REXX PGM
//SYSRXIN  DD *
 SAY 'test exec'
trace ?r
 PARSE ARG parm
 SAY parm
SAY 'this is the second line'
  a = 2
  b = a , /* continuation */
* 5
SAY b
SAY 'this is a very long message that extends to the end of the line'
SAY 'test exec end'

REXXLMG program code

         TITLE 'Create REXX load module'
REXXLMG  CSECT
REXXLMG  AMODE 24
REXXLMG  RMODE 24
         BAKR  R14,0              save return address
         BALR  R12,0
         USING *,R12
         LA    R13,SA             address internal save area
         MVC   4(4,R13),=C'F1ST'  indicate stack used
         LA    R9,8               preset ReturnCode (if PARM error)
         L     R3,0(R1)
         LH    R1,0(R3)
         LTR   R1,R1
         BZ    EXIT               no EXEC parm present
         CH    R1,=AL2(L'EXECPARM)
         BH    EXIT               EXEC parm too long
         BCTR  R1,0
         MVC   EXECPARM(0),2(R3)
         EX    R1,*-6
         IC    R0,=C' '           set blank delimiter
         STC   R0,EXECPARM+1(R1)  at parameter end
* search for '/' delimiter
         SR    R2,R2              clear R2
         TRT   EXECPARM(256),TRTTAB1
* R1: A(stop character), R2: stop character
         CH    R2,=H'01'          valid stop character
         BNE   EXIT               otherwise error
         CLI   PGMNAME,C'0'       test first character of name
         BNL   EXIT               numeric is invalid
         LR    R3,R1              save delimiter address
* search for ' ' delimiter
         TRT   1(256,R1),TRTTAB2
* R1: A(stop character), R2: delimiter code (X'01' = ok)
         CH    R2,=H'01'          valid stop character
         BNE   EXIT               otherwise error
         SR    R1,R3              length+1
         SH    R1,=H'2'           lengthcode
         BM    EXIT               omitted size value
         CH    R1,=H'15'
         BH    EXIT               size value too long
         PACK  D,1(0,R3)
         EX    R1,*-6
         CVB   R1,D               no of entries
         MH    R1,=H'8'           * entry size
         LA    R1,PGMHDRL(R1)     + length of program header
         ST    R1,PGMDISPL        = table size
         MVC   0(8,R3),=CL8' '    place blanks at end of pgm name
         CLI   PGMNAMEE,C' '
         BNE   EXIT               program name too long
         CLI   PGMNAME,C' '
         BE    EXIT               program name omitted
         OPEN  (SYSRXIN,(INPUT),SYSRXOUT,(OUTPUT))
         LA    R9,24              preload RC (if error)
         LTR   R15,R15
         BNZ   EXIT               open error
         LA    R6,0               accumulative pointer length
         L     R7,PGMDISPL        current text pointer
* generate program header (to identify generated load module)
         TIME  DEC,TOD,DATETYPE=YYYYMMDD,LINKAGE=SYSTEM
         MVC   HDRNAME,PGMNAME
         UNPK  HDRDATE,DATE(5)
         MVI   HDRDLM,C'/'
         UNPK  HDRTIME,TOD(4)
         MVC   TXTTEXT,=CL56' '
         LA    R6,0
         STCM  R6,B'0111',TXTADDR
         MVC   TXTLEN,=AL2(PGMHDRL)
         MVC   TXTTEXT(PGMHDRL),PGMHDR
         UNPK  TXTSEQ,SEQ1
         OC    TXTSEQ,=C'10000000'
         PUT   SYSRXOUT,TXTREC
         LA    R6,PGMHDRL(R6)    start of code proper
         STCM  R6,B'0111',ESD2ADDR
READLOOP GET   SYSRXIN,IO         read input statement
         LA    R1,IOE             statement end (column 72)
         LA    R0,RECL
* search for final blank
RECLOOP  BCTR  R1,0
         CLI   0(R1),C' '
         BNE   RECEND             end of record
         BCT   R0,RECLOOP
* empty line; generate single blank
         LA    R1,IO
RECEND   LA    R5,1(R1)
         LR    R0,R5              save address
         S     R5,=A(IO)          record length
* R5: record length
* test for overflow
         LA    R9,12              preset return code
         C     R6,PGMDISPL        test against maximum table size
         BNL   EXIT               vector area overflows into code
* create pointer entry
         MVC   TXTTEXT,=CL56' '
         STCM  R6,B'0111',TXTADDR
         MVC   TXTLEN,=AL2(8)
         STCM  R7,B'1111',TXTTEXT
         STCM  R5,B'1111',TXTTEXT+4
         AP    SEQ1,=P'1'
         UNPK  TXTSEQ,SEQ1
         OC    TXTSEQ,=C'10000000'
         PUT   SYSRXOUT,TXTREC
* create RLD entry
         MVC   RLDSEQ,TXTSEQ
         OI    RLDSEQ,C'4'
         STCM  R6,B'0111',RLDADDR
         PUT   SYSRXOUT,RLDREC
* update pointer
         LA    R6,8(R6)
* create data record(s)
         LA    R2,IO
NEXTTREC LA    R1,L'TXTTEXT
         CR    R1,R5
         BNH   *+6
         LR    R1,R5
         MVC   TXTTEXT,=CL56' '
         STCM  R7,B'0111',TXTADDR
         STH   R1,TXTLEN
         BCTR  R1,0
         MVC   TXTTEXT(0),0(R2)
         EX    R1,*-6
         LA    R1,1(R1)           true data length
         AR    R2,R1
         AR    R7,R1
         SR    R5,R1              residual length
         AP    SEQ2,=P'1'
         UNPK  TXTSEQ,SEQ2
         OC    TXTSEQ,=C'30000000'
         PUT   SYSRXOUT,TXTREC
         LTR   R5,R5
         BNZ   NEXTTREC
         B     READLOOP
READEOF  DS    0H
* create pointer length record
         MVC   TXTTEXT,=CL56' '
         STCM  R6,B'0111',TXTADDR
         MVC   TXTLEN,=AL2(4)
         LR    R1,R6
         SH    R1,=AL2(PGMHDRL)
         STCM  R1,B'1111',TXTTEXT     length of vector data
         OC    TXTSEQ,=C'20000000'
         PUT   SYSRXOUT,TXTREC
* create ESD records
         STCM  R7,B'1111',ESD1LEN
         STCM  R6,B'0111',ESD3ADDR
         PUT   SYSRXOUT,ESDREC
         PUT   SYSRXOUT,ENDREC
         CLOSE (SYSRXIN,,SYSRXOUT)
* sort object module into sequence
         LA    R1,SORTPARM
         LINK  EP=SORT
         LA    R9,16              preset return code (if sort error)
         LTR   R15,R15
         BNZ   EXIT               sort error
* create load module
         LINK  EP=IEWL,PARAM=(IEWOPT,IEWDD),VL=1
         LA    R9,20              preset return code (if LKED error)
         LTR   R15,R15
         BNZ   EXIT               load module generation error
         LA    R9,0               normal return
EXIT     LR    R15,R9             program return code
         PR    ,                  terminate program
         TITLE 'Data areas'
         LTORG
SORTPARM DC    X'80',AL3(PARMLIST)
         CNOP  2,4
PARMLIST DC    AL2(LIST1-LIST0)   SORT parameters
LIST0    DC    A(SORT0,SORT1)
         DC    A(REC0,REC1)
         DC    A(0,0)
         DC    A(0)
LIST1    EQU   *
SORT0    DC    C' SORT FIELDS=(73,8,CH,A)'
SORT1    EQU   *-1
REC0     DC    C' RECORD LENGTH=80,TYPE=F'
REC1     EQU   *-1
IEWOPT   DC    Y(IEW1-IEW0)       LKED options
IEW0     DC    C'LIST,MAP,AMODE=31,RMODE=ANY'
IEW1     EQU   *
IEWDD    DC    Y(IEW3-IEW2)       LKED DDNAME list
IEW2     DC    CL8'SYSLIN'
EXECPARM DC    CL128' '
         DC    C' ',CL8'/1000 '   default size (source lines)
         ORG   EXECPARM
PGMNAME  DC    CL8' '
IEW3     EQU   *
PGMNAMEE DS    C
         ORG
PGMDISPL DC    A(0)               program displacement
D        DS    0D,PL8
SA       DS    18F
IO       DS    CL80
         ORG   IO+RECL
IOE      EQU   *                  end of input statement
RECL     EQU   72                 size of input record
         ORG
SEQ1     DC    PL5'0'
SEQ2     DC    PL5'0'
TOD      DS    XL8                HHMMSSthxxxx0000 (packed)
DATE     DS    XL4,XL4            YYYYMMDD (packed)
* program header record (set into the object module)
PGMHDR   DS    0F
         USING *,R15
         B     START              bypass header information
         DC    AL1(START-*-1)
         DC    C'REXXLOAD-'
HDRNAME  DC    CL8' ',C':'        program name
HDRDATE  DS    CL9                compile date
HDRDLM   EQU   *-1                delimiter
HDRTIME  DS    CL7                compile time
PGMHDRE  ORG   *-1
START    DS    0H
PGMHDRL  EQU   START-PGMHDR
         ORG
         DROP  R15
* end of program header record
* object module records
         DS    0H                 align
ESDREC   DC    CL80' '
         ORG   ESDREC
         DC    X'02',CL9'ESD '
ESDCT    DC    XL2'0030',CL2' '
         DC    AL2(1)
         DC    CL8'REXXLOAD'      CSECT name
         DC    AL4(0)
ESD1LEN  DS    FL4
         DC    CL8'INSTPGM'       ENTRY INSTPGM
         DC    HL1'1'             ID
ESD2ADDR DS    AL3,CL1
ESD2LD   DC    AL3(1)
         DC    CL8'INSTPGML'      ENTRY INSTPGML
         DC    HL1'1'
ESD3ADDR DS    AL3,CL1
ESD3LD   DC    AL3(1)
         ORG   ESDREC+72
ESDSEQ   DC    CL8'00000001'
TXTREC   DC    CL80' '
         ORG   TXTREC
         DC    X'02',CL4'TXT '
TXTADDR  DS    AL3,CL2
TXTLEN   DS    HL2,CL2
TXTID    DC    AL2(1)             SD-ID
TXTTEXT  DS    CL56
         ORG   TXTREC+72
TXTSEQ   DS    CL8
RLDREC   DC    CL80' '
         ORG   RLDREC
         DC    X'02',CL9'RLD '
RLDLEN   DC    AL2(8),CL4' '
RLDPID   DC    AL2(1,1)           POS.ID, REL.ID.
RLDFLAG  DC    X'0C'              single entry
RLDADDR  DS    AL3
         ORG   RLDREC+72
RLDSEQ   DS    CL8
ENDREC   DC    CL80' '
         ORG   ENDREC
         DC    X'02',CL9'END'
         ORG   ENDREC+72
ENDSEQ   DC    CL8'90000000'
TRTTAB1  DC    256X'02'           all illegal stop characters
         ORG   TRTTAB1+C'/'
         DC    X'01'              valid stop character
         ORG   TRTTAB1+C' '
         DC    X'00'              bypass blanks
         ORG   TRTTAB1+C'A'
         DC    9X'00'             valid scan characters (A-I)
         ORG   TRTTAB1+C'J'
         DC    9X'00'             valid scan characters (J-R)
         ORG   TRTTAB1+C'S'
         DC    8X'00'             valid scan characters (S-Z)
         ORG   TRTTAB1+C'0'
         DC    10X'00'            valid scan characters (0-9)
         ORG   TRTTAB1+C'$'
         DC    X'00'              valid scan character (national char)
         ORG   TRTTAB1+C'#'
         DC    X'00'              valid scan character (national char)
         ORG   TRTTAB1+C'º'
         DC    X'00'              valid scan character (national char)
         ORG
TRTTAB2  DC    256X'02'           all illegal stop characters
         ORG   TRTTAB2+C'0'       except numeric digits
         DC    10X'00'            0-9
         ORG   TRTTAB2+C' '       except numeric digits
         DC    X'01'              valid stop character (blank)
         ORG
SYSRXIN  DCB   DDNAME=SYSRXIN,DEVD=DA,DSORG=PS,MACRF=GM,EODAD=READEOF
SYSRXOUT DCB   DDNAME=SYSRXOUT,DEVD=DA,DSORG=PS,MACRF=PM,              x
               LRECL=80,RECFM=F
         END

REXXLDR program code

         TITLE 'REXX load-module loader'
REXXLDR  CSECT
REXXLDR  AMODE 31
REXXLDR  RMODE ANY
         BAKR  R14,0
         BALR  R12,0
         USING *,R12
         LA    R13,SA
         L     R2,0(R1)
         LH    R3,0(R2)         L(complete argument, including lengths)
         LH    R4,2(R2)         L(first subargument)
         SH    R3,=H'4'         adjust length
         SR    R3,R4            correct length
         LA    R2,4(R4,R2)      A(Argument)
         ST    R2,AELEM
         ST    R3,LELEM
* process
         L     R1,=V(INSTPGML)
         L     R0,0(R1)
         ST    R0,NINSTPGM
         LOAD  EP=IRXEXEC
         LR    R15,R0
         CALL  (15),(P0,AARGLIST,FLAGS,AINSTBLK,P0,AEVALBLK,P0,P0),VL
         SR    R15,R15
         IC    R15,EVDATA
         N     R15,=F'7'        clear high-order
         PR    ,                terminate program
SA       DS    18F
P0       DC    A(0)
AARGLIST DC    A(AELEM)
AELEM    DC    A(0)
LELEM    DC    A(0)
         DC    2F'-1'
FLAGS    DC    X'80000000'      invoke as command
AINSTBLK DC    A(INSTBLK)
AEVALBLK DC    A(EVALBLK)
EVALBLK  DS    0F    align
         DS    F                reserved
EVSIZE   DC    A((EVDATAE-EVALBLK)/8)
EVLEN    DC    XL4'80000000'    L(data)
         DS    F                reserved
EVDATA   DS    CL64             data
EVDATAE  EQU   *
         DS    0F    align
INSTBLK  DC    CL8'IRXINSTB'
         DC    F'128'             L(INSTBLK header)
         DS    F                  reserved
         DC    V(INSTPGM)
NINSTPGM DS    A                  N(INSTBLK entries)
         DC    CL8' '             member name (unused)
         DC    CL8' '             DD name (unused)
         DC    CL8'TSO'           subcom
         DS    F                  reserved
         DC    F'0'               L(DSN), unused
         DC    CL54' '            DSN, unused
         ORG   INSTBLK+128
         IRXEFPL
         END


A S Rudd
Technical Consultant (Germany)                                              c A S Rudd 1995

