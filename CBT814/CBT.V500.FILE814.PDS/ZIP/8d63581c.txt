Writing to multiple members in a PDS

This article describes a routine that can be used to write one or
more members to any PDS. The members of the PDS will be
processed as sequential files, which means that a new member
will be created if it does not already exist, and it will be
overwritten with new records if it does already exist. The PDS
must be allocated using JCL or TSO under the DDNAME
PDS. It must have record length of 80.

If the calling program is written in Assembler, the DCB of the
PDS must not be defined in the calling program. If the calling
program is written in COBOL, the PDS must not be defined as
a file in the calling program - this is because the DCB of the
PDS is defined in the called routine WTPDS.


PROCEDURE FOR USING THIS ROUTINE

Suppose you have a COBOL program in which you wish to
call WTPDS to write records to one or more members in a
PDS. In your calling COBOL program, define the following
area in your working storage section:

      01  WS-CALL-WTPDS-PARMS.
          05  FUNCTION         PIC X.
          05  PDS-MEMBER-NAME  PIC X(08).
          05  WTPDS-RET-CODE   PIC X.

For each member to be created or overwritten with new
records, do the following:

1      Execute the following code to open the desired PDS
      member for processing as a sequential file:

      MOVE 'O' TO FUNCTION.
      MOVE 'member-name'  TO PDS-MEMBER-NAME.
      MOVE SPACES TO WTPDS-RETURN-CODE.
      CALL 'WTPDS' USING FUNCTION PDS-MEMBER-NAME WTPDS-RET-CODE.

2      Execute the following code n times to write n records in
      the PDS member that was opened in step 1:

      MOVE 'W' TO FUNCTION.
      MOVE SPACES TO WTPDS-RETURN-CODE.
      CALL 'WTPDS' USING FUNCTION record-area WTPDS-RET-CODE.

      where record-area is to be replaced with the name of the
      actual area which contains the record to be written.

3      Execute the following code to close the PDS member that
      was opened in step 1:

      MOVE 'C' TO FUNCTION.
      MOVE SPACES TO WTPDS-RETURN-CODE.
      CALL 'WTPDS' USING FUNCTION PDS-MEMBER-NAME WTPDS-RET-CODE.


WTPDS SOURCE CODE

         COPY REGDEF
WTPDS    CSECT
         USING WTPDS,R15        PROVIDE TEMPORARY ADDRESSABILITY
         SAVE  (14,12),T,'WTPDS &SYSDATE &SYSTIME'
         LA    R12,SAVEAREA     LOAD R12 WITH ADDRESS OF MY SAVE AREA
         ST    R13,4(R12)       STORE ADDRESS OF CALLER'S SAVE AREA
*                               IN MY SAVE AREA
         ST    R12,8(R13)       STORE ADDRESS OF MY SAVE AREA IN
*                               CALLER'S SAVE AREA
         LR    R13,R12          LOAD R13 WITH ADDRESS OF MY SAVE AREA
         LR    R12,R15          LOAD R12 WITH ADDRESS OF THIS PROGRAM
         DROP  R15              DROP TEMPORARY ADDRESSABILITY
         USING WTPDS,R12        PROVIDE PERMANENT ADDRESSABILITY
REALWORK EQU    *
         ST    R1,SAVEDR1
         RDJFCB PDS
         LA    R11,JFCB
         USING INFMJFCB,R11
         OI    JFCBIND1,JFCBPMEM
         L     R1,SAVEDR1
         L     R2,0(R1)         POINT R2 TO FIRST PARM (ACTION CODE)
         L     R3,4(R1)         POINT R3 TO SECOND PARM (MEMBER NAME,
*                               OR RECORD TO BE WRITTEN OUT)
         L     R4,8(R1)         POINT R4 TO THIRD PARM (RETURN CODE)
         CLI   0(R2),C'O'       IS THE FIRST PARM = O (OPEN)?
         BE    OPENIT
         CLI   0(R2),C'C'       IS THE FIRST PARM = C (CLOSE)?
         BE    CLOSEIT
         CLI   0(R2),C'W'       IS THE FIRST PARM = W (WRITE)?
         BE    WRITEIT
         MVI   0(R4),C'F'       INDICATE FAILURE
         B     EXIT
OPENIT   EQU   *
         MVC   JFCBELNM,0(R3)      MOVE MEMBER NAME
         OPEN  (PDS,(OUTPUT)),TYPE=J
         B     SUCCESS
CLOSEIT  CLOSE PDS
         B     SUCCESS
WRITEIT  PUT   PDS,0(R3)
         B     SUCCESS
SUCCESS  MVI   0(R4),C'S'       INDICATE SUCCESS
EXIT     EQU   *
         SR    R15,R15          SET RETURN CODE TO ZERO.
         L     R13,4(R13)       POINT TO PREVIOUS SAVE AREA
         RETURN (14,12),T,RC=(15) TERMINATE THE TASK
         LTORG
SAVEAREA DC    9D'0'
SAVEDR1  DC    F'0'
PDS      DCB   DDNAME=PDS,DSORG=PS,RECFM=FB,                           X
               MACRF=(PM),LRECL=80,EXLST=EXLST
EXLST    DS    0F
         DC    X'87',AL3(JFCB)
JFCB     DS    0D,176C          AREA FOR JFCB
         DSECT
         IEFJFCBN LIST=YES
         END


Barry K Nirmal
SCECO East (Saudi Arabia)                         c Barry K Nirmal 1993

