Displaying MVS subsystems

This REXX procedure is an easy way to see all the defined
MVS subsystems including those that have been added
dynamically after IPL.  For every subsystem, you receive its
name, and, if it is active, you are shown the address of the
corresponding SSVT and the number of functions it supports.
If you press the ENTER key the screen will be updated.

This procedure shows the basic mechanism within REXX to
access storage data, calculate addresses, and follow pointer
chains.  To install it, put the procedure SUBSYS into your
SYSPROC or SYSEXEC file and the panel SUBSYSP into
your ISPPLIB file. Then enter the command SUBSYS under
TSO/ISPF.

It has been tested under MVS/XA, TSO/E with REXX, and
ISPF (Version 3).  As it displays basic MVS control blocks the
author does not expect any release dependencies.  The display
panel has been designed for 30 subsystem entries - if more are
required it is easy to extend the number of variables.


THE SUBSYS PROCEDURE

/* REXX */
/***************************************************/
/* THIS REXX PROCEDURE DISPLAYS EACH MVS SUBSYSTEM */
/***************************************************/
/* YOU NEED 10 DIGITS FOR 31 BIT ADDRESSES */
NUMERIC DIGITS 10
/* START DISPLAY LOOP */
DO UNTIL(RC^=0)
  /* START FROM CVT ADDRESS */
  AC = STORAGE(00000010,4)
  AD = C2D(AC)
  AD = AD+296
  AX = D2X(AD)
  /* CONTINUE WITH JESCT */
  AC = STORAGE(AX,4)
  AD = C2D(AC)
  AD = AD+24
  AX = D2X(AD)
  /* AND THIS IS THE FIRST SSCT */
  AC = STORAGE(AX,4)
  AD = C2D(AC)
  AX = D2X(AD)
  /* INITIALIZE STEM VARIABLES */
  SSVT. = ''
  NUMB. = ''
  DO I=1 BY 1 UNTIL(AD=0)
    /* GET VALUES FOR THE DISPLAY */
    SC = STORAGE(AX,36)
    SX = C2X(SC)
    /* 1: NAME OF THE SUBSYSTEM */
    NAME.I = SUBSTR(SC,9,4)
    BC = SUBSTR(SC,17,4)
    BD = C2D(BC)
    BX = C2X(BC)
    /* IF SUBSYSTEM IS ACTIVE */
    IF BD^=0  THEN DO
      /* 2: ADDRESS TO SSVT */
      SSVT.I = BX
      VC = STORAGE(BX,4)
      VC = SUBSTR(VC,3,2)
      /* 3: NUMBER OF FUNCTIONS */
      NUMB.I = C2D(VC)
      END
    /* STEP TO NEXT SUBSYSTEM */
    AC = SUBSTR(SC,5,4)
    AD = C2D(AC)
    AX = D2X(AD)
    END I
  /* AND NOW CONVERT TO VARIABLES FOR THE DISPLAY */
  DO K=1 TO I
    LINE = "VT"K" = SSVT."K
    INTERPRET LINE
    LINE = "NA"K" = NAME."K
    INTERPRET LINE
    LINE = "NU"K" = NUMB."K
    INTERPRET LINE
    END K
  /* DISPLAY THE PANEL */
  ADDRESS ISPEXEC "DISPLAY PANEL(SUBSYSP)"
  /* END OF DISPLAY LOOP */
  END


THE SUBSYSP PANEL

)ATTR
  ]  TYPE(OUTPUT) INTENS(LOW)
  ?  TYPE(OUTPUT) INTENS(LOW) JUST(RIGHT)
)BODY
%----------------------------------  SUBSYS  -----------------------------------
%COMMAND ===>_ZCMD                                                             +
%
+
%  NAME    SSVT        NUMB           NAME    SSVT        NUMB
%
+ ]NA1 +  ]VT1     +  ?NU1 +         ]NA16+  ]VT16    +  ?NU16+
+ ]NA2 +  ]VT2     +  ?NU2 +         ]NA17+  ]VT17    +  ?NU17+
+ ]NA3 +  ]VT3     +  ?NU3 +         ]NA18+  ]VT18    +  ?NU18+
+ ]NA4 +  ]VT4     +  ?NU4 +         ]NA19+  ]VT19    +  ?NU19+
+ ]NA5 +  ]VT5     +  ?NU5 +         ]NA20+  ]VT20    +  ?NU20+
+ ]NA6 +  ]VT6     +  ?NU6 +         ]NA21+  ]VT21    +  ?NU21+
+ ]NA7 +  ]VT7     +  ?NU7 +         ]NA22+  ]VT22    +  ?NU22+
+ ]NA8 +  ]VT8     +  ?NU8 +         ]NA23+  ]VT23    +  ?NU23+
+ ]NA9 +  ]VT9     +  ?NU9 +         ]NA24+  ]VT24    +  ?NU24+
+ ]NA10+  ]VT10    +  ?NU10+         ]NA25+  ]VT25    +  ?NU25+
+ ]NA11+  ]VT11    +  ?NU11+         ]NA26+  ]VT26    +  ?NU26+
+ ]NA12+  ]VT12    +  ?NU12+         ]NA27+  ]VT27    +  ?NU27+
+ ]NA13+  ]VT13    +  ?NU13+         ]NA28+  ]VT28    +  ?NU28+
+ ]NA14+  ]VT14    +  ?NU14+         ]NA29+  ]VT29    +  ?NU29+
+ ]NA15+  ]VT15    +  ?NU15+         ]NA30+  ]VT30    +  ?NU30+
+
)INIT
)PROC
)END


Manfred Heinrich
Zurich Insurance (Germany)                                        c Xephon 1991

