Dataset existence verification program

EXISTS is a program that will indicate whether a dataset or a particular member
in a Partitioned Dataset (PDS) exists.  This is very useful for setting
condition codes for JCL processing or in CLISTs.  The input is a dataset name
and, if it is a PDS, a member name may be specified.  Also input is an optional
volume serial number if the dataset isn't catalogued.

Output consists of messages and return codes.  The message can be turned off by
use of the NOMESSAGE parameter.  The method of sending the message varies.  If
the program is called under TSO, TPUT issues the message.  If executed in batch,
the message will be sent to the destination indicated by the SYSPRINT DD
statement if one exists, otherwise the message will be sent via a Write To
Operator (WTO).  The return codes and messages are as follows:

Output consists of messages and return codes.  The message can be turned off by
use of the NOMESSAGE parameter.  The method of sending the message varies.  If
the program is called under TSO, TPUT issues the message.  If executed in batch,
the message will be sent to the destination indicated by the SYSPRINT DD
statement if one exists, otherwise the message will be sent via a Write To
Operator (WTO).  The return codes and messages are as follows:

EXIST0   MEMBER: XXXXXXXXXXXXXXXXXXXXXXXXXXX FOUND
EXIST0  DATASET: XXXXXXXXXXXXXXXXXXXXXXXXXXX FOUND ON: XXXXXX
EXIST4   MEMBER: XXXXXXXXXXXXXXXXXXXXXXXXXXX NOT FOUND
EXIST6  DATASET: XXXXXXXXXXXXXXXXXXXXXXXXXXX NOT A PDS
EXIST7  DATASET: XXXXXXXXXXXXXXXXXXXXXXXXXXX VOLUME IS NOT MOUNTED:XXXXXX
EXIST8  DATASET: XXXXXXXXXXXXXXXXXXXXXXXXXXX NOT FOUND ON: XXXXXX
EXIST8  DATASET: XXXXXXXXXXXXXXXXXXXXXXXXXXX NOT FOUND IN CATALOG
EXIST12 MISSING OR INVALID PARM, VALID EXAMPLES:
        YOUR.DATASET          OR   YOUR.DATASET(MBR)
        YOUR.DATASET/VOLSER   OR   YOUR.DATASET(MBR)/VOLSER
EXIST16 CATALOG NOT AVAILABLE


The syntax for the parameter string is positional and the string may end after
the last complete parameter.  The parameters are:  dataset name, volume serial
number (VOLSER), and NOMESSAGE.  The dataset name is required;  VOLSER is
optional for uncatalogued datasets;  NOMESSAGE causes no message to be printed.
These parameters are separated by slashes.  If VOLSER is null and NOMESSAGE is
required code two slashes.


USING EXISTS


TSO

CALL 'your.loadlib(EXISTS)'   +
     'some.dataset(member)/volser'


JCL

         //         EXEC PGM=EXISTS,
         //             PARM='some.dataset(member)/volser/NOMESSAGE'
         //SYSPRINT DD  SYSOUT=A

THE SOURCE LISTING FOR EXISTS

*=====================================================================
* PROGRAM: EXISTS
* FUNCTION
*    THIS PROGRAM WILL CHECK FOR DATASET EXISTENCE.  IT WILL ALSO
*    VERIFY A MEMBERS EXISTENCE IN A PDS.
* ENTRY POINT
*    EXIST
* INPUT
*   PARM'S
*    DSNAME
*    VOL SER (IF NOT CATALOGED)
*    NO MESSAGE OPTION
* OUTPUT
*    RETURN CODE TO CALLING PROGRAM
*       0 = DATASET FOUND, AND IF PDS,MEMBER FOUND IF SUPPLIED.
*       4 = PDS, MEMBER NOT FOUND
*       6 = SEQUENTIAL DATASET AND MEMBER SPECIFIED.
*       7 = VOLUME NOT MOUNTED
*       8 = DATASET NOT FOUND
*      12 = BAD CALL TO THIS MODULE
*      16 = CATALOG ERROR
*    MESSAGES
*      TSO TPUT'S
*      BATCH TO SYSPRINT IF PRESENT
*      BATCH TO WTO IF NO SYSPRINT
* CALL PARAMETERS
*    CALL EXISTS USING 'YOUR.DSN(MEM)/VOLSER/NOMESSAGE'.
*    // EXEC PGM=EXISTS,PARM='YOUR.DSN(MEM)/VOLSER/NOMESSAGE'.
*    //SYSPRINT DD SYSOUT=A
* FEATURES:
*   OBTAIN, CAMLIST, ...
*   DYNAMIC ALLOCATION
*   EXTRACT FOR TCB INFO: TSO
*   NOTE: DSCB1 RETURNED DOESN'T CONTAIN DSN.
*=====================================================================
         IEFZB4D0                DSECT FOR DYNALLOC
         IEFZB4D2                DSECT FOR DYNALLOC
         DSECT
         IECSDSL1 (1)            DSCB1
         INTRO BASEREG=R10,PROGRAM=EXISTS,VERSION=1,AUTHOR=DONDUNN
***** SEPARATE AND VALIDATE PARMS *****
         L     R1,0(R1)           GET PARMS
         XR    R2,R2                   CLEAR
         LH    R2,0(R1)                LOAD LENGTH
         CH    R2,=H'0'                  TOO SHORT?
         BE    BADPARM                 YES, END
         LA    R2,1(R2)                PREPARE FOR PARSE
         LA    R1,1(R1)                SET POINTER PAST LENGTH LESS 1
         LA    R3,DSN                  *** DSN
         LA    R5,L'DSN
         MVI   LOOKFOR1,C'/'
         MVI   LOOKFOR2,C'('
         BAL   R6,PARSE
         CLI   0(R1),C'('
         BNE   TRYVOL
         LA    R3,MBR                  *** MEMBER
         LA    R5,L'MBR
         MVI   LOOKFOR2,C')'
         BAL   R6,PARSE
         CLI   0(R1),C')'
         BNE   BADPARM
         BCT   R2,CONTINUE
         B     PARMDONE
CONTINUE LA    R1,1(R1)
         CLI   0(R1),C'/'
         BNE   BADPARM
TRYVOL   MVI   SHORTOK,YES
         LA    R3,VOL
         LA    R5,L'VOL
         MVI   LOOKFOR2,C'/'
         BAL   R6,PARSE
         LA    R3,NOMSG                *** NOMESSAGE
         LA    R5,L'NOMSG
         BAL   R6,PARSE
         B     PARMDONE
PARSE    MVI   SEPONLY,NO              PARSE OUT PARM LIST
         LA    R1,1(R1)                POINT PAST LAST SEPARATOR
         LR    R7,R1                   START OF THIS SEARCH
         BCT   R2,PARSLOOP             SHORTEN LENGTH, END?
         B     PARMDONE                   YES
PARSLOOP CLC   0(1,R1),LOOKFOR1
         BE    GOTSEP
         CLC   0(1,R1),LOOKFOR2
         BE    GOTSEP
         CLI   SEPONLY,YES
         BE    BADPARM
         MVC   0(1,R3),0(R1)            MOVE IN
         LA    R1,1(R1)                 INCR SENDER
         LA    R3,1(R3)                 INCR RECEIVER
         BCT   R2,MOREPARM
         B     PARMDONE
MOREPARM BCT   R5,PARSLOOP              THIS FIELD REMAINING
         MVI   SEPONLY,YES
         B     PARSLOOP
GOTSEP   CR    R7,R1                    LENGTH 0?
         BNER  R6                          NO, RETURN
         CLI   SHORTOK,YES              IS IT OK?
         BER   R6                       YES, RETURN
         B     BADPARM                     NO
***** GOT DSN, MBR AND MAYBE VOLSER, SEE IF IT EXISTS *****
PARMDONE CLC   NOMSG,NOMSGLIT
         BNE   MSGSON
         MVI   MSGONOFF,OFF
MSGSON   CLI   VOL,C' '            VOLSER SPECIFIED?
         BNE   NOTCATLG
***** CATALOGED DATASETS *****
         LOCATE LOCCAT             DOES DATASET EXIST?
         LTR   R15,R15             WAS LOCATE SUCCESSFUL?
         BZ    FOUNDCAT               YES, FOUND IT
         C     R15,=F'4'           BAD CATALOG
         BE    BADCATLG
         C     R15,=F'8'           DATA SET NOT FOUND?
         BE    NOTFOUND               NO, NOT FOUND
         B     BADCATLG            BAD CATALOG
FOUNDCAT MVC   VOL,CAMVOL          VOLSER FROM CAMLIST
NOTCATLG OBTAIN LOCDSCB            DOES DATASET EXIST ON PACK?
         LTR   R15,R15             WAS LOCATE SUCCESSFUL?
         BZ    FOUNDSCB               YES, FOUND IT
         C     R15,=F'4'           NOT MOUNTED?
         BE    BADMOUNT
         C     R15,=F'8'           DATA SET NOT FOUND?
         BE    NOTFOUND               NO, NOT FOUND
         B     BADCATLG            BAD CALL ETC
FOUNDSCB LA    R5,0                FOUND!
         MVC   MSG02DSN,DSN
         MVC   MSG02VOL,VOL
         LA    R3,MSG02
         BAL   R2,MESSAGES         PUT OUT MESSAGE
         CLI   MBR,C' '            WAS MEMBER SPECIFIED?
         BE    DONE                   NO, QUIT
         LA    R2,LOCDSCBA         ADDRESS DSCB1 AREA
* OLD    USING IECSDSL1,R2
         USING IECSDSL1+44,R2      PAST DSN
         TM    DS1DSORG,DS1DSGPO   IS IT REALLY A PDS?
         BZ    BADPDS                 NO
         MVC   DSNAME,DSN           WHAT TO
         LA    R1,DYNAPTR                 ALLOC
         DYNALLOC                      LOOK MOM NO DD STATEMTS !!
         LTR   R15,R15
         BNZ   NOTFOUND
***** OPEN, LOOK FOR MEMBER *****
         OPEN  PDS                THE DATA SET
         FIND PDS,MBR,D           FIND THE MBR
FIND     LR    R3,R15             SAVE STATUS
         CLOSE PDS                CLOSE UP SHOP
         LR    R15,R3             RESTORE STATUS
         C     R15,=F'0'           FOUND?
         BNE   BADMBR               NO
         LA    R5,0                FOUND!
         MVC   MSG01MBR,MBR        TELL'M
         LA    R3,MSG01               THE
         BAL   R2,MESSAGES               MESSAGE
DONE     TM    SYSPOPEN,OPEN       THE END
         BNE   FINISHED
         CLOSE SYSPRINT
FINISHED LR    R15,R5              SET CONDITION CODE
         EXIT
***** ERROR MESSAGES *****
BADMBR   LA    R5,4                MEMBER NOT FOUND
         MVC   MSG03MBR,MBR
         LA    R3,MSG03
         BAL   R2,MESSAGES            PUT OUT MESSAGE
         B     DONE
BADPDS   MVC   MSG04DSN,DSN        NOT A PDS
         LA    R3,MSG04
         BAL   R2,MESSAGES            PUT OUT MESSAGE
         B     DONE
BADMOUNT LA    R5,7                DATASET NOT MOUNTED
         MVC   MSG05DSN,DSN
         LA    R3,MSG05
         BAL   R2,MESSAGES            PUT OUT MESSAGE
         LA    R3,MSG11
         MVC   MSG11VOL,VOL
         BAL   R2,MESSAGES            PUT OUT MESSAGE
         B     DONE
NOTFOUND LA    R5,8                DATASET NOT FOUND
         MVC   MSG10DSN,DSN
         CLC   VOL,=CL6' '            NO VOL?
         BE    NOTFOUN1
         MVC   MSG10VOL,VOL
         B     NOTFOUN2
NOTFOUN1 MVC   MSG10CAT(L'CATMSG),CATMSG    NOT FOUND IN CATALOG
NOTFOUN2 LA    R3,MSG10
         BAL   R2,MESSAGES           PUT OUT MESSAGE
         B     DONE
BADPARM  LA    R5,12              BAD PARM PASSED
         LA    R3,MSG06
         BAL   R2,MESSAGES           PUT OUT MESSAGE
         LA    R3,MSG07
         BAL   R2,MESSAGES           PUT OUT MESSAGE
         LA    R3,MSG08
         BAL   R2,MESSAGES           PUT OUT MESSAGE
         LA    R3,MSG12
         BAL   R2,MESSAGES           PUT OUT MESSAGE
         B     DONE
BADCATLG LA    R5,16               BAD CATALOG
         LA    R3,MSG09
         BAL   R2,MESSAGES            PUT OUT MESSAGE
         B     DONE
***** PUT OUT MESSAGES (IF NECESSARY) *****
*****  DECIDE WHERE THE MESSAGES GO   *****
MESSAGES CLI   MSGONOFF,OFF
         BER   R2                     RETURN IF NO MESSAGES
         EXTRACT ADDRSPC,'S',FIELDS=(TSO)
         TM    ADDRSPC,TSO            ARE WE A TSO SESSION?
         BO    MESSTSO
         CLI   SYSPOPEN,OPEN          SYSPRINT OPEN?
         BE    MESSSYSP
         LA    R1,DYNSPTR             IS SYSPRINT DD ALLOCATED?
         DYNALLOC
         LTR   R15,R15
         BNE   MESSWTO                  NO, DO WTO INSTEAD
         OPEN (SYSPRINT,(OUTPUT))     OPEN THE SYSPRINT
         B     MESSSYSP
MESSTSO  TPUT  0(R3),80,EDIT          TELL TSO USER
         BR    R2                     RETURN
MESSSYSP PUT   SYSPRINT,0(R3)         TELL SYSPRINT DD CARD
         BR    R2                     RETURN
MESSWTO  MVC   TELLOP+8(80),0(R3)     TELL OPERATOR
TELLOP   WTO   '                                                     X
                                         '
         BR    R2                      RETURN
DSN      DC    CL44' '                DSN TO FIND
MBR      DC    CL8' '                 MBR TO FIND
VOL      DC    CL6' '                 VOL SER TO FIND
NOMSG    DC    CL9' '                 NO MESSAGE PARM
LOOKFOR1 DC    C'('                   CHARACTER TO FIND
LOOKFOR2 DC    C'/'                   CHARACTER TO FIND (VOLSER SEP)
ON       EQU   C'Y'
OFF      EQU   C'N'
YES      EQU   C'Y'
NO       EQU   C'N'
NOMSGLIT DC    C'NOMESSAGE'
MSGONOFF DC    AL1(ON)
SHORTOK  DC    AL1(NO)
SEPONLY  DC    AL1(NO)
LOCCAT   CAMLST NAME,DSN,,LOCCATA
LOCCATA  DS    0D
         DC    265C' '
         ORG   LOCCATA+6
CAMVOL   DS    CL6
         ORG
LOCDSCB  CAMLST SEARCH,DSN,VOL,LOCDSCBA
LOCDSCBA DS    0D
         DC    CL140' '
ADDRSPC  DS    F                     ARE WE TSO??
TSO      EQU   X'80'                 TSO ADDRESS SPACE
SYSPOPEN DC    C'N'                   SYSPRINT OPEN?
OPEN     EQU   C'Y'
PDS      DCB   DDNAME=PDS,                                               X
               DSORG=PO,                                                 X
               MACRF=R,                                                  X
               BLKSIZE=256,                                              X
               LRECL=256,                                                X
               KEYLEN=8,                                                 X
               BUFNO=1
SYSPRINT DCB   BLKSIZE=80,                                               X
               LRECL=80,                                                 X
               DDNAME=SYSPRINT,                                          X
               DEVD=PR,                                                  X
               DSORG=PS,                                                 X
               MACRF=PM
***** DYNAMIC ALLOCATION OF THE DATASET *****
DYNAPTR  DC    0F'0',X'80',AL3(DYNARB)
DYNARB   DC    AL1(20)                         LENGTH THIS RB, 20 FIXED
         DC    AL1(S99VRBAL),AL2(X'2000')      ALLOC, DO NOT MOUNT
DYNAERR  DC    AL2(0)                          ERROR CODE
DYNAINFO DC    AL2(0)                          INFO  CODE
         DC    A(DYNATEXT)                     POINTER TO POINTERS
         DC    A(0),A(0)                       RESERVED AND FLAGS
DYNATEXT DC    A(DYNADDN)          DDNAME      || TEXT
         DC    A(DYNAFREE)         FREE=CLOSE  ||
         DC    A(DYNADSN)          DSN         ||
         DC    X'80'               LAST ONE
         DC    AL3(DYNADISP)       DISP
DYNADDN  DC    Y(DALDDNAM),AL2(1),AL2(8)
DDNAME   DC    CL8'PDS'
DYNAFREE DC    Y(DALCLOSE),AL2(0)               FREE=CLOSE
DYNADSN  DC    Y(DALDSNAM),AL2(1),AL2(44)
DSNAME   DC    CL44' '                          DSN
DYNADISP DC    Y(DALSTATS),AL2(1),AL2(1),X'08'  DISP=SHR
***** DYNAMIC ALLOCATION OF 'SYSPRINT' *****
DYNSPTR  DC    0F'0',X'80',AL3(DYNSRB)
DYNSRB   DC    AL1(20)                         LENGTH THIS RB, 20 FIXED
         DC    AL1(S99VRBIN),AL2(X'0000')      INFORMATION REQUEST
DYNSERR  DC    AL2(0)                          ERROR CODE
DYNSINFO DC    AL2(0)                          INFO CODE
         DC    A(DYNSTEXT)                     POINTER TO POINTERS
         DC    A(0),A(0)                       RESERVED AND FLAGS
DYNSTEXT DC    A(DYNSDDN)          DDNAME      || TEXT
         DC    X'80'               LAST ONE    ||
         DC    A(DYNSDSN)          DSN         ||
DYNSDDN  DC    Y(DINDDNAM),AL2(1),AL2(8)
         DC    CL8'SYSPRINT'
DYNSDSN  DC    Y(DINRTDSN),AL2(1),AL2(44)
         DC    CL44' '                          DSN
MSG01    DC  CL17'EXIST0   MEMBER: '
MSG01MBR DC  CL8' '
         DC  CL55'FOUND'
MSG02    DC  CL17'EXIST0   DATASET: '
MSG02DSN DC  CL44' '
         DC  CL10'FOUND ON: '
MSG02VOL DC  CL6' '
         DC  CL3' '
MSG03    DC  CL17'EXIST4   MEMBER: '
MSG03MBR DC  CL8' '
         DC  CL55'NOT FOUND'
MSG04    DC  CL17'EXIST6  DATASET: '
MSG04DSN DC  CL44' '
         DC  CL19'NOT A PDS'
MSG05    DC  CL17'EXIST7  DATASET: '
MSG05DSN DC  CL44' '
         DC  CL29' '
MSG11    DC  CL31'        VOLUME IS NOT MOUNTED: '
MSG11VOL DC  CL6' '
         DC  CL43' '
MSG10    DC  CL16'EXIST8 DATASET: '
MSG10DSN DC  CL44' '
MSG10CAT DC  CL14'NOT FOUND ON:'
MSG10VOL DC  CL6' '
MSG06    DC  CL80'EXIST12 MISSING OR INVALID PARM, VALID EXAMPLES:'
MSG07    DC  CL80'        YOUR.DATASET          OR   YOUR.DATASET(MBR)'
MSG08    DC  CL80'        YOUR.DATASET/VOLSER   OR   YOUR.DATASET(MBR)/+
               VOLSER'
MSG12    DC  CL80'        YOUR.DATASET//NOMESSAGE    YOUR.DATASET(MBR)/+
               VOLSER/NOMESSAGE'
MSG09    DC  CL80'EXIST16 CATALOG NOT AVAILABLE'
CATMSG   DC  CL20'NOT FOUND IN CATALOG'
         LTORG
         END


Donald Dunn
Senior Software Specialist
Sacramento Municipal Utility District (USA)  © Xephon 1987

