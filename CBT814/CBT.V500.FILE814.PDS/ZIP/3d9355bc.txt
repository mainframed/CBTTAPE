Writing a cross memory program

Contrary to the belief of some systems programmers, MVS system design (System
Product Version 1 Release 2 and above) does provide for synchronous address
space communication and SRBs can be avoided.  In fact, programs can actually be
shared between address spaces, and can have concurrent access to both address
spaces.

Many System Programmers may have already used the Move to Primary (MVCP), Move
to Secondary (MVCS), and Move with Key (MVCK) instructions.  These instructions
provide a means of performing data movement between two address spaces.  Similar
to the Move Character (MVC) instruction, the MVCP, MVCS, and MVCK instructions
move data from the storage location addressed by the second operand to the
storage location addressed by the first operand.  Unlike the MVC however, the
storage locations addressed by the operands of the MVCP, MVCS, and MVCK need not
be (and commonly are not) in the same address space.

The introduction of these instructions provided a means by which shared data
could be contained within a private address space, thus reducing the use of
common areas and avoiding SRBs altogether; but for a program running in one
address space to access data in another address space, the address of the data
must still be known.  A Program Call (PC) routine is one way in which an address
space can provide a program that is used by other address spaces in order to
access its data more easily.

A PC routine has many advantages.  The transfer of control from the calling
routine to the offered PC routine is done without an interrupt (unlike the
SCHEDULE SVC used for SRBs).  The PC routine resides inside a private address
space so that data inside that private address space is easily accessible but at
the same time remains isolated.  The PC routine can make use of the MVCP, MVCS,
and MVCK instructions and move data directly between its address space and its
caller's address space.

The balance of this article describes one method in which a PC routine can be
defined and used.  As an example, let's assume that a Data Centre has a large
table that contains a look-up key and corresponding data.  At any given time,
any number of address spaces may need to look up values in this table.  It would
not be reasonable for each address space to maintain a copy of the table and it
is not desired to have the large table in common storage.  One solution to the
problem would be to have a started task create the table in its private area and
then offer a PC routine that would perform the table look-up operation.  Using
the MVCP and MVCS instructions the look-up key could be moved into the started
task's address space, the table searched, and the corresponding data moved back
into the caller's address space.  The table would be isolated in a private
address space and the look-up operation could be performed without an SVC
interrupt occurring and without the SRB overhead.  The several coding examples
that are provided demonstrate how to use a PC routine and show how the above
example might be implemented.  The coding examples show the instructions that
are required to define a PC routine, to write a PC routine, to call a PC
routine, and to withdraw a PC routine.

Before looking at the instructions required to define a PC routine, it is
necessary to understand how a PC routine is found and invoked.  The transfer of
control from the caller to the PC routine is achieved by using the Program Call
(PC) instruction.  The operand of the PC instruction is a PC number.  The PC
number is made up of a Linkage Index (LX) and an Entry Index (EX).  Each PC
routine offered by an address space is defined by an entry in an Entry Table.  A
pointer to the Entry Table is placed in a Linkage Table.  The PC instruction
uses the LX to index into the Linkage Table to find the address of the
associated Entry Table.  The EX value is then used to index into the Entry Table
to find the address of the requested PC routine.  This indexing operation is
illustrated in Figure 1.

The caller of a PC routine must therefore know the LX and EX values that are
associated with the routine.  There is a Linkage Table for each address space,
so if a PC routine is to be available to all other address spaces, a Linkage
Table entry referencing the Entry Table must be placed in every address space's
Linkage Table.  The PC routine will return to its caller by using the Program
Transfer (PT) instruction.


DEFINING A PC ROUTINE

Several macro instuctions are used to define the PC routine to MVS and are shown
in the following coding example.  The first step in defining a PC routine is to
reserve an LX.  The LXRES macro provides this service.  By specifying SYSTEM=YES
on the LXRES macro, system-wide connections (the PC routines available to all
address spaces) will be reserved.  As the PC routine will be made available to
all other address spaces, the routine must be authorised to issue the PT
instruction to any address space.  To define this authorisation an AX value of
'1' must be set.  The AXSET macro is used for this purpose.

The Entry Table that describes the PC routines is defined by the user.  The
Entry Table is made up of two main parts; a header that specifies the number of
PC routines, and a definition for each routine.  The definition of each routine
contains the PC routine address, and the PC routine's attributes.  In the
following coding example both PC routines that are defined will cause an address
space switch to occur and will run in supervisor state.  The address space
switch will result in the program running with a primary address space of its
creator and a secondary address space of the caller.  This is important to note
when determining in which direction data is moving when using the MVCS or MVCP
instructions.  To create the Entry Table, the ETCRE macro is used and given the
address of the Entry Table as defined by the user.  A token value is returned by
the ETCRE macro and must be saved.  This token is used in connecting and
removing the Entry Table.

The final step in defining the PC routine, is to connect the Entry Table to the
Linkage Tables in all existing and future address spaces.  The ETCON macro is
used for this and is provided with the LX value that was reserved and the token
that was provided when the Entry Table was created.  Once the Entry Table has
been connected, the PC routines are usable.  When an address space switch PC
routine is being used, address translation exceptions are not tolerated during
the table indexing operations.  As a result, it must be ensured that the address
space that defined and contains the PC routine is non-swappable.  If a PC
instruction is used and the address space containing the PC routine is swapped
out, an abend 0D5 will occur in the caller's address space.

In order for a PC routine to be called, the LX and EX values must be known by
its caller.  In the following coding the LX value is stored in a user-reserved
field of the CVT.  Any other commonly known and addressable storage area will
suffice.  It is important to ensure that no address spaces try to call the PC
routine until after the Entry Table connection is complete.  The LX value
therefore should not be revealed to the PC routine's callers until the Entry
Table connection is complete.  Issuing a PC instruction with an unconnected LX
value will result in an abend 0D6.


WRITING A PC ROUTINE

The following coding example shows a skeleton PC routine.  An A-type address
constant using the PC routine's CSECT name was used in the Entry Table
definition shown in the previous coding example.  As a result the Entry Table
entry would contain the entry point address of the PC routine.

On entry into the PC routine, the PCLINK macro is used with an operand of STACK.
With the STACK operand, this macro is used to save the cross memory environment
and the contents of several registers.  The PCLINK macro will return a stack
token in register 14 that must be saved and used when removing the stack entry.
Once the environment has been saved, all registers are available to the PC
routine and CSECT addressability should be established.


Register 1 can be used to pass an address from the caller to the PC routine.  If
an address space switch PC routine is being used, it is most important to
remember that addresses passed to the routine reference storage locations in the
secondary address space.  To move data from the secondary address space into the
primary, the MVCP instruction can be used (assuming equivalent storage keys).
To move data from the primary address space into the secondary address space the
MVCS instruction can be used (again, assuming equivalent storage keys).

In the following example, the caller has placed the address of a parameter list
in register 1.  The parameter list contains the addresses of two areas of
storage in the caller's address space.  The first parameter contains a value to
be used by the PC routine and the second parameter is an area in which a value
is to be returned by the PC routine.  Note that the address in the parameter
list must first be moved into the primary address space using an MVCP
instruction.  The address can then be loaded into a register and used in an MVCP
instruction to move the actual parameter into the primary address space.  Note
also that the PC routine cannot issue any SVCs with the exception of ABEND.

To return to its caller, the PC routine must use the PCLINK macro with the
UNSTACK operand, and provide the stack token.  Following the PCLINK macro, a PT
instruction is used to return control to the caller.  Register 15 may be used by
the PC routine to relay a return code to its caller.


CALLING A PC ROUTINE

The call to a PC routine is simple but convention dictates five steps; save all
registers, save the current secondary address space-id (ASID), issue the PC
instruction, re-load the previous secondary ASID, and re-load the registers.
These five steps are illustrated in the following coding example.  The saving
and re-loading of the registers is done using the caller's save area and the STM
and LM instructions as per normal convention.  To save the secondary ASID, the
Extract Secondary ASN (ESAR) instruction is used.  The ESAR instruction returns
the current secondary ASID into the operand register.  The secondary ASID value
contained in the register is then stored in the register 15 position of the save
area.  Once the registers and the secondary ASID have been saved the PC routine
can be called.

As mentioned earlier, in order to call a PC routine the LX and EX values must be
known.  In the first coding example, the CVTUSER field was used to save the LX
value and make it accessible to all address spaces.  In the following example,
the LX value is loaded from the CVTUSER field and the PC instruction is issued.
As the EX value is zero, the first PC routine in the Entry Table will be
invoked.  If a routine other than the first one was desired, the calling program
could supply a different EX value, determined by the position of the PC
routine's definition in the Entry Table.  After the PC routine has returned
control to the caller, the secondary ASID value is loaded from storage and set
using the Set Secondary ASN (SSAR) instruction.  The register values are re-
loaded and the program continues.  Note that the user of register 2 to contain
the PC number is also dictated by convention.


WITHDRAWING A PC ROUTINE

Before removing the PC routine, it should be indicated to any callers that the
routine is no longer available.  The easiest way to accomplish this may be to
zero the LX value that was placed in common storage and have any calling
routines first check for a non-zero LX value before issuing a PC instruction.
The removal of the PC routine is simply the reverse of the steps taken to define
it.  As shown in the following coding example, an ETDES macro is used to destroy
the Entry Table.  The ETDES macro is provided with the token returned by the
ETCRE macro when the Entry Table was created.  Next the authorisation of the PC
routine to issue the PT instruction is removed using the AXSET macro with an
authorisation value of zero.  The reserved Linkage Index can be freed by using
the LXFRE macro and providing the list returned by the LXRES macro.


SUMMARY

Cross Memory Services were introduced with MVS SP 1.2, and IBM has used them
heavily.  More and more system functions are being moved into private address
spaces and utilising Cross Memory Services.  For example, all enqueue control
blocks (RIBs and RIBEs) are maintained inside the GRS address space.  Many
readers may have already made use of a PC routine offered by the GRS address
space.  In fact, an article in MVS Update, issue 9, provided a program for
identifying dataset enqueues.  That program makes use of the GQSCAN macro.  The
GQSCAN macro simply expands into a call to a PC routine contained in the GRS
address space.  The PC routine moves in the request parameters and moves back
RIBs and RIBEs.

The next time you need data shared between address spaces, or want to isolate
your data, avoid SVC interrupts and SRB overheads, and consider a PC routine.
It may be just the ticket.


CODING EXAMPLES


Defining a PC routine

* BUILD AND CONNECT DEFINITION FOR CROSS MEMORY SERVICE
         MODESET KEY=ZERO,MODE=SUP     SUPERVISOR STATE, KEY ZERO

         LXRES LXLIST=LXL,SYSTEM=YES   ARRANGE SYSTEM LX

         LA    R2,1                    LOAD VALUE OF ONE
         AXSET AX=(R2)                 SET AX AUTHORITY TO ONE

         ETCRE ENTRIES=ETDESC          DEFINE ENTRY TABLES
         ST    R0,TKVALUE              AND SAVE TOKEN

         L     R3,CVPTR                CVT ADDRESS
         USING CVTMAP,R3               ADDRESS CVT

         ETCON LXLIST=LXL,TKLIST=TKL   CONNECT ENTRY TABLES TO ALL

         MVC   CVTUSER(4),LXVALUE      UPDATE CVT WITH LX

         MODESET KEY=NZERO,MODE=PROB   BACK TO PROBLEM PROGRAM STATE

* STORAGE AREAS
LXL      DS    0F
LXCOUNT  DC    F'1'
LXVALUE  DS    F
TKL      DS    0F
TKCOUNT  DC    F'1'
TKVALUE  DS    F
ETDESC   DS    0D
* --- ETD HEADER ---
         DC    X'00'
         DC    X'00'
         DC    H'2'                    TWO SERVICE ENTRIES FOLLOW
* --- ETD ENTRY ONE ---
         DC    X'00'                   ENTRY INDEX (EX)
         DC    X'C0'                   SPACE SWITCH & SUPERVISOR
         DC    H'0'
         DC    F'0'
         DC    A(PCRTN1)               RTN ADDRESS
         DC    X'FFFF'                 CALLER IN ANY KEY
         DC    X'00FF'                 SERVICE ACCESS TO 8-15 + CALLER
         DC    F'0'
* --- ETD ENTRY TWO ---
         DC    X'01'                   ENTRY INDEX (EX)
         DC    X'C0'                   SPACE SWITCH & SUPERVISOR
         DC    H'0'
         DC    F'0'
         DC    A(PCRTN2)               RTN ADDRESS
         DC    X'0080'                 CALLER IN KEY 8
         DC    X'00FF'                 SERVICE ACCESS TO 8-15 + CALLER
         DC    F'0'


A PC routine

PCRTN1   CSECT
         PCLINK STACK                  SAVE CALLERS INFO
         BALR  R11,0
         USING *,R11
         ST    R14,STACK               SAVE STACK ENTRY TOKEN
* CODE COULD BE PLACED HERE TO MOVE ANY PARMS FROM THE CALLER'S ADDRESS
* SPACE INTO AN AREA CONTAINED INSIDE THIS ADDRESS SPACE.
* FOR EXAMPLE: ON ENTRY TO PC ROUTINE, R1 POINTERS TO PARMLIST IN
* SECONDARY ADDRESS SPACE IN THE FORM:
*  R1 ---> A(PARM1),A(PARM2)
*  PARM1 IS A 8 BYTES AREA CONTAINING A CHARACTER VALUE
*  PARM2 IS A 80 BYTE AREA FOR A VALUE TO BE RETURNED
         L     R12,                    ADDRESS AREA FOR REENTRANCY
         USING AREA,R12

         SR    R2,R2                   ZERO ACCESS KEY

         L     R3,=F'4'                GET LENGTH OF PARM ADDRESS
         MVCP  INLIST(R3),0(R1),R2     MOVE IN ADDRESS OF FIRST PARM
         MVCP  INLIST+4(R3),4(R1),R2   MOVE IN ADDRESS OF SECOND PARM

         L     R3,=F'8'                GET LENGTH OF FIRST PARM
         L     R4,INLIST               GET ADDRESS OF FIRST PARM
         MVCP  INPARM(R3),0(R4),R2     MOVE IN FIRST PARM
*   PROCESS USING INPARM TO DETERMINE VALUE OF OUTPARM
         L     R3,=F'80'               GET LENGTH OF SECOND PARM
         L     R4,INLIST+4             GET ADDRESS OF SECOND PARM
         MVCS  0(R3,R4),OUTPARM,R2     MOVE OUT SECOND PARM
* REGISTER 15 CAN BE SET TO INDICATE AN APPROPRIATE RETURN CODE
         SR    R15,R15                 SET RETURN CODE
* RETURN TO CALLER
         SR    R3,R3                   CLEAR REG 3
         L     R14,STACK               LOAD STACK ENTRY TOKEN
         PCLINK UNSTACK,THRU=(R14)     RESTORE CALLERS INFO
         PT    R3,R14                  RETURN TO CALLER
AREA     DSECT
STACK    DS    F
INLIST   DS    F
         DS    F
INPARM   DS    CL8
OUTPARM  DS    CL80


Calling a PC routine

* REGISTER 1 MAY BE USED TO PASS AN ADDRESS TO THE PC ROUTINE
         LA    R1,PARMLIST             ADDRESS PARAMETER LIST

         STM   R14,R12,12(R13)         SAVE CALLER'S REGS

         ESAR  R2                      OBTAIN SECONDARY ASID
         ST    R2,16(,R13)             AND SAVE IT

         L     R2,CVTPTR               GET CVT ADDRESS
         USING CVT,R2

         L     R2,CVTUSER              GET LX FROM CVT
         LTR   R2,R2                   SERVICE AVAILABLE ?
         BZ    NOSERVICE               NO...GO DO SOMETHING ELSE
* THE 'EX' POSITION OF PC NUMBER (BITS 24-31) CAN BE SET TO
* INDICATE THE DESIRED PC ROUTINE
* FOR EXAMPLE:
*        A     R2,=F'1'                INDICATE SECOND PC ROUTINE
         PC    0(R2)                   REQUEST SERVICE

         L     R2,16(,R13)             GET SAVED SECONDARY ASID
         SSAR  R2                      AND RESTORE IT

         LM    R0,R12,20(R13)          RESTORE
         L     R14,12(,R13)                    REGS

         LTR   R15,R15                 SUCCESSFUL ?
         BZ    FOUND                   YES...GO DO SOMETHING
         B     NOTFOUND                NO....DO SOMETHING ELSE

PARMLIST DC    A(PARMOUT)              ADDRESS PARM 1
PARMLIST DC    A(PARMOUT)             ADDRESS PARM 1
         DC    A(PARMIN)              ADDRESS PARM 2
PARMOUT  DC    CL8'PARM 1'            THIS PARM GOES TO PC ROUTINE
PARMIN   DS    CL80                   THIS PARM IS RETURNED BY PC RTN


Withdrawing a PC routine

* DISABLE AND REMOVE CROSS MEMORY SERVICE
         L     R3,CVTPTR              CVT ADDRESS
         USING CVTMAP,R3              ADDRESS CVT

         MODESET KEY=ZERO,MODE=SUP    SUPERVISOR STATE, KEY ZERO

         MVC   CVTUSER,=F'0'          ZERO LX POINTER THAT CALLERS SEE

         ETDES TOKEN=TKVALUE,PURGE=YES DESTROY ENTRY TABLES

         SR    R2,R2                  VALUE OF ZERO
         AXSET AX=(R2)                REMOVE AUTHORISATION

         LXFRE LXLIST=LXL             FREE THE LINKAGE INDEX

         MODESET KEY=NZERO,MODE=PROB BACK TO PROBLEM PROGRAM STATE


Scott Dimond
MVS Systems Programmer (Canada)    © Xephon 1987
















