Querying transmitted files ready to be received

      Files ready to be RECEIVEd in the Output Queue:
      DestUser  Origin  Records  Creation
      -------- -------- -------  --------
      PT0240   WRITER9      299  01/21/95
      GATRACF  GATRACF      100  01/21/95
      TESTUSER GATRACF      190  01/22/95
      PTB260   WRITER9     1120  01/23/95

XMITQ REXX EXEC

/*-REXX/XMITQ------------------------------------------------------*/
/* Query for XMITed files that are ready to be RECEIVEd.           */
/* Format: XMITQ < UserId >                                        */
/*               <   *    >                                        */
/*-----------------------------------------------------------------*/
   address TSO; Trace 0
   Parse Source OpSys HowCalled Fn DD Dsn CmdName Env AddSpc ParseTok
   parse upper arg FindUser .
   if AddSpc = 'ISPF' then call SUBMITJOB     /* Called from ISPF  */
   if FindUser = '' then FindUser = UserId()
   'EXECIO * DISKR ISFIN (STEM LINE. FINIS' /* Read from SDSF output */
   do Ind = 1 to Line.0
      parse upper value strip (Line.Ind, 'T') with FileDesc
      parse var FileDesc 6 JobName 14 15 JobId 23 24 Owner 32 ,
                        54 Dest 62 75 Records 82 122 Status 126 ,
                       229 CrDate 237.
      select
         when Status <> 'USER' then iterate    /* Reject other files */
         when FindUser = Dest : FinsUser = '*' /* Select one or all */
              then do
                   if Owner = '????????' then Owner = JobName
                   if FirstTime <> 'NO' then call MSGHEADER
                   Msg = Dest Owner Records CrDate '        '
                   'SEND '''Msg''' USER('userId()')'
                   end
         otherwise nop
      end
   end
   exit
/*-----------------------------------------------------------------*/
MSGHEADER:
    Msg1 = 'Files ready to be RECEIVEd in the Output queue:'
    Msg2 = 'DestUser  Origin  Records Creation             '
    Msg3 = '-------- -------- ------- --------             '
    'SEND '''Msg1''' USER('userId()')'
    'SEND '''Msg2''' USER('userId()')'
    'SEND '''Msg3''' USER('userId()')'
    FirstTime = 'No'
    return
/*-----------------------------------------------------------------*/
SUBMITJOB:
    address ISPEXEC
    'FTOPEN TEMP'                        /* Include XMITQ skeleton */
    'FTINCL XMITQ'                       /* creating a temporary file */
    'FTCLOSE'
    'VGET ZTEMPF'
    address TSO
    x = outtrap('LINE.')                       /* Submit the job and */
    'SUBMIT ('''ztempf''')'                    /* trap output message  */
    RcSub = Rc
    x = outtrap('OFF')
    'DELETE '''ztempf'''
    exit

SKELETON JCL

//*-XMITQ/SKEL-------------------------------------------------------*/
//* Printing of SDSF output queue to the ISFOUT dataset.             */
//* JOBNAME MUST BE ONE CHARACTER LONG, or SDSF will select only the */
//* files with a matching jobname  (except the last character).      */
//* MSGCLASS=Z indicates that the job's output is to be deleted.     */
//*------------------------------------------------------------------*/
//S        JOB CLASS=A,MSGCLASS=Z,MSGLEVEL=(1,1)
//SDSF     EXEC PGM=SDSF,PARM='++24,240'      (24 lines by 240 columns)
//ISFOUT   DD   UNIT=VI0,DISP=(NEW,PASS)
//ISFIN    DD  *
O
++ALL
/*
//*----------------------------------------------------------------*/
//* Calls XMITQ to select "&FINDUSER" userid, and sends the asking */
//* user a message with the list of files ready to RECEIVE.        */
//*----------------------------------------------------------------*/
//IKJEFT01 EXEC PGM=IKJEFT01,PARM='%XMITQ &FINDUSER '
//SYSTSPRT DD SYSOUT=*
//SYSLBC   DD DSN=SYS1.BRODCAST,DISP=SHR
//SYSTSIN  DD DUMMY
//ISFIN    DD DSN=*.SDSF.ISFOUT,DISP=(OLD,DELETE)
//SYSPROC  DD DSN=GATAPD.TOOLS.EXEC,DISP=SHR        /* XMITQ location */


Patrick Leroy
Systems Analyst
Uniao de Bancos Portugueses (Portugal)                  c Xephon 1995

