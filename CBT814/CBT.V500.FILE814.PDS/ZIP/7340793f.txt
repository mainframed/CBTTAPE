Contention analysis - a batch extractor program

SAMPLE JCL

//* THIS JOB CAN BE RUN TO ANALYSE CONTENTION EVENTS.
//* BEFORE RUNNING THIS JOB, THE INPUT DATASET CONTAINING SMF RECORDS
//* TYPE 77 SHOULD BE AVAILABLE AND THE OUTPUT DATASET SHOULD BE
//* ALLOCATED.
//CONTEXTR EXEC PGM=CONTEXTR
//SMFDATA  DD DSN=INPUT.SMFR77,DISP=OLD
//PRINTOUT DD SYSOUT=A
//CONTLIST DD DISP=SHR,DSN=OUTPUT.LIST
//*

SOURCE CODE

CONTEXTR CSECT
         USING *,R10,R11           ESTABLISH ADDRESSABILITY
         STM   R14,R12,12(R13)     SAVE3 REGISTERS
         LR    R10,R15             SET FIRST BASE REGISTER
         LA    R11,2048(R10)       SET SECOND BASE REGISTER
         LA    R11,2048(R11)       AND INCREMENT TO PROPER VALUE
         LR    R12,R13             STORE PREVIOUS S/A ADDRESS
         LA    R13,SAVE3           LOAD ADDRESS OF THIS SAVE3 AREA
         ST    R12,SAVE3+4         CHAIN BACKWARDS
         ST    R13,8(R12)          CHAIN FORWARD
GETMATD  GETMAIN R,LV=40000
         LR    R9,R1 (R9) = ADDR OF THE ALLOCATED VIRTUAL STORAGE AREA
         LTR   15,15
         BZ    OKGETMAT
         LA    R15,4
         B     FINI
OKGETMAT ST    R9,R9SAVE
         OPEN  (PRINTDCB,(OUTPUT))
         OPEN  (CONTLDCB,(OUTPUT))
         LA    R1,0
         ST    R1,NRECORD
         LA    R1,0
         ST    R1,R77TOTAL
         MVI   CONTRECO,C' '
         MVC   CONTRECO+1(L'CONTRECO-1),CONTRECO
         OPEN  (SMFREDCB,(INPUT))
         USING SMF77LEN,R9
GETSMFRE GET   SMFREDCB,SMF77LEN
         CLI   SMF77RTY,X'4D'  TEST IF RECORD TYPE 77
         BE    OKRECTYP
         B     NOTSMF77
OKRECTYP MVI   PRINT,C'-'
         MVC   PRINT+1(L'PRINT-1),PRINT
         PUT   PRINTDCB,PRINT
*   PRINT SOME ELEMENTS OF THIS RECORD
         L     R3,R77TOTAL
         A     R3,=F'1'        INCREASE COUNTER
         ST    R3,R77TOTAL
         MVC   SMFDATE(4),SMF77DTE
         CALL  SUYYDDDF,(SMFDATE,STAMP),VL
         MVC   SMFRECST+24(9),DAYNO
         MVC   TIME(4),SMF77TME
         CALL  SUTIME,(TIME,TIMESTAM),VL
         MVC   SMFRECST+44(11),TIMESTAM
         LH    R4,SMF77EDN
         LA    R1,0
         CR    R4,R1
         BNE   ENQUEUE
         MVC   PRINT(133),BLANK
         MVC   PRINT+10(24),=C'NO ENQUEUE DATA SECTIONS'
         PUT   PRINTDCB,PRINT
         B     NOTSMF77
ENQUEUE  LR    R3,R9           (R3) = ADDRESS OF BEGIN OF THE RECORD
         L     R1,SMF77EDS
         AR    R3,R1           (R3) = ADDRESS OF THE ENQUEUE SECTION
         USING SMF77QNM,R3
LOOPENQU MVC   ETIME(11),TIMESTAM
         MVC   EDATE(9),DAYNO
         MVC   MAJORNAM+19(8),SMF77QNM
         MVC   RTYPE(8),SMF77QNM
         MVC   MINORNAM(44),SMF77RNM
         MVC   RNAME(44),SMF77RNM
*  TOTAL CONTENTION TIME
*  CONVERT UNITS (1UNIT=1024 MICROSECONDS) TO MICROSECONDS
         L     R7,SMF77WTT     (R7) = # OF UNITS
         SR    R6,R6           (R6) = 0
         SR    R1,R1
         LA    R1,1024
         MR    R6,R1           (R7) = MICROSECONDS
         SR    R6,R6           (R6) = 0
         SR    R1,R1
         LA    R1,1000         CONVERT MICROSECONDS INTO MILLISECONDS
         DR    R6,R1           (R7) = MILLISECONDS
         ST    R7,NUMBER
         CALL  CONVFICL,(NUMBER,RESULT),VL
         MVC   CONTTIM1(6),RESULT+1
         MVI   CONTDOT,C'.'
         MVC   CONTTIM2(1),RESULT+7
         SR    R6,R6           (R6) = 0
         SR    R1,R1
         LA    R1,1000         CONVERT MILLISECONDS INTO SECONDS
         DR    R6,R1           (R7) = SECONDS
         SR    R6,R6           (R6) = 0
         SR    R1,R1
         LA    R1,60           CONVERT SECONDS INTO MINUTES
         DR    R6,R1           (R7) = MINUTES
         ST    R7,NUMBER
         CALL  CONVFICL,(NUMBER,RESULT),VL
         MVC   TOTCONTI+59(10),RESULT
         MVC   CTIMEM(10),RESULT
         LH    R1,SMF77EXM
         ST    R1,NUMBER
         CALL  CONVFICL,(NUMBER,RESULT),VL
         MVC   EXREQWAI+37(10),RESULT
         MVC   WEXMIN(4),RESULT+6
         LH    R1,SMF77EXX
         ST    R1,NUMBER
         CALL  CONVFICL,(NUMBER,RESULT),VL
         MVC   EXREQWAI+58(10),RESULT
         MVC   WEXMAX(4),RESULT+6
         LH    R1,SMF77SHM
         ST    R1,NUMBER
         CALL  CONVFICL,(NUMBER,RESULT),VL
         MVC   SHREQWAI+37(10),RESULT
         MVC   WSHMIN(4),RESULT+6
         LH    R1,SMF77SHX
         ST    R1,NUMBER
         CALL  CONVFICL,(NUMBER,RESULT),VL
         MVC   SHREQWAI+58(10),RESULT
         MVC   WSHMAX(4),RESULT+6
         LH    R1,SMF77EVT
         ST    R1,NUMBER
         CALL  CONVFICL,(NUMBER,RESULT),VL
         MVC   TOTNUCON+33(10),RESULT
         MVC   NEVENTS(4),RESULT+6
         TM    SMF77DFG,B'10000000'   TEST IF RESOURCE STILL IN CONT?
         BZ    TESTOWNE               NOT, TEST OWNER
         MVC   PRINT(133),BLANK
         MVC   PRINT+10(35),=C'RESOURCE STILL IN CONTENTION !!!!!!'
         MVC   PRINT+35(35),=C'###################################'
         PUT   PRINTDCB,PRINT
TESTOWNE TM    SMF77DFG,B'00100000'   TEST IF OWNER EXCLUSIVE CONTROL?
         BZ    OWNERSHA               NOT, OWNER SHARES
         MVC   FIROWNER+66(19),=C', EXCLUSIVE CONTROL'
         MVC   OCONTRO(9),=C'EXCLUSIVE'
         B     TESTFIWA               TEST FIRST WAITING
OWNERSHA MVC   FIROWNER+66(19),=C', SHARED CONTROL   '
         MVC   OCONTRO(9),=C'SHARED   '
TESTFIWA TM    SMF77DFG,B'00010000'   TEST IF 1ST WAIT FOR EXCLUSIVE ?
         BZ    FIWAITSH               NOT, WAITING FOR SHARED USE
         MVC   FIRWAITE+80(9),=C'EXCLUSIVE'
         MVC   WCONTRO(9),=C'EXCLUSIVE'
         B     TESTSEWA               TEST SECOND WAITING
FIWAITSH MVC   FIRWAITE+80(9),=C'SHARED   '
         MVC   WCONTRO(9),=C'SHARED   '
TESTSEWA TM    SMF77DFG,B'00001000'   TEST IF 2 WAITN FOR EXCLUSIVE  ?
         BZ    SEWAITSH               NOT, WAITING FOR SHARED USE
         MVC   SECWAITE+80(9),=C'EXCLUSIVE'
         B     TESTEND                END OF THE TEST
SEWAITSH MVC   SECWAITE+80(9),=C'SHARED   '
TESTEND  LA    R1,0
         LH    R1,SMF77DOW
         ST    R1,NUMBER
         MVC   RESULT(10),BLANK
         CALL  CONVFICL,(NUMBER,RESULT),VL
         MVC   NUOWNERS+59(10),RESULT
         LA    R1,0
         LH    R1,SMF77DWR
         ST    R1,NUMBER
         MVC   RESULT(10),BLANK
         CALL  CONVFICL,(NUMBER,RESULT),VL
         MVC   NUWAITER+59(10),RESULT
         MVC   FIROWNER+58(8),SMF77DO1
         MVC   FOWNER(8),SMF77DO1
         MVC   SECOWNER+58(8),SMF77DO2
         MVC   FIRWAITE+58(8),SMF77DW1
         MVC   FWAITER(8),SMF77DW1
         MVC   SECWAITE+58(8),SMF77DW2
         LA    R1,0
         L     R1,SMF77AQL
         ST    R1,NUMBER
         CALL  CONVFICL,(NUMBER,RESULT),VL
         MVC   TOTWAITI+65(5),RESULT+5
         MVC   PRINT(133),BLANK
         MVC   PRINT+1(44),MINORNAM
         PUT   PRINTDCB,PRINT
         MVC   PRINT(133),BLANK
         MVC   TOTCONTI+43(8),CONTTIME
         MVC   CTIMES(8),CONTTIME
         MVC   PRINT+4(77),TOTCONTI
         PUT   PRINTDCB,PRINT
         MVC   PRINT(133),BLANK
         MVC   PRINT+4(27),MAJORNAM
         PUT   PRINTDCB,PRINT
         MVC   PRINT(133),BLANK
         MVC   PRINT+4(55),SMFRECST
         PUT   PRINTDCB,PRINT
         MVC   PRINT(133),BLANK
         MVC   PRINT+4(68),EXREQWAI
         PUT   PRINTDCB,PRINT
         MVC   PRINT(133),BLANK
         MVC   PRINT+4(68),SHREQWAI
         PUT   PRINTDCB,PRINT
         MVC   PRINT(133),BLANK
         MVC   PRINT+4(43),TOTNUCON
         PUT   PRINTDCB,PRINT
         MVC   PRINT(133),BLANK
         MVC   PRINT+4(69),NUOWNERS
         PUT   PRINTDCB,PRINT
         MVC   PRINT(133),BLANK
         MVC   PRINT+4(69),NUWAITER
         PUT   PRINTDCB,PRINT
         MVC   PRINT(133),BLANK
         MVC   PRINT+4(85),FIROWNER
         PUT   PRINTDCB,PRINT
*    CHECK IF SECOND OWNER  EXISTING?
         CLC   SMF77DO2(8),=X'0000000000000000'
         BE    NOSECOWN
         MVC   PRINT(133),BLANK
         MVC   PRINT+4(66),SECOWNER
         PUT   PRINTDCB,PRINT
NOSECOWN MVC   PRINT(133),BLANK
         MVC   PRINT+4(93),FIRWAITE
         PUT   PRINTDCB,PRINT
*    CHECK IF SECOND WAITER EXISTING?
         CLC   SMF77DW2(8),=X'0000000000000000'
         BE    NOSECWAI
         MVC   PRINT(133),BLANK
         MVC   PRINT+4(93),SECWAITE
         PUT   PRINTDCB,PRINT
NOSECWAI MVC   PRINT(133),BLANK
         MVC   PRINT+4(70),TOTWAITI
         PUT   PRINTDCB,PRINT
         L     R7,NRECORD
         LA    R1,1
         AR    R7,R1
         ST    R7,NRECORD
         ST    R7,RECN
         PUT   CONTLDCB,CONTRECO
         MVI   CONTRECO,C' '
         MVC   CONTRECO+1(L'CONTRECO-1),CONTRECO
         MVI   PRINT,C'-'
         MVC   PRINT+1(L'PRINT-1),PRINT
         PUT   PRINTDCB,PRINT
         SR    R1,R1
         LH    R1,SMF77EDL
         AR    R3,R1
         BCT   R4,LOOPENQU
         MVC   PRINT(133),BLANK
         PUT   PRINTDCB,PRINT
NOTSMF77 B     GETSMFRE
ENDATA   CLOSE (SMFREDCB,,CONTLDCB)
         MVC   PRINT(133),BLANK
         MVC   PRINT+1(32),=C'TOTAL NUMBER OF RECORDS TYPE 77 ='
         CALL  CONVFICL,(R77TOTAL,RESULT),VL
         MVC   PRINT+40(10),RESULT
         PUT   PRINTDCB,PRINT
         CLOSE (PRINTDCB)
FINI     L     R13,4(R13)
         L     R9,R9SAVE
         FREEMAIN  R,LV=40000,A=(R9)
         LA    R7,0
         LR    R15,R7
         RETURN (14,12),RC=(15)
SAVE3    DS    18F
NUMBER   DS    F
R77TOTAL DS    F
NRECORD  DS    F
MINORNAM DS    CL44
RESULT   DS    CL10
PRINT    DS    CL133
BLANK    DC    CL133' '
MAJORNAM DC    CL27'TYPE (MAJOR NAME):         '
SMFRECST DC    CL55'SMF RECORD STAMP: DATE:             , TIME:'
EXREQWAI DC    CL68'EXCLUSIVE REQUESTS WAITING: MINIMUM: 1234567891, MA*
               XIMUM: 1234567890'
SHREQWAI DC    CL68'SHARED    REQUESTS WAITING: MINIMUM: 1234567891, MA*
               XIMUM: 1234567890'
TOTNUCON DC    CL43'TOTAL NUMBER OF CONTENTION EVENTS:         '
NUOWNERS DC    CL69'NUMBER OF OWNERS USING THE RESOURCE AT MAXIMUM CONT*
               ENTION: 1234567890'
NUWAITER DC    CL69'NUMBER OF WAITERS WAITING FOR RSRCE AT MAXIMUM CONT*
               ENTION: 1234567890'
FIROWNER DC    CL85'FIRST RESOURCE OWNER DURING PERIOD OF MAXIMUM CONTE*
               NTION:         ,                  '
SECOWNER DC    CL85'2ND   RESOURCE OWNER DURING PERIOD OF MAXIMUM CONTE*
               NTION:                            '
FIRWAITE DC    CL93'FIRST WAITER WAITING DURING PERIOD OF MAXIMUM CONTE*
               NTION:         , WAITING FOR           USE'
SECWAITE DC    CL93'2ND   WAITER WAITING DURING PERIOD OF MAXIMUM CONTE*
               NTION:         , WAITING FOR           USE'
TOTWAITI DC    CL70'TOTAL NUMBER OF WAITING REQUESTS DURING THE MEASURE*
               MENT INTERVAL=     '
TOTCONTI DC    CL77'TOTAL RESOURCE CONTENTION TIME, IN SECONDS=        *
                , ===>            MINUTES'
CONTTIME DS   0CL8
CONTTIM1 DS   CL6
CONTDOT  DS   CL1
CONTTIM2 DS   CL1
STAMP    DS   0CL12
DAY      DS   CL3      BLANK
DAYNO    DS   CL2      BLANK
MONTH    DS   CL3      BLANK
YEAR     DS   CL2      19
YEAR1    DS   CL2      BLANK
TIME     DS    F
TIMESTAM DS   0CL11
HH       DS   CL2      BLANK
         DS   CL1      BLANK
MM       DS   CL2      BLANK
         DS   CL1      BLANK
SS       DS   CL2      BLANK
         DS   CL1      BLANK
DD       DS   CL2      BLANK
         DS   0D
CONTRECO DS   0CL164
RECN     DS   F        RECORD NAME
         DS   CL1      BLANK
RNAME    DS   CL44     RESOURCE NAME
         DS   CL1      BLANK
RTYPE    DS   CL8      RESOURCE TYPE
         DS   CL1      BLANK
CTIMES   DS   CL8      TOTAL RECOURCE CONTENTION TIME IN SECS
         DS   CL1      BLANK
CTIMEM   DS   CL10     TOTAL RESOURCE CONTENTION TIME IN MINS
         DS   CL1      BLANK
FOWNER   DS   CL8      FIRST RESOURCE OWNER
         DS   CL1      BLANK
OCONTRO  DS   CL9      OWNERS CONTROL TYPE (SHARED, EXCLUSIVE)
         DS   CL1      BLANK
FWAITER  DS   CL8      FIRST RESOURCE WAITER
         DS   CL1      BLANK
WCONTRO  DS   CL9      WAITERS CONTROL TYPE (SHARED, EXCLUSIVE)
         DS   CL1      BLANK
EDATE    DS   CL9      EVENTS DATE
         DS   CL1      BLANK
ETIME    DS   CL11     EVENTS TIME
         DS   CL1      BLANK
NEVENTS  DS   CL4      TOTAL NUMBER OF CONTENTION EVENTS
         DS   CL1      BLANK
WEXMIN   DS   CL4      MINIMUM NUMBER OF EXCLUSIVE REQUESTS WAITING
         DS   CL1      BLANK
WEXMAX   DS   CL4      MAXIMUM NUMBER OF EXCLUSIVE REQUESTS WAITING
         DS   CL1      BLANK
WSHMIN   DS   CL4      MINIMUM NUMBER OF SHARE     REQUESTS WAITING
         DS   CL1      BLANK
WSHMAX   DS   CL4      MINIMUM NUMBER OF SHARE     REQUESTS WAITING
         DS   CL1      BLANK
FILLER   DS   CL20     BLANK
         DS    0D
PRINTDCB DCB   MACRF=PT,RECFM=FBA,LRECL=133,BLKSIZE=133,DSORG=PS,      *
               DDNAME=PRINTOUT
CONTLDCB DCB   MACRF=PM,RECFM=FB,LRECL=164,DSORG=PS,                   *
               DDNAME=CONTLIST
SMFREDCB DCB   MACRF=GM,DSORG=PS,RECFM=VBS,DDNAME=SMFDATA,EODAD=ENDATA
SMFDATE  DS    F
R9SAVE   DS    F
         LTORG
         ERBSMFR  (77)
         END

CONVFICL SOURCE CODE

* PROCEDURE CONVFICL,(FIXNUMBE,CHARACTE),VL
* CALLING SEQUENCE  CALL  CONVFICL,(FIXNUMBE,CHARACTE),VL
* FUNCTION   : CONVERT FIXED BINARY NUMBER INTO CHARACTERS
CONVFICL CSECT
         USING *,R10,R11           ESTABLISH ADDRESSABILITY
         STM   R14,R12,12(R13)     SAVE3 REGISTERS
         LR    R10,R15             SET FIRST BASE REGISTER
         LA    R11,2048(R10)       SET SECOND BASE REGISTER
         LA    R11,2048(R11)       AND INCREMENT TO PROPER VALUE
         LR    R12,R13             STORE PREVIOUS SA ADDRESS
         LR    R2,R1  (R2) = POINTER TO ADDRESS OF THE PARM LIST
GETMATD  GETMAIN R,LV=500
         LR    R9,R1  (R9)= ADDR. OF THE ALLOCATED VIRT. STORAGE AREA
         LTR   15,15
         BZ    OKGETMAT
         B     FINI
OKGETMAT USING SVC34DSE,R9         ESTABLISH ADDRESSABILITY
         LA    R13,SAVE3           LOAD ADDRESS OF THIS SAVE3 AREA
         ST    R12,SAVE3+4         CHAIN BACKWARDS
         ST    R13,8(R12)          CHAIN FORWARD
         L     R3,0(R2)     (R3) = ADDRESS OF THE FIRST PARAMETER
         LA    R1,FIXNUMBE
         MVC   0(4,R1),0(R3)     MOVE FIXNUMBE
         L     R1,FIXNUMBE
         CVD   R1,PACKFIEL
         MVC   COPYPATE(12),PATTERN
         ED    COPYPATE(12),PACKFIE2
         L     R3,4(R2)     (R3) = ADDRESS OF THE SECOND PARAMETER
         LA    R1,COPYPATE
         MVC   0(10,R3),2(R1)  MOVE RESULT
FINI     FREEMAIN R,LV=500,A=(R9)
         L     R13,4(R13)
         LR    R15,R7
         RETURN (14,12),RC=(15)
BLANK    DC    CL133' '    BLANK
PATTERN  DC    XL12'402020202020202020202120'
         LTORG
SVC34DSE DSECT
SAVE3    DS    18F
PACKFIEL DS    0PL8
         DS    PL2
PACKFIE2 DS    PL6
FIXNUMBE DS    F
COPYPATE DS    CL12
         END

SUTIME SOURCE CODE

* FUNCTION                   : CONVERT TIME
*                              FROM TIME MACRO FORMAT INTO
*                              HH:MM:SS.DD
*                              EG: 11:03:10.14 ;TOTAL = 11 CHARS
SUTIME   CSECT
         USING *,R10,R11           ESTABLISH ADDRESSABILITY
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R10,R15             SET FIRST BASE REGISTER
         LA    R11,2048(R10)       SET SECOND BASE REGISTER
         LA    R11,2048(R11)       AND INCREMENT TO PROPER VALUE
         LR    R12,R13             STORE PREVIOUS SA ADDRESS
         LR    R2,R1      (R2) = POINTER TO ADDRESS OF THE PARM LIST
         ST    R2,R2SAVE  (R2) = POINTER TO ADDRESS OF THE PARM LIST
         LA    R3,SAVEARE1
         LR    R13,R3              LOAD ADDRESS OF THE SAVE AREA
         ST    R12,4(R3)           CHAIN BACKWARDS
         ST    R13,8(R12)          CHAIN FORWARD
         L     R3,0(R2)     (R3) = ADDRESS OF THE FIRST PARAMETER
         LA    R1,TIME
         MVC   0(4,R1),0(R3)     MOVE TIME
         LA    R6,0
         L     R7,TIME
         LA    R1,100
         DR    R6,R1
         ST    R7,SECONDS
         ST    R6,NUMBER
         CALL  CONVFICH,(NUMBER,RESULT),VL
         MVC   DD(2),RESULT+6
*            COMPUTE HOURS
         LA    R6,0
         L     R7,SECONDS
         LA    R1,3600
         DR    R6,R1
         ST    R7,NUMBER
         ST    R6,SECONDS
         CALL  CONVFICH,(NUMBER,RESULT),VL
         MVC   HH(2),RESULT+6
         LA    R6,0
         L     R7,SECONDS
         LA    R1,60
         DR    R6,R1
         ST    R7,NUMBER
         ST    R6,SECONDS
         CALL  CONVFICH,(NUMBER,RESULT),VL
         MVC   MM(2),RESULT+6
         ST    R6,NUMBER
         CALL  CONVFICH,(NUMBER,RESULT),VL
         MVC   SS(2),RESULT+6
         L     R2,R2SAVE
         L     R4,4(R2)           (R4) = ADDRESS OF THE SECOND PARAM.
         MVC   0(11,R4),STAMP
FINI     L     R13,4(R13)
         RETURN (14,12),RC=(15)
SAVEARE1 DS    18F
NUMBER   DS    F
RESULT   DS    CL8
RESULT12 DS    CL12
TIME     DS    F
SECONDS  DS    F
R2SAVE   DS    F
BLANK    DC    CL133' '
STAMP    DS   0CL11
HH       DS   CL2      BLANK
         DC   CL1':'   BLANK
MM       DS   CL2      BLANK
         DC   CL1':'   BLANK
SS       DS   CL2      BLANK
         DC   CL1'.'   BLANK
DD       DS   CL2      BLANK
         LTORG LTORG LTORG LTORG LTORG LTORG LTORG
         END

SUYYDDDF

Szczepan Kowalski
Software Specialist
The Johannesburg Stock Exchange (South Africa)     c Xephon 1995

