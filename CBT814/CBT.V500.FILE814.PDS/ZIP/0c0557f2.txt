Concatenating datasets in TSO/ISPF

      CONCAT SYSPROC USER.CLIST BEFORE

/*       CLIST : CONCAT                                  */
/*       DESC : General purpose concatenation CLIST      */
       PROC 2 DDNAME DSN BEFORE AFTER DEBUG
/*     Set up debugging options                          */
       CONTROL NOFLUSH NOLIST NOMSG
       IF &DEBUG = DEBUG THEN CONTROL LIST MSG CONLIST SYMLIST
/*     Validate options                                  */
       IF &BEFORE = &STR() AND &AFTER = &STR() THEN GOTO HELP
       IF &DDNAME = STEPLIB THEN DO
             WRITE DDNAME STEPLIB IS NOT ALLOWED, CONCAT NOT DONE
             EXIT CODE(4)
             END
       IF &DDNAME = STEPCAT THEN DO
             WRITE DDNAME STEPCAT IS NOT ALLOWED, CONCAT NOT DONE
             EXIT CODE(4)
             END
/*     Get current allocations and search for DDNAME     */
       SET &SYSOUTTRAP = 9999
       LISTALC STATUS
       SET &SYSOUTTRAP = 0
       SET &L = 1
       SET &LINES = &SYSOUTLINE
       DO WHILE &L <= &LINES
            SET &LINE = &&SYSOUTLINE&L
            IF &SUBSTR(1,&LINE) EQ &STR() THEN DO
                  SET &DD = &SUBSTR(3:10,&LINE      ) /* 10 spaces */
                  SET &DD = &DD
                  IF &DD = &DDNAME THEN GOTO EXISTS
                  END
            SET &L = &L + 1
            END
       WRITE DD &DDNAME NOT ALLOCATED, ALLOCATING &DSN TO &DDNAME
       ALLOCATE F(&DDNAME) DSN(&DSN) SHR
       SET &RC = &LASTCC
       IF &RC NE 0 THEN DO
             WRITE ALLOCATION FOR &DSN FAILED, RC=&RC
             EXIT CODE(4)
             END
       EXIT CODE(0)
/*     Extract dataset currently allocated to DDNAME       */
EXISTS: +
      SET &I = 0
DDLOOP: +
      SET &L = &L - 1                        /* previous line      */
      SET &I = &I + 1                        /* next index         */
      SET &SAVEDSN&I = &&SYSOUTLINE &L       /* save DSN           */
      SET &L = &L + 3                        /* next DD line       */
      IF &L GT &SYSOUTLINE THEN GOTO ALLOC   /* if done, go alloc  */
      SET &LINE = &&SYSOUTLINE&L             /* get DDLINE data    */
      SET &LINEDD = &SUBSTR(3:10,&LINE    )  /* 10 spaces */
      SET &LINEDD = &LINEDD                  /* extract any DD     */
      IF &LINEDD = &STR() THEN GOTO DDLOOP   /* if blank, get dsn  */
/*    Build dataset list from saved current allocations     */
ALLOC: +
     SET &II = 1
     DO WHILE &II LE &I
          SET &DSNAME = &&SAVEDSN&II
          SET &DSNAME = &STR('&DSNAME')
          SET &DSLIST = &STR(&DSLIST &DSNAME)
          SET &II = &II + 1
          END
     IF &BEFORE = &STR(BEFORE) THEN DO
          FREE F(&DDNAME)
          ALLOC F(&DDNAME) DA(&DSN &DSLIST) SHR
          SET &RC = &LASTCC
          IF &RC NE 0 THEN DO
                WRITE ALLOCATION FAILED
                WRITE RESTORING OLD ALLOCATION
                ALLOC F(&DDNAME) DA(&DSLIST) SHR REUSE
                EXIT CODE(4)
                END
          EXIT CODE(0)
          END
     IF &AFTER = &STR(AFTER) THEN DO
          FREE F(&DDNAME)
          ALLOC F(&DDNAME) DA(&DSLIST &DSN) SHR
          SET &RC = &LASTCC
          IF &RC NE 0 THEN DO
                WRITE ALLOCATION FAILED
                WRITE RESTORING OLD ALLOCATION
                ALLOC F(&DDNAME) DA(&DSLIST) SHR REUSE
                EXIT CODE(4)
                END
          EXIT CODE(0)
          END
HELP: +
      WRITE FORMAT OF COMMAND IS :
      WRITE
      WRITE CONCAT DDNAME DSN BEFORE | AFTER
      WRITE
      WRITE DDNAME IS THE DDNAME TO CONCATENATE DSN TO. IF NOT
      WRITE ALREADY ALLOCATED THEN IT WILL BE ALLOCATED
      WRITE
      WRITE DSN IS THE DATASET NAME TO CONCATENATE/ALLOCATE TO
      WRITE DDNAME
      WRITE
      WRITE BEFORE SPECIFIES THAT THE DATASET SHOULD BE CONCATENATED
      WRITE IN FRONT OF ANY EXISTING DATASETS ALLOCATED TO THE DDNAME
      WRITE
      WRITE AFTER SPECIFIES THAT THE DATASET SHOULD BE CONCATENATED
      WRITE AFTER ANY EXISTING DATASETS ALLOCATED TO THE DDNAME
      WRITE
      EXIT CODE(4)


David E Ellis
Senior Systems Programmer
Al Rajai Banking and Investment Corp (Saudi Arabia)     c D E Ellis 1995

