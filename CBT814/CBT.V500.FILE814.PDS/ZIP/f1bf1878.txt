Closing and de-allocating catalogs

The following routine can be used to close and de-allocate all
open catalogs in use by a given job.  I have used it as part of a
TSO command to allow a user to de-allocate all open catalogs
in their TSO session as well as in a dynamic PROCLIB
modification used under JES2 to be able to force JES to de-
allocate catalogs.  Allocated catalogs mean allocated volumes
and, if you try to vary off-line a volume containing an open
catalog, allocation will not let you.  The solution, therefore, is
to de-allocate the catalog.

         XC    CATPARMS(CATPARML),CATPARMS CLEAR PARAMETER LIST
         LA    R1,CATFLAGS             LOAD A(FLAGS)
         ST    R1,CATFLAGP             STORE IN PARM LIST
         LA    R1,CATRETP2             LOAD A(A(RETCODE POINTER))
         ST    R1,CATRETP1             STORE IN PARM LIST
         LA    R1,CATRETCD             LOAD A(RETCODE POINTER)
         ST    R1,CATRETP2             STORE IN PARM LIST
         LA    R1,CATPCCB              LOAD A(PCCB POINTER)
* CATPCCB CONTAINS A FULLWORD OF ZEROES
         ST    R1,CATPCCBP             STORE IN PARM LIST
         ST    R1,CATDDNMP             STORE IN PARM LIST
         ST    R1,CATACBP              STORE IN PARM LIST
         LA    R1,CATJSCB              LOAD A(JSCB POINTER)
         ST    R1,CATJSCBP             STORE IN PARM LIST
         L     R1,=X'15000000'         SET CLOSE/DEALLOC ALL CATALOGS
         ST    R1,CATFLAGS             STORE IN PARM LIST
         L     R14,CVTPTR              LOAD A(CVT)
         L     R14,CVTTCBP-CVT(,R14)   LOAD A(TCB WORDS)
         L     R14,4(,R14)             LOAD A(TCB OLD)
         L     R15,TCBJSCB-TCB(,R14)   LOAD A(JSCB)
         ICM   R1,15,JSCBPCC-IEZJSCB(R15) LOAD A(PCCB)
         BZ    PPNOPRET                IF NO PCCB, THEN SKIP IT
         ST    R15,CATJSCB             STORE A(JSCB)
         XC    CATRETCD,CATRETCD       CLEAR RETCODE AREA
         LA    R1,CATPARMS             LOAD A(IEFAB4F4 PLIST)
         LINK  EP=IEFAB4F4             LINK TO CATALOG DEALLOCATION
         ICM   R15,15,CATRETCD         LOAD CAT DEALLOC RETURN CODE
         BZ    PPNOPRET                IF ZERO, THEN OK
         .                             ELSE, HANDLE ERROR
         .
         .
PPNOPRET EQU   *                       SUCCESSFUL OR NO PCCB FOUND
         .
         .
         .
CATPARMS DS    0F
CATFLAGP DS    F                       IEFAB4F4 A(FLAGS)
CATRETP1 DS    F                       IEFAB4F4 A(RETCODE ADDR)
CATPCCBP DS    F                       IEFAB4F4 A(PCCB ADDR)
CATDDNMP DS    F                       IEFAB4F4 A(DDNAME ADDR)
CATACBP  DS    F                       IEFAB4F4 A(ACB ADDR)
CATJSCBP DS    F                       IEFAB4F4 A(JSCB ADDR)
*
CATRETP2 DS    F                       IEFAB4F4 RETCODE ADDR
CATRETCD DS    F                       IEFAB4F4 RETCODE
CATPCCB  DS    F                       IEFAB4F4 PCCB ADDR
CATJSCB  DS    F                       IEFAB4F4 JSCB ADDR
CATFLAGS DS    F                       IEFAB4F4 FLAGS
CATPARML EQU   *-CATPARMS              PARAMETER LIST LENGTH
         ......
         IEFPCCB
         CVT  DSECT=YES
         IKJTCB
         IEZJSCB


                                                                  c Reserved 1991

