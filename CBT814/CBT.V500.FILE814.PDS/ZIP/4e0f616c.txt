Recovering PDS members

The following module produces a report showing the locations of
directory entries and non-member start positions, to help recover
member(s) in a PDS (see Figure 1).  It was developed after our
team received requests from users to get back a previous version
of members accidently overwritten in the early days of on-screen
editing.  If the library gets compressed or holes are reused by a
third-party package, this program is only useful for noting the
contents and location of members.  The program's output is only
valid for that instant and users should not make any changes to the
library involved until the required ZAPs have been made.

The program works by reading the whole file block by block.  For
each directory entry, the program notes the name, the TTR to the
data, and the CCHHR offset into the directory block.  At each
end-of-file marker, provided it is not the real end-of-file, the TTR
and 80 bytes from the next block are noted (the offset from where
data is copied is given in the PARM).  The extracted data is then
sorted into TTR order and the report is produced.  To get back to
a previous version of a module, note the CCHHR of the directory
block involved, and the offset for the TTR field (add 8 to that
given for the member), then ZAP in the old TTR.

Another use is to keep a copy of a previous member.  This
requires that a gash directory entry is created (use the TSO
'rename alias' facility), then obtain the report and proceed as
before.

PDSHOLES TITLE 'FIND PSD HOLES'
*   THIS PROGRAM PRODUCES A LIST OF MEMBERS AND 80 BYTES FROM
*      THE 1ST BLOCK AFTER AN EOF IS DECTECTED.
*   THIS WILL AID IN FINDING A MISSUPDATED MEMBER
*   JCL-
*   //SYSPRINT                    MESSAGE D/S
*   //SYSLIB                      PDS TO PROCESS
*  PARM-
*      OFFSET INTO BLOCK OF DATA TO PRINT DEFAULT 0
*         FOR MACROS USE PARM=80 FOR PROTOTYPE STATEMENT
*  OUTPUT:
*         TTR / MEMBER /DATA
*         WHERE TTR IS TTR OF DATA
*               MEMBER IS NAME ASSOCIATED WITH THIS TTR
*               DATA IS
*                     A) ON MEMBER NAME LINE 'CCHHR/OFFSET' OF
DIRECTORY
*                               BLOCK FOR THIS MEMBERS ENTRY
*                     B) DATA FOR PARM OFFSET OF 1ST BLOCK AFTER AN
*                                                EOF
*         NOTE A TTR VALUE WITH DATA IS POSN OF UNMEMBERED
DATA(OLD
*         DATA) AND TTR CAN BE ZAPPED TO MEMBER NAME CCHHR
OFFSET + 8
*            LOCAL VARIABLES
         LCLA  &BUFNO
&BUFNO SETA 2500                    NUMBER OF ENTRIES ALLOWED
SCX00S   CSECT
         USING *,R15
PDSHOLES SAVE  (14,12),,*
         ENTRY PDSHOLES
         LR    R12,R13 .               HOLD OLD SAVE AREA
         CNOP  0,4 .
         ST    R13,PDLSAVE+4 .         PRESERVE
         BAL   R13,PDLSTAR .           NEW BASE REG
         USING *,R13
PDLSAVE  DC    18F'0' .
PDLSTAR  ST    R13,8(,R12) .           BACK LINKAGE
         DROP  R15
*      EXTRACT  PARM DATA
         LH    R2,ALLONES
         L     R3,0(,R1)               PARM POINTER
         AH    R2,0(R3)                HOW LONG
         BM    PDL002                  NOWT
         EX    R2,PDLPACK              CONVERT
         CVB   R2,PDLWORK                OFFSET REQUESTED
         STH   R2,PDLWORK                   TO BINARY
         OC    PDLFROM(2),PDLWORK            THEN MODIFY EXTRACT INST .
*        OPEN & CHECK FILES
PDL002   OPEN  ,MF=(E,PDLOPEN1)
         TM    PDLPRINT+DCBOFLGS-IHADCB,DCBOFOPN PRINTER OPEN
         BZ    PDLABORT                 NO FAIL PROG
         TM    PDLRFILE+DCBOFLGS-IHADCB,DCBOFOPN PDS OPEN
         BZ    PDLOMMIT                 NO FAIL PROG
         SR    R2,R2                      CLEAR R.C. REG
         RDJFCB ,MF=(E,PDLOPEN2)        READ JFCB
         OBTAIN DSCBREAD
         LTR   R2,R15             OK?
         BNZ   PDLOFAIL           NO
         SR    R3,R3
         ICM   R3,14,DSCBTTRL     GET LAST TTR
         SL    R3,=A(X'100')      -1 RECORD
*      GET CORE FOR TABLE 1,000 X 104 BYTES
         USING TAB,R8
         USING MEMB,R9
         GETMAIN EC,MF=(E,PDLCORE1)   BUFFER
         LTR   R15,R15                   OK
         BNZ   PDLCFAIL               NO GOOD REPORT
         L     R8,PDLHOLD              YES PICK UP START
         ST    R8,PDLTABLE             AND POINT OUR BASE
         LH    R10,PDLRFILE+DCBBLKSI-IHADCB BLOCK SIZE IN READ
         STH   R10,PDLRFILE+DCBLRECL-IHADCB LRECL READ
         L     R12,PDLCORE1             PICK UP BUFFER SIZE
         SR    R12,R10             LOSE THAT FROM BUFFER LEN
         AR    R12,R8              LOCATE POSN OF DATA BUFFER
         ST    R12,PDLBLOCK
         LA    R0,=F'1'            START OF FILE POINTER
         POINT PDLRFILE,(0)
*       READ   IN DIRECTORY BUILDING NAME TABLE
PDL003   BAL   R11,PDLINPUT              GET BLOCK
         LA    R9,2(,R12)                   POINT TO FIRST ENTRY.
         LH    R7,0(,R12)
         LA    R7,0(R7,R12)                  POINT R7 TO END OF BLOCK
PDL004   CLC   MEMBNAME,ALLONES          END ENTRY
         BE    PDL010                    YES
         MVC   TABNAME,MEMBNAME    EXTRACT MEMBER NAME
         MVC   TABTTR,MEMBTTR      AND START TTR
         LA    R0,MEMBNAME+8      ALLOW FOR KEY LEN ON DISP
         SR    R0,R12              NOW CALCULATE
         STC   R0,TABDATA+5        OFFSET INTO DIRECTORY BLOCK
         MVC   TABDATA(5),PDLRFILE+8  COPY CCHHR OF BLOCK
         TM    MEMBFLAG,ALIAS            IS ALIAS
         BZ    PDL005                   NO
         OI    TABFLAG,ALFLAG           SET SPECIAL FLAG
PDL005   LA    R0,TABDATA+6              STEP TO NEXT TABLE POSN.
         ST    R0,TABLINK          SAVE LINK
         LR    R8,R0               POINT TO NEXT
         CR    R8,R12                 FULL-UP
         BL    PDL006              NO
         B     PDLOVFLW                 YES REPORT
*        STEP UP DIRECTORY ENTRY
PDL006   SR    R4,R4
         LA    R5,31                    'AND' MASK FOR USER COUNT ONLY
         IC    R4,MEMBFLAG              EXTRACT COUNTS
         NR    R4,R5                     TURN OFF ALIAS BIT IF ON
         SLL   R4,1                     DOUBLE FOR USER COUNT
         LA    R9,MEMBNEXT(R4)           NOW POINT TO NEXT ENTRY
         CR    R9,R7                     AT END
         BL    PDL004                   NO CONTINUE WITH THIS BLOCK
         BE    PDL003                   NEW BLOCK
* ERROR
PDL009   MVC   PDLCONTL(22),=C'- DIRECTORY ERROR SCAN'
         B     PDLXWRIT                 TO ERROR MSG RTN
*        EOF ROUTINE
PDLREOF  OI    PDLFLAG,EOFED       WE'VE HIT AN EOF
         NOTE  PDLRFILE            WHERE?
         CR    R1,R3               PAST END
         BNL   PDL015              YES ALL DONE
*        STANDARD READ BLOCK
PDL010   BAL   R11,PDLINPUT        USED IN NON DIRECTORY READ
PDLINPUT READ  PDLREAD,SF,,,'S',MF=E
         CHECK PDLREAD             FINISH OR MAKE EODAD
         CLI   PDLREAD,X'7F'       OK
         BE    PDL011              YES
         PUT   PDLPRINT,ERRORBUF+3 ERROR MSG
         ABEND 1111,DUMP           AND QUIT IN ANGER
PDL011   NOTE  PDLRFILE            WHERE WAS WE
         CR    R1,R3               NOT AT EOF
         BH    PDL015              YES WE WAS
         TM    PDLFLAG,EOFED       LAST END OF FILE
         BZR   R11                 NO
         ST    R1,TABTTR           SAVE START
         OI    TABFLAG,DATAFLAG    THIS IS A DATA RECORD
*        THIS  FOLLOWING INSTRUCTION IS MODIFIED FOR EXTRACT POS
         MVC   TABDATA,0(R12)      GET FIRST RECORD
PDLFROM  EQU   *-2
         XI    PDLFLAG,EOFED       CLEAR EOF BEEN HIT
         MVC   TABNAME,=8C' '      NO MODULE NAME IN TABLE
         LA    R0,TABNEXT          STEP ON
         ST    R0,TABLINK          SAVE LINK
         LR    R8,R0               POINT TO NEXT
         CR    R8,R12                 FULL-UP
         BH    PDLOVFLW                 YES REPORT
         CR    R1,R3               WAS WE AT "EOF" TTR
         BLR   R11                 NO CARRY ON
*      LAST ENTRY REACHED  SORT INTO DISK ORDER
PDL015   MVC   TABTTR(TABSIZE),ALLONES    END MARKER
         XC    TABLINK,TABLINK     CLEAR LINK
*        PASS  SPM39TAG                 SORT TABLE IN TTR ORDER
         LA    R1,PDLTABLE               CONTROL FOR SORT
         L     R15,=V(SPM39TAG) LOAD ROUTINE ADDR
         BALR  R14,R15             PASS CONTROL
*      FILL IN REPORT  HEADER LAYOUT
         L     R8,PDLTABLE            BASE REGISTERS
         SR    R4,R4                   CLEAR WORKER
         MVC   HDRDSN,JFCB             TO EXTRACT DSN
         MVC   HDRVOL,JFCB+118         AND VOLUME FOR PAGE HEADER
*        PASS  SPM09DAT,(HDRDATE)     FILL IN DATE TO PAGE HEADER
         L     R15,=V(SPM09DAT) LOAD ROUTINE ADDR
         LA    R1,=A(HDRDATE)  LOAD PARAMETER LIST ADDRESS
         BALR  R14,R15             PASS CONTROL
*        PRODUCE HEADER
PDL016   UNPK  HDRPAGE,PAGENUMB         EXTRACT PGNO
         OI    HDRPAGE+1,240            GOOD SIGN
         AP    PAGENUMB,=P'1'           UP PAGE NUMBER
         ZAP   LINENUMB,=P'50'          FIX PAGE LENGTH
         PUT   PDLPRINT,HDRLINE         PRINT HDR
         PUT   PDLPRINT,SUBTITLE          AND COLUMN HEADINGS
         OI    PDLCONTL,C'0'            SET SKIP CONTROL
*        PRODUCE DETAIL LINE
PDL017   TM    TABFLAG,255              TEST FLAG BYTE
         BO    PDLFREE                  LAST ENTRY
         MVC   PDLNAME,TABNAME          SET UP ENTRY NAME
         CLM   R4,B'1110',TABTTR        SAME TTR
         BE    PDL018                   YES
         ICM   R4,14,TABTTR             FOR NEXT TIME
         UNPK  PDLTRACK(7),TABTTR(4)    CONVERT "TTR"
         TR    PDLTRACK(6),PDLXTABL-240 TO PRINTABLE
         MVI   PDLTRACK+6,64             HEX
PDL018   TM    TABFLAG,ALFLAG            AN ALIAS
         BZ    PDL020                    NO
         MVI   PDLDDATA,C'A'             SAY SO
         B     PDL021                    SKIP TO EXTRACT CCHHR/OFFSET
PDL020   TM    TABFLAG,DATAFLAG          DATA ITEM
         BZ    PDL021                    NO MUST BE DIRECTORY DATA
         MVC   PDLDDATA,TABDATA          COPY DATA TO PRINT LINE
         B     PDL022                    SKIP OVER TO PRINT BIT
PDL021   UNPK  PDLDDATA+2(13),TABDATA(7) CONVERT CCHHR
         TR    PDLDDATA+2(12),PDLXTABL-240 TO HEX
         ICM   R0,3,PDLDDATA+12          COLLECT OFFSET
         MVI   PDLDDATA+12,C'/'          SLIP IN SEPARATOR
         STCM  R0,3,PDLDDATA+13          RESTORE OFFSET
PDL022   L     R8,TABLINK                NEXT ITEM
         PUT   PDLPRINT,PDLCONTL        PRINT LINE
         MVC   PDLCONTL(121),PDLCONTL-1  BLANK OUT PRINT LINE
         SP    LINENUMB,=P'1'           REDUCE LINE COUNT
         BP    PDL017                   OK NEXT WILL FIT
         TM    TABFLAG,255              TEST FLAG
         BNO   PDL016                   NEW PAGE FIRST
PDLFREE  FREEMAIN E,MF=(E,PDLCORE1)
PDLCLOSE CLOSE   ,MF=(E,PDLOPEN1)        CLOSE DOWN
         L     R13,4(,R13)
         LR    R15,R2                    EXTRACT R.C.
         L     R14,12(,R13)
         LM    R0,R12,24(R13)            RETURN ALL REGS
         BR    R14                     WAY OUT


The remaining source code will be published in next month's
issue.

Jeffrey Alan Groves
Software Co-ordinator
Thomson Holidays (UK) © Thomson Holidays 1989

