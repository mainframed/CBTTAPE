The CP serial number in the PCCA


CPUSER SOURCE CODE

CPUSER    CSECT
HOUS_KEEP DS      0H                          LABEL HOUS_KEEP
          STM     R14,R12,12(R13)             STORE PAUSES REGS
          LR      R12,R15                     ESTABLISH BASE
          USING   CPUSER,R12                  ESTABLISH ADDRESSBLTY
          LA      R11,SAVEAREA                POINT TO OUR SAVE
          ST      R13,4(R11)                  SAVE CALLERS SAVE POINTER
          ST      R11,8(R13)                  SET FORWARD POINTER
          LR      R13,R11                     ADDRESS OF CURRENT SAVE
PRMLST    DS      0H
          L       R2,=F'16'                   LOAD OFFSET TO CVT
          L       R3,0(R2)                    LOAD ADDRESS OF CVT
          USING   CVT,R3                      MAP CVT
          L       R4,CVTPCCAT                 LOAD PCCAT ADDRESS
          SR      R6,R6                       INIT R6
          LA      R6,16(R6)                   LOAD MAX CP NUMBER
SCAN      C       R6,=F'00'                   COUNT AT ZERO?
          BE      SUMMARY                     YES, COMPLETE
          BCTR    R6,R0                       DECREMENT COUNTER
          L       R8,0(R4)                    LOAD PCCA ADDRESS
          C       R8,=F'00'                   IS CP INSTALLED?
          BNE     CONT                        NO, WRITE INFO
          LA      R4,4(R4)                    BUMP TO NEXT PCCA
          B       SCAN                        GOTO SCAN
CONT      MVC     SER_NUM+5(1),6(R8)          MOVE IN CP #
          MVC     SER_NUM+24(8),4(R8)         MOVE IN SERIAL #
          TPUT    SER_NUM,L'SER_NUM           DISPLAY IT
          LA      R4,4(R4)                    BUMP TO NEXT PCCA
          B       SCAN                        GOTO SCAN
** COMPLETE
SUMMARY   TPUT    SUMM1,L'SUMM1               GENERATE SUMMARY
          TPUT    BLANK,L'BLANK               "
          TPUT    SUMM2,L'SUMM2               "
          TPUT    SUMM3,L'SUMM3               "
          TPUT    SUMM4,L'SUMM4               "
          TPUT    SUMM5,L'SUMM5               "
RETURN    L       R13,SAVEAREA+4              RESTORE SAVE POINTER
          SR      R15,R15                     ZERO COND CODE
          RETURN  (14,12),RC=(15)             RETURN
SER_NUM   DC      C' CP #X SERIAL NUMBER IS XXXXXXXX'
SUMM1     DC      C'                        VVLPSSSS'
BLANK     DC      C'                 WHERE: '
SUMM2     DC      C'                        VV= HARDWARE VERSION'
SUMM3     DC      C'                         L= LPAR NUMBER'
SUMM4     DC      C'                         P= PR/SM PART NUMBER'
SUMM5     DC      C'                      SSSS= STATIC PORTION'
ZERO      DS      3F
SAVEAREA  DS      18A
* MAPS
          CVT     DSECT=YES
          IHAPCCAT
          END


             c Xephon 1996

