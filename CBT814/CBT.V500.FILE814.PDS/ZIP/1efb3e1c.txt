Migration levels of DFHSM-migrated datasets

THE MLVL REXX EXEC

/* REXX MLVL   OBTAINS HSM MIGRATION LEVEL INFORMATION FROM CATALOG */
/*              RETURN CODE = 0 IF DATASET ON LEVEL 0               */
/*              RETURN CODE = 1 IF DATASET ON LEVEL 1               */
/*              RETURN CODE = 2 IF DATASET ON LEVEL 2               */
/*                              AND LEVEL 2 IS TAPE                 */
/*              RETURN CODE = 7 IF DATASET IS A TAPE DATASET OR     */
/*                              IT IS A GDG BASE                    */
/*     MLVL COMMAND CAN BE USED AS A LINE COMMAND IN ISPF 3.4       */
/*     DATASET LIST -DISPLAY                                        */
/*     TRACE R                                                      */
/********************************************************************/
/***** DATASET NAME IS THE ONLY ARGUMENT                        *****/
ARG DSN .
VOLFND = 0
MIGRATE = 0
/***** USE OUTTRAP FUNCTION TO CAPTURE LISTCAT OUTPUT           *****/
Z = OUTTRAP("MUC.",'*',"NOCONCAT")
/***** ISSUE LISTCAT COMMAND                                    *****/
"LISTC ENT("DSN") ALL"
/***** EXAMINE LISTCAT OUTPUT IF NUMBER OF LINES GT 0           *****/
IF MUC.0 > 0 THEN
  DO
/*SAY MUC.10 */
POS7  = INDEX(MUC.7,"VOLSER------------")
POS10 = INDEX(MUC.10,"VOLSER------------")
POS11 = INDEX(MUC.11,"VOLSER------------")
SELECT
/***** SEARCH KEYWORD VOLSER FROM LINE 10 (SMS DATASET)         *****/
  WHEN POS10 ^= 0 THEN
    DO
    IND = 10
    POS = POS10
    VOLFND = VOLFND + 1
    END
/***** SEARCH KEYWORD VOLSER FROM LINE 7 (NON-SMS DATASET)      *****/
  WHEN POS7 ^= 0 THEN
    DO
    IND = 7
    POS = POS7
    VOLFND = VOLFND + 1
    END
/***** MIGRATED ACTIVE GENERATION DATASETS                      *****/
  WHEN POS11 ^= 0 THEN
    DO
    IND = 11
    POS = POS11
    VOLFND = VOLFND + 1
    END
  OTHERWISE
    DO
    END
END
/***** VOLUME INFORMATION WAS FOUND                             *****/
  IF VOLFND > 0 THEN
    DO
    IF SUBSTR(MUC.IND,POS+18,6) = "MIGRAT" THEN
      DO
      MIGRATE = 1
      END
    END
/***** IF VOLSER IS NOT MIGRAT THEN EXIT WITH RC 0              *****/
/***** DATASET IS NOT MIGRATED BY DFHSM                         *****/
  ELSE
    DO
    IF SUBSTR(MUC.1,1,7) = "CLUSTER" | ,
       SUBSTR(MUC.1,1,5) = "DATA "   | ,
       SUBSTR(MUC.1,1,6) = "INDEX "  THEN
      DO
      EXIT 0
      END
/***** IF VOLSER NOT FOUND EXIT WITH RC 7                       *****/
/***** LISTCAT COMMAND FAILED OR INFORMATION DO NOT BELONG      *****/
/***** TO A REAL DATASET                                        *****/
    ELSE
      DO
      EXIT 7
      END
    END
/***** KEYWORD DEVTYPE MUST BE FOUND ON THE SAME LINE           *****/
  ROS = INDEX(MUC.IND,"DEVTYPE------X'",POS+18)
  IF POS ^= 0 THEN
    DO
/***** DEVTYPE BEGINNING WITH CLASS "20" BELONGS TO DASD UNIT   *****/
    IF SUBSTR(MUC.IND,ROS+19,2) = "20" THEN
      DO
      IF MIGRATE ^= 0 THEN
        DO
        EXIT 1
        END
/***** IF MIGRATE INDICATOR IS NOT ON, THEN EXIT WITH RC 0      *****/
/***** DATASET IS NOT MIGRATED BY DFHSM                         *****/
      ELSE
        DO
        EXIT 0
        END
      END
/***** DEVTYPE BEGINNING WITH CLASS "80" BELONGS TO TAPE UNIT   *****/
    IF SUBSTR(MUC.IND,ROS+19,2) = "80" THEN
      DO
      IF MIGRATE ^= 0 THEN
        DO
        EXIT 2
        END
/***** IF MIGRATE INDICATOR IS NOT ON, THEN EXIT WITH RC 7      *****/
/***** DATASET IS A TAPE DATASET                                *****/
      ELSE
        DO
        EXIT 7
        END
      END
/***** OTHER "PREFIXES" ARE IGNORED, RETURN WITH RC 7           *****/
    ELSE
      DO
      EXIT 7
      END
    END
  ELSE
    DO
    EXIT 7
    END
  EXIT 7
  END
EXIT 7


Utility Programmer (Finland)                                               c Xephon 1996

