Easier accessing of PDS members

THE PROBLEM

Many dialog manager (ISPF) applications perform processing on
partitioned datasets (BPAM organisation).  In order to make a
user-friendly interface, the contents of the dataset index could be
displayed as a table, out of which the required member could be
selected (ISPF-like).  In addition, this table could be displayed in
standard ISPF format.  Unfortunately, there is no standard method
of directly obtaining the members of a partitioned dataset as the
FIND macro can only be used to locate a known member; the
index must be processed as a sequential dataset containing
unblocked records.

THE SOLUTION

The program, POINDEX, builds a standard temporary ISPF table
which can then be displayed (using the ISPF TBDISPL function)
and from which selections can be made.  The table has three
columns: MEMNAME, NEWNAME, and SPFINFO, containing
the member name, blanks, and the IPSF statistics, respectively.

SPINFO format

Displacement Length Description
    +0   2 version number
    +3   2 modification level
    +7   8 date created (yy/mm/dd)
    +17  8 date last modified (yy/mm/dd)
    +26  5 time last modified (hh:mm)
    +32  5 current size
    +38  5 initial size
    +44  5 number of modifications
    +50  7 id of user who last modified the
member

ISPF status information format

Displacement Length Description
    +0   8 member name
    +8   3 TTR address of associated data (hex)
    +11  1 length (in halfwords) of user data
    +12  1 version number (binary)
    +13  1 modification level (binary)
    +14  3 reserved
    +17  3 date created (X'yydddF')
    +20  1 reserved
    +21  3 date last modified (X'yydddF')
    +24  2 time last modified (hhmm)
    +26  2 current size (binary)
    +28  2 initial size (binary)
    +30  2 number of modifications (binary)
    +32  8 id of user who last modified the
member
    +40  2 reserved

HOW THE PROGRAM IS USED

The POINDEX program may be used only in the ISPF
environment.  It is called like this:

    ISPEXEC SELECT PGM(POINDEX) [PARM(tablename|TAB)]

where tablename is the name of the ISPF table to be created, the
default name is TAB.  The partitioned dataset to be processed is
defined by file DDNAME INDXPOIN.

The return code can either be zero, indicating normal completion,
or eight, indicating that the program parameter is too long (ie
more than 8 characters).

SAMPLE CLIST

ALLOC F(INDXPOIN) DA(datasetname) SHR REUS
ISPEXEC SELECT PGM(POINDEX) /* call program
SET &PAN = &STR(PANEL(panelname))  /* initialise panelname
SET &RC = 4            /* set loop control code
DO WHILE &RC LT 8      /* loop while selection pending
  ISPEXEC TBDISPL TAB &PAN   /* display and select
  SET &RC = &LASTCC    /* selection condition
  SET &PAN = &STR()    /* subsequent display
  IF &LASTCC LT 8 THEN DO    /* not END
    ...
  END
END

Subprograms used

The POINDEX program makes use of a general subroutine called
FINDPDSX.  This subroutine reads the index of the partitioned
dataset sequentially and returns the member name and the user
information pertaining to this member.  The subroutine has the
following call sequence:

    CALL FINDPDSX,(ddname,membername),VL

where ddname is the DDNAME used to define the partitioned
dataset (this DDNAME should not be used for BPAM processing
of this dataset) and membername is used to return the name of the
next member in the sequence together with its user data (if
present).

The program PDSCLEAR, described in MVS Update issue 21
(June 1988), could be used as the basis for the FINDPDSX
subroutine; however, the individual members must return for each
call rather than be stored in a table.

         TITLE 'SPF DIALOG MANAGER - PO-INDEX AS SPF-TABLE'
* PROGRAM-NAME   :   POINDEX            (ALIAS)
* FUNCTION       :   CREATION OF A TEMPORARY SPF-TABLE FROM A
*                    PO-DATASET INDEX.
* PROGRAM CONTENT:   PO-DATASET (DDNAME: INDXPOIN) INDEX IS READ
*                    SEQUENTIALLY. THE ENTRIES ARE FORMATTED IN SPF-
*                    FORMAT AND WRITTEN INTO THE SPF-TABLE (STANDARD-
*                    NAME 'TAB', BUT THE TABLE-NAME MAY BE SPECIFIED
*                    AS AN EXEC-PARAMETER.
*                    THE SPF-TABLE ENTRY CONTAINS THE FOLLOWING FIELDS:
*                     <OP> - BLANK
*                     <MEMNAME> - MEMBER-NAME IN PO-DATASET
*                     <NEWNAME> - BLANK
*                     <SPFINFO> - SPF-INFORMATION
* CALL           :   EXEC POINDEX<,PARM='TABLENAME'>
* PARAMETER: TABLENAME: NAME OF THE SPF-TABLE (1-8 CHARACTERS),
*                       OPTIONAL (DEFAULT TAB)
* DDNAME:    INDXPOIN - PO-DATASET
* RETURNCODE:  0 = OK
*              8 = NOK, EXEC-PARAMETER TOO LONG
POINDEX  CSECT
         STM   R14,R12,12(R13)           save registers
         BALR  R12,0                     load base register R12
         USING *,R12
         LA    R15,SA                    A(save-area)
         ST    R13,4(R15)                backward ptr
         ST    R15,8(R13)                forward ptr
         LR    R13,R15                   A(new save-area)
         B     SA_END                    jump over save-area
SA       DS    18A                       save-area
SA_END   DS    0H
         LA    R15,8               PRE-LOAD RETURNCODE (IF ERROR)
         L     R2,0(R1)            A(EXEC-PARAMETER)
         LH    R1,0(R2)            L(EXEC-PARAMETER)
         SH    R1,=H'1'            LC(EXEC-PARAMETER)
         BM    A100                NO PARAMETER
         CH    R1,=AL2(L'PARM)
         BNL   EOJ                 PARAMETER LENGTH-ERROR
         MVC   PARM,PARM-1         BLANK(<PARM>)
         MVC   PARM(0),2(R2)       TRANSFER PARM
         EX    R1,*-6
A100     LOAD  EP=ISPLINK
         ST    R0,AISPLINK
         L     R15,AISPLINK
         CALL  (15),(TBCREATE,TABNAME,,NL,NOWRITE,REPLACE),VL
A110     BAL   R14,GETINDX         GET PO-INDEX ENTRY
* R15: RETURN-CODE - 0 = OK
         LTR   R15,R15
         BNZ   A200                EOF-INDEX
* ELSE ENTRY READ AND FORMATTED - OP (=BLANK)
         MVC   VN,=CL8'OP'
         MVC   VL,=AL4(L'BLANK)
         L     R15,AISPLINK
         CALL  (15),(VREPLACE,VN,VL,BLANK),VL
* MEMBERNAME - MEMNAME
         MVC   VN,=CL8'MEMNAME'
         MVC   VL,=AL4(L'MEMNAME)
         L     R15,AISPLINK
         CALL  (15),(VREPLACE,VN,VL,MEMNAME),VL
* NEWNAME (=BLANK)
         MVC   VN,=CL8'NEWNAME'
         MVC   VL,=AL4(L'BLANK)
         L     R15,AISPLINK
         CALL  (15),(VREPLACE,VN,VL,BLANK),VL
* SPF-INFO (FORMATTED) - FDATA
         MVC   VN,=CL8'SPFINFO'
         MVC   VL,=AL4(LDATA)
         L     R15,AISPLINK
         CALL  (15),(VREPLACE,VN,VL,FDATA),VL
* WRITE TABLE ENTRY
         L     R15,AISPLINK
         CALL  (15),(TBADD,TABNAME),VL
         B     A110
* POSITION AT TABLE-TOP
A200     L     R15,AISPLINK
         CALL  (15),(TBTOP,TABNAME),VL
         LA    R15,0                     returncode (0 = ok)
EOJ      L     R13,4(R13)                restore A(old save-area)
         RETURN (14,12),RC=(15)
         TITLE 'EXTRACT PDS-INDEX'
* PROGRAM TO READ THE INDEX OF A PO-FILE AND TO FORMAT THE ENTRIES
* (SPF-FORMAT)
* INPUT (PO-FILE): INDXPOIN (READ AS PS).
         DC    A(0)
GETINDX  ST    R14,GETINDX-4
* GET PO-INDEX ENTRY
* R15: RETURN-CODE - 0 = OK - 4 = INDEX-END
         MVC   MEMBER,MEMBER-1 CLEAR
* GET INDEX ENTRY
         CALL  FINDPDSX,(DDNAME,MEMBER),VL
* R15: RETURN-CODE - 0 = OK - 4 = INDEX-END
         LTR   R15,R15
         BNZ   D110
* MEMBER NAME
         MVC   FNAME,MEMNAME
         CLC   MEMBLEN,=HL1'15'
         BNE   DA110                     NOT SPF-FORMAT
* FORMAT OUTPUT (SPF-FORMAT)
         MVC   FID,SPFID
         SR    R0,R0
         IC    R0,SPFVM+0                VERSION-NO.
         CVD   R0,D
         UNPK  FVM+0(2),D
         OC    FVM+0(2),=2C'0'
         MVI   FVM+2,C'.'
         IC    R0,SPFVM+1                MOD.-NO.
         CVD   R0,D
         UNPK  FVM+3(2),D
         OC    FVM+3(2),=2C'0'
         LH    R0,SPFSIZE                SIZE
         CVD   R0,D
         MVC   FSIZE,=X'402020202120'
         ED    FSIZE,D+5
         LH    R0,SPFINIT                INITIAL SIZE
         CVD   R0,D
         MVC   FINIT,=X'402020202120'
         ED    FINIT,D+5
         LH    R0,SPFMOD                 NO. OF MODIFICATIONS
         CVD   R0,D
         MVC   FMOD,=X'402020202120'
         ED    FMOD,D+5
         LA    R1,SPFCDATE
         BAL   R14,CVDATE                CONVERT DATE FORMAT
*R15: ADDRESS OF RESULT FIELD
         MVC   FCDATE,0(R15)
         LA    R1,SPFMDATE
         BAL   R14,CVDATE                CONVERT DATE FORMAT
*R15:    ADDRESS OF RESULT FIELD
         MVC   FMDATE,0(R15)
         MVC   FMTIME,=X'F021207A2020'
         ED    FMTIME,SPFMTIME
         MVI   FMTIME,C' '               REPLACE LEADING 0
DA110    LA    R15,0                     RETURN-CODE
D110     L     R14,GETINDX-4
         BR    R14                       RETURN
DDNAME   DC    CL8'INDXPOIN'
         DS    A
CVDATE   ST    R14,CVDATE-4
* R1: ADDRESS OF INPUT (P'YYDDDS')
* R15: RETURNED WITH ADDRESS OF RESULT FIELD
         ZAP   D,=P'0'
         MVO   D(8),0(1,R1)
* <D> = 00 00 00 00 00 00 0Y YC
         DP    D,=P'4'
         CP    D+7(1),=P'0'              REMAINDER
         BNE   *+10
         MVC   MONTAB+2(2),=P'29'        FEB
         ZAP   D,1(2,R1)                 DDDS
         LA    R15,MONTAB
         ZAP   MON,=P'1'
CVDATE2  CP    D,0(2,R15)
         BNH   CVDATE1
         SP    D,0(2,R15)
         LA    R15,2(R15)
         AP    MON,=P'1'
         B     CVDATE2
* <D>: 00 00 00 00 00 00 0D DC
* <MON>: 0M MC
CVDATE1  LH    R15,D+6                  * R15: 00 00 0D DC
         SRL   R15,4                    * R15: 00 00 00 DD
         LH    R14,MON                 * R14: 00 00 0M MC
         SRL   R14,4                   * R14: 00 00 00 MM
         SLL   R15,24                  * R15: DD 00 00 00
         SLDL  R14,8                   * R14: 00 00 MM DD
         ICM   R14,B'0100',0(R1)       * R14: 00 YY MM DD
         STCM  R14,B'0111',D           * <D>: YY MM DD ......
         MVC   EDFLD,=X'F02120612020612020'
         ED    EDFLD,D
         LA    R15,EDFLD+1
         L     R14,CVDATE-4
         BR    R14
EDFLD    DS    C' YY/MM/DD'
MON      DS    P'01'
MONTAB   DC    P'31,28,31,30,31,30,31,31,30,31,30,31'
D        DS    PL8
* DIALOG-MANAGER VARIABLES
NOWRITE  DC    CL8'NOWRITE'
REPLACE  DC    CL8'REPLACE'
VREPLACE DC    CL8'VREPLACE'
TBCREATE DC    CL8'TBCREATE'
TBADD    DC    CL8'TBADD'
TBTOP    DC    CL8'TBTOP'
BLANK    DC    C' '                      BLANK-CHARACTER
VN       DC    CL8' '
VL       DC    AL4(0)
NL       DC    C'(OP,MEMNAME,NEWNAME,SPFINFO)'
OP       DC    C' '
NEWNAME  DC    CL8' '
* SPF FORMAT
         DC    C' '                      CLEAR-BYTE
MEMBER   DS    CL256
         ORG   MEMBER
MEMNAME  DS    CL8
MEMBTTR  DS    XL3
MEMBLEN  DS    HL1
SPFVM    DS    2XL1
         DS    XL3
SPFCDATE DS    PL3
         DS    X
SPFMDATE DS    PL3
SPFMTIME DS    PL2
SPFSIZE  DS    HL2
SPFINIT  DS    HL2
SPFMOD   DS    HL2
SPFID    DS    CL7
         ORG
FFMT     DC    CL80' '
* SPF OUTPUT FORMAT
         ORG   FFMT
FNAME    DS    CL8
         DS    CL11
FDATA    DS    0C
FVM      DS    C'00.00'
         DS    CL2
FCDATE   DS    C'00/00/00'
         DS    CL2
FMDATE   DS    C'00/00/00'
FMTIME   DS    C' 00:00'
FSIZE    DS    C' 00000'
FINIT    DS    C' 00000'
FMOD     DS    C' 00000'
         DS    C
FID      DS    CL7
FLEN     EQU   *-FFMT
LDATA    EQU   *-FDATA
         ORG
         LTORG
         DC    C' '                      CLEAR-BYTE
PARM     DC    CL8'TAB'
TABNAME  EQU   PARM
AISPLINK DC    AL4(0)                    A(ISPLINK)
         END


A S Rudd (West Germany) © A S Rudd 1989

