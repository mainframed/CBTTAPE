Variables dump

INTRODUCTION

The following program repeatedly accesses all the active CLIST
variables and displays them on the screen.  It is basically a dump
program which looks at all the variables defined and their values
without introducing a lot of CLIST WRITE statements.


THE PROGRAM

VDUMP    CSECT
         STM   R14,R12,12(R13)     *SAVE REGS
         USING VDUMP,R12           *USE 12 AS BASE REGISTER
         LR    R12,R15
         LA    R2,SAVE             *GET ADDR OF SAVE AREA
         ST    R2,8(R13)           *CHAIN FORWARD
         ST    R13,4(R2)           *CHAIN BACKWARD
         LR    R13,R2              *SET SAVE AREA PTR
         B     BEGPROG             *BYPASS EYE CATCHER
         DC    C'VDUMP  ASSEMBLED ON :&SYSDATE TIME:&SYSTIME'
BEGPROG  L     R15,CVTPTR          *POINT TO CVTPTR
         L     R15,CVTTVT(R15)     *GET TSO VECTOR TABLE
         L     R15,TSVTVACC-TSVTTSVT(R15)   *VARIABLE ACCESS ROUTINE
         LTR   R15,R15             *IS IT PRESENT?
         BZ    LINKRTN             *NO THEN CALL IKJCT441
         CALL  (15),               *CALL VARIABLE ACCESS ROUTINE       *
               (FUNCT,             *FUNCTION = LOCATE                  *
               NAMEADD,            *VARIABLE NAME ADDRESS -RETURNED    *
               NAMELEN,            *LENGTH OF THE NAME    -RETURNED    *
               VALADD,             *VALUE OF VARIABLE     -RETURNED    *
               VALLEN,             *VALUE LENGTH          -RETURNED    *
               TOKEN),VL           *TOKEN & SET HIGH BIT ON
         B     CHKRETCD            *GO & CHECK THE RETURN CODE
LINKRTN  LINK  EP=IKJCT441,        *LINK TO THE REQUIRED ROUTINE       *
               PARAM=(FUNCT,       *FUNCTION = LOCATE
               NAMEADD,            *VARIABLE NAME ADDRESS -RETURNED    *
               NAMELEN,            *LENGTH OF THE NAME    -RETURNED    *
               VALADD,             *VALUE OF VARIABLE     -RETURNED    *
               VALLEN,             *VALUE LENGTH          -RETURNED    *
               TOKEN),VL           *TOKEN & SET HIGH BIT ON
CHKRETCD C     R15,ENDVARS         *END OF VARIABLES ?
         BE    RETURN
         LTR   R15,R15             *IN CASE OF ANY ERRORS ALSO
         BNZ   RETURN              *RETURN BACK
DISPVAR  EQU   *                   *DISPLAY THE VARIABLES
         MVI   NAME,C' '           *BLANK OUT DISPLAY AREA FIRST
         MVC   NAME+1(18),NAME
         MVI   VALUE,C' '
         MVC   VALUE+1(59),VALUE
         L     R3,NAMEADD
         L     R2,NAMELEN          *ONLY THE FIRST 19 CHARACTERS OF
         C     R2,=F'19'           *THE NAME WILL BE DISPLAYED
         BH    ONLY18
         LA    R4,NAME
         EX    R2,MVCINST          *MOVE THE VARIABLE NAME
         B     CHKVALUE
ONLY18   MVC   NAME,0(R3)
CHKVALUE L     R3,VALADD          *ONLY THE FIRST 60 CHARACTERS OF
         L     R2,VALLEN          *THE VARIABLE VALUE WILL BE DISPLAYED
         C     R2,=F'60'          *THE NAME WILL BE DISPLAYED
         BH    ONLY60
         LA    R4,VALUE
         EX    R2,MVCINST
         B     DISPLAY
ONLY60   MVC   VALUE,0(R3)
DISPLAY  TPUT  MSG,80              *DISPLAY THE LINE
         B     BEGPROG             *LOOP TO GET NEXT VARIABLE
RETURN   L     R13,4(R13)          *RESTORE OP SYS'S SAVE AREAS &
         LM    R14,R12,12(R13)     *REGISTERS
         BR    R14                 *F I N I T O
MVCINST  MVC   0(0,R4),0(R3)       *MOVE FROM AREA<-R3 TO AREA<-R4
         LTORG
SAVE     DC    18F'0'
FUNCT    DC    A(TSVELOC)          *LOCATE FUNCTION CODE
NAMEADD  DS    A                   *ADDRESS OF VARIABLE NAME
NAMELEN  DS    F                   *LENGTH OF VARIABLE NAME
VALADD   DS    A                   *ADDRESS OF VARIABLE VALUE
VALLEN   DS    F                   *LENGTH OF VALUE
TOKEN    DC    F'0'                *TOKEN VALUE
ENDVARS  DC    A(TSVRNOM)          *NO MORE VARIABLES SIGNAL
MSG      DS    0CL80
NAME     DS    CL19
         DC    C'='
VALUE    DS    CL60
CVTPTR   EQU   16                  *CVT OFFSET
CVTTVT   EQU   X'9C'               *TSO VECTOR TABLE OFFSET
         IKJTSVT
         DS    0F


C Sreenath Chary
Technical Consultant
Qantas Airways Ltd (Australia) © Xephon 1989

