Creating a pseudo system command

The general purpose WTO exits introduced with MVS/SP 2.1.2 provide the systems
programmer with an opportunity to create pseudo operator commands easily.  The
following WTO user exit facilitates the orderly shut-down of our system.  It
works as follows:

1    The master console operator enters a pseudo command (in our case,
'SHUTDOWN').

2    The operating system then issues an error message 'IEE305I SHUTDOWN COMMAND
INVALID'.

3    The WTO exit traps this message, suppresses it so that it will not appear
on any console (but will still appear in SYSLOG), and then issues a series of
MVS system commands that would normally be issued by the master console operator
during system shut-down.

Two features of the exit are worthy of note:

1    The exit determines which console was about to receive the IEE305I message
(that is, the console from which the SHUTDOWN command was issued), and does
nothing if the console is not one of a select few.

2    The exit searches through the ORE chain in the private area of the console
address space and will reply to certain outstanding WTORs if they are found.

This exit can obviously be modified for a number of applications.  It
essentially serves the same function for an operator that a CLIST does for a TSO
user.  As written, it is a stand-alone 'user specified' WTO exit that will
receive control only when message IEE305I is issued, assuming that the following
line has been added to the active MPFLSTxx member of SYS1.PARMLIB:

     IEE305I,SUP(NO),USEREXIT(WTOXIT02)

The code could have been incorporated into the default WTO user exit IEAVMXIT,
where a test for message IEE305I would need to be added to the code.

WTOXIT02 CSECT
WTOXIT02 AMODE 31
WTOXIT02 RMODE ANY
***   PGM NAME:  WTOXIT02
**    THIS EXIT IS ENTERED IN SUPERVISOR STATE, KEY 0, LIVES ABOVE THE
**    LINE, AND MUST BE REENTRANT.  IT MUST BE LINKEDITED AUTHORISED
**    AND PLACED IN AN AUTHORISED LINKLIST LIBRARY.  THE GETMAIN-ED
**    AREA FOR MGCR PARAMETER LIST MUST BE BELOW THE LINE.  ALL
**    ARE ISSUED USING THE MGCR SERVICE (SVC 34).
**    THE EXIT IS USED TO SHUT DOWN THE SYSTEM.  IT RECEIVES CONTROL
**    WHEN THE INVALID (PSEUDO) COMMAND 'SHUTDOWN' IS ENTERED (MSG
**    IEE305I).  AT THAT TIME, IT WILL DO THE FOLLOWING:
**    - ISSUE WTO 'WTOXIT02 - SHUTDOWN PSEUDO COMMAND ISSUED'
**    - ISSUE 'P RMF'
**    - ISSUE 'P SAR'
**    - ISSUE 'C OX----'       (WHERE ---- IS THE GRS SYSTEM ID)
**    - ISSUE 'REPLY ##,+'     (WHERE ## IS REPLY ID OF JCLRDR WTOR)
**    - ISSUE 'REPLY ##,STOP'  (WHERE ## IS REPLY ID OF UCC7ICOM WTOR)
**  (NOTE:  OUR VERSION OF THIS EXIT ACTUALLY ISSUES MANY MORE COMMANDS
**     THAN THIS).
**    THE 'SHUTDOWN' COMMAND MAY BE ENTERED FROM
**      - CONSOLE 1 OR 2,
**      - SDSF (ONLY IF THE USER HAS SDSF COMMAND CAPABILITY, AND EVEN
**              THEN THE WTOR'S WILL NOT BE REPLIED TO)
**    AN ATTEMPT TO ENTER THE COMMAND VIA AN INTERNAL READER WILL FAIL.
**    REGISTERS USED THROUGHOUT THE PROGRAM
**      R2    POINTER TO CTXT PARAMETER LIST
**      R3    POINTER TO MSG ATTRIBUTES STRUCTURE
**      R4    POINTER TO INCOMING MSG TEXT
**      R5    LOCAL WORK REGISTER
**      R6    LOCAL WORK REGISTER
**      R7    USED AS MAIN BAL REGISTER
**      R12   BASE REGISTER
**      R13   ADDRESS OF GETMAINED AREA (MY SAVE AREA AND WORK AREA)
**    DSECT MAPPING MACROS USED
**      GETMAINX         MAPS GETMAINED AREA
**      ZMSGTEXT         MAPPING OF THE MESSAGE TEST
**      IEZVX100         COMMUNICATIONS TASK EXIT PARAMETER LIST (CTXT)
**      CTXTATTR         DESCRIBES THE ATTRIBUTES OF THE INCOMING MSG
**      CVT              CVT MAPPING MACRO
**      IEECUCM          UCM MAPPING MACRO
**      IHAORE           ORE MAPPING MACRO
**      IHAWQE           WQE MAPPING MACRO
**    REFERENCES
**      GC28-1152  MVS/XA SPL: SYSTEM MODIFICATIONS
**      GC28-1147  MVS/XA SPL: USER EXITS
         SAVE  (14,12),,WTOXIT02.&SYSDATE.&SYSTIME
         LR    R12,R15            LOAD BASE REG WITH ENTRY POINT
         USING WTOXIT02,R12       SET UP BASE ADDRESSABILITY
         L     R2,0(R1)           SAVE ADDRESS OF CTXT PARAMETER LIST
         USING CTXT,R2            ESTABLISH ADDRESSABILITY TO IT
         GETMAIN RU,LV=4096,SP=230,LOC=BELOW  GETMAIN FOR S.A., ETC.
         ST    R13,4(R1)          SAVE CALLER'S SAVE AREA ADDR IN MINE
         ST    R1,8(R13)          SAVE MY SAVE AREA ADDRESS IN HIS S.A.
         LR    R13,R1             PUT MY SAVE AREA ADDRESS IN R13
         USING GETMAINX,R13
         L     R3,CTXTTXPJ        LOAD PTR TO MSG ATTRIBUTES (AND TEXT)
         USING CTXTATTR,R3        ESTABLISH ADDRESSABILITY TO IT
         LA    R4,CTXTTMSG        LOAD ADDRESS OF TEXT OF MESSAGE
         USING ZMSGTEXT,R4        ESTABLISH ADDRESSABILITY TO IT
IEE305I  EQU   *
         CLC   ZCMDNAME(8),=C'SHUTDOWN'   'SHUTDOWN' PSEUDO COMMAND?
         BNE   WINDUP                       NO, DO NOTHING
         L     R5,CTXTCIDP        LOAD "POINTER TO CONSOLE ID" INTO REG
         CLC   0(2,R5),=X'0001'   MSG FROM CONSOLE 1 OR SDSF?
         BE    CONTINUE             YES, BRANCH TO CONTINUE
         CLC   0(2,R5),=X'0002'   MSG FROM CONSOLE 2?
         BNE   WINDUP               NO,  DON'T PROCESS COMMAND
CONTINUE OI    CTXTRFB2,CTXTRHCO  SUPPRESS THE MESSAGE (HARD COPY ONLY)
         WTO   'WTOXIT02 - SHUTDOWN PSEUDO COMMAND ISSUED',            X
               ROUTCDE=2,DESC=2   ISSUE HIGHLIGHTED MSG
         BAL   R7,RMF             ISSUE "P RMF"
         BAL   R7,SAR             ISSUE "P SAR"
         BAL   R7,OMEGAMON        ISSUE "C OX----"
         BAL   R7,JCLRDR          ISSUE "REPLY ##,+"
         BAL   R7,UCC7ICOM        ISSUE "REPLY ##,STOP"
WINDUP   L     R4,SAVEAREA+4      PUT CALLER'S SAVE AREA ADDRESS IN R4
         FREEMAIN RU,LV=4096,A=(R13),SP=230 FREE MY SAVE AREA, ETC.
         LR    R13,R4             PUT CALLER'S SAVE AREA ADDRESS IN R13
         RETURN (14,12)           RETURN
RMF      XC    MGCRPL(MGCRLTH),MGCRPL         CLEAR THE PARAMETER LIST
         MVI   MGCRFLG1,X'80'                 SET FLAG (CMD ISSUED)
         MVC   MGCRTEXT(L'TEXT01),TEXT01      MOVE IN THE COMMAND
         LA    R1,(MGCRTEXT-MGCRPL)+L'TEXT01  GET MGCRPL LENGTH
         STC   R1,MGCRLGTH                    SAVE LENGTH IN THE MGCRPL
         SR    R0,R0                          CLEAR R0
         MGCR  MGCRPL                         ISSUE THE COMMAND
         BR    R7                             RETURN TO CALLING ROUTINE
SAR      XC    MGCRPL(MGCRLTH),MGCRPL         CLEAR THE PARAMETER LIST
         MVI   MGCRFLG1,X'80'                 SET FLAG (CMD ISSUED)
         MVC   MGCRTEXT(L'TEXT02),TEXT02      MOVE IN THE COMMAND
         LA    R1,(MGCRTEXT-MGCRPL)+L'TEXT02  GET MGCRPL LENGTH
         STC   R1,MGCRLGTH                    SAVE LENGTH IN THE MGCRPL
         SR    R0,R0                          CLEAR R0
         MGCR  MGCRPL                         ISSUE THE COMMAND
         BR    R7                             RETURN TO CALLING ROUTINE
OMEGAMON XC    MGCRPL(MGCRLTH),MGCRPL         CLEAR THE PARAMETER LIST
         MVI   MGCRFLG1,X'80'                 SET FLAG (CMD ISSUED)
         MVC   MGCRTEXT(L'TEXT03),TEXT03      MOVE IN THE COMMAND
         L     R5,16              LOAD ADDRESS OF CVT INTO REG
         USING CVTMAP,R5          ESTABLISH ADDRESSABILITY TO IT
         MVC   MGCRTEXT+4(4),CVTSNAME  MOVE GRS SYSID INTO OX----
         LA    R1,(MGCRTEXT-MGCRPL)+L'TEXT03  GET MGCRPL LENGTH
         STC   R1,MGCRLGTH                    SAVE LENGTH IN THE MGCRPL
         SR    R0,R0                          CLEAR R0
         MGCR  MGCRPL                         ISSUE THE COMMAND
         BR    R7                             RETURN TO CALLING ROUTINE
JCLRDR   L     R5,16              LOAD ADDRESS OF CVT INTO REG
         USING CVTMAP,R5          ESTABLISH ADDRESSABILITY TO IT
         L     R5,CVTCUCB         LOAD ADDRESS OF UCM BASE INTO REG
         USING UCM,R5             ESTABLISH ADDRESSABILITY TO IT
         L     R5,UCMRPYQ         LOAD ADDRESS OF FIRST ORE IN CHAIN
         LTR   R5,R5              DOES IT CONTAIN ZERO? (IE. NO WTOR'S)
         BZR   R7                   YES, RETURN TO CALLING ROUTINE
         USING OREF,R5            ESTABLISH ADDRESSABILITY TO ORE
ORELOOP1 L     R6,ORERWQE         LOAD ADDR. OF ASSOCIATED WQE INTO REG
         USING WQE,R6             ESTABLISH ADDRESSABILITY TO IT
         CLC   WQETXT+4(7),=C'JCL001A'   IS THIS THE JCLRDR WQE?
         BE    GOTJCLR                     YES, ISSUE "REPLY ##,STOP"
         L     R5,ORELKP          LOAD ADDRESS OF NEXT ORE IN CHAIN
         LTR   R5,R5              DOES IT CONTAIN ZERO? (IE. LAST WTOR)
         BZR   R7                   YES, RETURN TO CALLING ROUTINE
         B     ORELOOP1             NO, BRANCH TO NEXT ORE
GOTJCLR  XC    MGCRPL(MGCRLTH),MGCRPL         CLEAR THE PARAMETER LIST
         MVI   MGCRFLG1,X'80'                 SET FLAG (CMD ISSUED)
         MVC   MGCRTEXT(L'TEXT04),TEXT04      MOVE IN THE COMMAND
         MVC   MGCRTEXT+6(2),WQERPYID         INSERT THE REPLY ID
         LA    R1,(MGCRTEXT-MGCRPL)+L'TEXT04  GET MGCRPL LENGTH
         STC   R1,MGCRLGTH                    SAVE LENGTH IN THE MGCRPL
         SR    R0,R0                          CLEAR R0
         MGCR  MGCRPL                         ISSUE THE COMMAND
         BR    R7                             RETURN TO CALLER
UCC7ICOM L     R5,16              LOAD ADDRESS OF CVT INTO REG
         USING CVTMAP,R5          ESTABLISH ADDRESSABILITY TO IT
         L     R5,CVTCUCB         LOAD ADDRESS OF UCM BASE INTO REG
         USING UCM,R5             ESTABLISH ADDRESSABILITY TO IT
         L     R5,UCMRPYQ         LOAD ADDRESS OF FIRST ORE IN CHAIN
         LTR   R5,R5              DOES IT CONTAIN ZERO? (IE. NO WTOR'S)
         BZR   R7                   YES, RETURN TO CALLING ROUTINE
         USING OREF,R5            ESTABLISH ADDRESSABILITY TO ORE
ORELOOP2 L     R6,ORERWQE         LOAD ADDR. OF ASSOCIATED WQE INTO REG
         USING WQE,R6             ESTABLISH ADDRESSABILITY TO IT
         CLC   WQETXT+4(8),=C'UCC7.574'  IS THIS THE UCC7ICOM WQE?
         BE    GOTUCC7                     YES, ISSUE "REPLY ##,STOP"
         L     R5,ORELKP          LOAD ADDRESS OF NEXT ORE IN CHAIN
         LTR   R5,R5              DOES IT CONTAIN ZERO? (IE. LAST WTOR)
         BZR   R7                   YES, RETURN TO CALLING ROUTINE
         B     ORELOOP2             NO, BRANCH TO NEXT ORE
GOTUCC7  XC    MGCRPL(MGCRLTH),MGCRPL         CLEAR THE PARAMETER LIST
         MVI   MGCRFLG1,X'80'                 SET FLAG (CMD ISSUED)
         MVC   MGCRTEXT(L'TEXT05),TEXT05      MOVE IN THE COMMAND
         MVC   MGCRTEXT+6(2),WQERPYID         INSERT THE REPLY ID
         LA    R1,(MGCRTEXT-MGCRPL)+L'TEXT05  GET MGCRPL LENGTH
         STC   R1,MGCRLGTH                    SAVE LENGTH IN THE MGCRPL
         SR    R0,R0                          CLEAR R0
         MGCR  MGCRPL                         ISSUE THE COMMAND
         BR    R7                             RETURN TO CALLER
         LTORG
**    CHARACTER CONSTANTS
TEXT01   DC    C'P RMF'
TEXT02   DC    C'P SAR'
TEXT03   DC    C'C OX----'
TEXT04   DC    C'REPLY ##,+'
TEXT05   DC    C'REPLY ##,STOP'
**  GETMAINED DSECT
GETMAINX DSECT                    GETMAIN AREA MAPPING DSECT
SAVEAREA DS    18F                MY SAVE AREA
MGCR     IEZMGCR  DSECT=NO        USED TO ISSUE COMMANDS
**    MAPPING OF THE MESSAGE TEST
ZMSGTEXT DSECT
ZMSGID   DS    CL8                MESSAGE ID
ZCMDNAME DS    CL8                COMMAND NAME FOR IEE305I
         IEZVX100                 CTXT PARAMETER LIST MAPPING MACRO
         CVT   DSECT=YES          CVT MAPPING MACRO
UCM      IEECUCM FORMAT=NEW       UCM MAPPING MACRO
         IHAORE                   ORE MAPPING MACRO
         IHAWQE                   WQE MAPPING MACRO
         END   WTOXIT02


Richard Moseson
Senior Systems Programmer
Pittsburgh National Bank (USA)     © Pittsburgh National Bank 1987











































