Searching for alias names in copy operations


THE PROBLEM

When copying modules, the IEBCOPY utility program doesn't have any means of
knowing which are the alias names that should also be copied if you do not
specifically mention them.  You have to specify program names and all the
aliases to really copy all program information to another library.


SOLUTION

The SFALIAS COBOL program dynamically invokes the AMBLIST utility, searches for
alias names, and creates an output file with IEBCOPY utility control cards to
copy the specified programs and all of its aliases.  Then, you no longer need to
worry about alias names, or keep track of them, or maintain controls on them.


JCL EXAMPLE FOR RUNNING SFALIAS

//COPYALIA PROC SOUT=A,BIBL='TST1.LINKLIB'
//STEP1    EXEC PGM=SFALIAS
//* GENERATED INPUT FOR AMBLIST
//SYSIN    DD DSN=&&INAMB,DISP=(,DELETE,DELETE),UNIT=VIO,
//            SPACE=(TRK,(2,1)),DCB=(RECFM=F,BLKSIZE=80,LRECL=80)
//* AMBLIST OUTPUT
//SYSPRINT DD UNIT=VIO,SPACE=(CYL,(5,1)),
//            DCB=(RECFM=FB,BLKSIZE=1210,LRECL=121)
//* GENERATED IEBCOPY CONTROL CARDS
//INIEB    DD DSN=&&INIEB,DISP=(,PASS,DELETE),UNIT=SYSDA,
//            SPACE=(TRK,(5,1)),DCB=(RECFM=F,BLKSIZE=80,LRECL=80)
//* LINKLIB LIBRARY (FOR AMBLIST SEARCH)
//LIB      DD DSN=&BIBL,DISP=SHR
//*  MODULES TO BE COPIED (1 PER CARD STARTING IN COL 1)
//SYSLIB   DD DDNAME=SYSLIB
//STEP2    EXEC PGM=IEBCOPY,COND=(0,LT)
//SYSPRINT DD  SYSOUT=&SOUT
//SYSUT3   DD  UNIT=SYSDA,SPACE=(CYL,(2,1))
//SYSUT4   DD  UNIT=SYSDA,SPACE=(CYL,(2,1))
//INADD    DD  DSN=&BIBL,DISP=SHR
//OUTADD   DD  DSN=&&AUXFIL,DISP=(NEW,PASS,DELETE),UNIT=SYSDA,
//         SPACE=(CYL,(1,1))
//SYSIN    DD  DSN=&&INIEB,DISP=(OLD,DELETE,DELETE)
//*-----------------------------------------------------------------
//*
//* //S1       EXEC COPYALIA
//* //SYSLIB   DD   *
//* PROG1                         ONE PROGRAM NAME PER CARD
//* PROG2                         STARTING IN COLUMN 1
//* /*
//*-----------------------------------------------------------------


SFALIAS COBOL PROGRAM

       IDENTIFICATION DIVISION.
       PROGRAM-ID.    SFALIAS.
       REMARKS.
           FUNCTION - INPUT: CONTROL CARDS SPECIFYING PROGRAM  NAMES
                      OUTPUT: IEBCOPY CONTROL CARDS FOR THOSE PROGRAMS
                              AND THEIR ALIASES.
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT  SYSLIB   ASSIGN  SYS003-SYSLIB.
           SELECT  INAMB    ASSIGN  SYS004-SYSIN.
           SELECT  OUTAMB   ASSIGN  SYS005-SYSPRINT.
           SELECT  INIEB    ASSIGN  SYS006-INIEB.
       DATA DIVISION.
       FILE SECTION.
       FD  SYSLIB
                 LABEL RECORDS STANDARD
                 BLOCK CONTAINS 0 RECORDS
                 RECORD CONTAINS 80 CHARACTERS
                 DATA RECORD IS REG-A-SYSLIB.
       01        REG-A-SYSLIB.
         03      NOME-PGM-A-SYSLIB              PIC X(8).
         03      FILLER                         PIC X(72).
       FD  INAMB
                 LABEL RECORDS STANDARD
                 BLOCK CONTAINS 0 RECORDS
                 RECORD CONTAINS 80 CHARACTERS
                 DATA RECORD IS REG-A-INAMB.
       01        REG-A-INAMB.
         03      FILLER                         PIC X(37).
         03      NOME-PGM-A-INAMB               PIC X(8).
         03      FILLER                         PIC X(35).
       FD  OUTAMB
                 LABEL RECORDS STANDARD
                 BLOCK CONTAINS 0 RECORDS
                 RECORD CONTAINS 121 CHARACTERS
                 DATA RECORD IS REG-A-OUTAMB.
       01        REG-A-OUTAMB.
         03      LIN-A-OUTAMB.                  PIC X(121).
       FD  INIEB
                 LABEL RECORDS STANDARD
                 BLOCK CONTAINS 0 RECORDS
                 RECORD CONTAINS 80 CHARACTERS
                 DATA RECORD IS REG-A-INIEB.
       01        REG-A-INIEB.
         03      FILLER                         PIC X(5).
         03      MEMBER-A-INIEB                 PIC X(8).
         03      FILLER                         PIC X(67).
       WORKING-STORAGE SECTION.
       77        XAV-W-FIM                      PIC 9      VALUE ZERO.
       01        REG-W-SYSLIB                   VALUE SPACES.
         03      NOME-PGM-W-SYSLIB              PIC X(8).
         03      FILLER                         PIC X(72).
       01        REG-W-INAMB.
         03      FILLER                         PIC X(37) VALUE
                  ' LISTLOAD OUTPUT=XREF,DDN=LIB,MEMBER='.
         03      NOME-PGM-W-INAMB               PIC X(8)  VALUE SPACES.
         03      FILLER                         PIC X(45) VALUE SPACES.
       01        REG-W-OUTAMB                   VALUE SPACES.
         03      LIN-W-OUTAMB                   PIC X(121).
         03      LIN1-W-OUTAMB REDEFINES LIN-W-OUTAMB.
           05    FILLER                         PIC X(19).
           05    CONST-W-OUTAMB                 PIC X(13).
           05    FILLER                         PIC X(89).
         03      LIN2-W-OUTAMB REDEFINES LIN-W-OUTAMB.
           05    FILLER                         PIC X(20).
           05    TST-W-OUTAMB                   PIC X.
           05    MEMBER-W-OUTAMB                PIC X(8).
           05    FILLER                         PIC X(92).
       01        REG-W-INIEB.
         03      FILLER                         PIC X(5) VALUE ' S M='.
         03      MEMBER-W-INIEB                 PIC X(8).
         03      FILLER                         PIC X(67) VALUE SPACES.
       01        AUX-W-INIEB.
         03      FILLER                         PIC X(28) VALUE
                  ' COPY O=OUTADD,I=((INADD,R))'.
         03      FILLER                         PIC X(52) VALUE SPACES.
       PROCEDURE DIVISION.
       10-00-CORPO-DO-PROGRAMA SECTION.
       10-02-VAI-PROCESSAR.
           MOVE ZEROS TO RETURN-CODE.
           PERFORM 20-00-ETAPA-1.
           PERFORM 30-00-ETAPA-2.
       10-04-PARA-PROCESSAMENTO.
           STOP RUN.
       10-12-CORPO-DO-PROGRAMA-X.
           EXIT.
       20-00-ETAPA-1 SECTION.
       20-02-ABRE-ARQUIVOS.
           OPEN  INPUT SYSLIB.
           OPEN OUTPUT INAMB.
           OPEN OUTPUT INIEB.
       20-04-GRAVA-PRI-IEB.
           WRITE REG-A-INIEB FROM AUX-W-INIEB.
       20-06-LE-SYSLIB.
           READ SYSLIB INTO REG-W-SYSLIB
                AT END MOVE HIGH-VALUES TO REG-W-SYSLIB
                GO TO 20-10-FECHA-ARQS.
       20-08-NUCLEO-ETAPA-2.
           MOVE NOME-PGM-W-SYSLIB TO NOME-PGM-W-INAMB.
           MOVE NOME-PGM-W-SYSLIB TO MEMBER-W-INIEB.
           WRITE REG-A-INAMB FROM REG-W-INAMB.
           WRITE REG-A-INIEB FROM REG-W-INIEB.
           GO TO 20-06-LE-SYSLIB.
       20-10-FECHA-ARQS.
           CLOSE SYSLIB.
           CLOSE INAMB.
       20-12-CHAMA-AMBLIST.
           CALL 'AMBLIST'.
           IF RETURN-CODE NOT EQUAL ZEROS
              CALL 'CANCEL'.
       20-14-ETAPA-1-X.
           EXIT.
       30-00-ETAPA-2 SECTION.
       30-02-ABRE-ARQUIVOS.
           OPEN  INPUT OUTAMB.
       30-04-LE-OUTAMB.
           IF REG-W-OUTAMB NOT EQUAL HIGH-VALUES
              PERFORM 40-00-LE-OUTAMB
           ELSE
              GO TO 30-10-FECHA-ARQS.
       30-06-NUCLEO-ETAPA-2.
           IF CONST-W-OUTAMB NOT EQUAL '** ALIASES **'
              GO TO 30-04-LE-OUTAMB.
           MOVE SPACES TO REG-W-OUTAMB.
       30-08-GRAVA-ALIASES.
           PERFORM 40-00-LE-OUTAMB.
           IF TST-W-OUTAMB NOT EQUAL SPACE
              GO TO 30-04-LE-OUTAMB.
           MOVE MEMBER-W-OUTAMB TO MEMBER-W-INIEB.
           WRITE REG-A-INIEB FROM REG-W-INIEB.
           GO TO 30-08-GRAVA-ALIASES.
       30-10-FECHA-ARQS.
           CLOSE OUTAMB.
           CLOSE INIEB.
       30-12-ETAPA-2-X.
           EXIT.
       40-00-LE-OUTAMB SECTION.
       40-02-LEITURA.
           READ OUTAMB INTO REG-W-OUTAMB
                AT END MOVE HIGH-VALUES TO REG-W-OUTAMB.
       40-04-SAI-LEITURA.
           EXIT.


Arie Tikva (Brazil) © Xephon 1987



























































