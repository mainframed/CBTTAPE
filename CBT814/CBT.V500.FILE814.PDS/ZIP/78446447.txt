Deleting an unrelated VVR

In our shop we have a lot of unrelated VSAM Volume Records
(VVRs) in our VSAM Volume Datasets (VVDSs) that have
arisen over the years.  There are various kinds of problem that
can create an unrelated VVR.  I have often had problems with
the catalogs themselves and various IDCAMS jobs ending
abnormally.

An unrelated VVR is a VVR that can not be deleted with any
of the IDCAMS commands.  One of the usual reasons for this
may be that it has a pointer to a catalog that doesn't exist any
more.

One of the problems with an unrelated VVR is that you can't
define a new VSAM dataset with the same name as the
component name of the VVR on that disk.

To solve this problem I have developed a program that does an
ESDS delete, specifically built  to  do  this on a VVDS.  You
simply give the program the RBA of the CI and the number of
the record in that CI, or if you prefer you can give it the
component name instead  of  the record number. I have also
built in a simulate option.  You can look at the result on a snap
dataset that's specified in the JCL whether you run in simulate
or live mode.

I have written the program in as structured a form as possible,
so it should be easy to change it to do other things inside the
logic of an ESDS, if you want.  I have documented how to use
the program and how it works inside the source.

*****   THIS PROGRAM BASICALLY DELETES ONE RECORD IN A VSAM ESDS
*          DATASET WITH A CISIZE OF 4096 BYTES.
*       THE PROGAM DON'T WORK WITH SPANNED RECORDS.
*        PARAMETERS :
*          1.   RBA, RELATIVE BLOCK ADDRESS FOR CI. THIS
*                      IS WANTED IN DECIMAL.       (NEEDED)
*          2.   DELNAME, THIS IS THE COMPONENT NAME OF A VVDS RECORD.
*                 IF IT'S NOT A VVDS DON'T GIVE ANYTHING IN THIS PARM.
*                 IF THIS IS GIVEN, THE PROGRAM EXPECTS THE
*                 ESDS IS A VVDS (VSAM VOLUME DATASET), AND IT
*                 WILL SEARCH FOR A RECORD WITH THE COMPONENT NAME
*                 EQUAL TO DELNAME, AND DELETE IT.  (OPTIONAL)
*                 DEFAULT IS X44'99' WHICH MEANS NOT GIVEN.
*          3.   DELRECNR, THIS IS THE NUMBER INSIDE THE CI OF THE
*                 RECORD THAT YOU WANT TO DELETE. THIS IS USED ONLY
*                 WHEN DELNAME IS NOT GIVEN. THE RECORD IS COUNTED
*                 FROM 1. (THE FIRST IS NUMBER 1.) GIVEN IN DECIMAL.
*                 DEFAULT IS 0.                      (OPTIONAL)
*          4.   MODE, THE VALUES ARE LIVE OR SIMULATE. IF YOU SPECIFY
*                 LIVE THE PROGRAM REALLY DELETES THE RECORD, IF
*                 SIMULATE IS SPECIFIED THE RECORD IS NOT DELETED,
*                 BUT YOU CAN LOOK AT THE RESULT ON DD SNAP.
*                 DEFAULT IS "SIMU".                 (OPTIONAL)
* HOW TO RUN THIS PROGRAM:
*       SUBMIT JCL AS FOLLOWS:
*         EXEC PGM=SDELVVR,PARM=('R,X,Y,Z') WHERE R,X,Y AND Z ARE
*                                          THE PARMS DISCUSSED ABOVE.
*         STEPLIB DD DSN= AN APF-AUTHORISED LIBRARY
*                         THAT CONTAINS SDELVVR
*         SYSPRINT DD SYSOUT=*
*         SNAP    DD      A SNAP DATASETT
*         VVDS    DD      THE ESDS THAT YOU WANT TO DELETE A RECORD IN
*                             (IT'S ACTUALLY WRITTEN FOR A VVDS).
*    PROGRAM SDELVVR HAS TO BE LINKEDIT'ED WITH AC=1
*     HOW SDELVVR WORKS :
*       -  OPEN THE ESDS FOUND IN DD CARD VVDS.
*       -  READ THE CI AT THE RBA GIVEN IN PARAMETER 1.
*       -  SCAN EVERY RECORD IN THE GIVEN CI AND CHECK IF THIS
*           RECORD IS THE RIGHT ONE.
*       -  WRITE "RECORD FOUND" TO SYSPRINT IF IT IS THE RIGHT ONE.
*       -  WRITE "RECORD NOT FOUND" TO SYSPRINT IF IT IS NOT THE RIGHT
*           ONE (FOR EVERY RECORD BEEING SCANNED) .
*       -  WHEN (IF) THE RECORD IS FOUND IT'S BEING DELETED.
*      THERE MAY BE VARIOUS MESSAGES WRITTEN TO SYSPRINT AND A FEW
*       MAY BE WRITTEN AS WTO-MESSAGES. READ THE MESSAGES CAREFULLY.
*      IF YOU WANT A MORE DETAILED DESCRIPTION REFER TO THE COMMENTS
*****   IN THE PROGRAM AND FINALLY THE CODE ITSELF.
           MACRO
           PARMCONV &FROMADD
&S         SETC '&SYSNDX'
           AIF ('&FROMADD'(1,1) EQ '(').REGHAN
           L    PARIN&S.P,&FROMADD
           L    PARIN&S.P,0(PARIN&S.P)
           AGO  .WEITER
.REGHAN    ANOP
&A         SETC '&FROMADD(1)'
           L PARIN&S.P,0(&A.)
.WEITER    ANOP
           B     PARMCL&S
PARMHAN&S  ST    R14,BR1&S
           STM   R2,R11,SAVE&S
           USING PARMIN&S,PARIN&S.P MAPPING INPUT PARAMETER
           GETMAIN R,LV=ADBSZ&S   GET STOR. FOR OUT-PARM ADDRESSES
           LR    PARAD&S.P,R1     LET PARADDP POINT TO THEM
           LR    FRSTAD&S,R1      INITIATE FIRST ADDRESS POINTER
           USING PARADDR&S,PARAD&S.P MAPPING ADRESSES OF OUTPUT-PARM
           USING PARMOUT&S,PAROUT&S.P MAPPING OUTPUT PARAMETER
           LH    LNGT&S,PARMIN&S  LOAD THE PARAM.-IN LENGTH TO LENGTH
           BCTR  LNGT&S,0         HUMAN TO MACHINE LENGTH
           LA    PARIN&S.P,2(PARIN&S.P) INCR. PARIN POINTER
           USING COMMATB&S,COMMATB&S.P INITIALISERER COMMATB
           BAS   R14,BLDTB&S   BYGGE OPP TABELL FOR TRT INSTRUCTION
           LA BUFFEND&S,ADBSZ&S.(FRSTAD&S) BUFFERSIZE + FIRST ADDRESS
         DO WHILE,(EX,LNGT&S,TESTP&S,4),AND,(CR,PARAD&S.P,BUFFEND&S,LT)
             ST R1,COMMAP&S   STORE ADDRESS TO COMMA
             LR R3,R1      POINTER TO ','
             SR R3,PARIN&S.P              - PARINP = LENGTH OF OUT-PARM
             LA R4,2(R3)        ADD 2 FOR SPACE TO THE LENGTH FIELD
             GETMAIN R,LV=(R4)  GET STOR. FOR THIS OUT-PARAMETER
             IF (LTR,R15,R15,NZ)
               WTO 'GETMAIN FAILED'
             ENDIF
             LR PAROUT&S.P,R1   LET PARAM OUT POINTER POINT TO AREA
             ST R1,PARADDR&S    STOR POINTER TO PARAM IN PARADDR
             LA PARAD&S.P,4(PARAD&S.P) INCREMENT THE ADDRESS POINTER
             STH R3,PARMOUT&S   STORE LENGTH FIELD IN OUT PARAMETER
             LA PAROUT&S.P,2(PAROUT&S.P) INCR. PAROUTP BEYOND LGT. FLD
             SR LNGT&S,R3       LENGTH : = THE SPARE LENGTH
             BCTR LNGT&S,0      SUBTRACT ONE FOR THE COMMA
             BCTR R3,0          HUMAN TO MACHINE LENGTH
             IF (CH,R4,=H'2',GT)
               EX R3,MOVEPAR&S    MOVE ACTUAL PARM FROM INPUT TO OUTPUT
             ENDIF
             L  PARIN&S.P,COMMAP&S LOAD POINTER TO COMMA
             LA PARIN&S.P,1(PARIN&S.P) PARINP POINT TO NEXT INP. PARM
           ENDDO
           IF (EX,LNGT&S,TESTP&S,2)
             INFORM 'THE LAST CHARACTER IN PARM MUST NOT BE COMMA.'
             LA  R15,8(0)   SET CC = 8
             ST  R15,BR15&S
           ENDIF
           LA    LNGT&S,1(LNGT&S)  MACHINE TO HUMAN LENGTH
           LA    R4,2(LNGT&S)   ADD 2 FOR THE LENGTH FIELD IN OUTPARM
           GETMAIN R,LV=(R4)    GET STOR. FOR THE LAST OUTPUT PARM
           LR    PAROUT&S.P,R1  LET PAROUTP POINT TO GETM. STORAGE
           ST    R1,PARADDR&S   STORE POINTER TO PARMOUT IN ADD. BLOC
           OI    PARADDR&S,VLB&S MARK THIS AS THE LAST PARAMETER
           STH   LNGT&S,PARMOUT&S LNGT OF PARMOUT
           LA    PAROUT&S.P,2(PAROUT&S.P) POINT BEYOND THE LENGTH FIELD
           BCTR  LNGT&S,0       HUMAN TO MACHINE LENGTH
           IF (CH,R4,=H'2',GT)
             EX    LNGT&S,MOVEPAR&S MOVE LAST PARM TO PARMOUT
           ENDIF
           LR    R1,FRSTAD&S    LET REG 1 POINT ON FIRST ADDRESS
           LM    R2,R11,SAVE&S
           L     R14,BR1&S
           L     R15,BR15&S
           BR    R14            RETURN
TESTP&S    TRT   PARMIN&S.(0),COMMATB&S
MOVEPAR&S  MVC   PARMOUT&S.(0),PARMIN&S
*********  BUILDTAB  ************************************************
BLDTB&S    ST    R14,BR2&S
           GETMAIN R,LV=256
           LR    COMMATB&S.P,R1
           MVI   COMMATB&S,X'00'
           MVC   COMMATB&S+1(255),COMMATB&S
           MVI   COMMATB&S+X'6B',X'6B'
           DROP  COMMATB&S.P
           DROP  PARIN&S.P
           DROP  PARAD&S.P
           DROP  PAROUT&S.P
           L     R14,BR2&S
           BR    R14
**************   VARIABLES                *******************
COMMAP&S   DS    F
BR1&S      DS    F
BR2&S      DS    F
BR15&S     DS    F
SAVE&S     DS    10F
**************   DSECT AREA               *******************
PARMIN&S   DSECT
PARMOUT&S  DSECT
PARADDR&S  DSECT
COMMATB&S  DSECT
           DS  107X'00'
           DS  X'6B'
           DS  148X'00'
&SYSECT    CSECT
PARMCL&S   BAS R14,PARMHAN&S
**************             EQU'S          *******************
COMMATB&S.P EQU 5
BUFFEND&S   EQU 6
FRSTAD&S    EQU 7
LNGT&S      EQU 8
PAROUT&S.P  EQU 9
PARIN&S.P   EQU 10
PARAD&S.P   EQU 11
ADBSZ&S     EQU 40
VLB&S       EQU X'80'
           MEND
           MACRO
&LABEL     INFORM &MSG = 'YOU FORGOT TO GIVE A MSG.'
           GBLC  &S
           AIF ('&S' NE '').EVERYT
&S         SETC '&SYSNDX'
.FIRSTT    B   GIVEM&S
PRINTM&S   DS  0H
**********  PRINTMSG      *********************************************
*       THIS PROCEDURE          R3 CONTAINS THE LENGTH OF THE
*          MESSAGE AND THE MSG. IS POINTED TO BY R14.
           ST   14,BR&S    SAVE BRANCH REGISTER
           PUT  SYSPRINT   QSAM FIND BUFF AND LET R1 POINT ON IT
           MVI  0(1),X'00'     MOVE X'00' TO 1 BYTE OF BUFFER
           MVC  1(132,1),0(1)  MOVE X'00'S TO BUFFER
*      GET LENGTH FROM DCBLRECL FOR LATER USE
*      OPEN SYSPRINT IF IT'S NOT OPEN ALREADY
           BCTR 3,0        HUMAN TO MACHINE-LENGTH
           L    14,BR&S    LOAD BACK BRANCH REGISTER
           EX   3,MOVE1&S   MOVE MSG TO BUFFER POINTED TO BY R1
           O    3,=F'1'    TO ENSURE HALF-W ALIGNMENT ON BRANCH-ADDRESS
           LA   14,1(3,14) INCREMENT R14 TO POINT BEYOND MSG
           L    3,BR3&S    RESTORE REG 3
           BR   14         BRANCH BACK BEYOND MSG
MOVE1&S    MVC  0(0,1),0(14)
BR&S       DS   F
BR3&S      DS   F
GIVEM&S    DS   0H
.EVERYT    ANOP
&LABEL     DS   0H
&V         SETC '&SYSNDX'
           ST   3,BR3&S     SAVE REGISTER 3 BEFORE CHANGE IT
           LA   3,L'MSG&V.(0)
           BAS  14,PRINTM&S
MSG&V      DC   C&MSG
           DS   0H
           MEND
SDELVVR  OPSTART BASE=12
*SDELVVR  SFINTL BASEREG=(12,13)
         EQUAL
         ST   R1,PARPOINT
*    DYNAMIC ALLOCATION TESTING
         OPEN  (SYSPRINT,OUTPUT)
         LA    R3,SYSPRINT
         USING IHADCB,R3
         TM    DCBOFLGS,DCBOFOPN   CHECK SYSPRINT
         BNO   OPENFSYSP           SYSPRINT OPEN NOT OKEY
         WTO   'OPEN AV SYSPRINT IS OKEY.'
         OPEN  (SNAPP,OUTPUT)
         LA    R3,SNAPP
         USING IHADCB,R3
         TM    DCBOFLGS,DCBOFOPN   CHECK SNAPP OPEN
         BO    MAIN                SNAPP OPEN OKEY
         B     OPENFSNAP           SNAPP OPEN FAILED
**********   GETCI         ********************************************
GETCI    ST    R14,BRREG1
*        LA    R3,L'GETCIMSG(0)
*        BAS   R14,PRINTMSG
         GETMAIN R,LV=4096
         LR    RECP,R1
         ST    RECP,CISTART       START OF CI POINTER
         MODCB RPL=VRPL,AREA=(*,CISTART)
         OPEN  (VVDS)
         IF    (LTR,R15,R15,Z)
           INFORM 'OPEN OF VVDS WAS SUCCESSFULLY.'
         ELSE
           INFORM 'SOMETHING WRONG IN VVDS OPEN.'
         ENDIF
         TESTCB ACB=VVDS,OFLAGS=OPEN  CHECK IF DS IS OPEN.
         BNE   OPNVVDSF
         GET   RPL=VRPL
         L     R14,BRREG1
         BR    R14
**********   INITCIP       ********************************************
* THIS PROCEDURE INITIATE THE POINTER TO THE CI THAT IS ALREADY IS OPEN
INITCIP    ST    R14,BRREG1
           USING RDF,DP       MAP DEF. AREA WITH DP (ACTUALLY R5)
           USING CIDF,CIDFP   MAP CIDF AREA WITH CIDFP (ACT. R11)
           USING VVRHEAD,RECP MAP RECORD OR VVR-HEADER WITH RECP (R10)
           L     CIDFP,CISTART
           AH    CIDFP,CISIZE
           SH    CIDFP,=H'4'    CIDFP = CISTART+CISIZE-4
*                                LET R11 POINT TO CIDF
           L     R14,BRREG1
           BR    R14

This article will be concluded in next month's issue when we
shall be publishing the remaining source code for this program.

Knut Oyvind Mikelsen
Systems Programmer (Norway)                        © Reserved 1990

