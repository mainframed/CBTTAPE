An accounting information routine

Several weeks ago an application programmer asked me to
create a subroutine that would capture accounting information
for his billing system.  The accounting information he needed
consisted of the account number, job name, and programmer
name of the job that calls the subroutine.  All this information
is in the JCL job card.

After my research, I found that the system keeps the job name
information in the Job Control Table (JCT), while the account
number and the programmer name are in the Account Control
Table (ACT).  These control tables can be traced from the
current TCB as shown in Figure 1.

The program itself is very simple but a certain amount of
manipulation of some of the control block addresses is
required, notably:

o      The address of JSCB pointed by TCB has only 3 bytes, the
      same as the address of JCT pointed by JSCB.  To obtain
      their addresses, I zero the first byte of those addresses.

o      JCT points to the address of ACT on the first 3 bytes.  The
      last byte is reserved.  Right shifting is needed to obtain the
      ACT address.

o      JSCB points to the address of JCT minus X'10', not to the
      real JCT address.  The same thing happens on the ACT
      address.  Adjustments need to be made in the program to
      fit the real address.

Below are the account subroutine and the sample FORTRAN
program to call that subroutine.  In FORTRAN, I defined a
common area for the accounting information passed from the
routine.  The length of the account number is varied from shop
to shop: 144 characters are available, adjust accordingly to suit
your own environment.  Don't forget to link the account
subroutine with 31-bit AMODE and no call options.


ACCOUNT SUBROUTINE

*  THIS PROGRAM WILL PASS ACCOUNT NO., JOB NAME AND PROGRAMMER'S NAME
ACCOUNT  CSECT
BEGIN    SAVE  (14,12)
         USING ACCOUNT,R12
         LR    R12,R15
         LA    R2,SAVEAREA
         ST    R2,8(R13)
         ST    R13,4(R2)
         LR    R13,R2
         L     R4,=A(ACCT)            ESTABLISH COMMON AREA
         USING DATA,R4                    ADDRESSABILITY
         L     R2,536                 LOAD ADDRESS OF CURRENT TCB
         USING TCB,R2                 ESTABLISH ADDRESSABILITY
         L     R3,TCBJSCB             LOAD ADDRESS OF JSCB
         N     R3,MASK3               ZERO THE FIRST BYTE
         LR    R6,R3
         USING IEZJSCB,R6             ESTABLISH ADDRESSABILITY
         L     R9,JSCBJCT             LOAD ADDRESS OF JCT
         N     R9,MASK3               ZERO THE FIRST BYTE
         LR    R3,R9
         LA    R10,16(R3)             GET THE EXACT ADDR. OF JCT
         LR    R7,R10
         USING INJMJCT,R7             ESTABLISH ADDRESSABILITY
         MVC   JOBNAME,JCTJNAME       GET THE JOB NAME
         L     R5,JCTACTAD            LOAD ADDRESS OF ACT
         SRL   R5,8                   SHIFT TO GET ACT ADDRESS
         LA    R11,16(R5)             GET THE EXACT ADDRESS OF ACT
         LR    R8,R11
         USING IEFAACTB,R8            ESTABLISH ADDRESSABILITY
         MVC   USERNAME,ACTPRGNM      GET THE PROGRAMMER'S NAME
         MVC   ACCTNO(21),ACTACCNT+1  GET THE ACCOUNT INFO.
RETURN   EQU   *
         LH    R15,RETCODE
RTRN     L     R13,4(R13)
         RETURN (14,12),T,RC=(15)
*  DATA AREAS, DCBS, AND DSECTS
SAVEAREA DC    18F'0'
RETCODE  DC    H'0'
MASK3    DC    X'00FFFFFF'
DATA     DSECT
JOBNAME  DS    CL8
ACCTNO   DS    CL21
USERNAME DS    CL20
         EXTRN ACCT
ACCTADDR DC    A(ACCT)
*   DCB DSECT MAPPING MACRO
         IKJTCB DSECT=YES
         IEZJSCB
         IEFAJCTB
         END


SAMPLE FORTRAN PROGRAM

C ------  PROGRAM TO TEST SUBROUTINE ACCOUNT ----
      CHARACTER*21 ACCTNO
      CHARACTER*20 USERNAME
      CHARACTER*8  JOBNAME
      COMMON/ACCT/JOBNAME,ACCTNO,USERNAME
C  ---------------------  CALL ASSEMBLER ROUTINE HERE TO RETURN:
C                             JOBNAME, USERNAME, ACCTNO
      CALL ACCOUNT
      WRITE(6,2) JOBNAME, ACCTNO, USERNAME
 2    FORMAT(1X,A/)
      STOP
      END


Isak Surjana
Systems Analyst
The Ralph M Parsons Co (USA)                   c The Ralph M Parsons Co 1991

