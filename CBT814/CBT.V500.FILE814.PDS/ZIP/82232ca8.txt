ARCMD for MVS users

The ARCMD command, which is well known in VSE
environments, is a command that many MVS users would like
to use, especially users migrating from VSE to MVS or users
in an MVS environment who have to write their own console
interface. The goal of ARCMD/MVS is to provide the ability
to issue console commands for execution directly from a batch
job.

We tested it on an MVS/ESA Version 3 system, where it
worked well: we think that it should work in other
environments too.

Because the ARCMD/MVS uses an SVC, it must be linked
with AC 1 and will work only if executed from an APF-
authorized library.

//JOB1    JOB  (ACCT),'COMMENT',CLASS=A,MSGCLASS=X,
//            MSGLEVEL=(1,1)
//ASMAR  EXEC PGM=IEV90,
//       PARM='DECK,NOLOAD,NOXREF,NOESD,NORLD'
//SYSIN  DD  *
**************************************************************
*   MASC-ARCMD  "ATTENTION ROUTINE COMMAND"
*   THIS PROGRAM ALLOWS CUSTOMERS MIGRATING FROM VSE TO MVS
*   TO USE THE VSE/ARCMD IN AN MVS ENVIRONMENT.
*   THE TECHNOLOGY USED IN ARCMD/MVS IS ADAPTED FROM THE
*   IPO1.SAMPLIB(SMFDUMP) WHICH RUNS ON MOST MVS INSTALLATIONS
*   WORLDWIDE.
*   THE PROGRAM WILL BE STARTED WITH THE FOLLOWING JCL:
*   // EXEC PGM=ARCMD,PARM='XXXXXXXXXXXXXXXXXXXX'
*   THIS PROGRAM MAY BE FREELY USED BY ANY CUSTOMER. PLEASE SEND
*   A NOTIFICATION ABOUT YOUR EXPERIENCES IN USING OUR PRODUCT TO:
*   MASC AG
*   DEPT. SOFTWARE-SUPPORT
*   BIRKENSTRASSE 49
*   CH-6343 ROTKREUZ
*   SWITZERLAND
*   PHONE: '41 (0) 42 64 53 44
*   FAX:   '41 (0) 42 64 53 40
*   PROGRAM MATERIAL, COPYRIGHT (C) 1992, WALSER SOFTWARE-SUPPORT AG
*   DOCUMENTATION MATERIAL, COPYRIGHT (C) 1992  MASC AG, SWITZERLAND
********************************************************
ARCMD    CSECT
         USING *,R10
         SAVE  (14,12),,ARCMD_&SYSDATE
         USING ARCMD,R10
         LR    R10,R15
         ST    R1,ARARG
         SPLEVEL
         LA    R3,ARSAVE
         ST    R13,4(R3)
         ST    R3,8(R13)
         LR    R13,R3
         L     R1,ARARG
         C     R1,=F'0'
         BE    AR008
         L     R15,0(R1)
         SR    R1,R1
         LH    R1,0(R15)
         C     R1,=F'0'
         BE    AR008
         SH    R1,=H'1'
         LA    R2,ARPARM
         EX    R1,AR002
         B     AR003
AR002    MVC   0(0,R2),2(R15)
AR003    DS    0H
         MODESET MF=(E,ARSUPV)
         SLR   R0,R0
         LA    R1,ARSVCA
         SVC   34
         MODESET MF=(E,ARPROB)
         STIMER WAIT,BINTVL=ARTIME
         SLR   R15,R15
         B     AR009
AR008    DS    0H
         WTO   MF=(E,ARMSG)
         STIMER WAIT,BINTVL=ARTIME
         LA    R15,8
         B     AR009
AR009    DS    0H
         L     R13,4(R13)
         RETURN (14,12),RC=(15)
ARSVCA   DC    Y(42),Y(00)
ARPARM   DC    256C' '
ARARG    DS    F
ARSAVE   DS    18F
ARTIME   DC    F'3000'       30 SEC
ARSUPV   MODESET KEY=ZERO,MODE=SUP,MF=L
ARPROB   MODESET KEY=NZERO,MODE=PROB,MF=L
ARMSG    WTO   '*ARCMD: EXEC-PARM NOT GIVEN',MF=L
         LTORG
         DROP  R10
         END
/*
//SYSPRINT  DD SYSOUT=*
//SYSLIB    DD DSN=SYS1.MACLIB,DISP=SHR
//          DD DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1    DD UNIT=VIO,SPACE=(CYL,(5,5))
//SYSUT2    DD UNIT=VIO,SPACE=(CYL,(5,5))
//SYSUT3    DD UNIT=VIO,SPACE=(CYL,(5,5))
//SYSPUNCH  DD DSN=&&DECK,DISP=(,PASS),UNIT=VIO,
//             DCB=(RECFM=FB,LRECL=80,BLKSIZE=400),
//             SPACE=(400,(50,50))
/*
//LNKAR   EXEC PGM=IEWL,COND=(0,NE),
//        PARM='XREF,LIST,LET,CALL,AC=1,AMODE=24,RMODE=24'
//SYSPRINT  DD SYSOUT=*
//SYSUT1    DD UNIT=VIO,SPACE=(1024,(20,20))
//SYSLIN    DD DSN=&&DECK,DISP=(OLD,DELETE)
//SYSLMOD   DD DSN=RSYS1.LINKLIB(ARCMD),DISP=SHR
//*
//AMBLIST  EXEC PGM=AMBLIST,COND=(O,NE)
//SYSPRINT DD  SYSOUT=*
//SYSLIB   DD DSN=RSYS1.LINKLIB,DISP=SHR
//SYSIN    DD  *
  LISTIDR  TITLE=('LISTIDR MEMBER=ARCMD',1),MEMBER=(ARCMD)
/*


MASC AG (Switzerland)                              c MASC AG 1992

