Using CSVAPF services


THE RXXAPF REXX EXEC

/*REXX*/
PARSE ARG DSN OPTION
ADDRESS ISPEXEC 'VGET ZDLVOL'
DSN = SUBSTR(DSN,2,LENGTH(DSN)-2)
VOL = ZDLVOL
ADDRESS ISPEXEC 'VPUT (VOL) PROFILE '
ADDRESS ISPEXEC 'VPUT (DSN) PROFILE '
IF OPTION = "ADD" THEN DO
  C1 = 'DSN='||DSN||""
  C2 = 'OPT='||OPTION||""
  C3 = 'VOL='||ZDLVOL||""
  RC=RXXAPF(C3,C1,C2)
  CALL XX
  IF SUBSTR(RC,1,4) = "Done" THEN DO
     SAY NOW APF CONTAINS  ZDLVOL DSN
  END
END
IF OPTION = "DELETE" THEN DO
  C1 = 'DSN='||DSN||""
  C2 = 'OPT='||OPTION||""
  C3 = 'VOL='||ZDLVOL||""
  RC=RXXAPF(C3,C1,C2)
  IF SUBSTR(RC,1,4) = "Done" THEN DO
     SAY NOW APF CONTAINS  THIS FOLLOWING LIST
     OPTION = "LIST"
     C2 = 'OPT='||OPTION||""
     RC=RXXAPF(C2)
  END
END
EXIT
XX:
  CVT = C2X(STORAGE(10,4))                   /*  CVT ---> 10 Hex     */
  ADDR = X2D(CVT)                            /*  ADDR  CVT   dec     */
  CVTSCPIN = 832
  ADDR = ADDR + CVTSCPIN                     /*  ADDR  CVT + 832     */
  SCPIN = D2X(ADDR)                          /*  --->  CVTSCPIN Hex  */
  ASCPIN = SUBSTR(C2X(STORAGE(SCPIN,4)),2,7) /*  ADDR  SCPIN Hex     */
  SCPIN = X2D(ASCPIN)                        /*  ADDR  SCPIN Dec     */
  SCPIN = SCPIN + 28                         /*  ADDR  SCPIN + 16    */
  SCPIN = D2X(SCPIN)                         /*  --->  SMFID         */
  LOAD  = STORAGE(SCPIN,2)
  LOAD_MEM = "LOAD"||LOAD
  CTL_LOAD = "'SYS1.PARMLIB("||LOAD_MEM||")'"
  ADDRESS ISPEXEC "EDIT DATASET("CTL_LOAD") MACRO(LOCSYS)"
  ADDRESS ISPEXEC "VGET LIG"
  SYSP = SUBSTR(LIG,10,2)
  IEAS_MEM = "IEASYS"||LOAD
  CTL_SYSP = "'SYS1.PARMLIB("||IEAS_MEM||")'"
  ADDRESS ISPEXEC "EDIT DATASET("CTL_SYSP") MACRO(LOCAPF)"
  ADDRESS ISPEXEC "VGET LIG"
  K = INDEX(LIG,"APF=")
  IF K > 0 then do
     APF_MEM = SUBSTR(LIG,K+4,2)
     IF SUBSTR(APF_MEM,1,1) = "(" then do
         APF_MEM = SUBSTR(LIG,K+5,2)
     END
  END
  IEAAPF_M = "IEAAPF"||APF_MEM
  APF_MEMB = "'SYS1.PARMLIB("||IEAAPF_M||")'"
  ADDRESS ISPEXEC "EDIT DATASET("APF_MEMB") MACRO(APFADD)"
RETURN


THE LOCSYS EDIT MACRO

/*REXX*/
/* Locate the parameter SYSP in a loadxx member */
ADDRESS ISREDIT "MACRO"
ADDRESS ISREDIT "(LIG) = LINE .ZL"
ADDRESS ISPEXEC "VPUT LIG"
ADDRESS ISREDIT "END"


THE LOCAPF EDIT MACRO

/*locate the "APF=" parameter in the IEASYSxx member and take the
  first one if multiple members are specified */
/*REXX*/
ADDRESS ISREDIT "MACRO"
ADDRESS ISREDIT "FIND APF="
ADDRESS ISREDIT "(LIG) = LINE .ZCSR"
ADDRESS ISPEXEC "VPUT LIG"
ADDRESS ISREDIT "END"


THE ADDAPF EDIT MACRO

/* this macro adds the dataset following the first line */
/*REXX*/
ADDRESS ISREDIT "MACRO"
ADDRESS ISPEXEC "VGET (DSN) "
ADDRESS ISPEXEC "VGET (VOL) "
LINE = DSN||"        "||VOL||","
MSGE = "THIS FOLLOWING DATASET WAS DYNAMICALLY ADDED"
ADDRESS ISREDIT "LINE_AFTER .ZF = DATALINE '"LINE"' "
ADDRESS ISREDIT "LINE_AFTER .ZF = MSGLINE '"MSGE"' "


THE RXXAPF ASSEMBLER PROGRAM

RXXAPF CSECT
RXXAPF AMODE ANY
RXXAPF RMODE 24
***********************************************************************
* The program is called as follows:                                   *
*                     RXXAPF,("OPT=DELETE","DSN=dsn","VOL=Volser")    *
*                                < ADD   >                            *
*                                < LIST  >                            *
***********************************************************************
*  SAVEAREA CHAIN                                                     *
***********************************************************************
          USING RXXAPF,R12
          SAVE  (14,12)                save caller's registers
          LR    R12,R15                establish module base register
          LR    R7,R1
          USING EFPL,R7
          GETMAIN R,LV=WORKALEN        get working storage
CHAIN     ST    R13,4(R1)              chain savearea backward
          ST    R1,8(R13)              chain savearea forward
          LR    R13,1                  point this module's savearea
          USING WORKAREA,R13
          L     R8,EFPLARG             load ARG_TABLE
          L     R5,EFPLEVAL
          L     R5,0(,R5)
          USING EVALBLOCK,R5
          USING ARGTABLE_ENTRY,R8
*------------------------------------------------------------------*
*   PARAMETERS                                                     *
* The parameters are passed to REXX in the following form:         *
* RC=RXXAPF(C1,C2,C3,C4) or                                        *
* C1= 'DEVT=device_type'                                           *
* C2= 'UCB=xxx ' .....                                             *
*------------------------------------------------------------------*
CHECKPARM CLC   0(4,R8),=X'FFFFFFFF'
          BNE   LOADPARM
          B     ERROR1
LOADPARM  L     R9,ARGTABLE_ARGSTRING_PTR
          C     R9,=XL4'FFFFFFFF'
          BE    FIN
          CLC   =C'OPT=',0(R9)
          BNE   TST_DSNAME
          BAL   R7,DECODE_OPT
          B     NEXTONE
TST_DSNAME EQU *
          CLC   =C'DSN=',0(R9)
          BNE   TST_VOL
          BAL   R7,DECODE_DSN
          B     NEXTONE
TST_VOL   EQU   *
          CLC   =C'VOL=',0(R9)
          BNE   NEXTONE
          BAL   R7,DECODE_VOL
          B     NEXTONE
NEXTONE   LA    R8,8(R8)
          B     LOADPARM
FIN       EQU   *
          CLC   =C'ADD',OPTION
          BNE   TST_DELETE
          MVI   MSG,C' '
          MVC   MSG+1(69),MSG
          MVC   MSG,=C'Request for Add'
          MVC   MSG+18(L'DSN),DSN
          TPUT  MSG,70
          CSVAPF REQUEST=ADD,DSNAME=DSN,VOLUME=VOLSER,VOLTYPE=ANY,     X
               RETCODE=RETCODE,RSNCODE=RESCODE
          LTR  R15,R15
          BNZ  ERR_APF_ADD
          LOCATE INDSAB
          LTR   R15,R15
          BZ    RETURN
          MVI   MSG,C' '
          MVC   MSG+1(69),MSG
          MVC   MSG,=C'Warning dataset is not found'
          TPUT  MSG,70
          B     RETURN
TST_DELETE CLC  =C'DELETE',OPTION
          BNE   TST_LIST
          MVI   MSG,C' '
          MVC   MSG+1(69),MSG
          MVC   MSG,=C'Request for delete '
          MVC   MSG+19(L'DSN),DSN
          TPUT  MSG,70
          CSVAPF REQUEST=DELETE,DSNAME=DSN,VOLUME=VOLSER,VOLTYPE=ANY,  X
               RETCODE=RETCODE,RSNCODE=RESCODE
          LTR   R15,R15
          BNZ   ERR_APF_DEL
          B     RETURN
TST_LIST  CLC   =C'LIST',OPTION
          BNE   TST_QUERY
          GETMAIN R,LV=32767
          LR    R10,R1
          USING APFHDR,R10
          CSVAPF REQUEST=LIST,ANSAREA=(10),ANSLEN=ANSWLEN,             X
               RETCODE=RETCODE,RSNCODE=RESCODE
          LTR   R15,R15
          BNZ   ERR_LIST
          LA    R11,APFHOFF
          USING APFE,R11
          L     R6,0(,R10)
          LA    R11,X'10'(R10)
MAIN_APF_LIST   EQU *
          MVC   VOLSER,APFEVOLUME
          MVC   DSN,APFEDSNAME
          TPUT  VOLSER,51
          LA    R11,X'38'(R11)
          BCT   R6,MAIN_APF_LIST
          B     RETURN
TST_QUERY B     RETURN
ERROR1    TPUT =C'Parameter error                      ',22
          B     RETURN
ERR_LIST  TPUT =C'Function list error                  ',22
          B     RETURN
ERR_APF_ADD     EQU  *
          CH    R0,=X'0401'
          BNE   OTHER_ADD_ERR
          MVI   MSG,C' '
          MVC   MSG+1(69),MSG
          MVC   MSG,=CL70'APF already contains'
          MVC   MSG+20,DSN
          TPUT  MSG,60
          B     RETURN
OTHER_ADD_ERR   EQU *
          CH    R0,=C'0C01'
          BNE   RETURN
          MVI   MSG,C' '
          MVC   MSG+1(69),MSG
          MVC   MSG,=CL70'APF is not in dynamic format'
          TPUT  MSG,60
          B     RETURN
ERR_APF_DEL     EQU *
          CH    R0,=X'0402'
          BNE   OTHER_DEL_ERR
          MVI   MSG,C' '
          MVC   MSG+1(79),MSG
          MVC   MSG,=CL70'APF does not contain'
          MVC   MSG+22(L'DSN),DSN
          TPUT  MSG,60
          B     RETURN
OTHER_DEL_ERR   EQU *
          CH    R0,=C'0C01'
          BNE   RETURN
          MVI   MSG,C' '
          MVC   MSG+1(79),MSG
          MVC   MSG,=CL70'APF is not in dynamic format'
          TPUT  MSG,60
          B     RETURN
RETURN    MVC   EVALBLOCK_EVSIZE,=F'20'
          MVC   EVALBLOCK_EVLEN,=F'20'
          MVC   EVALBLOCK_EVDATA(4),=C'Done'
          L     R8,4(R13)              Pointer to caller's savearea
          FREEMAIN R,LV=WORKALEN,A=(R13)
          LR    R13,R8
RET       RETURN (14,12),RC=(15)
*------------------------------------------------------------------*
*DECODE_OPT:                                                       *
* Check OPT= parameter for QUERY, ADD, or DELETE                   *
*------------------------------------------------------------------*
DECODE_OPT EQU *
          MVC   OPTION,=6C' '
          L     R4,ARGTABLE_ARGSTRING_LENGTH
*         TPUT  (R9),(R4)
          SH    R4,=H'5'
          EX    R4,MOV_OPT
          B     SKIP_MOVE_OPT
MOV_OPT   MVC   OPTION(0),4(R9)
SKIP_MOVE_OPT   EQU *
          BR    R7
*------------------------------------------------------------------*
*DECODE_VOL:                                                       *
* Check VOL=xxx parameter                                          *
*------------------------------------------------------------------*
DECODE_VOL      EQU *
          MVC   VOLSER,=6C' '
          L     R4,ARGTABLE_ARGSTRING_LENGTH
*         TPUT  (R9),(R4)
          SH    R4,=H'5'
          EX    R4,MOV_VOL
          B     SKIP_MOVE_VOL
MOV_VOL   MVC   VOLSER(0),4(R9)
SKIP_MOVE_VOL   EQU *
          BR    R7
*------------------------------------------------------------------*
*DECODE_DSN                                                        *
* Check DSNAME=XXX parameter                                       *
*------------------------------------------------------------------*
DECODE_DSN      EQU *
          MVC   DSN,=44C' '
          L     R4,ARGTABLE_ARGSTRING_LENGTH
*         TPUT  (R9),(R4)
          SH    R4,=H'5'
          EX    R4,MOV_DSN
          B     SKIP_MOVE_DSN
MOV_DSN   MVC   DSN(0),4(R9)
SKIP_MOVE_DSN   EQU *
          BR    R7
          LTORG
          DS    0D
          DS    0D
ANSWLEN   DC    F'32767'
EPADR     DS    F
RETCODE   DS    F
RESCODE   DS    F
OPTION    DS    CL6
VOLSER    DS    CL6
          DC    C' '
INDSAB    CAMLST NAME,DSN,,LOCAREA
DSN       DS    CL44
LOCAREA   DS    0D
          DS    256C
MSG       DS    CL70
WORKAREA  DSECT
SAVEAREA  DS    18F
VALID     DS    F
MESSAGE   DS    CL20
PARM      DS    CL80
          DS    0D
FUNCT     DS    CL8
VARNAME   DS    CL8
VARVAL    DS    CL8
WORKALEN  EQU   *-WORKAREA
          IRXSHVB
          IEFJSCVT
          CVT  DSECT=YES
          IEFJESCT
          IRXARGTB
          IRXEVALB
          IRXEFPL
          CSVAPFAA DSECT=YES,APFHDR=YES,APFE=YES
          END


François Xavier Bessuges
Systems Engineer (France)             c Xephon 1996

