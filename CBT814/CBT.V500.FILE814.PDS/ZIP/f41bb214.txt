A program to empty a PDS
There is often a need to empty a Partitioned Data Set (PDS).  This may be caused
by testing, when you want to start over again, or during a full SYSGEN, when you
wish to copy your existing target library datasets and keep their allocation but
not their contents.  This program will allow you to delete all the directory
entries.
The program issues several data management macros to perform the deletes.  Input
is just the dataset name on the DD card of PDS.  Output is an empty PDS and a
message to the SYSPRINT DD card.  The program issues one of the following
messages based upon the execution of the job:
¥    PDS now contains no directory entries
¥    PDS was already empty
¥    Stow error
¥    More than 12 000 members in PDS, table size exceeded - program must be
changed.
PDSCLEAR SOURCE CODE
PDSCLEAR TITLE ' EMPTY A PDS'
***           PROGRAM: PDSCLEAR
* FUNCTION: THIS PROGRAM WILL EMPTY ALL DIRECTORY ENTRIES FROM A PDS.
*                *** BE CAREFUL WITH THIS ***
* INPUT: DDNAME PDS - POINTS TO PDS TO EMPTY.
* OUTPUT: EMPTY PDS.
*         MESSAGES IN DDNAME SYSPRINT.
* EXECUTION PARAMETERS:
*     //EMPTY    EXEC PGM=PDSCLEAR
*     //PDS      DD   DSN=PDS.TO.BE.EMPTIED,DISP=SHR
*     //SYSPRINT DD   SYSOUT=A
* FEATURES: STOW, BPAM
* DOCUMENTATION: GC26-4057 DATA MAN. MACRO INSTRUCTIONS
         INTRO BASEREG=R2,AUTHOR=DONDUNN,PROGNAM=PDSCLEAR,            +
               VERSION=1  (see issue 11 pp 8-12)
         OPEN  (DIRIN,INPUT)
         OPEN  (SYSPRINT,OUTPUT)    OPEN OUTPUT DATA SET.
         L     R3,=A(MEMBERS)       R3 = ADDR OF PDS MEMBER TABLE.
         LR    R6,R3                A START POINTER.
A110     GET   DIRIN                READ 256 BYTE DIRECTORY BLOCK.
         LH    R4,0(R1)             R4 = LL FIELD.
         SH    R4,=H'2'             R4 = LENGTH OF DIRECTORY DATA.
         LA    R5,2(R1)             R5 = ADDR OF FIRST DIRECTORY ENTRY.
A120     CLC   0(8,R5),=8X'FF'      HAVE WE REACHED END OF DIRECTORY --
         BE    A130                 YES
         MVC   0(8,R3),0(R5)        MOVE MEMBER NAME TO MEMBER TABLE.
         LA    R3,8(R3)
         C     R3,=A(MEMBEND)       HAS THE MEMBER TABLE FILLED UP --
         BE    TABFULL               -YES, TELL ERROR.
         SR    R1,R1
         NI    11(R5),X'1F'         TURN OFF ALL BUT ENTRY LENGTH.
         IC    R1,11(R5)            R1 = NUMBER OF USER HALF WORDS.
         SLL   R1,1                 R1 = NUMBER OF USER BYTES.
         LA    R1,12(R1)            R1 = LENGTH OF THIS DIRECTORY ENTRY
         AR    R5,R1                R5 = POINTER TO NEXT ENTRY.
         SR    R4,R1
         BNZ   A120                 BRANCH IF MORE ENTRIES.
         B     A110                 NO MORE ENTRIES IN THIS RECORD.
A130     CR    R3,R6                ALREADY EMPTY?
         BE    EMPTY
         S     R3,=F'8'             BUMP POINTER BACK ONE ENTRY.
         ST    R3,TABEND            STORE ADDR OF MEMBER TABLE END.
         CLOSE DIRIN
         FREEPOOL DIRIN
         OPEN  (DIROUT,OUTPUT)     DELETE FROM BOTTOM UP IS FASTER
         L     R3,TABEND           BOTTOM OF TABLE (LOGICAL BOTTOM)
         LA    R4,MEMBERS          TOP OF TABLE
         LA    R5,8                LENGTH OF AN ENTRY
AGAIN    STOW  DIROUT,0(R3),D      DELETE ONE
         LTR   R15,R15             SUCCESSFUL?
         BNZ   STOWEROR              NO, GO AWAY
         SR    R3,R5                 YES, DECREMENT MEMBER POINTER
         CR    R3,R4               END OF TABLE?
         BHE   AGAIN                 NO, REPEAT
         PUT   SYSPRINT,MESSAGE4 HAPPY ENDING MESSAGE
         B     THEEND
TABFULL  PUT   SYSPRINT,MESSAGE2    TELL'M PROBLEMS
         PUT   SYSPRINT,MESSAGE3
         B     THEEND
STOWEROR PUT   SYSPRINT,MESSAGE1
         B     THEEND
EMPTY    PUT   SYSPRINT,MESSAGE5
         CLOSE DIRIN
         FREEPOOL DIRIN
         B     DONE
THEEND   CLOSE DIROUT
DONE     CLOSE SYSPRINT
         EXIT
*======= DATA AND STUFF ==============================================
DIRIN    DCB   DDNAME=PDS,                                             +
               DSORG=PS,                                               +
               EODAD=A130,                                             +
               LRECL=256,                                              +
               BLKSIZE=256,                                            +
               RECFM=F,
               MACRF=GL
DIROUT   DCB   DDNAME=PDS,                                             +
               DSORG=PO,                                               +
               EODAD=THEEND,                                           +
               MACRF=W
SYSPRINT DCB   DDNAME=SYSPRINT,                                        +
               LRECL=90,                                               +
               BLKSIZE=90,                                             +
               RECFM=F,                                                +
               MACRF=(PM),
               DSORG=PS
MESSAGE1 DC  CL90'STOW ERROR'
MESSAGE2 DC  CL90'MORE THAN 12,000 MEMBERS IN PDS, TABLE SIZE EXCEEDED'
MESSAGE3 DC  CL90'PROGRAM MUST BE CHANGED.'
MESSAGE4 DC  CL90'PDS NOW CONTAINS NO DIRECTORY ENTRIES'
MESSAGE5 DC  CL90'PDS WAS ALREADY EMPTY'
         LTORG
TABEND   DS    A
MEMBERS  DS    1200CL80             TABLE OF MEMBERS NAMES.
MEMBEND  EQU   *
         DCBD  DSORG=PO,DEVD=DA
         END


Donald Dunn
Senior Software Specialist
Sacramento Municipal Utility District (USA)  © Xephon 1988














