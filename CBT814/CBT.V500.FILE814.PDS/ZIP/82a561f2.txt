Recovering PDS members (continued)

This month, we conclude this article with the publication of the
remaining source code for the program, PDSHOLES.
*        ERROR SECTION
PLDABORT WTO   'SPP0801I.. SYSPRINT DDCARD OMITTED',DESC=1,            X
               ROUTCDE=(2,11)
         LA    R2,4                      SET A NASTY R.C.
         B     PDLCLOSE                EXIT
PDLOMMIT MVC   PDLCONTL(25),=C'- SYSUT1 DD CARD OMITTED'
PDL4WRIT LA    R2,4                      SET A NASTY R.C.
PDLXWRIT PUT   PDLPRINT,PDLCONTL
         B     PDLERRXT(R2)             WAY OUT CODE
PDLERRXT B     PDLABEND                ABORT
         B     PDLCLOSE                WAY OUT
         B     PDLFREE                 CORE FREE FIRST
PDLCFAIL MVC   PDLCONTL(27),=C'- UNABLE TO GET WORK TABLE.'
         B     PDL4WRIT
PDLOFAIL MVC   PDLCONTL(27),=C'- FAILURE IN SYSLIB OBTAIN.'
         B     PDL4WRIT
PDLOVFLW MVC   PDLCONTL(27),=C'- INCORE TABLE OVER FLOWED.'
         LA    R2,8
         B     PDLXWRIT
PDLABEND OI    PDLOPEN1,X'80'          SET END ZONE
         CLOSE ,MF=(E,PDLOPEN1)        CLOSE PRINTER
         ABEND  90,DUMP                DUMP
PDLPACK  PACK  PDLWORK,2(1,R3)        GET PARM VALUE
*        DATA  STATEMENTS
PDLOPEN1 OPEN  (PDLPRINT,(OUTPUT),PDLRFILE,(INPUT)),MF=L
PDLOPEN2 EQU   PDLOPEN1+4
PDLHOLD  DC    F'0'
PDLTABLE DC    A(0)                   ADDRESS FOR TABLE STORE
         DC    A(PDLNUMB1)
PDLCORE1 GETMAIN EC,LV=&BUFNO*TABSIZE+20*1024,A=PDLHOLD,MF=L
PAGENUMB DC    PL2'1'                  PAGE COUNTER
LINENUMB DC    PL2'0',C' '
PDLCONTL DC    CL121' '                PRINT LINE
         ORG   PDLCONTL+1
PDLTRACK DS    CL8
PDLNAME  DS    CL8,C
PDLDDATA DS    CL80
         ORG
HDRLINE  DC    C'1'                    PAGE HEADING
         DC    20C' '
HDRDATE  DC    8C'X',C' VOL='
HDRVOL   DS    CL6                      VOLUME WE'RE USING
         DC    C' DSN='
HDRDSN   DS    CL44                     DSNAME WE'RE USING
         DC    C' PAGE '
HDRPAGE  DC    C'00',C'    '            PAGE NUMBER SPACE
         DC    20C' '
SUBTITLE DC    CL121'0 TTR   MEMBER  CCHHR/OFFSET OR DATA FROM MEMBER'
PDLXTABL DC    C'0123456789ABCDEF'      HEX TRANSLATE TABLE
PDLFLAG  DC    B'00000000'            FLAG BYTE
EOFED    EQU   B'00000001'            LAST READ CAUSED EOF
*        SKELETON READ
         READ  PDLREAD,SF,PDLRFILE,MF=L
PDLBLOCK EQU   PDLREAD+12            BUFFER POINTER FOR F
PDLNUMB1 DC    F'4,4'              SORT CONTROLS
PDLNUMB2 DC    A(0,14,2,13)            NUMBER OF ENTRIES
ALLONES  DC    14X'FFFF'                      HIGH VALUE TERMINATOR
PDLWORK  DS    D
DSCBAREA DS    37F                       OBTAINS DSCB AREA
DSCBTTRL EQU   DSCBAREA+54         END TTRC IN FORMAT 1
DSCBREAD CAMLST SEARCH,JFCB,JFCB+118,DSCBAREA
JFCB     DS    44F                      SPACE FOR JFCB WHEN READ
JREAD    DC    X'07',AL3(JFCB)
         DC    X'85',AL3(*+3)
*        DCB OPEN EXIT "SYSUT1"
         USING *,R15
         LA    R0,256                    MINIMUM BUFER
         CH    R0,DCBBLSKI-IHADCB(,R1) SMALLER THAN 256
         BNHR  R14                 NO
         STH   R0,DCBBLKSI-IHADCB(,R1) ENSURE BIG ENUF
         BR    R14                 OUT
         DROP  R15
*        DCB OPEN EXIT "SYSPRINT"
*              MAKE BLOCK SIZE = LRECL
PDLFILS  DC    0F'0',X'85',AL3(*+3)
         OC    DCBBLKSI-IHADCB(2,R1),DCBBLKSI-IHADCB(R1) BLOCK SIZE SET
         BNZR  R14                     YES
         MVC   DCBBLKSI-IHADCB(2,R1),DCBLRECL-IHADCB(R1) GET LRECL OVER
         BR    R14                      GO BACK
PDLPRINT DCB   MACRF=PM,LRECL=121,RECFM=FBA,DSORG=PS,DDNAME=SYSPRINT,  X
               EXLST=PDLFILS
PDLRFILE DCB   DSORG=PO,MACRF=R,DDNAME=SYSLIB,RECFM=U,EODAD=PDLREOF,   X
               EXLST=JREAD,SYNAD=PDLOOPS
         USING *,R15
PDLOOPS  SYNADAF ACSMETH=BPAM
         MVC   ERRORBUF,4(R1)    HOLD ERROR MESSAGE
         SYNADRLS ,
         BR    R14                     RETURN
         DROP R15
ERRORBUF DS    CL124                   COPY OF ERROR MSG
         LTORG ,
*        FORMAT OF WORK AREAS
TAB      DSECT ,                      TABLE ENTRY
TABLINK  DS    A                 LINK POINTER
TABTTR   DS    AL3
TABFLAG  DS    B
ALFLAG   EQU   64
DATAFLAG EQU   128
TABNAME  DS    CL8
TABDATA  DS    CL80
TABNEXT  DS    C
TABSIZE  EQU   TABNEXT-TAB
MEMB     DSECT ,                     DIRECTORY ENTRY
MEMBNAME DS    CL8
MEMBTTR  DS    AL3
MEMBFLAG DS    B                     CONTROL BYTE
ALIAS    EQU   X'80'
MEMBNEXT DS    X
*        THIS MODULE FILLS THE AREA POINTED TO BY THE 1ST ADDRESS
*              LIST WORD WITH AN 8 BYTE DATE  DD/MM/YY VIA TIME MACRO
* THIS DSECT DESCRIBES THE FORMAT OF A TABLE ENTRY
#TAB     DSECT
         USING *,8
$TABPREV DS    PL2                      THIS MONTH START DATE
$TABDATE DS    PL2                      NEXT MONTH START DATE
*  THIS DSECT DESCRIBES THE FORMAT OF THE RECEIVING AREA
$DAT     DSECT
$DATDATE DS    CL2                      SPACE FOR DAY
         DS    C                        SLASH SPACE
$DATMNTH DS    CL2                      ONES DIGIT
         DS    C                        SLASH SPACE
$DATYEAR DS    CL2                      YEAR
$SAV     DSECT
         USING *,13
$SAVFWR1 DS    0F .                     FIRST WORD OF SAVE AREA
$SAVHWR1 DS    0H
         DS    C
$SAVBYT2 DS    C
$SAVHWR2 DS    H
         DS    3F
$SAVDWR1 DS    D
SPM09DAT CSECT
         SAVE  (14,12),,*
         BALR  12,0
         USING *,12
         USING $DAT,9                  BASE
         L     9,0(0,1)                BARE $DATE DSECT
         XC    $DAT(8),$DAT            CLEAR DATE FIELD
         TIME  BIN                     GET THE ITEM
         ST    1,$SAVFWR1              STORE THE DATE
         MVO   $SAVHWR1,$SAVBYT2       MOVE UP YEAR BY 1/2 BYTE
         OI    $SAVBYT2,X'0F'          INSERT SIGN BITS
         CP    $SAVHWR1,=P'0'          YEAR = 0 IE LEAP YEAR
         BE    CDATLPYR                YES
CDATLTST MVC   $SAVDWR1+1(2),$SAVHWR1  MOVE YEAR TO WORK AREA
         MVI   $SAVDWR1,0              ZEROISE TOP BYTE
         DP    $SAVDWR1(3),=P'4'       DIVIDE BY 4
         CP    $SAVDWR1+2(1),=P'0'     LEAP YEAR
         BE    CDATLPYR                YES
*  SCAN TABLE
         LA    8,CDATABL1              NORMAL YEAR TABLE
         B     CDATEST                 GO TEST
CDATLPYR LA    8,CDATABL2              TABLE 2 LEAP YEARS
CDATEST  LA    3,12
CDATLOOP CP    $SAVHWR2,$TABDATE       DATE LESS THAN END OF THIS
         BNH   CDATCOMP                YES                 MONTH
         LA    8,$TABDATE              NO UP POINTER
         BCT   3,CDATLOOP
         AP    $SAVHWR1,=P'1'          *    NEXT YEAR
         LA    3,12
*  COMPLETION ROUTINE
CDATCOMP SP    $SAVHWR2,$TABPREV
         LA    7,13                    * NOTE R3 CONTAINS THE NUMBER
         SR    7,3                     * OF MONTHS LEFT IN YEAR
         CVD   7,$SAVDWR1
         UNPK  $DATMNTH,$SAVDWR1+6(2)  UNPACK MONTH CODE
* COMPLETE REMAINDER OF DATE AND RETURN
CDATEND  UNPK  $DATDATE,$SAVHWR2       UNPACK DATE
         UNPK  $DATYEAR,$SAVHWR1       UNPACK YEAR           ( SLASHES
         OC    $DAT(8),=C'00/00/00'    ENSURE CORRECT ZONES AND INSERT
         LR    1,9                     RESTORE R1
         L     14,12(0,13)             RESTORE 14 FROM SAVE AREA
         LM    2,12,28(13)             RESTORE REMAINING REG.
         BR    14                      RETURN
         LTORG
         DROP  9,12
CDATABL1 DC    PL2'000'                NORMAL YEAR
         DC    PL2'031'
         DC    PL2'059'
         DC    PL2'090'
         DC    PL2'120'
         DC    PL2'151'
         DC    PL2'181'
         DC    PL2'212'
         DC    PL2'243'
         DC    PL2'273'
         DC    PL2'304'
         DC    PL2'334'
         DC    PL2'365'
CDATABL2 DC    PL2'000'                LEAP YEAR
         DC    PL2'031'
         DC    PL2'060'
         DC    PL2'091'
         DC    PL2'121'
         DC    PL2'152'
         DC    PL2'182'
         DC    PL2'213'
         DC    PL2'244'
         DC    PL2'274'
         DC    PL2'305'
         DC    PL2'335'
         DC    PL2'366'
         TITLE 'SPM39TAG CHAINED BUFFER SORT '
*        THIS MODULE IS FOR SORTING A SET OF DATA ITEMS      IN
*      BUFFERS WHICH HAVE A LINK ADDRESS IN THE FIRST FOUR BYTES OF
*      EACH BUFFER TO THE NEXT.ALL BUFFERS MUST BE ON FULL-WORD
*      BOUNDARIES AND ONLY ADDRESS INFORMATION SHOULD BE IN THE LINK
*      POINTERS AS IN THE SORT THESE FIELDS ARE CHANGED SO THAT AT
*      THE END THE BUFFERS ARE CHAINED IN THE CORRECT ORDER.
*      THE PARAMETERS ARE        1) ADDRESS OF BEGINNING OF CHAIN
*                                2) ADDRESS OF CONTROL INFORMATION
*                  CONTROL INFORMATION (IN TWO ADJACENT FULL-WORDS)
*                  1) RELATIVE DISPLACEMENT OF FIELD START IN BUFFER
*                                          INCLUDING LINK FIELD
*                  2) KEY-FIELD LENGTH    (-VE FOR DECENDING SORT)
*          IE  IF FIELD  IS JUST AFTER BUFFER LINK AND 8 BYTES LONG
*                          DC F'4,8' IS THE CONTROL DATA
*        ON  EACH PASS  THE HIGHEST PROCEEDS TO THE END WHERE IT IS
*        RECHAINED TO THE HIGH Q, AND THE LOWEST IS ALSO FOUND THIS
*        IS THEN MOVED TO THE HEAD OF THE Q AND THE Q HEAD  POINTER
*        ALTERED TO POINT TO IT. THUS IN EACH PASS THE NUMBER OF
*        BUFFERS TO CHECK REDUCES BY TWO EACH PASS
*        DSECTS USED IN BASING WORK-LINKS
$LOW     DSECT
         USING *,R2
$LOWLINK DS    A
         USING *,R14
$QLINKS  DS
$Q       DSECT
         USING *,R10
$QLINK   DS    A
$R1      DSECT
         USING *,R11
$R1LINK  DS    A
         USING *,R6
$R1FIELD DS    C
$R2      DSECT
         USING *,R12
$R2LINK  DS    A
         USING *,R7
$R2FIELD DS    C
ASCEND   EQU  64
DESCEND  EQU  32
SPM39TAG CSECT ,
         USING *,R15
TAGSORT  SAVE  (14,12),,*
         ENTRY TAGSORT
         LR    R12,R13 .               HOLD OLD SAVE AREA
         L     R2,4(,R1)               LOCATE CONTROLS
         LM    R3,R4,0(R2)             PICK UP CONTROL VALUES
         LA    R5,ASCEND               ASCENDING ASSUMED
         LTR   R4,R4                   OK
         BP    TAG010                  YES
         BZ    TAGFAIL1                ERROR ZERO KEY
         LPR   R4,R4                   RESET KEY POSITIVE
         LA    R5,DESCEND              AND MARK ORDER
TAG010   BCTR  R4,0                    RESET FOR EXECUTED COMPARE
         SR    R9,R9                   SORTED Q POINTER
         LR    R14,R1
TAG020   LR    R11,R14                 LOCATE LOW CHAIN POINTER
         LR    R2,R14                  SET LOW RECORD POINTER
         SR    R8,R8                   CLEAR  SWOP MARKER
TAG030   LR    R10,R11
         L     R11,$QLINK              LOCATE  FIRST BUFFER
         LTR   R11,R11                 CHECK
         BZ    TAGFAIL2                NO LOGIC OR CHAIN FAULT
         L     R12,$R1LINK             LOCATE  NEXT BUFFER
         LTR   R12,R12                 CHECK
         BZ    TAG040                  CHAIN END
         LA    R6,$R1LINK(R3)          LOCATE KEY1
         LA    R7,$R2LINK(R3)          LOCATE KEY2
         EX    R4,TAGCOMP              COMPARE
         BE    TAG030                  SAME IGNORE
         EX    R5,TAGJUMP              TEST RESULT WILL GO TO TAG030
         LA    R8,1                    SET SWOP MARK
         ST    R12,$QLINK
         MVC   $R1LINK,$R2LINK
         ST    R11,$R2LINK
         LR    R11,R12               RESET  NEXT  Q POINTER
         LR    R6,R7
         B     TAG060
TAG040   LTR   R8,R8                   DID A SWOP OCCUR
         BZ    TAG050                  NO
         ST    R9,$R1LINK              NOW REVISE END OF CHAIN
         L     R9,$QLINK               MOVING TO SORTED CHAIN
         XC    $QLINK,$QLINK           AND FIX CHAIN END
*        SHIFT  LOWEST  TO  INITIAL CHAIN
         L     R11,$LOWLINK            LOCATE LOW BUFFER
         MVC   $LOWLINK,$R1LINK        REMOVE FROM CURRENT POSN
         MVC   $R1LINK,$QLINKS         SWITCH POINTERS
         ST    R11,$QLINKS             LOWEST NOW CORRECT
         LR    R14,R11                 POINT TO CURRENT LOW
         L     R7,$R1LINK
         LTR   R7,R7               HAS LINK END BEEN REACHED
         BNZ   TAG020                  NO
TAG050   ST    R9,$R1LINK              RECONNECT CHAIN
         RETURN (14,12),RC=0           AND RETURN
TAG060   L     R7,$LOWLINK             LOCATE CURRENT PASS LOWEST
         LA    R7,$R2FIELD(R3)         THUS RECORD FIELD
         EX    R4,TAGCOMP              GOT IT
         EX    R5,TAGJUMP2             JUMPS TO TAG070 IF SUCCESFULL
         B     TAG030                  IGNORE
*        FIND LOWEST RECORD
TAG070   LR    R2,R10                  LOW LINK
         B     TAG030
TAGFAIL1 ABEND 391,DUMP
TAGFAIL2 ABEND 392,DUMP
TAGCOMP  CLC   $R1FIELD,$R2FIELD       COMPARISON
TAGJUMP  NOP   TAG060
TAGJUMP2 NOP   TAG0070
         LTORG ,
         DCBD  DEVD=DA,DSORG=PS
         END   ,


Jeffrey Alan Groves
Software Co-ordinator
Thomson Holidays (UK) © Thomson Holidays 1989

