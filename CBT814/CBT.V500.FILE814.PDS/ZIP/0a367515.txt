Sending messages under MVS

The following program was written to try to simplify an operator's life.  It is
a console-started task invoked by the operator when needed.

The SENDTO program will send a message at multiple times to a designated user or
MVS console.  It was written to avoid having to repeat SEND commands from the
console.  The message text allowed is a maximum of 70 bytes but may be modified
easily.  The syntax of the commands is:

S SENDTO.CN04,MSG='ANY TEXT YOU WISH UP TO 70 BYTES'
S SENDTO.tsouser,MSG='ANY TEXT YOU WISH UP TO 70 BYTES'
S SENDTO.BRODCAST,MSG='ANY TEXT YOU WISH UP TO 70 BYTES'
S SENDTO.ALL,MSG='ANY TEXT YOU WISH UP TO 70 BYTES'

The step name coded in the MVS START command is used to indicate where the
message should be sent.  In the following routine, the format S SENDTO.CNxx is
used to send messages to an MVS console where xx is the two-digit console-id.
The format S SENDTO.tsouser is used to send messages to the specified TSO user
where tsouser is the individual TSO user-id.  The form S SENDTO.BRODCAST is used
to send messages to all MVS consoles and the S SENDTO.ALL form is used to send
the message to all logged on TSO users.

Some uses for SENDTO are to avoid having to re-type a message or hit the DUP key
on the console to repeat it.  We also use the console form to send messages to
our tape library, when requested tapes are not on the computer room floor,
asking the librarian to pull the tapes and bring them out.  It is also used in
the 'all TSO users' form to send notifications of problems or upcoming system
outages to all TSO users.

This program must be assembled and link-edited into either an APF-authorised
library in the LINKLIST or in a load library, that can be pointed to by a
STEPLIB DD card.


SOURCE CODE FOR SENDTO

SENDTO   CSECT                     ESTABLISH CSECT
         SAVE  (14,12),,SENDTO-&SYSDATE
CVTPTR   EQU   16
         LR    R12,R15            LOAD R12 W/EPA ADDRESS
         USING SENDTO,R12         ESTABLISH ADDRESSABILITY TO CSECT
         LA    R8,SAVEAREA        LOAD ADDR OF MY S/A
         ST    R8,8(,R13)         ST MY S/A ADDR IN CALLERS S/A
         ST    R13,4(,R8)         ST CALLERS S/A ADDR IN MY S/A
         LR    R13,R8             LOAD ADDR OF MY S/A IN R13
         SR    R3,R3              CLEAR R3
         L     R2,0(R1)           LOAD R2 W/A(PARM LIST)
         LH    R3,0(R2)           LOAD R3 W/LENGTH OF PARM LIST
         LTR   R3,R3              IS THERE ANY PARM
         BZ    ERROR              NO, THEN ISSUE MESSAGE
         C     R3,MAXPARM         IS PARM TOO LONG
         BH    ERROR              YESY, THEN ISSUE MESSAGE
         BCTR  R3,R0              DECREMENT R3 BY 1
         EX    R3,MVCMSG          EXECUTE MVC INSTRUCTION
         L     R1,16              LOAD R1 W/A(CVT)
         L     R1,0(R1)           LOAD R1 W/A(OLD-NEW POINTERS)
         L     R1,4(R1)           LOAD R1 W/A(TCB)
         L     R1,12(R1)          LOAD R1 W/(TIOT)
         MVC   STEPNAME,8(R1)     MOVE STEPNAME
         CLC   CONSPREF,STEPNAME  DOES STEPNAME BEGIN WITH 'CN'
         BNE   SETUSER            YES, GO CHECK CONSOLE NUMBER
         CLI   STEPNAME+2,C'0'    IS IT LESS THAN 0
         BL    SETUSER            YES, ASSUME USERID/ALL
         CLI   STEPNAME+2,C'9'    IS IT GREATER THAN 9
         BH    SETUSER            YES, ASSUME USERID/ALL
         CLI   STEPNAME+3,C'0'    IS IT LESS THAN 0
         BL    SETUSER            YES, ASSUME USERID/ALL
         CLI   STEPNAME+3,C'9'    IS IT GREATER THAN 9
         BH    SETUSER            YES, ASSUME USERID/ALL
         CLC   STEPNAME+4(4),BLANKS IS REST OF IT BLANK
         BNE   SETUSER            NO, ASSUME USERID/ALL
         MVC   COMMAND+84(2),STEPNAME+2 MOVE CONSOLE NUMBER TO MESSAGE
         B     SVCMSG             BRANCH TO ISSUE MESSAGES
         CLC   BRODCAST,STEPNAME  IS IT FOR ALL CONSOLES
         BNE   SETUSER            NO, ASSUME USERID/ALL
         MVC   COMMAND+80(L'BRDCAST),BRDCAST MOVE BROADCAST KEYWORD
         B     SVCMSG             BRANCH TO ISSUE MESSAGES
SETUSER  CLC   ALL,STEPNAME       IS THIS FOR ALL USERS
         BNE   ONEUSER            NO, GO SET UP FOR ONE USERID
         MVC   COMMAND+79(6),BLANKS ASSUME ALL USERIDS
         B     SVCMSG             BRANCH TO ISSUE MESSAGES
ONEUSER  MVC   COMMAND+80(L'USER),USER ASSUME USERID
         MVC   COMMAND+86(8),STEPNAME MOVE STEPNAME TO MESSAGE
         LA    R2,7               LOAD R2 W/NUMBER OF TIMES TO LOOP
         LA    R1,COMMAND+86      LOAD R2 W/A(USERID) IN COMMAND
PARNLOOP CLI   0(R1),C' '         END OF USERID
         BE    SETPAREN           INSERT RIGHT PAREN
         LA    R1,1(R1)           BUMP TO NEXT BYTE
         BCT   R2,PARNLOOP        GO CHECK NEXT
SETPAREN MVI   0(R1),C')'         MOVE PAREN TO MESSAGE
SVCMSG   L     R2,NUMLOOPS        LOAD R2 W/NUMBER OF TIMES TO LOOP
         MODESET KEY=ZERO,MODE=SUP GET AUTHORIZED
MSGLOOP  LA    R1,COMMAND         LOAD R1 W/ADDR OF COMMAND
         L     R0,REG0            LOAD R0 W/ADDR OF ASID
         SVC   34                 ISSUE SVC
         BCT   R2,MSGLOOP         ISSUE THE MESSAGE AGAIN
         MODESET KEY=NZERO,MODE=PROB GET UNAUTHORIZED
EXIT     L     R13,SAVEAREA+4     RELOAD R13 W/ADDR OF CALLERS S/A
         RETURN (14,12),RC=0      RETURN TO OS WITH RETCODE=0
ERROR    WTO   'MSG001I PARM FIELD MISSING OR OVER 70 CHARACTERS, MESSAX
               GE IGNORED',ROUTCDE=(1,2,11)
         B     EXIT
SAVEAREA DC    18F'0'             OS SAVE AREA
NUMLOOPS DC    F'20'              NUMBER OF TIMES TO ISSUE MESSAGE
MAXPARM  DC    F'70'              MAXIMUM PARM LENGTH
REG0     DS    0F,F'0'            WORKAREA FOR SVC 34
MVCMSG   MVC   COMMAND+8(0),2(R2) EXECUTED MVC
*   COMMAND IS MAX OF 70 CHARACTERS LONG AND IS STORED INTO COMMAND+4
*                    12345678901234567892123456789312345678941234567895
COMMAND  WTO   'SE ''                                                  X
                                   '',CN=08          ',ROUTCDE=11,MF=L
*              12345678961234567897
USER     DC    C'USER=(******** '
CONSPREF DC    C'CN'              CONSOLE STEPNAME PREFIX
ALL      DC    C'ALL '            ALL USERIDS STEPNAME
BRODCAST DC    C'BRODCAST'        ALL CONSOLES STEPNAME
STEPNAME DC    CL8' '             STORAGE FOR STEPNAME
BLANKS   DC    CL8' '
         END


SAMPLE PROCEDURE TO INVOKE SENDTO

//SENDTO    PROC MSG=
//SENDTO    EXEC PGM=SENDTO,PARM='&MSG'
//SYSABEND  DD SYSOUT=Q


Bruce Bordonaro
System Software Manager
EDS (USA) © EDS 1987


































































