A TSO MVS console


MVSCONS REXX EXEC

/* REXX --------------------------------------------------------------*/
/* Program : MVSCONS (REXX EXEC)                                      */
/* Comments: Rexx/ISPF full screen console                            */
/* Details : See ISPF help panel MVSCONSH                             */
/* This exec takes one argument, the number of lines to keep when you */
/* leave. If not numeric, it defaults to 0. The lines are saved in    */
/* the dataset allocated to MVSCONS, if available. If you specify 0   */
/* the facility is disabled (default).                                */
/*--------------------------------------------------------------------*/
Trace 'O'
Arg maxlines                              /* Maximum to keep on exit  */
Address ISPEXEC
'CONTROL ERRORS RETURN'
Call SET_UP maxlines                      /* Get started              */
Call GET_OLD                              /* Get any old data         */
Call GET_NEW_DATA                         /* Look for more data       */
line.cur = max(0,line.0-scr.depth+1)      /* Position to last page    */
Call DO_DISPLAY zcmd                      /* Show panel               */
Do while result = 0                       /* While still ok           */
   line.old = line.0
   Select
      When substr(scr.cmd,1,1) = '*'      /* Comment line             */
      Then Call WRITE_LINE scr.cmd
      When substr(scr.cmd,1,1) = '-'      /* Locate previous          */
      Then Call LOCATE_UP
      When substr(scr.cmd,1,1) = '+'      /* Locate next              */
      Then Call LOCATE_DOWN
      When word(scr.cmd,1) = '!'          /* Process wait             */
      Then Call AUTO_UPDATE
      When scr.cmd ^= '' then do          /* If command entered       */
         Address CONSOLE scr.cmd          /* then issue it to MVS     */
         Call WRITE_LINE '>'scr.cmd
      End
      Otherwise nop
   End
   Call GET_NEW_DATA                      /* Get any more data        */
   Call SET_CURLINE                       /* Handle scrolling         */
   Call DO_DISPLAY                        /* Display panel            */
End
Call PUT_OLD                              /* Save for next time       */
If cons.act = 'NO'
Then Address TSO 'CONSOLE DEACTIVATE'     /* End console              */
Exit
DO_DISPLAY:procedure expose line. scr.
parse arg cursor_pos, zedlmsg, lock, refresh
Call SET_DATA                             /* Build screen data        */
zdata = scr.data                          /* Screen variables         */
zcmd = ''
job = scr.jobname
jid = scr.jobid
msg = scr.message
if scr.find > 0 then scr.alarm = 'YES'    /* Sound alarm for find     */
alm = scr.alarm
linecur = line.cur
lineend = line.0
linemax = line.max
if cursor_pos ^= '' then cursor_pos = 'CURSOR('cursor_pos')'
                    Else cursor_pos = 'CURSOR(ZCMD)'
if zedlmsg ^= '' then msgdisp = 'MSG(ISRZ000)'
                 Else msgdisp = ''
if lock ^= '' then 'control display lock'
if refresh ^= '' then do
  'CONTROL DISPLAY REFRESH'
  'CONTROL DISPLAY LINE'
End
'DISPLAY PANEL(MVSCONS)' cursor_pos msgdisp /* Display screen         */
disprc = rc
'VGET (zverb zscrolla zscrolln)'            /* Get scroll info        */
scr.cmd = zcmd                              /* Save screen vars       */
scr.verb = zverb
scr.scrolla = zscrolla
scr.scrolln = zscrolln
scr.v_depth = vdepth
scr.jobname = job
scr.jobid = jid
scr.message = msg
scr.alarm = 'NO'
scr.find = 0
line.max = linemax                          /* New keep value         */
If ^datatype(line.max,'W') | line.max < 0 then line.max = 0
Return disprc
AUTO_UPDATE:procedure expose line. scr.     /* Process wait           */
Parse var scr.cmd . limit time .
If ^datatype(time,'W') | time < 0 then time = 0 /* Ensure valid time  */
If ^datatype(limit,'W') | limit < 0 then limit = 0 /* and limit       */
If scr.jobname ^= '' then jobmsg = ' job' scr.jobname /* Job select?  */
                     Else jobmsg = ''
If scr.jobid ^= '' then jidmsg = ' jobid' scr.jobid /* Jobid select?  */
                   Else jidmsg = ''
If scr.message ^= '' then msgmsg = ' msg' scr.message /* msg select?  */
                     Else msgmsg = ''
jobmsg = jobmsg||jidmsg||msgmsg
if jobmsg ^= '' then jobmsg = 'for'||jobmsg
If time = '0' then do
   ltime = 60                             /* Continuous 60 sec. waits */
   timmsg = ''
End
Else do
   ltime = time - time('R')               /* Time to wait             */
   timmsg = ' of' time 'seconds, '
End
If limit = '0' then limmsg = 'messages'
               Else limmsg = 'of' limit 'messages'
cnt = 0
Do while (ltime > 0,                      /* Time expired?            */
          &(cnt < limit | limit = '0'),   /* Limit reached?           */
          & result ^= 8)                  /* Interrupted?             */
   line.cur = max(0,line.0 - scr.v_depth +1) /* Set data position     */
   if time = 0 then dtime = ''
               Else dtime = ltime
   dmsg = 'Waiting:' dtime||timmsg||cnt limmsg jobmsg
   Call DO_DISPLAY ,dmsg,lock,refresh     /* Display screen           */
   Call GET_NEW_DATA min(ltime,60)        /* Wait for data            */
   If result = 8 then 'CONTROL DISPLAY REFRESH' /* Interupted         */
   If scr.jobname ^= '' then cnt = scr.find + cnt /* Count for job    */
                        Else cnt = line.new + cnt /* Count for any    */
   If time ^= '0' Then ltime = (time - time('E')%1)/* Time left to go */
End
line.cur = max(0,line.0 - scr.v_depth +1) /* Position to end          */
Return
WRITE_LINE:procedure expose line.         /* Write a line to log      */
parse arg dataline
line.0 = line.0 + 1
line = line.0
line.line = dataline
Return
GET_NEW_DATA:procedure expose line. scr.  /* See if any more data     */
Arg time                                  /* available now.           */
lines = line.0                            /* Current count            */
getcode = getmsg(JOB.,,,,time)            /* Get some more            */
Do while getcode = 0                      /* Got more data            */
   call process_lines
   getcode = getmsg(JOB.,,,,0)            /* Look for more data       */
End
line.new = lines - line.0                 /* New line count           */
line.0 = lines                            /* New final line           */
Return getcode
PROCESS_LINES:procedure expose job. line. scr. lines
If job.mdbgalrm = 'YES' then scr.alarm = 'YES' /* Sound alarm         */
mid = job.mdbgmid                                    /* Save msg id   */
If job.mdbgdom = 'YES' & line.xmid.mid.0 > 0 then do /* Remove old    */
   do j = 1 to line.xmid.mid.0                       /* highlight     */
      mline = line.xmid.mid.j                        /* for this      */
      drop line.h.mline                              /* msg id        */
   End
   Drop line.xmid.mid.
End
If job.mdbghold = 'YES' then              /* Held message             */
   line.xmid.mid.0 = job.mdbclcnt         /* Hilight message count    */
Do i = 1 to job.0                         /* Get all lines            */
   lines = lines+1
   If job.mdbghold = 'YES' then do        /* Held message             */
      line.h.lines = 'YES'                /* Flag as highlight and    */
      line.xmid.mid.i = lines             /* keep xref to remove later*/
   End
   If job.mdbcojbn ^= '' then do          /* If we have jobname then  */
      line = job.mdbgtimh job.mdbcojbn job.mdbcojid job.i /* use      */
      If scr.jobname||scr.jobid||scr.message ^= ''
      Then If check_sel(line) = 'OK'      /* Real data                */
           Then scr.find = scr.find + 1   /* Mark as selected job     */
   End
   Else
      line = job.mdbgtimh job.i           /* No jobname               */
   line.lines = line                      /* Save in array            */
End
Return
SET_DATA:procedure expose line. scr.               /* Build the data  */
scr.data = ''                                      /* for dynamic area*/
Do j = line.cur to min(line.cur + scr.depth,line.0+1) /* to look like */
   Select                                          /* individual lines*/
      When j = 0 then do                           /* Top of data     */
         line = line.top
         hi = '03'x
      End
      When j > line.0 then do                      /* Bottom of data  */
         line = line.bot
         hi = '03'x
      End
      Otherwise do
         line = line.j                       /* Real data             */
         Select
            When line.h.j = 'YES'            /* Highlighted line      */
            Then hi = '02'x
            When substr(line,1,1) = '*'      /* Comment               */
            Then hi = '03'x
            When substr(line,1,1) = '>'      /* Line we entered       */
            Then hi = '04'x
            When scr.jobname||scr.jobid||scr.message = ''
            Then hi = '01'x
            When check_sel(line) = 'OK'
            Then hi = '05'x
            Otherwise                        /* Lowlight              */
               hi = '01'x
         End
      End
   End
   scr.data = scr.data||hi||substr(line,line.off,scr.width-1)
End
Return
LOCATE_UP:procedure expose line. scr.       /* Locate prev sel        */
If scr.jobname||scr.jobid||scr.message = '' then return
Do j = line.cur-1 to 0 by -1
   If check_sel(line.j) = 'OK' then do      /* Real data              */
      line.cur = j
      Return
   End
End
Return
LOCATE_DOWN:procedure expose line. scr.     /* Locate next sel        */
If scr.jobname||scr.jobid||scr.message = '' then return
Do j = line.cur +1 to line.0
   If check_sel(line.j) = 'OK' then do      /* Real data              */
      line.cur = j
      Return
   End
End
Return
CHECK_SEL:procedure expose scr.
parse arg line
If substr(line,1,1) = '*',                  /* Comment                */
 | substr(line,1,1) = '>'                   /* Line we entered        */
   Then return 'NG'
job = substr(line,10,8)
jid = substr(line,19,8)
msg = substr(line,29)
parse var msg msg rest
if substr(msg,1,1) = '*',                  /* Looks like reply no     */
 & length(msg) = 3,
 & datatype(substr(msg,2,2),'W')
Then parse var rest msg rest
If verify(substr(msg,1,1),'+@*','M')       /* Strip off specials      */
Then msg = delstr(msg,1,1)
msg = left(msg,8)
If abbrev(job,scr.jobname,0),              /* Matches selection       */
 & abbrev(jid,scr.jobid,0),
 & abbrev(msg,scr.message,0)
Then return 'OK'
Return 'NG'
SET_CURLINE:procedure expose line. scr.   /* Perform scroll           */
Select                                    /* Allow for attribute bytes*/
   When scr.verb = 'DOWN' & scr.scrolla = 'MAX'
   Then line.cur = max(0,line.0 - scr.v_depth +1)  /* Last page       */
   When scr.verb = 'UP' & scr.scrolla = 'MAX'
   Then line.cur = min(line.0,1)                   /* First page      */
   When scr.verb = 'DOWN'                          /* Page down       */
   Then line.cur = min(line.cur + scr.scrolln,line.0 + 1)
   When scr.verb = 'UP'
   Then line.cur = max(line.cur - scr.scrolln,0)   /* Page up         */
   When scr.verb = 'LEFT' & scr.scrolla = 'MAX'
   Then line.off = 1                               /* First column    */
   When scr.verb = 'RIGHT' & scr.scrolla = 'MAX'
   Then line.off = scr.max - (scr.width-1)         /* Last column     */
   When scr.verb = 'LEFT'
   Then line.off = max(line.off - scr.scrolln,1)   /* Page left       */
   When scr.verb = 'RIGHT'                         /* Page Right      */
   Then line.off = min(line.off + scr.scrolln-1,scr.max - (scr.width-1))
   When scr.verb = '' & line.cur = line.old - scr.v_depth + 1
   Then line.cur = max(0,line.0 - scr.v_depth +1)  /* If at page max  */
   Otherwise nop                                   /* already, stay   */
End                                                /* there.          */
Return
GET_OLD:procedure expose line.       /* Get old lines from file       */
If line.max = 0 then return          /* Ignore history                */
Address TSO 'execio * diskr mvscons (finis stem LINE.'
If rc ^= 0 then say 'Unable to read MVSCONS, no old data available'
Return
PUT_OLD:procedure expose line.       /* Save data for later           */
If line.0 <= 0 Then return           /* Nothing to save               */
If line.max = 0 then return          /* Don't want to save            */
If line.max < line.0 then do         /* Too much to save so           */
   j = line.0 - line.max             /* from this line to end         */
   Do i = 1 to line.max              /* we reassign starting from 1   */
      j = j + 1
      line.i = line.j
   End
   line.0 = line.max                 /* This is the new line count    */
End
Address TSO 'execio' line.0 'diskw mvscons (finis stem LINE.'
If rc ^= 0 then say 'Data not saved to MVSCONS'
Return
SET_UP:procedure expose line. scr. cons.
arg maxlines                              /* Lines to keep            */
'PQUERY PANEL(MVSCONS) AREANAME(ZDATA)',  /* Check screen size        */
        'AREATYPE(AREATYPE) WIDTH(SWIDTH)',
        'DEPTH(SDEPTH) ROW(ROW) COLUMN(COLUMN)'
scr. = ''
scr.find = 0                              /* Nothing found yet        */
scr.alarm = NO                            /* No alarm yet             */
scr.depth = sdepth                        /* Save screen size         */
scr.width = swidth
scr.max   = 255
line. = ''
line.0 = 0
line.max = maxlines                       /* Initialize position      */
If ¬datatype(line.max,'W') | line.max < 0 then line.max = 0
line.cur = 0                              /* Start at top line        */
line.old = 0                              /* No lines yet             */
line.off = 1                              /* Start in column 1        */
line.bot = centre('*** Bottom of data ***',scr.width-1)
line.top = centre('*** Top of data ***',scr.width-1)
msg = msg('OFF')
getcode = getmsg(JOB.,SOL,'MVSCONS',,)    /* Test console             */
msg = msg(msg)
If getcode = 12 then do                   /* If console not active,   */
   Address TSO 'CONSOLE ACTIVATE'         /* make it active           */
   cons.act = 'NO'                        /* Remember to deactivate   */
End
Return


MVSCONS ISPF PANEL

)ATTR DEFAULT($+_)
  ? TYPE(OUTPUT) INTENS(HIGH) CAPS(OFF)
  _ TYPE(INPUT)   INTENS(HIGH) CAPS(ON)
   % TYPE(INPUT)   INTENS(HIGH) CAPS(ON) hilite(uscore)
  | AREA(DYNAMIC) SCROLL(ON) EXTEND(ON)
 01 TYPE(dataout) intens(low) CAPS(OFF)
 02 TYPE(dataOUT) INTENS(HIGH) CAPS(OFF)
 03 TYPE(dataOUT) INTENS(LOW) CAPS(OFF) COLOR(GREEN)
 04 TYPE(dataOUT) INTENS(HIGH) CAPS(OFF) COLOR(RED)
 05 TYPE(dataOUT) INTENS(HIGH) CAPS(OFF) COLOR(YELLOW)
)BODY expand(//) width(&zscreenw)
?ZUSER  ?ztime?zjdate$-/-/- MVS Console -/-/- Row?z   $of?z   $
$MVS CMD ===>_ZCMD / / $SCROLL ===>_ZUSC+
$Select=>%job     %jid     +%msg     $Job name, id & message / / Keep=>_z   +
|ZDATA -/-/-|
| / / |
| -/-/-|
+
)INIT
.zvars = '(linecur,lineend,linemax)'
.help = MVSCONSH
.alarm = &alm
)PROC
&VDEPTH = LVLINE(ZDATA)
)END


MVSCONSH HELP PANEL

)ATTR default(%$_)
 0 AREA(SCRL)                        /* scrollable area attribute    */
)BODY
%------------------------ MVS Console -----------------------------------------
%Command  ===>_ZCMD
$
0pnarea                                                                       0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
0                                                                             0
)AREA pnarea
%Help for MVS Console
$This panel provides an ISPF full screen MVS console. It is driven by MVSCONS
 exec.
$See%Initial Set up$below for help in preparation.

 To start the console display issue
%   'MVSCONS number'
$where number is the number of lines to keep as described below.

 All commands typed on the command line will be passed to MVS, with the
 exception of *, !, and ISPF commands.
   %*$is used to write a comment to the console display
   %!$is used to enter autoupdate. A wait limit and time can be passed ie ! 5
      5 will wait for 5 new messages or 5 seconds. The defaults are 0 messages
      0 seconds, which means no limit.  If a selection is in use, the message
      count is for this selection only.  Each new message will cause a screen
      refresh.  PA1 or ATTN will cancel the autoupdate mode at any time.

 The%SELECT$field is used to select jobs by 1 to 8 character name prefix,
 jobid and message.  Existing matches will be highlighted, new matches will
 also sound the alarm. The position of the selects is above the relevant
 columns. Use%-$to search up and%+$to search down for next selection.

 The%KEEP$field is used to change the number of lines to keep on exit.  If
 the%MVSCONS$file is not allocated, use the%TSO ALLOCATE$command.

 If the final full screen is displayed, then pressing ENTER maintains the
 display with the last full screen.

 Messages with a HOLD will be highlighted, and those with an ALARM setting
 will sound the alarm. Messages with reply will have their highlight removed
 when the reply is trapped. Highlighting will not be maintained across
 sessions.

%Initial set-up
$Before starting, the TSO userid must be capable of using the CONSOLE and
 CONSPROF commands. The following RACF commands would allow the user to
 operate as a master console.

 To allow use of TSO segments
%  'SETROPTS CLASSACT(ACCTNUM TSOPROC TSOAUTH OPERCMDS)'
   'SETROPTS RACLIST(OPERCMDS)'
   'RDEF ACCTNUM account UACC(NONE)'
   'RDEF TSOPROC tsoproc UACC(NONE)'
   'RDEF TSOAUTH CONSOLE UACC(NONE)'
   'RDEF TSOAUTH JCL UACC(NONE)'
   'RDEF TSOAUTH OPER UACC(NONE)'

$Dynamic parse must be available. This is described in the IBM manual
%  'System Programming Library: RACF' SC28-1343

$For the relevant userid
%  'ALU userid TSO(ACCT(account) PROC(tsoproc) MAXSIZE(4096))
      OPERPARM(AUTH(MASTER) ROUTCODE(ALL) LEVEL(ALL) MFORM(T,S,J))'
   'PE account CL(ACCTNUM) ID(userid)'
   'PE tsoproc CL(TSOPROC) ID(userid)'
   'PE CONSOLE CL(TSOAUTH) ID(userid)'
   'PE OPER CL(TSOAUTH) ID(userid)'
   'PE JCL CL(TSOAUTH) ID(userid)'
   'RDEF OPERCMDS MVS.MCSOPER.userid UACC(NONE)'
   'PE MVS.MCSOPER.userid CL(OPERCMDS) ID(userid)'
   'SETROPTS RACLIST(OPERCMDS) REFRESH'

%First time of use
$The first time the user logs on, issue
%  'CONSPROF UNSOLDISP(NO) SOLDISP(NO) UNSOLNUM(1000) SOLNUM(400)'
$adjusting values to your requirements.

%Notes on use
$If it is desired to capture the console display while not using the console
 display, issue
%  'CONSOLE ACTIVATE'
$otherwise MVSCONS will activate and deactivate the console session itself.

 If you wish to keep the display between invocations of MVSCONS, allocate a
 dataset to the MVSCONS ddname
%  'ALLOC FI(MVSCONS) NEW DELETE UNIT(VIO)
      SPACE(1,1) CYLINDERS RECFM(V,B) BLKSIZE(32760) LRECL(255)'
$The number of lines of console to keep in this dataset is set by the
 parameter passed to MVSCONS, eg
%   'MVSCONS 1000'
$to keep 1000 lines. The default is 0 (the file will not be used).

)PROC
)END


Mike Protts
Analyst Programmer
C&K Software Ltd (UK)               c Xephon 1996

