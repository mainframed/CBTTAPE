TSO DDNAMEs with invalid DSAB open counts

INTRODUCTION

When running TSO CLISTs and/or REXX EXECs, you can run
into the situation where an abend or other interruption in
processing causes a field in the Data Set Association Block
(DSAB) not to be reset to zero after the associated DCB has been
closed. When this happens, the DDNAME associated with the
dataset cannot be freed and reallocated. The remedy for this at our
shop was to log on to TSO again, but since we have large
amounts of processing which occur in a log-on CLIST, it was
both time-consuming and resource-consuming to have to do this.

The UNOPEN program was written to allow for flipping the
DSAB open count when the error situation described above
occurs. UNOPEN needs to be APF-authorized to run: it must
also be in the SYS1.PARMLIB member IKJTSO00 (or whatever
suffix is used). The syntax for the specification in IKJTSO00 is:

      AUTHCMD NAMES(          /* AUTHORIZED COMMANDS       */  +
         UNOPEN               /* UNOPEN user command       */  +
                   )          /*                           */

In addition to the program, I have included a member that can be
used with the TSO HELP command to be copied to the
installation's appropriate help dataset with a member name of
UNOPEN.

UNOPEN SOURCE CODE

UNOPEN   CSECT
         YREGS
UNOPEN   AMODE 24
UNOPEN   RMODE 24
         PUNCH ' SETCODE AC(1)'
         SAVE  (14,12),,UNOPEN-&SYSDATE
         LR    R12,R15             LOAD R12 W/EPA ADDRESS
         USING UNOPEN,R12          ESTABLISH ADDRESSABLITY
         LR    R14,R13             COPY CALLER'S S/A POINTER
         LA    R13,SAVE            POINT TO MY S/A
         ST    R13,8(,R14)         CHAIN ...
         ST    R14,4(,R13)         ... SAVE AREAS.
         LR    R3,R1               SAVE A(CPPL)
         USING CPPL,R3
         MVC   PPL0+PPLUPT-PPL,CPPLUPT   MOVE UPT POINTER TO PPL
         MVC   PPL0+PPLECT-PPL,CPPLECT   MOVE ECT POINTER TO PPL
         MVC   PPL0+PPLCBUF-PPL,CPPLCBUF MOVE CBUF POINTER TO PPL
         L     R5,X'10'            LOAD A(CVT)
         USING CVT,R5
         L     R15,CVTPARS         LOAD A(PARSE ROUTINE)
         LA    R1,PPL0             LOAD A(PARSE PLIST)
         BALR  R14,R15             CALL PARSE ROUTINE
         LTR   R15,R15             DID PARSE COMPLETE OK?
         BZ    PARSEOK             B IF YES
         TPUT  ERRPARS,L'ERRPARS   ELSE, PUT OUT MESSAGE
         B     EXIT8               EXIT WITH CC OF 8
PARSEOK  L     R4,ANSWER           LOAD A(PARSE ANSWER AREA)
         USING IKJPARMD,R4
         L     R1,DDPDE            LOAD A(PARSED DDNAME)
         LH    R15,DDPDE+4         LOAD LENGTH(PARSED DDNAME)
         BCTR  R15,*-*             SUBTRACT 1 FOR EXECUTE
         EX    R15,MVDD            EXECUTE MOVE OF DDNAME FROM PDE
*      ( MVC   DDNAME(*-*),0(R1)   )
         CLI   BUMP+1,X'01'        DOES USER WANT TO BUMP COUNT?
         BNE   FREEPDES            B IF NOT
         OI    FLAG,BUMPFG         TURN ON BUMP-RATHER-THAN-ZERO FG
FREEPDES IKJRLSA ANSWER            FREE PARSE PDES
         DROP  R4
         L     R6,CVTTCBP          LOAD A(TCBWORDS)
         L     R6,0(,R6)           LOAD A(CURRENT TCB)
         USING TCB,R6
         L     R8,TCBJSCB          LOAD A(JSCB)
         USING IEZJSCB,R8
         L     R6,JSCDSABQ         LOAD A(QDB FOR DSAB QUEUE)
         ICM   R6,15,X'C'(R6)      LOAD A(FIRST DSAB)
         USING DSAB,R6
DSABLOOP BZ    NODD                B IF END OF DSAB QUEUE
         L     R7,DSABTIOT         LOAD A(TIOT DD ENTRY)
         USING TIOENTRY,R7
         CLC   DDNAME,TIOEDDNM     IS THIS THE DDNAME USER WANTS?
         BE    HIT                 B IF YES
         ICM   R6,15,DSABFCHN      POINT TO NEXT DSAB
         B     DSABLOOP            LOOP
NODD     TPUT  ERRDDNAM,L'ERRDDNAM PUT OUT ERROR MESSAGE
         B     EXIT8               EXIT WITH CC OF 8
HIT      TM    FLAG,BUMPFG         DOES USER WANT TO BUMP COUNT?
         BZ    HIT1                B IF NOT
         CLC   DSABOPCT,=H'0'      IS OPEN DCB COUNT EXACTLY 0?
         BE    JUSTONE             B IF YES
         BNE   LT999               OUTPUT ERROR MESSAGE
HIT1     CLC   DSABOPCT,=H'1'      ARE THERE ANY OPEN DCBS?
         BE    JUSTONE             B IF EXACTLY ONE
         BH    GT1                 B IF MORE THAN ONE
         TPUT  NOOPEN,L'NOOPEN
         B     EXIT8               EXIT WITH CC OF 8
GT1      CLC   DSABOPCT,=H'999'    OVER 999 OPEN DCBS?
         BNH   LT999               B IF NOT
         TPUT  OVER999,L'OVER999
         B     EXIT8               EXIT WITH CC OF 8
LT999    LH    R15,DSABOPCT        LOAD OPEN COUNT
         CVD   R15,WORK            CONVERT TO DECIMAL
         ED    MSG0CT,WORK+6       EDIT OPEN COUNT TO MESSAGE
         MVC   MSG0DD,DDNAME       MOVE DDNAME TO MESSAGE
         TPUT  MSG0,MSG0L          ISSUE DDNAME HAS XXX OPEN DCBS.
         B     EXIT8               EXIT WITH CC OF 8
JUSTONE  TM    JSCBOPTS,JSCBAUTH   IS TSO SESSION AUTHORIZED?
         BO    AUTHOK              B IF YES
         TPUT  NOTAUTH,L'NOTAUTH   ELSE, ISSUE ERROR MESSAGE
         B     EXIT8               EXIT WITH CC OF 8
AUTHOK   MODESET KEY=ZERO          GET INTO KEY 0
         TM    FLAG,BUMPFG         DO WE WANT TO BUMP OPEN COUNT?
         BZ    ZAP0                B IF NOT
         MVI   DSABOPCT+1,X'01'    SET DSAB OPEN COUNT TO 1
         MVC   MSG1CT,=C' one'     CHANGE MESSAGE
         B     MODENZ              SKIP CLEAR OF OPEN COUNT
ZAP0     XC    DSABOPCT,DSABOPCT   SET DSAB OPEN COUNT TO 0
MODENZ   MODESET KEY=NZERO         GET BACK TO PROBLEM KEY
         MVC   MSG1DD,DDNAME       MOVE DDNAME TO MESSAGE
         TPUT  MSG1,MSG1L          ISSUE MESSAGE
         SR    R15,R15             SET CC OF 0
EXIT     L     R13,SAVE+4          LOAD A(CALLER'S SAVEAREA)
         RETURN (14,12),RC=(15)    RETURN TO CALLER
EXIT8    LA    R15,8               SET CC OF 8
         B     EXIT                EXIT
SAVE     DS    18F                 SAVEAREA.
WORK     DS    D                   WORK DOUBLEWORD
MSG0     DC    C'DDNAME '
MSG0DD   DC    CL8' ',C' has'
MSG0CT   DC    X'40202120',C' open DCB(s) associated with it-nothing doX
               ne.'
MSG0L    EQU   *-MSG0              LENGTH OF MSG0
MSG1     DC    C'Count of open DCBs has been set to '
MSG1CT   DC    C'zero',C' for DDNAME '
MSG1DD   DC    CL8' '
MSG1L    EQU   *-MSG1
ERRPARS  DC    C'Error in TSO parse routine.'
NOTAUTH  DC    C'Your TSO session is not authorized to use UNOPEN.'
NOOPEN   DC    C'The DDNAME you requested has no associated open DCBs.'
ERRDDNAM DC    C'The DDNAME you requested is not allocated to your TSO X
               session.'
OVER999  DC    C'This DDNAME has over 999 associated open DCBs-nothing X
               done.'
FLAG     DC    X'00'
BUMPFG   EQU   X'80'               COUNT IS TO BE SET TO 1, NOT 0
PPL0     DC    (PPLL/4)A(0)        PARSE PARAMETER LIST
         ORG   PPL0+PPLANS-PPL     MOVE LOC. CTR. TO ANSWER PL. PTR.
         DC    A(ANSWER)           POINT PPL TO ANSWER PLACE
         ORG   PPL0+PPLECB-PPL     MOVE LOC. CTR. TO ECB POINTER
         DC    A(PARSEECB)         POINT PPL TO PARSE ECB
         ORG   PPL0+PPLPCL-PPL     MOVE LOC. CTR. TO PARSE PCL PTR.
         DC    A(PCLDD)            POINT PPL TO PARSE PCL
         ORG   ,                   RESUME OLD LOCATION COUNTER VALUE
ANSWER   DC    A(0)                PARSE ANSWER PLACE
PARSEECB DC    A(0)                ECB FOR PARSE'S US .
DDNAME   DC    CL8' '              DDNAME RETURNED BY PARSE
MVDD     MVC   DDNAME(*-*),0(R1)   EXECUTED INSTRUCTION
PCLDD    IKJPARM ,                 PARSE PARAMETER CONTROL LIST
DDPDE    IKJIDENT 'DDNAME +',PROMPT='DDNAME +',                        X
               FIRST=ALPHA,OTHER=ALPHANUM,MAXLNTH=8,                   X
               HELP=('Allocated DDNAME whose associated DSAB should be X
               affected.')
BUMP     IKJKEYWD ,
         IKJNAME 'BUMP'
         IKJNAME 'NOBUMP'
         IKJENDP ,                 END OF PCL
         CVT   DSECT=YES           CVT
         IKJCPPL ,                 CPPL
         IKJTCB ,                  TCB
         IEZJSCB ,                 JSCB
         IHADSAB ,                 DSAB
         IKJPPL ,                  PPL
PPLL     EQU   *-PPL               LENGTH OF PPL
TIOT     DSECT
         IEFTIOT1 ,                TIOT
         END

TSO HELP Dataset Member UNOPEN

)F Function -

   UNOPEN - Advanced  command  to  make  freeable a  file  that  cannot
            be freed because the system thinks it is open.

            Under  certain circumstances,  when a program running under
            TSO abends or is otherwise interrupted, the DSAB associated
            with an open file  is  left with a open DCB count of 1 even
            though the DCB has been closed at task  termination.   This
            often happens when a  CLIST is interrupted while it has had
            a  file  open.   The  file  cannot be freed or reallocated,
            necessitating a new LOGON.

            This command should be used with the greatest caution lest
            you  mark a file as unopen  (0 open DCBs in the DSAB)  and
            free it while it is really open.   If  you  do  this,  the
            results will be unpredictable.
)X SYNTAX

   UNOPEN DDNAME NOBUMP|BUMP

Default: NOBUMP

   At completion of processing, &LASTCC will be set to:
        00 - No errors.
        08 - No appropriate DDNAME found.

)O OPERANDS

)P'DDNAME'  - The DDNAME to be marked as not open (or to be marked
              as open if BUMP is specified).

))NOBUMP    - The file erroneously marked as open will be marked as
              not open.

))BUMP      - The file marked as not open will be marked as open.


                                                                             c Xephon 1994

