Issuing console commands from TSO and JCL
CONSOLE COMMANDS FROM TSO CLISTS
To be able to issue any console command from TSO can be very helpful when
designing end-user ISPF dialogs with hidden console commands and making
automatic system maintenance from TSO in batch.  The command is issued under
master console authority.
The program named COMMAND is as follows:
*      TSO COMMAND
*      ISSUES INTERNAL OS/JES COMMAND FROM TSO:  THE COMMAND IS
*            FX:  COMMAND V 3A9,ONLINE
         CVT   DSECT=YES
         IEECUCM FORMAT=NEW,LIST=YES
WORKAREA DSECT                          GETMAINED SAVE/WORK
         USING WORKAREA,R13             ADDRESS SAVE/WORK
SAVE     DS    CL72
         DS    0F
CMDAREA  DC    CL256' '                 AREA FOR COMMAND
WORKLEN  EQU   *-WORKAREA               LENGTH OF SAVE/WORK
&ID      SETC  'COMMAND'
&IDLEN   SETA  KÕ&ID
&ID      INITR SIZE=WORKLEN
         L     R2,0(R1)                 GET PARM ADDR
         LH    R3,0(R2)                 GET PARM LENGTH
         CH    R3,=AL2(5+&IDLEN)        TEST FOR ZERO DATA
         BNH   EXIT                     IF ZERO, IGNORE
         MVI   CMDAREA,C' '             BLANK COMMAND
         MVC   CMDAREA+1(L'CMDAREA-1),CMDAREA BLANK COMMAND
         LR    R4,R3                    GET LENGTH OF COMMAND
         SH    R4,=AL2(5-4+&IDLEN)      SUBTRACT LENGTH OF HEADER
         SLL   R4,16                    PUT LENGTH IN FIRST HALF
         ST    R4,CMDAREA               SAVE LENGTH FOR COMMAND
         SH    R3,=AL2(5+&IDLEN+1)      SUBTRACT LENGTH OF HEADER + 1
         EX    R3,*+4                   MOVE IN COMMAND
         MVC   CMDAREA+4(0),&IDLEN+5(R2) MOVE IN COMMAND
         AUTHON                         REQUEST AUTH
         MODESET KEY=ZERO               GET KEY ZERO
         SR    R0,R0                    CLEAR FOR SVC 34
         LA    R1,CMDAREA               POINT TO START OF COMMAND
         L     R15,CVTPTR               GET CVT ADDRESS
         USING CVT,R15                  ADDRESS CVT
         L     R15,CVTCUCB              GET UCM ADDRESS
         SH    R15,=H'4'                POINT TO UCM PREFIX POINTER
         USING UCMPRFXP,R15             ADDRESS UCM PREFIX POINTER
         L     R15,UCMPRFXP             POINT TO UCM PREFIX
         USING UCMPRFX,R15              ADDRESS UCM PREFIX
         L     R15,UCMMCENT             POINT TO MASTER CONSOLE UCM
         USING UCMLIST,R15              ADDRESS MASTER CONS UCM
         IC    R0,UCMID                 GET MASTER UCM ENTRY NUMBER
         SVC   34                       ISSUE COMMAND WITH MCONS AUTH
         MODESET KEY=NZERO              RETURN TO USER KEY
         AUTHOFF                        REQUEST NO AUTH
         SRL   R4,16                    GET LENGTH AGAIN
         TPUT  CMDAREA+4,(R4)           NOTIFY USER OF COMMAND
EXIT     EQU   *
         EXITR                          RETURN
         LTORG
         END
The format of the command is:
COMMAND console-command
example: COMMAND V SYSLOG,HARDCPY,CMDS,ROUT=ALL
The program COMMAND will show the command to the issuer via a TPUT.  Another
program exists called COMMANDN which is identical to COMMAND execept that it
lacks the TPUT in order to hide the command function for the user in a CLIST.
CONSOLE COMMANDS FROM JCL
This facility gives users the ability to intersperse console commands within
their JCL.  The console command is fetched from the PARM field of an EXEC card
for the following program.  This technique also allows commands to be issued
dependent upon condition codes:
*   THIS PROGRAM ISSUES AN OPERATOR COMMAND FROM THE PARAMETER FIELD
*     EXECUTED VIA JCL OR CALL COMMAND FROM TSO
         CVT   DSECT=YES
         IEECUCM FORMAT=NEW,LIST=YES
JCLCMD   INITR
         L     R2,0(R1)                 GET PARM ADDR
         LH    R3,0(R2)                 GET PARM LENGTH
         LTR   R3,R3                    TEST LENGTH
         BZ    EXIT                     IF NONE, THEN EXIT
         MVC   CMDAREA,=CL256' '        BLANK COMMAND
         LR    R4,R3                    SAVE LENGTH
         LA    R3,4(R3)                 ADD LENGTH FIELD LENGTH
         SLL   R3,16                    PUT IN FIRST HALFWORD
         ST    R3,CMDAREA               PUT DOWN COMMAND LENGTH
         BCTR  R4,0                     REDUCE FOR EXECUTE
         EX    R4,*+4                   MOVE IN COMMAND
         MVC   CMDAREA+4(0),2(R2)       MOVE IN COMMAND
         AUTHON                         REQUEST AUTH
         MODESET KEY=ZERO               GET KEY ZERO
         SR    R0,R0                    CLEAR R0 FOR SVC 34
         LA    R1,CMDAREA               GET ADDR OF COMMAND
         L     R15,CVTPTR               GET CVT ADDRESS
         USING CVT,R15                  ADDRESS CVT
         L     R15,CVTCUCB              GET UCM ADDRESS
         SH    R15,=H'4'                POINT TO UCM PREFIX POINTER
         USING UCMPRFXP,R15             ADDRESS UCM PREFIX POINTER
         L     R15,UCMPRFXP             POINT TO UCM PREFIX
         USING UCMPRFX,R15              ADDRESS UCM PREFIX
         L     R15,UCMMCENT             POINT TO MASTER CONSOLE UCM
         USING UCMLIST,R15              ADDRESS MASTER CONS UCM
         IC    R0,UCMID                 GET MASTER UCM ENTRY NUMBER
         SVC   34                       ISSUE COMMAND
         MODESET KEY=NZERO              RETURN TO USE KEY
         AUTHOFF                        REQUEST NO AUTH
EXIT     EQU   *
         EXITR                          RETURN
         DS    0F
CMDAREA  DC    CL104' '                 AREA FOR COMMAND
         LTORG
         END
Example of call:
     //S17 EXEC PGM=JCLCMD,PARM=ÕV NET,INACT,F,ID=Y0271'
Nils Plum
Systems Programmer (Denmark)  © Nils Plum 1988




















