Total storage management (continued)

We conclude our look at total storage management with the
publication of the remainder of the source code of the JES2 exit
6 routine.

*****   SUBROUTINE  -  UNIT                             *****
*  SUMMARY  -  LOOKS FOR UNIT PARAMETER ON THE DD CARD.     *
*  INPUT REGISTERS  - R10 POINTS TO THE START OF DD.        *
*  OUTPUT REGISTERS - R15 POINTS TO THE UNIT PARAMETER, IF  *
*                     R15=0 THE PARAMETER IS NOT PRESENT.   *
UNIT     EQU   *
         LA    R10,5(R10)
UNI00    EQU   *
         CLI   0(R10),X'FE'
         BE    UNI01
         CLI   0(R10),X'41'
         BE    UNI02
         LA    R10,1(R10)
         B     UNI00
UNI01    EQU   *
         LA    R15,0
         BR    R14
UNI02    EQU   *
         LR    R15,R10
         BR    R14
*****   SUBROUTINE  -  SORTWK                           *****
*  SUMMARY  -  LOOKS FOR THE DDNAME SORTWKXX, WHERE XX IS   *
*              A NUMBER FROM 00 TO 32.                      *
*  INPUT REGISTERS   -  R3 POINTS TO THE DD CARD.           *
*  OUTPUT REGISTERS  -  R5 INDICATES THE TYPE OF EDIT, IF   *
*                       R5=0 IT'S NOT A SORT DD, R5=4 IS A  *
*                       SORT DD.                            *
SORTWK   EQU   *
         LA    R5,0
         XR    R7,R7
         IC    R7,6(R3)
         CH    R7,=H'8'
         BE    SORTWK00
         BR    R14
SORTWK00 EQU   *
         CLC   7(6,R3),=C'SORTWK'
         BE    SORTWK01
         BR    R14
SORTWK01 EQU   *
         MVC   $NUM(2),13(R3)
         OC    $NUM(2),=X'0F0F'
         CLC   $NUM,=X'FFFF'
         BE    SORTWK02
         BR    R14
SORTWK02 EQU   *
         MVC   $NUM(2),13(R3)
         CLC   $NUM,=C'32'
         BNH   SORTWK03
         BR    R14
SORTWK03 EQU   *
         MVC   $UNIT(4),$NOMVOL
         LA    R5,4
         BR    R14
*****      SUBROUTINE  -  D3380                         *****
*  SUMMARY  -  LOOKS FOR THE 3380 UNIT ON THE DD CARD.      *
*  INPUT REGISTERS  - R10 POINTS TO THE UNIT PARAMETER      *
*                     OF THE DD CARD, CODE X'41'.           *
*  OUTPUT REGISTERS - R15 POINTS TO THE 3380 PARAMETER, IF  *
*                     R15=0 THE PARAMETER IS NOT PRESENT.   *
D3380    EQU   *
         XR    R7,R7
         IC    R7,2(R10)
         CH    R7,=H'4'
         BNE   DIS00
         CLC   3(4,R10),=C'3380'
         BNE   DIS00
         LA    R5,7
         LA    R15,3(R10)
         BR    R14
DIS00    EQU   *
         LA    R15,0
         BR    R14
*****     SUBROUTINE  -  WK                             *****
*  SUMMARY   -  LOOKS FOR THE WK UNIT ON THE DD CARD.       *
*  INPUT REGISTERS  -  R10 POINTS TO THE UNIT PARAMETER     *
*                      OF THE DD CARD, CODE X'41'.          *
*  OUTPUT REGISTERS -  R15 POINTS TO THE WK PARAMETER, IF   *
*                      R15=0 THE PARAMETER IS NOT PRESENT.  *
WK       EQU   *
         XR    R7,R7
         IC    R7,2(R10)
         CH    R7,=H'2'
         BNE   WK00
         CLC   3(2,R10),=C'WK'
         BNE   WK00
         LA    R5,7
         LA    R15,3(R10)
         BR    R14
WK00     EQU   *
         LA    R15,0
         BR    R14
*****      SUBROUTINE  -  GE                            *****
*  SUMMARY   -  LOOKS FOR THE GE UNIT ON THE DD CARD.       *
*  INPUT REGISTERS  -  R10 POINTS TO THE UNIT PARAMETER     *
*  OUTPUT REGISTERS  - R15 POINTS TO THE GE PARAMETER, IF   *
*                      R15=0 THE PARAMETER IS NOT PRESENT.  *
GE       EQU   *
         XR    R7,R7
         IC    R7,2(R10)
         CH    R7,=H'2'
         BNE   GE00
         CLC   3(2,R10),=C'GE'
         BNE   GE00
         LA    R5,7
         LA    R15,3(R10)
         BR    R14
GE00     EQU   *
         LA    R15,0
         BR    R14
*****      SUBROUTINE  -  NOEXIT6                       *****
*  SUMMARY   -  LOOKS FOR THE NOEXIT6 UNIT ON THE DD CARD.  *
*  INPUT REGISTERS  - R10 POINTS TO THE UNIT PARAMETER      *
*                     OF THE DD CARD, CODE X'41'.           *
*  OUTPUT REGISTERS - R15 POINTS TO THE NOEXIT6 PARAMETER,  *
*                     IF R15=0 THE PARAMETER IS NOT PRESENT.*
NOEXIT6  EQU   *
         L     R1,REG1
         XR    R7,R7
         IC    R7,2(R10)
         CH    R7,=H'7'
         BNE   $NO00
         CLC   3(7,R10),=C'NOEXIT6'
         BNE   $NO00
         XR    R7,R7
         IC    R7,1(R10)
         CH    R7,=H'1'
         BNE   $NO01
         MVC   $UNIT(6),$NOEX6
         LA    R5,6
         LA    R15,3(R10)
         BR    R14
$NO00    EQU   *
         LA    R15,0
         BR    R14
$NO01    EQU   *
         CLI   10(R10),X'82'
         BNE   $NO02
         LA    R15,0
         BR    R14
$NO02    EQU   *
         LR    R4,R10
         L     R6,4(,R1)
         LH    R7,0(,R6)
         SH    R7,=H'8'
         STH   R7,0(,R6)
         XR    R7,R7
         LA    R10,10(R4)
RIP00    EQU   *
         CLI   0(R10),X'FE'
         BE    RIP01
         A     R7,=F'1'
         LA    R10,1(R10)
         B     RIP00
RIP01    EQU   *
         A     R7,=F'1'
         LA    R6,2(,R4)
         LA    R8,10(R4)
         LR    R9,R7
         MVCL  R6,R8
         XR    R7,R7
         IC    R7,1(R4)
         S     R7,=F'1'
         STC   R7,1(R4)
         L     R7,0(,R1)
         MVI   0(R7),C'Y'
         LA    R15,0
         BR    R14
*****        SUBROUTINE  -  EDIT                        *****
*  SUMMARY   -  CALCULATES UNIT PARAMETER LENGTH.           *
*  INPUT REGISTERS  -  R10 POINTS TO THE UNIT PARAMETER     *
*                      OF THE DD CARD, CODE X'41'.          *
*  OUTPUT REGISTERS -  R8 UNIT PARAMETER LENGTH.            *
EDIT     EQU   *
         XR    R6,R6
         XR    R7,R7
         LA    R8,1
         LR    R4,R10
         IC    R7,1(R10)
         LA    R10,2(R10)
LOOP     EQU   *
         IC    R6,0(R10)
         AR    R8,R6
         A     R8,=F'1'
         LA    R10,1(R6,R10)
         BCT   R7,LOOP
         CR    R8,R5
         BH    SHORT
         BL    LONG
         SH    R5,=H'1'
         EX    R5,$MVC
         BR    R14
*****        SUBROUTINE   -   SHORT                     *****
*  SUMMARY   -  EDIT UNIT PARAMETER (REDUCTION).            *
*  INPUT REGISTERS   -  R8 UNIT PARAMETER LENGTH            *
*  OUTPUT REGISTERS  -  NONE.                               *
SHORT    EQU   *
         LR    R7,R5
         SR    R7,R8
         L     R1,REG1
         L     R6,4(,R1)
         AH    R7,0(,R6)
         STH   R7,0(,R6)
         XR    R7,R7
         LA    R10,1(R4,R8)
SHORT00  EQU   *
         CLI   0(R10),X'FE'
         BE    SHORT01
         A     R7,=F'1'
         LA    R10,1(R10)
         B     SHORT00
SHORT01  EQU   *
         A     R7,=F'1'
         A     R5,=F'1'
         LA    R6,0(R5,R4)
         LA    R8,1(R4,R8)
         LR    R9,R7
         MVCL  R6,R8
         SH    R5,=H'2'
         EX    R5,$MVC
         BR    R14
*****        SUBROUTINE   -   LONG                      *****
*  SUMMARY   -  EDIT UNIT PARAMETER (LENGTHENING).          *
*  INPUT REGISTERS   -  R8 UNIT PARAMETER LENGTH            *
*  OUTPUT REGISTERS  -  NONE.                               *
LONG     EQU   *
         LR    R7,R5
         SR    R7,R8
         L     R1,REG1
         L     R6,4(,R1)
         AH    R7,0(,R6)
         STH   R7,0(,R6)
         XR    R7,R7
         LA    R10,1(R4,R8)
LONG00   EQU   *
         CLI   0(R10),X'FE'
         BE    LONG01
         A     R7,=F'1'
         LA    R10,1(R10)
         B     LONG00
LONG01   EQU   *
         A     R7,=F'1'
         LR    R2,R7
         LA    R6,1(R4,R8)
         LA    R8,COMODO
         LR    R9,R7
         MVCL  R8,R6
         LR    R7,R2
         LR    R9,R2
         LA    R8,COMODO
         A     R5,=F'1'
         LA    R6,0(R5,R4)
         MVCL  R6,R8
         SH    R5,=H'2'
         EX    R5,$MVC
         BR    R14
*****         DEFINE CONSTANTS AND WORK AREAS           *****
MSGUNIT  DC    C'EXIT6 JES2 CALCULATED UNIT PARAMETERS'
MSGUNCAT DC    C'EXIT6 JES2 UNCATALOG FOR DSN='
WTO01 WTO '                                                            X
                                             ',ROUTCDE=11,DESC=(6),MF=L
WTOLEN   EQU   *-WTO01
REMOVE1  DS    0F
         DC    AL1(12)
         DC    AL1(0)
         DC    AL1(0)
         DC    AL1(0)
PDSNUNCT DC    A(0)             ADDR DSN FOR UNCAT
         DC    A(0)
NAME1    DS    0F
         DC    AL1(68)
         DC    AL1(0)
         DC    AL1(0)
         DC    AL1(0)
PDSNLOC  DC    A(0)             ADDR DSN FOR LOCATE
         DC    A(0)
PCAMAREA DC    A(0)             ADDR CAMAREA
NEWCATLG EQU   X'01'
$NOMVOL  DC    X'0102E6D2'
$NOEX6   DC    X'0104F3F3F8F0'
$UNIT1   DC    X'0000'
         DC    X'0000'
         DC    X'000000'
*-------------------- PARAMETER UNIT=(GE,XX) ---------------*
$GE      DC    X'0202'
         DC    C'GE'
         DC    X'02'
*  TO MODIFY THE NUMBER OF GE UNITS ALLOCATED YOU MUST      *
*  OPERATE ON THE FOLLOWING INSTRUCTION.  FOR EXAMPLE,      *
*  FOR UNIT=(GE,6)  YOU MUST MODIFY IT TO: DC     X'F0F6',  *
*  FOR UNIT=(GE,15) YOU MUST MODIFY IT TO: DC     X'F1F2'.  *
         DC    X'F0F1'
*-------------------- PARAMETER UNIT=(WK,XX) ---------------*
$WK      DC    X'0202'
         DC    C'GE'
         DC    X'02'
*  TO MODIFY THE NUMBER OF WK UNITS ALLOCATED YOU MUST      *
*  OPERATE ON THE FOLLOWING INSTRUCTION.  FOR EXAMPLE,      *
*  FOR UNIT=(WK,4)  YOU MUST MODIFY IT TO: DC     X'F0F4',  *
*  FOR UNIT=(WK,15) YOU MUST MODIFY IT TO: DC     X'F1F5'.  *
         DC    X'F0F1'
*------------------ROUTINE DSNAME AREAS---------------------*
*---------------FOR AUTOMATIC RESTORE OF DMS----------------*
         $MODEND
         LTORG
MYDSECT  DSECT
SAVEAREA DS    18F
SAVEREGS DS    16F
$DSNAME  DS    CL44
$DISP    DS    X
REG1     DS    F
REG15    DS    F
*REMOVE   CAMLST UNCAT,DSNUNCAT
REMOVE   DS    0F
         DS    A
ADSNUNCT DS    A                      ADDR DSN FOR UNCAT
         DS    A
DSNUNCAT DS    CL44
*NAME     CAMLST NAME,DSNLOCAT,,CAMAREA
NAME     DS    0F
         DS    A
ADSNLOC  DS    A                      ADDR FOR LOCATE
         DS    A
ACAMAREA DS    A                      ADDR CAMAREA
DSNLOCAT DS    CL44
CAMAREA  DS    0D
         DC    CL265
$UNIT    DS    XL7
$NUM     DS    CL2
WTOAREA  DS    CL100
COMODO   DS    CL2048
MYDSSIZE EQU   *-MYDSECT
         END


Vincenzo Fontana
Technical Support
Enel Centro Di Teleinformatica (Italy)                   © Vincenzo Fontana 1990

