PDS upload/download facility using CLISTs

Peter Rosenberg's article in the October 1991 issue describes a
PDS upload/download facility using REXX, which is a great
idea.  Many systems programmers are increasingly utilizing PC
facilities in many areas.  A quick upload/download facility for
PDSs is advantageous as typing many send/receive commands
on a PC can be very laborious.

Attached is a CLIST program that provides an example for
those systems programmers who are less familiar with REXX
or who just prefer CLISTs.  This CLIST is functionally the
same as Peter Rosenberg's REXX EXEC, but shorter. Refer to
Peter Rosenberg's article PDS upload/download facility in
Issue 61 of MVS Update  for procedures on how to use this
facility.


PROGRAM COMMENTS

1      When executing this CLIST a fully qualified dataset name
      is required as the parameter - for example:
      TSO %PCLOAD hlq.pdsname

2      There is no need to remove your TSO prefix when running
      this CLIST.

3      If you're not executing this CLIST within an ISPF
      environment change the ISPEXEC SETMSG commands to
      WRITE statements.

4      There is no error handling in the CLIST other than
      checking for a valid dataset and checking for valid
      members in that dataset.


PCLOAD CLIST

PROC 1 PDS
/******************************************************/
/*           PC UPLOAD / DOWNLOAD FACILITY            */
/******************************************************/
  CONTROL NOMSG END(DEND)
/* CHECK FOR VALID DATASET NAME                       */
  IF &SYSDSN('&PDS') ^= OK THEN GOTO PDSERR
/* DELETE AND REALLOCATE UPLOAD/DOWNLOAD DATASETS     */
  DELETE '&SYSUID..UPLOAD'
  DELETE '&SYSUID..DOWNLOAD'
  FREE ATTRLIST(USRLST)
  ATTRIB USRLST RECFM(F B) LRECL(80) BLKSIZE(3120)
  ALLOC FILE(UPLOAD) DA('&SYSUID..UPLOAD') +
        MOD SPACE(20) TRACKS UNIT(TEMPDA) +
        CATALOG USING(USRLST) REUSE RELEASE
  ALLOC FILE(DOWNLOAD) DA('&SYSUID..DOWNLOAD') +
        MOD SPACE(20) TRACKS UNIT(TEMPDA) +
        CATALOG USING(USRLST) REUSE RELEASE
  FREE ATTRLIST(USRLST)
/* CREATE MEMBER LIST IN SYSOUTTRAP                   */
LISTMEM: +
 SET SYSOUTTRAP = 1000
  LISTDS '&PDS' MEM
 SET SYSOUTTRAP = 0
 SET OUTLINE = &SYSOUTLINE
 IF &OUTLINE < 7 THEN GOTO NOMEM
 SET LINE = 7
/* PLACE SEND/RECEIVE RECORDS IN APPROPRIATE DATASETS */
 OPENFILE UPLOAD OUTPUT
 OPENFILE DOWNLOAD OUTPUT
 DO WHILE &LINE <= &OUTLINE
  SET &RECORD = &&SYSOUTLINE&LINE
  SET &SYSDVAL = &STR(&RECORD)
  READDVAL &MEMBER
  SET &UPLOAD = &STR(SEND A:&MEMBER..MBR &PDS(&MEMBER) ASCII CRLF)
  PUTFILE UPLOAD
  SET &DOWNLOAD = &STR(RECEIVE A:&MEMBER..MBR &PDS(&MEMBER) ASCII CRLF)
  PUTFILE DOWNLOAD
  SET &LINE = &LINE + 1
 DEND
 CLOSFILE UPLOAD
 CLOSFILE DOWNLOAD
 FREE DATASET('&SYSUID..UPLOAD')
 FREE DATASET('&SYSUID..DOWNLOAD')
 SET &CNTR = &LINE - 7
 SET &ZMSG000L = &STR(PCLOAD FILES BUILT..&CNTR MEMBERS PROCESSED)
 ISPEXEC SETMSG MSG(ISPZ000)
 EXIT
PDSERR: +
 SET &ZMSG000L = &STR(&PDS NOT FOUND)
 ISPEXEC SETMSG MSG(ISPZ000)
 EXIT
NOMEM: +
 SET &ZMSG000L = &STR(NO MEMBERS FOUND IN &PDS)
 ISPEXEC SETMSG MSG(ISPZ000)
 EXIT


Gavin Foster
Systems Programmer
Australian Airlines (Australia)                         c Xephon 1992

