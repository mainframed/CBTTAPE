Automatic replies

  CAT9013P,USEREXIT(XAUTOREP),SUP(NO)
  IKT010D,USEREXIT(XAUTOREP),SUP(NO)
  IST020I,USEREXIT(XAUTOREP),SUP(NO)

AUTORESP CSECT
         TITLE 'AUTO-REPLY FACILITY FOR TEST SYSTEM'
AUTORESP AMODE 31                    31-BIT ADDRESSING
AUTORESP RMODE ANY                   RESIDENT ABOVE
*        STANDARD ENTRY LINKAGE                             *
         STM   R14,R12,12(R13)       SAVE CALLER'S REGISTER
         LR    R12,R15               LOAD ENTRY ADDRESS
         USING AUTORESP,R12          BASE REGISTER
         GETMAIN R,LV=1024,LOC=BELOW,SP=230 GET WORKAREA
         ST    R13,4(R1)             STORE BACKWARD
         ST    R1,8(R13)             STORE FORWARD
         L     R1,12+4+4+4(R13)      LOAD  CTXT POINTER
         L     R13,8(R13)            LOAD CURRENT SAVE
*        PROCESS CTXT                                       *
PROCESS  DS    0H                    LABEL PROCESS
         L     R11,0(0,R1)           LOAD CTXT POINTER
         USING CTXT,R11              MAP CTXT
         L     R10,CTXTTXPJ          LOAD TEXT MAJOR POINTER
         USING CTXTATTR,R10          MAP TEXT MAJOR AREA
*        PROCESS MESSAGE VIA BRANCH  TABLE                  *
MSGTB    DS    0H                    LABEL MSGTB
         CLC   CTXTTMSG(8),=C'CAT9013P' IS IT THIS MESSAGE?
         BE    RU                       YES, GO PROCESS IT
         CLC   CTXTTMSG(7),=C'IKT010D'  IS IT THIS MESSAGE?
         BE    RSIC                  YES, GO PROCESS IT
         CLC   CTXTTMSG(7),=C'IST020I' IS IT THIS MESSAGE?
         BE    ACTNCP
         B     END_MAIN              NO HITS, RETURN TO CALLER
*        RESPOND U                                          *
RU       DS    0H                    LABEL RU
         LA    R1,72(R13)            ADDRESS RESPONSE AREA
         MVC   0(REPUL,R1),REPU      MOVE REPLY IN
         MVC   6(2,R1),CTXTRPID      MOVE REPLY ID IN
         SR    R0,R0                 NULL R0
         SVC   34                    ISSUE REPLY
         B     END_MAIN              NO AUTOMATION
*        RESPOND SIC                                        *
RSIC     DS    0H                    LABEL RSIC
         TM    CTXTTFB1,CTXTTFWR     IS IT A WTOR?
         BNO   END_MAIN              NO, RETURN TO CALLER
         LA    R1,72(R13)            ADDRESS RESPONSE AREA
         MVC   0(REPSICL,R1),REPSIC          MOVE REPLY IN
         MVC   6(2,R1),CTXTRPID      MOVE REPLY ID IN
         SR    R0,R0                 NULL R0
         SVC   34                    ISSUE REPLY
         B     END_MAIN              NO AUTOMATION
*        ACTIVATE NCP                                       *
ACTNCP   DS    0H                    LABEL IKT010D
         LA    R1,72(R13)            ADDRESS RESPONSE AREA
         MVC   0(NCPL,R1),NCP        MOVE REPLY IN
         SR    R0,R0                 NULL R0
         SVC   34                    ISSUE REPLY
         B     END_MAIN              NO AUTOMATION
*        RETURN TO CALLER                                   *
END_MAIN DS    0H                    LABEL END_MAIN
         L     R13,4(R13)            RELOAD BACKWARD
         L     R1,8(R13)             RELOAD FORWARD
         FREEMAIN R,LV=1024,A=(R1),SP=230 FREE WORK AREA
         LM    R14,R12,12(R13)       RESTORE REGISTERS
         SR    R15,R15               0 RC
         BR    R14                   RETURN TO CALLER
*        PARMLIST 'REPSIC'                                  *
*        REPLY = "R ##,SIC"                                 *
REPSIC   DS    0F                    LABEL RSIC
         DC    X'00'                 NO TOKEN PRESENT
         DC    AL1(REPSICL)          LENGTH INDICATOR
         DC    X'0000'               NO START OR SECURITY TOKEN
         DC    C'R XX,SIC'           THIS IS THE REPLY
REPSICEND EQU  *                     CURRENT ADDRESS
REPSICL  EQU   REPSICEND-REPSIC      LENGTH OF PARAMETER
*        PARMLIST 'REPU'                                    *
*        REPLY = "R ##,U"                                   *
REPU     DS    0F                    LABEL REPU
         DC    X'00'                 NO TOKEN PRESENT
         DC    AL1(REPUL)            LENGTH INDICATOR
         DC    X'0000'               NO START OR SECURITY TOKEN
         DC    C'R XX,U'             THIS IS THE REPLY
REPUEND  EQU   *                     CURRENT ADDRESS
REPUL    EQU   REPUEND-REPU          LENGTH OF PARAMETER
*        PARMLIST 'NCP'                                     *
*        ISSUE 'V NET,ID=NCP001,LOAD=NO,SCOPE=ONLY,ACT'     *
NCP      DS    0F                    LABEL NCP
         DC    X'00'                 NO TOKEN PRESENT
         DC    AL1(NCPL)             LENGTH INDICATOR
         DC    X'0000'               NO START OR SECURITY TOKEN
         DC    C'V NET,ID=NCP001,LOAD=NO,SCOPE=ONLY,ACT'
NCPEND   EQU   *                     CURRENT ADDRESS
NCPL     EQU   NCPEND-NCP            LENGTH OF PARAMETER
*        MACRO IEZVX100 FOR MAPPING OF CTXT... DSECTS       *
         IEZVX100                    MACRO FO MAPPING
         END   AUTORESP              END PROGRAM


                                                                                                  c Xephon 1995

