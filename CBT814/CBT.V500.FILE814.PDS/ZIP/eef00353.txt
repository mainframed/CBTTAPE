Tracking changes to load modules


THE PROBLEM

Often, it is desirable to determine the dates when load modules
were last changed.  This can be useful to both applications and
systems programmers in the course of debugging a problem.  If
a problem can be pinpointed as having started on a given date,
then any programs which relate to the problem and have
changed would be obvious candidates for review.  As a
systems programmer, I was also interested in supplementing a
manual in-house change control procedure with an automated
way of specifically determining module change history.
Frequently, I found this information would be useful using the
last successful IPL date as a starting point.  One example of
this concerns a library which is in the LINKLIST and to which
many people can make changes.  Prior to performing an IPL or
a refresh of LLA, I would like to know what is changing just in
case someone has staged a change of which I am unaware.


THE SOLUTION

The AMBLIST utility from IBM provides the very useful
ability to list change information for all the modules in a library
using the LISTIDR control statement.  However, the listing
produced can be very cumbersome when your goal is simply to
determine what has changed since a given point in time.  I have
written a COBOL program called AMBANLYZ, which will
analyse the listing produced by AMBLIST.  By either using the
date of the last IPL as the default or a supplied date parameter,
the program will produce a summarized report of all the
modules which have changed.

Module changes listed comprise one of three types:

1      Link-edit of a program

2      Translation of a CSECT within a program

3      IMASPZAP to a CSECT within a program.

For all types of change, AMBANLYZ will list the module
name, change date, and type of change.  In addition, for a
TRANS or ZAP change, the applicable CSECT is listed.  Also,
for a ZAP, any available IDR information is printed.  The date
which the program compares is in Julian format and is input via
a PARM field in the JCL.  If the PARM is not present or it is
00000, the IPL date is used.  If the IPL date is needed, an
Assembler subroutine called GETIPLDT is called to retrieve
that information.  A complete summary of change history can
be acquired for an entire library by coding a very old date such
as 00001 in the PARM field.  Figure 1 shows an example of
the output generated by AMBANLYZ.


AMBANLYZ SOURCE CODE

IDENTIFICATION DIVISION.
PROGRAM-ID      AMBANLYZ.
ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT AMBLIST ASSIGN TO AMBLIST.
    SELECT RPTFILE ASSIGN TO RPTFILE.
DATA DIVISION.
FILE SECTION.
FD  RPTFILE
    LABEL RECORDS ARE STANDARD
    BLOCK 0 RECORDS.
01  RPT-REC                   PIC X(80).
FD  AMBLIST
    LABEL RECORDS ARE STANDARD
    BLOCK 0 RECORDS.
01  AMBLIST-RECORD            PIC X(121).
WORKING-STORAGE SECTION.
01  IN-REC.
    03  FILLER               PIC X(6).
    03  IR-POS-7-19.
        05  IR-POS-7-17      PIC X(11).
        05  FILLER           PIC XX.
    03  IR-POS-7-19-2 REDEFINES IR-POS-7-19.
        05  FILLER           PIC X(5).
        05  IR-POS-12-19     PIC X(8).
    03  FILLER               PIC X.
    03  IR-POS-21-29.
        05  IR-POS-21-28     PIC X(8).
        05  FILLER           PIC X.
    03  IR-POS-21-29-2 REDEFINES IR-POS-21-29.
        05  FILLER           PIC X.
        05  IR-POS-22-29     PIC X(8).
    03  FILLER               PIC X(14).
    03  IR-POS-44-57.
        05  FILLER           PIC X(6).
        05  IR-POS-50-51     PIC XX.
        05  IR-POS-52        PIC X.
        05  IR-POS-53-55     PIC XXX.
        05  FILLER           PIC XX.
    03  FILLER               PIC X(18).
    03  IR-POS-76-86.
        05  IR-POS-76-83     PIC X(8).
        05  FILLER           PIC XXX.
    03  IR-POS-76-86-2 REDEFINES IR-POS-76-86.
        05  FILLER           PIC X(5).
        05  IR-POS-81-82     PIC XX.
        05  IR-POS-83        PIC X.
        05  IR-POS-84-86     PIC XXX.
    03  FILLER               PIC X(5).
    03  IR-POS-92-94         PIC X(3).
    03  FILLER               PIC X(9).
    03  IR-POS-104-105       PIC XX.
    03  FILLER               PIC X(16).
01  HEAD-1.
    03  FILLER               PIC X(45) VALUE
    '        AMBANLYZ - LOAD MODULE CHANGE HISTORY'.
01  HEAD-2.
    03  FILLER               PIC X(41) VALUE
    'DATE USED IN COMPARISON FOR THIS REPORT: '.
    03  H2-YEAR              PIC XX.
    03  FILLER               PIC X     VALUE '.'.
    03  H2-DAY               PIC XXX.
    03  FILLER               PIC X     VALUE ' '.
    03  H2-DATE-TYPE         PIC X(12).
01  HEAD-3.
    03  FILLER               PIC X(57) VALUE
    ' MODULE    CHANGE DATE    CHANGE TYPE   CSECT    IDR INFO'.
01  HEAD-4.
    03  FILLER               PIC X(57) VALUE
    '--------   -----------    -----------  --------  --------'.
01  LINE-1.
    03  L1-MODULE            PIC X(8).
    03  FILLER               PIC X(5)  VALUE SPACES.
    03  L1-CHANGE-YEAR       PIC XX.
    03  FILLER               PIC X     VALUE '.'.
    03  L1-CHANGE-DAY        PIC X(3).
    03  FILLER               PIC X(10) VALUE SPACES.
    03  L1-CHANGE-TYPE       PIC X(5).
    03  FILLER               PIC X(5)  VALUE SPACES.
    03  L1-CSECT             PIC X(8).
    03  FILLER               PIC XX    VALUE SPACES.
    03  L1-IDR-INFO          PIC X(8).
01  COMPARE-DATE.
    03  COMPARE-YEAR         PIC 99.
    03  COMPARE-DAY          PIC 999.
01  HOLD-DATE.
    03  HOLD-YEAR            PIC 99.
    03  HOLD-DAY             PIC 999.
01  LINE-CNT                 PIC 99    VALUE 1.
01  EOF-FLAG                 PIC 9     VALUE 0.
    88  EOF                            VALUE 1.
LINKAGE SECTION.
01  PARM.
    03 PARM-LENGTH       PIC S9(4) COMP.
    03 PARM-DATE         PIC XXXXX.
PROCEDURE DIVISION USING PARM.
100-MAINLINE SECTION.
    OPEN INPUT  AMBLIST
         OUTPUT RPTFILE.
    IF PARM-DATE NOT NUMERIC OR PARM-DATE = '00000'
         CALL 'GETIPLDT' USING HOLD-DATE
         MOVE '(LAST IPL)' TO H2-DATE-TYPE
    ELSE
         MOVE PARM-DATE TO HOLD-DATE
         MOVE '(PARM VALUE)' TO H2-DATE-TYPE.
    MOVE HOLD-YEAR TO H2-YEAR.
    MOVE HOLD-DAY  TO H2-DAY.
    PERFORM 160-DISPLAY-HEADINGS.
    PERFORM 200-READ-AMBLIST.
    PERFORM 150-PROCESS UNTIL EOF.
    CLOSE AMBLIST RPTFILE.
    STOP RUN.
150-PROCESS.
    IF IR-POS-7-17 = 'MEMBER NAME'
        MOVE IR-POS-21-28 TO L1-MODULE
    ELSE
        IF IR-POS-83 = '/'      AND
           IR-POS-81-82 NUMERIC AND
           IR-POS-84-86 NUMERIC
               MOVE IR-POS-81-82 TO COMPARE-YEAR
               MOVE IR-POS-84-86 TO COMPARE-DAY
               IF COMPARE-DATE < HOLD-DATE
                   NEXT SENTENCE
               ELSE
                   MOVE 'TRANS' TO L1-CHANGE-TYPE
                   MOVE COMPARE-YEAR TO L1-CHANGE-YEAR
                   MOVE COMPARE-DAY  TO L1-CHANGE-DAY
                   MOVE IR-POS-12-19 TO L1-CSECT
                   MOVE SPACES TO L1-IDR-INFO
                   PERFORM 170-DISPLAY-LINE
    ELSE
        IF IR-POS-52 = '/'      AND
           IR-POS-50-51 NUMERIC AND
           IR-POS-53-55 NUMERIC
               MOVE IR-POS-50-51 TO COMPARE-YEAR
               MOVE IR-POS-53-55 TO COMPARE-DAY
               IF COMPARE-DATE < HOLD-DATE
                   NEXT SENTENCE
               ELSE
                   MOVE 'ZAP' TO TO L1-CHANGE-TYPE
                   MOVE COMPARE-YEAR TO L1-CHANGE-YEAR
                   MOVE COMPARE-DAY  TO L1-CHANGE-DAY
                   MOVE IR-POS-22-29 TO L1-CSECT
                   MOVE IR-POS-76-83 TO L1-IDR-INFO
                   PERFORM 170-DISPLAY-LINE
        ELSE
            IF IR-POS-44-57 = 'LINKAGE EDITOR'
                MOVE IR-POS-92-94 TO COMPARE-DAY
                MOVE IR-POS-104-105 TO COMPARE-YEAR
                IF COMPARE-DATE < HOLD-DATE
                    NEXT SENTENCE
                ELSE
                    MOVE 'LINK' TO L1-CHANGE-TYPE
                    MOVE COMPARE-YEAR TO L1-CHANGE-YEAR
                    MOVE COMPARE-DAY  TO L1-CHANGE-DAY
                    MOVE SPACES TO L1-CSECT
                    MOVE SPACES TO L1-IDR-INFO
                    PERFORM 170-DISPLAY-LINE.
    PERFORM 200-READ-AMBLIST.
160-DISPLAY-HEADINGS.
    WRITE RPT-REC FROM HEAD-1 AFTER ADVANCING PAGE.
    WRITE RPT-REC FROM HEAD-2 AFTER ADVANCING 1 LINE.
    WRITE RPT-REC FROM HEAD-3 AFTER ADVANCING 2 LINES.
    WRITE RPT-REC FROM HEAD-4 AFTER ADVANCING 1 LINE.
    MOVE 4 TO LINE-CNT.
170-DISPLAY-LINE.
    IF LINE-CNT > 57
        PERFORM 160-DISPLAY-HEADINGS.
    WRITE RPT-REC FROM LINE-1 AFTER ADVANCING 1 LINE.
    ADD 1 TO LINE-CNT.
200-READ-AMBLIST.
    READ AMBLIST INTO IN-REC AT END
         MOVE 1 TO EOF-FLAG.


GETIPLDT SOURCE CODE

*   PURPOSE:  USED BY AMBANLYZ TO RETRIEVE THE LAST IPL DATE
GETIPLDT  CSECT
          STM     R14,R12,12(R13)    ***********************************
          LA      R14,0(R13)         *                                 *
          BAL     R13,84(R15)        *                                 *
          USING   *,R13              *     H O U S E K E E P I N G     *
          DS      18F                *                                 *
          ST      R14,4(R13)         *                                 *
          ST      R13,8(R14)         ***********************************
          L       R4,0(R1)           GET PASSED FIELD ADDRESS
          L       R1,16              CVT ADDRESS
          L       R2,196(,R1)        SMF CONTROL BLOCK ADDRESS
          LA      R6,340(,R2)        IPL DATE
          UNPK    0(5,R4),1(3,R6)    UNPACK IPL DATE INTO PASSED FIELD
EOJ       L       R13,4(R13)         RELOAD CALLERS SAVE ADDRESS
          LM      R14,R12,12(R13)    RELOAD CALLERS REGISTERS
          LA      R15,0              ZERO RETURN CODE
          BR      R14                GO BACK TO CALLER
          END


SAMPLE JCL TO RUN AMBANLYZ

//*     CHANGE SYSLIB DD IN STEP1 TO REFLECT LOADLIB TO BE ANALYZED.
//*     CHANGE PARM VALUE IN STEP2 TO THE JULIAN DATE (YYDDD)
//*     FROM WHICH LOAD MODULE CHANGES ARE TO BE LISTED.
//*     IF NO PARM SUPPLIED OR IF IT IS 00000, IPL DATE WILL BE USED.
//STEP1    EXEC PGM=AMBLIST
//SYSPRINT DD   DSN=&&TEMP,DISP=(,PASS),
//   SPACE=(CYL,(25,1)),UNIT=SYSWK,DCB=BLKSIZE=22990
//SYSLIB   DD   DSN=SYS2.SYSTEM.LOADLIB,DISP=SHR
//SYSIN    DD   *
 LISTIDR
//STEP2    EXEC PGM=AMBANLYZ,PARM='00000'
//AMBLIST  DD   DSN=&&TEMP,DISP=(OLD,DELETE)
//RPTFILE  DD   SYSOUT=*


Ernie Ishman
Senior Systems Programmer
Geisinger System Services (USA)                    c Xephon 1990

