Listing dataset information from BCDS of DFHSM

//BCDSLIST JOB (O),'xxx'
//*
//BCDLIST EXEC PGM=BCDSLIST,PARM='DVOL=MVSR1C'
//STEPLIB  DD DSN=libname,DISP=SHR
//BCDSLIST DD DSN=dfhsm.bcds,DISP=SHR
//OUTFILE  DD SYSOUT=*
//SYSOUT   DD SYSOUT=*
//SORTIN   DD UNIT=SYSDA,DISP=(,DELETE),SPACE=(CYL,(1,1))
//SORTOUT  DD UNIT=SYSDA,DISP=(,DELETE),SPACE=(CYL,(1,1))

      DFHSM BCDS LISTING   - VOLUME   : MVSR1C                  PAGE : 6

   DATASET          BACKUP-DATASET     DASD  BACKUP  BACKUP  TRKS VER SYS
                                      VOLUME VOLUME   DATE   USED     CAT

-------------------------------------------------------------------------

SYS1.VTAMLIB      DFHSM.BACK....      MVSR1C BKP187 95/07/31   90 153 YES
                  DFHSM.BACK....      MVSR1C BKP046 95/08/16   90 154 YES

SYS10.ISPPLIB     DFHSM.BACK....      MVSR1C BKP111 95/06/29   30 005 YES

SYS10 PROCLIB1    DFHSM.BACK....      MVSR1C BKP172 95/06/22   45 004  NO
                  DFHSM BACK....      MVSR1C BKP175 95/06/27   45 005  NO

..

   TOTAL TRACKS BACKED UP : 31073  NUMBER OF DATASETS    : 275

BCDLIST SOURCE CODE

* PROGRAM TO GENERATE REPORTS FROM DFHSM BCDS
* REPORTS CAN BE GENERATED BASED ON ONE OF THE FOLLOWING PARAMETERS
*         HIGH-LEVEL QUALIFIER OF DATASETS
*         DASD VOLUME FROM WHICH BACKUP IS TAKEN
*         BACKUP VOLUME - ON THE VOLUME WHICH BACKUP IS CREATED
*         DATE OF BACKUP
*         ALL BACKUP DATASETS
* IF ALL DATSETS ARE REQUIRED, THE REPORT CAN BE SORTED BY:
*         DATASET NAME - DEFAULT
*         DASD VOLUME
*         BACKUP VOLUME
*         BACKUP DATE
* REPORTING OPTION IS REQUESTED BY PASSING PARAMETERS TO THE PROGRAM
*     PARM=PARAMETER
*         DVOL=VVVVVV
*         BVOL=BBBBBB
*         DATE=YY/MM/DD
*         SORT=V/B/D   (VOLUME/BACKUP VOLUME/DATE)
* IF NO PARAMETER IS PASSED, ALL DATASETS WILL BE LISTED, SORTED
* BY DATASET NAME.
         MACRO                                   PRINT PAGE HEADER
         PRNTHDR
         MVC   PRTPGNO,EDIT1
         ED    PRTPGNO,PAGENO+1
         PUT   OUTFILE,PRTREC1
         PUT   OUTFILE,@SPACES
         PUT   OUTFILE,PRTREC2
         PUT   OUTFILE,PRTREC3
         PUT   OUTFILE,PRTREC4
         PUT   OUTFILE,@SPACES
         MEND
         MACRO                                   GENERATE ONE RECORD
         GENREC
         MVC   ODSNAME,BKDNAME
         MVC   OBDSN,BKDBDSN
         MVC   ODVOL,BKDDVOL
         MVC   OBVOL,BKBBVOL
         MVC   OBDATE,BKP2YMD
         L     R8,BKBTRKS
         CVD   R8,DWORD
         AP    NTRACKS,DWORD(8)
         MVC   OTRACK,EDIT1
         ED    OTRACK,DWORD+5
         XR    R8,R8
         LH    R8,BKBVER
         CVD   R8,DWORD
         UNPK  OVERSN(3),DWORD(8)
         OI    OVERSN+2,X'F0'
         MVC   OSYSCAT,=CL3'NO'
         CLI   BKDCAT,X'40'                      CATALOG INDICATOR
         BL    *+10
         MVC   OSYSCAT,=CL3'YES'
         MEND
         MACRO                                   PRINT ONE RECORD
         PRNTREC &LBL1,&LBL2,&LBL3,&LBL4,&LBL5
&LBL1    CLC   ODSNAME,OLDDSN
         BNE   &LBL2
         CP    LINENO,P0
         BE    &LBL2
         MVC   OLDDSN,ODSNAME
         MVC   ODSNAME,@SPACES
         MVC   OUTREC1+1(132),OUTREC
         PUT   OUTFILE,OUTREC1
         B     &LBL3
&LBL2    MVC   OLDDSN,ODSNAME
         MVC   OUTREC1+1(132),OUTREC
         PUT   OUTFILE,OUTREC1
&LBL3    AP    RECNO,P1
         AP    LINENO,P1
         CP    LINENO,P50
         BL    &LBL4
         ZAP   LINENO,P0
         AP    PAGENO,P1
&LBL4    LA    R6,64(,R6)                        POINT TO NEXT
         SH    R7,=H'64'                         MINUS USED LENGTH
         BH    &LBL5                             MORE TO PROCESS
         B     GETNEXT                           READ NEXT RECORD
         MEND
BCDLIST  CSECT
         STM   R14,R12,R12(13)                   SAVE REGISTERS
         LR    R12,R15                           FIRST BASE REG
         LA    R11,4095(R12)
         LA    R11,1(R11)                        SECOND BASE REG
         USING BCDLIST,R12,R11                   ADDRESSABILITY
         LA    R15,BCDSAVE                       REG SAVE AREA ADDR
         ST    R13,4(R15)                        FORWARD POINTER
         ST    R15,8(R13)                        REVERSE POINTER
         LR    R13,R15                           NEW SAVE AREA ADDR
         L     R2,0(,R1)                         ADDRESS OF PARM
         XR    R3,R3                             CLEAR R3
         LH    R3,0(R2)                          LENGTH OF PARM
         LTR   R3,R3
         BZ    CONT1                             NO PARAMETERS PASSED
         BCTR  R3,0                              REDUCE BY 1
         EX    R3,MOVEPARM                       MOVE PARAMETER TO PGM
         CLC   PARM(5),=CL5'DVOL='               DASD VOLUME ?
         BNE   PAR1                              NO CHECK OTHER TYPE
         MVC   DVOL,PARM+5                       SAVE PARAMETER
         MVC   PRTTYPE,=C'    VOLUME   : '       INITIALIZE HEADER
         MVC   PRTPARM(6),DVOL                   INITIALIZE HEADER
         B     CONT1                             CONTINUE PROCESSIMG
PAR1     CLC   PARM(5),=CL5'BVOL='               BACKUP VOLUME
         BNE   PAR2                              NO . CHECK OTHER TYPE
         MVC   BVOL(6),PARM+5                    SAVE PARAMETER
         MVC   PRTTYPE,=C'BACKUP VOLUME: '       INITIALIZE HEADER
         MVC   PRTPARM(6),BVOL                   INITIALIZE HEADER
         B     CONT1                             CONTINUE PROCESSING
PAR2     CLC   PARM(5),=CL5'DATE='               BACKUP DATE PASSED
         BNE   PAR3                              CHECK OTHER TYPES
         MVC   BDATE(8),PARM+5                   SAVE PARAMETER
         MVC   PRTTYPE,=C'BACKUP DATE  : '       INITIALIZE HEADER
         MVC   PRTPARM(8),BDATE                  INITIALIZE HEADER
         B     CONT1                             CONTINUE PROCESSING
PAR3     CLC   PARM(5),=CL5'NAME='               DATASET NAME PASSRD
         BNE   PAR4                              CHECK OTHER TYPE
         MVC   DSNAME(44),PARM+5                 SAVE DATASET NAME
         MVC   PRTTYPE,=C'HIGH LVL QUAL: '       INITIALIZE HEADER
         MVC   PRTPARM(44),DSNAME                INITIALIZE HEADER
         LA    R3,DSNAME                         START ADDR OF NAME
         LA    R4,44                             INITIALIZE LENGTH
         XR    R5,R5                             CLEAR R5
NXTCHAR  CLI   0(R3),C' '                        BLANK FOUND
         BE    GETOUT                            THEN OVER
         LA    R5,1(,R5)                         INCR LENGTH
         LA    R3,1(,R3)                         POINT TO NEXT
         BCT   R4,NXTCHAR                        CHECK NEXT CHAR
GETOUT   ST    R5,DSNAMEL                        SAVE LENGTH
         B     CONT1                             CONTINUE PROCESSING
PAR4     CLC   PARM(5),=CL5'SORT='               SORT PARAMETER
         BNE   INVPARM                           NO . THEN INVALID
         MVC   SORTPARM(1),PARM+5                SAVE PARAMETER
         MVC   PRTTYPE,=C'    VOLUME   : '       IF SORT = V
         CLI   SORTPARM,C'V'                       HEADER = VOLUME
         BE    CONT1                             CONTINUE PROCESS
         MVC   PRTTYPE,=C'BACKUP VOLUME: '       IF SORT = B
         CLI   SORTPARM,C'B'                       HEADER = BACKUP VOL
         BE    CONT1                             CONTINUE PROCESS
         MVC   PRTTYPE,=C'BACKUP DATE  : '       IF SORT = D
         CLI   SORTPARM,C'D'                       HEADER= BACKUP DATE
         BNE   INVPARM                           IF NOT INVALID PARM
CONT1    OPEN  (OUTFILE,(OUTPUT))                OPEN OUTPUT FILE
         OPEN  BCDSACB                           OPEN BCDS DATASET
         CLC   DVOL(18),@SPACES                  VOL/BVOL/DATE PASSED
         BNE   BCDPOINT                          POSITION AT KEY
         CLI   SORTPARM,C' '                     SORT PARM PASSED
         BE    NOSORT                            NO SORTING
         OPEN  (SORTIN,(OUTPUT))                 OPEN SORT WORK FILE
NOSORT   CLC   DSNAME,@SPACES                    NO DATASET NAME
         BE    BCDPOINT                          PROCESS ALL DSN
         MVC   LOWKEY,DSNAME                     INIT .. LOW KEY
         L     R2,DSNAMEL                        NAME OF LENGTH FIELD
         BCTR  R2,0                              LESS 1
         EX    R2,MOVENAME                       MOVE NAME
BCDPOINT MVC   BKDNAME,LOWKEY                    INITIALIZE KEY
         POINT RPL=BCDSRP1                       POSITION AT KEY
GETNEXT  GET   RPL=BCDSRP1                       READ NEXT RECORD
         LTR   R15,R15                           CHECK RETURN CODE
         BNZ   PROGEND                           ERROR - TERMINATE
         CLC   BKDNAME,HIGHKEY                   END OF DATASET
         BNL   LISTEND                           TERMINATE
         CLC   BKDRTYP(2),=X'2000'               TYPE "D" RECORD
         BNE   GETNEXT                           NO SKIP IT
         LA    R6,BKDREC+144                     POINT TO REPEAT SECT
         USING BKDBREC,R6                        ADDRESSABILITY
         XR    R7,R7                             CLEAR
         LH    R7,BKDRECL                        RECORD LENGTH
         SH    R7,=H'144'                        MINUS CONSTANT PART
         CLC   DVOL(6),@SPACES                   NO DVOL PASSED
         BE    NODVOL                            CHECK OTHER TYPE
DVOL1    CLC   DVOL(6),BKDDVOL                   VOLUMES MATCH
         BNE   DVOL34                            CONTINUE
         MVC   BKP2YDDD,BKDDATE+1                GET BACKUP DATE
         BAL   R10,DATECNV                       CONVERT DATE
         MVC   BKBBDSN,BKDBDSN                   GET BACKUP DSN
         GET   RPL=BCDSRP2                       READ TYPE 'B' RECORD
         LTR   R15,R15                           ERROR ?
         BNZ   PROGEND                           THEN EXIT
         GENREC                                  CREATE ONE RECORD
         CP    LINENO,P0                         FIRST LINE OF PAGE ?
         BNE   DVOL31                            NO HEADER
         PRNTHDR                                 PRINT HEADING
         PRNTREC DVOL31,DVOL32,DVOL33,DVOL34,DVOL1
NODVOL   CLC   BDATE(8),@SPACES                  DATE PASSED ?
         BE    NOBDATE                           PROCESS OTHER TYPE
BDATE1   MVC   BKP2YDDD,BKDDATE+1                GET DATE
         BAL   R10,DATECNV                       CONVERT DATE
         CLC   BDATE(8),BKP2YMD                  DATES MATCH
         BNE   BDATE34                           NO .. SKIP IT
         MVC   BKBBDSN,BKDBDSN                   RECORD KEY
         GET   RPL=BCDSRP2                       READ "B" RECORD
         LTR   R15,R15                           CHECK RC
         BNZ   PROGEND                           ERROR .. RETURN
         GENREC                                  GENERATE RECORD
         CP    LINENO,P0                         FIRST LINE OF PAGE ?
         BNE   BDATE31                           NO .. NO HEADER
         PRNTHDR                                 PRINT HEADING
         PRNTREC BDATE31,BDATE32,BDATE33,BDATE34,BDATE1
NOBDATE  CLC   BVOL(6),@SPACES                   DATE PASSED ?
         BE    DSNAME1                           PROCESS OTHER TYPE
BVOL1    MVC   BKBBDSN,BKDBDSN                   RECORD KEY
         GET   RPL=BCDSRP2                       READ "B" RECORD
         LTR   R15,R15                           ERROR
         BNZ   PROGEND                           THEN RETURN
         CLC   BVOL(6),BKBBVOL                   DATES MATCH
         BNE   BVOL34                            CONTINUE
         MVC   BKP2YDDD,BKDDATE+1                BACKUP DATE
         BAL   R10,DATECNV                       CONVERT DATE
         GENREC                                  GENERATE RECORD
         CP    LINENO,P0                         FIRST LINE OF PAGE
         BNE   BVOL31                            NO HEADER
         PRNTHDR                                 PRINT HEADING
         PRNTREC BVOL31,BV0L32,BVOL33,BVOL34,BVOL1
DSNAME1  MVC   BKP2YDDD,BKDDATE+1                BACKUP DATE
         BAL   R10,DATECNV                       CONVERT DATE
         MVC   BKBBDSN,BKDBDSN                   RECORD KEY
         GET   RPL=BCDSRP2                       READ TYPE "B" RECORD
         LTR   R15,R15                           RETURN CODE ?
         BNZ   PROGEND                           ERROR .. EXIT
         GENREC                                  CREATE OUTPUT RECORD
         CLI   SORTPARM,C'.'                     SORTING NEEDED
         BNE   SORT1                             THEN WRITE TO SORTIN
         CP    LINENO,P0                         FIRST LINE OF PAGE
         BNE   DSNAME31                          THEN NO HEADER
         PRNTHDR
         PRNTREC DSNAME31,DSNAME32,DSNAME33,DSNAME34,DSNAME1
SORT1    PUT   SORTIN,OUTREC                     WRITE TO SORT WORK
         B     DSNAME34                          PROCESS NEXT ENTRY
SORT2    CLOSE SORTIN                            CLOSE SORT INPUT
SORT2X   LA    R1,PLIST1                         SORT=V THEN
         CLI   SORTPARM,C'V'                     SET SORT PARM
         BE    SORT3                             AND PERFORM SORT
         LA    R1,PLIST2                         IF SORT = B
         CLI   SORTPARM,C'B'                     SET SORT PARM
         BE    SORT3                             AND PERFORM SORT
         LA    R1,PLIST3                         SORT PARM FOR SORT=D
SORT3    LINK  EP=SORT                           SORT
         OPEN  SORTOUT                           OPEN SORTED OUTPUT
SORT4    GET   SORTOUT,OUTREC                    READ ONE REC
         MVC   PRTPARM(6),ODVOL                  INIT HEADER
         CLI   SORTPARM,C'V'                     FOR SORT = V
         BE    SORT40                            AND CONTINUE
         MVC   PRTPARM(6),OBVOL                  INIT HEADER
         CLI   SORTPARM,C'B'                     FOR SORT = B
         BE    SORT40                            AND CONTINUE
         MVC   PRTPARM(8),OBDATE                 HEADER FOR SORT = D
SORT40   CP    LINENO,P0                         FIRST LINE PF PAGE
         BE    SORT44                            THEN NO HEADER
         CLI   SORTPARM,C'D'                     DATE PASSED
         BNE   SORT41                            NO CHECK OTHER
         CLC   OLDDATE,OBDATE                    DATES SAME AS PREV
         BE    SORT5                             THEN NO NEW PAGE
         B     SORT43                            WRITE ON NEW PAGE
SORT41   CLI   SORTPARM,C'B'                     SORT = B PASSED
         BNE   SORT42                            NO CHECK OTHER TYPE
         CLC   OLDBVOL,OBVOL                     BVOL SAME AS PREV
         BE    SORT5                             WRITE ON SAME PAGE
         B     SORT43                            WRITE ON NEW PAGE
SORT42   CLC   OLDDVOL,ODVOL                     DASD VOLUME MATCH
         BE    SORT5                             WRITE ON SAME PAGE
SORT43   AP    PAGENO,P1                         INCR PAGE NO
         ZAP   LINENO,P0                         INIT LINE NO
SORT44   EQU   *
         PRNTHDR                                 WRITE HEADER
SORT5    MVC   OUTREC1+1(132),OUTREC
         PUT   OUTFILE,OUTREC1                   WRITE ONE RECORD
         MVC   OLDDVOL,ODVOL                     SAVE VOLID
         MVC   OLDBVOL,OBVOL                     SAVE BVOL
         MVC   OLDDATE,OBDATE                    SAVE DATE
         AP    RECNO,P1                          INCR REC NO
         AP    LINENO,P1                         INCR LINE NO
         CP    LINENO,P50                        PAGE SIZE REACHED
         BL    SORT4                             NO THEN CONTINUE
         ZAP   LINENO,P0                         INITIALIZE LINE NO
         AP    PAGENO,P1                         INITIALIZE PAGE NO
         B     SORT4                             CONTINUE PROCESS
INVPARM  WTO   ' BCDSLIST ..INVALID PARAMETERS PASSED',DESC=(6),       X
               ROUTCDE=(2,11)
         B     PROGEND
SORTEND  MVI   SORTPARM,C' '                     SORT PROCESS OVER
LISTEND  CLI   SORTPARM,C' '                     SORT PROCESSING
         BNE   SORT2                             CLOSE SORT FILE
         CP    RECNO,P0                          NO RECORDS FOUND
         BE    PROGEND                           THEN EXIT
         PUT   OUTFILE,@SPACES                   WRITE BLANK LINE
         PUT   OUTFILE,PRTREC4                   WRITE SEPARATOR
         MVC   PRTTRKS,EDIT2                     TOTAL TRACKS
         ED    PRTTRKS,NTRACKS+2                 MOVE TO OUTPUT
         MVC   PRTRECN,EDIT1                     NO OF RECORDS
         ED    PRTRECN,RECNO+1                   MOVE TO OUTPUT
         PUT   OUTFILE,@SPACES                   WRITE SPACES
         PUT   OUTFILE,PRTREC5                   WRITE LAST RECORD
         B     PROGEND                           END PROCESSING
PROGEND  L     R13,4(R13)                        LD SAVE AREA ADDRESS
         RETURN (14,12),RC=0                     EXIT
*  ROUTE TO CONVERT YYDDDF (PACKED) DATE TO YY/MM/DD FORMAT
DATECNV  XR    R2,R2                             CLEAR R2
         IC    R2,BKP2YDDD                       GET YY OF DATE
         SLL   R2,4                              SHIFT BY 4 DIGITS
         STH   R2,BKP2YEAR                       SAVE YEAR PART
         OI    BKP2YEAR+1,X'0C'                  YEAR AS PACKED DEC
         MVC   BKP2DAY,BKP2YDDD+1
         NI    BKP2DAY+1,X'FC'                   DAY IN PACKED DECIMAL
         XC    DWORD(8),DWORD                    CLEAR
         MVC   DWORD+6(2),BKP2YEAR               GET YEAR PART
         CVB   R2,DWORD                          YEAR IN BINARY
         XR    R3,R3                             CLEAR R3
         SRDL  R2,R2                             LAST 2 BITS OF YR
         LTR   R3,R3                             LEAP YEAR
         BZ    LEAP1                             PROCESS LEAP YEAR
         LA    R3,NLEAPTAB                       NON-LEAP YEAR
         B     *+8
LEAP1    LA    R3,LEAPTAB                        LEAP YEAR
         XR    R2,R2                             INIT MONTH
NXTMNTH  CP    BKP2DAY,0(2,R3)
         BNH   LASTMNTH
         LA    R3,2(,R3)                         CHECK NEXT MONTH
         LA    R2,1(,R2)                         INCR MONTH
         B     NXTMNTH                           PROCESS NEXT MONTH
LASTMNTH S     R3,=F'2'
         SP    BKP2DAY,0(2,R3)                   DAYS IN LAST MONTH
         CVD   R2,DWORD                          DECIMAL
         UNPK  BKP2MM(2),DWORD+6(2)
         OI    BKP2MM+1,X'F0'                    MONTH IN DISPLAY
         UNPK  BKP2DD(2),BKP2DAY(2)
         OI    BKP2DD+1,X'F0'                    DATE IN DISPLAY
         UNPK  BKP2YY(2),BKP2YEAR
         OI    BKP2YY+1,X'F0'                    YEAR IN DISPLAY
         BR    R10
BCDSAVE  DS    18F                               REGISTER SAVE MAIN
PARM     DC    CL50' '                           PARAMETER PASSED
DVOL     DC    CL6' '                            DASD VOLUME
BVOL     DC    CL6' '                            BACKUP VOLUME
BDATE    DC    CL8' '                            DATASET NAME
DSNAME   DC    CL44' '                           DATASET NAME
DSNAMEL  DC    F'0'                              LENGTH OF NAME FIELD
SORTPARM DC    CL1' '                            SORT PARAMETERS
MOVEPARM MVC   PARM(0),2(R2)
MOVENAME MVC   HIGHKEY(0),DSNAME
PAGENO   DC    PL4'1'                            PAGE NUMBER
RECNO    DC    PL4'0'                            RECORD COUMT
LINENO   DC    PL2'0'                            LINE NO
NTRACKS  DC    PL6'0'                            TRACKS
P0       DC    PL2'0'                            ZERO
P1       DC    PL2'1'                            ONE
P50      DC    PL2'50'                           PAGE SIZE
DWORD    DS    D                                 WORK AREA
LOWKEY   DC    CL44'A                                           '
HIGHKEY  DC    CL44'Z9999999999999999999999999999999999999999999'
@SPACES  DC    CL133' '
* SORT PARAMETER LISTS
PLIST1   DC    X'80',AL3(ADLST1)                 ADDR OF PARAMETER
         CNOP  2,4
ADLST1   DC    AL2(LISTE1-LISTB1)                LENGTH
LISTB1   DC    A(SORTA1)                         ADDR OF SORT STMT
         DC    A(SORTZ1)                         ADDR OF SORT STMT
         DC    A(RECA)                           ADDR OF RECORD STMT
         DC    A(RECZ)                           ADDR OF RECORD STMT
         DC    F'0'                              ADDR OF MODS
         DC    F'0'                              ADDR OF MODES
LISTE1   EQU   *                                 END OF PARAMETER
PLIST2   DC    X'80',AL3(ADLST2)                 ADDR OF PARAMETER
         CNOP  2,4
ADLST2   DC    AL2(LISTE2-LISTB2)                LENGTH
LISTB2   DC    A(SORTA2)                         ADDR OF SORT STMT
         DC    A(SORTZ2)                         ADDR OF SORT STMT
         DC    A(RECA)                           ADDR OF RECORD STMT
         DC    A(RECZ)                           ADDR OF RECORD STMT
         DC    F'0'                              ADDR OF MODS
         DC    F'0'                              ADDR OF MODES
LISTE2   EQU   *                                 END OF PARAMETER
PLIST3   DC    X'80',AL3(ADLST3)                 ADDR OF PARAMETER
         CNOP  2,4
ADLST3   DC    AL2(LISTE3-LISTB3)                LENGTH
LISTB3   DC    A(SORTA3)                         ADDR OF SORT STMT
         DC    A(SORTZ3)                         ADDR OF SORT STMT
         DC    A(RECA)                           ADDR OF RECORD STMT
         DC    A(RECZ)                           ADDR OF RECORD STMT
         DC    F'0'                              ADDR OF MODS
         DC    F'0'                              ADDR OF MODES
LISTE3   EQU   *                                 END OF PARAMETER
SORTA1   DC    C' SORT FIELDS=(93,6,CH,A,1,44,CH,A) '
SORTZ1   DC    C' '
SORTA2   DC    C' SORT FIELDS=(101,6,CH,A,1,44,CH,A) '
SORTZ2   DC    C' '
SORTA3   DC    C' SORT FIELDS=(109,8,CH,A,1,44,CH,A) '
SORTZ3   DC    C' '
RECA     DC    C' RECORD LENGTH=132,TYPE=F'
RECZ     DC    C' '
OUTFILE  DCB   DDNAME=OUTFILE,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=133,   X
               BLKSIZE=133
SORTIN   DCB   DDNAME=SORTIN,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=132,     X
               BLKSIZE=13200
SORTOUT  DCB   DDNAME=SORTOUT,DSORG=PS,MACRF=GM,EODAD=SORTEND
BCDSACB  ACB   AM=VSAM,DDNAME=BCDSLIST,MACRF=(KEY,SEQ,DIR,IN)
BCDSRP1  RPL   ACB=BCDSACB,OPTCD=(KEY,SEQ,NUP,KGE,MVE),                X
               AREA=BKDREC,AREALEN=912,                                X
               ARG=BKDNAME,KEYLEN=44
BCDSRP2  RPL   ACB=BCDSACB,OPTCD=(KEY,DIR,NUP,KEQ,MVE),                X
               AREA=BKBREC,AREALEN=400,                                X
               ARG=BKBBDSN,KEYLEN=44
BKP2YDDD DS    CL3                               DATE IN YYDDDF
         DS    0F
BKP2YMD  DS    0CL8                              DATE AS YY/MM/DD
BKP2YY   DS    CL2
         DC    C'/'
BKP2MM   DS    CL2
         DC    C'/'
BKP2DD   DS    CL2
BKP2DAY  DS    CL2
BKP2YEAR DS    CL2
LEAPTAB  DC   PL2'0',PL2'31',PL2'60',PL2'91',PL2'121',PL2'152',PL2'182'
         DC   PL2'213',PL2'244',PL2'274',PL2'305',PL2'335',PL2'366'
NLEAPTAB DC   PL2'0',PL2'31',PL2'59',PL2'90',PL2'120',PL2'151',PL2'181'
         DC   PL2'212',PL2'243',PL2'273',PL2'304',PL2'334',PL2'365'
OUTREC   DS    0CL132                            OUTPUT RECORD
ODSNAME  DS    CL44                              DATASET NAME
         DC    CL2' '
OBDSN    DS    CL44                              BACKUP DATASET
         DC    CL2' '
ODVOL    DS    CL6                               DASD VOLUME
         DC    CL2' '
OBVOL    DS    CL6                               BACKUP VOLUME
         DC    CL2' '
OBDATE   DS    CL8                               BACKUP DATE
         DC    CL2' '
OTRACK   DS    CL6                               TRACKS
         DC    CL1' '
OVERSN   DS    CL3                               VERSION
         DC    CL1' '
OSYSCAT  DS    CL3                               SYSCAT
OUTREC1  DC    CL133' '
OLDDSN   DC    CL44' '                           PREVIOUS DATASET
OLDDVOL  DC    CL6' '                            PREVIOUS VOLUME
OLDBVOL  DC    CL6' '                            PREVIOUS BVOL
OLDDATE  DC    CL8' '                            PREVIOUS DATE
EDIT1    DC    X'402020202120'                   EDIT PATTERNS
EDIT2    DC    X'4020202020202120'
* BACKUP DATASET RECORD
         DS    0F
BKDREC   DS    0CL912                            BCDS RECORD
BKDNAME  DS    CL44                              DATASET NAME
BKDRECL  DS    CL2                               RECORD LENGTH
BKDRTYP  DS    CL2                               RECORD TYPE
         DS    CL96                              FILLER
         DS    13CL64                            REPEAT 13 TIMES
* BACKUP VERSION RECORD
BKBREC   DS    0CL400                            BACKUP RECORD
BKBBDSN  DS    CL44                              BACKUP NAME
BKBRECL  DS    CL2                               RECORD LENGTH
BKBRTYP  DS    CL2                               RECORD TYPE
         DS    CL16                              FILLER
BKBNAME  DS    CL44                              DASD DATASET NAME
BKBBVOL  DS    CL6                               DASD VOLUME
         DS    CL20                              CAT INDICATOR
BKBVER   DS    CL2                               BACKUP DATE
BKBTRKS  DS    CL4
         DS    CL260                             REPEAT 13 TIMES
PRTREC1  DS    0CL133                            PAGE HEADER RECORD
         DC    CL1'1'                            NEXT PAGE CC
         DC    CL15' '
         DC    CL22' DFHSM BCDS LISTING - '
PRTTYPE  DC    CL15' '
PRTPARM  DC    CL44' '
         DC    CL20' '
PRTPAGE  DC    CL8' PAGE :'
PRTPGNO  DS    CL6
         DC    CL2' '
PRTREC2  DS    0CL133                            HEADING 1
         DC    CL1' '
         DC    CL44'        DATASET '
         DC    CL2' '
         DC    CL44'        BACKUP-DATASET '
         DC    CL2' '
         DC    CL6' DASD '
         DC    CL2' '
         DC    CL6'BACKUP'
         DC    CL2' '
         DC    CL8' BACKUP '
         DC    CL2' '
         DC    CL6'TRACKS'
         DC    CL1' '
         DC    CL3'VER'
         DC    CL1' '
         DC    CL3'SYS'
PRTREC3  DS    0CL133                            HEADING 2
         DC    CL1' '
         DC    CL44'                '
         DC    CL2' '
         DC    CL44'                        '
         DC    CL2' '
         DC    CL6'VOLUME'
         DC    CL2' '
         DC    CL6'VOLUME'
         DC    CL2' '
         DC    CL8'  DATE  '
         DC    CL2' '
         DC    CL6' USED '
         DC    CL1' '
         DC    CL3'   '
         DC    CL1' '
         DC    CL3'CAT'
PRTREC4  DC    CL1' ',132C'-'                    UNDERLINE
PRTREC5  DS    0CL133                            LAST RECORD
         DC    CL1' '
         DC    CL40'        TOTAL TRACKS BACKED UP   = '
PRTTRKS  DS    CL8
         DC    CL40'       NUMBER OF BACKUP DATASETS = '
PRTRECN  DS    CL6
         DC    CL38' '
* REPEATING PORTION FOR EACH BACKUP VERSION
BKDBREC  DSECT
BKDBDSN  DS    CL44                              BACKUP DATASET NAME
BKDDVOL  DS    CL6                               DASD VOLUME
BKDCAT   DS    CL2                               CAT INDICATOR
BKDDATE  DS    CL4                               BACKUP DATE
         DS    CL8                               UNKNOWN
         END   BCDLIST


M V George
Systems Programmer
Ministry of Interior (Qatar)                                                 c Xephon 1995

