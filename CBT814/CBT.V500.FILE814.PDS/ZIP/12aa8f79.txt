The VTOC environment and CVAF (continued)

In last month’s issue, the main facilities of CVAF were described in
detail.  This month we examine a practical implementation of
these facilities.

The following program will update a format 1 DSCB and, if
necessary, the VTOC index, if the requested update satisfies the
change criteria.  For those who are familiar with CVAF and have
coded programs that make use of the CVAFTST macro, you may
want to consider closely the code between labels BUILDVIB and
VTOCUPDT.

The same program can be used to update other portions of the
format 1 DSCB by inserting your own code where the program
comments indicate.

The special requirements of the program are as follows:

1   The load module should reside in an authorised system
LINKLIST library.

2   The command processor name (and any associated alias
names) should be included in the TSO authorised command list:

    •    For non-XA systems or systems running TSO releases
prior to TSO/E 1.3 this list is contained in the CSECT IKJEFTE2 of
module IKJEFT02.

    •    For TSO/E 1.3 (or higher) this list is contained in the
CSECT IKJEFTE2  of module IKJTABLS.

    •    For TSO/E 1.4 the AUTHCMD section of member


IKJTSO00 in SYS1.PARMLIB can be used.

The details of adding entries to the TSO authorised command list
are described in manuals SPL: TSO/E User Exits and Modifications,
Volume 2 or TSO/E Customization.

A return code 12 from CVAFTST indicates that either the volume
is not mounted or the VTOC Initialisation Block (VIB) is not
initialised.  This is commonly treated as a termination condition
and most programs seeing a CVAFTST return code 12 would end.
In the case of this program we have matched an entered VOLSER
to one found in a UCB, indicating that the volume must be
mounted, therefore the only possibility of a return code 12 is an
uninitialised VIB.  In describing the CVAF macros it was stated
that the CVAFVOL macro can be used to build the VIB for a
volume.  This is incorporated in the program, between labels
BUILDVIB and VTOCUPDT, and allows for successful completion of
certain requests that would otherwise be failed.

VTUPDATE CSECT
****** DESCRIPTION:  THIS PROGRAM READS, FROM A TSO SESSION, A DASD   *
*                    VOLSER, AN EXISTING DSNAME, AND A NEW DSNAME     *
*                    AND, IF PERMISSIBLE, UPDATES THE VOLUMES VTOC    *
*                    AND INDEXED VTOC WITH THE INFORMATION PROVIDED.  *
*   ATTRIBUTES:   LKED:  AC(1)                                        *
*   ASSEMBLY MACRO LIBRARIES                                          *
*           SYSLIB:  SYS1.MACLIB                                      *
*                    SYS1.AMODGEN                                     *
*   CVAF MACROS USED                                                  *
*                    CVAFDIR                                          *
*                    CVAFDSM                                          *
*                    CVAFTST                                          *
******               CVAFVOL                                          *
         STM   R14,R12,12(R13)            SAVE INCOMING ENVIRONMENT
         LR    R10,R15                    SET UP MODULE ...
         USING VTUPDATE,R10                  ADDRESSABILITY
         USING BUFLIST,R12                BUFFER LIST ADDRESSABILITY
         USING BFLE,R11                   B/L ENTRY ADDRESSABILITY
         USING UCBOB,R7                   SET ADDRESSABILITY FOR UCB
         ST    R13,LNKSAVE+4              SAVE OLD SAVE AREA ADDRESS
         LA    R13,LNKSAVE                GET NEW SAVE AREA ADDRESS
         MVC   DSNAME(44),=44X'40'        BLANK OUT DSNAME FIELD
         TPUT  LABEL1,9,ASIS              WRITE PROMPT
         TGET  VOLSER,6                   READ VOLSER
         OC    VOLSER(6),=6X'40'          ENSURE ALL CAPS
         TPUT  LABEL2,15,ASIS             WRITE PROMPT
         TGET  DSNAME,44                  READ OLD DSN
         OC    DSNAME(44),=44X'40'        ENSURE ALL CAPS
         MVC   NEWDSN(44),=44X'40'        BLANK OUT DSNAME FIELD
         TPUT  LABEL3,19,ASIS             WRITE PROMPT
         TGET  NEWDSN,44                  GET NEW DATA SET NAME
         OC    NEWDSN(44),=44X'40'        MAKE SURE ALL CAPS
         CLC   NEWDSN(44),=44X'FF'        INDEX VTOC HIGH KEY?
         BE    NOGO2                      YES - DON'T RENAME
*   DON'T ALLOW DSNAME UPDATE IF REQUESTED DATASET IS CATALOGUED ON   *
*   THE SAME VOLUME AS THAT SPECIFIED AT INPUT.  IF YOU WANT TO ALLOW *
*   THIS TYPE OF RENAME TO TAKE PLACE, THE CURRENT DATASET SHOULD BE  *
*   UNCATALOGUED AFTER THE RENAME HAS OCCURED.                        *
*   THE PREVENTION OF A DSNAME RENAME OF A CATALOGUED DATASET IS THE  *
*   ONLY SECURITY PROVIDED WITH THIS UTILITY, SO EXTREME CAUTION MUST *
*   BE EXHIBITED WHILE IT IS BEING USED.  ADDITIONAL SECURITY MAY BE  *
*   ADDED, THROUGH A SECURITY PACKAGE, TO VERIFY RENAME CAPABILITIES  *
*   FOR BOTH THE SOURCE AND TARGET DSNAME.  THIS LEVEL OF SECURITY IS *
*   LEFT TO THE DISCRETION OF THE INSTALLER.                          *
CATCHK   CLC   DSNAME(44),NEWDSN          NEW DSN?
         BE    START                      NO - NO CATALOG WORRY
         LOCATE LIST1                     FIND CATALOG ENTRY
         LTR   R15,R15                    ENTRY FOUND?
         BNZ   START                      NO - OK TO CHANGE NAME
         SR    R2,R2                      CLEAR R2
         LH    R2,WORKAREA                GET NUMBER OF VOLUMES
         LA    R3,WORKAREA+2              GET START OF VOLUME LIST
VOLLOOP  CLC   4(6,R3),VOLSER             VOLSER MATCH
         BE    CATERR                     YES - DON'T ALLOW DSN CHANGE
         LA    R3,12(,R3)                 POINT TO NEXT LIST ENTRY
         BCT   R2,VOLLOOP                 TAKE A LOOK
         B     START                      NOT CATALOGUED - GO AHEAD
LIST1    CAMLST NAME,DSNAME,,WORKAREA     LOCATE PARAMETER LIST
WORKAREA DS    0D
         DS    265C
         DS    0F
*   WITH ADDRESSABILITY TO THE CVT AND UCB, THE FOLLOWING CODE FINDS  *
*   THE UCB THAT CORRESPONDS TO THE ENTERED VOLSER.                   *
START    L     R6,16                      GET CVT ADDRESS
         USING CVTMAP,R6
         L     R8,CVTUCBSC                GET UCB SCAN ROUTINE ADDRESS
         XC    SCANWORK(100),SCANWORK     CLEAR SCAN WORK AREA
GETUCB   LA    R1,SCANPARM                GET ADDRESS OF SCAN PARMS
         LR    R15,R8                     GET SCAN ROUTINE ADDRESS
         BALR  R14,R15                    GET UCB
         LTR   R15,R15                    UCB FOUND?
         BNZ   NOUCBERR                   NO - ISSUE ERROR
         L     R7,SCANUCB                 LOAD UCB ADDRESS
         CLC   UCBVOLI(6),VOLSER          VOLSER MATCH?
         BE    CVAFCHK                    YES - CHECK CVAF STATUS
         B     GETUCB                     GET NEXT UCB
SCANPARM DC    A(SCANWORK)
         DC    A(SCANTYPE)
         DC    A(SCANUCB)
         DS    0D
SCANWORK DS    CL100
SCANTYPE DC    AL1(UCB3DACC)              DIRECT ACCESS UCB'S
SCANUCB  DS    F
*   END OF UCB SCAN CODE                  *
CVAFCHK  CVAFTST UCB-(UCBPTR)             TEST FOR VTOC INDEX
         B     PROCTBL(R15)               TAKE APPROPRIATE ACTION
PROCTBL  B     NOINDEX           RC(00) - NO VTOC INDEX
         B     NOINDEX           RC(04) - NO VTOC INDEX
         B     INDEX             RC(08) - VTOC INDEX
         B     BUILDVIB          RB(12) - VIB NOT YET INITIALISED
         B     NOGO1             RC(16) - NOT A DASD UCB
NOINDEX  OI    VTOCFLAG,X'40'             SET NO INDEX FLAG
         B     VTOCUPDT                   PROCEED
INDEX    OI    VTOCFLAG,X'80'             SET INDEX FLAG
         B     VTOCUPDT                   PROCEED
*   WE KNOW THE VOLUME IS MOUNTED SO THE VIB MUST BE UNINITIALISED.   *
*   ONLY ALLOW VIB INITIALISATION TO HAPPEN ONCE, THEN ERROR.         *
BUILDVIB TM    VTOCFLAG,X'01'             BUILDVIB FLAG SET?
         BO    NOGO3                      YES - DON'T TRY BUILD AGAIN
         OI    VTOCFLAG,X'01'             SET FLAG
         TPUT  OUTPUT6,41                 ISSUE MESSAGE
         MVC   CVPLMAP(4),=C'CVPL'        INITIALISE CVAF PARAMETER ...
         MVC   CVPLMAP+4(2),=X'0040'         LIST HEADER
         MODESET KEY=ZERO,MODE=SUP        GET IN SUPERVISOR STATE/KEY0
         CVAFVOL ACCESS=VIBBLD,UCB=(UCBPTR),MF=(E,CVPLMAP)
         LTR   R15,R15                    VIB BUILD OK?
         BNZ   VIBLDERR                   NO - ERROR BUILDING VIB
         CLI   CVSTAT,0                   VIB BUILD OK?
         BNE   VIBLDERR                   NO - ERROR BUILDING VIB
         MODESET KEY=NZERO,MODE=PROB      BACK TO NORMAL
         B     CVAFCHK                    CHECK AGAIN
VTOCUPDT MODESET KEY=ZERO,MODE=SUP        GET IN SUPERVISOR STATE/KEY0
*   ENSURE VTOC INTEGRITY                                             *
         ST    R7,UCBADDR                 SAVE UCB ADDRESS
         RESERVE (SYSVTOC,VOLSER,E,6,SYSTEMS),UCB=UCBADDR
*   SET UP ENVIRONMENT FOR VTOC ACCESS                                *
         MVC   DS1DSNAM(44),=44C' '       CLEAR AREA
         MVC   DS1DSNAM(44),DSNAME        MOVE IN DATA SET NAME
         LA    R12,BUFLIST1               GET ADDRESS OF BUFFER
         LA    R11,8(,R12)                GET ADDRESS OF LIST ENTRY
         XC    BUFLIST1(BFLHLN+BFLELN),BUFLIST1 CLEAR BUFFER AREA
         OI    BFLHFL,BFLHDSCB            SET DSCB ACCESS FLAG
         MVI   BFLHNOE,1                  INDICATE ONE BUFFER ENTRY
         LA    R3,DS1FMTID                GET WORK AREA ADDRESS
         ST    R3,BFLEBUF                 SAVE IN BUFFER
         MVI   BFLELTH,96                 INDICATE WORK AREA LENGTH
         OI    BFLEFL,BFLECHR             SET CCHHR FLAG
         MVC   CVPLMAP(4),=C'CVPL'        INITIALISE CVAF PARAMETER ...
         MVC   CVPLMAP+4(2),=X'0040'         LIST HEADER
         CLC   DSNAME(44),NEWDSN          DSN CHANGE?
         BE    RDFMT1                     NO - WON'T NEED IXVTOC UPDATE
         TM    VTOCFLAG,X'40'             VTOC INDEX?
         BO    RDFMT1                     NO - READ FORMAT 1 DSCB
*   READ IN THE VTOC MAP RECORDS AND THE FIRST HIGH LEVEL VIER FROM   *
*   THE INDEXED VTOC.                                                 *
         CVAFDIR ACCESS=READ,IXRCDS=KEEP,UCB=(UCBPTR),MF=(E,CVPLMAP),  X
               MAPRCDS=YES
         LTR   R15,R15                    READ OK?
         BNZ   VIRRDERR                   NO - ERROR READING MAPS/VIER
         CLI   CVSTAT,0                   READ OK?
         BNE   VIRRDERR                   NO - ERROR READING MAPS/VIER
*   READ IN THE FORMAT 1 DSCB THAT IS TO BE UPDATED FROM THE VTOC.    *
RDFMT1   CVAFDIR ACCESS=READ,BUFLIST=BUFLIST1,UCB=(UCBPTR),            X
               DSN=DS1DSNAM,MF=(E,CVPLMAP),IXRCDS=KEEP
         LTR   R15,R15                    READ OK?
         BNZ   F1RDERR                    NO - ERROR READING FMT1 DSCB
         CLI   CVSTAT,0                   READ OK?
         BNE   F1RDERR                    NO - ERROR READING FMT1 DSCB
*   SET UP CVAF BUFFER FOR FORMAT 1 DSCB UPDATE.                      *
*   ANY PORTION OF THE FORMAT 1 DSCB COULD BE UPDATED PRIOR TO ITS    *
*   BEING REWRITTEN TO THE VTOC.  THE DSNAME UPDATE WAS CHOSEN AS IT  *
*   REPRESENTS THE MOST COMPLICATED OF DSCB UPDATES WHEN AN INDEXED   *
*   VTOC IS ENABLED.                                                  *
*   THE CVAFDIR WITH THE WRITE OPTION IS USED TO UPDATE THE VTOC WITH *
*   THE DSNAME CHANGE.  IT REPLACES THE ENTIRE DSCB SO THAT ANY OTHER *
*   DSCB UPDATES WOULD ALSO BE REFLECTED IN THE CHANGED DSCB.         *
         MVC   DSNSAVE(44),DS1DSNAM       SAVE OLD DATA SET NAME
         MVC   DS1DSNAM(44),NEWDSN        PLACE NEW DSN INTO FMT1 DSCB
*   ADD ANY OTHER DSCB CHANGES AT THIS POINT                          *
         LA    R12,BUFLIST1               GET BUFFER ADDRESS
         LA    R11,8(,R12)                GET BUFFER LIST ENTRY ADDRESS
         LA    R3,DS1DSNAM                GET DSCB WORK AREA ADDRESS
         ST    R3,BFLEBUF                 SAVE IN BUFFER
         MVI   BFLELTH,140                INDICATE LENGTH FOR WRITE
         OI    BFLEFL,BFLEMOD             INDICATE BUFFER MODIFIED
         MVC   CCHHR(5),BFLEARG           SAVE DSCB LOCATION
         TPUT  LABEL4,22,ASIS             WRITE PROMPT
         TGET  VERIFY,1                   GET VERIFY INDICATOR
         OI    VERIFY,X'40'               MAKE SURE ALL CAPS
         CLI   VERIFY,C'Y'                GO ON?
         BNE   NOGO                       NO - SHUT 'ER DOWN
         CLC   DSNAME(44),NEWDSN          DSN CHANGE?
         BE    WTFMT1                     NO - WON'T NEED IXVTOC UPDATE
         TM    VTOCFLAG,X'40'             VTOC INDEX?
         BO    WTFMT1                     NO - WRITE FORMAT 1 DSCB
*   ADD NEW DSN TO APPROPRIATE VIER IF IT DOES NOT ALREADY EXIST ON   *
*   VOLUME.                                                           *
         CVAFDSM ACCESS=IXADD,ARG=CCHHR,UCB=(UCBPTR),                  X
               DSN=DS1DSNAM,MF=(E,CVPLMAP)
         LTR   R15,R15                    READ OK?
         BNZ   IXADDERR                   NO - ERROR ADDING TO VIER
         CLI   CVSTAT,0                   READ OK?
         BNE   IXADDERR                   NO - ERROR ADDING TO VIER
         L     R8,CVMRCDS                 GET MAP RECORD BUFFER ADDR
         BAL   R14,LINK                   LINK TOGETHER BUFFER PTRS
*   WRITE UPDATED MAP/VIER RECORDS TO THE VTOC INDEX                  *
         CVAFDIR ACCESS=WRITE,BUFLIST=(R8),MF=(E,CVPLMAP)
         LTR   R15,R15                    READ OK?
         BNZ   IXWRTERR                   NO - ERROR WRITING MAP/VIER
         CLI   CVSTAT,0                   READ OK?
         BNE   IXWRTERR                   NO - ERROR WRITING MAP/VIER
         BAL   R14,DELINK                 DELINK BUFFER PTRS
*   WRITE UPDATED FORMAT 1 DSCB.  THIS WRITE CAUSES THE UPDATE IN THE *
*   VTOC DSCB.  FIRST CHECK FOR DUPLICATE DATASET NAME IF THERE IS NO *
*   INDEXED VTOC.                                                     *
WTFMT1   TM    VTOCFLAG,X'40'             VTOC INDEX?
         BZ    WRITEOK                    YES - GO AHEAD WITH WRITE
         MVC   DSCBWORK(44),NEWDSN        MOVE IN DATA SET NAME
         LA    R12,BUFLIST2               GET ADDRESS OF BUFFER
         LA    R11,8(,R12)                GET ADDRESS OF LIST ENTRY
         XC    BUFLIST2(BFLHLN+BFLELN),BUFLIST2 CLEAR BUFFER AREA
         OI    BFLHFL,BFLHDSCB            SET DSCB ACCESS FLAG
         MVI   BFLHNOE,1                  INDICATE ONE BUFFER ENTRY
         LA    R3,DSCBWORK+44             GET WORK AREA ADDRESS
         ST    R3,BFLEBUF                 SAVE IN BUFFER
         MVI   BFLELTH,96                 INDICATE WORK AREA LENGTH
         OI    BFLEFL,BFLECHR             SET CCHHR FLAG
         CVAFDIR ACCESS=READ,BUFLIST=BUFLIST2,UCB=(UCBPTR),            X
               DSN=NEWDSN,MF=(E,CVPLMAP)
         LTR   R15,R15                    READ OK?
         BNZ   WRITEOK                    NO - GO AHEAD WITH WRITE
         CLI   CVSTAT,0                   READ OK?
         BE    MATCHERR                   YES - DSN ALREADY EXISTS ERROR
WRITEOK  CVAFDIR ACCESS=WRITE,BUFLIST=BUFLIST1,UCB=(UCBPTR),           X
               DSN=DS1DSNAM,MF=(E,CVPLMAP)
         LTR   R15,R15                    WRITE OK?
         BNZ   F1WRTERR                   NO - ERROR WRITING FMT1 DSCB
         CLI   CVSTAT,0                   WRITE OK?
         BNE   F1WRTERR                   NO - ERROR WRITING FMT1 DSCB
         CLC   DSNAME(44),NEWDSN          DSN CHANGE?
         BE    WRITEMSG                   NO - WON'T NEED IXVTOC UPDATE
         TM    VTOCFLAG,X'40'             VTOC INDEX?
         BO    WRITEMSG                   NO - ALL DONE
*   REMOVE OLD DATA SET NAME FROM VTOC INDEX RECORD                   *
         CVAFDSM ACCESS=IXDLT,UCB=(UCBPTR),DSN=DSNSAVE,MF=(E,CVPLMAP)
         LTR   R15,R15                    READ OK?
         BNZ   IXDLTERR                   NO - ERROR DELETING FROM VIER
         CLI   CVSTAT,0                   READ OK?
         BNE   IXDLTERR                   NO - ERROR DELETING FROM VIER
         L     R8,CVMRCDS                 GET MAP RECORD BUFFER ADDR
         BAL   R14,LINK                   LINK TOGETHER BUFFER PTRS
*   WRITE UPDATED MAP/VIER RECORDS TO THE VTOC INDEX                  *
         CVAFDIR ACCESS=WRITE,BUFLIST=(R8),MF=(E,CVPLMAP)
         LTR   R15,R15                    SUCCESSFUL REQUEST?
         BNZ   IXWRTERR                   NO - ISSUE ERROR MESSAGE
         CLI   CVSTAT,0                   CVAF ENVIRONMENT OK?
         BNE   IXWRTERR                   NO - ERROR
         BAL   R14,DELINK                 DELINK BUFFER PTRS
WRITEMSG TPUT  OUTPUT0,36                 PUT OUT MESSAGE ...
         B     END                           THEN END
ERROR    SR    R2,R2                      CLEAR
         IC    R2,CVSTAT                  R2 <- VALUE IN CVSTAT
         CVD   R2,TEMPAREA                CONVERT VALUE TO DECIMAL
         UNPK  ERRORCDE,TEMPAREA          UNPACK INTO ERRORCDE
         OI    ERRORCDE+7,X'F0'           CLEAR OUT SIGN
         BR    R14                        RETURN
*   THIS ROUTINE LINKS THE MAP AND VIER BUFFERS TOGETHER SO THAT ANY  *
*   UPDATES TO VTOC INDEX RECORDS CAN BE WRITTEN SIMULTANEOUSLY.      *
*   THE LINKING PROCESS PLACES THE VIER BUFFERS BETWEEN THE EXISTING  *
*   MAP BUFFERS AND ANY NEW VIXM BUFFERS.                             *
LINK     L     R2,CVBUFL2                 GET NEW VIXM BUFFER LIST ADDR
         L     R3,CVMRCDS                 GET MAP BUFFER LIST ADDR
LNKLP1   C     R2,CHNOFFST(,R3)           PTR EQUAL?
         BE    LNKSWAP1                   YES - SWAP
         L     R3,CHNOFFST(,R3)           GET NEXT BUFFER LIST ADDR
         B     LNKLP1                     CHECK AGAIN
LNKSWAP1 L     R4,CVIRCDS                 GET VIER BUFFER LIST ADDR
         ST    R4,CHNOFFST(,R3)           SAVE IN MAP BUFFER CHAIN
LNKLP2   CLC   CHNOFFST(4,R4),=F'0'       ZERO PTR?
         BE    LNKSWAP2                   YES - SWAP
         L     R4,CHNOFFST(,R4)           GET NEXT VIER BUFFER LST ADDR
         B     LNKLP2                     CHECK AGAIN
LNKSWAP2 ST    R2,CHNOFFST(,R4)           SAVE VIXM BUFFER ADDR
         BR    R14
*   THIS ROUTINE DELINKS THE INDEX BUFFERS SO EACH SET CAN BE         *
*   PROCESSED INDIVIDUALLY AGAIN.                                     *
DELINK   L     R2,CVBUFL2                 GET NEW VIXM BUFFER LIST ADDR
         L     R3,CVIRCDS                 GET VIER BUFFER LIST ADDR
         L     R4,CVMRCDS                 GET MAP BUFFER LIST ADDR
DELNKLP1 CR    R2,R4                      CORRECT BUFFER LIST?
         BE    REMOVE1                    YES - REMOVE IT
         L     R4,CHNOFFST(,R4)           GET NEXT BUFFER LIST ADDR
         B     DELNKLP1                   CHECK AGAIN
REMOVE1  XC    CHNOFFST(4,R3),CHNOFFST(R3) ZERO PTR
         L     R4,CVMRCDS                 GET MAP BUFFER LIST ADDR
DELNKLP2 CL    R3,CHNOFFST(,R4)           CHAIN PTRS MATCH?
         BE    PUTBACK                    YES - PUT IT BACK
         L     R4,CHNOFFST(,R4)           GET NEXT MAP BUFFER LIST ADDR
         B     DELNKLP2                   CHECK NEXT PTR
PUTBACK  ST    R2,CHNOFFST(,R4)           CHAIN NEW VIXM BUFFER LIST
         BR    R14
VIRRDERR BAL   R14,ERROR                  CONVERT TO PRINTABLE
         MVC   ERROR0+56(3),ERRORCDE+5    MOVE IN CVAF ERROR CODE
         TPUT  ERROR0,60                  PUT OUT ERROR MESSAGE
         B     END                        ALL DONE
F1RDERR  BAL   R14,ERROR                  CONVERT TO PRINTABLE
         MVC   ERROR1+56(3),ERRORCDE+5    MOVE IN CVAF ERROR CODE
         TPUT  ERROR1,60                  PUT OUT ERROR MESSAGE
         B     END                        ALL DONE
*   ERROR CODE 022 INDICATES THAT A DATASET BY THE NEW NAME ALREADY   *
*   EXISTS ON THE VOLUME.                                             *
IXADDERR BAL   R14,ERROR                  CONVERT TO PRINTABLE
         MVC   ERROR2+56(3),ERRORCDE+5    MOVE IN CVAF ERROR CODE
         TPUT  ERROR2,60                  PUT OUT ERROR MESSAGE
         B     END                        ALL DONE
IXWRTERR BAL   R14,ERROR                  CONVERT TO PRINTABLE
         MVC   ERROR3+56(3),ERRORCDE+5    MOVE IN CVAF ERROR CODE
         TPUT  ERROR3,60                  PUT OUT ERROR MESSAGE
         B     END                        ALL DONE
F1WRTERR BAL   R14,ERROR                  CONVERT TO PRINTABLE
         MVC   ERROR4+56(3),ERRORCDE+5    MOVE IN CVAF ERROR CODE
         TPUT  ERROR4,60                  PUT OUT ERROR MESSAGE
         B     END                        ALL DONE
IXDLTERR BAL   R14,ERROR                  CONVERT TO PRINTABLE
         MVC   ERROR5+56(3),ERRORCDE+5    MOVE IN CVAF ERROR CODE
         TPUT  ERROR5,60                  PUT OUT ERROR MESSAGE
         B     END                        ALL DONE
VIBLDERR MODESET KEY=NZERO,MODE=PROB      BACK TO NORMAL
         BAL   R14,ERROR                  CONVERT TO PRINTABLE
         MVC   ERROR6+56(3),ERRORCDE+5    MOVE IN CVAF ERROR CODE
         TPUT  ERROR6,60                  PUT OUT ERROR MESSAGE
         B     END                        ALL DONE
NOGO     TPUT  OUTPUT1,51                 PUT OUT MESSAGE ...
         B     END                           THEN END
NOGO1    TPUT  OUTPUT2,39                 PUT OUT MESSAGE ...
         B     ENDIT                         THEN END
NOGO2    TPUT  OUTPUT3,31                 PUT OUT MESSAGE ...
         B     ENDIT                         THEN END
NOGO3    TPUT  OUTPUT7,55                 PUT OUT MESSAGE ...
         B     ENDIT                         THEN END
NOUCBERR TPUT  OUTPUT4,44                 PUT OUT MESSAGE ...
         B     ENDIT                         THEN END
CATERR   TPUT  OUTPUT5,50                 PUT OUT ERROR MESSAGE
         B     ENDIT                      ALL DONE
MATCHERR TPUT  OUTPUT8,55                 PUT OUT ERROR MESSAGE
         B     NORLSE                     ALL DONE
END      CLC   DSNAME(44),NEWDSN          SAME DATASET?
         BE    NORLSE                     YES - NO BUFFERS TO RELEASE
         TM    VTOCFLAG,X'40'             VTOC INDEX?
         BO    NORLSE                     NO - NO BUFFERS TO RELEASE
         CVAFDIR ACCESS=RLSE,BUFLIST=0,MAPRCDS=NO,MF=(E,CVPLMAP),      X
               IXRCDS=NOKEEP
NORLSE   DEQ   (SYSVTOC,VOLSER,6,SYSTEMS) DEQ VTOC
         MODESET KEY=NZERO,MODE=PROB      GET BACK TO NORMAL
ENDIT    L     R13,LNKSAVE+4
         LM    R14,R12,12(R13)
         LA    R15,0
         BR    R14
LNKSAVE  DS    18F
VOLSER   DS    CL6
CCHHR    DS    CL5
UCBADDR  DS    F
SYSVTOC  DC    C'SYSVTOC '
VTOCFLAG DC    X'00'
VERIFY   DS    CL1
LABEL1   DC    C'VOLSER:  '
LABEL2   DC    C'DATASET NAME:  '
LABEL3   DC    C'NEW DATASET NAME:  '
LABEL4   DC    C'VERIFY UPDATE (Y/N):  '
OUTPUT0  DC    C'VUPDT000 - VTOC SUCCESSFULLY UPDATED'
OUTPUT1  DC    C'VUPDT001 - UPDATE TERMINATED BY VERIFY CONFIRMATION'
OUTPUT2  DC    C'VUPDT002 - VTOC ENVIRONMENT UNSUPPORTED'
OUTPUT3  DC    C'VUPDT101 - INVALID DATASET NAME'
OUTPUT4  DC    C'VUPDT102 - NO UCB FOUND FOR VOLSER SPECIFIED'
OUTPUT5  DC    C'VUPDT103 - DATA SET CATALOGUED. RENAME NOT ALLOWED'
OUTPUT6  DC    C'VUPDT104 - WAITING FOR VIB INITIALISATION'
OUTPUT7  DC  C'VUPDT105 - VIB UNINITIALISED.  UNKNOWN VTOC ENVIRONMENT'
OUTPUT8  DC  C'VUPDT106 - DATASET BY NEW NAME ALREADY EXISTS ON VOLUME'
ERROR0   DC    C'VUPDT003 - INDEX MAP/VIER READ ERROR.    CVAF ERROR '
         DC    C'CODE (XXX)'
ERROR1   DC    C'VUPDT004 - FORMAT 1 DSCB READ ERROR.     CVAF ERROR '
         DC    C'CODE (XXX)'
ERROR2   DC    C'VUPDT005 - VIER INDEX ADD ERROR.         CVAF ERROR '
         DC    C'CODE (XXX)'
ERROR3   DC    C'VUPDT006 - ERROR WRITING MAP/VIER.       CVAF ERROR '
         DC    C'CODE (XXX)'
ERROR4   DC    C'VUPDT007 - FORMAT 1 DSCB WRITE ERROR.    CVAF ERROR '
         DC    C'CODE (XXX)'
ERROR5   DC    C'VUPDT008 - VIER INDEX DELETE ERROR.      CVAF ERROR '
         DC    C'CODE (XXX)'
ERROR6   DC    C'VUPDT009 - ERROR BUILDING VIB.           CVAF ERROR '
         DC    C'CODE (XXX)'
DSNAME   DC    44C' '
DSNSAVE  DC    44C' '
NEWDSN   DC    44C' '
BUFLIST1 DS    CL(20)
BUFLIST2 DS    CL(20)
DSCBWORK DS    CL140
         DS    0F
         IECSDSL1 1
DSCBLTH  EQU   *-IECSDSL1
TEMPAREA DS    D
ERRORCDE DS    D
UCBPTR   EQU   7
CHNOFFST EQU   BFLHFCHN-BUFLIST
CVPLMAP  ICVAFPL DSECT=NO
         LTORG
BUFLIST  ICVAFBFL DSECT=YES
         CVT   DSECT=YES
UCBAREA  DSECT
         IEFUCBOB
         END


    © Xephon 1989

