Hexadecimal conversion CLISTs

Many people, particularly system programmers, often have to perform hexadecimal
calculations (eg when analysing a dump) or conversions from hexadecimal to
decimal.  The following CLIST, HEX, provides both of these capabilities: the
CLIST CONV converts decimal values to hexadecimal.


CLIST CONV

CONTROL NOFLUSH NOPROMPT NOCONLIST NOSYMLIST NOMSG
WRITE ***** HEXADECIMAL CALCULATION AND CONVERSION *****
WRITE
WRITE                  IT MAY BE ENTERED
WRITE        EITHER:           |          OR:
WRITE  HEXNUM                  |    (AFTER A HEX RESULT)
WRITE  HEXNUM1 + HEXNUM2       |          + HEXNUM
WRITE  HEXNUM1 - HEXNUM2       |          - HEXNUM
WRITE  HEXNUM1 * HEXNUM2       |          * HEXNUM
WRITE  HEXNUM1 / HEXNUM2       |          / HEXNUM
WRITE
WRITE
AP: -
SET &PREV = &STR()
GOTO A
E: -
WRITE INVALID
GOTO A
T: -
WRITE MORE THAN 8 DIGITS
A: -
WRITENR ?
READ
ERROR DO
        ERROR OFF
        GOTO E
      END
SET &S = &STR(&SYSDVAL)
ERROR OFF
IF &STR(&S) = &STR() THEN EXIT
SET &L = &LENGTH(&STR(&S))
SET &I = 1
DO WHILE &I <= &L AND &SUBSTR(&I:&I,&STR(&S)) = &STR( )
  SET &I = &I + 1
END
IF &I > 1 THEN DO
                 SET &S = &SUBSTR(&I:&L,&STR(&S))
                 SET &L = &LENGTH(&STR(&S))
               END
SET &I = 1
DO WHILE &I <= &L
  IF &SUBSTR(&I:&I,&STR(&S)) = &STR(+) -
  OR &SUBSTR(&I:&I,&STR(&S)) = &STR(-) -
  OR &SUBSTR(&I:&I,&STR(&S)) = &STR(*) -
  OR &SUBSTR(&I:&I,&STR(&S)) = &STR(/) -
  THEN DO
         SET &K = &I
         SET &I = &L + 2
       END
  SET &I = &I + 1
END
IF &I = &L + 1 THEN DO
                      SET &OPERAND1 = &STR(&S)
                      SET &OPERATOR = &STR()
                      SET &OPERAND2 = &STR()
                      GOTO HD
                    END
IF &K = &L THEN GOTO E
IF &K = 1 THEN DO
                 IF &STR(&PREV) = &STR() THEN GOTO E
                 SET &S1 = 0
                 GOTO JS
               END
SET &PREV = &STR()
SET &S1 = &SUBSTR(1:&K-1,&STR(&S))
JS: -
SET &S2 = &SUBSTR(&K:&K,&STR(&S))
SET &S3 = &SUBSTR(&K+1:&L,&STR(&S))
SET &SYSDVAL = &STR(&S1,&S2,&S3)
READDVAL OPERAND1 OPERATOR OPERAND2 XXXXXXXX
IF &STR(&XXXXXXXX) ^= &STR() THEN GOTO E
IF &STR(&OPERAND2) = &STR() THEN GOTO E
IF &STR(&OPERATOR) = &STR() THEN GOTO E
IF &STR(&OPERATOR) ^= &STR(+) AND &STR(&OPERATOR) ^= &STR(-) -
   AND -
   &STR(&OPERATOR) ^= &STR(*) AND &STR(&OPERATOR) ^= &STR(/) -
THEN GOTO E
IF &LENGTH(&STR(&OPERAND2)) > 8 THEN GOTO T
HD: -
IF &LENGTH(&STR(&OPERAND1)) > 8 THEN GOTO T
/*
SET &I = 1
DO WHILE &I <= 2
  IF &I = 1 THEN SET &S = &STR(&OPERAND1)
            ELSE SET &S = &STR(&OPERAND2)
  SET &V = 0
  SET &J = 1
  SET &L = &LENGTH(&STR(&S)) - 1
  DO WHILE &J <= &LENGTH(&STR(&S))
    SET &K = 1
    DO WHILE &K <= 16
      IF &SUBSTR(&J:&J,&STR(&S)) = -
         &SUBSTR(&K:&K,0123456789ABCDEF) -
      THEN DO
             SET &K = &K - 1
             ERROR DO
                     ERROR OFF
                     WRITE OPERAND > 7FFFFFFF
                     GOTO A
                   END
             SET &V = &V + &K * 16 ** &L
             ERROR OFF
             SET &K = 18
           END
      ELSE SET &K = &K + 1
    END
    IF &K = 17 THEN GOTO E
    SET &J = &J + 1
    SET &L = &L - 1
  END
  IF &I = 1 THEN SET &V1 = &V
            ELSE SET &V2 = &V
  SET &I = &I + 1
END
IF &STR(&OPERATOR) = &STR() -
THEN DO
       WRITE                 = &V1  DECIMAL
       GOTO AP
     END
/*
IF &STR(&OPERATOR) = &STR(/) -
THEN IF &V2 = 0 THEN DO
                       WRITE NO DIVISION BY ZERO
                       GOTO A
                     END
IF &STR(&PREV) ^= &STR() THEN SET &V1 = &STR(&PREV)
ERROR DO
        ERROR OFF
        WRITE RESULT > 7FFFFFFF | RESULT < -7FFFFFFF
        GOTO A
      END
C: -
SET &V = &V1 &OPERATOR &V2
IF &STR(&OPERATOR) = &STR(//) THEN GOTO JD
SET &PREV = &STR(&V)
IF &STR(&OPERATOR) ^= &STR(/) -
THEN IF &V < 0 THEN SET &Z = &STR(-)
               ELSE SET &Z = &STR()
ELSE IF &V1 < 0 THEN SET &Z = &STR(-)
                ELSE SET &Z = &STR()
JD: -
IF &V < 0 THEN SET &V = - &V
ERROR OFF
SET &ER = &STR()
DO WHILE &V > 0
  SET &R = &V // 16
  SET &V = &V / 16
  SET &R = &R + 1
  SET &H = &SUBSTR(&R:&R,0123456789ABCDEF)
  SET &ER = &STR(&H&ER)
END
IF &ER = &STR() THEN SET &ER = 0
IF &STR(&OPERATOR) = &STR(/) -
THEN DO
       SET &HELP = &STR(&ER)
       SET &OPERATOR = &STR(//)
       GOTO C
     END
IF &STR(&OPERATOR) = &STR(//) -
THEN DO
       SET &REM = &STR(&ER)
       SET &ER = &STR(&HELP)
     END
/*
SET &L = &LENGTH(&STR(&OPERAND1))
IF &L < &LENGTH(&STR(&OPERAND2)) THEN -
   SET &L = &LENGTH(&STR(&OPERAND2))
P: -
DO WHILE &LENGTH(&STR(&ER)) < &L
  SET &ER = &STR(0&ER)
END
IF &STR(&OPERATOR) = &STR(//) -
THEN DO
       SET &HELP = &STR(&ER)
       SET &ER = &STR(&REM)
       SET &OPERATOR = &STR(/)
       GOTO P
     END
IF &STR(&OPERATOR) ^= &STR(/) -
THEN WRITE                 = &Z&ER
ELSE DO
       IF &ER ^= 0 THEN SET &ZR = &STR(&Z)
                   ELSE SET &ZR = &STR()
       IF &HELP = 0 THEN SET &Z = &STR()
       WRITE                 = &Z&HELP      REMAINDER &ZR&ER
     END
GOTO A
CLIST HEX

CONTROL NOFLUSH NOPROMPT NOCONLIST NOSYMLIST NOMSG
WRITE ***** CONVERT FROM DECIMAL TO HEXA *****
WRITE
WRITE
A: -
WRITENR ?
READ
ERROR DO
        ERROR OFF
        WRITE NOT NUMERIC
        GOTO A
      END
SET &S = &STR(&SYSDVAL)
ERROR OFF
IF &STR(&S) = &STR() THEN EXIT
SET &L = &LENGTH(&STR(&S))
SET &I = 1
DO WHILE &I <= &L AND &SUBSTR(&I:&I,&STR(&S)) = &STR( )
  SET &I = &I + 1
END
IF &I > 1 THEN DO
                 SET &S = &SUBSTR(&I:&L,&STR(&S))
                 SET &L = &LENGTH(&STR(&S))
               END
SET &Z = &STR()
IF &SUBSTR(1:1,&STR(&S)) = &STR(-) -
OR &SUBSTR(1:1,&STR(&S)) = &STR(+) -
THEN DO
       IF &SUBSTR(1:1,&STR(&S)) = &STR(-) -
       THEN SET &Z = &STR(-)
       SET &S = &SUBSTR(2:&L,&STR(&S))
       SET &L = &LENGTH(&STR(&S))
       SET &I = 1
       DO WHILE &I <= &L AND &SUBSTR(&I:&I,&STR(&S)) = &STR( )
         SET &I = &I + 1
       END
       IF &I > 1 THEN DO
                        SET &S = &SUBSTR(&I:&L,&STR(&S))
                        SET &L = &LENGTH(&STR(&S))
                      END
     END
IF &DATATYPE(&STR(&S)) ^= NUM THEN DO
                                     WRITE NOT NUMERIC
                                     GOTO A
                                   END
ERROR DO
        ERROR OFF
        WRITE NUMBER TOO GREAT
        GOTO A
      END
SET &X = &S
ERROR OFF
SET &E = &STR()
DO WHILE &S > 0
  SET &R = &S // 16
  SET &S = &S / 16
  SET &R = &R + 1
  SET &H = &SUBSTR(&R:&R,0123456789ABCDEF)
  SET &E = &STR(&H&E)
END
IF &E = &STR() THEN SET &E = 0
WRITE             = &Z&E  HEXA
GOTO A


Walter Wiedemann
Consultant (West Germany)     © Walter Wiedemann 1987
































































