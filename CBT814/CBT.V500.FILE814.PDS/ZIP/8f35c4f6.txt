A TSO command to refresh RACF profiles

Over the years I have always found it irritating to have to log
off and log back on to TSO after the RACF access list for a
generic profile has been changed.  As a result, I have written a
short Assembler routine to combat this problem.

Under RACF, if access for a generic profile has been denied to
the TSO user before the change to the access rules, the user
will have to log back on to implement the changes.  This is
because a copy of the RACF profile is placed in the address
space of the user and is used for all subsequent access requests
to that profile until the session is terminated.  Discrete profiles
differ by the fact that they are always referenced from the
RACF database, so any changes take place immediately.

MVS keeps track of the profiles that have been copied into a
TSO user's address space by a series of Generic Anchor Table
Entries (GATEs), which are pointed to by the Accessor
Environment Element (ACEE).  Within each GATE is a profile
list block (GPRF), which contains various information about a
list of generic profiles, including a pointer to the In-storage
Generic Profile Map (GRPF) (see Figure 1).

Within each profile list entry in the GPRF is a flag indicating
whether or not the profile has been retrieved.  If the flag is on,
there will be a pointer to the profile within the address space
(GRPF).  If the flag is off, MVS will retrieve the profile from
the RACF database when required.

So, to refresh the RACF generic profiles within the address
space, all that has to be done is to turn off the flags for all
profile list entries, forcing RACF to retrieve the (updated)
profile from the RACF database.

Because this program changes protected storage within the
address space, it has to be in supervisor mode and have storage
protect key zero.  Therefore the program has to be linked with
AC=1 and reside in an authorized library in LINKLIST or
LPALIST.

As it will be executed from TSO, it will have to be in the TSO
authorized command table.  The program name should be
chosen so that it does not conflict with any other command, for
example REFRESH.

To use the program, just type REFRESH (or your chosen
command name) on the TSO command line after the RACF
profile has been changed and you will receive the message:
'RACF profiles refreshed'.

******************************************************
* PROGRAM TO REFRESH RACF PROFILES WITHIN
* ADDRESS SPACE WITHOUT HAVING TO RE-LOGON TO TSO.
* NEEDS TO BE IN AN AUTHORIZED LIBRARY AND IN THE TABLE OF
* AUTHORIZED COMMANDS FOR TSO.
******************************************************************
REFRESH   CSECT                         * INIT
          USING REFRESH,R12             *
          STM   R14,R12,12(R13)         *
          LR    R12,R15                 *
          L     R4,CVTPTR               * FIND CVT ADDR    (CVTPTR)
          USING CVTMAP,R4               * ADDR CVT
          L     R4,CVTTCBP              * FIND TCB POINTER (CVTTCBP)
          L     R5,12(,4)               * FIND CURRENT ASCB
          USING ASCB,R5
          L     R6,ASCBASXB             * FIND ASXB        (ASCBASXB)
          USING ASXB,R6
          L     R7,ASXBSENV             * FIND ACEE        (ASXBSENV)
          USING ACEE,R7
          L     R8,ACEEGATA             * GET GATE         (ACEEGATA)
          MODESET KEY=ZERO,MODE=SUP     * SUPERVISOR AND KEY 0
LOOPTAB   EQU   *
          L     R9,32(,R8)              * ADDRESS OF GPRF  (ATEPROFL)
          C     R9,=F'0'                * NO GPRF ?..
          BE    RETURN                  *..YES GET OUT
          CLC   12(2,R9),=X'0001'       * >1 PROFILE IN TABLE?
          BE    OK                      * NO - OK
          LA    R11,28(R9)              * GET LENGTH
NEXT      EQU   *
          NI    2(R11),X'00'            * CHANGE RACF FLAG (PLELRTRV)
          AH    R11,0(,R11)             * GET ...
          AH    R11,=H'8'               * ...NEXT PROF
          LH    R2,0(R11)               * LOAD LENGTH
          CH    R2,=H'0000'             * IS IT LAST?
          BE    FINISH                  * YES - GET NEXT
          SR    R2,R2                   * CLEAR REG2
          B     NEXT                    * NEXT PROFILE..
FINISH    EQU   *
          L     R10,36(,R8)             * GET NEXT GATE    (ATENEXT)
          B     LAST                    * IS IT LAST ?
OK        EQU   *
          L     R10,36(,R8)             * NEXT GATE        (ATENEXT)
          NI    30(R9),X'00'            * CHANGE RACF FLAG
LAST      EQU   *
          C     R10,=F'0'               * IS IT LAST
          BE    RETURN                  * YES GET OUT
          LR    R8,R10                  * LOAD LOOP PTR TO GATE
          SR    R10,R10                 * ZERO REG10
          B     LOOPTAB                 * LOOP
RETURN    EQU   *
          MODESET KEY=NZERO,MODE=PROB   * RESET MODE TO NORM
          TPUT  MSG1,34                 * TELL USER ITS OK
          L     R14,12(R13)             * RETURN
          LM    R0,R12,20(R13)          *   .
          BR    14                      *   .
MSG1      DC    C'REFRESH OF RACF PROFILES COMPLETED '
          IHAASCB
          IHAASXB
          IHAACEE
          CVT   DSECT=YES,LIST=YES
          END REFRESH


R D Scott
BOC Limited  (UK)                                               c BOC Limited 1991

