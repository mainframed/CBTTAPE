Suppressing job start and end messages

Because of the amount of console traffic that occurs, our
operators asked if it was possible to suppress job start and end
messages for test classes only. Our automated operations
people looked at ways of doing this with NetView/ACO but
found it difficult. I decided to implement two MPF exits to do
the job. The two MPF exits will compare the class contained in
the current JES2 JCT against a table of classes contained in the
exits themselves. Both exits can be expanded to bypass start
and end messages for started tasks and TSO by checking the
first three bytes of field SSIBJBID in the SSIB for 'STC' or
'TSU'. This check can also be performed by doing the same
check against field JCTJOBID in the JES2 JCT.

To activate these two exits, the following code must be added
to an MPF list:

      $HASP373,SUP(NO),USEREXIT(HASP373)
      $HASP395,SUP(NO),USEREXIT(HASP395)

For users with an automated operations package, AUTO(YES)
may need to be added. Both exits are currently active on an
MVS/XA 2.2.3 and an MVS/ESA 3.1.3 system.

The environment for both exits is as follows:

o      State - supervisor
o      Key - 0
o      RMODE - any
o      AMODE - 31
o      APF - authorized.

Both exits are protected by an ESTAE routine. For an ESA
system, the saving of registers and the return to IEAVX600 can
be replaced by the BAKR and PR instructions.


HASP373 SOURCE CODE

HASP373 TITLE 'WTO EXIT TO SUPPRESS JOB START MESSAGES'
HASP373 CSECT
HASP373 AMODE 31
HASP373 RMODE ANY
***********************************************************************
*        REMARKS.       THE EXIT IS ENTERED IN SUPERVISOR STATE, KEY  *
*                       0, LIVES ABOVE THE LINE, AND MUST BE          *
*                       REENTRANT. IT MUST BE LINKEDITED AUTHORIZED   *
*                       AND PLACED IN AN AUTHORIZED LINKLIST LIBRARY. *
*                       THE EXIT IS USED TO SUPPRESS JOB START        *
*                       MESSAGES FOR JES2 TEST CLASSES.               *
*        - HASP373 JOBNAME STARTED - INIT XX - CLASS X SYS XXXX       *
*        REGISTER CONVENTIONS  = STANDARD CONVENTIONS.                *
*                    REGISTER  2       = BAL                          *
*                    REGISTER  3       = @ CTXT PARAMETER LIST        *
*                    REGISTER  4       = @ MESSAGE ATTRIBUTES STRUCT  *
*                    REGISTER  5       = @ INCOMING MESSAGE TEXT      *
*                    REGISTER  6       = LOCAL WORK REGISTER          *
*                    REGISTER  7       = LOCAL WORK REGISTER          *
*                    REGISTER  8       = LOCAL WORK REGISTER          *
*                    REGISTER  9       = LOCAL WORK REGISTER          *
*                    REGISTER  10      = LOCAL WORK REGISTER          *
*                    REGISTER  11      = LOCAL WORK REGISTER          *
*                    REGISTER  12      = BASE REGISTER                *
*                    REGISTER  13      = SAVE AREA ADDRESS            *
*                    REGISTER  14      = RETURN ADDRESS               *
*                    REGISTER  15      = INITIAL ENTRY ADDRESS        *
*        DSECT MAPPING MACROS USED                                    *
*              IHAPSA             -    PSA                            *
*              IHAASCB            -    ASCB                           *
*              IEZVX100           -    COMMS TASK USER EXIT PARM LIST *
*              CVT                -    CVT                            *
*              IKJTCB             -    TCB                            *
*              IEZJSCB            -    JSCB                           *
*              IEZJSSIB           -    SSIB                           *
*              $BUFFER            -    JES2 BUFFER                    *
*              $TQE               -    JES2 TQE                       *
*              $JCT               -    JES2 JCT                       *
*              $SJB               -    JES2 SJB                       *
*              IFGRPL             -    RPL                            *
*              IHASDWA            -    SDWA                           *
*              WORKAREA           -    MAPS GETMAINED AREA            *
*              IEZVX100           -    CTXT                           *
*              CTXTATTR           -    ATTRIBUTES OF INCOMING MESSAGE *
*             MODULE TYPE = PROCEDURE                                 *
*                PROCESSOR = ASSEMBLER H VERSION 2                    *
*                ATTRIBUTES = KEY 0 SUPVR STATE                       *
*             EXIT - NORMAL = RETURN MACRO TO IEAVX600 (COMMS TASK)   *
*                OUTPUT = NONE                                        *
*                RETURN CODE = NO RETURN CODE TO IEAVX600             *
*             ABEND CODES = NONE                                      *
***********************************************************************
         $HASPEQU                      REGISTER 0
ZERO     EQU   X'00'                   ZERO
SPACE    EQU   C' '                    CHARACTER SPACE
WSUBPOOL EQU   230                     WORK AREA SUBPOOL
NOSDWA   EQU   X'0C'                   NO SDWA INDICATOR
         SAVE  (14,12),,HASP373.&SYSDATE.&SYSTIME
         USING HASP373,R12              SET UP BASE ADDRESSABILITY
         LR    R12,R15                  ENTRY POINT ADDRESS
         L     R3,0(,R1)                CTXT PARAMETER LIST
         USING CTXT,R3                  INFORM THE ASSEMBLER
GET      EQU   *
         GETMAIN RC,LV=WORKLEN,SP=230,LOC=BELOW,                       X
               RELATED=(FREE,'GET STORAGE')
         LTR   R15,R15                  GETMAIN OBTAINED?
         BNZ   SAERROR                  NO-
         LR    R14,R1                   CLEAR
         LA    R15,WORKLEN              THE
         LA    R10,LOWZERO              GOTTEN
         LA    R11,1(0,0)               AREA
         MVCL  R14,R10                  CLEAR
         ST    R13,4(,R1)               ADDRESS OF PREVIOUS SA
         ST    R1,8(,R13)               ADDRESS OF NEXT SA
         LR    R13,R1                   OUR SAVEAREA ADDRESS
         USING WORKAREA,R13             ADDRESSABILITY
SETESTAE EQU   *                        SETUP ESTAE ROUTINE
         STCM  R13,B'1111',ESTPARM1     MOVE TO ESTAE PARAM
         LA    R0,WORKLEN(0,0)          WORK AREA LENGTH
         STCM  R0,B'1111',ESTPARM2      MOVE TO ESTAE PARAM
         LA    R0,WSUBPOOL              WORK AREA SUBPOOL NO
         STCM  R0,B'1111',ESTPARM3      MOVE TO ESTAE PARAM
         ESTAE RECOVERY,CT,PARAM=ESTAPARM,MF=(E,ESTAEW)
$HASP373 EQU   *                        TEST FOR TESTING CLASSES
         LA    R8,0(0,0)                @ OF
         USING PSA,R8                   PSA
         L     R8,PSATOLD               @ OF THE ACTIVE
         USING TCB,R8                   TCB
         L     R8,TCBJSCB               @ OF THE JSCB
         USING IEZJSCB,R8               INFORM THE ASSEMBLER
         L     R8,JSCBACT               @ OF THE ACTIVE JSCB
         L     R8,JSCBSSIB              @ OF THE ACTIVE SSIB
         USING SSIB,R8                  INFORM THE ASSEMBLER
         CLC   SSIBJBID(L'JOB),JOB      BATCH JOB?
         BNE   RETURN                   NO- STC/TSU
         L     R8,SSIBSUSE              @ OF THE SJB
         USING SJB,R8                   INFORM THE ASSEMBLER
         L     R8,SJBJCT                @ OF THE JES2 JCT
         USING JCT,R8                   INFORM THE ASSEMBLER
         CLC   JCTID,JCTIDT             IS THIS THE JCT?
         BNE   RETURN                   NO- LET'S GET OUT
         LA    R6,0(0,0)                @ OF
         USING PSA,R6                   PSA
         L     R6,PSAAOLD               @ OF THE ACTIVE
         USING ASCB,R6                  ASCB
         L     R7,ASCBJBNI              @ OF OUR JOB NAME
         CLC   JCTJNAME,0(R7)           IS JCT FOR THE SAME JOB?
         BNE   RETURN                   NO- LET'S GET OUT
         LA    R6,TESTCLAS              @ TEST CLASS TABLE
         LA    R7,TESTCLNE              NO OF TABLE ENTRIES
         USING TESTDSCT,R6              INFORM THE ASSEMBLER
CLASSCHK EQU   *
         CLC   JCTJCLAS,JOBCLASS        MATCHING TEST CLASS?
         BE    SUPP373                  NO- LET'S TRY NEXT ONE
NXTCLASS EQU   *
         LA    R6,L'JOBCLASS(,R6)       MOVE ONTO NEXT ENTRY
         BCT   R7,CLASSCHK              DO WHILE R7 > 0?
         B     RETURN                   RETURN TO IEAVX600
SUPP373  EQU   *
         OI    CTXTRFB2,CTXTRHCO        SUPPRESS THE MESSAGE (HCO)
         B     RETURN                   RETURN TO IEAVX600
SAERROR  EQU   *                        WRITE TO OPERATOR
         WTO   'MPF37301 - SAVE AREA GETMAIN FAILED - PLEASE INFORM  CSX
               D',ROUTCDE=2,DESC=4      ISSUE HIGHLIGHTED MSG
         B     EXIT                     RETURN TO CALLER
FREEMERR EQU   *
         WTO   'MPF37302 - SAVE AREA FREEMAIN FAILED - PLEASE INFORM CSX
               D',ROUTCDE=2,DESC=4      ISSUE HIGHLIGHTED MSG
         B     EXIT                     RETURN TO CALLER
RETURN   EQU   *                        RETURN TO IEAVX600
         ESTAE 0                        CANCEL ESTAE ROUTINE
         LR    R1,R13                   SAVE NEW SAVEAREA ADDRESS
         L     R13,PREVSA               OLD SAVEAREA ADDRESS
         FREEMAIN RC,LV=WORKLEN,A=(1),SP=230,                          X
               RELATED=(GET,'FREE STORAGE')
         LTR   R15,R15                  FREEMAIN OKAY?
         BNZ   FREEMERR                 NO-
EXIT     EQU   *
         RETURN (14,12),,,RC=0          RESTORE REGS
RECOVERY DS    0H                       RECOVERY ROUTINE
         USING *,R9                     INFORM THE ASSEMBLER
         LR    R9,R15                   BASE ADDRESS
         LA    R3,NOSDWA(0,0)           NO SDWA INDICATOR
         CR    R0,R3                    ANY SDWA?
         BE    NOPSDWA                  NO-
SDWAPTR  EQU   *
         STM   R14,R12,12(R13)          STORE THE REGISTERS
         LR    R4,R1                    SDWA POINTER
         USING SDWA,R4                  INFORM THE ASSEMBLER
         L     R5,SDWAPARM              PARAM ADDRESS
         L     R6,0(,R5)                ADDRESS TO FREEMAIN
         LTR   R6,R6                    ANYTHING THERE?
         BZ    SETRPRET                 NO- JUST RETURN
         LM    R7,R8,4(R5)              LENGTH + SUBPOOL NO
         BAL   R2,FREEMN                FREEMAIN
SETRPRET EQU   *
         SETRP WKAREA=(4),RC=0,REGS=(14)
NOPSDWA  EQU   *
         LR    R10,R14                  SAVE RETURN ADDRESS
         LR    R5,R2                    SAVE PARM ADDRESS
         LM    R6,R8,0(R5)              R6 = ADDRESS TO FREE
*                                       R7 = LENGTH OF AREA TO FREE
*                                       R8 = SUBPOOL NO
         LTR   R6,R6                    ANYTHING THERE?
         BZ    NOSDWART                 NO- JUST RETURN
         BAL   R2,FREEMN                FREEMAIN
NOSDWART EQU   *
         SR    R15,R15                  CONTINUE WITH TERMINATION
         BR    R10                      RETURN TO CALLER
FREEMN   EQU   *           FREEMAIN AREA OBTAINED IN THE MAIN ROUTINE
         FREEMAIN RC,A=(R6),LV=(R7),SP=(R8)
         LTR   R15,R15                  ANY PROBLEMS?
         BZR   R2                       NO- RETURN TO CALLER
         WTO   'MPF37303 - FREEMAIN ERROR IN ESTAE RECOVERY ROUTINE -PLX
               EASE INFORM CSD',ROUTCDE=4,DESC=2
         BR    R2                       RETURN TO CALLER
         LTORG
LOWZERO  DC    X'00'                   LOW-VALUES
JCTIDT   DC    CL4'JCT '               JCT
JOB      DC    CL3'JOB'                 BATCH JOB
TESTCLAS DS    0C                      TEST INITIATOR CLASSES
         DC    C'A',C'E',C'N',C'O',C'T',C'Z'
TESTCLLN EQU   *-TESTCLAS              TESTCLASS TABLE LENGTH
TESTCLNE EQU   TESTCLLN/L'TESTCLAS     NO OF TABLE ENTRIES
TESTDSCT DSECT
JOBCLASS DS    C                       INITIATOR CLASS
         IHAPSA DSECT=YES
         IEZVX100
         CVT DSECT=YES,LIST=NO
         IHAASCB DSECT=YES,LIST=NO
         IKJTCB DSECT=YES,LIST=NO
         IFGRPL DSECT=YES,AM=VSAM
         IEZJSCB
         IEFJSSIB
         $BUFFER
         $TQE
         $JCT
         $SJB
         IHASDWA
HASPMSG  DSECT
HASPCHAR DS    C                       JES2 COMMAND LENGTH
HASPMSNO DS    CL7                     HASP373
         DS    C
HASPJBNM DS    CL8                     JOBNAME
         DS    C
HASPSTRT DS    CL7                     STARTED
         DS    CL3                      -
HASPIC   DS    CL4                     INIT
         DS    C
HASPINNO DS    CL2                     02
         DS    CL3                      -
HASPCC   DS    CL5                     CLASS
         DS    C
HASPCLAS DS    C                       E
         DS    CL3                      -
HASPSC   DS    CL3                     SYS
         DS    C
HASPSID  DS    CL4                     VSY0
WORKAREA DSECT
SAVEAREA DS    CL72                    SAVEAREA
PREVSA   EQU   SAVEAREA+4,4            ADDRESS OF PREVIOUS SAVEAREA
ESTAEW   ESTAE MF=L
LESTAEW  EQU   *-ESTAEW                ESTAE LENGTH
ESTAPARM DS    0F
ESTPARM1 DS    F                       ESTAE PARAM 1
ESTPARM2 DS    F                       ESTAE PARAM 2
ESTPARM3 DS    F                       ESTAE PARAM 3
WORKLEN  EQU   *-WORKAREA              WORKAREA LENGTH
         END  HASP373


HASP395 SOURCE CODE

HASP395 TITLE 'WTO EXIT TO SUPPRESS JOB END MESSAGES'
HASP395 CSECT
HASP395 AMODE 31
HASP395 RMODE ANY
***********************************************************************
*        REMARKS.       THE EXIT IS ENTERED IN SUPERVISOR STATE, KEY  *
*                       0, LIVES ABOVE THE LINE, AND MUST BE          *
*                       REENTRANT. IT MUST BE LINKEDITED AUTHORIZED   *
*                       AND PLACED IN AN AUTHORIZED LINKLIST LIBRARY. *
*                       THE EXIT IS USED TO SUPPRESS JOB END          *
*                       MESSAGES FOR JES2 SPECIFIED CLASSES           *
*        - HASP395 JOBNAME ENDED                                      *
*        REGISTER CONVENTIONS  = STANDARD CONVENTIONS.                *
*                    REGISTER  2       = BAL                          *
*                    REGISTER  3       = @ CTXT PARAMETER LIST        *
*                    REGISTER  4       = @ MESSAGE ATTRIBUTES STRUCT  *
*                    REGISTER  5       = @ INCOMING MESSAGE TEXT      *
*                    REGISTER  6       = LOCAL WORK REGISTER          *
*                    REGISTER  7       = LOCAL WORK REGISTER          *
*                    REGISTER  8       = LOCAL WORK REGISTER          *
*                    REGISTER  9       = LOCAL WORK REGISTER          *
*                    REGISTER  10      = LOCAL WORK REGISTER          *
*                    REGISTER  11      = LOCAL WORK REGISTER          *
*                    REGISTER  12      = BASE REGISTER                *
*                    REGISTER  13      = SAVE AREA ADDRESS            *
*                    REGISTER  14      = RETURN ADDRESS               *
*                    REGISTER  15      = INITIAL ENTRY ADDRESS        *
*        DSECT MAPPING MACROS USED                                    *
*              IHAPSA             -    PSA                            *
*              IHAASCB            -    ASCB                           *
*              IFGRPL             -    RPL                            *
*              IEZVX100           -    COMMS TASK USER EXIT PARM LIST *
*              CVT                -    CVT                            *
*              IKJTCB             -    TCB                            *
*              IEZJSCB            -    JSCB                           *
*              IEZJSSIB           -    SSIB                           *
*              $BUFFER            -    JES2 BUFFER                    *
*              $TQE               -    JES2 TQE                       *
*              $JCT               -    JES2 JCT                       *
*              $SJB               -    JES2 SJB                       *
*              IHASDWA            -    SDWA                           *
*              WORKAREA           -    MAPS GETMAINED AREA            *
*              IEZVX100           -    CTXT                           *
*              CTXTATTR           -    ATTRIBUTES OF INCOMING MESSAGE *
***********************************************************************
         $HASPEQU                      REGISTER 0
ZERO     EQU   X'00'                   ZERO
SPACE    EQU   C' '                    CHARACTER SPACE
WSUBPOOL EQU   230                     WORK AREA SUBPOOL
NOSDWA   EQU   X'0C'                   NO SDWA INDICATOR
         SAVE  (14,12),,HASP395.&SYSDATE.&SYSTIME
         USING HASP395,R12              SET UP BASE ADDRESSABILITY
         LR    R12,R15                  ENTRY POINT ADDRESS
         L     R3,0(,R1)                CTXT PARAMETER LIST
         USING CTXT,R3                  INFORM THE ASSEMBLER
GET      EQU   *
         GETMAIN RC,LV=WORKLEN,SP=230,LOC=BELOW,                       X
               RELATED=(FREE,'GET STORAGE')
         LTR   R15,R15                  GETMAIN OBTAINED?
         BNZ   SAERROR                  NO-
         LR    R14,R1                   CLEAR
         LA    R15,WORKLEN              THE
         LA    R10,LOWZERO              GOTTEN
         LA    R11,1(0,0)               AREA
         MVCL  R14,R10                  CLEAR
         ST    R13,4(,R1)               ADDRESS OF PREVIOUS SA
         ST    R1,8(,R13)               ADDRESS OF NEXT SA
         LR    R13,R1                   OUR SAVEAREA ADDRESS
         USING WORKAREA,R13             ADDRESSABILITY
SETESTAE EQU   *                        SETUP ESTAE ROUTINE
         STCM  R13,B'1111',ESTPARM1     MOVE TO ESTAE PARAM
         LA    R0,WORKLEN(0,0)          WORK AREA LENGTH
         STCM  R0,B'1111',ESTPARM2      MOVE TO ESTAE PARAM
         LA    R0,WSUBPOOL              WORK AREA SUBPOOL NO
         STCM  R0,B'1111',ESTPARM3      MOVE TO ESTAE PARAM
         ESTAE RECOVERY,CT,PARAM=ESTAPARM,MF=(E,ESTAEW)
$HASP395 EQU   *                        TEST FOR TESTING CLASSES
         LA    R8,0(0,0)                @ OF
         USING PSA,R8                   PSA
         L     R8,PSATOLD               @ OF THE ACTIVE
         USING TCB,R8                   TCB
         L     R8,TCBJSCB               @ OF THE JSCB
         USING IEZJSCB,R8               INFORM THE ASSEMBLER
         L     R8,JSCBACT               @ OF THE ACTIVE JSCB
         L     R8,JSCBSSIB              @ OF THE ACTIVE SSIB
         USING SSIB,R8                  INFORM THE ASSEMBLER
         CLC   SSIBJBID(L'JOB),JOB      BATCH JOB?
         BNE   RETURN                   NO- STC/TSU
         L     R8,SSIBSUSE              @ OF THE SJB
         USING SJB,R8                   INFORM THE ASSEMBLER
         L     R8,SJBJCT                @ OF THE JES2 JCT
         USING JCT,R8                   INFORM THE ASSEMBLER
         CLC   JCTID,JCTIDT             IS THIS THE JCT?
         BNE   RETURN                   NO- LET'S GET OUT
         LA    R6,0(0,0)                @ OF
         USING PSA,R6                   PSA
         L     R6,PSAAOLD               @ OF THE ACTIVE
         USING ASCB,R6                  ASCB
         L     R7,ASCBJBNI              @ OF OUR JOB NAME
         CLC   JCTJNAME,0(R7)           IS JCT FOR THE SAME JOB?
         BNE   RETURN                   NO- LET'S GET OUT
         LA    R6,TESTCLAS              @ TEST CLASS TABLE
         LA    R7,TESTCLNE              NO OF TABLE ENTRIES
         USING TESTDSCT,R6              INFORM THE ASSEMBLER
CLASSCHK EQU   *
         CLC   JCTJCLAS,JOBCLASS        MATCHING TEST CLASS?
         BE    SUPP375                  NO- LET'S TRY NEXT ONE
NXTCLASS EQU   *
         LA    R6,L'JOBCLASS(,R6)       MOVE ONTO NEXT ENTRY
         BCT   R7,CLASSCHK              DO WHILE R7 > 0?
         B     RETURN                   RETURN TO IEAVX600
SUPP375  EQU   *
         OI    CTXTRFB2,CTXTRHCO        SUPPRESS THE MESSAGE (HCO)
         B     RETURN                   RETURN TO IEAVX600
SAERROR  EQU   *                        WRITE TO OPERATOR
         WTO   'MPF37301 - SAVE AREA GETMAIN FAILED - PLEASE INFORM  CSX
               D',ROUTCDE=2,DESC=4      ISSUE HIGHLIGHTED MSG
         B     EXIT                     RETURN TO CALLER
FREEMERR EQU   *
         WTO   'MPF37302 - SAVE AREA FREEMAIN FAILED - PLEASE INFORM CSX
               D',ROUTCDE=2,DESC=4      ISSUE HIGHLIGHTED MSG
         B     EXIT                     RETURN TO CALLER
RETURN   EQU   *                        RETURN TO IEAVX600
         ESTAE 0                        CANCEL ESTAE ROUTINE
         LR    R1,R13                   SAVE NEW SAVEAREA ADDRESS
         L     R13,PREVSA               OLD SAVEAREA ADDRESS
         FREEMAIN RC,LV=WORKLEN,A=(1),SP=230,                          X
               RELATED=(GET,'FREE STORAGE')
         LTR   R15,R15                  FREEMAIN OKAY?
         BNZ   FREEMERR                 NO-
EXIT     EQU   *
         RETURN (14,12),,,RC=0          RESTORE REGS
RECOVERY DS    0H                       RECOVERY ROUTINE
         USING *,R9                     INFORM THE ASSEMBLER
         LR    R9,R15                   BASE ADDRESS
         LA    R3,NOSDWA(0,0)           NO SDWA INDICATOR
         CR    R0,R3                    ANY SDWA?
         BE    NOPSDWA                  NO-
SDWAPTR  EQU   *
         STM   R14,R12,12(R13)          STORE THE REGISTERS
         LR    R4,R1                    SDWA POINTER
         USING SDWA,R4                  INFORM THE ASSEMBLER
         L     R5,SDWAPARM              PARAM ADDRESS
         L     R6,0(,R5)                ADDRESS TO FREEMAIN
         LTR   R6,R6                    ANYTHING THERE?
         BZ    SETRPRET                 NO- JUST RETURN
         LM    R7,R8,4(R5)              LENGTH + SUBPOOL NO
         BAL   R2,FREEMN                FREEMAIN
SETRPRET EQU   *
         SETRP WKAREA=(4),RC=0,REGS=(14)
NOPSDWA  EQU   *
         LR    R10,R14                  SAVE RETURN ADDRESS
         LR    R5,R2                    SAVE PARM ADDRESS
         LM    R6,R8,0(R5)              R6 = ADDRESS TO FREE
*                                       R7 = LENGTH OF AREA TO FREE
*                                       R8 = SUBPOOL NO
         LTR   R6,R6                    ANYTHING THERE?
         BZ    NOSDWART                 NO- JUST RETURN
         BAL   R2,FREEMN                FREEMAIN
NOSDWART EQU   *
         SR    R15,R15                  CONTINUE WITH TERMINATION
         BR    R10                      RETURN TO CALLER
FREEMN   EQU   *           FREEMAIN AREA OBTAINED IN THE MAIN ROUTINE
         FREEMAIN RC,A=(R6),LV=(R7),SP=(R8)
         LTR   R15,R15                  ANY PROBLEMS?
         BZR   R2                       NO- RETURN TO CALLER
         WTO   'MPF37303 - FREEMAIN ERROR IN ESTAE RECOVERY ROUTINE -PLX
               EASE INFORM CSD',ROUTCDE=4,DESC=2
         BR    R2                       RETURN TO CALLER
         LTORG
LOWZERO  DC    X'00'                   LOW-VALUES
JCTIDT   DC    CL4'JCT '               JCT
JOB      DC    CL3'JOB'                 BATCH JOB
TESTCLAS DS    0C                      TEST INITIATOR CLASSES
         DC    C'A',C'E',C'N',C'O',C'T',C'Z'
TESTCLLN EQU   *-TESTCLAS              TESTCLASS TABLE LENGTH
TESTCLNE EQU   TESTCLLN/L'TESTCLAS     NO OF TABLE ENTRIES
TESTDSCT DSECT
JOBCLASS DS    C                       INITIATOR CLASS
         IHAPSA DSECT=YES
         IEZVX100
         CVT DSECT=YES,LIST=NO
         IHAASCB DSECT=YES,LIST=NO
         IKJTCB DSECT=YES,LIST=NO
         IFGRPL DSECT=YES,AM=VSAM
         IEZJSCB
         IEFJSSIB
         $BUFFER
         $TQE
         $JCT
         $SJB
         IHASDWA
HASPMSG  DSECT
HASPCHAR DS    C                       JES2 COMMAND LENGTH
HASPMSNO DS    CL7                     HASP395
         DS    C
HASPJBNM DS    CL8                     JOBNAME
         DS    C
HASPSTRT DS    CL5                     ENDED
WORKAREA DSECT
SAVEAREA DS    CL72                    SAVEAREA
PREVSA   EQU   SAVEAREA+4,4            ADDRESS OF PREVIOUS SAVEAREA
ESTAEW   ESTAE MF=L
LESTAEW  EQU   *-ESTAEW                 ESTAE LENGTH
ESTAPARM DS    0F
ESTPARM1 DS    F                        ESTAE PARAM 1
ESTPARM2 DS    F                        ESTAE PARAM 2
ESTPARM3 DS    F                        ESTAE PARAM 3
WORKLEN  EQU   *-WORKAREA              WORKAREA LENGTH
         END  HASP395


R F Perretta
Chief Systems Programmer (UK)                  c Xephon 1992

