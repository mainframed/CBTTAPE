Altering the retention period and expiry date

We recently experienced problems deleting datasets which had
expiry dates or retention periods. Our installation has a mixture of
SMS-managed and non-SMS-managed datasets.

The ALTER command changes the expiry date/retention period
in the catalog. Looking at the dataset using option 3.2 of ISPF
shows the expiry date/retention period recorded in the format 1
DSCB, while the LISTC command lists the expiry date/retention
period from the catalog. Attempts to delete the dataset using ISPF
option 3.2 return with an 'uncatalog error': the dataset is deleted
but the uncatalog fails because the expiry date/retention period in
the catalog prevents it.

SMS-managed datasets don't cause such problems because they
have the same expiry date/retention period in the format 1 DSCB
and the catalog. The expiry date/retention period should be altered
in both the format 1 DSCB and the catalog for both SMS and
non-SMS datasets.

Because the owners of datasets don't normally know where the
dataset resides, we have written the following REXX EXEC,
which checks for the management type of the dataset to be altered.
The REXX EXEC uses a REXX function which is based on the
article Retrieving DFSMS dataset classes by Ernie Ishman in the
June 1992 issue of MVS Update. The REXX function is invoked
like this:

      X = SYSRSMS(MGMTCLAS,dsname)

When the dataset is non-SMS-managed, X contains the value
'NOTSMS', otherwise it contains the management class. We also
use a program, ESCLOSE, which is link-edited in
SYS1.LINKLIB. ESCLOSE is an Assembler program that
performs an OPEN then a CLOSE of the dataset allocated to the
DDNAME ESCLOSE.

The REXX EXEC can be invoked from ISPF option 3.4 by
placing ESALTER before the dataset or it can be started under
TSO or in another REXX EXEC or CLIST.

The REXX EXEC runs successfully under MVS/ESA 4.2.2, DFP
3.3.0, ISPF/PDF 3.3.0, and TSO/E 2.3.1.

/* REXX  EXEC  ESALTER */
/* ----------------------------------------------------------------- */
/* Change expiry date or retention period                            */
/* Activate from ISPF option 3.4 or option 6                         */
/*               REXX EXEC or CLIST                                  */
/* Prompt for expiry date or retention period.                       */
/* ----------------------------------------------------------------- */
arg dsns                                 /* rexx entered from opt3.4 */
if dsns = ' '  then do                   /* if blank, rexx entered - */
   say enter full dsname                 /* from tso                 */
   pull dsns                             /* get dsn                  */
end
dsname = dsns
dsname = strip(dsname,b,"'")             /* strip quotes             */
sclass = sysrsms(mgmtclas,dsname)        /* dataset sms-managed?     */
if sclass = notfound then do             /* dataset not found        */
   say dsname " not found/allocated"
   exit
end
too  = ''                                /* init variable            */
do while too  = ''
  say "enter expiry date (yyyyddd) or ",
      "retention period (dddd)"
  pull too                               /* get answer               */
  if datatype(too,num) then do
     if length(too) <= 4 then type = retpd
     if length(too) > 7 then do
        say 'expdt/retpd too many digits' too
        exit
     end
     if length(too) = 5 or length(too) = 6 then do
        say 'expdt/retpd wrong format' too
        exit
     end
     if length(too) = 7 then type = expdt
  end
  else do
     say 'expdt/retpd not numeric' too
     exit
  end
end
if sclass = notsms then do               /* dataset not sms-managed  */
   if type = expdt then do
      date = substr(too,1,4)"/"substr(too,5,3)
      too = date
   end
   say "ALLOC F(ESCLOSE) DA('"DSNAME"') MOD "TYPE"("TOO")"
   "ALLOC F(ESCLOSE) DA('"DSNAME"') MOD "TYPE"("TOO")"
   ESCLOSE
   "FREE F(ESCLOSE)"
end
else do
   if type = retpd then type = for
   else type = to
   say "ALTER '"DSNAME"' "type"("too")"
   ALTER "'"DSNAME"' "type"("too")"
end


Walter Winnelinckx
Systems Programmer
Groep Boerenbond (Belgium)                                                       c Xephon 1994

