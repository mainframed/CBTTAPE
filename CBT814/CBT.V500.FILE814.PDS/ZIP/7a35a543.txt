An ISPF edit macro for change control

An example of an edit macro to track changed statements is
shown in SC34-4018 ISPF/PDF Edit Macros.  The following is
an improved version which is able to show any level of change.
Note that deleted statements are not recorded and therefore can
not be tracked.

 /*   EDIT MACRO TO FIND STATEMENTS CHANGED
 /*   PARAMETER: EXPLICIT LEVEL TO LOOK FOR (OPTIONAL); WILL DEFAULT
 /*              TO LATEST LEVEL.
 /*   SECOND PARAMETER: IF SPECIFIC LEVEL LESS THAN LATEST LEVEL SHOULD
 /*              BE SEARCHED, SPECIFY: ONLY. WILL DEFAULT TO LOOK FOR
 /*              ALL LEVELS EQUAL TO OR HIGHER INDICATED IN FIRST
 /*              PARM, IF ANY.
PROC 0
CONTROL   MSG NOFLUSH NOLIST NOCONLIST NOSYMLIST
ISREDIT MACRO (CHANGE ONLY) PROCESS
ERROR DO
 SET &RET = &LASTCC
 RETURN
END
ISREDIT (SAVE) = USER_STATE
ISREDIT (NUMBER,NUMTYPE) = NUMBER
ISREDIT (STATS) = STATS
ISREDIT (LEVEL) = LEVEL
IF &STR(&CHANGE..) = . THEN DO
 SET &CHANGE = &LEVEL
END
SET &SYSDVAL = &STR(&NUMTYPE)         /* SET &SYSDVAL TWICE TO SPLIT */
SET &SYSDVAL = &STR(&SYSDVAL)         /* INTO TOKENS IN READDVAL    */
READDVAL &R1 &R2 &R3
IF &STATS = OFF OR &NUMBER = OFF OR &R1 = COBOL OR +
(&R1 = NOSTD AND &R2 = COBOL) THEN DO
 SET &ZEDLMSG = &STR(CAN ONLY BE PERFORMED WITH NUMBER STD AND STATS ON)
 ISPEXEC SETMSG MSG(ISRZ001)
 EXIT CODE(8)
END
IF &DATATYPE(&STR(&CHANGE)) ^= NUM THEN DO
 SET &ZEDLMSG = &STR(DESIRED LEVEL MUST BE NUMERIC)
 ISPEXEC SETMSG MSG(ISRZ001)
 EXIT CODE(8)
END
IF &CHANGE > &LEVEL THEN DO
 SET &ZEDLMSG = &STR(SEARCH LEVEL IS TOO HIGH; MAX LEVEL IS &LEVEL)
 ISPEXEC SETMSG MSG(ISRZ001)
 EXIT CODE(8)
END
SET &SCANLEV = &CHANGE
ISREDIT NUMBER = OFF
ISREDIT (RECFM) = RECFM
IF &RECFM = F THEN DO
 ISREDIT (LRECL) = LRECL
 SET &COLB = &LRECL - 1
 SET &COLE = &LRECL
END
IF &RECFM = V THEN DO
 SET &COLB = 7
 SET &COLE = 8
END
SET &HITS = 0
ISREDIT EXCLUDE ALL
DO WHILE &SCANLEV LE &LEVEL
 ISREDIT FIND ALL '&SCANLEV' &COLB &COLE
 ISREDIT (FINDS) = FIND_COUNTS
 SET &HITS = &FINDS + &HITS
 SET &SCANLEV = &SCANLEV + 1
 IF &STR(&ONLY) = ONLY THEN DO
  SET &SCANLEV = LEVEL + 1
 END
END
ISREDIT USER_STATE = (SAVE)
IF &HITS = 0 THEN DO
 ISREDIT RESET EXCLUDED
END
ELSE DO
 ISREDIT UP MAX
END
IF &CHANGE = &LEVEL OR &STR(&ONLY..) = ONLY. THEN DO
 SET &ZEDLMSG = +
 &STR(NUMBER OF CHANGES AT LEVEL &CHANGE IS &HITS)
END
ELSE DO
 SET &ZEDLMSG = +
 &STR(NUMBER OF CHANGES AT LEVEL &CHANGE OR HIGHER IS &HITS)
END
ISPEXEC SETMSG MSG(ISRZ001)
EXIT CODE(&MAXCC)


Nils Plum
Systems Programmer (Denmark) © Nils Plum 1989

