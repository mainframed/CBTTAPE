Keeping a record of IPLs

In our installation we have two resident volumes for security
and maintenance purposes. This allows us to perform an IPL
from either volume.

In order to keep track of IPLs we have written a program that is
executed automatically from SYS1.PARMLIB(COMMND00).
This program records IPL information (system name, resident
volume name, physical address, date, and time) on SYSLOG.
It also adds a new record to a history file (BSAM) that can be
read from TSO (browse) or another program. The file
(DDIPLS) is created in PDF, with the following parameters:

o      Space units - track
o      Primary quantity - 3
o      Record format - FB
o      Record length - 80
o      Block size - 80.

Our CPU is an IBM 9121-260 running MVS/ESA 4.2 and
JES2.

SUTLCIPL CSECT                      BEGINNING OF PROGRAM
         USING *,R12                SET UP BASE REG
         SAVE  (14,12),,SUTLCIPL..&SYSDATE    SAVE REGISTERS
         LR    R12,R15
         LR    R2,R13
         LA    R13,IPOSAVE
         ST    R13,8(R2)
         ST    R2,4(R13)
* ADDRESS 0 = PSA (PREFIXED SAVE AREA)
* MAPS FIXED HARDWARE AND SOFTWARE STORAGE FOR THE RELATED PROCESSOR
* OFFSET OF X'10' (16) = FLCCVT (ADDRESS CVT (COMMUNICATIONS VECTOR
* TABLE))
         LA    R3,16                R3=POINTER CVT
         L     R3,0(R3)             R3=ADDRESS CVT
         LR    R7,R3                R7=ADDRESS CVT
         L     R3,196(R3)           R3=CVT + 196 POINTER TO CVTSMCA
* CVTSMCA = SYSTEM MANAGEMENT FACILITIES CONTROL AREA, WITH SMF
         LA    R4,16(R3)            R4=ADDRESS SMCASID (SYSTEM
*                                              IDENTIFICATION)
         MVC   CPU,0(R4)            MOVE SMCASID TO CPU
         L     R5,48(R7)            R5=CVT+48, POINTER TO CVTSYSAD
*                        (UCB ADDRESS  FOR THE SYSTEM RESIDENCE VOLUME)
         LA    R5,28(R5)           R5=UCB+28(ADDRESS NAME OF RESIDENCE)
         MVC   RESI,0(R5)           MOVE NAME OF RESIDENCE TO RESI
         L     R5,48(R7)            R5=CVT + 48, POINTER TO CVTSYSAD
         LA    R5,13(R5)            R5=UCB+13,PHYSICAL ADDRESS OF IPL
         MVC   ADRESSA,0(R5)        MOVE ADDRESS OF IPL TO ADRESSA
         LA    R4,340(R3)           R4=ADDRESS SMCAIDTE
         MVC   DATESMC,0(R4)        MOVE DATE OF IPL TO DATESMC
         LA    R4,336(R3)           R4=ADDRESS SMCAITM
         MVC   HORASMC,0(R4)        MOVE TIME OF IPL TO HORASMC
         UNPK  DATELEG,DATESMC      UNPACK DATE
         OI    DATELEG,X'F0'        MOVE HEX'F0' TO ZONE LAST BYTE
         L     R4,HORASMC           R4=TIME IPL
         CVD   R4,HORAPAK           CONVERT TO DECIMAL
         DP    HORAPAK,P36          DIVIDE BY 360000 = HOURS
         MVC   HORES1,HORAPAK       MOVE TO WORK
         UNPK  HORES,HORES1         UNPACK
         OI    HORES+3,X'F0'        MOVE HEX 'F0' TO ZONE LAST BYTE
         MVC   HH,HORES+2           MOVE TO OUTPUT
         MVC   TEMP1+4(4),HORAPAK+4 MOVE TO WORK REST DIVISION
         DP    TEMP1,P60            DIVIDE BY 6000 = MINUTES
         MVC   MINS1,TEMP1          MOVE TO WORK
         UNPK  MINS,MINS1           UNPACK
         OI    MINS+3,X'F0'         MOVE HEX 'F0' TO ZONE LAST BYTE
         MVC   MM,MINS+2            MOVE TO OUTPUT
         MVC   TEMP2+4(4),TEMP1+4   MOVE TO WORK REST DIVISION
         DP    TEMP2,P10            DIVIDE BY 100 = SECONDS
         MVC   SEGS1,TEMP2          MOVE TO WORK
         UNPK  SEGS,SEGS1           UNPACK
         OI    SEGS+3,X'F0'         MOVE HEX 'F0' TO LAST BYTE
         MVC   SS,SEGS+2            MOVE TO OUTPUT
         MVC   IPOWTOL+5(50),BLANCOS
         MVC   IPOWTOL+5(23),=C'SYSTEM NAME =          '
         MVC   IPOWTOL+28(4),CPU
         WTO   MF=(E,IPOWTOL)
         MVC   IPOWTOL+5(50),BLANCOS
         MVC   IPOWTOL+5(23),=C'RESIDENT VOLUME NAME = '
         MVC   IPOWTOL+28(6),RESI
         WTO   MF=(E,IPOWTOL)
         MVC   IPOWTOL+5(50),BLANCOS
         MVC   IPOWTOL+5(23),=C'RESIDENT PHYSICAL ADDR='
         MVC   IPOWTOL+28(3),ADRESSA
         WTO   MF=(E,IPOWTOL)
         MVC   IPOWTOL+5(50),BLANCOS
         MVC   IPOWTOL+5(23),=C'IPL DATE =             '
         MVC   IPOWTOL+28(5),DATELEG
         WTO   MF=(E,IPOWTOL)
         MVC   IPOWTOL+5(50),BLANCOS
         MVC   IPOWTOL+5(23),=C'IPL TIME =             '
         MVC   IPOWTOL+28(6),HORA
         WTO   MF=(E,IPOWTOL)
OPEN     EQU   *
         OPEN  (DDIPLS,(EXTEND))
WRITE    EQU   *
         WRITE EVENTO,SF,DDIPLS,WORKA
         CHECK EVENTO
         CLOSE (DDIPLS)
RETURN   EQU   *
         L     R13,IPOSAVE+4
         RETURN (14,12),T,RC=(15)
         BR    11
IPOSAVE  DS    18F
HORASMC  DS    CL8
WORKA    DS    OCL80
IPLACT   DS    OCL11
DATELEG  DS    CL5
HORA     DS    OCL6
HH       DS    CL2
MM       DS    CL2
SS       DS    CL2
ADRESSA  DS    CL3
RESI     DS    CL6
CPU      DS    CL4
RESTO    DC    56C' '
DATESMC  DS    PL4
HORAPAK  DS    1D
P36      DC    PL4'360000'
HORES1   DS    1F
HORES    DS    CL4
TEMP1    DC    1D'0'
P60      DC    PL4'6000'
MINS1    DS    CL4
MINS     DS    CL4
TEMP2    DC    1D'0'
P10      DC    PL4'100'
SEGS1    DS    CL4
SEGS     DS    CL4
BLANCOS  DC    50C' '
MENSAJE  DS    CL70
DDIPLS   DCB   DDNAME=DDIPLS,DSORG=PS,MACRF=(W),BLKSIZE=80,            X
               LRECL=80,RECFM=FB,DEVD=DA
IPOWTOL  WTO   '                                                       X
                              ',ROUTCDE=(1,5,11),DESC=(7),MF=L
         LTORG
         END                     , END OF PROGRAM


Joaquin Aramendia Barrau (Spain)                        c Xephon 1993

