REXX EXEC to print files

The following REXX EXEC can be invoked under MVS with
TSO to print whole files or parts of files on the system printer. The
EXEC will even work with members of PDSs or PDSEs.

/* REXX */
/* *******************************************************************/
/* * Procedure REXX . Print files or PDS members                     */
/* * M A C R O      . output on  RMT0 (EXPLOITATION)  .              */
/* *******************************************************************/
Address ISPEXEC
 "ISREDIT MACRO (CLASS) NOPROCESS"         /* this is a MACRO ...    */
Select
  When Class = '' Then
    Do
      Class = 'A'                          /*output class (default)  */
    End
  When Class = '?' Then
    Do
      Call Aide
      Exit
    End
  When Length(Class) > 9 | Datatype(Class) = 'NUM' Then
    Do
      ZEDSMSG = "invalid parameter"
      ZEDLMSG = "output class must be a letter or < 9    "
      "SETMSG MSG(ISRZ001)"
      Exit 12
    End
  Otherwise
      Upper Class
End /* Select */
 "ISREDIT (NBCH) = DATA_CHANGED"           /*if the file was modified*/
 If NBCH = 'YES' Then                      /* ............           */
   Do
     ZEDSMSG = "file was modified       "
     ZEDLMSG = "Save it before printing          or cancel       "
     "SETMSG MSG(ISRZ001)"
     Exit 12
   End
 "ISREDIT (DSN) = DATASET"                 /* Name of file           */
 "ISREDIT (MEM) = MEMBER"                  /* Name of member         */
  If MEM^='' Then Fichier="'"DSN"("MEM")'" /* the full name          */
             Else Fichier="'"DSN"'"
 "ISREDIT PROCESS RANGE C"                 /* Interception CMD 'C'   */
Select
  When rc = 0 Then
    Do                                     /* a part of the file     */
      "ISREDIT (LINE1) = LINENUM .ZFRANGE" /* first line to print    */
      "ISREDIT (LINE2) = LINENUM .ZLRANGE" /* last line to print     */
      NBLG = Line2 - Line1 + 1             /* Number of lines        */
    End
  When rc = 4 Then
    Do                                     /* all the file           */
      "ISREDIT (NBLG) = LINENUM .ZLAST"    /* Number of lines        */
      Line1 = 1                            /* first line             */
      Line2 = NBLG                         /* last line              */
    End
  Otherwise
    Exit 12
End /* Select */
Do
  NBLG=Strip(Format(NBLG,6,0),b,' ')
  ZEDSMSG = NBLG "lines written "
  ZEDLMSG = "... on printer       RMT0 (Exploitation) in class "Class
End
Address TSO
 "PRINTDS DSNAME("fichier")",
          "DEST(RMT0)",
          "CLASS("Class")",
          "LINES("Line1":"Line2")"
Address ISPEXEC
 "SETMSG MSG(ISRZ000)"
Exit rc
AIDE:
Say ''
Say 'Procedure REXX . Print files or members          .              '
Say 'M A C R O      . output on  RMT0 (EXPLOITATION)  .              '
Say ''
Say 'Syntax :  MLIST0 (Class)                                        '
Say '          Enter  MLIST0 in command line and use the 'c' command '
Say '          in edit mode to choose the part of the file to        '
Say '          print    (default is the full file         )          '
Say '                                                                '
Say '          you can use any output class                          '
Say '                    (default is a)                              '
Say ''
Return


Claude Dunand (France)                                                c Claude Dunand 1994

