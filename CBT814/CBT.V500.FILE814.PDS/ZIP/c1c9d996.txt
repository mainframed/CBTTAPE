Checking for dynamic SVC updating

With the advent of MVS/ESA came a new facility that allows
an authorized user to update the MVS SVC table to intercept
SVC calls with their own SVC front-end. This facility is
implemented via the SVCUPDTE macro. Prior to the
availability of SVCUPDTE, vendor products would frequently
load their own routines into global storage and 'steal' the SVC
table entry of a defined SVC to replace it with their own hooks
or intercept points. This mechanism is how many products
dynamically hooked themselves into the operating system. A
problem arises, however, when trying to perform debugging if
such SVC hooks are implemented. It makes it more difficult to
know who is involved in system problems when SVCs are
constantly being front-ended.

To help in the debugging process, I have modified a program I
wrote many years ago which originally just displayed SVC
table entries along with the SVC address, type, lock indicators,
and attribute bytes. In order to find out who was performing
SVC front-ending, either dynamically or by SVCUPDTE, I
modified the program to show the storage location of the SVC
code pointed to by the SVC table entry. Additionally, I dump
the first few bytes of the SVC itself in order to identify the
SVC code by any eyecatcher that might be present in the
module.

The program also labels each SVC by location (ie CSA, SQA,
PLPA, MLPA, FLPA, NUCLEUS, ECSA, ESQA, EPLPA,
EMLPA, EFLPA, ENUCLEUS) to determine if the SVC exists
in some non-standard system area, that is not in the above- or
below-the-line nucleus or LPA areas. SVCs which have
addresses pointing to above- or below-the-line CSA or SQA are
SVCs which have been added after IPL time, either through
dynamic front-ending or through SVCUPDTE.

By examining the module header information from the SVC,
one can hopefully determine what software has performed the
front-ending. As an example, CA-TOP SECRET installs itself
by front-ending the RACF SVC entries as well as others
including LINK, LOAD, XCTL, and STOW. The SVCMAP
program will show the CA-TOP SECRET eyecatcher as well as
the fact that the SVC entries point into CSA, proving that the
SVCs have been front-ended.

One interesting thing the program also does is to check the
Extended Service Router (ESR) SVCs. An ESR SVC is
actually just a router to another routine that is chosen based on
its ESR-id: the ESR-id is a value from 0 to 255 passed in
register 15 at the time of the SVC invocation. The current ESR
SVCs provided by IBM are SVCs 109, 116, 122, and 135. The
SVC table entry for an ESR SVC points to another table, which
tells how many ESR-ids are supported by the ESR SVC
followed by a series of entries similar to the SVC table entries,
which point to the actual ESR SVC code along with its lock
and attribute information.

In rewriting SVCMAP, I have included code to check for an
ESR SVC. When an ESR SVC is found (as indicated by a bit
in the SVC table entry), SVCMAP will map out each ESR-id
supported by the ESR SVC. You may notice that SVC 109 has
256 (0-255) ESR-ids defined; however, most of them are
unused and point to a routine which will issue a system 16D
abend: this indicates that an invalid ESR-id was requested. This
is similar to many of the SVC table entries which point to the
SVC that is labelled IGCERROR in the SVCMAP program.
IGCERROR is the routine which issues a system Fxx abend
(where xx is the hexadecimal SVC number) to indicate that an
invalid SVC was requested.

Armed with the information provided by SVCMAP, the
systems programmer will be better prepared to solve problems
relating to routines which perform dynamic SVC front-ending.
Of course, there is still the problem of what happens when
multiple products front-end the same SVC, as is the case with
ABEND-AID and EYEWITNESS (and I am sure that there are
many more examples). In such a case, the last software product
to steal the SVC table entry will be the one shown.

The problem then becomes to figure out where it saved the
original SVC table address it replaced. It may not be obvious
that, as a software product steals an SVC, it must save the old
address unless it is completely replacing the functions of the
old SVC. Since there is no convention as to where the stolen
SVC address should be placed, if more that one product is
performing front-ending, the task of tracking down those
products involved becomes formidable.

Another point needs to be made about SVCMAP: there is an
Assembler symbolic variable called &RECSZ at the front of
the program. It is currently set to 132 to allow more module
header information to be printed out - this assumes that the
listing will be printed. If you would like to get the same
information but at your TSO screen, &RECSZ can be changed
to 79, which will cause the entire program to format print lines
79 bytes in length. This will facilitate allocating the
SYSPRINT DD statement to your terminal session using the
TSO command:

          ALLOC DD(SYSPRINT) DA(*)

to view output at your terminal. You also might want to change
the names of the SVCs in the SVC name table in the program
to make them more meaningful for your installation.

SVCMAP   CSECT                         ESTABLISH CSECT
SVCMAP   AMODE 31
SVCMAP   RMODE 24
         GBLC  &RECSZ
&RECSZ   SETC  '132'
         SAVE  (14,12),,SVCMAP-&SYSDATE
         REGS  ,                       MACRO FOR REGISTER EQUATES
         LR    R12,R15                 LOAD R12 W/EPA ADDRESS
         USING SVCMAP ,R12             ESTABLISH ADDRESSABLITY
         GETMAIN RU,LV=WORKLEN         GETMAIN WORKAREA
         ST    R13,4(,R1)              ST CALLER'S S/A ADDR IN MY S/A
         ST    R1,8(,R13)              ST MY S/A ADDR IN CALLER'S S/A
         LR    R13,R1                  LOAD ADDR OF MY S/A IN R13
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY
         OPEN  (SYSPRINT,OUTPUT)       OPEN OUTPUT FILE
         LTR   R15,R15                 WAS IT SUCCESSFUL?
         BNZ   ERR01                   NO, GO ISSUE ERROR MESSAGE
         L     R3,CVTPTR               LOAD A(CVT)
         L     R2,CVTSMEXT-CVT(,R3)    LOAD A(CVT STG. MAP EXTENSION)
         USING CVTVSTGX,R2
         L     R15,CVTMLPAE            LOAD A(MLPA END)
         ST    R15,MLPAHIGH            STORE A(MLPA END)
         L     R15,CVTFLPAE            LOAD A(FLPA END)
         ST    R15,FLPAHIGH            STORE A(FLPA END)
         L     R15,CVTPLPAE            LOAD A(PLPA END)
         ST    R15,PLPAHIGH            STORE A(PLPA END)
         L     R15,CVTEPLPE            LOAD A(EPLPA END)
         ST    R15,EPLPHIGH            STORE A(EPLPA END)
         L     R15,CVTEFLPE            LOAD A(EFLPA END)
         ST    R15,EFLPHIGH            STORE A(EFLPA END)
         L     R15,CVTEMLPE            LOAD A(EMLPA END)
         ST    R15,EMLPHIGH            STORE A(EMLPA END)
         L     R15,CVTNUCE             LOAD A(NUC END)
         ST    R15,NUCHIGH             STORE A(NUC END)
         L     R15,CVTERWNE            LOAD A(ENUC END)
         ST    R15,ENUCHIGH            STORE A(ENUC END)
         L     R2,CVTGDA-CVT(,R3)      LOAD A(GDA)
         USING GDA,R2
         L     R15,GDACSA              LOAD A(CSA START)
         A     R15,GDACSASZ            ADD SIZE(CSA)
         BCTR  R15,R0                  DECREMENT
         ST    R15,CSAHIGH             STORE A(CSA END)
         L     R15,GDAECSA             LOAD A(ECSA START)
         A     R15,GDAECSAS            ADD SIZE(ECSA)
         BCTR  R15,R0                  DECREMENT
         ST    R15,ECSAHIGH            STORE A(ECSA END)
         L     R15,GDASQA              LOAD A(SQA START)
         A     R15,GDASQASZ            ADD SIZE(SQA)
         BCTR  R15,R0                  DECREMENT
         ST    R15,SQAHIGH             STORE A(SQA END)
         L     R15,GDAESQA             LOAD A(ESQA START)
         A     R15,GDAESQAS            ADD SIZE(ESQA)
         BCTR  R15,R0                  DECREMENT
         ST    R15,ESQAHIGH            STORE A(ESQA END)
         L     R2,CVTABEND-CVT(,R3)    LOAD A(SECONDARY CVT)
         L     R2,SCVTSVCT-SCVTSECT(,R2) LOAD A(SVC TABLE)
         L     R15,=A(PUT24)           LOAD A(PUT24) IN 24 BIT
         BSM   R0,R15                  GO TO 24 BIT MODE
PUT24    PUT   SYSPRINT,OTHDR1         PUT HEADER
         PUT   SYSPRINT,OTHDR2         PUT HEADER
         PUT   SYSPRINT,OTHDR3         PUT HEADER
         L     R15,=A(PUT24A+X'80000000') LOAD A(31 BIT MODE CODE)
         BSM   0,R15                   BRANCH IN 31 BIT MODE
PUT24A   NUCLKUP BYNAME,NAME=IGCERROR,ADDR=ADERROR
         NI    ADERROR,X'7F'           TURN OFF HIGH ORDER BIT
         LA    R9,256                  LOAD LOOP COUNTER
         SR    R7,R7                   ZERO REGISTER FOR SVC NUMBER
LOOP     MVC   OTREC,BLANKS            CLEAR OUTPUT RECORD
         MVI   OTLPAREN,C'('           INIT FIELD
         MVI   OTRPAREN,C')'           INIT FIELD
         LA    R8,SVCNAMTB             LOAD A(SVC NAME TABLE)
         LR    R10,R7                  COPY SVC NUMBER
         SLL   R10,3                   MULTIPLY BY 8 FOR IN
         AR    R8,R10                  ADD OFFSET TO SVC NAME TABLE
         AR    R10,R2                  ADD OFFSET TO SVC TABLE ORIGIN
         CVD   R7,DBLWORD              CONVERT SVC NUMBER TO DECIMAL
         UNPK  OTSVCDEC-1(4),DBLWORD+5(3) UNPACK SVC NUMBER
         OI    OTSVCDEC+2,X'F0'        FIX THE SIGN
         MVI   OTSVCDEC+3,C'('         START PARENTHESES
         XC    DBLWORD,DBLWORD         CLEAR AREA
         STC   R7,DBLWORD              STORE SVC NUMBER
         UNPK  OTSVCHEX(3),DBLWORD(2)  UNPACK SVC NUMBER
         MVI   OTSVCHEX+2,C')'         CLOSE PARENTHESES
         TR    OTSVCHEX,TRTAB          FIX UNPRINTABLE CHARACTERS
         TM    4(R10),SVCESR           IS THIS AN ESR SVC
         BO    ESRSTUFF PUT2A          YES, GO DO ESR STUFF
         MVC   SVCADDR,0(R10)          SAVE A(SVC)
         NI    SVCADDR,X'7F'           TURN OFF HIGH ORDER BIT
         UNPK  OTEPALOC(9),0(5,R10)    UNPACK ADDRESS
         TR    OTEPALOC,TRTAB          FIX UNPRINTABLE CHARACTERS
         MVI   OTEPALOC+8,C' '         CLEAR GARBAGE FROM UNPK
         MVC   OTSVCNAM,0(R8)          MOVE SVC NAME
         CLC   SVCADDR,ADERROR         IS IT A(IGCERROR)
         BNE   SKIPERR                 NO, SKIP IT
         MVC   OTSVCNAM,IGCERROR       ELSE, SAY THAT IT'S UNUSED
SKIPERR  SR    R15,R15                 CLEAR REGISTER
         IC    R15,4(,R10)             GET ATTRIBUTES
         UNPK  OTSVCLLA,6(2,R10)       UNPACK LOCKS
         STC   R15,OTSVCLLA+2          SAVE TYPE
         OI    OTSVCLLA+2,X'F0'        FIX SIGN
         TR    OTSVCLLA,TRTAB          FIX UNPRINTABLE CHARACTERS
         SRL   R15,5                   SHIFT TO GET TYPE
         IC    R15,TYPES(R15)          INDEX INTO TYPE TABLE
         STC   R15,OTSVCTYP+1          MOVE TO LINE
         BAL   R14,CHECKLOC            GO CHECK LOCATION
         CLC   OTSVCNAM,IGCERROR       IS IT IGCERROR
         BE    PUTLINE                 YES, DON'T BOTHER WITH HEADER
         L     R15,SVCADDR             LOAD A(SVC)
         MVC   OTSVCHDR,0(R15)         MOVE SVC HEADER INFO
         B     PUTLINE                 GO PUT OUT DETAIL LINE
ESRSTUFF L     R4,0(,R10)              LOAD A(ESR TABLE)
         L     R5,4(,R4)               LOAD NUMBER OF ESRS FOR SVC
         SR    R6,R6                   ZERO REGISTER FOR ESR NUMBER
LOOP2    LR    R11,R6                  COPY SVC NUMBER
         XC    DBLWORD,DBLWORD         CLEAR AREA
         STC   R11,DBLWORD             STORE SVC NUMBER
         UNPK  OTSVCNAM+6(3),DBLWORD(2) UNPACK SVC NUMBER
         TR    OTSVCNAM,TRTAB          FIX UNPRINTABLE CHARACTERS
         MVC   OTSVCNAM(L'ESR),ESR     MOVE ESR EYECATCHER
         MVC   OTSVCNAM+8,C' '         CLEAR GARBAGE FROM UNPK
         SLL   R11,3                   MULTIPLY BY 8 FOR IN
         AR    R11,R4                  ADD OFFSET TO ESR TABLE ORIGIN
         LA    R11,8(,R11)             ADD HEADER OFFSET
         MVC   SVCADDR,0(R11)          SAVE A(SVC)
         NI    SVCADDR,X'7F'           TURN OFF HIGH ORDER BIT
         UNPK  OTEPALOC(9),0(5,R11)    UNPACK ADDRESS
         TR    OTEPALOC,TRTAB          FIX UNPRINTABLE CHARACTERS
         MVI   OTEPALOC+8,C' '         CLEAR GARBAGE FROM UNPK
         SR    R15,R15                 CLEAR REGISTER
         IC    R15,4(,R11)             GET ATTRIBUTES
         UNPK  OTSVCLLA,6(2,R11)       UNPACK LOCKS
         STC   R15,OTSVCLLA+2          SAVE TYPE
         OI    OTSVCLLA+2,X'F0'        FIX SIGN
         TR    OTSVCLLA,TRTAB          FIX UNPRINTABLE CHARACTERS
         SRL   R15,5                   SHIFT TO GET TYPE
         IC    R15,TYPES(R15)          INDEX INTO TYPE TABLE
         STC   R15,OTSVCTYP+1          MOVE TO LINE
         BAL   R14,CHECKLOC            GO CHECK LOCATION
         L     R15,SVCADDR             LOAD A(SVC)
         MVC   OTSVCHDR,0(R15)         MOVE SVC HEADER INFO
         L     R15,=A(PUT3)            PREPARE FOR 24-BIT PUT
         BSM   R0,R15                  GO THERE IN 24-BIT MODE
PUT3     PUT   SYSPRINT,OTREC          PUT LINE
         L     R15,=A(PUT3A+X'80000000') PREPARE FOR 31-BIT MODE AGAIN
         BSM   R0,R15                  GO THERE IN 31-BIT MODE
PUT3A    LA    R6,1(,R6)               ADD 1 TO SVC NUMBER
         BCT   R5,LOOP2                LOOP TO GET NEXT ESR TBL ENTRY
         B     PUT2A                   ELSE, GO GET SVC TBL ENTRY
PUTLINE  L     R15,=A(PUT2)            PREPARE FOR 24-BIT PUT
         BSM   R0,R15                  GO THERE IN 24-BIT MODE
PUT2     PUT   SYSPRINT,OTREC          PUT LINE
         L     R15,=A(PUT2A+X'80000000') PREPARE FOR 31-BIT MODE AGAIN
         BSM   R0,R15                  GO THERE IN 31-BIT MODE
PUT2A    LA    R7,1(,R7)               ADD 1 TO SVC NUMBER
         BCT   R9,LOOP                 LOOP TO GET NEXT SVC TBL ENTRY
         CLOSE SYSPRINT                CLOSE OUTPUT FILE
RETURN   LR    R1,R13                  LOAD R1 W/A(SAVEAREA)
         L     R13,4(,R13)             LOAD R13 W/ADDR OF CALLER'S S/A
         FREEMAIN RU,LV=WORKLEN,A=(1)  FREEMAIN WORKAREA
         RETURN (14,12),RC=0           RETURN TO OS
CHECKLOC CLC   SVCADDR,CSAHIGH         IS ADDR IN RANGE
         BH    CKMLPA                  NO, GO CHECK NEXT AREA
         MVC   OTSVCLOC,CSA            ELSE, SHOW WHERE IT IS
         BR    R14                     RETURN TO MAINLINE
CKMLPA   CLC   SVCADDR,MLPAHIGH        IS ADDR IN RANGE
         BH    CKFLPA                  NO, GO CHECK NEXT AREA
         MVC   OTSVCLOC,MLPA           ELSE, SHOW WHERE IT IS
         BR    R14                     RETURN TO MAINLINE
CKFLPA   CLC   SVCADDR,FLPAHIGH        IS ADDR IN RANGE
         BH    CKPLPA                  NO, GO CHECK NEXT AREA
         MVC   OTSVCLOC,FLPA           ELSE, SHOW WHERE IT IS
         BR    R14                     RETURN TO MAINLINE
CKPLPA   CLC   SVCADDR,PLPAHIGH        IS ADDR IN RANGE
         BH    CKSQA                   NO, GO CHECK NEXT AREA
         MVC   OTSVCLOC,PLPA           ELSE, SHOW WHERE IT IS
         BR    R14                     RETURN TO MAINLINE
CKSQA    CLC   SVCADDR,SQAHIGH         IS ADDR IN RANGE
         BH    CKNUC                   NO, GO CHECK NEXT AREA
         MVC   OTSVCLOC,SQA            ELSE, SHOW WHERE IT IS
         BR    R14                     RETURN TO MAINLINE
CKNUC    CLC   SVCADDR,NUCHIGH         IS ADDR IN RANGE
         BH    CKENUC                  NO, GO CHECK NEXT AREA
         MVC   OTSVCLOC,NUC            ELSE, SHOW WHERE IT IS
         BR    R14                     RETURN TO MAINLINE
CKENUC   CLC   SVCADDR,ENUCHIGH        IS ADDR IN RANGE
         BH    CKESQA                  NO, GO CHECK NEXT AREA
         MVC   OTSVCLOC,ENUC           ELSE, SHOW WHERE IT IS
         BR    R14                     RETURN TO MAINLINE
CKESQA   CLC   SVCADDR,ESQAHIGH        IS ADDR IN RANGE
         BH    CKEPLPA                 NO, GO CHECK NEXT AREA
         MVC   OTSVCLOC,ESQA           ELSE, SHOW WHERE IT IS
         BR    R14                     RETURN TO MAINLINE
CKEPLPA  CLC   SVCADDR,EPLPHIGH        IS ADDR IN RANGE
         BH    CKEFLPA                 NO, GO CHECK NEXT AREA
         MVC   OTSVCLOC,EPLPA          ELSE, SHOW WHERE IT IS
         BR    R14                     RETURN TO MAINLINE
CKEFLPA  CLC   SVCADDR,EFLPHIGH        IS ADDR IN RANGE
         BH    CKEMLPA                 NO, GO CHECK NEXT AREA
         MVC   OTSVCLOC,EFLPA          ELSE, SHOW WHERE IT IS
         BR    R14                     RETURN TO MAINLINE
CKEMLPA  CLC   SVCADDR,EMLPHIGH        IS ADDR IN RANGE
         BH    CKECSA                  NO, GO CHECK NEXT AREA
         MVC   OTSVCLOC,EMLPA          ELSE, SHOW WHERE IT IS
         BR    R14                     RETURN TO MAINLINE
CKECSA   CLC   SVCADDR,ECSAHIGH        IS ADDR IN RANGE
         BH    NOAREA                  NO, GO SHOW UNKNOWN
         MVC   OTSVCLOC,ECSA           ELSE, SHOW WHERE IT IS
         BR    R14                     RETURN TO MAINLINE
NOAREA   MVC   OTSVCLOC,UNKNOWN        GO SHOW UNKNOWN
         BR    R14                     RETURN TO MAINLINE
ERR01    WTO   'SVC001I SYSPRINT FILE CANNOT BE OPENED',ROUTCDE=11
         ABEND 0001
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=&RECSZ,RECFM=FB
SVCNAMTB DS    0F
         DC    CL8'EXCP    '           SVC 0
         DC    CL8'WAIT    '           SVC 1
         DC    CL8'POST    '           SVC 2
         DC    CL8'EXIT    '           SVC 3
         DC    CL8'GETMAIN '           SVC 4
         DC    CL8'FREEMAIN'           SVC 5
         DC    CL8'LINK    '           SVC 6
         DC    CL8'XCTL    '           SVC 7
         DC    CL8'LOAD    '           SVC 8
         DC    CL8'DELETE  '           SVC 9
         DC    CL8'G/F-MAIN'           SVC 10
         DC    CL8'TIME    '           SVC 11
         DC    CL8'SYNCH   '           SVC 12
         DC    CL8'ABEND   '           SVC 13
         DC    CL8'SPIE    '           SVC 14
         DC    CL8'ERREXCP '           SVC 15
         DC    CL8'PURGE   '           SVC 16
         DC    CL8'RESTORE '           SVC 17
         DC    CL8'BLDLFIND'           SVC 18
         DC    CL8'OPEN    '           SVC 19
         DC    CL8'CLOSE   '           SVC 20
         DC    CL8'STOW    '           SVC 21
         DC    CL8'OPEN-J  '           SVC 22
         DC    CL8'CLOSE-T '           SVC 23
         DC    CL8'DEVTYPE '           SVC 24
         DC    CL8'TRKBAL  '           SVC 25
         DC    CL8'CATALOG '           SVC 26
         DC    CL8'OBTAIN  '           SVC 27
         DC    CL8'RESERVED'           SVC 28
         DC    CL8'SCRATCH '           SVC 29
         DC    CL8'RENAME  '           SVC 30
         DC    CL8'FEOV    '           SVC 31
         DC    CL8'ALLOC   '           SVC 32
         DC    CL8'IOHALT  '           SVC 33
         DC    CL8'MGCRQEDT'           SVC 34
         DC    CL8'WTO(R)  '           SVC 35
         DC    CL8'WTL     '           SVC 36
         DC    CL8'SEGLD/WT'           SVC 37
         DC    CL8'RESERVED'           SVC 38
         DC    CL8'LABEL   '           SVC 39
         DC    CL8'EXTRACT '           SVC 40
         DC    CL8'IDENTIFY'           SVC 41
         DC    CL8'ATTACH  '           SVC 42
         DC    CL8'CIRB    '           SVC 43
         DC    CL8'CHAP    '           SVC 44
         DC    CL8'OVLYBRCH'           SVC 45
         DC    CL8'TTIMER  '           SVC 46
         DC    CL8'STIMER  '           SVC 47
         DC    CL8'DEQ     '           SVC 48
         DC    CL8'RESERVED'           SVC 49
         DC    CL8'RESERVED'           SVC 50
         DC    CL8'SNAP    '           SVC 51
         DC    CL8'RESTART '           SVC 52
         DC    CL8'RELEX   '           SVC 53
         DC    CL8'DISABLE '           SVC 54
         DC    CL8'EOV     '           SVC 55
         DC    CL8'ENQ/RESV'           SVC 56
         DC    CL8'FREEDBUF'           SVC 57
         DC    CL8'RELBUF  '           SVC 58
         DC    CL8'OLTEP   '           SVC 59
         DC    CL8'(E)STAE '           SVC 60
         DC    CL8'IKJEGS6A'           SVC 61
         DC    CL8'DETACH  '           SVC 62
         DC    CL8'CHKPT   '           SVC 63
         DC    CL8'RDJFCB  '           SVC 64
         DC    CL8'RESERVED'           SVC 65
         DC    CL8'BTAMTEST'           SVC 66
         DC    CL8'RESERVED'           SVC 67
         DC    CL8'SYNADAF '           SVC 68
         DC    CL8'BSP     '           SVC 69
         DC    CL8'GSERV   '           SVC 70
         DC    CL8'BFR SVCS'           SVC 71
         DC    CL8'NO MACRO'           SVC 72
         DC    CL8'SPAR    '           SVC 73
         DC    CL8'DAR     '           SVC 74
         DC    CL8'DQUEUE  '           SVC 75
         DC    CL8'IFBSTAT '           SVC 76
         DC    CL8'RESERVED'           SVC 77
         DC    CL8'LSPACE  '           SVC 78
         DC    CL8'STATUS  '           SVC 79
         DC    CL8'RESERVED'           SVC 80
         DC    CL8'SETPRT  '           SVC 81
         DC    CL8'RESERVED'           SVC 82
         DC    CL8'SMFWTM  '           SVC 83
         DC    CL8'GRAPHICS'           SVC 84
         DC    CL8'DDRSWAP '           SVC 85
         DC    CL8'ATLAS   '           SVC 86
         DC    CL8'DOM     '           SVC 87
         DC    CL8'RESERVED'           SVC 88
         DC    CL8'RESERVED'           SVC 89
         DC    CL8'RESERVED'           SVC 90
         DC    CL8'VOLSTAT '           SVC 91
         DC    CL8'TCPEXCP '           SVC 92
         DC    CL8'TGET/PUT'           SVC 93
         DC    CL8'STCC    '           SVC 94
         DC    CL8'SYSEVENT'           SVC 95
         DC    CL8'STAX    '           SVC 96
         DC    CL8'IKJEGS9G'           SVC 97
         DC    CL8'PROTECT '           SVC 98
         DC    CL8'DYNALLOC'           SVC 99
         DC    CL8'IKJEFFIB'           SVC 100
         DC    CL8'QTIP    '           SVC 101
         DC    CL8'AQCTL   '           SVC 102
         DC    CL8'XLATE   '           SVC 103
         DC    CL8'TOPCTL  '           SVC 104
         DC    CL8'IMGLIB  '           SVC 105
         DC    CL8'RESERVED'           SVC 106
         DC    CL8'MODESET '           SVC 107
         DC    CL8'RESERVED'           SVC 108
         DC    CL8'ESR3    '           SVC 109
         DC    CL8'RESERVED'           SVC 110
         DC    CL8'JES-HAM '           SVC 111
         DC    CL8'PGRLSE  '           SVC 112
         DC    CL8'PG SVCS '           SVC 113
         DC    CL8'EXCPVR  '           SVC 114
         DC    CL8'RESERVED'           SVC 115
         DC    CL8'ESR1    '           SVC 116
         DC    CL8'DEBCHK  '           SVC 117
         DC    CL8'RESERVED'           SVC 118
         DC    CL8'TESTAUTH'           SVC 119
         DC    CL8'G/F-MAIN'           SVC 120
         DC    CL8'VSAM    '           SVC 121
         DC    CL8'ESR2    '           SVC 122
         DC    CL8'PURGEDQ '           SVC 123
         DC    CL8'TPIO    '           SVC 124
         DC    CL8'EVENTS  '           SVC 125
         DC    CL8'MSS     '           SVC 126
         DC    CL8'RESERVED'           SVC 127
         DC    CL8'RESERVED'           SVC 128
         DC    CL8'RESERVED'           SVC 129
         DC    CL8'RACHECK '           SVC 130
         DC    CL8'RACINIT '           SVC 131
         DC    CL8'RACLIST '           SVC 132
         DC    CL8'RACDEF  '           SVC 133
         DC    CL8'RESERVED'           SVC 134
         DC    CL8'RESERVED'           SVC 135
         DC    CL8'RESERVED'           SVC 136
         DC    CL8'ESR6    '           SVC 137
         DC    CL8'PGSER   '           SVC 138
         DC    CL8'CVAF    '           SVC 139
         DC    CL8'RESERVED'           SVC 140
         DC    CL8'RESERVED'           SVC 141
         DC    CL8'RESERVED'           SVC 142
         DC    CL8'RESERVED'           SVC 143
         DC    CL8'RESERVED'           SVC 144
         DC    CL8'RESERVED'           SVC 145
         DC    CL8'RESERVED'           SVC 146
         DC    CL8'RESERVED'           SVC 147
         DC    CL8'RESERVED'           SVC 148
         DC    CL8'RESERVED'           SVC 149
         DC    CL8'RESERVED'           SVC 150
         DC    CL8'RESERVED'           SVC 151
         DC    CL8'RESERVED'           SVC 152
         DC    CL8'RESERVED'           SVC 153
         DC    CL8'RESERVED'           SVC 154
         DC    CL8'RESERVED'           SVC 155
         DC    CL8'RESERVED'           SVC 156
         DC    CL8'RESERVED'           SVC 157
         DC    CL8'RESERVED'           SVC 158
         DC    CL8'RESERVED'           SVC 159
         DC    CL8'RESERVED'           SVC 160
         DC    CL8'RESERVED'           SVC 161
         DC    CL8'RESERVED'           SVC 162
         DC    CL8'RESERVED'           SVC 163
         DC    CL8'RESERVED'           SVC 164
         DC    CL8'RESERVED'           SVC 165
         DC    CL8'RESERVED'           SVC 166
         DC    CL8'RESERVED'           SVC 167
         DC    CL8'RESERVED'           SVC 168
         DC    CL8'RESERVED'           SVC 169
         DC    CL8'RESERVED'           SVC 170
         DC    CL8'RESERVED'           SVC 171
         DC    CL8'RESERVED'           SVC 172
         DC    CL8'RESERVED'           SVC 173
         DC    CL8'RESERVED'           SVC 174
         DC    CL8'RESERVED'           SVC 175
         DC    CL8'RESERVED'           SVC 176
         DC    CL8'RESERVED'           SVC 177
         DC    CL8'RESERVED'           SVC 178
         DC    CL8'RESERVED'           SVC 179
         DC    CL8'RESERVED'           SVC 180
         DC    CL8'RESERVED'           SVC 181
         DC    CL8'RESERVED'           SVC 182
         DC    CL8'RESERVED'           SVC 183
         DC    CL8'RESERVED'           SVC 184
         DC    CL8'RESERVED'           SVC 185
         DC    CL8'RESERVED'           SVC 186
         DC    CL8'RESERVED'           SVC 187
         DC    CL8'RESERVED'           SVC 188
         DC    CL8'RESERVED'           SVC 189
         DC    CL8'RESERVED'           SVC 190
         DC    CL8'RESERVED'           SVC 191
         DC    CL8'RESERVED'           SVC 192
         DC    CL8'RESERVED'           SVC 193
         DC    CL8'RESERVED'           SVC 194
         DC    CL8'RESERVED'           SVC 195
         DC    CL8'RESERVED'           SVC 196
         DC    CL8'RESERVED'           SVC 197
         DC    CL8'RESERVED'           SVC 198
         DC    CL8'RESERVED'           SVC 199
         DC    CL8'RESERVED'           SVC 200
         DC    CL8'RESERVED'           SVC 201
         DC    CL8'RESERVED'           SVC 202
         DC    CL8'RESERVED'           SVC 203
         DC    CL8'RESERVED'           SVC 204
         DC    CL8'RESERVED'           SVC 205
         DC    CL8'SPFCOPY '           SVC 206 USER
         DC    CL8'RESERVED'           SVC 207
         DC    CL8'RESERVED'           SVC 208
         DC    CL8'RESERVED'           SVC 209
         DC    CL8'RESERVED'           SVC 210
         DC    CL8'RESERVED'           SVC 211
         DC    CL8'RESERVED'           SVC 212
         DC    CL8'RESERVED'           SVC 213
         DC    CL8'RESERVED'           SVC 214
         DC    CL8'CICS    '           SVC 215
         DC    CL8'CICS    '           SVC 216
         DC    CL8'RESERVED'           SVC 217
         DC    CL8'RESERVED'           SVC 218
         DC    CL8'RESERVED'           SVC 219
         DC    CL8'RESERVED'           SVC 220
         DC    CL8'RESERVED'           SVC 221
         DC    CL8'SYNCSORT'           SVC 222 USER
         DC    CL8'SYNCSORT'           SVC 223 USER
         DC    CL8'RESERVED'           SVC 224
         DC    CL8'CICS    '           SVC 225 USER
         DC    CL8'CICS    '           SVC 226 USER
         DC    CL8'CA-11   '           SVC 227 USER
         DC    CL8'CA-11   '           SVC 228 USER
         DC    CL8'RESERVED'           SVC 229
         DC    CL8'RESERVED'           SVC 230
         DC    CL8'RESERVED'           SVC 231
         DC    CL8'RESERVED'           SVC 232
         DC    CL8'RESERVED'           SVC 233
         DC    CL8'RESERVED'           SVC 234
         DC    CL8'CA-1 X  '           SVC 235 USER
         DC    CL8'CA-1 Y  '           SVC 236 USER
         DC    CL8'CA-1 X  '           SVC 237 USER
         DC    CL8'CA-1 Y  '           SVC 238 USER
         DC    CL8'RESERVED'           SVC 239
         DC    CL8'RESERVED'           SVC 240
         DC    CL8'RESERVED'           SVC 241
         DC    CL8'NCCF    '           SVC 242 USER
         DC    CL8'RESERVED'           SVC 243
         DC    CL8'RESERVED'           SVC 244
         DC    CL8'CA-7    '           SVC 245 USER
         DC    CL8'CA-7    '           SVC 246 USER
         DC    CL8'RESERVED'           SVC 247
         DC    CL8'RESERVED'           SVC 248
         DC    CL8'ADABAS  '           SVC 249 USER
         DC    CL8'ADABAS  '           SVC 250 USER
         DC    CL8'RESERVED'           SVC 251
         DC    CL8'RESERVED'           SVC 252
         DC    CL8'RESERVED'           SVC 253
         DC    CL8'RESERVED'           SVC 254
         DC    CL8'F1DSCB  '           SVC 255 USER
TRTAB    EQU   *-240
         DC    C'0123456789ABCDEF'
OTHDR1   DS    0CL&RECSZ               OUTPUT HEADER 1
         DC    C'SVC NUM '
         DC    C'SVC NAME '
         DC    C'STOR '
         DC    C'  ENTRY  '
         DC    C'SVC '
         DC    C'LLA '
         DC    C'MODULE HEADER INFORMATION'
         DC    CL(&RECSZ-(*-OTHDR1))' '
OTHDR2   DS    0CL&RECSZ               OUTPUT HEADER 2
         DC    C'DEC(HX) '
         DC    C'         '
         DC    C'LOC  '
         DC    C'  POINT  '
         DC    C'TYP    '
         DC    C'    '
         DC    CL(&RECSZ-(*-OTHDR2))' '
OTHDR3   DC    (&RECSZ)C'_'              OUTPUT HEADER 3
BLANKS   DC    CL&RECSZ' '             BLANKS FOR CLEARING FIELDS
         DS    0F
CVTNUCE  DC    X'00FFFFFF'             END OF READ-ONLY NUCLEUS
CVTENUCS DC    X'01000000'             START OF READ-ONLY ENUCLEUS
IGCERROR DC    CL8'IGCERROR'           NAME FOR NUCLKUP
NUC      DC    C'NUC '                 STORAGE AREA LABEL
ENUC     DC    C'ENUC'                 STORAGE AREA LABEL
CSA      DC    C'CSA '                 STORAGE AREA LABEL
ECSA     DC    C'ECSA'                 STORAGE AREA LABEL
SQA      DC    C'SQA '                 STORAGE AREA LABEL
ESQA     DC    C'ESQA'                 STORAGE AREA LABEL
FLPA     DC    C'FLPA'                 STORAGE AREA LABEL
EFLPA    DC    C'EFLP'                 STORAGE AREA LABEL
MLPA     DC    C'MLPA'                 STORAGE AREA LABEL
EMLPA    DC    C'EMLP'                 STORAGE AREA LABEL
PLPA     DC    C'PLPA'                 STORAGE AREA LABEL
EPLPA    DC    C'EPLP'                 STORAGE AREA LABEL
UNKNOWN  DC    C'????'                 UNKNOWN AREA
ESR      DC    C'ESRID='               ESR IDENTIFIER
TYPES    DC    C'16??2?3?'             SVC TYPE TABLE
SVCESR   EQU   X'04'                   ESR INDICATOR
WORKAREA DSECT                         WORK AREA
SAVEAREA DC    18F'0'                  SHOULD BE FIRST IN WORKAREA
CSAHIGH  DS    F                       A(CSA END)
MLPAHIGH DS    F                       A(MLPA END)
FLPAHIGH DS    F                       A(FLPA END)
PLPAHIGH DS    F                       A(PLPA END)
SQAHIGH  DS    F                       A(SQA END)
NUCHIGH  DS    F                       A(NUC END)
ENUCHIGH DS    F                       A(ENUC END)
ESQAHIGH DS    F                       A(ESQA END)
EPLPHIGH DS    F                       A(EPLPA END)
EFLPHIGH DS    F                       A(EFLPA END)
EMLPHIGH DS    F                       A(EMLPA END)
ECSAHIGH DS    F                       A(ECSA END)
ADERROR  DS    F                       A(IGCERROR)
SVCADDR  DS    F                       A(SVC)
DBLWORD  DS    D                       CONVERSION AREA
OTREC    DS    0CL&RECSZ               OUTPUT RECORD
OTSVCDEC DC    CL3'   '                DECIMAL SVC NUMBER
OTLPAREN DC    C'('
OTSVCHEX DC    CL2'  '                 HEX SVC NUMBER
OTRPAREN DC    C') '
OTSVCNAM DC    CL8'    '               SVC NAME/ESR ID NUMBER
         DC    C' '
OTSVCLOC DC    CL4'    '               SVC LOCATION(CSA/SQA/ECSA/.... )
         DC    C' '
OTEPALOC DC    CL8'        '           SVC EPA/LOCATION
         DC    C' '
OTSVCTYP DC    C' 1'                   SVC TYPE (1/2/3/4/5/6)
         DC    CL2' '
OTSVCLLA DC    CL3'   '                LOCK/ATTRIBUTE BITS
         DC    C' '
OTSVCHDR DC    CL(&RECSZ-(*-OTREC))' ' MODULE HEADER INFO
HDRLEN   EQU   *-OTSVCHDR              LENGTH(MODULE HEADER INFO)
WORKLEN  EQU   *-WORKAREA              WORK AREA LENGTH
         CVT   LIST=NO,DSECT=YES,PREFIX=NO
         IHAGDA
         IHASCVT LIST=NO,DSECT=YES
         END



