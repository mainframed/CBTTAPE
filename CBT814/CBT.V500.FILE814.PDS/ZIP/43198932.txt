Issuing operator commands from TSO

The TSO command OPER allows OPER-authorised users (indicated in UADS) to issue
some operator commands from a TSO screen.  It could be useful to give all TSO
users the ability to use the DISPLAY subcommand of OPER.  Program OPER@, in
conjunction with a small CLIST, is a way to do so.

OPER@ is called twice.  On the  first call, indicated by a character in a
certain column after the OPER@ command, OPER authorisation is set temporarily.
On the second call, a modified OPER load module is linked, which allows only the
DISPLAY subcommand.  OPER authorisation is reset.

The setting of background mode at the first call of OPER@ prevents a call of
module OPER instead of OPER@ at the second call.

CLIST DU is an example to show how OPER@ can be used to give all TSO users
access to the DISPLAY U operator command.


CREATING PROGRAM OPER@

//* CREATE PROGRAM OPER@ (FROM TSO COMMAND OPER).
//* IT MUST BE AUTHORISED, THEREFORE INSERT 'OPER@' INTO IKJEFTE2
//* STEP 1: MAKE A COPY OF 'OPER'
//COPYl EXEC LKED,PARM='XREF,LIST,NCAL,REUS'
//LKED.SYSPRINT DD SYSOUT=*
//LKED.SYSLMOD DD DISP=SHR,DSN=SYS1.AUTHLIB
//LKED.LIB     DD DISP=SHR,DSN=SYS1.CMDLIB
 INCLUDE LIB(OPER)
 NAME OPER@(R)
/*
//* STEP 2: ZAP ALL SUBCOMMANDS, BUT NOT 'DISPLAY'
//*         (CHECK OFFSETS BEFORE)
//ZAP2 EXEC PGM=IMASPZAP,REGION=32K,COND=(0,LT)
//SYSPRINT DD SYSOUT=*
//SYSLIB DD DISP=SHR,DSN=SYS1.AUTHLIB
 NAME OPER@ IKJEE100
 VER 123F E2E3D6D7D4D5     STOPMN
 REP 123F FFFFFFFFFFFF
 VER 1246 D7D4             PM
 REP 1246 FFFF
 VER 1249 C4C9E2D7D3C1E8   DISPLAY  <---- ALLOWED ONLY
 VER 1251 C4               D
 REP 1251 FF
 VER 1253 D4D6D5C9E3D6D9   MONITOR
 REP 1253 FFFFFFFFFFFFFF
 VER 125B D4D5             MN
 REP 125B FFFF
 VER 125E C3ClD5C3C5D3     CANCEL
 REP 125E FFFFFFFFFFFF
 VER 1265 C3               C
 REP 1265 FF
 VER 1267 E2C5D5C4         SEND
 REP 1267 FFFFFFFF
 VER 126C E2C5             SE
 REP 126C FFFF
 VER 126F E2D3C9D7         SLIP
 REP 126F FFFFFFFF
 VER 1274 E2D3             SL
 REP 1274 FFFF
/*
//* STEP 3: ASSEMBLE AND LINK 'OPER@'
//CL3 EXEC ASMFCL,COND.ASM=(0,LT),COND.LKED=(0,LT),
// PARM.LKED='XREF,LIST,NCAL,REUS,AC=1'
//ASM.SYSPRINT DD SYSOUT=*
***********************************************************************
* FIRST CALL: SET THE OPER AUTHORISATION BIT IN PSCB.
* SECOND CALL: CALL MODIFIED OPER COMMAND (WHERE ONLY DISPLAY
* SUBCOMMAND IS ALLOWED), RESET OPER AUTHORISATION BIT.
***********************************************************************
OPER@    CSECT                               SERIALLY REUSABLE
         SAVE  (14,4),,                SAVE CALLER'S REGISTERS         X
               OPER@_&SYSDATE          IDENTIFY MODULE
         LR    R4,R15                  LOAD BASE REGISTER
         USING OPER@,R4                TELL IT ASSEMBLER
         ST    R13,SAVEAREA+4          CHAIN
         LR    R2,R13                    SAVEAREAS
         LA    R13,SAVEAREA
         ST    R13,8(R2)
         USING CPPL,R1
         L     R2,CPPLPSCB             ADDRESS OF PSCB IN CPPL
         USING PSCB,R2
         L     R3,CPPLECT              ADDRESS OF ECT IN CPPL
         USING ECT,R3
         L     R15,CPPLCBUF            ADDRESS OF COMMAND BUFFER
         DROP  R1
         CLC   0(CMDLEN,R15),CMD       MARK OF FIRST CALL:
         BNE   LINK                    OPER@                         W
         CLI   66(R15),C'W'            |                             |
         BNE   LINK                    1                            63
*                                            IT'S FIRST CALL
         TM    PSCBATR1,PSCBCTRL       ALREADY OPER AUTHORISED ?
         BO    PROGEND                   YES
         MODESET KEY=ZERO,MODE=SUP      INTO SUPERVISOR STATE
         OI    PSCBATR1,PSCBCTRL        SET OPER AUTHORISATION
         OI    PSCBATR2,PSCBCTRL        MARK OPER AUTHORISED TEMPORARILY
         MODESET KEY=NZERO,MODE=PROB    INTO PROBLEM STATE
         OI    ECTSWS,ECTBKGRD          SET BACKGROUND MODE -->
*                                       PREVENTS CALL OF OPER INSTEAD
*                                       OF OPER@
         B     PROGEND
*                                            IT'S SECOND CALL
LINK     NI    ECTSWS,255-ECTBKGRD     RESET BACKGROUND MODE
         L     R15,=V(IKJEE100)        ENTRY OF OPER
         BALR  R14,R15                 LINK TO MODIFIED OPER COMMAND
         TM    PSCBATR2,PSCBCTRL       ORIGINALLY OPER AUTHORISED ?
         BZ    PROGEND                   YES, DON'T RESET
         MODESET KEY=ZERO,MODE=SUP      INTO SUPERVISOR STATE
         NI    PSCBATR1,255-PSCBCTRL    RESET OPER AUTHORISATION
         NI    PSCBATR2,255-PSCBCTRL    NO MORE OPER AUTHORISED TEMP.
         MODESET KEY=NZERO,MODE=PROB    INTO PROBLEM STATE
         DROP  R2
PROGEND  SLR   R15,R15
         L     R13,4(R13)
         RETURN (14,4),RC=(15)         GO BACK
SAVEAREA DS    18F
CMD      DC    H'67',H'62',C'OPER@'    BEGIN OF COMMAND BUFFER IF
CMDLEN   EQU   *-CMD                     THERE'S A CHAR IN COLUMN 63
         LTORG
         IKJCPPL
         IKJPSCB
         IKJECT
         END   OPER@
//LKED.SYSLMOD DD DISP=SHR,DSN=SYS1.AUTHLIB
//LKED.SYSPRINT DD SYSOUT=*
 INCLUDE SYSLMOD(OPER@)
 NAME OPER@(R)


THE CLIST

PROC 0 DASD TAPE ONLINE OFFLINE
CONTROL NOMSG NOPROMPT
ERROR RETURN
IF &DASD NE DASD AND &TAPE NE TAPE +
 THEN DO
       WRITE MISSING ARGUMENT: DASD OR TAPE
       EXIT
      END
IF &DASD = DASD AND &TAPE = TAPE +
 THEN DO
       WRITE AMBIGUOUS ARGUMENTS: DASD AND TAPE
       EXIT
      END
IF &ONLINE = ONLINE AND &OFFLINE = OFFLINE +
 THEN DO
       WRITE AMBIGUOUS ARGUMENTS: ONLINE AND OFFLINE
       EXIT
      END
IF &DASD = DASD +
 THEN SET &PARMl = DASD
 ELSE SET &PARMl = TAPE
IF &ONLINE = ONLINE +
 THEN SET &PARM2 = &STR(,ONLINE)
 ELSE IF &OFFLINE = OFFLINE +
       THEN SET &PARM2 = &STR(,OFFLINE)
       ELSE SET &PARM2 =
CLEAR
ERROR OFF
OPER@                                                                  W
OPER@
DISPLAY U,&PARMl&PARM2
END
WRITE ***
READ


Walter Wiedemann
Consultant (West Germany)     © Walter Wiedemann 1987





























