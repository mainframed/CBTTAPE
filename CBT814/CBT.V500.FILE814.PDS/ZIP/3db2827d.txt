Retrieving DFSMS dataset classes

Many environments have TSO CP commands available to
allow for quick display of dataset level information. The
display would include such attributes as block size, record
length, organization, record format, etc. After installing
DFSMS, I also wanted the ability to display class constructs
associated with a given dataset using a TSO command. While
researching how to accomplish this, I found that the class
information can be retrieved via a new Callable Services
program called IGWASMS. This ability was introduced to DFP
3.2 via PTF UY90866 and DFP 3.3 via UY90867. After
applying the PTF, SYS1.CSSLIB will contain the required
module. The following program, called SMSINFO, is a sample
TSO CP implementation of this function. The code can be used
stand-alone or integrated into other existing batch or TSO
programs where it is desirable to retrieve DFSMS class
information. The code will also indicate a PDSE dataset if
applicable. Typical executions of SMSINFO from TSO would
look like this:

===> SMSINFO SYS2.DFSMS.VSAM.DATASET
SYS2.DFSMS.VSAM.DATASET
STORCLAS: FASTWRIT  MGMTCLAS: NOMIGRAT  DATACLAS: VSAMKSDS

===> SMSINFO SYS2.DFSMS.PDSE.DATASET
SYS2.DFSMS.PDSE.DATASET
STORCLAS: STANDARD  MGMTCLAS: STANDARD  DATACLAS: SOURCE    PDSE


SMSINFO SOURCE CODE

*   THIS PROGRAM IS A TSO COMMAND PROCESSOR USED TO
*   RETRIEVE CLASS CONSTRUCTS OF A DFSMS DATASET.
SMSINFO  CSECT
         STM   14,12,12(13)     SAVE REGS
         BALR  12,0             ESTABLISH BASE REG
         USING *,12
         ST    13,SAVEAREA+4    PRIOR SAVEAREA ADDRESS
         LA    13,SAVEAREA      CURRENT SAVEAREA ADDRESS
         L     3,0(,1)          CPPL ADDRESS
         LH    4,0(,3)          COMMAND BUFFER LENGTH
         LH    5,2(,3)          DISPLACEMENT TO PARM VALUE
         LA    5,4(5)           ADD HEADER LENGTH
         SR    4,5              ADJUST PARM LENGTH
         LTR   4,4              WAS THERE A PARM?
         BZ    DISPMSG4         IF NOT, DON'T PROCESS
         LA    3,0(5,3)         POINT TO TRUE PARAMETER
         BCTR  4,0              SUBTRACT FOR DSNAME MOVE
         EX    4,MOVEDSN        EXECUTE DSNAME MOVE
         OC    DSN,=44X'40'     FORCE UPPERCASE
         TPUT  DSN,LDSN         DISPLAY DATASET NAME BEING USED
*  FOLLOWING IS THE CALL INTERFACE TO SMS CALLABLE SERVICES
         CALL  IGWASMS,(RC,RS,PROB,DSNLEN,DSN,SMSDATA,DSTYPE)
         CLC   RC,=F'0'         RETURN CODE 0?
         BE    CHKRC0           YES, GO CHECK REASON
         CLC   RC,=F'8'         RETURN CODE 8?
         BE    CHKRC8           YES, GO CHECK REASON
         CLC   RC,=F'12'        RETURN CODE 12?
         BE    CHKRC12          YES, GO CHECK REASON
DISPMSG4 TPUT  MSG4,LMSG4       DISPLAY UNEXPECTED CODES MSG
         B     RETURN
CHKRC0   CLC   RS,=F'0'         REASON CODE 0?
         BNE   DISPMSG4         NO, UNEXPECTED
         MVC   MSG3STOR(8),SMSDATA     MOVE STOR CLASS
         CLC   MSG3STOR(8),BLANKS      IS STORCLAS BLANKS?
         BE    DISPMSG2                YES, NOT MANAGED
         MVC   MSG3MGMT(8),SMSDATA+30  MOVE MGMT CLASS
         MVC   MSG3DATA(8),SMSDATA+60  MOVE DATA CLASS
         MVC   MSG3PDSE(4),BLANKS      ASSUME NOT PDSE
         CLC   DSTYPE,=F'1'            IS THIS A PDSE?
         BNE   DISPMSG3                NO, SKIP MOVE
         MVC   MSG3PDSE(4),=C'PDSE'    INDICATE PDSE
DISPMSG3 TPUT  MSG3,LMSG3       DISPLAY CLASS CONSTRUCTS
         B     RETURN
CHKRC8   CLC   RS,=F'32'        REASON CODE 32?
         BNE   DISPMSG4         NO, UNEXPECTED
DISPMSG2 TPUT  MSG2,LMSG2       DISPLAY DSN NOT DFSMS MSG
         B     RETURN
CHKRC12  CLC   RS,=F'12'        REASON CODE 12?
         BNE   DISPMSG4         NO, UNEXPECTED
         TPUT  MSG1,LMSG1       DISPLAY DSN NOT FOUND MSG
RETURN   L     13,4(,13)        ADDRESS PRIOR SAVEAREA
         LM    14,12,12(13)     RESTORE REGS
         LA    15,0             SET RC
         BR    14               RETURN CONTROL
MOVEDSN  MVC   DSN(0),0(3)      SAVE DATASET NAME
SAVEAREA DC    18F'0'
RC       DC    F'0'             IGWASMS RETURN CODE
RS       DC    F'0'             IGWASMS REASON CODE
PROB     DC    2F'0'            IGWASMS PROBLEM CODE
DSNLEN   DC    F'44'            LENGTH OF DATASET NAME
DSN      DC    CL44' '          DATASET NAME
LDSN     EQU   *-DSN            LENGTH FOR TPUT
SMSDATA  DC    3CL30' '         SMSDATA RETURNED BY IGWASMS
DSTYPE   DC    F'0'             PDSE INDICATOR
BLANKS   DC    CL8' '
MSG1     DC    C'DATASET NOT FOUND'
LMSG1    EQU   *-MSG1           SIZE OF MSG1
MSG2     DC    C'DATASET NOT DFSMS MANAGED'
LMSG2    EQU   *-MSG2           SIZE OF MSG2
MSG3     DS    0F
         DC    C'STORCLAS: '
MSG3STOR DC    CL8' '
         DC    C' MGMTCLAS: '
MSG3MGMT DC    CL8' '
         DC    C' DATACLAS: '
MSG3DATA DC    CL8' '
         DC    CL2' '
MSG3PDSE DC    CL4' '
LMSG3    EQU   *-MSG3           SIZE OF MSG3
MSG4     DC    C' UNABLE TO PROCESS REQUEST'
LMSG4    EQU   *-MSG4           SIZE OF MSG4
         END


Ernie Ishman
Senior Systems Programmer
Geisinger System Services (USA)                                          c Xephon 1992

