The JES2 Job Information Service 


Source code

*-------------------------------------------------------------------*   
* PROGRAM-ID.    JES2CKPV                                           *   
* FUNCTION.      TO OBTAIN A JES2 CHECKPOINT VERSION IMAGE.         *   
*                THIS ROUTINE USES THE JES2 JOB INFORMATION SERVICE *   
*                INTERFACE WHICH PROVIDES A FORMALIZED INTERFACE TO *   
*                ACQUIRE DATA FROM A COPY OF THE CHECKPOINT DATA.   *   
*                THIS IS ACCOMPLISHED THROUGH THE SSI USING FUNCTION*   
*                CODE 71(SSOBSSJI).                                 *   
*                TWO FUNCTIONS ARE PROVIDED BY THIS INTERFACE:      *   
*                1. OBTAIN A CHECKPOINT VERSION                     *   
*                2. RELEASE A CHECKPOINT VERSION                    *   
*                TO ACCESS THE CHECKPOINT VERSION INFORMATION THE   *   
*                FOLLOWING ADDRESSABILITY IS REQUIRED:              *   
*                AR MODE                                            *   
*-------------------------------------------------------------------*   
         TITLE 'EQUATES'                                                
LOWZERO  EQU   X'00'                   BINARY ZERO                      
WSUBPOOL EQU   0                       WORK AREA SUBPOOL                
ZERO     EQU   X'00'                   ZERO                             
SPACE    EQU   C' '                    SPACE                            
SIGNF    EQU   X'F0'                   POSITIVE SIGN                    
         TITLE 'JES2 JOB INFORMATION SERVICES'                          
JES2CKPV CSECT                                                          
JES2CKPV AMODE ANY                                                      
JES2CKPV RMODE ANY                                                      
         BAKR  R14,0                   SAVE CALLER'S ARS + GPRS          
*                                      IN THE LINKAGE STACK             
         USING JES2CKPV,R12            INFORM THE ASSEMBLER             
         LAE   R12,0(R15,0)            SET UP PROGRAM BASE REGISTER      
GETWORK  EQU   *                                                        
         L     R9,=AL4(WORKALEN)       WORK AREA LENGTH                 
         STORAGE OBTAIN,               OBTAIN STORAGE                  X
               LENGTH=(R9),            NO OF BYTES TO OBTAIN           X
               ADDR=(R10),             PUT THE @ IN HERE               X
               SP=0,                   FOR SUBPOOL 0                   X
               KEY=8,                  IN KEY 8                        X
               LOC=BELOW,              ABOVE THE LINE                  X
               COND=NO,                THIS CONDITION                  X
               RELATED=(FREEWORK,'FREE WORK AREA')                      
         LAE   R13,0(R10,0)            @ OF WORKAREA                    
         USING WORKAREA,R13            INFORM THE ASSEMBLER             
         LA    R0,WORKAREA             @ THE WORKAREA                   
         LA    R1,WORKALEN(0,0)        LENGTH                           
         XR    R14,R14                 ZERO FILL                         
         XR    R15,R15                 PROPAGATE                        
         MVCL  R0,R14                  CLEAR THE AREA                   
         MVC   PREVSA,=C'F1SA'         PUT ACRONYM INTO SAVEAREA        
*                                      TO INDICATE STATUS SAVED ON      
*                                      THE LINKAGE STACK               
         BAS   R2,SSISTOR              GET THE SSI STORAGE              
         BAS   R2,SSICALLO             OBTAIN A CHECKPOINT VERSION      
         LTR   R10,R10                 DID WE GET A CHECKPOINT VERSION? 
         BNZ   CHKSSIRC                NO-                              
         BAS   R2,JES2HCT              GET THE JES2 HCT CONTROL BLOCK   
         BAS   R2,SSICALLR             RELEASE A CHECKPOINT VERSION     
         LTR   R10,R10                 DID WE RELEASE THE CHKP VERSION? 
         BZ    FSSISTOR                YES-                             
CHKSSIRC EQU   *                                                        
*..................................................................     
* CODE FOR CHECKING THE SSI RETURN CODES SHOULD BE PLACED HERE          
*..................................................................     
FSSISTOR EQU   *                                                        
         BAS   R2,FREESSI              FREE THE SSI STORAGE             
RETURN   EQU   *                                                        
         LAE   R1,0(R13,0)              ADDRESS TO FREE                 
         L     R9,=AL4(WORKALEN)        WORK AREA LENGTH                
         STORAGE RELEASE,               RELEASE THIS STORAGE           X
               ADDR=(R1),               AREA TO FREE                   X
               LENGTH=(R9),             LENGTH TO FREE                 X
               SP=0,                    IN THIS SUBPOOL                X
               KEY=8,                   IN THIS KEY                    X
               COND=NO,                 FOR THIS CONDITION             X
               RELATED=(GETWORK,'OBTAIN WORK AREA')                     
         XR    R15,R15                  SET RC                          
         PR                             RESTORE CALLER'S ARS            
         TITLE 'GET THE SSI STORAGE FOR SSOB,SSIB,SSJI,DSERV'           
SSISTOR  EQU   *                                                        
         LA    R9,SSOBHSIZ+SSJISIZE+SSIBSIZE+DSRVSZE+4(0,0)             
         STORAGE OBTAIN,                OBTAIN STORAGE                 X
               LENGTH=(R9),             NO OF BYTES TO OBTAIN          X
               ADDR=(R10),              PUT THE @ IN HERE              X
               SP=0,                    FOR SUBPOOL 229                X
               KEY=8,                   IN KEY 8                       X
               LOC=BELOW,               ABOVE THE LINE                 X
               COND=NO,                 THIS CONDITION                 X
               RELATED=(FREESSI,'FREE WORK AREA')                       
         LAE   R4,0(R10,0)              @ THE SSI AREA                  
         LR    R0,R4                    SWITCH                          
         LR    R1,R9                    LENGTH                          
         XR    R14,R14                  ZEROFILL                        
         XR    R15,R15                  PROPAGATE                       
         MVCL  R0,R14                   CLEAR THE AREA                  
         STCM  R4,B'1111',SSISTOR@      STORE FOR LATER                 
         BR    R2                       RETURN TO CALLER                
         TITLE 'OBTAIN A CHECKPOINT VERSION'                            
SSICALLO EQU   *                                                        
         LA    R5,4(,R4)                START OF SSOB                   
         O     R5,=X'80000000'          LAST IN LIST                    
         STCM  R5,B'1111',0(R4)         @ OF SSOB                       
         LA    R4,4(,R4)                START OF SSOB                   
*******************************************************************     
*                                                                 *     
* R1 --------->  +0 -----------------                             *     
*                   |               |                             *     
*                   |@ SSOB         |                             *     
*                   |               |                             *     
* R4 --------->  +4 |---------------|                             *     
*                   |  SSOB AREA    |                             *     
*                   |               |                             *     
*                   |               |                             *     
* R6 --------->  +N |---------------|                             *     
*                   |  SSJI AREA    |                             *     
*                   |               |                             *     
*                   |---------------|                             *     
* R5 --------->  +N |               |                             *     
*                   |  SSIB AREA    |                             *     
*                   |               |                             *     
*                   |---------------|                             *     
* R7 --------->  +N |               |                             *     
*                   |  DSERV AREA   |                             *     
*                   |               |                             *     
*                   -----------------                             *     
*******************************************************************     
         USING SSOB,R4                  INFORM THE ASSEMBLER            
INITSSOB EQU   *                                                        
         MVC   SSOBID,=C'SSOB'          EYECATCHER FOR THE SSOB        
         LA    R5,SSOBHSIZ(0,0)         LENGTH OF THE SSOB HEADER       
         STCM  R5,B'0011',SSOBLEN       PUT INTO THE SSOB               
         LA    R5,SSOBSSJI(0,0)         SSI FUNCTION NUMBER             
         STCM  R5,B'0011',SSOBFUNC      AND PUT INTO THE SSOB           
         LA    R5,SSOBHSIZ(,R4)         @ THE SSJI                      
         STCM  R5,B'1111',SSOBINDV      @ OF SSJI IN SSOB               
         LA    R5,SSJISIZE(,R5)         @ OF THE SSIB                   
         STCM  R5,B'1111',SSOBSSIB      @ OF SSIB IN SSOB               
INITSSIB EQU   *                                                        
         USING SSIB,R5                  INFORM THE ASSEMBLER            
         MVC   SSIBID,=C'SSIB'          EYECATCHER FOR THE SSIB        
         LA    R10,SSIBSIZE(0,0)        SIZE OF THE SSIB                
         STCM  R10,B'0011',SSIBLEN      PUT INTO THE SSIB               
         MVC   SSIBSSNM,=C'JES2'        SUBSYSTEM TO INVOKE            
INITSSJI EQU   *                                                        
         USING SSJI,R6                  INFORM THE ASSEMBLER            
         L     R6,SSOBINDV              @ OF SSJI                       
         LA    R10,SSJISIZE(0,0)        SIZE OF SSJI                    
         STCM  R10,B'0011',SSJILEN      PUT INTO THE SSJI               
         MVC   SSJIID,=C'SSJI'          EYECATCHER FOR THE SSJI        
         LA    R10,SSJIFOBT(0,0)        FUNCTION REQUEST= OBTAIN        
         STC   R10,SSJIFREQ             SET THE FUNCTION REQUIRED       
         LA    R10,SSJISVR#(0,0)        SERVICE VERSION NUMBER          
         STCM  R10,B'0011',SSJISVRN     PUT INTO THE SSJI               
         L     R7,SSOBSSIB              @ OF SSIB                       
         LA    R7,SSIBSIZE(,R7)         @ OF DSERV                      
         STCM  R7,B'1111',SSJIUSER      USER TOKEN IN SSJI              
INITDSRV EQU   *                                                        
         USING DSERV,R7                 INFORM THE ASSEMBLER            
         MVC   DSRVSSID,=C'DSRV'        EYECATCHER                     
         LA    R10,DSRVSZE(0,0)         SIZE OF DSERV                   
         STCM  R10,B'0011',DSRVLEN      INTO DSERV AREA                 
         LA    R10,DSRVSVR#(0,0)        CONTROL BLOCK VERSION NUMBER    
         STCM  R10,B'0011',DSRVSVRN     STORE INTO DSERV AREA           
SSIREQ   EQU   *                                                        
         MODESET MODE=SUP,KEY=ZERO      SUPERVISOR KEY 0 STATE          
         ICM   R1,B'1111',SSISTOR@      @ OF THE PARAMETER LIST         
         IEFSSREQ                       ISSUE THE OBTAIN REQUEST        
         LR    R10,R15                  STORE THE RETURN CODE           
         MODESET MODE=PROB,KEY=NZERO    PROBLEM KEY 8 STATE             
         BR    R2                       RETURN TO CALLER                
         TITLE 'RELEASE A CHECKPOINT VERSION'                           
SSICALLR EQU   *                                                        
         LA    R10,SSJIFREL(0,0)        FUNCTION REQUEST= RELEASE       
         STC   R10,SSJIFREQ             SET THE FUNCTION REQUIRED       
         MODESET MODE=SUP,KEY=ZERO      SUPERVISOR KEY 0 STATE          
         ICM   R1,B'1111',SSISTOR@      @ OF THE PARAMETER LIST         
         IEFSSREQ                       ISSUE THE OBTAIN REQUEST        
         LR    R10,R15                  STORE THE RETURN CODE           
         MODESET MODE=PROB,KEY=NZERO    PROBLEM KEY 8 STATE             
         BR    R2                       RETURN TO CALLER                
         TITLE 'ACCESS THE JES2 HCT'                                    
JES2HCT  EQU   *                                                        
         MODESET MODE=SUP,KEY=ZERO      SUPERVISOR KEY 0 STATE          
         LAM   R0,R15,=16AL4(0)         NICE CLEAN ACCESS REGISTERS     
         SAC   512                      LET'S GET INTO AR MODE           
         SYSSTATE ASCENV=AR             TELL WE ARE IN AR MODE          
         LAM   R8,R8,DSRVHCAL           ALET OF HCT INTO AR8            
         ICM   R8,B'1111',DSRVHCPT      @ OF THE HCT IN DATA SPACE      
         USING HCT,R8                   INFORM THE ASSEMBLER            
         LA    R0,JESHCT                MOVE THE                        
         LA    R1,$SAVELEN(0,0)         CHECKPOINTED                    
         LR    R9,R1                    JES2                            
         MVCL  R0,R8                    HCT                             
         SAC   0                        LET'S RETURN TO PRIMARY MODE     
         SYSSTATE ASCENV=P              TELL WE ARE IN PRIMARY MODE     
         MODESET MODE=PROB,KEY=NZERO    PROBLEM KEY 8 STATE             
         BR    R2                       RETURN TO CALLER                
         TITLE 'FREE THE THE SSI STORAGE FOR SSOB,SSIB,SSJI,DSERV'      
FREESSI  EQU   *                                                        
         ICM   R1,B'1111',SSISTOR@      @ OF THE SSI CB AREA            
         LA    R9,SSOBHSIZ+SSJISIZE+SSIBSIZE+DSRVSZE+4(0,0)             
         STORAGE RELEASE,               RELEASE THIS STORAGE           X
               ADDR=(R1),               AREA TO FREE                   X
               LENGTH=(R9),             LENGTH TO FREE                 X
               SP=0,                    IN THIS SUBPOOL                X
               KEY=8,                   IN THIS KEY                    X
               COND=NO,                 FOR THIS CONDITION             X
               RELATED=(SSISTOR,'GET THE SSI CONTROL BLOCKS')           
         BR    R2                       RETURN TO CALLER                
         TITLE 'LTORG'                                                  
         LTORG                                                          
         TITLE 'WORKAREA'                                               
WORKAREA DSECT                                                          
SAVEAREA DS    CL72                     SAVEAREA                          
PREVSA   EQU   SAVEAREA+4,4             PREVIOUS SAVE AREA @              
SSISTOR@ DS    AL4                      SSI STORAGE ADDRESS               
JESHCT   DS    CL($SAVELEN)             CHECKPOINTED JES2 HCT             
WORKALEN EQU   *-WORKAREA               WORKAREA LENGTH                   
         TITLE 'CVT DSECT'                                              
         CVT   DSECT=YES,LIST=NO        CVT                               
         TITLE 'JESCT DSECT'                                            
         IEFJESCT                       JES COMMUNICATION TABLE           
         TITLE 'SSOB MAPPING MACRO'                                     
         IEFSSOBH                                                       
         TITLE 'SSIB MAPPING MACRO'                                     
         IEFJSSIB                       SSIB                              
         TITLE 'SSJI MAPPING MACRO'                                     
SSJI     DSECT                                                          
         IAZSSJI                        SSJI                              
         TITLE 'DSERV MAPPING MACRO'                                    
         IAZDSERV                       DSERV                             
         TITLE 'JES2 EQUATES'                                           
         $HASPEQU                                                       
         TITLE 'JES2 GLOBALS'                                           
         COPY $HASPGBL                                                  
         TITLE 'JES2 HCT'                                               
         $HCT                                                           
         TITLE 'JES2 PCE'                                               
         $PCE                                                           
         TITLE 'JES2 PSV'                                               
         $PSV                                                           
         TITLE 'JES2 MIT'                                               
         $MIT                                                           
         TITLE 'JES2 XECB'                                              
         $XECB                                                          
         END                                                            


Rem Perretta
Senior Systems Programmer (UK)                    c Xephon 1996

