An ISPF-TSO/E RECEIVE interface

In the September 1988 issue of MVS Update, an ISPF-TSO/E
TRANSMIT interface was described in which the author
concluded it was not possible to give the TSO/E RECEIVE
command a more user-friendly interface because of the
interactive nature of the command.  Since we have had REXX
support in TSO, I have found a way around this problem
through the use of the QUEUE statement as can be seen in the
following.


CLIST TSORECV

/*********************** REXX  **************************************/
/* FUNCTION:                                                        */
/*      TSORECV assists the user to invoke the                      */
/*      TSO RECEIVE function thru a panel or as a command.          */
/********************************************************************/
/* TRACE I */
  ADDRESS ISPEXEC "DISPLAY PANEL(XFRRECV)"    /* display panel */
  retcode = rc
  DO WHILE retcode = 00
    usrpref = SYSVAR(syspref)            /* make sure the log is .. */
    "PROFILE PREFIX("SYSVAR(SYSUID)")"   /* written to userid.xxx.LOG */
    IF rods = '' THEN
    DO
      IF rmem = '' THEN
       rdataset = "DSN('"||rpro||"."||rgrp||"."||rtyp||"')"
      ELSE
       rdataset = "DSN('"||rpro||"."||rgrp||"."||rtyp||"("||rmem||")')"
    END
    ELSE rdataset = "DSN('"||rods||"')"
    IF ((ruid <> '') & (ruid <> SYSVAR(SYSUID) )) THEN
      ruser = "USERID("ruid")"
    ELSE ruser = ''
    DO
       x = PROMPT('ON')       /* prompting must be on */
       QUEUE rdataset roth    /* stack parameters for RECEIVE */
       QUEUE 'END'            /* stack last parameter too */
       QUEUE ''               /* will terminate query of RECEIVE */
       'RECEIVE' ruser        /* issue actual command */
    END
    DELSTACK                  /* delete anything that is left */
    IF usrpref <> ''  THEN
       "PROFILE PREFIX("usrpref")"   /* reset profile */
    ELSE
       "PROFILE NOPREFIX"
    ADDRESS ISPEXEC "CONTROL DISPLAY REFRESH"
    ADDRESS ISPEXEC "DISPLAY PANEL(XFRRECV)"    /* re-display panel */
    retcode = rc
  END


PANEL XFRRECV

%-------------------% TSO/E Receive panel %--------------------------------+
%COMMAND ===>_ZCMD
+
+SPECIFY RECEIVING DATASET:
+   PROJECT   %===>_RPRO    +         | PDS or SEQ datasets can be  |
+   GROUP     %===>_RGRP    +         | received only.              |
+   TYPE      %===>_RTYP    +         | If PDS then specify member  |
+   MEMBER    %===>_RMEM    +         | name too.                   |
+
+OTHER PARTITIONED OR SEQUENTIAL DATA SET:
+   OTHER DATASET %===>_RODS                                           +
+   VOLUME SERIAL %===>_RVOL  +
+
+RECEIVE DATA FOR AN OTHER USER:      (requires TSO OPER authorization)
+  USERID  %===>_RUID   +
+
+OTHER OPTIONS (e.g. space requirements):
+   OPTIONS%===>_ROTH
+
+Press %ENTER+ to execute RECEIVE or %END+ to exit.
)PROC
    IF (&RODS = ' ')
       VER (&RPRO,NB,NAME)
       VER (&RGRP,NB,NAME)
       VER (&RTYP,NB,NAME)
    VER (&RODS,DSNAME)
    VER (&RMEM,NAME)
    VER (&RVOL,NAME)
 VPUT (RUID RPRO RGRP RTYP RODS RVOL ROTH) PROFILE
)END


Karel Bastiaansen
Systems Programmer
IFF Nederland BV (The Netherlands)                            c Xephon 1990

