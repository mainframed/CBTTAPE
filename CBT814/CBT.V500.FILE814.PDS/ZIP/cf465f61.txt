Controlling the Address Space Vector Table

Today many environments need utilities and/or products which
use cross memory to communicate in different address spaces.
It is important to remember that, in MVS, every job in the run
state needs an entry in a system table called the Address Space
Vector Table (ASVT). As a rule this ASVT entry is released
when the job ends, although with the cross memory technique
the entry is busy until the next IPL. When the ASVT is full,
executing a job is impossible, and only an IPL can re-initialize
the table's entries.

If performing an IPL is a critical action in your data processing
centre, you need to know the status of the ASVT. Knowing the
percentage utilization can avoid having to stop activities. The
following module, which has been named SCANASVT, reads
the ASVT and issues a message to the console in a format that
is easy to read (see Figure 1).

If the percentage of unusable entries is higher than 70% of the
whole table, the module issues a non-scrollable message to
show critical values, and allows you to schedule an IPL with
reasonable advance warning.

This critical message appears as follows:

      ** ASVT ..% UTILIZATION REACHED. IPL REQUIRED **

The program has been tested under MVS/XA 2.2.3 and
MVS/ESA 3.1.3.


SOURCE OF SCANASVT PROGRAM

SCANASVT CSECT
**** PROGRAM EXAMINES THE ADDRESS SPACE VECTOR TABLE TO VERIFY  *
*            ITS UTILIZATION LEVEL.                             *
SAVERG   EQU   *
         STM   R14,R12,12(R13)             SAVE CALLER'S REGISTERS
         BALR  R8,0                        SET UP CSECT
         USING *,R8                         ADDRESSABILITY
         ST    13,SAVAREA+4                SET BACKWARD PTR
         LR    R2,R13
         LA    R13,SAVAREA                 GET ADDRESS OF SAVEAREA
         ST    R13,8(R2)                   SET FORWARD PTR
TOCVT    EQU   *
         OPEN  (OUTLST,(OUTPUT))
         L     R4,CVTPTR                   ESTABLISH ADDRESSABILITY
         USING CVTMAP,R4                    TO THE CVT
TOASVT   EQU   *
         L     R3,CVTASVT                  ESTABLISH ADDRESSABILITY
         USING ASVT,R3                      TO THE ASVT
LB010    EQU   *
         L     R5,ASVTAAV                  FREE SLOTS ON THE ASVT
         XR    R6,R6                        AVAILABLE QUEUE
         LR    R6,R5
LB020    EQU   *
         L     R5,ASVTAST                  FREE SLOTS ON THE
         AR    R6,R5                        START/SASI QUEUE
LB030    EQU   *
         L     R5,ASVTANR                  FREE SLOTS ON THE
         AR    R6,R5                        NON-REUS. REPL. QUEUE
LB040    EQU   *
         PUT   OUTLST,SEPAR
         MVC   DATOUT(74),BLANK
         MVC   OUT(16),=CL16'* ASVT SLOTS : '
         L     R5,ASVTMAXU                MAXIMUM NUMBER
         CVD   R5,DOPPIA                   OF ADDRESS SPACES
         AP    TOTAL,DOPPIA               TOTAL ASVT SLOTS
         AP    USED,DOPPIA
         UNPK  WASVT2,DOPPIA
         MVZ   WASVT2+3(1),WASVT2
         MVC   OUT+16(4),WASVT2
         MVC   WTO+210(4),WASVT2
LB050    EQU   *
         MVC   OUT+22(12),=CL12'AVAILABLE : '
         CVD   R6,DOPPIA
         SP    USED,DOPPIA                 AVAILABLE ASVT SLOTS
         UNPK  WASVT2,DOPPIA
         MVZ   WASVT2+3(1),WASVT2
         MVC   OUT+34(4),WASVT2
         MVC   WTO+231(4),WASVT2
LB060    EQU   *
         LA    R3,528(R3)                  FIRST ASVT SLOT
         ST    R3,APPO                     STORE ITS ADDRESS
         MVI   APPO,X'80'                  UNUSABLE SLOT IDENTIFIER
         LR    R5,R3
LB070    EQU   *
         MVC   APPO1(4),0(R5)
         CLC   APPO1,=X'80000000'          END OF ASVT AVAIL. QUEUE ?
         BNE   LB090
         AP    SWITCH,UNO
LB080    EQU   *
         CP    SWITCH,UNO                  END OF ASVT ?
         BH    LB110
         B     LB100
LB090    EQU   *
         CLC   APPO,APPO1                  UNUSABLE SLOT ?
         BNE   LB100
         AP    NOTUSE,UNO
LB100    EQU   *
         LA    R5,4(R5)                    NEXT ASVT SLOT
         B     LB070
LB110    EQU   *
         MVC   OUT+40(9),=CL9'IN USE : '
         SP    USED,NOTUSE                 IN USE ASVT SLOTS
         UNPK  WASVT2,USED
         MVZ   WASVT2+3(1),WASVT2
         MVC   OUT+49(4),WASVT2
         MVC   WTO+255(4),WASVT2
LB120    EQU   *
         UNPK  WASVT2,NOTUSE
         MVZ   WASVT2+3(1),WASVT2
         MVC   OUT+55(11),=CL11'UNUSABLE : '
         MVC   OUT+66(4),WASVT2
         MVC   WTO+276(4),WASVT2           UNUSABLE ASVT SLOTS
         MVC   OUT+72,=CL1'*'
         PUT   OUTLST,DATOUT
         PUT   OUTLST,SEPAR
LB130    EQU   *
         CLOSE OUTLST
WTO      WTO  (' ',D),                                                 X
               ('*****************************************',D),        X
               ('*      A  S  V  T    S  L  O  T  S      *',D),        X
               ('*     -----------------------------     *',D),        X
               ('*                                       *',D),        X
               ('*  TOTAL  :  ....   AVAILABLE  :  ....  *',D),        X
               ('*  IN USE :  ....   UNUSABLE   :  ....  *',D),        X
               ('*                                       *',D),        X
               ('*****************************************',DE)
LB140    EQU   *
         ZAP   COMODO,NOTUSE
         MP    COMODO,CENTO                UNUSABLE SLOTS RATE
         DP    COMODO,TOTAL
         ZAP   PERC,COMODO(2)
LB150    EQU   *
         CP    PERC,MAXPERC                RATE > 70 % ?
         BL    RETURN0
LB160    EQU   *                           YES, WARNING MESSAGE
         UNPK  WASVT2,PERC
         MVZ   WASVT2+3(1),WASVT2
         MVC   WT02+16(2),WASVT2+2
WTO2     WTO  ('** ASVT ..% UTILIZATION REACHED. IPL REQUIRED **',DE), X
               DESC=(11)                   NON-SCROLLABLE WTO
RETURN9  EQU   *
         L     R13,SAVAREA+4               RESTORE R13
         LM    R14,R12,12(R13)             RESTORE CALLER'S REGS
         LA    R15,9                       RETURN CODE 9
         BR    R14                         RETURN TO CALLER
RETURN0  EQU   *
         L     R13,SAVAREA+4               RESTORE R13
         LM    R14,R12,12(R13)             RESTORE CALLER'S REGS
         XR    R15,R15                     RETURN CODE 0
         BR    R14                         RETURN TO CALLER
*     W O R K    A R E A     *
SAVAREA  DS    18F
BLANK    DC    CL130' '
SEPAR    DS    0CL74
         DC    CL1' '
         DC    73CL1'*'
DATOUT   DS    CL1' '
AREAPUT  DS    0CL9
OUT      DS    CL8' '
FIL01    DS    CL1' '
WASVT    DS    CL4' '
FIL02    DS    CL60' '
DOPPIA   DS    D
APPO1    DS    F
WASVT2   DS    CL4' '
APPO     DS    F
UNO      DC    PL1'1'
SWITCH   DC    PL1'0'
PERC     DC    PL4'0'
NOTUSE   DC    PL4'0'
USED     DC    PL4'0'
TOTAL    DC    PL4'0'
COMODO   DC    PL6'0'
CENTO    DC    PL3'100'
MAXPERC  DC    PL4'70'
OUTLST   DCB   DDNAME=OUTLST,MACRF=(PM),DSORG=PS,                      *
               RECFM=FB,                                               *
               LRECL=74,                                               *
               BLKSIZE=740
         CVT   DSECT=YES
         IHAASVT
         END   SCANASVT


SAMPLE EXECUTION JCL

//SCANASVT PROC CLASS=Z
//*
//* DOC: EXAMINE ASVT STATUS/
//*
//STEPCNTL EXEC PGM=SCANASVT
//STEPLIB  DD DISP=SHR,DSN=..........
//SYSOUT   DD SYSOUT=&CLASS
//OUTLST   DD SYSOUT=&CLASS
//SYSABEND DD SYSOUT=&CLASS
//


Mauro Palumbo (Italy)                               c Xephon 1992

