Browsing and editing VIO datasets

The following article describes four CLISTs, which we have
found very useful in browsing and editing VIO datasets.


DDBROW

This CLIST can be used to browse VIO datasets.  When you
are using file tailoring with FTOPEN TEMP, the tailored
output gets written to temporary datasets.  The output normally
goes to the DDNAME ISPCTL1 or ISPCTL2, depending on
the logical screen from where the command was issued.  If you
don't have the DDNAMEs ISPCTL1 and ISPCTL2, please
modify it to suit your site requirements.  More than four
concatenated datasets are not supported by ISPF.

/**********************************************************************
/* The  following file is  DDBROW
/* Browse the datasets used in file tailoring output.(ISPCTL1/ISPCTL2)
/* Use TSO DDBROW. No parameters required.
/**********************************************************************
PROC 0
  ISPEXEC CONTROL ERRORS RETURN
  CONTROL  ASIS
  ISPEXEC VGET (ZSCREEN)
  IF &ZSCREEN = &STR() THEN SET &ZSCREEN = 1
  SET &DDN = &STR(ISPCTL)&ZSCREEN
  ISPEXEC LMINIT DATAID(DDVAR) DDNAME(&DDN)
  SET &RC = &LASTCC
  IF &RC > 0 THEN +
  DO
    ISPEXEC SETMSG MSG(&ZERRMSG)
    EXIT
  END
  ISPEXEC BROWSE DATAID(&DDVAR)
  SET &RC = &LASTCC
  IF &RC > 0 THEN +
    ISPEXEC SETMSG MSG(&ZERRMSG)
  ISPEXEC LMFREE DATAID(&DDVAR)
EXIT


DDB

This CLIST is a generalized version of DDBROW.  You enter
the DDNAME to be browsed as a parameter to the CLIST.  For
example, enter 'TSO DDB SYS0085' to browse a dataset
associated with the DDNAME SYS0085.  Enter 'TSO LISTA
STATUS SYSNAMES' to find out all the datasets with
DDNAMEs allocated to your session.

I would suggest a command table entry like this to use DDB:

      DDB     0    SELECT CMD(%DDB &ZPARM)


DDB source code

/**********************************************************************
/* The  following file is  DDB
/* Browse the dataset given the ddname. Especially useful for VIO
/* datasets. Use TSO DDB ddname.
/**********************************************************************
PROC 1 DDN
  ISPEXEC CONTROL ERRORS RETURN
  CONTROL  ASIS
  ISPEXEC LMINIT DATAID(DDVAR) DDNAME(&DDN)
  SET &RC = &LASTCC
  IF &RC > 0 THEN +
  DO
    ISPEXEC SETMSG MSG(&ZERRMSG)
    EXIT
  END
  ISPEXEC BROWSE DATAID(&DDVAR)
  SET &RC = &LASTCC
  IF &RC > 0 THEN +
    ISPEXEC SETMSG MSG(&ZERRMSG)
  ISPEXEC LMFREE DATAID(&DDVAR)
EXIT


DDEDIT

This CLIST can be used to edit VIO datasets.  When you are
using file tailoring with FTOPEN TEMP, the tailored output
gets written to temporary datasets.  The output normally goes
to the DDNAME ISPCTL1 or ISPCTL2 depending on the
logical screen from where the command was issued.  I often
use this to save the generated JCL for later reference.

I would suggest a command table entry like this to use
DDEDIT:

      DDEDIT  0    SELECT CMD(%DDEDIT)


DDEDIT source code

/**********************************************************************
/* The  following file is  DDEDIT
/* Edit the datasets used in file tailoring output.(ISPCTL1/ISPCTL2)
/* Use TSO DDEDIT. No parameters required.
/**********************************************************************
PROC 0
  ISPEXEC CONTROL ERRORS RETURN
  CONTROL ASIS
  ISPEXEC VGET (ZSCREEN)
  IF &ZSCREEN = &STR() THEN SET &ZSCREEN = 1
  SET &DDN = &STR(ISPCTL)&ZSCREEN
  ISPEXEC LMINIT DATAID(DDVAR) DDNAME(&DDN)
  SET &RC = &LASTCC
  IF &RC > 0 THEN +
  DO
    ISPEXEC SETMSG MSG(&ZERRMSG)
    EXIT
  END
  ISPEXEC EDIT DATAID(&DDVAR)
  SET &RC = &LASTCC
  IF  &RC > 4 THEN +
    ISPEXEC SETMSG MSG(&ZERRMSG)
  ISPEXEC LMFREE DATAID(&DDVAR)
EXIT


DDE

This CLIST is a generalized version of DDEDIT.  You enter
the DDNAME to edit as a parameter to the CLIST.  For
example, enter TSO DDE SYS0085 to edit the dataset
associated with the DDNAME SYS0085.  Enter TSO LISTA
STATUS SYSNAMES to find out all the datasets with
DDNAMEs allocated to your session.

I would suggest a command table entry like this to use DDE:

      DDE     0    SELECT CMD(%DDE  &ZPARM)


DDE source code

/**********************************************************************
/* The  following file is  DDE
/* Edit the dataset given the ddname. Especially useful for VIO
/* datasets. Use TSO DDE ddname.
/**********************************************************************
PROC 1 DDN
  ISPEXEC CONTROL ERRORS RETURN
  CONTROL  ASIS
  ISPEXEC LMINIT DATAID(DDVAR) DDNAME(&DDN)
  SET &RC = &LASTCC
  IF &RC > 0 THEN +
  DO
    ISPEXEC SETMSG MSG(&ZERRMSG)
    EXIT
  END
  ISPEXEC EDIT DATAID(&DDVAR)
  SET &RC = &LASTCC
  IF &RC > 4 THEN +
    ISPEXEC SETMSG MSG(&ZERRMSG)
  ISPEXEC LMFREE DATAID(&DDVAR)
EXIT


P Kumar (Australia)                                       c Xephon 1991

