The TSO SLEEP command
The following TSO command, SLEEP, is similar to the VM/CMS SLEEP command.  This
version only accepts input in seconds, which is sufficient for our needs.
*        SET THE TSO USER IN WAIT THE NUMBER OF SECS INDICATED
&ID      SETC  'SLEEP'
&IDLEN   SETA  K'&ID
&ID      INITR                          see issue 15 pages 20-24
         LR    R2,R1                    SAVE CPPL POINTER
         USING CPPL,R2                  ADDRESS CPPL
         L     R7,CPPLCBUF              GET ADDR OF BUFFER
         LH    R3,0(R7)                 GET PARM LENGTH
         CH    R3,=AL2(5+&IDLEN)        TEST FOR ZERO DATA
         BNH   EXIT                     IF ZERO, EXIT
         LR    R15,R3                   SAVE LENGTH
         SH    R15,=AL2(5+&IDLEN)       REDUCE BY COMMAND HEADER
         LA    R8,&IDLEN+4(R7)          GET ADDR OF 1ST BYTE AFTER CMD
SCAN     LA    R8,1(R8)                 POINT TO NEXT
         CLI   0(R8),C'0'               TAKE AWAY LEADING NONDIGITS/0
         BH    FIRSTDIG                 GOT A DIGIT
         BCT   R15,SCAN                 RECYCLE
         B     EXIT                     NO VALUE, THEN EXIT
FIRSTDIG LR    R9,R8                    SAVE ADDR OF FIRST DIGIT
SCAN2    LA    R9,1(R9)                 GET NEXT BYTE
         BCTR  R15,0                    COUNT DOWN RESIDUAL COUNT
         LTR   R15,R15                  SOMETHING LEFT
         BZ    ENDSCAN                  NO MORE INPUT
         CLI   0(R9),C'0'               LOOK FOR NONDIGITS
         BNL   SCAN2                    IF DIGIT RESCAN
ENDSCAN  SR    R9,R8                    COMPUTE LENGTH
         CH    R9,=H'9'                 TEST FOR TOO LONG
         BNH   LENOK                    LENGTH OK
         LA    R9,9                     ASSUME LENGTH OF 9
LENOK    BCTR  R9,R0                    REDUCE FOR EXECUTE
         LA    R10,7                    GET LENGTH OF DOUBLE WORD
         SLL   R10,R4                   SHIFT TO HIGH ORDER
         OR    R10,R9                   SET UP FOR EXECUTE
         EX    R10,PACK                 PACK THE NUMBER
         CVB   R11,DW                   CONVERT TO BINARY
         LA    R6,100                   GET IN HUNDREDS
         MR    R10,R6                   GET IN HUNDREDS
         ST    R11,INTVL                SAVE WAIT TIME
         LA    R9,1(R9)                 FOR TPUT
         STIMER WAIT,BINTVL=INTVL       WAIT
         EXITR                          see issue 15 pages 25-26
EXIT     TPUT  ERROR,L'ERROR            INFO ABOUT PARM ERROR
         EXITR                          see issue 15 pages 25-26
DW       DS    D                        WORK FOR CONVERT
INTVL    DC    F'100'                   WAIT INTERVAL
PACK     PACK  DW(0),0(0,R8)            EXECUTED PACK
ERROR    DC    C'PARM ERROR'            ERROR MSG
         IKJCPPL
         END
The format of the command is:
     SLEEP nnn
where nnn is the number of seconds for which the TSO user will be made to wait.

Nils Plum
Systems Programmer (Denmark)  © Nils Plum 1988








