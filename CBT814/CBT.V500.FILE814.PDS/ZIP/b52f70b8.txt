SAM 31-bit I/O interface

IO31     TITLE 'SAMPLE QSAM 31-BIT MODE I/O PROGRAM'
IO31     CSECT
IO31     AMODE 31
IO31     RMODE 24
***********************************************************************
*        REMARKS.       THIS IS AN I/O 31 BIT SAMPLE PROGRAM. IT      *
*                       USES THE NEW DCBE MACRO EXTENSION WHICH IS    *
*                       PART OF DFSMS/MVS 1.1.                        *
***********************************************************************
NEXTSA   EQU   8                       POINTER TO PREVIOUS SAVEAREA
SIGNF    EQU   X'F0'                   POSITIVE SIGN
ZERO     EQU   X'00'                   ZERO
         BAKR  R14,0                   SAVE CALLER'S ARS + GPRS
*                                      IN THE LINKAGE STACK
         LAE   R12,0(R15,0)            SET UP PROGRAM BASE REGISTER
         USING IO31,R12                INFORM THE ASSEMBLER
         STORAGE OBTAIN,LENGTH=WORKALEN,ADDR=(R10),SP=0,KEY=8,         X
               LOC=BELOW,COND=NO,RELATED=(FREEWORK,'FREE WORK AREA')
         LAE   R13,0(R1,0)             @ THE WORKAREA
         USING WORKAREA,R13            INFORM THE ASSEMBLER
         LAE   R13,SAVEAREA            @ THE SAVEAREA
         MVC   PREVSA,=C'F1SA'         PUT ACRONYM INTO SAVEAREA
*                                      TO INDICATE STATUS SAVED ON
*                                      THE LINKAGE STACK.
         BAL   R2,OPEN                 OPEN THE FILES
         BAL   R2,GETIO                OBTAIN I/O AREA
         BAL   R2,READSMF              READ THE SMF TYPE 14 RECORDS
SMFEOF   EQU   *
         ICM   R4,B'1111',RECADDR      RECORD @
         ICM   R5,B'1111',RECLEN       BUFFER LENGTH
         STORAGE RELEASE,ADDR=(R4),LENGTH=(R5),SP=0,KEY=8,             X
               COND=NO,RELATED=(GETWORK,'OBTAIN WORK AREA')
CLOSEFLE EQU   *
         CLOSE (SMFDCB),MODE=31        CLOSE THE FILES
RETURN   EQU   *
         LAE   R1,0(R13,0)             ADDRESS TO FREE
         SLR   R15,R15                 SET RC TO ZERO
         PR                            RESTORE CALLERS AR'S
*                                      GPR'S 2-14 AND RETURN
*                                      TO CALLER
OPEN     EQU   *
         OPEN (SMFDCB,(INPUT)),MODE=31  OPEN
         LA    R9,SMFDCB                ADDRESS SMF DCB
         USING IHADCB,R9                INFORM THE ASSEMBLER
         TM    DCBOFLGS,X'10'           OPEN OKAY?
         BZ    OPNERR1                  NO-
         BR    R2                       RETURN TO CALLER
READSMF  EQU   *
         USING SMFRCD14,R10             INFORM THE ASSEMBLER
         ICM   R10,B'1111',RECADDR      POINT TO RECORD
         GET   SMFDCB,(R10)             READ SMF TYPE 64 RECORDS
         B     READSMF                  YES-
GETIO    EQU   *
         ICM   R5,B'1111',RECLEN        BUFFER LENGTH
         STORAGE OBTAIN,LENGTH=(R5),ADDR=(R3),SP=0,KEY=8,              X
               LOC=ANY,COND=NO,RELATED=(FREEWORK,'FREE WORK AREA')
         STCM  R3,B'1111',RECADDR       STORE RECORD ADDRESS
         BR    R2                       RETURN TO CALLER
OPNERR1  EQU   *
         ABEND 001,DUMP
         LTORG
RECLEN   DC    AL4(32760)              MAX RECORD LENGTH
PRTDCBE  DCBE  RMODE31=BUFF            DCB EXTENSION
SMFDCBE  DCBE  RMODE31=BUFF            DCB EXTENSION
DW       DC    D'0'                    WORKAREA
SMFDCB   DCB   DSORG=PS,DEVD=DA,MACRF=(GM),DDNAME=SMFRECS,EODAD=SMFEOF,X
               DCBE=SMFDCBE
WORKAREA DSECT
SAVEAREA DC    18F'0'                  REGISTER SAVEAREA
PREVSA   EQU   SAVEAREA+4,4            PREVIOUS SAVEAREA
RECADDR  DS    AL4                     SMF TYPE 14 BUFFER @
IBUFLEN  DS    AL4                     SYSIN BUFFER LENGTH
INPBUFF@ DS    AL4                     SYSIN BUFFER @
WORKALEN EQU   *-WORKAREA              WORK AREA LENGTH
         DCBD  DSORG=(QS),DEVD=DA
SMFRCD14 DSECT
         IFGSMF14
         END   IO31


R F Perretta
Senior Systems Programmer (UK)                                   c Xephon 1995

