Migrating from TSO SYS1.UADS to RACF

RACF release 1.8 with TSO/E 4.0 enables you to move your
SYS1.UADS information into the RACF database.  After defining the
proper RACF structure we were confronted with the problem that
individual TSO users logging on after the conversion were finding
the command line cleared.  The command line is often used in
order to trigger a log-on extended CLIST.  Bypassing this problem
is accomplished by the following program, which extracts the UPT
and command line information from the SYS1.UADS and puts it
into the TUPT and TCOMMAND field of the RACF user profile TSO
segments.  This facility requires:

1   RACF 1.8 and TSO/E 4.0.

2   The definition of the user profile TSO segments.  This is done
by executing the CLIST generated by the RACONVRT command.

3   You must copy your SYS1.UADS into a sequential file with
the same LRECL and BLKSIZE.

After running the program, nobody will notice any difference at
logon and you can get rid of your SYS1.UADS instantly.

RACFTSO  TITLE 'RACF TSO MIGRATION PROGRAM'
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* RACFTSO  -  RACF USER TSO FIELDS CONTROL  -  BATCH                  *
* PURPOSE  -  To allow the RACF administration to alter the data      *
*             specified in the user profile TSO datafields i.o.       *
*             to be able to switch clean from UADS to RACF database.  *
* INPUT    -  UADS sequential file containing users TSO info.         *
* OUTPUT   -  Updated datafield in RACF database by ICHEINTY.         *
* REGISTER - 0 ENTRY POINT                                            *
*            1 POINTS TO CALLERS SAVE AREA                            *
*            2 BASED ON ACEE                                          *
*            3 LENGTH OF USERID                                       *
*            4 UADS USERID POINTER                                    *
*            5 UADS RECORD COUNTER (10 RECORDS FOR EACH USER)         *
*            6 POINTS TO END OF UADSUSERID                            *
*            7 LENGTH OF DATA TO BE SAVED                             *
*            8 RACF USERID                                            *
*           12 BASE REGISTER                                          *
*           13 POINTS TO OWN SAVE AREA                                *
*           14 RETURN ADDRESS                                         *
* COND. C. -  OUTPUT R15 :  00  = ok                                  *
*                           nn  = see RACF SPL ICHEINTY return codes  *
*                           12  = userid was not def. to RACF         *
*                           13  = unable to get acee of submitter     *
*                           15  = submiter does not have SPECIAL      *
* Program will continue if c.c. <= 12                                 *
* link not reentr. with auth. code = 1 in APF authorized library.     *
* Logic: IF SUBMITTER HAS SPECIAL DO                                  *
*           OPEN UADS FILE                                            *
*           DO WHILE NOT EOF UADS                                     *
*              GET UADS RECORD                                        *
*              IF COUNTER=0 GET USERID FROM POSITION 5-13             *
*              COUNTER = COUNTER + 1                                  *
*              IF COUNTER=9 GET FIRST PART OF COMMAND FROM POS.166-172*
*              IF COUNTER=10 DO                                       *
*                 GET SECOND PART OF COMMAND FROM POS. 1-74           *
*                 GET UPT FROM POS. 149-167                           *
*                 UPDATE RACF USER PROFILE                            *
*                 COUNTER=0                                           *
*                 ENDO                                                *
*              ENDO                                                   *
*           CLOSE UADS FILE                                           *
*           ENDO                                                      *
*         EXIT                                                        *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
RACFTSO  AMODE  24
RACFTSO  RMODE  24
RACFTSO  CSECT
         SAVE   (14,12)                   regs in callers save area
         LR     R12,R15                   r12 exe program base
         USING  RACFTSO,R12               r12 asm program base
         LA     R1,SAVE                   r1 -> own save area
         ST     R1,8(R13)                 link call area to save area
         ST     R13,SAVE+4                link save area to call area
         LA     R13,SAVE                  point r13 to own save area
*** first we switch ACEE's to check authority                      ***
         L      R2,PSAAOLD-PSA       load last dispatched ascb
         L      R2,ASCBASXB-ASCB(R2) then the extension block
         L      R2,ASXBSENV-ASXB(R2) get RACF control block
         USING  ACEE,R2              R2 -> asm base = acee-address
         LA     R15,13               set r.c.= $13
         LTR    R2,R2                is acee present ?
         BZ     OUT                  not present if 0
         LA     R15,15               set r.c. = $15
         TM     ACEEFLG1,ACEESPEC    check for special
         BZ     OUT                  user is not special if 0
         DROP   R2                   drop ACEE addressing
         XR     R15,R15              set returncode=0
         ST     R15,REGSAVE3         save for later
*** authorisation is alright, go for actual processing of UADS     ***
         OPEN   UADSIN              ds with the UADS info
CLEAR    XR     R5,R5               clear recordcounter
         XC     USERID,USERID       clear userid field
REQUEST  XC     UADSINA,UADSINA     clear command part of input record
         GET    UADSIN,UADSINA      get inputrecord
         C      R5,=F'00'           do we have a count of zero record?
         BNE    SKIPUID             if not,no userid can be found here
         LA     R4,UADSUID          get low userid address
         LA     R8,USERID           R8 --> RACF userid area
         LA     R6,UADSUID+7        get high userid address
         XR     R3,R3               userid length = 0
IDLOOP   CLI    0(R4),C' '          reached shorter userid ?
         BE     FNDIDLA             yes, requires special action
         CR     R4,R6               reached max userid length
         BE     FNDIDL              if so, we have got the user
         MVC    0(1,R8),0(R4)       move userid char. to area
         LA     R8,1(R8)            increase area pointer
         LA     R4,1(R4)            increase userid pointer
         LA     R3,1(R3)            increase userid length
         B      IDLOOP              keep checking
FNDIDLA  S      R3,=F'01'           decrease userid length 1
FNDIDL   MVI    0(R8),C' '          clear the UADS padding zero
         STC    R3,USNAM            set useridlength for RACF ICHEINTY
         MVC    USNM,USERID         move userid
SKIPUID  LA     R5,1(R5)            record counter + 1
         C      R5,=F'09'           reached a count of 9 record ?
         BNE    TEST10              if not, check for 10th
         MVC    CMDDATA(6),UADSCM1   move first part of COMMAND record
TEST10   C      R5,=F'10'            reached a count of 10 record ?
         BL     REQUEST              no, go for next UADS record
         MVC    CMDDATA+6(74),UADSCM2    move scnd part of COMMAND
         MVC    UPTDATA(24),UADSUPT  move UPT info into RACF TUPT
*** initialize RACF ICHEINTY work area and update user profile ***
         XC     USWRKIND,USWRK       clear install.data zone
         XC     USWRK(USWRKLEN),USWRK     clear RACF macrofields
         LA     R3,USLEN             length for macro
         ST     R3,USWRKA            workspace for macro
         MVC    USWRKIND(80),CMDDATA info -> RACFinfo
         LA     R7,CMDLNGT           get length
         STC    R7,USWRKIDL          put the length in macro variable
         LA     R7,1(R7)             increase length of data field
         STC    R7,USWRKINS          because this includes lengthbyte
         XR     R7,R7                clear R7
         LH     R7,USWRKINS          load only the first halfword
         ICHEACTN MF=(E,UPDATA),FLDATA=((7),USWRKIDL)
         ICHEINTY MF=(E,ALTER1),                                       C
               ENTRY=USNAM,                                            C
               ACTIONS=(UPDATA),                                       C
               OPTIONS=(FLDEF)
         XC    USWRKIND,USWRKIND      clear INTY data area
         MVC   USWRKIND(24),UPTDATA   info -> RACF info
         LA    R7,UPTLNGT             get length
         STC   R7,USWRKIDL            put the length in macro variab.
         LA     R7,1(R7)              increase length of data field
         STC    R7,USWRKINS           because this includes lengthbyte
         XR     R7,R7                 clear R7
         LH     R7,USWRKINS           load only the first halfword
         ICHEACTN MF=(E,UPTUPT),FLDATA=((7),USWRKIDL)
         ICHEINTY MF=(E,ALTER1),                                       C
               ENTRY=USNAM,                                            C
               ACTIONS=(UPTUPT),                                       C
               OPTIONS=(FLDEF)
         LTR    R15,R15               check returncode
         BZ     KNWNMSG               if zero let submitter know
         ST     R15,REGSAVE3          save returncode
         MVC    @MSGFLD(@MSGLEN),@NPMSG    wto mf=l to mf=e area
         MVC    @RACMSG,NKNWN         "unknown user" message
         MVC    @USERID,USNM          move userid in message field
         WTO    MF=(E,@MSGFLD)        issue message no user profile
         B      CLEAR                 go for next UADS userid
KNWNMSG  MVC    @MSGFLD(@MSGLEN),@NPMSG    wto mf=l to mf=e area
         MVC    @RACMSG,KNOWN         move alright message
         MVC    @USERID,USNM          move userid in message field
         WTO    MF=(E,@MSGFLD)        issue message user profile ok
         B      CLEAR                 go for next uads userid
OUT      ST     R15,REGSAVE3          save returncode
         WTO    '* AUTHORISATION FAILED * PROGRAM NOT RUN',ROUTCDE=(11)
GOBACK   EQU   *                      we have had the last record
         CLOSE (UADSIN)               and the input ds
         L      R15,REGSAVE3          get condition code back
         L      R13,SAVE+4            get callers area address
         RETURN (14,12),RC=(15)       restore registers and return
UADSIN   DCB    DDNAME=UADSIN,DSORG=PS,MACRF=GM,EODAD=GOBACK
KNOWN    DC     CL17'PROFILE UPDATED  '
NKNWN    DC     CL17'NOT KNOWN TO RACF'
CMDLNGT  EQU    80                max. length of command record
UPTLNGT  EQU    24                max. length of upt record
         LTORG
SAVE     DS     18F               register save area
REGSAVE3 DS     F                 errorcode save area
@NPMSG   WTO   '* WARNING * - XXXXXXX AAAAAAAAAAAAAAAAA              ',C
               ROUTCDE=11,MF=L
@MSGLEN  EQU    *-@NPMSG
@MSGFLD  DS     XL(@MSGLEN)
@USERID  EQU    *-4-39,8,C'C'
@RACMSG  EQU    *-4-31,17,C'C'
CMDDATA  DS     CL80              TCOMMAND save field
UPTDATA  DS     CL24              TUPT save field
USERID   DS     CL8               USERID save field
UADSINA  DS     0CL172            the input command record
UADSCM2  DS     CL4               start of second command part
UADSUID  DS     CL8               start of userid
         DS     CL136             skip
UADSUPT  DS     CL18              start of upt info
UADSCM1  DS     CL6               start of first command part
UPDATA   ICHEACTN FIELD=TCOMMAND,FLDATA=(*-*,*-*)
UPTUPT   ICHEACTN FIELD=TUPT,FLDATA=(*-*,*-*)
ALTER1   ICHEINTY ALTER,                                               C
               TYPE='USR',                                             C
               ENTRY=*-*,                                              C
               ACTIONS=*-*,                                            C
               MF=L
US       DS     0F
USNAM    DS     AL1                        length of userid
USNM     DS     CL8                        the userid
USWRK    DS     0F                         pointer for cleanup
USWRKA   DS     F                          workarea length
         DS     CL6
         DS     CL2
         DS     F
         DS     CL8
         DS     F
USWRKINS DS     AL2                        length of data & lengthfield
USWRKLEN EQU    *-USWRK                    specifies cleanup area
USWRKIDL DS     AL1                        data length field
USWRKIND DS     CL255                      data field for user
USLEN    EQU    *-USWRKA                   length of total area
         IHAACEE
         IHAASXB
         IHAASVT
         IHAASCB
         IHAPSA
         END


EXECUTION JCL

//USRUPDT  EXEC PGM=RACFTSO
//SYSPRINT DD   SYSOUT=*
//UADSIN   DD   DISP=SHR,DSN=your sequential SYS1.UADS


Ruud de Wit
Systems Programmer
Fokker Aircraft (The Netherlands) © Xephon 1989

