RACF resident started task table

Using started task procedures you may encounter some of the
following problems:

1   At most large sites running MVS/XA, the definition of
started task users requires a lot of procedural and administrative
handling, because installation of the started task table and the
definition of started task user-ids is done by different
departments.

2   The generic entry in the started task table enables people
who are authorised for SYS1.PROCLIB to define a procedure with
the same name as, for example, a special user-id.  By starting this
procedure they can use authorisations which belong to the special
user-id.

3   Sometimes, it is desirable to prevent the started task user-
ids from accessing the system or getting revoked as regular user-
ids in combination with a password.

4   It is necessary to IPL in order to get an updated started task
table active.


To solve these problems you could consider installing a RACF
RACINIT pre-processing exit as shown below.  Pre-requisites for
the installation of this exit are:

1   MVS/XA releases from 1.5; RACF releases from 1.6.

2   The definition of the RACF group class $STCTABL and
member class #STCUSER.

3   The addition of the generic entry in the started task table
(ICHRIN03) assigning a user-id with the same name as the
procedure.

4   Standard names for the started task user-ids which, in this
case, are defined as STCxxxx, where xxxx could be any
alphanumeric character string.


These naming conventions are your own choice and only
necessary if you want to solve problem 3.  If not, you can remove
the STCxxxx test coding given between the two *#######OPTION
comment labels.

After installing the exit and activating the RACF $STCTABL class,
the following RACF command should be issued:
    RDEF $STCTABL IMSPROD ADDMEM(STC001A)
This enables the assignment of user-id STC001A to the task
IMSPROD.  In combination with the correct RACF-owning
structure, this dynamic started task table enables you to assign a
supervisor responsible for started task administration.  The exit is
written in such a way that, until a RACF $STCTABL profile is
defined, the task will run with the user-id specified in the
ICHRIN03 started task table.  When all $STCTABL entries have
been defined you can remove the ICHRIN03 started task table
entries, leaving the generic entry as given in Q3.  Some of the
started task procedures, eg JES2, do not need any authorisation
and are probably not defined in the ICHRIN03 started task table.
This kind of procedure could be assigned to a STCxxxx user-id
without any authorisation.  Problems with RACF do not end in
problems with started tasks because user-ids equal to the
procedure name will be assigned and, since there is no
authorisation checking, the task will run as normal.

ICHRIX01 TITLE 'RACF RACINIT PREPROCESSING EXIT'
***********************************************************************
***      RACF preprocessing exit for propagation verification,      ***
***      and assignement of started task userid's.                  ***
***      This exit routine performs the following functions:        ***
***      1. Checks if the PROCNAME has been filled.                 ***
***         If a $STCTABL profile "PROCNAME" exists, the member     ***
***         of this profile (#STCUSER) wich  is the propagation     ***
***         STC user, wil be filled in as userid.                   ***
***         A standard Started Task table is now irrelevant.        ***
***      2. Checks if the user is a started task (STCxxxx) user     ***
***         and verifies if this user is used without password.     ***
***         This feature has to be added in order to prevent the    ***
***         use of a started task user as an ordinary user id.      ***
***********************************************************************
*** Logic:  IF userid = 'STC...' AND NOT PROPAGATED THEN ABEND 46E  ***
***         IF STARTED TASK PROCEDURE THEN DO                       ***
***            GET RACF $STCTABL "procname" PROFILE                 ***
***            IF PROFILE AND MEMBER AVAILABLE THEN ASSIGN USERID   ***
***            ELSE DO                                              ***
***               IF procname = userid THEN ABEND 57E               ***
***               ELSE ASSIGN ICHRIN03 USERID                       ***
***               ENDO                                              ***
***            ENDO                                                 ***
***         EXIT                                                    ***
*** Registers on entry:                                             ***
***      R1    Pointer to exit parameter list                       ***
***            Offset  4 - Flag byte address. Use bit 3             ***
***                        0 = PASSCHK YES specified                ***
***                        1 = PASSCHK NO  specified (propagation)  ***
***            Offset  8 - USERID address.                          ***
***            Offset 16 - START address, pointing to PROC name.    ***
***      Exit:                                                      ***
***      R15   0 - Continue normal SVC processing.                  ***
***            4 - Exit fails request.                              ***
*** not used   8 - Request accepted, no more SVC processing.        ***
***                Post processing exit will be invoked.            ***
***      MVS/XA AMODE and RMODE considerations:                     ***
***        RMODE(ANY)                                               ***
***        AMODE(31)                                                ***
***      Macros            Macro library                            ***
***      SAVE              SYS1.MACLIB                              ***
***      REGEQU            your own maclib                          ***
***      ICHRIXP           your own maclib (RACF 1.8 supplies one)  ***
***      ICHEINTY          SYS1.AMODGEN                             ***
***      Load module name  System library                           ***
***      ICHRIX01          SYS1.LPALIB                              ***
***      (RENT,REFR)                                                ***
***********************************************************************
ICHRIX01 CSECT
ICHRIX01 AMODE 31
ICHRIX01 RMODE ANY
         SAVE  (14,12),,ICHRIX01.RACF17.&SYSDATE
         LR    R12,R15             Program addressability
         USING ICHRIX01,R12        Program base
*       check if USERID <> STC...  and,                               *
*       check if proc field has been specified; PROC name <> blanks   *
         XR    R7,R7               Return code 0
         LR    R2,R1               Save parm list address in R2
         USING RIXPL,R2            Base RACINIT plist on R2
         L     R3,RIXUID           point to userid in parm list
         L     R4,RIXSTART         point to procname
         CLC   0(8,R4),=8C' '      has the procname been filled in
         BNE   GETSTC              if so, started task, go find userid
*####### OPTION                    start label
         CLC   1(3,R3),=C'STC'     userid = STC... ?
         BNE   RETURN              if not, normal user, leave
*       Check if user is a propagated user; check PASSCHK = NO        *
         L     R4,RIXFLAG          point to flag byte
         TM    0(R4),RIXPSCKN      PASSCHK = NO request
         BNZ   RETURN              nz: passchk=no, allowed (propagated)
         WTO   'RIX0046E - INVALID USE OF USERID - INITIATION FAILED', C
               ROUTCDE=(11)        0: combined with password: fail
         LA    R7,4                set R.C. = 04; fail job
*####### OPTION   END              end label
         B     RETURN              and leave
*       If PROC name <> blanks we have to provide a started task user *
*       First find length of procname; R4 --> procname; R5 = length   *
GETSTC   LA    R5,8                max. procname length is 8
         LA    R6,7(R4)            R6 -> end of procname field
PROCLOOP CLI   0(R6),C' '          is there a procname character ?
         BNE   ICHSTART            if so, go and get STC user from RACF
         BCTR  R6,R0               decrease procname pointer
         BCT   R5,PROCLOOP         while procname length <> 0, check
*       Establish workarea. Conditional GETMAIN is preferred.         *
ICHSTART GETMAIN RC,LV=STORLENG,LOC=ANY    getmain storage private area
         LTR   R15,R15             test availability of storage
         BZ    ICHBEGIN            if zero, storage available; go on
         WTO   'RIX0112I - ICHRIX01 - NO VIRTUAL STORAGE ALLOCATION',  C
               ROUTCDE=1           we have got a problem; try CSA
         GETMAIN RU,LV=STORLENG,SP=241,LOC=ANY     get storage from CSA
         LTR   R15,R15             test availability of storage
         BZ    ICHBEGIN            if zero, storage available; go on
         WTO   'RIX0113E - ICHRIX01 - NO VIRTUAL CSA ALLOCATION',      C
               ROUTCDE=1           this really is a problem
         B     RETURN              failed, get out
ICHBEGIN LR    R11,R1              dynamic area addressability
         USING STORAAAA,R11        base on R11
         LA    R1,SAVEAREA         R1 -> own savearea
         ST    R1,8(R13)           own save address in callers area
         ST    R13,SAVEAREA+4      save caller's savearea address
         LR    R13,R1              point our savearea addr. to R13
         STC   R5,PRCLNGTH         save length of procname
         LA    R10,GENWRK          R10 -> getmained area for ICHEINTY
         USING IGR,R10             base ICHEINTY area discr. on R10
*       Try to find the $STCTABLE profile with name of procedure      *
         XC    IGRWH(L'IGRWH),IGRWH         clear ICHEINTY workarea
         LA    R9,IGRTOTL                   get length of workarea
         ST    R9,IGRWHWL                   ICHEINTY needs this value
         XC    GENIDPN(L'GENIDPN),GENIDPN   clear profile name
         MVC   GENIDPNV(L'GENIDPNV),0(R4)   move procname
         MVC   GENIDPNL,PRCLNGTH            and length in ICHEINTY fld
         MVC   0(GENLEN,R11),GENLOC         move ICHEINTY constants in
*                                           getmained area.
         ICHEINTY MF=(E,(11)),                                         C
               ENTRY=GENIDPN,                                          C
               WKAREA=(R10)
         LTR   R15,R15             check profile existance
         BNZ   NOASSIGN            if not, no STC userid assignement
         CLI   IGRWFML,X'00'       member available ?
         BE    NOASSIGN            no; no STC userid assignement
         MVC   0(1,R3),IGRWFML     length of userid -> STC userid lngth
         MVC   1(8,R3),IGRWFM      STC userid assignement
         B     ICHEND              leave; all ok
*       We could not find a RACF $STCTABLE or #STCUSER; check if      *
*       STC table provided a userid <> procname, so installation      *
*       of this exit is downward compatible. if not: abend.           *
NOASSIGN XR    R5,R5               clear R5
         XR    R9,R9               clear R9
         L     R4,RIXSTART         R4 -> procname
         ICM   R5,1,PRCLNGTH       R5 = length of procname
         LA    R8,1(R3)            R8 -> userid
         ICM   R9,1,0(R3)          R9 = length of userid
         CLCL  R4,R8               compare those two over longest lngth
         BNE   ICHEND              n.e.; the STC table assigned userid
         MVC   0(9,R3),=8X'00'     clear userid field
         L     R4,RIXSTART         R4 -> procname; CLCL changed reg.
         MVC   @MSGFLD(@MSGLEN),@NPMSG   WTO MF=L to MF=E area
         MVC   @PROCNM(8),0(R4)    move procname in WTO area
         WTO   MF=(E,@MSGFLD)      issue message
         LA    R7,4                set R.C. = 04; fail job
ICHEND   DROP  R10                 no more workarea addressability
         L     R13,SAVEAREA+4      Restore R13
         DROP  R11                 no more ICHEINTY addressability
         FREEMAIN RC,LV=STORLENG,A=(11)    free getmained storage
         LTR   R15,R15             test freeing of storage
         BZ    RETURN              if ok, get out normal
         FREEMAIN RU,LV=STORLENG,A=(11),SP=241  free CSA storage
RETURN   DROP  R2                  release R2
         LR    R15,R7              get return code
         RETURN (14,12),RC=(15)    beat it
         LTORG
GENLOC   ICHEINTY LOCATE,                                              C
               TYPE='GEN',                                             C
               CLASS=GENIDCLN,                                         C
               GENERIC=YES,                                            C
               ENTRY=*-*,                                              C
               ACTIONS=GENACTN,                                        C
               MF=L,                                                   C
               WKAREA=*-*
GENLEN   EQU   *-GENLOC
GENACTN  ICHEACTN FIELD=MEMLST     $STCTABL member list #STCUSER
GENIDCLN DC    CL8'$STCTABL'       RACF class wich contains STC uid's
@NPMSG   WTO   'RIX0057E - XXXXXXXX - UNKNOWN STARTED TASK PROCEDURE - C
               NO USERID PROPAGATION',ROUTCDE=11,MF=L
@MSGLEN  EQU   *-@NPMSG
STORLENG EQU   STORZZZZ-STORAAAA   length of getmain
STORAAAA DSECT
GENLOCA  DS    CL(GENLEN)          area for ICHEINTY workfields
GENIDPN  DS   0CL9                 profile name
GENIDPNL DS    AL1                 wich consists of a lengthfield
GENIDPNV DS    CL8                 followed by the profile name
GENWRK   DS   0CL(L'IGRWH+255)     return arae of ICHEINTY
         DS    CL(L'IGRWH)         wich is all kind of flags
GENWRKV  DS    CL255               and the actual RACF information
SAVEAREA DS    18F                 csect register save area
PRCLNGTH DS    AL1                 proclength save field
@MSGFLD  DS    XL(@MSGLEN)         WTO message area
@PROCNM  EQU   *-4-62,8,C'C'       points to XXXXXXX in message
57E
STORZZZZ EQU   *
IGR      DSECT
IGRWH    DS   0CL30                this arae has to be min. 30 bytes
IGRWHWL  DS    F                   length of entire user area
         DS    CL6                 RBA return area
         DS    CL2                 flags
         DS    F                   duplicate DSN count
         DS    CL8                 reserved
IGRWHRL  DS    F                   length of returned area
IGRWHGL  DS    CL2                 total length of members
IGRWF    DS   0CL255               the requested profile information
IGRWFML  DS    AL1                 length of the first member name
IGRWFM   DS    CL8                 member name
         DS    CL246               we don't need the rest
IGRTOTL  EQU   *-IGRWH
         ICHRIXP                   plist for RACINIT pre/post pr. area
         END


MACRO DEFINITIONS

*        Install the following macros in one of your installation     *
*        macro libraries.                                             *
*        ICHRIXP only when running with previous RACF 1.8 releases    *
         MACRO
&NAME    ICHRIXP
RIXPL    DSECT
*       USE: DESCRIPTION OF AREA AT RACINIT PRE/POST ENTRY
RIXLNGTA DS    F                   number of param. fullwords
RIXFLAG  DS    F                   flag-byte address
RIXPSCKN EQU   B'00001000'         mask for passcheck bit
RIXUID   DS    F                   userid address
RIXPSSWA DS    F                   password address
RIXSTART DS    F                   proc start address
RIXINSTA DS    F                   installation param. address
RIXGRADR DS    F                   group address
RIXNWPSA DS    F                   newpassword address
RIXACEEA DS    F                   acee address
RIXPGMNA DS    F                   programmer name address
RIXACTIA DS    F                   accounting info address
RIXOIDCA DS    F                   oidcard address
RIXTERMA DS    F                   terminal identifier address
RIXWRKAA DS    F                   work area address
RIXABCDA DS    F                   abend code address
RIXRTCDA DS    F                   return code address.
RIXFLBA2 DS    F                   flag byte address reserved
RIXTRMDA DS    F                   terminal data address
RIXTRLNA DS    F                   terminal level number addr.
RIXJOBNA DS    F                   jobname address
RIXAPPLA DS    F                   application name address
RIXSUBPA DS    F                   subpool address
RIXACEE2 DS    F                   acee parameter address
RIXAPLDP DS    F                   application data pointer
RIXAPLLP DS    F                   application level pointer
RIXPCIA  DS    F                   passw. change interval addr
RIXPLCDA DS    F                   passw. last change data adr
RIXEND   EQU   *                   end-of-common-area
RIXLEN   EQU   RIXEND-RIXLNGTA     common-area length
         MEXIT
         MEND


STARTED TASK TABLE

***********************************************************************
*        Install the following table in LPA replacing the standard    *
*        started task table after completing all RACF $STCTABL        *
*        definitions.                                                 *
*        Initially only add the generic entry line.                   *
***********************************************************************
RACF     TITLE '** ICHRIN03 - RACF STARTED TASK TABLE **'
***********************************************************************
* ID  : ICHRIN03 - RACF STARTED TASK TABLE                            *
* USE : DEFINE THE USER IDS FOR STARTED TASKS.                        *
* DOC : SC28-1342-0 SPL: RACF, PAGE 5-13 5-15                         *
***********************************************************************
ICHRIN03 AMODE ANY
ICHRIN03 RMODE ANY
ICHRIN03 CSECT
         DC    AL2((TABEND-TABSTART)/32+32768)  number of entries
TABSTART EQU   *
* FORMAT ==>     PROCNAME    USERID      GROUPID    FLAG   RESERVED
*   GENERIC ENTRY. THE USERID IS THE SAME AS THE PROCNAME
         DC    C'*       ',C'=       ',C'        ',X'00',7X'00'
TABEND   EQU   *
         END


Ruud de Wit
Systems Programmer
Fokker Aircraft (The Netherlands) © Xephon 1989

