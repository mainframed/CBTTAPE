Documenting control flow in Assembler code

It is often the case that systems programmers have more
interest in coding Assembler than in documenting it properly
afterwards and also have received little or no training in proper
design techniques.  When faced with maintenance of
unstructured code it would be helpful to have a diagram of the
control flow next to the actual code. This REXX does just that
and also proves that programming without labels and branches
is not difficult given a suitable language.

This REXX is very simple to use - just specify an input source
file and an output print file. Two types of diagram can be
produced: a joining type (the default), where branches to the
same label are drawn with a single track line (see Figure 1),
and a full type, where all branches have their own separate
track line (see Figure 2).  Often both types are useful.

Other parameters which can be specified are the number of
track lines if the default value is not required and the keyword
NOBAL to exclude BAL instructions from being traced as
these tend not to be so important to the overall flow. The use of
TSO in batch is suggested for large files, because the amount
of iteration consumes a fair amount of CPU time.

Downward and upward branches are identified by the use of
different characters for the track lines, unknown labels are
flagged with a '?' and, if the allowed track lines are
insufficient, a warning is given and the line flagged with a
'@'.

The AFLOW REXX code runs under TSO/E Version 2.

AFLOW REXX

/* REXX : AFLOW - REXX IDENTIFIER - MUST BE FIRST LINE             */
/* CONTROL FLOW FORMATTING REXX FOR IBM ASSEMBLER SOURCE, PARMS:   */
/* SYNTAX: %AFLOW LIB.ASM(FRED) PFILE TRKS(22) FULL NOBAL          */
/* (1) DATASET FOR INPUT, (2) DATASET FOR PRINT OUTPUT  (REQUIRED) */
/* (2) TRACK NUMBER LIMIT CODED AS TRKS(NN) IF NOT DFT  (OPTIONAL) */
/* (4) FULL  OPTION WILL USE SEPERATE TRACKS (NOT JOIN) (OPTIONAL) */
/*  FULL MAKES SMALL BRANCHES MORE READABLE BUT LONGER ONES LESS   */
/* (5) NOBAL OPTION EXCLUDES BAL FROM FLOW TRACING      (OPTIONAL) */
/* NB: LRECL OF PRINT FILE MUST BE >= LRECL OF SOURCE + TRKS + 2   */
/* REGISTER EQUATES SHOULD NOT BE CODED IN SOURCE, USE YREGS MACRO */
/*******************************************************************/
ARG SOURCE PRINTF OPTIONS
IF SOURCE = ' ' | PRINTF = ' ' THEN
   CALL ERR 'DATASET PARAMETER WAS MISSING'
JOIN = 1; BN = 5                  /* JOIN DEFAULT, BAL DEFAULT     */
IF INDEX(OPTIONS,'FUL') > 0 THEN
   JOIN = 0                       /* FULL SPECIFIED AS OPTION      */
IF INDEX(OPTIONS,'NOB') > 0 THEN
   BN = 4                         /* SET ARRAY LIM BELOW BAL ENTRY */
PARSE VAR OPTIONS WITH 'TRKS(' LIM ')'
IF LIM = ' ' THEN                 /* DEFAULT NUMBER OF TRACK LINES */
   IF JOIN = 1 THEN
      LIM = 20                    /* LOWER DEFAULT TRACKS IF JOIN  */
   ELSE                           /* FEWER TRACKS MEANS FASTER RUN */
      LIM = 32
B.CDE.1 = ' BC ';   B.OFF.1 = 1   /* BRANCH NAME, NUMBER OF COMMAS */
B.CDE.2 = ' BCT ';  B.OFF.2 = 1
B.CDE.3 = ' BXH ';  B.OFF.3 = 2
B.CDE.4 = ' BXLE '; B.OFF.4 = 2
B.CDE.5 = ' BAL ';  B.OFF.5 = 1   /* MAY BE EXCLUDED WITH NOBAL    */

/* ALLOCATE FILES AND READ SOURCE FILE */
"ALLOC F(SOURCE) DA("SOURCE") SHR REUSE"
IF RC \= 0 THEN
   CALL ERR 'ALLOCATION OF SOURCE FILE FAILED'
"ALLOC F(PRINTF) DA("PRINTF") SHR REUSE"
IF RC \= 0 THEN
   CALL ERR 'ALLOCATION OF PRINTF FILE FAILED'
"EXECIO * DISKR SOURCE (STEM SRC. FINIS"   /* READ FILE INTO ARRAY */
IF RC \= 0 THEN
   CALL ERR 'ERROR READING THE SOURCE FILE'

/* INITIAL PASS, BUILD BRANCHING LIST */
MXD = 0; DUL = 0
DO I = 1 TO SRC.0                 /* FOR EACH LINE OF SOURCE INPUT */
   DO J = 1 TO LIM                       /* CLEAR TRACK LINE ARRAY */
      TRK.I.J = ' '
   END
   ADD.I = 0; DST.I = 0; NEW.I = ' '    /* TARGET, LENGTH, TAG     */
   IF SUBSTR(SRC.I,1,1) = ' ' THEN      /* IF NOT COMMENT OR LABEL */
      DO
         TOL = ANALCODE(SRC.I)              /* ANALYSE INSTRUCTION */
         IF TOL \= ' ' THEN                   /* IF A BRANCH THEN  */
            DO
               TOL = TOL || ' '                     /* ADD A BLANK */
               LEN = LENGTH(TOL)
               DO K = 1 TO SRC.0                 /* LOOK FOR LABEL */
                  IF SUBSTR(SRC.K,1,LEN) = TOL THEN
                     DO
                        ADD.I   = K          /* NOTE LABEL ELEMENT */
                        DST.I   = ABS(K-I)      /* BRANCH DISTANCE */
                        DUL     = DUL + 1
                        LST.DUL = DST.I    /* RECORD THIS DIST VAL */
                        LEAVE
                     END
               END
               IF ADD.I = 0 THEN          /* LABEL NOT FOUND FOR B */
                  NEW.I = '?'
            END
      END
   MXD = MAX(MXD, DST.I)                  /* NOTE HIGHEST DIST VAL */
END

/* PROCESS BY BRANCH DISTANCE,  LOW/HIGH DIRECTION BASED ON JOIN   */
IF JOIN = 1 THEN
   DO
      A = MXD; B = 1; C = -1; A2 = 1; B2 = LIM; C2 = 1; MXF = 0
   END
ELSE
   DO
      A = 1; B = MXD; C = 1; A2 = LIM; B2 = 1; C2 = -1; MXF = LIM
   END
DO T = A TO B BY C                    /* FOR EACH BRANCH DISTANCE  */
   F = 0
   DO I = 1 TO DUL WHILE (F = 0)         /* WAS DIST ACTUALLY USED */
      IF T = LST.I THEN
         F = 1                           /* YES PROCESS THIS DIST  */
   END
   DO I = 1 TO SRC.0 WHILE (F = 1)    /* SCAN THROUGH ANALYSED SRC */
      IF T = DST.I THEN               /* A BRANCH OF THIS DISTANCE */
         DO
            S = MIN(I, ADD.I)                     /* LOWEST POINT  */
            E = MAX(I, ADD.I)                     /* HIGHEST POINT */
            SAME = 0
            IF JOIN = 1 THEN                      /* ATTEMPT JOIN  */
               DO
                  DO K = 1 TO LIM WHILE (SAME = 0)   /* SAME DEST? */
                     SAME = K
                     IF ADD.I > I THEN          /* DOWNWARD BRANCH */
                        D = 1
                     ELSE                       /* UPWARD BRANCH   */
                        D = -1
                     DO J = I TO ADD.I BY D WHILE (SAME = K)
                        IF TRK.J.K = ' ' THEN
                           SAME = 0                  /* DIFF DEST  */
                     END
                     Z = ADD.I
                     IF TRK.Z.SAME \= '>' THEN       /* ENDS HERE? */
                        SAME = 0
                  END
                  IF SAME > 0 THEN          /* SUITABLE JOIN FOUND */
                     DO
                        NEW.I = '<'; TRK.I.SAME = '<'
                     END
               END
            IF SAME = 0 THEN          /* IF NOT FOUND ONE THE SAME */
               DO
                  FREE = 0                  /* LOOK FOR FREE TRACK */
                  DO K = A2 TO B2 BY C2 WHILE (FREE = 0)
                     FREE = K
                     DO J = S TO E WHILE (FREE = K)
                        IF TRK.J.K \= ' ' THEN
                           FREE = 0              /* NOT FREE TRACK */
                     END
                  END
                  IF FREE > 0 THEN        /* IF FOUND A FREE TRACK */
                     DO
                        IF JOIN = 1 THEN   /* NOTE LAST TRACK USED */
                           MXF = MAX(MXF, FREE)
                        ELSE
                           MXF = MIN(MXF, FREE)
                        IF ADD.I > I THEN
                           DO J = I TO ADD.I
                              TRK.J.FREE = '|'      /* DOWN *CHAR* */
                           END
                        ELSE
                           DO J = I TO ADD.I BY - 1
                              TRK.J.FREE = '!'      /* UP *CHAR*   */
                           END
                        NEW.I = '<'; TRK.I.FREE = '<'    /* ENTRY  */
                        J     = ADD.I
                        NEW.J = '>'; TRK.J.FREE = '>'    /* EXIT   */
                     END
                  ELSE
                     DO
                        SAY 'TRACK NUM OF ' LIM ' BLOWN AT SRC LINE' I
                        NEW.I = '@'
                     END
               END
         END
   END
END

/* PREPARE RESULTS INTO NEW ARRAY */
IF JOIN = 1 THEN
   DO
      A3 = 1;   B3 = MXF; USE = MXF         /* USED TRACKS IF JOIN */
   END
ELSE
   DO
      A3 = MXF; B3 = LIM; USE = LIM - MXF     /* TRACKS IF NO JOIN */
   END
DO I = 1 TO SRC.0                      /* FOR EACH SOURCE LINE     */
   TMP = NEW.I; NEW.I = ' '
   IF TMP = '<' | TMP = '>' THEN         /* BRANCH START/END POINT */
      DO
         Z = 0
         DO J = A3 TO B3 WHILE (Z = 0)            /* FIND THE CHAR */
            IF TRK.I.J = TMP THEN
                Z = J
         END
         DO J = Z TO B3                 /* PROPAGATE CHARS ACCROSS */
            IF TRK.I.J = ' ' THEN       /* DONT OVERWRITE A LINE   */
               TRK.I.J = TMP
         END
      END
   DO J = A3 TO B3
      NEW.I = NEW.I || TRK.I.J          /* START WITH TRACK LINES  */
   END
   NEW.I = NEW.I || TMP SRC.I          /* ADD ON SOURCE LINE       */
END

/* WRITE NEW FORMATTED PRINT TO PRINTF DDNAME */
"EXECIO * DISKW PRINTF (STEM NEW. FINIS"  /* WRITE FILE FROM ARRAY */
IF RC \= 0 THEN
   CALL ERR 'ERROR WRITING PRINT FORMAT FILE'
SAY 'AFLOW COMPLETED FORMAT, USED TRACKS' USE ' OUT OF ' LIM
"FREE F(SOURCE PRINTF)"
EXIT 0

/* FUNCTION : ANALCODE,  PARSE BRANCH ADDRESS LABEL VALUE OUT      */
ANALCODE:PROCEDURE EXPOSE BN B.
ARG CUR                         /* CURRENT LINE OF SOURCE DATASET  */
FIR = SUBSTR(CUR,1,15)          /* FIRST PART OF LINE WITH OP CODE */
/* LOOK FOR INSTRUCTIONS WITH STUFF BEFORE THE LABEL */
DO I = 1 TO BN
   X = INDEX(FIR,B.CDE.I)                  /* LOOK FOR INSTRUCTION */
   IF X > 0 THEN                                       /* IF FOUND */
      DO
         RES = SUBSTR(CUR,X+LENGTH(B.CDE.I))       /* GET OPERANDS */
         DO J = 1 TO B.OFF.I                        /* SKIP COMMAS */
            Y = INDEX(RES,',')
            RES = SUBSTR(RES,Y+1)
         END
         RETURN SUBWORD(RES,1,1)             /* EXTRACT FIRST WORD */
      END
END
/* LOOK FOR ALL OTHER POSSIBLE BRANCHES */
X = INDEX(FIR,' B')                             /* LOOK FOR BRANCH */
IF X > 0 THEN                                          /* IF FOUND */
   DO
      RES = SUBSTR(CUR,X+2)                       /* GET REMAINDER */
      Y   = INDEX(RES,' ')                        /* END OF B OPER */
      RES = SUBSTR(RES,Y+1)                       /* GET OPERANDS  */
      RETURN SUBWORD(RES,1,1)                /* EXTRACT FIRST WORD */
   END
RETURN ' '                                  /* RETURN LABEL/BLANK  */
/* SUBROUTINE : ERR, PRINT ERROR MESSAGE */
ERR:PROCEDURE
ARG MSG; SAY MSG; EXIT 8


                                          © Reserved 1990

