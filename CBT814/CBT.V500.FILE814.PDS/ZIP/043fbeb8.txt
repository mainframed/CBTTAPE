RACF ICHPWX01 exit routine

The RACF product provides a variety of user interfaces to
allow users to develop and enhance the base product by using
exit routines.  Although we attempt the minimum amount of
customization at our site we have been forced to write a variety
of exits.  The exit presented here is an example of a simple
piece of code that can be easily maintained.

When we installed RACF it was decided that the interval for
the expiry of passwords would be 36 days.  This value was
chosen to comply with company policy.  After setting each
user profile we found that some users were changing their
password change interval with a RACF command.  To
overcome this problem we decided to use the RACF exit
presented here.

The exit routine will only allow users who have security
administration authorization to issue the command in question.
Full details about the exit routine and the parameters passed to
it can be obtained from IBM's manual RACF Systems
Programming Library.  The comments contained in the exit
routine will guide you through the processing performed by the
Assembler code.

         TITLE     'ICHPWX01 --- RACF PASSWORD EXIT.'
ICHPWX01 CSECT
ICHPWX01 AMODE     ANY
ICHPWX01 RMODE     ANY
         USING     *,R15
         B         SKP
         DC        XL1'08'
         DC        C'ICHPWX01'
*        *************************************************************
*        * INVOKED AFTER RACINIT SVC,ALTUSER OR PASSWORD COMMAND     *
*        * ISSUED.THE EXIT CHECKS TO SEE IF THE COMMAND INCLUDED     *
*        * A CHANGE IN THE INTERVAL FOR PASSWORD EXPIRY.IF IT DOES A *
*        * TABLE IS CHECKED TO MAKE SURE THAT THE USER ISSUING THE   *
*        * REQUEST IS AUTHORIZED,IF NOT THE REQUEST IS REJECTED.IF   *
*        * THE USER IS IN THE TABLE THEN THE REQUEST IS ACCEPTED.    *
*        * USERS IN THE TABLE AT PRESENT ARE ALL TECHNICAL SUPPORT   *
*        * PERSONNEL.                                                *
*        * RACF:SPL,TSO GUIDE TO WRITING A TMP OR COMMAND PROCESSOR, *
*        * SYSTEM MACROS AND FACILITIES VOLUME 2,TSO,PRINCIPLES OF   *
*        * OPERATION,IBM PINK CARD.                                  *
*        *************************************************************
SKP      DS        0H
         STM       R14,R12,12(R13)     /* STORE REGISTERS. */
         DROP      R15                 /* DROP R15 AS BASE. */
         BALR      R12,R0              /* NORMAL ENTRY. */
         USING     *,R12               /* SETUP R12 AS BASE. */
PWRD0010 EQU       *
         LR        R4,R1               /* LOAD POINTER TO PWXP. */
         CLC       16(4,R4),=X'00000000' /* IS INTERVAL ADDRESS 0. */
         BE        RETURN              /* YES,DO NO MORE. */
PWRD0020 EQU       *
         SR        R5,R5               /* ZEROIZE R5. */
         LA        R6,ENDWORK          /* LOAD WORKAREA LENGTH. */
         GETMAIN   RC,LV=(R6)          /* GET STORAGE. */
         LTR       R15,R15             /* STORAGE AVAILABLE? */
         BNZ       RETURNB             /* NO,GET OUT. */
         LR        R5,R1               /* ELSE LOAD STORAGE ADDRESS. */
         USING     WORKAREA,R5         /* ESTABLISH R5 AS BASE. */
PWRD0030 EQU       *
         LR        R11,R13             /* SAVE SAVEAREA ADDRESS. */
         LA        R13,RSAVE           /* LOAD MY SAVEAREA ADDRESS. */
         ST        R11,4(,R13)         /* STORE ORIGINAL SAREA ADDR. */
         ST        R13,8(,R11)         /* STORE MY SAVEAREA ADDRESS. */
PWRD0040 EQU       *
         LA        R8,USRTBL           /* POINT TO START USER TABLE. */
         L         R9,20(R4)           /* POINT TO USERID. */
PWRD0050 EQU       *
         CLC       0(8,R8),1(R9)       /* USER ID IN TABLE? */
         BE        PWRD0070            /* YES,DO NECESSARY. */
         CLC       0(8,R8),=C'@@@@@@@@' /* END OF TABLE. */
         BE        PWRD0060            /* YES,DO NECESSARY. */
         LA        R8,8(0,R8)          /* POINT TO NEXT ID. */
         B         PWRD0050            /* BRANCH BACK. */
PWRD0060 EQU       *
         LA        R7,8                /* INVALID USER RC=8. */
         B         PWRD0080            /* GO FREE STORAGE. */
PWRD0070 EQU       *
         LA        R7,0                /* VALID USER RC=0. */
PWRD0080 EQU       *
         L         R13,4(R13)          /* LOAD ORIGINAL SAVEAREA. */
         LA        R0,ENDWORK          /* LOAD LENGTH OF WORKAREA. */
         FREEMAIN  RC,LV=(R0),A=(R5)   /* FREE STORAGE. */
         L         R14,12(R13)         /* LOAD RETURN ADDRESS. */
         LR        R15,R7              /* LOAD RETURN CODE. */
         LM        R0,R12,20(R13)      /* RELOAD REGISTERS. */
PWRDEND  EQU       *
         BR        R14                 /* RETURN TO CALLER. */
RETURN   EQU       *
         L         R14,12(R13)         /* RELOAD RETURN ADDRESS. */
         LA        R15,0               /* RETURN CODE = 0. */
         LM        R0,R12,20(R13)      /* RELOAD REGISTERS. */
         BR        R14                 /* RETURN TO CALLER.  */
RETURNB  EQU       *
         LM        R14,R12,12(R13)     /* RELOAD REGISTERS. */
         LA        R15,0               /* RETURN CODE = 0. */
         BR        R14                 /* RETURN TO CALLER. */
USERTBL  DS        0H
         DC        CL8'SYSTJXB '       /* USERID TABLE. */
         DC        CL8'SYSTIXM '
         DC        CL8'SYSTKSL '
         DC        CL8'SYSTEDC '
         DC        CL8'@@@@@@@@'       /* END OF TABLE. */
         LTORG
WORKAREA EQU       *
RSAVE    DS        18F
ENDWORK  EQU       *-WORKAREA
         END


John Bradley
Technical Support Engineer (UK)                                    c Xephon 1991

