A simple conditional step execution program


INTRODUCTION

Depending on the parameter it is passed, the following
program, which is called TODAY, sets a return code to denote
the day of the week, the day of the month, the month of the
year, or the Julian date. It was initially developed for the
conditional execution of certain steps within a job stream on
specific days and/or dates. The following examples will
illustrate its usage.

Example 1

Only execute the WEEKLY step on Sunday.

      .
      //DOW     EXEC PGM=TODAY,PARM='19DOW'
      .
      //WEEKLY  EXEC PGM=xxxxxxxx,COND=(1,DOW)

Example 2

Execute the MONTHLY step on the first day of each month.

      .
      //DOM     EXEC PGM=TODAY,PARM='19DOM'
      .
      //MONTHLY EXEC PGM=xxxxxxxx,COND=(1,DOM)

Example 3

Execute the XMAS step on December 25 of each year.

      .
      //MOY     EXEC PGM=TODAY,PARM='19MOY'
      //DOM     EXEC PGM=TODAY,PARM='19DOM'
      .
      //XMAS    EXEC PGM=xxxxxxxx,COND=((12,MOY),(25,DOM))

Example 4

Execute the PERIOD step every 91 days.

      .
      //JJJ     EXEC PGM=TODAY,PARM='19JJJ'
      .
      //PERIOD  EXEC PGM=xxxxxxxx,COND=((91,JJJ),(182,JJJ),
      //                              (273,JJJ),(364,JJJ))


SOURCE CODE FOR TODAY

 IDENTIFICATION DIVISION.
 PROGRAM-ID. TODAY.
 REMARKS. EXEX PGM=TODAY,PARM='19XXX'
  '19'  - CENTURY IS PASSED IN FOR CALCULATION.
  'XXX' - DOW PASSED IN?
             SET RETURN-CODE TO DAY-OF-WEEK, IE:
             1=SUN,2=MON,3=TUE,4=WED,5=THU,6=FRI,7=SAT
          DOM PASSED IN?
             SET RETURN-CODE TO DAY-OF-MONTH
          MOY PASSED IN?
             SET RETURN-CODE TO MONTH-OF-YEAR
          JJJ PASSED IN?
             SET RETURN-CODE TO JULIAN.
 ENVIRONMENT DIVISION.
 CONFIGURATION SECTION.
 SOURCE-COMPUTER. IBM.
 OBJECT-COMPUTER. IBM.
 INPUT-OUTPUT SECTION.
 FILE-CONTROL.
 DATA DIVISION.
 FILE SECTION.
 WORKING-STORAGE SECTION.
 77  RMAINDER                  PIC 9(8).
 01  YYMMDD.
      03  YY                   PIC 9(2).
      03  MM                   PIC 9(2).
      03  DD                   PIC 9(2).
 01  DAYS-SINCE-YEAR-0000      PIC S9(8) COMP-3 VALUE +0.
 01  LEAP-WORK                 PIC 9(8)V99.
 01  DATE-WORK.
      03  YEARS.
         05  LEAP              PIC S9(8) COMP-3.
         05  CENTURY           PIC 9(2).
         05  JULIAN.
             10 YY             PIC 9(2).
             10 JJJ            PIC 9(3).
      03  THIS-CENTURY REDEFINES YEARS.
         05  LEAPS             PIC S9(8) COMP-3.
         05  CENTURY-YEAR      PIC 9(4).
         05  CENTURY-JULIAN    PIC 9(3).
 01  DAYS-IN-MONTH.
     03  JAN-THRU-JUN         PIC 9(12)
         VALUE 312831303130.
     03  JUL-THRU-DEC         PIC 9(12)
         VALUE 313130313031.
 01  MONTHS REDEFINES DAYS-IN-MONTH.
     03  MONTH OCCURS 12 TIMES
         INDEXED BY MX         PIC 9(2).
 LINKAGE SECTION.
 01  PASS-DATA.
     03  FILLER                PIC S9(4) COMP.
     03  PASS-CENTURY          PIC 9(2).
     03  PASS-OPTION           PIC X(3).
 PROCEDURE DIVISION USING PASS-DATA.
 0000-START.
     MOVE PASS-CENTURY TO CENTURY.
     ACCEPT YYMMDD FROM DATE.
     ACCEPT JULIAN FROM DAY.
     MOVE 28 TO MONTH (2).
     IF PASS-OPTION = 'DOW'
        PERFORM 0900-CHECK-LEAP-YEAR
        PERFORM 0950-CALCULATE-DOW
     ELSE
        IF PASS-OPTION = 'DOM'
           MOVE DD TO RETURN-CODE
        ELSE
           IF PASS-OPTION = 'MOY'
              MOVE MM TO RETURN-CODE
           ELSE
              IF PASS-OPTION = 'JJJ'
                 MOVE JJJ TO RETURN-CODE
              ELSE
              DISPLAY 'INVALID OPTION PASSED, DOM OR DOW OR JJJ'.
     STOP RUN.
 0900-CHECK-LEAP-YEAR.
*    IF DATE IS IN A LEAP YEAR, ADJUST FOR LEAP
     DIVIDE CENTURY-YEAR BY +4
        GIVING LEAP REMAINDER RMAINDER.
     IF RMAINDER = 0
        MOVE 29 TO MONTH (2)
        SUBTRACT +1 FROM LEAP.
 0950-CALCULATE-DOW.
*    CALCULATE THE TRUE DAY-OF-WEEK
     MULTIPLY CENTURY-YEAR BY +365 GIVING DAYS-SINCE-YEAR-0000.
     SUBTRACT +365 FROM DAYS-SINCE-YEAR-0000.
     ADD LEAP TO DAYS-SINCE-YEAR-0000.
     ADD CENTURY-JULIAN TO DAYS-SINCE-YEAR-0000.
     DIVIDE DAYS-SINCE-YEAR-0000 BY +7 GIVING LEAP
        REMAINDER RMAINDER.
     IF RMAINDER = 0
        MOVE 7 TO RETURN-CODE
     ELSE
        MOVE RMAINDER TO RETURN-CODE.


Ray Smith
EDP Technical Support (USA)                               c Xephon 1992

