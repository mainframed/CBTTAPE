A PUT/GET utility for ISPF


PUT source code

/*  REXX * MVS - ISPF EDITOR *=====================================*/
/*  PUT for ISPF editor , in XEDIT style                           */
/*  Format:                                                        */
/*   PUT < first_line last_line > |  < membername >                */
/*       < number_of_lines >      |  < pds(member) >               */
/*                                |  < sequential file >           */
/*  Put without arguments assumes only the current_line            */
/*  Put without destination file creates a temporary file named    */
/*  userid.PUT,  with the specified lines.                         */
/*  This macro can be issued from pds members or sequential files. */
/*=================================================================*/
 address ISPEXEC
'ISREDIT MACRO (ARG)'                    /* get arguments          */
'ISREDIT (PDS) = DATASET'
'ISREDIT (MEM) = MEMBER'
'ISREDIT (LASTLINE) = LINENUM .ZLAST'    /* number of lines on file*/
'ISREDIT (LINE1, XXXXX) = DISPLAY_LINES' /* lines on the screen    */
 if rc=4 then line1 = lastline+1         /* rc=4 no display lines  */
 upper arg                               /* uppercase arguments    */
 arg1 = word(arg,1)                      /* separate  arguments    */
 arg2 = word(arg,2)
 arg3 = word(arg,3)
 temp_default = "'"userid()".PUT'"       /* default temporary file */
 lastarg = ""
 if arg1^="" then lastarg = word(arg,words(arg))
/*============= find out which arguments we have ==================*/
 select
    when arg1="?"|arg1="H"|arg1="HELP" then signal helpe
    when arg1 = ""  then line2=line1 /* no arg, put one line only  */
    when arg1 = "*" then do          /* put all lines from the     */
       line2 = lastline              /* current line               */
    end
    when datatype(arg1,"W") & datatype(arg2,"W") then do
       line1 = arg1                  /* first line and last line   */
       line2 = arg2
    end
    when datatype(arg1,"W") & arg2="*" then do
       line1 = arg1                  /* from arg1 to last line     */
       line2 = lastline
    end
    when datatype(arg1,"W") & ^datatype(arg2,"W") then do
       line2 = line1 + arg1 - 1      /* number of lines from       */
    end                              /* current line               */
    otherwise line2 = line1
 end
 if line2 > lastline then line2 = lastline
 if line1 > line2 then do
    zedlmsg = "==>>>> Line number arguments are invalid"
    address ISPEXEC 'SETMSG MSG(ISRZ001)'
    exit 99
 end
/*=========== copy lines to put into a REXX stem ==================*/
 zz = msg(off)
 x = 0
 do k = line1 to line2
  'ISREDIT (LIN) = line' k
   x = x + 1
   linha.x = lin
   linha.0 = x
 end
/*===== find out if outfile specified, or use default  ============*/
address TSO
 select                                   /* lastarg is not a file */
  when lastarg = "" then,                 /* use default           */
       outfile = temp_default
  when lastarg = "*" then,                /* use default           */
       outfile = temp_default
  when pos(".",lastarg)>0 then,           /* lastarg is a PS or a  */
       outfile = lastarg                  /* full PDS name         */
  when pos("(",lastarg)=0 &,
       pos(".",lastarg)=0 &,
       ^(datatype(lastarg,"W")) then,     /* just a member name,   */
       outfile = pds"("lastarg")"         /* assume current PDS    */
  otherwise,
       outfile = temp_default
 end
if pos("(",outfile) > 0 &,                /* if user did not supply*/
   right(outfile,1)^=")" then,            /* ending ")", put it    */
   outfile=outfile||")"
/*===== find out if outfile exists and what kind it is ============*/
parse var outfile pds1 "(" member1 ")"
answer = sysdsn(outfile)
if outfile = temp_default then do
   if answer = "OK" then aloc_type = 1
                    else aloc_type = 2
end
else do
   select
    when answer="OK" & member1^=""  then aloc_type = 3
    when answer="OK" & member1 =""  then aloc_type = 4
    when answer="MEMBER NOT FOUND"  then aloc_type = 5
    when answer="DATASET NOT FOUND" then aloc_type = 6
    otherwise do
       zedlmsg = "==>>>> " answer
       address ISPEXEC 'SETMSG MSG(ISRZ001)'
       exit 99
    end
   end
end
/*========== allocate outfile dataset and write to it =============*/
select
 when aloc_type = 1 then do
    "alloc da("outfile") dd(temp1) shr"
    if rc^=0 then signal erro
    call escrita
 end
 when aloc_type = 2 then do
    "alloc da("outfile") dd(temp1) new catalog ,
       dsorg(ps) blksize(6000) recfm(v,b) ,
       lrecl(150) tracks space(10 10)"
    if rc^=0 then signal erro
    call escrita
 end
 when aloc_type = 3 then do
    "alloc da("outfile") dd(temp1) shr"
    if rc^=0 then signal erro
    call leitura
    call escrita
 end
 when aloc_type = 4 then do
    xx = listdsi(outfile)
    if left(sysdsorg,2)^="PS" then do
       zedlmsg = "==>>>> File "outfile "is not sequential"
       address ISPEXEC 'SETMSG MSG(ISRZ001)'
       exit 99
    end
    "alloc da("outfile") dd(temp1) mod"
    if rc^=0 then signal erro
    call escrita
 end
 when aloc_type = 5 then do
    "alloc da("outfile") dd(temp1) mod"
    if rc^=0 then signal erro
    call escrita
    zedlmsg = "==>>>> New member "outfile "created"
    address ISPEXEC 'SETMSG MSG(ISRZ001)'
 end
 when aloc_type = 6 then do
    xx = listdsi(pds)
    if rc^=0 then signal erro
    if pos("(",outfile)>1 then dir = "dir(10)"
                          else dir = ""
    sysrecfm = insert(",",sysrecfm,1)
    "alloc da("outfile") dd(temp1) new catalog ,
       dsorg(ps) blksize("sysblksize") lrecl("syslrecl"),
       recfm("sysrecfm") tracks space(2 2)" dir
    if rc^=0 then signal erro
    call escrita
    zedlmsg = "==>>>> New file "outfile "has been allocated"
    address ISPEXEC 'SETMSG MSG(ISRZ001)'
 end
end
/*=================================================================*/
saida:
"free da("outfile")"
"free dd(temp1)"
exit
/*=================================================================*/
leitura:
 execio "*" diskr temp1 "(stem table. finis "
 execio table.0 diskw temp1 "(stem table."
return
/*=================================================================*/
escrita:
 execio linha.0 diskw temp1 "(stem linha. finis"
return
/*=================================================================*/
erro:
 zedlmsg = "==>>>> Error "rc" allocating "outfile
 address ISPEXEC 'SETMSG MSG(ISRZ001)'
exit 99
/*=================================================================*/
helpe:
say "Format:  PUT  <line_args>  <file_args>                         "
say
say "line_args can be:                                              "
say " (none)    No arguments, just one line is PUT.                 "
say " *         From the current line to the end of file.           "
say " nn        nn lines from the current line.                     "
say " ini *     From line ini to the end of file.                   "
say " ini end   From ini to end (absolute line numbers).            "
say
say "file_args can be:                                              "
say " (none)    Assumes temporary default file.                     "
say " member    A member of the current PDS (just the member name). "
say "           If the member already exists, data will be appended."
say "           If it does not exist, it will be created.           "
say " pds(mem)  A member of another PDS (the full name).            "
say "           If you specify a PDS that does not exist, it will   "
say "           be allocated and the member created.                "
say " seq       A sequential file. If the file already exists, data "
say "           will be appended to the end of it.                  "
say "           If it does not exist, it will be allocated.         "
exit


GET source code

/*  REXX * MVS - ISPF EDITOR *=====================================*/
/*  GET for ISPF editor , in XEDIT style                           */
/*  Format:                                                        */
/*   GET < sequential_file >  |  < firstline  lastline >           */
/*       < pds(member) >      |  < number_of_lines >               */
/*       < membername >       |                                    */
/*  This macro does not work while editing sequential files,       */
/*   it only works with PDS members.                               */
/*=================================================================*/
 address ISPEXEC
'ISREDIT MACRO (ARG)'
'ISREDIT (PDS) = DATASET'              /* PDS = current dataset    */
'ISREDIT (MEM) = MEMBER'               /* find out if this is a    */
                                       /* member                   */
 address TSO
 xx = msg(off)
 mem = space(mem,0)
 if mem = "" then do
    zedlmsg="==>>>> GET cannot be used in sequential files"
    address ISPEXEC 'SETMSG MSG(ISRZ001)'
    exit
 end
 temp_default = userid()".PUT"
 temp_member  = space(left(userid(),7)||"9",0)
 upper arg                             /* upper case arguments     */
 arg1 = word(arg,1)                    /* separate arguments       */
 arg2 = word(arg,2)
 arg3 = word(arg,3)
 select
    when arg1="?"|arg1="H"|arg1="HELP" then signal helpe
    when arg1 = "" then inf = temp_default
    when pos("(",arg1) ^= 0 then do
      if right(arg1,1)^=")" then inf=arg1||")"
                            else inf=arg1
    end
    when pos("(",arg1)=0 & pos(".",arg1)>0 then inf=arg1
    when pos("(",arg1)=0 & pos(".",arg1)=0 &,
       length(arg1)<9 then inf = pds"("arg1")"
    otherwise nop
 end
 if sysdsn("'"inf"'")^="OK" then do
    zedlmsg="==>>>> File "inf" not found"
    address ISPEXEC 'SETMSG MSG(ISRZ001)'
    exit 99
 end
 xx = listdsi(inf)
 if pos("(",inf) = 0 & left(sysdsorg,2)="PO" then do
    zedlmsg="==>>>> "inf" is a PDS, please specify member"
    address ISPEXEC 'SETMSG MSG(ISRZ001)'
    exit 99
 end
                                       /* allocate infile          */
 "alloc da('"inf"')   dd(ddinf) shr reuse"
  if rc^=0 then do
     zedlmsg="==>>>> Error allocating "inf
     address ISPEXEC 'SETMSG MSG(ISRZ001)'
     signal saida 99
  end
                                       /* allocate temp PDS member */
 outf = pds||"("temp_member")"
 "alloc da('"outf"') dd(ddout) shr reuse"
  if rc^=0 then do
     zedlmsg="==>>>> Error allocating "outf
     address ISPEXEC 'SETMSG MSG(ISRZ001)'
     signal saida 99
  end
 select                                /* find out how many lines  */
    when datatype(arg2,"W") &,         /* are to be copied         */
     datatype(arg3,"W") then do
       line1 = arg2
       line2 = arg3
       if arg2 > arg3 then do
          zedlmsg= "==>>>> Line number arguments are invalid"
          address ISPEXEC 'SETMSG MSG(ISRZ001)'
          signal saida 99
       end
    end
    when datatype(arg2,"W") &,
     ^datatype(arg3,"W") then do
       line1 = 1
       line2 = arg2
    end
    otherwise do
       line1 = 1
       line2 = 99999
    end
 end
 do line = line1 to line2              /* copy to temporary member */
    execio 1 diskr ddinf line
    if rc^=0 then leave
    execio 1 diskw ddout
 end
 "execio 0 diskw " ddout "(finis"
 "execio 0 diskr " ddinf "(finis"
 address ISPEXEC
 'ISREDIT RESET'
 'ISREDIT (L1, L2) = DISPLAY_LINES'    /* RC=4: No display lines,  */
  if rc = 4 then L1 = ".ZLAST"         /* assume end-of-file       */
  if rc = 0 then L1 = L1 - 1
  if rc = 8 then L1 = 0                /* RC=8: No existing data   */
                                       /* lines (empty member)     */
 'ISREDIT COPY' temp_member 'AFTER' L1 /* copy from temp member    */
saida:
 arg rc1
 if rc1="" then rc1 = 0
 address TSO
 "free da('"outf"')"
 "free da('"inf"')"
 "free dd(ddinf)"
 "free dd(ddout)"
 "delete '"outf"'"
exit rc1
/*===============================================================*/
helpe:
say "                                                             "
say "GET < sequential_file >  |  < firstline  lastline >          "
say "    < pds(member) >      |  < number_of_lines >              "
say "    < membername >       |                                   "
say "                                                             "
say " GET without arguments assumes the last PUT, and gets the    "
say " temporary file USERID().PUT.                                "
say "                                                             "
say " GET membername assumes a member from the current PDS.       "
say "                                                             "
say " firstline lastline   or   number_of_lines   are optional,   "
say " and determine which lines or how many lines you are getting."
say "                                                             "
say " The insertion is made before the top line of the screen     "
say " (the current_line).                                         "
say "                                                             "
say " GET does not work in sequential files, you can only issue   "
say " this macro while editing PDS members.                       "
exit


Lu“s Paulo Figueiredo Sousa Ribeiro
Systems Programmer
Edinfor (Portugal)               c Xephon 1996

