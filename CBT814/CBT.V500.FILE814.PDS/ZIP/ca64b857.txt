Swapping out a job waiting for a mount request

How do you swap out a batch job waiting for a mount request,
which is occupying an address space and consuming resources?
As our system is generally well loaded between 8.00 am and
5.00 pm, one domain has been reserved for production batch
jobs.  This domain has been defined with CNSTR equal to
(1,2), which means a maximum of two jobs can run together
within this domain.  Since our database has a time limit of 90
seconds between two commands, if a batch job working with
this database is swapped out for more than 90 seconds, it will
abend when it becomes swapped in.

When a job is waiting for a mount request, it becomes non-
swappable in its domain and another job is swapped out, with
the consequences described above.

To avoid this problem, the parameter JWT is set to 1 (so there
is a one minute wait before calling IEFUTL) and the exit
IEFUTL is modified as shown in the following listing.  The
exit IEFUTL must be in the LPALIB concatenation and must
be in an authorized state.  For started tasks, the parameter
TIME=1440 must be used in the EXEC statement (in this case,
time out and wait time hasn't any effect on this address space).

The SMF exit IEFUTL has the following defaults and actions:

                              Defaults                              Actions

TSO CPU time      1.30 minutes (JES2 parm)      30 second extension
Wait time      1 minute (SMFPARM/JWT)      24 minute extension
Job CPU time      as JES2 class definitions      1 minute extension
                                                                       before abend
Wait time      1 minute (SMFPARM/JWT)      job is swapped out

IEFUTL   CSECT
IEFUTL   AMODE 31
IEFUTL   RMODE 24
         SAVE  (14,12),,IEFUTL.IPO.&SYSTIME._&SYSDATE SAVE REGS
         LR    R12,R15                 LOAD REGISTER 12 FROM 15
         USING IEFUTL,R12              ESTABLISH ADDRESSABILITY TO
*                                      PROGRAM CSECT
         L     R11,ZERO(R1)            LOAD POINTER TO PARAMETER AREA
         USING SMFEPLA,R11             ESTABLISH ADDRESSABILITY TO SMF
*                                      PARAMETER AREA DSECT
         USING CVT,R4                  CVT ADDRESSABILITY
         USING ASCB,R5                 ASCB ADDRESSABILITY
         USING OUCB,R6                 OUCB ADDRESSABILITY
         USING ZUSER,R8                USER'S MEMORY ADDRESSABILITY
         C     R0,FULL4                SEE IF JOB/STEP TIME EXCEEDED
         BH    WAITTIME                NO.....GO DO ANALYSIS FOR WAIT
*                                      TIME EXCEEDED
         TM    SMFOPT,SMFTSO           SEE IF TSO SESSION
         BNO   TIMEJOB                 NO.....GO TO BATCH JOB
         CLI   SMFUSERC,ZERO           SEE IF CPU TIME HAS NOT YET BEEN
*                                      EXTENDED ONCE
         BNE   CANCEL                  NO....GO CANCEL TSO SESSION -
*                                      ALREADY EXTENDED ONCE
         TPUT  MESSAGEC,L'MESSAGEC     PUT OUT MESSAGE TO TERMINAL
         MVI   SMFUSERC,ONE            INDICATE ONE EXTENSION GRANTED
         L     R2,FULL8                INDICATE RETURN CODE OF 8 -
*                                      EXTEND CPU TIME IN SECONDS
         L     R1,FULL30               LOAD TIME EXTENSION IN SECONDS
*                                      INTO REGISTER 1
         B     RETURN                  GO RETURN TO CALLER
TIMEJOB  EQU *
         CLI   SMFUSERC,ZERO           SEE IF CPU TIME HAS NOT YET BEEN
*                                      EXTENDED ONCE
         BNE   CANCEL                  NO....GO CANCEL JOB BATCH   -
*                                      ALREADY EXTENDED ONCE
         MVI   SMFUSERC,ONE            INDICATE ONE EXTENSION GRANTED
         GETMAIN R,LV=WTOMA            GETMAIN AREA FOR WTO DATA
         LR    R7,R1                   SAVE ADDRESS OF GETMAINED AREA
         MVC   ZERO(WTOMA,R1),WTOA     MOVE WTOC CONSTANT INTO WORKAREA
         MVC   WTOMA-D59(L'SMFJOBN,R1),SMFJOBN MOVE JOBNAME INTO WTO
         SVC   WTOSVC                  ISSUE WTO SVC
         FREEMAIN R,LV=WTOMA,A=(R7)    FREEMAIN AREA FOR WTO DATA
         L     R2,FULL8                INDICATE RETURN CODE OF 8 -
*                                      EXTEND CPU TIME IN SECONDS
         L     R1,FULL60               LOAD TIME EXTENSION IN SECONDS
*                                      INTO REGISTER 1
         B     RETURN                  GO RETURN TO CALLER
*****    WAIT TIME EXCEEDED                                           *
WAITTIME EQU   *
         GETMAIN R,LV=ZUSERL           GETMAIN AREA FOR USER'S DATA
         LR    R8,R1                   SAVE ADDRESS OF GETMAINED AREA
         TM    SMFOPT,SMFTSO           SEE IF TSO SESSION
         BO    EXTTSO                  YES...GO TO EXTENSION TSO
         B     BATCH                   ELSE..GO TO BATCH
EXTTSO   EQU   *
         CLI   SMFUSERW,ZERO           SEE IF WAIT TIME HAS NOT YET
         BNE   CANCEL                  BEEN EXTENDED ONCE...YES..CANCEL
         MVI   SMFUSERW,ONE            INDICATE ONE EXTENSION GRANTED
         L     R2,FULL8                RETURN CODE OF 8
         L     R1,FULL1440             EXTEND WAIT TIME IN SECONDS
         B     RETURN                  GO RETURN TO CALLER
BATCH    EQU *
         L     R4,CVTPTR               LOAD POINTER TO CVT
         L     R5,CVTASCBH             LOAD POINTER TO THE FIRST ASCB
SEARCH   EQU *
         L     R6,ASCBJBNI             JOB NAME ADDRESS
         CLC   0(L'SMFJOBN,R6),SMFJOBN IF IT'S THE WAITING BATCH JOB
         BE    SWAPOUT                 YES....SWAPOUT THIS TASK
LOOP     MVC   FWORD,ASCBFWDP          NEXT ASCB
         L     R5,FWORD                IF THE NEXT ASCB IS LOW-VALUE
         LTR   R5,R5                   IT WAS THE LAST ASCB
         BNZ   SEARCH
*                                      NORMALLY, NEVER BY THIS POINT
*                                      IF TIME 1440 IS CODED FOR STC
         GETMAIN R,LV=WTOM             GETMAIN AREA FOR WTO DATA
         LR    R7,R1                   SAVE ADDRESS OF GETMAINED AREA
         MVC   ZERO(WTOM,R1),WTO       MOVE WTOC CONSTANT INTO WORKAREA
         SVC   WTOSVC                  ISSUE WTO SVC
         FREEMAIN R,LV=WTOM,A=(R7)     FREEMAIN AREA FOR WTO DATA
         B     CANCEL
SWAPOUT  EQU *
         GETMAIN R,LV=WTOMB            GETMAIN AREA FOR WTO DATA
         LR    R7,R1                   SAVE ADDRESS OF GETMAINED AREA
         MVC   ZERO(WTOMB,R1),WTOB     MOVE WTOC CONSTANT INTO WORKAREA
         MVC   WTOMB-D54(L'SMFJOBN,R1),SMFJOBN MOVE JOBNAME INTO WTO
         SVC   WTOSVC                  ISSUE WTO SVC
         FREEMAIN R,LV=WTOMB,A=(R7)    FREEMAIN AREA FOR WTO DATA
         L     R6,ASCBOUCB             LOAD POINTER TO OUCB
         MODESET KEY=ZERO,MODE=SUP     SET KEY ZERO MODE SUPERVISOR
         XC    OUCBSFL,OUCBSFL         TURN ON  SWAPPABLE
         L     R2,FULL8             RETURN CODE OF 8 WITHOUT EXTENSION
         B     RETURN                  GO RETURN TO CALLER
*****    ZERO REGISTER 2 TO INDICATE CANCEL                           *
CANCEL   DS    0H
         SLR   R2,R2   INDICATE RETURN CODE OF ZERO - CONTINUE (CANCEL)
*****    NORMAL END PROCESSING                                        *
RETURN   DS    0H
*        FREEMAIN R,LV=ZUSERL,A=(R8)   FREEMAIN AREA FOR USER'S DATA
         L     R14,D12(,R13)           LOAD ADDRESS FOR RETURN
         LR    R15,R2                  LOAD RETURN CODE FROM REGISTER 2
         LM    R2,R12,D28(R13)         RESTORE REGISTERS 2 TO 12
         BR    R14                     RETURN TO CALLER
FULL4    DC    F'4'
FULL8    DC    F'8'
FULL30   DC    F'30'
FULL60   DC    F'60'
FULL1440 DC    F'1440'
MESSAGEC DC    C'CPU TIME LIMIT HAS BEEN EXCEEDED. A SINGLE 30 SECONDS X
               EXTENSION WILL BE GRANTED. PLEASE LOGOFF SHORTLY'
WTO      WTO   'ERROR IN IEFUTL , PLEASE CALL THE SYSTEM PROGRAMMER',  X
               ROUTCDE=(2),DESC=(6),MF=L
WTOM     EQU   *-WTO
WTOA     WTO   'CPU  TIME LIMIT HAS BEEN EXCEEDED FOR THE JOB 12345678 X
               A SINGLE ONE MINUTE EXTENSION WILL BE GRANTED.',        X
               ROUTCDE=(2),DESC=(6),MF=L
WTOMA    EQU   *-WTOA
WTOB     WTO   'WAIT TIME LIMIT HAS BEEN EXCEEDED FOR THE JOB 12345678 X
               THIS JOB HAS BEEN SWAPPED OUT            ',             X
               ROUTCDE=(2),DESC=(6),MF=L
WTOMB    EQU   *-WTOB
ZUSER    DSECT
FWORD    DS F
ZUSERL   EQU   *-ZUSER
         DS    0F
PATCH    DC    8CL4'ZAP*'
SMFEPLA  DSECT
SMFJOBN  DS    CL8
SMFTIME  DS    CL4
SMFDATE  DS    CL4
SMFSID   DS    CL4
SMFUSID  DS    CL8
SMFSTEP  DS    CL1
SMFOPT   DS    CL1
SMFTSO   EQU   X'01'
SMFVS1   DS    CL1
SMFRESV  DS    CL1
SMFUSER  DS    CL4
         ORG   SMFUSER
SMFUSERC DS    CL1
SMFUSERW DS    CL1
         ORG
         CVT DSECT=YES
         IRAOUCB DSECT=YES
         IHAASCB DSECT=YES,LIST=YES
ZERO     EQU   0
ONE      EQU   1
D12      EQU   12
D28      EQU   28
D54      EQU   54
D59      EQU   59
WTOSVC   EQU   35
         END   IEFUTL


Christian Vandercamme
Systems Programmer (Belgium)                           © Xephon 1990

