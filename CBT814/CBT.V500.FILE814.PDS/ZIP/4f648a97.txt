Locating macro invocation in Assembler source

Sometimes it is useful to find all the occurrences of macro
invocations within an Assembler source program. There already
exist at least two possible ways to do this, both of which involve
assembling the program in question. The first is to use the
Stanford Linear Acclerator (SLAC) modification to IBM
Assembler H, which provides a macro cross reference. The
second is to move up to the IBM High-Level Assembler
(HLASM), which incorporates the SLAC modification as well as
a number of other useful enhancements. The advantage in
common to both of the above is that they report the dataset and
volume of the macro library in which the macro was found. The
disadvantages are maintaining SLAC modifications, spending
more money for HLASM, and the fact that assemblies consume a
good deal of resource, increasing with the size of the program
being assembled.

A while back, I decided to write a small program which would
scan Assembler source and report any character string which is
neither an Assembler directive nor a machine code mnemonic.
The result is a program, called MACROREF, which scans source
and reports both the names of macros invoked and the number of
times each is used. It was written to handle 'sloppy' coders: those
who do not begin their instructions exactly in column 10. It has
logic to scan each Assembler input line looking for lines which
may or may not have labels coded on them: it does, however,
assume labels will be simple. This means 1 to 8 characters in
length followed by a variable (but not infinite) number of blanks,
which would be followed by an instruction/macro
invocation/assembler directive. The program builds a table of all
character strings thought to be macro invocations by comparing
the strings against an internal table of Assembler directives and
mnemonics. If there is no match, the string is added to the macro
table. When all lines have been scanned, a bubble sort is
performed to sort the names of the macros into alphanumeric
order. The one bug I have occasionally run into arises when
instructions within in-line macros have been coded with
substitutable parameters, for example:

              L&X   R1,&PARM

assuming that when &X is null, &PARM would have the value of
a label name and when &X equals R, &PARM would have the
value of a register number or equate (L   R1,LABEL or LR
R1,R4, respectively after substitution). The MACROREF
program would quite incorrectly report that L&X was a macro
invocation. Other than this, the program has been extremely
reliable for a number of years.

The program can be invoked by the following CLIST under TSO
or ISPF:

PROC 1 DATASET LIST SYMLIST CONLIST
CONTROL NOFLUSH
IF &SYSISPF = ACTIVE THEN ISPEXEC CONTROL DISPLAY LINE START(1)
IF &SYMLIST ^= THEN CONTROL SYM
IF &CONLIST ^= THEN CONTROL CON
IF &LIST    ^= THEN CONTROL LIST
ISPEXEC CONTROL DISPLAY LINE START(1)
IF &LIST = LIST THEN CONTROL LIST
ALLOC DD(IN) DA(&DATASET) SHR REUSE
ALLOC DD(MACOUT) DA(*) SHR REUSE
CALL 'loadlib(MACROREF)'           /* update to correct name */
FREE DD(IN MACOUT)

Running the CLIST against the MACROREF source code itself
yields the following results:

 ABEND              1
 CLOSE              1
 DCB                2
 GET                1
 OPCODE           342
 OPEN               1
 PUT                1
 RETURN             1
 SAVE               1
 YREGS              1

MACROREF SOURCE CODE

         MACRO
&LABEL   OPCODE &OPC
         LCLA  &COUNT
&COUNT   SETA  K'&OPC-2
         AIF   (&COUNT LE 8).CHK1
         MNOTE 8,'OPCODE &OPC IS LONGER THAN 8 CHARACTERS'
         MEXIT
.CHK1    AIF   (T'&OPC NE 'O').GEN
&LABEL   DC    AL1(0)                  END OF TABLE INDICATOR
         AGO   .DONE
.GEN     ANOP
&LABEL   DC    AL1(&COUNT),CL9&OPC.    LENGTH FOR EXECUTE CLC
.DONE    MEND
MACROREF CSECT                    ESTABLISH CSECT
MACROREF AMODE 24
MACROREF RMODE 24
         SAVE  (14,12),,MACROREF-&SYSDATE
         YREGS                    REGISTER EQUATES
         LR    R12,R15            LOAD R12 W/EPA
         USING MACROREF,R12
         LA    R8,SAVEAREA        LOAD ADDR OF MY S/A
         ST    R8,8(,R13)         ST MY S/A ADDR IN CALLER'S S/A
         ST    R13,4(,R8)         ST CALLER'S S/A ADDR IN MY S/A
         LR    R13,R8             LOAD ADDR OF MY S/A IN R13
         OPEN  (IN,,MACOUT,OUTPUT) OPEN FILES
GETRECRD GET   IN                 READ A RECORD
         CLI   0(R1),C'*'         IS IT AN OPEN COMMENT
         BE    GETRECRD           YES, GO GET NEXT RECORD
         CLC   0(2,R1),=C'.*'     IS IT A MACRO COMMENT
         BE    GETRECRD           YES, GO GET NEXT RECORD
         LR    R4,R1              SAVE A(RECORD)
         LA    R2,10              LOAD LOOP COUNTER
INLOOP   CLI   0(R4),C' '         IS IT BLANK
         BE    FINDNB             YES, GO FIND NON-BLANK
         LA    R4,1(,R4)          ELSE, BUMP TO NEXT BYTE
         BCT   R2,INLOOP          LOOP TO CHECK AGAIN
         B     GETRECRD           NOTHING FOUND, GO GET NEXT RECORD
FINDNB   LA    R2,10              LOAD LOOP COUNTER
NBLOOP   CLI   0(R4),C' '         IS IT BLANK
         BNE   HAVEIT             NO,  MUST HAVE MACRO OR INST
         LA    R4,1(,R4)          ELSE, BUMP TO NEXT BYTE
         BCT   R2,NBLOOP          LOOP TO CHECK AGAIN
         B     GETRECRD           NOTHING FOUND, GO GET NEXT RECORD
HAVEIT   LM    R5,R7,MTABPARM     LOAD MNEMONICS TABLE PARMS
LOOP     SR    R8,R8              CLEAR REGISTER FOR IC
         IC    R8,0(R5)           GET LENGTH OF OPERATION CODE
         EX    R8,INSTRCLC        IS THERE A TABLE MATCH
         BE    GETRECRD           YES, GO GET NEXT RECORD
         BXLE  R5,R6,LOOP         ELSE, BUMP AND LOOP
         LA    R3,MACRONAM        ELSE, LOAD A(MACRO TABLE ENTRY)
         XC    MACRONAM,MACRONAM  CLEAR MACRO TABLE ENTRY
         LA    R2,L'MACRONAM      LOAD MAX SIZE OF MACRO NAME
MVLOOP   MVC   0(1,R3),0(R4)      MOVE MACRO NAME TO MACRO TABLE
         CLI   1(R4),C' '         END OF MACRO
         BE    UPMTAB             YES, GO UPDATE TABLE
         LA    R3,1(,R3)          ELSE, BUMP BY 1
         LA    R4,1(,R4)          BUMP BY 1
         BCT   R2,MVLOOP          LOOP
UPMTAB   LM    R5,R7,MACTLM       LOAD MACRO TABLE PARMS
         USING MACTB,R5
MLOOP    CLI   TBMAC,X'00'        IS IT A NULL ENTRY
         BE    MVMAC              YES, GO USE IT
         CLC   TBMAC,MACRONAM     IS IT ALREADY IN TABLE
         BE    INTAB              YES, GO UPDATE USE COUNT
         BXLE  R5,R6,MLOOP        ELSE, LOOP
         ABEND 2                  ELSE, TABLE OVERFLOW
MVMAC    MVC   TBMAC,MACRONAM     MOVE ENTRY TO TABLE
INTAB    LH    R1,MCNT            LOAD CURRENT COUNT
         LA    R1,1(,R1)          BUMP COUNT BY 1
         STH   R1,MCNT            STORE NEW CURRENT COUNT
         C     R5,MACEND          CHECK FOR NEW END-OF-TABLE
         BNH   GETRECRD           IF NOT HIGH, SKIP STORE
         ST    R5,MACEND          ELSE SAVE A(CURRENT LAST ENTRY)
         B     GETRECRD           GO GET NEXT RECORD
EOF      L     R0,MACTLM+4        LOAD LENGTH(MACRO TABLE ENTRY)
         L     R1,MACEND          LOAD A(LAST MACRO TABLE ENTRY)
SORTLOOP SR    R14,R14            CLEAR A(LAST SWITCH)
         L     R15,MACTLM+0       LOAD A(FIRST MACRO TABLE ENTRY)
         SR    R1,R0              POINT TO PENULTIMATE
         CR    R15,R1             TEST AGAINST FIRST ENTRY
         BH    SORTDONE           IF HIGH, SORT DONE
SORTCMPR CLC   0(8,R15),10(R15)   COMPARE KEYS
         BNH   SORTBUMP           IF NOT HIGH, GO POINT TO NEXT
         XC    0(10,R15),10(R15)  SWAP ENTRIES
         XC    10(10,R15),0(R15)  SWAP ENTRIES
         XC    0(10,R15),10(R15)  SWAP ENTRIES
         LR    R14,R15            SAVE A(SWITCHED ENTRY)
SORTBUMP BXLE  R15,R0,SORTCMPR    POINT TO NEXT AND GO COMPARE
         LTR   R1,R14             TEST LAST SWITCHED ADDRESS
         BNZ   SORTLOOP           NOT DONE, GO LOOP AGAIN
SORTDONE LM    R5,R7,MACTLM       LOAD MNEMONICS TABLE PARMS
PLOOP    CLI   TBMAC,X'00'        ARE WE AT END OF TABLE
         BE    CLOSE              YES, GO CLOSE FILES
         LH    R2,MCNT            LOAD COUNT
         CVD   R2,DBLWORD         CONVERT TO DECIMAL
         MVC   MACRONAM,TBMAC     MOVE TABLE ENTRY TO OUTPUT LINE
         MVC   MACROCNT(6),=X'402020202021' MOVE EDIT PATTERN
         ED    MACROCNT(6),DBLWORD+5 EDIT VALUE
PUT      PUT   MACOUT,OPLINE      WRITE OUTPUT LINE
         BXLE  R5,R6,PLOOP        LOOP
CLOSE    CLOSE (IN,,MACOUT)       ELSE CLOSE FILES
         L     R13,SAVEAREA+4     RELOAD R13 W/ADDR OF CALLERS S/A
         RETURN (14,12),RC=0      RETURN TO OS WITH RETCODE=0
SAVEAREA DC    18F'0'             STANDARD SAVEAREA
IN       DCB   DSORG=PS,MACRF=GL,DDNAME=IN,EODAD=EOF
MACOUT   DCB   DSORG=PS,MACRF=PM,DDNAME=MACOUT,LRECL=80,RECFM=FB
DBLWORD  DS    D                  AREA FOR CVD
MACEND   DS    A(MACTSTRT)        A(UPDATED MACRO TABLE END)
MTABPARM DC    A(MTABSTRT)        A(MNEMONICS TABLE)-----------------|
         DC    A(MTABELEN)        LENGTH(MNEMONCIS TABLE ENTRY)      |
         DC    A(MTABEND-MTABELEN) A(LAST MNEMONICS TABLE ENTRY)-----|
MACTLM   DC    A(MACTSTRT)        A(MACRO TABLE START)------------|
         DC    A(L'MACTSTRT)      LENGTH(MACRO TABLE ENTRY)       |
         DC    A(MACTEND-L'MACTSTRT) A(LAST MACRO TABLE ENTRY)----|
BLANKS   DC    CL8' '
OPLINE   DC    CL80' '            OUTPUT LINE-----------|
         ORG   OPLINE+1                                 |
MACRONAM DC    CL8' '             MACRO NAME            |
         DC    CL6' '             FILLER                |
MACROCNT DC    CL6' '             MACRO COUNT           |
         ORG   ,                  ----------------------|
         LTORG ,                  GENERATE LITERALS
INSTRCLC CLC   0(0,R4),1(R5)      EXECUTED INSTRUCTION
MTABSTRT OPCODE 'ACTR'            MNEMONICS TABLE-------------|
MTABELEN EQU     *-MTABSTRT       ENTRY LENGTH                |
         OPCODE 'AGO'                                         |
         OPCODE 'AIF'                                         |
         OPCODE 'AMODE'                                       |
         OPCODE 'ANOP'                                        |
         OPCODE 'CNOP'                                        |
         OPCODE 'COM'                                         |
         OPCODE 'COPY'                                        |
         OPCODE 'CSECT'                                       |
         OPCODE 'CXD'                                         |
         OPCODE 'DC'                                          |
         OPCODE 'DROP'                                        |
         OPCODE 'DS'                                          |
         OPCODE 'DSECT'                                       |
         OPCODE 'DXD'                                         |
         OPCODE 'EJECT'                                       |
         OPCODE 'END'                                         |
         OPCODE 'ENTRY'                                       |
         OPCODE 'EQU '                                        |
         OPCODE 'EXTRN'                                       |
         OPCODE 'GBLA'                                        |
         OPCODE 'GBLB'                                        |
         OPCODE 'GBLC'                                        |
         OPCODE 'ICTL'                                        |
         OPCODE 'ISEQ'                                        |
         OPCODE 'LCLA'                                        |
         OPCODE 'LCLB'                                        |
         OPCODE 'LCLC'                                        |
         OPCODE 'LTORG'                                       |
         OPCODE 'MACRO'                                       |
         OPCODE 'MEND'                                        |
         OPCODE 'MEXIT'                                       |
         OPCODE 'MNOTE'                                       |
         OPCODE 'NOP'                                         |
         OPCODE 'NOPR'                                        |
         OPCODE 'OPSYN'                                       |
         OPCODE 'ORG'                                         |
         OPCODE 'POP'                                         |
         OPCODE 'PRINT'                                       |
         OPCODE 'PUNCH'                                       |
         OPCODE 'PUSH'                                        |
         OPCODE 'REPRO'                                       |
         OPCODE 'RMODE'                                       |
         OPCODE 'SETA'                                        |
         OPCODE 'SETB'                                        |
         OPCODE 'SETC'                                        |
         OPCODE 'SPACE'                                       |
         OPCODE 'START'                                       |
         OPCODE 'TITLE'                                       |
         OPCODE 'USING'                                       |
         OPCODE 'WXTRN'                                       |
         OPCODE 'PR'       0101                               |
         OPCODE 'UPT'      0102                               |
         OPCODE 'SPM'      04                                 |
         OPCODE 'BALR'     05                                 |
         OPCODE 'BCTR'     06                                 |
         OPCODE 'B'        07                                 |
         OPCODE 'BCR'      07                                 |
         OPCODE 'BER'      07                                 |
         OPCODE 'BHR'      07                                 |
         OPCODE 'BMR'      07                                 |
         OPCODE 'BNER'     07                                 |
         OPCODE 'BNHR'     07                                 |
         OPCODE 'BNLR'     07                                 |
         OPCODE 'BNMR'     07                                 |
         OPCODE 'BNOR'     07                                 |
         OPCODE 'BNPR'     07                                 |
         OPCODE 'BNZR'     07                                 |
         OPCODE 'BOR'      07                                 |
         OPCODE 'BPR'      07                                 |
         OPCODE 'BR'       07                                 |
         OPCODE 'BZR'      07                                 |
         OPCODE 'SSK'      08                                 |
         OPCODE 'ISK'      09                                 |
         OPCODE 'SVC'      0A                                 |
         OPCODE 'BSM'      0B                                 |
         OPCODE 'BASSM'    0C                                 |
         OPCODE 'BASR'     0D                                 |
         OPCODE 'MVCL'     0E                                 |
         OPCODE 'CLCL'     0F                                 |
         OPCODE 'LPR'      10                                 |
         OPCODE 'LNR'      11                                 |
         OPCODE 'LTR'      12                                 |
         OPCODE 'LCR'      13                                 |
         OPCODE 'NR'       14                                 |
         OPCODE 'CLR'      15                                 |
         OPCODE 'OR'       16                                 |
         OPCODE 'XR'       17                                 |
         OPCODE 'LR'       18                                 |
         OPCODE 'CR'       19                                 |
         OPCODE 'AR'       1A                                 |
         OPCODE 'SR'       1B                                 |
         OPCODE 'MR'       1C                                 |
         OPCODE 'DR'       1D                                 |
         OPCODE 'ALR'      1E                                 |
         OPCODE 'SLR'      1F                                 |
         OPCODE 'LPDR'     20                                 |
         OPCODE 'LNDR'     21                                 |
         OPCODE 'LTDR'     22                                 |
         OPCODE 'LCDR'     23                                 |
         OPCODE 'HDR'      24                                 |
         OPCODE 'LRDR'     25                                 |
         OPCODE 'MXR'      26                                 |
         OPCODE 'MXDR'     27                                 |
         OPCODE 'LDR'      28                                 |
         OPCODE 'CDR'      29                                 |
         OPCODE 'ADR'      2A                                 |
         OPCODE 'SDR'      2B                                 |
         OPCODE 'MDR'      2C                                 |
         OPCODE 'DDR'      2D                                 |
         OPCODE 'AWR'      2E                                 |
         OPCODE 'SWR'      2F                                 |
         OPCODE 'LPER'     30                                 |
         OPCODE 'LNER'     31                                 |
         OPCODE 'LTER'     32                                 |
         OPCODE 'LCER'     33                                 |
         OPCODE 'HER'      34                                 |
         OPCODE 'LRER'     35                                 |
         OPCODE 'AXR'      36                                 |
         OPCODE 'SXR'      37                                 |
         OPCODE 'LER'      38                                 |
         OPCODE 'CER'      39                                 |
         OPCODE 'AER'      3A                                 |
         OPCODE 'SER'      3B                                 |
         OPCODE 'MER'      3C                                 |
         OPCODE 'DER'      3D                                 |
         OPCODE 'AUR'      3E                                 |
         OPCODE 'SUR'      3F                                 |
         OPCODE 'STH'      40                                 |
         OPCODE 'LA'       41                                 |
         OPCODE 'STC'      42                                 |
         OPCODE 'IC'       43                                 |
         OPCODE 'EX'       44                                 |
         OPCODE 'BAL'      45                                 |
         OPCODE 'BCT'      46                                 |
         OPCODE 'BC'       47                                 |
         OPCODE 'BE'       47                                 |
         OPCODE 'BH'       47                                 |
         OPCODE 'BL'       47                                 |
         OPCODE 'BLR'      47                                 |
         OPCODE 'BM'       47                                 |
         OPCODE 'BNE'      47                                 |
         OPCODE 'BNH'      47                                 |
         OPCODE 'BNL'      47                                 |
         OPCODE 'BNM'      47                                 |
         OPCODE 'BNO'      47                                 |
         OPCODE 'BNP'      47                                 |
         OPCODE 'BNZ'      47                                 |
         OPCODE 'BO'       47                                 |
         OPCODE 'BP'       47                                 |
         OPCODE 'BZ'       47                                 |
         OPCODE 'LH'       48                                 |
         OPCODE 'CH'       49                                 |
         OPCODE 'AH'       4A                                 |
         OPCODE 'SH'       4B                                 |
         OPCODE 'MH'       4C                                 |
         OPCODE 'BAS'      4D                                 |
         OPCODE 'CVD'      4E                                 |
         OPCODE 'CVB'      4F                                 |
         OPCODE 'ST'       50                                 |
         OPCODE 'N'        54                                 |
         OPCODE 'CL'       55                                 |
         OPCODE 'O'        56                                 |
         OPCODE 'X'        57                                 |
         OPCODE 'L'        58                                 |
         OPCODE 'C'        59                                 |
         OPCODE 'A'        5A                                 |
         OPCODE 'S'        5B                                 |
         OPCODE 'M'        5C                                 |
         OPCODE 'D'        5D                                 |
         OPCODE 'AL'       5E                                 |
         OPCODE 'SL'       5F                                 |
         OPCODE 'STD'      60                                 |
         OPCODE 'MXD'      67                                 |
         OPCODE 'LD'       68                                 |
         OPCODE 'CD'       69                                 |
         OPCODE 'AD'       6A                                 |
         OPCODE 'SD'       6B                                 |
         OPCODE 'MD'       6C                                 |
         OPCODE 'DD'       6D                                 |
         OPCODE 'AW'       6E                                 |
         OPCODE 'SW'       6F                                 |
         OPCODE 'STE'      70                                 |
         OPCODE 'LE'       78                                 |
         OPCODE 'CE'       79                                 |
         OPCODE 'AE'       7A                                 |
         OPCODE 'SE'       7B                                 |
         OPCODE 'ME'       7C                                 |
         OPCODE 'DE'       7D                                 |
         OPCODE 'AU'       7E                                 |
         OPCODE 'SU'       7F                                 |
         OPCODE 'SSM'      80                                 |
         OPCODE 'LPSW'     82                                 |
         OPCODE 'WRD'      84                                 |
         OPCODE 'RDD'      85                                 |
         OPCODE 'BXH'      86                                 |
         OPCODE 'BXLE'     87                                 |
         OPCODE 'SRL'      88                                 |
         OPCODE 'SLL'      89                                 |
         OPCODE 'SRA'      8A                                 |
         OPCODE 'SLA'      8B                                 |
         OPCODE 'SRDL'     8C                                 |
         OPCODE 'SLDL'     8D                                 |
         OPCODE 'SRDA'     8E                                 |
         OPCODE 'SLDA'     8F                                 |
         OPCODE 'TCH'      9F00                               |
         OPCODE 'STM'      90                                 |
         OPCODE 'TM'       91                                 |
         OPCODE 'MVI'      92                                 |
         OPCODE 'TS'       93                                 |
         OPCODE 'NI'       94                                 |
         OPCODE 'CLI'      95                                 |
         OPCODE 'OI'       96                                 |
         OPCODE 'XI'       97                                 |
         OPCODE 'LM'       98                                 |
         OPCODE 'TRACE'    99                                 |
         OPCODE 'LAM'      9A                                 |
         OPCODE 'STAM'     9B                                 |
         OPCODE 'SIO'      9C00                               |
         OPCODE 'SIOF'     9C01                               |
         OPCODE 'CLRIO'    9D01                               |
         OPCODE 'TIO'      9D00                               |
         OPCODE 'HDV'      9E01                               |
         OPCODE 'HIO'      9E00                               |
         OPCODE 'VAE'      A400                               |
         OPCODE 'VSE'      A401                               |
         OPCODE 'VME'      A402                               |
         OPCODE 'VDE'      A403                               |
         OPCODE 'VMAE'     A404                               |
         OPCODE 'VMSE'     A405                               |
         OPCODE 'VMCE'     A406                               |
         OPCODE 'VACE'     A407                               |
         OPCODE 'VCE'      A408                               |
         OPCODE 'VL'       A409                               |
         OPCODE 'VLE'      A409                               |
         OPCODE 'VLM'      A40A                               |
         OPCODE 'VLME'     A40A                               |
         OPCODE 'VLY'      A40B                               |
         OPCODE 'VLYE'     A40B                               |
         OPCODE 'VST'      A40D                               |
         OPCODE 'VSTE'     A40D                               |
         OPCODE 'VSTE'     A40D                               |
         OPCODE 'STNSM'    AC                                 |
         OPCODE 'STOSM'    AD                                 |
         OPCODE 'SIGP'     AE                                 |
         OPCODE 'MC'       AF                                 |
         OPCODE 'LRA'      B1                                 |
         OPCODE 'CLRCH'    B200                               |
         OPCODE 'CONCS'    B200                               |
         OPCODE 'DISCS'    B201                               |
         OPCODE 'STIDP'    B202                               |
         OPCODE 'STIDC'    B203                               |
         OPCODE 'SCK'      B204                               |
         OPCODE 'STCK'     B205                               |
         OPCODE 'SCKC'     B206                               |
         OPCODE 'STCKC'    B207                               |
         OPCODE 'SPT'      B208                               |
         OPCODE 'STPT'     B209                               |
         OPCODE 'SPKA'     B20A                               |
         OPCODE 'IPK'      B20B                               |
         OPCODE 'PTLB'     B20D                               |
         OPCODE 'SPX'      B210                               |
         OPCODE 'STPX'     B211                               |
         OPCODE 'STAP'     B212                               |
         OPCODE 'RRB'      B213                               |
         OPCODE 'PC'       B218                               |
         OPCODE 'SAC'      B219                               |
         OPCODE 'IPTE'     B221                               |
         OPCODE 'IPM'      B222                               |
         OPCODE 'IVSK'     B223                               |
         OPCODE 'IAC'      B224                               |
         OPCODE 'SSAR'     B225                               |
         OPCODE 'EPAR'     B226                               |
         OPCODE 'ESAR'     B227                               |
         OPCODE 'PT'       B228                               |
         OPCODE 'ISKE'     B229                               |
         OPCODE 'RRBE'     B22A                               |
         OPCODE 'SSKE'     B22B                               |
         OPCODE 'TB'       B22C                               |
         OPCODE 'DXR'      B22D                               |
         OPCODE 'CSCH'     B230                               |
         OPCODE 'HSCH'     B231                               |
         OPCODE 'MSCH'     B232                               |
         OPCODE 'SSCH'     B233                               |
         OPCODE 'STSCH'    B234                               |
         OPCODE 'TSCH'     B235                               |
         OPCODE 'TPI'      B236                               |
         OPCODE 'SAL'      B237                               |
         OPCODE 'RSCH'     B238                               |
         OPCODE 'STCRW'    B239                               |
         OPCODE 'STCPS'    B23A                               |
         OPCODE 'RCHP'     B23B                               |
         OPCODE 'SCHM'     B23C                               |
         OPCODE 'BAKR'     B240                               |
         OPCODE 'STURA'    B246                               |
         OPCODE 'MSTA'     B247                               |
         OPCODE 'PALB'     B248                               |
         OPCODE 'EREG'     B249                               |
         OPCODE 'ESTA'     B24A                               |
         OPCODE 'LURA'     B24B                               |
         OPCODE 'TAR'      B24C                               |
         OPCODE 'CPYA'     B24D                               |
         OPCODE 'SAR'      B24E                               |
         OPCODE 'EAR'      B24F                               |
         OPCODE 'STCTL'    B6                                 |
         OPCODE 'LCTL'     B7                                 |
         OPCODE 'CS'       BA                                 |
         OPCODE 'CDS'      BB                                 |
         OPCODE 'CLM'      BD                                 |
         OPCODE 'STCM'     BE                                 |
         OPCODE 'ICM'      BF                                 |
         OPCODE 'MVN'      D1                                 |
         OPCODE 'MVC'      D2                                 |
         OPCODE 'MVZ'      D3                                 |
         OPCODE 'NC'       D4                                 |
         OPCODE 'CLC'      D5                                 |
         OPCODE 'OC'       D6                                 |
         OPCODE 'XC'       D7                                 |
         OPCODE 'MVCK'     D9                                 |
         OPCODE 'MVCP'     DA                                 |
         OPCODE 'MVCS'     DB                                 |
         OPCODE 'TR'       DC                                 |
         OPCODE 'TRT'      DD                                 |
         OPCODE 'ED'       DE                                 |
         OPCODE 'EDMK'     DF                                 |
         OPCODE 'MVCSK'    E50E                               |
         OPCODE 'MVCDK'    E50F                               |
         OPCODE 'LASP'     E500                               |
         OPCODE 'TPROT'    E501                               |
         OPCODE 'MVCIN'    E8                                 |
         OPCODE 'AP'       FA                                 |
         OPCODE 'SP'       FB                                 |
         OPCODE 'MP'       FC                                 |
         OPCODE 'DP'       FD                                 |
         OPCODE 'SRP'      F0                                 |
         OPCODE 'MVO'      F1                                 |
         OPCODE 'PACK'     F2                                 |
         OPCODE 'UNPK'     F3                                 |
         OPCODE 'ZAP'      F8                                 |
         OPCODE 'CP'       F9                                 |
MTABEND  OPCODE ,                 END OF TABLE INDICATOR------|
MACTSTRT DC    200XL10'00'        MACRO TABLE ENTRIES ---------------|
MACTEND  EQU   *                  -----------------------------------|
MACTB    DSECT
TBMAC    DS    D
MCNT     DS    H
         END


                                                                                                          c Xephon 1994

