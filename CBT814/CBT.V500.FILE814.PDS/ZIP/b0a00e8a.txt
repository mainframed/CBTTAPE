Time-dependent command submission

CMDTIMER is a program that will submit commands based on the time of day and SMF
system-id.  It was written to handle situations where, only during certain time
ranges, certain commands may need to be issued.  Specific uses are for setting
up special volume mounting and/or initiator structures based on shift changes.
CMDTIMER should be started at IPL time but may also be used after IPL.  Its
method of operation is to scan control cards looking for time ranges within
which to execute the specified commands.  It also uses an additional criterion
of a one character SMF system-id to determine what system(s) should get a
particular command.  The source for CMDTIMER is shown below.

CMDTIMER makes use of a local table, which is assembled and linked into the same
authorised or LINKLIST library as CMDTIMER itself.  The source for this local
table, called SYSIDTAB, is also shown below.  SYSIDTAB was implemented for many
other programs to alleviate the need for re-assembling programs when new
processors are installed and/or existing processors are upgraded.  It contains
information such as a one character system-id, the SMF system-id, CPU type and
serial number, etc.

The process of CMDTIMER is as follows:

1    CMDTIMER is assembled and linked into an authorised or LINKLIST library (if
using an authorised library then include a STEPLIB DD in the JCL).  The program
must be link-edited with AC=1 because it uses the MODESET macro.

2    Optionally, SYSIDTAB is assembled and linked into a LINKLIST library (or
the CMDTIMER code can be modified to bypass system-id checking).

3    A catalogued procedure is built, similar to the one shown below to invoke
CMDTIMER.

4    A control card member is built and pointed to by the catalogued procedure.
A sample set of control cards is also shown below.  Note that comments are
allowed by coding an asterisk(*) in column 1 of a control card.

This program runs on MVS/SP 1.3.5 and JES2.  It cannot issue JES3 commands
unless further modifications are made to CMDTIMER as well as JES3.  Having
worked in a JES3 shop, I am aware of JES3 modifications to IATSI34 on various
MVS modifications tapes that would allow an authorised program to issue SVC 34s
in a special format that JES3 would accept as a command.  You are also currently
restricted to 60 characters for a command.  This can be modified by adding logic
to recognise a continuation in column 72.  I have not found this necessary for
my applications.


THE SOURCE CODE FOR CMDTIMER

         PUNCH ' SETCODE AC(1)'
CMDTIMER CSECT                     ESTABLISH CSECT
         SAVE  (14,12),,CMDTIMER-&SYSDATE
         LR    R12,R15            LOAD R12 W/EPA ADDRESS
         USING CMDTIMER,R12       ESTABLISH ADDRESSABILITY TO CSECT
         LA    R8,SAVEAREA        LOAD ADDR OF MY S/A
         ST    R8,8(,R13)         ST MY S/A ADDR IN CALLERS S/A
         ST    R13,4(,R8)         ST CALLERS S/A ADDR IN MY S/A
         LR    R13,R8             LOAD ADDR OF MY S/A IN R13
         L     R1,CVTPTR          LOAD R1/ADDR OF CVT
         L     R1,CVTSMCA-CVT(R1) LOAD R10 W/A(SMCA)
         LA    R1,SMCASID-SMCABASE(R1) LOAD R1 W/A(SMF SYSID)
         MVC   SMFID(4),0(R1)     MOVE SMFID TO HOLD AREA
         LOAD  EP=SYSIDTAB        LOAD SYSIDTAB
         LR    R2,R0              POINT TO LOAD MODULE
LOOPID   CLC   1(4,R2),SMFID      IS TABLE ENTRY = SMF SYSID
         BNE   BUMPID             NO, GO CHECK NEXT ENTRY
         MVC   SYSID,0(R2)        MOVE ONE CHAR ID TO SYSID
         B     OPENFILE           GO OPEN FILE
BUMPID   CLC   1(4,R2),=4C'*'     IS IT END OF TABLE?
         BE    WTOMSGID           NO ENTRY FOR SYSTEM
         LA    R2,40(R2)          BUMP TABLE TO NEXT ENTRY
         B     LOOPID             TRY AGAIN!
WTOMSGID WTO   'CMT001I NO TABLE ENTRY FOUND FOR THIS SYSTEM'
         B     RETURN             GIVE IT UP
OPENFILE OPEN  (SYSIN,INPUT)      OPEN SYSIN
         MODESET MODE=SUP         GET INTO SUPERVISOR STATE FOR LATER
GETLOOP  GET   SYSIN,RECORD       GET A RECORD
         CLI   RECORD,C'*'        IS IT A COMMENT
         BE    GETLOOP            YES, GO GET NEXT RECORD
         CLC   SYSID,SID          IS THE RECORD FOR THIS SYSID
         BNE   GETLOOP            NO, GO GET NEXT RECORD
         TIME  DEC                GET TIME
         O     R0,=X'0000000F'    PUT SIGN AT END
         ST    R0,TOD             STORE R0 AS TOD
         PACK  PACKFLD1,TIME1     PACK TIME1
         L     R2,PACKFLD1        LOAD R2 W/PACKFLD1
         SRL   R2,4               SHIFT OUT SIGN
         SLL   R2,16              SHIFT TIME UP TO HI ORDER 2 BYTES
         O     R2,=X'0000000F'    PUT SIGN AT END
         ST    R2,PACKFLD1        SAVE IN PACKFLD1
         PACK  PACKFLD2,TIME2     PACK TIME2
         L     R2,PACKFLD2        LOAD R2 W/PACKFLD2
         SRL   R2,4               SHIFT OUT SIGN
         SLL   R2,16              SHIFT TIME UP TO HI ORDER 2 BYTES
         O     R2,=X'0000000F'    PUT SIGN AT END
         ST    R2,PACKFLD2        SAVE IN PACKFLD2
         CP    PACKFLD2,PACKFLD1  IS TIME2 GREATER THAN TIME1
         BH    CHKTOD             YES, GO CHECK TOD
         AP    PACKFLD2,HOUR24    ELSE ADD 24 HOURS TO TIME2
CHKTOD   CP    TOD,PACKFLD1       IS TOD GREATER THAN TIME1
         BH    CHKTIME2           YES, GO CHECK TIME2
         AP    TOD,HOUR24         ELSE ADD 24 HOURS TO TOD
CHKTIME2 CP    TOD,PACKFLD2       IS TOD GREATER THAN TIME2
         BH    GETLOOP            YES, GO GET ANOTHER RECORD
DOCMD    MVC   COMMAND+4(L'CMD),CMD  MOVE COMMAND TO WTO BUFFER
         IPK   0(R0)              SAVE CURRENT PSW KEY INTO R2
         SPKA  0(R0)              SET PSW KEY TO ZERO
         STC   R2,PSWKEY          SAVE OLD PSW KEY FOR LATER
         SR    R0,R0              CLEAR R0
         LA    R1,COMMAND         POINT TO COMMAND
         SVC   34                 ISSUE COMMAND
         IC    R2,PSWKEY          LOAD R2 WITH ORIGINAL PSW KEY
         SPKA  0(R2)              GET BACK TO ORIGINAL PSW KEY
         STIMER WAIT,BINTVL=WAITTIME WAIT A LITTLE WHILE
         B     GETLOOP            GO GET ANOTHER COMMAND
EOF      CLOSE (SYSIN,DISP)       CLOSE SYSIN
RETURN   DELETE EP=SYSIDTAB       DELETE SYSIDTAB
         L     R13,SAVEAREA+4     RELOAD R13 W/ADDR OF CALLERS S/A
         RETURN (14,12),RC=0      RETURN TO OS WITH RETCODE=0
SAVEAREA DC    18F'0'             OS SAVEAREA
WAITTIME DC    A(50)              .5 SECONDS FOR WAIT
PACKFLD1 DC    PL4'0'             PACKING FIELD FOR TIME1
PACKFLD2 DC    PL4'0'             PACKING FIELD FOR TIME2
TOD      DC    PL4'0'             STORAGE FIELD FOR TOD
HOUR24   DC    PL4'2400000'       24 HOURS IN PACKED DECIMAL
SMFID    DC    CL4' '             SMF SYSID
SYSID    DC    CL1' '             1 CHARACTER ID FROM SYSID TABLE
PSWKEY   DC    CL1' '             1 BYTE AREA TO SAVE PSW KEY
COMMAND  WTO   '                                                       X
                        ',ROUTCDE=11,MF=L
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,RECFM=FB,LRECL=80,EODAD=EOF,      X
               MACRF=GM
RECORD   DS    0CL80
TIME1    DC    CL4'HHMM'
DASH     DC    C'-'
TIME2    DC    CL4'HHMM'
         DC    CL1' '
SID      DC    CL1' '
         DC    CL1' '
CMD      DC    CL60' '
         DC    CL8' '
         CVT   DSECT=YES
         IEESMCA
         END

THE SOURCE CODE FOR SYSIDTAB

SYSIDTAB CSECT
**********************************************************************
*       FIELD              DISPLACEMENT LENGTH                       *
*       SYSID              0            1                            *
*       SMFID              1            4                            *
*       JESID              5            4                            *
*       CPU TYPE           9            8                            *
*       SERIAL#            17           6                            *
*       CPU#               23           4                            *
*       EXPANSION AREA     27           13                           *
**********************************************************************
SYSK0   DC     C'K',C'SYSK',C'SYSK',C'3081-K32',C'020138',C'CP 0'
        DC     CL13' '
SYSK2   DC     C'K',C'SYSK',C'SYSK',C'3081-K32',C'220138',C'CP 2'
        DC     CL13' '
SYSL0   DC     C'L',C'SYSL',C'SYSL',C'3081-K32',C'021064',C'CP 0'
        DC     CL13' '
SYSL2   DC     C'L',C'SYSL',C'SYSL',C'3081-K32',C'221064',C'CP 2'
        DC     CL13' '
**********************************************************************
TABEND  DC     C'*',C****',C'****',C'********',C'******',C'****'
        DC     13C'*'
        END

A CATALOGUED PROCEDURE TO INVOKE CMDTIMER

//CMDTIMER PROC M=CMDSYSIN
//CMDTIMER EXEC PGM=CMDTIMER
//SYSUDUMP DD SYSOUT=*
//SYSIN    DD DISP=SHR,DSN=SYS1.PROCLIB(&M)

AN EXAMPLE OF CONTROL CARDS

**********************************************************************
* COL 01-04 = BEGIN TIME RANGE        COL 06-09 = END TIME RANGE     *
* COL 11    = SYSID CHARACTER         COL 13-72 = COMMAND TO EXECUTE *
**********************************************************************
0700-1659 K $TI1-5,0                 DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI6,210                 DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI7,3210                DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI8,4                   DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI9,5                   DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI10-11,LC              DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI12,R                  DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI13-15,BDE             DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI16,H                  DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI17,M                  DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI18,I                  DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI19,F9                 DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI20-21,Q               DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI22-23,I               DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI24-25,8               DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI26,O                  DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI27,8                  DAYTIME INITIATORS FOR SYSTEM K
0700-1659 K $TI28,8O                 DAYTIME INITIATORS FOR SYSTEM K
1700-0659 K $PI6                     NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI1,X                   NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI2,Z                   NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $T13-4,A                 NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI5,0                   NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI6,7                   NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI7,01234               NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI8,O                   NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI9,5321                NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI10,LCH                NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI11,O                  NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI12,R                  NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI13-17,BDE             NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI18,I                  NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI19,F9                 NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI20-21,Q               NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI22-23,I               NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI24-26,8               NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI27,8                  NIGHTTIME INITIATORS FOR SYSTEM K
1700-0659 K $TI28,80                 NIGHTTIME INITIATORS FOR SYSTEM K
0700-1759 L S RDR,JOB=MNTL0700       START JOB TO REMOUNT DAY PACKS L
1800-0659 L S RDR,JOB=MNTL1800       START JOB TO REMOUNT NIGHT PACKS L
0700-1759 K S RDR,JOB=MNTK0700       START JOB TO REMOUNT DAY PACKS K
1800-0659 K S RDR,JOB=MNTK1800       START JOB TO REMOUNT NIGHT PACKS K
2000-0659 L $SI28                    ONLY OPEN INIT28 FROM 8PM-7AM
0200-0659 L $SI22                    ONLY OPEN INIT22 FROM 2AM-7AM
0700-1759 K T IPS=DK                 SET DAYTIME IPS ON SYSTEM K
1800-0659 K T IPS=NK                 SET NIGHTTIME IPS ON SYSTEM K


Bruce Bordonaro
System Software Manager
EDS (USA) © EDS 1987






























