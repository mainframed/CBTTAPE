A LIFO storage stack
There is no hardware feature in 370 architecture, as in some other processor
architectures, for a Last In First Out (LIFO) stack to save and restore
registers or the data areas of subroutines.
Therefore, save areas and working storage are usually defined statically in case
non-re-entrant Assembler programs increase the load module size unnecessarily.
If the program is re-entrant or recursive, storage has to be obtained
dynamically by GETMAIN SVC and released by FREEMAIN SVC.  This leads to some
system overhead, especially if the program is designed in a modular way and the
number of subroutine calls is high.
The macro STACK (calling routine STACKRTN) is a highly efficient implementation
of a LIFO stack using the unused first word of the save areas pointed to by
register 13.  Existing programs can be adapted easily.
In case of routines using static save areas and working storage, you have to
insert a STACK GET call at entry (after saving registers and establishing a base
register) and a STACK FREE call at exit (before restoring registers and setting
the return code).  Additionally you must convert the working storage (including
the save area) to a DSECT.  Simply replace GETMAIN and FREEMAIN by STACK GET and
STACK FREE in routines using these macros to obtain and release this kind of
storage.  Solely in the main program a STACK INIT call has to be inserted in
front of the STACK GET call.  STACK INIT gets storage of the requested size for
the stack and does some initialisation.  Storage will be freed automatically by
the last STACK FREE operation.
It is absolutely mandatory that the save area is placed at the beginning of the
stack element obtained by STACK GET, ie the address contained in register 13 is
the address of the current stack element and of the current save area as well.
NON-RE-ENTRANT PROGRAM
MAIN     CSECT                     MAIN PROGRAM
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING MAIN,R12
         STACK INIT,100*1024       INIT STACK, SIZE = 100K   (INSERTED)
         STACK GET,72              GET NEW SAVEAREA          (INSERTED)
         USING SAVEAREA,R1                                   (INSERTED)
         ST    R13,SAVEAREA+4
         LR    R15,R13
         LA    R13,SAVEAREA
         ST    R13,8(,R15)
         DROP  R1                                            (INSERTED)
         .....
         L     R15,=V(SUBRTN)
         BALR  R14,R15
         .....
         L     R13,4(,R13)
         STACK FREE                FREE SAVEAREA             (INSERTED)
         LM    R14,R12,12(R13)
         SLR   R15,R15
         BR    R14
         DSECT                                               (INSERTED)
SAVEAREA DS    18F
         END   MAIN

SUBRTN   CSECT                     SUBROUTINE
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING SUBRTN,R12
         STACK GET,WORKLNG         GET WORKING STORAGE       (INSERTED)
         USING SAVEAREA,R1                                   (INSERTED)
         ST    R13,SAVEAREA+4
         LR    R15,R13
         LA    R13,SAVEAREA
         ST    R13,8(,R15)
         DROP  R1                                            (INSERTED)
         USING WORK,R13
         .....
         L     R13,4(,R13)
         STACK FREE                FREE WORKING STORAGE      (INSERTED)
         LM    R14,R12,12(R13)
         SLR   R15,R15
         BR    R14
WORK     DSECT                                               (INSERTED)
SAVEAREA DS    18F                 IMPORTANT:
BUFFER   DS    CL80                   SAVEAREA  M U S T  BE
WORKLNG  EQU   *-WORK                 PLACED AT BEGINNING OF AREA
         END   SUBRTN
RE-ENTRANT PROGRAM
MAIN     CSECT                     MAIN PROGRAM
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING MAIN,R12
         STACK INIT,100*1024       INIT STACK, SIZE = 100K   (INSERTED)
*        GETMAIN R,LV=72           GET NEW SAVEAREA          (REMOVED)
         STACK GET,72              GET NEW SAVEAREA          (INSERTED)
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         .....
         L     R15,=V(SUBRTN)
         BALR  R14,R15
         .....
         LR    R1,R13
         L     R13,4(,R1)
         ST    R15,16(,R13)
*        FREEMAIN R,LV=72,A=(R1)   FREE SAVEAREA             (REMOVED)
         STACK FREE                FREE SAVEAREA             (INSERTED)
         LM    R14,R12,12(R13)
         BR    R14
         END   MAIN

SUBRTN   CSECT                     SUBROUTINE
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING SUBRTN,R12
*        GETMAIN R,LV=WORKLNG      GET WORKING STORAGE       (REMOVED)
         STACK GET,WORKLNG         GET WORKING STORAGE       (INSERTED)
         ST    R13,4(,R1)
         ST    R1,8(,R13)
         LR    R13,R1
         USING WORK,R13
         .....
         LR    R1,R13
         L     R13,4(,R1)
         ST    R15,16(,R13)
*        FREEMAIN R,LV=WORKLNG,A=(R1) FREE WORKING STORAGE   (REMOVED)
         STACK FREE                FREE WORKING STORAGE      (INSERTED)
         LM    R14,R12,12(R13)
         BR    R14
WORK     DSECT
SAVEAREA DS    18F                 IMPORTANT:
BUFFER   DS    CL80                   SAVEAREA  M U S T  BE
WORKLNG  EQU   *-WORK                 PLACED AT BEGINNING OF AREA
         END   SUBRTN
Note: although this version of STACK is for XA, it can be adapted easily for
MVS/SP1.  The program STACKRTN is re-entrant and optionally can be brought into
LPA.
         MACRO
&NAME    STACK
.**********************************************************************
.* PERFORM STORAGE STACK OPERATIONS INIT / GET / FREE ,
.* USING THE FIRST WORD OF SAVEAREAS
.* STACK INIT,<LENGTH>   INITIALIZE STACK
.* STACK GET,<LENGTH>    GET NEW STACK ELEMENT
.* STACK FREE            FREE LAST STACK ELEMENT
.* <LENGTH> IS A CONSTANT, A SYMBOL OR REGISTER (2)..(12)
.**********************************************************************
         AIF   (N'&SYSLIST GE 1 AND N'&SYSLIST LE 2).STK010
         MNOTE 8,'INVALID NUMBER OF OPERANDS'
         MEXIT
.STK010  ANOP
         AIF   ('&SYSLIST(1)' EQ  'INIT').STK100
         AIF   ('&SYSLIST(1)' EQ  'GET' ).STK200
         AIF   ('&SYSLIST(1)' EQ  'FREE').STK300
         MNOTE 8,'INVALID OPTION ''&SYSLIST(1)'''
         MEXIT
.STK100  ANOP                     INIT
         AIF   (N'&SYSLIST EQ 2).STK120
         MNOTE 8,'MISSING LENGTH OPERAND'
         MEXIT
.STK120  ANOP
&NAME    LOAD  EP=STACKRTN        GET ADDR OF STACK ROUTINE
         LR    15,0               ADDR OF STACK ROUTINE
         SLR   1,1                0 - INDICATE INIT OF STACK
         AGO   .STK250
.STK200  ANOP                     GET
         AIF   (N'&SYSLIST EQ 2).STK220
         MNOTE 8,'MISSING LENGTH OPERAND'
         MEXIT
.STK220  ANOP
&NAME    L     15,0(,13)          ADDR OF STACK HEADER
         L     15,0(,15)          ADDR OF STACK ROUTINE
         LA    1,4(,0)            4 - INDICATE GET
.STK250  ANOP
         AIF   ('&SYSLIST(2)'(1,1) NE '(').STK260
         LR    0,&SYSLIST(2,1)    LOAD LENGTH VALUE
         AGO   .STK290
.STK260  ANOP
         AIF   (T'&SYSLIST(2) NE 'N').STK270
         AIF   (&SYSLIST(2) GT 4095).STK270
         LA    0,&SYSLIST(2)(,0)  LOAD LENGTH VALUE
         AGO   .STK290
.STK270  ANOP
         CNOP  0,4
         L     0,*+8              LOAD LENGTH VALUE
         B     *+8                BRANCH AROUND CONSTANT
         DC    A(&SYSLIST(2))     LENGTH VALUE
.STK290  ANOP
         BASSM 14,15              CALL STACK ROUTINE
         MEXIT
.STK300  ANOP                     FREE
         AIF   (N'&SYSLIST EQ 1).STK320
         MNOTE 8,'LENGTH OPERAND NOT ALLOWED FOR OPTION ''FREE'''
         MEXIT
.STK320  ANOP
&NAME    L     15,0(,13)          ADDR OF STACK HEADER
         L     15,0(,15)          ADDR OF STACK ROUTINE
         LA    1,8(,0)            8 - INDICATE FREE
         BASSM 14,15              CALL STACK ROUTINE
         MEND

*** PERFORM STORAGE STACK OPERATIONS INIT / GET / FREE ,
*   USING THE FIRST WORD OF SAVEAREAS
*   SAVEAREA MUST BE PLACED AT BEGINNING OF EVERY STACK ELEMENT
*   REGS ON ENTRY
*      R0  = REQUESTED LENGTH
*      R1  = TYPE OF CALL (INIT / GET / FREE)
*      R13 = ADDR OF SAVEAREA OF CALLER OF CALLER
*      R14 = ADDR WHERE TO RETURN
*      R15 = ADDR OF ENTRY
*   REGS USAGE
*      R0  = WORK
*      R1  = WORK
*      R2..R8  = UNUSED
*      R9  = LENGTH OF STACK (INIT)
*      R10 = ADDR OF TEMP SAVEAREA (INIT)
*      R11 = ADDR OF STACK
*      R12 = BASE
*      R13/R14  = UNCHANGED
*      R15 = WORK
*   REGS ON EXIT
*      R0  = UNDEFINED
*      R1  = ADDR OF STACK ELEMENT (GET) / UNDEFINED (INIT / FREE)
*      R2..R14 = UNCHANGED
*      R15 = UNDEFINED
*   ABNORMAL END
***  USER ABEND 2058 (X'80A') IN CASE OF STACK SPACE EXHAUSTED (GET)
STACKRTN CSECT                    REENTRANT
STACKRTN AMODE 31
STACKRTN RMODE ANY
         USING *,R15              BASE REG
         B     *+4(R1)            BRANCH TO REQ'D OPERATION
         B     STKINIT
         B     STKGET
         B     STKFREE
STKINIT  DS    0H                 INITIALIZATION OF STACK
         ST    R0,0(,R13)         SAVE LENGTH VALUE
         GETMAIN R,LV=16          GET TEMP SAVEAREA
         DROP  R15
         L     R0,0(,R13)         RESTORE LENGTH VALUE
         STM   R9,R12,0(R1)       SAVE REGS
         LR    R10,R1             ADDR OF TEMP SAVEAREA
         BALR  R12,0              LOAD BASE REG
         USING *,R12
         SLR   R1,R1              CLEAR REG
         SRDL  R0,3               DIVIDE REQ'D STACK LENGTH BY 8
         SLL   R0,3               MULTIPLY BY 8
         LTR   R1,R1              REMAINDER = 0 ?
         BZ    STK110              YES
         LA    R15,8
         ALR   R0,R15             ADD 8
STK110   DS    0H
         LA    R15,72             MIN LENGTH = 72
         CLR   R0,R15             REQ'D LENGTH >= MIN LENGTH ?
         BNL   STK120              YES
         LR    R0,R15             SET TO MIN
STK120   DS    0H
         LA    R15,STKHLNG
         ALR   R0,R15             INCREMENT BY STACK HEADER LENGTH
         LR    R9,R0              LENGTH OF STACK INCLUDING HEADER
         LTR   R14,R14            TEST ADDRESSING MODE OF CALLER
         BM    STK130              AMODE=31
        GETMAIN RU,LV=(R9),LOC=(BELOW,ANY) ALLOC STACK BELOW THE LINE
         B     STK140
STK130   DS    0H
         GETMAIN RU,LV=(R9),LOC=(ANY,ANY)   ALLOC STACK ABOVE THE LINE
STK140   DS    0H
         ST    R1,0(,R13)        ADDR OF STACK TO 1ST WORD OF SAVEAREA
         LR    R11,R1             ADDR OF STACK
         USING STACKD,R11
         BALR  R15,0              LOAD ADDR OF NEXT INSTRUCTION
         LA    R1,*-STACKRTN      OFFSET TO ENTRY
         SLR   R15,R1             ADDR OF ENTRY
         ST    R15,STKARTN        INIT ADDR OF STACK ROUTINE
         LR    R15,R11            ADDR OF STACK
         ALR   R15,R9             PLUS ITS LENGTH
         ST    R15,STKAEND        INIT ADDR OF END OF STACK
         LA    R15,STACKD+STKHLNG ADDR OF FIRST STACK ELEMENT
         ST    R15,STKATOP        INIT CURRENT TOP OF STACK
         SLR   R15,R15
         ST    R15,STKRSRV        INIT RESERVED FIELD
         DROP  R11
         DROP  R12
         LR    R1,R10             ADDR OF TEMP SAVEAREA
         LM    R9,R12,0(R1)       RESTORE REGS
         FREEMAIN R,LV=16,A=(R1)  FREE TEMP SAVEAREA
         BSM   0,R14              RETURN TO CALLER
STKGET   DS    0H                 GET NEW STACK ELEMENT
         L     R1,0(,R13)         GET ADDR OF STACK
         USING STACKD,R1
         STM   R11,R12,STKTEMP    SAVE REGS
         DROP  R1
         LR    R11,R1             ADDR OF STACK
         USING STACKD,R11
         LR    R12,R15            LOAD BASE REG
         USING STACKRTN,R12
         SLR   R1,R1              CLEAR REG
         SRDL  R0,3               DIVIDE REQ'D LENGTH BY 8
         SLL   R0,3               MULTIPLY BY 8
         LTR   R1,R1              REMAINDER = 0 ?
         BZ    STK210              YES
         LA    R15,8
         ALR   R0,R15             ADD 8
STK210   DS    0H
         LA    R15,72             MIN LENGTH = 72
         CLR   R0,R15             REQ'D LENGTH >= MIN LENGTH ?
         BNL   STK220              YES
         LR    R0,R15             SET TO MIN
STK220   DS    0H
         L     R15,STKATOP        ADDR OF CURRENT TOP OF STACK
         ALR   R0,R15             REQ'D LENGTH + ADDR OF TOP OF STACK
         CL    R0,STKAEND         STACK SPACE EXHAUSTED ?
         BH    STK230              YES
         ST    R0,STKATOP         NEW ADDR OF TOP OF STACK
         ST    R11,0(,R15)   ADDR OF STACK TO 1ST WORD OF NEW SAVEAREA
         LM    R11,R12,STKTEMP    RESTORE REGS
         DROP  R11
         LR    R1,R15             ADDR OF NEW STACK ELEMENT
         BSM   0,R14              RETURN TO CALLER
STK230   DS    0H
         WTO   '******* STACK SPACE EXHAUSTED',ROUTCDE=11,DESC=7
*                                  INFORM PROGRAMMER
         ABEND X'80A',DUMP        USER ABEND 2058
         DROP  R12
STKFREE  DS    0H                 FREE LAST STACK ELEMENT
         L     R1,0(,R13)         GET ADDR OF STACK
         USING STACKD,R1
         STM   R11,R12,STKTEMP    SAVE REGS
         DROP  R1
         LR    R11,R1             ADDR OF STACK
         USING STACKD,R11
         LR    R12,R15            LOAD BASE REG
         USING STACKRTN,R12
         L     R15,8(,R13)        GET ADDR OF LAST SAVEAREA, IT'S ADDR
*                                  OF LAST STACK ELEMENT
         ST    R15,STKATOP        NEW TOP OF STACK
         LA    R0,STACKD+STKHLNG  ADDR OF FIRST STACK ELEMENT
         CLR   R15,R0             STACK EMPTY ?
         BNH   STK310              YES
         LM    R11,R12,STKTEMP    RESTORE REGS
         BSM   0,R14              RETURN TO CALLER
STK310   DS    0H
         L     R0,STKAEND         ADDR OF END OF STACK
         SLR   R0,R11             CALCULATE LENGTH OF STACK
         LR    R1,R11             ADDR OF STACK
         LR    R15,R12            LOAD BASE REG
         DROP  R12
         USING STACKRTN,R15
         LM    R11,R12,STKTEMP    RESTORE REGS
         DROP  R11
         FREEMAIN RU,LV=(R0),A=(R1) FREE STORAGE OF STACK
         DROP  R15
         SLR   R0,R0
         ST    R0,0(,R13)         REMOVE ADDR OF STACK
         BSM   0,R14              RETURN TO CALLER
STACKD   DSECT ,                  STACK HEADER
STKARTN  DS    A                  ADDR OF STACK ROUTINE
STKAEND  DS    A                  HIGH END OF STACK
STKATOP  DS    A                  CURRENT TOP OF STACK
STKRSRV  DS    A                  RESERVED
STKTEMP  DS    2F                 TEMP SAVE AREA (R11,R12)
         DS    0D
STKHLNG  EQU   *-STACKD           LENGTH OF STACK HEADER
         END   STACKRTN


Walter Wiedemann
Consultant (West Germany)     © Walter Wiedemann 1988








































