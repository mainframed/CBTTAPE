A console clip utility

Here is a program that lets you clip, or re-label, disks from the operator
console.  This is handy because when you re-label a disk you usually have to go
into the computer room anyway to mount the pack, to vary it off-line, or to
reply to the WTORs from ICKDSF.  Once you're inside, you can usually find an
operator console a lot more readily than you can a TSO terminal, or keypunch, or
whatever you usually use to put your clip jobs together.

This program takes the unit address and volume serial number you specify,
creates a control card for the IBM ICKDSF utility, and dynamically invokes
ICKDSF.  Then it scans the SYSPRINT file from ICKDSF for messages and displays
them on the operator console.

ICKDSF is an APF-authorised program, so CLIP must be link-edited with AC=1 and
put into an APF-authorised library.  If you don't do this, you will get an S047
abend when ICKDSF is invoked.

To run CLIP, you need a procedure in SYS1.PROCLIB for the CLIP-started task.

SAMPLE PROCEDURE

//CLIP     PROC U=,V=
//*  U IS THE UNIT ADDRESS
//*  V IS THE NEW VOLUME SERIAL NUMBER
//CLIP     EXEC PGM=CLIP,PARM='&U./&V.'
//SYSIN    DD   UNIT=SYSDA,SPACE=(TRK,(1,1))
//SYSPRINT DD   UNIT=SYSDA,SPACE=(TRK,(1,1))

SOURCE CODE

         TITLE 'CLIP - CONSOLE CLIP PROGRAM'
*=====================================================================*
*  CLIP - CONSOLE CLIP PROGRAM                                        *
*     EXEC PGM=CLIP,PARM='UUU/VVVVVV'                                 *
*        UUU IS UNIT ADDRESS (WHICH MUST BE OFFLINE)                  *
*     VVVVVV IS NEW VOLUME SERIAL NUMBER                              *
*     RETURN CODE 28 MEANS: THE PARM WAS IN ERROR                     *
*                           THE SYSIN FILE DID NOT OPEN               *
*                           THE SYSPRINT FILE DID NOT OPEN            *
*     ANY OTHER RETURN CODE IS FROM ICKDSF                            *
*  THIS PROGRAM MUST BE LINK EDITED WITH AC=1 AND PLACED              *
*  IN AN APF AUTHORIZED LIBRARY.                                      *
*=====================================================================*
         DCBD  DSORG=PS,DEVD=DA
*   ENTRY LINKAGE
CLIP     CSECT
         USING CLIP,R15            SET UP TEMPORARY BASE
         B     AROUNDID            BRANCH AROUND EYECATCHER
         DC    AL1(ENDID-*-1)      LENGTH OF EYECATCHER
         DC    C'CLIP &SYSDATE. &SYSTIME. '
ENDID    EQU   *
AROUNDID DS    0H
         STM   R14,R12,12(R13)     SAVE REGISTERS
         LR    R12,R15             SET UP NEW BASE
         USING CLIP,R12            ...
         DROP  R15                 ...
         LA    R15,SAVEAREA        LINK SAVE AREAS
         ST    R15,8(R13)          HSA -> LSA
         ST    R13,4(R15)          LSA -> HSA
         LR    R13,R15             POINT TO OUR SAVE AREA
         LR    R2,R1               SAVE PARMLIST POINTER
*   PARSE THE JCL PARAMETER:  UUU/VVVVVV
         L     R4,0(R2)            R4 -> JCL PARM LENGTH
         LH    R5,0(R4)            R5 = LENGTH OF JCL PARM
         LA    R4,2(R4)            R4 -> JCL PARM DATA
*   GET THE UNIT ADDRESS
         LR    R1,R4               SAVE START OF PARM
UNITLOOP LTR   R5,R5               ARE WE OUT OF PARM?
         BNP   UNITLEND            YES...QUIT THE LOOP
         CLI   0(R4),C'/'          IS THIS A SLASH?
         BE    UNITLEND            YES...QUIT THE LOOP
         LA    R4,1(R4)            NO...POINT TO THE NEXT CHARACTER
         BCTR  R5,0                DOCK THE LENGTH
         B     UNITLOOP            AND TRY AGAIN
UNITLEND DS    0H                  -
         LR    R15,R4              GET THE LENGTH
         SR    R15,R1              OF THE FIRST FIELD
         C     R15,=F'3'           IS IT RIGHT?
         BNE   BADPARM             NO...BAG IT
         MVC   UNIT,0(R1)          SAVE THE UNIT
         LA    R4,1(R4)            POINT PAST THE SLASH
         BCTR  R5,0                AND TRIM THE LENGTH
*   GET THE VOLUME SERIAL NUMBER
         LR    R1,R4               SAVE THE START OF THE FIELD
VOLLOOP  LTR   R5,R5               ARE WE OUT OF PARM?
         BNP   VOLLEND             YES...QUIT THE LOOP
         LA    R4,1(R4)            NO...POINT TO THE NEXT CHARACTER
         BCTR  R5,0                DOCK THE LENGTH
         B     VOLLOOP             AND TRY AGAIN
VOLLEND  DS    0H                  -
         LR    R15,R4              GET THE LENGTH
         SR    R15,R1              OF THE FIRST FIELD
         BNP   BADPARM             ZERO LENGTH IS BAD
         C     R15,=F'6'           IS IT TOO BIG?
         BH    BADPARM             YES...BAG IT
         BCTR  R15,0               DECREMENT LENGTH FOR EXECUTE
         MVI   VOLUME,C' '         BLANK OUT THE VOLSER SLOT
         MVC   VOLUME+1(L'VOLUME-1),VOLUME  ...
MVCINS1  MVC   VOLUME(*-*),0(R1)   *** EXECUTED INSTRUCTION ***
         EX    R15,MVCINS1         MOVE THE VOLUME SERIAL NUMBER
         B     CREATEDS            GO CREATE THE DATASET
*   FLAG  BAD JCL PARAMETER
BADPARM  WTO   'CLIP: THE FORMAT OF THE JCL PARAMETER IS INCORRECT',   X
               ROUTCDE=2
         LA    R15,28              SET BAD RETURN CODE
         B     EXIT                GO BAIL OUT
CREATEDS DS    0H                  -
         OPEN  (SYSIN,(OUTPUT))    OPEN THE DATASET
         TM    SYSIN+(DCBOFLGS-IHADCB),DCBOFOPN  DID IT OPEN?
         BZ    NOSYSIN             NO...ERROR
*   PUT THE ICKDSF CONTROL CARD
         PUT   SYSIN,DSFCARD       PUT THE CARD
*   CLOSE THE FILE
         CLOSE (SYSIN,)            CLOSE IT
         B     CALLPROG            GO CALL ICKDSF
*   FLAG OPEN ERROR ON SYSIN FILE
NOSYSIN  WTO   'CLIP: THE SYSIN FILE DID NOT OPEN',                    X
               ROUTCDE=2
         LA    R15,28              SET BAD RETURN CODE
         B     EXIT                GO BAIL OUT
CALLPROG DS    0H                  -
         LA    R1,DUMMYPRM         POINT TO DUMMY PARM
         LINK  EP=ICKDSF           CALL THE PROGRAM
         LR    R3,R15              SAVE THE RETURN CODE
         CVD   R3,DWORK            CONVERT THE RETURN CODE TO DECIMAL
         MVC   RETCODE,=XL4'40202120'  SET UP RETURN CODE EDIT MASK
         ED    RETCODE,DWORK+6     EDIT IN THE RETURN CODE
RCMSG    WTO   'CLIP: ICKDSF RETURN CODE ISXXXX',                      X
               ROUTCDE=2
RETCODE  EQU   RCMSG+8+27,4,C'C'   MESSAGE INSERT FOR RETURN CODE
SCANSYSP DS    0H                  -
         OPEN  (SYSPRINT,(INPUT))     OPEN THE DATASET
         TM    SYSPRINT+(DCBOFLGS-IHADCB),DCBOFOPN  DID IT OPEN?
         BZ    NOSYSPR             NO...ERROR
*   READ ALL THE SYSPRINT RECORDS
         GET   SYSPRINT            GET THE FIRST RECORD
READLOOP DS    0H                  -
         LH    R15,0(R1)           R15 = LENGTH OF RECORD
         C     R15,=F'9'           IS IT LONG ENOUGH TO BE A MESSAGE?
         BL    SKIPREC             NO...SKIP IT
         LA    R1,5(R1)            POINT PAST THE RDW AND CARRIAGE CTL
         S     R15,=F'5'           AND ADJUST LENGTH
         CLC   =CL3'ICK',0(R1)     IS THIS A POTENTIAL ICK MESSAGE?
         BNE   SKIPREC             NO...SKIP IT
         CLI   3(R1),C'0'          IS IT ICKN, WHERE N IS NUMERIC?
         BL    SKIPREC             NO...SKIP IT
         MVI   DSFMSG,C' '         CLEAR OUT THE MESSAGE SLOT
         MVC   DSFMSG+1(L'DSFMSG-1),DSFMSG  ...
         C     R15,=A(L'DSFMSG)    IS THE LENGTH TOO BIG?
         BNH   OKLEN               NO...LEAVE IT
         L     R15,=A(L'DSFMSG)    YES...MAKE IT RIGHT
OKLEN    DS    0H                  -
         BCTR  R15,0               DECREMENT FOR EXECUTE
MVCINS3  MVC   DSFMSG(*-*),0(R1)   *** EXECUTED INSTRUCTION ***
         EX    R15,MVCINS3         MOVE THE MESSAGE TO THE WTO
DSFWTO   WTO   'CLIP: XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
               XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX',ROUTCDE=2
DSFMSG   EQU   DSFWTO+8+6,80,C'C'  MESSAGE INSERT FOR ICKDSF MESSAGE
SKIPREC  DS    0H                  -
         GET   SYSPRINT            GET THE NEXT
         B     READLOOP            AND TRY AGAIN
SYSEOF   DS    0H                  -
*   CLOSE THE FILE
         CLOSE (SYSPRINT,)         CLOSE IT
         LR    R15,R3              PUT BACK THE RETURN CODE
         B     EXIT                GO BAIL OUT
*   FLAG OPEN ERROR ON SYSPRINT FILE
NOSYSPR  WTO   'CLIP: THE SYSPRINT FILE DID NOT OPEN',                 X
               ROUTCDE=2
         LA    R15,28              SET BAD RETURN CODE
         B     EXIT                GO BAIL OUT
EXIT     DS    0H                  -
         L     R13,4(R13)          POINT TO CALLER'S SAVE AREA
         L     R14,12(R13)         RESTORE REGISTER 14
         LM    R0,R12,20(R13)      RESTORE REGISTERS 0 - 12
         MVI   12(R13),X'FF'       FLAG SAVE AREA
         BR    R14                 RETURN TO O.S.
DWORK    DS    D                   DOUBLEWORD WORK AREA
SAVEAREA DS    18F                 SAVEAREA
DSFCARD  DC    CL80' '             ICKDSF CONTROL CARD
         ORG   DSFCARD             ORG BACK TO REDEFINE FIELDS
         DC    C'  REFORMAT UNITADDRESS('
UNIT     DC    C'UUU'              UNIT ADDRESS
         DC    C') VOLID('
VOLUME   DC    C'VVVVVV'           VOLUME SERIAL NUMBER
         DC    C') NOVERIFY'
         ORG   ,                   RESET LOCATION COUNTER
DUMMYPRM DS    H'0'                DUMMY PARM FOR ICKDSF
SYSIN    DCB   DDNAME=SYSIN,                                           X
               MACRF=(PM),                                             X
               DSORG=PS,                                               X
               RECFM=F,                                                X
               LRECL=80,                                               X
               BLKSIZE=80
SYSPRINT DCB   DDNAME=SYSPRINT,                                        X
               MACRF=(GL),                                             X
               DSORG=PS,                                               X
               RECFM=VBA,                                              X
               EODAD=SYSEOF
         LTORG ,
         END   CLIP


Lynn Grant
Technical Consultant (USA)    © Xephon 1987


























































