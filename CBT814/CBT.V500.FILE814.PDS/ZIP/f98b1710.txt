Duplicate PDS library member detection

One of the essential activities associated with the introduction of
new PDS members into a multiple library concatenation set is that of
ensuring that the new member names are unique across the libraries
involved.  In some cases, the number of libraries which must be
included in the verification process can be considerable.  For
example, the addition of a new TSO CLIST to a library in the SYSPROC
concatenation requires uniqueness verification across not only the
SYSPROC concatenation set but also LNKLST and LPALST libraries, to
ensure that a load module of the same name as the CLIST is not
erroneously invoked as a command processor when the CLIST name is
entered without a '%' prefix.  The number of member names involved
can also be significant, not uncommonly numbering in the hundreds or
even thousands when system load libraries are included.  Under these
conditions, a utility which carries out the verification process with
maximum speed and efficiency can be singularly attractive.

MULTIMEM is a utility which attempts to address these requirements.
It reads the directories of an unlimited number of PDS libraries
directly using buffered QSAM and invokes SORT to collect any
duplicate entries for identification purposes.  Libraries are specified
in the JCL with a DDNAME prefix of 'PDS' and can have any DCB
attributes.  Report output is directed to the MULTIPRT data set.
Dataset open-related errors are reported via WTO; any others are
directed to MULTIPRT.  VIO assists in minimising turnaround time.

MULTIMEM CSECT
         STM   R14,R12,12(R13)             ENTRY
         LR    R12,R15
         USING MULTIMEM,R12
         ST    R13,SAVEAREA+4
         LA    R15,SAVEAREA
         ST    R15,8(R13)
         LA    R13,SAVEAREA
         B     MULT0010
RETURN   DS    0H                          EXIT
         SR    R15,R15
         L     R13,4(R13)
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14
MULT0010 DS    0H
         OPEN  (SORTIN,(OUTPUT),MULTIPRT,(OUTPUT))
         TM    SORTIN+(DCBOFLGS-IHADCB),DCBOFOPN CHECK OPEN
         BO    MULT0100                    OK
         MVC   WTX+8(45),OPENERR
         MVC   WTX+30(8),=CL8'SORTIN'      MOVE DDNAME
         BAL   R14,WTO                     WTO THE MESSAGE
         LA    R15,12                      SET RETURN CODE
         B     RETURN+2                    QUIT
MULT0100 DS    0H
         TM    MULTIPRT+(DCBOFLGS-IHADCB),DCBOFOPN CHECK OPEN
         BO    MULT0200                    OK
         MVC   WTX+8(45),OPENERR
         MVC   WTX+30(8),=CL8'MULTIPRT'    MOVE DDNAME
         BAL   R14,WTO                     WTO THE MESSAGE
         LA    R15,12                      SET RETURN CODE
         B     RETURN+2                    QUIT
MULT0200 DS    0H
         MVI   PRTLINE,C'1'                NEW PAGE
         LA    R7,MULTIPRT                 DCB ADDRESS
         LA    R8,PRTLINE                  BUFFER ADDRESS
         LA    R10,L'PRTLINE               BUFFER LENGTH
         MVC   PRTLINE+33(15),HEADING1
         BAL   R14,PUTREC                  PRINT HEADING
         MVC   PRTLINE+33(15),DASHES
         BAL   R14,PUTREC                  PRINT HEADING
         MVC   PRTLINE+24(33),HEADING2
         BAL   R14,PUTREC                  PRINT HEADING
         MVC   PRTLINE+24(33),DASHES
         BAL   R14,PUTREC                  PRINT HEADING
         BAL   R14,PUTREC                  PRINT BLANK LINE
         MVC   PRTLINE+01(19),HEADING3
         MVC   PRTLINE+26(27),HEADING4
         BAL   R14,PUTREC                  PRINT HEADING
         MVC   PRTLINE+01(19),DASHES
         MVC   PRTLINE+26(27),DASHES
         BAL   R14,PUTREC                  PRINT HEADING
         L     R3,PSATOLD-PSA              TCB ADDRESS
         ICM   R3,15,TCBTIO-TCB(R3)        TIOT ADDRESS
         LA    R3,TIOELNGH-TIOT1(R3)       START OF DD ENTRIES
MULT0300 DS    0H
         CLC   =F'0',0(R3)                 END OF ENTRIES?
         BE    MULT1000                    YES
         CLC   =C'PDS',TIOEDDNM-TIOELNGH(R3) DDNAME START WITH 'PDS'?
         BE    MULT0400                    YES
MULT0350 DS    0H
         LA    R2,0
         IC    R2,0(R3)                    GET LENGTH BYTE
         LA    R3,0(R2,R3)                 NEXT TIOT ENTRY
         B     MULT0300                    RECYCLE
MULT0400 DS    0H
         AP    PDSDDNMS,=PL1'1'            INCREASE DDNAMES FOUND COUNT
         MVC   PDS+DCBDDNAM-IHADCB(8),TIOEDDNM-TIOELNGH(R3) MOVE DDNAME
         RDJFCB (PDS)                      GET THE JFCB
         OPEN  (PDS,(INPUT))               OPEN
         TM    PDS+(DCBOFLGS-IHADCB),DCBOFOPN CHECK OPEN
         BO    MULT0600                    OK
         MVC   WTX+8(45),OPENERR
         MVC   WTX+30(8),PDS+DCBDDNAM-IHADCB MOVE DDNAME
         BAL   R14,WTO                     WTO THE MESSAGE
         LA    R15,12                      SET RETURN CODE
         B     RETURN+2                    QUIT
MULT0600 DS    0H
         LA    R7,PDS                      DCB ADDRESS
         BAL   R14,GETREC                  GET DIRECTORY BLOCK
         LR    R4,R1                       START OF BLOCK
         AH    R4,0(R1)                    PAST END OF BYTES USED
         LA    R5,2(R1)                    FIRST ENTRY
MULT0700 DS    0H
         CLC   0(8,R5),=8X'00'             ANY NAME?
         BE    MULT0800                    NO
         CLC   0(8,R5),=8X'FF'             LAST ENTRY?
         BE    MULT0900                    YES, OUT
         SR    R6,R6
         IC    R6,11(R5)                   NO. OF USER DATA HALFWORDS
         N     R6,=X'0000001F'             LEAVE LENGTH BITS (3 - 7)
         AR    R6,R6                       DOUBLE TO GET NO. OF BYTES
         AH    R6,=H'11'                   ADD 12 FOR NO. OF BYTES PRE-
*                                          CEDING USER DATA FIELDS LESS
*                                          1 FOR EXECUTE COMMAND
         EX    R6,MOVDIREN                 MOVE THE DIRECTORY ENTRY
         LA    R6,1(R6)                    RESET TO ACTUAL LENGTH
         MVC   BUFFER1+80(44),JFCBDSNM     MOVE DSNAME FOR JFCB
         LA    R7,SORTIN                   INITIALIZE REGS FOR...
         LA    R8,BUFFER1                  ...GENERALIZED PUT
         LA    R10,L'BUFFER1
         MVI   FILLCHAR,X'EE'
         BAL   R14,PUTREC                  PUT DIRECTORY ENTRY
MULT0800 DS    0H
         AR    R5,R6                       POINT TO NEXT ENTRY
         CR    R5,R4                       END OF USED BYTES?
         BL    MULT0700                    NO, PROCESS NEXT ENTRY
         B     MULT0600                    YES, PROCESS NEXT BLOCK
MULT0900 DS    0H
         CLOSE (PDS)                       CLOSE THIS PDS
         B     MULT0350                    NEXT TIOT ENTRY
MULT1000 DS    0H
         MVI   FILLCHAR,C' '
         CLOSE (SORTIN)                    CLOSE SORTIN
         LA    R7,MULTIPRT                 DCB ADDRESS
         LA    R8,PRTLINE                  BUFFER ADDRESS
         LA    R10,L'PRTLINE               BUFFER LENGTH
         CP    PDSDDNMS,=PL2'0'            ANY QUALIFYING DDNAMES?
         BNE   MULT1050                    YES
         BAL   R14,PUTREC                  PRINT BLANK LINE
         MVC   PRTLINE+01(42),NOPDSDDN
         BAL   R14,PUTREC                  PRINT MESSAGE
         B     MULT1600
MULT1050 DS    0H
         LA    R1,SORTPAL                  POINT TO SORT PARAMETER LIST
MULT1100 DS    0H
         LINK  EP=SORT                     CALL SORT
         LTR   R15,R15                     SORT OK?
         BZ    MULT1200                    YES
         LR    R2,R15                      SAVE SORT RETURN CODE
         BAL   R14,PUTREC                  PRINT BLANK LINE
         MVC   PRTLINE+01(32),SORTERR
         BAL   R14,PUTREC                  PRINT MESSAGE
         LR    R15,R2                      RESTORE SORT RETURN CODE
         B     RETURN+2
MULT1200 DS    0H
         OPEN  (SORTOUT,(INPUT))           OPEN SORTED RECORDS
MULT1300 DS    0H
         LA    R7,SORTOUT                  LOAD DCB ADDRESS
         BAL   R14,GETREC                  GET A RECORD
         CLC   SAVEMNAM,0(R1)              DUPLICATE NAME?
         BNE   MULT1400                    NO
         OI    FLAG1,DUPLICAT              INDICATE DUPLICATE FOUND
         LR    R2,R1                       SAVE CURRENT BUFFER ADDRESS
         LA    R7,MULTIPRT                 LOAD DCB ADDRESS
         BAL   R14,PUTREC                  PRINT BLANK LINE
         MVC   PRTLINE+01(8),SAVEMNAM      MOVE SAVED MEMBER NAME
         MVC   PRTLINE+26(44),SAVEDSNM     MOVE SAVED DSNAME
         BAL   R14,PUTREC                  PRINT LINE
MULT1350 DS    0H
         MVC   PRTLINE+26(44),80(R2)       MOVE CURRENT DSNAME
         BAL   R14,PUTREC                  PRINT LINE
         LA    R7,SORTOUT                  LOAD DCB ADDRESS
         BAL   R14,GETREC                  GET NEXT RECORD
         CLC   SAVEMNAM,0(R1)              DUPLICATE NAME?
         BNE   MULT1400                    NO
         LR    R2,R1                       LOAD INPUT BUFFER ADDRESS
         LA    R7,MULTIPRT                 DCB ADDRESS
         B     MULT1350                    RECYCLE
MULT1400 DS    0H
         MVC   SAVEMNAM,0(R1)
         MVC   SAVEDSNM,80(R1)
         B     MULT1300                    RECYCLE
MULT1500 DS    0H
         TM    FLAG1,DUPLICAT              ANY DUPLICATES FOUND?
         BO    MULT1550                    YES, SKIP 'NOT FOUND' MSG
         LA    R7,MULTIPRT                 DCB ADDRESS
         BAL   R14,PUTREC                  PRINT BLANK LINE
         MVC   PRTLINE+01(29),NOMULTM
         BAL   R14,PUTREC                  PRINT MESSAGE
MULT1550 DS    0H
         CLOSE (SORTOUT)                   CLOSE REMAINING FILES
MULT1600 DS    0H
         CLOSE (MULTIPRT)
         B     RETURN                      RETURN
* * * -     B R A N C H - A N D - L I N K    R O U T I N E S   - * * *
*           G E N E R A L I Z E D    G E T    R O U T I N E          *
GETREC   DS    0H
         ST    R14,R14SAVE
         GET   (R7)
         L     R14,R14SAVE
         BR    R14
*           G E N E R A L I Z E D     P U T    R O U T I N E          *
PUTREC   DS    0H
         ST    R14,R14SAVE
         PUT   (R7),(R8)                   R7 -> DCB, R8 -> BUFFER
         MVC   0(1,R8),FILLCHAR            MOVE FILL CHARACTER
         BCTR  R10,0                       R10 CONTAINS BUFFER LENGTH
         BCTR  R10,0
         EX    R10,INITBUF
         LA    R10,2(R10)                  BACK TO ORIGINAL LENGTH
         L     R14,R14SAVE
         BR    R14                         RETURN
*        G E N E R A L I Z E D    W T O    R O U T I N E              *
WTO      DS    0H
         ST    R14,R14SAVE                 SAVE RETURN ADDRESS
WTX      WTO   '                                                       X
                                                                       X
                        '                  120 CHARACTERS AVAILABLE
         MVC   WTX+8(1),FILLCHAR           GET FILL CHARACTER
         MVC   WTX+9(119),WTO+8            FILL BUFFER
         L     R14,R14SAVE                 LOAD RETURN ADDRESS
         BR    R14                         RETURN
*            E X E C U T E D   I N S T R U C T I O N S               *
INITBUF  MVC   1(0,R8),0(R8)     EXECUTED MOVE TO INITIALIZE A BUFFER
MOVDIREN MVC   BUFFER(0),0(R5)   MOVE DIRECTORY ENTRY FROM BLOCK
*                         D A T A    A R E A                         *
SAVEAREA DC    18F'0'
R14SAVE  DC    F'0'
FILLCHAR DC    C' '
PDSDDNMS DC    PL2'0'
PRTLINE  DC    CL080' '          PRINT LINE
BUFFER1  DC    CL130' '          WORK BUFFER
         ORG   BUFFER1
         DC    (L'BUFFER1)X'EE'  INITIALIZE
         ORG
FLAG1    DC    X'0'
DUPLICAT EQU   X'80'
SAVEMNAM DC    CL8' '
SAVEDSNM DC    CL44' '
SORTPAL  DC    X'80',AL3(SORTPRM)
         CNOP  2,4
SORTPRM  DC    AL2(SORTPRME-SORTPRMB)
SORTPRMB DC    A(SORTB)
         DC    A(SORTE)
         DC    A(RECORDB)
         DC    A(RECORDE)
         DC    A(0)
         DC    A(0)
SORTPRME DS    0H
SORTB    DC    C' SORT FIELDS=(1,8,CH,A,80,44,CH,A)'
SORTE    DC    C' '
RECORDB  DC    C' RECORD TYPE=F'
RECORDE  DC    C' '
OPENERR  DC    C'OPEN ERROR ON DDNAME ''XXXXXXXX'' - TERMINATING'
SORTERR  DC    C'ERROR CALLING SORT - TERMINATING'
NOPDSDDN DC    C'NO DDNAMES BEGINNING WITH ''PDS'' WERE FOUND'
NOMULTM  DC    C'NO MULTIPLICATE MEMBERS FOUND'
HEADING1 DC    C'M U L T I M E M'
HEADING2 DC    C'FIND MULTIPLICATE LIBRARY MEMBERS'
HEADING3 DC    C'MULTIPLICATE MEMBER'
HEADING4 DC    C'LIBRARIES CONTAINING MEMBER'
DASHES   DC    33C'-'
PDS      DCB   DDNAME=DUMMY,MACRF=GL,DSORG=PS,RECFM=F,LRECL=256,       X
               BLKSIZE=256,DEVD=DA,EODAD=MULT0900,BUFNO=32,EXLST=EXLST
SORTIN   DCB   DDNAME=SORTIN,MACRF=PM,DOSRG=PS,DEVD=DA,RECFM=FB,       X
               LRECL=130
SORTOUT  DCB   DDNAME=SORTOUT,MACRF=GL,DSORG=PS,DEVD=DA,EODAD=MULT1500
MULTIPRT DCB   DDNAME=MULTIPRT,MACRF=PM,DSORG=PS,DEVD=DA,RECFM=FBA,    X
               LRECL=80,BLKSIZE=4080
EXLST    DC    0F'0',AL1(7),AL3(INFMJFCB)
         IEFJFCBN
         LTORG
*     C O N T R O L    B L O C K    M A P P I N G    D S E C T S     *
         IHAPSA
         IKJTCB
         DSECT
         IEFTIOT1
         DCBD  DSORG=PS,DEVD=DA
         END


    © Xephon 1990
