A SYSOUT display facility
An effective table-based job output (SYSOUT) display is made possible by using
the features of TSO Extensions Release 3.  This can be used to provide an
alternative to purchasing products such as SDSF, or to provide a customised
facility to end users.  The facility shown here is achieved with a single CLIST,
one table panel, and two tutorials.
The following facility consists of a fully table-based ISPF dialogue and avoids
repetitive CLIST code in place of a processing loop.  Multiple selections are
supported and the dialogue is consistent with the other ISPF table displays that
users are already familiar with, such as member lists.
TABLE DISPLAY PANEL (ESCOUTX)
)ATTR
  £ TYPE(OUTPUT) INTENS(LOW) JUST(ASIS)
)BODY
%DATE &ZDATE ------------- SYSOUT DISPLAY FACILITY ----------------------------
%COMMAND ===>_ZCMD                                            SCROLL ===>_AMT +
%
%FINAL STATUS :       DEST ===>_ESCDEST   %CLASS ===>_Z+
%
%S+BROWSE  %R+ROUTE  %P+PURGE  %C+CANCEL  %N+NEWCLASS (NON HELD)  %X+EXC+
%
%  JOB NAME   JID        STATUS+                       %RESULT
+------------------------------------------------------------------------------
)MODEL
_Z£Z         £Z         £Z                             £Z
)INIT
 .ZVARS = '(ESCCLAS SEL ESCJOB ESCJID ESCRST ESCACT)'
 IF (&ESCDEST = &Z)
    &ESCDEST = ABCNODE   /* DEFAULT NODE FOR NEW DEST */
 IF (&ESCCLAS = &Z)
    &ESCCLAS = A        /* DEFAULT CLASS FOR NEW CLASS */
 .AUTOSEL = NO
 .HELP = ESCOUTY
 &AMT = &ZSCML
)PROC
 IF (&SEL = R,r)
    VER (&ESCDEST,NB,DSNAME)
 IF (&SEL = N,n)
    VER (&ESCCLAS,NB)
 VPUT (ESCDEST ESCCLAS) PROFILE
)END
TABLE DISPLAY TUTORIAL (ESCOUTY)
%TUTORIAL ------------------ %SYSOUT DISPLAY FACILITY%----------------TUTORIAL+
%COMMAND ===>_ZCMD
+
+ This facility allows you to display jobs in an ISPF table format and
+ then enter selections against one or more jobs. No special programs
+ are used to provide this feature other than standard IBM commands.
+
% Selection+of jobnames for display:
+
% By default+a list of jobs starting with your userid plus one character
+ are displayed (this is the same list as issued by the TSO command ST).
+
+ You may enter%RES jobname+to display jobs with any jobname but you may
+ not be able to process them if the system exit IKJEFF53 denies access.
+ This command is entered in the command field, RES without an operand
+ will redisplay the jobs starting with your userid again.
+
+ Pressing%enter+without any other selections or commands will update
+ the display and redisplay the jobs as currently on the system queues.
+ This allows you to confirm that any selections such as purge have
+ taken effect or to see if a job has finished executing and so on.
+
+ Press%enter+ to view the next page of this tutorial, or END to return.
)PROC
   &ZCONT = ESCOUTZ
)END
TABLE DISPLAY TUTORIAL (ESCOUTZ)
%TUTORIAL ----------------- &SYSOUT DISPLAY FACILITY%---------------- TUTORIAL+
%COMMAND ===>_ZCMD
+
% Processing+of jobnames displayed:
+
+ Selections are issued by entering the appropriate character next to the
+ desired jobname. More than one selection may be made and these will be
+ processed in sequence. Currently available options are displayed on
+ table panel.%BROWSE+will only operate on jobs in held output status
+ (that is in a held sysout class), the status display cannot indicate
+ if the output is held or not. Similarly it is not possible to ROUTE or
+ NEWCLASS output that is not held. EXC deletes the table line not the job.
+
% ROUTE+will use the value in the DEST field as well the CLASS value and
+ the dest can only be specified as up to eight characters with the same
+ restrictions as for the DEST operand of the TSO command OUTPUT.
+
% NEWCLASS+can be used to requeue output for the MVS system printer.
+
+ Press%enter+ to restart this tutorial, or END to return to display.
)PROC
   &ZCONT = ESCOUTY
)END
MAIN CLIST (ESCOUT)
PROC 0 LEVEL()
CONTROL MAIN NOFLUSH
PROFILE NOMSGID
/************************************************************/
/*                          ESCOUT                          */
/*                                                          */
/* TSO E R3 DEPENDENT                                       */
/*                                                          */
/* CLIST TO PROVIDE BETTER WAY OF HANDLING SYSOUT IN THE    */
/* ABSENCE OF SDSF BY TRAPPING THE STATUS COMMAND OUTPUT    */
/* AND LISTING THE JOBNAMES IN AN ISPF TABLE FORMAT FOR     */
/* SELECTION OF BROWSE, PRINT, DELETE AND SO ON.            */
/* SEE TUTORIAL DURING DISPLAY FOR COMMAND FORMATS ETC.     */
/* NOTE ST CMD ONLY LOOKS FOR USERID PLUS ONE CHAR JOBNAMES */
/************************************************************/
ISPEXEC VGET (ZSCREEN) ASIS
SET &TABNAM = JOBTT&ZSCREEN
SET &DSNAME = &SYSUID..TEMP.SYSOUT.SCR&ZSCREEN
RESTART:+
SET &FLAG  =
ISPEXEC TBCREATE &TABNAM KEYS(ESCJOB ESCJID)   +
        NAMES(ESCRST ESCACT) NOWRITE REPLACE
SET &ESCACT  =
SET &I = 1
SET &SYSOUTTRAP = 120   /* MAX 120 JOBNAMES ALLOWED FOR CAN BE UPPED */
STATUS &LEVEL
SET &BOT = &SYSOUTLINE
DO WHILE &BOT >= &I
   SET &TEMP = &&SYSOUTLINE&I
   GOTO SCAN
   NEXTLINE:+
   SET &I = &I + 1
END
GOTO TABLE
/* SCAN STATUS OUTPUT PER LINE */
SCAN:+
IF &FLAG = Y THEN +
   DO
   SET &ESCJOB = &SUBSTR(5:&F1,&TEMP)
   SET &ESCJID = &CH&SUBSTR(&F4:&F2,&TEMP)
   SET &XLEN   = &LENGTH(&TEMP)
   SET &ESCRST = &SUBSTR(&F5:&XLEN,&TEMP)
   ISPEXEC TBADD &TABNAM
   GOTO NEXTLINE
   END
IF &SUBSTR(1:6,&TEMP) = &STR(STATUS) THEN GOTO NEXTLINE
IF &LEVEL = THEN +
   IF &SUBSTR(1:7,&TEMP) = &STR(NO JOBS) THEN +
      DO
      SET &I = 300
      GOTO NEXTLINE
      END
IF &LEVEL ^=THEN +
   DO
   SET &LLEN = &LENGTH(&LEVEL) + 8
   SET &J    = &LLEN - 2
   IF &SUBSTR(1:&LLEN,&TEMP) = &STR(JOB &LEVEL NOT) THEN +
      DO
      SET &I = 300
      GOTO NEXTLINE
      END
   END
SET &XLEN = &LENGTH(&TEMP)
IF &LEVEL = THEN +
   SET &J = &LENGTH(&SYSUID) + 7
SET &F1 = &SYSINDEX(JOB,&TEMP,&J)
IF &F1 > 0 THEN +
   SET &CH = J
ELSE +
   DO
   SET &F1 = &SYSINDEX(TSU,&TEMP,&J)
   IF &F1 > 0 THEN +
      SET &CH = T
   ELSE +
      DO
      SET &F1 = &SYSINDEX(STC,&TEMP,&J)
      SET &CH = S
      END
   END
SET &J  = &F1 + 7
SET &F1 = &F1 - 2
SET &F2 = &SYSINDEX(&STR( ),&TEMP,&J) - 2
SET &ESCJOB = &SUBSTR(5:&F1,&TEMP)
SET &F4 = &F1 + 5
SET &ESCJID = &CH&SUBSTR(&F4:&F2,&TEMP)
SET &F5 = &F2 + 3
SET &ESCRST = &SUBSTR(&F5:&XLEN,&TEMP)
SET &FLAG = Y
ISPEXEC TBADD &TABNAM
GOTO NEXTLINE
/* TABLE DISPLAY SECTION */
TABLE:+
IF &SYSDSN('&DSNAME') ^= OK THEN -
   ALLOC DA('&DSNAME') LRECL(133) BLKSIZE(6118) -
         RECFM(F B) DSORG(PS) TRACKS SP(15 50) NEW
SET &SEL =
SET &JUMP =
ISPEXEC TBTOP   &TABNAM
ISPEXEC TBDISPL &TABNAM PANEL(ESCOUTX)
SET &RETC = &LASTCC
DO WHILE &RETC < 8
   ISPEXEC CONTROL DISPLAY SAVE
   IF (&STR(&ZCMD) =) AND (&SEL ^=) THEN GOTO SKIP
   IF &STR(&ZCMD) = THEN +
      DO
      SET &JUMP = Y
      SET &RETC = 8
      GOTO LOOPEND
      END
   SET &CLEN = &LENGTH(&STR(&ZCMD))
   IF &CLEN >= 3 THEN +
      IF &SUBSTR(1:3,&ZCMD) = RES THEN +
         DO
         SET &LEVEL =
         IF &CLEN >= 5 THEN +
            SET &LEVEL = &SUBSTR(5:&CLEN,&ZCMD)
         SET &JUMP = Y
         SET &RETC = 8
         GOTO LOOPEND
         END
   SKIP:+
   IF &SEL ^= THEN +
      DO
      SET &RETL = NEXTDIS1
      GOTO PROCESS
      NEXTDIS1:+
      ISPEXEC TBPUT   &TABNAM
      END
   DO WHILE &RETC = 4
      ISPEXEC CONTROL DISPLAY RESTORE
      ISPEXEC TBDISPL &TABNAM
      SET &RETC = &LASTCC
      ISPEXEC CONTROL DISPLAY SAVE
      IF &SEL ^= THEN +
         DO
         SET &RETL = NEXTDIS2
         GOTO PROCESS
         NEXTDIS2:+
         ISPEXEC TBPUT    &TABNAM
         END
   END
   SET &SEL =
   ISPEXEC TBTOP   &TABNAM
   ISPEXEC TBSKIP  &TABNAM NUMBER(&ZTDTOP)
   ISPEXEC CONTROL DISPLAY RESTORE
   ISPEXEC TBDISPL &TABNAM PANEL(ESCOUTX)
   SET &RETC = &LASTCC
   LOOPEND:+
END
ISPEXEC TBEND &TABNAM
IF &JUMP = Y THEN GOTO RESTART
FREE DA('&DSNAME')
DELETE '&DSNAME'
EXIT QUIT
/* SELECTION OPTION PROCESSING */
PROCESS:+
IF &ESCJID = THEN -
   SET &ESCOUT = &ESCJOB
ELSE -
   SET &ESCOUT = &STR(&ESCJOB(&ESCJID))
SET &SEL = &SEL
IF &SEL = S THEN GOTO BROWSE
IF &SEL = R THEN GOTO ROUTE
IF &SEL = P THEN GOTO PURGE
IF &SEL = N THEN GOTO NEWC
IF &SEL = C THEN GOTO CANCEL
IF &SEL = X THEN GOTO EXCLUDE
ISPEXEC SETMSG MSG(ISPZ001)
GOTO &RETL
/* BROWSE OUTPUT */
BROWSE:+
OUT &ESCOUT PRINT('&DSNAME') KEEP HOLD
IF &LASTCC = 0 THEN -
   DO
   ISPEXEC CONTROL ERRORS RETURN
   ISPEXEC BROWSE DATASET('&DSNAME')
   IF &LASTCC > 0 THEN +
      DO
      ISPEXEC SETMSG MSG(ISPZ001)
      SET &ESCACT = &STR(BROWSE FAILED)
      END
   ELSE +
      SET &ESCACT = &STR(JOB BROWSED)
   ISPEXEC CONTROL ERRORS CANCEL
   END
ELSE +
   DO
   ISPEXEC SETMSG MSG(ISPZ001)
   SET &ESCACT = &STR(OUTPUT COMMAND FAILED)
   END
GOTO &RETL
/* ROUTE OUTPUT */
ROUTE:+
OUT &ESCOUT DEST(&ESCDEST) NEWCLASS(&ESCCLAS)
IF &LASTCC = 0 THEN -
  SET &ESCACT = &STR(ROUTED TO &ESCDEST &ESCCLAS)
ELSE +
   DO
   ISPEXEC SETMSG MSG(ISPZ001)
   SET &ESCACT = &STR(ROUTE REQUEST FAILED)
   END
GOTO &RETL
/* NEWCLASS */
NEWC:+
OUT &ESCOUT NEWCLASS(&ESCCLAS)
IF &LASTCC = 0 THEN -
   SET &ESCACT = &STR(NEWCLASS &ESCCLAS)
ELSE +
   DO
   ISPEXEC SETMSG MSG(ISPZ001)
   SET &ESCACT = &STR(CLASS CHANGE FAILED)
   END
GOTO &RETL
/* CANCEL */
CANCEL:+
CANCEL &ESCOUT
IF &LASTCC = 0 THEN +
   SET &ESCACT = &STR(JOB CANCELLED)
ELSE +
   DO
   ISPEXEC SETMSG MSG(ISPZ001)
   SET &ESCACT = &STR(CANCEL COMMAND FAILED)
   END
GOTO &RETL
/* PURGE */
PURGE:+
CANCEL &ESCOUT PURGE
IF &LASTCC = 0 THEN +
   SET &ESCACT = &STR(JOB PURGED)
ELSE +
   DO
   ISPEXEC SETMSG MSG(ISPZ001)
   SET &ESCACT = &STR(PURGE COMMAND FAILED)
   END
GOTO &RETL
/* EXCLUDE */
EXCLUDE:+
ISPEXEC TBDELETE &TABNAM
GOTO &RETL
END


     © Xephon 1988




























































