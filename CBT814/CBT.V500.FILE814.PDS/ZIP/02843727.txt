Configuration information

I provide central MVS software support for a number of sites,
all with potentially different machines and configurations.  In
order to give the most effective support, I need an up-to-date,
overall picture of each installation in terms of software, CPU
model, device configuration etc and I prefer not to have to rely
solely on information supplied by the personnel involved.

Therefore, I have developed the following program which can
be run at each site to provide some of the required information.
Figure 1 shows a typical output listing.  When time permits, I
intend to develop a program which picks up information such
as CPU model and version, real and expanded storage
installed, and the MVS software levels.

This new program and the following program, together with a
program I had published in MVS Update issue 12 (MVS/XA
storage memory map on pages 18-24), which prints a map of
virtual storage, could then be combined to give a
comprehensive picture of any installation.

*     NOTE - SYSLIB MUST POINT TO MACLIB AND AMODGEN
*          - 'PRINT' DD STATEMENT REQUIRED
SYSINFO1 CSECT
SYSINFO1 AMODE ANY
SYSINFO1 RMODE 24
         SAVE  (14,12),,*
         LR    12,15               BASE
         USING SYSINFO1,12,9
         LA    9,4095(12)
         LA    9,1(9)
         ST    13,SAVEAREA+4       CHAIN SAVE AREAS FORWARD
         LA    15,SAVEAREA
         ST    15,8(13)            CHAIN SAVE AREAS BACKWARD
         LA    13,SAVEAREA         ADDRESS OF CURRENT SAVE AREA
         L     11,0(1)             ADDRESS OF POINTER
         USING INFODSCT,11           TO INFORMATION AREA
         USING CVTFIX,10           MAKE CVT ADDRESSABLE
         L     10,CVTPTR
         SH    10,=H'256'          BACKSTEP OVER CVT PREFIX
*  PICK UP SMF ID
         L     5,CVTSMCA           SYSTEM MANAGEMENT CONTROL AREA
         USING SMCABASE,5
         MVC   SMFID,SMCASID
*  PICK UP CPU ID
         USING PCCA,8
         L     5,CVTPCCAT          PHYSICAL CCA VECTOR TABLE
         LA    6,16                CATER FOR 16 CP'S
         LA    7,CPUID00
PCC01    L     8,0(5)              LOOP THROUGH VECTOR TABLE
         LTR   8,8                   AND PICK UP INFORMATION FROM
         BZ    PCC02                   EACH PCCA
         MVC   0(6,7),PCCACPID+2   CPU ID
         B     PCC03
PCC02    MVC   0(6,7),BLANKS
PCC03    LA    5,4(5)
         LA    7,6(7)
         BCT   6,PCC01
*  EXAMINE EACH UCB AND BUILD LIST AND COUNTS OF DEVICE TYPES
*  THIS SECTION IS NOT COMPREHENSIVE - IT CATERS FOR THE DEVICES
*  INSTALLED AT OUR INSTALLATIONS - IF IT NEEDS TO BE EXPANDED
*  REFER TO 'UCBTYP' IN THE 'DATA AREAS' MANUAL.
         USING UCBOB,2
         LA    4,COUNTSTA
         LA    2,3
UCB00000 LA    3,COUNTEND
         ZAP   0(3,4),=P'0'
         BXLE  4,2,UCB00000
UCB00    LA    1,UCBSCANP          PARAMETER LIST
         L     15,CVTUCBSC         ADDRESS OF UCB SCAN SERVICE
         BALR  14,15
         LTR   15,15
         BNZ   UCBEND
         L     2,UCBADDR
         TM    UCBSTAT,UCBONLI     DEVICE ON-LINE ?
         BNO   UCB00
         CLI   UCBTBYT3,UCB3TAPE   TAPE --------------------------
         BNE   UCB01
         CLI   UCBUNTYP,X'03'      3400 ?
         BNE   UCB00A
         AP    TAPE3400,=P'1'
         B     UCB00
UCB00A   CLI   UCBUNTYP,X'80'      3480 ?
         BNE   UCB00B
         AP    TAPE3480,=P'1'
         B     UCB00
UCB00B   AP    TAPEMISC,=P'1'      OTHERS
         B     UCB00
UCB01    CLI   UCBTBYT3,UCB3COMM   COMMUNICATIONS SECTION -------
         BNE   UCB02
         CLI   UCBUNTYP,X'15'      37X5 ?
         BNE   UCB01A
         AP    COMM37X5,=P'1'
         B     UCB00
UCB01A   CLI   UCBUNTYP,X'92'      2701 ?
         BNE   UCB01B
         AP    COMM2701,=P'1'
         B     UCB00
UCB01B   CLI   UCBUNTYP,X'93'      2703 ?
         BNE   UCB01C
         AP    COMM2703,=P'1'
         B     UCB00
UCB01C   AP    COMMMISC,=P'1'      OTHERS
         B     UCB00
UCB02    CLI   UCBTBYT3,UCB3CTC    CHANNEL TO CHANNEL ADAPTER ---
         BNE   UCB03
         AP    CTCADAP,=P'1'
         B     UCB00
UCB03    CLI   UCBTBYT3,UCB3DACC   DIRECT ACCESS SECTION --------
         BNE   UCB04
         CLI   UCBUNTYP,X'0B'      3350 ?
         BNE   UCB03A
         AP    DACC3350,=P'1'
         B     UCB00
UCB03A   CLI   UCBUNTYP,X'0E'      3380 ?
         BNE   UCB03B
         L     3,UCBEXTPT          POINTER TO UCB COMMON EXTENSION
         USING UCBCMEXT,3
         L     4,UCBCLEXT          POINTER TO DEVICE CLASS EXTENSION
         USING DCE,4
         CLI   DCEOBRDT,X'0E'      OBR DEVICE TYPE CODE BYTE 4
         BNE   UCB03A1
         AP    STD3380,=P'1'       STANDARD 3380
         B     UCB00
UCB03A1  CLI   DCEOBRDT,X'1E'
         BNE   UCB03A2
         AP    D3380,=P'1'         3380 D
         B     UCB00
UCB03A2  CLI   DCEOBRDT,X'2E'
         BNE   UCB03A3
         AP    E3380,=P'1'         3380 E
         B     UCB00
UCB03A3  CLI   DCEOBRDT,X'21'
         BNE   UCB03A4
         AP    J3380,=P'1'         3380 J
         B     UCB00
UCB03A4  CLI   DCEOBRDT,X'23'
         BNE   UCB03B
         AP    K3380,=P'1'         3380 K
         B     UCB00
UCB03B   AP    DACCMISC,=P'1'      OTHERS
         B     UCB00
UCB04    CLI   UCBTBYT3,UCB3DISP   DISPLAY SECTION --------------
         BNE   UCB05
         CLI   UCBUNTYP,X'09'      327X ?
         BNE   UCB04A
         AP    DISP327X,=P'1'
         B     UCB00
UCB04A   CLI   UCBUNTYP,X'0B'      3286 ?
         BNE   UCB04B
         AP    DISP3286,=P'1'
         B     UCB00
UCB04B   AP    DISPMISC,=P'1'      OTHERS
         B     UCB00
UCB05    CLI   UCBTBYT3,UCB3UREC   UNIT RECORD SECTION ----------
         BNE   UCB06
         CLI   UCBUNTYP,X'0E'      3800 ?
         BNE   UCB05A
         AP    UREC3800,=P'1'
         B     UCB00
UCB05A   CLI   UCBUNTYP,X'0B'      3203 ?
         BNE   UCB05B
         AP    UREC3203,=P'1'
         B     UCB00
UCB05B   CLI   UCBUNTYP,X'01'      2540 ?
         BNE   UCB05C
         AP    UREC2540,=P'1'
         B     UCB00
UCB05C   CLI   UCBUNTYP,X'09'      3211 ?
         BNE   UCB05D
         AP    UREC3211,=P'1'
         B     UCB00
UCB05D   AP    URECMISC,=P'1'      OTHERS
         B     UCB00
UCB06    CLI   UCBTBYT3,UCB3CHAR   CHARACTER READER SECTION -----
         BNE   UCB07
         AP    CHARRDR,=P'1'
         B     UCB00
UCB07    AP    UCBMISC,=P'1'       UNKNOWN DEVICE CLASS ---------
         B     UCB00
UCBEND   EQU   *
*  CALCULATE AMOUNT OF ONLINE DASD IN GIGABYTES
         ZAP   WORK3,=P'0'
         ZAP   WORK2,DACC3350
         MP    WORK2,=P'317'       M-BYTE PER 3350
         AP    WORK3,WORK2
         ZAP   WORK2,STD3380
         MP    WORK2,=P'630'       M-BYTE PER STD 3380
         AP    WORK3,WORK2
         ZAP   WORK2,D3380
         MP    WORK2,=P'630'       M-BYTE PER 3380D
         AP    WORK3,WORK2
         ZAP   WORK2,E3380
         MP    WORK2,=P'1260'      M-BYTE PER 3380E
         AP    WORK3,WORK2
         ZAP   WORK2,J3380
         MP    WORK2,=P'630'       M-BYTE PER 3380J
         AP    WORK3,WORK2
         ZAP   WORK2,K3380
         MP    WORK2,=P'1890'      M-BYTE PER 3380K
         AP    WORK3,WORK2
         ZAP   DASDGIGA,=P'0'
         MVO   DASDGIGA,WORK3+2(4)
*  BUILD UP THE INFORMATION IN THE PRINT AREA
         MVC   L01+10(4),SMFID
         MVC   L05+10(6),CPUID00
         MVC   L05+17(6),CPUID01
         MVC   L05+24(6),CPUID02
         MVC   L05+31(6),CPUID03
         MVC   L05+38(6),CPUID04
         MVC   L05+45(6),CPUID05
         MVC   L13+13(6),EP
         MVC   L13+22(6),EP
         MVC   L13+31(6),EP
         ED    L13+13(6),TAPE3400
         ED    L13+22(6),TAPE3480
         ED    L13+31(6),TAPEMISC
         MVC   L16+13(6),EP
         MVC   L16+22(6),EP
         MVC   L16+31(6),EP
         ED    L16+13(6),DISP327X
         ED    L16+22(6),DISP3286
         ED    L16+31(6),DISPMISC
         MVC   L19+13(6),EP
         MVC   L19+22(6),EP
         MVC   L19+31(6),EP
         MVC   L19+40(6),EP
         ED    L19+13(6),COMM37X5
         ED    L19+22(6),COMM2701
         ED    L19+31(6),COMM2703
         ED    L19+40(6),COMMMISC
         MVC   L22+13(6),EP
         MVC   L22+22(6),EP
         MVC   L22+31(6),EP
         MVC   L22+40(6),EP
         MVC   L22+49(6),EP
         ED    L22+13(6),UREC3800
         ED    L22+22(6),UREC3203
         ED    L22+31(6),UREC3211
         ED    L22+40(6),UREC2540
         ED    L22+49(6),URECMISC
         MVC   L25+13(6),EP
         MVC   L25+22(6),EP
         MVC   L25+31(6),EP
         MVC   L25+40(6),EP
         MVC   L25+49(6),EP
         MVC   L25+58(6),EP
         ED    L25+13(6),STD3380
         ED    L25+22(6),D3380
         ED    L25+31(6),E3380
         ED    L25+40(6),J3380
         ED    L25+49(6),K3380
         ED    L25+58(6),DACCMISC
         MVC   L27+13(6),EP
         ED    L27+13(6),CHARRDR
         MVC   L29+13(6),EP
         ED    L29+13(6),CTCADAP
         MVC   L31+13(6),EP
         ED    L31+13(6),UCBMISC
         MVC   L34+31(6),EP
         ED    L34+31(6),DASDGIGA+1
*  PRINT THE INFORMATION
         LA    1,AMODE24           SWITCH TO 24-BIT ADDRESSING MODE
         BSM   0,1
AMODE24  DS    0H
         OPEN  (PRINT,(OUTPUT))
         LA    5,L01
         LA    6,70
         LA    7,L99
PRT01    PUT   PRINT,(5)
         BXLE  5,6,PRT01
         CLOSE PRINT
         L     13,4(13)            RECOVER ADDR OF PREVIOUS SAVE AREA
         XR    15,15
        RETURN (14,12),RC=(15)
         LTORG
EP       DC    X'402020202020'
PRINT    DCB   DSORG=PS,MACRF=PM,DDNAME=PRINT
SAVEAREA DS    18F
WORK2    DS    CL8
WORK3    DS    CL8
BLANKS   DC    CL6' '
UCBSCANP DC    A(UCBWK)            UCB SCAN PARAMETERS
         DC    A(UCBDEVCL)
         DC    X'80'
         DC    AL3(UCBADDR)
         DS    0D
UCBWK    DC    XL100'00'           WORK AREA
UCBDEVCL DC    X'00'               REPORT ON ALL DEVICES
UCBADDR  DS    F                   ADDRESS OF UCB
*  PRINT AREA
L01      DC    CL70' SMF ID'
         DC    CL70' ======'
         DC    CL70' '
         DC    CL70' '
L05      DC    CL70' CPU ID'
         DC    CL70' ======'
         DC    CL70' '
         DC    CL70' '
         DC    CL70' ON-LINE DEVICES'
         DC    CL70' ==============='
         DC    CL70' '
         DC    CL70'   TAPE         3400     3480     ????'
         DC    CL70'   ----         ----     ----     ----'
L13      DC    CL70' '
         DC    CL70' '
         DC    CL70'  DISPLAYS      3276     3286     ????'
         DC    CL70'  --------      ----     ----     ----'
L16      DC    CL70' '
         DC    CL70' '
         DC    CL70'  COMMS         37X5     2701     2703     ????'
         DC    CL70'  -----         ----     ----     ----     ----'
L19      DC    CL70' '
         DC    CL70' '
         DC    CL70'  UNIT REC      3800     3203     3211     2540    C
                ????'
         DC    CL70'  --------      ----     ----     ----     ----    C
                ----'
L22      DC    CL70' '
         DC    CL70' '
         DC    CL70'  DASD        3380STD   3380D    3380E    3380J    C
               3380K    ????'
         DC    CL70'  ----        -------   -----    -----    -----    C
               -----    ----'
L25      DC    CL70' '
         DC    CL70' '
L27      DC    CL70'   CHAR RDR'
         DC    CL70'   --------'
         DC    CL70' '
L29      DC    CL70'   CTC'
         DC    CL70'   ---'
         DC    CL70' '
L31      DC    CL70'   UNKNOWN'
         DC    CL70'   -------'
         DC    CL70' '
         DC    CL70' '
L34      DC    CL70' G-BYTES OF ON-LINE DASD'
         DC    CL70' ======================='
L99      DC    CL70' '
         DC    CL70' '
INFODSCT DSECT                     MAP OF INFORMATION PICKED UP
SMFID    DS    CL4                   SMF ID
CPUID00  DS    CL6                   CPU ID - CATER FOR 16
CPUID01  DS    CL6
CPUID02  DS    CL6
CPUID03  DS    CL6
CPUID04  DS    CL6
CPUID05  DS    CL6
CPUID06  DS    CL6
CPUID07  DS    CL6
CPUID08  DS    CL6
CPUID09  DS    CL6
CPUID10  DS    CL6
CPUID11  DS    CL6
CPUID12  DS    CL6
CPUID13  DS    CL6
CPUID14  DS    CL6
CPUID15  DS    CL6
COUNTSTA EQU   *                     START OF UCB COUNTS
TAPE3400 DS    CL3                     TAPE DEVICES
TAPE3480 DS    CL3
TAPEMISC DS    CL3
COMM37X5 DS    CL3                     COMMUNICATIONS DEVICES
COMM2701 DS    CL3
COMM2703 DS    CL3
COMMMISC DS    CL3
CTCADAP  DS    CL3                     CHANNEL TO CHANNEL ADAPTERS
DACC3350 DS    CL3                     DASD DEVICES
STD3380  DS    CL3
D3380    DS    CL3
E3380    DS    CL3
J3380    DS    CL3
K3380    DS    CL3
DACCMISC DS    CL3
DISP327X DS    CL3                     DISPLAY DEVICES
DISP3286 DS    CL3
DISPMISC DS    CL3
UREC3800 DS    CL3                     UNIT RECORD DEVICES
UREC3203 DS    CL3
UREC2540 DS    CL3
UREC3211 DS    CL3
URECMISC DS    CL3
CHARRDR  DS    CL3                     CHARACTER READER DEVICES
UCBMISC  DS    CL3
COUNTEND DS    CL3                   END OF UCB COUNTS
DASDGIGA DS    CL4                   G-BYTES OF ONLINE DASD
         DS    0D
         CVT   DSECT=YES,PREFIX=YES
         IHAPCCA
         IEFUCBOB
         IECDDCE
         IEESMCA
         END


B Peters
Senior Systems Programmer
ICI plc (UK)                         © Xephon 1990

