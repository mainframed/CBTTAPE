Checking the contents of the PPT


PROGRAM SOURCE CODE

         TITLE     'BPPT - LIST PPT PROGRAM ENTRIES.'
*        **************************************************************
*        * PURPOSE:      TO LIST PPT PROGRAM ENTRIES.                 *
*        **************************************************************
BPPT     CSECT
BPPT     AMODE 31
BPPT     RMODE ANY
BPPT0010 EQU   *
         STM   14,12,12(13)
         LR    12,15
         USING BPPT,12
         ST    13,SAVEAREA+4
         LR    2,13
         LA    13,SAVEAREA
         ST    13,8(2)
         MODESET MODE=SUP,KEY=ZERO
         LA    2,PPTENT
         USING PPTENTRY,2
         XC    SCNTOKEN,SCNTOKEN
BPPT0020 EQU   *
         IEFPPSCN REQUEST=NEXT,TOKEN=SCNTOKEN,PPTINFO=PPTE
         LTR   15,15
         BNZ   END0010
         MVC   TEXT2(8),PPTNAME
         WTO   TEXT=MSGTXT
         B     BPPT0020
END0010  EQU   *
         IEFPPSCN REQUEST=END,TOKEN=SCNTOKEN
         MODESET MODE=PROB,KEY=NZERO
         L     13,SAVEAREA+4
         L     14,12(13)
         LM    0,12,20(13)
         BR    14
SCNTOKEN DC    A(0)
PPTE     DC    AL2(L'PPTENTRY)
PPTENT   DS    CL(L'PPTENTRY)
SAVEAREA DC    18F'0'
MSGTXT   DS    0F
TEXTL    DC    H'32'
TEXT1    DC    C'BPPT: PROGRAM NAME = '
TEXT2    DS    CL8
         CVT   DSECT=YES
         IEFJESCT
         IEFZB610
         END


ASSEMBLY AND LINK-EDIT JCL

//         JOB
//ASM1     EXEC  PGM=IEV90,PARM='OBJ,NODECK,XREF(SHORT)'
//SYSLIB   DD    DSN=SYS1.MACLIB,DISP=SHR
//         DD    DSN=SYS1.MODGEN,DISP=SHR
//SYSUT1   DD    UNIT=SYSDA,SPACE=(1700,(600,100))
//SYSUT2   DD    UNIT=SYSDA,SPACE=(1700,(300,50))
//SYSUT3   DD    UNIT=SYSDA,SPACE=(1700,(300,50))
//SYSPRINT DD    SYSOUT=*
//SYSLIN   DD    UNIT=SYSDA,SPACE=(80,(200,50)),DISP=(,PASS)
//SYSIN    DD    DSN=STS01.BD.SOURCE(BPPT),DISP=SHR
//*
//LKED1    EXEC  PGM=IEWL,PARM=('MAP,LET,LIST,NCAL,AC=1'),
//         COND=(0,NE,ASM1)
//SYSUT1   DD    UNIT=SYSDA,SPACE=(1024,(50,20))
//SYSPRINT DD    SYSOUT=*
//SYSLMOD  DD    DSN=SYS3.LINKLIB,DISP=SHR
//SYSLIB   DD    DSN=*.ASML.SYSLIN,DISP=(OLD,DELETE)
//SYSLIN   DD    *
 INCLUDE SYSLIB
 NAME    BPPT(R)
/*


EXECUTION JCL

//         JOB
//        MSGLEVEL=(1,1),NOTIFY=STS01
//*       **************************************************************
//*       * JOB CONTROL TO RUN BPPT IN BATCH.                          *
//*       **************************************************************
//STEP1   EXEC   PGM=BPPT


John Bradley
Senior Systems Programmer (UK)               c Xephon 1996

