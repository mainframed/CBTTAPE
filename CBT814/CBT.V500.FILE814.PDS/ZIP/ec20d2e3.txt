A dynamic storage management routine

In MVS Update, issue 25 (October 1988), an implementation of a 
Last In First Out (LIFO) stack was published.  However, this 
cannot satisfy the storage requirements not known at the time of 
entry to the main program or the storage requirements of 
subroutines.  To avoid the system overhead of frequent 
GETMAIN and FREEMAIN SVCs for such dynamic storage 
requests a storage management routine may be helpful.

The macro DYNAM (calling routine DYNAMRTN) is a highly 
efficient implementation of dynamic storage management 
complementary to the LIFO stack (ie to use this facility the 
STACK macro has to be used too).

Dynamic storage elements are obtained by DYNAM GET calls 
and released by DYNAM FREE calls.  Before executing a 
DYNAM GET the first time, execute a DYNAM INIT.  It obtains 
storage of the requested size and does some initialisation.  Before 
termination of the main program a DYNAM TERM should be 
executed to release the entire storage obtained by DYNAM INIT.

The initial size specified by DYNAM INIT must be sufficient for 
all DYNAM GET calls.  Take into account that every storage 
element has an 8-byte header.  If a free storage element of 
sufficient length is not available to satisfy a DYNAM GET 
request, routine DYNAMRTN tries to join adjacent free storage 
elements (this is sometimes called 'garbage collection'), then 
retries the search.  To avoid frequent garbage collection caused by 
storage fragmentation, increase the initial size.

EXAMPLE

MAIN     CSECT
         ..... for program prologue including STACK INIT and STACK GET
         ..... see MVS Update, issue 25, October 1988
         DYNAM INIT,4096          INIT DYNAMIC STORAGE, SIZE = 4K
         .....
         DYNAM GET,L'BUFFER       GET A BUFFER FROM DYNAMIC STORAGE
         LR    R2,R1              SAVE ITS ADDRESS
         .....
         L     R9,=A(5*L'BUFFER)
         DYNAM GET,(R9)           GET FIVE BUFFERS FROM DYNAMIC STORAGE
         ST    R1,SAVEADDR        SAVE ADDRESS
         .....
         DYNAM FREE,(R2)          FREE BUFFER
         DYNAM FREE,SAVEADDR      FREE FIVE BUFFERS
         .....
         DYNAM TERM               TERMINATE DYNAMIC STORAGE
         ..... for program epilogue including STACK FREE
         ..... see MVS Update, issue 25, October 1988
SAVEADDR DS    A
         DSECT 
BUFFER   DS    CL80
         END   MAIN

Note: although this version of DYNAM is for XA, it can be 
adapted easily for MVS/SP1.  The program DYNAMRTN is re-
entrant and optionally can be brought into LPA.

THE DYNAM MACRO

         MACRO
&NAME    DYNAM
.**********************************************************************
.* PERFORM DYNAMIC STORAGE OPERATIONS INIT / GET / FREE / TERM
.* NOTE: CAN ONLY BE USED IF MACRO STACK IS ALSO USED
.* DYNAM INIT,<LENGTH>   INITIALIZE DYNAMIC STORAGE
.* DYNAM GET,<LENGTH>    GET A DYNAMIC STORAGE ELEMENT
.* DYNAM FREE,<ADDRESS>  FREE A DYNAMIC STORAGE ELEMENT
.* DYNAM TERM            TERMINATE DYNAMIC STORAGE
.* <LENGTH> IS A CONSTANT OR A SYMBOL EQUAL TO THE LENGTH,
.*          OR A REGISTER (2)..(12) CONTAINING THE LENGTH
.* <ADDRESS> IS A STORAGE LOCATION (4 BYTES) CONTAINING THE ADDRESS,
.*           OR A REGISTER (2)..(12) CONTAINING THE ADDRESS
.**********************************************************************
         LCLA  &STKRSRV
&STKRSRV SETA  12                 OFFSET OF RESERVED FIELD IN STACK HDR
         AIF   (N'&SYSLIST GE 1 AND N'&SYSLIST LE 2).DYN010
         MNOTE 8,'INVALID NUMBER OF OPERANDS'
         MEXIT
.DYN010  ANOP
         AIF   ('&SYSLIST(1)' EQ  'INIT').DYN100
         AIF   ('&SYSLIST(1)' EQ  'GET' ).DYN200
         AIF   ('&SYSLIST(1)' EQ  'FREE').DYN300
         AIF   ('&SYSLIST(1)' EQ  'TERM').DYN400
         MNOTE 8,'INVALID OPTION ''&SYSLIST(1)'''
         MEXIT
.DYN100  ANOP                     INIT
         AIF   (N'&SYSLIST EQ 2).DYN120
         MNOTE 8,'MISSING LENGTH OPERAND'
         MEXIT
.DYN120  ANOP
&NAME    LOAD  EP=DYNAMRTN        GET ADDR OF DYNAMIC STORAGE ROUTINE
         LR    15,0               ADDR OF DYNAMIC STORAGE ROUTINE
         SLR   1,1                0 - INDICATE INIT OF DYNAMIC STORAGE
         AGO   .DYN250
.DYN200  ANOP                     GET
         AIF   (N'&SYSLIST EQ 2).DYN220
         MNOTE 8,'MISSING LENGTH OPERAND'
         MEXIT
.DYN220  ANOP
&NAME    L     15,0(,13)          ADDR OF STACK HEADER
         L     15,&STKRSRV.(,15)  ADDR OF DYNAMIC STORAGE HEADER
         L     15,0(,15)          ADDR OF DYNAMIC STORAGE ROUTINE
         LA    1,4(,0)            4 - INDICATE GET
.DYN250  ANOP
         AIF   ('&SYSLIST(2)'(1,1) NE '(').DYN260
         LR    0,&SYSLIST(2,1)    LOAD LENGTH VALUE
         AGO   .DYN290
.DYN260  ANOP
         AIF   (T'&SYSLIST(2) NE 'N').DYN270
         AIF   (&SYSLIST(2) GT 4095).DYN270
         LA    0,&SYSLIST(2)(,0)  LOAD LENGTH VALUE
         AGO   .DYN290
.DYN270  ANOP
         CNOP  0,4
         B     *+8                BRANCH AROUND CONSTANT
         DC    A(&SYSLIST(2))     LENGTH VALUE
         L     0,*-4              LOAD LENGTH VALUE
.DYN290  ANOP
         BASSM 14,15              CALL DYNAMIC STORAGE ROUTINE
         MEXIT
.DYN300  ANOP                     FREE
         AIF   (N'&SYSLIST EQ 2).DYN320
         MNOTE 8,'MISSING ADDRESS OPERAND'
         MEXIT
.DYN320  ANOP
&NAME    L     15,0(,13)          ADDR OF STACK HEADER
         L     15,&STKRSRV.(,15)  ADDR OF DYNAMIC STORAGE HEADER
         L     15,0(,15)          ADDR OF DYNAMIC STORAGE ROUTINE
         LA    1,8(,0)            8 - INDICATE FREE
.DYN350  ANOP
         AIF   ('&SYSLIST(2)'(1,1) NE '(').DYN360
         LR    0,&SYSLIST(2,1)    LOAD ADDRESS VALUE
         AGO   .DYN390
.DYN360  ANOP
         L     0,&SYSLIST(2)      LOAD ADDRESS VALUE
.DYN390  ANOP
         BASSM 14,15              CALL DYNAMIC STORAGE ROUTINE
         MEXIT
.DYN400  ANOP                     TERM
         AIF   (N'&SYSLIST EQ 1).DYN420
         MNOTE 8,'2ND OPERAND NOT ALLOWED FOR OPTION ''TERM'''
         MEXIT
.DYN420  ANOP
&NAME    L     15,0(,13)          ADDR OF STACK HEADER
         L     15,&STKRSRV.(,15)  ADDR OF DYNAMIC STORAGE HEADER
         L     15,0(,15)          ADDR OF DYNAMIC STORAGE ROUTINE
         LA    1,12(,0)           12 - INDICATE TERM
         BASSM 14,15              CALL DYNAMIC STORAGE ROUTINE
         MEND

SOURCE CODE FOR DYNAMRTN

***********************************************************************
* PERFORM DYNAMIC STORAGE OPERATIONS INIT / GET / FREE / TERM
* NOTE: CAN ONLY BE USED IF MACRO STACK IS ALSO USED
* REGS ON ENTRY
*    R0  = REQUESTED LENGTH (INIT / GET) / ADDR OF ELEMENT (FREE)
*    R1  = TYPE OF CALL (INIT / GET / FREE / TERM)
*    R13 = ADDR OF SAVEAREA OF CALLER
*    R14 = ADDR WHERE TO RETURN
*    R15 = ADDR OF ENTRY
* REGS USAGE
*    R0  = WORK (INIT / TERM) / REQUESTED LENGTH (GET) /
*          ADDR OF STORAGE ELEMENT (FREE)
*    R1  = WORK
*    R2..R4  = UNUSED
*    R5  = ADDR WHERE TO RETURN FROM SUBROUTINE
*    R6  = ADDR OF ADDR OF A STORAGE ELEMENT HEADER
*    R7  = ADDR OF A STORAGE ELEMENT HEADER
*    R8  = ADDR OF ADDR OF CURRENT STORAGE ELEMENT HEADER
*    R9  = ADDR OF CURRENT STORAGE ELEMENT HEADER
*    R10 = LENGTH OF DYNAMIC STORAGE INCL HEADER
*    R11 = ADDR OF DYNAMIC STORAGE HEADER
*    R12 = BASE
*    R13/R14 = UNCHANGED
*    R15 = WORK
* REGS ON EXIT
*    R0  = UNDEFINED
*    R1  = ADDR OF STORAGE ELEMENT (GET) / UNDEFINED (INIT/FREE/TERM)
*    R2..R14 = UNCHANGED
*    R15 = UNDEFINED
* ABNORMAL END
*    USER ABEND 2059 (X'808')
*      = DYNAMIC STORAGE SPACE EXHAUSTED (GET)
*    USER ABEND 2060 (X'80C')
*      = INVALID DYNAMIC STORAGE ELEMENT ADDRESS GIVEN (FREE)
DYNAMRTN CSECT                    REENTRANT
DYNAMRTN AMODE 31
DYNAMRTN RMODE ANY
         STM   R14,R12,12(R13)    SAVE REGS
         LR    R12,R15            LOAD BASE REG
         USING DYNAMRTN,R12
         B     *+4(R1)            BRANCH TO REQ'D OPERATION
         B     DYN100             INIT
         B     DYN200             GET
         B     DYN300             FREE
         B     DYN400             TERM
DYN900   LM    R14,R12,12(R13)    RESTORE REGS
         BSM   0,R14              RETURN TO CALLER
DYN100   SLR   R1,R1              CLEAR REG
         SRDL  R0,3               DIVIDE REQ'D ELEMENT LENGTH BY 8
         SLL   R0,3               MULTIPLY BY 8
         LTR   R1,R1              REMAINDER = 0 ?
         BZ    DYN110              YES
         LA    R15,8
         ALR   R0,R15             ADD 8
         B     DYN120
DYN110   LTR   R0,R0              LENGTH = 0 ?
         BNZ   DYN120              NO
         LA    R0,8               MIN LENGTH = 8
DYN120   LA    R15,L'DYNSHDR      LENGTH OF DYNAMIC STORAGE HEADER
         ALR   R0,R15             ADD IT TO REQ'D ELEMENT LENGTH
         LR    R10,R0             LENGTH OF STORAGE INCLUDING HEADERS
         LTR   R14,R14            TEST ADDRESSING MODE OF CALLER
         BM    DYN130              AMODE=31
         GETMAIN RU,LV=(0),LOC=(BELOW,ANY) ALLOC STRG BELOW THE LINE
         B     DYN140
DYN130   GETMAIN RU,LV=(0),LOC=(ANY,ANY)   ALLOC STRG ABOVE THE LINE
DYN140   L     R15,0(,R13)        ADDR OF STACK HEADER
         ST    R1,STKRSRV(,R15)   ADDR OF DYNAMIC STORAGE HEADER
*                                  TO RESERVED WORD OF STACK HEADER
         LR    R11,R1             ADDR OF DYNAMIC STORAGE
         USING DYNSHDR,R11
         ST    R12,DYNSARTN       INIT ADDR OF DYNAMIC STORAGE ROUTINE
         SLR   R0,R0
         ST    R0,DYNSAUSE        INIT: NO STORAGE USED
         LA    R9,L'DYNSHDR(,R11)
         ST    R9,DYNSAFRE        INIT ADDR OF 1ST FREE STORAGE ELEMENT
         USING DYNEHDR,R9
         ST    R0,DYNEANXT        INIT: NO NEXT STORAGE ELEMENT
         LR    R15,R10            LENGTH OF STORAGE
         LA    R1,L'DYNSHDR+L'DYNEHDR  L' OF STORAGE HDR + ELEMENT HDR
         SLR   R15,R1
         ST    R15,DYNELNG        INIT LENGTH OF FREE STORAGE ELEMENT
         DROP  R9
         ST    R0,DYNSRSRV        INIT RESERVED FIELD
         ST    R10,DYNSTOTL       INIT LENGTH OF STORAGE INCL HEADER
         ST    R0,DYNSRSR2        INIT RESERVED FIELD
         DROP  R11
         B     DYN900             RETURN
DYN200   SLR   R1,R1              CLEAR REG
         SRDL  R0,3               DIVIDE REQ'D ELEMENT LENGTH BY 8
         SLL   R0,3               MULTIPLY BY 8
         LTR   R1,R1              REMAINDER = 0 ?
         BZ    DYN210              YES
         LA    R15,8
         ALR   R0,R15             ADD 8
         B     DYN220
DYN210   LTR   R0,R0              LENGTH = 0 ?
         BNZ   DYN220              NO
         LA    R0,8               MIN LENGTH = 8
DYN220   L     R15,0(,R13)        ADDR OF STACK HEADER
         L     R11,STKRSRV(,R15)  ADDR OF DYNAMIC STORAGE HEADER
         USING DYNSHDR,R11
DYN225   L     R9,DYNSAFRE        ADDR OF 1ST FREE STORAGE ELEMENT
         USING DYNEHDR,R9
         LA    R8,DYNSAFRE        ADDR OF ADDR OF 1ST FREE ELEMENT
DYN230   LTR   R9,R9              NO MORE FREE STORAGE ELEMENT ?
         BZ    DYN290              YES, DO GARBAGE COLECTION
         L     R1,DYNELNG         FREE LENGTH
         CLR   R0,R1              LENGTH SUFFICIENT ?
         BNH   DYN240              YES
         LA    R8,DYNEANXT        ADDR OF ADDR OF NEXT FREE ELEMENT
         L     R9,DYNEANXT        ADDR OF NEXT FREE STORAGE ELEMENT
         B     DYN230
DYN240   SLR   R1,R0              REMAINING FREE LENGTH
         LA    R15,L'DYNEHDR      MIN LENGTH OF STORAGE ELEMENT
         CLR   R1,R15             LENGTH SUFFICIENT ?
         BNL   DYN245              YES, THERE'S A NEW FREE ELEMENT
         L     R15,DYNEANXT       ADDR OF NEXT FREE STORAGE ELEMENT
         ST    R15,0(,R8)         REMOVE FOUND ELEMENT FROM CHAIN
         B     DYN250
DYN245   LR    R7,R9              ADDR OF NEW USED STORAGE ELEMENT
         LA    R15,L'DYNEHDR      LENGTH OF ELEMENT HEADER
         ALR   R7,R15
         ALR   R7,R0              ADDR OF NEW FREE STORAGE ELEMENT
         SLR   R1,R15             NEW FREE LENGTH = L' ELEMENT HEADER
         ST    R1,DYNELNG-DYNEHDR(,R7) INIT: FREE LENGTH
         L     R15,DYNEANXT       ADDR OF NEXT FREE STORAGE ELEMENT
         ST    R15,DYNEANXT-DYNEHDR(,R7) INIT: ADDR OF NEXT FREE ELEMNT
         ST    R7,0(,R8)          REPLACE FREE ELEMENT IN CHAIN
DYN250   ST    R0,DYNELNG         INIT: USED LENGTH
         L     R15,DYNSAUSE       ADDR OF CURRENT 1ST USED ELEMENT
         ST    R15,DYNEANXT       INIT: OF NEXT USED ELEMENT
         ST    R9,DYNSAUSE        NEW USED ELEMENT NOW IS THE 1ST ONE
         DROP  R9
         DROP  R11
         LA    R1,L'DYNEHDR(,R9)  RETURN ADDR TO CALLER
         ST    R1,24(,R13)
         B     DYN900             RETURN
DYN290   BAL   R5,DYN800          DO GARBAGE COLLECTION
         WTO   '******* DYNAMIC STORAGE SPACE EXHAUSTED',              X
               ROUTCDE=11,DESC=7  INFORM PROGRAMMER
         ABEND X'80B',DUMP        USER ABEND 2059
DYN300   L     R15,0(,R13)        ADDR OF STACK HEADER
         L     R11,STKRSRV(,R15)  ADDR OF DYNAMIC STORAGE HEADER
         USING DYNSHDR,R11
         L     R9,DYNSAUSE        ADDR OF 1ST USED STORAGE ELEMENT
         USING DYNEHDR,R9
         LA    R8,DYNSAUSE        ADDR OF ADDR OF 1ST USED ELEMENT
         LA    R15,L'DYNEHDR      LENGTH OF STORAGE ELEMENT HEADER
         SLR   R0,R15             ADDR OF STORAGE ELEMENT TO SEARCH FOR
DYN330   LTR   R9,R9              NO MORE USED STORAGE ELEMENT ?
         BZ    DYN390              YES, INVALID ADDR GIVEN
         CLR   R0,R9              REQ'D STORAGE ELEMENT FOUND ?
         BE    DYN340              YES
         LA    R8,DYNEANXT        ADDR OF ADDR OF NEXT USED ELEMENT
         L     R9,DYNEANXT        ADDR OF NEXT USED STORAGE ELEMENT
         B     DYN330
DYN340   L     R15,DYNEANXT       ADDR OF NEXT USED STORAGE ELEMENT
         ST    R15,0(,R8)         REMOVE FOUND ELEMENT FROM CHAIN
         L     R15,DYNSAFRE       ADDR OF CURRENT 1ST FREE ELEMENT
         ST    R15,DYNEANXT       INIT: ADDR OF NEXT FREE ELEMENT
         ST    R9,DYNSAFRE        NEW FREE ELEMENT NOW IS THE 1ST ONE
         DROP  R9
         DROP  R11
         B     DYN900             RETURN
DYN390   WTO   '******* INVALID DYNAMIC STORAGE ELEMENT ADDRESS GIVEN',X
               ROUTCDE=11,DESC=7  INFORM PROGRAMMER
         ABEND X'80C',DUMP        USER ABEND 2060
DYN400   L     R15,0(,R13)        ADDR OF STACK HEADER
         L     R1,STKRSRV(,R15)   ADDR OF DYNAMIC STORAGE HEADER
         USING DYNSHDR,R1
         L     R0,DYNSTOTL        LENGTH OF STORAGE INCL HEADER
         DROP  R1
         FREEMAIN RU,LV=(0),A=(1) FREE DYNAMIC STORAGE
         B     DYN900             RETURN
DYN800   USING DYNSHDR,R11        GARBAGE COLLECTION (A SUBROUTINE)
         L     R9,DYNSAFRE        ADDR OF 1ST FREE STORAGE ELEMENT
         USING DYNEHDR,R9
         LA    R8,DYNSAFRE        ADDR OF ADDR OF 1ST FREE ELEMENT
DYN830   LTR   R9,R9              NO MORE FREE STORAGE ELEMENT ?
         BZR   R5                  YES, RETURN TO MAIN ROUTINE
         L     R1,DYNELNG         LENGTH OF FREE STORAGE
         LA    R1,L'DYNEHDR(R1,R9)  ADDR OF NEXT POSSIBLE ELEMENT HDR
         L     R7,DYNSAFRE        ADDR OF 1ST FREE STORAGE ELEMENT
         LA    R6,DYNSAFRE        ADDR OF ADDR OF 1ST FREE ELEMENT
DYN840   LTR   R7,R7              NO MORE FREE STORAGE ELEMENT ?
         BZ    DYN880              YES
         CLR   R7,R1              IS FREE ELEMENT ADJACENT TO OTHER ?
         BNE   DYN870              NO
         L     R15,DYNEANXT-DYNEHDR(,R7) ADDR OF NEXT FREE ELEMENT
         ST    R15,0(,R6)         REMOVE FREE ELEMENT FROM CHAIN
         L     R15,DYNELNG-DYNEHDR(,R7) LENGTH OF REMOVED FREE STORAGE
         L     R1,DYNELNG         LENGTH OF FREE STORAGE
         LA    R1,L'DYNEHDR(R15,R1)  PLUS LENGTH OF REMOVED ELEMENT
         ST    R1,DYNELNG         NEW LENGTH OF FREE STORAGE
         CLR   R0,R1              NOW LENGTH SUFFICIENT ?
         BH    DYN830              NO
         LA    R5,DYN225          ADDR WHERE TO BRANCH TO REPEAT GET
         B     DYN830             START GARBAGE COLLECTION AGAIN
DYN870   LA    R6,DYNEANXT-DYNEHDR(,R7) ADDR OF ADDR OF NEXT FREE ELMNT
         L     R7,DYNEANXT-DYNEHDR(,R7) ADDR OF NEXT FREE STORAGE ELMNT
         B     DYN840
DYN880   LA    R8,DYNEANXT        ADDR OF ADDR OF NEXT FREE ELEMENT
         L     R9,DYNEANXT        ADDR OF NEXT FREE STORAGE ELEMENT
         B     DYN830
         DROP  R9
         DROP  R11
         DROP  R12
DYNAMSH  DSECT ,                  DYNAMIC STORAGE HEADER
DYNSARTN DS    A                  ADDR OF DYNAMIC STORAGE ROUTINE
DYNSAUSE DS    A                  ADDRESS OF FIRST USED STORAGE ELEMENT
DYNSAFRE DS    A                  ADDRESS OF FIRST FREE STORAGE ELEMENT
DYNSRSRV DS    A                  RESERVED
DYNSTOTL DS    F                  LENGTH OF DYNAMIC STORAGE INCL HDR
DYNSRSR2 DS    F                  RESERVED
         DS    0D
DYNSHDR  EQU   DYNAMSH,*-DYNAMSH
DYNAMEH  DSECT ,                  STORAGE ELEMENT HEADER
DYNEANXT DS    A                  ADDRESS OF NEXT STORAGE ELEMENT
DYNELNG  DS    F                  LENGTH OF STORAGE ELEMENT
         DS    0D
DYNEHDR  EQU   DYNAMEH,*-DYNAMEH
STKRSRV  EQU   12,4      OFFSET,LENGTH OF RESERVED WORD IN STACK HEADER
         END   DYNAMRTN


Walter Wiedemann
Consultant (West Germany)	© Xephon 1989

