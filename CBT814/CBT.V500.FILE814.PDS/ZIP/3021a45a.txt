Using an alternative date for special
processing

INTRODUCTION

To simulate a specific banking process, we found it necessary to
obtain another date from the system without changing any user
code. Our solution involved creating a subsystem called DATE
with the simulated date coded in the same form as CVTDATE in
the SSCTSUSE field.

 Each job in the simulation has a job name beginning with '$2'
or 'C3'. The change is implemented through a hook in
IGC0001A (SVC time). Note that it was necessary to expand
IGC0001A by 512 bytes as the patch area around X'350' was
too small. In the expanded area, I check that the job name begins
with '$2' or 'C3': if it does, the date moved in the SVRB is
obtained from SSCTSUSE.

The code has been successfully tested under MVS/ESA 4.2.2. The
offset in IGC0001A will need to be adjusted for other releases.
The hook was implemented using the fact that register 3 points to
the CVT and register 9 is the base register.

THE ZAP TO IGC0001A

After expansion, the 512 bytes requested were at offset X'3B8'.
The following ZAP was implemented.

NAME  IGC0001A
VER 03B8 0000,0000
REP 03B8 5870,3128            HOOK     L R7,CVTJESCT
VER 03BC 0000,0000
REP 03BC 5870,7018                     L R7,JESCT
VER 03C0 0000
REP 03C0 1277                          LTR R7,R7
VER 03C2 0000,0000
REP 03C2 4780,9414            SCANSSCT BZ  OLDMVC
VER 03C6 0000,0000,0000
REP 03C6 D503,7008,93FE                CLC SSCTSNAM,=C'DATE'
VER 03CC 0000,0000
REP 03CC 4780,93BC                     BE  FOUND
VER 03D0 0000,0000
REP 03D0 BF7F,7004                     ICM R7,B'1111',SSCTSCTA
VER 03D4 0000,0000
REP 03D4 4780,9414                     BZ  RETURN
VER 03D8 0000,0000
REP 03D8 47F0,93A0                     B   SCANSSCT
VER 03DC 0000,0000
REP 03DC 58A0,0220            FOUND    L   R10,PSAANEW
VER 03E0 0000,0000
REP 03E0 BF4F,A0AC                     ICM R4,B'1111',ASCBJBNI
VER 03E4 0000,0000
REP 03E4 4770,93CC                     BNZ COMPARE
VER 03E8 0000,0000
REP 03E8 BF4F,A0B0                     ICM R4,B'1111',ASCBJBNS
VER 03EC 0000,0000,0000
REP 03EC D501,4000,9402       COMPARE  CLC JOBNAME(2),=C'$2'
VER 03F2 0000,0000
REP 03F2 4770,93E0                     BNE TESTC3
VER 03F6 0000,0000,0000
REP 03F6 D203,5084,7014                MVC SVRB+132,CVT+56
VER 03FC 0000,0000
REP 03FC 47F0,906C                     B   OLDCODE
VER 0400 0000,0000,0000
REP 0400 D501,4000,9404       TESTC3   CLC JOBNAME(2),=C'C3'
VER 0406 0000,0000
REP 0406 4770,9414                     BNE OLDMVC
VER 040A 0000,0000,0000
REP 040A D203,5084,7014                MVC SVRB+132,SSCTSUSE
VER 0410 0000,0000
REP 0410 47F0,906C                     B   OLDCODE
VER 0414 0000,0000,0000
REP 0414 D203,5084,3038       RETURN   MVC SVRB+132,CVT+56
VER 041A 0000,0000
REP 041A 47F0,906C                     B   OLDCODE
VER 041E 0000,0000
REP 041E C4C1,E3C5                     =C'DATE'
VER 0422 0000
REP 0422 5BF2                          =C'$2'
VER 0424 0000
REP 0424 C3F3                          =C'C3'
VER 0086 D203,5084,3038                MVC SVRB+132,CVT+56
REP 0086 47F0,9398,0700                B   HOOK

SCANDATE SOURCE CODE

SCANDATE  CSECT
SCANDATE  AMODE ANY
SCANDATE  RMODE 24
          USING SCANDATE,R12
          SAVE  (14,12)                SAVE CALLER'S REGISTERS
          LR    R12,R15                ESTABLISH MODULE BASE REGISTER
          LR    R7,R1
          GETMAIN R,LV=WORKALEN        GET WORKING STORAGE
          ST    R13,4(R1)              CHAIN SAVEAREA BACKWARD
          ST    R1,8(R13)              CHAIN SAVEAREA FORWARD
          LR    R13,1                  POINT TO THIS MODULE'S SAVEAREA
          USING WORKAREA,R13
          LA    R10,16
          L     R10,0(,R10)
          USING CVTMAP,R10
          L     R9,CVTJESCT
          USING JESCT,R9
          L     R8,JESSSCT
          USING SSCT,R8
SCANSSCT  LTR   R8,R8
          BZ    RETURN
          CLC   SSCTSNAM,=C'DATE'
          BE    DATEFUND               SUBSYSTEM PRESENT
          ICM   R8,B'1111',SSCTSCTA
          BZ    RETURN
          B     SCANSSCT
DATEFUND  EQU   *
          MODESET MODE=SUP,KEY=ZERO
          MVC   SSCTSUSE,=X'0094001F'  01 JANUARY 1994
          MODESET MODE=PROB,KEY=NZERO
          B     RETURN
RETURN    EQU   *
          L     R8,4(R13)              POINTER TO CALLER'S SAVEAREA
          FREEMAIN R,LV=WORKALEN,A=(R13)
          LR    R13,R8
          RETURN (14,12),RC=0
          LTORG
WORKAREA  DSECT
SAVEAREA  DS    18F
WORKALEN  EQU   *-WORKAREA
          IEFJSCVT
          CVT   DSECT=YES
          IEFJESCT
          END


François-Xavier Bessuges
Systems Programmer (France)                                              c Xephon 1994

