Console recovery using the SCSR (continued)

This month we conclude our look at console recovery by
publishing the remaining source code for SCSRCONS and the
SCSRSSI1 routine.

SCSRCONS (CONTINUED)

GETCMD   EQU   *
         XC    TRTABLE(256),TRTABLE    CLEAR TRANSLATE TABLE
         MVI   TRTABLE+C'>',C'>'       SET SEARCH ENTRY
         LA    R1,0                    CLEAR IT
         TRT   DATAAREA+5(75),TRTABLE  SEARCH
         BZ    CMDERROR                ZERO - NOT FOUND SO GO BACK
* THE SYNTAX OF THE COMMAND IS VALID SO WE CAN NOW SHIP THE COMMAND OFF
* TO MVS THROUGH OUR SUBSYSTEM CONSOLE
         LA    R8,DATAAREA+5           GET START ADDRESS
         LR    R2,R1                   GET LAST BYTE ADDRESS
         SR    R1,R8                   GET COMMAND LENGTH
         LTR   R1,R1                   ZERO?
         BZ    CMDERROR                YES - GO BACK
         XC    CMDSTORE(84),CMDSTORE   CLEAR AREA
         BCTR  R1,0                    REDUCE BY ONE
         EX    R1,CMDMVC1              MOVE IN COMMAND
         A     R1,=F'5'                ADD TO LENGTH
         STCM  R1,B'0011',CMDSTORE     SAVE LENGTH
         CLI   SSVTCNID+1,X'00'        VALID CONSOLE?
         BE    RETR6                   NO - DON'T ISSUE COMMAND
         SR    R0,R0                   CLEAR R0
         ICM   R0,B'0011',SSVTCNID     MOVE IN CONSOLE ID
         LA    R1,CMDSTORE             GET COMMAND AREA ADDRESS
         SVC   34                      ISSUE COMMAND
         B     FREE
CMDMVC   MVC   DATAAREA(1),CIBDATA
CMDMVC1  MVC   CMDSTORE+4(1),DATAAREA+5
CSTOP    EQU   *
         WTO   'SSIT010I - SSIT STOP REQUEST ACCEPTED',                X
               ROUTCDE=(1),DESC=(6)
         QEDIT ORIGIN=COMCIBPT,BLOCK=(3) FREE CIB
         B     CLEANUP                 WE'RE DONE - LET'S CLEAN UP
SSCTERR  EQU   *
         WTO   'SSIT101I - SSIT SSCVT NOT FOUND.  DYNAMIC BUILD WILL BEX
                ATTEMPTED.',ROUTCDE=(1),DESC=(6)
         GETMAIN RU,LV=36,SP=241       GET SSCVT STORAGE
         LTR   R15,R15                 STORAGE GET O.K.?
         BZ    SSCVTBLD                YES - BUILD THE SSCVT
         WTO   'SSIT905I - SSIT SSCVT STORAGE GETMAIN FAILURE.  SSIT SUX
               BSYSTEM IS TERMINATING.',ROUTCDE=(1),DESC=(1)
         B     FREE01                  FREE WORKAREA STORAGE & END
SSCVTBLD EQU   *
         XC    0(36,R1),0(R1)          CLEAR GETMAINED AREA
         LR    R2,R1                   SAVE ADDRESS
         L     R1,ASSCVT               GET LAST SSCVT ADDRESS
CHKSSCVT EQU   *
         CLC   SSCTSCTA(4),=F'0'       STILL OPEN?
         BE    FILLSSCT                YES - FILL IT UP
         L     R1,SSCTSCTA             GET NEXT SSCVT ADDRESS
         B     CHKSSCVT                GO BACK AND CHECK AGAIN
FILLSSCT EQU   *
         ST    R2,4(,R1)               SAVE NEXT SSCVT ADDRESS
         MVC   0(4,R2),=C'SSCT'        CONTROL BLOCK ID
         MVC   8(4,R2),=C'SSIT'        SUBSYSTEM ID
         LR    R1,R2                   UPDATE OUR SSCVT POINTER
         B     OURSSCT                 GO ON
*   GETMAIN FAILURES - ISSUE MESSAGES
GETERR01 EQU   *
         WTO   'SSIT906I - SSIT WORKAREA STORAGE GETMAIN FAILURE.  SSITX
                SUBSYSTEM IS TERMINATING.',ROUTCDE=(1),DESC=(1)
         B     RET00                   RETURN WITHOUT FREEING ANYTHING
GETERR02 EQU   *
         WTO   'SSIT907I - SSIT SSVT STORAGE GETMAIN FAILURE.  SSIT SUBX
               SYSTEM IS TERMINATING.',ROUTCDE=(1),DESC=(1)
         B     FREE01                  FREE WORKAREA STORAGE & END
CLEANUP  EQU   *
         TM    INITFLAG,X'80'          PREVIOUSLY INITIALIZED?
         B     FREE01                  FREE WORKAREA STORAGE & END
* WE CAN USE THE SUBSYSTEM CONSOLE SERVICE ROUTINE TO NOTIFY MVS THAT
* WE WOULD LIKE TO FREE THE SUBSYSTEM CONSOLE THAT WAS ALLOCATED TO OUR
* ADDRESS SPACE.  THIS IS DONE BY SETTING THE SUBSYSTEM NAME THAT IS
* MAKING THE REQUEST IN 'SCSNAME', PLACING THE CONSOLE ID INTO 'SCSNID'
* AND BY SETTING THE 'SCSRELSE' FLAG ON IN THE SCSR PARAMETER LIST
* PRIOR TO INVOKING THE SUBSYSTEM CONSOLE SERVICE ROUTINE.
         XC    SCSRWORK(SCSLEN),SCSRWORK  CLEAR PARM AREA
         LA    R6,SCSRWORK             GET PARM AREA ADDRESS
         USING SCSRPLST,R6             SET ADDRESSABILITY
         MVC   SCSACRO,=C'SCSR'        MOVE IN IDENTIFIER
         MVC   SCSNAME(8),=C'SSIT    ' MOVE IN SUBSYSTEM NAME
         OI    SCSFUNC1,SCSRELSE       SET RELEASE CONSOLE FLAG ON
         MVC   SCSCNID(2),SSVTCNID     MOVE IN CONSOLE ID
         CLI   SCSCNID+1,X'00'         A CONSOLE?
         BE    BROADOFF                NO - TURN BROADCAST OFF
         ST    R6,SCSRPARM             SAVE PARAMETER AREA ADDRESS
         LA    R1,SCSRPARM             GET ADDRESS OF PARM AREA ADDR
         LINK  EP=IEAVG700             GO TO SCSR
         LTR   R15,R15                 EVERYTHING O.K.?
         BZ    BROADOFF                YES - TURN BROADCAST OFF
         WTO   'SSIT915I - ERROR RELEASING SUBSYSTEM CONSOLE',         X
               ROUTCDE=(1),DESC=(6)
BROADOFF EQU   *
* WE CAN USE THE SUBSYSTEM CONSOLE SERVICE ROUTINE TO NOTIFY SVC35 THAT
* CONSOLE MESSAGES SHOULD NOT BE SENT TO ALL ACTIVE SUBSYSTEMS.  WE DO
* THIS BY SETTING THE 'SCSBRDOF' FLAG IN THE SCSR PARAMETER LIST PRIOR
* TO INVOKING THE SUBYSTEM CONSOLE SERVICE ROUTINE.
         XC    SCSRWORK(SCSLEN),SCSRWORK  CLEAR PARM AREA
         LA    R6,SCSRWORK             GET PARM AREA ADDRESS
         USING SCSRPLST,R6             SET ADDRESSABILITY
         MVC   SCSACRO,=C'SCSR'        MOVE IN IDENTIFIER
         MVI   SCSFUNC1,SCSBRDOF       SET BRODCAST OFF FLAG
         LA    R1,SCSRPARM             GET ADDRESS OF PARM AREA ADDR
         LINK  EP=IEAVG700             GO TO SCSR
         LTR   R15,R15                 EVERYTHING O.K.?
         BNZ   PLANB2                  NO - LET'S DO IT OURSELVES
         B     BYPASS2                 WE'RE O.K. SO LET'S CONTINUE
         DROP  R6
PLANB2   EQU   *
* THAT WAS SUCH A SIMPLE TASK THAT I DON'T SEE HOW ANYTHING COULD HAVE
* GONE WRONG.  WE WILL ISSUE A CONSOLE MESSAGE TO SAY WE HAD A PROBLEM
* BUT THEN WE WILL SET THE FLAG OURSELVES ANYWAY.
         WTO   'SSIT971I - SUBSYSTEM CONSOLE SERVICE ROUTINE FAILED TO X
               DISABLE THE BRODCAST ALL FLAG.',ROUTCDE=(1),            X
               DESC=(6)
         L     R5,16                   GET CVT ADDRESS
         USING CVT,R5                  SET ADDRESSABILITY
         L     R3,CVTCUCB              GET UCM ADDRESS
         XR    R5,R5                   CLEAR REGISTER
GETCNT2  L     R5,UCMBRDST-UCM(,R3)    GET COUNT OF SS REQUESTING MSGS
         LTR   R5,R5                   ALREADY ZERO?
         BZ    BYPASS2                 YES - DON'T REDUCE BY ONE
         LR    R1,R5                   SAVE COUNT VALUE
         S     R5,=X'00010000'         SUBTRACT ONE
         CS    R1,R5,UCMBRDST-UCM(R3)  SAVE NEW COUNT VALUE
         BNE   GETCNT2                 GET NEW COUNT
BYPASS2  EQU   *
*   DISABLE SSIT SUBSYSTEM
         L     R1,ASSCVT               GET SS ADDRESS
         XC    SSCTSSVT(4),SSCTSSVT    INDICATE NOT ACTIVE
         L     R4,PARMAREA             GET SSVTAREA ADDRESS
         FREEMAIN RU,LV=SSVTLEN,SP=245,A=(4)  FREE SSVTAREA STORAGE
FREE01   EQU   *
         L     R13,SAVEAREA+4          RESTORE SAVEAREA ADDRESS
         FREEMAIN RU,LV=WORKLEN,SP=245,A=(12) FREE WORKAREA STORAGE
RET00    EQU   *
         LM    R14,R12,12(R13)         RESTORE ENVIRONMENT
         SR    R15,R15                 SET RETURN CODE
         BR    R14                     RETURN
PARMMOVE MVC   CMD+4(1),0(R4)
WTO1     DC    C'SSIT85XI - SCSR REQUEST RETURN CODE R15:  XXXXXXXX'
WTOLIST  WTO   '                                                       X
                                                                       X
                         ',ROUTCDE=(1),DESC=(6),MF=L
WTOLEN   EQU   *-WTOLIST
WORKAREA DSECT
ASSCVT   DS    F
SAVEAREA DS    18F
INITFLAG DS    F
SSINIT   EQU   X'80'                   SSIT SUBSYSTEM IS ALREADY ACTIVE
NOCONSOL EQU   X'40'                   SUBSYSTEM CONSOLE NOT AVAILABLE
PARMAREA DS    F
ANSRAREA DS    F
ECBAREA  DS    F
DBL1     DS    D
         DS    F
DBL2     DS    D
         DS    F
CMD      DS    F
         DS    CL80
PARMLEN  DS    H
SCSRPARM DS    F
SCSRWORK DS    CL(SCSLEN)
WTOWORK  DS    CL(WTOLEN)
TRTABLE  DS    CL256
DATAAREA DS    CL80
CMDSTORE DS    CL84
JESID    DS    CL4
WORKLEN  EQU   *-WORKAREA
SSVT     DSECT
SSVTRSV  DS    XL2                     RESERVED
SSVTFNUM DS    XL2                     NUMBER OF SUPPORTED FUNCTIONS
SSVTFCOD DS    CL256                   FUNCTION CODE MATRIX
SSVTFRTN DS    F                       FUNCTION ROUTINE ADDRESS
         DS    F
SSVTEXT  EQU   *                       SSVT EXTENSION
SSVTSCVT DS    F                       SSCT ADDRESS
SSVTCNID DS    F                       CONSOLE ID
SSVTLEN  EQU   *-SSVT
         CVT   DSECT=YES
         IEECUCM DSECT=YES,LIST=YES
         IEZVG100 DSECT=YES
         IEFJSSOB (WT)
         IEFJSSIB
         IEFJSCVT
         IEFJESCT
         DSECT
         IEZCOM
         DSECT
         IEZCIB
         END

SCSRSSI1 PROGRAM

SCSRSSI1 CSECT
* SCSRSSI1 IS THE FUNCTION CODE 9 SUBSYSTEM ROUTINE FOR THE SSIT
* SUBSYSTEM.  IT MONITORS CONSOLE MESSAGES TO DETERMINE IF THEY HAVE
* BEEN ISSUED FOR THE SUBSYSTEM CONSOLE ACQUIRED BY THE SCSRCONS
* PROGRAM.  IF A MESSAGE MEETS THESE CRITERIA, SCSRSSI1 REISSUES THE
* MESSAGE FOR GLOBAL CONSOLE DISPLAY AS THE MESSAGES ISSUED TO THE
* SUBSYSTEM CONSOLE WOULD ONLY SHOW UP IN SYSLOG OTHERWISE.
* ATTRIBUTES    - RENT,AC(1)
* THE ROUTINE IS INVOKED BY SUBSYSTEM INTERFACE WHENEVER A FUNCTION
* CODE 9 SUBSYSTEM REQUEST IS MADE.
* INPUT         - R0 - ADDRESS OF THE SSCVT FOR THE SSIT SUBSYSTEM
*                 R1 - ADDRESS OF THE SSOB FOR THE FUNCTION CODE
*                        9 SUBSYSTEM REQUEST
*   MACRO LIBS    - SYS1.MACLIB
*                   SYS1.AMODGEN
         STM   R14,R12,12(R13)         SAVE INCOMING ENVIRONMENT
         LR    R9,R15                  SET UP MODULE ...
         USING SCSRSSI1,R9                ADDRESSABILITY
BEGIN    LR    R3,R0                   SAVE SSCVT ADDRESS
         L     R4,SSCTSSVT-SSCT(,R3)   R4 -> SSVT
         USING SSVT,R4                 SET UP ADDRESSABILITY
         LR    R8,R1                   R8 -> SSOB
         USING SSOB,R8                 SET UP ADDRESSABILITY
         MODESET MODE=SUP,KEY=ZERO     GET PROPERLY AUTHORIZED
*   GET TO FUNCTION DEPENDENT PORTION OF THE SSOB
         L     R3,SSOBINDV             SET UP ADDRESSABILITY ...
         USING SSWTBGN,R3                 TO FUNCTION DEPENDENT AREA
         L     R5,SSWTWQE              GET MAJOR WQE ADDRESS
         L     R6,SSWTMIN              GET MINOR WQE ADDRESS
         L     R7,SSWTORE              GET WTOR ELEMENT ADDRESS
         USING WQE,R5
         XC    SSOBRETN,SSOBRETN       CLEAR RETURN CODE
GETSTOR  EQU   *
         GETMAIN RU,LV=WORKLEN,SP=229,LOC=BELOW
         LTR   R15,R15                 STORAGE O.K.?
         BNZ   RETURN                  NO - DON'T DO ANYTHING
         LR    R12,R1                  SAVE STORAGE ADDRESS
         USING WORKAREA,R12
         CLC   WQEUCMID(1),SSVTCNID+1  CONSOLE ID MATCH?
         BNE   FREE01                  NO - RETURN
         CLI   SSVTCNID+1,X'00'        A VALID CONSOLE?
         BE    FREE01                  NO - RETURN
         LTR   R6,R6                   MINOR WQE?
         BNZ   MINORWQE                YES - PROCESS MINOR WQE
         MVC   WTOWORK(WTOLEN),WTOLIST MOVE IN WTO PATTERN
         MVC   WTOWORK+4(120),WQETXT+1 MOVE IN MESSAGE TEXT
         WTO   MF=(E,WTOWORK)          ISSUE MESSAGE
         B     FREE01                  RETURN
MINORWQE EQU   *
*   PROCESS THE MINOR LINES OF A MULTI-LINE WTO
         USING WMNM,R6
         MVC   WTOWORK(WTOLEN),WTOLIST MOVE IN WTO PATTERN
         L     R1,WMNMNX1              GET POINTER TO 2ND MINOR WQE
         LTR   R1,R1                   ANYTHING?
         BNZ   PART2                   YES - DO 2ND HALF
         MVC   WTOWORK+4(72),WMNMTXT1  MOVE IN TEXT
         WTO   MF=(E,WTOWORK)          ISSUE MESSAGE
         B     FREE01                  RETURN
PART2    EQU   *
* A MINOR WQE CONTAINS 2 LINES OF TEXT.  IF THE POINTER TO THE SECOND
* HALF WMNMNX1 IS NON-ZERO IT MEANS THE SECOND HALF IS BEING PROCESSED.
         MVC   WTOWORK+4(72),WMNMTXT2  MOVE IN TEXT
         WTO   MF=(E,WTOWORK)          ISSUE MESSAGE
FREE01   EQU   *
         FREEMAIN RU,LV=WORKLEN,SP=229,A=(12)
RETURN   EQU   *
         LM    R14,R12,12(R13)         RESTORE ENVIRONMENT
         SR    R15,R15                 CLEAR R15
         BR    R14
         $REQU
WTOLIST  WTO   '                                                       X
                                                                       X
                         ',ROUTCDE=(1),DESC=(6),MF=L
WTOLEN   EQU   *-WTOLIST
WORKAREA DSECT
         DS    0D
WTOWORK  DS    CL(WTOLEN)
WORKLEN  EQU   *-WORKAREA
SSVT     DSECT
SSVTRSV  DS    XL2                     RESERVED
SSVTFNUM DS    XL2                     NUMBER OF SUPPORTED FUNCTIONS
SSVTFCOD DS    CL256                   FUNCTION CODE MATRIX
SSVTFRTN DS    F                       FUNCTION ROUTINE ADDRESS
         DS    F
SSVTEXT  EQU   *                       SSVT EXTENSION
SSVTSCVT DS    F                       SSCT ADDRESS
SSVTCNID DS    F                       CONSOLE ID
SSVTLEN  EQU   *-SSVT
         CVT   DSECT=YES
         IEFJSSOB (WT)
         IEFJSSIB
         IEFJSCVT
         IEFJESCT
         IHAWQE DSECT=YES
         END


                                          © Xephon 1990

