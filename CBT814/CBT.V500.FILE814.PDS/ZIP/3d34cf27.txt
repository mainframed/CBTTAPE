Listing attributes of allocated datasets
The following program, which can be executed as a TSO command processor or as a
called TSO program, is designed to list the attributes of allocated datasets.
It is invoked as a TSO command processor with the following command:
     LISTDD ddname
and is invoked as a called TSO program with the following statement:
     CALL (LISTDD) 'ddname'
where ddname identifies the dataset to be enquired about.  By coding an * in
this field, all allocated datasets will be listed.
The output differs from the output produced by the LIST/LISTA command in the
following ways:
¥    Output is selectable by DDNAME
¥    VOLSER, dataset disposition, and status are shown
¥    Conditional disposition is not shown
¥    One line of output is produced for each allocated dataset.
The following return codes may be issued by the program:
¥    0 - function successfully completed
¥    4 - the dataset was not allocated
¥    8 - no DDNAME was specified.
LISTDD   CSECT ,
         USING LISTDD,R15          TEMPORARY ADDRESSABILITY
BEGIN    SAVE  (14,12),T,'LISTDD    &SYSDATE &SYSTIME'
         LA    R12,SAVEAREA        LOAD ADDRESS OF SAVE AREA
         ST    R13,4(,R12) .       PROVIDE BACKWARD
         ST    R12,8(,R13) ..       & FORWARD CHAINING
         LR    R13,R12             COPY NEW SAVE AREA ADDRESS
         LR    R12,R15             COPY BASE ADDRESS
         DROP  R15 .               DROP TEMPORARY ADDRESSABILITY
         USING LISTDD,R12          PERMANENT ADDRESSABILITY FOR PROGRAM
         LR    R3,R1               COPY PARM/CPPL ADDRESS
         TM    0(R3),X'80'         IS IT A PARM OR A CPPL ADDRESS
         BZ    GOTCPPL             BRANCH IF R3 ---> CPPL
         L     R4,0(,R3)           GET ADDRESS OF PARM FIELD
         LH    R6,0(,R4)           GET LENGTH OF PARM
         SH    R6,=H'1'            WAS A PARM SUPPLIED
         BM    EXIT                NO, EXIT WITH R/C = 8
         LA    R4,2(,R4)           ELSE POINT TO START OF PARM DATA
         B     GOTPARM             AND CONTINUE PROCESSING
GOTCPPL  L     R4,0(,R3)           GET POINTER TO COMMAND BUFFER
         LH    R5,2(,R4)           GET OFFSET TO COMMAND VARIABLE(S)
         LTR   R5,R5               ARE THERE ANY VARIABLES
         BZ    EXIT                NO, EXIT WITH R/C = 8
         LH    R6,0(,R4)           GET LENGTH OF COMMAND BUFFER
         SR    R6,R5               SUBTRACT VARIABLE OFFSET FROM LGH.
         SH    R6,=H'5'            SUBTRACT CONSTANT FROM LENGTH
         BM    EXIT                EXIT WITH R/C = 8 IF NO VARIABLES
         LA    R4,4(R4,R5)         POINT TO START OF VARIABLES
GOTPARM  CH    R6,=AL2(7)          DOES INPUT EXCEED MAXIMUM
         BNH   *+8                 NO, CONTINUE
         LA    R6,7                ELSE SET LENGTH TO MAX
         MVC   DDNAME(0),0(R4)     *** EXECUTED BY NEXT INSTR ***
         EX    R6,*-6              COPY THE DDNAME
         OC    DDNAME,=CL8' '      MAKE SURE DDNAME IS ALL UPPER-CASE
*** DETERMINE IF THE DATASET IS ALLOCATED BY SCANNING THE TIOT ***
         L     R2,16               POINT TO CVT
         L     R2,0(,R2)           POINT TO TCB/ASCB WORDS
         L     R2,4(,R2)           POINT TO TCB
         L     R2,12(,R2)          POINT TO TIOT
         LA    R1,TIOENTRY-TIOT1   OFFSET TO FIRST TIOT ENTRY
TIOSCAN  AR    R2,R1               POINT TO FIRST/NEXT ENTRY
         USING TIOENTRY,R2         PROVIDE TIOT ENTRY ADDRESSABILITY
         TM    TIOESTTA,TIOSLTYP   IS THIS ENTRY IN USE
         BO    TIOBYPAS            NO
         CLI   DDNAME,C'*'         IS REQUEST FOR FIRST ALL DATASETS
         BE    ALLOCATD            YES, EXIT NOW
         CLC   TIOEDDNM,DDNAME     IS THIS ENTRY FOR REQUESTED DDNAME
         BE    ALLOCATD            YES, EXIT NOW
TIOBYPAS ICM   R1,1,TIOELNGH       GET LENGTH OF THIS TIOT ENTRY
         BNZ   TIOSCAN             CONTINUE IF ANOTHER ENTRY AVAILABLE
         B     NOTFOUND            BRANCH IF DDNAME NOT FOUND
ALLOCATD MVI   RETCODE,0           SET FINAL RETURN CODE TO ZERO
         TPUT  CLRSCRN,CLRSCRNL,FULLSCR CLEAR THE SCREEN FOR STARTERS
         TPUT  MSGHDR,L'MSGHDR     OUTPUT THE MESSAGE HEADER LINE
         TPUT  BLANKS,L'BLANKS     OUTPUT A BLANK LINE
LOOP1    MVC   MSGDDN,TIOEDDNM     COPY THE DDNAME TO OUTPUT
         SR    R1,R1               CLEAR A WORK REGISTER
         ICM   R1,7,TIOEJFCB       GET THE JFCB ADDRESS FOR DATASET
         LA    R1,16(,R1)          INCREMENT PAST JFCB HEADER
         USING JFCBDSCT,R1         PROVIDE JFCB ADDRESSABILITY
         MVC   MSGDSN,JFCBDSNM     COPY THE DSNAME TO OUTPUT
         MVC   WORKAREA(1),JFCBIND2 AND THE JFCB INDICATOR BYTE
         NI    WORKAREA,JFCDISP+JFCSHARE TURN OFF UNINTERESTING BITS
         LA    R14,DISP            POINT TO DISP-1 TABLE
         LA    R0,DISP#             AND SET NUMBER OF TABLE ENTRIES
LOOP2    CLC   WORKAREA(1),0(R14)  LOOK FOR DISP-1 MATCH IN TABLE
         BE    EXIT2               EXIT IF/WHEN FOUND
         LA    R14,L'DISP(,R14)    ELSE POINT TO NEXT ENTRY
         BCT   R0,LOOP2            AND CONTINUE TRYING
EXIT2    MVC   MSGDISP1,1(R14)     COPY THE DISPOSITION TO OUTPUT
         MVC   MSGDISP2,=C'KEEP  ' ASSUME FINAL DISP=KEEP
         TM    JFCBIND2,JFCTEMP    IS FINAL DISPOSITION KEEP/DELETE
         BZ    *+10                BRANCH IF 'KEEP'
         MVC   MSGDISP2,=C'DELETE' ELSE RESET TO SHOW 'DELETE'
         MVC   MSGVOL,JFCBVOLS     COPY THE FIRST VOLSER TO OUTPUT
         DROP  R1                  DROP JFCB ADDRESSABILITY
         TPUT  MESSAGE,L'MESSAGE   OUTPUT A MESSAGE LINE
         SR    R1,R1               CLEAR A WORK REGISTER
LOOP3    IC    R1,TIOELNGH         GET LENGTH OF THIS TIOT ENTRY
         LA    R2,TIOENTRY(R1)     POINT TO NEXT TIOT ENTRY
         CLI   TIOELNGH,0          HAVE WE HIT END OF TIOT
         BE    EXIT                ALL DONE IF LENGTH IS ZERO
         TM    TIOESTTA,TIOSLTYP   IS THIS ENTRY IN USE
         BZ    CHKALL              NO, SEE IF LISTDD ALL OR CONCAT.
         CLI   DDNAME,C'*'         IS LISTDD ALL IN EFFECT
         BNE   EXIT                NO, WE'RE ALL DONE
         B     LOOP3               ELSE TRY THE NEXT TIOT ENTRY
CHKALL   CLI   DDNAME,C'*'         IS LISTDD ALL IN EFFECT
         BE    LOOP1               YES, LIST THIS ENTRY
         CLC   =CL8' ',TIOEDDNM    IS THIS ENTRY CONCAT FOR PREVIOUS
         BNE   EXIT                NO,WE'RE ALL DONE
         B     LOOP1               ELSE PRINT THE CONCATENATION INFO
NOTFOUND TPUT  ERRMSG,L'ERRMSG
         MVI   RETCODE,4           RESET FINAL RETURN CODE TO 4
EXIT     LA    R15,8               LOAD THE FINAL RETURN CODE
RETCODE  EQU   EXIT+3
         L     R13,4(,R13)    .    POINT TO PREVIOUS SAVE AREA
         RETURN (14,12),T,RC=(15)  TERMINATE THE TASK
         LTORG ,
SAVEAREA DC    9D'0'
WORKAREA DC    C' '
DISP     DC    0CL4' '
         DC    AL1(JFCNEW),C'NEW'
         DC    AL1(JFCMOD),C'MOD'
         DC    AL1(JFCOLD),C'OLD'
         DC    AL1(JFCOLD+JFCSHARE),C'SHR'
DISP#    EQU   (*-DISP)/L'DISP
         DC    C'????'
ERRMSG   DC    C'XXXXXXXX - DATASET NOT ALLOCATED'
         ORG   ERRMSG
DDNAME   DC    CL8' '
         ORG   ,
CLRSCRN  DC    X'C31140403C404000'
CLRSCRNL EQU   *-CLRSCRN
BLANKS   DC    C' '
*                123456789012345678901234567890123456789012345678901234
MSGHDR   DC    C'_DDNAME_  ___DISP___  VOLSER  ___________________DSNAMX
               E___________________'
*              5678901234567890123456789
MESSAGE  DC    CL79' '
         ORG   MESSAGE
MSGDDN   DC    C'_DDNAME_',C'  '
MSGDISP1 DC    C'XXX',C','
MSGDISP2 DC    C'YYYYYY',C'  '
MSGVOL   DC    C'VOLSER',C'  '
MSGDSN   DC    CL44' '
         ORG   ,
JFCBDSCT DSECT ,
         IEFJFCBN LIST=NO
TIOTDSCT DSECT ,
         IEFTIOT1
         END   BEGIN


Paul Horak
Consultant
SEI Corporation (USA)    © SEI Corporation 1988










































