A WTO exit for automated operations (continued)

This month we conclude our look at a WTO exit for
automating operations by publishing the remaining source code
for the program.

*****    SCHEDULE SRB INTO APPROPRIATE ADDRESS SPACE    *****
SRBSET   LA    R11,SRBAREA             LOAD R11 W/A(OUR SRB AREA)
         USING SRB,R11                 ESTABLISH ADDRESSABILITY
         MVC   SRBID,=CL4'SRB'         MOVE EYECATCHER TO SRB
         ST    R6,SRBASCB              SAVE A(ASCB) FOR SRB
         MVC   SRBPTCB,ASCBTNEW        SAVE A(FIRST READY TCB) FOR SRB
         MVC   SRBPASID,ASCBASID       SAVE ITS ASID FOR SRB
         LA    R1,SRBCODE              LOAD R15 W/A(SRB ROUTINE)
         ST    R1,SRBEP                SAVE AS SRB ENTRY POINT
         OI    SRBEP,SRBMODE           SET AMODE=31
         LA    R1,SRBCLEAN             LOAD R15 W/A(SRB ABTERM ROUTINE)
         ST    R1,SRBRMTR              SAVE AS SRB ABTERM ROUTINE
         OI    SRBRMTR,SRBRMODE        SET AMODE=31
         ST    R13,SRBPARM             SAVE A(WORKAREA) AS SRB PARM
         SCHEDULE SRB=(R11),SCOPE=LOCAL SCHEDULE SRB
         OI    EXITFLAG,SRBSCHED       SHOW SRB SCHEDULED
         BR    R9                      RETURN TO CALLER
         DROP  R11                     DROP PRIOR REFERENCE TO SRB
*****    SRB ABNORMAL TERMINATION ROUTINE    *****
SRBCLEAN BR    R14                     SRB ABTERM ERROR ROUTINE
*****    SRB ROUTINE TO CREATE/SCHEDULE IRB    *****
SRBCODE  LR    R12,R15                 LOAD R12 W/BASE ADDRESS
         USING SRBCODE,R12             ESTABLISH ADDRESSABILITY
         LR    R5,R0                   SAVE A(SRB)
         USING SRB,R5                  ESTABLISH ADDRESSABILITY
         LR    R13,R1                  LOAD R13 W/A(PARMLIST)
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY
         ST    R14,SRBRET              SAVE RETURN ADDRESS
         OI    EXITFLAG,SRBACTIV       SHOW SRB ACTIVE
GLOCK    SETLOCK OBTAIN,TYPE=LOCAL,REGS=USE,MODE=UNCOND,RELATED=FLOCK
         L     R4,SRBPTCB              LOAD R4 W/A(TCB FOR CIRB)
         CIRB  EP=IRBCODE,KEY=SUPR,MODE=SUPR,BRANCH=YES,SVAREA=YES,    X
               STAB=(DYN),RETIQE=NO,WKAREA=IQELEN
         OI    EXITFLAG,CIRBSCHD       SHOW CIRB SCHEDULED
         LR    R3,R1                   LOAD R3 W/(IRB)
         USING RBBASIC,R3              ESTABLISH ADDRESSABILITY
         L     R1,RBNEXAV              LOAD R1 W/A(IQE)
         USING IQESECT,R1              ESTABLISH ADDRESSABILITY
         ST    R3,IQEIRB               STORE A(IRB) IN IQE
         ST    R13,IQEPARAM            STORE IQE PARM LIST POINTER
         ST    R4,IQETCB               STORE A(TCB FROM SRB) IN IQE
         LCR   R1,R1                   COMPLEMENT R1
         L     R15,CVTPTR              LOAD R15 W/A(CVT)
         L     R15,CVT0EF00-CVT(R15)   LOAD R15 W/A(STG2 EXIT EFFECTOR)
         BALR  R14,R15                 GO SCHEDULE THE IQE
         OI    EXITFLAG,STG2SCHD       SHOW STAGE 2 SCHEDULED
FLOCK    SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,RELATED=GLOCK
         L     R14,SRBRET              RELOAD RETURN ADDRESS
         OI    EXITFLAG,SRBDONE        SHOW SRB DONE
         BR    R14                     RETURN TO CALLER
         DROP  R13                     DROP ADDRESSABILITY
*****    IRB ROUTINE TO ISSUE COMMAND/REPLY    *****
IRBCODE  SAVE  (14,12),,IRBVMXIT-&SYSDATE
         LR    R12,R15                 LOAD R12 W/BASE ADDRESS
         USING IRBCODE,R12             ESTABLISH ADDRESSABILITY
         USING WORKAREA,R1             ESTABLISH ADDRESSABILITY
         ST    R13,IRBSAVE+4           PERFORM
         LA    R10,IRBSAVE                SAVE
         ST    R10,8(R13)                     AREA
         LR    R13,R1                           CHAINING
         USING WORKAREA,R13            ESTABLISH ADDRESSABILITY
         OI    EXITFLAG,IRBACTIV       SHOW IRB ACTIVE
         ICM   R1,15,DELAY             LOAD DELAY VALUE, IF ANY
         BZ    NODELAY                 IF NONE, SKIP STIMER
         STIMER WAIT,BINTVL=DELAY      DELAY FOR SPECIFIED TIME
NODELAY  TM    TYPEFLAG,CMDRESP        IS THIS A COMMAND RESPONSE
         BO    GET24BIT                YES, SKIP REPLY SCAN
         L     R1,CVTPTR               LOAD R1 W/A(CVT)
         USING CVT,R1                  ESTABLISH ADDRESSABILITY
         L     R1,CVTCUCB              LOAD R1 W/A(UCM BASE)
         USING UCM,R1                  ESTABLISH ADDRESSABILITY
         ICM   R1,15,UCMRPYQ           LOAD R1 W/A(FIRST ORE), IF ANY
         BZ    NOSVC34                 IF NONE, SKIP SVC 34
         USING OREF,R1                 ESTABLISH ADDRESSABILITY
*        ASSUME THAT REPLY IS IN THE FORM: R XX,REPLY
ORELOOP  CLC   OREID,WTOAREA+6         IS THIS THE REPLY
         BE    GET24BIT                YES, GO GET STORAGE
         ICM   R1,15,ORELKP            LOAD R1 W/A(NEXT ORE), IF ANY
         BZ    NOSVC34                 IF NONE, SKIP SVC 34
         B     ORELOOP                 ELSE GO CHECK NEXT ORE
* BUFFER FOR SVC 34 MUST BE IN 24 BIT STORAGE (BELOW THE LINE)
GET24BIT GETMAIN RU,LV=WTOLEN,LOC=BELOW GET STORAGE FOR WORKAREA
         LR    R5,R1                   SAVE A(GETMAINED STORAGE)
         MVC   0(WTOLEN,R1),WTOAREA    MOVE WTO TO 24 BIT STORAGE
         SR    R0,R0                   CLEAR R0 FOR SVC 34
         SVC   34                      ISSUE SVC 34
         LR    R1,R5                   RELOAD A(GETMAINED STORAGE)
         FREEMAIN RU,A=(1),LV=WTOLEN   FREEMAIN 24 BIT STORAGE
NOSVC34  OI    EXITFLAG,IRBDONE        SHOW IRB DONE
         LR    R1,R13                  GET AREA ADDRESS
         L     R13,IRBSAVE+4           LOAD R13 W/A(CALLER SAVEAREA)
         FREEMAIN RU,A=(1),LV=WORKLEN,SP=245 FREEMAIN STORAGE
         RETURN (14,12),RC=0           RETURN TO CALLER
*****    CONSTANTS    *****
         DS    0F
STCNAME  DC    CL8'LLA     '           STC NAME TO SCHEDULE SRB AGAINST
WTOSKEL  WTO   '                                                       X
                                                                       X
                              ',ROUTCDE=1,DESC=5,MF=L 126 BYTE AREA
WTOLEN   EQU   *-WTOSKEL               LENGTH OF SAMPLE REPLY ENTRY
*****    MESSAGES WHOSE ROUTE AND DESC CODES ARE TO BE MODIFIED   *****
MSGTABL  DS    0F                      BEGINNING OF MESSAGE TABLE
$HASP085 IEECODE ID=$HASP085,TYPE=CMD,CMD='Z EOD'
*              JES2 TERMINATION COMPLETE
$HASP203 IEECODE ID=$HASP203,TYPE=CMD,CMD='$TR165.PR1,F=STD',          X
               SCAN='RMT165'
*              RMTXXX DISCONNECTED FROM LINE/RESET PRINTER FORMS
$HASP309 IEECODE ID=$HASP309,TYPE=DELETE
*              INIT NN INACTIVE
*SV300I  IEECODE ID=CSV300I,TYPE=SUP PERFORM IN MPFLST
*              PROBABLE INVALID RLD COUNT
DFH1505  IEECODE ID=DFH1505,TYPE=REPLY,CMD='R XX,GO',DELAY=15
*              REPLY GO OR CANCEL
DSN3104I IEECODE ID=DSN3104I,TYPE=CMD,CMD='P IRLMPROC'
*              DB2 TERMINATION COMPLETE
DXR009I  IEECODE ID=DXR009I,TYPE=CMD,CMD='-START DB2,PARM(DSNZPAR2)',  X
               SCAN='IRLM'
*              IRLM INITIALIZATION  COMPLETE (START DB2)
ERB100I  IEECODE ID=ERB100I,TYPE=CMD,CMD='F RMF,S III',                X
               SCAN=' ZZ : ACTIVE'
*EA994   IEECODE ID=IEA994,TYPE=CMD,CMD='S SVCDUMPS'
*              ALL SVC DUMP DATASETS ARE FULL
*EA995I  IEECODE ID=IEA995I,TYPE=SUP PERFORM IN MPFLST
*              SYMPTOM DUMP OUTPUT
*EC130I  IEECODE ID=IEC130I,TYPE=SUP PERFORM IN MPFLST
*              DD STATEMENT MISSING
*EC161I  IEECODE ID=IEC161I,TYPE=SUP PERFORM IN MPFLST
*              IMPLICIT VERIFY/VSAM-RELATED OPEN-CLOSE-EOV
IEC804A  IEECODE ID=IEC804A,TYPE=REPLY,CMD='R XX,CONT',DELAY=15
*              REPLY POST, CONT OR DROP
*EE361I  IEECODE ID=IEE361I,TYPE=CMD,CMD='S SMFDUMP*'
*              SMF DATA LOST
*EE362   IEECODE ID=IEE362,TYPE=SUP
*              SMF ENTER DUMPR FOR SYS1.MAN
*EE366I  IEECODE ID=IEE366I,TYPE=CMD,CMD='S SMFDUMP*'
*              SMF DATA LOST
IEFTMS0  IEECODE ID=IEFTMS0,TYPE=REPLY,CMD='R XX,U',DELAY=10
*              REPLY U FOR TMS INITIALIZATION
IEFTMS4  IEECODE ID=IEFTMS4,TYPE=REPLY,CMD='R XX,U',DELAY=10
*              VERIFY DATE FOR TMS
IEF238D  IEECODE ID=IEF238D,TYPE=REPLY,CMD='R XX,WAIT',DELAY=10,       X
               SCAN='WAIT'   REPLY DEVICE NAME, WAIT OR CANCEL
IEF433D  IEECODE ID=IEF433D,TYPE=REPLY,CMD='R XX,NOHOLD',DELAY=10
*              REPLY HOLD OR NOHOLD
IEF434D  IEECODE ID=IEF434D,TYPE=REPLY,CMD='R XX,NOHOLD',DELAY=10
*              INVALID REPLY, REPLY HOLD OR NOHOLD
*EF677I  IEECODE ID=IEF677I,TYPE=SUP PERFORM IN MPFLST
*              WARNING MESSAGE(S) ISSUED FOR JOB
*FB040I  IEECODE ID=IFB040I,TYPE=CMD,CMD='S LOGRECX'
*              SYS1.LOGREC AREA IS FULL
*FB060E  IEECODE ID=IFB060E,TYPE=CMD,CMD='S LOGRECX'
*              SYS1.LOGREC NEAR FULL
IKT010D  IEECODE ID=IKT010D,TYPE=REPLY,CMD='R XX,SIC',DELAY=5
*              XX USERS ACTIVE, REPLY SIC OR FSTOP
IKT012D  IEECODE ID=IKT012D,TYPE=REPLY,CMD='R XX,U',DELAY=10
*              TCAS TERMINATION, REPLY U OR DUMP
IST020I  IEECODE ID=IST020I,TYPE=CMD,CMD='S VTAMUP'
*              VTAM INITIALIZATION COMPLETE
U11601   IEECODE ID=U11-601,TYPE=REPLY,CMD='R XX,U',DELAY=10
*              UCC11 REPLY U OR PASSWORD
BBB001I  IEECODE ID=BBB001I,TYPE=CMD,CMD='S X',DELAY=10,               X
               SCAN=', BRUCE'     TEST MESSAGE
BBB002I  IEECODE ID=BBB002I,TYPE=REPLY,CMD='R XX,NO',DELAY=10,         X
               SCAN=', BRUCE'     TEST MESSAGE
MSGEND   IEECODE ID=********,TYPE=END  END OF MESSAGE TABLE
         LTORG
WORKAREA DSECT
EXITSAVE DS    18F                     STANDARD OS SAVEAREA-IEAVMXIT
IRBSAVE  DS    18F                     STANDARD OS SAVEAREA-IRBCODE
SRBRET   DS    F                       SAVE AREA FOR SRB RETURN ADDRESS
DELAY    DS    F                       DELAY TIME AREA FOR CMD/REPLY
SRBAREA  DS    CL(SRBSIZE)             AREA TO BUILD SRB IN
         DS    0F
WTOAREA  DS    CL(WTOLEN)' '           AREA FOR CMD/REPLY TO GO INTO
EXITFLAG DS    X                       FLAG BYTE
SRBSCHED EQU   X'80'                     SRB SCHEDULED
ASVTSCAN EQU   X'40'                     ASVT SCAN ACTIVE
SRBACTIV EQU   X'20'                     SRB ACTIVE
IRBACTIV EQU   X'10'                     IRB ACTIVE
CIRBSCHD EQU   X'08'                     CIRB SCHEDULED
STG2SCHD EQU   X'04'                     STAGE 2 SCHEDULED
IRBDONE  EQU   X'02'                     IRB DONE
SRBDONE  EQU   X'01'                     SRB DONE
TYPEFLAG DS    X                       FLAG BYTE
CMDRESP  EQU   X'80'                     COMMAND RESPONSE REQUIRED
RPLYRESP EQU   X'40'                     REPLY RESPONSE REQUIRED
WORKLEN  EQU   *-WORKAREA              CALC LENGTH FOR GETMAIN
TABLENT  DSECT
MSGID    DS    CL8                     MESSAGE ID
TYPE     DS    AL1                     TYPE (ADD, CHG, REPLY, CMD, ETC)
MSGLEN   DS    AL1                     MESSAGE ID LENGTH
CMDLEN   DS    AL1                     LENGTH OF CMDTEXT
ENTLEN   DS    AL1                     LENGTH OF THIS ENTIRE ENTRY
SCANLEN  DS    AL1                     LENGTH OF THIS SCAN DATA
DESCCODE DS    BL2                     WTO DESCRIPTOR CODES
ROUTCODE DS    BL2                     WTO ROUTE CODES
DELAYTIM DS    AL4                     DELAY TIME FOR COMMANDS/REPLIES
CMDTEXT  DS    0CL126                  VARIABLE LENGTH COMMAND TEXT
SCANTEXT DS    0CL80                   VARIABLE LENGTH SCAN TEXT
         IEZVX100
         IHASRB
         IHAPSA LIST=NO,DSECT=YES
         CVT   LIST=NO,DSECT=YES
         IHAASVT LIST=YES,DSECT=YES
         IHAASCB LIST=NO,DSECT=YES
         IHARB LIST=NO,DSECT=YES
         IHAIQE
         IEECUCM DSECT=YES,LIST=NO
         IHAORE  DSECT=YES
         END


Bruce Bordonaro
Systems Software Manager (USA) © Bruce Bordonaro 1990

