Debugging COBOL DL/I programs

The following article describes how to set up the COBOL II
interactive debug tool COBTEST to allow the debugging of IMS
programs.  On page 34 of the VS COBOL II General Information
manual, it is stated that IBM's BTS product is needed to debug
COBOL programs having DL/I calls.

This is not a very attractive way of using COBTEST: it requires
you to purchase BTS and supports only limited line mode
debugging; many of the wonderful features of the debugging tool
will not work (eg full screen colour display, source and auto-
monitor windows, find commands etc).  The Assembler code
presented here will interface COBTEST under ISPF to IMS, so
that EXEC DLI statements will be correctly executed.

The principal reason ISPF/COBTEST and IMS don't work
together is the fact they both want to use the same location in the
first save area under the TCB (save area pointed by TCBFSA).
The following program uses an ATTACH macro to execute the
IMS start-up program DFSRRC00 as a separate MVS subtask.
This way, both products have their own TCB and associated first
save area which they can use if they like.

However, running IMS this way creates a few minor problems.  A
means of synchronisation must be provided between the two
subtasks.  Also, in order for the DL/I call to be properly
processed, the parameter list with destination IMS must be passed
from the ISPF/COBOL task to the IMS task.  Synchronisation is
accomplished by alternating POST/WAIT sequences.  When the
COBOL program wants to execute a DL/I call, the CSECT
FAKEHLPI is entered via a branch from DFSLI000.  The code in
FAKEHLPI will store its entry value for register one, POST the
ECB that IMS is waiting for, and WAIT for IMS to signal
completion of its DL/I processing.  The application routine offered
to IMS, CSECT DLIAPPL, picks up the register one value
FAKEHLPI has stored, and passes it to IMS.  When IMS returns
from DL/I processing, the COBOL subtask is POSTed ready and
IMS WAITs for either a new DL/I call or a request to terminate.
In order for COBOL to enter the CSECT FAKEHLPI, a modified
version of module DFSLI000, which won't access the same first
save area field as COBOL and ISPF are using, is required.  This
modified stub must be link-edited to the COBOL application
program instead of the original IBM-supplied version. The
modified stub can be linked to the COBOL program with this
JCL:

//LKED   EXEC  PGM=IEWL
// PARM=(XREF,LIST,NOTEST)
//SYSLIN   DD  DSN=&&LOADSET,DISP=(OLD,DELETE)
//         DD  *
 INCLUDE OBJ(DFSLI000)  INCLUDE MODIFIED DLI LANGUAGE INTERFACE
/*
//SYSLMOD  DD  DSN=LOADLIB(MEMBER),DISP=SHR
//OBJ      DD  DSN=SYS2.OBJECT,DISP=SHR
//SYSLIB   DD  DSN=SYS1.COB2LIB,DISP=SHR
//SYSUT1   DD  UNIT=VIO,SPACE=(CYL,(1,1))
//SYSPRINT DD  SYSOUT=*

To invoke the IMS support program, CLIST IGZTCINV should
be changed by modifying the statement 'SELECT
PGM(IGZTISPF) PARM( ...' as follows:

    IF '&PSBNAME' = '' THEN +
        ISPEXEC SELECT PGM(IGZTISPF) PARM(COBTEST LOAD(&ZMEM:COBDD))
    ELSE +
        ISPEXEC SELECT PGM(SYSB143)  PARM(PSB=&PSBNAME)

This assumes CLIST variable &PSBNAME contains a valid IMS
PSB name.  The PSB name could be picked up with a VGET
from panel IGZTPIN2.  The DDNAMEs  STEPLIB, DFSRESLB,
IMS, IEFRDER, SYSUDUMP, DFSVSAMP, and also all the
required database clusters and datasets should be allocated exactly
as they would in batch.

For performance reasons, we have shortened the IGZTCINV
CLIST to an absolute minimum. We have moved all allocations to
a separate CLIST which is executed only once for the entire TSO
session.  Performance considerations are appropriate here, since it
takes about a full minute to start the IMS subtask on our 3081K in
MVS/SP.  The MVS/XA LLA feature could do much good by
speeding up the loading of the many IMS modules, but I have not
yet been able to verify this.

When assembling the two programs presented here, make sure
JES328X and IMS macro libraries are concatenated to SYSLIB.
(For local DSNAMEs, check the SMP DDDEFs JSXASM in the
MVS zone and GENLIBB in the IMS zone).

*  MAIN CODE FOR COBOL II DEBUGGER / IMS INTERFACE.
*  BEFORE ASSEMBLY ENSURE JES328X AND IMS MACRO LIBRARIES ARE
*  CONCATENATED TO SYSLIB.
*  THE STRUCTURED MACROS USED ARE THOSE DELIVERED WITH JES328X.
*  ATTRIBUTES:
*   REENTRANT, USER STORAGE KEY, PROBLEM STATE.
*  THIS CODE WAS DEVELOPED AND TESTED WITH:
*   COBOL 2: HCQ1200,HCL1200,JCL1200,JCL1221
*   IMS    : HIM1304
*   ISPF   : HIF2303,HDV2302
SYSB143 CSECT                  REENTRANT LINKAGE:
 USING *,R12                   ADDRESSABILITY IN THIS CSECT
 COPY  JSXMACS                 JES328X STRUCTURED MACRO DEFINITIONS
 SAVE  (14,12),,SYSB143.&SYSTIME.D.&SYSDATE   SAVE REGISTERS
 LR    R12,R15                 R12 = BASE REG FOR THIS CSECT
 L     R2,0(R1)                R2  = ADDRESS OF PARMS FOR THIS PROGRAM
 LA    R3,BYTES_REQUIRED       R3  = LENGTH OF STORAGE BLOCK
 GETMAIN R,                    OBTAIN WORKING STORAGE                  C
               LV=(R3),                                                C
               RELATED=WORKING_STORAGE
 LR    R11,R1                  R11 = WORKING STORAGE BLOCK ADDRESS
 USING WORKAREA,R11            R11 = BASE FOR OUR STORAGE
 LR    R0,R11                  R0  = ADDRESS OF WORK AREA
 LA    R1,BYTES_REQUIRED       R1  = LENGTH OF WORK AREA
 XR    R15,R15                 R15 = MVCL PADDING CHARACTER
 MVCL  R0,R14                  PAD ENTIRE WORK AREA WITH NULLS
 MVC   WORKEYE,=C'WEYE'        AND INIT WITH EYE CATCHER
 ST    R13,4(,R11)             CHAIN SAVE AREAS FOREWARD
 ST    R11,8(,R13)             CHAIN SAVE AREAS BACKWARD
 LR    R13,R11                 R13 = ADDRESS OF NEW SAVE AREA
 L     R1,4(R13)               R1 = ADDRESS OF PREVIOUS SAVE AREA
 LM    R0,R1,20(R1)            RESTORE R0-R1 TO PGM ENTRY VALUES
 ST    R1,SYSB143_PARM         SAVE PROGRAM ENTRY REG 1
 TPUT  MSG1,L'MSG1             MSG: PROGRAM STARTED
 MVI   DFS_DATA,C' '           1ST BYTE OF PARM STRING = SPACE
 MVC   DFS_DATA+1(99),DFS_DATA INIT ENTIRE PARM STRING TO SPACES
 LH    R4,0(R2)                R4 = TRUE LENGTH OF PARAMETER STRING
 SL    R4,=F'4'                R4 = LENGTH - LENGTH 'PSB='
 MVC   DFS_DATA(L'IMS_P1),IMS_P1 BUILD PARM STRING FOR IMS:  PART 1
 LA    R3,DFS_DATA+L'IMS_P1    R3 = WHERE TO SINK PSBNAME IN IMS PARM
 MVC   0(8,R3),6(R2)           BUILD PARM STRING FOR IMS:  PSBNAME
 LA    R3,0(R3,R4)             R4 = ADDRESS PSBNAME + LENGTH PSBNAME
 MVC   0(L'IMS_P2,R3),IMS_P2   BUILD PARM STRING FOR IMS:  PART 2
 LA    R3,L'IMS_P1+L'IMS_P2(R4) R3 = LENGTH OF PSBNAME + LENGTH P1+P2
 STH   R3,DFS_PARM             SET LENGTH OF PARAMETER FOR IMS
* TPUT DFS_DATA,100            MSG: SHOW DFSRRC00 PARM STRING ++DBUG++
 L     R15,=V(DLIAPPL)         R15 = EP OF OUR DLI APPL PROGRAM
 IDENTIFY EP=DLIAPPL,ENTRY=(R15) APPLICATION PROGRAM IN POSITION
 LA    R3,FAKE_ECESPRHT        R3 = WHERE DFHEI1 MUST FIND ECESPRHT
 LA    R2,ECESPRHT-DFSECP      R2 = DISPLACEMTENT OF ECESPRHT IN DFSECP
 SLR   R3,R2                   R3 = ORIGIN OF DFSECP BLOCK FOR DFHEI1
 L     R2,CVTPTR               R2 = ADDRESS OF CVT
 L     R2,(CVTTCBP-CVTMAP)(R2) R2 = ADDRESS OF TCB  PTR LIST
 L     R2,4(R2)                R2 = ADDRESS OF CURRENT TCB
 L     R2,(TCBFSA-TCB)(R2)     R2 = ADDRESS OF 1ST SAVE AREA FOR TCB
 ST    R3,20(R2)               STORE PTR FOR MODIFIED IMS HLPI
 LA    R2,HLPI_EP-4            R2 = ADDR OF PTR TO EPA OF FAKEHLPI -4
 ST    R2,FAKE_ECESPRHT        STORE BRANCH EP OF FAKEHLPI
 MVC   ATTACH_L(ATTACH_LEN),ATTACH_I INIT ATTACH LIST
 LA    R3,IMS_ECB              R3 = ADDRESS OF IMS TASK ECB
 LA    R4,DFS_PARM             R4 = ADDRESS OF PARM STRING FOR DFSRRC00
 XC    IMS_ECB,IMS_ECB         CLEAR 'IMS-READY' ECB
 ATTACH EP=DFSRRC00,           START-UP IMS ENVIRONMENT                C
               ECB=(R3),                                               C
               PARAM=((R4)),                                           C
               VL=1,                                                   C
               MF=(E,ATTACH_UL),                                       C
               SF=(E,ATTACH_L)
 ST    R1,TCB_PTR              SAVE ADDRESS OF NEW TCB
 LR    R2,R1                   R2 = ADDRESS OF NEWLY CREATED TCB
 WAIT  ECB=IMS_ECB             WAIT UNTIL IMS INITIALIZATION COMPLETE
 XR    R4,R4                   R4 = 0
 ICM   R4,B'0111',(TCBCMPC-TCB)(R2) R4 = TCB COMPLETION CODE, 3 BYTES
 IF    (Z)                     IF THERE IS NO COMPLETION CODE IN TCB
       TPUT  MSG2,L'MSG2           MSG: IMS INIT OK, STARTING COBTEST
       LINK  EP=ISPEXEC,           LINK TO ISPF TO START UP COBTEST    C
               PARAM=(ISPF_PLEN,ISPF_PARM),VL
       XR    R4,R4                 R4 = 0
       ICM   R4,B'0111',(TCBCMPC-TCB)(R2) R4 = TCB COMP CODE
       IF    (Z)                   IS THERE A COMPLETION CODE IN TCB ?
             XC    IMS_ECB,IMS_ECB CLEAR IMS ECB FOR RE-USE
             POST  CBL_ECB,TERM    POST CBL READY TO TERMINATE IMS TASK
             WAIT  ECB=IMS_ECB     WAIT FOR REQUESTED TERMINATION DONE
       ELSE
             TPUT  MSG5,L'MSG5     MSG: IMS ABENDED DURING DLI CALL
             BAL   R10,ABEND_MSG   MSG: ABEND CODE XXX
       ENDIF
 ELSE
       TPUT  MSG4,L'MSG4           MSG: IMS INITIALIZATION FAILURE
       BAL   R10,ABEND_MSG         MSG: ABEND CODE XXX
 ENDIF
 DETACH TCB_PTR                REMOVE COMPLETED TCB
 DELETE EP=DLIAPPL             DELETE MODULE FROM STORAGE
 TPUT  MSG3,L'MSG3             MSG: THIS PROGRAM HAS ENDED
 LA    R3,BYTES_REQUIRED       R3 = LENGTH OF WORKING STORAGE
 L     R13,4(R13)              RECOVER ADDRESS OF PREVIOUS SA
 FREEMAIN R,                          FREE ALL WORKING STORAGE         C
               LV=(3),                R3 = LENGTH OF OF STORAGE BLOCK  C
               A=(R11),               R11 = ADDRESS OF STORAGE BLOCK   C
               RELATED=WORKING_STORAGE
 XR    R15,R15                 R15 = 0
 RETURN (14,12),RC=(15)        RETURN TO CALLER WITH RC IN R15
* ABEND_MSG: FORMAT A 3 BYTE SYSTEM/USER ABEND CODE FIELD AND
* DISPLAY MESSAGE.
* ON ENTRY R4 CONTAINS ABEND CODE.
ABEND_MSG DS 0H
 MVI  TPUT_LNE,C' '           CLEAR TPUT MESSAGE BUFFER
 MVC  TPUT_LNE+1(69),TPUT_LNE INIT ENTIRE RECORD TO SPACES
 LR   R5,R4                   R5 = COPY OF ABEND CODES
 SRL  R4,12                   R4 = SYSTEM ABEND CODE (IF PRESENT)
 IF (LTR,R4,R4,NZ)            IF IS THIS A SYSTEM ABEND
    MVC  TPUT_LNE(20),=CL20'SYSTEM ABEND CODE '
    STH  R4,WORK                 STORE ABEND CODE IN WORK AREA
    UNPK UNPK_WRK(5),WORK(3)     UNPACK ABEND CODE IN UNPK_WRK
    TR   UNPK_WRK(4),HEXTAB      TRANSLATE UNPK_WRK TO HEX CHARS
    MVC  TPUT_LNE+18(3),UNPK_WRK+1 MOVE IN COMPLETION CODE
 ELSE
    MVC  TPUT_LNE(20),=CL20'USER ABEND CODE '
    N    R5,=X'00000FFF'         MASK USER ABEND CODE
    CVD  R5,UNPK_WRK             USER ABEND CODE FROM BINARY TO PACKED
    UNPK TPUT_LNE+16(4),UNPK_WRK+6(2) UNPACK ABEND CODE IN UNPK_WRK
    OI   TPUT_LNE+19,X'F0'       KILL INVALID CHARACTER
 ENDIF
 TPUT TPUT_LNE,32             ISSUE MSG ABEND OCCURED ...
 BR 10                        RETURN TO CALLER
 DROP  R11                    DROP WORK AREA ADDRESSABILITY
ATTACH_I  ATTACH EP=XXX,SF=L
MSG1      DC    C'IMS SUPPORT PGM, COMPILED &SYSDATE &SYSTIME'
MSG2      DC    C'IMS INITIALIZED, STARTING COBTEST ...'
MSG3      DC    C'IMS SUPPORT PGM ENDED '
MSG4      DC    C'IMS INITIALIZATION FAILED, COBTEST NOT STARTED'
MSG5      DC    C'IMS FAILED DURING USER DLI CALL'
HLPI_EP   DC    V(FAKEHLPI)    ENTRY POINT OF FAKE HLPI
ISPF_PARM DC    CL20'SELECT PGM(IGZTISPF)'
          DC    CL40' PARM(COBTEST LOAD(&&ZMEM:COBDD))'
ISPF_PLEN DC    F'60'          LENGTH OF PARAMETER STRING FOR ISPF
IMS_P1    DC    C'DLI,DLIAPPL,' PARTS 1 AND 2 OF IMS PARMSTRING -
IMS_P2    DC    C',7,0000,,0,,N,0,T,,,N,,,N' PSBNAME GOES IN BETWEEN
          DC    C'0123456789ABCDEF' HEX TRANS TABLE FOR ABEND CODES
HEXTAB    EQU   *-256
 LTORG
***********************************************************************
FAKEHLPI  CSECT                FAKE HLPI ENTERED BY BRANCH FROM DFSLI00
 STM   14,12,12(13)            SAVE REGS IN SA PROVIDED BY COBOL 2
 BALR  R12,0                   R12 = BASE REG FOR THIS CODE
 USING *,R12                   CSECT ADDRESSABILITY
 L     R2,CVTPTR               R2 = ADDRESS OF CVT
 L     R2,(CVTTCBP-CVTMAP)(R2) R2 = ADDRESS OF TCB  PTR LIST
 L     R2,4(R2)                R2 = ADDRESS OF CURRENT TCB
 L     R2,(TCBFSA-TCB)(R2)     R2 = ADDRESS OF 1ST SAVE AREA FOR TCB
 DO WHILE=(CLC,EPA_SAVE(4,R2),NE,MY_EPA)  LOOK FOR LOAD MODULE EPA
       L  R2,LOWER_SA(R2)      R2 = ADDRESS OF NEXT LOWER SAVE AREA
 ENDDO
 L     R2,LOWER_SA(R2)         R2 = ADDRESS OF NEXT LOWER SAVE AREA
 USING WORKAREA,R2             R2 = BASE FOR OUR WORKAREA DSECT
                                                                SPACE
 L     R3,TCB_PTR              R3 = ADDRESS OF TCB FOR IMS TASK
 XR    R4,R4                   R4 = 0
 ICM   R4,B'0111',(TCBCMPC-TCB)(R3) R4 = TCB COMPLETION CODE
 IF    (Z)                     IF IMS TASK IS ALIVE & HEALTHY
       ST    R1,CBL_PARM          STORE PARM POINTER PASSED FROM COBOL
       XC    IMS_ECB,IMS_ECB      CLEAR IMS ECB FOR RE-USE
       POST  CBL_ECB,DLICALL      WAKE-UP IMS TO PROCESS A DLI CALL
       WAIT  ECB=IMS_ECB          WAIT UNTIL IMS HAS COMPLETED DLI CALL
 ENDIF
 ICM   R4,B'0111',(TCBCMPC-TCB)(R3) R4 = TCB COMPLETION CODE
 IF    (NZ)                    IF IMS TASK DIED PROCESSING DLI CALL
       LR  R5,R4                  R5 = COPY OF ABEND CODE
       SRL R4,12                  R4 = SYSTEM ABEND CODE (IF PRESENT)
       IF (LTR,R4,R4,NZ)          IF IS THIS A SYSTEM ABEND
           ABEND (4),,,SYSTEM          TELL COBTEST SOMETHING IS WRONG
       ELSE                       NO, USER ABEND
           N    R5,=X'00000FFF'        MASK USER ABEND CODE
           ABEND (5)                   TELL COBTEST SOMETHING IS WRONG
       ENDIF
 ENDIF
 L     R15,IMS_RC              R15 = RETURN CODE FROM DLI
 LM    14,12,12(R13)           RELOAD REGS FOR COBOL
 BR    R14                     RETURN CONTROL TO COBOL PGM
 LTORG
MY_EPA DC V(SYSB143)           ENTRY POINT OF THIS  L O A D  MODULE
***********************************************************************
DLIAPPL   CSECT                DLI <APPLICATION> PROGRAM FOR DFSRRC00
 USING *,R12                   ADDRESSABILITY IN THIS CSECT
 SAVE  (14,12),,DLIAPPL        SAVE REGISTERS IN SA PROVIDE BY DFSRRC00
 LR    R12,R15                 R12 = BASE REG FOR THIS CSECT
 GETMAIN R,LV=72,                     OBTAIN SAVE AREA                 C
               RELATED=DYNAMIC_SAVE_AREA
 ST    R13,4(,R1)              CHAIN SAVE AREAS FOREWARD
 ST    R1,8(,R13)              CHAIN SAVE AREAS BACKWARD
 LR    R13,R1                  R13 = ADDRESS OF NEW SAVE AREA
 L     R1,4(R13)               R1 = ADDRESS OF PREVIOUS SAVE AREA
 LM    R0,R1,20(R1)            RESTORE R0-R1 TO PGM ENTRY VALUES
* FIND SYSB143 USER STORAGE BLOCK
 L     R11,CVTPTR               R11 = ADDRESS OF CVT
 L     R11,(CVTTCBP-CVTMAP)(R11) R11 = ADDRESS OF TCB  PTR LIST
 L     R11,4(R11)              R11 = ADDRESS OF CURRENT TCB
 L     R11,(TCBOTC-TCB)(R11)   R11 = ADDRESS OF MOTHER OF CURRENT TCB
 L     R11,(TCBFSA-TCB)(R11)   R11 = ADDRESS OF 1ST SAVE AREA FOR TCB
 DO WHILE=(CLC,EPA_SAVE(4,R11),NE,LMOD_EPA) LOOK FOR MODULE EPA
       L  R11,LOWER_SA(R11)    R11 = ADDRESS OF NEXT LOWER SAVE AREA
 ENDDO
 L     R11,LOWER_SA(R11)       R11 = ADDRESS OF NEXT LOWER SAVE AREA
 USING WORKAREA,R11            R11 = BASE FOR OUR WORKAREA DSECT
 IF   (CLC,WORKEYE,NE,=C'WEYE') TEST FOR DISASTER: WORK AREA NOT THERE
       ABEND 100,DUMP             SUICIDE, UNABLE TO PROCEED ...
 ENDIF
 XC    CBL_ECB,CBL_ECB         CLEAR COBOL ECB FOR RE-USE
 POST  IMS_ECB,0               SIGNAL INITIALZATION COMPLETE TO CBL
 WAIT  ECB=CBL_ECB             WAIT UNTIL POSTED AGAIN BY COBOL TASK
 LA    R2,TERM                 R2 = TERMINATE POSTCODE FOR COMPARE
 DO WHILE=(CH,R2,NE,CBL_PC)    DO WHILE NOT REQUESTED TO TERMINATE
       L     R1,FAKE_LIPARM       RELOAD PARM PTR PASSED FROM COBOL
       IC    R2,FAKE_LILAN        R2 = 1 BYTE WITH LILAN FROM CBL
       L     R15,CVTPTR           R15 = ADDRESS OF CVT
       L     R15,(CVTTCBP-CVTMAP)(R15) R15 = ADDRESS OF TCB  PTR LIST
       L     R15,4(R15)           R15 = ADDRESS OF CURRENT TCB
       L     R15,(TCBFSA-TCB)(R15) R15 = ADDR OF 1ST SAVE AREA FOR TCB
       L     15,24(,15)           REG1 CONTAINS A(PXPARMS)/A(ECP)
       USING PXPARMS,15
       ST    1,LIPARM             LIPARM = USER REG 1
       STC   R2,LILAN             SET LANGUAGE INDICATOR
       LR    1,15                 A(PXPARMS)/A(ECP)
       USING DFSECP,15            TELL ASSEMBLER
       L     15,ECESPRHT          A(EXT SUBSYS RQST HNDLR TABLE)
       L     15,4(,15)            PICK UP PRH ADDR
       BALR  14,15                ENTER PGM REQ HANDLER
       ST    R15,IMS_RC           SAVE IMS RETURN CODE FOR COBOL
       XC    CBL_ECB,CBL_ECB      CLEAR CBL ECB FOR REUSE
       POST  IMS_ECB,0            POST IMS READY, DLI CALL COMPLETE
       WAIT  ECB=CBL_ECB          WAIT UNTIL POSTED AGAIN BY COBOL TASK
       LA    R2,TERM              R2 = TERMINATE POSTCODE FOR COMPARE
 ENDDO
 LR    R2,R13                  R2 = COPY OF CURRENT SA ADDRESS
 L     R13,4(R13)              RECOVER ADDRESS OF PREVIOUS SA
 FREEMAIN R,LV=72,                    FREEMAIN SAVE AREA               C
               A=(R2),                R2 = ADDRESS OF SAVE AREA        C
               RELATED=DYNAMIC_SAVE_AREA
 XR    R15,R15                 CLEAR RC
 RETURN (14,12),RC=(15)        RETURN TO CALLER WITH RC IN R15
LMOD_EPA  DC    V(SYSB143)     ENTRY POINT OF THIS  L O A D  MODULE
 LTORG
***********************************************************************
WORKAREA  DSECT                WORKING STORAGE FOR THIS PROGRAM
MAIN_SAVE DS    18F            REENTRANT SAVE AREA FOR MAIN CODE
EPA_SAVE  EQU   16,4           EPA OFFSET IN SAVE AREA
LOWER_SA  EQU   8,4            NEXT LOWER SAVE AREA OFFSET
HLPI_SAVE DS    18F            SAVE AREA FOR HLPI
WORKEYE   DS    CL4            WORKING STORAGE EYE CATCHER
RETRY_PTR DS    F              ADDRESS WHERE TO RESUME SYSB143
ATTACH_L  ATTACH EP=XXX,SF=L   LISTFORM ATTACH MACRO
ATTACH_LEN EQU   *-ATTACH_L    LENGTH OF ATTACH LIST
ATTACH_UL DS    2F             STORAGE FOR USER PARAMETER LIST
TCB_PTR   DS    F              POINTER TO DFSRRC00 TCB
IMS_ECB   DS    F              ECB POSTED WHEN IMS READY
DLICALL   EQU   73             POST CODE, REQUEST A DLI CALL
TERM      EQU   19             POST CODE, REQUEST TO TERMINATE
CBL_ECB   DS    F              ECB FOR COBOL PROGRAM UNDER COBTEST
CBL_PC    EQU   CBL_ECB+2,2    POST CODE IN THE IMS ECB
SYSB143_PARM    DS F           SYSB143 PROGRAM ENTRY REG 1 VALUE
CBL_PARM  DS    F              R1  PASSED FROM COBOL TO HLPI  (PARMS)
IMS_RC    DS    F              R15 PASSED FROM HLPI  TO COBOL (RC)
RECUR_ERR DS    C              RECURSIVE ERROR FLAG
UNPK_WRK  DS    D              WORK AREA FOR UNPACK
          DS    H
WORK      DS    H              ABEND CODE UNPACK WORK AREA
          DS    H
TPUT_LNE  DS    CL70           LINE BUFFER FOR MESSAGES
DFS_PARM  DS    H              IMS PARM STRING, LENGTH HALFWORD
DFS_DATA  DS    CL100          IMS PARM STRING, CHARACTER STRING
FAKE_ECESPRHT   DS F           ECESPRHT TO FOOL DFSLI/DFHEI1  + X'FC'
                DS F           DUMMY WORD TO PRESERVE LAYOUT  + X'100'
FAKE_LIPARM     DS F           LIPARMS  TO FOOL DFSLI/DFHEI1  + X'104'
FAKE_LILAN      DS F           LILAN    TO FOOL DFSLI/DFHEI1  + X'108'
BYTES_REQUIRED  EQU *-WORKAREA LENGTH OF STORAGE BLOCK
 IKJTCB LIST=YES
 IHASDWA
 PARMBLK DSECT
 DFSECP  IMS
 CVT     DSECT=YES,PREFIX=YES
 END

The DFSLI000 replacement was written to be compatible with
both the standard batch environment and the COBTEST
environment.  To determine whether or not the COBOL program
is running under the debugger, the presence of a Log-on Work
Area (LWA) is tested for.  When a pointer to a valid LWA exists
in the ASXBLWA field, it is assumed the COBOL-DL/I program
runs under control of the debugger.

* UPJOHN REPLACEMENT DFSLI000,
* BEFORE LOADING ADDRESS OF IMS/VS ANCHOR BLOCK, A TEST IS MADE TO
* SEE IF WE'RE RUNNING UNDER A TERMINAL MONITOR PGM. (NON-ZERO ASXBLWA)
* IF UNDER TMP THE ANCHOR BLOCK PTR IS LOADED FROM OFFSET 20 DEC
* IN THE FIRST SAVE AREA INSTEAD OF 24.
* THIS MODIFICATION IS NEEDED FOR THE COBOL II DEBUG TOOL
* RELEASE V2.2.0, WHEN DEBUGGING A COBOL PGM WITH DLI CALLS.
* ATTRIBUTES: REENTRANT, USER STORAGE KEY, PROBLEM STATE.
*   THIS MODULE IS STORED AS OBJECT CODE.
DFSLI000 CSECT
 ENTRY DFHEI01
 ENTRY DFHEI1
DFHEI1  EQU *                   ENTRY POINTS FOR EXEC-DLI CALLS
DFHEI01 EQU *
 L     R15,CVTPTR               R15 = ADDRESS OF CVT
 L     R15,(CVTTCBP-CVTMAP)(R15) R15 = ADDRESS OF TCB / ASCB PTR LIST
 L     R15,12(R15)              R15 = ADDRESS OF CURRENT ASCB
 L     R15,(ASCBASXB-ASCB)(R15) R15 = ADDRESS OF AS EXTENSION BLOCK
 L     R15,(ASXBLWA-ASXB)(R15)  R15 = ADDRESS OF LWA IF PRESENT
 LTR   R15,R15                  SET CONDITION CODE IF ASXBLWA = ZERO
 BALR  R15,0                    RELOAD CSECT BASE, COND CODE UNCHANGED
 USING *,R15                    ADDRESSABILITY IN THIS CSECT
 BNZ   UNDER_DEBUG              IF RUNNING UNDER IKJEFT01 BRANCH
NATIVE DS    0H
       L     R15,CVTPTR               R15 = ADDRESS OF CVT
       L     R15,(CVTTCBP-CVTMAP)(R15) R15 = ADDRESS OF TCB  PTR LIST
       L     R15,4(R15)               R15 = ADDRESS OF CURRENT TCB
       L     R15,(TCBFSA-TCB)(R15)    R15 = ADDRESS OF FIRST SAVE AREA
       L     R15,24(,15)              R0 = ADDRESS OF PXPARMS OR ECP
       USING PXPARMS,15
       ST    1,LIPARM                 LIPARM = USER REG 1
       MVI   LILAN,LIASM              SET LANGUAGE INDICATOR
       LR    1,15                     A(PXPARMS)/A(ECP)
       USING DFSECP,15                TELL ASSEMBLER
       L     15,ECESPRHT              A(EXT SUBSYS RQST HNDLR TABLE)
       L     15,4(,15)                PICK UP PRH ADDR
       BR    15                       ENTER PGM REQ HANDLER
UNDER_DEBUG  DS 0H
       L     R15,CVTPTR               R15 = ADDRESS OF CVT
       L     R15,(CVTTCBP-CVTMAP)(R15) R15 = ADDRESS OF TCB  PTR LIST
       L     R15,4(R15)               R15 = ADDRESS OF CURRENT TCB
       L     R15,(TCBFSA-TCB)(R15)    R15 = ADDRESS OF FIRST SAVE AREA
       L     R15,20(,15)             R15 = ADDRESS OF PXPARMS OR ECP
       USING PXPARMS,15
       ST    1,LIPARM                 LIPARM = USER REG 1
       MVI   LILAN,LIASM              SET LANGUAGE INDICATOR
       LR    1,15                     A(PXPARMS)/A(ECP)
       USING DFSECP,15                TELL ASSEMBLER
       L     15,ECESPRHT              A(EXT SUBSYS RQST HNDLR TABLE)
       L     15,4(,15)                PICK UP PRH ADDR
       BR    15                       ENTER PGM REQ HANDLER
 LTORG
 DS    0D                       DOUBLE WORD BDY, FOR FIND CMD IN DUMP
 DC    C'DFSLI000'              EYE CATCHER / SEARCH ARGUMENT
 DC    C'COMPILED &SYSDATE &SYSTIME' WHEN COMPILED
 IHAASCB DSECT=YES
 IHAASXB DSECT=YES
 IKJTCB
 PARMBLK DSECT
 DFSECP  IMS
 CVT     DSECT=YES,PREFIX=YES
 END


Tim Alpaerts
Systems Programmer
Upjohn NV (Belgium) © Xephon 1989

