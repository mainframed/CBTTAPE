Listing allocated datasets

      ALC DDNAME
      ALC dataset name
      ALC member name

/*-REXX/ALC-----------------------------------------------------------*/
    address TSO
    trace 0
    parse upper arg Parms Search .
    x = outtrap('LINE.')         /* Trap LISTALC output in LINE stem  */
    'LISTALC HISTORY STATUS'
    x = outtrap('OFF')
    do Ind = 2 to Line.0 by 2
       parse var Line.Ind DSName .
       if DSName = 'TERMFILE'                /* Reject TERMFILEs DSNs */
          then do
               Ind = Ind - 1
               iterate
               end
       Ind1 = Ind + 1
       parse var Line.Ind1  3 DSorg 5 10 CrDate 18,
                           40 DDName 48 49 Disp 58
       if DDName <> ' '                       /* Save last DD Name    */
          then OldDDName = DDName
       if Search = ''                         /* No search for member */
          then if Parms='' |,                 /* Show line if no parms*/
                  pos(Parms,OldDDName)<> 0 |, /*         or found DD  */
                  pos(Parms,DSName)<>0        /*         or found DSN */
                  then say DDName left(DSName,44) CrDate DSOrg Disp
                  else nop
          else if pos(Parms,OldDDName) <> 0 /* Search existing member */
                  then if Exist(DSName'('Search')')
                          then do
                               Address ISPEXEC ,
                               'BROWSE DATASET('''DSName'('Search')'')'
                               end
    end
    exit
/*- Verify if the member exists in the Dataset -----------------------*/
EXIST:
   Exist = Sysdsn("'"arg(1)"'")
   If Exist = 'OK'
      then Return 1
      else  Return 0


Patrick Leroy
Systems Programmer
Uni‹o de Bancos Portugueses (Portugal)                      c Xephon 1995

