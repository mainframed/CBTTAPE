Alias name updates to the master catalog


DESCRIPTION

Adding and deleting alias name pointers in the master catalog
requires complicated control characters. Below is an ISPF
CLIST application that makes this task very easy. With this
application the task of adding, deleting, and displaying alias
names can be done interactively and on-line. This function
could be released to non-DP people such as TSO or security
administrators.

The application is made up of a CLIST, an ISPF entry panel,
an ISPF help panel, and an ISPF message member. It is
customized to a specific master catalog name and a specific
user catalog naming convention. Therefore, you will have to
change all references of SYS1.MASTCAT to the name of your
master catalog. As you can see in the CLIST the only part of
the user catalog name that changes is the middle qualifier.
Therefore, the only part of the user catalog name the user needs
to enter is the middle qualifier. You can change the &CAT
variable in the CLIST to conform to your own catalog naming
convention. Also change the user catalog name models in the
help panel and the message member.


TSO CLIST

PROC 0
/*===================================================================*/
/*   NAME   : DEFA                                                   */
/*   PURPOSE: TO ADD OR DELETE AN ALIAS POINTER TO THE MASTER CATALOG*/
/*===================================================================*/
   CONTROL NOFLUSH NOMSG
   SET &T =
   SET &NAME =
   SET &CAT =
   SET &EXITRC =
   DO WHILE &EXITRC ^= 8
     ISPEXEC DISPLAY PANEL(ZPMVSCAT)
     IF LASTCC = 8 THEN SET EXITRC = 8
     ELSE DO
      IF &T = A THEN DO
          DEFINE ALIAS(NAME('&NAME') RELATE('SYS1.&CAT..UCAT')) -
          CATALOG('SYS1.MASTCAT'/&CATPASS)
          IF &LASTCC GT 0 THEN DO
            WRITE ERROR ADDING ALIAS &NAME TO SYS1.&CAT..UCAT IN
            WRITE MASTER CATALOG SYS1.MASTCAT
            WRITE COMMON ERRORS ARE ADDING AN ALIAS THAT ALREADY EXISTS
            WRITE AND TRYING TO ADD AN ALIAS TO A CATALOG ENTRY THAT
            WRITE DOES NOT EXIST IN THE MASTER CATALOG.
            END
          ELSE DO
            WRITE ALIAS &NAME ADDED TO SYS1.MASTCAT
            END
          END
      IF &T = D THEN DO
          DELETE '&NAME' ALIAS CATALOG('SYS1.MASTCAT'/&CATPASS)
          IF &LASTCC GT 0 THEN DO
            WRITE ALIAS &NAME DOES NOT EXIST IN SYS1.MASTCAT
            END
          ELSE DO
            WRITE ALIAS &NAME DELETED FROM SYS1.MASTCAT
            END
      END
      IF &T = L THEN DO
        IF &NAME =   THEN DO
         LISTC ENT('SYS1.&CAT..UCAT') ALL CATALOG('SYS1.MASTCAT')
         END
        END
          LISTC ENT('&NAME') ALL CATALOG('SYS1.MASTCAT')
          END
        END
       END
     END
   END


ISPF ENTRY PANEL (ZPMVSCAT)

)ATTR DEFAULT(%+_)
  ^  TYPE(INPUT) INTENS(NON)
)BODY
+ZPMVSCAT
+                 ADD AND DELETE ALIAS POINTERS IN THE MASTER CATALOG
%
%
%
%
%
% TYPE OF FUNCTION :_T+                (%A+FOR ADD;%D+FOR DELETE;%L+FOR LIST)
% ALIAS            :_NAME    +         ( REQUIRED FOR ADD AND DELETE FUNCTIONS)
% CATALOG          :_CAT   +           ( REQUIRED FOR ADD FUNCTION )
% CATALOG PASSWORD :^CATPASS +         ( PASSWORD FOR THE MASTER CATALOG)
%                                     +( REQUIRED FOR ADD AND DELETE FUNCTIONS)
%
%
%                           PF13 - HELP
%                           PF15 - RETURN
)INIT
   .HELP = ZPMVSCAH
)PROC
   IF (&T = 'D')
     IF (&NAME = ' ')
       .MSG = ZCAT001A
       VER (&NAME,NONBLANK)
     IF (&CATPASS = ' ')
       .MSG = ZCAT003A
       VER (&CATPASS,NONBLANK)
   IF (&T ='A')
     IF (&NAME = ' ')
       .MSG = ZCAT001A
       VER (&NAME,NONBLANK)
     IF (&CAT = ' ')
       .MSG = ZCAT002A
       VER (&CAT,NONBLANK)
     IF (&CATPASS = ' ')
       .MSG = ZCAT003A
       VER (&CATPASS,NONBLANK)
   IF (&T = 'L')
     IF (&NAME = ' ')
       IF (&CAT = ' ')
         .MSG = ZCAT004A
         VER (&NAME,NONBLANK)
 VER (&T,NONBLANK)
)END
)BODY


ISPF HELP PANEL (ZPMVSCAH)

+ZPMVSCAH
%                ADD AND DELETE ALIAS POINTERS IN THE MASTER CATALOG
%
%
%
% TYPE OF FUNCTION :          +Type%A+to add and%D+to delete an alias
+                              from the master catalog.  Type%L+to list
+                              the master catalog alias entry or all
+                              alias entries for an ICF catalog.
% ALIAS            :          +Here you specify the alias pointer (a
+                              first level dataset qualifier) to be added
+                              deleted or displayed.
% CATALOG          :          +This value is the%middle+qualifier in the
+                              catalog name.  Where the catalog name is in the
+                              format SYS1.xxxx.UCAT.  This value is ignored
+                              for the delete function.  To list all the aliases
+                              pointing to the ICF catalog SYS1.xxxx.UCAT the
+                              value of the %alias+pointer above must be blank.
% CATALOG PASSWORD :          +Master password of the master catalog of
+                              the system you are logged onto.  The
+                              password must be specified for the%add+and
+                             %delete+functions.
)END


ISPF MESSAGE MEMBER (ZCAT00)

ZCAT001A
'ALIAS NAME IS REQUIRED'
ZCAT002A
'MIDDLE QUALIFIER OF CATALOG DSNAME REQUIRED - SYS1.????????.UCAT'
ZCAT003A
'MASTER CATALOG PASSWORD REQUIRED'
ZCAT004A
'ALIAS NAME OR VOLUME IS REQUIRED'


Gary Smith
Senior Systems Programmer (Canada)                       c Xephon 1992

