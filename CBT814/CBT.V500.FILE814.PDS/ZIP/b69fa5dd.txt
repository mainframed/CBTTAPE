Assembler edit macro

When developing an Assembler program, it is usual to edit a
source deck and then submit a batch job to assemble and link the
program.  If the job  fails with an error, it is  usual to edit the
source code and to try again.  In order to reduce the amount of
effort required to develop and test Assembler code, I developed an
edit macro called ASSEMBLE. This can be called either as a
macro within edit or as a CLIST.  This enables me to stay within
the source member and thus shorten the time required to produce a
working program.  Additionally, I can make sure that the program
will  be generated with the right options and have the correct
destination.  I no longer need worry about invalid options or JCL
errors.

The macro consists of a CLIST, two PANELs, and  a  skeleton
file.  The panel provides validated entry to define the Assembler
and link options required together with the source and destination
file names.  All the options are saved in the user profile pool.  The
CLIST fills in the source details (if called as an edit macro),
validates the Assembly/link options, and submits the JCL as
requested.  The source member is automatically saved before the
JCL is submitted.  For simple Assembler programs, all the default
options can be used.  All the options you ever need for Assembler
programs are included on the two panels.

THE ASSEMBLE CLIST

ISREDIT MACRO (PARM) NOPROCESS
CONTROL MSG
ISREDIT (DSN)=DATASET
ISREDIT (NAME)=MEMBER
SET &OBJCODE=&NAME
ISPEXEC VGET (ALIGN AMODE BATCH RMODE DECK APF ESD FLAG LET
LIST MAP  +
              NCALL OBJECT OVER RENT OL RELOC RENT2 TEST APF +
              XREF REFR REUS LOADLIB ICLASS OCLASS LKCOND INCLUDE +
              INCARD1 INCARD2 INCARD3 INCARD4 INCARD5 INCARD6 +
              INCDD1 INCD2 INCDD3 INCDD4 INCDD5  INCDD6 +
              INCDSN1 INCDSN2 INCDSN3 INCDSN4 INCDSN5 INCDSN6)
PROFILE
IF &NAME^= THEN SET &FILENAME=&DSN.(&NAME)
ELSE SET &FILENAME=&DSN
IF &SYSCAPS(&PARM) = NOPANEL THEN +
   DO
    SET &JCLS=Y
    GOTO GENERATE
   END
ISPEXEC DISPLAY PANEL(ASMP01)
GENERATE:+
ISPEXEC VPUT (ALIGN AMODE BATCH RMODE DECK APF ESD FLAG LET
LIST MAP  +
              NCALL OBJECT OVER RENT OL RELOC RENT2 TEST APF XREF +
              REFR REUS LOADLIB ICLASS OCLASS LKCOND INCLUDE)
PROFILE
IF &ICLASS= THEN SET &ICLASS=X
IF &OCLASS= THEN SET &OCLASS=X
SET &ASMOPT=&STR()
IF &ALIGN=N  THEN SET &ASMOPT=&STR(&ASMOPT.NOALIGN,)
IF &BATCH=Y  THEN SET &ASMOPT=&STR(&ASMOPT.BATCH,)
   ELSE SET &ASMOPT=&STR(&ASMOPT.NOBATCH,)
IF &DECK=Y   THEN SET &ASMOPT=&STR(&ASMOPT.DECK,)
   ELSE SET &ASMOPT=&STR(&ASMOPT.NODECK,)
IF &ESD=N    THEN SET &ASMOPT=&STR(&ASMOPT.NOESD,)
   ELSE SET &ASMOPT=&STR(&ASMOPT.ESD,)
IF &FLAG>0   THEN SET &ASMOPT=&STR(&ASMOPT.FLAG(&FLAG),)
IF &OBJECT=N THEN SET &ASMOPT=&STR(&ASMOPT.NOOBJECT,)
   ELSE SET &ASMOPT=&STR(&ASMOPT.OBJECT,)
IF &RENT=Y   THEN SET &ASMOPT=&STR(&ASMOPT.RENT,)
IF &RELOC=N  THEN SET &ASMOPT=&STR(&ASMOPT.NORLD,)
IF &TEST=Y   THEN SET &ASMOPT=&STR(&ASMOPT.TEST,)
IF &XREF=FULL OR &XREF=SHORT THEN DO
   SET &XREF=&STR((&XREF.))
   SET &ASMOPT=&STR(&ASMOPT.XREF&XREF. )
   END
   ELSE SET &ASMOPT=(&ASMOPT.NOXREF)
SET &LINKOPT=&STR(LIST,)
IF &LKCOND=  THEN SET &LKCOND=&STR(5,LT)
IF &AMODE = THEN SET &AMODE = 24
IF &RMODE = THEN SET &RMODE = 24
SET &MODE=&STR( MODE AMODE(&AMODE),RMODE(&RMODE))
IF &LET=Y   THEN SET &LINKOPT=&STR(&LINKOPT.LET,)
IF &MAP=Y   THEN SET &LINKOPT=&STR(&LINKOPT.MAP,)
IF &NCALL=Y THEN SET &LINKOPT=&STR(&LINKOPT.CALL,)
   ELSE SET &LINKOPT=&STR(&LINKOPT.NCAL,)
IF &OL=Y    THEN SET &LINKOPT=&STR(&LINKOPT.OL,)
IF &OVER=Y  THEN SET &LINKOPT=&STR(&LINKOPT.OVLY,)
IF &REFR=Y  THEN SET &LINKOPT=&STR(&LINKOPT.REFR,)
IF &REUS=Y  THEN SET &LINKOPT=&STR(&LINKOPT.REUS,)
IF &RENT2=Y THEN SET &LINKOPT=&STR(&LINKOPT.RENT,)
IF &TEST=Y  THEN SET &LINKOPT=&STR(&LINKOPT.TEST,)
IF &APF=Y   THEN SET &LINKOPT=&STR(&LINKOPT.AC=1)
ELSE SET &LINKOPT=&STR(&LINKOPT.AC=0)
IF &INCLUDE=Y AND &JCLS=Y THEN DO
   SET &JCLS=
   ISPEXEC DISPLAY PANEL (ASMP02)
   ISPEXEC VPUT (INCARD1 INCARD2 INCARD3 INCARD4 INCARD5
INCARD6 +
              INCDD1 INCDD2 INCDD3 INCDD4 INCDD5  INCDD6 +
              INCDSN1 INCDSN2 INCDSN3 INCDSN4 INCDSN5 INCDSN6 )
PROFILE
END
IF &JCLS=Y THEN DO
   ISREDIT SAVE
   SET &MAX = &LENGTH(&FILENAME)
   SET &C = 1
   DO WHILE &C < &MAX AND &SUBSTR(&C:&C,&FILENAME)^=&STR((
      SET &C = &C + 1
   END
   SET &A = &C - 1
   IF &A>1 AND &A<&MAX THEN DO
      SET &DSN=&STR(//        DD
DSN=&SUBSTR(1:&A,&FILENAME),DISP=SHR)
END
      ELSE SET &DSN =&STR(//*)
   SET &NULL=&STR()
   ISPEXEC FTOPEN TEMP
   ISPEXEC FTINCL ASMS01
   ISPEXEC FTCLOSE
   ISPEXEC VGET (ZTEMPF)
   SUBMIT '&ZTEMPF'
   SET ZEDSMSG = JCL IN CLASS &ICLASS
   SET ZEDLMSG = COMPILE && LINK JOB FOR &OBJCODE WAS SUB'D
   ISPEXEC SETMSG MSG(ISRZ000)
END
ELSE DO
  SET ZEDSMSG = NO JCL SUBMITTED
  SET ZEDLMSG = OPTIONS WERE CHANGED, BUT NO JCL WAS CREATED
  ISPEXEC SETMSG MSG(ISRZ000)
END

THE ASMP01 PANEL

)BODY
%----------------------  Assembler and Linkage Options  -----------------------
+
%       Continue (Y/N) %=>_Z+    Input Class%=>_Z+  Output Class%=>_Z+
+
+   Source Dataset Name%=>%_FILENAME
+   Destination Dataset%=>%_LOADLIB
+
+                       Module Name%=>_OBJCODE +
+
%       Assembler Options                         Linkage Options
+       Align        (Y)%=>_Z+                    Amode       (24)%=>_Z +
+       Batch        (N)%=>_Z+                    Rmode       (24)%=>_Z  +
+       Deck         (N)%=>_Z+                    Authorised   (N)%=>_Z+
+       ESD          (Y)%=>_Z+                    Step condition  %=>_Z
+
+       Flag         (0)%=>_Z+                    Let errors   (N)%=>_Z+
+       List         (Y)%=>_Z+                    Map          (N)%=>_Z+
+       Object code  (Y)%=>_Z+                    Overlaid     (N)%=>_Z+
+       Re-enterable (N)%=>_Z+                    Only Load    (N)%=>_Z+
+       Reloca'n Dir (Y)%=>_Z+                    Re-enterable (N)%=>_Z+
+       Test         (N)%=>_Z+                    Refreshable  (N)%=>_Z+
+       Xref        (NO)%=>_Z    +                Reusable     (N)%=>_Z+
+           (FULL,SHORT)                          Call search  (N)%=>_Z+
+                                                 INCLUDE card (N)%=>_Z+
)INIT
  .ZVARS = '(JCLS,ICLASS,OCLASS,ALIGN,AMODE,BATCH,RMODE,DECK,APF,ESD, +
  LKCOND,FLAG,LET,LIST,MAP,OBJECT,OVER,RENT,OL,RELOC,RENT2,TEST,REFR, +
                                  XREF,REUS,NCALL,INCLUDE )'
  .CURSOR = JCLS
)PROC
  VER (&FILENAME,NB)
  VER (&LOADLIB,NB)
  VER (&OBJCODE,NB)
  VER (&ALIGN,LIST,Y,N)
  VER (&BATCH,LIST,Y,N)
  VER (&DECK,LIST,Y,N)
  VER (&ESD,LIST,Y,N)
  VER (&FLAG,NUM)
  VER (&LIST,LIST,Y,N)
  VER (&OBJECT,LIST,Y,N)
  VER (&RENT,LIST,Y,N)
  VER (&RELOC,LIST,Y,N)
  VER (&TEST,LIST,Y,N)
  VER (&XREF,LIST,FULL,SHORT,NO)
  VER (&AMODE,LIST,24,31)
  VER (&RMODE,LIST,24,ANY)
  VER (&APF,LIST,Y,N)
  VER (&INCLUDE,LIST,Y,N)
  VER (&LET,LIST,Y,N)
  VER (&MAP,LIST,Y,N)
  VER (&OVER,LIST,Y,N)
  VER (&OL,LIST,Y,N)
  VER (&NCALL,LIST,Y,N)
  VER (&RENT2,LIST,Y,N)
  VER (&REFR,LIST,Y,N)
  VER (&REUS,LIST,Y,N)
  VER (&JCLS,NB,LIST,Y,N)
)END

THE ASMP02 PANEL

)BODY
%-----------------------  Assembler and Linkage Options  ----------------------
+
%      Submit JCL(Y/N) %=>_Z+              Module Name%=> &OBJCODE +
+
+ DD Name       Dataseset Name
+
%=>_INCDD1  +%=>_INCDSN1                                     +
%=>_INCDD2  +%=>_INCDSN2                                     +
%=>_INCDD3  +%=>_INCDSN3                                     +
%=>_INCDD4  +%=>_INCDSN4                                     +
%=>_INCDD5  +%=>_INCDSN5                                     +
%=>_INCDD6  +%=>_INCDSN6                                     +
+
+               Enter Additional Linkage editor cards below.
+
%=>_INCARD1
+
%=>_INCARD2
+
%=>_INCARD3
+
%=>_INCARD4
+
%=>_INCARD5
+
%=>_INCARD6
+
+
)INIT
  .ZVARS = '( JCLS )'
  .CURSOR = JCLS
)PROC
  VER (&JCLS,NB,LIST,Y,N)
)END

THE ASMS01 SKELETON

//&SYSUID.ASM JOB (00000000),'COMPILE &OBJCODE',
//             CLASS=&ICLASS,
//             MSGCLASS=&OCLASS,MSGLEVEL=(2,0),
//             NOTIFY=&SYSUID
//*            ASSEMBLE &FILENAME
//*            LINK TO &LOADLIB AS &OBJCODE
//ASM EXEC PGM=IEV90,
//          PARM='&ASMOPT'
//SYSLIB DD DSN=SYS4.PROD.MACLIB,DISP=SHR,DCB=BLKSIZE=23200
//         DD  DSN=SYS1.MACLIB,DISP=SHR
//         DD  DSN=SYS1.AMODGEN,DISP=SHR
//         DD  DSN=SYS1.HASPSRC,DISP=SHR
//         DD  DSN=OPC.V1R1M0.OPCMACS,DISP=SHR
&DSN
//SYSPRINT DD SYSOUT=&OCLASS
//SYSUT1 DD UNIT=VIO,SPACE=(CYL,5)
//SYSLIN DD DSN=&&LOADSET,UNIT=VIO,DISP=(MOD,PASS),
//             SPACE=(TRK,10),DCB=BLKSIZE=800
//SYSIN DD DSN=&FILENAME,DISP=SHR
//*
//LKED EXEC PGM=IEWL,COND=(&LKCOND),
//           PARM='&LINKOPT'
//SYSLIB DD DSN=SYS1.VSCLLIB,
//             DISP=SHR
//             DD DSN=ISP.V2R3M0.ISPLOAD,DISP=SHR
//             DD DSN=&LOADLIB,
//             DISP=SHR
//SYSLIN DD DSN=&&LOADSET,DISP=(OLD,DELETE)
//             DD DDNAME=SYSIN
//SYSLMOD DD DSN=&LOADLIB.(&OBJCODE),
//             DISP=SHR
//SYSUT1 DD UNIT=VIO,
//             SPACE=(CYL,1)
//SYSPRINT DD SYSOUT=&OCLASS
)SEL &INCDD1 ^= &NULL && &INCDSN1 ^= &NULL && &INCLUDE = Y
//&INCDD1  DD DSN=&INCDSN1,DISP=SHR
)ENDSEL
)SEL &INCDD2 ^= &NULL && &INCDSN2 ^= &NULL && &INCLUDE = Y
//&INCDD2  DD DSN=&INCDSN2,DISP=SHR
)ENDSEL
)SEL &INCDD3 ^= &NULL && &INCDSN3 ^= &NULL && &INCLUDE = Y
//&INCDD3  DD DSN=&INCDSN3,DISP=SHR
)ENDSEL
)SEL &INCDD4 ^= &NULL && &INCDSN4 ^= &NULL && &INCLUDE = Y
//&INCDD4  DD DSN=&INCDSN4,DISP=SHR
)ENDSEL
)SEL &INCDD5 ^= &NULL && &INCDSN5 ^= &NULL && &INCLUDE = Y
//&INCDD5  DD DSN=&INCDSN5,DISP=SHR
)ENDSEL
)SEL &INCDD6 ^= &NULL && &INCDSN6 ^= &NULL && &INCLUDE = Y
//&INCDD6  DD DSN=&INCDSN6,DISP=SHR
)ENDSEL
//SYSIN DD *
 &MODE
)SEL &INCARD1 ^= &NULL && &INCLUDE = Y
 &INCARD1
)ENDSEL
)SEL &INCARD2 ^= &NULL && &INCLUDE = Y
 &INCARD2
)ENDSEL
)SEL &INCARD3 ^= &NULL && &INCLUDE = Y
 &INCARD3
)ENDSEL
)SEL &INCARD4 ^= &NULL && &INCLUDE = Y
 &INCARD4
)ENDSEL
)SEL &INCARD5 ^= &NULL && &INCLUDE = Y
 &INCARD5
)ENDSEL
)SEL &INCARD6 ^= &NULL && &INCLUDE = Y
 &INCARD6
)ENDSEL
 NAME         &OBJCODE(R))
/*


M Hinsley
Junior Systems Programmer
Burton Group Financial Services (UK) © M Hinsley 1989

