Allocating PDS members

PROBLEM ADDRESSED

The standard IBM TSO allocation routines enable a member of a 
partitioned dataset to be allocated and subsequently processed as a 
normal sequential dataset.  However, when this member is used 
for output (ie the dataset is written to), the index entry is written 
with only the basic information pertaining to the member (ie the 
address of the data part in the dataset for the member).  This may 
be either merely unfortunate (eg in the case of ISPF statistical 
information), or erroneous (eg in the case of load modules, where 
the complete information necessary to load and execute the 
module is lost).  The POALLOC program solves this problem by 
updating the directory with the original information from the 
index.

SOLUTION

The program, POALLOC, is called to perform the allocation of 
the required member (the partitioned dataset containing the 
member having been previously allocated).  POALLOC saves the 
current directory information pertaining to this member in a 
CLIST variable ($MEMBER).  The member is then processed as 
usual.  At the end of the processing, POALLOC is called once 
again to update the directory entry with the information saved 
from the initial invocation.  In the case of a load module, this 
means that the load and execution information is once again 
present.  Naturally, the information represents the original status 
and not the status after being processed, but, provided no critical 
changes have been made, having this information is usually better 
than having no information at all.

SOFTWARE ENVIRONMENT

The POALLOC program may only be used in the TSO/ISPF 
environment.

CALLING SEQUENCE
	CALL 'libraryname(POALLOC)' [filename[membername]]
where
libraryname: is the name of the dataset containing the program 
POALLOC.

filename: is the name of the file (ie DD statement) which defines 
the partitioned dataset containing the required member.  The 
default is SYSUT1.  This entry, if specified, must be 8 characters 
(left-justified, padded with blanks).  This file, defining the 
partitioned dataset, must be allocated when POALLOC is called.

membername is the name of the member to be allocated (first 
call), this parameter must not be specified in the second call.

RETURN CODES

0	OK
12	$MEMBER CLIST variable not present (2nd call)
16	BLDL error, eg member not in partitioned dataset
20	STOW error, directory update could not be performed.

SAMPLE CLIST

ALLOC F(SYSUT2) DA(SPF.CLIST) SHR
CALL 'TEST.LOAD(POALLOC)' 'SYSUT2 ALPHA'
...
CALL 'TEST.LOAD(POALLOC)' 'SYSUT2'

The first POALLOC call allocates the member ALPHA (in file 
SYSUT2).

The second POALLOC call updates the directory entry in the 
partitioned dataset SPF.CLIST for the member ALPHA with the 
information (except the address of the member data, which could 
have changed) from the data saved in the original call.  The 
required processing of the dataset (member) is performed between 
these two calls.

SUBPROGRAMS USED

The POALLOC subprogram makes use of two general subroutines 
DYNALLOC and CLISTVAR.

DYNALLOC

A subroutine which performs the Dynamic Allocation service 
(SVC 99).  The subroutine has the following call sequence:

	CALL DYNALLOC,(functioncode,parameter[,normparm]),VL
where
functioncode is a direct value corresponding to the SVC 99 verb 
code, ie 01 for ALLOC, 02 for DEALLOC, etc.  Each call builds 
up the parameter list appropriate for that call, the actual SVC 99 is 
invoked with function code 00.

parameter is a sub-parameter in the form demanded by SVC 99.

normparm can be coded as 'N' to specify that the data supplied in 
the subparameter is normalised, ie trailing blanks have been 
removed.  This parameter is optional.  If the normalised parameter 
is not specified then normalisation is automatically performed.  
The normalisation involves removing all non-significant (trailing) 
blanks (X'40'); the minimum length of a normalised data field is 
one byte.  Binary data containing, or which may contain, X'40' 
must not be normalised.

Example
         CALL DYNALLOC,(02,P1),VL
         CALL DYNALLOC,(02,P2),VL
         CALL DYNALLOC,(00),VL
P1       DC   XL2'0002',HL2'1,8'
         DC   CL8'SYSUT1'
P2       DC   XL2'0007',HL2'0'

CLISTVAR

CLISTVAR is a subroutine that simplifies the access to the CLIST 
variable pool.  Two functions are available:

1	Store a variable in the pool

2	Fetch a variable from the pool.

The subroutine has the following call sequence:

	CALL CLISTVAR,(function,varname,varlen,vardata),VL
where
function is the function to be performed (ie C'1' - fetch, C'2' - 
store).
varname is the name of the variable, delimited by a blank, eg DC 
C'ALPHA '.
varlen is the length of the variable data (maximum length for 
fetch), format F.
vardata is the data corresponding to the specified variable name.  
For the store operation, the contents of this field are stored in the 
specified CLIST variable.  For the fetch operation, this field 
contains the current contents of the specified CLIST variable.

Example

         CALL  CLISTVAR,(FC,VN,VL,VD),VL
FC       DC    C'2'
VN       DC    C'BETA '
VL       DC    F'3'
VD       DC    C'123'

In the above example, '123' would be stored in the CLIST variable 
BETA.

The CLISTVAR subroutine was described in MVS Update, issue 
19.  The following modification must be made to the program in 
order to return the actual data, not a pointer to the data.

Original code (lines 100 - 114)

         LTR   R15,R15
         BNZ   CLIST_VAR_ERR
* test variable length
         L     R1,VAL                L(data)
         LTR   R1,R1                 ? Null-data
         BNZ   CLIST_VAR_EXIT        : no, ok
         MVC   RC,=H'8'              RETURN-CODE: no data retrieved
* test function
         CLI   0(R2),C'1'            ? fetch
         BNE   CLIST_VER_EXIT        :no, exit
         ST    R1,0(R4)              L(data)
         LA    R1,VA                 A(data)
         ST    R1,0(R5)
         B     CLIST_VAR_EXIT        : exit
CLIST_VAR_ERR ...

New code

         LTR   R1,R1                 ? Null-data
         BZ    CLIST_VAR_EXIT        : yes
         CLI   0(R2),C'1'            ? fetch
         BNE   CLIST_VAR_END         : no, exit
* test variable length
         MVC   RC,=H'8'              RETURN-CODE: no data retrieved
         L     R1,VAL                L(data)
         LTR   R1,R1                 ? Null-data
         BZ    CLIST_VAR_EXIT        : yes
* test function
         ST    R1,0(R4)              L(variable-data)
         BCTR  R1,0                  LC(variable-data)
         L     R15,VAPTR             A(data)
         MVC   0(0,R5),0(R15)        variable-data
         EX    R1,*-6                return data
CLIST_VAR_END  DS 0H
         MVC   RC,=H'0'              RETURN-CODE: ok
         B     CLIST_VAR_EXIT
CLIST_VAR_ERR ...

SOURCE CODE FOR PALLOC

         TITLE 'ALLOCATE Member of PO-Dataset'
* 1st Call-
* Input:
*  EXEC-Parm:   DDname (CL8), default SYSUT1, containing the member
*               to be allocated. The dataset must be preallocated.
*               Member (CL8), name of member to be allocated.
*  Output:
*   CLIST-Parm <$MEMBER>: Directory information pertaining to member
**
*  2nd Call-
*  Input:
*   EXEC-Parm:  DDname (CL8), default SYSUT1, containing the member.
*   CLIST-Parm <$MEMBER>: Directory information pertaining to member
* Output:
*               The directory entry is updated with the directory
*               information (except new entry address) from $MEMBER.
* Returncodes:
* 0  = ok
* 8  = nok, DYNALLOC error
* 12 = nok, $MEMBER variable not found
* 16 = nok, BLDL error
* 20 = nok, STOW (directory update) failed
POALLOC  CSECT
* initialise addressing
         STM   R14,R12,12(R13)         save registers
         BALR  R12,0                   load base register R12
         USING *,R12
         LA    R15,SA                  A(save-area)
         ST    R13,4(R15)              backward ptr
         ST    R15,8(R13)              forward ptr
         LR    R13,R15                 A(new save-area)
         B     SA_END                  jump over save-area
SA       DS    18A                     save-area
SA_END   DS    0H
         L     R2,0(R1)           A(EXEC-PARAMETER)
         LA    R4,SYSUT1          A(DCB)
         USING IHADCB,R4
         LH    R1,0(R2)           L(EXEC-PARAMETER)
         LTR   R1,R1
         BZ    A100               no EXEC-PARAMETER
         LA    R3,8               standard length
         CR    R1,R3
         BH    *+6                L(EXEC-Parm) > 8
         LR    R3,R1
* R3: L(DDName)
         BCTR  R3,0               LC(DDName)
         MVC   DDNAME,DDNAME-1    clear DDName
         MVC   DDNAME(0),2(R2)
         EX    R3,*-6
         MVC   DCBDDNAM,DDNAME    set DDName
         MVC   PM01DDN,DDNAME
         LA    R3,1(R3)           reset length
         SR    R1,R3              remaining PARM length
         BZ    A100               no field name
         BCTR  R1,0               LC(EXEC-PARAMETER)
         MVC   MEMBER(0),10(R2)
         EX    R1,*-6
         RDJFCB (SYSUT1,(INPUT))
         LA    R10,JFCB
         USING JFCBDSEC,R10
         MVC   PM21DSN,JFCBDSNM      DSName
         OPEN  (SYSUT1,(INPUT))
         BLDL  SYSUT1,BLDL
         MVC   RC,=H'16'          BLDL-Error
         LTR   R15,R15
         BNZ   EOJ                :error
* else BLDL ok
         STH   R15,RC             reset
* store member data as CLIST variable <$MEMBER>
         LH    R0,BLDLLEN
         ST    R0,VL
         CALL  CLISTVAR,(F2,VN,VL,MEMDATA),VL
         CLOSE SYSUT1
* free DDN
         CALL  DYNALLOC,(02,PM01),VL        DDNAME
         CALL  DYNALLOC,(02,PM11),VL        UNALLOC
         CALL  DYNALLOC,(00),VL
* allocate member
         CALL  DYNALLOC,(01,PM01),VL        DDNAME
         CALL  DYNALLOC,(01,PM21),VL        DSN
         MVC   PM22MEM,MEMBER
         CALL  DYNALLOC,(01,PM22),VL        MEMBER
         CALL  DYNALLOC,(01,PM23),VL        SHR
         CALL  DYNALLOC,(00),VL
         MVC   RC,=H'8'           RC, if error
* R15: returncode
         LTR   R15,R15            error return?
         BNZ   EOJ                :yes
         MVC   RC,=H'0'           reset RC
EOJ      TM    DCBOFLGS,X'10'     OPEN performed?
         BZ    NO_OPEN            :no
         CLOSE SYSUT1
NO_OPEN  LH    R15,RC             RETURN-CODE
         L     R13,4(R13)               restore A(old save-area)
         RETURN (14,12),RC=(15)
A100     DS    0H
* 2nd call, update directory
         OPEN  (SYSUT1,(UPDAT))
         LH    R0,DCBBLKSI
         GETMAIN R,LV=(0)         allocate buffer
         ST    R1,AIOBUF
* retrieve variable data
         MVC   VL,=F'256'         max length
         CALL  CLISTVAR,(F1,VN,VL,MEMDATA),VL
         MVC   RC,=H'12'          RC = 12, retrieval error
         LTR   R15,R15
         BNZ   EOJ
* else ok
         STH   R15,RC             reset
         MVC   OLD_C,C            save data
         MVC   OLD_USER,USER
         MVC   BLDLLEN,=H'256'
         BLDL  SYSUT1,BLDL
         MVC   RC,=H'16'          BLDL-Error
         LTR   R15,R15
         BNZ   EOJ                :error
         FIND  SYSUT1,TTR,C
* position at EOF
A110     L     R2,AIOBUF          A(IO buffer)
         READ  DECB,SF,SYSUT1,(2),'S'
         CHECK DECB
         B     A110               loop until EOF
A120     DS    0H                 positioned at EOF
* restore previously saved data
         MVC   NEW_MEM,MEMBER     membername
         MVC   NEW_C,OLD_C
         MVC   NEW_USER,OLD_USER
         MVC   NEW_TTR,TTR
         STOW  SYSUT1,NEW_MEM,R
         MVC   RC,=H'20'          STOW-Error
         LTR   R15,R15
         BNZ   EOJ                :error
         MVC   RC,=H'0'           ok
         B     EOJ                eoj
RC       DC    H'0'               returncode
         DC    C' '               clear byte
DDNAME   DC    CL8'SYSUT1'        default DDName
* BLDL entry
BLDL     DC    H'1'               1 entry in list
MEMDATA  DS    0XL256
BLDLLEN  DC    H'256'             max. length
MEMBER   DC    CL8' '
TTR      DC    XL3'0'
KZC      DC    XL3'0'
C        EQU   *-1
USER     DS    CL256' '
NEW_MEM  DC    CL8' '
NEW_TTR  DC    XL3'0'
NEW_C    DC    XL1'0'
NEW_USER DS    CL256' '
OLD_C    DC    XL1'0'
OLD_USER DS    CL256' '
VN       DC    CL8'$MEMBER'
VL       DS    A
* Function codes
F1       DC    C'1'                    retrieve variable
F2       DC    C'2'                    store variable
* DYNALLOC parameters
PM01     DC    XL2'0001',HL2'1,8'           DDNAME
PM01DDN  DC    CL8'SYSUT1'
PM11     DC    XL2'0007',HL2'0'             UNALLOC
PM21     DC    XL2'0002',HL2'1,44'          DSNAME
PM21DSN  DC    CL44' '
PM22     DC    XL2'0003',HL2'1,8'           DDNAME
PM22MEM  DC    CL8' '
PM23     DC    XL2'0004',HL2'1,1',X'08'     SHR
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=(R,W),DSORG=PO,DEVD=DA,EXLST=RDJFCB,X
               EODAD=A120
RDJFCB   DS    0A
         DC    X'87',AL3(JFCB)
JFCB     DS    CL176
AIOBUF   DC    A(0)               A(IO buffer)
         LTORG
         DCBD  DSORG=PO,DEVD=DA
JFCBDSEC DSECT
         IEFJFCBN
         END


A S Rudd (West Germany)	© A S Rudd 1989

