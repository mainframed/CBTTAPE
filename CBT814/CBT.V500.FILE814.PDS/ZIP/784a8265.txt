A dataset copying utility

COPYF REXX EXEC

/* REXX */
/*      TRACE R   */
ARG COPYARG TOARG .
/*-------------------------------------------------------------------*/
/*-----   INPUT DATASET NAME IS REQUIRED PARAMETER.             -----*/
/*-----   IF MISSING, TERMINATE.                                -----*/
/*-------------------------------------------------------------------*/
IF COPYARG = "" THEN
  DO
  ZEDSMSG = "DATA SET NAME MISSING"
  ZEDLMSG = "INPUT DATA SET NAME WAS NOT DEFINED, COPYF TERMINATED"
  ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
  EXIT (8)
  END
/*-------------------------------------------------------------------*/
/*-----   CHECK, THAT DATASET NAME IS FULLY-QUALIFIED.          -----*/
/*-----   IF NOT, ADD QUOTATION MARKS.                          -----*/
/*-------------------------------------------------------------------*/
IF SUBSTR(COPYARG,1,1) ^= "'" THEN
  DO
  ABC = "'"COPYARG"'"
  COPYARG = ABC
  END
/*-------------------------------------------------------------------*/
/*-----   AVAILABILITY OF INPUT DATASET IS VERY ESSENTIAL.      -----*/
/*-----   CHECK IT AND TERMINATE IF DATASET DOES NOT EXIST.     -----*/
/*-----   "NORECALL" PREVENTS HSM MIGRATED DATASETS NOT TO      -----*/
/*-----   BE RECALLED YET.                                      -----*/
/*-------------------------------------------------------------------*/
INFO = LISTDSI(COPYARG NORECALL)
IF INFO ^= 0 THEN
  DO
  IF SYSREASON ^= 9 THEN
    DO
    ZEDSMSG = "LISTDSI FAILED, REA="SYSREASON
    ZEDLMSG = SYSMSGLVL2
    ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
    EXIT (8)
    END
  END
COPYFIN = COPYARG
/*-------------------------------------------------------------------*/
/*-----   CHECK IF INPUT DATASET NAME ALREADY EXISTS AS A       -----*/
/*-----   VARIABLE IN THE PROFILE POOL.                         -----*/
/*-------------------------------------------------------------------*/
ADDRESS ISPEXEC "VGET (COPYFIN) PROFILE"
IF RC = 0 THEN
  DO
  /* DATASET NAME "COPYFIN" FOUND */
  CALL VPUT
  IF PUTRC = 0 THEN
    DO
    /* DATASET NAME "COPYFIN" REPLACED */
    END
  END
ELSE
  DO
  CALL VPUT
  /* DATASET NAME "COPYFIN" ADDED */
  END
IF TOARG ^= "" THEN
  DO
/*-------------------------------------------------------------------*/
/*-----   CALL COPYTO FUNCTION IF THE CALL OF COPYF INCLUDED    -----*/
/*-----   DATASET NAMES FOR INPUT AND OUTPUT.                   -----*/
/*-------------------------------------------------------------------*/
  ADDRESS ISPEXEC "SELECT CMD(COPYTO "TOARG")"
  END
EXIT
/* VPUT, INTERNAL SUBROUTINE                                         */
VPUT:
COPYFIN = COPYARG
/*-------------------------------------------------------------------*/
/*-----   VPUT THE INPUT DATASET NAME INTO PROFILE POOL FOR     -----*/
/*-----   COPYTO FUNCTION.                                      -----*/
/*-------------------------------------------------------------------*/
ADDRESS ISPEXEC "VPUT (COPYFIN) PROFILE"
IF RC = 0 THEN
  DO
  PUTRC = RC
  RETURN
  END
ELSE
  DO
  ZEDSMSG = "VPUT FAILED, RC="RC
  ZEDLMSG = "INPUT DATA SET NAME WAS NOT STORED, COPYF TERMINATED"
  ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
  EXIT (8)
  END


COPYTO REXX EXEC

/* REXX */
/*      TRACE R   */
ARG TOCOARG .
/*-------------------------------------------------------------------*/
/*-----   IF YOUR DFP VERSION IS 3.2 OR HIGHER THEN SET         -----*/
/*-----   THE VALUE OF VARIABLE DFP32 TO "YES"                  -----*/
/*-------------------------------------------------------------------*/
DFP32 = "NO"
/*-------------------------------------------------------------------*/
/*-----   OUTPUT DATASET NAME IS REQUIRED PARAMETER.            -----*/
/*-----   IF MISSING, TERMINATE.                                -----*/
/*-------------------------------------------------------------------*/
IF TOCOARG = "" THEN
  DO
  ZEDSMSG = "DATA SET NAME MISSING"
  ZEDLMSG = "OUTPUT DATA SET NAME WAS NOT DEFINED, COPYTO TERMINATED"
  ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
  EXIT (8)
  END
/*-------------------------------------------------------------------*/
/*-----   CHECK, THAT DATASET NAME IS FULLY-QUALIFIED.          -----*/
/*-----   IF NOT, ADD QUOTATION MARKS.                          -----*/
/*-------------------------------------------------------------------*/
IF SUBSTR(TOCOARG,1,1) ^= "'" THEN
  DO
  ABC = "'"TOCOARG"'"
  TOCOARG = ABC
  END
COPYFOUT = TOCOARG
/*-------------------------------------------------------------------*/
/*-----   INPUT DATASET NAME IS REQUIRED.                       -----*/
/*-----   TERMINATE IF NOT FOUND IN PROFILE POOL.               -----*/
/*-------------------------------------------------------------------*/
ADDRESS ISPEXEC "VGET (COPYFIN) PROFILE"
IF RC ^= 0 THEN
  DO
  /* DATASET NAME "COPYFIN" NOT FOUND */
  ZEDSMSG = "VGET FAILED, RC="RC
  ZEDLMSG = "INPUT DATA SET NAME WAS NOT DEFINED, COPYTO TERMINATED"
  ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
  EXIT (8)
  END
IF COPYFIN = " " THEN
  DO
  ZEDSMSG = "DATA SET NAME MISSING"
  ZEDLMSG = "INPUT DATA SET NAME WAS NOT DEFINED, COPYTO TERMINATED"
  ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
  EXIT (8)
  END
/*-------------------------------------------------------------------*/
/*-----   CHECK THE AVAILABILITY OF THE OUTPUT DATASET.         -----*/
/*-----   IF RESPONSE IS "DATASET NOT FOUND" ALLOCATE THE       -----*/
/*-----   OUTPUT USING INPUT DATASET AS A MODEL.                -----*/
/*-----   TERMINATE UNDER ANY OTHER CONDITION.                  -----*/
/*-------------------------------------------------------------------*/
INFO = SYSDSN(TOCOARG)
IF INFO ^= "OK" THEN
  DO
  IF INFO = "DATASET NOT FOUND" THEN
    DO
    CALL OUTALLOC
    END
  ELSE
    DO
    ZEDSMSG = "PROCESSING ERROR"
    ZEDLMSG = INFO" - OUTPUT DATA SET IS "TOCOARG
    ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
    EXIT (8)
    END
  END
/*-------------------------------------------------------------------*/
/*-----   CHECK IF OUTPUT DATASET NAME ALREADY EXISTS AS A      -----*/
/*-----   VARIABLE IN THE PROFILE POOL.                         -----*/
/*-------------------------------------------------------------------*/
ADDRESS ISPEXEC "VGET (COPYFOUT) PROFILE"
IF RC = 0 THEN
  DO
  /* DATASET NAME "COPYFOUT" FOUND */
  CALL VPUT
  IF PUTRC = 0 THEN
    DO
    /* DATASET NAME "COPYFOUT" REPLACED */
    END
  END
ELSE
  DO
  CALL VPUT
  /* DATASET NAME "COPYFIN" ADDED */
  END
DEFFI:
/*-------------------------------------------------------------------*/
/*-----   USING LIBDEF FUNCTION, ALLOCATE PANEL LIBRARY         -----*/
/*-----   WHERE THE MODIFIED PANELS ARE.                        -----*/
/*-------------------------------------------------------------------*/
PANELDS = USER.PANEL.LIBRARY    /* USE THE NAME OF YOUR OWN !!!! */
ADDRESS ISPEXEC "LIBDEF ISPPLIB DATASET ID('"PANELDS"')"
IF RC ^= 0 THEN
  DO
  ZEDSMSG = "LIBDEF FAILED, RC="RC
ZEDLMSG = "LIBDEF FUNCTION FOR PANEL LIBRARY FAILED, COPYTO TERMINATED"
  ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
  EXIT (8)
  END
/*-------------------------------------------------------------------*/
/*-----   CALL PDF COPY FUNCTION.                               -----*/
/*-------------------------------------------------------------------*/
ADDRESS ISPEXEC "SELECT PGM(ISRUMC)"
IF RC ^= 0 THEN
  DO
  ZEDSMSG = "PROGRAM CALL FAILED"
ZEDLMSG = "CALLING PDF COPY FINCTION (ISRUMC) FAILED, COPYTO TERMINATED"
  ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
  EXIT (8)
  END
/*        FREE PANEL LIBRARY ALLOCATION                              */
ADDRESS ISPEXEC "LIBDEF ISPPLIB"
CALL CLRPUT
EXIT
/* VPUT, INTERNAL SUBROUTINE                                         */
VPUT:
COPYFOUT = TOCOARG
/*-------------------------------------------------------------------*/
/*-----   VPUT THE OUTPUT DATASET NAME INTO PROFILE POOL        -----*/
/*-------------------------------------------------------------------*/
ADDRESS ISPEXEC "VPUT (COPYFOUT) PROFILE"
IF RC = 0 THEN
  DO
  PUTRC = RC
  RETURN
  END
ELSE
  DO
  ZEDSMSG = "VPUT FAILED, RC="RC
  ZEDLMSG = "OUTPUT DATA SET NAME WAS NOT STORED, COPYTO TERMINATED"
  ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
  EXIT (8)
  END
CLRPUT:
/*-------------------------------------------------------------------*/
/*-----   CLEAR COPYF/COPYTO VARIABLES IN THE PROFILE POOL      -----*/
/*-------------------------------------------------------------------*/
ADDRESS ISPEXEC "VERASE (COPYFIN COPYFOUT) PROFILE"
IF RC ^= 0 THEN
  DO
  ZEDSMSG = "VERASE FAILED, RC="RC
  ZEDLMSG = "DATA SET NAMES WERE NOT CLEARED"
  ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
  END
RETURN
OUTALLOC:
/*-------------------------------------------------------------------*/
/*-----   AUTOMATIC ALLOCATION ROUTINE FOR THE OUTPUT DATASET   -----*/
/*-------------------------------------------------------------------*/
OUTFIL = "CPYFRTOO"
M = MSG("OFF")
"FREE FI("OUTFIL")"
M = MSG("ON")
IF DFP32 = "YES" THEN
  DO
/*-------------------------------------------------------------------*/
/*-----   ALLOCATION USING LIKE PARAMETER, DFP 3.2 OR HIGHER    -----*/
/*-------------------------------------------------------------------*/
  "ALLOC FI("OUTFIL") DS("COPYFOUT") NEW CATALOG LIKE("COPYFIN")"
  IF RC ^= 0 THEN
    DO
    ZEDSMSG = "ALLOC FAILED, RC="RC
    ZEDLMSG = COPYFOUT" - AUTOMATIC OUTPUT DATA SET ALLOCATION FAILED"
    ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
    EXIT
    END
  ELSE
    DO
    ZEDSMSG = "OUTPUT DATA SET ALLOCATED"
    ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
    END
  END
ELSE
  DO
/*-------------------------------------------------------------------*/
/*-----   ALLOCATION WHEN DFP VERSION IS PRE 3.2                -----*/
/*-------------------------------------------------------------------*/
  INFO = LISTDSI(COPYFIN DIRECTORY NORECALL)
  IF INFO ^= 0 THEN
    DO
    IF SYSREASON ^= 9 THEN
      DO
      ZEDSMSG = "LISTDSI FAILED, REA="SYSREASON
      ZEDLMSG = SYSMSGLVL2
      ADDRESS ISPEXEC "SETMSG MSG(ISRZ001)"
      EXIT (8)
      END
    END
  ELSE
    DO
    LOOPCNTR = LENGTH(SYSRECFM)
    RPOS = 0
    RECFM = ""
    DO LOOPCNTR
      RPOS = RPOS + 1
      RECFM = RECFM||SUBSTR(SYSRECFM,RPOS,1)" "
    END
    ALLOPARM = "NEW CATALOG UNIT("SYSUNIT") LRECL("SYSLRECL")"
    ALLOPARM = ALLOPARM" BLKSIZE("SYSBLKSIZE") "SYSUNITS
    ALLOPARM = ALLOPARM" SPACE("SYSPRIMARY" "SYSSECONDS")"
    ALLOPARM = ALLOPARM" RECFM("RECFM")"
    IF SUBSTR(SYSDSORG,1,2) = "PO" THEN
      DO
      ALLOPARM = ALLOPARM" DIR("SYSADIRBLK")"
      END
/*-------------------------------------------------------------------*/
/*-----   VOLUME IS STILL REQUIRED IN SOME INSTALLATIONS        -----*/
/*-----   TURN THE NEXT STATEMENT INTO COMMENT IF NOT NEEDED    -----*/
/*-------------------------------------------------------------------*/
      ALLOPARM = ALLOPARM" VOLUME("SYSVOLUME")"
    "ALLOC FI("OUTFIL") DS("COPYFOUT") "ALLOPARM
    END
  END
"FREE FI("OUTFIL")"
RETURN


Utility Programmer (Finland)                                c Xephon 1995


