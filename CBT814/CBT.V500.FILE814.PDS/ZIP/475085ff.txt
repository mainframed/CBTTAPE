Suppressing SMF records

A frequent problem, which I have encountered when processing
SMF data, is the problem of abends when sorting the SMF data
using DFSORT and/or SYNCSORT. The problem stems from
specifying sort fields which extend beyond the length of the SMF
record. When either product encounters this situation, it abends.
Both sort products have options to ignore or handle this situation,
but they may not be set up this way by default in every
installation.

The most frequent occurrence of this problem is seen when
processing SMF record types 2 and 3 (dump header and trailer,
respectively), since they are both only 18 bytes long. Typically,
when a user wants to sort SMF data, he or she wants to sort on a
job name field for job-related SMF records or some field-stored
variable-sized record extensions for other SMF records.

Even though the SMF dump utility (IFASMFDP) provides for
record selection to the output dataset via control card input,
IFASMFDP uses this selection only against the records read from
the input dataset. IFASMFDP itself writes the SMF type 2 and 3
records, ignoring record suppression. In order to have SMF type 2
and 3 records not appear on the output dataset, an IFASMFDP
user exit must be coded.

There are three IFASMFDP user exits available. The USER1 exit
is used to examine records after they are read from the input
dataset, so it is not suitable for suppressing type 2 and 3 records.
The USER3 exit is given control once for each output dataset
after the dataset is closed, which again offers no help. The USER2
exit, which is given control after a record is selected to be written
to the output dataset, is the only one of these exits which can
suppress records which are generated by the IFASMFDP utility.

If IFASMFDP is requested to invoke the USER2 exit, which I
have named IFASMFU2, IFASMFDP will receive return codes
back from the exit when it is passed type 2 or 3 records to prevent
them from being written to the output dataset. Any other SMF
records can also be excluded by this exit by adding the
appropriate entries to the suppression table within the program.
The more acceptable way of suppressing other records, however,
would be to code them on the IFASMFDP control statement
OUTDD.

I have included a sample job stream to show use of my
IFASMFU2 exit. The first step invokes IFASMFDP with some
standard control cards and the additional USER2(IFASMFU2)
control card. The exit can have any name instead of IFASMFU2.
Since there is no STEPLIB in the JCL, the exit must reside in a
LINKLIST library, although there is no restriction preventing the
exit from residing in a private library. The next job step invokes
the sort utility to sort the output from IFASMFDP on the job
name field from the type 4 and 5 records (which is beyond the
end of the type 2 and 3 records). It would be at this point that the
sort would abend if it encountered any records shorter than any of
the fields specified in the SORT control card. The sorted data is
then passed to a user reporting program for further processing.

//IFASMFDP EXEC PGM=IFASMFDP
//SYSPRINT DD SYSOUT=*
//SYSUDUMP DD SYSOUT=*
//DUMPIN   DD DISP=SHR,DSN=SYS1.MAN1
//DUMPOUT  DD DISP=SHR,DSN=SYS2.SMF.EXTRACT
//SYSIN    DD *,DCB=BLKSIZE=80
 INDD(DUMPIN,OPTIONS(DUMP))
 OUTDD(DUMPOUT,TYPE(04,05))
 DATE(00000,99366)
 START(0000)
 END(2400)
 USER2(IFASMFU2)
/*
//SORT     EXEC PGM=SORT
//SYSOUT   DD SYSOUT=*
//SYSUDUMP DD SYSOUT=*
//SORTIN   DD DISP=SHR,DSN=SYS2.SMF.EXTRACT      UNSORTED SMF DATA
//SORTOUT  DD DISP=SHR,DSN=SYS2.SMF.EXTRACTS     SORTED SMF DATA
//SORTWK01 DD UNIT=SYSDA,SPACE=(CYL,(20,10))
//SORTWK02 DD UNIT=SYSDA,SPACE=(CYL,(20,10))
//SORTWK03 DD UNIT=SYSDA,SPACE=(CYL,(20,10))
//SYSIN    DD *,DCB=BLKSIZE=80
  SORT FIELDS=(19,8,CH,A),FILSZ=E100000
  RECORD TYPE=V,LENGTH=(32756,32756,32756,,1000)
/*
//SMFSUMM  EXEC PGM=SMFSUMM                      SMF SUMMARY PROGRAM
//SYSPRINT DD SYSOUT=*
//SMFDATA  DD DISP=SHR,DSN=SYS2.SMF.EXTRACTS     SORTED SMF DATA
//SYSUDUMP DD SYSOUT=*

SOURCE CODE FOR IFASMFU2

IFASMFU2 CSECT                         ESTABLISH CSECT
IFASMFU2 AMODE 24
IFASMFU2 RMODE 24
         SAVE  (14,12),,IFASMFU2-&SYSDATE
         YREGS                         REGISTER EQUATES
         LR    R12,R15                 LOAD R12 W/EPA ADDRESS
         USING IFASMFU2,R12
         ST    R13,SAVEAREA+4          ST CALLER'S S/A ADDR IN MY S/A
         LA    R15,SAVEAREA            LOAD A(MY SAVEAREA)
         ST    R15,8(,R13)             ST MY S/A ADDR IN CALLER'S S/A
         LR    R13,R15                 LOAD ADDR OF MY S/A IN R13
         SR    R15,R15                 SET DEFAULT RETURN CODE
         LM    R2,R4,0(R1)             LOAD PARMS
         LTR   R4,R4                   IS THERE AN A(SMF RECORD)
         BZ    RETURN                  NO, GO RETURN TO CALLER
         USING SMFREC2,R4
         SR    R2,R2                   CLEAR REGISTER
         IC    R2,SMF2RTY              INSERT RECORD TYPE
         LA    R3,SUPPRESS             LOAD A(SUPPRESSION TABLE)
         AR    R3,R2                   ADD OFFSET TO TABLE ENTRY
         CLI   0(R3),X'FF'             SHOULD IT BE SUPPRESSED
         BE    REJECT                  YES, REJECT IT
RETURN   L     R13,SAVEAREA+4          LOAD A(CALLER'S S/A)
         RETURN (14,12),,RC=(15)       RETURN TO CALLER
REJECT   LA    R15,4                   SET RETURN CODE TO REJECT
         B     RETURN                  GO RETURN TO CALLER
SAVEAREA DC    18F'0'                  SHOULD BE FIRST IN WORKAREA
SUPPRESS DC    256X'00'                SUPPRESSION TABLE ----------|
         ORG   SUPPRESS+2              TYPE 2  (DUMP HEADER)       |
         DC    X'FF'                   SUPPRESS                    |
         ORG   SUPPRESS+3              TYPE 3  (DUMP TRAILER)      |
         DC    X'FF'                   SUPPRESS                    |
         ORG   ,                       ----------------------------|
SMFREC2  DSECT
         IFASMFR 2
         END


                                                                                                         c Xephon 1994

