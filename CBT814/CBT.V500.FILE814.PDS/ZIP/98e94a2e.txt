Removing obsolete entries from RACF


PROGRAM FUNCTION

This program generates a report and a job to enable the
removal of obsolete users and groups from RACF access lists.
In the absence of a standard IBM utility, I decided to write this
program to save time when deleting users and groups from
RACF. Rather than running ICH/IRRUT100 and manually
generating the necessary PERMIT statements, this program can
be run to generate them for you.

The program checks all users/groups on the access lists of the
profiles within the resource classes specified on the SYSIN DD
card. If the user/group does not exist, a record is written to the
REPORT DD and the appropriate PERMIT commands are
generated in the CMDOUT DD.

The CMDOUT DD is used to generate a job in a member or
sequential dataset that contains all the necessary JCL and
PERMIT commands to clean up the RACF access lists. The
REPORT DD is used to generate a report that contains profile
names and access information for each resource class specified
in the SYSIN DD (see Figure 1). The SYSIN DD is used to
specify resource class names to be searched.


NOTES

o      The program must be APF-authorized.

o      It would be a good idea to secure this program within
      RACF as the report gives detailed access information.

o      This program has been successfully run on RACF 1.8.1,
      RACF 1.9 non-restructured, and RACF 1.9 structured
      databases.

o      The job card within CHKACC source should be tailored to
      suit your site's standards.

o      Timings were done on a ten cylinder RACF database 15%
      full. The actual time for each run was around 12 seconds.


CHKACC JCL

//USERID  JOB ,'CHKACC',MSGCLASS=Z,MSGLEVEL=(1,1),
//            CLASS=A,NOTIFY=USERID,REGION=1M
//************************************************
//*     JOB TO REMOVE OBSOLETE USERS/GROUPS      *
//************************************************
//STEP1   EXEC PGM=CHKACC
//STEPLIB  DD  DSN=HLQ.AUTH.LINKLIB,DISP=SHR
//REPORT   DD  SYSOUT=X
//CMDOUT   DD  DSN=HLQ.DATASET.NAME(MEMBER),DISP=SHR
//SYSIN    DD  *
 DATASET
 GROUP
 DASDVOL
 ETC --- ISSUE A 'SETROPTS LIST' TO GET A LIST OF ALL ACTIVE CLASSES
/*


CHKACC SOURCE CODE

INIT     TITLE 'REMOVE OBSOLETE USERS/GROUPS FROM RACF ACCESS LISTS'
CHKACC   CSECT                     STANDARD ENTRY
         SAVE  (14,12),,CNS.WTO..&SYSDATE  SAVE REGISTERS
         BALR  R12,0
         USING *,R12
         ST    R13,SAVEAREA+4
         LR    R2,R13
         LA    R13,SAVEAREA
         ST    R13,8(R2)
OPENDD   DS    0H
         OPEN  (CMDOUT,OUTPUT,REPORT,OUTPUT,SYSIN,INPUT)
         XC    RLPROF,RLPROF       START AT THE TOP OF THE LIST
         LA    R4,RACXTRL2         ASSUME FIRST PROFILE IS NOT GENERIC
         B     JOBCARD             BUILD JOB CARD IN CMDOUT DD
GETIN    DS    0H
         GET   SYSIN               GET RESOURCE CLASS NAME FROM SYSIN
         TRT   0(71,R1),TSTNBLK    FIND FIRST NON-BLANK
         BZ    GETIN               ENTIRE RECORD IS BLANK
         LR    R5,R1               LOAD RESOURCE CLASS NAME
CHKRLP   DS    0H
         RACXTRT TYPE=EXTRACTN,ENTITY=RLPROF,                          +
               CLASS=(5),                                              +
               RELEASE=1.8.1,                                          +
               MF=(E,(4))
         LTR   R15,R15             MORE ENTITIES?
         BNZ   GENLOOP             NO
GOTPROF  DS    0H
         LR    R10,R1              PRESERVE ADDRESS OF RETURNED AREA
         LH    R8,4(,R10)          LOAD OFFSET TO OPTIONAL FIELD
         LA    R8,0(R8,R10)        POINT TO OPTIONAL FIELD
         L     R7,8(,R8)           FIND LENGTH OF ALL USER-IDS
         LA    R7,16(R7,R8)        POINT TO FIRST ACCESS ENTRY
         L     R3,4(,R8)           GET NUMBER OF USERS IN ACCESS LIST
         LA    R8,12(,R8)          POINT TO FIRST OPTION FIELD
RESCHK   DS    0H
         MVC   RCLASS(8),0(R5)     MOVE IN CLASS NAME
         CLC   RCLASS,=CL8'GROUP'  IS RESOURCE CLASS GROUP?
         BE    SKIPCLS             YES
         CLC   RCLASS,=CL8'USER'   IS RESOURCE CLASS USER?
         BNE   GENCHK              NO
SKIPCLS  DS    0H
         AP    GENCTR,GENINC       PASS THROUGH CLASS ONLY ONCE
GENCHK   DS    0H
         LA    R4,RACXTRL2         ASSUME PROFILE IS NON-GENERIC
         MVC   GENRLP,=CL7' '      CLEAR OUT GENERIC OUTPUT RECORD
         MVC   GENREP,=CL4' '      CLEAR OUT GENERIC OUTPUT RECORD
         TM    6(R10),X'80'        IS ENTRY GENERIC?
         BZ    WRTREP1             NO
         MVC   GENRLP,=C'GENERIC'  SET GENERIC VALUE
         MVC   GENREP,=C' (G)'     SET GENERIC VALUE
         LA    R4,RACXTRL1         USE GENERIC RACXTRT LIST
         AP    GENCTR,GENINC       INCREMENT GENERIC COUNTER
WRTREP1  DS    0H
         MVC   OUTREC3,=CL133' '   CLEAR OUTREC3
         MVC   OUTREC3(10),=C'  CLASS - ' MOVE IN TEXT
         MVC   OUTREC3+10(8),0(R5) MOVE IN CLASS
         MVC   OUTREC3+18(12),=C'  PROFILE - ' MOVE IN TEXT
         MVC   OUTREC3+30(64),RLPROF MOVE IN PROFILE
         LA    R1,OUTREC3+84       POINT BEYOND PROFILE
         TRT   OUTREC3+30(64),TSTBLNK FIND FIRST BLANK
         MVC   0(4,R1),GENREP      MOVE IN GENERIC CHAR OR BLANK
         MVC   4(60,R1),=CL60' '   CLEAR OUT REMAINING LINE
         PUT   REPORT,OUTREC3      OUTPUT RECORD
CHKUSER  DS    0H
         LTR   R3,R3               TEST NUMBER OF USERS RETURNED
         BNP   ALLCHKD             NO USERS IN ACCESS LIST
USRLOOP  DS    0H
         LA    R0,8                8 BITS IN THE BYTE
         LA    R1,1                SET LOWERMOST BIT ON
GETACC   DS    0H
         CLM   R1,1,4(R7)          IS ACCESS MASK IN R1 CORRECT ?
         BE    GOTACC              YES
         SLL   R1,1                SET NEXT ACCESS MASK
         BCT   R0,GETACC           DECREMENT R0 AND BRANCH IF NOT ZERO
GOTACC   DS    0H
         LA    R1,8                LOAD MAXIMUM NUMBER OF ITERATIONS
         SR    R1,R0               SUBTRACT NUMBER OF REMAINING ITERNS
         SLL   R1,3                DETERMINE OFFSET INTO TABLE
         LA    R1,ACCTAB(R1)       POINT TO CORRESPONDING TABLE ENTRY
         MVC   ACCREC,0(R1)        MOVE IN CORRESPONDING TABLE ENTRY
USRCHK   DS    0H
         LA    R6,4(R8)            LOAD ADDRESS OF USER-ID
         RACXTRT TYPE=EXTRACT,ENTITY=(6),                              +
               RELEASE=1.8.1,                                          +
               MF=(E,RACXTUSR)
         LTR   R15,R15             USER STILL EXISTS ?
         BNZ   GRPCHK              NO
         L     R0,0(,R1)           LOAD SUBPOOL AND LEN OF GOTTEN AREA
         FREEMAIN R,LV=(0),A=(1)
         B     WRTREP2             WRITE OUT USER/ACCESS RECORD
GRPCHK   DS    0H
         RACXTRT TYPE=EXTRACT,ENTITY=(6),                              +
               RELEASE=1.8.1,                                          +
               MF=(E,RACXTGRP)
         LTR   R15,R15             GROUP STILL EXISTS ?
         BNZ   PUTREC              NO, BUILD PERMIT DELETE COMMANDS
         L     R0,0(,R1)           LOAD SUBPOOL AND LEN OF GOTTEN AREA
         FREEMAIN R,LV=(0),A=(1)
WRTREP2  DS    0H
         MVC   OUTREC4,=CL133' '   CLEAR OUTREC4
         MVC   OUTREC4+23(7),=C'USER - ' MOVE IN TEXT
         MVC   OUTREC4+30(8),0(R6) MOVE IN USERID
         MVC   OUTREC4+38(10),=C' ACCESS - ' MOVE IN TEXT
         MVC   OUTREC4+48(8),ACCREC MOVE IN ACCESS AUTHORITY
         PUT   REPORT,OUTREC4      OUTPUT RECORD
         B     NEXTUSR             GET NEXT ENTRY IN ACCESS LIST
PUTREC   DS    0H
         MVC   OUTREC1,=CL80' '    CLEAR OUTREC1
         MVC   OUTREC2,=CL80' '    CLEAR OUTREC2
         MVC   OUTREC1(8),=C' PERMIT ' MOVE IN KEYWORD
         MVC   OUTREC1+8(64),RLPROF MOVE IN PROFILE
         LA    R1,OUTREC1+72       POINT BEYOND PROFILE
         TRT   OUTREC1+8(64),TSTBLNK FIND FIRST BLANK
         MVC   0(2,R1),=C' +'      MOVE IN CONTINUATION
         MVC   2(64,R1),=CL64' '   CLEAR OUT REMAINING LINE
         MVC   OUTREC2(11),=C'        ID('  MOVE IN KEYWORD
         MVC   OUTREC2+11(8),0(R6) MOVE IN USERID
         LA    R1,OUTREC2+19       POINT BEYOND USERID
         TRT   OUTREC2+11(8),TSTBLNK FIND FIRST BLANK
         MVC   0(8,R1),=C') CLASS(' MOVE IN KEYWORD
         LA    R2,8(,R1)           POINT BEYOND KEYWORD
         MVC   0(8,R2),0(R5)       MOVE IN CLASS
         LA    R1,8(,R2)           POINT BEYOND CLASS
         TRT   0(8,R2),TSTBLNK     FIND FIRST BLANK
         MVC   0(9,R1),=C') DELETE ' MOVE IN KEYWORD
         MVC   9(7,R1),GENRLP      MOVE IN GENERIC
         PUT   CMDOUT,OUTREC1      OUTPUT RECORD
         PUT   CMDOUT,OUTREC2      OUTPUT RECORD
WRTREP3  DS    0H
         MVC   OUTREC5,=CL133' '   CLEAR OUTREC5
         MVC   OUTREC5+23(29),=C'USER/GROUP NO LONGER VALID - '
         MVC   OUTREC5+52(8),0(R6) MOVE IN USER-ID
         PUT   REPORT,OUTREC5      OUTPUT RECORD
NEXTUSR  DS    0H
         LA    R8,12(,R8)          POINT TO NEXT USER-ID
         LA    R7,5(,R7)           POINT TO NEXT ACCESS FIELD
         BCT   R3,USRLOOP          DECREMENT R3 AND BRANCH IF NOT ZERO
ALLCHKD  DS    0H
         L     R0,0(,R10)          LOAD SUBPOOL AND LTH OF GOTTEN AREA
         FREEMAIN R,LV=(0),A=(10)
         B     CHKRLP
GENLOOP  DS    0H
         CP    GENCTR,GENLIM       HAS COUNTER REACHED ITS LIMIT?
         BNL   NEXTRL              YES
         AP    GENCTR,GENINC       INCREMENT COUNTER
         XC    RLPROF,RLPROF       START AT THE TOP OF THE LIST
         LA    R4,RACXTRL1         GET GENERIC PROFILES
         B     CHKRLP
NEXTRL   DS    0H
         ZAP   GENCTR,GENINC       RESET COUNTER TO 1
         XC    RLPROF,RLPROF       START AT THE TOP OF THE LIST
         LA    R4,RACXTRL2         ASSUME FIRST PROFILE IS NOT GENERIC
         B     GETIN               GET NEXT RESOURCE CLASS NAME
CLOSEDD  DS    0H
         PUT   CMDOUT,JCARD10
         CLOSE (CMDOUT,,REPORT,,SYSIN) CLOSE DDNAMES
EXITJOB  DS    0H
         L     R13,SAVEAREA+4      STANDARD EXIT
         RETURN (14,12),RC=(15)
JOBCARD  DS    0H
         PUT   CMDOUT,JCARD1
         PUT   CMDOUT,JCARD2
         PUT   CMDOUT,JCARD3
         PUT   CMDOUT,JCARD4
         PUT   CMDOUT,JCARD5
         PUT   CMDOUT,JCARD6
         PUT   CMDOUT,JCARD7
         PUT   CMDOUT,JCARD8
         PUT   CMDOUT,JCARD9
         B     CHKRLP
SAVEAREA DS    18F
RACXTRL1 RACXTRT TYPE=EXTRACTN,                                        +
               SUBPOOL=0,                                              +
               SEGMENT='BASE',                                         +
               FIELDS=FLDLRLP,                                         +
               GENERIC=YES,                                            +
               RELEASE=1.8.1,                                          +
               MF=L
RACXTRL2 RACXTRT TYPE=EXTRACTN,                                        +
               SUBPOOL=0,                                              +
               SEGMENT='BASE',                                         +
               FIELDS=FLDLRLP,                                         +
               GENERIC=ASIS,                                           +
               RELEASE=1.8.1,                                          +
               MF=L
RACXTUSR RACXTRT TYPE=EXTRACT,                                         +
               SUBPOOL=0,                                              +
               CLASS='USER',                                           +
               SEGMENT='BASE',                                         +
               FIELDS=FLDLUSR,                                         +
               RELEASE=1.8.1,                                          +
               MF=L
RACXTGRP RACXTRT TYPE=EXTRACT,                                         +
               SUBPOOL=0,                                              +
               CLASS='GROUP',                                          +
               SEGMENT='BASE',                                         +
               FIELDS=FLDLGRP,                                         +
               RELEASE=1.8.1,                                          +
               MF=L
JCARD1   DC    CL80'//CHKACC  JOB ,CHKACC,MSGCLASS=X,MSGLEVEL=(1,1),'
JCARD2   DC    CL80'//            CLASS=A,REGION=1M'
JCARD3   DC    CL80'//***********************************************'
JCARD4   DC    CL80'//*     JOB TO REMOVE OBSOLETE USERS/GROUPS     *'
JCARD5   DC    CL80'//***********************************************'
JCARD6   DC    CL80'//STEP1   EXEC PGM=IKJEFT01,DYNAMNBR=20'
JCARD7   DC    CL80'//SYSTSPRT DD  SYSOUT=*'
JCARD8   DC    CL80'//SYSTSIN  DD  *'
JCARD9   DC    CL80' PROFILE NOPREFIX'
JCARD10  DC    CL80'/*'
SEGMNTB  DC    CL8'BASE'
RLPROF   DC    CL64' '
RCLASS   DC    CL8' '
ACCREC   DC    CL8' '
NUMACC   DC    CL4' '
GENRLP   DC    CL7' '
GENREP   DC    CL4' '
OUTREC1  DC    CL80' '
OUTREC2  DC    CL80' '
OUTREC3  DC    CL133' '
OUTREC4  DC    CL133' '
OUTREC5  DC    CL133' '
FLDLRLP  DC    A((FLDLRLPZ-*-4)/8)
         DC    CL8'ACLCNT'
         DC    CL8'USERID'
         DC    CL8'USERACS'
FLDLRLPZ EQU   *
FLDLUSR  DC    A((FLDLUSRZ-*-4)/8)
         DC    CL8'AUTHOR'
FLDLUSRZ EQU   *
FLDLGRP  DC    A((FLDLGRPZ-*-4)/8)
         DC    CL8'AUTHOR'
FLDLGRPZ EQU   *
GENCTR   DC    PL2'1'
GENINC   DC    P'1'
GENLIM   DC    P'2'
ACCTAB   DS    0F
         DC    CL8'NONE'
         DC    CL8'RESERVE1'
         DC    CL8'RESERVE2'
         DC    CL8'EXECUTE'
         DC    CL8'READ'
         DC    CL8'UPDATE'
         DC    CL8'CONTROL'
         DC    CL8'ALTER'
         DC    CL8'INVALID'
TSTBLNK  DS    0F
         DC    1X'01'
         DC    63X'00'
         DC    1X'01'
         DC    191X'00'
TSTNBLK  DS    0F
         DC    64X'01'
         DC    1X'00'
         DC    191X'01'
CMDOUT   DCB   DSORG=PS,MACRF=PM,DDNAME=CMDOUT,                        +
               RECFM=FB,LRECL=80,BLKSIZE=3120
REPORT   DCB   DSORG=PS,MACRF=PM,DDNAME=REPORT,                        +
               RECFM=FBA,LRECL=133,BLKSIZE=1330
SYSIN    DCB   DSORG=PS,MACRF=(GL),DDNAME=SYSIN,EODAD=CLOSEDD
         LTORG
         END   CHKACC


Gavin Foster
Systems Programmer (Australia)                        c Xephon 1993

