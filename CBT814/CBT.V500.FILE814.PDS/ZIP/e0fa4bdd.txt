Deleting migrated datasets without recall to DASD

One particular application was having occasional serious
problems caused by the incomplete deletion of an entire family
of GDGs after most of the versions had been migrated by
HSM. The application was written in such a way as to cause
each of the migrated versions to be recalled to DASD solely for
the purpose of deleting them. Sometimes there would not be
enough primary DASD space available to recall all of the
generations, but the application would be unaware of the
deletes that failed because of the HSM recall failures. This
situation caused 'out of balance' conditions in subsequent
processing. Rather than have the vendor of the application
rewrite the code to do HDELETEs on migrated GDGs, I wrote
two short REXX EXECs to solve the problem.

DAYOWEEK is an EXEC that will set the step condition code
to a number representing the day of the week when run as a
batch TSO job step. This was needed so a batch job could
delete only the appropriate cycle's GDGs.

ARCHDEL is an EXEC that issues HDELETE commands for
the fully-qualified GDG names that are catalogued to the
VOLSER of MIGRAT only. It will not affect generations that
are not migrated (though it could easily be modified to delete
them too, if required). ACRHDEL will process all of the
migrated generations of the GDG base name which is passed to
it as its only input argument.


DAYOWEEK REXX EXEC

/* REXX -------------------------------------------------------------*/
/* This EXEC sets a return code indicative of the day of the week.   */
/* Mon=1, Tue=2, Wed=3, Thu=4, Fri=5, Sat=6, Sun=7                   */
/*-------------------------------------------------------------------*/
weekday = Date(W)
Select
   When weekday = 'Monday' Then Exit 1
   When weekday = 'Tuesday' Then Exit 2
   When weekday = 'Wednesday' Then Exit 3
   When weekday = 'Thursday' Then Exit 4
   When weekday = 'Friday' Then Exit 5
   When weekday = 'Saturday' Then Exit 6
   When weekday = 'Sunday' Then Exit 7
   Otherwise Do
      Say 'Invalid return from Date(W) call.'
      Say 'Returned value was:' weekday
      Exit 16
   End
End


ARCHDEL REXX EXEC

/* REXX -------------------------------------------------------------*/
/* This EXEC issues HDELETE commands against all migrated versions   */
/* of the GDG base name which is passed to it.                       */
/*-------------------------------------------------------------------*/
Arg gdgbase .
gdgbase = strip(translate(gdgbase," ","'"))
Call OUTTRAP O.
"LISTCAT ENTRY('"gdgbase".*') VOL"
If RC ^= 0 then do
   Say "Error from LISTCAT against: '"gdgbase"'"
   Do l = 1 to O.0
      Say O.l
   End
   Exit rc
End
"NEWSTACK"
Do l = 1 to o.0
   o.l = Translate(o.l," ","-")
   If Word(o.l,1) = "NONVSAM" Then
      Do
         dsn = Word(o.l,2)
         l = l + 6
         o.l = Translate(o.l," ","-")
         If Word(o.l,2) = "MIGRAT" Then
            Do
               Queue "HDELETE '"dsn"'"
            End
      End
End
Do l = 1 to Queued()
   Pull line
   Say line
   line
End
"DELSTACK"
Exit


John Cornman (USA)                                  c Xephon 1993
