Front-ending RACF commands for end-users

RACF has improved greatly over recent years except in one
respect, which is the standard RACF ISPF interface. IBM has not
made full use of the capabilities of ISPF to display information
and generally has failed to make this a friendly interface,
especially for non-technical users. This article will demonstrate
how this deficiency can be quickly rectified.

A common requirement for local support personnel is to reset a
user's password. To avoid mistakes, the details of the user-id need
to be shown to the local administrator before making the update.
The REXX shown below prompts for the RACF user-id, displays
the name and other information, and then allows the password to
be reset and the user-id resumed.

After the update the displayed user-id information is refreshed
allowing confirmation that the update has succeeded. All this is
done using only full-screen ISPF panels so that line mode
commands and output are not seen by the end-user. If the user-id
does not exist or the update is not authorized, an ISPF message
(with alarm) is shown on the panel.

Many similar dialogs can be constructed and using ISPF table
displays is particularly effective when a list of profiles is extracted
with the RACF SEARCH command or for permit lists etc. It
should be an aim of all installations to eliminate the use of line
commands and line mode output, which really belong to the
teletype era of the 1970s. Use of the REXX OUTTRAP feature
and ISPF panels can achieve this with ease.

REXX RACFLOC

/* REXX **************************************************************/
/* LOCAL ADMINISTRATOR'S RACF INTERFACE - DISPLAY USER, RESET PASSWD */
/*********************************************************************/
ADDRESS ISPEXEC "DISPLAY PANEL(RACFLOC1)"      /* REQUEST USER-ID  */
DO WHILE (RC = 0)                              /* IF ENTER PRESSED */
   CALL DISPLAY                                /* DISPLAY USER-ID  */
   ADDRESS ISPEXEC "DISPLAY PANEL(RACFLOC1)"   /* REPEAT UNTIL PF3 */
END
EXIT 0                                         /* END OF MAIN REXX */
/* SUBROUTINE TO DISPLAY/UPDATE THE USER-ID */
DISPLAY:
CALL GATHER             /* EXTRACT USER-ID INFORMATION */
IF RESULT > 0           /* IF NOT FOUND OR NOT AUTH RETURN */
   THEN RETURN 4
ADDRESS ISPEXEC "DISPLAY PANEL(RACFLOC2)"   /* SHOW USER-ID INFO  */
DO WHILE (RC = 0)                                   /* IF ENTER   */
   ADDRESS TSO "ALU" USER "PASS("PASSWD") RESUME"      /* UPDATE  */
   IF RC = 0 THEN
      ADDRESS ISPEXEC "SETMSG MSG(RACFL002)"           /* OK      */
   ELSE
      ADDRESS ISPEXEC "SETMSG MSG(RACFL003)"           /* BAD     */
   CALL GATHER                                         /* REFRESH */
   ADDRESS ISPEXEC "DISPLAY PANEL(RACFLOC2)"  /* REDISPLAY INFO   */
END
RETURN 0
/* SUBROUTINE TO EXTRACT THE USERID INFORMATION */
GATHER:
X = OUTTRAP('VAR.')                   /* TRAP LISTUSER OUTPUT */
ADDRESS TSO "LISTUSER" USER
X = OUTTRAP('OFF')
IF RC > 0 THEN                        /* COMMAND FAILED */
   DO
      ADDRESS ISPEXEC "SETMSG MSG(RACFL001)"
      RETURN 4
   END
NAME   = GETWORD('NAME=' VAR.1)           /* NAME */
PASSDT = GETWORD('PASSDATE=' VAR.2)       /* PASSWORD DATE */
ATTRIB = GETWORD('ATTRIBUTES=' VAR.3)     /* ATTRIBUTES */
LASTAD = GETWORD('LAST-ACCESS=' VAR.5)    /* LAST USED DATE */
INST   = SUBSTR(VAR.7,20)                 /* INSTALLATION DATA */
RETURN 0
/* FUNCTION TO PARSE A LINE OF COMMAND OUTPUT */
GETWORD:
ARG KEYWORD LINE
IN = INDEX(LINE,KEYWORD)                  /* FIND KEYWORD */
IF IN > 0 THEN
   RET = SUBWORD(SUBSTR(LINE,IN+LENGTH(KEYWORD)),1,1)
ELSE
   RET = ' '
RETURN RET                /* RETURN WORD FOLLOWING KEYWORD */

RACFLOC1 PANEL

%----------------------------%RACF USER ADMINSTRATION%-------------------------+
%COMMAND ===>_ZCMD
+
+RACF ADMINISTRATION PASSWORD CHANGE AND RESUME USER
+
%RACF USER-ID%===>_USER    +      (Enter user-id)
+
+
+Enter user-id and press%ENTER+to process or press%END+to exit
)INIT
  .CURSOR = USER
)PROC
  VER (&USER,NB,NAME)
)END

RACFLOC2 PANEL

%----------------------------%RACF USER ADMINSTRATION%-------------------------+
%COMMAND ===>_ZCMD
+                                                                   %&ZDATE
+RACF ADMINISTRATION PASSWORD RESET AND RESUME USER                 %&ZJDATE
+                                                                   %&ZTIME
+USERID    +--->%&USER
+
+NAME      +--->%&NAME
+ATTRIBUTE +--->%&ATTRIB
+PASSDATE  +--->%&PASSDT
+LAST USED +--->%&LASTAD
+
+INST-DATA +--->%&INST

+
+PASSWORD  %===>_PASSWD  +          (Enter value for new password)
+
+
+Press%ENTER+to UPDATE or press%END/PF3+to EXIT
)INIT
  &PASSWD = ' '
)PROC
  VER (&PASSWD,NB)
)END

Message member RACFL00

RACFL001 'INVALID USERID'        .ALARM=YES
'USER-ID &USER NOT FOUND OR YOU ARE NOT AUTHORIZED TO UPDATE'

RACFL002 '&USER UPDATED'         .ALARM=NO
'USER-ID PASSWORD HAS BEEN CHANGED AND THE USERID RESUMED'

RACFL003 'UPDATE FAILED'         .ALARM=YES
'USER-ID PASSWORD CHANGED/RESUME HAS FAILED'


                                                                                                                 c Xephon 1994

