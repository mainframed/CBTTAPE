Improving your ISPF performance

Are you tired of the situation where you are problem solving
and browsing a dataset - just when you have identified the
problem you try to edit the dataset only to find out that you
cannot change data while in browse.  If you have then you
should keep on reading.

I have developed a little REXX for use in TSO/E Version 2 and
ISPF Version 2.3 (maybe it works on earlier releases) that,
with a single command on the command line from a browse
panel, can put you in edit mode on the dataset that you are
browsing.

It depends, unfortunately, on a simple modification to the ISPF
browse panel.  The comments in the REXX detail what
modification has to be made on the browse panel ISRBROBF.
You should add an entry in the ISPF command table (the
example below names it BREDIT) in order to make it easy to
use:

ZCTVERB ZCTTRUNC ZCTACT                     ZCTDESC
BRE      0       SELECT CMD(%BREDIT &ZPARM) Invoke EDIT on what you BROWSE

You should be aware that some functions (like the SUPERC
browse output) do not use the standard browse panel
ISRBROBF.

/*  This REXX-program does a call to ISPF EDIT,                */
/*  when it is called directly from the BROWSE Panel.          */
/*  This is useful if you regretted that you entered BROWSE    */
/*  and you want to enter EDIT on the same dataset.            */
/*  You can also supply a dataset (unqualified with userid)    */
/*  as parameter to ask for explicit EDIT of that.             */
/*  You can put an entry in the ISPF Command Table:            */
/*          BRE 0 SELECT CMD(%BRE &ZPARM)                      */
/*  so this REXX can be activated quick and easy.              */
/*-------------------------------------------------------------*/
/*  When called without parameter, the variables ZDSN and ZMEMB*/
/*  are VGET from the function pool. BROWSE are using these    */
/*  variables when it displayes the BROWSE panel ISRBROBF,     */
/*  but he 'fails' to put these the variables into the pool.   */
/*  Following fix can accomplish this and you will be happy    */
/*  to directly go from BROWSE to EDIT with a single keystroke.*/
/*     Add following to the )INIT, )REINIT and )PROC section:  */
/*         VPUT (ZDSN ZMEMB)          /* BRE - Fix  */         */
/*=============================================================*/
  arg dsn
/*  trace i  */
  Address ISPEXEC "CONTROL ERRORS RETURN"
  Address ISPEXEC "VGET (ZDSN ZMEMB Z) ASIS"
  Select
     When  dsn ^= z Then Signal Edit
     When zdsn ^= z Then Signal Edit_Member
     Otherwise do
        zedlmsg = "there is no dataset to edit"
        Signal Stop
        End
     END
Edit:
     Address ISPEXEC "LMINIT DATAID(DID) DATASET("dsn")"
     Address ISPEXEC "EDIT DATAID("did")"
     cc = rc
     Address ISPEXEC "LMFREE DATAID("did")"
     If cc > 8 Then Do
        zedlmsg = "Error after edit. RC = "cc
        If cc = 20 Then
           zedlmsg = "RC = 20 after Edit. Maybe dataset ",
                     dsn" not found"
        Signal Stop
        End
     Signal Term
Edit_Member:
     zdsn = "'"zdsn"'"
     Address ISPEXEC "LMINIT DATAID(DID) DATASET("zdsn")"
     Address ISPEXEC "EDIT DATAID("did") MEMBER("zmemb")"
     cc = rc
     Address ISPEXEC "LMFREE DATAID("did")"
     If cc > 8 Then Do
        zedlmsg = "Error after Edit. RC = "cc
        If cc = 20 Then
           zedlmsg = "RC = 20 after Edit. Maybe dataset ",
                     zdsn" not found"
        Signal Stop
        End
     Signal Term
Stop:
  Address ISPEXEC "SETMSG MSG(ISRZ000)"
Term:
  Return


Peter Rosenberg
Senior Analyst
CRI/AS (Denmark)                                      c Xephon 1990

