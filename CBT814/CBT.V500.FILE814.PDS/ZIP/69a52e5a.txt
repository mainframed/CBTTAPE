An automated operations facility (continued)
In this monthÕs issue we conclude this article by publishing the
source code for the program JTMP, along with the ESTAE exit
routine, an SVC to make programs authorised, and examples of
control cards and JCL.
THE PROGRAM JTMP
JTMP     CSECT
* THIS PROGRAM WILL EXECUTE CLISTS THAT ARE WRITTEN IN THE STORAGE *
* TABLE IN THE CSA
* IT IS EXECUTED AS A STARTED TASK IE:
*           S JTMP
*        --> THEN AN OUTSTANDING REPLY -> REPLY END TO STOP THE TASK
         STM   R14,R12,12(R13) SAVE AREA OF THE CALLER
         LR    12,R15          PUT ENTRY ADDRESS IN BASE REGISTER
         USING JTMP,R12        R12 = BASE REGISTER FOR ADDRESSABILTY
         ST    R13,SAVEAREA+4  ADDRESS OF SAVE AREA OF THE CALLER
         LR    R2,R13          ADDRESS OF CALLER SAVE AREA IN R2
         LA    R13,SAVEAREA    R13 ADDRESS OF MY SAVE AREA
         ST    R13,8(R2)       PUT ADDRESS IN SAVE AREA OF THE CALLER
* LOAD ESTAE ROUTINE
         LOAD  EP=JSTAE        STAE EXIT ROUTINE
         LR    R2,R0           LOAD ENTRY ADDRESS IN R2
         ESTAE (2),TERM=YES    SET ESTAE ON FOR ALL ABNORMAL ENDS
* CREATE THE CLIST TABLE IN THE CSA
*   CVTUSER FIELD POINTS TO A 12 BYTE AREA IN THE CSA
*      FIRST 4 BYTES = ADDRESS OF THE CLIST TABLE
*      SECOND 4 BYTES = DISPLACEMENT IN THE CLIST TABLE FOR JTIMERS
*      THIRD 4 BYTES = DISPLACEMENT IN THE CLIST TABLE FOR JTMP
*   CLIST TABLE IS 900 BYTES LONG = 100 CLIST ENTRIES
         LA    R0,1                 R0 = 1
         SVC   247                  PUT JSCBAUTH BIT ON
         MODESET KEY=ZERO,MODE=SUP  SUPERVISOR STATE AND KEY ZERO
         L     R10,CVTPTR           ADDRESS OF THE CVT
         USING CVTMAP,R10           ADDRESSABILITY TO THE CVT
         GETMAIN R,LV=12,SP=231,LOC=BELOW GETMAIN 12 BYTES IN CSA
         ST    R1,CVTUSER           STORE ADDRESS IN CVTUSER FIELD
         GETMAIN R,LV=900,SP=231,LOC=BELOW GETMAIN 900 BYTES IN CSA
         LR    R2,R1                MOVE ADDRESS IN R2
         L     R3,CVTUSER           LOAD CVTUSER FIELD
         ST    R2,0(R3)             STORE ADDRESS IN THE FIRST 4 BYTES
         LA    R1,0                 R1 = 0
         ST    R1,4(R3)             MOVE ZEROES TO SECOND 4 BYTES
         ST    R1,8(R3)             MOVE ZEROES TO THIRD 4 BYTES
         MODESET KEY=NZERO,MODE=PROB  BACK TO PROBLEM STATE
         SR    R0,R0                  R0 = 0.
         SVC   247                    CLEAR JSCBAUTH BIT
* PUT WTOR SO THAT YOU CAN STOP JTMP FROM THE CONSOLE
WTO      DS    0H
         XC    ECB1,ECB1       INITIALIZE EVENT CONTROL BLOCK
         WTOR  'JTMP   DISPATCHER (''END'' TO STOP)',                  X
               REPLY,3,ECB1,ROUTCDE=(9)
*                        WTOR TO ALL CONSOLES
         OPEN  (SYSOUT,(OUTPUT))   OPEN SYSOUT DATASET
         B     BEGIN
* TEST ON OPERATOR-REPLY TO STOP THIS PROGRAM
WTOR     DS    0H
         TM    ECB1,B'01000000'     TEST IF JTMP MUST STOP
         BO    EINDE                IF YES -->
BEGIN    DS    0H             ENQ IF THERE IS MORE THAN ONE JTMP
         ENQ   (QNAME,RNAME,E,,SYSTEM),RET=HAVE
         LA    R0,1                 R0 = 1
         SVC   247                  PUT JSCBAUTH BIT ON
         MODESET KEY=ZERO,MODE=SUP  SUPERVISOR STATE AND KEY ZERO
         L     R2,CVTUSER           LOAD CVTUSER FIELD
         L     R3,0(R2)             ADDRESS OF TABLE WITH CLISTS
         L     R4,8(R2)             OFFSET IN THE TABLE OF CLISTS
         AR    R3,R4           ADD OFFSET TO THE BEGIN OF THE TABLE
         CLC   0(1,R3),=X'AA'      CHECK IF CLIST MUST BE EXECUTED
         BE    FOUND               YES ->
         MODESET KEY=NZERO,MODE=PROB  BACK TO PROBLEM STATE
         SR    R0,R0                  R0 = 0
         SVC   247                    CLEAR JSCBAUTH BIT
         DEQ   (QNAME,RNAME,,SYSTEM)  DEQUEUE
         B     WACHT                  BRANCH TO WAIT
FOUND    DS    0H                    THERE IS AN EXECUTABLE CLIST
         MVC   0(1,R3),=C'Y'         NOTICE IT IN THE TABLE
         LA    R4,9(R4)              NEXT OFFSET FOR THE CLIST TABLE
         C     R4,=F'899'            CHECK IF END OF THE TABLE
         BL    SUBROUT               NO ->
         SR    R4,R4                 YES -> SET OFFSET EQUAL TO ZERO
SUBROUT  DS    0H
         ST    R4,8(R2)              STORE NEW OFFSET IN THE CSA
         MVC   PARAM+2(8),1(R3)      MOVE NAME OF THE CLIST
         MVC   NAAM(8),1(R3)
         MVC   PARAM(2),=H'8'         MOVE LENGTH FIELD
         DEQ   (QNAME,RNAME,,SYSTEM)  DEQUEUE
         LA    R1,PARAM               R1 -> PARAMETER
         MVC   ENDECB,ECBI            INITIALIZE EVENT CONTROL BLOCK
         MVC   OUT(8),PARAM+2       MOVE NAME OF CLIST AND PARAM
         PUT   SYSOUT,OUT             WRITE IT DOWN
* ATTACH IKJEFT01 -> EXECUTION OF THE CLIST
         ATTACH EP=IKJEFT01,PARAM=(PARAM),SM=SUPV,ECB=ENDECB
         ST    R1,TCB                 STORE ADDRESS OF THE TCB
         MODESET KEY=NZERO,MODE=PROB  BACK TO PROBLEM STATE
         XR    R0,R0                  R0 = 0
         SVC   247                    CLEAR JSCBAUTH FIELD
         L     5,ADKILL         LOAD ADDRESS OF TIMER ABEND ROUTINE
         STIMER REAL,(5),DINTVL=TWOMIN  CLIST MAY LAST TWO MINUTES
         WAIT  ECB=ENDECB             WAIT ON THE END OF THE CLIST
         LA    R0,1                 R0 = 1
         SVC   247                  PUT JSCBAUTH BIT ON
         MODESET KEY=ZERO,MODE=SUP  SUPERVISOR STATE AND KEY ZERO
         DETACH TCB                   DETACH THE CLIST
         MODESET KEY=NZERO,MODE=PROB  BACK TO PROBLEM STATE
         SR    R0,R0                  R0 = 0
         SVC   247                    CLEAR JSCBAUTH BIT
         B     WTOR                   BRANCH TO WTOR
WACHT    DS    0H                      WAIT FIVE SECONDS
         STIMER WAIT,DINTVL=TIJD
         B     WTOR                    CHECK OUTSTANDING WTOR
EINDE    DS    0H                      THE END
         CLOSE SYSOUT                  CLOSE SYSOUT DATASET
         LA    R0,1                 R0 = 1
         SVC   247                  PUT JSCBAUTH BIT ON
         MODESET KEY=ZERO,MODE=SUP  SUPERVISOR STATE AND KEY ZERO
         L     R2,CVTUSER            R2 = CVTUSER FIELD
         L     R3,0(R2)               R3 = ADDRESS OF THE TABLE
         MVC   CVTUSER(4),HEX40     MOVE HEX40 TO CVTUSER FIELD
         FREEMAIN R,LV=900,SP=231,A=(3)  FREEMAIN TABLE WITH CLISTS
         FREEMAIN R,LV=12,SP=231,A=(2)   FREEMAIN THE 12 BYTES
         MODESET KEY=NZERO,MODE=PROB  BACK TO PROBLEM STATE
         SR    R0,R0                  R0 = 0
         SVC   247                    CLEAR JSCBAUTH BIT
         ESTAE 0                      ESTAE OFF
         L     R13,SAVEAREA+4          LOAD ADDRESS OF SAVE AREA
         RETURN (14,12),RC=(15)        GO BACK
* CONSTANTS
SAVEAREA DC    18F'0'                  SAVE AREA
ADKILL   DC    A(KILL)                  ADDRESS OF TIMER ABEND ROUTINE
TWOMIN   DC    CL8'00023000'            ABEND AFTER 2' 30''
NAAM     DC    CL8' '                   NAME OF CLIST
         DS    0D
QNAME    DC    CL8'MHOENQSY'            QNAME FOR ENQUEUE
RNAME    DC    CL20'AUTOMATED OPERATIONS'  RNAME FOR ENQUEUE
         DS    0D
SYSOUT   DCB   DDNAME=SYSOUT,DSORG=PS,MACRF=(PM),LRECL=133,            X
               BLKSIZE=133,RECFM=FA
OUT      DC    CL133' '                 OUTPUT AREA
HEX40    DC    XL4'40404040'            CONSTANT
HEX00    DC    XL4'00000000'            CONSTANT
TIJD     DC    CL8'00000500'            WAIT FIVE SECONDS
         DS    0D
PARAM    DS    0CL10                    PARAMETER FOR IKJEFT01
         DC    H'8'                     LENGTH OF PARAMETER
         DC    CL8'        '            NAME OF CLIST
REPLY    DC    CL3'   '                 CONSTANT
ECB1     DC    F'0'                     CONSTANT
ECBI     DC    F'0'                     CONSTANT
ENDECB   DC    F'0'                     CONSTANT
TCB      DC    F'0'                     TCB ADDRESS
         CVT   DSECT=YES,LIST=NO        COMMUNICATION VECTOR TABLE
KILL     CSECT                          TIME ABEND ROUTINE
         SAVE  (14,12),,*               SAVE REGISTERS
         BALR  12,0                     ADDRESSABILITY
         USING *,12
         L     2,ADECB                  LOAD ADDRESS OF ECB
POSTEM   POST  (2),999                  POST TCB RETURN CODE = 999
         MVC   DWTO,WTOLIST             MOVE WTO
         L     7,ADNAAM                 LOAD ADDRESS OF NAME OF CLIST
         MVC   DWTO+10(8),0(7)          MOVE NAME OF CLIST TO WTO
         WTO   MF=(E,DWTO)              WTO CLIST KILLED
         RETURN (14,12),RC=0            GO BACK
WTOLIST  WTO   'CLIST           KILLED',ROUTCDE=(9),MF=L
WTOEND   EQU   *
DWTO     DS    CL(WTOEND-WTOLIST)
ADECB    DC    A(ENDECB)                ADDRESS OF ECB
ADNAAM   DC    A(NAAM)                  ADDRESS OF NAME OF CLIST
         END
THE ESTAE EXIT ROUTINE JSTAE
JSTAE    CSECT
         SAVE  (14,12)                SAVE REGISTERS
         BALR  12,0                   ADDRESSABILITY
         USING *,12
         LA    0,1                    SET R0 = 1
         SVC   247                    JSCBAUTH BIT ON
         MODESET KEY=ZERO,MODE=SUP    SUPERVISOR AND KEY ZERO
         L     10,CVTPTR              LOAD ADDRESS OF CVT
         USING CVTMAP,10              ADDRESSABILITY TO THE CVT
         L     2,CVTUSER              CVTUSER FIELD
         L     3,0(2)                 LOAD ADDRESS OF THE CLIST TABLE
         MVC   CVTUSER(4),=X'40404040'     CLEAR THE CVTUSER FIELD
         FREEMAIN RC,LV=900,SP=231,A=(3)   FREEMAIN CLIST TABLE
         FREEMAIN RC,LV=12,SP=231,A=(2)    FREEMAIN THE 12 BYTES
         MODESET KEY=NZERO,MODE=PROB  BACK TO PROBLEM STATE
         SR    0,0                    SET R0 = 0
         SVC   247                    JSCBAUTH BIT OFF
         LA    15,0                   R15 = 0
         BR    14                     RETURN
         CVT   DSECT=YES,LIST=NO      COMMUNICATION VECTOR TABLE
         END
SVC247 FOR MAKING A PROGRAM AUTHORISED
* THIS IS A SELF WRITTEN USER SVC ROUTINE (SVC 247) TO PLACE THE
* CALLER IN AUTHORISED STATE
IGC00247 CSECT                       AUTHORISATION ON/OFF SVC
         BALR  12,0                  PROGRAM ADRESSABILITY
         USING *,12
         L     2,28(5)               ADDRESS OF CALLERS RB TO R2
         L     2,180(4)              ADDRESS OF JSCB TO R2
         BCT   0,AUTHOFF             R0 ^= 1 REQUESTS AUTH OFF
AUTHON   OI    236(2),X'01'          SET JSCBAUTH BIT
         B     RETURN                EXIT WITH AUTHORISATION ON
AUTHOFF  NI    236(2),X'FE'          CLEAR JSCBAUTH BIT
RETURN   BR    14
         END   IGC00247
SAMPLE CONTROL CARDS
ZTIMER - AUTOMATIC REMINDER/COMMAND FACILTY *
* COL 1 TO 8 : EITHER DATE IN FORMAT 'DD/MM/YY'
*                 OR DATE IN FORMAT 'YY.DDD'
*                 OR 3-CHAR WEEKDAY 'MON TUE WED THU FRI SAT SUN'
*                 OR WORD 'WKDAY' FOR ANY WEEKDAY (MON TO FRI)
*                 OR WORD 'WORKD' FOR ANY WORKDAY
*                 OR WORD 'WKEND' FOR WEEKEND (SAT AND SUN)
*                 OR WORD 'ALL' FOR EVERY DAY EXECUTION
*                 OR '*' IN COL1 FOR A COMMENT CARD
* COL 10 TO 14: EITHER TIME IN FORMAT 'HH.MM'
*                 OR BLANKS FOR EXECUTION EVERY TIME.
* COL 16 TO 18: EITHER 'COM' TO ISSUE COMMAND
*                 OR 'DIS' TO ISSUE A HIGH-BRIGHTNESS MESSAGE
*                 OR 'JOB' TO SUBMIT A JOB
*                 OR 'CLI' TO EXECUTE A CLIST
* COL 20 TO 22: SYSTEM-ID WHERE THE ACTION IS TO BE DONE
*     ONLY FOR JES3 (RELEASE 2.2.1 AND MORE)
*                 SY1 - SY2 - BLANK
* COL 24 TO 72: EITHER THE MESSAGE TO BE DISPLAYED
*                  OR THE COMMAND AS ENTERED AT THE MASTER CONSOLE
*                  OR THE DATASETNAME THAT CONTAINS THE SPECIFIED
*                  JOB (BETWEEN SINGLE QUOTES)
WORKD    13.55 JOB     'PROD.JCLLIB(JOBNO1)'
WKEND    17.30 COM     F LLA,REFRESH
22/03/88 09.20 COM     SE 'meeting',USER=(xxxxxxx),NOW
MON      09.00 COM     SE 'Monday         ...',USER=(xxxxxxx),NOW
TUE      09.00 COM     SE 'Tuesday        ...',USER=(xxxxxxx),NOW
WED      09.00 COM     SE 'Wednesday      ...',USER=(xxxxxxx),NOW
THU      09.00 COM     SE 'Thursday       ...',USER=(xxxxxxx),NOW
FRI      09.00 COM     SE 'Friday         ...',USER=(xxxxxxx),NOW
SAMPLE CONTROL CARDS FOR THE HOLIDAYS
* ZTIMER - AUTOMATIC REMINDER/COMMAND FACILITY
* COL 1 TO 8 : DATE IN FORMAT 'DD/MM/YY'
01/01/89
27/03/89
01/05/89
04/05/89
13/05/89
21/07/89
15/08/89
01/11/89
11/11/89
25/12/89
PROCLIB JCL FOR JTIMER
//JTIMER   PROC SYS=SY1
//REMINDR  EXEC PGM=JTIMER,TIME=1440
//SYSABEND DD SYSOUT=D
//SYSIN    DD DSN=PDS.FOR.COMMANDS(COM&SYS),DISP=SHR
//SYSIN1   DD DSN=PDS.FOR.HOLIDAYS(HOLIDAY),DISP=SHR
PROCLIB JCL FOR JJOB  (JOB SUBMISSION)
//JJOB     PROC NAME=XXX
//STEP1    EXEC PGM=IEBGENER
//SYSUT1   DD DISP=SHR,DSN=&NAME
//SYSUT2   DD SYSOUT=(B,INTRDR)
//SYSPRINT DD SYSOUT=O
//SYSIN    DD DUMMY
PROCLIB JCL FOR JTMP
//JTMP     PROC
//JTMP     EXEC PGM=JTMP,TIME=1440
//SYSOUT   DD SYSOUT=T
//SYSABEND DD SYSOUT=T
//SYSPROC  DD DSN=PDS.FOR.CLISTS,DISP=SHR
//SYSTSPRT DD SYSOUT=T
//SYSTSIN  DD DUMMY
//*


	© Xephon 1989

