Displaying the DDNAME of a dataset

The following short program was written to display the DDNAME
to which a dataset is allocated and has proved very useful on
several occasions.  The dataset name is entered as a PARM value.
The dataset name comparison procedure is based on the number of
characters of the value specified in the PARM parameter.  In this
way a generic display capability can be provided.

DSN      START
         USING  *,15
         B      BEGPROG
         DC     C'DSN DATE: &SYSDATE TIME: &SYSTIME'
BEGPROG  STM    14,12,12(13)        *SAVE OPSYS REGISTERS
         LR     10,13               *SAVE OPSYS SAVE AREA
         LA     13,SAVE1            *GET OUR SAVE AREA
         ST     13,8(10)            *CHAIN FORWARD
         ST     10,4(13)            *CHAIN BACK
         DROP   15
         USING  DSN,12              *USE 12 AS BASE
         LR     12,15
***  ROUTINE CHKPARM WILL SETUP THE DSNAME INTO
PARMAREA.THE PAR-  ***
*    AMETER LENGTH IS SETUP IN THE HALF WORD PARMLEN.
*
***  IF NO PARAMETER WAS PASSED,THEN REGISTER 15 IS SET TO 8.
***
         BAL    14,CHKPARM
         LTR    15,15
         BNZ    ENDPROG
         MVI    FOUND,C'N'         *SET THE FOUND FLAG TO NO
         LA     2,TBLADD
         USING  TBLDSECT,2         *ADDRESS 2 AS DSN TABLE
GETTIOT  L      3,16               *   = CVT POINTER
         L      3,0(3)             *   = TCB ADDRESS LIST
         L      3,4(3)             *   = CURRENT TCB ADDRESS
         L      3,12(3)            *   = TIOT ADDRESS
         LA     3,24(3)            *   = FIRST DDNAME
LOOP     CLI    0(3),X'00'         *END OF ENTRIES?
         BE     ENDLOOP
         ICM    4,B'0111',12(3)    *GET JFCB ADDRESS
         LA     4,16(4)            *   = DSNAME ADDRESS
         BAL    14,STOREDD         *ROUTINE TO ADD DDNAME TO TABLE
         LH     11,PARML           *GET LENGTH OF DSNAME ENTERED
         EX     11,CLCDSN          *COMPARE THE DSNAME
         BNE    NOTFND
         MVI    FOUND,C'Y'
NOTFND   XR     5,5                *CLEAR 5
         IC     5,0(3)             *GET LENGTH OF DDNAME
         AR     3,5                *POINT 3 TO NEXT DDENTRY
         B      LOOP               *START THE LOOP ALL OVER AGAIN
ENDLOOP  CLI    FOUND,C'Y'         *HAS IT BEEN FOUND?
         BNE    ENDPROG
         LR     11,2
         LA     2,TBLADD
         SR     11,2               *11 = LENGTH OF SCREEN DISPLAY
         LTR    11,11              *HAS SCREEN ALREADY BEEN SHOWN?
         BZ     ENDPROG
         TPUT   (2),(11)           *OUTPUT THE DDNAME
ENDPROG  L      13,4(13)
         LM     14,12,12(13)
         BR     14
CLCDSN   CLC    PARM(0),0(4)
CHKPARM  SR     15,15
         L      2,0(1)
         CLC    0(2,2),=X'0000'     *ANY PARAMETERS PASSED AT ALL?
         BZ     NOPARMS
         LH     3,0(2)
         CH     3,=H'44'            *IS DSNAME LENGTH > 44
         BH     BADDSN              *YES.BAD DSNAME
         BCTR   3,0
         STH    3,PARML
         EX     3,MVCPARM           *MOVE THE DSNAME TO PARM
         B      EPARMCHK
BADDSN   TPUT   MSG1,50
         LA     15,8
         B      EPARMCHK
NOPARMS  TPUT   MSG2,50
         LA     15,8
EPARMCHK BR     14
MVCPARM  MVC    PARM(0),2(2)
MSG1     DC     CL50'BAD DATASET NAME'
MSG2     DC     CL50'NO PARAMETER ENTERED'
***    THIS ROUTINE SETS UP THE DDNAME & DSNAME INTO A SCREEN
AREA***
*      AND PTS OUT THE SCREEN WHEN EVER IT FILLS UP WHETHER THE
*
***    DSNAME HAS BEEN FOUND TILL THEN OR NOT.                    ***
STOREDD  LA     11,TBLEND
         CR     2,11               *HAVE WE REACHED END OF SCREEN?
         BL     ISRTDD
         LA     2,TBLADD           *SET 2 TO START OF TABLE
         CLI    FOUND,C'Y'
         BNE    ISRTDD
         TPUT   (2),1920           *OUTPUT THE SCREEN
ISRTDD   CLI    4(3),C' '          *IS DDNAME BLANK
         BE     MOVEDD
         CLI    FOUND,C'Y'         *WAS DSNAME FOUND BEFORE?
         BNE    NEWDD              *NO SO RESET THE ADDRESS & FLAG
         LR     11,2
         LA     2,TBLADD
         SR     11,2               *11 = LENGTH OF SCREEN DISPLAY
         LTR    11,11              *HAS SCREEN ALREADY BEEN SHOWN?
         BZ     NEWDD
         TPUT   (2),(11)           *OUTPUT THE DSN FOUND SO FAR
NEWDD    MVI    FOUND,C'N'         *RESET FOUND FLAG
         LA     2,TBLADD
MOVEDD   MVI    0(2),C' '          *FIRST BLANK OUT THE AREA
         MVC    1(79,2),0(2)
         MVC    DDNAME,4(3)        *MOVE THE DDNAME TO TABLE AREA
         MVC    DSNAME,0(4)        *MOVE THE DSNAME TO TABLE AREA
         LA     2,80(2)            *POINT 2 TO NEXT ENTRY
         BR     14
         LTORG
SAVE1    DC     18F'0'            REG SAVE AREA
PARML    DS     H
PARM     DC     CL44' '
FOUND    DS     C
TBLADD   EQU    *
         DS     24CL80
TBLEND   EQU    *
TBLDSECT DSECT
DDNAME   DS     CL8
         DS     CL3
DSNAME   DS     CL44
         DS     CL25
         END    DSN


C Sreenath Chary
Consultant
QANTAS (Australia) © Xephon 1989

