Delayed console commands

The number of VTAM applications that need to be started at IPL time
just keeps increasing all the time.  VTAM itself of course gets
started via a 'COM=START NET' entry in the COMMNDxx member of
PARMLIB.  We found, however, that we could not use the same means
of starting address spaces like NetView, TPX, ROSCOE etc, since
these required VTAM to be fully up.  The alternative, which we used
for some time, was to have the last entry in COMMNDxx start a job
which submitted a number of JES2 automatic commands to the
internal reader.

This had two drawbacks.  One was that, every once in a while, JES2
would finish coming up and submit the start commands before VTAM
had reached the point of accepting log-ons.  And because it happened
very infrequently, the operators would get very concerned when the
session manager logo failed to flash across the screen at nearby
terminals.

The other drawback was that when the commands did work correctly,
they would start all the specified address spaces almost
instantaneously, flooding the console with messages.

The solution which we ended up with involved writing the following
program that gets executed in a job started by one of the last entries
in PARMLIB's COMMNDxx member.  The program was designed to issue
a series of console commands, the first after a wait of 60 seconds
and the remainder at 2-second intervals.  Comments preceding the
actual code explain how the input is set up and how the values of
desired intervals may be specified.

CNSCMD   TITLE ' ***   PROGRAM TO ISSUE DELAYED CONSOLE COMMANDS   ***'
CNSLCMDS CSECT
*  This program will read a sequential file of card images, and
*  issue SVC 34 to cause the text of each card image to be issued as
*  a console command.  The first command will be issued after a wait of
*  x seconds, while subsequent commands are issued one after another
*  at y-second intervals.
*  (The default value for "x" is 60, while that for "y" is 2; but by
*  placing on the EXEC statement a PARM value say of PARM=(5,2), the
*  values for delay times can be changed to 5 seconds and 2 seconds
*  respectively.  In editing the values supplied via PARM, it is
*  assumed that if "x" is specified, then "y" is also, and that the
*  the valid range for each is 1 to 3 digits. (See code at @PARMCK).
*  NOTES  - Command text on each input card must start on column 1.
*         - This program must be link edited with an Authorization Code
*           of 1, and must be executed out of an authorized library.
         LCLC  &IDNAME                    For this CSECT's ID tag
         LCLC  &DATE6,&TIME4              For Date & Time of Assembly
&DATE6   SETC  '&SYSDATE'(1,2).'&SYSDATE'(4,2).'&SYSDATE'(7,2)
&TIME4   SETC  '&SYSTIME'(1,2).'&SYSTIME'(4,2)
&IDNAME  SETC  'CNSLCMDS/&DATE6/&TIME4'   Put together std. ID
BEGIN    SAVE  (14,12),,&IDNAME
         LR    R12,R15                    Establish R12 as
         USING CNSLCMDS,R12                 the Base Register
         LR    R11,R13                    Hold C(R13) in R11 for now
         LA    R13,SAVE18F
         ST    R13,8(R11)
         ST    R11,SAVE18F+4
*---------------------------------------------------------------------*
*                  M A I N L I N E    L O G I C                       *
*---------------------------------------------------------------------*
         BAL   R11,@PARMCK             Check PARM data, if any
         OPEN  CARDIN                  File of console commands
GET$TEXT EQU   *
         GET   CARDIN                  Get the text of a console cmd
*   Typical data on the record:  "S CNMPROC "
         LTR   R2,R2                   Good command text?
         BZ    GET$TEXT                If not, skip this record
         BAL   R11,@PUTCMD             If yes, go issue console command
         B     GET$TEXT                Get the next record
INPT$END EQU   *                       <===  EODAD for file CARDIN
         CLOSE CARDIN
EOJPOINT EQU   *
         L     R13,SAVE18F+4           Restore entry time value in R13
         RETURN (14,12),,RC=0          Exit with a Return Code of 0
         TITLE '*****   P E R F O R M E D     R O U T I N E S   ******'
@PARMCK  DS    0H          ---------    Let's edit supplied PARM data
         L     R9,0(R1)                 Addr of PARM data now in R9
         LH    R1,0(R9)                 Let's look at the count field
         LTR   R1,R1                    Was any PARM passed?
         BNP   @PARMCK9                 If not, use default values
         CH    R1,=H'3'                 Minimum length of input data
         BL    @PARMCK9                 If less than (x,y), ignore
         CH    R1,=H'7'                 Maximum length of input data
         BH    @PARMCK9                 If > (xxx,yyy), ignore
*                                       R1 has length of PARM data
         LA    R6,2(R9)                 Point R6 to start of PARM data
         XR    R4,R4                    Count of "x" digits
         XR    R5,R5                    Count of "y" digits
@PARMCK2 EQU   *
         CLI   0(R6),C','               Look for comma in PARM data
         BNE   @PARMCK3
         TM    SWBYTE,S#COMMA           Comma already encountered?
         BO    @PARMCK9                 More than 1 comma, ignore PARM
         OI    SWBYTE,S#COMMA
         B     @PARMCK5
@PARMCK3 EQU   *
         CLI   0(R6),C'0'               PARM data must be numeric
         BL    @PARMCK9                 If not, ignore
         CLI   0(R6),C'9'
         BH    @PARMCK9
         TM    SWBYTE,S#COMMA           Have we seen a comma yet?
         BO    @PARMCK4                 This digit is part of "Y" value
         LA    R4,1(R4)                 Increment count of "x" digits
         B     @PARMCK5
@PARMCK4 EQU   *
         LA    R5,1(R5)                 Increment count of "y" digits
@PARMCK5 EQU   *
         LA    R6,1(R6)
         BCT   R1,@PARMCK2
         LTR   R4,R4                    Any digits for "x" value?
         BNP   @PARMCK9                 If not, don't bother
         LTR   R5,R5                    Anything specified for "y"?
         BNP   @PARMCK9                 If not, just use defaults
         BCTR  R4,0                     lgth code value for "x" digits
         EX    R4,PACK@EM               Pack "x" value into WORKD
         CVB   R1,WORKD
         MH    R1,=H'100'               Convert secs to centi-seconds
         ST    R1,WAIT@X                Modify default value of "x"
         LA    R9,2(R4,R9)              Point to the value right of ","
         BCTR  R5,0                     lgth code value for "y"
         EX    R5,PACK@EM               Pack "y" value into WORKD
         CVB   R1,WORKD
         MH    R1,=H'100'               Convrt secs to centi-seconds
         ST    R1,WAIT@Y                Modify default value of "y"
@PARMCK9 EQU   *
         BR    R11                      Return to main program
PACK@EM  PACK  WORKD,2(*-*,R9)         Pack "x" or "y" value into WORKD
@CKINPT  DS    0H          ---------    Chk rec for good command text
*  At Input:   R1 Points to the 80-byte card image
*  At Output:  R1 still points to the input record
*              R2 holds the length code value (i.e. length minus 1)
*                 of the command test.
*                 (A 0 in R2 at output indicates an unuseable card).
         LA    R2,70(R1)                Point R2 to col-71
         LA    R3,2(R1)                 Point R3 to Col-03
@CKINPT1 EQU   *                   Start leftwards scan for a non-blank
         CR    R2,R3                    R2 at col-03 already?
         BNH   @CKINPT7                 If yes, can't use this record
         CLI   0(R2),C' '
         BH    @CKINPT3                 Last character of text located
         BCT   R2,@CKINPT1              Move R2 1 column to the left
@CKINPT3 EQU   *
         SR    R2,R1                    Get length code in R2
         B     @CKINPT9                   and exit
@CKINPT7 EQU   *
         SR    R2,R2                    Flag this as a bad record
@CKINPT9 EQU   *
         BR    R11
         SPACE 3
@PUTCMD  DS    0H       ----  Fill in Command Buffer, Issue Command
*  At Input:   R1 points to the text of the Console Command
*  At Output:  R2 Holds the length code value of the command text
*  First invocation of this routine will cause the command to be issued
*  after an interval of time indicated by the value in field WAIT@X.
*  Subsequent commands are issued at regular intervals of time
*  controlled by the value in the field WAIT@Y.
         MVI   CMD1TXT,C' '
         MVC   CMD1TXT+1(L'CMD1TXT-1),CMD1TXT
         EX    R2,MOVETXT               Move text to command buffer
         LA    R2,5(R2)                 Add 1 to get length, plus 4
         STC   R2,CNSLCMDL              Store length of command buffer
         LA    R1,WAIT@Y                Assume wait of "y" seconds
         TM    SWBYTE,S#REC01           1st command already issued?
         BO    @PUTCMD5                 If yes, continue
         OI    SWBYTE,S#REC01           No, this is the 1st Cnsl Cmd
         LA    R1,WAIT@X                Wait will be for "x" seconds
@PUTCMD5 EQU   *
         STIMER WAIT,BINTVL=(1)         Wait for "x/y" seconds
         MODESET KEY=ZERO,MODE=SUP      Get in supvsr state for SVC 34
         SR    R0,R0                    R0 must contain zero
         MGCR  CNSLCMD1                 Issue command via SVC 34
         MODESET KEY=NZERO,MODE=PROB    Get back to problem state
@PUTCMD9 EQU   *
         BR    R11
MOVETXT  MVC   CMD1TXT(*-*),0(R1)
         TITLE '         ******    MISCELLANEOUS DATA FIELDS    ******'
CNSLCMD1 DS    0F       <======   Command Buffer for Console Command(s)
         DC    AL1(0)
CNSLCMDL DC    AL1(*-*)                 Text length plus 4
         DC    AL2(0)                   No token present
CMD1TXT  DS    0CL72
         DC    CL72' '  <======   Text of Console Command
WORKD    DS    D
SAVE18F  DC    18F'0'                  This module's SAVE Area
WAIT@X   DC    F'6000'                 6000 units of 0.01 Secs
WAIT@Y   DC    F'200'                  200 hundredths Sec. or 2 Secs
CARDIN   DCB   DSORG=PS,LRECL=80,        /*  Sequential file with  /*  X
               MACRF=(GL),DDNAME=CMDIN,  /*  text of console       /*  X
               EODAD=INPT$END            /*  commands              /*
SWBYTE   DC    X'00'                   1 byte of indicator bits
S#REC01  EQU   X'80'
S#COMMA  EQU   X'40'
         END


B S Oberoi
Manager Technical Services
Allendale Insurance (USA) © B S Oberoi 1990
