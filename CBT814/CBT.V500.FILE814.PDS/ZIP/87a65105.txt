Graphic information with PDF 3.4

PDF's DSLIST function (option 3.4) is very useful for listing
datasets retrieved from a catalog or on a specified volume. The
dataset listed may be processed by selecting line commands (E
for edit, D for delete, I for information, R for rename, Z for
compress etc). We may, in the same way, select any existing
command (eg TSO command, CLIST, module etc) on the list.

The CLIST 'G' shown below can also be used at any ISPF
level, providing that a dataset name is coded after (eg TSO %G
datasetname). Figure 1 shows a DSLIST resulting from the
PDF 3.4 function and the way to invoke the 'G' command.

Figure 2 shows the result of the CLIST 'G', displaying its
information about the processed dataset through a number of
graphs (two graphs for sequential datasets, three graphs for
PDSs).

As can be seen in the example in Figure 2 for the dataset
SYSLAB.EIEIC04.PRINT:

o      Allocated space is 77% of the maximum allocation (the
      maximum allocation is the sum of the primary allocation
      plus fifteen times the secondary allocation).

o      The used/allocated space is 44%.

o      The used/allocated directory blocks is 60%.


CLIST 'G'

PROC 1 DSN
       /* IF EVER DATASET IS NOT FOUND */
IF &SYSDSN(&DSN) NE OK THEN DO
                   WRITE DATASET &SYSLC(NOT FOUND)
                   EXIT
                   END
LISTDSI &DSN  DIRECTORY
IF &LASTCC GT 4 THEN DO
                   WRITE UNABLE TO GRAPH DATASET'S INFORMATION
                   EXIT
                   END
       /* NOW, ALL IS OK */
                         SET &GRPVOL  = &SYSVOLUME
                         SET &GRPUNIT = &SYSUNIT
                         SET &GRPORG  = &SYSDSORG
                         SET &GRPRECFM = &SYSRECFM
                         SET &GRPLRECL = &SYSLRECL
                         SET &GRPBLKSZ = &SYSBLKSIZE
                         SET &GRPKEYLN = &SYSKEYLEN
                         SET &GRPSPUN  = &SYSUNITS
                         SET &GRPPRIM  = &SYSPRIMARY
                         SET &GRPSEC   = &SYSSECONDS
                         SET &GRPXT    = &SYSEXTENTS
                         SET &GRPALL   = &SYSALLOC
                         SET &GRPUSED  = &SYSUSED
                         SET &GRPADIR  = &SYSADIRBLK
                         SET &GRPUDIR  = &SYSUDIRBLK
                         SET &GRPMBRS  = &SYSMEMBERS
                         SET &GRPCRE   = &STR(&SYSCREATE)
                         SET &GRPREF   = &STR(&SYSREFDATE)
                         SET &GRPEXP   = &STR(&SYSEXDATE)
                         SET &GRPPASS  = &SYSPASSWORD
                         SET &GRPRACF  = &SYSRACFA
                         SET &GRPUPDT  = &SYSUPDATED
                         SET &GRPGDIR = &STR( )
                         SET &GRPGALL = &STR( )
                         SET &GRPGUSED = &STR( )
     IF &GRPUDIR = &STR() OR +
                            &GRPADIR = &STR() THEN GOTO GRPSPACE
     IF &GRPUDIR LE &STR(000) OR +
                            &GRPADIR LE &STR(000) THEN GOTO GRPSPACE
                         SET &GRPPCT = &EVAL((&GRPUDIR*100)/&GRPADIR)
                         SET &PD = &GRPPCT
     IF &GRPPCT GT 100 THEN SET &GRPPCT = 100
                         SET &GRPPCT = &EVAL((&GRPPCT*40)/100)
     IF &GRPPCT GT 0 THEN DO
  SET &GRPG1 = &STR(================================================)
             SET &GRPG1 = &SUBSTR(1:&GRPPCT,&GRPG1)
          END
          ELSE SET &GRPG1 = &STR()
  SET &GRPG2 = &STR(------------------------------------------------)
                         SET &GRPG1 = &STR(&GRPG1&GRPG2)
                         SET &GRPGDIR = &SUBSTR(1:40,&GRPG1)
GRPSPACE: +
     IF &GRPUSED = &STR() OR +
                            &GRPALL  = &STR() THEN GOTO DISPINF
     IF &GRPUSED LE &STR(000) OR +
                            &GRPALL  LE &STR(000) THEN GOTO DISPINF
                         SET &GRPPCT = &EVAL((&GRPUSED*100)/&GRPALL)
                         SET &P2 = &GRPPCT
     IF &GRPPCT GT 100 THEN SET & GRPPCT = 100
                         SET &GRPPCT = &EVAL((&GRPPCT*40)/100)
     IF &GRPPCT GT 0 THEN DO
  SET &GRPG1 = &STR(===============================================)
             SET &GRPG1 = &SUBSTR(1:&GRPPCT,&GRPG1)
          END
          ELSE SET &GRPG1 = &STR()
  SET &GRPG2 = &STR(-----------------------------------------------)
                         SET &GRPG1 = &STR(&GRPG1&GRPG2)
                         SET &GRPGUSED = &SUBSTR(1:40,&GRPG1)
     IF &GRPPRIM = &STR() OR +
                            &GRPSEC  = &STR() THEN GOTO DISPINF
                         SET &GRPTOT = &GRPPRIM + (15*&GRPSEC)
     IF &GRPTOT LE &STR(000)  THEN GOTO DISPINF
                         SET &GRPPCT = &EVAL((&GRPALL*100)/&GRPTOT)
                         SET &P1 = &GRPPCT
     IF &GRPPCT GT 100 THEN SET &GRPPCT = 100
                         SET &P3     = &EVAL((&GRPPCT*&P2)/100)
                         SET &P3     = &EVAL((&P3*40)/100)
                         SET &GRPPCT = &EVAL((&GRPPCT*40)/100)
     IF &GRPPCT LT &P3 THEN SET &P3 = 0
                         SET &GRPPCT = &GRPPCT - &P3
     IF &P3 GT 0 THEN DO
  SET &GRPG0 = &STR(************************************************)
             SET &GRPG0 = &SUBSTR(1:&P3,&GRPG0)
          END
          ELSE SET &GRPG0 = &STR()
     IF &GRPPCT GT 0 THEN DO
  SET &GRPG1 = &STR(================================================)
             SET &GRPG1 = &SUBSTR(1:&GRPPCT,&GRPG1)
          END
          ELSE SET &GRPG1 = &STR()
  SET &GRPG2 = &STR(------------------------------------------------)
                         SET &GRPG1 = &STR(&GRPG0&GRPG1&GRPG2)
                         SET &GRPGALL = &SUBSTR(1:40,&GRPG1)
DISPINF:    ISPEXEC DISPLAY PANEL(GRPANEL)
            IF &LASTCC = 0 THEN GOTO DISPINF
            END


GRPANEL PANEL

)ATTR default($+_)
   TYPE(OUTPUT) INTENS(HIGH) CAPS(OFF)
  ¡ TYPE(OUTPUT) INTENS(LOW)  CAPS(OFF)
)BODY
$--------------------------   Datasets INFORMATION   -------------------------
$Command ==>_zcmd                                                            +
+             DSName =dsn
+             VOLUME =grpvol          + Created ............ :grpcre
+               UNIT =grpunit         + Expires ............ :grpexp
+              DSORG =grporg          + Last Ref. date ..... :grpref
+              RECFM =grprecfm        + PASSWORD indication  :grppass
+              LRECL =grplrecl        + RACF indication .... :grpracf
+            BLKSIZE =grpblksz        + Change indicator ... :grpupdt
+         Key Length =grpkeyln
+         Space Unit =grpspun +!   +      Alloc. and 