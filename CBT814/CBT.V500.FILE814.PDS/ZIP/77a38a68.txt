An alias update and display dialog

This article describes a simple method whereby your systems
support staff can update and display alias information from the
master catalog and test master catalog under ISPF. Three
functions are provided, namely:

1      The ability to define an alias

2      The ability to delete an alias

3      The ability to display alias information.

Both the master catalog and the test master catalog names are
hard-coded in the CLISTs and will have to be changed. An
alternative approach would be to input these names into the
prompt panels and to retrieve them through the ISPEXEC GET
service. The CLISTs should be installed in the SYSPROC
concatenation and the panels in the ISPPLIB concatenation. To
invoke this dialog, add 'PANEL(PAL01)' to a primary or menu
panel, or invoke the Dialog Function Option 1 and specify
PANEL=PAL01.


CAL01 CLIST

PROC 0
 /* DOC: THIS CLIST IS USED TO ADD AND DELETE ALIASES.               */
 CONTROL END(ENDO) NOFLUSH
 ISPEXEC VGET (O,ALIAS,RELATE,PT)
 /********************************************************************/
 /*  DELETE ALIAS                                                    */
 /*  MASTER OR TEST MASTER CATALOG                                   */
 /********************************************************************/
 IF &O = D THEN -
  GOTO DELAL
 /********************************************************************/
 /*  DEFINE ALIAS IN MASTER CATALOG                                  */
 /********************************************************************/
 IF &PT = 1 THEN -
  DO
   DEFINE ALIAS(NAME('&ALIAS') RELATE('&RELATE.')) -
   CATALOG('SYS1.MSTRCAT'/MPDW)
 IF &LASTCC = 0 THEN -
  DO
   WRITE  TSO ALIAS(&ALIAS) ALIAS ADDED TO CATALOG
   LISTC CATALOG('SYS1.MSTRCAT'/MPWD) -
         ENT('&ALIAS.') ALIAS ALL
   GOTO ENDALL
  ENDO
 ELSE -
   GOTO ENDALL
  ENDO
 ELSE -
 /********************************************************************/
 /*  DEFINE ALIAS IN TEST MASTER CATALOG                             */
 /********************************************************************/
 IF &PT = 2 THEN -
  DO
   DEFINE ALIAS(NAME('&ALIAS') RELATE('&RELATE.')) -
   CATALOG('SYS1.TMSTRCAT'/MPDW)
 IF &LASTCC = 0 THEN -
  DO
   WRITE TSO ALIAS(&ALIAS) ALIAS ADDED TO CATALOG
   LISTC CATALOG('SYS1.TMSTRCAT'/MPDW) -
         ENT('&ALIAS.') ALIAS ALL
   GOTO ENDALL
  ENDO
 ELSE -
   GOTO ENDALL
  ENDO
 /********************************************************************/
 /*  DEFINE ALIAS IN MASTER AND TEST MASTER CATALOG                  */
 /********************************************************************/
   DEFINE ALIAS(NAME('&ALIAS') RELATE('&RELATE')) -
   CATALOG('SYS1.MSTRCAT'/MPDW)
 IF &LASTCC = 0 THEN -
  DO
   WRITE  TSO ALIAS(&ALIAS) ALIAS ADDED TO CATALOG
   LISTC CATALOG('SYS1.MSTRCAT'/MPDW) -
         ENT('&ALIAS.') ALIAS ALL
  ENDO
 ELSE -
   GOTO ENDALL
   DEFINE ALIAS(NAME('&ALIAS') RELATE('&RELATE.')) -
   CATALOG('SYS1.TMSTRCAT'/MPDW)
 IF &LASTCC = 0 THEN -
  DO
   WRITE TSO ALIAS(&ALIAS) ALIAS ADDED TO CATALOG
   LISTC CATALOG('SYS1.TMSTRCAT'/MPDW) -
         ENT('&ALIAS.') ALIAS ALL
   GOTO ENDALL
  ENDO
 ELSE -
   GOTO ENDALL
DELAL: -
 IF &PT = 1 THEN -
  DO
 /********************************************************************/
 /*  DELETE ALIAS IN MASTER CATALOG                                  */
 /********************************************************************/
   DELETE ('&ALIAS') -
   CATALOG('SYS1.MSTRCAT'/MPDW) ALIAS
 IF &LASTCC = 0 THEN -
  DO
   WRITE  ALIAS(&ALIAS) ALIAS DELETED FROM CATALOG
   GOTO ENDALL
  ENDO
 ELSE -
   GOTO ENDALL
  ENDO
 ELSE -
 /********************************************************************/
 /*  DELETE ALIAS IN TEST MASTER CATALOG                             */
 /********************************************************************/
 IF &PT = 2 THEN -
  DO
   DELETE ('&ALIAS') -
   CATALOG('SYS1.TMSTRCAT'/MPDW) ALIAS
 IF &LASTCC = 0 THEN -
  DO
   WRITE ALIAS(&ALIAS) ALIAS DELETED FROM CATALOG
   GOTO ENDALL
  ENDO
 ELSE -
   GOTO ENDALL
  ENDO
 /********************************************************************/
 /*  DELETE ALIAS IN MASTER AND TEST CATALOG                         */
 /********************************************************************/
   DELETE ('&ALIAS') -
   CATALOG('SYS1.MSTRCAT'/MPDW) ALIAS
 IF &LASTCC = 0 THEN -
  DO
   WRITE  ALIAS(&ALIAS) ALIAS DELETED FROM CATALOG
  ENDO
 ELSE -
   GOTO ENDALL
   DELETE ('&ALIAS') -
   CATALOG('SYS1.TMSTRCAT'/MPDW) ALIAS
 IF &LASTCC = 0 THEN -
  DO
   WRITE ALIAS(&ALIAS) ALIAS DELETED FROM CATALOG
  ENDO
ENDALL: -
   WRITE PROCESSING NOW ENDED....


CAL02 CLIST

PROC 0
 /* DOC: THIS CLIST IS USED TO DISPLAY ALIAS ENTRIES.                */
PROFILE NOPREFIX
 ISPEXEC VGET (VALUE)
LISTC ALIAS NAME ENTRIES(&VALUE) ALL
PROFILE PREFIX(&SYSUID)
END


PAL01 PANEL

)BODY
%------------------------- Alias Utility Main Menu ----------------------------
%OPTION  ===>_ZCMD
%                                                              +G Date:%&ZDATE
%   1 +ALIAS       - Define Or Delete Alias                    +J Date:%&ZJDATE
%   2 +DISPLAY     - Display Alias Information                 +Userid:%&ZUSER
%                                                              +Prefix:%&ZPREFIX
%                                                                +Time:%&ZTIME
%                                                                +Term:%&ZTERM
%                                                                +Keys:%&ZKEYS
%
%
%
%
%
+Enter%END+command to terminate.
)PROC
  &ZSEL = TRANS( TRUNC (&ZCMD,'.')
            /*******************************************************/
            /*     Define/Delete Alias Pointers                    */
            /*******************************************************/
                1,'PANEL(PAL02)'
            /*******************************************************/
            /*     Display Alias                                   */
            /*******************************************************/
                2,'PANEL(PAL03)'
              ' ',' '
                *,'?' )
)END


PAL02 PANEL

)ATTR
 _ TYPE(INPUT) JUST(LEFT)
 % TYPE(TEXT)  INTENS(HIGH) SKIP(ON)
)BODY
%-------------------------- Define Or Delete Aliases --------------------------
%COMMAND ===>_ZCMD
%
%
%
%   Option:     ====>_O%                         -  C=Create D=Delete
%   Alias:      ====>_ALIAS   %                  -  Alias Name
%   Relate To:  ====>_RELATE                 %   -  Relate to (only for create)
%   Catalog:    ====>_PT%                        -  P = Master Catalog
%                                                -  T = Test Master Catalog
%                                                -  Enter P or T, or PT
%
%
+Enter%END+command to terminate.
%
)INIT
  .CURSOR  = O
  &O       = 'C'
  &PT      = 'PT'
  &ALIAS   = ''
  &ZCMD    = ''
  &RELATE  = 'SYS1.TSOCATLG'
)PROC
  VER (&O,NB)
  VER (&ALIAS,NB)
  VER (&PT,NB)
  VER (&O,LIST,C,D)
  IF (&O = 'C')
     VER (&RELATE,NB)
  VER (&PT,ALPHA)
  IF (&PT = 'P')
     &PT  =  1
  IF (&PT = 'T')
     &PT  =  2
  IF (&PT = 'PT')
     &PT  =  3
  VER (&PT,NUM)
  &ZCMD = 1
  &ZSEL = TRANS( TRUNC(&ZCMD,'.')
                1,'CMD(%CAL01)'
                ' ',' '
                 *,'?' )
)END


PAL03 PANEL

)BODY
%-------------------------- Alias Display Panel -------------------------------
%COMMAND ===> _ZCMD                                                    +       +
+
+
+  Enter%Alias+To Be Displayed:         %====>_VALUE   +
+
%
+Enter%END+command to terminate.
%
)INIT
   .CURSOR = VALUE
)PROC
   VER (&VALUE,NONBLANK)
   VPUT (VALUE) SHARED
   &ZSEL = 'CMD(CAL02)'
)END


R F Perretta
Systems Consultant (UK)                                    c Xephon 1992

