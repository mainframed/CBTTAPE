Benchmarking using event timings
It is often useful to determine the actual speed at which functions are
performed, both by the operating system and application programs.  One way to do
this is to perform timing benchmarks, usually with a stand-alone system.  The
problem with this method is that stand-alone testing does not reasonably compare
to the real world of CPU and I/O contention.  I have found it useful to measure
timings of events both on stand-alone systems and during normal system
contention times to give me a good representation of how programs will perform.
To this end, the program below was written to perform event timings and provide
an easy mechanism to which others can add routines for the purpose of
benchmarking.
The TIMER program basically calculates the time taken for a particular event by
storing the current time, branching to the routine that performs the event,
returning to get the current time, and then calculating and printing the
difference as the event time.  For precision, the event timings are in
microseconds.  I also added a feature to make the program non-swappable and
change its dispatching priority to a high value to see the difference between
running as a regular batch job versus a very high priority job.  The value at
label PRIORITY controls this and should be set appropriately.  As the program is
currently written, using a priority of 255 should not cause any performance
problems, even with the 15 million instruction loop.  If any other routines are
added, changes in the priority should be considered.  To enable the program to
change its priority and make itself non-swappable, all that needs to be done is
to code any value in the EXEC statement PARM keyword.   In order to use this
feature, the program must be run from an authorised library.
One interesting feature is the way the program changes its dispatching priority.
This is done in the DONTSWAP routine by building a parameter list and branch
entering the ASCBCHAP routine pointed to by the CVTVEAC0 field of the CVT.
Another nice feature is the ability to test the differences in using VIO and
real DASD when doing I/O, as well as the ability to vary the file blocking and
buffering.  Some sample JCL to invoke the program is shown below.  Symbolic
variables are supplied to alter DCB attributes and buffering.  You can
dramatically demonstrate the difference that blocking and buffering makes by
making a few runs of the program varying these values.  A sample output listing
from the program is given.  Note that the heading line contains a CPU version
code, the values of which are shown for both 3090 and 3081 CPUs.  For other
machines, consult the appropriate CPU functional characteristics manual.
TIMER SOURCE CODE
         PUNCH ' SETCODE AC(1)'
         MACRO
&LABEL   CLOCK &EVENT,&NAME
&LABEL   STCK  BEFORE                  GET TIME BEFORE INVOCATION
         BAL   R10,&EVENT              BRANCH TO PERFORM EVENT
         STCK  AFTER                   GET TIME AFTER INVOCATION
         MVC   OUTEVENT,=CL40&NAME     MOVE EVENT NAME
         BAL   R10,CALCTIME            BRANCH TO CALCULATE TIME DIFF.
         MEND
*       THIS ROUTINE DELIBERATELY DOES THINGS THAT SHOULD              *
*       TAKE TIME AND MEASURES AND PRINTS OUT THAT TIME.               *
TIMER    CSECT
         SAVE  (14,12),,TIMER-&SYSDATE
         LR    R11,R15                 LOAD R11 AS BASE REGISTER
         USING TIMER,R11               ESTABLISH ADDRESSABILITY
         LA    R12,2048(R11)           LOAD R12 AS SECOND BASE
         LA    R12,2048(R12)           LOAD R12 AS SECOND BASE
         USING TIMER+4096,R12          ESTABLISH ADDRESSABILITY
         LA    R8,SAVEAREA             LOAD ADDR OF MY S/A
         ST    R8,8(R13)               ST MY S/A ADDR IN CALLERS S/A
         ST    R13,4(R8)               ST CALLERS S/A ADDR IN MY S/A
         LR    R13,R8                  LOAD ADDR OF MY S/A IN R13
         LR    R2,R1                   SAVE PARM ADDRESS
         CLOCK OPENSYS,'** SYSPRINT OPEN FOR OUTPUT **' OPEN SYSPRINT
         L     R6,CVTPTR               LOAD R6 W/PTR TO CVT
         S     R6,=F'256'              POINT TO CVT PREFIX
         USING CVTFIX,R6               ESTABLISH ADDRESSABILITY
         TM    CVTDCB,CVTMVSE          IS MVS/XA RUNNING
         BO    SHOWXA                  YES, PUT IT IN OUTPUT
         MVC   HDRLINE+24(3),=CL3'370' ELSE SHOW 370
         B     GETREL                  GO GET RELEASE NUMBER
SHOWXA   MVC   HDRLINE+24(3),=CL3'XA ' SHOW XA
GETREL   MVC   HDRLINE+36(2),CVTNUMB   MOVE RELEASE
         MVC   HDRLINE+39(2),CVTLEVL   MOVE VERSION
         MVC   HDRLINE+53(16),CVTPROD  MOVE SCP PRODUCT NAME/FMID
         L     R6,CVTSMCA              LOAD R6 W/A(SMCA)
         USING SMCABASE,R6             ESTABLISH ADDRESSABILITY
         MVC   HDRLINE+15(4),SMCASID   MOVE SMF ID
         MODESET KEY=ZERO,MODE=SUP     GET INTO KEY 0/SUPERVISOR STATE
         STIDP SERIAL                  GET CPU SERIAL NUMBER
         MODESET KEY=NZERO,MODE=PROB   GET BACK TO PROBLEM STATE
         MVC   CPUTYPE(2),SERIAL+4     MOVE CPU TYPE
         UNPK  CPUTYPE(5),CPUTYPE(3)   UNPACK CPU TYPE INTO DISPLAY
         MVC   HDRLINE+80(4),CPUTYPE   MOVE CPU TYPE
         MVC   CPUTYPE(1),SERIAL       MOVE CPU VERSION
         UNPK  CPUTYPE(3),CPUTYPE(2)   UNPACK CPU VERSION INTO DISPLAY
         MVC   HDRLINE+90(2),CPUTYPE   MOVE CPU VERSION
         CLOCK PUTHDR,'PUT OUT HEADING INFORMATION LINE'
         B     TIMERTN                 GO DO REST OF TIMINGS
RETURN   CLOSE SYSPRINT                CLOSE SYSPRINT
         L     R13,SAVEAREA+4          LOAD R13 W A/(CALLER SAVEAREA)
         RETURN (14,12),RC=0           RETURN TO CALLER
***      START OF CLOCKING INVOCATIONS
TIMERTN  CLOCK WTOSTRT,'ISSUE WRITE TO OPERATOR START'
         NI    0(R2),X'7F'             TURN OFF HI-ORDER BIT
         L     R1,0(R2)                LOAD R1 W/ADDR(PARMLIST)
         LH    R2,0(R1)                LOAD R2 W/LENGTH OF PARMLIST
         C     R2,=F'0'                WAS THERE ANY PARM
         BH    ARNDSWAP                YES, NO DONTSWAP OR ASCBCHAP
         CLOCK DONTSWAP,'ISSUE SYSEVENT DONTSWAP AND ASCBCHAP'
ARNDSWAP CLOCK ENQSYS,'ENQ MACRO SCOPE=SYSTEM'
         CLOCK DEQSYS,'DEQ MACRO SCOPE=SYSTEM'
         CLOCK ENQSYSS,'ENQ MACRO SCOPE=SYSTEMS (GRS OVERHEAD)'
         CLOCK DEQSYSS,'DEQ MACRO SCOPE=SYSTEMS (GRS OVERHEAD)'
         CLOCK NOTHING,'NOOP BRANCH REGISTER (BR10)'
         CLOCK TIMEDEC,'TIME SVC IN DECIMAL'
         CLOCK TIMEBIN,'TIME SVC IN BINARY '
         CLOCK TIMETUN,'TIME SVC IN TIMER UNITS'
         CLOCK LONGAP,'LONG ADD PACK INSTRUCTION'
         CLOCK RACSTATS,'CHECK RACF STATUS IN SYSTEM'
         CLOCK NOTHING,'NOOP BRANCH REGISTER (BR10)'
         CLOCK BLDLBR14,'BLDL MACRO FOR IEFBR14'
         CLOCK LOADBR1B,'LOAD MACRO FOR IEFBR14 BY BLDL LIST'
         CLOCK DELTBR1B,'DELETE MACRO FOR IEFBR14 BY BLDL LIST'
         CLOCK LOADBR14,'LOAD MACRO FOR IEFBR14 BY NAME'
         CLOCK DELTBR14,'DELETE MACRO FOR IEFBR14 BY NAME'
         CLOCK LINKBR14,'LINK MACRO TO IEFBR14 BY NAME'
         CLOCK GET4KR,'GETMAIN 4K SUBPOOL=0 R-FORM'
         CLOCK FREE4KR,'FREEMAIN 4K SUBPOOL=0 R-FORM'
         CLOCK GET4KRU,'GETMAIN 4K SUBPOOL=0 RU-FORM'
         CLOCK FREE4KRU,'FREEMAIN 4K SUBPOOL=0 RU-FORM'
         CLOCK ALRLOOP,'15 MILLION ALR INSTRUCTIONS'
         CLOCK MVCLONG,'ONE MVCL OF 4096 BYTES'
         CLOCK MVCX16,'SIXTEEN MVCS OF 256 BYTES'
         CLOCK MVCX16,'SIXTEEN MVCS OF 256 BYTES'
         CLOCK MVCX16,'SIXTEEN MVCS OF 256 BYTES'
         CLOCK MVCX16,'SIXTEEN MVCS OF 256 BYTES'
         CLOCK MVCX16,'SIXTEEN MVCS OF 256 BYTES'
         CLOCK MVCX16,'SIXTEEN MVCS OF 256 BYTES'
         CLOCK MVCX16,'SIXTEEN MVCS OF 256 BYTES'
         CLOCK MVCX16,'SIXTEEN MVCS OF 256 BYTES'
         CLOCK MVCLONG,'ONE MVCL OF 4096 BYTES'
         CLOCK ECBWAIT,'WAIT ON POSTED ECB'
         CLOCK VIOOPENO,'VIO OPEN FOR OUTPUT'
         LA    R2,50
PUTLOOP  CLOCK VIOPUT,'VIO PUT'
         BCT   R2,PUTLOOP
         CLOCK VIOCLOSE,'VIO CLOSE'
         CLOCK VIOOPENI,'VIO OPEN FOR INPUT'
         LA    R2,50
GETLOOP  CLOCK VIOGET,'VIO GET'
         BCT   R2,GETLOOP
         CLOCK VIOCLOSE,'VIO CLOSE'
         CLOCK DAOPENO,'DASD OPEN FOR OUTPUT'
         LA    R2,50
PUTLOOPD CLOCK DAPUT,'DASD PUT'
         BCT   R2,PUTLOOPD
         CLOCK DACLOSE,'DASD CLOSE'
         CLOCK DAOPENI,'DASD OPEN FOR INPUT'
         LA    R2,50
GETLOOPD CLOCK DAGET,'DASD GET'
         BCT   R2,GETLOOPD
         CLOCK DACLOSE,'DASD CLOSE'
         CLOCK WTOEND,'ISSUE WRITE TO OPERATOR END'
         B     RETURN
***      END OF CLOCKING INVOCATIONS
***      START OF TIME DIFFERENCE CALCULATION ROUTINE
CALCTIME LM    R6,R7,BEFORE            LOAD BEFORE TIME
         SRDL  R6,12                   IGNORE LESS THAN MICROSECOND
         LM    R8,R9,AFTER             LOAD AFTER TIME
         SRDL  R8,12                   IGNORE LESS THAN MICROSECOND
         SLR   R9,R7                   COMPUTE THE DIFFERENCE
         CVD   R9,DOUBLE               CONVERT TO DECIMAL
         OI    DOUBLE+7,X'0F'          MAKE SIGN PRINTABLE
         UNPK  OUTLINE+1(9),DOUBLE+3(5) UNPACK TO OUTPUT
         LA    R6,OUTLINE+1            LOAD R6 W/A(OF DISPLAY TIME)
         LA    R7,8                    LOAD R7 W/LOOP COUNT
BLNKLOOP CLI   0(R6),C'0'              IS THIS POSITION A ZERO
         BNE   EBB                     NO, SIGNIF DIGIT THEN DONE
         MVI   0(R6),C' '              ELSE MAKE LEADING ZERO A BLANK
         LA    R6,1(R6)                POINT TO NEXT POSITION
         BCT   R7,BLNKLOOP             LOOP BACK TO CHECK NEXT POSITION
EBB      PUT   SYSPRINT,OUTLINE        WRITE LINE
         BR    R10                     RETURN
***      END OF TIME DIFFERENCE CALCULATION ROUTINE
***      START OF CLOCKED ROUTINES
PUTHDR   PUT   SYSPRINT,HDRLINE
         BR    R10
WTOSTRT  WTO   'BEGIN TIMER ROUTINE'
         BR    R10
DONTSWAP SYSEVENT DONTSWAP             MAKE MYSELF NON-SWAPPABLE
         USING PSA,R0                  ESTABLISH ADDRESSABILITY
         L     R2,PSAAOLD              LOAD R2 W/A(MY ASCB)
         ST    R2,ASCBADDR             STORE IN PARM LIST
         USING ASCB,R2                 ESTABLISH ADDRESSABILITY
         L     R1,=A(IN31BIT+X'80000000') LOAD R1 W/31 BIT ADDRESS
         BSM   R0,R1                   BRANCH IN 31 BIT MODE
IN31BIT  L     R2,ASCBOUCB             LOAD R2 W/A(MY OUCB)
         USING OUCB,R2                 ESTABLISH ADDRESSABILITY
         MODESET KEY=ZERO,MODE=SUP     GET INTO KEY 0/SUPERVISOR STATE
         NI    OUCBAFL,255-OUCBAPG     TURN OFF APG FLAG BIT
         SR    R0,R0                   CLEAR R0
         LA    R1,PARMS                LOAD R1 W/A(ASCBCHAP PARMS)
         STM   R0,R15,SAVEREGS         SAVE REGISTERS
         LA    R13,CHAPSAVE            POINT TO NEW SAVE AREA
         L     R15,CVTPTR              LOAD R15 W/A(CVT)
         L     R15,CVTVEAC0-CVTMAP(R15) LOAD R15 W/A(ASCBCHAP ROUTINE)
         BALR  R14,R15                 BRANCH TO ROUTINE
         LM    R0,R15,SAVEREGS         RELOAD REGISTERS
         LA    R1,IN24BIT              LOAD R1 W/24 BIT ADDRESS
         BSM   R0,R1                   BRANCH IN 24 BIT MODE
IN24BIT  MODESET KEY=NZERO,MODE=PROB   GET BACK TO PROBLEM STATE
         BR    R10
SAVEREGS DC    16F'0'                  SAVE AREA BEFORE ASCBCHAP
CHAPSAVE DC    18F'0'                  SAVE AREA FOR ASCBCHAP
PARMS    DC    X'8000' --------|
PRIORITY DC    H'253'      <===|====== SET APPROPRIATE HIGH PRIORITY
ASCBADDR DC    F'0'    --------|
WTOEND   WTO   'END TIMER ROUTINE'
         BR    R10
ENQSYS   ENQ   (QNAME,RNAME,E,,SYSTEM)
         BR    R10
QNAME    DC    CL8'CLOCK'
RNAME    DC    C'TEST'
DEQSYS   DEQ   (QNAME,RNAME,,SYSTEM)
         BR    R10
ENQSYSS  ENQ   (QNAME,RNAME,E,,SYSTEMS)
         BR    R10
DEQSYSS  DEQ   (QNAME,RNAME,,SYSTEMS)
         BR    R10
RACSTATS RACSTAT
         BR    R10
OPENSYS  OPEN  (SYSPRINT,OUTPUT)
         BR    R10
VIOOPENO OPEN  (SYSUTVIO,OUTPUT)
         BR    R10
VIOCLOSE CLOSE SYSUTVIO
         BR    R10
VIOOPENI OPEN  (SYSUTVIO,INPUT)
         BR    R10
VIOPUT   PUT   SYSUTVIO
         BR    R10
VIOGET   GET   SYSUTVIO
         BR    R10
DAOPENO  OPEN  (SYSUTDA,OUTPUT)
         BR    R10
DACLOSE  CLOSE SYSUTDA
         BR    R10
DAOPENI  OPEN  (SYSUTDA,INPUT)
         BR    R10
DAPUT    PUT   SYSUTDA
         BR    R10
DAGET    GET   SYSUTDA
         BR    R10
MVCX16   L     R6,=A(TOAREA)
         L     R7,=A(FROMAREA)
         MVC   0000(256,R7),0000(R6)  1
         MVC   0256(256,R7),0256(R6)  2
         MVC   0512(256,R7),0512(R6)  3
         MVC   0768(256,R7),0768(R6)  4
         MVC   1024(256,R7),1024(R6)  5
         MVC   1280(256,R7),1280(R6)  6
         MVC   1536(256,R7),1536(R6)  7
         MVC   1792(256,R7),1792(R6)  8
         MVC   2048(256,R7),2048(R6)  9
         MVC   2304(256,R7),2304(R6) 10
         MVC   2560(256,R7),2560(R6) 11
         MVC   2816(256,R7),2816(R6) 12
         MVC   3072(256,R7),3072(R6) 13
         MVC   3328(256,R7),3328(R6) 14
         MVC   3584(256,R7),3584(R6) 15
         MVC   3840(256,R7),3840(R6) 16
         BR    R10
MVCLONG  L     R6,=A(TOAREA)
         LA    R7,=A(FROMLEN)
         L     R8,=A(FROMAREA)
         LA    R9,=A(FROMLEN)
         MVCL  R6,R8
         BR    R10
BLDLBR14 MVC   PDS2NAME,=CL8'IEFBR14'
         BLDL  0,BLDLLIST
         BR    R10
BLDLLIST DC    H'1',H'60'
         IHAPDS DSECT=NO,PDSBLDL=YES
LOADBR1B LOAD  DE=PDS2NAME
         BR    R10
DELTBR1B DELETE DE=PDS2NAME
         BR    R10
LOADBR14 LOAD  EP=IEFBR14
         BR    R10
DELTBR14 DELETE EP=IEFBR14
         BR    R10
LINKBR14 LINK  EP=IEFBR14
         BR    R10
GET4KR   GETMAIN R,LV=GMLEN            OBTAIN A PAGE OF STORAGE.
         ST    R1,GMSAVE               SAVE GETMAINED ADDRESS
         BR    R10
GMSAVE   DC    F'0'                    SAVE AREA FOR GETMAINED ADDRESS
GMLEN    EQU   4096                    TO ASK FOR ONE PAGE FROM SP 0.
FREE4KR  L     R1,GMSAVE               POINT TO AREA TO BE FREED
         FREEMAIN R,LV=GMLEN,A=(1)
         BR    R10
GET4KRU  GETMAIN RU,LV=GMLEN           OBTAIN A PAGE OF STORAGE.
         ST    R1,GMSAVE               SAVE GETMAINED ADDRESS
         BR    R10
FREE4KRU L     R1,GMSAVE               POINT TO AREA TO BE FREED
         FREEMAIN RU,LV=GMLEN,A=(1)
         BR    R10
TIMEDEC  TIME  DEC
         BR    R10
TIMEBIN  TIME  BIN
         BR    R10
TIMETUN  TIME  TU
         BR    R10
NOTHING  BR    R10
LONGAP   AP    COUNT,INCR
         BR    R10
COUNT    DC    PL8'0'
INCR     DC    PL2'1'
ECBWAIT  WAIT  ECB=ECB
         BR    R10
         DS    0F
ECB      DC    X'80000000'
ALRLOOP  L     R6,=A(1000000)
ALR1     ALR   R0,R0 1
         ALR   R0,R0 2
         ALR   R0,R0 3
         ALR   R0,R0 4
         ALR   R0,R0 5
         ALR   R0,R0 6
         ALR   R0,R0 7
         ALR   R0,R0 8
         ALR   R0,R0 9
         ALR   R0,R0 10
         ALR   R0,R0 11
         ALR   R0,R0 12
         ALR   R0,R0 13
         ALR   R0,R0 14
         BCT   R6,ALR1    15
         BR    R10
***      END OF CLOCKED ROUTINES
SYSUTDA  DCB   DDNAME=SYSUTDA,DSORG=PS,                                X
               MACRF=(PL,GL)
SYSUTVIO DCB   DDNAME=SYSUTVIO,DSORG=PS,                               X
               MACRF=(PL,GL)
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,RECFM=FBA,                     X
               LRECL=133,BLKSIZE=6118,MACRF=PM
HDRLINE  DC    CL133' RUN ON SMF ID=XXXX MVS/XXX RELEASE=XX.XX PROD LEVX
               EL=XXXXXXXXXXXXXXXX CPU MODEL=XXXX VERS=XX     '
OUTLINE  DS    0CL133
OUTPFX   DC    CL28' 999999999 MICROSECONDS FOR '
OUTEVENT DC    CL40' '             EVENT NAME
         DC    CL65' '             FILLER
SAVEAREA DC    18F'0'                  STANDARD SAVE AREA
DOUBLE   DC    D'0'
BEFORE   DC    D'0'
AFTER    DC    D'0'
SERIAL   DS    D                       CPU SERIAL NUMBER
CPUTYPE  DS    D                       FOR UNPACK OF CPU TYPE/VERSION
         LTORG
         DS    0D
FROMAREA DC    16CL256'FROMAREAFROMAREA'
FROMLEN  EQU   *-FROMAREA
         DS    0D
TOAREA   DS    CL(FROMLEN)
         IHAPSA
         IHAASCB
         IRAOUCB
         CVT   DSECT=YES,PREFIX=YES
         IEESMCA
         END
SAMPLE JCL
//TIMER    PROC DARF=FB,DALR=80,DABL=6160,DABUF=5,DAUNIT=SYSDA,
//         VIORF=FB,VIOLR=80,VIOBL=6160,VIOBUF=5,VIOUNIT=VIO,PRM=
//TIMER    EXEC PGM=TIMERX,PARM=&PRM
//STEPLIB  DD DISP=SHR,DSN=your.authorised.library
//SYSUTVIO DD DISP=(,DELETE),SPACE=(CYL,(1,1)),UNIT=&VIOUNIT,
//         DCB=(RECFM=&VIORF,LRECL=&VIOLR,BLKSIZE=&VIOBL,BUFNO=&VIOBUF)
//SYSUTDA  DD DISP=(,DELETE),SPACE=(CYL,(1,1)),UNIT=&DAUNIT,
//         DCB=(RECFM=&DARF,LRECL=&DALR,BLKSIZE=&DABL,BUFNO=&DABUF)
//SYSPRINT DD SYSOUT=*
//SYSUDUMP DD SYSOUT=*
//         PEND
//TIMER1L  EXEC TIMER,VIOBL=800,DABL=800,PRM=X LOW PRIORITY
//TIMER2H  EXEC TIMER,VIOBL=800,DABL=800
//TIMER3L  EXEC TIMER,VIOBL=1600,DABL=1600,PRM=X LOW PRIORITY
//TIMER4H  EXEC TIMER,VIOBL=1600,DABL=1600
//TIMER5L  EXEC TIMER,VIOBL=3120,DABL=3120,PRM=X LOW PRIORITY
//TIMER6H  EXEC TIMER,VIOBL=3120,DABL=3120
SAMPLE EDITED PROGRAM OUTPUT
   209891 MICROSECONDS FOR ** SYSPRINT OPEN FOR OUTPUT **
RUN ON SMF ID=333B MVS/XA RELEASE=03.8 PROD LEVEL=SP2.1.7 JBB2217
                  CPUMODEL=3090 VERS=10
       11 MICROSECONDS FOR PUT OUT HEADING INFORMATION LINE
      842 MICROSECONDS FOR ISSUE WRITE TO OPERATOR START
      147 MICROSECONDS FOR ISSUE SYSEVENT DONTSWAP AND ASCBCHAP
      982 MICROSECONDS FOR ENQ MACRO SCOPE=SYSTEM
       50 MICROSECONDS FOR DEQ MACRO SCOPE=SYSTEM
      106 MICROSECONDS FOR ENQ MACRO SCOPE=SYSTEMS (GRS OVERHEAD)
       94 MICROSECONDS FOR DEQ MACRO SCOPE=SYSTEMS (GRS OVERHEAD)
        1 MICROSECONDS FOR NOOP BRANCH REGISTER (BR10)
       29 MICROSECONDS FOR TIME SVC IN DECIMAL
       19 MICROSECONDS FOR TIME SVC IN BINARY
       18 MICROSECONDS FOR TIME SVC IN TIMER UNITS
        1 MICROSECONDS FOR LONG ADD PACK INSTRUCTION
        4 MICROSECONDS FOR CHECK RACF STATUS IN SYSTEM
        1 MICROSECONDS FOR NOOP BRANCH REGISTER (BR10)
    28648 MICROSECONDS FOR BLDL MACRO FOR IEFBR14
      710 MICROSECONDS FOR LOAD MACRO FOR IEFBR14 BY BLDL LIST
       54 MICROSECONDS FOR DELETE MACRO FOR IEFBR14 BY BLDL LIST
    16134 MICROSECONDS FOR LOAD MACRO FOR IEFBR14 BY NAME
       57 MICROSECONDS FOR DELETE MACRO FOR IEFBR14 BY NAME
    49680 MICROSECONDS FOR LINK MACRO TO IEFBR14 BY NAME
      107 MICROSECONDS FOR GETMAIN 4K SUBPOOL=0 R-FORM
       67 MICROSECONDS FOR FREEMAIN 4K SUBPOOL=0 R-FORM
       68 MICROSECONDS FOR GETMAIN 4K SUBPOOL=0 RU-FORM
       47 MICROSECONDS FOR FREEMAIN 4K SUBPOOL=0 RU-FORM
   337484 MICROSECONDS FOR 15 MILLION ALR INSTRUCTIONS
        2 MICROSECONDS FOR ONE MVCL OF 4096 BYTES
       63 MICROSECONDS FOR SIXTEEN MVCS OF 256 BYTES
       20 MICROSECONDS FOR SIXTEEN MVCS OF 256 BYTES
       21 MICROSECONDS FOR SIXTEEN MVCS OF 256 BYTES
       21 MICROSECONDS FOR SIXTEEN MVCS OF 256 BYTES
       21 MICROSECONDS FOR SIXTEEN MVCS OF 256 BYTES
       21 MICROSECONDS FOR SIXTEEN MVCS OF 256 BYTES
       21 MICROSECONDS FOR SIXTEEN MVCS OF 256 BYTES
       21 MICROSECONDS FOR SICTEEN MVCS OF 256 BYTES
        1 MICROSECONDS FOR ONE MVCL OF 4096 BYTES
       16 MICROSECONDS FOR WAIT ON POSTED ECB
    27209 MICROSECONDS FOR VIO OPEN FOR OUTPUT
        2 MICROSECONDS FOR VIO PUT
          .
          .
        1 MICROSECONDS FOR VIO PUT
     4786 MICROSECONDS FOR VIO CLOSE
   172443 MICROSECONDS FOR VIO OPEN FOR INPUT
       14 MICROSECONDS FOR VIO GET
          .
          .
        1 MICROSECONDS FOR VIO GET
     2713 MICROSECONDS FOR VIO CLOSE
    83948 MICROSECONDS FOR DASD OPEN FOR OUTPUT
        3 MICROSECONDS FOR DASD PUT
          .
          .
        2 MICROSECONDS FOR DASD PUT
    89865 MICROSECONDS FOR DASD CLOSE
   155946 MICROSECONDS FOR DASD OPEN FOR INPUT
       14 MICROSECONDS FOR DASD GET
          .
          .
        2 MICROSECONDS FOR DASD GET
    15245 MICROSECONDS FOR DASD CLOSE
      837 MICROSECONDS FOR ISSUE WRITE TO OPERATOR END
CPU VERSION CODES
                    3090                       3081
      150            05            D            03
      180            10            G            13
      200            20            K            23
      400            40            G/PERF       53
      120E           03            K/PERF       63
      150E           06            GX           93
      180E           11            KX           A3
      200E           21
      280E           09
      300E           31
      400E           41
      500E           51
      600E           61
      120S           01
      150S           14
      170S           15
      180S           12
      200S           22
      300S           32
      280S           08
      400S           42
      500S           50
      600S           62


Bruce Bordonaro
Systems Software Manager (USA)     © Bruce Bordonaro 1989

























































