Dynamically updating the EDT (continued)

We conclude this article with the publication of the remaining
source code for the program ALTEREDT.

ALTEREDT PROGRAM SOURCE (CONTINUED)

UPDATE   EQU   *
*   THE EDT HAS BEEN VERIFIED AND IS OF A PROPER FORMAT.  CONFIRMATION
*   TO CONTINUE WITH THE UPDATE HAS BEEN RECEIVED SO LET'S DO IT.
         MODESET KEY=ZERO,MODE=SUP   GET AUTHORIZED
         ST    R3,JESEDT-JESCT(,R4)  PUT IN NEW EDT ADDRESS
         MODESET KEY=NZERO,MODE=PROB GET BACK TO NORMAL
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG01),EDTMSG01 MOVE IN MESSAGE
         PUT   OUTPUT,OUTREC         PRINT MESSAGE
         MVC   WTOWORK1(WTOLEN1),WTOLIST1 MOVE IN WTO TEMPLATE
         MVC   WTOWORK1+4(L'EDTMSG01),OUTREC MOVE IN MESSAGE
         WTO   MF=(E,WTOWORK1)       SEND WTO
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG00),EDTMSG00 MOVE IN MESSAGE
         MVC   OUTREC+26(2),EDTMEM+6 MOVE IN SUFFIX
         PUT   OUTPUT,OUTREC         PRINT MESSAGE
         MVC   WTOWORK1(WTOLEN1),WTOLIST1 MOVE IN WTO TEMPLATE
         MVC   WTOWORK1+4(L'EDTMSG00),OUTREC MOVE IN MESSAGE
         WTO   MF=(E,WTOWORK1)       SEND WTO
*   ESTABLISH THE CONSOLE COMMUNICATION ENVIRONMENT.
         LA    R5,ANSRAREA           ADDR OF RESPONSE AREA FOR QEDIT
         EXTRACT (5),FIELDS=COMM     GET ADDR OF COMMUNICATION AREA
         L     R5,ANSRAREA           LOAD ADDR OF COMMUNICATION AREA
         USING COMLIST,R5
         L     R3,COMCIBPT           GET ADDRESS OF CIB
         USING CIBNEXT,R3
         C     R3,=F'0'              CIB EXISTS?
         BE    SETCOUNT              NO - GO SET COUNT
         QEDIT ORIGIN=COMCIBPT,BLOCK=(3) YES - FREE IT
         LTR   R15,R15               GO O.K.?
         BZ    SETCOUNT              YES - GO SET COUNT
REPEAT   XC    ECBAREA(4),ECBAREA    CLEAR ECB
         LA    R8,ECBAREA            GET ECB ADDRESS
         MVI   WTOREPLY,C' '         PRIME REPLY AREA
         LA    R9,WTOREPLY           GET REPLY AREA ADDRESS
         WTOR  'EDTUPD50 - ERROR CONDITION RECOGNIZED IN CONSOLE INTERFX
               ACE.  PROGRAM IS TERMINATING.  SHOULD UPDATED EDT BE RETX
               AINED (Y/N)?',                                          X
               (R9),1,(R8),ROUTCDE=(1)
         WAIT  ECB=ECBAREA           WAIT FOR REPLY
         OI    0(R9),X'40'           SET TO UPPER CASE
         CLI   0(R9),C'Y'            LEAVE NEW EDT?
         BE    NODEL                 YES - DON'T DELETE
         CLI   0(R9),C'N'            FREE THE EDT?
         BE    LOADOLD               YES - FREE AND END
         B     REPEAT                RE-ISSUE MESSAGE
SETCOUNT EQU   *
*   SET LIMIT ON MODIFY COMMANDS
         QEDIT ORIGIN=COMCIBPT,CIBCTR=1  ONE MODIFY AT A TIME
         TM    FLAG+1,CONINIT        CONSOLE ENVIRONMENT INITIALIZED?
         BO    WAIT                  YES - THEN JUST GO WAIT
         WTO   'EDTUPD07 - ALTEREDT CONSOLE INTERFACE ENABLED.  WAITINGX
                FOR FURTHER REQUESTS.',ROUTCDE=(1),DESC=(6)
         OI    FLAG+1,CONINIT    SET THE CONSOLE INTERFACE ENABLED FLAG
*   WAIT FOR ANY OPERATOR REQUESTS.
WAIT     L     R8,COMECBPT           ADDRESS OF COMMUNICATION ECB
         WAIT  1,ECB=(8)             WAIT FOR EVENT
         BAL   R6,CMDPROC            PROCESS COMMAND
         B     WAIT                  GO WAIT
*   OUR TASK HAS RECEIVED A REQUEST FROM THE OPERATOR.  DETERMINE
*   THE TYPE OF REQUEST AND ACT ACCORDINGLY.
CMDPROC  EQU   *
         L     R3,COMCIBPT           GET ADDRESS OF CIB
         LTR   R3,R3                 VALID POINTER?
         BZ    RETR6                 NO - RETURN
         CLI   CIBVERB,CIBMODFY      IS IT A MODIFY COMMAND ?
         BE    CMODIFY               YES - GO PROCESS
         CLI   CIBVERB,CIBSTOP       IS IT A STOP COMMAND ?
         BE    CSTOP                 YES - GO PROCESS
RETR6    EQU   *
         QEDIT ORIGIN=COMCIBPT,BLOCK=(3) FREE CIB
         BR    R6                    RETURN
CMODIFY  EQU  *
*   THIS IS A MODIFY COMMAND SO WE MUST CHECK FOR VALID SYNTAX.
         LH    R7,CIBDATLN           GET COMMAND LENGTH
         CLC   CIBDATA(9),=C'LOAD,EDT=' VALID COMMAND?
         BE    GETCMD                YES - GO GET IT
CMDERR01 EQU   *
         WTO   'EDTUPD51 - INVALID COMMAND FORMAT.  PLEASE RE-ENTER.', X
               ROUTCDE=(1),DESC=(6)
         B     RETR6                 RETURN
CMDERR02 EQU   *
         WTO   'EDTUPD52 - INVALID COMMAND FORMAT.  OPTION MUST BE ''OLX
               D'' OR VALID EDT SUFFIX.  PLEASE RE-ENTER.',            X
               ROUTCDE=(1),DESC=(6)
         B     RETR6                 RETURN
CMDERR03 EQU   *
         WTO   'EDTUPD53 - EDT SUFFIX INVALID.  THE EDT SUFFIX MUST BE X
               ''AA - 99''.  PLEASE RE-ENTER.',ROUTCDE=(1),DESC=(6)
         B     RETR6                 RETURN
GETCMD   EQU   *
         CH    R7,=H'12'             DATA LENGTH OF 12?
         BE    CHKOLD                YES - OPTION MUST BE OLD
         CH    R7,=H'11'             DATA LENGTH OF 11?
         BE    CHKSUFX               YES - OPTION IS EDT SUFFIX
         B     CMDERR01              NO - WE HAVE A CMD FORMAT ERROR
*   THE SYNTAX OF THE COMMAND IS VALID SO NOW WE MUST CHECK FOR
*   CORRECT COMMAND INPUT.
CHKOLD   EQU   *
*   DETERMINE IF THE REQUEST IS A VALID LOAD OLD EDT REQUEST.
         CLC   CIBDATA+9(3),=C'OLD'  OPTION IS OLD?
         BNE   CMDERR02              NO - ISSUE ERROR
         QEDIT ORIGIN=COMCIBPT,BLOCK=(3) FREE CIB
LOADOLD  L     R8,PTRJESCT           GET JESCT POINTER
         L     R9,OLDEDT             GET LAST EDT POINTER VALUE
         LTR   R9,R9                 VALID?
         BZ    NOOLD                 NO - DON'T RESET
         MODESET KEY=ZERO,MODE=SUP   GET AUTHORIZED
         ST    R9,JESEDT-JESCT(,R8)  PUT IN OLD EDT ADDRESS
         MODESET KEY=NZERO,MODE=PROB GET BACK TO NORMAL
         XC    OLDEDT(4),OLDEDT      CLEAR IT
         TM    FLAG,RELOAD           A RELOAD REQUEST?
         BO    FREEEND               YES - FREE CURRENT ENTRY
         WTO   'EDTUPD40 - ORIGINAL EDT IS REACTIVATED.',              X
               ROUTCDE=(1),DESC=(6)
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG18),EDTMSG18 MOVE IN MESSAGE
         PUT   OUTPUT,OUTREC         WRITE OUT MESSAGE
         B     FREEEND               FREE STORAGE AND END
NOOLD    EQU   *
         TM    FLAG,RELOAD           A RELOAD REQUEST?
         BO    DEFAULT               YES - LOAD NEW EDT
         WTO   'EDTUPD41 - ORIGINAL EDT IS CURRENTLY ACTIVE.',         X
               ROUTCDE=(1),DESC=(6)
         B     FREEEND               FREE STORAGE AND END
CHKSUFX  EQU   *
*   DETERMINE IF THE EDT SUFFIX VALUE IS CORRECT.
         QEDIT ORIGIN=COMCIBPT,BLOCK=(3) FREE CIB
         CLI   CIBDATA+9,C'A'        VALID SUFFIX?
         BL    CMDERR03              NO - WE HAVE A PARAMETER ERROR
         CLI   CIBDATA+9,C'9'        VALID SUFFIX?
         BH    CMDERR03              NO - WE HAVE A PARAMETER ERROR
         CLI   CIBDATA+10,C'A'       VALID SUFFIX?
         BL    CMDERR03              NO - WE HAVE A PARAMETER ERROR
         CLI   CIBDATA+10,C'9'       VALID SUFFIX?
         BH    CMDERR03              NO - WE HAVE A PARAMETER ERROR
         OI    FLAG,RELOAD           SET RELOAD FLAG
         MVC   EDTID(2),CIBDATA+9    SAVE EDT SUFFIX
         B     LOADOLD               LOAD OLD EDT
CSTOP    EQU   *
*   A HALT REQUEST HAS BEEN RECEIVED.  FIND OUT WHAT SHOULD BE DONE
*   WITH THE ALTERNATE EDT AND ACT ACCORDINGLY.
         QEDIT ORIGIN=COMCIBPT,BLOCK=(3) FREE CIB
MSGAGAIN XC    ECBAREA(4),ECBAREA    CLEAR ECB
         LA    R8,ECBAREA            GET ECB ADDRESS
         MVI   WTOREPLY,C' '         PRIME REPLY AREA
         LA    R9,WTOREPLY           GET REPLY AREA ADDRESS
         WTOR  'EDTUPD06 - REQUEST TO TERMINATE EDT UPDATE PROGRAM RECEX
               IVED.  SHOULD UPDATED EDT BE RETAINED (Y/N)?',          X
               (R9),1,(R8),ROUTCDE=(1)
         WAIT  ECB=ECBAREA           WAIT FOR REPLY
         OI    0(R9),X'40'           SET TO UPPER CASE
         MVI   FLAG,X'00'            CLEAR FLAG AREA
         CLI   0(R9),C'Y'            LEAVE NEW EDT?
         BE    NODEL                 YES - DON'T DELETE
         CLI   0(R9),C'N'            FREE THE EDT?
         BE    LOADOLD               YES - FREE AND END
         B     MSGAGAIN              RE-ISSUE MESSAGE
NODEL    EQU   *
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG02),EDTMSG02 MOVE IN MESSAGE
         PUT   OUTPUT,OUTREC         WRITE OUT MESSAGE
         CLOSE OUTPUT
         XR    R15,R15               CLEAR RETURN CODE
         B     END                   RETURN
PARMERR1 EQU   *
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG11),EDTMSG11 MOVE IN MESSAGE
         B     ERROROUT              OUTPUT ERROR MESSAGE
PARMERR2 EQU   *
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG12),EDTMSG12 MOVE IN MESSAGE
         B     ERROROUT              OUTPUT ERROR MESSAGE
OPENERR  EQU   *
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG19),EDTMSG19 MOVE IN MESSAGE
         B     ERROROUT              OUTPUT ERROR MESSAGE
VERERROR EQU   *
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG20),EDTMSG20 MOVE IN MESSAGE
         B     ERROROUT              OUTPUT ERROR MESSAGE
EDTERROR EQU   *
         LA    R14,*+6               SET BRANCH ADDRESS
         BSM   R0,R14                ESTABLISH AMODE=24
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG13),EDTMSG13 MOVE IN MESSAGE
         OI    FLAG,ERROR            SET ERROR FLAG
         B     FREEEND               FREE LOADED EDT
GETERROR EQU   *
         LA    R14,*+6               SET BRANCH ADDRESS
         BSM   R0,R14                ESTABLISH AMODE=24
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG14),EDTMSG14 MOVE IN MESSAGE
         B     ERROROUT              OUTPUT ERROR MESSAGE
LOADERR  EQU   *
         LA    R14,*+6               SET BRANCH ADDRESS
         BSM   R0,R14                ESTABLISH AMODE=24
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG15),EDTMSG15 MOVE IN MESSAGE
         B     ERROROUT              OUTPUT ERROR MESSAGE
NOMEMERR EQU   *
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG16),EDTMSG16 MOVE IN MESSAGE
         B     ERROROUT              OUTPUT ERROR MESSAGE
ALLDONE  EQU   *
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG17),EDTMSG17 MOVE IN MESSAGE
         PUT   OUTPUT,OUTREC         WRITE OUT MESSAGE
         CLOSE OUTPUT                CLOSE FILE
         L     R15,=F'4'             SET RETURN CODE
         B     END                   RETURN
FREEEND  EQU   *
         CLC   MODADDR(4),=4X'00'    NULL ENTRY?
         BE    NOMODENT              YES - DON'T DELETE IT
         DELETE EPLOC=EDTMEM         DELETE EDT MEMBER
         MODESET KEY=ZERO,MODE=SUP   GET AUTHORIZED
         L     R1,MODADDR            GET AREA ADDRESS
         L     R2,MODLEN             GET AREA LENGTH
         FREEMAIN RU,LV=(2),SP=241,A=(1)
         MODESET KEY=NZERO,MODE=PROB GET BACK TO NORMAL
         XC    MODADDR(4),MODADDR    CLEAR IT
NOMODENT EQU   *
         TM    FLAG,RELOAD           A RELOAD REQUEST?
         BO    DEFAULT               YES - LOAD NEW EDT
         TM    FLAG,ERROR            AN ERROR?
         BO    ERROROUT              YES - ISSUE ERROR AND END
         CLOSE OUTPUT                CLOSE FILE
         XR    R15,R15               SET RETURN CODE
         B     END                   RETURN
ERROROUT EQU   *
         PUT   OUTPUT,OUTREC         WRITE OUT ERROR MESSAGE
         MVC   WTOWORK1(WTOLEN1),WTOLIST1 MOVE IN WTO TEMPLATE
         MVC   WTOWORK1+4(L'EDTMSG00),OUTREC MOVE IN MESSAGE
         WTO   MF=(E,WTOWORK1)       SEND WTO
         MVC   OUTREC(132),BLANKS    CLEAR RECORD AREA
         MVC   OUTREC(L'EDTMSG99),EDTMSG99 MOVE IN MESSAGE
         PUT   OUTPUT,OUTREC         WRITE OUT ERROR MESSAGE
         MVC   WTOWORK1(WTOLEN1),WTOLIST1 MOVE IN WTO TEMPLATE
         MVC   WTOWORK1+4(L'EDTMSG00),OUTREC MOVE IN MESSAGE
         WTO   MF=(E,WTOWORK1)       SEND WTO
         L     R9,MODADDR            GET MODULE ADDRESS
         LTR   R9,R9                 ANYTHING LOADED?
         BNZ   LOADOLD               YES - FREE AND END
         CLOSE OUTPUT                CLOSE FILE
         L     R15,=F'8'             SET RETURN CODE
END      EQU   *
         L     R13,SAVEAREA+4        GET OLD SAVEAREA ADDRESS
         L     R14,12(,R13)          GET RETURN ADDRESS
         LM    R0,R12,20(R13)        RELOAD REGS
         BR    R14
FLAG     DS    F
RELOAD   EQU   X'80'                 A NEW EDT LOAD HAS BEEN REQUESTED
ERROR    EQU   X'40'                 AN ERROR CONDITION EXISTS
CONINIT  EQU   X'80'                 CONSOLE INTERFACE IS ENABLED
EDTPARM  DC    AL1(X'80')
         DC    AL3(EDTIDLEN)
EDTIDLEN DC    H'2'
EDTID    DC    C'00'
WTOREPLY DS    CL1
BLANKS   DC    132C' '
OUTREC   DS    CL132
EDTMSG00 DC   C'EDTUPD00 - ALTERNATE EDT (XX) LOADED SUCCESSFULLY.    '
EDTMSG01 DC   C'EDTUPD01 - OLD EDT ADDR XXXXXXXX/NEW EDT ADDR XXXXXXXX'
EDTMSG02 DC   C'EDTUPD02 - ALTERNATE EDT LEFT ACTIVE AT TERMINATION.  '
EDTMSG11 DC   C'EDTUPD11 - PARAMETER LENGTH DOES NOT EQUAL TWO.       '
EDTMSG12 DC   C'EDTUPD12 - INVALID EDT SUFFIX.  VALID VALUES AA - 99. '
EDTMSG13 DC   C'EDTUPD13 - LOADED EDT IS NOT IN MVS/XA 2.2.0 FORMAT.  '
EDTMSG14 DC   C'EDTUPD14 - ERROR OBTAINING EDT AREA STORAGE.          '
EDTMSG15 DC   C'EDTUPD15 - ERROR LOADING ALTERNATE EDT MEMBER.        '
EDTMSG16 DC   C'EDTUPD16 - SPECIFIED EDT MEMBER NOT FOUND.            '
EDTMSG17 DC   C'EDTUPD17 - EDT UPDATE TERMINATED BY OPERATOR.         '
EDTMSG18 DC   C'EDTUPD18 - ORIGINAL EDT HAS BEEN REACTIVATED.         '
EDTMSG19 DC   C'EDTUPD19 - ERROR OPENING DATASET CONTAINING EDT.      '
EDTMSG20 DC   C'EDTUPD20 - ERROR VERIFYING REQUESTED NEW EDT.         '
EDTMSG99 DC   C'EDTUPD99 - ALTERNATE EDT WAS NOT LOADED.              '
WOTLIST1 WTO   '                                                      'X
              ,ROUTCDE=(1),DESC=(6),MF=L
WTOLEN1  EQU  *-WTOLIST1
WTOWORK1 DS   CL(WTOLEN1)
SAVEAREA DS   18F
PTRJESCT DS   F
ECBAREA  DS   F
ANSRAREA DS   F
INPARMS  DS   F
OLDEDT   DS   F
MODADDR  DS   F
MODLEN   DS   F
EDTLUVSP DS   F
EDTGENSP DS   F
EDTGRPSP DS   F
EDTUCBSP DS   F
EDTMSKTP DS   F
EDTGRPPP DS   F
EDTPREFP DS   F
EDTTAPEP DS   F
DBL1     DS   D
         DS   F
DBL2     DS   D
         DS   F
EDTMEM   DC   C'IEFEDT00'
OUTPUT   DCB  MACRF=(PM),DSORG=PS,DDNAME=SYSOUT,LRECL=132
PDSIN    DCB  MACRF=R,DSORG=PO,DDNAME=EDT
BLDLLIST DS   0F
         DC   H'01'
         DC   H'62'
         DS   CL8
         DS   CL3
         DS   X
         DS   X
         DS   X
         DS   CL62
         CVT  DSECT=YES
         IEFJESCT
         DSECT
         IEZCOM
         DSECT
         IEZCIB
         END


                                                            © Xephon 1990

