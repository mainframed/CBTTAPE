Global DDNAME allocation and de-allocation

The following program is a multi-purpose routine which
invokes the DYNALLOC routine.  It was specifically coded at
our site to concatenate a variable number of datasets whose
names would be supplied at run time.  The utility will also de-
allocate resources and determine the dataset name allocated to a
DDNAME.  As coded, a maximum of 50 DDNAMEs can be
allocated and a maximum of 255 datasets per DDNAME can be
concatenated, although either of these limitations could easily
be modified.

* FUNCTION:   THIS IS A GLOBAL UTILITY ROUTINE WHICH WHEN CALLED WILL*
*             PERFORM 1 OF THE 5 FUNCTIONS LISTED BELOW:             *
*             A) GIVEN THE DDNAME IN THE JCL, DETERMINE THE DATASET  *
*                NAME ASSOCIATED WITH IT.                            *
*             B) ALLOCATE THE SUPPLIED DDNAME AND ASSIGN TO IT THE   *
*                SUPPLIED DATASET NAME.                              *
*             C) SAME AS (B) BUT DEALLOCATE THE RESOURCES INSTEAD    *
*             D) ALLOCATE THE SUPPLIED DDNAME AND ASSIGN TO IT THE   *
*                CONCATENATION OF THE SUPPLIED DATASET NAMES.        *
*             E) SAME AS (D) BUT DECONCATENATE THE RESOURCES INSTEAD *
* TABLES IN PROGRAM:                                                 *
*                 DD TABLE (WORKING STORE)       DD LISTS (GETMAIN)  *
*        -----------------------------------     ------LIST1-        *
*        | DDNAME  | ADDR. LIST1  | LENGTH |     |  DDNAME  |        *
*        -----------------------------------     ------------        *
*        | DDNAME  | ADDR. LIST2  | LENGTH |     |  DDNAME  |        *
*        -----------------------------------     ------------        *
*        |                                 |     .     ------LIST2-  *
*        |   MAXIMUM OF 50 ENTRIES         |     .     |          |  *
*        |                                 |     .     .          .  *
*        |                                 |     .     .          .  *
*        -----------------------------------     ------.     ------L *
*        | DDNAME  | ADDR. LISTN  | LENGTH |     |  DDN.     |       *
*        -----------------------------------     ------.     .       *
*                                                      |     .       *
*                                                      ------.       *
*                                                            .       *
*                                                            |-------*
* PARAMETER   1) FUNCTION INDICATOR       CL1                        *
* AREA:            1; DETERMINE DSN ASSOCIATED WITH DDNAME           *
*                  2; ALLOCATE DDNAME AND DSN                        *
*                  3; DEALLOCATE DDNAME AND DSN                      *
*                  4; ALLOCATE DDNAME AND CONCATENATE DSN'S          *
*                  5; DEALLOCATE DDNAME AND CONCATENATED DSN'S       *
*             2) RETURN INDICATOR         CL1                        *
*                  0; PROCESSING COMPLETED OKAY                      *
*                  1; SYSTEM ERROR; COULD NOT DEALLOCATE THE DDNAME  *
*                     OF THE CONCATENATED LIST.                      *
*                     PROCESSING CAN CONTINUE IF THIS DDNAME IS NOT  *
*                     USED AGAIN.                                    *
*                  2; NUMBER OF UNIQUE CONCATENATED DD STATEMENTS    *
*                     CURRENTLY ALLOCATED EXCEEDS 50.                *
*                  3; NO. DSN'S FOR CONCAT <2 OR >255                *
*                  4; ERROR ENCOUNTERED WHEN CONCATENATING DSN'S     *
*                  5; ERROR ENCOUNTERED WHEN DECONCATENTING DSN'S    *
*                  6; ERROR DETERMINING DATASET NAME                 *
*                  7; ERROR ENCOUNTERED WHILE ALLOCATING RESOURCES   *
*                  8; ERROR ENCOUNTERED WHILE DEALLOCATING RESOURCES *
*                  9; FUNCTION INDICATOR INVALID                     *
*             3) FILLER                   CL2                        *
*             4) ERROR CODE               XL4                        *
*                  CONTENTS OF R15 FROM JFCB MACRO, OR,              *
*                  INFO(2 BYTES) & REASON CODE(2 BYTES) FROM DYNALLOC*
*             5) DD NAME                  CL8                        *
*             6) NO, OF DSN'S             XL2                        *
*                USED TO SPECIFY THE NUMBER OF DSN'S TO BE CONCATE-  *
*                NATED WITH FUNCTION '4' ABOVE.                      *
*             7) DATASET NAME             CL44                       *
* REFERENCES: GC28-1150  MVS/XA SYSTEM MACROS & FACILITIES           *
** JFCB:- REQUIRED TO OBTAIN THE DATASET NAME ALLOCATED TO A DDNAME
JFCB     DSECT
         IEFJFCBN LIST=YES
** DSECTS USED BY THE DYNALLOC MACRO
         IEFZB4D0
         IEFZB4D2
** DSECT FOR PARAMETER AREA
PARMAREA DSECT
PARMFIND DS    CL1                 FUNCTION INDICATOR
PARMRIND DS    CL1                 RETURN INDICATOR
         DS    CL2                 FILLER
PARMEROR DS    0XL4                REG 15 FROM JFCB MACRO
PARMINFO DS    XL2                 INFO CODE FROM DYNALLOC SVC
PARMREAS DS    XL2                 REASON CODE FROM DYNALLOC SVC
PARMDDN  DS    CL8                 DD NAME
PARMNCON DS    H                   NO. OF DSN'S IN CONCATENATION
PARMDSN  DS    255CL44             DATASET NAME(S)
** DSECT FOR THE DD TABLE (USED FOR DD CONCATENATION)
DDTABLE  DSECT
DDNAME1  DS    CL8                 FIRST DD NAME IN CONCATENATION
DDVSTABA DS    A                   ADDR OF START OF GETMAIN AREA
DDVSTABL DS    H                   & ITS LENGTH
* WORKAREA AND DCB INFORMATION                                       *
DYNAWORK CSECT
         DC    CL8'DYNAWORK'
WORKRBP  DS    0F,F                REQUEST BLOCK POINTER
WORKRB   DS    CL(S99RBEND-S99RB)  REQUEST BLOCK
WORKTUPL DS    3AL4                TEXT UNIT POINTERS
WORKUNIT DS    CL2554              TEXT UNIT AREA
PARMADDR DS    0F,A                ADDRESS OF USER PARAMETER AREA
GENLOOP  DS    H                   INIT SET TO NO. OF DD'S IN CONCAT
GENNUM   DC    PL3'0'              GENERATED CONCAT DD SUFFIX
GENDDN   DS    0CL8                GENERATED DD NAME
GENPREFX DC    CL4'@N01'
GENSUFX  DS    CL4
** INCORE TABLE OF THE 1ST DDNAME OF THE CONCATENATION & A POINTER
** TO THE ALLOCATED GETMAIN AREA. A MAXIMUM OF 50 ENTRIES ARE PERMITTED
** SEE DSECT DDTABLE FOR LAYOUT.
         DS    0D
DDTABLEL DC    50XL14'00'
         DC    X'FF'                END OF TABLE MARKER
DDLISTA  DS    0F,A                 ADDRESS OF THE DD LIST (FIRST DD
DDLISTP  DS    A                      SUPPLIED, REST GENNED) & ITS
*                                     CURRENT POINTER (USED WHEN ALLOC)
** JFCB DCB
         DS    0D
FILEDCB  DCB   EXLST=FILELST,                                          X
               DDNAME=DUMMY,                                           X
               MACRF=(E)
FILELST  DS    0F
         DC    X'07'
         DC    AL3(FILEJFCB)
FILEJFCB DS    (JFCBLGTH)C
* PROGRAM SECTION                                                    *
PRGDYNA  CENTER SAVE=DYNASAVE        installation initialization macro
         L     R11,=V(DYNAWORK)
         USING DYNAWORK,R11
SN01JFCB EQU   R10
         LA    R10,FILEJFCB
         USING JFCB,R10
         USING S99RBP,R9                      BASE REG'S USED BY THE
         USING S99RB,R8                         DYNALLOC ROUTINE;
         USING S99TUPL,R7
         USING S99TUNIT,R6
         USING PARMAREA,R5                    PARAMETER AREA
** VALIDATE AND PROCESS THE PARAMETER LIST
         LTR   R1,R1               CHECK FOR PARAMETERS
         BNZ   DYNA0005
         WTO   ' ** PRGDYNA 001 ** NO PARAMETERS SUPPLIED, PROGRAM WILLX
                BE TERMINATED.',ROUTCDE=(1,11)
         LA    R8,4
         ABEND (R8)
DYNA0005 EQU   *
         ICM   R5,B'1111',0(R1)    OBTAIN ADDR OF PARAMETER LIST
         ST    R5,PARMADDR           & STORE (POSS HELPFUL IN DEBUGING)
         MVI   PARMRIND,C'0'       INITIALIZE RETURN INDICATOR
         XC    PARMEROR,PARMEROR     AND ERROR CODE
** FUNCTION '1': DETERMINE DSN
         CLI   PARMFIND,C'1'       GET DSN ?
         BNE   DYNA0020            NO, TRY NEXT FUNCTION
         BAL   R4,DYNA0100
         LTR   R15,R15             OK ?
         BZ    DYNAEXIT            YES, RETURN TO CALLER
         MVI   PARMRIND,C'6'       SET RETURN INDICATOR
         ST    R15,PARMEROR            AND REASON CODE
         B     DYNAEXIT            RETURN TO CALLER
** FUNCTION '2': ALLOCATE DD & DSN
DYNA0020 EQU   *
         CLI   PARMFIND,C'2'       ALLOCATE ?
         BNE   DYNA0030            NO, TRY NEXT FUNCTION
         BAL   R4,DYNA0666         SET UP ADDRESSABILITY FOR DYNALLOC
         LA    R2,PARMDDN            SVC, ADDRESS DSN AND DDN, THEN
         LA    R3,PARMDSN            EXECUTE
         BAL   R4,DYNA0200           FUNCTION
         LTR   R15,R15             OK ?
         BZ    DYNAEXIT            YES, RETURN TO CALLER
         MVI   PARMRIND,C'7'       SET RETURN INDICATOR
         MVC   PARMINFO,S99INFO    INFORMATION CODE &
         MVC   PARMREAS,S99ERROR     REASON CODE
         B     DYNAEXIT            RETURN TO CALLER
** FUNCTION '3': DEALLOCATE DD & DSN
DYNA0030 EQU   *
         CLI   PARMFIND,C'3'       DEALLOCATE ?
         BNE   DYNA0040            NO, TRY NEXT FUNCTION
         BAL   R4,DYNA0666         SET UP ADDRESSABILITY FOR DYNALLOC
         LA    R1,PARMDDN          R1 := ADDRESS OF DD NAME
         BAL   R4,DYNA0300         GO DEALLOCATE DD
         LTR   R15,R15             OK ?
         BZ    DYNAEXIT            YES, RETURN TO CALLER
         MVI   PARMRIND,C'8'       SET RETURN INDICATOR
         MVC   PARMINFO,S99INFO    INFORMATION CODE &
         MVC   PARMREAS,S99ERROR     REASON CODE
         B     DYNAEXIT            RETURN TO CALLER
** FUNCTION '4': ALLOCATE A CONCATENATION OF DATASETS
DYNA0040 EQU   *
         CLI   PARMFIND,C'4'        CONCATENATION  ?
         BNE   DYNA0090             NO, TRY NEXT FUNCTION
         CLC   PARMNCON,=H'2'       1 < NO. DSN'S < 256
         BL    DYNA0045
         CLC   PARMNCON,=H'255'
         BNH   DYNA0050
DYNA0045 EQU   *                    NUMBER OF DSNS INCORRECT
         MVI   PARMRIND,C'3'
         B     DYNAEXIT
DYNA0050 EQU   *
         LA    R2,DDTABLEL
         USING DDTABLE,R2           ADDRESS THE DD TABLE
DYNA0052 EQU   *
         CLI   DDNAME1,X'00'        ENTRY FREE ?
         BE    DYNA0054             YES, GO FILL IN DETAILS
         LA    R2,14(R2)            NEXT ENTRY
         CLI   0(R2),X'FF'          END OF DD TABLE ?!
         BNE   DYNA0052             NO
         MVI   PARMRIND,C'2'        GEEE!, LIMIT OF 50 EXCEEDED
         B     DYNAEXIT
DYNA0054 EQU   *                    SETUP REGS FOR GETMAIN
         LH    R1,PARMNCON          LOAD NO. OF DD'S REQUIRED
         LA    R1,1(R1)             ADD 1 ENTRY FOR E.O.L. MARKER
         SLL   R1,3                 EACH ENTRY IS 8 BYTES LONG
         LR    R0,R1                SETUP R0 FOR GETMAIN REQUEST
         MVC   DDNAME1,PARMDDN      STORE CONCAT. DD NAME & LENGTH OF DD
         STH   R0,DDVSTABL            LIST IN THE DD TABLE
         GETMAIN R,LV=(0)           OBTAIN VIRTUAL STORAGE FOR DD LIST
*                                   STORE DD LIST ADDR IN:
         ST    R1,DDVSTABA           - DD TABLE FOR FUTURE DECONCAT
         ST    R1,DDLISTA            - W/S FOR USE IN ALLOCATING DD'S
         ST    R1,DDLISTP            - W/S TO POINT AT CURRNT DD IN LIST
         DROP  R2
         MVC   GENLOOP,PARMNCON     SET UP LOOP COUNTER (NO. OF DD'S)
         LA    R2,PARMDDN
         B     DYNA0058             GO ALLOCATE 1ST DD & DSN
** THIS LOOP GENERATES A DD FOR EACH SUPPLIED DSN & THEN PERFORMS THE
** DD/DSN ALLOCATION. DD RANGE:- @N010001 TO @N019999
DYNA0056 EQU   *
         AP    GENNUM,=PL1'1'      ADD 1 TO THE DD COUNTER
         UNPK  GENSUFX,GENNUM      MAKE PRINTABLE
         OI    GENSUFX+3,X'F0'
         LA    R2,GENDDN           ADDRESS GENERATED DD NAME
         LA    R5,44(R5)           POINT TO NEXT DSN IN PARM LIST
         L     R1,DDLISTP          POINT TO NEXT SLOT IN DD LIST
         LA    R1,8(R1)
         ST    R1,DDLISTP
DYNA0058 EQU   *
         MVC   0(8,R1),0(R2)       PUT DDNAME INTO DD LIST
         MVI   8(R1),X'FF'         ANTICIPATE E.O.L. MARKER
         LA    R3,PARMDSN          POINT TO DSN TO BE ALLOCATED
         BAL   R4,DYNA0666         SET UP ADDRESSABILITY FOR DYNALLOC
         BAL   R4,DYNA0200           & GO ALLOCATE
         LTR   R15,R15             OK ?
         BZ    DYNA0060            YES, ALLOCATE NEXT DD/DSN
         L     R5,PARMADDR         POINT BACK TO START OF PARM LIST
         MVI   PARMRIND,C'7'       SET RETURN INDICATOR
         MVC   PARMINFO,S99INFO    INFORMATION CODE &
         MVC   PARMREAS,S99ERROR     REASON CODE
         B     DYNAEXIT            RETURN TO CALLER
DYNA0060 EQU   *
         LH    R1,GENLOOP          GEN & ALLOCATE ANOTHER DD/DSN ????
         SH    R1,=H'1'
         STH   R1,GENLOOP
         LTR   R1,R1
         BNZ   DYNA0056            IF ALL DD'S ALLOCATED, GO CONCAT
** THE 1ST DD (SUPPLIED BY USER) AND THE REMAINING GENNED DD'S HAVE
** NOW BEEN SETUP AND ALLOCATED & RESIDE IN THE DD LIST.
** THE NEXT FUNCTION IS TO CONCATENATE THE DD'S & THEREBY CONCATENATING
** THE DSN'S THEMSELVES. THE ACTUAL DD OF THE CONCAT IS THE FIRST DD
** (IE THE ONE SUPPLIED BY THE USER) IN THE DD LIST
         L     R5,PARMADDR         POINT BACK TO START OF PARM LIST
         BAL   R4,DYNA0666         SET UP ADDRESSABILITY FOR DYNALLOC
         L     R1,DDLISTA          R1:=DD LIST START ADDRESS
         BAL   R4,DYNA0400         EXECUTE CONCATENATION
         LTR   R15,R15             OK ?
         BZ    DYNAEXIT            YES, RETURN TO CALLER
         MVI   PARMRIND,C'4'       SET RETURN INDICATOR
         MVC   PARMINFO,S99INFO    INFORMATION CODE &
         MVC   PARMREAS,S99ERROR     REASON CODE
         B     DYNAEXIT            RETURN TO CALLER
** FUNCTION '5': DEALLOCATE A CONCATENATION OF DATA SETS
DYNA0090 EQU   *
         CLI   PARMFIND,C'5'       DECONCATENATION ?
         BE    DYNA0092            YES
         MVI   PARMRIND,C'9'       INVALID FUNCTION INDICATOR
         B     DYNAEXIT
** DECONCATENATE THE DD STRING
DYNA0092 EQU   *
         BAL   R4,DYNA0666         SET UP ADDRESSABILITY FOR DYNALLOC
         BAL   R4,DYNA0500           SVC AND THEN EXECUTE FUNCTION
         LTR   R15,R15             OK ?
         BZ    DYNA0094            YES, NOW DEALLOCATE
         MVI   PARMRIND,C'5'       SET RETURN INDICATOR
         MVC   PARMINFO,S99INFO    INFORMATION CODE &
         MVC   PARMREAS,S99ERROR     REASON CODE
         B     DYNAEXIT            RETURN TO CALLER
** SCAN DOWN DD TABLE FOR CONCATENATION DD ENTRY, THEN:
** 1) DEALLOCATE EACH DD
** 2) FREE DD TABLE ENTRY
** 3) RELEASE VIRTUAL STORAGE THAT CONTAINS THE DD LIST
DYNA0094 EQU   *                   SCAN DOWN DD TABLE
         LA    R2,DDTABLEL
         USING DDTABLE,R2          ADDRESS THE DD TABLE
DYNA0095 EQU   *
         CLC   DDNAME1,PARMDDN     GET CORRECT ENTRY
         BE    DYNA0096
         LA    R2,14(R2)           NEXT ENTRY
         CLI   0(R2),X'FF'         END OF DD TABLE REACHED WITHOUT
         BNE   DYNA0095              LOCATING DD ?
         MVI   PARMRIND,C'1'         - SHOULD NEVER HAPPEN !
         B     DYNAEXIT
** DEALLOCATE EACH DD OF THE CONCATENATION
DYNA0096 EQU   *
         L     R3,DDVSTABA         POINT TO START OF DD LIST
DYNA0097 EQU   *
         BAL   R4,DYNA0666         SET UP ADDRESSABILITY FOR DYNALLOC
         LR    R1,R3               ADDRESS DD NAME
         BAL   R4,DYNA0300         GO DEALLOCATE
         LTR   R15,R15             OK ?
         BZ    DYNA0098            YES, DEALLOCATE NEXT DD IN LIST
         MVI   PARMRIND,C'8'       SET RETURN INDICATOR
         MVC   PARMINFO,S99INFO    INFORMATION CODE &
         MVC   PARMREAS,S99ERROR     REASON CODE
         B     DYNA0099            GO FREE TABLE ENTRY
DYNA0098 EQU   *
         LA    R3,8(R3)            POINT TO NEXT LIST DD ENTRY
         CLI   0(R3),X'FF'         E.O.L. ?
         BNE   DYNA0097            NO
DYNA0099 EQU   *                   FREE THE DD TABLE ENTRY
         MVI   DDNAME1,X'00'       FREE ENTRY
         LH    R0,DDVSTABL         SET UP REGS FOR FREEMAIN
         L     R1,DDVSTABA           (LENGTH & ADDRESS)
         FREEMAIN R,LV=(0),A=(1)
         B     DYNAEXIT            RETURN TO CALLER
         DROP  R2
**           J F C B   &   D Y N A L L O C    S V C
**                  S U B R O U T I N E S
** JFCB:-  ISSUE A READ OF THE SWA SYSTEMS JFCB, TO DETERMINE THE
**         DATASET NAME FROM THE SUPPLIED DDNAME
DYNA0100 EQU   *
         MVC   FILEDCB+40(8),PARMDDN   XFER THE DDNAME
         RDJFCB FILEDCB            READ SYSTEM JFCB
         MVC   PARMDSN,JFCBDSNM    STORE NAME
         BR    R4                  AND RETURN
* DYNALLOC SVC 99:- ALLOCATE A DDNAME WITH A DATASET NAME
*          REGISTER 2 ADDRESS OF DDNAME
*          REGISTER 3 ADDRESS OF DATASET NAME
DYNA0200 EQU   *
         MVI   S99VERB,S99VRBAL         REQUEST DSN ALLOCATION
         ST    R7,S99TXTPP              POINT TO START OF TEXT PTRS
         ST    R6,S99TUPTR
         MVI   S99TUPTR,X'00'           INDICATE FURTHER TEXT ENTRY
         MVC   S99TUKEY,=AL2(DALDDNAM)  SPECIFY DDNAME
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,H'8'            LENGTH OF DDNAME
         MVC   S99TUPAR(8),0(R2)        & ITS NAME
         LA    R7,4(R7)                 POINT TO NEXT TEXT PTR
         LA    R6,14(R6)                POINT TO NEXT TEXT UNIT
         ST    R6,S99TUPTR
         MVI   S99TUPTR,X'00'           INDICATE FURTHER TEXT ENTRY
         MVC   S99TUKEY,=AL2(DALDSNAM)  SPECIFY DSNAM
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'44'          LENGTH OF DSNAME
         MVC   S99TUPAR(44),0(R3)       & ITS NAME
         LA    R7,4(R7)                 POINT TO NEXT TEXT PTR
         LA    R6,50(R6)                POINT TO NEXT TEXT UNIT
         ST    R6,S99TUPTR
         MVI   S99TUPTR,S99TUPLN        INDICATE LAST TEXT ENTRY
         MVC   S99TUKEY,=AL2(DALSTATS)  SPECIFY DISP OF SHR
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'1'           LENGTH OF PARM
         MVI   S99TUPAR,X'08'           & ITS DISP OF SHR
         LA    R1,S99RBP           R1 POINTS TO RB POINTER
         DYNALLOC                  INVOKE SVC 99 TO PERFORM REQUEST
         BR    R4                  RETURN
* DYNALLOC SVC 99:- DEALLOCATE A DDNAME & THEREFORE ITS DSN ALSO
*          REGISTER 1 ADDRESSES THE DDNAME
DYNA0300 EQU   *
         MVI   S99VERB,S99VRBUN    REQUEST DD DEALLOCATION
         ST    R6,S99TUPTR
         MVI   S99TUPTR,S99TUPLN   INDICATE FIRST & ONLY ENTRY
         MVC   S99TUKEY,=AL2(DUNDDNAM) SPECIFY DDNAME TO BE DEALLOCATED
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'8'               LENGTH OF DDNAME
         MVC   S99TUPAR(8),0(R1)            & ITS NAME
         LA    R1,S99RBP           R1 POINTS TO RB POINTER
         DYNALLOC                  INVOKE SVC 99 TO PERFORM REQUEST
         BR    R4                  RETURN
* DYNALLOC SVC 99:- CONCATENATE DDNAMES
*          REGISTER 1 POINTS TO THE LIST OF DDNAMES IN THE GETMAIN AREA
DYNA0400 EQU   *
         MVI   S99VERB,S99VRBCC    REQUEST DD CONCATENATION
         ST    R6,S99TUPTR
         MVI   S99TUPTR,S99TUPLN   INDICATE FIRST & ONLY ENTRY
         MVC   S99TUKEY,=AL2(DDCDDNAM) SPECIFY DDNAMES TO BE CONCAT.
         MVC   S99TUNUM,PARMNCON       NO. TO BE CONATENATED
DYNA0410 EQU   *                   BUILD LIST OF DD NAMES
         MVC   S99TULNG,=H'8'      LENGTH OF DDNAME (ALWAYS 8)
         MVC   S99TUPAR(8),0(R1)     & ITS NAME
         LA    R6,10(R6)           POINT TO NEXT TEXT UNIT ENTRY
         LA    R1,8(R1)            POINT TO NEXT DSN IN GETMAIN AREA
         CLI   0(R1),X'FF'         END OF DD LIST ?
         BNE   DYNA0410
         LA    R1,S99RBP           R1 POINTS TO RB POINTER
         DYNALLOC                  INVOKE SVC 99 TO PERFORM REQUEST
         BR    R4                  RETURN
* DYNALLOC SVC 99:- DECONCATENATE DDNAMES
DYNA0500 EQU   *
         MVI   S99VERB,S99VRBDC    REQUEST DD DECONCATENATION
         ST    R6,S99TUPTR
         MVI   S99TUPTR,S99TUPLN   INDICATE FIRST & ONLY ENTRY
         MVC   S99TUKEY,=AL2(DDCDDNAM) SPECIFY DDNAME TO BE DECONCAT.
         MVC   S99TUNUM,=H'1'
         MVC   S99TULNG,=H'8'               LENGTH OF DDNAME
         MVC   S99TUPAR(8),PARMDDN          & ITS NAME
         LA    R1,S99RBP           R1 POINTS TO RB POINTER
         DYNALLOC                  INVOKE SVC 99 TO PERFORM REQUEST
         BR    R4                  RETURN
* DYNALLOC SVC 99:- SET UP ADDRESSABILITY TO THE PARAMETER AREAS
DYNA0666 EQU   *
         LA    R9,WORKRBP          LOAD THE BASE REGISTERS
         LA    R8,WORKRB
         LA    R7,WORKTUPL
         LA    R6,WORKUNIT
         ST    R8,S99RBPTR         MAKE RBPTR POINT TO RB
         OI    S99RBPTR,S99RBPND   TURN ON HIGH ORDER BIT IN RBPTR
         XC    S99RB(S99RBEND-S99RB),S99RB   CLEAR OUT RB
         MVI   S99RBLN,S99RBEND-S99RB        STORE LENGTH OF RB FLD
         ST    R7,S99TXTPP              POINT TO START OF TEXT PTRS
         BR    R4
DYNAEXIT EQU   *
         CRETURN (14,12),RC=0                 installation exit macro
         LTORG
         END


Paul Sapsford
Systems Programmer (UK)                                     © Xephon 1990

