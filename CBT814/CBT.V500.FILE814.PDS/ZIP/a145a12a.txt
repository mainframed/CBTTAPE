Listing the indexed VTOC status

Periodically, through a combination of various system
occurrences, the indexed VTOC on a DASD volume can become
disabled.  Although usually this does not cause any problems with
accessing the volume, it can have performance implications as the
benefits realised when using the indexed VTOC are no longer in
effect.  The program included with this article provides an
ISPF/PDF interface that allows you to display the indexed VTOC
status of any or all DASD volumes.  By entering the VOLSER of
a particular volume, the status of that volume's indexed VTOC can
be determined.  If the entry field is left blank, the status of the
indexed VTOC of all on-line volumes will be displayed.

Figure 1 illustrates how a sample display would appear.  This
display would be generated by leaving the VOLSER field blank in
the IVTOCPNL selection panel.  In this example, the first six
volumes do not have an indexed VTOC dataset allocated to them.
The last volume, VOL012, has an indexed VTOC that has been
disabled and, as a result, could be re-activated by simply re-
enabling the current indexed VTOC.

The program, IXVTOCP, reads the input parameter from the
IVTOCPNL (either a single VOLSER or blanks) and examines the
appropriate volumes.  For the appropriate volumes, the status of
the indexed VTOC is determined using CVAFTST.  If it is
determined that the volume does not have an active indexed
VTOC, the program tries to determine whether an indexed VTOC
has ever been active on the volume by using CVAFFILT to check
for datasets prefixed with the indexed VTOC qualifiers (ie -
SYS1.VTOCIX.).  If a dataset is found that meets the name
criterion, the dataset name is included in the table display output.
If no dataset is found, the dataset name field is filled with 'N/A'
(denoting it is not available).

Since CVAFFILT accesses the VTOC directly, the program must
be authorised at the time the macro is invoked.  As most shops
have an authorisation SVC capable of performing this function,
this code has been left out of the IXVTOCP program.  Your
version of an authorisation SVC that performs the same function
as MODESET KEY=ZERO,MODE=SUP should be included
immediately following label READDSCB to ensure proper
program execution.

The IXVTOCP program should reside ina LNKLST library, a
library allocated to the STEPLIB DDNAME in the log-on
procedure, or a library allocated to the ISPLLIB DDNAME in the
log-on procedure.  It has no link-edit attribute requirements.

The following panels and message members must be incorporated
into the existing ISPF/PDF environment.  The functionality of the
VTOCPNL panel can be added to an existing selection panel
member in one of your panel libraries.

VTOCPNL PANEL LIBRARY MEMBER
%--------------------------- VTOC PROCESSING OPTIONS ----------------
%SELECT VTOC OPTION ===>_ZCMD                                       +
%
%   1 +IDXVTOC   - Indexed VTOC availability display
)INIT
  .CURSOR = ZCMD
)PROC
  &ZSEL = TRANS( TRUNC (&ZCMD,'.')
                1,'PGM(IXVTOCP)')
  &ZTRAIL = .TRAIL
)END
IVTOCPNL PANEL LIBRARY MEMBER
%---------------------- INDEXED VTOC CHECK UTILITY ------------------
%COMMAND ===>_ZCMD                                                  +
%
%
%VOLUME +===>_Z     +   (Blank to display all volumes)
)INIT
  .HELP = IXVTC001
  .ZVARS = '(VOLSER)'
  .CURSOR = VOLSER
  &VAMT='HALF'
  &ZCSKEY='VAMT'
 )PROC
   VER (&VOLSER,PICT,CCCCCC)
 )END
IVTOCTBL PANEL LIBRARY MEMBER
)ATTR
  @ TYPE(OUTPUT) INTENS(LOW)
)BODY
%-------------------- NON INDEXED VTOC VOLUME LIST ------------------
%SELECT OPTION ===>_ZCMD                           %SCROLL ===>_VAMT+
%
+VOLUME   UCB     DSNAME
+
)MODEL
@Z      @Z       @Z
)INIT
  &VAMT='HALF'
  &ZCSKEY='VAMT'
  .ZVARS='(VOLSER UCB DS1DSNAM)'
)END
IXVTC001 PANEL LIBRARY MEMBER
%------------------- USING THE INDEXED VTOC CHECK UTILITY -----------
%COMMAND ===>_ZCMD                                                  +
+
   The INDEXED VTOC CHECK UTILITY provides the user with an ISPF/PDF
   interface that displays the status of indexed VTOCs.
    - if the status of the indexed VTOC of a single volume is required,
      the volser of that volume is entered in the VOLUME field.
      - eg. - VOLUME ===> VOL001       (Blank to display all volumes)
        -  this will cause the indexed VTOC status for volume VOL001
           to be displayed.
   - if the status of the indexed VTOC of all online volumes is
           required, the VOLUME field is left blank.

)END
IVTOC00 MESSAGE LIBRARY MEMBER
IVTOC001   'VOLUME HAS INDEXED VTOC'  .ALARM=YES
'REQUESTED VOLUME HAS ENABLED INDEXED VTOC'
IVTOC002   'NO INDEXED VTOC SUPPORT'  .ALARM=YES
'DF/DS OR DFP IS NOT INSTALLED AND INDEXED VTOCS ARE NOT SUPPORTED'
IVTOC003   'INVALID VOLUME'  .ALARM=YES
'SPECIFIED VOLUME IS NOT ONLINE OR DASD VOLUME'
IVTOC004   'CVAF PROCESSING ERROR'  .ALARM=YES
'DF/DS OR DFP IS INSTALLED AND ERROR WAS ENCOUNTERED DURING CVAF'
IVTOC005   'VOLUME HAS NO IDX VTOC'
'SPECIFIED VOLUME CURRENTLY HAS NO INDEXED VTOC ENABLED'
IVTOC006   'FUNCTION NOT SUPPORTED'  .ALARM=YES
'REQUESTED FUNCTION IS NOT YET SUPPORTED FROM PANEL'
IVTOC008   'IDX VTOC DSN AVAILABLE'
'VOLUME HAD INDEXED VTOC DATASET BUT IT IS NOT ENABLED'
IVTOC009   'VOLSER TABLE OVERFLOW'  .ALARM=YES
'PGM TABLE OVERFLOW - NOT ALL VOLUMES DISPLAYED'
IXVTOCP PROGRAM
IXVTOCP  CSECT
         STM   R14,R12,12(R13)           SAVE INCOMING ENVIRONMENT
         LR    R10,R15                   SAVE MODULE BASE ADDRESS
         USING IXVTOCP,R10               SET UP MODULE ADDRESSABILITY
         USING UCBOB,R7                  ESTABLISH R7 AS UCB BASE
         GETMAIN R,LV=WORKLEN,SP=0       GETMAIN WORK AREA STORAGE
         LTR   R15,R15                   STORAGE ACQUIRED?
         BNZ   QUICKRET                  NO = SHUT 'ER DOWN
         LR    R9,R1                     SET UP ...
         USING WRKDSECT,R9                  WORK AREA ADDRESSABILITY
         ST    R9,WORKADDR               SAVE WORK AREA ADDRESS
         ST    R13,SAVEAREA+4            SAVE SAVEAREA ADDRESS
         LA    R13,SAVEAREA              LOAD R13 WITH NEW SAVEAREA
         LOAD  EP=ISPLINK                GET ISPLINK ADDRESS
         ST    R0,LINKADDR               SAVE ISPLINK ADDRESS
         L     R8,16                     SET UP ...
         USING CVTMAP,R8                   CVT ADDRESSABILITY
         L     R6,CVTUCBSC               GET UCB SCAN ROUTINE ADDRESS
         ST    R6,UCBSCRTN               SAVE UCB SCAN ROUTINE ADDRESS
         DROP  R8                        DROP CVT ADDRESSABILITY
         MVC   VOLSER,=6C' '             CLEAR VOLSER AREA
         L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(VDEFINE,CL6,VOLSER,CHAR,L6),VL,MF=(E,ISPLINK)
         LTR   R15,R15                   DEFINE OK?
         BNZ   RETURN                    NO - CLEAN UP & PACK IT IN
         MVC   ZCMD,=80C' '              CLEAR COMMAND AREA
         L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(VDEFINE,CL80,ZCMD,CHAR,L80),VL,MF=(E,ISPLINK)
         LTR   R15,R15                   DEFINE OK?
         BNZ   RETURN                    NO - CLEAN UP & PACK IT IN
         MVC   UCB,=4C' '                CLEAR UCB AREA
         L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(VDEFINE,CL4,UCB,CHAR,L4),VL,MF=(E,ISPLINK)
         LTR   R15,R15                   DEFINE OK?
         BNZ   RETURN                    NO - CLEAN UP & PACK IT IN
         MVC   DS1DSNAM,=44C' '          CLEAR DS1DSNAM AREA
         L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(VDEFINE,CL44,DS1DSNAM,CHAR,L44),VL,MF=(E,ISPLINK)
         LTR   R15,R15                   DEFINE OK?
         BNZ   RETURN                    NO - CLEAN UP & PACK IT IN
         LA    R3,BLANKMSG               GET ADDRESS OF BLANK MESSAGE
         ST    R3,LISTMSG                SAVE MESSAGE ADDRESS
         MVI   ALLFLAG,C'N'              INITIALIZE FLAG
DISPLAY0 MVC   VSERTAB(240),=240C'*'     INITIALIZE VOLSER TABLE
         L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(TBCREATE,TBLNAME,NOKEY,TBLIST,NOWRITE,REPLACE),  X
               VL,MF=(E,ISPLINK)
         C     R15,=F'4'                 SUCCESSFUL CREATE?
         BH    RETURN                    NO - CLEAN UP & PACK IT IN
         MVC   ZCMD,=80C' '              CLEAR COMMAND AREA
         CLI   ALLFLAG,C'Y'              ALL FLAG SET?
         BNE   NOTSET                    NO - BYPASS
         MVC   VOLSER,=6C' '             CLEAR VOLSER AREA
NOTSET   L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         L     R3,LISTMSG                LOAD MESSAGE ADDRESS
         CALL  (15),(DISPLAY,IVTOCPNL,(R3)),VL,MF=(E,ISPLINK)
         LTR   R15,R15                   DISPLAY OK?
         BZ    INIT                      YES - PERFORM MORE INITIALZ'N
         L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(VGET,ZVERB),VL,MF=(E,ISPLINK)
         L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(VCOPY,ZVERB,LENVERB,ADDRVERB),VL,MF=(E,ISPLINK)
         L     R1,LENVERB                GET VERB LENGTH
         LTR   R1,R1                     LENGTH > 0?
         BZ    RETURN                    NO - CLEAN UP & PACK IT IN
         L     R1,ADDRVERB               GET VERB ADDRESS
         CLC   0(6,R1),=C'RETURN'        VERB = 'RETURN'?
         BE    RETURN                    YES - CLEAN UP & PACK IT IN
         CLC   0(3,R1),=C'END'           VERB = 'END'?
         BE    RETURN                    YES - CLEAN UP & PACK IT IN
INIT     LA    R3,BLANKMSG               GET ADDRESS OF BLANK MESSAGE
         ST    R3,LISTMSG                SAVE MESSAGE ADDRESS
         CLC   VOLSER,=6C' '             VOLSER?
         BE    LISTALL                   NO - CHECK ALL VOLUMES
         XC    SCANWORK(100),SCANWORK    CLEAR UCB SCAN WORK AREA
UCBSCAN0 LA    R1,SCANADDR               LOAD ADDRESS OF SCAN PARMS
         L     R15,UCBSCRTN              LOAD ADDRESS OF SCAN ROUTINE
         BALR  R14,R15                   LINK TO ROUTINE
         LTR   R15,R15                   UCB FOUND?
         BNZ   IERROR01                  NO - ISSUE ERROR MESSAGE
         L     R7,SCANUCB                GET UCB ADDRESS
         CLC   UCBVOLI,VOLSER            VOLSER MATCH?
         BE    IVTOCCHK                  YES - CHECK FOR INDEXED VTOC
         B     UCBSCAN0                  NO - CHECK NEXT UCB
IVTOCCHK TM    UCBSTAT,X'80'             ONLINE?
         BZ    IERROR01                  NO - ISSUE ERROR MESSAGE
         CVAFTST UCB=(R7)                CHECK FOR CVAF STATUS
         LTR   R15,R15                   CVAF?
         BZ    NOCVAF01                  YES - DISPLAY MESSAGE
         C     R15,=F'4'                 CVAF/NO INDEXED VTOC?
         BE    ONEONLY                   YES - GET INFORMATION
         C     R15,=F'8'                 CVAF/INDEXED VTOC?
         BE    IXVMSG01                  YES - DISPLAY MESSAGE
         B     IERROR02                  NO - ISSUE MESSAGE
ONEONLY  BAL   R5,NOIXVTOC               BUILD TABLE INFORMATION
         B     BRTBL1(R15)               GO WHERE WE SHOULD
BRTBL1   B     NODSN                     NO INDEXED VTOC DSN FOUND
         B     ADSN                      INDEXED VTOC DSN FOUND
NODSN    LA    R3,IVTOC005               GET MESSAGE NAME ADDRESS
         ST    R3,LISTMSG                SAVE ADDRESS
         B     GODISP                    GO DISPLAY
ADSN     LA    R3,IVTOC008               GET MESSAGE NAME ADDRESS
         ST    R3,LISTMSG                SAVE ADDRESS
GODISP   B     TBLDISP0                  DISPLAY TABLE
LISTALL  MVI   ALLFLAG,C'Y'              SET FLAG
         XC    SCANWORK(100),SCANWORK    CLEAR UCB SCAN WORK AREA
UCBSCAN1 LA    R1,SCANADDR               LOAD ADDRESS OF SCAN PARMS
         L     R15,UCBSCRTN              LOAD ADDRESS OF SCAN ROUTINE
         BALR  R14,R15                   LINK TO ROUTINE
         LTR   R15,R15                   UCB FOUND?
         BNZ   TBLDISP0                  DISPLAY TABLE
         L     R7,SCANUCB                GET UCB ADDRESS
         TM    UCBSTAT,X'80'             ONLINE?
         BZ    UCBSCAN1                  NO - TRY NEXT UCB
         CVAFTST UCB=(R7)                CHECK FOR CVAF STATUS
         LTR   R15,R15                   CVAF?
         BZ    NOCVAF01                  YES - DISPLAY MESSAGE
         C     R15,=F'4'                 CVAF/NO INDEXED VTOC?
         BE    TBLCHECK                  YES - CHECK EXISTING ENTRIES
         B     UCBSCAN1                  NO - TRY NEXT UCB
TBLCHECK LA    R4,VSERTAB                GET ADDRESS OF VOLSER TABLE
TBLLOOP  CLC   0(6,R4),=C'******'        OPEN ENTRY?
         BE    TABLEUPD                  YES - ADD ENTRY
         CLC   0(6,R4),UCBVOLI           VOLSER IN TABLE?
         BE    UCBSCAN1                  YES - TRY NEXT UCB
         CLC   0(6,R4),=C'@@@@@@'        TABLE FULL?
         BE    TBLFULL                   YES - PROCESS FULL TABLE
         LA    R4,6(R4)                  INCREMENT TABLE POINTER
         B     TBLLOOP                   CHECK NEXT TABLE ENTRY
TABLEUPD MVC   0(6,R4),UCBVOLI           SAVE VOLSER IN TABLE
         MVC   VOLSER(6),UCBVOLI         SAVE VOLSER IN VARIABLE
         BAL   R5,NOIXVTOC               PROCESS VOLUME
         B     UCBSCAN1                  TRY NEXT UCB
TBLFULL  LA    R3,IVTOC009               GET ADDRESS OF MESSAGE NAME
         ST    R3,LISTMSG                SAVE VALUE
         B     DISPLAY0                  RE-DISPLAY PANEL
NOSUPP   LA    R3,IVTOC006               GET ADDRESS OF MESSAGE NAME
         ST    R3,LISTMSG                SAVE VALUE
         B     DISPLAY0                  RE-DISPLAY PANEL
IXVMSG01 LA    R3,IVTOC001               GET ADDRESS OF MESSAGE NAME
         ST    R3,LISTMSG                SAVE VALUE
         B     DISPLAY0                  RE-DISPLAY PANEL
IERROR01 LA    R3,IVTOC003               GET ADDRESS OF MESSAGE NAME
         ST    R3,LISTMSG                SAVE VALUE
         B     DISPLAY0                  RE-DISPLAY PANEL
IERROR02 LA    R3,IVTOC004               GET ADDRESS OF MESSAGE NAME
         ST    R3,LISTMSG                SAVE VALUE
         B     DISPLAY0                  RE-DISPLAY PANEL
NOIXVTOC MVC   DS1DSNAM(44),=44C' '      CLEAR AREA
         MVC   DSNAREA(12),=C'SYS1.VTOCIX.' MOVE IN DSNAME PREFIX
         MVC   DSNAREA+12(6),VOLSER      MOVE IN DSNAME SUFFIX
         USING BUFLIST,R1
         USING BFLE,R2
         LA    R1,BUFAREA                GET ADDRESS OF BUFFER
         LA    R2,8(,R1)                 GET ADDRESS OF LIST ENTRY
         XC    BUFAREA(BFLHLN+BFLELN),BUFAREA CLEAR BUFFER AREA
         OI    BFLHFL,BFLHDSCB           SET DSCB ACCESS FLAG
         MVI   BFLHNOE,1                 INDICATE ONE BUFFER ENTRY
         LA    R3,DS1DSNAM               GET WORK AREA ADDRESS
         ST    R3,BFLEBUF                SAVE IN BUFFER
         MVI   BFLELTH,DSCB1LTH          INDICATE WORK AREA LENGTH
         OI    BFLEFL,BFLECHR            SET CCHHR FLAG
         DROP  R1,R2
         XC    FCLAREA(FCLHDLEN+FCLDSNEL),FCLAREA CLEAR FCL AREA
         USING FCLMAP,R1
         USING FCLDSN,R2
         LA    R1,FCLAREA                GET FILTER CRITERIA LIST ADDR
         LA    R2,FCLHDEND               GET FCL ENTRY ADDRESS
         MVC   FCLID,=C'FCL '            MOVE IN HEADER
         MVC   FCLCOUNT(2),=H'1'         SET COUNT
         MVI   FCL1FLAG,X'80'            SET FULLY QUALIFIED DSN FLAG
         MVI   FCLDSNLG,X'12'            SET NAME LENGTH
         LA    R8,DSNAREA                GET ADDRESS OF DSNAREA
         ST    R8,FCLDSNA                SAVE ADDRESS
READDSCB EQU   *
*   INSERT YOUR INSTALLATIONS AUTHORIZATION SVC AT THIS POINT
*        MODESET KEY=ZERO,MODE=SUP
         CVAFFILT ACCESS=READ,UCB=(R7),FCL=FCLAREA,BUFLIST=BUFAREA,    X
               MF=(E,CVAFMAP)
         MODESET KEY=NZERO,MODE=PROB
         TM    FCLDSNST,X'01'            DATASET FOUND?
         BO    CHECKDSN                  YES - GO ON
         TM    FCLDSNST,X'02'            DSN FOUND?
         BNO   NOIDXDSN                  NO - MUST BE NO INDEXED VTOC
         LA    R1,FCLAREA                GET FCL AREA ADDRESS
         TM    FCL1FLAG,X'80'            DSN FULLY QUALIFIED?
         BNO   NOIDXDSN                  NO - WE'RE ALL DONE THEN
         NI    FCL1FLAG,X'7F'            SET FULLY QUALIFIED FLAG OFF
         MVI   FCLDSNLG,X'0D'            RESET LENGTH
         MVC   DSNAREA+12(6),=C'*     '  SET DSN FILTER
         B     READDSCB                  GO CHECK AGAIN
CHECKDSN DROP  R1,R2
         MVC   UCB(4),=4C' '             CLEAR UCB AREA
         MVC   UCB(3),UCBNAME            MOVE IN UCB ADDRESS
         L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(TBADD,TBLNAME,TBLIST),VL,MF=(E,ISPLINK)
         LA    R15,4                     SET RETURN CODE
         BR    R5                        RETURN
NOIDXDSN MVC   DS1DSNAM(44),=44C' '      CLEAR DSNAME AREA
         MVC   DS1DSNAM(3),=C'N/A'       MOVE IN NOT AVAILABLE FLAG
         MVC   UCB(4),=4C' '             CLEAR UCB AREA
         MVC   UCB(3),UCBNAME            MOVE IN UCB ADDRESS
         L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(TBADD,TBLNAME,TBLIST),VL,MF=(E,ISPLINK)
         LA    R15,0                     SET RETURN CODE
         BR    R5                        RETURN
NOCVAF01 LA    R3,IVTOC002               GET ADDRESS OF MESSAGE NAME
         ST    R3,LISTMSG                SAVE VALUE
         B     DISPLAY0                  RE-DISPLAY PANEL
TBLDISP0 L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(TBTOP,TBLNAME),VL,MF=(E,ISPLINK)
         L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         L     R3,LISTMSG                LOAD MESSAGE ADDRESS
         CALL  (15),(TBDISPL,TBLNAME,PNLNAME,(R3),BLANKMSG),          X
               VL,MF=(E,ISPLINK)
         C     R15,=F'8'                 FINISHED?
         BNE   REDISPL                   NO - DISPLAY AGAIN
         LA    R3,BLANKMSG               GET ADDRESS OF BLANK MESSAGE
         ST    R3,LISTMSG                SAVE MESSAGE ADDRESS
         L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(VGET,ZVERB),VL,MF=(E,ISPLINK)
         L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(VCOPY,ZVERB,LENVERB,ADDRVERB),VL,MF=(E,ISPLINK)
         L     R1,LENVERB                GET VERB LENGTH
         LTR   R1,R1                     LENGTH > 0?
         BZ    RETURN                    NO - CLEAN UP & PACK IT IN
         L     R1,ADDRVERB               GET VERB ADDRESS
         CLC   0(6,R1),=C'RETURN'        VERB = 'RETURN'?
         BE    RETURN                    YES - CLEAN UP & PACK IT IN
REDISPL  L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(TBCLOSE,TBLNAME),VL,MF=(E,ISPLINK)
         LTR   R15,R15                   CLOSE OK?
         BNZ   RETURN                    NO - RETURN
         B     DISPLAY0                  RE-DISPLAY
RETURN   L     R15,LINKADDR              LOAD ISPLINK ADDRESS
         CALL  (15),(TBCLOSE,TBLNAME),VL,MF=(E,ISPLINK)
         LTR   R15,R15                   CLOSE OK?
         BNZ   RETURN                    NO - RETURN
         L     R1,WORKADDR               GET GETMAIN AREA ADDRESS
         L     R13,SAVEAREA+4            RESTORE SAVE AREA POINTER
         DROP  R9                        DROP ADDRESSABILITY
         FREEMAIN R,LV=WORKLEN,A=(1),SP=0   FREE STORAGE
QUICKRET LM    R14,R12,12(R13)           RESTORE ENVIRONMENT
         LA    R15,0                     SET RETURN CODE
         BR    R14                       RETURN
DISPLAY  DC    C'DISPLAY'
VDEFINE  DC    C'VDEFINE'
VGET     DC    C'VGET'
VCOPY    DC    C'VCOPY'
IVTOCPNL DC    C'IVTOCPNL'
CL4      DC    C'(UCB)'
CL6      DC    C'(VOLSER)'
CL44     DC    C'(DS1DSNAM)'
CL80     DC    C'(ZCMD)'
TBLIST   DC    C'(VOLSER UCB DS1DSNAM)'
TBCREATE DC    C'TBCREATE'
TBADD    DC    C'TBADD'
TBTOP    DC    C'TBTOP'
TBCLOSE  DC    C'TBCLOSE'
TBDISPL  DC    C'TBDISPL'
NOWRITE  DC    C'NOWRITE'
REPLACE  DC    C'REPLACE'
TBLNAME  DC    C'IVTOCNME'
PNLNAME  DC    C'IVTOCTBL'
NOKEY    DC    CL80' '
ZVERB    DC    CL8'ZVERB'
CHAR     DC    CL8'CHAR'
L4       DC    F'4'
L6       DC    F'6'
L44      DC    F'44'
L80      DC    F'80'
IVTOC001 DC    C'IVTOC001'
IVTOC002 DC    C'IVTOC002'
IVTOC003 DC    C'IVTOC003'
IVTOC004 DC    C'IVTOC004'
IVTOC005 DC    C'IVTOC005'
IVTOC006 DC    C'IVTOC006'
IVTOC008 DC    C'IVTOC008'
IVTOC009 DC    C'IVTOC009'
BLANKMSG DC    CL80' '
SCANADDR DC    A(SCANWORK)
         DC    A(SCANTYPE)
         DC    A(SCANUCB)
SCANTYPE DC    AL1(UCB3DACC)
         DS    0D
SCANWORK DS    CL100
SCANUCB  DS    F
VSERTAB  DC    240C'*'
         DC    6C'@'
VOLSER   DS    CL6
UCB      DS    CL4
DSNAREA  DS    CL44
ALLFLAG  DS    CL1
ISPLINK  CALL  ,(,,,,,,,),VL,MF=L
CVAFMAP  CVAFFILT MF=L
FCLAREA  DS    CL(FCLHDLEN+FCLDSNEL)
BUFAREA  DS    CL20
WRKDSECT DSECT
LINKADDR DS    F
WORKADDR DS    F
UCBSCRTN DS    F
SAVEAREA DS    18F
ZCMD     DS    CL80
LENVERB  DS    F
ADDRVERB DS    F
LISTMSG  DS    F
         IECSDSL1 1
DSCB1LTH EQU   *-IECSDSL1
WORKLEN  EQU   *-WRKDSECT
BUFLIST  ICVAFBFL DSECT=YES
FCLMAP   ICVFCL
         CVT   DSECT=YES
         IECDIOCM
         DSECT
         IEFUCBOB
         END


    © Xephon 1989

