Displaying MVS IPL environmental information

In the days when there were no on-line spool viewing products
such as SDSF, SYSLOGs were routinely printed (and stacked
away in a corner) or archived to tapes.  The only way to
determine the IPL time and date, the IPL device address, etc,
was to search through the hardcopy SYSLOGs manually (if
they were still available).  Needless to say, a considerable
amount of time and effort was required to perform such manual
SYSLOG searches!

The following program, PWLIPL, was developed and, so far,
is still very useful for displaying the following information
quickly on a screen:

1      Elapsed date and time since last IPL

2      Exact Julian date and time of the last IPL

3      Volume serial number and device address of the IPL volume

4      Whether CLPA was specified by the operator

5      FMID of the MVS operating system

6      SMF-id.

This program has been working under various releases of MVS
up to and including MVS/ESA 3.1.3.  The main logic to
calculate the IPL time and date is based on the RMCTTOD
field which contains the SRM current time of day in units of
1024 microseconds.

This program can be invoked by any TSO user and can be
called any time to get the IPL environmental information
without searching the on-line or off-line SYSLOGs (or when
the on-line SPOOL viewing facility is not available). The
following is a sample of the output produced by the program,
PWLIPL, when called from TSO:

 LAST IPL WAS 02 DAYS, 05 HOURS, 32 MINUTES, 28 SECONDS AGO.
 IPL DATE IS     91.298   IPL TIME IS     7:08:55
 CLPA WAS NOT DONE
 FMID IS: JBB3113
 SMFID IS: MVS1
 IPL VOLSER IS: SYSRES                   IPL DEV ADDR IS: 730
 ***

This program is also designed to be called by any program to
pass back the IPL date and time information.  A sample calling
program, PWLDRVRP, has been attached to illustrate the
calling sequence.


Source listing for program PWLIPL

         TITLE ' GET IPL ENVIRONMENTAL INFORMATION'
*REGS ON ENTRY
*        R0 =  REQUEST CODE FROM CALLING PROGRAM
*              1 - REQUEST TO GET IPL DATE AND TIME
*        R1 =  ADDR OF PARM AREA
*              +0    ID  C'IPLP'
*              +4    4 BYTES TO SAVE IPL DATE
*              +8    4 BYTES TO SAVE IPL TIME
BASE     EQU  11
         USING IPL,BASE
         USING SAVEAREA,R13
***       I  N  I  T  I  A  L  I  Z  A  T  I  O  N                 ***
IPL      CSECT
         STM   R14,R12,12(R13)         ESTABLISH REQUIRED O/S LINKAGES
         LR    R9,R0
         LR    R10,R1
         LR    BASE,R15
         LR    R7,R13
         LR    R2,R1
         GETMAIN RC,LV=GETMAINL
         LTR   R15,R15
         BNZ   RETURN9
         LR    R13,R1
         ST    R7,4(,R13)
         ST    R13,8(,R7)
         XC    IPLPARM,IPLPARM
         CH    R9,=H'1'
         BNE   IPL010
         CLC   =C'IPLP',0(R10)
         BNE   IPL010
         STH   R9,REQCODE
         ST    R10,IPLPARM            SAVE PARM AREA ADDR
IPL010   DS    0H
         TIME
         ST    R0,CURRTIME
         ST    R1,CURRDATE
         XC    WORK,WORK
         SLR   R2,R2
         IC    R2,CURRTIME
         SLL   R2,4
         ST    R2,WORK+4
         OI    WORK+7,X'0F'
         CVB   R2,WORK             GET CURRENT HOUR
         MH    R2,=H'3600'         CONVERT TO SECONDS
         SLR   R3,R3
         IC    R3,CURRTIME+1
         SLL   R3,4
         ST    R3,WORK+4
         OI    WORK+7,X'0F'
         CVB   R3,WORK             GET CURRENT MINS
         MH    R3,=H'60'           CONVERT TO SECONDS
         SLR   R4,R4
         IC    R4,CURRTIME+2
         SLL   R4,4
         ST    R4,WORK+4
         OI    WORK+7,X'0F'
         CVB   R4,WORK             GET CURRENT SECS
         AR    R4,R3
         AR    R4,R2               CURRENT TIME IN SECONDS
         ST    R4,CURRTIME
         L     R2,CVTPTR           ADDRESS OF CVT
         USING CVT,R2
         $AM31
         L     R3,CVTOPCTP         ADDR OF RESOURCE MANAGER CT (RMCT)
         DROP  R2
         L     R5,124(0,3)         THIS FIELD IS IN UNITS OF
*                                      1024 MICROSECONDS
         M     R4,=F'1024'
         D     R4,=F'1000000'
         ST    R5,IPLTIME          SAVE TIME SINCE IPL IN SECONDS
         XR    R4,R4
         D     R4,=F'86400'
         L     R5,IPLTIME          RESTORE TIME SINCE IPL IN SECONDS
         ST    R4,IPLTIME          PORTION OF TIME SINCE IPL IN SECS
         XR    R4,R4
         D     R4,=F'60'
         LR    R8,R4               SECONDS
         XR    R4,R4
         D     R4,=F'60'
         LR    R7,R4               MINUTES
         XR    R4,R4
         D     R4,=F'24'
         LR    R6,R4               HOURS
         MVC   LINE(MESSL),MESSAGE
         CVD   R5,WORK             R5 = NO. OF DAYS SINCE IPL
         L     R2,CURRDATE
         SLL   R2,16
         SRL   R2,16
         ST    R2,TEMPDATE
         CP    TEMPDATE,WORK
         BNL   DATE0100
         SP    CURRDATE,=P'1000'
         AP    CURRDATE,=P'365'
DATE0100 DS    0H
         SP    CURRDATE,WORK       IPL DATE IN JULIAN DATE FORMAT
         UNPK  LINE+DAYS(2),WORK+6(2)
         OI    LINE+DAYS+1,C'0'
         CVD   R6,WORK
         UNPK  LINE+HOURS(2),WORK+6(2)
         OI    LINE+HOURS+1,C'0'
         CVD   R7,WORK
         UNPK  LINE+MINUTES(2),WORK+6(2)
         OI    LINE+MINUTES+1,C'0'
         CVD   R8,WORK
         UNPK  LINE+SECONDS(2),WORK+6(2)
         OI    LINE+SECONDS+1,C'0'
         CLC   REQCODE,=H'1'
         BE    IPL020              CALLED BY BATCH PROGRAM
         LA    R10,LINE
         TPUT  (10),MESSL
IPL020   DS    0H
         L     R5,CURRTIME         CURRENT TIME IN SECONDS
         C     R5,IPLTIME
         BNL   DATE0200
         CP    TEMPDATE,=P'1'      CURR DAYS
         BNL   DATE0110
         SP    CURRDATE,=P'1000'   DECR 1 YEAR
         AP    CURRDATE,=P'365'    ADD 365 DAYS
DATE0110 DS    0H
         SP    CURRDATE,=P'1'      SUBTRACT 1 FROM CURR DATE
         A     R5,=F'86400'        ADD 1 DAY TO CURRENT TIME IN SECS
DATE0200 DS    0H
         S     R5,IPLTIME          SUBTRACT TIME SINCE IPL IN SECONDS
         XR    R4,R4
         D     R4,=F'60'
         LR    R8,R4               SECONDS
         XR    R4,R4
         D     R4,=F'60'
         LR    R7,R4               MINUTES
         LR    R6,R5               HOURS
         CVD   R8,WORK
         L     R8,WORK+4
         SRL   R8,4
         STCM  R8,1,IPLTIME+3      SECONDS
         CVD   R7,WORK
         L     R7,WORK+4
         SRL   R7,4
         STCM  R7,1,IPLTIME+2      MINS
         CVD   R6,WORK
         L     R6,WORK+4
         SRL   R6,4
         STCM  R6,1,IPLTIME+1      HOURS
         MVI   IPLTIME,X'00'
         L     R2,IPLTIME
         SLL   R2,4
         ST    R2,IPLTIME
         OI    IPLTIME+3,X'0F'
         XC    LINE(80),LINE
         MVC   LINE(MESSL5),MESSAGE5
         ED    LINE+MSG5IPLT(L'MSS5IPLT),IPLTIME   IPL TIME (HH:MM:SS)
         ED    LINE+MSG5IPLD(L'MSS5IPLD),CURRDATE
         CLC   REQCODE,=H'1'
         BNE   IPL030
         L     R1,IPLPARM
         MVC   4(4,R1),CURRDATE
         MVC   8(4,R1),IPLTIME
         B     RETURN
IPL030   DS    0H
         LA    R10,LINE
         TPUT  (10),MESSL5
         L     R2,CVTPTR           ADDRESS OF CVT
         USING CVT,R2
         L     R3,CVTASMVT
         DROP  R2
         L     R5,0(0,3)             ASMFLAG1
         ST    R5,ASMFLAG1
         TM    ASMFLAG1+1,B'00001000'
         BNO   COLD
         MVC   LINE(MESSL1),MESSAGE1
         MVC   LINE+DONE(3),=C'NOT'
TPUT1    DS    0H
         LA    R10,LINE
         TPUT  (10),MESSL1
         B     FMID01
COLD     DS    0H
         MVC   LINE(MESSL1),MESSAGE1
         MVC   LINE+DONE(3),=C'   '
         B     TPUT1
FMID01   DS    0H
         $FMID
         XC    LINE(80),LINE
         MVC   LINE(MESSL2),MESSAGE2
         MVC   LINE+MSG2FMID(8),0(R1)
         LA    R10,LINE
         TPUT  (10),MESSL2
SMFID    DS    0H
         $#SMFID
         XC    LINE(80),LINE
         MVC   LINE(MESSL3),MESSAGE3
         MVC   LINE+MSG3SMFI(4),0(R1)
         LA    R10,LINE
         TPUT  (10),MESSL3
IPLVOL   DS    0H
         L     R2,16
         L     R2,X'30'(R2)              UCB ADDR FOR SYSRES
         XC    LINE(80),LINE
         MVC   LINE(MESSL4),MESSAGE4
         MVC   LINE+MSG4IPLV(6),X'1C'(R2)     IPL VOLSER
         MVC   LINE+MSG4IPLD(3),X'D'(R2)      IPL DEVICE ADDR
         LA    R10,LINE
         TPUT  (10),MESSL4
RETURN   DS    0H
         LR    R1,R13
         L     R13,4(,R13)
         FREEMAIN R,LV=GETMAINL,A=(1)    FREE OUR WORKAREA
RETURN9  DS    0H
         LM    R14,R12,12(R13)
         XR    R15,R15
         BR    R14
ZERO     DC    F'0'
MESSAGE  DC    C'LAST IPL WAS '
DAYS     EQU   *-MESSAGE
         DC    C'   DAYS, '
HOURS    EQU   *-MESSAGE
         DC    C'   HOURS, '
MINUTES  EQU   *-MESSAGE
         DC    C'   MINUTES, '
SECONDS  EQU   *-MESSAGE
         DC    C'   SECONDS AGO.'
MESSL    EQU   *-MESSAGE
MESSAGE1 DC    C' CLPA WAS '
DONE     EQU   *-MESSAGE1
         DC    C'    DONE'
MESSL1   EQU   *-MESSAGE1
MESSAGE2 DC    C' FMID IS: '
MSG2FMID EQU   *-MESSAGE2
         DS    CL8
MESSL2   EQU   *-MESSAGE2
MESSAGE3 DC    C' SMFID IS: '
MSG3SMFI EQU   *-MESSAGE3
         DS    CL4
MESSL3   EQU   *-MESSAGE3
MESSAGE4 DC    C' IPL VOLSER IS: '
MSG4IPLV EQU   *-MESSAGE4
         DS    CL6
         DC    C'                 IPL DEV ADDR IS: '
MSG4IPLD EQU   *-MESSAGE4
         DS    CL3
MESSL4   EQU   *-MESSAGE4
MESSAGE5 DC    C'IPL DATE WAS '
MSG5IPLD EQU   *-MESSAGE5
MSS5IPLD DC    X'40202020204B202020'
         DC    C'   IPL TIME WAS '
MSG5IPLT EQU   *-MESSAGE5
MSS5IPLT DC    X'402020207A20207A2020'
MESSL5   EQU   *-MESSAGE5
         LTORG
         DC    40CL3'ZAP'
**************************
*     D S E C T S        *
**************************
SAVEAREA DSECT
         DS    18F
LINE     DS    CL80
WORK     DS    D
ASMFLAG1 DS    CL4
TEMPDATE DS    F
CURRDATE DS    F
CURRTIME DS    F
IPLTIME  DS    F
IPLPARM  DS    F             ADDR OF PARM AREA PASSED BY CALLING PGM
REQCODE  DS    H
GETMAINL EQU   *-SAVEAREA
         CVT   DSECT=YES,LIST=YES
         END   IPL


Source listing for program PWLDRVRP

PWLDRVRP INIT                    installation initialization macro
         LA    R0,1
         LA    R1,IPLPARM
         LINK  EP=PWLIPL
         OPEN  (SYSPRINT,OUTPUT)
         LA    R4,IPLPARM
         PUT   SYSPRINT,(R4)
         CLOSE (SYSPRINT)
         B     EXIT
IPLPARM  DC    C'IPLP'
         DS    2F              TO RETRIEVE IPL DATE AND TIME
SYSPRINT DCB   DDNAME=SYSPRINT,MACRF=PM,DSORG=PS
         END   PWLDRVRP


Source listing for macro $AM31

         MACRO
         $AM31
* SET TO 31-BIT ADDRESSING
         L     R14,16
         TM    X'74'(R14),X'80'       MVS/XA?
         BZ    AM31A&SYSNDX
         LA    R14,AM31A&SYSNDX
         O     R14,AM31B&SYSNDX        INDICATE 31-BIT ADDRESSING
*        BSM   0,R14      CHANGE TO AMODE=31 & BRANCH TO AM31A&SYSNDX
         DC    X'0B0E'                BSM  R0,R14
         DS    0F
AM31B&SYSNDX  DC    X'80000000'
AM31A&SYSNDX  DS    0H
         MEND


Source listing for macro $FMID

         MACRO
         $FMID
         L   R1,16
         SH  R1,=H'32'
         MEND


Source listing for macro $#SMFID

         MACRO
         $#SMFID
         L     R1,16                     CVT
         L     R1,X'C4'(R1)              SMCA
         LA    R1,X'10'(R1)              POINT TO SMFID
.EXIT    MEXIT
         MEND


Peter W K Lee
Software Analyst (Canada)                        c Xephon 1992

