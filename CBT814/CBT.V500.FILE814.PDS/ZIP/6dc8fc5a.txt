Browsing ISPF command table entries

PRTCMDS is a CLIST which enables the user to browse the
ISPF command table entries and optionally print the command
table.  This uses a file tailoring technique to extract information
from the table.  The command table is sorted in the command
verb order.  For more information refer to the CLIST
comments within the code.


PRTCMDS CLIST

/*********************************************************************/
/*  The following file is PRTCMDS                                    */
/*  Include this in a PDS which is in SYSPROC concatenation          */
/*********************************************************************/
PROC 0
  ISPEXEC CONTROL ERRORS RETURN
  ISPEXEC VGET ZSCREEN ASIS
  CONTROL ASIS
/*********************************************************************/
/* Before using this CLIST make sure the following members are copied*/
/* to appropriate datasets.                                          */
/* PRTCMDS  (clist)       - any dataset in SYSPROC concatenation.    */
/* PRTPAN   (panel)       - any dataset in ISPPLIB concatenation.    */
/* PRTSKEL  (skeleton)    - any dataset in ISPSLIB concatenation.    */
/* Also make sure you have a standard dataset printing program.      */
/* Examine the variable values. Change to suit your site standards.  */
/*********************************************************************/
  SET &PRINT  = &STR(N)
  SET &TB     = &STR(ISPCMDS)   /* Command table         */
  SET &DEST   = &STR(R0)        /* Print destination     */
  SET &CLASS  = &STR(A)         /* Print class           */
  SET &COPIES = 1               /* Print copies          */
  SET &SKEL   = &STR(PRTSKEL)   /* Name of skeleton      */
  SET &PAN    = &STR(PRTPAN)    /* Name of panel         */
/*********************************************************************/
/* Build dataset name for ddname ISPFILE.                            */
/*********************************************************************/
  SET &D1     = &STR(DAT)&SUBSTR(1:2,&SYSJDATE)&SUBSTR(4:6,&SYSJDATE)
  SET &T1     = &STR(TI)&SUBSTR(1:2,&SYSTIME)&SUBSTR(4:5,&SYSTIME)+
                &SUBSTR(7:8,&SYSTIME)
  SET &SCR    = &STR(S000000)&ZSCREEN
  IF &SYSPREF ^= &STR() THEN +
       SET &DS = &SYSPREF..&D1..&T1..&SCR
  ELSE +
       SET &DS = &SYSUID..&D1..&T1..&SCR
  ALLOC FI(ISPFILE) DA('&DS') +
        RECFM(F B A) BLKSIZE(23408) LRECL(133) REUSE
/*********************************************************************/
/* Sort the command table in command verb order. We are not storing  */
/* sorted table. Sorting is done for the report only.                */
/* If sorted command table is not required comment the TBSORT line.  */
/*********************************************************************/
  ISPEXEC TBSORT &TB FIELDS (ZCTVERB C,A)
  ISPEXEC FTOPEN
  ISPEXEC FTINCL &SKEL
  SET &FTINCLRC = &LASTCC
  ISPEXEC FTCLOSE
  FREE FI(ISPFILE)
/*********************************************************************/
/* Browse the dataset containing all entries of command table.       */
/*********************************************************************/
  ISPEXEC BROWSE DATASET('&DS')
  SET &BROWRC = &LASTCC
  IF &ZERRMSG ^= &STR() THEN   +
     ISPEXEC SETMSG MSG(&ZERRMSG)
  SET &ERR1  = &STR()
  SET &ERR2  = &STR()
  SET &ERR3  = &STR()
  IF &BROWRC > 0 THEN +
  DO
    SET &ERR1 = &STR(Could not browse the dataset containing command +
                     table entries.)
    SET &ERR2 = &STR(Browse return code=&BROWRC for dataset "&DS".)
    IF &FTINCLRC > 0 THEN +
       SET &ERR3 = &STR(File tailoring failed with a return code of +
                        &FTINCLRC..)
  END
  ISPEXEC DISPLAY PANEL(&PAN)
  SET &DISPRC = &LASTCC
  IF &PRINT = &STR(Y) && &DISPRC = 0 THEN +
  DO
/*********************************************************************/
/* If you don't use PRINTOFF change the following command and use    */
/* any dataset printing program with suitable parameters.            */
/*********************************************************************/
     PRINTOFF '&DS' COPIES(&COPIES) DEST(&DEST) CLASS(&CLASS) NOHEADING
  END
  CONTROL NOMSG
  DELETE '&DS'
EXIT


PRTPAN PANEL

)ATTR
/*********************************************************************/
/*  The following file is PRTPAN                                     */
/*  Include this in a PDS which is in ISPPLIB concatenation          */
/*********************************************************************/
)BODY EXPAND($$)
%-$-$- Print ISPF command table  -$-$-
%Command ===>_ZCMD                                                            +
+                                                                             +
+-----------------------------------------------------------------------------%
+Would you like to print ISPF command table %===>_Z+
+                         Print destination %===>_Z       +
+                               Print class %===>_Z+
+                              Print copies %===>_Z +
+-----------------------------------------------------------------------------%
+                                                                             +
%&ERR1                                                                        +
%&ERR2                                                                        +
%&ERR3                                                                        +
+                                                                             +
)INIT
  .ZVARS  = '(PRINT DEST CLASS COPIES)'
  .CURSOR = PRINT
)PROC
  VER(&PRINT,NB,LIST,Y,N)
  IF (&PRINT='Y')
     VER(&DEST,NB)
     VER(&CLASS,NB)
     VER(&COPIES,NB)
)END


PRTSKEL SKELETON

)CM ******************************************************************/
)CM The following file is PRTSKEL.                                   */
)CM Include this in a PDS which is in ISPSLIB concatenation.         */
)CM ******************************************************************/
)TB 2 11 17 77
)SET LCNT = 61
)DOT ISPCMDS
)CM -- Begin print headings for the table
)SEL &LCNT GT 60
======================================================================?
==============================================================
 Verb     Trunc Action                                                 ?
     Description
 ======================================================================?
==============================================================
)SET LCNT = 8
)ENDSEL
)CM -- End print headings for the table
!&ZCTVERB!&ZCTTRUNC!&ZCTACT!&ZCTDESC
)SET LCNT = &LCNT + 1
)ENDDOT
_____________________________________________   End of command table  ?
_____________________________________________________________


G Lalitha (Australia)                              c Xephon 1991

