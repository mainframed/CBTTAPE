DASD/tape information display

/* "Q" Exec */
Address "TSO"
Parse Upper Arg parms
"CALL 'TEST.LOADLIB(QUERYCMD)'" "'"parms"'"

Sample displayS

Q DA TEST*
--CUU-TYPE-STATUS--VOLSER-----SIZE-----ATTRIBUTE-------SHAREABLE-------
| 123 3390 ONLINE  TEST01   ( 3,339)    PRIVATE      NON-SHAREABLE    |
| 130 3390 ONLINE  TEST02   ( 3,339)    PRIVATE      NON-SHAREABLE    |
| 150 3380 ONLINE  TEST03   ( 1,770)    STORAGE      NON-SHAREABLE    |
-----------------------------------------------------------------------

Q TA
--CUU-TYPE-STATUS--VOLSER---CUU-TYPE-STATUS--VOLSER---CUU-TYPE-STATUS--VOLSER-- | 200 3420 ONLINE  123456 | 201 3420 OFFLINE        | 202 3420 OFFLINE        | | 300 3480 ONLINE         | 301 3480 ONLINE         | 302 3480 OFFLINE        | ---------------
----------------------------------------------------------------

The program source

         TITLE 'QUERYCMD - DO A QUERY COMMAND A BIT LIKE VM'
***********************************************************************
* QUERYCMD: SCAN ALL UCB'S, CHECKING FOR EITHER DASD OR TAPES, AND    *
*           DISPLAY INFO ON TSO.                                      *
* PARMS:    PASSED VIA 'PARM=...'.                                    *
*           POSSIBLE FORMS ARE DESCRIBED IN THE 'HELPMSG' MESSAGES    *
*           AT THE END OF THIS PROGRAM (DISPLAYED BY ENTERING '?'     *
*           AS THE ONLY PARM PASSED TO THE PROGRAM).                  *
* OUTPUT:   RETURN CODE AND DISPLAY VIA 'TPUT'                        *
*           0  -  OK                                                  *
*           4  -  REQUESTED CUU OR VOLID NOT FOUND                    *
*           8  -  PARMS NOT PASSED, INCORRECT LENGTH, OR INVALID      *
***********************************************************************
QUERYCMD CSECT
         BAKR  R14,0                    SAVE CALLER DATA ON STACK
         LR    R12,R15                  GET ENTRY POINT
         USING QUERYCMD,R12             ADDRESSABILITY
         L     R2,0(R1)                 GET ADDR OF PARM
         STLINENO LINE=1                ENSURE DISPLAY STARTS @ LINE1
* SCAN THE PARMS PASSED. FIRST CHECK IS TO SEE IF TOO MUCH/TOO LITTLE *
* HAS BEEN PASSED.                                                    *
         CLC   0(2,R2),=H'1'            NONE AT ALL?
         BL    BADPARM
         BE    GIVEHELP                 A CRY FOR HELP???
         CLC   0(2,R2),=H'10'           TOO MANY?
         BH    BADPARM
         LR    R3,R2                    LOAD PARM ADDRESS
         LA    R3,2(R3)                 JUMP PAST LENGTH
         LH    R2,0(R2)                 GET LENGTH
         XR    R4,R4                    CLEAR COUNT
         LA    R5,PRM1                  POINT TO FIRST PARM WORKAREA
* CHECK THAT FIRST PARM IS 'DA' OR 'TA'...                            *
PRMLOOP1 DS    0H
         CLI   0(R3),C' '               END OF PARM?
         BE    PARM1                    YES..CHECK IT OUT
         LA    R4,1(R4)                 BUMP COUNT
         CH    R4,=H'2'                 TOO LONG?
         BH    BADPARM                  YES..
         MVC   0(1,R5),0(R3)            NO...SAVE CHARACTER
         LA    R3,1(R3)                 BUMP SCAN FIELD
         LA    R5,1(R5)                 BUMP OUTPUT FIELD
         BCT   R2,PRMLOOP1              CONTINUE SCAN...
         OI    SWITCH,ALLUCBS           MUST WANT ALL DASD/TAPE
         MVC   MSG6(MSG6L),MSG6A        CHANGE HEADING LINE
PARM1    DS    0H
         CLC   PRM1,DA                  DASD?
         BE    PRM1DA
         CLC   PRM1,TA                  TAPE?
         BNE   BADPARM
PRM1TA   DS    0H
         MVI   UCBCHECK+1,UCB3TAPE      SET UP TO LOOK FOR TAPE
         TM    SWITCH,ALLUCBS           WANT ALL TAPES?
         BO    GETPRM2                  YES..
         MVC   MSG6(MSG6L),MSG6B        NO...CHANGE HEADING LINE
         B     GETPRM2
PRM1DA   DS    0H
         MVI   UCBCHECK+1,UCB3DACC      SET UP TO LOOK FOR DASD
         OI    SWITCH,GETDASD           SHOW WE'RE LOOKING FOR DASD
* NOW GET SECOND PARM (IF ANY) AND CHECK ITS FORMAT. THIS COULD BE:   *
*    - CUU       SINGLE CUU                                           *
*    - VOLSER    SINGLE VOLSER                                        *
*    - VOL*      GENERIC VOLSER(S)                                    *
*    - CUU-CUU   RANGE OF CUU VALUES                                  *
*    - ONLINE    ALL ON-LINE UNITS                                    *
GETPRM2  DS    0H
         TM    SWITCH,ALLUCBS           ANY MORE PARMS?
         BO    SCANUCBS                 NO...DISPLAY THE LOT
         BCTR  R2,0                     TAKE 1 OFF LENGTH FOR SPACE
         XR    R4,R4                    CLEAR COUNT
         LA    R5,PRM2                  POINT TO SECOND PARM WORKAREA
         LA    R3,1(R3)                 BUMP PAST SPACE
PRMLOOP2 DS    0H
         CLI   0(R3),C' '               END OF PARM?
         BE    TOOMANY                  YES..MUST BE TOO MANY PARMS
         LA    R4,1(R4)                 BUMP COUNT
         CH    R4,=H'7'                 TOO LONG?
         BH    BADPARM                  YES...
         MVC   0(1,R5),0(R3)            NO...SAVE CHARACTER
         LA    R3,1(R3)                 BUMP SCAN FIELD
         LA    R5,1(R5)                 BUMP OUTPUT FIELD
         BCT   R2,PRMLOOP2              CONTINUE SCAN...
* NOW TRY AND WORK OUT THE FORMAT OF THE PARM (CUU, ON-LINE, ETC)...  *
PARM2    DS    0H
         CH    R4,=H'7'                 WAS IT 'CUU-CUU'?
         BE    CHKRNGE1                 ...MAYBE...
         CH    R4,=H'3'                 WAS IT CUU?
         BE    CHKCUU                   ...MAYBE...
* WE MUST ASSUME THAT ANYTHING ELSE IS EITHER A REQUEST FOR ON-LINE   *
* DEVICES OR A VOLID SPECIFICATION (FULLY-QUALIFIED OR GENERIC)...    *
         CLC   PRM2(6),ONLINE           ON-LINE REQUEST?
         BNE   CHKVOLID                 NO...MUST BE A VOLID
         OI    SWITCH,ONLIN             SHOW WE WANT ALL ON-LINE UNITS
         B     SCANUCBS                 GO AND DISPLAY THEM
CHKVOLID DS    0H
         BCTR  R5,0                     BACK UP 1 TO LAST CHAR IN PRM2
         CLI   0(R5),C'*'               GENERIC VOLID SPECIFIED?
         BE    GENVOLID                 YES..
         OI    SWITCH,ONEVOLID          NO...SHOW JUST 1 VOLID REQUIRED
         MVC   VOLID,PRM2               SAVE VOLID TO LOOK FOR
         B     SCANUCBS                 GO AND DISPLAY IT
GENVOLID DS    0H
         OI    SWITCH,GENERIC           SHOW IT'S A GENERIC REQUEST
         BCTR  R4,0                     TAKE 1 OFF LEN FOR '*'
         BCTR  R4,0                     TAKE 1 OFF LEN FOR 'EX' INSTR
         ST    R4,EXLENGTH              SAVE LENGTH FOR 'EX'
         MVC   VOLID,PRM2               SAVE VOLID FOR 'EX'
         B     SCANUCBS                 GO AND DISPLAY THEM
* SEE IF A VALID CUU WAS SPECIFIED. NOTE THAT IF IT ENDS IN '*' THEN  *
* IT MUST BE A GENERIC VOLID...                                       *
CHKCUU   DS    0H
         BCTR  R3,0                     BACK UP 1 TO LAST CHARACTER
         CLI   0(R3),C'*'               ENDS WITH '*'?
         BE    GENVOLID                 YES..
         TRT   PRM2(3),TRTAB            ENSURE VALID HEX
         BNZ   BADCUU                   NO...
SET1CUU  DS    0H
         OI    SWITCH,ONECUU            SHOW WE JUST WANT THE ONE
         MVC   CUU,PRM2                 SAVE THE CUU
         B     SCANUCBS                 GO AND DISPLAY IT
* SEE IF A VALID CUU-CUU RANGE WAS ENTERED...                         *
CHKRNGE1 DS    0H
         CLI   PRM2+3,C'-'              CORRECT FORMAT?
         BNE   BADPARM                  NO...
         TRT   PRM2(3),TRTAB            ENSURE VALID HEX (1ST CUU)
         BNZ   BADRANGE
         TRT   PRM2+4(3),TRTAB          ENSURE VALID HEX (2ND CUU)
         BNZ   BADRANGE
         MVC   TRTAB+C'A'(6),LETTERS    MOVE X'0A->0F' INTO XLATE TABLE
         MVC   TRTAB+C'0'(10),NUMBERS   MOVE X'00->09' INTO XLATE TABLE
         MVC   FWORD(3),PRM2            SAVE 1ST CUU
         BAL   R9,CONVCUU               CONVERT TO BINARY FOR COMPARES
         MVC   BINCUU1,FWORD            SAVE BINARY VALUE OF 1ST CUU
         MVC   FWORD(3),PRM2+4          SAVE 2ND CUU
         BAL   R9,CONVCUU               CONVERT TO BINARY FOR COMPARES
         MVC   BINCUU2,FWORD            SAVE BINARY VALUE OF 2ND CUU
         L     R8,BINCUU1               GET 1ST CUU
         L     R9,BINCUU2               GET 2ND CUU
         CR    R8,R9                    SEE IF WE'VE GOT A VALID RANGE
         BH    BADRANGE                 ...NO
         BE    SET1CUU                  THE SAME! TREAT AS ONLY 1 CUU
         OI    SWITCH,RANGE             SHOW WE WANT A RANGE
* SCAN THROUGH THE UCB'S, LOOKING FOR THE TYPE WE WANT...             *
SCANUCBS DS    0H
         LA    R7,99                    FORCE HEADINGS
         USING CVTMAP,R4                ADDRESSABILITY TO CVT
         L     R4,16                    GET CVT POINTER
         L     R4,CVTUCBSC              GET A(UCB SCAN ROUTINE)
         ST    R4,UCBSCA                SAVE IT
         DROP  R4
         USING UCBOB,R4                 ADDRESSABILITY TO UCB
         LA    R3,MSG3CUU1              POINT TO FIRST MSG FIELD
         XR    R5,R5                    SET COUNT OF ITEMS IN LINE
UCBLOOP  DS    0H
         LA    R1,PARMA                 PARMLIST ADDRESS
         L     R15,UCBSCA               GET SCAN ROUTINE ADDRESS
         BALR  R14,R15                  CALL SCAN ROUTINE
         LTR   R15,R15                  END OF UCB'S?
         BNZ   ENDUCBS                  YES..CLEAN UP, ETC
         L     R4,ADDRUCB               NO...GET UCB ADDRESS
* THE NEXT INSTRUCTION IS UPDATED TO INSERT THE CORRECT BYTE FOR DASD *
* OR TAPE, AS REQUESTED...                                            *
UCBCHECK TM    UCBTBYT3,X'00'           IS IT UCB TYPE WE WANT?
         BNO   UCBLOOP                  NO...GET NEXT ONE
* NOW WE'VE GOT A UCB OF THE TYPE WE WANT - IF WE WANT ALL THEN GO    *
* AND GET OUR INFO, OTHERWISE SEE IF IT'S THE CUU OR VOLID REQUIRED,  *
* OR AN ON-LINE ONE IF THAT'S WHAT WE'RE LOOKING FOR...               *
GOTUCB   DS    0H
         TM    SWITCH,ALLUCBS           WANT ALL UNITS?
         BO    GETINFO                  YES..
         TM    SWITCH,ONLIN             WANT ONLY ON-LINE UNITS?
         BNO   CHK1VOL                  NO...MUST BE CUU/VOL OR GENERIC
         TM    UCBSTAT,UCBONLI          IS THIS ONE ON-LINE?
         BO    GETINFO                  YES..
         B     UCBLOOP                  NO...IGNORE IT
CHK1VOL  DS    0H
         TM    SWITCH,ONEVOLID          WANT JUST ONE VOLID?
         BO    COMPVOL                  YES..
         TM    SWITCH,GENERIC           WANT GENERIC VOLIDS?
         BNO   COMPCUU                  NO...
         L     R9,EXLENGTH              YES..GET LENGTH FOR COMPARE
         EX    R9,EXCLC                 ONE OF THE VOLIDS WE WANT?
         BNE   UCBLOOP                  NO...
         B     GETINFO                  YES..
COMPCUU  DS    0H
         TM    SWITCH,RANGE             RANGE REQUESTED?
         BNO   CLCCUU                   NO...SEE IT WE WANT THIS ONE
         MVC   FWORD(3),UCBNAME         SAVE UCB CUU
         BAL   R9,CONVCUU               CONVERT TO BINARY FOR COMPARE
         L     R8,FWORD                 GET VALUE OF UCB CUU
         L     R9,BINCUU1               GET VALUE OF LOW CUU IN RANGE
         CR    R8,R9                    LOWER?
         BL    UCBLOOP                  YES..FORGET IT
         L     R9,BINCUU2               GET VALUE OF HIGH CUU IN RANGE
         CR    R8,R9                    HIGHER?
         BH    UCBLOOP                  YES..FORGET IT
         B     GETINFO                  MUST BE IN OUR RANGE
CLCCUU   DS    0H
         CLC   CUU(3),UCBNAME           IS IT THE CUU WE WANT?
         BNE   UCBLOOP                  NO...
         B     GETINFO                  YES..
COMPVOL  DS    0H
         CLC   UCBVOLI(6),VOLID         IS IT THE VOLID WE WANT?
         BNE   UCBLOOP                  NO...
* GET THE DEVICE CHARACTERISTICS FOR DISPLAY. WE WON'T GO OVERBOARD,  *
* JUST A FEW SIMPLE CHECKS...                                         *
* ALSO, GET THE NUMBER OF CYLINDERS ON THE DEVICE, BUT ONLY IF WE'RE  *
* NOT GETTING *ALL* DASD, AS THat DISPLAY DOESN'T HAVE ENOUGH ROOM TO *
* FIT THIS EXTRA INFORMATION IN (and don't do this for any off-line   *
* devices either!).                                                   *
* NB. AS WE GET THE FORMAT4 FROM THE VTOC THERE IS SOMETIMES AN       *
* EXTRA (CE?) CYLINDER ADDED ON - WE WILL TAKE THIS OFF IF WE CAN     *
* RECOGNIZE THAT IT IS THERE (EG 886 CYLINDERS ON A 3380 'D').        *
GETINFO  DS    0H
         TM    SWITCH,ALLUCBS           LOOKING FOR ALL VOLUMES?
         BO    CARRYON                  YES..SKIP THIS BIT
         TM    SWITCH,GETDASD           LOOKING FOR DASD?
         BNO   CARRYON                  NO...
         TM    UCBSTAT,UCBONLI          IS THIS ONE ON-LINE?
         BNO   CARRYON                  NO...
         MVC   26(8,R3),=C'(?????) '    YES..SET UP DEFAULT SIZE
         LSPACE UCB=(R4),               GET THE FORMAT4 DSCB...        X
               F4DSCB=F4DSCB,                                          X
               MSG=F4ERRMSG
         LTR   R15,R15                  LSPACE WORKED OK?
         BZ    CHKCYLS                  YES..
         ST    R15,FWORD                NO...SAVE RETURN CODE
         TM    SWITCH,MSGSENT           ALREADY DISPLAYED ERROR TEXT?
         BO    SHOWRC                   YES..
         OI    SWITCH,MSGSENT           NO...SET SO WE DON'T REPEAT IT
         TPUT  F4ERRMSG,30              DISPLAY ERROR TEXT
SHOWRC   DS    0H
         UNPK  DWORD(3),FWORD+3(2)      UNPACK RETURN CODE + 1 BYTE
         TR    DWORD(2),HEXTAB-240      XLATE TO PRINTABLE HEX
         MVC   26(5,R3),=C'RC=X"'       SET UP CONSTANT
         MVC   31(2,R3),DWORD           MOVE IN RC
         MVI   33(R3),C'"'
         B     CARRYON                  IGNORE REST OF THIS BIT
CHKCYLS  DS    0H
         MVC   26(7,R3),=X'4020206B202120'  YES..MOVE IN EDIT PATTERN
         LH    R1,F4DSCB+18             GET NUMBER OF CYLINDERS
         CH    R1,=H'886'               886 CYLS (3380 'D')?
         BE    TAKE1OFF                 YES..
         CH    R1,=H'1771'              1771 CYLS (3380 'E')?
         BE    TAKE1OFF                 YES..
         CH    R1,=H'2656'              2656 CYLS (3380 'K')?
         BE    TAKE1OFF                 YES..
         CH    R1,=H'1114'              1114 CYLS (3390 MODEL1)?
         BE    TAKE1OFF                 YES..
         CH    R1,=H'2227'              2227 CYLS (3390 MODEL2)?
         BE    TAKE1OFF                 YES..
         CH    R1,=H'3340'              3340 CYLS (3390 MODEL3)?
         BE    TAKE1OFF                 YES..
         CH    R1,=H'10018'             10018 CYLS (3390 MODEL9)?
         BE    TAKE1OFF                 YES..
         B     GETCYLS                  NO...
TAKE1OFF DS    0H
         BCTR  R1,0                     THERE IT GOES...
GETCYLS  DS    0H
         CVD   R1,DWORD                 CONVERT TO DECIMAL
         ED    26(7,R3),DWORD+5         EDIT IN NUMBER OF CYLINDERS
         MVI   26(R3),C'('              MAKE IT LOOK PRETTY
         MVI   33(R3),C')'
CARRYON  DS    0H
         TM    UCBFL1,UCBBOX            BOXED?
         BO    ITSBOXED                 YES..
         TM    UCBSTAT,UCBALOC          ALLOCATED?
         BO    ITSALLOC                 YES..
         TM    UCBSTAT,UCBONLI          ON-LINE?
         BO    ITSONLIN                 YES..
         B     ITSOFLIN                 NO...LET'S CALL IT OFF-LINE
ITSONLIN DS    0H
         MVC   9(7,R3),ONLINE           SET UP MSG
         B     GETVOLID                 GO AND GET VOLID
ITSOFLIN DS    0H
         MVC   9(7,R3),OFFLINE          SET UP MSG
         B     GETVOLID                 GO AND GET VOLID
ITSBOXED DS    0H
         MVC   9(7,R3),BOXED            SET UP MSG
         B     GETDEVTP                 GO AND GET DEVICE TYPE
ITSALLOC DS    0H
         MVC   9(7,R3),ALLOC            SET UP MSG
         B     GETVOLID                 GO AND GET THE VOLID
GETVOLID DS    0H
         MVC   17(6,R3),UCBVOLI         MOVE VOLID TO MSG LINE
GETDEVTP DS    0H
         MVC   4(4,R3),QUERIES          SET UP UNKNOWN DEVTYPE
         CLI   UCBTBYT4,X'0E'           3380? DASD
         BE    SET3380                  YES..
         CLI   UCBTBYT4,X'0F'           3390? DASD
         BE    SET3390                  YES..
         TM    UCBTBYT4,UCB3400         3400? TAPE
         BO    SET3400                  YES..
         TM    UCBTBYT4,UCB3480         3480? CART
         BO    SET3480                  YES..
         B     DISPLAY                  NO...LEAVE AS '????'
SET3380  DS    0H
         MVC   4(4,R3),=C'3380'
         B     CHKUCBS                  YES..
SET3390  DS    0H
         MVC   4(4,R3),=C'3390'
CHKUCBS  DS    0H                       YES..
         TM    SWITCH,ALLUCBS           DOING ALL UCBS?
         BO    DISPLAY                  YES..SKIP EXTRA INFO BIT
         MVC   51(13,R3),NONSHARE       DEFAULT TO NON-SHAREABLE
         TM    UCBTBYT2,UCBRR           SHAREABLE?
         BNO   CHECKPRI                 NO...
         MVC   51(13,R3),SHARE          YES..SET TO SHAREABLE
CHECKPRI DS    0H
         TM    UCBSTAB,UCBBPRV          PRIVATE?
         BNO   CHECKPUB                 NO...
         MVC   38(7,R3),PRIVATE         YES..SHOW THAT IN MSG
         B     DISPLAY
CHECKPUB DS    0H
         TM    UCBSTAB,UCBBPUB          PUBLIC?
         BNO   CHECKSTR                 NO...
         MVC   38(7,R3),PUBLIC          YES..SHOW THAT IN MSG
         B     DISPLAY
CHECKSTR DS    0H
         TM    UCBSTAB,UCBBSTR          STORAGE?
         BNO   DISPLAY                  NO...FORGET IT
         MVC   38(7,R3),STORAGE         YES..SHOW THAT IN MSG
         B     DISPLAY
SET3400  DS    0H
         MVC   4(4,R3),=C'3400'
         B     DISPLAY
SET3480  DS    0H
         MVC   4(4,R3),=C'3480'
* DISPLAY THE LINE (IF ITS FULL)...                                   *
DISPLAY  DS    0H
         MVC   0(3,R3),UCBNAME          MOVE CUU TO MSG LINE
         TM    SWITCH,ALLUCBS           DISPLAYING ALL UCBS?
         BNO   DISPLAY3                 NO...
         LA    R5,1(R5)                 BUMP COUNTER
         LA    R3,26(R3)                BUMP TO NEXT DISPLAY SLOT
         C     R5,=F'3'                 FULL LINE YET?
         BNE   UCBLOOP                  NO...GET NEXT UCB
         B     DISPLAY5                 YES..DISPLAY THE LINE
DISPLAY3 DS    0H
         MVI   MSG3LIN2,C' '            CLEAR OUT '|'S FOR SINGLE UNIT
         MVI   MSG3LIN3,C' '
DISPLAY5 DS    0H
         BAL   R9,FIRSTLIN              SEE IF WE WANT A HEADING
         OI    SWITCH,FOUND1            SHOW WE FOUND AT LEAST ONE
         TPUT  MSG3,MSG3L               DISPLAY INFO
         TM    SWITCH,ONECUU+ONEVOLID   DOING ONE VOLI/ONE CUU?
         BNZ   DOTRAIL                  NO...THAT'S IT THEN...
         MVI   MSG3,C' '                YES..CLEAR OUT LINE
         MVC   MSG3+1(MSG3L-1),MSG3
         MVI   MSG3LIN1,C'|'            PUT BACK THE '|'S
         MVI   MSG3LIN2,C'|'
         MVI   MSG3LIN3,C'|'
         MVI   MSG3LIN4,C'|'
         XR    R5,R5                    RESET COUNTER
         LA    R3,MSG3CUU1              AND POINTER
         B     UCBLOOP                  AND GET NEXT UCB
* END OF UCBS - DISPLAY LAST LINE IF NECESSARY...                     *
ENDUCBS  DS    0H
         TM    SWITCH,ONEVOLID          LOOKING FOR ONE VOLID?
         BO    NOVOLID                  YES..CAN'T HAVE FOUND IT THEN
         TM    SWITCH,ONECUU            LOOKING FOR JUST ONE CUU?
         BO    NOCUU                    YES..CAN'T HAVE FOUND IT THEN
         TM    SWITCH,FOUND1            FOUND ANYTHING FOR OTHER OPTS?
         BO    LASTONE                  YES..SKIP NEXT BIT
         TM    SWITCH,RANGE             LOOKING FOR CUU RANGE?
         BO    NOCUURNG                 YES..CAN'T HAVE FOUND ANY
         TM    SWITCH,GENERIC           LOOKING FOR GENERIC VOLIDS?
         BO    NOGENFND                 YES..CAN'T HAVE FOUND ANY
LASTONE  DS    0H
         CLC   MSG3CUU1(3),=C'   '      ANYTHING TO DISPLAY?
         BE    DOTRAIL                  NO...SKIP FINAL DISPLAY LINE
         TPUT  MSG3,MSG3L               YES..DISPLAY WHAT'S LEFT
DOTRAIL  DS    0H
         MVI   MSG6,C'-'
         MVC   MSG6+1(MSG6L-1),MSG6
         TPUT  MSG6,MSG6L               DISPLAY TRAILER LINE
* RETURN TO CALLER WITH RELEVANT RETURN CODE...                       *
RETURN   DS    0H
         L     R15,RETC                 LOAD RETURN CODE
         PR    ,                        RESTORE CALLER DATA, RETURN
* INVALID OR NO PARMS PASSED...                                       *
BADPARM  DS    0H
         TPUT  MSG1,MSG1L               TELL USER PARM IS BAD/MISSING
         B     SETRC8
* CUU SPECIFIED WAS NOT VALID HEX...                                  *
BADCUU   DS    0H
         MVC   MSG2TEXT,PRM2            SET CUU IN MSG
         TPUT  MSG2,MSG2L               TELL USER CUU IS BAD
         B     SETRC8
* VOLID SPECIFIED WAS NOT FOUND...                                    *
NOVOLID  DS    0H
         MVC   MSG5TEXT,VOLID           SET VOLID IN MSG
         TPUT  MSG5,MSG5L               NO MATCHING VOLID FOUND
         MVC   RETC,=F'4'               SET RC=4
         B     RETURN
* CUU SPECIFIED WAS NOT FOUND...                                      *
NOCUU    DS    0H
         MVC   MSG4TEXT,CUU             SET CUU IN MSG
         TPUT  MSG4,MSG4L               NO MATCHING UCB FOUND
         MVC   RETC,=F'4'               SET RC=4
         B     RETURN
* TOO MANY PARMS PASSED...                                            *
TOOMANY  DS    0H
         TPUT  MSG7,MSG7L               TOO MANY
         B     SETRC8
* INVALID CUU RANGE PASSED...                                         *
BADRANGE DS    0H
         MVC   MSG8TEXT,PRM2            SET CUU-CUU IN MSG
         TPUT  MSG8,MSG8L               BAD RANGE
         B     SETRC8
* NO MATCHES FOUND FOR A CUU-CUU RANGE...                             *
NOCUURNG DS    0H
         MVC   MSG9TEXT,PRM2            SET CUU-CUU IN MSG
         TPUT  MSG9,MSG9L               NO RANGE FOUND
         B     SETRC8
* NO MATCHES FOUND FOR A GENERIC VOLID...                             *
NOGENFND DS    0H
         MVC   MSGATEXT,VOLID           SET GENERIC VOLID IN MSG
         TPUT  MSGA,MSGAL               NO VOLID(S) FOUND
         B     SETRC8
* SET RC=8...                                                         *
SETRC8   DS    0H
         MVC   RETC,=F'8'               SET RC=8
         B     RETURN
* DISPLAY HELP INFO (IF PARM WAS A '?')...                            *
GIVEHELP DS    0H
         CLI   2(R2),C'?'               WAS HELP REQUESTED?
         BNE   BADPARM                  NO...
         STLINENO LINE=1                ENSURE DISPLAY STARTS @ LINE1
         TPUT  HELPMSG1,50              DISPLAY HELP INFO
         TPUT  HELPMSG2,50
         TPUT  HELPMSG3,1
         TPUT  HELPMSG4,50
         TPUT  HELPMSG3,1
         TPUT  HELPMSG5,50
         TPUT  HELPMSG6,50
         TPUT  HELPMSG7,50
         TPUT  HELPMSG8,50
         TPUT  HELPMSG9,50
         TPUT  HELPMSGA,50
         TPUT  HELPMSGB,50
         TPUT  HELPMSGC,50
         TPUT  HELPMSG3,1
         TPUT  HELPMSGD,50
         TPUT  HELPMSGE,50
         B     RETURN
*                    + + S U B R O U T I N E + + +                    *
* CONVERT CHARACTER CUU (EG '94F') INTO ITS BINARY EQUIVALENT. THIS   *
* IS SO THAT VALID RANGE COMPARISONS CAN BE MADE IF A RANGE OF CUU'S  *
* HAS BEEN SELECTED...                                                *
CONVCUU  DS    0H
         MVI   FWORD+3,X'00'            CLEAR OUT LAST BYTE
         TR    FWORD(3),TRTAB           CONVERT C'A->F' INTO X'A->F'
         XC    DWORD,DWORD              CLEAR OUT WORKAREA
         PACK  DWORD+5(3),FWORD(4)      REMOVE ZONES
         L     R8,DWORD+4               LOAD '000CUU00'
         SRL   R8,8                     REMOVE TRAILING '00'
         ST    R8,FWORD                 SAVE BINARY CUU VALUE
         BR    R9                       RETURN FROM SUBROUTINE
* SEE IF WE NEED HEADINGS... WE WILL USE 21 LINES AS A SCREENFUL.     *
FIRSTLIN DS    0H
         CH    R7,=H'21'                TIME FOR HEADINGS YET?
         BH    SETLINE                  YES..
         LA    R7,1(R7)                 NO...BUMP LINE COUNT
         BR    R9                       RETURN FROM ROUTINE
SETLINE  DS    0H
         TPUT  MSG6,MSG6L               DISPLAY HEADING LINE
         LA    R7,1                     RESET LINE COUNT
         BR    R9                       RETURN FROM ROUTINE
         LTORG                          LITERAL POOL
OFFLINE  DC    CL7'OFFLINE'
ONLINE   DC    CL7'ONLINE '
BOXED    DC    CL7'BOXED  '
ALLOC    DC    CL7'ALLOC  '
PRIVATE  DC    CL7'PRIVATE'
PUBLIC   DC    CL7'PUBLIC '
STORAGE  DC    CL7'STORAGE'
SHARE    DC    CL13'SHAREABLE'
NONSHARE DC    CL13'NON-SHAREABLE'
HEXTAB   DC    C'0123456789ABCDEF'
LETTERS  DC    X'0A0B0C0D0E0F'
NUMBERS  DC    X'00010203040506070809'
BINCUU1  DS    F
BINCUU2  DS    F
FWORD    DS    F
EXLENGTH DS    F
DWORD    DS    D
EXCLC    CLC   UCBVOLI(0),VOLID
F4DSCB   DS    CL96
F4ERRMSG DS    CL30
QUERIES  DC    CL4'????'
DA       DC    CL3'DA '
TA       DC    CL3'TA '
CUU      DS    CL3
VOLID    DS    CL6
PRM1     DC    CL2' '
PRM2     DC    CL7' '
SWITCH   DC    X'00'                    SWITCH FIELD
FOUND1   EQU   X'01'
GENERIC  EQU   X'02'
RANGE    EQU   X'04'
GETDASD  EQU   X'08'
MSGSENT  EQU   X'04'
ALLUCBS  EQU   X'10'
ONECUU   EQU   X'20'
ONEVOLID EQU   X'40'
ONLIN    EQU   X'80'
RETC     DS    F
*                0 1 2 3 4 5 6 7 8 9 A B C D E F
TRTAB    DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 0
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 1
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 2
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 3
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 4
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 5
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 6
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 7
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 8
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' 9
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' A
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' B
         DC    X'FF000000000000FFFFFFFFFFFFFFFFFF' C  (ABCDEF)
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' D
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF' E
         DC    X'00000000000000000000FFFFFFFFFFFF' F  (0123456789)
MSG1     DC    C'>>> PARM IS MISSING/INVALID - ENTER "?" FOR FORMAT...'
MSG1L    EQU   *-MSG1
MSG2     DC    C'>>> INVALID CUU SPECIFIED...(CUU).'
MSG2TEXT EQU   MSG2+29,3
MSG2L    EQU   *-MSG2
MSG3     EQU   *
MSG3LIN1 DC    CL2'| '
MSG3CUU1 DC    CL4' '
MSG3TYP1 DC    CL5' '
MSG3STA1 DC    CL8' '
MSG3VOL1 DC    CL7' '
MSG3LIN2 DC    CL2'| '
MSG3CUU2 DC    CL4' '
MSG3TYP2 DC    CL5' '
MSG3STA2 DC    CL8' '
MSG3VOL2 DC    CL7' '
MSG3LIN3 DC    CL2'| '
MSG3CUU3 DC    CL4' '
MSG3TYP3 DC    CL5' '
MSG3STA3 DC    CL8' '
MSG3VOL3 DC    CL7' '
MSG3LIN4 DC    CL1'|'
MSG3L    EQU   *-MSG3
MSG4     DC    C'>>> NO MATCHING UCB FOUND...(CUU).'
MSG4TEXT EQU   MSG4+29,3
MSG4L    EQU   *-MSG4
MSG5     DC    C'>>> NO MATCHING VOLID FOUND...(VOLSER).'
MSG5TEXT EQU   MSG5+31,6
MSG5L    EQU   *-MSG5
MSG6     DC    C'--CUU-TYPE-STATUS--VOLSER-----SIZE-----ATTRIBUTE---'
         DC    C'----SHAREABLE---------------'
MSG6L    EQU   *-MSG6
MSG6A    DC    C'--CUU-TYPE-STATUS--VOLSER---CUU-TYPE-STATUS--VOLSER'
         DC    C'---CUU-TYPE-STATUS--VOLSER--'
MSG7     DC    C'>>> TOO MANY PARMS PASSED...'
MSG7L    EQU   *-MSG7
MSG8     DC    C'>>> INVALID CUU RANGE SPECIFIED...(AAA-BBB).'
MSG8TEXT EQU   MSG8+35,7
MSG8L    EQU   *-MSG8
MSG9     DC    C'>>> NO MATCHING CUU''S FOUND...(AAA-BBB).'
MSG9TEXT EQU   MSG9+31,7
MSG9L    EQU   *-MSG9
MSGA     DC    C'>>> NO MATCHING VOLID''S FOUND...(VOLSER).'
MSGATEXT EQU   MSGA+33,6
MSGAL    EQU   *-MSGA
HELPMSG1 DC    CL50'                   Q U E R Y C M D'
HELPMSG2 DC    CL50'                   ==============='
HELPMSG3 DC    C' '
HELPMSG4 DC    CL50'    DISPLAY TAPE OR DASD INFO ON TSO. PARMS ARE:'
HELPMSG5 DC    CL50'         ??           DISPLAY ALL UNITS'
HELPMSG6 DC    CL50'         ?? CUU       DISPLAY ONLY UNIT "CUU"'
HELPMSG7 DC    CL50'         ?? VOLSER    DISPLAY ONLY UNIT "VOLSER"'
HELPMSG8 DC    CL50'         ?? ONLINE    DISPLAY ONLY ONLINE UNITS'
HELPMSG9 DC    CL50'         ?? XXX*      DISPLAY ONLY ONLINE UNITS'
HELPMSGA DC    CL50'                      WITH VOLIDS STARTING "XXX"'
HELPMSGB DC    CL50'         ?? AAA-BBB   DISPLAY ONLY UNITS WITHIN'
HELPMSGC DC    CL50'                      CUU RANGE OF "AAA" TO "BBB"'
HELPMSGD DC    CL50'         WHERE "??" IS:- DA FOR DASD'
HELPMSGE DC    CL50'                         TA FOR TAPE'
* UCB SCAN ROUTINE PARAMETERS...                                      *
PARMA    DC    A(WORKAREA)        ADDR OF 100 BYTE WORKAREA
PARMB    DC    A(DEVCLASS)        ADDR OF DEVCLASS BYTE
PARMC    DC    X'80',AL3(ADDRUCB) ADDR OF WORD TO HOLD UCB ADDR
UCBSCA   DS    F                  ADDR OF UCB SCAN ROUTINE (EX CVT)
ADDRUCB  DS    F                  ADDR OF NEXT UCB
WORKAREA DS    0D
         DC    XL100'00'
DEVCLASS DC    X'00'
UCBDEF   DSECT
         IEFUCBOB
         CVT   DSECT=YES
         END                     , END OF PROGRAM


M C Carson (UK)                                                               c Xephon 1995

