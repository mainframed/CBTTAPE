Calculating the capture ratio

The capture ratio is the relationship between the amount of CPU
really used and the amount of CPU measured for RMF in each
Performance Group. The following program uses the current SMF
file (SYS1.MANx) and parameters defining the date and time
limits as input and calculates the ratio using SMF records
generated for RMF. Figure 1 shows an example of the report that
is produced: it includes the input parameters, the capture ratio of
each period, and the capture ratio total.

JCL TO COMPILE, LINK, AND GO THE CAPTRTIO PROGRAM

//..... JOB
// EXEC ASMHCLG
//C.SYSPRINT DD SYSOUT=*
//C.SYSIN DD DSN=SYS1.USERSRC(CAPTRTIO),DISP=SHR
//L.SYSPRINT DD SYSOUT=*
//G.IMPRE DD SYSOUT=W
//SMFIN DD DSN=SYS1.MAN1,DISP=SHR,AMP='BUFND=50'
//SYSIN DD *
DATE=93293,93293,TIME=0900,1015
//

    LISTING OF CONTROL CARDS

DATE=93289,93295,TIME=0000,2359

 REC READ FROM SYSIN:         1


  DAY     HOUR      MILLSEC CPU    MILLSEC CAPT  PERC

93.293 09:00:00          205681          184005    89

93.293 09:15:00          210832          184979    87

93.293 09:30:00          209827          185350    88

93.293 09:45:01          196009          171272    87

93.293 10:00:00          240924          208137    86

93.293 10:15:00          235740          201354    85

REC READ:    39177  NO INTERVALS:   6 SUM PERC:      522  TOT PERC:    87

Figure 1: Output from the CAPTRTIO program

CAPTRTIO SOURCE CODE

*******************************************************
* THIS PROGRAM COMPUTES THE "CAPTURE RATIO".         *
* "CAPTURE RATIO" IS THE RELATIONSHIP BETWEEN CPU    *
* ACCOUNT BY RMF VS TOTAL CPU USED                  *
* READ SYS1.MANX AND OBTAIN INFORMATION FROM         *
* SMF'S REGISTERS 70(X'46') AND 72(X'48').           *
*******************************************************
*        SETUP                      <-------------
CAPTRTIO CSECT
         STM   14,12,12(13)
         LR    3,15
         USING CAPTRTIO,3
         ST    13,SAVE+4
         LA    12,SAVE
         ST    12,8(13)
         LR    13,12
         B     PASADA
         DC    CL8'CAPTRTIO'
SAVE     DS    18F
*     OPEN FILES                 <-------------
PASADA   EQU   *
         OPEN  SYSIN
         OPEN  (DCBSALE,(OUTPUT))
         PUT   DCBSALE,TITSYSIN
*     ACCEPT SYSIN               <-------------
OTROSYS  EQU   *
         GET   SYSIN,AREASYS
         AP    CONTLEO,=P'1'               ADD TO COUNTER
         MVC   LINAREA(80),AREASYS         MOVE TO OUTPUT
         PUT   DCBSALE,LINEA1
*     VALIDATION ROUTINE         <-------------
         CLC   AREASYS(5),=C'DATE='        DATE ?
         BNE   ESTIME                      NO. GO TO ESTIME ?
         TRT   AREASYS+5(5),TABLA          NUMERIC ?
         BNZ   ERRORSYS                    NO. GO TO ERROR
         CLI   AREASYS+10,C','             ANOTHER PARAMETER ?
         BNE   OTROSYS                     NO. GO TO OTHER SYSIN
         TRT   AREASYS+11(5),TABLA         NUMERIC ?
         BNZ   ERRORSYS                    NO. GO TO ERROR
         PACK  DIAINI,AREASYS+5(5)         CONVERT TO
         PACK  DIAFIN,AREASYS+11(5)                   DECIMAL
         B     OTROSYS                     GO TO OTHER SYSIN
ESTIME   EQU   *
         CLC   AREASYS(5),=C'TIME='        TIME ?
         BNE   ERRORSYS                    NO. GO TO ERROR
         TRT   AREASYS+5(4),TABLA          NUMERIC ?
         BNZ   ERRORSYS                    NO. GO TO ERROR
         PACK  INICTIME(3),AREASYS+5(4)    CONVERT TO DECIMAL
         MVN   INICTIME+2(1),=C'0'            WITHOUT SIGN
         CLI   AREASYS+9,C','              ANOTHER PARAMETER ?
         BNE   OTROSYS                     NO. GO TO OTHER SYSIN
         TRT   AREASYS+10(4),TABLA         NUMERIC ?
         BNZ   ERRORSYS                    NO. GO TO ERROR
         PACK  FINTIME(3),AREASYS+10(4)    CONVERT TO DECIMAL
         MVN   FINTIME+2(1),=C'0'          WITHOUT SIGN
         B     OTROSYS                     GO TO OTHER SYSIN
ERRORSYS EQU   *
         PUT   DCBSALE,LINEAE               MSG. ERROR
         B     EOF
FINSYSIN EQU   *                            EOF OF SYSIN
         CLOSE SYSIN
         MVC   LINOLEI,MASK1                PRINT  NUMBER
         ED    LINOLEI,CONTLEO                 OF
         PUT   DCBSALE,LINEAO                    RECORDS READ
         ZAP   CONTLEO,=P'0'                        FROM
         PUT   DCBSALE,TITULO                           SYSIN
         OPEN  SMFIN                        OPEN SMF
*     MAIN ROUTINE               <-------------
LEO      EQU   *
         GET   RPL=RPLSMFIN                 READ SMF
         LTR   15,15                        IS IT OK?
         BNZ   ERROR                        NO. GO TO ERROR
         LA    4,AREASMF                    POINT TO START I/O AREA
         AP    CONTLEO,=P'1'                ADD 1 TO COUNTER
         CLC   4(6,4),=C'SMFEOF'            IS IT LAST RECORD ?
         BE    EOF                          YES. GO TO END OF FILE
         CP    10(4,4),DIAINI               COMPARE DAY PARAMETER
         BL    LEO                          LOW. GO TO READ SMF
         CP    10(4,4),DIAFIN               COMPARE DAY PARAMETER
         BH    EOF                          HIGH. GO TO READ SMF
         CLI   5(4),X'46'                   IS IT RECORD TYPE 70
         BE    ES70                         YES. GO TO PROCESS
         CLI   5(4),X'48'                   IS IT RECORD TYPE 72
         BE    ES72                         YES. GO TO PROCESS
         B     LEO                          NO. GO TO READ SMF
*  TYPE 70 PROCESS               <-------------
ES70     EQU   *
         MVC   FW1,28(4)                    LOAD REG5 WITH START
         L     5,FW1                            RMF PRODUCT
         AR    5,4                                  SECTION
         CP    10(4,5),INICTIME             COMPARE TIME PARAMETER
         BL    LEO                          LOW. GO TO READ SMF
         CP    10(4,5),FINTIME              COMPARE TIME PARAMETER
         BH    LEO                          HIGH. GO TO READ SMF
         CP    10(4,5),ANTTIME              TWO RECS 70 WITH = TIME
         BE    SALTO70                      YES. JUMP
         BAL   12,CALCULO                   NO. LINK TO
SALTO70  EQU   *
         MVC   FW1,44(4)                    LOAD REG5 WITH START
         L     5,FW1                            RMF CPU
         AR    5,4                                  SECTION
         MVC   FW1,48(4)                    LOAD REG8 WITH END
         LH    8,FW1                            RMF CPU
         MH    8,FW1+2                              SECTION
         LA    9,0(8,5)                     REG9 WITH CPU IDENT ADDR.
*  CPU TIME USED                 <-------------
         ZAP   TOTWAIT,=P'0'                TOTWAIT = 0
OTRACPU  EQU   *
         MVC   DW1,0(5)                     MOVE TOTAL CPU WAIT TIME
         L     6,DW1+4                      REG6 WITH LAST 4 BYTES
         SRA   6,12                         BIT 51 = 1 MICROSECOND
*                                     REG6 = WAIT TIME IN MICROSECONDS
         L     7,DW1                        REG7 WITH FIRST 4 BYTES
         SLA   7,20                         SYNCHRONIZE REG6 & REG7
         AR    7,6                    REG7 = WAIT TIME IN MICROSECONDS
         SR    6,6                          CLEAR REG6
         D     6,=F'1000'             REG7 = WAIT TIME IN MILLISECONDS
         CVD   7,DW1                        CONVERT TO PACKED
         AP    TOTWAIT,DW1                  ADD TO TOTAL WAIT TIME
         AH    5,48(4)                      ADD LENGTH OF CPU SECTION
         CR    5,9                          END OF CPU SECTION ?
         BL    OTRACPU                      NO. OTHER CPU
         MVC   FW1,28(4)                    LOAD REG5 WITH START
         L     5,FW1                            RMF PRODUCT
         AR    5,4                                  SECTION
         ZAP   PERIODOM,=P'0'               MOVE TO PERIODOM
         MVC   PERIODOM+6(1),18(5)               MINUTES OF
         DP    PERIODOM,=P'10'                      LENGTH OF INTERVAL
         ZAP   PERIODOS,=P'0'               MOVE TO PERIODOS
         MVC   PERIODOS+6(1),19(5)               SECONDS OF
         DP    PERIODOS,=P'10'                      LENGTH OF INTERVAL
         MP    PERIODOM(6),=P'60'           CONVERT MINUTES TO SECONDS
         AP    PERIODOS(6),PERIODOM(6)      ADD TO SECONDS
         MP    PERIODOS(6),=P'1000'        CONVERT SECONDS TO MILLISECS
         AP    PERIODOS(6),20(2,5)          ADD MILLISECONDS
         LH    5,50(4)                      LOAD NUMBER OF CPU SECTIONS
         CVD   5,DW1                        CONVERT TO PACKED
         MP    PERIODOS(6),DW1+6(2)         MULTIPLY BY MILLISECONDS
         ZAP   TOTCPU,PERIODOS(6)           MOVE INTERVAL TIME
         SP    TOTCPU,TOTWAIT               SUBTRACT WAIT TIME
*  TOTCPU = CPU USED             <-------------
         B     LEO                          GO TO READ SMF
*  CPU TIME CAPTURED             <-------------
ES72     EQU   *
         MVC   FW1,28(4)                    LOAD REG5 WITH START
         L     5,FW1                            RMF PRODUCT
         AR    5,4                                  SECTION
         CP    10(4,5),INICTIME             COMPARE TIME PARAMETER
         BL    LEO                          LOW. GO TO READ SMF
         CP    10(4,5),FINTIME              COMPARE TIME PARAMETER
         BH    LEO                          HIGH. GO TO READ SMF
         CP    10(4,5),ANTTIME              TWO RECS 70 WITH = TIME
         BE    SALTO72                      YES. JUMP
         BAL   12,CALCULO                   NO. LINK TO
SALTO72  EQU   *
         MVC   FW1,36(4)                    LOAD REG5 WITH START
         L     5,FW1                            RMF WORKLOAD CONTROL
         AR    5,4                                  SECTION
         MVC   FW1,92(5)                    LOAD REG8 WITH CPU TIME(MS)
         L     8,FW1                            FOR SU MULTIPLY BY 16
         SRA   8,4                          DIVIDE BY 16
         SR    6,6                          CLEAR REG6
         L     7,=F'1000000'                LOAD REG7 WITH SU
         DR    6,8                                    PER SECOND
         LR    9,7                          SAVE SU/SEC IN REG9
         PACK  DW1,30(2,5)                  LOAD REG8 WITH
         CVB   8,DW1                            CPU COEFFICIENT
         LR    6,7                          REG6 WITH SU/SEC
         MR    6,8                          REG7 = SU/SEC * COEFF CPU
         ST    7,SUTCB                      SAVE IN SU OF TCB
         LR    7,9                          RESTORE SU/SEC
         PACK  DW1,38(2,5)                  LOAD REG8 WITH
         CVB   8,DW1                            SRB COEFFICIENT
         LR    6,7                          REG6 WITH SU/SEC
         MR    6,8                          REG7 = SU/SEC * COEFF SRB
         ST    7,SUSRB                      SAVE IN SU OF SRB
         MVC   FW1,44(4)                    LOAD REG5 WITH START
         L     5,FW1                            RMF PERFORMANCE GROUP
         AR    5,4                                  SECTION
         MVC   FW1,48(4)                    LOAD REG9 WITH END
         LH    8,FW1                            RMF PERFORMANCE
         MH    8,FW1+2                              GROUP
         LA    9,0(8,5)                               SECTION
OTROPER  EQU   *
         MVC   FW1,28(5)                    LOAD REG7 WITH CPU TOTAL
         L     7,FW1                            SERVICE UNITS X
         MH    7,=H'1000'                           MILLISECONDS
         SR    6,6                          CLEAR REG6
         D     6,SUTCB                      REG7 WITH TCB CPU
         CVB   6,TOTCAPT                    REG6 WITH CPU TOTAL
         AR    7,6                          ADD TCB CPU TO
         CVD   7,TOTCAPT                        CPU TOTAL
         MVC   FW1,44(5)                    LOAD REG7 WITH SRB TOTAL
         L     7,FW1                            SERVICE UNITS X
         MH    7,=H'1000'                           MILLISECONDS
         SR    6,6                          CLEAR REG6
         D     6,SUSRB                      REG7 WITH SRB CPU
         CVB   6,TOTCAPT                    REG6 WITH CPU TOTAL
         AR    7,6                          ADD SRB CPU TO
         CVD   7,TOTCAPT                        CPU TOTAL
*  TOTCAPT = CPU CAPTURED        <-------------
         AH    5,48(4)                      ADD LENGTH WORKLOAD SECTION
         CR    5,9                          END OF SECTIONS?
         BL    OTROPER                      NO. GO TO OTHER PERF. GROUP
         B     LEO                          YES. GO TO READ SMF
*  PRINT RECORDS                 <-------------
CALCULO  EQU   *
         CLC   ANTTIME,=X'9999999F'         FIRST TIME?
         BE    CALCINIT                     YES. GO TO INIT
         UNPK  DW1,ANTDIA                   MOVE
         MVZ   DW1,=10C'0'                      DAY
         MVC   LIN3DIA(2),DW1+3                    TO
         MVC   LIN3DIA+3(3),DW1+5                    PRINT
         MVI   LIN3DIA+2,C'.'                             AREA
         UNPK  DW1,ANTTIME                  MOVE
         MVZ   DW1,=10C'0'                      TIME
         MVC   LIN3HORA+6(2),DW1+6                  TO
         MVC   LIN3HORA+3(2),DW1+4                    PRINT
         MVC   LIN3HORA(2),DW1+2                           AREA
         MVI   LIN3HORA+2,C':'
         MVI   LIN3HORA+5,C':'
         MVC   LIN3CPU,MASK2                MOVE TOTCPU TO
         ED    LIN3CPU,TOTCPU                    PRINT AREA
         MVC   LIN3CAPT,MASK2               MOVE TOTCAPT TO
         ED    LIN3CAPT,TOTCAPT                  PRINT AREA
         CP    TOTCPU,=P'0'                 IS TOTCPU = 0 ?
         BNE   HAY70                        NO. GO PRINT
         MVC   LIN3CPU,=C'NO HAY 70'        YES.
         MVC   LIN3PORC,=C'NO '                  MOVE ERROR MSG
         B     IMPRIME
HAY70    EQU   *
         CP    TOTCAPT,=P'0'                 IS TOTCAPT = 0 ?
         BNE   HAY72                         NO. GO PRINT
         MVC   LIN3CPU,=C'NO HAY 72'         YES.
         MVC   LIN3PORC,=C'NO '                  MOVE ERROR MSG
         B     IMPRIME
HAY72    EQU   *
         MP    TOTCAPT,=P'100'               PERCENT OF CAPTURED
         DP    TOTCAPT,TOTCPU+2(6)               BY USED
         MVC   LIN3PORC,MASK1                MOVE TO
         ED    LIN3PORC,TOTCAPT                  PRINT AREA
         AP    CANTPRO,=P'1'                 ADD 1 TO INTERVAL COUNT
         AP    SUMPRO,TOTCAPT(2)             ADD % TO PERCENT COUNT
IMPRIME  EQU   *
         PUT   DCBSALE,LINEA3                PRINT
CALCINIT EQU   *
         ZAP   TOTCPU,=P'0'                  CLEAR TOTCPU
         ZAP   TOTCAPT,=P'0'                 CLEAR TOTCAPT
         MVC   ANTTIME,10(5)                 SET TIME
         MVC   ANTDIA,14(5)                  SET DAY
         BR    12
ERROR    EQU   *
         WTO   'ERROR IN GET                                 '
         DC    X'0000'                       ABEND BY 0C1
*  END OF FILE PROCESSING        <-------------
EOF      EQU   *
         MVC   LIN2LEI,MASK1                 MOVE NUMBER OF RECORDS
         ED    LIN2LEI,CONTLEO                   READ TO PRINT AREA
         MVC   LIN2PERI,MASK1                PRINT NUMBER OF INTERVALS
         ED    LIN2PERI,CANTPRO                  TO PRINT AREA
         MVC   LIN2SPRO,MASK2                PRINT SUM OF PERCENTS TO
         ED    LIN2SPRO,SUMPRO                   PRINT AREA
         CP    CANTPRO,=P'0'                 NUMBER OF INTERVALS = 0 ?
         BNE   DIVIDO                        NO. GO TO DIVIDE
         AP    CANTPRO,=P'1'                 YES. SET NUMBER TO 1
DIVIDO   EQU   *
         DP    SUMPRO,CANTPRO                MOVE
         MVC   LIN2PROT,MASK1                    CAPTURE RATIO
         ED    LIN2PROT,SUMPRO                           TO PRINT AREA
         PUT   DCBSALE,LINEA2                PRINT
         CLOSE SMFIN
         CLOSE DCBSALE
         L     13,SAVE+4                     RESTORE
         LM    14,12,12(13)                          REGS
         SR    15,15                                      AND
         BR    14                                             BYE
*  SMF FILE DEFINITION           <-------------
SMFIN    ACB   DDNAME=SMFIN,EXLST=LIST,MACRF=(ADR)
LIST     EXLST EODAD=EOF
RPLSMFIN RPL   ACB=SMFIN,AREA=AREASMF,AREALEN=32767,OPTCD=(ADR),       X
               RECLEN=LENSMF,ARG=RBASMF
PTRSMF   DS    F
LENSMF   DS    F
RBASMF   DC    F'0'
*  SYSIN FILE DEFINITION         <-------------
SYSIN    DCB   DSORG=PS,MACRF=(GM),DEVD=DA,DDNAME=SYSIN,BLKSIZE=80,    X
               RECFM=F,LRECL=80,EODAD=FINSYSIN
AREASYS  DC    CL80' '
*  PRINT FILE DEFINITION
DCBSALE  DCB   DSORG=PS,MACRF=(PM),DEVD=DA,DDNAME=IMPRE,BLKSIZE=133,   X
               RECFM=FA
TITSYSIN DC    CL133'1    LISTING OF CONTROL CARDS'
TITULO   DC    CL133'1  DAY     HOUR       MILLSEC CPU    MILLSEC CAPT X
                PERC'
LINEAO   DS    0CL133
         DC    C'0 REC READ FROM SYSIN: '
LINOLEI  DC    CL9' '
         DC    CL100' '
LINEA2   DS    0CL133
         DC    C'0 REC READ: '
LIN2LEI  DC    CL9' '
         DC    C'  NO INTERVALS: '
LIN2PERI DC    CL6' '
         DC    C' SUM PERC: '
LIN2SPRO DC    CL13' '
         DC    C'  AVG PERC: '
LIN2PROT DC    CL6' '
         DC    CL50' '
LINEA3   DC    C'0 '
LIN3DIA  DC    CL6' '
         DC    C' '
LIN3HORA DC    CL8' '
         DC    C' '
LIN3CPU  DC    CL15' '
         DC    C' '
LIN3CAPT DC    CL15' '
         DC    C'  '
LIN3PORC DC    CL4' '
         DC    CL90' '
LINEA1   DC    C'0'
LINAREA  DC    CL132' '
LINEAE   DC    C'0'
LINERRS  DC    CL132'   PARAMETER ERROR'
CANT     DC    CL10' '
         DC    CL100' '
CONTLEO  DC    PL4'0'                        RECORDS READ COUNTER
DIAINI   DC    PL4'0'                        INITIAL DAY
DIAFIN   DC    PL4'9999999'                  FINAL DAY
TOTCAPT  DS    D                             CPU CAPTURED TOTAL
TOTCPU   DS    D                             CPU USED TOTAL
TOTWAIT  DS    D                             CPU WAIT TOTAL
SUTCB    DC    F'0'                          SERVICE UNITS OF TCB
SUSRB    DC    F'0'                          SERVICE UNITS OF SRB
FW1      DC    F'0'                          WORK FULLWORD
INICTIME DC    X'0000000F'                   INITIAL TIME CONSTANT
FINTIME  DC    X'0240000F'                   FINAL TIME CONSTANT
ANTTIME  DC    X'9999999F'                   BEFORE TIME CONSTANT
ANTDIA   DC    CL4' '                        BEFORE DAY CONSTANT
DW1      DS    D                             WORD DOUBLEWORD
SUMPRO   DC    PL6'0'                        PERCENT TOTAL
CANTPRO  DC    PL3'0'                        COUNT OF INTERVALS
PERIODOS DS   CL8                            SECONDS OF INTERVAL
PERIODOM DS   CL8                            MINUTES OF INTERVAL
PERIODOC DS   CL8                            MILLISECONDS OF INTERVAL
MASK1    DC    X'402020202020202020'         MASKS
MASK2    DC    X'402020202020202020202020202020'
TABLA    DS    0CL256                        VALIDATION TABLE
         DC    240X'FF'
         DC    10X'00'
         DC    6X'FF'
* WARNING. DON'T MOVE THE FOLLOWING LINES. THEY PREVENT ADDRESSING
*          PROBLEMS.
        LTORG
AREASMF DS    129CL256
        END


Nestor Lacabe
Systems Programmer
Sevel Argentina SA (Argentina)                                                   c Xephon 1994

