Checking for RESTART before submitting JCL


MSUB SOURCE CODE

/* --------------------------------------- */
/* This Rexx Checks for RESTART  parameter */
/* in the JCL file. If you are in EDIT &   */
/* there is a RESTART parameter, the jcl   */
/* is not submitted. You have to use MSUB R*/
/* to submit. If in TSO, it will prompt    */
/* --------------------------------------- */
/* Naming Conventions Used:-               */
/* Rexx Commands and Functions have the    */
/*      first letter in Upper Case         */
/* TSO/ISPF Commands in Upper Case         */
/* User data ( Variables/Constants/Labels) */
/* have a combination Of Upper & Lower case*/
/*     and                                 */
/* Alphanumeric user data starts with x    */
/* Numeric user data      starts with n    */
/* Switches / Flags       start  with s    */
/* --------------------------------------- */
Trace O
Arg xTsoName xTsoParm .
'ISREDIT MACRO (xParm) PROCESS'
If Rc=0 Then Call SubInEdit
  Else Call SubInTso xTsoName xTsoParm
Exit nRc
/* ------------------ */
/*  SubInEdit         */
/* ------------------ */
SubInEdit:
nRc=0
xParm=Translate(Strip(xParm))
Select
 When xParm='' Then Nop
 When Abbrev('IGNORE',xParm) Then Nop
 When Abbrev('RESTART',xParm) Then Nop
 Otherwise Do
  zedsmsg='Invalid Paramter Fnd'
  zedlmsg='Valid Parameters with MSUB are blank, R(ESTART) & I(GNORE)'
  'ISPEXEC SETMSG MSG(ISRZ001)'
  Return
  End
 End
sSubmit='N'
Select
 When xParm\='' & Abbrev('IGNORE',xParm) Then Nop
 Otherwise Do
  'ISREDIT (nLeftBnd nRiteBnd)=BOUNDS'
  'ISREDIT BOUNDS'
  'ISREDIT (nDispRow,nDispCol) = DISPLAY_LINES'
  'ISREDIT (nLastRow) = LINENUM .ZLAST'
  'ISREDIT EXCLUDE ALL'
  'ISREDIT FIND // 1 2 ALL'
  'ISREDIT FIND JOB FIRST NX'
  If Rc\=0 Then Do
    'ISREDIT RESET'
    zedsmsg='No JOB Card Found'
    zedlmsg='No JOB card is present in the JCL - Job Not Submitted'
    'ISPEXEC SETMSG MSG(ISRZ001)'
    Return
    End
  'ISREDIT (nRow) = LINENUM .ZCSR'
  Do Forever
   If nRow>nLastRow Then Leave
   'ISREDIT (xData) = LINE' nRow
   nRow=nRow+1
   Select
    When Left(xData,3)='//*' Then Nop
    When Left(xData,2)\='//' Then Nop
    Otherwise Do
     If Word(xData,2)='JOB' Then Call CheckSpaces
       Else xJclParms=Word(xData,2)
     If Pos('RESTART',xJclParms)>0 Then Do
       If xParm\='' & Abbrev('RESTART',xParm) Then sSubmit='Y'
         Else Do
          'ISREDIT RESET'
          zedsmsg='RESTART Found-Use MSUB R'
          zedlmsg='RESTART is present in Line' nRow-1 '- Use MSUB I or MSUB R(ESTART) to Submit the Jcl'
          'ISPEXEC SETMSG MSG(ISRZ001)'
          nRc=1
          Return
          End                            /* End Do        */
       End                               /* End If        */
     End                                 /* End Otherwise */
    End                                  /* End Select    */
   If Right(Strip(xJclParms),1)=',' Then Nop
    Else Do
     'ISREDIT FIND " JOB " NEXT NX'
     If Rc\=0 Then Leave
     'ISREDIT (nRow) = LINENUM .ZCSR'
     End
   End  /* End Do Forever */
  'ISREDIT BOUNDS = (nLeftBnd nRiteBnd)'
  End
 End
'ISREDIT RESET'
If xParm\='' & Abbrev('RESTART',xParm) & sSubmit='N' Then Do
  zedsmsg='No RESTART fnd-Use MSUB'
  zedlmsg='No RESTART is present in the JCL - Either use MSUB or add RESTART parameter'
  'ISPEXEC SETMSG MSG(ISRZ001)'
  nRc=1
  Return
  End
'ISREDIT SUBMIT'
If Rc=0 Then Do
  nRc=1
  If nDispRow>1 Then 'ISREDIT LOCATE ' nDispRow
  End
 Else Do
  zedsmsg='Job Not Submitted'
  zedlmsg='Job Could Not Be Submitted - Return Code = ' Rc
  'ISPEXEC SETMSG MSG(ISRZ001)'
  nRc=0
  End
Return
/* ------------------ */
/*  SubInTso          */
/* ------------------ */
SubInTso:
nRc=0
Arg xDsName xParm
If xDSName='' Then Do
  Say Center('Please Enter The Dataset Name Which You Want To Submit',76)
  Say Center('TSO Rules Apply ie If a DSN is not in quotes, your userid',76)
  Say Center('will be prefixed to the DSName',76)
  Say Center('* * * * * * * * * * * * * * * ',76)
  Pull xDSName .
  If xDSName='' Then Do
    Say '*Error* No Name Entered - Program Aborted'
    Exit
    End
  End
xAvail=SYSDSN(xDSName)
If xAvail\='OK' Then Do
  Say '*Error*' xDSName 'Does Not Exist - Program Aborted'
  Exit
  End
xParm=Translate(Strip(xParm))
Select
 When xParm='I' Then Nop
 Otherwise Call CheckRestart
 End
Address TSO 'SUBMIT' xDsName
If Rc\=0 Then DO
  Say '*Error* Job Could Not Be Submitted - Return Code = ' Rc
  Exit
  End
Return
/* ------------------ */
/*  CheckRestart      */
/* ------------------ */
CheckRestart:
Address 'TSO'
xMsgSt=MSG('OFF')
'FREE DD(JCLDSN)'
xAskNb=MSG(xMsgSt)
'ALLOC DD(JCLDSN) DSN('xDsName') SHR KEEP'
If Rc\=0 Then Do
  Say '*Error* Unable To Allocate' xDsName
  Say '        Program Aborted - Return Code From Allocate = ' Rc
  Exit
  End
'EXECIO * DISKR JCLDSN (STEM XREC.'
If Rc\=0 Then Do
  Say '*Error* Unable To Read' xDsName
  Say '        Program Aborted - Return Code From Read = ' Rc
  Exit
  End
'EXECIO 0 DISKR JCLDSN (FINIS'
nRec.0=xRec.0
xMsgSt=MSG('OFF')
'FREE DD(JCLDSN)'
xAskNb=MSG(xMsgSt)
If xParm\='' & Abbrev('RESTART',xParm) Then sSubmit='N'
 Else sSubmit='Y'
sRestart='N'
Do I=1 By 1 Until I=nRec.0
 xData=xRec.I
 Select
  When Left(xData,3)='//*' Then Iterate
  When Left(xData,2)\='//' Then Iterate
  When Left(xData,2)='//' & Word(xData,2)='JOB' Then Call CheckSpaces
  When sJobCnt='Y' Then xJclParms=Word(xData,2)
  Otherwise Iterate
  End
 If Pos('RESTART',xJclParms)>0 Then Do
   sRestart='Y'
   If xParm\='' & Abbrev('RESTART',xParm) Then Do
     sSubmit='Y'
     Leave I
     End
    Else Do
     Say Strip(Translate(xData,' ','~'),'T',' ')
     Say Center('The Above Card Is From Your JCL',76)
     Say Center('It Has A RESTART Parameter',76)
     Say Center('Type S(UBMIT) To Submit Otherwise Jcl Will Not Be Submitted',76)
     Say Center('-----------------------------------------------------------',76)
     Pull xAns .
     If xAns\='' & Abbrev('SUBMIT',xAns) Then Nop
       Else Do
        Say '*Note*  JCL Not Submitted'
        Exit
        End
      End
     End
    Else Do
     If Right(xJclParms,1)=',' Then sJobCnt='Y'
       Else sJobCont='N'
     End
 End
If sRestart='N' & xParm\='' & Abbrev('RESTART',xParm) Then Do
  Say Center('This JCL does not have a RESTART parameter',76)
  Say Center('Type S(UBMIT) To Submit Otherwise Jcl Will Not Be Submitted',76)
  Say Center('-----------------------------------------------------------',76)
  Pull xAns .
  If xAns\='' & Abbrev('SUBMIT',xAns) Then Nop
    Else Do
     Say '*Note*  JCL Not Submitted'
     Exit
     End
  End
Return
/* ------------------ */
/*  CheckSpaces       */
/* ------------------ */
CheckSpaces:
If Pos("'",xData)>0 Then Do
  nPos1=Pos("'",xData)
  nPos2=Pos("'",xData,nPos1+1)
  If nPos2=0 Then nPos2=nPos1+1
  xJobName=Translate(Substr(xData,nPos1,nPos2-nPos1+1),'~',' ')
  xData=Overlay(xJobName,xData,nPos1)
  End
xJclParms=Word(xData,3)
Return


Moyeen Ahmed Khan
Senior Systems Designer
Manulife Financial (Canada)                   c Xephon 1996

