Protecting catalogued tape files

A solution to the problem of protecting catalogued tape files was presented in
MVS Update, April 1987.  We employed the same basic idea of locating catalogued
tape files by a user type 3 SVC combined with ZAPs in the OPEN and EOV modules.
Our tape protection mechanism includes additional features.  One of these
features addresses the 17 bytes limitation of the tape header's DSN field.


THE PROBLEM

On the one hand the DSNs of tape datasets often exceed 17 characters (in
particular the DSNs of generation data groups), and on the other hand all
catalogued tapes should be write-protected.


THE SOLUTION

The FL1SYSCD field of a tape header 1 label is a constant.  In MVS SP1.3 this
field's content is: IBM OS/VS 370.  Using FL1SYSCD as an extension to the DSN
field extends the DSN field length up to a maximum of 30 characters.  The
leading characters are stored in the FL1SYSCD field, while the last 17
characters are stored by OPEN and EOV routines in the DSN field.


IMPLEMENTATION

A type 3 user SVC moves up to 13 characters to the FL1SYSCD field of HDR1.  If
the DSN is shorter than 18 characters, the same field is spaced out.


USER SVC 241 (IGC0024A)

***********************************************************************
*              SVC 241  (TYPE 3)                                      *
*  ROUTINE FOR MOVING BEGINNING OF DSN - WHEN MORE THAN 17 -          *
*  FROM JFCB TO FL1SYSCD FIELD IN HDR1 LABEL.                         *
*  EXECUTED BY IFG0196T (CSECT OF IGC0001I)                           *
*       AND BY IFG0552D (CSECT OF IGC0005E)                           *
*  PROCESSING:  1.  FOR TEMPORARY DATASETS THE SVC ZEROS THE CREATION *
*                   DATE.                                             *
*               2.  IF DSN LENGTH IS LESS THAN 18 CHARACTERS THE      *
*                   FL1SYSCD CONTAINS BLANKS.                         *
*               3.  IF DSN LENGTH IS LONGER THAN 30 CHARACTERS THE    *
*                   FL1SYSCD CONTAINS BLANKS.  THE DATASET IS NOT     *
*                   PROTECTED FROM UNINTENDED REWRITE !!!!!!!!!!      *
*               4.  IF DSN LENGTH IS 18 TO 30 CHARACTERS THE          *
*                   FL1SYSCD CONTAINS THE FIRST BYTES OF THE DSN      *
*                   COMPLETING THE LAST 17 BYTES OF DSN WHICH ARE     *
*                   PLACED BY STANDARD OPEN ROUTINES IN FL1ID FIELD.  *
*  REGISTERS USAGE:                                                   *
*                     R2 - WORK REGISTER                              *
*                     R3 - WORK REGISTER                              *
*                     R4 - OPEN WORKAREA                              *
*                     R5 - CURRENT RB (STANDARD IN SVCS TYPE 3 OR 4)  *
*                     R6 - WORK REGISTERS                             *
*                     R11 - OPEN WORKAREA                             *
*                     R14 - RETURN ADDRESS (STANDARD IN SVCS )        *
***********************************************************************
SVC241   START
START    BALR  R11,0
         USING *,R11
         USING OPENWA,R4
         LA    R5,32(R5)               SVRB REGS SAVE AREA ADR
         L     R4,16(R5)               RESTORE REG 4 - BEG OF WORKAREA
****** RESTORE RUINED INSTRUCTION IN IFG0196T AND IFG0552D
         MVC   FL1SYSCD,SYSCODE
         MVC   FL1SYSCD,=CL13' '       BLANK FL1SYSCD
         CLI   JFCBDSNM+17,X'40'       DSN LONGER THAN 17 ?
         BE    RETURN                  IF NOT, BRANCH
         CLC   JFCBDSNM(3),=C'SYS'     TEMPORARY DATASET ?
         BNE   CKLENGTH                IF NOT, BRANCH
         CLI   JFCBDSNM+9,C'T'         TEMPORARY DATASET ?
         BNE   CKLENGTH                IF NOT, BRANCH
         CLI   JFCBDSNM+18,C'A'        TEMPORARY DATASET ?
         BNE   CKLENGTH                IF NOT, BRANCH
         MVI   JFCBCRDT,X'00'          ZERO CREATION DATE
         B     RETURN
CKLENGTH LA    R2,JFCBDSNM+17          DSN+17
         LR    R3,R2
         LA    R2,1(R2)                19TH BYTE OF DSN
         LA    R6,13                   CHECK 7 BYTES (18+12+1=30+1)
LOOP     CLI   0(R2),X'40'             FIRST BALNK AFTER DSN ?
         BE    CKMOVE                  IF YES, BRANCH
         LA    R2,1(R2)                NEXT BYTE
         BCT   R6,LOOP
CKMOVE   SR    R2,R3                   DSN LENGTH - 17
         BCTR  R2,0                    LENGTH - 1 FOR EX COMMAND
         LTR   R2,R2                   NEGATIVE LENGTH ?
         BM    RETURN                  IF YES, BRANCH
         CH    R2,=H'12'               NAME LONGER THAN 30 CHARS ?
         BH    RETURN                  IF YES, BRANCH
         EX    R2,MV
RETURN   BR    R14
MV       MVC   FL1SYSCD(0),JFCBDSNM    MOVE BEG OF DSN
SYSCODE  DC    C'IBM OS/VS 370'
         LTORG
         DC    C'END SVC241'
OPENWA   DSECT
         IECDSECT
         END

The SVC is invoked by ZAPs in IFG0196T (OPEN) and IFG0552D (EOV).  These ZAPs
are applied by usermods in the SMP/E format and are currently installed on MVS
SP1.3.4 at put level 8508.

++USERMOD(MLEOV2D)  /*
    MLM END OF VOLUME CHANGES IN MODULE IGC0005E.
    CSECT:    IFG0552D.
    CHANGE:  1. CALL SVC 241 (HDR1:COMPLETE DSN, ZEROING CREATION DATE
                IN TEMPORARY FILES).
    CONSIDERATIONS:
          1. THE LPALIB DD CARD SHOULD POINT TO SYS1.MVSTEST.
          2. BEFORE APPLYING THE USERMOD, IGC0005E AND ITS ALIASES
             SHOULD BE COPIED FORM LPALIB TO MVSTEST.
          3. APPLYING IBM PTFS OR APARS WILL CAUSE ID CHECK.
             TO PREVENT THE ID CHECK THE FOLLOWING SHOULD BE DONE
             WHEN APPLYING IBM'S MAINTENANCE:
             A. DELETE THE USERMOD FROM SMPCDS. USE SMP UCLIN STATEMENT
                TO DELETE IT.
             B. APPLY IBM CORRECTION.
             C. COPY CORRECTED VERSION FROM LPALIB TO MVSTEST
             D. CHECK IF THE USERMOD SHOULD NOT BE CHANGED
                BECAUSE OF IBM'S PTF OR APAR !!!!!!!!.
             E. IF THE ZAP OF THIS USERMOD SHOULD BE CHANGED, CHANGE IT.
             F. APPLY THE USERMOD.
                                   */.
++VER(Z038) FMID(HDQ1102) REQ(MLSV241).
++ZAP(IFG0552D).
  NAME IFG0552D
 VER 01A8 D20C403C31E6
 REP 01A8 0AF1             SVC 241
 REP 01AA 47000000         CNOP

++USERMOD(MLOPN6T)  /*
    MLM OPEN CHANGES IN MODULE IGC0001I.     OPEN.
    CSECT:    IFG0196T.
    CHANGE:  CALL SVC 241 (COMPLETE DSN IN HDR1, ZEROING CREATION DATE
                           IN TEMPORARY FILE)
    CONSIDERATIONS:
          1. THE LPALIB DD CARD SHOULD POINT TO SYS1.MVSTEST.
          2. BEFORE APPLYING THE USERMOD, IGC0001I AND ITS ALIASES
             SHOULD BE COPIED FORM LPALIB TO MVSTEST.
          3. APPLYING IBM PTFS OR APARS WILL CAUSE ID CHECK.
             TO PREVENT THE ID CHECK THE FOLLOWING SHOULD BE DONE
             WHEN APPLYING IBM'S MAINTENANCE:
             A. DELETE THE USERMOD FROM TARGET ZONE . USE SMP UCLIN
                STATEMENT TO DELETE IT.
             B. APPLY IBM CORRECTION.
             C. COPY CORRECTED VERSION FROM LPALIB TO MVSTEST
             D. CHECK IF THE USERMOD SHOULD NOT BE CHANGED
                BECAUSE OF IBM'S PTF OR APAR !!!!!!!!.
             E. IF THE ZAP OF THIS USERMOD SHOULD BE CHANGED, CHANGE IT.
             F. APPLY THE USERMOD.
                                   */.
++VER(Z038) FMID(JDQ1110) REQ(MLSV241) PRE(UZ44387).
++ZAP(IFG0196T).
  NAME IFG0196T
 VER 01E4 D20C403C36EB
 REP 01E4 0AF1             SVC 241
 REP 01E6 4700,0000        CNOP

Another type 3 user SVC attempts to locate the DSN of the tape file in the
catalog.  This SVC combines the FL1SYSCD and the DSN field of tape header 1
label, before issuing an SVC 26 (LOCATE).


Avi Rosenthal
Manager of Systems Programming Group
Malam Systems (Israel)   © Malam Systems 1987




























