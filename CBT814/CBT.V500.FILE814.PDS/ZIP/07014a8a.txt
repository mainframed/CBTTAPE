Executing operator commands from a program

The following subroutine has been developed to execute any
operator command from a program.  This command is useful in
re-routing any SYSOUT datasets or jobs to be executed for
automated operations.  The length of the operator command
must not exceed 126 bytes and the calling program must be
linked 'AC=1' and reside in an authorized library.  We have
further restricted this command to the RACF operation group
user.  The calling sequence of this subroutine is as follows:

      CALL COMMND(X)

where COMMND is the entry name of the subroutine and X is
a character string containing the command to be executed.  The
length of the character string must not exceed 126 bytes.

COMMND01 CSECT
*   TAKE A CHARACTER STRING FROM THE ARGUMENT AND USE SVC34 TO
*   STICK IT INTO MVS AS AN OPERATOR COMMAND. USES MODESET SO
*   CALLING PROGRAM MUST BE AUTHORIZED.
         ENTRY COMMND
COMMND   STM R14,R12,12(R13)        SAVE CALLERS REGISTERS
         LR   R12,R15               INITIALIZE BASE REGISTER
         USING COMMND,R12           ESTABLISH ADDRESSABILITY
         LA    R14,SAVEAREA         MY SAVEAREA
         ST    R14,8(R13)           LINK TO CALLERS
         ST    R13,SAVEAREA+4       LINK TO MINE
         LA    R13,SAVEAREA         POINT R13 TO MY SAVE AREA
         LR    R2,R1                SAVE REGISTER 1
         L     R5,0(R1)             LOAD PARAMETER ADDRESS
         S     R2,=F'4'             GET OFFSET TO 2NDARY PARM LIST
         L     R2,0(R2)             GET OFFSET TO 2NDARY PARM LIST
         L     R3,0(R2,R1)          GET A(L'STRING1)
         L     R3,0(R3)             GET L'STRING1
LNTHTEST LTR   R3,R3                TEST LENGTH - POSITIVE?
         BNP   EXIT                 BR IF NO PARM SPECIFIED.
         C     R3,=F'126'           CHECK FOR MAXIMUM OF 126 BYTES
         BH    EXIT                 GREATER, IGNORE THE COMMAND
         MVI   XCMD,C' '            ZERO THE COMMAND AREA FOR THE
         MVC   XCMD+1(125),XCMD     MACRO MGCR
         BCTR  R3,0                 SUBTRACT 1 FROM LENGTH FOR MOVE
         EX    R3,MOVE              MOVE COMMAND FROM PARAMETER LIST
         L     R7,PSAAOLD-PSA       ADDRESS CURRENT ASCB
         USING ASCB,R7
         L     R4,ASCBASXB          ADDRESS ASXB
         USING ASXB,R4
         L     R3,ASXBSENV          ADDRESS ACEE
         USING ACEE,R3
         TM    ACEEFLG1,ACEERACF    RACF DEFINED USER?
         BO    CHKSTATS             YES, CHECK FOR OPERATION STATUS
         WTO   '** YOU MUST BE RACF USER TO EXECUTE THIS PROGRAM **',  *
               ROUTCDE=(11)
         B     EXIT
CHKSTATS TM    ACEEFLG1,ACEEOPER    RACF OPERATIONS STATUS USER?
         BO    NOCHECK
         WTO   '** YOU ARE NOT AUTHORIZED TO EXECUTE THIS PROGRAM **', *
               ROUTCDE=(11)
         B     EXIT
         DROP  R7,R4,R3
NOCHECK  MODESET KEY=ZERO       SUPERVISOR MODE
         SR    R0,R0
         MGCR  INPUTC           INTERNAL START OR REPLY COMMAND
         MODESET KEY=NZERO      RESET SUPERVISOR MODE
*        END OF PROGRAM ... CLOSE DOWN AND RETURN.
EXIT     DS    0H                       NORMAL RETURN.
         L     R13,SAVEAREA+4
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
*        CONSTANTS AND DATA AREAS ETC....
SAVEAREA DC    18F'0'
MOVE     MVC   XCMD(0),0(R5)          MOVE COMMAND FROM AN ARGUMENT
         DS    0F
INPUTC   DC    X'80'
         DC    AL1(TOKENC-INPUTC)
         DC    X'0000'
XCMD     DS    CL126
TOKENC   DS    CL1
         LTORG
         IHAPSA
         IHAASCB
         IHAASXB
         IHAACEE
         END


Ashwani Puri
Systems Programmer
Western Atlas International (UK)          © Ashwani Puri 1990

