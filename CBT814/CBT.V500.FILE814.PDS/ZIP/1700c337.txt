MSGCLASS and DEST for started tasks

//STEP    EXEC ASMHCL,PARM.ASM='NODECK,LOAD,RENT',
//    PARM.LKED='LIST,NCAL,LET,XREF,RENT,REUS',
//    OUTAT='*'
//ASM.SYSLIB DD DSN=SYS1.SMPMTS,DISP=SHR
//           DD DSN=SYS1.HASPSRC,DISP=SHR
//           DD DSN=SYS1.MACLIB,DISP=SHR
//           DD DSN=SYS1.MODGEN,DISP=SHR
//ASM.SYSIN  DD *
         TITLE 'JES2 EXIT NR 2     JOB CONTROL STATEMENT SCAN'
*********************************************************************
*        JES2 USER EXIT FOR JOB STATEMENT SCAN                      *
*  |||||  JES2 MAIN TASK ENVIRONMENT   ||||                         *
*     ENTRY POINT = JESEX002                                        *
*     REGISTER USAGE ON ENTRY OF EXIT :                             *
*                    R0   = 0 FOR INITIAL JOB STATEMENT             *
*                         = 4 FOR SUBSEQ. JOB CONTIN. STAT.         *
*                    R1   ADDRESS OF 3-WORD PARAMETER LIST :        *
*                         WORD 1 : POINTER TO STATEMENT IMAGE       *
*                         WORD 2 : POINTER TO THE EXIT FLAG BYTE    *
*                                  (RXITFLAG) IN THE PCE            *
*                         WORD 3 : POINTER TO JCTXWRK FIELD IN JCT  *
*                    R2-9 NOT USED                                  *
*                    R10  ADDRESS OF THE JOB'S JCT                  *
*                    R11  ADDRESS OF THE JES2 HCT                   *
*                    R12  NOT USED                                  *
*                    R13  ADDRESS OF THE PCE                        *
*                    R14  RETURN ADDRESS                            *
*                    R15  ENTRY ADDRESS                             *
*     REGISTER USAGE ON RETURN :                                    *
*                    R0-14 THE SAME AS ON ENTRY                     *
*                             (RESTORED BY $RETURN MACRO)           *
*                    R15   RETURN CODE                              *
*                          = 0  CONTINUE NORMAL HASPRDR PROCESSING  *
*                          = 4  CONTINUE NORMAL HASPRDR PROCESSING, *
*                          EXCEPT FOR ADDITIONAL EXITS ASSOCIATED   *
*                               WITH THIS EXIT                      *
*                          = 8  CANCEL JOB                          *
*                          =12  PURGE JOB                           *
*********************************************************************
         COPY  $HASPGBL
JESEX002 $MODULE $BUFFER,                                           *
               $HASPEQU,                                            *
               $JCT,                                                *
               $NHD,                                                *
               $JQE,                                                *
               $HCCT,                                               *
               $PCE,                                                *
               $CAT,                                                *
               $HCT,                                                *
               $JOE,                                                *
               $MIT,                                                *
               RPL
         TITLE 'JES2 EXIT NO 2     JOB CONTROL STATEMENT SCAN'
*********************************************************************
*     ENTRY AND LINKAGE                                            **
*********************************************************************
EXIT2    $ENTRY  CSECT=YES,BASE=R12    EXIT ROUTINE ENTRY POINT
         $SAVE
         LR    R12,R15                 LOAD BASE REGISTER
         LR    R8,R1                   PARMLIST
         USING PCE,R13
         USING HCT,R11
         USING JCT,R10
         SPACE 2
*********************************************************************
*     NO PROCESSING IF JCT DOES NOT EXISTS                         **
*********************************************************************
         L     R7,8(R8)
         LTR   R7,R7                   JCT AVAILABLE
         BZ    RETURN0                 IF NO,RETURN.
         SPACE 2
*********************************************************************
*     SET MSGCLASS TO X                                            **
*********************************************************************
         L     R6,0(R8)             LOAD A(JCL TEXT AREA) IN R6,
         LA    R6,2(R6)             SKIP '//'
* STC STARTING WITH ABCDE
         CLC   0(5,R6),=C'ABCDE'    SEARCH FOR STCNAME
         BE    CLASSX
*********************************************************************
*     CHANGE DESTINATION TO DEST10                                 **
*********************************************************************
* STC STARTING WITH VWXYZ
         CLC   0(5,R6),=C'VWXYZ'    SEARCH FOR STCNAME
         BE    CDEST10
         B     RETURN0
CDEST10  EQU   *
       $DEST DEST=DEST10,CONV=SCHAR,NODE=$OWNNODE,ERRET=RETURN0,LEN=8
         ST    R1,JCTPROUT          SET DEST=DEST10
         B     CLASSX
CLASSX   MVI   JCTMCLAS,C'X'        AND SET MSGCLASS=X
         B     RETURN0
DEST10   DC    CL8'DEST10  '        SET DESTINATION
         LTORG
*********************************************************************
*       RETURN TO CALLER                                            *
*********************************************************************
RETURN0  SR    R15,R15
         $RETURN RC=(R15)
         $MODEND
         END
//LKED.SYSLMOD DD DSN=SYS1.JES2.LOAD,DISP=SHR
//LKED.SYSIN DD *
   NAME JESEX002(R)
/*


Dirk Moelans
Systems Programmer
Groep Boerenbond (Belgium)                                   c Xephon 1995

