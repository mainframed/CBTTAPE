A WTO exit for automated operations

Many different versions of WTO exits have appeared in MVS Update in
the past.  My goal in writing a WTO exit was to make it look more like
an automated operations tool for those who do not have or cannot
afford a real automated tool.  My exit was written and tested on
MVS/SP 2.2.0 but should be applicable to any MVS/XA system that
supports the newer IEAVMXIT WTO exit (rather than the older
IEECVXIT).

It is designed to run in 31-bit addressing mode and uses extended
SQA for its work area.  Since you can specify very large values for
extended SQA, it is much less likely for the exit to exhaust common
storage if problems occur.  My version was modelled after a version
of the old IEECVXIT WTO exit which has circulated on various SHARE
and Connecticut Bank and Trust tapes and which was sometimes
referred to as 'R2D2'.  Even though my version was written to allow
message suppression, I suggest that any messages that are to be
suppressed are specified in PARMLIB member MPFLSTxx.  The MPF list
affords the ease of changing messages via operator command rather
than re-assembling a user exit.

There are some points to be aware of when using this exit.  As with
all WTO exits, you should not issue a message from the exit that
would cause the exit to be invoked recursively.  Even though the exit
is written in 31-bit mode, the buffer for SVC 34 (command
processing) must be obtained from 24-bit storage (this is a
restriction of SVC 34).  The buffer used for command response/WTOR
reply is limited to 126 bytes, which is an SVC 34/35 restriction.
The exit schedules an SRB into another address space in order to use
that target address space for issuing commands or replying to
WTORs.  The target address space must be resident when an SRB is
scheduled and preferably the address space should be non-swappable.
To that end, I use the LINKLIST Lookaside Address Space (LLA) to
schedule the SRB against.

This exit checks for message suppression/deletion last, so trying to
change or add routing or descriptor codes at the same time as
performing suppression/deletion will have no effect.  Multiple values
may be specified for the TYPE operand of the IEECODE macro such as
TYPE=(SUP,CMD), TYPE=(ADD,REPLY), etc.  The DELAY operand
specifies the number of seconds to wait before issuing a command or
reply.  This is useful in allowing operators time to respond manually
to a WTOR.  While DELAY is allowed for TYPE=CMD, it should not be
necessary unless you expect to have a flood of messages which would
then cause a flood of commands to be issued.

The exit checks that a reply is still outstanding before it attempts to
reply to it.  The exit requires replies in the REPLY operand to be
coded REPLY='R xx,reply text'.  The xx is the reply-id which will be
filled in by the exit.  This reply-id, in this specific location in the
reply text, will be used to check that the WTOR is still outstanding.
For debugging purposes, there is a flag called EXITFLAG which is used
to provide a flow trace of module execution.  It is useful, if
examining a dump, to determine what routines in the exit have
executed.  This flag is part of one large area of GETMAINed storage
from ESQA which is mapped by the WORKAREA DSECT.

One of the nicer features in this program, which I have not seen in
any other program of its type, is the ability to scan for optional text
strings in a message before taking action.  This feature is used when
responding to WTOR IEF238D, since this message may or may not be
issued with the 'WAIT' option.  It is also used to respond to WTO
ERB100I, since after the RMF ZZ session is started, I start an RMF
Monitor III session.

This program is by no means an exhaustive example of what the WTO
exit can do.  Features which might be added, but which I have not
undertaken, are to allow the exit to issue multiple commands when
TYPE=CMD is specified (I use it to start a started task which issues
multiple commands), the ability to highlight or 'lowlight' specific
messages, and the ability to notify specific users of problems (such
as issuing SEND commands to systems programmers when CHPIDs,
storage, or processors go off-line).  These are all things which a true
automated operations tool may do, but it would not be a very
extensive task to add such features to this exit.

One drawback to this exit versus a truly automated operations utility
is that the automated utilities get all messages whereas the WTO
exit does not.  Another problem is that the exit does not receive
control for operator commands.  An automated tool could handle
commands such as not allowing people to purge all output queues
globally or to issue MVS RESET commands for certain jobs.  If you are
looking for this level of control, then an automated operations tool is
really the place to look.

IEAVMXIT TITLE ' PROCESS CHANGES AND REPLIES TO WTO MESSAGES '
IEAVMXIT AMODE 31
IEAVMXIT RMODE ANY
         MACRO
&LABEL   IEECODE &ROUT=,&DESC=,&ID=********,&TYPE=,&CMD=,&DELAY=0,     X
               &SCAN=
         LCLC  &CD(5),&MSGID
         LCLA  &I,&N,&MLEN,&TYP,&ELEN,&CLEN,&DLY,&SLEN
         LCLB  &B(32),&T(8)
         GBLA  &SW
         AIF   (&SW NE 0).EQUDONE
         PUSH  PRINT
         PRINT NOGEN
ADD      EQU   X'01'                      ADD CODES
CHG      EQU   X'02'                      CHANGE CODE
REPLY    EQU   X'04'                      REPLY TO WTOR
CMD      EQU   X'08'                      ISSUE COMMAND
SUP      EQU   X'10'                      SUPPRESS MSG FROM CONSOLE
DELETE   EQU   X'20'                      DELETE MESSAGE
NOHC     EQU   X'40'                      SUPPRESS MSG FROM HARDCOPY
END      EQU   X'80'                      END OF LIST INDICATOR
         POP   PRINT
&SW      SETA  1
.EQUDONE ANOP
&I       SETA  1
.DCHK    AIF   (T'&DESC EQ 'O').RCHK
&N       SETA  &DESC(&I)
&I       SETA  &I+1
         AIF   (&N GE 1 AND &N LE 16).ASSIGND
         MNOTE 8,'&DESC(&I) IS INVALID DESCRIPTOR - IGNORED'
         AGO   .NXTD
.ASSIGND ANOP
&B(&N)   SETB  1
.NXTD    AIF   (&I LE N'&DESC).DCHK
&I       SETA  1
.RCHK    AIF   (T'&ROUT EQ 'O').ASSIGNI
&N       SETA  &ROUT(&I)
&I       SETA  &I+1
         AIF   (&N GE 1 AND &N LE 16).ASSIGNR
         MNOTE 8,'ROUT(&I) IS INVALID ROUTE - IGNORED'
         AGO   .NXTR
.ASSIGNR ANOP
&B(&N+16) SETB 1
.NXTR    AIF   (&I LE N'&ROUT).RCHK
.ASSIGNI AIF   (K'&ID LT 9).ASSIGNL
         MNOTE 4,'&ID IS INVALID LENGTH ID - TRUNCATED TO 8'
&MSGID   SETC  '&ID'(1,8)
&MLEN    SETA  8
         AGO   .ASSIGNT
.ASSIGNL ANOP
&MSGID   SETC  '&ID'
&MLEN    SETA  K'&ID
.ASSIGNT ANOP
         AIF   (T'&TYPE NE 'O').TYPECHK
         MNOTE 4,'TYPE VALUE IS MISSING - DUMMY ASSUMED'
         AGO   .DUMTYP
.TYPECHK ANOP
&I       SETA  1
.TLOOP   AIF   (&I GT N'&TYPE).GEN
         AIF   ('&TYPE(&I)' NE 'ADD'   AND '&TYPE(&I)' NE 'CHG'    AND X
                '&TYPE(&I)' NE 'REPLY' AND '&TYPE(&I)' NE 'CMD'    AND X
                '&TYPE(&I)' NE 'SUP'   AND '&TYPE(&I)' NE 'DELETE' AND X
                '&TYPE(&I)' NE 'NOHC'  AND '&TYPE(&I)' NE 'END'  ).TERR
         AIF   ('&TYPE(&I)' EQ 'ADD').SET8
         AIF   ('&TYPE(&I)' EQ 'CHG').SET7
         AIF   ('&TYPE(&I)' EQ 'REPLY').SET6
         AIF   ('&TYPE(&I)' EQ 'CMD').SET5
         AIF   ('&TYPE(&I)' EQ 'SUP').SET4
         AIF   ('&TYPE(&I)' EQ 'DELETE').SET3
         AIF   ('&TYPE(&I)' EQ 'NOHC').SET2
         AIF   ('&TYPE(&I)' EQ 'END').SET1
.SET8    ANOP
&T(8)    SETB  1
&I       SETA  &I+1
         AGO   .TLOOP
.SET7    ANOP
&T(7)    SETB  1
&I       SETA  &I+1
         AGO   .TLOOP
.SET6    ANOP
&T(6)    SETB  1
&I       SETA  &I+1
         AGO   .TLOOP
.SET5    ANOP
&T(5)    SETB  1
&I       SETA  &I+1
         AGO   .TLOOP
.SET4    ANOP
&T(4)    SETB  1
&I       SETA  &I+1
         AGO   .TLOOP
.SET3    ANOP
&T(3)    SETB  1
&I       SETA  &I+1
         AGO   .TLOOP
.SET2    ANOP
&T(2)    SETB  1
&I       SETA  &I+1
         AGO   .TLOOP
.SET1    ANOP
&T(1)    SETB  1
&I       SETA  &I+1
         AGO   .TLOOP
.GEN     ANOP
         AIF   (&T(1) AND (&T(2) OR &T(3) OR &T(4) OR &T(5) OR         X
                &T(6) OR &T(7) OR &T(8))).ENDERR
&I       SETA  1
&CLEN    SETA  0                        SET DEFAULT TO 0
&SLEN    SETA  0                        SET DEFAULT TO 0
         AIF   (T'&CMD EQ 'O').SKIPCMD
&CLEN    SETA  K'&CMD-2                 LESS 2 FOR QUOTES
.SKIPCMD ANOP
         AIF   (T'&SCAN EQ 'O').SKIPSCN
&SLEN    SETA  K'&SCAN-2
.SKIPSCN ANOP
&ELEN    SETA  (8+1+4+2+2+4+&CLEN+&SLEN)
.*             MSG+TYPE+FLAGS+DESC+ROUT+DELAY+CMD+SCAN
&CD(&I)   SETC  '&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)&B(8)'
&CD(&I+1) SETC  '&B(9)&B(10)&B(11)&B(12)&B(13)&B(14)&B(15)&B(16)'
&CD(&I+2) SETC  '&B(17)&B(18)&B(19)&B(20)&B(21)&B(22)&B(23)&B(24)'
&CD(&I+3) SETC  '&B(25)&B(26)&B(27)&B(28)&B(29)&B(30)&B(31)&B(32)'
&CD(&I+4) SETC  '&T(1)&T(2)&T(3)&T(4)&T(5)&T(6)&T(7)&T(8)'
&LABEL   DC    CL8'&MSGID'              MESSAGE ID
         DC    BL1'&CD(5)'              TYPE INDICATORS
         DC    AL1(&MLEN,&CLEN,&ELEN,&SLEN) LENGTH FIELDS
         DC    BL2'&CD(1)&CD(2)'        DESC CODES
         DC    BL2'&CD(3)&CD(4)'        ROUT CODES
         AIF   (T'&DELAY NE 'N').DLYERR
&DLY     SETA  &DELAY
         AIF   (&DLY GT 3600).DLYERR2
         DC    AL4(&DLY*100)            DELAY IN 1/100TH SECONDS
         AIF   (NOT (&T(5) OR &T(6))).NOCMD
         AIF   (&T(5) AND &T(6)).NOTBOTH
         AIF   (T'&CMD EQ 'O').CMDERR
         AIF   (&CLEN GT 126).CMDLERR
         DC    CL&CLEN&CMD
.NOCMD   AIF   (T'&SCAN EQ 'O').DONE
         DC    CL&SLEN&SCAN
         AGO   .DONE
.CMDERR  MNOTE 8,'TYPE=REPLY/CMD SPECIFIED BUT CMD= OPERAND MISSING'
         MEXIT
.TERR    MNOTE 8,'TYPE=&TYPE(&I) IS AN INVALID TYPE'
         MEXIT
.ENDERR  MNOTE 8,'TYPE=END MUTUALLY EXCLUSIVE WITH ANY OTHER OPERANDS'
         MEXIT
.CMDLERR MNOTE 8,'CMD= TEXT IS GREATER THAN 126 BYTES LONG'
         MEXIT
.NOTBOTH MNOTE 8,'TYPE=REPLY AND TYPE=CMD ARE MUTUALLY EXCLUSIVE'
         MEXIT
.DLYERR2 MNOTE 8,'DELAY=&DELAY IS OVER ONE HOUR, NOT ALLOWED'
         MEXIT
.DLYERR  MNOTE 8,'DELAY=&DELAY IS AN INVALID NUMERIC VALUE'
.DONE    MEND
*****    INITIALIZATION PROCESSING    ****
IEAVMXIT CSECT
         SAVE  (14,12),,IEAVMXIT-&SYSDATE
         LR    R12,R15                  LOAD R12 W/BASE ADDRESS
         USING IEAVMXIT,R12             ESTABLISH ADDRESSABILITY
         LR    R6,R1                    SAVE R1 PARM REG
         GETMAIN RU,LV=WORKLEN,SP=245,LOC=ANY GET STORAGE FOR WORKAREA
         LR    R2,R1                    LOAD R2 W/A(AREA) FOR MVCL
         LA    R3,WORKLEN               LOAD R3 W/WORKAREA LENGTH
         SR    R5,R5                    CLEAR R5 FOR MCVL PAD + FROM LEN
         MVCL  R2,R4                    CLEAR WORK AREA
         ST    R13,4(R1)                PERFORM
         ST    R1,8(R13)                   SAVEAREA
         LR    R13,R1                         CHAINING
         USING WORKAREA,R13             ESTABLISH ADDRESSABILITY
         L     R2,0(R6)                 LOAD R2 W/A(USER EXIT PARM LIST)
         USING CTXT,R2                  ESTABLISH ADDRESSABILITY
         L     R3,CTXTTXPJ              LOAD R3 W/PTR TO MAJOR LINE TEXT
         USING CTXTATTR,R3              ESTABLISH ADDRESSABILITY
         LA    R8,MSGTABL               POINT TO TABLE OF MESSAGES
         USING TABLENT,R8               ESTABLISH ADDRESSABILITY
*****    CHECK FOR MESSAGE ID'S TO BE HANDLED    *****
TABLLOOP TM    TYPE,END                 AT END OF TABLE
         BO    RETURN                   YES, GO RETURN TO CALLER
         SR    R6,R6                    CLEAR FOR IC
         IC    R6,MSGLEN                INSERT LENGTH OF MSGID
         BCTR  R6,R0                    SUBTRACT 1 FOR EXECUTE OF CLC
         EX    R6,CLCMSGID              EXECUTE CLC INSTRUCTION
         BE    FOUNDMSG                 MSGID MATCH, GO SET UP PARM
         IC    R6,ENTLEN                ELSE, GET LENGTH OF TABLE ENTRY
         LA    R8,0(R6,R8)              POINT TO NEXT ENTRY
         B     TABLLOOP                 GO CHECK NEXT ENTRY
CLCMSGID CLC   MSGID(0),CTXTTMSG        IS WTO MSGID = TABLE MSGID
*****    SCAN FOR OPTIONAL TEXT    *****
FOUNDMSG MVC   DELAY,DELAYTIM           SAVE DELAY TIME FOR CMD/REPLY
         LA    R1,TABLENT               LOAD A(CURRENT TABLE ENTRY)
         SR    R6,R6                    CLEAR FOR IC
         IC    R6,ENTLEN                INSERT TOTAL ENTRY LENGTH
         AR    R1,R6                    ADD ENTRY LENGTH TO A(ENTRY)
         SR    R15,R15                  CLEAR FOR ICM
         ICM   R15,1,SCANLEN            LOAD SCAN LENGTH IF ANY
         BZ    CHKADD                   NO SCAN DATA, SKIP CHECK
         SR    R1,R15                   SUBTRACT LENGTH OF SCAN DATA
         SR    R6,R6                    CLEAR FOR IC
         IC    R6,MSGLEN                INSERT MESSAGE ID LENGTH
         LA    R5,CTXTTMSG(R6)          LOAD A(SCAN START)
         LA    R6,1                     LOAD INCREMENT
         LA    R7,CTXTTMSG              LOAD A(MESSAGE)
         AH    R7,CTXTTLEN              ADD LENGTH OF MESSAGE
         SR    R7,R15                   SUBTRACT SCAN DATA LENGTH
         BCTR  R15,R0                   DECREMENT FOR CLC
SCANLOOP EX    R15,CLCSCAN              EXEC CLC OF SCAN DATA VS TABLENT
         BE    CHKADD                   FOUND, THEN CONTINUE
         BXLE  R5,R6,SCANLOOP           NOT FOUND, DO SCAN AGAIN
         B     RETURN                   END OF LOOP, THEN RETURN
CLCSCAN  CLC   0(0,R1),0(R5)            LOOK FOR SCAN DATA IN MSG
*****    CHECK FUNCTION TYPE    *****
CHKADD   TM    TYPE,ADD                 SHOULD WE ADD CODES
         BZ    CHKCHG                   NO, GO CHECK NEXT
         BAL   R9,ADDCODE               ELSE, PERFORM ROUTINE
CHKCHG   TM    TYPE,CHG                 SHOULD WE CHANGE CODES
         BZ    CHKREPLY                 NO, GO CHECK NEXT
         BAL   R9,CHGCODE               ELSE, PERFORM ROUTINE
CHKREPLY TM    TYPE,REPLY               SHOULD WE REPLY
         BZ    CHKCMD                   NO, GO CHECK NEXT
         BAL   R9,RPLYCODE              ELSE, PERFORM ROUTINE
CHKCMD   TM    TYPE,CMD                 SHOULD WE ISSUE COMMAND
         BZ    CHKSUP                   NO, GO CHECK NEXT
         BAL   R9,CMDCODE               ELSE, PERFORM ROUTINE
CHKSUP   TM    TYPE,SUP                 SUPPRESS MSG FROM CONSOLE
         BZ    CHKNOHC                  NO, GO CHECK NEXT
         BAL   R9,SUPCODE               ELSE, PERFORM ROUTINE
CHKNOHC  TM    TYPE,NOHC                SUPPRESS MSG FROM HARDCOPY
         BZ    CHKDEL                   NO, GO CHECK NEXT
         BAL   R9,NOHCCODE              ELSE, PERFORM ROUTINE
CHKDEL   TM    TYPE,DELETE              SHOULD WE DELETE MESSAGE
         BZ    RETURN                   NO, GO RETURN
         BAL   R9,DELCODE               ELSE, PERFORM ROUTINE
*****    RETURN TO CALLER/FREEMAIN IF NECESSARY    *****
RETURN   TM    EXITFLAG,SRBSCHED        WAS AN SRB SCHEDULED
         BO    RETURN2                  YES, SKIP FREEMAIN
         LR    R1,R13                   ELSE LOAD R1 W/A(SAVEAREA)
         L     R13,4(,R13)              LOAD R13 W/A(CALLER SAVEAREA)
         FREEMAIN RU,A=(1),LV=WORKLEN,SP=245   FREEMAIN STORAGE
         RETURN (14,12),RC=0            RETURN TO CALLER
RETURN2  L     R13,4(,R13)              LOAD R13 W/A(CALLER SAVEAREA)
         RETURN (14,12),RC=0            RETURN TO CALLER
*****    ADD NEW ROUTING/DESCRIPTOR CODES    *****
ADDCODE  OI    CTXTRFB1,CTXTRCRC+CTXTRCDC SHOW CHG ROUTE/DEST CODES
         L     R5,CTXTRCP              LOAD R5 W/PTR TO ROUTE CODES
         USING CTXTROUT,R5             ESTABLISH ADDRESSABILITY
         OC    CTXTROUT(2),ROUTCODE    ADD ANY NEW ROUTE CODES(2 BYTES)
         L     R5,CTXTDCP              LOAD R5 W/PTR TO DESC CODES
         USING CTXTDESC,R5             ESTABLISH ADDRESSABILITY
         OC    CTXTDESC(2),DESCCODE    ADD ANY NEW DESC CODES(2 BYTES)
         BR    R9                      RETURN TO CALLER
*****    CHANGE ROUTING/DESCRIPTOR CODES    *****
CHGCODE  OI    CTXTRFB1,CTXTRCRC+CTXTRCDC SHOW CHG ROUTE/DEST CODES
         L     R5,CTXTRCP              LOAD R5 W/PTR TO ROUTE CODES
         USING CTXTROUT,R5             ESTABLISH ADDRESSABILITY
         MVC   CTXTROUT(2),ROUTCODE    CHANGE DESC CODES AS REQUESTED
         L     R5,CTXTDCP              LOAD R5 W/PTR TO DESC CODES
         USING CTXTDESC,R5             ESTABLISH ADDRESSABILITY
         MVC   CTXTDESC(2),DESCCODE    CHANGE ROUTE CODES AS REQUESTED
         BR    R9                      RETURN TO CALLER
*****    SUPPRESS MESSAGE FROM CONSOLES    *****
SUPCODE  OI    CTXTRFB2,CTXTRHCO       FORCE HARDCOPY ONLY
         BR    R9                      RETURN TO CALLER
*****    SUPPRESS MESSAGE FROM HARDCOPY    *****
NOHCCODE OI    CTXTRFB2,CTXTRNHC       FORCE NO HARDCOPY
         BR    R9                      RETURN TO CALLER
*****    DELETE THE MESSAGE FROM EXISTENCE    *****
DELCODE  OI    CTXTRFB2,CTXTRDTM       DELETE THE MESSAGE
         BR    R9                      RETURN TO CALLER
*****    ISSUE MVS/JES2 COMMANDS AFTER APPROPRIATE DELAY    *****
CMDCODE  OI    TYPEFLAG,CMDRESP        INDICATE COMMAND REQUIRED
         MVC   WTOAREA(WTOLEN),WTOSKEL MOVE CANNED REPLY TEXT TO AREA
         IC    R6,CMDLEN               INSERT LENGTH OF CMD INTO R6
         BCTR  R6,R0                   SUBTRACT 1 FOR EXECUTE OF MVC
         EX    R6,MVCCMD               EXECUTE MVC INSTRUCTION
         B     ASVTFIND                GO TO COMMON SRB SCHED. ROUTINE
MVCCMD   MVC   WTOAREA+4(0),CMDTEXT    MOVE COMMAND TEXT TO WTO AREA
*****    REPLY TO MESSAGES AFTER APPROPRIATE DELAY    *****
RPLYCODE OI    TYPEFLAG,RPLYRESP       INDICATE REPLY REQUIRED
         MVC   WTOAREA(WTOLEN),WTOSKEL MOVE CANNED REPLY TEXT TO AREA
         IC    R6,CMDLEN               INSERT LENGTH OF CMD INTO R6
         BCTR  R6,R0                   SUBTRACT 1 FOR EXECUTE OF MVC
         EX    R6,MVCCMD               EXECUTE MVC INSTRUCTION
         MVC   WTOAREA+6(2),CTXTRPID   MOVE REPLY ID TO AREA
*****    COMMON SRB SCHEDULING CODE FOR COMMAND/REPLY    *****
ASVTFIND OI    EXITFLAG,ASVTSCAN       SHOW ASVT SCAN ACTIVE
         L     R1,CVTPTR               LOAD R1 W/A(CVT)
         L     R1,CVTASVT-CVT(R1)      LOAD R1 W/A(ASVT)
         USING ASVT,R1                 ESTABLISH ADDRESSABILITY
         LA    R14,ASVTENTY            LOAD R14 W/A(FIRST ASCB ENTRY)
         USING ASVTENTY,R14            ESTABLISH ADDRESSABILITY
         L     R15,ASVTMAXU            LOAD R15 W/MAX NO. ASCBS
ASVTLOOP ICM   R6,15,ASVTENTY          LOAD ASVT ENTRY(ASCB), IF THERE
         BM    ASVTBUMP                NOT IN USE, GO POINT TO NEXT
         USING ASCB,R6                 ESTABLISH ADDRESSABILITY
         ICM   R7,15,ASCBJBNS          LOAD R7 W/A(STC JOBNAME)
         BZ    ASVTBUMP                GO POINT TO NEXT ENTRY
         CLC   STCNAME,0(R7)           IS THIS THE STC WE WANT
         BE    SRBSET                  GO SET UP RST OF SRB
ASVTBUMP LA    R14,ASVTENTY+4          ELSE POINT TO NEXT ASVT ENTRY
         BCT   R15,ASVTLOOP            GO BACK TO LOOP THRU ASVT
         BR    R9                      ELSE RETURN TO CALLER

The remaining source code for this WTO exit for automating
operations will appear in next month's issue.


Bruce Bordonaro
Systems Software Manager (USA) © Bruce Bordonaro 1990
