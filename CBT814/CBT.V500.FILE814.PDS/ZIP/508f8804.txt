Running CLISTs around the FORTRAN IAD

The VS FORTRAN Version 2 Interactive Debugger (IAD) is an
extremely useful and highly recommended tool which rapidly
becomes indispensable to applications programmers.  It does,
however, suffer from one minor irritation stemming from the
common need to allocate files for I/O before program execution
and to free these files after the program has run.  The sensible
solution is to write short initiation and termination CLISTs.
Unfortunately it is easy to forget to run these CLISTs and it is
frustrating to have invoked the IAD only to realise that the
required files have not been allocated.

This article explains how the IAD can be simply altered to allow
automatic execution of initiation and termination CLISTs around
the IAD.  All that is required are minor modifications to the IAD
entry panel, AFFFP11; the IAD CLIST, AFFFC11; and an extra
panel definition for error trapping.

The termination CLIST is also useful for programs that require
clean-up routines to be run after IAD invocation.

The enhancements described here assume the initiation and
termination CLISTs are contained in libraries which are part of the
SYSPROC concatenation.  Obviously it is straightforward to
extend the modifications to allow the execution of any CLIST.

The following additions to the IBM-supplied panel and CLIST are
necessary:
1   An extra three lines should be added to the )BODY section of
the IAD entry panel AFFFP11 to allow the input of the
CLIST names:
+INITIATION AND TERMINATION CLISTS: (Must be part of SYSPROC concatenation)
+           INITIATION%===>_IADCINIT+
+          TERMINATION%===>_IADCTERM+
    The ISPF variables IADCINIT and IADCTERM, which
contain the names of the CLISTs to be executed, should be
verified as member names and VPUT into the profile pool by
the )PROC section of the panel definition.
2   The following code to execute the initiation CLIST should be
included in the IAD CLIST, AFFFC11, at the start of the 'DO'
sequence immediately after message ISRF905 for invalid
organisation has been set.
         /* get user CLIST names, execute initiation CLIST if
         /* non blank, if error display panel so that CLIST name
         /* can be changed
         /*
         ISPEXEC VGET (IADCINIT, IADCTERM)
    CERR:IF &IADCINIT ^= &Z THEN +
           DO
             %&IADCINIT
             SET RCODE  = &LASTCC
             IF &RCODE ^= 0 THEN +
               DO
                 ISPEXEC DISPLAY PANEL(AFFFP11E)
                 IF &LASTCC = 8 THEN +
                   DO
                     SET ZFPRFC = 8
                     ISPEXEC VPUT ZFPRFC SHARED
                     EXIT
                   END
                 ISPEXEC VGET IADCINIT
                 GOTO CERR
               END
           END

    Note that most of this code is concerned with error
processing, typically required because the requested CLIST
was not found.  It is not straightforward to display an
informative message on the AFFFP11 panel when there is an
initiation CLIST error since display of this panel (and
execution of the AFFFC11 CLIST) is under control of the
ISRFPR program which controls all of the ISPF/PDF
foreground options (primary option 4).  Instead an error
panel, AFFFP11E, is displayed allowing the user to change
the CLIST name, to continue, or to exit.  If the user chooses
to exit, the ISPF system variable ZFPRFC is set to non-zero
so that an 'ABNORMAL TERMINATION' message is
displayed on the IAD entry panel by the ISRFPR program.
3   The following code should be inserted near the end of the
AFFFC11 CLIST immediately after the IAD files
(AFFPRINT, AFFIN etc) are freed.

    /* execute termination CLIST if non blank, if non-zero return
    /* code then set ZFPRFC so that abnormal termination message
    /* issued
    /*
    IF &IADCTERM ^= &Z THEN +
      DO
        %&IADCTERM
        SET RCODE = &LASTCC
        IF &RCODE ^= 0 THEN +
        DO
          SET ZFPRFC = &RCODE
          ISPEXEC VPUT ZFPRFC SHARED
        END
      END

    Note again that the ZFPRFC variable is set to the return code
if there is an error to display the abnormal termination
message.
4   This panel AFFFP11E is displayed by the AFFFC11 CLIST if there is an error executing the initiation CLIST.

)ATTR
@ type(output) color(yellow)
)BODY
%----------------INITIATION CLIST ERROR-------------------------------
%COMMAND ===>_ZCMD                                                   +
%
%
%********************************************************************
%*+                                                                %*
%*+ The initiation CLIST@IADCINIT+ended with a return code         %*
%*+                                                                %*
%*+  of@z  +. Probably the CLIST could not be found.               %*
%*+                                                                %*
%*+                                                                %*
%*+  Enter a different CLIST name below or leave blank to continue.%*
%*+                                                                %*
%*+  New CLIST name%===>_TEMP    +                                 %*
%*+                                                                %*
%*+                                                                %*
%*+  Press%enter+to continue or %PF3+ to exit.                     %*
%*+                                                                %*
%********************************************************************
)INIT
  &temp = ' '
  .zvars = '(rcode)'
)PROC
  ver (&temp,name)
  &iadcinit = &temp
  vput iadcinit profile
)END


Dr Robert Frost
Tessella (Abingdon) Ltd (UK) © Xephon 1989

