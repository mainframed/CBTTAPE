Adding the programmer’s name to the JCT
INTRODUCTION
The purpose of the following program is to copy the 20-character programmer name
field from the RACF or TOP/SECRET Access Control Environment Element (ACEE) to
the JES2 Job Control Table (JCT).
Normally the programmer name from the JCL job statement is placed in the JES2
JCT.  However, in the case of TSO without a pre-prompt exit, the programmer name
is omitted resulting in blanks in the JCT.
This program should be invoked once after TSO log-on to copy the programmer name
field.  Any spin-off datasets created within the session then contain the
programmer’s name on the JES2 page header.
The following program can be invoked either by a TSO call or as a TSO command
(providing it is in STEPLIB or the LINKLIST).
THE PROGRAM
SETPNAME CSECT ,
**********************************************************************
* MACLIBS: SYS1.MACLIB, SYS1.HASPSRC & SYS1.AMODGEN & SYS1.MACLIB
* LINKEDIT ATTRIBUTES:  REUS, AC(1)
* RETURN CODES: 0 = PROGRAMMER NAME COPIED TO THE JES2 JCT
*               4 = PROGRAMMER NAME NOT COPIED, FOR ONE OF FOLLOWING
*                   REASONS: (1) UNABLE TO LOCATE THE JES2 JCT
*                            (2) PROGRAMMER NAME ALREADY IN JES2 JCT
*                            (3) UNABLE TO LOCATE THE RACF ACEE
* REGISTER USAGE -  0   -- UNUSED --
*                   1   TEMPORARY WORK REGISTER
*                   2   ADDRESS OF CVT
*                   3   ADDRESS OF CALLER'S TCB
*                   4   ADDRESS OF CALLER'S JSCB
*                   5   ADDRESS OF CALLER'S SSIB
*                   6   ADDRESS OF CALLER'S SJB
*                   7   ADDRESS OF CALLER'S JCT
*                   8   ADDRESS OF CALLER'S ASCB
*                   9   ADDRESS OF CALLER'S ASXB OR ACEE
*                  10   -- UNUSED --
*                  11   -- UNUSED --
*                  12   SETPNAME BASE REGISTER
*                  13   POINTER TO SAVEAREA
*                  14   -- UNUSED --
*                  15   RETURN CODE
**********************************************************************
         USING SETPNAME,R15       TEMPORARY ADDRESSABILITY
         SAVE  (14,12),T,'SETPNAME &SYSDATE &SYSTIME'
         LA    R12,SAVEAREA       LOAD ADDRESS OF SAVE AREA
         ST    R13,4(,R12) .      PROVIDE BACKWARD
         ST    R12,8(,R13) ..      & FORWARD CHAINING
         LR    R13,R12            COPY NEW SAVE AREA ADDRESS
         LR    R12,R15            COPY BASE ADDRESS
         DROP  R15 .              DROP TEMPORARY ADDRESSABILITY
         USING SETPNAME,R12       PERMANENT ADDRESSABILITY FOR PROGRAM
         LA    R15,4              SET DEFAULT RETURN CODE
         L     R2,CVTPTR          POINT TO CVT
         USING CVT,R2             PROVIDE CVT ADDRESSABILITY
         L     R1,CVTTCBP         POINT TO TCB/ASCB WORD(S)
         L     R3,4(,R1)          POINT TO OUR TCB
         USING TCB,R3             PROVIDE TCB ADDRESSABILITY
         L     R4,TCBJSCB         POINT TO OUR JSCB
         USING IEZJSCB,R4         PROVIDE JSCB ADDRESSABILITY
         L     R5,JSCBSSIB        POINT TO SSIB
         USING SSIB,R5            PROVIDE SSIB ADDRESSABILITY
         L     R6,SSIBSUSE        POINT TO SJB
         USING SJB,R6             PROVIDE SJB ADDRESSABILITY
         L     R7,SJBJCT          POINT TO JES JCT
         USING JCT,R7             PROVIDE JES JCT ADDRESSABILITY
         CLC   =C'JCT ',JCTID     IS THIS REALLY THE JCT
         BNE   EXIT               NO, TERMINATE
         CLC   =CL20' ',JCTPNAME  IS PRGMR NAME IN PLACE
         BNE   EXIT               YES, TERMINATE
         L     R1,CVTTCBP         POINT TO TCB/ASCB WORD(S)
         L     R8,12(,R1)         POINT TO OUR ASCB
         USING ASCB,R8            PROVIDE ASCB ADDRESSABILITY
         L     R1,ASCBJBNS        GET ADDR OF OUR JOB NAME
         CLC   JCTJNAME,0(R1)     IS JCT FOR SAME JOB
         BNE   EXIT               NO, TERMINATE
         L     R9,ASCBASXB        GET ADDRESS OF ASXB
         L     R9,(ASXBSENV-ASXB)(,R9)  GET ADDRESS ACEE
         CLC   JCTJNAME,216(R9)   DOES JOB NAME MATCH JOB NAME IN ACEE
         BNE   EXIT               NO, TERMINATE
         MODESET KEY=ZERO,MODE=SUP
         MVC   JCTPNAME,224(R9)   COPY PROGRAMMER NAME FROM
*                                          THE ACEE TO THE JCT..
         MODESET KEY=NZERO,MODE=PROB
         SR    R15,R15            SET RETURN CODE TO ZERO
EXIT     L     R13,4(,R13)    .   POINT TO PREVIOUS SAVE AREA
         RETURN (14,12),T,RC=(15) TERMINATE THE TASK
         LTORG ,
SAVEAREA DC    9D'0'
         CVT   DSECT=YES,LIST=NO
         IKJTCB DSECT=YES,LIST=NO
         IEZJSCB
         IEFJSSIB
         IHAASCB DSECT=YES,LIST=NO
         IHAASXB DSECT=YES
         IFGRPL DSECT=YES,AM=VSAM
         $HASPEQU
         $TQE
         $SJB
         $BUFFER
         $JCT
         END


Paul Horak
Consultant
SEI Corporation (USA)    © SEI Corporation 1988




























