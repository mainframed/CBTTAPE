Obtaining free space information

This program, VOLSPACE, is different from the one published in issue 1 of MVS
Update in that it uses a little known function -SVC 78 (X'4E') - to obtain the
free space information.

The program checks all the generated UCBs for DASD, ignores the ones that don't
exist (ie VOLSER = X'000000000000'), uses SVC 78 to find the free space
information, and then re-formats it for display.

*------------------------------------------------------------------*
* THIS PROGRAM WHICH RUNS AS A TSO COMMAND WILL LIST THE FREE      *
* SPACE INFORMATION ON ALL 3350 AND 3380 DISKS THAT ARE KNOWN TO   *
* THE OPERATING SYSTEM.                                            *
*------------------------------------------------------------------*
         LREGS                       - STANDARD REGISTER EQUATES
         LSAVE MOD=VOLSPACE,VER=001  - STANDARD SAVE
         TPUT  OUTMSG1,L'OUTMSG1
         TPUT  OUTMSG3,L'OUTMSG3
         L     R3,CVTPTR                LOAD ADDRESS OF CVT
         USING CVTMAP,R3
         XC    WORKAREA,WORKAREA        CLEAR WORKAREA
         LA    R1,WORKAREA              PUT ADDR OF WORKAREA
         ST    R1,PARMWA                    IN PARM LIST
         LA    R1,DEVCLASS              PUT ADDR OF DEVTYPE
         ST    R1,PARMDEVT                  IN PARM LIST
         MVI   DEVCLASS,UCB3DACC        SET FOR DASD ONLY
         LA    R1,ADDRUCB               PUT ANSWER ADDRESS
         ST    R1,PARMUCB                   IN PARM LIST
         OI    PARMUCB,X'80'            INDICATE PARM LIST END
         USING UCBOB,R2                 SETUP UCB ADDRESSABILITY
SEARCH   DS    0H
         LA    R1,PARMLIST              SETUP PARMLIST ADDR IN R1
         L     R15,CVTUCBSC             GET SCAN SERVICE ADDRESS
         BALR  R14,R15                  GO TO SCAN SERVICE
         LTR   R15,15                   HAS UCB BEEN RETURNED
         BNZ   FINISH                   NO,AT END AND NO MATCH
         L     R2,ADDRUCB               GET UCB ADDR
         CLC   UCBVOLI,=X'000000000000' CHECK FOR NO VOLUME
         BNE   FOUND
NOVOL    EQU   *
         MVC   O4ADDR,UCBNAME           UNIT ADDRESS
         TPUT  OUTMSG4,L'OUTMSG4
         B     SEARCH
FOUND    EQU   *
         LR    R0,R2                    PUT UCB ADDR IN R0
         LA    R1,RESULT                POINT R1 TO OUTPUT AREA
         SVC   78
         MVC   O5VOL,UCBVOLT            SETUP VOLUME NAME
         MVC   O5ADDR,UCBNAME           UNIT ADDRESS
         MVC   O5EXTLC,OMCYLS
         MVC   O5EXTLT,OMTRKS
         MVC   O5EXTS,OEXTNT
         MVC   O5TOTSC,OCYLS
         MVC   O5TOTST,OTRKS
         CLI   UBCTBYT4,X'0E'            3380 ?
         BNE   NOT3380
         MVC   O5TYPE,=C'3380'
         B     MSGOUT
NOT3380  EQU   *
         CLI   UCBTBYT4,X'0B'            3350 ?
         BNE   NOT3350
         MVC   O5TYPE,=C'3350'
         B     MSGOUT
NOT3350  EQU   *
         MVC   O5TYPE,=C'****'
MSGOUT   EQU   *
         TPUT  OUTMSG5,L'OUTMSG5
         B     SEARCH
FINISH   EQU   *
         TPUT  OUTMSG2,L'OUTMSG2
         LRETURN RC=0                - STANDARD RETURN
PARMLIST DS    3F
         ORG   PARMLIST
PARMWA   DS    F                     ADDR OF 100 BYTE WORKAREA
PARMDEVT DS    F                     ADDR OF BYTE WITH DEVTYPE
PARMUCB  DS    F                     ADDR OF UCB ADDRESS RETURNED.
DEVCLASS DS    CL1                   DEVICE CLASS
ADDRUCB  DS    F                     THE OUTPUT UCB ADDR PUT HERE
WORKAREA DS    CL100                 WORK AREA
         DS    0D
OUTMSG1  DS    0CL70
         DC    CL4'ADDR'
         DC    CL1' '
         DC    CL6'VOLSER'
         DC    CL4' '
         DC    CL4'TYPE'
         DC    CL4' '
         DC    CL4'EXTS'
         DC    CL4' '
         DC    CL12'LARGEST FREE'
         DC    CL3' '
         DC    CL14'TOTAL FREE   '
         DC    CL10' '
OUTMSG3  DS    0CL70
         DC    CL4'----'
         DC    CL1' '
         DC    CL6'------'
         DC    CL4' '
         DC    CL4'----'
         DC    CL4' '
         DC    CL4'----'
         DC    CL4' '
         DC    CL11'CYLS TRKS  '
         DC    CL4' '
         DC    CL14'CYLS TRKS     '
         DC    CL10' '
OUTMSG5  DS    0CL70
O5ADDR   DS    CL3
         DC    CL2' '
O5VOL    DS    CL6
         DC    CL4' '
O5TYPE   DS    CL4
         DC    CL4' '
O5EXTS   DS    CL4
         DC    CL4' '
O5EXTLC  DS    CL4
         DC    CL1' '
O5EXTLT  DS    CL4
         DC    CL6' '
O5TOTSC  DS    CL4
         DC    CL1' '
O5TOTST  DS    CL4
         DC    CL5' '
         DC    CL10' '
OUTMSG2  DS    0CL17
         DC    CL17'VOLSPACE ENDED OK'
OUTMSG4  DS    0CL26
O4ADDR   DS    CL3
         DC    CL2' '
         DC    CL21'DEVICE NOT AVAILABLE'
RESULT   DS    OCL30                RETURNED FROM SVC 78
         DS    CL6                  'SPACE='
OCYLS    DS    CL4                  TOTAL FREE SPACE CYLS
         DS    CL1                  ','              AND
OTRKS    DS    CL4                                   TRACKS
         DS    CL1                  ','
OEXTNT   DS    CL4                  FREE EXTENTS
         DS    CL1                  '/'
OMCYLS   DS    CL4                  LARGEST FREE SPACE IN CYLS
         DS    CL1                  ','                   AND
OMTRKS   DS    CL4                                        TRACKS
         LTORG
         IEFUCBOB                   UCB MACRO ID
         CVT      DSECT=YES
         END


Chris Lawless
Technical Support Manager
Crown Financial Management (UK)    © Chris Lawless 1987















































