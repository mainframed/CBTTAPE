Altering memory limits

// JOBCARD
//*
//ASMHCL   EXEC ASMHCL,
//         PARM.C=('OBJECT,RENT'),
//         PARM.L=('XREF,LET,RENT,REUS,REFR,AC=1')
//C.SYSLIB   DD DSN=SYS1.MACLIB,DISP=SHR
//           DD DSN=SYS1.MODGEN,DISP=SHR
//C.SYSIN    DD *
         TITLE 'IEFUSI - MVS SMF STEP INITIATION EXIT'
* INITIALIZATION ROUTINE (SET REGION SIZE LIMITS)
IEFUSI   CSECT
IEFUSI   AMODE 31
IEFUSI   RMODE ANY
         STM   R14,R12,12(R13)      STORE REGISTERS
         LR    R12,R15              BASE REGISTER
         USING IEFUSI,R12           ESTABLISH BASE
         B     SETLIMIT             SKIP CONSTANTS
         DC    CL8'IEFUSI'
         DC    CL8'&SYSDATE'
         DC    CL8'&SYSTIME'
SETLIMIT EQU   *
         L     R11,16(R1)           ADDRESS OF REGION LIMIT SETTINGS
         L     R3,4(R11)            LOAD ADDRESS OF REGION REQUESTED
         L     R4,MEGA16            LOAD 16MB VALUE
         BCTR  R4,R0                16MB LESS 1 FOR COMPARE
         CR    R3,R4                REQUEST GREATER THAN 16MB?
         BH    SETBELOW             YES, SET BELOW 16MB LIMIT ONLY
SETABOVE EQU   *
         L     R3,MEGA97            LOAD 97MB VALUE
         ST    R3,16(R11)           SET EXTENDED REGION LIMIT 97MB
         L     R4,MEGABYTE          EREGION-SIZE = EREGION-LIMIT - 1MB
         SR    R3,R4                FOR MVS OVERHEAD....
         ST    R3,20(R11)           SET EXTENDED REGION SIZE 96MB
         B     TELLVSM              SET VSM BIT
SETBELOW EQU   *
         L     R3,CVTPTR            GET CVT ADDRESS
         USING CVTMAP,R3            MAP CVT
         L     R3,CVTGDA            GET ADDRESS OF VSM GLOBAL DATA
         DROP  R3                   NO LONGER NEEDED
         USING GDA,R3               MAP GDA
         L     R3,GDAPVTSZ          MAX PRIVATE AREA SIZE BELOW 16MB
         DROP  R3                   NO LONGER NEEDED
         L     R4,KILO512           REGION-LIMIT = PRIVATE AREA - 512K
         SR    R3,R4                FOR MVS OVERHEAD....
         ST    R3,8(R11)            STORE REGION-LIMIT
         L     R4,KILO256           REGION-SIZE = REGION-LIMIT - 256K
         SR    R3,R4                FOR MVS OVERHEAD....
         ST    R3,12(R11)           STORE REGION-SIZE
* TELL VIRTUAL STORAGE MANAGER THAT IEFUSI SETS LIMITS
TELLVSM  EQU   *
         L     R3,0(R11)            TELL VSM IEFUSI IS SUPPLYING INFO
         L     R4,HIGHBIT           LOAD HIGH-ORDER BIT VALUE
         OR    R3,R4                SET HIGH-ORDER BIT
         ST    R3,0(R11)            TELL VSM IEFUSI IS SUPPLYING INFO
RETURN   EQU   *
         LM    R0,R12,20(R13)       RESTORE ALL EXCEPT 14,15
         L     R14,12(R13)          RESTORE RETURN ADDRESS
         BR    R14                  RETURN TO CALLER
         DS    0F
KILO256  DC    XL4'00040000'        256K
KILO512  DC    XL4'00080000'        512K
MEGABYTE DC    XL4'00100000'        1MB
MEGA16   DC    XL4'01000000'        16MB
MEGA97   DC    XL4'06100000'        97MB
HIGHBIT  DC    XL4'80000000'
         CVT   DSECT=YES
         IHAGDA
         END
/*
//*
//L.SYSLMOD  DD DSN=SYS1.LPALIB(IEFUSI),DISP=SHR
//L.SYSLIB   DD DSN=SYS1.LPALIB,DISP=SHR
//           DD DSN=SYS1.LINKLIB,DISP=SHR


Ray Smith (USA)                                                                          c Xephon 1996

