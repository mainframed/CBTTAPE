Extended MCS console without RACF 1.9

SOURCE CODE FOR IKJCNXAC

*/* TSO EXIT TO ACTIVATE CONSOLE. PASS TO CONSOLE COMMAND IN THE
*/* MSGOPER AREA THE FLAG MIGID TO YES.
*/* LINK PARAMETERS:
*/*       RENT REUSE REFR AC(0) AMODE(31) RMODE(ANY)
         TITLE 'MVS/ESA TSO CONSOLE ACTIVATION EXIT'
IKJCNXAC CSECT
         SAVE  (14,12),,IKJCNXAC_CONSOLE_ACTIVATE_EXIT(D=&SYSDATE)
         LR    R12,R15
         USING IKJCNXAC,R12       CSECT ADDRESSABILITY
         LR    R11,R1             SAVE POINTER PARAMETER LIST
         STORAGE OBTAIN,LENGTH=LDYNAMIC,LOC=(BELOW,ANY),KEY=1,SP=129
         LTR   R15,R15            TEST RETURN CODE
         BNZ   NO_STORAGE_AVAIL_WORKAREA
         LR    R10,R1
         ST    R13,4(R10)
         ST    R10,8(,R13)
         LR    R13,R10            LOAD ADDR SAVEAREA
         USING WORKAREA,R13       ADDR.LITY WORKAREA
*        TPUT  MSG001I,MSG001IL,ASIS    INFO MESSAGE
         USING PARMS,R11          ADDR.LITY TO PARAMETER LIST
         USING PARM_ENTRY,R10     ADDR.LITY TO ENTRY 16
         MVI   OPER_AUTH,X'00'    SET SWITCH TO  NO  OPER AUTHORITY
         L     R10,ENTRY5         LOAD ADDR  ENTRY5 (PSCB)
         L     R9,PARM_ENTRY_DATA LOAD ADDR PSCB
         USING PSCB,R9            ADDR.LITY TO PSCB
         TM    PSCBATR1,PSCBCTRL  TEST IF OPER AUTHORITY
         BZ    NO_OPER_AUTH       IF NO AUTH BRANCH
         L     R10,ENTRY14        LOAD ADDRESS OF ENTRY14 (FLAG)
         OI    PARM_ENTRY_DATA,CONSOLE_AUTH  SET AUTH CONSOLE TO YES
         MVI   OPER_AUTH,X'FF'    SET SWITCH TO YES  OPER AUTHORITY
NO_OPER_AUTH   EQU *
         DROP  R9
         L     R10,ENTRY16        LOAD ADDRESS ENTRY16 (OPERPARM)
         SLR   R8,R8              ZERO REG8 RETURN CODE
         STORAGE OBTAIN,LENGTH=MCSOPLEN,LOC=(BELOW,ANY),KEY=1,SP=129
         LTR   R15,R15            TEST RETURN CODE
         BNZ   NO_STORAGE_AVAIL_MCSOPER
         LR    R9,R1              R9 POINT TO MCSOPER AREA
         USING MCSOPPRM,R9        ADDR.LITY TO OPERPARM
         CLI   OPER_AUTH,X'00'    CHECK SWITCH IF OPER AUTH.
         BE    NO_MCSAUTH_ALL     NO AUTH BRANCH
         MVI   MCSOATH1,MCSOAALL  AUTH=ALL
NO_MCSAUTH_ALL EQU *
*              MCSOATH1=MCSOINFO  AUTH=INFO  (DEFAULT)
         MVI   MCSOMIG,MCSOMIGY        MIGID=YES
         MVI   MCSORCDT,MCSORCAL       ROUTING CODE=ALL
         ST    R9,PARM_ENTRY_DATA      STORE ADDR AREA OPERPARM
         ICM   R3,15,LL_MCSOPER        LOAD LL MCSOPER AREA
         ST    R3,PARM_ENTRY_LENGTH    STORE LL MCSOPER AREA
         ICM   R3,15,KEY_ENTRY16       LOAD KEY PARM
         ST    R3,PARM_ENTRY_KEY       STORE KEY ENTRY16
*        TPUT  MSG002I,MSG002IL,ASIS   INFO  MESSAGE
RETURN   EQU   *
         LR    R1,R13                  SAVE REG13
         L     R13,4(,R13)             LOAD REG13 POINTER BACK
         STORAGE RELEASE,ADDR=(R1),LENGTH=LDYNAMIC,KEY=1,SP=129
         LR    R15,R8                  LOAD RC
         L     R14,12(,R13)            LOAD RETURN ADDR
         LM    R0,R12,20(R13)          RESTORE REGISTERS
         BR    R14                     RETURN VIA REGISTER 14
NO_STORAGE_AVAIL_WORKAREA EQU *
         SR    R8,R8
         IC    R8,=AL1(16)
         TPUT  MSG001E,MSG001EL,ASIS   ERROR MESSAGE
         B     RETURN
NO_STORAGE_AVAIL_MCSOPER EQU *
         SR    R8,R8
         IC    R8,=AL1(16)
         TPUT  MSG002E,MSG002EL,ASIS   ERROR MESSAGE
         B     RETURN
*                    EQUATES E COSTANTI                             *
LL_MCSOPER     DC  A(MCSOPLEN)
KEY_ENTRY16    DC  A(2)
MSG001I  EQU   *
         DC    C'IKJCNXAC'
         DC    C'- EXIT ACTIVE.'
         DC    X'15'
MSG001IL EQU   *-MSG001I
MSG002I  EQU   *
         DC    C'IKJCNXAC'
         DC    C'- OBTAIN MIG.'
         DC    X'15'
MSG002IL EQU   *-MSG002I
MSG001E  EQU   *
         DC    C'IKJCNXAC'
         DC    C'- GETMAIN ERROR FOR WORKAREA.'
         DC    X'15'
MSG001EL EQU   *-MSG001E
MSG002E  EQU   *
         DC    C'IKJCNXAC'
         DC    C'- GETMAIN ERROR FOR MCSOPER.'
         DC    X'15'
MSG002EL EQU   *-MSG002E
         IEZVG111                      MAP OPERPARM AREA
         IKJPSCB                       MAP PROTECT STEP CONTROL BLOCK
*                    EXIT PARAMETER                                 *
PARMS                DSECT        DESCRIBE THE PARAMETER_ENTRY
ENTRYS               DS  0F       ALIGN ON A FULL WORD
ENTRY_DEFAULT        DS  9F
ENTRY10              DS  F
ENTRY11              DS  F
ENTRY12              DS  F
ENTRY13              DS  F
ENTRY14              DS  F
ENTRY15              DS  F
ENTRY16              DS  F        POINT TO "PARM_ENTRY16"
                     ORG ENTRYS+4*4
ENTRY5               DS  F        POINT TO "PSCB"
*                    ENTRY                                          *
PARM_ENTRY           DSECT        DESCRIBE THE PARAMETER_ENTRY
                     DS  0F       ALIGN ON A FULL WORD
PARM_ENTRY_KEY       DS  F        HERE'S THE KEY FIELD
PARM_ENTRY_LENGTH    DS  F        HERE'S THE LENGTH OF THE DATA
PARM_ENTRY_DATA      DS  CL4088   HERE'S THE DATA/ PTR TO DATA
LPAM_ENTRY           EQU *-PARM_ENTRY
CONSOLE_AUTH         EQU X'10'    BIT TO AUTH CONSOLE COMMAND.
*                    WORKAREA                                       *
WORKAREA   DSECT
SAVEAREA   DS    18F
OPER_AUTH  DS   X
LDYNAMIC   EQU   *-WORKAREA
           END   IKJCNXAC

SOURCE CODE FOR IKJCNXDE

*/* TSO EXIT TO DEACTIVATE CONSOLE COMMAND. FREE THE MIGID WITH
*/* THE MACRO MCSOPER REQUEST=RELEASE
*/* LINK PARMS:
*/*       RENT REUSE REFR AC(1) AMODE(31) RMODE(ANY)
         TITLE 'MVS/ESA TSO CONSOLE DEACTIVATION EXIT'
IKJCNXDE CSECT
         SAVE  (14,12),,IKJCNXDE_CONSOLE_DEACTIVATE_EXIT(D=&SYSDATE)
         LR    R12,R15
         USING IKJCNXDE,R12         CSECT ADDRESSABILITY
         LR    R11,R1               SAVE POINTER PARAMETER LIST
         STORAGE OBTAIN,LENGTH=LDYNAMIC,LOC=(BELOW,ANY),KEY=1,SP=129
         LTR   R15,R15              TEST RETURN CODE
         BNZ   NO_STORAGE_AVAIL_WORKAREA
         LR    R10,R1               SAVE POINTER A WORKAREA
         ST    R13,4(R10)
         ST    R10,8(,R13)
         LR    R13,R10              LOAD ADDR WORKAREA
         USING WORKAREA,R13         ADDRESSABILITY TO WORKAREA
         SLR   R8,R8                ZERO REG. RETURN CODE
*        TPUT  MSG001I,MSG001IL,ASIS  INFO MESSAGE
         USING PARMS,R11              ADDRESSABILITY TO PARAMETER LIST
         L     R10,ENTRY11            LOAD ADDR ENTRY11
         USING PARM_ENTRY,R10         ADDRESSABILITY TO ENTRY
         ICM   R2,15,PARM_ENTRY_DATA  LOAD ADDRESS OPERPARM
         BZ    FREE_MIGID             NO OPERPARM AREA BRANCH
*        TPUT  MSG003I,MSG003IL,ASIS  INFO MESSAGE
         ICM   R3,15,PARM_ENTRY_LENGTH  LOAD LL OF AREA
*  RELASE OPERPARM AREA OBTAINED FROM EXIT IKJCNXAC
         STORAGE RELEASE,LENGTH=(R3),ADDR=(R2),KEY=1,SP=129
FREE_MIGID EQU   *
         L     R10,ENTRY12            LOAD ADDR ENTRY12
         USING PARM_ENTRY,R10         ADDRESSABILITY A ENTRY
         LA    R3,PARM_ENTRY_DATA     LOAD ADDRESS MIGID
         BZ    FREE_END               NO AREA BRANCH
*        TPUT  MSG004I,MSG004IL,ASIS  INFO MESSAGE
         MODESET MODE=SUP                   SET IN SUPERVISOR MODE
         MODESET EXTKEY=ZERO,SAVEKEY=(2)    SET PSW KEY TO ZERO
         STC   R2,KEYSAVE                   SAVE PSW KEY
         MCSOPER REQUEST=RELEASE,MIGID=(R3) RELEASE MIGID
         LTR   R15,R15                      TEST RETURN CODE
         BZ    OK_FREE_MIGID                IF OK BRANCH
         TPUT  MSG002E,MSG002EL,ASIS        ERROR MESSAGE
OK_FREE_MIGID  EQU *
         MODESET KEYADDR=KEYSAVE,WORKREG=1  RESTORE PSW KEY
         MODESET MODE=PROB                  SET IN PROBLEM MODE
FREE_END EQU   *
*        TPUT  MSG002I,MSG002IL,ASIS   INFO MESSAGE
RETURN   EQU   *
         LR    R1,R13                  SAVE REG13
         L     R13,4(,R13)             LOAD REG13 POINTER BACK
         STORAGE RELEASE,ADDR=(R1),LENGTH=LDYNAMIC,KEY=1,SP=129
         LR    R15,R8                  SET RC
         L     R14,12(,R13)            LOAD RETURN ADDRESS
         LM    R0,R12,20(R13)          RESTORE REGISTERS
         BR    R14                     RETURN VIA REGISTER 14
NO_STORAGE_AVAIL_WORKAREA EQU *
         SR    R8,R8
         IC    R8,=AL1(16)
         TPUT  MSG001E,MSG001EL,ASIS   ERROR MESSAGE
         B     RETURN
*                    EQUATES E COSTANTI                             *
KEYSAVE  DC    X'00'
MSG001I  EQU   *
         DC    C'IKJCNXDE'
         DC    C'- EXIT ACTIVE.'
         DC    X'15'
MSG001IL EQU   *-MSG001I
MSG002I  EQU   *
         DC    C'IKJCNXDE'
         DC    C'- FREE MIG.'
         DC    X'15'
MSG002IL EQU   *-MSG002I
MSG003I  EQU   *
         DC    C'IKJCNXDE'
         DC    C'- FREE AREA MCSOPER.'
         DC    X'15'
MSG003IL EQU   *-MSG003I
MSG004I  EQU   *
         DC    C'IKJCNXDE'
         DC    C'- ALL OK.'
         DC    X'15'
MSG004IL EQU   *-MSG004I
MSG001E  EQU   *
         DC    C'IKJCNXDE'
         DC    C'- GETMAIN ERROR FOR WORKAREA.'
         DC    X'15'
MSG001EL EQU   *-MSG001E
MSG002E  EQU   *
         DC    C'IKJCNXDE'
         DC    C'- MIGID NOT FREED.'
         DC    X'15'
MSG002EL EQU   *-MSG002E
         LTORG
         IEZVG111               MAP OPERPARM AREA
*                    EXIT PARAMETER                                 *
PARMS                DSECT      DESCRIBE THE PARAMETER_ENTRY
                     DS  0F     ALIGN ON A FULL WORD
ENTRY_DEFAULT        DS  9F
ENTRY10              DS  F
ENTRY11              DS  F
ENTRY12              DS  F
ENTRY13              DS  F
ENTRY14              DS  F
ENTRY15              DS  F
ENTRY16              DS  F      POINT TO "PARM_ENTRY16"
*                    ENTRY                                          *
PARM_ENTRY         DSECT        DESCRIBE THE PARAMETER_ENTRY
                   DS  0F       ALIGN ON A FULL WORD
PARM_ENTRY_KEY     DS  F        HERE'S THE KEY FIELD
PARM_ENTRY_LENGTH  DS  F        HERE'S THE LENGTH OF THE DATA
PARM_ENTRY_DATA    DS  CL4088   HERE'S THE DATA/ PTR TO DATA
LPAM_ENTRY         EQU *-PARM_ENTRY
*                    WORKAREA                                       *
WORKAREA DSECT
SAVEAREA DS    18F
LDYNAMIC EQU   *-WORKAREA
         ORG   WORKAREA
         END   IKJCNXDE


                                                                                                                c Xephon 1994

