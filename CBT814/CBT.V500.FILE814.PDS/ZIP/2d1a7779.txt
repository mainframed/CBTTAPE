Graphic display of DASD space usage

There are many MVS tools available to display and print
reports on the status of individual DASD devices, but there is
nothing available, short of one of the few commercial products,
which provides an easy to read graphical display of groups or
pools of DASD volumes on a TSO terminal.  The following
Assembler program, REXX EXEC, panels, and control file
produce displays like those in Figures 1, 2, and 3, which show
aggregated data on DASD pools as defined by the user.

On a colour terminal, the space and fragmentation displays use
colour-coded bars to show whether the displayed values are
within normal limits (green), are at a warning level (yellow), or
are a cause for real concern (red).  On a non-colour terminal,
these levels are shown by different characters within each bar.
The REXX EXEC sets space levels at 60 percent and 80
percent, and fragmentation levels at fragmentation index values
of 300 and 500.  These threshold levels are rather arbitrary and
are easily changed within the EXEC.  To put all the pieces
together, the following steps need to be taken.


STEP ONE

Assemble and link the Assembler program LSPACE into a
non-authorized load library named userid.TECH.LOAD, where
userid is the user-id of the TSO user who will be running the
program.  The member name of the load module should be
LSPACE.

*---------------------------------------------------------------------*
* LSPACE PROGRAM ACCUMULATES VOLUME FREE SPACE AND FRAGMENTATION DATA *
* FOR DASD POOLS. TO DISPLAY THE RESULTS IN AN EASY TO READ GRAPH ON  *
* A CRT, THE RESULTS ARE PASSED TO THE REXX EXEC WHICH CALLED IT.     *
* POOLS ARE DEFINED IN AN INPUT PARAMETER DATA SET (SYSIN).           *
* REGISTER USAGE:                                                     *
*     R0  - USED AS HIGH ORDER REG FOR DIVIDE INSTRUCTIONS            *
*     R1  - INPUT PARAMETER LIST (REFRESH TIME)                       *
*     R2  - USED BY TRT TO RECEIVE THE FUNCTION BYTE FROM THE TABLE   *
*     R3  - BASE REG FOR CVT ADDRESSABILITY THEN ANCHORS TABLE2 DSECT *
*     R4  - BASE REG FOR UCB ADDRESSABILITY                           *
*     R5  - POINTER TO TABLE1 DATA AREA                               *
*     R6  - USED DURING SCAN OF SYSIN RECORDS THEN SUMS VOLS IN POOLS *
*     R7  - LENGTH TO SCAN DURING SYSIN PROCESSING THEN SUMS # POOLS  *
*     R8  - MARKS START OF SCANNED FIELD DURING SYSIN PROCESSING      *
*     R9  - REMEMBERS THE ENDING ADDRESS OF TABLE1                    *
*     R10 - BASE FOR DATA AREA RETURNED FROM LSPACE                   *
*     R11 - BASE FOR RETURNED FORMAT 4 DSCB                           *
*     R12 - BASE REGISTER                                             *
*     R13 - SAVE AREA POINTER                                         *
*     R14 - RETURN ADDRESS                                            *
*     R15 - INITIALLY ENTRY POINT ADDRESS, SET TO RETURN CODE ON EXIT *
*---------------------------------------------------------------------*
LSPACE   CSECT ,
         USING LSPACE,R12        TELL ASSEMBLER
         STM   R14,R12,12(R13)   SAVE REGISTERS
         LR    R12,R15           LOAD START ADDR INTO BASE REG
         ST    R13,SAVEAREA+4    SAVE CALLERS SAVEAREA ADDRESS
         LA    R10,SAVEAREA      ->R10 TO WORKING STORAGE SAVEAREA
         ST    R10,8(R13)        SAVE SAVEAREA ADDR IN PASSED SAVEAREA
         LR    R13,R10           ->R13 TO SAVEAREA FOR OTHERS TO USE
         USING SAVEAREA,R13      EXTRA BASE REG TO ADDRESS TABLE DATA
         B     VMFIBALL          SKIP OVER EYECATCHERS
         DC    CL10'LSPACE'      NAME OF PROGRAM FOR VMFIBALL
         DC    CL10'&SYSDATE'    ASSEMBLY DATE
         DC    CL10'&SYSTIME'    ASSEMBLY TIME
VMFIBALL DS    0H
         LR    R1,R2             RESTORE PARMLIST POINTER
MAINLINE DS    0H
         LA    R10,RETDATA       ->R10 TO RETURNED DATA AREA
         USING LSPDRETN,R10      USE DSECT NAMES TO ADDRESS IT
         LA    R11,F4DSCBAR      ->R11 TO RETURNED FORMAT 4 DSCB
         USING IECSDSL4,R11      USE DSECT NAMES TO ADDRESS IT
         LA    R5,TBL1AREA       POINT R5 TO START OF TABLE1 STORAGE
         USING TABLE1,R5         USE DSECT AND R5 TO ADDRESS IT
         OPEN  SYSIN             OPEN CONTROL CARD FILE
GETCARD  DS    0H
         GET   SYSIN,SYSINREC    READ CONTROL RECORD
         LA    R6,SYSINREC       POINT R6 AT START OF SYSIN REC
         LA    R7,79             80 BYTE LENGTH TO SCAN IN R7 ( - 1)
         MVI   TRTTABLE,X'00'    INITIALIZE TRANSLATE TABLE TO X'00'S
         MVC   TRTTABLE+1(255),TRTTABLE
         MVI   TRTTABLE+C' ',X'FF' MARK X'40' AS FUNCTION BYTE OF NOTE
         EX    R7,TRTINST        LOOK FOR 1ST BLANK
         BC    8,PARMERR         NO BLANKS ON THIS RECORD
         LR    R8,R6             SCAN START ADDR TO R8
         LR    R6,R1             POINT R6 AT 1ST BLANK FOUND
         SR    R1,R8             GET LENGTH OF FIRST PARM IN R1
         BZ    PARMERR           BAD PARM RECORD (STARTED WITH BLANK)
         C     R1,FULL8          CHECK LENGTH OF 1ST PARM
         BH    PARMERR           1ST PARM LONGER THAN 8 (POOL NAME)
         MVC   WORKPOOL,BLANKS   BLANK OUT WORK POOL NAME FIELD
         SR    R7,R1             ADJUST REMAINING SCAN LENGTH
         BCTR  R1,0              DECREMENT LENGTH FOR EXECUTE
         EX    R1,MVCINST        EXECUTE THE MVC INST TO R5 LOCATION
         MVI   TRTTABLE,X'FF'    INITIALIZE TRANSLATE TABLE TO X'FF'S
         MVC   TRTTABLE+1(255),TRTTABLE
         MVI   TRTTABLE+C' ',X'00' MARK X'40' AS FUNCTION BYTE TO SKIP
         EX    R7,TRTINST        LOOK FOR 1ST NONBLANK START OF PARM 2
         BC    8,PARMERR         NO SECOND PARM SUPPLIED - ALL BLANKS
         LR    R8,R6             SCAN START ADDR TO R8
         LR    R6,R1             POINT R6 AT 1ST NONBLANK FOUND
         SR    R1,R8             GET NUMBER OF BLANKS SKIPPED IN R1
         SR    R7,R1             ADJUST REMAINING SCAN LENGTH
         MVI   TRTTABLE,X'00'    INITIALIZE TRANSLATE TABLE TO X'00'S
         MVC   TRTTABLE+1(255),TRTTABLE
         MVI   TRTTABLE+C' ',X'FF' MARK X'40' AS FUNCTION BYTE OF NOTE
         EX    R7,TRTINST        LOOK FOR NEXT BLANK
         BC    8,PARMERR         NO BLANK DELIMITED 2ND PARM
         LR    R8,R6             SCAN START ADDR TO R8
         SR    R1,R8             GET LENGTH OF 2ND PARM IN R1
         MVC   WORKVOL(6),BLANKS CLEAR OUT WORK VOLSER FIELD
         C     R1,FULL6          CHECK LENGTH OF 2ND PARM
         BH    PARMERR           2ND PARM LONGER THAN 6 (VOLSER)
         LR    R15,R1            REMEMBER VOLSER LENGTH IN R15
         BCTR  R1,0              DECREMENT LENGTH FOR EXECUTE
         EX    R1,MVCINST2       EXECUTE THE MVC INST TO WORKVOL
         LA    R6,WORKVOL        POINT R6 AT START OF WORKVOL FIELD
         LA    R7,5              6 BYTE LENGTH TO SCAN IN R7 ( - 1)
         MVI   TRTTABLE,X'00'    INITIALIZE TRANSLATE TABLE TO X'00'S
         MVC   TRTTABLE+1(255),TRTTABLE
         MVI   TRTTABLE+C'*',X'FF' MARK C'*' AS FUNCTION BYTE OF NOTE
         EX    R7,TRTINST        LOOK FOR ASTERISK
         BC    7,GOTASTER        BRANCH IF ASTERISK FOUND
         C     R15,FULL6         CHECK LENGTH OF VOLSER AGAIN
         BNE   PARMERR           NO * MEANS VOLSER MUST BE EXACTLY 6
         LA    R1,6(R6)          MAKE R1 LOOK LIKE SCAN FOUND * AT +6
GOTASTER DS    0H
         SR    R1,R6             R1 GETS LENGTH OF VOLSER TO MATCH
         BZ    PARMERR           NOTHING BEFORE THE '*' IS AN ERROR
         LR    R6,R1             R6 GETS NUMBER OF CHARS BEFORE '*'
         BCTR  R6,0              -1 FOR EXECUTED CLC LENGTH
* FIND MATCHING VOLSERS IN UCBS AND FILL IN TABLE1
         L     R3,16             LOAD ADDRESS OF CVT
         USING CVTMAP,R3         ADDRESSABILITY FROM DSECT
         MVI   UCBDEVCL,UCB3DACC ONLY WANT DASD
         USING UCBOB,R4          MAP THE UCB
NEXTUCB  DS    0H
         LA    R1,UCBSCANL       POINT TO PARM LIST
         L     R15,CVTUCBSC      LOAD ADDRESS OF UCB SCAN ROUTINE
         BALR  R14,R15           CALL UCB SCAN ROUTINE
         LTR   R15,R15           CHECK FOR NON ZERO = NO MORE UCBS
         BNZ   GETCARD           BRANCH IF NO MORE UCBS - GO GET SYSIN
         L     R4,UCBADDR        SET BASE FOR ADDRESSING RETRIEVED UCB
         EX    R6,CLCINST        EXECUTE CLC TO SEE IF THIS MATCHES
         BNE   NEXTUCB           NO, GO LOOK AT NEXT UCB
         MVC   UCB,UCBADDR       MATCH, FILL IN TABLE ENTRY & BUMP
         MVC   VOLSER,UCBVOLI    MOVE IN FULL VOLSER
         MVC   POOL,WORKPOOL     MOVE IN POOL NAME
         LSPACE F4DSCB=F4DSCBAR,DATA=RETDATA,UCB=UCBADDR
         LTR   R15,R15           CHECK FOR NON ZERO = LSPACE ERROR
         BNZ   LSPACERR          GIVE MESSAGE AND EXIT
         MVC   TRKSTOT+2(2),DS4DEVSZ NUMBER OF CYLINDERS PER DEVICE
         XR    R1,R1             CLEAR WORK REG
         LH    R1,DS4DEVSZ       GET NUMBER OF CYLINDERS PER DEVICE
         MH    R1,DS4DEVSZ+2     MULTIPLY BY NUMBER OF TRKS PER CYL
         ST    R1,TRKSTOT        PLUG TOTAL TRACKS INTO TABLE ENTRY
         L     R1,LSPDTCYL       LOAD NUMBER OF FREE CYLINDERS
         MH    R1,DS4DEVSZ+2     MULTIPLY BY NUMBER OF TRKS PER CYL
         A     R1,LSPDTTRK       ADD IN ADDITIONAL NUMBER OF FREE TRKS
         ST    R1,TRKSFREE       PLUG FREE TRACKS INTO TABLE ENTRY
         L     R1,LSPDLCYL       LOAD LARGEST NUMBER OF FREE CYLINDERS
         MH    R1,DS4DEVSZ+2     MULTIPLY BY NUMBER OF TRKS PER CYL
         A     R1,LSPDLTRK       ADD IN ADDITIONAL NUMBER OF FREE TRKS
         ST    R1,TRKSMOST       PLUG FREE TRACKS INTO TABLE ENTRY
         MVC   FRAGINDX,LSPDFRAG PLUG FRAGMENTATION INDEX IN TABLE ENT
         LA    R5,TBL1ENTL(R5)   BUMP R5 TO NEXT TABLE1 ENTRY
         LA    R15,TBL1END       GET ADDRESS OF END OF TABLE1
         CR    R5,R15            RUNNING OVER END?
         BNL   PARMERR           YES, ISSUE MESSAGE AND END
         B     NEXTUCB           LOOK FOR MORE MATCHING VOLSERS
LSPACERR DS    0H                ERROR FROM LSPACE MACRO COMES HERE
         TPUT  LSPACMSG,L'LSPACMSG
         B     RETURN
PARMERR  DS    0H                PARAMETER CARD ERRORS COMES HERE
         TPUT  PARMEMSG,L'PARMEMSG
         B     RETURN
EOF      DS    0H                DONE BUILDING TABLE 1
         LA    R15,TBL1AREA      R15 POINTS TO START OF TABLE1 AREA
         CR    R15,R5            ANY ENTRIES GET PUT IN TABLE1?
         BE    PARMERR           NO VALID PARMS WERE PROCESSED
* NOW BUILD SUMMARY TABLE2 FROM TABLE1 ENTRIES
         LA    R3,TBL2AREA       R3 POINTS TO START OF TABLE2 AREA
         USING TABLE2,R3         DSECT FOR FIELD ADDRESSABILITY
         LR    R9,R5             POINT R9 PAST END OF TABLE1 AREA
         LR    R5,R15            POINT R5 AT START OF TABLE1 AREA
         XR    R7,R7             CLEAR R7 TO SUM # OF POOLS
NEWPOOL  DS    0H
         XR    R6,R6             CLEAR R6 TO COUNT VOLS IN POOL
         MVC   POOLNAME,POOL     MOVE POOLNAME INTO TABLE2 ENTRY
         LA    R7,1(R7)          INCREMENT POOL COUNT
SAMEPOOL DS    0H
         LA    R6,1(R6)          ADD 1 TO COUNT OF VOLS IN POOL
         L     R1,POOLTRKS       GET CURRENT TOTAL TRAKS IN POOL
         A     R1,TRKSTOT        ADD IN TRACKS FOR THIS DEVICE
         ST    R1,POOLTRKS       PUT SUM BACK IN TABLE2 ENTRY FIELD
         L     R1,POOLFREE       GET CURRENT FREE TRAKS IN POOL
         A     R1,TRKSFREE       ADD IN FREE TRACKS FOR THIS DEVICE
         ST    R1,POOLFREE       PUT SUM BACK IN TABLE2 ENTRY FIELD
         L     R1,POOLFRAG       GET CURRENT FRAGMENTATION INDEX
         A     R1,FRAGINDX       ADD IN FRAG INDEX FOR THIS DEVICE
         ST    R1,POOLFRAG       PUT SUM BACK (NEEDS DIVIDE BY # VOLS)
         LA    R5,TBL1ENTL(R5)   BUMP TO NEXT TABLE1 ENTRY
         CR    R9,R5             REACH END OF TABLE1?
         BE    TABLDONE          YES, GO FINISH UP
         CLC   POOLNAME,POOL     ANOTHER ENTRY FOR THE SAME POOL?
         BE    SAMEPOOL          YES, LOOP AND ADD TO POOL ENTRY
         XR    R0,R0             NO, CLEAR R0 FOR DIVIDE
         DR    R0,R6             DIVIDE R0-R1 BY R6 TO GET AVERAGE
         ST    R1,POOLFRAG       PUT AVERAGE FRAGMENTATION INDEX BACK
         L     R1,POOLTRKS       R1 GETS TOTAL TRACKS IN POOL
         S     R1,POOLFREE       POOLTRKS - POOLFREE = TOTAL USED TRKS
         MH    R1,HALF100        MULTIPLY BY 100 FOR PERCENTAGE
         XR    R0,R0             NO, CLEAR R0 FOR DIVIDE
         D     R0,POOLTRKS       TOTAL USED TRKS / POOLTRKS = PCT USED
         ST    R1,POOLPCT        SAVE PERCENT USED IN POOL RECORD
         LA    R3,TBL2ENTL(R3)   BUMP TO NEXT TABLE2 ENTRY
         LA    R15,TBL2END       GET ADDRESS OF END OF TABLE2
         CR    R3,R15            OFF END OF TABLE2?
         BL    NEWPOOL           NO, GO FILL IN NEW POOL TBL2 ENTRY
TABLDONE DS    0H                YES, COMPUTE FRAG INDEX AVERAGE
         XR    R0,R0             CLEAR R0 FOR DIVIDE
         DR    R0,R6             DIVIDE R0-R1 BY R6 TO GET AVERAGE
         ST    R1,POOLFRAG       PUT AVERAGE FRAGMENTATION INDEX BACK
         L     R1,POOLTRKS       R1 GETS TOTAL TRACKS IN POOL
         S     R1,POOLFREE       POOLTRKS - POOLFREE = TOTAL USED TRKS
         MH    R1,HALF100        MULTIPLY BY 100 FOR PERCENTAGE
         XR    R0,R0             NO, CLEAR R0 FOR DIVIDE
         D     R0,POOLTRKS       TOTAL USED TRKS / POOLTRKS = PCT USED
         ST    R1,POOLPCT        SAVE PERCENT USED IN POOL RECORD
         LA    R3,TBL2ENTL(R3)   BUMP TO NEXT TABLE2 ENTRY
         MVC   POOLNAME,BLANKS   NO, INIT NEXT POOLNAME TO BLANKS
REXXFER  DS    0H                GIVE TBL2AREA TO REXX AND EXIT
         LINK  EP=IRXEXCOM,PARAM=(ARXEXCOM,0,0,SHVBLK),VL=1
RETURN   DS    0H
         CLOSE SYSIN             CLOSE INPUT FILE
         L     R13,SAVEAREA+4    RESTORE R13
         LM    R14,R12,12(R13)   RESTORE R14 TO R12
         XR    R15,R15           ZERO RETURN CODE REG
         BR    R14               RETURN
         LTORG ,                 PUT LITERALS BEFORE WORKING STORAGE
* EXECUTED INSTRUCTIONS FROM PARM DECODING ROUTINE                    *
TRTINST  TRT   0(0,R6),TRTTABLE        EXECUTED TRT TO FIND ' ' NB '*'
MVCINST  MVC   WORKPOOL(0),0(R8)       MOVE PARM DATA TO WORKPOOL
MVCINST2 MVC   WORKVOL(0),0(R8)        MOVE PARM DATA TO WORKVOL
CLCINST  CLC   UCBVOLI(0),WORKVOL      COMPARE PARTIAL VOLSER
* DATA AREAS, CONSTANTS, DCB, ETC...ETC...ETC...                      *
PARMEMSG DC    C'ERROR IN LSPACE PARAMETER CARDS (SYSIN)'
LSPACMSG DC    C'ERROR RETURNED FROM LSPACE MACRO'
WORKPOOL DC    CL8' '
WORKVOL  DC    CL6' '
BLANKS   DC    CL8' '
FULL6    DC    F'6'
FULL8    DC    F'8'
DUBLZERO DC    D'0'
HALF100  DC    H'100'
TRTTABLE DC    256X'00'                A BUNCH OF ZEROS
         ORG   TRTTABLE+C' '
         DC    C' '                    THE GUY WE ARE LOOKING FOR
         ORG   ,                       RESET LOCATION COUNTER
TBL2AREA DC    10XL24'00'        ROOM FOR 10 POOL ENTRIES
TBL2END  DC    CL8' '            END OF TABLE MARKER
F4DSCBAR DS    CL96              FORMAT 4 DSCB RETURNED HERE
RETDATA  DS    CL36              SPACE DATA RETURNED HERE
UCBSCANL DC    A(UCBWORK)        PARAMETER LIST FOR UCBSCAN ROUTINE
         DC    A(UCBDEVCL)
         DC    X'80'
         DC    AL3(UCBADDR)
UCBWORK  DC    XL100'00'
UCBDEVCL DS    CL1
UCBADDR  DS    F                 UCB ADDRESS GOES HERE
SYSINREC DS    CL80
         DS    0D
SHVBLK   DC    A(0)                    SHVNEXT
         DS    F                       SHVUSER
         DC    CL1'S'                  SHVCODE - S = SET VARIABLE
         DC    XL1'00'                 SHVRET
         DC    H'0'                    NOT USED
         DC    F'0'                    SHVBUFL LENGTH OF FETCH BUFFER
         DC    A(TBL2DATA)             SHVNAMA ADDRESS OF EXT VARB NAME
         DC    F'8'                    SHVNAML LENGTH OF EXT VARB NAME
         DC    A(TBL2AREA)             SHRVALA ADD OF VALUE BUFFER
         DC    F'240'                  SHRVALL LENGTH OF VALUE SET
TBL2DATA DC    C'TBL2DATA'             CONSTANT OF TBL2DATA
ARXEXCOM DC    CL8'IRXEXCOM'
SYSIN    DCB   DSORG=PS,DDNAME=SYSIN,MACRF=(GM),LRECL=80,EODAD=EOF
SAVEAREA DC    18F'0' ADDRESSED BY REG 13
TBL1AREA DC    100XL36'00'       ROOM FOR 100 VOLUME ENTRIES
TBL1END  DS    0H                END OF TABLE MARKER
*Ñ DSECTS ÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑÑ*
         LSPACE MF=(D,DATA)      DSECT TO MAP LSPACE RETURNED DATA AREA
         DSECT
         IEFUCBOB
TABLE1   DSECT                   36-BYTE TABLE ENTRIES
POOL     DS    CL8
UCB      DS    F
VOLSER   DS    CL6
         DS    XL2
TRKSTOT  DS    F
TRKSFREE DS    F
TRKSMOST DS    F
FRAGINDX DS    F
TBL1ENTL EQU   *-TABLE1
TABLE2   DSECT                   24-BYTE TABLE ENTRIES
POOLNAME DS    CL8
POOLTRKS DS    F
POOLFREE DS    F
POOLFRAG DS    F
POOLPCT  DS    F
TBL2ENTL EQU   *-TABLE2
         CVT   DSECT=YES
         DSECT
         IECSDSL1 (4)            DSECT TO MAP FORMAT 4 DSCB (VTOC)
         END   LSPACE


STEP TWO

Place the REXX EXEC in a library in the SYSPROC
concatenation of the user's log-on procedure (or in the
SYSEXEC concatenation, if your system is so configured).
The member name of the EXEC should be LSPACE.


LSPACE REXX

/* REXX LSPACE exec to display the output of the LSPACE program       */
/* as bar graphs of DASD Space availability and fragmentation by pool.*/
/**********************************************************************/
ISPEXEC "VGET ZCOLORS" /* See if we have a color terminal */
stat = SYSDSN(lspace.parms)
If stat <> "OK" Then
   Do
     Say Userid()".LSPACE.PARMS file not found, execution impossible."
     Exit
   End
Call MSG 'OFF'      /* Turn off annoying messages */
"FREE FI(SYSIN)"
"ALLOC FI(SYSIN) DA(LSPACE.PARMS)"
Do Forever
   ISPEXEC "DISPLAY PANEL(LSMAIN)"  /* Show menu selection panel */
   If rc <> 0 Then Exit             /* Exit on PF3 */
   "CALL TECH.LOAD(LSPACE)"         /* Call the assembler program */
   If tbl2data = "TBL2DATA" Then /* Uninitialized variable = failure */
      Do
        Say "LSPACE call failed."
        Exit
      End
   Do i = 1 to 6                    /* Loop thru table2 entries */
      Parse var tbl2data 1 pool 9 trks 13 free 17 frag ,
                         21 pct 25 tbl2data
      If pool = "" Then         /* Null field signals end of table */
         Do
            numpools = i -1     /* Fix up total number of pools */
            Leave
         End
      Interpret "poolnam"i "= pool"   /* set variables for this pool */
      spacebars.i = C2d(pct) % 2
      fragbars.i  = C2d(frag) % 20
      Select
         When spacebars.i > 39 Then  /* 40 bars = 80% full */
            Do
               spacecolor.i = "RED"
               spacechar.i = "@"
            End
         When spacebars.i > 29 Then  /* 30 bars = 60% full */
            Do
               spacecolor.i = "YELLOW"
               spacechar.i = "0"
            End
         Otherwise
            Do
               spacecolor.i = "GREEN"
               spacechar.i = "-"
            End
      End
      Select
         When fragbars.i > 24 Then   /* 25 bars = frag index of 500 */
            Do
               fragcolor.i = "RED"
               fragchar.i = "@"
            End
         When fragbars.i > 14 Then   /* 15 bars = frag index of 300 */
            Do
               fragcolor.i = "YELLOW"
               fragchar.i = "0"
            End
         Otherwise
            Do
               fragcolor.i = "GREEN"
               fragchar.i = "-"
            End
      End
   End
   If func = "SPACE" Then            /* User selected SPACE display */
      Do
         title = "DASD Space Utilization by Pool"
         scale = "    Percent Full"
         Do i = 1 to numpools
            If zcolors > 1 Then spacechar.i = "|"
            Interpret "color"i "=" spacecolor.i
            Interpret "bar"||2*i-1 "= Copies(spacechar.i,spacebars.i)"
            Interpret "bar"||2*i "= Copies(spacechar.i,spacebars.i)"
         End
      End
   Else /* func = FRAG , User selected fragmentation display */
      Do
         title = "  DASD Fragmentation by Pool"
         scale = "Fragmentation Index"
         Do i = 1 to numpools
            If zcolors > 1 Then fragchar.i = "|"
            Interpret "color"i "=" fragcolor.i
            Interpret "bar"||2*i-1 "= Copies(fragchar.i,fragbars.i)"
            Interpret "bar"||2*i "= Copies(fragchar.i,fragbars.i)"
         End
      End
   ISPEXEC "DISPLAY PANEL(LSDISP)" /* Show the common report panel */
End


STEP THREE

Place the panels LSMAIN and LSDISP in a library in the
ISPPLIB concatenation of the user's log-on procedure.  The
member names of the panels should be LSMAIN and LSDISP.

LSMAIN panel

)ATTR
] TYPE(TEXT) INTENS(HIGH) SKIP(ON) COLOR(WHITE)
% TYPE(TEXT) INTENS(LOW) SKIP(ON) COLOR(GREEN)
+ TYPE(TEXT) INTENS(LOW) SKIP(ON) COLOR(BLUE)
$ TYPE(TEXT) INTENS(HIGH) SKIP(ON) COLOR(TURQUOISE)
[ TYPE(OUTPUT) INTENS(HIGH) SKIP(ON) COLOR(TURQUOISE)
~ TYPE(TEXT) INTENS(HIGH) SKIP(ON) COLOR(RED)
_ TYPE(INPUT) CAPS(ON) COLOR(WHITE)
)BODY
+--------------------------$DASD Status Monitor Menu+--------------------------
]COMMAND~===>_ZCMD
+
+
+  %1 ]SPACE  $-+Show DASD space utilization by pool.
+
+  %2 ]FRAG   $-+Show DASD Fragmentation index by pool.
+
)INIT
  .CURSOR = ZCMD
  &FUNC = ''
  &ZCMD = ''
)PROC
  &FUNC = TRANS( TRUNC (&ZCMD,'.')
    1,'SPACE'
    2,'FRAG'
  ' ',' '
    *,'?' )
  &ZTRAIL = .TRAIL
)END

LSDISP panel

)ATTR
   ~ TYPE(OUTPUT) INTENS(LOW)
   # TYPE(OUTPUT) INTENS(HIGH)
)BODY
+----------------------#title                         +------------------------
+
+
+                           ~scale              +
+            0----1----2----3----4----5----6----7----8----9---10
+ #POOLNAM1+ ~BAR1                                              +
+            ~BAR2                                              +
+
+ #POOLNAM2+ ~BAR3                                              +
+            ~BAR4                                              +
+
+ #POOLNAM3+ ~BAR5                                              +
+            ~BAR6                                              +
+
+ #POOLNAM4+ ~BAR7                                              +
+            ~BAR8                                              +
+
+ #POOLNAM5+ ~BAR9                                              +
+            ~BAR10                                             +
+            0----1----2----3----4----5----6----7----8----9---10
+
+
+                             To exit, press PF3
)INIT
 .ATTR (BAR1) = 'COLOR(&COLOR1)'
 .ATTR (BAR2) = 'COLOR(&COLOR1)'
 .ATTR (BAR3) = 'COLOR(&COLOR2)'
 .ATTR (BAR4) = 'COLOR(&COLOR2)'
 .ATTR (BAR5) = 'COLOR(&COLOR3)'
 .ATTR (BAR6) = 'COLOR(&COLOR3)'
 .ATTR (BAR7) = 'COLOR(&COLOR4)'
 .ATTR (BAR8) = 'COLOR(&COLOR4)'
 .ATTR (BAR9) = 'COLOR(&COLOR5)'
 .ATTR (BAR10) = 'COLOR(&COLOR5)'
)END

STEP FOUR

Create a sequential file named userid.LSPACE.PARMS with
RECFM=FB,LRECL=80,BLKSIZE=3120. Use ISPF edit to
create pool definition records for one to five pools in this file.
The format of each record is poolname volser; where poolname
must be 1 to 8 characters, must begin in column 1, and must be
followed by at least one blank; and volser,  which must be 2 to
6 characters and must be followed by at least one blank. If
volser is less than 6 characters, the final character must be an
asterisk (*) to indicate a generic volume serial number that will
include all volume serial numbers that match all positions to
the left of the asterisk.

For example:

      POOL1 PACK01
      POOL1 PACK02
      POOL2 MVS*

would create two pool entries. POOL1 will contain packs
PACK01 and PACK02. POOL2 will contain all packs whose
volume serial numbers begin with 'MVS'.  Pool names which
specify the same pool must all appear consecutively.  No more
than 100 fully resolved volume serial numbers and 10 pools are
supported.


OVERVIEW OF OPERATION

To run the program, enter 'TSO %LSPACE' on the ISPF
command line.  The EXEC checks for the existence of
userid.LSPACE.PARMS, allocates it, then displays the
selection panel LSMAIN.  The user selects option 1 to see the
space utilization display, or option 2 to see the fragmentation
display.  The EXEC calls the Assembler program LSPACE to
read the pool definitions and accumulate space and
fragmentation data.  The Assembler program returns its
findings in the REXX variable 'tbl2data'.  For each defined
pool, the EXEC parses this data into variables representing the
pool name (pool), total tracks in the pool (trks), total free
tracks in the pool (free), the average fragmentation index for
the pool (frag), and the pool's space utilization percentage
(pct).  Based on the 'pct' and 'frag' values, the EXEC sets the
length and color of the corresponding bars.  After setting the
title and scale, the EXEC displays the common display panel,
LSDISP, with the space or fragmentation data as selected by
the user.  Pressing PF3 returns to the selection menu,
LSMAIN, from which the process can be repeated by selecting
another option.  Pressing PF3 from the LSMAIN display,
terminates the process.


OVERVIEW OF THE LSPACE ASSEMBLER PROGRAM

The program takes its name from the MVS macro which
retrieves the space, fragmentation, and VTOC data we are
interested in.  The program opens and reads the SYSIN file of
pool definition records.  After parsing and validating a record,
it scans through the system's UCBs looking for matching
volume serial numbers.  For each record in the SYSIN file, one
entry is added to table 1 for each matching volume serial
number containing the pool name (POOL), UCB address
(UCB), volume serial number (VOLSER), total tracks
(TRKSTOT), free tracks (TRKSFREE), largest free extent
(TRKSMOST), and fragmentation index (FRAGINDX).  This
information is gathered by the LSPACE macro which is coded
with the option which also retrieves the format 4 (VTOC) data
from the volume corresponding to the input UCB address.

After table 1 is filled in by exhausting the SYSIN records, it
contains one entry per volume serial number to be reported
upon.  The program next builds table 2 to aggregate the table 1
data into one table entry per pool.  The resulting table 2 area
consisting of 240 bytes (ie ten 24-byte pool entries) is passed as
a single variable to the REXX environment via the LINK to
IRXEXCOM and its associated shared variable block at
SHVBLK.


POSSIBLE MODIFICATIONS

The panels are set up to display at most five pools, but since
the Assembler program supports up to 10 pools, the panels and
EXEC could be modified to display narrower bars if more
pools are required.

A common parameter (SYSIN) file could be used if there were
no need to customize the pool definitions for each user.  Only
the beginning part of the EXEC would need modification
where LSPACE.PARMS would be replaced by the name of the
common file in quotes.

The LSPACE load module should be executed out of a
common load library which is available to all users.  Assemble
and link or copy the load module to the common library and
replace the EXEC statement CALL TECH.LOAD(LSPACE)
with a call to the common library, for example:

      CALL 'COMMON.LOAD.LIB(LSPACE)'


John Cornman
Senior Systems Programmer
Key Services Corporation (USA)                        c Xephon 1991

