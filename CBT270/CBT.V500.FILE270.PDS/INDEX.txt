INDEX    TITLE ' - SCAN A STRING FOR A CHARACTER'
***********************************************************************
***                                                                 ***
***  INDEX WILL SET A RETURN CODE BASED ON THE POSITION OF A        ***
***  CHARACTER IN A STRING. *&Â¬%$#@ TIRED OF THOSE VM GUYS          ***
***  HAVING BETTER STRING FUNCTIONS!........SOMEDAY...              ***
***                                                                 ***
***  SYNTAX:                                                        ***
***    @ INDEX 'CHRDIRSTR'                                          ***
***    WHERE:                                                       ***
***          CHR - IS THE CHAR TO SEARCH STRING FOR                 ***
***          DIR - IS DIRECTION OF SEARCH,  <  OR  >                ***
***          STR - IS THE STRING TO SEARCH                          ***
***                                                                 ***
***  RETURN CODES:                                                  ***
***    (0)  - CHARACTER WAS NOT FOUND IN STRING                     ***
***    (N)  - CHARACTER WAS FOUND AT POSITION N                     ***
***                                                                 ***
***  EXAMPLE:                                                       ***
***    SET &STRING = &STR(SOME.STRING)                              ***
***    @ INDEX '.>&STRING'                                          ***
***    SET &SEPERATOR = &LASTCC                                     ***
***  WOULD SET SEPERATOR = 5                                        ***
***                                                                 ***
***********************************************************************
         EJECT
INDEX    ENTERR LEVEL=V1M0
         EJECT
         L     R11,0(R1)          R11-> PARM
         LA    R11,0(R11)         CLEAR HI BIT
         LH    R3,0(R11)          R3 = PARM LENGTH
         CH    R3,=H'2'
         BH    TABLINIT
         L     R15,=F'9999'       MISSING STRING PARAMETER
         B     EXIT
TABLINIT SLR   R2,R2              R2 = 0
         IC    R2,2(R11)
         STC   R2,TABL(R2)
         SH    R3,=H'02'
DELCHK   CLI   3(R11),C'>'        CHECK FOR REVERSE SEARCH
         BE    REVSCAN
         EX    R3,FWD             COPY STRING TO BUFFER
         B     SCANIT
REVSCAN  MVINC STRBUFF,0(R3),4(R11),TABLE=OLD
         MVC   REVFLAG(2),=C'FF'  INDICATE REVERSE
SCANIT   SLR   R15,R15            R15 = 0; INIT TO NOT FOUND
         EX    R3,SCAN
         BZ    EXIT
         LR    R5,R1
         LA    R4,STRBUFF
         SR    R5,R4
         CLC   REVFLAG(2),=C'FF'  CHECK FOR REVERSE
         BE    ADJUST
         LA    R15,1(R5)          R15 = POSITION IN STRING
         B     EXIT
ADJUST   SR    R3,R5              ADJUST POSITION FOR REVERSE
         LR    R15,R3             R15 = POSITION IN STRING
*
* RETURN TO WHOEVER CALLED US:
*
EXIT     LEAVER
         SPACE
FWD      MVC   STRBUFF(0),4(R11)
SCAN     TRT   STRBUFF,TABL
         EJECT
*
* STATIC WORKAREA:
*
         LTORG
         EJECT
***********************************************************************
***                     DYNAMIC WORKAREA                            ***
***********************************************************************
         SPACE
WORKAREA DSECT
SAVEAREA DS    18F
REVFLAG  DS    H
STRBUFF  DS    CL256
TABL     DS    CL256
WORKLEN  EQU   *-WORKAREA
         END
