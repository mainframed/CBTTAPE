ISPCDSN TITLE ' - DATASET SELECTION BY CURSOR FROM ISPF DISPLAY'
***********************************************************************
***                                                                 ***
***  Author:                                                        ***
***      Gordon J. Schillinger                                      ***
***      Washington Data Processing Service Center                  ***
***      Ph.  (206) 902-3147                           03/19/87     ***
***                                                                 ***
***                                                                 ***
***  Modification log:                                              ***
***                                                                 ***
***      V3R1                                          10/20/88     ***
***        - Provided a default variable name to contain the        ***
***          dataset name, with an option to override.              ***
***        - Added an additional variable to dataset processing     ***
***          that returns the cursor position within the dsname.    ***
***        - Allow coding of an options list to include or limit    ***
***          the amount of information returned to the application. ***
***        - Added an option to obtain cursor position on screen    ***
***          as well as the current panel id.                       ***
***        - Added an option to allow the extraction of an entire   ***
***          ISPF panel screen image.                               ***
***                                                                 ***
***      V2R1                                          03/01/88     ***
***        - Extracts dataset names from updated screen image to    ***
***          allow processing of message fields.                    ***
***        - Extracts member names or dataset names within a set    ***
***          of parentheses.                                        ***
***        - Allows cursor positioning on beginning and ending      ***
***          quotes and parentheses.                                ***
***                                                                 ***
***      Original version                              03/19/87     ***
***                                                                 ***
***********************************************************************
         EJECT
***********************************************************************
***                                                                 ***
***  Ispcdsn will extract a dataset, pointed to by the cursor,      ***
***  from an ISPF display panel.  Dataset names are scanned for     ***
***  following standard naming and length conventions.              ***
***                                                                 ***
***  Optionally, an override variable name to contain the dataset   ***
***  name may be passed to the program as a parameter, and will be  ***
***  placed into the SHARED variable pool if no errors are          ***
***  encountered.  If errors are encountered, a return code         ***
***  reflecting the type of error will be returned following        ***
***  completion of the program.  Although this was previously a     ***
***  required parameter, it is no longer necessary since the        ***
***  dataset name is returned, by default, to the variable          ***
***  &ISPCDSN.                                                      ***
***                                                                 ***
***  Other enhancements include the availablity of screen           ***
***  statistics pertaining to cursor location and panel id and the  ***
***  availablity of a full screen image.                            ***
***                                                                 ***
***  Return Codes:                                                  ***
***    (4)  - Dataset name contains unmatched quotes                ***
***    (8)  - Dataset name missing between quotes                   ***
***    (12) - Cursor not on a dataset name                          ***
***    (16) - Dataset name larger than the 56 byte maximum          ***
***    (20) - Control Block parameter error                         ***
***                                                                 ***
***********************************************************************
         EJECT
***********************************************************************
***                                                                 ***
***   SYNTAX:                                                       ***
***                                                                 ***
***   ISPCDSN SELECT PGM(ISPCDSN) PARM(xxxx (NODsn  ))              ***
***                                          SCReen                 ***
***                                          FSCReen                ***
***   where:                                                        ***
***      xxxx    - is the name of the variable in which you wish    ***
***                use as an override to the default dataset        ***
***                variable &ISPCDSN.   (OPTIONAL, user specified)  ***
***                                                                 ***
***      NODsn   - requests ISPCDSN to bypass dataset name          ***
***                processing.  This is the default when FSCReen    ***
***                is specified.  If not specified with SCReen      ***
***                option, cursor must be on a valid dataset name.  ***
***                Otherwise, a bad return code indicating the      ***
***                invalidity of the dataset string will be set     ***
***                and none of the variables will be return via     ***
***                the shared variable pool.   (OPTIONAL)           ***
***                                                                 ***
***      SCReen  - requests screen characteristics be returned,     ***
***                which include cursor row position (&ISPCROW),    ***
***                cursor column position (&ISPCCOL), and panel     ***
***                id of current display (&ISPCPID).   (OPTIONAL)   ***
***                                                                 ***
***      FSCReen - requests the full set of screen information      ***
***                variables, which consists of ones set by SCReen  ***
***                and one which returns a mirror image of the      ***
***                current screen (&ISPCSCR).  The screen image     ***
***                size (in bytes) is returned in &ISPCSCS.  By     ***
***                default, dsname processing is bypassed unless    ***
***                an override dsn is specified.   (OPTIONAL)       ***
***                                                                 ***
***      NOTE:  All parameters are optional.  Therefore, if none    ***
***             are specified, only dataset processing is           ***
***             performed.  This is due, in part, to the fact that  ***
***             this was the only function ISPCDSN was originally   ***
***             designed to perform.  Added to the dataset          ***
***             processing was the returning of the cursor          ***
***             position within the dataset string in &ISPCPOS.     ***
***                                                                 ***
***     VARIABLES RETURNED:                                         ***
***        &ISPCDSN or &xxxx - dataset name at cursor               ***
***        &ISPCPOS          - cursor position in dataset string    ***
***        &ISPCROW          - cursor row position on screen        ***
***        &ISPCCOL          - cursor col position on screen        ***
***        &ISPCPID          - current paned id of display          ***
***        &ISPCSCS          - screen size (bytes) returned         ***
***        &ISPCSCR          - screen image currently displayed     ***
***                                                                 ***
***********************************************************************
         EJECT
***********************************************************************
***          --- V3R1 Examples ---                                  ***
***                                                                 ***
***    ISPEXEC SELECT PGM(ISPCDSN)                                  ***
***    IF &LASTCC = 0 THEN +                                        ***
***      ISPEXEC VGET (ISPCDSN) SHARED                              ***
***    - will, by default, return the dsname in &ISPCDSN, along     ***
***      with the cursor position, &ISPCPOS, within the dsname.     ***
***                                                                 ***
***    ISPEXEC SELECT PGM(ISPCDSN) PARM(DSN (FSCR))                 ***
***    ISPEXEC VGET (DSN ISPCSCR) SHARED                            ***
***    - will return dsname in &DSN, cursor position within dsname  ***
***      in &ISPCPOS, and the entire screen image in &ISPCSCR.      ***
***      Also, &ISPCROW, &ISPCCOL and &ISPCPID will contain cursor ***
***      position and panel id.  If DSN were not specified as an    ***
***      override dsn, the dsname would not have been returned to   ***
***      the application, as it is mutually exclusive with FSCREEN, ***
***      by default.                                                ***
***                                                                 ***
***    ISPEXEC SELECT PGM(ISPCDSN) PARM( (SCR))                     ***
***    ISPEXEC VGET (DSN ISPCSCR) SHARED                            ***
***    - will return dsname in &ISPCDSN, cursor position within     ***
***      dsname in &ISPCPOS, and cursor row, column and panel id    ***
***      in &ISPCROW, &ISPCCOL, and &ISPCPID.                       ***
***                                                                 ***
***********************************************************************
         EJECT
***********************************************************************
*                 --V2R1 EXAMPLES--    ( | = CURSOR IN EXAMPLES)      *
* Here's an example of the updated screen image enhancement.  Prior   *
* to V2R1, trying to extract this VIO dsn following a compress        *
* would result in a null line.  Now; however, the VIO dataset         *
* displayed when help is entered can be browsed to view the output    *
* from the compress operation.  (via HOTKEYS)                         *
*                                                                     *
* ----------------------------  LIBRARY UTILITY  ----------- COMPRESS *
* OPTION  ===>                                                        *
* COMPRESS LISTING IS IN SYS88062.T095611.RA000.GS#00155.R0000004     *
*    BLANK - DISPLAY MEMBER LIST   |       B - BROWSE MEMBER          *
*    C - COMPRESS DATA SET                 P - PRINT MEMBER           *
*    X - PRINT INDEX LISTING               R - RENAME MEMBER          *
*    L - PRINT ENTIRE DATA SET             D - DELETE MEMBER          *
*    I - DATA SET INFORMATION              S - DATA SET INFORMATION (S*
*                                                                     *
* ISPF LIBRARY:                                                       *
*    PROJECT ===> SOFTD155                                            *
*    GROUP   ===> MISC      ===>           ===>           ===>        *
*    TYPE    ===> CLIST                                               *
*                                                                     *
*                                                                     *
* RESULTS IN:                                                         *
*                                                                     *
* BROWSE -- SYS88062.T095611.RA000.GS#00155.R0000004 - LINE 000000 COL*
* COMMAND ===>                                                  SCROLL*
* ********************************* TOP OF DATA  **********************
* -----   PDSFAST/XA      VER 3.3FC        CPUID 3084-66122146        *
*    COPYRIGHT (C) 1986 SOFTWARE ENGINEERING OF AMERICA, INC.       DA*
*                                                                     *
* PDF101I  START PDSFAST EXECUTION                                    *
*                                                                     *
* PDF103C        COPY OUTDD=ISP09561,INDD=ISP09561                    *
* PDF106F  COMPRESS VOLSER/DSN: SYS014 / GS00155.JOBS.CNTL            *
* PDF110I  OPERATION IS COMPRESS - ISP09561 (DISK)                    *
* PDF156M  ADMUSP6   - MOVED -    ADR 000225  0019000225              *
* PDF156M  ALIST     IN PLACE     ADR 00011D  001900011D              *
* PDF156M  APDGEOF   IN PLACE     ADR 00001D  001900001D              *
* PDF156M  APGDSTY   IN PLACE     ADR 000027  0019000027              *
* PDF156M  COPYFILE  IN PLACE     ADR 00001B  001900001B              *
* PDF156M  CSRDSN    - MOVED -    ADR 000301  0019000301              *
* PDF156M  CT        IN PLACE     ADR 000025  0019000025              *
* PDF156M  DEFNTABL  - MOVED -    ADR 000217  0019000217              *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                 --V2R1 EXAMPLES--  (cont.)                          *
* Here's an example of the dsname within parentheses enhancement:     *
*                                                                     *
*  ALLOC F(CDSN) DSN(MY.LOAD)                                         *
*                       |                                             *
*                                                                     *
*    Now returns "MY.LOAD" instead of "DSN(MY.LOAD)" which would      *
*    result in an "INVALID DATASET NAME" when processed, as in        *
*    the previous release.                                            *
*                                                                     *
*    A dataset name and member may be extracted as before if cursor   *
*    is placed on the dataset name portion:                           *
*                                                                     *
*  SRC.ASM(ISPCDSN)                                                   *
*       |                                                             *
*                                                                     *
*                                                                     *
*  Cursor may be positioned on beginning or ending quotes:            *
*                                                                     *
*  'GS00155.SRC.ASM(ISPCDSN)'                                         *
*  |                                                                  *
*           OR                                                        *
*  'GS00155.SRC.ASM(ISPCDSN)'                                         *
*                           |                                         *
*   ...however, the first would return the dsn and member and         *
*      the second, only the member.                                   *
*                                                                     *
***********************************************************************
         EJECT
ISPCDSN  ENTERR WA=WORKAREA,SA=SAVEAREA,WL=WORKLEN,LEVEL=V310
         EJECT
***********************************************************************
***                  REGISTER USAGE                                 ***
***      R2 = WORK                R3 = WORK                         ***
***      R4-> TLD CONTROL BLOCK   R5->CURRENT SCREEN IMAGE          ***
***      R6 = OFFSET TO CURSOR    R7 = WORK                         ***
***      R8-> DSNAME LEFT DELIM   R9-> CURSOR ON SCREEN             ***
***      R10= WORK                R11->ENTRY PARMLIST               ***
***********************************************************************
         LR    R11,R1             R11-> PARMLIST
         L     R4,0(R11)          R4->  CONTROL BLOCK
         BAL   R14,FMTPARMS       FORMAT ENTRY PLIST
         CLC   0(2,R4),=C'TLD'    ARE WE AT A TLD?
         BE    VARINIT            YES, INITIALIZE VARS
         SETRET RC=20             NO,  EXIT WITH RC(20)
VARINIT  BAL   R14,IVAR           INITIALIZE ISPF VARIABLES
GETCSR   L     R5,128(R4)         R5-> CURRENT SCREEN IMAGE
         LH    R6,166(R4)         R6 = OFFSET TO CURSOR
         TM    OPFLAG,X'06'       SCR OR FSCR?
         BZ    GETDSN             NO,  SKIP SCREEN PROCESSING
GETINFO  BAL   R14,SCRSTATS       YES, GET SCREEN INFO
         TM    OPFLAG,X'84'       OVERRIDE DSN & FSCR?
         BO    GETDSN             YES, PROCESS DSN
         TM    OPFLAG,X'0C'       NODSN, OR FSCR?
         BNZ   SETEM              YES, SKIP DSN PROCESSING
GETDSN   MVC   TRNTBL,TBLINIT     NO,  INIT DSN TRANS TABLE
         BAL   R14,DSNPROC             & EXTRACT DSNAME
         LTR   R15,R15            DSNPROC RC=(0)?
         BNZ   RETURN             NO, RETURN W/ BAD RC
SETEM    BAL   R14,SVAR           SET SHARED VARIABLES
         SETRET                   RC(0); NO ERRORS
*
* RETURN TO WHOEVER CALLED US:
RETURN   LEAVER
         EJECT
***********************************************************************
***            SUBROUTINE TO PARSE ENTRY PARMLIST                   ***
***              ENTRY:   R11-> ISPCDSN ENTRY PLIST                 ***
***********************************************************************
FMTPARMS ST    R14,RETADDR
         L     R3,4(R11)          R3-> NON-STD CMD BUFF
         PARSEKMD BUFF=(R3),BUFFLEN=100,FLAG=OPFLAG,PARM=WORKAREA
         L     R14,RETADDR
         BR    R14
         EJECT
***********************************************************************
***           SUBROUTINE TO EXTRACT SCREEN STATISTICS               ***
***             ENTRY:   R5-> CURRENT SCREEN IMAGE                  ***
***                      R6 = SCREEN OFFSET TO CSR                  ***
***********************************************************************
SCRSTATS ST    R14,RETADDR
         IC    R3,=C')'
         STC   R3,USRVARS-1       MARK END OF SYSVAR LIST
         LA    R2,VARNAMES
         CALL ISPLINK,(=CL8'VGET',0(R2),=CL8'SHARED'),VL,MF=(E,RPPL)
         IC    R3,=C' '
         STC   R3,USRVARS-1       RESET LIST
         LR    R3,R6              COPY OFFSET TO LOW ORDER REG
         LA    R3,1(,R3)          CVT OFFSET TO REL SCR POS
         LA    R2,0               CLEAR HIGH ORDER REG
         D     R2,SCRW            R2 = CSR COL POSITION
         LTR   R2,R2              CSR = LRECL?
         BZ    SETPOS             YES, NO ADJ REQUIRED
         LA    R3,1(,R3)          NO,  ADJ ROW POSITION
SETPOS   ST    R2,CSRCOL          SAVE COL POSITION
         ST    R3,CSRROW          SAVE ROW POSITION
         MVC   PANELID(8),344(R4) CAPTURE PANEL ID
         TM    OPFLAG,X'04'       FSCREEN?
         BNO   SCRRET             NO,  SKIP IT
         LH    R2,SCRW+2          GET SCREEN WIDTH
         MH    R2,SCRD+2          CALC SCREEN SIZE
         ST    R2,SCRSIZE         STORE SCREEN SIZE IN POOL
         MOVE  SCREEN,(R2),(R5)   CAPTURE SCREEN IMAGE
SCRRET   L     R14,RETADDR
         BR    R14
         EJECT
***********************************************************************
***           SUBROUTINE TO INITIALIZE ISPF VARIABLES               ***
***********************************************************************
IVAR     ST    R14,RETADDR
         MVC   VARRAYS(VARRLEN),VARRINIT   INITIALIZE ARRAYS
         MVI   DSNAME,C' '
         MVC   DSNAME+1(55),DSNAME         CLEAR DSNAME
         MVC   PANELID(8),=CL8' '          CLEAR PANELID
         LA    R2,VARNAMES                 R2-> VARIABLE LIST
         CALL ISPLINK,(=CL8'VDEFINE',0(R2),VARSTOR,VARFMTS,            X
               VARLENS,=CL8'LIST'),VL,MF=(E,RPPL)
         L     R14,RETADDR
         BR    R14
***********************************************************************
***           SUBROUTINE TO REPLACE AN ISPF VARIABLE                ***
***********************************************************************
RVAR     ST    R14,RETADDR2
         LA    R2,ODSNVAR+2       R2-> REPLACEMENT VAR NAME
         CALL ISPLINK,(=CL8'VREPLACE',0(R2),=F'56',DSNAME),            X
               VL,MF=(E,RPPL)
         L     R14,RETADDR2
         BR    R14
         EJECT
***********************************************************************
***           SUBROUTINE TO PROCESS DATA SET NAME                   ***
***             ENTRY:  R5-> SCREEN IMAGE                           ***
***                     R6-> OFFSET TO CURSOR                       ***
***********************************************************************
DSNPROC  ST    R14,RETADDR
         LA    R15,0              NORMAL RC
         LA    R9,0(R6,R5)        R9-> CURSOR LOCATION
         LR    R3,R6              R3 = COPY OF CSR OFFSET
         LA    R7,0               CLR FOR TABLE MODIFY
         IC    R7,=C'('
         STC   R7,TRNTBL(R7)      MAKE LEFT PAREN A DELIMETER
         TRT   0(1,R9),TRNTBL     ARE WE ON DATA SET NAME?
         BZ    SCANLFT            YES, BEGIN SCANNING FOR START
         CLI   0(R9),C'('         IS IT LEFT PAREN?
         BZ    EXPARENS           YES, EXTRACT WITHIN PARENS
         CLI   0(R9),C''''        IS IT A QUOTE?
         BNZ   CSRINVLD           NO, CURSOR INVALID
         TRT   1(1,R9),TRNTBL     IS DSN TO RIGHT OF QUOTE?
         BZ    SCANLFT            YES, PREPARE TO EXTRACT IT
         BCTR  R9,0               NO,  CHECK LEFT OF QUOTE
         TRT   0(1,R9),TRNTBL     WAS IT THERE?
         BNZ   CSRINVLD           NO,  CURSOR INVALID
         BCTR  R6,0               YES, BACK SPACE CURSOR
         B     SCANLFT                 AND SCAN LEFT
CSRINVLD SETRET RC=12,L=DSNRET    EXIT WITH RC(12)
SCANLFT  LA    R9,0(R6,R5)        R9-> CURSOR LOCATION
         TRT   0(1,R9),TRNTBL     BYTE IN TABLE?
         BNZ   LFTDELTR           NO,  FOUND LEFT DELIMETER
         BCTR  R6,R0              YES, DECREMENT OFFSET; CHECK NEXT
         B     SCANLFT
LFTDELTR CLI   0(R9),C'('         WAS DELIMETER LEFT PAREN?
         BNE   INPARENS           NO, INCLUDE PARENS IN DSN
EXPARENS IC    R7,=C')'
         STC   R7,TRNTBL(R7)      MAKE RIGHT PAREN A DELIMETER
         B     LFTPTR             EXTRACT WITHING PARENS
INPARENS IC    R7,=C'('
         LA    R8,0
         STC   R8,TRNTBL(R7)      INCLUDE PARENS IN DSN IF PRESENT
         CLI   0(R9),C''''        WAS DELIMETER A QUOTE?
         BNE   LFTPTR             NO,  LEAVE BITS UNCHANGED
         OI    QFLAG,X'40'        YES, FLIP BIT 6; INDICATES LEFT QUOTE
LFTPTR   LA    R8,0(R9)           R8-> LEFT DELIMETER
         LA    R6,1(R6)           INCR TO LEFTMOST POSITION
         LA    R9,0(R6,R5)        R9-> LEFT POSITION OF DSNAME
         SR    R1,R1              CLEAR FOR TEST
         SR    R2,R2              CLEAR FOR TEST
SCANRT   TRT   0(56,R9),TRNTBL    SCAN UNTIL NOT IN TABLE OR BUFFER END
         CR    R1,R2              HAVE REGS. CHANGED?
         BNE   EXTRACT            YES, EXTRACT DATASET
         SETRET RC=16,L=DSNRET    NO,  EXIT WITH RC(16)
EXTRACT  LA    R7,0(R1)           R7-> RIGHT DELIMETER
         CLI   0(R7),C''''        WAS DELIMETER A QUOTE?
         BNE   MASKCHK            NO,  LEAVE BITS UNCHANGED
         OI    QFLAG,X'80'        YES, FLIP BIT 7;INDICATES RIGHT QUOTE
MASKCHK  TM    QFLAG,X'C0'        CHECK FOR QUOTES SURROUNDING DSNAME
         BZ    SETVPREP           NO QUOTES
         BO    QUOTES             MATCHING QUOTES
         SETRET RC=4,L=DSNRET     UNMATCHED; EXIT WITH RC(4)
QUOTES   LA    R9,0(R8)           PICK UP LEFT QUOTE
         BCTR  R6,0               ADJ OFFSET TO QUOTE
         BCTR  R7,0               DECR TO RT DELIMETER-1
         CR    R9,R7              ARE ADDRESSES SAME?
         BNE   RTQUOTE            NO,  INCLUDE RIGHT QUOTE
         SETRET RC=8,L=DSNRET     YES, MISSING DSNAME; EXIT WITH RC(8)
RTQUOTE  LA    R7,2(R7)           PICK UP RIGHT QUOTE
SETVPREP SR    R7,R9              R7 = LENGTH OF DATASET NAME
         BCTR  R7,0               ADJ FOR EX
         EX    R7,COPYDSN         COPY DSN TO VAR STOR AREA
         TM    OPFLAG,X'80'       OVERRIDE DEFAULT DSN VAR?
         BNO   DSNPOS                NO,  RETAIN DEFAULT
         MVC   USRVARS(8),ODSNVAR+2  YES, UPDATE VAR IN LIST
         BAL   R14,RVAR           CREATE NEW VAR & COPY OLD
DSNPOS   SR    R3,R6              CALC CSR POS IN DSN
         LA    R3,1(,R3)          R3 = CVT'D REL POS
         ST    R3,CSRPOS          PLACE IN FUNC VAR POOL
DSNRET   L     R14,RETADDR
         BR    R14
COPYDSN  MVC   DSNAME(0),0(R9)
         EJECT
***********************************************************************
***           SUBROUTINE TO SET ISPF VARIABLES                      ***
***********************************************************************
SVAR     ST    R14,RETADDR
         LA    R2,USRVARS-1
         IC    R3,=C'('
         STC   R3,0(R2)
         CALL ISPLINK,(=CL8'VPUT',0(R2),=CL8'SHARED'),VL,MF=(E,RPPL)
         L     R14,RETADDR
         BR    R14
         EJECT
***********************************************************************
***                  PARSE MACRO AREA                               ***
***********************************************************************
OPLIST   OPERLIST SUBL,POS         PARSE SUBL FIRST; POS IS OPTIONAL
POS      OPER ,X'8000',POSITIONAL=YES,SUBFLD=ODSNVAR
SUBL     OPER ,X'4000',SUBLIST=SUBLST,POSITIONAL=YES
SUBLST   OPERLIST SKWD1,SKWD2,SKWD3
SKWD1    OPER NODSN,X'0800',MINLEN=3
SKWD2    OPER FSCREEN,X'0400',MINLEN=4
SKWD3    OPER SCREEN,X'0200',MINLEN=3
         EJECT
***********************************************************************
***           TRANSLATE INITIALIZATION TABLE                        ***
***********************************************************************
TBLINIT  DC    256X'FF'
         ORG   TBLINIT+C'.'
         DC    X'00'              .
         ORG   TBLINIT+C'$'
         DC    X'00'              $
         ORG   TBLINIT+C'#'
         DC    2X'00'             # @
         ORG   TBLINIT+C'a'
         DC    9X'00'             a..i
         ORG   TBLINIT+C'('
         DC    X'00'              (
         ORG   TBLINIT+C'j'
         DC    9X'00'             j..r
         ORG   TBLINIT+C')'
         DC    X'00'              )
         ORG   TBLINIT+C's'
         DC    8X'00'             s..z
         ORG   TBLINIT+C'A'
         DC    9X'00'             A..I
         ORG   TBLINIT+C'J'
         DC    9X'00'             J..R
         ORG   TBLINIT+C'S'
         DC    8X'00'             S..Z
         ORG   TBLINIT+C'0'
         DC    10X'00'            0..9
         ORG
TBLSIZE  EQU   250                LENGTH OF TABLE (BYTES)
         EJECT
***********************************************************************
***                     STATIC WORKAREA                             ***
***********************************************************************
SCRMAX   EQU   4096               MAXIMUM SCREEN SIZE
VARRINIT DS    0F
         DC    C'('
         DC    CL9'ZSCREEND'      *SYS VAR*
         DC    CL9'ZSCREENW'      *SYS VAR*
         DC    CL9'ISPCDSN'
         DC    CL9'ISPCPOS'
         DC    CL9'ISPCROW'
         DC    CL9'ISPCCOL'
         DC    CL9'ISPCPID'
         DC    CL9'ISPCSCS'
         DC    CL9'ISPCSCR'
         DC    C')'
         DS    0F
         DC    CL8'FIXED'         ZSCREEND FORMAT  *SYS*
         DC    CL8'FIXED'         ZSCREENW FORMAT  *SYS*
         DC    CL8'CHAR'          ISPCDSN FORMAT
         DC    CL8'FIXED'         ISPCPOS FORMAT
         DC    CL8'FIXED'         ISPCROW FORMAT
         DC    CL8'FIXED'         ISPCCOL FORMAT
         DC    CL8'CHAR'          ISPCPID FORMAT
         DC    CL8'FIXED'         ISPCSCS FORMAT
         DC    CL8'CHAR'          ISPCSCR FORMAT
         DC    F'4'               ZSCREEND LENGTH  *SYS*
         DC    F'4'               ZSCREENW LENGTH  *SYS*
         DC    F'56'              ISPCDSN LENGTH
         DC    F'4'               ISPCPOS LENGTH
         DC    F'4'               ISPCROW LENGTH
         DC    F'4'               ISPCCOL LENGTH
         DC    F'8'               ISPCPID LENGTH
         DC    F'4'               ISPCSCS LENGTH
         DC    A(SCRMAX)          ISPCSCR LENGTH
VARRLEN  EQU   *-VARRINIT
         LTORG
         EJECT
***********************************************************************
***                     DYNAMIC WORKAREA                            ***
***********************************************************************
         SPACE
WORKAREA DSECT
SAVEAREA DS    18F
RETADDR  DS    A
RETADDR2 DS    A
QFLAG    DS    X                  QUOTED DSN FLAG
TRNTBL   DS    XL(TBLSIZE)        TRANSLATION TABLE FOR DSN
OPFLAG   DS    X                  OPERANDS FLAG
ODSNVAR  DS    H                  OVERRIDE DSN VAR LEN
         DS    CL8                OVERRIDE DSN VAR NAME
RPPL     CALL  ,(0,0,0,0,0,0),MF=L
         EJECT
KPPL     KPPL
         EJECT
*
*  VARIABLE ARRAYS FOR ISPF SERVICES
VARRAYS  DS    0F
VARNAMES DS    C                  (
SYSVARS  DS    CL9                ZSCREEND  *SYS*
         DS    CL9                ZSCREENW  *SYS*
USRVARS  DS    CL9                ISPCDSN OR SPECIFIED DSN
         DS    CL9                ISPCPOS
         DS    CL9                ISPCROW
         DS    CL9                ISPCCOL
         DS    CL9                ISPCPID
         DS    CL9                ISPCSCS
         DS    CL9                ISPCSCR
         DS    C                  )
VARFMTS  DS    0F
         DS    CL8                ZSCREEND FORMAT  *SYS*
         DS    CL8                ZSCREENW FORMAT  *SYS*
         DS    CL8                ISPCDSN FORMAT
         DS    CL8                ISPCPOS FORMAT
         DS    CL8                ISPCROW FORMAT
         DS    CL8                ISPCCOL FORMAT
         DS    CL8                ISPCPID FORMAT
         DS    CL8                ISPCSCS FORMAT
         DS    CL8                ISPCSCR FORMAT
VARLENS  DS    F                  ZSCREEND LENTH  *SYS*
         DS    F                  ZSCREENW LENTH  *SYS*
         DS    F                  ISPCDSN LENGTH
         DS    F                  ISPCPOS LENGTH
         DS    F                  ISPCROW LENGTH
         DS    F                  ISPCCOL LENGTH
         DS    F                  ISPCPID LENGTH
         DS    F                  ISPCSCS LENGTH
         DS    F                  ISPCSCR LENGTH
VARSTOR  DS    0F
SCRD     DS    F
SCRW     DS    F
DSNAME   DS    CL56
CSRPOS   DS    F
CSRROW   DS    F
CSRCOL   DS    F
PANELID  DS    CL8
SCRSIZE  DS    F
SCREEN   DS    CL(SCRMAX)
*
WORKLEN  EQU   *-WORKAREA
         END
