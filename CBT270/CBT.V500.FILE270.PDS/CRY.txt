ENCRYPT  CSECT
*
*
***********************************************************************
***********************************************************************
*                                                                     *
*        AUTHOR: D.L.SCHWARZ                                          *
*                                                                     *
*        DATE: OCT. 17, 1985. - VERSION 2, MOD. 0                     *
*                                                                     *
*        FUNCTION:                                                    *
*                                                                     *
*           THIS MODULE PERFORMS THE FUNCTION OF ENCRYPTION AND       *
*           DECRYPTION OF DATA OPERATING AS A PRIMARY COMMAND         *
*           UNDER ISPF/PDF EDIT.  THE MODULE IS LINKED INTO THE       *
*           COMMAND LIBRARY UNDER THE NAME 'CRY'.                     *
*                                                                     *
*                                                                     *
*        OPERATION:                                                   *
*                                                                     *
*           THE ENCRYPTION IS PERFORMED BY UTILIZING A KEY VALUE      *
*           SUPPLIED AS AN OPERAND OF THE PRIMARY COMMAND.  THIS      *
*           VALUE IS PASSED TO PROGRAM R050A90 AT ENTRY 'SETKEY',     *
*           FOLLOWED BY RECURSIVE CALLS TO THE PROGRAM AT ENTRY       *
*           POINTS 'ENCIPH' OR 'DECIPH', DEPENDING ON THE FUNCTION    *
*           DESIRED.                                                  *
*                                                                     *
*           EXAMPLES OF USE:                                          *
*                                                                     *
*           !CRY ENCRYPT,KEYVALUE                                     *
*           !CRY DECRYPT,KEYVALUE                                     *
*                                                                     *
*           THE 'KEYVALUE' IS A USER SUPPLIED KEYWORD WHICH IS        *
*           MASSAGED AND USED AS THE ENCRYPT/DECRYPT CODE VALUE.      *
*           THE KEYWORD IS LIMITED TO EIGHT (8) BYTES IN LENGTH,      *
*           BUT MAY BE SHORTER, THIS PROGRAM WILL PAD IT TO THE       *
*           CORRECT LENGTH.                                           *
*                                                                     *
*        CHANGES/UPDATES:                                             *
*                                                                     *
*           10/17/85.  VERSION 2, MODIFICATION 0.                     *
*                                                                     *
*                                                                     *
***********************************************************************
***********************************************************************
*
***********************************************************************
*                                                                     *
*                      REGISTER EQUATES                               *
*                                                                     *
***********************************************************************
*
R0       EQU   0
R1       EQU   1
PARMREG  EQU   1
R2       EQU   2
PREG     EQU   2
KEYREG   EQU   2
R3       EQU   3
R4       EQU   4                    WORK REG
R5       EQU   5                    WORK REG
R6       EQU   6                    WORK REG
R7       EQU   7                    WORK REG
R8       EQU   8                    WORK REG
R9       EQU   9                    WORK REG
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13                   SAVE AREA PTR.
R14      EQU   14                   RETURN REGISTER
R15      EQU   15                   RETURN CODE REG.
*
***********************************************************************
*                                                                     *
*                 GENERAL EQUATES                                     *
*                                                                     *
***********************************************************************
*
*
BLANK    EQU   64                   BLANK
ZERO     EQU   0                    BINARY ZERO
KEYVAL   EQU   KEY
*
*
*
         USING ENCRYPT,R15          ESTABLISH BASE
         B     BEGIN
*
         DC    CL36' ENCRYPT  - VERSION 2.00 - 10/17/85. '
*
BEGIN    EQU   *
         STM   14,12,12(R13)        SAVE ALL REGISTERS
         LA    R2,SAVEA             OUR SAVE AREA
         ST    R2,8(,R13)           CHAIN POINTER
         ST    R13,SAVEA+4          CHAIN POINTER
         LR    R13,R2               POINT TO OURS
         LR    R12,R15
         DROP  R15
         USING ENCRYPT,R12          SET OUR BASE
*
***********************************************************************
*        THE FOLLOWING CALLS TO ISPLINK ARE TO OBTAIN THE VALUES
*        NECESSARY FOR THE OPERATION OF THE PROGRAM
***********************************************************************
*
***********************************************************************
*    1.  FIRST CALL TO OBTAIN THE FUCTION (ENCRYPT/DECRYPT)
*        DESIRED, AND THE KEY VALUE.
***********************************************************************
*
         CALL  ISPLINK,(VDEF,NL,FCDE,TYPEC,LEN8),VL
*
***********************************************************************
*    2.  SECOND CALL TO DEFINE THE BUFFER AREA THAT WE WILL BE
*        USING.
***********************************************************************
*
         CALL  ISPLINK,(VDEF,LL,BUFFER,TYPEC,LEN256),VL
*
***********************************************************************
*    3.  THIRD CALL TO OBTAIN THE LOGICAL RECORD LENGTH (LRECL) OF
*        THE DATA WE ARE OPERATING ON.
***********************************************************************
*
         CALL  ISPLINK,(VDEF,DL,LRECL,TYPEF,LEN4),VL
*
***********************************************************************
*    4.  FOURTH CALL TO DEFINE THIS PROGRAM AS AN EDIT MACRO.
***********************************************************************
*
         CALL  ISPLINK,(EDIT,LEN0,PARM1)
*
***********************************************************************
*    5.  FIFTH CALL TO DEFINE THE LOGICAL RECORD LENGTH (LRECL) TO
*        EDIT.
***********************************************************************
*
         CALL  ISPLINK,(EDIT,LEN0,PARM2)
*
***********************************************************************
*    6.  SIXTH CALL TO OBTAIN THE LINE NUMBER OF THE LAST RECORD IN
*        THE CURRENT DATA SET.
***********************************************************************
*
         CALL  ISPLINK,(EDIT,LEN0,PARM3)
*
***********************************************************************
*    7.  SEVENTH CALL TO SET THE CURRENT PROFILE TO 'CAPS OFF' TO
*        ASSURE THAT SPF WILL NOT FOLD THE ENCRYPTED DATA TO UPPER
*        CASE.
***********************************************************************
*
         CALL  ISPLINK,(EDIT,LEN0,PARM6)
*
***********************************************************************
*        ALL NECESSARY PARAMETERS HAVE NOW BEEN OBTAINED AND DEFINED.
*        NOW CHECK THE FUNCTION CODE FOR VALIDITY (ENCRYPT/DECRYPT).
***********************************************************************
*
         OC    FCDE(7),BLANKS       FOLD THE CODE TO UPPER CASE
         CLC   FCDE(7),ENCMDUP      COMPARE FOR OURS
         BE    ENCIPH1              ITS US, GO PROCESS
         CLC   FCDE(7),DECMDUP      COMPARE FOR OURS
         BNE   EXIT                 NOT US, EXIT
*
DECIPH1  EQU   *
         MVI   FUNC+3,X'04'         SET FUNCTION FOR DECRYPT
         B     ITSUS                GO PROCESS
ENCIPH1  EQU   *
         MVI   FUNC+3,X'00'         SET FUNCTION FOR ENCRYPT
*
***********************************************************************
*        NOW CHECK THE KEY VALUE SPECIFIED.
***********************************************************************
*
ITSUS    EQU   *
         SR    PREG,PREG
         LA    PREG,KEY             POINT TO KEY
         SR    R4,R4
SEARCH   EQU   *
         LA    R4,1(R4)             START COUNT
         CH    R4,LIMIT             LIMIT REACHED?
         BE    FOUNDB               YES
         CLI   0(PREG),BLANK        FOUND BLANK?
         BE    FOUNDB               YES
         CLI   0(PREG),ZERO         BINARY ZEROS?
         BE    FOUNDB               YES
         LA    PREG,1(PREG)         NO, INCREMENT
         B     SEARCH               AND GO BACK
FOUNDB   EQU   *
         CH    R4,ONE               ANY DATA?
         BE    EXIT                 NO, EXIT
*
         OC    KEYVAL(8),BLANKS     FOLD TO UPPER CASE
*
***********************************************************************
*        LINK TO THE EN/DECIPH PROGRAM AT ENTRY POINT 'SETKEY'
*        TO ESTABLISH THE KEY VALUE.
***********************************************************************
*
         LA    R1,KEYADD            SET UP PARM REG
         L     R15,SETADD           ADDRESS OF SETKEY ENTRY
         BALR  R14,R15              GO TO SETKEY
         LTR   R15,R15              CHECK RETURN CODE
         BNZ   ERRET                NOT ZERO, ERROR
*
         MVC   LINENO,=F'0'         SET LINE NUMBER TO ZERO
*
LOOP1    EQU   *
         L     R11,LINENO           GET LINE NUMBER
         LA    R11,1(,R11)          INCREMENT
         ST    R11,LINENO           STORE BACK
         CLC   LINENO,LLINE         LAST LINE DONE?
         BH    EXIT                 YES, DONE
*
***********************************************************************
*        CALL ISPLINK TO READ IN THE DESIGNATED LINE FROM THE
*        DATA SET.
***********************************************************************
*
         CALL  ISPLINK,(EDIT,LEN0,PARM4)
*
***********************************************************************
*
***********************************************************************
*        CALL THE EN/DECIPH ROUTINE TO PERFORM THE EN/DECRYPTION
*        ON THE CURRENT LINE.
***********************************************************************
*
         LA    PARMREG,PARMLIST     SET R1 FOR CIPH ROUTINE
         L     R6,FUNC              GET FUNCTION
         L     R15,CIPHADD(R6)      GET ENTRY POINT REQUESTED
         BALR  R14,R15              GO DO THE DESIRED FUNCTION
         LTR   R15,R15              CHECK RETURN CODE
         BNZ   ERRET                NOT ZERO, ERROR
*
***********************************************************************
*        CALL ISPLINK TO WRITE BACK THE NEWLY EN/DECRYPTED LINE.
***********************************************************************
*
         CALL  ISPLINK,(EDIT,LEN0,PARM5)
*
***********************************************************************
*
         B     LOOP1                GET THE NEXT RECORD
*
***********************************************************************
*        EXIT CODE FOLLOWS.
***********************************************************************
*
ERRET    EQU   *
         ST    R15,CONCODE          GET RETURN CODE
         B     EXIT1                LEAVE
*
EXIT     EQU   *
         SR    R15,R15              SET RETURN CODE
         B     EXIT1                LEAVE
*
EXIT1    EQU   *
         L     R13,SAVEA+4          RESTORE 13
         L     R14,12(,R13)         RESTORE 14
         L     R15,CONCODE          SET RETURN CODE
         LM    R0,R12,20(R13)       RESTORE 0-12
*
         BR    R14                  RETURN
*
*
***********************************************************************
*                                                                     *
*        CONSTANTS FOLLOW                                             *
*                                                                     *
***********************************************************************
*
         DS    0F                   ALIGN
*
SETADD   DC    V(SETKEY)            VCON OF SETKEY ROUTINE
CIPHADD  EQU   *
         DC    V(ENCIPH)            VCON OF ENCIPH ROUTINE
         DC    V(DECIPH)            VCON OF DECIPH ROUTINE
FUNC     DC    F'0'                 FUNCTION SAVE
CONCODE  DC    F'0'                 RETURN CODE SAVE
ONE      DC    X'00000001'
LIMIT    DC    H'8'                 LIMIT FOR KEY
ENCMDUP  DC    C'ENCRYPT'           COMPARE VALUE
DECMDUP  DC    C'DECRYPT'           COMPARE VALUE
BLANKS   DC    CL8' '               USED FOR FOLDING
*
NL       DC    C'(FCDE,KEY)'
*
         DS    0F                   ALIGN
FCDE     DC    CL8' '               AREA FOR THE FUNCTION
KEY      DC    CL8' '               AREA FOR THE KEY VALUE
*
DL       DC    C'(LRECL,LLINE,LINENO)'
*
         DS    0F                   ALIGN
LRECL    DC    F'0'                 AREA FOR THE DATA LENGTH
LLINE    DC    F'0'                 AREA FOR THE LAST LINE NUMBER
LINENO   DC    F'0'                 CURRENT LINE NUMBER
*
LL       DC    C'(BUFFER)'
*
         DS    0F                   ALIGN
VDEF     DC    CL8'VDEFINE'
EDIT     DC    CL8'ISREDIT'
TYPEC    DC    CL8'CHAR'
TYPEF    DC    CL8'FIXED'
LEN0     DC    F'0'
LEN4     DC    F'4'
LEN8     DC    F'8'
LEN256   DC    F'256'
*
PARM1    DC    C'¢MACRO (FCDE,KEY)¢'
*
PARM2    DC    C'¢(LRECL) = LRECL¢'
*
PARM3    DC    C'¢(LLINE) = LINENUM .ZLAST ¢'
*
PARM4    DC    C'¢(BUFFER) = LINE &&LINENO ¢'
*
PARM5    DC    C'¢LINE &&LINENO =(BUFFER) ¢'
*
PARM6    DC    C'¢ CAPS OFF ¢'
*
         DS    0F                   ALIGN
KEYADD   DC    A(KEYVAL)            KEY ADDRESS
PARMLIST DC    A(BUFFER)            BUFFER ADDRESS
         DC    A(BUFLEN)            BUFFER LENGTH ADDRESS
BUFLEN   DC    A(BUFFEND-BUFFER)    BUFFER LENGTH
SAVEA    DC    18F'0'               REGISTER SAVE AREA
*
BUFFER   DC    CL256' '             ENCRYPT/DECRYPT BUFFER
BUFFEND  EQU   *
*
         END ENCRYPT
