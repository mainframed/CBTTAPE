*          DATA SET 752ABDATE  AT LEVEL 001 AS OF 06/26/74
DATE     TITLE 'DATE OF YEAR ROUTINE'
***********************************************************************
*        *DATE*                                                       *
*         ----                                                        *
*   PURPOSE:  A SUBROUTINE TO COMPUTE AND RETURN THE PRESENT DATE.    *
*             THE DATE IS RETURNED IN THE FORM:                       *
*             DAY-OF-WEEK MONTH DAY, YEAR                             *
*             TYPICALLY:                                              *
*             THURSDAY NOVEMBER 13, 1969                              *
*                                                                     *
*             THE RETURNED STRING IS IN CHARACTER FORMAT, AND IS LEFT *
*             ADJUSTED IN A 7 WORD ARRAY. THE LONGEST DATE POSSIBLE   *
*             WILL JUST FILL 7 WORDS (28 BYTES).                      *
*   CALLING SEQUENCE:                                                 *
*               DIMENSION STRING(7)                                   *
*               CALL DATE(STRING)                                     *
*               WRITE (6,1) STRING                                    *
*             1 FORMAT (10X,7A4)                                      *
*                                                                     *
*   METHOD:                                                           *
*             STRIAGHT FORWARD. THE DAY OF THE WEEK SECTION IS        *
*             MODELLED AFTER A FORMULA CALLED 'ZELLER'S CONGRUENCE',  *
*             DOCUMENTED IN A BOOK CALLED  PROBLEMS FOR COMPUTER      *
*             SOLUTION, THE RAND CORPORATION                          *
*                                                                     *
*        NO INPUT IS NEEDED. NO OPTIONS OR DIAGNOSTICS ARE IMPLEMENTED*
*        AND ANY MALFUNCTIONS SHOULD BE REFFERED TO                   *
*        CUSTOMER SUPPORT, 902-3400                                   *
*                                                                     *
*                                            NOVEMBER 15, 1969        *
***********************************************************************
*                                                                     *
DATE     CSECT
         STM   14,12,12(13)    SAVE REGISTERS
         BALR  11,0            SET BASE REGISTER
         USING *,11            INFORM ASSEMBLER OF BASE REG.
         L     6,0(1)          LOAD REG 6 WITH A(ARGUMENT)
         TIME  DEC             CALL DATE
         SR    0,0             ZERO REG 0
         ST    1,RETURNED      STORE RETURNED DATE FOR LATER USE
         SRL   1,12            SHIFT OUT ALL BUT THE YEAR
         STM   0,1,CVBAREA     STORE YEAR IN WORK AREA
         OI    CVBAREA+7,X'0F' INSERT SIGN INTO YEAR
         CVB   1,CVBAREA       CONVERT YEAR INTO BINARY
         LR    9,1             KEEP YEAR IN REG 9
         D     0,=F'4'         DIVIDE YEAR BY FOUR TO DETERMINE
         LR    10,0            IF IT IS A LEAP YEAR
         UNPK  YEAR+2(2),CVBAREA+6(2) UNPK & INSERT YEAR IN '19--'
         SR    0,0             ZERO REG 0
         L     1,RETURNED      LOAD 1 WITH DATE RETURNED FROM SVC 11
         SLL   1,16            SHIFT OUT YEAR
         SRL   1,16            SHIFT BACK TO ORIGINAL POSITION
         STM   0,1,CVBAREA     STORE RESULT INTO WORK AREA
         CVB   1,CVBAREA       CONVERT THE DAY OF YEAR TO BINARY
         EJECT
         LA    8,11            |
         C     1,=F'31'        |
         BNH   END             |
         S     1,=F'31'        |
         LA    8,1(8)          |
         CR    10,0            |
         BE    LEAP            |
         C     1,=F'28'        |
         BNH   END             |  DETERMINE MONTH AND DAY OF
         S     1,=F'28'        |  THE MONTH, TAKING INTO ACCOUNT
         LA    8,1             |  LEAP YEARS.
         B     MARCH           |   REG7 = BCT REGISTER
LEAP     C     1,=F'29'        |  AT ARRIVAL TO STATMENT 'END'
         BNH   END             |   REG1 = DAY IN MONTH
         S     1,=F'29'        |   REG8 = MONTH COUNT (JAN=11,FEB=12,
         LA    8,1             |          MAR=1....DEC=10)
MARCH    LA    7,3             |
         B     MIDDLE          |
LOOP     C     1,=F'30'        |
         BNH   END             |
         S     1,=F'30'        |
         LA    8,1(8)          |
MIDDLE   C     1,=F'31'        |
         BNH   END             |
         S     1,=F'31'        |
         LA    8,1(8)          |
         BCT   7,LOOP          |
         B     MARCH           |
END      LR    7,8             LOAD REG 7 WITH MONTH COUNT
         C     8,=F'10'        SEE IF MONTH IS JAN OR FEB
         BNH   NTJANFEB        IF NOT GO TO 'NTJANFEB'
         S     9,=F'1'         SUBTRACT 1 FROM YEAR
NTJANFEB LR    2,9             |
         SRDA  8,32            |
         M     8,=F'26'        |
         S     9,=F'2'         |
         D     8,=F'10'        |  ZELLERS CONGRUENCE
         AR    9,1             |
         AR    9,2             |
         SRDA  2,32            |
         D     2,=F'4'         |
         AR    9,3             |
         S     9,=F'34'        |
         SR    8,8             |
         D     8,=F'7'         |
         EJECT
         MVI   0(6),X'40'      BLANK FIRST BYTE OF ARGUMENT
         MVC   1(27,6),0(6)    PROPOGATE BLANKS THOUGH ENTIRE STRING
         LR    5,1             LOAD REG 5 WITH DAY OF MO. CNT.
         MH    8,=H'12'        MULTIPLY BY TWELVE TO GET ADDRESS
         LA    4,DAYS(8)       LOAD REG 4 WITH ACTUAL ADDRESS
         MVC   0(12,6),0(4)    MOVE THAT DAY INTO ARGUMENT
         TRT   0(12,6),TRTABLE LOCATE FIRST BLANK IN ARGUMENT
         S     7,=F'1'         SUBTRACT 1 FROM MONTH COUNT
         MH    7,=H'12'        MULTIPLY MO. CNT. BY 12 TO GET ADDRESS
         LA    4,MONTHS(7)     LOAD REG 4 WITH ACTUAL ADDRESS
         MVC   1(12,1),0(4)    MOVE THE MONTH INTO ARGUMENT
         TRT   1(12,1),TRTABLE FIND NEXT BLANK IN STRING (ARG)
         CVD   5,CVBAREA       CONVERT DAY OF MONTH TO DECIMAL
         OI    CVBAREA+7,X'0F' INSERT CORRECT SIGN FOR PRINTING
         MVC   CVBAREA(4),PATTERN MOVE EDIT PATTERN TO WORK AREA
         ED    CVBAREA(4),CVBAREA+6 EDIT & UNPK DAY OF MONTH
         MVC   1(2,1),CVBAREA+2 MOVE DAY OF MONTH INTO STRING
         MVC   3(2,1),COMMA    MOVE DELIMITING COMMA INTO STRING
         MVC   5(4,1),YEAR     MOVE YEAR INTO STRING
         LM    14,12,12(13)    *          *
         SR    15,15           * EPILOGUE *
         MVI   12(13),X'FF'    *          *
         BR    14              RETURN
         EJECT
*   CONSTANTS SECTION
*
CVBAREA  DS    D
RETURNED DS    F
YEAR     DC    CL4'19  '
DAYS     DC    CL12'SUNDAY      '
         DC    CL12'MONDAY      '
         DC    CL12'TUESDAY     '
         DC    CL12'WEDNESDAY   '
         DC    CL12'THURSDAY    '
         DC    CL12'FRIDAY      '
         DC    CL12'SATURDAY    '
MONTHS   DC    CL12'MARCH       '
         DC    CL12'APRIL       '
         DC    CL12'MAY         '
         DC    CL12'JUNE        '
         DC    CL12'JULY        '
         DC    CL12'AUGUST      '
         DC    CL12'SEPTEMBER   '
         DC    CL12'OCTOBER     '
         DC    CL12'NOVEMBER    '
         DC    CL12'DECEMBER    '
         DC    CL12'JANUARY     '
         DC    CL12'FEBRUARY    '
TRTABLE  DC    64X'00'
         DC    X'FF'
         DC    191X'00'
PATTERN  DC    X'40202020'
COMMA    DC    CL2', '
         END
TESTDATE CSECT      TEST DRIVER FOR VARIOUS DATE PROGRAMS
         USING TESTDATE,R15
         SAVE  (14,12),,*
         LR    R14,R13
         ST    R13,SAVE+4
         CNOP  0,4
         BAL   R13,AROUND
         DROP  R15
         USING SAVE,R13
SAVE     DC    18A(0)
AROUND   DS    0H
         ST    R13,8(R14)
         CALL     DATE,(DATE)
         ST    R15,RC              SAVE   RETURN CODE
         NOP   END
         CNOP  0,4
         BAL   R1,SVC35
         DC    AL2(32)        LENGTH
         DC    XL2'8000'      MCS FLAGS
DATE     DC    CL28' '
         DC    XL2'0000'      DESCRIPTOR CODES
         DC    XL2'FFFF'      ROUTING FLAGS
SVC35    DS    0H
         SVC   35             WTO SVC
         SPACE 1
END      EQU   *
         L     R15,RC              RESTORE RETURN CODE
         L     R13,SAVE+4
       RETURN  (14,12),T,RC=(15)
RC       DC    F'2048'
         SPACE
R15 EQU 15
R14 EQU 14
R13 EQU 13
R1  EQU 1
R0  EQU 0
         END   TESTDATE
