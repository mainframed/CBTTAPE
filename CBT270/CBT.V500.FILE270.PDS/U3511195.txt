000010 ID DIVISION.                                                     08/15/75
000020 PROGRAM-ID.    U3511195.                                         19511U35
000030 AUTHOR.        HOWARD H. GLASTETTER.                                LV005
000040 INSTALLATION.  WASHINGTON STATE LIQUOR CONTROL BOARD.
000050 DATE-WRITTEN.  JULY 2, 1975
000060 DATE-COMPILED.
000070 REMARKS.        THIS PROGRAM CREATES A TABLE OF UNRESOLVED DSN
000080                 RECORDS.  IT THEN USES DSN RECORDS TO RESOLVE
000090                 THE TABLE.  THE TABLE IN TURN IS USED TO RESOLVE
000100                 BACK-REFERENCES IN ANY OF THE FOLLOWING DSN
000110                 RECORDS THAT REQUIRE RESOLVING.
000120 ENVIRONMENT DIVISION.
000130 INPUT-OUTPUT SECTION.
000140 FILE-CONTROL.
000150     SELECT      RESOURCE-FILE-IN    ASSIGN  UT-S-FILEIN1.
000160     SELECT      UNRESOLVED-FILE-IN  ASSIGN  UT-S-FILEIN2.
000170     SELECT      RESOURCE-FILE-OUT   ASSIGN  UT-S-FILEOUT.
000180 DATA DIVISION.
000190 FILE SECTION.
000200 FD  RESOURCE-FILE-IN
000210     RECORD CONTAINS 80 CHARACTERS
000220     BLOCK CONTAINS 0 RECORDS
000230     LABEL RECORDS ARE STANDARD
000240     RECORDING MODE IS F
000250     DATA RECORD IS RESOURCE-RECORD.
000260 01  RESOURCE-RECORD.
000270     05  R-TYPE                  PIC X.
000280         88  DSN-RECORD          VALUE IS 'D'.
000290     05  R-DDNAME                PIC X(8).
000300     05  R-DSN                   PIC X(44).
000310     05  R-DSN-BREAKDOWN REDEFINES R-DSN.
000320         10  R-REF-INDICATOR     PIC X.
000330         10  FILLER              PIC X(39).
000340     05  R-STEPNAME                  PIC X(8).
000350     05  R-PROGNAME                  PIC X(8).
000360     05  R-PROCNAME                  PIC X(8).
000370     05  FILLER                      PIC X(3).
000380 FD  UNRESOLVED-FILE-IN
000390     RECORD CONTAINS 80 CHARACTERS
000400     BLOCK CONTAINS 0 RECORDS
000410     LABEL RECORDS ARE STANDARD
000420     RECORDING MODE IS F
000430     DATA RECORD IS UNRESOLVED-RECORD.
000440 01  UNRESOLVED-RECORD               PIC X(80).
000450 FD  RESOURCE-FILE-OUT
000460     RECORD CONTAINS 80 CHARACTERS
000470     BLOCK CONTAINS 0 RECORDS
000480     LABEL RECORDS ARE STANDARD
000490     RECORDING MODE IS F
000500     DATA RECORD IS UPDATED-RECORD.
000510 01  UPDATED-RECORD                 PIC X(80).
000520 WORKING-STORAGE SECTION.
000530 77  FILLER      PIC X(28)   VALUE 'WORKING STORAGE STARTS HERE '.
000540 77  TABLE-COUNT             PIC S9(5)   COMP-3   VALUE ZERO.
000550 77  TABLE-RECORDS-RESOLVED  PIC S9(5)   COMP-3   VALUE ZERO.
000560 77  REFERBACK-DSNS-RESOLVED PIC S9(5)   COMP-3   VALUE ZERO.
000570 77  RECS-IN                 PIC S9(5)   COMP-3   VALUE ZERO.
000580 77  RECS-OUT                PIC S9(5)   COMP-3   VALUE ZERO.
000590 77  I                       PIC S9999   COMP SYNC VALUE +1.
000600 01  BACK-REF-TABLE.
000610     05  TABLE-ELEMENT   OCCURS  200.
000620         10  FILLER          PIC X.
000630         10  TB-DDNAME       PIC X(8).
000640         10  TB-REF-DSN      PIC X(36).
000650         10  TB-PROC         PIC X(8).
000660         10  TB-STEPNAME     PIC X(8).
000670         10  TB-REF-STEPNAME PIC X(8).
000680         10  TB-REF-DDNAME   PIC X(8).
000690         10  FILLER          PIC XXX.
000700     SKIP2
000710 01  PROCESS-SWITCHES.
000720     05  BACK-REF-END        PIC X.
000730     88  GOOD-BACK-REF       VALUE IS '0'.
000740     88  TABLE-IS-BUILT      VALUE IS '1'.
000750     05  RESOURCE-END        PIC X.
000760     88  GOOD-RESOURCE       VALUE IS '0'.
000770     88  ALL-RECORDS-PROCESSED   VALUE IS '1'.
000780     05  RESOLVED-SWITCH     PIC X.
000790     88  DSN-RESOLVED        VALUE IS '1'.
000800     EJECT
000810 PROCEDURE DIVISION.
000820 LOGIC-CONTROL SECTION.
000830     PERFORM HOUSEKEEPING-10.
000840     PERFORM BUILD-TABLE-20 UNTIL TABLE-IS-BUILT.
000850     PERFORM RESOLVE-TBL-AND-DSNS-30 UNTIL ALL-RECORDS-PROCESSED.
000860     PERFORM END-JOB-60.
000870     GOBACK.
000880 END-OF-LOGIC-CONTROL-SECTION.
000890     SKIP3
000900 HOUSEKEEPING-10.
000910     OPEN INPUT RESOURCE-FILE-IN, UNRESOLVED-FILE-IN
000920         OUTPUT RESOURCE-FILE-OUT.
000930         MOVE SPACES TO BACK-REF-TABLE.
000940         MOVE '000' TO PROCESS-SWITCHES.
000950     SKIP2
000960* A.1
000970     SKIP2
000980 BUILD-TABLE-20.
000990     READ UNRESOLVED-FILE-IN
001000         AT END MOVE '1' TO BACK-REF-END.
001010     IF GOOD-BACK-REF
001020         ADD 1 TO TABLE-COUNT
001030         MOVE UNRESOLVED-RECORD TO TABLE-ELEMENT (TABLE-COUNT).
001040*    END-IF
001050 RESOLVE-TBL-AND-DSNS-30.
001060     READ RESOURCE-FILE-IN
001070         AT END MOVE '1' TO RESOURCE-END.
001080     IF GOOD-RESOURCE
001090         ADD 1 TO RECS-IN
001100* A.2
001110         IF DSN-RECORD AND R-REF-INDICATOR NOT = '*'
001120             THEN
001130                 PERFORM RESOLVE-TABLE-40
001140                 VARYING I FROM 1 BY 1 UNTIL I > TABLE-COUNT
001150* A.3
001160             ELSE IF DSN-RECORD AND R-REF-INDICATOR = '*'
001170                    THEN PERFORM RESOLVE-DSN-50
001180                         VARYING I FROM 1 BY 1 UNTIL DSN-RESOLVED
001190                         OR I > TABLE-COUNT
001200                         MOVE 0 TO RESOLVED-SWITCH.
001210*    END IF
001220     SKIP2
001230* A.4
001240     SKIP2
001250     IF GOOD-RESOURCE
001260         WRITE UPDATED-RECORD FROM RESOURCE-RECORD
001270         ADD 1 TO RECS-OUT.
001280     SKIP2
001290* A.2 EXTENDED LOGIC
001300     SKIP2
001310 RESOLVE-TABLE-40.
001320     IF R-DDNAME = TB-REF-DDNAME (I)
001330     AND R-STEPNAME = TB-REF-STEPNAME (I)
001340     AND R-PROCNAME = TB-PROC (I)
001350         THEN
001360             MOVE R-DSN TO TB-REF-DSN (I)
001370             ADD 1 TO TABLE-RECORDS-RESOLVED.
001380     SKIP2
001390* A.3 EXTENDED LOGIC
001400     SKIP2
001410 RESOLVE-DSN-50.
001420     IF TB-DDNAME (I) = R-DDNAME
001430     AND TB-STEPNAME (I) = R-STEPNAME
001440     AND TB-PROC (I) = R-PROCNAME
001450         MOVE TB-REF-DSN (I) TO R-DSN
001460         MOVE '1' TO RESOLVED-SWITCH
001470         ADD 1 TO REFERBACK-DSNS-RESOLVED.
001480     SKIP2
001490* A.5
001500     SKIP2
001510 END-JOB-60.
001520     DISPLAY 'U3511195 -- RESOLVE BACKREF PROGRAM CONTROL TOTALS'.
001530     DISPLAY RECS-IN ' RECORDS IN, ' RECS-OUT ' RECORDS OUT.'
001540     DISPLAY TABLE-COUNT ' REFERBACK RECORDS, '
001550             TABLE-RECORDS-RESOLVED ' RESOLVED IN THE TABLE, '
001560             REFERBACK-DSNS-RESOLVED ' RESOLVED BY THE TABLE.'
001570     DISPLAY 'THE ABOVE LINES INDICATE ERR IF TOTALS UNEQUAL.'.
001580     CLOSE RESOURCE-FILE-IN
001590           UNRESOLVED-FILE-IN
001600           RESOURCE-FILE-OUT.
