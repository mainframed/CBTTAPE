*          DATA SET CBT931     AT LEVEL 001 AS OF 02/25/79
* PACKLIST SOURCE DSN=WDPSC.DN00150.FILE(PACKLIST)                      00001
*                                                                       00002
* PACKLIST        VERSION 1.0         WDPSC                             00003
*        NOT WRITTEN AT STATE OF WASHINGTON, BUT SUBSTANTIALLY          00004
*        ALTERED ON INPUT SIDE TO USE OUTPUT FROM TRACE TABLE SAMPLE    00005
*        ROUTINE.                                                       00006
* PACKLIST                                                              00007
*        IS THE MODULE THAT BUILDS THE IEAPAK00 MEMBER OF               00008
*        SYS1.PARMLIB. THE DATA IS OBTAINED BY USING THE ROUTINE        00009
*        PSWSAMP (SEE NEXT ENTRY).   THE CONSTRUCTION IS BASED ON       00010
*        FREQUENCY OF INTERUPTS IN A GIVEN LPA MODULE. ASSOCIATION      00011
*        BY FREQUENCY IS POSSIBLE, BUT IS NOT ENFORCED.                 00012
*                                                                       00013
*        THE INTERUPTS IN AND TO LPA ARE COUNTED. THEN THE COUNTS FOR   00014
*        EACH MODULE ARE DIVIDED BY THE LENGTH OF THAT MODULE. THIS     00015
*        INDEX VALUE IS SORTED INTO DESCENDING ORDER. THE PACK LIST     00016
*        IS THEN BUILT, TAKING PAGE BOUNDARIES INTO ACCOUNT. WHEN       00017
*        THIS IS FINISHED, THE INDIVIDUAL RECORDS ARE REVERSED IN       00018
*        ORDER TO PLACE THE MOST ACTIVE CLOSEST TO THE LPA PAGE         00019
*        DIRECTORY ON THE LPA PAGING DATA SET.                          00020
*                                                                       00021
*        ALL MEMBERS OF LPALIB SHOULD BE INCLUDED IN THE PACKLIST AT    00022
*        THE FRONT OF IEAPAK00. IF A MEMBER IS NOT INCLUDED IN THE      00023
*        PACK LIST, IT GOES TO THE SPOT DESIRED FOR THE MOST ACTIVE     00024
*        LPA MEMBERS.                                                   00025
*                                                                       00026
*  INVOKING JCL:                                                        00027
* //PACKLIST EXEC PGM=PACKLIST                                          00028
* //SYSPRINT DD  SYSOUT=A             REPORT ON ACTIVITY                00029
* //SYSLPA   DD  DSN= DATA SET CONTAINING OUTPUT OF LISTLPA OF AMBLIST  00030
* //SYSWORK  DD  TEMPORARY  WORK DATA SET                               00031
* //SYSPSW   DD  DSN= DATA SET CONTAINING SAMPLES GATHERED BY PSWSAMP   00032
* //SYSTEMP  DD  WORK TAPE, MUST BE TAPE BECAUSE IT IS READ BACKWARDS   00033
* //SYSPUNCH DD  DSN= OUTPUT DATA SET FOR MODEL IEAPAK00                00034
*                                                                       00035
* HERE IS A JOBSTREAM THAT WORKED, GET RID OF THE '* ' TO RUN IT        00036
* AFTER CHECKING ON THE EXISTANCE OF THE DATASETS CODED.                00037
*                                                                       00038
* //PAK00150 JOB (2100,56-D),'LLUEB 0679 T',CLASS=A,NOTIFY=LL00150      00039
* //*---------------------------------------------------------------    00040
* /*SETUP        PAK00150,2100-56-D,A200K,02                            00041
* /*MESSAGE  LINDLUEBCK,3-0679,00.30,TD,WDPSC                           00042
* //*---------------------------------------------------------------    00043
* //PACKLIST EXEC PGM=PACKLIST                                          00044
* //STEPLIB  DD DSN=WDPSC.SOFTLOAD,DISP=SHR                             00045
* //SYSPRINT DD SYSOUT=A                                                00046
* //SYSLPA   DD DSN=BRAD761.LPA,DISP=SHR                                00047
* //SYSWORK  DD DSN=&&WORK,UNIT=SYSDA,SPACE=(CYL,1),DISP=(NEW,DELETE)   00048
* //SYSPSW   DD DSN=PSWS,UNIT=TAPE16,DISP=OLD,                          00049
* //            VOL=SER=015040                                          00050
* /*MESSAGE  PACKLIST,PSWS             -015040-T-I-3-LIBRARY            00051
* //*----------------------------------------------------------------   00052
* //         DD DSN=PSWS,UNIT=AFF=SYSPSW,DISP=OLD,                      00053
* //            VOL=SER=017019                                          00054
* /*MESSAGE  PACKLIST,PSWS             -017019-T-I-3-LIBRARY            00055
* //*----------------------------------------------------------------   00056
* //SYSTEMP  DD DSN=PACKLIST.WORKTAPE,UNIT=TAPE16,DISP=(NEW,KEEP)       00057
* /*MESSAGE  PACKLIST,PACKLIST.WORKTAPE-      -T-O-3-LIBRARY            00058
* //*----------------------------------------------------------------   00059
* //SYSPUNCH DD DSN=LL00150.PACKLIST(IEAPAK00),DISP=SHR                 00060
         SPACE 5                                                        00061
         LCLA  &PL                                                      00062
TBS      DSECT                     TABLE ENTRY FOR LPA MODULE           00063
TN       DS    CL8                 NAME                                 00064
TS       DS    CL4                 START ADDRESS                        00065
TL       DS    CL4                 LENGTH                               00066
TF       DS    CL4                 NUMBER OF FAULTS                     00067
TFL      DS    F                   FAULTS DIVIDED BY LENGTH             00068
TBL      EQU   *-TBS               LENGTH OF TABLE ENTRY                00069
LPAE     DSECT                     LPA LIST PRINT SEGMENT               00070
LN       DS    CL8                 MODULE NAME                          00071
         DS    C                                                        00072
LX       DS    0CL17               TRANSLATE NEXT 17 BYTES              00073
LS       DS    CL6                 START ADDRESS                        00074
         DS    CL4                                                      00075
LL       DS    CL6                 LENGTH                               00076
         DS    C                                                        00077
         REGISTER                                                       00078
PACKLIST START 0                                                        00079
         SAVE  (14,12)            SAVE ALL REGS                         00080
         BAL   R12,80(,R15)        BRANCH AROUND SAVE                   00081
         USING *,R13               WILL ALSO BE BASE                    00082
         DS    18F                 SAVE AREA                            00083
         ST    R12,8(,R13)         FORWARD CHAIN                        00084
         ST    R13,4(,R12)         BACK CHAIN                           00085
         LR    R13,R12             NOW POINT TO OUR SAVE AREA           00086
         SPACE 3                                                        00087
         OPEN  (SYSLPA,(INPUT))    INPUT IS AMBLIST OUTPUT              00088
         TM    SYSLPA+48,X'10'     OPEN  SUCCESSFUL?                    00089
         LA    R4,101                                                   00090
         BZ    ABEND               NO  USER ABEND 101                   00091
IT050    GET   SYSLPA,PI           SEARCH FOR NUMERIC SECTION... GET    00092
         CLI   CC,C'1'          Q. TITLE PAGE                           00093
         BNE   IT050               NO... GET NEXT PRINT IMAGE           00094
         CLI   CC+31,C'N'       Q. N FOR START OF NUMERICAL LISTING     00095
         BNE   IT050               NO... MUST STILL BE IN ALPHABETIC    00096
         USING TBS,R9              TABLE DSECT                          00097
         GETMAIN EU,LV=96000,A=TAB GET THE  TABLE WORK AREA             00098
         L     R9,TAB              POINT TO IT                          00099
IT100    GET   SYSLPA,PI           READ PRINT IMAGE                     00100
         CLI   CC,X'40'         Q. DETAIL LINE                          00101
         BNE   IT100               NO... IGNORE                         00102
         LA    R1,S1               PIINT TO FIRST SEGMENT               00103
         BAL   14,IT200            ADD TO TABLE                         00104
         LA    R1,S2               POINT TO SECOND SEGMENT              00105
         BAL   R14,IT200              CONVERT                           00106
         B     IT100               GET NEXT RECORD                      00107
         SPACE                                                          00108
         USING LPAE,R1             SEGMENT DSECT                        00109
IT200    CLI   LL,X'40'         Q. BLANK LENGTH (NOT MODULE NAME)       00110
         BCR   8,R14              YES... IGNORE                         00111
         XC    TF,TF               CLEAR COUNT COUNTER                  00112
         MVC   TN,LN               SAVE MODULE NAME                     00113
         PACK  LPAT,LL(6)                                               00114
         AP    LPATOTAL,LPAT                                            00115
         TR    LX,TOBIN            CONVERT START AND LENGTH TO BIN      00116
         PACK  TS+1,LS(7)         SAVE START ADDRESS                    00117
         PACK  TL+1,LL(7)              LENGTH                           00118
ONETIME  NOP   ITEND                    ONE TIME SWITCH                 00119
         OI    ONETIME+1,X'F0'          CHANGE TO BRANCH                00120
         MVC   LPALOW,TS                SAVE LPA LOW                    00121
         MVI   LPALOW,0                                                 00122
ITEND    EQU   *                                                        00123
         LA    R9,TBL(,R9)         POINT TO NEXT TABLE ENTRY            00124
         BR    R14                 RETURN                               00125
         SPACE 3                                                        00126
IT900    S     R9,TAB+4            SET END OF TABLE                     00127
         ST    R9,TAB+8            EOD(SYSLPA)... SAVE TABLE END        00128
         L     R1,TS               LAST MOD START ADDR                  00129
         A     R1,TL               ADD LENGTH OF MOD                    00130
         LA    R1,0(,R1)                                                00131
         ST    R1,LPAHI            SAVE LPA HIGH ADDR                   00132
         CLOSE SYSLPA             DON'T NEED THIS ANYMORE               00133
         SPACE                                                          00134
         L     R9,16              CVT ADDR                              00135
         L     R9,356(,R9)        PVT ADDR                              00136
         LH    R8,26(R9)          CSA LOW -HIGH 16 BITS                 00137
         SLL   R8,8              ADD LOW 8 BITS                         00138
         LA    R8,0(,R8)          ZERO HIGH BYTE                        00139
         ST    R8,SPALOW          SAVE                                  00140
         EJECT                                                          00141
         OPEN  (SYSPSW,(INPUT))   PROCESS  COUNT DATA                   00142
         LA    R4,102                                                   00143
         TM    SYSPSW+48,X'10'    OPEN SUCCESSFUL?                      00144
         BZ    ABEND                                                    00145
R100      GET   SYSPSW                                                  00146
         LR    R5,R1                                                    00147
         LA    R6,32                                                    00148
         SLR   R5,R6                                                    00149
          LH    R7,SYSPSW+62                                            00150
         AR    R7,R5                                                    00151
R120     BXH   R5,R6,R100                                               00152
          TM    2(R5),X'70'                                             00153
          BZ    R120                                                    00154
         L     R12,4(,R5)          GET ADDR                             00155
         LA    R12,0(,R12)                                              00156
         C     R12,SPALOW       Q. IN ADDRESS SPACE AREA                00157
         BNL   R150                NO...                                00158
         AP    ASCTR,=P'1'         BUMP COUNTER                         00159
         B     R120                READ AGAIN                           00160
R150     C     R12,LPALOW       Q. IN CSA                               00161
         BNL   R200                NO...                                00162
R160     AP    CSACTR,=P'1'       BUMP CSA COUNTER                      00163
         B     R120                GET ANOTHER TRACE RECORD             00164
R200     C     R12,LPAHI        Q. ABOVE LPA                            00165
         BNL   R160                YES... COUNT AS CSA                  00166
         AP    LPACTR,=P'1'       BUMP LPA COUNTER                      00167
         LM    R9,R11,TAB          GET TABLE LIMITS                     00168
         SR    R9,R10                                                   00169
R250     BXH   R9,R10,R160                                              00170
         C     R12,TS           Q. EXCEED MODULE START                  00171
         BNL   R250                                                     00172
         SR    R9,R10                                                   00173
         L     R12,TF                                                   00174
         LA    R12,1(,R12)    BUMP COUNT COUNT BY ONE                   00175
         ST    R12,TF              STORE RESULT                         00176
         B     R120                GET NEXT RECORD                      00177
R900      EQU   *                                                       00178
         SPACE                                                          00179
         CLOSE SYSPSW                                                   00180
         EJECT                                                          00181
          LM    R9,R11,TAB    GET TABLE LIMITS                          00182
         SR    R9,R10                                                   00183
R910     BXH   R9,R10,R920         POINT TO NEXT ENTRY                  00184
         L     R7,TF              GETY NUMBER OF FAULTS                 00185
         M     R6,=F'100000'         ALLOW 5 DECIMAL PLACES             00186
         D     R6,TL               CALC FAULTS / LENGTH                 00187
         ST    R7,TFL              SAVE ANSWER                          00188
         B     R910                DO FOR NEXT ENTRY                    00189
         SPACE                                                          00190
*  ORDER TABLE BY DESCENDING FAULTS/BYTE (TFL)                          00191
R920     L     R8,TAB+8      SAVE ACTUAL END OF TABLE                   00192
R930     LM    R9,R11,TAB    SET UP TO BEGIN THE SORT                   00193
         MVI   SORTTEST,0    HAS-SOMETHING-BEEN-MOVED BYTE              00194
R940     LR    R5,R9                                                    00195
         BXH   R9,R10,R960   INCREMENT FOR NEXT ENTRY                   00196
         CLC   TFL,20(R5)    COMPARE ON FAULTS PER BYTE                 00197
         BH    R950          EXCHANGE THE ENTRIES                       00198
         BNE   R940          LOW                                        00199
         CLC   TL,12(R5)     EQUAL, COMPARE ON LENGTH                   00200
         BNH   R940                                                     00201
R950     TS    SORTTEST      SET THE HAS-SOMETHING-BEEN-MOVED BYTE      00202
         XC    0(TBL,R9),0(R5) EXCHANGE THE ENTRIES                     00203
         XC    0(TBL,R5),0(R9) EXCHANGE THE ENTRIES                     00204
         XC    0(TBL,R9),0(R5) EXCHANGE THE ENTRIES                     00205
         B     R940                                                     00206
R960     SR    R11,R10       SHRINK THE TABLE FOR A TIME                00207
         ST    R11,TAB+8                                                00208
         TS    SORTTEST                                                 00209
         BNZ   R930                                                     00210
         ST    R8,TAB+8      RESET THE END OF THE TABLE                 00211
         EJECT                                                          00212
         ED    TASCCTR,ASCTR     SHOW ADDRESS SPACE FAULTS              00213
         ED    TCSACTR,CSACTR                                           00214
         ED    TLPACTR,LPACTR      LPA                                  00215
         SPACE 2                                                        00216
         OPEN (SYSPRINT,(OUTPUT),SYSWORK,(OUTPUT))                      00217
         LA    R4,103                                                   00218
         TM    SYSPRINT+48,X'10'    OPEN OK?                            00219
         BZ    ABEND                NO   USER ABEND 103                 00220
         LA    R4,104                                                   00221
         TM    SYSWORK+48,X'10'   OPEN OK?                              00222
         BZ    ABEND                NO   USER ABEND 104                 00223
         PUT   SYSPRINT,T           PRINT TITLE                         00224
         ED    LPASIZE,LPATOTAL                                         00225
         PUT   SYSPRINT,ST2                                             00226
         PUT   SYSPRINT,ST               SUBTITLE                       00227
         MVI   ST,C'1'                                                  00228
         EJECT                                                          00229
         LA    R7,56               LINES PER PAGE                       00230
         LA    R6,CS+1             CARD IMAGE POINTER                   00231
         LM    R9,R11,TAB        RESET TABLE LIMITS                     00232
         SR    R9,R10                                                   00233
         ST    R9,SAVE9                                                 00234
P100     L     R9,SAVE9                                                 00235
         MVI   FITNOFIT,0                                               00236
P150     BXH   R9,R10,P900                                              00237
         CLI   TF,X'FF'       Q. USED ENTRY                             00238
         BE    P150              YES                                    00239
* WHEN THIS THING IS WORKING PRINT IT HERE BY B P300                    00240
         ST    R9,SAVE9            SAVE CURR LOC                        00241
          L     R1,TL                                                   00242
          N     R1,=F'4095'                                             00243
          A     R1,PAGELEN                                              00244
         C     R1,=F'4096'         OVER A PAGE ?                        00245
         BNH   NOTOVER                                                  00246
*  TRY TO FIND ENTRY WHICH WILL FIT                                     00247
         SR   R9,R10                                                    00248
         ST   R9,SAVE9                                                  00249
          MVI   FITNOFIT,255                                            00250
P225     BXH   R9,R10,P230         TRY TO FIND A FIT                    00251
         CLI   TF,X'FF'         Q. ALREADY USED FLAG                    00252
         BE    P225     YES.... DO NOT USE                              00253
         L     R1,PAGELEN          CURRENT PAGELEN                      00254
         A     R1,TL               LEN OF MOD                           00255
         C     R1,=F'4096'       Q.FIT                                  00256
         BH    P225                                                     00257
         B     NOTOVER                                                  00258
* AGAIN, WHEN WORKING, NOPRINT JUST PUNCH AND USED                      00259
P230     EQU   *                                                        00260
         SR    R9,R10                                                   00261
         S     R1,TL                                                    00262
         ST    R1,PAGELEN                                               00263
P231     MVI   FITNOFIT,0                                               00264
         BCTR  R6,0                MOVE BACK TO COMMA                   00265
         MVI   0(R6),C')'          MOVE IN CLOSE PAREN                  00266
         CLI   FIRSTPG,X'FF'      Q. FIRST PAGE GROUP                   00267
         BE    P232     YES.... SKIP COMMA                              00268
         MVI   1(R6),C','          MOVE IN COMMA                        00269
P232     PUT   SYSWORK,CS         PUNCH CARD                            00270
         MVI   FIRSTPG,X'00'       SW TO FALSE                          00271
         MVI   CS,X'40'            TO CLEAR                             00272
         MVC   CS+1(79),CS         CLEAR                                00273
         MVI   CS,C'('             OPEN PAREN - NEXT CARD               00274
         LA    R6,CS+1             RESET CARD POINTER                   00275
         PUT   SYSPRINT,PGB                                             00276
         BCTR  R7,0                                                     00277
P235     L     R1,PAGELEN                                               00278
         N     R1,=F'4095'                                              00279
         ST    R1,PAGELEN                                               00280
         B     P100                                                     00281
NOTOVER  ST    R1,PAGELEN          CURRENT PAGE LEN                     00282
         MVC   0(8,R6),TN          MOVE MODULE NAME TO PUNCH            00283
P250     LA    R6,1(,R6)           NEXT CHAR (KNOW FIRST WASN'T BLANK   00284
         CLI   0(R6),X'40'      Q. BLANK                                00285
         BNE   P250                NO... CONTINUE SEARCH                00286
         MVI   0(R6),C','          YES... COMMA AFTER NAME              00287
         LA    R6,1(,R6)           POINT PAST COMMA                     00288
         C     R6,CE            Q. CARD FILLED                          00289
         BL    P300                NO... GO PRINT                       00290
         PUT   SYSWORK,CS         PUNCH CARD IMAGE                      00291
         LA    R6,CS+1             RESET CARD POINTER                   00292
         MVI   CS,X'40'            INSURE LEADING PAREN GONE            00293
         MVC   CS+1(79),CS         BLANK REST OF CARD IMAGE             00294
P300     EQU   *                                                        00295
         MVC   PN,TN               PRINT MODULE NAME                    00296
         UNPK  PS(7),TS+1                START ADDRESS                  00297
         MVI    TF,X'FF'                                                00298
         UNPK  PL(7),TL+1                LENGTH                         00299
         TR    PT,TOHEX-240                                             00300
         MVI   PS+6,X'40'          BLANK GARBAGE                        00301
         MVI   PL+6,X'40'          BLANK GARBAGE                        00302
         MVC   PF(18),=X'40202020206B2020214020214B2020202020'          00303
         L     R8,TF               GET NUMBER FAULTS                    00304
         LA    R8,0(,R8)                                                00305
         CVD   R8,DWD                              IN DECIMAL           00306
         ED    PF,DWD+4                            READABLE             00307
         L     R8,TFL              GET FACTOR                           00308
         CVD   R8,DWD                         IN DECIMAL                00309
         ED    PFL,DWD+4                      READABLE                  00310
         BCTR  R7,0                                                     00311
         LTR   R7,R7                                                    00312
         BP    P400                                                     00313
         LA    R7,56                                                    00314
         PUT   SYSPRINT,ST               SUBTITLE                       00315
P400     EQU   *                                                        00316
         PUT   SYSPRINT,P          PRINT DETAIL LINE                    00317
         L     R1,PAGELEN                                               00318
         N     R1,=F'4095'                                              00319
         BZ    P231                                                     00320
         ST    R1,PAGELEN                                               00321
P480     CLI   FITNOFIT,255                                             00322
         BE    P225                                                     00323
         B     P100                LOOK FOR NEXT ENTRY                  00324
         EJECT                                                          00325
P900     BCTR  R6,0                PUNCH LAST CARD BACK UP POINTER      00326
         MVI   0(R6),C')'          MOVE IN TERMINATING PAREN            00327
         MVI   1(R6),C','          WANT COMMA ON LAST CARD              00328
         PUT   SYSWORK,CS                                               00329
         CLOSE SYSPRINT                                                 00330
         CLOSE SYSWORK                                                  00331
         EJECT                                                          00332
         OPEN  (SYSTEMP,(RDBACK),SYSPUNCH,(OUTPUT))                     00333
         LA    R4,105                                                   00334
         TM    SYSTEMP+48,X'10'     SUCCESSFUL OPEN                     00335
         BZ    ABEND                                                    00336
         LA    R4,106                                                   00337
         TM    SYSPUNCH+48,X'10'                                        00338
         BZ    ABEND                                                    00339
S100     L     R9,TAB              USE TABLE TO STORE CARDS             00340
S110     GET   SYSTEMP,(R9)       READ PAGE GROUP BACKWARDS             00341
         CLI   0(R9),C'('        Q. FIRST CARD IN GROUP                 00342
         BE    S150                YES...GO PUNCH                       00343
         CLI   0(R9),C' '        Q. INTERMEDIATE CARD                   00344
         BE    S130                YES...SAVE IT                        00345
         CLI   0(R9),C')'        Q. LAST CARD IN PAGE GROUP             00346
         BNE   ZERO                NO...CONFUSED--BLOWUP                00347
         MVI   CPARSW,X'FF'        SET CLOSE PAREN SW TO TRUE           00348
         B     S110                BYPASS CARD IN ERROR                 00349
*  ERROR IN CARD IS IT IS A CARD WITH ONLY A OPEN PAREN AND COMMA       00350
S130     LA    R6,54(R9)           EARLIEST BLANK POS                   00351
         LA    R9,80(R9)           LOC NEXT CARD                        00352
         TM    CPARSW,X'FF'      Q. NEED CLOSE PAREN                    00353
         BNO   S110                NO...BYPASS                          00354
         MVI   CPARSW,X'00'        SET CLOSE PAREN SW TO FALSE          00355
S135     CLI   0(R6),X'40'       Q. BLANK                               00356
         BNE   S140                NO...TRY NEXT POS                    00357
         BCTR  R6,0                BACK ONE                             00358
         MVI   0(R6),C')'          CLOSE PAREN                          00359
         MVI   1(R6),C','          COMMA                                00360
         B     S110                GET NEXT CARD                        00361
S140     LA    R6,1(,R6)          NEXT CARD POS                         00362
         CR    R6,R9             Q. AT NEXT CARD                        00363
         BL    S135                NO...CONTINUE SEARCH                 00364
MESSUP   DC    H'0'                ERROR IN SEARCHING CARD              00365
S150     TM    CPARSW,X'FF'       Q. STILL NEED CLOSE PAR               00366
         BNO   S154                 NO...DO NOT SET UP                  00367
         LA    R6,53(R9)            ADDR OF ONLY CARD IN GROUP          00368
S152     LA    R6,1(R6)             EARLIEST BLANK POS                  00369
         CLI   0(R6),X'40'        Q. BLANK                              00370
         BNE   S152                 NO...TRY NEXT POS                   00371
         BCTR  R6,0                 BACK ONE                            00372
         MVI   0(R6),C')'           CLOSE PAREN                         00373
         MVI   1(R6),C','           COMMA                               00374
         MVI   CPARSW,X'00'         CLOSE PAR SW TO FALSE               00375
         B     S156                 MUST BE ONLY ONE CARD               00376
S154     L     R7,TAB               LAST CARD IN GROUP                  00377
         CR    R7,R9              Q.FIRST AND LAST THE SAME             00378
         BE    S156                YES...NO SWITCH                      00379
         XC    0(80,R7),0(R9)      SWITCH                               00380
         XC    0(80,R9),0(R7)             FIRST AND                     00381
         XC    0(80,R7),0(R9)                       LAST                00382
S156     L     R7,TAB               FIRST CARD                          00383
         LA    R8,80                LEN OF CARD                         00384
S160     PUT   SYSPUNCH,0(R7)       PUNCH CARD                          00385
         BXLE  R7,R8,S160           LOOP THRU PAGE GROUP                00386
         B     S100                 NEXT GROUP                          00387
S900     CLOSE SYSTEMP                                                  00388
         CLOSE SYSPUNCH                                                 00389
         EJECT                                                          00390
         L     R13,4(,R13)                                              00391
         RETURN (14,12),RC=0                                            00392
         SPACE 3                                                        00393
ABEND    EQU *                                                          00394
         ABEND (4),DUMP,STEP,USER                                       00395
P        EQU   *                   PRINT LINE                           00396
         DC    X'40'               SINGLE SPACE                         00397
PN       DS    CL8                 MODULE NAME                          00398
         DC    CL1' '                                                   00399
PT       DS    0CL14               TRANSLATE TO HEX                     00400
PS       DS    CL7                      MODULE START ADDRESS            00401
PL       DS    CL7                      LENGTH OF MPOULE                00402
PE       DS    0CL18               MOVE EDIT MASK HERE                  00403
PF       DS    CL9                 NUMBER OF FAULTS                     00404
PFL      DS    CL9                 FACTOR                               00405
         DC    CL2' '                                                   00406
WLEN     DC    CL20' '                                                  00407
BLANKS   DC    CL20' '                                                  00408
T        DC    C'1OTHER='                                               00409
TASCCTR  DC    X'40206B2020206B202021'                                  00410
         DC    C',CSA='                                                 00411
TCSACTR  DC    X'40206B2020206B202021'                                  00412
         DC    C',LPA='                                                 00413
TLPACTR  DC    X'40206B2020206B202021'                                  00414
         DC    C'.                        '                             00415
LPATOTAL DC    PL5'0'                                                   00416
ST2      DC    C'0        LPASIZE IS '                                  00417
LPASIZE  DC    X'402020206B2020206B202021'                              00418
         DC    C'   BYTES                                       '       00419
         DC    23C' '                                                   00420
ST       DC    C'0    NAME  START LENGTH     COUNTS      C/L'           00421
         DC    CL23' '                                                  00422
PGB      DC    32C' ',8C'*',30C' '                                      00423
PI       EQU   *                   PRINT IMAGE FROM SYSLPA HERE         00424
CC       DS    C                   PRINTER CARRAIGE CONTROL CHARACTER   00425
S1       DS    CL60                SEGMENT ONE                          00426
S2       DS    CL60                SEGMENT TWO                          00427
CS       DC    C'('                                                     00428
CEN      DC    79X'40'                                                  00429
CE       DC    A(CS+54)                                                 00430
PAGELEN  DC    F'0'                                                     00431
TOHEX    DC    C'0123456789ABCDEF'                                      00432
TOBIN    DC    256X'00'                                                 00433
         ORG   TOBIN+X'C1'         POSITION TO 'A'                      00434
         DC    X'0A0B0C0D0E0F'     TRANSLATE A-F                        00435
         ORG   TOBIN+X'F0'         POSITION AT '0'                      00436
         DC    X'00010203040506070809'                                  00437
         ORG                                                            00438
         DS    0F                                                       00439
DWD      DS    D                                                        00440
TAB      DC    A(0,TBL,0)                                               00441
         DS    0F                                                       00442
ZERO     DC    F'0'                                                     00443
SAVE9    DC    F'0'                                                     00444
SPALOW   DC    X'00000000'                                              00445
LPALOW   DC    X'7FFFFFFF'                                              00446
LPAHI    DC    X'00000000'                                              00447
HIVALUE  DC    X'7FFFFFFF'                                              00448
LPAT     DC  PL3'0'                                                     00449
FITNOFIT DC    X'00'                                                    00450
SORTTEST DC    H'0'                                                     00451
ASCTR    DC    PL4'0'                                                   00452
CSACTR   DC    PL4'0'                                                   00453
LPACTR   DC    PL4'0'                                                   00454
CPARSW   DC    X'00'                                                    00455
FIRSTPG  DC    X'FF'     FIRST PAGE GROUP SW=TRUE                       00456
         LTORG                                                          00457
         PRINT NOGEN                                                    00458
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,RECFM=FBA,LRECL=60,   X00459
               BLKSIZE=60                                               00460
SYSLPA   DCB   DDNAME=SYSLPA,DSORG=PS,MACRF=GM,RECFM=FBA,EODAD=IT900    00461
SYSWORK  DCB   DDNAME=SYSTEMP,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=80,     X00462
               BLKSIZE=3040                                             00463
SYSPSW DCB DDNAME=SYSPSW,DSORG=PS,MACRF=GL,RECFM=FB,EODAD=R900          00464
SYSTEMP  DCB   DDNAME=SYSTEMP,DSORG=PS,MACRF=GM,EODAD=S900              00465
SYSPUNCH DCB   DDNAME=SYSPUNCH,DSORG=PS,MACRF=PM,RECFM=FB,LRECL=80,    X00466
               BLKSIZE=3040                                             00467
         END                                                            00468
