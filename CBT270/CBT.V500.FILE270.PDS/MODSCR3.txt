*          DATA SET 761KKMSCR3 AT LEVEL 001 AS OF 04/02/81
         TITLE 'MODSCR3 - FULLSCR3 INTERFACE TO HANDLE MOD BLOCKS.'
*
*      PROGRAM MODSCR3 IS DESIGNED TO MAKE FULLSCREEN APPLICATIONS
*  MORE EFFICIENT AND EASIER TO DEVELOP.
*
*   AUTHOR: KERMIT KISER - WASHINGTON STATE DP SERVICE CENTER (WDPSC).
*          (206) 902-3119   ***  MODSCR3 VERSION 1.0 - OCTOBER 1980 ***
*
*  CALLING SEQUENCE :
*
*    CALL 'MODSCR3' USING (NORMAL FULLSCR3 PARMS), MOD1, MOD2,...MODN.
*
*     01 MOD1.
*       02 FLAG1         PIC X  VAL=O MEANS R1 CONTAINS OFFSET ADDRESS
*                               VAL=R MEANS R1 CONTAINS ROW OF ADDRESS
*       02 FLAG2         PIC X  VAL=A MEANS MESSAGE BEGINS WITH ATTRIB
*                               VAL=B MEANS ATTRIB AND MOVE CURSOR HERE
*                               VAL=C MEANS MOVE CURSOR TO THIS FIELD
*                               VAL=  MEANS NO ATTR AND NO CURSOR
*       02 R1            PIC S9999 COMP VAL=ROW OR OFFSET  ADDRESS
*       02 R2            PIC S9999 COMP VAL=COL OR ZERO OF ADDRESS
*       02 LEN1          PIC S9999 COMP VAL=LENGTH OF MESSAGE
*       02 MESS1         PIC X(LEN) DATA TO SEND TO 3270
*
*
*********************************************************************
*    THIS PROGRAM IS DEPENDENT ON TCAM BEING ASSEMBLED WITH THE     *
*    'SPFSCRN' MACROS OR EQUIVALENT.                                *
*********************************************************************
         EJECT
MODSCR3  ENTERR LEVEL=V1M0        R13 -> WORKAREA
*                                 R12 -> ADDRESSABILITY
         LR    R11,R1             R11->PARM LIST
         ST    R11,FULLPARM
         L     R10,0(,R11)        R10->IOPARM
         SLR   R15,R15
         EJECT
*********************************************************************
*        MAIN  PROCESSING CONTROL ROUTINE:                          *
*********************************************************************
         SPACE 2
*
*        COUNT THE INPUT PARMAMETERS GIVEN TO US:
*
         BAL   R14,PARMCHK              LOOK AT PARMS
         LTR   R15,R15            RC = 0?  OK?
         BNZ   GOUT               NO. -> GO TO FAST EXIT
         SPACE 2
*
*              BUILD OUTBUFF FROM MODBLOCKS PROVIDED:
*
         TM    MF1,X'C0'                V3 TYPE WITH MODBLOCKS?
         BNO   CFS3                     NO->IGNORE
         BAL   R14,BLDBUFF              BUILD NEW OUTBUFF
         LTR   R15,R15            RC = 0?  OK?
         BNZ   GOUT               NO. -> GO TO FAST EXIT
         SPACE 2
*
*              BUILD NEW FULLSCR3 PARM LIST WITH NEW OUTLEN,OUTBUFF:
*
NOBLD    BAL   R14,PARMBLD
         LTR   R15,R15            RC = 0?  OK?
         BNZ   GOUT               NO. -> GO TO FAST EXIT
         SPACE 2
*
*              PROCEED WITH CALL TO FULLSCR3:
*
CFS3     L     R1,FULLPARM
         CALL  FULLSCR3
         SPACE 2
*
*              CLEANUP ANY LEFTOVERS:
*
         BAL   R14,CLEANUP
         EJECT
*
*              NOW WE ARE ALL DONE:
*
GOUT     LEAVER
         EJECT
PARMCHK  ST    R14,L1LS
*
         CLI   0(R10),C'3'              V3 CALL?
         BE    CP                       YES
         CLI   0(R10),C'S'              V3 CALL?
         BNE   PEX                      NO->IGNORE V2
CP       OI    MF1,X'80'                FLAG V3 CALL
         SPACE
*                                       COUNT THE FULLSCR3 PARMS GIVEN:
         SLR   R7,R7                    R7=0 INIT CNT
         LA    R9,6                     MAX NORMAL PARMS
         LA    R8,2(,R10)               R8->XES
PPLOOP   CLI   0(R8),C' '               ONE THERE?
         BE    NOCNT                    NO
         LA    R7,1(,R7)                R7+1 COUNT
NOCNT    LA    R8,1(,R8)                R8->NEXT X
         BCT   R9,PPLOOP                MORE?
         LA    R7,1(,R7)                R7+1 - COUNT IOPARM
         STH   R7,PC1                   SAVE COUNT
         SPACE
*                                       COUNT MOD BLKS ALSO:
         SLR   R9,R9
         LA    R8,0(,R11)               R8->PARM1
P2LOOP   TM    0(R8),X'80'              LAST ONE?
         BO    CLP                      YES
         LA    R8,4(,R8)                R8+4 = NEXT PARM
         LA    R9,1(,R9)                R9+1 = COUNT IT
         B     P2LOOP                   MORE?
CLP      LA    R9,1(,R9)                R9+1 COUNT LAST ONE
         STH   R9,PC2                   SAVE COUNT
         SPACE
         CH    R9,PC1                   GOOD COUNT?
         BL    PERR                     NO
         BE    PEX                      YES BUT NO MODBLKS
         OI    MF1,X'40'                YES FLAG PRESENCE OF MODBLKS
         B     PEX
         SPACE
PERR     LA    R15,99                   SAY BAD PARMS
PEX      L     R14,L1LS
         BR    R14
         EJECT
*
*              SUBRTN TO BUILD NEW OUTBUFF FROM OLD AND MODBLOCKS:
*
BLDBUFF  ST    R14,L1LS
         SPACE
*              FIRST GET THE NEW OUTBUFF AREA:
         LA    R0,4095                  R0=BUF LEN
         GETMAIN R,LV=(0)              R1->NEW OUTBUFF
         ST    R1,BUFADD                SAVE ADDR
         LR    R9,R1                    R9->OUTBUFF
         XC    BUFLEN,BUFLEN            SET LEN=0
         SPACE
*              NEXT MOVE ANY OLD-OUTBUFF INTO THE NEW OUTBUFF:
         BAL   R14,SAVESCRN             MOVE ANY OLD-OUTBUFF
         SPACE
*              THEN ADD EACH MODBLOCK TO THE NEW OUTBUFF:
         LH    R8,PC1                   R8=# OF FS3 PARMS
         SLL   R8,2                     R8*4 = OFFSET TO MODS
         LA    R8,0(R8,R11)             R8->FIRST MODBLK PTR
MBLOOP   L     R7,0(,R8)                R7->NEXT MODBLK
         BAL   R14,MOVEMOD              GET IT IN OUTBUFF
         TM    0(R8),X'80'              LAST ONE?
         BO    BEX                      YES -> DONE
         LA    R8,4(,R8)                R8+4 - PT TO NEXT PARM
         B     MBLOOP                   AGAIN
         SPACE
BEX      L     R14,L1LS
         BR    R14
         EJECT
PARMBLD  ST    R14,L1LS
         SPACE
*              CHECK FOR OLD OUTLEN,OUTBUFF AND SKIP THEM:
*
         XC    MYPARMS,MYPARMS          CLEAR
         LH    R8,PC1                   R8=FS3 PARM CNT
         BCTR  R8,R0                    R8-1 SKIP IOPARM
         LA    R9,4(,R11)               R9->SECOND PARM
         CLI   2(R10),C' '              IS IT LEN?
         BE    CFOB                     NO
         BCTR  R8,R0                    YES SKIP
         LA    R9,4(,R9)                PT PAST
CFOB     CLI   3(R10),C' '              OUTBUFF THERE?
         BE    ROPL                     NO
         BCTR  R8,R0                    YES SKIP
         LA    R9,4(,R9)                PT PAST
         SPACE
*              BUILD A NEW IOPARM AND POINT NEW PARM LIST THERE:
*
ROPL     MVC   MIOPARM(8),0(R10)        MOVE IOPARM
         MVC   MIOPARM+2(2),=C'XX'      FORCE OUTBUFF
         LA    R2,MIOPARM               R2-> NEW IOPARM
         ST    R2,MYPARMS               SAVE IN NEW PARM LIST
         SPACE
*              ADD THE NEW OUTLEN,OUTBUFF TO THE NEW PARM LIST:
*
         LA    R2,BUFLEN                R2->NEW OUTLEN
         ST    R2,MYPARMS+4             SAVE IN PARMLIST
         MVC   MYPARMS+8(4),BUFADD      NEW OUTBUFF
         LTR   R8,R8                    ANY OTHERS
         BNZ   MOP                      YES
         OI    MYPARMS+8,X'80'          SAY LAST
         SPACE
PBEX     LA    R1,MYPARMS               R1->NEW PARMLIST
         ST    R1,FULLPARM              SAVE PTR
         L     R14,L1LS
         BR    R14
         SPACE
*              ADD ANY OTHER OLD FULLSCR3 PARMS TO THE NEW PARM LIST:
*
MOP      LA    R7,MYPARMS+8             R7->FIRST EMPTY SLOT
MOPL     LA    R7,4(,R7)                R7->NEXT SLOT
         MVC   0(4,R7),0(R9)            PASS THE PARM
         LA    R9,4(,R9)                NEXT PARM
         BCT   R8,MOPL                  MORE?
         OI    0(R7),X'80'              SAY LAST
         B     PBEX
         EJECT
*
*              SUBRTN TO FREE ANY LEFTOVER NEW OUTBUFF:
*
CLEANUP  ST    R14,L1LS
         TM    MF1,X'40'                NEW OUTBUF BUILT?
         BNOR  R14                      NO
         LA    R0,4095                  R0=LEN
         L     R1,BUFADD                R1->OUTBUF
         FREEMAIN R,LV=(0),A=(1)        KILL IT
         L     R14,L1LS
         BR    R14
         EJECT
SAVESCRN ST    R14,L2LS
         SPACE
*              WAS AN OUTLEN,OUTBUFF COMBO PASSED?:
*
         CLI   2(R10),C' '              OUTLEN?
         BE    IWCC                     NO
         L     R7,4(,R11)               R7->OUTLEN
         LH    R7,0(,R7)                R7=OUTBUFF LEN
         STH   R7,BUFLEN                SAVE
         CLI   3(R10),C' '              OUTBUFF
         BNE   MTB                      YES
         LA    R15,99                   BAD PARMS
         B     SSX                      XIT
         SPACE
*              YES -> MOVE THE OUTBUFF PASSED INTO THE NEW OUTBUFF:
*
MTB      L     R6,8(,R11)               R6->OUTBUFF OLD
         LR    R5,R7                    R5=MOVE LENGTH
         LR    R4,R9                    R4->NEW OUTBUFF
         MVCL  R4,R6                    MOVE OLD->NEW
         LR    R9,R4                    R9->PAST DATA
         SPACE
SSX      L     R14,L2LS
         BR    R14
         SPACE
*              NO  -> PUT A DEFAULT WRITE-CONTROL-CHAR IN NEW OUTBUFF:
*                  ->  ALSO SPF CODE FOR AUTO RESHOW FEATURE:
         SPACE
IWCC     MVC   0(7,R9),=X'C3115D7F114040' INSERT DEFAULT WCC & SPF STR
         LA    R9,7(,R9)                R9+7 - PT PAST
         MVC   BUFLEN,=H'7'             SET INIT LEN TO 7
         B     SSX
         EJECT
*
*              SUBRTN TO ADD A MODBLOCK TO THE NEW OUTBUFF:
*
MOVEMOD  ST    R14,L2LS
         SPACE
*              IGNORE THE MODBLOCK IF NO DATA PRESENT:
*
         LH    R2,6(,R7)                R2=DATA LENGTH
         LTR   R2,R2                    ANY?
         BNZ   HASDATA                  YES
         CLI   1(R7),C'B'               CURSOR MOVE?
         BE    HASDATA                  YES
         CLI   1(R7),C'C'               CURSOR MOVE?
         BNER  R14                      NO
         SPACE
*              FIND THE 3270 ADDRESS NEEDED & PUT IN NEW OUTBUFF:
*
HASDATA  SLR   R4,R4
         CLI   0(R7),C'R'               ROW,COL ADDR?
         BE    RCFORM                   YES
         LH    R5,2(,R7)                GET HIS OFFSET
         B     OFFORM
RCFORM   LH    R5,2(,R7)                GET HIS ROW
         BCTR  R5,R0                    R5-1
         MH    R5,=H'80'                R5*80
         AH    R5,4(,R7)                R5+COL=OFFSET+1
         BCTR  R5,R0                    R5-1=OFFSET ADDR
OFFORM   D     R4,=F'64'          (R4,R5)/64 R4=REMAINDER,R5=QUOTIENT
         N     R5,=X'0000003F'          JUST IN CASE
         MVI   0(R9),X'11'              NEED SBA HERE
         STC   R5,1(R9)                 PUT ROW IN WORKAREA
         STC   R4,2(R9)                 PUT COL IN WORKAREA
         TR    1(2,R9),TABLE1           CHANGE TO 3270 ADDRESS
         LA    R9,3(,R9)                R9+3 PT PAST
         LH    R6,BUFLEN                R6=CURRENT LENGTH
         AH    R6,=H'3'                 R6+3 = NEW OUTBUFF LENGTH
         EJECT
*
*              NEXT FIND THE OUTPUT DATA:
*
         LA    R4,8(,R7)                R4->DATA
         LH    R5,6(,R7)                R5=DATA LEN
         SPACE
*
*              PUT IN A START-FIELD ORDER IF REQUESTED:
*
         CLI   1(R7),C'A'               ATTR PRESENT?
         BE    DOSF                     YES
         CLI   1(R7),C'B'               ATTR PRESENT?
         BNE   CFIC                     NO
DOSF     LTR   R5,R5                    R5=0? AT LEAST ONE DATA BYTE?
         BZ    CFIC                     NO
         MVI   0(R9),X'1D'              PUT SF IN OUTBUFF
         MVC   1(1,R9),0(R4)            TACK ON THE ATTR CHAR
         LA    R4,1(,R4)                R4+1 -> NEXT DATA BYTE
         BCTR  R5,R0                    R5-1 = NEW DATA LEN
         LA    R9,2(,R9)                R9+2 PP
         LA    R6,2(,R6)                R6+2 INCR LEN
         SPACE
*              CHECK FOR INSERT CURSOR REQUEST:
*
CFIC     CLI   1(R7),C'C'               MOVE CURSOR?
         BE    DOIC                     YES
         CLI   1(R7),C'B'               MOVE CURSOR?
         BNE   MTD                      NO
DOIC     MVI   0(R9),X'13'              PUT IN IC CHAR
         LA    R9,1(R9)                 R9+1 -> NEXT FREE BYTE
         LA    R6,1(R6)                 R6+1 = NEW OUTPUT LENGTH
         SPACE
*              FINALLY WE CAN MOVE IN THE NEW DATA:
*
MTD      LTR   R5,R5                    R5=0? ANY DATA LEFT TO MOVE?
         BZ    MMX                      NO->EXIT NOW
         LR    R1,R5                    R1=R5 SAVE THE LENGTH
         LR    R2,R9                    R2->BUFF LOC
         LR    R3,R5                    R3=DATA LEN
         MVCL  R2,R4                    MOVE THE DATA
         LR    R9,R2                    R9= UPDATED BUFF LOC
         AR    R6,R1                    R6+R1=NEW OUTPUT LENGTH
         SPACE
MMX      STH   R6,BUFLEN                SAVE THE OUTPUT LENGTH
         L     R14,L2LS
         BR    R14
         EJECT
*
*        STATIC WORKAREA:
*
TABLE1   DC    C' ABCDEFGHI¢.<(+|&&JKLMNOPQR!$*);¬'
         DC    C'-/STUVWXYZ ,%_>?0123456789:#@''="'
         ORG   TABLE1+X'2A'
         DC    X'6A'
         ORG
*
*
         LTORG
         EJECT
*
*   WORKING STORAGE AREA - DYNAMIC:
*
WORKAREA DSECT
SAVEAREA DS    18F
L1LS     DS    F
L2LS     DS    F
L3LS     DS    F
*
PC1      DS    H
PC2      DS    H
MF1      DS    X
*
BUFADD   DS    F
BUFLEN   DS    H
*
FULLPARM DS    F
MYPARMS  DS    10F
MIOPARM  DS    D
CLL      DS    0X
WORKLEN  EQU   *-WORKAREA
         END
