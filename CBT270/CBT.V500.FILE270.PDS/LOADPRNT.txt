000010 ID DIVISION.                                                     01/07/80
000020 PROGRAM-ID.     LOADPRNT.                                        764LODPR
000030 AUTHOR.         JEFF SPREHN.                                     NT LV004
000040     EJECT
000050 ENVIRONMENT DIVISION.
000060 CONFIGURATION SECTION.
000070 SOURCE-COMPUTER. IBM-370.
000080 OBJECT-COMPUTER. IBM-370.
000090 SPECIAL-NAMES.
000100     C01 IS NEW-PAGE.
000110 INPUT-OUTPUT SECTION.
000120 FILE-CONTROL.
000130
000140     SELECT  EXTRACT-FILE        ASSIGN UT-S-EXTRACT.
000150     SELECT  REPORT-FILE         ASSIGN UT-S-REPORT.
000160     EJECT
000170 DATA DIVISION.
000180
000190 FILE SECTION.
000200
000210 FD  EXTRACT-FILE
000220     LABEL RECORDS ARE   STANDARD
000230     RECORDING MODE IS   F
000240     RECORD CONTAINS     16 CHARACTERS
000250     BLOCK CONTAINS      0 RECORDS
000260     DATA RECORDS IS     EXTRACT-REC.
000270
000280 01  EXTRACT-REC.
000290     05  CALLING-PROGRAM                     PIC X(8).
000300     05  CALLED-PROGRAM                      PIC X(8).
000310     EJECT
000320 FD  REPORT-FILE
000330     LABEL RECORDS ARE   STANDARD
000340     RECORDING MODE IS   F
000350     RECORD CONTAINS     80 CHARACTERS
000360     BLOCK CONTAINS      0 RECORDS
000370     DATA RECORDS IS     REPORT-REC.
000380
000390 01  REPORT-REC.
000400     05  FILLER                              PIC X(01).
000410     05  PRINT-REC                           PIC X(79).
000420     EJECT
000430 WORKING-STORAGE SECTION.
000440
000450 77  THE-LINE-COUNT      VALUE 99            PIC 99.
000460 77  PREVIOUS-KEY                            PIC X(8).
000470 77  THE-TITLE-LINE                          PIC X(79).
000480 77  THE-FIRST-COL-HEAD                      PIC X(79).
000490 77  THE-PAGE-NO         VALUE ZEROS         PIC 9(3).
000500 77  PREVIOUS-RECORD     VALUE LOW-VALUES    PIC X(16).
000510
000520 01  ABOUT-TO-ABORT                          PIC X.
000530
000540 01  ABORT-NUMBER
000550       REDEFINES ABOUT-TO-ABORT              PIC 9.
000560
000570 01  BREAKDOWN-INPUT-REC.
000580     05  BYTES-1-8                           PIC X(8).
000590     05  BYTES-9-16                          PIC X(8).
000600
000610 01  WDPSC-TITLE-LINE.
000620     05  FILLER                              PIC X(41)  VALUE
000630         'WASHINGTON DATA PROCESSING SERVICE CENTER'.
000640     05  FILLER         VALUE SPACES         PIC X(30).
000650     05  FILLER         VALUE 'PAGE '        PIC X(05).
000660     05  PAGE-NUMBER                         PIC ZZ9.
000670
000680 01  TITLE-LINE-1.
000690     05  FILLER                              PIC X(31)  VALUE
000700         'CALLING PROGRAM CROSS REFERENCE'.
000710     05  FILLER         VALUE SPACES         PIC X(40).
000720     05  THE-RUN-DATE                        PIC X(08).
000730
000740 01  TITLE-LINE-2.
000750     05  FILLER                              PIC X(31)  VALUE
000760         'CALLED PROGRAM CROSS REFERENCE '.
000770     05  FILLER         VALUE SPACES         PIC X(40).
000780     05  THE-RUN-DATE                        PIC X(08).
000790
000800 01  COL-HEAD-1A.
000810     05  FILLER                              PIC X(23)  VALUE
000820         'CALLING          CALLED'.
000830     05  FILLER         VALUE SPACES         PIC X(56).
000840
000850 01  COL-HEAD-1B.
000860     05  FILLER                              PIC X(24)  VALUE
000870         'CALLED           CALLING'.
000880     05  FILLER         VALUE SPACES         PIC X(55).
000890
000900 01  COL-HEAD-2.
000910     05  FILLER                              PIC X(24)  VALUE
000920         'PROGRAM          PROGRAM'.
000930     05  FILLER         VALUE SPACES         PIC X(55).
000940
000950 01  COL-HEAD-3.
000960     05  FILLER                              PIC X(25)  VALUE
000970         '--------         --------'.
000980     05  FILLER         VALUE SPACES         PIC X(54).
000990
001000 01  DETAIL-LINE.
001010     05  PGM-NAME-ON-LEFT                    PIC X(8).
001020     05  FILLER      VALUE SPACES            PIC X(9).
001030     05  PGM-NAME-ON-RIGHT                   PIC X(8).
001040     05  FILLER      VALUE SPACES            PIC X(54).
001050
001060     EJECT
001070 LINKAGE SECTION.
001080
001090 01  PARM-AREA.
001100     05  PARM-CNT                            PIC S9(4) COMP.
001110     05  PARM-VALUE                          PIC X(7).
001120
001130     EJECT
001140 PROCEDURE DIVISION USING PARM-AREA.
001150
001160 0000-OPEN.
001170
001180     OPEN   INPUT  EXTRACT-FILE
001190           OUTPUT  REPORT-FILE.
001200
001210     IF PARM-VALUE = 'CALLING' OR 'CALLED '
001220          NEXT SENTENCE
001230     ELSE
001240          DISPLAY '*********************************'
001250          DISPLAY '*                               *'
001260          DISPLAY '*  PURPOSELY ABORTING           *'
001270          DISPLAY '*                               *'
001280          DISPLAY '*       INVALID PARM VALUE      *'
001290          DISPLAY '*                               *'
001300          DISPLAY '*                               *'
001310          DISPLAY '*********************************'
001320          MOVE SPACE TO ABOUT-TO-ABORT
001330          ADD 1 TO ABORT-NUMBER.
001340
001350     MOVE CURRENT-DATE TO THE-RUN-DATE OF TITLE-LINE-1.
001360
001370     MOVE CURRENT-DATE TO THE-RUN-DATE OF TITLE-LINE-2.
001380
001390     IF PARM-VALUE = 'CALLING'
001400         MOVE TITLE-LINE-1 TO THE-TITLE-LINE
001410         MOVE COL-HEAD-1A TO THE-FIRST-COL-HEAD
001420     ELSE
001430         MOVE TITLE-LINE-2 TO THE-TITLE-LINE
001440         MOVE COL-HEAD-1B TO THE-FIRST-COL-HEAD.
001450
001460 1000-READ.
001470
001480     READ  EXTRACT-FILE
001490        AT END
001500          GO TO 9999-QUIT.
001510
001520     IF EXTRACT-REC = PREVIOUS-RECORD
001530           GO TO 1000-READ
001540     ELSE
001550           MOVE EXTRACT-REC TO PREVIOUS-RECORD.
001560
001570     IF PARM-VALUE = 'CALLING'
001580           MOVE CALLING-PROGRAM OF EXTRACT-REC
001590             TO BYTES-1-8 OF BREAKDOWN-INPUT-REC
001600           MOVE CALLED-PROGRAM OF EXTRACT-REC
001610             TO BYTES-9-16 OF BREAKDOWN-INPUT-REC
001620     ELSE
001630           MOVE CALLED-PROGRAM OF EXTRACT-REC
001640             TO BYTES-1-8 OF BREAKDOWN-INPUT-REC
001650           MOVE CALLING-PROGRAM OF EXTRACT-REC
001660             TO BYTES-9-16 OF BREAKDOWN-INPUT-REC.
001670
001680     IF THE-LINE-COUNT IS GREATER THAN 85
001690         ADD 1 TO THE-PAGE-NO
001700         MOVE THE-PAGE-NO TO PAGE-NUMBER OF WDPSC-TITLE-LINE
001710         MOVE WDPSC-TITLE-LINE TO PRINT-REC
001720         WRITE REPORT-REC AFTER NEW-PAGE
001730         MOVE THE-TITLE-LINE TO PRINT-REC
001740         WRITE REPORT-REC AFTER 2
001750         MOVE SPACES TO PRINT-REC
001760         WRITE REPORT-REC AFTER 2
001770         MOVE THE-FIRST-COL-HEAD TO PRINT-REC
001780         WRITE REPORT-REC AFTER 1
001790         MOVE COL-HEAD-2 TO PRINT-REC
001800         WRITE REPORT-REC AFTER 1
001810         MOVE COL-HEAD-3 TO PRINT-REC
001820         WRITE REPORT-REC AFTER 1
001830         MOVE SPACES TO PRINT-REC
001840         WRITE REPORT-REC AFTER 1
001850         MOVE LOW-VALUES TO PREVIOUS-KEY
001860         MOVE +09 TO THE-LINE-COUNT.
001870
001880     IF BYTES-1-8 OF BREAKDOWN-INPUT-REC = PREVIOUS-KEY
001890           MOVE SPACES
001900             TO PGM-NAME-ON-LEFT OF DETAIL-LINE
001910           MOVE BYTES-9-16 OF BREAKDOWN-INPUT-REC
001920             TO PGM-NAME-ON-RIGHT OF DETAIL-LINE
001930           MOVE DETAIL-LINE TO PRINT-REC
001940           WRITE REPORT-REC AFTER 1
001950           ADD +01 TO THE-LINE-COUNT
001960     ELSE
001970           MOVE BYTES-1-8 OF BREAKDOWN-INPUT-REC
001980             TO PGM-NAME-ON-LEFT OF DETAIL-LINE
001990           MOVE BYTES-9-16 OF BREAKDOWN-INPUT-REC
002000             TO PGM-NAME-ON-RIGHT OF DETAIL-LINE
002010           MOVE DETAIL-LINE TO PRINT-REC
002020           WRITE REPORT-REC AFTER ADVANCING 2
002030           ADD +02 TO THE-LINE-COUNT
002040           MOVE BYTES-1-8 OF BREAKDOWN-INPUT-REC
002050             TO PREVIOUS-KEY.
002060
002070     GO TO 1000-READ.
002080     EJECT
002090 9999-QUIT.
002100
002110     CLOSE  EXTRACT-FILE
002120            REPORT-FILE.
002130
002140     STOP RUN.
