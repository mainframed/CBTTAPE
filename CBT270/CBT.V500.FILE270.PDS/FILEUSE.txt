       ID DIVISION.
       PROGRAM-ID.     FILEUSE.
       AUTHOR.         JEFF SPREHN.
           EJECT
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
           C01 IS NEW-PAGE.
       INPUT-OUTPUT SECTION.

       FILE-CONTROL.

           SELECT  INPUT-FILE          ASSIGN UT-S-INPUT.
           SELECT  REPORT-FILE         ASSIGN UT-S-REPORT.
           EJECT
       DATA DIVISION.

       FILE SECTION.

       FD  INPUT-FILE
           LABEL RECORDS ARE   STANDARD
           RECORDING MODE IS   F
           RECORD CONTAINS     80 CHARACTERS
           BLOCK CONTAINS      0 RECORDS
           DATA RECORDS IS     INPUT-REC.

       01  INPUT-REC.
           05  INPUT-DSNAME                       PIC X(44).
           05  FILLER                             PIC X(36).
           EJECT

       FD  REPORT-FILE
           LABEL RECORDS ARE   STANDARD
           RECORDING MODE IS   F
           RECORD CONTAINS     133 CHARACTERS
           BLOCK CONTAINS      0 RECORDS
           DATA RECORDS IS     REPORT-REC.

       01  REPORT-REC.
           05  FILLER                              PIC X.
           05  REPORT-LINE                         PIC X(132).
           EJECT
       WORKING-STORAGE SECTION.

       77  TIME-TO-QUIT        VALUE 'NO '         PIC XXX.
       77  PENDING-PRINT-LINE                      PIC X(132).
       77  THE-LINE-CTR        VALUE +99           PIC S99.
       77  HOLD-JOBNAME                            PIC X(8).

           EJECT
      *
      ***********************************************
      *   P A S S D S C B   P A R A M E T E R S     *
      ***********************************************
      *
       01  PASSDSCB-DSN                            PIC X(44).
       01  PASSDSCB-VOL                            PIC X(6).
       01  PASSDSCB-VALID-SWITCH                   PIC X(3).
       01  PASSDSCB-ERR-MSG                        PIC X(30).
       01  PASSDSCB-DSCB1.
           05  FILLER                              PIC X(17).
           05  LAST-ACCESSOR                       PIC X(08).
           05  FILLER                              PIC X(06).
           05  LAST-ACCESS-DATE.
               10  BYTE-1                          PIC X.
               10  BYTES-2-3                       PIC XX.
           05  FILLER                              PIC X(106).
           EJECT
      *
      ***********************************************
      *   F I L E A T T R   P A R A M E T E R S     *
      ***********************************************
      *
       01  FILEATTR-IN-DSN                         PIC X(44).
       01  FILEATTR-OUT-VALID-SWITCH               PIC X(3).
       01  FILEATTR-OUT-ERROR-MESSAGE.
           05  BYTES-1-13                          PIC X(13).
           05  FILLER                              PIC X(17).
       01  FILEATTR-OUT-VOLSER                     PIC X(6).
       01  FILEATTR-OUT-LRECL                      PIC 9(5).
       01  FILEATTR-OUT-BLKSIZE                    PIC 9(5).
       01  FILEATTR-OUT-DSORG                      PIC X(2).
       01  FILEATTR-OUT-RECFM                      PIC X(1).
       01  FILEATTR-OUT-DEVICE                     PIC X(1).
       01  FILEATTR-IN-VOLSER                      PIC X(6).
           EJECT
      *
      ***********************************************
      *   J T O S C O N V   P A R A M E T E R S     *
      ***********************************************
      *


       01  JTOSCONV-I-O-AREA.
           05  FILLER                              PIC X(8).
           05  STANDARD-DATE.
               10  MONTH                           PIC XX.
               10  DAYY                            PIC XX.
               10  YEAR                            PIC XX.
           05  JULIAN-DATE                         PIC X(5).
           05  INVALID-INPUT-DATE                  PIC X(3).

           EJECT
      *
      ***********************************************
      *   R E P O R T   P R I N T   L I N E S       *
      *            (GENERATED BY CODEIT)            *
      ***********************************************
      *
       01  TITLE-LINE.
           05  FILLER
                PIC X(47)
                VALUE ' DATASET INFORMATION FOR ALL DATASETS BEGINNING'.
           05  FILLER
                PIC X(28)
                VALUE ' WITH HIGH LEVEL QUALIFIER: '.
           05  THE-PREFIX
                PIC X(08).
           05  FILLER
                PIC X(47)
                VALUE  SPACE.
           05  FILLER
                PIC X(02)
                VALUE  SPACE.

       01  COL-HEAD-1.
           05  FILLER
                PIC X(47)
                VALUE  SPACE.
           05  FILLER
                PIC X(47)
                VALUE ' RECORD                                     DAT'.
           05  FILLER
                PIC X(38)
                VALUE 'E  OF   JOBNAME/LOGONID               '.

       01  COL-HEAD-2.
           05  FILLER
                PIC X(47)
                VALUE ' DATASET NAME                                  '.
           05  FILLER
                PIC X(47)
                VALUE ' FORMAT     LRECL   BLKSIZE  DSORG  VOLUME  LAS'.
           05  FILLER
                PIC X(38)
                VALUE 'T OPEN  OF LAST USER                  '.

       01  COL-HEAD-3.
           05  FILLER
                PIC X(47)
                VALUE ' --------------------------------------------  '.
           05  FILLER
                PIC X(47)
                VALUE '---------  -------  -------  -----  ------  ---'.
           05  FILLER
                PIC X(38)
                VALUE '------  ---------------               '.

       01  DETAIL-LINE.
           05  FILLER
                PIC X(01)
                VALUE  SPACE.
           05  THE-DSNAME
                PIC X(44).
           05  FILLER
                PIC X(02)
                VALUE  SPACE.
           05  THE-REC-FORMAT
                PIC X(09).
           05  FILLER
                PIC X(03)
                VALUE  SPACE.
           05  THE-LRECL
                PIC ZZZZ9.
           05  FILLER
                PIC X(04)
                VALUE  SPACE.
           05  THE-BLKSIZE
                PIC ZZZZ9.
           05  FILLER
                PIC X(04)
                VALUE  SPACE.
           05  THE-DSORG
                PIC XX.
           05  FILLER
                PIC X(04)
                VALUE  SPACE.
           05  THE-VOL-SER-NO
                PIC X(06).
           05  FILLER
                PIC X(02)
                VALUE  SPACE.
           05  THE-DATE-OF-LAST-OPEN.
               10  THE-MONTH
                PIC XX.
               10  FILLER
                PIC X(01)
                VALUE '-'.
               10  THE-DAY
                PIC XX.
               10  FILLER
                PIC X(01)
                VALUE '-'.
               10  THE-YEAR
                PIC XX.
           05  FILLER
                PIC X(06)
                VALUE  SPACE.
           05  THE-LAST-USER
                PIC X(08).
           05  FILLER
                PIC X(19)
                VALUE  SPACE.

       01  COMMON-ERROR-LINE.
           05  FILLER
                PIC X(01)
                VALUE  SPACE.
           05  THE-DSNAME
                PIC X(44).
           05  FILLER
                PIC X(02)
                VALUE  SPACE.
           05  THE-ERROR-REASON
                PIC X(66).
           05  FILLER
                PIC X(19)
                VALUE  SPACE.

       01  DATASET-ON-TAPE-PRINT-LINE.
           05  FILLER  VALUE SPACE         PIC X(01).
           05  THE-DSNAME                  PIC X(44).
           05  FILLER  VALUE SPACES        PIC X(02).
           05  FILLER                      PIC X(18)   VALUE
                   'DATASET IS ON TAPE'.
           05  FILLER  VALUE SPACES        PIC X(18).
           05  THE-VOLSER                  PIC X(6).
           05  FILLER  VALUE SPACES        PIC X(43).
           EJECT
      *
      **************************************************************
      *   O T H E R   W O R K I N G   S T O R A G E   F I E L D S  *
      **************************************************************
      *

       01  YEAR-OF-LAST-ACCESS.
           05  FILLER   VALUE LOW-VALUE            PIC X.
           05  LAST-ACCESS-YEAR                    PIC X.

       01  REDEF-YR-OF-LAST-ACCESS
             REDEFINES YEAR-OF-LAST-ACCESS         PIC S9(2) COMP.

       01  DAY-OF-LAST-ACCESS.
           05  LAST-ACCESS-DAY                     PIC XX.

       01  REDEF-DAY-OF-LAST-ACCESS
             REDEFINES DAY-OF-LAST-ACCESS         PIC S9(3) COMP.

       01  LAST-ACCESS-DATE-DISPLAY.
           05  LAST-ACCESS-YEAR                    PIC 9(2).
           05  LAST-ACCESS-DAY                     PIC 9(3).

           EJECT
       LINKAGE SECTION.

       01  PARM-AREA.
           05  FILLER                               PIC S9(4) COMP.
           05  PARM-PREFIX                          PIC X(8).

           EJECT
       PROCEDURE DIVISION USING PARM-AREA.

           PERFORM 1000-INITIALIZATION
              THRU 1999-EXIT-INITIALIZATION.

           PERFORM 2000-MAIN-PROCESS
              THRU 2999-EXIT-MAIN-PROCESS

                    UNTIL

                         TIME-TO-QUIT = 'YES'.

           PERFORM 9000-END-OF-JOB
              THRU 9999-EXIT-END-OF-JOB.

           STOP RUN.
           EJECT
       1000-INITIALIZATION.

           OPEN  INPUT  INPUT-FILE
                 OUTPUT REPORT-FILE.

           PERFORM 3000-READ-INPUT-FILE
              THRU 3999-EXIT-READ.

           MOVE PARM-PREFIX TO THE-PREFIX OF TITLE-LINE.

       1999-EXIT-INITIALIZATION.
           EXIT.
           EJECT
       2000-MAIN-PROCESS.

           MOVE INPUT-DSNAME OF INPUT-REC TO FILEATTR-IN-DSN.

           CALL 'FILEATTR' USING FILEATTR-IN-DSN
                                 FILEATTR-OUT-VALID-SWITCH
                                 FILEATTR-OUT-ERROR-MESSAGE
                                 FILEATTR-OUT-VOLSER
                                 FILEATTR-OUT-LRECL
                                 FILEATTR-OUT-BLKSIZE
                                 FILEATTR-OUT-DSORG
                                 FILEATTR-OUT-RECFM
                                 FILEATTR-OUT-DEVICE.

           IF FILEATTR-OUT-VALID-SWITCH = 'YES'
                GO TO 2100-CALL-PASSDSCB.

      *    START CASE STATEMENT

             IF BYTES-1-13 OF FILEATTR-OUT-ERROR-MESSAGE
               EQUAL 'FORMAT-1 DSCB'
                MOVE 'DATASET NOT FOUND ON VOLUME INDICATED BY CATALOG'
                  TO THE-ERROR-REASON OF COMMON-ERROR-LINE
                MOVE INPUT-DSNAME OF INPUT-REC
                  TO THE-DSNAME OF COMMON-ERROR-LINE
                MOVE COMMON-ERROR-LINE TO PENDING-PRINT-LINE
                PERFORM 4000-WRITE-REPORT
                   THRU 4999-EXIT-WRITE-REPORT
                PERFORM 6000-OVERSTRIKE-ERR-LINE
                   THRU 6999-EXIT-OVERSTRIKE
                GO TO 2900-TAKE-ANOTHER-READ
           ELSE
             IF BYTES-1-13 OF FILEATTR-OUT-ERROR-MESSAGE
               EQUAL 'INVALID SYNTA'
                MOVE 'INVALID DATASET NAME'
                  TO THE-ERROR-REASON OF COMMON-ERROR-LINE
                MOVE INPUT-DSNAME OF INPUT-REC
                  TO THE-DSNAME OF COMMON-ERROR-LINE
                MOVE COMMON-ERROR-LINE TO PENDING-PRINT-LINE
                PERFORM 4000-WRITE-REPORT
                   THRU 4999-EXIT-WRITE-REPORT
                PERFORM 6000-OVERSTRIKE-ERR-LINE
                   THRU 6999-EXIT-OVERSTRIKE
                GO TO 2900-TAKE-ANOTHER-READ
           ELSE
             IF BYTES-1-13 OF FILEATTR-OUT-ERROR-MESSAGE
               EQUAL 'DATASET IS ON'
                MOVE FILEATTR-OUT-VOLSER
                  TO THE-VOLSER OF DATASET-ON-TAPE-PRINT-LINE
                MOVE INPUT-DSNAME OF INPUT-REC
                  TO THE-DSNAME OF DATASET-ON-TAPE-PRINT-LINE
                MOVE DATASET-ON-TAPE-PRINT-LINE
                  TO PENDING-PRINT-LINE
                PERFORM 4000-WRITE-REPORT
                   THRU 4999-EXIT-WRITE-REPORT
                PERFORM 7000-OVERSTRIKE-ONTAPE-LINE
                   THRU 7999-EXIT-OVERSTRIKE
                GO TO 2900-TAKE-ANOTHER-READ
           ELSE
             MOVE FILEATTR-OUT-ERROR-MESSAGE
               TO THE-ERROR-REASON OF COMMON-ERROR-LINE
             MOVE INPUT-DSNAME OF INPUT-REC
               TO THE-DSNAME OF COMMON-ERROR-LINE
             MOVE COMMON-ERROR-LINE TO PENDING-PRINT-LINE
             PERFORM 4000-WRITE-REPORT
                THRU 4999-EXIT-WRITE-REPORT
             PERFORM 6000-OVERSTRIKE-ERR-LINE
                THRU 6999-EXIT-OVERSTRIKE
             GO TO 2900-TAKE-ANOTHER-READ.

      *    END CASE STATEMENT

       2100-CALL-PASSDSCB.

           MOVE INPUT-DSNAME OF INPUT-REC TO PASSDSCB-DSN.
           MOVE FILEATTR-OUT-VOLSER TO PASSDSCB-VOL.

           CALL 'PASSDSCB' USING PASSDSCB-DSN
                                 PASSDSCB-VOL
                                 PASSDSCB-VALID-SWITCH
                                 PASSDSCB-ERR-MSG
                                 PASSDSCB-DSCB1.

           IF PASSDSCB-VALID-SWITCH = 'YES'
               NEXT SENTENCE
           ELSE
               MOVE PASSDSCB-ERR-MSG
                 TO THE-ERROR-REASON OF COMMON-ERROR-LINE
               MOVE INPUT-DSNAME OF INPUT-REC
                 TO THE-DSNAME OF COMMON-ERROR-LINE
               MOVE COMMON-ERROR-LINE TO PENDING-PRINT-LINE
               PERFORM 4000-WRITE-REPORT
                  THRU 4999-EXIT-WRITE-REPORT
               GO TO 2900-TAKE-ANOTHER-READ.

           MOVE INPUT-DSNAME OF INPUT-REC
             TO THE-DSNAME OF DETAIL-LINE.

      *    START CASE STATEMENT

               IF FILEATTR-OUT-RECFM = 'F'
                   MOVE 'FIXED    ' TO THE-REC-FORMAT OF DETAIL-LINE
           ELSE
               IF FILEATTR-OUT-RECFM = 'V'
                   MOVE 'VARIABLE ' TO THE-REC-FORMAT OF DETAIL-LINE
           ELSE
               MOVE 'UNDEFINED' TO THE-REC-FORMAT OF DETAIL-LINE.

      *    END CASE STATEMENT

           MOVE FILEATTR-OUT-LRECL
             TO THE-LRECL OF DETAIL-LINE.
           MOVE FILEATTR-OUT-BLKSIZE
             TO THE-BLKSIZE OF DETAIL-LINE.
           MOVE FILEATTR-OUT-DSORG
             TO THE-DSORG OF DETAIL-LINE.
           MOVE FILEATTR-OUT-VOLSER
             TO THE-VOL-SER-NO OF DETAIL-LINE.
           PERFORM 8000-CONVERT-LAST-ACCESS-DATE
              THRU 8999-EXIT-CONVERSION.
           MOVE LAST-ACCESSOR OF PASSDSCB-DSCB1
             TO HOLD-JOBNAME.

           TRANSFORM HOLD-JOBNAME
              FROM
                  '0123456789$#@ABCDEFGHIJKLMNOPQRSTUVWXYZ|'
                TO
                  '||||||||||||||||||||||||||||||||||||||| '.

           IF HOLD-JOBNAME = '||||||||'
               MOVE LAST-ACCESSOR OF PASSDSCB-DSCB1
                 TO THE-LAST-USER OF DETAIL-LINE
           ELSE
               MOVE 'UNKNOWN '
                 TO THE-LAST-USER OF DETAIL-LINE.

           MOVE DETAIL-LINE TO PENDING-PRINT-LINE.
           PERFORM 4000-WRITE-REPORT
              THRU 4999-EXIT-WRITE-REPORT.

       2900-TAKE-ANOTHER-READ.

           PERFORM 3000-READ-INPUT-FILE
              THRU 3999-EXIT-READ.

       2999-EXIT-MAIN-PROCESS.
           EXIT.
           EJECT
       3000-READ-INPUT-FILE.

           READ INPUT-FILE
               AT END
                   MOVE 'YES' TO TIME-TO-QUIT.

       3999-EXIT-READ.
           EXIT.
           EJECT
       4000-WRITE-REPORT.

           IF THE-LINE-CTR GREATER THAN +55
                PERFORM 5000-WRITE-HEADINGS
                   THRU 5999-EXIT-WRITE-HEADINGS.

           MOVE PENDING-PRINT-LINE TO REPORT-LINE OF REPORT-REC.
           WRITE REPORT-REC AFTER 1.
           ADD +1 TO THE-LINE-CTR.

       4999-EXIT-WRITE-REPORT.
           EXIT.
           EJECT
       5000-WRITE-HEADINGS.

           MOVE TITLE-LINE TO REPORT-LINE OF REPORT-REC.
           WRITE REPORT-REC AFTER NEW-PAGE.
           MOVE COL-HEAD-1 TO REPORT-LINE OF REPORT-REC.
           WRITE REPORT-REC AFTER 2.
           MOVE COL-HEAD-2 TO REPORT-LINE OF REPORT-REC.
           WRITE REPORT-REC AFTER 1.
           MOVE COL-HEAD-3 TO REPORT-LINE OF REPORT-REC.
           WRITE REPORT-REC AFTER 1.
           MOVE SPACES TO REPORT-LINE OF REPORT-REC.
           WRITE REPORT-REC AFTER 1.
           MOVE +6 TO THE-LINE-CTR.

       5999-EXIT-WRITE-HEADINGS.
           EXIT.
           EJECT
       6000-OVERSTRIKE-ERR-LINE.

           MOVE SPACES TO THE-DSNAME OF COMMON-ERROR-LINE.
           MOVE COMMON-ERROR-LINE TO REPORT-LINE OF REPORT-REC.
           WRITE REPORT-REC AFTER 0.
           MOVE COMMON-ERROR-LINE TO REPORT-LINE OF REPORT-REC.
           WRITE REPORT-REC AFTER 0.
           MOVE COMMON-ERROR-LINE TO REPORT-LINE OF REPORT-REC.
           WRITE REPORT-REC AFTER 0.

       6999-EXIT-OVERSTRIKE.
           EXIT.
           EJECT
       7000-OVERSTRIKE-ONTAPE-LINE.

           MOVE SPACES
             TO THE-DSNAME OF DATASET-ON-TAPE-PRINT-LINE.
           MOVE SPACES
             TO THE-VOLSER OF DATASET-ON-TAPE-PRINT-LINE.
           MOVE DATASET-ON-TAPE-PRINT-LINE
             TO REPORT-LINE OF REPORT-REC.
           WRITE REPORT-REC AFTER 0.
           MOVE DATASET-ON-TAPE-PRINT-LINE
             TO REPORT-LINE OF REPORT-REC.
           WRITE REPORT-REC AFTER 0.
           MOVE DATASET-ON-TAPE-PRINT-LINE
             TO REPORT-LINE OF REPORT-REC.
           WRITE REPORT-REC AFTER 0.

       7999-EXIT-OVERSTRIKE.
           EXIT.
           EJECT
       8000-CONVERT-LAST-ACCESS-DATE.

           MOVE BYTE-1 OF LAST-ACCESS-DATE OF PASSDSCB-DSCB1
             TO LAST-ACCESS-YEAR OF YEAR-OF-LAST-ACCESS.

           MOVE REDEF-YR-OF-LAST-ACCESS
             TO LAST-ACCESS-YEAR OF LAST-ACCESS-DATE-DISPLAY.

           MOVE BYTES-2-3 OF LAST-ACCESS-DATE OF PASSDSCB-DSCB1
             TO LAST-ACCESS-DAY OF DAY-OF-LAST-ACCESS.

           MOVE REDEF-DAY-OF-LAST-ACCESS
             TO LAST-ACCESS-DAY OF LAST-ACCESS-DATE-DISPLAY.

           MOVE LAST-ACCESS-DATE-DISPLAY
             TO JULIAN-DATE OF JTOSCONV-I-O-AREA.

           CALL 'JTOSCONV' USING JTOSCONV-I-O-AREA.

           IF INVALID-INPUT-DATE OF JTOSCONV-I-O-AREA = 'YES'
               MOVE '??' TO THE-MONTH OF THE-DATE-OF-LAST-OPEN
                                            OF DETAIL-LINE
               MOVE '??' TO THE-DAY OF THE-DATE-OF-LAST-OPEN
                                            OF DETAIL-LINE
               MOVE '??' TO THE-YEAR OF THE-DATE-OF-LAST-OPEN
                                            OF DETAIL-LINE
               GO TO 8999-EXIT-CONVERSION.

           MOVE MONTH OF STANDARD-DATE OF JTOSCONV-I-O-AREA
             TO THE-MONTH OF THE-DATE-OF-LAST-OPEN OF DETAIL-LINE.
           MOVE DAYY  OF STANDARD-DATE OF JTOSCONV-I-O-AREA
             TO THE-DAY OF THE-DATE-OF-LAST-OPEN OF DETAIL-LINE.
           MOVE YEAR  OF STANDARD-DATE OF JTOSCONV-I-O-AREA
             TO THE-YEAR OF THE-DATE-OF-LAST-OPEN OF DETAIL-LINE.

       8999-EXIT-CONVERSION.
           EXIT.
           EJECT
       9000-END-OF-JOB.

           CLOSE INPUT-FILE
                 REPORT-FILE.

       9999-EXIT-END-OF-JOB.
           EXIT.

