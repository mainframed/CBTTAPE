000010 ID DIVISION.                                                     04/16/76
000020 PROGRAM-ID.    JTOSCONV.                                         23528JTO
000030 AUTHOR.        JEFF SPREHN.                                      SC LV006
000040 INSTALLATION.  LABOR AND INDUSTRIES.
000050 DATE-WRITTEN.  APRIL, 1974.
000060 DATE-COMPILED.
000070 REMARKS.
000080*****************************************************************
000090*  THIS PROGRAM IS A CALLED MODULE THAT PERFORMS TWO FUNCTIONS: *
000100*                                                               *
000110*    1.  EDITS FOR THE VALIDITY OF THE JULIAN DATE THAT IS      *
000120*        BEING PASSED TO THIS MODULE AND MOVES 'YES' TO         *
000130*        INVALID-INPUT-DATE OF JTOSCONV-I-O-AREA IF             *
000140*        THE DATE BEING PASSED CANNOT BE CONVERTED TO STANDARD, *
000150*        ELSE, IF THE DATE CAN BE CONVERTED TO STANDARD, IT     *
000160*        MOVES 'NO ' TO INVALID-INPUT-DATE OF                   *
000170*        JTOSCONV-I-O-AREA.                                     *
000180*                                                               *
000190*    2.  CALLS 'JTOSDATE' AND MOVES THE RESULTANT STANDARD      *
000200*        DATE OF THE FORM (MMDDYY) TO INPUT-DATE OF             *
000210*        JTOSCONV-I-O-AREA SO THAT IT IS AVAILABLE TO           *
000220*        THE MAIN-LINE CALLING PROGRAM IF AND ONLY IF THE       *
000230*        JULIAN DATE BEING PASSED TO THIS MODULE WAS            *
000240*        SUCCESSFULLY VALIDATED.                                *
000250*                                                               *
000260*                                                               *
000270*    THEREFORE THIS MODULE CAN BE USED FOR TWO PURPOSES:        *
000280*                                                               *
000290*        1.  CONVERT JULIAN DATES TO DATES OF THE FORM (MMDDYY) *
000300*            WITHOUT FEAR OF AN '0C7'.                          *
000310*                                                               *
000320*        2.  VALIDATE JULIAN DATES  (IN THIS CASE WE ARE NOT    *
000330*            CONCERNED WHETHER THIS MODULE CALLED 'JTOSDATE'    *
000340*            OR NOT).                                           *
000350*                                                               *
000360*****************************************************************
000370     EJECT
000380 ENVIRONMENT DIVISION.
000390 CONFIGURATION SECTION.
000400 SOURCE-COMPUTER. IBM-370-158.
000410 OBJECT-COMPUTER. IBM-370-158.
000420     EJECT
000430 DATA DIVISION.
000440
000450 WORKING-STORAGE SECTION.
000460
000470 77  WORK-STOR  VALUE 'WORKING  STORAGE'     PIC X(16).
000480
000490 01  INTERMEDIATE-WORK-DATE.
000500     05  MONTH                               PIC 99.
000510     05  DAYY                                PIC 99.
000520     05  YEAR                                PIC 99.
000530
000540 01  STANDARD-DATE.
000550     05  YEAR                                PIC 99.
000560     05  MONTH                               PIC 99.
000570     05  DAYY                                PIC 99.
000580
000590 01  JULIAN-DATE-WORK.
000600     05  YEAR                                PIC 99.
000610     05  DAYY                                PIC 999.
000620
000630 01  INPUT-DATE-VALIDATION.
000640     05  DATE-BREAK                          PIC X(05).
000650     05  TEST-DATE-BREAK REDEFINES DATE-BREAK.
000660         10  YEAR-TEST                       PIC 9(2).
000670         10  DAYY                            PIC 9(3).
000680
000690 01  CALCULATE-RESULTS.
000700     05  QUOTEN.
000710         10  QUOTENTS                        PIC S99V9.
000720     05  QUOTEN-BREAK REDEFINES QUOTEN.
000730         10  QUOT-NUM                        PIC S99V.
000740         10  QUOT-REMAIN                     PIC SV9.
000750     EJECT
000760 LINKAGE SECTION.
000770
       01  JTOSCONV-I-O-AREA.
           05  MODULE-NAME                         PIC X(8).
           05  INPUT-DATE                          PIC X(6).
           05  JULIAN-DATE                         PIC X(5).
           05  INVALID-INPUT-DATE                  PIC X(3).


000870     EJECT
000880 PROCEDURE DIVISION     USING     JTOSCONV-I-O-AREA.
000890
000900 0100.
000910     MOVE 'JTOSCONV' TO MODULE-NAME OF JTOSCONV-I-O-AREA.
000920     MOVE JULIAN-DATE OF JTOSCONV-I-O-AREA
000930       TO DATE-BREAK OF INPUT-DATE-VALIDATION.
000940     MOVE 'NO ' TO INVALID-INPUT-DATE OF
000950         JTOSCONV-I-O-AREA.
000960     PERFORM 15700-VALIDATE-DATE THRU 15799-EXIT-VALIDATE-DATE.
000970
000980     IF INVALID-INPUT-DATE OF JTOSCONV-I-O-AREA
000990        EQUAL TO 'YES'
001000           GO TO 99999-GOBACK.
001010
001020     MOVE JULIAN-DATE OF JTOSCONV-I-O-AREA TO JULIAN-DATE-WORK.
001030
001040     CALL 'JTOSDATE'
001050         USING
001060             JULIAN-DATE-WORK
001070             STANDARD-DATE.
001080
001090     MOVE CORR STANDARD-DATE
001100       TO INTERMEDIATE-WORK-DATE.
001110     MOVE INTERMEDIATE-WORK-DATE
001120         TO INPUT-DATE OF JTOSCONV-I-O-AREA.
001130     GO TO 99999-GOBACK.
001140*
001150*    PARAGRAPHS 15700-VALIDATE-DATE THRU 15799-EXIT-VALIDATE-DATE
001160*    IS ALWAYS PERFORMED.  IT GUARANTEES THAT THE DATE MOVED TO
001170*    DATE-BREAK MAY BE PROPERLY CONVERTED TO STANDARD. IF THE DATE
001180*    FAILS THE VALIDATION, 'YES' IS MOVED TO INVALID-INPUT-DATE.
001190*
001200 15700-VALIDATE-DATE.
001210     IF DATE-BREAK  OF  INPUT-DATE-VALIDATION  NOT NUMERIC
001220         GO TO 15788-REJECT-DATE.
001230
001240     MOVE ZEROS TO QUOTENTS    OF CALCULATE-RESULTS
001250                   QUOT-REMAIN OF CALCULATE-RESULTS
001260                   QUOT-NUM    OF CALCULATE-RESULTS.
001270     COMPUTE QUOTENTS = YEAR-TEST / 04.
001280
001290     IF QUOT-REMAIN NOT EQUAL TO ZEROS
001300        THEN
001310             IF DAYY     OF INPUT-DATE-VALIDATION
001320               IS LESS THAN 001 OR GREATER THAN 365
001330                 GO TO 15788-REJECT-DATE.
001340
001350     IF QUOT-REMAIN EQUAL TO ZEROS
001360        THEN
001370             IF DAYY     OF INPUT-DATE-VALIDATION
001380               IS LESS THAN 001 OR GREATER THAN 366
001390                 GO TO 15788-REJECT-DATE.
001400
001410     GO TO 15799-EXIT-VALIDATE-DATE.
001420
001430 15788-REJECT-DATE.
001440     MOVE 'YES' TO INVALID-INPUT-DATE OF JTOSCONV-I-O-AREA.
001450
001460 15799-EXIT-VALIDATE-DATE.
001470     EXIT.
001480
001490 99999-GOBACK.
001500     GOBACK.
