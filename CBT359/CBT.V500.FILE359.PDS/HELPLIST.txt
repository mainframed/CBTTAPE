         TITLE '    H E L P L I S T   -   V E R S I O N   1 . 2   '
************************************************************
*                                                          *
*              'HELPLIST' PROGRAM AT FPC                   *
*                                                          *
************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE
*        PROGRAM. HELPLIST.
*        AUTHOR. UNKNOWN - FROM TOM GEE.
*        MODIFIOR. BILL GODFREY,  PLANNING RESEARCH CORPORATION.
*        INSTALLATION. FEDERAL POWER COMMISSION.
*        DATE-UPDATED IN SOURCE LIBRARY. JANUARY 8 1974.
*        REMARKS.
*            DATE ADDED TO HEADING.
*            SUBCOMMAND NAMES PREFIXED WITH 'SUBCOMMAND - '.
*            INDENTATION CHANGED. CHECK FOR )X SYNTAX ADDED.
*            USER MACROS - EASYCOME,GEHEN,REGISTER
*
*        MODIFIOR. JERRY PITTENGER, PLANNING RESEARCH CORPORATION.
*        INSTALLATION. OFFICE OF MANAGEMENT AND BUDGET (OMB).
*        DATE-UPDATED IN SOURCE LIBRARY. AUGUST 17 1977.
*        REMARKS:
*            DATE REMOVED FROM HEADING. MULTIPLE HEADING LINES.
*            CHANGE FROM )O OR )X OR )F HIGHLIGHTED BETTER.
*            ALLOW NO ALIAS TO PRINT BY USING BLDL AGAINST
*            THE SYSUT1 LIBRARY/LIBRARIES DIRECTORY. INSERTED
*            SUBCOMMANDS ON SEPARATE PAGES WITH THE SUBCOMMAND
*            NAME IN THE HEADER BLOCK.
         EJECT
*            CURRENT REGISTER USAGE:
*
*            R0 = SYSTEM PARM REG
*            R1 = SYSTEM PARM REG
*            R2 = PROGRAM PARM REG + WORK REG
*            R3 = WORK REG
*            R4 = RECORD LENGTH
*            R5 = BLOCKSIZE
*            R6 = SPARE
*            R7 = SPARE
*            R8 = OFFSET INTO MEMBER DSECT
*            R9 = WORK REG
*           R10 = ADDRESS OF 'LINE'
*           R11 = I/O AREA BASE
*           R12 = PROGRAM BASE
*           R13 = SAVE AREA POINTER
*           R14 = SYSTEM RETURN ADDR
*           R15 = PROGRAM RETURN ADDR
*
*        NON-STANDARD MACROS USED: KOMMEN AND GEHEN
*
*        UPDATED, 04DEC84, WILLIAM SMITH, SYNTEX (USA), INC.
*                 PALO ALTO, CALIFORNIA  94304
*        REMARKS:
*            ELIMINATED USE OF ALL NON-STANDARD MACRO CALLS - ALL CODE
*            IS NOW IN-LINE
*
         EJECT
HELPLIST CSECT
         STM   14,12,12(13)
         LR    12,15
         USING HELPLIST,12
         LA    11,SAVEEM
         ST    13,4(11)
         ST    11,8(13)
         LR    13,11
         L     2,0(,1)             REG 2 --> PARM
         MVI   LINE-1,C' '
         MVC   LINE,LINE-1
         LA    R10,LINE                POINT R10 TO 'LINE'
         MVI   HEAD-1,C' '
         OPEN  (IN,,HELP,,OUT,OUTPUT)
         TM    IN+48,X'10'             OPEN OK?
         BO    PARMCHK                 YES....SYSIN SPECIFIED
         BZ    OPENERR                 NO....CAN'T OPEN
PARMCHK  CLI   1(2),0              PARM?
         BE    NOPARM
         LH    3,0(,2)             R3  =  LENGTH OF PARM
         CLI   1(2),9              PARM LENGTH GREATER THAN 9?
         BNH   *+8                 NO - BRANCH
         LA    3,9                 YES - REDUCE TO 9
         BCTR  3,0                 LENGTH MINUS 1 FOR EX
NOPARM   EQU   *
         LH    R0,HELP+62               BLOCKSIZE
         GETMAIN R,LV=(0)
         LR    R11,R1                   SAVE POINTER TO I/O AREA
CNTRL    EQU   *
         GET   IN,REC                   READ A CONTROL CARD
         MVC   MEM1(8),REC              MOVE TO BLDL AREA
         MVC   MEMNAME,REC              SAVE DESIRED MEMBER NAME
         LA    R1,HELP                  POINT TO DCB FOR BLDL
         LA    R0,LSTSTRT               POINT TO BLDL LIST
         BLDL  (R1),(R0)                GO GET IT
         B     *+4(R15)                 TEST RETURN CODE
         B     MOVE                     GOOD MEMBER
         B     BADONE                   ALIAS OR INVALID MEMBER
         B     BLDLERR                  GO ISSUE ABEND..PERM I/O ERROR
BADONE   EQU   *
         MVC   MEM1+8(6),=X'000000000000' ZERO OUT BLDL TTR FIELD
         B     CNTRL                    GO GET THE NEXT MEMBER
MOVE     TM    MEM1+13,X'80'            IS IT AN ALIAS???
         BO    INDICATE                 ALIAS GO INDICATE SO
         OR    R7,R7                    ZERO OUT R7
         OR    R6,R6                    ZERO OUT R6
         LA    R6,8                     LOAD A CONSTANT
         LA    R14,REC                  LOAD ADDRESS TO CHECK
         LA    R6,0(R6,R14)             COMBINE THE TWO
COMPR    CLI   0(R6),X'40'              CHECK FOR A BLANK
         BNE   COMPDONE                 IF NOT...FINISHED
         CR    R6,R14                   FINISHED YET???
         BE    COMPDONE                 YES
         BCT   R6,COMPR                 DOWN 1 AND CHECK AGAIN
COMPDONE EQU   *                        R6 ALREADY -1 FOR EXECUTE
         SR    R6,R14                   LEAVE THE NUMBER OF CHARS
         STC   R6,MEMLEN                SAVE MEMBER LENGTH FOR EXEC
         LA    R7,8                     LOAD A CONSTANT
         LA    R6,1(R6)                 PUT R6 BACK UP
         SR    R7,R6                    GET NUMBER OF BLANKS
         STC   R7,OFFSET                OFFSET FOR CENTERING ON HDR
         SRL   R7,1                     DIVIDE BY 2
         STC   R7,MEMOFFST              SAVE FOR OFFSETTING
         NI    OP,255-STILLSC           TELL EM NOT SUBCOMMAND ANYMORE
         NI    OP,255-SCSAVED           SUBCOMMAND NOT SAVED ANY MORE
         FIND  HELP,MEMNAME,D           FIND MEMBER
         B     *+4(R15)                 TEST RETURN CODE
         B     *+12                     GOOD RETURN
         B     BADNAME                  INVALID NAME
         B     IOERROR                  I/O ERROR
RDSETUP  NI    OP,255-OPINDENT-OPCMDS   TURN OFF INDENTION & CMDS
         CLC   MEMNAME,=CL8'COMMANDS'   COMMANDS?
         BNE   *+8                      NO, ....
         OI    OP,OPCMDS                YES, INDICATE SUCH
         TM    OP,ALIAS                 ALIAS ALREADY CAUSED TOF???
         BO    READEM                   YES, NO HEADER
         ZAP   CTRLIN,=P'0'        FORCE HEADING
READEM   EQU   *
          NI    OP,255-ALIAS             TURN OFF ALIAS FLAG
         READ  DECB,SF,HELP,(R11),'S'
         CHECK DECB
         LH    R5,HELP+62               BLOCK SIZE
         L     R1,DECB+16               A(IOB)
         SH    R5,14(R1)                MINUS RESIDUAL CCW COUNT
         BCTR  R5,0                     DOWN ONE
         LR    R3,R11                   REC START
         LA    R4,80                    REC LENGTH
         LA    R5,0(R3,R5)              REC END
PROCESS  TM    OP,OPCMDS                CMDS?
         BZ    PROCESSA                 NO, CONTINUE
         MVC   LINEPL(72),0(R3)         YES, COPY IN RECORD
         B     LISTER                   GO LIST IT
PROCESSA CLC   0(2,R3),=C')F'           FUNCTION
         BE    MAJOR
         CLC   0(2,R3),=C')S'           SUBCOMMANDS
         BE    MAJOR
         CLC   0(2,R3),=C')X'           SYNTAX
         BE    MAJOR
         CLC   0(2,R3),=C')O'           OPERANDS
         BE    INDENT
         CLI   0(R3),C'='               SUB COMMAND
         BNE   CHEKOP                   NO
         OI    OP,SUBCOMD               TELL EM SUBCOMD
         NI    OP,255-SCSAVED           TELL EM NEW SUBCOMMAND
         NI    OP,255-STILLSC           NOT CONTINUED SUBCMD
         B     SUBCMD                   GO SEPARATE
CHEKOP   CLC   0(2,R3),=C'))'           YES - OPERAND?
         BE    NOTDUMB                  YES - BRANCH
         CLC   0(2,R3),=C'  '           NO, CHECK FOR LACK OF NEATNESS
         BE    NOTDUMB                  VERY GOOD - BRANCH
         MVC   LINEIND(72),0(R3)  SOME DUMMY DIDN'T LEAVE 1ST 2 BLANK
         B     LISTER
NOTDUMB  MVC   LINEIND(70),2(R3)        YES, SET LINE WITH INDENTION
         B     LISTER
NORMAL   TM    OP,MAJ                   MAJOR SEPARATION???
         BNO   LISTER1                  NO....LIST IT
         MVC   LINEMAJ(70),2(R3)        MOVE IN DATA
         MVC   3(3,R10),=C'***'          INSERT HIGHLIGHTER
         MVC   18(3,R10),=C'***'         INSERT HIGHLIGHTER
         NI    OP,255-MAJ               TURN OFF SWITCH
         B     LISTER
LISTER1  MVC   LINEPL(70),2(R3)
LISTER   CP    CTRLIN,CTRMAX
         BNH   *+10
         ZAP   CTRLIN,=P'0'
         TM    OP,CMDSEP                NEED COMMAND NAME???
         BO    HEADALI                  YES...GO DO IT
         CP    CTRLIN,=P'0'
         BE    HEADING1                 GO SEE IF STILL SUBCOMMAND
REPORTD  AP    CTRLIN,=P'1'
         CLI   LINE,C'0'           DOUBLE SPACING?
         BNE   *+10                NO - BRANCH AROUND AP
         AP    CTRLIN,=P'1'
         TM    OP,SUBCOMD                IS IT SUBCOMMANDS???
         BNO   PUTIT                     NO....LIST ASIS
         NI    OP,255-SUBCOMD            TURN OFF SWITCH
         MVC   3(3,R10),=C'***'          INSERT HIGHLIGHTER
PUTIT    PUT   OUT,LINE
         MVC   LINE,LINE-1         CLEAR LINE
BXLE     BXLE  R3,R4,PROCESS       SCAN ENTIRE BLOCK
         B     READEM                   READ ANOTHER BLOCK
HEADING1 TM    OP,STILLSC               IS IT STILL SUBCOMMAND????
         BO    SUBCMD                   YES.....GO DO IT THAT WAY
HEADING  MVC   HEAD,HEAD-1              CLEAR IT
         NI    OP,255-STILLSC           NOT SUBCOMMAND ANYMORE
         MVI   HEAD,C'1'                SET TO EJECT
         MVC   HEAD+2(60),MRXID          TOP OF PAGE ID
         PUT   OUT,HEAD                 WRITE IT
HEADALI  AP    CTRLIN,=P'5'             1 EA. FOR HDR + HIGHLIGHTERS
         NI    OP,255-CMDSEP            TURN OFF COMMAND PRINT SW
* THE OTHERS ARE ALREADY TAKEN INTO ACCOUNT
         MVC   HEAD,HEAD-1              CLEAR LINE
         MVI   HEAD,C'0'                DOUBLE SPACE
         MVC   HEAD+35(10),=C'----------' COMMAND HIGHLIGHTER
         PUT   OUT,HEAD                 PUT IT
         MVI   HEAD,C' '                SET TO SINGLE SPACE
         MVC   HEAD+35(10),=C'          ' BLANK OUT HEADER
         OR    R6,R6                    ZERO OUT R6
         IC    R6,MEMOFFST              GET THE OFFSET FOR MOVE
         LA    R7,HEAD+36               PICK UP MOVE LOCATION
         LA    R7,0(R7,R6)              LOAD R7 W/LOCATION + OFFSET
         IC    R6,MEMLEN                PICK UP LENGTH TO MOVE
MOVEMEM  MVC   0(0,R7),MEMNAME          EXECUTED BELOW
         EX    R6,*-6                   MOVE IN THE MEMBER NAME
         PUT   OUT,HEAD                 PUT OUT CENTERED NAME
         MVC   HEAD+35(10),=C'----------' COMMAND HIGHLIGHTER
         PUT   OUT,HEAD                 PUT IT
         MVC   HEAD,HEAD-1              BLANK OUT HEAD
         MVI   HEAD,C'-'                SET TO TRIPLE SPACE
         PUT   OUT,HEAD                 WRITE BLANK HEADS
         B     REPORTD
MAJOR    NI    OP,255-OPINDENT          TURN OFF INDENTION
         MVI   CC,C'0'                  DOUBLE SPACE
         OI    OP,MAJ                   TURN ON SWITCH
         B     NORMAL
SUBCMD   TM    OP,STILLSC               IS IT STILL SUBCOMMAND???
         BO    SUBCMD1                  YES, LONG MEMBER
         CP    CTRLIN,=P'05'            ENOUGH ROOM FOR NEW ONE??
         BL    ADD5                     YES, GO GETEM
SUBCMD1  MVC   HEAD,HEAD-1              CLEAR IT
NEWPG    ZAP   CTRLIN,=P'0'             REINITIALIZE LINE COUNT
         MVI   HEAD,C'1'                SET TO EJECT
         MVC   HEAD+2(59),MRXID          TOP OF PAGE ID
         PUT   OUT,HEAD                 WRITE IT
ADD5     AP    CTRLIN,=P'5'             1 EA. FOR HDR + HIGHLIGHTERS
* THE OTHERS ARE ALREADY TAKEN INTO ACCOUNT
         MVC   HEAD,HEAD-1              CLEAR LINE
         MVI   HEAD,C'0'                DOUBLE SPACE
         MVC   HEAD+27(27),DASHES       COMMAND HIGHLIGHTER
         PUT   OUT,HEAD                 PUT IT
         MVC   HEAD,HEAD-1              CLEAR THE DASHES
         MVI   HEAD,C' '                SET TO SINGLE SPACE
         OR    R6,R6                    CLEAR IT
         MVI   HEAD+41,C'-'             PUT IN THE SEPARATOR
         LA    R7,HEAD+28               PICK UP LOCATION
         IC    R6,OFFSET                PICK UP THE OFFSET
         LA    R7,0(R6,R7)              POINT R7 TO LOCATION + OFFSET
         IC    R6,MEMLEN                PICK UP LENGTH TO MOVE
MEMMVC   MVC   0(0,R7),MEMNAME          EXECUTED BELOW
         EX    R6,*-6                   MOVE IN MEMNAME
* PRESUPPOSES THAT SUBCOMMAND HAS AN '=' SIGN IN FRONT OF IT
         TM    OP,SCSAVED               HAS SUBCOMMAND BEEN SAVED???
         BNO   R3MOVE                   IF NOT GET OFF OF R3
         MVC   HEAD+44(8),SCSAVE        HAS BEEN SAVED....GET IT
         B     DONEMOVE                 GO AROUND R3 MOVE
R3MOVE   MVC   HEAD+44(8),1(R3)         MOVE IN SUBCOMMAND NAME
         MVC   SCSAVE(8),1(R3)          SAVE THE SUBCOMMAND NAME
         OI    OP,SCSAVED               AND TURN ON THE FLAG
DONEMOVE LA    R14,HEAD+44              SET UP FOR '=' CHECK
         LA    R15,8                    LENGTH TO CHECK
SUBCX    CLI   0(R14),C'='              IS IT AN '=' SIGN
         BE    SUBCX1                   YES....GO BLANK IT
         LA    R14,1(R14)               BUMP TO NEXT CHAR
         BCT   R15,SUBCX                GO CHECK FURTHER
SUBCX1   MVC   0(9,R14),=C'         '   MOVE IN BLANKS
         PUT   OUT,HEAD                 WRITE IT
         MVC   HEAD+27(27),DASHES       HIGHLIGHT IT
         PUT   OUT,HEAD                 WRITE EM
         MVC   HEAD,HEAD-1              BLANK IT
         MVI   LINE,C'-'                SET FOR TRIPLE SPACE
         OI    OP,STILLSC               SET SUBCOMMAND HEADER SWITCH
         TM    OP,SUBCOMD               LONG MEMBER OR NEW SUBCOMMAND?
         BNO   REPORTD                  LONG MEMBER...LINE PRESENT
         NI    OP,255-SUBCOMD           TURN OFF THE SWITCH
         B     BXLE                     GO GET SOME MORE
INDENT   OI    OP,OPINDENT              TURN ON INDENTION
         MVI   CC,C'0'                  DOUBLE SPACE
         OI    OP,MAJ                   TURN ON SWITCH
         B     NORMAL
FINI     DS    0H
         CLOSE (OUT,,HELP,,IN)
         L     13,4(13)
         LM    14,12,12(13)
         SR    R15,R15
         BR    R14
         SPACE 2
BADNAME  DS    0H
         MVC   LINE+10(L'ENAME),ENAME   INVALID MEMBER NAME
         B     MSGOUT
IOERROR  DS    0H
         MVC   LINE+10(L'EIO),EIO       I/O ERROR
MSGOUT   MVC   LINE+1(8),MEMNAME        PUT IN MEMBER NAME
         MVI   CC,C'1'                  EJECT A PAGE
         PUT   OUT,LINE                 PRINT ERROR MSG
         MVC   LINE,LINE-1              CLEAR OUT LINE
         B     CNTRL
INDICATE EQU   *                        ALIAS PROCESSING
         MVC   HEAD,HEAD-1              CLEAR LINE
         TM    OP,ALIAS                 BEEN HERE BEFORE???
         BNO   INDIC1                   NO, FORCE HEADING
         MVI   HEAD,C'0'                DOUBLE SPACE
         B     INDIC2                   NO HEADING
INDIC1   MVI   HEAD,C'1'                FORCE HEADING
         NI    OP,255-STILLSC           NOT SUBCMD ANYMORE
         MVC   HEAD+2(59),MRXID         INSTALLATION CODE
         PUT   OUT,HEAD
         MVC   HEAD,HEAD-1
         MVI   HEAD,C'0'                DOUBLE SPACE
         ZAP   CTRLIN,=P'0'             RESET LINE COUNT
         AP    CTRLIN,=P'1'
INDIC2   MVC   HEAD+27(27),DASHES       HIGHLITE
         PUT   OUT,HEAD
         MVC   HEAD,HEAD-1              CLEAR LINE
         MVI   HEAD,C' '                SINGLE SPACE
         MVC   HEAD+30(8),MEMNAME       MOVE IN MEMBER NAME
         MVC   HEAD+38(12),=C' IS AN ALIAS'
         PUT   OUT,HEAD
         MVC   HEAD,HEAD-1              CLEAR LINE
         MVC   HEAD+27(27),DASHES       HIGHLITE
         PUT   OUT,HEAD
         MVC   HEAD,HEAD-1              CLEAR LINE
         MVI   HEAD,C' '                SPACE
         PUT   OUT,HEAD
         AP    CTRLIN,=P'5'             MAKE LINECT CORRECT
         OI    OP,ALIAS                 SAY WE HAVE BEEN HERE
         OI    OP,CMDSEP                PRINT COMMAND NAME ALSO
         B     CNTRL                    GO GET NEXT MEMBER
         EJECT
         DS    0D
MEMNAME  DC    CL8'0'
REC      DC    CL80'0'
         DC    C'0'
LINE     DS    0CL133
CC       DC    CL1'0'
         DC    CL132'0'
LINEMAJ  EQU   LINE+6
LINEPL   EQU   LINE+8
LINEIND  EQU   LINEPL
         DC    CL1'0'
HEAD     DC    CL133'0'
ENAME    DC    C'- INVALID AS SPECIFIED'
EIO      DC    C'- I/O ERROR OCCURRED WHILE LOCATING NAME'
H9       DC    H'9'
DASHES   DC    27C'-'       SUBCOMMAND HIGHLIGHTER
MEMLEN   DC    B'0'
OFFSET   DC    B'0'
MEMOFFST DC    B'0'
OP       DC    B'0'
OPINDENT EQU   B'10000000'
OPCMDS   EQU   B'01000000'
STILLSC  EQU   B'00100000'  SUBCOMMAND HEADER FLAG
SCSAVED  EQU   B'00010000'  SUBCOMMAND HAS BEEN SAVED FLAG
ALIAS    EQU   B'00001000'  SPARE FLAG
MAJ      EQU   B'00000100'  MAJOR SEPARATION
SUBCOMD  EQU   B'00000010'  SUBCOMMAND SEPARATION
CMDSEP   EQU   B'00000001'  PRINT COMMAND NAME FLAG
SCSAVE   DC    D'0'         SUBCOMMAND SAVE AREA
BLKADDR  DC    F'0'
CTRMAX   DC    P'055'
CTRLIN   DC    P'000'
         SPACE
         SPACE
         PRINT NOGEN
         LTORG
         SPACE
         DS    0D
LSTSTRT  DC    X'0001000E'   TELL BLDL MAX NUMBER AND LENGTH
MEM1     DC    2F'0'
DUMMYFW  DC    F'0'
DUMMYHW  DC    H'0'
BLDLERR  ABEND 2222,DUMP     BLDL ERROR
OPENERR  ABEND 1111,DUMP     CAN'T OPEN
MRXID   DC C'                 SYNTEX  - TSO COMMAND LANGUAGE REFERENCE'
SAVEEM   DC    18F'0'        SAVE AREA
IN       DCB   DSORG=PS,MACRF=GM,EODAD=FINI,DDNAME=SYSIN
         SPACE
OUT      DCB DSORG=PS,MACRF=PM,RECFM=FA,BLKSIZE=133,DDNAME=SYSPRINT
         SPACE
HELP     DCB   DSORG=PO,MACRF=R,EODAD=CNTRL,DDNAME=SYSUT1
         END
*PCA  *  OBJ,LIST,NODECK,NOESD,NORLD
*PLINK*  DCBS,LIST,XREF
*1ALIA*
*2ALIA*
