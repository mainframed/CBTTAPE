PROC 0 DEBUG SMMODE DSN PORT CNT HEADER PAGEJECT FORMNAME FORMS -
       NUMBRING CARGCNTL FCBID OCLASS PL TM BM -
       PAGELEN TMARGIN BMARGIN OPTIONS
/* ------------------------------------------------------------------ */
/* VTAM PRINTER SUPPORT - RELEASE 4.1                                 */
/*   - UPDATED 13NOV84, WILLIAM SMITH, SYNTEX TECHNICAL SUPPORT       */
/*   . INITIAL RELEASE OF DIALOG MANAGER FRONT-END FOR VPSPRINT       */
/* ------------------------------------------------------------------ */
SET &NOMSG = NOEJMESS
IF &DEBUG = DEBUG THEN -
   DO
      CONTROL MSG NOFLUSH CONLIST SYMLIST
      SET &NOMSG =
   END
ELSE -
   CONTROL NOMSG NOFLUSH NOLIST
ISPEXEC VGET (PORT,SMMODE)
ISPEXEC DISPLAY PANEL(VPSPRINT)
IF &LASTCC = 8 THEN -
   DO
      ISPEXEC SETMSG MSG(VPS032)
      EXIT CODE(8)
   END
/* ------------------------------------------------------------------ */
BEGIN: -
   SET &COPIES = &STR(COPIES(&CNT))
/* ------------------------------------------------------------------ */
IF &HEADER Â¬=  THEN -
   SET &HEADER = &STR(NOHEADER )
ELSE -
   SET &HEADER =
/* ------------------------------------------------------------------ */
IF &FORMNAME =  THEN -
   SET &FORMS =
ELSE -
   SET &FORMS = &STR(FORMS(&FORMNAME) )
/* ------------------------------------------------------------------ */
   SET &NUMBRING = &STR(&NUMBRING)
/* ------------------------------------------------------------------ */
   SET &PAGEJECT = &STR(&PAGEJECT)
/* ------------------------------------------------------------------ */
IF &FCBID = THEN -
   SET &FCBID =
ELSE -
   SET &FCBID = &STR(FCB(&FCBID) )
/* ------------------------------------------------------------------ */
   SET &CLASS = &STR(CLASS(&OCLASS))
/* ------------------------------------------------------------------ */
IF &PL =  THEN -
   SET &PAGELEN =
ELSE -
   SET &PAGELEN = &STR(PAGELEN(&PL) )
/* ------------------------------------------------------------------ */
IF &TM =  THEN -
   SET &TMARGIN =
ELSE -
   SET &TMARGIN = &STR(TMARGIN(&TM) )
/* ------------------------------------------------------------------ */
IF &BM =  THEN -
   SET &BMARGIN =
ELSE -
   SET &BMARGIN = &STR(BMARGIN(&BM) )
/* ------------------------------------------------------------------ */
IF &OPTIONS =  THEN -
   SET &OPTIONS =
/* ------------------------------------------------------------------ */
IF &SMMODE =  Y THEN -
   DO
     SET &NOMSG =
     ISPEXEC CONTROL DISPLAY SM
   END
ELSE -
   ISPEXEC CONTROL DISPLAY LINE
VPSPRINT &DSN +
         &PORT +
         &NOMSG +
         &COPIES +
         &HEADER+
         &PAGEJECT +
         &FORMS+
         &NUMBRING +
         &CARGCNTL +
         &FCBID+
         &CLASS +
         &PAGELEN+
         &TMARGIN+
         &BMARGIN+
         &OPTIONS
IF &LASTCC = 0 THEN -
   DO
     ISPEXEC LOG     MSG(VPS034)
     ISPEXEC DISPLAY MSG(VPS031) CURSOR(DSN)
     IF &LASTCC = 8 THEN -
        EXIT
     ELSE -
     GOTO BEGIN
   END
ELSE -
   EXIT CODE(&LASTCC)
