WIZARD   TITLE 'DCMS SRM DATA DISPLAY ROUTINE'
************************************************
*                                              *
*  MODIFIED 9/10/80 BY J. LEMA AT MEMOREX CORP *
*  TO CORRECT 0C9 PROBLEM DUE TO REGISTER NOT  *
*  CLEARED PRIOR TO INSERT CHARACTER.          *
*  FIX ID IS JFL80254                          *
*                                              *
************************************************
*                                              *
*  MODIFIED 5/21/81 BY THE MYSTERY PROGRAMMER  *
*  TO CORRECT SCALING OF LSCT VARIABLES        *
*  FIX ID IS RDS81142                          *
*                                              *
*  MODIFIED 13OCT81 --- FLAGGED FJP/13OCT81    *
*     . SCALE DOWN THE RUA TO APPEAR RIGHT     *
*     . CHANGE EXIT PFK FROM R12 TO R3         *
*                                              *
* ----------------------------------------------------------------------
*
*  MODIFIED 02FEB84 --- WJS/SYNTEX TECHNICAL SUPPORT
*     . RE-ASSEMBLED/LINKED ON A SP/1.3.3 SYSTEM USING JBB1326 PRIVATE
*       MACROS USING ASSEMBLER-H (5668-962)
*     . ADDED THE FOLLOWING MACROS TO ELIMINATE MESSAGE "IEV044 -
*       UNDEFINED SYMBOL" IN MACRO IRARMCT:  IRAICSP, IRARSPL, IEFENFPM;
*       THIS CHANGE WAS INSTITUTED AFTER OPENING AN INCIDENT WITH SRM
*       DEVELOPMENT
*
* ----------------------------------------------------------------------
WIZARD   DCMSTART R,LV=4096,STAE=YES,IOCB=YES
         L     R3,CVTPTR
         USING CVT,R3
         L     R11,CVTOPCTP
         DROP  R3
         USING RMCNS,R11
*                                     DO STATIC DISPLAY ITEMS  LINE 1
         LA    R5,STATLINE
         MVC   0(79,R5),BLANKS
         L     R2,RMCTWMST
         LA    R2,WMSTID-WMST(R2)      SET
         MVC   0(L'IPSMSG,R5),IPSMSG      CURRENT
         MVC   L'IPSMSG(2,R5),0(R2)              IPS
*                                     DO STATIC DISPLAY ITEMS  LINE 2
         LA    R5,79(R5)
         MVC   0(79,R5),BLANKS
         MVC   0(79,R5),ERVTITL
         L     R2,RMPTCPU
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   5(3,R5),DECWORK4+1
         L     R2,RMPTIOC
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   13(3,R5),DECWORK4+1
         L     R2,RMPTERV
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   21(4,R5),DECWORK4
*                                     DO STATIC DISPLAY ITEMS  LINE 3
         LA    R5,79(R5)
         MVC   0(79,R5),BLANKS
         MVC   0(79,R5),RFCTITL
         L     R10,RMCTWMST
         USING WMST,R10
*        L     R2,WMSTIPC
*        ST    R2,DECWORK1
*        BAL   R14,@DECIMAL
*        MVC   06(02,R5),DECWORK4+1
*        MVC   09(01,R5),DECWORK4+3
         MVC   07(4,R5),WMSTIPC
         MVC   18(4,R5),WMSTIPI
         MVC   29(4,R5),WMSTIPM
*                                                          LINE 4
         LA    R5,79(R5)
         MVC   0(79,R5),BLANKS
*
         LA    R5,79(R5)
         MVC   0(79,R5),BLANKS
*                                                          LINE 5
         TM    @MODE,X'0F'
         BO    RUNSRMS
DOFLASH  MVI   @MODE,X'7F'
         B     RUNSRMS
STARTMD  MVI   @MODE,X'0F'
         B     RUNSRMS
RUNSRMS  EQU   *
         XC    IOCBWAIT(4),IOCBWAIT
         LA    R0,SCREEN
         LA    R1,DOMAINZ
         BAL   R14,@CLEAR
         LA    R5,LINE17           COPY
         LA    R2,5                    STATIC
         LA    R6,STATLINE                   LINES
STATLOOP EQU   *
         MVC   0(79,R5),0(R6)      ONTO
         LA    R5,79(R5)               CURRENT
         LA    R6,79(R6)                      SCREEN
         BCT   R2,STATLOOP
*
**
*
         LH    R7,RMCTDMNC
         L     R6,RMCTDMDT
         LA    R5,DOMAINZ
         MVC   0(79,R5),PAGETITL
         ST    R7,DECWORK1
         BAL   R14,@DECIMAL
         MVC   26(3,R5),DECWORK4+1
         LA    R4,13               ***LIMIT OF DOMAINS DISPLAYED
         CR    R4,R7
         BNL   DODOMAIN
         LR    R7,R4
DODOMAIN EQU   *
         LA    R5,79(R5)
         MVC   0(79,R5),DMNTITL
DMNLOOP  EQU   *
         LA    R5,79(R5)
         BAL   R15,DMNLINE
         LA    R6,DMDTLEN(R6)
         BCT   R7,DMNLOOP
         LA    R5,79(R5)
         MVI   0(R5),C' '
         MVC   1(79,R5),0(R5)
         LA    R4,1(R4)
         ST    R4,DMLINE
         LA    R5,DOMAINZ    +158
         LA    R6,RCTNAMES
*        LA    R3,11
*        LA    R4,RMCASRC
*WAPLOOP EQU   *
*        L     R2,0(R4)
*        ST    R2,DECWORK1
*        BAL   R14,@DECIMAL
*        MVC   48(22,R5),0(R6)
*        MVC   72(4,R5),DECWORK4
*        LA    R6,22(R6)
*        LA    R4,4(R4)
*        LA    R5,79(R5)
*        BCT   R3,SWAPLOOP
**       SH    R5,=H'79'        BACKUP OVER LAMONT CRANSTON
**       MVC   48(22,R5),0(R6)
*        LH    R2,RMCAINUS
*        ST    R2,DECWORK1
*        BAL   R14,@DECIMAL
*        MVC   72(4,R5),DECWORK4
*        LA    R5,79(R5)
*        LA    R6,22(R6)
*        MVC   48(22,R5),0(R6)
*        LH    R2,RMCACIUS
*        ST    R2,DECWORK1
*        BAL   R14,@DECIMAL
*        MVC   72(4,R5),DECWORK4
*        LA    R5,079(R5)
         MVC   48(28,R5),RCTTITL
         LA    R5,79(R5)
         MVC   48(28,R5),RCDTITL
         LA    R5,79(R5)
         XC    @LOW(16),@LOW
         SR    R2,R2
RCTLOOP  EQU   *
         LH    R2,RCCUICTL        U I C
         ST    R2,@LOW
         LH    R2,RCCUICTH
         ST    R2,@HI
         LH    R2,RCVUICA
         ST    R2,@AV
         L     R2,RCVUICC
         ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
         XC    @LOW(16),@LOW
         LH    R2,RCCCPUTL      C P U
         SRL   R2,4
         ST    R2,@LOW
         LH    R2,RCCCPUTH
         SRL   R2,4
         ST    R2,@HI
         LH    R2,RCVCPUA
         SRL   R2,4
         ST    R2,@AV
         L     R2,RCVCPUC
         ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
         XC    @LOW(16),@LOW
         LH    R2,RCCPTRTL       P A G E
         ST    R2,@LOW
         LH    R2,RCCPTRTH
         ST    R2,@HI
         LH    R2,RCVPTR
         ST    R2,@AV
         L     R2,RCVBPTCT
         ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
         XC    @LOW(16),@LOW
         LH    R2,RCCASMTL       A S M Q
         ST    R2,@LOW
         LH    R2,RCCASMTH
         ST    R2,@HI
         LH    R2,RCVASMQA
         ST    R2,@AV
         L     R2,RCVASMQ
         ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
         XC    @LOW(16),@LOW      DEMAND PAGE
         LH    R2,RCCDPRTL
         ST    R2,@LOW
         LH    R2,RCCDPRTH
         ST    R2,@HI
         LH    R2,RCVDPR
         ST    R2,@AV
         L     R2,RCVBDPCT
         ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
         XC    @LOW(16),@LOW      CPU + DEMAND PAGE
*        LH    R2,RCCCPUPL
*        ST    R2,@LOW
*        LH    R2,RCCCPUPL
*        ST    R2,@HI
*        LH    R2,RCVCPUA
*        ST    R2,@AV
*        L     R2,RCVCPUC
*        ST    R2,@CT
         LH    R2,RCCCPUPL      C P U
         SRL   R2,4
         ST    R2,@LOW
         LH    R2,RCCCPUPH
         SRL   R2,4
         ST    R2,@HI
         LH    R2,RCVCPUA
         SRL   R2,4
         ST    R2,@AV
         L     R2,RCVCPUC
         ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
         XC    @LOW(16),@LOW      PAGE DELAY WITH DEMAND PAGING
         LH    R2,RCCMSPTL
         ST    R2,@LOW
         LH    R2,RCCMSPTH
         ST    R2,@HI
         LH    R2,RCVMSPP
         ST    R2,@AV
*        L     R2,RCVCPUC
*        ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
         XC    @LOW(16),@LOW      PAGE DELAY
         LH    R2,RCCPDLTL
         ST    R2,@LOW
         LH    R2,RCCPDLTH
         ST    R2,@HI
         LH    R2,RCVMSPP
         ST    R2,@AV
*        L     R2,RCVCPUC
*        ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
         LH    R2,RCCTOTUT
         ST    R2,@LOW
         LH    R2,RCCLCHUT
         ST    R2,@HI
         LH    R2,RCCLCHRR
         ST    R2,@AV
         LH    R2,RCVTOTDF
         ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
         XC    @LOW(16),@LOW      A V Q
*        LH    R2,RCCPDLTL
*        ST    R2,@LOW
*        LH    R2,RCCPDLTH
*        ST    R2,@HI
         LH    R2,RCVAFQC
         ST    R2,@AV
         LH    R2,RCVAVQC
         ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,LSCTNAME
         LA    R5,79(R5)
         LA    R5,79(R5)
         LA    R5,79(R5)
LSCTLOOP EQU   *
         MVC   48(28,R5),LSTTITL
         LA    R5,79(R5)
         MVC   48(28,R5),LSCTITL
         LA    R5,79(R5)
         XC    @LOW(16),@LOW
         LH    R2,LSCTUCTL        U I C
         ST    R2,@LOW
         LH    R2,LSCTUCTH
         ST    R2,@HI
         LH    R2,RCVUICA                                  RDS81142
         ST    R2,@AV                                      RDS81142
*        L     R2,RCVUICC
*        ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
         XC    @LOW(16),@LOW
         LH    R2,LSCTASTL        A S M
         BCT   R2,*+4              DCR BY 1 FOR PRINT      RDS81142
         ST    R2,@LOW
         LH    R2,LSCTASTH
         BCT   R2,*+4              DCR BY 1 FOR PRINT      RDS81142
         ST    R2,@HI
         LH    R2,RCVASMQA                                 RDS81142
         ST    R2,@AV                                      RDS81142
*        L     R2,RCVUICC
*        ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
         XC    @LOW(16),@LOW
         LH    R2,LSCTAFQL        A F Q
         ST    R2,@LOW
         LH    R2,LSCTAFQH
         ST    R2,@HI
         LH    R2,RCVAFQA                                  RDS81142
         ST    R2,@AV                                      RDS81142
*        L     R2,RCVUICC
*        ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
         XC    @LOW(16),@LOW
         L     R2,LSCTMTEL        T H I N K
         BAL   R15,LSSCALE   SCALE TO 1/10 SEC.            RDS81142
         ST    R2,@LOW
         L     R2,LSCTMTEH
         BAL   R15,LSSCALE   SCALE TO 1/10 SEC.            RDS81142
         ST    R2,@HI
         L     R2,LSCTMTE
         BAL   R15,LSSCALE   SCALE TO 1/10 SEC.            RDS81142
         ST    R2,@AV
*        L     R2,LSCTMTED
*        ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
         XC    @LOW(16),@LOW
         L     R2,LSCTCNT         N U M B E R
         ST    R2,@LOW
*        L     R2,LSCTMTEH
         ST    R2,@HI
*        L     R2,LSCTMTE
         ST    R2,@AV
*        L     R2,LSCTMTED
*        ST    R2,@CT
         BAL   R15,XLINE
         MVC   48(8,R5),0(R6)
         LA    R6,8(R6)
         LA    R5,79(R5)
*-------       END OF LSCT DISPLAY
         B     NEXTPAGE                                    RDS81142
         SPACE 5                                           RDS81142
LSSCALE  EQU   *           SCALE THINK TIME TO TENTH SECS. RDS81142
         LTR   R2,R2       SKIP IF ZERO                    RDS81142
         BZR   R15                                         RDS81142
         L     R3,=F'100'  MILLISECONDS TO 1/10 SEC.       RDS81142
         SR    R8,R8                                       RDS81142
         LR    R9,R2                                       RDS81142
         A     R9,=F'50'   ROUND OFF                       RDS81142
         DR    R8,R3                                       RDS81142
         LR    R2,R9                                       RDS81142
         BR    R15                                         RDS81142
NEXTPAGE CLI   @MODE,X'7F' .       IS IT FLASH MODE?       RDS81142
         BNE   SRMPUT1 .           IF NOT, GO TO R/W
SRMPUT0  EQU   *
         MVI   @MODE,X'7F'
         OI    IOCBFLGD,IOCBDYON
         OI    IOCBFLGC,IOCBCCLR+IOCBCRAO+IOCBCFRA
         TRMIO IOCB,WRITE,FORMAT=SCREEN,IMAGE=DOMAINZ
         PFKEY DEF=NO,ATTN=SRMEXIT,ENTER=RUNSRMS,        FJP/13OCT81   X
               K9=RUNSRMS,K3=SRMEXIT,K12=STARTMD         FJP/13OCT81
         B     RUNSRMS
SRMPUT1  EQU   *
         MVI   @MODE,X'0F'
         NI    IOCBFLGD,255-IOCBDYON
         OI    IOCBFLGC,IOCBCCLR+IOCBCRAO+IOCBCFRA
         TRMIO IOCB,FORMAT=SCREEN,IMAGE=DOMAINZ
         PFKEY DEF=DCMSOVER,ATTN=SRMEXIT,ENTER=RUNSRMS,  FJP/13OCT81   X
               K9=DCMSOVER,K3=SRMEXIT,K12=DOFLASH        FJP/13OCT81
         EJECT
DMNLINE  EQU   *
         USING DMDT,R6
         SR    R2,R2
         IC    R2,DMDTNO
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   0(3,R5),DECWORK4+1
         IC    R2,DMDTLO
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   4(3,R5),DECWORK4+1
         IC    R2,DMDTHI
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   08(3,R5),DECWORK4+1
         IC    R2,DMDTWT
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   12(3,R5),DECWORK4+1
         LH    R2,DMDTMPLT
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   16(3,R5),DECWORK4+1
         LH    R2,DMDTRUA
         LA    R2,8(,R2)           ADD FOR ROUNDING  FJP/13OCT81
         SRL   R2,4                DIVIDE BY 16      FJP/13OCT81
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   20(3,R5),DECWORK4+1
         LH    R2,DMDTGOOU
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   24(3,R5),DECWORK4+1
         LH    R2,DMDTCMPL
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   28(3,R5),DECWORK4+1
         LH    R2,DMDTOUTU
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   32(3,R5),DECWORK4+1
         LH    R2,DMDTINCU
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   36(3,R5),DECWORK4+1
         MVC   DECWORK4(4),=CL4'   0'
         SR    R1,R1             CLEAR R1 FOR IC  -- JFL80254
         IC    R1,DMDTWT        CURRENT WEIGHT
         MH    R1,DMDTRUA        TIMES
         SR    R0,R0             READY USER AVG
         LH    R2,DMDTMPLT       DIVIDED BY
         LTR   R2,R2
         BZ    CCINODR
         DR    R0,R2             MPL TARGET
         ST    R1,DECWORK1       EQUALS
         BAL   R14,@DECIMAL      CONTENTION INDEX
CCINODR  MVC   40(3,R5),DECWORK4+1
         MVI   45(R5),C'|'
         BR    R15
         EJECT
XLINE    EQU   *
         L     R2,@LOW
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   56(4,R5),DECWORK4
         L     R2,@HI
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   61(4,R5),DECWORK4
         L     R2,@AV
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   66(4,R5),DECWORK4
         L     R2,@CT
         ST    R2,DECWORK1
         BAL   R14,@DECIMAL
         MVC   72(4,R5),DECWORK4
         BR    R15
         EJECT
IPSMSG   DC    C' CURRENT IPS =  '
INITINT  DC  F'500'      .      FIVE SECOND INTERVAL
SCREEN   SFMT
DOMAINZ  FIELD 79
LINE2    FIELD 79
LINE3    FIELD 79
LINE4    FIELD 79
LINE5    FIELD 79
LINE6    FIELD 79
LINE7    FIELD 79
LINE8    FIELD 79
LINE9    FIELD 79
LINE10   FIELD 79
LINE11   FIELD 79
LINE12   FIELD 79
LINE13   FIELD 79
LINE14   FIELD 79
LINE15   FIELD 79
LINE16   FIELD 79
LINE17   FIELD 79
LINE18   FIELD 79
LINE19   FIELD 79
LINE20   FIELD 79
LINE21   FIELD 79
LINE22   FIELD 79
LINE23   FIELD 79,INTEN=LO,CURSOR=AFTER
         SFEND
PAGETITL DS    0CL79
         DC    CL40'DOMAIN TABLE (DMDT)       NNN DOMAINS   '
         DC    CL40'     |  SWAP ANALYSIS DATA    (RMCA)    '
DMNTITL  DS    0CL79
         DC    CL40'DMN MIN MAX WT MPLT RUA GOU CMP OUT INC '
         DC    CL40'CCI  |  SWAP REASONS          COUNTS  '
ERVTITL  DS    0CL79
         DC    CL79' CPU=NNN I/O=NNN ENQ=NNN   '
RFCTITL  DS    0CL79
         DC    CL79' CPU = NN.N I/O = NN.N MSO = NN.N '
RCDTITL  DS    0CL30
         DC    CL30'VARIABLE LOW HIGH AVG  COUNT  '
RCTTITL  DS    0CL30
         DC    CL30'RESOURCE CONTROL TABLE (RCT) '
LSCTITL  DS    0CL30
         DC    CL30'VARIABLE LOW HIGH AVG         '
LSTTITL  DS    0CL30
         DC    CL30'LOGICAL SWAP CONTROL  (LSCT) '
RCTNAMES DS    0C
         DC    CL8'U I C   '
         DC    CL8'C P U   '
         DC    CL8'P A G E '
         DC    CL8'A S M Q '
         DC    CL8'DEMAND  '
         DC    CL8'DMD+CPU '
         DC    CL8'PGDLY+DM'
         DC    CL8'PGDELAY '
         DC    CL8'L C H   '
         DC    CL8'A V Q   '
         DC    CL8'        '
LSCTNAME DS    0C
         DC    CL8'UIC     '
         DC    CL8'ASMQ    '
         DC    CL8'AFQ     '
         DC    CL8'THINK   '
         DC    CL8'# LS    '
         DC    CL8'        '
REASONS  DS    0C
         DC    CL22'TERMINAL INPUT    (01)'
         DC    CL22'TERMINAL OUTPUT   (02)'
         DC    CL22'LONG WAIT         (03)'
         DC    CL22'AUX STG SHORTAGE  (04)'
         DC    CL22'REAL STG SHORTAGE (05)'
         DC    CL22'DETECTED WAIT     (06)'
         DC    CL22'REQUESTED         (07)'
         DC    CL22'CAP: ENQ EXCHANGE (08)'
         DC    CL22'CAP: WLM EXCHANGE (09)'
         DC    CL22'CAP: UNILATERAL   (0A)'
         DC    CL22'ONLY THE SHADOW KNOWS '
         DC    CL22'IN CORE USERS         '
         DC    CL22'OUTREADY USERS        '
SRMEXIT  LINK  EP=CLR3270                                   FJP/13OCT81
         B     SRMEXIT1   B FOR DCMSTOP ALIGNMENT           FJP/13OCT81
SRMEXIT1 DCMSTOP RC=(15)                                    FJP/13OCT81
         DSGEN  SCREEN
DMLINE   DS    F .
@LOW      DC   F'0'
@HI       DC   F'0'
@AV       DC   F'0'
@CT       DC   F'0'
@MODE    DS   X .
STATLINE DS    5CL79
         EJECT
         CVT   DSECT=YES
         EJECT
RMCNS    RMCNS
         EJECT
DMDT     IRADMDT
         EJECT
WAMT     IRAWAMT
         EJECT
WMST     IRAWMST
         EJECT
ICSP     IRAICSP
         EJECT
RSPL     IRARSPL
         EJECT
ENFPM    IEFENFPM
         END
