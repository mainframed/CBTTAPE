***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'FLUSHSP' --- FLUSH BUFFERED SPACES  *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY   FLOFFSP  IS COUNT OF SPACES TO LEAVE
*                   R0 = 0   DO ALL SPACES
*                   R0 Â¬ 0   DO ONLY TRIPLE SPACES
*                   FLOFFCH  CHANNEL SKIP VALUE
         SPACE
FLUSHSP $ENTER ,
         LR    X1,R0               RETAIN PARM VALUE
         L     TEMP,FLOFFCH        INITIAL CHANNEL SKIP
         LTR   TEMP,TEMP           ANY PRESENT .Q
         BZ    FLSP01              BRANCH IF NONE
         STC   TEMP,FLBLNKCC       INSERT CARRIAGE CONTROL SKIP
         OI    FLBLNKCC,C'0'       CHANGE TO CHARACTER
         LA    R0,FLBLNK           POINT TO BLANK LINE
        $CALL  PUTPRT              PRINT THE CHANNEL SKIP
         XC    FLOFFCH,FLOFFCH     CLEAR CHANNEL SKIP
         L     COUNT,FLOFFSP       CURRENT SPACES TO LEAVE
         AH    COUNT,PLZ           PLUS LINE LENGTH
         S     COUNT,DEFFFTO+TAB3DEF     LESS POSITION ON PAGE
         ST    COUNT,FLOFFSP       UPDATE SPACES TO LEAVE
         SPACE
FLSP01   DS    0H
         L     COUNT,FLOFFSP       GET SPACES TO LEAVE
         LTR   COUNT,COUNT         ANY LINES TO DO .Q
         BNP   FLSP05              BRANCH IF NONE AT ALL
         SPACE
FLSP02   DS    0H
         LA    TEMP,3              MAXIMUM PRINTER SPACE
         CR    COUNT,TEMP          LESS THAN THAT TO DO .Q
         BNL   FLSP03              BRANCH IF NOT
         LTR   X1,X1               DO ALL SPACES .Q
         BNZ   FLSP04              BRANCH IF NOT
         LR    TEMP,COUNT          SET FEWER SPACES
FLSP03   DS    0H
         SR    COUNT,TEMP          LESS NUMBER WE DO NOW
         IC    TEMP,SPACTABL(TEMP) GET ASA CARRIAGE CONTROL
         STC   TEMP,FLBLNKCC       INSERT CARRIAGE CONTROL
         LA    R0,FLBLNK           POINT TO BLANK LINE
        $CALL  PUTPRT              PRINT THE BLANK LINE
         LTR   COUNT,COUNT         MORE LINES LEFT .Q
         BP    FLSP02              BRANCH BACK IF YES
FLSP04   DS    0H
         ST    COUNT,FLOFFSP       UPDATE SPACES LEFT OUTSTANDING
         SPACE
FLSP05   DS    0H
        $EXIT  RC=0                AND RETURN TO CALLER
