***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'GETQST' --- GET QUOTED STRING       *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER:   $CALL  GETQST                                    *
*                                                                     *
*        CC=BOMIT  IF ARG IS OMITTED   (COUNT=1)                      *
*        CC=BDNO   IF MISMATCHED QUOTE (COUNT=ADDRESS)                *
*                                      (TEMP=LENGTH-1)                *
*        CC=BPOS   IF STRING OR        (COUNT=ADDRESS)                *
*                  QUOTED STRING       (TEMP=LENGTH-1)                *
*                                                                     *
*        QSTRING = F'0'                UNQUOTED STRING                *
*                = F'1'                MISMATCHED QUOTED STRING       *
*                = F'2'                QUOTED STRING                  *
         SPACE
GETQST  $ENTER ,
         LA    R0,0                DELIMITER COUNT
         ST    R0,QSTRING          ASSUME UNDELIMITED STRING
        $CALL  GETARG              GET START OR ALL OF STRING
         BC    BOMIT,GETQST05      BRANCH IF OMITTED OPERAND
         SPACE
         LR    X1,COUNT            POINT TO OPERAND
         CLI   0(X1),C' '          TOO SMALL FOR DELIMITER .Q
         BNH   GETQST04            BRANCH IF YES
         CLI   0(X1),C'A'-C' '-1   TOO BIG FOR DELIMITER .Q
         BNL   GETQST04            BRANCH IF YES
         TRT   0(1,X1),GETQSTTB    IS IT A POSSIBLE DELIMITER .Q
         BZ    GETQST04            BRANCH IF NOT
         SPACE
         LR    R1,X1               START OF DELIMITED STRING
GETQST01 DS    0H
         LA    R1,1(,R1)           BUMP DOWN OPERAND STRING
         C     R1,ARGSTOP          PASSED THE END .Q
         BH    GETQST03            MISMATCHED DELIM IF YES
         CLC   0(1,R1),0(X1)       ENDING DELIMITER .Q
         BNE   GETQST01            CONTINUE SEARCH IF NOT
         C     R1,ARGSTOP          AT THE END .Q
         BNL   GETQST02            GOT ARGUMENT IF YES
         CLI   1(R1),C' '          DELIMITER FOLLOWED BY BLANK .Q
         BNE   GETQST01            NO, KEEP SCANNING
GETQST02 DS    0H
         LA    R0,2                DELIMITER COUNT
         ST    R0,QSTRING          SET DELIMITER COUNT
         LR    TEMP,R1             LAST CHARACTER
         SR    TEMP,X1             LESS FIRST GIVES LENGTH-1
         LA    R1,1(,R1)           NEXT CHAR TO SCAN
         ST    R1,ARGSTART         ADDR OF START FOR NEXT CALL
         B     GETQST04            SUCCESSFUL RETURN
         SPACE
GETQST03 DS    0H
         LA    R0,1                DELIMITER COUNT
         ST    R0,QSTRING          SET DELIMITER COUNT
         LR    COUNT,X1            POINT TO RESULT
        $CC    DNO                 INDICATE ERROR CONDITION
         B     GETQST05            AND GO TO RETURN
         SPACE
GETQST04 DS    0H
         LR    COUNT,X1            POINT TO RESULT
        $CC    POS                 SET FOR SUCCESS
         SPACE
GETQST05 DS    0H
        $RESTOR KEEP=(COUNT,TEMP)  RESTORE CALLER'S REGISTERS
         BR    RETURN              RETURN TO CALLER
         SPACE
         ORG   *-C' '
GETQSTTB DS    0CL256              DELIMITER TABLE
         ORG   GETQSTTB+C' '
         DC    64X'00'                  INITIAL SETTING
         ORG   GETQSTTB+C''''
         DC    C''''                    SINGLE QUOTE
         ORG   GETQSTTB+C'"'
         DC    C'"'                     DOUBLE QUOTE
         ORG   GETQSTTB+C'/'
         DC    C'/'                     SLASH
         ORG   GETQSTTB+C'|'
         DC    C'|'                     OR BAR
         ORG   GETQSTTB+C'!'
         DC    C'!'                     EXCLAMATION MARK
         ORG   GETQSTTB+C'¬'
         DC    C'¬'                     NOT SIGN
         ORG   GETQSTTB+C'¢'
         DC    C'¢'                     CENT SIGN
         ORG   ,
