***********************************************************************
*                                                                     *
*              'BXHORIZ' INTERNAL ROUTINE --- PROCESS TOPS AND BOTTOMS*
*                                                                     *
***********************************************************************
         SPACE
BXHORIZ $ENTER ,
         CLI   ICORFLG,ICORNONE    NOT FORMATTING INTO CORE .Q
         BNE   BXH05               BRANCH IF TRUE
BXH01    DS    0H
        $IFON  SWITCHS,FEMPTY,BXH02     BRANCH IF PAGE EMPTY
         LH    TEMP,PLCT           LINES LEFT ON PAGE
         BCTR  TEMP,0              DECREMENT BY ONE
         LH    R1,BMMRG            BOTTOM MARGIN
         AH    R1,FNSEP            PLUS FOOTNOTE MARGIN
         CR    TEMP,R1             BELOW BOTTOM MARGIN .Q
         BNL   BXH03               BRANCH IF NOT
        $CALL  PAGE                YES, FORCE PAGE EJECT
BXH02    DS    0H
        $CALL  PAGETOP             GET NEXT PAGE STARTED
         B     BXH01               AND TRY AGAIN
BXH03    DS    0H
         CLC   PLCT,RMTRIG         REMOTE SET FOR HERE .Q
         BNE   BXH04               BRANCH IF NOT
         LH    R1,RMTRIG           REMOTE NUMBER TO IMBED
        $CALL  RMIMBED             AND TRIGGER SAME FIRST
         B     BXH01               NOW START AGAIN
         SPACE
BXH04    DS    0H
         STH   TEMP,PLCT           SET NEW LINES REMAINING
BXH05    DS    0H
         L     X3,WRKBUFFA         WORKING BUFFER ADDRESS
         MVC   0(L'WRKBUFF,X3),BLANKS   CLEAR BUFFER
         LA    X3,1(,X3)           AND POINT TO TARGET
         SPACE
         L     X1,BXPTR            DEFINING HORIZ BITS
         USING BXAREA,X1           AREA LOOKS LIKE THIS
         LH    COUNT,BXBLKLN       GET LENGTH OF DATA
         LA    X1,BXDATA           POINT TO DATA
         DROP  X1
         LA    X2,BXTRM            ASSUME ONLINE OUTPUT
        $IFON  TTYPE,TONLINE,BXH06 BRANCH IF TRUE
        $IFON  SWITCHS,TROSWS,BXH06     BRANCH IF UPPER TRANSLATE
         LA    X2,BXPRT            'TIS OFFLINE OUTPUT
BXH06    DS    0H
         S     COUNT,F1            DECREMENT LENGTH LEFT
         BM    BXH10               BRANCH OUT IF DONE
         IC    R1,0(,X1)           GET CHAR BITS
         N     R1,F15              PURIFY RESULT
         AR    R1,R1               DOUBLE IT
         LA    R1,0(R1,X2)         POINT TO CHAR EQUIVALENT
         MVC   0(1,X3),0(R1)       ENTER CHAR
        $IFOFF SWITCHS,TRSWS,BXH07 BRANCH IF NO TRANSLATE
         L     TEMP,TRANADO        OUTPUT TRANS TABLE ADDRESS
         TR    0(1,X3),0(TEMP)     CONVERT SINGLE CHAR
BXH07    DS    0H
         CLI   1(R1),C' '          SECOND CHAR BLANK .Q
         BE    BXH09               BRANCH IF YES
         MVI   1(X3),X'16'         ENTER BACKSPACE
         MVC   2(1,X3),1(R1)       ENTER SECOND CHAR
        $IFOFF SWITCHS,TRSWS,BXH08 BRANCH IF NO TRANSLATE
         TR    2(1,X3),0(TEMP)     CONVERT SECOND CHAR
BXH08    DS    0H
         LA    X3,2(,X3)           BUMP OUTPUT POINTER
BXH09    DS    0H
         LA    X1,1(,X1)           BUMP INPUT POINTER
         LA    X3,1(,X3)           BUMP OUTPUT POINTER
         B     BXH06               DO IT AGAIN
BXH10    DS    0H
         L     R1,WRKBUFFA         STARTED HERE
         SR    X3,R1               LENGTH OF RESULT
         L     X1,BXPTR            RETAIN BOX ANCHOR
         XC    BXPTR,BXPTR         CLEAR FOR PRINT REQUEST
         MVC   HDPTRSAV,FMTOLD1    SAVE LINK PTRS/LOCAL SWITCHES
        $OFF   SWITCHS,JUADSWS     SOME DON'T COUNT
         ST    X3,NOCHARS          SETUP AND CALL LINKPUT
         L     R0,WRKBUFFA         RECORD POINTER
         ST    R0,OUTPARM          IN COMMON AREA
         XC    OLDFIRST(PARMPUT-OLDFIRST),OLDFIRST   NO OLD DATA
         LA    R1,OUTPARM          COMMON DATA AREA
        $CALL  LKPUT               BUILD CHARACTER BLOCKS
         L     COUNT,NOCHARS       CURRENT CHAR COUNT
         BCTR  COUNT,0             LESS ONE
         ST    COUNT,NOCHARS       UPDATE
        $ON    SWITCHS,HDPSWS      PRINT HORIZONTAL BOX LINE
        $CALL  PRINT               PRINT THE LINKS
        $OFF   SWITCHS,HDPSWS      CLEAR HEADING SWITCH
        $MVC   FMTOLD1,HDPTRSAV    RESTORE LINK PTRS/LOCAL SWITCHES
         ST    X1,BXPTR            RESTORE BOX ANCHOR
        $CALL  RMNEXT              SET UP FOR NEXT REMOTE
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
BXTRM    DS    0CL32               ONLINE TERMINAL BOX CHARACTERS
         DC    C'  '                    0 - BLANK
         DC    C'- '                    1 - RIGHT DASH
         DC    C'- '                    2 - LEFT DASH
         DC    C'- '                    3 - DASH
         DC    C'| '                    4 - DOWN BAR
         DC    C'+ '                    5 - DOWN AND RIGHT CORNER
         DC    C'+ '                    6 - DOWN AND LEFT CORNER
         DC    C'+ '                    7 - DOWN AND ACROSS INTERSECT
         DC    C'| '                    8 - UP BAR
         DC    C'+ '                    9 - UP AND RIGHT CORNER
         DC    C'+ '                   10 - UP AND LEFT CORNER
         DC    C'+ '                   11 - UP AND ACROSS INTERSECT
         DC    C'| '                   12 - BAR
         DC    C'+ '                   13 - DOWN AND RIGHT INTERSECT
         DC    C'+ '                   14 - DOWN AND LEFT INTERSECT
         DC    C'+ '                   15 - INTERSECTION
         SPACE
BXPRT    DS    0CL32               OFFLINE PRINTER CHARACTERS
         DC    X'4040'                  0 - BLANK
         DC    X'BF40'                  1 - RIGHT DASH
         DC    X'BF40'                  2 - LEFT DASH
         DC    X'BF40'                  3 - DASH
         DC    X'4F40'                  4 - DOWN BAR
         DC    X'AC40'                  5 - DOWN AND RIGHT CORNER
         DC    X'BC40'                  6 - DOWN AND LEFT CORNER
         DC    X'BFBC'                  7 - DOWN AND ACROSS INTERSECT
         DC    X'4F40'                  8 - UP BAR
         DC    X'AB40'                  9 - UP AND RIGHT CORNER
         DC    X'BB40'                 10 - UP AND LEFT CORNER
         DC    X'BFAB'                 11 - UP AND ACROSS INTERSECT
         DC    X'4F40'                 12 - BAR
         DC    X'4FAB'                 13 - DOWN AND RIGHT INTERSECTION
         DC    X'4FBB'                 14 - DOWN AND LEFT INTERSECTION
         DC    X'4FBF'                 15 - INTERSECTION
*                                           ALSO X'8F40' BUT FOR ALA
