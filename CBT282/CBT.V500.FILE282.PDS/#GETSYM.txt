***********************************************************************
*                                                                     *
*              INTERNAL FUNCTION 'GETSYM'                             *
*                                                                     *
***********************************************************************
         SPACE
*        TO ENTER:   $CALL  GETSYM                                    *
*                                                                     *
*        CC=BOMIT IF ARG IS OMITTED    (COUNT=1)                      *
*        CC=BPOS  IF ARG IS FOUND      (COUNT=ADDR)                   *
*                    (UNSUBSCRIPTED)   (TEMP=LENGTH-1)                *
*        CC=BNEG  IF ARG IS FOUND      (COUNT=ADDR)                   *
*                    (SUBSCRIPTED)     (TEMP=LENGTH-1)                *
*        CC=BDNO  IF INVALID SUBSCRIPT (COUNT=ADDR)                   *
*                 SUBSTRING OR SYMBOL TOO LONG                        *
         SPACE
GETSYM  $ENTER ,
         MVC   SRNM,BLANKS         CLEAR NAME BUFFER
         MVC   SRNMSUB,SRNMSUB+L'SRNMSUB     SET DEFAULT SUBSCRIPT
         MVC   SRNMFR,SRNMFR+L'SRNMFR        SET DEFAULT 'FROM' SUBSTR
         MVC   SRNMTO,SRNMTO+L'SRNMTO        SET DEFAULT 'TO' SUBSTR
         MVI   SRNMFLG,X'00'       CLEAR ALL SUBSCRIPT VALUE
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION PLEASE
        $CALL  GETARG              GET A STRING
         BC    BOMIT,GTSYM20       BRANCH IF NO OPERAND
         LR    R1,COUNT            START OF SCANNED OPERAND
         LA    RETURN,GTSYM04      END OF STRING EXIT
         CLI   0(R1),C'*'          THE BIG STRING .Q
         BNE   GTSYM01             BRANCH IF NOT
         LA    R1,1(,R1)           NEXT CHAR IS THE LAST
         BR    RETURN              TAKE DELIMITER EXIT
         SPACE
GTSYM01  DS    0H
         CLI   0(R1),C'A'-X'40'    OK FOR SURE .Q
         BNL   GTSYM03             BRANCH IF YES
        $IFON  SWITCHS,NUPSWS,GTSYM02   BRANCH IF NO CONVERSION
         CLI   0(R1),C'$'          A DOLLAR SIGN .Q
         BE    GTSYM03
         CLI   0(R1),C'#'          A HASH SIGN .Q
         BE    GTSYM03
         CLI   0(R1),C'@'          AN AT SIGN .Q
         BE    GTSYM03
         CLI   0(R1),C'_'          AN UNDERSCORE .Q
         BE    GTSYM03
         BR    RETURN              ELSE A REF NAME DELIMITER
GTSYM02  DS    0H
         CLI   0(R1),C' '          A BLANK DELIM .Q
         BER   RETURN
         CLI   0(R1),C'='          AN EQUAL DELIM .Q
         BER   RETURN
         CLI   0(R1),C'+'          A PLUS DELIM .Q
         BER   RETURN
         CLI   0(R1),C'-'          A MINUS DELIM .Q
         BER   RETURN
         CLI   0(R1),C'*'          A STAR DELIM .Q
         BER   RETURN
         CLI   0(R1),C'/'          A SLASH DELIM .Q
         BER   RETURN
         CLI   0(R1),C'.'          A DOT DELIM .Q
         BER   RETURN
         CLI   0(R1),C''''         A QUOTE DELIM .Q
         BER   RETURN
         CLI   0(R1),C'&&'         AN AMPERSAND DELIM .Q
         BER   RETURN
         CLI   0(R1),C')'          A RIGHT PAREN DELIM .Q
         BER   RETURN
         CLI   0(R1),C'('          A LEFT PAREN DELIM .Q
         BER   RETURN
GTSYM03  DS    0H
         LA    R1,1(,R1)           NEXT CHAR POINTER
         B     GTSYM01             SCAN IT NEXT
         SPACE
GTSYM04  DS    0H
         ST    R1,ARGSTART         SET START FOR NEXT ITEM
         LR    TEMP,R1             END PLUS ONE OF RESULT
         SR    TEMP,COUNT          LESS INITIAL CHARACTER
         S     TEMP,F1             LENGTH-1 OF RESULT
         BM    GTSYM20             NULL IF 1ST CHAR IS SPECIAL
         LA    R0,L'RNSYM-1        MAX LENGTH-1 OF REFERENCE SYMBOL
         CR    TEMP,R0             COMPARE WITH ACTUAL
         BNH   GTSYM05             BRANCH IF OK
         MVI   SRNMFLG,SRNMTSYM    SET SYMBOL ERROR FLAG
         B     GTSYM19             AND SET CC AND RETURN
GTSYM05  DS    0H
         EX    TEMP,GETSYMVC       MOVE IN SEARCH NAME
        $IFON  SWITCHS,NUPSWS,GTSYM06   BRANCH IF NO UPPER CASE
         OC    SRNM,BLANKS         UPPER CASE REFERENCE NAMES
GTSYM06  DS    0H
         CLI   0(R1),C'('          SUBSCRIPTED ITEM .Q
         BE    GTSYM07             BRANCH IF YES
        $CC    POS                 SET CC BPOS FOR SUCCESS
         B     GTSYM21             AND GO TO RETURN
         SPACE
GTSYM07  DS    0H
         LA    R1,1(,R1)           BUMP OVER LEFT PAREN
         CLI   0(R1),C')'          NULL SUBSCRIPT .Q
         BE    GTSYM11             BRANCH IF YES
         CLC   =C'*)',0(R1)        ALL SUBSCRIPT .Q
         BE    GTSYM10             BRANCH IF YES
         CLC   =C'*+)',0(R1)       ALL PLUS SUBSCRIPT .Q
         BE    GTSYM08             BRANCH IF YES
         CLC   =C'*-)',0(R1)       ALL MINUS SUBSCRIPT .Q
         BNE   GTSYM12             BRANCH OUT IF NOT
         OI    SRNMFLG,SRNMALLN    SET NEGATIVE SWITCH
         B     GTSYM09             GO BUMP PTRS
GTSYM08  DS    0H
         OI    SRNMFLG,SRNMALLP    SET POSITIVE SWITCH
GTSYM09  DS    0H
         LA    R1,1(,R1)           BUMP POINTER ONE
         LA    TEMP,1(,TEMP)       AND LENGTH-1 BY ONE
GTSYM10  DS    0H
         LA    R1,1(,R1)           BUMP POINTER BY ONE
         LA    TEMP,1(,TEMP)       AND LENGTH-1 BY ONE
GTSYM11  DS    0H
         LA    R1,1(,R1)           BUMP OVER ')'
         LA    TEMP,2(,TEMP)       BUMP TO LENGTH-1 OF STRING
         ST    R1,ARGSTART         SET START FOR NEXT TIME
         OI    SRNMFLG,SRNMALL     SET ALL FLAG
        $CC    NEG                 SET CC BNEG
         B     GTSYM21             AND RETURN
         SPACE
GTSYM12  DS    0H
         BAL   RETURN,GTSYMSS      GET SUBSCRIPT AND/OR SUBSTRING
         CLI   SRNMFLG,X'00'       ANY ERRORS .Q
         BNE   GTSYM19             BRANCH IF YES
         L     TEMP,ARGSTART       CONTINUE SCAN FROM HERE
         SR    TEMP,COUNT          LESS START POSITION
         BCTR  TEMP,0              GIVES LENGTH-1 OF RESULT
        $CC    NEG                 SET CC TO BNEG FOR SUBSCRIPT
         B     GTSYM21             GO TO RETURN
         SPACE
GTSYM19  DS    0H
        $CC    DNO                 RETURN ERROR COND CODE
         B     GTSYM21             BRANCH TO RETURN
         SPACE
GTSYM20  DS    0H
        $CC    OMIT                SET CC BOMIT FOR DON'T KNOW
GTSYM21  DS    0H
        $RESTOR KEEP=(COUNT,TEMP)  RESTORE CALLER'S REGS
         BR    RETURN              RETURN TO CALLER
         SPACE
GETSYMVC MVC   SRNM(*-*),0(COUNT)  COPIES SEARCH ARGUMENT
         SPACE
         LTORG
         SPACE 2
***********************************************************************
*                                                                     *
*              GTSYMSS  ---  SCAN SUBSCRIPT AND/OR SUBSTRING          *
*                                                                     *
***********************************************************************
         SPACE
GTSYMSS  DS    0H
        $SAVE  ,                   SAVE CALLER'S REGISTERS
         BAL   RETURN,GTSYMNUM     GET FIRST NUMBER
         BC    BOMIT+BDNO,GTSSE01  BRANCH IF INVALID
         B     *+4(R15)            BRANCH ON DELIMITER
         B     GTSSE01                  ERROR IF UNKNOWN
         B     GTSS01                   BRANCH TO SUBSCRIPT IF COMMA
         B     GTSS03                   BRANCH TO SUBSTRING IF COLON
         B     GTSS03                   BRANCH TO SUBSTRING IF OR BAR
         B     GTSS01                   BRANCH TO SUBSCRIPT IF ')'
         SPACE
GTSS01   DS    0H
         LPR   COUNT,COUNT         POSITIVE SUBSCRIPT VALUE
         C     COUNT,F32767        TOO BIG .Q
         BH    GTSSE01             BRANCH IF YES
         STH   COUNT,SRNMSUB       ENTER SUBSCRIPT VALUE
         B     *+4(R15)            BRANCH ON DELIMITER
         DC    AL4(0)                   QUIT IF UNKNOWN
         B     GTSS02                   BRANCH IF COMMA
         DC    AL4(0)                   QUIT IF COLON
         DC    AL4(0)                   QUIT IF OR BAR
         B     GTSS99                   END OF RIGHT PAREN
         SPACE
GTSS02   DS    0H
         BAL   RETURN,GTSYMNUM     GET SECOND NUMBER
         BC    BOMIT+BDNO,GTSSE02  BRANCH IF INVALID
         B     *+4(R15)            BRANCH ON DELIMITER
         B     GTSSE02                  ERROR IF UNKNOWN
         B     GTSSE02                  ERROR IF COMMA AGAIN
         B     GTSS03                   COLON, SET FIRST SUBSTR
         B     GTSS03                   OR BAR, SET FIRST SUBSTR
         B     GTSSE02                  RIGHT PAREN, BAD SYNTAX
         SPACE
GTSS03   DS    0H
         CH    COUNT,SRNMFR+L'SRNMFR    FIRST SUBSTR TOO SMALL .Q
         BL    GTSSE02             ERROR IF YES
         CH    COUNT,SRNMTO+L'SRNMTO    FIRST SUBSTR TOO BIG .Q
         BH    GTSSE02             ERROR IF YES
         STH   COUNT,SRNMFR        SET FIRST SUBSTRING
         B     *+4(R15)            BRANCH ON DELIMITER
         DC    AL4(0)                   QUIT IF UNKNOWN
         DC    AL4(0)                   QUIT IF COMMA
         B     GTSS05                   COLON, GET SECOND SUBSTR
         B     GTSS04                   OR BAR, GET SECOND SUBSTR LEN
         DC    AL4(0)                   QUIT IF RIGHT PAREN
         SPACE
GTSS04   DS    0H
         BAL   RETURN,GTSYMNUM     GET SECOND SUBSTRING LENGTH
         LH    R1,SRNMFR           THE FROM VALUE
         LA    COUNT,0(R1,COUNT)   ADD LENGTH GIVING TO VALUE
         BCTR  COUNT,0             LESS ONE
         B     GTSS06              CONTINUE CHECK
         SPACE
GTSS05   DS    0H
         BAL   RETURN,GTSYMNUM     GET SECOND SUBSTRING VALUE
GTSS06   DS    0H
         BC    BDNO,GTSSE03        BRANCH IF INVALID
         B     *+4(R15)            BRANCH ON DELIMITER
         B     GTSSE03                  BRANCH IF OTHER
         B     GTSSE03                  BRANCH IF COMMA
         B     GTSSE03                  BRANCH IF COLON AGAIN
         B     GTSSE03                  BRANCH IF OR BAR AGAIN
         B     GTSS07                   BRANCH IF RIGHT PAREN
         SPACE
GTSS07   DS    0H
         BC    BOMIT,GTSS99        BRANCH IF SECOND SUBSTR NULL
         CH    COUNT,SRNMFR+L'SRNMFR    SECOND SUBSTR TOO SMALL .Q
         BL    GTSSE03             ERROR IF YES
         CH    COUNT,SRNMTO+L'SRNMTO    SECOND SUBSTR TOO BIG .Q
         BH    GTSSE03             ERROR IF YES
         STH   COUNT,SRNMTO        SET SUBSTRING TWO VALUE
         B     GTSS99              AND GO TO TERMINATE
         SPACE
GTSSE01  DS    0H
         OI    SRNMFLG,SRNMTSUB    SUBSCRIPT ERROR
         B     GTSS99              AND GO TO TERMINATE
GTSSE02  DS    0H
         OI    SRNMFLG,SRNMTFR     FROM SUBSTRING ERROR
         B     GTSS99              AND GO TO TERMINATE
GTSSE03  DS    0H
         OI    SRNMFLG,SRNMTTO     TO SUBSTRING ERROR
         B     GTSS99              AND GO TO TERMINATE
         SPACE
GTSS99   DS    0H
         L     R1,ARGSTART         ADDRESS OF LAST DELIMITER
         LA    R1,1(,R1)           PLUS ONE
         ST    R1,ARGSTART         CONTINUE SCAN FROM THERE
        $RESTOR ,                  RESTORE CALLER'S REGISTERS
         BR    RETURN              AND RETURN TO CALLER
         SPACE 2
***********************************************************************
*                                                                     *
*              GETSYMNUM  ---  SCAN EXPRESSION AND DELIMITER          *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY -- ARGSTART -> VERIFIED DELIMITER
*                    ARGSTOP  -> END OF STRING
*
*        ON EXIT  -- ARGSTART -> NEXT DELIMITER
*                    R15 = 0  IF OTHER DELIMITER
*                    R15 = 4  IF COMMA DELIMITER
*                    R15 = 8  IF COLON DELIMITER
*                    R15 =12  IF OR BAR DELIMITER
*                    R15 =16  IF RIGHT PAREN DELIMITER
*
*                    CC = BOMIT  IF NOTHING   (COUNT) = 1
*                    CC = BPOS   IF +VE OR 0  (COUNT) = NUMBER
*                                             (TEMP)  = LEN-1
*                    CC = BNEG   IF -VE       (COUNT) = NUMBER
*                                             (TEMP)  = LEN-1
*                    CC = BDNO   IF ERROR     (COUNT) = START ADDR
*                                             (TEMP)  = ERROR NUMBER
         SPACE
GTSYMNUM DS    0H
        $SAVE  ,                   SAVE CALLER'S REGISTERS
         L     X1,ARGSTART         CURRENT DELIMITER POINTER
         LA    X1,1(,X1)           START FROM HERE
         ST    X1,ARGSTART         AND SET SAME
         LA    X3,0                SET OTHER DELIMITER
        $CALL  GETNUM              TAKE FIRST FLING FOR DELIM
         BC    15-BDNO,GTSN02      THIS MUST FAIL
         L     X2,ARGSTART         THIS IS WHERE WE DIED
         LA    X3,16               CODE FOR RIGHT PAREN
         CLI   0(X2),C')'          WAS IT RIGHT PAREN .Q
         BE    GTSN01              BRANCH IF YES
         LA    X3,12               CODE FOR OR BAR
         CLI   0(X2),C'|'          WAS IT OR BAR .Q
         BE    GTSN01              BRANCH IF YES
         LA    X3,8                CODE FOR COLON
         CLI   0(X2),C':'          WAS IT COLON .Q
         BE    GTSN01              BRANCH IF YES
         LA    X3,4                CODE FOR COMMA
         CLI   0(X2),C','          WAS IT COMMA .Q
         BE    GTSN01              BRANCH IF YES
         LA    X3,0                ELSE IT'S OTHER
GTSN01   DS    0H
         LM    R0,R1,ARGSTART      RETAIN SCAN POINTERS
         ST    X1,ARGSTART         RESCAN FROM ORIG START
         BCTR  X2,0                BACKUP END POINTER BEFORE DELIM
         ST    X2,ARGSTOP          AND SET TO TERMINATE SCAN
        $CALL  GETNUM              GET RESULT
         STM   R0,R1,ARGSTART      RESET NEXT SCAN POINTERS
GTSN02   DS    0H
         LR    R15,X3              RETURN DELIMITER VALUE
        $RESTOR KEEP=(COUNT,TEMP,R15)   RESTORE MOST REGS
         BR    RETURN              AND RETURN TO CALLER
