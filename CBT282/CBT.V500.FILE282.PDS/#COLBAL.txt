***********************************************************************
*                                                                     *
*              COLBAL --- COLUMN BALANCE ROUTINE                      *
*                                                                     *
***********************************************************************
         SPACE
COLBAL  $ENTER ,
        $IFOFF SWITCHS,BCSWS,COLB30     QUIT IF NO BALANCE IN EFFECT
         SPACE 2
*--STRUCTURE OF LINKED TEXT LINES IN MULTIPLE COLUMNS
*
*  POINTER
*     |
*     |     **********************************
*     '---> * + *  COLUMN DESCRIPTOR RECORD  *
*           **|*******************************
*             |
*     .-------'
*     |
*     |     ************************
*     '---> * + *  LINE 1 OF TEXT  *
*           **|*********************
*             |
*     .-------'
*     |
*     |     ************************
*     '---> * + *  LINE 2 OF TEXT  *
*           **|*********************
*             |
*     .-------'
*     |
*     |     ************************
*     '---> * + *  LINE 3 OF TEXT  *
*           **|*********************
*             |
*     .-------'
*     .
*     .
*     .     ************************
*     '---> * 0 *  LAST TEXT LINE  *
*           ************************
         EJECT
***********************************************************************
*                                                                     *
*  'BC' IS ON -- SEE IF WE HAVE ANYTHING WE CAN BALANCE               *
*                                                                     *
***********************************************************************
         SPACE
         SR    R1,R1               CLEAR WORKING COLUMN NUMBER
         LA    X3,1(,R1)           BALANCE FROM COLUMN ONE, NORMALLY
         SR    COUNT,COUNT         CLEAR LINE COUNT
COLB01   DS    0H
         LA    R1,1(,R1)           NEXT COLUMN
         C     R1,CDCOUNT          OVER THE TOP .Q
         BH    COLB04              DONE IF YES
         SPACE
         LR    X1,R1               COLUMN TO EXAMINE
         BCTR  X1,0                COLUMN LESS ONE
         MH    X1,MCALEN           DESCRIPTOR OFFSET
         LA    X1,MCAREA(X1)       POINT TO DESCRIPTOR
         USING CPFKFN,X1           TELL THE ASSEMBLER
         A     COUNT,CPFKFNC       ADD COLUMN LINE COUNT
         BZ    COLB02              IF TOTAL ZERO, START WITH NEXT
        $IFOFF CPFKFNG,COLLOCK,COLB01   BRANCH IF COLUMN NOT LOCKED
         SR    COUNT,COUNT         ELSE, START OVER AGAIN
COLB02   DS    0H
         LA    X3,1(,R1)           BALANCE FROM NEXT COLUMN
         B     COLB01              TRY NEXT COLUMN
         SPACE
COLB04   DS    0H
         LTR   COUNT,COUNT         ANY LINES TO BALANCE .Q
         BZ    COLB30              NO, QUIT
         C     X3,CDCOUNT          ANY COLUMNS TO BALANCE .Q
         BNL   COLB30              NO, QUIT
         EJECT
***********************************************************************
*                                                                     *
*  THERE IS STUFF WE CAN TRY TO BALANCE -- MAKE IT ALL ONE COLUMN     *
*                                                                     *
***********************************************************************
         SPACE
         XC    SCAREA(CPFKFNL),SCAREA   CLEAR TEMP SEQUENTIAL TARGET
         ST    COUNT,SCQCNT        SET EXPECTED LINE COUNT
         SR    COUNT,COUNT         CLEAR WORKING LINE COUNT
         LR    R1,X3               START COLUMN NUMBER
         LA    TEMP,SCQANCH+FNQFCHN-FNQELEM  CHAIN ADDRESS POINTER
         USING FNQELEM,TEMP        TELL THE ASSEMBLER
COLB11   DS    0H
         LR    X1,R1               COLUMN NUMBER
         BCTR  X1,0                COLUMN NUMBER MINUS ONE
         MH    X1,MCALEN           COLUMN DESCRIPTOR OFFSET
         LA    X1,MCAREA(X1)       COLUMN DESCRIPTOR ADDR
         MVC   FNQFCHN,CPFKFNA     ENTER COLUMN DATA
         XC    CPFKFN(CPFKFNL),CPFKFN   CLEAR COLUMN DESCRIPTOR
         B     COLB13              NOW GO TO END OF SEQUENTIAL
         SPACE
COLB12   DS    0H
         L     TEMP,FNQFCHN        NEXT LINE ELEMENT
        $IFON  FNQFLAG,FNQOVLY,COLB13   BRANCH IF OVERLAY LINE
         LA    COUNT,1(,COUNT)     BUMP RECORD COUNT
COLB13   DS    0H
         OC    FNQFCHN,FNQFCHN     AT THE END .Q
         BNZ   COLB12              BRANCH IF NOT
         SPACE
         LA    R1,1(,R1)           NEXT COLUMN NUMBER
         C     R1,CDCOUNT          DONE THE LAST COLUMN .Q
         BNH   COLB11              BRANCH IF NOT
         SPACE
         C     COUNT,SCQCNT        TOTALS TALLY .Q
         BNE   ABCOLB01            LET'S TAKE A LOOK IF NOT
         SPACE
***********************************************************************
*                                                                     *
*  CALCULATE NUMBER OF LINES DESIRED PER COLUMN                       *
*                                                                     *
***********************************************************************
         SPACE
         L     TEMP,CDCOUNT        COLUMN COUNT
         SR    TEMP,X3             LESS BALANCE COLUMN START NUMBER
         A     TEMP,F1             NUMBER OF COLUMNS TO BALANCE
         L     R0,SCQCNT           NUMBER OF LINES TO BALANCE
         SRDL  R0,32               PREPARE FOR DIVIDE
         DR    R0,TEMP             LINES BY COLUMNS
         LTR   R0,R0               REMAINDER ZERO .Q
         BZ    COLB21              BRANCH IF YES
         LA    R1,1(,R1)           BUMP LINES PER COLUMN
         EJECT
***********************************************************************
*                                                                     *
*  CONSTRUCT A COLUMN                                                 *
*                                                                     *
***********************************************************************
         SPACE
*
*--MAKE TEMP POINT TO COLUMN DESCRIPTOR RECORD
*
COLB21   DS    0H
         LR    X1,X3               COLUMN NUMBER
         BCTR  X1,0                COLUMN NUMBER MINUS ONE
         MH    X1,MCALEN           COLUMN DESCRIPTOR OFFSET
         LA    X1,MCAREA(X1)       COLUMN DESCRIPTOR ADDRESS
         MVC   CPFKFNA,SCQANCH     REMAINDER OF LINES IN COLUMN
         SR    COUNT,COUNT         ZERO COLUMN LINE COUNT
         LA    TEMP,CPFKFNA+FNQFCHN-FNQELEM  START OF LIST
*
*--THROW AWAY SKIPS AT THE TOP OF A COLUMN
*
COLB21P5 DS    0H
         OC    FNQFCHN,FNQFCHN     IS THERE A FIRST LINE .Q
         BZ    COLB25              IF NOT, GET OUT
         L     X2,FNQFCHN          IS THIS FIRST LINE A
         TM    FNQFLAG-FNQELEM(X2),FNQSSET   'SKIP AT EOB' .Q
         BO    COLB22P3            BRANCH IF IT IS
         TM    FNQSKSP-FNQELEM(X2),SKSPSK  IS IT A SKIP .Q
         BZ    COLB22P3            BRANCH IF NOT
*
*--FIRST LINE IS A SKIP, SO FREE IT AND DELETE IT
*
        $SAVE  ,                   SAVE R0 AND R1
         LR    R0,X2               ADDRESS
         LA    R1,FNQOVHD
         AH    R1,FNQRDW-FNQELEM(X2)  LENGTH
         L     X2,0(X2)            POINT TO THE SECOND LINE
         ST    X2,FNQFCHN          SAVE POINTER TO SECOND LINE
        $CALL  FREECELL            GOODBYE, FIRST LINE
        $RESTOR  ,                 RESTORE R0 AND R1
         B     COLB21P5            TEST FOR ANOTHER SKIP
         EJECT
*
*--PICK UP THE DESIRED NUMBER OF LINES
*
COLB22   DS    0H
         OC    FNQFCHN,FNQFCHN     IS THERE A NEXT LINE .Q
         BZ    COLB25              IF NOT, GET OUT
         L     X2,FNQFCHN          IS NEXT LINE AN OVERLAY .Q
COLB22P3 DS    0H
         TM    FNQFLAG-FNQELEM(X2),FNQOVLY
         BO    COLB22P9            BRANCH IF YES
         LA    COUNT,1(,COUNT)     BUMP LINE COUNT
         CR    COUNT,R1            ALL LINES GOT .Q
         BL    COLB22P9            BRANCH IF NOT
*
         CLI   FNQCORSW-FNQELEM(X2),ICORNONE   ARE WE IN A BLOCK .Q
         BE    COLB23              BRANCH IF NOT
         TM    FNQFLAG-FNQELEM(X2),FNQEOB   AT LAST LINE OF BLOCK .Q
         BO    COLB23       BRANCH IF YES -- NOW HAVE FULL COLUMN
*
COLB22P9 DS    0H
         L     TEMP,FNQFCHN        ACCEPT THIS NEXT LINE
         B     COLB22              AND TRY AGAIN
*
*--GET DOWN TO THE LAST LINE OF THIS COLUMN (PLUS ITS OVERLAYS)
*
*--FIRST, HAVE TO SEE IF WE GOT HERE FROM THE END OF A BLOCK
*
COLB23   DS    0H
         L     TEMP,FNQFCHN        POINT AT LAST LINE
         TM    FNQFLAG,FNQEOB      IS THIS LINE END OF BLOCK .Q
         BZ    COLB24P1            BRANCH IF NOT
         OC    FNQFCHN,FNQFCHN     IS THERE ONE AFTER IT .Q
         BZ    COLB25              IF NOT, GET OUT
*
*--WAS THE BLOCK TERMINATED WITHOUT A BREAK .Q
*
         TM    FNQFLAG,FNQNOBRK
         BZ    COLB25              BRANCH IF TERMINATED BY A BREAK
*
*--GRAB THE NEXT LINE IF IT IS NOT IN A BLOCK
*
COLB23P2 DS    0H
         L     X2,FNQFCHN          POINT TO NEXT LINE
         CLI   FNQFLAG-FNQELEM(X2),ICORNONE   IN A BLOCK .Q
         BNE   COLB25              BRANCH IF YES
         LA    COUNT,1(,COUNT)     TAKE THIS LINE
*
COLB24   DS    0H
         L     TEMP,FNQFCHN        POINT AT LAST LINE
COLB24P1 DS    0H
         OC    FNQFCHN,FNQFCHN     IS THERE ONE AFTER IT .Q
         BZ    COLB25              IF NOT, GET OUT
         L     X2,FNQFCHN          IS LINE AFTER LAST AN OVERLAY .Q
         TM    FNQFLAG-FNQELEM(X2),FNQOVLY
         BO    COLB24              BRANCH IF YES
         EJECT
***********************************************************************
*                                                                     *
*  SAVE THIS COLUMN, PREPARE TO CONSTRUCT NEXT                        *
*                                                                     *
***********************************************************************
         SPACE
COLB25   DS    0H
         ST    COUNT,CPFKFNC       SET COLUMN LINE COUNT
         MVC   SCQANCH,FNQFCHN     REST OF LINES
         XC    FNQFCHN,FNQFCHN     MARK COLUMN END
         SPACE
         LA    X3,1(,X3)           NEXT COLUMN NUMBER
         C     X3,CDCOUNT          DONE .Q
         BNH   COLB21              BRANCH IF NOT
         XC    SCAREA(CPFKFNL),SCAREA   CLEAR TEMP DESCRIPTOR
         SPACE
COLB30   DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         DROP  TEMP,X1
         SPACE
ABCOLB01 DS    0H
        $LOGERR 1017               COLUMN LINE COUNTS DON'T MATCH
