***********************************************************************
*                                                                     *
*              .TB (TABS) --- CONTROL                                 *
*                                                                     *
***********************************************************************
         SPACE
*        .TB COMMAND FORMAT
*
*        .TB OP1 <OP2 ... >>
*
*        WHERE OP1 IS: T1 | SET <CHAR> | C1/T1<MODE> | <STRING>T1<MODE>
*
*        WHERE  <STRING> IS:  'STRING' | "STRING" | /STRING/
*
*        AND    <MODE>   IS:  LEFT | RIGHT| CENTRE | 'CHAR'
*
         SPACE
*        REGISTER USAGE:
*
*              TEMP:  LENGTH OF FILL STRING
*              COUNT: ADDRESS OF FILL STRING
*              X1:    BASE REGISTER USED TO ADDRESS TAB BUFFER
*              X3:    CURRENT TAB VALUE
         SPACE
TB      $ENTER ,
        $CALL  PRINTNB             CLEAR PRINT BUFFER
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION
         USING TBAREA,X1           SET UP X1 FOR PERMANENT USING
        $CALL  GETARG              GET CHARACTER ARGUMENT
         BC    15-BOMIT,TB10       BRANCH IF ARGUMENT FOUND
         LA    R1,TBANCHR          PTR TO CURRENT USER TAB ANCHOR
        $CALL  TBFREBLK            ELSE FREE ALLOCATED TAB FIELDS
         B     TB120               THEN RETURN
TB10     DS    0H
         SR    X3,X3               X3 CONTAINS CURRENT TAB VALUE
         ST    COUNT,ARGSTART      RESET SCAN PTR TO FIRST .TB OPERAND
TB20     DS    0H                  LOOP FOR .TB OPERANDS
        $CALL  GETYESNO            SCAN OFF FIRST ARGUMENT
         BC    BOMIT,TB120         RETURN IF NO MORE
         CLI   YNRESULT,YNSET      WAS OPERAND 'SET' .Q
         BNE   TB40                BRANCH IF NO
         SPACE
*                                  .TB SET <CHAR>
        $ON    SWITCHS,FNOCASE     NO CASE CONVERSION
        $CALL  GETARG              GET CHARACTER ARGUMENT
         BC    BOMIT,TB30          BRANCH TO RESET DEFAULT IF OMITTED
         LTR   TEMP,TEMP           LENGTH-1 OF ARG ZERO .Q
         BNZ   TBE02               ERROR IF NOT
         MVC   TBIND(1),0(COUNT)   ENTER EXTRA USER TAB CHAR
         B     TB20                AND RESUME SCANNING
TB30     DS    0H
         MVC   TBIND(1),TBIND+1    RESET TO INITIAL VALUE
         B     TB120               GO TO RETURN
         SPACE
TB40     DS    0H                  CHECK FOR STRING ARGUMENT
         ST    COUNT,ARGSTART      SAVE PTR FOR RESCANNING
         CLI   0(COUNT),C''''      1ST CHAR = SINGLE QUOTE .Q
         BE    TB60                BRANCH IF YES
         CLI   0(COUNT),C'"'       1ST CHAR = DOUBLE QUOTE .Q
         BE    TB60                BRANCH IF YES
         CLI   1(COUNT),C'/'       2ND CHAR = SLASH .Q
         BNE   TB50                BRANCH IF NO
         SPACE
*                                  .TB C1/T1
         LA    TEMP,1              SET LENGTH OF FILL CHAR
         BAL   RETURN,TBGETBLK     GET TAB AREA AND CHAIN IT
         BC    BNEG,TBE06          ERROR IF NO CORE
         BAL   RETURN,TBMFILL      SET FILL CHAR AND LENGTH
         LA    COUNT,2(,COUNT)     POINT TO T1 FIELD
         ST    COUNT,ARGSTART      SAVE PTR TO RESUME SCANNING
         B     TB80                NOW PROCESS T1 FIELD
TB50     DS    0H
         CLI   0(COUNT),C'/'       FIRST CHAR = SLASH .Q
         BNE   TB70                BRANCH IF NO
         SPACE
TB60     DS    0H                  .TB <STRING>T1
         LR    TEMP,COUNT          START OF STRING
TB62     DS    0H
         LA    TEMP,1(,TEMP)       NEXT CHARACTER
         C     TEMP,ARGSTOP        PAST THE END OF OPERANDS .Q
         BH    TBE05               ERROR IF YES
         CLC   0(1,TEMP),0(COUNT)  SAME DELIMITER CHARACTER .Q
         BNE   TB62                BRANCH IF NOT
         LA    R1,1(,TEMP)         NEXT CHAR TO SCAN
         ST    R1,ARGSTART         SET SCANNING ADDRESS
         LA    COUNT,1(,COUNT)     POINT TO ARGUMENT
         SR    TEMP,COUNT          LENGTH OF STRING
         BNP   TBE01               ERROR IF NOTHING THERE
         BAL   RETURN,TBGETBLK     GET SPACE FOR RESULT
         BC    BNEG,TBE06          BRANCH IF NO SPACE
         BAL   RETURN,TBMFILL      SET FILL CHAR AND LENGTH
         B     TB80                PROCESS T1 FIELD
         SPACE
TB70     DS    0H                  .TB T1
         ST    COUNT,ARGSTART      SAVE PTR TO T1 FIELD
         LA    TEMP,1              DEFAULT LENGTH OF FILL = 1
         BAL   RETURN,TBGETBLK     OBTAIN CORE FOR TBAREA
         BC    BNEG,TBE06          ERROR IF NO CORE
         LA    COUNT,BLANKS        DEFAULT FILL IS BLANK
         BAL   RETURN,TBMFILL      SET FILL CHAR AND LENGTH
TB80     DS    0H
         BAL   RETURN,TBGETMON     GET T1 FIELD AND ENSURE MONOTONIC
         BC    BDNO,TB90           BRANCH IF NOT NUMERIC
         BC    BOMIT,TBE01         ISSUE ERROR IF OMITTED
         BC    BNEG,TB120          ERROR ALREADY SET, NOW RETURN
         A     X3,DEFTABL+TAB3DEF  ADJUST FOR LEFT TABS
         STH   X3,TBVALUE          SET VALUE FOR T1 FIELD
         L     COUNT,ARGSTART      RESTORE CURRENT PTR
         B     TB20                RESUME SCAN OF ARGUMENTS
TB90     DS    0H
         L     TEMP,ARGSTOP        SET STOP ADDRESS IN TEMP
         L     R1,ARGSTART         OBTAIN CURRENT STARTING PTR
         BCTR  R1,0                BACKUP BY ONE
         ST    R1,ARGSTOP          SET NEW STOP PTR
         ST    COUNT,ARGSTART      RESET STARTING PTR
         BAL   RETURN,TBGETMON     SECOND TRY TO GET T1 OPERAND
         BC    BDNO,TBE02          ERROR IF T1 NOT NUMERIC
         BC    BOMIT,TBE01         ISSUE ERROR IF OMITTED
         BC    BNEG,TB120          ERROR ALREADY SET, NOW RETURN
         STH   X3,TBVALUE          SAVE VALUE OF T1 FIELD
         ST    TEMP,ARGSTOP        RESTORE ORIGINAL STOP PTR
        $CALL  GETYESNO            CHECK FOR LEFT,RIGHT, OR CENTRE
         BC    BOMIT,TB120         THAT'S ALL FOLKS
         CLI   YNRESULT,YNLEFT     WAS LEFT SPECIFIED .Q
         BNE   TB95                BRANCH IF NOT
         A     X3,DEFTABL+TAB3DEF  ADJUST FOR LEFT TABS
         STH   X3,TBVALUE          RESET TAB
         B     TB20                AND CONTINUE
TB95     DS    0H
         CLI   YNRESULT,YNRGHT     WAS RIGHT SPECIFIED .Q
         BNE   TB100               BRANCH IF NO
         LA    R1,TBRIGHT          INDICATE RIGHT SPECIFIED
         STC   R1,TBTYPE           SAVE FLAG IN TYPE FIELD
         B     TB20                CONTINUE SCANNING
TB100    DS    0H
         CLI   YNRESULT,YNCNTR     WAS CENTRE SPECIFIED .Q
         BE    TB105               BRANCH IF YES
         CLI   YNRESULT,YNCND      WAS COND SPECIFIED .Q
         BNE   TB110               BRANCH IF NO
TB105    DS    0H
         LA    R1,TBCENTRE         INDICATE CENTRE SPECIFIED
         STC   R1,TBTYPE           SAVE FLAG IN TYPE FIELD
         B     TB20                RESUME SCANNING
TB110    DS    0H
         CLI   0(COUNT),C''''      WAS SINGLE QUOTE FOUND .Q
         BNE   TBE02               ERROR IF NO
         CLI   2(COUNT),C''''      WAS 3RD CHAR A QUOTE .Q
         BNE   TBE05               ERROR IF NO
         LA    R1,TBOK             INDICATE ALIGN CHAR FOUND
         STC   R1,TBTYPE           SAVE FLAG IN TYPE FIELD
         MVC   TBCHAR(1),1(COUNT)  MOVE IN ALIGNMENT CHARACTER
         CLC   1(3,COUNT),0(COUNT) WAS '''' SPECIFIED .Q
         BNE   TB115               BRANCH IF NO
         LA    COUNT,1(,COUNT)     SKIP OVER FIRST QUOTE
TB115    DS    0H
         LA    COUNT,2(,COUNT)     BUMP PTR OVER NEXT 2 QUOTES
         ST    COUNT,ARGSTART      NOW POINTING TO LAST QUOTE
        $CALL  GETARG              GET NEXT ARGUMENT
         LTR   TEMP,TEMP           WAS LENGTH-1 ZERO .Q
         BZ    TB20                CONTINUE SCANNING IF YES
         B     TBE02               ELSE ISSUE ERROR
TBE01    DS    0H
         MVI   EFLAG003,ERRSET     OMITTED CONTROL WORD PARM
         B     TB120               RETURN TO MAIN CONTROLLER
TBE02    DS    0H
         MVI   EFLAG005,ERRSET     ILLEGAL CONTROL WORD PARM
         B     TB120               RETURN TO MAIN CONTROLLER
TBE05    DS    0H
         MVI   EFLAG015,ERRSET     MISMATCHED DELIMITER
         B     TB120               RETURN TO MAIN CONTROLLER
TBE06    DS    0H
         MVI   EFLAG024,ERRSET     WORKING STORAGE EXHAUSTED
         B     TB120               RETURN TO MAIN CONTROLLER
TBE07    DS    0H
         MVI   EFLAG035,ERRSET     ARGUMENT NOT NUMERIC
         B     TB120               RETURN TO MAIN CONTROLLER
         SPACE
TBMFILL  DS    0H
         STH   TEMP,TBLEN          SAVE LENGTH OF FILL CHAR
         BCTR  TEMP,0              DECREMENT LENGTH BY 1
         EX    TEMP,TBMVE          MOVE IN FILL STRING
         BR    RETURN              RETURN TO CALLER
TBMVE    MVC   TBFILL(*-*),0(COUNT) MOVE IN STRING
         SPACE
TB120    DS    0H
        $EXIT  RC=0                RETURN TO CALLER
         SPACE
         DROP  X1
         EJECT
         COPY  #TBGBLK
         EJECT
         COPY  #TBGMON
         EJECT
         COPY  #TBCBLK
         EJECT
         COPY  #TBFBLK
