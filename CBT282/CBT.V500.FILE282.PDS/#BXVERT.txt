***********************************************************************
*                                                                     *
*              'BXVERT' INTERNAL ROUTINE --- PROCESS SIDES OF BOXES   *
*                                                                     *
***********************************************************************
         SPACE
*        ON ENTRY --  (R0) -> RDW OF OUTPUT LINE
         SPACE
BXVERT  $ENTER ,
         L     X1,BXPTR            BOX DATA ANCHOR
         LTR   X1,X1               ANYTHING THERE .Q
         BZ    BXV07               BRANCH IF NOT
         SPACE
         USING BXAREA,X1           AREA LOOKS LIKE THIS
         LH    COUNT,BXBLKLN       LENGTH OF OVERLAY
         LTR   COUNT,COUNT         ANY DATA .Q
         BNP   BXV07               BRANCH IF NOT
         LR    X3,R0               OUTPUT BUFFER ADDR
        $IFON  TTYPE,TONLINE,BXV01 BRANCH IF ONLINE
         CLI   4(X3),SUPRSPAC      PRINTER OVERLAY LINE .Q
         BE    BXV07               YES, GO TO RETURN
         LA    X3,1(,X3)           BUMP ONE FOR CARRIAGE CONTROL
BXV01    DS    0H
         LA    X3,4(,X3)           BUMP OVER RDW
         AH    X3,ADJCNT           PLUS ADJUST VALUE
         LA    X2,BXDATA           OVERLAY STRING CONTROL
         DROP  X1
         SPACE
BXV02    DS    0H
         S     COUNT,F1            DECREMENT LENGTH LEFT BY ONE
         BM    BXV06               BRANCH IF DONE
         TM    0(X2),BXDOWN        A BAR IN THIS COLUMN .Q
         BNO   BXV03               BRANCH IF NOT
         CLI   0(X3),C' '          IS FORMATTED LINE BLANK .Q
         BNE   BXV03               BRANCH IF NOT
         MVI   0(X3),C'|'          'OR' BAR FOR VERT LINES
        $IFOFF SWITCHS,TRSWS,BXV03 BRANCH IF NO TRANSLATE
        $IFON  SWITCHS,TRSUP,BXV03 BRANCH IF TRANSLATE SUPPRESSED
         L     R1,TRANADO          ADDR OF OUTPUT TRANS TABLE
         TR    0(1,X3),0(R1)       CONVERT SINGLE CHAR
BXV03    DS    0H
         LA    X3,1(,X3)           NEXT OUTPUT CHAR
         LA    X2,1(,X2)           NEXT OVERLAY CHAR
        $IFOFF TTYPE,TONLINE,BXV02 BRANCH IF NOT ONLINE
BXV04    DS    0H
         CLI   0(X3),CBACKSP       BACKSPACE IN ONLINE OUTPUT .Q
         BE    BXV05               BRANCH IF YES
         CLI   0(X3),CESCAPE       ESCAPE IN ONLINE OUTPUT .Q
         BNE   BXV02               BRANCH IF NOT
         L     TEMP,VDEVICEA       TERMINAL DESCRIPTOR BLOCK
         LTR   TEMP,TEMP           DOES IT EXIST .Q
         BZ    BXV05               BRANCH IF NOT
         LA    TEMP,TDEVAESC-TDEVICEA(,TEMP) LENGTH-1 AFTER ESC TABLE
         SR    R0,R0               CLEAR WORK REG
         IC    R0,1(,X3)           FUNCTION CHARACTER
         AR    TEMP,R0             POINT TO LENGTH-1
         IC    R0,0(,TEMP)         LENGTH-1 AFTER ESCAPE
         AR    X3,R0               BUMP BY LENGTH-1
BXV05    DS    0H
         LA    X3,2(,X3)           SKIP BKSP AND NEXT CHAR
         B     BXV04               TRY AGAIN
         SPACE
BXV06    DS    0H
         LR    R1,R0               RDW POINTER
         SR    X3,R0               LENGTH OF OVERLAYED OUTPUT
         CH    X3,0(,R1)           NEW LENGTH LONGER .Q
         BNH   BXV07               BRANCH IF NOT
         STH   X3,0(,R1)           SET NEW RDW LENGTH
BXV07    DS    0H
        $EXIT  RC=0                RETURN TO CALLER
