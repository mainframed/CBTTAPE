         MACRO
&NAME    ELSEIF &EXP
         COPY  $MGBLDEF
         LCLC  &X
         AIF   (&$MDGLVL GT 0).OK02
         MNOTE 8,'''ELSEIF'' IS VALID ONLY WITHIN AN IF-GROUP'
         MNOTE 0,' - MACRO GENERATION TERMINATED'
         MEXIT
.OK02    AIF   ('&$MIFELS(&$MDGLVL)' EQ 'F').OK04
         MNOTE 8,'''ELSEIF'' INVALID FOLLOWING ''ELSE'' STATEMENT'
         MNOTE 0,' - MACRO GENERATION TERMINATED'
         MEXIT
.OK04    AIF   ('&$MLTAG(&$MDGLVL)' EQ '').OK20
         MNOTE 8,'''ELSEIF'' INVALID FOLLOWING ''DO'' STATEMENT'
         MNOTE 0,' - MACRO GENERATION TERMINATED'
         MEXIT
.OK20    AIF   ('&$MFNBR(&$MDGLVL)' NE '').OK30
         $MGFNBR  ,                GET A UNIQUE TAG
.OK30    J     $MDF&$MFNBR(&$MDGLVL) BRANCH AROUND ELSEIF-GROUP
&X       SETC  '$MDF&$MDGSEQ(&$MDGLVL)'
&$MDGLVL SETA  &$MDGLVL-1          SO 'IF' CAN BUMP IT UP AGAIN
         AIF   ('&NAME' EQ '').SKIP40
&NAME    DC    0H'0'               DEFINE TAG
.SKIP40  ANOP
&X       IF    &SYSLIST(1),&SYSLIST(2),&SYSLIST(3),&SYSLIST(4),        C
               &SYSLIST(5),&SYSLIST(6),&SYSLIST(7),&SYSLIST(8),        C
               &SYSLIST(9),&SYSLIST(10),&SYSLIST(11),&SYSLIST(12),     C
               &SYSLIST(13),&SYSLIST(14),&SYSLIST(15),&SYSLIST(16),    C
               &SYSLIST(17),&SYSLIST(18),&SYSLIST(19),&SYSLIST(20),    C
               &SYSLIST(21),&SYSLIST(22),&SYSLIST(23),&SYSLIST(24),    C
               &SYSLIST(25),&SYSLIST(26),&SYSLIST(27),&SYSLIST(28),    C
               &SYSLIST(29),&SYSLIST(30),&SYSLIST(31),&SYSLIST(32)
&$MDGNAM(&$MDGLVL) SETC '&NAME'    SET NAME FOR 'ENDO'
.*       DC    0C'$MDGLVL &$MDGLVL $MDGNAM &$MDGNAM(&$MDGLVL)'
         MEND
