         MACRO
&N       BEGTEST &LVL
         GBLA  &TESTLVL            SET TO > 0 TO ACTIVATE
         GBLC  &$A,&$B,&$C,&$D     SET ONE OR MORE TO 'Y' TO ACTIVATE
         LCLA  &I,&J
         LCLC  &TEXT
         AIF   ('&N' EQ '').SKIP010
&N       EQU   *
.SKIP010 ANOP
         AIF   ('&LVL' NE '' AND '&LVL'(1,1) GT 'Z').TEST510
.*
.* OPTIONAL CODE (LOCATED BETWEEN 'BEGTEST' AND 'ENDTEST') IS INCLUDED
.* OR EXCLUDED BASED ON VALUE SPECIFIED IN MACRO OPERAND AND VALUE
.* OF CORRESPONDING SET SYMBOL. IN FOLLOWING DOC'N, EXAMPLE IS FOR
.* OPERAND 'A' AND SET SYMBOL '&$A'. LOGIC IS IDENTICAL FOR B, C, & D.
.*
.*     SET SYMBOL &$A:  ------->     'Y'                NOT 'Y'
.*                                ------------       ------------
.*                         'A'    INCLUDE CODE       EXCLUDE CODE
.*     MACRO OPERAND:  NOT 'A'  TEST NEXT LETTER    TEST NEXT LETTER
.*
.* EXAMPLE:
.*
.*          GBLC  &$A
.* &$A      SETC  'Y'
.*          BEGTEST A
.*          ...   THIS CODE WILL BE ASSEMBLED
.*          ENDTEST ,
.* &$A      SETC  'N'   (ANY VALUE OTHER THAN 'Y')
.*          BEGTEST A
.*          ...  THIS CODE WILL NOT BE ASSEMBLED
.*          ENDTEST
.*
.*          GBLA  &TESTLVL
.* &TESTLVL SETA  10
.*          BEGTEST 10  (ANY VALUE 0 - 10)
.*          ... THIS CODE WILL BE ASSEMBLED
.*          ENDTEST
.*          BEGTEST 20  (ANY VALUE > 10)
.*          ... THIS CODE WILL NOT BE ASSEMBLED
.*          ENDTEST
.*
         AIF   ('&LVL' NE 'A').TEST020
         AIF   ('&$A' EQ 'Y').MEND999
         AGO   .LOOP520
.TEST020 AIF   ('&LVL' NE 'B').TEST030
         AIF   ('&$B' EQ 'Y').MEND999
         AGO   .LOOP520
.TEST030 AIF   ('&LVL' NE 'C').TEST040
         AIF   ('&$C' EQ 'Y').MEND999
         AGO   .LOOP520
.TEST040 AIF   ('&LVL' NE 'D').MSG050
         AIF   ('&$D' EQ 'Y').MEND999
         AGO   .LOOP520
.MSG050  MNOTE *,' ---> INVALID CHAR OPERAND &LVL - OPT CODE INCLUDED'
         AGO   .MEND999
.*
.* IF MACRO OPERAND IS NUMERIC (> 0), THEN VALUE IS COMPARED WITH SET
.* SYMBOL &TESTLVL, WHICH MUST BE PREVIOUSLY BE SET BY PROGRAMMER.
.* IF MACRO OPERAND IS > &TESTLVL, THEN THE OPTIONAL CODE IS EXCLUDED.
.* NOTE: THE DEFAULT VALUE FOR &TESTLVL IS 0.
.*
.TEST510 AIF   (&LVL LE &TESTLVL).MEND999
.LOOP520 ANOP
&TEXT    AREAD NOPRINT
         AIF   ('&TEXT'(1,1) EQ '*').LOOP520
&J       SETA  &J+1
&I       SETA  1
.LOOP530 AIF   ('&TEXT'(&I,1) EQ ' ').NEXT540 SCAN PAST NAME FIELD
&I       SETA  &I+1
         AIF   (&I GT 65).LOOP520
         AGO   .LOOP530
.NEXT540 AIF   ('&TEXT'(&I,1) NE ' ').NEXT550 SCAN PAST LEADING SPACES
&I       SETA  &I+1
         AIF   (&I GT 65).LOOP520
         AGO   .NEXT540
.NEXT550 AIF   ('&TEXT'(&I,8) NE 'ENDTEST ').LOOP520
&J       SETA  &J-1
         AIF   (&J NE 1).MSG560
         MNOTE *,' ===> ENDTEST STATEMENT ENCOUNTERED - 1 STATEMENT SKIX
               PPED'
         AGO   .MEND999
.MSG560  MNOTE *,' ===> ENDTEST STATEMENT ENCOUNTERED - &J STATEMENTS SX
               KIPPED'
.MEND999 MEND
