PROC 0 DEBUG
/* ----------------------------------------------------------------- */
/*     JIM MARSHALL                                                  */
/*     TRANSMIT / RECEIVE SYSTEM                                     */
/*     'TRANSMIT MEMBERS OF A PDS TO TSO  USER'                      */
/* ----------------------------------------------------------------- */

CONTROL NOLIST NOMSG NOFLUSH PROMPT    /* SET DEFAULT LIST OPTION */
IF &DEBUG = DEBUG                +
  THEN CONTROL LIST,MSG,CONLIST, +
               SYMLIST
SET &NSY     = &STR()
SET &SY      = &STR()
SET &TUSER   = &STR()
SET &NTF     = &STR(YES)
SET &MES     = &STR(NO)
SET &WHOUSER = &STR()
SET &M0      = &STR()
ISPEXEC CONTROL ERRORS RETURN
SET ZWINTTL = &STR(IDTF: TRANSMIT FILE TO A TSO USER)
LOOP:  +
       ISPEXEC CONTROL DISPLAY REFRESH
       ISPEXEC ADDPOP COLUMN(2) ROW(2)
       SET &LASTCC = 0
       SET &MAXCC  = 0
       ISPEXEC DISPLAY PANEL(IDT#P0)
       SET  &RC = &LASTCC
       ISPEXEC REMPOP
       IF &RC = 8 THEN GOTO EXIT
       IF  &MAXCC NE 0  THEN  GOTO ERRMSG
       /* ------------------------------------------------------ */
       /*   NOW WE WILL CHECK TO SEE IF THE FILE EXISTS          */
       /* ------------------------------------------------------ */
       SET &LASTCC = 0
       IF  &SYSDSN(&IFILE)  ¬= OK  THEN +
           DO
           WRITE
           WRITE DSN=&IFILE   DOES NOT EXIST OR IS
           WRITE NOT CATALOGED. PLEASE CORRECT !
           WRITE
           GOTO LOOP
           END
       /*--------------------------------------------------------*/
       /*    SINCE THE FILE EXISTS, WE NEED TO CONSTRUCT THE     */
       /*    NODE NAME TRANSPARENT TO THE USER.                  */
       /*--------------------------------------------------------*/
       SET   &LASTCC = 0
       SELECT
         WHEN (&SY = 1A) +
           DO
           SET  &NSY = &STR(FMSROC1A)
           END
         WHEN (&SY = 1B) +
           DO
           SET  &NSY = &STR(FMSROC1B)
           END
         WHEN (&SY = 1C) +
           DO
           SET  &NSY = &STR(FMSROC1C)
           END
         WHEN (&SY = 1E) +
           DO
           SET  &NSY = &STR(FMSROC1E)
           END
         WHEN (&SY = 1F) +
           DO
           SET  &NSY = &STR(FMSROC1F)
           END
         WHEN (&SY = 2A) +
           DO
           SET  &NSY = &STR(FMSROC2A)
           END
         WHEN (&SY = 2B) +
           DO
           SET  &NSY = &STR(FMSROC2B)
           END
         WHEN (&SY = 2C) +
           DO
           SET  &NSY = &STR(FMSROC2C)
           END
         WHEN (&SY = 3A) +
           DO
           SET  &NSY = &STR(FMSROC3A)
           END
         WHEN (&SY = 3B) +
           DO
           SET  &NSY = &STR(FMSROC3B)
           END
         WHEN (&SY = 3C) +
           DO
           SET  &NSY = &STR(FMSROC3C)
           END
         OTHERWISE  +
           DO
           WRITE  NODE NUMBER ERROR = &SY
           GOTO  ENDIT
           END
       END
       /* ------------------------------------------------------ */
       SET &WHOUSER = &STR(&NSY..&TUSER)
       /* ------------------------------------------------------ */
       IF  &NTF = &STR(YES)  THEN  +
           DO
           SET &NOTFY  =  &STR(NOTIFY(ALL))
           END
       ELSE  +
           DO
           SET &NOTFY  =  &STR(NONOTIFY)
           END
       /* ------------------------------------------------------ */
       IF    &MES = &STR(YES)  THEN  +
           DO
           SET &MESSAGE = &STR(MESSAGE)
           END
       ELSE  +
           DO
           SET &MESSAGE = &STR(       )
           END
       /* ------------------------------------------------------ */
       CONTROL MSG
       SET &XCMD = &STR( TSOEXEC TRANSMIT &WHOUSER DA(&IFILE))
       SET &XCMD = &STR(&XCMD)&STR( &NOTFY &MESSAGE )
       SET &XCMD = &STR(&XCMD) &STR( MEMBERS )
       SET &LPARN = &STR((
       SET &RPARN = )
       SET &XCMD = &STR(&XCMD)&STR(&LPARN)
       ISPEXEC LMINIT DATAID(XMITID) DATASET(&IFILE)
                                       /* GENERATE DATAID           */
       IF (&LASTCC ¬= 0)               /* CHECK THE CONDCODE        */ +
          THEN GOTO ERREXIT            /*                           */
       ISPEXEC LMOPEN DATAID(&XMITID)
                                       /* OPEN THE DATASET          */
       IF (&LASTCC ¬= 0)               /* CHECK THE CONDCODE        */ +
          THEN GOTO ERREXIT            /*                           */
       SET &DISPCMD = &STR(ISPEXEC LMMDISP DATAID(&XMITID))
       SET &DISPCMD = &DISPCMD &STR(OPTION(DISPLAY)
       SET &DISPCMD = &DISPCMD &STR( STATS(YES) COMMANDS(ANY)
       IF (&STR(&M0)  ¬= )             /* WAS A PATTERN SUPPLIED    */ +
          THEN SET &DISPCMD = &DISPCMD &STR( MEMBER(&STR(&M0))
WRITE  &DISPCMD
       SET &SAVECC = &LASTCC           /*                           */
       IF (&SAVECC = 4  )              /* D/S PROBLEM BUT NOT FATAL */ +
          THEN GOTO LOOP               /* REDISPLAY PANEL           */
       IF (&SAVECC = 8  )              /* USER HIT END              */ +
          THEN SET DONE = YES          /* SET LOOP CONTROL          */
          ELSE SET &DONE = NO          /*                           */
       IF (&SAVECC = 10 )              /* NO DATAID                 */ +
          THEN GOTO ERREXIT            /*                           */
       SET MEMSEL = NO                 /*                           */
       DO WHILE (&DONE = NO)           /*                           */
          IF (&SAVECC > 8)             /* DIS A SEVERE ERROR OCCUR  */ +
             THEN GOTO ERREXIT         /* YES - LEAVE               */
             ELSE DO                   /*                           */
                SET &DONE1 = NO        /*                           */
                DO WHILE (&DONE1 = NO)
                   IF (&ZLLCMD = S)    /* WAS MEMBER SELECTED       */ +
                      THEN DO          /* YES - ADD MEMBER TO LIST  */
                          SET &XCMD = &STR(&XCMD)&STR( &ZLMEMBER)
                          ISPEXEC LMMDISP DATAID(&XMITID) OPTION(PUT)  +
                          MEMBER(&ZLMEMBER) ZLUDATA(*XMITTED)
                          SET MEMSEL =  YES
                       END             /*                           */
                   IF (&ZLLCMD = B)    /* WAS MEMBER SELECTED       */ +
                      THEN DO          /* YES - ADD MEMBER TO LIST  */
                          ISPEXEC BROWSE MEMBER(&ZLMEMBER)             +
                                  DATAID(&XMITID)
                          SET &LASTCC  =  0
                          ISPEXEC LMMDISP DATAID(&XMITID) OPTION(PUT)  +
                          MEMBER(&ZLMEMBER) ZLUDATA(*BROWSED)
                       END             /*                           */
                   IF (&ZLLCMD = E)    /* WAS MEMBER SELECTED       */ +
                      THEN DO          /* YES - ADD MEMBER TO LIST  */
                          ISPEXEC EDIT   MEMBER(&ZLMEMBER)             +
                                  DATAID(&XMITID)
                          SET &LASTCC  =  0
                          ISPEXEC LMMDISP DATAID(&XMITID) OPTION(PUT)  +
                          MEMBER(&ZLMEMBER) ZLUDATA(*EDITED)
                       END             /*                           */
                   ISPEXEC LMMDISP DATAID(&XMITID) OPTION(GET)
                                       /* SEE IF THERE ARE MORE     */
                                       /* MEMBERS TO PROCESS        */
                   IF (&LASTCC = 8)    /*                           */ +
                      THEN SET &DONE1 = YES
                END                    /*                           */
             END                       /*                           */
WRITE  &DISPCMD
       SET &SAVECC = &LASTCC           /*                           */
       IF (&SAVECC = 8)                /* DID USER HIT END          */ +
          THEN SET &DONE = YES         /* YES - WE ARE DONE         */
       END                             /*                           */
       SET &XCMD = &STR(&XCMD)&STR(&RPARN)
       IF (&DEBUG = DEBUG)             /* IF NOT IN DEBUG MODE      */ +
           THEN DO                     /* ISSUE COMMAND             */
             WRITE &XCMD               /*                           */
             GOTO LMCLOSE              /* ISSUE COMMAND             */
           END                         /*                           */
       IF (&MEMSEL = YES)              /* DID USER SELECT MEMBERS   */ +
           THEN       &XCMD            /* YES - ISSUE COMMAND JDM   */
LMCLOSE: +
       ISPEXEC LMCLOSE DATAID(&XMITID)
                                       /* CLOSE THE DATASET         */
       ISPEXEC CONTROL DISPLAY REFRESH
       GOTO  LOOP
ERRMSG:  +                             /*                           */
  WRITE  **  SEVERE  PANEL  ERROR ***
ERREXIT: +                             /*                           */
  EXIT CODE(0)                         /*                           */
