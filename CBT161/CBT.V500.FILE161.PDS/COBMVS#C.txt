PROC 0 DEBUG
/*--------------------------------------------------------*/
/*      INVOKE COBOL-FOR-MVS SYNTAX CHECKER               */
/*--------------------------------------------------------*/
CONTROL  NOLIST   MSG PROMPT NOFLUSH
IF &DEBUG NE &STR() THEN +
   CONTROL LIST CONLIST SYMLIST
ISPEXEC CONTROL ERRORS RETURN
ISPEXEC VGET (PODSN,USERLIB,QUOTE,CBTERM) SHARED
IF &QUOTE  = &STR() THEN +
   DO
   SET  &QUOTE  = &STR(QUOTE)
   END
IF &CBTERM = &STR() THEN +
   DO
   SET  &CBTERM = &STR(TERM)
   END
/*--------------------------------------------------------*/
/*      MAKE MOST OF THE ALLOCATIONS HERE                 */
/*--------------------------------------------------------*/
CONTROL NOMSG
FREE FI(COBMTERM,COBMIN,COBMLIB,COBMPUN,  +
        COBMUT1,COBMUT2,COBMUT3,COBMUT4,COBMUT5,COBMUT6,COBMUT7)
DELETE   $TERM$.$COBMVS$
DELETE   $LIST$.$COBMVS$
CONTROL MSG
ALLOC FI(COBMLIN) DUMMY REUSE
ALLOC FI(COBMPUN) DUMMY REUSE
ALLOC FI(COBMUT1) UNIT(VIO) SPACE(1 1) CYL NEW DELETE
ALLOC FI(COBMUT2) UNIT(VIO) SPACE(1 1) CYL NEW DELETE
ALLOC FI(COBMUT3) UNIT(VIO) SPACE(1 1) CYL NEW DELETE
ALLOC FI(COBMUT4) UNIT(VIO) SPACE(1 1) CYL NEW DELETE
ALLOC FI(COBMUT5) UNIT(VIO) SPACE(1 1) CYL NEW DELETE
ALLOC FI(COBMUT6) UNIT(VIO) SPACE(1 1) CYL NEW DELETE
ALLOC FI(COBMUT7) UNIT(VIO) SPACE(1 1) CYL NEW DELETE
ALLOC FI(ISPLUSR)  DA('IGY.V2R1M0.SIGYCOMP')  SHR REUSE
ISPEXEC  LIBDEF  ISPLLIB  LIBRARY ID(ISPLUSR)
/*--------------------------------------------------------*/
/*      THIS IS WHERE THE LOOP BEGINS                     */
/*--------------------------------------------------------*/
LOOP:  +
   ISPEXEC CONTROL DISPLAY SAVE
   ISPEXEC ADDPOP COLUMN(1) ROW(1)
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ISPEXEC DISPLAY PANEL(COBMVS#P)
   SET &RC = &LASTCC
   ISPEXEC REMPOP
   IF &RC = 8 THEN GOTO QUIT
   IF  &MAXCC NE 0  THEN  GOTO ERRMSG
   SET &LASTCC = 0
   SET &MAXCC  = 0
   /*--------------------------------------------------------*/
   /*   CHECK TO SEE IF ALL THE FILES ARE REALLY THERE.      */
   /*--------------------------------------------------------*/
   IF  &SYSDSN(&PODSN)  ¬=  OK  THEN  +
       DO
       WRITE
       WRITE   ERROR: INPUT FILE DOES NOT EXIST OR MIGHT NOT
       WRITE          BE CATALOGED.
       WRITE
       GOTO LOOP
       END
   ELSE   +
       DO
       ALLOC FI(COBMIN) DA(&PODSN)  SHR REUSE
       END
   /* ---------------------------------------------------------- */
   IF &USERLIB ¬= &Z  THEN  +
      DO
      ALLOC FI(COBMLIB) DA(&USERLIB) SHR REUSE
      SET &BOOKS = &STR(,LIB)
      END
   ELSE  +
      DO
      SET &BOOKS = &STR()
      END
   /* ---------------------------------------------------------- */
   IF  &CBTERM  = &STR(BROWSE)  THEN  +
       DO
       CONTROL  NOMSG
       FREE FI(COBMTERM)
       DELETE   $TERM$.$COBMVS$
       CONTROL  MSG
       ALLOC FI(COBMTERM) DA($TERM$.$COBMVS$) NEW CATALOG +
             SPACE(1 1) TRACKS
       ALLOC FI(COBMPRT) DUMMY REUSE
       SET  &OPT1 = &STR(TERM)
       END
   IF  &CBTERM  = &STR(TERM)  THEN  +
       DO
       ALLOC FI(COBMTERM) DA(*)  REUSE
       ALLOC FI(COBMPRT) DUMMY REUSE
       SET  &OPT1 = &STR(TERM)
       END
   IF  &CBTERM  = &STR(LIST)  THEN  +
       DO
       CONTROL  NOMSG
       DELETE   $LIST$.$COBMVS$
       FREE FI(COBMPRT)
       CONTROL  MSG
       ALLOC FI(COBMPRT) DA($LIST$.$COBMVS$) NEW CATALOG +
             SPACE(5 5) TRACKS
       ALLOC FI(COBMTERM) DUMMY REUSE
       SET  &OPT1 = &STR(NOTERM)
       END
   SET &OPTIONS = &STR(&QUOTE)&STR(&BOOKS)&STR(,)&STR(&OPT1)
   /* ---------------------------------------------------------- */
   WRITE
   WRITE
   COBMVS '&OPTIONS'
   SET   &RC = &LASTCC
   WRITE
   WRITE  RETURN CODE = &RC
   /* ---------------------------------------------------------- */
   IF  &CBTERM  = &STR(BROWSE)  THEN  +
       DO
       SET &LASTCC = 0
       SET &MAXCC  = 0
       ISPEXEC BROWSE DATASET($TERM$.$COBMVS$)
       IF &LASTCC = 12 THEN +
          DO
          WRITE   BROWSE ERROR
          END
       CONTROL  NOMSG
       DELETE   $TERM$.$COBMVS$
       CONTROL  MSG
       ISPEXEC CONTROL DISPLAY RESTORE
       END
   IF  &CBTERM  = &STR(TERM)  THEN  +
       DO
       CONTROL  NOMSG
       FREE FI(COBMTERM)
       CONTROL  MSG
       END
   IF  &CBTERM  = &STR(LIST)  THEN  +
       DO
       SET &LASTCC = 0
       SET &MAXCC  = 0
       ISPEXEC BROWSE DATASET($LIST$.$COBMVS$)
       IF &LASTCC = 12 THEN +
          DO
          WRITE   BROWSE ERROR
          END
       CONTROL  NOMSG
       DELETE   $LIST$.$COBMVS$
       CONTROL  MSG
       ISPEXEC CONTROL DISPLAY RESTORE
       END
   IF &USERLIB ¬= &Z  THEN  +
      DO
      FREE  FI(COBMLIB)
      END
   GOTO LOOP
/*--------------------------------------------------------*/
/*   WE WILL BUILD THE CMD AND SAVE THE VARIABLES, WHY ?  */
/*   SHALL BE BACK AND PROBABLY TO COMPILE THE SAME CODE! */
/*--------------------------------------------------------*/
ERRMSG:  +
  WRITE  **  SEVERE  PANEL  ERROR ***
QUIT:    +
ISPEXEC VPUT (PODSN,USERLIB,CBTERM) SHARED
ISPEXEC  LIBDEF  ISPLLIB
FREE FI(ISPLUSR)
CONTROL  NOMSG
FREE FI(COBMLIN,COBMPRT,COBMTERM,COBMIN,COBMLIB,COBMPUN,  +
        COBMUT1,COBMUT2,COBMUT3,COBMUT4,COBMUT5,COBMUT6,COBMUT7)
DELETE   $LIST$.$COBMVS$
DELETE   $TERM$.$COBMVS$
CONTROL  NOLIST MSG PROMPT NOFLUSH
END
