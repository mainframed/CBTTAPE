PROC 0 SLSS(SLSS) FUNC(D) DEBUG SELECT()
/*------------------------------------------------------*/
/*     MAINTAIN OUR SLSS                                */
/*------------------------------------------------------*/
  CONTROL NOMSG PROMPT NOFLUSH
  IF &DEBUG = DEBUG THEN CONTROL NOMSG LIST CONLIST SYMLIST
/*------------------------------------------------------*/
/*  ALLOC THE SLSS TABLE PDS TO A DDNAME OF 'SLSSTBL'   */
/*  SO WE MAKE SURE THIS DOESN'T GET ACCIDENTLY STORED  */
/*  IN SOMEONE ELSES PDS.                               */
/*                                                      */
/*  I SAY THIS BECAUSE WE MUST ALSO HAVE THE PDS ALLOC  */
/*  TO THE DDNAME OF 'ISPTLIB'. THIS IS CRAZY BUT       */
/*  TBOPEN GIVES YOU NO OTHER OPTION. THE PDS IS READ   */
/*  OUT OF THE ISPTLIB CONCATENATION AND WRITTEN BACK   */
/*  TO THE SLSSTBL DDNAME.                              */
/*                                                      */
/*  NOW IF YOU ARE ISPF/PDF V1 YOU CAN NOT DO THE       */
/*  'LIBDEF' FUNCTION AND MUST HAVE THE SLSS PDS IN     */
/*  THE ISPTLIB CONCATENATION BEFORE IT IS OPEN.        */
/*                                                      */
/*                                                      */
/*------------------------------------------------------*/
  ISPEXEC LIBDEF ISPTLIB DATASET ID('$354JDM.SLSS.TABLES')
  FREE FI(SLSSTBL)
  ALLOC FI(SLSSTBL) DA('$354JDM.SLSS.TABLES') SHR
  IF &LASTCC ¬= 0 THEN  +
     DO
     WRITE  TABLE FILE ALLOCATION FAILED !!!!
     EXIT
     END
  SET &LASTCC = 0
  ISPEXEC TBOPEN &SLSS
  IF &LASTCC >0 THEN GOTO TBCREATE
     ELSE GOTO DISPLAY
TBCREATE: +
  SET &CREATE = Y
  ISPEXEC TBCREATE &SLSS KEYS(MANID)  +
      NAMES(MANID MANDASH NAME STATUS TNL TNL0 TNL1 TNL2 +
      TNL2 TNL3 TNL4 TNL5 TNL6 TNL7 +
      TNL8 TNL9 TNLA TNLB TNLC TNLD TNLE TNLF BOOK SB)
  IF &LASTCC =0 THEN GOTO ADD
      WRITE SLSS TABLE CREATION FAILED - RETURN CODE &LASTCC
      EXIT
/*------------------------------------------------------*/
/*     DISPLAY THE MENU                                 */
/*------------------------------------------------------*/
DISPLAY: +
  IF &FUNC = A THEN GOTO ADD
  SET &CREATE =
  ISPEXEC TBDISPL &SLSS PANEL(SLSS#PS)
  SET &LCC = &LASTCC
  IF &LCC = 0 THEN GOTO TESTOPT
  IF &LCC = 4 THEN GOTO UPDATE
  IF &LCC = 8 THEN GOTO CLOSE
  WRITE TABLE DISPLAY FAILED WITH RETURN CODE &LCC
  GOTO CLOSE
  /*------------------------------------------------------*/
  /*     TEST THE OPTIONS IN THE MAIN MENU                */
  /*------------------------------------------------------*/
TESTOPT:  +
  IF &STR(&OPT) = CANCEL THEN GOTO EXIT
  IF &STR(&OPT) = ADD THEN GOTO ADDS
  IF &STR(&OPT) = SORT THEN GOTO SORT
  IF &STR(&OPT) = PRT   THEN GOTO PRINT
  IF &STR(&OPT) = SAVE THEN GOTO SAVEIT
  IF &SELECT    = D THEN GOTO DELETE
  IF &SELECT    = S THEN GOTO REFRESH
  IF &STR(&OPT) = THEN GOTO DISPLAY
  SET &MANID    = &SUBSTR(1:9,&STR(&OPT      ))
  ISPEXEC TBGET &SLSS
  SET &LCC = &LASTCC
  IF &LCC = 0 THEN GOTO REFRESH
  IF &LCC = 8 THEN WRITE &OPT ENTRY NOT IN TABLE
  GOTO DISPLAY
  /*------------------------------------------------------*/
  /*     NOW SAVE THE SLSS                                */
  /*------------------------------------------------------*/
SAVEIT:  +
  ISPEXEC TBSAVE &SLSS PAD(30) LIBRARY(SLSSTBL)
  GOTO DISPLAY
  /*------------------------------------------------------*/
  /*     WE WANT TO ADD AN ENTRY TO THE SLSS              */
  /*------------------------------------------------------*/
ADDS:  +
  ISPEXEC TBVCLEAR &SLSS
ADD:     +
  ISPEXEC DISPLAY PANEL(SLSS#PA)
  IF &LASTCC >0 THEN GOTO NOADD
  SET &MANDASH = &SUBSTR(11:12,&STR(&MANID         ))
  SET &MANID   = &SUBSTR(1:9,&STR(&MANID           ))
  SET &BOOK    = &SUBSTR(1:4,&STR(&BOOK            ))
  SET &SB      = &SUBSTR(1:2,&STR(&SB              ))
  ISPEXEC TBADD &SLSS
  IF &LASTCC  ¬= 0 THEN WRITE &MANID ENTRY EXISTS USE REFRESH
  IF &SELECT   = R THEN GOTO DISPLAY
  GOTO ADD
NOADD:  +
  SET &FUNC = D
  GOTO DISPLAY
  /*------------------------------------------------------*/
  /*     DELETE AN ENTRY TO SLSS                          */
  /*------------------------------------------------------*/
DELETE:  +
  ISPEXEC TBDELETE &SLSS
  GOTO DISPLAY
  /*------------------------------------------------------*/
  /*     UPDATE AN ENTRY TO SLSS                          */
  /*------------------------------------------------------*/
UPDATE:  +
  IF &SELECT = D THEN GOTO DELETE
  IF &SELECT = R THEN GOTO REFRESH
  ISPEXEC TBMOD &SLSS
  GOTO DISPLAY
  /*------------------------------------------------------*/
  /*     REFRESH AN ENTRY                                 */
  /*------------------------------------------------------*/
REFRESH:  +
  ISPEXEC DISPLAY PANEL(SLSS#PA)
  IF &LASTCC >0 THEN GOTO DISPLAY
  SET &MANDASH = &SUBSTR(11:12,&STR(&MANID         ))
  SET &MANID   = &SUBSTR(1:9,&STR(&MANID           ))
  SET &BOOK    = &SUBSTR(1:4,&STR(&BOOK            ))
  SET &SB      = &SUBSTR(1:2,&STR(&SB              ))
  ISPEXEC TBMOD &SLSS
  GOTO DISPLAY
  /*------------------------------------------------------*/
  /*     HERE IS WHERE WE WILL SORT THE SLSS BY MANUAL    */
  /*     NUMBER.                                          */
  /*------------------------------------------------------*/
SORT: +
  ISPEXEC TBTOP &SLSS
  ISPEXEC TBSKIP &SLSS NUMBER(+1)
  CONTROL NOMSG
  FREE FI(SORTIN)
  CONTROL MSG
  ALLOC FI(SORTIN) UNIT(V3380) NEW SPA(10,10) TR
  OPENFILE SORTIN OUTPUT
READ:  +
  SET &AB   = &SUBSTR(1:3,&STR(-&MANDASH    ))
  SET &A    = &SUBSTR(1:9,&STR(&MANID        ))
  IF &TNL   = THEN SET &TNL = N
  SET &B    = &SUBSTR(1:1,&STR(&TNL ))
  IF &STR(&STATUS) = THEN SET &STATUS = H
      ELSE SET &STATUS = O
  SET &C    = &SUBSTR(1:7,&STR(&STATUS      ))
  SET &D    = &STR(&BOOK)
  SET &E    = &STR(&SB)
  SET &SORTIN = &STR(&A&AB  &B &C &D &E &NAME)
  PUTFILE SORTIN
  /*------------------------------------------------------*/
  /*     WE NEED TO TEST TO SEE IF THERE IS A TNL. IF SO  */
  /*     THEN WE MUST INCLUDE THEM.                       */
  /*------------------------------------------------------*/
    IF &TNL = N THEN GOTO READ1SKP
       IF &STR(&TNL0) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNL0)
       PUTFILE SORTIN
       IF &STR(&TNL1) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNL1)
       PUTFILE SORTIN
       IF &STR(&TNL2) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNL2)
       PUTFILE SORTIN
       IF &STR(&TNL3) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNL3)
       PUTFILE SORTIN
       IF &STR(&TNL4) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNL4)
       PUTFILE SORTIN
       IF &STR(&TNL5) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNL5)
       PUTFILE SORTIN
       IF &STR(&TNL6) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNL6)
       PUTFILE SORTIN
       IF &STR(&TNL7) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNL7)
       PUTFILE SORTIN
       IF &STR(&TNL8) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNL8)
       PUTFILE SORTIN
       IF &STR(&TNL9) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNL9)
       PUTFILE SORTIN
       IF &STR(&TNLA) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNLA)
       PUTFILE SORTIN
       IF &STR(&TNLB) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNLB)
       PUTFILE SORTIN
       IF &STR(&TNLC) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNLC)
       PUTFILE SORTIN
       IF &STR(&TNLD) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNLD)
       PUTFILE SORTIN
       IF &STR(&TNLE) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNLE)
       PUTFILE SORTIN
       IF &STR(&TNLF) = THEN GOTO SKIPIT
       SET &SORTIN = &STR(&A&AB.A &TNLF)
       PUTFILE SORTIN
SKIPIT: +
  SET &SORTIN = &STR(&A&AB.Z END OF TNLS)
  PUTFILE SORTIN
READ1SKP:  +
  ISPEXEC TBSKIP &SLSS NUMBER(+1)
  SET &LCC = &LASTCC
  IF &LCC = 0 THEN GOTO READ
  IF &LCC = 8 THEN GOTO SORTIT
  WRITE TPSKIP FAILED WITH &LCC DURING SORT ROUTINE
  GOTO CLOSE
/*------------------------------------------------------*/
/*   WE MUST NOW SORT THE FILE. FREE ALL THE FILES SO   */
/*   WE CAN ALLOCATE THEM CORRECTLY.                    */
/*------------------------------------------------------*/
SORTIT:  +
  CLOSFILE SORTIN
 CONTROL NOMSG
FREE FI(SYSPRINT SYSIN SORTMSG SORTOUT SORTWK01 +
       SORTWK02 SORTWK03 SORTLIB  ATTR80 SYSOUT)
CONTROL MSG
ATTR ATTR80 BLKSIZE(6160) LRECL(80) RECFM(F B)
ALLOC FI(SORTOUT) UNIT(V3380) NEW SPA(10,10) TR +
        USI(ATTR80)
ALLOC FI(SORTLIB) DS('SYS1.SORTLIB') SHR
ALLOC FI(SORTMSG) DUMMY
ALLOC FI(SORTWK01) UNIT(SYSDA) SPA(10,10) TR
ALLOC FI(SORTWK02) UNIT(SYSDA) SPA(10,10) TR
ALLOC FI(SORTWK03) UNIT(SYSDA) SPA(10,10) TR
ALLOC FI(SYSPRINT) DUMMY
ALLOC FI(SYSOUT)  DUMMY
ALLOC FI(SYSIN) UNIT(V3380) SPA(1) TR
  OPENFILE SYSIN OUTPUT
  SET &SYSIN = &STR( SORT FIELDS=(2,12,CH,A) )
  PUTFILE SYSIN
  SET &SYSIN = &STR( END )
  PUTFILE SYSIN
  CLOSFILE SYSIN
WRITE ****** SORT BEING INVOKED ******
CALL 'SYS1.PPLINK(SORT)'
FREE FI(SYSPRINT ATTR80 SYSIN SORTMSG SORTIN +
       SORTWK01 SORTWK02 SORTWK03 SORTLIB SYSOUT)
WRITE ******    SORT ENDED   *********
  OPENFILE SORTOUT
  SET &CLOSEFLG = N
  SET &GTNL = N
  SET &CNT = 0
  ISPEXEC TBTOP &SLSS
CLEARIT: +
  ISPEXEC TBSKIP &SLSS NUMBER(+1)
  IF &LASTCC ¬= 0 THEN GOTO CLEAROK
  ISPEXEC TBDELETE &SLSS
  GOTO CLEARIT
CLEAROK: +
ERROR DO
    SET &LCC = &LASTCC
    IF &LCC ¬= 400 THEN RETURN
    SET &CLOSEFLG = Y
    END
READIN:  +
  IF &CLOSEFLG = Y THEN GOTO ENDSORT
  GETFILE SORTOUT
  IF &SUBSTR(15:17,&STR(&SORTOUT)) = END THEN GOTO TBSKIP
  IF &GTNL = Y THEN GOTO SETTNL
  SET &MANID = &SUBSTR(1:9,&STR(&SORTOUT))
    SET &MANDASH = &SUBSTR(11:12,&STR(&SORTOUT))
  SET &TNL   = &SUBSTR(15,&STR(&SORTOUT))
     IF &TNL = N THEN SET &TNL =
         ELSE SET &TNL = Y
  SET &STATUS = &SUBSTR(17,&STR(&SORTOUT))
     IF &STATUS = H THEN SET &STATUS =
        ELSE SET &STATUS = &STR(*)
  SET &BOOK   = &SUBSTR(25:28,&STR(&SORTOUT))
  SET &SB     = &SUBSTR(30:31,&STR(&SORTOUT))
  SET &NAME   = &SUBSTR(33:80,&STR(&SORTOUT))
   IF &TNL ¬= Y THEN GOTO TBSKIP
      ELSE SET &GTNL = Y
     GOTO READIN
SETTNL: +
   IF &CNT = 0 THEN SET &TNL0 = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 1 THEN SET &TNL1 = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 2 THEN SET &TNL2 = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 3 THEN SET &TNL3 = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 4 THEN SET &TNL4 = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 5 THEN SET &TNL5 = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 6 THEN SET &TNL6 = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 7 THEN SET &TNL7 = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 8 THEN SET &TNL8 = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 9 THEN SET &TNL9 = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 10 THEN SET &TNLA = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 11 THEN SET &TNLB = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 12 THEN SET &TNLC = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 13 THEN SET &TNLD = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 14 THEN SET &TNLE = &SUBSTR(15:23,&STR(&SORTOUT))
   IF &CNT = 15 THEN SET &TNLF = &SUBSTR(15:23,&STR(&SORTOUT))
   SET &CNT = &CNT + 1
   GOTO READIN
TBSKIP: +
  SET &CNT = 0
     SET &GTNL = N
  ISPEXEC TBADD &SLSS
  ISPEXEC TBVCLEAR &SLSS
  GOTO READIN
ENDSORT: +
ERROR OFF
    ISPEXEC TBTOP &SLSS
    CLOSFILE SORTOUT
    FREE FI(SORTOUT)
    GOTO DISPLAY
/*------------------------------------------------------*/
/*    PRINT THE FILE WE HAVE CREATED WITH ALL THE DATA  */
/*    WE HAVE. DO THIS WITH A BATCH JOB AND COBOL PGM.  */
/*    MORE EFFICIENT!!!                                 */
/*------------------------------------------------------*/
PRINT: +
  CONTROL NOMSG
  ATTR FB LRECL(80) RECFM(F B) BLKSIZE(6160)
  ALLOC FI(REPORT) DA(TABLE.DATA) NEW CATALOG UNIT(3380) +
        VOL(USER60) SPACE(1,1) TR  USING(FB)
  OPENFILE REPORT OUTPUT
  ISPEXEC TBTOP &SLSS
  ISPEXEC TBSKIP &SLSS NUMBER(+1)
  PNEXT: +
     SET &A   = &SUBSTR(1:9,&STR(&MANID           ))
     SET &B   = &SUBSTR(1:2,&STR(&MANDASH         ))
     SET &C   = &SUBSTR(1:1,&STR(&STATUS          ))
     SET &D   = &SUBSTR(1:4,&STR(&BOOK            ))
     SET &E   = &SUBSTR(1:2,&STR(&SB              ))
     SET &FF  = &SUBSTR(1:1,&STR(&TNL             ))
     IF  &FF  = Y THEN SET &F = Y
         ELSE  SET &F = N
     SET &G   = &STR(&NAME)
     SET &REPORT = &STR(&A&B&C&D&E&F&G)
     PUTFILE REPORT
  PREAD: +
     ISPEXEC TBSKIP &SLSS NUMBER(+1)
     SET &LCC   = &LASTCC
     IF  &LCC   = 0  THEN GOTO PNEXT
     IF  &LCC   = 8  THEN GOTO PCLOSE
     WRITE  TABLE PRINT FAILED WITH RC = &LCC
     GOTO CLOSE
  PCLOSE: +
     CLOSFILE REPORT
     FREE FI(REPORT FB)
     ISPEXEC FTOPEN   TEMP
     ISPEXEC FTINCL   SLSS#SP
     ISPEXEC FTCLOSE
     ISPEXEC VGET     ZTEMPF
     SUBMIT  '&ZTEMPF'
     CONTROL MSG
     GOTO DISPLAY
CLOSE: +
  ISPEXEC TBCLOSE &SLSS PAD(30) LIBRARY(SLSSTBL)
  GOTO WRAPUP
EXIT: +
  ISPEXEC TBEND &SLSS
WRAPUP: +
  FREE FI(SLSSTBL)
  EXIT
