PROC 0 DEBUG
/* ----------------------------------------------------------- */
/*      THIS A CLIST TO SETUP ALL THE FILES DYNAMICALLY FOR    */
/*      DOING A COBOL V2 SYNTAX CHECK.                         */
/*   05/09/92 - JDM - RECODED TO ELIMINATE SKELETON FILE       */
/* ----------------------------------------------------------- */
CONTROL NOLIST NOMSG  NOFLUSH  PROMPT
IF  &DEBUG Â¬= &STR() THEN  +
    CONTROL  MSG  LIST  CONLIST  SYMLIST
ISPEXEC  CONTROL ERRORS RETURN
ISPEXEC VGET (PODSN,USERLIB,APOST,SEQ,FLAGW,NONUM) SHARED
IF &APOST  = &STR() THEN +
   DO
   SET  &APOST = &STR(APOST)
   END
IF &SEQ    = &STR() THEN +
   DO
   SET  &SEQ = &STR(NOSEQ)
   END
IF &FLAGW  = &STR() THEN +
   DO
   SET  &FLAGW = &STR(FLAGW)
   END
IF &NONUM  = &STR() THEN +
   DO
   SET  &NONUM = &STR(NONUM)
   END
ALLOC FI($$COB$$)  DA('SYS1.COB2COMP') SHR REUSE
ISPEXEC  LIBDEF  ISPLLIB  LIBRARY ID($$COB$$)
/* ----------------------------------------------------------- */
/*      THNGS ARE NOW "RIGHT", LET'S DO                        */
/* ----------------------------------------------------------- */
/*                                                             */
LOOP:  +
      ISPEXEC CONTROL DISPLAY REFRESH
      ISPEXEC ADDPOP COLUMN(1) ROW(1)
      SET  &LASTCC = 0
      SET  &MAXCC  = 0
      ISPEXEC DISPLAY PANEL(COB2#P) CURSOR(PODSN)
      SET  &RC = &LASTCC
      ISPEXEC REMPOP
      IF &RC = 8 THEN GOTO ALLDONE
      IF &MAXCC NE 0  THEN  GOTO ERRMSG
      SET  &LASTCC = 0
      SET  &MAXCC  = 0
      /* ----------------------------------------- */
      /*      PROCESS THE TRANSACTION              */
      /* ----------------------------------------- */
      IF  &USERLIB = &STR( ) THEN +
          DO
          SET &ULIB = &STR( )
          END
      ELSE  +
          DO
          SET &ULIB = &STR(LIB(&USERLIB))
          END
      COB2CHK   &PODSN &APOST &SEQ  &ULIB +
                &FLAGW &NONUM
      GOTO LOOP
/*                                                             */
/* ----------------------------------------------------------- */
/*      TIDY UP AND GO HOME.                                   */
/* ----------------------------------------------------------- */
ERRMSG:  +
  WRITE  **  SEVERE  PANEL  ERROR ***
ALLDONE: +
ISPEXEC VPUT (PODSN,USERLIB,APOST,SEQ,FLAGW,NONUM) SHARED
ISPEXEC  LIBDEF  ISPLLIB
FREE FI($$COB$$)
EXIT
