000010 IDENTIFICATION DIVISION.                                               84
000020 PROGRAM-ID.           SLSSPRT.                                         05
000030 AUTHOR.               CPT PETERSEN.
000040 INSTALLATION.         AFISC/ADDS NORTON AFB CA  AV 876 6681
000050 DATE-WRITTEN.         FEB 12,1985
000060 DATE-COMPILED.
000070 REMARKS.
000080
000090     TITLE       SLSS DATA ENTRY SYSTEM
000100
000110     FUNCTION    PROVIDE DATA PRINT BY SUBJECT CAPABILITY FOR THE
000120                 SLSS LIBRARY SYSTEM.
000130
000140     SOURCE      LOCATED AT TXTJJP.SOURCE.DATA(SLSSPRNT)
000150
000160     LMOD        LOCATED AT SYS1.SPROGLIB(SLSSPRNT)
000170
000180 ENVIRONMENT DIVISION.
000190 INPUT-OUTPUT SECTION.
000200 FILE-CONTROL.
000210     SELECT SORT-FILE ASSIGN TO UT-S-SORTFILE.
000220     SELECT PRINT-FILE ASSIGN TO UT-S-PRNTFILE.
000230     SELECT SUBJECTS-FILE ASSIGN TO UT-S-SUBJECTS.
000240     SELECT MANUALS-IN ASSIGN TO UT-S-MANUALS.
000250
000260 DATA DIVISION.
000270 FILE SECTION.
000280 FD  MANUALS-IN
000290     RECORDING MODE IS F
000300     BLOCK CONTAINS 0 RECORDS
000310     LABEL RECORDS ARE STANDARD
000320     DATA RECORD IS MANUAL-REC.
000330 01  MANUAL-REC               PIC X(80).
000340
000350 FD  SUBJECTS-FILE
000360     RECORDING MODE IS F
000370     BLOCK CONTAINS 0 RECORDS
000380     LABEL RECORDS ARE STANDARD
000390     DATA RECORD IS SUBJECT-REC.
000400 01  SUBJECT-REC               PIC X(80).
000410
000420 FD  PRINT-FILE
000430     RECORDING MODE IS F
000440     BLOCK CONTAINS 0 RECORDS
000450     LABEL RECORDS ARE STANDARD
000460     DATA RECORD IS P-REC.
000470 01  P-REC                     PIC X(133).
000480
000490 SD  SORT-FILE
000500     DATA RECORD IS SORT-REC.
000510 01  SORT-REC.
000520     02  SORT-MANID            PIC X(9).
000530     02  SORT-MANDASH          PIC XX.
000540     02  SORT-MAN-STATUS       PIC X.
000550     02  SORT-BOOK-NUMBER      PIC X(4).
000560     02  SORT-MAN-SUBJECT      PIC XX.
000570     02  SORT-TNL              PIC X.
000580     02  SORT-MAN-NAME         PIC X(59).
000590     02  FILLER                PIC XX.
000600
000610 WORKING-STORAGE SECTION.
000620 77  PAGE-CNTR                 PIC 9(6)  COMP-3 VALUE 0.
000630 77  SORT-EOF                  PIC X     VALUE '0'.
000640 77  SUBJECT-EOF               PIC X     VALUE '0'.
000650 77  SUBJECT-FOUND             PIC X     VALUE '0'.
000660 77  SAVE-SUBJECT-CODE         PIC XX    VALUE SPACES.
000670 77  LINE-COUNT                PIC 9(3)  COMP-3 VALUE 0.
000680
000690 01  SUBJECT-NAMES.
000700     02  SUBJECT-CODE          PIC XX.
000710     02  SUBJECT-NAME          PIC X(78).
000720
000730 01  PRINT-REC.
000740     02  FILLER                PIC X     VALUE SPACE.
000750     02  PRINT-BOOK-NUMBER     PIC X(4)  VALUE SPACES.
000760     02  FILLER                PIC X     VALUE SPACE.
000770     02  PRINT-MANID           PIC X(9)  VALUE SPACES.
000780     02  FILLER                PIC X     VALUE '-'.
000790     02  PRINT-MANDASH         PIC XX    VALUE SPACES.
000800     02  FILLER                PIC XX    VALUE SPACES.
000810     02  PRINT-MAN-SUBJECT     PIC XX    VALUE SPACES.
000820     02  FILLER                PIC XXX   VALUE SPACES.
000830     02  PRINT-TNL             PIC X     VALUE SPACE.
000840     02  FILLER                PIC XXX   VALUE SPACES.
000850     02  PRINT-MAN-STATUS      PIC X     VALUE SPACE.
000860     02  FILLER                PIC X     VALUE SPACE.
000870     02  PRINT-MAN-NAME        PIC X(59) VALUE SPACES.
000880     02  FILLER                PIC X(43) VALUE SPACES.
000890
000900 01  HEADER2-REC.
000910     02  FILLER                PIC X     VALUE '1'.
000920     02  FILLER                PIC X(51) VALUE
000930     'SYSTEM LIBRARY SUBSCRIPTION SERVICE SUBJECT LISTING'.
000940     02  FILLER                PIC X(5)  VALUE SPACES.
000950     02  PRINT-DATE            PIC X(8)  VALUE SPACES.
000960     02  FILLER                PIC X(7)  VALUE ' PAGE: '.
000970     02  PAGE-NUMBER           PIC ZZZZZ9.
000980     02  FILLER                PIC X(55) VALUE SPACES.
000990
001000 01  HEADER3-REC.
001010     02  FILLER                PIC X     VALUE '0'.
001020     02  FILLER                PIC X(9)  VALUE 'SUBJECT:'.
001030     02  PRINT-SUBJECT         PIC X(78) VALUE SPACES.
001040     02  FILLER                PIC X(45) VALUE SPACES.
001050
001060 01  HEADER4-REC.
001070     02  FILLER                PIC X     VALUE '0'.
001080     02  FILLER                PIC X(36) VALUE
001090     'BOOK   MANUAL #   SUBJ TNL ST   NAME'.
001100     02  FILLER                PIC X(96) VALUE SPACES.
001110
001120 01  HEADER5-REC.
001130     02  FILLER                PIC X     VALUE '0'.
001140     02  FILLER                PIC X(132) VALUE SPACES.
001150
001160 PROCEDURE DIVISION.
001170 1000-INITIALIZE.
001180
001190*    SORT RECORDS
001200
001210     SORT SORT-FILE ON ASCENDING KEY SORT-MAN-SUBJECT
001220                    SORT-MANID   SORT-MANDASH
001230                    USING MANUALS-IN
001240                    OUTPUT PROCEDURE SORT-OUT.
001250
001260     STOP RUN.
001270
001280 SORT-OUT SECTION.
001290     OPEN OUTPUT PRINT-FILE.
001300     OPEN INPUT SUBJECTS-FILE.
001310     MOVE CURRENT-DATE TO PRINT-DATE.
001320     PERFORM RETURN-SORT.
001330     PERFORM PRINT-SORT UNTIL SORT-EOF = '1'.
001340     CLOSE PRINT-FILE.
001350     CLOSE SUBJECTS-FILE.
001360 SORT-OUT-EXIT. EXIT.
001370
001380 RETURN-SORT.
001390     RETURN SORT-FILE AT END MOVE '1' TO SORT-EOF.
001400
001410 PRINT-SORT.
001420     IF SORT-MAN-SUBJECT NOT = SAVE-SUBJECT-CODE
001430        THEN MOVE '0' TO SUBJECT-FOUND
001440             MOVE SORT-MAN-SUBJECT TO SAVE-SUBJECT-CODE
001450             PERFORM READ-SUBJECT-FILE UNTIL SUBJECT-FOUND = '1'
001460             PERFORM PRINT-HEADERS.
001470     MOVE SORT-MANID       TO PRINT-MANID.
001480     MOVE SORT-MANDASH     TO PRINT-MANDASH.
001490     MOVE SORT-MAN-NAME    TO PRINT-MAN-NAME.
001500     MOVE SORT-MAN-STATUS  TO PRINT-MAN-STATUS.
001510     MOVE SORT-BOOK-NUMBER TO PRINT-BOOK-NUMBER.
001520     MOVE SORT-MAN-SUBJECT TO PRINT-MAN-SUBJECT.
001530     MOVE SORT-TNL         TO PRINT-TNL.
001540     ADD 1 TO LINE-COUNT.
001550     IF LINE-COUNT > 45 THEN PERFORM PRINT-HEADERS
001560                             MOVE 1 TO LINE-COUNT.
001570     WRITE P-REC FROM PRINT-REC.
001580     PERFORM RETURN-SORT.
001590
001600 READ-SUBJECT-FILE.
001610     IF SUBJECT-EOF NOT = '1'
001620        THEN IF SUBJECT-CODE < SORT-MAN-SUBJECT
001630        THEN READ SUBJECTS-FILE INTO SUBJECT-NAMES AT END
001640             MOVE '1' TO SUBJECT-EOF.
001650     IF SUBJECT-EOF = '1'
001660        THEN MOVE HIGH-VALUES TO SUBJECT-CODE
001670             MOVE SPACES TO SUBJECT-NAME.
001680     IF SUBJECT-CODE > SORT-MAN-SUBJECT
001690        THEN MOVE SPACES TO PRINT-SUBJECT
001700             MOVE '1' TO SUBJECT-FOUND.
001710     IF SUBJECT-CODE = SORT-MAN-SUBJECT
001720        THEN MOVE '1' TO SUBJECT-FOUND
001730             MOVE SUBJECT-NAME TO PRINT-SUBJECT.
001740
001750 PRINT-HEADERS.
001760     ADD 1 TO PAGE-CNTR.
001770     MOVE PAGE-CNTR TO PAGE-NUMBER.
001780     WRITE P-REC FROM HEADER2-REC.
001790     WRITE P-REC FROM HEADER3-REC.
001800     WRITE P-REC FROM HEADER4-REC.
001810     WRITE P-REC FROM HEADER5-REC.
001820     MOVE 0 TO LINE-COUNT.
001830
