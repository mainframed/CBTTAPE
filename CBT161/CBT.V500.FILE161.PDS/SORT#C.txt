PROC 0 DEBUG
/*--------------------------------------------------------*/
/*      ISPF/PDF CLIST FOR DOING INTERACTIVE SORTS        */
/*      04/30/99 - JDM - DRIVE DIALOG WITH CLIST          */
/*--------------------------------------------------------*/
CONTROL  NOMSG NOLIST NOFLUSH PROMPT
IF  &DEBUG Â¬= &STR()  THEN   +
    CONTROL MSG LIST SYMLIST CONLIST
ISPEXEC CONTROL ERRORS RETURN
FREE FI(SYSPRINT SYSIN SYSOUT SORTIN SORTOUT    +
        SORTWK01 SORTWK02 SORTWK03 SORTWK04)
ALLOC FI(SYSOUT) DUMMY
ISPEXEC VGET (SDSNIN,SDSNOUT,CC1)
/*--------------------------------------------------------*/
/*      THIS IS WHERE THE LOOP BEGINS                     */
/*--------------------------------------------------------*/
LOOP:  +
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ISPEXEC ADDPOP COLUMN(1) ROW(1)
   ISPEXEC DISPLAY PANEL(SORT#P) CURSOR(SDSNIN)
   SET &RC = &LASTCC
   ISPEXEC REMPOP
   IF &RC = 8 THEN GOTO ALLDONE
   SET &LASTCC = 0
   SET &MAXCC  = 0
   ALLOC FI(SORTIN) DA(&SDSNIN) SHR
   ALLOC FI(SORTOUT) DA(&SDSNOUT) SHR
   ALLOC FI(SYSPRINT) DA(*)
   ISPEXEC FTOPEN TEMP
   ISPEXEC FTINCL SORT#S
   ISPEXEC FTCLOSE
   ISPEXEC VGET   ZTEMPF
   ALLOC FI(SYSIN) DA('&ZTEMPF') SHR REUSE
   WRITE
   WRITE     *-------------------------*
   WRITE     *                         *
   WRITE     *   SORT BEING INVOKED    *
   SET &LASTCC = 0
   /* ------------------------------------------------- */
   /*      INVOKE THE FRONT-END FOR SORT SINCE WE       */
   /*      DO NOT WANT THEM TO USE THE TSO "CALL"       */
   /* ------------------------------------------------- */
   SORTLNK
   IF &LASTCC  = 0 THEN  +
      DO
      WRITE     *  SORT WAS SUCCESSFUL    *
      END
   ELSE +
      DO
      WRITE     *  SORT WAS UNSUCCESSFUL  *
      END
      WRITE     *      SORT ENDED         *
      WRITE     *-------------------------*
      WRITE
      GOTO LOOP
ALLDONE:  +
  FREE FI(SYSOUT SORTIN SORTOUT    +
        SORTWK01 SORTWK02 SORTWK03 SORTWK04)
  ALLOC FI(SYSIN)    DA(*) REUSE
  ALLOC FI(SYSPRINT) DA(*) REUSE
  EXIT CODE(0)
