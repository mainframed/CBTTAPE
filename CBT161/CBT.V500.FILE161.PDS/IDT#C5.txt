PROC 0 DEBUG
/* ----------------------------------------------------------------- */
/*     JIM MARSHALL                                                  */
/*     TRANSMIT / RECEIVE SYSTEM                                     */
/*     'TRANSMIT MEMBERS OF A PDS TO TSO  USER'                      */
/* ----------------------------------------------------------------- */
CONTROL NOFLUSH NOMSG
IF &DEBUG ¬=  &STR() THEN +
   CONTROL LIST MSG NOFLUSH PROMPT CONLIST SYMLIST
SET &NSY     = &STR()
SET &SY      = &STR()
SET &TUSER   = &STR()
SET &NTF     = &STR(YES)
SET &MSN     = &STR(NO)
SET &DOMSG   = &STR()
SET &WHOUSER = &STR()
SET &M1      = &STR()
SET &M2      = &STR()
SET &M3      = &STR()
SET &M4      = &STR()
SET &M5      = &STR()
SET &M6      = &STR()
SET &M7      = &STR()
SET &M8      = &STR()
SET &M9      = &STR()
SET &MA      = &STR()
SET &MB      = &STR()
SET &MC      = &STR()
SET &MD      = &STR()
SET &ME      = &STR()
SET &MF      = &STR()
ISPEXEC CONTROL ERRORS RETURN
LOOP:  +
       ISPEXEC CONTROL DISPLAY REFRESH
       ISPEXEC ADDPOP COLUMN(2) ROW(2)
       SET &LASTCC = 0
       SET &MAXCC  = 0
       ISPEXEC DISPLAY PANEL(IDT#P5)
       SET  &RC = &LASTCC
       ISPEXEC REMPOP
       IF &RC = 8 THEN GOTO ENDIT
       IF  &MAXCC NE 0  THEN +
           DO
           SET &ENUM = &STR(C5A)
           GOTO ERRMSG
           END
      /* -------------------------------------------------------- */
      /*   NOW WE WILL CHECK TO SEE IF THE FILE EXISTS            */
      /* -------------------------------------------------------- */
       SET &LASTCC = 0
       IF  &SYSDSN(&IFILE)  ¬= OK  THEN +
           DO
           WRITE
           WRITE DSN=&IFILE   DOES NOT EXIST OR IS
           WRITE NOT CATALOGED. PLEASE CORRECT !
           WRITE
           GOTO LOOP
           END
       /*--------------------------------------------------------*/
       /*    SINCE THE FILE EXISTS, WE NEED TO CONSTRUCT THE     */
       /*    NODE NAME TRANSPARENT TO THE USER.                  */
       /*--------------------------------------------------------*/
       SET   &LASTCC = 0
       SELECT
         WHEN (&SY = 1A) +
           DO
           SET  &NSY = &STR(FMSROC1A)
           END
         WHEN (&SY = 1B) +
           DO
           SET  &NSY = &STR(FMSROC1B)
           END
         WHEN (&SY = 1C) +
           DO
           SET  &NSY = &STR(FMSROC1C)
           END
         WHEN (&SY = 1E) +
           DO
           SET  &NSY = &STR(FMSROC1E)
           END
         WHEN (&SY = 1F) +
           DO
           SET  &NSY = &STR(FMSROC1F)
           END
         WHEN (&SY = 2A) +
           DO
           SET  &NSY = &STR(FMSROC2A)
           END
         WHEN (&SY = 2B) +
           DO
           SET  &NSY = &STR(FMSROC2B)
           END
         WHEN (&SY = 2C) +
           DO
           SET  &NSY = &STR(FMSROC2C)
           END
         WHEN (&SY = 3A) +
           DO
           SET  &NSY = &STR(FMSROC3A)
           END
         WHEN (&SY = 3B) +
           DO
           SET  &NSY = &STR(FMSROC3B)
           END
         WHEN (&SY = 3C) +
           DO
           SET  &NSY = &STR(FMSROC3C)
           END
         OTHERWISE  +
           DO
           WRITE  NODE NUMBER ERROR = &SY
           GOTO  ENDIT
           END
       END
       /*--------------------------------------------------------*/
       /*   NOW WE WILL FINISH BUILDING THE COMMAND.             */
       /*--------------------------------------------------------*/
       SET &WHOUSER = &STR(&NSY..&TUSER)
       /*--------------------------------------------------------*/
       IF  &NTF = &STR(YES)  THEN  +
           DO
           SET &NOTFY  =  &STR(NOTIFY(ALL))
           END
       ELSE  +
           DO
           SET &NOTFY  =  &STR(NONOTIFY)
           END
       /*--------------------------------------------------------*/
       IF  &MSN = &STR(YES)   THEN  +
           DO
           SET &DOMSG = &STR(MSG)
           END
       ELSE +
           DO
           SET &DOMSG = &STR()
           END
       /*--------------------------------------------------------*/
       CONTROL MSG
       TSOEXEC TRANSMIT &WHOUSER DA(&IFILE) &NOTFY  &DOMSG +
               MEMBERS(&M0 &M1 &M2 &M3 &M4 &M5 &M6 &M7 +
                       &M8 &M9 &MA &MB &MC &MD &ME &MF)
       ISPEXEC CONTROL DISPLAY REFRESH
       GOTO  LOOP
ERRMSG:  +
       WRITE  **  SEVERE PANEL - &ENUM   **
ENDIT:   +
       EXIT CODE(0)
