PROC 0 DEBUG
/* ----------------------------------------------------------------- */
/*     JIM MARSHALL                                                  */
/*     TRANSMIT / RECEIVE SYSTEM                                     */
/*     'TRANSMIT A FILE TO A TSO OR VM/CMS USER'                     */
/* ----------------------------------------------------------------- */
CONTROL NOFLUSH NOMSG
IF &DEBUG ¬=  &STR() THEN +
   CONTROL LIST MSG NOFLUSH PROMPT CONLIST SYMLIST
SET &NSY    =  &STR()
SET &SY     =  &STR()
SET &NTF    =  &STR(YES)
SET &MSN    =  &STR(NO)
SET &DOMSG  =  &STR()
SET &TUSER  =  &STR()
SET &IFILE  =  &STR()
SET &WHOUSER = &STR()
SET &TYP     = &STR(SEQ)
SET &NOTFY  =  &STR()
LOOP:  +
       ISPEXEC CONTROL DISPLAY REFRESH
       ISPEXEC ADDPOP COLUMN(2) ROW(2)
       SET &LASTCC = 0
       SET &MAXCC  = 0
       ISPEXEC DISPLAY PANEL(IDT#P2)
       SET  &RC = &LASTCC
       ISPEXEC REMPOP
       IF &RC = 8 THEN GOTO ENDIT
       IF  &MAXCC NE 0  THEN +
           DO
           SET &ENUM = &STR(C2A)
           GOTO ERRMSG
           END
       /*--------------------------------------------------------*/
       /*    NOW WE WANT TO CHECK THE EXISTENCE OF THE FILE, IF  */
       /*    IT DOES NOT EXIST, THEN WE WILL TELL THE USER.      */
       /*--------------------------------------------------------*/
       SET   &LASTCC = 0
       IF    &SYSDSN(&IFILE) ¬= OK  THEN +
             DO
             WRITE
             WRITE &IFILE
             WRITE DOES NOT EXIST OR IS NOT CATALOGED.
             WRITE PLEASE CORRECT.
             WRITE
             GOTO LOOP
             END
       /*--------------------------------------------------------*/
       /*    SINCE THE FILE EXISTS, WE NEED TO CONSTRUCT THE     */
       /*    NODE NAME TRANSPARENT TO THE USER.                  */
       /*--------------------------------------------------------*/
       SET   &LASTCC = 0
       SELECT
         WHEN (&SY = 1A) +
           DO
           SET  &NSY = &STR(FMSROC1A)
           END
         WHEN (&SY = 1B) +
           DO
           SET  &NSY = &STR(FMSROC1B)
           END
         WHEN (&SY = 1C) +
           DO
           SET  &NSY = &STR(FMSROC1C)
           END
         WHEN (&SY = 1E) +
           DO
           SET  &NSY = &STR(FMSROC1E)
           END
         WHEN (&SY = 1F) +
           DO
           SET  &NSY = &STR(FMSROC1F)
           END
         WHEN (&SY = 2A) +
           DO
           SET  &NSY = &STR(FMSROC2A)
           END
         WHEN (&SY = 2B) +
           DO
           SET  &NSY = &STR(FMSROC2B)
           END
         WHEN (&SY = 2C) +
           DO
           SET  &NSY = &STR(FMSROC2C)
           END
         WHEN (&SY = 3A) +
           DO
           SET  &NSY = &STR(FMSROC3A)
           END
         WHEN (&SY = 3B) +
           DO
           SET  &NSY = &STR(FMSROC3B)
           END
         WHEN (&SY = 3C) +
           DO
           SET  &NSY = &STR(FMSROC3C)
           END
         OTHERWISE  +
           DO
           WRITE  NODE NUMBER ERROR = &SY
           GOTO  ENDIT
           END
       END
       /*--------------------------------------------------------*/
       /*    CONTINUE ON CREATING THE REST OF THE VARIABLES.     */
       /*--------------------------------------------------------*/
       SET &WHOUSER = &STR(&NSY..&TUSER)
       /*--------------------------------------------------------*/
       IF  &NTF = &STR(YES)  THEN  +
           DO
           SET  &NOTFY = &STR(NOTIFY(ALL))
           END
       ELSE  +
           DO
           SET  &NOTFY = &STR(NONOTIFY)
           END
       /*--------------------------------------------------------*/
       IF  &MSN  = &STR(YES) THEN  +
           DO
           SET  &DOMSG = &STR(MSG)
           END
       ELSE  +
           DO
           SET  &DOMSG = &STR()
           END
       /*--------------------------------------------------------*/
       ISPEXEC CONTROL ERRORS RETURN
       CONTROL MSG
       TSOEXEC TRANSMIT &WHOUSER &TYP DA(&IFILE) &NOTFY &DOMSG
       ISPEXEC CONTROL DISPLAY REFRESH
       GOTO  LOOP
ERRMSG:  +
       WRITE  **  SEVERE PANEL - &ENUM   **
ENDIT:   +
       EXIT CODE(0)
