PROC 0 DEBUG
/* ----------------------------------------------------------- */
/*      THIS A CLIST TO SETUP ALL THE FILES DYNAMICALLY FOR    */
/*      DOING A COBOL SYNATX CHECK.                            */
/* ----------------------------------------------------------- */
CONTROL NOLIST NOMSG  NOFLUSH  PROMPT
IF  &DEBUG Â¬= &STR() THEN  +
    DO
    CONTROL  MSG  LIST  CONLIST  SYMLIST
    SET &SHOW  =  &STR(SHOW)
    END
ELSE  +
    SET &SHOW  =  &STR()
ISPEXEC  CONTROL ERRORS RETURN
ISPEXEC VGET (USERLIB,PODSN,APOST,SEQ,FLAGW,NOBATCH,    +
              NONUM,NOLVL)   SHARED
IF &APOST  = &STR() THEN +
   DO
   SET  &APOST =  &STR(APOST)
   END
IF &SEQ    = &STR() THEN +
   DO
   SET  &SEQ = &STR(NOSEQ)
   END
IF &FLAGW  = &STR() THEN +
   DO
   SET  &FLAGW = &STR(FLAGW)
   END
IF &NOBATCH  = &STR() THEN +
   DO
   SET  &NOBATCH = &STR(NOBATCH)
   END
IF &NONUM  = &STR() THEN +
   DO
   SET  &NONUM = &STR(NONUM)
   END
IF &NOLVL  = &STR() THEN +
   DO
   SET  &NOLVL = &STR(NOLVL)
   END
IF &LANG   = &STR() THEN +
   DO
   SET  &LANG  = &STR(LANGLVL(2))
   END
ALLOC FI($$COB$$)  DA('SYS1.VSCOLIB')  SHR REUSE
ISPEXEC  LIBDEF  ISPLLIB  LIBRARY ID($$COB$$)
/* ----------------------------------------------------------- */
/*      THNGS ARE NOW "RIGHT", LET'S DO                        */
/* ----------------------------------------------------------- */
/*                                                             */
LOOP: +
      ISPEXEC CONTROL DISPLAY REFRESH
      ISPEXEC ADDPOP COLUMN(1) ROW(1)
      SET  &LASTCC = 0
      SET  &MAXCC  = 0
      ISPEXEC DISPLAY PANEL(COBOL#P) CURSOR(PODSN)
      SET  &RC = &LASTCC
      ISPEXEC REMPOP
      IF &RC = 8 THEN GOTO ALLDONE
   IF  &MAXCC NE 0  THEN  GOTO ERRMSG
      SET  &LASTCC = 0
      SET  &MAXCC  = 0
      /* ----------------------------------------- */
      /*      PROCESS THE TRANSACTION              */
      /* ----------------------------------------- */
      IF  &USERLIB = &STR( ) THEN +
          DO
          SET &ULIB = &STR( )
          END
      ELSE  +
          DO
          SET &ULIB = &STR(LIB(&USERLIB))
          END
      YCOBOL    &PODSN &APOST &SEQ  &ULIB  &NOBATCH +
                &FLAGW &NONUM &LANG &NOLVL &SHOW
      GOTO LOOP
/* ----------------------------------------------------------- */
/*      TIDY UP AND GO HOME.                                   */
/* ----------------------------------------------------------- */
ERRMSG:  +
  WRITE  **  SEVERE  PANEL  ERROR ***
ALLDONE:  +
ISPEXEC VPUT (USERLIB,PODSN,APOST,SEQ,FLAGW,NOBATCH,    +
              NONUM,NOLVL) SHARED
ISPEXEC  LIBDEF  ISPLLIB
FREE FI($$COB$$)
EXIT
