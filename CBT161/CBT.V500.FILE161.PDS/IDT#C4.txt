PROC 0 DEBUG
/* ----------------------------------------------------------------- */
/*     JIM MARSHALL                                                  */
/*     TRANSMIT / RECEIVE SYSTEM                                     */
/*     'TRANSMIT A FILE TO TSO OR VM/CMS USERS USING DISTR LIST'     */
/* ----------------------------------------------------------------- */
CONTROL NOFLUSH   MSG  PROMPT
IF &DEBUG ¬=  &STR() THEN +
   CONTROL LIST MSG NOFLUSH PROMPT CONLIST SYMLIST
SET &TUSER  = &STR()
SET &TYP    = &STR(SEQ)
SET &IFILE  = &STR()
SET &NOTFY  = &STR()
SET &NTF    = &STR(YES)
ISPEXEC CONTROL DISPLAY REFRESH
LOOP:  +
       ISPEXEC ADDPOP COLUMN(2) ROW(2)
       SET &LASTCC = 0
       SET &MAXCC  = 0
       ISPEXEC DISPLAY PANEL(IDT#P4)
       SET  &RC = &LASTCC
       ISPEXEC REMPOP
       IF &RC = 8 THEN GOTO ENDIT
       IF  &MAXCC NE 0  THEN +
           DO
           SET &ENUM = &STR(C4A)
           GOTO ERRMSG
           END
       /*--------------------------------------------------------*/
       /*    NOW WE WANT TO CHECK THE EXISTENCE OF THE FILE. IF  */
       /*    IT DOES NOT EXIST, THEN WE WILL TELL THE USER.      */
       /*--------------------------------------------------------*/
       SET   &LASTCC = 0
       IF    &SYSDSN(&IFILE) ¬= OK  THEN +
             DO
             WRITE
             WRITE DSN=&IFILE
             WRITE DOES NOT EXIST OR IS NOT CATALOGED.
             WRITE PLEASE CORRECT.
             WRITE
             GOTO LOOP
             END
       IF    &NTF = &STR(YES)  THEN  +
             DO
             SET  &NOTFY = &STR(NOTIFY(ALL))
             END
       ELSE  +
             DO
             SET  &NOTFY = &STR(NONOTIFY)
             END
       ISPEXEC CONTROL ERRORS RETURN
       CONTROL MSG
       TSOEXEC TRANSMIT (&TUSER) DA(&IFILE) &NOTFY
       ISPEXEC CONTROL DISPLAY REFRESH
       GOTO  LOOP
ERRMSG:  +
       WRITE  **  SEVERE PANEL - &ENUM   **
ENDIT:   +
       EXIT CODE(0)
