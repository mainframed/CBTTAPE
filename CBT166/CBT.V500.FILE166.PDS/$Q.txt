 /********************* $Q       REXX/CLIST      *********************/
 /* $Q                                                               */
 /*                                                                  */
 /*   This REXX exec takes the parm and issues that as a TSO command */
 /*   and then displays the output in the forms of ISPF EDIT NOTE    */
 /*   lines.  This only works if the TSO command does PUTLINE (not   */
 /*   TPUTs).                                                        */
 /*                                                                  */
 /* SYNTAX: $Q                                                       */
 /*                                                                  */
 /********************************************************************/


Address ISPEXEC

Parse Source . . EDIT_MACRO_NAME .

'CONTROL ERRORS RETURN'
'ISREDIT MACRO (CMD)'
If Rc Â¬=0 Then Do
   ZEDSMSG = EDIT_MACRO_NAME||' Error'
   ZEDLMSG = EDIT_MACRO_NAME||' can only run as an ISPF EDIT macro !!!'
   'SETMSG MSG(ISRZ001)'
   Exit
   End
If CMD = '' Then Exit

'ISREDIT (STRTLINE) = LINENUM .ZFIRST'
'ISREDIT (ENDLINE) = LINENUM .ZLAST'
'ISREDIT (CURRTOP,CURRBOT) = DISPLAY_LINES'

X = OUTTRAP('O.')
Address TSO CMD
If O.0 = 0 Then Do
   ZEDSMSG = 'No trapped output'
   ZEDLMSG = 'TSO Command does not generate any output to be trapped'
   'SETMSG MSG(ISRZ000)'
   End
Else Do
   BLANKLNE = ''
   DASHLINE = Copies('=',72)
   DATELINE = Date(W)||', '||,
              Date(U)||' at '||Time()
   FOOTAGE =  "Output from command '"||,
              Translate(CMD)"'"
   DATELINE = Center(DATELINE,72)
   FOOTAGE  = Center(FOOTAGE,72)
   'ISREDIT LINE_BEFORE' CURRTOP '= MSGLINE  (DATELINE)'
   'ISREDIT LINE_BEFORE' CURRTOP '= MSGLINE  (FOOTAGE)'
   Do I=1 To O.0            /*  Display trapped output from TSO cmd */
      MYNOTE = O.I
      'ISREDIT LINE_BEFORE' CURRTOP '= NOTELINE (MYNOTE)'
      End
   'ISREDIT LINE_BEFORE' CURRTOP '= INFOLINE (DASHLINE)'
   End
X = OUTTRAP('OFF')
'ISREDIT UP' O.0+3
Exit
