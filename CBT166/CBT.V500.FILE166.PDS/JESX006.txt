//ASM1      EXEC PGM=IEUASM,PARM='LOAD,NODECK,RENT,TEST'
//SYSPRINT    DD SYSOUT=*,DCB=BUFNO=1
//SYSGO       DD DSN=&&A,SPACE=(TRK,(10,10)),DCB=(LRECL=80,
//             BLKSIZE=800,RECFM=FB),DISP=(,PASS,DELETE),UNIT=SYSDA
//SYSLIB      DD DSN=SYS1.SMPMTS,DISP=SHR,UNIT=SYSDA,VOL=SER=DIPOSP,
//             DCB=BLKSIZE=19040
//            DD DSN=SYS1.SMPSTS,DISP=SHR,UNIT=SYSDA,VOL=SER=DIPOSP
//            DD DSN=SYS1.HASPSRC,DISP=SHR,UNIT=SYSDA,VOL=SER=DLIB02
//            DD DSN=SYS1.MACLIB,DISP=SHR
//            DD DSN=SYS1.AMODGEN,DISP=SHR
//SYSUT1      DD UNIT=VIO,SPACE=(CYL,15)
//SYSUT2      DD UNIT=SYSDA,SPACE=(CYL,(5,5))
//SYSUT3      DD UNIT=SYSDA,SPACE=(CYL,(5,5))
//SYSIN       DD *
***********************************************************************
*                                                                     *
*  MODULE NAME:              JESX006                                  *
*                                                                     *
*  MODULE TYPE:              JES2 USER EXIT 6                         *
*                                                                     *
*  LOAD MODULE NAME:         JESX006                                  *
*                                                                     *
*  LOAD LIBRARY:             SYS2.LINKLIB                             *
*                                                                     *
*  ASSEMBLER ATTRIBUTES:     RENT                                     *
*                                                                     *
*  LKED ATTRIBUTES:          RENT,REUS                                *
*                                                                     *
*  MODULE AUTHORIZATION:     NONE                                     *
*                                                                     *
*  MACROS:                  $SYSMSG, $ENTRY, $MODULE, GETMAIN         *
*                           FREEMAIN, WTO, $$WTO, LOCATE, $MODEND     *
*                                                                     *
*  MACRO LIBRARIES:         SYS1.HASPSRC, SYS1.MACLIB, SYS1.AMODGEN   *
*                           SYS1.SMPMTS SYS1.SMPSTS                   *
*                                                                     *
*  ABENDS:                  NONE                                      *
*                                                                     *
*  FUNCTION:                TO PROVIDE JCL STANDARDS ENFORCEMENT      *
*                                                                     *
*  PARAMETERS:              REFER: JES2 MODULES AND MACROS            *
*                                                                     *
*  INTERNAL TABLES:         A TABLE OF CLASS DEFINITIONS              *
*                                                                     *
*  CALLED MODULES:          NONE                                      *
*                                                                     *
*  CALLING MODULES:         JES2 HASPCNVT                             *
*                                                                     *
*  PROGRAM FLOW:                                                      *
*                                                                     *
*                                                                     *
*  PARAMETER LAYOUTS:       REFER: JES2 MODULES AND MACROES           *
*                                                                     *
*                                                                     *
*                                                                     *
*  AUTHOR:                   KIRK STICKEN, INFORMATION SERVICES       *
*                            (214)-334-2031                           *
*                            FRITO LAY                                *
*                            DALLAS, TX                               *
*                                                                     *
*  MODIFICATION RECORD:      NEW PROGRAM 11/27/85 KIRK STICKEN        *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         MACRO                         .
&LAB     $SYSMSG &M                    .
         LCLC &MSG                     .
         GBLB &TIMES                   .
&MSG     SETC '&M'                     .
         AIF  (&TIMES).MSG1            .
&TIMES   SETB 1                        .
         PUSH PRINT                    .  REMEMBER OUR PRINT STATUS
         PRINT GEN                     .    AND PRINT THIS FOR HISTORY
         B    Q&SYSNDX                 .  SKIP PROCESS SUBROUTINE
PUTMSG   DS   0H                       .  ROUTINE ENTERED TO PUT     XXX
                                       .  MESSAGE ON JES2 SYSMSGS    XXX
                                       .  DATASET
         ST   R14,R14SAVE              .  SAVE HOME BASE
         LH   R15,0(R1)                .  GET MESSAGE LENGTH
         A    R15,=F'10'               .  MAKE IT LONGER
         N    R15,=F'127'              .  BUT NOT TOO LONG
         ST   R15,MYRPL+RPLRLEN-IFGRPL .  PUT IN RPL
         BCTR R15,R0                   .  DEC FOR MOVE
         EX   R15,PUTMVC               .  MOVE MESSAGE INTO AREA
         MVC  MSG+10(1),$CCOMCHR-HCTDSECT(R11) MOVE IN COMMAND CHAR
         MVC  MSG(10),=CL10'           ' . CLEAR FIRST TEN SPACES
         LA   R1,MYRPL                 .  POINT TO RPL
         PUT  RPL=(1)                  .  AND OUTPUT THE RPL
         L    R14,R14SAVE              .  SO GO HOME NOW
         BR   R14                      .  SEE HOW GOOD IT IS
PUTMVC   MVC  MSG+10(0),2(R1)          .  MOVE IN MSG
         POP  PRINT                    .  RESTORE PRINT STATUS
Q&SYSNDX DS   0H                       .  BRANCH POINT FROM PUTMSG
.MSG1    ANOP                          .
&LAB     LA    R1,Z&SYSNDX             .  POINT TO MESSAGE LENGTH
         BAL   R14,PUTMSG              .  BRANCH TO MSG ROUTINE
         B     N&SYSNDX                .  SKIP OVER MESSAGE
Z&SYSNDX DC    AL2(L'M&SYSNDX)         .  THE MESSAGE LENGTH
M&SYSNDX DC    C&MSG                   .   AND THE MESSAGE ITSELF
N&SYSNDX DS    0H                      .  THE SKIP-AROUND-POINT
         MEND                          .
         COPY  $HASPGBL                .  COPY THOSE HASP GLOBALS
JESX006A $MODULE SYSP=(NOGEN,NOGEN,NODATA,NOGEN,NOGEN),                X
               TITLE='INFERNAL TEXT PROCESSOR -- REPUBLICBANK DALLAS', X
               ENVIRON=SUBTASK,                                        X
               $BUFFER,                .                               X
               $PCE,                   .  R8                           X
               $DCT,                   .                               X
               $PDDB,                  .                               X
               $HASPEQU,               .                               X
               $HCT,                   .  R11                          X
               KEYS,                   .                               X
               TEXT,                   .                               X
               $JCT,                   .  R10                          X
               $JQE,                   .                               X
               $MIT,                   .                               X
               $CNVWORK,               .                               X
               UCB,                    .                               X
               CVT,                    .                               X
               DEB,                    .                               X
               $CAT,                   .                               X
               WPL,                    .                               X
               NEL,                    .                               X
               RPL                     .
SUBPOOL  EQU   00                      .  GETMAIN SUBPOOL
EX000    EQU   B'10000000'             .  EXIT BITS FOR JCTXMASK
EX001    EQU   B'01000000'             .    THESE CAN TURN
EX002    EQU   B'00100000'             .      ON OR OFF
EX003    EQU   B'00010000'             .        A PARTICULAR EXITS
EX004    EQU   B'00001000'             .         INVOCATION
EX005    EQU   B'00000100'             .          STATUS
EX006    EQU   B'00000010'             .
EX007    EQU   B'00000001'             .
         DROP  R13                     .  DO NOT USE R13 FOR PCE
         USING PCEDSECT,R8             .  HERE WE USE R8 FOR PCE
JESX006  $ENTRY CSECT=YES,BASE=(R9,R12) . BEGIN EXIT ROUTINE
         STM   R14,R12,12(R13)         .  SAVE ENTRY REGISTERS
         LR    R9,R15                  .  APPLY BASE FOR PROGRAM
         LA    R12,4095(R9)            .    AND THE
         LA    R12,1(R12)              .      OVERFLOW BASE TOO
         TM    JCTJOBFL,JCTBATCH       .  IS THIS BATCH?
         BNO   RNOMEM4                 .    NO, NEVER COME BACK HERE
         USING TEXT,R7                 .  APPLY BASE FOR TEXT AREA
         L     R7,4(R1)                .  GET PASSED PARMS
         L     R6,PCEUSER0             .  IS PTR TO WORK AREA SET?
         LTR   R6,R6                   .       WELL, IS IT?
         BNZ   BEGIN                   .     YES, GO WORK ON IT
UINIT    DS    0H                      .     NO , INIT WORK AREAS
         LR    R3,R0                   .  SAVE R0
         LR    R4,R1                   .   AND R1
         L     R2,GETSIZE              .  PREPARE FOR GETMAIN
         GETMAIN RC,LV=(2)             .  GET WORKING STORAGE
         LTR   R15,R15                 .  CHECK GETMAIN
         BNZ   RNOMEM1                 .  ERROR NO MEMORY AVAILABLE !
         LR    R6,R1                   .  SAVE GETMAIN ADDRESS
         LR    R14,R1                  .  CLEAR
         L     R15,GETSIZE             .    WORKING
         LR    R0,R14                  .       STORAGE
         SR    R1,R1                   .          AREA
         MVCL  R14,R0                  .            NOW
         ST    R6,PCEUSER0             .  SAVE MEMORY POINTER
         OI    FLAGS-SAVE(R6),MGOT     .  SET MEMORY GOTTEN FLAG
         MVC   WORKINST-SAVE(L'INSTSHEL,R6),INSTSHEL . INIT SHEL INSTR
         LA    R14,68                  .  INIT
         SLL   R14,24                  .    THE
         ST    R14,CAMLOC-WORK(R6)     .     LOCATE
         LA    R14,DSN-WORK(R6)        .       DATA
         ST    R14,CAMLOC-WORK+4(R6)   .         AREA
         LA    R14,CAMRET-WORK(R6)     .             |
         ST    R14,CAMLOC-WORK+12(R6)  .             |
         SLR   R14,R14                 .             |
         ST    R14,CAMLOC-WORK+8(R6)   .             |
         PUSH  USING                   .
         USING WORK,R6                 .  TEMPORARY BASE FOR WORK
         LA    R2,JPCEMSG              .  GET PCE SYSMSG ACB
         LA    R15,MSG                 .  GET PCE SYSMSG ACB
         MVI   MYRPL-IFGRPL+RPLREQ,RPLPUT . THIS IS A PUT
         ST    R2,MYRPL-IFGRPL+RPLDACB .  SAVE ACB INTO RPL HERE
         ST    R15,MYRPL-IFGRPL+RPLAREA . SAVE MSG ADDRESS IN RPL HERE
         MVI   MYRPL-IFGRPL+RPLOPT1,RPLSEQ . THIS IS SEQUENTIAL PUT
         LA    R15,L'MSG               .
         ST    R15,MYRPL-IFGRPL+RPLRLEN . SAVE MSG LENGTH IN RPL HERE
         POP   USING                   .
         LR    R0,R3                   .  RESET
         LR    R1,R4                   .    PARM REGISTERS
BEGIN    DS    0H                      .
         USING WORK,R13                .  ADDRESS GETMAINED AREA
         ST    R6,8(R13)               .  STORE OURS THERE
         ST    R13,4(R6)               .  STORE THEIRS HERE
         LR    R13,R6                  .  RESET SAVE AREA POINTER
         LR    R15,R0                  .  GET ENTRY CODE FOR INDEX FUNC
         N     R15,=F'15'              .  USE ONLY LAST 4 BITS OF CODE
         B     FUNCTION(R15)           .  INDEX TO FUNCTION CODE
INSTSHEL LA    R15,0(R0,R9)            .  SHELL INSTRUCTION
FUNCTION B     DOTEXT                  .  PROCESS TEXT RECORD
         B     DOLAST                  .  PROCESS LAST CALL
         EJECT                         .
**********************************************************************
*                                                                    *
*   DOTEXT DETERMINES THE TYPE OF TEXT RECORD PASSED, AND GOES TO    *
*   PROCESSING ROUTINES TO HANDLE THE TEXT RECORD                    *
*                                                                    *
**********************************************************************
DOTEXT   DS    0H                      .  ANOTHER IMPASS, YES?
         LA    R5,JOBTABLE             .  GET THE TAB OF "S" TYPE ADDRS
         SLR   R15,R15                 .  CLEAR FOR NEXT IC
         IC    R15,STRINDCS            .  GET RECORD TYPE
         N     R15,=F'15'              .    USE LAST 4 BITS
         SLA   R15,2                   .      TIMES 4
         B     TXTTYPE(R15)            .         INDEX TO TXT TYPE
TXTTYPE  B     RETURN                  .  00 - TYPE 0 - UNUSED
         B     DOJOB                   .  04 - TYPE 1 - JOB STATEMENT
         B     DOEXEC                  .  08 - TYPE 2 - EXEC STMT
         B     RETURN                  .  12 - TYPE 3 - UNUSED
         B     DODD                    .  16 - TYPE 4 - DD STMT
         B     RETURN                  .  20 - TYPE 5 - UNUSED
         B     RETURN                  .  24 - TYPE 6 - UNUSED
         B     RETURN                  .  28 - TYPE 7 - UNUSED
         B     DOPROC                  .  32 - TYPE 8 - PROC STMT
         B     RETURN                  .  36 - TYPE 9 - UNUSED
         B     RETURN                  .  40 - TYPE A - UNUSED
         B     RETURN                  .  44 - TYPE B - UNUSED
         B     RETURN                  .  48 - TYPE C - UNUSED
         B     RETURN                  .  52 - TYPE D - UNUSED
         B     RETURN                  .  56 - TYPE E - UNUSED
         B     RETURN                  .  60 - TYPE F - UNUSED
         B     RETURN                  .
         EJECT                         .
**********************************************************************
*                                                                    *
*  DOLAST IS THE FINAL STEP IN THE JOBS JCL.  ALL JCL IS CONVERTED   *
*  AND JES CALLS US WITH THE LAST CALL INDICATOR SET.                *
*                                                                    *
*  AT THIS POINT WE LOOK AT THE BIT SETTINGS LEFT BY SCANNING THE    *
*  JCL FOR THE JOB. IF ANY ARE SET THAT WE DONT LIKE (IE:TIME=1440)  *
*  WE CAN SET THE ERROR FLAG ON AND CANCEL THE JOB WITH A MESSAGE.   *
*                                                                    *
**********************************************************************
DOLAST   DS    0H                      .  NOW WE CAN DECIDE TO KILL 'EM
         OI    FLAGS,LASTCALL          .  SET LAST CALL FLAG
         L     R15,JTAPE               .  GET JOB TAPE COUNT
         A     R15,STAPE               .     ADD STEP TAPE COUNT
         ST    R15,JTAPE               .         AND SAVE
         L     R15,STAPEM              .  CHECK STEP TAPE MAX
         C     R15,STAPE               .    AGAINST CURRENT STEP TAPES
         BNL   OKSTAPEM                .       IF NOT HIGHEST JUST SKIP
         L     R15,STAPE               .          OTHERWISE SAVE FOR
         ST    R15,STAPEM              .             LATER DECISIONS
OKSTAPEM DS    0H                      .  OH BOY
         PUSH  USING                   .
         USING CLASSD,R3               .  LOCATE CLASS DEFINITIONS
         L     R3,CLASSPTR             .    IF NOT FOUND JUST GO
         LTR   R3,R3                   .       AND BYPASS THIS HOOEY
         BZ    OKTIME2                 .
         TM    FLAGS1,TESTBTCH         .  IS THIS TEST BATCH
         BNO   NOTSTB1                 .     NOT TEST BATCH
         TM    FLAGS2,JCARD            .  DID USER HARD CODE CLASS?
         BO    NOTSTB1                 .    YES, SKIP FINAL OVERRIDE
CLOOP1   DS    0H                      .
         CLC   MAXTIME,CJOBTIME        .  IS TIME OK FOR CLASS
         BNH   CKTP1                   .    YES, GO CHECK STEP TAPES
         CLI   CID,C'*'                .  IS THIS SILLY? CL NOT FOUND
         BE    SILLY1                  .    YES, VERY. JUST IGNORE ME
         LA    R3,CENTL(R3)            .  SKIP TO NEXT TABLE ENTRY
         B     CLOOP1                  .      AND LOOP YOUR BUNS OFF
CKTP1    CLC   STAPEM,CSTEPTP          .  IS STEP TAPE MAX OK FOR CLASS
         BNH   CKTP2                   .     YES, CHECK JOB TAPE MAX
         CLI   CID,C'*'                .  IS THIS SILLY AGAIN?
         BE    SILLY1                  .     WHAT A BOONDOGGLE,GET HOME
         LA    R3,CENTL(R3)            .  SKIP TO NEXT TABLE ENTRY
         B     CLOOP1                  .      AND TRY IT FROM THE TOP
CKTP2    CLC   JTAPE,CJOBTP            .  IS JOB  TAPE MAX OK FOR CLASS
         BNH   RECATC                  .    YES, RECATALOG THIS CLASS
         CLI   CID,C'*'                .  IS THIS SILLY TOO MUCH??
         BE    SILLY1                  .    WHY DO I LIKE THIS SO MUCH?
         LA    R3,CENTL(R3)            .  ONCE AGAIN INTO THE BREACH
         B     CLOOP1                  .   DEER FIENDS
RECATC   DS    0H                      .  CLASS NEEDS TO BE SET AGAIN
         CLI   CID,C'*'                .  IS THIS SILLY? CL NOT FOUND
         BE    SILLY1                  .    YES, VERY. JUST IGNORE ME
         CLC   CDTIME,=F'0'            .  IS THIS CLASS DEFAULTABLE
         BNE   RECATC1                 .     NO, BUT WHO CARES ANYWAY
         LA    R3,CENTL(R3)            .
         B     CLOOP1                  .
RECATC1  DS    0H                      .
         CLC   CID,CLASS               .    BUT DID IT REALLY CHANGE?
         BE    NOTSTB1                 .       OF COURSE NOT, FOOL
         ST    R3,CLASSPTR             .  REMEMBER THE NEW JOB CLASS
         MVC   CLASS,CID               .    AND PUT THE NEW CLASS
         MVC   JCTJCLAS,CLASS          .      INTO THE JES2 CONTROL
         MVC   JCTCLASS,CLASS          .        BLOCKS THAT HE
         L     R14,PCEJQE              .           WANTS THEM
         MVC   JQEJCLAS-JQEDSECT(1,R14),CLASS         IN
         B     NOTSTB1                 .  BYPASS ALL SILLINESS
SILLY1   DS    0H                      .  OH BOY
         OI    FLAGS1,ERROR            .  JOB NOT FIT CLASS STRUCTURE
        $SYSMSG '$HASP920 (E6) TEST BATCH JOB DOES NOT FIT CLASS STRUCTU
               URE'                    .
         L     R3,CLASSPTR             .  RESET THE CLASS DEFF POINTER
NOTSTB1  DS    0H                      .  OH BOY
         CLC   CJOBTIME,MAXTIME        .  DID USER WANT TOO MUCH TIME
         BNL   CKTAPE                  .  NO, GO CHECK TAPE SITU.
         OI    FLAGS1,ERROR            .  WEE GWANG KILLEM NOW!
        $SYSMSG '$HASP920 (E6) JOB MAXIMUM CPU TIME INVALID FOR CLASS'
CKTAPE   DS    0H                      .  OH BOY
         CLC   CSTEPTP,STAPEM          .  WHAT IS STEP TAPE MAX
         BNL   OKSTAPE                 .    STEP MAX OK FOR CLASS
         TM    FLAGS2,STAPEE           .  DID USER SEE THIS MSG YET?
         BO    OKSTAPE                 .     WHY YES, DONT MAKE IT MAD
         OI    FLAGS2,STAPEE           .     WHY NO, SET BIT TO REMEMBR
         OI    FLAGS1,ERROR            .  I LOVE A GOOD ERROR.
        $SYSMSG '$HASP920 (E6) STEP TAPE LIMIT EXCEEDED'
OKSTAPE  DS    0H                      .  OH BOY
         CLC   CJOBTP,JTAPE            .  IS JOB TAPE LIMIT OK
         BNL   OKJTAPE                 .    WHY CANT I EVER BE HAPPY
         TM    FLAGS2,JTAPEE           .  HAS USER SEEN THIS ALREADY?
         BO    OKJTAPE                 .     SHUCKY DARNS
         OI    FLAGS2,JTAPEE           .     OH GOODY
         OI    FLAGS1,ERROR            .  BYTE THIS ONE!
        $SYSMSG '$HASP920 (E6) JOB TAPE LIMIT EXCEEDED'
OKJTAPE  DS    0H                      .
OKTIME2  DS    0H                      .  NOW PUT OUT A MESSAGE
*                                      .     SAYING WHAT A NICE BOY
*                                      .     THE USER IS AND HOW WE
*                                      .     LIKE HIM AND ALL OF HIS
*                                      .     RELATIVES. ALSO TELL HIM
*                                      .     WHY WE ARE CANCELLING HIM
*                                      .     AND  TELL HIM WHAT THE
*                                      .     EXIT THINKS HE IS USING.
         MVC   MSGX+2(L'STATMSG),STATMSG . INIT FOR STATS MESSAGE
         MVC   MSGX+2+STATC-STATMSG(L'STATC),CLASS . INIT CLASS
         L     R14,MAXTIME             .
         CVD   R14,DWORK               .
         UNPK  MSGX+2+STATT-STATMSG(L'STATT),DWORK . INIT TIME
         OI    MSGX+2+STATT-STATMSG+L'STATT-1,X'F0'  CLEAR SIGN
         L     R14,STAPEM              .
         CVD   R14,DWORK               .
         UNPK  MSGX+2+STATS-STATMSG(L'STATS),DWORK . INIT STAPES
         OI    MSGX+2+STATS-STATMSG+L'STATS-1,X'F0'  CLEAR SIGN
         L     R14,JTAPE               .
         CVD   R14,DWORK               .
         UNPK  MSGX+2+STATJ-STATMSG(L'STATJ),DWORK . INIT JTAPES
         OI    MSGX+2+STATJ-STATMSG+L'STATJ-1,X'F0'  CLEAR SIGN
         L     R14,REGIONJ             .
         CVD   R14,DWORK               .
         UNPK  MSGX+2+STATR-STATMSG(L'STATR),DWORK . INIT JREGION
         OI    MSGX+2+STATR-STATMSG+L'STATR-1,X'F0'  CLEAR SIGN
         LA    R14,L'STATMSG           .  GET MESSAGE LENGTH FOR PUT
         STCM  R14,B'0011',MSGX        .     PUT  IN CORRECT PLACE
         LA    R1,MSGX                 .  GET THE MESSAGE
         BAL   R14,PUTMSG              .    AND GO PUT THE MESSAGE OUT
         TM    FLAGS1,ERROR            .  HAS AN ERROR BEEN ENCOUNTERED
         BNO   RETURN                  .
************
************
************     BYPASS CANCEL FOR TESTING PURPOSES
************
************
************
*       $SYSMSG '$HASP920 (E6) BATCH JOB WILL BE CANCELLED WHEN USER EXI
               IT TRIAL PERIOD EXPIRES'
        $SYSMSG '$HASP920 (E6) BATCH JOB HAS BEEN CANCELLED BY JES2 USEC
               R EXIT 6'
         MVC   RC,=H'8'                .  GIVE THE JOB A JCL ERROR
         LA    R1,MSG                  .    AND PUT A MESSAGE ON
         MVC   MSG(MSG1L),MSG1         .      THE JES2 LOG FOR ALL
       $$WTO   (R1)                    .        TO SEE.
         B     RETURN                  .
         POP   USING                   .
         EJECT                         .
**********************************************************************
*                                                                    *
* DOJOB, DOEXEC, DODD, AND DOPROC ARE THE MAJOR ENTRYS FOR SCANNING  *
* THE JOBS JCL TEXT. A BIT IS SET TO REMEMBER THE TEXT TYPE, SOME    *
* INITIALIZATION IS DONE, AND DOKEY IS ENTERED TO LOCATE THE         *
* TEXT KEY PROCESSOR ROUTINE.                                        *
*                                                                    *
**********************************************************************
         USING JUMPTAB,R5              .  ADDRESS BRANCH TABLE
         USING USERTEXT,R4             .  ADDRESS TEXT KEY PTRS
DOJOB    DS    0H                      .
         LA    R4,STRJKEY              .  REFERENCE FIRST JOB KEY
         ST    R4,CURPTR               .  SAVE CURRENT KEY POINTER
         OI    FLAGS,JOBU              .  REMEMBER THIS IS JOB TEXT
         OI    FLAGS1,JOBCARD          .  AND REMEMBER JOB CARD FOUND
         B     DOKEY                   .  GO PROCESS ALL KEYS
DOEXEC   DS    0H                      .
         LA    R4,STREKEY              .  REFERENCE FIRST EXEC KEY
         ST    R4,CURPTR               .  SAVE CURRENT KEY POINTER
         OI    FLAGS,EXECU             .  REMEMBER THIS IS EXEC TEXT
         L     R15,JTAPE               .  ADD STEP TAPES
         A     R15,STAPE               .    TO JOB TAPES TO GET A
         ST    R15,JTAPE               .      RUNNING JOB TAPE TOTAL.
         L     R15,STAPEM              .  FIND STEP TAPE
         C     R15,STAPE               .    MAX AS AN ONGOING
         BNL   OKSTM1                  .      PROCESS
         L     R15,STAPE               .  SAVE STEP TAPE MAX
         ST    R15,STAPEM              .    FOR LATER CHECKING
OKSTM1   DS    0H                      .
         SLR   R15,R15                 .  CLEAR FOR COUNTING
         ST    R15,STAPE               .     STEP TAPES.
         B     DOKEY                   .  GO PROCESS ALL EXEC KEYS
DODD     DS    0H                      .
         TM    STRDINDC,DTXSYSIN+DTXSYOUT+DTXSUBSK                   XXX
                                       .  IS THIS DD CARD A SYSOUT     X
                                       .  OR A SUBSYSTEM REQUEST?
         BM    RETURN                  .     YES, DO NOT PROCESS
         XC    UNITSW,UNITSW           .  CLEAR UNIT BIT SETTINGS
         XC    CAMDEVB3,CAMDEVB3       .  CLEAR CATALOG BITS FOR TAPE
         NI    FLAGS3,255-DSNS2        .  CLEAR DSN BITS
         MVI   DSN,C' '                .  CLEAR OUT THE ENTIRE
         MVC   DSN+1(L'DSN-1),DSN      .     DATASET NAME AREA
         LA    R4,STRDKEY              .  REFERENCE FIRST DD KEY
         ST    R4,CURPTR               .  SAVE CURRENT KEY POINTER
         OI    FLAGS,DDU               .  REMEMBER THIS IS DD CARD
         B     DOKEY                   .  GO PROCESS ALL DD KEYS
DOPROC   DS    0H                      .
         LA    R4,STREKEY              .  REFERENCE FIRST PROC KEY
         ST    R4,CURPTR               .  SAVE CURRENT KEY POINTER
         OI    FLAGS,PROCU             .  REMEMBER THIS IS A PROC
DOKEY    DS    0H                      .  ****** OH BOY ********
         L     R4,CURPTR               .  MAKE SURE POINT TO TEXT
         SLR   R15,R15                 .  CLEAR FOR INDEX LOAD
         IC    R15,USERKEY             .  GET KEY TO INDEX WITH
         SLA   R15,1                   .  TIMES 2 = INDEX TO TABLE
         LA    R15,JUMP(R15)           .  PULL IN JUMP TABLE ENTRY
         MVC   WORKINST+2(2),0(R15)    .  BUILD VALID "LA" INSTRUCTION
         SLR   R14,R14                 .  SET LENGTH TO 0 FOR EXECUTE
         EX    R14,WORKINST            .  LA R15,XXX(9) GET ADDRESS
         BALR  R14,R15                 .  CALL FUNCTION
         B     SKIPKEY                 .  SKIP KEY JUST PROCESSED
         EJECT                         .
**********************************************************************
*                                                                    *
*  LASTKEY IS INVOKED WHEN THE "END-OF-TEXT" TEXT KEY FOR A SINGLE   *
*  JCL RECORD IS ENCOUNTERED. (IE: END OF JCL RECORD).               *
*  BRANCH TO CLEANUP ROUTINES BASED ON TEXT UNIT TYPE.               *
*                                                                    *
**********************************************************************
LASTKEY  DS    0H                      .
         TM    FLAGS,DDU               .  DITTO FOR THIS DD CARD?
         BO    CHECKDD                 .    YESUM, GETUM CLEANED
         TM    FLAGS,EXECU             .  IS THIS EXEC CARD DONE NOW
         BO    CHECKEX                 .    YES, GO CLEAN UP WHATEVER
         TM    FLAGS,PROCU             .  AND HOW ABOUT PROCS TODAY?
         BO    CHECKPR                 .    TSK TSK...DIRTY PROCS!
         TM    FLAGS,JOBU              .  IS THIS JOB CARD DONE NOW
         BO    CHECKJOB                .    YES, CHECK THE JOB PARMS
         B     RETURN                  .  TEXT RCD DONE, GET SOME MORE
         EJECT                         .
**********************************************************************
*                                                                    *
* JOB CARD JCL WAS JUST PROCESSED.                                   *
*                                                                    *
* IF JOB CLASS WAS NOT SPECIFIED DEFAULT CLASS BASED ON JOB TIME.    *
* IF CLASS AND TIME ARE NOT SPECIFIED USED DEFAULT CLASS FROM        *
*     READER.                                                        *
* IF TIME IS NOT SPECIFIED, DEFAULT THE TIME BASED ON JOB CLASS.     *
*                                                                    *
* CHECK JOB FLAGS SET DURING SCAN TO SEE IF USER TRYED TO USE        *
*    JCL KEYWORDS WE DONT WANT THEM TO USE IN RBD                    *
*       THINGS LIKE ADRSP=REAL ETC ARE FROWNED UPON.                 *
*                                                                    *
**********************************************************************
         PUSH  USING                   .
CHECKJOB DS    0H                      .  REMEMBER JOBCARD ENCOUNTERED
         LA    R3,CLASSTAB             .  GET CLASS TABLE
         USING CLASSD,R3               .    REFERENCE BY NAME
         CLI   CLASS,C'A'              .  IS CLASS SET
         BL    N1                      .     NO, GO CHECK TIME
         CLC   MAXTIME,=F'0'           .  IS TIME AND CLASS SET
         BE    DEFTIME                 .     NO, GO DEFAULT TIME
VALTIME  DS    0H                      .  VALIDATE TIME FOR CLASS
         BALR  R15,R0                  .  SET LOOP BACK ADDRESS
         CLI   CID,C'*'                .  IS END OF TABLE?
         BE    NOCLASS                 .     YES, INVALID CLASS ON JOB
         CLC   CID,CLASS               .  IS CLASS FOUND?
         BE    CFND1                   .     YES, USE FOR CHECK
         LA    R3,CENTL(R3)            .  GET NEXT CLASS ENTRY
         BR    R15                     .  LOOP BACK
CFND1    DS    0H                      .  CLASS AND TIME EXIST
         CLC   CJOBTIME,MAXTIME        .  DOES CLASS TIME FIT JOB TIME
         BNL   OKTIME1                 .  TIME IS OK
         OI    FLAGS1,ERROR            .  AGAIN TO CANCEL OR NOT?
        $SYSMSG '$HASP920 (E6) TIME IS INCORRECT FOR JOB CLASS'
         B     OKTIME1                 .
DEFTIME  DS    0H                      .  DEFAULT TIME TO CLASS MAX
         BALR  R15,R0                  .  SET LOOP BACK ADDRESS
         CLI   CID,C'*'                .  IS END OF TABLE?
         BE    NOCLASS                 .     YES, INVALID CLASS ON JOB
         CLC   CID,CLASS               .  IS CLASS FOUND?
         BE    CFND2                   .     YES, USE FOR CHECK
         LA    R3,CENTL(R3)            .  GET NEXT CLASS ENTRY
         BR    R15                     .  LOOP BACK
NOCLASS  DS    0H                      .
*        OI    FLAGS1,ERROR            .
*       $SYSMSG '$HASP920 (E6) JOB IS USING INVALID CLASS'
         B     OKTIME1                 .
CFND2    DS    0H                      .  CLASS WAS FOUND IN TABLE
         CLC   CDTIME,=F'0'            .  IS DEFAULT TIME THERE
         BE    OKTIME1                 .   NO, CANNOT DEFAULT TIME
         OI    FLAGS3,TIMEDEF          .  REMEMBER TIME DEFAULTED
         MVC   MAXTIME,CDTIME          .  REMEMBER DEFAULT TIME
         MVC   DF1(3),=AL1(TIMEJK,2,4) .  SET UP TEXT KEYS FOR TIME
         MVI   DF2,2                   .     DEFAULT
         MVI   DF3,ENDK                .  MOVE IN END OF TEXT KEY
         L     R14,MAXTIME             .  GET TIME TO DEFAULT
         SRDA  R14,32                  .  SHIFT FOR DIVIDE
         D     R14,=F'60'              .  14=SEC, 15=MIN
         CVD   R14,DWORK               .  MAKE SECONDS BCD NUMERIC
         UNPK  DSEC,DWORK              .    AND PUT
         OI    DSEC+L'DSEC-1,X'F0'     .       INTO DEFAULT KEY AREA
         CVD   R15,DWORK               .  MAKE MINUTES BCD NUMERIC
         UNPK  DMIN,DWORK              .    AND PUT
         OI    DMIN+L'DMIN-1,X'F0'     .       INTO DEFAULT KEY AREA
         MVC   USERKEY(L'DF),DF        .  MOVE ENTIRE DEFAULT TIME TO
         LH    R15,STRLTH              .    TEXT KEY AREA AND UPDATE
         LA    R15,L'DF-1(R15)         .      THE LENGTH FIELDS
         STH   R15,STRLTH              .         SO JES2 IS HAPPY
         B     OKTIME1                 .
N1       DS    0H                      .  CLASS WAS NOT SET,
         CLC   MAXTIME,=F'0'           .    BUT IS TIME?
         BE    N1N2                    .      NO CLASS AND TIME NOT SET
DEFCLASS DS    0H                      .  PICK CLASS BASED ON TIME
         BALR  R15,R0                  .  SET LOOP BACK ADDRESS
         CLI   CID,C'*'                .  IS END OF TABLE?
         BE    CFND3                   .     YES, USE LAST AS DEFAULT
         CLC   CDTIME,=F'0'            .  IS CLASS DEFAULTABLE?
         BE    SKIPD2                  .     NO, SKIP IT
         CLC   CJOBTIME,MAXTIME        .  IS TIME A MATCH
         BNL   CFND3                   .     YES, USE FOR CHECK
SKIPD2   LA    R3,CENTL(R3)            .  GET NEXT CLASS ENTRY
         BR    R15                     .  LOOP BACK
CFND3    DS    0H                      .  TIME FOUND IN CLASS TABLE
         MVC   CLASS,CID               .  MOVE IN CLASS ID
         CLI   CLASS,C'*'              .  IS THIS  A VALID CLASS
         BNE   TRULAST                 .     YES, DO NOT RESET TO 0
         MVI   CLASS,C'0'              .  NOT VALID CLASS, RESET TO 0
TRULAST  DS    0H                      .
         MVC   JCTJCLAS,CLASS          .  MOVE CLASS TO JES2
         MVC   JCTCLASS,CLASS          .     PLACES THAT ARE FUN
         L     R14,PCEJQE              .       PLACES TO LIVE
         MVC   JQEJCLAS-JQEDSECT(L'JQEJCLAS,R14),CLASS  ALSO INTO JQE
         MVI   USERKEY,CLASSJK         .  BUILD CLASS= KEYS IN TEXT
         MVI   USER#,1                 .    AREA AND
         MVI   USERL1,1                .       MOVE IN ALL DATA
         MVC   USERTXT(L'CLASS),CLASS  .         SO THAT JES KNOWS
         MVI   USERTXT+L'CLASS,ENDK    .            THAT I WROTE THIS
         LH    R15,STRLTH              .  UPDATE TEXT RCD LENGTH
         LA    R15,L'CLASS+3(R15)      .    WITH LENGTH OF JOB CLASS=
         STH   R15,STRLTH              .      TEXT KEYS
         B     OKTIME1                 .  GO CHECK OTHER STUFF.
N1N2     DS    0H                      .  CLASS AND TIME NOT SET, DEFLT
         MVC   CLASS,JCTJCLAS          .  GET CLASS FROM JES2 DFLTS
         CLI   CLASS,C'A'              .  IS IT VALID CLASS
         BNL   CHECKJOB                .    YES, GO CHECK AGAIN
         MVI   CLASS,C'0'              .  NOT VALID SOMEHOW, SET TO 0
         B     CHECKJOB                .     AND CHECK AGAIN
OKTIME1  DS    0H                      .
         ST    R3,CLASSPTR             .  SAVE CLASS TABLE POINTER
         CLC   =C'DP',USER             .  IS USER SPECIAL?
         BE    SPECUS1                 .  COULD BE
         CLC   =C'DS',USER             .  OR IS IT PC
         BE    SPECUS1                 .  MIGHT BE
         CLC   =C'RDPP ',USER          .  OR IS IT GOD
         BE    SPECUSER                .  YES INDEED
         B     NOTSPEC                 .  USER IS NOT SPECIAL
SPECUS1  DS    0H                      .
         TRT   USER+3(1),TRTNUM        .  IS THIRD POS NUMBER (DP0...)
         BZ    NOTSPEC                 .  USER IS NOT SPECIAL
SPECUSER DS    0H                      .
         OI    FLAGS2,SPUSER           .  USER IS SPECIAL
NOTSPEC  DS    0H                      .
         CLI   CBATCH,C'C'             .  IS CLASS PRIVILEDGED?
         BNE   OKCLASSX                .     NO, CHECK NEXT GIZMO
         TM    FLAGS2,SPUSER           .  IS USER SPECIAL?
         BO    OKCLASSX                .  USER IS ALLOWED TO USE CLASS
*        OI    FLAGS1,ERROR            .  AAAHHHHH!!! WHAT A JOY!!
*       $SYSMSG '$HASP920 (E6) USER CANNOT USE PRIVLEDGED JOB CLASS'
OKCLASSX DS    0H                      .
         CLI   CLASS,C'0'              .  IS THIS TEST BATCH?
         BL    RETURN                  .     NO, SKIP TESTS
         OI    FLAGS1,TESTBTCH         .  REMEMBER THIS IS TEST BATCH
OKRGN1   DS    0H                      .
         TM    FLAGS1,REALMEM          .  IS IT TEST ADDRSP=REAL?
         BNO   NOREAL                  .     NO, CONTINUE
         OI    FLAGS1,ERROR            .    YES, CANCEL JOB
        $SYSMSG '$HASP920 (E6) TEST BATCH CANNOT USE ADDRSP=REAL'
NOREAL   DS    0H                      .  IS IT TEST REGION=XXXK
         L     R14,REGIONJ             .  GET REGION FROM JOB
         C     R14,=F'500'             .  CHECK AGAINST CONVTR PARMS
         BNH   NORGN                   .     NO, REGION OK
         OI    FLAGS1,ERROR            .    YES, CANCEL JOB
        $SYSMSG '$HASP920 (E6) TEST BATCH CANNOT USE REGION= ON JOB CARC
               D'                      .
NORGN    TM    FLAGS1,JOBHOLD          .  IS IT TEST TYPRUN=HOLD
         BNO   NOHOLD                  .     NO, CONTINUE
         OI    FLAGS1,ERROR            .    YES, SET CANCEL JOB FLAG
        $SYSMSG '$HASP920 (E6) TEST BATCH CANNOT USE TYPRUN=HOLD ON JOBC
               CARD'
NOHOLD   L     R15,MAXTIME             .  GET JOB TIME REQUESTED
         C     R15,=F'86400'           .  IS IT TIME=1440 ON JOB CARD?
         BL    NOTIME                  .     NO, TIME OK SO FAR
        $SYSMSG '$HASP920 (E6) TEST BATCH CANNOT USE TIME=1440 ON JOB  C
               CARD'                   .
         OI    FLAGS1,ERROR            .    YES, TIME IS INVALID
NOTIME   DS    0H                      .
         TM    FLAGS3,PERFJ            .  IS PERFORM= ON JOB CARD
         BNO   NOPERFJ                 .     NO, PERFORM IS NOT THERE
*        OI    FLAGS1,ERROR            .    YES, NOW SHOWEM WHO IS BOSS
*       $SYSMSG '$HASP920 (E6) JOB CANNOT USE PERFORM ON JOB CARD'
NOPERFJ  DS    0H                      .
*        LH    R15,USERLEN             .  DOES JOB NAME START WITH
*        N     R15,=F'15'              .    USER ID.?
*        BCTR  R15,R0                  .
*        EX    R15,USERCLC             .  THIS WILL TELL ME NOW
*        BE    OKTIMEQ                 .    USER IS OK, CONTINUE
*       $SYSMSG '$HASP920 (E6) JOB MUST CONTAIN USERID IN JOBNAME'
*        OI    FLAGS1,ERROR            .    USER IS AFU, TELL HIM SO
OKTIMEQ  DS    0H                      .
         TM    FLAGS3,TIMEDEF          .  HAS TIME ALREADY DEFAULTED
         BO    RETURN                  .     YES, DO NOT DEFAULT AGAIN
         BAL   R14,OVERTIME            .  OVERRIDE JOB CARD TIME
        $SYSMSG '$HASP920 (E6) JOB TIME LIMIT SET TO "TIME=(,1)"'
         B     RETURN                  .  ALL DONE WITH JOB CARD
*USERCLC CLC   USER(0),JOB             .
         POP   USING                   .
         EJECT                         .
**********************************************************************
*                                                                    *
*  A DD CARD WAS JUST PROCESSED.                                     *
*                                                                    *
*  CHECK TO SEE IF CATALOG NEEDS TO BE READ TO COUNT TAPES.          *
*  IF SO, GO READ CATALOG AND COUNT TAPES.                           *
*                                                                    *
*  DSN=XX.YY(ZZ) WHERE ZZ CAN BE ANYTHING CAUSES ONLY 1 CAT READ.    *
*  DSN=XX.YY     CAUSES UP TO 2 CATALOG READS, ONCE FOR XX.YY        *
*                AND ONCE FOR XX.YY(0) IF XX.YY NOT CATALOGED.       *
*                                                                    *
*                                                                    *
**********************************************************************
CHECKDD  DS    0H
         TM    FLAGS1,TESTBTCH         .  IS THIS JOB TEST BATCH
         BNO   RETURN                  .    NO, DO NOT DO FOR PROD
         TM    UNITSW,UNITAFF+VOLREF   .  DID USER HAVE A UNIT=AFF
         BM    RETURN                  .    YES, NOT A TAPE
         CLC   =C'NULLFILE ',DSN       .  IS THIS A DUMMY DSN
         BE    RETURN                  .    YES, DO NOT READ THE CAT
**********************************************************************
*                                                                    *
*                      CHECK THE UNIT AND VOLSER SPECS.              *
*                      DECIDE WHETHER TO READ CATALOG OR NOT.        *
*                                                                    *
**********************************************************************
         TM    UNITSW,VOLSPEC+VOLTAPE  .  DID USER PUT VOL=SER=XXX IN? X
                                       .     WITH A VOLSER THAT IS TAPE
         BO    KEEPTAPE                .  YES, GO COUNT A TAPE HAHA..
         TM    UNITSW,VOLSPEC          .  DID USER SAY VOL=SER=XXX     X
                                       .    AND NOT SAY TAPE VOLSER
         BO    RETURN                  .
         TM    UNITSW,TAPEUNIT+DISPNEW .  IS IT A NEW TAPE?
         BO    KEEPTAPE                .    YES, GO COUNT A TAPE
         TM    UNITSW,UNITXIST+DISPNEW .  IS IT A NEW ANYTHING
         BO    RETURN                  .    YES, NOT A TAPE, GET OUT
**********************************************************************
*                                                                    *
*                      I CANT DECIDE IF TAPE OR NOT FROM JCL,        *
*                      I MIGHT WANT TO READ THE CATALOG IF THE       *
*                      DSN IS NOT A TEMP.                            *
*                                                                    *
**********************************************************************
LOCATEIT DS    0H                      .
         CLI   DSN,C'&&'               .  IS THIS A TEMP
         BE    CK4TAPE1                .    TEMPS DO NOT MATTER
         CLC   =C'A',DSN               .  IS THIS A VALID DSN
         BH    CK4TAPE1                .    SHOOT, MUST BE A TEMP
         CLC   $SID,=CL4'CPU4'         .  IS THIS CPU4?
         BE    RETURN                  .   YES, SKIP CATALOG READ     XX
                                       .   DUE TO CPU4 NOT DEFINED     X
                                       .   ON ALL PATHS
         TM    FLAGS3,DSNS2            .  SEE IF DSN=XX.YY(ZZ) THERE
         BO    CHECKAGN                .    IF SO, READ XX.YY(0)
         LOCATE CAMLOC                 .  LOCATE THE DSNAME
         C     R15,=F'0'               .  WHAT ABOUT RETURN CODES?
         BNE   CHECKAGN                .     MVS IS A FART IN THE WIND
         CLI   CAMSER,X'00'            .  IS VOLSER SET MEANINGFUL
         BE    RETURN                  .     MVS IS A BIG FART IN THE  X
                                       .     WIND
         TM    CAMDEVB3,UCB3TAPE       .  DOES THE CATALOG SAY "TAPE"
         BNO   RETURN                  .    NOT A TAPE, GET OUT
         B     KEEPTAPE                .    IS A TAPE, REMEMBER IT
CK4TAPE1 DS    0H                      .
         TM    UNITSW,TAPEUNIT         .  IS IT TEMP TAPE?
         BO    KEEPTAPE                .     YES, GO COUNT IT FOR ME
         B     RETURN                  .      NO, JUST GO HOME
CHECKAGN DS    0H                      .  FIRST READ FAILED, CHECK     X
                                       .    AS IF IT WERE GEN (0)
         TRT   DSN,TRTDSN              .  FIND FIRST BLANK
         BZ    RETURN                  .     OOPS, THERE ISNT ONE
         MVC   0(3,R1),=C'(0)'         .  PRETEND IT IS GEN
         LOCATE CAMLOC                 .  READ MY CATALOG AGAIN
         C     R15,=F'0'               .  DID MVS SAY "KIRK IS A FOOL"
         BNE   RETURN                  .     YES, I HATE A SMARTALECK
         TM    CAMDEVB3,UCB3TAPE       .  DID THE CATALOG SAY "TAPE"
         BNO   RETURN                  .     NO, I CANNOT COUNT AIR
KEEPTAPE DS    0H                      .  A TAPE HAS BEEN FOUND
         L     R15,STAPE               .   SO COUNT
         A     R15,=F'1'               .     INTO THE STEP TAPE
         ST    R15,STAPE               .        BUCKET
         B     RETURN                  .  GO TRY AGAIN
         EJECT                         .
**********************************************************************
*                                                                    *
*  EITHER AN EXEC OR A PROC STATEMENT WAS JUST PROCESSED. VERY       *
*  SIMILAR PROCESSING OCCURS FOR BOTH.                               *
*                                                                    *
*  VALIDATE THAT INVALID EXEC AND PROC KEYWORDS ARE NOT USABLE       *
*  BY THE GENERAL POPULATION.                                        *
*                                                                    *
**********************************************************************
CHECKEX  DS    0H                      .  READ NEXT LINE
CHECKPR  DS    0H                      .  READ PREVIOUS LINE
         TM    FLAGS1,TESTBTCH         .  IF THIS IS TEST, CONTINUE
         BNO   RETURN                  .    NOT TEST, WE DONT CARE
         TM    FLAGS2,EPERF            .  DID TEST WANT PERFORM
         BNO   NOEPERF                 .     NO PERFORM NEEDED
*        OI    FLAGS1,ERROR            .  AGAIN I CAN KILL EM
*       $SYSMSG '$HASP920 (E6) TEST BATCH CANNOT USE PERFORM= ON EXEC CB
               ARD'
*        NI    FLAGS2,255-EPERF        .  TURN OFF INDICATOR BIT
NOEPERF  TM    FLAGS2,EADRS            .  DID TEST WANT ADDRSPC=REAL
         BNO   NOEADRS                 .     NO, NOT TOO BRIGHT IS HE
         OI    FLAGS1,ERROR            .    YES, SO CANCEL HIM
        $SYSMSG '$HASP920 (E6) TEST BATCH CANNOT USE ADDRSPC= ON EXEC CA
               ARD'                    .
         NI    FLAGS2,255-EADRS        .  RESET THE BIT
NOEADRS  TM    FLAGS2,EDPRT            .  DID TEST WANT DPRTY=(NN,NN)
         BNO   NOEDPRT                 .     NO, AGAIN WHAT A DUMMY
         OI    FLAGS1,ERROR            .    YES, CANCEL HIS REAR
        $SYSMSG '$HASP920 (E6) TEST BATCH CANNOT USE DPRTY= ON EXEC CARC
               D'
         NI    FLAGS2,255-EDPRT        .  RESET THE BIT
NOEDPRT  DS    0H                      .
         B     RETURN                  .  WEE DUN NOW.
         EJECT                         .
**********************************************************************
*                                                                    *
*  RANDOMLY ENTERED KEYWORD PROCESSING ROUTINES FOR JOB CARDS        *
*  FOLLOW HERE.                                                      *
*                                                                    *
**********************************************************************
JADRSP   DS    0H                      .  PROCESS ADRSP     JOB
         CLC   =C'REAL',USERTXT        .    IS IT ADRSP=REAL?
         BNE   SKIPKEY                 .      NO, CONTINUE
         OI    FLAGS1,REALMEM          .     YES, REMEMBER THIS
         B     SKIPKEY                 .
JTIME    DS    0H                      .  PROCESS TIME      JOB
         OI    FLAGS3,TIMEJJ           .  REMEMBER TIME WAS HERE
         SLR   R14,R14                 .  CLEAR 14 FOR IC
         SLR   R15,R15                 .  CLEAR 15 TIME ACCUMULATOR
         IC    R14,USER#               .  GET OPERAND COUNT FIELD
         C     R14,=F'2'               .  ANY OPERANDS?
         BE    DOTIME2                 .    YES, 2 OF THEM
         C     R14,=F'1'               .
         BE    DOTIME1                 .    YES, 1 OPERAND
         B     SKIPKEY                 .     NO, 0 OPERANDS
DOTIME1  IC    R14,USERL1              .  GET FIRST OPERAND LENGTH
         C     R14,=F'0'               .  DOES IT HAVE ANY LENGTH
         BE    SKIPKEY                 .  DOES ME NO GOOD
         BCTR  R14,R0                  .  GET LENGTH RELATIVE 0
         XC    DWORK,DWORK             .
         EX    R14,TPACK               .  PACK TIME OPERAND
         CVB   R14,DWORK               .  PUT TIME IN MINUTES IN R14
         SRDA  R14,32                  .
         M     R14,=F'60'              .  TIMES 60 TO GET SECONDS
         B     TCOMPARE                .
DOTIME2  IC    R14,USERL1              .  GET OPERAND LENGTH 1
         C     R14,=F'0'               .  IS IT 0?
         BE    DOTIME2A                .     YES, GET SECOND OPERAND
         BCTR  R14,R0                  .      NO, DECREMENT RELATIVE 0
         XC    DWORK,DWORK             .
         EX    R14,TPACK               .  PACK MINUTES
         CVB   R14,DWORK               .
         SRDA  R14,32                  .  CONVERT TO
         M     R14,=F'60'              .      SECONDS  IN R15
         SLR   R14,R14                 .
         IC    R14,USERL1              .  GET LENGTH AGAIN
DOTIME2A LA    R1,3(R14,R4)            .  INDEX TO USERL2 LENGTH FLD
         IC    R14,0(R1)               .    AND PULL IT IN
         C     R14,=F'0'               .  IS LENGTH 2 =0
         BE    TCOMPARE                .
         BCTR  R14,R0                  .  CONVERT
         XC    DWORK,DWORK             .
         EX    R14,TPACK1              .     TIME
         CVB   R14,DWORK               .         TO BINARY
         AR    R15,R14                 .  GET TOTAL TIME REQUESTED
TCOMPARE C     R15,MAXTIME             .  IS THIS ONE BIGGER THAN MAX
         BNH   TEXIT                   .    YES, JUST FLOW HOME
         ST    R15,MAXTIME             .     NO, SAVE FOR LATER
TEXIT    DS    0H                      .  PREPARE TO LEAVE FOR HOME
         L     R15,MAXTIME             .  GET THE USER SPECIFIED TIME
         ICM   R15,B'1000',=X'7F'      .   SET FLAG ID TO IEFUTL
         ST    R15,JCTUSERF            .    AND SAVE IN JCT FOR IEFUTL
         B     SKIPKEY                 .  SKIP THIS TIME KEY
TPACK    PACK  DWORK,USERTXT(0)        .
TPACK1   PACK  DWORK,1(0,R1)           .
JCLASS   DS    0H                      .  PROCESS CLASS     JOB
         MVC   CLASS,USERTXT           .
         OI    FLAGS2,JCARD            .  REMEMBER JOB CARD
         B     SKIPKEY                 .
JREGION  DS    0H                      .  PROCESS REGION    JOB
         SLR   R14,R14                 .  GET NUMBER OF DIGITS
         IC    R14,USERL1              .    IN REGION REQUEST
         C     R14,=F'15'              .  DID USER GO OVERBOARD
         BH    SKIPKEY                 .     LOOKS THAT WAY
         C     R14,=F'0'               .  DID USER ACT STUPID
         BE    SKIPKEY                 .     LOOKS THAT WAY
         BCTR  R14,R0                  .  SUBTRACT 2 FOR
         BCTR  R14,R0                  .     EXECUTE OF PACK
         EX    R14,TPACK               .  PACK THE REGION VALUE
         CVB   R14,DWORK               .     AND MAKE IT BINARY
         ST    R14,REGIONJ             .  REGION= SAVED FOR LATER
         B     SKIPKEY                 .
JPRTY    DS    0H                      .  PROCESS PRTY      JOB
         OI    FLAGS1,JOBPRTY          .  PRTY=XX ON JOB CARD
         B     SKIPKEY                 .
JTYPRUN  DS    0H                      .  PROCESS TYPRUN    JOB
         CLC   =C'HOLD',USERTXT        .    IS IT TYPRUN=HOLD?
         BNE   SKIPKEY                 .      NO, CONTINUE
         OI    FLAGS1,JOBHOLD          .     YES, REMEMBER THIS
         B     SKIPKEY                 .
JJOBN    DS    0H                      .  PROCESS JOBNAME   JOB
         SLR   R15,R15                 .  CLEAR FOR EXEC INSTR
         IC    R15,USERL1              .  GET JOBNAME LENGTH
         MVC   JOB,=CL8'        '      .  PRE CLEAR JOBNAME FIELD
         N     R15,=F'15'              .  LIMIT JOB NAME LEN
         BCTR  R15,R0                  .  RELATIVE 0 FOR EX
         EX    R15,JOBMVC              .  MOVE IN JOBNAME
         B     SKIPKEY                 .
JOBMVC   MVC   JOB(0),USERTXT          .  EXECUTED JOBNAME MOVE
JPERF    DS    0H                      .
         OI    FLAGS3,PERFJ            .  USER SAID PERFORM=
         B     SKIPKEY                 .
JOBUSER  DS    0H                      .  PROCESS JOBNAME   JOB
         SLR   R15,R15                 .  CLEAR FOR EXEC INSTR
         IC    R15,USERL1              .  GET USER    LENGTH
         N     R15,=F'15'              .  LIMIT USER LENGTH
         STH   R15,USERLEN             .  SAVE USER LENGTH
         MVC   USER,=CL8'        '     .  PRE CLEAR USER    FIELD
         BCTR  R15,R0                  .  RELATIVE 0 FOR EX
         EX    R15,USERMVC             .  MOVE IN USER
         B     SKIPKEY                 .
USERMVC  MVC   USER(0),USERTXT         .  EXECUTED USER    MOVE
         EJECT                         .
**********************************************************************
*                                                                    *
*   RANDOMLY ENTERED EXEC AND PROC KEYWORD PROCESSOR ROUTINES        *
*   FOLLOW HERE.                                                     *
*                                                                    *
**********************************************************************
EXPRFMD  DS    0H                      .  PROCESS PERFORM.XXX=    EXEC
         OI    FLAGS2,EPERF            .
         B     SKIPKEY                 .
EXPRFM   DS    0H                      .  PROCESS PERFORM=        EXEC
         OI    FLAGS2,EPERF            .
         B     SKIPKEY                 .
EXADRSD  DS    0H                      .  PROCESS ADRSP.XXX=      EXEC
         OI    FLAGS2,EADRS            .
         B     SKIPKEY                 .
EXADRS   DS    0H                      .  PROCESS ADRSP=          EXEC
         OI    FLAGS2,EADRS            .
         B     SKIPKEY                 .
EXDPRTD  DS    0H                      .  PROCESS DPRTY.XXX=      EXEC
         OI    FLAGS2,EDPRT            .
         B     SKIPKEY                 .
EXDPRT   DS    0H                      .  PROCESS DPRTY=          EXEC
         OI    FLAGS2,EDPRT            .
         B     SKIPKEY                 .
EXTIMED  DS    0H                      .  PROCESS TIME.XX=        EXEC
         OI    FLAGS2,ETIME            .
         SLR   R14,R14                 .  CLEAR 14 FOR IC
         SLR   R15,R15                 .  CLEAR 15 TIME ACCUMULATOR
         IC    R14,USER#               .  GET OPERAND COUNT FIELD
         C     R14,=F'3'               .  ANY OPERANDS?
         BE    DFTIME3                 .    YES, 3 OF THEM
         C     R14,=F'2'               .  ANY OPERANDS?
         BE    DFTIME2                 .    YES, 2 OF THEM
         B     SKIPKEY                 .     NO, 0 OPERANDS
DFTIME2  IC    R14,USERL1              .  GET FIRST OPERAND LENGTH
         LA    R1,3(R14,R4)            .  INDEX TO SECOND OPERAND LEN
         IC    R14,0(R1)               .  GET THAT LENGTH
         C     R14,=F'0'               .
         BE    SKIPKEY                 .
         BCTR  R14,R0                  .
         EX    R14,TPACK5              .  PACK MINUTES
         CVB   R14,DWORK               .
         SRDA  R14,32                  .
         M     R14,=F'60'              .
         B     FCOMPARE                .
DFTIME3  DS    0H                      .  GET OPERAND LENGTH 1
         IC    R14,USERL1              .
         LA    R1,3(R14,R4)            .  POINT TO MINUTES LEN
         IC    R14,0(R1)               .  GET THE LENGTH
         C     R14,=F'0'               .
         BE    DFTIME3A                .  NO MINUTES, GET SECONDS
         BCTR  R14,R0                  .
         EX    R14,TPACK5              .
         CVB   R14,DWORK               .
         SRDA  R14,32                  .
         M     R14,=F'60'              .
DFTIME3A DS    0H                      .
         SLR   R14,R14                 .
         IC    R14,0(R1)               .  GET LENGTH AGAIN
         LA    R1,1(R14,R1)            .  POINT TO LAST FIELD
         IC    R14,0(R1)               .  GET THE LENGTH
         C     R14,=F'0'               .
         BE    FCOMPARE                .  NO SECONDS, COMPARE
         BCTR  R14,R0                  .
         EX    R14,TPACK5              .
         CVB   R14,DWORK               .
         AR    R15,R14                 .  ADD SECONDS AND MINUTES
FCOMPARE C     R15,MAXTIME             .  IS THIS ONE BIGGER THAN MAX
         BNH   FEXIT                   .    YES, JUST FLOW HOME
         ST    R15,MAXTIME             .     NO, SAVE FOR LATER
FEXIT    B     SKIPKEY                 .  SKIP THIS TIME KEY
TPACK5   PACK  DWORK,1(0,R1)           .
EXTIME   DS    0H                      .  PROCESS TIME=           EXEC
         OI    FLAGS2,ETIME            .
         SLR   R14,R14                 .  CLEAR 14 FOR IC
         SLR   R15,R15                 .  CLEAR 15 TIME ACCUMULATOR
         IC    R14,USER#               .  GET OPERAND COUNT FIELD
         C     R14,=F'2'               .  ANY OPERANDS?
         BE    DETIME2                 .    YES, 2 OF THEM
         C     R14,=F'1'               .
         BE    DETIME1                 .    YES, 1 OPERAND
         B     SKIPKEY                 .     NO, 0 OPERANDS
DETIME1  IC    R14,USERL1              .  GET FIRST OPERAND LENGTH
         C     R14,=F'0'               .  DOES IT HAVE ANY LENGTH
         BE    SKIPKEY                 .  DOES ME NO GOOD
         BCTR  R14,R0                  .  GET LENGTH RELATIVE 0
         XC    DWORK,DWORK             .
         EX    R14,TPACK2              .  PACK TIME OPERAND
         CVB   R14,DWORK               .  PUT TIME IN MINUTES IN R14
         SRDA  R14,32                  .
         M     R14,=F'60'              .  TIMES 60 TO GET SECONDS
         B     ECOMPARE                .
DETIME2  IC    R14,USERL1              .  GET OPERAND LENGTH 1
         C     R14,=F'0'               .  IS IT 0?
         BE    DETIME2A                .     YES, GET SECOND OPERAND
         BCTR  R14,R0                  .      NO, DECREMENT RELATIVE 0
         XC    DWORK,DWORK             .
         EX    R14,TPACK2              .  PACK MINUTES
         CVB   R14,DWORK               .
         SRDA  R14,32                  .  CONVERT TO
         M     R14,=F'60'              .      SECONDS  IN R15
         SLR   R14,R14                 .
         IC    R14,USERL1              .  GET LENGTH AGAIN
DETIME2A LA    R1,3(R14,R4)            .  INDEX TO USERL2 LENGTH FLD
         IC    R14,0(R1)               .    AND PULL IT IN
         C     R14,=F'0'               .  IS LENGTH 2 =0
         BE    ECOMPARE                .
         BCTR  R14,R0                  .  CONVERT
         XC    DWORK,DWORK             .
         EX    R14,TPACK3              .     TIME
         CVB   R14,DWORK               .         TO BINARY
         AR    R15,R14                 .  GET TOTAL TIME REQUESTED
ECOMPARE C     R15,MAXTIME             .  IS THIS ONE BIGGER THAN MAX
         BNH   EEXIT                   .    YES, JUST FLOW HOME
         ST    R15,MAXTIME             .     NO, SAVE FOR LATER
EEXIT    B     SKIPKEY                 .  SKIP THIS TIME KEY
TPACK2   PACK  DWORK,USERTXT(0)        .
TPACK3   PACK  DWORK,1(0,R1)           .
         EJECT                         .
**********************************************************************
*                                                                    *
*  RANDOMLY ENTERED DD TEXT KEY PROCESSORS FOLLOW HERE.              *
*                                                                    *
**********************************************************************
DDDISP   DS    0H                      .  DD CARD DISPOSITION
         CLC   =C'NEW',USERTXT         .  IS IT DISP=(NEW,,,,,,,)
         BE    ISNEW                   .    YES, GO SET BIT
         SLR   R14,R14                 .  CLEAR FOR NEXT IC
         IC    R14,USERL1              .  GET THE FIRST DISP LENGTH
         C     R14,=F'0'               .  IS IT DISP=(,,,,,) IE:NEW
         BNE   SKIPKEY                 .    NO, JUST GET OUT
ISNEW    DS    0H                      .
         OI    UNITSW,DISPNEW          .
         B     SKIPKEY                 .
DUNITK   DS    0H                      .
         OI    UNITSW,UNITXIST         .  UNIT KEYWORD FOUND
         CLC   =C'TAPE',USERTXT        .  IS IT UNIT=TAPE
         BE    ISTAPE                  .
         CLC   =C'3400',USERTXT        .  IS IT UNIT=3400
         BE    ISTAPE                  .
         B     SKIPKEY                 .
ISTAPE   DS    0H                      .
         OI    UNITSW,TAPEUNIT         .  REMEMBER TAPE DSN
         B     SKIPKEY                 .
DDAFF    DS    0H                      .  UNIT=AFF=
         OI    UNITSW,UNITAFF          .    TURN ON BIT SETTING
         B     SKIPKEY                 .       AND GET NEXT BIG ONE
DDVOLS   DS    0H                      .  VOL=SER=
         TM    FLAGS1,TESTBTCH         .  IS IT TEST BATCH
         BNO   SKIPKEY                 .
         SLR   R14,R14                 .  CLEAR FOR GODLIKE INSERT
         IC    R14,USERL1              .     GET SERIAL LIST COUNT
         C     R14,=F'0'               .  DID USER HAVE A SER LIST?
         BE    SKIPKEY                 .    NO, TRY TO FAKE ME OUT MAN.
         OI    UNITSW,VOLSPEC          .    YES, TURN ON BIT SETTING
         SLR   R15,R15                 .
         L     R14,16                  .  POINT TO CVT
         L     R14,CVTILK2-CVT(R14)    .  GET UCB LOOKUP TABLE
         OI    UNITSW,VOLTAPE          .  MAKE THE DEFAULT TO TAPE VOL
         PUSH  USING                   .
UCBLOOP1 DS    0H                      .
         ICM   R15,B'0011',0(R14)      .  POINT TO A UCB
         LA    R14,2(R14)              .    AND POINT TO NEXT LOOKUP
         USING UCBOB,R15               .  ADDRESS UCB BY NAME
         CL    R15,=X'0000FFFF'        .  IS THIS THE END OF UCB TAB?
         BE    SKIPKEY                 .
         CLC   UCBVOLI,USERTXT         .  IS VOLSER A MATCH
         BE    UCBCK1                  .     YES, CHECK IF TAPE UCB
         B     UCBLOOP1                .      NO, LOOP UNTIL DONE
UCBCK1   DS    0H                      .
         TM    UCBTBYT3,UCB3TAPE       .
         BO    SKIPKEY                 .
         NI    UNITSW,FF-VOLTAPE       .
         B     SKIPKEY                 .       AND GET NEXT KEYWORD
         POP   USING                   .
DDREFS   DS    0H                      .  VOL=REF=
         OI    UNITSW,VOLREF           .  AND REMEMBER IT FOR LATER
         B     SKIPKEY                 .       AND GET NEXT KEYWORD
DDSNK    DS    0H                      .  DSNAME KEY FOUND
         CLI   DSN,C' '                .  IS DSN ALREADY SPECIFIED
         BNE   SKIPKEY                 .  SKIP THIS NONSENSE
         SLR   R14,R14                 .
         IC    R14,USERL1              .  GET LENGTH OF DSNAME
         C     R14,=F'0'               .  IS DSNAME THERE
         BE    SKIPKEY                 .   NO, JUST GET OUT
         C     R14,=F'127'             .  IS HIGH BIT SET
         BH    TWODSNS                 .
         BCTR  R14,R0                  .
         EX    R14,DSNMVC              .  MOVE DSNAME TO WORK
         B     SKIPKEY                 .
DSNMVC   MVC   DSN(0),USERTXT          .
TWODSNS  DS    0H                      .
         OI    FLAGS3,DSNS2            .
         IC    R14,USERTXT             .  GET LENGTH OF FIRST DSN
         C     R14,=F'44'              .  IS IT VALID
         BH    SKIPKEY                 .    NO, GET OUT
         C     R14,=F'0'               .  IS IT REALLY VALID
         BE    SKIPKEY                 .    NO, GET OUT
         BCTR  R14,R0                  .
         EX    R14,DSN2MVC             .  MOVE IN FIRST DSN
         B     SKIPKEY                 .
DSN2MVC  MVC   DSN(0),USERTXT+1        .
DDUMMK   DS    0H                      .  DUMMY DSNAME FOUND
         MVC   DSN(09),=C'NULLFILE '   .
         B     SKIPKEY                 .
DDDK     DS    0H                      .
         MVI   DD,C' '                 .  CLEAR DDNAME
         MVC   DD+1(L'DD-1),DD         .
         SLR   R14,R14                 .
         IC    R14,USER#               .  GET COUNT OF OPERANDS
         C     R14,=F'0'               .
         BE    SKIPKEY                 .  NO DDNAME ENTERED
         IC    R14,USERL1              .  GET DDNAME LENGTH
         C     R14,=F'0'               .
         BE    SKIPKEY                 .  NO DDNAME LENGTH
         BCTR  R14,R0                  .
         EX    R14,DDMVC               .
         B     SKIPKEY                 .
DDMVC    MVC   DD(0),USERTXT           .
         EJECT                         .
**********************************************************************
*                                                                    *
* SKIPKEY IS A ROUTINE TO SKIP OVER THE CURRENT TEXT KEYWORD.        *
* IT SKIPS OVER ALL THE CURRENT MAJOR TEXT KEYWORD AND ALL           *
* PARENTHESIZED SUBFIELDS FOR THE MAJOR KEYWORD.                     *
*                                                                    *
* IT RETURNS DIRECTLY TO "DOKEY"                                     *
*                                                                    *
**********************************************************************
SKIPKEY  DS    0H                      .
         L     R1,CURPTR               .  R1=KEY
         SLR   R2,R2                   .  CLEAR FOR
         SLR   R3,R3                   .    FURTHER PROCESSING
         LA    R1,1(R1)                .  POINT PAST TEXT KEY
         IC    R2,0(R1)                .  GET COUNT FIELD
         LA    R1,1(R1)                .  POINT TO LENG OR COUNT
         C     R2,=F'0'                .  IS COUNT 0
         BE    EXIT1                   .
LOOP1    TM    0(R1),X'80'             .  IS THIS COUNT FIELD
         BO    MORE#                   .  GO PROCESS COUNT FIELD
         IC    R3,0(R1)                .  GET LENGTH
         LA    R1,1(R3,R1)             .    INDEX PAST THIS KEY
         BCT   R2,LOOP1                .
         B     EXIT1                   .
MORE#    IC    R3,0(R1)                .  GET COUNT FIELD
         N     R3,=F'127'              .  STRIP OFF HIGH BIT
         LA    R2,0(R3,R2)             .  ADD NEW COUNT TO OLD
         LA    R1,1(R1)                .  POINT TO NEW COUNT OR LEN
         BCT   R2,LOOP1                .
EXIT1    ST    R1,CURPTR               .  KEY HAS BEEN SKIPPED,SAVE PTR
         B     DOKEY                   .  RETURN TO NEXT PROCESS
         EJECT                         .
**********************************************************************
*                                                                    *
* SKIPKEY6 IS A ROUTINE TO SKIP OVER THE CURRENT TEXT KEYWORD.       *
* IT SKIPS OVER ALL THE CURRENT MAJOR TEXT KEYWORD AND ALL           *
* PARENTHESIZED SUBFIELDS FOR THE MAJOR KEYWORD.                     *
*                                                                    *
* IT RETURNS INDIRECTLY THRU R14.                                    *
*                                                                    *
**********************************************************************
SKIPKEY6 DS    0H                      .
         L     R1,CURPTR               .  R1=KEY
         SLR   R2,R2                   .  CLEAR FOR
         SLR   R3,R3                   .    FURTHER PROCESSING
         LA    R1,1(R1)                .  POINT PAST TEXT KEY
         IC    R2,0(R1)                .  GET COUNT FIELD
         LA    R1,1(R1)                .  POINT TO LENG OR COUNT
         C     R2,=F'0'                .  IS COUNT 0
         BE    EXIT6                   .
LOOP6    TM    0(R1),X'80'             .  IS THIS COUNT FIELD
         BO    MORE#6                  .  GO PROCESS COUNT FIELD
         IC    R3,0(R1)                .  GET LENGTH
         LA    R1,1(R3,R1)             .    INDEX PAST THIS KEY
         BCT   R2,LOOP6                .
         B     EXIT6                   .
MORE#6   IC    R3,0(R1)                .  GET COUNT FIELD
         N     R3,=F'127'              .  STRIP OFF HIGH BIT
         LA    R2,0(R3,R2)             .  ADD NEW COUNT TO OLD
         LA    R1,1(R1)                .  POINT TO NEW COUNT OR LEN
         BCT   R2,LOOP6                .
EXIT6    ST    R1,CURPTR               .  KEY HAS BEEN SKIPPED,SAVE PTR
         BR    R14                     .  RETURN TO NEXT PROCESS
         EJECT                         .
***********************************************************************
*                                                                     *
*  OVERTIME SCANS FOR THE TIME KEYWORD ON THE JOB CARD AND            *
*  DELETES IT FROM THE TEXT RECORD. THEN IT ATTACHES A DEFAULT        *
*  JOB TIME FACTOR OF TIME=(,1)                                       *
*                                                                     *
***********************************************************************
OVERTIME DS    0H                      .
         ST    R14,R14SAVE             .  REMEMBER WHERE HOME IS
         TM    FLAGS3,TIMEJJ           .  DID USER EVEN USE TIME
         BNO   OVDEFTM                 .   NO, JUST DEFAULT TIME
         LA    R4,STRJKEY              .  GET FIRST JOB KEY
         ST    R4,CURPTR               .  SAVE FOR SCANNER
OVLOOP1  DS    0H                      .
         CLI   USERKEY,ENDK            .  IS THIS KEY THE END ?
         BE    OVDEFTM                 .   YES, DO DEFAULTS
         CLI   USERKEY,TIMEJK          .  IS THIS KEY JOB TIME?
         BE    OVFND1                  .   YES, FIND NEXT KEY
         BAL   R14,SKIPKEY6            .  SKIP  CURRENT KEY
         L     R4,CURPTR               .
         B     OVLOOP1                 .
OVFND1   DS    0H                      .  TIME KEY FOUND
         L     R4,CURPTR               .
         ST    R4,TIMEPTR              .  SAVE IT
         BAL   R14,SKIPKEY6            .  SKIP TO NEXT KEY
         L     R4,CURPTR               .
         ST    R4,OTHPTR               .  SAVE NEXT KEY PTR
         S     R4,TIMEPTR              .  GET LENGTH OF TIME KEY
         ST    R4,TIMELEN              .  WE GETIN CLOSE NOW
         LH    R4,STRLTH               .  GET LENGTH OF ALL
         LA    R4,0(R4,R7)             .  POINT TO PAST LAST KEY
         S     R4,OTHPTR               .  LEN OF OTHER KEY
         ST    R4,OTHLEN               .  SAVE LEN OTHER KEY
         LM    R14,R1,TIMEPTR          .  SET FOR MVCL
         AR    R15,R1                  .  MAKE IT A BIG ONE
         MVCL  R14,R0                  .  TIME KEY IS SQUOZE OUT
         LH    R14,STRLTH              .
         S     R14,TIMELEN             .  TIME IS GONE NOW
         STH   R14,STRLTH              .  FOR GOOD PERHAPS
OVDEFTM  DS    0H                      .  NOW DEFAULT THE TIME
         LH    R14,STRLTH              .
         LA    R14,0(R14,R7)           .  POINT TO PAST LAST KEY
         S     R14,=F'1'               .  POINT TO ENDK
         MVC   0(03,R14),=AL1(TIMEJK,2,0)
         MVC   3(02,R14),=X'01F1'      .
         MVI   5(R14),ENDK             .  TIME=(,1)
         LH    R14,STRLTH              .
         LA    R14,5(R14)              .
         STH   R14,STRLTH              .
         L     R14,R14SAVE             .
         BR    R14                     .
         EJECT                         .
**********************************************************************
*                                                                    *
*  THE MANY FACES OF RETURN.                                         *
*                                                                    *
*  "RETURN" IS THE NORMAL EXIT POINT FOR THIS ROUTINE.               *
*                                                                    *
*   RNOMEM1 IS FOR  RETURNING WHEN MEMORY HAS NOT BEEN GOTTEN.       *
*                                                                    *
*   RNOMEM4 IS FOR  RETURNING WHEN MEMORY HAS NOT BEEN GOTTEN,       *
*      AND I NEVER WANT TO BE CALLED AGAIN.                          *
*                                                                    *
*                                                                    *
**********************************************************************
RETURN   DS    0H                      .
         NI    FLAGS,255-JOBU-DDU-EXECU-PROCU . RESET TXT TYPE FLAGS
         TM    FLAGS,LASTCALL          .  IS THIS LAST CALL
         BNO   NOTLAST                 .
         SLR   R15,R15                 .
         ST    R15,PCEUSER0            .  CLEAR POINTER
         LH    R3,RC                   .  GET THE RETURN CODE
         L     R4,4(R13)               .  SAVE USERS SAVE AREA
         L     R1,GETSIZE              .  GET FREEMAIN SIZE
         L     R14,12(R4)              .  GET RETURN AREA POINTER
         LR    R2,R13                  .
         FREEMAIN R,LV=(1),A=(2)       .  FREE GETMAIN AREA
         LR    R15,R3                  .  PUT RC INTO R15
         LR    R13,R4                  .  PUT SAVE ARE BACK IN 13
         LM    R0,R12,20(R13)          .
         BR    R14                     .  GO BACK TO MASTER
NOTLAST  DS    0H                      .  GET THE RETURN CODE
         LH    R15,RC                  .  GET THE RETURN CODE
         L     R13,4(R13)              .  RESET SAVE AREA POINTER
         L     R14,12(R13)             .
         LM    R0,R12,20(R13)          .
         BR    R14                     .  GOBACK
RNOMEM1  DS    0H                      .  ERROR RETURN, NOMEMORY AVAIL
         LM    R14,R12,12(R13)         .  RESTORE USERS REGISTERS
         SLR   R15,R15                 .  SET RETURN CODE 0
         BR    R14                     .  RETURN TO CALLER
RNOMEM4  DS    0H                      .  ERROR RETURN, NOMEMORY AVAIL
         NI    JCTXMASK,FF-EX006       .  DO NOT CALL AGAIN
         LM    R14,R12,12(R13)         .  RESTORE USERS REGISTERS
         SLR   R15,R15                 .  SET RETURN CODE 0
         BR    R14                     .  RETURN TO CALLER
         EJECT                         .
GETSIZE  DC    0F'0',AL1(SUBPOOL),AL3(WORKLEN)
**********************************************************************
*                                                                    *
*  FOLLOWS IS A TABLE OF "BASE/DISPLACEMENT" ADDRESSES FOR USE IN    *
*  BRANCHING TO TEXT KEY PROCESSOR ROUTINES.                         *
*                                                                    *
**********************************************************************
JOBTABLE DC    256S(SKIPKEY)           . DEFAULT IS IGNORE TEXT RECORD
         ORG   JOBTABLE+2*ADRSPJK      .
         DC    S(JADRSP)               . PROCESS JOB ADRSP=REAL
         ORG   JOBTABLE+2*TIMEJK       .
         DC    S(JTIME)                . PROCESS JOB TIME=
         ORG   JOBTABLE+2*CLASSJK      .
         DC    S(JCLASS)               . PROCESS JOB CLASS=
         ORG   JOBTABLE+2*REGINJK      .
         DC    S(JREGION)              . PROCESS JOB REGION=
         ORG   JOBTABLE+2*PRFMJK       .
         DC    S(JPERF)                . PROCESS JOB PERFORM=
         ORG   JOBTABLE+2*PRTYJK       .
         DC    S(JPRTY)                . PROCESS JOB PRTY=
         ORG   JOBTABLE+2*TYPRUNJK     .
         DC    S(JTYPRUN)              . PROCESS JOB TYPRUN=
         ORG   JOBTABLE+2*JOBK         .
         DC    S(JJOBN)                . PROCESS JOB NAME=
         ORG   JOBTABLE+2*USERK        .
         DC    S(JOBUSER)              . PROCESS JOB USER=
         ORG   JOBTABLE+2*ENDK         .
         DC    S(LASTKEY)              . PROCESS LAST KEY FOR JOB
         ORG   JOBTABLE+2*PRFMPEK      .
         DC    S(EXPRFMD)              . PROCESS EXEC PERFORM.XXX=
         ORG   JOBTABLE+2*PRFMEEK      .
         DC    S(EXPRFM)               . PROCESS EXEC PERFORM=
         ORG   JOBTABLE+2*ADRSPPEK     .
         DC    S(EXADRSD)              . PROCESS EXEC ADRSP.XXX=
         ORG   JOBTABLE+2*ADRSPEEK     .
         DC    S(EXADRS)               . PROCESS EXEC ADRSP=
         ORG   JOBTABLE+2*SDPPEK       .
         DC    S(EXDPRTD)              . PROCESS EXEC DPRTY.XX=
         ORG   JOBTABLE+2*SDPEEK       .
         DC    S(EXDPRT)               . PROCESS EXEC DPRTY=
         ORG   JOBTABLE+2*TIMEPEK      .
         DC    S(EXTIMED)              . PROCESS EXEC TIME.XXX=
         ORG   JOBTABLE+2*TIMEEEK      .
         DC    S(EXTIME)               . PROCESS EXEC TIME=
         ORG   JOBTABLE+2*UNITK        . PROCESS DD UNIT=
         DC    S(DUNITK)               .
         ORG   JOBTABLE+2*DSNAMEK      . PROCESS DD DSNAME=
         DC    S(DDSNK)               .
         ORG   JOBTABLE+2*DUMMK        . PROCESS DD DUMMY
         DC    S(DDUMMK)              .
         ORG   JOBTABLE+2*DDK          . PROCESS DD NAME=
         DC    S(DDDK)                .
         ORG   JOBTABLE+2*AFFMK        . PROCESS DD UNIT=AFF=
         DC    S(DDAFF)               .
         ORG   JOBTABLE+2*SERMK        . PROCESS DD VOL=SER=XXXX
         DC    S(DDVOLS)              .
         ORG   JOBTABLE+2*REFMK        . PROCESS DD VOL=REF=XXXX
         DC    S(DDREFS)              .
         ORG   JOBTABLE+2*DISPK        . PROCESS DD DISP=
         DC    S(DDDISP)               .
         ORG   ,                       .
MSG1     WTO   '$HASP920 (E6) -- JOB FAILED REPUBLICBANK JCL EXIT. REVIC
               EW SYSTEM MESSAGES FOR EXACT CAUSES',MF=L,              C
               ROUTCDE=(2),DESC=(6)   .
MSG1L    EQU   *-MSG1                  .
STATSMSX DC    127CL1' '               .
         ORG   STATSMSX                .
         DC    C'$HASP920 (E6) CLASS='  .
STATC    DC    CL1' '                  .
         DC    C', TIME='              .
STATT    DS    CL5                     .
         DC    C', JTAPES='            .
STATJ    DS    CL5                     .
         DC    C', STAPES='            .
STATS    DS    CL5                     .
         DC    C', JREGION='           .
STATR    DS    CL4                     .
STATLEN  EQU   *-STATSMSX              .
         ORG   STATSMSX                .
STATMSG  DS    CL(STATLEN)             .
         ORG   ,                       .
         EJECT                         .
**********************************************************************
*                                                                    *
*  FOLLOWS IS A TABLE OF LIMITS FOR EACH AVAILABLE JOB CLASS.        *
*  STEPTAP IS MAX STEP TAPES ALLOWED                                 *
*  JOBTAPE IS MAX JOB TAPES ALLOWED                                  *
*  JOBCPU IS MAX CPU TIME FOR THE JOB CLASS                          *
*  DFLT TIME IS WHAT TO USE WHEN CLASS OR TIME IS DEFAULTED          *
*                                                                    *
**********************************************************************
CLASSTAB DS    0F                      .
*              STEPTAP |JOBTAPE |JOBCPU  | CLASS |DFLT TIME
CB       DC    F'32768',F'32768',F'86399',C'BP  ',F'000'
CI       DC    F'32768',F'32768',F'86399',C'IC  ',F'000'
CK       DC    F'32768',F'32768',F'86399',C'KP  ',F'000'
CO       DC    F'32768',F'32768',F'86400',C'OP  ',F'000'    CPCS
CP       DC    F'32768',F'32768',F'86400',C'PP  ',F'000'    IMS
CQ       DC    F'32768',F'32768',F'86400',C'QP  ',F'000'    CICS
CR       DC    F'32768',F'32768',F'86399',C'RP  ',F'000'
CU       DC    F'32768',F'32768',F'86399',C'UP  ',F'000'
CV       DC    F'32768',F'32768',F'86399',C'VP  ',F'000'
CX       DC    F'32768',F'32768',F'86399',C'XC  ',F'000'
CY       DC    F'32768',F'32768',F'86400',C'YP  ',F'000'    IMS TEST
C0       DC    F'00000',F'00000',F'00020',C'0T  ',F'001'
C1       DC    F'00000',F'00000',F'00060',C'1T  ',F'001'
C2       DC    F'00000',F'00000',F'00150',C'2T  ',F'001'
C3       DC    F'00000',F'00000',F'03000',C'3T  ',F'001'
C4       DC    F'00001',F'32768',F'00060',C'4T  ',F'001'
C5       DC    F'00002',F'32768',F'00150',C'5T  ',F'001'
C6       DC    F'32768',F'32768',F'03000',C'6T  ',F'001'
C7       DC    F'00000',F'00000',F'00030',C'7S  ',F'001'
C8       DC    F'00003',F'32768',F'01000',C'8S  ',F'001'
C9       DC    F'32768',F'32768',F'86399',C'9R  ',F'000'
         DC    F'32768',F'32768',F'86399',C'*   ',F'000'  TABLE END
TRTDSN   DC    256XL1'00'              .  TRT TABLE TO FIND FIRST
         ORG   TRTDSN+X'40'            .    BLANK IN DSN
         DC    XL1'04'                 .
         ORG   ,                       .
TRTNUM   DC    256XL1'00'              .  TRT TABLE TO FIND
         ORG   TRTNUM+C'0'             .    NUMERIC BYTE IN FIELD
         DC    10XL1'04'               .
         ORG   ,                       .
         LTORG                         .
         EJECT                         .
CLASSD   DSECT                         .
CSTEPTP  DS    F                       .
CJOBTP   DS    F                       .
CJOBTIME DS    F                       .
CID      DS    C                       .
CBATCH   DS    CL1                     . BATCH FLAGS
CTEST    EQU   C'T'                    .  TEST
CSPEC    EQU   C'S'                    . SPECIAL
CRECOV   EQU   C'R'                    . RECOVERY
CPROD    EQU   C'P'                    . PRODUCTION
CCNTRL   EQU   C'C'                    . LIMITED TO TECH AND PC
FILLER   DS    CL2                     .
CDTIME   DS    F                       .
CENTL    EQU   *-CSTEPTP               .
         EJECT                         .
WORK     DSECT                         .
SAVE     DS    18F                     .
DWORK    DS    D                       .  AREA FOR DATA CONVERSION
R14SAVE  DS    F                       .  SAVE AREA FOR R14
CURPTR   DS    F                       .  CURRENT TEXT KEY POINTER
TIME     DS    F                       .  TIME= AREA
STAPE    DS    F                       .  STEP TAPES
STAPEM   DS    F                       .  STEP TAPE MAX
JTAPE    DS    F                       .  JOB TAPES
REGIONJ  DS    F                       .  JOB REGION
MAXTIME  DS    F                       .  MAX CPU TIME REQUESTED
TIMEPTR  DS    F                       .  FLDS USED
TIMELEN  DS    F                       .    WHEN SQUEEZING OUT
OTHPTR   DS    F                       .       THE TIME
OTHLEN   DS    F                       .          KEYWORD
RC       DS    H                       .  RETURN CODE
USERLEN  DS    H                       .  USERID LENGTH
WORKINST DS    CL6                     .  AREA FOR DYNAMIC INSTRUCTION
USER     DS    CL08                    .
JOB      DS    CL08                    .
DSN      DS    CL44                    .
DD       DS    CL08                    .
UNITSW   DS    CL01                    .  UNIT SWITCH SETTINGS
TAPEUNIT EQU   B'10000000'             .
UNITXIST EQU   B'01000000'             .
UNITAFF  EQU   B'00100000'             .
VOLSPEC  EQU   B'00010000'             .
VOLREF   EQU   B'00001000'             .
VOLTAPE  EQU   B'00000100'             .
DISPNEW  EQU   B'00000010'             .
U08      EQU   B'00000001'             .
CLASSPTR DS    F                       .  POINTER TO CLASS DEF
CLASS    DS    C                       .  THE CLASS USER SAID
FLAGS    DS    C                       .  SOME FLAGS
MGOT     EQU   B'10000000'             .  MEMORY GOTTEN FLAG
LASTCALL EQU   B'01000000'             .  LAST JES2 INVOCATION FLAG
JOBU     EQU   B'00100000'             .  PROCESSING JOB CARD
PROCU    EQU   B'00010000'             .             PROC
DDU      EQU   B'00001000'             .             DD
EXECU    EQU   B'00000100'             .             EXEC
UNUSED6  EQU   B'00000010'             .
UNUSED7  EQU   B'00000001'             .
FLAGS1   DS    C                       .      JOB FLAGS
REALMEM  EQU   B'10000000'             .  JOB ADRSP=REAL
F102     EQU   B'01000000'             .
JOBRGN   EQU   B'00100000'             .  JOB REGION=
JOBPRTY  EQU   B'00010000'             .  JOB PRTY=
JOBHOLD  EQU   B'00001000'             .  JOB TYPRUN=HOLD
TESTBTCH EQU   B'00000100'             .  JOB IS TESTBATCH
ERROR    EQU   B'00000010'             .  JOB SHOULD BE FAILED
JOBCARD  EQU   B'00000001'             .  JOB CARD ENCOUNTERED
FLAGS2   DS    C                       .      EXEC FLAGS
EPERF    EQU   B'10000000'             .
EADRS    EQU   B'01000000'             .
EDPRT    EQU   B'00100000'             .
ETIME    EQU   B'00010000'             .
SPUSER   EQU   B'00001000'             .  SPECIAL USER DP,DS,RDPP
JCARD    EQU   B'00000100'             .
STAPEE   EQU   B'00000010'             .
JTAPEE   EQU   B'00000001'             .
FLAGS3   DS    C                       .  MORE FLAGS
TIMEJJ   EQU   B'10000000'             .
TIMEDEF  EQU   B'01000000'             .
DSNS2    EQU   B'00100000'             .
PERFJ    EQU   B'00010000'             .
E305     EQU   B'00001000'             .
E306     EQU   B'00000100'             .
E307     EQU   B'00000010'             .
E308     EQU   B'00000001'             .
         DS    0D
CAMRET   DS    CL280                   .
         ORG   CAMRET                  .
CAMCNT   DS    H                       .
CAMDEVB1 DS    CL1                     .
CAMDEVB2 DS    CL1                     .
CAMDEVB3 DS    CL1                     .
CAMDEVB4 DS    CL1                     .
CAMSER   DS    CL6                     .
CAMSEQ   DS    CL2                     .
CAMELEN  EQU   *-CAMDEVB1              .
         ORG   CAMRET+20               .
MSG      DS    CL255                   .
         ORG   MSG+20                  .
MSGX     DS    CL255                   .
         ORG   ,                       .
DF1      DS    CL3                     .
DMIN     DS    CL4                     .
DF2      DS    C                       .
DSEC     DS    CL2                     .
DF3      DS    C                       .
DLEN     EQU   *-DF1                   .
         ORG   DF1                     .
DF       DS    CL(DLEN)                .
         ORG   ,                       .
         DS    0F                      .
CAMLOC   CAMLST NAME,DSN,,CAMRET       . LOCATE FUNCTIONS
MYRPL    RPL   ACB=JPCEMSG,AM=VSAM     .
MYRPLL   EQU   *-MYRPL                 .
WORKLEN  EQU   *-SAVE                  .
         EJECT                         .
JUMPTAB  DSECT                         .
JUMP     DS    256F                    .
USERTEXT DSECT                         .
USERKEY  DS    C                       .
USER#    DS    C                       .
USERL1   DS    C                       .
USERTXT  DS    CL255                   .
JESX006  CSECT                         .
         $MODEND                       .
         END
//LKED      EXEC PGM=IEWL,REGION=256K,COND=(0,LT,ASM1),
//             PARM=('LIST,LET,MAP,RENT')
//SYSLIB      DD DSN=SYS1.LINKLIB,DISP=SHR
//SYSLIN      DD DSN=&&A,DISP=(OLD,DELETE)
//            DD DDNAME=SYSIN          .
//SYSLMOD     DD DISP=SHR,DSN=SYS2.LINKLIB
//SYSPRINT    DD SYSOUT=*,DCB=(BLKSIZE=121,BUFNO=1)
//SYSUT1      DD UNIT=SYSDA,SPACE=(CYL,(5,5))
//SYSIN       DD *
  NAME JESX006N(R)
/*
//
