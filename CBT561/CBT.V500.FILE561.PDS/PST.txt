          ISREDIT MACRO (LEVEL REST) NOPROCESS

          IF &NRSTR(&REST) ¬= THEN +
             DO
             SET ZEDSMSG = &NRSTR(INVALID OPERAND)
             SET ZEDLMSG = &NRSTR(ONLY A FILE NAME OF UP TO 5 +
             ALPHANUMERIC BYTES IS VALID)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

          IF &NRSTR(&LEVEL) = THEN +
             DO
             SET LEVEL = LIST
             END

          IF &NRSTR(&LEVEL) = LIST THEN +
             DO
             GOTO PASTLIST
             END

          IF &NRSTR(&LEVEL) = HELP THEN +
             DO
             ISPEXEC CONTROL DISPLAY SAVE
             ISPEXEC SELECT PGM(ISPTUTOR) PARM(#PASTE)
             ISPEXEC CONTROL DISPLAY RESTORE
             EXIT CODE(1)
             END

          IF &DATATYPE(&NRSTR(&LEVEL)) = NUM THEN +
                 SET LEVEL = &LEVEL
          IF &LENGTH(&NRSTR(&LEVEL)) > 5 THEN +
             DO
             SET ZEDSMSG = &NRSTR(INVALID LEVEL)
             SET ZEDLMSG = &NRSTR(ONLY A FILE NAME OF LESS THAN 6 +
             BYTES IS VALID)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

          CONTROL NOFLUSH NOMSG
          ISREDIT PROCESS DEST
          SET LCC = &LASTCC
          CONTROL FLUSH MSG
          IF &LCC > 0 THEN +
             DO
             SET ZEDSMSG = &NRSTR(PASTE CMD PENDING)
             SET ZEDLMSG = &NRSTR(USING "A" OR "B" LINE COMMANDS +
             YOU MUST SPECIFY THE DESTINATION OF THE PASTE)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

          ISREDIT (LINE) = LINENUM .ZDEST

          SET TABLE = &NRSTR(CUT&LEVEL)

          ISPEXEC TBSTATS &TABLE STATUS1(ST1) LIBRARY(ISPPROF)
          SET LCC = &LASTCC
          IF &ST1 ¬= 1 THEN +
             DO
             SET ZEDSMSG = &NRSTR(DATA NOT CUT)
             SET ZEDLMSG = &NRSTR(THE REQUESTED FILE (&LEVEL) WAS +
             NOT CUT)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

          ISPEXEC TBOPEN &TABLE LIBRARY(ISPPROF)
          ISPEXEC TBTOP &TABLE

          SET CNT = 0
          SET LCC = 0
          DO WHILE &LCC = 0

             ISPEXEC TBSKIP &TABLE
             SET LCC = &LASTCC

             IF &LCC = 0 THEN DO
                 ISREDIT LINE_AFTER &LINE = DATALINE (RECORD)
                 SET LINE = &LINE + 1
                 SET CNT = &CNT + 1
                 END

             END

          ISPEXEC TBEND &TABLE
          SET ZEDSMSG = &NRSTR(&CNT LINES PASTED)
          SET ZEDLMSG = &NRSTR(&CNT LINES PASTED FROM FILE &LEVEL)
          ISPEXEC SETMSG MSG(ISRZ000)
          EXIT CODE(1)
PASTLIST: +
          ISPEXEC TBCREATE CUTLIST NAMES(LEVEL RECCNT CREATED +
                  RECORD) NOWRITE

          ISPEXEC TBVCLEAR CUTLIST
          ISPEXEC LMINIT DATAID(ID) DDNAME(ISPPROF)
          ISPEXEC LMOPEN DATAID(&ID)
          ISPEXEC LMMLIST DATAID(&ID) MEMBER(MEM)
          IF &LASTCC = 0 THEN SET PREF = &SUBSTR(1:3,&MEM         )
          ELSE SET PREF = ZZZ
          SET LEVEL = &SUBSTR(4:8,&MEM        )

          DO WHILE &PREF < CUT
              ISPEXEC LMMLIST DATAID(&ID) MEMBER(MEM)
              IF &LASTCC = 0 THEN SET PREF = &SUBSTR(1:3,&MEM         )
              ELSE SET PREF = ZZZ
              SET LEVEL = &SUBSTR(4:8,&MEM        )
              END

          DO WHILE &PREF = CUT
             ISPEXEC TBSTATS &MEM      +
                     CDATE(CREATED)    +
                     ROWCURR(RECCNT)   +
                     LIBRARY(ISPPROF)
             SET RECCNT = &RECCNT
             ISPEXEC TBOPEN &MEM LIBRARY(ISPPROF) NOWRITE
             ISPEXEC TBSKIP &MEM
             ISPEXEC TBEND  &MEM
             ISPEXEC TBADD CUTLIST

             ISPEXEC LMMLIST DATAID(&ID) MEMBER(MEM)
             IF &LASTCC = 0 THEN SET PREF = &SUBSTR(1:3,&MEM     )
             ELSE SET PREF = ZZZ
             SET LEVEL = &SUBSTR(4:8,&MEM        )
             END

          ISPEXEC LMMLIST DATAID(&ID) OPTION(FREE)
          ISPEXEC LMCLOSE DATAID(&ID)
          ISPEXEC LMFREE DATAID(&ID)

          ISPEXEC TBTOP CUTLIST
          SET PANEL = #PASD
CUTLOOP:  +
          ISPEXEC TBTOP CUTLIST
          ISPEXEC TBSKIP CUTLIST NUMBER(&ZTDTOP)
          ISPEXEC TBDISPL CUTLIST PANEL(&PANEL)
          SET PCC = &LASTCC
          SET PANEL = #PASD
          IF &PCC > 4 THEN DO
             ISPEXEC TBEND CUTLIST
             EXIT CODE(1)
             END
IF &LSEL  = B THEN DO
          ISPEXEC CONTROL DISPLAY SAVE
          SET BRTITLE = &STR(FILE FOLDER "&LEVEL")
          ALLOC F(CUTTEXT) REUSE NEW UNIT(SYSDA) +
                LRECL(80) BLKSIZE(6160) RECFM(F B) +
                DSORG(PS) SP(5 5) TR
          ISPEXEC LMINIT DATAID(ID) DDNAME(CUTTEXT)
          ISPEXEC LMOPEN DATAID(&ID) OPTION(OUTPUT)
          SET TABLE = &STR(CUT&LEVEL)
          ISPEXEC TBOPEN &TABLE NOWRITE LIBRARY(ISPPROF)
          ISPEXEC TBTOP &TABLE
          DO WHILE &RECCNT > 0
              ISPEXEC TBSKIP &TABLE
              ISPEXEC LMPUT DATAID(&ID) MODE(INVAR) +
                  DATALOC(RECORD) DATALEN(80)
              SET RECCNT = &RECCNT - 1
              END
              ISPEXEC TBEND &TABLE
              ISPEXEC LMCLOSE DATAID(&ID)
              ISPEXEC BROWSE DATAID(&ID) PANEL(CNADBROW)
              ISPEXEC LMFREE DATAID(&ID)
              FREE F(CUTTEXT)
              ISPEXEC CONTROL DISPLAY RESTORE
              IF &PCC = 4 THEN SET PANEL =
              GOTO CUTLOOP
          END
IF &LSEL  = S THEN DO
          ISPEXEC CONTROL ERRORS RETURN
          ISREDIT PASTE &LEVEL
          SET LCC = &LASTCC
          ISPEXEC CONTROL ERRORS CANCEL
          ISPEXEC TBEND CUTLIST
          IF &LCC ¬= 0 THEN EXIT CODE(&LCC)
          EXIT CODE(0)
          END
          IF &PCC = 4 THEN SET PANEL =
          GOTO CUTLOOP
