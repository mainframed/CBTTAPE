          ISREDIT MACRO (LEVEL REST) NOPROCESS
/******************************************************************
/*     DETERMINE IF WE ARE RUNNING IN DEBUG MODE                  *
/*     AND SET CONTROL ACCORDINGLY                                *
/******************************************************************
         CONTROL MSG FLUSH PROMPT
         ISPEXEC  CONTROL  ERRORS CANCEL
         ISPEXEC VGET (SPFDBUG) PROFILE
         IF &LASTCC ¬= 0 THEN SET &SPFDBUG = N
         IF &SPFDBUG = Y THEN CONTROL LIST MSG SYMLIST CONLIST
/******************************************************************
/*               MAIN CLIST LOGIC                                 *
/******************************************************************

          IF &NRSTR(&REST) ¬= THEN +
             DO
             SET ZEDSMSG = &NRSTR(INVALID OPERAND)
             SET ZEDLMSG = &NRSTR(ONLY A FILE NAME OF 5 BYTES OR +
             LESS IS VALID)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

          IF &NRSTR(&LEVEL) = THEN +
             DO
             SET LEVEL = A
             END

          IF &NRSTR(&LEVEL) = HELP THEN +
             DO
             ISPEXEC CONTROL DISPLAY SAVE
             ISPEXEC SELECT PGM(ISPTUTOR) PARM(#CUT)
             ISPEXEC CONTROL DISPLAY RESTORE
             EXIT CODE(0)
             END

          IF &NRSTR(&LEVEL) = LIST THEN +
             DO
             GOTO CUTLIST
             END

          IF &LENGTH(&NRSTR(&LEVEL)) > 5 THEN +
             DO
             SET ZEDSMSG = &NRSTR(INVALID LEVEL)
             SET ZEDLMSG = &NRSTR(ONLY A FILE NAME OF 5 BYTES OR +
             LESS IS VALID)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

          IF &DATATYPE(&NRSTR(&LEVEL)) = NUM THEN +
              SET LEVEL = &LEVEL

          CONTROL NOFLUSH NOMSG
          ISREDIT PROCESS RANGE C M
          SET LCC = &LASTCC
          CONTROL FLUSH MSG
          ISREDIT (CMD) = RANGE_CMD
          IF &LCC > 0 THEN +
             DO
             SET ZEDSMSG = &NRSTR(RANGE NOT SPECIFIED)
             SET ZEDLMSG = &NRSTR(USING "M" OR "C" LINE COMMANDS +
             YOU MUST SPECIFY THE LINES TO CUT)
             ISPEXEC SETMSG MSG(ISRZ001)
             EXIT CODE(12)
             END

          ISREDIT (FLINE) = LINENUM .ZFRANGE
          ISREDIT (LLINE) = LINENUM .ZLRANGE

          SET TABLE = &NRSTR(CUT&LEVEL)

          ISPEXEC TBCREATE &TABLE NAMES(RECORD) WRITE REPLACE +
                  LIBRARY(ISPPROF)
          SET LCC = &LASTCC

          SET CNT = 0
          DO WHILE &FLINE ¬> &LLINE

             ISREDIT (RECORD) = LINE &FLINE
             ISPEXEC TBADD &TABLE
             SET CNT = &CNT + 1
             IF &CMD = M THEN DO
                 ISREDIT DELETE &FLINE &FLINE
                 SET LLINE = &LLINE - 1
                 END
             ELSE +
                 SET FLINE = &FLINE + 1

             END

          ISPEXEC TBCLOSE &TABLE PAD(10) LIBRARY(ISPPROF)
          SET ZEDSMSG = &NRSTR(&CNT LINES CUT)
          SET ZEDLMSG = &NRSTR(&CNT LINES CUT AND SAVED INTO FILE +
          &LEVEL)
          ISPEXEC SETMSG MSG(ISRZ000)
          EXIT CODE(1)
CUTLIST:  +
          ISPEXEC TBCREATE CUTLIST NAMES(LEVEL RECCNT CREATED +
                  RECORD) NOWRITE

          ISPEXEC TBVCLEAR CUTLIST
          ISPEXEC LMINIT DATAID(ID) DDNAME(ISPPROF)
          ISPEXEC LMOPEN DATAID(&ID)
          ISPEXEC LMMLIST DATAID(&ID) MEMBER(MEM)
          IF &LASTCC = 0 THEN SET PREF = &SUBSTR(1:3,&MEM         )
          ELSE SET PREF = ZZZ
          SET LEVEL = &SUBSTR(4:8,&MEM        )

          DO WHILE &PREF < CUT
              ISPEXEC LMMLIST DATAID(&ID) MEMBER(MEM)
              IF &LASTCC = 0 THEN SET PREF = &SUBSTR(1:3,&MEM         )
              ELSE SET PREF = ZZZ
              SET LEVEL = &SUBSTR(4:8,&MEM        )
              END

          DO WHILE &PREF = CUT
             ISPEXEC TBSTATS &MEM      +
                     CDATE(CREATED)    +
                     ROWCURR(RECCNT)   +
                     LIBRARY(ISPPROF)
             SET RECCNT = &RECCNT
             ISPEXEC TBOPEN &MEM LIBRARY(ISPPROF) NOWRITE
             ISPEXEC TBSKIP &MEM
             ISPEXEC TBEND  &MEM
             ISPEXEC TBADD CUTLIST

             ISPEXEC LMMLIST DATAID(&ID) MEMBER(MEM)
             IF &LASTCC = 0 THEN SET PREF = &SUBSTR(1:3,&MEM     )
             ELSE SET PREF = ZZZ
             SET LEVEL = &SUBSTR(4:8,&MEM        )
             END

          ISPEXEC LMMLIST DATAID(&ID) OPTION(FREE)
          ISPEXEC LMCLOSE DATAID(&ID)
          ISPEXEC LMFREE DATAID(&ID)

          ISPEXEC TBTOP CUTLIST
          SET PANEL = #CUTD
CUTLOOP:  +
          ISPEXEC TBTOP CUTLIST
          ISPEXEC TBSKIP CUTLIST NUMBER(&ZTDTOP)
          ISPEXEC TBDISPL CUTLIST PANEL(&PANEL)
          SET PCC = &LASTCC
          SET PANEL = #CUTD
          IF &PCC > 4 THEN DO
             ISPEXEC TBEND CUTLIST
             EXIT CODE(1)
             END
IF &LSEL  = S  OR &LSEL = B THEN DO
          ISPEXEC CONTROL DISPLAY SAVE
          SET BRTITLE = &STR(FILE FOLDER "&LEVEL")
          ALLOC F(CUTTEXT) REUSE NEW UNIT(SYSDA) +
                LRECL(80) BLKSIZE(6160) RECFM(F B) +
                DSORG(PS) SP(5 5) TR
          ISPEXEC LMINIT DATAID(ID) DDNAME(CUTTEXT)
          ISPEXEC LMOPEN DATAID(&ID) OPTION(OUTPUT)
          SET TABLE = &STR(CUT&LEVEL)
          ISPEXEC TBOPEN &TABLE NOWRITE LIBRARY(ISPPROF)
          ISPEXEC TBTOP &TABLE
          DO WHILE &RECCNT > 0
              ISPEXEC TBSKIP &TABLE
              ISPEXEC LMPUT DATAID(&ID) MODE(INVAR) +
                  DATALOC(RECORD) DATALEN(80)
              SET RECCNT = &RECCNT - 1
              END
              ISPEXEC TBEND &TABLE
              ISPEXEC LMCLOSE DATAID(&ID)
              ISPEXEC BROWSE DATAID(&ID) PANEL(CNADBROW)
              ISPEXEC LMFREE DATAID(&ID)
              FREE F(CUTTEXT)
              IF &PCC = 4 THEN SET PANEL =
              ISPEXEC CONTROL DISPLAY RESTORE
              GOTO CUTLOOP
          END
IF &LSEL  = D THEN DO
          ISPEXEC CONTROL DISPLAY SAVE
          ISPEXEC TBERASE CUT&LEVEL LIBRARY(ISPPROF)
          ISPEXEC TBDELETE CUTLIST
          IF &PCC = 4 THEN SET PANEL =
          ISPEXEC CONTROL DISPLAY RESTORE
          GOTO CUTLOOP
          END
          IF &PCC = 4 THEN SET PANEL =
          GOTO CUTLOOP
