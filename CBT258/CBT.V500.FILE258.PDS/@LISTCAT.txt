PROC 0 U()
/*==================================================================*/
/* Program:   WIN3270A                                              */
/* Support:   DAF                                                   */
/* Language:  CLIST                                                 */
/*                                                                  */
/* Purpose:   A test clist for the WIN3270 sub command environment. */
/*            Displays a full screen scrollable catalog list.       */
/*            See WIN3270T clist for large amounts of comments.     */
/*                                                                  */
/* History:   18Oct95  Initial coding                           DAF */
/* End.                                                             */
/*==================================================================*/

CONTROL NOCAPS
IF &U = THEN SET U = &SYSUID

/* capture output from some tso command */
SET &SYSOUTTRAP = 999     /* TURN ON AND CAPTURE 999 LINES OF OUTPUT */
LISTC L(&U)
SET &LINES = &SYSOUTLINE   /* GET NUMBER OF LINES CREATED BY COMMAND */
SET &COUNT = 1
SET DSC = 1
DO WHILE(&COUNT <= &LINES)
  SET QQ = &&SYSOUTLINE&COUNT
  IF &SUBSTR(6:7,&QQ) ¬= IN THEN +
    DO
      SET PQ = &SUBSTR(17:&SYSINDEX(!,&QQ!)-1,&QQ    )
      SET TABLE&DSC = &PQ
      LISTDSI '&PQ' NORECALL
      IF &LASTCC = 0 THEN +
        SET TABDSA&DSC = &STR(&SYSVOLUME &SYSDSORG &SYSALLOC)
      ELSE +
        SET TABDSA&DSC = &STR(?)
      SET DSC = &DSC + 1
    END
  SET COUNT = &COUNT + 1
END
SET &SYSOUTTRAP = 0   /* turn off output line capture */

CALL 'SASDDF.A.LOADLIB(WIN3270)'
WA value=\ type=label color=white hilite=none
WA value=@ type=label color=blue hilite=uline
WA value={ type=label color=yellow hilite=none
WA value=% type=output color=red hilite=none
WA value=! type=output color=blue hilite=none
WA value=_ type=output color=cyan hilite=none
WA value=$ type=input color=green hilite=none
WC name=main

WS 01 \List catatalog of: !U                                        +
{User:!SYSUID
WS 02 \Command ===>$zcmd                                            +
{Time:!XTIME
WS 03 %msg
WS 04 @Data set name                             Volume Org. Alloc.
WS 05 _A1                                       !DSI1
WS 06 _A2                                       !DSI2
WS 07 _A3                                       !DSI3
WS 08 _A4                                       !DSI4
WS 09 _A5                                       !DSI5
WS 10 _A6                                       !DSI6
WS 11 _A7                                       !DSI7
WS 12 _A8                                       !DSI8
WS 13 _A9                                       !DSI9
WS 14 _A10                                      !DSI10
WS 15 _A11                                      !DSI11
WS 16 _A12                                      !DSI12
WS 17 _A13                                      !DSI13
WS 18 _A14                                      !DSI14
WS 19 _A15                                      !DSI15
WS 20 _A16                                      !DSI16
WS 21 _A17                                      !DSI17
WS 22 _A18                                      !DSI18
WS 23 _A19                                      !DSI19
WS 24 _A20                                      !DSI20

SET &TOPROW = 1
SET ZCMD = &STR()
SET MSG = &STR(&dsc datasets displayed.)
DO WHILE(&SYSCAPS(&STR(&ZCMD))¬=&STR(END))
  /* subcommand to display the defined screen */
  SET XTIME = &SYSTIME
  SET ZCMD = &STR( )
  /* display the captured output */

  SET I = 1
  DO WHILE(&I <= 20)
    SET TABP = &EVAL(&TOPROW+&I-1)
    IF &TABP <= &DSC THEN +
      DO
        SET QQ = &&TABLE&TABP
        SET A&I = &STR(&QQ)
        SET QQ = &&TABDSA&TABP
        SET DSI&I = &STR(&QQ)
      END
    ELSE +
      DO
        SET A&I = &STR( )
        SET DSI&I = &STR( )
      END
    SET I = &I + 1
  END

  WD name=main row=1 col=1
  IF &WINSYSK = PF3 THEN +
    SET ZCMD = &STR(END)                   /* end */
  IF &WINSYSK = PF15 THEN +
    SET ZCMD = &STR(END)
  IF &WINSYSK = PF19 THEN +
    DO
      SET TOPROW = &EVAL(&TOPROW - 10)      /* up */
      IF &TOPROW < 1 THEN +
        DO
          SET TOPROW = 1
          SET MSG = &STR(At top.)
        END
    END
  IF &WINSYSK = PF20 THEN +
    DO
      SET LSTTOP = &TOPROW                  /* down */
      SET TOPROW = &EVAL(&TOPROW + 10)
      IF &TOPROW > &DSC THEN +
        DO
          SET TOPROW = &LSTTOP
          SET MSG = &STR(At bottom.)
        END
    END
END

  /* subcommand to erase the screen */
FIN:  +
  WE name=main
  /* subcommand end WIN3270 */
  WX

EXIT
