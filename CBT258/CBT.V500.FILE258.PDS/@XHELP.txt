PROC 1 TOPIC
/*==================================================================*/
/* Program:   WIN3270A                                              */
/* Support:   DAF                                                   */
/* Language:  CLIST                                                 */
/*                                                                  */
/* Purpose:   A test clist for the WIN3270 sub command environment. */
/*            Displays a full screen scrollable help.               */
/*            See WIN3270T clist for large amounts of comments.     */
/*                                                                  */
/* History:   18Oct95  Initial coding                           DAF */
/* End.                                                             */
/*==================================================================*/

CONTROL NOCAPS   /* CONLIST SYMLIST */

/* capture output from some tso command */
IF &STR(&TOPIC) = ALL THEN +
  DO
    SET TOPIC = &STR()
    SET LEVEL = 1
    SET DEPTH&LEVEL = &STR(&TOPIC)
  END
ELSE +
  DO
    IF &SYSDSN('SYS1.HELP(&TOPIC)') = &STR(OK) |        +
       &SYSDSN('TSO.MVS.HELP(&TOPIC)') = &STR(OK) THEN +
      DO
        SET LEVEL = 1
        SET DEPTH&LEVEL = &STR(&TOPIC)
      END
    ELSE +
      DO
        WRITE No help exists for &TOPIC
        EXIT CODE(99)
      END
  END

AGAIN: +
SET &SYSOUTTRAP = 999     /* TURN ON AND CAPTURE 999 LINES OF OUTPUT */
HELP &TOPIC
SET &LINES = &SYSOUTLINE   /* GET NUMBER OF LINES CREATED BY COMMAND */
SET &COUNT = 1
SET DSC = 1
DO WHILE(&DSC <= &LINES)
  SET QQ = &&SYSOUTLINE&DSC
  SET TABLE&DSC = &STR(&QQ)
  SET DSC = &DSC + 1
END
SET &SYSOUTTRAP = 0   /* turn off output line capture */

CALL 'SASDDF.A.LOADLIB(WIN3270)'
WA value=\ type=label color=white hilite=none
WA value=@ type=label color=blue hilite=uline
WA value={ type=label color=yellow hilite=none
WA value=% type=output color=red hilite=none
WA value=! type=output color=blue hilite=none
WA value=_ type=output color=cyan hilite=none
WA value=$ type=input color=green hilite=none
WC name=main

WS 01 \Help information for!DESCR                                   +
{User:!SYSUID
WS 02 \Command ===>$zcmd                                            +
{Time:!XTIME
WS 03 %msg
WS 04 @
WS 05 _A1
WS 06 _A2
WS 07 _A3
WS 08 _A4
WS 09 _A5
WS 10 _A6
WS 11 _A7
WS 12 _A8
WS 13 _A9
WS 14 _A10
WS 15 _A11
WS 16 _A12
WS 17 _A13
WS 18 _A14
WS 19 _A15
WS 20 _A16
WS 21 _A17
WS 22 _A18
WS 23 _A19
WS 24 _A20
SET ROWMAX = 20
SET SCRAMT = 10
IF &WINSYSNR > 24 THEN +
  DO
    WS 25 _A21
    WS 26 _A22
    WS 27 _A23
    SET ROWMAX = 23
    SET SCRAMT = 12
    IF &WINSYSNR > 27 THEN +
      DO
        WS 28 _A24
        WS 29 _A25
        WS 30 _A26
        WS 31 _A27
        WS 32 _A28
        SET ROWMAX = 28
        SET SCRAMT = 14
        IF &WINSYSNR > 32 THEN +
          DO
            WS 33 _A29
            WS 34 _A30
            WS 35 _A31
            WS 36 _A32
            WS 37 _A33
            WS 38 _A34
            WS 39 _A35
            WS 40 _A36
            WS 41 _A37
            WS 42 _A38
            WS 43 _A39
            SET ROWMAX = 39
            SET SCRAMT = 18
          END
      END
  END

SET &TOPROW = 1
SET ZCMD = &STR( )
SET MSG = &STR(Help entry contains &DSC lines.)
IF &TOPIC = THEN +
  SET DESCR = &STR(all TSO commands)
ELSE SET DESCR = &STR(&TOPIC)

DO WHILE(&SYSCAPS(&STR(&ZCMD))¬=&STR(END))
  /* subcommand to display the defined screen */
  SET XTIME = &SYSTIME
  SET ZCMD = &STR( )

  /* fill display variables and display the captured output */
  SET I = 1
  DO WHILE(&I <= &ROWMAX)
    SET TABP = &EVAL(&TOPROW+&I-1)
    IF &TABP <= &DSC THEN +
      DO
        SET QQ = &&TABLE&TABP
        SET A&I = &STR(&QQ)
      END
    ELSE +
        SET A&I = &STR( )
    SET I = &I + 1
  END
  WD name=main row=1 col=1

  /* process commands and function keys */
  IF &WINSYSK = PF1 THEN                   /* drill down help */ +
    DO
      IF &STR(&WINSYST) ¬=  THEN +
        DO
          IF &SYSDSN('SYS1.HELP(&SYSCAPS(&WINSYST))') = &STR(OK) |  +
             &SYSDSN('TSO.MVS.HELP(&TOPIC)') = &STR(OK) THEN        +
            DO
              SET DEPTH&LEVEL = &STR(&TOPIC)
              SET LEVEL = &EVAL(&LEVEL+1)
              SET TOPIC = &WINSYST
              WE name=main
              WX
              GOTO AGAIN
            END
          ELSE +
            SET MSG = &STR(No help for &WINSYST exists.)
        END
    END

  IF &WINSYSK = PF3 THEN                   /* go back or exit */ +
    DO
      IF &LEVEL > 1 THEN +
        DO
          SET LEVEL = &EVAL(&LEVEL-1)
          SET TOPIC = &&DEPTH&LEVEL
          WE name=main
          WX
          GOTO AGAIN
        END
      ELSE +
        SET ZCMD = &STR(END)               /* exit */
    END
  IF &WINSYSK = PF15 THEN                  /* go back or exit */  +
    DO
      IF &LEVEL > 1 THEN +
        DO
          SET LEVEL = &EVAL(&LEVEL-1)
          SET TOPIC = &&DEPTH&LEVEL
          WE name=main
          WX
          GOTO AGAIN
        END
      ELSE +
        SET ZCMD = &STR(END)               /* exit */
    END

  IF &WINSYSK = PF4 THEN +
    SET ZCMD = &STR(END)                   /* return = exit */
  IF &WINSYSK = PF16 THEN +
    SET ZCMD = &STR(END)

  IF &WINSYSK = PF19 THEN +
    DO
      SET MSG = &STR( )
      SET TOPROW = &EVAL(&TOPROW - &SCRAMT)      /* up */
      IF &TOPROW < 1 THEN +
        DO
          SET TOPROW = 1
          SET MSG = &STR(At top.)
        END
    END

  IF &WINSYSK = PF20 THEN +
    DO
      SET MSG = &STR( )
      SET LSTTOP = &TOPROW                  /* down */
      SET TOPROW = &EVAL(&TOPROW + &SCRAMT)
      IF &TOPROW > &DSC THEN +
        DO
          SET TOPROW = &LSTTOP
          SET MSG = &STR(At bottom.)
        END
    END
END

  /* subcommand to erase the screen */
FIN:  +
  WE name=main
  /* subcommand end WIN3270 */
  WX

EXIT
