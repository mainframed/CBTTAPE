PROC 0
/*==================================================================*/
/* Program:   WIN3270T                                              */
/* Support:   DAF                                                   */
/* Language:  CLIST                                                 */
/* Purpose:   A test clist for the WIN3270 sub command environment. */
/* History:   25Aug95  Initial coding                           DAF */
/*            25Aug95  Added message level parm                 DAF */
/*            27Oct95  Added box to display                     DAF */
/* End.                                                             */
/*==================================================================*/

CONTROL NOCAPS
CALL 'SASDDF.A.LOADLIB(WIN3270)'
WA value=~ type=label color=blue hilite=reverse
WA value=\ type=label color=white hilite=none
WA value={ type=label color=yellow hilite=none
WA value=% type=output color=red hilite=none
WA value=! type=output color=blue hilite=none
WA value=$ type=input color=green hilite=none
WA value=_ type=button color=cyan just=center
WA value=@ type=radio color=cyan just=left
WA value=# type=checkbox color=cyan just=left
/* subcommand to create the screen, defines the current window */
WC name=main
/* define all the variables in the window(s) */
SET ZCMD = &STR()
SET MSG = &STR(Initial message)
SET OT = &STR(Red)
SET BLUE = &STR()
SET GREEN = &STR()
SET FRIED = &STR()
SET POACHED = &STR()
SET OVER = &STR()
SET GRILLED = &STR()
SET OK = &STR()
SET CANCEL = &STR()
SET HELP = &STR()
/* subcommands define the format of the screen */

WS 01 ~123456789012345678901234567890123456789012345678901234567890123\
WS 02 ~ \Miliways Menu                               {User:!SYSUID  ~ \
WS 03 ~ \Command ===>$zcmd                           {Time:!XTIME   ~ \
WS 04 ~ %msg                                                        ~ \
WS 05 ~ \Todays menu is from Mars a small red planet that circles   ~ \
WS 06 ~ \the dull yellow sun called Sol.                            ~ \
WS 07 ~ \                                                           ~ \
WS 08 ~ \   Pick one of these                                       ~ \
WS 09 ~ \        @OT Blue rats with a red dog sauce.                ~ \
WS 10 ~ \        @OT Red fish in a white wino sauce.                ~ \
WS 11 ~ \        @OT White cats stuffed with marsh mellow.          ~ \
WS 12 ~ \                                                           ~ \
WS 13 ~ \   Pick many of these strange things                       ~ \
WS 14 ~ \        #Blue birds.              #Green eggs and ham.12345~ \
WS 15 ~ \        #Fried green potatoes.    #Poached turkey.         ~ \
WS 16 ~ \        #Over fried beans.        #Grilled pumpkin.        ~ \
WS 17 ~ \                                                           ~ \
WS 18 ~ \             _Order  \      _Exit  \     _Help  \          ~ \
WS 19 ~ \                                                           ~ \
WS 20 ~---------1---------2---------3---------4---------5---------6---\
/* subcommand to erase the screen */
WE name=help
/* subcommand to create the screen */
WC name=help
/* subcommands define the format of the screen */
WS 01 ~                                                               \
WS 02 ~ \Miliways Help                                              ~ \
WS 03 ~ \Command ===>$zcmd                                          ~ \
WS 04 ~ %msg                                                        ~ \
WS 05 ~ \                                                           ~ \
WS 06 ~ \      Sorry mate there is no help for the menu. You        ~ \
WS 07 ~ \      just have to take pot luck with this stuff.          ~ \
WS 08 ~ \      There is no free lunch to be found here.             ~ \
WS 09 ~ \                                                           ~ \
WS 10 ~ \                                                           ~ \
WS 11 ~ \                       _Goback  \                          ~ \
WS 12 ~ \                                                           ~ \
WS 13 ~                                                               \

  DO WHILE(&SYSCAPS(&STR(&ZCMD))¬=&STR(END))
    /* subcommand to display the defined screen */
    SET XTIME = &SYSTIME
    SET ZCMD = &STR( )
    WD name=main row=1 col=1

    /* process the selection */
    IF &exit = X THEN GOTO FIN
    ELSE IF &HELP = X THEN +
      DO
        SET ZCMD = &STR()
        WD name=help row=1 col=1
        SET WINSYSK = &STR()
        SET OK = &STR()
        SET ZCMD = &STR()
      END
    ELSE IF &ORDER = X THEN +
      DO
        IF &OT = Red THEN +
          SET MSG = &STR(You ordered: Red fish in a white wino sauce.)
        ELSE IF &OT = Blue THEN +
          SET MSG = &STR(You ordered: Blue rats with a red dog sauce.)
        ELSE IF &OT = White THEN +
          SET MSG = &STR(You ordered: White cats stuffed with marsh ...)
      /* ... and other truely strange tastes ! */
      END

    IF &STR(&ZCMD) = HELP THEN +
      DO
        SET ZCMD = &STR()
        WD name=help row=1 col=1 delay=8
        SET WINSYSK = &STR()
        SET ZCMD = &STR()
        SET OK = &STR() /* turn button off to indicate it is processed*/
      END
    ELSE IF &STR(&ZCMD) ¬= &STR( ) THEN +
      DO
        &ZCMD
      END

    IF &STR(&WINSYSK) = PF3 THEN +
      SET ZCMD = &STR(END)
    IF &STR(&WINSYSK) = PF15 THEN +
      SET ZCMD = &STR(END)
  END

  /* subcommand to erase the screen */
FIN:  +
  WE name=help
  WE name=main
  /* subcommand end WIN3270 */
  WX

EXIT
