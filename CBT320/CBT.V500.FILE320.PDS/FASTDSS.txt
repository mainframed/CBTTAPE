//JGSTDSS   JOB  (9542,Q301),'JOHN GRADY SULLIVAN',CLASS=Q,MSGCLASS=T
/*JOBPARM   LINES=15,TIME=10
//*JOBPARM   SYSAFF=(JGPT)
//*ROUTE     XEQ   JGS2
//* JOBCAT    DD  DISP=SHR,DSN=CATALOG.VSYSGEN
//*    *****  SYSJS.PDS.ASM(DSSEXITS)   *****
//S1  EXEC  ASMHCL,                OR ASMFCL = IFOX00 (XF) ASSEMBLER
//       PARM.ASM='NODECK,OBJECT,SYSPARM(JGS)',
//       REGION.ASM=500K,
//       COND.LKED=(04,LT),
//       PARM.LKED='LET,LIST,MAP,NCAL,REFR,RENT,REUS',
//       MAC1='SYS1.AMODGEN'
//SYSLIB   DD  UNIT=3390,VOL=SER=INDIAN          SYS1.MACLIB
//         DD  UNIT=3380,VOL=SER=NAVAJO          SYS1.AMODGEN
//         DD  DISP=SHR,DSN=SYS1.SAMPLIB,        DF/DSS PRIVATE MACROS
//             UNIT=3390,VOL=SER=INDIAN
//* STEPLIB  DD  DISP=SHR,DSN=SYSJS.ASMHV2.DUDALOAD
//SYSIN    DD  *
FASTDSS  TITLE 'JOHN GRADY SULLIVAN - DRIVER FOR DF/DSS V2.5.0'
*
*  JOHN GRADY SULLIVAN - (SHARE CODE LLU)                       * JGS *
*   MANAGER, TECHNICAL SERVICES DEPARTMENT / B737               * JGS *
*   LOMA LINDA UNIVERSITY MEDICAL CENTER                        * JGS *
*   P.O. BOX 2000                                               * JGS *
*   11234 ANDERSON STREET                                       * JGS *
*   LOMA LINDA,                                                 * JGS *
*   CALIFORNIA  92354                                           * JGS *
*   714-824-4317 X5116                                          * JGS *
*
*  JAMES R. LEWIS - (SHARE CODE LAS - WAS KS - KAISER STEEL)    **JRL**
*   MANAGER, OPERATIONS AND SYSTEMS PROGRAMMING                 **JRL**
*   SENIOR SYSTEMS PROGRAMMER                                   **JRL**
*   LOCKHEED AIRCRAFT SERVICE COMPANY                           **JRL**
*   P.O. BOX 33                                                 **JRL**
*   ONTARIO,                                                    **JRL**
*   CALIFORNIA  91761                                           **JRL**
*   714-988-2259                                                **JRL**
*
*  ARNOLD CASINGHINO - CONNECTICUT BANK AND TRUST COMPANY (CBT) * JGS *
*  MVS MODIFICATIONS TAPE FILE 320 - (11/81) (8/82) (03/84)     * JGS *
*                                    (04/84) (04/89-V23.57)     * JGS *
*                                    (07/90-V25.63)             * JGS *
*
*  STEVE HAGGERTY - LOS ANGELES MVS USER'S GROUP    (07/84)     * JGS *
*  MERV HEMP      - LOS ANGELES MVS USER'S GROUP    (06/88)     * JGS *
*
*  IF YOU USE THIS EXIT, LET ME KNOW AND I WILL SEND YOU UPDATES* JGS *
*   STEPHANIE WINKLER @ KAISER FOUNDATION HOSPITAL 415-946-3407 * JGS *
*   JOHN ROBERT @ HUB DISTRIBUTING            714-988-6431 X770 * JGS *
*
*   EDWARD THE              (V23.56 89/02/10)      805-961-4633 * JGS *
*   U.C. SANTA BARBARA, 1205 E. GIRVETZ HALL,                   * JGS *
*   SANTA BARBARA, CALIFORNIA 93106                             * JGS *
*
*   GLEN KUHN  509-376-8355 (V23.57 89/04/19)                   * JGS *
*   WESTINGHOUSE HANFORD COMPANY                                * JGS *
*   C/O BCSR - ATTN: GLEN KUHN  A1-05                           * JGS *
*   FEDERAL BUILDING                                            * JGS *
*   825 JADWIN ACENUE                                           * JGS *
*   RICHLAND, WASHINGTON 99352                                  * JGS *
*
*  HISTORY :
*        WRITTEN BY JOHN GRADY SULLIVAN, JUNE 1980,             * JGS *
*         TO SUPPORT IBM'S PROGRAM PRODUCT DRWDASDR (5740-UT1)  * JGS *
*         81/06/15 TO SUPPORT DF/DSS V1    ADRDSSU  (5740-UT3)  * JGS *
*         88/06/02 TO SUPPORT DF/DSS V2.3  ADRDSSU  (5665-327)  * JGS *
*         DF/DSS V2.5.0 (HAE2502) 90/04/13 ADRDSSU  (5665-327)  * JGS *
*        FASTDSS (ADRDSSU V1 R2.0) IS 25 TRACKS OF 3350.        * JGS *
*        FASTDSS (ADRDSSU V1 R2.1) IS 11 TRACKS OF 3380.        * JGS *
*        FASTDSS (ADRDSSU V2 R3.0) IS 25 TRACKS OF 3380.        * JGS *
*        FASTDSS (ADRDSSU V2 R4.0) IS 32 TRACKS OF 3380.        * JGS *
*        FASTDSS (ADRDSSU V2.5.0) IS 36 TRACKS OF 3380.         * JGS *
*
*        MODIFIED BY J.R.LEWIS 12/02/80 - SEE **JRL**           **JRL**
*         TO ADD NON-ZERO ABEND AND                             **JRL**
*         SELECTIVE ENQ (FOR STORAGE VOLUMES)                   **JRL**
*
*        MODIFIED BY KSC SYSTEMS SUPPORT 06/15/81               **JRL**
*         TO SUPPORT DF/DSS (ADRDSSU (5740-UT3))                **JRL**
*         RATHER THAN DRWDASDR (5740-UT1).                      **JRL**
*
*        MODIFIED BY JGS - 02/20/82 FOLLOWING ENHANCEMENTS :    * JGS *
*        JGS - ADDED ASSEMBLER SYSPARM SUPPORT IN ORDER TO      * JGS *
*         SIMPLIFY SHARING OF IMPROVEMENTS TO THESE EXITS.      * JGS *
*         VALID OPTIONS ARE SYSPARM(JGS) OR SYSPARM(JRL).       * JGS *
*         (SEE BELOW FOR SYSPARM SUPPORTED DIFFERENCES).        * JGS *
*         AUTHORIZATION ROUTINE SUPPORT FOR THE 'VOLUME'        * JGS *
*           ENTRIES WHERE THE FORMAT 1 DSCB IS NOT PASSED TO    * JGS *
*           OUR EXIT.                                           * JGS *
*
*        MODIFIED BY JGS - 07/12/82 FOLLOWING ENHANCEMENTS :    * JGS *
*        JGS - ADDED TESTAUTH TO SKIP AUTH SVC'S IF UN-NEEDED.  * JGS *
*
*        MODIFIED BY JGS - 08/16/82 FOLLOWING ENHANCEMENTS :    * JGS *
*        JGS - ADDED CODE TO UTILIZE MVS/SP 1.2+ (1.3, 2.XA)'S  * JGS *
*              UCB LOOKUP ROUTINE (IF AVAILABLE).  (DOWNWARD    * JGS *
*              COMPATIBLE WITH BASE MVS 3.8 AND SHOULD BE       * JGS *
*              UPWARD COMPATIBLE WITH MVS/XA).                  * JGS *
*
*        MODIFIED BY JGS - 01/11/84 FOLLOWING ENHANCEMENTS :    * JGS *
*        JGS - SUPPORT DF/DSS RELEASE 2.0 (HAE1400).            * JGS *
*              PARAMETER LISTS PASSED TO EXITS WERE CHANGED     * JGS *
*              (WILL NOT FUNCTION WITH PRIOR RELEASES).         * JGS *
*              ENHANCED AUTHORIZATION ROUTINE TO ISSUE MLWTO    * JGS *
*              WITH ADDITIONAL INFORMATION REGARDING THE        * JGS *
*              CURRENT ACTIVITY, AND TO ALLOW ADDITIONAL        * JGS *
*              REPLIES FOR USABILITY AND DEBUGGING.             * JGS *
*              IF YOU REPLY WITH A NUMBER 0 - 9 TO THE          * JGS *
*              AUTHORIZATION PROMPT, THE REPLY WILL BE          * JGS *
*              MULTIPLIED BY 4 AND USED AS THE RETURN CODE      * JGS *
*              FROM THE EXIT.                                   * JGS *
*              RENUMBERED SOURCE (SORRY).                       * JGS *
*
*        MODIFIED BY JGS - 04/19/84 FOLLOWING ENHANCEMENTS :    * JGS *
*        JGS - SUPPORT DF/DSS RELEASE 2.1 (HAE1500).            * JGS *
*              USE THE DF/DSS R2.X MACRO ADRUPB  TO MAP THE     * JGS *
*              PARAMETER LIST PASSED TO THE AUTHORIZATION EXIT. * JGS *
*              USE THE DF/DSS R2.X MACRO ADRUNQB TO MAP THE     * JGS *
*              PARAMETER LIST PASSED TO THE VOLUME ENQUE EXIT.  * JGS *
*              (MACROS ARE IN SYS1.SAMPLIB, NOT SYS1.MACLIB).   * JGS *
*              ADDED AN IDENTIFY OF MODULE/CSECT ADRRELVL       * JGS *
*              (WXTRN FOR R2.0 COMPATABILITY) WHICH CONTAINS    * JGS *
*              DF/DSS VERSION - RELEASE - MODIFICATION LEVEL.   * JGS *
*              ADDED A TEST FOR MVS IN THE UCB SCAN ROUTINE     * JGS *
*              FOR VS/1 USERS OF DF/DSS WITH A SHORTER CVT.     * JGS *
*              ADDED A CHECK FOR SPECIAL JOBNAME/STEPNAME       * JGS *
*              COMBINATION AND ALWAYS ISSUE PASSWORD PROMPT     * JGS *
*              IF PRESENT (JGSTDSS/TESTDSS).                    * JGS *
*
*        MODIFIED BY JGS - 06/22/84 FOLLOWING ENHANCEMENTS :    * JGS *
*        JGS - REMOVED REFERENCE TO CVTILK2 (CVT+X'028')        * JGS *
*              (NAMED CVTLLTRM IN MVS/XA CVT) TO AVOID          * JGS *
*              ASSEMBLY ERROR - UNDEFINED SYMBOL (IN THE        * JGS *
*              CODE PATH FOR PRE SP 1.3 UCB LOOK-UP).           * JGS *
*
*        MODIFIED BY JGS - 88/06/02 FOLLOWING ENHANCEMENTS :    * JGS *
*         SUPPORT DF/DSS V2.3 ADRDSSU (5665-327) (HAE2302).     * JGS *
*         AMODE=31 AND SPLEVEL 2 (CURRENT DEFAULT - MVS/XA 2.2) * JGS *
*         IMPROVED AUTHORIZATION CHECKING IN MVS/XA CAUSED      * JGS *
*         ABEND 306-0C WHILE TESTING FROM AN AUTHORIZED LIBRARY,* JGS *
*         SO CODE WAS ADDED TO TURN ON THE BITS                 * JGS *
*         CDSYSLIB AND CDAUTH IN THE CDE.                       * JGS *
*
*        MODIFIED BY JGS - 89/05/30 FOLLOWING ENHANCEMENTS :    * JGS *
*         SUPPORT DF/DSS V2.4 ADRDSSU (5665-327) (HAE2402).     * JGS *
*         ADD VERSION-RELEASE-MODIFICATION LEVEL INFORMATION    * JGS *
*         TO MESSAGE ADR992I.                                   * JGS *
*
         EJECT ,
*
*        SYSPARM SUPPORTED DIFFERENCES ARE AS FOLLOWS :         * JGS *
*        MAGIC SVC REQUIREMENTS AND SVC NUMBER (HANDY FOR       * JGS *
*         TESTING FROM AN UNAUTHORIZED DATASET).                * JGS *
*        JGS - RETURN CODE AS PASSED FROM DF/DSS (NO ABEND)     * JGS *
*        JGS - ALWAYS USE SHORT DURATION ENQUE                  * JGS *
*        JRL - ABEND IF DF/DSS GIVES NON-ZERO RETURN CODE       **JRL**
*        JRL - USE LONG DURATION ENQUE FOR STORAGE VOLUMES,     **JRL**
*              AND SHORT DURATION ENQUE IF NOT STORAGE          **JRL**
*
*        ANY OTHER SYSPARM WILL GET :                           * JGS *
*         MNOTE 4,'SYSPARM(&SYSPARM) NOT SUPPORTED'             * JGS *
*         NO MAGIC SVC                                          * JGS *
*         RETURN CODE AS ISSUED BY DF/DSS (NO ABEND)            * JGS *
*         LONG VTOC ENQUE SUPPORT                               * JGS *
*         OPERATOR PROMPT FOR U OR T IF THERE ARE PASSWORD      * JGS *
*          PROTECTED DATASETS ON THE OUTPUT (TARGET) VOLUME     * JGS *
         EJECT ,
*  FUNCTIONS :
*    1. INVOKE IBM'S DF/DSS (FAST DUMP/RESTORE PROGRAM) UTILITY
*         YET PROVIDING OUR USER EXITS TO MAKE LIFE EASIER.
*         KEEP THE VANILLA IBM PRODUCT AROUND IN CASE ANYONE
*         SHOULD TRY TO RUN IT.  WE ALSO AVOID MAINTENANCE (SMP)
*         INTERSECTION PROBLEMS.  AFTER DOING MAINTENANCE, JUST BE
*         SURE TO REASSEMBLE AND RELINK THIS MODULE.
*    2. ADRUENQ - USER VTOC ENQUE ROUTINE.
*         SUPPRESS THE SYSVTOC ENQUE ON THE FROM (SOURCE) VOLUME.
*    3. ADRUPSWD - USER AUTHORIZATION DECISION ROUTINE.
*         SUPPRESS PASSWORD CHECKING ON THE FROM (SOURCE) VOLUME.
*         PROMPT OPERATOR TO ALLOW CONTINUATION WITHOUT PASSWORD
*           CHECKING ON THE TO (TARGET) VOLUME.
*         THIS ROUTINE IS ALSO APPLICABLE TO VSAM DATASETS.     * JGS *
*           WITH THESE EXITS, YOU WILL NO LONGER NEED VSAM      * JGS *
*           JOBCAT OR STEPCAT DD CARDS IN YOUR JCL IN ORDER TO  * JGS *
*           PERFORM OPERATIONS AGAINST PACKS WITH VSAM ON THEM. * JGS *
*         I DON'T KNOW ABOUT EFFECTS ON RACF OR ACF-2.          * JGS *
*
*  INPUT :
*    1. REGULAR OS JCL PARM FIELD AS EXPECTED BY DF/DSS.
*        PARM FIELD IS PASSED TO DF/DSS WITHOUT ANY EXAMINATION.
*          DF/DSS SEEMS TO IGNORE INVALID PARM OPTIONS.
*
*  OUTPUT :
*    1. NONE - OTHER THAN THAT PROVIDED BY DF/DSS.
*
*  NOTE : DF/DSS (AND THESE EXITS) MUST RUN AUTHORIZED.         * JGS *
*        SINCE THEY ARE LINK EDITED REENTRANT, PROGRAM FETCH    * JGS *
*        WILL LOAD THEM INTO STORAGE SUBPOOL 252 (KEY ZERO),    * JGS *
*        SO THEY BETTER BE KEPT REENTRANT.                      * JGS *
*
         AIF   ('&SYSPARM' EQ 'JGS').SPARMOK                    * JGS *
         AIF   ('&SYSPARM' EQ 'JRL').SPARMOK                    * JGS *
         MNOTE 4,'SYSPARM(&SYSPARM.) NOT SUPPORTED'             * JGS *
.SPARMOK ANOP                                                   * JGS *
         EJECT
FASTDSS  CSECT
         SPACE 1
FASTDSS  AMODE 31                                               * JGS *
FASTDSS  RMODE ANY                                              * JGS *
         SPACE 1
*
*  JOHN GRADY SULLIVAN - JUNE, 1980
*
*  FUNCTION :
*         FRONT END / DRIVER FOR DF/DSS (PGM=ADRDSSU)
*
*  INPUT :
*    REGISTER 1 POINTS TO A ONE WORD PARAMETER LIST :
*      PARAMETER 1 -> OS PARM FIELD IN JCL
*
*  OUTPUT :
*    R15  :        THE RETURN CODE FROM DF/DSS (PGM=ADRDSSU)    * JGS *
*  OUTPUT (OPTIONAL WITH SYSPARM 'JRL') :
*    R15  = 0  =>  THE RETURN CODE FROM DF/DSS (PGM=ADRDSSU)    **JRL**
*
*    R15 Â¬= 0  =>  USER '99' ABEND WILL BE ISSUED               **JRL**
*
*                                                               * JGS *
*  REGISTER USAGE :
*        R0    SYSTEM
*        R1    SYSTEM - OS PARM FIELD.
*        R2    WORK
*        R3    WORK
*        R4    WORK
*        R5    WORK
*        R6    UNUSED
*        R7    UNUSED
*        R8    UNUSED
*        R9    UNUSED
*        R10   UNUSED
*        R11   BASE REGISTER FOR WORK AREA TO MAINTAIN REENTRANCY.
*        R12   BASE REGISTER FOR PROGRAM
*        R13   SYSTEM - SAVE AREA CHAIN
*        R14   RETURN ADDRESS
*        R15   WORK, SYSTEM, AND RETURN CODE
*
         EJECT
         GBLC  &SYSSPLV
&SYSSPLV SETC  '1'                 SET TO ENSURE MVS/SP VER. 1  * JGS *
         MNOTE *,'*        THE VALUE OF SPLEVEL IS NOW 1'       * JGS *
* * *    SPLEVEL SET=1             SEE MVS/SP 1.3 UZ56757-8307  * JGS *
         SPACE 2
         SPLEVEL SET               RESET AS APPROPRIATE (2 OR 3)* JGS *
         SPACE 2
         SAVE (14,12),,FASTDSS__JOHN_GRADY_SULLIVAN_&SYSDATE.
         SPACE 1
         LA    12,0(,15)           CLEAR HIGH ORDER BIT/BYTE
         USING FASTDSS,12          ESTABLISH ADDRESSABILITY
         SPACE 1
         GETMAIN R,LV=72           GETMAIN A NEW SAVE AREA
         ST    13,4(1)             BACKCHAIN TO PREVIOUS SAVE AREA
         ST    1,8(13)             FORWARD CHAIN FROMPREVIOUS SAVE AREA
         LR    13,1                MAKE OUR SAVE AREA CURRENT
         SPACE 2
R0       EQU   0                   REGISTER EQUATES
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
*  TO KEEP OUR PROGRAM REENTRANT, ONE MUST OBTAIN AND
*   INITIALIZE A WORK AREA EXTERNAL TO OUR PROGRAM
*   FOR THINGS WHICH MAY BE MODIFIED DURING EXECUTION
*   (SUCH AS SVC PARAMETER LISTS, RETURN CODE SAVE AREAS, ETC.).
         SPACE 1
         GETMAIN   R,LV=FASTRLEN   GETMAIN FOR MODIFIABLE AREA
         SPACE 1
*  SET UP TO INITIALIZE OUR NEWLY FOUND WORK AREA
         LR    R2,R1               TARGET ADDRESS
         LA    R3,FASTRLEN         TARGET LENGTH
         LA    R4,FASTRSTR         SOURCE ADDRESS
         LR    R5,R3               SOURCE LENGTH
         SPACE 1
         MVCL  R2,R4               INITIALIZE MODIFIABLE AREA
         SPACE 1
         LA    R11,0(,R1)          SAVE THE ADDRESS TO OUR AREA
         USING FASTRSTR,R11        ESTABLISH ADDRESSABILITY
         SPACE 3
*        CHECK AND/OR GET AUTHORIZATION                         * JGS *
         SPACE 1
         TESTAUTH FCTN=1           TEST CURRENT AUTHORIZATION   * JGS *
         LTR   R15,R15             ARE WE ALREADY AUTHORIZED ?  * JGS *
         BZ    FASTAUTH            R15 = 0 => YES, SO BRANCH    * JGS *
         SPACE 2                                                * JGS *
         OI    AUTHFLAG,X'01'      INDICATE AUTH SVC USED       * JGS *
         MNOTE 0,'MAGIC SVC ISSUED HERE (MAYBE). '              * JGS *
         AIF   ('&SYSPARM' NE 'JGS').TRYJRL1                    * JGS *
         L     R1,=C'AUTH'         INDICATE AUTH ON             * JGS *
         SVC   211                 GET AUTHORIZED               * JGS *
         AGO   .ENDMOD1                                         * JGS *
.TRYJRL1 AIF   ('&SYSPARM' NE 'JRL').ENDMOD1                    * JGS *
         LA    R1,=C'AUTH'                                      **JRL**
         SVC   237                 GET AUTHORIZED WHILE TESTING **JRL**
.ENDMOD1 ANOP                                                   * JGS *
         SPACE 1
         L     R2,CVTPTR           POINT TO THE CVT             * JGS *
         USING CVT,R2              ADDRESSABILITY               * JGS *
         L     R2,CVTTCBP          R2 -> A(NEXT & CURRENT TCBS) * JGS *
         L     R2,4(,R2)           R2 -> OUR TCB                * JGS *
         DROP  R2                  UNADDRESSABILITY             * JGS *
         USING TCB,R2              ADDRESSABILITY               * JGS *
         L     R2,TCBJPQ           R2 -> JPQ (JOB PACK QUEUE)CDE* JGS *
         DROP  R2                  UNADDRESSABILITY             * JGS *
         USING CDENTRY,R2          ADDRESSABILITY               * JGS *
         CLC   CDNAME(8),=CL8'FASTDSS '    CDE FOR OUR MODULE ? * JGS *
         BNE   FASTNCDA            NO, BRANCH, NOT OUR MODULE   * JGS *
         SPACE 1
         MODESET KEY=ZERO          CHANGE PSW KEY AS NEEDED     * JGS *
         SPACE 1
         OI    CDATTR2,CDSYSLIB+CDAUTH  IND PGM & LIB ARE AUTH  * JGS *
         DROP  R2                  UNADDRESSABILITY             * JGS *
         SPACE 1
         MODESET KEY=NZERO         CHANGE PSW KEY AS NEEDED     * JGS *
         SPACE 1                                                * JGS *
FASTNCDA DS    0H                  SKIP CHANGE TO CDAUTH        * JGS *
         SPACE 1                                                * JGS *
FASTAUTH DS    0H                  AUTHORIZATION IS NOW OK      * JGS *
         EJECT
*  ISSUE IDENTIFY SVCS TO CREATE ENTRY POINTS WITH OUR VERSIONS
*   OF ADRUENQ AND ADRUPSWD CONTAINING OUR USER EXITS.
*   THIS WILL CAUSE DF/DSS TO USE OUR ROUTINES RATHER THAN
*   LOAD THE REAL ONES FROM LINKLIB AT EXECUTION.
         SPACE 1
*  THE ENTRY POINTS CREATED STARTING WITH JGS..... ARE TO MAKE IT
*   CLEAR IN A DUMP THAT THE JGS SUPPLIED ENTRY POINTS ARE PRESENT.
*   THEY WILL ALSO STAND OUT IN A LOAD MODULE MAP.
*   THESE ARE ACTUALLY RESOLVED AT LINK EDIT TIME, NOT AT EXECUTION.
         SPACE 1
         BALR  R10,0               ADDRESS FOR DEBUGGING
         LA    R2,SUBRTBLE         GET ADDRESS OF SUBROUTINE TABLE
         LA    R3,SUBRTCNT         COUNT OF ENTRIES IN TABLE
         SPACE 1
IDLDLOOP DS    0H
         LA    R0,0(,R2)           R0 -> ENTRY NAME
         L     R1,8(,R2)           R1 -> ENTRY POINT OF ROUTINE
         LA    R1,0(,R1)           CLEAR HIGH ORDER BIT/BYTE    * JGS *
         LTR   R1,R1               TEST FOR 0 (UNRESOLVED VCON) * JGS *
         BZ    IDLDSKIP            YES, 0 => UNRESOLVED VCON, BR* JGS *
         SPACE 1
         IDENTIFY EPLOC=(0),ENTRY=(1)    CREATE THE ENTRY
         SPACE 1
         LTR   R15,R15             TEST RETURN CODE FROM IDENTIFY
         BNZ   JGSABEND            IF NOT ZERO, GO ABEND
         SPACE 1
         LA    R0,0(,R2)           R0 -> ENTRY NAME
         LOAD  EPLOC=(0)           INCREMENT USE COUNT (FOR DEBUGGING)
         SPACE 1
IDLDSKIP DS    0H                                               * JGS *
         LA    R2,12(,R2)          BUMP TO NEXT SUBROUTINE NAME
         BCT   R3,IDLDLOOP         ISSUE IDENTIFY & LOAD UNTIL DONE
         EJECT
*  GET THE PARM FIELD ADDRESS FROM THE PRIOR OS SAVE AREA.
         L     R1,4(,R13)          R1 = ADDRESS OF PRIOR SAVE AREA
         L     R1,24(,R1)          R1 = ADDRESS OF PARM FIELD POINTER
         SPACE 1
*  NOW LINK TO DF/DSS, PASSING THE OS PARM FIELD.
         LINK  EP=ADRDSSU,MF=(E,(1)),SF=(E,ADRDLINK)   OFF WE GO...
         SPACE 1
         ST    R15,FASTRET         SAVE THE RETURN CODE FROM DF/DSS
         SPACE 3
*  ISSUE DELETE SVCS TO REMOVE OUR ENTRY POINTS.                * JGS *
         SPACE 1                                                * JGS *
         LA    R2,SUBRTBLE         ADDRESS OF SUBROUTINE TABLE  * JGS *
         LA    R3,SUBRTCNT         COUNT OF ENTRIES IN TABLE    * JGS *
         SPACE 1                                                * JGS *
DELELOOP EQU   *                                                * JGS *
         LA    R0,0(,R2)           R0 -> ENTRY NAME             * JGS *
         L     R1,8(,R2)           R1 -> ENTRY POINT OF ROUTINE * JGS *
         LA    R1,0(,R1)           CLEAR HIGH ORDER BIT/BYTE    * JGS *
         LTR   R1,R1               TEST FOR 0 (UNRESOLVED VCON) * JGS *
         BZ    DELESKIP            YES, 0 => UNRESOLVED VCON, BR* JGS *
         SPACE 1                                                * JGS *
         DELETE EPLOC=(0)          LOWER USE COUNT FROM LOAD    * JGS *
         SPACE 1                                                * JGS *
         LTR   R15,R15             TEST RETURN CODE FROM DELETE * JGS *
         BNZ   JGSABEND            IF NOT ZERO, GO ABEND        * JGS *
         SPACE 1                                                * JGS *
DELESKIP DS    0H                                               * JGS *
         LA    R2,12(,R2)          BUMP TO NEXT SUBROUTINE NAME * JGS *
         BCT   R3,DELELOOP         ISSUE DELETE'S UNTIL DONE    * JGS *
         SPACE 2                                                * JGS *
         TM    AUTHFLAG,X'01'      WAS      AUTH SVC USED       * JGS *
         BNO   NOUNAUTH            NO,  BRANCH, SKIP UNAUTH CODE* JGS *
         AIF   ('&SYSPARM' NE 'JGS').TRYJRL2                    * JGS *
         MNOTE *,'MAGIC SVC ISSUED HERE'                        * JGS *
         L     R1,=C'UNAU'         INDICATE AUTH OFF            * JGS *
         NI    AUTHFLAG,X'FF'-X'01' INDICATE AUTH SVC UNUSED    * JGS *
         SVC   211                 ISSUE AUTH SVC               * JGS *
         AGO   .ENDMOD2                                         * JGS *
.TRYJRL2 AIF   ('&SYSPARM' NE 'JRL').ENDMOD2                    * JGS *
         MNOTE 0,'JIM, I NEED SOME CODE HERE... '               * JGS *
* * *    LA    R1,=C'AUTH'                                      **JRL**
* * *    SVC   237                 ISSUE AUTH SVC               **JRL**
.ENDMOD2 ANOP                                                   * JGS *
         SPACE 2                                                * JGS *
NOUNAUTH DS    0H                                               * JGS *
         SPACE 2                                                * JGS *
         EJECT
*  PRESERVE RETURN CODE IN R2 SINCE FREEMAIN GIVES A RETURN CODE IN R15
         L     R2,FASTRET          PICK UP RETURN CODE FROM WORKAREA
         SPACE 1
         FREEMAIN  R,LV=FASTRLEN,A=(R11)   FREEMAIN WORK AREA
         SPACE 1
         LR    R1,R13              SET UP FOR FREEMAIN OF SAVE AREA
         L     R13,4(,R13)         POINT TO PRIOR (CALLER'S) SAVE AREA
         FREEMAIN  R,LV=72,A=(1)   ISSUE FREEMAIN OF OUR SAVE AREA
         SPACE 3
         LR    R15,R2              RELOAD R15 AFTER FREEMAIN
         SPACE 1
         AIF   ('&SYSPARM' NE 'JGS').TRYJRL3                    * JGS *
*        PRESERVE RETURN CODE AS PASSED FROM DF/DSS,            * JGS *
*        AND SUPPRESS ABEND FOR NON-ZERO RETURN CODE            * JGS *
         AGO   .ENDMOD3                                         * JGS *
.TRYJRL3 AIF   ('&SYSPARM' NE 'JRL').ENDMOD3                    * JGS *
         LTR   R15,15              TEST OF ALL OK               **JRL**
         BNZ   JRLABEND            NON-ZERO, ABEND              **JRL**
.ENDMOD3 ANOP                                                   * JGS *
         SPACE 1
         RETURN (14,12),T,RC=(15)  RETURN TO CALLER
         SPACE 5                                                **JRL**
JRLABEND WTO   'ADR993I DF/DSS NON-ZERO RETURN CODE. STEP WILL  ABEND...
               '                                                **JRL**
         SPACE 2                                                **JRL**
         WTO   'ADR993I CHECK SYSPRINT.'                        **JRL**
         SPACE 2                                                **JRL**
         ABEND 99,DUMP                                          **JRL**
         EJECT
*  NON MODIFIABLE PROGRAM STORAGE
         SPACE 2
JGSABEND DC    F'0'                CAUSE AN S-0C1 ABEND IF EXECUTED
         DC    C' ABEND 0C1 FORCED DUE TO ERROR.'
         DC    C' REG 10 POINTS TO THE FAILING CODE. '
         SPACE 2
*  ADRUENQ  AND ADRUPSWD ARE THE ENTRY POINTS ACTUALLY
*   NEEDED DURING EXECUTION.
*  ADRRELVL IS A CSECT WHICH CONTAINS THE DF/DSS                * JGS *
*   VERSION - RELEASE - MODIFICATION LEVEL (X'00VVRRMM')        * JGS *
*  THE OTHERS ARE TO MAKE OUR PRESENCE KNOWN IN A DUMP
         SPACE 1
SUBRTBLE DC    0F'0'               BOUNDARY ALIGNMENT
         DC    CL8'ADRDSSU',V(ADRDSSU)    DF/DSS ENTRY POINT
         WXTRN ADRRELVL            UNRESOLVED VCON IS OK        * JGS *
         DC    CL8'ADRRELVL',V(ADRRELVL)  DF/DSS VER-REL-MOD    * JGS *
         DC    CL8'ADRUENQ',V(ADRUENQ)    USER ENQUE ROUTINE
         DC    CL8'JGSUENQ',V(JGSUENQ)    USER ENQUE ROUTINE EP
         DC    CL8'ADRUPSWD',V(ADRUPSWD)  USER AUTHORIZATION ROUTINE
         DC    CL8'JGSUPSWD',V(JGSUPSWD)  USER AUTHORIZATION ROUTINE EP
SUBRTCNT EQU   (*-SUBRTBLE)/12     COUNT OF SUBROUTINES IN TABLE
         SPACE 1
         LTORG
         SPACE 2
*  THIS IS THE WORK AREA FOR PRESERVING REENTRANCY.
*  THIS DATA IS ACTUALLY COPIED TO A GETMAINED AREA DURING EXECUTION,
*   WHICH IS ADDRESSED BY R11.
         SPACE 1
FASTRSTR DC    0D'0',C'FASTRSTR'   BOUNDARY ALIGNMENT AND ID
FASTRET  DC    F'0'                RETURN CODE FROM DF/DSS
         SPACE 1
ADRDLINK LINK  EP=ADRDSSU,SF=L     LIST FORM OF LINK MACRO
         SPACE 1
AUTHFLAG DC    X'00'               X'01' => AUTH SVC USED
         DC    CL8'FASTREND'
FASTREND DC    0D'0'               END OF MODIFIABLE AREA
FASTRLEN EQU   FASTREND-FASTRSTR   LENGTH OF WORK AREA
         SPACE 2
         DROP  ,                   DROP ALL PREVIOUS BASE REGISTERS
         SPACE 1
*  THIS IS THE LOGICAL END OF FASTDSS - DRIVER FOR DF/DSS
         TITLE 'JOHN GRADY SULLIVAN - ADRUENQ - ENQUE ROUTINE'
ADRUENQ  CSECT
         SPACE 1
ADRUENQ  AMODE 31                                               * JGS *
ADRUENQ  RMODE ANY                                              * JGS *
         SPACE 1
         ENTRY JGSUENQ             CREATE AN EXTERNAL SYMBOL ENTRY
JGSUENQ EQU    *                   DEFINE THE SYMBOL IN THE PROGRAM
         SPACE 1
*
*  JOHN GRADY SULLIVAN - JUNE, 1980
*
*  FUNCTION :
*         BYPASS THE SYSVTOC ENQUE ON THE FROM (SOURCE) VOLUME
*
*  INPUT :
*    REGISTER 1 POINTS TO A TWO WORD PARAMETER LIST :
*      PARAMETER 1 -> VOLUME SERIAL FIELD IN THE UCB
*      PARAMETER 2 =  FLAGS (NOT A POINTER TO FLAGS) INDICATING * JGS *
*                       THE OPERATION IN PROGRESS               * JGS *
*        BYTE 1 : FLAGS            UNFLG1 = 4(R10)              * JGS *
*          BIT 0 : END OF PARAMETER LIST (ALWAYS 1)             * JGS *
*          BIT 1 :       RESERVED.                              * JGS *
*          BIT 2 : 1 => DUMP OPERATION                          * JGS *
*          BIT 3 :       RESERVED                               * JGS *
*          BIT 4 : 1 => COPY OPERATION                          * JGS *
*          BIT 5 : 1 => PRINT OPERATION                         * JGS *
*          BIT 6 :       RESERVED                               * JGS *
*          BIT 7 :       RESERVED                               * JGS *
*        BYTE 2 : FLAGS            UNFLG2 = 5(R10)              * JGS *
*          BIT 0 : 1 => DATASET-BY-NAME OPERATION               * JGS *
*          BIT 1 : 1 => FULL VOLUME OPERATION                   * JGS *
*          BIT 2 : 1 => PARTIAL VOLUME OPERATION                * JGS *
*          BIT 3 - 7 :   RESERVED                               * JGS *
*        BYTE 3 : FLAGS            UNFLG3 = 6(R10)              * JGS *
*          BIT 0 : 1 => TOLERATE(ENQFAILURE) IS USED            * JGS *
*          BIT 1 - 7 :   RESERVED                               * JGS *
*        BYTE 4 :        RESERVED         = 7(R10)              * JGS *
*
*
*  OUTPUT :
*    R15  = 0  =>  ENQUE ON THE VOLUME BEING DUMPED FOR THE     * JGS *
*                  DURATION OF THE OPERATION (LONG ENQUE)       * JGS *
*    R15  = 4  =>  ENQUE ON VOLUME ONLY FOR THE DURATION        * JGS *
*                  OF THE VTOC ACCESS (SHORT ENQUE).            * JGS *
*                  RC = 4 REQUIRED AS PER APAR PP91793          * JGS *
*                  AND DOCUMENTATION APAR PP94829.              * JGS *
*    R15  = ?  =>  SAME AS 0 => LONG ENQUE                      * JGS *
*
*    JGS  : ALWAYS SET R15 = 4 TO GET SHORT ENQUE               * JGS *
*    JRL  : SET R15 = 0 FOR LONG ENQUE IF STORAGE VOLUME        **JRL**
*    JRL  : SET R15 = 4 FOR SHORT ENQUE IF NOT STORAGE VOLUME   **JRL**
*
         EJECT 1
*
*  REGISTER USAGE :
*                                                               * JGS *
*        R0    SYSTEM
*        R1    INPUT  - POINTER TO THE PARAMETER LIST           * JGS *
*        R1    WORK   - POINTER TO THE UCB PARAMETER LIST (UPL) * JGS *
*        R2    CVT=>,   OUTPUT RETURN CODE ( 0 OR 4 )           * JGS *
*        R3    UCB=>                                            * JGS *
*        R4    UCBILK2=>                                        * JGS *
*        R5    UCB LOOK UP ROUTINE (INTERNAL) WORK REGISTER     * JGS *
*        R6
*        R7
*        R8    PARAMETER 2 -> VOLUME SERIAL NUMBER IN UCB       * JGS *
*        R9
*        R10   BASE REGISTER FOR ADRUNQB (PARAMETER LIST/FLAGS) * JGS *
*        R11   BASE REGISTER FOR REENTRANT WORK AREA            * JGS *
*        R12   BASE REGISTER FOR PROGRAM
*        R13   SYSTEM - SAVE AREA CHAIN
*        R14   RETURN ADDRESS
*        R15   SYSTEM & RETURN CODE
*                                                               **JRL**
* A SCAN OF UCB'S USING CVTILK2 TABLE IS REQUIRED DUE TO        **JRL**
* APAR PP89259 CHANGING THE PARAMETER NOW POINTING TO           **JRL**
* A STORAGE AREA NOT IN UCB.                                    **JRL**
*                                                               **JRL**
         EJECT
         SAVE (14,12),,ADRUENQ__JOHN_GRADY_SULLIVAN_&SYSDATE.
         SPACE 1
         LA    12,0(,15)           CLEAR HIGH ORDER BIT/BYTE
         USING ADRUENQ,12          ESTABLISH ADDRESSABILITY
         SPACE 1
         LA    R10,0(,R1)          -> INPUT PARAMETER LIST      * JGS *
         USING ADRUNQB,R10         ESTABLISH ADDRESSABILITY     * JGS *
         SPACE 1                                                **JRL**
         GETMAIN R,LV=72           GETMAIN A NEW SAVE AREA      **JRL**
         SPACE 1                                                **JRL**
         ST    R13,4(,R1)          BACKCHAIN                    **JRL**
         ST    R1,8(,R13)          FORWARD CHAIN                **JRL**
         LR    13,R1               CURRENT SAVE AREA            **JRL**
         EJECT                                                  **JRL**
* TO KEEP OUR PROGRAM REENTRANT, ONE MUST OBTAIN AND            **JRL**
*  INITIALIZE A WORK AREA EXTERNAL TO OUR PROGRAM               **JRL**
*  FOR THINGS WHICH MAY BE MODIFIED DURING EXECUTION            **JRL**
*  (SUCH AS SVC PARAMETER LISTS, RETURN CODE SAVE AREA, ETC.).  **JRL**
         SPACE 1                                                **JRL**
         GETMAIN R,LV=UENQRLEN GETMAIN FOR MODIFIABLE AREA      **JRL**
         SPACE 1                                                **JRL**
* SETUP TO INITIALIZE OUR NEWLY FOUND WORK AREA                 **JRL**
         LR    R2,R1               TARGET AREA                  **JRL**
         LA    R3,UENQRLEN         TARGET LENGTH                **JRL**
         LA    R4,UENQRSTR         SOURCE ADDRESS               **JRL**
         SPACE 1                                                **JRL**
         MVCL  R2,R4               INITIALIZE MODIFIABLE AREA   **JRL**
         SPACE 1                                                **JRL**
         LA    R11,0(,R1)          SAVE THE ADDRESS OF OUR AREA **JRL**
         USING UENQRSTR,R11 ESTABLISH ADDRESSABILITY            **JRL**
         SPACE 3                                                **JRL**
*  R10 -> ADRUNQB (THE INPUT PARAMETER LIST).                   * JGS *
* GET THE PARAMETER ADDRESS FROM THE PRIOR OS SAVE AREA         **JRL**
* * *    L     R1,4(,R13)          R1 = ADDRESS PREV SAVE AREA  **JRL**
* * *    L     R1,24(,R1)          R1 = ADDR OF PARAMETER LIST  **JRL**
* * *    USING ADRUNQB,R1          ESTABLISH ADDRESSABILITY     * JGS *
         L     R8,UNUCBVSA         ADDRESS OF VOLID TO R8       * JGS *
* * *    LA    R10,4(,R1)          R10 -> FLAGS, NOT HOLD FLAGS * JGS *
         SPACE 2                                                **JRL**
         AIF   ('&SYSPARM' NE 'JGS').TRYJRL4                    * JGS *
         MNOTE 0,'SUBROUTINE BYPASSED, SHORT ENQUE FORCED'      * JGS *
         LA    R2,4                SETUP FOR SHORT ENQUE/RESERVE* JGS *
         B     UENQRET             BYPASS REST OF SUBROUTINE    * JGS *
         SPACE 1                                                * JGS *
         AGO   .ENDMOD4                                         * JGS *
.TRYJRL4 AIF   ('&SYSPARM' NE 'JRL').ENDMOD4                    * JGS *
.ENDMOD4 ANOP                                                   * JGS *
         EJECT                                                  **JRL**
* SCAN UCB LIST FOR VOLID SPECIFIED                             **JRL**
*                                                               * JGS *
*        JGS - ADDED CODE TO UTILIZE MVS/SP 1.2+ (1.3, 2.XA)'S  * JGS *
*              UCB LOOKUP ROUTINE (IF AVAILABLE).  (DOWNWARD    * JGS *
*              COMPATIBLE WITH BASE MVS 3.8 AND SHOULD BE       * JGS *
*              UPWARD COMPATIBLE WITH MVS/XA).                  * JGS *
*        JGS - THANKS! TO C. JOHN KINN AT LOCKHEED (BURBANK)    * JGS *
*              FOR PUTTING THE BASIC CODE ON THE JES2 MODS TAPE * JGS *
*                                                               * JGS *
*  REGISTER USAGE :                                             * JGS *
*  R1    UPL  - UCB LOOK-UP ROUTINE PARAMETER LIST              * JGS *
*  R2    WORK / CVT POINTER                                     * JGS *
*  R3    WORK / UCB POINTER                                     * JGS *
*  R4    WORK - UCB LOOK-UP (CVTILK2 POINTER)                   * JGS *
*  R5    WORK - UCB LOOK-UP DEVICE CLASS                        * JGS *
*  R13   SAVE - MUST POINT TO A USABLE SAVE AREA                * JGS *
*  R14   LINKAGE RETURN ADDRESS (FROM UCB LOOK-UP ROUTINE)      * JGS *
*  R15   WORK / LINKAGE ADDRESS ( TO  UCB LOOK-UP ROUTINE)      * JGS *
*                                                               * JGS *
         SPACE 5                                                * JGS *
         PUSH  USING                                            * JGS *
         SPACE 1                                                * JGS *
* UCB SCAN ROUTINE - INITIALIZATION                             * JGS *
         SPACE 1                                                * JGS *
         LA    R1,UPLWRKAD         ADDRESS THE UPL WORK AREA    * JGS *
         LR    R4,R1               CLEAR STORAGE                * JGS *
         LA    R5,UPLSIZE            FROM GETMAIN               * JGS *
         SLR   R15,R15                 FOR UCB                  * JGS *
         MVCL  R4,R14                    PARAMETER LIST         * JGS *
         SPACE 1                                                * JGS *
***      MVI   UPLCLASS,X'00'      SET DEVICE CLASS IN UPL (ANY)* JGS *
         MVI   UPLCLASS,UCB3DACC   SET DEVICE CLASS IN UPL (DA) * JGS *
         LA    R2,UPLWORK          STORE POINTER                * JGS *
         ST    R2,UPLWRKAD           TO WORK AREA               * JGS *
         LA    R2,UPLCLASS         STORE POINTER                * JGS *
         ST    R2,UPLCLSAD           TO DEVICE CLASS            * JGS *
         LA    R2,UPLUCB           STORE POINTER                * JGS *
         ST    R2,UPLUCBAD           TO UCB                     * JGS *
         OI    UPLUCBAD,X'80'      INDICATE END OF LIST         * JGS *
         EJECT                                                  * JGS *
* UCB SCAN ROUTINE - PREPARE FOR INVOCATION OR EMULATION        * JGS *
         SPACE 1                                                * JGS *
UCBUSCAN DS    0H                                               * JGS *
         L     R2,CVTPTR           GET CVT ADDRESS              * JGS *
         SPACE 1                                                * JGS *
         USING CVT,R2              CVT ADDRESSABILITY           * JGS *
         SPACE 1                                                * JGS *
* CHECK FOR MVS (VS/1 HAS A SHORTER CVT)                        * JGS *
         TM    CVTDCB,CVTMVS2      MVS SYSTEM ?                 * JGS *
         BNO   UCBUEMUL            NO, NOT MVS, SO BRANCH       * JGS *
         SPACE 1                                                * JGS *
         L     R15,CVT+X'434'      ADDR (IOS UCB SCAN) (ANY)    * JGS *
*        L     R15,CVTRV628        ADDR (IOS UCB SCAN) (NO SP)  * JGS *
*        L     R15,CVTUCBSC        ADDR (IOS UCB SCAN) (SP & XA)* JGS *
*                                  = CVTUCBSC  DC  V(IOSVSUCB)  * JGS *
         SPACE 2                                                * JGS *
         LTR   R15,R15             BR IF THIS                   * JGS *
         BNZ   UCBUSP2               IS MVS / SP 1.2, 1.3, 2.XA * JGS *
         EJECT                                                  * JGS *
* UCB SCAN ROUTINE - EMULATION                                  * JGS *
         SPACE 1                                                * JGS *
UCBUEMUL DS    0H                                               * JGS *
         USING UCBDSECT,R3         UCB ADDRESSABILITY           * JGS *
         SPACE 1                                                * JGS *
         ICM   R4,B'1111',UPLWORK  BR IF THIS IS                * JGS *
         BNZ   UCBUBEGN              SECOND OR LATER CALL       * JGS *
*  SYMBOL CVTILK2 REPLACED BY CVT OFFSET TO AVOID ASSEMBLY      * JGS *
*        ERRORS (UNDEFINED SYMBOL) USING MVS/XA CVT MACRO.      * JGS *
         L     R4,CVT+X'028'       ADDR (UCB VECTOR TABLE) NO-XA* JGS *
*        L     R4,CVTILK2          ADDR (UCB VECTOR TABLE) NO-XA* JGS *
         BCTR  R4,0                BACK UP TO ZEROTH            * JGS *
         BCTR  R4,0                  UCB POINTER                * JGS *
UCBUBEGN LA    R15,4               ASSUME UCB WILL NOT BE FOUND * JGS *
         IC    R5,UPLCLASS         RE-FETCH DEVICE CLASS        * JGS *
         SR    R3,R3               ZERO UCB ADDRESS POINTER     * JGS *
         SPACE 1                                                * JGS *
UCBUNEXT LA    R4,2(,R4)           BUMP TO NEXT POINTER         * JGS *
         ICM   R3,B'0011',0(R4)    ADDR OF UCB                  * JGS *
         BP    UCBUEXAM            BR IF VALID UCB ADDRESS      * JGS *
         BZ    UCBUNEXT            BR IF NULL ENTRY             * JGS *
         TM    1(R4),X'FF'         EXIT IF END                  * JGS *
         BO    UCBUEND               OF VECTOR                  * JGS *
         SPACE 1                                                * JGS *
UCBUEXAM CLM   R5,B'0001',UCBTBYT3 BR IF THIS UCB               * JGS *
         BNE   UCBUNEXT              IS NOT DESIRED CLASS       * JGS *
         ST    R3,UPLUCB           PROVIDE ADDRESS OF UCB       * JGS *
         ST    R4,UPLWORK          REMEMBER WHERE TO RESUME     * JGS *
         SPACE 1                                                * JGS *
* CHECK FOR DUPLICATE CHANNEL PATH                              * JGS *
         SPACE 1                                                * JGS *
*  SYMBOL CVTILK2 REPLACED BY CVT OFFSET TO AVOID ASSEMBLY      * JGS *
*        ERRORS (UNDEFINED SYMBOL) USING MVS/XA CVT MACRO.      * JGS *
         L     R4,CVT+X'028'       ADDR (UCB VECTOR TABLE) NO-XA* JGS *
*        L     R4,CVTILK2          ADDR (UCB VECTOR TABLE) NO-XA* JGS *
UCBULOP2 ICM   R3,B'0011',0(R4)    ADDR OF NEXT UCB             * JGS *
         CL    R4,UPLWORK          EXIT IF NO DUPLICATE         * JGS *
         BE    UCBUSPXT              CHANNEL PATH FOUND         * JGS *
         LA    R4,2(,R4)           ADDR OF NEXT POINTER         * JGS *
         CL    R3,UPLUCB           CONTINUE LOOP IF THIS IS NOT * JGS *
         BNE   UCBULOP2              DUPLICATE CHANNEL PATH     * JGS *
         L     R4,UPLWORK          SETUP TO RESUME SCAN         * JGS *
         B     UCBUNEXT              AND GO RE-ENTER LOOP       * JGS *
         SPACE 1                                                * JGS *
UCBUSPXT SR    R15,R15             GIVE APPROPRIATE RETURN CODE * JGS *
         B     UCBUEND             EXIT                         * JGS *
         SPACE 1                                                * JGS *
         DROP  R2                  DROP => CVT                  * JGS *
         DROP  R3                  DROP => UCB                  * JGS *
         EJECT                                                  * JGS *
* UCB SCAN ROUTINE - INVOCATION OF THE MVS VERSION              * JGS *
         SPACE 1                                                * JGS *
UCBUSP2  DS    0H                                               * JGS *
         BALR  R14,R15             ENTER MVS UCB SCAN ROUTINE   * JGS *
         SPACE 3                                                * JGS *
* UCB SCAN ROUTINE - COMMON RETURN POINT                        * JGS *
         SPACE 1                                                **JRL**
* DECIDE IF THIS IS THE DESIRED UCB                             * JGS *
UCBUEND  DS    0H                                               * JGS *
         L     R3,8(,R1)           R3 = A(RETURNED UCB ADDRESS) * JGS *
         L     R3,0(,R3)           R3 => UCB                    * JGS *
         USING UCBDSECT,R3         UCB ADDRESSABILITY           * JGS *
         LTR   R15,R15             R15 = 0 => GOT A UCB         * JGS *
         BZ    UCBCHECK            YES, 0, SO BRANCH (VALIDATE) * JGS *
         B     UCBLAST             NO - OUT OF UCB'S            * JGS *
UCBCHECK DS    0H                  UCB VALIDATION               * JGS *
         CLC   UCBVOLI,0(R8)       DESIRED VOLID ?              * JGS *
         BE    UCBISIT             EQUAL => YES, SO BRANCH      * JGS *
         B     UCBUSCAN            NO, BRANCH, AND SCAN AGAIN   * JGS *
         SPACE 2                                                * JGS *
UCBLAST  DS    0H                  UCB NOT FOUND                * JGS *
         MVC   ADR994I+4+8(6),0(R8) MOVE IN VOLID               **JRL**
         LA    R2,4                SETUP FOR SHORT ENQUE/RESERVE**JRL**
         WTO   MF=(E,ADR994I)                                   **JRL**
         B     UENQRET                                          **JRL**
         SPACE 2                                                * JGS *
UCBISIT  DS    0H                  UCB FOUND & VALIDATED        * JGS *
         LA    R2,4                SETUP FOR SHORT ENQUE/RESERVE**JRL**
         SPACE 2                                                **JRL**
         TM    UCBSTAB,UCBBSTR     STORAGE VOLUME ?             **JRL**
         BZ    NOTSTOR             NO, NOT STORAGE, SO BRANCH   **JRL**
         MVC   ADR995I+4+8(6),0(R8) MOVE IN VOLID               **JRL**
         WTO   MF=(E,ADR995I)                                   **JRL**
         SLR   R2,R2               MAKE LONG ENQUE/RESERVE      **JRL**
         B     UENQRET                                          **JRL**
         SPACE 2                                                **JRL**
NOTSTOR  MVC   ADR995I+4+8(6),0(R8) MOVE IN VOLID               **JRL**
         MVC   ADR995I+4+15(7),=CL7'PRIVATE'                    **JRL**
         TM    UCBSTAB,UCBBPUB     PUBLIC VOLUME ?              * JGS *
         BZ    NOTPUB              NO, NOT PUBLIC, SO BRANCH    * JGS *
         MVC   ADR995I+4+15(7),=CL7' PUBLIC'                    * JGS *
         SPACE 2                                                * JGS *
NOTPUB   MVC   ADR995I+4+32(9),=CL9'NOT USED.'                  **JRL**
         WTO   MF=(E,ADR995I)                                   **JRL**
         B     UENQRET                                          **JRL**
         SPACE 2                                                **JRL**
         DROP  R3                  DROP => UCB                  * JGS *
         POP   USING               RESTORE USINGS               * JGS *
         SPACE 5                                                **JRL**
*  R2 SHOULD CONTAIN A VALID RETURN CODE (0 OR 4) AT THIS POINT * JGS *
         SPACE 1                                                * JGS *
UENQRET  FREEMAIN R,LV=UENQRLEN,A=(R11) FREEMAIN WORK AREA      **JRL**
         SPACE 1                                                **JRL**
         LR    R1,R13              SETUP FOR SAVEAREA FREEMAIN  **JRL**
         L     R13,4(,R13)         PRIOR SAVE AREA              **JRL**
         FREEMAIN R,LV=72,A=(1)    FREE OUR SAVEAREA            **JRL**
         SPACE 1                                                **JRL**
         LR    R15,R2              RETURN CODE                  **JRL**
         RETURN (14,12),T,RC=(15)  RETURN TO CALLER
         SPACE 2                                                **JRL**
         LTORG                                                  **JRL**
         EJECT                                                  **JRL**
* THIS IS THE WORK AREA FOR PRESERVING REENTRANCY.              **JRL**
* THIS DATA IS ACTUALLY COPIED TO A GETMAINED AREA DURING EXEC. **JRL**
* WHICH IS ADDRESSED BY R11                                     **JRL**
         SPACE 1                                                **JRL**
UENQRSTR DC    0D'0',C'UENQRSTR'   BOUNDARY ALIGNMENT AND ID    **JRL**
         SPACE 1                                                **JRL**
ADR994I  WTO   'ADR994I VOLSER NOT MOUNTED, RESERVE NOT USED.', **JRL**-
               MF=L                                             **JRL**
         SPACE 1                                                **JRL**
* ALTERNATE WORDS==>           PRIVATE          NOT USED.       **JRL**
*                               PUBLIC          NOT USED.       * JGS *
ADR995I  WTO   'ADR995I VOLSER STORAGE, RESERVE USED.    ',     **JRL**-
               MF=L                                             **JRL**
         SPACE 2
UPLDSECT DC    0D'0'               UCB PARAMETER LIST DSECT     * JGS *
*                                  (NEED DOUBLEWORD BOUNDARY)   * JGS *
         SPACE 1                                                * JGS *
UPLWRKAD DS    A(UPLWORK)          POINTER TO UPL WORK AREA     * JGS *
UPLCLSAD DS    A(UPLCLASS)         POINTER TO DEVICE CLASS      * JGS *
UPLUCBAD DS    A(UPLUCB)           POINTER TO UCB ADDRESS       * JGS *
UPLUCB   DS    F                   UCB ADDRESS                  * JGS *
UPLWORK  DS    CL100               WORK AREA (DBLWORD BOUNDARY) * JGS *
UPLSAVE  DS    18F                 SAVE AREA                    * JGS *
UPLCLASS DS    X'00'               DEVICE CLASS                 * JGS *
UPLSIZE  EQU   *-UPLDSECT          LENGTH OF UPL                * JGS *
         SPACE 2                                                * JGS *
         DC    CL8'UENQREND'                                    **JRL**
UENQREND DC    0D'0'               END OF MODIFIABLE AREA       **JRL**
UENQRLEN EQU   UENQREND-UENQRSTR   LENGTH OF WORK AREA          **JRL**
         SPACE 3
         DROP  ,                   DROP ALL PREVIOUS BASE REGISTERS
         SPACE 1
*  THIS IS THE LOGICAL END OF JGSUENQ/ADRUENQ - USER ENQUE ROUTINE
         TITLE 'JOHN GRADY SULLIVAN - ADRUPSWD - AUTHORIZATION ROUTINE'
ADRUPSWD CSECT
         SPACE 1
ADRUPSWD AMODE 31                                               * JGS *
ADRUPSWD RMODE ANY                                              * JGS *
         SPACE 1
         ENTRY JGSUPSWD            CREATE AN EXTERNAL SYMBOL ENTRY
JGSUPSWD EQU   *                   DEFINE THE SYMBOL IN THE PROGRAM
         SPACE 1
*
*  JOHN GRADY SULLIVAN - JUNE, 1980
*
*  FUNCTION :
*    REDUCE OR ELIMINATE OPERATOR PASSWORD PROMPTING WHILE RUNNING
*      DF/DSS
*
*                                                               * JGS *
*  INPUT :
*    REGISTER 1 POINTS TO A FOUR WORD PARAMETER LIST            * JGS *
*    (ADRUPB DSECT BASED OFF OF REGISTER R10) :                 * JGS *
*      UPDSCBAD  :                                              * JGS *
*      PARAMETER 1 -> FORMAT 1 DSCB FOR THE PASSWORD PROTECTED  * JGS *
*        OR VSAM DATASET, OR 0 FOR VOLUME LEVEL ENTRY           * JGS *
*      UPCATP    :                                              * JGS *
*      PARAMETER 2 -> CATALOG NAME, OR 0 FOR VOLUME LEVEL ENTRY * JGS *
*      UPUCBVSA  :                                              * JGS *
*      PARAMETER 3 -> VOLUME SERIAL FIELD IN THE UCB            * JGS *
*      PARAMETER 4 =  FLAGS (NOT A POINTER TO FLAGS) INDICATING * JGS *
*                       THE OPERATION IN PROGRESS               * JGS *
*      UPFLG1    :                                              * JGS *
*        BYTE 1 : FLAGS            UPFLG1 = 12(R10)             * JGS *
*          BIT 0 : END OF PARAMETER LIST (ALWAYS 1)             * JGS *
*          BIT 1 : 1 => SOURCE VOLUME, 0 => TARGET VOLUME       * JGS *
*          BIT 2 : 1 => DUMP OPERATION                          **JRL**
*          BIT 3 : 1 => RESTORE OPERATION                       **JRL**
*          BIT 4 : 1 => COPY OPERATION                          **JRL**
*          BIT 5 : 1 => PRINT OPERATION                         **JRL**
*          BIT 6 : 1 => DEFRAG OPERATION                        * JGS *
*          BIT 7 : 1 => COMPRESS OPERATION                      * JGS *
*      UPFLG2    :                                              * JGS *
*        BYTE 2 : FLAGS            UPFLG2 = 13(R10)             **JRL**
*          BIT 0 : 1 => DATASET-BY-NAME OPERATION               **JRL**
*          BIT 1 : 1 => FULL VOLUME OPERATION                   **JRL**
*          BIT 2 : 1 => PARTIAL (BY TRACKS) VOLUME OPERATION    * JGS *
*          BIT 3 : 1 => RELEASE OPERATION                       * JGS *
*          BIT 4 - 7 :   RESERVED                               * JGS *
*      UPFLG3    :                                              * JGS *
*        BYTE 3 : FLAGS            UPFLG3 = 14(R10)             * JGS *
*          BIT 1 : 1 => VOLUME ENTRY, 0 => DATASET ENTRY        * JGS *
*          BIT 2 : 1 => VSAM DATASET IN AN ICF CATALOG          * JGS *
*          BIT 3 - 7 :   RESERVED                               * JGS *
*        BYTE 4 :        RESERVED  UPFLG4 = 15(R10)             * JGS *
*
*  OUTPUT :
*    FOR THE SOURCE (FROM) VOLUME, SET REG 15 = 0 TO BYPASS
*      PASSWORD CHECKING.
*    FOR THE TARGET (TO) VOLUME, ASK THE OPERATOR WHETHER THIS
*      OPERATION SHOULD CONTINUE (U), OR TERMINATE (T).
*
         EJECT
*
*    RETURN CODE   MEANING
*    DEC   HEX
*      0 =  0  =>  BYPASS PASSWORD CHECKS, DO NOT RETURN TO THIS EXIT
*      4 =  4  =>  BYPASS PASSWORD CHECKS THIS DS, RETURN FOR NEXT DS
*      8 =  8  =>  DO PASSWORD CHECKING THIS DS, RETURN NEXT DS
*     12 =  C  =>  DO PASSWORD CHECKING THIS VOLUME, DO NOT RETURN
*     16 = 10  =>  DO NOT PROCESS THIS DS, RETURN FOR NEXT DS   * JGS *
*     20 = 14  =>  TERMINATE THIS COPY, DUMP, OR RESTORE OPERATION
*
*  REGISTER USAGE :
*        R0    SYSTEM
*        R1    INPUT - POINTER TO THE PARAMETER LIST / WORK
*        R2    WORK / RETURN CODE MANIPULATION
*        R3    WORK
*        R4    WORK / UCB POINTER
*        R5    WORK
*        R6    PARAMETER 1 -> FORMAT 1 DSCB (OR 0 => VOLUME ENTRY)
*        R7    PARAMETER 2 -> CATALOG NAME  (OR 0 => VOLUME ENTRY)
*        R8    PARAMETER 3 -> VOLUME SERIAL NUMBER IN UCB
*        R9
*        R10   BASE REGISTER FOR ADRUPB (PARAMETER LIST & FLAGS)* JGS *
*        R11   BASE REGISTER FOR WORK AREA TO MAINTAIN REENTRANCY.
*        R12   BASE REGISTER FOR PROGRAM
*        R13   SYSTEM - SAVE AREA CHAIN
*        R14   RETURN ADDRESS
*        R15   WORK, SYSTEM, AND RETURN CODE
         SPACE 3
**       XC    0(4,15),0(15)       CAUSE AN ABEND S0C1 ON SECOND PASS
**       SR    15,15
**       BR    14                  AND RETURN
         SPACE 1
         SAVE (14,12),,ADRUPSWD_JOHN_GRADY_SULLIVAN_&SYSDATE.
         SPACE 1
         LA    12,0(,15)           CLEAR HIGH ORDER BIT/BYTE
         USING ADRUPSWD,12         ESTABLISH ADDRESSABILITY
         SPACE 1
         LA    R10,0(,R1)          -> INPUT PARAMETER LIST      * JGS *
         USING ADRUPB,R10          ESTABLISH ADDRESSABILITY     * JGS *
         EJECT
         GETMAIN R,LV=72           GETMAIN A NEW SAVE AREA
         SPACE 1
         ST    13,4(1)             BACKCHAIN TO PREVIOUS SAVE AREA
         ST    1,8(13)             FORWARD CHAIN PREVIOUS SAVE AREA
         LR    13,1                MAKE OUR SAVE AREA CURRENT
         SPACE 2
*  TO KEEP OUR PROGRAM REENTRANT, ONE MUST OBTAIN AND
*   INITIALIZE A WORK AREA EXTERNAL TO OUR PROGRAM
*   FOR THINGS WHICH MAY BE MODIFIED DURING EXECUTION
*   (SUCH AS SVC PARAMETER LISTS, RETURN CODE SAVE AREAS, ETC.).
         SPACE 1
         GETMAIN   R,LV=PSWDRLEN   GETMAIN FOR MODIFIABLE AREA
         SPACE 1
*  SET UP TO INITIALIZE OUR NEWLY FOUND WORK AREA
         LR    R2,R1               TARGET ADDRESS
         LA    R3,PSWDRLEN         TARGET LENGTH
         LA    R4,PSWDRSTR         SOURCE ADDRESS
         LR    R5,R3               SOURCE LENGTH
         SPACE 1
         MVCL  R2,R4               INITIALIZE MODIFIABLE AREA
         SPACE 1
         LA    R11,0(,R1)          SAVE THE ADDRESS TO OUR AREA
         USING PSWDRSTR,R11        ESTABLISH ADDRESSABILITY
         SPACE 3
*  R10 -> ADRUPB (THE INPUT PARAMETER LIST).                    * JGS *
* * *    GET THE PARAMETER ADDRESSES FROM THE PRIOR OS SAVE AREA.
* * *    L     R1,4(,R13)          R1 = ADDRESS OF PRIOR SAVE AREA
* * *    L     R1,24(,R1)          GET ADDRESS OF PARAMETER LIST IN R1
* * *    USING ADRUPB,R1           ESTABLISH ADDRESSABILITY     * JGS *
         L     R6,UPDSCBAD         R6 -> FORMAT 1 DSCB / 0      * JGS *
         L     R7,UPCATP           R7 -> CATALOG NAME / 0       * JGS *
         L     R8,UPUCBVSA         R8 -> VOLUME SERIAL IN UCB   * JGS *
* * *    LA    R9,UPFLG1           R9 -> FLAGS, NOT HOLD FLAGS  * JGS *
* * *    USING UPFLG1,R9           ESTABLISH ADDRESSABILITY     * JGS *
* * *    DROP  R1                  DROP => ADRUPB PARAMETER LIST* JGS *
* * *    LM    R6,R8,0(R1)         GET THE ADDRESSES TO PARAMETERS
* * *    LA    R9,12(,R1)          R9 -> FLAGS, NOT HOLD FLAGS  * JGS *
         SPACE 3
         MVI   PSWDRCU,X'00'       RC = 0, SUPPRESS PASSWORD PROMPTING
         EJECT
PSWDPREP EQU   *                   SET UP FOR PROMPTING THE OPERATOR
         SPACE 1
*  INDICATE WHICH OPERATION IS BEING PERFORMED IN THE PARAMETER
*   MESSAGE ('OP = ???' IS THE DEFAULT, AND IS ALREADY IN THE MESSAGE).
         SPACE 1
         TM    UPFLG1,UPDUMP       IS THIS A DUMP ?             * JGS *
         BZ    PSWDNDMP            NO, NOT DUMP, SO BRANCH      * JGS *
         MVC   ADR992IO(8),=CL8'DUMP.   '        OVERLAYS 'OP = ???'
PSWDNDMP EQU   *
         SPACE 1
         TM    UPFLG1,UPREST       IS THIS A RESTORE ?          * JGS *
         BZ    PSWDNRST            NO, NOT RESTORE, SO BRANCH   * JGS *
         MVC   ADR992IO(8),=CL8'RESTORE.'        OVERLAYS 'OP = ???'
PSWDNRST EQU   *
         SPACE 1
         TM    UPFLG1,UPCOPY       IS THIS A COPY ?             * JGS *
         BZ    PSWDNCPY            NO, NOT COPY, SO BRANCH      * JGS *
         MVC   ADR992IO(8),=CL8'COPY.   '        OVERLAYS 'OP = ???'
PSWDNCPY EQU   *
         SPACE 1
         TM    UPFLG1,UPPRINT      IS THIS A PRINT ?            * JGS *
         BZ    PSWDNPRT            NO, NOT PRINT, SO BRANCH     * JGS *
         MVC   ADR992IO(8),=CL8'PRINT.  '        OVERLAYS 'OP = ???'
PSWDNPRT EQU   *
         SPACE 1
         TM    UPFLG1,UPDEFRAG     IS THIS A DEFRAG ?           * JGS *
         BZ    PSWDNDFR            NO, NOT DEFRAG, SO BRANCH    * JGS *
         MVC   ADR992IO(8),=CL8'DEFRAG. '        OVERLAYS 'OP = ???'
PSWDNDFR EQU   *
         SPACE 1
         TM    UPFLG1,UPCOMPR      IS THIS A COMPRESS ?         * JGS *
         BZ    PSWDNCMP            NO, NOT COMPRESS, SO BRANCH  * JGS *
         MVC   ADR992IO(8),=CL8'COMPRESS'        OVERLAYS 'OP = ???'
PSWDNCMP EQU   *
         SPACE 1
         TM    UPFLG2,UPRLSE       IS THIS A RELEASE?           * JGS *
         BZ    PSWDNRLS            NO, NOT RELEASE, SO BRANCH   * JGS *
         MVC   ADR992IO(8),=CL8'RELEASE.'        OVERLAYS 'OP = ???'
PSWDNRLS EQU   *
         EJECT ,
         L     R2,CVTPTR           POINT TO THE CVT             * JGS *
         USING CVT,R2              ADDRESSABILITY               * JGS *
         L     R2,CVTTCBP          R2 -> A(NEXT & CURRENT TCBS) * JGS *
         L     R2,4(,R2)           R2 -> OUR TCB                * JGS *
         DROP  R2                  UNADDRESSABILITY             * JGS *
         USING TCB,R2              ADDRESSABILITY               * JGS *
         L     R2,TCBTIO           R2 -> TIOT (JOBNAME)         * JGS *
         DROP  R2                  UNADDRESSABILITY             * JGS *
         MVC   ADR991DJ(8),0(R2)   JOBNAME  TO WTOR (OPER PROMPT)
         MVC   ADR991DS(8),8(R2)   STEPNAME TO WTOR (OPER PROMPT)
         SPACE 1
         MVC   ADR992IJ(8),0(R2)   JOBNAME  TO WTO  (PARAMETERS)
         MVC   ADR992IS(8),8(R2)   STEPNAME TO WTO  (PARAMETERS)
         SPACE 1
         LA    R4,0(,R8)                       R4 -> VOLSER IN UCB
         S     R4,=F'28'                       R4 -> BEGINNING OF UCB
         USING UCBDSECT,R4                     ADDRESSABILITY   * JGS *
         SPACE 1
         MVC   ADR991DU(3),UCBNAME             UCB CUU TO WTOR
         MVC   ADR991DV(6),UCBVOLI             UCB VOLSER TO WTOR
         MVC   ADR991DX(58),=CL58'TARGET VOLUME = VOLSER (PARTIAL VOLUM*
               E). (U,T, ?).'                                   * JGS *
         MVC   ADR991DX+16(6),UCBVOLI          UCB VOLSER TO WTOR
         MVC   ADR992IV+7(6),UCBVOLI           UCB VOLSER TO WTO* JGS *
         SPACE 1
         DROP  R4                  DROP => UCB                  * JGS *
         EJECT
*  IF THIS IS THE FROM DEVICE, EXIT AND SUPPRESS PASSWORD PROMPTING
*  UNLESS IT IS OUR TESTING STEPNAME
         TM    UPFLG1,UPFRTO       0 => TARGET (TO) DEVICE ?    * JGS *
         BZ    PSWDNSRC            YES, 0 => TARGET, SO BRANCH  * JGS *
         SPACE 1
*  LOOK FOR OUR TESTING JOBNAME / STEPNAME COMBINATION          * JGS *
*        CLC   ADR991DJ(8),=CL8'JGSTDSS '   TEST - JOBNAME      * JGS *
*        BNE   PSWDRET             NO - BRANCH, SKIP PW PROMPT  * JGS *
         CLC   ADR991DS(8),=CL8'TESTDSS '   TEST - STEPNAME     * JGS *
         BNE   PSWDRET             NO - BRANCH, SKIP PW PROMPT  * JGS *
         SPACE 1
         MVC   ADR991DX(6),=CL6'SOURCE'                         * JGS *
         MVC   ADR992IW(5),=CL5' READ'                          * JGS *
         NOP   PSWDRET             NOP RETURN (COULD ZAP TO BR) * JGS *
PSWDNSRC EQU   *                                                * JGS *
         SPACE 2
         TM    UPFLG2,UPDSN        1 => DATASET-BY-NAME OPER ?  **JRL**
         BZ    PSWDNDSN            0 => NO, NOT DS-BY-NAME, SO BRANCH
         SPACE 1
*  DATASET BY NAME OPERATION -
*    SET RETURN CODE AND PROMPT OPERATOR ACCORDINGLY
         SPACE 1
*  RETURN CODE X'08' FOR THE VOLUME ENTRY WILL CAUSE THE EXIT   * JGS *
*              TO BE INVOKED AT THE DATASET LEVEL               * JGS *
         MVI   PSWDRCU,X'08'       RETURN CODE IF OPER SAYS 'U' * JGS *
         TM    UPFLG3,UPIND        1 => VOLUME LEVEL ENTRY    ? * JGS *
         BNZ   PSWDNVDS            1 => YES, VOLUME ENTRY, SO BR* JGS *
         LTR   R6,R6               R6 = 0 => NO DSN VOLUME ENTRY* JGS *
         BZ    PSWDNVDS            0 => YES, NO DSN, SO BRANCH  * JGS *
         SPACE 1
*  RETURN CODE X'04' WILL BYPASS OPERATOR PROMPT ONLY FOR THIS DATASET
         MVI   PSWDRCU,X'04'       RETURN CODE IF OPER SAYS 'U'
         SPACE 1
PSWDNVDS EQU   *
         SPACE 1
*        ADD TEXT TO 'TARGET VOLUME = VOLSER.' (FROM ABOVE)     * JGS *
         MVC   ADR991DX+23(17),=CL24'(BY DSNAME).    '          * JGS *
         MVC   ADR992ID(24),=CL24'(BY DSNAME).    '             * JGS *
         SPACE 1
PSWDNDSN EQU   *
         EJECT
*  NOT DATASET BY NAME - FULL OR PARTIAL VOLUME OPERATION
         SPACE 1
*        ADD TEXT TO 'TARGET VOLUME = VOLSER.' (FROM ABOVE)     * JGS *
         TM    UPFLG2,UPFRTO       1 => FULL VOLUME             * JGS *
         BZ    PSWDNFVO            0 => NO, NOT FULL, SO BRANCH * JGS *
         MVI   PSWDRCU,X'00'       RETURN CODE IF OPER SAYS 'U'
         MVC   ADR991DX+23(17),=CL24'(FULL VOLUME).'            * JGS *
         MVC   ADR992ID(24),=CL24'(FULL VOLUME).'               * JGS *
         MVC   ADR992IV+14(24),=CL24'(FULL VOLUME).'            * JGS *
PSWDNFVO EQU   *
         SPACE 2
         TM    UPFLG2,UPPART       1 => PARTIAL VOLUME          * JGS *
         BZ    PSWDNPVO            0 => NO, NOT PARTIAL, SO BR  * JGS *
         MVI   PSWDRCU,X'00'       RETURN CODE IF OPER SAYS 'U'
         MVC   ADR991DX+23(17),=CL24'(PARTIAL VOLUME).'         * JGS *
         MVC   ADR992ID(24),=CL24'(PARTIAL VOLUME).'            * JGS *
         MVC   ADR992IV+14(24),=CL24'(PARTIAL VOLUME).'         * JGS *
PSWDNPVO EQU   *                                                * JGS *
         SPACE 2
         EJECT
*  VOLUME OR DATA SET LEVEL ENTRY ?                             * JGS *
         SPACE 1
         MVC   ADR991DV+08(19),=CL19'ACCESS REQUESTED - '       * JGS *
         MVC   ADR992IE,=CL13'VOLUME ENTRY.'                    * JGS *
         SPACE 1
         TM    UPFLG3,UPIND        1 => VOLUME LEVEL ENTRY    ? * JGS *
         BNZ   PSWDNVLE            1 => YES, VOLUME ENTRY, SO BR* JGS *
         LTR   R6,R6               R6 = 0 => NO DSN VOLUME ENTRY* JGS *
         BZ    PSWDNVLE            0 => YES, NO DSN, SO BRANCH  * JGS *
         SPACE 1                                                * JGS *
         MVC   ADR992IE,=CL13'DSNAME ENTRY.'                    * JGS *
         MVC   ADR991DX(4),=CL4'DSN='                           * JGS *
         MVC   ADR991DX+4(44),0(R6)    DSNAME TO WTOR           * JGS *
         MVC   ADR991DX+4+44(2),=CL2'  '  BLANK ').' IN WTOR    * JGS *
         MVC   ADR992IV(4),=CL4'DSN='                           * JGS *
         MVC   ADR992IV+4(44),0(R6)    DSNAME TO WTO            * JGS *
PSWDNVLE EQU   *                                                * JGS *
         SPACE 3
*  CATALOG NAME PRESENT ?                                       * JGS *
         SPACE 1
         LTR   R7,R7               R7 = 0 => NO CAT NAME PASSED * JGS *
         BZ    PSWDNCAT            0 => YES, NO CAT, SO BRANCH  * JGS *
         SPACE 1                                                * JGS *
         MVC   ADR992IC(4),=CL4'VSAM'                           * JGS *
         TM    UPFLG3,UPVSMDAT     1 => VSAM DS IN ICF CAT    ? * JGS *
         BZ    PSWDNICF            0 => NO, NOT ICF/VSAM, SO BR * JGS *
         MVC   ADR992IC(4),=CL4' ICF'                           * JGS *
PSWDNICF EQU   *                                                * JGS *
         MVC   ADR992IC+4(44),0(R7)    CATALOG NAME TO WTO      * JGS *
         B     PSWDECAT            BRANCH AROUND ALTERNATE CODE * JGS *
         SPACE 1
PSWDNCAT EQU   *                                                * JGS *
         SPACE 2
*  NO CATALOG NAME PRESENT - END MLWTO WITHOUT CATALOG NAME     * JGS *
         MVC   ADR992IC-4(4),ADR992IL SHIFT END FLAGS OF MLWTO  * JGS *
         SPACE 1
PSWDECAT EQU   *                                                * JGS *
         SPACE 3
         EJECT
PSWDPRMT XC    PSWDECB,PSWDECB     CLEAR THE ECB TO AWAIT THE REPLY
         SPACE 1
         WTOR  ,PSWDRPLY,,PSWDECB,MF=(E,ADR991D)   ASK THE OPER
         SPACE 1
         WAIT  ECB=PSWDECB         WAIT FOR A REPLY
         SPACE 1
         L     R1,PSWDMGID         GET WTO MESSAGE ID TO DOM
         LTR   R1,R1               R1 = 0 => NO MESSAGE TO DOM
         BZ    PSWDNDOM            0, SO BRANCH, NO MESSAGE TO DOM
         SPACE 1
         DOM   MSG=(1)             DOM - DELETE MESSAGE ADR992I
         SPACE 1
PSWDNDOM OI    PSWDRPLY,C' '       MAKE REPLY UPPER CASE
         CLI   PSWDRPLY,C'U'       U (CONTINUE) ?
         BE    PSWDU               YES, U, SO BRANCH
         CLI   PSWDRPLY,C'T'       T (TERMINATE) ?
         BE    PSWDT               YES, T, SO BRANCH
         CLI   PSWDRPLY,C'Y'       Y (CONTINUE) ?               * JGS *
         BE    PSWDU               YES, Y, SO BRANCH            * JGS *
         CLI   PSWDRPLY,C'N'       N (TERMINATE) ?              * JGS *
         BE    PSWDT               YES, N, SO BRANCH            * JGS *
         CLI   PSWDRPLY,C'D'       D (DEBUG 0C1 DUMP) ?         * JGS *
         BE    PSWDD               YES, D, SO BRANCH            * JGS *
         CLI   PSWDRPLY,C'0'       0 (DEBUG RETURN CODE) ?      * JGS *
         BNL   PSWD0               YES, 0-9, SO BRANCH          * JGS *
         SPACE 1
*  OPERATOR GAVE AN INVALID RESPONSE -                          * JGS *
*    OPERATOR WANTS TO KNOW WHAT OPERATION IS BEING PERFORMED   * JGS *
*    OR MAYBE JUST MADE A MISTAKE IN THE REPLY.                 * JGS *
         SPACE 1
         SLR   R0,R0               ZERO R0 FOR MLWTO            * JGS *
         WTO   MF=(E,ADR992I)      TELL OPERATOR - REPLY IN ERROR
         SPACE 1
         ST    R1,PSWDMGID         SAVE WTO MESSAGE ID FOR DOM
         SPACE 1
         B     PSWDPRMT            ASK OPERATOR AGAIN
         EJECT
PSWDU    B     PSWDRET             GO EXIT - RETURN CODE IS ALREADY SET
         SPACE 2
PSWDT    MVI   PSWDRCU,X'14'       TERMINATE THIS OPERATION
         B     PSWDRET             AND GO EXIT
         SPACE 2
PSWDD    DC    H'0'                ABEND 0C1 (FOR DUMP)         * JGS *
         DC    CL10'ABEND 0C1 '    IDENTIFY NON-ACCIDENTAL DUMP * JGS *
         DC    0H'0'               BOUNDARY ALIGNMENT           * JGS *
         SPACE 2
PSWD0    IC    R2,PSWDRPLY         PUT RESPONSE IN R2           * JGS *
         SLL   R2,2                MULTIPLY BY 4                * JGS *
         N     R2,=F'63'           TURN OFF EXTRANEOUS BITS     * JGS *
         STC   R2,PSWDRCU          SAVE RETURN CODE             * JGS *
         B     PSWDRET             AND GO EXIT                  * JGS *
         SPACE 5
PSWDRET  SR    R2,R2               CLEAR R2
         IC    R2,PSWDRCU          PICK UP RETURN CODE FROM WORKAREA
*  SAVE OUR RETURN CODE IN R2 SINCE FREEMAIN GIVES A RETURN CODE IN R15
         SPACE 1
         FREEMAIN  R,LV=PSWDRLEN,A=(R11)   FREEMAIN WORK AREA
         SPACE 1
         LR    R1,R13              SET UP FOR FREEMAIN OF SAVE AREA
         L     R13,4(,R13)         POINT TO PRIOR (CALLER'S) SAVE AREA
         FREEMAIN  R,LV=72,A=(1)   ISSUE FREEMAIN OF OUR SAVE AREA
         SPACE 3
         LR    R15,R2              RELOAD R15 AFTER FREEMAIN
         RETURN (14,12),T,RC=(15)  RETURN TO CALLER
         EJECT
         LTORG
         EJECT
*  THIS IS THE WORK AREA FOR PRESERVING REENTRANCY.
*  THIS DATA IS ACTUALLY COPIED TO A GETMAINED AREA DURING EXECUTION,
*   WHICH IS ADDRESSED BY R11.
         SPACE 1
PSWDRSTR DC    0D'0',C'PSWDRSTR'   BOUNDARY ALIGNMENT AND ID
         SPACE 1
PSWDMGID DC    F'0'                ADR992I WTO MESSAGE ID FOR DOM
PSWDECB  DC    F'0'                ECB FOR WAIT ON WTOR
PSWDRCU  DC    F'0'                DEFAULT RETURN CODE IF OPER SAYS 'U'
PSWDRPLY DC    CL10'  PSWDRPLY'    WILL SHOW UP IN DUMP
         SPACE 2
         PUSH  PRINT                                            * JGS *
         PRINT ON,GEN,DATA         FOR CALCULATING OFFSETS      * JGS *
         SPACE 1
*              0        10        20        30        40        50
*              01234567890123456789012345678901234567890123456789012345
*                 60        70        80        90       100       110
*              67890123456789012345678901234567890123456789012345678901
*                    120
*              23456789012         MAXIMUM OF 122 CHARACTERS IN WTOR
         SPACE 2
* ALTERNATE WORDS ==> 'TARGET VOLUME = VOLSER.'  OVERLAY DSN=...* JGS *
ADR991D  WTOR  'ADR991D JOBNAME., STEPNAME, CUU, VOLSER. ACCESS REQUEST*
               ED - DSN=A2345678.B2345678.C2345678.D2345678.E2345678. (*
               U,T, ?)',                                               *
               ,1,,DESC=(2),ROUTCDE=(4),MF=L     REPLY LENGTH=1 * JGS *
         SPACE 2
ADR991DJ EQU   ADR991D+12+8,8,C'C'     JOBNAME                  * JGS *
ADR991DS EQU   ADR991D+12+18,8,C'C'    STEPNAME                 * JGS *
ADR991DU EQU   ADR991D+12+28,3,C'C'    UCB UNIT ADDRESS         * JGS *
ADR991DV EQU   ADR991D+12+33,6,C'C'    VOLSER. ACCESS REQUESTED * JGS *
ADR991DX EQU   ADR991D+12+60,58,C'C'   DSN= | TARGET VOL=...U,T)* JGS *
         SPACE 2
*DR992I  WTO   'ADR992I JOBNAME., STEPNAME, REPLY IN ERROR. REPLY WITH *
               "U" (USE) OR "T" (TERMINATE) - TARGET OF OP = ???',     *
               DESC=(2),ROUTCDE=(4),MF=L           COPY,PRINT,RESTORE.
ADR992I  WTO   ('ADR/JGS992I JOBNAME., STEPNAME, V.2.5 PARAMETERS ARE: *
               ',L),                                                   *
               ('REPLY WITH "U" (CONTINUE) OR "T" (TERMINATE) OP = ??? *
               ',L),                                                   *
               ('WRITE (PARTIAL VOLUME). VOLUME ENTRY.                 *
               ',D),                                                   *
               ('VOLUME=VOLSER (PARTIAL VOLUME).                       *
               ',D),                                                   *
               ('VSAM CAT= (NONE).                                     *
               ',D),                                                   *
               ('THIS ENDING LINE WILL NOT APPEAR IN THE MLWTO.        *
               ',E),                                                   *
               DESC=(2),ROUTCDE=(4),MF=L                        * JGS *
         SPACE 2
ADR992IJ EQU   ADR992I+4+012,8,C'C'   JOBNAME                   * JGS *
ADR992IS EQU   ADR992I+4+022,8,C'C'   STEPNAME                  * JGS *
ADR992IR EQU   ADR992I+4+034,3,C'C'   VERSION-RELEASE-MOD LEVEL * JGS *
ADR992IO EQU   ADR992I+4+111,8,C'C'   OPERATION                 * JGS *
ADR992IW EQU   ADR992I+4+124,5,C'C'   READ | WRITE              * JGS *
ADR992ID EQU   ADR992I+4+130,18,C'C'  PARTIAL | FULL | BY DSNAME* JGS *
ADR992IE EQU   ADR992I+4+148,13,C'C'  ENTRY (VOLUME | DSNAME)   * JGS *
ADR992IV EQU   ADR992I+4+182,48,C'C'  VOLUME=... | DSN=...      * JGS *
ADR992IC EQU   ADR992I+4+240,53,C'C'  VSAM CAT=...              * JGS *
ADR992IL EQU   ADR992I+4+294,53,C'C'  END LINE LEN + LINE TYPE  * JGS *
         SPACE 1
         POP   PRINT                                            * JGS *
         SPACE 2
         DC    CL8'PSWDREND'
PSWDREND DC    0D'0'               END OF MODIFIABLE AREA
PSWDRLEN EQU   PSWDREND-PSWDRSTR   LENGTH OF WORK AREA
         SPACE 2
         DROP  ,                   DROP ALL PREVIOUS BASE REGISTERS
         SPACE 2
*  THIS IS THE LOGICAL END OF JGSUPSWD/ADRUPSWD
*        - USER AUTHORIZATION ROUTINE
         SPACE 3
         IEZBITS                   DEFINE BITS TO TEST
         SPACE 6
         ADRUPB                    AUTHORIZATION EXIT PARM LIST * JGS *
         SPACE 3
         ADRUNQB                   ENQUE EXIT PARAMETER LIST    * JGS *
         EJECT
         SPACE 3                                                * JGS *
         IHACDE ,                                               * JGS *
         SPACE 3                                                * JGS *
         IKJTCB ,                                               * JGS *
         SPACE 3                                                * JGS *
         PUSH  PRINT                                            * JGS *
         PRINT NOGEN                                            * JGS *
UCBDSECT DSECT                                                  **JRL**
         SPACE 1                                                **JRL**
         IEFUCBOB                                               **JRL**
         POP   PRINT                                            * JGS *
         SPACE 5
         CVT   DSECT=YES                                        **JRL**
         SPACE 5
         END   FASTDSS,(JOHN.GRADY,.SUL,LIVAN)                  * JGS *
/*
//LKED.SYSLMOD   DD  DISP=SHR,DSN=SYS1.LINKLIB,
//             UNIT=3390,VOL=SER=INDIAN,SPACE=
//LKED.SYSPRINT  DD  SYSOUT=*
//LKED.SYSLIB    DD  DISP=SHR,DSN=SYS1.LINKLIB,
//             UNIT=3390,VOL=SER=INDIAN
//LKED.SYSIN     DD  *
  INCLUDE  SYSLIB(ADRDSSU)
  ENTRY    FASTDSS
  ORDER    FASTDSS(P)
  SETCODE  AC(1)
  NAME     FASTDSS(R)
/*
//TESTDSS    EXEC  PGM=FASTDSS,REGION=2000K
//TESTDSS.SYSPRINT DD SYSOUT=*
//SYSUDUMP   DD  SYSOUT=*
//STEPLIB    DD  DISP=(SHR,PASS),DSN=*.S1.LKED.SYSLMOD,
//             VOL=REF=*.S1.LKED.SYSLMOD
//DISK       DD  DISP=SHR,DSN=SYS1.NUCLEUS,
//             UNIT=3390,VOL=SER=INDIAN
//OUTDD      DD  UNIT=SYSDA,SPACE=(CYL,(2,2))   NO VIO FOR DF/DSS V2
//SYSIN      DD  *
  DUMP     INDD(DISK) OUTDD(OUTDD) TRACKS( (0,0,0,0) )
  DUMP     INDD(DISK) OUTDD(OUTDD)     -
           DATASET( INCLUDE(SYS1.BLGMAC2) BY((DSORG Â¬= VSAM)) )
  COMPRESS DDN(DISK)     -
                    INCLUDE(SYS1.BLGMAC2) BY((DSORG Â¬= VSAM))
  PRINT    INDD(DISK) TRACKS( (0,0,0,0) )
/*
