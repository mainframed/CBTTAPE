DIST     TITLE 'D I S T  ***  RETRIEVE SOURCE FROM DISTRIBUTION TAPES'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1986  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2002  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
*                                                                     *
*        THE FUNCTION OF THIS PROGRAM, AND SOME OF THE DETAILS WERE   *
*        SUGGESTED BY THE PL/I PROGRAM NAMED DIST WRITTEN BY SEYMOUR  *
*        J. METZ (AT PINKERTON COMPUTER CONSULTANTS)                  *
*                                                                     *
***********************************************************************
         EJECT ,
***********************************************************************
*                                                                     *
*        DIST READS IBM SOURCE DISTRIBUTION FILES, AND EXTRACTS       *
*        REQUESTED MEMBERS FOR SUBSEQUENT PROCESSING.                 *
*        OUTPUT APPEARS ON SYSPRINT (REQUIRED) AND SYSUT2/SYSPUNCH    *
*        (OPTIONAL; DD DUMMY IF NOT WANTED).                          *
*        THE IBM INPUT TAPES ARE SUPPLIED ON SYSUT1 WITH DISP=PASS    *
*        FOR ANY ONE VALID FILE. ADDITIONAL FILES WILL BE READ BY     *
*        JFCB MODIFICATION.                                           *
*                                                                     *
*        SYSIN CONTROL CARDS:                                         *
*                                                                     *
*        DSN=DATASET NAME              REQUIRED FOR SL TAPES          *
*                                                                     *
*        VOLSER FILESEQ (MEMBER)                                      *
*              OTHER CARDS SPECIFY THE VOLUME SERIAL IN COLUMN 1      *
*              OR * FOR THE PREVIOUS VOLUME                           *
*              THE SECOND OPERAND SPECIFIES THE FILE NUMBER           *
*              THE OPTIONAL THIRD OPERAND SPECIFIES THE MEMBER TO BE  *
*              COPIED. OMISSION OF THE MEMBER NAME RESULTS IN A LIST  *
*              OF MEMBERS PASSED OVER.                                *
*                                                                     *
***********************************************************************
         SPACE 1
         PRINT &PRTSOR
DIST     SAVEM ZERO,BASE=R12,PARM=R9                    ADDED ON 86114
         SERVINIT ,          LOAD THE @SERVICE ROUTINE
         SERVCALL LPALD,=CL8'@INPREAD'  LOAD THE CARD READER
         ST    R0,@INPREAD
         SERVCALL LPALD,=CL8'@PRINTER'  AND THE PRINTER ROUTINES
         ST    R0,@PRINTER
         PRTOPEN SYSPRINT,OPT=(ABEND)  OPEN THE PRINTER         GP03027
         PRTV HEADER,TITLE=1   WRITE THE HEADER
         INPOPEN SYSIN,OPT=(ABEND)  OPEN THE CONTROL CARD FILE
         LA    R14,SYSUT1    POINT TO DDN
         LA    R15,VOL       POINT TO VOLSER
         LA    R0,DSN        POINT TO DSN
         LA    R1,SEQ+2      POINT TO FILE SEQUENCE
         STM   R14,R1,MODLIST  SKELETON MODJFCB LIST
         MVI   MODLIST,1     SET DDN PRESENT
         MVI   MODLIST+4,3   SET VOLSER PRESENT
         MVI   MODLIST+12,X'82'  SET SEQUENCE AND END OF LIST
*        NOTE OMISSION OF DSN (6) AT +8
         EJECT ,
***********************************************************************
*                                                                     *
*        GET NEXT CONTROL CARD AND ANALIZE IT.                        *
*              DSN= IS STORED; MEMBER/FILE REQUEST DONE IMEDIATELY    *
*                                                                     *
***********************************************************************
         SPACE 1
CONTLOOP INPGET ,            GET A CONTROL CARD
         BXH   R15,R15,CONTERR  SIGNAL ERROR ON INPUT
         LR    R7,R1         SAVE ADDRESS
         USING CARD,R7       REMEMBER IT
         PRTLIST CONCARD     LIST IT
         LA    R6,CARD       SET START OF SCAN
         LA    R5,CARD+71    SET END OF SCAN
         LA    R4,1          SET SCAN INCREMENT
         CLC   =C'DSN=',CARD  DSN CHANGE REQUEST ?
         BNE   NONEWDSN      NO
         MVC   DSN,CARD+4    SAVE NEW NAME
         MVI   MODLIST+8,6   SET DSN CHANGED
         B     CONTLOOP
         SPACE 1
NONEWDSN CLI   CARD,C' '     VALID VOLSER ?
         BNH   BADCARD       NO
         LR    R1,R6         SAVE STARTING POSITION
         BAL   R9,DOBLANK    FIND NEXT BLANK
         B     BADCARD       NONE ?
         LR    R15,R6        COPY CURRENT POSITION
         SR    R15,R1        LESS START
         BNP   BADCARD       HUH ?
         CH    R15,=Y(L'VOL)  NOT TOO LONG ?
         BH    BADCARD
         BCTR  R15,0         DECREMENT FOR EXECUTE
         MVC   VOL,BLANKS    PRE-CLEAR
         EX    R15,MOVEVOL   MOVE VOLSER
         XC    NUMCARDS,NUMCARDS  RESET CARD COUNTER
         BAL   R9,NOBLANK    GET NEXT NON-BLANK
         B     BADCARD       INVALID CARD - FILE REQUIRED
         MVC   SEQ,OLDSEQ    COPY OLD SEQUENCE
         CLC   =C'* ',0(R6)  SAME SEQUENCE ?
         BNE   LOOKNUM       NO
         LA    R6,1(,R6)
         B     LOOKNAME
LOOKNUM  BAL   R9,GETNUMB    EXPECT A NUMERIC
         B     BADCARD       FAIL IF NOT NUMERIC
         LTR   R0,R0         VALID FILE NUMBER ?
         BNP   BADCARD       NO
         C     R0,=F'32767'  VALID FILE NUMBER ?
         BH    BADCARD
         ST    R0,SEQ        SAVE FILE SEQUENCE
LOOKNAME MVC   NEWNAME,=8X'FF'  PRESET TO HIGH NAME
         BAL   R9,NOBLANK    LOOK FOR ANOTHER NON-BLANK
         B     NAMEEND       REQUEST TO SCAN ENTIRE FILE
         LA    R15,L'NAME    GET MEMBER NAME LENGTH
         LA    R14,NAME      AND ADDRESS
         MVC   NAME,BLANKS   CLEAR
NAMELOOP CLI   0(R6),C' '    VALID NAME ?
         BNH   NAMEEND       NO; END OF NAME
         MVC   0(1,R14),0(R6)  MOVE ONE BYTE
         AR    R14,R4        UP
         BXH   R6,R4,NAMEEND  SKIP IF CARD SCANNED
         BCT   R15,NAMELOOP  ELSE TRY AGAIN
         SPACE 2
***********************************************************************
*                                                                     *
*        REQUEST FOR A NEW MEMBER:                                    *
*        IF VOLUME OR FILE NUMBER HAS CHANGED, NEED TO CLOSE INPUT    *
*        FILE, CHANGE THE JFCB, AND OPEN.                             *
*                                                                     *
***********************************************************************
         SPACE 1
NAMEEND  CLC   =C'* ',VOL    SAME VOLUME ?
         BNE   *+10          NO
         MVC   VOL,OLDVOL    RE-USE PREVIOUS ONE
*        NOTE - IF NO VOLUME SPECIFIED, WILL USE JFCB'S
REOPEN   CLC   VOL,OLDVOL    SAME AS PRIOR VOLUME ?
         BNE   DATAOPEN      NO; NEED TO MODIFY
         CLC   SEQ,OLDSEQ    SAME FILE SEQUENCE ?
         BE    LEAVOPEN      YES; CHECK IF NEED TO REREAD
DATAOPEN TM    FLAGS,FOPEN   IS DATA FILE OPEN ?
         BZ    DACLOSED      NO; NO HASSLE
         CLC   VOL,OLDVOL    SAVE VOLUME ?
         BNE   SETDISP       NO; USE DISP
         CLC   OLDSEQ,SEQ    WHERE ON TAPE ARE WE ?
         BL    SETLEAV       LOWER; GO TO EOF
         INPCLOSE DEV=2,OPT=REREAD  POSITION NEAR START OF FILE
         B     DACLOSED      SET CLOSED
SETLEAV  INPCLOSE DEV=2,OPT=LEAVE  LEAVE NEAR END OF FILE
         B     DACLOSED
SETDISP  INPCLOSE DEV=2,OPT=DISP  LEAVE AS SET BY USER
DACLOSED NI    FLAGS,255-DOPEN   RESET OPEN
         XC    OLDNAME,BLANKS  CLEAR OLD NAME
         CLI   VOL,C' '      IS THERE A VOLUME SERIAL ?
         BH    *+8           YES
         MVI   MODLIST+4,0   NO-OP THE VOL-SER CHANGE
         LA    R1,MODLIST    SET MODIFICATION LIST
         SVC   &SVCJFCB      CHANGE THE JFCB A BIT
         MVI   MODLIST+4,3   SET FOR VOL-SER PRESENT
         INPOPEN SYSUT1,DEV=2,OPT=ABEND  TRY TO OPEN DATA FILE
         CH    R15,=H'8'     OPENED ?
         BNL   BADOPEN       NO
         OI    FLAGS,FOPEN   SHOW FILE OPEN
LEAVOPEN CLC   NAME,OLDNAME  IF NEW NAME IS HIGHER, READ
         BNH   DATAOPEN      ELSE MUST REPOSITION TO START
         TM    FLAGS,FEODAD  BUT IF EOF HIT PREVIOUSLY,
         BNZ   KEEPIT        MEMBER NOT FOUND
         MVC   OLDNAME,NAME
         MVC   OLDVOL,VOL
         MVC   OLDSEQ,SEQ
         SPACE 2
***********************************************************************
*                                                                     *
*        INPUT FILE IS NOW OPEN.  SEARCH FOR THE REQUIRED MEMBER      *
*                                                                     *
***********************************************************************
         SPACE 1
LOOKLOOP INPGET DEV=2        GET A DATA CARD
         BXH   R15,R15,DATAERR
         LR    R8,R1         COPY THE ADDRESS
         USING DATA,R8       DECLARE IT
         CLC   =C'./',DATA   IEBUP--- CONTROL CARD ?
         BNE   LOOKLOOP      NO; IGNORE IT
         LA    R6,DATA       PREPARE FOR BX-- PROCESSING
         LA    R5,DATA+71
         PRTLIST MSGDATA     ELSE PRINT IT
         BAL   R9,DOBLANK    FIND A BLANK
         B     LOOKLOOP      BAD CARD ?
         BAL   R9,NOBLANK    FIND VERB
         B     LOOKLOOP      BAD CARD
         CLC   =C'ADD ',0(R6)  ADD REQUEST ?
         BE    HAVEADD       YES; PROCESS IT
         CLC   =C'REP',0(R6)  REPLACE OR REPRO REQUEST ?
         BNE   LOOKLOOP      NO; IGNORE IT
HAVEADD  BAL   R9,DOBLANK    FIND END OF VERB
         B     LOOKLOOP      SKIP IF BAD
         BAL   R9,NOBLANK    FIND START OF OPERAND FIELD
         B     LOOKLOOP      SKIP
         LR    R1,R6         SAVE CURRENT POSITION
LOOKNAMQ CLC   =C'NAME=',0(R6)  IEBUPDTE NAME FIELD ?
         BNE   LOOKNAMX      NO; TRY AGAIN
         LA    R6,5(,R6)     SKIP VERB
         B     LOOKNAMY      AND SCAN TO END
LOOKNAMX BXLE  R6,R4,LOOKNAMQ  TRY AGAIN
         LR    R6,R1         RESTORE STARTING SCAN POSITION
LOOKNAMY MVC   NEWNAME,BLANKS  CLEAR NAME
         LA    R14,NEWNAME
         LA    R15,L'NEWNAME
LOOKDAT  CLI   0(R6),C'='    NON-IEBUPDAT FORM ?
         BE    LOOKLOOP      NO; NOT VALID CARD
         CLI   0(R6),C','    END OF FIELD ?
         BE    HAVENAME      YES; PROCEED
         CLI   0(R6),C' '    END OF FIELD ?
         BNH   HAVENAME      YES; HAVE NAME
         MVC   0(1,R14),0(R6) DO AGAIN
         AR    R14,R4        UP ONE
         BXH   R6,R4,HAVENAME  ALL DONE
         BCT   R15,LOOKDAT   ELSE TRY AGAIN
         CLI   0(R6),C'='    8-BYTE OPERAND ?
         BE    LOOKLOOP      YES; NOT IEBUPDAT CARD
HAVENAME CLC   NEWNAME,NAME  COMPARE TO USER'S REQUEST
         BL    LOOKLOOP      CONTINUE READING
         BH    KEEPIT        NOT FOUND - SET MINOR ERROR
         SPACE 2
***********************************************************************
*                                                                     *
*        REQUESTED MEMBER HAS BEEN LOCATED.  COPY UNTIL               *
*              END-FILE, OR UNTIL THE START OF THE NEXT MEMBER.       *
*                                                                     *
***********************************************************************
         SPACE 1
         TM    FLAGS,FOUTOP  OUTPUT OPEN ?
         BNZ   DATACOPY
         PRTOPEN SYSUT2,OPT=(ABEND),DEV=2
         OI    FLAGS,FOUTOP  SET OPEN
         CH    R15,=H'8'     REALLY ?
         BNH   DATACOPY
         PRTL  '0***** SYSUT2 OPEN FAILED *****'                GP02325
         PRTCLOSE
         ABEND 1442,DUMP
         SPACE 1
DATACOPY PRTL  DATA,CC=NO,DEV=2  'PUNCH' THE CONTROL CARD       GP02325
         INC   NUMCARDS      COUNT CARDS PUNCHED
         INPGET DEV=2        GET ANOTHER CARD
         BXH   R15,R15,DATAERR
         LR    R8,R1         SAVE THE CARD ADDRESS
         CLC   =C'./',DATA   CONTROL CARD ?
         BNE   DATACOPY      NO
         LA    R6,DATA
         LA    R5,DATA+71
         BAL   R9,DOBLANK    FIND A BLANK
         B     DATACOPY      COPY THE FUNNY CARD
         BAL   R9,NOBLANK    NOW FIND VERB
         B     DATACOPY      NONE ?
         CLC   =C'ADD ',0(R6)  ADD CARD ?
         BE    KEEPIT        YES; STASH FOR NEXT REQUEST
         CLC   =C'REP',0(R6)  REPLACE/REPRO CARD ?
         BE    KEEPIT
         CLC   =C'ENDUP',0(R6)  END OF FILE ?
         BNE   DATACOPY      NO; JUST COPY
DATAEOD  OI    FLAGS,FEODAD  SET LOGICAL OR PHYSICAL EOF
         B     DATAMEND      DONE WITH THIS CONTROL CARD
         SPACE 1
KEEPIT   INPKEEP DEV=2       'REREAD' THE PRESENT CARD
DATAMEND ICM   R0,15,NUMCARDS  TEST CARDS FOUND
         BZ    DATANONE      NONE ?
         PRTLIST MSGOVNUM    SHOW NUMBER OF CARDS COPIED
         B     CONTLOOP
DATANONE OI    RETCH+1,4     SET ERROR CODE
         PRTLIST MSGNOMEM    SHOW MEMBER NOT FOUND
         B     CONTLOOP
         SPACE 1
CONTERR  PRTL  '0***** I/O ERROR ON SYSIN *****'                GP02325
         PRTCLOSE ,          FORCE PRINT LINE OUT
         ABEND 2540,DUMP
         SPACE 1
DATAERR  PRTL  '0***** I/O ERROR ON SYSUT1 *****'               GP02325
         PRTCLOSE ,          FORCE PRINT LINE OUT
         ABEND 2400,DUMP
         SPACE 1
BADCARD  LA    R2,5(,R6)     GET SCAN POINTER
         SR    R2,R7         LESS CARD START
         PRTLIST MSGBDCAR    SIGNAL BAD CONTROL CARD
         OI    RETCH+1,8     SET LEVEL 8 ERROR
         B     CONTLOOP      GO BACK FOR MORE PUNISHMENT
         SPACE 1
BADOPEN  PRTL  '0***** SYSUT1 OPEN FAILED *****'                GP02325
         PRTCLOSE ,
         ABEND 2400,DUMP
         SPACE 1
EXITS    SERVTERM ,          CLOSE AND FREE EVERYTHING
         LH    R9,RETCH
         ENDM  RC=(R9)       RETURN
         SPACE 2
GETNUMB  SLR   R0,R0         CLEAR ACCUMULATOR
         LA    R1,C'0'       PRESET FOR NULL
         LA    R14,5         SET MAXIMUM LENGTH ACCEPTABLE
PARMNULP CLI   0(R6),C'0'    NUMERIC ?
         BL    PARMNULR      NO
         CLI   0(R6),C'9'
         BH    PARMNULR
         SLDL  R0,8          SHIFT OVER
         IC    R1,0(,R6)     LOAD NEXT DIGIT
         BXH   R6,R4,PARMNULN  TRY NEXT ONE
         BCT   R14,PARMNULP
PARMNULR BCTR  R6,0          FAKE FOR AUTO INCREMENT AFTER STORE
PARMNULN STM   R0,R1,WORK    STASH CHARACTER STRING
         PACK  DB,WORK(8)    PACK IT
         CVB   R0,DB         GET BINARY VALUE
         BXH   R6,R4,0(R9)   TAKE ERROR EXIT IF PAST END
         CLI   0(R6),C' '    ELSE CHECK VALID TERMINATOR
         BE    4(,R9)        YES; TAKE GOOD RETURN
         BR    R9            TAKE ERROR EXIT
         SPACE 2
NOBLANK  CLI   0(R6),C' '    VALID CHARACTER ?
         BH    4(,R9)        YES; RETURN
         BXLE  R6,R4,NOBLANK  TRY AGAIN
         BR    R9            TAKE ERROR RETURN
         SPACE 1
DOBLANK  CLI   0(R6),C' '    BLANK ?
         BNH   4(,R9)        YES
         BXLE  R6,R4,DOBLANK  TRY AGAIN
         BR    R9            TAKE ERROR RETURN
         SPACE 2
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=3
SYSUT2   PUNWORK SYSUT2,SYSPUNCH,RECFM=FB,WIDTH=80
SYSIN    INPWORK SYSIN,WIDTH=80,EDIT=128,EODAD=EXITS
SYSUT1   INPWORK SYSUT1,SOURCE,WIDTH=80,EDIT=128,EODAD=DATAEOD
MOVEVOL  MVC   VOL(0),CARD   SAVE VOL-SER
         SPACE 1
HEADER   VCON  '#D I S T  ***  RETRIEVAL LOG'
         FDPRT *END
CONCARD  FDPRT ' ',NL,LEN=5
         FDPRT CARD,LEN=80
         FDPRT *END
MSGDATA  FDPRT ' ',NL,LEN=20
         FDPRT DATA,LEN=80
         FDPRT *END
MSGBDCAR FDPRT BLANKS,(R2),NL
         FDPRT '| - LAST COLUMN SCANNED'
         FDPRT '*****',NL
         FDPRT 'INVALID CONTROL CARD',PAD
         FDPRT '*****'
         FDPRT *END
MSGOVNUM FDOPT NL,CC=C'&&'   PREFERRED OVERPRINT/PRINT IF NO OVPRT
         FDPRT NUMCARDS,I,RADJ,LEN=110
         FDPRT 'RECORDS PROCESSED',PAD
         FDPRT *END
MSGNOMEM FDPRT '***** MEMBER',NL
         FDPRT NAME,PAD,DEB
         FDPRT 'NOT FOUND *****'
         FDPRT *END
BLANKS   DC    CL88' '       CONSTANT
         SPACE 1
SAVE     DSECT ,             SAVE/WORK AREA
DB       DS    D
WORK     DS    D
@SERVICE DS    A             @SERVICE ADDRESS
@PRINTER DS    A             @PRINTER ADDRESS
@INPREAD DS    A             @INPREAD ADDRESS
MODLIST  DC    0F'0',X'01',AL3(SYSUT1) DDN PTR             1/4
         DC    X'03',AL3(VOL)          VOL-SER PTR         2/4
         DC    X'06',AL3(DSN)          DSN PTR             3/4
         DC    X'82',AL3(SEQ+2)        FILESEQ PTR         4/4
DSN      DS    CL44          (TAPE) DATASET NAME
SEQ      DS    F             (TAPE) FILE SEQUENCE
NAME     DS    CL8           (TAPE) MEMBER NAME
VOL      DS    CL6           (TAPE) VOLSER
OLDVOL   DS    CL6           CURRENT/PRIOR VOLSER
OLDSEQ   DS    F             CURRENT/PRIOR FILE
OLDNAME  DS    CL8           LAST REQUESTED NAME
NEWNAME  DS    CL8           (TAPE) NEXT MEMBER NAME
NUMCARDS DS    F             NUMBER OF CARDS IN MEMBER
RETCH    DC    H'0'          RETURN CODE
FLAGS    DC    X'00'         PROCESSING FLAGS
FBUILD   EQU   X'80'           ALTERNATE ENTRY POINT - BUILD INDEX
FOUTOP   EQU   X'04'           PUNCH OUTPUT IS OPEN
FOPEN    EQU   X'02'           DATA FILE IS OPEN
FEODAD   EQU   X'01'           DATA FILE IS AT END-FILE
DOPEN    EQU   FOPEN+FEODAD    DATA FILE RESET
SAVEND   EQU   *
         SPACE 1
DATASECT DSECT ,             SYSUT1 DATA DEFINITION
DATA     DS    CL80
         SPACE 1
CARDSECT DSECT ,             SYSIN DATA DEFINITION
CARD     DS    CL80
         SPACE 1
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         DCBD  DEVD=DA,DSORG=PS
         END   ,
