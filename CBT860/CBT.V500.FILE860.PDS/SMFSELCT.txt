SMFS     TITLE 'S M F S E L C T * - SMF DATA EXTRACTION SERVICES'
         COPY  OPTIONGB      GET GLOBAL OPTION DEFINITIONS       90071
         SPACE 1                                                 90071
         SYSPARM LIST=YES    SHOW ASSEMBLY/ENVIRONMENT OPTIONS   90071
         SPACE 1                                                 90071
*        ORIGINAL PROGRAM WRITTEN BY DICK OXLEY AT NIS OR ADR         *
*        MAJOR REWRITE FOR MVS BY G. POSTPISCHIL, E.S.P.         90071
***********************************************************************
         SPACE 1                                                 90071
***********************************************************************
*                                                                     *
*        THIS PROGRAM READS CUMULATIVE SMF TAPES (E.G., MONTHLY OR    *
*        DAILY DUMP/MERGE FILES) AND EXTRACTS DATA:                   *
*                                                                     *
*        PARM=YYMON          EXTRACT DATA FOR SPECIFIED MONTH         *
*        PARM=YYMONSDED      EXTRACT FOR START/END DAY                *
*              ,I=SMCA...       SELECT BY SYSTEM IDS                  *
*              ,I¬SMCA...       REJECT BY SYSTEM IDS                  *
*              ,T=NNN        SELECT SPECIFIC RECORD TYPE              *
*              ,T=(NN,N,NNN)   SELECT BY TYPE                         *
*              ,T¬(NN,N,NNN)   EXCLUDE BY RECORD TYPE                 *
*              ,A=ACCTLIST...   SELECT BY ACCOUNT                     *
*              ,J=JOBNAMES...   SELECT BY JOBNAMES                    *
*                                                                     *
*        TYPE SKIP OR SELECT MAY BE 1-3 DIGITS, OR A SUBLIST OF       *
*          1-3 DIGIT SMF RECORD TYPES.                                *
*        ACCT AND JOBNAME ENTRIES MAY CONTAIN * (GENERAL MATCH FOR    *
*          REST OF NAME) OR ? (MATCH FOR THAT POSITION).              *
*        ACCT AND SMCA MUST BE 4 BYTES EACH, JOBNAMES 8 BYTES EACH.   *
*                                                                     *
*    SMFIN   - SMF INPUT DATA; LIKE CONCATENATION ONLY                *
*    SMFOUT  - SELECTED DATA                                          *
*    SMFSKIP - UNSELECTED DATA                                        *
*        AT LEAST ONE OUTPUT DD MUST BE PRESENT                       *
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90071
         EJECT ,                                                 90071
         PRINT &PRTSOR       SAVE A TREE (OR AT LEAST A BUSH)    90071
SMFSELCT SAVEM ZERO15,BASE=(R11,R12),PARM=(R8),BNDRY=PAGE        90071
         LA    R10,DYNWORK   SET WORK AREA FOR EXIT              90071
         DROP  R13                                               90071
         USING DYNWORK,R10                                       90071
         SERVINIT ,          LOAD @SERVICE ROUTINE               90071
         SERVCALL LPALD,=CL8'@PRINTER'  GET THE PRINTER ROUTINE  90071
         ST    R0,@PRINTER   STASH IT                            90071
         PRTOPEN SYSPRINT,OPT=(DUMMY,NOWTO)                      83327
         CH    R15,=H'4'     GOOD OPEN ?                         83327
         BNH   PRTOPNOK      YES; LEAVE LOG/CONSOLE FILE CLOSED  83327
         PRTOPEN CONSOLE,OPT=(DUMMY,NOWTO),DEV=2  OPEN A MESSAGE FILE
PRTOPNOK PRTV  HEAD1,TITLE=1 MAKE THE TITLE                      83327
         TIME  ,             GET THE CURRENT DATE                84159
         STCM  R1,7,RUNDATE   SAVE IT                            84159
         STCM  R1,7,BILSTART  SET START OF BILLING PERIOD        84159
         STCM  R1,7,BILSTEND   AND END                           84159
         LA    R8,0(,R8)                                         84159
         LTR   R8,R8         ANY PARM SUPPLIED ?                 84159
         BZ    INITNPRM      NO                                  84159
         ICM   R8,7,1(R8)    GET PARM POINTER                    84159
         BZ    INITNPRM                                          84159
         XR    R7,R7                                             84159
         ICM   R7,3,0(R8)    GET LENGTH                          84159
         BNP   INITNPRM      SKIP IF NULL OR INVALID             84159
PRMTCOMA LTR   R7,R7         STILL TEXT TO DO ?                  92043
         BNP   INITNPRM      NO                                  92043
         CLI   2(R8),C','    LEADING COMMA ?                     85115
         BNE   PRMNLCOM      NO                                  84159
         LA    R8,1(,R8)     SKIP IT                             84159
PRMTCOMS SH    R7,=H'1'      ALLOW FOR IT                        84159
         BNP   INITNPRM      NO PARM                             84159
PRMNLCOM CLC   =C'A=',2(R8)  ACCOUNT FIELD PRESENT ?             84159
         BE    PRMTACCT      YES; PROCESS IT - NO DATE           84159
         CLC   =C'J=',2(R8)  JOBNAMES ?                          85217
         BE    PRMTJOBS      YES; NO DATE                        85217
         CLC   =C'I=',2(R8)  SYSTEM ID MATCH ?                   90071
         BE    PRMTIDS                                           90071
         CLC   =C'I¬',2(R8)  SYSTEM ID EXCLUSION ?               90071
         BE    PRMTIDN                                           90071
         CLC   =C'T=',2(R8)   RECORD TYPE SELECTION              92043
         BE    PRMTYPE       PROCESS ITEM OR LIST                92043
         CLC   =C'T¬',2(R8)    TYPE EXCLUSION ?                  92043
         BE    PRMNTYP       YES; PROCESS                        92043
         CH    R7,=H'5'      LONG ENOUGH FOR DATE ?              84159
         BL    PRMBAD        NO                                  84159
         CLC   =C'DEBUG',2(R8)  DEBUG PRINT MODE ?               85115
         BNE   PRMNBUG       NO                                  85115
         OI    OPTFLAG,OFGDEBUG  SET DEBUG BIT                   85115
         LA    R8,5(R8,0)    SKIP OVER IT                        85115
         SH    R7,*-2        ADJUST LENGTH                       85115
         BP    PRMTCOMA      CHECK FOR COMMA AGAIN               85115
         B     INITNPRM      ELSE DEFAULT REST                   85115
PRMNBUG  TM    OPTFLAG,OFGDATE  ALREADY PROCESSED A DATE ?       92043
         BNZ   PRMBAD        YES; INVALID PARAMETER              92043
         OI    OPTFLAG,OFGDATE   SET DATE PROCESSED              92043
*        CHECK FOR DATE RANGE SELECTION.  FORMAT IS :            84159
*        PARM=YYMON     WHERE 'YY' IS THE YEAR (+1900)           84159
*              'MON' THE THREE BYTE EBCDIC MONTH                 84159
*             THE FORM "MONYY" IS ALSO ACCEPTED.                 84160
*                                                                84239
*        A RANGE OTHER THAN A COMPLETE MONTH MAY BE SPECIFIED USING
*        PARM=YYMONSDED, WHERE 'SD' IS THE TWO-DIGIT STARTING DAY OF
*        THE PERIOD, AND 'ED' IS THE TWO-DIGIT END DAY. IF 'ED' DOES
*        NOT EXCEED 'SD', THEN THE MONTH (AND POSSIBLY THE YEAR) IS
*        INCREMENTED BY ONE. IF 'ED' IS SUPPLIED AS 'XX', THEN   85119
*        THE START DAY WILL BE USED FOR THE END DAY              85119
*                                                                84239
         LA    R2,2(,R8)     POINT TO YEAR                       84160
         LA    R1,MONAMES    GET THE EBCDIC MONTH TABLE          84159
         LA    R0,12         NUMBER OF ENTRIES                   84159
         LA    R15,13        NUMBER OF ENTRIES+1                 84159
PRMMLOOP CLC   0(3,R1),2+2(R8)  MATCHES PARM ?                   84159
         BE    PRMMOSET      YES; SET MONTH                      84159
         CLC   0(3,R1),2(R8)   'MONYY' FORMAT AND MATCH ?        84160
         BE    PRMMOFLP      YES; FIX YEAR PTR                   84160
         LA    R1,9(,R1)     GET NEXT MONTH                      84159
         BCT   R0,PRMMLOOP                                       84159
         B     PRMBAD        INVALID PARM                        84159
PRMMOFLP LA    R2,2+3(,R8)   POINT TO PRESUMED YEAR              84160
PRMMOSET SR    R15,R0        R15 = BINARY MONTH 1-12             84159
         LA    R5,0(R15,R15)  SAVE MONTH OFFSET IN MONTAB        84159
         BAL   R4,PRM2BIN    CONVERT TWO DIGITS TO BINARY        84159
         LR    R1,R3         SAVE THE YEAR                       84159
         LA    R0,1          SET START DAY                       84159
         STM   R15,R1,BINMON   SET MONTH/DAY/YEAR FOR CONDATE    84159
         CH    R7,=H'9'      PARM LONG ENOUGH FOR DAY RANGE ?    84239
         BL    PRMDOMON      NO; DEFAULT TO FULL MONTH           84239
         CLI   2+3+2(R8),C'0'  NUMERIC ?                         84239
         BL    PRMDOMON      NO                                  84239
         CLI   2+3+2+1(R8),C'0'  TWO DIGITS ?                    84239
         BL    PRMDOMON      NO                                  84239
         LA    R2,2+3+2(,R8) POINT TO PAIR OF DIGITS             84239
         BAL   R4,PRM2BIN    CONVERT TO BINARY                   84239
         ST    R3,BINDAY     STASH                               84239
         BAL   R9,CONDATE    CONVERT TO PACKED                   84239
         MVC   BILSTART,WORK MOVE START OF RANGE                 84239
         MVC   BINSAVE(3*4),BINMON  SAVE START DATE              90071
         LM    R15,R1,BINMON RELOAD FOR 'XX' FORM                85119
         LA    R2,2+3+2+2(,R8)  GET END DAY                      84239
         CLC   =C'XX',0(R2)  SPECIAL CODE FOR START=END DAY ?    85119
         BE    PRMDYMON      LEAVE START AS END DAY              85119
         BAL   R4,PRM2BIN    CONVERT TO BINARY                   84239
         LM    R15,R1,BINMON RELOAD MONTH, DAY, YEAR OF START    84239
         CR    R0,R3         IS END DAY HIGHER ?                 84239
         BL    PRMDYMON      YES                                 84239
         LA    R15,1(,R15)   ELSE INCREMENT THE MONTH            84239
         CH    R15,=H'12'    PAST DECEMBER ?                     84239
         BNH   PRMDYMON      NO                                  84239
         LA    R1,1(,R1)     BUMP THE YEAR                       84239
         LA    R15,1         AND SET MONTH TO JANUARY            84239
PRMDYMON LR    R0,R3         SET END DAY                         84239
         STM   R15,R1,BINMON                                     84239
         BAL   R9,CONDATE    GET PACKED END DATE                 84239
         MVC   BILSTEND,WORK SAVE IT                             84239
         MVC   BINMON(3*4),BINSAVE  RESTORE START DATE           90071
         OI    OPTFLAG,OFGPART  SHOW PARTIAL DATE RANGE          84239
         LA    R0,2+2+2+3    SET LENGTH TO SKIP                  84239
         B     PRMNOMON      REJOIN COMMON                       84239
         SPACE 1                                                 84239
PRMDOMON BAL   R9,CONDATE    CONVERT TO PACKED FORM 00YYDDD      84159
         MVC   BILSTART,WORK   SET START OF RANGE                84159
         LH    R3,MONTAB-2(R5)  LAST VALID DAY FOR THIS MONTH    84159
         ST    R3,BINDAY     SET IT                              84159
         BAL   R9,CONDATE    CONVERT TO PACKED                   84159
         MVC   BILSTEND,WORK  SET IT                             84159
         LA    R0,2+3        SET LENGTH TO SKIP                  84239
PRMNOMON AR    R8,R0         NEXT PARM                           84239
         SR    R7,R0         ADJUST RESIDUAL LENGTH              84239
         BP    PRMTCOMA      PROCESS NEXT PARAMETER              92043
         B     INITNPRM      ELSE BEGIN PROCESSING               92043
         SPACE 1                                                 84159
PRM2BIN  CLI   0(R2),C'0'    NUMERIC FIRST CHARACTER ?           84159
         BL    PRMBAD        NO; TOO BAD                         84159
         CLI   1(R2),C'0'    NUMERIC SECOND ?                    84159
         BL    PRMBAD                                            84159
         PACK  DB,0(2,R2)    PACK IT                             84159
         CVB   R3,DB         CONVERT TO BINARY                   84159
         BR    R4            RETURN TO CALLER                    84159
         SPACE 1                                                 90071
PRMTIDN  OI    FLAG,FGNOSID  SKIP RECORD IF SYSTEM ID MATCHES    83327
PRMTIDS  SH    R7,=H'2'      SET RESIDUAL LENGTH                 90071
         BNP   INITNPRM      NO DATA                             90071
         LA    R8,2(,R8)     SKIP I=                             92043
         BAL   R14,FINDCOMA  FIND NEXT COMMA                     92043
         SRDL  R0,2          FAKE DIVIDE BY 4                    92043
         SRA   R1,32-2       GET REMAINDER                       92043
         BNZ   PRMBAD        INVALID                             90071
         LTR   R0,R0         AT LEAST ONE ?                      83327
         BNP   PRMBAD        NO                                  90071
         ST    R0,NUMSID     SAVE NUMBER OF SYSTEM IDS TO COMPARE
         ST    R2,@SIDLIST   SAVE ADDRESS FOR TESTS              92043
         B     PRMTCOMA      DONE WITH THIS PARM                 92043
         SPACE 1                                                 84159
PRMTACCT SH    R7,=H'2'      ALLOW FOR ACCT LENGTH               84159
         BNP   INITNPRM                                          84159
         LA    R8,2(,R8)     SKIP A=                             92043
         BAL   R14,FINDCOMA  GET END OF PARAMETER                92043
         SRDL  R0,2          FAKE DIVIDE BY 4                    92043
         SRA   R1,32-2       GET REMAINDER                       92043
         BNZ   PRMBAD        INVALID                             92043
         LTR   R0,R0         AT LEAST ONE ?                      92043
         BNP   PRMBAD        NO                                  92043
         CH    R0,=Y(MAXACCT)  WILL THEY FIT ?                   84159
         BH    PRMBAD        NO; TOO LONG - SO LONG.             84159
         ST    R0,SELNUM     SET NUMBER OF ACCOUNT VALUES        84159
         SLL   R0,2          *4                                  84159
         LR    R1,R0         MOVE FOR EXECUTE                    92043
         BCTR  R1,0          MAKE EXECUTE LENGTH                 92043
         EX    R1,SELPRMVC   MOVE TO SELECTION FIELD             92043
         B     PRMTCOMA      CONTINUE PROCESSING                 92043
SELPRMVC MVC   SELACCT(0),0(R2)  MOVE ACCOUNTS FROM PARM         92043
         SPACE 1                                                 85217
PRMTJOBS SH    R7,=H'2'      ALLOW FOR J= LENGTH                 85217
         BNP   INITNPRM                                          85217
         LA    R8,2(,R8)     SKIP J=                             92043
         BAL   R14,FINDCOMA  FIND END OF STRING                  92043
         SRDL  R0,3          FAKE DIVIDE BY 8                    92043
         SRA   R1,32-3       GET REMAINDER                       92043
         BNZ   PRMBAD        INVALID                             92043
         LTR   R0,R0         AT LEAST ONE ?                      92043
         BNP   PRMBAD        NO                                  92043
         CH    R0,=Y(JOBMAX)  WILL THEY FIT ?                    85217
         BH    PRMBAD        NO; TOO LONG - SO LONG.             85217
         ST    R0,JOBNUM     SET NUMBER OF NAMES                 92043
         SLL   R0,3          *8                                  85217
         LR    R1,R0         COPY FOR EXECUTE                    92043
         BCTR  R1,0          MAKE EXECUTE LENGTH                 92043
         EX    R1,JOBPRMVC   MOVE TO SELECTION FIELD             92043
         B     PRMTCOMA      CONTINUE PROCESSING                 92043
JOBPRMVC MVC   JOBNAMES(0),0(R2)  MOVE ACCOUNTS FROM PARM        92043
         SPACE 1                                                 92043
*    FIND NEXT COMMA; R0 - LENGTH R2 - OLD START, R7/R8 UPDATED  92043
FINDCOMA LA    R2,2(,R8)     SAVE OLD START                      92043
         SLR   R0,R0         SET NULL LENGTH                     92043
         LTR   R7,R7         GET RESIDUAL PARM LENGTH            92043
         BNPR  R14           NONE; JUST RETURN                   92043
FINDCOMB CLI   2(R8),C','    TRAILING COMMA ?                    92043
         BER   R14           YES; RETURN                         92043
         AH    R0,=H'1'      UP CHARACTER COUNTER                92043
         LA    R8,1(,R8)     UP POINTER                          92043
         BCT   R7,FINDCOMB   TRY AGAIN                           92043
         BR    R14           END OF STRING                       92043
         SPACE 1                                                 92043
PRMTYPE  MVI   SKIPTAB,4     SET TO SKIP RECORD BY TYPE          92043
         MVC   SKIPTAB+1(L'SKIPTAB-1),SKIPTAB  FOR ALL RECORDS   92043
         SLR   R3,R3         SET VALUE TO STORE FOR REQUEST      92043
         B     PRMTYCOM      GO TO COMMON                        92043
PRMNTYP  LA    R3,4          SET TO EXCLUDE SPECIFIED RECORDS    92043
PRMTYCOM ICM   R3,8,=C','    SET FOR SINGLE ITEM PROCESSING      92043
         ICM   R3,4,=C','    SET LIST SEPARATOR                  92043
         LA    R8,2(,R8)     SKIP OVER                           92043
         SH    R7,=H'2'      ALLOW FOR REQUEST LENGTH            92043
         BNP   PRMBAD        INVALID PARM                        92043
         CLI   2(R8),C'('    SUBLIST ?                           92043
         BNE   PRMTYLOP      NO; LOOP TO PROCESS                 92043
         ICM   R3,8,=C')'    SET DESIRED TERMINATION CHARACTER   92043
         LA    R8,1(,R8)     SKIP LIST START                     92043
         SH    R7,=H'1'      ADJUST RESIDUAL LENGTH              92043
         BNP   PRMBAD                                            92043
PRMTYLOP SLR   R14,R14                                           92043
         SLR   R15,R15       CLEAR ACCUMULATORS                  92043
         LA    R0,4          SET MAX TO LOOK AT                  92043
PRMTYDIG CLM   R3,8,2(R8)    IS THIS A TERMINATOR ?              92043
         BE    PRMTYTRM      YES                                 92043
         CLM   R3,4,2(R8)    IS THIS AN ITEM SEPARATOR ?         92043
         BE    PRMTYTRM      YES                                 92043
         CLI   2(R8),C'0'    IS THIS NUMERIC ?                   92043
         BL    PRMBAD        NO                                  92043
         CLI   2(R8),C'9'    IN RANGE ?                          92043
         BH    PRMBAD        NO                                  92043
         SLDL  R14,8         MAKE ROOM                           92043
         IC    R15,2(,R8)    PLACE INTO ACCUMULATOR              92043
         LA    R8,1(,R8)     NEXT INPUT POSITION                 92043
         BCT   R7,PRMTYCNT   CHECK COUNT                         92043
         CLM   R3,8,=C','    DOING A LIST ?                      92043
         BNE   PRMBAD        YES; INCORRECT END                  92043
PRMTYCNT BCT   R0,PRMTYDIG   GET NEXT DIGIT                      92043
         B     PRMBAD        TOO LONG                            92043
PRMTYTRM LTR   R15,R15       NULL PARAMETER ?                    92043
         BNZ   PRMTYCON      NO; EXPAND                          92043
         CLM   R3,8,=C','    SINGLE ITEM ?                       92043
         BE    PRMBAD        YES; NOT ALLOWED                    92043
         B     PRMTYSKP      SKIP OVER IT                        92043
PRMTYCON STM   R14,R15,DB2   STASH                               92043
         PACK  DB,DB2        PACK                                92043
         CVB   R1,DB         MAKE BINARY                         92043
         CH    R1,=H'255'    LEGAL TYPE ?                        92043
         BH    PRMBAD        NO                                  92043
         LA    R1,SKIPTAB(R1)  GET RECORD'S TABLE SLOT           92043
         STC   R3,0(,R1)     SET APPROPRIATE SELECT/SKIP VALUE   92043
PRMTYSKP CLM   R3,8,2(R8)    MATCHES END PARAMETER ?             92043
         BE    PRMTCOMS      YES; RESUME PARM SCAN               92043
         LA    R8,1(,R8)     SKIP OVER SEPARATOR                 92043
         SH    R7,=H'1'      ADJUST RESIDUAL LENGTH              92043
         BP    PRMTYLOP      GET NEXT ITEM                       92043
         B     INITNPRM      DONE WITH PARM PROCESSING           92043
         SPACE 1                                                 84159
SELPDATE FDPRT 'SMF DATA EXTRACTION FOR',NL,PADR                 84159
         FDPRT BILMONTH,DEB                                      84159
         FDPRT ',',PADR                                          84159
         FDPRT BILYEAR                                           84159
         FDPRT '(',PADL                                          84159
         FDPRT BILSTART,DATJ                                     84159
         FDPRT 'TO',PAD                                          84159
         FDPRT BILSTEND,DATJ                                     84159
         FDPRT ')'                                               84159
         FDPRT *END                                              84159
SELPAHED FDPRT 'OUTPUT IS LIMITED TO ACCOUNTS :',PADR,NL         84159
         FDPRT *END                                              84159
SELPACCT FDOPT IND=32                                            84159
         FDPRT 0(R3),4,PADR                                      84159
         FDPRT *END                                              84159
JOBPAHED FDPRT 'OUTPUT IS LIMITED TO JOBS :',PADR,NL             85217
         FDPRT *END                                              85217
JOBPACCT FDOPT IND=32                                            85217
         FDPRT 0(R3),8,PADR                                      85217
         FDPRT *END                                              85217
         SPACE 1                                                 84159
PRMBAD   PRTF  '0INVALID PARM FIELD'                             84159
         SERVTERM ,                                              84159
         ABEND 16                                                84159
         SPACE 1                                                 84159
INITNPRM CP    BILSTART,BILSTEND  USER DATE RANGE ?              84159
         BL    INITRATE      YES; USE IT                         84159
         BH    INITFORC      FORCE PRIOR MONTH                   85258
         TM    OPTFLAG,OFGPART  EXPLICIT REQUEST FOR ONE DAY ?   85258
         BNZ   INITRATE      YES; HONOR IT                       85258
*                                                                84159
*        SET RANGE FOR PREVIOUS MONTH BY DEFAULT                 84159
INITFORC MVO   DB,RUNDATE(1)  MAKE 0-0YYX                        85258
         OI    DB+7,X'0F'    FORCE SIGN                          84159
         CVB   R1,DB         GET BINARY YEAR                     84159
         MVC   DB+6(2),RUNDATE+1  GET DAYS BACK                  84159
         CVB   R0,DB         MAKE BINARY                         84159
         MVI   MONFEB+1,28   RESET FEBRUARY DAYS                 84159
         TM    RUNDATE,X'01'  ODD YEAR ?                         84159
         BNZ   INITNLIP      NOT LEAP                            84159
         TM    RUNDATE,X'12'  MULTIPLE OF FOUR ?                 84159
         BM    INITNLIP      NO                                  84159
         MVI   MONFEB+1,29   SET FOR LEAP                        84159
INITNLIP LA    R14,MONTAB-2                                      84159
         LR    R15,R14       SAVE START-2                        84159
         LA    R3,11         DO TWELVE TIMES                     84159
INITNLOP SH    R0,2(,R15)    IN THIS MONTH ?                     84159
         BNP   INITNLOM      YES                                 84159
         LA    R15,2(,R15)                                       84159
         BCT   R3,INITNLOP                                       84159
INITNLOM LH    R0,0(,R15)    DAYS IN PRIOR MONTH                 84159
         SR    R15,R14       GET MONTH'S OFFSET                  84159
         BP    INITNLON      MONTH NOT ZERO                      84159
         LA    R15,12*2      SET FOR PREVIOUS DECEMBER           84159
         BCTR  R1,0          AND GET PRIOR YEAR                  84159
         LA    R0,31         USUAL DAYS IN DECEMBER              84159
INITNLON SRL   R15,1         NUMBER OF PREVIOUS MONTH            84159
         STM   R15,R1,BINMON SAVE MONTH/DAY/YEAR OF PRIOR MONTH  84159
         BAL   R9,CONDATE    CONVERT BACK TO PACKED              84159
         MVC   BILSTEND,WORK  SET END DATE                       84159
         MVI   BINDAY+3,1    SET FOR FIRST DAY OF MONTH          84159
         BAL   R9,CONDATE    CONVERT DATE                        84159
         MVC   BILSTART,WORK  MOVE CONVERTED DATE                84159
INITRATE L     R1,BINMON     GET BILLING MONTH                   84159
         MH    R1,=H'9'      CONVERT TO OFFSET                   84159
         LA    R1,MONAMES-9(R1)  POINT TO NAME                   84159
         MVC   BILMONTH,0(R1)   STASH IT IN HEADER RECORD        84159
         TM    OPTFLAG,OFGPART  USER SPECIFIED RANGE ?           84239
         BZ    *+10          NO                                  84239
         MVC   BILMONTH+3(6),=C'(PART)'  INDICATE PARTIAL        84239
         MVC   BILYEAR(2),=C'19'  SET DEFAULT YEAR               90071
         UNPK  DB(3),BILSTART(2)  UNPACK YEAR                    84159
         MVC   BILYEAR+2(2),DB   NOT 2000 YET ?                  84159
         PRTLIST SELPDATE    SHOW DATES OF RUN                   84159
         ICM   R2,15,SELNUM  GET NUMBER OF ACCOUNTS SELECTED     84159
         BZ    NOSELACT      NONE; TEST FOR JOB SELECTION        85217
         PRTSPACE 1          SPACE A LINE                        84165
         PRTLIST SELPAHED    MAKE HEADER                         84159
         LA    R3,SELACCT    POINT TO FIRST ACCOUNT              84159
PRTACCT  PRTLIST SELPACCT    PRINT ONE ACCOUNT                   84159
         LA    R3,4(,R3)     POINT TO NEXT ONE                   84159
         BCT   R2,PRTACCT    REPEAT FOR ALL                      84159
         B     NOACTSEL      ALL DONE                            85217
NOSELACT ICM   R2,15,JOBNUM  GET NUMBER OF JOBS SELECTED         92043
         BZ    NOACTSEL      NONE; PROCESS EVERYTHING            85217
         PRTSPACE 1          SPACE A LINE                        85217
         PRTLIST JOBPAHED    MAKE HEADER                         85217
         LA    R3,JOBNAMES   POINT TO FIRST JOBNAME              85217
PRTJOBS  PRTLIST JOBPACCT    PRINT ONE NAME                      85217
         LA    R3,8(,R3)     POINT TO NEXT ONE                   85217
         BCT   R2,PRTJOBS    REPEAT FOR ALL                      85217
NOACTSEL SLR   R5,R5         COUNT INPUT DD CARDS                83327
         MVC   DB,SMFIN+DCBDDNAM-IHADCB  MASTER INPUT NAME       92043
         BAL   R9,DDCHECK    CHECK FOR IT                        83327
         BM    MISSDD                                            83327
         BZ    *+8                                               83327
         LA    R5,1(,R5)     UP DD COUNT                         83327
         BXH   R5,R5,OPENINP ANY INPUT DD CARDS ?                83327
         PRTV  MSGNOINP,DEV=(1,2)  SIGNAL NO INPUT               83327
         B     MISSDDX       GO TO EXIT                          83327
         SPACE 1                                                 83327
DDCHECK  DEVTYPE DB,DB2      LOOK FOR DD CARD                    83327
         BXH   R15,R15,DDCHECKF  NOT FOUND - FAIL                83327
         TM    DB2+6,UCB3TAPE+UCB3DACC  SUPPORTED DEVICE ?       83327
         BM    DDCHECK0      OK ?                                83327
         CLI   DB2+6,0       DD DUMMY ?                          83327
         BNE   DDCHECKF      NO                                  83327
DDCHECK0 ICM   R15,1,DB2+6   LOAD TYPE TO SET CONDITION CODE     83327
         LTR   R15,R15       SET CONDITION FOR ENTIRE REGISTER   92043
         BR    R9            RETURN TO CALLER                    83327
DDCHECKF LNR   R15,R9        SET CONDITION CODE NEGATIVE         83327
         BR    R9            RETURN WITH ERROR                   83327
         SPACE 1                                                 83327
MISSDDO  PRTLIST MSGBADDD,DEV=(1,2)                              83327
         B     MISSDDX                                           83327
MISSDD   PRTLIST MSGNODD,DEV=(1,2)  SHOW REQUIRED DD UNUSABLE    83327
MISSDDX  SERVTERM ,          CLOSE AND FREE                      83327
         ABEND 3400          SIGNAL MISSING INPUT                83327
         SPACE 1                                                 90071
INSTOR   PRTV  MSGNOREG,DEV=(1,2)  INSUFFICIENT REGION           83327
         SERVTERM ,          CLOSE AND FREE                      83327
         ABEND 804,DUMP        IN CASE OF FUNNY PROBLEMS         83327
         SPACE 1                                                 83327
OPENINP  OPEN  (SMFIN,(INPUT,REREAD))                            83327
         TM    SMFIN+DCBOFLGS-IHADCB,DCBOFOPN  OPENED ?          83327
         BNO   MISSDDO       BRANCH IF NOT                       83327
         MVC   DB,SMFNOUT    LOOK FOR SMFOUT                     92043
         BAL   R9,DDCHECK                                        92043
         BNP   MISSOUT       NONE                                92043
         OPEN  (SMFOUT,(OUTPUT,REREAD))                          90071
         TM    SMFOUT+DCBOFLGS-IHADCB,DCBOFOPN  OPENED ?         90071
         BNZ   OUTOPEN                                           92043
MISSOUT  PRTLIST MSGBADDD,DEV=(1,2)                              92043
         LA    R0,RET14      POINT TO RETURN                     92043
         STCM  R0,7,SMFOUT+DCBPUTA-IHADCB  MAKE FAKE PUT ADDRESS 92043
OUTOPEN  MVC   DB,SMFNSKIP   IS REJECT FILE PRESENT ?            92043
         BAL   R9,DDCHECK                                        92043
         BNP   MISSSKIP      NO                                  92043
         OPEN  (SMFSKIP,(OUTPUT,REREAD))                         92043
         TM    SMFSKIP+DCBOFLGS-IHADCB,DCBOFOPN  OPENED ?        92043
         BNZ   SKIPOPEN                                          92043
MISSSKIP TM    SMFOUT+DCBOFLGS-IHADCB,DCBOFOPN  IS SMFOUT ALSO CLOSED ?
         BZ    MISSDDO       AT LEAST ONE REQUIRED               92043
         PRTLIST MSGBADDD,DEV=(1,2)                              92043
         LA    R0,RET14      POINT TO RETURN                     92043
         STCM  R0,7,SMFSKIP+DCBPUTA-IHADCB  MAKE FAKE PUT ADDRESS
         SPACE 1                                                 83327
SKIPOPEN L     R4,=A(PFXOHEAD+32*1024)  TOTAL BUFFER SPACE USED  83327
         GETMAIN RC,LV=(R4),BNDRY=PAGE  CONDITIONAL STORAGE      90071
         BXH   R15,R15,INSTOR   NOT ENOUGH ?                     90071
         LR    R2,R1         COPY                                90071
         CLRL  (R2),(R4)     CLEAR STORAGE I'M KEEPING           83327
         ST    R2,OUTBUFF    SET START OF INTERMEDIATE SORT BUFFER
         LA    R1,PFXL(,R2)  START OF INPUT BUFFER               83327
         ST    R1,WORKBUFF   SAVE START OF INPUT BUFFER          83327
         A     R1,=A(32*1024)   ADD BUFFER SIZE                  83327
         ST    R1,WORKBEND   SAVE END OF BUFFER ADDRESS(+1)      83327
         EJECT                                                   90071
***********************************************************************
*                                                                     *
*    SMF PROCESSING - READ INPUT SEGMENT BY SEGMENT, SKIP BAD GUYS    *
*                                                                     *
***********************************************************************
         SPACE 2                                                 90071
INPLOOP  L     R8,OUTBUFF    GET OUTPUT BUFFER                   83327
         USING PFXMAP,R8     DECLARE IT                          83327
         B     NEWRECD                                           83327
         SPACE 1                                                 83327
FLUSHALL L     R15,SEGCNTR                                       83327
         A     R15,SEGSKIP                                       83327
         ST    R15,SEGSKIP   SET SEGMENTS SKIPPED                83327
NEWRECD  LA    R4,SMFRSV     POINT TO BEGINNING OF NEW RECORD    83327
         SLR   R5,R5                                             83327
         ST    R5,SEGCNTR    SET SEGMENT COUNTER TO ZERO         83327
         STC   R5,VSPREV     ZERO 'PREVIOUS' SEGMENT FLAGS       83327
         NI    FLAG,255-FGIOERR  RESET I/O ERROR FLAG            83327
         SPACE 1                                                 83327
NEWSEGD  BAL   R9,GETINP     GET ANOTHER SEGMENT                 83327
         LR    R2,R1         SAVE THE SEGMENT ADDRESS            83327
         INC   SEGCNTR       INCREASE THE SEGMENT COUNTER        83327
         TM    FLAG,FGIOERR  I/O ERROR ?                         83327
         BNZ   FLUSHALL      YES; FLUSH PREVIOUS AND CURRENT     83327
         LA    R0,4                                              83327
         SLR   R3,R3                                             83327
         ICM   R3,3,0(R2)    GET THE PRESUMED SEGMENT LENGTH     83327
         BNP   FLUSHALL      ERROR - FLUSH                       83327
         TM    VSPREV,1      ANY PRIOR SEGMENTS STACKED ?        83327
         BZ    NEWSEGM       NO; THIS MUST BE NEW OR COMPLETE SEG
         TM    2(R2),2       ADDITIONAL SEGMENT ?                83327
         BNZ   MOVESEV       YES; TACK IT ON                     83326
         SPACE 1                                                 83327
FLUSHSEG L     R15,SEGCNTR   GET SEGMENT COUNTER                 83327
         BCTR  R15,0         LESS CURRENT SEGMENT                83327
         A     R15,SEGSKIP   MAKE SEGMENTS SKIPPED               83327
         ST    R15,SEGSKIP   SET SEGMENTS SKIPPED                83327
         LA    R15,1                                             83327
         ST    R15,SEGCNTR   RESET COUNTER FOR CURRENT SEGMENT   83327
         LA    R4,SMFRSV     START A NEW RECORD                  83327
NEWSEGM  TM    2(R2),3       SINGLE SEGMENT ?                    83327
         BZ    MOVESEV       YES; MOVE AND PROCESS IT            83327
         TM    2(R2),2       OUT OF SEQUENCE ?                   83327
         BNZ   FLUSHALL      YES; KILL ALL                       83327
MOVESEV  MVC   VSPREV,2(R2)  SAVE CURRENT SPANNING BITS          83326
MOVESEG  AR    R2,R0         SET TO DATA PORTION OF SEGMENT      83327
         SR    R3,R0         AND SET DATA LENGTH                 83327
         BM    FLUSHALL      BAD - FLUSH PRIOR AND CURRENT       83327
         LR    R14,R4        COPY COPY START ADDRESS             83327
         LR    R15,R3        COPY LENGTH                         83327
         LA    R1,0(R14,R15)  GET LAST BYTE+1 OF OUTPUT          83327
         C     R1,WORKBEND   COMPARE TO END OF BUFFER(+1)        83327
         BH    FLUSHALL      TOO LARGE                           83327
         AR    R4,R3         ADDRESS FOR NEXT SEGMENT            83327
         AR    R5,R3         SIZE AFTER MOVE                     83327
         MVCL  R14,R2        COPY NEW SEGMENT                    83327
         TM    VSPREV,1      LAST ?                              83327
         BNZ   NEWSEGD       NO; READ NEXT SEGMENT               83327
*NEXT*   B     NEWREC        PROCESS NEW RECORD                  83327
         SPACE 1                                                 83327
NEWREC   XC    PFXMAP(PFXL),PFXMAP  CLEAR SORT KEY               90071
         LA    R5,4(,R5)     ALLOW FOR LENGTH OF LENGTH          90129
         STCM  R5,3,RDWOUT   COMPLETE RECORD DESCRIPTOR          90071
         XC    SEGCNTR,SEGCNTR  CLEAR SEGMENT COUNTER            83327
         INC   E15IN         ADD TO TOTAL RECORDS IN             83327
         TM    SMFRSV,X'39'  SYSTEM CODE FIELD OK ?              93013
         BZ    TESTLEN            BRANCH IF SO                   84171
BADLEN   INC   E15SBAD       ADD TO BAD RECORD COUNT             83327
         B     E15SKP        SKIP THIS RECORD                    83327
         SPACE 1                                                 90071
TESTLEN  IC    R1,SMFTYP     GET RECORD TYPE                     83327
         LA    R15,E15TIN    POINT TO INPUT ARRAY                83327
         BAL   R14,TYPECLR   COUNT AND TOTAL                     83327
         LM    R2,R3,@SIDLIST  GET ADDRESS/LENGTH OF SYSTEM IDS  83327
         LTR   R3,R3         ANY TO DO ?                         83327
         BNP   SKPCHK        NO; BYPASS CHECKING                 83327
         LA    R5,SIDPICK    SET FOR RECORD SELECTION            83327
         TM    FLAG,FGNOSID    ARE WE IN EXCLUDE MODE ?          83327
         BZ    CPULOOP       NO                                  83327
         LA    R5,SIDSKIP    SET FOR EXCLUSION ON MATCH          83327
CPULOOP  CLC   SMFCPU,0(R2)  MATCHING SYSTEM ID ?                83327
         EX    0,0(,R5)      EXECUTE BRANCH                      83327
         LA    R2,4(,R2)     NEXT TABLE ENTRY                    83327
         BCT   R3,CPULOOP    TRY ANOTHER                         83327
         EX    0,4(,R5)      TAKE FINAL TEST                     83327
         SPACE 1                                                 83327
SKIPSID  INC   E15SCPU       ADD TO TOTAL SKIPPED - BAD CPU ID   83327
         B     E15WALT            GO SKIP THIS RECORD            92043
         SPACE 1                                                 83327
SIDPICK  BE    SKPCHK   1/4  SID MATCH - CONTINUE WITH RECORD    83327
         B     SKIPSID  2/4  NONE MATCHED - SKIP RECORD          90068
SIDSKIP  BE    SKIPSID  3/4  SID MATCH - SKIP RECORD             83327
         B     SKPCHK   4/4  NONE MATCHED - PROCESS RECORD       83327
         SPACE 1                                                 90071
SKPCHK   SLR   R2,R2         CLEAR FOR TRT                       83327
         TRT   SMFTYP,SKIPTAB   IS RECORD TYPE DESIRED ?         92043
         LTR   R2,R2         TO BE SKIPPED ?                     92043
         BNZ   TYPSKIP       YES; DO SO                          92043
         TRT   SMFTYP,PROCTAB  GET PROCESS TABLE                 83327
         B     *+4(R2)       PROCESS ACCORDING TO CODE           83327
         B     TYPNONE       0 - ENTRY NOT IN TABLE              90071
         B     TYPJOB        4 - SET FOR JOB-TYPE TIME-STAMP     90071
         B     TYP30         8 - TYPE 30                         90071
         B     TYP32        12 - TYPE 32                         90071
*        B     TYPSKIP      16 - RECORD TO BE SKIPPED            83327
         SPACE 1                                                 90071
TYPSKIP  INC   E15SREQ       COUNT AS SKIPPED BY REQUEST         83327
         B     E15WALT                                           92043
TYPSDAT  INC   E15SDAT                                           90071
         B     E15WALT                                           92043
TYPSJOB  INC   E15SJOB                                           90071
E15WALT  PUT   SMFSKIP,RDWOUT  WRITE TO REJECTED RECORD FILE     92043
         SPACE 1                                                 92043
E15SKP   INC   E15STOT       ADD TO TOTAL RECORDS SKIPPED        83327
         SLR   R0,R0                                             90071
         ICM   R0,3,RDWOUT   GET RECORD SIZE                     90071
         C     R0,MAXSKIP     GET LARGEST NOT WRITTEN            90071
         BNH   *+8                                               90071
         ST    R0,MAXSKIP     SAVE LARGEST SIZE                  90071
         B     NEWRECD       GO BUILD NEXT RECORD                83327
         SPACE 1                                                 90071
TYP30    ICM   R0,3,38+RDWOUT  ID SECTION PRESENT ?              90071
         BZ    TYPNONE       NO                                  90071
         ICM   R1,15,32+RDWOUT  OFFSET TO ID SECTION             85312
TPCMPFX  LA    R1,RDWOUT(R1)   POINT TO ID SECTION               84171
         MVC   PFXACCT,24(R1)  SMF30UIF                          84171
         MVC   PFXRDAT,68(R1)  SMF30RSD                          84171
         MVC   PFXJOBN,0(R1)   SMF30JBN                          84171
         B     LONGCHEK      JOIN COMMON CODE                    85312
         SPACE 1                                                 84171
TYP32    ICM   R0,3,RDWOUT+38  SMF32ILN                          90071
         BZ    TYPNONE                                           90071
         ICM   R1,15,RDWOUT+32  SMF32IOF                         85312
         B     TPCMPFX                                           84171
         SPACE 1                                                 84171
TYPJOB   MVC   PFXACCT,SMFACCT    MOVE ACCOUNT NUMBER            90071
         MVC   PFXRDAT,SMFRDAT    MOVE READER DATE               90071
LONGCHEK LA    R15,PFXRDAT   GET DATE AND TIME                   85312
         BAL   R14,VALCHECK  CHECK DATE AND TIME                 85312
         B     BADLEN        SKIP THIS RECORD                    85312
         B     DATECHEK      GO TO CHECK DATE AND JOB SELECT     90071
         SPACE 1                                                 90071
TYPNONE  ICM   R0,15,SELNUM  ANY JOB SELECTION ?                 90071
         BNZ   TYPSJOB       YES; THIS IS NOT A JOB RECORD       90071
         ICM   R0,15,JOBNUM  ANY JOB SELECTION ?                 90071
         BNZ   TYPSJOB       YES; NO JOBNAME IN THIS RECORD      90071
         MVC   PFXRDAT,SMFHDAT  DEFAULT TO HEADER DATE           90071
         SPACE 1                                                 90071
***********************************************************************
*                                                                     *
*        REJECT RECORDS OUTSIDE OF DATE RANGE                         *
*                                                                     *
***********************************************************************
DATECHEK LA    R15,SMFHDAT   POINT TO DATE WRITTEN               90071
         BAL   R14,VALCHECK  CHECK IT                            90071
          B    BADLEN        BAD RECORD                          90071
         CP    SMFHDAT,BILSTART  ARE WE IN RANGE ?               90071
         BL    DATENOHD      NO                                  90071
         CP    SMFHDAT,BILSTEND  ?                               90071
         BNH   DATEPASS      YES                                 90071
DATENOHD CP    PFXRDAT,BILSTART  READER DATE IN RANGE ?          90071
         BL    TYPSDAT       NO; SKIP RECORD                     90071
         CP    PFXRDAT,BILSTEND  ?                               90071
         BH    TYPSDAT       NO                                  90071
         SPACE 1                                                 90071
***********************************************************************
*                                                                     *
*        SELECT RECORDS WITH THE SPECIFIED ACCOUNT ONLY               *
*                                                                     *
***********************************************************************
DATEPASS ICM   R0,15,SELNUM  ACCOUNT MATCHING REQUIRED ?         90071
         BZ    ACCTPASS      NO                                  90071
         LA    R1,SELACCT    POINT TO ACCOUNT MASK               90071
TESTACCT LR    R15,R1        COPY START                          90071
         LA    R14,4         SET MAXIMUM LENGTH                  90071
         LA    R2,PFXACCT                                        90071
         BAL   R9,CLCMASK    PERFORM MASKED COMPARE              92043
         BE    ACCTPASS      ACCOUNT MATCHED                     92043
TESTABAD LA    R1,4(,R1)     NEXT MASK ENTRY                     90071
         BCT   R0,TESTACCT   TRY IT                              90071
         B     TYPSJOB       NO MATCH FOUND                      90071
         SPACE 1                                                 90071
***********************************************************************
*                                                                     *
*        SELECT RECORDS WITH THE SPECIFIED JOBNAME ONLY               *
*                                                                     *
***********************************************************************
ACCTPASS ICM   R0,15,JOBNUM  JOBNAME MATCHING REQUIRED ?         90071
         BZ    JOBNPASS      NO                                  90071
         LA    R1,JOBNAMES   POINT TO JOBNAME MASK               90071
TESTJOBN LR    R15,R1        COPY START                          90071
         LA    R14,8         SET MAXIMUM LENGTH                  90071
         LA    R2,PFXJOBN                                        90071
         BAL   R9,CLCMASK    PERFORM MASKED COMPARE              92043
         BE    JOBNPASS      YES; FOUND A MATCH                  92043
TESTJBAD LA    R1,4(,R1)     NEXT MASK ENTRY                     90071
         BCT   R0,TESTJOBN   TRY IT                              90071
         B     TYPSJOB       NO MATCH FOUND                      90071
         SPACE 1                                                 92043
CLCMASK  CMASK MA=R15,ML=R14,L=R14,A=R2,RET=R9  MASKED COMPARE   92043
         SPACE 1                                                 90071
***********************************************************************
*                                                                     *
*        WRITE OUTPUT RECORD AND COLLECT STATISTICAL DATA             *
*                                                                     *
***********************************************************************
JOBNPASS INC   MOUTOUT       COUNT RECORDS WRITTEN               83327
         LA    R4,RDWOUT     GET RECORD ADDRESS                  90071
         LM    R14,R15,TOTBYTES  GET BYTE COUNT                  83327
         SLR   R0,R0                                             83327
         ICM   R0,3,RDWOUT   GET RECORD SIZE                     83327
         C     R0,MAXOUT     GET LARGEST WRITTEN                 83327
         BNH   *+8                                               83327
         ST    R0,MAXOUT     SAVE LARGEST SIZE                   83327
         ALR   R15,R0        INCREASE COUNT                      83327
         BC    12,*+8        BRANCH IF NO CARRY                  83327
         AH    R14,=H'1'     PROPAGATE CARRY                     83327
         STM   R14,R15,TOTBYTES  STASH BYTE COUNT BACK           83327
         IC    R1,5(,R4)     GET RECORD TYPE                     83327
         LA    R15,MOUTTOUT  POINT TO ARRAY                      90071
         BAL   R14,TYPECLR   COUNT AND TOTAL                     83327
         LA    R14,4                                             83327
         LA    R15,OCCSIDND-4  POINT TO LAST SYSTEM ID ENTRY     83327
         SLR   R0,R0         COMPARE                             83327
         LA    R1,OCCSID     TABLE OF SYSTEM ID OCCURENCES       83327
         ICM   R2,15,SMFCPU-RDWOUT(R4)  GET THE SYSTEM ID        83327
         BZ    REFDAT        SKIP IF ZERO                        83327
REFCPU   C     R2,0(,R1)     MATCH ?                             83327
         BE    REFDAT        YES; DO DATE                        83327
         C     R0,0(,R1)     SPARE ENTRY ?                       83327
         BNE   REFCPUX       NO; BUMP                            83327
         ST    R2,0(,R1)     STASH IT                            83327
         B     REFDAT        COMPARE DATE                        83327
REFCPUX  BXLE  R1,R14,REFCPU   TRY AGAIN                         83327
REFDAT   ICM   R2,15,SMFHDAT  ANY DATE IN RECORD ?               90071
         BZ    E35PRIME      NO; SKIP DATE CHECK                 83327
         BAL   R9,REFDATE    DO IT                               83327
         ICM   R2,15,PFXRDAT  GET THE READER DATE                90071
         BZ    E35PRIME                                          83327
         LA    R9,E35PRIME   SET RETURN ADDRESS                  83327
         SPACE 1                                                 83327
REFDATE  ICM   R0,15,REFDLO  LOW DATE EVER SET ?                 83327
         BZ    REFDATEL      NO; FORCE IT                        83327
         CR    R2,R0         IS NEW ONE LOWER ?                  83327
         BNL   REFDATEH      NO                                  83327
REFDATEL ST    R2,REFDLO     SAVE LOW DATE                       83327
REFDATEH C     R2,REFDHI     AFTER LATE DATE ?                   83327
         BNHR  R9            NO; RETURN                          83327
         ST    R2,REFDHI     SAVE HIGH DATE                      83327
         BR    R9                                                83327
E35PRIME PUT   SMFOUT,RDWOUT  WRITE THIS RECORD                  90071
         B     INPLOOP       PROCESS ANOTHER RECORD              90071
         SPACE 2                                                 90071
SYNAD    OI    FLAG,FGIOERR  INDICATE I/O ERROR ON GET           83327
RET14    BR    R14           RETURN                              83327
         SPACE 1                                                 83327
GETINP   LM    R14,R15,INPBUFF  GET INPUT BUFFER ADDRESS/SIZE    83327
         LTR   R15,R15       ANY TO DO ?                         83327
         BP    GETINN        YES                                 83327
GETING   GET   SMFIN         GET ANOTHER BLOCK                   83327
         XC    INPBUFF(8),INPBUFF                                83327
         TM    FLAG,FGIOERR  I/O ERROR ?                         83327
         BNZR  R9            YES; RETURN TO CALLER               83327
         LR    R14,R1        COPY ADDRESS                        83327
         SLR   R15,R15       CLEAR                               83327
         ICM   R15,3,DCBLRECL-IHADCB+SMFIN  GET THE SIZE         83327
         BZ    GETING        IGNORE ZERO SIZE - GAP ERROR ?      83327
         BM    GETINF        ON A TAPE DRIVE ?                   83327
         CLM   R15,3,DCBBLKSI-IHADCB+SMFIN  VALID ?              83327
         BH    GETINF        NO; FAIL                            83327
         ICM   R0,15,0(R14)  GET THE BLOCK DESCRIPTOR            83327
         CLM   R0,B'0011',ZEROES  ZERO LOW ?                     83327
         BNE   GETINF        NO; FAIL                            83327
         SRL   R0,16         FLIP                                83327
         CR    R0,R15        LOGICAL SIZE MATCHES PHYSICAL ?     83327
         BNH   GETINB        YES; OR LOWER (VS QSAM)             83327
GETINF   L     R15,SEGCNTR   COUNT AS A BAD SEGMENT              83327
         AH    R15,=H'1'                                         83327
         A     R15,SEGSKIP   SEGMENTS SKIPPED                    83327
         ST    R15,SEGSKIP                                       83327
         SLR   R15,R15                                           83327
         ST    R15,SEGCNTR                                       83327
         STC   R15,VSPREV                                        83327
         LA    R4,SMFRSV     START A NEW BLOCK                   83327
         B     GETING        READ ANOTHER BLOCK                  83327
GETINB   LR    R15,R0        SET SIZE OF BLOCK                   83327
         LA    R0,4          SIZE OF BLOCK DESCRIPTOR WORD       83327
         AR    R14,R0        UP ADDRESS                          83327
         SR    R15,R0        NEW SIZE                            83327
GETINN   CH    R15,=H'2'     LONG ENOUGH FOR SDW ?               84031
         BL    GETINV        NO                                  84031
         LR    R1,R14        RETURN SEGMENT ADDRESS TO USER      84031
         SLR   R0,R0                                             83327
         ICM   R0,3,0(R14)   GET NEW SDW                         83327
         BNP   GETINV        INVALID ?                           84031
         CH    R0,=H'4'      AT LEAST MINIMUM ?                  83327
         BL    GETINF        NO                                  83327
         AR    R14,R0                                            83327
         SR    R15,R0        AFTER THIS ONE                      83327
         BM    GETINF        TOO LONG                            83327
         STM   R14,R15,INPBUFF  SAVE                             83327
         BR    R9            RETURN TO CALLER                    83327
         SPACE 1                                                 84031
GETINV   CLI   0(R14),X'80'  VSAM END OF BLOCK ?                 84031
         BNE   GETINF        NO; ERROR                           84031
         B     GETING        YES; GET NEXT BLOCK                 84031
         SPACE 2                                                 90071
*        END OF INPUT PROCESSING ROUTINE                         90071
         SPACE 1                                                 90071
INEOF    CLOSE (SMFIN,LEAVE,SMFOUT,LEAVE,SMFSKIP,LEAVE) CLOSE SMF FILES
         XC    DB,DB         CLEAR                               83327
         L     R0,E15SBAD    SKIPS DUE TO LENGTH/FLAG ?          83327
         A     R0,MINSBAD                                        83327
         BZ    SUMTEST       NO                                  83327
SUMSET4  OI    CCODE+L'CCODE-1,4  SET LEVEL 4 ERROR              83327
SUMTEST  ICM   R0,15,MOUTOUT  ANY OUTPUT ?                       83327
         BZ    SUMSET8       NO; ERROR                           83327
         L     R0,SEGSKIP    GET SKIP COUNT                      83327
         A     R0,SEGIOERR   ADD ERRORS                          83327
         BZ    SUMNOERR      SKIP IF NO ERRORS                   83327
SUMSET8  OI    CCODE+L'CCODE-1,8  SET NOT SO MINOR ERROR         83327
SUMNOERR OI    REFDLO+L'REFDLO-1,X'0F'  MAKE POSITIVE SIGN       83327
         OI    REFDHI+L'REFDHI-1,X'0F'                           83327
         LM    R14,R15,TOTBYTES  GET TOTAL BYTES WRITTEN         83327
         ICM   R0,15,MOUTOUT GET NUMBER OF RECORDS WRITTEN       83327
         BNP   SUMMPRNT      LEAVE DB SET TO ZERO                83327
         DR    R14,R0        GET AVERAGE BLOCK SIZE              83327
         ST    R15,DB        SAVE FOR PRINT                      83327
SUMMPRNT PRTLIST SUMOUT,DEV=(1,2)                                83327
         LM    R2,R3,@SIDLIST  GET SYSTEM ID LIST                83327
         LTR   R3,R3         ANY ?                               83327
         BNP   NOSIDSEL      NO; NO SELECTION                    83327
         PRTLIST MSGSIDSL    PRINT HEADER                        83327
SIDSHOW  PRTLIST MSGSID      PRINT NEXT ID                       83327
         LA    R2,4(,R2)     SKIP TO NEXT                        83327
         BCT   R3,SIDSHOW    REPEAT                              83327
NOSIDSEL LA    R2,OCCSID     GET LIST OF IDS FOUND               83327
         LA    R3,(OCCSIDND-OCCSID)/4  MAXIMUM TO CHECK          83327
         ICM   R0,15,0(R2)   ANY AT ALL ?                        83327
         BZ    SHOWSIDN      NO; SKIP                            83327
         PRTLIST MSGSIDOC    PRINT HEADER                        83327
SHOWSID  ICM   R0,15,0(R2)   LAST ENTRY ?                        83327
         BZ    SHOWSIDN      YES                                 83327
         PRTLIST MSGSID      PRINT ID                            83327
         LA    R2,4(,R2)                                         83327
         BCT   R3,SHOWSID    REPEAT FOR ALL                      83327
SHOWSIDN PRTLIST HEADOUT,TITLE=3                                 83327
         LA    R2,256        NUMBER OF ENTRIES TO DO             83327
         LA    R3,E15TIN     POINT TO DAILY INPUT                83327
         LA    R4,MOUTTOUT   MASTER OUTPUT                       90071
PRINT2   L     R0,0(,R3)                                         83327
         O     R0,0(,R4)                                         83327
         BZ    PRINT2UP      SKIP IF ALL ZERO                    83327
         LA    R1,256                                            83327
         SR    R1,R2         GET TYPE                            83327
         ST    R1,DB                                             83327
         PRTLIST MSGCOUNT    PRINT ONE LINE OF COUNTERS          83327
PRINT2UP LA    R0,4                                              83327
         AR    R3,R0                                             83327
         AR    R4,R0                                             83327
         BCT   R2,PRINT2                                         83327
         PRTSPACE 1                                              83327
         PRTLIST TOTCOUNT    PRINT TOTALS (INDEX 256)            83327
*NEXT*   B     ALLDONE                                           90071
         SPACE 1                                                 90071
ALLDONE  LH    R9,CCODE      GET CONDITION CODE                  83327
         PRTLIST MSGCCODE                                        83327
         SERVTERM ,          CLOSE AND FREE EVERYTHING           83327
         ENDM  RC=(R9)       RETURN TO SYSTEM                    83327
         SPACE 2                                                 90071
*        END OF MAIN PROGRAM                                     90071
         SPACE 1                                                 83327
*        ROUTINE TO COUNT SMF RECORDS BY FUNCTION AND TYPE       83327
*          IN: R15 - ARRAY BASE   R1 - SMF TYPE                  83327
*                                                                83327
TYPECLR  ICM   R1,B'1110',ZEROES  CLEAR THE UNUSED BYTES         83327
         SLL   R1,2          CONVERT TYPE TO OFFSET              83327
         LA    R1,0(R1,R15)  GET ARRAY ENTRY                     83327
         INC   0(R1),WORK=R0   COUNT ENTRY BY TYPE               83327
         LA    R15,4*256(,R15)  SPACE TO TOTAL                   83327
         INC   0(R15),WORK=R0                                    83327
         BR    R14           RETURN TO CALLER                    83327
         SPACE 1                                                 85312
*        SUBROUTINE TO CHECK VALIDITY OF TIME AND DATE FIELDS    85312
*        RETURN - R14/BAD, 4(R14)/OK; IN DATE 0(R15), TIME 4(R15)
*                                                                85312
VALCHECK STM   R0,R1,DB      SAVE A LITTLE                       85312
*OLD*    ICM   R0,15,4(R15)  CHECK TIME                          85312
*OLD*    BMR   R14           INVALID                             85312
*OLD*    C     R0,=A(24*60*60*100)  NOT TOO HIGH ?               85312
*OLD*    BNLR  R14           TOO BAD                             85312
         CLI   0(R15),0      THREE-BYTE DATE FORMAT ?            85312
         BE    VALCHEK3      YES                                 85312
         CLI   0(R15),1      PHUNNY NEW DATE ?                   90071
         BE    VALCHEK3      CHECK REST                          90071
         CLI   0(R15),X'19'  ELSE FOUR-BYTE ?                    85312
         BE    VALCHEK4      YES                                 85312
         CLI   0(R15),X'20'  IF I LIVE TO SEE IT ?               85312
         BNER  R14           NO                                  85312
VALCHEK4 DS    0H            NOTHING SPECIAL                     85312
VALCHEK3 UNPK  DB2,0(4,R15)  UNPACK THE DATE                     85312
         LA    R0,7          CHECK SEVEN NUMERICS                85312
         LA    R1,DB2        FIRST TEST POSITION                 85312
VALCHEKL CLI   0(R1),C'0'    VALID ?                             85312
         BLR   R14           NO                                  85312
         CLI   0(R1),C'9'    VALID ?                             85312
         BHR   R14           NO                                  85312
         LA    R1,1(,R1)                                         85312
         BCT   R0,VALCHEKL   TRY NEXT ONE                        85312
         CLI   0(R1),X'C0'   PACKED PLUS ?                       85312
         BLR   R14           NO                                  85312
         CLI   0(R1),X'C9'                                       85312
         BNH   VALCHEKS      SET SIGN                            85312
         CLI   0(R1),C'0'    PREFERRED PLUS ?                    85312
         BLR   R14           NO; KILL                            85312
         CLI   0(R1),C'9'    DITTO ?                             85312
         BHR   R14           DITTO                               85312
VALCHEKS OI    3(R15),X'0F'  MAKE GOOD PLUS SIGN                 85312
         CP    2(2,R15),=P'1'  VALID DAY ?                       85312
         BLR   R14           NO                                  85312
         CP    2(2,R15),=P'366'  DITTO                           85312
         BHR   R14           DITTO                               85312
         LM    R0,R1,DB      RELOAD                              85312
         B     4(,R14)       RETURN                              85312
         SPACE 1                                                 84159
CONDATE  LM    R14,R0,BINMON GET MONTH/DAY/YEAR                  84159
         MH    R0,=H'10'     FINAGLE                             84159
         CVD   R0,DB         GET YY0S                            84159
         MVC   WORK(1),DB+6  MOVE YEAR                           84159
         MVI   MONFEB+1,28   RESET FEBRUARY DAYS                 84159
         TM    WORK,X'01'    ODD YEAR ?                          84159
         BNZ   CONDATEN      YES; NOT LEAP                       84159
         TM    WORK,X'12'    MULTIPLE OF FOUR ?                  84159
         BM    CONDATEN      NO                                  84159
         MVI   MONFEB+1,29                                       84159
CONDATEN SLL   R14,1         CONVERT MONTH TO OFFSET             84159
         LA    R1,MONTAB-4(R14)  CHANGE TO STOP ADDRESS          84159
         LA    R14,MONTAB-2  SET FOR JANUARY                     84159
         LA    R0,2          SET BXH INCREMENT                   84159
CONDATEL BXH   R14,R0,CONDATEC  QUIT IF DONE ADDING              84159
CONDATEA AH    R15,0(,R14)   ADD PRIOR MONTH'S DAYS IN           84159
         BXLE  R14,R0,CONDATEA  DO NEXT MONTH                    84159
CONDATEC CVD   R15,DB        CONVERT DAYS                        84159
         OI    DB+7,X'0F'    PLUS, POSITIVELY                    84159
         MVC   WORK+1(2),DB+6  TACK DAYS ON                      84159
         BR    R9            RETURN TO CALLER                    84159
         SPACE 2                                                 83327
         PUSH  USING                                             90071
         DROP  ,                                                 90071
OUTEX    LR    R8,R15                                            90071
         USING OUTEX,R8      MAKE LOCAL BASE                     90071
         USING DYNWORK,R10   RESPECIFY WORK AREA                 90071
         USING IHADCB,R1     DECLARE THE PASSED DCB              83327
         ICM   R0,3,DCBLRECL  ANY LRECL ?                        83327
         BNZ   *+10          YES; USE IT                         83327
         MVC   DCBLRECL,=H'32760'      USE MAX                   90071
         ICM   R0,3,DCBBLKSI  ANY BLOCKSIZE ?                    83327
         BNZ   *+10          YES; USE IT                         83327
         MVC   DCBBLKSI,DCBBLKSI-IHADCB+SMFIN   COPY FROM INPUT  90071
         ICM   R0,3,DCBBLKSI  ANY BLOCKSIZE NOW?                 90071
         BNZ   *+10          YES; USE IT                         83327
         MVC   DCBBLKSI,=H'32760'      USE MAX                   90071
         CLI   DCBRECFM,0    ANY RECFM AT ALL ?                  83327
         BNE   *+8           YES; TEST IT                        83327
         MVI   DCBRECFM,DCBRECV+DCBRECBR+DCBRECSB  SET VBS       83327
         TM    DCBRECFM,255-DCBRECV-DCBRECBR-DCBRECSB  FUNNY BITSINESS?
         BNZ   OUTEXBAD      YES; FAIL                           83327
         TM    DCBRECFM,DCBRECV  RECFM=V ?                       83327
         BZ    OUTEXBAD      NO; ERROR                           83327
         OI    DCBRECFM,DCBRECBR+DCBRECSB  MAKE (V)BS ANYWAY     83327
         BR    R14           RETURN TO CALLER                    83327
OUTEXBAD LA    R3,SMFNOUT-SMFOUT+IHADCB   POINT TO FILE NAME     92043
         PRTLIST MSGBADUT,DEV=(1,2)  SET FOR BAD OUTPUT          92043
         SERVTERM ,          CLOSE AND FREE                      83327
         ABEND 13            QUIT                                83327
         POP   USING                                             90071
         SPACE 2                                                 83327
         LTORG ,                                                 83327
         EJECT                                                   90071
*        CONSTANTS AND STUFF                                     90071
         DC    H'0'          MONTAB-2  MUST BE ZERO              84159
MONTAB   DC    H'31'         DAYS PER MONTH                      84159
MONFEB   DC    H'28'         SET TO 28 OR 29                     84159
         DC    H'31,30,31,30,31,31,30,31,30'                     84159
MONDEC   DC    H'31'         DECEMBER                            84159
MONAMES  DC    CL9'JANUARY'                                      84159
         DC    CL9'FEBRUARY'                                     84159
         DC    CL9'MARCH'                                        84159
         DC    CL9'APRIL'                                        84159
         DC    CL9'MAY'                                          84159
         DC    CL9'JUNE'                                         84159
         DC    CL9'JULY'                                         84159
         DC    CL9'AUGUST'                                       84159
         DC    CL9'SEPTEMBER'                                    84159
         DC    CL9'OCTOBER'                                      84159
         DC    CL9'NOVEMBER'                                     84159
         DC    CL9'DECEMBER'                                     84159
         SPACE 2                                                 90071
SMFIN    DCB   DDNAME=SMFIN,DSORG=PS,MACRF=(GL),EODAD=INEOF,           *
               OPTCD=Z,SYNAD=SYNAD,RECFM=U,LRECL=32760,BLKSIZE=32760
         SPACE 1                                                 90071
SMFOUT   DCB   DDNAME=SMFOUT,DSORG=PS,MACRF=(PM),BFTEK=S,EXLST=OUTEXL
SMFNOUT  DC    CL8'SMFOUT'                                       92043
SMFSKIP  DCB   DDNAME=SMFSKIP,DSORG=PS,MACRF=(PM),BFTEK=S,EXLST=OUTEXL
SMFNSKIP DC    CL8'SMFSKIP'                                      92043
OUTEXL   DC    0A(0),X'85',AL3(OUTEX)  OUTPUT EXIT               83327
         SPACE 1                                                 90071
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=5                        83327
CONSOLE  PRTWORK *CONSOLE                                        83327
         SPACE 1                                                 90071
         PRINT &PRTMAC                                           90071
PROCTAB  TRTSMF ,            PULL IN THE SMF RECORD DEFINITIONS  90068
         PRINT &PRTSYS                                           90071
         SPACE 1                                                 90071
HEAD1    VCON  '#SMFSELCT - INPUT/OUTPUT STATISTICS'             83327
         SPACE 1                                                 83327
MSGBADUT FDOPT NL,CC=C'0'                                        92043
         FDPRT '***** DCB PARAMETERS FOR',PAD                    92043
         FDPRT 0(R3),L'DCBDDNAM,DEB,PAD                          92043
         FDPRT 'UNUSABLE FOR VBS *****'                          92043
         FDPRT *END                                              92043
         SPACE 1                                                 83327
SUMOUT   FDOPT NL,CC=C'0'                                        83327
         FDPRT 'INPUT SMF STATISTICS :',NL                       90071
         FDOPT IND=10                                            83327
         FDCLC SEGIOERR,ZEROES,4,BE=LIBALL                       83327
         FDPRT SEGIOERR,I,RADJ,PAD,LEN=10                        83327
         FDPRT 'DATA CHECKS ON INPUT,'                           83327
LIBALL   FDCLC SEGSKIP,ZEROES,4,BE=LICALL                        83327
         FDPRT SEGSKIP,I,RADJ,PAD,LEN=10                         83327
         FDPRT 'SEGMENTS SKIPPED.'                               83327
LICALL   FDPRT E15IN,I,RADJ,PAD,NL,LEN=10                        83327
         FDPRT 'RECORDS IN,'                                     83327
         FDPRT MOUTOUT,I,RADJ,PAD,LEN=10                         90071
         FDPRT 'RECORDS OUT.'                                    83327
         FDCLC E15STOT,ZEROES,4,BE=LINALL                        83327
         FDPRT E15STOT,I,RADJ,PAD,NL,LEN=10                      83327
         FDPRT 'RECORDS BYPASSED FOR THE FOLLOWING REASONS:'     83327
         FDPRT E15SBAD,I,RADJ,PAD,NL,LEN=15                      83327
         FDPRT 'INVALID LENGTH OR SYSTEM CODE FIELD.'            83327
         FDPRT E15SCPU,I,RADJ,PAD,NL,LEN=15                      83327
         FDPRT 'REJECTED DUE TO SYSTEM ID'                       83327
         FDPRT E15SDAT,I,RADJ,PAD,NL,LEN=15                      90071
         FDPRT 'REJECTED DUE TO DATE SELECT'                     90071
         FDPRT E15SJOB,I,RADJ,PAD,NL,LEN=15                      90071
         FDPRT 'REJECTED DUE TO JOB/ACCT SELECT'                 90071
         FDPRT E15SREQ,I,RADJ,PAD,NL,LEN=15                      83327
         FDPRT 'SKIPPED VIA TABLE OF SMF TYPES.'                 83327
         FDGOTO LIPOUT                                           83327
LINALL   FDPRT 'NO RECORDS SKIPPED',RADJ,LEN=25                  83327
LIPOUT   FDOPT NL,CC=C'0'                                        83327
         FDOPT IND=0                                             83327
         FDPRT 'OUTPUT MASTER STATISTICS :',NL                   83327
         FDOPT IND=10                                            83327
         FDPRT MOUTOUT,I,RADJ,PAD,LEN=10                         83327
         FDPRT 'RECORDS ON NEW MASTER.'                          83327
         FDCLC E35DUPE,ZEROES,4,BE=LIVALL                        83327
         FDPRT E35DUPE,I,RADJ,PAD,LEN=10                         83327
         FDPRT 'DUPLICATE RECORDS SKIPPED.'                      83327
LIVALL   FDCLC MOUTOUT,ZEROES,4,BE=LIZALL  NO RECORDS, NO STATS  92043
         FDCLC DB,ZEROES,4,BE=LITALL  SKIP AVERAGE               83326
         FDPRT 'AVERAGE OUTPUT RECORD LENGTH IS',PAD,NL          83327
         FDPRT DB,4,I,PADL                                       83327
         FDPRT ','                                               83327
LITALL   FDPRT 'MAXIMUM SIZE IS',PAD                             83327
         FDPRT MAXOUT,I,PAD                                      83327
         FDCLC MAXSKIP,ZEROES,4,BE=LIDALL                        83327
         FDPRT '   THE LARGEST SKIPPED RECORD WAS'               83327
         FDPRT MAXSKIP,I,PAD                                     83327
         FDPRT 'BYTES LONG'                                      83327
LIDALL   FDPRT 'THE LOWEST OUTPUT DATE IS',NL                    83327
         FDPRT REFDLO,DATE,PAD                                   83327
         FDPRT '('                                               83327
         FDPRT REFDLO,DATJ                                       83327
         FDPRT ')    THE HIGHEST DATE IS',PADR                   83327
         FDPRT REFDHI,DATE,PAD                                   83327
         FDPRT '('                                               83327
         FDPRT REFDHI,DATJ                                       83327
         FDPRT ')'                                               83327
LIZALL   FDPRT *END                                              83327
         SPACE 1                                                 83327
TOTCOUNT FDPRT 'TOTALS :',NL,LEN=8                               83327
         FDGOTO MSGCOUNX                                         83327
MSGCOUNT FDPRT DB,4,I,RADJ,NL,LEN=8                              83327
MSGCOUNX FDPRT 0(R3),4,I,RADJ,LEN=18                             83327
         FDPRT 0(R4),4,I,RADJ,LEN=13                             83327
         FDPRT *END                                              83327
         SPACE 1                                                 83327
HEADOUT  FDPRT 'SMF TYPE',NL,LEN=8                               83327
         FDPRT 'SMF IN',RADJ,LEN=18                              90071
         FDPRT 'SMF OUT',RADJ,LEN=13                             90071
         FDPRT *END                                              83327
         SPACE 1                                                 83327
MSGNOINP VCON  '0***** SMFIN DD MUST BE SUPPLIED *****'          90071
MSGNODD  FDPRT '*****',NL                                        83327
         FDPRT DB,DEB,PAD                                        83327
         FDPRT 'DD CARD UNUSABLE *****'                          83327
         FDPRT *END                                              83327
MSGBADDD FDPRT '***** OPEN FAILED FOR',NL                        83327
         FDPRT DB,DEB,PAD                                        83327
         FDPRT 'FILE *****'                                      83327
         FDPRT *END                                              83327
MSGNOREG VCON  '0***** INSUFFICIENT STORAGE AVAILABLE *****'     83327
MSGSIDSL FDOPT NL,CC=C'0'                                        83327
         FDPRT 'INPUT RECORDS WILL BE',PAD                       83327
         FDTM  FLAG,FGNOSID,BZ=MSGSIDIN                          83327
         FDPRT 'EX'                                              83327
         FDGOTO MSGSIDCM                                         83327
MSGSIDIN FDPRT 'IN'                                              83327
MSGSIDCM FDPRT 'CLUDED FOR THE FOLLOWING IDS :',PADR             83327
         FDPRT *END                                              83327
MSGSIDOC FDOPT NL,CC=C'0'                                        83327
         FDPRT 'RECORDS WRITTEN FOR IDS :'                       83327
         FDPRT *END                                              83327
MSGSID   FDOPT IND=10                                            83327
         FDPRT 0(R2),4,PAD                                       83327
         FDPRT *END                                              83327
         SPACE 1                                                 83327
MSGCCODE FDCLC CCODE,ZEROES,2,BE=MSGCCODX                        83327
         FDOPT NL,CC=C'-'                                        83327
         FDPRT '***** COMPLETION CODE'                           83327
         FDPRT CCODE,I,PAD                                       83327
         FDPRT '*****'                                           83327
MSGCCODX FDPRT *END                                              83327
         SPACE 2                                                 90071
SAVE     DSECT ,             SAVE AND WORK AREA                  83327
DYNWORK  DS    0D            START OF GLOBAL DYNAMIC AREA        90071
@SERVICE DS    A             ADDRESS OF @SERVICE ROUTINE         83327
@PRINTER DS    A             ADDRESS OF @PRINTER ROUTINE         83327
@SIDLIST DS    A     1/2     ADDRESS OF SYSTEM ID LIST           83327
NUMSID   DS    F     2/2     NUMBER OF SYSTEM IDS IN LIST        83327
DDNAME   DS    D
DB       DS    D             WORK SPACE                          83327
DB2      DS    D               MORE                              83327
WORK     DC    D'0'    2/2   OVERFLOW WORK AREA                  84159
TOTBYTES DS    D             BYTE COUNTER                        83327
ZEROES   DC    F'0'          CONSTANT                            83327
BINMON   DC    F'0'          MONTH FOR DATE ROUTINE              84159
BINDAY   DC    F'0'          DAY OF MONTH                        84159
BINYEAR  DC    F'0'          YEAR (MOD 100)                      84159
BINSAVE  DS    3F                                                90071
RECCOUNT DC    F'0'          NUMBER OF RECORDS IN FILE AFTER HEADER
SELNUM   DC    F'0'          NUMBER OF ACCOUNTS TO CHECK         84159
SELACCT  DC    25CL4' '      ACCOUNT NUMBERS FROM PARM FIELD     84159
MAXACCT  EQU   (*-SELACCT)/L'SELACCT  MAXIMUM NUMBER ALLOWED     84159
JOBNUM   DC    F'0'          NUMBER OF JOBNAMES TO SELECT ON     92043
JOBMAX   EQU   ((MAXACCT+7)/2)  MAX JOBNAMES                     85217
JOBNAMES DC    (JOBMAX)CL8' '   REDEFINE                         85217
BILYEAR  DS    CL4           START YEAR                          90071
BILMONTH DS    CL9           START MONTH                         90071
RUNDATE  DC    PL3'0'        DATE OF RUN                         84159
BILSTART DC    PL3'0'        BEGINNING ACCOUNT DATE              84159
BILSTEND DC    PL3'0'        END DATE FOR CURRENT RUN            84159
CCODE    DC    Y(0)          PROGRAM RETURN CODE                 83327
VSPREV   DC    X'00'         SEGMENT CONTROL FLAGS               83327
FLAG     DC    X'00'         PROCESSING FLAGS                    83327
FGIOERR  EQU   X'80'           SYNAD ENTERED                     83327
FGNOSID  EQU   X'08'           SYSTEM IDS ARE TO BE EXCLUDED     83327
FGSEOF   EQU   X'20'           SORT EOF                          83327
FGSREC   EQU   X'02'           SORT RECORD AVAILABLE             83327
FGMEOF   EQU   X'10'           MASTIN EOF                        83327
FGMREC   EQU   X'01'           MASTIN RECORD AVAILABLE           83327
OPTFLAG  DS    X             OPTIONS                             90071
OFGDEBUG EQU   X'80'           DEBUG MODE                        90071
OFGPART  EQU   X'40'           PARTIAL MONTH                     90071
OFGDATE  EQU   X'01'           DATE PROCESSED                    92043
INPBUFF  DS    2A                                                90071
OUTBUFF  DS    A                                                 90071
WORKBUFF DS    A                                                 90071
WORKBEND DS    A             END OF BUFFER                       83327
MASTBUFF DS    A                                                 90071
SEGCNTR  DS    A             CONSOLIDATED SEGMENT COUNT          83327
SEGSKIP  DS    A             NUMBER OF SEGMENTS SKIPPED DUE TO ERRORS
SEGIOERR DS    A             SEGMENTS IN ERROR                   83327
E35DUPE  DS    A             DUPLICATE RECORDS                   83327
MAXSKIP  DS    A             SIZE OF LARGEST RECORD SKIPPED      83327
MAXOUT   DS    A             SIZE OF LARGEST RECORD WRITTEN      83327
REFDLO   DS    PL4           LOWEST DATE WRITTEN                 83327
REFDHI   DS    PL4           HIGHEST DATE WRITTEN                83327
OCCSID   DS    64CL4         SYSTEM IDS WRITTEN                  83327
OCCSIDND EQU   *               END OF LIST                       83327
*        THE FOLLOWING COUNTERS ARE SEQUENCE DEPENDENT           83327
E15IN    DC    F'0'     1/6                                      90071
E15OUT   DC    F'0'     2/6                                      90071
E15STOT  DC    F'0'     3/6                                      90071
E15SBAD  DC    F'0'     4/6                                      90071
E15SCPU  DC    F'0'     5/6                                      90071
E15SREQ  DC    F'0'     6/6                                      90071
E15SDAT  DC    F'0'     7/6                                      90071
E15SJOB  DC    F'0'     8/6                                      90071
MININ    DC    F'0'     1/6                                      90071
MINOUT   DC    F'0'     2/6                                      90071
MINSTOT  DC    F'0'     3/6                                      90071
MINSBAD  DC    F'0'     4/6                                      90071
MINSCPU  DC    F'0'     5/6                                      90071
MINSREQ  DC    F'0'     6/6                                      90071
MOUTOUT  DC    F'0'                                              90071
*        END OF SEQUENCE DEPENDENT CONSTANTS                     83327
         SPACE 1                                                 83327
E15TIN   DC    257F'0'                                           83327
MOUTTOUT DC    257F'0'                                           83327
SKIPTAB  DC    XL256'0'      0-PROCESS, 4-SKIP RECORD TYPE       92043
SAVEND   EQU   *             LENGTH OF WORK AREA                 83327
         SPACE 2                                                 90071
*        MAP SORT PREFIX (OUTBUFF), AND SMF RECORD (WORKBUFF)    90071
         SPACE 1                                                 90071
PFXMAP   DSECT                    MAP SORT PREFIX                90071
PFXACCT  DS    CL8                ACCOUNT NUMBER                 90071
PFXRDAT  DS    CL4                READER DATE                    90071
PFXJOBN  DS    CL8                JOBNAME                        90071
RDWOUT   DS    XL4             RDW FOR OUTPUT                    90071
PFXL     EQU   *-PFXMAP           PREFIX LENGTH                  90071
PFXOHEAD EQU   (((PFXL+7)/8)*8)     LENGTH FOR GETMAIN OVERHEAD  83327
         SPACE 1                                                 90071
*        THIS PORTION MAPS WORKBUFF - ACTUAL RECORD - RDW        90071
         SPACE 1                                                 90071
SMFRSV   DS    C                  SYSTEM CODE FIELD              90071
SMFTYP   DS    C                  RECORD TYPE                    90071
SMFHTIM  DS    CL4                HEADER TIME                    90071
SMFHDAT  DS    CL4                HEADER DATE                    90071
SMFCPU   DS    CL4                CPU ID/MODEL                   90071
SMFJOBN  DS    CL8                JOBNAME                        90071
SMFRTIM  DS    CL4                READER TIME                    90071
SMFRDAT  DS    CL4                READER DATE                    90071
SMFACCT  DS    CL8                ACCOUNT NUMBER                 90071
         SPACE 2                                                 90071
         DCBD  DSORG=PS,DEVD=TA                                  90071
         SPACE 1                                                 83327
         IEFUCBOB ,                                              83327
         CVT   DSECT=YES                                         94234
         IHACDE ,                                                94234
         END   ,
