BLKSI    TITLE 'S U B B L K S I  ***  BLOCKSIZE CALCULATION'
         COPY  OPTIONGB
         SPACE 2
         SYSPARM LIST=YES
         SPACE 2
         PRINT &PRTSOR
***********************************************************************
*                                                                     *
*    THIS PROGRAM PRODUCES BLOCKSIZES FOR SPECIFIC RECORD SIZES ON    *
*        A DASD DEVICE. THE CALCULATIONS ARE TAKEN FROM THE BLKDISK   *
*        PROGRAM WRITTEN BY BRUCE LELAND.                             *
*    IN: R0 - UCB ADDRESS                                             *
*        R1 - RECFM : KEYLEN : LRECL                                  *
*    OUT:  R15 - 0 OK; 4 MINOR ERROR;                                 *
*                8 UCB INVALID, LRECL INVALID                         *
*               12 PROGRAM ERROR                                      *
*                                                                     *
*        R1 - SUGGESTED BLOCK SIZE                                    *
*        R0 - RECFM MAY BE UPDATED (E.G., BLOCKED ADDED/REMOVED)      *
*                                                                     *
***********************************************************************
SUBBLKSI PGMHEAD ZERO,BASE=R12,PARM=R1
         ST    R1,NEWFKLEN   RETURN R1, POSSIBLY MODIFIED; ZERO R0
         LTR   R8,R0         COPY UCB ADDRESS
         BZ    EXIT8           NONE?
         STCM  R1,4,KEYLEN+L'KEYLEN-1  USER'S KEY LENGTH
         STH   R1,LRECL+L'LRECL-2      USER'S RECORD LENGTH
         STCM  R1,8,RECFM
         SRL   R1,24+5       RIGHT JUSTIFY TOP THREE BITS
         IC    R1,RECFMTAB(R1)    GET EQUIVALENT INDEX
         STC   R1,RECIX      SAVE IT
         ICM   R0,15,LRECL   ANY INPUT ?
         BZ    EXIT8LR       NO; QUIT
         C     R0,=X'00008000'    LRECL=X?
         BNL   SPANTEST      YES; SPECIAL HANDLING
         CH    R0,=H'32760'  EXCEEDS SYSTEM MAXIMUM ?
         BH    EXIT8LR       YES; INVALID
         B     DEVCHECK      LOOK AT DEVICE TYPE
SPANTEST CLI   RECIX,4       RECFM=V ?
         BNE   EXIT8LR       NO; INVALID LRECL/RECFM COMBO
         LA    R0,1
         ST    R0,LRECL      GET TWO BLOCKS PER TRACK OR WHATEVER
         B     DEVCHECK      AND PROCEED
         SPACE 1
*                ?  ?T V  VT F  FT U  UT
RECFMTAB DC    X'00,00,04,04,08,08,00,00'
         SPACE 1
*  SOME OF THE FOLLOWING CODE WAS COPIED FORM CBT TAPE #323, FILE 296 *
*    AND UPDATED FROM #469, FILE 296.                                 *
*  ORIGINAL PROGRAM BLK3330, BLKDISK, ETC. BY A. BRUCE LELAND         *
         SPACE 1
         USING UCBOB,R8
DEVCHECK SR    R2,R2
         MVC   DEVTYPE,UCBTBYT4
         NI    DEVTYPE,DVCTYPMK   MASK UNWANTED BITS
         CLI   UCBTBYT3,UCB3DACC  DIRECT ACCESS ?
         BE    DEVDASD              YES
         MVICC 4                  SET MINOR ERROR
         MVC   NEWBLKSI,LRECL     SET UNBLOCKED
         ZI    NEWBLKSI,X'10'     RESET BLOCKED
         CLI   UCBTBYT3,UCB3TAPE  TAPE OR CARTRIDGE DEVICE ?
         BNE   FIXUP                LEAVE UNBLOCKED
         LH    R1,=H'32760'       MAX MVS BLOCK
         ST    R1,NEWBLKSI                                      GP11156
         B     FIXUP
         SPACE 1
DEVDASD  BAL   R9,DEVCALC
         SPACE 1
*---------------------------------------------------------------------*
*   FIX UP RECFM TO MATCH BLKSIZE AND RECORD LENGTH CHANGES           *
*---------------------------------------------------------------------*
FIXUP    L     R9,NEWBLKSI   LOAD PROPOSED BLOCK SIZE           GP11156
         S     R9,KEYLEN     DECREASE BY KEY LENGTH
         BNP   EXIT8LR       INVALID RECORD LENGTH
         L     R7,LRECL      GET RECORD LENGTH
         CLI   RECIX,4       BRANCH BY TYPE
         BL    FIXUPU        RECFM=U
         BE    FIXUPV        RECFM=V AND RECFM=D
         SR    R8,R8
         DR    R8,R7
         LTR   R9,R9         AT LEAST ONE ?
         BP    *+8           SHOULD NOT HAPPEN ?
         LA    R9,1
         MR    R8,R7         MAKE MULTIPLE FOR NON-DASD PATH
         ZI    NEWFKLEN,X'10'   SET UNBLOCKED
         CR    R9,R7         UNBLOCKED ?
         BE    PGMEXIT       YES
         OI    NEWFKLEN,X'10'   SET BLOCKED
         B     PGMEXIT       AND GET OUT
         SPACE 1
FIXUPV   SH    R9,=H'4'      ALLOW FOR BDW
         OI    NEWFKLEN,X'10'  SET BLOCKED
         CR    R7,R9         RECORD LARGER THAN BLOCK ?
         BNH   PGMEXIT       NO; WE'RE OK
         OI    NEWFKLEN,X'08'   ADD SPANNED
         B     PGMEXIT
         SPACE 1
FIXUPU   TM    NEWFKLEN,X'C0'  REALLY RECFM=U ?
         BNO   PGMEXIT       NO; LEAVE IT ALONE
         STH   R9,NEWBLKSI+2   RETURN BLOCK SIZE AS RECORD LENGTH
         B     PGMEXIT
         SPACE 1
EXIT8LR  MVC   NEWBLKSI+2(2),ZEROES    LRECL EXCEEDS DEVICE MAXIMUM
EXIT8    MVICC 8
         B     PGMEXIT
         SPACE 1
EXIT12   MVICC 12
         SPACE 1
PGMEXIT  PGMEXIT COPYRET=(RETCODE,12)  RETURN R15, R0, R1
         SPACE 5
***
***      DEVCALC COMPUTATIONS
***
         SPACE 1
DEVCALC  LA    R5,1          RECORDS PER BLOCK
         XC    NEWBLKSI,NEWBLKSI   SET FOR NO FIT               GP11156
BLOKLOOP L     R7,LRECL      GET RECORD LENGTH
         MR    R6,R5         GET NEXT BLOCK SIZE
         CH    R7,=H'32760'  STILL VALID ?
         BHR   R9            NO; RETURN TO CALLER
         LR    R0,R7         COPY
         ICM   R0,B'0100',KEYLEN+3    K  OF RKDD
         ICM   R0,B'1000',=X'01'      R  OF RKDD
         TRKCALC FUNCTN=TRKCAP,UCB=(R8),REGSAVE=YES,RKDD=(0),          *
               MF=(E,CALCLIST)
         LTR   R1,R0         COPY NUMBER OF BLOCKS * NUMBER OF RECORDS
         BNPR  R9            NONE; RETURN
         MR    R0,R5
         LA    R5,1(,R5)     INCREMENT BLOCKING FACTOR
         C     R1,OPTVALUE   WORSE THAN BEFORE ?
         BL    BLOKLOOP      YES; TRY AGAIN
         ST    R1,OPTVALUE
         ST    R7,NEWBLKSI    TENTATIVE RETURN                  GP11156
         B     BLOKLOOP
         SPACE 5
         SPACE 1
SAVE     DSECT ,             DYNAMIC WORK AND SAVE AREA
ZEROES   DC    D'0'
CALCLIST TRKCALC MF=L
         SERVDEFS ,          EXPAND SERVICE ADDRESSES
NEWBLKSI EQU   RSNCODE,4,C'F'     NEW BLOCKSIZE FIELD           GP11156
NEWFKLEN EQU   RR1CODE,4,C'F'     NEW RECFM/KEYLEN/LRECL        GP11156
KEYLEN   DC    F'0'             KEY LENGTH
LRECL    DC    F'0'             LRECL
OPTVALUE DC    F'0'          USAGE FOR THAT SIZE
RECFM    DC    X'00'         RECORD FORMAT
RECIX    DC    X'00'         RECORD FORMAT U-0 V-4 F-8
DEVTYPE  DC    X'00'         UCBTBYT4 MASKED
SAVEEND  EQU   *
         SPACE 2
         PRINT NOGEN
         CVT   DSECT=YES
         IEFUCBOB ,
         IHADVCT ,
         END   ,
