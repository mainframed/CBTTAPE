@SVC     TITLE '@ S E R V I C E  ***  INITIALIZATION AND HOUSEKEEPING'
         MACRO ,                                                 90337
&NM      AUCH  &EXIT                                             90337
         LCLA  &I                                                90337
&I       SETA  &SYSNDX                                           90337
&NM      TESTAUTH FCTN=1     ARE WE AUTHORIZED ?                 90337
         BXLE  R15,R15,ZZZZ&I                                    90337
         L     R15,PSATOLD-PSA  GET MY TCB                       90337
         TM    TCBPKF-TCB(R15),X'80'  KEY 8 OR LARGER ?          90337
         BNZ   &EXIT         NO; TAKE EXIT                       90337
ZZZZ&I   DS    0H                                                90337
         MEND  ,                                                 90337
         MACRO ,                                                 82108
&NM      JDYN  &J,&D,&SKIP=                                      82108
         LCLA  &I                                                82108
         LCLB  &F1,&F2                                           82108
         LCLC  &L                                                82108
         AIF   (T'&SKIP EQ 'O').NOSKIP                           82108
&F1      SETB  ('&SKIP' EQ 'BLANK')    SKIP IF BLANK OR ZERO     82108
&F2      SETB  ('&SKIP' EQ 'ZERO')     SKIP IF ZERO              82108
&I       SETA  2*&F1+&F2                                         82108
         AIF   (&I EQ 1 OR &I EQ 2).NOSKIP                       82108
&I       SETA  2                                                 82108
         MNOTE 4,'SKIP=&SKIP INVALID; ''BLANK'' ASSUMED'         82108
.NOSKIP  ANOP  ,                                                 82108
&L       SETC  'L'''                                             82108
&NM      DC    AL.2(&I),AL.6(&L.JFC&J),AL1(JFC&J-INFMJFCB,DAL&D) 82108
         MEND  ,                                                 82108
         SPACE 1                                                 83100
         MACRO ,                                                 86250
&NM      DDYN  &J,&D,&SKIP=,&L=8                                 86250
         LCLA  &I                                                86250
         LCLB  &F1,&F2                                           86250
         AIF   (T'&SKIP EQ 'O').NOSKIP                           86250
&F1      SETB  ('&SKIP' EQ 'BLANK')    SKIP IF BLANK OR ZERO     86250
&F2      SETB  ('&SKIP' EQ 'ZERO')     SKIP IF ZERO              86250
&I       SETA  2*&F1+&F2                                         86250
         AIF   (&I EQ 1 OR &I EQ 2).NOSKIP                       86250
&I       SETA  2                                                 86250
         MNOTE 4,'SKIP=&SKIP INVALID; ''BLANK'' ASSUMED'         86250
.NOSKIP  ANOP  ,                                                 86250
&NM      DC    AL.2(&I),AL.6(&L),AL1(&J,DAL&D)                   86250
         MEND  ,                                                 86250
         SPACE 1                                                 86250
         PUNCH '   ORDER @SERVICE(P) '  EASIER DEBUGGING        GP05020
         SPACE 1                                                 86250
         ACTR  99999                                             85070
         SPACE 2                                                 82108
         GBLB  &LOCO,&NOLO                                       92279
         SPACE 1                                                 81151
         COPY  OPTIONGB                                          81183
         SPACE 1                                                 81183
         SYSPARM LIST=YES                                        81183
         SPACE 1                                                 83100
         AIF   ('&LOCAL' NE 'MVS' AND '&LOCAL' NE 'RTI').NOFORCE
&SVC@SVC SETA  255           SUPPORT LOCAL CODE                 GP02317
.NOFORCE SPACE 1                                                 83100
         LCLC  &SVCNAME                                          83100
         LCLA  &ISVC,&JSVC                                       83100
         AIF   (&SVC@SVC EQ 0).NOSVC#S                           83100
&ISVC    SETA  (&SVC@SVC-(&SVC@SVC/10)*10)+1                     83100
&JSVC    SETA  &SVC@SVC/10                                       83100
&SVCNAME SETC  '{ABCDEFGHI'                                      83100
&SVCNAME SETC  'IGC00'.'&JSVC'.'&SVCNAME'(&ISVC,1)               83100
.NOSVC#S SPACE 2                                                 83100
***********************************************************************
*                                                                     *
*        COPYRIGHT 1981-1994  EXPERT SYSTEM PROGRAMMING, INC.         *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 2                                                 81151
@SERVICE RSECT ,             CSECT BEFORE EQUATES               GP99026
         SPACE 1                                                 81151
*        LOCAL OPTIONS                                           81151
&LOCO    SETB  ('&LOCAL' EQ 'PID')  SPECIAL LOCAL CODE           92279
         SPACE 1                                                 81151
WYLRECLN EQU   232           MAXIMUM WYLBUR LENGTH ACCEPTED      87179
*                 USE 133 FOR OBS WYLBUR < 5.1                   81151
*                 NEW NIH FORMAT IS 500 REGARDLESS               81151
*              MULTI-VOLUME LOCATE MAXIMA :                      85202
         GBLA  &WYLDMAX      MAXIMUM NUMBER OF WYLBUR VOLUMES (<=16)
         SPACE 1
         GBLC  &FLAGNAM                                         GP05120
&FLAGNAM SETC  'ASCBRCTF'    X/A AND LATER - X'01'              GP05120
         AIF   (&MVSXA).HAVEFG                                  GP05120
&FLAGNAM SETC  'ASCBFLG1'    SP AND EARLIER- X'80'              GP05120
.HAVEFG  SPACE 1                                                GP05120
&WYLDMAX SETA  10                                                85202
         EJECT
         PRINT &PRTSOR
*
*        THIS MODULE PERFORMS COMMON VALIDITY CHECKING FOR BATCH
*          CLEANUP PROGRAMS (HIGH ACCESS RATE).
*                                                                81151
*        THE CODE IS RE-ENTRANT THROUGH USE OF THE (CURRENT) TCB 81151
*        FIRST LEVEL SAVE AREA, THE FIRST WORD OF WHICH WILL HEAD
*        A CHAIN OF WORK AREAS.  THE SECOND WORD IN EACH WORK AREA
*        IDENTIFIES THE FUNCTION; E.G. 'SERV' FOR THE @SERVICE   81151
*        ROUTINE, 'PRTN' FOR @PRINTER WORK AREAS, ETC.           81151
*                                                                81151
*        COMMON CALLING CONVENTION :                             81151
*                                                                81151
*        R0  - HIGH TWO BYTES RESERVED                           81151
*              THIRD BYTE IS A FUNCTION MODIFIER                 81151
*              FOURTH BYTE IS THE FUNCTION CODE                  81151
*        R1  - HIGH BYTE IS (OPTIONAL) LENGTH/FLAG               81151
*              LOW THREE BYTES - PARM ADDRESS, VALUE, OR ZERO    81151
*        R15 - BASE                                              81151
*        R14 - RETURN ADDRESS                                    81151
*                                                                81151
*        THE PROGRAM MAY BE LOADED USING THE SERVINIT MACRO; CALLS
*        MAY BE MADE WITH THE SERVCALL MACRO; TERMINATION WITH   81151
*        SERVTERM.                                               81151
*                                                                81151
*        FUNCTIONS ARE MAPPED USING ' COPY SERVFLAG'; THIS COPY IS
*        EXPANDED BY DEFAULT IN 'SERVINIT'.                      81151
*                                                                81151
*        RETURN CODES ARE PASSED IN R15 :                        81151
*                                                                81151
*        16 - INVALID CALLING PARAMETER (E.G. UNDEFINED FUNCTION),
*              R1=0 WHEN ADDRESS/VALUE IS REQUIRED, ETC.         81151
*                                                                81151
*         8 - ERROR IN SUPPLIED INFORMATION; FAILED VALIDITY CHECK...
*              R0 (LOW BYTE) CONTAINS ERROR CODE (SEE 'VR' AND 'VC'
*              EQUATES IN SERVFLAG FOR MEANING)                  81151
*                                                                81151
*         4 - ERROR IN SUPPLIED INFORMATION; R0 CONTAINS ERROR FLAG
*              OR ZERO (FOR SOME FUNCTIONS)                      81151
*                                                                81151
*         0 - SUCCESSFUL. R0 CONTAINS A FUNCTION DEPENDENT VALUE, E.G.
*              ZERO, AN ADDRESS, FLAGS, ETC.                     81151
*                                                                90273
***********************************************************************
*        ALL JES2 CONTROL BLOCK DEPENDENT CODE HAS BEEN SPLIT TO A    *
*        NEW GROUP OF MODULES.  THE SOURCE NAME IS @SRVJNNN, WHERE    *
*        "NNN" IS THE (HIGHEST) JES2 RELEASE UNDER WHICH THE CODE     *
*        ASSEMBLES AND RUNS CORRECTLY.  THE MODULES ARE LINK-EDITED   *
*        WITH A NAME OF THE FORM @SRVJESX, WHERE "JESX" IS THE SUB-   *
*        SYSTEM NAME CORRESPONDING TO THAT SOURCE LEVEL; ONE LOAD MOD *
*        MAY HAVE ALIAS ENTRIES (E.G., JESA, JESB, JES2, ETC.). 90273 *
***********************************************************************
*  MAINTENANCE:                                                       *
*  2003-09-07  GYP  ADDED SAVE AREA PUSH-DOWN STACK. THIS ALLOWS      *
*                   LIMITED NESTING (E.G., SERVTERM CALLS @INPREAD    *
*                   CLOSE WHICH USES SERVCALL ALCFD)                  *
*                                                                     *
*  2004-02-25  GYP  FIXED 0C4 ON TCBFSA ACCESS UNDER TSO              *
*  2005-01-16  GYP  RETROFITTED TO RUN UNDER MVS 3.8J (AGAIN)         *
*                                                                     *
***********************************************************************
         SPACE 1
@SERVICE RSECT ,                                     REPLACED ON 82108
         AMODE 31            WHEEE                              GP99026
         RMODE 24            BOOOO                              GP99026
         B     SERVICE-@SERVICE(,R15)  SKIP AROUND ID            88360
         DC    AL1(L'SERVID)   LENGTH FOR SNAP                   81151
SERVID   DC    CL19'@SERVICE - &SYSDATE'                         83100
         SPLEVEL TEST        SET DEFAULT SP LEVEL                92297
         AIF   (&SVC@SVC EQ 0).NOSVCEN                           83100
         ENTRY &SVCNAME                                          83100
&SVCNAME BASR  R5,0          SET SVC ENTRY CODE                  83100
         USING *,R5          FUNNY BASE FOR A WHILE              83100
         B     SKIPSVID      SKIP SVC ID                        GP03129
         DC    C'*ESPSVC*'   ID FOR SUBSERV LOADER              GP03129
SKIPSVID LR    R7,R0         SAVE ENTRY VALUE                    83100
         LR    R9,R1                                             83100
         LTCB  R8            JUST IN CASE IT IS NOT SVC ENTRY    83100
         LA    R12,0(,R5)    LOAD CLEARED BASE                   85083
         SH    R12,=Y(&SVCNAME-@SERVICE+2)  SET TRUE BASE VALUE  85083
         DROP  R5                                                88360
         USING @SERVICE,R12  MAKE MAIN BASE                      88360
         AM31  WORK=R15      JUST IN CASE - GET HIGH            GP99026
         ICM   R4,15,SERVID+1  ID FIELD                          85083
         LTR   R0,R0         INITIALIZATION REQUEST ?            88360
         BNZ   SVCINDUP      NO; NO DUPLICATE CHECK              88360
         L     R2,TCBFSA-TCB(,R8)  POINT TO CHAIN HEAD           85083
         N     R2,=X'00FFFFFF'  CLEAN HIGH BYTE                 GP05016
SVCINLUP L     R6,0(,R2)     GET NEXT ENTRY                     GP04057
         LA    R6,0(,R6)     CLEAN IT                           GP04057
         LTR   R6,R6         AND TEST IT                        GP04057
         BZ    SVCINDUP      UNLESS NO MORE                      85083
         USING FSAWORK,R6                                        85083
         CL    R4,FSAID      SAME ID ?                           85083
         BE    SVCDUPL       YES; DON'T GETMAIN AGAIN            85083
         LR    R2,R6         SWAP                                85083
         B     SVCINLUP      TRY AGAIN                           85083
SVCINDUP LA    R2,FSALEN                                         83100
         LA    R15,FGSVC                                        GP03076
         STORAGE OBTAIN,LENGTH=(R2),SP=(15),COND=YES,LOC=BELOW  GP03076
         BXH   R15,R15,SVCFAILX                                  83100
         LR    R6,R1                                             83100
         CLRL  (R6),(R2)     CLEAR ENTIRE AREA                   83100
         ST    R8,FSATCB     SET TCB ADDRESS                     83100
         ST    R2,FSASPLEN   SET GETMAIN LENGTH                  83100
         MVI   SUBPOOL,FGSVC   SET SVC SUBPOOL/FLAG              83100
         MVC   FSAID,SERVID+1                                    83100
         LR    R0,R7         RESTORE ENTRY VALUES                83100
         LR    R1,R9                                             83100
         LA    R13,SAVEAREA   MAKE A FAKE SAVE AREA              83100
         LTR   R0,R0         ENTRY CODE ZERO ?                   88360
         BNZ   SVCCOMON      NO; GO TO COMMON PROCESSING         88360
         L     R7,TCBFSA-TCB(,R8)  POINT TO CHAIN HEAD           83100
         N     R7,=X'00FFFFFF'  CLEAN IT                        GP05016
         LR    R8,R6         COPY WORK AREA ADDRESS              83100
SVCRINLP L     R6,0(,R7)     GET NEXT ENTRY                     GP04057
         LA    R6,0(,R6)     CLEAN IT                           GP04057
         LTR   R6,R6         AND TEST IT                        GP04057
         BZ    SVCINNEW      YES; USE OUR AREA                   83100
         CL    R4,FSAID      SAME ID ?                           83100
         BE    SVCDUPL       YES; JUST QUIT                      83100
         LR    R7,R6         SWAP                                83100
         B     SVCRINLP      TRY AGAIN                           83100
SVCINNEW ST    R8,0(,R7)     LINK OUR AREA IN                    83100
         LR    R6,R8         COPY WORK AREA ADDRESS              83100
         MVI   SUBPOOL,0     RESET SVC OWNERSHIP                 83100
SVCDUPL  SR    R15,R15       SET GOOD COMPLETION                 85083
         LR    R1,R6         RETURN WORK AREA ADDRESS            83100
         LR    R0,R12        RETURN BASE ADDRESS                 83100
         SVC   3             RETURN                              83100
         DROP  R6                                                88359
.NOSVCEN SPACE 1                                                 83100
*ERVICE  BSM   R14,0         PRESERVE CALLER'S AMODE            GP99026
* BSM DUMMIED - SERVCALL NOW USES BASSM                         GP03076
SERVICE  STM   R14,R12,12(R13)                                   81151
         LR    R12,R15
         USING @SERVICE,R12                                      90154
         SR    R5,R5         INDICATE CALL ENTRY                 83100
         SPACE 1                                                 81151
*        LOCATE OR ESTABLISH WORK AREA                           81151
*                                                                81151
         LTCB  R8            CURRENT TCB                         83100
         AM31  WORK=R15      GET HIGH                           GP99026
         ICM   R4,15,SERVID+1  ID FIELD                          81151
         L     R7,TCBFSA-TCB(,R8)  POINT TO CHAIN HEAD           81151
         N     R7,=X'00FFFFFF'  FUNNY TSO STUFF ?               GP05016
SVCINITL L     R6,0(,R7)     GET NEXT ENTRY                     GP04057
         LA    R6,0(,R6)     CLEAN IT                           GP04057
         LTR   R6,R6         AND TEST IT                        GP04057
         BZ    SVCINGET      YES; GET A WORK AREA                81151
         USING FSAWORK,R6    DECLARE IT                          81151
         CL    R4,FSAID      SAME ID ?                           81151
         BE    SVCHAVE       YES; USE IT                         81151
         LR    R7,R6         SWAP                                81151
         B     SVCINITL      TRY AGAIN                           81151
SVCINGET LA    R2,FSALEN     GET LENGTH                          81151
         STORAGE OBTAIN,LENGTH=(R2),COND=YES,LOC=BELOW          GP03076
         BXLE  R15,R15,SVCINITW  CLEAR IT                        81151
SVCFAIL  LM    R14,R12,12(R13)  ERROR IN PROCESSING OR ENVIRONMENT
SVCFAILX LA    R15,16        SET MAJOR ERROR                     83100
         SR    R0,R0         INDICATE ENVIRONMENT (PGM ERROR)    81165
         BSM   0,R14         RETURN                             GP99026
         DROP  R6                                                81151
         USING FSAWORK,R7                                        81151
SVCINITW MVC   FSALINK-FSAWORK(4,R1),FSALINK  CHAIN OLD AREA     81151
         ST    R1,FSALINK    CHAIN THIS AHEAD OF OLD ONE         81151
         DROP  R7                                                81151
         USING FSAWORK,R6                                        81151
         LR    R6,R1         COPY THIS ONE TO PERMANENT REGISTER 81151
         LA    R14,4(,R1)    START TO CLEAR AFTER LINK FIELD     81151
         LR    R15,R2        GET LENGTH                          81151
         SH    R15,=Y(4)     ADJUST LENGTH                       81151
         SR    R0,R0                                             81151
         SR    R1,R1                                             81151
         MVCL  R14,R0        CLEAR IT                            81151
         ST    R4,FSAID      SET ID OF THIS AREA (@SVC)          81151
         ST    R2,FSASPLEN   SAVE GETMAIN LENGTH                 81151
         ST    R8,FSATCB     AND TCB                             81151
         LM    R0,R1,20(R13)   RELOAD ENTRY PARAMETERS           81151
SVCHAVE  LR    R9,R13        COPY USER'S SAVE AREA ADDRESS       81151
         LA    R13,SAVEAREA   AND ESTABLISH OURS                 81151
         DROP  R6            FSA HEADER NO LONGER ACCESSIBLE     81151
         USING SAVEAREA,R13                                      81151
         ST    R9,4(,R13)    MAKE BACK CHAIN                     81151
         ST    R13,8(,R9)      AND FORWARD CHAIN                 81151
SVCCOMON XC    RETCH,RETCH   CLEAR RETURN CODES                  83100
         STM   R0,R1,CALLR0  SAVE PARMS FOR QUICK ACCESS         81151
         LA    R15,255       ENTRY CODE MASK                     81151
         NR    R0,R15        MASK IT                             81151
         LTR   R5,R5         SVC ENTRY?                         GP99026
         BNZ   SVENT31       YES; AM31 EXPECTED                 GP99026
         TM    12(R9),X'80'  CALLER IN AMODE 31 (SAVED R14)?    GP99026
         BNZ   SVENT31       YES; HANDLE ENTRY PARMS            GP99026
         STCM  R1,8,CALLB0   TRANSFER LENGTH OPTION TO R0       GP99026
         LR    R7,R1         SAVE ENTRY PARM                    GP99026
         N     R7,=X'00FFFFFF'  CLEAR HIGH BYTE                 GP99026
         B     SVENTCM                                          GP99026
SVENT31  LA    R7,0(,R1)     COPY AND CLEAN ENTRY ADDRESS       GP99026
SVENTCM  ST    R7,RETR1      ALSO SAVE FOR R1 RETURN VALUE       81165
         LTR   R6,R0         SAVE FUNCTION VALUE                GP99026
         BZ    ENTCLOSE      TERMINATION ENTRY
         AIF   (NOT &LOCO).N0L0        SKIP LOCAL LOADS          81151
         ICM   R0,15,@USRVOLT   FIRST TIME ?
         BNZ   ENTRYNT1      NO
         L     R2,CVTPTR     GET CVT BACK                        92306
         ICM   R2,15,CVTUSER-CVTMAP(R2)  USERCVT EXTENSION ?     92306
         BZ    ENTRYNT1      NO ?                                92306
         MVC   @USRVOLT,UCLVOLT-USERCVT(R2)  COPY ADDRESS        92306
ENTRYNT1 LR    R0,R6         RESTORE ENTRY FUNCTION
.N0L0    LA    R10,EXIT      SET NORMAL RETURN ADDRESS           81151
         N     R0,=X'0000FFFF'  CLEAR HIGH BYTE                 GP03076
         BIX   ERR=BADENTRY,SRL=4,LOC=(GROUP0,GROUP1,GROUP2),W2=R14
         SPACE 1                                                 81151
ENTERROR DS    0H            ALIAS OF BADENTRY                   81151
BADENTRY MVI   RV,VRPARM     INVALID PARM/ENTRY CODE
BADRET16 MVI   RC,16         SET RETURN CODE 16
         B     EXIT          AND EXIT WITH ERROR
         SPACE 1
*  USED TO INDICATE ERROR IN FUNCTIONS NORMALLY RETURNING AN R0 VALUE
BADRET12 MVI   RC,12         SET RETURN CODE 12
         B     EXIT          AND EXIT WITH ERROR
         SPACE 1
BADCHAR  MVI   RV,VRSYNT     SYNTAX ERROR OR BAD CHARACTERS IN PARM
BADRET8  MVI   RC,8          SET RETURN CODE 8
         SPACE 1
EXIT     CLI   SUBPOOL,FGSVC SVC RETURN ?                        83100
         BE    ENTCLOSE      YES                                 83100
         LM    R15,R1,RETCH  GET RETURN CODES                    83100
         L     R13,4(,R13)
         L     R14,12(,R13)  GET RETURN ADDRESS
         LM    R2,R12,28(R13)  RESTORE OTHERS
         BSM   0,R14         RETURN TO CALLER                   GP99026
         SPACE 1
BADRET4  MVI   RC,4          SET MINOR ERROR
         B     EXIT          AND RETURN
         SPACE 1                                                 81359
BADWNAME MVI   RV,VRNWYL     NOT WYLBUR/LIBPAK NAME
         B     BADRET8       SET ERROR
         SPACE 1
BADLEVEL MVI   RV,VRDLON     SET TOO MANY INDEX LEVELS           81137
         B     BADRET8
         SPACE 1
BADPSWD  MVI   RV,VRNPSW     SET NO PASSWORD ENTRY
         B     BADRET8
         SPACE 1
BADOSNAM MVI   RV,VRNTOS     SHOW NOT VALID OS NAME
         B     BADRET8       SET BAD RETURN CODE
         SPACE 1                                                 81151
SHORTDSN MVI   RV,VRDLEN     SET BAD LENGTH                      81151
         B     BADRET8       DATASET TOO SHORT FOR SPECIAL NAME  81151
         SPACE 1                                                 81151
SHORTOSN MVI   RV,VRDLEN     SET BAD LENGTH (UNQUALIFIED NAME)   81151
         B     BADRET4       BUT USE LEVEL 4 ERROR CODE          81151
         SPACE 1                                                 81137
BADWINDX MVI   RV,VRNWYX     SET INVALID INDEX                   81137
         B     BADRET8       AND FAIL                            81137
         SPACE 2
GROUP0   BIX   ERR=BADENTRY,PFX=ENT,                                   *
               LOC=(CLOSE,INITG,LPALD,AUTHS,ERROR,UCBUM,TIOLP,SORTA,DVC*
               TS,CRMSV,CATTL,JESVC)                             90273
         SPACE 2
*        ENTRY CODE 0 - CLOSE AND FREE EVERYTHING
*        MODE=0   FULL CLOSE AND FREEMAIN                        81151
*        MODE=1   CLOSE/FREE EXCEPT @PRINTER AND @SERVICE WORK AREA
         SPACE 1
ENTCLOSE LA    R5,LOADLIST   GET MODULE LIST                     81151
         LA    R6,LOADADDR   GET ADDRESS LIST                    81151
         SR    R8,R8         SET CONSTANT ZERO                   81151
         ST    R8,WYLUCB     RESET PROTECT PROCESSING FLAG       81151
         LA    R8,LOADNUM      GET NUMBER IN LIST                81151
         CLI   CALLMODE,0    FULL CLOSE ?                        81151
         BE    ENTCLOSL      YES                                 81151
         CLI   SUBPOOL,FGSVC SVC ENTRY ?                         83100
         BE    ENTCLOSL      YES; FREE ALL                       83100
         BCTR  R8,0          ELSE LEAVE PRINTER OPEN             81151
         BCTR  R8,0          ELSE LEAVE SCREENS OPEN             87308
ENTCLOSL MVC   SAVEARE2(SAVEARE2-SAVEAREA),SAVEAREA  PUSH STACK GP03250
         STM   R5,R8,SAVEARE3  SEE IF THIS FIXES THINGS         GP05020
         BAS   R10,DELTMOD   DELETE MODULE                       81165
         LM    R5,R8,SAVEARE3  SEE IF THIS FIXES THINGS         GP05020
         MVC   SAVEAREA(SAVEARE2-SAVEAREA),SAVEARE2  POP STACK  GP03250
         LA    R5,LOADLENT(,R5)     BUMP THE NAME POINTER        81165
         LA    R6,L'LOADADDR(,R6)   BUMP THE ADDRESS POINTER    GP99026
         BCT   R8,ENTCLOSL   DO NEXT ONE                         81151
         LM    R3,R4,WYLFREEM     SEE IF GETMAIN DONE
         LTR   R3,R3
         BZ    CLOSENF       NO
         LTR   R4,R4
         BZ    CLOSENF
         SR    R15,R15                                          GP03076
         IC    R15,SUBPOOL                                      GP03076
         STORAGE RELEASE,ADDR=(R3),LENGTH=(R4),SP=(15),COND=YES GP03076
CLOSENF  XC    WYLFREEM(8),WYLFREEM  CLEAR IT
         ICM   R0,15,EXWCRMLN  ANY CSA GOTTEN ?                  85076
         BZ    CLOSENCH                                          85076
         L     R11,=A(BASCRMSV)  GET BASE FOR SCHEDULE           85076
         BAS   R14,ENTSCHFR-BASCRMSV(,R11)  CSA FREEMAIN ?       85076
         XC    RETR0,RETR0   CLEAR RETURN CODE AGAIN             85076
CLOSENCH L     R11,=A(DYNBASE)  MAKE BASE FOR CLOSEDYN           82108
         BAS   R10,CLOSEDYN-DYNBASE(,R11)  CLEAN TIOT            82108
         CLI   SUBPOOL,FGSVC SVC ENTRY ?                         83100
         BE    CLOSENFF                                          83100
         CLI   CALLMODE,0    FULL CLOSE ?                        81151
         BNE   EXIT          NO; RETAIN WORK AREA                81151
         L     R15,=V(SUBSERV0)  GET STORAGE RELEASE ROUTINE    GP03303
         BASR  R14,R15       RELEASE STORAGE AND T.S.A.         GP03303
CLOSENFF LR    R6,R13        GET SAVE AREA                       83100
         LA    R15,SAVEAREA-FSAWORK  GET HEADER LENGTH           81151
         SR    R6,R15                                            81151
         DROP  R13                                               81151
         USING FSAWORK,R6    DECLARE FULL AREA                   81151
         LM    R2,R3,FSASPLEN  GET LENGTH AND TCB BACK           81151
         CLI   SUBPOOL,FGSVC SVC ENTRY ?                         83100
         BE    EXITSVC       YES; SPECIAL CODE                   83100
         L     R3,TCBFSA-TCB(,R3)  POINT TO CHAIN HEAD           81151
         N     R3,=X'00FFFFFF'  FUNNY TSO STUFF ?               GP05016
CLOSFLP  CL    R6,0(,R3)     GET NEXT ENTRY                     GP04057
         BE    CLOSEFNC      YES; UNCHAIN                        81151
         LA    R3,0(,R3)     CLEAN IT                           GP04057
         L     R3,0(,R3)     TRY NEXT ONE                       GP99026
         LTR   R3,R3                                            GP04057
         BNZ   CLOSFLP                                           83100
         B     CLOSEFNF      DON'T BOMB ON ERROR                 83100
CLOSEFNC MVC   0(4,R3),FSALINK  UNCHAIN OURS                     81151
CLOSEFNF L     R13,SAVEAREA+4  GET USER'S SAVE AREA ADDRESS      83100
         FREEMAIN R,LV=(R2),A=(R6)  FREE OUR WORK SPACE          81151
*NEW*    LR    R15,R2        COPY SP/LEN                        GP03076
*NEW*    SRL   R15,24        SHIFT SP LOW                       GP03076
*NEW*    N     R2,=X'00FFFFFF'  KILL SP IN LENGTH               GP03076
*NEW*    STORAGE RELEASE,ADDR=(R6),LENGTH=(R2),SP=(15),COND=YES GP03076
         LM    R14,R12,12(R13)  RELOAD USER'S REGISTERS          81151
         SR    R15,R15                                           81151
         SR    R0,R0         SET RETURN CODES                    81151
         BSM   0,R14         RETURN                             GP99026
         SPACE 1                                                 83100
EXITSVC  LM    R7,R9,RETCH   SAVE RETURN REGISTERS               83100
         LR    R1,R6         COPY ADDRESS                        83100
         O     R2,SUBPOOL    SET SUBPOOL 250                     83100
         FREEMAIN R,LV=(R2),A=(1)  FREE IT UP                    83100
*NEW*    SR    R15,R15                                          GP03076
*NEW*    IC    R15,SUBPOOL                                      GP03076
*NEW*    STORAGE RELEASE,ADDR=(1),LENGTH=(R2),SP=(15),COND=YES  GP03076
         LR    R15,R7        COPY RETURN CODES                   83100
         LR    R0,R8                                             83100
         LR    R1,R9                                             83100
         SVC   3             RETURN TO CONTENTS SUPERVISION      83100
         DROP  R6                                                81151
         USING SAVEAREA,R13  RE-ESTABLISH WORK AREA              81151
         SPACE 2                                                 81137
*        ENTRY CODE 1 - FORCE LOCAL LOAD/GETMAINS                81151
*                                                                81151
*                                                                81137
         AIF   (&NOLO).N0L1D                                     81151
ENTINITG B     EXIT          NO SPECIAL CODE FOR GETMAINS        81151
         AGO   .N0L1C                                            81151
.N0L1D   ANOP  ,                                                 81151
ENTINITG LA    R2,DCPROT     POINT TO PROTECT NAME               81165
         LA    R6,@PROTECS   POINT TO ADDRESS                    81165
         ICM   R15,15,0(R6)  ALREADY DONE ?                     GP99026
         BNZ   EXIT          NO; QUIT                            81165
         BAS   R10,LDLPABAL  LOAD IT                             81165
         XC    RETR0,RETR0   CLEAR RETURN CODE AGAIN             81165
         LA    R7,=CL44'WYL.ZZ040012.GERHARDP.LIB'  DUMMY NAME   81137
         L     R11,=A(GRUP2BAS)   GET BASE FOR WYLBUR GROUP      81359
         B     ENTDSWYL-GRUP2BAS(,R11)   FORCE OPEN/GETMAIN      81359
.N0L1C   TITLE '@ S E R V I C E  ***  MODULE LOAD / DELETE'      81165
*        ENTRY CODE 2 - LOAD MODULE FROM LPA OR LINKLIB          81151
*        LOAD FROM LPA; IF NOT FOUND, ISSUE LOAD (NO DCB)        81145
*        MODE=0   LOAD MODULE                                    90217
*        MODE=1   DELETE MODULE                                  90217
*        MODE=2   ZERO (LOCAL) USERCVT ENTRY FOR MODULE          90217
*                                                                81145
ENTLPALD LTR   R2,R7         ANY PARM ?                          81145
         BZ    BADENTRY      NO; TOO BAD                         81145
         CLI   CALLMODE,2    USERCVT RESET ?                     88330
         BE    ENTLPA@0      YES                                 88330
         LA    R5,LOADLIST   GET SPECIAL NAME LIST               81151
         LA    R6,LOADADDR   AND CORRESPONDING ADDRESSES         81151
         LA    R8,LOADNUM      AND NUMBER OF ENTRIES             81151
LDLPALOP CLC   0(8,R5),0(R7) USER'S MODULE ?                     81151
         BNE   LDLPAINC      NO                                  81151
         CLI   CALLMODE,0    LOAD ?                              81165
         BNE   LDLPADEL      NO; DELETE                          81165
         ICM   R0,15,0(R6)   LOADED PREVIOUSLY ?                 81151
         BNZ   LDLPAST0      JUST RETURN ADDRESS- LOADED OR IN LPA
         B     LDLPABAL      USER MAY DELETE, ELSE A CLOSE CALL WILL
LDLPAINC LA    R5,LOADLENT(,R5)     BUMP NAME POINTER            81165
         LA    R6,L'LOADADDR(,R6)   BUMP ADDRESS POINTER        GP99026
         BCT   R8,LDLPALOP   TRY NEXT ENTRY                      81151
         SR    R6,R6         SPECIAL ENTRY NOT FOUND             81151
         CLI   CALLMODE,0    LOAD ?                              81165
         BE    LDLPABAL      YES                                 81165
         DELETE EPLOC=(R7)   ELSE JUST DELETE                    81165
         B     EXIT          AND QUIT                            81165
LDLPABAL BAS   R9,LOADLPA                                        81165
LDLPAST0 ST    R0,RETR0      SET ADDRESS FOR USER                81145
         LTR   R6,R6         SPECIAL ENTRY ?                     81151
         BZR   R10           NO                                  81165
         ST    R0,0(,R6)     SAVE ADDRESS FOR OUR USE            81151
         LH    R8,4(,R6)     GET COUNT BACK                     GP99026
         LA    R8,1(,R8)     UP BY 1 (NO OVERFLOW CHECK)         81165
         STH   R8,4(,R6)     SET IT BACK                        GP99026
         BR    R10           AND RETURN                          81165
         SPACE 1                                                 88330
ENTLPA@0 DS    0H            RESET MODULE ADDRESS IN USER CVT    88330
         AIF   (NOT &LOCO).NOT@0LP                               88330
         AUCH  BADRET8       AUTHORIZATION REQUIRED              90337
         LA    R15,PATULIST  GET TABLE OF LOCAL MODULES          92297
         LA    R0,(PATULEND-PATULIST)/PATULENG  # OF ENTRIES     92297
LPA@0LUP CLC   0(8,R2),4(R15)  MATCHING NAME ?                   88330
         BE    LPA@0MAT      YES                                 88330
         LA    R15,PATULENG(,R15)  NEXT                          92297
         BCT   R0,LPA@0LUP   TRY AGAIN                           88330
         B     EXIT          NOT FOUND - NO ERROR                88330
LPA@0MAT L     R0,0(,R15)    GET OFFSET IN USER CVT              88330
         L     R15,CVTPTR    GET THE CVT POINTER                 88330
         ICM   R3,15,CVTUSER-CVTMAP(R15)  GET USER CVT           92297
         BZ    EXIT          OR OUT                              88330
         AR    R3,R0         POINT TO ENTRY                      88330
         MODESET KEY=ZERO                                        88330
         XC    0(4,R3),0(R3) RESET THE ENTRY                     88330
         MODESET KEY=NZERO                                       88330
.NOT@0LP B     EXIT          RETURN                              88330
         SPACE 1                                                 81145
LOADLPA  DS    0H            MLPA/PLPA SEARCH CODE               84087
         AIF   (NOT &LOCO).NOTLOLP                               84087
         LA    R15,PATULIST  GET LOCAL MODULE NAMES              92297
         LA    R0,(PATULEND-PATULIST)/PATULENG  NUMBER           92297
LOADLPAL CLC   0(8,R2),4(R15)  USER NAME MATCHES ?               84087
         BE    LOADLPAM      YES; LOOK UP IN USER CVT            84087
         LA    R15,PATULENG(,R15)  GET NEXT ENTRY                92297
         BCT   R0,LOADLPAL   TRY IT                              84087
         B     LOADMLPA      ELSE DO SLOW WAY                    84087
         SPACE 1                                                 92297
         PUSH  PRINT                                             92297
         PRINT ON,GEN,NODATA                                     92297
USERCVT  USERCVT SECT=D,OPT=LIST  EXPAND MODULE NAMES            92297
         POP   PRINT                                             92297
         SPACE 1                                                 92297
LOADLPAM L     R0,0(,R15)    GET THE OFFSET TO THE ADDRESS       92297
         L     R15,CVTPTR    GET THE CVT POINTER                 84087
         ICM   R15,15,CVTUSER-CVTMAP(R15)                        92297
         BZ    LOADMLPA      NO USER CVT ?                       84087
         ALR   R15,R0        GET THE ADDRESS OF THE ADDRESS      84087
         ICM   R0,15,0(R15)  TEST THE ADDRESS POINTER            84087
*NEXT*   BZ    LOADMLPA      NOT COMPLETED                       84087
         BNZR  R9            RETURN TO CALLER                    84087
.NOTLOLP SPACE 1                                                 84087
LOADMLPA LPALOOK EPLOC=(R2),DCB=4  GET LPA ADDRESS OR LOAD IT   GP03262
         CH    R15,=H'4'     DID IT WORK ?                      GP03262
         BNHR  R9            YES                                GP03262
         SR    R0,R0         ELSE SHOW NOT IN LPA               GP03262
         BR    R9            RETURN TO CALLER - 0 OR ADDRESS IN R0
         SPACE 1                                                 81165
LDLPADEL LA    R10,EXIT      ENSURE EXIT ADDRESS LOADED          81165
DELTMOD  SR    R2,R2         CLEAR BRANCH COUNTER                81165
         L     R15,0(,R6)    GET ADDRESS (OR 0)                 GP05020
         LH    R3,4(,R6)     GET USE COUNT                      GP05020
         ST    R2,0(,R6)     CLEAR ADDRESS                       81165
         ST    R2,4(,R6)     CLEAR COUNT AND FLAGS              GP99026
         LTR   R15,R15       EVER LOADED ?                      GP05020
         BZR   R10           NO; JUST RETURN                     81165
         LA    R14,DELTRET   SET RETURN FROM CLOSE CALL          81165
         SR    R0,R0         SET FOR NORMAL CLOSE               GP05020
         SR    R1,R1         -DITTO-                            GP05020
         ICM   R2,1,8(R5)    GET CLOSE INDEX                     81165
         BZR   R14           NONE; JUST DELETE                   81165
DELTBASE B     *(R2)         ELSE GO TO CLOSE CODE               81165
DFGNORM  EQU   *-DELTBASE    NORMAL CLOSE (R0=R1=0; BALR 14,15)  81165
         B     DELTNORM      DO NORMAL CLOSE                     81165
DFGNEG   EQU   *-DELTBASE    R0 NEGATIVE; BALR 14,15             81165
         B     DELTNEG                                           81165
DFGPROT  EQU   *-DELTBASE    PROTECT/R1=0, BALR 14,14, NOPR      81165
         B     DELTPROT                                          81165
DFGOBT   EQU   *-DELTBASE    OBTAINS/L 15,20(15), BALR 14,15     81165
         B     DELTOBT       INDIRECT R15                        81165
DFGPRNT  EQU   *-DELTBASE    R0=255,0                            81165
*        B     DELTPRNT      PRINTER CLOSE                       81165
         SPACE 1                                                 81165
DELTPRNT L     R0,=AL1(0,0,255,0)  SET PRINTER CLOSE             81165
         B     DELTNORM      GO TO COMMON                        81165
DELTNEG  BCTR  R0,0          SET R0 TO -1                        81165
         B     DELTNORM                                          81165
DELTOBT  L     R15,24(,R15)  GET @OBCLOSE ADDRESS                81165
DELTNORM BR    R15           BALR R14,R15 (R14 PRESET TO DELTRET)
         SPACE 1                                                 81165
DELTPROT BASR  R14,R15       INVOKE @PROTECS                     81165
         NOPR  0             SKIP (OS RETURN)                    81165
         SPACE 1                                                 81165
DELTRET  LTR   R3,R3         ANYTHING LEFT IN USE COUNT ?       GP05020
         BPR   R10           YES; LPA MODULE.  RETURN TO CALLER  81165
         DELETE EPLOC=(R5)   DELETE IT                           81165
         BR    R10           AND RETURN                          81165
         EJECT ,                                                 86013
*        ENTRY CODE 3 - AUTHORIZATION DIDDLING                   86013
*        CALLING RB CHECKED AGAINST LIST                         86013
*        ALL RETURNS WITH R15=0                                  86013
*                                                                86013
ENTAUTHS SR    R9,R9         JUST IN CASE                        86013
         IC    R9,CALLMODE   GET REQUEST                         86013
         CLI   CALLMODE,APFMAX  VALID ?                          86013
         BNL   BADENTRY                                          86013
         SLL   R9,2          MAKE OFFSET                         86013
         LA    R9,APFSETOI(R9)  GET APPROPRIATE INSTRUCTION      86013
APFCOM   CLI   SUBPOOL,FGSVC DID CALLER USE SVC ?                86013
         BE    AUTHSETS                                         GP02305
         BNE   EXIT          NO; JUST RETURN                     86013
         AUCH  EXIT          CONTINUE IF AUTHORIZED CALLER      GP02305
         MODESET KEY=ZERO    GET PRIVIED                        GP02305
AUTHSETS L     R2,PSATOLD-PSA  GET CURRENT TCB                   86013
         USING TCB,R2                                            86013
         L     R2,TCBJSCB    GET THE JSCB                        86013
         USING IEZJSCB,R2                                        86013
         L     R3,JSCBCSCB   GET THE CSCB                        86013
         USING CHAIN,R3                                          86013
         L     R4,PSAAOLD-PSA  GET THE ASCB                      90217
         USING ASCB,R4       DECLARE IT                          90217
         EX    0,0(,R9)      EXECUTE THE REQUEST                 86013
         CLI   SUBPOOL,FGSVC DID CALLER USE SVC ?               GP02305
         BE    EXIT          YES; DONE                          GP02305
         MODESET KEY=NZERO   RESTORE                            GP02305
         B     EXIT          RETURN TO USER                      86013
         SPACE 1                                                 86013
APFSETOI OI    JSCBOPTS,JSCBAUTH                                 86013
APFSETNI NI    JSCBOPTS,255-JSCBAUTH                             86013
PSWSETOI OI    JSCBSWT1,JSCBPASS                                 86013
PSWSETNI NI    JSCBSWT1,255-JSCBPASS                             86013
CANSETOI OI    CHACT,CHCL    SET CANCELLABLE                     86013
CANSETNI NI    CHACT,255-CHCL  SET NON-CANCELLABLE               86013
TIMSETOI OI    &FLAGNAM,ASCBTOFF  NO 522                         90217
TIMSETNI NI    &FLAGNAM,255-ASCBTOFF  ALLOW 522, ETC. AGAIN      90217
APFMAX   EQU   (*-APFSETOI)/(APFSETNI-APFSETOI)                  86013
         DROP  R2,R3,R4                                          90217
         TITLE '@ S E R V I C E  ***  UNIT CONTROL BLOCK SCANS'  81145
*        ENTRY CODE 5 - UCB SCANS                                81151
*        MODE 0 - SCAN ALL UCBS/R1=0 FIRST, R1=A GET NEXT AFTER A
*        MODE 1 - LOCATE UCB BY NAME; R1 POINTS TO CL3 OF NAME   81151
*        MODE 2 - LOCATE UCB BY VOLUME SERIAL; R1 POINTS TO CL6  81151
*        MODE 3 - LOCATE DISK UCB BY VOLSER; R1 POINTS TO CL6    81151
*        MODE 4 - GET GENERIC OR DEVICE NAME FROM UCB; R1 => UCBOB
*                            OR FAKE UCBTBYT1-(UCBTBYT1-UCBOB)   90217
*        MODE 5 - GET DEVICE TYPE FROM NAME; R1 => CL8 NAME      90217
*                                                                81151
*        RETURNS: R15 - 16 - R1 ZERO WHEN REQUIRED               81151
*        R15=8  R0=0 UCB NOT FOUND /  R15=0  R0=UCB ADDRESS      81151
*        R15=12 (ESA, OS/390) CONFIGURATION CHANGED             GP99027
         SPACE 1                                                 81151
ENTUCBUM IC    R0,CALLMODE   GET SUB-FUNCTION                    81151
         XC    RETR0,RETR0   CLEAR RETURN ADDRESS FOR ERROR     GP02238
         L     R4,UNITILK    PRE-XA - RELOAD SCAN POINTER       GP05033
         BIX   ERR=BADRET12,PFX=ENT,LOC=(UCBLP,UCBNM,UCBVS,UCBDK,UCBGN,*
               UCBDT)                                            86250
ENTUCBLP LTR   R3,R7         ANY UCB ?                           81151
         BNZ   SCANUCB0      YES; RESTART                        81151
         BAS   R9,UNITINIT   INITIALIZE UCB SEARCH              GP02242
SCANUCB0 BAS   R9,UNITLOOP   FIND UCB ADDRESS IN R3             GP03076
         USING UCBOB,R3      DECLARE IT                          81137
STOREUCB LTR   R3,R3         ANY ?
         BNP   BADRET8       NO; SET ERROR
         ST    R3,RETR0      SET INTO RETURN
         B     EXIT          EXIT
         SPACE 2
ENTUCBVS BAS   R9,UNITINIT   INITIALIZE UCB SEARCH              GP02242
         BAS   R10,VSNUCB7   LOOK FOR VSN                        81151
         B     BADRET8       NOT FOUND
         B     STOREUCB      RETURN OK                           81151
         SPACE 2                                                 81137
ENTUCBDK BAS   R9,UNITINIT   INITIALIZE UCB SEARCH              GP02242
         BAS   R10,VSNUCB7   LOOK FOR VSN                        81151
         B     BADRET8       NOT FOUND                           81137
         CLI   UCBTBYT3,UCB3DACC  DISK ?                         81137
         BNE   VSUCBLOP      NO; TRY NEXT ENTRY                  81137
         TM    UCBSTAT,UCBONLI                                   81137
         BZ    VSUCBLOP      OFF-LINE; KEEP GOING                81137
         B     STOREUCB      RETURN OK                           81151
         SPACE 1
VSNUCB   BAS   R9,UNITINIT   INITIALIZE UCB SEARCH              GP03076
VSNUCB7  LTR   R7,R7         ANY PARM ?                          81151
         BZ    BADRET12      NO; FAIL                            81151
VSUCBLOP BAS   R9,UNITLOOP   GET A UCB                          GP03076
         LTR   R3,R3         ANY FOUND ?
         BNPR  R10           NO; SIGNAL ERROR
         TM    UCBTBYT3,UCB3DACC+UCB3TAPE  VOL-SER FIELD PRESENT ?
         BNM   VSUCBLOP      NO; TRY NEXT UCB
         CLC   UCBVOLI,0(R7)   MATCHING PARM ?
         BNE   VSUCBLOP      NO; TRY AGAIN
         B     4(,R10)       TAKE GOOD EXIT
         SPACE 2
ENTUCBNM BAS   R9,UNITINIT   INITIALIZE UCB SEARCH              GP02242
         LTR   R7,R7         ANY PARM ?                          81151
         BZ    BADRET12      NO; FAIL                            81151
NMUCBLOP BAS   R9,UNITLOOP   GET A UCB                          GP03076
         LTR   R3,R3         ANY FOUND ?
         BNP   BADRET8       NO; SIGNAL ERROR
         CLC   UCBNAME,0(R7)   MATCHING PARM ?
         BNE   NMUCBLOP      NO; TRY AGAIN
         B     STOREUCB      TAKE NORMAL EXIT
         SPACE 2
         AIF   (NOT &MVSESA).OLDUCBL                            GP99027
UNITINIT XC    UNITPREV,UNITPREV                                GP99014
         L     R4,CVTPTR     GET CVT  (DON'T NEED ANY MORE?)    GP99027
         USING CVTMAP,R4                                        GP99027
LOOKNIOC XC    UNITWORK(UNITCLR),UNITWORK  CLEAR WORK AREA      GP99014
         LA    R0,L'UNITUDEV  LENGTH OF DEVICE SEGMENT          GP99034
         STCM  R0,3,UNITUDEL  SET FOR CALL                      GP99034
         MVI   UNITUFLG,0    CLEAR PROCESS FLAG                 GP99027
         CLI   SUBPOOL,FGSVC   IN SVC ?                         GP99027
         BE    LOOKUAUT      YES; USE AUTHORIZED ENTRY          GP99027
         TESTAUTH KEY=YES,RBLEVEL=1  IS CALLER IN KEY ZERO?     GP99027
         BXLE  R15,R15,LOOKZAUT  YES                            GP99027
         TESTAUTH FCTN=1     IS CALLER AUTHORIZED?              GP99027
         BXH   R15,R15,LOOKNAUT  NO                             GP99027
         B     LOOKUAUT                                         GP99027
LOOKZAUT OI    UNITUFLG,UNITUZER  ALREADY                       GP99027
LOOKUAUT OI    UNITUFLG,UNITUAUT  SET AUTHORIZATION FLAG        GP99027
LOOKNAUT BR    R9            RETURN TO CALLER OF UNITINIT       GP99027
         SPACE 1                                                GP99027
UNITLOOP TM    UNITUFLG,UNITUAUT  AUTHORIZED?                   GP99027
         BNZ   LOOKAUCB      YES                                GP99014
         UCBSCAN COPY,WORKAREA=UNITWORK,                               *
               UCBAREA=UNITCOPY,DYNAMIC=YES,                           *
               UCBPAREA=UNITUPFX,DCEAREA=UNITUDEV,DCELEN=UNITUDEL,     *
               MF=(E,UNITUPAT)                                  GP99014
*DEFER*        RANGE=3DIGIT,
         SR    R3,R3         CLEAR                              GP02242
         LTR   R15,R15       GOOD COMPLETION ?                  GP02242
         BNZ   UNITLOOS                                         GP02242
         LA    R3,UNITCOPY   POINT TO LOCAL UCB                 GP99014
UNITLOOS ST    R3,UNITPTR    AND SET IT                         GP99014
         B     LOOKUCOM      JOIN COMMON                        GP99014
LOOKAUCB TM    UNITUFLG,UNITUZER  ALREADY IN KEY ZERO ?         GP99027
         BNZ   LOOKASUP      YES                                GP99027
         MODESET KEY=ZERO    AVOID 0C2                          GP99027
LOOKASUP UCBSCAN ADDRESS,WORKAREA=UNITWORK,NOPIN,                      *
               UCBPTR=UNITPTR,RANGE=3DIGIT,LOC=BELOW,DYNAMIC=YES,      *
               MF=(E,UNITUPAT)                                  GP99014
         LR    R3,R15        PRESERVE                           GP99027
         TM    UNITUFLG,UNITUZER  WERE IN KEY ZERO ?            GP99027
         BNZ   LOOKALEV      YES                                GP99027
         MODESET KEY=NZERO   RESTORE                            GP99027
LOOKALEV LR    R15,R3        REPLACE                            GP99027
LOOKUCOM CH    R15,=H'12'    IOC CHANGED?                       GP99015
         BE    LOOKUCHG      MAKE USER RESTART THE SCAN         GP99014
         CH    R15,=H'4'     OTHERWISE?                         GP99015
         BNL   LOOKUEND      LAST ONE DONE                      GP99014
*DEFER*  USING UCBOB,R3      DECLARE IT                         GP99014
*OLD*    MVC   UNITPREV,UZBNAME(R13) GET EBCDIC UCB NAME        GP99014
*OLD*    XC    UNITPREV,UNIT000    PUT INTO SEQUENCE            GP99014
         L     R3,UNITPTR    LOAD THE UCB POINTER               GP99014
         BR    R9            RETURN TO CALLER                   GP99027
LOOKUCHG MVI   RC,12         SET RECONFIGURATION CODE           GP99027
         B     EXIT          AND RETURN TO CALLER               GP99027
LOOKUEND SR    R3,R3         SET END OF LIST                    GP99027
         AGO   .COMUCB                                          GP99027
.OLDUCBL ANOP  ,                                                GP99027
UNITINIT L     R4,CVTPTR     GET CVT
         USING CVTMAP,R4                                         81137
         AIF   (NOT &MVSXA AND NOT &MVSESA).OLDUCB              GP97241
         XC    UNITWORK(UNITCLR),UNITWORK  CLEAR WORK AREA       90201
         LA    R14,UNITWORK  LOAD WORK AREA                      90201
         LA    R15,UNITCLAS  LOAD CLASS POINTER (=>0)            90201
         LA    R0,UNITPTR    GET UCB ADDRESS POINTER             90201
         STM   R14,R0,UNITPARM  MAKE LIST                        90201
         OI    UNITPARM+8,X'80'  SET END OF LIST                 90201
         L     R4,CVTUCBSC   LOAD LOOK-UP ROUTINE ADDRESS        90201
         BR    R9            RETURN                              90201
         SPACE 1                                                 90201
UNITBUMP DS    0H                                                90201
UNITLOOP LA    R1,UNITPARM   GET SEARCH PARAMETER LIST           90201
         LR    R15,R4        COPY LOOK-UP ROUTINE ENTRY          90201
         BASR  R14,R15       LOOK A LITTLE                       90201
         BXH   R15,R15,UNITENDS  NO MORE                         90201
         L     R3,UNITPTR    LOAD THE UCB POINTER                90201
         TM    UCBWGT,UCBMTPXP  MULTIPLE EXPOSURE DEVICE ?       90201
         BZR   R9            NO; RETURN                          90201
         CL    R3,UCBBASE    BASE UCB ?                          90201
         BNE   UNITLOOP      NO; SKIP THIS ONE                   90201
         AGO   .COMUCB                                           90201
.OLDUCB  L     R4,CVTILK2    GET UCB LOOKUP TABLE                90201
         XC    UNITPREV,UNITPREV   REPOSITION SEARCH
         BR    R9            RETURN TO CALLER
         SPACE 1
UNITBUMP LA    R4,2(,R4)     POINT TO NEXT UCB SLOT
UNITLOOP SR    R3,R3         ZERO HIGH BYTE
         ICM   R3,3,0(R4)    LOAD NEXT UCB ADDRESS
         BZ    UNITBUMP      SKIP IF DUMMY ENTRY
         CLM   R3,3,UNITMASK  END OF LOOKUP TABLE ?              81145
         BE    UNITENDS      YES, EXIT FROM SCAN
         ST    R4,UNITILK    SAVE SCAN POINTER FOR NEXT CALL    GP05033
         MVC   DB(3),UCBNAME     COPY EBCDIC UCB NAME
         XC    DB(3),UNIT000    PUT INTO NUMERIC/ALPHA COLLATING SEQ
         CLC   DB(3),UNITPREV     THIS CUU LOWER THAN PREVIOUS ?
         BNH   UNITBUMP      NO, SKIP IT
         MVC   UNITPREV,DB     SAVE CUU FOR NEXT ENTRY
.COMUCB  BR    R9            RETURN TO CALLER                    90201
         DROP  R4            NO LONGER CVT                       90201
UNITENDS SR    R3,R3         SIGNAL END OF CHAIN
         BR    R9            RETURN TO CALLER
         SPACE 2                                                 81165
ENTUCBGN LA    R1,BLANKS     SET DEFAULT UNIT NAME               81165
         ST    R1,RETR1      RETURN TO USER                      81165
         LTR   R3,R7         LOAD UCB/DEVTYPE ADDRESS            81165
         BZ    BADRET8       NONE ?                              81165
         SR    R5,R5                                             81215
         SR    R4,R4                                             81165
         IC    R5,UCBTBYT3                                       81215
         IC    R4,UCBTBYT4                                       81165
         LA    R1,GENBYT3    POINT TO MAJOR DEVICE GROUP TABLE   81165
         LA    R2,GENBYT3L   NUMBER OF ENTRIES TO DO             81165
         CLI   UCBID,X'FF'   DID USER SUPPLY A REAL UCB ?        81165
         BNE   GEN3LOOP      NO                                  81165
         AIF   (&MVS).NOHATI                                     81347
         TM    UCBATI,UCBHPDV   HASP PSEUDO-DEVICE ?             81165
         BZ    *+12          NO                                  81165
         LA    R1,GENBYT3H   YES; USE HASP TABLE                 81165
         LA    R2,1(,R2)     UP ENTRY COUNT                      81165
.NOHATI  AIF   (NOT &MVS).NOVIO                                  81347
         CLI   UCBTBYT3,UCB3DACC  DIRECT ACCESS ?                81165
         BNE   GEN3LOOP      NO                                  81165
         TM    UCBTBYT2,UCBRVDEV  VIO DEVICE ?                   81165
         BZ    GEN3LOOP      NO                                  81165
         LA    R15,X'80'                                         81165
         OR    R4,R15        MAKE FAKE HIGH BIT                  81165
.NOVIO   SPACE 1                                                 81165
GEN3LOOP CLM   R5,1,0(R1)    MATCHING GROUP ?                    81215
         BE    GEN3FUND      YES                                 81165
         LA    R1,4(,R1)                                         81165
         BCT   R2,GEN3LOOP   TRY AGAIN                           81165
         B     BADRET4       SET MINOR ERROR                     81165
GEN3FUND ICM   R1,7,1(R1)    LOAD GROUP TABLE ADDRESS           GP99026
GEN4LOOP CLI   8(R1),X'FF'   END OF GROUP LIST ?                 81165
         BE    GEN4FUN4      YES; USE GROUP ENTRY                81165
         CLM   R4,1,8(R1)    MATCHING DEVICE ?                   81165
         BE    GEN4FUND      YES; STASH IT                       81165
         LA    R1,9(,R1)                                         81165
         B     GEN4LOOP      TRY NEXT ENTRY                      81165
GEN4FUN4 MVI   RC,4          SET MINOR ERROR                     81165
GEN4FUND ST    R1,RETR1      SET STRING ADDRESS                 GP99026
         BR    R10           RETURN TO USER                      81165
         SPACE 1                                                 86250
ENTUCBDT SR    R3,R3                                             86250
         LTR   R7,R7         ANY PARM ?                          86250
         BZ    STOREUCB      RETURN CODE 8                       86250
         LA    R1,GENBYT3    GET THE NAME/TYPE TABLE             86250
         LA    R2,GENBYT3L   GET THE NUMBER OF ENTRIES           86250
GDTMLOOP L     R4,0(,R1)     GET THE SUB-TYPE TABLE              86250
GDTELOOP CLC   0(8,R4),0(R7)  MATCHING TYPE ?                    86250
         BE    GDTMATCH      YES                                 86250
         TM    8(R4),X'FF'   TABLE END ?                         86250
         BO    GDTMBUMP      YES                                 86250
         LA    R4,9(,R4)     TRY NEXT ENTRY                      86250
         B     GDTELOOP                                          86250
GDTMBUMP LA    R1,4(,R1)     POINT TO NEXT DEVICE TYPE           86250
         BCT   R2,GDTMLOOP   AND TRY IT                          86250
         B     STOREUCB      NO MATCH; SET CODE 8                86250
GDTMATCH ICM   R3,2,0(R1)    LOAD MAJOR TYPE                     86250
         IC    R3,8(,R4)      AND MINOR DEVICE TYPE              86250
         B     STOREUCB      RETURN TYPE AND CC=0                86250
         SPACE 1                                                 81165
         AIF   (&MVS).NOHAAD                                     81347
GENBYT3H DC    0A(0),AL1(UCB3UREC),AL3(GENURECH)                 81165
.NOHAAD  ANOP  ,                                                 81165
GENBYT3  DC    0A(0),AL1(UCB3UREC),AL3(GENUREC)                  81165
         DC    AL1(UCB3DACC),AL3(GENDASD)                        81165
         DC    AL1(UCB3TAPE),AL3(GENTAPE)                        81165
         DC    AL1(UCB3DISP),AL3(GENDISP)                        81165
GENBYT3L EQU   (*-GENBYT3)/4   NUMBER OF (NORMAL) ENTRIES        81165
         DROP  R3            DROP UCB MAPPING                    90201
         SPACE 1                                                 81245
*        DATA WITH REQUIRED R12 ADDRESSABILITY                   81245
*                                                                81245
BLANKS   DC    CL16' '       LOTS OF BLANKS                      81245
ZEROES   DC    XL10'0'       A FEW ZEROES                        82108
         TITLE '@ S E R V I C E  ***  TIOT SCANS'                81145
*        ENTRY CODE 6 - TIOT SCANS                               81151
*        MODE 0 - LOCATE FIRST/NEXT TIOT ENTRY                   81151
*        MODE 1 - LOCATE ONLY REGULAR ENTRIES; SKIP SPECIAL/CONCAT
*        MODE 2 - LOCATE ENTRY BY DDNAME                         81151
*        MODE 3 - LOCATE SINGLE/UNCONCATENATED ENTRY BY UCB ADDRESS
*        MODE 4 - LOCATE SIOT ENTRY BY TIOT ENTRY ADDRESS        90308
*        MODE 5 - COPY TOKENIZED CONTROL BLOCK TO WORK AREA     GP03075
*        MODE 6 - CONVERT TOKEN TO ADDRESS (EVEN IS 16-BYTE PFX)
*        MODE 7 - CONVERT TOKEN TO ADDRESS (EVEN IS BLOCK ADDR) GP03075
*        MODE 8 - LOOP THROUGH DASB CHAIN                       GP03075
*        MODE 9 - LOCATE DSAB BY DDNAME                         GP03075
*                                                                81151
*        RETURNS: R15 - 16 - R1 ZERO WHEN REQUIRED               81151
*        R15=0  R0=0 NOT FOUND / R0=TIOENTRY OF MATCH            81151
         SPACE 1                                                 81151
ENTTIOLP CLI   CALLMODE,VENSIOTE/256  NON-TIOT FUNCTION ?        90338
         BNL   ENTTIOL2      YES; LEAVE IT ALONE                 90338
         BAS   R9,INITTIOT   INITIALIZE FOR TIOT SCANNING        90338
         XC    RETR0,RETR0   CLEAR RETURN IN CASE OF ERROR      GP03076
ENTTIOL2 IC    R0,CALLMODE   GET SUB-FUNCTION                    90338
         BIX   ERR=BADRET12,PFX=ENT,LOC=(LPTIO,TIOLK,TIODD,TIOUA,SIOTE,*
               SWARL,SWAAD,SWAAB,DSABL,DSABD)                   GP03075
ENTLPTIO LTR   R7,R7         ANY RESTART VALUE SUPPLIED ?        81151
         BZR   R6            NO; RETURN FIRST ENTRY              81145
         BASR  R9,0          SET LOOP ADDRESS                    81145
         CR    R1,R7         FOUND USER'S ?                      81145
         BNER  R8            NO; TRY AGAIN                       81145
         BASR  R9,R8         GET NEXT ENTRY                      81145
         BR    R6            SET FOR USER                        81145
         SPACE 1                                                 81145
ENTTIOLK LA    R9,ENTTIOK1   SET LOOP ADDRESS                    81145
         LTR   R7,R7         ANY RESTART VALUE SUPPLIED ?        81145
         BZR   R9            NO; GET FIRST ENTRY                 81145
         BASR  R9,0          SET FIRST LOOP ADDRESS              81145
         CR    R1,R7         FOUND USER'S ?                      81145
         BNER  R8            NO; TRY AGAIN                       81145
         BASR  R9,R8         GET NEXT ENTRY                      81145
         USING TIOELNGH,R1   DECLARE IT                          81145
ENTTIOK1 CLI   TIOEDDNM,C' '  CONCATENATION ?                    81145
         BER   R8            NOT SUPPORTED                       81145
ENTTIOK2 CLI   TIOEDDNM+4,C'*'  PGM=*...                         81165
         BER   R8            DITTO                               81145
         CLC   =C'SYSUDUMP',TIOEDDNM  DUMP ?                     81151
         BER   R8                                                81151
         CLC   =C'SYSABEND',TIOEDDNM  DUMP ?                     81151
         BER   R8                                                81151
         CLC   =C'SYSMDUMP',TIOEDDNM  DUMP ?                     90217
         BER   R8                                                90217
         CLC   =C'JOBLIB ',TIOEDDNM                              81145
         BER   R8                                                81145
         CLC   =C'STEPLIB ',TIOEDDNM                             81145
         BER   R8                                                81145
         CLC   =C'JOBCAT ',TIOEDDNM                              81145
         BER   R8                                                81145
         CLC   =C'STEPCAT ',TIOEDDNM                             81145
         BER   R8                                                81145
         CLC   =C'SORTLIB',TIOEDDNM  SPECIAL ?                   93084
         BER   R8            YES; SKIP                           93084
         CLC   =C'SORTW',TIOEDDNM  DITTO ?                       93084
         BER   R8            YES; DON'T MESS UP                  93084
         CLC   =C'PRINT',TIOEDDNM+3  SYS/SUM/ERR/MSG-PRINT ?     81245
         BER   R8            YES; SKIP                           81245
         CLC   =C'PUNCH',TIOEDDNM+3  SYSPUNCH OR WHAT-NOT ?      83254
         BER   R8            YES; SKIP                           83254
         CLC   =C'DEBUG',TIOEDDNM+3  SYSDEBUG OR WHAT-NOT ?     GP03003
         BER   R8            YES; SKIP                          GP03003
         CLC   =C'TRACE',TIOEDDNM+3  DEBTRACE OR PGMTRACE ?     GP03003
         BER   R8            YES; SKIP                          GP03003
         AIF   (NOT &LOCO).NOPROC                                92297
         CLC   =C'JOBPROC ',TIOEDDNM  USER PROCLIB ?             92297
         BER   R8            YES; SKIP                           92297
.NOPROC  CLC   =C'SYSTSIN',TIOEDDNM  BATCH TSO ?                 92297
         BER   R8            YES                                 92297
         CLC   =CL8'SYSTSPRT',TIOEDDNM                           92297
         BER   R8                                                92297
         CLC   =C'SYSTERM ',TIOEDDNM                             81270
         BER   R8            SKIP ALSO                           81270
SAVETIOT ST    R1,RETR0      SET FOR USER                        81145
         BR    R10           AND RETURN                          82108
         SPACE 1                                                 81145
ENTTIODD LTR   R7,R7         ANY PARM ?                          81145
         BZ    BADRET12      NO; FAIL                            81145
         BASR  R9,0          SET RECURSION REGISTER              81151
         CLC   TIOEDDNM,0(R7)  USER'S DDNAME ?                   81145
         BER   R6            YES; RETURN IT                      81145
         BR    R8            ELSE TRY NEXT ENTRY                 81145
         SPACE 1                                                 81145
ENTTIOUA LTR   R7,R7         ANY ADDRESS ?                       81145
         BZ    BADRET12                                          81145
         BASR  R9,0          SET RECURSION ADDRESS               81151
         CLM   R7,7,TIOEFSRT   SAME UCB ADDRESS ?                81145
         BNER  R8            NO; TRY ANOTHER                     81145
         CLI   TIOELNGH,20   SINGLE UCB ?                        81145
         BNER  R8            NO                                  81145
         CLI   TIOELNGH+20,0   LAST ENTRY ?                      81145
         BE    ENTTIOK1      YES; NOW CHECK DDNAME               93084
         CLI   TIOEDDNM+20,C' '  CONCATENATION ?                 81145
         BER   R8            YES; SKIP                           81145
         LTR   R7,R7         X'80' BIT ON ?                      86336
         BNM   ENTTIOK1      NO; DO NORMAL EXCLUSION             93084
         CLC   =C'SYS',TIOEDDNM  DYNAMIC DD ?                    86336
         BNE   ENTTIOK1      NO                                  93084
         CLI   TIOEDDNM+3,C'0'  SYSNNNNN ?                       86336
         BNLR  R8            YES; IGNORE IT                      86336
         B     ENTTIOK1      NO; JUST CHECK DDNAME               93084
         SPACE 1                                                 81145
INITTIOT LTCB  R1            GET CURRENT TCB                     83100
         USING TCB,R1                                            81145
         L     R1,TCBTIO     GET TIOT ADDRESS                    81145
         USING TIOT1,R1                                          81145
         LA    R1,TIOELNGH   POINT TO FIRST ENTRY                81145
         USING TIOELNGH,R1                                       81145
         LA    R8,BUMPTIOT   SET FOR REGISTER BRANCHES           81145
         LA    R6,SAVETIOT   SET STORE/EXIT ADDRESS              81145
         SR    R2,R2         CLEAR IC REGISTER                   81145
         ST    R2,RETR0      CLEAR RETURN                       GP03078
         B     RTRNTIOT      CHECK FOR LAST ENTRY                81145
         SPACE 1                                                 81145
BUMPTIOT ICM   R2,1,TIOELNGH                                     81145
         BZR   R10           EXIT WITH R0=0                      82108
         AR    R1,R2                                             81145
RTRNTIOT CLI   TIOELNGH,0    END ?                               81145
         BNER  R9            NO; RETURN TO CALLER                81145
         BR    R10           EXIT WITH R0=0                      82108
         DROP  R1                                                81145
         TITLE '@ S E R V I C E  ***  DATA'                      81359
*        READ-ONLY STORAGE                                       81151
*                                                                81151
LOADLIST DS    0A            LIST OF (POSSIBLE) LOADED MODULES   81151
         DC    CL8'@BANDAID',AL1(DFGNORM) ABEND FORMATTING AID  GP03134
LOADLENT EQU   *-LOADLIST    LENGTH OF ONE ENTRY                 81165
         DC    CL8'@CATREAD',AL1(DFGNORM) ICF CATALOG READER    GP03134
         DC    CL8'@CVLREAD',AL1(DFGNEG)  CVOL CATALOG READER    81165
         DC    CL8'@DCBEXIT',X'0'                                81165
         DC    CL8'@FDRREAD',AL1(DFGNORM) READ FDR DUMP TAPES    81165
         DC    CL8'@FORMATS',AL1(DFGNORM) DATA FORMATTING       GP97241
         DC    CL8'@INPREAD',AL1(DFGPRNT) INPUT PROCESSOR        81194
         DC    CL8'@MESSAGE',AL1(DFGNORM) MESSAGE PROCESSING    GP03134
         DC    CL8'@OBTAINS',AL1(DFGOBT)  FAST VTOC READ         81165
         DC    CL8'@PARSER ',AL1(DFGNORM) STRING PARSING        GP03082
         DC    CL8'@SRVJES2',AL1(0)       JES2 EXTENSION        GP03134
         DC    CL8'@TMSREAD',AL1(DFGNORM)                        81165
         DC    CL8'@VOLREAD',AL1(DFGNORM)  FAST VTOC READER      81236
         DC    CL8'@WRITER ',AL1(DFGNORM)  GENERAL OUTPUT RTNE  GP03134
         DC    CL8'DEBTRACE',AL1(DFGNORM) DATA SNAP             GP03134
         DC    CL8'LEXVOLT ',X'0'  VOLUME ATTRIBUTES   1/N       81165
         DC    CL8'PGMTRACE',AL1(DFGNORM) INSTRUCTION TRACE PGM GP03134
         DC    CL8'SUBCAT  ',AL1(0)       ICF CATALOG LOOKUP    GP03134
         DC    CL8'SUBCOMP ',AL1(0)       DATA SET+ COMPARE     GP03134
         DC    CL8'SUBCPOOL',AL1(DFGNORM) CELL POOL SERVICES    GP04234
         DC    CL8'SUBDESRV',AL1(0)       DESERV SERVICES       GP03261
         DC    CL8'SUBFETCH',AL1(0)       PROGRAM FETCH         GP03261
         DC    CL8'SUBLPALK',AL1(0)       LPA LOOKUP/PGM LOAD   GP03261
         DC    CL8'SUBTIMER',AL1(0)       STOP WATCH            GP03134
         DC    CL8'SUBVERB ',AL1(0)       VERB LOOKUP           GP03261
         DC    CL8'SUBWTO  ',AL1(DFGNORM)  WRITE TO OPERATOR    GP03134
DCPROT   DC    CL8'@PROTECS',AL1(DFGPROT) FAST SVC 98 2/N        81165
DCSCRN   DC    CL8'@SCREENS',AL1(DFGPRNT) SCREENS - ALMOST LAST  87308
DCPRNT   DC    CL8'@PRINTER',AL1(DFGPRNT) PRINTER - LAST ENTRY   81165
LOADNUM  EQU   (*-LOADLIST)/LOADLENT  NUMBER OF ENTRIES          81165
ALPHAS   DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789',C'ABCDEF' 99026
NUMTAB   EQU   ALPHAS+26,16,C'C'  DEFINE TR FOR HEX DIGITS       92297
MASK     DC    X'8040201008040201'                              GP97241
UNITMASK DC    0H'0'    FFFF MASK FOR END OF UCB LOOKUP TABLE    81151
FFS      DC    8X'FF'        UNKNOWN PASSWORD 'LIST' REQUEST    GP97241
ZZEROES  DC    0CL8'00000000',C'0000'  EIGHT EBCDIC ZEROES       81165
UNIT000  DC    0C'000'       XC MASK FOR NUM/ALPHA COLLATING    GP97241
FOUR0    DC    C'0000'       GROUP SUB-ACCOUNT                   81151
ACCT#DEF DC    C'C904001S'   DEFAULT ACCOUNT FOR START JOBS      81151
ACCT#SYS DC    C'Z904'       SYSTEMS ACCOUNT                     81151
ACCT#FLP DC    C'Z909'       SPECIAL BYPASS NUMBER               81151
ACCT#SUB DC    C'0010'       DEFAULT SUBACCOUNT FOR SHORT ACCT CHECK
         SPACE 1                                                 81151
*        WORKING STORAGE (SAVED ON TCBFSA CHAIN)                 81151
*                                                                81151
         FSAWORK ,           DECLARE FSA HEADER                  81151
SAVEAREA DC    9D'0'         STANDARD SAVE AREA                 GP97241
SAVEARE2 DC    9D'0'         RECURSION SAVE AREA                GP03250
SAVEARE3 DC    2D'0'         RECURSION SAVE AREA                GP05020
SUBPOOL  DC    A(0)          SUBPOOL FOR GETMAIN/FREEMAINS       83100
FGSVC    EQU   250             SUBPOOL USED FOR SVC ENTRY        83100
@USRVOLT DC    A(0)          VOLUME DEFINITIONS (AMS/PID)        81151
         SPACE 1                                                 81151
LOADADDR DS    0D            ADDR OF LOADED MODULES IN LOADLIST SEQ.
         DC    (LOADNUM-3)D'0'  ALL OTHERS IN BETWEEN           GP05020
@PROTECS DC    D'0'          FAST PROTECT (PASSWORD) CODE        81151
@SCREENS DC    D'0'
@PRINTER DC    D'0'          PRINTER SERVICE ROUTINE             81151
@TMSSVCY DC    A(0)          ADDRESS OF IGCYYY00 TMS WORK AREA   82052
DYNTIOT  DC    A(0,0)        +A(VOLMOUNT DD IN TIOT), -USED FOR O
         SPACE 1                                                 81151
RETCH    DC    0XL8'0'       RETURN VALUES                      GP97241
RETR15   DC    0A(0),XL3'0'                                      81165
RC       DC    X'0'          RETURN CODE                        GP97241
RETR0    DC    0A(0),XL3'0'                                      81165
RV       DC    X'00'         RETURN FLAGS                       GP97241
RETR1    DC    A(0)          ZERO OR MODIFIED R1 RETURN          81165
CALLR0   DC    A(0)          CALL PARM R0        1/2             81151
CALLADD  DC    A(0)          CALL PARM R1        2/2             81151
CALLB0   EQU   CALLR0+0,1,C'X'  OLD R1:0                        GP99026
CALLOPT  EQU   CALLR0+1,1,C'X'  FNCTION OPTIONS                 GP06287
CALLMODE EQU   CALLR0+2,1,C'X'  FUNCTION MODIFIER VALUE          81151
CALLFUN  EQU   CALLR0+3,1,C'X'  FUNCTION VALUE (GROUP 0-3;FUN 4-7)
WYLFREEM DC    A(0,0)        FREEMAIN ADDRESS/LENGTH            GP97241
EXWCRMLN DC    F'0'    1/3   LENGTH OF CSA GOTTEN                85070
EXWCRMAD DC    F'0'    2/3   ADDRESS OF GOTTEN CSA               85070
EXWCRMCD DC    A(0)    3/3   CSA ADDRESS OF USER'S CODE          85070
EXWCRMID DC    F'0'          DESIRED ASID FOR SCHEDULE           85070
SCHWSTAE ESTAE STAEEXIT,CT,MF=L  1/2                             85070
SCHWSTAL EQU   *-SCHWSTAE        2/2                             92298
SCHEDFLG DC    X'00'         STATUS FLAG                         85070
SCHEDEST EQU   X'80'           ESTAE ISSUED                      85070
SCHEDCUR EQU   X'10'           STAE RECURSION FLAG               85070
SCHEDENT DC    X'00'         ENTRY CODE                          85070
EXWJ2ASC DC    F'0'          JES2 ASCB                           85070
EXWJ2NAM DC    CL8'JES2    ' JES2 START NAME+CL4                 85070
         SPACE 1                                                GP97241
MEMNO    DC    CL8' '        ACCOUNT NUMBER                     GP97241
WYLUCB   DC    F'0'          WYL001 UCB ADDRESS; NEGATIVE IF NONE
WYLALIST DC    A(0)          GETMAIN START LIST                 GP97241
WYLAINC  DC    F'0'          ENTRY LENGTH FOR BXLE =WAX+L'WAX    81151
WYLACURR DC    A(0)          NEXT AVAILABLE ENTRY               GP97241
WYLAEND  DC    A(0)          END OF LIST                        GP97241
COPYINX  DS    CL8           ROOM FOR TSO USER-ID AND POINT      86314
COPYDSN  DC    CL50' '       CONVERTED GDG NAME                 GP97241
         ORG   COPYDSN       RE-USE                              81270
FMTDSORG DC    CL3' '        DSFMT - DSORG                       81270
FMTRECFM DC    CL6' '        DSFMT - RECFM                       81270
FMTOPTCD DC    CL8' '        DSFMT - OPTCD                       81270
FMTBLKL  DC    CL5' '        DSFMT - BLKSIZE                     81270
FMTLRECL DC    CL5' '        DSFMT - LRECL                       81270
FMTORGLN EQU   *-FMTDSORG    LENGTH OF LIST                      81270
         ORG   ,                                                 81270
UNITILK  DC    A(0)          PREVIOUS CVTILK2 POINTER           GP05033
UNITPREV DC    CL3' ' .      SAVE CURRENT UCB                   GP97241
FMTPRIV  DC    X'00'         RETURN LIST FOR FMT FUNCTION - CL17 81215
FMTACCT  DC    CL8' '        ACCOUNT                             81215
FMTUSER  DC    CL8' '        USER ID                             81215
         SPACE 1                                                 85070
         ORG   COPYDSN       RE-USE                              85070
EXWAJOBN DC    CL8' '        JOB NUMBER (JOB NNNN/STC NNNN..)    85070
EXWAJOB  DC    CL8' '        JOB NAME                            85070
EXWASTEP DC    CL8' '        STEP NAME                           85070
EXWAPROC DC    CL8' '        PROC NAME                           85070
EXWAPGM  DC    CL8' '        CURRENT PROGRAM                     85070
EXWASTP# DC    H'0'          STEP NUMBER                         85070
EXWAASID DC    H'0'          REQUESTED ASID                      85070
         SPACE 1                                                GP99014
         AIF   (NOT &MVSESA).OLDUCBW                            GP99014
         ORG   COPYDSN       SAVE A LITTLE STORAGE              GP99027
UNITUPAT UCBSCAN MF=(L,UNITPATU)                                GP99014
UNITWORK DC    XL100'0'      WORK AREA                          GP99014
UNITPTR  DC    A(0)          RETURNED UCB ADDRESS               GP99014
UNITUPFX DC    XL48'0'       COPY OF UCB PREFIX                 GP99014
UNITCOPY DC    XL48'0'       COPY OF UCB                        GP99014
UNITCLR  EQU   *-UNITWORK    SIZE TO CLEAR                      GP99014
UNITUFLG DC    X'0'          PROCESSING FLAG                    GP99027
UNITUAUT EQU   X'80'           CURRENT PROCESS IS AUTHORIZED    GP99027
UNITUZER EQU   X'40'           CURRENT PROCESS IS IN KEY 0      GP99027
UNITUDEL DC    Y(L'UNITUDEV)  LENGTH OF DEVICE SGEMENT          GP99014
UNITUDEV DC    XL256'0'      DEVICE DEPENDENT SEGMENT           GP99014
         AGO   .NOUCBLK                                         GP99027
.OLDUCBW AIF   (NOT &MVSXA).NOUCBLK                             GP99027
         ORG   COPYDSN       SAVE A LITTLE STORAGE               90201
UNITPARM DS    A(UNITWORK,UNITCLAS,UNITPTR)                      90201
UNITWORK DS    XL100         WORK AREA                           90201
UNITPTR  DS    A             RETURNED UCB ADDRESS                90201
UNITCLAS DS    X             REQUESTED DEVICE CLASS OR 0         90201
UNITCLR  EQU   *-UNITWORK    SIZE TO CLEAR                       90201
.NOUCBLK ORG   ,                                                 85070
         SPACE 2                                                GP97241
         AIF   (NOT &LOCO).N2L99                                 81151
         USERVOLT ,          DEFINE VOLT CALL/MAP               GP97241
         A$GDA DSECT         ACCOUNT TABLE                       89177
.N2L99   SPACE 2                                                 81151
FSAWORK  DSECT ,                                                 81165
         PRINT GEN                                               81165
WYLPLIST PSWSECT WYL=YES     EXPAND PASSWORD FOR WYLBUR          81165
         SPACE 1                                                 81165
         PRINT &PRTSOR                                           81165
         ORG   PSWDATA       RE-USE                              81165
DB       DC    35D'0'        WORK AREA                          GP97241
DBEND    EQU   *             END OF DB WORK AREA                 81165
         ORG   DB            DEFINITION FOR RDJFCB               81359
RDJFCB   DS    XL176         NEW JFCB                            81359
RDOPLIST DC    A(RDDUMDCB)   RDJFCB (OPEN) LIST                  81359
RDEXLIST DC    A(RDJFCB)     JFCB ADDRESS                        81359
RDDUMDCB DCB   DDNAME=ANY,MACRF=(WP),DSORG=PS,EXLST=RDEXLIST     90217
ALTJFCB  DS    XL176'0'      ALTERNATE JFCB                      90217
         SPACE 2                                                 85070
         ORG   DB            REDEFINE FOR CRMS FUNCTIONS         85070
SCHEDSAV DC    15F'0'   1/3  SAVE AREA                           85070
SCHEDXC  EQU   *        2/3  AREA TO BE CLEARED WITH XC          85070
SCRETCH  DC    F'0'     3/3  RETURN CODE                         85070
SCSAVKEY DC    F'0'          STORAGE PROTECT KEY                 85070
SCHEDONX DC    A(0)          SAVE AREA FOR USER'S SPIE EXIT ADDRESS
         DC    Y(0)            SPIE MASK BITS                    85070
SCHEDXCL EQU   *-SCHEDXC     LENGTH TO BE CLEARED                85070
SCHEDADC DS    F                                                 85097
SCHEDADP DS    CL8           BC-MODE PSW OF ERROR                85097
SCHEDARC DS    F                                                 85097
SCHEDARG DS    16F           REGISTERS AT TIME OF ERROR          85097
SCHECBLS DC    A(0),X'80',AL3(0)  STIMER/SCHEDULE ECB            85070
TIMECB   DC    A(0)          STIMER ECB                          85070
         AIF   (NOT &MVSXA AND NOT &MVSESA).NOLSWA              GP97241
LISTSWA  SWAREQ FCODE=RL,EPA=LISTEPA,MF=L                        90308
LISTEPA  DS    A(LISTSVA)                                        90308
LISTSVA  DS    7A  REQUEST/RESPONSE LIST (IBM LIES; 4A  ENOUGH) 90309
LISTSWAL EQU   *-LISTSWA     LENGTH TO CLEAR                     90308
         ORG   DBEND         ,                                   90308
.NOLSWA  SPACE 1                                                 90308
@SERVICE RSECT ,                                                GP99026
         LTORG ,                                                GP97241
         SPACE 2                                                 81165
         AIF   (&MVS).NOHAPS                                     81347
GENURECH DC    CL8'SYSIN',X'01'        2540R                     81165
         DC    CL8'SYSOUT',X'08'       1403                      81165
         DC    CL8'INTRDR',X'05'       2520                      81165
         DC    CL8'SYSPUNCH',X'02'     2540P                     81165
.NOHAPS  SPACE 1                                                 81165
GENUREC  DC    CL8'2540R',X'01'                                  81165
         DC    CL8'2540P',X'02'                                  81165
         DC    CL8'1403',X'08'                                   81165
         DC    CL8'3211',X'09'                                   81165
         DC    CL8'3800',X'0E'                                   90201
         DC    CL8'AFP',X'0F'                                    90201
         DC    CL8'4245',X'11'                                   90201
         DC    CL8'4248',X'13'                                   90201
         DC    CL8'UNIT RCD',X'FF'                               81165
GENTAPE  DC    CL8'2400',X'01'                                   81165
         DC    CL8'3420',X'03'                                   81165
         DC    CL8'3480',X'80'                                   86187
         DC    CL8'3490',X'81'         ALSO 9370 TAPE ?          91254
         DC    CL8'TAPE',X'FF'                                   81165
GENDASD  DC    CL8'2305-1',X'06'                                 81165
         DC    CL8'2305-2',X'07'                                 81165
         DC    CL8'9345',X'04'                                  GP99026
         DC    CL8'2314',X'08'                                   81165
         DC    CL8'3330',X'09'                                   81165
         DC    CL8'3340',X'0A'                                   81165
         DC    CL8'3350',X'0B'                                   81165
         DC    CL8'3375',X'0C'                                   82018
         DC    CL8'3330-1',X'0D'                                 81165
         DC    CL8'3380',X'0E'                                   82018
         DC    CL8'3390',X'0F'                                   90310
         DC    CL8'3330V',X'89'                                  81165
         DC    CL8'3340V',X'8A'                                  81165
         DC    CL8'3350V',X'8B'                                  81165
         DC    CL8'3375V',X'8C'                                  92297
         DC    CL8'3380V',X'8E'                                  92297
         DC    CL8'3390V',X'8F'                                  92297
         DC    CL8'3330-1V',X'8D'                                81165
         DC    CL8'DASD',X'FF'                                   81165
GENCOMM  DC    CL8'COMM',X'FF'                                   81165
GENDISP  DC    CL8'3278',X'07'                                   81165
         DC    CL8'3277',X'09'                                   81165
         DC    CL8'3284',X'0A'                                   81165
         DC    CL8'3286',X'0B'                                   81165
         DC    CL8'DISPLAY ',X'FF'                               81165
         SPACE 3                                                 90338
         PUSH  USING                                             89177
*        LOCAL ACCOUNTING RELATED LOOK-UP FUNCTIONS              89177
*                                                                89177
         AIF   (NOT &LOCO).N3L01                                 90154
ENTLKGDA LTR   R7,R7         ANY PARAMETER ?                     89177
         BZ    BADENTRY                                          89177
         BASR  R11,0                                             89177
         USING *,R11         LOCAL BASE                          89177
         LA    R9,8          PRESET RETURN/ERROR CODE            89177
         MODESET KEY=ZERO    GET PRIVILEGED                      89177
         A$GDA USE,R5,ERRET=ENTLKGDY   GET GDA OR OUT            89177
         CLI   CALLMODE,1    TEST ENTRY                          89177
         BL    ENTGFORM      FORM LOOKUP                         89177
         LM    R2,R4,A$GPAINC  PAPER                             89177
         SR    R4,R2         BACK-UP ONE ENTRY                   89177
SPAPLOOP BXH   R4,R2,ENTLKGDX  NO MATCH                          89177
         CLC   0(8,R2),0(R7) MATCHES USER REQUEST ?              89177
         BNE   SPAPLOOP      NO                                  89177
         B     SFORMRET      JOIN COMMON MOVER/RETURN            89177
ENTGFORM LM    R2,R4,A$GSFINC  LOAD SIZE, LAST, FIRST ENTRY      89177
         ICM   R14,15,0(R7)  LOAD THE USER'S FORM                89177
         ICM   R15,15,4(R7)   LOAD LOW FOUR BYTES                92352
         SR    R4,R2         SPACE PRIOR TO FIRST ENTRY          89177
SFORMLOP BXH   R4,R2,ENTLKGDX  FAIL IF FORM NOT FOUND            89177
         CLM   R15,14,4(R4)  MATCHING LOW FORM ?                 92352
         BNE   SFORMLOP      NO; TRY AGAIN                       92352
         C     R14,0(,R4)    MATCHING FORM ?                     89177
         BNE   SFORMLOP                                          89177
SFORMRET SR    R9,R9         SET FOR GOOD RETURN                 89177
         ST    R4,RETR1      RETURN ADDRESS FOR SVC CALL         92352
         CLI   SUBPOOL,FGSVC    WAS THIS AN SVC ENTRY ?          92352
         BE    ENTLKGDX      YES; JUST RETURN ADDRESS            92352
         LA    R1,COPYDSN    GET LONG WORK AREA                  89177
         BCTR  R2,0          MAKE EXECUTE LENGTH                 89177
         EX    R2,MVCGDANT   MOVE A GDA ENTRY                    89177
         ST    R1,RETR1      RETURN ADDRESS TO CALLER            89177
ENTLKGDX A$GDA DROP,R5,ERRET=ENTLKGDY  RELEASE GDA               89177
ENTLKGDY MODESET KEY=NZERO   RESTORE PROGRAM KEY                 89177
         STC   R9,RC         SET RETURN CODE                     89177
         B     EXIT          RETURN                              89177
MVCGDANT MVC   COPYDSN(0),0(R4)  PRESERVE ENTRY DATA             89177
         AGO   .C3L01                                            90154
.N3L01   SPACE 1                                                 90154
ENTLKGDA EQU   BADENTRY      LOCAL CODE ONLY                     90154
.C3L01   POP   USING                                             89177
         TITLE '@ S E R V I C E  ***  ACCOUNT NUMBER PROCESSING' 81145
GROUP1   BIX   ERR=BADENTRY,PFX=ENT,LOC=(ACGET,USGET,AUTST,FMTAC,      *
               LKGDA),BASE=@SERVICE                              89177
         SPACE 2
*        ENTRY CODE 16 - GET ACCOUNT NUMBER OF CURRENT USER
*        RETURN R15 - ALWAYS ZERO (UNLESS R1 SUPPLIED AS ZERO)   81151
*        0(8,R1) - UPDATED WITH CURRENT ACCOUNT NUMBER (OR DEFAULT)
*        R0 - HIGH TWO BYTES - BINARY ACCOUNT EQUIVALENT         81151
*             LOW BYTE - PRIVILEGES (MAPPED BY VAA FLAGS)        81151
*
*        NOTE: LOCAL ACCOUNT NUMBERS ARE FOUR BYTES, PLUS A FOUR BYTE
*          REQUIRED SUBACCOUNT. FIRST BYTE IS ALPHANUMERIC (CURRENTLY
*          ONLY ALPHA); REMAINING THREE BYTES MUST BE NUMERIC.   81151
*          THE SUBACCOUNT MUST BE ALPHANUMERIC.                  81151
*        A LOCAL MODIFICATION TO IEFUJV SAVES THE 8-BYTE NUMBER  81151
*          (FOR A VALID JOB) IN JMRUSEID.                        81151
*                                                                81151
*        MODE=0  GET CURRENT ACCOUNT FROM JMR / RETURN PRIVY FLAGS
*                WHEN THERE IS NO JMR, LOCATES $JCT ACCOUNT.     83254
*                SUPPLIED BY LOCAL HASPRDR CHANGE.               83254
*        MODE=1  TEST ACCOUNT IN R1           / RETURN OFFSET/FLAGS
*        MODE=2  TEST ACCOUNT IN R1, RETURN IT/ RETURN OFFSET/FLAGS
*        MODE=3  CONVERT OFFSET TO ACCOUNT IN INTERNAL FORM      81151
*        MODE=4  CONVERT OFFSET TO ACCOUNT IN EXTERNAL FORM      81151
         SPACE 1                                                 81151
ENTACGET LTR   R1,R7         ANY PARM ADDRESS ?                 GP99026
         BZ    BADENTRY      NO; FAIL
         AIF   (&LOCO).N1L1D                                     81151
         B     EXIT          ACCOUNT FUNCTIONS NOT SUPPLIED      81151
         AGO   .N1L1C                                            81151
.N1L1D   IC    R0,CALLMODE   GET SUB-FUNCTION                    81151
         BIX   ERR=BADENTRY,PFX=ENT,BASE=@SERVICE,                     *
               LOC=(GETAC,ACTST,ACTST,ACCON,ACCON)              GP97241
ENTGETAC LA    R10,EXIT      JUST IN CASE - SET THE RETURN ADDRESS
GETACCT  LTCB  R2            GET CURRENT TCB                     86314
         USING TCB,R2                                            81137
         LAT   R2,TCBTCT,ACCTGNON  START TASK IF NO TCT         GP99026
         USING SMFTCT,R2                                         81137
         LAT   R2,TCTJMR,ACCTGNON  TRY DEFAULT IF NO JMR        GP99026
         USING JMR,R2
         CLI   JMRUSEID,C'A'  WAS ACCOUNT NUMBER FILLED IN ?     85021
         BNL   ACCTGGOT      PRESUMABLY - USE IT                 85021
ACCTGNON LA    R2,ACCT#DEF-(JMRUSEID-JMR)  POINT TO DEFAULT ACCOUNT
         AIF   (NOT &MVS).NJESJCT                                83254
         LTCB  R15           GET TCB BACK                        83254
         ICM   R15,15,TCBJSCB-TCB(R15)  GET JSCB                GP99026
         BZ    ACCTJNON      OR SKIP                             83254
         USING IEZJSCB,R15                                       83254
         ICM   R15,15,JSCBSSIB  GET SUBSYSTEM INFORMATION BLOCK  83254
         BZ    ACCTJNON      OR OUT                              83254
         USING SSIBEGIN,R15                                      83254
         CLC   =C'JES2',SSIBSSNM  PRODUCTION JES ?               83254
         BNE   ACCTJNON      NO                                  83254
         ICM   R15,15,SSIBSUSE  GET SJB ADDRESS                  83254
         BZ    ACCTJNON                                          83254
*        USING SJBDSECT,R15                                      83254
*        CLC   =C'SJB ',SJBID   REALLY SJB ?                     83254
*        BNE   ACCTJNON      NO                                  83254
*        ICM   R15,15,SJBJCT   GET $JCT                          83254
*        BZ    ACCTJNON                                          83254
*        USING JCTDSECT,R15                                      83254
*        LA    R2,JCTACCTN-(JMRUSEID-JMR)  FAKE POINTER          83254
*        CLI   JCTJOBID,C'S'   START JOB ?                       85097
*        BNE   ACCTGGOT      NO; GO TO MOVER                     85097
*        DROP  R15                                               83254
         L     R11,=A(ENTJESVC)  GO TO EXTERNAL JES SERVICES     90273
         BASR  R9,R11        INVOKE - RETURN IF SVC              90273
.NJESJCT SPACE 1                                                 83254
ACCTJNON OI    RV,VAASTC     SET START TASK FLAG                 83254
         MVC   0(8,R1),JMRUSEID  MOVE CANNED ACCOUNT NUMBER      82018
         B     ACCTPRIM      JOIN COMMON CODE                    82018
         SPACE 1                                                 82018
ACCTGGOT MVC   0(8,R1),JMRUSEID   SEND ACCOUNT NUMBER TO USER   GP99026
         SPACE 1
ACCTCOMM DS    0H            WAS LA R10,EXIT                     86314
ACCTPRIV NI    RV,255-VAASTC  MAKE SURE IT'S CLEAN               81215
         L     R15,PSAAOLD-PSA  GET ASCB ADDRESS                 85097
         ICM   R15,15,ASCBCSCB-ASCB(R15)  GET CSCB               85097
         BZ    ACCTPRIS      SET START TASK IF NO CSCB           85097
         USING CHAIN,R15     DECLARE IT                          85097
         CLI   CHVCD,X'0C'   MOUNT ?                             85097
         BE    ACCTPRIS      YES; TREAT AS START JOB             85097
         CLI   CHVCD,X'04'   START COMMAND ?                     85097
         BNE   ACCTPRIM      NO                                  85097
         TM    CHTRKID,CHINITID  INITIATOR ?                     85097
         BO    ACCTPRIM      YES; TREAT AS JOB                   85097
ACCTPRIS OI    RV,VAASTC     SET START TASK FLAG                 85097
ACCTPRIM CLI   0(R1),C'Z'    SPECIAL ACCOUNT ?                   92297
         BE    ACCTPRSY      YES; CHECK FOR SYSTEMS PRIVILEGES   92297
         OI    RV,VAAINH     PROVISIONALLY SET IN-HOUSE          81215
         CLC   =C'G036',0(R1)    IN-HOUSE ODD-BALL ?             92297
         BE    ACCTPRCM      YES                                 82052
         CLC   =C'C90',0(R1)   OVERHEAD FOR TESTING ?            92297
         BE    ACCTPRCM      YES                                 81215
         CLC   =C'F500',0(R1)  BENCHMARKS ?                      92297
         BE    ACCTPRCM      YES                                 92297
         NI    RV,255-VAAINH                                     81215
         OI    RV,VAAUSER    ELSE SET USER                       81215
         CLC   0(4,R1),=C'6500'  USER ?                          81215
         BH    ACCTPRCM      YES                                 81215
         NI    RV,255-VAAUSER-VAAINH  RESET                      81215
         OI    RV,VAAUSER    DEFAULT                             92297
         B     ACCTPRCM      GET OUT                             92297
ACCTPRSY CLC   ACCT#SYS(3),0(R1)   SPECIAL ACCOUNT ?             81215
         BNE   ACCTPROH      NO; JUST SET OVERHEAD               81215
         CLI   3(R1),C'4'    SPECIAL RANGE ?
         BL    ACCTPROH      NO; OVERHEAD                        81215
         BE    ACCTGSYS      CHECK FOR SYSTEMS
         CLI   3(R1),C'8'
         BNH   ACCTPRSU      IN RANGE - TECHNICAL SUPPORT, ETC.  81215
         CLI   4(R1),C'0'    ANOTHER SPECIAL ?                   81215
         BE    ACCTPROH      YES; JUST SET OVERHEAD              81215
         LA    R1,4(,R1)     ACCT 0909 IS SPECIAL; REAL ACCT IN NEXT 4
         B     ACCTPRIV      TRY IT AGAIN WITH LOWER NUMBER      81215
ACCTPRSU TM    3(R1),1       905,907 ?                           86314
         BNZ   ACCTPROH      YES - JUST PLAIN OVERHEAD           86314
         OI    RV,VAASUP     SET TECHNICAL SUPPORT, ETC.         81215
         B     ACCTPROH      AND GO TO SET OVERHEAD ALSO         81215
ACCTGSYS CLC   ACCT#SUB(3),4(R1)  PROPER SUB-ACCOUNT ?           81151
         BNE   ACCTPROH      NO; SET OVERHEAD                    81215
         OI    RV,VAASYS     SET SYSTEM PRIVILEGES
ACCTPROH OI    RV,VAAOHD     SET OVERHEAD ACCOUNT                81215
ACCTPRCM BR    R10           RETURN TO CALLER                    81215
         DROP  R2
         SPACE 2
*        VALIDITY CHECK AN 8 BYTE ACCOUNT NUMBER                 81151
*        USER SUPPLIES 8 BYTE ACCOUNT # VIA R1; OR HIGH BYTE OF  81151
*              R1 MAY BE 4 TO INDICATE SHORT ACCOUNT             81151
*        RETURNS:  R15 - 16 - ZERO ADDRESS IN R1                 81151
*        R15 - 8  SUPPLIED ACCOUNT IS INVALID; REASON IN R0      81151
*        R15 - 4  ACCOUNT IS VALID, BUT NOT PERMITTED BATCH RUNS 81151
*        R15 - 0  ACCOUNT VALID. FOR R15 0 AND 4, R0 IS RETURNED 81151
*              AS FOR ACGET FUNCTION (BINARY//PRIVILEGES)        81151
*        IF MODE=2, AND RETURN IS 0 OR 4, THE NUMBER IS WRITTEN  81151
*              BACK TO THE USER'S AREA.                          81151
         SPACE 1
ENTACTST MVC   MEMNO,0(R8)   SET ACCOUNT FIELD                   93242
         CLI   CALLB0,4      CHECK FOUR BYTES ONLY ?            GP99026
         BNE   *+10          NO                                  81151
         MVC   MEMNO+4(4),ACCT#SUB  USE DEFAULT SUB-ACCOUNT      81151
         LA    R10,ACCTEXIT  SET RETURN ADDRESS
*        TREAT AS IN-LINE SUBROUTINE
         SPACE 1
CHECKACT CLI   MEMNO,C'A'    VALID CHARACTER ?                   92297
         BL    BADCHAR       NO                                  92297
         CLI   MEMNO,C'I'    IN FIRST RANGE ?                    92297
         BNH   CHECKAXX      YES; PROCEED                        92297
         CLI   MEMNO,C'J'    VALID SECOND RANGE ?                92297
         BL    BADCHAR       NO                                  92297
         CLI   MEMNO,C'R'    IN SECOND RANGE ?                   92297
         BNH   CHECKAXX      YES; PROCEED                        92297
         CLI   MEMNO,C'S'    THIRD RATER ?                       92297
         BL    BADCHAR       NO                                  92297
         CLI   MEMNO,C'Z'    THIRD GROUP ?                       92297
         BNH   CHECKAXX      YES; PROCEED                        92297
*DEFER*  CLI   MEMNO,C'0'    IN RANGE ?          DEFER UNTIL     92297
*DEFER*  BL    BADCHAR       NO                  COMPATIBILITY   92297
*DEFER*  CLI   MEMNO,C'9'    NUMERIC ?           PROBLEMS HAVE BEEN
         BH    BADCHAR       TOO BAD             ADDRESSED.      92297
CHECKAXX CLC   ACCT#FLP,MEMNO  SPECIAL ACCOUNT ?                 81151
         BNE   CHECKAZE      NO
         CLC   MEMNO+4(2),ACCT#FLP  SPECIAL SPECIAL ?            81151
         BE    BADACCT       YES; INVALID
         MVC   MEMNO(4),MEMNO+4
         MVC   MEMNO+4(4),ACCT#FLP  FLIP                         81151
CHECKAZE MVC   DB,MEMNO    CHECK 1 ALPHA, 3 NUMERIC, 4 ALPHAMERIC
         NC    DB,=X'C0F0F0F0C0C0C0C0'                           92297
         CLC   DB,=X'C0F0F0F0C0C0C0C0'  CORRECT ZONES ?          92297
         BNE   BADCHAR       NO; INVALID CHARACTERS
         LA    R3,MEMNO      SET FOR CHECK
         LA    R4,ALPHAS    TABLE OF VALID DIGITS
         LA    R5,36        FOR BCT
OKC      CLC   0(1,R3),0(R4)   MATCH FOUND IN TABLE ?            92300
         BE    OKIE          YES
         LA    R4,1(,R4)   NO, BUMP
         BCT   R5,OKC       AND CHECK AGAIN
         B     BADACCT       NO MATCH FOUND IN TABLE
OKIE     LA    R2,36         TO CALCULATE INDEX
         SR    R2,R5        REG. 2 NOW CONTAINS INDEX VALUE
         MH    R2,=Y(1000)   SCALE                               92297
         PACK  DB(8),1(3,R3)     PACK NUMERICS
         CVB   R5,DB    CONVERT IT
         AR    R5,R2        ADD IN INDEX
         LR    R0,R5         SAVE NUMERIC ACCOUNT
         SR    R4,R4       FOR DIVIDE
         D     R4,=F'8'     DIVIDE BY BITS PER BYTE
         L     R2,CVTPTR                                         92297
         ICM   R2,15,CVTUSER-CVTMAP(R2)  GET USER CVT            92297
         BZR   R10           LET SOMETHING RUN                   92297
         ICM   R2,15,UCLACCT-USERCVT(R2)  GET ACCOUNT TABLE      92297
         BZR   R10           CALL SYSTEMS ASAP                   92297
         ALR   R5,R2         ADDR. OF BYTE IN TABLE              92297
         LA    R4,MASK(R4)   ADDR. OF BIT MASK
         MVC   DB(1),0(R5)  COPY BYTE FROM MODULE
         NC    DB(1),0(R4)   TAKE OUT OTHER NUMBERS
         CLC   DB(1),0(R4)  IS NUMBER IN TABLE ?
         BNE   BADACCT       NO
         BR    R10           INDICATE GOOD ACCOUNT
         SPACE 2
ACCTEXIT AL    R5,=F'4500'   SPACE TO SECOND TABLE               82297
         MVC   DB(1),0(R5)   COPY RUN PRIVILEGE BYTE
         LA    R1,MEMNO      SET ADDRESS FOR ACCTCOMM
         CLI   CALLMODE,VENACTSM/256  RETURN VALUE TO USER ?     81151
         BNE   ACCTEXNM      NO                                  81151
         LA    R14,7         SET L-1 FOR MOVE                    81151
         CLI   CALLB0,4      LENGTH=4 ?                         GP99026
         BNE   *+8           NO                                  81151
         LA    R14,3         ELSE MOVE ONLY 4                    81151
         EX    R14,ACCTEXMV  RETURN TO USER'S AREA               81151
ACCTEXNM STH   R0,RETR0      RETURN OFFSET IN HIGH PORTION OF R0
         LA    R10,EXIT      SET FOR MAIN EXIT                   86314
         NC    DB(1),0(R4)   TAKE OUT OTHER NUMBERS
         CLC   DB(1),0(R4)   IS NUMBER DISABLED IN TABLE ?
         BE    ACCTCOMM      NO; LEAVE 0 RETURN CODE             82297
         MVI   RC,4          SET MINOR ERROR
         B     ACCTCOMM      GO TO SET PRIVILEGE FLAGS
ACCTEXMV MVC   0(0,R8),MEMNO  RETURN (POSSIBLY CHANGED) ACCOUNT  93242
         SPACE 1
BADACCT  MVI   RV,VRACCT     SET FOR BAD ACCOUNT
         B     BADRET8       SET RETURN CODE 8
         SPACE 2
*        CONVERT BINARY ACCOUNT TO EBCDIC                        81151
*        R1 CONTAINS BINARY ACCOUNT NUMBER                       81151
*        RETURNS:  R15 - 16 - NUMBER NOT IN RANGE (1-35999)      81151
*        R15 - 0 - EBCDIC ACCOUNT NUMBER IN R0                   81151
*          WITH MODE=4, FORMAT IS EXTERNAL (DSN) FORM WITH LEADING
*          ALPHA, ELSE RETURNED WITH LEADING NUMERIC.            81151
ENTACCON LTR   R7,R7         ANY PARM ?                         GP99026
         BNP   BADENTRY      NO; ERROR
         C     R7,=F'35999'  VALID RANGE ?
         BH    BADENTRY      NO; FAIL
         SR    R6,R6
         D     R6,=F'10'     ISOLATE LAST DIGIT
         STC   R6,RETR0+3    SAVE IT
         SR    R6,R6
         D     R6,=F'10'
         STC   R6,RETR0+2    THIRD DIGIT
         SR    R6,R6
         D     R6,=F'10'     GET SECOND BYTE
         STC   R6,RETR0+1    SET SECOND DIGIT                    92297
         LA    R7,ALPHAS(R7)  POINT TO ALPHABETICS
         MVC   RETR0(1),0(R7)  MOVE HIGH CHARACTER               92300
         OC    RETR0(4),=X'00F0F0F0'  MAKE NUMERICS
         B     EXIT          EXIT OK
.N1L1C   SPACE 2                                                 81151
*        GET AND CHECK TSO USER-ID                               86314
*                                                                86314
ENTUSGET IC    R0,CALLMODE   GET SUB-FUNCTION                    86314
         BIX   ERR=BADENTRY,PFX=ENT,BASE=@SERVICE,LOC=(USGT2,USTST)
         SPACE 1                                                 86314
GETUID   DS    0H            INTERNAL SUBROUTINE                 86314
ENTUSGT2 MVC   COPYINX,=CL8' '  CLEAR RETURN LOCATION            86314
         LA    R1,COPYINX    POINT TO FAKE RETURN                86314
         LR    R7,R1         DITTO                               86314
         ST    R1,RETR1      SET USER'S RETURN ADDRESS           86314
         L     R2,PSAAOLD-PSA   GET CURRENT ASCB                 86314
         ICM   R14,15,ASCBTSB-ASCB(R2)  ARE WE RUNNING UNDER TSO ?
         BZ    ENTUSGT3      NO                                  86314
         L     R3,PSATOLD-PSA  GET CURRENT TCB                   86314
         L     R3,TCBTIO-TCB(,R3)  GET THE TIOT                  86314
         MVC   COPYINX,0(R3) COPY JOBNAME AS USER-ID             86314
         BR    R10           RETURN TO CALLER OR EXIT            86314
         AIF   (&LOCO).N1LUID                                    86314
ENTUSGT3 B     BADWINDX      NO ID; FAKE BAD INDEX LEVEL CODE    86314
ENTUSTST BR    R10           UID TEST - CAN'T CHECK - PASS IT    86314
         AGO   .N1NUID                                           86314
.N1LUID  SPACE 1                                                 86314
ENTUSGT3 LR    R6,R10        PRESERVE RETURN REGISTER            86314
         BAS   R10,GETACCT   GET THE CURRENT ACCOUNT NUMBER      86314
         LR    R10,R6        RESTORE RETURN                      86314
         L     R3,CVTPTR     GET THE CVT                         86314
         ICM   R3,15,CVTUSER-CVTMAP(R3)  GET THE USER EXTENSION  86314
         BZ    BADWINDX      TOO BAD                             86314
         ICM   R3,15,UCLUADS-USERCVT(R3)  UADS EXTENSION PRESENT ?
         BZ    BADWINDX      NO                                  86314
         LM    R3,R5,0(R3)   LOAD THE BXLE POINTERS              86314
         SR    R3,R4         BACK-UP FOR CHECK UP FRONT          86314
USGT4LOP BXH   R3,R4,BADWINDX  NOT FOUND                         86314
         CLC   COPYINX,8(R3)  MATCHING ACCOUNT ?                 86314
         BNE   USGT4LOP      NO; TRY AGAIN                       86314
         OI    RETCH+3,4     SET MINOR ERROR                     86314
         MVC   COPYINX,0(R3) COPY MATCHING USER ID               86314
         MVI   COPYINX+7,C' '   MAKE TRAILING BLANK              86314
         BR    R10           RETURN TO CALLER                    86314
         SPACE 1                                                 86314
ENTUSTST L     R3,CVTPTR     GET THE CVT                         86314
         ICM   R3,15,CVTUSER-CVTMAP(R3)  GET THE USER EXTENSION  86314
         BZ    BADWINDX      TOO BAD                             86314
         ICM   R3,15,UCLUADS-USERCVT(R3)  UADS EXTENSION PRESENT ?
         BZ    BADWINDX      NO                                  86314
         LM    R3,R5,0(R3)   LOAD THE BXLE POINTERS              86314
         SR    R3,R4         BACK-UP FOR CHECK UP FRONT          86314
USTS4LOP BXH   R3,R4,BADWINDX  NOT FOUND                         86314
         CLC   0(7,R3),0(R7)  MATCHING ID ?                      86314
         BNE   USTS4LOP      NO; TRY AGAIN                       86314
         ST    R3,RETR1      RETURN ENTRY ADDRESS                86314
         BR    R10           EXIT                                86314
.N1NUID  SPACE 1                                                 86314
ENTAUTST B     BADENTRY      NOT YET IMPLEMENTED                 81151
         SPACE 2                                                 81215
*        FORMAT CHECK WITHOUT VALIDITY CHECK                     81215
*        R1(R7) IS ADDRESS OF DATA TO BE CHECKED                 81215
*        RETURNS R15=0 IF VALID; R0=RESULT AREA:                 81215
*              XL1(VAA PRIV),CL8'ACCOUNT',CL8'USERID'            81215
*                                                                81215
         AIF   (&LOCO).N1LFMT                                    81236
ENTFMTAC B     BADENTRY      NOT IMPLEMENTED                     81236
         AGO   .N1CFMT                                           81236
.N1LFMT  SPACE 1                                                 81236
ENTFMTAC LTR   R7,R7         ANY PARM ?                          81215
         BZ    BADENTRY      NO; TOO BAD                         81215
         MVI   FMTPRIV,0                                         81215
         MVC   FMTACCT,ZZEROES  INIT TO ZERO                     81215
         MVC   FMTUSER,BLANKS   INIT USER TO BLANKS              81215
         MVC   MEMNO+4(4),ACCT#SUB  SET DEFAULT SUBACCOUNT       81215
         IC    R0,CALLMODE   GET ENTRY CODE                      81215
         BIX   ERR=BADENTRY,PFX=ENT,BASE=@SERVICE,                     *
               LOC=(ACFMT,FMTLB,FMTWY,FMTTS)                    GP97241
ENTACFMT CLI   CALLB0,4      EXPLICIT LENGTH OF 4 ?             GP99026
         BNE   FMTAC8        NO; DO 8                            81215
         MVC   FMTACCT(4),0(R7)  MOVE WITHOUT SUB-ACCOUNT        81215
         MVC   MEMNO(4),0(R7)                                    81215
         B     FMTACCOM                                          81215
FMTAC8   MVC   FMTACCT,0(R7)  MOVE ACCOUNT AND SUB-ACCOUNT       81215
         MVC   MEMNO,FMTACCT                                     81215
FMTACCOM CLI   MEMNO,C'A'    VALID CHARACTER ?                   92297
         BL    BADACCT       NO                                  92297
         CLI   MEMNO,C'I'    IN FIRST RANGE ?                    92297
         BNH   FMTACZON      YES; PROCEED                        92297
         CLI   MEMNO,C'J'    VALID SECOND RANGE ?                92297
         BL    BADACCT       NO                                  92297
         CLI   MEMNO,C'R'    IN SECOND RANGE ?                   92297
         BNH   FMTACZON      YES; PROCEED                        92297
         CLI   MEMNO,C'S'    THIRD RATER ?                       92297
         BL    BADACCT       NO                                  92297
         CLI   MEMNO,C'Z'    THIRD GROUP ?                       92297
         BNH   FMTACZON      YES; PROCEED                        92297
*DEFER*  CLI   MEMNO,C'0'    IN RANGE ?          DEFER UNTIL     92297
*DEFER*  BL    BADACCT       NO                  COMPATIBILITY   92297
*DEFER*  CLI   MEMNO,C'9'    NUMERIC ?           PROBLEMS HAVE BEEN
         BH    BADACCT       TOO BAD             ADDRESSED.      92297
FMTACZON MVC   DB,=X'C0F0F0F0C0C0C0C0'                           92297
         NC    DB,MEMNO                                          81215
         CLC   DB,=X'C0F0F0F0C0C0C0C0'  VALID ZONES ?            92297
         BNE   BADACCT                                           81215
         LA    R1,MEMNO                                          81215
         BAS   R10,ACCTPRIV  GET PRIVILEGES                      81215
         MVC   FMTPRIV,RV    COPY PRIVILEGE BYTE                 81215
         LA    R1,FMTPRIV    POINT TO RETURN LIST                81215
         ST    R1,RETR0      RETURN ADDRESS IN R0                81215
         B     EXIT          RETURN TO CALLER                    81215
         SPACE 1                                                 81215
ENTFMTLB CLI   4(R7),C'.'    PROPER INDEX FOR LIBPAK NAME ?      81215
         BNE   BADWINDX      NO; SET INVALID INDEX               81215
         MVC   MEMNO(4),0(R7)   COPY PRESUMED ACCOUNT            81215
         MVC   FMTACCT(4),0(R7)  DITTO                           81215
         B     FMTACCOM      GO TO CHECK ACCOUNT                 81215
         SPACE 1                                                 81215
ENTFMTWY CLI   3(R7),C'.'    PROPER INDEX ?                      81215
         BNE   BADWINDX      NO                                  81215
         CLI   12(R7),C'.'   ACCOUNT INDEX ?                     81215
         BNE   BADWNAME                                          81215
         MVC   MEMNO,4(R7)   MOVE PRESUMED ACCOUNT               81215
         MVC   FMTACCT,4(R7)                                     81215
         MVC   FMTUSER,13(R7)  AND PRESUMED USER ID              81215
         B     FMTACCOM      CHECK ACCOUNT                       81215
         SPACE 1                                                 81215
ENTFMTTS B     BADENTRY      NOT IMPLEMENTED                     81215
.N1CFMT  SPACE 1                                                 89177
         LTORG ,                                                 89177
         TITLE '@ S E R V I C E  ***  SVA / SWA SERVICES'        90338
         ORG   @SERVICE+4096   ELIMINATE A FEW HLASM ERRORS     GP97241
***********************************************************************
*                                                                     *
*        SWARL - CONVERT SVA ADDRESS/TOKEN TO SWA ADDRESS(31-BIT)     *
*              SERVCALL - COPY TEXT TO LOCAL WORK AREA, RETURN ADDR.  *
*              SERVICE - RETURN 31-BIT ADDRESS ONLY                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90316
ENTSWARL LTR   R2,R7         TOKEN PASSED ?                      90316
         BZ    BADENTRY      NO; FAIL                            90316
         PUSH  USING                                            GP97241
         BASR  R11,0         MAKE LOCAL BASE                     90316
         USING *,R11                                             90316
         LA    R11,SIOTBASE  MAKE SIOT BASE                      90316
         USING SIOTBASE,R11                                      90316
         B     SIOTCOMN      JOIN COMMON SWARL CODE              90316
         POP   USING                                            GP97241
         SPACE 1                                                GP02265
***********************************************************************
*                                                                     *
*        SWAAD - CONVERT SVA ADDRESS/TOKEN TO SWA ADDRESS(31-BIT)     *
*              AND RETURN 31-BIT ADDRESS ONLY                         *
*                                                                     *
***********************************************************************
         SPACE 1                                                GP02265
ENTSWAAD LTR   R2,R7         TOKEN PASSED ?                     GP02265
         BZ    BADENTRY      NO; FAIL                           GP02265
         PUSH  USING                                            GP02265
         BASR  R11,0         MAKE LOCAL BASE                    GP02265
         USING *,R11                                            GP02265
         LA    R11,SIOTBASE  MAKE SIOT BASE                     GP02265
         USING SIOTBASE,R11                                     GP02265
         N     R2,=X'00FFFFFF'  CLEAN TOKEN                     GP03075
         BZ    BADENTRY      NO; FAIL                           GP03075
         TSX   R9,LOOKSWA,AMODE=31  CONVERT TOKEN               GP02265
         LTR   R2,R1         TEST                               GP02265
         BZ    BADRET8       OOPS                               GP02265
         B     SIOTECM       JOIN COMMON EXIT CODE              GP02265
         POP   USING                                            GP02265
         SPACE 1                                                GP02265
***********************************************************************
*                                                                     *
*   SWAAB - CONVERT SVA ADDRESS/TOKEN TO SWA ADDRESS(31-BIT)          *
*           AND RETURN 31-BIT ADDRESS ONLY                            *
*           RETURN CALLER'S ADDRESS IF EVEN (E.G., TIOEFSRT)          *
*                                                                     *
***********************************************************************
         SPACE 1                                                GP03075
ENTSWAAB LTR   R2,R7         TOKEN PASSED ?                     GP03075
         BZ    BADENTRY      NO; FAIL                           GP03075
         PUSH  USING                                            GP03075
         BASR  R11,0         MAKE LOCAL BASE                    GP03075
         USING *,R11                                            GP03075
         LA    R11,SIOTBASE  MAKE SIOT BASE                     GP03075
         USING SIOTBASE,R11                                     GP03075
         N     R2,=X'00FFFFFF'  CLEAN TOKEN                     GP03075
         BZ    BADENTRY      NO; FAIL                           GP03075
         EX    R2,EXTMODD    IS CALLER'S ADDRESS ODD ?          GP03075
         BZ    SIOTECM       NO; RETURN EXACT ADDRESS           GP03075
         TSX   R9,LOOKSWA,AMODE=31  CONVERT TOKEN               GP03075
         LTR   R2,R1         TEST                               GP03075
         BZ    BADRET8       OOPS                               GP03075
         B     SIOTECM       JOIN COMMON EXIT CODE              GP03075
         POP   USING                                            GP03075
         SPACE 1                                                GP02265
***********************************************************************
*                                                                     *
*        SIOT LOOK-UPS REQUIRE 31-BIT MODE IN AND AFTER SP2           *
*        THE SIOT TEXT IS MOVED TO THE LOCAL WORK AREA, AND RETURNED  *
*        VIA R1; THE DSAB ADDRESS IS RETURNED IN R0                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90308
ENTSIOTE LTR   R7,R7         ANY ADDRESS PASSED ?                90308
         BZ    BADRET16      NO; ERROR                           90308
         PUSH  USING                                            GP97241
         BASR  R11,0         MAKE LOCAL BASE                     90308
         USING *,R11         DECLARE IT                          90308
SIOTBASE LTCB  R3            GET THE TCB                         90308
         ICM   R2,15,TCBJSCB-TCB(R3)  GET THE JSCB               90308
         BZ    BADRET12      NONE ?                              90308
         USING IEZJSCB,R2    DECLARE IT                         GP13007
         LAT   R3,JSCDSABQ,BADENTRY GET DSAB QUEUE               90308
         USING QDB,R3        DECLARE QUEUE DESCRIPTOR            90308
         LA    R3,QDBFELMP-(DSABFCHN-DSAB)  FAKE IT              90308
         USING DSAB,R3                                           90308
PULLDSAB ICM   R3,15,DSABFCHN  NEXT DSAB ?                       90308
         BZR   R10           NO; QUIT                            90308
         C     R7,DSABTIOT   FOR THIS ENTRY ?                    90308
         BNE   PULLDSAB      NO                                  90308
         ST    R3,RETR0      RETURN DSAB ADDRESS                 90308
         SR    R2,R2                                             92285
         ICM   R2,7,DSABSSVA  GET SIOT VIRTUAL ADDRESS           90308
         BZR   R10           NO SIOT ?                           90308
         USING INDMSIOT,R2   DECLARE IT                          90308
SIOTCOMN TSX   R9,LOOKSWA,AMODE=31  GO TO SWA LOOK-UP            90316
         LTR   R2,R1         ANYTHING RETURNED ?                 90308
         BZ    BADRET8       NO; FAIL                            90308
         CLI   SUBPOOL,FGSVC SVC ENTRY ?                         90309
         BE    SIOTECM       YES; RETURN SWA ADDRESS TO USER     90310
         LA    R2,DB         POINT TO RETURN AREA                90308
         LA    R0,SIOTLGTH   SET LENGTH TO MOVE                  90308
         TSX   R9,MOVE31,AMODE=31  MOVE TEXT FROM R1 TO R2       90308
SIOTECM  ST    R2,RETR1      RETURN TO USER                      90310
         BR    R10           RETURN                              90308
         SPACE 2                                                 90308
***********************************************************************
*                                                                     *
*        SWA LOOK-UP SUBROUTINE                                       *
*        R1 - RETURNED SWA ADDRESS                                    *
*        R2 - REQUESTED SVA ADDRESS; 24-BIT, RIGHT-JUSTIFIED          *
*        R9 - RETURN IN 24 OR 31 BIT MODE                             *
*                                                                     *
***********************************************************************
LOOKSWA  DS    0H                                                90316
         AIF   (NOT &MVSXA AND NOT &MVSESA).NOSVA2              GP97241
         SPACE 1                                                 90308
         EX    R2,EXTMODD    IS IT A TOKEN OR AN ADDRESS?       GP03075
         BZ    LOOKSVA       ADDRESS - RETURN AS IS             GP03075
         XC    LISTSWA(LISTSWAL),LISTSWA  CLEAR IT               90308
         STCM  R2,7,LISTSVA+4  PLACE IN SECOND WORD OF LIST      90308
         LA    R1,LISTSVA    GET SVA ADDRESS                     90308
         ST    R1,LISTEPA    STORE IN POINTER                    90308
         MVC   LISTSWA+8,=C'RL'  SET FUNCTION                    90308
         LA    R1,LISTSWA+8                                      90308
         ST    R1,LISTSWA+4  POINTER TO FUNCTION                 90308
         SWAREQ EPA=LISTEPA,MF=(E,LISTSWA),UNAUTH=YES            90308
         SR    R1,R1         SIGNAL ERROR                        90308
         BXH   R15,R15,LOOKSWA8  UNEXPECTED FAILURE              90308
         L     R1,LISTSVA    GET STORAGE ADDRESS OF JFCB         90308
         B     LOOKSWA8      COMMON RETURN                       92279
.NOSVA2  ANOP  ,                                                 92279
LOOKSVA  LA    R1,16(,R2)    SPACE TO JFCB PROPER                90310
LOOKSWA8 RET31 R9            RETURN TO CALLER                    90308
EXTMODD  TM    =X'01',*-*    TEST FOR ODD REGISTER VALUE        GP03075
         SPACE 2                                                 90308
***********************************************************************
*                                                                     *
*        31-BIT DATA MOVER                                            *
*          R0 - LENGTH    R1 - FROM     R2 - TO ADDRESS               *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90308
MOVE31   STM   R14,R3,20(R13)  SAVE A BIT                        90308
         LR    R14,R1        COPY FROM ADDRESS                   90308
         LR    R15,R0        COPY LENGTH                         90308
         LR    R3,R0         COPY LENGTH                         90308
         MVCL  R2,R14        MOVE TEXT                           90308
         LM    R14,R3,20(R13)  RESTORE                           90308
         RET31 R9            RETURN IN CALLER'S MODE             90308
         POP   USING                                             90308
         SPACE 1                                                GP02265
***********************************************************************
*                                                                     *
*    DSABL - LOOPS THROUGH THE DSAB CHAIN                             *
*      WHEN R1 IS ZERO, RETURNS FIRST ENTRY                           *
*      OTHERWISE RETURNS NEXT ENTRY FROM R1 (NO CHECK)                *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90308
         PUSH  USING                                            GP03075
ENTDSABL BASR  R11,0         MAKE LOCAL BASE                    GP03075
         USING *,R11         DECLARE IT                         GP03075
         XC    RETR0(L'RETR0+L'RETR1),RETR0   CLEAR IN CASE OF ERROR
         LTR   R3,R7         ANY ADDRESS PASSED ?               GP03075
         BNZ   NEXTDSAB      YES; GET NEXT ONE AFTER            GP03075
         LTCB  R3            GET THE TCB                        GP03075
         ICM   R2,15,TCBJSCB-TCB(R3)  GET THE JSCB              GP03075
         BZ    BADRET12      NONE ?                             GP03075
         USING IEZJSCB,R2    DECLARE IT                         GP13007
         LAT   R3,JSCDSABQ,BADENTRY GET DSAB QUEUE              GP03075
         USING QDB,R3        DECLARE QUEUE DESCRIPTOR           GP03075
         LA    R3,QDBFELMP-(DSABFCHN-DSAB)  FAKE IT             GP03075
         USING DSAB,R3                                          GP03075
NEXTDSAB ICM   R3,15,DSABFCHN  NEXT DSAB ?                      GP03075
         BZ    BADRET4       NO; QUIT                           GP03075
         ICM   R4,15,DSABTIOT  GET TIOT ADDRESS                 GP03075
         ST    R4,RETR0      RETURN TIOT ADDRESS                GP03075
         ST    R3,RETR1      RETURN DSAB ADDRESS                GP03075
         BR    R10           RETURN                             GP03075
         POP   USING                                            GP03075
         SPACE 1                                                GP02265
***********************************************************************
*                                                                     *
*    DSABD - LOCATE REQUESTED DD NAME (POINTED TO BY R1)              *
*      RETURN R1=0 IF NOT FOUND, ELSE DSAB ADDRESS                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90308
         PUSH  USING                                            GP03075
ENTDSABD BASR  R11,0         MAKE LOCAL BASE                    GP03075
         USING *,R11         DECLARE IT                         GP03075
         XC    RETR0(L'RETR0+L'RETR1),RETR0   CLEAR IN CASE OF ERROR
         LTR   R7,R7         ANY ADDRESS PASSED ?               GP03075
         BZ    BADRET12      NO; FAIL BIG                       GP03075
         LTCB  R3            GET THE TCB                        GP03075
         ICM   R2,15,TCBJSCB-TCB(R3)  GET THE JSCB              GP03075
         BZ    BADRET12      NONE ?                             GP03075
         USING IEZJSCB,R2    DECLARE IT                         GP13007
         LAT   R3,JSCDSABQ,BADENTRY GET DSAB QUEUE              GP03075
         USING QDB,R3        DECLARE QUEUE DESCRIPTOR           GP03075
         LA    R3,QDBFELMP-(DSABFCHN-DSAB)  FAKE IT             GP03075
         USING DSAB,R3                                          GP03075
BUMPDSAB ICM   R3,15,DSABFCHN  NEXT DSAB ?                      GP03075
         BZ    BADRET4       NO; QUIT                           GP03075
         ICM   R4,15,DSABTIOT  GET TIOT ADDRESS                 GP03075
         BZ    BUMPDSAB      HUH ?                              GP03075
         USING TIOENTRY,R4   DECLARE IT                         GP03075
         CLC   TIOEDDNM,0(R7)  REQUESTED DD NAME ?              GP03075
         BNE   BUMPDSAB      NO                                 GP03075
         ST    R4,RETR0      RETURN TIOT ADDRESS                GP03075
         ST    R3,RETR1      RETURN DSAB ADDRESS                GP03075
         BR    R10           RETURN                             GP03075
         POP   USING                                            GP03075
         TITLE '@ S E R V I C E  ***  SORT AND LOOKUP ROUTINES'  82024
*        MISCELLANEOUS SERVICE ROUTINES                          82024
*                                                                82024
ENTSORTA LTR   R10,R7        ANY PARM ?                          82024
         BZ    BADENTRY      NO; TOO BAD                         82024
         PUSH  USING                                            GP97241
         BASR  R11,0                                             82024
         USING *,R11         MAKE LOCAL BASE                     82024
         IC    R0,CALLMODE   GET SUB-FUNCTION                    82024
         BIX   ERR=BADENTRY,PFX=ENT,BASE=@SERVICE,                     *
               LOC=(SORTB,SORTH,,BINLK)                         GP97241
         SPACE 1                                                 82024
*        COMMON PARAMETER AREA FOR SORT ROUTINES :               82024
*        R1 => +0     START OF AREA (LOW ENTRY)                  82024
*              +4     X'80'+ADDRESS OF LAST ENTRY  OR            82024
*                     X'00'+NUMBER OF ENTRIES                    82024
*              +8     C'A' OR C'D' FOR SORT SEQUENCE             82024
*              +9     COMPARE OFFSET FROM START OF ENTRY         82024
*              +10    COMPARE LENGTH                             82024
*              +11    ENTRY LENGTH (<=255)                       82024
*              +12    BINLK ONLY - ADDRESS OF COMPARE KEY        82038
*                                                                82024
*        THE HEAPSORT ALGORITHM WAS DISCOVERED BY WILLIAMS AND FLOYD.
*        THIS VERSION ADAPTED FROM NIJENHUIS AND WILF IN         82024
*        'COMBINATORIAL ALGORITHMS' (ACAD. PRESS).               82024
*                                                                82024
         USING SORTPARM,R10  DECLARE USER'S WORK AREA            82024
ENTSORTH MVC   SORTHMBA(SORTHPAL),SORTHPAT  MOVE EXECUTE PATTERNS
         ICM   R7,15,SOPLOW  GET START TABLE ADDRESS             82024
         BNP   BADENTRY      BAD ADDRESS                         82024
         SR    R8,R8                                             82024
         ICM   R8,1,SOPLEN   COPY ENTRY SIZE                     82024
         BZ    BADENTRY                                          82024
         ICM   R1,15,SOPHIGH    ADDRESS OR COUNT PASSED ?        82024
         BZ    BADENTRY      NEITHER                             82024
         BP    SORTHCNT      COUNT                               82024
         N     R1,=X'7FFFFFFF'   MASK FLAG BIT                   82024
         B     SORTHCN2                                          82024
SORTHCNT MR    R0,R8         GET OFFSET TO LAST                  82024
         SR    R1,R8         MAKE RELATIVE                       82024
         AR    R1,R7         ADD START ADDRESS                   82024
SORTHCN2 SR    R1,R7         GET TOTAL SIZE                      82024
         BM    BADENTRY      ERROR                               82024
         BZ    EXIT          NO ENTRIES                          82024
         SR    R0,R0                                             82024
         DR    R0,R8                                             82024
         MR    R0,R8         ENSURE END IS MULTIPLE              82024
         ST    R1,SRTMAXO    SET MAXIMUM OFFSET                  82024
         SR    R5,R5                                             82024
         ICM   R5,1,SOPCOL   GET COMPARE LENGTH                  82024
         BZ    BADENTRY      NOT POSITIVE ?                      82024
         SR    R6,R6                                             82024
         IC    R6,SOPCOO     GET COMPARE OFFSET                  82024
         LA    R1,0(R6,R5)   OFFSET+LENGTH                       82024
         CR    R1,R8         VALID ?                             82024
         BH    BADENTRY      NO                                  82024
         STC   R6,SORTHCAA+5   SET COMPARE OFFSET                82024
         STC   R6,SORTHCBA+5                                     82024
         ICM   R15,3,SORTHCAA+2  GET PATTERN                     82024
         AR    R15,R6        ADD OFFSET                          82024
         AR    R15,R8        ADD ENTRY LENGTH                    82024
         STCM  R15,3,SORTHCAA+2  SET COMPARE OFFSET + ONE ENTRY  82024
         ICM   R15,3,SORTHCBA+2   GET B IN S FORMAT              82024
         AR    R15,R6        B+OFFSET                            82024
         STCM  R15,3,SORTHCBA+2   SET B+OFFSET                   82024
         BCTR  R5,0          SET CLC LENGTH-1                    82024
         STC   R5,SORTHCAA+1   SET CLC LENGTH                    82024
         STC   R5,SORTHCBA+1                                     82024
         LR    R14,R8                                            82024
         BCTR  R14,0         ENTRY LENGTH-1                      82024
         STC   R14,SORTHMBA+1  SET MOVE LENGTH                   82024
         STC   R14,SORTHMAA+1                                    82024
         CLI   SOPASC,C'D'   DESCENDING SORT ?                   82024
         BNE   SORTH102      NO                                  82024
         MVI   SORTHBAA+1,X'B0'  MAKE BNL                        82024
         MVI   SORTHBBA+1,X'D0'   MAKE BNH                       82024
         SPACE 1                                                 82024
SORTH102 L     R6,SRTMAXO    GET OFFSET TO LAST ELEMENT          82024
         LR    R5,R6         COPY                                82024
         SR    R5,R8         FINAGLE                             82024
         BM    EXIT          ONE ENTRY                           82024
         SR    R4,R4         CLEAR FOR DIVIDE                    82024
         DR    R4,R8         GET NUMBER OF ENTRIES IN R5         82024
         SRL   R5,1          HALVE                               82024
         MR    R4,R8         OFFSET TO MIDDLE ELEMENT IN R5      82024
SORTH104 LA    R1,0(R5,R7)   A(L)                                82024
         EX    0,SORTHMBA    MOVE A(L) TO SAVE AREA              82024
         BAS   R9,SORTHEAP   HEAP IT                             82024
         SR    R5,R8         GET PRIOR ELEMENT                   82024
         BNM   SORTH104      REPEAT FOR IT                       82024
*                                                                82024
         SR    R5,R5         L=1                                 82024
         L     R6,SRTMAXO    GET OFFSET TO LAST ENTRY            82024
         SR    R6,R8         GET PRIOR ENTRY                     82024
SORTH110 LR    R1,R6         M                                   82024
         AR    R1,R8         M+1                                 82024
         AR    R1,R7         A(M+1)                              82024
         EX    0,SORTHMBA    B=A(M+1)                            82024
         LR    R14,R1                                            82024
         LR    R1,R7         A(1)                                82024
         EX    0,SORTHMAA    A(M+1)=A(1)                         82024
         BAS   R9,SORTHEAP   SORT                                82024
         SR    R6,R8         GET PRIOR ENTRY                     82024
         BNM   SORTH110      REPEAT                              82024
         B     EXIT          ALL DONE ?                          82024
         SPACE 1                                                 82024
SORTHEAP LR    R3,R5         I=L                                 82024
SORTH117 LA    R4,0(R3,R3)   J=I+I                               82024
         AR    R4,R8         RELATIVE TO 0                       82024
         CR    R4,R6         J ? M                               82024
         BH    SORTH125      HIGH                                82024
         BE    SORTH121      J=M                                 82024
         LA    R1,0(R4,R7)   A(J)                                82024
         EX    0,SORTHCAA      A(J+1) > A(J)                     82024
         EX    0,SORTHBAA                                        82024
         AR    R4,R8         J=J+1                               82024
SORTH121 LA    R1,0(R4,R7)   A(J)                                82024
         EX    0,SORTHCBA       B >= A(J)  ?                     82024
         EX    0,SORTHBBA    YES                                 82024
         LA    R14,0(R3,R7)  A(I)                                82024
         EX    0,SORTHMAA    A(I)=A(J)                           82024
         LR    R3,R4         I=J                                 82024
         B     SORTH117                                          82024
*                                                                82024
SORTH125 LA    R14,0(R3,R7)  A(I)                                82024
         LA    R1,SORTHOLD                                       82024
         EX    0,SORTHMAA    A(I)=B                              82024
         BR    R9            RETURN                              82024
         SPACE 1                                                 82024
SORTHPAT MVC   SORTHOLD(0),0(R1)       MVC B,A(X)                82024
         MVC   0(0,R14),0(R1)          MVC A(Y),A(X)             82024
         CLC   0(0,R1),0(R1)           CLC A(J+1),A(J)           82024
         CLC   SORTHOLD(0),0(R1)       CLC B+OFF,OFF+A(J)        82024
         BNH   SORTH121                                          82024
         BNL   SORTH125                                          82024
         SPACE 2                                                 82024
*        BUBBLE SORT (ALSO KNOWN AS TRIANGULAR SORT)             82024
*        VERY SLOW UNLESS DATA MOSTLY SEQUENCED ALREADY.         82024
*                                                                82024
ENTSORTB MVC   SORTBBC(SORTBPAL),SORTBPAT  MOVE EXECUTE PATTERNS 82024
         ICM   R2,15,SOPLOW  GET START TABLE ADDRESS             82024
         BNP   BADENTRY      BAD ADDRESS                         82024
         SR    R6,R6                                             82024
         ICM   R6,1,SOPLEN   COPY ENTRY SIZE                     82024
         BZ    BADENTRY                                          82024
         ICM   R5,15,SOPHIGH    ADDRESS OR COUNT PASSED ?        82024
         BZ    BADENTRY      NEITHER                             82024
         BP    SORTBCNT      COUNT                               82024
         N     R5,=X'7FFFFFFF'   MASK FLAG BIT                   82024
         B     SORTBCN2                                          82024
SORTBCNT MR    R4,R6         GET OFFSET TO LAST                  82024
         SR    R5,R6         MAKE RELATIVE                       82024
         AR    R5,R2         ADD START ADDRESS                   82024
SORTBCN2 CR    R5,R2         COMPARE END AGAINST START           82024
         BL    BADENTRY      ERROR                               82024
         BE    EXIT          NO ENTRIES                          82024
         SR    R1,R1                                             82024
         ICM   R1,1,SOPCOL   GET COMPARE LENGTH                  82024
         BZ    BADENTRY      NOT POSITIVE ?                      82024
         BCTR  R1,0                                              82024
         STC   R1,SORTBBC+1  SET INTO CLC                        82024
         SR    R4,R4                                             82024
         IC    R4,SOPCOO     GET COMPARE OFFSET                  82024
         LA    R3,0(R1,R4)   OFFSET+LENGTH                       82024
         CR    R3,R6         VALID ?                             82024
         BH    BADENTRY      NO                                  82024
         STC   R4,SORTBBC+3   SET COMPARE OFFSET                 82024
         STC   R4,SORTBBC+5                                      82024
         LR    R14,R6                                            82024
         BCTR  R14,0         ENTRY LENGTH-1                      82024
         STC   R14,SORTBXC1+1  SET MOVE LENGTH                   82024
         STC   R14,SORTBXC2+1                                    82024
         CLI   SOPASC,C'D'   DESCENDING SORT ?                   82024
         BNE   SORTB102      NO                                  82024
         MVI   SORTBB+1,X'B0'  MAKE BNL                          82024
         SPACE 1                                                 82024
SORTB102 LR    R4,R6         COPY LENGTH                         82024
         LR    R7,R5         COPY END ADDRESS                    82024
         SR    R5,R4         NEXT TO LAST ENTRY ADDRESS          82024
         CR    R5,R2                                             82024
         BL    EXIT          NO ENTRIES                          82024
         SPACE 1                                                 82024
SORTBGIN LA    R3,0(R2,R6)                                       82024
SORTBCLC EX    0,SORTBBC     COMPARE TO NEXT ENTRY               82024
         EX    0,SORTBB      BRANCH NOT HIGH                     82024
SORTBXC  EX    0,SORTBXC1    FLIP                                82024
         EX    0,SORTBXC2      TWO                               82024
         EX    0,SORTBXC1        ENTRIES                         82024
SORTBEND BXLE  R3,R6,SORTBCLC                                    82024
         BXLE  R2,R4,SORTBGIN                                    82024
         B     EXIT                                              82024
         SPACE 1                                                 82024
SORTBPAT CLC   0(0,R2),0(R3)           CLC COFF(CLEN,R2),COFF(R3)
         BNH   SORTBEND                BNH                       82024
         XC    0(0,R2),0(R3)           XC  0(LEN,R2),0(R3)       82024
         XC    0(0,R3),0(R2)                                     82024
         SPACE 2                                                 82038
*        BINARY TABLE LOOKUP                                     82038
*          ENTRY PARM - SAME AS SORTPARM LIST +A(COMPKEY)        82038
*        RETURNS R15:16 INVALID PARMS                            82038
*                R15:4  NO MATCH FOUND                           82038
*                R15:0  R0:A(ENTRY)                              82038
*                                                                82038
ENTBINLK MVC   SORTBBC(BINLPPAL),BINLPPAT  MOVE PATTERNS         82038
         ICM   R7,15,SOPLOW  GET START TABLE ENTRY               82038
         BNP   BADENTRY      INVALID                             82038
         SR    R8,R8                                             82038
         ICM   R8,1,SOPLEN   ENTRY SIZE                          82038
         BZ    BADENTRY      INVALID                             82038
         ICM   R1,15,SOPHIGH  ADDRESS OR COUNT PASSED ?          82038
         BZ    BADENTRY                                          82038
         BP    BINLKCNT      COUNT                               82038
         N     R1,=X'7FFFFFFF'  MASK FLAG BIT                    82038
         B     BINLKCN2                                          82038
BINLKCNT MR    R0,R8         GET OFFSET TO LAST ENTRY            82038
         SR    R1,R8         RELATIVE TO FIRST                   82038
         AR    R1,R7         PLUS START ADDRESS                  82038
BINLKCN2 SR    R1,R7         TOTAL SIZE                          82038
         BM    BADENTRY      NO ENTRIES                          90340
         SR    R0,R0                                             82038
         DR    R0,R8                                             82038
         MR    R0,R8         ENSURE END IS MULTIPLE              82038
         LA    R6,0(R1,R7)   SAVE HIGH ADDRESS                   82038
         AR    R1,R8         MAKE TRUE SIZE                      82053
         SR    R7,R8         FINAGLE BASE START ADDRESS          82053
         ICM   R4,15,SOPKEY  GET KEY ADDRESS                     82038
         BZ    BADENTRY      INVALID                             82038
         LR    R5,R8         COPY ENTRY SIZE                     82038
BINLKDBL SLL   R5,1          DOUBLE                              82038
         CR    R5,R1         SPANS ARRAY ?                       82038
         BL    BINLKDBL      NO; REDOUBLE                        82038
         LR    R0,R5         SAVE INCREMENT                      82038
         MVC   SORTBBC+5(1),SOPCOO  SET COMPARE OFFSET           82038
         SR    R1,R1                                             82038
         ICM   R1,1,SOPCOL   GET COMPARE LENGTH                  82038
         BZ    BADENTRY                                          82038
         BCTR  R1,0                                              82038
         STC   R1,SORTBBC+1  SET INTO COMPARE                    82038
         CLI   SOPASC,C'D'   DESCENDING ARRAY ?                  82038
         BNE   BINLKLUP      NO; BEGIN PROCESSING                82038
         MVI   SORTBB+1,X'20'  MAKE BH                           82038
         SPACE 1                                                 82038
BINLKLUP LA    R1,0(R5,R7)   GET NEXT TEST ADDRESS               82038
         CR    R1,R6         IN ARRAY ?                          82038
         BH    BINLKBUM      NO; DESCEND                         82038
         EX    0,SORTBBC     COMPARE TO KEY                      82038
         EX    0,SORTBB      BRANCH TO USE LOWER TABLE ENTRY     82038
         BNE   BINLKBUN      NO MATCH; USE HIGHER ENTRY IN TABLE 82038
         ST    R1,RETR0      RETURN MATCHING ENTRY ADDRESS       82038
         B     EXIT            AND RETURN                        82038
BINLKBUN LNR   R0,R0         SET FOR HIGHER PORTION OF TABLE     82038
BINLKBUM SRA   R0,1          HALVE INTERVAL                      82038
         SR    R5,R0         SET NEXT OFFSET                     82038
         LPR   R0,R0         MAKE POSITIVE AGAIN                 82038
         CR    R0,R8         LESS THAN ONE ENTRY ?               82038
         BNL   BINLKLUP      NO; TRY AGAIN                       82038
         B     BADRET4       ELSE RETURN NOT FOUND               82038
         SPACE 1                                                 82038
BINLPPAT CLC   0(0,R4),0(R1)  CLC KEY(SOPCOL),SOPCOO(R1)         82038
         BL    BINLKBUM      BL/BH TO EARLIER TABLE ENTRY        82038
BINLPPAL EQU   *-BINLPPAT                                        82038
         SPACE 1                                                 82024
FSAWORK  DSECT ,             WORK/SAVE AREA                      82024
         ORG   DB                                                82024
SORTBBC  CLC   0(0,R2),0(R3)           CLC COFF(CLEN,R2),COFF(R3)
SORTBB   BNH   SORTBEND                BNH                       82024
SORTBXC1 XC    0(0,R2),0(R3)           XC  0(LEN,R2),0(R3)       82024
SORTBXC2 XC    0(0,R3),0(R2)                                     82024
SORTBPAL EQU   *-SORTBBC                                         82024
         SPACE 1                                                 82024
         ORG   DB            RE-USE                              82024
SRTMAXO  DS    A             OFFSET TO HIGH ELEMENT              82024
SORTHMBA MVC   SORTHOLD(0),0(R1)       MVC B,A(X)                82024
SORTHMAA MVC   0(0,R14),0(R1)          MVC A(Y),A(X)             82024
SORTHCAA CLC   0(0,R1),0(R1)           CLC A(J+1),A(J)           82024
SORTHCBA CLC   SORTHOLD(0),0(R1)       CLC B+OFF,OFF+A(J)        82024
SORTHBAA BNH   SORTH121                                          82024
SORTHBBA BNL   SORTH125                                          82024
SORTHPAL EQU   *-SORTHMBA    LENGTH TO MOVE                      82024
SORTHOLD DS    CL256         TEMPORARY SAVE AREA                 82024
         SPACE 1                                                 82024
SORTPARM DSECT ,                                                 82024
SOPLOW   DS    A             LOWEST ADDRESS TO SORT              82024
SOPHIGH  DS    A             HIGHEST ADDRESS (X'80') OR # ENTRIES
SOPASC   DS    C             C'A' OR C'D' FOR SORT SEQUENCE      82024
SOPCOO   DS    X             COMPARE OFFSET                      82024
SOPCOL   DS    X             COMPARE LENGTH                      82024
SOPLEN   DS    X             ENTRY LENGTH                        82024
SOPKEY   DS    A             (BINLK) ADDRESS OF COMPARE KEY      82038
@SERVICE RSECT ,                                                GP99026
         POP   USING                                            GP97241
         TITLE '@ S E R V I C E  ***  DASD DEVICE DEPENDENT CODE'
*        ENTRY CODE 8 - DEVICE DEPENDENT DASD FUNCTIONS          82109
*        DASD DEVICE DEPENDENT SERVICE ROUTINES. ALL ARE ENTERED 82108
*        WITH R1 POINTING TO A WORK AREA, EXCEPT DVTBL, WHERE R1 82108
*        CONTAINS THE VALUE OF THE FIRST WORD OF THE TABLE, AND  82108
*        DVCAP, WHICH HAS A WORK AREA MATCHING THAT OF TRKCALC.  82108
*                                                                82108
ENTDVCTS LTR   R7,R7         ANY PARM SUPPLIED ?                 82108
         BZ    BADENTRY      NO; ERROR                           82108
         PUSH  USING                                            GP97241
         BASR  R11,0         MAKE LOCAL BASE                     82108
         USING *,R11                                             82108
         IC    R0,CALLMODE   GET THE SUBFUNCTION                 82108
         BIX   ERR=BADENTRY,PFX=ENT,BASE=@SERVICE,                     *
               LOC=(DVTBL,DVCAP,DVEXT,DVSPC)                    GP97241
         SPACE 1                                                 82108
*        DVTBL RETURNS THE ADDRESS OF A DEVICE TABLE ENTRY FOR THE
*        DEVICE. CONTENTS IS SAME AS PRODUCED IN IECZDTAB.       82108
*        R1:  <256 DEVICE TYPE >256 UCB ADDRESS <0 DCB ADDR (OPEN)
*        RETURN R0:TABLE ENTRY R15:0 SUPPORTED; :4 NOT IN SYSTEM 82108
*              R15>4 ERROR                                       82108
*                                                                82108
ENTDVTBL LTR   R7,R7         DCB ADDRESS PASSED ?                82108
         BNM   DVTBLSUB      NO; USE LONG CODE                   82108
         MVC   RETR0+1(3),DCBDVTBA-IHADCB(R7)  COPY TABLE ADDRESS
         B     EXIT          RETURN OK (EVEN IF IT ISN'T)        82108
DVTBLSUB ST    R7,DB         MAKE FAKE PARM LIST                 82108
         LA    R7,DB                                             82108
         USING TRKCALC,R7    DECLARE THE COMMON WORK AREA        82108
         BAS   R9,DVTBLOC    LOCATE TABLE ENTRY                  82108
         ST    R15,RETR15    SET RETURN CODE                     82108
         CH    R15,=Y(4)     VALID ADDRESS ?                     82108
         BH    EXIT          NO; DON'T CLOBBER IT                82108
         ST    R3,RETR0      PASS ADDRESS                        82108
         B     EXIT          RETURN TO CALLER                    82108
         SPACE 1                                                 82108
DVTBLOC  SR    R3,R3         CLEAR FOR TYPE INSERTION            82108
         SR    R4,R4         CLEAR FOR TEST OFFSET               82115
         SR    R5,R5         CLEAR FOR SUBTYPE INSERTION         82108
         ICM   R3,15,STARDCTA  LOAD THE ADDRESS                 GP99026
         BZ    DVTBLOCM      ZERO; MAJOR ERROR                   82115
         TM    STARDCTA,X'80'   DEVICE TABLE ADDRESS SUPPLIED ?  82115
         BZ    DVTBLOCN      NO                                  82115
DVTBLOC0 SR    R15,R15       RETURN ZERO CODE                    82108
         BR    R9            RETURN OK                           82108
DVTBLOCM LA    R15,12        SET MAJOR ERROR                     82108
         BR    R9            RETURN TO CALLER                    82108
DVTBLOCN CH    R3,=Y(256)    TYPE OR UCB ADDRESS ?               82108
         BNL   DVTBLOCU      UCB                                 82108
         LR    R5,R3         COPY TYPE                           82108
         B     DVTBLOCV      VALIDITY CHECK                      82108
DVTBLOCU CLI   UCBTBYT3-UCBOB(R3),UCB3DACC  DASD UCB ?           82108
         BNE   DVTBLOCM      NO; MAJOR ERROR                     82108
         IC    R5,UCBTBYT4-UCBOB(,R3)  GET SUB-TYPE              82108
DVTBLOCV N     R5,=A(127)    REMOVE X'80' BIT (MSS ?)            82108
         BZ    DVTBLOCM      ZERO IS NOT SUPPORTED               82108
         CH    R5,=Y(DVCTYPMK)  IN RANGE OF VALID DEVICES ?      82108
         BH    DVTBLOCM      NO; FAIL IT                         82108
*        THIS CRUTTY CODE IS REQUIRED TO TEST IF THE SUB-TYPE IS 82108
*        PRESENT IN THE SYSTEM (ALTERNATIVE IS A LOOP THROUGH THE
*        UCBS AND A DEVICE COMPARE)                              82108
*        NOTE THAT THIS IS DONE EVEN WHEN THE USER PASSES A UCB  82108
*        ADDRESS - IT MAY NOT BE A REAL UCB, BUT A FAKE.         82108
*                                                                82108
         L     R3,CVTPTR     GET THE CVT                         82108
         L     R3,CVTZDTAB-CVTMAP(,R3)  GET THE DASD DEVICE TABLE
         USING DVCTI,R3      DECLARE THE DVCT INDEX MAPPING      82115
         LA    R14,DVCTIOFF  POINT TO FIRST OFFSET FIELD         82115
         LA    R15,1(,R5)    NUMBER OF DESIRED ENTRY + 1         82108
         B     DVTBLOTU      SKIP FIRST BYTE (GARBAGE ?)         82108
DVTBLOTL CLI   0(R14),0      FILLER BYTE ?                       82108
         BE    DVTBLOTU      YES; SKIP IT                        82108
         CLM   R5,1,DVCTIOFF-DVCTI(R14)  OFFSET LOWER THAN TYPE ?
         BNL   DVTBLOTN      YES; DEVICE NOT IN SYSTEM           82108
DVTBLOTU LA    R14,1(,R14)                                       82108
         BCT   R15,DVTBLOTL  TRY NEXT ENTRY                      82108
         IC    R4,DVCTIOFF(R5)  GET CORRESPONDING OFFSET FOR THIS
         LTR   R4,R4         FILLER ENTRY ?                      82108
         BNZ   DVTBLOTS      NO; USE IT                          82108
DVTBLOTN LA    R3,LOCZDTAB   POINT TO LOCAL TABLE                82108
         IC    R4,DVCTIOFF(R5)                                   82108
         LTR   R4,R4         EXISTING DEVICE ?                   82108
         BNP   DVTBLOCM      NO; ERROR                           82108
         LA    R15,4         RETURN UNSUPPORTED, BUT VALID DEVICE
DVTBLOTS AR    R3,R4         POINT TO DEVICE ENTRY               82108
         BR    R9            RETURN TO CALLER                    82108
         SPACE 2                                                 82108
*        THIS CODE WAS ADAPTED FROM A DISASSEMBLED VERSION OF    82108
*        IEC0SCR1 IN AN OLD MVS SYSTEM  (PRIOR TO 3380 SUPPORT). 82108
*        IT IS COMPATIBLE WITH TRKCALC, AND REQUIRES A WORK AREA 82108
*        AS EXPANDED AND MAPPED BY TRKCALC. TRKCALC PROPER IS NOT
*        USED BECAUSE IT DOES NOT EXIST IN OLDER SYSTEMS.        82108
*        TRKCALC FUNCTIONALITY IS AVAILABLE AS A LOCAL MACRO NAMED
*        SERVCALC.                                               82108
*                                                                82108
ENTDVCAP LA    R10,EXIT      SET RETURN ADDRESS                  82108
         SR    R4,R4                                             82108
         SR    R5,R5                                             82108
         SR    R2,R2         WORK REGISTER                       82108
         L     R3,STARUCBA   LOAD THE ADDRESS OR TYPE            82108
         TM    STARFLGS,STARDTU  CHECK TYPE OF DEVICE TYPE       82108
         BZ    TRCHVADR      USER PROVIDED DEVICE TABLE ENTRY    82108
         BM    TRCHVUCB      USER PROVIDED UCB ADDRESS           82108
         IC    R5,STARTYPE   USER PROVIDED DASD SUB-TYPE         82108
         B     TRCHVDVX      FIND DEVICE TABLE ENTRY FOR IT      82108
TRCHVUCB IC    R5,UCBTBYT4-UCBOB(,R3) GET DEVICE TYPE FROM UCB   82108
         CLI   UCBTBYT3-UCBOB(R3),UCB3DACC  DASD UCB ?           82108
         BE    TRCHVDVX      YES; PROCEED                        82108
TRCMAJER MVI   RC,12         SET MAJOR ERROR                     82108
         B     EXIT          RETURN WITH MAJOR ERROR             82108
TRCHVADR TM    STARFLGS,STARLOC  31-BIT MODE?                   GP99133
         BNZ   DVCAPSUB      YES; HOPE ADDRESS IS VALID         GP99133
         N     R3,=X'00FFFFFF'  CLEAN ADDRESS                   GP99133
         B     DVCAPSUB      JOIN COMMON                        GP99133
TRCHVDVX BAS   R9,DVTBLOCV   CALL DVT LOOKUP ROUTINE             82108
         CH    R15,=Y(4)     ERROR ?                             82108
         BH    TRCMAJER      YES; QUIT                           82108
         USING DVCT,R3       DECLARE RETURNED DEVICE ENTRY       82115
         SPACE 1                                                 82108
*        TRKCALC CODE PROPER; ALSO ENTRY FOR DVSPC FUNCTION      82108
*        CODE FOR 3390 AND 9345 ADDED FOR ESA                   GP99133
*                                                                82108
DVCAPSUB CLI   STARR,1       DID USER SUPPLY RECORD NUMBER?     GP99133
         BNL   *+8           YES                                GP99133
         MVI   STARR,1       ELSE SET TO MINIMUM SUPPORTED      GP99133
         ICM   R4,1,STARKL   GET AND TEST KEY LENGTH             82108
         BNZ   TRCNKOV       NON-ZERO; NO ADJUSTMENT             82108
         IC    R4,DVCOVNK    GET UNKEYED BLOCK ADJUSTMENT        82108
         LCR   R4,R4                                             82108
TRCNKOV  ICM   R5,3,STARDL   GET AND TEST DATA LENGTH            82108
         BNZ   TRCNDALN      NON-ZERO; OK                        82108
         LA    R5,1          END-FILE ACTS AS LENGTH 1           82108
         STH   R5,STARDL     UPDATE                             GP99133
TRCNDALN TM    DVCFLAGS,DVCMODU   MODULO DEVICE ?                84087
         BZ    TRCNFMOD      NO                                  82108
         TM    DVCMOD1+1,X'23'   9345?                          GP99133
         BO    TRC9345                                          GP99133
         TM    DVCMOD1+1,X'22'   3390?                          GP99133
         BO    TRC3390                                          GP99133
         SR    R0,R0                                             82108
         ICM   R0,1,DVCOVNK  GET MODULO VALUE                    84087
         BNZ   *+8           HAVE IT (OLD FORMAT)                84087
         LH    R0,DVCMOD1    GET NEW (SP 1.3) MODULUS            84087
         AH    R5,DVCTOL     ADD ADJUSTMENT                      82108
         SR    R4,R4                                             82108
         DR    R4,R0                                             82108
         MR    R4,R0         TRUNCATE TO MULTIPLE OF MODULUS     82108
         LR    R4,R5         SAVE DATA SIZE                      82108
         SR    R5,R5                                             82108
         ICM   R5,1,STARKL   GET KEY LENGTH                      82108
         BZ    TRCNFKEY      SKIP IF UNKEYED                     82108
         AR    R5,R4         ADD DATA TO KEY LENGTH              82108
         AH    R5,DVCTOL     ADD ADJUSTMENT FOR KEY FIELD        82108
         SR    R4,R4         CLEAR FOR DIVIDE                    82108
         DR    R4,R0                                             82108
         MR    R4,R0         TRUNCATE TO MULTIPLE                82108
         LR    R4,R5         COPY TOTAL SIZE                     82108
TRCNFKEY LH    R5,DVCOVHD    GET BASIC BLOCK OVERHEAD            82108
         B     TRC2BOHD      JOIN COMMON CODE                    82108
         SPACE 1                                                GP99133
TRC3390  LA    R6,DATA3390                                      GP99133
         B     TRCNUMOD                                         GP99133
TRC9345  LA    R6,DATA9345                                      GP99133
         USING DVMSECT,R6    DECLARE NEW MODULE AND OVERHEAD VALUES
TRCNUMOD ICM   R4,12,STARDL                                     GP99133
         ICM   R4,2,STARKL                                      GP99133
         ICM   R4,1,STARRKDD                                    GP99133
         LR    R0,R4                                            GP99133
         SR    R3,R3                                            GP99133
         SLL   R4,16                                            GP99133
         SRDL  R4,56                                            GP99133
         LTR   R5,R5                                            GP99133
         BZ    TRC90NOK                                         GP99133
         A     R5,DVMMOD1                                       GP99133
         D     R4,DVMOHD1                                       GP99133
         LTR   R4,R4                                            GP99133
         BZ    *+8                                              GP99133
         LA    R5,1(,R5)                                        GP99133
         M     R4,DVMMOD1                                       GP99133
         LR    R15,R5                                           GP99133
         LA    R15,6(,R15)                                      GP99133
         LR    R4,R0                                            GP99133
         SLL   R4,16                                            GP99133
         SRDL  R4,56                                            GP99133
         AR    R5,R15                                           GP99133
         A     R5,DVMOHD2                                       GP99133
         D     R4,DVMMOD2                                       GP99133
         LTR   R4,R4                                            GP99133
         BZ    *+8                                              GP99133
         LA    R5,1(,R5)                                        GP99133
         L     R4,DVMMOD2                                       GP99133
         MR    R4,R4                                            GP99133
         LR    R3,R5                                            GP99133
TRC90NOK LR    R4,R0                                            GP99133
         SRDL  R4,48                                            GP99133
         A     R5,DVMMOD1                                       GP99133
         D     R4,DVMOHD1                                       GP99133
         LTR   R4,R4                                            GP99133
         BZ    *+8                                              GP99133
         LA    R5,1(,R5)                                        GP99133
         M     R4,DVMMOD1                                       GP99133
         LR    R15,R5                                           GP99133
         LA    R15,6(,R15)                                      GP99133
         LR    R4,R0                                            GP99133
         SRDL  R4,48                                            GP99133
         AR    R5,R15                                           GP99133
         A     R5,DVMOHD3                                       GP99133
         D     R4,DVMMOD2                                       GP99133
         LTR   R4,R4                                            GP99133
         BZ    *+8                                              GP99133
         LA    R5,1(,R5)                                        GP99133
         M     R4,DVMMOD2                                       GP99133
         AR    R5,R3                                            GP99133
         LR    R4,R5                                            GP99133
         CLI   STARRKDD,X'01'                                   GP99133
         BNE   TRC90NT1                                         GP99133
         SR    R5,R5                                            GP99133
         L     R5,DVMTRKLN                                      GP99133
         STH   R5,STARBAL                                       GP99133
         TM    STARFLGS,STARREMV                                GP99133
         BNO   TRC90CAP                                         GP99133
         LR    R0,R5                                            GP99133
         B     TRCEXIT0                                         GP99133
         SPACE 1                                                GP99133
TRC90NT1 TM    STARFLGS,STARUBAL                                GP99133
         BO    TRC90CAP                                         GP99133
         LR    R0,R4                                            GP99133
         SR    R5,R5                                            GP99133
         IC    R5,STARRKDD                                      GP99133
         TM    STARFLGS,STARREMV                                GP99133
         BO    *+6                                              GP99133
         BCTR  R5,0                                             GP99133
         MR    R4,R4                                            GP99133
         LCR   R5,R5                                            GP99133
         L     R4,DVMTRKLN                                      GP99133
         AR    R5,R4                                            GP99133
         STH   R5,STARBAL                                       GP99133
         LR    R4,R0                                            GP99133
         SPACE 1                                                GP99133
TRC90CAP SR    R0,R0                                            GP99133
         ICM   R0,3,STARBAL                                     GP99133
         TM    STARFLGS,STARFUNC                                GP99133
         BNO   TRC90BAL                                         GP99133
         SR    R0,R4                                            GP99133
         BNM   TRC90RPT                                         GP99133
         SR    R0,R0                                            GP99133
         B     TRCWNMAX                                         GP99133
         SPACE 1                                                GP99133
TRC90RPT LR    R5,R0                                            GP99133
         LR    R0,R4                                            GP99133
         SR    R4,R4                                            GP99133
         DR    R4,R0                                            GP99133
         LA    R0,1(,R5)                                        GP99133
         B     TRCEXIT0                                         GP99133
         SPACE 1                                                GP99133
TRC90BAL TM    STARFLGS,STARREMV                                GP99133
         BNO   TRC90NRM                                         GP99133
         AR    R0,R4                                            GP99133
         B     TRCWTBAV                                         GP99133
         SPACE 1                                                GP99133
TRC90NRM CR    R0,R4                                            GP99133
         BL    TRC90NBL                                         GP99133
         SR    R0,R4                                            GP99133
         B     TRCWTBAV                                         GP99133
         SPACE 1                                                GP99133
TRC90NBL TM    STARFLGS,STARMAXS                                GP99133
         BO    TRC90MXB                                         GP99133
         SR    R0,R0                                            GP99133
         B     TRCWNMAX                                         GP99133
         SPACE 1                                                GP99133
TRC90MXB SR    R4,R4                                            GP99133
         ICM   R4,2,STARKL                                      GP99133
         BZ    TRC90UNK                                         GP99133
         SR    R3,R3                                            GP99133
         SLL   R4,16                                            GP99133
         SRDL  R4,56                                            GP99133
         A     R5,DVMMOD1                                       GP99133
         D     R4,DVMOHD1                                       GP99133
         LTR   R4,R4                                            GP99133
         BZ    *+8                                              GP99133
         LA    R5,1(,R5)                                        GP99133
         M     R4,DVMMOD1                                       GP99133
         LR    R15,R5                                           GP99133
         LA    R15,6(,R15)                                      GP99133
         ICM   R5,1,STARKL                                      GP99133
         AR    R5,R15                                           GP99133
         A     R5,DVMOHD2                                       GP99133
         D     R4,DVMMOD2                                       GP99133
         LTR   R4,R4                                            GP99133
         BZ    *+8                                              GP99133
         LA    R5,1(,R5)                                        GP99133
         L     R4,DVMMOD2                                       GP99133
         MR    R4,R4                                            GP99133
         LR    R3,R5                                            GP99133
         SR    R0,R3                                            GP99133
TRC90UNK S     R0,DVMOHD3                                       GP99133
         LR    R5,R0                                            GP99133
         D     R4,DVMMOD2                                       GP99133
         M     R4,DVMMOD2                                       GP99133
         LR    R0,R5                                            GP99133
         LR    R5,R0                                            GP99133
         D     R4,DVMMOD3                                       GP99133
         LTR   R4,R4                                            GP99133
         BZ    *+8                                              GP99133
         LA    R5,1(,R5)                                        GP99133
         M     R4,DVMMOD1                                       GP99133
         A     R5,DVMMOD1                                       GP99133
         SR    R0,R5                                            GP99133
         BM    TRCNTFIT                                         GP99133
         SR    R0,R0                                            GP99133
         B     TRCWNMAX                                         GP99133
         SPACE 1                                                 82108
TRCNFMOD AR    R4,R5         ADD DATA TO KEY LENGTH OR -OVERHEAD 82108
         SR    R5,R5                                             82108
         IC    R5,DVCOVLB    GET LAST BLOCK OVERHEAD             82108
         TM    DVCFLAGS,DVC2BOV  TWO-BYTE OVERHEAD ?             82108
         BZ    TRC2BOHD      NO                                  82108
         LH    R5,DVCOVHD    YES; LOAD TWO-BYTE OVERHEAD         82108
TRC2BOHD AR    R4,R5         ADJUST SIZE OF RECORD               82108
         CLI   STARR,X'01'   FIRST RECORD ?                      82108
         BH    TRCNFRST      NO                                  82108
         ICM   R5,3,DVCTRKLN   GET TRACK LENGTH                  82108
         STH   R5,STARBAL    INTO TRACK BALANCE FIELD            82108
         TM    STARFLGS,STARREMV   REMOVE RECORD FUNCTION ?      82108
         BNO   TRCHVBAL      NO                                  82108
         TM    STARFLGS,STARFUNC  CAPACITY REQUESTED ?           82108
         BZ    TRCHVBAL      YES; SET IT                         82108
         LR    R0,R5         COPY FOR COMMON EXIT                82108
         B     TRCEXIT0      AND EXIT                            82108
         SPACE 1                                                 82108
TRCNFRST TM    STARFLGS,STARUBAL  USER PROVIDED BALANCE ?        82108
         BZ    TRCNFRSB      NO; CALCULATE IT                    82108
         TM    STARFLGS,STARREMV  REMOVE LAST RECORD ?           82108
         BZ    TRCNFRSU      NO                                  82108
         AR    R0,R4         MAKE NEW BALANCE                    82108
TRCNFRSU CLM   R0,3,DVCTRKLN  VALID LENGTH ?                     82108
         BNH   TRCHVBAL      YES; USE IT                         82108
         SR    R0,R0                                             82108
         ICM   R0,3,DVCTRKLN RESTORE DEVICE TRACK LENGTH         82108
         B     TRCHVBAL      USE IT                              82108
TRCNFRSB LR    R0,R4         COPY RECORD LENGTH                  82108
         BAS   R9,CALCTOL    APPLY TOLERANCE AS NECESSARY        82108
         SR    R5,R5                                             82108
         IC    R5,STARR      GET RECORD NUMBER                   82108
         TM    STARFLGS,STARREMV   REMOVE LAST RECORD ?          82108
         BO    TRCPRCNO      YES                                 82108
         BCTR  R5,0          NO; USE NUMBER OF PRECEDING RECORDS 82108
TRCPRCNO MR    R4,R4         AMOUNT USED BY ALL RECORDS          82108
         LCR   R5,R5                                             82108
         SR    R8,R8                                             82108
         ICM   R8,3,DVCTRKLN GET TRACK SIZE                      82108
         AR    R5,R8         AMOUNT LEFT                         82108
         STH   R5,STARBAL    NEW BALANCE                         82108
         LTR   R5,R5         CHECK FOR NEGATIVE VALUE            82108
         BNM   *+8           NO; OK                              82108
         STCM  R2,12,STARBAL SET BALANCE TO ZERO                 82108
         LR    R4,R0                                             82108
TRCHVBAL SR    R0,R0                                             82108
         ICM   R0,3,STARBAL  GET TRACK BALANCE BACK              82108
         TM    STARFLGS,STARFUNC  DOES USER WANT BALANCE OR CAP ?
         BNO   TRCWTBAL      BALANCE                             82108
         SR    R0,R4         CAPACITY FOR AT LEAST ONE ?         82108
         BM    TRCWNMAX      NO; TOO BAD                         82108
         BAS   R9,CALCTOL    APPLY TOLERANCE                     82108
         LR    R5,R0                                             82108
         LTR   R0,R4         BETTER SAFE THAN SORRY             GP97241
         BNP   TRCWNMAX      DON'T LIKE 0C9                     GP97241
         SR    R4,R4                                             82108
         DR    R4,R0                                             82108
         LA    R0,1(,R5)     RETURN NUMBER OF RECORDS            82108
         B     TRCEXIT0                                          82108
         SPACE 1                                                 82108
TRCWTBAL TM    STARFLGS,STARREMV  REMOVE LAST RECORD ?           82108
         BNO   TRCWNREM      NO                                  82108
         BAS   R9,CALCTOLL   APPLY TOLERANCE UNLESS LAST RECORD  82108
         AR    R0,R4                                             82108
         CLM   R0,3,DVCTRKLN VALID LENGTH ?                      82108
         BNH   TRCWTBAV      YES                                 82108
         SR    R0,R0                                             82108
         ICM   R0,3,DVCTRKLN RESTORE TRACK LENGTH                82108
TRCWTBAV STH   R0,STARBAL                                        82108
         B     TRCEXIT0                                          82108
         SPACE 1                                                 82108
TRCWNREM BAS   R9,CALCTOLL   APPLY TOLERANCE UNLESS LAST RECORD  82108
         CR    R0,R4         WILL RECORD FIT ON TRACK ?          82108
         BL    TRCWTFIT      NO; SEE IF USER WANTS MAX           82108
         SR    R0,R4         SET RESIDUAL LENGTH                 82108
         STH   R0,STARBAL                                        82108
         B     TRCEXIT0                                          82108
         SPACE 1                                                 82108
TRCWTFIT TM    STARFLGS,STARMAXS  NO FIT - DOES USER WANT SIZE ? 82108
         BNO   TRCWNMAX      NO; RETURN CODE 4 - NO FIT          82108
         SR    R5,R5                                             82108
         TM    DVCFLAGS,DVCMODU   MODULO DEVICE ?                84087
         BZ    TRCWNMOD      NO                                  82108
         SH    R0,DVCOVHD    REMOVE BLOCK OVERHEAD               82108
         BNP   TRCWNMAX      NO FIT                              82108
         CLI   STARKL,0      UNKEYED ?                           82108
         BE    TRCWFMOF      YES; JUST DO DATA LENGTH            82108
         ICM   R2,1,DVCOVNK  GET MODULUS                         84087
         BNZ   *+8           HAVE IT (OLD FORMAT)                84087
         LH    R2,DVCMOD1    GET NEW (SP 1.3) MODULUS            84087
         IC    R5,STARKL     GET KEY LENGTH                      82108
         AH    R5,DVCTOL     ADD ADJUSTMENT                      82108
         DR    R4,R2                                             82108
         MR    R4,R2         TRUNCATE TO MULTIPLE OF MODULUS     82108
         SR    R0,R5         GET RESIDUAL LENGTH FOR DATA        82108
         BNP   TRCWNMAX      NONE WILL FIT                       82108
TRCWFMOF LR    R5,R0         COPY SIZE AVAILABLE FOR DATA        82108
         SR    R4,R4                                             82108
         DR    R4,R2                                             82108
         MR    R4,R2         TRUNCATE TO MODULUS                 82108
         LR    R0,R5         COPY SIZE                           82108
         LH    R4,DVCTOL     GET ADJUSTMENT VALUE                82108
         B     TRCWYKEY      JOIN COMMON CODE                    82108
         SPACE 1                                                 82108
TRCWNMOD IC    R5,DVCOVLB    GET OVERHEAD                        82108
         TM    DVCFLAGS,DVC2BOV                                  82108
         BNO   TRCWNLST                                          82108
         LH    R5,DVCOVHD                                        82108
TRCWNLST SR    R4,R4                                             82108
         CLI   STARKL,X'00'                                      82108
         BNE   TRCWNKEY                                          82108
         IC    R4,DVCOVNK                                        82108
         SR    R5,R4         KEYED-UNKEYED OVERHEAD              82108
TRCWNKEY SR    R0,R5         RESIDUAL AFTER OVERHEAD             82108
         IC    R4,STARKL                                         82108
TRCWYKEY SR    R0,R4         LESS KEY LENGTH                     82108
         BNP   TRCWNMAX      NO FIT AT ALL                       82108
         ST    R0,RETR0      RETURN LARGEST THAT WILL FIT        82108
         B     TRCNTFIR                                         GP99133
TRCNTFIT XC    RETR0,RETR0   NO VALID RETURN DATA               GP99133
TRCNTFIR LA    R15,8         SET RETURN CODE                     82108
         B     TRCEXIT       AND RETURN WITH 8 (NO FIT; MAX IN R0)
TRCWNMAX LA    R15,4         RETURN CODE 4 - NO FIT              82108
         ST    R0,RETR0      RETURN RESULT (0)                  GP99133
TRCEXIT  ST    R15,RETR15    SET RETURN CODE                     82108
         BR    R10           RETURN TO EXIT OR CALLER            82108
TRCEXIT0 ST    R0,RETR0      RETURN RESULT                       82108
         SR    R15,R15       CLEAR RETURN CODE                   82108
         B     TRCEXIT       AND RETURN                          82108
         SPACE 2                                                 82108
*        CALCULATION OF TOLERANCE (2314)                         82108
*                                                                82108
CALCTOLL TM    STARFLGS,STARLAST  SPECIAL ISAM LAST RECORD REQUEST ?
         BNZR  R9            YES; NO ADJUSTMENT                  82108
CALCTOL  TM    DVCFLAGS,DVCFTOL  DOES TOLERANCE APPLY ?          82108
         BZR   R9            NO; NO ADJUSTMENT                   82108
         CLI   STARKL,X'00'  KEYED DATA ?                        82108
         BE    CALCTOLN      NO; LENGTH IS OK                    82108
         IC    R2,DVCOVNK    GET DECREMENT FOR UNKEYED BLOCK     82108
         SR    R4,R2         REMOVE KEY OVERHEAD                 82108
CALCTOLN MH    R4,DVCTOL     BE TOLERANT                         82108
         SRL   R4,DVCTSHFT   GET EFFECTIVE DATA/KEY LENGTH       82108
         IC    R2,DVCOVNLB   GET BLOCK OVERHEAD (NOT LAST)       82108
         AR    R4,R2         SET LENGTH OF NOT LAST BLOCK        82108
         CLI   STARKL,X'00'  KEYED RECORD ?                      82108
         BNER  R9            YES; OVERHEAD OK                    82108
         IC    R2,DVCOVNK    GET UNKEYED ADJUSTMENT              82108
         SR    R4,R2         REMOVE KEYED OVERHEAD               82108
         BR    R9                                                82108
         SPACE 2                                                 82108
*        THE FOLLOWING LOCAL DEVICE TYPE TABLE IS PROVIDED FOR   82108
*        SUPPORT (E.G. MOVE/COPY) FOR DEVICES NOT GENERATED IN   82108
*        THE RUNNING SYSTEM.                                     82108
*                                                                82108
LOCZDTAB DC    AL1(0,0,0,0)       UNSUPPORTED TYPES 0-3          82108
         DC    AL1(DVZ9345-LOCZDTAB)   9345                04   GP99125
         DC    AL1(0)                                      05   GP99125
         DC    AL1(DVZ2305A-LOCZDTAB)  2305-1              06    82108
         DC    AL1(DVZ2305B-LOCZDTAB)  2305-2              07    82108
         DC    AL1(DVZ2314-LOCZDTAB)   2314                08    82108
         DC    AL1(DVZ3330-LOCZDTAB)   3330 SINGLE         09    82108
         DC    AL1(DVZ3340-LOCZDTAB)   3340                0A    82108
         DC    AL1(DVZ3350-LOCZDTAB)   3350                0B    82108
         DC    AL1(DVZ3375-LOCZDTAB)   3375                0C    82108
         DC    AL1(DVZ333D-LOCZDTAB)   3330 DOUBLE         0D    82108
         DC    AL1(DVZ3380-LOCZDTAB)   3380                0E    82108
         DC    AL1(DVZ3390-LOCZDTAB)   3390                0F   GP99125
         SPACE 1                                                 82108
*                 CYL TRK LEN OHDNKFG TOL ALT  R0TSDS B/S MOD    84087
*VZ2311  DC    X'00CB000A0E29511414010219001E'                   84087
*VZ2301  DC    X'000100C85003BA35350002000000'                   84087
*VZ2303  DC    X'0050000A131C9226260002000000'                   84087
*VZ2302  DC    X'00FA002E137851141401021900C8'                   84087
DVZ9345  DC    X'05A0000FBC98000000100023000F04A0D50000EE0023'  GP99125
*VZ2321  DC    X'140A051407D06410100302190190'                   84087
DVZ2305A DC    X'0030000838E8027ACA080200000100EA5A5700A8'       84087
DVZ2305B DC    X'006000083A0A01215B08020000010076B4B10054'       84087
DVZ2314  DC    X'00CB00141C7E922D2D010216003C00000000'  0-RPS    82108
DVZ3330  DC    X'019B0013336DBFBF38000200008500ED807C0069'       84087
*VZ3340  DC    X'015D000C2157F2F24B800200000C0125403D008C'       84087
DVZ3340  DC    X'02BA000C2157F2F24B00020000180125403D008C'       84087
DVZ3350  DC    X'0230001E4B36010B5208020000960185807B009C'       84087
DVZ3375  DC    X'03C0000C8CA000E0001000BF000C0340C4BB00C00020'   84087
DVZ333D  DC    X'032F0013336DBFBF38000200008500ED807C0069'       84087
DVZ3380  DC    X'0376000FBB6001000090010B000F04E0DDD600E00020'   84087
DVZ3390  DC    X'0459000FE5A2000000900022000F0594E00001100022'  GP99125
*                 0 1 2 3 4 5 6 7 8 9 A B C D E F 0 1 2 3 4 5    84087
         SPACE 2                                                GP99133
DVMSECT  DSECT ,         MAP NEW MODULO DEVICE CONSTANTS        GP99133
DVMMOD1  DS    F             MOD 1                              GP99133
DVMMOD2  DS    F             MOD 2                              GP99133
DVMMOD3  DS    F             MOD 3                              GP99133
DVMOHD1  DS    F             OHD 1                              GP99133
DVMOHD2  DS    F             OHD 2                              GP99133
DVMOHD3  DS    F             OHD 3                              GP99133
DVMTRKLN DS    F             RAW TRACK LENGTH                   GP99133
@SERVICE RSECT ,                                                GP99133
         SPACE 1                                                GP99133
DATA3390 DC    F'6,34,238,232,306,646'  MOD1-3, OHD1-3          GP99133
         DC    AL4(58786)    X'E5A2'    TRACK SIZE              GP99133
DATA9345 DC    F'6,34,238,232,238,612'  MOD1-3, OHD1-3          GP99133
         DC    AL4(48280)    X'BC98'                            GP99133
         SPACE 2                                                 82108
*                                                                82108
*        DVEXT - CALCULATE NUMBER OF TRACKS IN EXTENT LISTS      82108
*        R1 - 12 BYTE AREA; WD0 <0 DVTBL <256 TYPE >256 UCB ADDR 82108
*              WD2 <0 ADDRESS OF LIST OF XL10 ENTRIES; WD3 NUMBER
*              WD2 = 0 NONE; ELSE FMT1 ADDRESS                   82108
*              WD3 = 0 OR FMT3 STORAGE ADDRESS                   82108
*        RETURNS: R0 - NUMBER OF TRACKS (OR ZERO ON ERROR)       82108
*              R15 - 0 OK; 4 - TERMINATED BY NULL/MISSING EXTENT 82108
*              R15>4 - INVALID REQUEST OR EXTENT; R1 BAD EXTENT ADDR
*                                                                82108
ENTDVEXT BAS   R9,DVTBLOC    GET DEVICE TABLE ENTRY              82108
         CH    R15,=Y(4)     VALID ?                             82108
         BNH   *+12          YES                                 82108
         ST    R15,RETR15    SET RETURN CODE                     82108
         B     EXIT          AND QUIT                            82108
         LM    R5,R6,STARDCTA+4  LOAD ADDRESSES                  82108
         LTR   R5,R5         CONDENSED LIST PASSED ?             82108
         BM    DVEXTLST      YES; CHECK COUNT                    82108
         BZ    DVEXTF3       FORMAT 3 ONLY ?                     82108
         USING IECSDSL1,R5   DECLARE FORMAT 1                    82108
         SR    R4,R4         CLEAR EXTENT COUNTER                82108
         IC    R4,DS1NOEPV   LOAD NUMBER OF EXTENTS              85196
         CLI   DS1EXT1,X'40' LABEL EXTENT PRESENT ?              85196
         BNE   *+8           NO                                  85196
         LA    R4,1(,R4)     UP EXTENT COUNT                     85196
         LTR   R4,R4         SET CC                              85196
         BZ    EXIT          ZERO; ALL DONE                      82108
         LA    R2,DS1EXT1    POINT TO FIRST EXTENT               82108
         DROP  R5            DONE WITH DS1 MAPPING               82108
         LA    R8,3          SET FOR MAXIMUM IN THIS DSCB        82108
         CR    R8,R4         FULL ?                              82108
         BNH   *+6           YES                                 82108
         LR    R8,R4         USE ONLY WHAT'S AVAILABLE           82108
         SR    R4,R8         SET COUNT AFTER                     82108
         BAS   R9,DVEXTSUB   TOTAL EXTENTS                       82108
         LTR   R4,R4         ANY MORE ?                          82108
         BNP   EXIT          NO; RETURN                          82108
         LTR   R6,R6         DSCB 3 PASSED ?                     82108
         BZ    BADRET4       NO; SET WARNING THAT FMT 3 NEEDED   82108
         B     DVEXTF3C      GO TO COMMON FOR F3                 82108
DVEXTF3  LTR   R6,R6         FORMAT 3 ADDRESS PASSED ?           82108
         BZ    BADRET8       NO; BOOBOO                          82108
         LA    R4,13         SET FOR 13 EXTENTS                  82108
DVEXTF3C LA    R8,(DS3FMTID-DS3EXTNT)/(DS1EXT2-DS1EXT1) COUNT    82108
         CR    R8,R4         FULL ?                              82108
         BNH   *+6           YES                                 82108
         LR    R8,R4         USE ONLY WHAT'S AVAILABLE           82108
         SR    R4,R8         SET REMAINDER                       82108
         USING IECSDSL3,R6   DECLARE FMT3 MAPPING                82108
         LA    R2,DS3EXTNT   POINT TO FIRST EXTENT               82108
         BAS   R9,DVEXTSUB   GET TRACKS IN FIRST FOUR            82108
         LTR   R8,R4         ANY MORE ?                          82108
         BNP   EXIT          NO; RETURN                          82108
         LA    R2,DS3ADEXT   POINT TO REMAINDER                  82108
         DROP  R6            DONE WITH FMT3 MAPPING              82108
         BAS   R9,DVEXTSUB   AND DO THEM                         82108
         B     EXIT                                              82108
DVEXTLST LTR   R8,R6         COPY COUNT                          82108
         BZ    BADRET4       WARNING                             82108
         BM    BADENTRY      TOO BAD                             82108
         CH    R8,=Y(16)     MORE THAN MAX SUPPORTED ?           82108
         BH    BADENTRY      ERROR; QUIT                         82108
         LTR   R2,R5         COPY LIST ADDRESS                   82108
         LA    R9,EXIT       FALL THROUGH TO GOOD EXIT           82108
         SPACE 1                                                 82108
         USING MAPEXTNT,R2   DECLARE EXTENT MAPPING              82108
DVEXTSUB CLC   XTWHOLE,ZEROES   ALL ZERO ?                       93205
         BE    BADRET4       YES; SET WARNING - PREMATURE END    82108
         CLC   XTLOCYL(4),XTHICYL VALID RANGE ?                  82108
         BH    DVEXTR8       NO; FAIL                            82108
*SEE     TM    DVCFLAGS,DVZNOCYL  CYLNDER NUMBER VALID ?         82108
*NEXT:   BNZ   DVEXTSNO      NO                                  82108
*FAILS   CLC   XTHICYL,DVCCYL  VALID CYL ?                       82108
*ON3380D BNL   DVEXTR8       NO                                  82108
DVEXTSNO CLC   XTLOTRK,DVCTRK   VALID TRACK NUMBER ?             82108
         BNL   DVEXTR8       NO                                  82108
         CLC   XTHITRK,DVCTRK                                    82108
         BNL   DVEXTR8                                           82108
         LH    R10,XTHICYL   GET END CYLINDER                    82108
         SH    R10,XTLOCYL   LESS START CYLINDER                 82108
         BM    DVEXTR8       FAIL BAD RANGE                      93218
         LH    R15,XTHITRK   GET HIGH TRACK                      82108
         SH    R15,XTLOTRK   LESS START TRACK                    82108
         AH    R15,=Y(1)     PLUS ONE FOR RELATIVITY             82108
         CLI   XTTYPE,XTTSPLIT  SPLIT EXTENT ?                   82108
         BNE   DVEXTSEQ      NO                                  82108
         CLC   XTLOTRK,XTHITRK  VALID RANGE ?                    82108
         BH    DVEXTR8       NO                                  82108
         AH    R10,=Y(1)     GET CYLINDER COUNT                  82108
         MR    R14,R10       GET TOTAL TRACKS                    82108
         A     R15,RETR0     ADD TO RUNNING TOTAL                82108
         ST    R15,RETR0     RESET TOTAL                         82108
         B     DVEXTSUP      GET NEXT EXTENT                     82108
DVEXTSEQ MH    R10,DVCTRK    CONVERT TO TRACKS                   82108
         AR    R10,R15       ADD ODD TRACKS                      82108
         A     R10,RETR0     ACCUMULATE                          82108
         ST    R10,RETR0     STASH NEW TOTAL                     82108
DVEXTSUP LA    R2,XTLEN(,R2)   GET NEXT EXTENT                   82108
         BCT   R8,DVEXTSUB   DO NEXT EXTENT                      82108
         BR    R9            RETURN TO CALLER                    82108
DVEXTR8  ST    R2,RETR1      RETURN THE ADDRESS OF THE BAD EXTENT
         B     BADRET8       RETURN ERROR                        82108
         DROP  R2                                                82108
         SPACE 2                                                 82108
*        SPACE CALCULATIONS                                      82108
*        R1 POINTS TO A 2 OR 3 WORD LIST; WORD 1 AS FOR DVEXT    82108
*              WD2 XL1 - JFCBCTRI SPACE TYPE  XL3 - PRIMARY AMOUNT
*                (FOR CHEATERS - IF =X'FF', CHANGES TRKS=>CEIL CYLS)
*              WD3 IF ALLOC FOR AVG. BLOCKS, THE BLOCKSIZE; ELSE 0
*        R15:0 OK, R0 HAS REQUIRED TRACKS; R15> INVALID REQUEST  82108
*                                                                82108
ENTDVSPC BAS   R9,DVTBLOC    GET THE DEVICE TABLE ENTRY INTO R3  82108
         CH    R15,=Y(4)     VALID ?                             82108
         BNH   *+12          YES                                 82108
         ST    R15,RETR15    SET RETURN CODE                     82108
         B     EXIT          AND QUIT                            82108
         SR    R0,R0                                             82108
         ICM   R0,7,STARDCTA+5  GET AMOUNT REQUESTED             82108
         CLI   STARDCTA+4,X'FF'  CONVERT TRACKS TO CYLINDERS ?   82108
         BE    DVSPCTTC      YES; SPECIAL                        82108
         TM    STARDCTA+4,JFCBCYL  CYLINDER REQUEST ?            82108
         BNO   DVSPCNCY      NO                                  82108
         MH    R0,DVCTRK     CONVERT TO TRACKS                   82108
DVSPCRT0 ST    R0,RETR0                                          82108
         B     EXIT          RETURN                              82108
DVSPCNCY TM    STARDCTA+4,JFCBAVR  BLOCK LENGTH REQUEST ?        82108
         BNZ   DVSPCBLK      YES; THIS GETS MESSY                82108
*        COMMON ENTRY FOR ROUND CHECK                            82108
*                                                                82108
DVSPCRND TM    STARDCTA+4,JFCROUND  ROUND TO CYLINDER SIZE ?     82108
         BZ    DVSPCRT0      NO; RETURN                          82108
DVSPCTTC SRDA  R0,32         SWAP REGISTERS                      82108
         LH    R2,DVCTRK     GET TRACKS PER CYLINDER             82108
         AR    R1,R2         FINAGLE                             82108
         BCTR  R1,0          SET FOR TRUNCATION                  82108
         DR    R0,R2         GET CYLINDERS INTO R1               82108
         CLI   STARDCTA+4,X'FF'  RETURN CYLINDERS ?              82108
         BE    *+6           YES; SKIP MULTIPLY                  82108
         MR    R0,R2         CONVERT BACK TO TRACKS              82108
         ST    R1,RETR0      RETURN TO USER                      82108
         B     EXIT          AND RETURN                          82108
         SPACE 1                                                 82108
*        CALCULATE SPACE FOR AVERAGE BLOCKS                      82108
*                                                                82108
DVSPCBLK XC    DB,DB         PREPARE TO BUILD FINAGLED STAR MAP  82108
         ICM   R15,15,STARDCTA+8  LOAD AVERAGE BLOCKSIZE         82108
         BNP   BADRET8       ERROR                               82108
         CH    R15,=Y(32760)  GREATER THAN OS MAXIMUM ?          82108
         BH    BADRET8       YES; FAIL IT                        82108
         SR    R2,R2         MAKE SURE DVCAPSUB WORK REGISTERS   82108
         SR    R4,R4           ARE CLEARED                       82108
         SR    R5,R5                                             82108
         STM   R1,R7,24(R13) SAVE VITAL (AND SOME OTHER) REGISTERS
         LA    R7,DB-4       DON'T NEED FIRST WORD               82108
         STH   R15,STARDL    SET DATA LENGTH                     82108
         MVI   STARFLGS,STARFUNC  SET FOR CAPACITY CALCULATION   82108
         MVI   STARR,1       SET RECORD NUMBER JUST IN CASE      82108
         BAS   R10,DVCAPSUB  GET NUMBER OF RECORDS PER TRACK     82108
         LM    R1,R7,24(R13) RESTORE REGISTERS                   82108
         LM    R14,R15,RETR15   GET RETURNED R15, R0             82108
         XC    RETR15(8),RETR15  CLEAR EVERYTHING                82108
         LTR   R14,R14       0 COMPLETION CODE ?                 82108
         BNZ   BADRET8       NO; RETURN LEVEL 8 ERROR            82108
         SR    R1,R1                                             82108
         ICM   R1,7,STARDCTA+5  GET NUMBER OF RECORDS            82108
         LTR   R15,R15       WILL ANY FIT ?                      82108
         BNP   BADRET8       BUT SHOULD NOT HAVE GOTTEN HERE     82108
         SR    R0,R0         CLEAR FOR DIVIDE                    82108
         AR    R1,R15        ADD ONE TRACKS WORTH                82108
         BCTR  R1,0          LESS ONE FOR TRUNCATION             82108
         DR    R0,R15        GET NUMBER OF TRACKS REQUIRED       82108
         LR    R0,R1         COPY FOR CYLINDER ROUNDING          82108
         B     DVSPCRND      TEST FOR ROUND                      82108
         POP   USING                                            GP97241
         SPACE 2                                                 82108
         LTORG ,                                                 82108
         SPACE 1                                                 82108
         PRINT &PRTMAC                                           82108
TRKCALC  DSECT ,             MAPPING OF DV CALLING PARMS         82108
         SERVCALC MF=D       SAME AS TRKCALC EXPANSION           82108
         SPACE 1                                                 82108
         ESADVCT ,    MAPPING OF LATEST DEVICE TABLE ENTRY      GP06284
DVZNOCYL EQU   X'80'         DVCFLAGS - CYL. NO. INVALID (3340)  82108
         AIF   (&MVS).HAVMODU                                   GP04234
DVCMODU  EQU   X'10'         DVCFLAGS - MODULO DEVICE           GP04234
DVCMOD1  EQU   DVCBPSEC+2,2,C'A'  MODULUS                       GP04234
DVCOVH1  EQU   DVCBPSEC+4,2,C'A'  OVERHEAD VALUE 1              GP04234
DVCOVH2  EQU   DVCBPSEC+6,2,C'A'  OVERHEAD VALUE 2              GP04234
.HAVMODU SPACE 1                                                 82108
MAPEXTNT MAPEXTNT ,          INCLUDE EXTENT MAPPING              82108
@SERVICE RSECT ,                                                GP99026
         PRINT &PRTSOR                                           82108
         SPACE 1                                                 85070
ENTCRMSV B     BADENTRY      NO CROSS-MEMORY SERVICES            85070
         AIF   (NOT &MVS).CRMSDON                                85070
         ORG   ENTCRMSV      MVS CROSS-MEMORY SERVICES           85070
         TITLE '@ S E R V I C E  ***  CROSS-MEMORY AND ASCB SERVICES'
         PUSH  USING         SAVE REGISTER ASSIGNMENTS           85070
         BASR  R11,0         MAKE LOCAL BASE                     85070
         USING *,R11                                             85070
BASCRMSV LTR   R7,R1         ANY PARM                            85070
         BNZ   CRMSVENT      YES; PROCEED                        85070
         CLI   CALLMODE,VENSCHFR/256  FREE CSA ?                 85070
         BE    CRMSVENT      YES; NO PARM NEEDED                 85070
         CLI   CALLMODE,VENGASID/256  FUNCTION WITH DEFAULT ?    85070
         BL    BADENTRY      NO; FAIL                            85070
         CLI   CALLMODE,VENSSLOC/256  IN RANGE ?                 85070
         BH    BADENTRY      NO; FAIL                            85070
CRMSVENT CLI   CALLMODE,VENSWAPN/256   PRIVILEGES REQUIRED ?     85070
         BH    CRMSVAUT      NO; PROCEED                         85070
         AUCH  BADRET8       AUTHORIZATION REQUIRED              90337
CRMSVAUT CLI   SUBPOOL,FGSVC SVC ENTRY ?                         85070
         BNE   CRMSVAUC      NO                                  85070
         CLI   CALLMODE,VENPGFIX/256  CRMS ENTRY ?               85070
         BL    BADENTRY      YES; FAIL                           85070
CRMSVAUC IC    R0,CALLMODE   GET SUB-FUNCTION                    85070
         LA    R14,EXIT      SET RETURN FROM SCH-- ROUTINES      85076
         BIX   ERR=BADENTRY,PFX=ENT,BASE=@SERVICE,                     *
               LOC=(SCHFR,SCHIN,SCHMV,SCHED,PGFIX,SWAPY,SWAPN,GASID,GAS*
               JB,GASCB,SSLOC,SSSET)                            GP97241
         SPACE 1                                                 85070
         AIF   (&MVSXA OR &MVSESA).XAPFIX                       GP97241
ENTPGFIX N     R7,=X'00FFF000'  CLEAN FOR MVS MAXIMUM            85070
         ST    R7,DB+4                                           85070
*        LA    R14,PASTFIXS  TABLE OF PAGES PREVIOUSLY FIXED     85070
*        LR    R15,R14       SAVE START OF TABLE                 85070
*        LA    R0,3          SET INCREMENT                       85070
*        LA    R1,PASTFIXX-3-PASTFIXS(R14)  LAST ENTRY           85070
*AGEFIXO CLM   R7,B'1110',0(R14)  DID WE FIX THIS PAGE BEFORE ?  85070
*        BNE   PAGEFIXG      TRY AGAIN                           85070
*        B     EXIT          RETURN TO USER                      85070
*AGEFIXG BXLE  R14,R0,PAGEFIXO  TRY AGAIN                        85070
*        MVC   0(PASTFIXX-PASTFIXS-3,R15),3(R15)  SLIDE DOWN     85070
*        STCM  R7,B'1110',PASTFIXX-3-PASTFIXS(R15)  SAVE CURRENT ONE
         LA    R8,4          SET PROVISIONAL ERROR FLAG          85070
         LA    R4,2          DO PAGE-FIX TWICE                   85070
PAGEFIXL LA    R2,DB                                             85070
         MVI   DB,0          JUST IN CASE                        85070
         PGFIX R,A=(R7),EA=(R7),ECB=(R2),LONG=Y,RELATED=(AUNT)   92297
         LTR   R15,R15       TEST RETURN                         85070
         BZ    PAGEFIXP      CONTINUE                            85070
         CH    R15,=H'8'                                         85070
         BNE   BADRET4       TOO BAD                             85070
         WAIT  ECB=DB        WAIT FOR COMPLETION                 85070
         ICM   R15,7,DB+1    TEST COMPLETION                     85070
         BNZ   BADRET4       QUIT WITH ERROR                     85070
PAGEFIXP BCT   R4,PAGEFIXL   FIX IT TWICE                        85070
         L     R4,PSATOLD-PSA   GET MY TCB                       85070
         USING TCB,R4        DECLARE IT                          85070
         MODESET MODE=SUP,KEY=ZERO    GET VERY PRIVILEGED        85070
 SETLOCK OBTAIN,TYPE=SALLOC,REGS=USE,MODE=UNCOND,RELATED=(UNCLE) 85070
         ICM   R5,7,TCBFOEA  GET THE FOE CHAIN                   85070
         USING FOE,R5        DECLARE IT                          85070
         BZ    PAGEFIXB      NONE ?                              85070
         ICM   R6,3,DB+5     GET ADDRESS FIXED                   85070
         N     R6,=XL4'FFF0'  CONVERT TO FOE FORMAT              85070
PAGEFIXF CLM   R6,3,FOEVINDX  SAME VIRTUAL PAGE ADDRESS ?        85070
         BE    PAGEFIXD      YES                                 85070
         ICM   R5,7,FOEFLINK   GET NEXT                          85070
         BNZ   PAGEFIXF                                          85070
         B     PAGEFIXB      TERMINATE WITH ERROR                85070
PAGEFIXD LH    R2,FOEFXCT    GET FIX COUNT                       85070
         SH    R2,=H'1'      SET TO LEAVE FIXED                  85070
         BNP   PAGEFIXB      ERROR IF NOT AT LEAST 1             85070
         STH   R2,FOEFXCT    STASH IT BACK                       85070
         SR    R8,R8         RESET ERROR FLAG                    85070
PAGEFIXB DS    0H            ERROR AND GOOD TERMINATION          85070
         SETLOCK RELEASE,TYPE=SALLOC,REGS=USE,RELATED=(UNCLE)    85070
         MODESET MODE=PROB,KEY=NZERO  RESTORE KEY AND MODE       85070
         LTR   R8,R8         ANY ERROR ?                         85070
         BNZ   BADRET4       YES; RETURN ERROR CODE              85070
         LA    R2,DB                                             85070
         PGFREE R,A=(R7),ECB=(R2),RELATED=(AUNT)  ONCE (LEAVE IT FIXED)
         B     EXIT          GO TO COMMON EXIT                   85070
         DROP  R4,R5                                             85070
         AGO   .COMPFIX                                          90201
.XAPFIX  ANOP  ,                                                 90201
ENTPGFIX MVI   DB,0          JUST IN CASE - RESET 'ECB'          90201
         LA    R2,DB         LOAD ECB ADDRESS                    90201
         MODESET MODE=SUP,KEY=ZERO                               90201
         PGSER R,FIX,A=(R7),ECB=(R2),LONG=Y,TCB=0,BRANCH=Y       90201
         LR    R8,R15        PRESERVE RETURN CODE                90201
         MODESET MODE=PROB,KEY=NZERO  RESTORE ENVIRONMENT        90201
         LTR   R15,R8        DONE ?                              90201
         BZ    EXIT          YES                                 90201
         WAIT  ECB=DB        ELSE WAIT FOR COMPLETION            90201
*   NOTE THAT FAILURE IS SIGNALED BY AN ABEND !                  90201
         B     EXIT          RETURN                              90201
.COMPFIX SPACE 1                                                 90201
*        VALIDATE ASID IN R1(R7); RETURN ASCB ADDRESS            85070
*                                                                85070
ENTGASCB LTR   R1,R7         ANY ID PASSED ?                     85070
         BNZ   GASCBNDF      YES                                 85070
         L     R1,PSAAOLD-PSA   GET MY ASCB                      85070
         LH    R1,ASCBASID-ASCB(,R1)  LOAD MY ASID               85070
GASCBNDF L     R3,CVTPTR                                         85070
         USING CVTMAP,R3                                         85070
         L     R15,CVTASVT   GET THE ASCB VECTOR TABLE           85070
         USING ASVT,R15                                          85070
         C     R1,ASVTMAXU   VALID ?                             85070
         BH    BADRET4       NO; BOOBOO                          85070
         SLL   R1,2          CONVERT TO INDEX                    85070
         LA    R5,ASVTFRST(R1)  POINT TO ASCB POINTER            85070
         ICM   R5,15,0(R5)   LOAD AND TEST                       85070
         BNP   BADRET4       INVALID - TOO BAD                   85070
         ST    R5,RETR0      SET RETURN TO USER = ASCB ADDRESS   85070
         B     EXIT          RETURN                              85070
         DROP  R15,R3                                            85070
         SPACE 1                                                 85070
*        VALIDATE ASID AND RETURN JOB AND OTHER INFORMATION      85070
*                                                                85070
ENTGASID LTR   R1,R7         ANY ID PASSED ?                     85070
         BNZ   GASIDNDF      YES                                 85070
         L     R1,PSAAOLD-PSA   GET MY ASCB                      85070
         LH    R1,ASCBASID-ASCB(,R1)  GET ASID                   85070
GASIDNDF L     R3,CVTPTR                                         85070
         USING CVTMAP,R3                                         85070
         L     R15,CVTASVT   GET THE ASCB VECTOR TABLE           85070
         USING ASVT,R15                                          85070
         C     R1,ASVTMAXU   VALID ?                             85070
         BH    BADRET4       NO; BOOBOO                          85070
         SLL   R1,2          CONVERT TO INDEX                    85070
         LA    R5,ASVTFRST(R1)  POINT TO ASCB POINTER            85070
         ICM   R5,15,0(R5)   LOAD AND TEST                       85070
         BNP   BADRET4       INVALID - TOO BAD                   85070
         ST    R5,RETR0      RETURN ASCB ADDRESS                 85070
         B     GTCMASCB      GO TO COMMON RETURN                 85070
         DROP  R3,R15                                            85070
         SPACE 1                                                 85070
*        VALIDATE JOBNAME, AND RETURN NAME AND ID...             85070
*                                                                85070
ENTGASJB LTR   R1,R7         ANY PARAMETER PASSED ?              85070
         BNZ   GASJBNDF      YES                                 85070
         L     R5,PSAAOLD-PSA  LOAD MY ASCB                      85070
         B     GTCMASCB      GO TO COMMON FORMATTING             85070
GASJBNDF L     R3,CVTPTR                                         85070
         USING CVTMAP,R3                                         85070
         L     R15,CVTASVT   GET THE ASCB VECTOR TABLE           85070
         USING ASVT,R15                                          85070
         L     R0,ASVTMAXU   GET HIGHEST VALID ASID              85070
         SLL   R0,2          *4 FOR FAST CHECK                   85070
         SR    R1,R1         SET INDEX FOR NEXT ASID             85070
GTIAASID LA    R1,4(,R1)     GET NEXT ASID INDEX                 85070
         CR    R1,R0         VALID ?                             85070
         BH    BADRET4       NO; QUIT                            85070
GTIAACOM LA    R5,ASVTFRST(R1)  POINT TO ASCB POINTER            85070
         ICM   R5,15,0(R5)   LOAD AND TEST                       85070
         BNP   GTIAASID      SKIP UNUSED/FREE SLOT               85070
         USING ASCB,R5                                           85070
         LA    R14,8         SET DIFFERENTIAL OFFSET - FAKE CSCB 85070
         ICM   R2,15,ASCBJBNI  JOB CSCB ?                        85070
         BNZ   GTIAAJOB      YES; SKIP                           85070
         SR    R14,14        RESET CSCB ORIGIN OFFSET            85070
         ICM   R2,15,ASCBCSCB  GET THE CSCB                      85070
         BZ    GTIAASID                                          85070
         USING CHAIN,R2      DECLARE THE CSCB                    85070
         CLI   CHVCD,X'98'   TS USER ?                           85070
         BNE   GTIANTSO      NO                                  85070
GTIAUTST CLC   CHKEY,0(R7)   MATCHING USER ID ?                  85070
         BE    GTIAASTP      YES; SKIP OTHER COMPARES            85070
GTIANTSO CLI   CHVCD,X'04'   START TASK/INIT ?                   85070
         BNE   GTIAASID      NO                                  85070
GTIAAJOB SLR   R2,R14        MAKE FAKE CSCB ORIGIN               85070
         CLC   0(8,R7),CHCLS  MATCHING NAME ?                    85070
         BNE   GTIAASID      NO                                  85070
         CLI   8(R7),C' '    STEP, ETC. REQUEST ?                85070
         BNH   GTIAASTP      NO                                  85070
         CLC   8(8,R7),CHPROCSN  PROC NAME MATCH ?               85070
         BE    GTIAASTP      YES                                 85070
         CLC   8(8,R7),CHKEY  START ID ?                         85070
         BE    GTIAASTP      NO                                  85070
         CLI   4+8(R7),C' '  THREE BYTE PARM ?                   85070
         BNE   GTIAASID      NO                                  85070
         CLC   CHUNIT,8(R7)  UCB NAME MATCH ?                    85070
         BNE   GTIAASID                                          85070
GTIAASTP CLC   CHASID,ASCBASID  JUST IN CASE                     85070
         BNE   GTIAASID                                          85070
         ST    R5,RETR0      RETURN ASCB ADDRESS                 85070
*        B     GTCMASCB      GO TO COMMON RETURN                 85070
         DROP  R2,R3,R15                                         85070
         SPACE 1                                                 85070
GTCMASCB LH    R0,ASCBASID   GET THE ASID                        85070
         STH   R0,EXWAASID   RETURN TO CALLER                    85070
         LA    R1,EXWAJOB    GET RETURN AREA                     85070
         ST    R1,RETR1      RETURN TO USER                      85070
         MVC   EXWAJOBN(EXWASTP#-EXWAJOBN),BLANKS  PRE-CLEAR     85070
         ICM   R3,15,ASCBJBNI  INITIATED JOBNAME ?              GP99026
         BZ    GTCMJCSB      NO                                  85070
         SH    R3,=H'8'      MAKE IT INTO FAKE CSCB              85070
         B     GTCMHCSB                                          85070
GTCMJCSB ICM   R3,15,ASCBCSCB  GET THE CSCB                     GP99026
         BZ    BADRET4       NONE; SKIP DISPLAY                  85070
         USING CHAIN,R3                                          85070
GTCMHCSB LA    R14,CHCLS     POINT TO PRESUMED JOBNAME           85070
         LA    R15,CHSTEP    AND PRESUMED STEPNAME               85070
         CLI   CHVCD,X'0C'   MOUNT COMMAND ?                     85070
         BNE   GTCMMCSB      NO                                  85070
         LA    R14,=CL8'Mount'  MAKE FAKE JOBNAME                85070
         LA    R15,CHKEY     POINT TO ADDRESS FIELD              85070
         B     GTCMVCSB      FORMAT IT                           85070
GTCMMCSB CLI   CHVCD,X'98'   TIME-SHARING TASK ?                 85070
         BNE   GTCMTCSB      NO                                  85070
         LA    R14,CHKEY     POINT TO USER ID                    85070
         TM    0(R14),255-C' '  ANY USERID ?                     85070
         BNZ   *+8           YES                                 85070
         LA    R14,=CL8'Logon'  MAKE PLAIN LOGON                 85070
         LA    R15,CHCLS     POINT TO PROCNAME                   85070
         B     GTCMVCSB                                          85070
GTCMTCSB TM    CHTRKID,CHINITID  INITIATOR CSCB ?                85070
         BNO   GTCMICSB      NO                                  85070
         LA    R15,BLANKS    NO STEPNAME                         85070
         B     GTCMVCSB      GO TO MOVE NAMES                    85070
GTCMICSB TM    0(R15),255-C' '  ANYTHING IN THE STEPNAME ?       85070
         BNZ   GTCMVCSB      YES; DISPLAY IT                     85070
         LA    R15,CHPROCSN  POINT TO PROCSTEP                   85070
         TM    0(R15),255-C' '  ANYTHING IN IT ?                 85070
         BZ    GTCMPCSB      NO; TRY FOR UNIT                    85070
         CLC   =C'IEFPROC ',0(R15)  START STEP NAME ?            85070
         BNE   GTCMVCSB      NO; DISPLAY IT                      85070
GTCMPCSB LA    R15,BLANKS    POINT TO BLANKS                     85070
         TM    CHUNIT,X'C0'  VALID CONTENTS IN UNIT ?            85070
         BNO   GTCMVCSB      NO; LEAVE BLANK                     85070
         MVC   EXWASTEP(3),CHUNIT  USE UNIT NAME                 85070
GTCMVCSB MVC   EXWAJOB,0(R14)  MOVE PRESUMED JOBNAME             85070
         CLI   0(R14),0      ADDRESS SPACE BUILD ?               85070
         BE    EXIT          YES, OR SIMILAR FUNNY - SKIP CRUD   85070
         TM    0(R15),255-C' '  ANYTHING IN STEPNAME ?           85070
         BZ    *+10          NO; SKIP THE MOVE                   85070
         MVC   EXWASTEP,0(R15)  MOVE THE STEPNAME                85070
         B     EXIT          RETURN TO USER                      85070
         DROP  R3,R5                                             85070
         SPACE 2                                                 85070
ENTSCHFR STM   R0,R14,SCHEDSAV   ***ONEXIT ENTRY FOR CSA FREEMAIN
         MVI   SCHEDENT,4    SET DELETE ENTRY                    85070
         SR    R5,R5         INDICATE FREEMAIN REQUEST           85070
         B     SCHEDCOM      GO TO COMMON CODE                   85070
         SPACE 1                                                 85070
SCHEDELE SR    R5,R5         FORCED ENTRY (STAE INTERCEPT)       85070
         MVI   SCRETCH+3,8   SET ERROR                           85070
         B     SCHEDCOM      PROCESS AND RETURN                  85070
         SPACE 1                                                 85070
*        ENTRY TO GET CSA R0=LENGTH OF USE PORTION               85070
*                                                                85070
ENTSCHIN STM   R0,R14,SCHEDSAV                                   85070
         MVI   SCHEDENT,1    SET GET CSA ENTRY                   85070
         LTR   R5,R7         COPY SIZE TO BE GOTTEN              85070
         BNP   SCHEDR16      MAJOR ERROR                         85070
         B     SCHEDCOM      ELSE HONOR                          85070
         SPACE 1                                                 85070
*        ENTRY TO MOVE USER CODE TO CSA R0=LEN; R1=ADDRESS       85070
*                                                                85070
ENTSCHMV LR    R0,R7         COPY USER CODE ADDRESS              85070
         L     R1,4(,R13)    GET CALLER'S SAVE AREA              85070
         L     R1,20+2*4(,R1)  GET CALLER'S R2                   85070
ENTSCHMO STM   R0,R14,SCHEDSAV                                   85070
         MVI   SCHEDENT,2    SET INIT ENTRY                      85070
         ICM   R0,15,EXWCRMLN   ANY CSA GOTTEN ?                 85070
         BZ    SCHEDR16      NO; MAJOR ERROR                     85070
         B     SCHEDCOM                                          85070
         SPACE 1                                                 82200
*        ENTRY TO SCHEDULE AN SRB FOR PREVIOUSLY INITIALIZED CSA CODE
*                                                                85070
ENTSCHED STM   R0,R14,SCHEDSAV   SAVE CALLER'S REGISTERS         85070
         ICM   R0,15,EXWCRMLN   ANY CSA GOTTEN ?                 85070
         BZ    SCHEDR16      NO; MAJOR ERROR                     85070
         MVI   SCHEDENT,3    SET SRB SCHEDULE ENTRY              85070
         ST    R7,EXWCRMID   SET DESIRED ASID                    85070
SCHEDCOM XC    SCHEDXC(SCHEDXCL),SCHEDXC  CLEAR ALL              85070
         SPACE 1                                                 85070
         USING PSA,R0        LOW STORAGE (PSA) ADDRESSABILITY    85070
         MODESET MODE=SUP                                        85070
         SR    R2,R2         ZERO KEY (?)                        85070
         MODESET SAVEKEY=(2),EXTKEY=ZERO  LOAD STORAGE KEY       85070
         ST    R2,SCSAVKEY   SAVE FOR EXIT                       85070
         TM    SCHEDFLG,SCHEDEST  ESTAE ALREADY ISSUED ?         85070
         BNZ   SCHREDO       YES; DON'T DO IT AGAIN              85070
         OI    SCHEDFLG,SCHEDEST  SHOW ISSUED                    85070
         MVC   SCHWSTAE(SCHWSTAL),PATWSTAE  MAKE STAE LIST       92298
         ESTAE STAEEXIT,CT,PARAM=(R13),TERM=YES,BRANCH=YES,SVEAREA=(13)*
               ,MF=(E,SCHWSTAE)                                  85070
         BXLE  R15,R15,SCHREDO GO ON IF OK                       85070
SCHEDR16 LA    R15,16        SET MAJOR ERROR                     85077
         ST    R15,SCRETCH   SHOW ERROR                          85077
         B     SCHEXIT       EXIT WITH ERROR                     85070
SCHREDO  CLI   SCHEDENT,4    FREE REQUEST ?                      85070
         BE    SCHFREE       YES; REQUEST TO FREE CSA            85070
         CLI   SCHEDENT,1    GET REQUEST ?                       85070
         BNE   SCHEDNGT      NO                                  85070
         LA    R5,CSASIZE(,R5)  ADD FIXED OVERHEAD               85070
         CL    R5,EXWCRMLN   CURRENT AREA LARGE ENOUGH ?        GP99026
         BNH   SCHREDOP      YES; USE IT                         85070
         BAS   R9,SCHFREEP   FREE PROGRAM AREA                   85070
         LR    R6,R5         GET SIZE AGAIN                      85070
         BAS   R9,SCHGREEP   GET PROGRAM AREA                    85070
SCHREDOP LM    R15,R0,EXWCRMLN  GET CSA LENGTH/ADDRESS           85070
         LR    R7,R0         SET CSA BASE                        85070
         LR    R1,R15        MAKE R0/R1 FOR MVCL                 85070
         L     R14,=A(CSACODE)   GET COMMON CODE                GP97241
         LA    R15,CSASIZE   GET FIXED LENGTH                    85070
         MVCL  R0,R14        COPY CSA CODE; ZERO UNUSED PORTION  85070
         LA    R1,CSASIZE(,R7)  GET START OF USER'S ENTRY        85070
         ST    R1,EXWCRMCD   SAVE FOR USER                       85070
         B     SCHEXIT       EXIT                                85070
         SPACE 1                                                 85070
SCHEDNGT CLI   SCHEDENT,2    INITIALIZATION REQUEST ?            85070
         BNE   SCHEDSCH      NO; SCHEDULE SRB                    85070
         LM    R0,R1,SCHEDSAV  RELOAD USER'S PARAMETERS          85070
         LTR   R0,R0         VALID ADDRESS ?                     85070
         BZ    SCHEDR16      NO; FAIL                            85070
         LTR   R1,R1         VALID LENGTH ?                      85070
         BNP   SCHEDR16      NO; FAIL                            85070
         LM    R14,R15,EXWCRMLN  GET LENGTH AND ADDRESS          85070
         AR    R15,R14       MAKE LAST BYTE                      85070
         L     R14,EXWCRMCD  GET START OF USER'S CODE            85070
         SR    R15,R14       MAKE LENGTH                         85070
         MVCL  R14,R0        COPY USER'S CODE TO CSA             85070
         B     SCHEXIT       AND EXIT                            85070
         SPACE 1                                                 85070
SCHEDSCH CLI   SCHEDENT,3    DOUBLE-CHECK SRB SCHEDULE REQUEST ? 85070
         BNE   SCHEDR16      HUH ?                               85070
         L     R7,EXWCRMAD   GET CSA ADDRESS                     85070
         USING CSACODE,R7    DECLARE CODE AREA                   85070
         SPACE 1                                                 85070
         L     R15,CVTPTR    CVT ADDRESS                         85070
         USING CVTMAP,R15    CVT ADDRESSABILITY                  85070
         L     R14,CVTASVT   ASVT ADDRESS                        85070
         USING ASVT,R14      ASVT ADDRESSABILITY                 85070
         L     R10,EXWCRMID  GET REQUESTED ASID                  92323
         N     R10,=X'7FFFFFFF'  KILL 31-BIT MODE FLAG           92323
         BNZ   SCHEDSCA      LOOK FOR IT                         85070
         L     R10,PSAAOLD-PSA  GET CURRENT ASCB                 85070
         LH    R10,ASCBASID-ASCB(,R10)  0 FOR DEFAULT (THIS ASID)
         NC    EXWCRMID,=X'80000000'  KEEP MODE OPTION           92323
         O     R10,EXWCRMID  PRESERVE AMODE                      92323
         ST    R10,EXWCRMID  PASS BACK TO CALLER                 85070
SCHEDSCA C     R10,ASVTMAXU  COMPARE TO MAX                      85070
         BH    SCHEDR12      ERROR                               85070
         SLL   R10,2         CONVERT TO OFFSET                   85070
         LA    R14,ASVTFRST(R10)   ADDRESS SPACE ENTRY           85070
         USING ASVTENTY,R14  ADDRESSABILITY FOR ENTRY            85070
         ICM   R10,15,ASVTENTY   ASCB ADDRESS                    85070
         BP    FOUNDASC      PROCEED IF OK                       85070
         USING ASCB,R10      ASCB ADDRESSABILITY                 85070
SCHEDR12 MVI   SCRETCH+3,12  SET NOT FOUND                       85070
         B     SCHEXIT       AND EXIT                            85070
FOUNDASC TM    ASCBFLG1,ASCBTERM+ASCBABNT  IS IT USABLE ?        85070
         BNZ   SCHEDR12      NO; SET ERROR                       85070
         TM    ASCBDSP1,ASCBFAIL+X'80'  SAFE ?                   85070
         BNZ   SCHEDR12      NO; FAIL REQUEST                    85070
         DROP  R10,R14,R15                                       85070
         SPACE 1                                                 85070
         LA    R9,CSASRB     ADDR OF AREA TO BUILD SRB           85070
         USING SRB,R9                                            85070
         XC    SRB(SRBSIZE),SRB   CLEAR SRB                      85070
         ST    R10,SRBASCB   SAVE TARGET ASCB ADDRESS            85070
         MVC   SRBID,=CL4'SRB'         CONTROL BLOCK ID          85070
         LR    R14,R7        GET ROUTINE ADDRESS                 85070
         ST    R14,SRBEP     INTO ENTRY POINT                    85070
*    NOTE THAT X'80' IN SRBEP CAUSES 31-BIT DISPATCH             92297
         LA    R14,CSAQUIT-CSACODE(,R14)  GET ADDRESS OF TERM    85070
         ST    R14,SRBRMTR   INTO SRB                            85070
         TM    EXWCRMID,X'80'  31-BIT MODE RQUESTED ?            92323
         BZ    SCHEDA24      NO                                  92323
         OI    SRBEP,X'80'   REQUEST 31-BIT DISPATCH             92323
         OI    SRBRMTR,X'80'  DITTO FOR RECOVERY                 92323
SCHEDA24 L     R0,PSATOLD    GET TCB ADDRESS                     85070
         ST    R0,SRBPTCB    INTO SRB                            85070
         L     R1,PSAAOLD    GET ASCB ADDRESS                    85070
         ST    R1,CSAASCB-CSACODE(,R7)  FOR CROSS-MEMORY POST    90201
         USING ASCB,R1                                           85070
         LH    R0,ASCBASID   GET MY ASID                         85070
         STH   R0,SRBPASID                                       85070
         XC    SRBCPAFF,SRBCPAFF   KILL CPU AFFINITY             85070
         DROP  R1,R9                                             85070
         ST    R7,CSAPTCOD-CSACODE(,R7)   STORE CODE POINTER     90201
         XC    USERSPEX-CSACODE(8,R7),USERSPEX-CSACODE(R7) CLEAR 90201
         MVI   USERLOOP+2-CSACODE(R7),1  SET LOOP COUNTER        90201
*        C     R0,EXWCRMID   SCHEDULE TO SAME ADRESS SPACE ?     85070
*        BE    SCHEDLCL      YES; DO LOCALLY                     85070
         MVI   CSAECB-CSACODE(R7),0      CLEAR ECB               90201
         MVI   TIMECB,0      CLEAR STIMER ECB                    85070
         LA    R14,CSAECB    SCHEDULE ECB                        85070
         LA    R15,TIMECB    STIME ECB                           85070
         STM   R14,R15,SCHECBLS  BUILD ECB LIST                  85070
         MVI   SCHECBLS+4,X'80'  SET END OF LIST BIT             85070
         STIMER REAL,STIMEXIT,BINTVL==A(120*100) WAIT 2 MINUTES  85070
         SCHEDULE SCOPE=GLOBAL,SRB=(R9) CSASRB  HOP FOR THE BEST 85070
         SPACE 1                                                 85070
         TM    CSAECB,X'40'  POSTED ?                            85070
         BO    PREPAID       YES                                 85070
         WAIT  ECBLIST=SCHECBLS   AWAIT COMPLETION OF SOMETHING  85070
PREPAID  TM    TIMECB,X'40'  TIMER FINISHED ?                    85070
         BNZ   PREPAID2      YES                                 85070
         TTIMER CANCEL       ELSE CLEAR IT OUT                   85070
PREPAID2 TM    CSAECB,X'40'  SRB COMPLETION POSTED ?             85070
         BNZ   PREPAID3      YES; OK                             85070
         MVI   SCRETCH+3,4   ELSE SET RETURN CODE 4 - SRB ERROR  85070
PREPAID3 L     R2,SCSAVKEY   GET STORAGE KEY                     85070
         MODESET KEYADDR=(2)  RESET STORAGE KEY                  85070
         B     SCHEXIT       AND EXIT                            85070
         SPACE 1                                                 85070
*        SIMULATE SCHEDULE IF SAME ASID                          85070
*                                                                85070
*CHEDLCL SPIE  SCHEDLEX,((1,15)) SAVE USER'S SPIE EXIT ADDRESS   85070
*        ST    R1,SCHEDONX   SAVE USER'S SPIE                    85070
*        ONSPIE SCHEDLEX     SET MY OWN ADDRESS                  85070
*        MODESET MODE=PROB   MAKE SPIE WORK                      85070
*CHEDLPP L     R13,EXWCRMCD  LOAD USER'S CODE ADDRESS            85070
*        LA    R15,USERENTY-USERCODE(,R13)  GET ENTRY ADDRESS    85070
*        BASR  R14,R15       CALL IT                             85070
*        BASR  R11,0         IN CASE USER CREAMED REGISTERS      85070
*        USING SCHEDLPQ,R10  DECLARE TEMPORARY BASE              85070
*CHEDLPQ SL    R11,=A(SCHEDLPQ-EXHBWORK)  GET TRUE BASE BACK     85070
*        USING EXHBWORK,R10                                      85070
*        L     R13,SQSP      RESTORE SQSP ADDRESS                85070
*        MVC   SQSPIAD,SCHEDONX  RESTORE USER'S SPIE EXIT        85070
*        MVC   SQSPICA+4(2),SCHEDONX+4  RESET MASK               85070
*        MODESET MODE=SUP    PERMIT FREEMAIN, ETC.               85070
*        B     SCHEXIT       AND QUIT                            85070
*CHEDLEX BASR  R11,0         MAKE LOCAL BASE                     85070
*        LA    R12,SCHEDLEX-EXHBWORK+2  OFFSET TO EXIT           85070
*        SLR   R11,R12       GET GLOBAL BASE BACK                85070
*        L     R12,EXWCRMCD  LOAD USER'S CODE ADDRESS            85070
*        USING USERCODE,R12  DECLARE IT                          85070
*        L     R13,SQSP      LOAD SQA CODE                       85070
*        ICM   R15,15,USERSPEX  TEST FOR EXIT                    85070
*        BZ    SCHEDR4X      NONE; RETURN CODE 4                 85070
*        ICM   R0,15,USERLOOP  TEST RETRY COUNTER                85070
*        BNP   SCHEDR4X      EXCEEDED                            85070
*        BCTR  R0,0                                              85070
*        ST    R0,USERLOOP   REPLACE IT                          85070
*        XC    USERSPEX(4),USERSPEX  SHOW EXIT TAKEN             85070
*        ONSPIE SCHEDLEX,WORK=R14  RE-INSTATE THIS SPIE EXIT     85070
*        LM    R0,R14,USERSAVE   RELOAD USER'S REGISTERS         85070
*        BR    R15           GO TO USER EXIT                     85070
*        DROP  R12                                               85070
*        SPACE 2                                                 85070
*CHEDR4X MVC   SQSPIAD,SCHEDONX   RESTORE USER'S SPIE EXIT       85070
*        MVC   SQSPICA+4(2),SCHEDONX+4  RESET MASK               85070
*        MODESET MODE=SUP    PERMIT FREEMAIN, ETC.               85070
SCHEDR4  MVI   SCRETCH+3,4   SET RETURN CODE 4                   85070
SCHEXIT  TM    SCHEDFLG,SCHEDEST  ESTAE ISSUED ?                 85070
         BZ    SCHEXITM      NO; SKIP FREE                       85070
         ICM   R0,15,EXWCRMLN  STORAGE FREED ?                   85070
         BNZ   SCHEXITM      NO; KEEP ESTAE                      85070
         ESTAE 0,BRANCH=YES,SVEAREA=(13)  KILL ESTAE             85070
         NI    SCHEDFLG,255-SCHEDEST  RESET ESTAE FLAG           85076
SCHEXITM L     R2,SCSAVKEY   GET STORAGE KEY                     85070
         MODESET KEYADDR=(2)  RESET STORAGE KEY                  85070
         MODESET MODE=PROB   RESTORE PROBLEM PROGRAM MODE        85070
SCHEXIT0 LM    R0,R15,SCHEDSAV  RESTORE R0-R14; SET R15 RETURN CODE
         ST    R15,RETR15    PROPAGATE RETURN CODE               92323
         MVC   RETR0,EXWCRMCD  RETURN USER'S ADDRESS             85070
         CLI   SCHEDENT,0    CALLED FROM STAE ROUTINE ?          85070
         BNE   SCHEXITR                                          85070
         ABEND 999,DUMP      ELSE BOMB                           85070
SCHEXITR MVI   SCHEDENT,0    SHOW NO LONGER IN CRMS CODE         85070
         BR    R14           RETURN TO CALLER                    85070
         SPACE 2                                                 85070
SCHFREE  LA    R9,SCHEXIT    SET RETURN ADDRESS FROM SUBROUTINE  85070
         SPACE 1                                                 85070
SCHFREEP LM    R6,R7,EXWCRMLN  LOAD LENGTH/ADDRESS               85070
         XC    EXWCRMLN(12),EXWCRMLN  CLEAR                      85070
         LTR   R6,R6         ANY LENGTH ?                        85070
         BNPR  R9            NO; RETURN                          85070
         SETLOCK OBTAIN,TYPE=SALLOC,MODE=UNCOND,REGS=USE,RELATED='?'
         FREEMAIN RU,LV=(R6),A=(R7),SP=228,BRANCH=(YES,GLOBAL) CSA
         SETLOCK RELEASE,TYPE=SALLOC,REGS=USE,RELATED='?'        85070
         BR    R9            RETURN TO CALLER                    85070
*OLD*EE0 FREEMAIN R,LV=(R6),A=(R7)  FREE SP0                     85070
SCHFREE0 SR    R15,R15                                          GP03076
         IC    R15,SUBPOOL                                      GP03076
         STORAGE RELEASE,ADDR=(R7),LENGTH=(R6),SP=(15),COND=YES GP03076
         BR    R9                                                85070
         SPACE 1                                                 85070
SCHGREEP XC    EXWCRMLN(12),EXWCRMLN CLEAR JUST IN CASE          85070
         LTR   R6,R6         VALID LENGTH SPECIFIED ?            85070
         BNP   SCHEDR16      NO                                  85070
         SETLOCK OBTAIN,TYPE=SALLOC,MODE=UNCOND,REGS=USE,RELATED='?'
         GETMAIN RU,LV=(R6),SP=228,BRANCH=(YES,GLOBAL) FREE      85070
         LR    R5,R15        SAVE RETURN CODE                    85070
         LR    R7,R1         SAVE ADDRESS                        85070
         SETLOCK RELEASE,TYPE=SALLOC,REGS=USE,RELATED='?'        85070
SCHGREEC BXH   R5,R5,SCHEDR16   FAIL IF GETMAIN FAILED           85070
         STM   R6,R7,EXWCRMLN   SET LENGTH/ADDRESS IN WORK AREA  85070
         BR    R9            RETURN TO CALLER                    85070
*OLD*EE0 GETMAIN R,LV=(R6)                                       85070
SCHGREE0 SR    R15,R15                                          GP03076
         IC    R15,SUBPOOL                                      GP03076
         STORAGE OBTAIN,LENGTH=(R6),SP=(15),COND=YES            GP03076
         LR    R5,R15        COPY RETURN CODE                    85070
         LR    R7,R1         COPY RETURN ADDRESS                 85070
         B     SCHGREEC      REJOIN COMMON                       85070
         SPACE 1                                                 85070
         PUSH  USING                                             85070
         DROP  ,                                                 85070
         USING STAEEXIT,R15                                      85070
STAEEXIT CH    R0,=H'12'     WORK AREA PRESENT ?                 85070
         BNE   STAEEXT2      YES                                 85070
STAEEXT1 SR    R15,R15                                           85070
         BR    R14           CONTINUE WITH ABEND                 85070
         USING SDWA,R1       DECLARE THE PASSED WORK AREA        85070
STAEEXT2 STM   R14,R12,12(R13)  JUST IN CASE ...                 85070
         L     R11,SDWAPARM  GET SAVE AREA BACK                  85070
         USING SAVEAREA,R11  DECLARE SAVE AREA                   85070
         TM    SCHEDFLG,SCHEDCUR   RECURSION ?                   85070
         BNZ   STAEEXT1      YES; ABEND                          85070
         MVC   SCHEDADC,=C'PSW '                                 85097
         MVC   SCHEDADP,SDWACTL1 GET BC-MODE PSW                 85097
         MVC   SCHEDARC,=C'REGS'                                 85097
         MVC   SCHEDARG(4*16),SDWAGRSV  SAVE REGS FOR DUMP       85097
         OI    SCHEDFLG,SCHEDCUR  PREVENT LOOPING                85070
         ST    R11,SDWASR13  ENSURE SAVE/WORK REGISTER IN RETRY  85070
         LR    R2,R15        COPY BASE ADDRESS                   85070
         LA    R3,STAEEXIT-BASCRMSV  GET BASE OFFSET             85070
         SR    R2,R3         MAKE BASE                           85070
         ST    R2,SDWASR11   ENSURE RETRY BASE                   85070
         LR    R4,R2         COPY CRMS BASE                      85070
         SH    R4,=Y(BASCRMSV-@SERVICE)  GET MAIN BASE OFFSET    85070
         ST    R4,SDWASR12   SET MAIN BASE FOR RETRY             85070
         LA    R3,SCHEDELE-BASCRMSV(,R2)   GET FREEMAIN ROUTINE GP97241
         ST    R3,SDWARTYA   SET RETRY ADDRESS                   85070
         OI    SDWAACF2,SDWAFREE+SDWAUPRG  RETURN REGS, FREE SDWA
         NI    SDWACMPF,255-SDWAREQ  KILL DUMP                   85070
         MVI   SDWARCDE,4    REQUEST RETRY                       85070
         LM    R14,R12,12(R13)  RELOAD REGISTERS                 85070
         LA    R15,4         SET RETRY                           85070
         BR    R14           RETURN TO OS                        85070
         SPACE 1                                                 85070
STIMEXIT POST  TIMECB,0                                          85070
         BR    R14                                               85070
         SPACE 2                                                 85070
         LTORG ,                                                 85070
PATWSTAE ESTAE STAEEXIT,CT,MF=L                                  92298
         SPACE 2                                                 85070
*        CODE MOVED TO CSA; MAY RUN 24 OR 31-BIT ADDRESSING MODE 85070
         DROP  ,                                                 85070
         USING CSACODE,R13                                       85070
@SERVICS RSECT ,             AVOID ADDRESSABILITY CONFLICTS     GP99026
CSACODE  LR    R13,R15       SET BASE ADDRESS                    85070
         ST    R14,CSARET-CSACODE(,R13)    SAVE RETURN ADDRESS   90201
         SPACE 1                                                 85070
         LA    R12,CSARFRR   ADDRESS OF FRR                      85070
         SETFRR A,FRRAD=(R12),WRKREGS=(R10,R8),PARMAD=(R11)      85070
         USING FRPA,R11      ADDRESS FRR PARM AREA               85070
         ST    R9,FRPCSAWA   PASS CSAWORK ADDRESS TO FRR         85070
         LA    R14,CSARETRY  SET RETRY ADDRESS                   85070
         STM   R13,R14,FRPSRBBA    BASE AND RETRY ADDRESS TO FRR 85070
         DROP  R11                                               85070
         L     R14,CSARET    GET RETURN ADDRESS                  85211
         STM   R0,R15,CSABIGSV-CSACODE(R13)  SAVE EVERYTHING     90201
         LA    R13,USERCODE  ADDRESS USER CODE                   85070
         LA    R15,USERENTY-USERCODE(,R13)  SET ENTRY ADDRESS    85070
         BASR  R14,R15       INVOKE USER CODE                    85070
         SPACE 1                                                 85070
         BASR  R13,0         IN CASE USER LOUSED UP              85070
         USING CSABASE2,R13  DECLARE IT                          85070
CSABASE2 LM    R0,R14,CSABIGSV  GET REGISTERS BACK               85070
         USING CSACODE,R13   DECLARE RESTORED BASE               85070
         LR    R10,R15       SET CODE FOR SLOW POST              85070
         B     CSAEXIT       GO TO COMMON EXIT                   89092
         SPACE 1                                                 89092
         USING CSACODE,R13         GET BASE ADDRESSABILITY       85070
         USING SDWA,R1             SYSTEM DIAGNOSTIC WORK AREA   85070
CSARFRR  L     R13,CSAPTCOD-CSARFRR(,R15)  GET BASE BACK         85070
         L     R12,SDWAPARM  FRR PARM                            85070
         USING FRPA,R12                                          85070
         ICM   R15,15,USERSPEX  TEST FOR EXIT                    85070
         BZ    CSAFRRX       NONE; RETURN CODE 12                85070
         ICM   R0,15,USERLOOP  TEST RETRY COUNTER                85070
         BNP   CSAFRRX       EXCEEDED                            85070
         BCTR  R0,0                                              85070
         ST    R0,USERLOOP-CSACODE(,R13)   REPLACE IT            90201
         LA    R4,CSAUEXIT   SET TO ENTER USER'S CODE            85070
         B     CSAFRRY       GO TO USER EXIT VIA FRR RETRY       85070
CSAFRRX  LA    R4,CSARETRY   SET RETRY                           85070
CSAFRRY  SETRP RC=4,RETADDR=(R4),RECORD=NO RECPARM=FRRNAMES      85070
         BR    R14                                               85070
         SPACE 1                                                 85070
CSAUEXIT BASR  R13,0                                             85070
         USING CSABASE3,R13                                      90201
CSABASE3 L     R15,USERSPEX                                      90201
         XC    USERSPEX-CSABASE3(4,R13),USERSPEX-CSABASE3(R13)   90201
         LM    R0,R14,USERSAVE                                   85070
         BR    R15           RE-ENTER USER'S CODE                85070
         SPACE 1                                                 85070
FRRNAMES DC    CL8'ESP INC.' SET IDENTIFIER                      85070
         DC    CL8'@SERVICE'   AND CSECT NAME                    85070
         DC    CL8'CSARFRR'    AND FRR                           85070
         DROP  ,                                                 85070
CSARETRY LA    R10,12        SET MAJOR ERROR CODE                89092
CSAEXIT  BASR  R13,0         ESTABLISH BASE                      89092
         USING CSABASE4,R13                                      90201
CSABASE4 SETFRR D,WRKREGS=(R2,R3)  DELETE FRR                    90201
         B     CSAQUITS      JOIN COMMON POST                    89092
CSAQUIT  LA    R10,12        SET MAJOR ERROR                     89092
CSAQUITS BASR  R13,0         ESTABLISH BASE FOR POST CODE        89092
         USING CSABASE5,R13                                      90201
CSABASE5 L     R9,CSARET     GET RETURN ADDRESS                  90201
         ICM   R0,15,CSAECB  IS CALLER WAITING ON ECB ?          89092
         BM    CSAXPOST      YES; USE CROSS-MEMORY POST          89092
         LR    R1,R10        COPY POST CODE                      89092
         ICM   R1,8,=X'40'   SET ECB POST CODE                   89092
CSARPOST CS    R0,R1,CSAECB-CSABASE5(R13)  POST THE QUICK WAY    90201
         BER   R9            AND EXIT                            89092
CSAXPOST LA    R11,CSAECB    GET ECB ADDRESS                     85070
         O     R11,=X'80000000'  CROSS-MEMORY POST FLAG          85070
         LR    R12,R9        ON ERROR, JUST RETURN               85070
         L     R13,CSAASCB   SET MY ASCB ADDR.                   85070
         L     R1,CVTPTR     SET CVT AGAIN                       85070
         L     R15,CVT0PT01-CVT(,R1)   GET NON-I/O POST ENTRY    85070
         LR    R14,R9        SET RETURN ADDRESS                  89205
         BR    R15           MAIL IT (RETURN TO R14=R9)          89205
CSAPTCOD DC    A(0)          ADDRESS OF THIS AREA                85070
CSAECB   DC    A(0)          COMPLETION ECB                      85070
CSAASCB  DC    A(0)          MY ASCB ADDRESS                     85070
CSARET   DC    A(0)          SRB RETURN ADDRESS                  85070
CSABIGSV DC    16A(0)        BIG SAVE AREA                       85070
CSASRB   DC    6D'0'         SRB                                 85070
         SPACE 1                                                 85070
         LTORG ,                                                 85070
         DS    0D                                                85070
CSASIZE  EQU   *-CSACODE     LENGTH OF FIXED PREFIX              85070
USERCODE DS    0D            START OF USER'S CODE                85070
USERSAVE DC    16A(0)        USER'S SAVE AREA                    85070
USERSPEX DC    A(0)          USER'S ONSPIE EXIT ADDRESS          85070
USERLOOP DC    F'4096'       RETRY LIMIT COUNTER                 85070
USERENTY STM   R0,R14,USERSAVE-USERCODE(R13)                     85070
*        ABOVE SEQUENCE IS MANDATORY PREFIX FOR USER CODE        85070
         SPACE 3                                                 85070
         DROP  ,                                                 85070
@SERVICE RSECT ,             RESTORE BASE CSECT                 GP99026
*        SET ADDRESS SPACE (NON) SWAPPABLE                       85070
*                                                                85070
         USING BASCRMSV,R11                                      85070
         USING SAVEAREA,R13                                      85070
         USING @SERVICE,R12                                      85070
ENTSWAPY SYSEVENT DONTSWAP   REQUEST NON-SWAP                    90273
         ST    R15,RETCH     SET RETURN CODE                     90273
         BR    R10           RETURN TO CALLER                    90273
ENTSWAPN SYSEVENT OKSWAP     ALLOW SWAPS                         90273
         ST    R15,RETCH     SET RETURN CODE                     90273
         BR    R10           RETURN TO CALLER                    90273
         EJECT ,                                                 85070
*        MVS AND JES2 DEPENDENT REQUESTS                         85070
*                                                                85070
         DROP  ,                                                 85070
ENTJESVC BASR  R11,0         MAKE LOCAL BASE                     90273
         USING *,R11         DECLARE IT                          90273
         USING SAVEAREA,R13                                      90273
         USING @SERVICE,R12                                      90273
         CLI   SUBPOOL,FGSVC  CALLED AS SVC ?                    90273
         BER   R9            YES; RETURN TO CALLER               90273
         MVI   EXWJ2NAM,C' '   RUN UNDER CURRENT SUBSYSTEM       90273
         CLI   CALLR0+3,VENJESVC  DID WE GET CALLED FOR JES SVCS?
         BNE   *+10          NO                                  90273
         MVC   EXWJ2NAM,0(R7)  COPY REQUESTED SUBSYSTEM          90273
         L     R11,=A(BASCRMSV)  LOAD GROUP BASE                 90273
         USING BASCRMSV,R11                                      90273
         BAS   R10,ENTSSLOC  LOCATE (DEFAULT) SUBSYS NAME        90273
         CLI   CALLR0+3,VENJESVC  DID WE GET CALLED FOR JES SVCS?
         BNE   *+10          NO                                  90273
         MVC   0(8,R7),EXWJ2NAM  RETURN SYSTEM NAME             GP06284
         MVC   RETR0(8),CALLR0  RESTORE ENTRY REGISTERS          90273
         MVC   DB(4),=C'@SRV'  MAKE MODULE PREFIX                90273
         MVC   DB+4(4),EXWJ2NAM  MAKE MODULE SUFFIX              90273
         AIF   (NOT &LOCO).NOJVC1                                90273
         CLC   =C'JES2',EXWJ2NAM  PRIMARY SUBSYSTEM ?            90273
         BNE   JESVCLUK      NO; NEED TO DO LOOK-UP              90273
         L     R2,CVTPTR                                         90273
         ICM   R2,15,CVTUSER-CVTMAP(R2)                          92297
         BZ    JESVCLUK                                          90273
         ICM   R15,15,UCLSERV2-USERCVT(R2)                       90273
         BNZ   JESVCXFR      GO TO IT                            90273
.NOJVC1  ANOP  ,                                                GP03262
JESVCLUK LPALOOK EPLOC=DB    LOOK FOR EXTENSION (NO LOAD)       GP03262
         CH    R15,=H'4'     ANY FOUND ?                        GP03262
         BNH   JESVCLOK      YES; USE IT                        GP06284
*---------------------------------------------------------------------*
*   FOR DEBUGGING @SRV--- MODULES, LOOK FOR PRE-LOADED MODULE         *
*---------------------------------------------------------------------*
         PUSH  USING                                            GP06284
         L     R3,CVTPTR                                        GP06284
         LTCB  R4,HAVE=CVT,USE=YES  LOAD MY TCB ADDRESS         GP06284
         LA    R5,TCBJPQ-CDCHAIN+CDENTRY  POINT TO THE PACK LISTGP06284
         USING CDENTRY,R5                                       GP06284
JESVLJPA ICM   R5,7,CDCHAIN+1  LOAD CDE                         GP06284
         BZ    JESVDLPA      HUH ?                              GP06284
         CLC   DB,CDNAME     DOES IT MATCH THE REQUEST ?        GP06284
         BNE   JESVLJPA      TRY AGAIN                          GP06284
         L     R0,CDENTPT    LOAD ENTRY POINT                   GP06284
         B     JESVCLOK      AND USE IT                         GP06284
         SPACE 1                                                GP06284
*---------------------------------------------------------------------*
*   TRY JOB'S LOAD LIST                                               *
*---------------------------------------------------------------------*
JESVDLPA LA    R6,TCBLLS-LLECHN+LLE  POINT TO LOAD LIST         GP06284
         USING LLE,R6                                            *TSM*
JESVLLLE ICM   R6,7,LLECHN+1                                    GP06284
         BZ    BADRET4       NOT FOUND; FAIL THE REQUEST        GP06284
         ICM   R5,7,LLECDPT+1  GET CDE                          GP06284
         BZ    JESVLLLE      HUH?                               GP06284
         CLC   DB,CDNAME     DOES IT MATCH THE REQUEST ?        GP06284
         BNE   JESVLLLE      TRY AGAIN                          GP06284
         L     R0,CDENTPT    LOAD ENTRY POINT                   GP06284
         B     JESVCLOK      AND USE IT                         GP06284
         POP   USING                                            GP06284
         SPACE 1                                                 90273
JESVCLOK LR    R15,R0        COPY THE ENTRY ADDRESS             GP03262
         AIF   (NOT &LOCO).NOJVC2                                90273
         CLC   =C'JES2',EXWJ2NAM  PRIMARY SUB-SYSTEM ?           90273
         BNE   JESVCXFR      NO                                  90273
         LR    R7,R15        SAVE OVER FINAGLING                 90273
         L     R2,CVTPTR                                         90273
         ICM   R2,15,CVTUSER-CVTMAP(R2)                          92297
         BZ    JESVCXFR      NONE                                90273
         MODESET KEY=ZERO                                        90273
         ST    R7,UCLSERV2-USERCVT(,R2)  STASH FOR FASTER ACCESS 90273
         MODESET KEY=NZERO                                       90273
         LR    R15,R7        RESTORE ADDRESS                     90273
.NOJVC2  SPACE 1                                                GP03262
JESVCXFR L     R1,4(,R13)    GET CALLER'S R13                    90273
         L     R0,12(,R1)    GET RETURN ADDRESS                  90273
         ST    R0,RETCH      SAVE AS ENTRY ADDRESS               90273
         ST    R15,12(,R1)   AND MAKE @SVCJESX THE 'RETURN'      90273
         B     EXIT          AND 'RETURN' VIA JES SERVICE RTNE   90273
         SPACE 2                                                 90273
         USING BASCRMSV,R11                                      85070
         USING @SERVICE,R12                                      85070
         USING SAVEAREA,R13                                      85070
ENTSSSET LA    R1,EXLOCNAM   POINT TO DEFAULT                    87270
SSSETMVC MVC   EXWJ2NAM,0(R1)  MOVE SUBSYSTEM NAME               87270
         LTR   R1,R7         USER REQUESTED NAME ?               87270
         BZ    ENTSSLOC      SKIP MOVE                           87270
         EX    0,SSSETMVC    MOVE USER'S NAME                    87270
         SPACE 1                                                 85070
ENTSSLOC CLI   EXWJ2NAM,C' '   SUB-SYSTEM NAME SET BEFORE ?      85070
         BH    *+10          YES                                 90274
         MVC   EXWJ2NAM,EXLOCNAM  NO; MOVE DEFAULT               85070
         L     R6,CVTPTR     GET THE CVT                         85070
         USING CVTMAP,R6                                         85070
         ICM   R5,15,CVTJESCT  SUBSYSTEM VECTOR TABLE            85070
         BNP   BADRET4       MAJOR BOOBOO                        85070
         USING JESCT,R5      DECLARE IT                          85070
         CLI   EXWJ2NAM,C' '   SUBSYSTEM DEFAULTED ?             85070
         BH    EXLOCHSY      NO                                  87270
         MVC   EXWJ2NAM(L'JESPJESN),JESPJESN  COPY PRIMARY NAME  85070
         L     R4,PSATOLD-PSA  GET MY TCB BACK                   87270
         L     R4,TCBJSCB-TCB(,R4)  GET THE JSCB                 87270
         ICM   R4,15,JSCBSSIB-IEZJSCB(R4)  TEST SSIB             87270
         BZ    EXLOCHSY      LEAVE PRIMARY                       87270
         CLI   SSIBSSNM-SSIB(R4),C' '  SUBSYSTEM PRESENT ?       87270
         BNH   EXLOCHSY      NO; KEEP PRIMARY                    87270
         CLC   EXLOCNMS,SSIBSSNM-SSIB(R4)  RUNNING ALONE ?       87270
         BE    EXLOCHSY      YES; KEEP PRIMARY                   87270
         MVC   EXWJ2NAM(4),SSIBSSNM-SSIB(R4) USE STARTER'S NAME  87270
EXLOCHSY LA    R5,JESSSCT-(SSCTSCTA-SSCT)  CHEAT                 87270
         USING SSCT,R5                                           85070
EXLOCSLP ICM   R5,15,SSCTSCTA   GET NEXT SSCVT                   85070
         BNP   BADRET4       USER CHOSE BAD NAME ?               85070
         CLC   SSCTSNAM,EXWJ2NAM  DESIRED SUBSYSTEM ?            85070
         BNE   EXLOCSLP      NO                                  85070
         AIF   (&MVSSP).NEWSVT                                  GP04234
         ICM   R4,15,SSCTSSVT  TEST FOR $HCT                    GP04234
         AGO   .COMSVT                                          GP04234
.NEWSVT  ICM   R4,15,SSCTSUS2  TEST FOR $HCCT                   GP02328
.COMSVT  BNP   BADRET4       NONE ?                              85070
         L     R15,CVTASVT   GET THE ASCB VECTOR TABLE           85070
         USING ASVT,R15                                          85070
         L     R0,ASVTMAXU   GET HIGHEST VALID ASID              85070
         SLL   R0,2          *4 FOR FAST CHECK                   85070
         SR    R1,R1         SET INDEX FOR NEXT ASID             85070
EXLOCSAD LA    R1,4(,R1)     GET NEXT ASID INDEX                 85070
         CR    R1,R0         VALID ?                             85070
         BH    BADRET4       NO; QUIT                            85070
EXLOCSOM LA    R3,ASVTFRST(R1)  POINT TO ASCB POINTER            85070
         ICM   R3,15,0(R3)   LOAD AND TEST                       85070
         BNP   EXLOCSAD      SKIP UNUSED/FREE SLOT               85070
         USING ASCB,R3                                           85070
         ICM   R2,15,ASCBJBNI  JOB CSCB ?                        85070
         BNZ   EXLOCSAD      YES; SKIP                           85070
         ICM   R2,15,ASCBCSCB  GET THE START CSCB                85070
         BZ    EXLOCSAD                                          85070
         USING CHAIN,R2      DECLARE THE CSCB                    85070
         CLI   CHVCD,X'04'   START CODE ?                        85070
         BNE   EXLOCSAD      NO                                  85070
         CLC   CHCLS,EXWJ2NAM   MATCHING ID ?                    85070
         BNE   EXLOCSAD      NO                                  85070
         LH    R0,ASCBASID   GET AS ID                           85070
         CH    R0,CHASID     JUST IN CASE                        85070
         BNE   EXLOCSAD                                          85070
         STM   R3,R4,RETR0   RETURN ASCB/$HCCT ADDRESSES         85070
*        USING SSVT,R4       DECLARE THE $HCCT                   85070
*        ICM   R2,15,$SVPIT  INITIATOR POINTER ?                 85070
*        BZ    BADRET4       NO ?                                85070
*        C     R3,$SVPOSTE+4  SAME ASCB IN $HCCT                 85070
*        BNE   BADRET4       NO                                  85070
*        CLC   $SVQLOKE,EXWJ2NAM  RIGHT EXPANSION ?              87270
*        BNE   BADRET4       NO; ERROR ?                         85070
         BR    R10           AND GO BACK                         90273
EXLOCNAM DC    CL8' JES    '  INITIALIZATION PARAMETER FOR SSID  87270
EXLOCNMS DC    C'MSTR'                                           87270
         SPACE 1                                                 85070
FRPA     DSECT ,                                                 85070
FRPCSAWA DS    A             CSAWORK ADDRESS                     85070
FRPSRBBA DS    A                                                 85070
FRPSRBRA DS    A                                                 85070
FRPSDVSL EQU   *-FRPA        MINIMUM LENGTH                      85070
         SPACE 2                                                 85070
         PUSH  PRINT                                             85070
         PRINT NOGEN                                             85070
         IHAASCB ,                                               85070
         IHAASVT ,                                               85070
         IHASRB ,                                                85070
         IHASDWA ,                                               85070
         IHAWSAVT CLASS=GLOBAL                                   85070
         IHAFRRS ,                                               85070
         AIF   (&MVSXA OR &MVSESA).FEEFIE                       GP97241
         IHAFOE ,                                                85070
.FEEFIE  ANOP  ,                                                 90201
         IEFJESCT ,                                              85070
         IEFJSCVT ,                                              85070
CSCB     DSECT ,                                                 85070
         IEECHAIN ,                                              85070
         POP   PRINT                                             85070
@SERVICE RSECT ,                                                GP99026
         POP   USING         RESTORE REGISTER ASSIGNMENTS        85070
.CRMSDON TITLE '@ S E R V I C E  ***  CATALOG SERVICE ROUTINES'  85147
*        LOCAT/LOCMT/LOCRT ENTRIES ARE FOR CATALOG LOOKUP.       85147
*        LOCMT WILL ISSUE LOCATE ONLY FOR MASTER CATALOG AND CVOLS
*              THAT ARE MOUNTED OR PERMANENTLY RESIDENT.         85147
*        LOCRT WILL ISSUE LOCATE ONLY FOR RESIDENT PACKS.        85147
*                                                                85147
*        R1=> CL44'DSN',2(X4'TYPE',C6'VOLSER')                   85147
*        RETURN R15,R0 FROM LOCATE; R1 WORK AREA                 85147
*          +44 IN USER'S AREA VOL TYPE/SER; +54 CVOL TYPE/SER    85147
         SPACE 1                                                 85147
ENTCATTL LTR   R6,R7         PARM REQUIRED                       85147
         BZ    BADENTRY                                          85147
         BASR  R11,0         MAKE LOCAL BASE                     85147
         USING *,R11                                             85147
         CLI   CALLMODE,VENCATCO/256  CONNECT REQUEST ?          85321
         BE    CCONENT       YES                                 85321
         USING CTLCALL,R6    DECLARE CALLER'S LIST               85147
         L     R14,LOCNMPAT  GET LOCATE CAMLST                   85147
         LA    R15,CATDSN    POINT TO REQUEST FIELD              85147
         SR    R0,R0         NO CVOL (MVS 3.8)                  GP08084
         LA    R1,CATWORK    POINT TO WORK AREA                  85147
         STM   R14,R1,CATCAM   MAKE REQUEST LIST                 85147
         MVI   CATDSN,C' '                                       85147
         MVC   CATDSN+1(L'CATDSN-1),CATDSN  CLEAR                85147
         MVC   CATVSER(CATCSER-CATVSER+L'CATCSER),CATDSN CLEAR   85147
         XC    CATCTYPE,CATCTYPE  CLEAR RESULTS                  85147
         XC    CATWORK(2),CATWORK  CLEAR WORK AREA               85147
         LA    R1,CATDSN                                         85147
         LA    R2,CATLDSN    CALLER'S NAME                       85147
         LA    R0,8          MAXIMUM INDEX LENGTH                85147
CATIXLUP CLI   0(R2),C' '    TRAILING BLANK ?                    85147
         BE    CATLOOK       YES; JUST LOCATE AS IS              85147
         CLI   0(R2),C'.'    INDEX DEMARKATION ?                 85147
         BE    CATIXBMP      YES                                 85147
         MVC   0(1,R1),0(R2) MOVE A BYTE                         85147
         LA    R1,1(,R1)                                         85147
         LA    R2,1(,R2)                                         85147
         BCT   R0,CATIXLUP                                       85147
         CLI   0(R2),C' '    END ?                               85147
         BE    CATLOOK       YES; GO TO SHORT NAME LOOKUP        85147
CATIXBMP LOCATE CATCAM       LOCATE THE INDEX                    85147
         BXH   R15,R15,CATLOOK  ERROR - DO NORMALLY              85147
         CLI   CATWORK+2+4,C' '   ANY CVOL RETURNED ?           GP08084
         BNH   CATLOOK       NO; DO NORMAL LOOKUP               GP08084
         MVC   CATCTYPE(L'CATCTYPE+L'CATCSER),CATWORK+2 CVOL DATA
         MVC   CATCVOL,CATCSER                                   85147
         CLI   CALLMODE,0    LOCATE ENTRY ?                     GP08084
         BE    CATLOOK       YES; DO NORMAL LOOKUP              GP08084
         LA    R7,CATCVOL    POINT TO VOL-SER                    85147
         BAS   R10,VSNUCB    LOCATE VOLUME                       85147
         B     CATNOTUP      VOLUME NOT UP                       85147
         LA    R0,CATCVOL    POINT TO CVOL NAME                  85147
         ST    R0,CATCAM+8      AND ADD TO LIST                 GP08084
         MVC   CATCAM(4),LOCNVPAT  SET FOR CVOL                  85147
         TM    UCBSTAT-UCBOB(R3),UCBPRES+UCBSYSR+UCBRESV MOUNTED 85147
         BZ    CATNOTUP      NO; PREVENT MOUNT BY LOCATE SVC     85147
         TM    UCBSTAT-UCBOB(R3),UCBPRES+UCBSYSR  RESIDENT ?     85147
         BNZ   CATLOOK       YES; DO LOOKUP                      85147
         CLI   CALLMODE,VENLOCRT/256  RESIDENT TASK MODE ?       85147
         BNE   CATLOOK       NO; USE IT                          85147
CATNOTUP LA    R15,4         FAKE CVOL CANNOT BE OPENED          85147
         SR    R0,R0                                             85147
         B     CATCMRET      GO TO COMMON RETURN                 85147
         SPACE 1                                                 85147
CATLOOK  MVC   CATDSN,CATLDSN  MOVE NAME TO BE LOCATED           85147
         XC    CATWORK(2),CATWORK                                85147
         LOCATE CATCAM       LOOK FOR IT                         85147
         MVC   CATLDSN,CATDSN  RETURN POSSIBLY ALTERED NAME      85147
         ICM   R1,3,CATWORK  ANY DATA MOVED ?                    85147
         BZ    CATCMRET      NO                                  85147
         MVC   CATVTYPE(L'CATVTYPE+L'CATVSER),CATWORK+2  VOL DATA
CATCMRET LA    R1,CATWORK    RETURN WORK AREA ADDRESS            85147
         STM   R15,R1,RETR15  RETURN TO USER                     85147
         B     EXIT                                              85147
         SPACE 2                                                 85147
LOCNMPAT CAMLST NAME,1,,3    LOCATE, NO CVOL                     85147
         ORG   LOCNMPAT+4                                        85147
LOCNVPAT CAMLST NAME,1,2,3   LOCATE BY CVOL                      85147
         ORG   LOCNVPAT+4    SAVE SOME SPACE                     85147
         SPACE 1                                                 85147
FSAWORK  DSECT ,             DECLARE OUR WORK SPACE              85147
         ORG   DB                                                85147
CATCAM   DS    4F            CAMLST                              85147
CATDSN   DS    CL44          CAMLST DSN                          85147
CATCVOL  DS    CL6           CAMLST CVOL                         85147
CATWORK  DS    CL259         CAMLST WORK AREA 1/2                85147
CATWREST DS    CL6           CAMLST WORK AREA 2/2                85147
         ORG   ,                                                 85147
CTLCALL  DSECT ,             USER'S AREA                         85147
CATLDSN  DS    CL44          DSNAME TO BE LOCATED                85147
CATVTYPE DS    XL4           VOL TYPE                            85147
CATVSER  DS    CL6           VOL SER                             85147
*        FROM HERE ON, DOUBLES AS CONNECT CALL LIST              85321
CATCTYPE DS    XL4           CVOL TYPE                           85147
CATCSER  DS    CL6           CVOL SER                            85147
CATCIND  DS    CL8           CONNECT: INDEX                      85321
         SPACE 1                                                 85147
@SERVICE RSECT ,             RESUME                             GP99026
         SPACE 1                                                 85321
*        CONNECT INDEX ENTRY.  R1 => XL4'TYPE', CL6'CVOL', CL8'INDX'
*        RETURN R15,R0 AS RECEIVED FROM CONNECT, OR              85321
*        R0=0, R15=12 WHEN TYPE IS HEX ZERO AND CVOL NOT UP.     85321
*                                                                85321
CCONENT  CLRL  CTGAREA,CTGALEN  CLEAR WORK AREA                  85321
         MVI   CTPIND,C' '   PREPARE TO BLANK                    85321
         MVC   CTPIND+1(3*44-1),CTPIND  BLANK ALL NAMES          85321
         MVI   CTGOPTN1,CTGNAME+CTGCNAME  CAT BY NAME, NOT ACB   85321
         MVI   CTGOPTN3,CTGCMS+CTGAMO  NEW STYLE                 85321
         LA    R1,CTPIND     MAKE POINTER                        85321
         ST    R1,CTGFVENT     TO INDEX NAME                     85321
         LA    R1,CTGFL      POINTER TO                          85321
         ST    R1,CTGFVCRE     CREATION DATE FPL                 85321
         LA    R1,CTPCAT     POINTER TO                          85321
         ST    R1,CTGFVNAM     CATALOG NAME (CVOL)               85321
         MVI   CTGFLDNO,1    ONE ENTRY IN FPL                    85321
         LA    R1,CTPDCDAT   NAME OF FIELD                       85321
         ST    R1,CTGFLDNM     POINTER                           85321
         MVI   CTGFLNG+L'CTGFLNG-1,L'CTPDATE   LENGTH OF DATA    85321
         LA    R1,CTPDATE    POINTER                             85321
         ST    R1,CTGFLPT      TO DATE                           85321
         L     R14,CTPCAMP   GET CAMLIST PATTERN                 85321
         LA    R15,CTPCAT    GET CATALOG NAME                    85321
         SR    R0,R0         CLEAR CVOL                          85321
         LA    R1,CTPVOL     CVOL TYPE/SERIAL FIELD              85321
         STM   R14,R1,CTPCAM   BUILD CAMLIST                     85321
         USING CATCTYPE,R6   DECLARE IT                          85321
         MVC   CTPCAT(9),=C'SYSCTLG.V'  CVOL CATALOG NAME        85321
         MVC   CTPCAT+9(6),CATCSER  MAKE SYSCTLG.V DSN           85321
         MVC   CTPVOL(10),CATCTYPE  PASS CALLER'S TYPE/SERIAL    85321
         CLI   CTPVOL+2,UCB3DACC  DASD REQUEST ?                 85321
         BE    CCONDASD      YES                                 85321
         LA    R7,CATCSER    ELSE POINT TO VOLSER                85321
         BAS   R10,VSNUCB    LOOK FOR IT                         85321
         B     CCONBAD       NOT UP                              85321
         MVC   CTPVOL(4),UCBTBYT1-UCBOB(R3)  MOVE RESULT         85321
         NI    CTPVOL+1,255-UCBRR-UCBVLPWR-UCBDVPWR  FIX TYPE    85321
         CLC   =X'200A',CTPVOL+2   3340 ?                        85321
         BNE   *+8           NO                                  85321
         NI    CTPVOL+1,255-UCBRPS  LOW VISCOSITY (NO CONSISTENCY)
CCONDASD LA    R14,CTPIND    GET OUTPUT INDEX                    85321
         LA    R15,CATCIND   GET USER'S INDEX (OR DSN)           85321
         LA    R0,8          MAXIMUM TO TEST                     85321
CCONINDL CLI   0(R15),C' '   END OF NAME ?                       85321
         BNH   CCONINDX      YES                                 85321
         CLI   0(R15),C'.'   INDEX END ?                         85321
         BE    CCONINDX      YES                                 85321
         MVC   0(1,R14),0(R15)  MOVE ONE BYTE                    85321
         LA    R14,1(,R14)                                       85321
         LA    R15,1(,R15)                                       85321
         BCT   R0,CCONINDL   TRY AGAIN                           85321
CCONINDX L     R2,CVTPTR     GET THE CVT                         85321
         USING CVTMAP,R2     DECLARE IT                          85321
         MVC   CTPDATE,CVTDATE  SAVE THE DATE                   GP99027
         L     R2,CVTCBSP    GET THE AMCBS                       85321
         USING AMCBS,R2                                          85321
         LA    R1,SPCAXCN-(CAXCHN-CAXID)  POINT TO CHAIN HEAD    85321
         DROP  R2                                               GP97241
         USING CAXID,R1      DECLARE CAX ENTRY                   85321
CCONCVLP ICM   R1,15,CAXCHN  GET AN ENTRY                       GP99026
         BNZ   CCONCVMS      CHECK FOR MASTER                    85321
CCONBAD  MVI   RC,12         SET ERROR CODE                      85321
         B     EXIT          AND QUIT                            85321
CCONCVMS TM    CAXFLGS,CAXMCT  MASTER CATALOG ENTRY ?            85321
         BZ    CCONCVLP      NO; TRY AGAIN                       85321
         MVC   CTPMCAT,CAXCNAM  SAVE VSAM CATALOG NAME           85321
         DROP  R1,R6                                            GP97241
         LA    R0,CTGFV                                          85321
         ST    R0,CTGFVT     SET VECTOR TABLE ADDRESS            85321
         LA    R0,CTGWORK                                        85321
         ST    R0,CTGWKA     UNNECESSARY WORK AREA ?             85321
         LA    R0,CTGWORKL   IDCAMS SIZE OF WORK AREA            85321
         STH   R0,CTGWORK    SET SIZE                            85321
         MVI   CTGOPTNS,CTGDEFIN  DEFINE A CVOL                  85321
         MVI   CTGFVTYP,C'X'  ALIAS REQUESTED                    85321
         CATALOG CTPCAM      BUILD 'DSN' FOR SYSCTLG.V...        85321
*        IGNORE RETURN CODE - MAY ALREADY EXIST                  85321
         OI    CTGOPTN1,CTGBYPSS  BYPASS PASSWORD ON MASTER      85321
         CATALOG CTGPL       PROCESS IT                          85321
         STM   R15,R0,RETR15  PASS REGISTERS BACK TO USER        85321
         B     EXIT          AND RETURN                          85321
         SPACE 2                                                 85321
CTPCAMP  CAMLST CAT,1,,3     CVOL CATALOG DSN CATALOG REQUEST PATTERN
         ORG   CTPCAMP+4     LEAVE ONLY FLAGS                    85321
CTPDCDAT DC    CL8'DSETCRDT'   NAME OF DATE FIELD                85321
         SPACE 1                                                 85321
FSAWORK  DSECT ,                                                 85321
         ORG   DB            RE-USE                              85321
CTGAREA  DS    0X            START OF AREA TO CLEAR              85321
CTPIND   DC    CL44' '       INDEX TO BE CONNECTED               85321
CTPCAT   DC    CL44'SYSCTLG.V------'  CVOL 'DSN'                 85321
CTPMCAT  DC    CL44'CATALOG.V------'  MASTER CATALOG NAME        85321
CTPDATE  DC    PL4'0'        CVTDATE                            GP99027
CTPVOL   DC    XL4'0',CL6' '   TYPE/CVOL SERIAL                  85321
CTPCAM   CAMLST CAT,CTPCAT,,CTPVOL  BUILD 'DSN' SYSCTLG.V..      85321
CTGPL    DS    0F            VSAM 'CAMLST' EXPANSION             85321
CTGOPTN1 DC    X'06'         FIRST OPTION BYTE                   85321
CTGBYPSS EQU   X'80'           BYPASS MASTER PASSWORD CHECK      85321
CTGNAME  EQU   X'04'           ENTRY BY NAME RATHER THAN ID      85321
CTGCNAME EQU   X'02'           CATALOG BY NAME RATHE THAN ACB    85321
CTGOPTN2 DC    X'00'         SECOND OPTION BYTE                  85321
CTGOPTN3 DC    X'81'         THIRD OPTION BYTE                   85321
CTGCMS   EQU   X'80'           CATALOG MANAGEMENT REQUEST        85321
CTGAMO   EQU   X'01'           CTGPL, NOT CAMLST EXPANSION       85321
CTGOPTN4 DC    X'00'         FOURTH OPTION BYTE                  85321
CTGFVT   DS    A             POINTER TO FIELD VECTOR TABLE (DEFINE)
CTGCAT   DS    A             POINTER TO CATALOG DSN (VSAM/MASTER)
CTGWKA   DS    A             WORK AREA ?                         85321
CTGOPTNS DS    X'00'         REQUEST TYPE                        85321
CTGDEFIN EQU   X'08'           DEFINE                            85321
         DC    3X'00',4F'0'  NOT USED HERE                       85321
CTGFV    DS    0F            FIELD VECTOR TABLE                  85321
CTGFVTYP DC    C'X'          ADD AN ALIAS                        85321
CTGFVPRO DS    X                                                 85321
CTGFVELM DS    2X                                                85321
CTGFVDCH DS    A                                                 85321
CTGFVICH DS    A                                                 85321
CTGFVVCH DS    A                                                 85321
CTGFVIND DS    A                                                 85321
CTGFVENT DS    A(CTPIND)     POINTER TO INDEX TO BE CONNECTED    85321
CTGFVSTY DS    A                                                 85321
CTGFVOWN DS    A                                                 85321
CTGFVEXP DS    A                                                 85321
CTGFVCRE DS    A(CTGFL)      FPL FOR CREATION DATE               85321
CTGFVVLT DS    A                                                 85321
CTGFVRNG DS    A                                                 85321
CTGFVDVT DS    A                                                 85321
CTGFVSPC DS    A                                                 85321
CTGFVAMD DS    A                                                 85321
CTGFVATR DS    A                                                 85321
CTGFVBUF DS    A                                                 85321
CTGFVLRS DS    A                                                 85321
CTGFVLMT DS    A                                                 85321
CTGFVGAT DS    A                                                 85321
CTGFVNAM DC    A(CTPCAT)     CATALOG NAME (SYSCTLG.V......)      85321
CTGFVPWD DS    A                                                 85321
CTGFVWKA DS    A                                                 85321
CTGFL    DS    0F            FIELD PARAMETER LIST                85321
CTGFLDNO DC    X'01'           NUMBER OF ENTRIES                 85321
CTGFLDCD DS    X               NO TEST                           85321
CTGFLDGC DS    X               NO TYPE GROUP CODE                85321
CTGFLDRE DS    X               NO TEST/NO RETURN CODE            85321
CTGFLDWA DS    A               NO WORK AREA                      85321
CTGFLDNM DS    A(CTPDCDAT)   POINTER TO NAME OF FIELD            85321
CTGFLCHN DS    A               NO POINTER                        85321
CTGFLNG  DC    F'4'          LENGTH OF DATA                     GP99027
CTGFLPT  DC    A(CTPDATE)    COPY OF CVTDATE                    GP99027
         SPACE 1                                                 85321
CTGWORK  DC    A(0)          LENGTH FIELD(S)                     85321
         DS    16XL16'0'     VSAM WORK AREA (?)                  85321
CTGWORKL EQU   *-CTGWORK     LENGTH OF WORK AREA                 85321
CTGSIZE  EQU   *-CTGPL         SIZE TO BE CLEARED                85321
CTGALEN  EQU   *-CTGAREA     LENGTH TO CLEAR                     85321
         SPACE 1                                                 85321
IGGCAXWA DSECT ,             HAND-EXPANSION OF PL/S MAPPING      85321
CAXID    DS    F                                                 85321
CAXCHN   DS    A             FORWARD LINK                        85321
CAXFLGS  DS    X             ENTRY FLAGS                         85321
CAXMCT   EQU   X'04'           MASTER CATALOG                    85321
         DS    43X             CRUD                              85321
CAXCNAM  DS    CL44          CATALOG NAME (CATALOG.V......)      85321
         SPACE 1                                                 85321
AMCBS    DS    5F            CRUDE                               85321
SPCAXCN  DS    F             MVS/SP CAX CHAIN POINTER            85321
@SERVICE RSECT ,             RESUME                             GP99026
         SPACE 1                                                 90217
         LTORG ,                                                 90217
         TITLE '@ S E R V I C E  ***  DATASET VALIDITY CHECKS'   81359
GROUP2   BASR  R11,0         MAKE LOCAL BASE                     85203
         USING *,R11                                             85203
GRUP2BAS LTR   R7,R7         ANY PARM ?                          85203
         BNZ   GROUP2NZ      YES; ACCEPT                         85203
         CH    R0,=Y(VENWYLOC)  MULTI-PACK LOCATE ?              85203
         BL    BADENTRY      NO; BAD REQUEST                     85203
GROUP2NZ CH    R0,=Y(VENWCOMP)  DSN TYPE ENTRY ?                 85203
         BE    GROUP2A       NO                                  81151
         CLI   CALLB0,0      LENGTH SUPPLIED ?                  GP99026
         BE    GROUP2A       NO                                  81151
         CLI   CALLB0,44+6   OVER MAX ?                         GP99026
         BNL   GROUP2A       YES; LEAVE                          81151
         SR    R14,R14                                           81151
         IC    R14,CALLB0    GET MOVE LENGTH                    GP99026
         BCTR  R14,0         SET FOR EXECUTE                     81151
         MVI   COPYDSN,C' '                                      81151
         MVC   COPYDSN+1(49),COPYDSN  BLANK                      81151
         EX    R14,COPYDMVC  MAKE FULL 50 BYTES (VSN/DSN)        81151
         LA    R7,COPYDSN    POINT TO NEW AREA                   81151
GROUP2A  BIX   ERR=BADENTRY,PFX=ENT,BASE=@SERVICE,                     *
               LOC=(TSTDS,DSLIB,DSWYL,DSTSO,VSNFG,WCOMP,ALLOC,WYLOC)
         SPACE 2
*        ENTRY CODE 32 - DATASET NAME CHECK
*        ENTRY R1 POINTS TO DSN OF LENGTH 44, UNLESS LENGTH IN HIGH
*          BYTE OF R1.                                           81151
*        RETURNS: R15 - 16 - R1 ADDRESS INVALID                  81151
*        R15 - 8 - INVALID NAME, REASON IN R0 (VR FLAGS)         81151
*        R15 - 4 - UNQUALIFIED NAME (E.G. PASSWORD, SYSCTLG)     81151
*        R15 - 0 - VALID NAME (R0=0 UNLESS INDEX IS WYLBUR, THEN 81151
*              R0=VRNWYL)                                        81151
         SPACE 1
ENTTSTDS IC    R0,CALLMODE   GET THE MODE                        81165
         BIX   ERR=BADENTRY,PFX=ENT,BASE=@SERVICE,                     *
               LOC=(DSTST,DSCAT,DSABB,DSDS1,DSDJ1,DSFMT,DSMEM,RJFCB,   *
               PDSDE,DSDS4,DSDJ4,DDCLR)                          90217
         SPACE 1                                                 81165
ENTDSTST LA    R10,DSNCHEKZ  POST-CHECK CHECK                    81151
         USING DSCB1,R7      FAKE IT                             81137
         SPACE 1
CHECKDSN LA    R5,L'DS1DSNAM-1  SET LENGTH-1 FOR TRT CODE        81347
CHECKDSM LR    R6,R7         COPY ADDRESS OF DSN (OR MEMBER)     81347
         L     R9,=A(CMPNBTRT)  ASSUME BASE FOR TRT              81215
*HLASM*  USING CMPNBTRT,R9   SET FOR TRT TABLE                   81215
         LA    R14,1         SET CONSTANT                        81151
         LA    R15,8         DITTO                               81151
         SR    R0,R0         CLEAR INDEX LEVEL COUNTER           81151
         SR    R1,R1         CLEAR HIGH BYTE FOR TRT INSERTION   81165
         STM   R6,R7,DB      SAVE ADDRESS OF DATASET NAME        81165
         STC   R15,DB        SET PRELIMINARY LENGTH              81165
         STC   R15,DB+4      DITTO                               81165
DSNCHKIX LTR   R5,R5         ANY MORE TO DO ?                    81151
         BMR   R10           NO; NAME IS VALID                   81151
         CLI   0(R6),C'0'    NUMERIC AT START OF INDEX LEVEL ?   81151
         BNL   BADOSNAM      YES; ERROR                          81151
         SR    R2,R2         PRE-LOAD R2 IN CASE ALL ALPHA       81151
         EX    R5,DSCTRTNB   SCAN FOR SPECIAL CHARACTERS         81151
         BNE   *+8           FOUND                               81151
         LA    R1,1(R5,R6)   NONE FOUND; SET END ADDRESS         81151
         SR    R1,R6         NUMBER OF BYTES SCANNED             81151
         BNP   BADOSNAM      NULL INDEX LEVEL ?                  81151
         SR    R5,R1         LENGTH LEFT TO DO +1                81151
         SR    R5,R14        LENGTH LEFT (-1 FOR TRT EX)         81151
         LA    R6,1(R1,R6)   NEXT STRING TO BE CHECKED           81151
         CR    R1,R15        MORE THAN 8 ?                       81151
         BH    BADOSNAM      YES; FAIL                           81151
         STC   R1,DB+4       SET LENGTH OF CURRENT INDEX LEVEL   81165
         B     *+4(R2)       BRANCH ACCORDING TO CHARACTER TYPE  81151
         B     0(,R10)       ALL ALPHA - RETURN OK               81151
         B     BADCHAR       INVALID CHARACTER FOR OS NAME       81151
         B     DSCINDX       PERIOD - CHECK RESIDUAL LENGTH      81151
         LTR   R5,R5         BLANK FOUND; ANY MORE ?             81151
         BMR   R10           NO; VALID                           81151
         EX    R5,DSCTRTBL   SEE IF REMAINDER BLANK              81215
         BER   R10           YES; NAME OK                        81151
         B     BADOSNAM      EMBEDDED BLANKS IN NAME             81151
DSCINDX  LTR   R5,R5         ALL CHECKED ?                       81151
         BM    BADOSNAM      YES; PERIOD MAY NOT BE LAST         81151
         AR    R0,R14        UP COUNT OF INDEX LEVELS            81151
         MVC   DB(4),DB+4    SAME ADDRESS/LENGTH OF PRIOR INDEX  81165
         ST    R6,DB+4       SAVE START ADDRESS OF THIS ONE      81165
         STC   R15,DB+4      SAVE PROVISIONAL LENGTH             81165
         B     DSNCHKIX      AND CHECK NEXT LEVEL                81151
         PUSH  USING                                            GP97227
         DROP  ,                                                GP97227
         USING CMPNBTRT,R9   SET FOR TRT TABLE                  GP97227
DSCTRTNB TRT   0(0,R6),DSCNBTRT  FIND SPECIAL CHARACTERS         81151
DSCTRTBL TRT   0(0,R6),CMPNBTRT  TRT FOR NON-BLANK               81215
         POP   USING                                            GP97227
         SPACE 1
DSNCHEKZ LTR   R0,R0         ANY INDEX LEVELS AT ALL ?
         BNP   SHORTOSN      NO; NOT ALLOWED                     81151
         AIF   (NOT &NOLO).N2L0                                  81151
         BAS   R10,WYLINDEX  IS THIS A WYLBUR INDEX ?            81151
         B     EXIT          NO
         MVI   RV,VRNWYL     SET NOT WYLBUR FOR WYLBUR CHECK
.N2L0    B     EXIT          BUT TAKE NORMAL EXIT                81151
         SPACE 2
*        ENTRY CODE 32/MODE=1 - DATASET NAME CHECK FOR CATALOG ENTRY
*        CODE SAME AS ENTRY 32 (DSTST), BUT GDG NAME IS TESTED FOR
*          AND CONVERTED TO EBCDIC PRIOR TO CHECKING.            81151
         SPACE 1                                                 81151
ENTDSCAT MVC   COPYDSN,0(R7)   MOVE DSN TO WORK AREA             81151
         LA    R2,COPYDSN+35  POINT TO HIGHEST POSITION FOR GDG NAME
         LA    R3,34         SET COUNTER FOR MAX POSSIBILITIES
GDGSCAN  CLI   8(R2),C' '    TRAILING BLANK ?
         BNE   GDGTEST       NO; TEST FOR GDG NAME
         BCTR  R2,0          BACK-UP
         BCT   R3,GDGSCAN    TRY BEFORE
GDGTEST  CLI   0(R2),C'.'    POSSIBLE GDG NAME ?
         BNE   ENTDSTST      NO; USE NORMAL DSN PROCESSING
         MVC   DB,=C'G0000V00'  MAKE GDG MASK
         NC    DB,1(R2)      MASK WITH DSN
         CLC   DB,=X'C700000000E5F0F0'  MATCH ?
         BNE   ENTDSTST      NO; DO NORMALLY
         XC    2(4,R2),=X'FFFFFFFF'  FLIP GENERATION COUNTER
         LA    R7,COPYDSN    POINT TO BOWDLERIZED VERSION
         B     ENTDSTST      AND TEST IT
COPYDMVC MVC   COPYDSN(0),0(R7)  MOVE TO WORK AREA               81151
         SPACE 2                                                 81165
*        ENTRY CODE 32/MODE=2  ABBREVIATED DATASET NAME          81165
*        FOR VALID DSN, RETURNS ADDRESS OF 8 BYTES;              81165
*          E.G. SYS1.PROCLIB RETURNS PROCLIB, ETC.               81165
*        FOR NAMES WITH MORE THAN TWO LEVELS, THE LAST (DSN)     81165
*          PORTION IS RETURNED, EXCEPT FOR SPECIFIC &LOCAL VALUES.
*        FOR AN INVALID NAME, R1 WILL POINT TO 'BAD DSN'         81165
         SPACE 1                                                 81165
ENTDSABB LA    R1,=CL8' BAD DSN'  PROVISIONALLY POINT TO ERROR   81165
         ST    R1,RETR1      SET R1 RETURN ADDRESS               81165
         AIF   (NOT &MVSESA).NOQUSIN                            GP97227
         MVC   COPYDSN,0(R7)  MOVE NAME                         GP97227
         LA    R14,COPYDSN+12                                   GP97227
         LA    R15,30        NOT TOO LONG FOR INSERTION         GP03237
DSABBQUE CLC   =C'.? ',0(R14)   IS IT A FUNNY JES INPUT FILE?   GP97227
         BNE   DSABBBMP      NO                                 GP97227
         MVC   1(5,R14),=C'SPOOL'  IDENTIFY AS A SPOOLED TYPE   GP03237
         LA    R7,COPYDSN    REPOINT                            GP97227
         B     DSABBCOM                                         GP97227
DSABBBMP LA    R14,1(,R14)   TRY NEXT                           GP97227
         BCT   R15,DSABBQUE                                     GP97227
DSABBCOM DS    0H                                               GP97227
.NOQUSIN BAS   R10,CHECKDSN  CHECK DATASET NAME                  81165
         MVC   DB+8(9),BLANKS  BLANK NAME                        81165
         CH    R0,=Y(2)      FEWER THAN TWO LEVELS ?             81165
         BL    DSALAST       YES; USE LAST FRAGMENT              81165
         CLC   =C'SYS',DS1DSNAM  POSSIBLE TEMPORARY NAME ?       81165
         BNE   DSANTEMP      NO                                  81165
         CLC   =C'.T',DS1DSNAM+8                                 81165
         BNE   DSANTEMP                                          81165
         MVC   DB+24(5),ZZEROES   EBCDIC ZEROES                  81165
         NC    DB+24(5),DS1DSNAM+3  FIVE NUMERICS AFTER SYS ?    81165
         CLC   DB+24(5),ZZEROES                                  81165
         BNE   DSANTEMP      NO                                  81165
         MVC   DB+24(6),ZZEROES                                  81165
         NC    DB+24(6),DS1DSNAM+10  NUMERIC TIME ?              81165
         CLC   DB+24(6),ZZEROES                                  81165
         BNE   DSANTEMP      NO                                  81165
         MVI   DB+8,C'&&'    SET FLAG FOR TEMP NAME              81165
*TEST*   B     DSALAST       USE LAST PORTION PREFIXED BY AMPERSAND
DSANTEMP EQU   *                                                 81165
         AIF   ('&LOCAL' EQ 'CNW').NO322NL                       83030
         AIF   (&LOCO OR '&LOCAL' EQ 'PID').NO322NC              92279
         CH    R0,=Y(3)      MORE THAN THREE LEVELS ?            81165
         BH    DSANLAST      YES; USE NEXT TO LAST LEVEL         81165
         AIF   ('&LOCAL' NE 'EPA').NO322NW                       83030
         AGO   .NO322NE                                          81165
.NO322NL CH    R0,=Y(2)      MORE THAN TWO LEVELS ?              83030
         BH    DSANLAST      YES; USE NEXT TO LAST               81165
.NO322NE BAS   R10,WYLINDEX  IS THIS A WYLBUR NAME ?             81165
         B     DSANLAST      NO; USE NEXT TO LAST                81165
         B     DSALAST       YES; SHORT NAME - USE LAST          81165
.NO322NW SPACE 1                                                 83030
DSANLAST LA    R2,DB         GET NEXT TO LAST                    81183
         SR    R15,R15       CLEAR FOR INSERT                   GP05105
         ICM   R15,7,1(R2)   GET TEXT ADDRESS                   GP05105
         CLC   =C'MVS',0(R15)  UID.SYS.FILE ?                   GP05105
         BE    DSALAST       YES?  USE LAST PORTION             GP05105
         CLC   =C'ESA',0(R15)  UID.SYS.FILE ?                   GP05105
         BE    DSALAST       YES?  USE LAST PORTION             GP05105
         CLC   =C'OS3',0(R15)  UID.SYS.FILE ?                   GP05105
         BE    DSALAST       YES?  USE LAST PORTION             GP05105
         CLC   =C'Z90',0(R15)  UID.SYS.FILE ?                   GP05105
         BE    DSALAST       YES?  USE LAST PORTION             GP05105
         B     DSALASTC      GO TO COMMON                        81165
.NO322NC ANOP  ,                                                 81165
DSALAST  LA    R2,DB+4       POINT TO LAST PORTION OF NAME       81165
DSALASTC SR    R14,R14                                           81165
         IC    R14,0(,R2)    GET LENGTH                          81165
         SR    R15,R15                                          GP99027
         ICM   R15,7,1(R2)   AND ADDRESS OF FRAGMENT            GP99027
         BCTR  R14,0         SET LENGTH FOR EXECUTE              81165
         EX    R14,DSABMVC   MOVE FRAGMENT                       81165
         LA    R1,DB+9       POINT TO USER PORTION               81165
         CLI   DB+8,C'&&'    TEMP NAME ?                         81165
         BNE   *+6           NO                                  81165
         BCTR  R1,0          BACKSPACE                           81165
         ST    R1,RETR1      RETURN ADDRESS TO USER              81165
         B     EXIT          AND GET OUT                         81165
DSABMVC  MVC   DB+9(0),0(R15)  MOVE DSN FRAGMENT                 81165
         SPACE 2                                                 81165
*        ENTRY CODE 32/MODE 3  OBTAIN DSCB 1 FOR VOL/DSN         81165
*                                                                81165
ENTDSDS1 LA    R2,6(,R7)     GET DSN ADDRESS                    GP03126
         LR    R0,R7         VOLUME SERIAL                       81165
         B     DSDS1CON                                         GP03126
         SPACE 1                                                 81200
*        ENTRY CODE 32/MODE 4   OBTAIN DSCB 1 FOR DSN/VOL FROM JFCB
*                                                                81200
*           MAY NOW BE USED AFTER RJFCB (JFCB SAVED OVER CALL)   94089
ENTDSDJ1 MVC   ALTJFCB,0(R7)  COPY JFCB TO SAFE AREA             94089
         LA    R2,ALTJFCB    POINT TO NEW AREA                   94089
DSDS1COM LA    R0,JFCBVOLS-INFMJFCB(,R2)  POINT TO FIRST VOLUME  94089
DSDS1CON LA    R1,DB+16      ADDRESS OF RETURN AREA              81200
         ST    R1,RETR1      RETURN TO USER                      81165
         L     R14,DSDS1OBT  GET FLAG BITS                       81165
         LR    R15,R2        SET DSN ADDRESS                     94089
         STM   R14,R1,DB     MAKE OBTAIN LIST                    81165
         OBTAIN DB           GET THE FORMAT 1 DSCB               81165
         CH    R15,=H'8'     GOOD RETURN ?                       86048
         BNE   DSDS1EXT      NO; LEAVE AS IS                     86048
         CLI   0(R2),X'04'   DSCB 4 REQUEST ?                    86048
         BNE   DSDS1EXT      NO; JUST RETURN                     86048
         CLI   DB+16,C'4'    DSCB 4 READ ?                       86048
         BNE   DSDS1EXT                                          86048
         SR    R15,R15       FAKE GOOD RETURN                    86048
DSDS1EXT ST    R15,RETR15    RETURN TO USER                      86048
         BR    R10                                               81165
DSDS1OBT CAMLST SEARCH,1,2,3   OBTAIN FORMAT 1 DSCB              81165
         ORG   DSDS1OBT+4    KEEP ONLY THE FLAG WORD             81165
         SPACE 1                                                 86040
*        OBTAIN FORMAT 4 DSCB                                    86040
ENTDSDS4 LR    R1,R7         COPY VOLSER ADDRESS                 86040
         B     DSDJ4COM      GO TO COMMON                        86040
ENTDSDJ4 LA    R1,JFCBVOLS-INFMJFCB(,R7)  POINT TO VOLSER        86040
DSDJ4COM LA    R2,ALTJFCB    POINT TO WORK FIELD                 94089
         MVI   0(R2),X'04'                                       94089
         MVC   1(L'COPYDSN-1,R2),0(R2)                           94089
         MVC   JFCBVOLS-INFMJFCB(6,R2),0(R1)  SAVE VOL-SER       94089
         B     DSDS1COM      DO OBTAIN                           86040
         SPACE 2                                                 81270
*        ENTRY CODE 32/MODE 5 - FORMAT DSORG/OPTCD/RECFM/LRECL   81270
*        INPUT R1 POINTS TO JFCB OR DSCB1 DSORG FIELD            81270
*        OUTPUT R1 POINTS TO WORK FIELD :                        81270
*        CL3 DSORG CL6 RECFM CL8 OPTCD CL5 BLKL CL5 LRECL        81270
ENTDSFMT LTR   R7,R7         ANY ?                               81270
         BZ    BADENTRY      NO; TOO BAD                         81270
         MVI   FMTDSORG,C' '                                     81270
         MVC   FMTDSORG+1(FMTORGLN-1),FMTDSORG  CLEAR RETURN AREA
         USING DS1DSORG,R7   USE COMMON DSCB1/JFCB MAPPING       81270
         SR    R0,R0                                             81270
         MVI   FMTLRECL+L'FMTLRECL-1,C'X'  PRESET FOR X          81270
         ICM   R0,3,DS1LRECL   GET THE RECORD LENGTH             81270
         BM    DSFMTRLN      SKIP LRECL=X                        81270
         CVD   R0,DB         MAKE PACKED                         81270
         MVC   FMTLRECL(5),=X'2020202120'  MAKE EDIT MASK        81270
         ED    FMTLRECL-1(6),DB+5  EDIT RECORD LENGTH            81270
DSFMTRLN ICM   R0,3,DS1BLKL  GET BLOCK SIZE                      81270
         CVD   R0,DB                                             81270
         MVC   FMTBLKL(5),=X'2020202120'  MAKE EDIT MASK         81270
         ED    FMTBLKL-1(6),DB+5                                 81270
         LA    R1,PATDSORG   POINT TO LIST OF DSORGS             81270
         LA    R2,16         LOOP THROUGH 16 BITS                81270
         SR    R3,R3                                             81270
         IC    R3,DS1DSORG   GET FIRST BYTE OF DSORG             81270
         ICM   R3,2,DS1DSORG+1  GET SECOND BYTE                  81270
         SLL   R3,15         ADJUST FOR BXLE                     81270
         OR    R3,R2         MAKE NON-ZERO LOW BYTE              81270
         LA    R4,FMTDSORG   SET OUTPUT FIELD                    81270
         MVI   FMTDSORG,C'?'   PRESET IF ALL BITS ARE OFF        81270
DSFMTOGL BXH   R3,R3,DSFMTOGN  SKIP IF BIT OFF                   81270
         MVC   0(2,R4),0(R1)   MOVE ABBREVIATION                 81270
         LA    R4,FMTDSORG+2   SET SECOND POSITION               81270
DSFMTOGN LA    R1,2(,R1)     SKIP TO NEXT ABBREVIATION           81270
         BCT   R2,DSFMTOGL   TRY AGAIN                           81270
         LA    R4,FMTRECFM   POINT TO OUTPUT FIELD               81270
         TM    DS1DSORG,JFCORGMQ  TCAM MESSY QUEUE ?             83100
         BZ    *+10          NO                                  83100
         MVC   DB+3(2),PATRECMQ  YES; ADJUST RECFM               83100
         MVI   FMTRECFM,C'?'  PRESET IF NOTHING FOUND            81270
         MVC   DB(6),PATRECFM+4  COPY MINOR OPTIONS              81270
         SR    R1,R1                                             81270
         IC    R1,DS1RECFM   GET RECORD FORMAT                   81270
         SRA   R1,6          ISOLATE MAJOR TYPE                  81270
         BP    DSFMTFMD      OK                                  81270
         MVI   DB,C'D'       CHANGE FOR POSSIBLE D FORMAT (?)    81270
         B     DSFMTFMB                                          81270
DSFMTFMD LA    R1,PATRECFM(R1)  POINT TO MAJOR TYPE              81270
         MVC   0(1,R4),0(R1)  MOVE IT                            81270
         LA    R4,1(,R4)     UP OUTPUT ADDRESS                   81270
DSFMTFMB IC    R1,DS1RECFM   GET FULL FORMAT AGAIN               81270
         LA    R2,5          SET FOR 5 ADDITIONAL OPTIONS        81270
         LA    R3,DB         POINT TO ABBREVIATIONS              81270
         SLL   R1,25         ADJUST FOR BXLE                     81270
         OR    R1,R2         MAKE LOW NON-ZERO                   81270
DSFMTFML BXH   R1,R1,DSFMTFMN                                    81270
         MVC   0(1,R4),0(R3)  MOVE ABBREVIATION                  81270
         LA    R4,1(,R4)                                         81270
DSFMTFMN LA    R3,1(,R3)                                         81270
         BCT   R2,DSFMTFML   DO ALL                              81270
         LA    R3,PATOPTPS   SET QSAM/BSAM COMMON                81270
         TM    DS1DSORG+1,0  FUNNY ?                            GP09186
         BNZ   DSFMTEX       YES; DON'T HAVE INFO               GP09186
         TM    DS1DSORG,JFCORGPO+JFCORGPS  PS/PO ?               81270
         BNZ   DSFMTOPT      YES; USE IT                         81270
         LA    R3,PATOPTDA   SET DA                              81270
         TM    DS1DSORG,JFCORGDA  IS IT BDAM ?                   81270
         BNZ   DSFMTOPT      YES; USE IT                         81270
         LA    R3,PATOPTIS   SET ISAM                            81270
         TM    DS1DSORG,JFCORGIS  IS IT ISAM ?                   81270
         BZ    DSFMTEX       NO; DO COMMON EXIT CODE             81270
DSFMTOPT LA    R4,FMTOPTCD   POINT TO OUTPUT FIELD               81270
         SR    R1,R1                                             81270
         IC    R1,DS1OPTCD   GET OPTIONS                         81270
         LA    R2,8          SET FOR EIGHT OPTIONS               81270
         SLL   R1,23         ADJUST FOR BXLE                     81270
         OR    R1,R2         MAKE LOW NON-ZERO                   81270
DSFMTOPL BXH   R1,R1,DSFMTOPN                                    81270
         MVC   0(1,R4),0(R3)  MOVE ABBREVIATION                  81270
         LA    R4,1(,R4)                                         81270
DSFMTOPN LA    R3,1(,R3)                                         81270
         BCT   R2,DSFMTOPL   DO ALL                              81270
DSFMTEX  LA    R1,FMTDSORG   POINT TO START OF FIELDS            81270
         ST    R1,RETR1      RETURN TO USER                      81270
         B     EXIT          RETURN OK                           81270
PATRECMQ DC    C'GR'         TCAM MESSAGE QUEUE                  83100
PATDSORG DC    C'GSTXTQ? AMTR? ? ISPSDACXCQMQPOU '               81270
PATRECFM DC    C' VFUTBSAM '                                     81270
PATOPTPS DC    C'WUCHQZTJ'   PS/PO COMMON OPTCD                  81270
PATOPTDA DC    C'WOEFA84R'   DA OPTCD - 2 RESERVED               81270
PATOPTIS DC    C'WUMIY4LR'   IS OPTCD - 1 RESERVED, U NOT USED   81270
         SPACE 2                                                 81347
*        ENTRY CODE 32/MODE 6 - MEMBER NAME VALIDITY CHECK       81347
*        INPUT R1 POINTS TO MEMBER NAME                          81347
*        OUTPUT R15: 0 IF VALID; ELSE R15 NOT 0, R0 REASON CODE  81347
*                                                                81347
ENTDSMEM LA    R5,8-1        SET LENGTH MINUS 1 FOR TRT          81347
         CLI   7(R7),X'C0'   POSSIBLE SVC NAME ?                 81347
         BNE   DSMEMCL       NO                                  81347
         CLI   6(R7),C'$'    NON-BLANK PRIOR ?                   81347
         BL    BADOSNAM      NO; BLANK OR GARBAGE - FAIL         81347
         BCTR  R5,0          DON'T CHECK LAST BYTE               81347
DSMEMCL  BAS   R10,CHECKDSM  CHECK NAME WITH R5 LENGTH PRESET    81347
         LTR   R0,R0         ANY PERIODS IN NAME ?               81347
         BZ    EXIT          NO; GOOD NAME                       81347
         B     BADOSNAM      TOO BAD                             81347
         SPACE 2                                                 81359
*        ENTRY CODE 32/MODE 7 - READ JFCB FOR SPECIFIED DDNAME   81359
*        INPUT R1 POINTS TO DDNAME                               81359
*        OUTPUT R15 - RETURNED FROM RDJFCB MACRO                 81359
*              R1 - JFCB ADDRESS IF R15=0                        81359
ENTRJFCB MVC   RDDUMDCB(PATDCBL),PATDCB  MAKE PHONY DCB          90217
ENTRJFCC LA    R2,RDDUMDCB                                       90217
         LA    R3,RDJFCB     POINT TO NEW JFCB                   81359
         STM   R2,R3,RDOPLIST  MAKE LISTS                        81359
         MVI   RDOPLIST,X'80'  SET END OF LIST BIT FOR DCBS      81359
         MVI   RDEXLIST,X'87'  SET JFCB EXIT ADDRESS             81359
         LA    R2,RDEXLIST                                       81359
         STCM  R2,7,RDDUMDCB+DCBEXLSA-IHADCB  SET EXIT LIST ADDRESS
         MVC   DCBDDNAM-IHADCB+RDDUMDCB,0(R7)  COPY USER'S DDNAME
         RDJFCB MF=(E,RDOPLIST)  READ THE JFCB                   81359
         ST    R15,RETR15    SET THE RETURN CODE                 81359
         BXH   R15,R15,EXIT  EXIT IF NOT READ                    81359
         ST    R3,RETR1      ELSE RETURN THE ADDRESS             81359
         BR    R10           AND RETURN                          90217
         SPACE 1                                                 81359
PATDCB   DCB   DDNAME=ANY,MACRF=(E),DSORG=PS,EXLST=2             81359
PATDCBL  EQU   *-PATDCB                                          90217
         SPACE 2                                                 82123
*        ENTRY CODE 32/MODE 8 - DIRECTORY ENTRY VALIDITY CHECKING
*                                                                82123
         USING SERVPDS,R7    DECLARE USER'S AREA                 82123
ENTPDSDE ICM   R3,15,PDDNEXT   GET ADDRESS OF MEMBER             82123
         BZ    BADENTRY      BOMB IF BAD                         82123
         LA    R5,DB+16                                          82123
         TM    CALLOPT,X'80'  RETURN OPTIONAL DATA ?            GP06287
         BZ    *+8           NO; JUST RETURN ADDRESS IN R0      GP06287
         LA    R5,PDDNEXT+PDDCLRLN  ABUT TO BASIC AREA          GP06287
         USING SERVPDSR,R5   DECLARE OPTIONAL RETURN AREA        82123
         USING PDS2,R3       DECLARE MEMBER MAPPING              82123
         XC    PDDTYPE(PDDCLRLN),PDDTYPE  CLEAR RETURN AREA      82123
         MVI   PDDRALIS,C' '                                     82123
         MVC   PDDRALIS+1(PDDRBLNK-1),PDDRALIS  BLANK EXTENDED RETURN
         IC    R4,PDS2INDC   GET FLAG/LENGTH                     82123
         N     R4,=A(PDS2LUSR)  GET LENGTH ONLY                  82123
         STC   R4,PDDUDLEN   SET USER LENGTH IN HALF-WORDS       82123
         LA    R4,12(R4,R4)  GET MEMBER ENTRY LENGTH             82123
         ST    R4,PDDINCR    SET INCREMENT FOR BXLE              82123
         MVC   PDDRNAME,PDS2NAME  COPY MEMBER NAME               82123
         CLI   PDDRNAME+7,X'C0'  POSSIBLE SVC NAME ?             82123
         BNE   PDENSVC       NO                                  82123
         CLC   =C'IGC',PDDRNAME                                  82123
         BNE   PDENSVC       NO                                  82123
         MVI   PDDRNAME+7,C'+'   MAKE PRINTABLE                  82123
PDENSVC  TM    PDS2INDC,PDS2ALIS  ALIAS ?                        82123
         BZ    *+8           NO                                  82123
         MVI   PDDRALIS,C'*' FLAG AS ALIAS                       82123
         SPACE 1                                                 82123
*        CHECK IF THE ENTRY IS IN SPF FORMAT, AND RECFM IS F OR V
*                                                                82123
PDETSPF  LA    R9,PDETDTX    SET FAST EXIT                       82123
         CLI   PDDUDLEN,SPFUDLEN    SPF LENGTH ?                 82123
         BNER  R9            NO                                  82123
         CLI   SPFFLGS,C'W'  SSI WYLBUR ?                       GP04234
         BE    PDETSPF2      YES; RECFM=U IS VALID               91270
         TM    PDDRECFM,JFCUND  RECFM=F OR V ?                   82123
         BNMR  R9            NEITHER                             82123
PDETSPF2 TM    SPFMODDT,254  POSSIBLE DATE FIELD ? 01YYDDDF
         BNZR  R9            NO                                 GP99347
         TM    SPFMODDT+3,X'0F'  PLUS ?                          82123
         BNOR  R9            NO                                  82123
         TM    SPFCRTDT+3,X'0F'                                  82123
         BNOR  R9                                                82123
         CLC   SPFCRTDT,SPFMODDT  VALID RANGE ?                  82123
         BHR   R9            NO                                  82123
*        ADD TEST TO CHECK FOR EBCDIC SPFUID ?                   82123
         UNPK  DB,SPFCRTDT   CHECK DATE FOR NUMERICS             82123
*WHY*    TR    DB,NUMTAB-C'0'     MAKE EBCDIC                    92297
         NC    DB,ZZEROES                                        82123
         CLC   DB,ZZEROES                                        82123
         BNER  R9            NO; FAIL                            82123
         UNPK  DB(5),SPFMODTM(3)                                 82123
*WHY*    TR    DB(4),NUMTAB-C'0'  MAKE EBCDIC                    92297
         NC    DB(4),ZZEROES                                     82123
         CLC   DB(4),ZZEROES                                     82123
         BNER  R9                                                82123
         UNPK  DB,SPFMODDT   CHECK MOD DATE                      82123
*WHY*    TR    DB,NUMTAB-C'0'     MAKE EBCDIC                    92297
         NC    DB,ZZEROES                                        82123
         CLC   DB,ZZEROES                                        82123
         BNER  R9            DITTO                               82123
         MVI   PDDTYPE,PDDTSPF  SET SPF ENTRY TYPE               82123
         LA    R1,SPFMODDT   USE CHANGE DATE AS PSEUDO-SSI       91270
         BAS   R9,PDESSIUS   DO SSI                              91270
         UNPK  PDDRDATE,SPFMODDT  UNPACK THE MODIFICATION DATE   82123
*WHY*    TR    PDDRDATE,NUMTAB-C'0'  MAKE EBCDIC                 92297
         MVC   PDDRMAIN,SPFUID  SET THE USER ID                  82123
         B     PDEEXIT       RETURN OK                           82123
         SPACE 1                                                 82123
*        TEST FOR AN ENTRY PRODUCED BY IEBUPDTX.  NOTE THAT THE CURRENT
*        VERSION SUPPORTS RECFM=U (WYLBUR EDIT), HENCE NO RECFM TEST
*        IS MADE.                                                82123
*                                                                82123
PDETDTX  LA    R9,PDETLKED   SET FAST EXIT                       82123
         CLI   PDDUDLEN,DTXUDLEN       MINIMUM DTX LENGTH ?      82123
         BE    PDECDTX       YES; CHECK CONTENTS                 82123
         CLI   PDDUDLEN,DTXUDLTM      ALTERNATE LENGTH ?         82123
         BNER  R9            NO; SKIP                            82123
PDECDTX  CLC   PDS2NAME,DTXMODNM  PRODUCTION ALIAS ?             82123
         BE    PDEMDTX       YES; ACCEPT IT                      82123
         CLC   PDS2NAME(2),DTXID  LIB. ID MATCH ?                82123
         BNER  R9            NO; SKIP                            82123
PDEMDTX  TM    DTXMODNO+L'DTXMODNO-1,X'0C'  PLUS SIGN ?          82123
         BNOR  R9            NO; SKIP                            82123
         TM    DTXVER+L'DTXVER-1,X'0C'                           82123
         BNOR  R9                                                82123
         MVI   PDDTYPE,PDDTDTX   HOPE FOR THE BEST               82123
         MVC   PDDRMAIN,DTXMODNM  RETURN MAIN MEMBER             82123
         BAS   R9,PDESSIUD   GET SSI                             82123
         CLI   PDDUDLEN,DTXUDLTM  TIME-STAMP PRESENT ?           82123
         BNE   PDEEXIT       NO; EXIT                            82123
         UNPK  DB,DTXTIMES                                       82123
         TR    DB+1(5),NUMTAB-C'0'                               92297
         MVC   PDDRDATE,DB+1  RETURN UPDATE DATE                 82123
         B     PDEEXIT       SKIP LINK-EDIT TEST                 82123
         SPACE 1                                                 82123
*        IF RECFM=U, TEST FOR LINK-EDIT ENTRY. IF NOT, MAY BE ANYTHING
*                                                                82123
PDETLKED LA    R9,PDENLKED   SET FAST EXIT                       82123
         TM    PDDRECFM,JFCUND  RECFM=U ?                        82123
         BNOR  R9            NO; NOT LKED ENTRY                  82123
         CLI   PDDUDLEN,11   MINIMUM LKED LENGTH ?               82123
         BLR   R9            NO                                  82123
         CLI   PDDUDLEN,23   LONGER THAN MAX ?                   82123
         BHR   R9            YES                                 82123
         MVC   DB(1),PDS2FTB1   COPY VS LKED FLAG                82123
         NI    DB,PDSAOSLE   LEAVE ONLY THE VS BIT               82123
         MVC   DB+1(1),PDDUDLEN  SET FLAG FOR TR                 82123
         LA    R14,PDELKTAB-11  TENTATIVELY SET OS LIST          82123
         TM    PDS2FTB1,PDSAOSLE+PDSAPFLG  VS + APF ?            82123
         BNO   PDESLKED      NO; USE OS PROCESSING               82123
         BCTR  R14,0         FOR VS + APF, SLIDE TABLE OVER      82123
         OI    DB,PDSAPFLG   TURN THE APF FLAG ON                82123
PDESLKED TR    DB+1(1),0(R14)  CONVERT LENGTH TO FLAGS           82123
         OC    DB+1(1),DB    OR THE VS AND APF BITS IN           82123
         CLI   DB+1,X'FF'    INVALID ENTRY ?                     82123
         BER   R9            YES                                 82123
         MVI   DB+5,PDS2SSI+PDSAPFLG+PDS2SCTR  MAKE MASK FOR VS  82123
         TM    PDS2FTB1,PDSAOSLE  VS LKED ?                      82123
         BNZ   *+8           YES                                 82123
         MVI   DB+5,PDS2SCTR   LEAVE ONLY TESTABLE BIT           82123
         MVC   DB+3(1),PDS2ATR  GET THE PRIMARY ATTRIBUTE BYTE   82123
         NI    DB+3,PDS2SCTR  ISOLATE IT                         82123
         MVC   DB+4(1),PDS2FTB1  GET OPTION BYTE                 82123
         OC    DB+4(1),DB+3  ADD ATTRIBUTE BIT                   82123
         NC    DB+4(1),DB+5  MASK WITH OS OR VS MASK             82123
         NC    DB+5(1),DB+1  MASK RETURN VALUE                   82123
         CLC   DB+4(1),DB+5  DO FLAGGED OPTIONS MATCH ?          82123
         BNER  R9            NO; RETURN NOT LKED                 82123
         MVC   PDDFLAGS,DB+1  SET FLAGS FOR OPTIONAL ENTRIES     82123
         MVI   PDDTYPE,PDDTLKED  SET LKED ENTRY                  82123
         L     R0,=C'2431'                                       90204
         TM    PDS2FTB2,X'10'  PDSLRMOD  RESIDENCE MODE ?        90204
         BZ    *+8                                               90204
         SLL   R0,16                                             90204
         STCM  R0,12,PDDRRMOD  SET RESIDENCE MODE                90204
         L     R0,=C'2431'                                       90204
         TM    PDS2INDC,PDS2ALIS  ALIAS ENTRY ?                  90204
         BNZ   PDESLKMA                                          90204
         TM    PDS2FTB2,X'03'  PDSMAMOD                          90204
         B     PDESLKMC                                          90204
PDESLKMA TM    PDS2FTB2,X'0C'  PDSAAMOD                          90204
PDESLKMC BZ    *+8                                               90204
         SLL   R0,16                                             90204
         BNO   *+8           SKIP IF BOTH BITS NOT ON            90204
         ICM   R0,12,=C'AN'  SET FOR AMODE=ANY                   90204
         STCM  R0,12,PDDRAMOD  ADDRESSING MODE                   90204
         TM    PDDFLAGS,PDS2SSI+PDSAPFLG+PDDFREAL SSI/APF/MAIN ? 82123
         BZ    PDEEXIT       NO                                  82123
         LA    R0,1          MAKE CONSTANT                       82123
         LA    R4,PDSS01     POINT TO VARIABLE TEXT FIELD        82123
         TM    PDDFLAGS,PDS2SCTR  SCATTER DATA PRESENT ?         82123
         BZ    *+8           NO                                  82123
         LA    R4,PDSS01LN(,R4)  SKIP OVER IT                    82123
         TM    PDDFLAGS,PDDFREAL   MAIN MEMBER ENTRY NAME ?      82123
         BZ    PDEMLKED      NO                                  82123
         MVC   PDDRMAIN,PDS2MNM-PDSS02(R4)  COPY MAIN MEMBER NAME
         LA    R4,PDSS02LN(,R4) SKIP OVER IT                     82123
PDEMLKED TM    PDDFLAGS,PDS2SSI  SSI DATA PRESENT ?              82123
         BZ    PDEILKED      NO; SKIP OVER                       82123
         AR    R4,R0                                             82123
         OR    R4,R0                                             82123
         SLR   R4,R0         ROUND TO HALF-WORD BOUNDARY         82123
         LR    R14,R4        COPY SSI ADDRESS                    82123
         SLR   R14,R3        GET SSI OFFSET                      82123
         STH   R14,PDDOSSI   SAVE SSI OFFSET                     82123
         BAS   R9,PDESSI     FORMAT SSI                          82123
         LA    R4,PDSS03LN(,R4)  SKIP SSI                        82123
PDEILKED TM    PDDFLAGS,PDDFAPF  APF DATA PRESENT ?              82123
         BZ    PDEEXIT       NO                                  82123
         LR    R14,R4                                            82123
         SLR   R14,R3        GET OFFSET TO ENTRY                 82123
         STH   R14,PDDOAPF   SAVE OFFSET                         82123
         MVC   PDDRAPF,=C'AC=0'  MAKE PATTERN                    82123
         MVN   PDDRAPF+3(1),1(R4)  COPY APF CODE                 82123
         B     PDEEXIT       AND RETURN                          82123
         SPACE 2                                                 82123
*        PROCESS UNIDENTIFIED ENTRIES                            82123
*                                                                82123
PDENLKED CLI   PDDUDLEN,2    LONG ENOUGH FOR SSI ?               82123
         BL    PDEEXIT       NO; RETURN                          82123
         BAS   R9,PDESSIUD   DO SSI IN USERDATA FIELD            82123
         SPACE 1                                                 82123
PDEEXIT  LA    R1,PDDRALIS   SET RETURN ADDRESS                  82123
         ST    R1,RETR0      INTO USER'S RETURN                  82123
         B     EXIT          AND EXIT OK                         82123
         SPACE 1                                                 82123
PDESSIUD LA    R1,PDS2USRD   POINT TO USER DATA FIELD            82123
PDESSIUS SLR   R1,R3         GET OFFSET FROM START               82123
         STH   R1,PDDOSSI    SET OFFSET                          82123
         OI    PDDFLAGS,PDDFSSI  SET SSI PRESENT                 82123
         SPACE 1                                                 82123
PDESSI   LH    R1,PDDOSSI    GET OFFSET TO SSI                   82123
         AR    R1,R3         GET ADDRESS OF SSI                  82123
         UNPK  DB(9),0(5,R1)  UNPACK SSI+                        82123
         TR    DB,NUMTAB-C'0'     MAKE EBCDIC FROM HEX           92297
         MVC   PDDRSSI,DB    MOVE TO SSI RETURN FIELD            82123
         TM    3(R1),X'0F'   LOW ORDER SIGN ?                    82123
         BNO   PDESSINF      NO                                  82123
         MVC   DB+1(7),PDDRSSI  COPY LOW SEVEN BYTES ONLY        82123
         MVI   DB,C'0'       MAKE LEADING ZERO                   82123
         MVC   PDDRSSI,DB    MOVE TO SSI RETURN FIELD            82123
PDESSINF CLC   =C'019',DB    OLD YEARS?
         BE    PDESSINY      YES; ACCEPT IT
         CLC   =C'020',DB    NEW YEARS?
         BE    PDESSIND
         CLC   =C'001',DB    OLD NEW STYLE?
         BE    PDESSIND      TRY IT
         CLC   DB(3),ZZEROES  FORMAT 000YYDDD ?                  82123
         BNER  R9            NO; NO DATE                         82123
PDESSINY CLI   DB+3,C'7'     VALID DECADE ?                      82123
         BLR   R9            NO                                  82123
PDESSIND CLC   DB+5(3),ZZEROES  CHECK DAY > 0                    82137
         BNHR  R9            NO; FAIL                            82137
         CLC   DB+5(3),=C'366'  CHECK DAY <= 366                 82137
         BHR   R9                                                82137
         MVC   PDDRDATE,DB+3  MOVE PRESUMED DATE                 82123
         NC    DB,ZZEROES    CHECK FOR NUMERICS                  82123
         CLC   DB,ZZEROES    ALL NUMERIC ?                       82123
         BER   R9            YES; RETURN WITH DATE               82137
         MVC   PDDRDATE,BLANKS  ELSE BLANK DATE AGAIN            82123
         BR    R9            RETURN                              82123
         SPACE 2                                                 82123
*        LOAD-MODULE ATTRIBUTES BY LENGTH             NO AC     AC
         DC    AL1(255)                                         11
PDELKTAB DC    AL1(0,255,PDS2SSI,255)                 11-14     12-15
         DC    AL1(PDS2SCTR,PDDFREAL)                 15-16     16-17
         DC    AL1(PDS2SSI+PDS2SCTR,PDS2SSI+PDDFREAL) 17-18     18-19
         DC    AL1(255,PDS2SCTR+PDDFREAL,255)         19-21     20-22
         DC    AL1(PDS2SSI+PDS2SCTR+PDDFREAL,255)     22-23     23-24
         SPACE 2                                                 82123
         PRINT &PRTMAC                                           82123
         SERVPDS DSECT=YES,RETURN=DSECT,VER=1   PDSDE CALL PARAMETERS
         SPACE 1                                                 82123
         PRINT &PRTSYS                                           82123
         IHAPDS PDSBLDL=NO   EXPAND DIRECTORY MAPPING            82123
         SPACE 2                                                 82123
         PRINT &PRTMAC                                           82123
         MAPPDS ,            ADD LOCAL/SPECIAL EXPANSIONS        82123
@SERVICE RSECT ,                                                GP99026
         PRINT &PRTSOR                                           82123
         SPACE 2                                                 90217
*        ENTRY 32/MODE 11 - DDCLR                                90217
*        R1 POINTS TO A DDNAME                                   90217
*          UNLESS THE DD REPRESENTS A PDS, THE DATASET IS OPENED 90217
*          AND REPOSITIONED AT THE BEGINNING.                    90217
*        THE FUNCTION IS USED TO CLEAR A DISP=MOD DATASET.       90217
*                                                                90217
ENTDDCLR LTR   R7,R7         ANY NAME ?                          90217
         BZ    BADENTRY      NO                                  90217
         CLI   0(R7),C' '    ANY ?                               90217
         BNH   BADENTRY      NO                                  90217
         MVC   RDDUMDCB(PATSAML),PATSAM  MOVE BSAM DCB PATTERN   90217
         BAS   R10,ENTRJFCC  GET THE JFCB (NO RETURN IF BAD)     90217
         LA    R10,DDCLRTST  IN CASE NO ENTRY FOUND              90217
         BAS   R9,INITTIOT   PREPARE FOR SEARCH                  90217
         BAS   R9,ENTTIODD   FIND ENTRY                          90217
DDCLRTST ICM   R1,15,RETR0   ANY TIOT ENTRY FOUND ?              90217
         BZ    BADENTRY      NO; INVALID REQUEST                 90217
         CLI   TIOELNGH-TIOELNGH(R1),20  SIMPLE ENTRY ?          90217
         BNE   BADENTRY      NO; FAIL                            90217
         SR    R3,R3                                             90217
         ICM   R3,7,TIOEFSRT-TIOELNGH(R1)  GET THE UCB           90217
         BZ    DDCLRDUM      NONE ?                              90217
         CLI   UCBTBYT3-UCBOB(R3),UCB3DACC  DASD DEVICE ?        90217
         BNE   DDCLRDUM      YES; SKIP OBTAIN                    90217
         MVC   ALTJFCB,RDJFCB  PREVENT OBTAIN CONFLICT           90217
         LA    R7,ALTJFCB    POINT TO JFCB                       90217
         BAS   R10,ENTDSDJ1  READ THE DSCB                       90217
         BXH   R15,R15,BADENTRY  ERROR                           90217
         TM    DB+16+DS1DSORG-DS1FMTID,JFCORGPO  PDS ?           90217
         BNZ   EXIT          YES; IGNORE THE REQUEST             90217
DDCLRDUM OPEN  (,(OUTPUT,REREAD)),MF=(E,RDOPLIST)  OPEN WIDE     90217
         XC    DB,DB                                             90217
         OI    DB+3,1                                            90217
         POINT RDDUMDCB,DB   USELESS POINT ?                     90217
         OI    DCBOFLGS-IHADCB+RDDUMDCB,DCBOFLWR  FORCE AN EOF WRITE
         CLOSE (,REREAD),MF=(E,RDOPLIST)  CLOSE WITH REREAD      90217
         B     EXIT          AND RETURN                          90217
         SPACE 1                                                 90217
PATSAM   DCB   DDNAME=ANY,MACRF=(WP),DSORG=PS,EXLST=2            90217
PATSAML  EQU   *-PATSAM                                          90217
         EJECT ,
*        ENTRY CODE 33 - CHECK LIBPAK DATASET NAME
*        ENTRY AND RETURNS SAME AS CODE 32 (DSTST), EXCEPT THAT  81151
*        ADDITIONAL VR ERROR FLAGS MAY BE SET.                   81151
         SPACE 1
         USING DS1DSNAM,R7   FAKE DSCB DEFINITION                81270
ENTDSLIB BAS   R10,CHECKDSN  CHECK FOR OS DSNAME                 81151
         LTR   R0,R0         ANY INDEX AT ALL ?
         BNP   SHORTOSN      NO; ERROR                           81151
         AIF   (&LOCO).N2L1D                                     81151
         B     BADWNAME      DSLIB FUNCTIONS NOT SUPPORTED       81151
         AGO   .N2L1C                                            81151
.N2L1D   CH    R0,=Y(2)      ONE OR TWO INDEX LEVELS ?           81151
         BH    BADLEVEL      TOO MANY; FAIL
         CLI   DS1DSNAM+4,C'.'  ACCOUNT INDEX LEVEL ?
         BNE   BADWNAME      NO; BAD NAME
         MVC   MEMNO(4),DS1DSNAM
         MVC   MEMNO+4(4),ACCT#SUB   SET DUMMY SUB-ACCOUNT       81151
         MVC   DB(4),=X'C0F0F0F0'  MAKE ACCT MASK (EXTERNAL)     92297
         NC    DB(4),MEMNO   CORRECT ZONES ON ?                  81151
         CLC   DB(4),=X'C0F0F0F0'                                92297
         BNE   BADWNAME      NO; DON'T CHECK ACCT                81151
         BAS   R10,CHECKACT  CHECK ACCOUNT                      GP03076
         B     EXIT          OK ?
.N2L1C   SPACE 2                                                 81359
ENTDSTSO IC    R0,CALLMODE   GET SUB-FUNCTION                    86314
         BIX   ERR=BADENTRY,PFX=ENT,BASE=@SERVICE,                     *
               LOC=(DSTS2,DSTSX,DSTET)                          GP97241
         SPACE 1                                                 86314
ENTDSTS2 MVC   COPYDSN(44),0(R7)   MOVE DSN TO WORK AREA         86314
         LA    R7,COPYDSN    POINT TO BOWDLERIZED VERSION        86314
         LA    R2,COPYDSN+35  POINT TO HIGHEST POSITION FOR GDG NAME
         LA    R3,34         SET COUNTER FOR MAX POSSIBILITIES   86314
DSTGDGLP CLI   8(R2),C' '    TRAILING BLANK ?                    86314
         BNE   DSTGDGTS      NO; TEST FOR GDG NAME               86314
         BCTR  R2,0          BACK-UP                             86314
         BCT   R3,DSTGDGLP   TRY BEFORE                          86314
DSTGDGTS CLI   0(R2),C'.'    POSSIBLE GDG NAME ?                 86314
         BNE   DSTGDGT2      NO; USE NORMAL PROCESSING           86314
         MVC   DB,=C'G0000V00'  MAKE GDG MASK                    86314
         NC    DB,1(R2)      MASK WITH DSN                       86314
         CLC   DB,=X'C700000000E5F0F0'  MATCH ?                  86314
         BNE   DSTGDGT2      NO; DO NORMALLY                     86314
         XC    2(4,R2),=X'FFFFFFFF'  FLIP GENERATION COUNTER     86314
DSTGDGT2 BAS   R10,CHECKDSN  SYNTAX CHECK THE NAME               86314
         LTR   R0,R0         ANY INDEX AT ALL ?                  86314
         BNP   SHORTOSN      NO; ERROR                           86314
         LA    R10,EXIT      RESTORE BRANCH RETURN               86317
         SPACE 2                                                 86314
*        ENTDSTSX - SUB-FUNCTION 1 - VALIDITY-CHECK A USERID INDEX
*                                                                86314
ENTDSTSX MVC   DB,=CL8' '    PRE-BLANK                           86314
         LA    R14,8         SET MAXIMUM LENGTH                  86314
         LR    R15,R7        SET INDEX START                     86314
         LA    R0,1          SET INCREMENT                       86314
         LA    R1,DB         SET OUTPUT POSITION                 86314
DSTSXLOP CLI   0(R15),C' '   END ?                               86314
         BE    DSTSXLUP      YES                                 86314
         CLI   0(R15),C'.'   ALTERNATE ?                         86314
         BE    DSTSXLUP      YES                                 86314
         MVC   0(1,R1),0(R15)  COPY ONE BYTE                     86316
         AR    R15,R0                                            86314
         AR    R1,R0         INCREMENT                           86314
         BCT   R14,DSTSXLOP  TRY AGAIN                           86314
         B     BADWINDX      INVALID INDEX LEVEL/ID              86314
DSTSXLUP CLI   DB,C' '       VALID ?                             86314
         BNH   BADWINDX      NO                                  86314
         LA    R7,DB         POINT TO INDEX                      86314
         B     ENTUSTST      VERIFY USER ID                      86314
         SPACE 2                                                 86314
*        ENTDSTET - CODE 2 - PREFIX USER-ID TO A DSN             86314
*                                                                86314
ENTDSTET LR    R8,R7         PRESERVE USER'S DSN ADDRESS         86316
         BAS   R10,GETUID    GET CURRENT USER ID                 86314
         CLI   COPYINX,C' '  WAS ONE GOTTEN ?                    86314
         BNH   BADWINDX      NO; SET BAD INDEX                   86314
         LA    R14,COPYINX+L'COPYINX                             86314
DSTETLOP BCTR  R14,0         GET PRIOR BYTE                      86314
         CLI   0(R14),C' '   ANY ?                               86314
         BNH   DSTETLOP      NO; BACK-UP ONE MORE                86314
         MVI   1(R14),C'.'                                       86314
         MVC   2(44,R14),0(R8)  COPY REST OF USER'S NAME         86314
         B     EXIT          TAKE GOOD RETURN                    86314
         EJECT
*        ENTRY CODE 34 - WYLBUR DSNAME CHECK
*        ENTRY AND RETURNS SAME AS CODE 32 (DSTST), EXCEPT THAT  81151
*        ADDITIONAL VR ERROR FLAGS MAY BE SET.                   81151
*                                                                81151
*        CCSI USES WYLBUR NAMES OF THE FORM :                    81151
*          INDEX.ACCOUNT.USERID.----                             81151
*        WHERE INDEX IS A THREE BYTE STRING DEFINED IN IGPVOLT   81151
*        ACCOUNT IS THE EXTERNAL FORM OF ACCOUNT+SUBACCOUNT      81151
*        USERID IS AN EIGHT BYTE STRING                          81151
*        --- IS A DATASET NAME OR ONE INDEX LEVEL+NAME           81151
*        LOCAL MODIFICATIONS TO SVC 98 (PROTECT) ARE REQUIRED FOR
*        THIS CODE TO FUNCTION :                                 81151
*          R0=R15=UCB ADDRESS OF DISK CONTAINING PASSWORD DATASET
*              IF NOT SYSRES (PSWD DSN IS ON SHARED PACK)        81151
*          PASSWORD OF 8X'FF' FOR THE LIST (04) AND TTR (05)     81151
*              FUNCTIONS RETURNS FIRST MATCHING DSN ENTRY.       81151
*                                                                81151
*        MODULE @PROTECS SIMULATES SVC 98 IN READ-ONLY MODE BY   81151
*          USING FULL-TRACK I/O AND STACKING TRACKS IN STORAGE,  81151
*          THUS PRODUCING SIGNIFICANT IMPROVEMENTS FOR HIGH-VOLUME
*          ACCESS (E.G. WEEKLY CLEANUP JOBS RUNNING THROUGH ALL  81151
*          VOLUMES AND CATALOG ENTRIES).                         81151
         SPACE 1
         USING DS1DSNAM,R7   USE FAKE DSCB DEFINITION            81137
ENTDSWYL IC    R0,CALLMODE   GET SUB-FUNCTION                    85223
         BIX   ERR=BADENTRY,PFX=ENT,BASE=@SERVICE,                     *
               LOC=(DSWY2,WYLDX,DSGET,DSWYC)                    GP97241
         SPACE 1                                                 85223
*        DSWYC - CHECK WYLBUR CATALOG NAME (GDG PERMITTED)       85223
*                                                                85223
ENTDSWYC MVC   COPYDSN(44),0(R7)   MOVE DSN TO WORK AREA         85223
         LA    R2,COPYDSN+35  POINT TO HIGHEST POSITION FOR GDG NAME
         LA    R3,34         SET COUNTER FOR MAX POSSIBILITIES   85223
WYLGDGLP CLI   8(R2),C' '    TRAILING BLANK ?                    85223
         BNE   WYLGDGTS      NO; TEST FOR GDG NAME               85223
         BCTR  R2,0          BACK-UP                             85223
         BCT   R3,WYLGDGLP   TRY BEFORE                          85223
WYLGDGTS CLI   0(R2),C'.'    POSSIBLE GDG NAME ?                 85223
         BNE   ENTDSWY2      NO; USE NORMAL PROCESSING           85223
         MVC   DB,=C'G0000V00'  MAKE GDG MASK                    85223
         NC    DB,1(R2)      MASK WITH DSN                       85223
         CLC   DB,=X'C700000000E5F0F0'  MATCH ?                  85223
         BNE   ENTDSWY2      NO; DO NORMALLY                     85223
         XC    2(4,R2),=X'FFFFFFFF'  FLIP GENERATION COUNTER     85223
         LA    R7,COPYDSN    POINT TO BOWDLERIZED VERSION        85223
         SPACE 1                                                 85223
*        DSWYL - VALIDITY CHECK A WYLBUR NAME                    85223
*                                                                85223
ENTDSWY2 BAS   R10,CHECKDSN  SYNTAX CHECK THE NAME               81165
         LTR   R0,R0         ANY INDEX AT ALL ?
         BNP   SHORTOSN      NO; ERROR                           81151
         AIF   ('&LOCAL' NE 'CNW' AND '&LOCAL' NE 'EPA').N2L2M   81165
         CH    R0,=Y(3)      SUFFICIENT LEVELS ?                 81165
         BL    SHORTDSN      NO; TOO SHORT                       81165
         BAS   R10,WYLINDEX  SEE IF WYLBUR INDEX                 81165
         B     BADWNAME      NO                                  81165
         B     EXIT          NO FURTHER TESTS (YET?)             81165
         AGO   .N2L2C        GO TO COMMON EXIT                   81165
.N2L2M   AIF   (&NOLO).N2L2D                                     81165
         B     EXIT          DSWYL FUNCTIONS NOT SUPPLIED        93218
         AGO   .N2L2C                                            81151
.N2L2D   CH    R0,=Y(3)      SUFFICIENT INDEX LEVELS ?           81151
         BL    SHORTDSN      NO; INVALID NAME                    81151
         CH    R0,=Y(4)      BUT NOT TOO MANY ?
         BH    BADLEVEL      TOO MANY; TOO BAD
         BAS   R10,WYLINDEX  PROPER WYLBUR INDEX LEVEL ?        GP03076
         B     BADWINDX      NO                                  81137
         CLI   DS1DSNAM+12,C'.'  8 BYTE ACCOUNT ?
         BNE   BADWNAME      DEFINITELY NOT
         CLI   DS1DSNAM+21,C'.'  8 BYTE USER ID ?
         BNE   BADWNAME      NO; TOO BAD
         SPACE 1
CHECKWYL MVC   MEMNO,DS1DSNAM+4   MOVE PRESUMED ACCOUNT NUMBER
         BAS   R10,CHECKACT  CHECK THE ACCOUNT                  GP03076
         LA    R10,EXIT      SET RETURN ADDRESS
         ICM   R15,15,WYLUCB    GET WYL001 (PASSWORD DSN) ADDRESS
         BMR   R10           NOT UP; SKIP CHECKING - ACCEPT
         BP    WYLPFIND      HAVE IT
         MVI   WYLUCB,X'FF'  PROVISIONALLY SHOW NOT FOUND
         STM   R7,R10,DB+16  SAVE REGISTERS
         LA    R7,=C'WYL001'  SET PACK TO SEARCH FOR
         BAS   R10,VSNUCB    GET UCB FOR IT                     GP03076
         B     WTONWYL1      FAIL IT
         USING UCBOB,R3                                          81137
         CLI   UCBTBYT3,UCB3DACC   DIRECT ACCESS ?
         BNE   VSUCBLOP      NO, SKIP
         TM    UCBSTAT,UCBCHGS+UCBUNLD  COMING OFF ?
         BNZ   VSUCBLOP
         TM    UCBSTAT,UCBONLI  ONLINE ?
         BZ    VSUCBLOP
         LM    R7,R10,DB+16  RELOAD REGISTERS
         B     WYLPUCB       USE IT
         SPACE 1
WTONWYL1 WTO   '@SERVICE *** UNABLE TO USE WYL001 PACK',ROUTCDE=(1,11)
         ABEND 252,DUMP      SKIP REST
         SPACE 2
WYLPUCB  ST    R3,WYLUCB     SAVE ADDRESS
         LA    R14,WAX+L'WAX   LENGTH OF ONE SAVED ACCT/ID ENTRY 81151
         ST    R14,WYLAINC   SET INTO BXLE LENGTH                81151
         LA    R14,PSWDATA   SET PROTECT RETURN AREA             81165
         LA    R15,PSWWYLB   SET DSN TO BE FOUND                 81165
         LA    R0,FFS        SET PASSWORD ADDRESS                81151
         STM   R14,R0,WYLPLIST  MAKE PROTECT LIST                81151
         MVI   PSWPCODE,PSWFLIST  SET 'LIST' CODE                81165
         MVI   PSWDSNL,PSWWYLL  SET LENGTH OF 'DATASET' ENTRY    81165
         MVC   PSWWYLB,=CL9'WYLBUR '  MAKE PREFIX AND BLANKS     81165
         DROP  R3
         LA    R3,PSWDATAN   POINT TO FAKE WORK AREA             81165
         LA    R4,DBEND-PSWDATAN   SET WORK AREA LENGTH          81165
         LA    R2,4096-DBEND+PSWDATAN(,R4)  SET FOR 4K BLOCK     81165
*OLD*    O     R2,SUBPOOL                                        83100
*OLD*    GETMAIN R,LV=(R2)   SEE IF AVAILABLE                    81151
         SR    R15,R15                                          GP03076
         IC    R15,SUBPOOL                                      GP03076
         STORAGE OBTAIN,LENGTH=(R2),SP=(15),COND=YES            GP03076
         BXH   R15,R15,WYLUCONT  IGNORE IF NOT GOTTEN
         LR    R3,R1         COPY ADDRESS                        81151
         LA    R4,0(,R2)     COPY LENGTH AND CLEAR SUBPOOL       83100
         STM   R3,R4,WYLFREEM  SAVE FREEMAIN PARMS
WYLUCONT ST    R3,WYLALIST   SET START LIST ENTRY
         ST    R3,WYLACURR   SET CURRENTLY AVAILABLE SLOT
         AR    R4,R3         LAST ENTRY+
         S     R4,WYLAINC    SUBTRACT
         S     R4,WYLAINC    THREE
         S     R4,WYLAINC    TO PREVENT CLOBBERING STORAGE
         ST    R4,WYLAEND    STASH IN LIST END
         MVC   0(16,R3),BLANKS     INITIALIZE TO BLANKS          81151
         SPACE 1
WYLPFIND MVC   PSWACCT,MEMNO   REPLACE BY MODIFIED ACCOUNT NUMBER
         MVC   PSWUSER,DS1DSNAM+13  COPY WYLBUR USERID           81165
WYLPFINP LM    R3,R5,WYLALIST  GET LIST OF VALID USERS FOR BXLE
WACCT    EQU   0,16          ACCOUNT/USER ENTRY IN PROTECT LIST
WAX      EQU   16,1          INDEX ENTRY IN PROTECT LIST
WAXFAIL  EQU   X'FF'         ERROR IN PROTECT
WAXGONE  EQU   X'FE'         ENTRY NOT FOUND
WYLPLOOP CLC   PSWACCT(16),WACCT(R3)   MATCHES ?                 81165
         BNE   WYLPBXLE      NO; CHECK NEXT ENTRY
WYLINX   SR    R14,R14       CLEAR FOR INDEX INSERT
         IC    R14,WAX(,R3)  GET INDEX SUBSCRIPT
         L     R1,@USRVOLT   GET ADDRESS OF VOLT
         USING USERVOLT,R1
         CLI   WAX(R3),WAXGONE  PROTECT FAILED ?
         BHR   R10           YES; PERMIT THIS UNCHECKED
         BE    BADPSWD       FAIL IF NO PASSWORD FOUND
         CLC   WAX(1,R3),VTWYIXNO  VALID SUBSCRIPT ?
         BNL   BADWNAME      NO; MUST BE OBSOLETE ENTRY
         MH    R14,VTWYENLN  CONVERT INDEX TO OFFSET             81359
         AL    R14,VTWYIXAD  GET INDEX ENTRY
         CLI   CALLMODE,VENDSGET/256  DSGET ENTRY ?              81165
         BNE   *+10          NO                                  81165
         MVC   DS1DSNAM(3),0(R14)  SET CORRECT INDEX NAME        81165
         CLC   DS1DSNAM(3),0(R14)  MATCHES ?
         BER   R10           YES; SURPRISE !
         B     BADWNAME      OTHERWISE FAIL IT
WYLPBXLE BXLE  R3,R4,WYLPLOOP  TRY AGAIN / ACTUALLY GOES ONE OVER
         L     R15,WYLUCB    RESTORE UCB ADDRESS
         LR    R0,R15        COPY UCB ADDRESS OF PASSWORD DSN ON WYL001
*        @PROTECT WYLPLIST   TRY TO FIND PASSWORD ENTRY FOR IT
         LA    R1,WYLPLIST     POINT TO REQUEST LIST             81151
         ICM   R14,15,@PROTECS    GET MODULE ADDRESS             81151
         BZ    WYLP98        NOT LOADED, USE SVC                 81151
         BASR  R14,R14         INVOKE FAST VERSION               81151
WYLP98   SVC   98              USE OS CODE IF FAST CODE FAILS (NO DD)
         BXLE  R15,R15,WYLPADDX  FOUND; ADD TO TABLE             81165
         MVI   PSWCPSW,WAXFAIL  PRESET FOR PROTECT ERROR         81165
         CH    R15,=Y(16)    NOT FOUND ?
         BH    WYLPMAJR      OTHER ERROR - FAIL
         MVI   PSWCPSW,WAXGONE  ELSE SET NO PASSWORD FOUND       81165
         CLC   ACCT#SYS,PSWACCT    SPECIAL ACCT ?                81165
         BE    WYLPADD       YES - IS SOMEONE CHEATING ?
         CLC   FOUR0,PSWSUB     BEEN HERE BEFORE ?               81165
         BE    WYLPADD       YES; NOT VALID
         MVC   PSWSUB,FOUR0   TRY WITHOUT SUBACCOUNT             81165
         B     WYLPFINP      AND TRY AGAIN
         SPACE 1
WYLPMAJR WTO   '@SERVICE - ERROR IN @PROTECS',ROUTCDE=(1,11)
         ABEND 98,DUMP
         SPACE 1
WYLPADDX MVC   PSWCPSW(1),PSWHINDX  SET INDEX VALUE              81165
WYLPADD  LM    R3,R4,WYLACURR  RESTORE; JUST IN CASE
WYLPADD2 CR    R3,R4         ROOM FOR MORE ENTRIES ?
         BL    WYLPADD3      YES; ADD IT IN
         L     R3,WYLALIST   SET TOP OF LIST
         ST    R3,WYLACURR   BACK INTO 'CURRENT' POINTER
WYLPADD3 MVC   WACCT(32,R3),PSWACCT  SAVE ACCT,USERID + BLANKS   81165
         MVC   WAX(1,R3),PSWCPSW SAVE INDEX/FLAG BYTE            81165
         CLI   PSWCPSW,WAXGONE  ERROR ENTRY ?                    81165
         BNL   WYLPADD4      YES; LEAVE AS IS
         CLI   PSWCPSW,C' '     GARBAGE ENTRY ?                  81165
         BL    WYLPADD4      NO; LEAVE AS IS
         MVI   WAX(R3),0     USE DEFAULT
WYLPADD4 LA    R15,WAX+L'WAX(,R3)   SET NEXT ENTRY
         ST    R15,WYLACURR  AND SAVE IT
         CLC   PSWACCT,MEMNO  SAME AS ORIGINAL REQUEST  ?        81165
         BE    WYLINX        YES; NOW CHECK INDEX NAME
         LR    R3,R15        SET TO CHECK CURRENT ENTRY
         MVC   PSWACCT,MEMNO   ELSE RESTORE ORIGINAL CODE        81165
         B     WYLPADD2      AND ENTER FULL CODE ALSO
.N2L2C   SPACE 2                                                 81151
*        ENTRY CODE 34/MODE=1 - CHECK HIGH-LEVEL INDEX FOR WYLBUR NAME
*        ENTRY R1 - FULL DSN (IF HIGH BYTE IS 3, INDEX ONLY)     81151
*        RETURNS: R15 - 16 - INVALID ADDRESS                     81151
*        R15 - 4 - NOT WYLBUR INDEX                              81151
*        R15 - 0 - WYLBUR INDEX                                  81151
         SPACE 1                                                 81151
         AIF   (&NOLO).N2L3D                                     81151
         AIF   ('&LOCAL' NE 'CNW' AND '&LOCAL' NE 'EPA').N2L3M   81165
ENTWYLDX LA    R9,WYLINDEX   SET FOR FULL INDEX CHECKING         81165
         CLI   CALLB0,2      SHORT FORM ?                       GP99026
         BNE   *+8           NO                                  81165
         LA    R9,WYLDEXIX   SKIP PERIOD CHECK                   81165
         BASR  R10,R9        VALID INDEX LEVEL ?                 81165
         B     BADRET4       NO; SET LEVEL 4 RETURN              81165
         B     EXIT          ELSE RETURN OK                      81165
         SPACE 1 ,                                               81165
WYLINDEX CLI   DS1DSNAM+2,C'.'    VALID TWO BYTE INDEX ?         81165
         BNER  R10           NO; NOT VALID WYLBUR NAME           81165
WYLDEXIX CLC   =C'DS',DS1DSNAM  PROPER INDEX LEVEL ?             81165
         BE    4(,R10)       YES; RETURN OK                      81165
         BR    R10           ELSE RETURN WITH ERROR              81165
         AGO   .N2L3C        SKIP OTHER STUFF                    81165
.N2L3M   ANOP  ,                                                 81165
ENTWYLDX B     BADRET4       SET 'NOT WYLBUR INDEX' RETURN       81151
         AGO   .N2L3C                                            81151
.N2L3D   ANOP  ,                                                 81151
ENTWYLDX LA    R9,WYLINDEX   SET FOR FULL INDEX CHECKING         81151
         CLI   CALLB0,3      SHORT FORM ?                       GP99026
         BNE   *+8           NO                                  81151
         LA    R9,WYLDEXIX   SKIP PERIOD CHECK                   81151
         BASR  R10,R9        VALID INDEX LEVEL ?                 81151
         B     BADRET4       NO; SET LEVEL 4 RETURN
         B     EXIT          ELSE RETURN OK
         SPACE 1 ,
WYLINDEX CLI   DS1DSNAM+3,C'.'    VALID THREE BYTE INDEX ?
         BNER  R10           NO; NOT VALID WYLBUR NAME
WYLDEXIX ICM   R1,15,@USRVOLT   USRVOLT LOADED ALREADY ?         92297
         BZR   R10           RETURN NOT FOUND                    92297
         USING USERVOLT,R1   DECLARE MAPPING
         LH    R2,VTWYENLN   GET LENGTH OF ONE ENTRY             81359
         SR    R14,R14
         IC    R14,VTWYIXNO  GET NUMBER OF LEVELS DEFINED
         L     R1,VTWYIXAD   GET ADDRESS OF FIRST ONE
         DROP  R1
WYLDEXLP CLC   DS1DSNAM(3),0(R1)  MATCHING INDEX ?
         BE    4(,R10)       YES; RETURN AS VALID
         AR    R1,R2         ELSE REPEAT FOR NEXT ONE            81359
         BCT   R14,WYLDEXLP
         BR    R10           ELSE RETURN NOT FOUND
.N2L3C   SPACE 2                                                 81151
*        ENTRY CODE 34/MODE=2   EXPAND (POSSIBLY) SHORT NAME TO LONG
*        NAME IS WRITTEN BACK TO USER AREA, AND PASSED TO VALIDITY
*        CHECK.                                                  81165
         AIF   (NOT &LOCO).N2L4M                                 93038
ENTDSGET MVC   COPYDSN,DS1DSNAM  COPY TO WORK AREA               93038
         LA    R14,DS1DSNAM                                      93038
         LA    R15,L'DS1DSNAM                                    93038
         LA    R1,COPYDSN                                        93038
         CLI   0(R1),C'.'    UID PREFIX ?                        93038
         BE    DSGETNU       YES                                 93038
         CLI   0(R1),C';'    UID+ACCT PREFIX ?                   93038
         BE    DSGETNU       YES                                 93038
         CLI   0(R1),C''    ACCOUNT ONLY ?                      93038
         BE    DSGETNN       YES                                 93038
         CLI   0(R1),C'&&'   SPECIAL INPUT NAME ?                93038
         BNE   DSGETSHT      NO                                  93038
         MVC   0(4,R14),=C'SYS1' MOVE INDEX LEVEL                93038
         B     DSGETN4       JOIN COMMON                         93038
DSGETSHT CLI   8(R1),C' '    SHORT NAME ?                        93218
         BNE   DSGETCHK      DEFINITELY NOT                      93218
         LR    R14,R1        GET START ADDRESS                   93218
         LA    R0,8                                              93218
DSGETSHL CLI   0(R14),C'.'   INDEX ?                             93218
         BE    DSGETCHK      YES; DON'T PREFIX                   93218
         LA    R14,1(,R14)                                       93218
         BCT   R0,DSGETSHL   TRY ALL                             93218
         BCTR  R1,0          FIX FOR EXPECTED ';' CONTROL        93218
DSGETNU  LTCB  R2            GET THE TCB                         93038
         L     R2,TCBTIO-TCB(,R2)  GET THE TIOT                  93038
         MVC   0(8,R14),0(R2)  MOVE USER ID?(JOBNAME)            93038
         LA    R0,8          SET LENGTH TO SKIP                  93038
         ST    R14,12(,R13)  SAVE START                          93038
         AR    R14,R0        PAST LAST BYTE                      93038
DSGETNV  BCTR  R14,0         BACK-UP ONE                         93038
         CLI   0(R14),C' '   TRAILING GUNK ?                     93038
         BH    DSGETNW       NO                                  93038
         BCT   R0,DSGETNV                                        93038
DSGETNW  L     R14,12(,R13)  RESTORE START                       93038
         CLI   0(R1),C'.'    UID PLUS ACCOUNT ?                  93038
         BE    DSGETNX       NO; GO TO COMMON EXIT               93038
         LTCB  R2            GET TCB                             93038
         L     R2,TCBTCT-TCB(,R2)   GET TCT ADDRESS              93038
         USING SMFTCT,R2                                         93038
         L     R2,TCTJMR     GET JMR                             93038
         USING JMR,R2                                            93038
         CLI   JMRUSEID,C' ' ANY ACCOUNT ?                       93038
         BNH   DSGETNX       NO; GO TO COMMON                    93038
         AR    R14,R0        GET END AGAIN                       93038
         MVI   0(R14),C'.'   MAKE INDEX POINT                    93038
         MVC   1(8,R14),JMRUSEID  MOVE ACCOUNT NUMBER            93038
         CLI   1(R14),C'0'   OLD-FASHIONED (WRONG) ACCOUNT       93038
         BL    DSGETN9       NO; LEAVE IT                        93038
         BE    DSGETN9Z                                          93038
         NI    1(R14),X'CF'  CONVERT TO EXTERNAL FORM            93038
         B     DSGETN9                                           93038
DSGETN9Z MVI   1(R14),C'Z'   REPLACE BY EXTERNAL                 93038
         DROP  R2                                                93038
DSGETN9  LA    R14,9(R14,0)  MAKE HALF-WORD CONSTANT             93038
         AH    R0,DSGETN9+2  SET NEW LENGTH                      93038
DSGETNC  BCTR  R14,0         BACK-SPACE ONE                      93038
         CLI   0(R14),C' '   ANY THERE THERE ?                   93038
         BH    DSGETND       YES                                 93038
         BCT   R0,DSGETNC    GO AGAIN                            93038
DSGETND  L     R14,12(,R13)  RESTORE START                       93038
         B     DSGETNX       ADD INDEX POINT, ETC.               93038
DSGETNN  LTCB  R2            GET TCB                             83100
         L     R2,TCBTCT-TCB(,R2)   GET TCT ADDRESS              81165
         USING SMFTCT,R2                                         81165
         L     R2,TCTJMR     GET JMR                             81165
         USING JMR,R2                                            81165
         CLI   JMRUSEID,C' '  ANY ACCOUNT ?                      93038
         BNH   DSGETCHK      NO; WILL FAIL                       93038
         MVC   0(4,R14),JMRUSEID MAKE MAJOR ACCOUNT              93038
         CLI   0(R14),C'0'   OLD VERSION OF ACCOUNT NUMBER ?     93038
         BL    DSGETN4       NO                                  93038
         BE    DSGETN4Z      SPECIAL                             93038
         NI    0(R14),X'CF'  CONVERT TO EXTERNAL FORM            93038
         B     DSGETN4                                           93038
DSGETN4Z MVI   0(R14),C'Z'   REPLACE BY EXTERNAL                 93038
         DROP  R2                                                93038
DSGETN4  LA    R0,4          SET LENGTH TO FOUR                  93038
DSGETNX  LA    R1,1(,R1)     SKIP OVER SPECIAL CHARACTER         93038
         AR    R14,R0        ADJUST OUTPUT ADDRESS               93038
         SR    R15,R0        ADJUST OUTPUT LENGTH                93038
         MVI   0(R14),C'.'   MAKE INDEX POINT                    93038
         LA    R14,1(,R14)   SKIP IT                             93038
         BCTR  R15,0         AND SET FINAL LENGTH                93038
DSGETNL  LA    R0,COPYDSN+1  POINT PAST CONTROL CHARACTER        93038
         LA    R1,43         MAXIMUM LENGTH                      93038
         MVCL  R14,R0        MOVE REMAINDER                      93038
DSGETCHK BAS   R10,CHECKDSN  SEE IF NAME IS VALID                81165
*                                                                81347
*        SPECIAL INSERTION FOR WYLCOMP - RETURN ADDRESS OF WYLBUR
*        PASSWORD RECORD (VALID ONLY IN NON-@PROTECS MODE)       81347
DSGETST1 LA    R1,PSWDATA    POINT TO RETURN FIELD               82011
         ST    R1,RETR1      RETURN TO USER IN R1                82011
         B     ENTDSWY2                                          81165
         AGO   .N2L4C                                            81165
.N2L4M   ANOP  ,                                                 81165
ENTDSGET B     BADENTRY      NOT YET IMPLEMENTED                 81151
.N2L4C   SPACE 2                                                 81165
*        ENTRY CODE 36 - LOCAL VOLUME ATTRIBUTE LOOKUP           81151
*        ENTRY R1 => CL6'VOLSER'                                 81151
*        RETURNS: R15 - 16 - BAD ADDRESS IN R1                   81151
*        R15 - 0 - ALL OTHERS.                                   81151
*        R0 - LOW TWO BYTES - ATTRIBUTE FLAGS (0 IF VOLUME NOT FOUND)
         SPACE 1                                                 81151
ENTVSNFG CLI   CALLMODE,1    CHECK SUB-CODE                      82052
         BH    ENTVSTMS      =2 - CHECK VOLSER FOR TMS           82052
         BE    BADENTRY      =1 - DSANY (VS/DSN CHECK) NOT YET   82052
         AIF   (&LOCO).N2L9D                                     81151
         B     EXIT          RETURN VOLUME FOUND; NO SPECIAL BITS
         AGO   .N2L9C                                            81151
.N2L9D   L     R15,@USRVOLT  GET USRVOLT                         92297
VSNFIND  MVC   INVOL,0(R7)   COPY VOLSER
         XC    INFLAGS(6),INFLAGS  CLEAR ALL FLAGS
         LA    R1,INVOL      POINT TO PARM LIST
         SR    R0,R0         SET SIMPLE LOOKUP REQUEST
         LTR   R15,R15       HAVE ONE ?                          92297
         BZ    EXIT          NO; RETURN ALL ZEROES               92297
         BASR  R14,R15       DO SEARCH                          GP03076
         MVC   RETR0+2(2),INFLTEST   RETURN FLAGS
         LTR   R15,R15       CHECK RETURN                        92306
         BNZ   EXIT          RETURN IF OK
         B     BADRET8       ELSE SET NOT FOUND
.N2L9C   SPACE 1                                                 82052
*        ENTRY CODE 36/MODE=2  CHECK VOLSER FOR TMS CONTROL      82052
*        R1 => CL6'VOLSER'                                       82052
*        RETURNS :  R15 - 16  (R1) INVALID                       82052
*                   R15 - 8   &SVCTMSY = 0 OR IGCYYY00 NOT IN LPA
*                   R15 - 4   NOT TMS VOLUME                     82052
*                   R15 - 0   TMS VOLUME;  R0 = REL. BLOCK NUMBER
ENTVSTMS DS    0H                                                82052
         AIF   (&SVCTMSY EQ 0).NOTMSVS                           82052
         ICM   R15,15,@TMSSVCY  SEE IF TMS RESIDENT MODULE LOCATED
         BM    VSTMSCAL      YES; CALL IT                        82052
         L     R2,CVTPTR     GET THE CVT                         90316
         USING CVTMAP,R2                                         90316
         ICM   R2,15,CVTJESCT  SUBSYSTEM VECTOR TABLE            90316
         BNP   VSTMSLPA      MAJOR BOOBOO                        90316
         USING JESCT,R2      DECLARE IT                          90316
         LA    R2,JESSSCT-(SSCTSCTA-SSCT)  CHEAT                 90316
         USING SSCT,R2                                           90316
VSTMSLUP ICM   R2,15,SSCTSCTA   GET NEXT SSCVT                   90316
         BNP   VSTMSLPA      USER CHOSE BAD NAME ?               90316
         CLC   =C'TMS ',SSCTSNAM  NEW NAME FOR UCC-1 ?           92358
         BE    VSTMSTVT      YES; LOOK FOR TVT                   92358
         CLC   =C'UCC1',SSCTSNAM  DESIRED SUBSYSTEM ?            90316
         BNE   VSTMSLUP      NO                                  90316
VSTMSTVT ICM   R0,15,SSCTSUSE  TEST FOR TVT                      90316
         BP    VSTMSADD      OK ?                                90316
         DROP  R2                                                90316
VSTMSLPA LA    R2,=CL8'IGGU0100'  4.7.10 UP ?                    90316
         BAS   R9,LOADLPA    TRY IT                              90316
         LTR   R0,R0                                             90316
         BNZ   VSTMSADD      USE IT                              90316
         LA    R2,IGCYYY00   POINT TO MODULE NAME                82052
         BAS   R9,LOADLPA    SEE IF IN LPA                       82052
VSTMSADD LTR   R15,R0        CHECK ADDRESS                       90316
         BZ    BADRET8       NOT FOUND; SET RETURN CODE 8        82052
         ST    R0,@TMSSVCY   SAVE ADDRESS FOR NEXT TIME          82052
         OI    @TMSSVCY,X'80'  INDICATE LPA RESIDENCE            82052
VSTMSCAL LR    R1,R7         SET VOLSER POINTER                  82052
         LA    R14,8         SET FOR MOST COMMON DISPLACEMENT    86203
         CLC   =C'TVT',0(R15)  UCC-1 V4.7.10 AND UP ?            86203
         BE    VSTMSCAV      YES                                 86203
         CLC   =C'TMVT',0(R15)  UCC-1 V4.7 AND UP ?              85083
         BE    VSTMSCAV      YES                                 86203
         SR    R14,R14       ELSE USE OLD, OLD OLD HASH OFFSET   86203
VSTMSCAV L     R15,0(R14,R15)   GET TMS HASH ROUTINE ADDRESS     85083
         BASR  R14,R15       CALL IT                             82052
         LTR   R0,R1         TEST RETURN                         84087
         BZ    BADRET4       NOT IN TMS                          82052
         ST    R0,RETR0      SET BLOCK NUMBER IN RETURN          82052
         B     EXIT                                              82052
IGCYYY00 DC    C'IGG',CL3'&SVCTMSY',C'00'  TMS RESIDENT MODULE   82052
         AGO   .NOTMSVY                                          82052
.NOTMSVS B     BADRET8       TMS SUPPORT NOT SPECIFIED           82052
.NOTMSVY SPACE 1                                                 81359
         LTORG ,                                                 81359
         TITLE '@ S E R V I C E  ***  WYLBUR COMPRESS/EXPAND'    82052
*        ENTRY CODE 37 - WYLBUR BUFFER COMPRESSION/DECOMPRESSION 81151
*        MODE=0 COMPRESS                                         81151
*        MODE=1 DECOMPRESS                                       81151
*                                                                81151
*        ENTRY R1 - ADDRESS OF WORK AREA (SEE SERVCOMP MACRO FOR 81151
*              MAPPING.                                          81151
*        RETURNS: R15 - 16 - INVALID R1 OR BAD ADDRESS IN SERVCOMP
*              AREA (E.G. ZERO BUFFER ADDRESS)                   81151
*        R15 - 8 - ERROR. ERROR CODE IN R0 MAPPED BY VCM FLAGS   81151
*        R15 - 4 - COMPRESS / BUFFER FULL; USER SHOULD WRITE IT  81151
*              AND RETURN THE SAME RECORD FOR COMPRESSION.       81151
*                R0 IS SET TO THE BUFFER LENGTH TO BE WRITTEN (MAY BE
*                ZERO IF NOTHING IN CURRENT BUFFER).  THIS RETURN MAY
*                (AND MUST) BE FORCED AFTER THE LAST RECORD HAS BEEN
*                COMPRESSED TO PERMIT WRITING THE FINAL BUFFER.  81151
*                  DECOMPRESS / BUFFER IS EXHAUSTED; USER SHOULD 81151
*              READ ANOTHER.                                     81151
*                  ADDRESS OF THE BUFFER MUST BE STORED IN WCMBUFAD
*                  AND THE BUFFER LENGTH IN WCMBUFMX.            81151
*        R15 - 0 - RECORD PROCESSED.  WARNING FLAGS, MAPPED BY   81151
*              VCM CODES, MAY BE RETURNED IN R0.                 81151
*                                                                81151
*        COMPRESS:  FOR INITIALIZATION, WCM FLAGS MUST BE SET.   81151
*              WCMCODAD MAY BE SET TO THE ADDRESS OF A 16 BYTE   81151
*                'CODE' WORD FOR ENCRYPTING TEXT.                81151
*              FOR EACH BUFFER, USER SETS WCMBUFAD/WCMBUFMX.     81151
*              FOR EACH RECORD, USER SETS WCMRECAD/WCMRECLN. A ZERO
*                RECORD ADDRESS IS USED TO TRUNCATE THE CURRENT BUFFER
*                R15 WILL BE SET TO 4, R0 TO THE BUFFER WRITE LENGTH.
*                USED AFTER INPUT EOF TO WRITE FINAL BUFFER.     81151
*              WCMF2NPR - ALL COMPRESSION/DECOMPRESSION/TRANSLATION
*                IS SKIPPED; ALL FLAGS ARE IGNORED. CODE RECEIVES
*                AND RETURNS COMPLETE RECORDS USING THE WCMRECAD 81151
*                AND WCMRECLN FIELDS.  FEATURE IS USED FOR DATASET
*                MIGRATION TO ANOTHER DEVICE TYPE, WHERE DECOMPRESSION
*                AND RECOMPRESSION WOULD BE WASTEFUL.            81151
*              WCMF1NIH PERMITS NEW NIH FORMAT (500 BYTE RECORDS).
*                IF USED, WCMFG3 SHOULD BE SET ON A RECORD BASIS.
*              WCMF1INT INDICATES THAT LINE NUMBERS ARE PRESENT IN
*                THE RECORD IN CL8 FORM.                         81151
*              WCMF1EDT INDICATES THAT LINE NUMBERS ARE PRESENT IN
*                THE RECORD IN 4C.3C FORM, WITH LEADING BLANKS AND
*                TRAILING BLANKS. THE CODE WILL ALSO SUPPORT THE 81151
*                CL8 FORM WITH BLANKS REPLACING ZEROES.          81151
*                BY DEFAULT, THESE SEQUENCE NUMBERS ARE ASSUMED TO
*                BE IN THE LAST 8 BYTES OF EACH RECORD.          81151
*              WCMF1TSO SPECIFIES THAT THE SEQUENCE NUMBERS ARE IN
*                THE FIRST 8 BYTES OF THE RECORD.                81151
*              WCMF1OSI REQUESTS OSI FORMAT - THE HIGH BIT IN A LINE
*                NUMBER IS TURNED ON TO INDICATE FULL-WORD FORMAT.
*        WCMF1OSI+WCMF1NIH INDICATE OSI/SSI FORMAT. X'80' FOR FULL-
*                WORD LINE #, BUT X'40' NOT 16-BIT LENGTH INDICATOR.
*              WCMF1HWD REQUESTS LINE NUMBERS IN THE FORM OF TWO 81151
*                HALF-WORDS (OLD RAND FORMAT)                    81151
*              WCMF2RDW MAY BE SET TO INDICATE V-FORMAT INPUT    81151
*                RECORDS; IF ON, WCMRECLN NEED NOT BE SET.       81151
*                                                                81151
*        THIS CODE IS BASED ON THE NIH WYLBUR COMPRESSION/DECOMPRESSION
*        ROUTINES. THE 'CODE' PROCESSING WAS MODIFIED TO TRANSLATE
*        ONLY THE TEXT, AND NOT THE LENGTHS AND LINE NUMBERS.    81151
*        SUBSTANTIAL CHANGES AND ENHANCEMENTS ARE DUE TO:        81151
*              SEYMOUR METZ, RICHARD OXLEY AND GERHARD POSTPISCHIL
*                                                                81151
ENTWCOMP BASR  R11,0         WYLBUR COMPRESS/DECOMPRESS          81151
         USING *,R11         NEW BASE FOR COMPRESS/EXPAND CODE   81145
         LR    R10,R7        COPY PARM                           81151
         USING SERVCOMP,R10  DECLARE MAPPING                     81151
         SR    R4,R4         ZERO HIGH BYTE                     GP99147
         ICM   R4,7,WCMBUFAD+1  LOAD BUFFER ADDRESS              81151
         BZ    DCMPARM       NONE; ERROR                         81151
         CLI   CALLMODE,1    DECOMPRESS FUNCTION ?               81151
         BE    ENTWDCOM      YES; GO TO IT                       81151
         LH    R5,WCMBUFMX   GET BUFFER SIZE                     81151
         CH    R5,=AL2(2+506)  LARGE ENOUGH ?                    81151
         BL    DCMBKLEN      NO; FAIL                            81151
         SR    R6,R6         ZERO HIGH BYTE                     GP99147
         ICM   R6,7,WCMRECAD+1  CHECK BUFFER ADDRESS             81151
         BNZ   CMPWNEOF      OK                                  81151
         SPACE 1                                                 81151
*        RETURN TO USER WITH BUFFER LENGTH AND 'WRITE' CODE      81151
*                                                                81151
CMPWRITE LH    R1,WCMBUFLN   GET CURRENT BUFFER LENGTH           81151
         CH    R1,=Y(7)      ANYTHING IN IT ?                    81151
         BNL   CMPWRINZ      YES                                 81151
         SR    R1,R1         ELSE RESET IT                       81151
         STH   R1,0(,R4)     ALSO CLEAR BUFFER LENGTH            81151
CMPWRINZ TM    0(R4),WCMF1NIH   NIH DATA IN BUFFER ?             81151
         STH   R1,0(,R4)     SET NEW BUFFER LENGTH               81151
         BZ    CMPWRINH      NO                                  81151
         OI    0(R4),WCMF1NIH  ELSE RESET NIH FLAG               81151
CMPWRINH STCM  R1,12,WCMBUFLN  CLEAR BUFFER USED FOR NEXT CALL   81151
         ST    R1,RETR0      RETURN WRITE LENGTH TO USER IN R0   81151
         B     BADRET4       SET WARNING RETURN - WRITE REQUIRED 81151
         SPACE 1                                                 81151
CMPWNEOF LH    R3,WCMBUFLN   GET CURRENT BUFFER USE              81151
         CH    R3,=Y(7)      ANYTHING IN IT ?                    81151
         BNL   CMPWNPR       YES                                 81151
         LA    R3,2                                              81151
         STH   R3,WCMBUFLN   CORRECT IT                          81151
         STH   R3,0(,R4)       AND SET INTO BUFFER HEADER        81151
CMPWNPR  TM    WCMFG2,WCMF2NPR  BYPASS COMPRESSION ?             81151
         BZ    CMPGTLEN      NO; GET COMPRESS LENGTH             81151
         LH    R7,WCMRECLN   GET LENGTH OF COMPRESSED RECORD     81151
         CH    R7,=Y(4)      VALID ?                             81151
         BNH   DCMRCLEN      NO; ERROR                           81151
         LA    R1,2(,R7)     ADD BUFFER OVERHEAD                 81151
         CR    R1,R5         WILL IT FIT INTO ANY BUFFER ?       81151
         BH    DCMRCLEN      NO; FAIL                            81151
         LA    R1,0(R3,R7)   GET BUFFER SIZE AFTER MOVE          81151
         CR    R1,R5         WILL IT FIT THIS ONE ?              81151
         BH    CMPWRITE      NO; REQUEST WRITE ON CURRENT ONE    81151
         STH   R1,WCMBUFLN   SET CURRENT USE                     81151
         TM    WCMFG1,WCMF1NIH   USER PERMITTED NIH FORMAT ?     81151
         BZ    CMPNPNH       NO                                  81151
         TM    0(R7),WCMF3NIF   ANY NIH BITS ON IN RECORD ?      81151
         BZ    CMPNPNH       NO                                  81151
         OI    0(R4),WCMF1NIH  SET IN BUFFER HEADER ALSO         81151
CMPNPNH  AR    R4,R3         GET NEXT BUFFER ADDRESS             81151
         BCTR  R7,0            DECREMENT MOVE LENGTH             81151
         EX    R7,CMPNPMVC   MOVE SEGMENT TO BUFFER              81151
         B     EXIT          RETURN                              81151
CMPNPMVC MVC   0(0,R4),0(R6)   MOVE COMPRESSED SEGMENT           81151
         SPACE 1                                                 81151
CMPGTLEN TM    WCMFG1,WCMF1NIH  USER PERMITTED NIH FORMAT ?      81151
         BZ    CMPGTLNH      NO                                  81151
         NI    WCMFG1,255-WCMF1HWD  TURN OTHER OFF               91175
         B     CMPGTLCH                                          81151
CMPGTLNH MVI   WCMFG3,0      RESET NIH CONTROL BITS              81151
CMPGTLCH TM    WCMFG1,WCMF1INT+WCMF1EDT  LINE #'S FROM RECORD ?  81151
         BNZ   *+8           YES                                 81151
         NI    WCMFG1,255-WCMF1TSO  RESET LEFT LINE NUMBERS      81151
         SR    R5,R3         GET REMAINING SPACE IN BUFFER       81151
         CH    R5,=Y(4)      LONG ENOUGH FOR MINIMUM RECORD ?    81151
         BNH   CMPWRITE      NO; REQUEST BUFFER WRITE NOW        81151
         LH    R7,WCMRECLN   GET USER'S (FIXED) RECORD LENGTH    81151
         TM    WCMFG2,WCMF2RDW  BUT USING V-FORMAT ?             81151
         BZ    CMPGTNV       NO; CHECK IT                        81151
         ICM   R7,3,0(R6)    GET LENGTH FROM RECORD              81151
         SH    R7,=Y(4)      COMPENSATE FOR RDW                  81151
         BM    DCMRCLEN      SIGNAL BAD LENGTH                   81151
         LA    R6,4(,R6)     SKIP OVER RDW TO TEXT               81151
         B     CMPGTNVV      GO TO COMMON                        81151
CMPGTNV  LTR   R7,R7         ANY LENGTH ?                        81151
         BNP   DCMRCLEN      NO; SIGNAL BAD LENGTH               81151
CMPGTNVV TM    WCMFG1,WCMF1INT+WCMF1EDT  EXTRACT LINE # FROM RECORD ?
         BNZ   CMPGTNUM      YES                                 81151
         L     R1,WCMLINE#   GET LINE NUMBER                     81151
         B     CMPCKNUM      GO TO COMMON                        81151
CMPGTNUM SH    R7,=Y(8)      ADJUST REMAINING TEXT LENGTH        81151
         BM    DCMRCLEN      NONE ?                              81151
         TM    WCMFG1,WCMF1TSO  LEFT-ADJUSTED NUMBER ?           81151
         BZ    CMPGTNEW      NO                                  81151
         MVC   WCMLINEB,0(R6)  MOVE FROM FRONT OF RECORD         81151
         LA    R6,8(,R6)     SKIP OVER IT                        81151
         B     CMPGTCNV      GO TO CHECK AND CONVERT             81151
CMPGTNEW LA    R1,0(R6,R7)   POSITION TO NUMBER                  81151
         MVC   WCMLINEB,0(R1)  MOVE TO WORK AREA                 81151
CMPGTCNV TM    WCMFG1,WCMF1INT   INTEGER NUMBERS ?               81151
         BNZ   CMPGTINT      YES                                 81151
         CLI   WCMLINEB+4,C'.'  EDIT FORMAT ?                    81151
         BNE   CMPGTCNP      NO                                  81151
         L     R0,WCMLINEB   GET FIRST FOUR BYTES                81151
         STCM  R0,15,WCMLINEB+1  SHIFT RIGHT                     81151
         MVI   WCMLINEB,C'0'   MAKE LEADING ZERO                 81151
CMPGTCNP LA    R0,8                                              81151
         LA    R1,WCMLINEB+8                                     81151
CMPGTCNL BCTR  R1,0                                              81151
         CLI   0(R1),C' '    BLANK ?                             81151
         BNE   *+8           NO                                  81151
         MVI   0(R1),C'0'    REPLACE BY ZERO                     81151
         BCT   R0,CMPGTCNL   CHECK ALL EIGHT                     81151
CMPGTINT LA    R0,8                                              81151
         LA    R1,WCMLINEB+8                                     81151
CMPGTINL BCTR  R1,0                                              81151
         TM    0(R1),C'0'    NUMERIC ZONE ?                      81151
         BNO   DCMSEQ#       NO; SIGNAL BAD SEQUENCE NUMBER      81151
         CLI   0(R1),C'9'    REALLY NUMERIC ?                    81151
         BH    DCMSEQ#       NO; ERROR                           81151
         BCT   R0,CMPGTINL                                       81151
         PACK  DB,WCMLINEB   PACK THE NUMBER                     81151
         CVB   R1,DB         CONVERT TO BINARY                   81151
CMPCKNUM ST    R1,WCMLINE#   SET AS NEXT LINE NUMBER             81151
         C     R1,WCMLINEP   GREATER THAN PRIOR NUMBER ?         81151
         BNH   DCMSEQSQ      NO; NOT PERMITTED                   81151
         C     R1,=F'99999999'  EXCEEDS MAXIMUM ?                81151
         BH    DCMSEQ#       YES; FAIL                           81151
         ST    R1,DCMPRLIN   SET INTO HOLD AREA                  81151
         TM    WCMFG1,WCMF1OSI  OSI STYLE ?                      81151
         BZ    CMPCKNOS      NO                                  81151
         TM    WCMFG1,WCMF1NIH  SSI STYLE ?                      91175
         BNZ   CMPCKNOS      YES                                 91175
         OI    DCMPRLIN,X'80'   INDICATE FULL-WORD FORMAT        81151
         B     CMPCKNOH                                          81151
CMPCKNOS TM    WCMFG1,WCMF1HWD   HALF-WORD FORMAT ?              81151
         BZ    CMPCKNOH      NO                                  81151
         SR    R0,R0                                             81151
         D     R0,=F'1000'   SPLIT INTO HALF-WORDS               81151
         CH    R0,=Y(9999)   OVERFLOW ?                          81151
         BH    DCMSEQ#       YES; BAD NUMBER                     81151
         STH   R0,DCMPRLIN   SET TOP HALF-WORD                   81151
         STH   R1,DCMPRLIN+2  AND BOTTOM                         81151
CMPCKNOH LA    R1,WYLRECLN   SET MAXIMUM RECORD LENGTH SUPPORTED 81151
         TM    WCMFG1,WCMF1NIH  NIH FORMAT ALLOWED ?             81151
         BZ    *+8           NO                                  81151
         LA    R1,500        ELSE SET NEW NIH MAXIMUM            81151
         TM    WCMFG1,WCMF1NIH+WCMF1OSI  SSI FORMAT ?            91175
         BNO   *+8           NO                                  91175
         LA    R1,255        INTERMEDIATE LENGTH                 91175
         CR    R7,R1         WILL RECORD FIT ?                   81151
         BNH   CMPCKLEN      YES                                 81151
         LR    R7,R1         TRUNCATE IT                         81151
         MVI   RV,VCMRCLEN   SET WARNING FLAG ON                 81151
CMPCKLEN STM   R6,R7,DB      SAVE INPUT RECORD ADDR/LEN OVER PRESS
         SPACE 2                                                 81151
*        ON INPUT TO THIS ROUTINE, PRESS EXPECTS R6 TO POINT TO AN
*        INPUT BUFFER OF EBCDIC CODE. R7 CONTAINS A COUNT OF THE 81151
*        BYTES IN THIS BUFFER.                                   81151
*                                                                81151
         LA    R14,1         MAKE SOME CONSTANTS                 81151
         LA    R15,15                                            81151
         LA    R0,16                                             81151
*        SR    R1,R1         CLEAR HIGH BYTE/DONE @ CMPCKNOH     81165
         LA    R4,DCMPREST   POINT TO START OF COMPRESSED TEXT   81151
CMPROVER L     R7,DB+4       RESTORE ORIGINAL LENGTH             87180
         SR    R7,R6         LESS CURRENT INPUT POSITION         87180
         A     R7,DB         PLUS START                          87180
         SR    R7,R14        REDUCE LENGTH FOR TRT               81151
         BM    CMPALLBL      NO TEXT - JUST SET LENGTH           81151
         LR    R3,R7         SAVE FULL LENGTH                    87180
         MIN   R7,=H'255',TYPE=H  TRUNCATE IF TOO LONG           87180
         EX    R7,CMPTRTBL   TRT TO FIRST NON-BLANK              81151
         BNE   CMPROVES      FOUND NON-BLANK IN RANGE            87180
         LR    R7,R3         RESTORE ORIGINAL LENGTH             87180
         SH    R7,=H'255'    LESS LENGTH JUST CHECKED            87180
         SR    R7,R14        LENGTH FOR NEXT EXECUTE             87180
         BM    CMPALLBL      REALLY ALL TRAILING BLANKS          87180
         EX    R7,CMPTRTB2   CHECK SECOND STRING                 87180
         BE    CMPALLBL        DONE IF ONLY TRAILING BLANKS      81151
CMPROVES LR    R2,R1         SAVE END ADDRESS                    81151
         SR    R1,R6         GET NUMBER OF BLANKS                81151
         LR    R3,R1         AND SAVE FOR LENGTH BYTE            81151
         LR    R6,R2         SET NEXT TRT START ADDRESS          81151
         L     R7,DB+4       RESTORE ORIGINAL LENGTH             87180
         SR    R7,R6         LESS CURRENT INPUT POSITION         87180
         A     R7,DB         PLUS START                          87180
         SR    R7,R14        REDUCE LENGTH FOR TRT               87180
         BM    CMPALLBL      NO TEXT - JUST SET LENGTH ?         87180
         EX    R7,CMPTRTNB   TRT TO FIRST BLANK                  81151
         BNE   CMPRBCKA      DID WE RUN PAST END OF BUF ?        81151
         LA    R1,1(R6,R7)   YES, SET STOP ADDRESS AFTER INPUT   81151
CMPRBCKA CR    R3,R15        MORE THAN 15 BLANKS ?               81151
         BNH   CMPRNXTA      NO, DO NORMALLY                     81151
         MVI   0(R4),X'F0'   YES, SET LENGTH BYTE OF 15 BLANKS   81151
         AR    R4,R14        SET NEXT OUTPUT ADDRESS             81151
         SR    R3,R15        REDUCE BLANK COUNT BY 15            81151
         B     CMPRBCKA      CHECK REMAINING BLANKS              81151
CMPRNXTA SLL   R3,4          SET BLANK COUNT IN LEFT NIBBLE      81151
         SR    R1,R6         COPY NON-BLANK COUNT                81151
         SR    R7,R1               REDUCE TRT COUNT              81151
CMPRBCKB CR    R1,R15        MORE THAN 15 NON-BLANKS ?           81151
         BNH   CMPRNXTB      NO                                  81151
         AR    R3,R15        YES; SET COUNT OF 15 IN LENGTH      81151
         STC   R3,0(,R4)     SET LENGTH BYTE                     81151
         SR    R1,R15        REDUCE NON-BLANK COUNT BY 15        81151
         SR    R3,R3         CLEAR LEADING BLANK COUNT           81151
         MVC   1(15,R4),0(R6)  MOVE 15 NON-BLANKS TO BUFFER      81151
         AR    R6,R15        ADJUST INPUT ADDRESS                81151
         AR    R4,R0         ADJUST OUTPUT ADDRESS               81151
         CR    R1,R15        MORE THAN 15 BLANKS REMAINING ?     87180
         BL    CMPROVER      NO; COMPUTE TRUE RESIDUAL LENGTH    87180
         B     CMPRBCKB      DO NEXT NON-BLANK GROUP             81151
CMPRMVC  MVC   1(0,R4),0(R6)   MOVE TEXT STRING                  81151
CMPTRTBL TRT   0(0,R6),CMPNBTRT  SCAN FOR NON-BLANK              81151
CMPTRTB2 TRT   256(0,R6),CMPNBTRT  SCAN FOR NON-BLANK            87180
CMPTRTNB TRT   0(0,R6),CMPBLTRT  SCAN FOR BLANK                  81151
CMPRNXTB OR    R3,R1         PUT NON-BLANK COUNT IN LENGTH       81151
         STC   R3,0(,R4)     SET NEXT LENGTH FIELD               81151
         BCTR  R1,0          SET MOVE LENGTH FOR EXECUTE         81151
         EX    R1,CMPRMVC    MOVE TEXT                           81151
         LA    R6,1(R1,R6)   ADJUST INPUT POINTER                81151
         LA    R4,2(R1,R4)   SET NEXT OUTPUT ADDRESS             81151
         B     CMPROVER      MORE INPUT TEXT ?                   87180
         SPACE 1                                                 87180
CMPALLBL LR    R3,R4         GET CURRENT OUTPUT LOCATION         82018
         LA    R2,DCMPREST   POINT TO START OF TEXT              82018
         SR    R3,R2         SET LENGTH                          82018
         STH   R3,DCMPRESL   INTO LENGTH FIELD                   82018
         SPACE 1                                                 81151
         L     R4,WCMBUFAD   GET BUFFER ADDRESS                  82018
         LH    R5,WCMBUFLN   GET CURRENT USE                     82018
         LA    R8,0(R4,R5)   GET MOVE ADDRESS                    82018
         SR    R15,R15       SET LENGTH OF LENGTH FIELD - 1      81151
         TM    WCMFG1,WCMF1NIH  NIH FORMAT ?                     81151
         BZ    CMPCNNIH      NO                                  81151
         NI    WCMFG3,WCMF3NSP+WCMF3MRK+WCMF308  LEAVE USER BITS 81151
         CH    R3,DB+6       ORIGINAL EXCEEDS COMPRESSED LENGTH ?
         BL    CMPCNRAW      NO; USE COMPRESSED TEXT             81151
         OI    WCMFG3,WCMF3RAW  SET TO USE UNCOMPRESSED TEXT     81151
         LH    R3,DB+6       GET ORIGINAL LENGTH                 82018
         STH   R3,DCMPRESL   AND SET INTO LENGTH FIELD           82018
CMPCNRAW SR    R15,R15       SET LENGTH OF LENGTH -1             89313
         CLI   DCMPRESL,0    TWO BYTE LENGTH FIELD ?             89313
         BE    CMPCNL16      NO                                  89313
         OI    WCMFG3,WCMF3L16  YES; SET DOUBLE-BYTE LENGTH      89313
         LA    R15,1         PRESET FOR TWO-BYTE LENGTH FIELD    89313
CMPCNL16 LA    R14,5(R3,R5)  GET NEW LENGTH IN BUFFER            82018
         AR    R14,R15                                           81151
         CH    R14,WCMBUFMX  WILL IT FIT ?                       81151
         BH    CMPWRITE      NO; WRITE BUFFER FIRST              81151
         OC    DCMPRLIN(1),WCMFG3  SET NIH FLAGS                 81151
         TM    WCMFG3,WCMF3NIF  ANY SET ?                        81151
         BZ    CMPCNNIH      NO                                  81151
         OI    0(R4),WCMF1NIH  ALSO SET NIH OPTION IN BUFFER     82018
CMPCNNIH LA    R14,5(R3,R5)  GET NEW BUFFER SIZE                 82018
         AR    R14,R15         +LENGTH LENGTH                    81151
         CH    R14,WCMBUFMX  WILL IT FIT ?                       81151
         BH    CMPWRITE      NO; REQUEST WRITE                   81151
         STH   R14,WCMBUFLN  SET NEW LENGTH                      81151
         MVC   0(4,R8),DCMPRLIN  MOVE LINE NUMBER                81151
         LA    R1,5(R8,R15)  SET TEXT LOCATION FOR ENCRYPTION    81151
         LR    R0,R3         SET TEXT LENGTH                     82018
         LA    R2,DCMPRESL+1   POINT TO 1-BYTE LENGTH FIELD      82018
         SR    R2,R15        ADJUST FOR L16                      82018
         AR    R3,R15        ADJUST MOVE LENGTH                  82018
         TM    WCMFG3,WCMF3RAW  MOVE UNCOMPRESSED ?              81151
         BZ    CMPCMMVC      NO                                  81151
         EX    R15,CMPMVCTX   MOVE LENGTH FIELD ONLY             81151
         LA    R8,1(R8,R15)  ADJUST DESTINATION                  81151
         LM    R2,R3,DB      GET USER ADDRESS/LENGTH BACK        82018
         SH    R3,=Y(1)      ADJUST FOR LENGTH ALREADY MOVED     82018
         BM    CMPCMDON      NOTHING TO MOVE                     81151
CMPCMMVC CH    R3,=H'256'    LONGER THAN ONE SEGMENT ?           87179
         BL    CMPCMMVD      NO                                  87179
         MVC   4(256,R8),0(R2)  MOVE ONE SEGMENT                 87179
         LA    R8,256(,R8)                                       87179
         LA    R2,256(,R2)                                       87179
         SH    R3,=H'256'                                        87179
CMPCMMVD EX    R3,CMPMVCTX   MOVE USER OR COMPRESSED DATA        82018
CMPCMDON BAS   R14,CMPCODE   ENCRYPT THE TEXT                    81151
         L     R1,WCMLINE#   GET CURRENT LINE NUMBER             81151
         C     R1,WCMLINEP   HIGHER THAN PREVIOUS ?              81151
         BNH   DCMSEQ#       NO; SIGNAL ERROR                    81151
         ST    R1,WCMLINEP   SET CURRENT AS NEW PREVIOUS         81151
         A     R1,WCMDELTA   SET NUMBER FOR NEXT LINE            81151
         ST    R1,WCMLINE#                                       81151
         B     EXIT          RETURN OK                           81151
CMPMVCTX MVC   4(0,R8),0(R2)   MOVE TEXT TO BUFFER               82018
         SPACE 1                                                 81151
CMPNBTRT DC    64X'1'        TABLE FOR BLANK SCAN                81151
         DC    X'00',191X'1'                                     81151
CMPBLTRT DC    64X'0'        TABLE FOR NON-BLANK SCAN            81151
         DC    X'1',191X'0'                                      81151
         SPACE 1                                                 81151
DSCNBTRT DC    256AL1(4)     SET ALL CHARACTERS INVALID          81151
         ORG   DSCNBTRT+C' '                                     81151
         DC    AL1(12)       BLANK                               81151
         ORG   DSCNBTRT+C'.'                                     81151
         DC    AL1(8)        PERIOD (INDEX LEVEL)                81151
         ORG   DSCNBTRT+C'$'                                     81151
         DC    AL1(0)        NATIONAL                            81151
         ORG   DSCNBTRT+C'#'                                     81151
         DC    AL1(0)                                            81151
         ORG   DSCNBTRT+C'@'                                     81151
         DC    AL1(0)                                            81151
         ORG   DSCNBTRT+C'A'                                     81151
         DC    9AL1(0)       A-I                                 81151
         ORG   DSCNBTRT+C'J'                                     81151
         DC    9AL1(0)       J-R                                 81151
         ORG   DSCNBTRT+C'S'                                     81151
         DC    8AL1(0)       S-Z                                 81151
         ORG   DSCNBTRT+C'0'                                     81151
         DC    10AL1(0)      0-9                                 81151
         ORG   ,                                                 81151
         EJECT ,                                                 81145
*        DECOMPRESSION OF WYLBUR BUFFERS                         81151
*        INITIALIZATION - USER DECLARES WORK AREA USING SERVCOMP 81151
*        WCMCODAD TO BE SET TO CL16 AREA WITH CODE WORD IF DECRYPTION
*          IS REQUIRED, ELSE ZERO.                               81151
*        WCMRECMX TO BE SET TO MAXIMUM RECORD LENGTH USER CAN HANDLE.
*        LINE NUMBER OPTIONS SHOULD BE SET INTO WCMFG1 (SEE COMPRESS
*          DESCRIPTION - PROCESSING IS REVERSED). IN ADDITION, USER
*          MAY SPECIFY WCMF1LCC - IF WCMF1TSO IS ON, THEN THE FIRST
*          TEXT BYTE PRECEDES THE LINE NUMBER (CARRIAGE CONTROL).
*          WCMF1NB# MAY BE SET TO INDICATE GENERATED LINE NUMBERS
*          ARE TO BE PLACED INTO THE RECORD EVEN IF THE CL8 AREA 81151
*          IS NON-BLANK.                                         81151
*        ON A BUFFER BASIS, THE USER STORES THE BUFFER ADDRESS AND
*          LENGTH IN WCMBUFAD/WCMBUFMX. WHEN THE BUFFER IS EXHAUSTED,
*          A RETURN CODE OF 4 IS ISSUED TO SIGNAL THAT A READ IS NEEDED
*        ON A RECORD BASIS, THE USER SETS THE RECORD ADDRESS IN  81151
*          WCMRECAD.  THE TEXT LENGTH WILL BE RETURNED IN WCMRECLN
*          (INCLUDING LINE NUMBERS, IF ANY).                     81151
*          A ZERO RECORD ADDRESS WILL BE REPLACED BY A WORK RECORD
*          FOR LOCATE MODE PROCESSING.
*        WCMF2RDW CAUSES A V-FORMAT RECORD TO BE RETURNED IN THE 81151
*          USER'S AREA. WCMRECMX SHOULD ALLOW FOR THE ADDITIONAL 81151
*          FOUR BYTES. WCMRECLN WILL NOT INCLUDE THE 4 BYTE LENGTH.
         SPACE 1                                                 81151
ENTWDCOM LA    R14,DCMRECLN  SET MAXIMUM RECORD LENGTH SUPPORTED 81187
         ICM   R0,7,WCMRECAD+1  ANY RECORD ADDRESS ?             81187
         BNZ   DCMNLOC                                           81187
         LA    R0,DCMUSREC   USE OUR WORK RECORD                 81187
         ST    R0,WCMRECAD     FOR LOCATE MODE                   81187
         ICM   R0,3,WCMRECMX   DID USER SPECIFY LENGTH ?         81187
         BNZ   DCMNLOC       YES; LEAVE IT ALONE                 81187
         STH   R14,WCMRECMX  ELSE SET MAXIMUM AS DEFAULT         81187
DCMNLOC  SR    R0,R0                                             81187
         ICM   R0,3,WCMRECMX   GET USER'S MAXIMUM LENGTH         81145
         BNP   DCMRCLEN      SET BAD                             81145
         CR    R0,R14        LARGER THAN MAX ?                   81145
         BNH   *+12          NO                                  81145
         STH   R14,WCMRECMX  TRUNCATE IT                         81145
         MVI   RV,VCMRCLEN   WARNING - TRUNCATION                81145
         LA    R14,1         SET MINIMUM RECORD LENGTH REQUIRED  81145
         TM    WCMFG1,WCMF1INT+WCMF1EDT  NUMBERED ?              81145
         BZ    *+8           NO                                  81145
         LA    R14,8(,R14)   UP BY LENGTH OF NUMBER              81145
         CR    R0,R14        AT LEAST MINIMUM LENGTH ?           81145
         BL    DCMRCLEN      NO; SET BAD LENGTH                  81145
         NI    WCMFG1,255-WCMF1NIH  TURN OFF NIH FORMAT BIT      83297
         MVI   WCMFG3,0      RESET NIH LINE FLAGS                83297
         ICM   R2,3,WCMBUFMX   GET BUFFER SIZE                   83297
         ICM   R3,3,0(R4)    GET PURPORTED BLOCK LENGTH          83297
         BNM   DCMNNIHB      NOT NEGATIVE; NOT NIH FORMAT        83297
         OI    WCMFG1,WCMF1NIH  TURN NIH FORMAT ON               83297
         MVI   WCMFG3,WCMF3NIH  TURN ON THE LINE NIH FLAG ALSO   83297
DCMNNIHB LH    R9,=X'7FFF'   MAKE MASK                           83297
         NR    R3,R9         MASK BLOCK DESCRIPTOR               83297
         BZ    DCMBKLEN      FAIL; INVALID BLOCK LENGTH          83297
         NR    R2,R9         MAKE USER'S SUPPLIED LENGTH         83297
         BZ    DCMNBFSZ      NOT SUPPLIED; ASSUME OK ?           83297
         CR    R3,R2         WILL BLOCK FIT INTO BUFFER ?        83297
         BH    DCMBKLEN      NO; FAIL THE REQUEST                83297
DCMNBFSZ STH   R3,WCMBUFMX   PLACE TRUE LENGTH INTO WORK AREA    83297
DCMRSTRT LH    R9,WCMBUFLN   GET CURRENT BUFFER OFFSET           91175
         CH    R9,WCMBUFMX   MORE IN THIS BLOCK ?                81145
         BNL   DCMREAD       NO; READ ANOTHER                    81145
         CH    R9,=Y(2)      FIRST TIME ?                        81145
         BNL   DCMNEWLN      NO; DO NEXT LINE                    81145
         TM    WCMFG1,WCMF1INT+WCMF1EDT  ANY NUMBERING ?         81145
         BNZ   *+8           YES                                 81145
         NI    WCMFG1,255-WCMF1TSO   TURN LEFT-ADJUST OFF        81145
         SPACE 1                                                 81145
*        REG   USE                                               81145
*        R2    RECORD BYTE COUNT FOR VALIDITY CHECK              81145
*        R3    IC/ICM FOR LENGTH                                 81145
*        R5    -> LINE #                                         81145
         SPACE 1                                                 81145
         LA    R4,2(,R4)          POINT R4 AT LINE NUMBER        81145
         LR    R5,R4              POINTER TO INBUF IN R5         81145
         LA    R2,2               SET RECORD BYTE COUNTER        81145
DCMUP    SR    R3,R3               ASSUME 8-BIT LENGTH FIELD     81145
         LA    R14,5(,R2)    GET NEW MINIMUM LENGTH              91253
         CH    R14,WCMBUFMX  STILL IN BUFFER ?                   91253
         BH    DCMNEBIG      NO; AVOID 0C4                       91253
         IC    R3,4(,R5)                                         81145
         TM    WCMFG3,WCMF3NIH        NEW NIH FORMAT ?           81145
         BZ    DCMUPOLD             NO  - CHECK OLD LIMIT        81145
         TM    0(R5),WCMF3L16       YES - 16-BIT LENGTH FIELD ?  81145
         BZ    DCMUPCOM                 - NO  - ACCEPT LENGTH    89313
         TM    WCMFG1,WCMF1OSI  OSI/SSI FORMAT ?                 91175
         BNZ   DCMUPCOM      YES; L16 BIT HAS OTHER USE          91175
         LA    R14,1(,R14)   BUMP ONE MORE                       91253
         CH    R14,WCMBUFMX  STILL IN BUFFER ?                   91253
         BH    DCMNEBIG      NO; AVOID 0C4                       91253
         ICM   R3,B'0011',4(R5)         - YES - GET LENGTH       81145
         CH    R3,=Y((500/15+1)*16)                              81145
*                                  MAX. PREST COUNT EXCEEDED ?   81145
         BH    DCMNENIH             YES - ERROR                  91175
         TM    0(R5),WCMF3RAW        NIH UNCOMPRESSED ?          81145
         BZ    DCMUP1               NO  - PASSED CHECK           81145
         CH    R3,=Y(500)           YES - VALID SIZE ?           81145
         BH    DCMNENIH                 - NO  - ERROR            91175
DCMUP1   LA    R3,1(,R3)           FUDGE                         81145
         B     DCMUPCOM            REJOIN DCMUP MAIN LINE        81145
DCMNEBIG TM    WCMFG3,WCMF3NIH  NIH FORMAT ?                     91253
         BZ    DCMNEDIT      NO; OVERRUN DUE TO ERROR?           91253
DCMNENIH TM    WCMFG1,WCMF1OSI  BEEN HERE BEFORE ?               91253
         BNZ   DCMNEDIT      YES; REAL ERROR                     91253
         OI    WCMFG1,WCMF1OSI  RETRY AS OSI/SSI FORMAT          91175
         L     R4,WCMBUFAD                                       91175
         B     DCMRSTRT      AND RETRY THIS BUFFER               91175
DCMUPOLD CH    R3,=Y((WYLRECLN/15+1)*16)                         87347
*                                  MAX. PREST COUNT EXCEEDED ?   81145
         BH    DCMNEDIT           YES - ERROR                    81145
DCMUPCOM LA    R2,5(R2,R3)        BUMP RECORD COUNT BY PREST CT  81145
         LA    R5,5(R5,R3)        BUMP POINTER PAST THIS LINE    81145
         CH    R2,WCMBUFMX        CUM COUNT > TOTAL BYTE COUNT ? 81145
         BH    DCMNEBIG           YES - ERROR (OR OSI)           91253
         BL    DCMUP              GO ADD NEXT LINE COUNT         81145
         SPACE 1                                                 81145
*        REG   USE IN DEBLOCKING PREST LINES                     81145
*        R1       NONBLANK MSK FOR PREST                         81145
*                 NONBLANK CNT FOR PREST                         81145
*        R2    -> DCMREC DATA                                    81145
*        R3       LENGTH FIELD                                   81145
*        R4    -> LINE # IN CURRENT RECORD                       81145
*              -> PREST BYTE                                     81145
*              -> TEXT                                           81145
*        R5       LENGTH FIELD                                   81145
*                 BLANK  COUNT FOR PREST                         81145
*                 EX REG       FOR PREST                         81145
*        R7    BYTE COUNT FOR OUTPUT OVERFLOW TEST               81145
*        R9    BYTE COUNT FOR EOB TEST                           81145
         SPACE 1                                                 81145
         LA    R9,2               SET RECORD BYTE COUNTER        81145
         B     DCMNXTLN      GET FIRST LINE                      81145
DCMNEWLN BAS   R14,DCMGETUP  ADJUST PAST PRIOR LINE              81145
         AR    R9,R3         POINT TO NEXT RECORD                81145
DCMNXTLN L     R4,WCMBUFAD   GET BUFFER START ADDRESS            81145
         CH    R9,WCMBUFMX   DOUBLE-CHECK                        81145
         BNL   DCMREAD       GET ANOTHER BLOCK                   81145
         AR    R4,R9         GET CURRENT LINE                    81145
         STH   R9,WCMBUFLN   SAVE CURRENT BUFFER OFFSET          81145
         TM    WCMFG2,WCMF2NPR   NO UNBLOCKING ?                 81145
         BZ    DCMNXTBK      WRONG; WASTE TIME                   81145
         ST    R4,WCMRECAD   GIVE USER CURRENT RECORD (LOCATE MODE)
         BAS   R14,DCMGETLN  GET LENGTH OF LINE                  81145
         STH   R15,WCMRECLN  PASS IT BACK                        81145
         B     EXIT          AND EXIT WITHOUT ANY OTHER WORK     81145
DCMNXTBK LA    R0,DCMREC          BLANK DCMREC                   81145
         LA    R1,DCMRECLN                                       81145
         SR    R14,R14                                           81145
         SR    R15,R15                                           81145
         ICM   R15,B'1000',BLANKS                                81145
         MVCL  R0,R14                                            81145
         LA    R0,4          SET RDW LENGTH                      81145
         SLL   R0,16                                             81145
         ST    R0,DCMVREC    INTO RDW                            81145
         MVC   DB(1),0(R4)   COPY NIH FLAGS                      81151
         NI    DB,WCMF3NIF   LEAVE ONLY FLAG BITS                81151
         NI    WCMFG3,WCMF3NIH  RESET FLAG BITS                  81151
         OC    WCMFG3,DB     SET FLAG BITS                       81151
         LA    R2,DCMREC          R2-> 1ST OUTPUT BUFFER LOC.    81145
         MVC   WCMLINE#(4),0(R4)  MOVE LINE NUMBER TO USER FIELD 81145
         SPACE 1                                                 81145
*              PROCESS LINE NUMBERS                              81145
         SPACE 1                                                 81145
         TM    WCMFG3,WCMF3NIH        NIH FORMAT ?               81145
         BO    DCMFULLN             YES - DO FULLWORD FORMAT     81145
         TM    WCMFG1,WCMF1HWD   USER THINKS IT'S HALF-WORD ?    81145
         BNZ   DCMHALFW      YES; TRY IT                         81145
         TM    WCMFG1,WCMF1OSI   USER REQUESTED OSI STYLE ?      81145
         BZ    DCMFULLW      NO; MUST BE FULLWORD BY DEFAULT ?   81145
         TM    WCMFG3,X'80'  OSI STYLE FULLWORD BIT ?            81145
         BZ    DCMHALFW      NO; SHOULD BE HALF-WORD             81145
         NI    WCMLINE#,X'7F'  RESET OSI FULL-WORD BIT           81145
         B     DCMFULLW      GO TO DO IT                         81145
DCMHALFW TM    WCMLINE#,X'C0'   OVERFLOW ?                       81145
         BNZ   DCMSEQ#       YES- FAIL IT                        81145
         LA    R14,1000      MAKE SCALING FACTOR                 81145
         LH    R1,WCMLINE#   GET INTEGER PORTION                 81145
         CH    R1,=Y(9999)   LEGAL ?                             81145
         BH    DCMSEQ#       NO; FAIL                            81145
         MR    R0,R14        SCALE                               81145
         ICM   R0,3,WCMLINE#+2   GET SUPPOSED DECIMAL PORTION    81145
         CR    R0,R14        IS IT VALID ?                       81145
         BNL   DCMSEQ#       NO                                  81145
         AR    R1,R0         ELSE ADD IT IN                      81145
         B     DCMCOMMW      GO TO COMMON                        81145
DCMFULLW TM    WCMLINE#,255-X'07'  INVALID BITS ON ?             81145
         BNZ   DCMSEQ#       YES; FAIL BAD SEQUENCE FIELD        81145
DCMFULLN NI    WCMLINE#,X'07'  RESET NIH FLAG BITS               81145
         L     R1,WCMLINE#   GET FULL-WORD VALUE                 81145
         C     R1,=F'99999999'  VALID ?                          81145
         BH    DCMSEQ#       NO                                  81145
DCMCOMMW ST    R1,WCMLINE#   STASH IT BACK                       81145
         CVD   R1,DB         MAKE PACKED                         81145
         UNPK  WCMLINEB,DB+3(5)  MAKE ZONED                      81145
         OI    WCMLINEB+7,C'0'  MAKE GOOD ZONE                   81145
         TM    WCMFG1,WCMF1INT  INTEGER NUMBER ?                 81145
         BNZ   DCMEDBCK      YES; SKIP FINAGLING                 81145
         MVI   WCMLINEH,C' '   SET THE SPILL BYTE BLANK          81236
         CLI   WCMLINEB,C'0'   LEADING ZERO ?                    81145
         BE    DCMNSPIL      YES; OK                             81236
         MVI   RV,VCMSEQ#    SHOW SEQUENCE TRUNCATION (WARNING)  81145
         MVC   WCMLINEH,WCMLINEB  SAVE THE HIGH BYTE FOR USER    81236
DCMNSPIL MVC   WCMLINEB(4),WCMLINEB+1  SHIFT OVER                81236
         MVI   WCMLINEB+4,C'.'  MAKE DECIMAL POINT               81145
         CLI   WCMLINEH,C' '   9-BYTE NUMBER ?                   81270
         BNE   DCMEDLST      YES; NO ZERO SUPPRESS ON TOP FOUR   81270
         LA    R3,WCMLINEB        SET POINTER TO 1ST DIGIT OF LINE #
DCMEDNXT CLI   0(R3),C'0'         IS DIGIT ZERO ?                81145
         BNE   DCMEDLST           NO                             81145
         MVI   0(3),C' '          YES-CHANGE TO BLANK            81145
         LA    R3,1(,R3)          BUMP  POINTER                  81145
         B     DCMEDNXT           ARE THERE MORE ?               81145
DCMEDLST LA    R3,WCMLINEB+7      POINT TO LAST DIGIT OF LINE #  81145
DCMEDSTP CLI   0(R3),C'0'         IS DIGIT ZERO ?                81145
         BNE   DCMEDBCK           JOIN COMMON                    81145
         MVI   0(R3),C' '         YES-CHANGE TO BLANK            81145
         BCT   R3,DCMEDSTP        REDUCE TRAILING ZERO POINTER   81145
DCMEDBCK TM    WCMFG1,WCMF1TSO   LEFT-ADJUSTED LINE NUMBERS ?    81145
         BZ    DCMNONUM      NO                                  81145
         MVC   0(8,R2),WCMLINEB   MOVE TO RECORD                 81145
         LA    R0,4+8        SET CURRENT RECORD LENGTH           81145
         STH   R0,DCMVREC    BACK INTO RDW                       81145
         LA    R2,8(,R2)          ALLOW FOR LINE #               81145
DCMNONUM BAS   R14,DCMGETLN  GET LENGTH AND ADJUST R4/R9         81145
         SR    R7,R7              ZERO OUT OUTPUT COUNTER        81145
         LTR   R5,R3              ANYTHING TO MOVE ?             81145
         BNP   DCMLNOUT            NO  - GO WRITE LINE           81145
         LR    R0,R3         COPY LENGTH                         81151
         LR    R1,R4         COPY START TEXT ADDRESS             81151
         BAS   R14,CMPCODE   DECRYPT                             81151
*                                  YES - PREST TEXT ?            81145
         TM    WCMFG3,WCMF3RAW+WCMF3NIH                          81145
         BNO   DCMMORE                 -  YES - DECOMPRESS IT    81145
         LR    R7,R3         SET DATA LENGTH FOR RDW             81145
         MVCL  R2,R4                   -  NO  - MOVE IT ALL      81145
         B     DCMLNOUT                -        AND GO WRITE LINE
         SPACE 1                                                 81145
DCMMOVER MVC   0(0,R2),1(R4)      EXECUTED FOR DECOMPRESS        81145
         SPACE 2                                                 81145
DCMMORE  IC    R5,0(,R4)          LOAD PREST COUNTER BYTE        81145
         LA    R1,15              MASK FOR NONBLANK COUNT        81145
         NR    R1,R5              GET NONBLANK COUNT             81145
         SRL   R5,4               GET BLANK    COUNT             81145
         AR    R2,R5              SKIP OUTPUT PTR OVER BLANKS    81145
         AR    R7,R5              BUMP OUTPUT COUNT              81145
         AR    R7,R1                                             81145
         CH    R7,=Y(500)          MAXIMUM LINE EXCEEDED ?       81145
         BH    DCMNEDIT             YES - ERROR                  81145
         SR    R3,R1              DROP INPUT CNT BY NONBLANK CNT 81145
         BNP   DCMNEDIT           MUST BE 1 LEFT FOR PREST BYTE  81145
         LTR   R5,R1              ANY NONBLANKS ?                81145
         BZ    DCMCHECK            NO  - CHECK END-OF-LINE       81145
         BCTR  R5,0               DECR NONBLANK COUNT FOR EX     81145
         EX    R5,DCMMOVER        MOVE NONBLANKS                 81145
         AR    R2,R1              MOVE OUTPUT PTR OVER NONBLANKS 81145
DCMCHECK LA    R4,1(R1,R4)        MOVE INPUT PTR OVER NONBLANKS  81145
         BCT   R3,DCMMORE         END-OF-LINE ?                  81145
*                                  YES - GO WRITE LINE           81145
         SPACE 1                                                 81145
DCMLNOUT CLC   WCMLINEP,WCMLINE#  LINE NUMBERS IN SEQUENCE ?     81145
         BL    DCMLNOUQ      YES                                 81145
         MVI   RV,VCMSEQSQ   SET WARNING - OUT OF SEQUENCE       81145
DCMLNOUQ MVC   WCMLINEP,WCMLINE#  SAVE FOR NEXT TIME AROUND      81145
         LH    R8,WCMRECMX   GET USER'S LENGTH                   81145
         LH    R0,DCMVREC    GET RDW OVERHEAD (4 OR 12/TSO)      81145
         AR    R0,R7         GET TOTAL LENGTH                    81145
         SH    R0,=Y(4)      SUBTRACT RDW                        81145
         CR    R0,R8         COMPARE TO USER'S LENGTH            81145
         BNH   DCMLNTRU      OK                                  81145
         LR    R7,R8         TRUNCATE THE RECORD                 81145
         MVI   RV,VCMRCLEN   SHOW TRUNCATION                     81145
DCMLNTRU TM    WCMFG1,WCMF1TSO   LEFT-ADJUSTED LINE NUMBERS ?    81145
         BNZ   DCMLNLEN      YES; SET LINE LENGTH                81145
         TM    WCMFG1,WCMF1INT+WCMF1EDT  ANY NUMBERS ?           81145
         BZ    DCMLNLEN      NO; SET LINE LENGTH                 81145
         CR    R8,R7         LOTS OF BLANKS AT END ?             81145
         BNH   DCMLNCLP      NO                                  81145
         LR    R7,R8         ADD LINE NUMBERS AT END             81145
DCMLNCLP LA    R8,DCMREC-8(R7)  GET LINE NUMBER LOCATION         81145
         TM    WCMFG1,WCMF1NB#   UNCONDITIONAL NUMBER ?          81145
         BNZ   DCMLNMVC      YES; MOVE IT                        81145
         CLC   0(8,R8),BLANKS IS IT BLANK ?                      81145
         BNE   DCMLNLEN      NO; DON'T CREAM DATA                81145
DCMLNMVC MVC   0(8,R8),WCMLINEB  MOVE LINE NUMBER                81145
DCMLNLEN AH    R7,DCMVREC    MAKE NEW LENGTH                     81145
         LA    R14,5         SET MINIMUM VALID LENGTH            81151
         CR    R7,R14        AT LEAST MINIMUM ?                  81151
         BNL   *+6           YES                                 81151
         LR    R7,R14        USE MINIMUM                         81151
         STH   R7,DCMVREC    SET IT BACK                         81145
         TM    WCMFG1,WCMF1TSO+WCMF1LCC  LEADING CARRIAGE CONTROL ?
         BNO   DCMWRITE      NO; DO FINAL RITES                  81145
         MVC   DB,DCMREC     SAVE LINE NUMBER                    81145
         MVC   DCMREC(1),DCMREC+8  MOVE CARRIAGE CONTROL         81145
         MVC   DCMREC+1(8),DB   RESTORE LINE NUMBER              81145
*                                                                81145
*        OUTPUT RECORDS HERE                                     81145
*                                                                81145
DCMWRITE L     R14,WCMRECAD  GET USER'S RECORD ADDRESS           81145
         LH    R15,WCMRECMX  GET USER'S MAXIMUM LENGTH           81145
         SR    R0,R0         SET V-FORMAT FINAGLE OVERHEAD       81145
         LA    R1,4          SET OTHER                           81145
         TM    WCMFG2,WCMF2RDW  V-FORMAT RETURN ?                81145
         BZ    *+8           NO                                  81145
         LR    R0,R1         SET V-FORMAT OVERHEAD               81145
         SR    R1,R1         BUT SET OTHER TO 0                  81145
         AR    R15,R0        ALLOW FOR RDW                       81145
         LA    R2,DCMVREC    GET V-HEADER                        81145
         LH    R3,DCMVREC    GET CURRENT LENGTH                  81145
         LR    R4,R3         COPY IT                             81145
         AR    R2,R1         SET TEXT LOCATION                   81145
         SR    R3,R1         ADJUST LENGTH                       81145
         ICM   R3,8,BLANKS     SET PADDING BYTE                  81145
         MVCL  R14,R2        MOVE TO USER                        81145
         SR    R4,R1                                             81145
         SR    R4,R0         SET TEXT LENGTH                     81145
         STH   R4,WCMRECLN   INTO CURRENT LENGTH                 81145
         B     EXIT          EXIT TO USER                        81145
         SPACE 1                                                 81145
*        SUBROUTINE TO GET CURRENT RECORD LENGTH                 81145
*                                                                81145
DCMGETUP L     R4,WCMBUFAD   GET BUFFER ADDRESS                  81145
         CH    R9,WCMBUFMX   DOUBLE-CHECK OFFSET                 81145
         BNL   DCMREAD       GET ANOTHER BLOCK                   81145
         AR    R4,R9         SET TO CURRENT LINE                 81145
DCMGETLN LR    R15,R9        REMEMBER STARTING POSITION          81145
         SR    R3,R3              CLEAR FOR IC/ICM               81145
         IC    R3,4(,R4)          ASSUME 8-BIT LENGTH            81145
         MVC   DB(1),0(R4)   COPY NIH FLAGS                      81151
         NI    DB,WCMF3NIF   LEAVE ONLY FLAG BITS                81151
         NI    WCMFG3,WCMF3NIH  RESET FLAG BITS                  81151
         OC    WCMFG3,DB     SET FLAG BITS                       81151
         TM    WCMFG3,WCMF3NIH   NIH FORMAT ?                    83289
         BZ    DCMGETL8      NO; ONLY 8 BIT RECORD LENGTH        83289
         TM    WCMFG3,WCMF3L16  TWO-BYTE LENGTH ?                83289
         BZ    DCMGETL8      NO; ONLY 8 BIT RECORD LENGTH        89313
         TM    WCMFG1,WCMF1OSI  OSI/SSI FORMAT ?                 91175
         BNZ   DCMGETL8      YES; L16 BIT HAS OTHER USE          91175
         ICM   R3,B'0011',4(R4)   GET 16-BIT LENGTH              81145
         LA    R9,1(,R9)          ADJUST CTR FOR 16-BIT LENGTH   81145
         LA    R4,1(,R4)          ADJUST BLOCK FOR 16-BIT LENGTH 81145
DCMGETL8 LA    R9,5(,R9)     UPDATE INPUT BLOCK COUNT            81145
         LA    R4,5(,R4)     POINT TO TEXT START                 81145
         SR    R15,R9        GET DIFFERENCE                      81145
         LPR   R15,R15       GET LINE OVERHEAD                   81145
         AR    R15,R3        ADD TEXT LENGTH                     81145
         BR    R14           RETURN TO CALLER                    81145
         SPACE 1                                                 81145
DCMPARM  MVI   RV,VCMPARM    INVALID PARAMETER LIST              81145
         B     BADRET8       SET LEVEL 8 ERROR                   81145
DCMNEDIT MVI   RV,VCMNEDIT   NOT EDIT FORMAT                     81145
         B     BADRET8                                           81145
DCMBKLEN MVI   RV,VCMBKLEN   INVALID BLOCK LENGTH                81145
         B     BADRET8                                           81145
DCMRCLEN MVI   RV,VCMRCLEN   INVALID RECORD LENGTH               81145
         B     BADRET8                                           81145
DCMSEQ#  MVI   RV,VCMSEQ#    INVALID SEQUENCE NUMBER             81145
         B     BADRET8       SET ERROR CODE                      81145
DCMSEQSQ MVI   RV,VCMSEQSQ   LINE NUMBER OUT OF SEQUENCE         81151
         B     BADRET8                                           81151
         SPACE 1                                                 81145
DCMREAD  XC    WCMBUFLN,WCMBUFLN  SET BUFFER OFFSET FOR NEXT TIME
         B     BADRET4       SET WARNING CODE - NEW BUFFER REQUIRED
         SPACE 2                                                 81151
*        ENCRYPTION/DECRYPTION SUBROUTINE                        81151
*        R0 - LENGTH (MAY BE ZERO)                               81151
*        R1 - TEXT ADDRESS                                       81151
*        R14 - RETURN ADDRESS                                    81151
*                                                                81151
CMPCODE  TM    WCMFG2,WCMF2COD+WCMF2PSW  WORK TO BE DONE ?       81151
         BO    CMPCODER      YES; CONVERT                        81151
         BMR   R14           NO; RETURN                          81151
         OI    WCMFG2,WCMF2COD  SHOW CHECKING DONE               81151
         STM   R0,R6,20(R13) SAVE CALLER'S REGISTERS             81151
         SR    R15,R15       ZERO HIGH BYTE                     GP99147
         ICM   R15,7,WCMCODAD+1  CODEWORD ADDRESS SUPPLIED ?     81151
         BZR   R14           NO; JUST GET OUT                    81151
         CLI   0(R15),C' '   NULL ?                              81151
         BER   R14           DITTO                               81151
         LA    R0,4          DO FOUR WORDS                       81151
         SR    R1,R1         CLEAR ACCUMULATOR                   81151
CMPCODEC A     R1,0(,R15)    ADD ANOTHER WORD IN                 81151
         LA    R15,4(,R15)   POINT TO NEXT WORD                  81151
         BCT   R0,CMPCODEC   DO ALL FOUR                         81151
         ST    R1,WCMCODAD   REPLACE IN WORK AREA                81151
         OI    WCMFG2,WCMF2PSW  SHOW TRANSLATION REQUIRED        81151
         LM    R0,R1,20(R13)  RELOAD ENTRY PARMS                 81151
CMPCODER STM   R2,R6,28(R13) SAVE CALLER'S REGISTERS             81151
         LTR   R0,R0         ANY LENGTH ?                        81151
         BNPR  R14           NO; JUST RETURN                     81151
         LR    R15,R1        COPY START ADDRESS                  81151
         AR    R1,R0         MAKE END ADDRESS+1                  81151
         LA    R0,1          SET BXLE INCREMENT                  81151
         SR    R1,R0         MAKE TRUE END ADDRESS               81151
         L     R4,CMPCROOT   LOAD ROOT VALUE                     81151
         L     R3,WCMCODAD   LOAD CUMULATIVE CODE WORD           81151
CMPCODEL MR    R2,R4         GENERATE NEXT ELEMENT               81151
         LR    R5,R3         LOAD INTO WORK REGISTER             81151
         SRL   R5,24         ISOLATE HIGH BYTE                   81151
         SR    R6,R6                                             81151
         IC    R6,0(,R15)    GET TEXT BYTE                       81151
         TM    CALLMODE,1    DECOMPRESS ?                        81151
         BNE   *+6           NO                                  81151
         LCR   R5,R5         ELSE FLIP                           81151
         AR    R6,R5         TRANSFORM                           81151
         STC   R6,0(,R15)    SET BACK                            81151
         BXLE  R15,R0,CMPCODEL  DO FOR ALL                       81151
         ST    R3,WCMCODAD   SET FOR NEXT TIME                   81151
         LM    R2,R6,28(R13) RESTORE REGISTERS                   81151
         BR    R14           RETURN TO CALLER                    81151
CMPCROOT DC    F'65639'                                          81151
         SPACE 1                                                 81145
FSAWORK  DSECT ,             WORK SPACE                          81151
         ORG   DB+16         RE-USE PART OF AREA                 81165
DCMUSREC DC    A(0,0)        LOCATE MODE - 508 BYTE WORK AREA    81187
DCMVREC  DC    0A(0),Y(0,0)  CURRENT RECORD - V-FORMAT HEADER    81145
DCMREC   DC    2CL256' '     WORK RECORD                         81145
DCMRECLN EQU   *-DCMREC      LENGTH OF WORK SPACE                81145
         SPACE 1                                                 81151
         ORG   DCMVREC       RE-USE                              81151
DCMPRLIN DC    F'0'          LINE NUMBER (+FLAGS)                81151
DCMPRESL DC    H'0'          LENGTH FIELD                        81151
DCMPREST DC    2CL250' '     WORK SPACE (=USER LENGTH)           81151
         DC    CL44' '       +EXTRA FOR COMPRESS FIELDS          81151
@SERVICE RSECT ,                                                GP99026
         SPACE 1                                                 86250
         LTORG ,                                                 86250
         TITLE '@ S E R V I C E  ***  DSN/VOLUME ALLOCATION'     82109
*        VOLUME AND DATASET ALLOCATION                           81347
*                                                                81347
ENTALLOC BASR  R11,0         MAKE LOCAL BASE                     81347
         USING *,R11         DECLARE IT                          81347
DYNBASE  IC    R0,CALLMODE   GET THE MODIFIER FLAG               81347
         BIX   ERR=BADENTRY,PFX=ENT,BASE=@SERVICE,                     *
               LOC=(VSALC,ALCDS,ALCFR,ALCDD,ALCFD)              GP97241
         SPACE 1                                                 81347
*        LOCATE OR ALLOCATE A TIOT ENTRY FOR VOLUME PASSED IN    81347
*        JFCBVOLS.                                               81347
ENTVSALC LTR   R8,R7         SAVE THE VOLUME ADDRESS             82108
         BZ    BADENTRY                                          82108
         STM   R7,R10,DB+16  SAVE SOME REGISTERS                 82108
         BAS   R10,VSNUCB    GET A UCB                           82108
         B     ALCVLNMT      SET VOLUME NOT MOUNTED              82108
         USING UCBOB,R3                                          82108
         CLI   UCBTBYT3,UCB3DACC  DIRECT ACCESS ?                82108
         BNE   VSUCBLOP      NO; SKIP                            82108
         TM    UCBSTAT,UCBONLI  ON-LINE ?                        82108
         BZ    VSUCBLOP      NO; TRY AGAIN                       82108
         LM    R7,R10,DB+16  RESTORE REGISTERS                   82108
         STM   R1,R9,DB      SAVE ALL VITAL REGISTERS            82108
         BAS   R9,INITTIOT   INITIALIZE FOR TIOT LOOKUP          82108
         XC    RETR0,RETR0   MAKE SURE RETURN IS CLEAN           82108
         LR    R7,R3         COPY THE UCB ADDRESS                82108
         BAS   R10,ENTTIOUA  LOOK FOR SIMPLE DD FOR UNIT         82108
         LM    R1,R9,DB      RELOAD VITAL REGISTERS              82108
         ICM   R4,15,RETR0   WAS A TIOT ENTRY FOUND ?            82108
         BNZ   EXIT          YES; RETURN (ADDRESS IN R0 ALREADY) 82108
         BAS   R10,CLOSEDYN  MAKE SURE TIOT IS CLEAN             82108
         AIF   (&MVS OR NOT &LOCO).DO99TST                       82108
         AUCH  ALCVFAIL      GO TO FAIL IF NOT AUTHORIZED        90337
         SR    R4,R4         ZERO HIGH BYTE                     GP99147
         ICM   R4,7,DYNTIOT+1  LOCATED BEFORE ?                  82108
         BNZ   ALCVDYN       YES; USE IT                         82108
         BAS   R9,INITTIOT   RESET FOR TIOT SCAN                 82108
         LA    R7,=CL8'VOLMOUNT'  POINT TO OPTIONAL DD NAME      82108
         BAS   R10,ENTTIODD  LOCATE BY DD NAME                   82108
         LM    R1,R9,DB      RESTORE REGISTERS AGAIN             82108
         ICM   R4,15,RETR0   GET RETURN CODE - TIOT ENTRY        82108
         BZ    ALCVFAIL      UNABLE TO GET TIOT                  82108
         USING TIOELNGH,R4   DECLARE TIOT ENTRY                  82108
ALCVDYN  CLI   TIOELNGH,20   SINGLE UCB ENTRY ?                  82108
         BNE   ALCVFAIL      NO; UNABLE TO USE                   82108
         CLI   TIOELNGH+20,0   LAST ENTRY ?                      82108
         BE    *+12          YES; SKIP NEXT CHECK                82108
         CLI   TIOEDDNM+20,C' '  CONCATENATED ?                  82108
         BE    ALCVFAIL      YES; DO NOT USE                     82108
         ST    R4,RETR0      SET ADDRESS FOR USER                82108
         L     R5,TIOEFSRT-1 GET OLD UCB ADDRESS                 82108
         MODESET KEY=ZERO    GET PRIVILEGED                      82108
         STCM  R3,7,TIOEFSRT  SET NEW UCB ADDRESS                82108
         MODESET KEY=NZERO   RESTORE KEY (?)                     82108
         STM   R4,R5,DYNTIOT  SET TIOT ENTRY/OLD UCB ADDRESS     82108
         MVI   DYNTIOT,X'80'  INDICATE TIOT MODIFIED             82108
         B     EXIT          RETURN TO CALLER                    82108
.DO99TST AIF   (NOT &MVS).ALVFAIL                                82108
         MVC   DB(DYNLEN),DYNPARMP  MOVE SVC 99 PATTERN          82108
         LA    R1,DYNPARM-DYNPARMP+DB  LIST REQUEST ADDRESS      82108
         STCM  R1,7,DYNPARMP+1-DYNPARMP+DB  SET POINTER          82108
         LA    R1,DYNTUP1-DYNPARMP+DB  TEXT ELEMENT POINTER      82108
         ST    R1,DYNPARM+8-DYNPARMP+DB                          82108
         LA    R14,DYNTU1-DYNPARMP+DB   TEXT UNIT ADDRESSES      82108
         LA    R15,DYNTU2-DYNPARMP+DB                            82108
         LA    R0,DYNTU3-DYNPARMP+DB                             82108
         LA    R1,DYNTU4-DYNPARMP+DB                             82108
         LA    R2,DYNTU5-DYNPARMP+DB                             82108
         STM   R14,R2,DYNTUP1-DYNPARMP+DB                        82108
         LA    R1,DYNTU6-DYNPARMP+DB                             82108
         STCM  R1,7,DYNTUP6+1-DYNPARMP+DB                        82108
         MVC   DYNUNIT-DYNPARMP+DB,UCBNAME  SET UNIT NAME        82108
         MVC   DYNVOLSR-DYNPARMP+DB,UCBVOLI  AND VOLSER          82108
         LA    R1,DYNPARMP-DYNPARMP+DB   GET SVC 99 LIST         82108
         SVC   99            ALLOCATE THE VOLUME                 82108
         MVC   RETR0,DYNPARM+4-DYNPARMP+DB  SET RETURN CODES     82108
         BXH   R15,R15,BADRET4  RETURN LEVEL 4 - SVC 99 ERROR    82108
         MVC   DYNTIOT(8),DYNDDNAM-DYNPARMP+DB  SAVE DDNAME      82108
         LA    R7,DYNTIOT    POINT TO DDNAME                     82108
         XC    RETR0,RETR0   CLEAR TIOT RETURN                   82108
         STM   R1,R9,DB+8    SAVE MOST REGISTERS                 82108
         BAS   R9,INITTIOT   SET FOR TIOT SCAN                   82108
         BAS   R10,ENTTIODD  LOCATE DD BY NAME                   82108
         LM    R1,R9,DB+8    RESTORE REGISTERS                   82108
         ICM   R4,15,RETR0   FOUND ?                             82108
         BNZ   EXIT          YES; R0 HAS TIOT ENTRY              82108
         BAS   R10,CLOSEDYN  JUST IN CASE - FREE IT              82108
*        ELSE FALL THROUGH TO NOT MOUNTABLE (PROGRAM ERROR ?)    82108
.ALVFAIL SPACE 2                                                 82108
ALCVFAIL MVI   RV,8          SHOW NO DD USABLE                   82108
         B     BADRET8       RETURN WITH ERROR                   82108
         DROP  R3            DONE WITH UCB                       82108
         SPACE 1                                                 82108
*        SUBROUTINE TO FREE DYNAMIC ALLOCATION                   82108
*        ENTRY CODE 48/MODE 2                                    82109
*                                                                82108
ENTALCFR DS    0H            EXTERNAL FREE ENTRY (R10 PRESET)    82108
CLOSEDYN DS    0H                                                82108
         AIF   (&MVS OR NOT &LOCO).NOMVSLO                       82108
         ICM   R2,15,DYNTIOT  WAS TIOT MODIFIED ?                82108
         BNMR  R10           NO; RETURN                          82108
         AUCH  0(R10)        RETURN IF NOT AUTHORIZED            90337
         MODESET KEY=ZERO                                        82108
         USING TIOELNGH,R2   DECLARE MAPPING FOR TIOENTRY        82108
         MVC   TIOEFSRT(3),DYNTIOT+5  RESTORE OLD UCB            82108
         MVI   DYNTIOT,0     RESET FLAG BIT                      82108
         DROP  R2                                                82108
         MODESET KEY=NZERO                                       82108
.NOMVSLO AIF   (NOT &MVS).NOMVSCL                                82108
         CLI   DYNTIOT,0     ANY ENTRY ?                         82108
         BER   R10           NO; RETURN                          82108
         LA    R1,DYNTIOT    POINT TO DDNAME                     90290
         BAS   R14,CLOSEDAL  DE-ALLOCATE ENTRY                   90290
         XC    DYNTIOT(8),DYNTIOT  SHOW CLEAR REGARDLESS OF CODE 82108
         BR    R10           RETURN TO CALLER                    82108
         SPACE 1                                                 86250
*        SUBROUTINE TO FREE DYNAMIC ALLOCATION                   86250
*        ENTRY CODE 48/MODE 4                                    86250
*                                                                86250
ENTALCFD LTR   R1,R7         ANY PARAMETER POINTER ?             90290
         BZ    BADENTRY      NO; TOO BAD                         86250
         BAS   R14,CLOSEDAL  DE-ALLOCATE BY DDNAME               90290
         STM   R15,R0,RETR15  PASS RETURN CODES TO USER          86250
         BR    R10           RETURN TO CALLER                    86250
         SPACE 1                                                 90290
CLOSEDAL MVC   DB(DUDLEN),DUDPARMP  GET UNALLOCATE LIST          90290
         MVC   DUDDDNAM-DUDPARMP+DB,0(R1)  SET DDNAME            90290
         LA    R15,DUDPARM-DUDPARMP+DB                           90290
         STCM  R15,7,DUDPARMP+1-DUDPARMP+DB                      90290
         LA    R15,DUDTUP1-DUDPARMP+DB  TEXT ELEMENT POINTER     90290
         ST    R15,DUDPARM+8-DUDPARMP+DB                         90290
         LA    R15,DUDTU1-DUDPARMP+DB   TEXT UNIT ADDRESSES      90290
         LA    R0,DUDTU2-DUDPARMP+DB                             90290
         STM   R15,R0,DUDTUP1-DUDPARMP+DB                        90290
         OI    DUDTUP2-DUDPARMP+DB,X'80'  SET END OF LIST        90290
         LA    R1,DUDPARMP-DUDPARMP+DB   GET SVC 99 LIST         86250
         SVC   99            FREE THE REQUEST                    86250
         BR    R14           RETURN TO CALLER                    90290
         SPACE 1                                                 82108
DUDPARMP DC    0A(0),X'80',AL3(DUDPARM)  PARM POINTER            82108
DUDPARM  DC    AL1(20,S99VRBUN,0,0)  ALLOC/WITH MOUNT            90289
         DC    Y(0,0),A(DUDTUP1)   ERROR CODES/TEXT UNIT POINTER 82108
         DC    A(0),AL1(0,0,0,0)   RESERVED/FLAGS                82108
DUDTUP1  DC    A(DUDTU1)     DDNAME                              82108
DUDTUP2  DC    X'80',AL3(DUDTU2)  REMOVE PERM ATTRIBUTE          90290
DUDTU1   DC    Y(DUNDDNAM,1,8)                                   82108
DUDDDNAM DC    CL8' '        DDNAME                              82108
DUDTU2   DC    Y(DUNUNALC,0)                                     90290
DUDLEN   EQU   *-DUDPARMP    LENGTH TO RELOCATE                  82108
.NOMVSCL BR    R10           RETURN TO CALLER                    82108
.NNMVSCL SPACE 2                                                 82108
         AIF   (NOT &MVS).NO99PRM                                82108
DYNPARMP DC    0A(0),X'80',AL3(DYNPARM)  PARM POINTER            82108
DYNPARM  DC    AL1(20,S99VRBAL,S99NOMNT,0)  ALLOC/NO MOUNT       82108
         DC    Y(0,0),A(DYNTUP1)   ERROR CODES/TEXT UNIT POINTER 82108
         DC    A(0),AL1(0,0,0,0)   RESERVED/FLAGS                82108
DYNTUP1  DC    A(DYNTU1)     RETURN DDNAME                       82108
         DC    A(DYNTU2)     UNIT NAME                           82108
         DC    A(DYNTU3)     VOLSER                              82108
         DC    A(DYNTU4)     LABEL=PRIVATE ?                     82108
         DC    A(DYNTU5)     DISP=OLD                            82108
DYNTUP6  DC    X'80',AL3(DYNTU6)   FREE=CLOSE                    82108
DYNTU1   DC    Y(DALRTDDN,1,8)                                   82108
DYNDDNAM DC    CL8' '        RETURNED DDNAME                     82108
DYNTU2   DC    Y(DALUNIT,1,3)                                    82108
DYNUNIT  DC    CL3' '        SUPPLIED UNIT NAME                  82108
DYNTU3   DC    Y(DALVLSER,1,6)                                   82108
DYNVOLSR DC    CL6' '        SUPPLIED VOLUME SERIAL              85253
DYNTU4   DC    Y(DALPRIVT,0)   PRIVATE                           82108
DYNTU5   DC    Y(DALSTATS,1,1)  DISP=                            82108
         DC    X'01'                 OLD                         82108
DYNTU6   DC    Y(DALCLOSE,0) FREE=CLOSE ?                        82108
DYNLEN   EQU   *-DYNPARMP    LENGTH TO RELOCATE                  82108
.NO99PRM SPACE 1                                                 82108
*        ENTRY CODE 48/MODE 1                                    81347
*        ALLOCATE A DATASET GIVEN THE JFCB                       81347
         USING JFCBDSNM,R8   DECLARE THE USER'S JFCB             81347
ENTALCDS LTR   R8,R7         SAVE THE JFCB ADDRESS               82108
         BZ    BADENTRY                                          82108
         LA    R7,JFCBVOLS   POINT TO FIRST VOLUME SERIAL        81347
         STM   R7,R10,DB+16  SAVE SOME REGISTERS                 81347
         BAS   R10,VSNUCB    GET A UCB                           81347
         B     ALCVLNMT      SET VOLUME NOT MOUNTED              81347
         USING UCBOB,R3                                          81347
         CLI   UCBTBYT3,UCB3DACC  DIRECT ACCESS ?                81347
         BNE   VSUCBLOP      NO; SKIP                            81347
         TM    UCBSTAT,UCBONLI  ON-LINE ?                        81347
         BZ    VSUCBLOP      NO; TRY AGAIN                       81347
         LM    R7,R10,DB+16  RESTORE REGISTERS                   81347
         ST    R3,DB         SET ADDRESS                         81347
         BAS   R10,CLOSEDYN  CLEAN PRIOR TIOT ENTRY              82109
         AIF   (NOT &MVS).ALDAUTH                                82108
         AUCH  ALCDO99       USE SVC 99 IF NOT AUTHORIZED        90337
.ALDAUTH LA    R0,0(,R8)     PASS CLEAN JFCB ADDRESS             82108
         LA    R1,DB         ADDRESS OF UCB                      81347
         SVC   32            ALLOCATE                            81347
         LTR   R15,R15       GOOD RETURN ?                       81347
         BZ    EXIT          YES                                 81347
         STM   R15,R0,RETR15    RETURN ERROR IN R15 AND R0       81359
         B     BADRET4       SET ERROR                           81347
         SPACE 1                                                 82108
         AIF   (NOT &MVS).ALCDN99                                82108
ALCDO99  MVC   DB+8(DYDLEN),DYDPARMP  MOVE FIXED RB99 PORTION    82108
         LA    R1,DYDPARM-DYDPARMP+DB+8  POINT TO POINTER ADDRESS
         STCM  R1,7,DYDPARMP+1-DYDPARMP+DB+8                     82108
         LA    R6,DYDTUP1-DYDPARMP+DB+8  POINT TO FIRST TEXT POINTER
         ST    R6,DYDPARM+8-DYDPARMP+DB+8  SET IT                82108
         XC    0(32*4,R6),0(R6)  CLEAR TEXT UNIT POINTER SPACE   86187
         LA    R5,32*4(,R6)  LEAVE ROOM FOR 32 TEXT UNIT POINTERS
         XC    0(256,R5),0(R5)  CLEAR TEXT UNIT SPACE            86187
         BAS   R9,B99COMON   BUILD FIRST UNIT                    82108
         MVI   1(R4),DALRTDDN  RETURN DDNAME                     82108
         MVI   3(R4),1       ONE ENTRY                           82108
         MVI   5(R4),8       LENGTH 8                            82108
         MVC   6(8,R4),BLANKS                                    82108
         LA    R5,6+8(,R4)   FIX NEXT POINTER                    82108
         LA    R4,DYDDSN     POINT TO SHORT LIST FOR DSN         82108
         BAS   R9,B99MOVE    PUT AT FIXED ADDRESS IN HEAP        82108
         BAS   R9,B99COMON   BUILD HEADER                        82108
         MVI   1(R4),DALUNIT  UCB NAME                           82108
         MVI   3(R4),1       NUMBER                              82108
         MVI   5(R4),3       LENGTH 3                            82108
         MVC   6(3,R4),UCBNAME  MOVE UCBNAME                     82108
         LA    R5,6+3(,R4)   SET NEXT ADDRESS                    82108
         BAS   R9,B99FLAG    BUILD A FLAG ENTRY                  82108
         MVI   1(R4),DALSTATS  SET DISP STATUS                   82108
         MVI   6(R4),X'04'   =NEW                                82108
         BAS   R9,B99FLAG                                        82108
         MVI   1(R4),DALNDISP  NORMAL DISPOSITION                82108
         MVI   6(R4),X'08'   KEEP                                82108
         BAS   R9,B99FLAG                                        82108
         MVI   1(R4),DALCDISP  CONDITIONAL DISPOSITION           82108
         MVI   6(R4),X'08'   KEEP ON ABEND                       82108
         BAS   R9,B99COMON   GET A SHORT ENTRY                   82108
         MVI   1(R4),DALCYL  ALLOCATE CYLINDERS                  82108
         TM    JFCBCTRI,JFCBCYL  REALLY ?                        82108
         BO    B99DSPC       YES                                 82108
         MVI   1(R4),DALTRK  ELSE SET FOR TRACKS                 82108
         TM    JFCBCTRI,JFCBAVR  AVERAGE BLOCK ?                 82108
         BZ    B99DSPC       NO; TRACK OR ABSTR                  82108
         MVI   1(R4),DALBLKLN  BLOCKSIZE BETTER BE NON-ZERO      82108
B99DSPC  TM    JFCBIND1,JFCRLSE  TRACK RELEASE ON CLOSE ?        82108
         BNO   B99DNRL       NO                                  82108
         BAS   R9,B99COMON   GET A SHORT ENTRY                   82108
         MVI   1(R4),DALRLSE  SET FOR RELEASE                    82108
B99DNRL  TM    JFCBCTRI,JFCONTIG+JFCMIXG+JFCALX  SPECIAL SPACE ? 82108
         BZ    B99DNCON      NO                                  82108
         BAS   R9,B99FLAG    BUILD A FLAG ENTRY                  82108
         MVI   1(R4),DALSPFRM                                    82108
         MVC   6(1,R4),JFCBCTRI                                  82108
         NI    6(R4),JFCONTIG+JFCMIXG+JFCALX  SET SPACE TYPE     82108
B99DNCON TM    JFCBCTRI,JFCROUND  ROUND ?                        82108
         BZ    B99DNRND      NO                                  82108
         BAS   R9,B99COMON   BUILD A SHORT ENTRY                 82108
         MVI   1(R4),DALROUND  REQUEST ROUNDING                  82108
B99DNRND BAS   R9,B99COMON   MAKE A SHORT ENTRY                  82108
         MVI   1(R4),DALPRIVT  FLAG PRIVATE PACK                 82108
         TM    JFCFLGS1,JFCBPROT+JFCBADSP  PSWD/RACF PROTECT ?   82108
         BZ    B99DNPRO      NO                                  82108
         BAS   R9,B99FLAG    BUILD A FLAG ENTRY                  82108
         MVI   1(R4),DALPASPR                                    82108
         MVC   6(1,R4),JFCFLGS1  MOVE FLAG BYTE                  82108
         NI    6(R4),JFCBPROT+JFCBADSP  LEAVE ONLY               82108
B99DNPRO CLC   JFCBXPDT,ZEROES  EXPIRATION DATE ?                82108
         BE    B99DNXPD      NO                                  82108
         SR    R0,R0                                             82108
         IC    R0,JFCBXPDT   GET YEAR                            82108
         MH    R0,=Y(1000)                                       82108
         AH    R0,JFCBXPDT+1 COMBINE YEAR AND DAY                82108
         CVD   R0,DB         STASH TEMPORARILY                   82108
         UNPK  6(5,R5),DB(8)  UNPACK TO INTENDED TEXT UNIT       82108
         OI    10(R5),C'0'   MAKE POSITIVE SIGN                  82108
         BAS   R9,B99COMON   GET A COMMON TEXT UNIT              82108
         MVI   1(R4),DALEXPDT  SET DATE                          82108
         MVI   3(R4),1       ONE ENTRY                           82108
         MVI   5(R4),5       LENGTH 5                            82108
         LA    R5,6+5(,R4)   SET NEXT ADDRESS                    82108
B99DNXPD LA    R4,DYDLIST    GET JFCB TO TEXT UNIT LIST          82108
         BAS   R9,B99MOVE    MOVE EVERYTHING ELSE                82108
         SH    R6,=Y(4)      POINT TO LAST POINTER               82108
         OI    0(R6),X'80'   SET END OF LIST BIT                 82108
         SPACE 1                                                 82108
         LA    R1,DYDPARMP-DYDPARMP+DB+8  SET PARM POINTER       82108
         SVC   99            ALLOCATE THE DATASET                82108
         MVC   RETR0,DYDPARM+4-DYDPARMP+DB+8  SET RETURN CODES   82108
         BXH   R15,R15,BADRET4  RETURN LEVEL 4 - SVC 99 ERROR    82108
         LA    R10,EXIT      SET FOR RETURN OK                   82108
         L     R1,DYDTUP1-DYDPARMP+DB+8  DDNAME TEXT UNIT ADDRESS
         MVC   DYNTIOT(8),6(R1)  SET RETURNED DDNAME FOR FREE    82108
         MVC   JFCBDSNM(44),6+8+6(R1)  UPDATE DSNAME             82108
         B     CLOSEDYN      FREE THE TIOT ENTRY, ETC.           82108
         SPACE 2                                                 82108
*        SUBROUTINE TO MOVE JFCB DATA TO SVC 99 LIST             82108
*        R4 - LIST POINTER; R5 - TEXT UNIT AREA; R6 - TEXT POINTERS
*                                                                82108
B99MOVE  SR    R0,R0         CLEAR FOR LENGTH INSERTION          82108
         SR    R1,R1         CLEAR FOR JFCB OFFSET               82108
         IC    R0,0(,R4)     GET NEXT LIST ENTRY                 82108
         LA    R15,X'3F'     MAKE LENGTH MASK                    82108
         NR    R15,R0        MASK IT                             82108
         BZR   R9            ZERO LENGTH - END OF LIST           82108
         IC    R1,1(,R4)     GET JFCB OFFSET                     82108
         LA    R1,INFMJFCB(R1)  GET ADDRESS IN JFCB              82108
         CR    R15,R0        UNMASKED LENGTH ?                   82108
         BCTR  R15,0         SET EXECUTE LENGTH                  82108
         BE    B99MOVER      YES; JUST BUILD TEXT UNIT           82108
         TM    0(R4),X'80'   CHECK FOR BLANKS ?                  82108
         BZ    B99CHZER      NO; CHECK FOR ZEROES                82108
         LA    R14,BLANKS    POINT TO BLANKS (16)                82108
         EX    R15,B99CLC    ALL BLANK ?                         82108
         BE    B99MSKIP      YES; SKIP THIS ONE                  82108
B99CHZER LA    R14,ZEROES    POINT TO ZEROES (10)                82108
         EX    R15,B99CLC    ALL ZERO ?                          82108
         BE    B99MSKIP      YES; SKIP IT                        82108
B99MOVER XC    0(6,R5),0(R5)  MAKE SURE UNUSED BYTES ARE CLEARED 86251
         MVC   1(1,R5),2(R4)  MOVE TEXT UNIT TYPE                82108
         MVI   3(R5),1       SET NUMBER OF ITEMS                 82108
         STC   R0,5(,R5)     SET ITEM LENGTH                     82108
         NI    5(R5),X'3F'   KILL FLAG BITS                      86251
         EX    R15,B99MVC    MOVE TEXT OF ITEM                   82108
         ST    R5,0(,R6)     SET TEXT UNIT POINTER               82108
         LA    R6,4(,R6)     UP POINTER ADDRESS                  82108
         LA    R5,6+1(R5,R15)  POINT TO NEXT TEXT UNIT SPACE     82108
B99MSKIP LA    R4,3(,R4)     UP INPUT LIST                       82108
         B     B99MOVE       LOOK FOR MORE                       82108
B99CLC   CLC   0(0,R1),0(R14)  TEST FOR BLANKS/ZEROES            82108
B99MVC   MVC   6(0,R5),0(R1)   MOVE TEXT                         82108
         SPACE 1                                                 82108
*        THIS ROUTINE BUILDS A BASIC HEADER AND ADVANCES POINTERS
*                                                                82108
B99COMON XC    0(6,R5),0(R5) CLEAR NEW TEXT UNIT                 82108
         ST    R5,0(,R6)     STORE UNIT ADDRESS                  82108
         LA    R6,4(,R6)     SET NEXT AVAILABLE POINTER ADDRESS  82108
         LR    R4,R5         SAVE ADDRESS                        82108
         LA    R5,4(,R5)     SET ADDRESS FOR SHORT UNIT          82108
         BR    R9            RETURN                              82108
         SPACE 1                                                 82108
*        THIS ROUTINE BUILDS A FLAG ENTRY                        82108
*                                                                82108
B99FLAG  XC    0(7,R5),0(R5)                                     82108
         ST    R5,0(,R6)     MAKE TEXT UNIT POINTER              82108
         LA    R6,4(,R6)     SET NEXT AVAILABLE POINTER          82108
         LR    R4,R5         SAVE ADDRESS OF CURRENT UNIT        82108
         MVI   3(R4),1       ONE ENTRY                           82108
         MVI   5(R4),1       ONE BYTE LENGTH                     82108
         LA    R5,6+1(,R5)   NEXT UNIT ADDRESS                   82108
         BR    R9            RETURN                              82108
         SPACE 2                                                 90364
*        ENTRY CODE 48/MODE 3                                    86250
*        ALLOCATE AN EXISTING DATASET (WITH SPECIAL PARM LIST)   86250
*        R1 => +0  DDNAME RETURNED; IF >' ' ON ENTRY, REQUESTED DDN
*              +4  ADDRESS OF DATASET NAME                       86250
*              +8  ADDRESS OF MEMBER NAME (OR 0, OR PTR <= ' ')  86250
*              12  ADDRESS OF VOLUME (OR 0, OR PTR <= ' ')       86250
*              16  ADDRESS OF DISPOSITION FLAG (01 OLD, 08 SHR) OR 0
*                  IF X'80' IS ON, ADDRESS IS 8-BYTE LIST: DISP, NDISP,
*                  CDISP,FLAG1,FLAG2,FLAG2,FLAG2,FLAG2           90364
*                  IF WAIT BITS IN FLAG2 ON, USE SVC ENTRY OR APF=1
*                                                                86250
ENTALCDD LTR   R7,R7         PARM LIST PASSED ?                  86250
         BZ    BADENTRY      NO; TOO BAD                         86250
         XC    PSWLIST,PSWLIST  USE TEMPORARY HOLD AREA          86250
         ICM   R0,15,0(R7)   DDNAME ?                            86250
         BNP   BADENTRY      NO                                  86250
         ST    R0,PSWLIST    STASH DDN ENTRY                     86250
         ICM   R0,15,4(R7)   GET DSN ENTRY                       86250
         BZ    BADENTRY      NONE; QUIT                          86250
         ST    R0,PSWLIST+4                                      86250
         BM    ALCDDENL      NO MORE                             86250
         ICM   R0,15,8(R7)   MEMBER NAME                         86250
         ST    R0,PSWLIST+8                                      86250
         BM    ALCDDENL                                          86250
         ICM   R0,15,12(R7)  VOL-SER                             86250
         ST    R0,PSWLIST+12                                     86250
         BM    ALCDDENL                                          86250
         ICM   R0,15,16(R7)  DISP                                86250
         ST    R0,PSWLIST+16                                     90364
ALCDDENL NI    PSWLIST,X'7F'                                     86250
         NI    PSWLIST+4,X'7F'                                   86250
         NI    PSWLIST+8,X'7F'                                   86250
         NI    PSWLIST+12,X'7F'                                  86250
         NI    PSWLIST+16,X'7F'                                  86250
         MVC   DB+8(DYDLEN),DYDPARMP  MOVE FIXED RD99 PORTION    86250
         LA    R1,DYDPARM-DYDPARMP+DB+8  POINT TO POINTER ADDRESS
         STCM  R1,7,DYDPARMP+1-DYDPARMP+DB+8                     86250
         LA    R6,DYDTUP1-DYDPARMP+DB+8  POINT TO FIRST TEXT POINTER
         ST    R6,DYDPARM+8-DYDPARMP+DB+8  SET IT                86250
         XC    0(32*4,R6),0(R6)  CLEAR TEXT UNIT POINTER SPACE   86250
         LA    R5,32*4(,R6)  LEAVE ROOM FOR 32 TEXT UNIT POINTERS
         XC    0(256,R5),0(R5)  CLEAR TEXT UNIT SPACE            86250
         BAS   R9,B99COMON   BUILD FIRST UNIT                    86250
         MVI   1(R4),DALRTDDN  RETURN DDNAME                     86250
         MVI   3(R4),1       ONE ENTRY                           86250
         MVI   5(R4),8       LENGTH 8                            86250
         MVC   6(8,R4),BLANKS                                    86250
         LA    R5,6+8(,R4)   FIX NEXT POINTER                    86250
         BAS   R9,B99COMON   MAKE AN ENTRY                       91329
         MVI   1(R4),DALRTORG  RETURN DSORG                      91329
         MVI   3(R4),1       ONE ENTRY                           91329
         MVI   5(R4),2       LENGTH 2                            91329
         LA    R5,6+2(,R4)   SET NEXT ADDRESS                    91329
         LA    R4,DDDDSN     POINT TO LIST FOR DSN, MEMBER, ETC. 86250
         BAS   R9,D99MOVE    MOVE TO HEAP                        86250
         ICM   R2,15,PSWLIST+12  DID USER SUPPLY A VOLSER?      GP05163
         BZ    DDNVSER       NO                                 GP05163
         CLI   0(R2),C' '    REALLY ?                           GP05163
         BNH   DDNVSER                                          GP05163
         BAS   R9,B99COMON   BUILD EXTRA UNIT                   GP05163
*  UNDER TSO, A VOLSER GETS AN UNWANTED UNIT, AND ALLOCATION FAILS
*  WITH RETURN CODE 4, ERROR CODE 039C (UNIT/SER CONFLICT)      GP05163
         MVI   1(R4),DALUNIT   SPECIFY UNIT TO AVOID TSO ERROR  GP05163
         MVI   3(R4),1       ONE ENTRY                          GP05163
         MVI   5(R4),8       LENGTH 8                           GP05163
         MVC   6(8,R4),=CL8'SYSALLDA'                           GP05163
         LA    R5,6+8(,R4)   FIX NEXT POINTER                   GP05163
DDNVSER  BAS   R9,B99FLAG    BUILD A FLAG ENTRY                  86250
         MVI   1(R4),DALSTATS  SET DISP STATUS                   86250
         ICM   R2,15,PSWLIST+16  DID USER SUPPLY DISPOSITION ?   86250
         BZ    D99NDISP      NO; NEED IT                         86250
         MVC   6(1,R4),0(R2)   MOVE USER'S REQUEST               90364
D99NDISP NI    6(R4),X'0B'   LEAVE SHR/0/MOD/OLD BITS ONLY       86250
         BNZ   *+8           OK                                  86250
         MVI   6(R4),8       DEFAULT DISP=SHR                    86250
         BAS   R9,B99FLAG                                        86250
         MVI   1(R4),DALNDISP  NORMAL DISPOSITION                86250
         LTR   R2,R2         DID USER SUPPLY LIST ?              90364
         BZ    D99NDISN      NO                                  90364
         TM    0(R2),X'80'   LONG FORM ?                         90364
         BZ    D99NDISN      NO                                  90364
         MVC   6(1,R4),1(R2)  MOVE NORMAL DISPOSITION            90364
D99NDISN NI    6(R4),X'0B'   MASK SHR/0/MOD/OLD                  90364
         BNZ   *+8           OK ?                                90364
         MVI   6(R4),X'08'   KEEP (DEFAULT)                      86250
         BAS   R9,B99FLAG                                        86250
         MVI   1(R4),DALCDISP  CONDITIONAL DISPOSITION           86250
         LTR   R2,R2         DID USER SUPPLY LIST ?              90364
         BZ    D99NDISC      NO                                  90364
         TM    0(R2),X'80'   LONG FORM ?                         90364
         BZ    D99NDISC      NO                                  90364
         MVC   6(1,R4),2(R2)  MOVE ABNORMAL DISPOSITION          90364
D99NDISC NI    6(R4),X'0B'   MASK SHR/0/MOD/OLD                  90364
         BNZ   *+8           OK ?                                90364
         MVI   6(R4),X'08'   KEEP ON ABEND                       86250
         SH    R6,=Y(4)      POINT TO LAST POINTER               86250
         OI    0(R6),X'80'   SET END OF LIST BIT                 86250
         LTR   R2,R2         EXTENDED DISP LIST ?                90364
         BZ    D99NAUTH      NO                                  90364
         TM    0(R2),X'80'   LONG LIST ?                         90364
         BZ    D99NAUTH      NO                                  90364
         MVC   DYDPARM+2-DYDPARMP+DB+8(1),3(R2)  MOUNT OPTION    90364
         ICM   R0,15,4(R2)   SPECIAL OPTIONS ?                   90364
         BZ    D99NAUTH      NO                                  90364
         STCM  R0,15,DYDFLGS2-DYDPARMP+DB+8  SET USER'S FLAGS2   90364
         CLI   SUBPOOL,FGSVC  SVC ENTRY ?                        90364
         BE    D99YAUCH      YES                                 90364
         TESTAUTH FCTN=1     AUTHORIZED ?                        90364
         BXH   R15,R15,D99NAUTH  NO                              90364
         MODESET KEY=ZERO,MODE=SUP  GET VERY PRIVILEGED          90364
D99YAUCH L     R2,PSAAOLD-PSA  GET MY ASCB ADDRESS               90364
         USING ASCB,R2       DECLARE IT                          90364
         TM    &FLAGNAM,ASCBTOFF  USER WITHOUT SMF EXITS ?       90364
         BZ    D99NTOFF      NO                                  90364
         SR    R2,R2         YES; DON'T CHANGE                   90364
         B     D99YTOFF      SKIP AROUND                         90364
D99NTOFF OI    &FLAGNAM,ASCBTOFF  SET TO PREVENT 522             90364
D99YTOFF LA    R1,DYDPARMP-DYDPARMP+DB+8  SET PARM POINTER       90364
         SVC   99            ALLOCATE THE DATASET                90364
         LTR   R2,R2         DID WE SET SMF BIT ?                90364
         BZ    *+8           NO                                  90364
         NI    &FLAGNAM,255-ASCBTOFF  ELSE TURN IT OFF AGAIN     90364
         CLI   SUBPOOL,FGSVC WERE WE CALLED AS AN SVC ?          90364
         BE    D99CAUTH      YES; DON'T KILL MODE                90364
         LR    R3,R15        PRESERVE THE RETURN CODE            90364
         MODESET KEY=NZERO,MODE=PROB  RESTORE NORMAL MODE        90364
         LR    R15,R3        RESTORE RETURN CODE                 90364
         B     D99CAUTH      JOIN COMMON CODE                    90364
         SPACE 1                                                 86250
D99NAUTH LA    R1,DYDPARMP-DYDPARMP+DB+8  SET PARM POINTER       86250
         NC    DYDPARM+2-DYDPARMP+DB+8(1),=AL1(0)  RESET AUTH   GP99063
         NC    DYDFLGS2-DYDPARMP+DB+8(2),=AL1(0,0)  RESET AUTH  GP99063
         SVC   99            ALLOCATE THE DATASET                86250
D99CAUTH MVC   RETR0,DYDPARM+4-DYDPARMP+DB+8  SET RETURN CODES   86250
         BXH   R15,R15,BADRET4  RETURN LEVEL 4 - SVC 99 ERROR    86250
         L     R1,PSWLIST    GET DDNAME POINTER                  86250
         L     R15,DYDTUP1-DYDPARMP+DB+8  DDNAME TEXT UNIT ADDRESS
         MVC   0(8,R1),6(R15)  RETURN NEW DDNAME                 86250
         ICM   R0,3,6+8+6(R15)  TEST RETURNED DSORG              91329
         BZ    BADRET4       SET WARNING - DS MAY NOT EXIST      91329
         B     EXIT          SET FOR RETURN OK                   86250
         SPACE 2                                                 86250
*        SUBROUTINE TO MOVE USER DATA TO SVC 99 LIST             86250
*        R4 - LIST POINTER; R5 - TEXT UNIT AREA; R6 - TEXT POINTER
*                                                                86250
D99MOVE  SR    R0,R0         CLEAR FOR LENGTH INSERTION          86250
         SR    R1,R1         CLEAR FOR PSWLIST OFFSET            86250
         IC    R0,0(,R4)     GET NEXT LIST ENTRY                 86250
         LA    R15,X'3F'     MAKE LENGTH MASK                    86250
         NR    R15,R0        MASK IT                             86250
         BZR   R9            ZERO LENGTH - END OF LIST           86250
         IC    R1,1(,R4)     GET OFFSET                          86250
         L     R1,PSWLIST(R1)  GET ADDRESS POINTER               86250
         LTR   R1,R1         ANY PROVIDED ?                      86250
         BZ    D99MSKIP      NO; UNABLE TO USE                   86250
         CR    R15,R0        UNMASKED LENGTH ?                   86250
         BCTR  R15,0         SET EXECUTE LENGTH                  86250
         BE    D99MOVER      YES; JUST BUILD TEXT UNIT           86250
         TM    0(R4),X'80'   CHECK FOR BLANKS ?                  86250
         BZ    D99CHZER      NO; CHECK FOR ZEROES                86250
         LA    R14,BLANKS    POINT TO BLANKS (16)                86250
         EX    R15,B99CLC    ALL BLANK ?                         86250
         BE    D99MSKIP      YES; SKIP THIS ONE                  86250
D99CHZER LA    R14,ZEROES    POINT TO ZEROES (10)                86250
         EX    R15,B99CLC    ALL ZERO ?                          86250
         BE    D99MSKIP      YES; SKIP IT                        86250
D99MOVER XC    0(6,R5),0(R5)  MAKE SURE UNUSED BYTES ARE CLEARED 86251
         MVC   1(1,R5),2(R4)  MOVE TEXT UNIT TYPE                86250
         MVI   3(R5),1       SET NUMBER OF ITEMS                 86250
         STC   R0,5(,R5)     SET ITEM LENGTH                     86250
         NI    5(R5),X'3F'   KILL FLAG BITS                      86251
         EX    R15,B99MVC    MOVE TEXT OF ITEM                   86250
         ST    R5,0(,R6)     SET TEXT UNIT POINTER               86250
         LA    R6,4(,R6)     UP POINTER ADDRESS                  86250
         LA    R5,6+1(R5,R15)  POINT TO NEXT TEXT UNIT SPACE     86250
D99MSKIP LA    R4,3(,R4)     UP INPUT LIST                       86250
         B     D99MOVE       LOOK FOR MORE                       86250
         SPACE 1                                                 86250
DDDDSN   DDYN  4,DSNAM,L=44  ALCDD - DSN                         86250
         DDYN  8,MEMBR,SKIP=BLANK    MEMBER NAME                 86250
         DDYN  12,VLSER,L=6,SKIP=BLANK  VOLUME                   86250
         DDYN  0,DDNAM,SKIP=BLANK    PREFERRED DDNAME            86250
         DC    X'00'         END OF LIST                         86250
         SPACE 2                                                 82108
DYDPARMP DC    0A(0),X'80',AL3(DYDPARM)  PARM POINTER            82108
DYDPARM  DC    AL1(20,S99VRBAL,S99NOMNT,0)  ALLOC/NO MOUNT       82108
         DC    Y(0,0),A(DYDTUP1)   ERROR CODES/TEXT UNIT POINTER 82108
         DC    A(0)                RESERVED                      90364
DYDFLGS2 DC    AL1(0,0,0,0)  FLAGS, TOO                          90364
DYDTUP1  EQU   *                                                 82108
DYDLEN   EQU   *-DYDPARMP    LENGTH TO MOVE                      82108
         SPACE 1                                                 82108
DYDDSN   JDYN  BDSNM,DSNAM   MAY BE RETURNED AS NEW NAME         82108
         DC    X'00'         END OF VERY SHORT LIST              82108
DYDLIST  JDYN  BELNM,MEMBR,SKIP=BLANK  SKIP MEMBER IF ZERO/BLANK 82108
         JDYN  BPQTY,PRIME,SKIP=ZERO                             82108
         JDYN  BSQTY,SECND,SKIP=ZERO                             82108
         JDYN  BDQTY,DIR,SKIP=ZERO                               82108
DYDVLSER JDYN  BVOLS,VLSER                                       82108
         ORG   DYDVLSER                                          82108
         DC    AL1(X'80'+6)  FORCE VOLSER LENGTH TO 6 BYTES      82108
         ORG   ,                                                 82108
         JDYN  BLKSI,BLKSZ,SKIP=ZERO                             82108
         JDYN  DSORG,DSORG,SKIP=ZERO                             82108
         JDYN  EROPT,EROPT,SKIP=ZERO                             82108
         JDYN  KEYLE,KYLEN,SKIP=ZERO                             82108
         JDYN  LIMCT,LIMCT,SKIP=ZERO                             82108
         JDYN  LRECL,LRECL,SKIP=ZERO                             82108
         JDYN  NCP,NCP,SKIP=ZERO                                 82108
         JDYN  OPTCD,OPTCD,SKIP=ZERO                             82108
         JDYN  RECFM,RECFM,SKIP=ZERO                             82108
         DC    X'00'         END OF LIST                         82108
.ALCDN99 DROP  R3,R8                                             82108
ALCVLNMT MVI   RV,4          SET VOLUME NOT UP                   81347
         B     BADRET8       SET RETURN CODE 8                   81347
         SPACE 1                                                 82108
         AIF   (NOT &MVS).NO99MAP                                82108
         PRINT &PRTMAC                                           82108
         IEFZB4D0 ,                                              82108
         IEFZB4D2 ,                                              82108
         PRINT &PRTSOR                                           82108
.NO99MAP SPACE 2                                                 82108
@SERVICE RSECT ,                                                GP99026
         LTORG ,                                                 81359
         TITLE '@ S E R V I C E  ***  WYLBUR MULTI-VOLUME LOCATE'
*        THIS FUNCTION PERFORMS OVERLAPPED LOCATES (OBTAINS) FOR DATA
*        SETS ON RESIDENT WYLBUR VOLUMES.                        85202
*          SERVICE WYLOC,DSNAME :                                85202
*              R15=0, R0=A(UCB) R1=A(UCB | 0)  DSN FOUND         85202
*              R15=4         DSN NOT FOUND                       85202
*              R15=8,12,16   MAJOR ERROR (CALLER SHOULD OBTAIN)  85202
*                                                                85202
*          SERVICE WYLOC,0   FREES THE RESIDENT TABLE            85202
*                                                                85202
         DROP  ,                                                 85202
         USING @SERVICE,R12                                      85202
         USING SAVEAREA,R13                                      85202
ENTWYLOC B     BADENTRY      FAIL IF NOT LOCAL INSTALLATION      85202
         AIF   (NOT &NOLO).NOWYLOC  SKIP WYLBUR VTOC LOCATE      85202
         ORG   ENTWYLOC      ELSE REDEFINE                       85202
         BASR  R11,0         MAKE LOCAL BASE                     85202
         USING *,R11         DECLARE IT                          85202
         XC    WYOMAIN,WYOMAIN  CLEAR GETMAIN ADDRESS            85202
         XC    RETR0(8),RETR0  CLEAR RETURNS                     85202
         CLI   SUBPOOL,FGSVC  SERVICE CALL ?                     85202
         BNE   BADENTRY      NO; NOT SUPPORTED                   85202
         L     R8,CVTPTR     GET THE CVT                         85202
         ICM   R8,15,CVTUSER-CVTMAP(R8)  ANY LOCAL EXTENSION ?   85202
         BZ    WYORET12      NO; SET MAJOR ERROR                 85202
         USING USERCVT,R8    DECLARE IT                          85202
         L     R6,UCLWYL     GET PRESUMED WYLBUR TABLE           85202
         LTR   R7,R1         TEST USER'S PARM                    85202
         BZ    WYOFREE       ZERO - FREE WORK AREA               85202
         LTR   R6,R6         TEST VTOC TABLE                     85202
         BP    WYLOCTAB      HAVE TABLE - PROCEED                85202
         BM    WYORET12      PRIOR ERROR BUILDING TABLE          85202
         SPACE 1                                                 85202
*        BUILD A WYLBUR VTOC TABLE                               85202
*                                                                85202
         ST    R7,CALLAD     SAVE POINTER TO DSN                GP99026
         SR    R10,R10       MAKE COUNTER FOR NUMBER OF VOLUMES  85202
         LA    R2,UNITBUMP   SET SUBROUTINE FOR FAST ENTRY       85202
         BAS   R9,UNITINIT   INITIALIZE UCB SCAN                 85202
         BAS   R9,UNITLOOP   GET FIRST UCB                       85202
         LTR   R3,R3         ANY UCB LOCATED ?                   85202
         BZ    WYOLUCB       NO MORE                             85202
         USING UCBOB,R3                                          85202
         CLI   UCBTBYT3,UCB3DACC  DASD DEVICE ?                  85202
         BNER  R2            NO; TRY ANOTHER                     85202
         TM    UCBSTAT,UCBPRES+UCBRESV  MOUNTED ?                85202
         BZR   R2            NO; SKIP                            85202
         TM    UCBSTAB,UCBBPUB+UCBBSTR  PUBLIC OR STORAGE ?      85202
         BZR   R2            NEITHER; SKIP                       85202
         TM    UCBSTAT,UCBONLI  ON-LINE ?                        85202
         BZR   R2            NO; SKIP                            85202
         CLC   UCBVOLI(4),=C'WYL001'  WYLBUR PACK ?              85202
         BNER  R2            NO                                  85202
         LA    R10,1(,R10)   UP COUNTER                          85202
         BR    R2            CALL BACK                           85202
         SPACE 1                                                 85202
WYOLUCB  LTR   R2,R10        ANY VOLUMES FOUND ?                 85202
         BZ    WYOFAIL       NO; WHAT'S GOING ON ?               85202
         LA    R3,&WYLDMAX   NUMBER OF WYLBUR DRIVES EXPECTED    85202
         CR    R2,R3         MORE THAN SUPPORTED ?               85202
         BNH   WYONVOK       NO; OK                              85202
         WTO   'MSG791E EXCESSIVE WYLBUR VOLUMES FOUND - CALL SYSTEMS',*
               DESC=1,ROUTCDE=1                                  85202
         LR    R2,R3         TRUNCATE                            85202
WYONVOK  LA    R3,1(,R2)     SPACE REQUIRED                      85202
         MH    R3,=Y(WVTLEN)   ENTRIES+OVERHEAD * SIZE           85202
         GETMAIN RC,LV=(R3),SP=241  GET STORAGE                  85202
         BXH   R15,R15,WYOFAIL   NO WAY ?                        85202
         ST    R1,WYOMAIN                                        85202
         ST    R3,WYOMAIN+4  SAVE GETMAIN VALUES                 85202
         MVI   WYOMAIN+4,241   ALSO SET SUB-POOL NUMBER          86187
         LR    R6,R1         SAVE THE RETURNED ADDRESS           85202
         CLRL  (R6),(R3)     INITIALIZE TO ZEROES                85202
         USING WVTDSECT,R6   DECLARE THE BASE                    85202
         STH   R10,WVTNUM    NUMBER OF VOLUMES IN TABLE          85202
         MVC   WVTSIZE,=Y(WVTLEN)  ENTRY SIZE                    85202
         ST    R3,WVTMAIN    SAVE GETMAIN SIZE                   85202
         MVI   WVTMAIN,241   ADD SUBPOOL FOR FREEMAIN            85202
         MVC   WVTINDEX,=C'WYL001'  SET OWNER                    85202
         LA    R5,WVTENTRY   POINT TO FIRST ENTRY                85202
         USING WVTENTRY,R5   DECLARE IT                          85202
         ST    R5,WVTMOST    PREVENT ERROR IN LSPACE CODE (LATER)
         MVI   PSWDSN,X'04'  PREPARE FOR DSCB 4 NAME             85202
         MVC   PSWDSN+1(L'PSWDSN-1),PSWDSN  MAKE WHOLE NAME      85202
         LA    R2,UNITBUMP   SET SUBROUTINE FOR FAST ENTRY       85202
         BAS   R9,UNITINIT   INITIALIZE UCB SCAN                 85202
         BAS   R9,UNITLOOP   GET FIRST UCB                       85202
         LTR   R3,R3         ANY UCB LOCATED ?                   85202
         BZ    WYOLVOL       NO MORE                             85202
         CLI   UCBTBYT3,UCB3DACC  DASD DEVICE ?                  85202
         BNER  R2            NO; TRY ANOTHER                     85202
         TM    UCBSTAT,UCBPRES+UCBRESV  MOUNTED ?                85202
         BZR   R2            NO; SKIP                            85202
         TM    UCBSTAB,UCBBPUB+UCBBSTR  PUBLIC OR STORAGE ?      85202
         BZR   R2            NEITHER; SKIP                       85202
         TM    UCBSTAT,UCBONLI  ON-LINE ?                        85202
         BZR   R2            NO; SKIP                            85202
         CLC   UCBVOLI(4),=C'WYL001'  WYLBUR PACK ?              85202
         BNER  R2            NO                                  85202
         MVC   WVEVOL,UCBVOLI  SAVE VOLUME                       85202
         ST    R3,WVEUCBA    SET UCB ADDRESS                     85202
         ST    R10,PSWCPSW   SAVE THE VOLUME COUNTER             86187
         LA    R0,WVEVOL     POINT TO VOLUME SERIAL              85202
         LA    R2,PSWDSN     GET DSN TO LOCATE                  GP03126
         LR    R3,R11        SAVE OUR BASE                       85202
         L     R11,=A(GRUP2BAS)  GET BASE FOR GROUP 2 FUNCTIONS  85202
*        NOTE THAT DSDJ4COM CLOBBERS WYO---- DATA, HENCE PSW AREA IS
*              USED FOR FMT 4 NAME                               86187
         BAS   R10,DSDS1CON-GRUP2BAS(,R11)  CALL OBTAIN         GP03126
         LR    R11,R3        RESTORE OUR BASE                    85202
         L     R10,PSWCPSW   RESTORE THE VOLUME COUNTER          86187
         LA    R2,UNITBUMP   RESTORE FAST RETURN ADDRESS         86187
         LA    R3,DB+16      USE OBTAIN'S RETURN                 85202
         USING DS4IDFMT,R3   DECLARE IT                          85202
         CLI   DS4IDFMT,C'4'   REALLY FORMAT 4 DSCB ?            85202
         BE    WYOHVOL       BRANCH IF DSCB 4 READ OK            85202
         MVC   DB(MSG792L),MSG792  MOVE MESSAGE PATTERN          85202
         MVC   DB+4+22(6),WVEVOL  MOVE VOLSER                    85202
         WTO   MF=(E,DB)     TELL OPSIE                          85202
         INCH WVTNUM,INC=-1  DECREMENT VOLUME COUNT              85202
         BR    R2            RESUME SCAN LOOP                    85202
MSG792   WTO   'MSG792E ERROR ON PACK XXXXXX',MF=L,DESC=1,ROUTCDE=1
MSG792L  EQU   *-MSG792                                          85202
         SPACE 1                                                 85202
WYOHVOL  MVC   WVEVTOC,DS4VTOCE+2  COPY VTOC EXTENTS             85202
         MVC   WVETPC,DS4DEVSZ+2   COPY TRACKS/CYLINDER          85202
         MVC   WVEDSPT+1(1),DS4DEVDT  DSCBS PER TRACK            85202
         MVC   WVEDEPT+1(1),DS4DEVDB  PDS BLOCKS/TRACK           85202
         MVC   WVEFRDS,DS4DSREC  COPY NUMBER OF FREE DSCBS       85202
         DROP  R3                                                85202
         LH    R3,WVEVTOC+4   END CC                             85202
         SH    R3,WVEVTOC   LESS START CC                        85202
         BZ    *+8           SKIP IF LESS THAN ONE               85202
         MH    R3,WVETPC    * TRACKS/CYLINDER                    85202
         AH    R3,WVEVTOC+6  PLUS END TRACK                      85202
         SH    R3,WVEVTOC+2  LESS START TRACK                    85202
         AH    R3,=H'1'     NUMBER OF TRACKS IN EXTENT           85202
         STH   R3,WVEVSIZE  COMPLETE DEB EXTENT PATTERN          85202
         MVI   WVEDVMOD,X'50'  SET READ ACCESS                   85202
         LH    R3,WVEVTOC+6   GET END TRACK                      85202
         LA    R3,1(,R3)   PLUS ONE                              85202
         CH    R3,WVETPC    IS END ON CYLINDER BOUNDARY ?        85202
         BE    *+8           YES                                 85202
         OI    WVEDVMOD,X'08'  PROHIBIT HEAD-SWITCHING           85202
         LA    R5,WVELEN(,R5)  POINT TO NEXT ENTRY               85202
         BCTR  R10,R2        TRY AGAIN                           85202
         DROP  R5                                                85202
WYOLVOL  ICM   R2,3,WVTNUM   STILL HAVE AT LEAST ONE ?           85202
         BZ    WYOFAIL       NO; FATAL ERROR                     85202
         SR    R2,R2         SET EXPECTED VALUE                  85202
         CS    R2,R6,UCLWYL  STORE NEW TABLE ADDRESS             85202
         BZ    WYOLVOK       STORED; CLEAR MAIN PTRS             85202
         LR    R1,R6         COPY ADDRESS                        85202
         L     R0,WVTMAIN    RELOAD SUB-POOL AND SIZE            85202
         FREEMAIN R,LV=(0),A=(1)  FREE IT UP                     85202
         LR    R6,R2         USE TABLE BUILT ELSEWHERE           85202
WYOLVOK  XC    WYOMAIN(8),WYOMAIN  CLEAR FREEMAIN REQUEST        85202
         L     R7,CALLAD     RELOAD PARM REGISTER - JUST IN CASE
         XC    RETCH(12),RETCH  CLEAR RETURNS AGAIN              85202
         SPACE 2                                                 85202
WYLOCTAB CLRL  WYOWORK,WYOWORKL  CLEAR THE WORKING STORAGE       85202
         MVC   PSWDSN,0(R7)  DSN TO OUR MEMORY (LESS I/O PAGING) 85203
         LA    R14,WYONIT    GET INTENDED DEB, AVT, ... ADDRESS  85224
         LA    R15,WYPLEN    GET PATTERN LENGTH                  85224
         LA    R0,WYPDAVT    GET PATTERN ADDRESS                 85224
         LR    R1,R15        COPY PATTERN LENGTH                 85224
         MVCL  R14,R0        INITIALIZE                          85224
         LA    R9,WYOAVT     POINT TO SKELETON DEB PREFIX        85202
         USING DEBAVT,R9     DECLARE DEB PREFIX                  85202
         LA    R10,WYODCB     GET DCB ADDRESS                    85202
         USING IHADCB,R10                                        85202
         L     R3,WVEUCBA    GET FIRST UCB ADDRESS               85202
         USING UCBOB,R3                                          85202
         MVZ   DCBDEVT,UCBTBYT3   COPY DA FLAG                   85202
         MVN   DCBDEVT,UCBTBYT4   SET DEVICE TYPE                85202
         IC    R1,UCBTBYT4   GET THE DISK SUB-TYPE               85202
         LA    R0,X'0F'      MASK FOR SUB-TYPE INDEX             85202
         NR    R1,R0         GET INDEX                           85202
         L     R15,CVTPTR    GET UCB BACK                        85202
         L     R14,CVTXAPG-CVTMAP(,R15) GET RESIDENT APPENDAGES  85202
         MVC   DEBAVT(DEBPREFX-DEBAVT),0(R14) COPY CANNED ADDRESS
         L     R14,CVTZDTAB-CVTMAP(,R15)  GET CVT DEVICE POINTER 85202
         IC    R1,0(R1,R14)   GET INDEX TO DEVICE ENTRY          85202
         AR    R14,R1        GET ENTRY FOR THIS TYPE             85202
         ST    R14,DCBDVTBL  SAVE THE DEVICE TABLE ADDRESS       85202
         LA    R14,WYOBR14   GET DUMMY APPENDAGE                 85216
         MVC   0(WYDUMAPL,R14),WYDUMAPG  BUILD IT                85223
         ST    R14,DEBEOEA   SET END-OF-EXTENT APPENDAGE ADDRESS 85216
         LA    R14,WYOECBS   GET ECB LIST                        85202
         LA    R15,WYOECB    POINT TO FIRST ECB                  85202
         LH    R0,WVTNUM     GET NUMBER OF VOLUMES               85202
         SH    R0,=H'1'      LESS ONE                            85202
         BNP   WYOECBLL      ONLY ONE VOLUME ?                   85202
WYOECBLP ST    R15,0(,R14)   SET ECB ENTRY                       85202
         LA    R15,WYOIOBL(,R15)  POINT TO NEXT ENTRY            85202
         LA    R14,4(,R14)   DITTO                               85202
         BCT   R0,WYOECBLP     REPEAT                            85202
WYOECBLL ST    R15,0(,R14)   SET LAST ECB                        85202
         OI    0(R14),X'80'  SET END OF LIST BIT                 85202
         MVI   DCBOFLGS,X'12'  SET DCB OPEN                      85202
         MVC   DCBTIOT(4),=X'0018D008'   TIOT OFFSET / MACRF = E 85202
         STCM  R10,7,DEBDCBAD+1   SET DCB ADDRESS INT DEB        85202
         MVI   DEBLNGTH,(WYOPATL+7)/8                            85202
         MVI   DEBOFLGS,X'40'+DEBEOF+DEBRERR  SET DISP=OLD, PDS, RER
         MVI   DEBOPATB,DEBPOSIT+DEBACCS  LEAVE/OUT              85202
         ST    R9,DEBAPPAD   SAVE APPENDAGE ADDR.                85202
         MVI   DEBEXSCL,4    16 BYTES IN ACCESS METHOD SECTION   85202
         LA    R14,DEBBASND  POINT TO FIRST DEB EXTENT           85202
         LA    R15,WVTENTRY  POINT TO FIRST VOLUME ENTRY         85202
         USING WVTENTRY,R15                                      85202
         LH    R0,WVTNUM     GET NUMBER OF VOLUMES TO DO         85202
         SLL   R0,1          DOUBLE FOR FAKE EXTENTS             85224
         STC   R0,DEBNMEXT   SET EXTENT COUNT INTO DEB           85202
         SRL   R0,1          RESTORE CORRECT NUMBER              85224
WYOFLEXT MVC   0(16,R14),WVEDVMOD  MOVE EXTENT DESCRIPTION       85202
         MVC   16(4,R14),WVEDVMOD  COPY UCB AGAIN                85224
         MVC   22(4,R14),=X'FFFFFFFF'  MAKE INVALID CYLINDER/HEAD
         LA    R14,32(,R14)  SKIP TRUE AND FAKE EXTENT           85224
         LA    R15,WVTNEXT   GET NEXT TABLE ENTRY                85202
         BCT   R0,WYOFLEXT   REPEAT                              85202
         MVC   0(16,R14),WYPXIND  ADD PDS EXTENT INDEX           85202
         DROP  R15                                               85202
         LA    R1,DEBBASIC                                       85202
         STCM  R1,7,DCBDEBAD+1   SAVE DEB ADDRESS                85202
         L     R2,PSATOLD-PSA  GET TCB ADDRESS                   85202
         ST    R2,DEBTCBAD   ALSO SETS MODULES LOADED TO ZERO    85202
         LA    R2,TCBDEB-(DEBDEBAD-DEBBASIC)-TCB(,R2)            85202
         SR    R0,R0                                             85202
WYODLOOP CLM   R0,7,DEBDEBB-DEBBASIC(R2)  END OF CHAIN ?         85202
         BE    WYODLADD      YES; ADD OURS                       85202
         L     R2,DEBDEBAD-DEBBASIC(,R2)                         85202
         N     R2,=X'00FFFFFF'  CLEAR HIGH BYTE                 GP99027
         B     WYODLOOP                                          85202
WYODLADD STCM  R1,7,DEBDEBB-DEBBASIC(R2)  PLACE DEB ON CHAIN     85202
         DEBCHK WYODCB,TYPE=ADD,AM=EXCP                          85202
         OI    WYOFLAG,WYFDEB  SET DEBCHK DONE                   85202
         LA    R5,WYOXDAP                                        85202
         LH    R4,WVTNUM     GET NUMBER OF IOBS TO DO            85202
         SR    R3,R3         CLEAR EXTENT NUMBER                 85202
         LA    R2,WVTENTRY   POINT TO EXTENT ENTRY               85202
         USING WVTENTRY,R2                                       85202
         LA    R9,WYOIOB     GET IOB IN THIS SECTION             85202
         USING WYOXDAP,R5    DECLARE ENTRY                       85202
         STCM  R9,7,DCBIOBAA  SET DCB IOB ADDRESS                85202
WYOIOBLP LA    R9,WYOIOB     GET IOB IN THIS SECTION             85202
         USING IOBFLAG1,R9                                       85202
         LA    R0,WYOECB                                         85202
         ST    R0,IOBECBPT   SET ECB ADDRESS                     85202
         LA    R0,WYODCB                                         85202
         ST    R0,IOBDCBPT   SET DCB ADDRESS                     85202
         LA    R14,PSWDSN    GET DSN TO LOCATE                   85202
         ST    R14,WYOCCW1   INTO SEARCH ID CCW                  85209
         LA    R14,WYOCCW1                                       85202
         ST    R14,WYOCCW2   ADDRESS FOR FIRST TIC               85202
         LA    R14,WYOCCHHR  GET COUNT FIELD                     85202
         ST    R14,WYOCCW3   FOR READ ID OF NEXT RECORD          85209
         OC    WYOCCW1(WYOORLEN),WYPCCW  MAKE CCWS COMPLETE      85202
         MVC   WYOCCHHR(4),WVEVTOC  USE VTOC START               85203
         MVI   WYOCCHHR+4,1  START WITH RECORD 1 (DSCB4 ?)       85203
         MVC   WYOEXT,WYOCCHHR  SAVE START ADDRESS FOR RESTART   85209
         STC   R3,WYOMBB     SET EXTENT COUNT                    85202
         BAS   R9,SCHEDIO1   EXCP                                85202
         LA    R2,WVTNEXT    POINT TO NEXT EXTENT                85202
         LA    R3,2(,R3)     INCREASE EXTENT COUNT               85224
         LA    R5,WYOIOBL(,R5)  NEXT IOB                         85202
         BCT   R4,WYOIOBLP   REPEAT                              85202
         DROP  R2,R5,R9                                          85202
         SPACE 1                                                 85202
         LH    R2,WVTNUM     GET NUMBER OF ENTRIES               85202
WYOWAIT  WAIT  (R2),ECBLIST=WYOECBS  WAIT FOR ALL TO FINISH      85202
         LA    R2,WYOECBS                                        85202
WYOCHKLP L     R5,0(,R2)     LOAD ECB ENTRY                      85202
         USING WYOECB,R5                                         85202
         CLI   WYOECB,X'7F'  ENTRY FOUND ?                       85202
         BNE   WYOCHKNF      NO                                  85202
         SR    R14,R14       CLEAR FOR IC                        85202
         IC    R14,WYOMBB    GET EXTENT COUNTER                  85202
         SLL   R14,4         *16 (16-BYTE EXTENT ENTRY)          85202
         LA    R14,WYODEBEX(R14)  GET EXTENT ENTRY               85202
         ICM   R14,7,1(R14)  LOAD UCB ADDRESS                    85202
         SR    R15,R15       CLEAR                               85202
         CS    R15,R14,RETR0   FIRST TIME ?                      85202
         BZ    WYOCHKBP      YES; STASHED - TRY FOR MORE         85202
         SR    R15,R15       CLEAR                               85202
         CS    R15,R14,RETR1  ELSE TRY FOR SECOND                85202
         B     WYOEXIT       BUT EXIT ANYWAY                     85202
WYOCHKNF CLI   WYOECB,X'44'  INTERCEPTED ?                       85202
         BE    WYOCHKRS      YES; RETRY                          85202
         CLI   WYOECB,X'48'  PURGED ?                            85202
         BNE   WYOCHKBP      NO; TRY NEXT                        85202
WYOCHKRS SH    R5,=Y(WYOIOB-WYOXDAP)  POSITION TO SEGMENT        85202
         USING WYOXDAP,R5                                        85209
         MVC   WYOCCHHR,WYOEXT  SET START ADDRESS                85209
         BAS   R9,SCHEDIOB   RETRY THE I/O                       85202
         B     WYOWAIT       AND WAIT FOR COMPLETION             85202
         DROP  R5                                                85202
WYOCHKBP TM    0(R2),X'80'   END OF LIST ?                       85202
         LA    R2,4(,R2)     POINT TO NEXT ENTRY                 85202
         BZ    WYOCHKLP      NO; TRY NEXT                        85202
         ICM   R0,15,RETR0   ANYTHING FOUND ?                    85202
         BNZ   WYOEXIT       YES; RETURN CODE 0                  85202
         MVI   RC,4          ELSE SET MINOR ERROR                85202
         B     WYOEXIT                                           85202
         SPACE 2                                                 85202
WYOFREE  L     R5,UCLWYL     GET TABLE POINTER                   85202
         SLA   R5,1          KILL SIGN BIT                       85202
         BZ    WYONFREE      NOTHING TO FREE                     85202
         SRL   R5,1          RESTORE ADDRESS                     85202
         L     R6,WVTMAIN-WVTDSECT(,R5) RESTORE GETMAIN PARAMETERS
         STM   R5,R6,WYOMAIN   SET FOR EXIT TO FREE              85202
WYONFREE ST    R7,UCLWYL     CLEAR TABLE POINTER                 85202
         B     WYOEXIT       AND EXIT                            85202
         SPACE 1                                                 85202
         USING WYOXDAP,R5                                        85202
SCHEDIO1 LA    R1,WYOCCW1     SET FOR NORMAL CCW CHAIN           85202
SCHEDIO  ST    R1,IOBSTART-IOBFLAG1+WYOIOB  START CCW ADDRESS    85202
         ST    R1,IOBRESTR-IOBFLAG1+WYOIOB   INTO RESTART ALSO   85202
SCHEDIOB MVI   IOBFLAG1-IOBFLAG1+WYOIOB,X'42'  IOBCMDCH+IOBUNREL 85202
         EXCP  WYOIOB        SCHEDULE AN I/O                     85202
         BR    R9            RETURN TO CALLER                    85202
         SPACE 1                                                 85202
WYOFAIL  WTO 'MSG793E WYLBUR VTOC TABLE BUILD FAILED',DESC=1,ROUTCDE=1
WYORET12 MVI   RC,12         SET MAJOR ERROR                     85202
WYOEXIT  ICM   R1,15,WYOMAIN   ADDITIONAL MEMORY TO BE FREED ?   85202
         BZ    WYOEXIT2      NO                                  85202
         L     R0,WYOMAIN+4  GET SUBPOOL/LENGTH                  85202
         FREEMAIN R,LV=(0),A=(1)  FREE IT                        85202
WYOEXIT2 TM    WYOFLAG,WYFDEB  NEED DEBCHK FREE ?                85202
         BZ    WYOEXIT3      NO                                  85202
         DEBCHK WYODCB,TYPE=DELETE,AM=EXCP                       85202
WYOEXIT3 LA    R1,WYODEB    GET DEB ADDRESS BACK                 85202
         L     R2,PSATOLD-PSA  GET TCB                           85202
         LA    R2,TCBDEB-(DEBDEBAD-DEBBASIC)-TCB(,R2)            85202
WYOEXLUP CLM   R1,7,DEBDEBB-DEBBASIC(R2)  OUR DEB NEXT ?         85202
         BE    WYOEXDEL                                          85202
         ICM   R2,7,DEBDEBB-DEBBASIC(R2)  GET NEXT DEB           85202
         BNZ   WYOEXLUP      LOOK AT IT                          85202
         B     WYOEXDLT      HOW DID THIS HAPPEN ?               85202
WYOEXDEL MVC   DEBDEBB-DEBBASIC(3,R2),DEBDEBB-DEBBASIC(R1)       85202
WYOEXDLT B     EXIT          QUIT                                85202
WYDUMAPG DS    0H            PATTERN E-O-E APPENDAGE             85223
         B     0(,R14)       GO TO ABNORMAL END APPENDAGE        85223
WYDUMAPL EQU   *-WYDUMAPG    DUMMY END-OF-EXTENT APPENDAGE       85216
         SPACE 2                                                 85202
         LTORG ,                                                 85202
         SPACE 2                                                 85202
         PRINT &PRTSYS                                           85202
         SPACE 1                                                 85202
WYPCCW   CCW   X'A9',PSWDSN-PSWDSN,X'60',L'PSWDSN  SEARCH KEY = DSN
         CCW   X'08',WYOCCW1-WYOCCW1,0,0   TIC BACK              85224
         CCW   X'12',WYOCCHHR-WYOCCHHR,X'20',8   READ NEXT ID    85224
WYPDAVT  DC    AL1((WYOPATL+7)/8,0,0,0)                          85202
WYPDEB   DC    F'0'          TCB ADDR                            85203
         DC    X'10',AL3(0)  DEB LINK PTR                        85202
         DC    X'68',AL3(0)    IRB PTR                           85202
         DC    F'0'          FLAGS                               85202
         DC    F'0'          IOB                                 85202
         DC    X'FC',AL3(0) .    PRTY / ECB                      85202
         DC    X'0F',AL3(WYPDCB)                                 85202
         DC    X'02',AL3(0)  APP ADDR                            85202
         DC    (2*&WYLDMAX)XL16'0'  UCB/EXTENT/SIZE PER VOLUME   85224
WYPXIND  DC    AL1(0,2,4,6,8,10,12,14,16,18,20,22,24,26,28,30) EXTPTR
WYPDCB    DCB   DDNAME=*SVC255*,DSORG=PS,DEVD=DA,MACRF=(E)       85202
WYPLEN   EQU   *-WYPDAVT                                         85202
.NOWYLOC SPACE 2                                                 85202
FSAWORK  DSECT ,             RESUME COMMON SAVE AREA             85202
         AIF   (NOT &NOLO).NOWYSAV                               85202
         ORG   COPYDSN       USE SAVE PORTION OF WORK AREA       85202
WYOMAIN  DC    2A(0)         GETMAIN ADDRESS/SUBPOOL/LENGTH      85202
WYOFLAG  DC    X'00'         SPECIAL PROCESSING FLAG             85202
WYFDEB   EQU   X'80'                                             85202
WYOBR14  DC    0Y(0),XL(WYDUMAPL)'0'  E-O-E APPENDAGE            85223
         ORG   DB            USE END OF WORK AREA FOR REMAINDER  85202
WYOWORK  DS    0D                                                85202
WYOAVT   DC    8F'0'         AVT                                 85202
WYONIT   DC    AL1((WYOPATL+7)/8,0,0,0)                          85202
WYODEB   DC    F'0'          TCB ADDR                            85203
         DC    X'10',AL3(0)  DEB LINK PTR                        85202
         DC    X'68',AL3(0)    IRB PTR                           85202
         DC    F'0'          FLAGS                               85202
         DC    F'0'          IOB                                 85202
         DC    X'FC',AL3(0) .    PRTY / ECB                      85202
         DC    X'0F',AL3(WYODCB)                                 85202
         DC    X'02',AL3(0)  APP ADDR                            85202
WYODEBEX DC    (2*&WYLDMAX)XL16'0'  UCB/EXTENT/SIZE PER VOLUME   85224
         DC    AL1(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15) EXTPTR 85203
WYOPATL  EQU   *-WYOAVT                                          85202
WYODCB   DCB   DDNAME=*SVC255*,DSORG=PS,DEVD=DA,MACRF=(E)        85202
WYOECBS  DC    (&WYLDMAX)A(0)  ECB LIST                          85202
         SPACE 1                                                 85202
WYOXDAP  DS    0D            FIRST IOB BLOCK                     85202
         XDAP  WYOECB,RI,WYODCB,WYOCCHHR,8,1,MF=L  ECB/IOB       85202
WYOIOB   EQU   WYOECB+4                                          85202
WYOMBB   EQU   WYOIOB+32,8,C'X'  DASD ADDRESS M B B C C H H R    85202
WYOCCHHR EQU   WYOMBB+3,5,C'X'  DEFINE CCHHR FIELD ONLY          85202
         ORG   WYOIOB+48                                         85216
WYOCCW1  CCW   X'A9',PSWDSN,X'40',L'PSWDSN  M/T SEARCH ID        85224
WYOCCW2  CCW   8,WYOCCW1,0,0    TRY AGAIN                        85224
WYOCCW3  CCW   X'12',WYOCCHHR,X'20',5  READ NEXT RECORD'S ID     85209
WYOORLEN EQU   *-WYOCCW1     LENGTH OF OC                        85202
WYOEXT   DS    XL5'0'        FIRST CCHHR                         85209
         DS    0D            FORCE CCW ALIGNMENT                 85209
WYOIOBL  EQU   *-WYOXDAP     LENGTH OF ONE ENTRY                 85202
         DC    (&WYLDMAX-1)XL(WYOIOBL)'0'  ROOM FOR MORE         85202
WYOWORKL EQU   *-WYOWORK     LENGTH TO CLEAR                     85202
.NOWYSAV SPACE 1                                                 85202
         ORG   ,             GET MAXIMUM SIZE OF WORK AREA       85202
FSALEN   EQU   *-FSAWORK                                         85202
         SPACE 1                                                 81145
         PRINT GEN                                               81145
         SERVCOMP ,          EXPAND COMPRESS/EXPAND WORK AREA    81145
         PRINT &PRTSOR                                           81145
         SPACE 2
@SERVICE RSECT ,                                                GP99026
         SPACE 1
         COPY SERVFLAG       EXPAND FLAGS
         SPACE 1
         PRINT NOGEN
         REGEQU ,                                                81137
         SPACE 1                                                 81137
         CVT   DSECT=YES                                         81137
         SPACE 1                                                 81137
         IKJTCB ,                                                81137
         SPACE 1                                                 81151
         IHACDE ,                                                81151
         SPACE 1                                                 81145
         IHALLE ,                                                81151
         SPACE 1                                                 81145
IEFTIOT1 DSECT ,                                                 81145
         IEFTIOT1 ,                                              81145
         SPACE 1                                                 81137
         IEFTCT ,                                                81137
         SPACE 1                                                 81137
UCBDSECT DSECT ,                                                 81137
         IEFUCBOB ,                                              81137
         SPACE 1                                                 81137
         IEFJMR ,
         SPACE 1                                                 81137
DSCB1    DSECT ,                                                 81137
         IECSDSL1 1          EXPAND FORMAT 1 DSCB                81137
         SPACE 1                                                 82108
DSCB3    DSECT ,                                                 82108
         IECSDSL1 3          EXPAND FORMAT 3 DSCB                82108
         SPACE 1                                                 85202
         IECSDSL1 4          EXPAND FORMAT 4 DSCB                85202
         SPACE 1                                                 85202
         IEZIOB ,                                                90273
         SPACE 1                                                 81200
         IEFJFCBN ,                                              81200
         SPACE 2                                                 81359
         DCBD  DSORG=PS                                          81359
         SPACE 1                                                 81359
         IEZJSCB ,                                               83254
         SPACE 1                                                 83254
         AIF   (NOT &MVS).BIGEND                                 83100
         IHAPSA ,                                                83100
         SPACE 1                                                 90308
         IHAQDB ,                                                90308
         SPACE 1
         IHADSAB ,                                               90308
         SPACE 1                                                 90308
         IEFASIOT ,                                              90308
         SPACE 1                                                 83100
@SERVICE RSECT ,                                                GP99026
         IEZDEB ,            FOR SP1.3.3/5 JES                   84316
         IFGRPL ,                                                90154
         IEFJSSIB ,                                              83254
         SPACE 1                                                 83254
.BIGEND  AIF   (NOT &LOCO).LOCEND                                84087
USERTCB  USERTCB ,                                               84087
WVTDSECT USERWYL ,           EXPAND WYLBUR VTOC TABLE            85202
.LOCEND  END   ,                                                 84087
