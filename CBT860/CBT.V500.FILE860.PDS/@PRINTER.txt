@PRT     TITLE '@ P R I N T E R  ***  PRINTER SERVICES'
         PUNCH '   ORDER @PRINTER(P) '  MAKE DUMPS NEATER       GP05032
         SPACE 1
         COPY  OPTIONGB                                          81159
         SPACE 2                                                 82102
         LCLC  &DEFUCS       DEFAULT UCS ID                      81159
         LCLC  &DEFFCB       DEFAULT FCB ID                     GP08145
         SPACE 1                                                 82102
         SYSPARM LIST=YES    MERGE AND SHOW OPTIONS              81159
         EJECT ,
@PRINTER CSECT ,
***********************************************************************
*                                                                     *
*                    INSTALLATION DEPENDENT VALUES                    *
*                                                                     *
         AIF   (NOT &MVSSP).NOSPLV                               93164
         SPLEVEL SET         USE CURRENT SP LEVEL                93164
.NOSPLV  AIF   ('&LOCAL   '(1,3) EQ 'ISO').LOCISO                82109
MAXWIDTH EQU   204           MAX DEVICE WIDTH TO SUPPORT (3800/9700)
         SPACE 1
DEFPUW   EQU   80            DEFAULT PUNCH WIDTH
         SPACE 1
DEFPRW   EQU   132           DEFAULT PRINT WIDTH
         SPACE 1
&DEFUCS  SETC  'TN  '        DEFAULT PRINT TRAIN                GP05190
         SPACE 1
&DEFFCB  SETC  '6LPI'        DEFAULT FORMS CONTROL              GP08145
         SPACE 1                                                 81159
DEFUNPRT EQU   C'.'          DEFAULT UNPRINTABLE CHARACTER       81186
         SPACE 1
DEFLPP6  EQU   60            LINES/PAGE DEFAULT (6 LPI)
DEFLPP8  EQU   82            LINES/PAGE DEFAULT (8 LPI)
         AGO   .LOCCOM                                           82109
.LOCISO  ANOP  ,                                                 82109
MAXWIDTH EQU   204           MAX DEVICE WIDTH TO SUPPORT (3800/9700)
         SPACE 1
DEFPUW   EQU   80            DEFAULT PUNCH WIDTH
         SPACE 1
DEFPRW   EQU   132           DEFAULT PRINT WIDTH
         SPACE 1
&DEFUCS  SETC  'P11 '        DEFAULT PRINT TRAIN (PN/P11)        82109
         SPACE 1
&DEFFCB  SETC  '6LPI'        DEFAULT FORMS CONTROL              GP08145
         SPACE 1                                                 81159
DEFUNPRT EQU   C'.'          DEFAULT UNPRINTABLE CHARACTER       81186
         SPACE 1
DEFLPP6  EQU   57            LINES/PAGE DEFAULT (6 LPI)          82109
DEFLPP8  EQU   76            LINES/PAGE DEFAULT (8 LPI)          82109
.LOCCOM  SPACE 1                                                 82109
*                                                                     *
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1981  EXPERT SYSTEM PROGRAMMING INC.               *
*        COPYRIGHT 2002-2005  EXPERT SYSTEM PROGRAMMING               *
*                             176 OLD STAGE COACH ROAD                *
*                             BRADFORD, VT 05033-8844                 *
*                                                                     *
*        ALL RIGHTS RESERVED.                                         *
*                                                                     *
***********************************************************************
*                                                                     *
*  MAINTENANCE:                                                       *
*                                                                     *
*  1999.026  GYP  BASIC AMODE 31 SUPPORT, EXCEPT PRTF                 *
*  1999.063  GYP  ADDED PRTS SUPPORT (NULL TERMINATED STRING)         *
*  2002.234  GYP  ADD PRTF SUPPORT FOR 31-BIT ADDRESS; CO-REQUISITE   *
*                   CHANGES TO PRTF, PRTCOM, PRTWORK, AND MAPPRT MACS *
*  2003.022  GYP  'FIXED' PRTF SUPPORT; CHANGED ALL PRT MACROS        *
*  2003.304  GYP  REPLACED FORMATTING WITH #CNVRT CALLS (TO @FORMATS) *
*  2004.234  GYP  FIXUP TO RUN UNDER MVS 3.8J                         *
*  2008.145  GYP  FIXED UCS/FCB PROCESSING (0 TO JFCB IF DEFAULT)     *
*                                                                     *
***********************************************************************
         EJECT ,
***********************************************************************
*        PRINT SERVICE ROUTINE                                        *
*          TO USE WITH MACROS, LOAD EP=@PRINTER / ST R0,@PRINTER      *
*          ALSO NOTE THE LPALD OPTION OF THE SERVCALL MACRO.     82102*
*                                                                     *
*        FUNCTIONS :                                                  *
*                                                                     *
* &NM    PRTWORK  DDNAME,ALTDDNM,WIDTH=,LPP=,TITLE=,FOOTER=,RECFM=    *
*         DEFINITION OF PRINTER WORK AREA. ASSOCIATION BETWEEN A WORK *
*         AREA AND A 'DEVICE NUMBER' (1-8) IS MADE AT OPEN TIME.      *
*                                                                     *
* &NM    PRTOPEN  WORK,DEV=N   OPENS PRINT/PUNCH FILE 'N'             *
*                                                                     *
* &NM    PRTCLOSE DEV=N OR DEV=ALL  CLOSES FILE 'N' OR ALL FILES      *
*                                                                     *
* &NM    PRTCLOSE SPIN,DEV=N   NOP UNLESS SYSOUT FILE MAY BE SPUN OFF *
*                                                                     *
* &NM    PRTROOM M,DEV=       RESERVES 'M' LINES ON PAGE OR EJECTS    *
*                                                                     *
* &NM    PRTSPACE M,DEV=      SPACES 'M' BLANKS (UP TO ONE PAGE)      *
*                                                                     *
* &NM    PRTLIST LISTAD,DEV=  PROCESSES A LIST OF REQUESTS SPECIFIED  *
*          BY FDPRT DEFINITIONS.                                      *
*                                                                     *
* &NM    PRTV  RECAD,DEV=,TITLE=M,FOOTER=N,CC=   PRINTS A V-FORMAT    *
*          RECORD, OR SETS A NEW TITLE AND FOOTER LINE                *
*                                                                     *
* &NM    PRTF  RECAD,LEN,DEV=,TITLE=M,FOOTER=N,CC=,AM=24  PRINT FIXED *
*          LENGTH RECORD. IF 'LEN' OMITTED, USES L'RECAD.             *
*                                                                     *
* &NM    PRTF  RECAD,LEN,DEV=,TITLE=M,FOOTER=N,CC=,PRT=(PRTWORK...)   *
*          FIXED LENGTH RECORD. IF 'LEN' OMITTED, USES L'RECAD.       *
*                                                               GP99063
* &NM    PRTS  RECAD,LEN,DEV=,TITLE=M,FOOTER=N,CC=,END=  PRINTS A     *
*          STRING, TERMINATED BY X'00' OR END CHARACTER.        GP99063
*                                                                     *
* &NM    PRTBIG - BIG LETTER ROUTINE - SEE 'PSEPAR' PAGE         81186*
*                                                                81186*
* &NM    PRTSNAP LISTADDR - LIST OF SNAP/DUMP REQUESTS           83331*
*                                                               GP99029
* &NM    PRTITEM ITEMAD,DEV=  PROCESSES A SINGLE REQUEST SPECIFIED    *
*          BY AN FDPRT DEFINITION. (MAY BE GOTO OR EXEC).       GP99029
*                                                               GP99029
***********************************************************************
         PRINT &PRTSOR
@PRINTER CSECT ,                                                 82031
         USING @PRINTER,R15                                      82031
         ENTRY PRINTEST      ****DEBUG*****                      83331
PRINTEST B     PRINTER       SKIP ID                             82031
         DC    AL1(L'PRINTID)   LENGTH FOR SNAP                  82031
PRINTID  DC    C'@PRINTER - &SYSDATE'                            82031
PRINTER  BSM   R14,0         SAVE CALLER'S AMODE                GP99026
         STM   R14,R12,12(R13)   XX                              82031
         LR    R12,R15       SET PRIMARY BASE REGISTER           82102
         DROP  R15                                               82031
         USING @PRINTER,R12                                      82102
         AM31  WORK=R15      LET'S LIVE DANGEROUSLY             GP99026
         SPACE 1                                                 82031
*        LOCATE OR ESTABLISH WORK AREA                           82031
*                                                                82031
         ICM   R4,15,PRINTID+1  ID FIELD                         82031
         BAL   R9,LOOKFSA    FIND THE FSA ENTRY                  93164
           B   CURSTEST      FOUND IT, BUT CHECK RECURSION       84176
         LTCB  R5            GET MY TCB BACK                     87311
         LA    R3,FSWLEN     GET LENGTH                          82031
         BAL   R9,GETFSA     GET SPACE FOR PERMANENT WORK AREA   93164
*        L     R7,TCBFSA-TCB(,R5)  CHAIN AS FIRST ENTRY          82031
         USING PRT0WORK,R8                                       82031
         MVC   FSWLINK,FSWLINK-PRT0WORK(R7)  CHAIN OLD AREA      82031
         ST    R8,FSWLINK-PRT0WORK(,R7)  CHAIN THIS AHEAD OF OLD ONE
         SLR   R6,R6         CLEAR MASTER WORK AREA POINTER      84176
         B     PRTPREP       PREPARE COMMON PREFIX ENTRIES       84176
CURSTEST TM    USEXFLAG,USXFACT  CALLED FROM USER EXIT ?         84176
         BZ    PRTHAVE       NO; USE FIXED AREA                  84176
         LR    R5,R3         SAVE TCB ADDRESS                    84176
         LA    R3,FSWLEN     SET LENGTH                          84176
         LR    R6,R8         SAVE OLD ENTRY                      84176
         BAL   R9,GETFSA     GET ANOTHER AREA                    93164
         MVC   @FORMATS-PRT0WORK(4,R8),@FORMATS-PRT0WORK(R6)    GP03326
PRTPREP  ST    R6,USEX@WRK   SAVE MASTER WORK ADDRESS            84176
         MVI   USEXFLAG,0                                        84176
         ST    R5,FSWTCB     SAVE TCB ADDRESS                    84176
PRTHAVE  LR    R10,R13       COPY USER'S SAVE AREA ADDRESS       82031
         LA    R13,SAVE       AND ESTABLISH OURS                 82031
         DROP  R8            FSA HEADER NO LONGER ACCESSIBLE    GP99028
         USING SAVE,R13                                          82031
         ST    R10,SAVE13-SAVE(,R13)  MAKE BACK CHAIN            82031
         ST    R13,SAVEFWD-SAVE(,R10)    AND FORWARD CHAIN       82031
         XC    PRT0CLR(PRT0CLRL),PRT0CLR  CLEAR MOST OF GOTTEN AREA
         ICM   R0,15,@FORMATS  FORMATTING LOADED ?              GP03326
         BNZ   PRTHAVEF      YES; DON'T DO IT AGAIN             GP03326
         LPALOOK EP=@FORMATS,DCB=4  LOCATE THE FORMATTING ROUTINE
         ST    R0,@FORMATS   SAVE FOR FORMATTING CALLS          GP03326
PRTHAVEF MVC   CALLPARM(8),SAVE0-SAVE(R10)  COPY R0 AND R1       81271
         MVC   CALLLEN,CALLAD   OLD AM24 LENGTH PARAMETER
         CLI   CALLFUN,CFRECF31  IS THIS THE NEW PRTF?          GP02234
         AIF   (&MVSXA).DOACR0                                  GP04234
         BNE   NORMPARM      SKIP PRTS TEST                     GP05229
         B     EXIT16        ACR 0 NOT SUPPORTED (USE OLD FORM) GP04234
         AGO   .DONACR                                          GP04234
.DOACR0  ANOP  ,                                                GP04234
         BE    ACR0PARM      YES; DON'T CLOBBER ADDRESS         GP02234
         CLI   CALLFUN,CFSTR   IS THIS PRTS?                    GP99120
         BNE   NORMPARM      NO                                 GP99029
ACR0PARM STAM  R0,R0,CALLACR0  EXTRACT PARAMETER
.DONACR  ANOP  ,                                                GP04234
NORMPARM TM    12(R10),X'80'  WAS CALLER IN AMODE 31?           GP99029
         BNZ   DONEPARM      YES; NOTHING ELSE TO DO            GP99029
         NI    CALLAD,X'80'  NO; CLEAR HIGH BYTE (KEEP HIGH BIT)GP03025
DONEPARM MVC   PLSPACE(6),MINVCON  INITIALIZE SPACING RECORD     81159
         MVC   PLSPACCC+1(MAXWIDTH),PLSPACCC  CLEAR TEXT         81159
         CLI   CALLDEV,0     USER SPECIFIED DEVICE NUMBERS ?
         BNE   RECURSED      YES
         MVI   CALLDEV,1     ELSE DEFAULT TO FILE 1
         B     RECURSED      SKIP TEST                           82102
         SPACE 1
RECURSE  CLI   CALLDEV,0     ALL REQUESTS DONE ?
         BE    EXITSET       YES; QUIT
RECURSED LA    R1,1          SET TM BIT
         LA    R2,8          TEST 8 BITS
LOOPPW   STC   R1,PRT0OMSK   SET MASK BIT                        82031
         STC   R1,DB         SET FOR LOOKPW                      82031
         NC    PRT0OMSK,CALLDEV  IN USER'S REQUEST               82031
         BNZ   LOOKPW        ON - FIND ASSOCIATED WORK AREA
BUMPPW   SLL   R1,1          SET NEXT BIT
         BCT   R2,LOOPPW     TRY AGAIN
EXITSET  ICM   R15,15,IEFSD095  BIG LETTER ROUTINE LOADED ?     GP99028
         BZ    EXITSETD      NO                                  81159
         DELETE EPLOC=MODSD095  DELETE IT                        81159
         XC    IEFSD095,IEFSD095  FOR POSTERITY                 GP04062
EXITSETD LH    R7,RETCH      GET RETURN CODE                     82031
         LR    R8,R13        SAVE OLD SAVE AREA                  82031
EXITSETT L     R13,SAVE13-SAVE(,R13)  RESTORE OLD ONE            84176
         PUSH  USING                                            GP03325
         DROP  R13                                              GP03325
         USING SAVE,R8                                          GP03325
         ICM   R15,15,USEX@WRK  RECURSION RETURN ?              GP03325
         BZ    EXITSETO      NO                                  84176
         MVC   PRT0OPEN-PRT0WORK(2,R15),PRT0OPEN  SAVE OFLGS    GP03325
         B     EXITSETF      FORCE FREE GETMAINED SAVE AREA      84176
EXITSETO CLI   PRT0OPEN,0    ANY DCBS OPEN ?                    GP03325
         BNE   EXITFREE      YES; DON'T FREE WORK AREA           82031
         DELETEST EP=@FORMATS,LEN=4                             GP03316
EXITSETF SH    R8,=Y(SAVE-PRT0WORK)  GET PREFIX BACK             84176
         USING PRT0WORK,R8                                      GP03325
         L     R3,FSWTCB     SET TCB ADDRESS                    GP03325
         BAL   R9,FREEFSA    UNCHAIN AND FREE AREA               93164
         POP   USING                                            GP03325
EXITFREE LR    R15,R7        COPY RETURN CODE                    82031
EXITFREF LM    R0,R12,SAVE0-SAVE(R13)  RELOAD REGISTERS          82031
         L     R14,SAVE14-SAVE(,R13)  RESTORE RETURN             82031
         BSM   0,R14         RETURN TO CALLER                   GP99026
         SPACE 1
EXIT16   MVI   RETCC,16      SET DISASTER
         B     EXITSET       AND QUIT
         SPACE 1
         USING SAVE,R13                                         GP99028
         USING PRNTWORK,R8   DECLARE DYNAMIC PRINT WORK AREA
LOOKPW   XI    DB,X'FF'      COMPLEMENT IT                       81273
         NC    CALLDEV,DB    AND COMPLIMENT IT                   81273
         LA    R4,9
         SR    R4,R2         GET RELATIVE DEVICE NUMBER
         O     R4,=C'PRT0'   MAKE PW ID FIELD NAME
         BAL   R9,LOOKFSA    FIND THE WORK AREA                  93164
         B     BRANCHPW      FOUND; USE IT                       82031
         USING TCB,R3
BUMPTCB  CLM   R3,15,TCBJSTCB  TOP OF TREE ?                    GP99028
         BE    NONEPW        YES; SEE IF OPEN
         ICM   R3,15,TCBOTC   GET PARENT                        GP99028
         BZ    NONEPW        NONE ?                              82031
         BAL   R9,LOOTFSA    TRY AGAIN                           93164
           B   BRANCHPW      FOUND; USE IT                       82031
         B     BUMPTCB       ELSE TRY AGAIN                      82031
         DROP  R3,R8                                             82031
         USING SAVE,R13                                         GP99029
NONEPW   SLR   R8,R8         JUST IN CASE
         CLI   CALLFUN,CFOPEN  OPEN ?                           GP99029
         BE    BRANCHPW      YES; PROCESS
SET4     OI    RETCC,RETC4   SET MINOR ERROR - NOT OPEN
         B     RECURSE       SEE IF MORE
         SPACE 1
BRANCHPW IC    R0,CALLFUN    GET REQUESTED FUNCTION              81193
         BIX   ERR=EXIT16,             BRANCH BY INDEX OFFSET          *
               LOC=(PCLOSE,PSPIN,POPEN,PROOM,PSPACE,PLIST,PRECV,PRECF,P*
               SEPAR,PSNAP,PLIST,PRECS,PRECF)    BRANCH BY REQ. GP02234
CFCLOSE  EQU   0             CLOSE   FUNCTION                   GP99029
CFSPIN   EQU   1             TCLOSE  FUNCTION                   GP99029
CFOPEN   EQU   2             OPEN    FUNCTION                   GP99029
CFROOM   EQU   3             ROOM    FUNCTION                   GP99029
CFSPACE  EQU   4             SPACE   FUNCTION                   GP99029
CFLIST   EQU   5             LIST    FUNCTION                   GP99029
CFRECV   EQU   6             RECV    FUNCTION                   GP99029
CFRECF   EQU   7             RECF    FUNCTION                   GP99029
CFSEPAR  EQU   8             SEPAR   FUNCTION                   GP99029
CFSNAP   EQU   9             SNAP    FUNCTION                   GP99029
CFITEM   EQU   10            ITEM    FUNCTION                   GP99029
CFSTR    EQU   11            STRING  FUNCTION                   GP99063
CFRECF31 EQU   12            PRTF AMODE 31 ENTRY (L IN PWFLEN)  GP02234
         SPACE 1                                                 82031
LOOKFSA  LTCB  R3            GET THE TCB                         86288
LOOTFSA  LA    R7,TCBFSA-TCB(,R3)  POINT TO CHAIN HEAD           87311
LOOPFSA  L     R8,FSWLINK-PRT0WORK(R7)   GET NEXT ENTRY         GP03027
         N     R8,=X'00FFFFFF'  CLEAN IT (NEED IN TSO)          GP03027
         BZ    4(,R9)        EXIT; NO ENTRY FOUND                82031
         DROP  R13                                              GP99029
         USING PRT0WORK,R8   DECLARE IT                          82031
         CL    R4,FSWID      MATCH ?                             82031
         BNE   LOORFSA       NO; TRY SOME MORE                   86288
         CLI   CALLFUN,CFOPEN  OPEN ?                           GP99029
         BNER  R9            NO; RETURN FOUND                    86288
*        THIS INSERTION WAS MADE TO HANDLE MULTI-TASKING WITH    86288
*        INDEPENDENT PROGRAMS.  THE SAME DEVICE NUMBER MAY BE    86288
*        USED MORE THAN ONCE FOR DISTINCT DDNAMES, E.G., WHEN    86288
*        COPYVOL ATTACHES WYLCOPY.                               86288
         LTCB  R15           GET MY TCB BACK                     86288
         C     R15,FSWTCB    SAME TCB ?                          86288
         BER   R9            YES; MUST ACCEPT MATCH              86288
         SR    R15,R15                                          GP99028
         ICM   R15,15,CALLAD   LOAD THE USER'S AREA             GP99029
         BZ    LOORFSA       INVALID; IGNORE TO FORCE OPEN ERROR 86288
         USING PRNTWORK,R8                                       86288
         USING SAVE,R13                                         GP99029
         USING PRTMAP,R15    MAP USER'S AREA                     86288
         CLC   PWDDNAM,PUDDNAM  MATCH ?                          86288
         BER   R9            YES; PROCESS                        86288
         CLC   PWDDNAM,PUDDALT  ALTERNATE ?                      86288
         BER   R9                                                86288
         CLI   PWDDNAM,C'*'  CONSOLE OR TPUT ?                   86288
         BNE   LOORFSA       NO                                  86288
         CLC   PWDDNAM(2),PUDDNAM  MATCH ?                       86288
         BER   R9            YES                                 86288
         CLC   PWDDNAM(2),PUDDALT                                86288
         BER   R9            YES                                 86288
LOORFSA  LR    R7,R8                                             82031
         B     LOOPFSA       TRY AGAIN                           82031
         DROP  R13,R15                                          GP99029
         USING PRT0WORK,R8   RESTORE BASE WORK AREA              86288
         SPACE 1                                                 82031
GETFSA   GETMAIN RC,LV=(R3)  GET THE WORK AREA                   93164
         LR    R8,R1         SAVE THE ADDRESS                    82031
         LR    R0,R1         SET ADDRESS FOR CLEAR               82031
         LR    R1,R3         COPY THE LENGTH                     82031
         SLR   R14,R14       CLEAR ADDRESS                       82031
         SLR   R15,R15       FROM LENGTH                         82031
         MVCL  R0,R14        CLEAR THE AREA                      82031
         ST    R4,FSWID      SET THE CONTROL BLOCK ID FIELD      82031
         ST    R3,FSWSPLEN   SET THE SUBPOOL/LENGTH FIELD        82031
         BR    R9            RETURN TO CALLER                    82031
         USING PRNTWORK,R8   SET FOR DEVICE WORK AREA            82031
         USING SAVE,R13                                         GP99029
         SPACE 1                                                 82031
FREEFSA  L     R2,TCBFSA-TCB(,R3)  GET HIGH-LEVEL SAVE AREA      82102
FREEFSAL CL    R8,0(R2)   THIS ONE ?                            GP99028
         BE    FREEFSAU      YES; UNCHAIN
         ICM   R2,15,0(R2)   GET NEXT                           GP99028
         BNZ   FREEFSAL      TRY AGAIN
         B     FREEFSAM      ELSE FREE
FREEFSAU MVC   0(4,R2),PWLINK  UNCHAIN THIS ONE
FREEFSAM L     R0,PWSPLEN    GET GETMAIN LENGTH/SUBPOOL
         FREEMAIN R,LV=(0),A=(R8)
         BR    R9            RETURN TO CALLER                    82031
         EJECT ,
*        PROCESS FIXED LENGTH RECORD                            GP02234
*                                                               GP02234
PRECF    ICM   R6,15,CALLAD   GET RECORD ADDRESS                GP99029
         BZ    EXIT16        NONE ?                             GP02234
         SLR   R7,R7                                            GP02234
         ICM   R7,1,CALLLEN  GET USER SUPPLIED LENGTH           GP02234
PRECFCOM BP    PRECCOM       USE IT (NO ADJUST FOR USER'S CC)   GP02234
         LH    R7,PWWIDTH    ELSE GET DEFAULT WIDTH
         TM    CALLFLG,CLNOCC   CC PRESENT ?                     81193
         BNZ   PRECCOM       NO                                  81193
         LA    R7,1(,R7)     INCREASE LENGTH FOR CC              81193
         B     PRECCOM
         SPACE 1
*        PROCESS STRING RECORD - TERMINATED BY X'00' OR END CHAR.
*                                                               GP99063
PRECS    ICM   R6,15,CALLAD   GET RECORD ADDRESS                GP99029
         BZ    EXIT16        NONE ?                             GP99063
         SR    R0,R0
         IC    R0,CALLLEN    LOAD SEARCH END OF STRING CHARACTER
         LR    R14,R6        LOAD START OF STRING
         LR    R15,R14       COPY IT
         AH    R15,PWWIDTH   SET ADDRESS AFTER STRING
         TM    CALLFLG,CLNOCC   CC PRESENT ?                    GP99063
         BNZ   PRECSTRY      NO                                 GP99120
         LA    R15,1(,R15)   INCREASE LENGTH FOR CC             GP99063
PRECSTRY SRST  R15,R14       LOOK FOR END
         BO    PRECSTRY      RESUME AFTER INTERRUPTION
         LR    R7,R15        LOAD END ADDRESS
         SR    R7,R6         GET LENGTH
         B     PRECCOM       JOIN COMMON
         AGO   .OLDSTR
         SLR   R14,R14                                          GP99063
         ICM   R14,1,CALLLEN  GET USER SUPPLIED END CHARACTER   GP99063
         LH    R15,PWWIDTH    ELSE GET MAXIMUM WIDTH            GP99063
         TM    CALLFLG,CLNOCC   CC PRESENT ?                    GP99063
         BNZ   PRECSLUP      NO                                 GP99120
         LA    R15,1(,R15)   INCREASE LENGTH FOR CC             GP99063
PRECSLUP XC    DBSAVE,DBSAVE  CLEAR TRT AREA                    GP99120
         LA    R0,8          SET SCAN STOPPER
         STC   R0,DBSAVE(R14)  MAKE SCAN STOPPER AT END CHAR
         LA    R1,0(R6,R15)  DEFAULT END POSITION - END OF LINE GP99120
         BCTR  R15,0                                            GP99120
         EX    R15,PRECSTRT                                     GP99120
         LR    R7,R1         COPY HIT ADDRESS                   GP99120
         SR    R7,R6         GET LENGTH                         GP99120
         B     PRECCOM                                          GP99063
PRECSTRT TRT   0(0,R6),DBSAVE  LOOK FOR END OF STRING           GP99120
.OLDSTR  ANOP  ,
         SPACE 1                                                GP99063
*        V-FORMAT PRINTING
*
PRECV    ICM   R6,15,CALLAD  GET RECORD ADDRESS                 GP99029
         BZ    EXIT16        NONE ?
         LH    R7,0(,R6)     GET LENGTH
         SH    R7,=H'4'      ALLOW FOR RDW LENGTH
         BM    EXIT16        ERROR
         LA    R6,4(,R6)     SPACE TO DATA
         SPACE 1
PRECCOM  BAL   R9,CLEAROUT   CLEAR STACKED REQUESTS              93164
           B   RECURSE       RETURN IF NOT OPEN                  81169
         BAL   R14,SHEETEST  TEST FOR SHEET EJECT                93164
         LA    R4,PWRECCC    POINT TO WORK RECORD                81169
         LA    R5,L'PWREC+1  LENGTH+CC
         TM    CALLFLG,CLNOCC  CC SUPPLIED ?
         BZ    PRECVC        YES
         LA    R4,1(,R4)     ELSE SET TO MOVE DATA
         BCTR  R5,0          ADJUST LENGTH
         MVI   PWRECCC,C' '  SINGLE SPACE
PRECVC   ICM   R7,8,BLANKS   SET BLANK FILL
         MVCL  R4,R6         MOVE TEXT AND PAD
         ICM   R1,15,PWTRAN   ANY TRANSLATION TO BE DONE ?      GP99028
         BZ    *+10          NONE                               GP99029
         TR    PWREC,0(R1)   TRANSLATE IT
         CLI   CALLTIT,0     TITLE/FOOTER OPTION ?
         BNE   PTITLE        YES; PROCESS DIFFERENTLY
         LA    R14,RECURSE   SET RETURN POINT
         SPACE 2
*        COMMON PUT ROUTINE WITH TRUNCATION
*
PUTTRUNC STM   R14,R9,PRTSAVE
         LA    R0,L'PWREC
         LA    R1,PWREC      POINT TO TEXT
         #CNVRT DBKR,BP=PRECVL  DEBLANK                         GP03316
         SLR   R0,R0         ELSE SET MINIMUM
PRECVL   AH    R0,=H'5'      ADD RDW AND CC LENGTH
         STH   R0,PWVCON     SET NEW LENGTH                     GP05032
         B     PUTPRINC      GO TO COMMON
         SPACE 2
*        COMMON PRINT ROUTINE
*
PUTPRINT STM   R14,R9,PRTSAVE   SAVE REGISTERS
PUTPRINC MVC   PWCCSAV,PWRECCC  SAVE CALLER'S CONTROL BYTE       81167
         CLI   PWRECCC,C'&&'   SPECIAL CONTROL CHARACTER ?       81173
         BNE   PUPNAMP       NO                                  81173
         TM    DCBRECFM-IHADCB+PWDCB,DCBRECCC  ANY CC IN OUTPUT ?
         BZ    PUPNAMP       NO; WILL BE TREATED AS BLANK        81173
         MVI   PWRECCC,C'+'  REPLACE BY OVERPRINT                81173
PUPNAMP  LH    R7,PWCURLN    PRESERVE CURRENT LINE COUNT         85246
         LA    R0,PUXFLIN*256  GET EXIT TYPE FOR LOCEX           85246
         TM    PWFLAG,PWFPUN   PUNCH FILE ?                      85246
         BZ    PUTNAMS       NO                                  85246
         CLI   PWRECCC,C'+'  OVERPRINT ON A PUNCH ?              85246
         BE    PUPPUTEX      YES; IGNORE                         85246
         MVI   PWRECCC,C' '   FIX UP  ***  IGNORE V/W, ETC.      85246
         B     PUTNAMT       SKIP AROUND CC CODE                 85246
PUTNAMS  LA    R3,ASATAB+ASANUMLN-1  POINT TO VALID CHARACTERS   85246
         LA    R4,ASANUMLN-1    ONE SHORT TO DEFAULT TO BLANK    85246
         SLR   R5,R5                                             85246
PUTCL    CLC   PWRECCC,0(R3)   MATCHING CONTROL CHARACTER ?      85246
         BE    PUTCV         FOUND IT                            85246
         BCTR  R3,0          GET PREVIOUS ONE                    85246
         BCT   R4,PUTCL                                          85246
         MVI   PWRECCC,C' '  REPLACE BY BLANK                    85246
PUTCV    IC    R5,ASANUM-ASATAB(,R3)                             85246
         LH    R3,PWCURLN                                        85246
         SR    R3,R5                                             85246
         STH   R3,PWCURLN    SAVE IT FOR USER                    85246
         AH    R0,PWMAXLN    ADD LINES PER PAGE                 GP05120
         SR    R0,R3         LESS REMAINDER = CURRENT           GP05120
PUTNAMT  LR    R5,R0         SAVE FLAG                           85246
         BAL   R14,LOCEX     IS THERE A USER EXIT ?              93164
         LTR   R6,R15        MOVE EXIT ADDRESS                   85246
         BZ    PUTNAMU       NO                                  85246
         LR    R14,R5        MOVE FLAGS                          85246
         SLL   R14,16        SHIFT INTO PROPER BYTE              85246
         ICM   R14,3,DCBLRECL-IHADCB+PWDCB  SET MAX LENGTH       85246
         LA    R1,PWVCON     GET RECORD ADDRESS                  85246
         BAL   R3,TAKEX      TAKE USER EXIT                      93164
         LA    R1,PWVCON     RESTORE THE RECORD ADDRESS          85246
PUTNAMU  STH   R7,PWCURLN    RESTORE TRUE LINE COUNT             85246
         BXH   R15,R15,PUPPUTEX  SKIP OUT IF USER DOESN'T WANT IT
         TM    PWFLAG,PWFPUN   PUNCH ?                           81201
         BNZ   PUPPUT        YES; SKIP CONTROL AND PUT OUT       85246
         LA    R3,ASATAB+ASANUMLN-1  POINT TO VALID CHARACTERS   81291
         LA    R4,ASANUMLN-1    ONE SHORT TO DEFAULT TO BLANK    81291
         SLR   R5,R5
PUPCL    CLC   PWRECCC,0(R3)   MATCHING CONTROL CHARACTER ?
         BE    PUPCV         FOUND IT
         BCTR  R3,0          GET PREVIOUS ONE                    81271
         BCT   R4,PUPCL
         MVI   PWRECCC,C' '  REPLACE BY BLANK
PUPCV    IC    R5,ASANUM-ASATAB(,R3)                             81271
         LH    R3,PWCURLN
         SR    R3,R5
         BM    PUPPEJE       DO PAGE EJECT
         STH   R3,PWCURLN
         TM    DCBRECFM-IHADCB+PWDCB,DCBRECCC  ANY CONTROL CODE ?
         BNZ   PUPPUT        YES; DO ASIS
         SH    R5,=H'1'      CHECK NUMBER TO SKIP
         BZ    PUPPUT        SINGLE SPACE
         BM    PUPPUTEX      OVERPRINT - SKIP
         MVI   PLSPACCC,C' '  ENSURE SINGLE BLANK                81159
         LA    R1,PLSPACE    GET SPACER RECORD
PUPNCC   BAL   R14,PUTPUT    INSERT BLANK LINES                  93164
         BCT   R5,PUPNCC
         B     PUPPUT        PRINT TEXT
PUPPEJE  BAL   R14,HOTFOOT   PRODUCE FOOTERS                     93164
         SPACE 1                                                 90309
***********************************************************************
*                                                                     *
*        ADD SHEET EJECT REQUEST TO OUTPUT                            *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90309
         TM    PWFLG2,PWFNUST  SHEET EJECT REQUESTED ?           90309
         BZ    PUPNSHT       NO                                  90309
         NI    PWFLG2,255-PWFNUST  RESET SHEET EJECT             90309
         MVC   EDITSAVE(NEWSHELT),NEWSHEET  COPY SHEET EJECT     90309
         LA    R1,EDITSAVE   SET FOR RECORD                      90309
         BAL   R14,PUTPUT    AND WRITE IT                        93164
         ICM   R0,15,PWPAGE  GET PAGE NUMBERS                    90309
         A     R0,=X'00010001'  SET FOR NEXT PAGE                90309
         N     R0,=X'7FFE7FFE'  SET CURRENT AS EVEN PAGE         90309
*        NOTE THAT THIS IS INCORRECT FOR TWO-UP AND FOUR-UP      90309
PUPNSHT  ICM   R0,15,PWPAGE  GET PAGE AND SUBPAGE                81193
         L     R15,PWPU@     GET USER'S LIST ADDRESS             84170
         TM    PWFLAG,PWFGPAGE+PWFUPAGE  USER'S NUMBERS ?        84170
         BNO   *+8           NO                                  84170
         ICM   R0,15,PUPAGE-PRTMAP(R15)  YES; GET THEM           84170
         A     R0,=X'00010001'   INCREMENT BOTH                  81193
         STCM  R0,15,PWPAGE  SET NEW PAGE COUNTS BACK            81193
         TM    PWFLAG,PWFGPAGE   PAGE NUMBER FEED-BACK ?         81201
         BZ    PUPNPF        NO
         STCM  R0,15,PUPAGE-PRTMAP(R15)  UPDATE USER'S NUMBERS   81193
PUPNPF   LA    R0,PUXFTIT*256  SET FOR TITLE EXIT                84170
         LR    R5,R0         SAVE FLAG                           84170
         BAL   R14,LOCEX     LOCATE EXIT ADDRESS                 93164
         LTR   R6,R15        SAVE EXIT ADDRESS UNTIL LATER       84170
         BZ    PUPNEX        NONE                                84170
         LA    R1,PWTIT1     POINT TO FIRST TITLE                84170
         SLL   R5,16         PLACE EXIT TYPE FLAG INTO BYTE 0    84170
         LR    R14,R5        TRANSFER BACK                       84170
         ICM   R14,4,PWTIT#+L'PWTIT#-1  ADD NUMBER OF TITLES     84170
         ICM   R14,3,=Y(L'PWTIT1)  ADD TITLE LENGTH/SPACING      84170
         BAL   R3,TAKEX      TAKE USER EXIT                      93164
PUPNEX   LH    R3,PWTIT#     GET NUMBER OF TITLES                84170
         LA    R4,PWTIT1     POINT TO FIRST TITLE
         MVI   PWRECCC,C'1'  PAGE EJECT IF NO TITLES
         LTR   R3,R3
         BNP   PUPTTN
         LA    R5,PWTOPPG    POINT TO TITLE EDIT AREA
         BAL   R14,EDITIT    EDIT PAGE NUMBER, DATE, ETC.        93164
         MVI   PWRECCC,C' '  SKIP AFTER TITLE
PUPTTL   LR    R1,R4         SET TITLE LINE ADDRESS
         BAL   R14,PUTPUT    WRITE THE TITLE LINE                93164
         LA    R4,L'PWTIT1(,R4)  SKIP TO NEXT TITLE
         BCT   R3,PUPTTL     DO NEXT ONE
PUPTTN   LH    R1,PWMAXLN
         SH    R1,PWTIT#     SUBTRACT TITLE LINES
         SH    R1,PWFOOT#    AND FOOTERS
         SH    R1,=H'1'      ALLOW FOR NEW LINE
         STH   R1,PWCURLN
         OI    PWFLAG,PWFHEAD  SHOW HEADER PRODUCED              81201
PUPPUT   LA    R1,PWVCON     GET THE RECORD ADDRESS
         BAL   R14,PUTPUT    PUT THE RECORD                      93164
PUPPUTEX MVC   PWRECCC,PWCCSAV  RESTORE CALLER'S CONTROL BYTE    81167
         LM    R14,R9,PRTSAVE  RESTORE                           81167
         BR    R14           RETURN OK
         SPACE 2                                                 81159
*        FINAL CLEAN-OUT FOR MC FORMAT                           81159
MCFINAL  CLI   PWMCC,C'N'    EVER USED ?                         81159
         BER   R9            NO; RETURN TO CALLER                81159
         TM    PWFLAG,PWFPUN   PUNCH FILE ?                      81201
         BNZR  R9            YES; CLOSE WILL CLEAR               81159
         MVI   PWMCC,X'01'   SET FOR PRINT, NO SPACE             81159
         LA    R1,PWMCREC    GET RECORD                          81159
         BAL   R14,PUTPUTNP   GO TO SPECIAL PUT ENTRY            93164
         MVI   PWMCC,C'N'    SET 'NEVER USED' FLAG               81159
         BR    R9            RETURN TO CALLER                    81159
         SPACE 2
*        LOW-LEVEL PUT SERVICE ROUTINE
*
PUTPUT   STM   R14,R5,PUTWORK  SAVE SOME REGISTERS               81159
         MVC   DB(5),0(R1)   SAVE HEADER                         81159
         TM    DCBRECFM-IHADCB+PWDCB,DCBRECCM  MACHINE CODE ?    81159
         BZ    PUTPUT2       NO; DO NORMALLY                     81159
         TM    PWFLAG,PWFPUN  PUNCH FILE ?                       81201
         BZ    PUTPUTMC      NO                                  81159
         MVI   4(R1),X'01'   SET PUNCH/FEED/STACKER 1            81159
         B     PUTPUT2       AND CONTINUE                        81159
PUTPUTMC LR    R4,R14        SAVE RETURN ADDRESS                 81159
         LR    R5,R1         SAVE RECORD ADDRESS                 81159
         MVC   PWMCC(1),4(R1)  COPY CURRENT CONTROL CHARACTER    81159
         LA    R14,MCCTAB    GET CONVERSION TABLE                81159
         LA    R15,ASATAB-MCCTAB-1  GET NUMBER OF ENTRIES        81159
PUTPUTML CLC   PWMCC,ASATAB-MCCTAB(R14)  MATCH ?                 81159
         BE    PUTPUTMF      YES                                 81159
         LA    R14,1(,R14)                                       81159
         BCT   R15,PUTPUTML  TRY NEXT ONE                        81159
PUTPUTMF MVC   PWMCC,0(R14)  REPLACE BY MACHINE CODE             81159
         LA    R1,PWMCREC    POINT TO OLD RECORD                 81159
         LA    R15,6                                             81159
         CH    R15,0(,R1)    MINIMUM RECORD ?                    81159
         BL    PUTPUTMN      NO                                  81159
         BE    *+8           ONE DATA BYTE                       81159
         OI    PWMCC,2       CONVERT TO 'NOPRINT' CCW CODE       81159
         STH   R15,0(,R1)    MAKE CERTAIN                        81159
PUTPUTMN BAL   R14,PUTPUT1   PROCESS THIS ONE                    93164
         MVC   PWMCREC(MAXWIDTH+5),0(R5)  SAVE CURRENT TEXT      81159
         LR    R14,R4        RESTORE RETURN                      81159
         LR    R1,R5         RESTORE RECORD ADDRESS              81159
         LM    R4,R5,PUTWORK+24  RESTORE OTHER REGISTERS         81159
         BR    R14           RETURN TO CALLER                    81159
PUTPUTNP MVC   DB(5),0(R1)   SAVE USER'S DATA                    81159
PUTPUT1  STM   R14,R3,PUTWORK                                    81159
PUTPUT2  LH    R0,0(,R1)     GET RECORD LENGTH (+CC+RDW)
         LA    R15,6         SET MINIMUM RECORD LENGTH           81159
         CR    R0,R15        AT LEAST MINIMUM ?                  81159
         BNL   PUTPUT2L      YES                                 81159
         LR    R0,R15        SET NEW MINIMUM                     81159
         STH   R0,0(,R1)     UPDATE RECORD                       81159
PUTPUT2L TM    DCBRECFM-IHADCB+PWDCB,DCBRECCC  CONTROL ?
         BNZ   PUTPUTCC      YES
         CLI   4(R1),C'+'    OVERPRINT ?
         BER   R14           YES; JUST QUIT
         LA    R1,1(,R1)     SHIFT OVER
         BCTR  R0,0          FIX LENGTH
         SLL   R0,16
         STCM  R0,15,0(R1)   SET NEW ADDRESS
         SRL   R0,16         FIX UP
PUTPUTCC TM    DCBRECFM-IHADCB+PWDCB,DCBRECF  FIXED/U ?
         BNZ   PUTPUTFX      YES; SPECIAL
         CH    R0,DCBLRECL-IHADCB+PWDCB  WILL IT FIT ?
         BNH   PUTPUTPT      YES
         MVC   0(2,R1),DCBLRECL-IHADCB+PWDCB  USE MAX
         B     PUTPUTPT
PUTPUTFX LA    R1,4(,R1)     POINT TO TEXT
         SH    R0,=H'4'      ADJUST LENGTH
PUTPUTPT LR    R3,R0         COPY LENGTH
         LR    R0,R1         COPY THE RECORD ADDRESS
         LR    R2,R1         COPY THE RECORD ADDRESS FOR LOCATE MODE
         PUT   PWDCB         PUT IT OUT
         TM    DCBMACF2-IHADCB+PWDCB,DCBMRLCP  LOCATE MODE ?
         BZ    PUTPUTEX      NO; JUST EXIT
         ICM   R3,8,BLANKS   MAKE PADDING CHARACTER
         LR    R0,R1         COPY RECORD ADDRESS
         LH    R1,DCBLRECL-IHADCB+PWDCB  GET LENGTH
         MVCL  R0,R2         MOVE/PAD RECORD
PUTPUTEX LM    R14,R3,PUTWORK  RESTORE                           81159
         MVC   0(5,R1),DB    REPLACE RDW/CC
         BR    R14           RETURN TO CALLER
         SPACE 1                                                 81163
PUTWTO   SRDL  R0,32         ZERO R0; V-CON ADDRESS IN R1        81163
         WTO   MF=(E,(1))    WRITE TO CONSOLE                    81163
         BR    R14           RETURN TO CALLER                    81163
         SPACE 1                                                 83331
PUTTPUT  LR    R1,R0         COPY V-FORMAT RECORD ADDRESS        83331
         SLR   R0,R0                                             83331
         ICM   R0,3,0(R1)    LOAD LENGTH                         83331
         LA    R1,4(,R1)     POINT TO TEXT                       83331
         TPUT  (1),(0),R     NO OPTIONS                          83331
         BR    R14           RETURN TO PUT                       83331
         SPACE 2
*        PRODUCE FOOTERS
*
HOTFOOT  TM    PWFLAG,PWFHEAD  TITLE ON THIS PAGE ?              81201
         BZR   R14           NO; EXIT
         NI    PWFLAG,255-PWFHEAD                                81201
         TM    PWFLAG,PWFPUN   PUNCH FILE ?                      81201
         BNZR  R14           YES; EXIT
         STM   R14,R6,FOOTSAVE  SAVE REGISTERS                   81193
         ICM   R0,3,PWFOOT#  ANY FOOTERS ?                       84170
         BZ    HOTNFEX       NO                                  84170
         LA    R0,PUXFOOT*256  SET FOR FOOTER EXIT               84170
         LR    R5,R0         SAVE FLAG                           84170
         BAL   R14,LOCEX     LOCATE EXIT ADDRESS                 93164
         LTR   R6,R15        SAVE EXIT ADDRESS UNTIL LATER       84170
         BZ    HOTNFEX       NONE                                84170
         LH    R1,PWTIT#     GET NUMBER OF TITLES                84170
         MH    R1,=Y(L'PWTIT1)  MAKE INTO OFFSET                 84170
         LA    R1,PWTIT1(R1)  POINT TO FIRST FOOTER              84170
         SLL   R5,16         PLACE EXIT TYPE FLAG INTO BYTE 0    84170
         LR    R14,R5        TRANSFER BACK                       84170
         ICM   R14,4,PWFOOT#+L'PWFOOT#-1  ADD NUMBER OF FOOTERS  84170
         ICM   R14,3,=Y(L'PWTIT1)  ADD TITLE LENGTH/SPACING      84170
         BAL   R3,TAKEX      TAKE USER EXIT                      93164
HOTNFEX  TM    DCBRECFM-IHADCB+PWDCB,DCBRECCC  CONTROL ?         84170
         BZ    HOTFOOTL      NO; DO THE HARD WAY
         ICM   R0,3,PWFOOT#  ANY FOOTERS PRESENT ?
         BZ    HOTFOOTX      NO; QUIT
HOTFOOTL LA    R6,HOTFOOTP   RETURN FROM IN-LINE SUBROUTINE      81193
         LH    R3,PWCURLN    GET CURRENT LINE RESIDUAL           81193
         LTR   R3,R3         ANY SPACERS TO DO ?
         BNPR  R6            NO; JUST PRODUCE FOOTERS            81193
         SPACE 1                                                 81193
*        SUBROUTINE TO SPACE N (IN R3) LINES.  NO REGS SAVED     81193
*                                                                81193
NSPACE   MVC   PLSPACE(4),MINVCON  SET LENGTH FOR CC ONLY        81193
         LA    R4,3
         ICM   R5,7,ASATAB   SINGLE; DOUBLE; TRIPLE SPACE        81271
         TM    DCBRECFM-IHADCB+PWDCB,DCBRECCC  ANY CC ?
         BNZ   HTFPCL        YES
         SRL   R5,16         ELSE SINGLE SPACE
         LA    R4,1
HTFPCL   SLR   R2,R2
         DR    R2,R4
         LTR   R3,R3
         BZ    HTFPCL2
HTFPCL1  LA    R1,PLSPACE    SET THE RECORD ADDRESS
         STC   R5,PLSPACCC   SET CARRIAGE CONTROL FOR THIS GROUP
         BAL   R14,PUTPUT    PUT IT OUT                          93164
         BCT   R3,HTFPCL1
HTFPCL2  LTR   R3,R2         SEE IF ANY LEFT TO DO
         BNPR  R6            NO                                  81193
         SRL   R5,8          SET FOR NEXT CONTROL CHARACTER
         BCT   R4,HTFPCL
         BR    R6            RETURN TO CALLER                    81193
         SPACE 1                                                 81193
HOTFOOTP LH    R3,PWFOOT#    GET NUMBER OF FOOTERS
         LTR   R3,R3         ANY ?
         BNP   HOTFOOTX      NO; QUIT
         LH    R4,PWTIT#     GET NUMBER OF TITLES
         MH    R4,=Y(L'PWTIT1)  MAKE INTO OFFSET
         LA    R4,PWTIT1(R4)  POINT TO FIRST FOOTER
         LA    R5,PWBOTPG    POINT TO FOOTER EDIT AREA
         BAL   R14,EDITIT    EDIT PAGE NUMBER, DATE, ETC.        93164
HTFTTL   LR    R1,R4         SET THE FOOTER ADDRESS
         BAL   R14,PUTPUT    WRITE THE FOOTER LINE               93164
         LA    R4,L'PWTIT1(,R4)  SKIP TO NEXT TITLE
         BCT   R3,HTFTTL     DO NEXT ONE
HOTFOOTX MVI   PWCURLN,X'FF'  FORCE EJECT
         MVI   PLSPACCC,C' '   RESET SPACE CONTROL
         LM    R14,R6,FOOTSAVE                                   81193
         BR    R14           RETURN TO CALLER
         SPACE 2
*        TITLE/FOOTER STORE ROUTINE
*
PTITLE   NI    PWFLAG,255-PWFPRINT  TURN STACKED REQUEST FLAG OFF
         TM    PWFLAG,PWFPUN   PUNCH ?                           81201
         BNZ   RECURSE       YES; EXIT
         LA    R0,L'PWREC
         LA    R1,PWREC      GET RECORD LENGTH/ADDRESS
         #CNVRT DBKR,BP=PTITVL   DEBLANK                        GP03316
         SLR   R0,R0         ELSE SET MINIMUM
PTITVL   AH    R0,=H'5'      ADD RDW AND CC LENGTH
         STH   R0,PWVCON     SET NEW LENGTH                     GP05032
         SLR   R2,R2
         LH    R3,PWTIT#     GET TITLE COUNT
         SLR   R4,R4
         IC    R2,CALLTIT    GET TITLE REQUEST
         SRA   R2,4          ISOLATE FROM FOOTER NUMBER
         BZ    PTIFOOT       FOOTER ONLY
         CR    R2,R3         LARGER ?
         BNH   PTITOFF       NO, USE
         OI    RETCC,4       SET MINOR ERROR
         B     PTIFOOT
PTITOFF  MH    R2,=Y(L'PWTIT1)  MAKE OFFSET
         LA    R2,PWTIT1-L'PWTIT1(R2)  POINT TO NEW TITLE
         MVC   0(L'PWTIT1,R2),PWVCON  MOVE TITLE
         CLI   4(R2),C'-'    DELAYED EJECT REQUEST ?             81208
         MVI   4(R2),C' '    FORCE SINGLE SPACE
         MVI   PWTIT1+4,C'1'  EXCEPT IN FIRST TITLE
         BNE   PTITNOW       NOT DELAY; FORCE EJECT NOW          81208
         LA    R0,10                                             81208
         CH    R0,PWCURLN    SEE IF 10 LINES ON PAGE             81208
         BNH   PTITDLY       YES; DELAY PAGE EJECT               81208
PTITNOW  BAL   R14,HOTFOOT   FORCE FOOTER FOR PRIOR PAGE         93164
         XC    PWSPAGE,PWSPAGE   CLEAR SUB-PAGE COUNT            81264
PTITDLY  BAL   R14,EDITCLR   CLEAR EDIT OPTIONS FOR THIS LINE    93164
         LA    R5,PWTOPPG    SET PAGE INFO
         BAL   R14,EDITFND   SCAN FOR INSERTIONS                 93164
         ICM   R14,15,PWTRAN  TRANSLATION REQUIRED ?            GP99028
         BZ    PTIFOOT       NO                                  82204
         TR    5(L'PWTIT1-5,R2),0(R14)  TRANSLATE                82204
PTIFOOT  IC    R4,CALLTIT    GET FOOTER REQUEST
         LA    R15,15
         NR    R4,R15        MASK FOOTER NUMBER
         BZ    RECURSE       EXIT IF NONE
         LH    R2,PWFOOT#    GET NUMBER OF FOOTERS
         CR    R4,R2         VALID ?
         BH    SET4          NO; QUIT
         LA    R2,0(R2,R3)   GET TITLES+FOOTERS
         SR    R2,R4         GET INDEX
         MH    R2,=Y(L'PWTIT1)  GET OFFSET
         LA    R2,PWTIT1(R2)  POINT TO IT
         MVC   0(L'PWTIT1,R2),PWVCON  MOVE FOOTER
         MVI   4(R2),C' '    FORCE SINGLE SPACE
         BAL   R14,EDITCLR   CLEAR EDIT INSERTIONS               93164
         LA    R5,PWBOTPG    SET FOR FOOTER INSERTIONS
         BAL   R14,EDITFND   SCAN FOR INSERTIONS                 93164
         ICM   R14,15,PWTRAN  TRANSLATE TABLE ?                 GP99028
         BZ    RECURSE       NO                                  82204
         TR    5(L'PWTIT1-5,R2),0(R14)  TRANSLATE                82204
         B     RECURSE       GO FOR MORE                         82204
         SPACE 2
EDITFND  STM   R14,R12,SAVE14     SAVE REGISTERS
         XC    DBSAVE,DBSAVE      SET ALL VALID                 GP12295
         MVI   DBSAVE+C'#',C'#'   EXCEPT OCTOTHORPE             GP12295
         LA    R6,4(,R2)     POINT TO FIRST POSSIBLE POSITION -1  12295
EDFNBUMP LR    R7,R2         SAVE START                         GP12295
         LA    R14,1         MAKE INCREMENT
         LA    R6,1(,R6)     SKIP PRIOR #                       GP12295
         LA    R15,L'PWTIT1-3(,R7)      STOP ADDRESS            GP12295
         SR    R15,R6        LENGTH TO SCAN                     GP12295
         BM    EDFNEXIT                                         GP12295
         EX    R15,EXFIND#   LOOK FOR ANOTHER POUND             GP12295
         BNZ   EDFNTSTP        FOUND ONE; TEST FURTHER          GP12295
EDFNEXIT LM    R14,R12,SAVE14    RESTORE
         BR    R14           RETURN TO CALLER
EXFIND#  TRT   0(*-*,R6),DBSAVE      SCAN FOR #                 GP12295
         SPACE 1
EDFNTSTP LA    R6,0(,R1)     USE OLD REGISTER                   GP12295
         LR    R2,R7         RESTORE LINE START                 GP12295
         CLC   CANTITPG+1(2),1(R6)  PAGE NUMBER REQUEST ?        81271
         BNE   EDFNTSTS      NO; CHECK FOR SUB-PAGE
         LA    R1,PWTOPPG-PWTOPPG(,R5)  SET ADDRESS              81271
         B     EDFNPLEN      GO TO PAGE/SUB-PAGE COMMON CODE     81271
         SPACE 1
EDFNTSTS CLC   =C'S#',1(R6)  SUB-PAGE NUMBER ?
         BNE   EDFNTSTD      NO; CHECK FOR DATE
         LA    R1,PWTOPPS-PWTOPPG(,R5)  SET ADDRESS              81271
EDFNPLEN ST    R6,0(,R1)     SAVE ADDRESS OF INSERTION FIELD     81271
         LA    R0,3          SET SKIP INCREMENT                  81271
         AR    R6,R0         POSITION TO NEXT BYTE               81271
EDFNSLOP CLI   0(R6),C'#'    MORE ?
         BNE   EDFNSLEN      NO
         AR    R0,R14        INCREASE LENGTH BY 1                81271
         BXLE  R6,R14,EDFNSLOP
EDFNSLEN STC   R0,0(,R1)     SET LENGTH                          81271
         B     EDFNBUMP      TRY FOR MORE
         SPACE 1
EDFNTSTD CLC   CANTITDT+1(L'CANTITDT-1),1(R6)  DATE INSERTION ?  81271
         BNE   EDFNTSTT      NO; CHECK FOR TIME
         LA    R1,PWTOPDT-PWTOPPG(,R5)  SET ADDRESS              81271
         B     EDFNTSDT      GO TO COMMON                        81271
         SPACE 1
EDFNTSTT CLC   CANTITTM+1(L'CANTITTM-1),1(R6)  TIME INSERTION ?  81271
         BNE   EDFNBUMP      NO; TRY NEXT BYTE
         LA    R1,PWTOPTM-PWTOPPG(,R5)  SET ADDRESS              81271
EDFNTSDT ST    R6,0(,R1)     SAVE ADDRESS                        81271
         LA    R0,8          SET LENGTH                          81271
         STC   R0,0(,R1)     ADD LENGTH                          81271
         AR    R6,R0         AND SKIP                            81271
         B     EDFNBUMP      TRY MORE
         SPACE 2
EDITCLR  CLI   PWRECCC,C'#'  AUTOMATIC DATE/TIME/PAGE ?          81271
         BNE   EDITCLR#      NO                                  81271
         LH    R15,PWWIDTH   GET USER'S WIDTH                    81271
         LA    R0,5(,R15)    GET WIDTH+CC+VCON LENGTH            81271
         LA    R6,CANTITLN   GET CANNED TITLE'S LENGTH           81271
         SR    R15,R6        GET MOVE INDENT                     81271
         BNP   EDITCLR#      TOO LARGE; TOO BAD                  81271
         STH   R0,0(,R2)     MAKE NEW LENGTH                     81271
         AR    R15,R2        GET LINE OFFSET                     81271
         MVC   5(CANTITLN,R15),CANTITLE  MOVE CANNED TITLE       81271
EDITCLR# LA    R15,L'PWTIT1(,R2)  GET END OF LINE                81271
         LA    R1,PWTOPPG    POINT TO EDIT WORK AREA
         LA    R0,(PWBOTPG-PWTOPPG)/2  NUMBER OF ENTRIES
         SLR   R6,R6
EDITCLRL CLM   R2,7,1(R1)    IN THIS LINE ?
         BH    EDITCLRB      NO
         CLM   R15,7,1(R1)   REALLY IN LINE ?
         BNH   EDITCLRB      NO
         ST    R6,0(,R1)     CLEAR ENTRY
EDITCLRB LA    R1,4(,R1)     TRY NEXT ONE
         BCT   R0,EDITCLRL   IF ANY
         BR    R14           ELSE RETURN
         SPACE 2
EDITIT   STM   R14,R4,EDITSAVE  SAVE CALLER'S REGISTERS
         SR    R2,R2         CLEAR HIGH                         GP99124
         ICM   R2,7,PWTOPPG+1-PWTOPPG(R5)  PAGE # EDIT ?
         BZ    EDITITPS      NO; TRY SUB-PAGE
         SLR   R3,R3
         IC    R3,PWTOPPG-PWTOPPG(,R5)  GET LENGTH
         LA    R1,PWPAGE     POINT TO PAGE COUNTER
         BAL   R4,EDITMVCL   MOVE TO USER'S AREA                 93164
EDITITPS ICM   R2,7,PWTOPPS+1-PWTOPPG(R5)  SUB-PAGE # EDIT ?
         BZ    EDITITDT      NO; TRY DATE
         SLR   R3,R3
         IC    R3,PWTOPPS-PWTOPPG(,R5)  GET LENGTH
         LA    R1,PWSPAGE    POINT TO PAGE COUNTER
         BAL   R4,EDITMVCL   MOVE TO USER'S AREA                 93164
EDITITDT ICM   R2,7,PWTOPDT+1-PWTOPPG(R5)  DATE ?
         BZ    EDITITTM      NO; CHECK TIME
         #CNVRT DATE         GET DATE                           GP03316
         MVC   0(8,R2),0(R1)   MOVE IT
EDITITTM ICM   R2,7,PWTOPTM+1-PWTOPPG(R5)  TIME ?
         BZ    EDITITEX      NO; QUIT
         #CNVRT TIME         GET TIME                           GP03316
         MVC   0(8,R2),0(R1)   MOVE IT
EDITITEX LM    R14,R4,EDITSAVE   RESTORE REGISTERS
         BR    R14           RETURN
         SPACE 1
EDITMVCL BCTR  R3,0          MAKE EXECUTE LENGTH                GP04070
         EX    R3,EXOUTBLK   CLEAR OUTPUT AREA                  GP04070
         LA    R3,1(,R3)     RESTORE LENGTH                     GP04070
         #FMT  (0(R1),2),(R3),SIGN=N,TRUNC=Y,TYPE=AINT          GP04070
         AR    R2,R3         END OF OUTPUT                      GP04070
         SR    R2,R1         START OF MOVE                      GP04070
         MVCL  R2,R0         MOVE RESULT                        GP04070
         BR    R4            RETURN TO CALLER
EXOUTBLK MVC   0(0,R2),BLANKS  CLEAR PAGE NUMBER (1-10 BYTES)   GP04070
         SPACE 2
CLEAROUT TM    PWFLAG,PWFPRINT  STACKED PRINT REQUEST ?          81201
         BZ    CHECKDCB      NO
         BAL   R14,PUTTRUNC  PRINT PREVIOUS RECORD               93164
         NI    PWFLAG,255-PWFPRINT  TURN IT OFF                  81201
         TM    PWFLAG,PWFPUN   PUNCH FILE ?                      81201
         BNZ   CHECKDCB      YES; SKIP
         CLI   PWULOP+4,C'+'   OVERPRINT REQUEST ?
         BNE   CHECKDCB      NO
         MVC   PWRECCC(L'PWREC+1),PWULOP+4  COPY OVER            81193
         BAL   R14,PUTTRUNC  PRINT IT                            93164
         MVI   PWULOP+4,C' '   RESET
         MVC   PWULOP+5(L'PWREC),PWULOP+4  CLEAR IT
CHECKDCB TM    PWDCB+DCBOFLGS-IHADCB,DCBOFOPN  DCB OPEN ?
         BZR   R9            NO; RETURN
         B     4(,R9)        RETURN OK
         SPACE 2                                                 82102
*        RESERVE SPACE ON A PAGE
*
PROOM    BAL   R6,PSPAROM    GO TO ROOM/SPACE COMMON CODE        93164
         BNM   RECURSE
         B     PSPACE3       GO TO DO FOOTERS                    81271
         SPACE 2
*        GENERATE BLANK LINES
*
PSPACE   LA    R6,PSPACE2    POINT TO RETURN ADDRESS             81271
         SPACE 1                                                 81271
PSPAROM  BAL   R9,CLEAROUT                                       93164
           B   RECURSE       SKIP IF NO DCB
         BAL   R14,SHEETEST  TEST FOR SHEET EJECT                93164
         TM    PWFLAG,PWFPUN   PUNCH ?                           81201
         BNZ   RECURSE       YES; EXIT
         SLR   R1,R1         CLEAR FOR IC                        90310
         ICM   R1,15,CALLAD  GET USER'S SKIP COUNT              GP99029
         BNP   RECURSE
         LH    R2,PWCURLN
         SR    R2,R1
         BR    R6            RETURN TO CALLER                    81271
         SPACE 1                                                 81271
PSPACE2  BP    PSPPCS        NO FOOTERS YET                      81271
PSPACE3  BAL   R14,HOTFOOT   PRODUCE ANY FOOTERS                 93164
         B     RECURSE
         SPACE
PSPPCS   STH   R2,PWCURLN    SET LINE COUNT AFTER SPACING        81193
         LR    R3,R1         LOAD LINES TO SPACE                 81193
         BAL   R6,NSPACE     SPACE (R3) LINES                    93164
         B     RECURSE
         SPACE 2                                                 84170
*        LOCATE USER EXIT ADDRESSES                              84170
*        R0.2 FLAG MASK .3 NUMBER                                84170
*        RETURN VIA R14, EXIT IN R15, CC SET FROM R15            84170
*                                                                84170
LOCEX    TM    PWFLAG,PWFPXLST  USER SUPPLIED EXIT LIST ?        84170
         BZ    LOCEXNON      NO                                  84170
         ICM   R15,15,PWPU@  GET THE WORK ADDRESS                84170
         BZ    LOCEXNON      HUH ?                               84170
         LA    R15,PUPAGE-PRTMAP(,R15)    SPACE TO FIRST EXTENSION
         TM    PWFLAG,PWFGPAGE  PAGE EXTENSION PRESENT ?         84170
         BZ    *+8           NO                                  84170
         LA    R15,PUEXLST-PUPAGE(,R15)  SKIP PAGES              84170
         ICM   R15,15,0(R15) ANY ADDRESS ?                       84170
         BZ    LOCEXNON      NO                                  84170
LOCEXLP  CLM   R0,8,1(R15)   LINE NUMBER = 0?                   GP05120
         BE    LOCEXAL       YES; ACCEPT AS MATCH               GP05120
         CLM   R0,1,1(R15)   LINE NUMBER MATCH ?                 84170
         BNE   LOCEXUP       NO; TRY NEXT ENTRY                  84170
LOCEXAL  STCM  R0,2,USEXPARM STASH MASK BYTE                     84176
         NC    USEXPARM(1),0(R15)  CHECK FOR MATCH               84176
         TM    USEXPARM,255-PUXFEND  BIT OTHER THAN EOL ?        84176
         BZ    LOCEXUP       NO; GET NEXT ENTRY                  84170
         CLC   =F'0',2(R15)  EXIT ADDRESS PRESENT ?              84170
         BE    LOCEXUP       NO                                  84170
         ICM   R15,15,2(R15) LOAD EXIT ADDRESS                   84170
         BR    R14           RETURN TO CALLER WITH CC SET        84170
LOCEXUP  TM    0(R15),PUXFEND  END OF LIST ?                     84170
         LA    R15,6(,R15)   SPACE TO NEXT ONE, ANYWAY           84170
         BZ    LOCEXLP       NO; TRY NEXT ENTRY                  84170
LOCEXNON SR    R15,R15       CLEAR R15 AND SET CC=0              84170
         BR    R14                                               84170
         SPACE 1                                                 84170
*        INVOKE A USER EXIT                                      84170
*                                                                84170
*        R14: PRESET TO FLAGS/LINE/COUNT/SIZE, FUNCTION DEPENDENT
*        R1:  RECORD ADDRESS TO BE PASSED TO THE USER            84170
*        R6:  EXIT ADDRESS (FROM PRIOR LOCEX INVOKATION)         84170
*        R3:  RETURN ADDRESS (FROM BAL)                          84170
*                                                                84170
*        USER EXIT IS ENTERED VIA BALR R15,R14, WITH R1 POINTING TO:
*        LIST: FLAGS, ETC. (R14)                                 84170
*         + 4  PRINTER WORK AREA                                 84170
*         + 8  ORIGINAL SAVE AREA FROM CURRENT PRT FUNCTION      84170
*         +12  RECORD TO BE PROCESSED (INCL. RDW, CC)            84170
*         +16    RESERVED (0)                                    84170
         SPACE 1                                                 84170
TAKEX    LR    R15,R8        PRINTER WORK AREA                   84170
         L     R0,SAVE13     USER SAVE AREA ADDRESS              84170
         SLR   R2,R2         RESERVED                            84170
         STM   R14,R2,USEXPARM   BUILD USER PARAMETER LIST       84176
         XC    USEXSAVE(16*4),USEXSAVE  CLEAR FOR DEBUGGING      84176
         ST    R13,USEXSAVE+24   BACKLINK FOR USER'S SAVE AREA   84176
         STM   R2,R12,SAVE2  SAVE OUR REGISTERS                  84170
         OI    USEXFLAG,USXFACT  SHOW USER EXIT BEING TAKEN      84176
         LA    R1,USEXPARM     USER PARAMETER LIST               84176
         AIF   (&MVSXA).XASYNC                                  GP04234
         SYNCH (R6),RESTORE=YES                                 GP04234
         AGO   .COMSYNC                                         GP04234
.XASYNC  SYNCH (R6),RESTORE=YES,AMODE=DEFINED                   GP99029
.COMSYNC NI    USEXFLAG,255-USXFACT  RESET USER EXIT             84176
         BR    R3            RETURN TO CALLER                    84170
         SPACE 2                                                 90309
***********************************************************************
*                                                                     *
*        SUBROUTINE TO HANDLE SHEET EJECT REQUESTS                    *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90309
SHEETEST TM    PWFLG2,PWF4050  XEROX MODE ?                      90309
         BZR   R14           NO                                  90309
         TM    CALLFLG,CLSHEET  NEW SHEET ?                      90309
         BZR   R14           NO                                  90309
         OI    PWFLG2,PWFNUST  REQUEST IT                        90309
         BR    R14                                               90309
         SPACE 2
         LTORG ,
         SPACE 1
*        DATA
*
PATDCB   DCB   DDNAME=ANY,DSORG=PS,MACRF=PM,EROPT=ACC,EXLST=123
PATEND   EQU   *
         SPACE 1
MODSD095 DC    CL8'IEFSD095'   BIG LETTER ROUTINE                81159
MINVCON  DC    X'00050000'   +1ST BLANK - MINIMUM V RECORD    1/2
BLANKS   DC    CL10' '                                     2/2   81193
NEWSHEET DC    Y(11,0),C' ',6X'21'  LOCAL XEROX SHEET FEED ORDER 90309
NEWSHELT EQU   *-NEWSHEET    LENGTH OF NEW SHEET ORDER           90309
         SPACE 1                                                 81159
MCCTAB   DC    X'0911190189C9E1E309' SS,DS,TS,NS,CH1,CH9,CH12,SS 81159
ASATAB   DC    C' 0-+19C '    SS,DS,TS,NS,CH1,CH9,CH12,SS        81159
ASANUM   DC    AL1(1,2,3,0,255)  INCREMENT FOR ASATAB ' 0-+1'    81271
ASANUMLN EQU   *-ASANUM      NUMBER OF ENTRIES                   81294
         SPACE 1
DATETABL DC    H'0,31,59,90,120,151,181,212,243,273,304,334,365'
DATEPAT  DC    X'F0212020612020612020'
DATEJUL  DC    X'F021204B202020'       YY.DDD FORM               81193
HEXTAB   DC    C'0123456789ABCDEF'  HEX CONVERSION
DBEDMASK DC    X'40202020202020202020202020202120'
DBEDMONY DC    X'40206B2020206B2020206B2020206B2021204B2020'
CANTITLE DC    C'RUN ON '                                        82031
CANTITDT DC    C'##/##/##',C'  AT '                              81294
CANTITTM DC    C'##:##:##',C'  PAGE '                            82031
CANTITPG DC    C'#P##'                                           81271
CANTITLN EQU   *-CANTITLE                                        81271
         EJECT
*        OPEN PROCESSING: R1 POINTS TO WORK AREA CREATED BY EITHER
*          PRTWORK OR PUNWORK MACROS.  IF PRIMARY DDNAME BEGINS WITH
*          AN ASTERISK, CONSOLE OUTPUT WILL BE PERFORMED.        81166
*          IF PRIMARY DD BEGINS WITH A PERCENT SIGN, SECOND WORD 83275
*          OF NAME IS IN-STORAGE PUT ADDRESS (NO I/O DONE).      83275
*          THIRD AND FOURTH WORDS (ALT DD) ARE UCS AND FCB.      83275
*          SECOND BYTE OF DDNAM FIELD IS LINES/PAGE.             83275
*          REGISTER 0 MUST SPECIFY ONE DEVICE NUMBER ONLY.       81166
*        TITLE/FOOTER REQUESTS ARE IGNORED FOR PUNCH AND CONSOLE 81166
*        RETURN CODES :                                          81166
*        R15=0  REQUEST COMPLETED                                81166
*        R15=4  REQUEST COMPLETED, BUT ALTERNATE DDNAME USED     81166
*        R15=8  DD DUMMY; ALL CALLS WILL BE IGNORED.             81166
*        R15=12 NO USABLE DD FOUND; CALLS WILL RETURN WITH ERRORS.
*                                                                81166
*        WITH R15=8, A WTO INDICATED THE MISSING DDNAME(S) WILL BE
*          ISSUED UNLESS THE PRTOPEN REQUESTED 'NOWTO'           81166
*        IF PRTOPEN SPECIFIED 'ABEND', RETURN CODE 12 WILL NOT BE
*          ISSUED, INSTEAD AN ABEND 1403 IS TAKEN.               81166
*        IF PRTOPEN SPECIFIED ABEND AND NOT 'DUMMY', RETURN CODE 8
*          WILL NOT BE ISSUED, BUT AN ABEND IS TAKEN.            81166
*
         PUSH  USING                                            GP99029
POPEN    LA    R11,2048(,R12)                                   GP99029
         LA    R11,2048(,R11)  MAKE LOCAL BASE                  GP99029
         USING @PRINTER,R12,R11  ADD LOCAL BASE                 GP99029
         LTR   R8,R8         WAS THIS OPENED ALREADY ?           82102
         BNZ   EXIT12        YES; SET ERROR
         CLI   CALLDEV,0     MORE THAN ONE DEVICE SPECIFIED ?
         BNE   EXIT12        YES; ALSO ERROR
         ICM   R7,15,CALLAD    ANY WORK AREA ?                  GP99029
         BZ    EXIT12        NO; ERROR
         NI    CALLFLG,CLABE+CLABEDU+CLNWTO  REMOVE UNWANTED BITS
         USING PRTMAP,R7     DECLARE MAPPING OF USER AREA
         MVC   DB,PUDDNAM    TEMPORARY SAVE AREA FOR USER'S DD NAME
         CLI   PUDDNAM,C'%'  SPECIAL REQUEST ?                   83331
         BE    POPTFAKE      YES; SKIP ALTERNATE TEST            83331
         CLI   PUDDNAM,C'*'  DITTO ?                             83331
         BE    POPTFAKE      YES                                 83331
         CLI   PUDDALT,C'%'  SPECIAL ALTERNATE ?                 83331
         BE    POPPFAKE      YES; TEST PRIMARY FIRST             83331
         CLI   PUDDALT,C'*'  SPECIAL ?                           83331
         BNE   POPTFAKE      NO                                  83331
POPPFAKE DEVTYPE PUDDNAM,DB  DD CARD AVAILABLE ?                 83331
         BXH   R15,R15,POPPFAKA  NO; USE ALTERNATE               83331
         CLI   DB+2,0        DD DUMMY ?                          83331
         BE    POPPFAKA      YES; USE ALTERNATE                  83331
         MVC   DB,PUDDNAM    RESTORE PRIMARY NAME                83331
         B     POPTFAKE                                          83331
POPPFAKA MVC   DB,PUDDALT    SET ALTERNATE NAME                  83331
POPTFAKE LA    R0,7          SET MAX SUPPORTED TITLE/FOOTER COUNT
         SLR   R14,R14
         SLR   R15,R15
         CLI   DB,C'*'       SPECIAL NAME ?                      83331
         BE    POPPU1        YES; IGNORE TITLE/FOOTER REQUESTS   81163
         TM    PUPRFG,PUPUNCH  PUNCH OPTION ?
         BNZ   POPPU1        YES; SKIP TITLES/FOOTERS
         IC    R14,PUTIT#    GET NUMBER OF HEADERS
         CR    R14,R0          LEGAL ?
         BNH   *+6
         LR    R14,R0          NO; USE MAX
         IC    R15,PUFOOT#   AND FOOTERS
         CR    R15,R0          LEGAL ?
         BNH   *+6
         LR    R15,R0          NO; USE MAX
         LA    R15,1(R14,R15)   ADD ONE FOR UNDERLINE AREA       81138
         MH    R15,=Y(L'PWTIT1)  TIMES LENGTH
POPPU1   L     R3,=A(PWSIZE)  GET LENGTH OF WORK AREA            81201
         ALR   R3,R15        GET GETMAIN SIZE REQUIRED
         BAL   R9,GETFSA     GET STORAGE FOR WORK AREA           93164
         ST    R7,PWPU@      SAVE USER'S LIST ADDRESS
         MVC   PWDCB(PATEND-PATDCB),PATDCB  INITIALIZE DCB
         MVC   DCBDDNAM-IHADCB+PWDCB(8),DB  SET THE DDNAME       83331
         MVC   PWDDNAM,DB    DITTO                               83331
         MVC   PWUCS,=CL4'&DEFUCS '    SET DEFAULT UCS          GP08145
         MVC   PWFCB,=CL4'&DEFFCB '      AND FCB                GP08145
         LA    R1,PWDCB      GET DCB ADDRESS
         ST    R1,PWDCB@     STASH THE ADDRESS
         LTCB  R3            GET MY TCB BACK                     86288
         ST    R3,PWTCB      SAVE IT                             83275
         MVI   PWDCB@,X'BF'  MAKE OPEN/OUTPUT/LEAVE LIST FORM    81159
         LA    R1,PWEXLIST
         STCM  R1,7,DCBEXLST+1-IHADCB+PWDCB  SET EXITLIST ADDR
         LOAD  EPLOC=@DCBEXIT   LOAD DCB-EXIT PROCESSOR
         LA    R1,PWVCON     USE THE TITLE FOR JFCB
         LA    R2,O2LVL      GET DCBEXIT PARAMETER LIST
         STM   R0,R2,PWEXLIST
         MVI   PWEXLIST,X'05'  SET AS DCB EXIT
         MVI   PWEXLIST+4,X'07'  SET JFCB ADDRESS
         MVI   PWEXLIST+8,X'80'  SET EXIT PARM ENTRY
         MVC   OXDCBXID,@DCBEXID   SET DCBEXIT LIST CONSTANT     81271
         MVC   OXFLAG1(3),=AL1(OX1FULLT+OX1TRUNC,OX2CCADJ,OX3BLKTB)
         SLR   R1,R1         FOR ICM
         ICM   R1,1,PUFILL   USER SPECIFIED FILL BYTE ?          81186
         BNZ   *+8           YES                                 81186
         LA    R1,DEFUNPRT   ELSE USE ASSEMBLED DEFAULT          81186
         STC   R1,PWFILL     SAVE IT                             81186
         MVC   PWFLAG,PUPRFG  MOVE USER'S OPEN LIST OPTIONS      81201
         TM    PUPRFG,PUFG1BUF  SINGLE BUFFER ?                 GP08088
         BZ    *+8           NO                                 GP08088
         MVI   DCBBUFNO-IHADCB+PWDCB,1  FORCE INGLE BUFFER      GP08088
         NI    PWFLAG,PWFPUN+PUPGPAGE+PUPUPAGE+PUPGXLST  OPTIONS 84170
         CLI   PWDDNAM,C'%'  REQUEST FOR STORAGE ACCESS METHOD ? 83331
         BNE   POPNFAKE                                          83275
         OI    PWFLAG,PWFAKE   SET FAKE ACCESS METHOD            83275
         LA    R3,PWVCON     GET FAKE JFCB ADDRESS               83275
         USING INFMJFCB,R3                                       83275
         MVC   JFCUCSID,BLANKS   JUST IN CASE                    83331
         MVC   JFCFCBID,BLANKS                                   83331
         MVC   JFCNCP,PUDDNAM+1   MOVE LINES/PAGE                83275
         CLI   PUDDNAM,C'%'  ARE WE DOING THE ALTERNATE ENTRY ?  83331
         BNE   POPNCONS      YES; DON'T USE INVALID DATA         83331
         OC    JFCUCSID,PUDDALT    MOVE REQUESTED UCS            83331
         CLI   JFCUCSID,C' '       ANY ?                        GP08145
         BNH   *+10                NO; RETAIN DEFAULT           GP08145
         MVC   PWUCS,JFCUCSID      ELSE COPY USER'S             GP08145
         OC    JFCFCBID,PUDDALT+4    AND FCB                     83331
         CLI   JFCFCBID,C' '       ANY ?                        GP08145
         BNH   *+10                NO; RETAIN DEFAULT           GP08145
         MVC   PWFCB,JFCFCBID      ELSE COPY USER'S             GP08145
         DROP  R3                                                83275
         B     POPNCONS      RESUME STANDARD CODE                83275
POPNFAKE CLI   PWDDNAM,C'*'   CONSOLE PROCESSING ?               83331
         BNE   POPNCONS      NO                                  81163
         OI    PWFLAG,PWFPUN+PWFCON  FORCE CONSOLE/PUNCH MODE    81201
         MVI   OXDRECFM,X'40'  SET DEFAULT RECFM=V               81163
         ICM   R1,1,PUWIDTH  DID USER SPECIFY WIDTH ?            81186
         BNZ   POPLNLEN      YES; HONOR IT                       81163
         B     POPLNPRT      ELSE SET DEFAULT LENGTH             81163
POPNCONS OC    OXDRECFM,PUCCFG  COPY USER'S RECFM
         BNZ   POPLNUSR      CHECK IT
         MVI   OXDRECFM,DCBRECF  SET PUNCH DEFAULT F(B)          81271
         TM    PWFLAG,PWFPUN   PUNCH OUTPUT ?                    81271
         BNZ   POPLNPUR      YES; DEFAULT IS NO CC               81271
         MVI   OXDRECFM,DCBRECV  ELSE SET FOR V(B)A              81271
POPLNUSR TM    OXDRECFM,DCBRECLA  ANY RECFM ?
         BNZ   *+8           YES
         OI    OXDRECFM,DCBRECV  SET MY DEFAULT
         TM    OXDRECFM,CLNOCC   NO CONTROL CHARACTER ?
         BZ    POPLNYC       YES; CHECK WHICH
POPLNPUR NI    OXDRECFM,255-DCBRECCC-CLNOCC  RESET               81271
         B     POPLNDEF
POPLNYC  TM    OXDRECFM,DCBRECCC  ANY SUPPLIED ?
         BM    POPLNDEF      USE IF M OR A ON, BUT NOT BOTH      81271
         OI    OXDRECFM,DCBRECCA  DEFAULT TO ASA
         NI    OXDRECFM,255-DCBRECCM
POPLNDEF ICM   R1,1,PUWIDTH  GET USER'S WIDTH                    81186
         BP    POPLNLEN      YES (?)
         LA    R1,DEFPUW     SET FOR PUNCH
         TM    PUPRFG,PUPUNCH   PUNCH OUTPUT ?
         BNZ   *+8           YES
POPLNPRT LA    R1,DEFPRW     ELSE SET FOR DEFAULT PRINT SIZE     81163
POPLNLEN STH   R1,PWWIDTH    SAVE TEXT WIDTH
         TM    OXDRECFM,DCBRECCC  CONTROL ?
         BZ    *+8           NO
         LA    R1,1(,R1)     ALLOW FOR IT
         STH   R1,OXLRECLF   SET FIXED DEFAULT
         LA    R1,4(,R1)     ADD RDW LENGTH
         STH   R1,OXLRECLV   SET VARIABLE DEFAULT
POPPU2C  LA    R1,DCBEXIT2   POST-DCBEXIT DCBEXIT                81159
         ST    R1,OXOPLIST                                       81159
         MVI   OXOPLIST,OXTEXITF+X'80'  SET LAST ENTRY           83275
         MVI   PWCURLN,X'FF'  FORCE PAGE EJECT FIRST TIME
         LH    R0,PULPP      GET USER'S LINES/PAGE SPECIFICATION
         CH    R0,=H'15'     ANYTHING USABLE ?
         BNL   POPLPPDF      YES
         LA    R0,DEFLPP6    PREFERRED LINES/PAGE COUNT
POPLPPDF STC   R0,PWMAXLN+1
         TM    PUPRFG,PUPGPAGE   PAGE NUMBER FEED-BACK ?
         BZ    POPNPF        NO
         MVC   PWPAGE,PUPAGE  COPY STARTING PAGE#-1
POPNPF   TM    PUPRFG,PUPUNCH  PUNCH OPTION ?
         BNZ   POPPU3        YES; SKIP TITLES/FOOTERS
         LA    R0,7          SET MAXIMUM LEGAL COUNT
         SLR   R14,R14
         IC    R14,PUTIT#    GET TITLE COUNT
         CR    R14,R0          LEGAL ?
         BNH   *+6
         LR    R14,R0          ELSE CLIP
         STH   R14,PWTIT#    SET TITLE COUNT
         IC    R14,PUFOOT#   GET TITLE COUNT
         CR    R14,R0          LEGAL ?
         BNH   *+6
         LR    R14,R0          ELSE CLIP
         STH   R14,PWFOOT#   SET TITLE COUNT
POPPU3   TM    PWFLAG,PWFCON+PWFAKE  CONSOLE OR STORAGE ?        83331
         BZ    POPDVT        NEITHER                             81163
         SR    R15,R15                                          GP99034
         ICM   R15,7,PWEXLIST+1  LOAD @DCBEXIT ADDRESS          GP99034
         LA    R1,PWDCB      LOAD DCB ADDRESS                    81163
         MVI   PWDCB+DCBOFLGS-IHADCB,DCBOFUEX  SET DUMMY CALL    81163
         BALR  R14,R15       BUILD BASIC DCB PARMS               93164
         MVI   PWDCB+DCBOFLGS-IHADCB,DCBOFOPN  SET DCB OPEN      81163
         NI    DCBMACR2-IHADCB+PWDCB,255-DCBMRLCP  RESET LOCATE MODE
         OI    DCBMACR2-IHADCB+PWDCB,DCBMRMVP  SET MOVE MODE     83275
         L     R14,PWDDNAM+4   LOAD IN-STORAGE 'PUT' ADDRESS     83331
         TM    PWFLAG,PWFCON   CONSOLE MODE ?                    83331
         BZ    POPPHPUT      NO; STASH USER'S 'PUT' ADDRESS      83331
         LA    R14,PUTWTO    GET WTO ROUTINE                     83331
         AIF   (NOT &MVS).NOSVSTP                                83331
         CLI   PWDDNAM+1,C'U'   *U(SER) REQUEST ?                83331
         BNE   POPPHPUT      NO                                  83331
         L     R15,PSAAOLD-PSA  GET ASCB ADDRESS                 83331
         ICM   R15,15,ASCBTSB-ASCB(R15)  RUNNING UNDER TSO ?     83331
         BZ    POPPHPUT      NO                                  83331
         LA    R14,PUTTPUT   SET FOR PUT ROUTINE                 83331
.NOSVSTP ANOP  ,                                                 83331
POPPHPUT STCM  R14,7,PWDCB+DCBPUT+1-IHADCB  SET PHONY PUT ADDRESS
         TM    PWFLAG,PWFCON+PWFPUN  CONSOLE OR PUNCH MODE ?     84170
         BNZ   POPEXIT       YES; SKIP ALL ELSE                  83275
         CLI   PWVCON+JFCUCSID-INFMJFCB,C' '  DID USER UCS ?     83275
         BNH   POPEXIT       NO                                  83275
         XC    DB,DB         SET 'DUMMY' SIGNAL                  83275
         MVC   PLSPACE(8),PATBLDL  MOVE BLDL PATTERN             83275
         CLI   PWFILL,X'FF'  INHIBIT TRANSLATION ?               93043
         BNE   POPTRFIL      NO                                  93043
         LA    R15,255                                           93043
POPTRFIQ STC   R15,PWTRTAB(R15)  TRANSLATE TO SELF               93043
         BCT   R15,POPTRFIQ                                      93043
         B     POPTRFIS                                          93043
POPTRFIL MVC   PWTRTAB(1),PWFILL INITIALIZE TRANSLATE TABLE      83275
         MVC   PWTRTAB+1(255),PWTRTAB                            83275
POPTRFIS LA    R3,PWVCON     POINT TO FAKE JFCB                  83275
         B     POPLOCFX      GO TO UCS PROCESSING                83275
         SPACE 1                                                 81163
POPDVT   DEVTYPE PWDCB+DCBDDNAM-IHADCB,DB  GET DEVICE TYPE
         BXH   R15,R15,POPDEVAL   FAILED; CHECK FOR ALTERNATE    81165
         CLI   DB+2,0        DD DUMMY ?                          81165
         BNE   POPDEVT       NO; USE THIS ONE                    81165
         CLI   PUDDALT,C' '  ALTERNATE SUPPLIED ?                81165
         BNH   POPDEVDU      NO; LEAVE DUMMY                     81165
         DEVTYPE PUDDALT,DB  CHECK FOR ALTERNATE                 81165
         BXH   R15,R15,POPDEVDU  USE FIRST ONE                   81165
         CLI   DB+2,0        ALSO DUMMY ?                        81165
         BE    POPDEVDU      YES                                 81165
POPDEVAL OI    RETCC,4       SHOW ALTERNATE USED                 81165
         CLI   PUDDALT,C' '  ALTERNATE SUPPLIED ?                81165
         BNH   POPNOP        NO; ERROR                           81165
         MVC   PWDCB+DCBDDNAM-IHADCB(8),PUDDALT  SET ALTERNATE   81166
         DEVTYPE PWDCB+DCBDDNAM-IHADCB,DB  GET DEVICE TYPE       81166
         BXH   R15,R15,POPNOP   FAIL IF NEITHER SUPPLIED         81165
         CLI   DB+2,0        ALSO DUMMY ?                        81165
         BNE   POPDEVT       NO; GOOD                            81165
POPDEVDU MVI   RETCC,8       INDICATE DUMMY DD                   81165
         TM    CALLFLG,CLABEDU  ABEND ON DD DUMMY ?             GP03030
         BZ    POPDEVT       NO                                 GP03030
         OI    CALLFLG,CLERR  SET ERROR (ABEND, POSS. MSG) FLAG GP03030
POPDEVT  MVC   PWDDNAM,PWDCB+DCBDDNAM-IHADCB  SAVE DD NAME       81201
         RDJFCB MF=(E,PWDCB@)  GET THE JFCB                      81201
         BXH   R15,R15,POPNOP  FORGET THIS IF BAD
         AIF   (&SVC@SVC NE 0).NO@SVC1                          GP99029
         LOAD  EPLOC==CL8'@SERVICE',ERRET=POPCOMOD              GP02338
         ST    R0,@SERVICE   LOAD AND SET SERVICE ROUTINE       GP02338
         OI    @SERVICE,X'80'  DO IT RIGHT                      GP02338
.NO@SVC1 LTCB  R3,USE=YES                                        90316
         ICM   R3,15,TCBJSCB                                     90316
         BZ    POPNJCT                                           90316
         USING IEZJSCB,R3                                        90316
         ICM   R3,15,JSCBJCT GET SVA ADDRESS OF JCT              90316
         DROP  R3                                                90316
         SERVICE SWARL,(R3)  GET THE SWA ADDRESS OF JCT          90338
         LR    R3,R1         SAVE FOR LATER                      90316
POPNJCT  MVI   CALLAD+3,0    SET AUTHORIZED FLAG                 93164
         TESTAUTH FCTN=1     CALLING PROGRAM AUTHORIZED ?        93164
         BXLE  R15,R15,POPCMOD  YES                              93164
         OI    CALLAD+3,1    SET UNAUTHORZIED                    93164
         AIF   (&SVC@SVC EQ 0).NOSIOT1                          GP99029
         SERVICE APFON       AUTHORIZE US                        93164
         AGO   .NOSIOT2                                         GP02338
.NOSIOT1 B     POPCOMOD      CAN'T GET AUTHORIZED               GP02338
.NOSIOT2 SPACE 1                                                GP02338
POPCMOD  MODESET KEY=ZERO    GET AROUND XA FETCH PROTECT         93164
         LTR   R3,R3         ANY JCT ADDRESS ?                   91001
         BZ    POPMNJCT      NO                                  91001
         USING INJMJCT,R3    DECLARE IT                          90316
         MVC   PWSOUTCL,JCTJMGPO  COPY MESSAGE CLASS             91001
         DROP  R3                                                90316
POPMNJCT SERVICE TIODD,PWDDNAM  LOOK FOR DD CARD                 90309
         BXH   R15,R15,POPCSIOT  NO SIOT                         90309
         LR    R1,R0         COPY TIOT ENTRY ADDRESS             90309
         SERVICE SIOTE,(R1)  GET MATCHING SIOT ENTRY             90309
         BXH   R15,R15,POPCSIOT                                  90309
         USING INDMSIOT,R1   DECLARE IT                          90309
         MVC   PWSOUTWT,SCTOUTNM  COPY WRITER NAME               91001
         MVC   PWSOUTFM,BLANKS  PREPARE TRAILING BLANK FILL      91001
         MVC   PWSOUTFM(L'SCTOUTNO),SCTOUTNO  COPY FORM 'NUMBER' 91001
         CLI   SCTOUTPN,C'*'  USE MSGCLASS ?                     91001
         BE    POPMSIOT      YES                                 91001
         MVC   PWSOUTCL,SCTOUTPN  COPY SYSOUT CLASS              91001
POPMSIOT DS    0H                                               GP02339
         AIF   ('&LOCAL' NE 'PID').NOPIDX                       GP02339
         CLI   PWSOUTCL,C'X' OUTPUT CLASS X ?                    91001
         BE    POPYSIOT      YES; SET XEROX MODE                 90309
         CLI   PWSOUTWT,C'X' OUTPUT WRITER X-SOMETHING ?         91001
         BNE   POPNSIOT      NO; ASSUME NOT XEROX                90309
POPYSIOT OI    PWFLG2,PWF4050  SET FOR LOCAL 4050 CODE           90309
POPNSIOT DS    0H     *OBS*  RESTORE 24-BIT ADDRESSING MODE      91001
.NOPIDX  DROP  R1                                                90316
POPCSIOT MODESET KEY=NZERO   AROUND TEST CODE                    93164
         TM    CALLAD+3,1    USER AUTHORIZED ?                   93164
         BZ    POPCOMOD      YES; PRESERVE                       93164
         AIF   (&SVC@SVC EQ 0).NOSIOT4                          GP02338
         SERVICE APFOF       ELSE RESET                          93164
         AGO   .NOSIOT4                                         GP02338
.NOSIOT4 SPACE 1                                                GP02338
POPCOMOD ICM   R0,15,@SERVICE  DID WE LOAD @SERVICE ?           GP03033
         BZ    POPCOMOS      NO; NO NEED TO DELETE              GP03033
         DELETE EPLOC==CL8'@SERVICE'  GET RID OF @SERVICE       GP02338
         XC    @SERVICE,@SERVICE                                GP02338
POPCOMOS LA    R3,PWVCON                                        GP03033
         USING INFMJFCB,R3   ELSE DECLARE IT
         MVC   EDITSAVE(5),JFCUCSID  SAVE UCS/OPT                85156
         MVC   PLSPACE(8),PATBLDL  MOVE BLDL PATTERN             81159
         CLI   PWFILL,X'FF'  NO-TRANSLATE OPTION ?               93043
         BE    POPNPFIL      YES; DON'T CREAM                    93043
         MVC   PWTRTAB(1),PWFILL INITIALIZE TRANSLATE TABLE      81186
         MVC   PWTRTAB+1(255),PWTRTAB                            81159
POPNPFIL CLI   JFCUCSID,C' '   ANY UCS ID ?                      81159
         BNH   *+10          NO                                 GP08145
         MVC   PWUCS,JFCUCSID   RESET UCS DEFAULT               GP08145
         OC    PWUCS,BLANKS    MAKE MVS ZEROES INTO BLANKS      GP08145
         MVC   JFCUCSID,PWUCS  PROPAGATE BACK                   GP08145
         CLC   PWUCS,=CL4'&DEFUCS '    IS IT DEFAULT ?          GP08145
         BNE   *+10          NO; KEEP IT                        GP08145
         XC    JFCUCSID,JFCUCSID   ELSE CLEAR IT                GP08145
         CLI   JFCFCBID,C' '   ANY FCB ID ?                     GP08145
         BNH   *+10          NO                                 GP08145
         MVC   PWFCB,JFCFCBID   RESET FCB DEFAULT               GP08145
         OC    PWFCB,BLANKS    MAKE MVS ZEROES INTO BLANKS      GP08145
         CLC   PWFCB,=CL4'&DEFFCB '    IS IT DEFAULT ?          GP08145
         BNE   *+10          NO; KEEP IT                        GP08145
         XC    JFCFCBID,JFCFCBID   ELSE CLEAR IT                GP08145
         CLI   DB+2,UCB3UREC   DOING DIRECT PRINT/PUNCH ?        81159
         BNE   POPNPRT       NO                                  81159
         LA    R14,PATPUN    GET TABLE OF UCBTBYT4 FOR PUNCHES   81159
         LA    R15,L'PATPUN    AND NUMBER                        81159
POPNPULP CLC   DB+3(1),0(R14)                                    81159
         BNE   POPNPULL      NO MATCH                            81159
         OI    PWFLAG,PWFPUN   SET FOR PUNCH                     81201
         B     POPNPRT       AND SKIP OUT                        81159
POPNPULL LA    R14,1(,R14)                                       81159
         BCT   R15,POPNPULP  TRY NEXT ENTRY                      81159
         LA    R14,PATPRT    GET TABLE OF PRINTER TYPES          81159
         LA    R15,L'PATPRT     AND NUMBER                       81159
POPNPRLP CLC   DB+3(1),0(R14)  MATCH ?                           81159
         BNE   POPNPRLL      NO                                  81159
         CLI   DB+3,X'0B'    3203 ?                              85315
         BE    POPY3203      YES; USE UCS3 AND FCB2              85315
         CLI   DB+3,X'09'    3211 ?                              81159
         BH    POPNPRT       NOTHING YET FOR 3800 OR 1443        81159
         BE    POPN3211      3211 CODE                           81159
         MVI   PLSPACE+7,C'1'  MAKE UCS1 FOR LOOKUP              81159
POPC3203 CLC   =C'11 ',JFCUCSID+1  3211 REQUEST ?                85315
         BNE   POPNPRT       NO; SKIP OUT                        81159
         MVC   JFCUCSID+1(3),=C'N  '  MAKE PREFERRED ID          81159
         B     POPNPRT                                           81159
POPY3203 MVI   PLSPACE+7,C'3'  MAKE UCS3 FOR LOOKUP              85315
         B     POPC3203      JOIN COMMON                         85315
POPN3211 MVI   PLSPACE+7,C'2'  MAKE UCS2 FOR LOOKUP              81159
         CLC   =C'N  ',JFCUCSID+1  1403 VERSION ?                81159
         BNE   POPNPRT       NO                                  81159
         MVC   JFCUCSID+1(3),=C'11 '  MAKE 3211 EQUIVALENT       81159
         B     POPNPRT                                           81159
POPNPRLL LA    R14,1(,R14)                                       81159
         BCT   R15,POPNPRLP  TRY AGAIN                           81159
POPNPRT  TM    PWFLAG,PWFPUN   PUNCH ?                           81201
         BNZ   POPLOCFX      YES; SKIP FINAGLE
         TM    JFCRECFM,JFCFIX  F/U FORMAT ?
         BNZ   POPLOCMD      YES; CHANGE MODE
         TM    JFCRECFM,JFCRCFM  ANY MODE ?
         BNZ   POPLOCFX      YES; USE MOVE MODE
         TM    OXDRECFM,DCBRECF  FIXED DEFAULT ?
         BZ    POPLOCFX      NO; MAY BE OK
POPLOCMD NI    DCBMACR2-IHADCB+PWDCB,255-DCBMRMVP  RESET MOVE MODE
         OI    DCBMACR2-IHADCB+PWDCB,DCBMRLCP  SET LOCATE MODE
POPLOCFX CLC   =C'UN ',JFCUCSID  USER REQUESTED BYPASS OF TRT ?  90294
         BE    POPUCSBP      YES; BYPASS TRANSLATION             90294
         CLC   =C'U11 ',JFCUCSID  ALTERNATE BYPASS ?             90294
         BE    POPUCSBP      YES; BYPASS                         90294
         CLC   =C'NONE',JFCUCSID  USER REQUESTED BYPASS OF TRT ? 90294
         BNE   POPUCS        NO                                  81159
         XC    JFCUCSID,JFCUCSID  CLEAR IT OUT                  GP99355
         B     POPUCSBP      LOOK FOR FCB                        81159
POPUCS   BAL   R9,UCSLOAD    GET UCS IMAGE AND BUILD TRANSLATE TABLE
         AIF   ('&LOCAL' NE 'SFT').SKIPUCS                      GP99355
         XC    JFCUCSID,JFCUCSID  CLEAR IT OUT                  GP99355
.SKIPUCS ANOP  ,                                                GP99355
POPUCSBP CLC   =C'D132',PWFCB  NEW LASER DOUBLE-PAGE ?          GP08145
         BNE   *+8           NO                                  90056
         MVI   PWMAXLN+1,130  ALLOW 130 LINES PER PAGE           90056
         CLI   PWFCB,C'8'    FCB=8...                           GP08145
         BE    POPFCB8        YES - PWMAXLN = 60+22 = 82    *TSM*
         CLC   PWFCB,=C'STD3'                                   GP08145
         BNE   POPNFCB        NO  - PWMAXLN = 60            *TSM*
POPFCB8  MVI   PWMAXLN+1,DEFLPP8   DEFAULT FOR 8LPI         *TSM*
POPNFCB  CLI   JFCNCP,15     NCP REQUESTED ?                *TSM*
         BL    POPDOP1       MAYBE, BUT IGNORE
         MVC   PWMAXLN+1(1),JFCNCP  COPY OVER
POPDOP1  MVI   JFCNCP,0      RESET IT ANYWAY
         ICM   R0,15,DB      DUMMY ?                             81271
         BZ    POPEXIT       YES; LEAVE IT CLOSED
         TM    DB+2,UCB3DACC+UCB3TAPE   DISK/TAPE DS ?           81271
         BNM   POPNTMOD      NO                                  81271
         MVC   JFCUCSID(5),EDITSAVE  RESTORE DBUFN               85156
         TM    JFCBIND1,JFCPDS   PDS MEMBER ?                    81271
         BNZ   POPNTMOD      YES; MOD NOT SUPPORTED              81271
         TM    JFCBIND2,JFCNEW  DISP=NEW ?                       81271
         BO    POPSTMOD      YES; FORCE MOD                      81271
         TM    JFCBIND2,JFCMOD  ALREADY MOD ?                    81271
         BNZ   POPNTMOD      YES; AVOID EXTRA OVERHEAD           81271
POPSTMOD OI    JFCBIND2,JFCNEW   SET DISP=NEW                    81271
         OI    JFCBFLG2,JFCMODNW   CHANGE NEW TO MOD AFTER OPEN  81271
         DROP  R3
POPNTMOD OPEN  MF=(E,PWDCB@),TYPE=J  OPEN FOR OUTPUT             81271
         TM    PWDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN NOW ?
         BZ    POPNOP        NO; TOO BAD
         L     R5,DCBDEBAD-IHADCB+PWDCB   GET DEB ADDRESS
         N     R5,=X'00FFFFFF'  CLEAN IT                        GP99028
         USING DEBBASIC,R5                                       81159
         ICM   R5,7,DEBSUCBA+1  LOAD AND TEST THE UCB ADDRESS   GP99028
         BZ    POPPNUCB      SKIP IF NO UCB                     GP99028
         N     R5,=X'00FFFFFF'  CLEAN IT                        GP99028
         USING UCBOB,R5      ELSE DECLARE UCB                    81159
         TM    UCBTBYT3,UCB3DACC+UCB3TAPE  DASD OR TAPE ?        85156
         BM    POPPNUCS      YES; SKIP AND LEAVE 108 SEGMENT     85156
         CLI   UCBTBYT3,UCB3UREC  UNIT RECORD ?                  81159
         BNE   POPPNUCB      NO                                  81159
         TM    UCBTBYT2,X'80'   UCS EXTENSION PRESENT ?          81159
         BZ    POPPNUCB      NO                                  81159
         CLI   UCBTBYT4,X'0B'   3203 ?                           85315
         BE    POPPYUCS      YES                                 85315
         CLI   UCBTBYT4,X'08'   1403 ?                           81159
         BL    POPPNUCB      NO                                  81159
         CLI   UCBTBYT4,X'09'   3211 ?                           81159
         BH    POPPNUCB      NO                                  81159
POPPYUCS DS    0H            PRINTER HAS UCS                     85315
         AIF   (&MVS).NOATI                                      82102
         TM    UCBATI,UCBHPDV   PSEUDO-DEVICE ?                  81159
         BNZ   POPPNUCB      YES; SKIP                           81159
.NOATI   LAT   R1,UCBXTADR,POPPNUCB  GET UCS EXTENSION           81159
         USING INFMJFCB,R3   GET JFCB BACK                       81159
         MVC   JFCUCSID,UCBUCSID-UCBUCS(R1)  COPY UCS BACK       81159
         OC    JFCUCSID,BLANKS  NEVER TRUST ANYONE ...           82102
         MVC   PWUCS,JFCUCSID   UPDATE FOR LOAD                 GP08145
         L     R5,PWTRAN     SAVE TRANSLATE TABLE ADDRESS        81159
         BAL   R9,UCSLOAD    UPDATE UCS                          93164
         DROP  R5                                                81271
         ICM   R0,15,PWTRAN  STILL HAVE A TABLE ?               GP99028
         N     R0,=X'7FFFFFFF'  MAKE SURE OF ADDRESS            GP99029
         BNZ   POPPNUCB      YES; USE IT                         81271
         N     R5,=X'7FFFFFFF'  MAKE SURE OF ADDRESS            GP99029
         ST    R5,PWTRAN     SET PRIOR ADDRESS IF NO NEW ONE     81271
POPPNUCB DS 0H PWUCS,JFCUCSID  SAVE UCS FOR USER                 81271
POPPNUCS DS 0H PWFCB,JFCFCBID    ALSO FCB                        85156
*OLD*    OC    PWFCB,BLANKS  DON'T TAKE CHANCES                  82102
         DROP  R3                                                81271
         LH    R1,PWDCB+DCBLRECL-IHADCB  GET NEW RECORD LENGTH   81201
         TM    DCBRECFM-IHADCB+PWDCB,DCBRECCC  CONTROL CHARACTERS ?
         BZ    *+6           NO
         BCTR  R1,0
         TM    DCBRECFM-IHADCB+PWDCB,DCBRECF  FIXED/U ?
         BNZ   *+8           YES
         SH    R1,=H'4'
         LA    R0,L'PWREC    GET MAXIMUM RECORD LENGTH
         CR    R1,R0         FITTING ?
         BNH   *+6             YES
         LR    R1,R0         ELSE CLIP
         STH   R1,PWWIDTH    SET NEW WIDTH
         B     POPEXIT       OK
POPNOP   MVI   RETCC,12      SHOW UNUSABLE
         TM    CALLFLG,CLABE  ABEND ON BAD DD ?                 GP03030
         BZ    POPEXIT       NO; RETURN ERROR                   GP03030
         OI    CALLFLG,CLERR  SET ERROR (ABEND, POSS. MSG) FLAG GP03030
POPEXIT  TM    PWFLAG,PWFPUN    PUNCH MODE ?                     81201
         BZ    POPEXITU      NO                                  81163
         XC    PWTRAN,PWTRAN   YES; KILL TRANSLATION             81163
POPEXITU MVC   PLSPACE(6),MINVCON  INITIALIZE SPACING RECORD     81163
         MVC   PLSPACCC+1(MAXWIDTH),PLSPACCC  CLEAR TEXT         81159
         LA    R0,1
         STCM  R0,7,DCBEXLST+1-IHADCB+PWDCB  KILL THE EXIT LIST
         DELETE EPLOC=@DCBEXIT  FREE THE EXIT ROUTINE
         CLI   RETCC,8       MAJOR ERROR ?
         BH    POPFREE       YES; FREEMAIN AGAIN                 86292
         TM    PWDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN ?
         BNZ   POPEXITC      YES; CHAIN
         MVI   RETCC,8       SET LEVEL 8 ERROR FOR DUMMY
         TM    CALLFLG,CLERR  ABEND REQUESTED ?
         BNZ   POPFREE       YES
         LA    R0,PUTPUTEX   GET PUT EXIT ADDRESS                81193
         STCM  R0,7,PWDCB+DCBPUT+1-IHADCB  SET PHONY PUT ADDRESS 81193
POPEXITC L     R3,PWTCB      GET THE TCB ADDRESS                 83275
         USING TCB,R3
         ICM   R2,15,TCBFSA  GET SAVE AREA                      GP99028
         DROP  R3
         BZ    EXIT16        SET MAJOR ERROR
POPLOOP  ICM   R1,15,0(R2)   IS LINK POINTER ZERO ?             GP99028
         BZ    POPLOOS       YES; CHAIN OURS IN
         LR    R2,R1
         B     POPLOOP       TRY AGAIN
POPLOOS  STCM  R8,15,0(R2)   CHAIN THIS BLOCK IN                GP99028
         OC    PRT0OPEN,PRT0OMSK  SET DEVICE OPEN BIT            82031
         L     R1,SAVE13     GET USER'S SAVE AREA                81193
         ST    R8,SAVE0-SAVE(,R1)  RETURN WORK AREA IN R0        81193
         MVC   PWVCON(6),MINVCON  INIT PRINT RECORD
         MVC   PWREC,PWREC-1
         MVC   PWMCREC(MAXWIDTH+5),PWVCON  INIT MCC RECORD       81159
         MVI   PWMCC,C'N'    SET 'NEVER USED' FLAG               81159
         TM    PWFLAG,PWFPUN   PUNCH ?                           81201
         BNZ   EXITSET       YES; DONE
         MVC   PWULOP(L'PWTIT1),PWVCON  INIT UNDER LINE
         LH    R2,PWTIT#     GET NUMBER OF TITLES
         AH    R2,PWFOOT#      AND FOOTERS
         BZ    EXITSET       QUIT IF NONE
         MVC   PWTIT1(6),MINVCON  SET MINIMUM TITLE              81159
         MVC   PWTIT1+5(L'PWTIT1-5),PWTIT1+4  PROPAGATE BLANKS   81159
         LA    R1,PWTIT1
POPTITL  BCT   R2,POPTITM
         ICM   R0,3,PWTIT#   ANY TITLES ?                        81271
         BZ    EXITSET       NO
         MVI   PWTIT1+4,C'1'  SET EJECT FOR FIRST ONE
         B     EXITSET
POPTITM  MVC   L'PWTIT1(L'PWTIT1,R1),0(R1)  INIT TITLE           81159
         LA    R1,L'PWTIT1(,R1)
         B     POPTITL       DO NEXT
         SPACE 1
POPFREE  TM    CALLFLG,CLERR  WAS THERE AN ERROR ?              GP03030
         BNZ   POPFREE1      YES; FORCE THE MESSAGE             GP03030
         TM    CALLFLG,CLNWTO  SUPPRESS WTO ?                   GP03030
         BNZ   POPFREE2      YES                                GP03030
POPFREE1 MVC   PWVCON(POPWTOL),POPWTO  MAKE WTO PATTERN
         MVC   PWVCON+4(8),PWDDNAM   INSERT DD NAME              83331
         WTO   MF=(E,PWVCON)   WRITE THE MESSAGE
         CLI   PUDDALT,C' '  PRINTABLE ALTERNATE ?
         BNH   POPFREE2      NO
         MVC   PWVCON+4(8),PUDDALT  ALSO SHOW ALTERNATE
         WTO   MF=(E,PWVCON)
POPFREE2 BAL   R9,FREEFSAM   FREE THE WORK AREA                  93164
         TM    CALLFLG,CLERR ABEND ON ERROR ?                   GP03030
         BZ    EXITSET       NO; JUST RETURN ERROR CODE         GP03030
         ABEND 1403,DUMP     QUIT                               GP03033
         SPACE 1
POPWTO   WTU   'DD-NAME? DD MISSING OR UNUSABLE',MF=L,ROUTCDE=11
POPWTOL  EQU   *-POPWTO
@DCBEXIT DC    0CL8'@DCBEXIT',C'@'   DCB EXIT ROUTINE            81271
@DCBEXID DC    C'DCBEXITP'   EXIT LIST IDENTIFIER                81271
         SPACE 1
EXIT12   MVI   RETCC,12      SET MAJOR ERROR
         B     EXITSET       AND QUIT
         SPACE 1
PATBLDL  DC    AL2(1,60),C'UCS0'  PATTERN FOR IMAGELIB BLDL      85315
PATPUN   DC    X'0203050C'   PUNCHES 2540,1442,2520,3525         81159
PATPRT   DC    X'08090A0B0E30' 1403,3211,1443,3203,3800,?        85315
         SPACE 2                                                 81159
*        UCS LOAD - TRANSLATE TABLE BUILD                        81159
UCSLOAD  MVC   DB+4(1),PLSPACE+7  SAVE THE INDEX BYTE            85315
         CLI   DB+4,C'0'     UNSPECIFIED ?                       85315
         BNE   *+8           NO; USE IT                          85315
         MVI   PLSPACE+7,C'1'  LOAD THROUGH UCS1, UCS2 AND UCS3  85315
         IMGLIB OPEN         OPEN IMAGELIB                       81159
         AIF   (NOT &SVS).NIMGBUG                                82102
         OI    DCBOFLGS-IHADCB(R1),DCBOFOPN  FIXED BY APAR       81159
.NIMGBUG LR    R4,R1         SAVE DCB ADDRESS                    81159
         USING INFMJFCB,R3   DECLARE JFCB MAPPING                81159
*CSLOAD2 MVC   PLSPACE+8(4),JFCUCSID  COMPLETE MODULE NAME       81159
UCSLOAD2 MVC   PLSPACE+8(4),PWUCS     COMPLETE MODULE NAME      GP08145
*OOPS?*  MVI   PLSPACE+7,C'1'   SET UCS1                         81159
UCSLAGIN EQU   *             RETRY                               81159
         BLDL  (R4),PLSPACE  TRY IT                              81159
         BXH   R15,R15,UCSLFAIL   NOT FOUND                      81159
         AIF   (&MVS).UCSMVS                                     82102
         LOAD  DE=PLSPACE+4,DCB=(R4)   LOAD THE UCS IMAGE        81159
         AGO   .UCSCOM                                           81159
.UCSMVS  LOAD  DE=PLSPACE+4,DCB=(R4),ERRET=UCSLFAIL              88304
.UCSCOM  LR    R1,R0         COPY MODULE ADDRESS                 81159
         SLR   R14,R14                                           81159
         SLR   R15,R15                                           81159
         SLR   R0,R0                                             81159
         ICM   R14,1,1(R1)   GET NUMBER OF LINES                 81159
         BZ    UCSLFADE      DELETE MODULE                       81159
         CLI   PWFILL,X'FF'  INHIBIT TRANSLATE ?                 93043
         BE    UCSLFADE      YES; SKIP TABLE BUILD               93043
         MVC   PWTRTAB(1),PWFILL INITIALIZE TABLE                81186
         MVC   PWTRTAB+1(255),PWTRTAB                            81159
UCSLLINE IC    R0,2(,R1)     GET NUMBER OF BYTES IN LINE         81159
         LA    R1,1(,R1)                                         81159
         AR    R15,R0        UP TOTAL COUNT                      81159
         BCT   R14,UCSLLINE  DO NEXT LINE                        81159
UCSLCSCH IC    R14,2(,R1)    GET PRINTABLE CHARACTER             81159
         STC   R14,PWTRTAB(R14)  PLACE INTO TRANSLATE TABLE      81159
         LA    R1,1(,R1)                                         81159
         BCT   R15,UCSLCSCH  DO ALL                              81159
         MVI   PWTRTAB+C' ',C' '   ADD BLANK                     81159
         LA    R1,PWTRTAB    GET TABLE ADDRESS                   81159
         ST    R1,PWTRAN     SET INTO TRANSLATE ADDRESS WORD     81159
         NI    JFCUCSOP,255-JFCFOLD  TURN FOLD OPTION OFF        81159
         CLC   PWTRTAB+X'81'(1),PWFILL    ANYTHING SET ?         81186
         BNE   UCSLDELT      YES; LEAVE IT ALONE                 81159
*XEROX*  OI    JFCUCSOP,JFCFOLD   SET FOLD OPTION ON             81159
         MVI   PWTRTAB+X'21',X'21'  LOCAL SHEET FEED STRING      89163
         MVC   PWTRTAB+X'81'(9),PWTRTAB+X'C1'  MAKE UPPER CASE   81159
         MVC   PWTRTAB+X'91'(9),PWTRTAB+X'D1'    ALPHABETICS FROM
         MVC   PWTRTAB+X'A2'(8),PWTRTAB+X'E2'      LOWER CASE    81159
UCSLDELT DELETE DE=PLSPACE+4                                     81159
UCSLEXIT DS    0H            IMGLIB DOESN'T EXPAND LABELS        81159
         IMGLIB CLOSE,(R4)                                       81159
         MVC   PLSPACE+7(1),DB+4  RESTORE INDEX BYTE             85315
         BR    R9            RETURN TO CALLER                    81159
UCSLFADE DELETE DE=PLSPACE+4                                     81159
UCSLFAIL CLI   DB+4,C'0'     LOOP MODE ?                         85315
         BNE   UCSLEXIT      NO; JUST QUIT                       85315
         CLI   PLSPACE+7,C'3'   REACHED MAX ?                    85315
         BNL   UCSLFAIT      YES; QUIT                           81159
         IC    R1,PLSPACE+7                                      81159
         LA    R1,1(,R1)                                         81159
         STC   R1,PLSPACE+7  CHANGE FROM UCS1 TO UCS2, ETC.      81159
         B     UCSLAGIN                                          81159
UCSLFAIT XC    PWTRAN,PWTRAN    SIGNAL NO TABLE                  81159
         B     UCSLEXIT      GO TO COMMON                        81159
         POP   USING                                            GP99029
         SPACE 1
*        @DCBEXIT POST-PROCESSING EXIT                           83275
*              THIS MODULE MAY BE CALLED BY ROUTINES IN KEY ZERO.
*              AS A RESULT, 30A ABENDS MAY OCCUR IN FREEPOOL WHEN
*              GETPOOL IS DONE BY OPEN. IF ALL PARAMETERS ARE    83275
*              VALID, THIS EXIT WILL DO THE GETPOOL.             83275
*        CODE CORRECTED FOR SAM/E (SEE IECQBFG1 IN LPALIB)       85157
         SPACE 1                                                 83275
         PUSH  USING                                             83275
         DROP  ,                                                 83275
         USING DCBEXIT2,R12                                      83275
         USING IHADCB,R11                                        83275
DCBEXIT2 BSM   R14,0         PRESERVE CALLER'S AMODE            GP99029
         LR    R12,R15                                           83275
         AM31  WORK=R15      GET HIGH                           GP99029
         LR    R11,R1        COPY DCB ADDRESS                    83275
         LR    R9,R14        SAVE RETURN ADDRESS                 83275
         LR    R10,R1        COPY DCB ADDRESS                    83275
         LA    R4,PWDCB-PRNTWORK                                 83275
         SLR   R10,R4                                            83275
         USING PRNTWORK,R10                                      83275
         TM    PWFLAG,PWFCON+PWFAKE  NON-I/O ACCESS METHOD ?     83275
         BNZ   DCBEXITX      YES; SKIP GETPOOL                   83275
         SLR   R5,R5                                             83275
         ICM   R5,3,DCBBUFL  GET BUFFER LENGTH                   83275
         BZ    DEXNBUFL      NONE; TRY BLOCKSIZE                 83275
         CH    R5,DCBBLKSI   NOT SMALLER THAN BLOCKSIZE ?        83275
         BNL   DEXYBUFL      RIGHT; USE LARGER SIZE              83275
DEXNBUFL ICM   R5,3,DCBBLKSI  GET BLOCKSIZE                      83275
         BZ    DCBEXITX      OPEN WILL BOMB                      83275
DEXYBUFL LA    R5,7(,R5)                                         83275
         N     R5,=X'0000FFF8'  ROUND TO DOUBLE-WORD SIZE        83275
         BZ    DCBEXITX      ?                                   83275
         SLR   R4,R4                                             85157
         ICM   R1,7,DCBBUFCA   HAVE A BUFFER ADDRESS ?           83275
         BZ    DEXNBUFP      NO; PROCEED                         83275
         N     R1,=X'00FFFFFF'  CLEAN IT                        GP99028
         TM    DCBBUFCA+L'DCBBUFCA-1,1   VALID ADDRESS ?         83275
         BZ    DEXBUFIN      YES; RE-INITIALIZE BUFFERS          85157
DEXNBUFP ICM   R4,1,DCBBUFNO GET NUMBER OF BUFFERS               85157
         BNZ   *+12          OK                                  83275
         LA    R4,1          SET FOR SINGLE BUFFER               83275
         STC   R4,DCBBUFNO   STASH BACK                          83275
         LR    R3,R5         COPY BUFFER SIZE                    83275
         MR    R2,R4         GET SPACE FOR BUFFERS               83275
         LA    R3,8(,R3)     PLUS BUFFER DESCRIPTOR WORD         83275
         L     R14,PWTCB     GET MY TCB ADDRESS BACK             83275
         L     R14,TCBRBP-TCB(,R14)  GET THE RB                  83275
         USING RBBASIC,R14   DECLARE RB                          83275
         TM    RBSTAB2,RBTCBNXT  ONLY RB ?                       83275
         BNZ   DCBEXITX      YES ?                               83275
         L     R14,RBLINK                                        83275
         SLR   R15,R15       SIGNAL UNPRIVILEGED CALLER          83275
         TM    RBOPSW+1,X'F0'  RUNNING IN KEY ZERO ?             83275
         BNZ   DCBEXITX *VIO BUG* WAS B DEXBUFSP      NO         83275
         LA    R15,252       SET FOR SPECIAL SUB-POOL            83275
DEXBUFSP STC   R15,PWBUFSP   SAVE FOR FREEPOOL                   83275
         DROP  R14                                               83275
         AIF   (NOT &MVS).OLDMAIN                                83275
*        GETMAIN RC,LV=(R3)  CONDITIONAL GETMAIN                 83275
         SLL   R15,8         SUBPOOL=0|252;CONDITIONAL GETMAIN   83275
         LR    R0,R3         LENGTH                              83275
         SLR   R1,R1         ADDRESS (IGNORED?)                  83275
         SVC   120           GETMAIN RC                          83275
         BXH   R15,R15,DCBEXITX  LET OPEN BOMB IF NO SPACE       83275
         AGO   .DOMAIN                                           83275
.OLDMAIN SLL   R15,24        SHIFT SUB-POOL NUMBER               83275
         OR    R3,R15        MAKE SP/LEN                         83275
         GETMAIN R,LV=(R3)   GET STORAGE OR BOMB                 83275
.DOMAIN  LR    R2,R1         SAVE POOL ADDRESS                   85160
         CLRL  (R2),(R3)     ZERO IT                             85160
         LR    R1,R2         RESTORE ADDRESS                     85160
         STH   R4,4(,R1)     SET BUFNO                           85160
         STH   R5,6(,R1)     SET BUFSZ                           83275
         STCM  R1,7,DCBBUFCA   SET BUFFER CONTROL BLOCK INTO DCB 83275
         STH   R5,DCBBUFL    UPDATE BUFFER LENGTH                83275
DEXBUFIN LA    R15,8(,R1)    POINT TO FIRST BUFFER               85157
         TM    4(R1),X'40'   F, NOT D, ALIGNMENT ?               85157
         BZ    *+8           NO                                  85157
         LA    R15,12(,R1)   SET NON-DOUBLE ADDRESS              85160
         ST    R15,0(,R1)    SET FIRST BUFFER ADDRESS            85160
         ICM   R4,1,5(R1)    LOAD AND TEST BUFNO                 85160
         BNZ   *+12          OK                                  85157
         LA    R4,1          FORCE                               85157
         STC   R4,5(,R1)     STASH IT BACK                       85160
         ICM   R5,3,6(R1)    BUFLEN                              85157
DEXBUFLP LR    R1,R15        GET NEXT BUFFER                     85157
         BCT   R4,DEXBUFST   SET BUFFER LINK POINTER             85157
         ST    R4,0(,R1)     TERMINATE CHAIN                     85157
DCBEXITX MVI   DCBNCP,0      JUST IN CASE                        83275
         LR    R14,R9        RESTORE EXIT ADDRESS                83275
         BSM   0,R14         RETURN IN CALLER'S AMODE           GP99029
         SPACE 1                                                 85157
DEXBUFST LA    R15,0(R1,R5)   POINT TO NEXT BUFFER               85157
         ST    R15,0(,R1)    STASH NEXT BUFFER ADDRESS           85157
         B     DEXBUFLP      TRY AGAIN                           85157
         POP   USING         RESTORE OLD USINGS                  83275
         EJECT ,
*        CLOSE ROUTINE
*
PCLOSE   BALR  R11,0         SET LOCAL BASE                      93164
         USING *,R11         DECLARE IT                          82102
         LTCB  R3            GET MY TCB                          86292
         CL    R3,PWTCB      SAME TCB ?                         GP99028
         BNE   SET4          NO; MINOR ERROR                     86292
         BAL   R9,CLEAROUT   CLEAR PRIOR LINES; DO FOOTERS; ETC. 93164
           NOP 0             CONTINUE IF NOT OPEN
         BAL   R14,HOTFOOT   PRODUCE FOOTERS                     93164
         BAL   R9,MCFINAL    WRITE FINAL MC RECORD               93164
         MVC   DB(1),PRT0OMSK  GET CURRENT DEVICE MASK           82102
         XI    DB,X'FF'      INVERT IT                           82102
         NC    PRT0OPEN,DB   RESET DEVICE OPEN BIT               82102
         TM    PWFLAG,PWFCON+PWFAKE  CONSOLE/STORAGE MODE ?      83275
         BNZ   PCLCHAIN      YES; JUST FREE IT UP                81163
         TM    PWDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN ?
         BZ    PCLCHAIN      NO; UNCHAIN
         CLOSE MF=(E,PWDCB@)
PCLOPOOL TM    PWDCB+DCBBUFCA-IHADCB+L'DCBBUFCA-1,1  FREEPOOLED ?
         BNZ   PCLCHAIN      YES; DON'T DO IT AGAIN              83275
         ICM   R1,7,PWDCB+DCBBUFCA-IHADCB   DIRTY POOL ?         83275
         BZ    PCLCHAIN      YES; SKIP FREEPOOL AND 30A ABEND    83275
         N     R1,=X'00FFFFFF' CLEAN IT                         GP99028
         SLR   R14,R14                 CLEAR REGISTER
         IC    R14,5(,R1)    GET BUFNO
         MH    R14,6(,R1)      TIMES BUFLEN
         LA    R0,8(,R14)    PREFIX
         TM    4(R1),X'40'   SIXTEEN-BYTE PREFIX ?
         BNO   *+8           NO
         LA    R0,16(,R14)   SET LONG PREFIX
         ICM   R0,8,PWBUFSP  SET SUB-POOL
         SVC   10            FREE THE BUFFERS                    83275
         LA    R0,1                                              85160
         STCM  R0,7,PWDCB+DCBBUFCA-IHADCB  DE-ACTIVATE POOL      85160
PCLCHAIN LA    R9,RECURSE    SET EXIT FROM IN-LINE SUBROUTINE    82031
         B     FREEFSA       FREE SAVE AREA AND RECURSE          82102
         SPACE 2
*        MVS - CLOSE WITH SPIN  (REQUIRES MVS WITH 3800 ENHANCEMENTS)
*                                          SU: FDM1133, ...      82214
*
PSPIN    BALR  R11,0         MAKE LOCAL BASE                     93164
         USING *,R11         LOCAL BASE REGISTER                 82102
         LTCB  R3            GET MY TCB                          86292
         CL    R3,PWTCB      SAME TCB ?                         GP99028
         BNE   SET4          NO; MINOR ERROR                     86292
         BAL   R9,CLEAROUT   CLEAR PRIOR LINES; DO FOOTERS; ETC. 93164
           B   RECURSE       SKIP IF NOT OPEN
         BAL   R14,HOTFOOT   PRODUCE FOOTERS                     93164
         BAL   R9,MCFINAL    WRITE FINAL MC RECORD               93164
         TM    PWFLAG,PWFCON+PWFAKE  CONSOLE/STORAGE MODE ?      83275
         BNZ   RECURSE       YES; SKIP OUT                       81163
         AIF   (NOT &MVS).NOSPIN                                 82102
         USING TCB,R3
         ICM   R3,15,TCBTIO  GET THE TIOT ADDRESS               GP99028
         BZ    SET4          HUH ?
         SLR   R1,R1                                             93051
         ICM   R1,3,DCBTIOT-IHADCB+PWDCB  GET TIOT OFFSET        93051
         AR    R3,R1         GET TIOT ENTRY ADDRESS              93051
         USING TIOELNGH,R3   DECLARE ENTRY
         TM    TIOELINK,TIOESYOT SPOOLED (SYSOUT) DATASET ?      81169
         BZ    RECURSE       NO; JUST EXIT
         DROP  R3
         XC    PRTSAVE(56),PRTSAVE  CLEAR LIST                   82052
         MVC   PRTSAVE(4),PWDCB@  SET DCB ADDRESS               GP08085
         MVI   PRTSAVE+10,X'01'   SET SPIN DISP=E               GP08085
         MVI   PRTSAVE+21,56      SET PARM LENGH                GP08085
         SLR   R1,R1         CLEAR FOR ICM                       82052
         ICM   R1,2,CALLAD+3   GET USER'S COPIES OPTION          82052
         BNZ   *+8           NON-ZERO                            82052
         LA    R1,256        SET FOR ONE COPY                    82052
         LA    R1,1(,R1)     STARTING WITH COPY 1                82052
         STCM  R1,3,PRTSAVE+18  SET COPIES                       82052
         LA    R1,PRTSAVE    POINT TO PARAMETER LIST             82052
         SVC   81            ISSUE SETPRT                        82052
.NOSPIN  B     RECURSE       AND EXIT                            81166
         SPACE 2                                                 83275
         LTORG ,                                                 83275
         EJECT ,
*        PLIST IS ENTERED WITH R1 POINTING TO A CHAIN OF FD/FDPRT
*              REQUESTS. OUTPUT, WITH POSSIBLE EXCEPTION OF WIDTH,
*              IS AS DOCUMENTED FOR THE GOSSIP FORMATTING ROUTINE.
*                                                                82102
PLIST    BALR  R11,0         MAKE LOCAL BASE                     93164
         USING *,R11         MAKE LOCAL BASE                     82102
         BAL   R14,SHEETEST  TEST FOR SHEET EJECT                93164
         ICM   R5,15,CALLAD   GET CALLER'S R1                   GP99029
         BZ    EXIT16        TOO BAD
         XC    PLSTXCLR(PLSTXLEN),PLSTXCLR  CLEAR FDEXEC/FDBR CODE
         CLI   CALLTIT,0     TITLE/FOOTER REQUEST ?
         BE    PLISTNTF      NO
         BAL   R9,MAKECPRN   PRINT PREVIOUS LINE                 93164
         USING FDSECT,R5     DECLARE LIST MAPPING
PLISTNTF TM    PWFLAG,PWFPRINT   UNPRINTED DATA ?                81201
         BNZ   MAKELIST      YES; SKIP CLEARING
         BAL   R9,MAKEPNEW   SET FOR NEW LINE PROCESSING         93164
MAKELIST CLI   FDLINK,0      DUMMY ENTRY ?
         BNE   MAKELISN      NO; CONTINUE
         ICM   R0,15,PLSTEXEC  IN EXEC ?                        GP99029
         BNZ   MAKELXEQ      YES; POP THIS REQUEST
MAKELEXT LA    R9,RECURSE    SET RETURN POINT
         CLI   CALLTIT,0     TITLE PROCESSING ?
         BER   R9            NO; LEAVE STACKED LINE
         SPACE 1
MAKECPRT CLI   CALLTIT,0     TITLE PROCESSING ?
         BNE   PTITLE        YES; QUIT THIS CALL
MAKECPRN TM    PWFLAG,PWFPRINT  DATA UNPRINTED ?                 81201
         BZ    MAKEPNEW      NO; RETURN
MAKEPRNT BAL   R14,PUTTRUNC   PRINT THE LINE                     93164
         TM    PWFLAG,PWFPUN   PUNCH FILE ?                      81201
         BNZ   MAKEPNEW      YES; SKIP
         CLI   PWULOP+4,C'+'   OVERPRINT REQUEST ?
         BNE   MAKEPNEW      NO
         MVC   PWRECCC(L'PWREC+1),PWULOP+4  COPY OVER            81193
         BAL   R14,PUTTRUNC  PRINT IT                            93164
         MVI   PWULOP+4,C' '   RESET
         MVC   PWULOP+5(L'PWREC),PWULOP+4  CLEAR IT
MAKEPNEW MVI   PWRECCC,C' '
         MVC   PWREC,PWREC-1   CLEAR THE PRINT LINE
         NI    PWFLAG,255-PWFPRINT  RESET PRINT FLAG             81201
         NI    PWLSTEDT,255-FDFPADL-FDFNL   RESET NL AND LEFT PAD
         L     R14,PLSTINDT  GET AUTOMATIC INDENTATION           81271
         STH   R14,PWSCRCUR  SET FOR NEXT OFFSET                 81271
         LA    R0,PWREC(R14)  GET LINE ADDRESS                   81271
         ST    R0,PWBUFNXT   SET NEXT BYTE ADDRESS               81201
         LA    R0,L'PWREC+1+4  GET LENGTH OF V-FORMAT RECORD
         SLL   R0,16
         ST    R0,PWVCON     SET FOR MAXIMUM
         BR    R9            RETURN TO CALLER
         SPACE 1
MAKELISN TM    FDTYPE,FDFNOP  NULL ENTRY ?
         BZ    MAKELISP      NO; PROCEED
MAKELINC CLM   R5,15,PLSTEXEN  END OF EXEC RANGE ?              GP99029
         BE    MAKELXEQ      YES; POP
         CLI   CALLFUN,CFITEM  CALLED TO PROCESS SINGLE ITEM?   GP99029
         BE    MAKELEXT      YES; GET OUT                       GP99029
         SLR   R0,R0
         ICM   R0,1,FDLINK   GET LENGTH
         BZ    MAKELEXT      QUIT IF END
         AR    R5,R0         ELSE BUMP LIST ENTRY
         B     MAKELIST      AND CHECK IT
MAKELXEQ L     R5,PLSTEXEC   GET PRIOR FD LIST ADDRESS BACK
         XC    PLSTEXEC(8),PLSTEXEC  CLEAR STACKED REQUEST
         B     MAKELINC
MAKELISP LA    R2,FDDATA-2   SET FOR SHORT (FDPRT) REQUEST
         TM    FDTYPE,FDFPRT  IS IS SHORT REQUEST ?
         BO    MAKELISR      YES
         TM    FDTYPE,FDFOPT  OPTION ENTRY ?
         BNZ   MAKEOPTS      YES; PROCESS
         TM    FDTYPE,FDFGOTO  BRANCH ?
         BNZ   MAKELGOT      YES; PROCESS
         LA    R2,FDDATA     POINT TO LONG DATA FIELD
MAKELISR IC    R0,PWLSTEDT   SAVE PREVIOUS OPTION                81201
         DROP  R5                                               GP99029
         USING FDDATA,R2     SET FOR FD/FDPRT COMPATIBILITY
         XC    PWLSTWRK,PWLSTWRK  CLEAR WORK SPACE               81201
         STC   R0,DB         SAVE PRIOR EDIT OPTION
         MVC   PWLSTLEN+3(1),FDOLEN  COPY PROV. OUTPUT LENGTH    81201
         MVC   PWLSTLTX+3(1),FDILEN    AND INPUT LENGTH          81201
         TM    FDTYPE-FDSECT(R5),FDFREGLN IS ILEN A REGISTER ?   81271
         BZ    MAKELIRF      NO                                  81271
         CLI   FDILEN,13     VALID REGISTER ?                    81271
         BNL   MAKELEXT      NO; SKIP                            81271
         L     R15,PWLSTLTX  GET REGISTER NUMBER                 81271
         SLL   R15,2         MAKE INTO WORD OFFSET               81271
         L     R15,SAVE0-SAVE(R10,R15)  GET ORIGINAL REGISTER CONTENTS
         STC   R15,PWLSTLTX+3   SAVE LOW BYTE AS NEW LENGTH      81271
MAKELIRF MVC   PWLSTEDT,FDEDIT-FDSECT(R5) COPY EDIT OPTION       81271
         TM    DB,FDFPADR    PRIOR RIGHT PAD ?
         BZ    *+8           NO
         OI    PWLSTEDT,FDFPADL  PROPAGATE AS CURRENT LEFT PAD   81201
         MVC   PWLSTDAT,FDDATA     AND DATA TYPE                 81201
         NI    PWLSTDAT,255-FDDLIT  REMOVE LITERAL FLAG BIT      81201
         MVI   PLSTULOP,0    CLEAR UNDERLINE/OVERPRINT BYTE
         TM    FDTYPE-FDSECT(R5),FDFPRT FDPRT REQUEST ?         GP99029
         BO    MAKELISD      YES; SKIP FDOPT ACCESS
         MVC   PWCUROPT(1),FDOPT7-FDSECT(R5) BASIC FIELD ATTR    81201
         TM    FDOPT7-FDSECT(R5),FDFDFLT+FDFPREV OPTION OVERRIDE ?
         BZ    MAKELISU      NO
         LA    R1,=AL1(FDFSKIP)  DEFAULT OPTION
         TM    FDOPT7-FDSECT(R5),FDFDFLT DEFAULT ?              GP99029
         BNZ   MAKELISC      YES; OVERRIDE
         LA    R1,PWPREOPT   USE PREVIOUS FIELD'S OPTIONS        81201
MAKELISC MVC   PWCUROPT,0(R1)  REPLACE OPTIONS                   81201
MAKELISU LA    R15,PLFUND    SET UNDERLINE
         TM    FDOPT9-FDSECT(R5),FDFUNDER UNDERLINE ?           GP99029
         BNZ   MAKELISI      YES
         LA    R15,PLFINT    SET INTENSE
         TM    FDOPT9-FDSECT(R5),FDFREV+FDFBLINK SPECIAL ?      GP99029
         BNZ   MAKELISI      YES
         TM    FDOPT7-FDSECT(R5),FDFNDISP NON-DISPLAY ?         GP99029
         BNM   MAKELISD      OR NONE
         TM    FDOPT7-FDSECT(R5),FDFINT INTENSE ?               GP99029
         BZ    MAKELISD      NO
MAKELISI TM    PWFLAG,PWFPUN   PUNCH FILE ?                      81201
         BNZ   MAKELISD      YES; SKIP
         TM    DCBRECFM-IHADCB+PWDCB,DCBRECCC  CONTROL ?
         BZ    MAKELISD      NO; SKIP
         STC   R15,PLSTULOP  SET UNDERLINE/OVERPRINT FLAG
MAKELISD NI    PWCUROPT,X'3C'  MASK                              81201
         LA    R6,FDTEXT     ADDRESS FOR LITERAL INPUT
         TM    FDTYPE-FDSECT(R5),FDFIN INPUT FD ENTRY ?         GP99029
         BNZ   MAKELISA      YES; LITERALS NOT ALLOWED
         TM    FDDATA,FDDLIT  WAS LITERAL USED ?
         BNZ   MAKELISB      YES
MAKELISA LA    R6,FDSADD     POINT TO S ADDRESS
         BAL   R14,MAKESADD  GET VALUE                           93164
         L     R6,DB         LOAD ALL FOUR BYTES OF ADDRESS WORD 82025
         TM    FDTYPE-FDSECT(R5),FDFINDAD INDIRECT ADDRESS ?     81271
         BZ    MAKELISB      NO                                  81271
         N     R6,=X'FFFFFFFC'   TRUNCATE ADDRESS TO WORD BOUNDARY
         ICM   R6,15,0(R6)   LOAD NEW ADDRESS FROM OLD ONE       82025
MAKELISB ST    R6,PWLSTADD   SET PROV. INPUT ADDRESS             81201
         TM    FDTYPE-FDSECT(R5),FDFIN INPUT ENTRY ?            GP99029
         BZ    MAKELISO      NO; PROCESS OUTPUT
         DROP  R2            DROP MAPPINGS                      GP99029
*87260*  CLI   PWLSTDAT,FDDASIS  CHARACTER FIELD ALREADY ?       81201
*87260*  BNH   *+8           YES
*87260*  MVI   PWLSTDAT,FDDCHAR  FORCE CHARACTER FIELD           81201
         LM    R0,R1,PWLSTLEN  GET TOTAL AND INPUT LENGTHS       81201
         LTR   R0,R0
         BP    *+6
         LR    R0,R1         IF OUTLEN=0, USE INPUT
         LTR   R1,R1
         BP    *+6
         LR    R1,R0         IF INLEN=0, USE OUTPUT
         LTR   R0,R0         ALL ZERO ?
         BNP   MAKELINC      YES; IGNORE ENTRY
         STM   R0,R1,PWLSTLEN  SET LENGTHS BACK                  81201
         OI    PWLSTEDT,FDFPADL+FDFPADR  SET PADDING             81201
         TM    PWCUROPT,FDFPROT  PROTECTED FIELD (USED FOR LPEN) 81201
         BZ    MAKELPRO      NO; GO TO COMMON MOVER
*        B     MAKELISO      YES; TREAT AS OUTPUT
         SPACE 1
MAKELISO TM    PWCUROPT,FDFSKIP  DID USER SPECIFY ANY ?          81201
         BNZ   *+8
         OI    PWCUROPT,FDFSKIP  DEFAULT - CURSOR SKIP FIELD     81201
MAKELPRO TM    PWLSTDAT,X'40'  EXTENDED DATA TYPE ?             GP03287
         BZ    MAKELPRX      NO                                 GP03287
         USING FDDATA,R2                                        GP03287
         LA    R6,FDTBAD                                        GP03287
         BAL   R14,MAKESADD  GET ADDRESS OF TABLE               GP03287
         ST    R6,DB         SAVE                               GP03287
         MVC   DB+4(4),FDTBAD  PASS DATA TO FORMATTER           GP03287
         DROP  R2                                               GP03287
         LA    R2,DB         PASS TO @FORMATS                   GP03287
MAKELPRX LM    R0,R1,PWLSTLTX  LOAD TEXT LENGTH/ADDRESS          81201
         SLR   R15,R15
         ICM   R15,1,PWLSTDAT  GET DATA TYPE                     81201
         CH    R15,=H'1'     CHAR (0 OR 1) TRANSLATION ?         90001
         BNH   MAKELPRP      YES; DO AFTER DE-NULLING            90001
         BCTR  R15,0         FINAGLE                            GP03287
         #CNVRT CONVERT,BM=MAKELINC  INVOKE CONVERSION @ CVC+TYPE
MAKELPRP LA    R1,0(,R1)     CLEAR HIGH BYTE OF ADDRESS          82025
MAKELPRS TM    PWCUROPT,FDFNULL  NULL DELETION ?                 81201
         BZ    MAKENULX      NO; NO NULL SUPPRESSION
         LTR   R0,R0         ANY LENGTH ?
         BNP   MAKENULX      NO; EXIT
MAKENULL CLI   0(R1),0       LEADING NULL ?
         BNE   MAKENULT      NO; CHECK FOR TRAILING NULL
         LA    R1,1(,R1)
         BCT   R0,MAKENULL
MAKENULT LTR   R0,R0         ALL NULL ?
         BNP   MAKENULX      YES
         LR    R2,R1         GET START OF STRING
         AR    R2,R0         GET LAST BYTE + 1 OF STRING
MAKENULU BCTR  R2,0          BACKSPACE A BYTE
         CLI   0(R2),0       TRAILING NULL ?
         BNE   MAKENULX      NO
         BCT   R0,MAKENULU   KEEP IT UP
MAKENULX TM    PWLSTEDT,FDFDEBL  DEBLANK FROM LEFT ?             81201
         BZ    MAKELBLF      NO
         #CNVRT DBKL         DEBLANK FROM LEFT                  GP03316
MAKELBLF TM    PWLSTEDT,FDFDEBR  DEBLANK FROM RIGHT ?            81201
         BZ    MAKELBRT
         #CNVRT DBKR         DEBLANK ON RIGHT                   GP03316
MAKELBRT TM    PWLSTEDT,FDFDEBZ  STRIP LEADING ZEROES ?          81201
         BZ    MAKELBLY                                          90001
         #CNVRT DBKZ         EDIT OUT LEADING ZEROES            GP03316
MAKELBLY CLI   PWLSTDAT,1    CHAR=0 OR CHAR=1 FUNCTION ?         90001
         BH    MAKELBLZ      NO                                  90001
         SLR   R15,R15       SET CHAR=1 (MOVE AND TRANSLATE)     90001
         #CNVRT CONVERT      INVOKE MOVE AND TRANSLATE          GP03316
         LA    R1,0(,R1)     JUST IN CASE                        90001
MAKELBLZ LH    R15,PWWIDTH   SET MAXIMUM INPUT LENGTH
         CR    R0,R15
         BNH   *+6
         LR    R0,R15        TRUNCATE
         STM   R0,R1,PWLSTLTX  STASH BACK                        81201
         ICM   R1,15,PWLSTLEN  GET AND TEST FIELD LENGTH         81201
         BNZ   *+8           OK IF NON-ZERO
         ST    R0,PWLSTLEN   ELSE USE INPUT LENGTH               81201
         SPACE 1
         TM    PWPREOPT,FDFPROT  PRIOR INPUT FIELD ?             81201
         BNZ   *+8           NO
         OI    PWLSTEDT,FDFPADL  FORCE A SPACE                   81201
         TM    PWCUROPT,FDFPROT  OUTPUT ?                        81201
         BNZ   *+8           YES
         OI    PWLSTEDT,FDFPADL  FORCE AN SF                     81201
         TM    PWCUROPT,FDFNDISP  DETECTABLE ?                   81201
         BNM   *+8           NO
         OI    PWLSTEDT,FDFPADL   FORCE AN SF                    81201
         CLC   PWPREOPT(1),PWCUROPT  DIFFERENT OPTIONS ?         81201
         BE    *+8           NO
         OI    PWLSTEDT,FDFPADL                                  81201
         LH    R0,PWWIDTH    GET LINE SIZE
         BCTR  R0,0          ALLOW FOR RELATIVITY
         SH    R0,PWSCRCUR   GET RESIDUAL BYTES ON LINE-1        81201
         TM    PWLSTEDT,FDFPADL     SF TO BE EXPANDED ?          81201
         BNZ   *+8           YES; LEAVE SHORT
         AH    R0,=H'1'      ELSE UP BY ONE
         C     R0,PWLSTLEN   COMPARE RESIDUAL TO FIELD LENGTH    81201
         BNL   *+8           DATA WILL FIT ON CURRENT LINE
         OI    PWLSTEDT,FDFNL   ELSE FORCE ON NEW LINE           81201
         CLC   PWSCRCUR,PLSTINDT+2  ALREADY ON NEW LINE ?       GP04070
         BH    *+8           NO                                  81271
         NI    PWLSTEDT,255-FDFNL-FDFPADL   RESET NEW LINE REQUEST
MAKELNPB TM    PWLSTEDT,FDFNL  NEW LINE REQUIRED ?               81201
         BZ    MAKELNPU      NO; SEE IF SF REQUIRED
         LM    R0,R1,PWLSTLTX  GET TEXT LENGTH/ADDRESS           81201
         LTR   R14,R0        COPY LENGTH                         81193
         BNP   MAKELNPC      NONE; JUST GET A NEW LINE           81193
         BCTR  R14,0                                             81193
         EX    R14,MAKEMVC1  MOVE TO SAVE AREA                   81193
         LA    R1,DBSAVE     GET AREA ADDRESS                    81193
         ST    R1,PWLSTADD   SET NEW ADDRESS                     81201
MAKELNPC BAL   R9,MAKECPRT   MAKE A NEW LINE                     93164
MAKELNPU IC    R0,PWCUROPT   GET CURRENT OPTIONS                 81201
         STC   R0,PWPREOPT   SET INTO PREVIOUS OPTIONS           81201
         TM    PWLSTEDT,FDFPADL   SF REQUIRED ?                  81201
         BZ    *+8           NO
         XI    PWPREOPT,FDFPROT  SET OPTIONS DISSIMILAR          81201
         BAL   R14,MAKESFE   EXPAND SF OR SFE                    93164
         LM    R0,R2,PWLSTLEN  GET LENGTHS AND ADDRESS BACK      81201
         CR    R1,R0         TEXT GREATER THAN OUTPUT LENGTH ?
         BL    MAKEPADL      CHECK FOR PADDING
         BE    MAKEPADM      EQUAL - MOVE
         TM    PWLSTEDT,FDFRADJ  RIGHT-ADJUST ?                  81201
         BZ    MAKEPADQ      NO; JUST TRUNCATE ON RIGHT
         AR    R2,R1         ADD FIELD LENGTH
         SR    R2,R0         SET SHORTER OUTPUT LENGTH
MAKEPADQ LR    R1,R0         TRUNCATE
         B     MAKEPADS      STASH BACK
MAKEPADL TM    PWLSTEDT,FDFRADJ  RIGHT-ADJUST OPTION ?           81201
         BZ    MAKEPADM      NO; MOVE.  CLEAN-UP LATER
         SR    R0,R1         MAKE FILL LENGTH
         BNP   MAKEPRAB      ERROR ?
         AH    R0,PWSCRCUR   ADD SCREEN POSITION                 81201
         BAL   R14,MAKESRA   SKIP THE HARD WAY                   93164
MAKEPRAB LM    R1,R2,PWLSTLTX  GET LENGTH/ADDRESS BACK           81201
         LR    R0,R1         NEW TOTAL LENGTH
MAKEPADS STM   R0,R2,PWLSTLEN  SAVE ADJUSTED VALUES              81201
MAKEPADM BAL   R14,MAKETEXT  MOVE TEXT                           93164
         LM    R0,R1,PWLSTLEN  GET LENGTHS BACK                  81201
         SR    R0,R1         RESIDUAL LENGTH TO TACK ON
         BNP   MAKEPADP      NONE; CHECK FOR TRAILING PAD
         AH    R0,PWSCRCUR   MAKE NEW SCREEN POSITION            81201
         BAL   R14,MAKEEUA   MAKE NULLS TO NEXT FIELD            93164
MAKEPADP B     MAKELINC      GET NEXT FIELD
         SPACE 1
         USING FDSECT,R5
MAKEOPTS TM    FDEDIT-FDSECT(R5),FDFNL NEW LINE REQUEST ?       GP99029
         BZ    MAKEOPTL      NO
         BAL   R9,MAKECPRN   YES; DO CONDITIONAL PRINT           93164
MAKEOPTL TM    FDEDIT-FDSECT(R5),FDOSBAP ADDRESS PRESENT ?       81159
         BZ    MAKEOPCC      NO; CHECK FOR CARRIAGE CONTROL      81201
         CLI   FDOSBA,X'FE'  TWO-BYTE FORMAT ?                   81159
         BL    MAKEOPCC      NO, OR UNSUPPORTED                  81201
         BE    *+8           REFERENCE TO CURRENT LINE           81159
         BAL   R9,MAKECPRN   REFERENCE TO NEXT LINE; PRINT PRIOR 93164
         LH    R1,PWSCRCUR   GET CURRENT LINE POSITION           81201
         SLR   R0,R0                                             81159
         IC    R0,FDOSBA+1   GET RELATIVE OFFSET                 81159
         LH    R15,PWWIDTH   GET DATA WIDTH                      81159
         CR    R0,R15        IF LESS THAN WIDTH, TAB REQUEST     81159
         BL    MAKEOIND      JUST SET NEW OFFSET                 81159
         SH    R0,=H'253'    FINAGLE                             81159
         BM    MAKEOUND      BACKSPACE (UNDENT) OPTION           81159
         SH    R0,=H'1'      + OR - 1 FROM CURRENT POSITION      81159
         AR    R1,R0         ADD/SUBTRACT FROM CURRENT POSITION  81159
         BM    MAKEOPCC      SKIP IF MINUS                       81201
         CR    R1,R15        IN THIS LINE                        81159
         BNL   MAKEOPCC      DITTO IF TOO HIGH                   81201
         B     MAKEOSET      ELSE USE NEW POSITION               81159
MAKEOUND AR    R0,R15        UNDENT FROM END OF LINE             81159
MAKEOIND LR    R1,R0         INDENT                              81159
MAKEOSET STH   R1,PWSCRCUR   SET NEW OFFSET                      81201
         LA    R1,PWREC(R1)  SET NEW OUTPUT ADDRESS              81159
         ST    R1,PWBUFNXT                                       81201
         NI    PWLSTEDT,255-FDFPADL-FDFPADR  RESET PRIOR PADS    81271
MAKEOPCC TM    FDEDIT-FDSECT(R5),FDOPCCP CARR. CONTROL PRESENT ? 81201
         BZ    MAKEOPIN      NO; CHECK FOR INDENT                81271
         OI    PWFLAG,PWFPRINT   SHOW DATA IN BUFFER             81201
         MVC   PWRECCC,FDOCC  MOVE USER'S CODE UNCHECKED         81201
         B     MAKELINC      GET NEXT ENTRY                      81159
MAKEOPIN TM    FDEDIT-FDSECT(R5),FDOINDP INDENT PRESENT ?        81271
         BZ    MAKELINC      NO; DO ANOTHER                      81271
         SLR   R0,R0                                             81271
         IC    R0,FDOIND     LOAD INDENT VALUE                   81271
         CH    R0,PWWIDTH    REASONABLE ?                        81271
         BNL   MAKELINC      NO; IGNORE                          81271
         ST    R0,PLSTINDT   YES; SAVE IT                        81271
         B     MAKELINC      GET NEXT FD                         81271
         SPACE 1
MAKELGOT LA    R6,FDGOTO     POINT TO S ADDRESS
         IC    R0,FDTYPE-FDSECT(R5) GET TYPE OPERAND             81193
         BIX   ERR=MAKELINC,PFX=MAKEL,                                 *
               LOC=(GOG,GEX,GBR,GTM,GCI,GCL,INC,INC,INC,INC,INC,INC,INC*
               ,SNP,SPC,UEX)                                     83331
         SPACE 1
MAKELGCL LA    R6,FDBVAR     GET COMPARE VARIABLE
         BAL   R14,MAKESADD  GET ADDRESS                         93164
         LR    R1,R6         SAVE ADDRESS
         LA    R6,FDBCLC     GET COMPARISON STRING
         BAL   R14,MAKESADD  GET ITS ADDRESS                     93164
         SLR   R4,R4
         ICM   R4,1,FDBLEN   GET COMPARE LENGTH
         BZ    MAKELINC      SKIP IF NONE
         BCTR  R4,0
         EX    R4,MAKELGXL   COMPARE
         B     MAKELGOI      GO TO COMMON
         SPACE 1
MAKELGCI LA    R6,FDBVAR     GET VARIABLE
         BAL   R14,MAKESADD  GET ADDRESS                         93164
         IC    R4,FDBIDA     GET COMPARE BYTE
         EX    R4,MAKELGXI   COMPARE
         B     MAKELGOI      GO TO COMMON
MAKELGXL CLC   0(0,R1),0(R6)   REMOTE CLC
MAKELGXI CLI   0(R6),0         REMOTE CLI
MAKELGXT TM    0(R6),0         REMOTE TM
         SPACE 1
MAKELGTM LA    R6,FDBVAR     GET COMPARISON VARIABLE
         BAL   R14,MAKESADD  GET ADDRESS                         93164
         IC    R4,FDBIDA     GET IMMEDIATE DATA
         EX    R4,MAKELGXT   TEST UNDER MASK
MAKELGOI GETCC R3            GET CONDITION CODE                  93168
         SRL   R3,28           IN LOW BYTE
         LA    R15,3         MAKE MASK
         NR    R3,R15          ISOLATE IT
         LA    R15,=AL1(0,2,4,4)  GET COND CODE/ BRANCH OFFSET
         AR    R3,R15        GET ADDRESS
         MVC   PLSTCC,0(R3)  SAVE CODE
         LA    R6,FDBRE      GET START ADDRESS
         B     MAKELGBR      GO TO COMMON BRANCH CODE
         SPACE 1
MAKELGEX ICM   R0,15,PLSTEXEC  ALREADY IN EXEC ?                GP99029
         BNZ   MAKELINC      YES; TREAT AS NOP
         LA    R6,FDBRE      GET FIRST FD ADDRESS
         BAL   R14,MAKETADD  SEE IF VALID                        93164
         TM    FDBRL,X'F0'   GET SECOND ADDRESS
         BZ    MAKELGEY        ONLY ONE - TREAT START AS END
         LA    R6,FDBRL      ELSE GET END
         BAL   R14,MAKESADD  RELOCATE IT                         93164
MAKELGEY STM   R5,R6,PLSTEXEC  SET EXEC RESTART AND END ADDRESSES
         LA    R6,FDBRE      GET START ADDRESS
         B     MAKELGOG        GO TO COMMON
         SPACE 1
MAKELGBR SLR   R3,R3
         IC    R3,PLSTCC     GET PRIOR CODE
         AR    R6,R3         POINT TO BRANCH ADDRESS
MAKELGOG BAL   R14,MAKETADD  GET ADDRESS VALUE                   93164
         LR    R5,R6         COPY OVER
         B     MAKELIST      CONTINUE PROCESSING
         SPACE 1                                                 83331
*        PRINT A SNAP DUMP : PARMS ARE FLAGS/ADDR/LEN BASE     83331
*                                                                83331
MAKELSNP BAL   R9,MAKECPRT   START A NEW LINE                    93164
         XC    SNPFLAGS(2),SNPFLAGS  ONLY TWO SUPPLIED IN LIST   83331
         MVC   SNPFLAG3(2),FDBRE  COPY FLAGS                     83331
         LA    R6,FDBRL      POINT TO S-FORM ADDRESS             83331
         BAL   R14,MAKESADD  CONVERT                             93164
         ST    R6,SNPADDR                                        83331
         SLR   R6,R6                                             83331
         ICM   R6,3,FDBRH    GET HALF-WORD LENGTH                83331
         BZ    MAKELINC      IGNORE NULL LINE                    83331
         TM    SNPFLAG3,SNFSLEN  S-FORMAT LENGTH FIELD ?         83331
         BZ    MAKELSNQ      NO                                  83331
         LA    R6,FDBRH      POINT TO LENGTH                     83331
         BAL   R14,MAKESADD  CONVERT AS ADDRESS                  93164
         LTR   R6,R6         ANY LENGTH ?                        83331
         BNP   MAKELINC      NOT POSITIVE - IGNORE               83331
MAKELSNQ ST    R6,SNPLEN     PLACE INTO LIST                     83331
         TM    SNPFLAG4,SNFUBASE  BASE ADDRESS SUPPLIED ?        83331
         BZ    MAKELSNR      NO                                  83331
         LA    R6,FDBVAR     POINT TO S-FORM                     83331
         BAL   R14,MAKESADD  GET ADDRESS                         93164
         ST    R6,SNPBASE    PASS IT                             83331
MAKELSNR LR    R7,R11        PRESERVE OUR BASE                   83331
         LA    R11,PSNAP+2   SET SNAP'S BASE                     83331
         BAL   R9,PSNAPITS-PSNAP-2(,R11)   PROCESS THE REQUEST   93164
         LR    R11,R7        RESTORE OUR BASE                    83331
         BAL   R9,MAKECPRT   START WITH A CLEAN LINE             93164
         B     MAKELINC      DO NEXT FD                          83331
         SPACE 1                                                 82109
*        FDROOM (0,N) / FDSPACE (N,0) / FDREPT (N,CHAR)          82109
*                                                                82109
MAKELSPC CLI   0(R6),0       SPACE REQUEST ?                     82109
         BH    MAKELSPP      YES                                 82109
         TM    PWFLAG,PWFPRINT  ANY DATA IN CURRENT LINE ?       82109
         BZ    MAKELINC      NO; IGNORE ROOM REQUEST             82109
         SLR   R0,R0                                             82109
         IC    R0,1(,R6)     GET SPACE REQUESTED                 82109
         AH    R0,PWSCRCUR   ADD CURRENT OFFSET                  82109
         CH    R0,PWWIDTH    WILL IT FIT IN CURRENT LINE ?       82109
         BNH   MAKELINC      YES                                 82109
         BAL   R9,MAKECPRT   ELSE FORCE A NEW LINE               93164
         B     MAKELINC                                          82109
MAKELSPP L     R2,PWBUFNXT   GET NEXT BUFFER POSITION            82109
         LH    R0,PWWIDTH    GET LINE WIDTH                      82109
         SH    R0,PWSCRCUR   ROOM ON THIS LINE                   82109
         BNP   MAKELINC      NONE - IGNORE THE REQUEST           82109
         MVI   0(R2),C' '    PROVISIONALLY BLANK                 82109
         CLI   1(R6),C' '    BLANK REQUEST ?                     82109
         BNH   *+10          YES                                 82109
         MVC   0(1,R2),1(R6)  NO; MOVE USER'S BYTE               82109
         SLR   R1,R1                                             82109
         IC    R1,0(,R6)     GET REPEAT COUNT                    82109
         LA    R15,1(,R2)    SET TO ADDRESS FOR EXECUTED MVC     82109
         CR    R1,R0         ROOM ON THIS LINE ?                 82109
         BNH   *+6           YES                                 82109
         LR    R1,R0         ELSE TRUNCATE THE REQUEST           82109
         LA    R14,0(R2,R1)  NEW OUTPUT LOCATION                 82109
         ST    R14,PWBUFNXT                                      82109
         LH    R14,PWSCRCUR  GET CURRENT OFFSET                  82109
         AR    R14,R1        NEW OFFSET                          82109
         STH   R14,PWSCRCUR                                      82109
         OI    PWFLAG,PWFPRINT  SET BUFFER NON-EMPTY             82109
         SH    R1,=H'2'      -1 FOR BYTE MOVED; -1 FOR EXECUTE   82109
         BM    MAKELINC      ONE BYTE ONLY                       82109
         EX    R1,MAKEMVC    SLIDING MOVE                        82109
         B     MAKELINC      DO MORE                             82109
         SPACE 1                                                 81193
MAKELUEX BAL   R14,MAKETADD  CHECK AND GET EXIT ADDRESS          93164
         LR    R14,R5        COPY CURRENT FD ADDRESS             81193
         LA    R1,PWVCON     PRINT RECORD                        81193
         BAL   R3,TAKEX      SCHEDULE USER EXIT                  93164
         LR    R3,R1         SAVE POTENTIAL NEW FD ADDRESS       81193
         LTR   R4,R15        SAVE AND TEST RETURN CODE           81193
         BZ    MAKELINC      ZERO; GET NEXT FD ENTRY             81193
         STC   R4,DB         SET FOR TM TESTS                    81193
         TM    DB,8          PRINT OR NEW LENGTH REQUEST ?       81193
         BZ    MAKEUXNL      NO                                  81193
         LA    R0,L'PWREC    GET RECORD LENGTH                   81193
         LA    R1,PWREC      GET RECORD ADDRESS                  81193
         #CNVRT DBKR         DEBLANK RIGHT                      GP03316
         LTR   R1,R0         ANY LENGTH ?                        81193
         BZ    MAKEUXRS      NO; RESET LINE FLAGS                81193
         STH   R0,PWSCRCUR   SET CURRENT USE                     81201
         LA    R1,PWREC(R1)  SET NEXT OFFSET                     81193
         ST    R1,PWBUFNXT                                       81201
         OI    PWFLAG,PWFPRINT  SHOW DATA PRESENT                81201
MAKEUXNL TM    DB,12         NEW LENGTH ONLY ?                   81193
         BNM   MAKEUXQT      YES OR NONE; CHECK FOR QUIT         81193
         TM    DB,4          RESET ?                             81193
         BNZ   MAKEUXRS      YES                                 81193
         BAL   R9,MAKECPRT   PRINT PRIOR LINE, IF ANY            93164
MAKEUXRS BAL   R9,MAKEPNEW   INITIALIZE FOR NEW LINE             93164
MAKEUXQT STC   R4,DB         STASH FLAG AGAIN                    81193
         TM    DB,16         TERMINATE ?                         81193
         BNZ   MAKELEXT      YES                                 81193
         TM    DB,2          NEW FD ?                            81193
         BZ    MAKELINC      NO; DO NEXT FD                      81193
         LR    R5,R3         SET NEW FD ADDRESS                  81193
         B     MAKELIST      CHECK IT                            81193
         SPACE 1
MAKETADD TM    0(R6),X'F0'   VALID ADDRESS ?
         BZ    MAKELINC        DEFINITELY NOT - SKIP
MAKESADD CLI   0(R6),X'DF'   VALID REGISTER ?
         BH    MAKELEXT      NO; TAKE ERROR EXIT
         ST    R1,DB         SAVE USER'S VALUE
         ICM   R6,3,0(R6)    GET S FORM OF ADDRESS
         LR    R1,R6         COPY
         N     R6,=X'00000FFF'  GET DISPLACEMENT
         SRL   R1,10
         N     R1,=X'0000003C'  GET BASE *4
         BZ    MAKESADT      DON'T ADD R0 FOR ABSOLUTE ADDRESS
         CH    R1,=Y(13*4)
         BL    MAKESADR
         ALR   R6,R10        USE USER STACK VALUE
         B     MAKESADT
MAKESADR AL    R6,SAVE0-SAVE(R1,R10)  GET USER'S VALUE           81271
MAKESADT L     R1,DB         RESTORE USER'S REGISTER             82025
         ST    R6,DB         SAVE ADDRESS WITH HIGH BYTE         82025
         LA    R6,0(,R6)     KILL HIGH BYTE                      82025
         BR    R14           RETURN TO CALLER
         SPACE 1
MAKEEUA  DS    0H            EUA ORDER ON A PRINTER ?
MAKESRA  DS    0H            REPEAT TO ADDRESS - FORCED BLANKS
MAKESBA  L     R15,PWBUFNXT  SET BUFFER ADDRESS                  81201
         CH    R0,PWSCRCUR   ALREADY POSITIONED ?                81201
         BER   R14           YES; SKIP
         STH   R0,PWSCRCUR   SET CURRENT SCREEN ADDRESS          81201
         CH    R0,PWWIDTH    FITS ON SCREEN ?
         BNLR  R14           NO; RETURN
         LA    R15,PWREC     SET FIRST BYTE
         AR    R15,R0        UPDATE ADDRESS
MAKESOFF ST    R15,PWBUFNXT  AND SET IT                          81201
         BR    R14           RETURN
         SPACE 1
MAKESFE  IC    R0,PWCUROPT   DUPLICATE HIGH IN LOW BYTE FOR SF   81201
MAKESF   CLM   R0,1,PWPREOPT  ANY CHANGE IN MAIN OPTION ?        81201
         STC   R0,PWPREOPT   SET NEW FIELD OPTION                81201
         BNE   MAKESF7       YES; EXPAND AN SF
         TM    PWPREOPT,FDFPROT  PROTECTED FIELD ?               81201
         BZ    MAKESF7       NO; MUST EXPAND SF
         TM    PWPREOPT,FDFNDISP  DETECTABLE ?                   81201
         BNMR  R14           NO; RETURN WITHOUT SF
MAKESF7  L     R15,PWBUFNXT  GET OUTPUT BUFFER                   81201
         LA    R15,1(,R15)   SET NEW BUFFER ADDRESS
         LH    R0,PWSCRCUR   GET CURRENT SCREEN ADDRESS          81201
         AH    R0,=H'1'      UP BY ONE
         CH    R0,PWWIDTH    WILL IT FIT ?
         BNLR  R14           NO; JUST RETURN
MAKESOF0 STH   R0,PWSCRCUR   SET NEW SCREEN ADDRESS              81201
         B     MAKESOFF      GO TO MAKE NEW BUFFER ADDRESS
         SPACE 1
MAKETEXT L     R15,PWBUFNXT  GET OUTPUT BUFFER                   81201
         SH    R1,=H'1'      MAKE LENGTH FOR EXECUTE
         BMR   R14           SKIP IF MINUS
         CH    R1,MAKETEXH+2  SEE IF TOO LONG
         BNH   *+8           NO
MAKETEXH LA    R1,L'PWREC    SET FOR MAX SUPPORTED
         LH    R0,PWSCRCUR   GET CURRENT SCREEN POSITION         81201
         CH    R0,PWWIDTH    WILL IT FIT ?
         BNL   MAKESOF0      NO; RETURN WITH ERROR
         OI    PWFLAG,PWFPRINT  SET TEXT IN BUFFER               81201
         AR    R0,R1         MAKE NEW ADDRESS
         CH    R0,PWWIDTH    WOULD IT FIT ?
         BH    MAKESOF0      NO; RETURN ERROR
         EX    R1,MAKEMVC    MOVE
         CLI   PLSTULOP,PLFINT  UNDERLINE/OVERPRINT ?
         BL    MAKETEXN      NO
         BH    MAKETEXU      UNDERLINE
         EX    R1,MAKEMVC2   MAKE SECOND COPY
         B     MAKETEXX      AND SET FLAG
MAKETEXU MVI   PWULOP-PWVCON(R15),C'_'    MAKE FIRST UNDERLINE
         SH    R1,=H'1'      ANY MORE ?
         BNP   MAKETEXV      NO
         EX    R1,MAKEMVC3   PROPAGATE
MAKETEXV LA    R1,1(,R1)     RESTORE LENGTH-1
MAKETEXX MVI   PWULOP+4,C'+'  SET OVERPRINT FLAG
MAKETEXN LA    R1,1(,R1)     MAKE LENGTH AGAIN
         LH    R0,PWSCRCUR   GET SCREEN OFFSET                   81201
         AR    R0,R1         MAKE NEW OFFSET
         AR    R15,R1         AND NEW BUFFER
         B     MAKESOF0      RESET
         DROP  R5
         SPACE 1
MAKEMVC  MVC   0(0,R15),0(R2)  MOVE TEXT
MAKEMVC1 MVC   DBSAVE(0),0(R1)  SAVE DATA OVER NEW LINE CALL     81193
MAKEMVC2 MVC   PWULOP-PWVCON(0,R15),0(R2)  MOVE SECOND COPY OF TEXT
MAKEMVC3 MVC   PWULOP-PWVCON+1(0,R15),PWULOP-PWVCON(R15) UNDERLINE
         SPACE 1                                                 82102
         LTORG ,             IT'S ABOUT TIME FOR ANOTHER         82102
         TITLE '@ P R I N T E R  ***  BIG LETTERS AND SEPARATOR PAGE'
*                                                                81159
*        USES MODIFIED VERSION OF IEFSD095 TO PRINT BIG LETTERS  81159
*                                                                81159
*        INPUT ITEM (X'80' OFF IN R1) R1 => CL8                  81159
*              WILL BE PRINTED ON SAME PAGE, LEFT-ADJUSTED       81159
*              OPTIONS MAY BE SET IN TITLE FIELD (R0 2ND BYTE)   81159
*                                                                81159
*        INPUT LIST (X'80' ON IN R1) =>  XL1 FLAG                81159
*                                  40 XEROX 9700; ALWAYS EJECT   81159
*                                  20 XEROX 4050; AUX TRAY FEED  90100
*                                  02 SKIP INITIAL EJECT(S)      81163
*                                 01 FIRST TIME, EJECT BEFORE PRINT
*                                  01 EJECT TWICE BEFORE PRINT   81159
*                            XL1 FORMAT OPTIONS                  81159
*                                  80 USE BIG LETTERS ON NARROW PAPER
*                                  20 WITH 10, CHARACTER SET SELECTOR
*                                  10 SELECTOR: 0 12*12, 10 8*8  91056
*                                      20 5*5                    91056
*                                  08 CENTER EACH LINE           81159
*                                  04 SLANT IMAGE LEFT           91056
*                                  02 SLANT IMAGE RIGHT          91056
*                                  01 PRINT NEGATIVE IMAGE       81159
*                            XL1 LENGTH OF EACH SEGMENT (1-10)   81159
*                                IF 0, DEFAULTED TO 8            81159
*                            XL1 NUMBER OF LIES TO BE PRINTED    81159
*                            CL() FIRST TEXT LINE                81159
*                            CL() TEXT OF SECOND LINE            81159
*                            ...  UP TO SPECIFIED NUMBER OF LINES
*                                                                81159
         SPACE 1                                                 *TSM*
PSEPAR   BALR  R11,0         MAKE LOCAL BASE                     93164
         USING *,R11         SET LOCAL BASE                      82102
         BAL   R9,CLEAROUT   PRINT STACKED LINE; TEST DCB OPEN   93164
           B   RECURSE       EXIT IF NOT OPEN                    81159
         BAL   R14,SHEETEST  TEST FOR SHEET EJECT                93164
         TM    PWFLAG,PWFPUN   PUNCH MODE ?                      81201
         BNZ   RECURSE       YES; NO SUPPORT AVAILABLE           81159
         USING PSELIST,R7                                        81159
         L     R7,CALLAD     GET WORK AREA ADDRESS
         N     R7,=X'7FFFFFFF'  CLEAN IT
         BZ    EXIT16        EXIT IF NONE                        81159
         ICM   R15,15,IEFSD095  BIG LETTER ROUTINE LOADED ?     GP99028
         BNZ   PSELOAD       YES                                 81159
         AIF   (NOT &MVS).SVS095                                 82102
         LOAD  EPLOC=MODSD095,ERRET=EXIT16  LOAD BIG LETTERS     81193
         AGO   .COM095                                           81193
.SVS095  LOAD  EPLOC=MODSD095   LOAD IT                          81193
.COM095  ST    R0,IEFSD095   SAVE ADDRESS                        81193
PSELOAD  LA    R14,PSEDATA   BUILD LIST FOR 095                  81159
         LA    R15,PSECOUNT                                      81159
         LA    R0,PWREC+6    OUTPUT ADDRESS                      81159
         LA    R1,PSECNTRL   CONTROL BITS                        81159
         STM   R14,R1,PSEPARM  MAKE LIST FOR 095                 81159
         MVI   PSEPARM+12,X'80'  SET END LIST BIT                81159
         SLR   R14,R14                                           81159
         BCTR  R14,0         SET LINE NUMBER TO -1               81159
         LA    R15,8                                             81159
         STM   R14,R15,PSECOUNT                                  81159
         LA    R14,MAXWIDTH+5   SET VCON LENGTH                  81159
         STH   R14,PWVCON    MAKE LENGTH                         81159
         MVI   PWRECCC,C' '  RESET CC                            81159
         MVC   PWREC,PWREC-1  CLEAR REST OF RECORD               81159
         TM    CALLAD,X'80'  SINGLE ELEMENT OPTION ?            GP03025
         BZ    PSENAME       YES; DO SINGLE NAME ONLY            81159
         SPACE 1                                                 91056
         TM    DCBRECFM-IHADCB+PWDCB,DCBRECCC  ANY CONTROL CHARS ?
         BZ    RECURSE       NO; NOT SUPPORTED AT PRESENT        81159
         BAL   R14,HOTFOOT   TERMINATE PRIOR PAGE                93164
         BAL   R9,MCFINAL    WRITE TRAILING MCC LINE             93164
         XC    SPCWORK(SPCLEAR),SPCWORK  CLEAR WORK AREA         81159
         MVC   PSLFLAG(2),PSEFLAG    INITIALIZE FLAG/FORM        81159
*NONONO  NI    PSLFORM,PSFCENT+PSFNEG  LEAVE ONLY VALID BITS     81159
         SLR   R0,R0                                             81159
         ICM   R0,1,PSENUM   GET NUMBER OF LINES                 81159
         BNZ   *+8                                               81159
         LA    R0,3          DEFAULT TO THREE                    81159
         STH   R0,PSLNUM     COPY                                81159
         ICM   R0,1,PSELEN   GET LENGTH OF EACH ENTRY            81159
         BNZ   *+8           SUPPLIED                            81159
         LA    R0,8          SET DEFAULT OF 8 BYTES              81159
         STH   R0,PSLLEN     SAVE LENGTH                         81159
         CLI   PSLNUM+1,2    EXACTLY TWO LINES ?                 81159
         BNE   PSEDFLN2      NO                                  81159
         MVI   PSLTWEEN+1,6  SET FOR 6 (REALLY 10) LINES BETWEEN 81159
         MVI   PSLLEAD+1,10  AND 10 BEFORE FIRST ONE             81159
         B     PSEDFLD2                                          81159
PSEDFLN2 MVI   PSLTWEEN+1,5  SET FOR 5 LINES BETWEEN ITEMS       81159
         CLI   PSLNUM+1,3    MORE THAN THREE ITEMS ?             81159
         BNH   PSEDFLD2      NO                                  81159
         MVI   PSLTWEEN+1,0  SET NO SEPARATION                   81159
PSEDFLD2 EQU   *             LATER CALCULATE VERTICAL CENTER ?   81159
         MVC   SPCSING(SPCLEN),ASATAB  MOVE ASA CONTROL CHARS    81131
         MVC   SPCCH12N,SPCCH12    CHANNEL 12 SKIP FOR 9700      81159
         TM    DCBRECFM-IHADCB+PWDCB,DCBRECCM   MACHINE CODE ?   81159
         BZ    PSENOTM       NO                                  81159
         MVC   SPCSING(SPCLEN),MCCTAB  YES, USE MACHINE CODE     81131
         MVC   SPCCH12N,SPCCH12   COPY CHANNEL 12 SKIP FOR 9700  81159
         OI    SPCCH12N,X'02'          WITH 'NO WRITE' BIT ON    81159
PSENOTM  LA    R10,2                   OUTPUT TWO PAGES          81159
         TM    PSEFLAG,PSF1PAGE  PRODUCE A SINGLE PAGE ONLY?    GP99120
         BZ    PSEDOPAG      NO; STANDARD ZIG-ZAP TWO PAGE      GP99120
         LA    R10,1         YES; SET FOR ONE                   GP99120
         SPACE 1                                                 81159
PSEDOPAG TM    PSLFLAG,PSFX9700  X9700 FIRST TIME ?              81159
         BZ    PSEN9700                 NO                       81131
         NI    PSLFLAG,255-PSFX9700     YES - NOW TURN OFF       81159
         MVI   PWREC,C' '                                        81159
         MVC   PWREC+1(L'PWREC-1),PWREC  CLEAR PRINT LINE        81159
         MVC   PWRECCC,SPCCH12N                                  81159
         NI    PWRECCC,255-2 *****TEST*****                      89057
         MVC   PWREC(NEWSHELT-5),NEWSHEET+5  MAKE SHEET EJECT    90309
         LA    R1,PWVCON     SET RECORD ADDRESS                  81159
         BAL   R14,PUTPUTNP                                      93164
         MVC   PWREC(6),BLANKS                                   81159
*OBS*    MVC   PWRECCC,SPCSING  PRINT ONE LINE                   89057
*OBS*    BAL   R14,PUTPUTNP  **9700 PROBLEM BYPASS**             93164
         B     PSELOOP       YES; PAGE EJECT AND PRINT           81159
PSEN9700 TM    PSLFLAG,PSFSKEJE   INHIBIT EJECTS ?               81159
         BNZ   PSENSKP       YES                                 81159
         AIF   ('&LOCAL' EQ 'CCSI').CH9  NO EXTRA EJECT DESIRED  81163
         AIF   ('&LOCAL' EQ 'IIH').DOCH9                         81163
         TM    PSLFLAG,PSFDOEJE   EXTRA (FIRST TIME) EJECT ?     81163
         BZ    PSELOOP       NO; START WITH ONE EJECT            81163
*        NOTE THAT THIS CODE PRODUCES AN 'EXTRANEOUS' EJECT AT THE
*        BEGINNING OF EACH DATASET.  THE SOLE PURPOSE IS TO ENSURE
*        THAT THE SEPARATOR STARTS ON A CLEAN PAGE AT INSTALLATIONS
*        WHICH DO NOT OTHERWISE ENFORCE THIS (VIA HASP/JES SEPARATORS
*        OR OPERATOR PRINTER ADJUSTMENT).  BLECH !               81163
         MVC   PWRECCC,SPCEJECT   SET FOR EXTRA EJECT            81163
         AGO   .PUTCH9                                           81163
.DOCH9   MVC   PWRECCC,SPCCH9     CH9 TO PREVENT HALF-PAGE DRECK 81159
.PUTCH9  LA    R1,PWVCON     SET RECORD ADDRESS                  81159
         BAL   R14,PUTPUTNP                                      93164
.CH9     SPACE 1                                                 81159
PSELOOP  MVC   PWRECCC,SPCEJECT  SET UP PAGE EJECT               81159
         LA    R1,PWVCON     SET RECORD ADDRESS                  81159
         BAL   R14,PUTPUTNP            ... AND ZOOM ....         93164
         SPACE 1                                                 81159
PSENSKP  MVC   PWRECCC,SPCSING   SINGLE SPACE                    81159
         LH    R9,PSLLEAD    GET LEADING BLANK LINE COUNT        81159
         LTR   R9,R9         ANY ?                               81159
         BNP   PSENLEAD      NO                                  81159
         TM    PSLFLAG,PSFAUXTY  AUXILIARY TRAY ?                90100
         BZ    PSENAUXT      NO                                  90100
         NI    PSLFLAG,255-PSFAUXTY  RESET IT                    90100
         MVC   PWREC(12),=12X'11'  MAKE TRAY FEED                90100
PSENAUXT LA    R1,PWVCON     GET BLANK RECORD                    90100
PSENAUXP BAL   R14,PUTPUTNP  PRINT A BLANK                       93164
         MVI   PWREC,C' '    CLEAR TRAY SELECT                   90100
         MVC   PWREC+1(L'PWREC-1),PWREC  CLEAR PRINT LINE        90100
         BCT   R9,PSENAUXP   REPEAT IT                           90100
PSENLEAD LA    R5,PSEITEM    POINT TO USER'S LIST                81159
         LH    R6,PSLNUM     LOAD NUMBER TO BE DONE              81159
PSELOOPM MVC   PSEDATA,BLANKS  CLEAR DATA ITEM                   81159
         LH    R1,PSLLEN     GET ITEM LENGTH                     81159
         CH    R1,=H'10'     EXCEEDS MAX ?                       81159
         BNH   PSENWIDE        NO                                81159
         LA    R0,PWREC      POINT TO START OF TEXT              81159
         ST    R0,PSE@VREC   USE IT FOR FULL 132 BYTE LINE       81159
         LA    R1,10         YES; TRUNCATE                       81159
PSENWIDE STC   R1,PSECNLEN   SET LENGTH                          81159
         BCTR  R1,0          SET EXECUTE LENGTH                  81159
         EX    R1,PSEMVC     MOVE USER DATA                      81159
         LA    R1,PSLFORM    POINT TO INPUT OPTION BYTE          91056
         BAL   R14,PSEPOPT   DECODE OPTIONS                      93164
         SLR   R4,R4         CLEAR                               81159
         BCTR  R4,0                                              81159
         ST    R4,PSECOUNT   SET STARTING LINE NUMBER            81159
         BAL   R9,PSEZOOM    PRINT 16 LINES                      93164
         CH    R6,=H'1'      LAST ITEM ON PAGE ?                 81159
         BNH   PSENTWEE      YES                                 81159
         LH    R9,PSLTWEEN   COUNT OF BLANK LINES BETWEEN ITEMS  81159
         LTR   R9,R9         ANY ?                               81159
         BZ    PSENTWEE      NO; SKIP                            81159
         LA    R1,PWVCON     POINT TO RECORD                     81159
         BAL   R14,PUTPUTNP  PRINT IT                            93164
         BCT   R9,*-4        REPEAT FOR ALL                      81159
PSENTWEE AH    R5,PSLLEN     SPACE TO NEXT ITEM                  81159
         BCT   R6,PSELOOPM   REPEAT FOR NEXT ITEM                81159
         BCT   R10,PSELOOP             DO TWO PAGES              81159
         B     RECURSE       RETURN TO CALLER                    81159
         SPACE 1                                                 81159
*        WRITE ONE SET OF BIG LETTERS (8 BYTES)                  81167
*        16 LINES PRINTED (2 LEADING/TRAILING BLANKS), EXCEPT    81167
*        BLANKS CLIPPED ON TOP OR BOTTOM OF PAGE                 81167
PSENAME  LA    R1,CALLTIT    POINT TO OPTION BYTE                91056
         BAL   R14,PSEPOPT   DECODE OPTIONS                      93164
         SLR   R4,R4                                             81167
         BCTR  R4,0          STARTING RELATIVE LINE IS -1        81167
         LH    R2,PWCURLN    GET CURRENT LINES LEFT ON PAGE      81167
         LTR   R2,R2                                             81167
         BNP   PSENAMJ       PAGE FULL                           81167
         CR    R2,R3         ROOM FOR 16 LINES ?                 81167
         BNL   PSENAMF       YES                                 81167
PSENAMJ  TM    CALLTIT,PSFNEG  PRINTING A NEGATIVE ?             91056
         BNZ   PSENAMP       NO; POP A PAGE                      91056
         BCTR  R3,0                                              81167
         BCTR  R3,0          SET FOR 14 LINES                    81167
         CR    R2,R3         WILL FOURTEEN FIT ?                 81167
         BNL   PSENAMF       YES; IGNORE TRAILING BLANK LINES    81167
PSENAMP  BAL   R14,HOTFOOT   FORCE FOOTERS AND EJECT             93164
         LA    R4,1          IGNORE LEADING BLANK LINES          81167
PSENAMF  MVC   PSEDATA(8),0(R7)  MOVE ITEM TO DATA LIST          81159
PSENAMLP ST    R4,PSECOUNT   SET COUNT                           81159
         LA    R1,PSEPARM    GET IEFSD095 PARM LIST              81159
         L     R15,IEFSD095                                      81159
         BASSM R14,R15       INVOKE IT                          GP99029
         LA    R14,MAXWIDTH+5   SET VCON LENGTH                  81159
         STH   R14,PWVCON    MAKE LENGTH                         81159
         LA    R1,PWVCON     GET RECORD                          81159
         BAL   R14,PUTPRINT  PRINT IT                            93164
         LA    R4,1(,R4)     UP CURRENT LINE NUMBER              81159
         BCT   R3,PSENAMLP   REPEAT FOR NEXT LINE                81159
         B     RECURSE       RETURN TO CALLER                    81159
         SPACE 1                                                 91056
*        CONVERT OPTION BITS IN ONE BYTE TO THREE BYTES WORTH    91056
PSEPOPT  MVC   PSECNTRL+1,0(R1)  MOVE OPTION BYTE TO PRIMARY OPTIONS
         PACK  PSECNTRL(1),0(1,R1)  FLIP HIGH NYBBLE AS CHAR.SET 91056
         IC    R0,0(,R1)                                         91056
         SRL   R0,1          MAKE SHIFT BITS                     91056
         STC   R0,PSECNTRL+2  MAKE SHIFT OPTION                  91056
         NI    PSECNTRL+2,3  LEAVE ONLY LEFT AND RIGHT SHIFT     91056
         NI    PSECNTRL+1,PSFCENT+PSFNEG  LEAVE ONLY CENTER AND NEGATE
         NI    PSECNTRL,3    LEAVE ONLY CHAR. SET 0-3            91056
         LA    R3,16         PRINT 16 LINES                      81167
         TM    0(R1),X'30'   SMALL CHARACTER SET ?               91056
         BM    PSEPOPTN      YES                                 91056
         TM    0(R1),X'80'   FORCE WIDE ?                        91056
         BNZ   PSEPOPTW      YES; KEEP LARGE                     86064
         CLC   PWWIDTH,=H'80'  NARROW MODE ?                     86064
         BH    PSEPOPTW      NO; WIDE                            86064
PSEPOPTN LA    R3,12         ONLY TWELVE LINES TO DO             86064
         TM    PSECNTRL,3    DID USER CHOOSE A CHARACTER SET ?   91056
         BNZ   PSEPOPTW      YES; KEEP IT                        91056
         OI    PSECNTRL,1    FORCE SMALL CHARACTER SET           86064
PSEPOPTW TM    0(R1),X'40'   IS THERE AN EXTENDED LIST ?         91056
         BZR   R14           NO; JUST RETURN                     91056
         L     R2,SAVE13-SAVE(,R13)    GET OLD SAVE AREA         91056
         L     R2,SAVE2-SAVE(,R2)                                91056
         MVC   PSEXOPTS,0(R2)  MOVE ADDITIONAL OPTIONS           91056
         OI    PSECNTRL+1,X'80'  SHOW EXTRA OPTIONS PASSED       91056
         BR    R14                                               91056
         SPACE 2                                                 81159
PSEZOOM  CLC   CANTITDT,PSEDATA   DATE REQUEST ?                 81271
         BNE   PSEZOOMY      NO                                  81159
         #CNVRT DATE         GET DATE                           GP03316
         B     PSEZOOMM      MOVE TO DATA FIELD                  81159
PSEZOOMY CLC   CANTITTM,PSEDATA   TIME REQUEST ?                 81271
         BNE   PSEZOOMT      NO                                  81159
         #CNVRT TIME         GET THE TIME                       GP03316
PSEZOOMM MVC   PSEDATA(8),0(R1)  COPY TIME OR DATA               81159
PSEZOOMT LA    R3,15         SET FOR 12 LINES OF DATA, 2 BLANKS AFTER
         L     R4,PSECOUNT   GET STARTING LINE COUNT             81159
         SR    R3,R4         FINAGLE TO GET LEADING LINES        81159
PSEZOOMD ST    R4,PSECOUNT   SET COUNT                           81159
         LA    R1,PSEPARM    GET IEFSD095 PARM LIST              81159
         L     R15,IEFSD095                                      81159
         BASSM R14,R15       INVOKE IT                          GP99029
         LA    R1,PWVCON     GET RECORD                          81159
         BAL   R14,PUTPUTNP  PRINT IT                            93164
         LA    R4,1(,R4)     UP CURRENT LINE NUMBER              81159
         BCT   R3,PSEZOOMD   REPEAT FOR NEXT LINE                81159
         BR    R9            RETURN TO CALLER                    81159
         SPACE 1                                                 81159
PSEMVC   MVC   PSEDATA(0),0(R5)  MOVER FOR BIG DATA ITEM         81159
         DROP  R7            DROP MAPPING                        81159
         SPACE 2                                                 81159
PSELIST  DSECT ,                                                 81159
PSEFLAG  DS    X             FLAG BYTE                           81159
PSFX9700 EQU   X'40'           XEROX 9700 SPECIAL EJECT (CNW)    81159
PSFAUXTY EQU   X'20'           XEROX - AUXILIARY TRAY FEED       90100
PSF1PAGE EQU   X'04'           LASER - ONLY PRINT ONE PAGE      GP99120
PSFSKEJE EQU   X'02'           SKIP EJECTS UNLESS OTHER FLAGS ON 81159
PSFDOEJE EQU   X'01'           EXTRA EJECT IF ON (FIRST TIME)    81159
PSEFORM  DS    X             FORMATTING OPTIONS                  81159
PSFCENT  EQU   X'08'           CENTER ON LINE                    81159
PSFNEG   EQU   X'01'           PRINT REVERSED BACKGROUND         81159
PSELEN   DS    X             LENGTH OF EACH ENTRY (1-10)         81159
PSENUM   DS    X             NUMBER OF LIST ENTRIES              81159
PSEITEM  DS    C             FIRST DATA ITEM                     81159
@PRINTER CSECT ,                                                 81159
         TITLE '@ P R I N T E R  ***  SNAP/STORAGE DUMP FUNCTION'
*        THIS ENTRY FORMATS STORAGE SNAPS/DUMPS                  83331
*        CALLING SEQUENCE:   R1 => PARM LIST                     83331
*        PARM LIST:                                              83331
*              XL4  - FLAG BYTES (X'80' IF NOT LAST SET)         83331
*                F1 - 80 : ANOTHER LIST ENTRY FOLLOWS           GP99120
*                F2 -        RESERVED                           GP99120
*                F3 - 80 : (FDSNAP) LENGTH IS S-FORMAT          GP99120
*                     01 : CONVERT FROM ASCII                   GP99120
*                F4 - C0 : DUMP IN TEXT FORMAT ONLY             GP99120
*                     80 : DUMP IN HEX FORMAT ONLY              GP99120
*                     40 : DUMP IN HEX AND TEXT (HORIZONTAL)    GP99120
*                     00 : DUMP IN HEX AND TEXT (VERITICALLY)   GP99120
*                     04 : LIST ENTRY HAS BASE VALUE ENTRY      GP99120
*                     02 : OMIT ABSOLUTE ADDRESS ON OUTPUT      GP99120
*                     01 : PROCESS BASE/OFFSET                  GP99120
*              AL4  - START ADDRESS (X'80' IF NOT LAST GROUP)    83331
*              AL4  - LENGTH (>=0) | X'80' END ADDRESS           83331
*                AL4 (OPTIONAL BASE FOR OFFSET CALCULATION)      83331
*                                                                83331
PSNAP    BALR  R11,0         MAKE A LOCAL BASE                   93164
         USING *,R11                                             83331
         BAL   R9,CLEAROUT   PRINT STACKED LINES; TEST DCB OPEN  93164
           B   RECURSE       DCB NOT OPEN                        83331
         BAL   R14,SHEETEST  TEST FOR SHEET EJECT                93164
         ICM   R7,15,CALLAD  GET LIST ADDRESS                   GP99029
         BZ    EXIT16        INVALID                             83331
         USING SNAPSECT,R7   MAP CALLING LIST                    83331
         BAL   R9,SNAPLANK   CLEAR THE PRINT LINE                93164
PSNAPNEW MVC   SNPFLAGS,SNAPFLAG  COPY USER'S FLAGS              83331
PSNAPSET LM    R2,R3,SNAPADDR  GET START/END ADDRESS             83331
         L     R0,=X'7FFFFFFF'  MAKE MASK                        83331
         NR    R2,R0         MASK START ADDRESS                  83331
         LTR   R3,R3         WAS END ADDRESS PASSED ?            83331
         BNM   PSNAPSEL      NO                                  83331
         NR    R3,R0         DELETE FLAG BYTE                    83331
         SR    R3,R2         CALCULATE LENGTH                    83331
         BNM   *+6           OK IF NON-NEGATIVE                  83331
         LR    R3,R2         ELSE SET END=START                  83331
         AH    R3,=H'1'      GET NUMBER OF BYTES TO DO           83331
PSNAPSEL LR    R4,R2         SET DEFAULT BASE ADDRESS            83331
         LR    R5,R7         SAVE LIST ADDRESS POINTER           83331
         TM    SNPFLAG4,SNFUBASE  PROCESS USER'S BASE ?          83331
         BZ    PSNAPNOF      NO                                  83331
         L     R4,SNAPBASE   GET USER'S BASE                     83331
         NR    R4,R0         CLEAN IT UP                         83331
         LA    R7,L'SNAPBASE(,R7)  ADJUST LIST FOR BASE          83331
PSNAPNOF STM   R2,R4,SNPADDR  SAVE ADDRESS/LENGTH/BASE           83331
         BAL   R9,PSNAPIT    PROCESS ONE SET                     93164
         LA    R7,SNAPBASE-SNAPADDR(,R7)  SKIP GROUP             83331
         TM    SNAPADDR-SNAPSECT(R5),SNFMORE  LIST OF ADDRESSES ?
*        BZ    PSNAPNLS      NO; DONE WITH THIS GROUP            83331
         BNZ   PSNAPSET      DO NEXT GROUP ENTRY                 83331
PSNAPNLS LA    R7,L'SNAPFLAG(,R7)  SKIP FLAGS                    83331
         TM    SNPFLAG1,SNFMORE  ANOTHER SET ?                   83331
         BNZ   PSNAPNEW      YES; DO IT                          83331
         B     RECURSE       NO; QUIT                            83331
         DROP  R7                                                83331
         SPACE 2                                                 83331
PSNAPIT  LTR   R6,R3         ANY LENGTH AT ALL ?                 83331
         BNPR  R9            NO; RETURN TO CALLER                83331
PSNAPITS STM   R2,R9,SNPSAVE SAVE CALLER'S REGISTERS             83331
         LM    R2,R3,SNPADDR  LOAD ADDRESS/LENGTH                83331
         L     R0,=X'7FFFFFFF'  SET MAXIMUM ?                    83331
         NR    R2,R0         MASK SIGN BIT                       83331
         LR    R6,R3         COPY LENGTH                         83331
         L     R1,CVTPTR     GET THE CVT                         83331
         L     R0,CVTMZ00-CVTMAP(,R1)  GET MAXIMUM VALID ADDRESS 83331
         LR    R5,R2         COPY STARTING ADDRESS               83331
         LR    R7,R5         COPY START                          83331
         ALR   R7,R6         END ADDRESS (+1)                    83331
         BCTR  R7,0          END ADDRESS                         83331
         CR    R7,R2         LESS THAN START ?                   83331
         BNH   PSNAPSEM      YES; SET END TO MAXIMUM             83331
         CLR   R7,R0         OVER MAXIMUM ?                      83331
         BNH   *+6           NO                                  83331
PSNAPSEM LR    R7,R0         TRUNCATE TO MAXIMUM                 83331
         LR    R3,R7         COPY END ADDRESS                    83331
         SR    R3,R2         LESS START                          83331
         LA    R3,1(,R3)     NO CHANCE OF OVERFLOW NOW (?)       83331
         ST    R3,SNPLEN     SAVE LENGTH TO BE PROCESSED         83331
         SLR   R1,R1                                             83331
         IC    R1,SNPFLAG4   GET FORMAT TYPE FLAGS               83331
         SRL   R1,6          ISOLATE                             83331
         SLL   R1,2          CONVERT TO OFFSET                   83331
         LH    R15,OPTSNAP1(R1)  LOAD LOGICAL LINE LENGTH        83331
         LH    R14,OPTSNAP1+2(R1)  LOAD PHYSICAL LENGTH          83331
         TM    SNPFLAG4,SNFOFF  ARE WE DISPLAYING OFFSETS ?      83331
         BZ    *+8           NO                                  83331
         LA    R14,11(,R14)  ALLOW FOR SIGNED OFFSET             83331
         TM    SNPFLAG4,SNFABS  SUPPRESS ABSOLUTE ADDRESS ?      83331
         BNZ   *+8           YES                                 83331
         LA    R14,10(,R14)  ELSE ALLOW FOR IT                   83331
         CH    R14,PWWIDTH   WILL IT FIT ?                       83331
         BNH   SNAPLLEN      YES                                 83331
         SRL   R15,1         ELSE HALVE                          83331
         TM    SNPFLAG4,SNFVERT  HEX TYPE OPTION ?               83331
         BM    SNAPLLEN      YES                                 83331
         LA    R2,10         MAKE DIVISOR                        83331
         LH    R15,PWWIDTH   GET WIDTH BACK                      83331
         SH    R15,OPTSNAP1+2(R1)  LESS PHYSICAL LENGTH          83331
         AH    R15,OPTSNAP1(R1)  PLUS LOGICAL = BYTES FREE       83331
         SLR   R14,R14                                           83331
         DR    R14,R2        MAKE MULTIPLE OF TEN                83331
         LTR   R15,R15       AT LEAST ONE ?                      83331
         BP    *+8           YES                                 83331
         LA    R15,1         FORCE AT LEAST ONE                  83331
         MR    R14,R2        GET WIDTH BACK                      83331
SNAPLLEN STH   R15,SNPLLEN   SET LOGICAL LINE LENGTH             83331
SNAPLOOP LR    R0,R5         GET CURRENT START ADDRESS           83331
         LA    R4,PWREC      POINT TO TEXT                       83331
         TM    SNPFLAG4,SNFABS  SUPPRESS ABSOLUTE ADDRESS ?      83331
         BNZ   SNAPNABS      YES                                 83331
         ST    R0,DB                                             83331
         UNPK  DBWORK(9),DB(5)                                   83331
         TR    DBWORK(8),TRANNUM-C'0'                            83331
         MVC   0(8,R4),DBWORK  SHOW ABSOLUTE ADDRESS             83331
         LR    R14,R4        POINT TO START                      83331
         LA    R15,2                                             83331
SNAPNALP CLI   0(R14),C'0'   LEADING ZERO ?                      83331
         BNE   SNAPNALZ      NO; QUIT                            83331
         MVI   0(R14),C' '   REPLACE BY LEADING BLANK            83331
         LA    R14,1(,R14)                                       83331
         BCT   R15,SNAPNALP  GET RID OF SOME LEADING ZEROES      83331
SNAPNALZ LA    R4,10(,R4)    SKIP OVER ADDRESS                   83331
SNAPNABS S     R0,SNPBASE      LESS STARTING ADDRESS             83331
         ST    R0,SNPOFFS    SAVE LINE OFFSET                    83331
         TM    SNPFLAG4,SNFOFF  OFFSET ?                         83331
         BZ    SNAPNOFF      NO                                  83331
         LA    R1,C' '       CLEAR SIGN SAVER                    83331
         LTR   R0,R0         NEGATIVE ?                          83331
         BNM   *+10          NO                                  83331
         LPR   R0,R0         MAKE POSITIVE                       83331
         LA    R1,C'-'       SAVE THE SIGN                       83331
         ST    R0,DB                                             83331
         UNPK  DBWORK+1(9),DB(5)  UNPACK IT                      83331
         TR    DBWORK+1(8),TRANNUM-C'0'  MAKE EBCDIC             83331
         LA    R14,DBWORK    POINT TO SIGN BYTE (WORST CASE)     83331
         LA    R15,7         NEED TO CHECK ALL BUT LAST          83331
SNAPNOLP MVI   0(R14),C' '   SET LEADING BLANK                   83331
         CLI   1(R14),C'0'   LEADING ZERO ?                      83331
         BNE   SNAPNOLZ      NO                                  83331
         LA    R14,1(,R14)                                       83331
         BCT   R15,SNAPNOLP                                      83331
SNAPNOLZ STC   R1,0(,R14)    SET LEADING SIGN                    83331
         MVC   0(9,R4),DBWORK  MOVE OFFSET                       83331
         LA    R4,11(,R4)    SET TO NEXT AVAILABLE POSITION      83331
SNAPNOFF LH    R2,SNPLLEN    GET LOGICAL SIZE OF ONE LINE        83331
         L     R6,SNPLEN     GET ADJUSTED LENGTH                 83331
         CR    R6,R2         IS IT LESS THAN MAXIMUM ?           83331
         BNL   *+6           YES                                 83331
         LR    R2,R6         ELSE COPY BYTES TO DO IN THIS LINE  83331
         SR    R6,R2         GET RESIDUAL LENGTH                 83331
         ST    R6,SNPLEN     SAVE IT                             83331
         LR    R6,R2         SET LENGTH PROCESSED                83331
         TM    SNPFLAG4,SNFVERT  CHECK FORMATTING OPTION         83331
         BO    SNAPVERT      VERTICAL FORMAT                     83331
         BZ    SNAPTEXT      PLAIN TEXT                          83331
         TM    SNPFLAG4,SNFDUAL  TEXT + HEX ?                    83331
         BZ    SNAPTHEX      NO; JUST HEX                        83331
         SPACE 1                                                 83331
SNAPDUAL LH    R2,SNPLLEN    GET LOGICAL LINE LENGTH BACK        83331
         LA    R14,3(,R2)    MAXIMUM LOGICAL BYTES ON LINE       83331
         SRL   R14,2         NUMBER OF BLANK FILLERS             83331
         LA    R3,0(R2,R14)  BLANKS PLUS CHARACTERS              83331
         LA    R3,0(R2,R3)   BLANKS PLUS HEX CHARACTERS          83331
         LA    R3,0(R3,R4)   FIRST AVAILABLE BYTE ON LINE        83331
         MVI   2(R3),C'*'    FRAME IT                            83331
         LA    R15,3(R2,R3)  END BYTE                            83331
         MVI   0(R15),C'*'   FRAME IT                            83331
         LR    R2,R6         RESTORE CURRENT LENGTH              83331
         LR    R15,R2                                            83331
         BCTR  R15,0         EXECUTE LENGTH                      83331
         LA    R3,3(,R3)     POSITION PAST FRAMING               83331
         EX    R15,EXMVC35   MOVE TEXT TO END OF LINE            83331
         ICM   R9,15,PWTRAN     OUTPUT TRANSLATION TABLE ?       91042
         BNZ   *+8           YES; USE IT                         91042
         L     R9,=A(MYTRTAB) ELSE DEFAULT TO PN                GP03074
         EX    R15,EXEBCTR   GET RID OF UNPRINTABLES             91042
         B     SNAPDUTX      GO TO COMMON HEX FORMATTING         91043
         SPACE 1                                                 83331
SNAPTHEX LR    R15,R6        COPY LENGTH                         83331
         BCTR  R15,0         LESS ONE FOR MOVE                   83331
SNAPDUTX LA    R3,DBSAVE     SET TO START OF COPIED LINE         83331
         EX    R15,EXMVC35   COPY TEXT TO WORK AREA              83331
         LR    R1,R4         POINT TO TEXT                       83331
SNAPDUHX TM    SNPFLAG3,SNFASCII  TRANSLATE FROM ASCII ?         83331
         BZ    SNAPDUHY      NO                                  83331
         EX    R15,EXASCTR   YES; TRANSLATE                      83331
SNAPDUHY LA    R0,4          SET CONSTANT                        83331
         LA    R15,8         SET ANOTHER CONSTANT                83331
         LR    R4,R0         SET NUMBER OF WORDS IN ONE GROUP    83331
SNAPHEXW LA    R14,8*16+4    SET INPUT/OUTPUT LENGTH             83331
         CR    R2,R0         LESS THAN ONE WORD ?                83331
         BNL   SNAPHEX4      NO                                  83331
         LR    R14,R2        GET LENGTH                          83331
         SLL   R14,5         GET LENGTH*2 IN HIGH NYBBLE         83331
         OR    R14,R2        GET UNPACK MASK                     83331
SNAPHEX4 EX    R14,EXUNPK    UNPACK IT                           83331
         SRL   R14,4         ISOLATE THE OUTPUT LENGTH           83331
         BCTR  R14,0         LESS ONE                            83331
         EX    R14,EXTRHEX   TRANSLATE TO PRINTABLE EBCDIC       83331
         LA    R14,1(R14,R1) GET LAST BYTE+1                     83331
         MVI   0(R14),C' '   PUT TRAILING BLANK IN               83331
         LA    R1,1(R15,R1)  SET NEXT OUTPUT ADDRESS             83331
         BCT   R4,SNAPHEXG   SKIP IF NOT END OF GROUP            83331
         LR    R4,R0         RESET GROUP COUNTER                 83331
         LA    R1,1(,R1)     EXTRA OUTPUT SPACE                  83331
SNAPHEXG AR    R3,R0         SET NEXT INPUT ADDRESS              83331
         SR    R2,R0         GET RESIDUAL LENGTH                 83331
         BP    SNAPHEXW      DO NEXT WORD                        83331
         B     SNAPPUMP      PRINT AND GET NEXT                  83331
         SPACE 1                                                 83331
EXTRHEX  TR    0(0,R1),TRANNUM   TRANSLATE TO PRINTABLE HEX      83331
EXUNPK   UNPK  0(0,R1),0(0,R3)  UNPACK ONE WORD                  83331
EXMVC35  MVC   0(0,R3),0(R5)   MOVE DATA TO PRINT LINE           83331
EXTRANZ  TR    0(0,R3),TRANZONE  TRANSLATE ZONES                 83331
EXTRANN  TR    0(0,R3),TRANNUM   TRANSLATE NUMERICS              83331
EXASCTR  TR    0(0,R3),TRASTOEB    TRANSLATE FROM ASCII (TAPE)   83331
EXEBCTR  TR    0(0,R3),0(R9)  TRANSLATE TO PRINTABLE CHARACTERS  91042
         SPACE 1                                                 83331
SNAPVERT TM    PWFLAG,PWFPUN+PWFCON  PUNCH OR CONSOLE OUTPUT ?   83331
         BNZ   SNAPTEXT      YES; SINGLE-SPACE                   83331
         MVI   PWRECCC,C'0'   DOUBLE-SPACE FIRST LINE OF THREE   83331
         LH    R0,PWCURLN    GET LINES REMAINING ON PAGE         83331
         SH    R0,=H'4'      AT LEAST FOUR ?                     83331
         BNM   SNAPTEXT      YES                                 83331
         MVI   PWRECCC,C'1'  REQUEST PAGE EJECT                  83331
SNAPTEXT BCTR  R2,0          SET FOR EXECUTE                     83331
         LR    R3,R4         SET OUTPUT ADDRESS                  83331
         EX    R2,EXMVC35    MOVE TEXT TO PRINT LINE             83331
         TM    SNPFLAG3,SNFASCII  TRANSLATE FROM ASCII ?         83331
         BZ    *+8           NO                                  83331
         EX    R2,EXASCTR    YES                                 83331
         ICM   R9,15,PWTRAN     OUTPUT TRANSLATION TABLE ?       91042
         BNZ   *+8           YES; USE IT                         91042
         L     R9,=A(MYTRTAB) ELSE DEFAULT TO PN                GP03074
         EX    R2,EXEBCTR    GET RID OF UNPRINTABLES             91042
         TM    SNPFLAG4,SNFVERT  VERTICAL FORMAT ?               83331
         BNO   SNAPPUMP      NO; PRINT AND INCREMENT             83331
         BAL   R9,SNAPRINT   PRINT THE LINE                      93164
         EX    R2,EXMVC35    MOVE THE DATA AGAIN                 83331
         TM    SNPFLAG3,SNFASCII  TRANSLATE FROM ASCII ?         83331
         BZ    *+8           NO                                  83331
         EX    R2,EXASCTR    YES                                 83331
         EX    R2,EXTRANZ    TRANSLATE ZONES                     83331
         BAL   R9,SNAPRINT   PRINT THE ZONES                     93164
         EX    R2,EXMVC35    MOVE THE DATA AGAIN                 83331
         TM    SNPFLAG3,SNFASCII  TRANSLATE FROM ASCII ?         83331
         BZ    *+8           NO                                  83331
         EX    R2,EXASCTR    YES                                 83331
         EX    R2,EXTRANN    TRANSLATE NUMERICS                  83331
*        B     SNAPPUMP      PRINT AND BUMP                      83331
         SPACE 1                                                 83331
SNAPPUMP BAL   R9,SNAPRINT   PRINT THE LINE                      93164
SNAPBUMP BXLE  R5,R6,SNAPLOOP  DO ANOTHER LINE UNLESS DONE       83331
         LM    R2,R9,SNPSAVE RESTORE REGISTERS                   83331
         BR    R9            RETURN TO CALLER                    83331
         SPACE 1                                                 83331
SNAPRINT BAL   R14,PUTTRUNC  PRINT A LINE                        93164
SNAPLANK MVI   PWRECCC,C' '  SET SINGLE SPACED                   83331
         MVC   PWREC,PWREC-1   BLANK ENTIRE LINE                 83331
         BR    R9            RETURN TO CALLER                    83331
         SPACE 1                                                 83331
OPTSNAP1 DC    Y(100,102+2)  LINE FORMAT                         83331
OPTSNAP2 DC    Y(32,(32*(3*4+1))/4+3) DUAL HEX + EBCDIC          83331
OPTSNAP3 DC    Y(48,48*(2*4+1)/4) HEX FORMAT                     83331
OPTSNAP4 DC    Y(100,100)    VERTICAL HEX                        83331
         SPACE 1                                                 83331
TRANZONE DC    16C'0',16C'1',16C'2',16C'3'   VERTICAL FORMAT     83331
         DC    16C'4',16C'5',16C'6',16C'7'   ZONE TRANSLATION TABLE
         DC    16C'8',16C'9',16C'A',16C'B'                       83331
         DC    16C'C',16C'D',16C'E',16C'F'                       83331
TRANNUM  DC    16C'0123456789ABCDEF'   VERTICAL FORMAT - NUMERICS
         SPACE 1                                                 83331
*        TRANSLATION IS FROM ANSI3/ISCII VOLUME LABELS           90309
*                                                                90309
TRASTOEB TRTAB TYPE=ITOE     REQUEST ISCII TO EBCDIC             90309
         SPACE 1                                                 84170
MYTRTAB  TRTAB CODE=TN       EXPAND TN TRAIN'S TRANSLATE TABLE  GP05277
         SPACE 2                                                 83331
SNAPSECT DSECT ,             CALLING PARAMETER LIST              83331
SNAPFLAG DS    XL4           FLAG BYTES                          83331
SNAPADDR DS    A             BEGIN ADDRESS                       83331
SNAPLEND DS    A             LENGTH OR END ADDRESS               83331
SNAPBASE DS    A             (OPTIONAL) BASE FOR OFFSETS         83331
@PRINTER CSECT ,                                                 83331
         SPACE 1                                                 84170
         LTORG ,                                                 84170
         SPACE 2                                                 81201
         FDSECT ,                                                81201
         SPACE 2                                                 81201
PRTMAP   MAPPRT ,            MAPPING OF USER'S PRTWORK AREA      81201
         SPACE 2
PRNTWORK MAPPRTWK WIDTH=MAXWIDTH MAP OF PRINT DCB AND WORK AREA  81201
         SPACE 1                                                 83331
         ORG   PWREC         BAD, DANGEROUS, UNDESIRABLE MAPPING 83331
         SPACE 1
         PRINT &PRTSYS
PRT0WORK FSAWORK PFX=FSW     PRIMARY WORK AREA                   82031
SAVE     DC    A(0)          LOCAL SAVE AREA                     82031
SAVE13   DS    A             CALLER'S R13                        82031
SAVEFWD  DS    A             LOWER R13                           82031
SAVE14   DS    A                                                 82031
SAVE15   DS    A                                                 82031
SAVE0    DS    A                                                 82031
SAVE1    DS    A                                                 82031
SAVE2    DS    A                                                 82031
SAVE3    DS    A                                                 82031
SAVE4    DS    A                                                 82031
SAVE5    DS    A                                                 82031
SAVE6    DS    A                                                 82031
SAVE7    DS    A                                                 82031
SAVE8    DS    A                                                 82031
SAVE9    DS    A                                                 82031
SAVE10   DS    A                                                 82031
SAVE11   DS    A                                                 82031
SAVE12   DS    A                                                 82031
USEXSAVE DS    16A           SAVE AREA DURING USER EXIT          84176
USEXPARM DS    5A            EXIT PARM LIST                      84176
USEX@WRK DS    A             MASTER PRINTER WORK AREA            84176
USEXFLAG DC    X'00'         USER EXIT ACTIVE IF <> 0            84176
USXFACT  EQU   X'80'           USER EXIT IN PROGRESS             84176
PRT0OPEN DC    X'00'         OPEN FLAGS                          82031
PRT0OMSK DC    X'00'         CURRENT DEVICE MASK BIT             82031
PRT0CLR  EQU   *             START OF AREA TO CLEAR              82031
DB       DS    D             WORK AREA
@SERVICE DS    0A            TEMP OPEN STORAGE OVER SIOT LOOK   GP02338
PUTWORK  DS    0A            8A SAVE AREA OVERLAYING DBWORK      81159
DBWORK   DS    CL16
DBWORK2  DS    CL16
RETCH    DS    0H,XL1        RETURN CODE
RETCC    DS    X               RETURN CODE - BYTE
RETC4    EQU   4                 MINOR ERROR
RETC8    EQU   8                 PROBLEM
RETC12   EQU   12                MAJOR ERROR
RETC16   EQU   16                DISASTER
         SPACE 1
PLSTXCLR EQU   *
PLSTULOP DS    X             UNDERLINE/OVERPRINT FLAG
PLFUND   EQU   2             UNDERLINE
PLFINT   EQU   1             OVERPRINT
PLSTCC   DS    X             FD-TEST COND. CODE BRANCH VALUE
PLSTEXEC DS    A             FD EXEC RESUME ADDRESS
PLSTEXEN DS    A             FD EXEC END OF PERFORM RANGE
PLSTINDT DS    A             AUTOMATIC LINE INDENT VALUE         81271
PLSTXLEN EQU   *-PLSTXCLR
         SPACE 1
CALLPARM DS    0XL8'0'       USER'S PARAMETERS (MOVED GPR0-1)
CALLFLG  DS    X      R0:0   FLAGS                              GP99029
CLABE    EQU   X'80'           ABEND IF NOT OPENED              GP99029
CLNWTO   EQU   X'20'           SUPPRESS WTO IF DD NOT FOUND     GP99029
CLSHEET  EQU   X'20'           START NEXT PAGE ON NEW SHEET      90309
CLABEDU  EQU   X'10'           ABEND IF DD DUMMY                GP03030
CLASA    EQU   X'04'           RECORD CONTAINS ASA CONTROL      GP99029
CLMCC    EQU   X'02'           RECORD CONTAINS MACHINE CODE     GP99029
CLNOCC   EQU   X'01'           NO CONTROL CHARACTER IN RECORD   GP99029
CLERR    EQU   X'01'           ERROR CONDITION W/ABEND IN OPEN  GP03030
CALLTIT  DS    X      R0:1   0 OR TITLE/FOOTER NUMBER(S)        GP99029
CALLDEV  DS    X      R0:2   0 OR DEVICE MASKS                  GP99029
CALLFUN  DS    X      R0:3   REQUESTED FUNCTION                 GP99029
CALLAD   DS    A      R1:0   LIST/RECORD ADDRESS OR VALUE       GP99029
CALLACR0 DS    0A            NEW PARAMETERS (PASSED VIA ACR0)   GP03025
         DS    X    ACR0:0     RESERVED                         GP03025
         DS    X    ACR0:1     RESERVED                         GP03025
         DS    X    ACR0:2     RESERVED                         GP03025
CALLLEN  DS    X    ACR0:3   LENGTH FOR PRTF FUNCTION           GP99029
         SPACE 1
PRTSAVE  DC    12F'0'        SAVE AREA
EDITSAVE DC    7F'0'         SAVE AREA FOR EDIT FUNCTION
FOOTSAVE DC    9F'0'         SAVE AREA FOR FOOTER FUNCTION       81193
         SPACE 1                                                 81159
PSEPARM  DS    0F            IEFSD095 PARAMETER LIST             81159
PSE@DATA DS    A             ADDRESS OF INPUT DATA               81159
PSE@COUN DS    A             ADDRESS OF LINE COUNTER             81159
PSE@VREC DS    A             ADDRESS OF OUTPUT RECORD            81159
PSE@CONT DS    A             ADDRESS OF CONTROL BITS/LENGTH; EOL 81159
PSECOUNT DS    F             NUMBER OF CURRENT LINE              81159
PSECNTRL DS    3X      1/3   CONTROL FLAGS                       81159
PSECNLEN DS    X       2/3   LENGTH OF INPUT ITEM                81159
PSEXOPTS DS    XL8     3/3   ADDITIONAL OPTIONS                  91056
PSEDATA  DS    CL10          INPUT ITEM                          81159
SPCWORK  EQU   *             START OF XC CLEAR AREA              81159
PSLNUM   DS    H             NUMBER OF ITEMS TO BE DONE          81159
PSLLEN   DS    H             LENGTH OF EACH ITEM                 81159
PSLFLAG  DS    X             COPY OF PROCESSING FLAG             81159
PSLFORM  DS    X             LOCAL COPY OF FORMATTING OPTIONS    81159
PSLLEAD  DS    H             NUMBER OF BLANK LINES AFTER EJECT   81159
PSLTWEEN DS    H             NUMBER OF BLANK LINES BETWEEN ITEMS 81159
SPCLEAR  EQU   *-SPCWORK     LENGTH TO CLEAR WITH XC             81159
SPCSING  DS    X     CONTROL CHARACTERS :  SINGLE SPACE          81159
SPCDOUB  DS    X             DOUBLE SPACE                        81159
SPCTRIP  DS    X             TRIPLE SPACE                        81159
SPCOVER  DS    X             OVERPRINT (NO SPACE)                81159
SPCEJECT DS    X             PAGE EJECT                          81159
SPCCH9   DS    X             CHANNEL 9                           81159
SPCCH12  DS    X             CHANNEL 12                          81159
SPCCH12N DS    X             CHANNEL 12/NO PRINT                 81159
SPCLEN   EQU   *-SPCSING     L-1 MATCHES ASA/MCC TABLE           81159
         SPACE 1                                                 83331
         ORG   PSEPARM       REDEFINE FOR PSNAP FUNCTION         83331
SNPSAVE  DS    8F            SAVE AREA R2-R9                     83331
SNPFLAGS DS    0XL4          FLAG WORD                           83331
SNPFLAG1 DS    X             CONTROL FLAG                        83331
SNFMORE  EQU   X'80'           ANOTHER ENTRY FOLLOWS             83331
SNPFLAG2 DS    X                                                 83331
SNPFLAG3 DS    X                                                 83331
SNFSLEN  EQU   X'80'           FDSNAP ONLY - LEN FIELD IS S-FORMAT
SNFASCII EQU   X'01'           CONVERT FROM ASCII                83331
SNPFLAG4 DS    X                                                 83331
SNFHEX   EQU   X'80'           DUMP IN HEX ONLY                  83331
SNFDUAL  EQU   X'40'           PRINT HEX AND EBCDIC TEXT         83331
SNFLIST  EQU   SNFHEX+SNFDUAL  BOTH OFF - EBCDIC ONLY            83331
SNFVERT  EQU   SNFHEX+SNFDUAL  BOTH ON - PRINT VERTICAL HEX FORMAT
SNFUBASE EQU   X'04'           USER SUPPLIED BASE VALUE          83331
SNFABS   EQU   X'02'           SKIP ABSOLUTE ADDRESS             83331
SNFOFF   EQU   X'01'           BASE ADDRESS/OFFSETS TO BE DONE   83331
SNPADDR  DS    A      1/2    ADDRESS OF CURRENT DUMP GROUP       83331
SNPLEN   DS    A      2/2    LENGTH OF CURRENT REQUEST           83331
SNPBASE  DS    A             BASE ADDRESS OF CURRENT REQUEST     83331
SNPOFFS  DS    A             OFFSET FOR CURRENT LINE FROM BASE   83331
SNPLLEN  DC    H'0'          LOGICAL LENGTH OF ONE TEXT LINE     83331
         ORG   ,                                                 83331
PRT0CLRL EQU   *-PRT0CLR     LENGTH TO CLEAR ON ENTRY            82031
IEFSD095 DC    A(0)          ADDRESS OF SEPARATOR BIG LETTER ROUTINE
@FORMATS DC    V(@FORMATS)                                      GP03287
         SPACE 1                                                 81159
PLSPACE  DC    CL((MAXWIDTH+5+3)/4*4)' '   SPACING RECORD        81159
PLSPACCC EQU   PLSPACE+4,1,C'C'        CARRIAGE CONTROL          81159
         SPACE 1
         ORG   PRTSAVE       RE-USE SAVE AREAS
         PRINT &PRTMAC
O2LVL DCBEXITD DSECT=NO,PREFIX=OX
         PRINT &PRTSYS                                           81275
         SPACE 1
         ORG   PRTSAVE                                           81193
GWSAVE0  DS    D             SAVE AREA FOR FLOAT REG 0
GWSAVE2  DS    D                                                 81193
GWSAVE4  DS    D                                                 81193
GWSAVE6  DS    D                                                 81193
CVGINT   DS    D             INTEGER SUBTRACT AREA               81193
         ORG   ,
         DS    0F            ALIGN                               83331
DBSAVE   DS    CL256         SAVE AND WORK AREA FOR LIST/CONVERT 81193
FSWLEN   EQU   *-PRT0WORK      GETMAIN LENGTH                    82031
         SPACE 2
         DCBD  DSORG=PS      EXPAND IHADCB
         SPACE 2
         IEFJFCBN ,          EXPAND JFCB
         SPACE 2
CVTDSECT DSECT ,
         CVT   DSECT=YES
         SPACE 1
         IKJTCB ,
         SPACE 1
         IEZDEB ,                                                81159
         SPACE 1                                                 90309
         IEFASIOT ,                                              90309
         SPACE 1                                                 81159
TIODSECT DSECT ,
         IEFTIOT1 ,          MAP TIOT
         SPACE 1                                                 81159
         IEFUCBOB ,                                              81159
         SPACE 1                                                 82031
         REGEQU ,                                                82031
         SPACE 1                                                 83275
         IHARB ,                                                 83257
         SPACE 1                                                 90309
         IHACDE ,                                                90309
         SPACE 1                                                 83275
         AIF   (NOT &MVS).BIGEND                                 83331
         IHAPSA ,                                                83331
         IHAASCB ,                                               83331
         IEZJSCB ,                                               90316
         IEFAJCTB ,                                              90316
         SPACE 1                                                 83331
.BIGEND  END   ,
