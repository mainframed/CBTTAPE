LLOG     TITLE 'L E X L O G O N  ***  LOCAL LOGON EXIT'          87303
         MACRO ,                                                 87303
&NM      LPPA  &P,&PARM=HAVE,&WORK=R15,&PARMR=R1                 87303
&NM      LPP   &P,PARM=&PARM,WORK=&WORK,PARMR=&PARMR             87303
         L     &WORK,OFFADD(,&WORK)                              87303
         MEND  ,                                                 87303
         SPACE 1                                                 87303
         MACRO ,                                                 87303
&NM      LPP   &P,&PARM=HAVE,&PARMR=R1,&WORK=R15                 87303
         LCLC  &N                                                87303
&N       SETC  '&NM'                                             87303
         AIF   ('&PARM' EQ 'HAVE').HAVEPRM                       87303
&N       L     &PARMR,ADDRPARM                                   87303
&N       SETC  ''                                                87303
.HAVEPRM ANOP  ,                                                 87303
&N       L     &WORK,PPP&P-PPPDSECT(,&PARMR)                     87303
         MEND  ,                                                 87303
         SPACE 1                                                 87303
         MACRO ,                                                 87303
&NM      TMSG  &AD,&LN,&OP                                       87303
         LCLC  &L,&N                                             87303
&N       SETC  '&NM'                                             87303
         AIF   ('&AD' EQ '(1)' OR '&AD' EQ '(R1)').NOC1          87303
         AIF   ('&AD'(1,1) EQ '(').DOR1                          87303
&N       LA    R1,&AD        LOAD MESSAGE ADDRESS                87303
         AGO   .NOB1                                             87303
.DOR1    ANOP  ,                                                 87303
&N       LR    R1,&AD(1)     LOAD MESSAGE ADDRESS                87303
.NOB1    ANOP  ,                                                 87303
&N       SETC  ''                                                87303
.NOC1    AIF   ('&LN' EQ '').DFL                                 87303
         AIF   ('&LN' EQ '(0)' OR '&LN' EQ '(R0)').DOR0          87303
&N       LA    R0,&LN        LOAD MESSAGE LENGTH                 87303
         AGO   .NOB0                                             87303
.DOR0    ANOP  ,                                                 87303
&N       LR    R0,&LN(1)     LOAD MESSAGE LENGTH                 87303
         AGO   .NOB0                                             87303
.DFL     ANOP  ,                                                 87303
&L       SETC  'L'''                                             87303
&N       LA    R0,&L&AD      LOAD MESSAGE LENGTH                 87303
.NOB0    ANOP  ,                                                 87303
&N       SETC  ''                                                87303
.NOC0    AIF   ('&OP' EQ '').PUT                                 87303
&L       SETC  '01'          ASIS                                87303
         AIF   ('&OP' EQ 'ASIS').OPLO                            87303
&L       SETC  '02'          CONTROL                             87303
         AIF   ('&OP' EQ 'CONTROL').OPLO                         87303
&L       SETC  '03'          FULL-SCREEN                         87303
         AIF   ('&OP' EQ 'FULLSCR').OPLO                         87303
         AIF   ('&OP' EQ 'EDIT').PUT                             87303
         MNOTE 4,'INVALID TPUT OPTION &OP'                       87303
         AGO   .PUT                                              87303
.OPLO    ANOP  ,                                                 87303
&N       ICM   R1,IOOO,=AL1(&L)  SET TPUT OPTION                 87303
&N       SETC  ''                                                87303
.PUT     ANOP  ,                                                 87303
&N       TPUT  (1),(0),R     WRITE THE MESSAGE                   87303
         MEND  ,                                                 87303
         SPACE 1                                                 87315
         MACRO ,                                                 87309
&NM      VALFD &PFX,&FIELD,&TEXT,&OPT,&PRIV=NO,&LEN=8,&TLEN=9    87314
         GBLA  &LINE                                             87309
         AIF   (&LINE GT 0).UPLINE                               87309
&LINE    SETA  4                                                 87314
.UPLINE  ANOP  ,                                                 87309
&LINE    SETA  &LINE+2                                           87309
         FDOPT SBA=(&LINE,1)                                     87315
         AIF   ('&TEXT' EQ '').NOTAG                             87309
&NM      FD    &TEXT,TURQ,SKIP,RADJ,NL,LEN=&TLEN                 87315
         AGO   .DONTAG                                           87314
.NOTAG   ANOP  ,                                                 87314
&NM      FD    ' ',TURQ,SKIP,NL,LEN=&TLEN                        87315
.DONTAG  FDOPT WHITE,SKIP                                        87314
         FDTM  &PFX.FG,&PFX.FTXT+&PFX.FERR,BM=&NM.2,BZ=&NM.1,BO=&NM.0
&NM.0    FDOPT RED,INTENSE,MONO,REV                              87314
&NM.1    FD    '==>',PREV                                        87309
&NM.2    FD    ' ',PINK,LP,SKIP  FINAGLE DISTINCT OPTIONS        87309
         FDOPT SBA=(*,&TLEN+7)                                   87314
         AIF   ('&PRIV' EQ 'YES').PROTECT                        87314
&NM.IN   FDIN  &FIELD,&OPT,GREEN,DEB,UP,MDT,LEN=&LEN             87359
         AGO   .PROCOM                                           87314
.PROTECT ANOP  ,                                                 87314
&NM.IN   FDINP &FIELD,&OPT,GREEN,DEB,UP,MDT,LEN=&LEN             87359
.PROCOM  FD    ' ',INT,PINK  ANOTHER DISTINCT FINAGLE FIELD      87309
.MEND    MEND  ,                                                 87309
         SPACE 1                                                 87315
         MACRO ,                                                 87309
&NM      VARFD &PFX,&FIELD,&TEXT,&OPT,&PRIV=NO,&LEN=8,&TLEN=9    87314
         GBLA  &LINE                                             87309
         AIF   (&LINE GT 0).UPLINE                               87309
&LINE    SETA  4                                                 87314
.UPLINE  ANOP  ,                                                 87314
&NM      FDOPT SBA=(&LINE,40)                                    87314
         AIF   ('&TEXT' EQ '').NOTAG                             87309
         FD    &TEXT,TURQ,SKIP,RADJ,LEN=&TLEN                    87314
         AGO   .DONTAG                                           87314
.NOTAG   ANOP  ,                                                 87314
         FD    ' ',TURQ,SKIP,LEN=&TLEN                           87314
.DONTAG  FDOPT WHITE,SKIP                                        87314
         FDTM  &PFX.FG,&PFX.FTXT+&PFX.FERR,BM=&NM.2,BZ=&NM.1,BO=&NM.0
&NM.0    FDOPT RED,INTENSE,MONO,REV                              87314
&NM.1    FD    '==>',PREV                                        87309
&NM.2    FDOPT SBA=(*,&TLEN+47)                                  87314
         FD    ' ',PINK,INT      FINAGLE DISTINCT OPTIONS        87309
         AIF   ('&PRIV' EQ 'YES').PROTECT                        87314
&NM.IN   FDIN  &FIELD,&OPT,GREEN,DEB,UP,MDT,LEN=&LEN             87359
         AGO   .PROCOM                                           87314
.PROTECT ANOP  ,                                                 87314
&NM.IN   FDINP &FIELD,&OPT,GREEN,DEB,UP,MDT,LEN=&LEN             87359
.PROCOM  FD    ' ',INT,PINK  ANOTHER DISTINCT FINAGLE FIELD      87309
.MEND    MEND  ,                                                 87309
         SPACE 1                                                 87314
         MACRO ,                                                 87314
&NM      FDINIT &FLD,&AF,&DF                                     87314
         LCLC  &L                                                87314
         LCLA  &I                                                87314
&L       SETC  'L'''                                             87314
&I       SETA  &SYSNDX                                           87314
&NM      CLI   &FLD.LEN,0    ALREADY FILLED ?                    87315
         BNE   ZZZZ&I        YES                                 87315
         MVI   &FLD.FG,FIDFTXT  SHOW TEXT PRESENT                87314
         MVI   &FLD.LEN,&L&AF SET FIELD LENGTH                   87314
         MVC   &FLD.TEXT,&AF PRIME THE INPUT FIELD               87314
         AIF   ('&DF' EQ '').MEND                                87314
         CLC   &FLD.TEXT,BLANKS  ANY DATA ?                      87314
         BH    ZZZZ&I        YES                                 87314
         MVC   &FLD.TEXT,&DF MOVE DEFAULT                        87314
.MEND    ANOP  ,                                                 87314
ZZZZ&I   DS    0H                                                87314
         MEND  ,                                                 87314
         MACRO ,                                                 88340
&NM      NEWPASS &MODE=TEST                                      88340
         LCLC  &N                                                88340
&N       SETC  '&NM'                                             88340
         AIF   ('&MODE' EQ 'RESET').RESET                        88340
         AIF   ('&MODE' EQ 'ERRESET').RESERR                     88341
&N       TM    FNPFG,FNPFTXT  ANY NEW PASSWORD TEXT ?            88340
&N       SETC  ''                                                88340
         BZ    *+4+4+4       NO; JUST RESET                      88340
         CLI   FNPTEXT,C' '  SIGNIFICANT ?                       88340
         BH    *+4+6+4       YES; BRANCH AROUND RESET            88340
         AGO   .RESET                                            88341
.RESERR  ANOP  ,                                                 88341
&N       TM    FNPFG,FNPFERR  ERROR IN TEXT ?                    88341
         BZ    *+4+6+4       NO; DON'T CHANGE                    88341
.RESET   ANOP  ,                                                 88340
&N       XC    FNPTEXT,FNPTEXT  CLEAR TEXT                       88340
         NI    FNPFG,255-FNPFERR-FNPFTXT  RESET FLAGS            88340
         MEND  ,                                                 88340
         SPACE 1                                                 87303
         COPY  OPTIONGB      ESTABLISH GLOBALS                   87303
         SPACE 1                                                 87303
         SYSPARM LIST=YES    SET AND DISPLAY GLOBAL OPTIONS      87303
         SPACE 1                                                 87310
         GBLC  &CRT                                              87310
&CRT     SETC  '3270'        SUPPORT FOR 3270S                   87310
         EJECT ,                                                 87303
***********************************************************************
*                                                                     *
*    LOGONXIT P3.2 - BEN MOORE, SCHERING-PLOUGH, INC         18 MAR 81
*                    3030 JACKSON AVE, MEMPHIS, TN 38151         87303
*                    901/320-2341                                87303
*                                                                87303
*        COPIED FROM CONNECTICUT BANK & TRUST TAPE FILE 167      87303
*        ALL THEIR LOCAL DENDENCIES WERE REMOVED, AND A FEW REPLACED
*        BY OUR OWN (SVC 255, MACROS, ETC.).                     87303
*                                                                     *
***********************************************************************
         SPACE 1                                                 87303
         PRINT &PRTSOR                                           87303
         SPLEVEL SET         SET DEFAULT SP LEVEL                92309
LEXLOGON SAVEM ZERO12,BASE=(R9,R10,R11,R12),PARM=R2,BNDRY=PAGE   87303
         SPACE 1                                                 87303
*        ASSEMBLY OPTIONS                                        87303
MAXWAIT  EQU   120           NUMBER OF SECONDS TO WAIT FOR INPUT 87303
MAXERR   EQU   3             MAXIMUM # OF BAD USERID/PASSWORD COMBOS
         SPACE 1
         MVI   NXTBLANK+C' ',C' '  MAKE TRT TABLE FOR BLANK SCAN 87303
         BAL   R14,HOUSKEEP        GO DO HOUSEKEEPING
         TM    GBITS1,GBYPASS  LOCAL RESOURCE FAILURE ?          91003
         BNZ   RETURN        YES; LET IBM DO IT                  91003
         BXH   R15,R15,RETURN2     WAS HOUSEKEEPING SUCCESSFUL   87303
         TM    GBITS2,GDISPLAY  DISPLAY TERMINAL ?               87309
         BZ    SERVNTO       NO; DO IT THE OLD FASHIONED WAY     87309
         BAL   R14,FULLSCRN  GO TO FULL-SCREEN I/O               87309
         BXLE  R15,R15,RETURN  SUCCESSFUL LOGON                  87309
         B     RETURN2       ELSE FAIL                           87309
SERVNTO  BAL   R14,NTOPARSE  GO PARSE COMMAND BUFFER             87309
         BXLE  R15,R15,RETURN   SUCCESSFUL ?                     87333
*        B     RETURN2       NO                                  87303
         TITLE 'L E X L O G O N  ***  RETURN TO CALLER'
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    RETURN                                                          P3
*                                                                    P3
* FUNCTION -                                                         P3
*    PERFORMS CLEANUP AND RETURNS TO CALLER.                         P3
*                                                                    P3
* OPERATION -                                                        P3
*    1) CANCEL STIMER.                                               P3
*    2) CANCEL STAX.                                                 P3
*    3) IF DEBUG REQUESTED, CALL DEBUG ROUTINE.                      P3
*    4) IF UADS WAS OPENED, CLOSE AND FREE BUFFER.                   P3
*    5) IF ANYTHING WAS ENQ'D ON, DEQ IT                             P3
*    6) IF LOGON IS TO BE DENIED, SET DISCONNECT INDICATOR AND WAIT  P3
*       5 SECONDS IF THIS IS A DISPLAY TERMINAL TO GIVE THE USER     P3
*       TIME TO READ THE DIAGNOSTICS.                                P3
*    7) IF ATTENTION WAS HIT, GO CHECK NEW COMMAND.                  P3
*    8) IF RECONNECT WAS SPECIFIED, LEAVE NOW AS PER AZ06786.        P3
*    9) IF LOGON IS TO BE ALLOWED, INDICATE DON'T PROMPT AND UPDATE  P3
*       DOPE VECTORS.                                                P3
*   10) CANCEL ESTAE.                                                P3
*   11) RETURN TO CALLER.                                            P3
*                                                                    P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    RETURN                                                          P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    NONE                                                            P3
*                                                                    P3
* MESSAGES -                                                         P3
*    NONE                                                            P3
*                                                                    P3
*********************************************************************P3
         SPACE 1                                                 87303
RETURN2  SRL   R15,1         MAKE ACTUAL CODE AGAIN (AFTER BXH)  87303
RETURN   LR    R2,R15        PROTECT RETURN CODE                 87303
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       CANCEL STIMER.                                               P3
*                                                                    P3
*********************************************************************P3
         TTIMER CANCEL             CANCEL STIMER                     P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       CANCEL STAX.                                                 P3
*                                                                    P3
*********************************************************************P3
         STAX  0                   CANCEL STAX                       P3
         STAX  0                   CANCEL STAX                       P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       IF DEBUG REQUESTED, CALL DEBUG ROUTINE.                      P3
*                                                                    P3
*********************************************************************P3
         TM    GBITS1,GDEBUG       WAS DEBUG REQUESTED               P3
         BZ    RTRNDBUG            NO - CONTINUE
         BAL   R14,DEBUG           YES - GO TO DEBUG ROUTINE
RTRNDBUG DS    0H
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       IF UADS WAS OPENED, CLOSE AND FREE BUFFER.                   P3
*                                                                    P3
*********************************************************************P3
         TM    GSYSUADS+DCBOFLGS-IHADCB,DCBOFOPN DID UADS OPEN
         BZ    RTRNNOPN            NO - NO NEED TO DO FREEMAIN
         LH    R0,GSYSUADS+DCBBLKSI-IHADCB YES - GET BLKSIZE
         MH    R0,=H'10'     MULTIPLY BY 10                      87303
         ICM   R1,B'1111',ADDRUBUF GET ADDRESS OF BUFFER             P3
         BZ    RTRNNOPN            IF ZERO - SKIP FREEMAIN           P3
         FREEMAIN R,LV=(0),A=(1)   FREE AREA FOR SYS1.UADS BUFFER
         CLOSE MF=(E,GOPENLST)     CLOSE SYS1.UADS
RTRNNOPN DS    0H
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       IF ANYTHING WAS ENQ'D ON, DEQ IT                             P3
*                                                                    P3
*********************************************************************P3
         TM    GBITS2,GSPFENQ0     WAS MEMBER0 ENQ'D ON              P3
         BZ    RTRNDEQ0            NO - GO CONTINUE                  P3
         MVC   GENQLIST,ENQLIST2   YES - COPY ENQ LIST               P3
         MVI   GENQLIST+1,52       PUT LENGTH OF RNAME IN ENQ LIST   P3
         MVC   GRNAME(44),GJFCB    MOVE DSNAME TO RNAME              P3
         LH    R15,LUSERID         GET LENGTH OF USERID              P3
         LA    R1,GMEMBER(R15)     POINT TO BYTE AFTER USERID        P3
         MVI   0(R1),C'0'          ADD SUFFIX TO MAKE MEMBER0        P3
         MVC   GRNAME+44(8),GMEMBER APPEND MEMBER0 NAME TO DSN       P3
         LA    R15,QNAME2          GET ADDRESS OF QNAME (SPFDSN  )   P3
         ST    R15,GENQLIST+4      PUT IN ENQ LIST                   P3
         LA    R15,GRNAME          GET ADDR OF RNAME (DSN+MEMBER0)   P3
         ST    R15,GENQLIST+8      PUT IN ENQ LIST                   P3
         LA    R1,GENQLIST         GET ADDRESS OF ENQ LIST           P3
         DEQ   MF=(E,(1))          DEQ LIKE SPF DOES                 P3
         NI    GBITS2,255-GSPFENQ0 TURN OFF MEMBER0 ENQ'D ON         P3
RTRNDEQ0 DS    0H                                                    P3
         TM    GBITS2,GSPFRES      WAS DSN RESERVED                  P3
         BZ    RTRNRES             NO - CONTINUE                     P3
         MVC   GENQLIST,RESLIST    YES - COPY RESERVE LIST           P3
         MVI   GENQLIST+1,44       PUT LENGTH OF RNAME IN RES LIST   P3
         MVC   GRNAME(44),GJFCB    MOVE DSNAME TO RNAME              P3
         LA    R15,QNAME2          GET ADDRESS OF QNAME (SPFDSN  )   P3
         ST    R15,GENQLIST+4      PUT IN RESERVE LIST               P3
         LA    R15,GRNAME          GET ADDR OF RNAME (DSN)           P3
         ST    R15,GENQLIST+8      PUT IN RESERVE LIST               P3
         LA    R15,ADDRUCB         GET ADDRESS OF UCB ADDRESS        P3
         ST    R15,GENQLIST+12     PUT IN RESERVE LIST               P3
         LA    R1,GENQLIST         GET ADDRESS OF RESERVE LIST       P3
         DEQ   MF=(E,(1))          DEQ LIKE SPF DOES                 P3
         NI    GBITS2,255-GSPFRES  TURN OFF DSN RESERVED             P3
RTRNRES  DS    0H                                                    P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       IF LOGON IS TO BE DENIED, SET DISCONNECT INDICATOR AND WAIT  P3
*      10 SECONDS IF THIS IS A DISPLAY TERMINAL TO GIVE THE USER     P3
*       TIME TO READ THE DIAGNOSTICS.                                P3
*                                                                    P3
*********************************************************************P3
         TM    GBITS1,GDENY        IS LOGON TO BE DENIED             P3
         BNO   RTRNOK              NO - CONTINUE
         LPPA  SW,PARM=      LOAD CONTROL SWITCH ADDRESS         87303
         OI    SW1-SWDSECT(R15),SWDISC  SET DISCONNECT           87303
         GTSIZE ,                  DETERMINE TERMINAL TYPE           P3
         CH    R0,=H'1'      WAS THIS A DISPLAY                  87303
         BNH   RTRNEND             NO - CONTINUE                 87303
         STIMER WAIT,BINTVL==A(10*100) YES - WAIT A BIT          87303
         B     RTRNEND             THEN CONTINUE                     P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       IF ATTENTION WAS HIT, GO CHECK NEW COMMAND.                  P3
*                                                                    P3
*********************************************************************P3
RTRNOK   TM    GBITS1,GATTNHIT     WAS ATTENTION HIT                 P3
         BO    RTRNCHEK            YES - GO CHECK NEW COMMAND        P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       IF RECONNECT WAS SPECIFIED, LEAVE NOW AS PER AZ06786.        P3
*    FAILS IN X/A - JUST HOPE ALL DATA ARE THE SAME                  P3
*********************************************************************P3
*X/A-FAILTM    GBITS1,GRECON+GBYPASS  BYPASS OR RECONNECT ?      87333
         TM    GBITS1,GBYPASS  BYPASS ?                          93252
         BNZ   RTRNEND       YES - LEAVE NOW AS PER AZ06786      87333
         LPPA  SW,PARM=      RELOAD SWITCH ADDRESS               93252
         TM    GBITS1,GRECON  RECONNECT REQUESTED ?              93252
         BZ    *+8           NO                                  93252
         OI    SW2-SWDSECT(R15),SWRECN  TELL X/A TO RECONNECT    93252
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       IF LOGON IS TO BE ALLOWED, INDICATE DON'T PROMPT AND UPDATE  P3
*       DOPE VECTORS.                                                P3
*                                                                    P3
*********************************************************************P3
         LPPA  SW,PARM=      GET ADDRESS OF CONTROL SWITCHES     87303
         OI    SW1-SWDSECT(R15),SWNPRM  DON'T PROMPT             87303
         LPP   UID           GET ADDRESS OF USERID DESCRIPTOR    87303
         MVC   OFFCUR(L'OFFCUR,R15),LUSERID  LENGTH OF USERID    87303
         L     R15,OFFADD(,R15)  GET ADDRESS OF USERID
         MVC   0(7,R15),L$UUID    MOVE USERID TO DATA AREA       87333
         LPP   PSW           GET ADDRESS OF PASSWORD DESCRIPTOR  87303
         MVI   OFFCUR+1(R15),8  SET LENGTH OF PASSWORD           87303
         L     R15,OFFADD(,R15)  GET ADDRESS OF PASSWORD
         MVC   0(8,R15),GPASS      MOVE PASSWORD TO DATA AREA
         LPP   ACT           GET ADDRESS OF ACCT NUM DESCRIPTOR  87303
         MVI   OFFCUR+1(R15),8  SET LENGTH OF ACCOUNT NUMBER     87303
         L     R15,OFFADD(,R15)  GET ADDRESS OF ACCOUNT NUMBER
         MVC   0(8,R15),L$UACCT   MOVE ACCOUNT NUMBER TO DATA AREA
         LPP   PRO           GET ADDRESS OF PROC NAME DESCRIPTOR 87303
         MVI   OFFCUR+1(R15),8  SET LENGTH OF PROC NAME          87303
         L     R15,OFFADD(,R15)  GET ADDRESS OF PROC NAME
         MVC   0(8,R15),L$UTSPRC   MOVE PROC NAME TO DATA AREA   87333
         LPPA  ECT           GET ADDRESS OF ECT                  87303
         MVC   ECTSWS-ECT(1,R15),GECTSWS SET ECT SWITCHES        87334
         SPACE 1                                                 87310
*********************************************************************P3
*                                                                    P3
*        IS USER TO RUN WITHOUT UADS SUPPORT ?                   87310
*                                                                    P3
*********************************************************************P3
         TM    GBITS1,GNOUADS  UADS USED ?                       87310
         BZ    RTRNEND       YES; PROCEED                        87310
         LPPA  SW            GET SWITCHES BACK                   87310
         OI    SW1-SWDSECT(R15),SWNUAD  NO UADS                  87310
         OI    SW2-SWDSECT(R15),SWSA+SWUA+SWGRP+SWUPT            87361
         CLI   YN2SES,C'N'   RUN MORE THAN ONE SESSION ?         87361
         BE    *+8           NO; LET IT ENQUEUE                  87361
         OI    SW2-SWDSECT(R15),SWNENQ  NO; FORCE AN ENQ         87361
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       CANCEL ESTAE.                                                P3
*                                                                    P3
*********************************************************************P3
         SPACE 1                                                     P3
RTRNEND  SERVTERM ,          CLOSE AND FREE LOCAL STUFF          87333
         ESTAE 0                   CANCEL ESTAE
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       RETURN TO CALLER.                                            P3
*                                                                    P3
*********************************************************************P3
         SPACE 1                                                 87303
         ENDM  RC=(R2)       RETURN WITH CONDITION CODE          87303
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       IF COMMAND WAS NOT 'LOGON' DENY LOGON.  IF COMMAND WAS       P3
*       'LOGON', REENTER LOGON EXIT WITH NEW COMMAND.                P3
*                                                                    P3
*********************************************************************P3
         SPACE 1                                                 87333
RTRNCHEK XC    FDW(MAPFID-FDW),FDW  CLEAR ALL CONTROL INFORMATION
         MVI   FDWOPT,FDWKEY12  FOLD 24 TO 12 PF KEYS            88214
         LA    R14,LISTFD    POINT TO FD LIST                    87333
         LA    R15,FDINWORK  POINT TO INPUT AREA                 87333
         LA    R0,FDINWORX-FDINWORK  WORK AREA LENGTH            87333
         STM   R14,R0,FDWFDA  MAKE CALLING PARAMETER LIST        87333
         LA    R0,PARSFULL   GET LINE MODE PARSE TABLE           87333
         ST    R0,FDWSCAC    SET CONTROL ADDRESS                 87333
         SCRINIT FDW         CLEAR EVERYTHING                    87333
         SLR   R2,R2                                             87361
         SCRLIST FDW         MAKE WORK AREA ATB ADDRESSES        87333
         L     R14,GCPPL+CPPLCBUF-CPPL  GET COMMAND BUFFER       87333
         LH    R15,0(,R14)   GET BUFFER LENGTH                   87333
         LA    R14,4(,R14)   BUFFER TEXT                         87333
         SH    R15,=H'4'     LESS LENGTH OF LENGTH               87333
         BNP   RTRNCHID      NOTHING TO PARSE                    87333
         STM   R14,R15,FDWSCAN  INIT SCAN ADDRESS AND LENGTH     87333
         SCREDIT FDW         DO BACKSPACE EDITING                88340
         SCRSCAN FDW         DO SIMPLE SCAN                      87333
         SCRMOVE FDW         COPY ACCEPTED PARAMETERS            87333
         CLC   KLOGON,DB     LOGON REQUEST                       88002
         BE    RTRNRENT      YES; EXIT WITH RE-ENTRY             87333
RTRNCHID LPPA  SW,PARM=      GET ADDRESS OF CONTROL SWITCHES     87333
         OI    SW1-SWDSECT(R15),SWDISC   FORCE DISCONNECT        87303
         B     RTRNEND             RETURN                            P3
         SPACE 1                                                 87303
RTRNRENT SERVTERM ,          CLOSE AND FREE LOCAL STUFF          87333
         ESTAE 0                   CANCEL ESTAE                  87303
         ENDM  RETADDR=(R15) FREEMAIN, ETC., THEN RE-ENTER       90301
         TITLE 'L E X L O G O N  ***  HOUSEKEEPING'              87303
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    HOUSKEEP                                                         *
*                                                                     *
* FUNCTION -                                                          *
*    PERFORMS HOUSEKEEPING FUNCTIONS FOR LEXLOGON.                    *
*                                                                     *
* OPERATION -                                                         *
*    1) ISSUES ESTAE.  IF ESTAE IS UNSUCCESSFUL, TELLS TSO USER AND   *
*       DENIES LOGON.                                                 *
*    2) MOVES STIMER EXIT TO DYNAMIC AREA AND ISSUES STIMER.         P3
*    3) ISSUES STAX.  IF STAX IS UNSUCCESSFUL, TELLS TSO USER AND    P3
*       DENIES LOGON.                                                P3
*    4) CHECKS TO SEE IF THIS IS 'USERID ENQ FAIL'.  IF SO, TELLS TSO *
*       USER AND DENIES LOGON.                                        *
*    5) CHECKS TO SEE IF THIS IS 'RESOURCE FAILURE'.  IF SO, TELLS TSO*
*       USER AND DENIES LOGON.                                        *
*    6) CHECKS TO SEE IF THIS IS 'ABEND'.  IF SO, TELLS TSO USER AND  *
*       DENIES LOGON.                                                 *
*    7) GET APF AUTHORIZED AND GET TERMINAL ID FROM TSB.             P3
*    8) ATTEMPTS TO OPEN SYS1.UADS.  IF OPEN IS UNSUCCESSFUL, TELLS   *
*       TSO USER AND DENIES LOGON.                                    *
*    9) GETS STORAGE FOR UADS DATA BLOCKS.                            *
*   10) READS $SYSTEM$ BLOCK IF PRESENT.                           P3.1
*   11) BUILDS CBUF AND CPPL.                                         *
*   12) CLEAR TERMINAL BUFFERS.                                      P3
*   13) IF DISPLAY TERMINAL, CLEAR THE SCREEN.                       P3
*   14) RETURNS TO THE MAINLINE.                                      *
*                                                                     *
* ENTRY POINTS -                                                      *
*    HOUSKEEP                                                         *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    GETAUTH                                                          *
*                                                                     *
* INPUT -                                                             *
*    NONE                                                             *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*    'ESTAE UNSUCCESSFUL - RC=NNNN'                                   *
*    'STAX UNSUCCESSFUL - RC=NNNN'                                   P3
*    'USERID CURRENTLY LOGGED ON'                                     *
*    'LOGON RESOURCE FAILURE. CONTACT TECHNICAL SUPPORT'           P3.2
*    'LOGON ABEND. CONTACT TECHNICAL SUPPORT'                         *
*    'OPEN OF SYS1.UADS UNSUCCESSFUL'                                 *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87303
HOUSKEEP ST    R14,HSKPSAVE        SAVE RETURN ADDRESS           87303
         ST    R2,ADDRPARM         SAVE ADDRESS OF PARMS
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*       ISSUES ESTAE.  IF ESTAE IS UNSUCCESSFUL, TELLS TSO USER AND   *
*       DENIES LOGON.                                                 *
*                                                                     *
***********************************************************************
         MVC   GESTAE(LGESTAE),LESTAE COPY ESTAE PARM LIST
         LA    R3,ESTAEXIT         GET ADDRESS OF ESTAE EXIT
         LA    R4,ESTAEPRM         GET ADDRESS OF ESTAE PARMS
         STM   R9,R13,ESTAEPRM  SAVE BASES FOR EXIT ROUTINES     87303
         ESTAE (3),PARAM=(4),MF=(E,GESTAE) ESTABLISH ESTAE
         LTR   R15,R15             WAS ESTAE SUCCESSFUL
         BNZ   HSKPNSTA            NO - GO GIVE MESSAGE
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       MOVES STIMER EXIT TO DYNAMIC AREA AND ISSUES STIMER.         P3
*                                                                    P3
*********************************************************************P3
         MVC   GTIMRXIT,TIMERXIT   MOVE STIMER EXIT TO DYNAMIC AREA  P3
         MVC   GTIMRWTO,TIMERWTO   COPY WTO TEXT TO DYNAMIC AREA     P3
*FAILS*  STIMER REAL,GTIMRXIT,BINTVL==A(5*MAXWAIT) 10 MIN. LOGON 87303
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       ISSUES STAX.  IF STAX IS UNSUCCESSFUL, TELLS TSO USER AND    P3
*       DENIES LOGON.                                                P3
*                                                                    P3
*********************************************************************P3
         XC    GDSTAX(LSTAX),GDSTAX CLEAR DUMMY STAX PARM LIST       P3
         LA    R1,GDSTAX           GET ADDRESS OF DUMMY STAX PRM LST P3
         USING STX,R1              ESTABLISH ADDRESSABILITY          P3
         LA    R15,STAXDXIT        GET ADDRESS OF DUMMY STAX EXIT    P3
         ST    R15,STXEXIT         PUT IN STAX PARAMETER LIST        P3
         LA    R15,ESTAEPRM        GET ADDRESS OF ESTAE PARAM LIST   P3
         STCM  R15,7+8*&MVSXA,STXNUSER  PUT IN STAX PARAMETER LIST
         DROP  R1                  DROP STAX PRM LIST ADDRESSABILITY P3
         STAX  MF=(E,(1))          ISSUE FIRST STAX                  P3
         LTR   R15,R15             WAS STAX SUCCESSFUL               P3
         BNZ   HSKPNSTX            NO - GO GIVE MESSAGE              P3
         XC    GSTAX(LSTAX),GSTAX  YES - CLEAR STAX PARM LIST        P3
         LA    R1,GSTAX            GET ADDRESS OF STAX PARAM LIST    P3
         USING STX,R1              ESTABLISH ADDRESSABILITY          P3
         LA    R15,STAXEXIT        GET ADDRESS OF STAX EXIT          P3
         ST    R15,STXEXIT         PUT IN STAX PARAMETER LIST        P3
         LPP   IN,PARMR=R2,PARM=,WORK=R2  GET INPUT DESCRIPTOR   87303
         LH    R15,OFFMAX(,R2)  GET LENGTH OF INPUT BUFFER       87303
         STH   R15,STXISIZ         PUT IN STAX PARAMETER LIST        P3
         LA    R15,L'STAXOBUF      GET LENGTH OF STAX MESSAGE        P3
         STH   R15,STXOSIZ         PUT IN STAX PARAMETER LIST        P3
         LA    R15,STAXOBUF        GET ADDRESS OF STAX MESSAGE       P3
         ST    R15,STXOBUF         PUT IN STAX PARAMETER LIST        P3
         L     R15,OFFADD(,R2)  GET ADDRESS OF ORIGINAL BUFF AREA
         ST    R15,STXIBUF         PUT IN STAX PARAMETER LIST        P3
         LA    R15,ESTAEPRM        GET ADDRESS OF ESTAE PARAM LIST   P3
         STCM  R15,7+8*&MVSXA,STXNUSER  PUT IN STAX PARAMETER LIST
         OI    GSTAX+STXOPTS-STX,KBIT1 INDICATE REPLACE=NO       92309
         DROP  R1                  DROP STAX PRM LIST ADDRESSABILITY P3
         STAX  MF=(E,(1))          ESTABLISH STAX                    P3
         LTR   R15,R15             WAS STAX SUCCESSFUL               P3
         BNZ   HSKPNSTX            NO - GO GIVE MESSAGE              P3
         SPACE 1                                                 87334
         LPPA  ECT,PARM=LOAD                                     87334
         MVC   GECTSWS,ECTSWS-ECT(R15) COPY ECT OPTIONS          87334
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF THIS IS 'RESOURCE FAILURE'.  IF SO, TELLS TSO*
*       USER AND DENIES LOGON.                                        *
*                                                                     *
***********************************************************************
         TM    SW1-SWDSECT(R15),SWRESBD  RESOURCE FAILURE ?      87303
         BO    HSKPRESC            YES - GO GIVE MESSAGE
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF THIS IS 'ABEND'.  IF SO, TELLS TSO USER AND  *
*       DENIES LOGON.                                                 *
*                                                                     *
***********************************************************************
         TM    SW2-SWDSECT(R15),SWABND   WAS THIS 'ABEND' ?      87303
         BO    HSKPABND            YES - GO GIVE MESSAGE
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       GET APF AUTHORIZED AND GET TERMINAL ID FROM TSB.             P3
*                                                                    P3
*********************************************************************P3
         MVC   GTERMID,ABNDWTO     ASSUME 'UNKNOWN'                  P3
         BAL   R14,GETAUTH         GO GET APF AUTHORIZATION          P3
         L     R4,PSATOLD-PSA  GET ADDRESS OF MY TCB             87310
         USING TCB,R4                                            87310
         L     R15,TCBJSCB   GET ADDRESS OF MY JSCB              87310
         USING IEZJSCB,R15                                       87310
         TM    JSCBOPTS,JSCBAUTH   MAY I ISSUE MODESET ?         87310
         BZ    HSKPGTRM            NO - CAN'T GET TERMINAL FROM TSB  P3
         L     R15,PSAAOLD-PSA     YES - GET ADDRESS OF MY ASCB    P3.2
         USING ASCB,R15                                          87310
         L     R3,ASCBTSB    GET ADDRESS OF MY TSB               87310
         USING TSB,R3                                            87310
         ICM   R2,15,ASCBASXB  GET ASXB                          87333
         BZ    SKIPLWA       HUH ?                               87333
         ICM   R2,15,ASXBLWA-ASXB(R2)  GET LWA ADDRESS           87333
         USING LWA,R2                                            87333
SKIPLWA  DS    0H                                                87333
         MODESET MF=(E,KEYZERO)    GET KEY 0 FOR MOVE FROM TSB       P3
         ICM   R0,15,TCBFSA  IS THERE A SAVE AREA ?              87310
         BNZ   HAVEFSA       YES                                 87310
         LA    R0,INITSAVE   NO; FAKE ONE                        87310
         ST    R0,TCBFSA     NEED FOR @SERVICE ROUTINE           87310
HAVEFSA  LTR   R2,R2         LWA PASSED ?                        87333
         BZ    HAVEPECT      NO; PASS                            87333
         ICM   R1,15,LWAPECT IS THERE AN ECT POINTER IN THE LWA? 87333
         BNZ   HAVEPECT      YES                                 87333
         LPPA  ECT,PARM=LOAD,PARMR=R15,WORK=R15  GET ECT ADDRESS 87333
         ST    R15,LWAPECT   FIX IBM'S OMISSION                  87333
HAVEPECT LTR   R3,R3         IS THERE A TSB ?                    90296
         BNZ   HAVETSB       YES                                 90296
         L     R3,TCBTIO     GET TIOT                            90296
         MVC   GTERMID,0(R3)  USE BATCH JOB NAME AS ID           90296
         B     HAVECRT       SKIP TSB CHECKS                     90296
HAVETSB  MVC   GTERMID,TSBTRMID  COPY TERMINAL TO DYN. AREA      90296
         TM    TSBSTAT,TSBDSPLY  CRT ?                           87360
         BNZ   HAVECRT       YES                                 87360
         CLI   TSBLNSZ,72    OLD-STYLE TTY SIZE ?                87360
         BNE   HAVECRT       NO; DON'T MESS WITH IT              87360
         MVI   TSBLNSZ,80    (ALMOST) ALL ASYNCH TERMINALS       87360
HAVECRT  DS    0H                                                87360
         MODESET MF=(E,NONZERO)    RETURN TO USER KEY                P3
         DROP  R2,R3,R4,R15                                      87333
         SPACE 1                                                 87333
HSKPGTRM SERVINIT ,          LOAD AND PREP THE SERVICE ROUTINE   87309
         BXH   R15,R15,BYPASS    RESOURCE FAILURE  -  IBM LOGON  91003
         SERVCALL LPALD,=CL8'@SCREENS'  SCREEN FORMATTER         87309
         BXH   R15,R15,BYPASS    RESOURCE FAILURE  -  IBM LOGON  91003
         ST    R0,@SCREENS   STASH IT                            87309
         MVC   GTIMRWTO+15(8),GTERMID MOVE TERMID TO TIMER WTO       P3
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*        FIND EXISTING, OR ESTABLISH NEW LOCAL ON-LINE USER DATA AREA *
*        (LOUD); WILL BE INITIALIZED WITH USER ID LATER               *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87303
         XC    A$SVCRB(A$SLENRB),A$SVCRB  CLEAR PARM AREA        87303
         MVC   A$SID,=C'A$RB'  IDENTIFY THE BLOCK                87303
         MVC   A$SFCTN(3),=AL1(A$SFLOUD,A$STSO,A$SBUILD+A$SDEBUG)
         LA    R0,GMSGAREA   GET RETURN MESSAGE                  87303
         ST    R0,A$SERMSG   SET FOR MESSAGE PROCESSING          87303
         MVC   A$SACCT,GTERMID  SET THE TERMID                   87303
         ACCTSVC A$SVCRB,ERR=BYPASS  QUIT IF FUNNY ENVIRONMENT   91003
         BXLE  R15,R15,HAVELOUD  OK; AS EXPECTED                 87303
         SLR   R0,R0                                             87303
         ICM   R0,OOII,GMSGAREA  ANY MESSAGE RETURNED ?          87303
         BZ    HSKPNOPN      TELL USER OPEN FAILED               87303
         LA    R1,GMSGAREA+4  POINT TO TEXT                      87303
         SH    R0,=H'4'      ADJUST MESSAGE LENGTH               87303
         B     CC4DENY       ISSUE MESSAGE AND DENY LOGON        87303
HAVELOUD MVC   ADDRLOUD,A$SBUFR  COPY ADDRESS OF DATA AREA       87303
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*       ATTEMPTS TO OPEN SYS1.UADS.  IF OPEN IS UNSUCCESSFUL, TELLS   *
*       TSO USER AND DENIES LOGON.                                    *
*                                                                     *
***********************************************************************
         MVC   GSYSUADS,SYSUADS    MOVE SYS1.UADS DCB TO DYNAMIC AREA
         MVC   GOPENLST,OPENUPDT   MOVE OPEN LIST TO DYNAMIC AREA    P3
         LA    R15,GJFCB           GET ADDRESS OF JFCB AREA          P3
         ST    R15,EXLST           PUT IN EXLST                      P3
         MVI   EXLST,X'87'         INDICATE JFCB ADDRESS             P3
         LA    R15,EXLST           GET ADDRESS OF EXLST              P3
         LA    R1,GSYSUADS         GET ADDRESS OF SYSUADS DCB
         USING IHADCB,R1           GET DCB ADDRESSABILITY            P3
         STCM  R15,B'0111',DCBEXLSA PUT ADDRRESS OF EXLST IN DCB     P3
         STCM  R1,B'0111',GOPENLST+1 PUT ADDR OF DCB IN OPEN LIST    P3
         DROP  R1                  DROP DCB ADDRESSABILITY           P3
         OPEN  MF=(E,GOPENLST)     ATTEMPT TO OPEN SYS1.UADS
         TM    GSYSUADS+DCBOFLGS-IHADCB,DCBOFOPN DID UADS OPEN
         BZ    HSKPNOPN            NO - GO TO ERROR ROUTINE
         RDJFCB MF=(E,GOPENLST)    YES - READ JFCB FOR LATER ENQ     P3
         L     R15,PSATOLD-PSA     GET ADDRESS OF MY TCB             P3
         L     R15,TCBTIO-TCB(,R15) GET ADDRESS OF MY TIOT           P3
         SLR   R0,R0                                             93051
         ICM   R0,3,GSYSUADS+DCBTIOT-IHADCB GET OFFSET OF UADS   93051
         AR    R15,R0        GET TIOT ENTRY                      93051
         MVC   ADDRUCB,16(R15)     SAVE UCB ADDRESS FOR RESERVE      P3
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*       GETS STORAGE FOR UADS DATA BLOCKS.                            *
*                                                                     *
***********************************************************************
         LH    R0,GSYSUADS+DCBBLKSI-IHADCB GET BLKSIZE
         MH    R0,=H'10'     MULTIPLY BY 10                      87303
         GETMAIN RU,LV=(0),BNDRY=PAGE GET AREA FOR SYS1.UADS BUFFER  P3
         ST    R1,ADDRUBUF         SAVE ADDRESS FOR LATER
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*       BUILDS CBUF AND CPPL.                                         *
*                                                                     *
***********************************************************************
         LPP   IN,PARM=,PARMR=R2    GET INPUT BUFFER DESCRIPTOR  87303
         LH    R1,OFFCUR(,R15)   GET LENGTH OF INPUT BUFFER      87305
         LA    R14,4(,R1)    ADD LENGTH AND OFFSET               87305
         STH   R14,GCBUF+CBUFLEN-CBUF PUT IN CBUF
         XC    GCBUF+CBUFOFFS-CBUF(2),GCBUF+CBUFOFFS-CBUF ZERO OFFSET
         L     R0,OFFADD(,R15)   GET ADDRESS OF INPUT BUFFER     87305
         LA    R14,GCBUF+CBUFTEXT-CBUF  GET PARSE BUFFER         87305
         LA    R15,252       SET MAXIMUM LENGTH OF BUFFER        87305
         ICM   R1,IOOO,BLANKS  MAKE BLANK FILL                   87305
         MVCL  R14,R0        MOVE TEXT TO PARSE BUFFER           87305
         LA    R15,GCBUF           GET ADDRESS OF COMMAND BUFFER
         ST    R15,GCPPL+CPPLCBUF-CPPL PUT IN CPPL
         LPPA  UPT,PARMR=R2  GET ADDRESS OF UPT                  87303
         ST    R15,GCPPL+CPPLUPT-CPPL PUT IN CPPL
         L     R15,PSATOLD-PSA     GET ADDRESS OF MY TCB             P3
         L     R15,TCBJSCB-TCB(R15) GET ADDRESS OF MY JSCB           P3
         L     R15,JSCBPSCB-IEZJSCB(,R15) GET ADDRESS OF MY PSCB     P3
         ST    R15,GCPPL+CPPLPSCB-CPPL PUT IN CPPL
         LPPA  ECT,PARMR=R2  GET ADDRESS OF ECT                  87303
         ST    R15,GCPPL+CPPLECT-CPPL PUT IN CPPL
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       GET ASID FOR LATER.                                          P3
*                                                                    P3
*********************************************************************P3
         L     R15,PSAAOLD-PSA     GET ADDRESS OF MY ASCB          P3.2
         UNPK  WORKAREA(3),ASCBASID+1-ASCB(2,R15) UNPACK             P3
         TR    WORKAREA(2),TRTABLE-240 CONVERT TO EBCDIC             P3
         MVC   GASID,WORKAREA      COPY TO DYNAMIC AREA              P3
         MVC   GTIMRWTO+76(2),GASID MOVE ASID TO TIMER WTO         P3.1
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       CLEAR TERMINAL BUFFERS.                                      P3
*                                                                    P3
*********************************************************************P3
*    IN BATCH MODE, TCLEARQ GOES INTO A LOOP IN EFT02/EFT09      92077
*    DON'T NEED IT EVER ???                                      92077
*        TCLEARQ INPUT             CLEAR TERMINAL BUFFERS            P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       IF DISPLAY TERMINAL, CLEAR THE SCREEN.                       P3
*                                                                    P3
*********************************************************************P3
         GTSIZE ,                  DETERMINE TERMINAL TYPE           P3
         CH    R0,=H'1'      WAS THIS A DISPLAY                  87303
         BNH   HSKPCLR             NO - CONTINUE                 87303
         TMSG  CLRSCRN,,FULLSCR  CLEAR THE SCREEN                87303
         OI    GBITS2,GDISPLAY     INDICATE DISPLAY TERMINAL         P3
HSKPCLR  DS    0H                                                    P3
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*       RETURNS TO THE MAINLINE.                                      *
*                                                                     *
***********************************************************************
HSKPEXIT DS    0H
         L     R14,HSKPSAVE        RESTORE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*       INFORM USER THAT ESTAE RETURN CODE WAS NOT ZERO AND DENY      *
*       LOGON.                                                        *
*                                                                     *
***********************************************************************
HSKPNSTA DS    0H                  NON-ZERO RETURN CODE FROM ESTAE
         LR    R2,R15              PROTECT RETURN CODE
         MVC   GESTAMSG(L'ESTAEMSG),ESTAEMSG COPY MESSAGE
         CVD   R15,WORKAREA        CONVERT TO DECIMAL
         OI    WORKAREA+7,X'0F'    FIX SIGN
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK
         MVC   GESTAMSG+24(4),WORKAREA+1 MOVE INTO MESSAGE
         LA    R0,L'ESTAEMSG       GET LENGTH OF MESSAGE
         LA    R1,GESTAMSG         POINT TO MESSAGE
         B     MSGDENY       RETURN WITH CODE IN R2              87303
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       INFORM USER THAT STAX RETURN CODE WAS NOT ZERO AND DENY      P3
*       LOGON.                                                       P3
*                                                                    P3
*********************************************************************P3
HSKPNSTX DS    0H                  NON-ZERO RETURN CODE FROM STAX    P3
         LR    R2,R15              PROTECT RETURN CODE               P3
         MVC   GSTAXMSG(L'STAXMSG),STAXMSG COPY MESSAGE              P3
         CVD   R15,WORKAREA        CONVERT TO DECIMAL                P3
         OI    WORKAREA+7,X'0F'    FIX SIGN                          P3
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK                      P3
         MVC   GSTAXMSG+23(4),WORKAREA+1 MOVE INTO MESSAGE           P3
         LA    R0,L'STAXMSG        GET LENGTH OF MESSAGE             P3
         LA    R1,GSTAXMSG         POINT TO MESSAGE                  P3
         B     MSGDENY       RETURN WITH CODE IN R2              87303
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*       INFORM USER THAT LOGON HAD A RESOURCE FAILURE AND DENY LOGON. *
*                                                                     *
***********************************************************************
HSKPRESC DS    0H
         LA    R0,L'RESCFAIL       GET LENGTH OF MESSAGE
         LA    R1,RESCFAIL         POINT TO MESSAGE
         B     CC4DENY       WRITE TO USER AND FAIL              87303
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*       INFORM USER THAT LOGON HAD AN ABEND AND DENY LOGON.           *
*                                                                     *
***********************************************************************
HSKPABND DS    0H
         LA    R0,L'LOGABEND       GET LENGTH OF MESSAGE
         LA    R1,LOGABEND         POINT TO MESSAGE
         B     CC4DENY       INFORM USER AND FAIL                87303
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*       INFORM USER THAT OPEN FAILED AND DENY LOGON.                  *
*                                                                     *
***********************************************************************
HSKPNOPN DS    0H                  SYS1.UADS DID NOT OPEN
         LA    R0,L'OPENMSG        GET LENGTH OF MESSAGE
         LA    R1,OPENMSG          POINT TO MESSAGE
CC4DENY  LA    R2,4          SET RETURN CODE                     87303
MSGDENY  TPUT  (1),(0),R     ISSUE MESSAGE                       87303
COMDENY  LR    R15,R2        SET RETURN CODE                     87303
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     HSKPEXIT            EXIT
         TITLE 'L E X L O G O N  ***  FULL-SCREEN LOGON'         87309
***********************************************************************
*                                                                     *
*        FULLSCRN - HANDLES COMPLETE LOGON WITH FULL-SCREEN I/O       *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87309
NTOPARSE DS    0H            USE ALL BUT PAGE I/O IN COMMON      87360
FULLSCRN ST    R14,HSKPSAVE  SAVE RETURN ADDRESS                 87309
         SPACE 1                                                 87360
***********************************************************************
*                                                                     *
*        INITIALIZE AND SET ENVIRONMENTAL VARIABLES                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87360
         SCROPEN TERMINAL,OPT=(NOWTO,SA)    INITIALIZE           87317
*        NOTE THAT 'SA' REQUESTS ONLY SET ATTRIBUTE, RATHER THAN SFE
*        (START FIELD EXTENDED).  SOMEONE TRANSLATES SFE PAIR COUNTS
*        (HEX 02 AND 03) INTO COLONS!                            87317
         CH    R15,=H'4'     PROBLEM ?                           87309
         BH    FULLRSRC      YES; RESOURCE FAILURE               87309
         MVI   FSFLAGS,FSONCE  SET FIRST TIME FLAG               87333
         XC    ERRCOUNT,ERRCOUNT                                 87333
         XC    FDW(MAPFID-FDW),FDW  CLEAR ALL CONTROL INFORMATION
*DEFER   LA    R1,GCPECB     GET THE ATTENTION ECB               90301
*DEFER   XC    0(4,R1),0(R1) CLEAR IT                            90301
*DEFER   ST    R1,FDWATECB   PASS TO SUBTASK                     90301
         LA    R14,LISTFD    POINT TO FD LIST                    87309
         LA    R15,FDINWORK  POINT TO INPUT AREA                 87313
         LA    R0,FDINWORX-FDINWORK  WORK AREA LENGTH            87309
         STM   R14,R0,FDWFDA  MAKE CALLING PARAMETER LIST        87309
         LA    R0,PARSFULL   GET LINE MODE PARSE TABLE           87312
         ST    R0,FDWSCAC    SET CONTROL ADDRESS                 87312
         LA    R0,GMSGAREA   GET ERROR MESSAGE AREA              87360
         ST    R0,FDWSCMSG   USE FOR SCANNER MESSAGES            87360
         ICM   R7,15,ADDRLOUD  GET LOCAL ONLINE USER DATA        87309
         BZ    FULLRSRC      OOPS                                87309
         MVI   ERRINDEX,ERXTOOM  PRESET FOR TOO MANY BAD PASSWORDS
         L     R15,CVTPTR                                        87314
         L     R15,CVTSMCA-CVTMAP(R15)                           87314
         MVC   GSYSID,16(R15)  SMF SYSTEM ID                     87314
         USING A$LSECT,R7    DECLARE IT                          87309
         MVI   YNMAIL,C'Y'                                       87334
         TM    GECTSWS,ECTNMAL  NO MAIL ?                        87334
         BZ    *+8           NO                                  87334
         MVI   YNMAIL,C'N'                                       87334
         MVI   YNNOTE,C'Y'                                       87334
         TM    GECTSWS,ECTNNOT  NOTICES ?                        87334
         BZ    *+8           YES                                 87334
         MVI   YNMAIL,C'N'   NO                                  87334
         MVI   YNRECON,C'?'  PRESET                              92114
         MVI   YNIBM,C'?'                                        92114
         MVI   YN2SES,C'?'                                       92114
         MVI   YNUPDAT,C'?'                                      92114
         LPPA  SW,PARM=LOAD                                      87334
         TM    SW1-SWDSECT(R15),SWENQBD  UID ENQUEUE FAILED ?    87334
         BZ    *+8           NO; MULTIPLE SESSIONS POSSIBLE      87334
         MVI   YN2SES,C'N'   NO MULTIPLE SESSIONS                93251
         SPACE 1                                                 87359
***********************************************************************
*                                                                     *
*        RESTART POINT FOR LOGON (TTY) OR CLEAR (CRT) REQUESTS        *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87360
FULLINIT MVI   FDWOPT,FDWMKEEP+FDWKEY12 - KEEP DATA AFTER MOVE   88214
         SCRINIT FDW         CLEAR EVERYTHING                    87312
         TIME  ,             GET THE TIME AND DATE               87314
         STM   R0,R1,CURRTIME  STASH THEM                        87314
         SLR   R2,R2         CLEAR ERROR MESSAGE LENGTH          87361
         IC    R3,FSFLAGS    SAVE FLAGS                          91007
         MVI   FSFLAGS,FSUID   FORCE FULL EXPANSION              91007
         SCRLIST FDW         MAKE WORK AREA ATB ADDRESSES        87314
         MVC   SVPATB,FPSATB SAVE PSWD ATTRIBUTE ADDRESS         90301
         STC   R3,FSFLAGS    FIX FLAGS AGAIN                     91007
         L     R14,GCPPL+CPPLCBUF-CPPL  GET COMMAND BUFFER       87312
         LH    R15,0(,R14)   GET BUFFER LENGTH                   87312
         LA    R14,4(,R14)   BUFFER TEXT                         87312
         SH    R15,=H'4'     LESS LENGTH OF LENGTH               87312
         BNP   FULLSHOT      NOTHING TO PARSE                    87312
         STM   R14,R15,FDWSCAN  INIT SCAN ADDRESS AND LENGTH     87312
         SCREDIT FDW         DO BACKSPACE EDITING                88340
         SCRSCAN FDW         DO SIMPLE SCAN                      87312
         LR    R3,R15        SAVE RETURN CODE                    87360
         OI    FDWOPT,FDWOKEEP  KEEP INPUT FIELDS                87313
         SCRMOVE FDW         COPY ACCEPTED PARAMETERS            87314
         CLC   KLOGOFF,DB    LOGOFF REQUEST                      88002
         BE    FULLBUCK      EXIT QUIETLY                        87313
         BXH   R3,R3,FULLNTOS  MAKE ERROR MESSAGE                87360
         TM    FIDFG,FIDFTXT ANY USER-ID ?                       87313
         BNZ   FULLSHOW      YES; DISPLAY IT                     87359
         TM    A$LFLG1,A$LFGOK+A$LFGAL  USER WITH AUTO-LOGON ?   87362
         BNO   FULLNDID      NO; PROMPT                          87359
*NO?     CLI   GTERMID,C'L'  LOCAL TERMINAL ?                    87360
*NO?     BNE   FULLNDID      NO; DON'T AUTO                      87360
         LA    R1,A$LCACT    POINT TO CURRENT ACCOUNT            88185
         CLI   0(R1),C'A'    WAS IT PRESET ?                     92300
         BNL   *+8           YES                                 88185
         LA    R1,A$UACCT    POINT TO DEFAULT ACCOUNT            88185
         MVC   FACTEXT,0(R1) MOVE ACCOUNT                        88185
         MVC   FIDTEXT,A$UUID  COPY USER ID                      87359
         MVI   FIDFG,FIDFTXT SHOW TEXT PRESENT                   87359
         MVI   FACFG,FACFTXT  DITTO                              87359
         MVI   FPSFG,FPSFTXT  FAKE PASSWORD                      87359
         MVI   FIDLEN,8                                          87359
         MVI   FACLEN,8      SET FOR AUTO-LOGON                  87359
         MVI   FPSLEN,8                                          87359
         MVC   A$SFCTN(3),=AL1(A$SFENCR,A$STSO,A$SUKEY)          87360
         MVC   A$SUID,FIDTEXT                                    87360
         MVC   A$SPSWD,A$UPSWD                                   87360
         ACCTSVC A$SVCRB                                         91003
         MVC   FPSTEXT,A$SPSWD                                   87360
         B     FULLNUSR      TRY TO PROCESS IT                   87360
         SPACE 2                                                 87309
***********************************************************************
*                                                                     *
*        HANDLE (POSSIBLE) NEW USER-ID AND (RE)PROMPT FOR OTHER STUFF *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87360
FULLBMRK NI    FDWOPT,255-FDWMKDEL  KEEP NON-ESSENTIALS          87360
         SCRMARK FDW         MAKE ERROR MESSAGE/SET CURSOR       87360
         NEWPASS ,           TEST AND RESET NEW PASSWORD         88340
FULLBMSG CLI   GMSGAREA+1,0  ANY MESSAGE ?                       87333
         BH    FULLSHOW                                          87333
         MVI   GMSGAREA+1,32 FAKE IT                             87333
         MVC   GMSGAREA+4(28),=C'Missing or invalid parameter'   87333
FULLSHOW SCRINIT FDW         (RE-)INITIALIZE THE PAGE            87309
         OI    FDWOPT,FDWOKEEP  KEEP INPUT FIELDS                87313
FULLSHOT TM    GBITS1,GATTNHIT  USER IMPATIENT ?                 87309
         BNZ   FULLBUCK      YES; GET OUT                        87309
         TM    FIDFG,FIDFTXT ANY UID ?                           87312
         BZ    FULLLIST      NOT YET - NEED IT                   87312
         CLC   KLOGOFF,FIDTEXT  USER CHANGED INTENTIONS ?        90301
         BE    FULLBACK      YES; QUIT UNGRACEFULLY              90301
         CLC   A$UUID,FIDTEXT  UID CHANGED ?                     87312
         BNE   FULLNUSR      YES; GET NEW STUFF                  87309
         TM    A$LFLG1,A$LFGOK  VALID ENTRY ?                    87309
         BZ    FULLNUSR      NO; REREAD                          87309
         TM    FSFLAGS,FSUID  USER-ID FOUND ?                    87309
         BNZ   FULLLIST      YES; NO NEED TO DO IT AGAIN         87309
FULLNUSR NI    FSFLAGS,255-FSUID-FSACCT-FSPASS-FSNPASS  RESET    87309
         NI    FDWOPT,255-FDWLOCK  LOCK PRIVILEGED FIELDS        87314
         CLI   FIDTEXT,C' '  IS THERE ONE ?                      87312
         BNH   FULLLIST      NO; PROMPT FOR IT                   87309
         CLI   GMSGAREA+1,0  ANY ERROR MESSAGE TO BE SHOWN ?     88329
         BH    *+10          HOPE THIS DOESN'T PUT USER IN LOOP  88329
         XC    A$SERMSG,A$SERMSG  PREVENT MSG ERASURE            88330
         MVC   A$SUID,FIDTEXT  MOVE USER-ID                      87312
         MVC   A$SFCTN(3),=AL1(A$SFREAD,A$STSO,A$SUKEY+A$SBUILD) 87360
         MVC   A$SBUFR,ADDRLOUD   PASS RECORD BUFFER             87309
         LA    R0,A$USIZE    GET SIZE OF USER RECORD IN LOUD     87309
         STH   R0,A$SBUFL    PASS IT                             87309
         ACCTSVC A$SVCRB     CALL THE SVC                        91003
         LA    R0,GMSGAREA   GET MESSAGE AREA                    88330
         ST    R0,A$SERMSG   RESTORE PARAMETER LIST              88330
         BXH   R15,R15,FULLBDID  PROCEED WITH BAD ID             87309
         ICM   R0,OOII,SVPATB  PSWD ATB ADDRESS SET ?            90301
         BZ    *+8           NO                                  87360
         STCM  R0,OOII,FDWCUR PLACE CURSOR IN PASSWORD           87360
***********************************************************************
*                                                                     *
*        CHECK FOR RECONNECT AND DUPLICATE REQUESTS                   *
*                                                                     *
***********************************************************************
         L     R15,CVTPTR    GET THE CVT BACK                    87359
         L     R15,CVTASVT-CVTMAP(,R15) GET THE ADD.SPC.VECTOR   87359
         USING ASVT,R15                                          87359
         SLR   R14,R14       ASID                                87359
LOOPASID LA    R14,1(,R14)   NEXT ASID                           87359
         C     R14,ASVTMAXU  VALID ?                             87359
         BH    DONEASID      NO                                  87359
         LR    R1,R14        COPY THE ASID                       87359
         SLL   R1,2          CHANGE INTO OFFSET                  87359
         LA    R1,ASVTFRST(R1) POINT TO ASCB SLOT                87359
         ICM   R1,15,0(R1)   IS THERE AN ASCB ?                  87359
         BNP   LOOPASID      NO; TRY NEXT                        87359
         USING ASCB,R1       DECLARE IT                          87359
         ICM   R2,15,ASCBTSB  TSB ?                              87359
         BZ    LOOPASID      NO                                  87359
         USING TSB,R2                                            87359
         ICM   R3,15,ASCBCSCB                                    87359
         BZ    LOOPASID                                          87359
         USING CHAIN,R3                                          87359
         CLC   FIDTEXT(7),CHKEY  SAME USER ID ?                  87359
         BNE   LOOPASID      NO                                  87359
         MODESET MF=(E,KEYZERO) NEED FOR TSB                     88002
         LA    R3,YNRECON   SET FOR RECONNECT                    87360
         TM    TSBSTAT,TSBDISC  DISCONNECTED ?                   87359
         BO    *+8           YES                                 87359
         LA    R3,YN2SES     ELSE SET DUPLICATE                  87360
         MODESET MF=(E,NONZERO)  RESTORE MY KEY                  88002
         MVI   0(R3),C'Y'    SET 'DEFAULT' OPTION                87360
         DROP  R15,R1,R2,R3                                      87359
DONEASID DS    0H                                                87359
***********************************************************************
*                                                                     *
*        NEW USER-ID: SET DEFAULT VALUES FOR NON-ESSENTIAL OPTIONS    *
*                                                                     *
***********************************************************************
         TM    GBITS2,GMEMENQ  PRIOR MEMBER ENQUEUED ?           87361
         BZ    FULLNENQ      NO                                  87361
         DEQ   RET=HAVE,MF=(E,GENQLIST)  FREE THE OLD ENTRY      87361
         NI    GBITS2,255-GMEMENQ                                87361
FULLNENQ LA    R15,A$USIZE                                       87333
         LR    R1,R15                                            87333
         LA    R14,U$ER      POINT TO LOCAL COPY                 87333
         LA    R0,A$LUREC    GET CSA COPY                        87333
         MVCL  R14,R0        COPY OVER                           87333
         FDINIT FAC,L$UACCT  PRIME ACCOUNT NUMBER                87333
         FDINIT FPR,L$UTSPRC,=CL8'SESSION'  PROCEDURE            87359
*        FDINIT FDE,L$UPROUT  DESTINATION                        87314
         FDINIT FMC,L$UMSGCL,=C'Z'  MSGCLASS DEFAULT IS Z        87314
         FDINIT FML,GMSGLEVL,=C'1,1'  MSGLEVEL                   87314
         FDINIT FGE,L$UTSGEN,=CL8'TSO'  UNIT= GENERIC            87314
         FDINIT FCM,L$UTSCMD  INITIAL COMMAND                    87314
         TM    L$UPRIVY,L$UPFSYS  SYSTEMS PROGRAMMER ?           87359
         BZ    FULLHGEN      NO                                  87314
         OI    FDWOPT,FDWLOCK  UNLOCK PRIVILEGED FIELDS          87314
FULLHGEN OI    FSFLAGS,FSUID  SHOW USER-ID PRESENT               87309
         B     FULLLIST                                          87309
FULLBDID OI    FIDFG,FIDFTXT+FIDFERR                             87313
         OI    FDWFG,FIDFTXT+FIDFERR                             87313
         INC   ERRCOUNT,WORK=R15  COUNT BAD LOGONS               87333
         CH    R15,=Y(MAXERR)  TOO MANY ?                        87333
         BH    FULLBACK      YES                                 87333
FULLLIST TIME  ,             GET THE TIME AND DATE               87314
         STM   R0,R1,CURRTIME  STASH THEM                        87314
         SCRMARK FDW         LOCATE AND FLAG MISSING FIELDS      87314
         NEWPASS ,           TEST AND RESET NEW PASSWORD         88340
         SLR   R2,R2                                             87361
         ICM   R2,OOII,GMSGAREA  GET ERROR MESSAGE LENGTH        87361
         SH    R2,=H'4'      GET TRUE LENGTH                     87361
         BNM   *+6           ZERO OR PLUS                        87361
         SLR   R2,R2         ELSE MAKE ZERO                      87361
         SCRLIST FDW         FORMAT THE PAGE                     87309
         TM    FSFLAGS,FSONCE+FSUID  FIRST TIME WITH UID ?       87333
         BNO   FULLLONE      NO                                  87333
         TM    FDWFG,FIDFERR  ANY ERROR ?                        87333
         BNZ   FULLLONE      NO (MISSING REQ. FIELD IS ERROR)    87333
         NI    FSFLAGS,255-FSONCE  RESET FIRST TIME              87333
         B     FULLONCE      SKIP FIRST I/O                      87333
FULLLONE NI    FSFLAGS,255-FSONCE  RESET FIRST TIME FLAG         87333
         TM    GBITS2,GDISPLAY  GOING TO CRT ?                   87360
         BNZ   FULLCRT       YES                                 87360
         SPACE 1                                                 87360
***********************************************************************
*                                                                     *
*        TTY / NTO  INPUT PROCESSING                                  *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87360
         SLR   R0,R0                                             87360
         ICM   R0,OOII,GMSGAREA  CANNED MESSAGE ?                87360
         BZ    NTONMSG       NO                                  87360
         SH    R0,=H'4'      TRUE LENGTH                         87360
         LA    R4,PARSUID    SET FOR USER-ID PARSE               87360
         LA    R1,GMSGAREA+4                                     87360
         TPUT  (1),(0),R     WRITE ERROR MSG                     87360
NTONMSG  CLI   YNHELP,C'Y'   USER NEEDS HELP ?                   88221
         BNE   NTONHLP       NO                                  88221
         MVI   YNHELP,C'N'   RESET                               88221
         LA    R3,NTOHELPL   GET HELP LENGTH                     88221
         LA    R2,NTOHELP    AND HELP TEXT                       88221
NTOHLP   LR    R1,R2         SAVE START OF MESSAGE               88221
         SLR   R0,R0         SET LENGTH                          88221
NTOHLP1  CLI   0(R2),X'15'   NL ?                                88221
         BE    NTOHLP2       YES                                 88221
         AH    R0,=H'1'      UP LENGTH                           88221
         LA    R2,1(,R2)                                         88221
         BCT   R3,NTOHLP1                                        88221
NTOHLP2  TPUT  (1),(0),R     WRITE A LINE                        88221
         LTR   R3,R3         ANY MORE ?                          88221
         BNP   NTONHLP       NO                                  88221
         CLI   0(R2),X'15'   NL ?                                88221
         BNE   NTOHLP        NO                                  88221
         LA    R2,1(,R2)                                         88221
         BCT   R3,NTOHLP                                         88221
NTONHLP  LA    R1,PROMUID    POINT TO USER-ID PROMPT             87360
         TM    FIDFG,FIDFTXT+FIDFERR  NEED UID ?                 87360
         BNM   NTOGET        YES                                 87360
         LA    R4,PARSPSWD   SET FOR PASSWORD PARSE              87360
         LA    R1,PROMPSWD   ELSE TRY PASSWORD                   87360
         TM    FPSFG,FPSFTXT+FPSFERR  PSWD ?                     87360
         BNM   NTOGET        YES                                 87360
         LA    R1,PROMNPSW                                       87360
         LA    R4,PARSNPSW   SET FOR NEW PASSWORD                87360
         TM    FNPFG,FNPFERR  IN ERROR ?                         87360
         BNZ   NTOGET        YES; IF SPECIFIED, MUST BE CORRECT  87360
         LA    R1,PROMANY                                        87360
         LA    R4,PARSREST   PARSE KEYWORD                       87360
NTOGET   SLR   R2,R2                                             87360
         IC    R2,0(,R1)     GET PROMPT LENGTH                   87360
         LA    R3,1(,R1)     AND LENGTH                          87360
         TPUT  (R3),(R2),ASIS  DON'T CR                          87360
         ST    R4,FDWSCAC    SET SCANNER LIST START              87360
         LA    R1,GCBUF+CBUFTEXT-CBUF                            87360
         LA    R0,L'GCBUF-(CBUFTEXT-CBUF)                        87360
         ST    R0,FDWSCAN+4  SET SCANNER LENGTH                  87360
         ST    R1,FDWSCAN    AND ADDRESS                         87360
         BAL   R14,GETSHORT  GET SHORT RESPONSE                  87360
         BXH   R15,R15,FULLRSRC  RESOURCE ERROR ?                87360
         ST    R1,FDWSCAN+4  SET SCAN LENGTH                     87360
         SCREDIT FDW         DO BACKSPACE EDITING                88340
         ICM   R1,15,FDWSCAN+4  RESTORE LENGTH AFTER EDITING     91007
         BP    NTOGETH       INPUT; CHECK FOR HELP               88340
         NEWPASS MODE=ERRESET  RESET NEWPASS REQUEST IF ERROR    88340
         B     FULLJOIN      AND ANALYZE INPUT                   88340
NTOGETH  CH    R1,=H'1'      LENGTH=1 ?                          88221
         BE    QHELP         TEST FOR ?                          88221
         CH    R1,=H'4'      LENGTH=4 ?                          91007
         BNE   NHELP         NO                                  88221
         CLC   =C'HELP',GCBUF+CBUFTEXT-CBUF  HELP ?              88221
         BE    YHELP         YES                                 88221
         BNE   NHELP         NO                                  88221
QHELP    CLI   GCBUF+CBUFTEXT-CBUF,C'?'  SHORT HELP ?            88221
         BNE   NHELP         NO                                  88221
YHELP    MVI   YNHELP,C'Y'   REQUEST HELP                        88221
         B     NTONMSG       REPEAT                              88221
NHELP    LA    R2,8          PRESET ERROR CODE                   87360
         TM    GBITS1,GATTNHIT  PA1 INTERRUPT ?                  87360
         BNZ   FULLBUCK      YES; GET OUT                        87360
         XC    GMSGAREA,GMSGAREA                                 87360
*WHY*    OI    FDWOPT,FDWMKDEL  IGNORE INVALID NON-ESSENTIALS    87360
         XC    DB,DB         CLEAR ESCAPE CLAUSE                 87360
         SCRSCAN FDW         DO SIMPLE SCAN                      87360
         CLC   KLOGOFF,DB    IMPATIENT USER ?                    88002
         BE    FULLBUCK      YES; EXIT NOW                       87360
         CLC   KLOGON,DB     RESTART ?                           88002
         BE    FULLINIT      YES; START ALL OVER                 87360
         CH    R15,=H'8'     SCAN ERROR ?                        87360
         BNE   FULLJOIN      NO; JOIN POST-PROCESSING            87360
FULLNTOS MVC   GMSGAREA+4(10),=C'Invalid : '                     87360
         L     R0,FDWSCAN    GET SCANNER RESTART                 87360
         LA    R1,GCBUF+L'GCBUF-1                                87360
         SR    R1,R0         LENGTH TO COPY                      87360
         ICM   R1,IOOO,BLANKS  SET BLANK PADDING                 88227
         LA    R14,GMSGAREA+14                                   87360
         LA    R15,L'GMSGAREA-14                                 87360
         MVCL  R14,R0        MOVE BAD INPUT                      87360
         MVI   GMSGAREA+1,71 SET MAX WIDTH                       87360
         B     FULLLONE      REDISPLAY WITH ERROR                87360
         SPACE 1                                                 87360
***********************************************************************
*                                                                     *
*        CRT (FULL-PAGE) INPUT                                        *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87360
FULLCRT  SCRPAGE FDW         DISPLAY THE PAGE AND SOLICIT INPUT  87309
         LTR   R15,R15       CHECK RETURN CODE                   87309
         BNM   FULLINP       HAVE INPUT - CHECK IT               87309
         CLI   FDWIAID,1     CHECK ERROR CONDITION               87309
         BNL   FULLRSRC      CAN'T HANDLE                        87309
         WTO   MF=(E,TTIMRWTO)  WRITE MESSAGE                    87309
         MVI   ERRINDEX,ERXTOOL  SET MESSAGE INDEX (WON'T SHOW?) 88002
         ABEND 522           QUIT ON TIME-OUT                    87309
FULLINP  LA    R5,FDWICOD    POINT TO CONVERTED AID              87309
         OI    FDWOPT,FDWOKEEP  KEEP INPUT                       87309
         BLOOK T=QUICKIES,R=FDWICOD  LOOK FOR NON-DATA FUNCTIONS 87309
FULLONCE XC    GMSGAREA,GMSGAREA  RESET ERROR MESSAGES           87309
         SCRANAL FDW         ANALYZE INPUT                       87309
         SPACE 1                                                 87360
***********************************************************************
*                                                                     *
*        INPUT PROCESSING AND VERIFICATION                            *
*                                                                     *
***********************************************************************
FULLJOIN TM    FIDFG,FIDFTXT+FIDFERR  INPUT AVAILABLE ?          87313
         BNM   FULLTUID      NO; PROMPT FOR IT                   87313
         CLC   KLOGOFF,FIDTEXT  USER CHANGED INTENTIONS ?        90301
         BE    FULLBACK      YES; QUIT UNGRACEFULLY              90301
         MVC   L$UUID,FIDTEXT  COPY (FOR ABEND & OTHER MSGS)     87313
         MVC   LUSERID+L'LUSERID-1,FIDLEN  ALSO COPY LENGTH      87313
         B     FULLHUID      CONTINUE                            87313
FULLTUID MVC   FDWCUR,FIDATB PUT CURSOR IN USERID FIELD          87309
FULLNDID OI    FIDFG,FIDFTXT+FIDFERR                             87313
         B     FULLMARK      REPEAT THE DISPLAY                  87314
FULLHUID TM    FPSFG,FPSFTXT+FPSFERR  PASSWORD ?                 88002
         BNM   FULLHOPS      YES                                 88002
         ICM   R0,OOII,SVPATB  PSWD ATB SAVED ?                  91007
         BNZ   *+8           YES                                 91007
         ICM   R0,OOII,FPSATB  ELSE GET CURRENT                  91007
         STCM  R0,OOII,FDWCUR  PUT CURSOR THERE UNLESS OTHER ERROR
FULLHOPS TM    FACFG,FACFTXT+FACFERR   DITTO FOR ACCOUNT ?       87313
         BNM   FULLBAC       NEED ACCOUNT                        87313
         MVC   DB,=X'C0F0F0F0C0C0C0C0'  MAKE ACCOUNT PATTERN     92300
         NC    DB,FACTEXT    MASK ZONES                          87312
         CLC   DB,=X'C0F0F0F0C0C0C0C0'  VALID ?                  92300
         BNE   FULLBAC       NO; ACCOUNT BAD                     87312
         CLI   FACLEN,8      LENGTH=8 ?                          87310
         BE    FULLHACT      YES; PROCEED                        87314
FULLBAC  OI    FACFG,FACFTXT+FACFERR SET ERROR                   87313
FULLHACT DS    0H                                                87314
FULLMARK SCRMARK FDW         CHECK FOR BAD AND OMITTED FIELDS    87314
         NEWPASS ,           TEST AND RESET NEW PASSWORD         88340
         OI    FDWOPT,FDWOKEEP  KEEP INPUT                       87309
         SCRMOVE FDW         MERGE                               87309
         CLI   YNIBM,C'Y'    BYPASS OUR LOGON ?                  87333
         BE    BYPASS        YES; SKIP EVERYTHING                87333
         TM    FDWFG,FIDFERR  ANY ERROR ?                        87314
         BNZ   FULLBMSG      YES; MAKE USER FIX IT               87309
         SLR   R0,R0                                             87333
         ICM   R0,3,L$UTSRGN DID USER SPECIFY A REGION ?         87333
         BZ    FULLHRGN      NO; ACCEPT IT                       88002
         AM31  ,                                                 92300
         L     R1,CVTPTR                                         88002
         L     R1,CVTGDA-CVTMAP(,R1)  GET GLOBAL DATA AREA       88002
         L     R1,PASIZE-GDA(,R1)  GET PRIVATE AREA SIZE         88002
         AM24  WORK=R15                                          92300
         SRL   R1,10         CONVERT TO K                        88002
         SH    R1,=Y(3*64)   FINAGLE FOR LSQA, ETC.              88002
         CR    R0,R1         VALID SIZE ?                        88002
         BNH   FULLHRGN      OK                                  87333
         STCM  R1,OOII,L$UTSRGN  SET TO LEGAL MAX                88002
         MVI   FRGFG,0       FORCE FIELD REFRESH                 87333
         MVI   FRGLEN,0                                          88002
         OI    FDWFG,FIDFERR SET ERROR                           87333
         MVC   FDWCUR,FRGATB PUT CURSOR IN IT                    87333
FULLHRGN ICM   R0,3,L$UTSTIM GET CPU TIME                        87333
         CH    R0,=Y(1439)   LEGAL ?                             87333
         BNH   FULLHTIM      YES                                 87333
         OI    FCPFG,FCPFERR+FCPFTXT                             87333
         OI    FDWFG,FIDFERR SET ERROR                           87333
         MVC   FDWCUR,FCPATB PUT CURSOR IN IT                    87333
FULLHTIM CLI   L$UMSGCL,C' ' ANY MESSAGE CLASS ?                 88002
         BNH   FULLHMGC      NO; OK                              88002
         CLI   L$UMSGCL,C'A' ALPHA ?                             88002
         BL    FULLBMGC      NO; ERROR                           88002
         CLI   L$UMSGCL,C'9' ALPHAMERIC ?                        88002
         BNH   FULLHMGC      YES; ACCEPT                         88002
FULLBMGC OI    FMCFG,FMCFERR+FMCFTXT  FLAG ERRONEOUS             88002
         OI    FDWFG,FMCFERR+FMCFTXT                             88002
         MVC   FDWCUR,FMCATB                                     88002
FULLHMGC CLI   GMSGLEVL,C' '  ANY MSGLEVEL ?                     88002
         BNH   FULLHMGL      NO                                  88002
         CLI   GMSGLEVL,C'0'  VALID NUMERIC ?                    88002
         BL    FULLBMGL      NO                                  88002
         CLI   GMSGLEVL,C'2'  VALID ?                            88002
         BH    FULLBMGL                                          88002
         CLI   GMSGLEVL+1,C' '  MORE THAN ONE ?                  88002
         BNH   FULLHMGL      NO                                  88002
         CLI   GMSGLEVL+1,C','  VALID ?                          88002
         BNE   FULLBMGL      NO                                  88002
         CLI   GMSGLEVL+2,C'0'  VALID SECOND NUMERIC ?           88002
         BL    FULLBMGL      NO                                  88002
         CLI   GMSGLEVL+2,C'1'  VALID ?                          88002
         BNH   FULLHMGL      YES                                 88002
FULLBMGL OI    FMLFG,FMLFERR+FMLFTXT  FLAG ERRONEOUS             88002
         OI    FDWFG,FMLFERR+FMLFTXT                             88002
         MVC   FDWCUR,FMLATB                                     88002
FULLHMGL DS    0H            CHECK UNIT AND PROC ?               88002
FULLNESS TM    FDWFG,FIDFERR ANY ERROR ?                         87333
         BNZ   FULLBMRK      YES; REDISPLAY                      87333
         CLI   YNHELP,C'Y'   DISPLAY HELP TEXT ?                 88221
         BE    FULLBMRK      YES; REPROCESS                      88221
         SPACE 1                                                 87360
***********************************************************************
*                                                                     *
*        PROCEED WITH LOGON REQUEST - CHECK FOR VALID LOCAL LOGON     *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87360
         TM    GBITS2,GMEMENQ  ALREADY ENQUEUED ?                87361
         BZ    FULLENQU      NO                                  87361
         DEQ   RET=HAVE,MF=(E,GENQLIST)  IN CASE USER CHANGED OPTIONS
         NI    GBITS2,255-GMEMENQ  RESET FLAG                    87361
FULLENQU MVC   GENQLIST(LENQLIST),ENQLIST  MAKE ENQ TEST         87361
         CLI   YNRECON,C'Y'  RECONNECT ?                         87361
         BNE   FULLENQC                                          87361
         ENQ   (QNAME,L$UUID,S,7),MF=(E,GENQLIST)  ENQUEABLE ?   87361
         BXH   R15,R15,FULLENQD   NO; RECONNECT MAY BE VALID     87361
*XA-FAIL MVI   YNRECON,C'N'  CANCEL RECONNECT REQUEST            87361
         OI    YNRECON,0     *****DEBUG*****KEEP FOR ZAPPING     93252
FULLENQC CLI   YN2SES,C'N'   RUN WITHOUT UADS ?                  87361
         BE    FULLENQO      YES; EXCLUSIVE                      87361
         ENQ   (QNAME,L$UUID,S,7),RET=USE,MF=(E,GENQLIST)  SHARE 87361
         LTR   R15,R15       MAY WE SHARE ?                      87361
         BZ    FULLENQD      HAVE IT                             87361
         CH    R15,=H'8'     OR WE HAVE IT ALREADY ?             87361
         BE    FULLENQD      OK                                  87361
         MVC   GMSGAREA(MSG698LN),MSG698  ENQUEUE FAILED - SHR   87361
         B     FULLSHOW      START OVER AGAIN                    87361
FULLENQO ENQ   (QNAME,L$UUID,E,7),RET=USE,MF=(E,GENQLIST)  EXCLUSIVE
         LTR   R15,R15       DONE ?                              87361
         BZ    FULLENQD      YES                                 87361
         CH    R15,=H'8'     HAVE IT ALREADY ?                   87361
         BE    FULLENQD      SHOULD BE OK ?                      87361
         MVC   GMSGAREA(MSG699LN),MSG699  MAKE ERROR MESSAGE     87361
         B     FULLSHOW      AND RESTART                         87361
FULLENQD OI    GBITS2,GMEMENQ  SHOW MEMBER ENQUEUED UPON         87361
         MVC   A$SUID,L$UUID  MOVE USER-ID                       87309
         MVC A$SFCTN(3),=AL1(A$SFVALD,A$STSO,A$SAKEY+A$SUKEY+A$SBUILD)
         MVC   A$SACCT,L$UACCT SET ACCOUNT                       87333
         MVC   A$SPSWD,GPASS HAVE PASSWORD                       87310
         MVC   A$SBUFR,ADDRLOUD   PASS RECORD BUFFER             87309
         LA    R0,A$USIZE    GET SIZE OF USER RECORD IN LOUD     87309
         STH   R0,A$SBUFL    PASS IT                             87309
         ACCTSVC A$SVCRB     CALL THE SVC                        91003
         BXLE  R15,R15,FULLSOME  WE HAVE A WINNER                87333
         INC   ERRCOUNT,WORK=R15  COUNT FAILURES                 87333
         CH    R15,=Y(MAXERR)  TOO MANY ?                        87333
         BH    FULLBACK                                          87333
         OI    FPSFG,FPSFTXT+FPSFERR  SHOW PASSWORD BAD          87333
         OI    FDWFG,FPSFTXT+FPSFERR                             87333
         ICM   R0,OOII,SVPATB  PSWD ATB SAVED ?                  91007
         BNZ   *+8           YES                                 91007
         ICM   R0,OOII,FPSATB  ELSE GET CURRENT                  91007
         STCM  R0,OOII,FDWCUR  PUT CURSOR THERE UNLESS OTHER ERROR
         B     FULLBMRK      DO SOME SOLICITING                  87333
FULLSOME TM    FNPFG,FNPFTXT NEW PASSWORD REQUESTED ?            87334
         BZ    FULLNNEW      NO                                  87334
         CLI   FNPTEXT,C' '  NULL LENGTH ?                       87361
         BNH   FULLKNEW      YES; JUST RESET IT                  87361
         CLC   GNEWPSWD,GPASS  OLD=NEW PASSWORD ?                88340
         BE    FULLKNEW      YES; IGNORE CHANGE REQUEST          88340
         TM    A$UADMIN,A$UAFPSW  MAY USER CHANGE ?              87360
         BZ    FULLKNEW      NO; KILL REQUEST                    87360
         MVC   A$SFCTN(3),=AL1(A$SFNPSW,A$STSO,A$SUKEY+A$SBUILD) 87334
         MVC   A$SPSWD,GNEWPSWD                                  87334
         ACCTSVC A$SVCRB     REQUEST NEW PASSWORD ASSIGNMENT     91003
         OI    FNPFG,FNPFERR  SET IT BAD                         87360
         BXH   R15,R15,FULLBMRK  NO GOOD                         87334
FULLKNEW NEWPASS MODE=RESET  CLEAR THE REQUEST                   88340
FULLNNEW CLI   YNUPDAT,C'Y'  MAKE CHANGES PERMANENT ?            87334
         BNE   FULLSNEW      NO                                  87334
         TM    A$UADMIN,A$UAFMOD  USER ALLOWED TO MODIFY ?       87360
         BZ    FULLSNEW      NO; IGNORE REQUEST                  87360
         MVC   A$SFCTN(3),=AL1(A$SFLOCK,A$STSO,A$SUKEY+A$SBUILD) 87334
         ACCTSVC A$SVCRB     REREAD AND LOCK THE RECORD          91003
         MODESET MF=(E,KEYZERO)                                  87334
         MVC   A$UTSPRC,L$UTSPRC                                 87334
         MVC   A$UTSTIM,L$UTSTIM                                 87334
         MVC   A$UMSGCL,L$UMSGCL                                 87334
         MVC   A$UTSRGN,L$UTSRGN                                 87334
         MVC   A$UTSGEN,L$UTSGEN                                 87334
         MVC   A$UTSCMD,L$UTSCMD                                 87334
         MODESET MF=(E,NONZERO)                                  87334
         MVI   A$SFCTN,A$SFREP  NOW REPLACE IT                   87334
         ACCTSVC A$SVCRB     REWRITE THE RECORD                  91003
         SPACE 1                                                 87360
***********************************************************************
*                                                                     *
*        DO FINAL CLEAN-UP AND EXIT                                   *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87360
FULLSNEW LA    R15,L$UUID+7  LAST BYTE                           87309
         LA    R14,8         SET LENGTH                          87309
TRAILEE  CLI   0(R15),C' '   TRAILING BLANK ?                    87309
         BH    TRAILER       NO                                  87309
         BCTR  R15,0         GET PRIOR BYTE                      87309
         BCT   R14,TRAILEE   REPEAT                              87309
TRAILER  MIN   R14,=Y(7),TYPE=H  SET USER-ID LENGTH              87309
         STH   R14,LUSERID   SET INDEX LENGTH                    87309
         MVI   L$UUID+7,C' ' MAKE SURE                           87334
         BAL   R14,BUILDJCL  BUILD JCL                           87309
         SLR   R2,R2         CLEAR RETURN CODE                   87309
         CLI   YNMAIL,C'N'   NO MAIL ?                           87334
         BNE   *+8           NO                                  87334
         OI    GECTSWS,ECTNMAL  NO MAIL                          87334
         CLI   YNMAIL,C'Y'   YES MAIL ?                          87334
         BNE   *+8           NO                                  87334
         NI    GECTSWS,255-ECTNMAL                               87334
         CLI   YNNOTE,C'N'   NO NOTE ?                           87334
         BNE   *+8           NO                                  87334
         OI    GECTSWS,ECTNNOT  NO NOTE                          87334
         CLI   YNNOTE,C'Y'   YES NOTE ?                          87334
         BNE   *+8           NO                                  87334
         NI    GECTSWS,255-ECTNNOT                               87334
         CLI   YNRECON,C'N'  CANCEL RECONNECT ?                  87361
         BNE   *+8           NO                                  87361
         NI    GBITS1,255-GRECON  KILL RECONNECT                 87361
         CLI   YNRECON,C'Y'  DID USER REQUEST RECONNECT ?        87315
         BNE   *+8           NO                                  87315
         OI    GBITS1,GRECON  RECONNECT                          87315
         MVC   A$SACCT,=CL8'TSO'  IDENTIFY SUBSYSTEM             87321
         MVI   A$SFCTN,A$SFLOGN  REQUEST SUBSYSTEM LOGON         87321
         MVI   A$SCNTL,A$SUKEY+A$SBUILD+A$SDEBUG                 87326
         XC    A$SPSWD,A$SPSWD  NO DATA AREA                     87321
         ACCTSVC A$SVCRB     PERFORM LOGON                       91003
         MVI   ERRINDEX,0    RESET ERROR MESSAGE                 88002
         CLI   YN2SES,C'N'   RUN WITHOUT UADS ?                  87361
         BNE   FULLSOLO      NO; RUN WITHOUT (UNLESS RECONNECT)  87333
*        DO UADS LOOK-UP LATER                                   87333
         B     FULLSOPT      SKIP AROUND                         87334
FULLSOLO OI    GBITS1,GNOUADS  RUN WITHOUT UADS                  87310
FULLSOPT LPP   SA,PARM=LOAD  GET THE SYSTEM ATTRIBUTE ADDRESS    87310
         MVI   OFFCUR+1(R15),16  SET CURRENT LENGTH (BITS)       87363
         L     R15,OFFADD(,R15)  GET VALUE                       87363
         MVC   0(2,R15),L$UTSPRV  COPY ATTRIBUTES                87310
         NI    0(R15),255-PSCBRRBA  RESET 'INVALID' UADS         87310
         LPP   UA            GET USER ATTRIBUTES                 87310
         MVI   OFFCUR+1(R15),8  SET CURRENT LENGTH               87363
         L     R15,OFFADD(,R15)                                  87363
         MVC   0(1,R15),L$UTSPRV+2  AND OURS                     87310
         LPP   GEN           GET GENERIC UNIT NAME               87310
         MVI   OFFCUR+1(R15),8  SET MAXIMUM LENGTH               87310
         L     R15,OFFADD(,R15)                                  87310
         MVC   0(8,R15),L$UTSGEN  MOVE GROUP                     87333
         LPPA  UPT           GET USER PROFILE TABLE              87310
         USING UPT,R15       DECLARE IT                          87310
         MVI   UPTCDEL,X'16'  CHAR DELETE IS BACK-SPACE          87310
         MVI   UPTLDEL,X'23'  LINE DELETE IS IBM DEFAULT         87310
         OC    UPTSWS,L$UTSPRO  MOVE PROFILE BITS                87310
         MVC   UPTPREFX,L$UUID  MOVE ID                          87310
         MVC   UPTPREFL,LUSERID+L'LUSERID-L'UPTPREFL MOVE LENGTH 87310
         DROP  R15                                               87310
         B     FULLEXIT                                          87309
FULLRSRC LPPA  SW,PARM=LOAD  GET SWITCHES                        87309
         OI    SW1-SWDSECT(R15),SWRESBD  RESOURCE FAILURE        87309
         MVI   ERRINDEX,ERXRESC  FLAG RESOURCE FAILURE MSG       88002
         B     FULLBACK      FAIL BIG                            87309
FULLBUCK MVI   ERRINDEX,0    CANCEL MESSAGE                      88002
FULLBACK LA    R2,8          SET MAJOR ERROR                     87309
         OI    GBITS1,GDENY  INHIBIT LOGON                       87309
FULLEXIT TM    GBITS2,GDISPLAY  GOING TO CRT ?                   87360
         BZ    FULLBOUT      NO; DON'T LITTER                    87360
         TMSG  CLRSCRN,,FULLSCR  CLEAR SCREEN                    87312
FULLBOUT LTR   R15,R2        RETURN ERROR CODE                   87309
         BZ    FULLBRET      OK; RETURN                          87361
FULLBOFF MVC   A$SACCT,=CL8'TSO'  IDENTIFY SUB-SYSTEM            87361
         MVC   A$SFCTN(3),=AL1(A$SFLOGF,A$STSO,A$SUKEY+A$SBUILD) 88227
         XC    A$SPSWD,A$SPSWD                                   87361
         MVC   A$SBUFR,ADDRLOUD                                  87361
         ACCTSVC A$SVCRB,ERR=FULLBOFT  DISCONNECT; DON'T FAIL    91003
FULLBOFT LR    R15,R2        RESTORE RETURN CODE                 91003
FULLBRET TM    GBITS2,GMEMENQ  ENQUEUED ON MEMBER ?              87361
         BZ    FULLSENQ      NO                                  87361
         DEQ   RET=HAVE,MF=(E,GENQLIST)  KILL IT                 87361
         NI    GBITS2,255-GMEMENQ                                87361
         LR    R15,R2        RESTORE RETURN CODE                 87361
FULLSENQ SLR   R1,R1                                             88002
         ICM   R1,OOOI,ERRINDEX  ANY MESSAGE ?                   88002
         BZ    FULLNMSG      NO                                  88002
         SLR   R0,R0                                             88002
         IC    R0,0(,R1)     GET MESSAGE LENGTH                  88002
         ICM   R1,OIII,1(R1) GET MESSAGE ADDRESS                 88002
         TPUT  1(R1),(R0)    WRITE THE MESSAGE                   88002
         LR    R15,R2        RESTORE RETURN CODE                 88002
FULLNMSG L     R14,HSKPSAVE  GET RETURN ADDRESS                  87309
         BR    R14           RTURN WITH CC                       87309
         SPACE 1                                                 87333
BYPASS   OI    GBITS1,GBYPASS  USE IBM'S LOGON                   87333
         SLR   R2,R2                                             87333
         B     FULLBOFF      RETURN                              87333
         SPACE 1                                                 87309
QUICKIES BTAB  *PA1,FULLBACK,BASE=*                              87310
         BTAB  *PA2,FULLSHOW           REDISPLAY                 87309
         BTAB  *CLEAR,FULLINIT         RE-INITIALIZE             87314
         BTAB  *PF3,FULLBACK           TSO-STYLE EXIT            87310
         BTAB  *END                                              87309
         SPACE 1                                                 87309
LISTFD   FDOPT NL,SBA=(1,1)                                      87310
*        FDREPT 21,C'>'      MAKE LEFT FRAME                     87309
         FD    '> > > > > > > > > > >',NL,TURQ                   92300
         FD    ' Public Interest Data ',GREEN                    92300
         FD    'TSO',WHITE                                       87333
         FD    ' Logon ',YELLOW                                  87321
*        FDREPT 80,C'<'      RIGHT FRAME (LONG ENOUGH FOR MODEL 5)
         FD    '< < < < < < < < < < <',TURQ                    87333  O
         FD    CURRDATE,WDAY,DEB,RADJ,NL,PINK,LEN=20             87314
         FD    ',',PADR,PREV                                     87315
         FD    CURRDATE,DATJ,1,PAD,PINK                          87321
         FD    CURRTIME,1,TIME,GREEN                             87321
         FD    'System',RADJ,BLUE,LEN=10                         87314
         FD    GSYSID,PAD,DEB,TURQ  SMF SYSTEM ID                87314
         FD    'Terminal',RED,RADJ,LEN=11                        87314
         FD    GTERMID,PINK,PAD,UNDER                            87314
         FDOPT SBA=(3,1)                                         87309
         FDCLI GMSGAREA+1,0,BE=NOMSGFD                           87309
         FD    GMSGAREA+4,(R2),INT,NL,DEB,YELLOW,MONO,UND        87361
NOMSGFD  FDOPT TURQ,SBA=(4,20)                                   87309
         FD    'To exit from this function, use PA1 or PFK 3.',PINK
IDFD     VALFD FID,L$UUID,'User-id'                              87314
         FDTM  FSFLAGS,FSUID,BZ=ENDFD   SKIP IF NO ID            87314
ACFD     VARFD FAC,L$UACCT,'Account'                             87360
PSFD     VALFD FPS,GPASS,'Password',NONDISP                      87314
         FDTM  A$UADMIN,A$UAFPSW,BZ=NPMODFD  NOT ALLOWED PSWD MOD
NPFD     VARFD FNP,GNEWPSWD,'New Pswd',NONDISP                   87314
NPMODFD  EQU   *             NEW PASSWORD BYPASS                 87359
PRFD     VALFD FPR,L$UTSPRC,'Procedure',PRIV=YES                 87333
CPFD     VARFD FCP,L$UTSTIM,'CPU time',I,PRIV=YES,LEN=4          87333
         FD    'minutes',GREEN                                   87334
MCFD     VALFD FMC,L$UMSGCL,'MsgClass',LEN=1                     87333
MLFD     VARFD FML,GMSGLEVL,'MsgLevel',LEN=3                     87314
RGFD     VALFD FRG,L$UTSRGN,'Region',I,LEN=4                     87333
         FD    'K',GREEN                                         87315
GEFD     VARFD FGE,L$UTSGEN,'Unit',PRIV=YES                      87334
         FD    ' ',PINK,NL   SPACER                              87317
         FD    'Command',PAD,NL,TURQ                             87317
CMFDIN   FDIN  L$UTSCMD,GREEN,DEB,UP,LEN=60                      87333
*OLD*    FDCLI GSYSID+3,C'A',BNE=YISPF                           88221
*OLD*    FD  'ISPF IS NOT AVAILABLE ON THIS SYSTEM',RADJ,NL,PINK,LEN=75
         FDGOTO NISPF                                            88221
YISPF    FD    'You may use %ISPPDF to invoke ISPF/PDF; optionally with*
                PANEL(#)',RADJ,NL,PINK,LEN=75                    87334
NISPF    FD    ' ',NL                                            87334
         FD    'Place a Y or N prior to each option to be changed :',NL
MAFDIN   FDIN  YNMAIL,NL,UPPER,LEN=1                             87321
         FD    'MAIL',TURQ,LEN=10                                87321
NOFDIN   FDIN  YNNOTE,UPPER,LEN=1                                87334
         FD    'NOTICES',TURQ,LEN=10                             87334
RCFDIN   FDIN  YNRECON,UPPER,LEN=1                               87321
         FD    'RECONNECT',TURQ,LEN=10                           87333
BYFDIN   FDIN  YNIBM,UPPER,LEN=1                                 87333
         FD    'IBM-LOGON',TURQ,LEN=10                           87333
DUFDIN   FDIN  YN2SES,UPPER,LEN=1                                87333
         FD    '2 Sessions',TURQ,LEN=10                          87361
         FDTM  A$UADMIN,A$UAFMOD,BZ=ENDFD  SKIP IF NOT ALLOWED MODS
UPFDIN   FDIN  YNUPDAT,UPPER,LEN=1                               87334
         FD    'SAVE',TURQ                                       87334
ENDFD    FD    *END                                              87309
         SPACE 1                                                 87312
*        SCANNER CONTROL LIST FOR OUR PARSER                     87312
PARSFULL FDSCAN LOGON,LGFD,POS,LONG  LOOK FOR LOGON OR LOGOFF    87314
PARSUID  FDSCAN UID,IDFDIN,POS2,REQ    USER-ID (MANDATORY)       87360
PARSPSWD FDSCAN PSWD,PSFDIN,POS2,REQ   PASSWORD (MANDATORY)      87360
PARSNPSW FDSCAN 'NEWPSWD',NPFDIN,POS                             87360
*        NOTE THAT 'POS2' INDICATES CHAINED FIELDS (/ OR , SEPARATOR)
PARSREST FDSCAN 'ACCOUNT',ACFDIN,REQ   ACCOUNT() REQUIRED        87314
         FDSCAN 'ACCT',ACFDIN,REQ      ACCT() ALIAS              87314
         FDSCAN 'PROCEDURE',PRFDIN,REQ  EXEC PROC (REQ. OR DFLT) 87314
         FDSCAN 'COMMAND',CMFDIN       CMD=                      87314
         FDSCAN 'CMD',CMFDIN           CMD=                      87314
         FDSCAN CPU,CPFDIN             CPU TIME                  87314
         FDSCAN TIME,CPFDIN            CPU TIME (PREFERRED)      87314
         FDSCAN MSGCLASS,MCFDIN        MSGCLASS=                 87314
         FDSCAN MSGLEVEL,MLFDIN        MSGLEVEL=                 87314
         FDSCAN REGION,RGFDIN          REGION=                   87314
         FDSCAN UNIT,GEFDIN            UNIT=                     87314
         FDSCAN PROCEDURE,PRFDIN       PROC=                     87361
         FDSCAN HELP,HPFDIN,KEY,MOVE=Y                           88221
         FDSCAN '?',HPFDIN,KEY,MOVE=Y     HELP REQUEST           88221
         FDSCAN MAIL,MAFDIN,KEY,MOVE=Y                           87314
         FDSCAN NOMAIL,MAFDIN,KEY,MOVE=N                         87314
         FDSCAN NOTICES,NOFDIN,KEY,MOVE=Y                        87334
         FDSCAN NONOTICES,NOFDIN,KEY,MOVE=N                      87334
         FDSCAN RECONNECT,RCFDIN,KEY,MOVE=Y                      87314
         FDSCAN BYPASS,BYFDIN,KEY,MOVE=Y                         87333
         FDSCAN DUPLICATE,DUFDIN,KEY,MOVE=Y                      87333
         FDSCAN MULTIPLE,DUFDIN,KEY,MOVE=Y                       87333
         FDSCAN SAVE,UPFDIN,KEY,MOVE=Y                           87334
         FDSCAN LOGOFF,LGFD,KEY,LONG   AND LOGOFF AT ANY TIME    87361
         FDSCAN LOGON,LGFD,KEY,LONG  ALLOW LOGON ON RE-PROMPT    87361
         FDSCAN END                                              87312
         SPACE 1                                                 87312
LGFD     FDIN  DB,DEB,UP,LEN=8  INPUT FOR LOGON/LOGOFF           87312
HPFDIN   FDIN  YNHELP,UPPER,LEN=1  ***DUMMY FDIN FOR HELP***     88221
         FD    *END                                              87312
         SPACE 1                                                 87309
TERMINAL SCRWORK TERMINAL,TITLE=4                                87309
         SPACE 1                                                 87360
PROMUID  DC    AL1(10),C'User-Id ? '                             87360
PROMPSWD DC    AL1(10),C'Password? '                             87360
PROMNPSW DC    AL1(10),C'New Pswd? '                             87360
PROMANY  DC    AL1(18),C'Option(s) or CR > '                     87360
MSG698   DC    Y(43,0),C'MSG698 USER-ID IS IN USE - NOT SHARABLE'
MSG698LN EQU   *-MSG698                                          87361
MSG699   DC    Y(32,0),C'MSG699 USER-ID IS ALREADY IN USE'       87361
MSG699LN EQU   *-MSG699                                          87361
         SPACE 1                                                 88002
*        TABLE OF ERROR MESSAGES AND INDEX DEFINITIONS           88002
ERRTAB   DC    AL1(L'RESCFAIL),AL3(RESCFAIL)  RESOURCE FAILURE   88002
ERXRESC  EQU   *-ERRTAB      RESOURCE FAILURE                    88002
         DC    AL1(L'TOOMANY),AL3(TOOMANY)                       88002
ERXTOOM  EQU   *-ERRTAB      TOO MANY BAD PASSWORDS/USERIDS      88002
         DC    AL1(L'TOOLONG),AL3(TOOLONG)                       88002
ERXTOOL  EQU   *-ERRTAB      USER WAITED TOO LONG                88002
         SPACE 1                                                 88002
TOOMANY  DC    C'MSG693 TOO MANY INCORRECT ENTRIES - LOGON DENIED'
TOOLONG  DC    C'MSG694 INPUT TIMED OUT'                         88002
         SPACE 2                                                 88221
NTOHELP  DC    C'TSO LOGON Processing:',X'15'                    88221
         DC    C'To the USER-ID prompt, please respond with your id, a *
               slash or comma, and your',X'15'                   88221
         DC    C'password. If you have password change authority, you m*
               ay append another comma',X'15'                    88221
         DC    C'or slash, and a new password. Passwords must be at lea*
               st five alpha-numerics.',X'15'                    88221
         DC    C'           You may optionally specify :',X'15'  88221
         DC C'ACCT=acctsuba - authorized account and sub-account',X'15'
         DC    C'MSGCLASS=x (x is A, P or Z) for JCL and LOG printing (*
               A is std, Z is none)',X'15'                       88221
         DC    C'REGION=nnnn(K) - running region. 2048 is adequate, 614*
               4 is maximum.',X'15'                              88221
         DC    C'MAIL/NOMAIL - message option',X'15'             88221
         DC    C'NOTICES/NONOTICES - notify option',X'15'        88221
         DC    C'SAVE - make changed options permanent defaults.',X'15'
         DC    C'RECONNECT - (preset by this exit)',X'15'        88221
         DC    C'BYPASS - use IBM, not local LOGON code',X'15'   88221
         DC    C'DUPLICATE or MULTIPLE - permit multiple sessions with *
               the same user-id.',X'15'                          88221
         DC C'     Must be used on the first of multiple logons.',X'15'
         DC C'LOGON or LOGOFF - restart or cancel current LOGON.',X'15'
NTOHELPL EQU   *-NTOHELP                                         88221
         TITLE 'L E X L O G O N  ***  BUILD JCL'                 87303
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    BUILDJCL                                                        P3
*                                                                    P3
* FUNCTION -                                                         P3
*    1) MOVE MODEL JCL TO JCL IMAGES AREA.                           P3
*    2) BUILD JOB CARD.                                              P3
*    3) BUILD EXEC CARD.                                             P3
*    4) BUILD JOBPARM CARD.                                          P3
*    5) CLEAN UP JOBPARM CARD AND SET LENGTH IN DOPE VECTOR          P3
*    6) CHECKS TO SEE IF THE USER IS A CORPORATE USER.  I.E. USERID  P3
*       HAS SYSTEMS PRIVILEGES. ELSE ISSUE MESSAGE AND IGNORE JCL.   P3
*    7) GET JCL FROM USER.                                           P3
*    8) INDICATE JCL AND RETURN TO CALLER.                           P3
*                                                                    P3
* OPERATION -                                                        P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    BUILDJCL                                                        P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    FINDINFO                                                        P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    REGISTER 15 CONTAINS THE RETURN CODE FOR THE MAINLINE.  THIS    P3
*    WILL BE AS FOLLOWS -                                            P3
*       |------|------------------------|--------------------------| P3
*       |RETURN|                        |                          | P3
*       |CODE  |REASON                  |ACTION                    | P3
*       |------|------------------------|--------------------------| P3
*       |   0  |NO ERRORS ENCOUNTERED.  |CONTINUE WITH LOGON.      | P3
*       |------|------------------------|--------------------------| P3
*       |NON-0 |TPUT/TGET RETURN CODE.  |DENY LOGON.               | P3
*       |------|------------------------|--------------------------| P3
*                                                                    P3
* MESSAGES -                                                         P3
*    'ENTER NEXT JCL CARD OR 'END''                                  P3
*    'ENTER LAST JCL CARD OR 'END''                                  P3
*    'JCL IGNORED'                                                   P3
*                                                                    P3
*********************************************************************P3
         SPACE 1                                                 87303
BUILDJCL DS    0H                                                    P3
         ST    R14,BJCLSAVE        SAVE RETURN ADDRESS               P3
         ST    R2,BJCLSAVE+4       SAVE R2                           P3
         ST    R3,BJCLSAVE+8       SAVE R3                           P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       MOVE MODEL JCL TO JCL IMAGES AREA.                           P3
*                                                                    P3
*********************************************************************P3
         LPP   JCL,PARM=     GET JCL DESCRIPTOR                  87303
         L     R0,OFFADD(,R15)  GET ADDRESS OF JCL IMAGES        87303
         LR    R3,R0               SAVE ADDRESS OF JCL IMAGES        P3
         LH    R1,OFFMAX(,R15)  GET LENGTH OF JCL IMAGES         87303
         LA    R14,JCLMODEL        GET ADDRESS OF JCL MODEL          P3
         L     R15,LJCLMODL        GET FILL CHARACTER AND LENGTH     P3
         MVCL  R0,R14              MOVE MODEL JCL                    P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       BUILD JOB CARD.                                              P3
*                                                                    P3
*********************************************************************P3
         MVC   2(7,R3),L$UUID     MOVE IN USERID                    P3
         MVC   MJOBACCT-MJOBCARD(4,R3),L$UACCT   MAIN ACCOUNT    88329
         MVC   MJOBSUBA-MJOBCARD(4,R3),L$UACCT+4  SUB-ACCOUNT    88329
         MVC   MJOBPGNM-MJOBCARD(L'L$UUID,R3),L$UUID USER-ID     88329
BJCLGPNM DS    0H                                                    P3
         LA    R15,MJOBPGNM+L'MJOBPGNM+1-MJOBCARD(,R3) NEXT BLANK
         CLI   L$UMSGCL,C' '       WAS 'MSGCLASS' SPECIFIED      87333
         BNH   BJCLGMCL            NO - CONTINUE                     P3
         MVC   0(L'MSGCLASS,R15),MSGCLASS YES - MOVE IN KEYWORD      P3
         MVC   10(1,R15),L$UMSGCL  MOVE IN MSGCLASS              87333
         LA    R15,11(,R15)        BUMP OVER MSGCLASS                P3
BJCLGMCL DS    0H                                                    P3
         CLI   GMSGLEVL,C' '       WAS 'MSGLEVEL' SPECIFIED          P3
         BNH   BJCLGMLE            NO - CONTINUE                     P3
         MVC   0(L'MSGLEVEL,R15),MSGLEVEL YES - MOVE IN KEYWORD      P3
         MVC   11(3,R15),GMSGLEVL  MOVE IN MSGLEVEL                  P3
         TRT   11(4,R15),NXTBLANK  FIND NEXT BLANK                   P3
         MVI   0(R1),C')'          MOVE IN ')'                       P3
BJCLGMLE DS    0H                                                    P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       BUILD EXEC CARD.                                             P3
*                                                                    P3
*********************************************************************P3
         LA    R3,80(,R3)          BUMP OVER TO EXEC CARD            P3
         MVC   16(8,R3),L$UTSPRC   USE PROC                      87333
         MVC   2(8,R3),16(R3)      COPY PROCNAME TO STEPNAME         P3
         SLR   R0,R0                                             87333
         ICM   R0,3,L$UTSTIM TIME= SPECIFIED ?                   87333
         BZ    BJCLGCPU      NO; LET SYSTEM DEFAULT              87333
         CVD   R0,DB+8       MAKE PACKED                         87333
         MVC   DB(8),=X'F020202020202120'                        87333
         ED    DB,DB+12      MAKE PRINTABLE                      87333
         TRT   16(9,R3),NXTBLANK   YES - FIND NEXT BLANK             P3
         MVC   0(L'TIME,R1),TIME   MOVE IN KEYWORD                   P3
         MVC   L'TIME(4,R1),DB+4  MOVE TIME                      87333
BJCLGCPU ICM   R0,3,L$UTSRGN ANY REGION ?                        87333
         BZ    BJCLGREG      NO                                  87333
         CVD   R0,DB+8       MAKE PACKED                         87333
         MVC   DB,=X'F020202020202120'                           87333
         ED    DB,DB+12      MAKE IT PRINTABLE                   87333
         TRT   16(55,R3),NXTBLANK  FIND END OF CARD              87333
         MVC   0(8,R1),=C',REGION='                              87333
         MVC   8(4,R1),DB+4  COPY REGION                         87333
         MVI   12(R1),C'K'   INDICATE UNIT                       87333
BJCLGREG TRT   16(55,R3),NXTBLANK  FIND END OF CARD              87309
         MVI   0(R1),C','    MAKE CONTINUATION INDICATOR         87309
         LA    R3,80(,R3)    POINT TO NEXT CARD                  87309
         CLI   L$UTSCMD,C' '    ANY INITIAL COMMAND ?            87333
         BNH   BJCLJPRM      NO; LEAVE PARM BLANK                87309
         CLI   L$UTSCMD,C'.' ALTERNATE EMPTY ?                   88185
         BE    BJCLJPRM      YES; LEAVE IT ALONE                 88185
         MVC   9(L'L$UTSCMD,R3),L$UTSCMD  MOVE COMMAND           87333
         MVI   8(R3),C''''   MAKE FRAME                          87309
         LA    R1,9+L'L$UTSCMD-1(,R3)  POINT TO LAST BYTE        87333
         LA    R0,L'L$UTSCMD-1                                   87333
BJCLGCMD CLI   0(R1),C' '    TRAILING BLANK ?                    87333
         BH    BJCLGCMN      NO                                  87333
         BCTR  R1,0                                              87333
         BCT   R0,BJCLGCMD                                       87333
BJCLGCMN MVI   1(R1),C''''   MAKE TRAILING QUOTE                 87333
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       BUILD JOBPARM CARD.                                          P3
*                                                                    P3
*********************************************************************P3
BJCLJPRM LA    R3,89(,R3)    BUMP TO JOBPARM CARD + 9            87309
         CLI   GFORMS,C' '   ANY FORMS ?                         87303
         BNH   BJCLGFOR      NO; SKIP                            87303
         MVC   0(L'FORMS,R3),FORMS NO - MOVE IN KEYWORD              P3
         MVC   7(4,R3),GFORMS      MOVE IN FORMS                 87303
         TRT   7(5,R3),NXTBLANK    FIND NEXT BLANK                   P3
         LR    R3,R1               UPDATE POINTER                    P3
BJCLGFOR DS    0H                                                    P3
         CLI   GROOM,C' '    ANY ROOM NUMBER ?                   87303
         BNH   BJCLGROM      NO; SKIP                            87303
         MVC   0(L'ROOM,R3),ROOM   NO - MOVE IN KEYWORD              P3
         MVC   6(4,R3),GROOM  MOVE IN ROOM                       87303
         TRT   6(5,R3),NXTBLANK    FIND NEXT BLANK                   P3
         LR    R3,R1               UPDATE POINTER                    P3
BJCLGROM DS    0H                                                    P3
         CLI   GCOPIES,0           WAS 'COPIES' SPECIFIED            P3
         BE    BJCLGCOP            YES - CONTINUE                    P3
         MVC   0(L'COPIES,R3),COPIES NO - MOVE IN KEYWORD            P3
         MVC   8(3,R3),GCOPIES     MOVE IN COPIES                    P3
         TRT   8(4,R3),NXTBLANK    FIND NEXT BLANK                   P3
         LR    R3,R1               UPDATE POINTER                    P3
BJCLGCOP DS    0H                                                    P3
         CLI   GLINECT,0           WAS 'LINECT' SPECIFIED            P3
         BE    BJCLGLIN            YES - CONTINUE                    P3
         MVC   0(L'LINECT,R3),LINECT NO - MOVE IN KEYWORD            P3
         MVC   8(3,R3),GLINECT     MOVE IN LINECT                    P3
         TRT   8(4,R3),NXTBLANK    FIND NEXT BLANK                   P3
         LR    R3,R1               UPDATE POINTER                    P3
BJCLGLIN DS    0H                                                    P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       CLEAN UP JOBPARM CARD AND SET LENGTH IN DOPE VECTOR          P3
*                                                                    P3
*********************************************************************P3
         LPP   JCL,PARM=,WORK=R2  GET JCL DESCRIPTOR             87303
         L     R3,OFFADD(,R2)  GET ADDRESS OF JCL IMAGES         87303
         MVI   LENJMODL-80+9(R3),C' '  CLEAN UP JOBPARM CARD     87309
         LA    R0,LENJMODL   ASSUME 4 JCL CARDS                  87309
         CLI   LENJMODL-80+10(R3),C' '  WAS JOBPARM USED?        87309
         BNE   BJCLGLEN      YES; LENGTH CORRECT                 87309
         LA    R0,LENJMODL-80  NO; SET ONE LESS                  87309
BJCLGLEN STH   R0,OFFCUR(,R2)  SET LENGTH                        87309
         TM    GBITS2,GJCL         WAS 'JCL' SPECIFIED               P3
         BZ    BJCLALL             NO - QUIT                         P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       CHECKS TO SEE IF THE USER IS A CORPORATE USER.  I.E. USERID  P3
*       HAS SYSTEMS PRIVILEGES.  ELSE ISSUE MESSAGE AND IGNORE JCL.
*                                                                    P3
*********************************************************************P3
         TM    L$UPRIVY,L$UPFSYS  IS THIS A CORPORATE USER       88002
         BNZ   BJCLLOOP            YES - 'JCL' OK                    P3
         TMSG  BADJCL        INVALID OPTION MESSAGE              87303
         B     BJCLALL             AND RETURN TO CALLER              P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       GET JCL FROM USER.                                           P3
*                                                                    P3
*********************************************************************P3
BJCLLOOP DS    0H                                                    P3
         LH    R14,OFFMAX(,R2)  GET MAXIMUM LENGTH OF JCL        87303
         SH    R14,OFFCUR(,R2)  SUBTRACT OFF WHAT HAS BEEN USED  87303
         BNP   BJCLALL       QUIT IF IT IS FULL                  87303
         LA    R4,80         GET LENGTH FOR TGET                 87303
         CR    R14,R4        IS THIS LAST CARD                   87303
         BNE   BJCLNLST            NO - CONTINUE                     P3
         TMSG  LASTCARD      WRITE MESSAGE                       87303
         B     BJCLTGET            AND READ RESPONSE                 P3
BJCLNLST TMSG  NEXTCARD      WRITE MESSAGE                       87303
BJCLTGET L     R3,OFFADD(,R2)   GET ADDRESS OF JCL AREA          87303
         AH    R3,OFFCUR(,R2)   GET ADDRESS OF NEXT JCL CARD     87303
         LR    R1,R3               GET ADDRESS FOR TGET              P3
         MVC   0(80,R3),BLANKS     BLANK INPUT AREA                  P3
         LR    R0,R4         SET MAX LENGTH                      87303
         BAL   R14,GETSHORT  GET A RESPONSE SOON                 87303
         LTR   R15,R15             WAS RETURN CODE ZERO              P3
         BZ    BJCLTGOK            YES - CONTINUE                    P3
         BAL   R14,BJCLTGER        NO - GO TO ERROR ROUTINE          P3
BJCLTGOK DS    0H                                                    P3
         OC    0(80,R3),BLANKS     FORCE TO UPPERCASE                P3
         CLC   KEND,0(R3)          WAS THIS 'END'                    P3
         BE    BJCLALL             YES - QUIT                        P3
         CLI   0(R3),C' '          NO - WAS THIS BLANK LINE          P3
         BE    BJCLLOOP            YES - ASK AGAIN                   P3
         LH    R15,OFFCUR(,R2)  NO - GET CURRENT LENGTH          87303
         LA    R15,80(,R15)        ADD THIS CARD TO IT               P3
         STH   R15,OFFCUR(,R2)   UPDATE STRING DOPE VECTOR       87303
         B     BJCLLOOP            AND LOOP                          P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       INDICATE JCL AND RETURN TO CALLER.                           P3
*                                                                    P3
*********************************************************************P3
BJCLALL  LPPA  SW,PARM=LOAD  GET SWITCHES                        87303
         OI    SW1-SWDSECT(R15),SWJCL  SHOW JCL BEING RETURNED   87303
BJCLRTRN DS    0H                                                    P3
         SLR   R15,R15             SET RETURN CODE                   P3
BJCLEXIT DS    0H                                                    P3
         L     R14,BJCLSAVE        RESTORE RETURN ADDRESS            P3
         L     R2,BJCLSAVE+4       RESTORE R2                        P3
         L     R3,BJCLSAVE+8       RESTORE R3                        P3
         BR    R14                 RETURN TO CALLER                  P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       IF RETURN CODE DID NOT INDICATE ATTENTION INTERRUPT, ISSUE   P3
*       MESSAGE TO USER AND DENY LOGON.                              P3
*                                                                    P3
*********************************************************************P3
BJCLTGER DS    0H                  NON-ZERO RETURN CODE FROM TGET    P3
         LA    R1,8                GET 8 IN R1 FOR COMPARISON        P3
         CR    R1,R15              WAS THIS AN ATTENTION INTERRUPT   P3
         BNE   BJCLTGNA            NO - GO ISSUE MESSAGE             P3
         TM    GBITS1,GATTNHIT     YES - DID I GET ANY INPUT         P3
         BZR   R14                 NO - RETURN AS IF NULL INPUT      P3
         B     BJCLEXIT            YES - LET MAINLINE HANDLE INPUT   P3
BJCLTGNA DS    0H                  NOT AN ATTENTION INTERRUPT        P3
         LR    R2,R15              PROTECT RETURN CODE               P3
         MVC   GTGETMSG(L'TGETMSG),TGETMSG COPY MESSAGE              P3
         CVD   R2,WORKAREA         CONVERT TO DECIMAL                P3
         OI    WORKAREA+7,X'0F'    FIX SIGN                          P3
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK                      P3
         MVC   GTGETMSG+23(4),WORKAREA+1 MOVE INTO MESSAGE           P3
         TMSG  GTGETMSG,L'TGETMSG  WRITE MESSAGE                 87303
         LR    R15,R2              RESTORE RETURN CODE               P3
         OI    GBITS1,GDENY        DENY LOGON                        P3
         B     BJCLEXIT            EXIT                              P3
         TITLE 'L E X L O G O N  ***  GET TIMED INPUT'           87303
***********************************************************************
*                                                                     *
*        GET ROUTINES WITH TIME-OUT                                   *
*        INPUT AREA IN R1, MAX LENGTH IN R0, RETURN VIA R14           *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87303
GETDARK  STM   R14,R2,GETSAVER  ENTRY FOR NON-DISPLAY MESSAGE    87303
         OI    GETSAVER,X'80'  SET DARK FLAG                     87303
         MVC   GMSGAREA(LDARK1),DARK1  MAKE PREFIX               87303
         LH    R14,0(,R15)   GET LENGTH OF MESSAGE               87303
         SH    R14,=H'5'     GET LENGTH FOR MOVE                 87303
         EX    R14,GETDARKM  MOVE                                87303
         LA    R1,GMSGAREA+LDARK1+1(R14)  NEXT AVAILABLE BYTE    87303
         MVC   0(LDARK2,R1),DARK2  MOVE SUFFIX                   87303
         LA    R0,LDARK1+1+LDARK2(,R14)  SET MESSAGE LENGTH      87303
         TPUT  GMSGAREA,(0),FULLSCR  MAKE FULL-SCREEN MESSAGE    87303
         LTR   R15,R15       CHECK RETURN                        87303
         BZ    GETSHORC      ALL'S WELL                          87303
         B     GETSHORY      EXIT WITH CC                        87303
GETSHORT STM   R14,R2,GETSAVER  SAVE CALLER'S REGISTERS          87303
         NI    GETSAVER,255-X'80'  SET GET FLAG                  87303
GETSHORC LM    R14,R15,GETSAVER+8  RELOAD LENGTH/ADDRESS         87303
         BCTR  R14,0         MAKE EXECUTE LENGTH                 87303
         EX    R14,GETSHOR0  CLEAR THE INPUT AREA                87303
         LA    R2,MAXWAIT    LOAD MAXIMUM SECONDS TO WAIT        87303
GETSHORN LM    R0,R1,GETSAVER+8  RELOAD USER'S PARAMETERS        87303
         ICM   R1,IOOO,=X'90'  FORCE TGET, NOWAIT                87303
         TGET  (1),(0),R     TEST FOR INPUT                      87303
         CH    R15,=H'4'     BUFFER USED ?                       87303
         BNE   GETSHORX      YES; OR ERROR - RETURN              87303
         BCT   R2,GETSHORE   CONTINUE IF WAIT NOT EXPIRED        87303
         WTO   MF=(E,TTIMRWTO)     TELL OPERATOR WHY             87305
         ABEND 522                 AND ABEND WITH U0522          87305
GETSHORE STIMER WAIT,BINTVL==A(100)  WAIT ONE SECOND             87303
         B     GETSHORN      AND TRY AGAIN                       87303
GETSHORX L     R14,GETSAVER+8  RELOAD LENGTH                     87303
         L     R2,GETSAVER+12  RELOAD ADDRESS (KEEP R15)         87303
         BCTR  R14,0         MAKE EXECUTE LENGTH                 87303
         EX    R14,GETSHOR4  MAKE UPPER CASE                     87303
         ST    R1,GETSAVER+12  RETURN LENGTH TO USER             87303
GETSHORY ST    R15,GETSAVER+4  SET RETURN CODE                   87303
         TM    GETSAVER,X'80'  GETDARK ENTRY ?                   87303
         BZ    GETSHORZ      NO; RETURN                          87303
         TMSG  CLRSCRN,,FULLSCR  CLEAR SCREEN                    87303
GETSHORZ LM    R14,R2,GETSAVER  RELOAD REGISTERS                 87303
         LTR   R15,R15       SET RETURN CODE                     87303
         BR    R14           AND RETURN                          87303
GETSHOR0 XC    0(0,R15),0(R15)  CLEAR USER'S BUFFER              87303
GETSHOR4 OC    0(0,R2),BLANKS  FILL AND UPPER-CASE USER'S INPUT  87303
GETDARKM MVC   GMSGAREA+LDARK1(0),4(R15)  MOVE USER'S TEXT       87303
DARK1    DC    X'27F1C3115B603C5D7F00115B601D60'  WRT;CLR;PROT   87303
LDARK1   EQU   *-DARK1                                           87303
DARK2    DC    X'1D4C13115D7F1D60'  IN;IC;SBA 24,80;PROT         87303
LDARK2   EQU   *-DARK2                                           87303
         TITLE 'L E X L O G O N  ***  GET APF AUTHORIZATION'     87303
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    GETAUTH                                                         P3
*                                                                    P3
* FUNCTION -                                                         P3
*    GET APF AUTHORIZED.                                             P3
*                                                                    P3
* OPERATION -                                                        P3
*    ISSUE SVC 217 WITH R0 SET TO 1.                                 P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    GETAUTH                                                         P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    NONE                                                            P3
*                                                                    P3
* MESSAGES -                                                         P3
*    NONE                                                            P3
*                                                                    P3
* COMMENTS -                                                         P3
*    NONE                                                            P3
*                                                                    P3
*********************************************************************P3
         SPACE 1                                                 87303
GETAUTH  ST    R14,GAUTSAVE  SAVE RETURN ADDRESS                 87303
         SERVICE APFON       CALL RESTRICTED AUTHORIZATION ENTRY 87303
         L     R14,GAUTSAVE        RESTORE RETURN ADDRESS            P3
         BR    R14                 RETURN TO CALLER                  P3
         TITLE 'L E X L O G O N  ***  DEBUG'                     87303
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    DEBUG                                                            *
*                                                                     *
* FUNCTION -                                                          *
*    PROVIDE DEBUGGING INFORMATION.                                   *
*                                                                     *
* OPERATION -                                                         *
*    1) DISPLAY DEBUGGING INFORMATION.                                *
*                                                                     *
* ENTRY POINTS -                                                      *
*    DEBUG                                                            *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    NONE                                                             *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* MESSAGES -                                                          *
*    DEBUGGING INFORMATION AS REQUIRED                                *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87303
DEBUG    DS    0H
         ST    R14,DBUGSAVE        SAVE RETURN ADDRESS
         TMSG  L$UUID       WRITE UID                            87303
         TMSG  GPASS         AND PASSWORD                        87303
         TMSG  L$UACCT      AND ACCOUNT                          87333
         TMSG  L$UTSPRC      AND PROCEDURE NAME                  87333
         TMSG  GNEWPSWD      AND NEW PASSWORD                    87303
         L     R14,DBUGSAVE        RESTORE RETURN ADDRESS
         BR    R14                 RETURN TO CALLER
         TITLE 'L E X L O G O N  ***  ESTAE EXIT'                87303
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    ESTAEXIT                                                         *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AFTER AN ABEND TO PASS CONTROL TO A RETRY ROUTINE.       *
*                                                                     *
* OPERATION -                                                         *
*    1) CHECKS TO SEE IF SDWA WAS OBTAINED.  IF NOT, POINT TO RETRY   *
*       ROUTINE, SAVE ABEND CODE AND RETURN TO ABEND.  IF SDWA WAS    *
*       OBTAINED, SAVE ABEND CODE AND ISSUE SETRP MACRO TO RETURN TO  *
*       ABEND SPECIFING RETRY ROUTINE.  IN EITHER CASE, DO NOT RETRY P3
*       IF THIS IS A RECURSIVE ABEND.                                P3
*                                                                     *
* ENTRY POINTS -                                                      *
*    ESTAEXIT                                                         *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    IF NO SDWA WAS OBTAINED REGISTER 1 CONTAINS THE ABEND CODE.      *
*    IF AN SDWA WAS OBTAINED REGISTER 1 CONTAINS THE ADDRESS OF THE   *
*    SDWA.                                                            *
*                                                                     *
* OUTPUT -                                                            *
*    ADDDRESS OF RETRY ROUTINE.                                       *
*                                                                     *
* MESSAGES -                                                          *
*    NONE                                                             *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87303
ESTAEXIT DS    0H
         PUSH  USING               SAVE MAIN LINE'S ADDRESSABILITY
         DROP  ,                   DROP MAIN LINE'S ADDRESSABILITY   P3
         USING ESTAEXIT,R12        ESTABLISH EXIT ADDRESSABILITY
         USING SAVE,R13      ESTABLISH DYNAMIC ADDRESSABILITY    87303
         LR    R12,R15             LOAD BASE
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*       CHECKS TO SEE IF SDWA WAS OBTAINED.  IF NOT, POINT TO RETRY   *
*       ROUTINE, SAVE ABEND CODE AND RETURN TO ABEND.  IF SDWA WAS    *
*       OBTAINED, SAVE ABEND CODE AND ISSUE SETRP MACRO TO RETURN TO  *
*       ABEND SPECIFING RETRY ROUTINE.  IN EITHER CASE, DO NOT RETRY P3
*       IF THIS IS A RECURSIVE ABEND.                                P3
*                                                                     *
***********************************************************************
         LA    R15,12              GET 12 IN R15                     P3
         CR    R0,R15              WAS SDWA OBTAINED                 P3
         BNE   HAVESDWA            YES - BRANCH
         LA    R0,ESTAERTY         NO - POINT TO RETRY ROUTINE
         L     R13,16(,R2)         GET DYNAMIC BASE FROM PARM LIST P3.1
         ST    R1,ABENDCDE         SAVE ABEND CODE
         XC    ABENDPSW,ABENDPSW   INDICATE PSW NOT AVAILABLE        P3
         LA    R15,4               INDICATE RETRY ROUTINE SUPPLIED
         TM    GBITS2,GABEND       ABENDED BEFORE                    P3
         BO    NOSDWA2             YES - ABEND RECURSION             P3
         OI    GBITS2,GABEND       NO - SET ABEND INDICATOR          P3
         BR    R14                 RETURN TO CALLER
NOSDWA2  DS    0H                                                    P3
         SLR   R15,R15             INDICATE NO RETRY                 P3
         BR    R14                 RETURN TO CALLER
         USING SDWA,R1       DECLARE THE WORK AREA               87303
HAVESDWA L     R2,SDWAPARM   GET ADDRESS OF PARAM LIST           87303
         L     R13,16(,R2)         GET DYNAMIC BASE FROM PARM LIST P3.1
         MVC   ABENDCDE,SDWAABCC   SAVE ABEND CODE               87303
         MVC   ABENDPSW,SDWAEC1    SAVE EC-MODE PSW              93197
         MVC   ABENDREG(16*4),SDWAGRSV  COPY REGISTERS           87303
         ST    R14,12(,R13)        SAVE RETURN ADDRESS
         TM    GBITS2,GABEND       ABENDED BEFORE                    P3
         BO    HAVSDWA2            YES - ABEND RECURSION             P3
         OI    GBITS2,GABEND       NO - SET ABEND INDICATOR          P3
         SETRP REGS=(14),RC=4,RETADDR=ESTAERTY,FRESDWA=YES RETURN
HAVSDWA2 DS    0H                                                    P3
         SETRP REGS=(14),RC=0      DO NOT RETRY                      P3
         SPACE 1                                                 87303
         POP   USING               RETURN MAIN LINE'S ADDRESSABILITY
         TITLE 'L E X L O G O N  ***  ESTAE RETRY'               87303
***********************************************************************
*                                                                     *
* ROUTINE NAME -                                                      *
*    ESTAERTY                                                         *
*                                                                     *
* FUNCTION -                                                          *
*    ENTERED AFTER AN ABEND TO RETRY.                                 *
*                                                                     *
* OPERATION -                                                         *
*    1) ISSUES MESSAGE TO TSO USER AND OPERATOR INDICATING TYPE OF   P2
*       ABEND AND DENIES LOGON.  IF ABEND U0522, SKIP MESSAGES AND   P3
*       SDUMP.                                                       P3
*    2) IF APF AUTHORIZED, TAKE SDUMP.                               P3
*                                                                     *
* ENTRY POINTS -                                                      *
*    ESTAERTY                                                         *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    NONE                                                             *
*                                                                     *
* INPUT -                                                             *
*    ABENDCDE CONTAINS THE ABEND CODE PLACED THERE BY ESTAEXIT.       *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS THE RETURN CODE FOR MAINLINE.  THIS WILL BE *
*    AS FOLLOWS -                                                     *
*       |------|------------------------|--------------------------|  *
*       |RETURN|                        |                          |  *
*       |CODE  |REASON                  |ACTION                    |  *
*       |------|------------------------|--------------------------|  *
*       |  16  |LEXLOGON ABENDED.       |DENY LOGON SWITCH IS SET. |  *
*       |------|------------------------|--------------------------|  *
*                                                                     *
* MESSAGES -                                                          *
*    'LOGON USER EXIT ABEND TNNNN'                                    *
*    'USER-ID ON TERMINAL LOGON USER EXIT ABEND TNNNN - ASID X'NN''  P3
*                                                                     *
***********************************************************************
         SPACE 1                                                 87303
ESTAERTY LM    R9,R13,0(R1)  RELOAD BASES FROM PARM LIST         87303
         SPACE 1                                                 87303
***********************************************************************
*                                                                     *
*       ISSUES MESSAGE TO TSO USER AND OPERATOR INDICATING TYPE OF   P2
*       ABEND AND DENIES LOGON.  IF ABEND U0522, SKIP MESSAGES AND   P3
*       SDUMP.                                                       P3
*                                                                     *
***********************************************************************
         CLC   =H'522',ABENDCDE+2  WAS THIS U0522                87303
         BE    NOSDUMP             YES - SKIP MESSAGES AND SDUMP     P3
         MVC   GABNDMSG,ABNDMSG    NO - COPY ABEND MESSAGE           P3
         L     R15,ABENDCDE        GET COMPLETION CODE
         SLL   R15,8               SHIFT OUT TCBFLGS1
         SRL   R15,20              AND SHIFT OUT USER ABEND CODE
         LTR   R15,R15             WAS IT SYSTEM ABEND CODE
         BZ    USERABND            NO - MUST BE USER ABEND
         MVI   GABNDMSG+22,C'S'    YES - INDICATE SYSTEM ABEND
         STH   R15,WORKAREA        PUT COMPLETION CODE IN WORKAREA
         UNPK  WORKAREA+3(5),WORKAREA(3) UNPACK
         TR    WORKAREA+3(4),TRTABLE-240 CONVERT TO EBCDIC
         MVC   GABNDMSG+23(3),WORKAREA+4 MOVE TO MESSAGE
         B     ABNDTPUT            GO TELL OPERATOR
USERABND DS    0H
         MVI   GABNDMSG+22,C'U'    INDICATE USER ABEND
         L     R15,ABENDCDE        GET COMPLETION CODE               P3
         SLL   R15,20              SHIFT OUT TCBFLGS1
         SRL   R15,20              SHIFT BACK
         CVD   R15,WORKAREA        CONVERT TO DECIMAL
         OI    WORKAREA+7,X'0F'    FIX SIGN
         UNPK  WORKAREA(5),WORKAREA+5(3) UNPACK
         MVC   GABNDMSG+23(4),WORKAREA+1 MOVE TO ABEND MESSAGE
ABNDTPUT AM24  WORK=R15      FIX MODE
         TMSG  GABNDMSG,L'ABNDMSG  TELL USER                     87303
         MVC   WORKAREA(5),GABNDMSG+22 SAVE ABEND CODE               P3
         MVC   GABNDWTO,ABNDWTO    COPY WTO TEXT TO DYNAMIC AREA     P2
         MVC   GABNDWTO+46(5),WORKAREA COPY ABEND CODE               P3
         CLI   L$UUID,0           HAS USERID BEEN RETRIEVED         P2
         BE    ABENDWTO            NO - LEAVE TEXT WITH 'UNKNOWN'    P2
         MVC   GABNDWTO+4(7),L$UUID YES - COPY TO WTO               P2
ABENDWTO DS    0H                                                    P2
         MVC   GABNDWTO+15(8),GTERMID COPY TERMID TO DYNAMIC AREA    P3
         MVC   GABNDWTO+61(2),GASID COPY ASID TO DYNAMIC AREA        P3
         WTO   MF=(E,GABNDWTO)     TELL OPERATOR                     P2
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       IF APF AUTHORIZED, TAKE SDUMP.                               P3
*                                                                    P3
*********************************************************************P3
         L     R15,PSATOLD-PSA     GET ADDRESS OF MY TCB             P3
         L     R15,TCBJSCB-TCB(,R15) GET ADDRESS OF MY JSCB          P3
         TM    JSCBOPTS-IEZJSCB(R15),JSCBAUTH CAN I TAKE SDUMP       P3
         BZ    NOSDUMP             NO - SKIP SDUMP                   P3
         LH    R15,GABNDWTO        YES - GET LENGTH OF WTO           P3
         SH    R15,=H'4'           GET LENGTH OF TEXT                P3
         STC   R15,GABNDWTO+3      SET UP FOR SDUMP                  P3
         MVC   GSDUMP,SDUMP        COPY SDUMP PARAMETER LIST         P3
         LA    R1,GSDUMP           POINT TO SDUMP PARAMETER LIST     P3
         SDUMP HDRAD=GABNDWTO+3,MF=(E,(1)) TAKE SDUMP                P3
NOSDUMP  DS    0H                                                    P3
         OI    GBITS1,GDENY        DENY LOGON                        P3
         LA    R15,16              SET RETURN CODE
         B     RETURN              RETURN
         TITLE 'L E X L O G O N  ***  STAX EXIT'                 87303
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    STAXEXIT                                                        P3
*                                                                    P3
* FUNCTION -                                                         P3
*    ENTERED AFTER AN ATTENTION INTERRUPT HAS OCCURED.               P3
*                                                                    P3
* OPERATION -                                                        P3
*    1) CHECKS LENGTH OF INPUT BUFFER.  IF ZERO, EXIT.               P3
*    2) UPDATES ORIGINAL INPUT BUFFER.                               P3
*    3) REBUILDS CBUF.                                               P3
*    4) POSTS CP ECB.                                                P3
*    5) RETURNS TO CALLER.                                           P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    STAXEXIT                                                        P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    ATTENTION EXIT PARAMETER LIST (AEPL)                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    NONE                                                            P3
*                                                                    P3
* MESSAGES -                                                         P3
*    NONE                                                            P3
*                                                                    P3
*********************************************************************P3
         SPACE 1                                                 87303
STAXEXIT DS    0H                                                    P3
         SAVE  (14,12),,STAXEXIT   SAVE CALLER'S REGISTERS           P3
         USING AEPL,R1             ESTABLISH ADDRESSABILITY TO AEPL  P3
         L     R2,AEPLUSAD         GET ADDRESS OF USADDR FROM AEPL   P3
         LR    R3,R13              SAVE CALLER'S R13                 P3
         LM    R9,R13,0(R2)  RELOAD BASES FROM PARM LIST         87303
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       CHECKS LENGTH OF INPUT BUFFER.  IF ZERO, EXIT.               P3
*                                                                    P3
*********************************************************************P3
         L     R1,AEPLTAIE         GET ADDRESS OF TAIE FROM AEPL     P3
         DROP  R1                  DROP APEL ADDRESSABILITY          P3
         USING TAIE,R1             ESTABLISH ADDRESSABILITY TO TAIE  P3
         ICM   R1,B'0011',TAIEMSGL GET LENGTH OF INPUT BUFFER        P3
         DROP  R1                  DROP TAIE ADDRESSABILITY          P3
         BZ    STAXRTRN      QUIT IF THERE IS NO NEW TEXT        87303
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       UPDATES ORIGINAL INPUT BUFFER.                               P3
*                                                                    P3
*********************************************************************P3
         SPACE 1                                                 87303
         LPP   IN,PARM=LOAD,PARMR=R2,WORK=R2  GET INPUT DESCRIPTOR
         L     R15,OFFADD(,R2)  GET ADDRESS OF ORIGINAL BUFFER   87303
         STH   R1,OFFCUR(,R2)  UPDATE ACTUAL LENGTH              87303
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       REBUILDS CBUF.                                               P3
*                                                                    P3
*********************************************************************P3
         LA    R1,4(,R1)           ADD 4 FOR LENGTH AND OFFSET       P3
         STH   R1,GCBUF+CBUFLEN-CBUF UPDATE BUILT CBUF               P3
         XC    GCBUF+CBUFOFFS-CBUF(2),GCBUF+CBUFOFFS-CBUF ZERO OFF   P3
         MVC   GCBUF+CBUFTEXT-CBUF(252),0(R15) COPY STAX RESPONSE    P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       POSTS CP ECB.                                                P3
*                                                                    P3
*********************************************************************P3
         POST  GCPECB              POST CP ECB                       P3
         OI    GBITS1,GATTNHIT     INDICATE ATTENTION INTERRUPT      P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       RETURNS TO CALLER.                                           P3
*                                                                    P3
*********************************************************************P3
STAXRTRN DS    0H                                                    P3
         LR    R13,R3              RESTORE CALLER'S R13              P3
         RETURN (14,12),RC=0       RETURN TO CALLER                  P3
         TITLE 'L E X L O G O N  ***  DUMMY STAX EXIT'           87303
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    STAXDXIT                                                        P3
*                                                                    P3
* FUNCTION -                                                         P3
*    ENTERED AFTER AN ATTENTION INTERRUPT HAS OCCURED.               P3
*                                                                    P3
* OPERATION -                                                        P3
*    1) RETURNS TO CALLER.                                           P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    STAXDXIT                                                        P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    ATTENTION EXIT PARAMETER LIST (AEPL)                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    NONE                                                            P3
*                                                                    P3
* MESSAGES -                                                         P3
*    NONE                                                            P3
*                                                                    P3
* NOTES -                                                            P3
*    ADDRESSABILITY IS ESTABLISHED TO REST OF EXIT AND TO DYNAMIC    P3
*    AREA IN CASE THIS MAY BE NEEDED IN THE FUTURE.                  P3
*                                                                    P3
*********************************************************************P3
         SPACE 1                                                 87303
STAXDXIT DS    0H                                                    P3
         SAVE  (14,12),,STAXDXIT   SAVE CALLER'S REGISTERS           P3
         USING AEPL,R1             ESTABLISH ADDRESSABILITY TO AEPL  P3
         L     R2,AEPLUSAD         GET ADDRESS OF USADDR FROM AEPL   P3
         LR    R3,R13              SAVE CALLER'S R13                 P3
         LM    R9,R13,0(R2)  RELOAD BASES FROM PARM LIST         87303
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       RETURNS TO CALLER.                                           P3
*                                                                    P3
*********************************************************************P3
         LR    R13,R3              RESTORE CALLER'S R13              P3
         RETURN (14,12),RC=0       RETURN TO CALLER                  P3
         TITLE 'L E X L O G O N  ***  TIMER EXIT'                87303
*********************************************************************P3
*                                                                    P3
* ROUTINE NAME -                                                     P3
*    TIMERXIT                                                        P3
*                                                                    P3
* FUNCTION -                                                         P3
*    ENTERED AFTER A STIMER INTERRUPT HAS OCCURED.                   P3
*                                                                    P3
* OPERATION -                                                        P3
*    1) ISSUE MESSAGE TO OPERATOR AND ABEND WITH U0522.              P3
*                                                                    P3
* ENTRY POINTS -                                                     P3
*    TIMERXIT                                                        P3
*                                                                    P3
* EXTERNAL REFERENCES -                                              P3
*    NONE                                                            P3
*                                                                    P3
* INPUT -                                                            P3
*    NONE                                                            P3
*                                                                    P3
* OUTPUT -                                                           P3
*    NONE                                                            P3
*                                                                    P3
* MESSAGES -                                                         P3
*    'USER-ID ON TERMINAL LOGON TERMINATED DUE TO WAIT TIME EXCEEDED P3
*     - ASID X'NN''                                                  P3
*                                                                    P3
* COMMENTS -                                                         P3
*    THIS ROUTINE IS MOVED TO GTIMRXIT BY THE HOUSEKEEPING ROUTINE.  P3
*    THE LABEL AFTER LTIMRXIT, TTIMRWTO MUST BE KEPT ALIGNED WITH    P3
*    GTIMRWTO IN THE DYNAMIC AREA.                                   P3
*                                                                    P3
*********************************************************************P3
         SPACE 1                                                 87303
TIMERXIT DS    0F                                                    P3
         PUSH  USING               SAVE MAINLINE'S ADDRESSABILITY    P3
         DROP  ,                   DROP MAINLINE'S ADDRESSABILITY    P3
         SAVE  (14,12),,TIMERXIT   SAVE CALLER'S REGISTERS           P3
         USING TIMERXIT,R12        GET TEMPORARY ADDRESSABILITY      P3
         LR    R12,R15             SET TEMPORARY BASE                P3
         SPACE 1                                                 87303
*********************************************************************P3
*                                                                    P3
*       ISSUE MESSAGE TO OPERATOR AND ABEND WITH U0522.              P3
*                                                                    P3
*********************************************************************P3
         WTO   MF=(E,TTIMRWTO)     TELL OPERATOR WHY             87303
         ABEND 522                 AND ABEND WITH U0522              P3
         DS    F                   FILL OUT TO WORD IF NECESSARY     P3
LTIMRXIT EQU   *-TIMERXIT          CALCULATE LENGTH OF STIMER EXIT   P3
TTIMRWTO DS    C                   CORRESPONDS TO GTIMRWTO AFTER MOV P3
         SPACE 1                                                 87303
         POP   USING               RESTORE MAINLINE'S ADDRESSABILITY P3
         TITLE 'L E X L O G O N  ***  CONSTANTS'
KBIT0    EQU   X'80'
KBIT1    EQU   X'40'
KBIT2    EQU   X'20'
KBIT3    EQU   X'10'
KBIT4    EQU   X'08'
KBIT5    EQU   X'04'
KBIT6    EQU   X'02'
KBIT7    EQU   X'01'
         SPACE 1                                                 87303
LESTAE   ESTAE ,MF=L               ESTAE PARAMETER LIST
LPUTGET  PUTGET ,MF=L              PUTGET PARAMETER LIST
KLOGON   DC    CL8'LOGON'          VALID COMMAND NAME
KLOGOFF  DC    CL8'LOGOFF'         VALID COMMAND NAME                P3
KEND     DC    C'END'              INDICATE END OF 'JCL'             P3
BLANKS   DC    CL80' '             BLANKS                            P3
TRTABLE  DC    C'0123456789ABCDEF' TRANSLATE HEX TO EBCDIC
OPENUPDT OPEN  (SYSUADS,UPDAT),MF=L OPEN FOR UPDATE                  P3
OPENOUT  OPEN  (SYSUADS,OUTPUT),MF=L OPEN FOR OUTPUT                 P3
SYSUADS  DCB   DDNAME=SYSUADS,MACRF=(R,W),DSORG=PO
LSYSUADS EQU   *-SYSUADS           CALCULATE LENGTH OF SYSUADS DCB
KEYZERO  MODESET KEY=ZERO,MF=L     GET KEY 0                         P3
NONZERO  MODESET KEY=NZERO,MF=L    RETURN TO USER KEY                P3
QNAME    DC    C'SYSIKJUA'         QNAME FOR ENQ
QNAME2   DC    C'SPFDSN  '         QNAME FOR ENQ                     P3
ENQLIST  ENQ   (,,E,,SYSTEM),RET=TEST,MF=L
         DS    F                   ALLOW ROOM FOR RESERVE UCB ADDR   P3
LENQLIST EQU   *-ENQLIST
ENQLIST2 ENQ   (,,E,,SYSTEM),RET=HAVE,MF=L                           P3
RESLIST  RESERVE (,,E,,SYSTEMS),RET=HAVE,UCB=0,MF=L                  P3
SDUMP    SDUMP MF=L                SDUMP PARAMETER LIST              P3
LSDUMP   EQU   *-SDUMP             CALCULATE LENGTH OF SDUMP PARM    P3
MSGCLASS DC    C',MSGCLASS='       MSGCLASS KEYWORD                  P3
MSGLEVEL DC    C',MSGLEVEL=('      MSGLEVEL KEYWORD                  P3
TIME     DC    C',TIME='           TIME KEYWORD                      P3
FORMS    DC    C',FORMS='          FORMS KEYWORD                     P3
ROOM     DC    C',ROOM='           ROOM KEYWORD                      P3
COPIES   DC    C',COPIES='         COPIES KEYWORD                    P3
LINECT   DC    C',LINECT='         LINECT KEYWORD                    P3
         SPACE 1                                                 87303
         TITLE 'L E X L O G O N  ***  MESSAGES'
*                          1         2
*                0123456789012345678901234567
ESTAEMSG DC    C'ESTAE UNSUCCESSFUL - RC=XXXX'
STAXOBUF DC    C'ENTER ''LOGON'' OR ''LOGOFF'' -'                    P3
*                          1         2                               P3
*                012345678901234567890123456                         P3
STAXMSG  DC    C'STAX UNSUCCESSFUL - RC=XXXX'                        P3
*                          1         2                               P3
*                012345678901234567890123456                         P3
TGETMSG  DC    C'TGET UNSUCCESSFUL - RC=XXXX'                        P3
RESCFAIL DC    C'LOGON RESOURCE FAILURE. CONTACT TECHNICAL SUPPORT'
LOGABEND DC    C'LOGON ABEND. CONTACT TECHNICAL SUPPORT'         92300
OPENMSG  DC    C'OPEN OF SYS1.UADS UNSUCCESSFUL'
*                          1         2
*                012345678901234567890123456
ABNDMSG  DC    C'LOGON USER EXIT ABEND      '
*                     1         2         3         4         5      P3
*               4567890123456789012345678901234567890123456789012345678
ABNDWTO  WTO   'UNKNOWN ON TERMINAL LOGON USER EXIT ABEND       - ASID X
               X''NN''',DESC=2,ROUTCDE=(2,11),MF=L                   P3
*              90 123                                                P3
*               6                                                    P3
LABNDWTO EQU   *-ABNDWTO           CALCULATE LENGTH OF WTO MESSAGE   P2
*                     1         2         3         4         5      P3
*               4567890123456789012345678901234567890123456789012345678
TIMERWTO WTO   'UNKNOWN ON TERMINAL LOGON TERMINATED DUE TO WAIT TIME EX
               XCEEDED - ASID X''NN''',ROUTCDE=(2,11),MF=L         P3.1
*              90123456789012345 678                               P3.1
*               6         7                                          P3
LTIMRWTO EQU   *-TIMERWTO          CALCULATE LENGTH OF WTO MESSAGE   P3
*                     1         2         3         4                P3
*               4567890123456789012345678901234567890 123            P3
LOGWTO   WTO   'UNKNOWN LOGGING ON TERMINAL - ASID X''NN''',         P3X
               MCSFLAG=HRDCPY,MF=L                                   P3
LLOGWTO  EQU   *-LOGWTO            CALCULATE LENGTH OF WTO MESSAGE   P3
CLRSCRN  DC    X'27F5C31D40'  CLEAR SCREEN                       87303
LASTCARD DC    C'ENTER LAST JCL CARD OR ''END'''                     P3
NEXTCARD DC    C'ENTER NEXT JCL CARD OR ''END'''                     P3
BADJCL   DC    C'JCL IGNORED'                                        P3
         TITLE 'L E X L O G O N  ***  MODEL JCL CARDS'
LJCLMODL DC    0F'0',C' ',AL3(LENJMODL) FILL CHARACTER AND LENGTH    P3
JCLMODEL DS    0C                  START OF MODEL FOR JCL            P3
*                             1         2          3         4       P3
*                   012345678901234567890123 456789012345678901234 5 P3
MJOBCARD DC    CL80'//USER-ID  JOB '                             88329
         ORG   MJOBCARD+15   START OF ACCOUNT FIELD              88329
         AIF   ('&LOCAL' EQ 'PID').JESACCT                       92300
MJOBACCT DC    CL8' ',C','''   ACCOUNT, START OF PROG.NAME       88329
MJOBSUBA EQU   MJOBACCT+4,4,C'C'                                 88329
MJOBPGNM DC    CL12' ',C''''   PROGRAMMER NAME                   88329
         AGO   .JESCOM                                           88329
.JESACCT ANOP  ,             START OF LOCAL ACCOUNT DATA         88329
         DC    C'('          START OF ACCOUNT FIELD              88329
MJOBACCT DC    CL4' '        MAJOR ACCOUNT                       88329
         DC    C','                                              88329
MJOBSUBA DC    CL4' ',C'),'''  SUB-ACCOUNT                       88329
MJOBPGNM DC    CL12' ',C''''   PROGRAMMER NAME                   88329
.JESCOM  ORG   ,             LENGTH=80                           88329
MEXECARD DC    CL80'//PROCNAME EXEC '                                P3
MEXEPARM DC    CL80'// PARM= '    PARM FOR INITIAL COMMAND       87309
MJOBPARM DC    CL80'/*JOBPARM '                                      P3
LENJMODL EQU   *-JCLMODEL          CALCULATE LENGTH OF JCL MODEL     P3
         SPACE 1                                                 87303
*        MAPPING OF PASSED PARAMETER DESCRIPTORS                 87303
PPPDSECT DSECT ,                                                 87303
PPPSW    DS    A             POINTER TO SWITCH DESCRIPTOR        87303
OFFADD   EQU   0,4,C'A'      ADDRESS OFFSET                      87303
OFFMAX   EQU   4,2,C'H'      MAX LENGTH OFFSET                   87303
OFFCUR   EQU   6,2,C'H'      CUR LENGTH OFFSET                   87303
PPPIN    DS    A             POINTER TO INPUT BUFFER DESCRIPTOR  87303
PPPUID   DS    A             POINTER TO USERID DESCRIPTOR        87303
PPPPSW   DS    A             POINTER TO PASSWORD DESCRIPTOR      87303
PPPACT   DS    A             POINTER TO ACCOUNT DESCRIPTOR       87303
PPPPRO   DS    A             POINTER TO PROCEDURE NAME           87303
PPPREG   DS    A             ADDRESS OF REGION SIZE              87303
PPPJCL   DS    A             POINTER TO JCL DESCRIPTOR           87303
PPPNPS   DS    A             POINTER TO NEW PASSWORD DESCRIPTOR  87303
*                            SUPPORTED UNDER RACF OR SUCH ONLY   87303
PPPSA    DS    A             POINTER TO SYSTEM ATTRIBUTE DESC.   87303
PPPUA    DS    A             POINTER TO USER ATTRIBUTE DESCRIPTOR
PPPGEN   DS    A             POINTER TO GENERIC UNIT DESCRIPTOR  87303
*                            MAY BE VOL-SER (SYSTEM MOD)         87303
PPPUPT   DS    A             POINTER TO UPT DESCRIPTOR           87303
PPPECT   DS    A             POINTER TO ECT DESCRIPTOR           87303
PPPCAN   DS    A             POINTER TO CSCB CANCEL ECB          87303
PPPCC    DS    A             POINTER TO LAST RETURN CODE         87303
PPPPGN   DS    A             POINTER TO PERFORMANCE GROUP NUMBER 87303
PPPOUT   DS    A             POINTER TO SYSOUT DESTINATION DESC. 87303
PPPGRP   DS    A             POINTER TO GROUP DESCRIPTOR (RACF)  87303
         SPACE 1                                                 87303
SWDSECT  DSECT ,             MAPPING OF CONTROL SWITCHES         87303
SW1      DS    X                                                 87303
SWENQBD  EQU   X'80'           USERID ENQUEUE FAILED             87303
SWRESBD  EQU   X'20'           RESOURCE FAILURE                  87303
SWDISC   EQU   X'10'           DISCONNECT                        87303
SWNPRM   EQU   X'08'           DON'T PROMPT                      87303
SWNUAD   EQU   X'04'           NO UADS                           87303
SWJCL    EQU   X'02'           JCL SUPPLIED                      87303
SWNUADE  EQU   X'01'           NO UADS EXTENDED                  87303
SW2      DS    X                                                 87303
SWSA     EQU   X'80'           SYSTEM ATTRIBUTES SUPPLIED        87303
SWUA     EQU   X'40'           USER ATTRIBUTES SUPPLIED          87303
SWGRP    EQU   X'20'           GENERIC GROUP SUPPLIED            87303
SWUPT    EQU   X'10'           UPT SUPPLIED                      87303
SWNENQ   EQU   X'08'           DON'T ENQUEUE USER                87303
SWDEST   EQU   X'04'           DESTINATION SUPPLIED              87303
SWABND   EQU   X'02'           PRE-LOGON ABEND RECURSION         87303
SWRECN   EQU   X'01'           RECONNECT USER                    87303
SW3      DS    X                                                 87303
SWMAIL   EQU   X'80'           USER WANTS MAIL                   87303
SWNOTIC  EQU   X'40'           USER WANTS NOTICES                87303
SWNCRT   EQU   X'20'           INHIBIT FULL-SCREEN LOGON         87303
SWTSBPW  EQU   X'10'           STORE PASSWORD IN TSB             87303
         TITLE 'L E X L O G O N  ***  SYSTEM MAPPINGS'           87303
         PUSH  PRINT                                             87303
         PRINT &PRTSYS       SET SYSTEM MACRO EXPANSION OPTION   87303
         IKJCPPL ,                 GENERATE CPPL DSECT
         DS    0F
LCPPL    EQU   *-CPPL              CALCULATE LENGTH OF CPPL
         SPACE 1                                                 87303
CBUF     DSECT                     COMMAND BUFFER DSECT
CBUFLEN  DS    H                   LENGTH
CBUFOFFS DS    H                   OFFSET
CBUFTEXT DS    C                   TEXT
         SPACE 1                                                 87303
STX      DSECT                                                       P3
STXEXIT  DS    F                   ADDRESS OF STAX EXIT              P3
STXISIZ  DS    H                   LENGTH OF IBUF                    P3
STXOSIZ  DS    H                   LENGTH OF OBUF                    P3
STXOBUF  DS    F                   ADDRESS OF OBUF                   P3
STXIBUF  DS    F                   ADDRESS OF IBUF                   P3
STXOPTS  DS    (1+3*&MVSXA)X       OPTION FLAGS                      P3
STXNUSER DS    XL(3+&MVSXA)        ADDRESS OF USER AREA              P3
         SPACE 1                                                 87303
AEPL     DSECT                                                       P3
AEPLTAIE DS    F                   ADDRESS OF THE TAIE               P3
AEPLIBUF DS    F                   ADDRESS OF IBUF                   P3
AEPLUSAD DS    F                   ADDRESS OF USER AREA              P3
         SPACE 1                                                 87303
         IKJTAIE ,                 GENERATE TAIE DSECT               P3
         SPACE 1                                                 87303
         IKJCSPL ,                 GENERATE CSPL DSECT
         DS    0F
LCSPL    EQU   *-CSPL              CALCULATE LENGTH OF CSPL
         SPACE 1                                                 87303
         IKJCSOA ,                 GENERATE CSOA DSECT
         DS    0F
LCSOA    EQU   *-CSOA              CALCULATE LENGTH OF CSOA
         SPACE 1                                                 87303
         IKJPPL ,                  GENERATE PPL DSECT
         DS    0F
LPPL     EQU   *-PPL               CALCULATE LENGTH OF PPL
         SPACE 1                                                 87303
         IKJEFFGF GFDSECT=YES      GENERATE GNRLFAIL DSECT
         IHAASVT ,                 GENERATE ASVT DSECT
         IHAASCB ,                 GENERATE ASCB DSECT
         IHAASXB ,                 GENERATE ASXB DSECT               P3
         IHAGDA ,                                                88002
         SPACE 1                                                 87303
         IKJEFLWA ,          FROM SOURCE TAPES                   87333
*DEFER*  IKJEFUAD NODSP,ALL    DITTO                             87333
         SPACE 1                                                 87303
         IKJTSB ,                  GENERATE TSB DSECT
         SPACE 1                                                 87303
         CVT   DSECT=YES           GENERATE CVT DSECT               .P3
         SPACE 1                                                 87303
         DCBD  DSORG=PS,DEVD=DA    GENERATE DCB DSECT
         SPACE 1                                                 87303
         IHASDWA ,                 GENERATE SDWA DSECT
         SPACE 1                                                 87359
         IEECHAIN ,                                              87359
         SPACE 1                                                 87303
         IHAPSA ,                  GENERATE PSA DSECT                P3
         SPACE 1                                                 87303
         IKJTCB ,                  GENERATE TCB DSECT                P3
         SPACE 1                                                 87303
         IEZJSCB ,                 GENERATE JSCB DSECT               P3
         PRINT &PRTMAC       EXPAND SPECIAL MACROS               87303
         SPACE 1                                                 87303
         IKJPSCB ,                 GENERATE PSCB DSECT               P3
         SPACE 1                                                 87303
         IKJECT ,                  GENERATE ECT DSECT              P3.2
         SPACE 1                                                 87303
         IKJUPT ,                                                87303
         SPACE 1                                                 87303
         POP   PRINT         RESTORE MAIN-LINE PRINT OPTION      87303
         TITLE 'L E X L O G O N  ***  DYNAMIC AREA'
SAVE     DSECT ,             CONTINUATION OF SAVE/WORK AREA      87303
INITSAVE DS    9D            FAKE SAVE AREA IF TCBFSA IS ZERO    87310
VERSAVEA DS    9D                  VERIFY ROUTINES SAVE AREA
DB       DS    2D            WORK AREA                           87303
WORKAREA DS    D                   DOUBLE WORD WORK AREA
GETSAVER DS    5F            SAVE AREA FOR GET ROUTINES          87303
ADDRPARM DS    F                   ADDRESS OF PARMS
ADDRUBUF DS    F                   ADDRESS OF SYS1.UADS BUFFER
ADDRUCB  DS    F                   ADDRESS OF SYS1.UADS UCB          P3
ADDRLOUD DS    F             ADDRESS OF LOCAL ONLINE USER DATA   87303
PASSOFF  DS    F                   OFFSET TO MATCHING PSWD OFFSET BLOCK
EXLST    DS    F                   DCB EXIT LIST FOR RDJFCB          P3
@SERVICE DS    A             ADDRESS OF THE SERVICE ROUTINE      87309
@SCREENS DS    A             ADDRESS OF THE SCREEN FORMATTER     87309
CURRTIME DS    A        1/2                                      87314
CURRDATE DS    A        2/2                                      87314
         SPACE 1                                                 87303
         PUSH  PRINT                                             87303
         PRINT &PRTMAC       EXPAND IT                           87303
A$SVCRB  A$SVCRB DSECT=NO    EXPAND LOCAL ACCOUNTING PARM AREA   87303
         POP   PRINT                                             87303
         SPACE 1                                                 87309
FDW      MAPFDW DSECT=NO                                         87309
         SPACE 1                                                 87309
FDINWORK FDIN  *EXPAND       @SCREENS INPUT WORK AREA            87309
         ORG   FDINWORK      REDEFINE                            87309
MAPFID   MAPFIW DSECT=NO,PFX=FID,LEN=8                           87309
MAPFAC   MAPFIW DSECT=NO,PFX=FAC,LEN=8                           87309
MAPFPS   MAPFIW DSECT=NO,PFX=FPS,LEN=8                           87309
MAPFNP   MAPFIW DSECT=NO,PFX=FNP,LEN=8                           87314
MAPFPR   MAPFIW DSECT=NO,PFX=FPR,LEN=8                           87309
MAPFCP   MAPFIW DSECT=NO,PFX=FCP,LEN=4                           87309
MAPFMC   MAPFIW DSECT=NO,PFX=FMC,LEN=1                           87309
MAPFML   MAPFIW DSECT=NO,PFX=FML,LEN=3                           87309
MAPFRG   MAPFIW DSECT=NO,PFX=FRG,LEN=4                           87309
MAPFGE   MAPFIW DSECT=NO,PFX=FGE,LEN=8                           87309
MAPFCM   MAPFIW DSECT=NO,PFX=FCM,LEN=60                          87309
MAPFMA   MAPFIW DSECT=NO,PFX=FMA,LEN=1                           87309
MAPFRC   MAPFIW DSECT=NO,PFX=FRC,LEN=1                           87309
         ORG   ,                                                 87309
FDINWORX EQU   *             END OF WORK AREA                    87313
GJFCB    DS    0F                                                87303
         IEFJFCBN ,                                              87303
         SPACE 1                                                 87303
GMSGAREA DS    0CL125              COMMON MESSAGE AREA
GESTAMSG DS    0CL(L'ESTAEMSG)     ESTAE MESSAGE
GSTAXMSG DS    0CL(L'STAXMSG)      STAX MESSAGE                      P3
GTGETMSG DS    0CL(L'TGETMSG)      TGET MESSAGE                      P3
GLOGWTO  DS    0CL(LLOGWTO)        LOGON WTO MESSAGE                 P3
GABNDMSG DS    0CL(L'ABNDMSG)      ABEND MESSAGE
GABNDWTO DS    0CL(LABNDWTO),CL125 ABEND WTO MESSAGE                 P2
         SPACE 1                                                 87303
*        THE FOLLOWING STORAGE IS FOR THE CPPL
         DS    0F                  ALIGN FOR CPPL
GCPPL    DS    XL(LCPPL)           CPPL
         SPACE 1                                                 87303
*        THE FOLLOWING STORAGE IS FOR THE CBUF
GCBUF    DS    XL256               CBUF
         SPACE 1                                                 87303
*        THE FOLLOWING STORAGE IS FOR ESTAE
GESTAE   ESTAE ,MF=L               ESTAE PARAMETER LIST
LGESTAE  EQU   *-GESTAE            CALCULATE LENGTH OF ESTAE PARM LIST
ESTAEPRM DS    5F                  ESTAE EXIT PARAMETER LIST       P3.1
ABENDCDE DS    F                   ABEND CODE FROM ESTAE EXIT
ABENDPSW DS    CL8                 ABEND PSW FROM ESTAE EXIT         P3
ABENDREG DS    16F           REGISTERS FROM ABEND                87303
         SPACE 1                                                 87303
*        THE FOLLOWING STORAGE IS FOR STAX                           P3
GDSTAX   STAX  ,MF=L               DUMMY STAX PARAMETER LIST         P3
GSTAX    STAX  ,MF=L               STAX PARAMETER LIST               P3
LSTAX    EQU   *-GSTAX             CALCULATE LENGTH OF STAX PRM LIST P3
         SPACE 1                                                 87303
*        THE FOLLOWING STORAGE IS FOR SAVE AREAS FOR INTERNAL ROUTINES
HSKPSAVE DS    F                   HOUSKEEPING ROUTINE
BJCLSAVE DS    3F                  BUILD JCL SUBROUTINE              P3
GAUTSAVE DS    F                   GET AUTHORIZATON SUBROUTINE       P3
DBUGSAVE DS    F                   DEBUG ROUTINE
         SPACE 1                                                 87303
*        THE FOLLOWING STORAGE IS FOR STAX AND STIMER EXITS          P3
         DS    0F                  FORCE ALIGNMENT                   P3
GTIMRXIT DS    XL(LTIMRXIT)        STIMER EXIT ROUTINE IS MOVED HERE P3
GTIMRWTO DS    CL(LTIMRWTO)        TIMER WTO MESSAGE                 P3
GCPECB   DS    F                   CP ECB                            P3
         SPACE 1                                                 87303
*        THE FOLLOWING STORAGE IS FOR SWITCHES                     P3.2
GBITS    DS    0F                  SWITCHES                          P3
GBITS1   DS    X                   SWITCHES                          P3
GDENY    EQU   KBIT0               LOGON DENIED
GRECON   EQU   KBIT1               'RECON' SPECIFIED
GNOUADS  EQU   KBIT2           RUN WITHOUT UADS ENTRY            87310
GBYPASS  EQU   KBIT3         BYPASS OUR LOGON (USE IBM'S)        87333
GDEBUG   EQU   KBIT4               'DEBUG' SPECIFIED
GDUPUSER EQU   KBIT5               DUPLICATE USERID LOGGED ON
GLIST    EQU   KBIT6               'LISTINFO' SPECIFIED              P3
GATTNHIT EQU   KBIT7               ATTENTION HIT                     P3
GBITS2   DS    X                   SWITCHES                          P3
GSPFRES  EQU   KBIT0               DSN RESERVED                      P3
GSPFENQ0 EQU   KBIT1               MEMBER0 ENQ'D ON                  P3
GMEMENQ  EQU   KBIT2         SYSIKJUA/UID ENQUEUED UPON          87361
GJCL     EQU   KBIT3               'JCL' SPECIFIED                   P3
GABEND   EQU   KBIT4               ABEND HAS OCCURED                 P3
GDISPLAY EQU   KBIT5               DISPLAY TERMINAL                  P3
GREWRITE EQU   KBIT6               REWRITE FIRST UADS BLOCK        P3.2
FSFLAGS  DS    X             FULL-SCREEN SWITCHES                87309
FSUID    EQU   X'80'           USER-ID HAS BEEN FOUND            87309
FSACCT   EQU   X'40'           ACCOUNT SET                       87309
FSPASS   EQU   X'20'           PASSWORD SET                      87309
FSNPASS  EQU   X'10'           NEW PASSWORD SPECIFIED            87309
FSONCE   EQU   X'01'           RESET AT FIRST SCREEN I/O         87333
GECTSWS  DS    X                   COPY OF ECTSWS                  P3.2
         SPACE 1                                                 87303
*        THE FOLLOWING STORAGE IS FOR GNRLFAIL
GGFPARMS DS    XL(GFLENGF)         GNRLFAIL PARAMETER LIST
GGFPARMP DS    F                   GNRLFAIL PARM LIST POINTER
         SPACE 1                                                 87303
GMEMBER  DS    CL8                 MEMBER NAME                       P3
GACCTNUM DS    CL8           DESIRED ACCOUNT NUMBER              87333
GPASS    DS    CL8           PURPORTED OLD PASSWORD              87333
GNEWPSWD DS    CL8                 NEW PASSWORD                      P2
GTERMID  DS    CL8                 TERMINAL ID FROM TSB              P3
GSYSID   DS    CL4           SMF SYSTEM ID                       87314
GASID    DS    CL2                 ASID FROM ASCB                    P3
LUSERID  DS    H                   LENGTH OF USERID
GFORMS   DS    CL4                 FORMS                             P3
GROOM    DS    CL4                 ROOM                              P3
GMSGLEVL DS    CL3                 MSGLEVEL                          P3
GCOPIES  DS    CL3                 COPIES                            P3
GLINECT  DS    CL3                 LINECT                            P3
YNMAIL   DS    C             MAIL YES/NO FLAG                    87314
YNNOTE   DS    C             NOTICES YES/NO FLAG                 87334
YNRECON  DS    C             RECONNECT Y/N FLAG                  87314
YN2SES   DS    C             RUN WITH/WITHOUT UADS               87333
YNIBM    DS    C             Y FOR IBM LOGON                     87333
YNUPDAT  DS    C             MAKE CHANGES PERMANENT              87334
YNHELP   DS    C             Y TO DISPLAY HELP TEXT              88221
SVPATB   DS    XL2           SAVED ATTRIBUTE ADDRESS FOR PASSWORD FLD
ERRINDEX DS    X             ERROR MESSAGE INDEX                 88002
ERRCOUNT DS    F             COUNT OF FAILED LOGONS              87303
* THE FOLLOWING STORAGE IS FOR ACCESS OF SYS1.UADS
GOPENLST DS    F                   OPEN LIST
GTTR1ST  DS    F                   TTR OF FIRST BLOCK                P3
GLEN1ST  DS    F                   LENGTH OF FIRST BLOCK             P3
GSYSUADS DS    XL(LSYSUADS)        DCB FOR SYS1.UADS
         READ  DECB,SF,MF=L        READ/WRITE DECB LIST              P2
         SPACE 1                                                 87303
* THE FOLLOWING STORAGE IS FOR ENQ
         DS    0F                  ALIGNMENT                         P3
GENQLIST DS    XL(LENQLIST)        ENQ LIST
GRNAME   DS    CL52                RNAME FOR SPF TYPE ENQ            P3
         SPACE 1                                                 87303
* THE FOLLOWING STORAGE IS FOR SDUMP                                 P3
         DS    0F                  ALIGNMENT                         P3
GSDUMP   DS    XL(LSDUMP)          SDUMP PARAMETER LIST              P3
         SPACE 1                                                 87303
* THE FOLLOWING STORAGE IS FOR BLDL                                  P3
GBLDL    DS    0F                  BLDL LIST                         P3
GBLL     DS    H                   LL                                P3
GBFF     DS    H                   FF                                P3
GBNAME   DS    CL8                 NAME                              P3
GBTTR    DS    XL4                 TTRC                              P3
         SPACE 1                                                 87303
NXTBLANK DC    256AL1(0)     TRT TABLE                           87303
         DS    0D                  FILL OUT DSECT
         SPACE 1                                                 87333
U$ER     A$USER DSECT=NO,PFX=L$U  LOCAL COPY OF USER RECORD      87333
SAVEND   EQU   *             END OF GOTTEN WORK AREA             87303
         SPACE 1                                                 87303
         PRINT &PRTSYS       SYSTEM MACRO PRINT OPTION           87303
         IHACDE ,                                                87303
         SPACE 1                                                 87303
USERCVT  USERCVT ,           USER CVT EXTENSION                  87303
USERTCB  USERTCB ,           DON'T NEED HERE ?                   87303
         A$GDA DSECT         GLOBAL DATA AREA                    91006
         SPACE 1                                                 87303
         PRINT &PRTMAC                                           87303
A$LSECT  A$LOUD ,            LOCAL ON-LINE USER DATA             87303
         END   ,
