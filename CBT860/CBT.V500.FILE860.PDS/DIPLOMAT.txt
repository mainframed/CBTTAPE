DIPL TITLE 'DIPLOMACY ---  THE GAME OF DOUBLE-CROSSING YOUR FRIENDS'
         SPACE 2
         MACRO
&NM      RETCH &X
         LCLA  &I,&J
&I       SETA  K'&X
         AIF   (&I LT 1 OR &I GT 4).BAD
&NM      OI    RETCODE+1,&X
         MEXIT
.BAD     MNOTE 4,'INVALID RETURN CODE'
         MEND
         SPACE 2
         MACRO
&NM      CALL  &X
         AIF   ('&X' NE 'XPRNT').NOPR
&NM      L     R15,VXPRNT    USE V-CON
         AGO   .BALR
.NOPR    ANOP  ,
&NM      L     R15,=V(&X)
.BALR    BALR  R14,R15
         MEND
         EJECT
         PRINT NOGEN
DIPLOMAT START 0
         REGEQU ,
         SPACE
         USING *,R12
         STM   R14,R12,12(R13)
         LR    R12,R15
         LA    R9,SAVEAREA
         ST    R9,8(,R13)
         ST    R13,4(,R9)
         LR    R13,R9
         LM    R10,R11,=A(DIPLCOMM,DIPLSUBS)  GET SUBROUTINE / DATA ADD
         USING DIPLSUBS,R11
         USING DIPLCOMM,R10
         LM    R2,R3,CHECKPRO   GET CHECKPOINT START/END ADDRESS
         SR    R3,R2         MAKE INTO LENGTH
         ST    R3,CHECKPRO+4 STASH LENGTH BACK
         LM    R1,R2,ARET#BEG
         AR    R1,R2
         ST    R1,ARET#BEG   RELOACTE
         CALL  XPROP         OPEN OUTPUT FILES
         OPEN  (SYSIN,(INPUT))
         TM    SYSIN+48,X'10'
         BZ    EXIT16        NOT OPEN, BOO
         BAL   R9,GETCARD    GET THE FIRST CARD
         NOP   0             IGNORE TYPE
         BAL   R9,PARMA      LOOK AT IT
         CLI   PARM1,C'$'    CONTROL CARD ?
         BNE   CONBANIN      GO TO INITIALIZE
         CLC   =C'INIT',PARM1+1   IS IT INIT ?
         BE    CONBAN        SKIP INPUT OF OLD DATA
CONBANIN BAL   R9,NOTINIT    RESTORE PREVIOUS STATUS FROM DISK
CONBAN   MVC   BANONE(17),BLANKS   CLEAR
         MVC   BANTWO(2),DATE   COPY ' F' OR 'SP'
         MVC   BANTWO+3(4),YEAR-2   COPY YEAR
         OI    READ,RFULL    RE-READ SAME CARD ON NEXT REQUEST
         BAL   R9,BANNER
         SPACE 2
CONEJECT LA    R1,VNULL      SET FOR PAGE EJECT
         CALL  XPRNT         PRINT SIMPLE MESSAGE
CONTROL  TM    FLAG,STOP     SCREECHING HALT ?
         BO    EXIT12        YES
         LA    R9,CONTROL    SET PROVISIONAL RETURN POINT
         TM    FLAG,SEND     NORMAL END ?
         BO    EXIT
         SPACE
CONNEW   OI    READ,RCON     GET A CONTROLCOMMAND
         BAL   R9,GETCARD    GET A CARD
         B     EXIT          NONE GOTTEN
         BAL   R9,PARMA      LOOK AT CURRENT CARD
         B     CONNEW        MAJOR ERROR - SKIP IT
         LA    R2,VERBTAB    GET VERB/ CONTROL TABLE
         LA    R3,(VERBTABE-VERBTAB)/12  NO. OF ENTRIES
CONVERB  MVC   *+7(1),4(R2)  MOVE LENGTH - 1
         CLC   PARM1+1(0),5(R2)  SAME COMMAND ?  (SKIP $)
         BE    CONVB         YES, DO VALIDITY CHECK
         LA    R2,12(,R2)    GET NEXT COMMAND
         BCT   R3,CONVERB    TRY AGAIN
CONME    MVC   CAMSG(L'MSINCO),MSINCO   INCORRECT COMMAND OR BAD TIME
CONPR    BAL   R9,ECHO       TELL PLAYER
         B     CONNEW
         SPACE
CONBT    MVC   CAMSG(L'MSBADT),MSBADT   ILL-TIMED ?
         B     CONPR         PUT MESSAGE
         SPACE
CONVB    CLI   0(R2),X'FF'   IS IT A CATCH-ALL CODE ?
         BE    CONLR         YES, CONTINUE WITH IT
         TM    FLAG,STAT     IS STATUS TO BE PRINTED ?
         BZ    *+12          NO, TRY FOR CHECKPOINT
         OI    READ,RFULL    SAVECARD FOR LATER
         B     STATUS        DISPLAY BOARD STATUS ANDMAKE UP ORDER SHEE
         TM    FLAG,SAVE     WAS SAVE REQUESTED ?
         BZ    CONVT         NO,  CHECK TIMING
         OI    READ,RFULL    SET FOR RE-PROCESS
         B     CHEKPOIN      AND CHECKPOINT CURRENT INFO
CONVT    MVC   DB(1),0(R2)   MOVE CONTROL BITS
         NC    DB(1),PLAY    CHECK VALIDITY AT THIS TIME
         BZ    CONBT         ILL-TIMED
CONLR    L     R15,0(,R2)    GET BRANCH ADDRESS
         BALR  R9,R15        CALL CODE
         B     CONTROL       IF CONTROL RETURNS HERE, RETURN TO CONTROL
         SPACE 2
EXIT16   RETCH 16
         B     EXIT
         SPACE
EXIT12   RETCH 12
         B     EXIT
         EJECT
INITIAL  LA    R1,VINIT
         CALL  XPRNT         EJECT
         OI    FLAG,STAT+SAVE   PRINT STATUS AND SAVE
         MVI   PLAY,PMOVE+PMSG   SET FOR MOVE EXPECTED
         MVI   VERBTAB,0     KILL 'INIT' COMAND
         MVI   NEWPLAY,PMOVE+PMSG
         MVC   NEWDATE,=C'SPRING 1901'   SET NEW PLAYNG DATE
         L     R4,APRO#END   MAKE A WORK AREA
         LA    R5,7          DO FOR MAX OF SEVEN COUNTRIES
         ZAP   MAXSUP,P0     SET NO. OF PLAYERS
INITRLOP MVC   0(80,R4),BLANKS    CLEAR OUT
         BAL   R9,GETCARD    GET A DATA CARD
         B     INITREND      SKIP IF CONTROL CARD
         CLC   CARD,BLANKS   BLANK CARD ?
         BE    INITRLOP      YES, RE-READ
         MVC   0(80,R4),CARD   ELSE SAVE THE TEXT
         AP    MAXSUP,P1     UP THE ACTIVE COUNT
         LA    R4,80(,R4)    NEXT AREA
         BCT   R5,INITRLOP   TRY FOR MORE
INITREND CP    MAXSUP,P0     ANY CARDS SUPPLIED ?
         BE    INITDONE      NO; ALLOW IT
         CP    MAXSUP,=P'2'  ELSE NEED AT LEAST 3 PLAYERS
         BH    INITASGN      OK
         LA    R1,NEED3      GET NASTY
         CALL  XPRNT
         RETCH 12            KVETCH ABOUT IT
         B     CONTROL       AND TERMINATE
         SPACE
INITASGN ZAP   DB,MAXSUP     MOVE TO DOUBLE WORD
         CVB   R5,DB
         MVI   ROUTE,X'80'   RESET ROUTING
         MVI   NEWROUTE,X'80'
         LA    R6,TABASGN    GET RANDOM WORK TABLE
INITASGL LA    R3,3          SET FOR THREE DIGIT RANDOM NUMBER
         XR    R4,R4         CLEAR ACCUMULATOR
INITASGR LA    R8,7          USE ONLY EVERY SEVENTH DIGIT
         BAL   R14,RANDOM    GET A DIGIT
         BCT   R8,INITASGR+4    DO IT AGAIN
         MH    R4,=H'10'     SHIFT
         AR    R4,R0         ADD DIGIT IN
         BZ    INITASGR      SKIP IF ZERO
         BCT   R3,INITASGR   GET ANOTHER DIGIT
         STH   R4,2(,R6)     SAVE IN WORK TABLE
         OC    ROUTE,0(R6)   PROPAGATE ROUTCODE
         OC    NEWROUTE,0(R6)
         LA    R6,4(,R6)     NEXT TABLE ENTRY
         BCT   R5,INITASGL   DO FOR NEXT ENTRY
         LA    R1,MSASSGN    GET ASSIGNMENT MESSAGE
         CALL  XPRNT
         LA    R6,TABASGN
         LA    R7,7
         L     R8,APRO#END   GET NAME ENTRIES
INITCLOP XR    R4,R4
         LR    R1,R6         GET TABLE
         LA    R2,7
         XR    R5,R5         ZERO RESULT
INITCTST CLC   2(2,R1),=H'1'   SKIP GARBAGE
         BNH   INITCINC
         CH    R4,2(,R1)
         BH    INITCINC      SKIP IF ALREADY HIGHER
         LR    R5,R1         SAVE ADDRESS OF THIS ENTRY
         LH    R4,2(,R1)     SAVE VALUE
INITCINC LA    R1,4(,R1)
         BCT   R2,INITCTST   FIND LARGEST
         LTR   R5,R5         ANY FOUND ?
         BZ    INITDONE      NO, DONE
         MVC   AORDER,AORDER-1   CLEAR PRINT LINE
         MVC   AORDCOUN+8(11),=C'ASSIGNED TO'
         MVC   AORDCOUN+21(80),0(R8)   COPY NAME OF PLAYER
         MVC   2(2,R5),=H'1'  SET DONE
         LA    R1,0(,R5)     POINT TO NATION BIT
         BAL   R9,FORMACON   CONVERT TO NATION
         MVC   AORDCOUN(7),1(R2)  MOVE NATION
         LA    R1,VAORD      GET PRINT LINE
         CALL  XPRNT         SHOW IT
         LA    R8,80(,R8)
         BCT   R7,INITCLOP   DO FOR ALL
INITDONE CALL  DIPLINIT      PRINT OUT THE RULES
         L     R2,CHECKPRO
         MVC   0(STATEND-CHECKIN,R2),CHECKIN   INITIALIZE FOR MAP
         B     CONTROL                 TRY IT
         EJECT
CHEKPOIN CLI   RETCODE+1,4   ANY BAD ERRORS ?
         BH    CHEKPERR      YES - KVETCH ABOUT THEM
         OPEN  (DISK,(OUTPUT))   OPEN THE FILE
         TM    DISK+48,X'10'
         BZ    EXIT16        TOO BAD
         MVI   INITECB,0     CLEAR
         LM    R2,R3,CHECKPRO   GET CHECKPOINT RECORD ADDRESS/LENGTH
         MVC   CHECKIT(CHECKNEW-CHECKIT),CHECKNEW  MOVE 'NEW' PROCESSIN
         MVC   0(CHECKLEN,R2),CHECKIN   MOVE STUFF FROM THIS MOVE
         WRITE INITECB,SF,DISK,(R2),(R3)   CHECKPOINT IT
         CHECK INITECB
CHEKPEND CLOSE (DISK)
         NI    FLAG,255-SAVE    TURN OFF CHECKPOINT REQUEST FLAG
         B     CONTROL       CONTINUE PROCESSING
         SPACE
CHEKPERR LA    R1,VCHKIG     GET CHECKPOINT SKIPPED MESSAGE
         CALL  XPRNT         PRINT IT
         B     CHEKPEND      EXIT
         SPACE 2
NOTINIT  OI    READ,RFULL    SKIP 'GET'
         SPACE
CHEKPINP OPEN  (DISK,(INPUT))
         TM    DISK+48,X'10'
         BZ    EXIT16
         MVI   NINITECB,0
         LM    R2,R3,CHECKPRO   GET ADDRESS
         READ  NINITECB,SF,DISK,(R2),(R3)
         CHECK NINITECB
         CLOSE (DISK)
         MVC   CHECKIN(CHECKLEN),0(R2)  COPY STATUS, ETC.
         BR    R9            RETURN TO CALLER
         SPACE 2
INITIAM  LA    R1,NINEOF
         CALL  XPRNT
         B     INITIAL
         SPACE 2
SYNAD    LA    R1,VIOERR
         CALL  XPRNT
         B     EXIT16
         EJECT
STATUS   NI    FLAG,255-STAT  TURN REQUEST OFF
         CLI   RETCODE+1,4   MAJOR ERROR ENCOUNTERED ?
         BH    CONEJECT      YES - SKIP STATUS FORMATTING
         LA    R1,VSTATUS    PUT HEADER
         CALL  XPRNT
         CALL  DIPLMAPS      PRINT A MAP SHOWING CURRENT DISPOSITION
         LA    R1,VSTATUS    PAGE EJECT
         CALL  XPRNT
         ZAP   COPTOT,P0     CLEAR COUNT OF FORCES
         MVI   STRETMSG+3,0
         MVI   STRETNOM+3,0
         MVI   STBUIMSG+3,0
         MVI   STBUINOM+3,0
         MVI   STBUIREM+3,0
         LA    R4,STATAB
         LA    R5,7
         USING STASECT,R4
STATL    ZAP   STAFOR,P0
         ZAP   STACON,P0
         ZAP   STABUIL,P0
         MVI   VSTAM+4,C'-'  INITIAL TRIPLE SPACE
         MVC   STASFOR+17(10),=CL10';  ALLOWED'
         MVI   STASCON+4,C'.'
         L     R6,APRO#BEG   GET FIRST PROVINCE
         USING PROVINCE,R6   DECLARE IT
STATP    MVC   STAMCL,STAMCL-1   CLEAR THE RECORD
         LA    R1,STAMCOUN   FORMAT THE COUNTRY, UNIT AND FORCE
         BAL   R9,FORMCOUN   COUNTRY, FORCE AND PROVINCE
         CLC   STABIT,PROCC
         BNE   STATP1        NO FORCE OF THIS COUNTRY
         TM    PROF,PROFARM+PROFNAV  ANY FORCE ON IT ?
         BNZ   STATPUN1      YES, UP FORCES COUNT
         SPACE
STATP1   MVC   STAMUNIT,BLANKS
         MVC   STAMCOAS,BLANKS   CLEAR INVALID UNIT RELATED INFORMATION
         MVC   DB(1),ROUTE   GET PLAYING COUNTRIES
         NC    DB(1),STABIT  MASK WITH ACTIVE PLAYERS
         BZ    STATPIN       IGNORE NON-PLAYER
         TM    PROF,PROFSUP  SUPPORT CENTER ?
         BZ    STATPIN       NO, SKIP
         CLC   STABIT,PROSC  CAPTURED BY THIS COUNTRY ?
         BE    STATPUN       YES, ADD IT TO SUPP COUNT
STATP2   CLC   STABIT,PROFHOM   HOME COUNTY ?
         BNE   STATPIN       NO, IGNORE
         B     STATF         ELSE SHOW UNDER OCCUPATION
         SPACE
STATPUN1 AP    STAFOR,P1     UP NUMBER OF FORCES ON THE BOARD
         SPACE
STATPUN  TM    PROF,PROFSUP  SUPPLY CENTER ?
         BZ    STATPUT       NO, COMPLETED FORMATTING
         AP    STACON,P1     UP CONTROLLED COUNT
         CLC   STABIT,PROFHOM   HOME SUPPLY CENTER ?
         BE    STATF         YES
         MVC   STAMSTAT(L'STASU),STASU   SHOW SUPPLY CENTER
         B     STATPW
STATF    MVC   STAMSTAT(L'STAHO),STAHO
         CLC   PROSC,STABIT  HOME AND OCCUPIED ?
         BNE   STATPW        NO
         TM    PROF,PROFARM+PROFNAV
         BNZ   STATPW        NOT AVAILABLE
         CLI   DATE,C'S'     DOING SPRING MOVE ?
         BE    STATPW        YES, SKIP
         TM    NEWPLAY,PBUIL   BUILDS TO BE DONE ?
         BZ    STATPW        NO, SKIP
         AP    STABUIL,P1    SHOW AVAILABLE FOR BUILD
         MVC   STAMSTAT(L'STABUA),STABUA   SHOW AVAILABLE FOR BUILD
         B     STATPUT       NOW SHOW IT
         SPACE
STATPW   TM    PROF,PROFNAV+PROFARM   OCCUPIED ?
         BNZ   STATPW1
         CLC   PROFHOM,STABIT   HOME CITY ?
         BNE   STATPUT       NO, PUT OUT
         CLC   PROSC,STABIT  CONTROLLED ?
         BE    STATPUT       YES, OK
         B     STATPWB       SEE IF CONTROLLED, AND BY WHOM
STATPW1  CLC   STABIT,PROCC  OCC. BY SAME CO. ?
         BNE   STATPWA       YES
         CLC   PROSC,PROCC   BUT CONTROLLED ?
         BE    STATPUT       OK
STATPWB  CLI   PROSC,0       UNDER CONTROL ?
         BNE   STATPW3       SHOW WHO
         MVC   STAMSTAT+L'STASU+4(14),=C'NOT CONTROLLED'
         B     STATPUT       DISPLAY
STATPWA  CLC   PROSC,STABIT  ASSIGNED TO US ?
         BE    STATPW2       YES, SHOW WHO
         CLC   PROFHOM,STABIT  OR HOME COUNTRY ?
         BNE   STATPUT       NO
STATPW2  MVC   STAMSTAT+2+L'STASU(L'STAOCC),STAOCC
         LA    R1,PROCC      GET OCCUPANT
         B     STATPW4       AND FORMAT
STATPW3  MVC   STAMSTAT+2+L'STASU(L'STAOCC),STAOCN  CONTROLLED
         LA    R1,PROSC      GET OWNER
STATPW4  MVC   STAMSTAT+3+L'STASU+L'STAOCC(2),DCBYDFLT  'BY'
         BAL   R9,FORMACON   MAKE THIS COUNTRY
         MVC   STAMSTAT+6+L'STASU+L'STAOCC(7),1(R2)
         CLI   1(R2),C' '    ANY COUNTRY ?
         BNE   STATPUT       YES, PUT OUT
         MVC   STAMSTAT+2+L'STASU(L'STAOCC+11),BLANKS  ELSE CLEAR OUT
         SPACE
STATPUT  LA    R1,VSTAM
         CALL  XPRNT
         MVI   VSTAM+4,C' '  SET FOR SINGLE SPACING
         B     STATPIN       NEXT PROVINCE
         SPACE
STATPIN  LA    R6,PROLEN(,R6) NEXT PROVINCE
         C     R6,APRO#END   DONE ?
         BL    STATP         NO, TRY MORE
         TM    NEWPLAY,PRET  ANY RETREATS ?
         BZ    STAREND
         LA    R7,34         MAX TO LOOK AT
         L     R6,ARET#BEG   GET FIRST ENTRY
STARLOOP CLC   STABIT,PROCC  FORCE OF SAME COUNTRY ?
         BNE   STARLINC      NO
         OC    STRETMSG+3(1),STABIT   GIVE A RETREAT MESSAGE
         OC    STRETNOM+3(1),STABIT   DON'T GIVE A SIT OUT
         MVC   STAMCL,STAMCL-1     CLEAR THE RECORD
         LA    R14,STATAB    GET START OF STATUS TABLE
         LR    R1,R4         GET CURRENT ADDRESS
         SR    R1,R14        GET DISPLACEMENT
         LA    R1,SUPUNTAB(R1)   GET ADJECTIVE TABLE ADDRESS
         MVC   STAMCOUN(8),1(R1)
         LA    R1,STAMUNIT
         BAL   R9,FORMUNIT   FORMAT UNIT AND PROVINCE
         MVC   STAMSTAT(L'DCONMRT),DCONMRT  SHOW RETREATING UNIT
         LA    R1,VSTAM
         CALL  XPRNT         PRINT IT
         AP    STAFOR,P1     UP NO. OF FORCES
STARLINC LA    R6,PROLEN(,R6)
         BCT   R7,STARLOOP   TRU ALL OF THEM
STAREND  MVC   STASMSG,STASMSG-1
         MVC   STASCOU,STASNAT   SHOW NATION
         MVC   STASFOR,STAMASK
         MVC   STASCON,STAMASK
         ED    STASFOR,STAFOR
         ED    STASCON,STACON
         MVC   STASMSG,BLANKS
         TM    NEWPLAY,PBUIL   BUILD EXPECTED ?
         BZ    STATCINP      NO
         CP    STAFOR,STACON
         BE    STATCINP
         BH    STATCINR      REMOVE, SET MESSAGE FLAG
         CP    STABUIL,P0    ROOM TO BUILD ?
         BNH   STATCINP      NO, DONT SET FLAG FOR THIS ONE
         OC    STBUIMSG+3(1),STABIT   GIVE A BUILD MESSAGE
         MVC   STASMSG(L'STATBMB),STATBMB   SHOW BUILDS
         ZAP   DB,STACON           GET NO. CONTROLLED
         SP    DB,STAFOR     LESS CURRENT GIVES MAX ALLOWED
         CP    DB,STABUIL    IS ALLOWED NUMBER LOWER THAN AVAILABLE ?
         BNL   *+10          NO, LEAVE IT
         ZAP   STABUIL,DB    SET BUILDS TO MAX ALLOWED
         ZAP   DB,STABUIL    SHOW HOW MANY MAY BE BUILT
         B     STATCINF      SET BUILD/REMOVE BIT
STATCINR OC    STBUIREM+3(1),STABIT   SHOW REMOVE ORDER
         MVC   STASMSG(L'STATBMR),STATBMR   SHOW REMOVE REQUIRED
         ZAP   DB,STAFOR
         SP    DB,STACON     SHOW NUMBER TO BE REMOVED
STATCINF OI    NEWPLAY,PNBUIL   BUILD / REMOVE REQUIRED
         OC    STBUINOM+3(1),STABIT   GIVE NO SIT OUT
         MVC   STATBMRN(4),STAMASK   EDIT MASK
         ED    STATBMRN(4),DB+6  SHOW NUMBER OF UNITS
         MVC   STASMSG+L'STATBMR(L'STATBMRN),STATBMRN   MOVE NO.
STATCINP LA    R1,VSTAS
         TM    NEWPLAY,PBUIL   BUILDS ?
         BO    STATCINX      YES, GIVE FULL MESSAGE
         MVC   STASFOR+17(16),BLANKS  ELSE CLEAR POSS. BAD ALLOWED
STATCINX CALL  XPRNT         SHOW IT
STATCIN  ZAP   DB,STAFOR     GET LOWEST VALUE OF FORCES/SUPPLY
         CP    DB,STACON
         BNH   *+10
         ZAP   DB,STACON
         AP    COPTOT,DB     CUMULATIVE TOTAL VALID FORCES
         LA    R4,STASLEN(,R4)   NEXT COUNTRY
         BCT   R5,STATL
         TM    NEWPLAY,PBUIL+PNBUIL   BUILD NECESSARY ?
         BNM   STATCINI
         CLI   NEWDATE,C'S'  ALREADY ADVANCED ?
         BE    STATCINI      YES
         MVC   NEWDATE(L'SPRING),SPRING
         PACK  DB,NEWDATE+7(4)   GET YEAR
         AP    DB,P1         ADD ONE TO IT
         UNPK  NEWDATE+7(4),DB+5(3)  GET NEW YEAR
         OI    NEWDATE+10,C'0'  FORCE ZONE
         NI    NEWPLAY,255-PBUIL-PNBUIL  TURN BUILD OFF
         OI    NEWPLAY,PMOVE   NEXT PLAY IS A MOVE
STATCINI TM    NEWPLAY,PRET  DOING RETREATS ?
         BO    STATMO        YES, GIVE ORDERS
         SPACE 2
         LA    R4,STATAB     GET STATUS TABLE BACK
         LA    R5,7
         ZAP   MAXSUP,P0
         MVI   MAXSFG,0
         XR    R6,R6
STATWLOP ZAP   DB,STAFOR     GET LOWER OF FORCES/SUPPLY CNTRS
         CP    DB,STACON
         BNH   *+10
         ZAP   DB,STACON
         CP    DB,P0         NONE ALLOWED ?
         BNE   STATWNZ
         XI    ROUTE,X'FF'   FLIP
         OC    ROUTE,STABIT   TURN LOSING COUNTRY OFF
         XI    ROUTE,X'FF'
         NC    NEWROUTE,ROUTE  COPY TO ALTERNATE
         B     STATWINC
STATWNZ  CP    MAXSUP,DB     HIGHER ?
         BH    STATWINC      NOT GOOD ENOUGH
         BL    STATWSAV      NEW HIGH
         OI    MAXSFG,1      SHOW IF A TIE
         B     STATWINC
STATWSAV ZAP   MAXSUP,DB     STASH NEW HIGH
         MVI   MAXSFG,0      TURN TIE OFF
         LR    R6,R4         REMEMBER THIS ADDRESS
STATWINC LA    R4,STASLEN(,R4)   NEXT COUNTRY
         BCT   R5,STATWLOP
         TM    MAXSFG,1      TIE ?
         BO    STATMO        YES; WAIT FOR RESOLUTION
         ZAP   DB,MAXSUP
         AP    DB,MAXSUP     DOUBLE
         CP    DB,COPTOT     MAJORITY OF FORCES ?
         BH    BANNWIN       YES - THIS PLAYER WON
         SPACE 2
*        IF IT'S TIME FOR A MOVE, GIVE THE ORDER SHEETS
*
STATMO   MVI   VSTO+3,0      TELL ALL ABOUT IT
         MVC   VSTOY,NEWDATE   SET DATE FOR MOVES
         MVC   VSTOT,=CL8'ORDERS'
         LA    R1,VSTO       GET PAGE HEADER
         CALL  XPRNT
         TM    NEWPLAY,PMOVE   TIME FOR MOVES ?
         BZ    STATNMOV      NO, SKIP
         LA    R4,STATAB     GET STATUS TABLE
         LA    R5,7          DO ALL COUNTRIES
         USING STASECT,R4
STATML   CP    STAFOR,P1     DOES THIS COUNTRY HAVE ANY UNITS ?
         BL    STATMCC       TELL ALL ABOUT IT
         MVC   VSTOF+3(1),STABIT
         MVC   VNULL+3(1),STABIT
         MVC   VSTOM+3(1),STABIT
         LA    R1,VSTOM      GET EXPLANATION
         CALL  XPRNT
         L     R6,APRO#BEG   GET FIRST PROVINCE
         USING PROVINCE,R6
STATMF   CLC   STABIT,PROCC
         BNE   STATFI        NOT OCCUPIED BY PLAYER
         TM    PROF,PROFNAV+PROFARM   ANY FORCES THERE ?
         BZ    STATFI        NO, IGNORE
         LA    R1,VSTOFU     GET OUTPUT
         BAL   R9,FORMUNIT   FORMAT UNIT AND PROVINCE
         LA    R1,VSTOF
         CALL  XPRNT         FORMAT PRINTED
STATFI   LA    R6,PROLEN(,R6)   NEXT PROVINCE
         C     R6,APRO#END   ANY ?
         BL    STATMF        YES, DO IT
         LA    R1,VNULL      PAGE EJECT
         CALL  XPRNT
         B     STATMCI       GO TO  INCREMENT
STATMCC  MVC   STANOUN+3(1),STABIT  TELL PLAYER NONE
         LA    R1,STANOUN
         CALL  XPRNT
STATMCI  LA    R4,STASLEN(,R4)
         BCT   R5,STATML     DO NEXT COUNTRY, IF ANY
         MVI   VNULL+3,0     RESET ROUTING FOR NULL TITLE
STATNMOV EQU   *             END OF MOVES
         SPACE 2
         TM    NEWPLAY,PBUIL   TIME FOR BUILDS ?
         BZ    STBNMOV       NO
         XI    STBUINOM+3,X'FF'
         LA    R3,=A(STBUIMSG,STBUIREM,STBUINOM)  GET MESSAGES
         LA    R4,3          NO. OF MESSAGES
         B     STMSGCOM      GO TO COMMON MESSAGE PROCESSOR
         SPACE 2
STBNMOV  TM    NEWPLAY,PRET  RETREATS ?
         BZ    CONEJECT      NO
         XI    STRETNOM+3,X'FF'
         LA    R3,=A(STRETMSG,STRETNOM)  GET MESSAGE ADDRESSES
         LA    R4,2          NO. OF MESSAGES
         SPACE
STMSGCOM L     R1,0(,R3)     GET NEXT MESSAGE
         CLI   3(R1),0       NO BITS ON ?
         BE    STMSGCON      YES, SKIP IT
         CALL  XPRNT
STMSGCON LA    R3,4(,R3)     NEXT MESSAGE
         BCT   R4,STMSGCOM   DO IT
         B     CONEJECT      DO PAGE EJECT AND CONTINUE
         EJECT
BANNER   CALL  XPCLS         CLOSE ALL FILES
         LA    R4,STATAB     GET NATION TABLE
         LA    R5,7          DO FOR 7 NATIONS
         USING STASECT,R4
BANLOOP  MVI   BANFG,X'80'
         MVC   DB(1),ROUTE   GET ALLOWED ROUTING
         NC    DB(1),STABIT  IS IT ALLOWED ?
         BZ    BANLINC       NO - SKIP INACTIVE COUNTRIES
         MVC   BANDD(7),STASNAT   MOVE DD NAME
         CLI   BANONE,C' '   NATION NAME REQUEST ?
         BNE   BANLINK       NO, DO AS IS
         MVC   BANONE+1(7),STASNAT   COPY NAME
BANLINK  LA    R1,BANONE     PASS ADDRESS
         LINK  EP=ASMANY2    GO TO N.I.S. BIG LETTER ROUTINE
BANLINC  LA    R4,STASLEN(,R4)   GET NEXT COUNTRY
         BCT   R5,BANLOOP
         DROP  R4
         CALL  XPROP         RE-OPEN THE FILES
         BR    R9            RETURN TO CALLER
         SPACE 2
*        DECLARE THE WINNER
*
BANNWIN  MVC   BANONE(7),STASNAT-STASECT(R6)  (6) IS WINNER
         MVI   BANONE+7,C' '
         MVC   BANTWO(8),=CL8'WINS'
         BAL   R9,BANNER
         MVI   PLAY,0        DIALLOW ANY MORE ENTRIES FOR ANYTHING
         MVI   NEWPLAY,0
         OI    FLAG,STAT+SEND    FORCE STATUS TO BE RECORDED
         B     CHEKPOIN      FORCE DISK WRITE
         SPACE 2
         RANDOM
         SPACE 2
BUGGER   XI    FLAG,DEBUG    FLIP DEBUG SWITCH
         B     CONTROL       KEEP GOING
         EJECT
DISK     DCB   DDNAME=DISK,DSORG=PS,MACRF=(RC,WC),EODAD=INITIAL,       *
               RECFM=U,LRECL=7294,BLKSIZE=7294,SYNAD=SYNAD
SAVEAREA DS    18F
         SPACE 2
BANONE   DC    CL8' '        BANNER HEADLINE
BANFG    DC    X'80'         PAGE EJECT FLAG
BANTWO   DC    CL8' '        SUB-HEADER
BANDD    DC    CL8' ',AL2(689,137),X'54'  BLKSI,RECL,RECFM
VINIT    WTO   '$   $INITIALIZATION',MF=L
VCHKIG   WTO   '-***** CURRENT SET OF REQUESTS MUST BE RESUBMITTED ERRO*
               R FREE - CHECKPOINT NOT TAKEN *****',MF=L
NINEOF   WTO   '- *****  EOF ON DISK; $INIT ASSUMED  *****',MF=L
VIOERR   WTO   '-  *****  I/O ERROR ON DISK FILE  *****',MF=L
STANOUN  WTO   '0THIS COUNTRY HAS NO UNITS TO BE ORDERED',MF=L
STRETMSG WTO   '0PLEASE SUBMIT YOUR RETREAT ORDERS',MF=L
STRETNOM WTO   '0PLEASE WAIT FOR RETREATS TO BE PLAYED',MF=L
STBUIMSG WTO   '0PLEASE SUBMIT YOUR BUILD ORDERS',MF=L
STBUINOM WTO   '0PLEASE WAIT FOR BUILDS/REMOVES TO BE PLAYED',MF=L
STBUIREM WTO   '0PLEASE SUBMIT YOUR REMOVE ORDERS',MF=L
STATBMB  DC    C' YOU MAY BUILD'
STATBMR  DC    C'YOU MUST REMOVE'
STATBMRN DC    C' NNN UNITS.'
MSASSGN  WTO   '0 COUNTRY ASSIGNMENTS',MF=L
NEED3    WTO   '0**** A MINIMUM OF 3 PLAYERS IS REQUIRED ****',MF=L
TABASGN  DC    0H'0',AL1(32,0,0,0)   RUSSIA
         DC    AL1(2,0,0,0)     ENGLAND
         DC    AL1(4,0,0,0)    FRANCE
         DC    AL1(64,0,0,0)   TURKEY
         DC    AL1(1,0,0,0)  AUSTRIA
         DC    AL1(8,0,0,0)   GERMANY
         DC    AL1(16,0,0,0)   ITALY
         SPACE 2
         LTORG
         TITLE 'DIPLOMACY --- MOVE PROCESSING'
DIPLMOVE CSECT
         USING *,R12
         LR    R12,R15
         USING ORDSECT,R4
         USING PROVINCE,R6
         BAL   R9,DATECHEK
         MVI   DIDMOVE,0     RESET WHO MOVED
         XC    STANDOFF(34),STANDOFF
         LA    R1,STANDOFF
         ST    R1,STANDAD
         L     R4,AORD#BEG   START AT START AGAIN
         LA    R7,STATAB     GET STATUS TABLE
         USING STASECT,R7
         LA    R8,7          NO. OF COUNTRIES
MOFLP    L     R6,APRO#BEG   GET FIRST PROVINCE
MOFL     TM    PROF,PROFNAV+PROFARM   UNIT THERE ?
         BZ    MOFLI         NO, SKIP
         CLC   STABIT,PROCC  OCCUPIED BY THIS COUNTRY ?
         BNE   MOFLI         NO, TRY NEXT PROVINCE
         MVC   ORDCO,PROCC   COPY COUNTRY OCCUPYING
         MVC   ORDUN,PROF    COPY TYPE OF FORCE AND BITS
         NI    ORDUN,PROFARM+PROFNAV  LEAVE ONLY UNIT TYPE FLAGS
         MVC   ORDLOC,PRO#   COPY PROVINCE NUMBER
         TM    PROF,PROFNAV+PROFTWO+PROFSOU   COAST REQUIRED ?
         BNO   MOFLU         NO
         OI    ORDLOC,X'80'  YES - SET SOUTH COAST
MOFLU    LA    R4,ORDLEN(,R4)   SKIP TO NEXT ORDER
MOFLI    LA    R6,PROLEN(,R6)   NEXT PROVINCE
         C     R6,APRO#END
         BL    MOFL          DO NEXT PROVINCE
         LA    R7,STASLEN(,R7)
         BCT   R8,MOFLP      DO FOR NEXT COUNTRY
         DROP  R7
         L     R6,ARET#BEG
         LA    R7,35
MOFRL    XC    PROVINCE(PROLEN),PROVINCE   CLEAR RETEAT TABLE
         LA    R6,PROLEN(,R6)
         BCT   R7,MOFRL
         SPACE 2
*        PROCESS MAJOR CONTROL CARD - NATION OR MESSAGE
*
DOMIN    OI    READ,RCON     SET FOR CONTROL READ
         MVI   VCARD+3,0     RESTORE GENERAL ROUTING
         MVI   VCARD+4,C'0'   RESTORE DOUBLE SKIP
         BAL   R9,GETCARD    GET A CARD
         B     DOMAN         NO MORE, START PLAY
         BAL   R9,PARMA      ANALYZE
         B     DOMINB        BAD CARD, START PROCESSING NOW
         LA    R1,COPFX      GET STATAB PLUS PREFIX
         LA    R2,(STATEND-COPFX)/STASLEN   NO. OF ENTRIES
DOMINF   CLC   1(7,R1),PARM1+1   SAME ENTRY ?
         BE    DOMING        YES, CHECK
         LA    R1,STASLEN(,R1)
         BCT   R2,DOMINF     TRY AGAIN
         CLC   PARM1+1(7),VERBDEBG+5  DEBUG ?
         BNE   DOMINB        NO
         XI    FLAG,DEBUG    SET DEBUG FLAG
         MVI   VCARD+3,0     ROUTE ALL
         BAL   R9,ECHO       PRINT IT
         B     DOMIN         GET MORE INPUT
         SPACE 1
DOMINB   OI    READ,RFULL    REPEAT READ NEXT TIME
         B     DOMAN         ASSUME END OF INPUT FOR THIS MOVE
         SPACE
DOMING   MVC   VCARD+3(1),0(R1)   COPY ROUTING CODE
         CLI   0(R1),0       IS THIS A MESSAGE OR SOME SUCH ?
         BNE   DOMINO        NOT MESSAGE
         BAL   R9,MESSAGE    GO TO MESSAGE
         B     DOMIN
         SPACE
DOMINO   MVC   CUCO,0(R1)    SAVE CURRENT COUNTRY
         NC    CUCO,ROUTE    IS THIS COUNTRY PLAYING ?
         BZ    DOMIN         NO, IGNORE ITS INPUT
         OI    VCARD+3,128   ALSO ROUTE TO GAME KEEPER
         OC    DIDMOVE,CUCO  SHOW IF COUNTRY SUPPLIED INPUT
         BAL   R9,ECHO       SHOW IT
         SPACE 2
*        LOOP FOR MOVE REQUEST CARD
*
DOMIR    MVI   VCARD+4,C'0'  RESTORE SPACING
         BAL   R9,GETCARD    GET A REQUEST
         B     DOMIN         END OF CONTROL INPUT OR EOF
         MVC   VCARD+3(1),CUCO   ROUTE ONLY TO ISSUING COUNTRY
         OI    VCARD+3,128   ALSO ROUTE TO GAME KEEPER
         XC    NEWORD,NEWORD   CLEAR ORDER WORK AREA
         LA    R4,NEWORD     USE IT
         ZAP   ORDPCON,P0    CLEAR
         ZAP   ORDPSUP,P0
         ZAP   ORDCNT,P0     CLEAR COUNTER
         BAL   R9,PARMA      LOOK AT IT
         B     DOMORBID      BAD CARD, DON'T LOOK AT IT
         AP    CNTMOV,P1     UP ORDER COUNT
         MVC   PCARD-1(4),STAMASK    MOVE EDIT MASK
         ED    PCARD-1(4),CNTMOV   EDIT ORDER NUMBER
         BAL   R9,ECHO       SHOW IT
         MVC   PCARD,PCARD-1 CLEAR CARD IMAGE
         MVI   VCARD+4,C'+'  OVERPRINT FIRST MESSAGE
         LA    R3,PARM1      GET FIRST PARM
         MVC   ORDCO,CUCO    COPY COUNTRY
         MVC   ORDSCO,CUCO
         MVI   TESTSFLG,TSTCO+TST1   SET FLAG FOR COUNTRY, FIRST FIELD
         BAL   R9,TESTSSET   TEST FIRST FIELD
         B     DOMORBID      TOO BAD
         MVC   ORDUN,CUUN    COPY UNIT
         MVC   ORDLOC,CULOC
         ZAP   ORDCNT,CNTMOV
         BAL   R9,FINDORD    FIND TYPE OF ORDER
         MVI   ORDORD,ORDMO  ASSUME MOVE IF NOT FOUND
         TM    ORDORD,ORDCON   CONVOY REQUEST ?
         BZ    DOMORSTT            NO
         TM    ORDUN,PROFNAV  NAVY ?
         BZ    DOMORBID            ARMY CAN'T CONVOY
         IC    R6,ORDLOC           GET CONVOYING FROM ADDRESS
         BAL   R9,ADPROV           GET PROVINCE ADDRESS
         USING PROVINCE,R6
         TM    PROF,PROFSEA
         BZ    DOMORBID            BOO IF NOT OCEAN
         B     DOMORSC             KEEP CHECKING
DOMORSTT TM    ORDORD,ORDST        STAND ?
         BZ    DOMORM        NO,
         MVC   ORDSCO,ORDCO
         MVC   ORDSUN,ORDUN
         MVC   ORDSLOC,ORDLOC
         MVC   ORDSORD,ORDORD
DOMORST  MVC   ORDNLOC,ORDSLOC
         B     DOMORLA
         SPACE
DOMORM   TM    ORDORD,ORDMO  MOVE REQUEST ?
         BZ    DOMORSC       NO, MORE INPUT
         MVC   ORDSCO,ORDCO
         MVC   ORDSLOC,ORDLOC
         MVC   ORDSUN,ORDUN
         MVC   ORDSORD,ORDORD
         MVI   TESTSFLG,TSTCO+TST3  SAME CO., DEST FIELD
         B     DOMORTB
         SPACE
DOMORSC  BAL   R9,FINDNAT    SEE IF NATION SUPPLIED
         NOP   0             OK IF NATION SPECIFIED
         MVI   TESTSFLG,TST2   SECONDARY FROM FIELD
         BAL   R9,TESTSSET   CHECK IT
         B     DOMORBID      TOO BAD
         MVC   ORDSUN,CUUN
         MVC   ORDSLOC,CULOC
         BAL   R9,FINDORD    FIND SECONDARY ORDER
         MVI   ORDSORD,ORDMO ASSUME MOVE IF NOT FOUND
         TM    ORDSORD,ORDST   SECONDARY STAND ?
         BO    DOMORST       YES, COMPLETE NEW TOO FIELD
         SPACE
DOMORT   MVI   TESTSFLG,TST3  DESTINATION CHECK
DOMORTB  BAL   R9,TESTSSET
         B     DOMORBID      TOO BAD
         MVC   ORDNLOC,CULOC   SET DESTINATION
         SPACE
DOMORLA  CLC   ORDSLOC,ORDNLOC   NEW UNIT LOC SAME AS OLD ?
         BNE   DOMORLA1      NO
         BAL   R9,FORCESTS   MAKE SECONDARY STAND
DOMORLA1 CLC   ORDLOC,ORDNLOC   OLD LOC SAME AS NEW ?
         BNE   DOMORLA2      NO
         BAL   R9,FORCEST    VALID ONLY IF STANDING
DOMORLA2 TM    ORDORD,ORDST+ORDMO  MOVE OR STAND ?
         BNZ   DOMORLP       YES, SKIP CHECK
         TM    ORDORD,ORDSUP   SUPPORT ?
         BO    DOMORLS
         MVI   TESTSFLG,TST2
         TM    ORDSUN,PROFARM   CONVOYED UNIT MUST BE ARMY
         BZ    DOBDUP
         IC    R6,ORDSLOC    OLD LOC
         BAL   R9,ADPROV     GET IT
         TM    PROF,PROFSEA+PROFLAN   MAY BE NEITHER WATER NOR INLAND
         BNZ   DOBDUP        TOO BAD
         MVI   TESTSFLG,TST3
         IC    R6,ORDNLOC    NEW LOCATION
         BAL   R9,ADPROV
         TM    PROF,PROFSEA+PROFLAN
         BNZ   DOBDUP
         B     DOMORLP
DOMORLS  MVC   CKUN,ORDUN
         MVC   CKLOC,ORDLOC
         MVC   CKNLOC,ORDNLOC
         BAL   R9,CHECKSUP   VALID SUPPORT DESTINATION ?
         B     DOBDUP        NO
         SPACE
DOMORLP  CLI   0(R3),C' '    EXCEESSSSS PARMS ?
         BE    DOMADD        NO, OK.  NOW ADD IT TO PROPER TABLE LOACTI
         MVC   CAMSG(L'MSBDMANY),MSBDMANY   TOO MANY
         BAL   R9,SCREAM
         B     DOMIR         TRY NEXT CARD
         SPACE 2
DOBDUP   MVC   CAMSG+12(L'MSBDUNPR),MSBDUNPR  BAD UNIT/PROVINCE
         LA    R1,DCBDU1
         TM    TESTSFLG,TST1
         BO    DOBDUPM
         LA    R1,DCBDU2
         TM    TESTSFLG,TST2
         BO    DOBDUPM
         LA    R1,DCBDU3
DOBDUPM  MVC   CAMSG(11),0(R1)
DOBDUPW  BAL   R9,WARNER
         B     DOMIR
         SPACE 2
DOMADD   L     R4,AORD#BEG   FIND ORDER TABLE ENTRY
         LA    R5,34         MAX ORDERS
         USING ORDSECT,R4
DOMADL   CLC   ORDCO,NEWORD+ORDCO-ORDSECT   SAME COUNTRY ?
         BNE   DOMADI        NO, TRY NEXT ONE
         CLC   ORDLOC,NEWORD+ORDLOC-ORDSECT   SAME LOCATION ?
         BNE   DOMADI        NO, TRY AGAIN
         CLI   ORDORD,0      SPECIFIED PREVIOUSLY ?
         BNE   DOMADUP
         MVC   ORDSECT(ORDLEN),NEWORD   ADD IT TO THE TABLE
         B     DOMIR         AND DO NEXT INPUT
DOMADI   LA    R4,ORDLEN(,R4)   GET NEXT ENTRY
         BCT   R5,DOMADL     FALL THROUGH IF EXHAUSTED
         SPACE
DOMORBID MVC   CAMSG(L'MSBDORD),MSBDORD   BAD ORDER
         BAL   R9,SCREAM
         B     DOMIR
         SPACE
DOMADUP  MVC   CAMSG(L'MSBDODU),MSBDODU
         B     DOBDUPW
         SPACE 2
DOMAN    L     R12,=A(DIPLMOV2)   GO TO NEXT CONTROL SECTION
         BR    R12           AND START PROCESSING MOVES
         EJECT
TESTSSET XC    CUCO+1(2),CUCO+1
         ST    R9,PARMBACK
         MVI   PARMBACK+7,4
         BAL   R9,TESTUNIT
         BAL   R9,TESTPROV
         B     DOBDUP
         BAL   R9,FINDPROV
         B     DOBDUP
         LA    R2,DB+1
TESTSSF  XR    R6,R6
         LA    R15,127
         IC    R6,0(,R2)
         NR    R6,R15
         MH    R6,=AL2(PROLEN)
         SH    R6,=AL2(PROLEN)
         BM    DOBDUP
         A     R6,APRO#BEG
         TM    TESTSFLG,TST1+TST2  PRIMARY OR SECONDARY 'FROM' ?
         BNZ   TESTSS12      YES, ACCEPT IT
         MVC   CUUN,ORDSUN   COPY SECONDARY UNIT
         TM    CUUN,PROFARM  ARMY ?
         BO    TESTSSNA      YES, SEE IF OCEAN
         TM    CUUN,PROFNAV  FLEET ?
         BZ    DOBDUP        ERROR IN PROCESSING ?
         TM    PROF,PROFLAN  FLEET ON LAND ?
         BO    TESTSSFI      YES, MUST HAVE WRONG PROVINCE
         B     TESTSSMV      ACCEPT
TESTSSNA TM    PROF,PROFSEA  ARMY AFLOAT ?
         BO    TESTSSFI      YES, LET THEM DROWN
         B     TESTSSMV      ACCEPT
TESTSS12 CLC   ORDSCO,PROCC  SAME COUNTRY ?
         BE    TESTSS1T
         TM    TESTSFLG,TST1
         BO    TESTSSFI
         MVC   ORDSCO,PROCC  USE CORRECT COUNTRY IF SPEC OMITTED
TESTSS1T CLI   DB+2,0        ONE PROVINCE ONLY ?
         BE    TESTSSMV      YES, IGNORE UNIT SPECIFICATION
TESTSSFC TM    PROF,PROFARM+PROFNAV   OCCUPIED ?
         BZ    TESTSSFI      NO
TESTSSUN MVC   DB(1),PROF
         NI    DB,PROFARM+PROFNAV
         NC    DB(1),CUUN
         BZ    TESTSSFI      TRY NEXT ONE, IF ANY
TESTSSMV MVC   CULOC,0(R2)   COPY LOCATION
         TM    TESTSFLG,TST3
         BO    TESTSSNW
         MVC   CUUN,PROF     COPY OLD UNIT
         NI    CUUN,PROFARM+PROFNAV
         TM    PROF,PROFNAV+PROFTWO+PROFSOU   SOUTH COAST ?
         BNO   TESTSSNC      NO, SET NORTH COAST / NO COAST
         OI    CULOC,X'80'   SET SOUTH COAST
         B     TESTSSEX      AND EXIT
TESTSSNW TM    ORDSUN,PROFNAV  FLEET ?
         BZ    TESTSSNC      NO, SET NORTH COAST
         TM    PROF,PROFTWO  COAST TEST NECESSARY ?
         BZ    TESTSSNC      NO
         CLI   PARM0+4,C'-'   WAS COAST GIVEN ?
         BNE   TESTSSCL      NO, SEE IF WE CAN DETERMINE WHICH
         CLI   PARM0+5,C'E'   EASTCOAST ?
         BE    TESTSSNC      YES
         CLI   PARM0+5,C'N'  NORTH COAST ?
         BE    TESTSSNC      YES
         OI    CULOC,X'80'   SET SOUTH COAST
         B     TESTSSEX
TESTSSCL MVC   CKUN,ORDSUN
         MVC   CKLOC,ORDSLOC
         MVC   CKNLOC,CULOC
         NI    CKNLOC,X'7F'
         MVI   HEXTEXT,0
         BAL   R9,CHECKMOV
         B     TESTSSC2
         OI    HEXTEXT,1
         MVC   CULOC,CKNLOC
TESTSSC2 OI    CKNLOC,X'80'  FLIP THE COAST BIT
         BAL   R9,CHECKMOV   TRY OTHER WAY
         B     TESTSSC3
         OI    HEXTEXT,2
         MVC   CULOC,CKNLOC
TESTSSC3 TM    HEXTEXT,3     IS THE COAST CLEAR ?
         BNM   DOBDUP        NO, EITHER OR NONE VALID
         B     TESTSSEX
TESTSSNC NI    CULOC,X'7F'   CLOBBER COAST
TESTSSEX TM    TESTSFLG,TST3   NEED TO CHECK ?
         BZ    TESTSSRT      NO, EXIT
         TM    ORDORD,ORDCON   CONVOY ?
         BO    TESTSSRT            YES, SKIP NOW AND TEST LATER
         MVC   CKUN,ORDSUN
         MVC   CKLOC,ORDSLOC
         LA    R1,2
TESTSSE2 MVC   CKNLOC,CULOC
         BAL   R9,CHECKMOV
         B     *+8
         B     TESTSTEX
         TM    CKUN,PROFARM
         BO    TESTSSEA
         XI    CULOC,X'80'
         BCT   R1,TESTSSE2
         B     DOBDUP
TESTSSEA TM    PROF,PROFLAN
         BO    DOBDUP
         OI    ORDFP,ORDCON  VALID ONLY IF CONVOYED
TESTSTEX CLC   ORDSLOC,ORDLOC
         BE    TESTSSRT
         MVC   CKUN,ORDUN
         MVC   CKLOC,ORDLOC
         MVC   CKNLOC,CULOC
         LA    R9,CHECKMOV   SET TO CHECK EXACT MOVE
         TM    ORDORD,ORDSUP   SUPPORT ORDER ?
         BZ    TESTSTX2      NO
         LA    R9,CHECKSUP   SET TO CHECK PHYSICAL MOVE FEASIBILITY
TESTSTX2 BALR  R9,R9         GO TO CHECK MOVE / SUPPORT REQUEST
         B     DOBDUP
TESTSSRT LM    R14,R15,PARMBACK
         B     0(R14,R15)
         SPACE
TESTSSFI LA    R2,1(,R2)
         CLI   0(R2),0       ANY ?
         BNE   TESTSSF
         B     DOBDUP
         SPACE 2
         LTORG
         TITLE 'DIPLOMACY --- MOVE PROCESSING - SECOND LOAD'
DIPLMOV2 CSECT
         USING *,R12
         SPACE 2
         BAL   R9,ORDURE     DUMP
         MVI   VCARD+3,0     RESTORE GENERAL ROUTING
         OI    FLAG,STAT+SAVE   SET SAVE AND STATUS PRINTOUT FLAGS
         TM    READ,RFULL    CARD IN BUFFER ?
         BO    DOMAN1        YES, LEAVE
         OI    READ,RFULL    SET FOR CARD IN BUFFER
         MVC   CARD(L'DCENDORD),DCENDORD
DOMAN1   CLI   DIDMOVE,0     ANY MOVES RECEIVED ?
         BE    DOMANB        NO - BAD SET
         CLC   ORDCOUNT,ZEROES   ANY PIECES LEFT ?
         BNE   DOMAN2        YES, OK
DOMANB   LA    R1,VORDZERO   NASTY MESSAGE
         CALL  XPRNT
         RETCH 12            SET NASTY ERROR CODE
         B     CONTT         AND CONTINUE
         SPACE
DOMAN2   CLI   RETCODE+1,4   ANY MAJOR ERRORS ?
         BH    CONTT         YES - GO NO FURTHER
         LA    R1,VORDACC    GET ACCEPTED ORDER HEADER
         CALL  XPRNT
         MVI   MAXSFG,0      CLEAR COUNTRY
         L     R4,AORD#BEG
         LA    R5,34
         USING ORDSECT,R4
DOMAP    CLI   ORDCO,0
         BE    DOMAPI        SKIP ENTRIES THAT ARE EMPTY
         BAL   R9,FORMAORD
         CLI   ORDORD,0      ANY ORDER ?
         BNE   DOMAPP        YES, PRINT IT
         MVC   AORDACT,TABORD+1   SET FOR STANDS
         MVC   AORDSCO-2(10),DCBYDFLT  DEFAULT
         BAL   R9,FORCEST
DOMAPP   LA    R1,VAORD      GET ORDER
         CLC   MAXSFG,ORDCO  SAME COUNTRY ?
         BE    DOMAPPR       YES, OK
         MVC   MAXSFG,ORDCO  COPY COUNTRY
         MVI   VAORD+4,C'0'  SET FOR DOUBLE SKIP
DOMAPPR  CALL  XPRNT
         MVI   VAORD+4,C' '  RESET SKIP
         BAL   R9,FORABS     MAKE ABSOLUTE LOCATIONS
DOMAPI   LA    R4,ORDLEN(,R4)
         BCT   R5,DOMAP
         SPACE 2
         LA    R1,DCONRES
         CALL  XPRNT
         MVC   AORDER,AORDER-1
         LA    R1,VAORD
         CALL  XPRNT
*
*        CHECK SUPPORT / CONVOY MATCHES
*
         L     R4,AORD#BEG
         LH    R5,ORDCOUNT
DOMSCHEK TM    ORDORD,ORDSUP+ORDCON  SUPPORT OR CONVOY ?
         BZ    DOMSRCON      NO
         L     R6,AORD#BEG
         LH    R7,ORDCOUNT
DOMSLP   CR    R4,R6         SKIP THIS ONE
         BE    DOMSIN
         TM    ORDORD,ORDSUP   SUPPORT ORDER ?
         BO    DOMSCSUP      YES
         USING NORSECT,R6
         CLC   ORDSLOC,NORSLOC   IS FROM LOCATION SAME FOR CONVOY ?
         BNE   DOMSIN        NO, GET NEXT ONE
         CLC   ORDNLOC,NORNLOC   IS TO LOCATION SAME ?
         BNE   DOMSIN        NO, GET NEXT
         CLC   ORDCO,NORCO   SAME COUNTRY ?
         BNE   DOMSAT        NO, ACCEPT
         OI    NORFP,ORDCON  SHOW CONVOY REQUIRED
         B     DOMSAT        ACCEPT CONVOY
DOMSCSUP TM    NORORD,ORDMO  OTHER ORDER A MOVE ?
         BO    DOMSCNEW      YES - FIND MATCH ON NEW PROVINCE
         CLC   ORDALN,NORALO   SUPPORT FOR EXISTING UNIT IN-PLACE ?
         BNE   DOMSIN        TOO BAD - TRY NEXT ONE
         B     DOMSAT        OK
DOMSCNEW CLC   ORDALS,NORALS   SUPPORT MOVE; IS NEW = ?
         BNE   DOMSIN        NO
         CLC   ORDALN,NORALN   TO = ?
         BE    DOMSAT        YES
DOMSIN   LA    R6,ORDLEN(,R6)
         BCT   R7,DOMSLP
DOMSDA   BAL   R9,FORMAORD
         MVC   AORDMSG+9(L'DCONTF),DCONTF
         LA    R1,ORDORD
         BAL   R9,FORMANOR
         MVC   AORDMSG(8),1(R2)
         LA    R1,VAORD
         CALL  XPRNT
         BAL   R9,FORCEST
         OI    ORDFP,ORDST   SKIP PRINTING OF SUCCESSFUL STAND
         B     DOMSCINC
DOMSAT   L     R6,AORD#BEG
         LH    R7,ORDCOUNT
DOMSAL   CR    R4,R6
         BE    DOMSAN
         TM    NORORD,ORDMO  MOVE ?
         BZ    DOMSAN
         CLC   ORDALO,NORALN  OLD = NEW ?
         BNE   DOMSAN        NOT ATTACK
         TM    ORDORD,ORDCON
         BO    DOMSAC        YES, SHOW CONVOY REQUIRED ATTACK RESOLUTN
         CLC   ORDALN,NORALO  ATTACK FROM SUPPORT DESTINATION ?
         BNE   DOMSAM        YES - SUPPORT CANCELLED
         OI    ORDFP,ORDSUP  SHOW ATTACKED SUPPORT
         B     DOMSAN        OK
DOMSAC   OI    ORDORD,ORDCONA   SHOW ATTACKED CONVOY
         B     DOMSCINC
DOMSAM   LR    R7,R6         SAVE OVER CALL
         BAL   R9,FORMAORD
         LR    R6,R7         RESTORE
         MVC   AORDMSG(L'DCONAT),DCONAT
         LA    R1,NORSUN     SHOW ATTACKING UNIT
         LA    R2,AORDMSG+L'DCONAT
         BAL   R9,FORMALOC
         LA    R1,VAORD
         CALL  XPRNT
         BAL   R9,FORCEST
         OI    ORDFP,ORDST   SKIP PRINTING OF SUCCESSFUL STAND
         B     DOMSCINC
DOMSAN   LA    R6,ORDLEN(,R6)
         BCT   R7,DOMSAL
         B     DOMSCINC
DOMSRCON TM    ORDFP,ORDCON  DOES ORDER REQUIRE A CONVOY ?
         BZ    DOMSCINC      NO
         L     R6,AORD#BEG
         LH    R7,ORDCOUNT
DOMSRLP  CR    R4,R6
         BE    DOMSRIN
         TM    NORORD,ORDCON   CONVOY REQUEST ?
         BZ    DOMSRIN
         CLC   ORDNLOC,NORNLOC   SAME TO LOC ?
         BNE   DOMSRIN
         CLC   ORDUN(3),ORDSUN-ORDSECT(R6)  SUPPORTING CONVOY ORDRR ?
         BE    DOMSCINC      YES, OK
DOMSRIN  LA    R6,ORDLEN(,R6)
         BCT   R7,DOMSRLP
         BAL   R9,FORMAORD
         NI    ORDFP,255-ORDCON
         BAL   R9,FORCEST
         OI    ORDFP,ORDST   SKIP PRINTING OF SUCCESSFUL STAND
         MVC   AORDMSG(L'DCONOC),DCONOC
         LA    R1,VAORD
         CALL  XPRNT
DOMSCINC LA    R4,ORDLEN(,R4)
         BCT   R5,DOMSCHEK
         SPACE 2
         BAL   R9,ORDURE
         BAL   R9,CONTORD
MOVELOOP MVC   OLDCOUNT,ORDCOUNT   SAVE CURRENT ORDER COUNT
         MVI   CONFG,X'FF'   INITIALIZE CONTROL FLAG
         L     R12,=A(DIPLMOV3)  GET NEXT CONTROL SECTION
         BR    R12           AND DO IT
         EJECT
         USING ORDSECT,R4
         USING NORSECT,R6
MOVEXIT  BAL   R9,ORDURE     DO ORDER TABLE AND GET REMAINING COUNT
         CLC   ORDCOUNT,ZEROES
         BE    MOVRET        YES, CHECK FOR RETREATS
         MVC   AORDER,AORDER-1
         LA    R1,VAORD
         CALL  XPRNT
         L     R4,AORD#BEG
         LA    R5,34
         SPACE
MOVEN1   CLI   ORDCO,0       OCCUPIED ?
         BE    MOVEN2        NO, SKIP
         BAL   R9,FORMAORD   FORMAT THE ORDER
         MVC   AORDMSG(L'DCONFAIL),DCONFAIL   SHOW IT FAILED
         MVC   AORDMSG+2+L'DCONFAIL(24),=C'- POSSIBLE PROGRAM ERROR'
         LA    R1,VAORD
         CALL  XPRNT         PRINT IT
MOVEN2   LA    R4,ORDLEN(,R4) POINT TO NEXT ONE
         BCT   R5,MOVEN1
         SPACE
MOVRET   L     R4,ARET#BEG   GET RETREAT TABLE
         MVI   VAORD+4,C'0'  SET FOR INITIAL DOUBLE SKIP
MOVRET1  CLI   0(R4),0       ANY OR LAST ONE ?
         BE    MOVRETN       YES
         LA    R2,PROLAND-PROVINCE(,R4)
         LA    R3,24
MOVRET2  CLI   0(R2),0       EMPTY SLOT
         BE    MOVRET5       YES, IGNORE
         USING PROVINCE,R6
         IC    R6,0(,R2)
         BAL   R9,ADPROV
         TM    PROF,PROFARM+PROFNAV    OCCUPIED ?
         BZ    MOVRET3       NO, OK TO LEAVE IN
         MVI   0(R2),0       ELSE ZERO CHOICE OUT
         B     MOVRET5
MOVRET3  LA    R14,STANDOFF  SEE IF IN STANDOFF TABLE
         MVC   CULOC,0(R2)   COPY
         NI    CULOC,X'7F'   DELETE COAST
MOVRET4L MVC   CUUN,0(R14)   COPY
         NI    CUUN,X'7F'    DELETE COAST
         CLC   CULOC,CUUN    WAS LOCATION A STAND-OFF ?
         BNE   MOVRET4E      NO, TRY AGAIN
         MVI   0(R2),0       ELSE REMOVE AS RETREAT LOCATION
         B     MOVRET5
MOVRET4E LA    R14,1(,R14)   TRY NEXT LOCATION
         C     R14,STANDAD   DONE ?
         BL    MOVRET4L      NO, TRY AGAIN
MOVRET5  LA    R2,1(,R2)     TRY NEXT RETREAT OPTION
         BCT   R3,MOVRET2
         SPACE
         MVC   AORDER,AORDER-1
         LA    R1,PROCC-PROVINCE(,R4)  MAKE BASIC ORDER
         LA    R2,SUPUNTAB
         BAL   R9,FORMACON+4
         MVC   AORDCOUN,1(R2)   COUNTRY
         LA    R1,CKUN
         LA    R2,AORDPUN    UNIT / PROVINCE
         MVC   CKUN,PROF-PROVINCE(R4)
         NI    CKUN,PROFARM+PROFNAV
         MVC   CKLOC,PRO#-PROVINCE(R4)
         BAL   R9,FORMALOC
         OC    PROLAND-PROVINCE(24,R4),PROLAND-PROVINCE(R4)  ANY LEFT ?
         BNZ   MOVRETW       YES, SHOW THEM
         MVC   AORDMSG(L'DCONMAN),DCONMAN   SHOW ANNIHILATED
         XC    0(PROLEN,R4),0(R4)   UNIT ANNIHILATED
         B     MOVRETP
MOVRETW  MVC   AORDACT(L'DCONMRTO),DCONMRTO  MOVE TO MESSAGE
         OI    NEWPLAY,PRET  RETREATS ONLY
         LA    R1,AORDACT+2+L'DCONMRTO  FIRST LOCATION
         LA    R2,PROLAND-PROVINCE(,R4)
         LA    R3,24
MOVREW1  CLI   0(R2),0       ZERO ?
         BE    MOVREW2       SKIP EMPTY ENTRY
         IC    R6,0(,R2)
         BAL   R9,ADPROV
         MVC   0(4,R1),PRONAME
         LA    R1,6(,R1)
MOVREW2  LA    R2,1(,R2)
         BCT   R3,MOVREW1
MOVRETP  LA    R1,VAORD      PRINT CHOICES OR ANNIHILATED MESSAGE
         CALL  XPRNT
         MVI   VAORD+4,C' '  SINGLE SPACE
         LA    R4,PROLEN(,R4)
         B     MOVRET1       TRY MORE
         SPACE 2
MOVRETN  TM    NEWPLAY,PRET   ANY RETREATS TO DO ?
         BZ    MOVRETS       NO, CHECK SEASON
         L     R12,=A(DIPLRETR)   GET AUTOMATIC RETREAT CODE
         B     RETAUTO-DIPLRETR(,R12) AND SEE IF PROGRAM CAN HANDLE THE
         SPACE
MOVRETS  CLI   DATE,C'S'     SPRING ?
         BE    MOVREF        YES - SET FOR FALL MOVE
         OI    NEWPLAY,PBUIL   ELSE SET FOR BUILD CHECK
         B     CONTT
MOVREF   MVC   NEWDATE(L'FALL),FALL   SHOW FALL OF SAME YEAR
         OI    NEWPLAY,PMOVE SET FOR MOVE
         B     CONTT
         SPACE 1
         LTORG
         TITLE 'DIPLOMACY --- MOVE PROCESSING - THIRD LOAD'
DIPLMOV3 CSECT
         USING *,R12
MOV3LOOP MVC   OLDCOUNT,ORDCOUNT   SAVE START COUNT FOR THIS ITERATION
         NI    CONFG,ORDST+ORDMO  LEAVE ONLY STAND-OFF AND DAISY-CHAIN
         MVI   RETLOC,0            CLEAR RETREAT LOCATION
         SPACE
         L     R4,AORD#BEG
         LH    R5,ORDCOUNT
DOMUV    TM    ORDORD,ORDMO+ORDST
         BZ    DOMUVI
         TM    ORDFP,ORDCON+ORDCONA   UNPROCEESSSED CONVOY ORDER ?
         BM    DOMUVI        YES - DEFER TILL LATER
         BAL   R9,SUPINIT
         TM    ORDORD,ORDST  STAND ?
         BO    DOMUVL        YES - DON'T CHECK OCCUPATION - THIS IS IT
         IC    R6,ORDNLOC
         BAL   R9,ADPROV
         USING PROVINCE,R6
         TM    PROF,PROFARM+PROFNAV   OCCUPIED ?
         BNZ   DOMUVI        YES, DEFER
         USING NORSECT,R6
DOMUVL   L     R6,AORD#BEG
         LH    R7,ORDCOUNT
DOMUL    CR    R4,R6
         BE    DOMULI
         CLC   ORDALN,NORALN   SAME NEW LOCATIOB ?
         BNE   DOMULI        NO, IGNORE
         TM    NORORD,ORDSUP  SUPPORTING ORDER ?
         BZ    DOMUVI        NO; RESOLVE LATER
         CLC   ORDALS,NORALS   SUPPORT FOR THIS UNIT ?
         BNE   DOMUVI        NO; RESOLVE LATER ON
         BAL   R9,SUPADD
DOMULI   LA    R6,ORDLEN(,R6)
         BCT   R7,DOMUL
         SPACE
*        UNCONTESTED MOVE OR STAND / WITH OPTIONAL SUPPORT
*        PROCESSED HERE
         SPACE
         LA    R7,DOMSAVE    GET LIST OF CONNECTED ORDERS FOR THIS MOVE
DOMULTIP L     R4,0(,R7)     GET ORDER ADDRESS / THEN SUPPORT ADDRESSES
         BAL   R9,FORMAORD
         MVC   AORDMSG(L'DCONSUC),DCONSUC
         TM    ORDORD,ORDMO+ORDST  MOVE/STAND ?
         BNZ   *+10
         MVC   AORDMSG(L'DCONDEL),DCONDEL  NO; PRINT 'DELIVERED'
         BAL   R9,MOVPRINT
DOMULTIS BAL   R9,ACCORD     GET RID OF IT
         LA    R7,4(,R7)     POINT TO NEXT ORDER ADDRESS
         C     R7,DOMSAVP    DONE ALL ?
         BL    DOMULTIP      NO, DO NEXT ONE
         L     R4,DOMSAVE    GET FIRST ONE BACK
         BAL   R9,ACCZERO    OK TO CLEAR OUT ORDER ENTRY
         B     MOVEREPT      CONDENSE TABLE - SOME PRIOR ONES MAY NOW B
         SPACE
DOMUVI   ZAP   ORDPSUP,P1    CLEAR SUPPORT COUNT
         ZAP   ORDPCON,P0    CLESR CONFLICT COUNT
         MVC   ORDATC,ORDCO   COPY COUNTRY
         TM    ORDORD,ORDCON+ORDCONA   ATTACKED AND/OR CONVOY ?
         BZ    DOMUVSA       NO
         BM    DOMUVS        CONVOY
         OI    CONFG,ORDCONA   SHOW ATTACKED CONVOY PROCESSING
DOMUVS   OI    CONFG,ORDCON   SHOW CONVOY PROCESSING NEEDED
DOMUVSA  TM    ORDFP,ORDSUP   ATTACKED SUPPORT ?
         BZ    *+8
         OI    CONFG,ORDSUP  YES, SHOW IT
DOMUVIE  LA    R4,ORDLEN(,R4)
         BCT   R5,DOMUV
         SPACE 2
         ZAP   CONCNT,P0     CLEAR HIGHEST CONFLICT COUNT
         L     R4,AORD#BEG   START
         LH    R5,ORDCOUNT   GET NUMBER TO CHECK
COMLOOP  L     R6,AORD#BEG
         LH    R7,ORDCOUNT
COMCON   CR    R4,R6
         BE    COMCONIN
         USING NORSECT,R6
         TM    ORDORD,ORDMO  MOVE ?
         BZ    COMCSTT       NO, DO OTHER TESTS
         CLC   ORDALN,NORALO  ATTACKING STANDING UNIT ?
         BNE   COMCSTA       NO
         TM    NORORD,ORDSUP+ORDCON+ORDST  REALLY STANDING ?
         BNZ   COMCONF       YES - UP CONFLICT COUNT
COMCSTT  TM    ORDORD,ORDSUP+ORDCON  SUPPORT / CONVOY ?
         BNZ   COMCSCO       YES - SPECIAL TEST
COMCSTA  CLC   ORDALN,NORALN   SAME NEW LOCATION ?
         BNE   COMCONIN      NO, OGNIRE
         TM    NORORD,ORDMO  MOVE ?
         BO    COMCONF       YES, CONFLICT
         TM    NORORD,ORDSUP   SUPPORT ORDER ?
         BZ    COMCONIN      NO
         CLC   ORDSCO(5),NORSCO   SUPPORT FOR THIS UNIT ?
         BE    COMOURS       YES, UP SUPPORT
         B     COMCONIN      ELSE IGNORE
COMCSCO  CLC   ORDALO,NORALN   POSSIBLE SUPPORT FOR THIS ONE ?
         BNE   COMCONIN      NO, IGNORE
         TM    NORORD,ORDMO  MOVE ORDER AGAINST THIS ONE
         BO    COMCONF       YES
         CLC   NORALN,NORALS   POSSIBLE SUPPORT ?
         BNE   COMCONIN      NO
         TM    NORORD,ORDSUP   REALLY SUPPORT ?
         BZ    COMCONIN      NO
         TM    NORSORD,ORDST   SUPPORT FOR THIS STAND ?
         BZ    COMCONIN
COMOURS  AP    ORDPSUP,P1    UP SUPPORT
         OC    ORDATC,NORCO  OR IN COUNTRY SUPPORTING THIS UNIT
         B     COMCONIN
COMCONF  AP    ORDPCON,P1
COMCONIN LA    R6,ORDLEN(,R6)
         BCT   R7,COMCON
         LA    R4,ORDLEN(,R4)
         BCT   R5,COMLOOP
         EJECT
*        CHECK AND PROCESS ATTACKED CONVOYS HERE
*
         TM    CONFG,ORDCONA   ANY ATTACKED CONVOY MOVES ?
         BZ    NOCONAT       NO, SKIP IT
         L     R4,AORD#BEG
         LH    R5,ORDCOUNT
         LTR   R5,R5         TO BE ON THE SAFE SIDE
         BNP   MOVEND
DOCLOOP  TM    ORDORD,ORDCON+ORDCONA   ATTACKED CONVOY ?
         BNO   DOCLINC       NO, IGNORE
         BAL   R9,SUPINIT
         USING NORSECT,R6
         L     R6,AORD#BEG
         LH    R7,ORDCOUNT
DOCLATT  CR    R4,R6
         BE    DOCLATIN
         TM    NORORD,ORDMO    MOVE ORDER ?
         BZ    DOCLATIN      NO
         CLC   ORDALO,NORALN   ATTACKING THIS LOCATION ?
         BNE   DOCLATIN
         BAL   R9,SUPADD
DOCLATIN LA    R6,ORDLEN(,R6)
         BCT   R7,DOCLATT
         SPACE
         LA    R8,DOMSAVE
DOCLSUP  L     R4,0(,R8)
         TM    ORDORD,ORDSUP   SUPPORT ORDER ?
         BO    DOCLSEN       YES- REACHED END OF MOVES
         L     R6,AORD#BEG
         LH    R7,ORDCOUNT
DOCLSUL  CR    R4,R6
         BE    DOCLSUI
         TM    NORORD,ORDSUP   SUPPORT ORDER ?
         BZ    DOCLSUI       NO
         TM    ORDORD,ORDCON   GOING AGAINST CONVOY ?
         BO    DOCLSUC       YES - NEED SPECIAL COMPARE
         CLC   ORDALS,NORALS SAME FROM LOCATIOB ?
         BNE   DOCLSUI       NO
         CLC   ORDALN,NORALN   SAME DESTINATION ?
         BNE   DOCLSUI       NO
         B     DOCLSAD       YES - ADD COUNT
DOCLSUC  CLC   ORDALO,NORALS   SUPPORTING THIS UNIT ?
         BNE   DOCLSUI       NO
DOCLSAD  BAL   R9,SUPADD     ADD SUPPORTING UNIT
DOCLSUI  LA    R6,ORDLEN(,R6)
         BCT   R7,DOCLSUL    DO NEXT ONE
         LA    R8,4(,R8)
         C     R8,DOMSAVP    LAST ONE ?
         BL    DOCLSUP       NO, KEEP COUNTING
DOCLSEN  MVI   MAXSFG,0      CLEAR FLAG
         ZAP   MAXSUP,P0     ZERO MAXIMUM SUPPORT COUNTER
         LA    R8,DOMSAVE
         XR    R6,R6
         XR    R7,R7
DOCLMAX  L     R4,0(,R8)     GET NEXT ONE
         TM    ORDORD,ORDSUP   SUPPORT ?
         BO    DOCLMEN       YES - REACHED END OF LIST
         CP    MAXSUP,ORDPSUP   BETTER SUPPORT THAN PREVIOUS ?
         BH    DOCLMAI       NO, IGNORE IT
         BE    DOCLMAT       POSSIBLE STANDOFF
         ZAP   MAXSUP,ORDPSUP  SAVE NEW MAX COUNT
         LR    R6,R4         SAVE ADDRESS OF MAX
DOCLMAT  LR    R7,R4         SAVE ADDRESS OF SECOND ONE FOR STANDOFF TE
DOCLMAI  LA    R8,4(,R8)     NEXT ONE
         C     R8,DOMSAVP    DONE ?
         BL    DOCLMAX       NO, TRY AGAIN
         SPACE
DOCLMEN  CR    R6,R7         STANDOFF ?
         BE    DOCLNOST      NO
         OI    MAXSFG,1      SET STANDOFF FLAG
DOCLNOST C     R6,DOMSAVE    CONVOY ATTACK REPELLED ?
         BNE   DOCLNOC       NO, CANCEL CONVOY
         NI    NORORD,255-ORDCONA   DELETE UNDER ATTACK FLAG
         LA    R7,DOMSAVE+4   AND DO NOT TOUCH ORDER FOR IT
         B     DOCLELIM      NOW GO TO ELIMINATE GARBAGE
DOCLNOC  LA    R7,DOMSAVE    ELIMINATE CONVOY ALSO
         SPACE
DOCLELIM L     R4,0(,R7)     GET NEXT ENTRY
         BAL   R9,FORMAORD
         TM    ORDORD,ORDCON    CONVOY ORDER ?
         BZ    DOCLELM       NO, TRY OTHER
         MVC   AORDMSG(L'DCONFAIL),DCONFAIL   SHOW CONVOY FAILED
         B     DOCLACP       PRINT / ACCORD
DOCLELM  TM    ORDORD,ORDSUP    SUPPORT ?
         BZ    DOCLELW       NO, MUST BE MOVE
         MVC   AORDMSG(L'DCONDEL),DCONDEL   SHOW SUPPORT DELIVERED
         B     DOCLACP       PRINT / ACCORD
DOCLELW  CP    ORDPSUP,MAXSUP   WAS THIS A LOSER ?
         BL    DOCLELF       YES PRINT AND STAND
         TM    MAXSFG,1      STAND-OFF ?
         BO    DOCLELS       YES - PRINT AND STAND
         MVC   AORDMSG(L'DCONSUCS),DCONSUCS   SUCCESSFUL
         IC    R6,ORDALO
         BAL   R9,ADPROV
         USING PROVINCE,R6
         MVI   ORDLOC,0      REMOVE FROM OLD LOCATION
         NI    PROF,255-PROFARM   CLEAR ARMY OUT
         MVI   PROCC,0
DOCLACP  BAL   R9,MOVPRINT   PRINT IT
         BAL   R9,ACCORD     DELETE THE ENTRY
         B     DOCLELIN      GO TO NEXT ONE
DOCLELS  MVC   AORDMSG(L'DCONSTAN),DCONSTAN   SHOW STAND-OFF
         B     DOCLACS       PRINT AND STAND
DOCLELF  MVC   AORDMSG(L'DCONFAIL),DCONFAIL   SHOW MOVE FAILED
DOCLACS  BAL   R9,MOVPRINT   PRINT IT
         BAL   R9,FORCEST    FORCE A STAND
         OI    ORDFP,ORDST   SUPPRESS SUCCESSFUL MOVE PRINTING
DOCLELIN LA    R7,4(,R7)     GET NEXT ORDER
         C     R7,DOMSAVP    END ?
         BL    DOCLELIM      NO, DO NEXT ONE
         B     MOVEREPT      AND CLEAN UP THE TABLES
         SPACE
DOCLINC  LA    R4,ORDLEN(,R4)
         BCT   R5,DOCLOOP
NOCONAT  EQU   *
         EJECT
*        VALIDITY CHECK CONVOY REQUESTS
*
         TM    CONFG,ORDCON  ANY CONVOYS TO PROCESS ?
         BZ    NOCONV        NO
         L     R4,AORD#BEG
         LH    R5,ORDCOUNT
         LTR   R5,R5         DONE ?
         BNP   MOVEND        YES
DOCVOOP  TM    ORDFP,ORDCON+ORDCONA  UNPROCESSED CONVOY ORDER ?
         BNM   DOCVINC       NO, IGNORE
         TM    ORDORD,ORDMO  IS THIS THE UNIT BEING CONVOYED ?
         BZ    DOCVINC       NO; FIND ONE
         BAL   R9,SUPINIT
         USING NORSECT,R6
         L     R6,AORD#BEG
         LH    R7,ORDCOUNT
DOCVATT  CR    R4,R6
         BE    DOCVATIN
         TM    NORORD,ORDCON  SUPPORTING CONVOY ORDER ?
         BZ    DOCVATIN
         CLC   ORDSLOC,NORSLOC
         BNE   DOCVATIN
         CLC   ORDNLOC,NORNLOC
         BNE   DOCVATIN
         BAL   R9,SUPADD
         STC   R7,0(,R15)    SET DESCENDING COUNTER
DOCVATIN LA    R6,ORDLEN(,R6)
         BCT   R7,DOCVATT
         SPACE
         CLC   DOMSAVE+4(4),ZEROES   ANY ATTACK ?
         BNE   DOCVSUP       YES
         BAL   R9,FORMAORD   SHOULDN'T HAVE GOTTEN HERE
         MVC   AORDMSG(L'DCONOC),DCONOC
         BAL   R9,MOVPRINT   PRINT IT
         NI    ORDFP,255-ORDCON  TURN OFF UNITS CONVOY FLAG
         BAL   R9,FORCEST    MAKE IT STAND AT THE OLD LOCATION
         OI    ORDFP,ORDST    FORCED STAND
         B     DOCVINC
         SPACE
DOCVSUP  MVC   CKLOC,ORDALO
         MVC   CKNLOC,ORDALN
         SPACE
*        DO A TRIANGULAR SORT;  IF A CONVOY PATH EXISTS, ITMUST SHOW
*          UP AT SOME STEP DURING THE SORTING PROCESS
*
         LA    R4,DOMSAVE+4  BEGIN ADDRESS
         L     R7,DOMSAVP    LAST + 1 ADDRESS
         SH    R7,DH4        MAKE LAST ADDRESS
         LR    R5,R4         ADDR OF HIGHEST SCANNED TO DATE
         LR    R6,R4         ADDRESS FOR BACKWARD FLIP
         B     DOCSRT3       MUST CHECK PATH BEFORE FLIP
         SPACE
DOCSRT1  CLC   0(1,R5),4(R5)   IN SEQUENCE ?
         BL    DOCSRT8       YES, TRY ANOTHER
         LR    R6,R5         DO BACK FLIP
         B     *+8           AVOID DECREMENTING FIRST FLIP
DOCSRT2  SH    R6,DH4        GET PREVIOUS ELEMENT
         CR    R6,R4         PRIOR TO START OF LIST ?
         BL    DOCSRT8       YES, ALL IN SEQUENCE UP TO (5)
         CLC   0(1,R6),4(R6)  IN SEQUENCE ?
         BL    DOCSRT8       YES, DONE
         LM    R14,R15,0(R6)   LOAD BOTH ELEMENTS
         LR    R0,R14        REVERSE SEQUENCE
         STM   R15,R0,0(R6)  STASHED FLIPPED SEQUENCE
         SPACE
DOCSRT3  STM   R1,R8,24(R13)   SAVE REGISTERS
         MVC   CKUN,CKLOC    START OF SEARCH - FROM ELEMENT
         MVI   MAXSFG,0      CLEAR SUCCESS FLAG
         LA    R7,DOMSAVE+4   GET CHAIN OF ELEMENTS
DOCSRT4  L     R4,0(,R7)     GET NEXT ELEMNT
         LA    R4,0(,R4)     CLEAR INDEX BYTE OUT
         LTR   R4,R4         OVERSHOT LIST ?
         BZ    DOCSRTLD      YES - FLIP AGAIN IF NECESSARY
         IC    R6,ORDALO     GET LOCATION OF THIS UNIT
         BAL   R9,ADPROV     GET PROVINCE ENTRY
         USING PROVINCE,R6
         LA    R1,PROSEA     GET ADDRESS OF FLEET MOVES
         LA    R2,L'PROSEA   NUMBER OF MOVES
DOCSRT5  MVC   DB(1),0(R1)   COPY THE ENTRY
         NI    DB,X'7F'      IGNORE COAST
         BZ    DOCSRT7       SKIP DUMMY ENTRY
         CLC   CKUN,DB       POINT TO PREVIOUS LOC ?
         BNE   DOCSRT5A      NO
         OI    MAXSFG,1      YES, SHOW BACK LINK HAS BEEN FOUND
         B     DOCSRT6       TEST IF DONE
DOCSRT5A CLC   CKNLOC,DB     POINTS TO DESTINATION ?
         BNE   DOCSRT7       NO, UP AGAIN
         OI    MAXSFG,2      SET END FOUND
DOCSRT6  TM    MAXSFG,3      WAS A COMPLETE CHAIN FOUND ?
         BO    DOCSFND       UES
DOCSRT7  LA    R1,1(,R1)     DO NEXT POTENTIAL MOVE
         BCT   R2,DOCSRT5    AND TRY IT
         TM    MAXSFG,1      WAS A LINK FOUND ?
         BZ    DOCSRTLD      NO, NEED ANOTHER FLIP
         MVI   MAXSFG,0      RESET FLAG
         MVC   CKUN,PRO#     SET LINK UNIT TO SCAN FOR
         LA    R7,4(,R7)     AND TRY NEXT ONE
         C     R7,DOMSAVP    END OF UNITS ?
         BL    DOCSRT4       AND CONTINUE TESTING
DOCSRTLD LM    R1,R8,24(R13) RESTORE THE REGS
         B     DOCSRT2       TRY ANOTHER FLIP
         SPACE
DOCSRT8  LA    R5,4(R5,0)    GET NEXT ENTRY
DH4      EQU   *-2
         CR    R5,R7         DONE ?
         BL    DOCSRT1       NO, TRY AGAIN
         LA    R7,DOMSAVE    GET START OF CHAIN
DOCSNOC  L     R4,0(,R7)     GET NEXT ENTRY
         BAL   R9,FORMAORD
         MVC   AORDMSG(L'DCONCIN),DCONCIN  SHOW INCOMPLETE CONVOY
         BAL   R9,MOVPRINT   PRINT IT
         BAL   R9,FORCEST    FORCE A STAND
         OI    ORDFP,ORDST   SUPPRESS SUCCESSFUL STAND MESSAGE
         LA    R7,4(,R7)     NEXT
         C     R7,DOMSAVP
         BL    DOCSNOC       NO NEXT
         B     MOVEREPT      GO TO COMPRESS THE TABLE
         SPACE 2
DOCSFND  MVI   4(R7),0       INDICATE ENTRY UNUSED, IF ANY
         LM    R1,R8,24(R13)   RESTORE
         LA    R7,DOMSAVE+4  GET FIRST CONVOYING UNIT
         MVI   MAXSFG,0      RESET FLAG
DOCSFDEL L     R4,0(,R7)     GET FIRST UNIT
         LA    R4,0(,R4)     CLEAR IT OUT
         CLI   0(R7),0       FIRST UNUSED UNIT ?
         BNE   *+8           NO
         OI    MAXSFG,1      SET UNITS UNUSED FLAG
         L     R6,AORD#BEG
         LH    R8,ORDCOUNT
         USING NORSECT,R6
DOCSFDES CR    R4,R6
         BE    DOCSFDEI      SKIP
         TM    NORORD,ORDSUP IS THIS A UNIT SUPPORTING THIS ONE
         BZ    DOCSFDEI      NO
         CLC   ORDALO,NORALN
         BNE   DOCSFDEI      NO
         ST    R8,DOCS9      SAVE
         LR    R4,R6         SAVE OVER ORDER
         BAL   R9,FORMAORD
         MVC   AORDMSG(L'DCONDEL),DCONDEL  SHOW SUPPORT DELIVERED
         BAL   R9,MOVPRINT   PRINT IT
         BAL   R9,ACCORD     PROCESS IT
         L     R4,0(,R7)     GET FOUR BACK
         LA    R4,0(,R4)
         L     R8,DOCS9      RESTORE
DOCSFDEI LA    R6,ORDLEN(,R6)
         BCT   R8,DOCSFDES   TRY AGAIN
         BAL   R9,FORMAORD
         BAL   R9,ACCORD
         TM    MAXSFG,1      UNUSED ?
         BO    DOCSFDEU
         MVC   AORDMSG(L'DCONDEL),DCONDEL   SHOW CONVOY DELIVERED
         B     DOCSFDEP
DOCSFDEU MVC   AORDMSG(L'DCONCUN),DCONCUN   UNUSED
DOCSFDEP BAL   R9,MOVPRINT   PRINT IT
         LA    R7,4(,R7)     NEXT
         C     R7,DOMSAVP
         BL    DOCSFDEL
         L     R4,DOMSAVE    GET CONVOYED UNIT
         OI    ORDFP,ORDCONA   SHOW SUCCESSFUL CONVOY
         IC    R6,ORDALO     GET FROM LOCATION
         BAL   R9,ADPROV     GET ADDRESS
         USING PROVINCE,R6
         NI    PROF,255-PROFARM-PROFNAV-PROFSOU   CLEAR IT OUT
         MVI   PROCC,0       EMPTY
         MVI   ORDLOC,0      PARTIAL MOVE
         B     MOVEREPT      GO TO COMPRESS THE TABLE
         SPACE
DOCVINC  LA    R4,ORDLEN(,R4)
         BCT   R5,DOCVOOP
         SPACE 2
*        DON'T TRUST ANYBODY - THIS REALLY SHOULDN'T HAPPEN
*        CHECK FOR CONVOYING FLEET LEFT ALL ALONE
         L     R4,AORD#BEG
         LH    R5,ORDCOUNT
         LTR   R5,R5
         BNP   MOVEND        BUT HOW DID WE GET HERE ?
         MVI   MAXSFG,0      CLEAR FLAG
DOCBLOOP TM    ORDORD,ORDCON   CONVOY ORDER ?
         BZ    DOCBLOOM      NO
         MVC   AORDMSG(L'DCONCUN),DCONCUN  SHOW UNUSED
         BAL   R9,MOVPRINT
         BAL   R9,FORCEST    MAKE IT STAND
         OI    ORDFP,ORDST   SUPPRESS SUCCESSFUL STAND MESSAGE
         OI    MAXSFG,1      SHOW UNIT CHANGED
DOCBLOOM LA    R4,ORDLEN(,R4)  GET NEXT ORDER
         BCT   R5,DOCBLOOP
         TM    MAXSFG,1      ANY CHANGED ?
         BNZ   MOVEREPT      YES - GO CLEAN UP
         SPACE 2
NOCONV   EQU   *
         EJECT
*        PROCESS ATTACKED SUPPORTS HERE
*
         TM    CONFG,ORDSUP    ANY ?
         BZ    NOATSUP       NO
COASGAIN L     R4,AORD#BEG         GET FIRST ONE
         LH    R5,ORDCOUNT
COASLOOP TM    ORDFP,ORDSUP  ATTACKED SUPPORT ?
         BZ    COASINC       NO
         ZAP   MAXSUP,ORDPSUP   STASH SUPPORT COUNT FOR THIS SUPPORT
         BAL   R9,SUPINIT    SAVE THE ADDRESS
         L     R6,AORD#BEG
         LH    R7,ORDCOUNT
COASLOP  CR    R4,R6
         BE    COASLIN
         USING NORSECT,R6
         CLC   ORDALO,NORALN   ATTACK OR SUPPORT FOR THIS UNIT ?
         BNE   COASLIN       NO, SKIP
         TM    NORFP,ORDSUP  ALSO ATTACKED SUPPORT ?
         BO    COASINC       YES - NOT SURE HOW
         TM    NORORD,ORDMO   MOVE ?
         BNO   COASLIN       NO
         CP    NORPSUP,MAXSUP    SUPERIOR FORCE ?
         BNH   COASLIN       NO, IGNORE
         ZAP   MAXSUP,NORPSUP  ELSE REMEMBER IT
         BAL   R9,SUPADD     AND SAVE THE ADDRESS
COASLIN  LA    R6,ORDLEN(,R6)
         BCT   R7,COASLOP
         NI    ORDFP,255-ORDSUP   TURN OFF THE ATTACK FLAG
         CP    MAXSUP,ORDPSUP   WAS THIS A SUCCESSFULL ATTACK ?
         BNH   COASGAIN      NO, DO OTHER ATTACKED SUPPORTS
         SPACE
COASWEAK BAL   R9,FORMAORD   FORMAT CANCELLED MOVE/SUPPORT
         MVC   AORDMSG(L'DCONAT),DCONAT   SHOW ATTACKED
         L     R6,DOMSAVE+4   GET ATTACKER'S LOCATION
         LA    R1,NORSUN     PASS IT
         LA    R2,AORDMSG+L'DCONAT  OUTPUT ADDRESS
         BAL   R9,FORMALOC   AND FORMAT IT
         BAL   R9,MOVPRINT   PRINT IT
         BAL   R9,FORCEST    CONVERT SUPPORT TO A STAND
         OI    ORDFP,ORDST   SHOW FORCED STAND
         B     MOVEREPT      GO TO CLEAN UP THE TABLE
         SPACE
COASINC  LA    R4,ORDLEN(,R4)
         BCT   R5,COASLOOP
         NI    CONFG,255-ORDSUP  NO MORE ATTACKED SUPPORTS LEFT ?
NOATSUP  EQU   *
         EJECT
*        PROCESS FACE-OFFS
*
         TM    CONFG,ORDST   FACE-OFF CHECK REQUIRED ?
         BZ    NOCONFA       NO
         L     R4,AORD#BEG
         LH    R5,ORDCOUNT
COFLOOP  TM    ORDORD,ORDMO  MOVE ?
         BZ    COFLINC       NP
         CP    ORDPCON,P0    NO OTHER CONFLICT ?
         BNE   COFLINC       NO
         BAL   R9,SUPINIT
         L     R6,AORD#BEG
         LH    R7,ORDCOUNT
COFLMAT  CR    R4,R6
         BE    COFLIN
         USING NORSECT,R6
         TM    NORORD,ORDMO  ALSO A MOVE ?
         BZ    COFLIN        NO
         CLC   ORDALN,NORALO   NEW = OLD ?
         BNE   COFLIN
         CLC   ORDALO,NORALN   OLD = NEW ?
         BNE   COFLIN        NO
         CP    ORDPSUP,NORPSUP   EQUAL SUPPORT ?
         BE    COFLADD       YES, PROCESS FACE-OFF
         ST    R6,DOMSAVE+4   SET STRONGER ONE IN LIST, WEAKER IN (4)
         BL    COFLST        OK, WAS WEAKER
         ST    R4,DOMSAVE+4   ELSE SWAP THEM
         LR    R4,R6         GET WEAK ONE
COFLST   B     COASWEAK      ATTACKED BY
         SPACE
COFLADD  BAL   R9,SUPADD     SAVE THIS ALSO
         B     DOSLOOP4      PROCESS SUPPORTING ORDERS ONLY
         SPACE
COFLIN   LA    R6,ORDLEN(,R6)
         BCT   R7,COFLMAT
COFLINC  LA    R4,ORDLEN(,R4)
         BCT   R5,COFLOOP
         NI    CONFG,255-ORDST  SHOW FACE-OFFS DONE
NOCONFA  EQU   *
         EJECT
*        DAISY CHAIN CHECK
*
         TM    CONFG,ORDMO   DAISY-CHAIN CHECK REQUIRED ?
         BZ    NODAISY
         L     R4,AORD#BEG
         LH    R5,ORDCOUNT
DAISLOOP TM    ORDORD,ORDMO  MOVE ?
         BZ    DAISINC       NO
         CLI   ORDLOC,0      GARBAGE ?
         BE    DAISINC       NO
         CP    ORDPCON,P1    SINGLE ATTACK ONLY ?
         BNE   DAISINC       NO
         MVC   CKUN(3),ORDALO   MOVE CURRENT, 'PREVIOUS' AND NEW LOC
         BAL   R9,SUPINIT    SET TABLE ENTRY
DAISOOP  L     R6,AORD#BEG
         LH    R7,ORDCOUNT
         USING NORSECT,R6
DAISREP  TM    NORORD,ORDMO  ALSO A MOVE ?
         BZ    DAISKIP       NO, SKIP IT
         CLI   NORLOC,0      CLEARED OUT ?
         BE    DAISKIP       YES, IGNORE
         CP    NORPCON,P1    ALSO ONLY ONE ATTACK ?
         BNE   DAISKIP       NO
         CLC   CKNLOC,NORALO   SITTING IN DESTINATION ?
         BNE   DAISKIP       NO, SKIP IT
DAISDUP  LA    R1,DOMSAVE
         C     R6,0(,R1)     ALREADY IN THERE ?
         BE    DAISKIP       YES, IGNORE IT
         LA    R1,4(,R1)
         C     R1,DOMSAVP    END OF CHAIN ?
         BL    DAISDUP       NO, TRY AGAIN
         BAL   R9,SUPADD     ADD IT TO THE CHAIN
         CLC   CKLOC,NORALN  COMPLETE CIRCLE ?
         BE    DAISFOUN      YES
         MVC   CKNLOC,NORALN   ELSE PUT THE NEW LOCATION IN
         B     DAISOOP       AND TRY FOR NEXT ONE IN THE CHAIN
         SPACE 1
DAISFOUN L     R4,DOMSAVE    GET FIRST ONE
         IC    R6,ORDALO     GET THE LOCATION
         BAL   R9,ADPROV
         USING PROVINCE,R6
         MVI   PROCC,0
         NI    PROF,255-PROFSOU-PROFARM-PROFNAV  CLEAR OUT
         MVI   ORDLOC,0
         MVI   OLDCOUNT,X'7F'   FORCE ANOTHER ITERATION
         B     MOVEREPT
         SPACE
DAISKIP  LA    R6,ORDLEN(,R6)
         BCT   R7,DAISREP
DAISINC  LA    R4,ORDLEN(,R4)
         BCT   R5,DAISLOOP   TRY MORE
         NI    CONFG,255-ORDMO
NODAISY  EQU   *             NO
         EJECT
*        CONTROL SEQUENCE OF PROCESSING
*        DO STAND ORDERS THAT ARE CONTESTED
*
         L     R4,AORD#BEG
         LH    R5,ORDCOUNT
SOSLOOP  TM    ORDORD,ORDST  STAND ?
         BO    DOSLOOP1      YES, PROCESS IT
         LA    R4,ORDLEN(,R4)
         BCT   R5,SOSLOOP
         SPACE 2
*        TRY TO AVOID PROBLEMS.  LOOK FOR A CONNECTED CHAIN OF MOVES
*        ===>===>===>   IF FOUND, DO THE LAST ONE FIRST; THIS WILL
*        MAKE IT EASIER FOR THE OTHERS TO BE RESOLVED
*        TWO POSSIBLE ERROR CONDITIONS - 34 OR MORE ENTRIES ? HOW
*        MOVES FOR A RING; IF SO, THE ONE WITH HIGHEST SUPPORT COUNT
*        WILL BE DONE FIRST.
*
         MVI   SOCFLAG,0     ZERO FLAG
SOCAGAIN L     R4,AORD#BEG
         LH    R5,ORDCOUNT
SOCLOOP  TM    ORDORD,ORDMO    MOVE ?
         BZ    SOCLINC       NO, IGNORE
         BAL   R9,SUPINIT    SAVE ADDRESS
SOCREPT  MVC   CKLOC,ORDALN  SET START POSITION OF CHAIN SEARCH
         L     R6,AORD#BEG
         LH    R7,ORDCOUNT   SEARCH FOR MOVE FROM THIS DESTINATION
         USING NORSECT,R6
SOCLOP   CR    R4,R6         SAME ONE ?
         BE    SOCLIN        YES, SKIP
         TM    NORORD,ORDMO  ALSO A MOVE ?
         BZ    SOCLIN        NO, SKIP
         CLC   CKLOC,NORALO  FROM OLD LOCATION ?
         BNE   SOCLIN        NO, IGNORE
         L     R8,DOMSAVP    REMEMBER OLD VALUE
         BAL   R9,SUPADD     ADD THIS ONE IN
         CR    R8,R1         WAS ENTRY STASHED ?
         BE    SOCRING       NO, WE HAVE A RING
         LR    R4,R6         SWAP
         LR    R5,R7         SWAP
         LA    R9,DOMSAVE+4*34
         CR    R1,R9         ARE ALL ENTRIES EXHAUSTED ?
         BL    SOCREPT       NO, CONTINUE SEARCH
SOCCER   SH    R1,DH4        YES, USE LAST ENTRY
         L     R4,0(,R1)     ADDRESS OF LAST ONE
         B     DOSLOOP1      PROCESS IT
         SPACE
SOCRING  OI    SOCFLAG,1     SET FOR A RING FOUND
         TM    SOCFLAG,3     ARE RINGS TO BE DONE THIS TIME AROUND ?
         BNO   SOCLINC       NO, SKIP IT TILL NEXT TIME
         ZAP   MAXSUP,P0     SET HIGHEST SUPPORT COUNT FOUND
         LA    R8,DOMSAVE    POINT TO START OF LIST
         L     R4,DOMSAVE    JUST IN CASE - LOAD FIRST ADDRESS
SOCRLP   C     R8,DOMSAVP    LIST EXHAUSTED ?
         BNL   DOSLOOP1      YES, PROCESS THIS ENTRY
         L     R6,0(,R8)     LOAD THIS ORDER
         LA    R8,4(,R8)     AND POINT TO NEXT ORDER
         CP    MAXSUP,NORPSUP  HIGHER SUPPORT COUNT ?
         BH    SOCRLP        NO, IGNORE
         ZAP   MAXSUP,NORPSUP   REMEMBER THIS ONE
         LR    R4,R6         AND SAVE ITS ADDRESS
         B     SOCRLP        TRY FOR HIGHER ONE
         SPACE
SOCLIN   LA    R6,ORDLEN(,R6)
         BCT   R7,SOCLOP     TRY NEXT ORDER
         L     R1,DOMSAVP    GET LAST OR ONLY ONE ON CHAIN
         B     SOCCER        GO TO DO IT
         SPACE
SOCLINC  LA    R4,ORDLEN(,R4) TRY ANOTHER ONE
         BCT   R5,SOCLOOP    CURSE, CURSE, AND RE-CURSE
         TM    SOCFLAG,2     WAS RING SEARCH DONE ?
         BO    SOCLAST       YES, NO MORE TO BE DONE
         TM    SOCFLAG,1     WAS A RING FOUND ?
         BZ    SOCLAST       NO, CONTINUE
         MVI   SOCFLAG,2     SET FOR RING SEARCH
         B     SOCAGAIN      AND DO RINGS
SOCLAST  EQU   *
         EJECT
*        TRY TO PROCESS ANY THAT ARE LEFT
*
         L     R4,AORD#BEG
         LH    R5,ORDCOUNT
         LTR   R5,R5         ERROR ?
         BNP   MOVEND        YES, EXIT
DOSLOOP  TM    ORDORD,ORDST+ORDMO+ORDSUP  WHATEVER ?
         BZ    DOSLINC
DOSLOOP1 BAL   R9,SUPINIT
         USING NORSECT,R6
DOSLOOP2 LA    R1,STANDOFF
         LA    R2,34
         MVC   CKLOC,ORDALN  PROVISIONALLY SET CONFLICT ADDRESS
         TM    ORDORD,ORDST+ORDMO   STAND/MOVE ?
         BNZ   DOSSOLP       YES, ADDRESS CORRECT
         MVC   CKLOC,ORDALO  ELSE USE OLD ADDRESS
DOSSOLP  CLI   0(R1),0       EMPTY ENTRY ?
         BE    DOSSIER       YES, USE IT
         CLC   CKLOC,0(R1)   SAME ?
         BE    DOSSIER2      SKIP, ALREADY IN THERE
         LA    R1,1(,R1)
         BCT   R2,DOSSOLP
         B     DOSSIER2      AVOID SETTING (1)
DOSSIER  MVC   0(1,R1),CKLOC   SAVE THIS ADDRESS
         LA    R1,1(,R1)     UP POINTER
         ST    R1,STANDAD    SET NEW VALUE
DOSSIER2 L     R6,AORD#BEG
         LH    R7,ORDCOUNT
         MVC   RETLOC,CKLOC  SAVE LOCATION TO WHICH RETREATS CAN'T GO
DOSLATT  CR    R4,R6
         BE    DOSLATIN
         TM    NORORD,ORDST+ORDSUP  STAND OR SUPPORT ORDER ?
         BZ    DOSLATM       NO
         CLC   CKLOC,NORALO  IN OUR LOCATION ?
         BE    DOSLATP
         B     DOSLATIN
DOSLATM  TM    NORORD,ORDMO  MOVE ?
         BZ    DOSLATIN
         CLC   CKLOC,NORALN  IN OUR LOCATION ?
         BNE   DOSLATIN
DOSLATP  BAL   R9,SUPADD
         TM    NORORD,ORDST+ORDSUP+ORDCON
         BZ    DOSLATIN
         TM    ORDORD,ORDMO   MOVE ?
         BZ    DOSLATIN      NO, DON'T CHECK FLIP
         MVC   0(4,R15),DOMSAVE
         ST    R6,DOMSAVE    FLIP STANDING TO START OF LIST
DOSLATIN LA    R6,ORDLEN(,R6)
         BCT   R7,DOSLATT
         SPACE
DOSLOOP4 LA    R8,DOMSAVE    ENTRY FOR SUPPORT ADDITION ONLY
DOSLSUP  L     R4,0(,R8)
         L     R6,AORD#BEG
         LH    R7,ORDCOUNT
DOSLSUL  CR    R4,R6
         BE    DOSLSUI
         TM    NORORD,ORDSUP
         BZ    DOSLSUI
         TM    ORDORD,ORDMO  MOVE ?
         BO    DOSLSUM       YES, TEST
         TM    NORSORD,ORDST   STAND ?
         BZ    DOSLSUI       NO, CAN'T BE SUPPORT FOR THIS ONE
         CLC   ORDALO,NORALN   SUPPORT AT OUR ADDRESS ?
         BNE   DOSLSUI       NO
         B     DOSLSUA       ADD IN
DOSLSUM  CLC   ORDALS,NORALS   SAME LOCATION ?
         BNE   DOSLSUI
         CLC   ORDALN,NORALN
         BNE   DOSLSUI
DOSLSUA  BAL   R9,SUPADD
DOSLSUI  LA    R6,ORDLEN(,R6)
         BCT   R7,DOSLSUL
         LA    R8,4(,R8)
         C     R8,DOMSAVP    DONE
         BL    DOSLSUP       NO
DOSLSEN  MVI   MAXSFG,0
         ZAP   MAXSUP,P0
         LA    R8,DOMSAVE
         XR    R6,R6
         XR    R7,R7
DOSLMAX  L     R4,0(,R8)
         C     R4,DOMSAVE    FIRST ENTRY ?
         BE    DOSLMAX1      YES, DO UNCONDITIONALLY
         TM    ORDORD,ORDSUP
         BO    DOSLMEN
DOSLMAX1 CP    MAXSUP,ORDPSUP   ENOUGH SUPPORT ?
         BH    DOSLMAI
         BE    DOSLMAT
         ZAP   MAXSUP,ORDPSUP    SET NEW MAX COUNT
         LR    R6,R4
DOSLMAT  LR    R7,R4
DOSLMAI  LA    R8,4(,R8)
         C     R8,DOMSAVP
         BL    DOSLMAX
DOSLMEN  CR    R6,R7         STANDOFF ?
         BE    DOSLNOC       NO
         OI    MAXSFG,1      YES
         LA    R7,DOMSAVE+4  PROV.
         L     R4,DOMSAVE
         NI    ORDFP,255-ORDSUP  JUST IN CASE - TURN OFF ATTACKED SUPP
         TM    ORDORD,ORDMO  MOVE ?
         BZ    DOSLELIM
DOSLNOC  LA    R7,DOMSAVE
         TM    MAXSFG,1      STAND-OFF ?
         BO    DOSLELIM      YES, SKIP CHECKING FOR RETREATS
         TM    NORORD,ORDMO   WAS SUCCESSFUL ORDER A MOVE ?
         BZ    DOSLELIM      NO, DON'T BOTHER CHECKING
         SPACE 2
*        IN CASE OF SUPERIOR ATTACK - ONE OR MORE ORDERS ARE INVALID IF
*        THEY WILL CAUSE A UNIT OF THE MOVING OR SUPPORTING COUNTRY
*        TO RETREAT.   THIS CODE CHECKS FOR POTENTIAL RETREATS, AND
*        WILL CANCEL ONE SUPPORT ORDER, IF POSSIBLE; ELSE THE MOVE
*        ORDER IS CANCELLED
*
         STM   R1,R8,24(R13)   SAVE THE REGISTERS
         LA    R7,DOMSAVE-4   GET LIST
         LA    R6,0(,R6)           MAKE SURE OF HIGH BYTE
DOSALOOP LA    R7,4(,R7)     GET NEXT ENTRY
         C     R7,DOMSAVP    LAST ENTRY ?
         BNL   DOSALEND      YES - NO FORCED RETREATS FOR SAME COUNTRY
         L     R4,0(,R7)     GET NEXT ORDER
         LA    R4,0(,R4)     CLOBBER HIGH BYTE
         CR    R4,R6         SAME AS WINNING ENTRY ?
         BE    DOSALOOP            YES - SKIP IT
         TM    ORDORD,ORDMO    IS THIS ORDER A MOVE ?
         BZ    DOSALST       NO, CHECK ORIGINAL LOCATION
         CLI   ORDLOC,0      IS THIS A PARTIALLY DILODGED MOVE ?
         BNE   DOSALOOP      NO, LEAVE IT IN
         CLC   ORDALN,NORALN   SAME DESTINATION ?
         BE    DOSACHEK      YES - WILL BE DISLODGED - CHECK COUNTRIES
         B     DOSALOOP            ELSE IGNORE
         SPACE
DOSALST  CLC   ORDALO,NORALN   SUCCESSFULL ATTACK ON RESTING UNIT ?
         BNE   DOSALOOP      NO, IGNORE
DOSACHEK MVC   DB(1),NORATC   GET COUNTRIES INVOLVED IN ATTACK
         NC    DB(1),ORDCO   SAME AS RETREATING UNIT ?
         BZ    DOSALOOP      NO, CHECK OTHERS
         MVC   SUPCO,ORDCO    SAVE RETREATING COUNTRY
         ST    R4,SUPWORK    SAVE ADDRESS OF DISLODGED UNIT
         LA    R7,DOMSAVE-4   GET LIST AGAIN
DOSASLOP LA    R7,4(,R7)           NEXT ENTRY
         C     R7,DOMSAVP     END ?
         BNL   DOSASEND      YES - NOT FOUND; CANCEL MOVE
         L     R4,0(,R7)     CHECK FOR SUPPORT FROM SAME CO.
         LA    R4,0(,R4)
         CR    R4,R6         SAME ?
         BE    DOSASLOP      YES, IGNORE
         TM    ORDORD,ORDSUP   SUPPORT ORDER ?
         BZ    DOSASLOP            NO, IGNORE
         CLC   ORDSCO(5),NORSCO   SUPPORT FOR SUCCESSFULL UNIT ?
         BNE   DOSASLOP          NO
         CLC   ORDCO,SUPCO   SAME COUNTRY ?
         BNE   DOSASLOP            NO, KEEP LOOKING
         B     DOSACAN       YES - CANCEL SUPPORT ORDER
DOSASEND CLC   NORCO,SUPCO   IS MOVING COUNTRY SAME ?
         BNE   DOSALEND      NO - PROGRAM ERROR - IGNORE IT
         LR    R4,R6         LOAD CENCALLED ORDER ADDRESS
DOSACAN  BAL   R9,FORMAORD    FORMAT THE BAD ORDER
         MVC   AORDMSG(L'DCATFAIL),DCATFAIL   TELL WHY
         L     R6,SUPWORK          GET FRIGHTENED UNIT BACK
         LA    R1,CKUN       COPY LOCATION AND UNIT
         MVC   CKUN,NORUN
         MVC   CKLOC,NORALO
         LA    R2,AORDMSG+L'DCATFAIL
         BAL   R9,FORMALOC
         BAL   R9,MOVPRINT   SHOW IT
         ST    R4,SUPWORK    SAVE ITS ADDRESS
         LA    R7,DOMSAVE-4
DOSAPLOP L     R4,0(,R7)     GET NEXT ONE
         LA    R4,0(,R4)     JUST IN CASE
         L     R6,SUPWORK    GET OUR ADDRESS BACK
         CR    R4,R6         SAME ?
         BE    DOSAPINC      YES, SKIP IT
         TM    ORDORD,ORDSUP   IS THIS A SUPPORT ?
         BZ    DOSAPINC      NO, IGNORE
         TM    NORORD,ORDMO+ORDST   WAS OURS STAND/MOVE ?
         BNZ   DOSAPNLO      YES - CHECK NEW LOCATION
         CLC   ORDALN,NORALO   SUPPORT FOR THIS UNIT ?
         BNE   DOSAPINC      NO
         B     DOSAPDEL      YES - DELETE SUPPORT
DOSAPNLO CLC   NORALS(2),ORDALS   SUPPORT FOR THIS ?
         BNE   DOSAPINC      NO, SKIP
DOSAPDEL BAL   R9,FORMAORD   FORMAT ORDER / CLOBBERS (6)
         MVC   AORDMSG(L'DCONFAIL),DCONFAIL   SHOW IT FAILED
         BAL   R9,MOVPRINT   PRINT IT
         BAL   R9,FORCEST    CONVERT TO A STAND
         OI    ORDFP,ORDST   SHOW FORCED STAND
DOSAPINC LA    R7,4(,R7)     GET NEXT ONE
         C     R7,DOMSAVP    ALL SCANNED ?
         BL    DOSAPLOP      NO, DO NEXT
         L     R4,SUPWORK    GET CANCELLED ONE BACK
         BAL   R9,FORCEST    CANCEL - IF PARTIAL MOVE, THEN TOO BAD
         OI    ORDFP,ORDST    SHOW FORCED STAND
         B     MOVEREPT
         SPACE
DOSALEND LM    R1,R8,24(R13)   RESTORE REGS
         SPACE 2
DOSLELIM L     R4,0(,R7)
         BAL   R9,FORMAORD
         C     R4,DOMSAVE    FIRST ENTRY IN TABLE ?
         BE    DOSLELW       YES, NO NEED TO CHECK FOR SUPPORT
         TM    ORDORD,ORDSUP
         BZ    DOSLELW
         MVC   AORDMSG(L'DCONDEL),DCONDEL
         B     DOSLACP
DOSLELW  CP    ORDPSUP,MAXSUP
         BL    DOSLELF       YES
         TM    MAXSFG,1      STANDOFF ?
         BO    DOSLELS
         MVC   AORDMSG(L'DCONSUCS),DCONSUCS
         TM    ORDORD,ORDSUP   IS THIS A SUPPORT ORDER ?
         BZ    DOSLACP       NO
         C     R4,DOMSAVE    FIRST ENTRY IN TABLE ?
         BE    DOSLELIN      YES, LEAVE IT
DOSLACP  BAL   R9,MOVPRINT   PRINT IT
         BAL   R9,ACCORD
         B     DOSLELIN
DOSLELS  MVC   AORDMSG(L'DCONSTAN),DCONSTAN
         TM    ORDORD,ORDSUP   SUPPORT ORDER ?
         BZ    DOSLACS       NO, PRINT
         C     R4,DOMSAVE    FIRST ENTRY IN TABLE ?
         BE    DOSLELIN      YES - SKIP IT
         B     DOSLACS
DOSLELF  MVC   AORDMSG(L'DCONFAIL),DCONFAIL
DOSLACS  BAL   R9,MOVPRINT   PRINT IT
         BAL   R9,FORCEST    FORCE A STAND
         OI    ORDFP,ORDST   SUPPRESS SUCCESSFUL STANDS
DOSLELIN LA    R7,4(,R7)
         C     R7,DOMSAVP
         BL    DOSLELIM
         B     MOVEREPT
         SPACE
DOSLINC  LA    R4,ORDLEN(,R4)
         BCT   R5,DOSLOOP
         SPACE 2
MOVEREPT BAL   R9,ORDURE
         CLC   ORDCOUNT,ZEROES
         BE    MOVEND
         BAL   R9,CONTORD
         CLC   ORDCOUNT,OLDCOUNT   ANY ELIMINATED ?
         BNE   MOV3LOOP      YES, TRY AGAIN - SOME MAY NOW BE UNCONTEST
         SPACE
MOVEND   L     R12,=A(DIPLMOV2)  RETURN TO PREVIOUS CSECT
         B     MOVEXIT-DIPLMOV2(,R12)  DO IT
         SPACE 2
MOVPRINT CLC   DCONSUCS,AORDMSG
         BNE   MOVPRINP
         TM    ORDFP,ORDST
         BOR   R9
MOVPRINP LA    R1,VAORD
         CALL  XPRNT
         BR    R9
         SPACE 2
         LTORG
         TITLE 'DIPLOMACY --- BUILD PROCESSING'
DIPLBUIL CSECT
         USING *,R12
         LR    R12,R15
         SPACE
         BAL   R9,DATECHEK   DO COMMON STUFF
         MVC   DIDMOVE,ROUTE  ALLOW ALL PLAYERS
         USING STASECT,R7
         LA    R7,STATAB
         LA    R8,7
         USING ORDSECT,R4
         L     R4,AORD#BEG
         LA    R5,34
BUINLP   CP    STAFOR,STACON   REMOVE / BUILD ?
         BE    BUININ        NO
         BH    BUINLD        REMOVE
         CP    STABUIL,P0    ROOM TO BUILD ?
         BNH   BUININ
         MVC   DB(1),ROUTE   VALID PLAYER ?
         NC    DB(1),STABIT
         BZ    BUININ        NO, BUILD NOT ALLOWED
         ZAP   MAXSUP,STACON
         SP    MAXSUP,STAFOR  GET NUMBER ALLOWD TO BUILD
         LA    R1,ORDBUIL    GET ORDER CODE
         B     BUINLI
BUINLD   ZAP   MAXSUP,STAFOR
         SP    MAXSUP,STACON  GET NUMBER TO REMOVE
         LA    R1,ORDREM     LOAD ORDER CODE
BUINLI   STC   R1,ORDORD
         MVC   ORDCO,STABIT
         LA    R4,ORDLEN(,R4)
         SP    MAXSUP,P1
         BP    BUINLI
BUININ   LA    R7,STASLEN(,R7)
         BCT   R8,BUINLP
         BAL   R9,ORDURE
         SPACE 2
BUMIN    OI    READ,RCON
         MVI   VCARD+3,0
         MVI   VCARD+4,C'0'
         BAL   R9,GETCARD
         B     BUMAN
         BAL   R9,PARMA
         B     BUMINB
         LA    R7,COPFX
         LA    R8,(STATEND-COPFX)/STASLEN
BUMINF   CLC   STASNAT,PARM1+1   SAME ENTRY ?
         BE    BUMING        YES
         LA    R7,STASLEN(,R7)
         BCT   R8,BUMINF
BUMINB   OI    READ,RFULL
         B     BUMAN
         SPACE
BUMING   MVC   VCARD+3(1),STABIT
         CLI   STABIT,0
         BNE   BUMINO
         BAL   R9,MESSAGE
         B     BUMIN
         SPACE
BUMINO   OI    VCARD+3,128   GIVE TO GAME KEEPER ALSO
         BAL   R9,ECHO
         SPACE
BUMIR    MVI   VCARD+4,C'0'
         BAL   R9,GETCARD
         B     BUMIN
         BAL   R9,PARMA
         B     BUMORBID
         AP    CNTMOV,P1
         MVC   PCARD-1(4),STAMASK
         ED    PCARD-1(4),CNTMOV
         MVC   VCARD+3(1),STABIT
         OI    VCARD+3,128
         L     R4,AORD#BEG
         LA    R5,34
BUMIF    CLC   STABIT,ORDCO
         BE    BUMFND
         LA    R4,ORDLEN(,R4)
         BCT   R5,BUMIF
BUMORBID MVC   CAMSG(L'MSBDORD),MSBDORD
         BAL   R9,WARNER
         B     BUMIR
         SPACE
BUMFND   MVC   ORDSORD,ORDORD
         MVI   TESTSFLG,0
         LA    R3,PARM1
         BAL   R9,FINDORD
         NOP   0             OK IF NO ORDER
         CLC   ORDORD,ORDSORD  BUT IF SPECIFIED, MUST BE OK
         BNE   BUMORBID
         MVI   CUUN,0
         BAL   R9,TESTUNIT
         MVC   ORDUN,CUUN
         BAL   R9,TESTPROV
         B     BUMORBID
         BAL   R9,FINDPROV
         B     BUMORBID
         LA    R2,DB+1
BUMWHAT  TM    ORDORD,ORDBUIL
         BO    BUMBLE
         IC    R6,0(,R2)
         BAL   R9,ADPROV
         USING PROVINCE,R6
         CLC   ORDCO,PROCC
         BNE   BUMWHAI
         TM    PROF,PROFARM+PROFNAV
         BZ    BUMWHAI
         NI    PROF,255-PROFARM-PROFNAV-PROFSOU
         MVI   PROCC,0
BUMDONE  XC    ORDSECT(ORDLEN),ORDSECT
         MVC   CAMSG(L'MSBDONE),MSBDONE
BUMECH   BAL   R9,ECHO
         B     BUMIR
         SPACE
BUMWHAI  LA    R2,1(,R2)
         CLI   0(R2),0
         BE    BUMORBID
         B     BUMWHAT
         SPACE
BUMBLE   IC    R6,0(,R2)
         BAL   R9,ADPROV
         TM    PROF,PROFARM+PROFNAV   ALREADY OCCUPIED ?
         BNZ   BUMWHAI       CAN'T BE IT
         TM    PROF,PROFSUP  SUPPORT CENTER ?
         BZ    BUMWHAI       NO
         CLC   PROFHOM,ORDCO   SAME COUNTRY
         BNE   BUMWHAI
         TM    PROF,PROFLAN  ARMY ONLY ?
         BO    BUMARM
         TM    ORDUN,PROFNAV
         BZ    BUMARM
         OI    PROF,PROFNAV
         TM    PROF,PROFTWO
         BZ    BUMCOM
         CLI   PARM0+5,C'S'   SOUTH COAST ?
         BNE   BUMCOM        NO, ASSUME OTHERWISE
         OI    PROF,PROFSOU
         B     BUMCOM
BUMARM   OI    PROF,PROFARM
BUMCOM   MVC   PROSC,PROFHOM
         MVC   PROCC,PROFHOM
         B     BUMDONE
         SPACE 2
BUMAN    CLI   RETCODE+1,4   MAJOR ERRORS ?
         BH    CONTT         YES, NO FURTHER PROCESSING
         OI    NEWPLAY,PMOVE  SET FOR MOVE NEXT
         MVC   NEWDATE(L'SPRING),SPRING   SET FOR SPRING
         PACK  DB,NEWDATE+7(4)
         AP    DB,P1
         UNPK  NEWDATE+7(4),DB+5(3)
         OI    NEWDATE+10,C'0'
         BAL   R9,ORDURE
         MVI   VSTAM+4,C'0'
         MVI   VSTAM+3,0
         MVC   AORDER,AORDER-1
         TM    READ,RFULL
         BO    BUMAN1
         OI    READ,RFULL
         MVC   CARD(L'DCENDORD),DCENDORD
BUMAN1   OI    FLAG,STAT+SAVE
         MVI   VAORD+4,C'0'
         L     R4,AORD#BEG
         LA    R5,34
BUMOUT   TM    ORDORD,ORDREM
         BZ    BUMNXT
         L     R6,APRO#BEG
BUMPRO   CLC   ORDCO,PROCC
         BNE   BUMPROI
         TM    PROF,PROFSUP  SUPPLY CENTER ?
         BO    BUMPROI       YES, IGNORE
         TM    PROF,PROFARM+PROFNAV
         BNZ   BUMPDEL       YES - TAKE IT OFF
BUMPROI  LA    R6,PROLEN(,R6)
         C     R6,APRO#END
         BL    BUMPRO
         L     R6,APRO#BEG   SCAN FOR SUPPLY CNTR NOT A HOME
BUMPSR   CLC   ORDCO,PROCC   COUNTRY IS OCCUPYING ?
         BNE   BUMPSOI       NO
         CLC   PROFHOM,ORDCO   HOME CENTER ?
         BE    BUMPSOI       YES, SKIP
         TM    PROF,PROFARM+PROFNAV   SITTING IN IT ?
         BNZ   BUMPDEL       YES, DELETE IT
BUMPSOI  LA    R6,PROLEN(,R6)    TRY AGAIN
         C     R6,APRO#END
         BL    BUMPSR        TRY MORE
         L     R6,APRO#ARM
BUMPH    CLC   ORDCO,PROCC
         BNE   BUMPHOI
         TM    PROF,PROFARM+PROFNAV
         BZ    BUMPHOI
BUMPDEL  LR    R7,R6         SAVE OVER BAL
         MVC   ORDUN,PROF    COPY UNIT
         NI    ORDUN,PROFARM+PROFNAV   AND ONLY UNIT
         MVC   ORDLOC,PRO#   COPY LOCATION
         TM    PROF,PROFTWO+PROFSOU+PROFNAV
         BNO   *+8           NOT SOUTH COAST
         OI    ORDLOC,X'80'  ELSE SET SOUTH COAST
         BAL   R9,FORMAORD   FORMAT REMOVE
         MVC   AORDSCO(10),DCBYDFLT    SHOW WHY - DEFAULT
         LA    R1,VAORD      SHOW IT
         MVI   VAORD+4,C' '  RESET DOUBLE SPACE
         CALL  XPRNT
         LR    R6,R7         GET PROVINCE BACK
         MVI   PROCC,0       SHOW EMPTY
         NI    PROF,255-PROFARM-PROFNAV-PROFSOU     CLEAR OUT
         B     BUMNXT
BUMPHOI  LA    R6,PROLEN(,R6)
         C     R6,APRO#END
         BL    BUMPH
BUMNXT   LA    R4,ORDLEN(,R4)
         BCT   R5,BUMOUT
         B     CONTT
         DROP  R4,R6,R7
         SPACE 2
         LTORG
         TITLE 'DIPLOMACY --- RETREAT PROCESSING'
DIPLRETR CSECT
         USING *,R12
         LR    R12,R15
         SPACE
         BAL   R9,DATECHEK   DO COMMON INITIALIZATION
         MVC   DIDMOVE,HADMOVE  PRESERVCE MOVE STATUS
         B     RETAUCOM      GO TO COMMON CODE
         SPACE
*        ENTERED FROM MOVE CODING ALSO TO CHECK FOR AUTO RETREATS
*
         SPACE 2
RETAUTO  L     R4,AORD#BEG
         LA    R5,34
         LA    R7,34*ORDLEN(,R4)   LAST ENTRY + 1
RETAUTOL MVC   0(ORDLEN,R4),0(R7)   COPY LAST
         LA    R4,ORDLEN(,R4)
         BCT   R5,RETAUTOL   CLEAR ALL
         SPACE
RETAUCOM MVI   MAXSFG,1      SET FOR AUTO RETREATS ALLOWED
         USING STASECT,R7
         LA    R7,STATAB     GET COUNTRY TABLE
         LA    R8,7
         USING ORDSECT,R4
         USING PROVINCE,R6
         L     R4,AORD#BEG
RETINPP  LA    R5,34         SET FOR MAX OF 34
         L     R6,ARET#BEG   GET RETREAT TABLE
RETINLP  CLC   PROCC,STABIT  THIS COUNTRY ?
         BNE   RETIIN        NO
         ST    R6,DB         STASH RETREAT ADDRESS
         MVC   ORDPCON(4),DB   IN ORDER
         MVC   ORDUN,PROF    MAKE UNIT
         NI    ORDUN,PROFARM+PROFNAV  CLEAR OUT
         MVC   ORDSUN,ORDUN
         MVC   ORDCO,PROCC
         MVC   ORDSCO,PROCC
         MVI   ORDORD,ORDRET   SET FOR AUTO RETREAT
         MVI   ORDSORD,ORDRET
         MVC   ORDLOC,PRO#   SET CURRENT LOCATION
         TM    PROF,PROFNAV+PROFSOU+PROFTWO  SOUTH COAST ?
         BNO   *+8           NO, LEAVE IT
         OI    ORDLOC,X'80'  YES
         MVC   ORDSLOC,ORDLOC
         MVI   ORDNLOC,0     PROVISIONALLY CLEAR OUT
         LA    R14,PROLAND   NOW CHECK FOR SINGLE DESTINATION
         LA    R15,24
RETIEL   CLI   0(R14),0      EMPTY ENTRY ?
         BE    RETIEN        YES, SKIP
         CLC   ORDNLOC,0(R14)  DUPLICATE ?
         BE    RETIEN        YES, IGNORE
         CLI   ORDNLOC,0     FOUND ONE ?
         BNE   RETIEZ        NO
         MVC   ORDNLOC,0(R14)  STASH - BUT KEEP ONLY IF SINGLE ONE
RETIEN   LA    R14,1(,R14)   AGAIN
         BCT   R15,RETIEL
         B     RETIEF        AUTO RETREAT POSSIBLE FOR THIS FORCE
RETIEZ   MVI   MAXSFG,0      AUTO RETREAT NOT POSSIBLE - CHOICE EXISTS
         MVI   ORDNLOC,0     CLEAR DESTINATION
RETIEF   BAL   R9,FORABS     MAKE ABSULOTE ADDRESSES
         LA    R4,ORDLEN(,R4) POINT TO NEXT AVAILABLE ORDER
RETIIN   LA    R6,PROLEN(,R6)
         BCT   R5,RETINLP    TRY FOR MORE
         LA    R7,STASLEN(,R7)   REPEAT FOR NEXT COUNTRY
         BCT   R8,RETINPP
         CLI   VSTOT+1,C'M'  CALLED FROM MOVE PROCESSING ?
         BNE   RETIOD        NO, CONTINUE - CHECK INPUT CARDS
         TM    MAXSFG,1      IS AUTO RETREAT FEASIBLE ?
         BO    RETAUHD       YES, PUT SPECIAL HEADER
         LA    R1,MSAUTNO    NO, MUST SUBMIT
         CALL  XPRNT         PRINT NO-NO
         B     CONTT         AND EXIT
RETAUHD  LA    R1,MSAUTOK    SHOW AUTO RETREAT PROCESSING
         CALL  XPRNT
         B     RETANH        AND START IT
         SPACE
RETIOD   BAL   R9,ORDURE     SHOW ORDER TABLE
         SPACE 2
RETIN    OI    READ,RCON     SET FOR CONTROL CARD
         MVI   VCARD+3,0     SET FOR GENERAL ROUTING
         MVI   VCARD+4,C'0'  DOUBLE SKIP
         BAL   R9,GETCARD
         B     RETAN         NOT FOUND - FINISH RETREATS
         BAL   R9,PARMA      LOOK AT IT
         B     RETINB        INVALID
         LA    R7,COPFX      GET TABLE
         LA    R8,(STATEND-COPFX)/STASLEN   NUMBER
RETINF   CLC   STASNAT,PARM1+1  NATION OR MESSAGE ?
         BE    RETING        YES
         LA    R7,STASLEN(,R7)
         BCT   R8,RETINF     TY AGAIN
RETINB   OI    READ,RFULL    NOT RECOGNIZED - REREAD LATER
         B     RETAN         AND DO CURRENT STUFF
RETING   MVC   VCARD+3(1),STABIT  COPY ROUTING
         MVC   CUCO,STABIT   SAVE COUNTRY, IF ANY
         CLI   STABIT,0      MESSAGE ?
         BNE   RETINO        NO, PROCESS
         BAL   R9,MESSAGE    GO TO MESSAGE PROCESSOR
         B     RETIN         READ AGAIN
         SPACE 2
RETINO   OI    VCARD+3,128   ROUTE TO GAME KEEPER ALSO
         BAL   R9,ECHO       SHOW IT
         SPACE
RETIR    MVI   VCARD+4,C'0'
         BAL   R9,GETCARD
         B     RETIN         END OF DATA
         BAL   R9,PARMA
         B     REMORBID      INVALID PARM FIELDS
         AP    CNTMOV,P1     ACCEPT
         MVC   PCARD-1(4),STAMASK
         ED    PCARD-1(4),CNTMOV   SHOW CARD NUMBER
         MVC   VCARD+3(1),CUCO   COPY COUNTRY
         OI    VCARD+3,128
         L     R4,AORD#BEG
         LA    R5,34
REMIF    CLC   CUCO,ORDCO    SAME COUNTRY ?
         BE    REMFND        OK
REMIFI   LA    R4,ORDLEN(,R4)
         BCT   R5,REMIF
REMORBID MVC   CAMSG(L'MSBDORD),MSBDORD   BAD ORDER
         SPACE
REMORSE  BAL   R9,WARNER     PRINT IT
         B     RETIR         GET MORE INPUT
         DROP  R7
         SPACE
REMFND   CLI   ORDNLOC,0     ALREADY USED ?
         BNE   REMIFI        YES - TY FOR ANOTHER
         LA    R3,PARM1      DO PARM CHECKING
         LA    R4,NEWORD     USE DUMMY ENTRY
         XC    NEWORD,NEWORD
         ZAP   ORDPCON,P0
         ZAP   ORDPSUP,P0
         ZAP   ORDCNT,P0
         BAL   R9,ECHO
         MVC   PCARD,PCARD-1
         MVI   VCARD+4,C'+'  FIRST MESSAGE IS OVERPRINTED
         MVC   ORDCO,CUCO
         MVC   ORDSCO,CUCO
         MVI   TESTSFLG,0
         XC    CUCO+1(2),CUCO+1   CLEAR OTHER STUFF
         BAL   R9,TESTUNIT   LOOK FOR UNIT
         BAL   R9,TESTPROV   LOOK FOR PROVINCE
         B     REMORBID      TOO BAD
         BAL   R9,FINDPROV   LOOK FOR PROVINCES
         B     REMORBID      TOO BAD
         LA    R2,DB+1
         USING NORSECT,R6
REMFLOP  L     R6,AORD#BEG   GET ORDER TABLE
         LA    R7,34         MAX
REMFLOC  CLC   NORALO,0(R2)  SAME ?
         BE    REMFOF        YES - SEE IF USABLE
REMFLIN  LA    R6,ORDLEN(,R6) TRY NEXT ORDER
         BCT   R7,REMFLOC
REMFLIN2 LA    R2,1(,R2)
         CLI   0(R2),0
         BNE   REMFLOP       CHECK ALTERNATE NAME
         B     REMORBID
REMFOF   CLC   CUCO,ORDCO    SAME COUNTRY ?
         BNE   REMFLIN       NO, CHECK FOR OTHER NAME
         TM    NORORD,ORDMO   HAS THIS ONE BEEN ORDERED ALREADY ?
         BZ    REMFOFM       NO, ACCEPT IT
         CLI   DB+1,0        MORE THAN ONE ?
         BNE   REMFLIN2      YES, TRY FOR MORE
         MVC   CAMSG(L'MSBDODU),MSBDODU   DUPLICATE
         B     REMORSE
         SPACE
REMFOFM  CLI   NORALN,0      SINGLE CHOICE ONLY ?
         BE    REMFOFI       NO, CHECK IT
         MVC   NORORD,ORDMO  SET FOR MOVE
         LR    R4,R6         COPY
         B     REMDONE
REMFOFI  LA    R14,DCORDM    GET ORDER TABLE
         LA    R15,(DCORDR-DCORDM)/4+1  NO. TO CHECK
         LA    R2,2          ALSO CHECK FOR SUBSIDIARY ORDER (E.G. TO)
REMFOFO  CLC   1(3,R14),0(R3)  ORDER SPECIFIED ?
         BE    REMFOFF       YES
         LA    R14,4(,R14)
         BCT   R15,REMFOFO   TRY AGAIN
         B     *+12                SKIP OVER
REMFOFF  LA    R3,8(,R3)     SKIP OVER VALID ORDER
         BCT   R2,REMFOFO    TRY FOR SUBSIDIARY ORDER
         LR    R7,R6         SAVE OVER CHECKING
         BAL   R9,TESTUNIT   UNIT SUPPLIED ?
         BAL   R9,TESTPROV   DESTINATION SUPPLIED ?
         B     REMORBID      NO
         BAL   R9,FINDPROV
         B     REMORBID
         LR    R4,R7               USE ACTUAL ORDER FOR WORKINF IN
         LA    R1,DB+1
         MVC   MESS9,ORDPCON  GET RETREAT ENTRY
         L     R6,MESS9
         USING PROVINCE,R6
REMFOP   MVC   DB(1),0(R1)   COPY IT
         NI    DB,X'7F'
         LA    R2,PROLAND
         LA    R3,24
REMFOPL  CLI   0(R2),0
         BE    REMFOPI
         CLC   DB(1),0(R2)    THIS ONE ?
         BE    REMFOPF       OK
         XI    DB,X'80'
         CLC   DB(1),0(R2)    THIS ONE ?
         BE    REMFOPF       ACCEPT PROVISIONALLY
REMFOPI  LA    R2,1(,R2)
         BCT   R3,REMFOPL    TRY NEXT ONE
         LA    R1,1(,R1)
         CLI   0(R1),0
         BNE   REMFOP
         B     REMORBID
REMFOPF  MVC   ORDNLOC,0(R1)
         TM    ORDUN,PROFARM
         BO    REMDON0       YES - ZERO COAST
         TM    PROF,PROFTWO  TWO-COAST ?
         BZ    REMDON0       NO, ZERO
         CLI   PARM0+5,C'S'  SOUTH COAST SPECIFIED ?
         BNE   REMDON0       NO
         OI    ORDNLOC,X'80'
         B     REMDONE
REMDON0  NI    ORDNLOC,X'7F'   CLOBBER COAST
REMDONE  LA    R1,2          TRY TWICE ANYWAY
         MVC   CKUN,ORDUN    COPY UNIT
         MVC   CKLOC,ORDLOC  COPY CURRENT LOCATION
         MVC   CKNLOC,ORDNLOC  COPY NEW LOCATION
REMFOL   BAL   R9,CHECKMOV   MOVE POSSIBLE ?
         B     *+8           NO
         B     REMFMO        YES, ACCEPT
         TM    ORDUN,PROFNAV FLEET ?
         BZ    REMFBAD       TOO BAD
         XI    CKNLOC,X'80'  FLIP IT
         BCT   R1,REMFOL     TRY OTHER WAY
         B     REMFBAD
REMFMO   MVC   ORDNLOC,CKNLOC  SAVE NEW LOCATION
         MVI   ORDORD,ORDMO  MOVE
         BAL   R9,FORABS     MAKE ABSOLUTE VALUES ALSO
         MVC   ORDCNT,CNTMOV
         MVC   CAMSG(8),=C'ACCEPTED'
         B     REMORSE       TELL ALL ABOUT IT
         SPACE
REMFBAD  MVI   ORDORD,0      RESET TEMP INFO
         MVI   ORDNLOC,0
         B     REMORBID      BAD ORDER
         EJECT
RETAN    LA    R1,VRETRES    GET REOLUTION MESS
         CALL  XPRNT
RETANH   BAL   R9,ORDURE
         BAL   R9,CONTORD
         TM    READ,RFULL
         BO    RETAN1
         MVC   CARD(L'DCENDORD),DCENDORD
         OI    READ,RFULL
RETAN1   CLI   RETCODE+1,4
         BH    CONTT         YES - NO FURTHER PROCESSING
         OI    FLAG,STAT+SAVE   CHECKPOINT AND SHOW
         L     R4,AORD#BEG
         LH    R5,ORDCOUNT
         MVI   MAXSFG,0
RETAL    CLI   ORDLOC,0      LAST ORDER DONE ?
         BE    RETALL        YES
         CLI   ORDNLOC,0     EMPTY ?
         BE    RETAF         YES, NO CHECK
         CLI   ORDLOC+ORDLEN,0
         BE    RETAF         SKIP IF LAST
         LA    R6,ORDLEN(,R4)   SCAN FOR CONFLICTS
         LR    R7,R5
         USING NORSECT,R6
RETAC    CLC   ORDALN,NORALN
         BNE   RETACI
         OI    ORDFP,ORDCONA   CANCEL
         MVI   ORDNLOC,0     CLEAR THIS ONE ALSO
         OI    NORFP,ORDCONA
RETACI   LA    R6,ORDLEN(,R6)
         BCT   R7,RETAC      DO ALL FOLLOWING ONES
         BAL   R9,FORABS     REDO THIS ONE
RETAF    BAL   R9,FORMAORD   DO THIS ORDER
         TM    ORDORD,ORDRET   DEFAULTED ?
         BZ    RETAFO        NO
         MVC   AORDMSG(10),DCBYDFLT    SHOW WHY - DEFAULT
RETAFO   CLI   ORDNLOC,0     ANNIHILATED ?
         BNE   RETAFN        NO
         MVC   AORDACT(L'DCONMAN),DCONMAN   ANN
         B     RETAP         NOW PRINT IT
RETAFS   MVC   AORDMSG(L'DCONSTAN),DCONSTAN
         MVI   AORDMSG+L'DCONSTAN,C' '
         B     RETAP
RETAFN   TM    ORDORD,ORDRET   DEFAULT ?
         BZ    RETAFM        NO
         LA    R1,1          DO ONLY ONCE FOR AN ARMY
         MVC   CKUN,ORDUN    COPY UNIT
         MVC   CKLOC,ORDLOC  COPY LOCATION
         MVC   CKNLOC,ORDNLOC
         TM    ORDUN,PROFARM ARMY ?
         BO    RETAFL        OK
         LA    R1,2
RETAFL   BAL   R9,CHECKMOV   MAKE SURE OF COAST
         B     *+8           NO
         B     RETAFP        OK - SET IT
         XI    CKNLOC,X'80'
         BCT   R1,RETAFL
RETAFP   MVC   ORDNLOC,CKNLOC   SET IT BACK
RETAFM   IC    R6,ORDNLOC
         BAL   R9,ADPROV
         USING PROVINCE,R6
         TM    PROF,PROFARM+PROFNAV   ALREADY OCCUPIED ?
         BNZ   RETAFS        YES - ERROR IN CODE
         OC    PROF,ORDUN
         TM    PROF,PROFTWO+PROFNAV
         BNO   RETAPC        OK - NO COAST
         TM    ORDNLOC,X'80'   SOUTH ?
         BZ    RETAPC        NO
         OI    PROF,PROFSOU
RETAPC   MVC   PROCC,ORDCO
         CLI   DATE,C'S'     SPRING ?
         BE    RETAP         YES - PRINT
         MVC   PROSC,ORDCO   OCCUPIES ALSO
RETAP    MVI   VAORD+4,C' '
         CLC   MAXSFG,ORDCO  SAME COUNTRY ?
         BE    RETAPP        YES
         MVC   MAXSFG,ORDCO
         MVI   VAORD+4,C'0'
RETAPP   LA    R1,VAORD
         CALL  XPRNT
         LA    R4,ORDLEN(,R4)
         BCT   R5,RETAL
         SPACE 2
RETALL   NI    NEWPLAY,255-PRET   TURN OFF RETREATS
         L     R12,=A(DIPLMOV2)
         B     MOVRETN-DIPLMOV2(,R12)  GO BACK FOR BUILD CHECK
         SPACE 2
MSAUTNO  WTO   '0***** ON YOUR NEXT PLAY SUBMIT RETREATS *****',       *
               MF=L
MSAUTOK  WTO   '1 AUTOMATIC RETREAT RESOLUTION',MF=L
VRETRES  WTO   '1 RETREAT RESOLUTION',MF=L
         SPACE 2
         LTORG
         TITLE 'DIPLOMACY --- COMMON SUBROUTINES'
DIPLSUBS CSECT
         USING *,R11
         SPACE
OFFMESS  ST    R9,MESS9      SAVE RETURN POINT
         LA    R6,VOMESS     SET HEADER
         B     OMESS
         SPACE
MESSAGE  ST    R9,MESS9      SAVE RETURN POINT
         LA    R6,VMESS      SET NORMAL HEADER
OMESS    XC    DB,DB         ZERO DESTINATION COUNTRY
         LA    R4,PARM2      START SCAN FOR DESTINATION NATION, IF ANY
         LA    R5,(PARME-PARM2)/8
MESSP    CLI   0(R4),C' '    PARM SUPPLIED ?
         BE    MESSPFX       NO, GIVE MESSAGE HEADER
         LA    R1,STATAB     GET TABLE OF STATISTICS AND NATIONS
         LA    R2,7          NO.
         USING STASECT,R1    DECLARE IT
MESSPC   CLC   0(3,R4),STASNAT  SAME NATION "
         BE    MESSFND       YES, USE IT
         CLC   =C'FROM ',0(R4)    NO 'TO' ?
         BNE   MESSP2
         MVI   DB+1,C'F'     SHOW 'FROM'
         CLI   DB,0
         BNE   MESSP2
         MVI   DB,X'FF'
MESSP2   LA    R1,STASLEN(,R1)
         BCT   R2,MESSPC     TRY AGAIN
         LA    R4,8(,R4)     NEXT PARM
         BCT   R5,MESSP
         B     MESSPFX       GIVE GENERAL MESSAGE
MESSFND  CLI   DB+1,C'F'     'FROM' FOUND ?
         BE    MESSFRM       YES, SHOW 'FROM' NATION
         OC    DB(1),STABIT  COPY NATION'S ROUTING BIT
         B     MESSP2        TRY AGAIN
         DROP  R1
MESSFRM  MVC   VMESS1+24(7),0(R4)   COPY 'FROM' NATION
         LA    R6,VMESS1
MESSPFX  MVC   3(1,R6),DB    COPY ROUTING BITS
         LR    R1,R6         GET MESSAGE ADDRESS
         CALL  XPRNT         HEADER
MESSLOOP BAL   R9,GETCARD    GET A CARD
         B     MESSEX        END OF MESSAGES
         MVC   VCARD+3(1),DB  COPY ROUTING
         BAL   R9,ECHO       PRINT IT
         B     MESSLOOP      AND DO FOR NEXT ONE
         SPACE
MESSEX   L     R9,MESS9      GET RETURN ADDRESS
         BR    R9            AND EXIT
         EJECT
*        FORMAT OUTPUT FIELDS
*
         USING STASECT,R4
         USING PROVINCE,R6
         USING STAMCOUN,R1
FORMCOUN MVC   STAMCOUN,STASNAT   COPY NATION
         LA    R1,STAMUNIT   SKIP TO UNIT FORMATTING
         USING STAMUNIT,R1
         SPACE
FORMUNIT TM    PROF,PROFNAV  NAVY ON IT ?
         BZ    FORMAR        NO
         MVC   STAMUNIT(L'STAF),STAF   SHOW FLEET
FORMAR   TM    PROF,PROFARM  ARMY ?
         BZ    FORMUX        NO, SET FOR PROVINCE
         MVC   STAMUNIT(L'STAA),STAA  SHOW ARMY
FORMUX   LA    R1,STAMPROV   SET FOR PROVINCE FIELD
         USING STAMPROV,R1
         SPACE
FORMPROV MVC   STAMCOAS,BLANKS
         MVC   STAMPROV,PRONAME   PROVINCE NAME
         TM    PROF,PROFTWO+PROFNAV
         BNOR  R9            NOT FLEET WITH 2 COATS
         MVC   STAMCOAS,STACOS
         TM    PROF,PROFSOU  ON SOUTH ?
         BOR   R9            YES, RETURN
         MVI   STAMCOAS+1,C'N'   MAKE NORTH COAST
         CLI   PRONAME,C'B'  IS IT BULGARIA ?
         BNER  R9            NO, OK
         MVI   STAMCOAS+1,C'E'  CHANGE TO EAST COAST
         BR    R9            RETURN
         DROP  R1,R4,R6
         EJECT
PARMA    STM   R1,R9,PARMSAVE   STORE OVER PRINT
         ST    R9,PARMBACK   SAVE RETURN ADDR.
         MVI   PARMBACK+7,4    SET FOR GOOD RETURN
         MVI   PARM1,C' '    CLEAR PARM FIELDS
         MVC   PARM1+1(PARME-PARM1-1),PARM1   CLEAR ALL
         LA    R3,CARD
         LA    R4,80
         LA    R5,PARM1-L'PARM1
         LA    R6,(PARME-PARM1)/L'PARM1+1
PARB     CLI   0(R3),C' '
         BNE   PARMB
         LA    R3,1(,R3)     SKIP SEP. BLANKS
         SH    R4,H1
         BP    PARB
PAREX    LM    R1,R9,PARMSAVE
         LM    R14,R15,PARMBACK
         B     0(R14,R15)    RETURN
         SPACE
PARMB    CLI   0(R3),C','    COMMA ?
         BE    PARB          YES, SKIP OVER
         LA    R5,L'PARM1(,R5)
         BCT   R6,PARMP      ROOM FOR ANOTHER ?
         MVC   CAMSG(L'MSEXP),MSEXP   TELL PLAYER TOO MANY
         RETCH 4
PARREXM  BAL   R9,ECHO
PAREX0   MVI   PARMBACK+7,0    SET FOR BAD EXIT
         B     PAREX
         SPACE
PARMP    LR    R7,R5
         LA    R8,8
PARMC    MVC   0(1,R7),0(R3)   COPY A BYTE
         LA    R3,1(,R3)     NEXT I/P
         LA    R7,1(,R7)     NEXT O/P
         SH    R4,H1
         BNP   PAREX         END OF CARD
         CLI   0(R3),C' '    TRAIL. BLANK ?
         BE    PARMCE
         CLI   0(R3),C','    SEP. COMMA ?
         BE    PARMCC        GO TO COMMA SKIP
         BCT   R8,PARMC      TRY AGAIN, IF ROOM
         MVC   CAMSG+10(L'MSEXPL),MSEXPL   TOO LONG
         MVC   CAMSG(8),0(R5)  START OF TOO LONG
         BAL   R9,ECHO
         MVC   CAMSG,CAMSG-1
         RETCH 4             MINOR KVETCH
PARMLC   LA    R3,1(,R3)     SKIP BAD TRAILING INFO
         SH    R4,H1
         BNP   PAREX
         CLI   0(R3),C' '
         BE    PARMCE
         CLI   0(R3),C','
         BNE   PARMLC        KEEP SKIPPING
PARMCC   LA    R3,1(,R3)     SKIP COMMA
         SH    R4,H1
PARMCE   B     PARB
         SPACE
PARMSAVE DS    9F
         EJECT
GETCARD  ST    R9,GETBACK
         TM    READ,REOF     EOF ?
         BZ    *+10          NO, SKIP
         MVC   CARD(5),=C'$END'
         TM    READ,REOF+REND+RCON   ABNORMAL ENTRY ?
         BNO   GETCA         NO, OK
         OI    FLAG,SEND     FORCE END
         L     R12,=A(DIPLOMAT)
         B     CONTROL-DIPLOMAT(,R12)  RETURN TO PROCESS FORCED END
GETCA    TM    READ,REOF+RCON  INVALID RECURSION ?
         BNO   GETCD         N
         OI    READ,REND     FORCE ABNORMAL EXIT IF RECURSION AGAIN
GETCD    TM    READ,REOF+RFULL  EOF OR BUFFER PRELOADED ?
         BNZ   GETCG         YES, SKIP READ
         MVC   PCARD,PCARD-1   CLEAR CARD IMAGE
         MVI   VCARD+3,0     CLAER ROUTCDE
         GET   SYSIN,CARD    GET IT
GETCG    NI    READ,255-RFULL   TURN OFF FULL
         LA    R1,CARD       SEE IF CONTROL CARD
         LA    R14,80        SET FOR MAX LENGTH
GETCL    CLI   0(R1),C' '
         BNE   GETCB
         LA    R1,1(,R1)
         BCT   R14,GETCL
GETCB    CLI   0(R1),C'$'    CONTROL CARD ?
         BE    GETCC         YES
         TM    READ,RCON
         BZ    GETCD4
         TM    READ,REOF
         BO    GETCD0        TOO BAD
         MVC   CAMSG(L'MSCONN),MSCONN
         BAL   R9,ECHO
         RETCH 8
         B     GETCD
GETCD0   L     R9,GETBACK
         BR    R9            RETURN EOF / CONTROL MODE SWITCH
GETCD4   TM    READ,REOF
         BO    GETCD0        FORCE SWITCH
         NI    READ,255-RCON   TURN OFF CONTROL REQUEST
         L     R9,GETBACK
         B     4(,R9)
         SPACE
GETCC    TM    READ,RCON     CONTROL CARD REQUT ?
         BO    GETCD4        YES, OK
         OI    READ,RFULL    END OF DATA MODE, SET FOR SWITCH TO CONTRO
         B     GETCD0
         SPACE 2
ENDSYN   MVC   CAMSG-40(L'EOFMSG),EOFMSG
         BAL   R9,ECHO
ENDEND   OI    READ,REOF     SET END-FILE READ SWITCH
         MVC   CARD(5),=C'$END'
         B     GETCG         PROCESS
         SPACE 2
ECHO     LA    R1,VCARD
         CALL  XPRNT
         BR    R9
         EJECT
         USING ORDSECT,R4
         USING PROVINCE,R6
FINDPROV XC    DB,DB
         LA    R2,DB+1       FIRST O/P FIELD
         MVI   FINDPCLC+1,2  SET FOR LENGTH OF THREE BYTES IN NAME
         L     R6,APRO#BEG   GET FIRST PROVINCE POINTER
         CLI   PARM0+3,C' '  THREE OR FOUR ?
         BE    FINDPCLC      THREE, USE AS IS
         MVI   FINDPCLC+1,3  SET FOR FOUR
FINDPCLC CLC   PARM0(0),PRONAME   SAME PROVINCE NAME ?
         BNE   FINDPINC      NO, TRY NEXT
         MVC   0(1,R2),PRO#  COPY NUMBER
         LA    R2,1(,R2)     NEXT O/P BYTE
         CLI   PARM0+3,C' '  DONE ?
         BNE   4(,R9)        OK IF FOUR BYTE NAME
FINDPINC LA    R6,PROLEN(,R6)   NEXT PROV.
         C     R6,APRO#END   END ?
         BL    FINDPCLC      NO, TRY AGAIQN
         CLI   DB+1,0        ANY FOUND ?
         BNE   4(,R9)        NO, GIVE OK RETURN
         BR    R9            TOO BAD
         SPACE 2
TESTUNIT CLC   =C'F ',0(R3)  FLEET ABBR. ?
         BE    TESTUF        YES
         CLC   =C'FLEET ',0(R3)
         BE    TESTUF        YES
         CLC   =C'A ',0(R3)   ARMY ?
         BE    TESTUA
         CLC   STAA,0(R3)    'ARMY ' ?
         BE    TESTUA
         TM    TESTSFLG,TST1+TST2
         BZR   R9
         MVC   CAMSG(L'MSBDUNM),MSBDUNM
         B     WARNER
TESTUA   OI    CUUN,PROFARM  SHOW ARMY
         B     TESTUFA
TESTUF   OI    CUUN,PROFNAV  SET NAVY
TESTUFA  LA    R3,8(,R3)     POINT TO NEXT PARM
         BR    R9
         SPACE 2
TESTPROV MVC   PARM0,BLANKS
         LA    R2,2          SET FOR THREE BYTE NAME
         CLI   3(R3),C' '    IS IT THREE ?
         BE    TESTPRO       YES
         CLI   3(R3),C'-'    COAST ?
         BE    TESTPRO       YES, AND THREE
         LA    R2,3          SET FOR FOUR BYTE PROV NAME
         CLI   4(R3),C' '    IS IT FOUR ?
         BE    TESTPRO       YES
         CLI   4(R3),C'-'    WITH COAST ?
         BNER  R9            NO, BAD RETURN
TESTPRO  EX    R2,TESTPMVC   MOVE TO PARM
         LA    R15,1(R2,R3)
         MVC   PARM0+4(3),0(R15)   MOVE COAST OR BLANKS
         LA    R3,8(,R3)
         B     4(,R9)        TAKE GOOD REUTRN
         SPACE 2
FINDORD  LA    R14,DCORDM    GET 'MOVE' ORDER TABLE
         LA    R15,DCORDME   AND END
         TM    TESTSFLG,TST2
         BZ    *+8
         LA    R14,DCORDM2
         TM    PLAY,PMOVE    PROCESSING MOVES ?
         BO    FINDOL
         LA    R14,DCORDB    SET FOR BUILD
         LA    R15,DCORDBE
         TM    PLAY,PBUIL    OR BUILDS ?
         BO    FINDOL
         LA    R14,DCORDR    RETREAT
         LR    R15,R14
FINDOL   CLC   0(3,R3),1(R14)   MATCH ?
         BE    FINDOLF       OK
         LA    R14,4(R14)
         CR    R14,R15
         BL    FINDOL        TRY NEXT
         CLI   0(R3),C' '    OMITTED PARM ?
         BNER  R9            NO - IT IS BAD
         LA    R14,DCORDM2   USE 'STANDS' AS DEFAULT
FINDOLF  LA    R3,8(,R3)     BUMP TO NEXT PARM
         TM    TESTSFLG,TST2   SECONDARY ?
         BO    FINDOLF2
         MVC   ORDORD,0(R14)   COPY ORDER
         B     FINDOLAH      GO TO LOOK-AHEAD
FINDOLF2 MVC   ORDSORD,0(R14)
FINDOLAH LA    R14,DCORDMX   GET TABLE OF 'TO' AND 'AT'
         LA    R15,(DCORDME-DCORDMX)/4   NO.
FINDOLNX CLC   0(3,R3),1(R14)  IF 'MOVE TO' OR 'STAND AT' FORM
         BE    FINDOLXI      THEN SKIP NEXT VERB ALSO
         LA    R14,4(,R14)
         BCT   R15,FINDOLNX
         B     4(,R9)
FINDOLXI LA    R3,8(,R3)     SKIP NEXT VERB
         B     4(,R9)
         SPACE 2
FINDNAT  LA    R14,STATAB
         LA    R15,7
         USING STASECT,R14
FINDNAL  CLC   0(3,R3),STASNAT
         BE    FINDNAM
         LA    R14,STASLEN(,R14)
         BCT   R15,FINDNAL
         BR    R9
FINDNAM  MVC   ORDSCO,STABIT
         LA    R3,8(,R3)     BUMP TO NEXT INPUT PARM
         B     4(,R9)
         DROP  R14
         SPACE 2
SCREAM   RETCH 8
         SPACE 2
WARNER   ST    R9,WARN9
         SPACE
WARERR   BAL   R9,ECHO
         MVI   VCARD+4,C' '
         MVC   PCARD,PCARD-1
         L     R9,WARN9
         BR    R9
         SPACE 2
         USING PROVINCE,R6
CHECKSUP STM   R1,R6,24(R13)   CHECK SUPPORT LOCATIONS
         IC    R6,CKLOC      GET FROM LOCATION
         LA    R15,127       GET MASK
         NR    R6,R15        IGNORE COAST, IF ANY
         MH    R6,=AL2(PROLEN)  MAKE OFFSET
         SH    R6,=AL2(PROLEN)
         BM    CHECKMBD      BAD OFFSET
         A     R6,APRO#BEG   FIRST ONE
         IC    R3,CKNLOC     GET NEW LOCATION
         NR    R3,R15        IGNORE COAST
         LA    R2,L'PROSEA   LENGTH
         LA    R1,PROLAND    SET FOR ARMY
         TM    CKUN,PROFARM  ARMY REQUESTED ?
         BO    CHECKSLP      YES, DO IT
         LA    R1,PROSEA     SET FOR NAVY
CHECKSLP IC    R4,0(,R1)     GET POSSIBLE DESTINATION
         NR    R4,R15        MASK COAST
         CR    R3,R4         SAME ?
         BE    CHECKMEX      YES, ACCEPT
         LA    R1,1(,R1)     TRY AGAIN
         BCT   R2,CHECKSLP
         B     CHECKMBD      TOO BAD
         SPACE 2
         USING PROVINCE,R6
CHECKMOV STM   R1,R6,24(R13)
         LA    R15,127
         IC    R6,CKLOC
         NR    R6,R15
         MH    R6,=AL2(PROLEN)
         SH    R6,=AL2(PROLEN)
         BM    CHECKMBD
         A     R6,APRO#BEG
         LA    R2,L'PROSEA
         LA    R1,PROLAND
         TM    CKUN,PROFARM
         BO    CHECKMLP
         LA    R1,PROSEA
         TM    PROF,PROFTWO
         BZ    CHECKMLP
         LA    R2,L'PROSOU
         TM    CKLOC,X'80'   SOUTH COAST ?
         BZ    CHECKMLP
         LA    R1,PROSOU
CHECKMLP CLC   CKNLOC,0(R1)
         BE    CHECKMEX
         LA    R1,1(,R1)
         BCT   R2,CHECKMLP
CHECKMBD LM    R1,R6,24(R13)
         BR    R9
CHECKMEX LM    R1,R6,24(R13)
         B     4(,R9)
         SPACE 2
DATECHEK EQU   *
         CLC   PARM2(4),FALL+1   FALL ?
         BNE   *+10
         MVC   PARM2(L'FALL),FALL  MOVE WITH LEADING ZERO
         CLC   DATE,PARM2    IS THE SEASON CORRECT ?
         BNE   BCONBT        NO, GIVE BAD TIME
         CLC   YEAR-2(4),PARM3  CORRECT YEAR ?
         BNE   BCONBT        NO, GIVE BAD TIME
         MVC   VSTOY,DATE
         MVI   VSTO+3,0      ALL ROUT
         MVC   VSTOT,PARM1
         LA    R1,VSTO       USE COMMON HEADER
         CALL  XPRNT
         MVC   NEWDATE,DATE
         MVI   NEWPLAY,PMSG
         ZAP   CNTMOV,P0     CLEAR ORDER NUMBER
         L     R6,APRO#BEG   GET FIRST PROVINCE
         USING PROVINCE,R6
         L     R4,AORD#BEG   GET START OF ORDER TABLE
         LA    R5,35         NO. OF ENTRIES + 1
         USING ORDSECT,R4
MOCL     XC    ORDSECT(ORDLEN),ORDSECT   CLEAR AN ENTRY
         ZAP   ORDPCON,P0    CLEAR
         ZAP   ORDCNT,P0     CLEAR ORDER NUMBER
         ZAP   ORDPSUP,P0
         LA    R4,ORDLEN(,R4) BUMP
         BCT   R5,MOCL       DO NEXT
         BR    R9
         EJECT
         USING PROVINCE,R6
         USING ORDSECT,R4
FORMAORD MVC   AORDER,AORDER-1
         CP    ORDCNT,P0           DEFAULTED ?
         BE    FORMAOR0            YES, SKIP NUMBER
         MVC   AORDNUMB,STAMASK
         ED    AORDNUMB,ORDCNT   SHOW ORDER NUMBER
FORMAOR0 ST    R9,PARMBACK         SAVE RETURN ADDR
         LA    R1,ORDCO
         LA    R2,SUPUNTAB
         BAL   R9,FORMACON+4
         MVC   AORDCOUN,1(R2)
         MVC   CKUN(2),ORDUN   MOVE UNIT/LOCATION
         LA    R1,CKUN       POINT TO IT
         CLI   ORDLOC,0      HAS IT BEEN DELETED ALREADY ?
         BNE   *+10          NO, LEAVE IT
         MVC   CKLOC,ORDALO  ELSE USE ABSOLUTE LOCATION
         LA    R2,AORDPUN
         BAL   R9,FORMALOC
         LA    R1,ORDORD
         BAL   R9,FORMANOR
         MVC   AORDACT,1(R2)
         LA    R8,AORDSCO          OUTPUT FIELD
         CLI   ORDORD,ORDMO  MOVE ?
         BL    FORMAORX      STAND - EXIT
         BE    FORMAORT      MOVE - SHOW DEST
         TM    ORDORD,ORDREM+ORDBUIL   BUILD/REMOVE ?
         BNZ   FORMAORX      YES, JUST EXIT
         TM    ORDORD,ORDRET   RETREAT ?
         BO    FORMAORT
         LA    R1,ORDSCO
         LA    R2,SUPUNTAB   GET ADJECTIVE TABLE
         BAL   R9,FORMACON+4   SKIP LOAD IN SUBR
         MVC   AORDSCO,1(R2)
         LA    R1,ORDSUN
         LA    R2,AORDSUN
         BAL   R9,FORMALOC
         LA    R1,ORDSORD
         BAL   R9,FORMANOR
         MVC   AORDSACT,1(R2)
         TM    ORDSORD,ORDST
         BO    FORMAORX
         LA    R8,AORDNLOC         OUTPUT FIELD
FORMAORT MVC   CKUN,ORDSUN
         MVC   CKLOC,ORDNLOC
         LA    R1,CKUN
         LA    R2,HEXTEXT
         MVC   HEXTEXT,HEXTEXT-1
         BAL   R9,FORMALOC
         MVC   0(L'AORDNLOC,R8),HEXTEXT+6
FORMAORX L     R9,PARMBACK
         BR    R9
         SPACE 2
FORMACON LA    R2,STATAB
         LA    R14,7
FORMACOL CLC   0(1,R1),0(R2)
         BER   R9
         LA    R2,14(,R2)
         BCT   R14,FORMACOL
         LA    R2,BLANKS
         BR    R9
         SPACE 2
FORMALOC TM    0(R1),PROFARM
         BZ    FORMALOF
         MVC   0(5,R2),STAA
         B     FORMALOP
FORMALOF TM    0(R1),PROFNAV
         BZ    FORMALOP
         MVC   0(5,R2),STAF
FORMALOP LA    R15,127
         IC    R6,1(,R1)
         NR    R6,R15
         MH    R6,=AL2(PROLEN)
         SH    R6,=AL2(PROLEN)
         BMR   R9
         A     R6,APRO#BEG
         MVC   AORDPLOC-AORDPUN(4,R2),PRONAME
         TM    0(R1),PROFNAV
         BZR   R9
         TM    PROF,PROFTWO
         BZR   R9
         MVC   AORDPLOC+4-AORDPUN(3,R2),STACOS
         TM    1(R1),X'80'   SOUTH ?
         BOR   R9            OK
         MVI   AORDPLOC-AORDPUN+5(R2),C'N'
         CLI   PRONAME,C'B'
         BNER  R9
         MVI   AORDPLOC+5-AORDPUN(R2),C'E'
         BR    R9
         SPACE 2
FORMANOR LA    R2,TABORD
         LA    R14,(TABORDE-TABORD)/9
FORMANOL CLC   0(1,R1),0(R2)
         BER   R9
         LA    R2,9(,R2)
         BCT   R14,FORMANOL
         LA    R2,BLANKS
         BR    R9
         SPACE 2
         USING ORDSECT,R4
SUPINIT  XC    DOMSAVE(4*34),DOMSAVE
         LA    R1,DOMSAVE+4
         ST    R1,DOMSAVP
         ST    R4,DOMSAVE
SUPSET4  MVI   SUPFG,0
         MVC   SUPCO,ORDCO
         MVC   SUPUN,ORDUN
         MVC   SUPLOC,ORDALO
         MVC   SUPNLOC,ORDALN
         BR    R9
         SPACE 2
         USING NORSECT,R6
SUPSET6  MVI   SUPFG,0
         MVC   SUPCO,NORCO
         MVC   SUPUN,NORUN
         MVC   SUPLOC,NORALO
         MVC   SUPNLOC,NORALN
         BR    R9
         SPACE 2
SUPADD   L     R1,DOMSAVP
         ST    R6,SUPWORK
         LA    R15,DOMSAVE
SUPADD1  CLC   1(3,R15),SUPWORK+1   ALREADY IN TABLE ?
         BER   R9            YES - SKIP
         LA    R15,4(,R15)   TRY AGAIN
         CR    R15,R1
         BL    SUPADD1
         ST    R6,0(,R15)
         LA    R1,4(,R1)
         ST    R1,DOMSAVP
         BR    R9
         SPACE 2
ORDURE   TM    FLAG,DEBUG    DEBUG MODE ?
         BZ    ORDURER       NO, SKIP PRINT
         LA    R1,VORDHEAD
         CALL  XPRNT
ORDURER  MVC   HEXTEXT,HEXTEXT-1   CLEAR LINE
         L     R4,AORD#BEG
         LA    R5,34
         XR    R6,R6
         USING ORDSECT,R4
ORDULOOP CLI   ORDCO,0       ANY ORDER ?
         BE    ORDUINC       NO, SKIP
         LA    R6,1(,R6)
         TM    FLAG,DEBUG    DEBUG MODE ?
         BZ    ORDUINC       NO, SKIP PRINT
         LA    R1,HEXTEXT+17  START INDENTED
         LA    R2,ORDSECT
         LA    R3,(ORDLEN+3)/4
ORDUHEX  UNPK  0(9,R1),0(5,R2)
         LA    R1,8(,R1)
         LA    R2,4(,R2)
         BCT   R3,ORDUHEX
         TR    HEXTEXT+17(2*ORDLEN),HEXTAB-C'0'  MAKE PRINTABLE
         MVC   HEXTEXT+17+2*ORDLEN(9),BLANKS     WIPE BEHIND
         MVC   HEXTEXT+1(4),HEXTEXT+17  MOVE ORDER NUMBER
         MVI   HEXTEXT+5,C' '          PLUS SEPARATOR
         MVC   HEXTEXT+6(2),HEXTEXT+21 CO
         MVI   HEXTEXT+8,C' '
         MVC   HEXTEXT+9(2),HEXTEXT+23 UN
         MVI   HEXTEXT+11,C' '
         MVC   HEXTEXT+12(2),HEXTEXT+25 LO
         MVI   HEXTEXT+14,C' '
         MVC   HEXTEXT+15(2),HEXTEXT+27 ORD
         MVI   HEXTEXT+17,C' '
         MVC   HEXTEXT+18(2),HEXTEXT+29 CO
         MVI   HEXTEXT+20,C' '
         MVC   HEXTEXT+21(2),HEXTEXT+31 UN
         MVI   HEXTEXT+23,C' '
         MVC   HEXTEXT+24(2),HEXTEXT+33 LO
         MVI   HEXTEXT+26,C' '
         MVC   HEXTEXT+27(2),HEXTEXT+35 SO
         MVI   HEXTEXT+29,C' '
         MVC   HEXTEXT+30(2),HEXTEXT+37
         MVI   HEXTEXT+32,C' '
         MVC   HEXTEXT+33(4),HEXTEXT+39
         MVI   HEXTEXT+37,C' '
         MVC   HEXTEXT+38(4),HEXTEXT+43
         MVI   HEXTEXT+42,C' '
         MVC   HEXTEXT+43(2),HEXTEXT+47
         MVI   HEXTEXT+45,C' '
         MVC   HEXTEXT+46(2),HEXTEXT+49
         MVI   HEXTEXT+48,C' '
         MVC   HEXTEXT+49(2),HEXTEXT+51
         MVI   HEXTEXT+51,C' '
         MVC   HEXTEXT+52(2),HEXTEXT+53
         MVI   HEXTEXT+54,C' '
         LA    R1,VORDHEXT
         CALL  XPRNT
ORDUINC  LA    R4,ORDLEN(,R4)
         BCT   R5,ORDULOOP
         STH   R6,ORDCOUNT
         MVC   HEXTEXT,HEXTEXT-1
         TM    FLAG,DEBUG    DEBUG MODE ?
         BZR   R9            NO, EXIT
         LA    R1,VORDHEXT
         CALL  XPRNT         MAKE A SPACER
         BR    R9
         SPACE 2
ACCZERO  ST    R9,ACC9       SAVE RETURN ADDRESS
         B     ACCDEL        GO TO DELETE
         SPACE
         USING PROVINCE,R6
         USING ORDSECT,R4
ACCORD   IC    R6,ORDNLOC
         ST    R9,ACC9
         BAL   R9,ADPROV
         TM    ORDORD,ORDSUP+ORDCON
         BNZ   ACCSTAN       CHANGE TO A STAND
         TM    ORDORD,ORDST
         BO    ACCOCC
         CLI   ORDLOC,0      PREVIOUSLY CLEARED OUT ?
         BE    ACCORD1       YES, SKIP DELETE
         IC    R6,ORDLOC
         BAL   R9,ADPROV
         NI    PROF,255-PROFARM-PROFNAV-PROFSOU
         MVI   PROCC,0       JUST IN CASE
         IC    R6,ORDNLOC
         BAL   R9,ADPROV
ACCORD1  TM    PROF,PROFARM+PROFNAV
         BZ    ACCMOVE
         XC    HEXTEXT,HEXTEXT
         LA    R1,PROLAND
         LA    R2,L'PROLAND
         TM    PROF,PROFARM
         BO    ACCRET1
         LA    R1,PROSEA
         TM    PROF,PROFTWO
         BZ    ACCRET1
         LA    R2,L'PROSOU
         TM    PROF,PROFSOU
         BZ    ACCRET1
         LA    R1,PROSOU
ACCRET1  MVC   CKLOC,ORDLOC
         NI    CKLOC,X'7F'
         LA    R14,PROLAND
         LA    R3,HEXTEXT(R1)
         SR    R3,R14
ACCRET2  MVC   CKUN,0(R1)    COPY ORIGINAL LOC
         NI    CKUN,X'7F'    CLOBBER COAST
         BZ    ACCRET4
         CLC   CKUN,CKLOC    ATTACK ADDRESS ?
         BE    ACCRET4       YES - SKIP IT
         MVC   0(1,R3),0(R1)   ELSE COPY IT
ACCRET3  LA    R3,1(,R3)
ACCRET4  LA    R1,1(,R1)
         BCT   R2,ACCRET2
         L     R1,AORD#BEG
         LA    R2,34         DO ALL, JUST IN CASE
         USING NORSECT,R1
ACCDOR1  CR    R6,R1         THIS ONE ?
         BE    ACCDOR2       YES
         CLC   ORDALN,NORALO   STOOD AT THAT LOCATION ?
         BE    ACCDOR5       YES, WIPE IT OUT
         TM    ORDFP,ORDCON+ORDCONA   WAS THIS A SUCCESSFUL CONVOY ?
         BNO   ACCDOR2       NO
         CLC   ORDALN,NORALN  CONVOYED TO THIS LOCATION ?
         BNE   ACCDOR2       NO
         TM    NORUN,PROFARM  MAKE SURE IT'S AN ARMY
         BO    ACCDOR3       YES, PROCESS
ACCDOR2  LA    R1,ORDLEN(,R1) TRY AGAIN
         BCT   R2,ACCDOR1
         B     ACCRETO       NOT FOUND, IGNORE
ACCDOR3  CLI   NORLOC,0      ALREADY DELETED ?
         BE    ACCDOR4       YES, SKIP DELETE
         IC    R6,NORLOC     GET OLD LOCATION
         BAL   R9,ADPROV
         NI    PROF,255-PROFSOU-PROFARM-PROFNAV  DELETE IT
         MVI   PROCC,0       SHOW NO OCCUPATION
ACCDOR4  IC    R6,ORDALN     GET NEW LOCATION
         BAL   R9,ADPROV
         MVC   NEWPRO,0(R6)
         LA    R6,NEWPRO     USE DUMMY ENTRY
         MVC   PROCC,NORCO   SHOW THIS COUNTRY
         NI    PROF,255-PROFSOU-PROFARM-PROFNAV  DELETE IT
         OC    PROF,NORUN    SHOW THIS FORCE
ACCDOR5  TM    NORORD-NORSECT(R1),ORDMO   WAS OLD ORDER A MOVE ?
         BZ    ACCDOR6       NO, DELETE IT
         MVI   NORLOC-NORSECT(R1),0  YES - CLEAR IT OUT
         B     ACCMOVE       BUT KEEP THE ORDER IN
ACCDOR6  XC    0(ORDLEN,R1),0(R1)   DELETE OLD ORDER
         DROP  R1
ACCRETO  MVC   AORDER,AORDER-1   CLEAR ENTRY
         LA    R1,PROCC
         LA    R2,SUPUNTAB
         BAL   R9,FORMACON+4
         MVC   AORDSCO,1(R2)
         LA    R1,CKUN
         LA    R2,AORDSUN
         MVC   CKUN,PROF
         NI    CKUN,PROFARM+PROFNAV
         MVC   CKLOC,PRO#
         TM    PROF,PROFSOU
         BZ    *+8
         OI    CKLOC,X'80'
         BAL   R9,FORMALOC
         MVC   AORDSACT(L'DCONMRT),DCONMRT
         MVC   DB(1),PROCC   GET THE OLD COUNTRY
         NC    DB(1),DIDMOVE   SIPPLIED ANY ORDERS ?
         BNZ   ACCRETP       YES - PRINT
         MVC   AORDSACT(L'DCONMAN),DCONMAN  ELSE SET ANNIHILATED
ACCRETP  LA    R1,VAORD
         CALL  XPRNT
         CLC   DCONMAN,AORDSACT   ANNIHILATED ?
         BE    ACCMOVE
         L     R1,ARET#BEG
         LA    R2,33         SCAN THROUGH ONE LESS - USE LAST ?
ACCREN   CLI   0(R1),0
         BNE   ACCRENI       SKIP IF USED
         MVC   0(PROLAND-PROVINCE,R1),PROVINCE   COPY PROVINCE INFO
         MVC   PROLAND-PROVINCE(2*L'PROLAND,R1),HEXTEXT  + RETREAT LOC
         B     ACCMOVE
ACCRENI  LA    R1,PROLEN(,R1)
         BCT   R2,ACCREN
ACCMOVE  IC    R6,ORDALN     GET PROVINCE BACK
         BAL   R9,ADPROV
         NI    PROF,255-PROFSOU-PROFNAV-PROFARM
         MVC   PROCC,ORDCO
         NI    ORDUN,PROFARM+PROFNAV
         OC    PROF,ORDUN
         TM    PROF,PROFTWO+PROFNAV
         BZ    ACCOCC
         TM    ORDNLOC,X'80'
         BZ    ACCOCC
         OI    PROF,PROFSOU
ACCOCC   CLI   NEWDATE,C'S'   SPRING ?
         BE    ACCSTAN       YES, SEE IF STAND OR DELETE
         MVC   PROSC,PROCC   SHOW OWNERSHIP ALSO
ACCSTAN  TM    ORDORD,ORDMO  MOVE ORDER ?
         BO    ACCDEL        YES - DELETE IT
         TM    ORDFP,ORDST   ALREADY PROCESSED BEFORE ?
         BO    ACCDEL        YES - DELETE IT
         BAL   R9,FORCEST    FORCE A STAND
         OI    ORDFP,ORDST   SHOW FORCED STAND
         NI    ORDFP,255-ORDSUP   TURN OFF ATTACKED SUPPORT
         B     ACCDCOM
ACCDEL   XC    ORDSECT(ORDLEN),ORDSECT
         ZAP   ORDCNT,P0
         ZAP   ORDPCON,P0
         ZAP   ORDPSUP,P0
ACCDCOM  MVI   OLDCOUNT,X'7F'   FORCE ANOTHER ITERATION
         L     R9,ACC9
         BR    R9
         SPACE 2
CONTORD  L     R4,AORD#BEG
         LR    R6,R4
         LA    R7,34*ORDLEN(,R4)
         LA    R5,34
CONTORT  CLI   ORDORD,0      EMPTY ?
         BE    CONTMOV       YERS
         LA    R4,ORDLEN(,R4)
         CR    R6,R4
         BH    *+6
         LR    R6,R4
         BCT   R5,CONTORT
         BR    R9
CONTMOV  LA    R6,ORDLEN(,R6)
         CR    R6,R7         DONE ?
         BH    CONTFILL
         MVC   ORDSECT(ORDLEN),0(R6)
         MVC   0(ORDLEN,R6),0(R7)   AND WIPE OUT THE ONE BEING COPIED
         B     CONTORT
CONTFILL MVC   ORDSECT(ORDLEN),0(R7)
         LA    R4,ORDLEN(,R4)
         BCT   R5,CONTFILL
         BR    R9
         SPACE 2
FORCEST  CLI   ORDLOC,0      PARTIALLY DISLODGED UNIT ?
         BE    FORCERET       YES - FORCED RETREAT INSTEAD
         MVC   ORDNLOC,ORDLOC
         MVI   ORDORD,ORDST  FORCE UNIT TO STAND
         MVC   ORDSCO(ORDNLOC-ORDSCO),ORDCO   COPY
         NI    ORDFP,255-ORDCON-ORDCONA  CLEAR CONVOY FLAGS
FORABS   MVC   ORDALO,ORDLOC
         MVC   ORDALS,ORDSLOC
         MVC   ORDALN,ORDNLOC
         NC    ORDALO(3),=3X'7F'   MAKE ONE COAST
         MVI   OLDCOUNT,X'7F'   FORCE ANOTHER ITERATION
         BR    R9            UNIT FORCED TO STAND
         SPACE
FORCESTS MVI   ORDSORD,ORDST
         MVC   ORDNLOC,ORDSLOC
         B     FORABS
         SPACE 2
FORCERET ST    R9,ACC9       PARTIALLY MOVED UNIT MUST RETREAT
         MVC   AORDER,AORDER-1   CLEAR PRINT LINE
         LA    R1,ORDCO            GET COUNTRY
         LA    R2,SUPUNTAB
         BAL   R9,FORMACON+4   FORMAT COUNTY
         MVC   AORDSCO,1(R2)  IDENTIFY COUNTRY
         LA    R1,CKUN             GET UNIT
         LA    R2,AORDSUN      OUTPUT
         MVC   CKUN,ORDUN    COPY UNIT
         MVC   CKLOC,ORDALO   COPY LOCATION
         TM    ORDFP,ORDCON+ORDCONA  SUCCESSFULLY CONVOYED UNIT ?
         BNO   *+10          NO
         MVC   CKLOC,ORDALN  YES; NAME BY NEW LOCATION
         ST    R6,0(,R13)    SAVE
         BAL   R9,FORMALOC   FORMAT THE LOCATION
         L     R6,0(,R13)    SAVE  AND RESTORE
         MVC   AORDSACT(L'DCONMRT),DCONMRT   SHOW RETREATS
         MVC   DB(1),ORDCO   GET COUNTRY
         NC    DB(1),DIDMOVE   DID COUNTRY SUPPLY A MOVE ?
         BNZ   FORRETP       YES, RETREAT OK
         MVC   AORDSACT(L'DCONMAN),DCONMAN   ELSE SHOW ANNIHILATED
FORRETP  LA    R1,VAORD      GET THE ORDER
         CALL  XPRNT               ND PRINT IT
         CLC   DCONMAN,AORDSACT    ANNI ?
         BE    ACCDEL        YES - DELETE THE ORDER
         L     R1,ARET#BEG   ELSE GET THE RETREAT TABLE
         LA    R2,33               AND LOOK FOR AN EMPTY SLOT
FORREN   CLI   0(R1),0       AVAILABLE ?
         BE    FORRENT        YES - USE IT
         LA    R1,PROLEN(,R1)   GET NEXT
         BCT   R2,FORREN     RRY AGAIN
FORRENT  STM   R1,R8,24(R13)    SAVE ALL
         TM    ORDFP,ORDCON+ORDCONA   SUCCESSFULLY CONVOYED ARMY ?
         BNO   FORROLD       NO
         MVC   ORDALO,ORDALN    FINAGLE
         MVC   ORDSLOC,ORDNLOC
FORROLD  IC    R6,ORDALO     GET RETREATING FROM ADDRESS
         BAL   R9,ADPROV     GET ORIGINAL PROVINCE
         USING PROVINCE,R6
         MVC   0(PROLAND-PROVINCE,R1),PROVINCE    MOVE BASIC INFO
         XC    PROLAND-PROVINCE(2*L'PROLAND,R1),PROLAND-PROVINCE(R1)
         LR    R7,R6               SHIFT
         LR    R6,R1               OVER
         NI    PROF,255-PROFARM-PROFNAV-PROFSOU    CLEAR OUT
         MVC   PROCC,ORDCO   MAKE IT LOOK LIKE ORIGINAL ORDER
         OC    PROF,ORDUN    UNIT
         MVC   CKLOC,RETLOC   GET THE RETREAT LOCATION
         NI    CKLOC,X'7F'   CLOBBER COAST
         LA    R14,PROLAND   GET FIRST O/P FIELD
         LA    R1,PROLAND-PROVINCE(,R7)   GET ARMY INPUT FIELD
         LA    R15,L'PROLAND    NO. OF ENTRIES
         TM    PROF,PROFARM   ARMY ?
         BO    FORREL              YES, PROCESS
         LA    R1,L'PROLAND(,R1)   POINT TO SEA
         TM    PROF,PROFTWO   DUAL-COAST ?
         BZ    FORREL              NO
         LA    R15,L'PROSOU   USE HALF AN ENTRY ONLY
         TM    ORDSLOC,X'80'   ASSUME WAS A MOVE ORDER - GET COAST
         BZ    FORREL              AND PROCESS
         OI    PROF,PROFSOU   SET SOUTH COAST
         LA    R1,L'PROSOU(,R1)    GET OTHER HALF
FORREL   MVC   CKUN,0(R1)
         NI    CKUN,X'7F'    CLOBBER COAST
         BZ    FORRUP          SKIP ZERO ENTRY
         CLC   CKUN,CKLOC    SAME AS ATTACK LOCATION ?
         BE    FORRUP        YES - CAN'T GO TO ATTACKER'S OLD LOC
         MVC   0(1,R14),0(R1)    COPY POSSIBLY VALID RETREAT LOC
         LA    R14,1(,R14)
FORRUP   LA    R1,1(,R1)
         BCT   R15,FORREL          AGAIN
         LM    R1,R8,24(R13)    RESTORE
         DROP  R6
         B     ACCDEL              DELETE THE ORDER
         SPACE
ADPROV   LA    R15,127
         NR    R6,R15
         MH    R6,=AL2(PROLEN)
         SH    R6,=AL2(PROLEN)
         A     R6,APRO#BEG
         BR    R9
         SPACE
*        COMMON RETURN POINT TO MAIN CODE
*
CONTT    TM    READ,REOF     WAS END-FILE SET ?
         BZ    CONTT2        NO, LEAVE AS IS
         NI    READ,255-REND-REOF   ALLOW STATUS PRINTOUT AFTER EOF
         OI    READ,RFULL    SHOW CARD IN BUFFER
         MVC   CARD(5),=C'$END'   MOVE END GARBAGE
CONTT2   L     R12,=A(DIPLOMAT)   GET MAIN BASE
         B     CONEJECT-DIPLOMAT(,R12)
         SPACE
BCONBT   L     R12,=A(DIPLOMAT)
         B     CONBT-DIPLOMAT(,R12)
         SPACE 2
EXIT     CALL  XPCLS         CLOSE ALL PRINT DATASETS
         L     R13,4(,R13)
         LM    R14,R12,12(R13)
         LA    R15,0         RETURN CODE
RETCODE  EQU   *-2
         BR    R14
         SPACE 2
         LTORG
         TITLE 'DIPLOMACY --- DUMMY SECTIONS'
PROVINCE DSECT
PRONAME  DS    CL4           ABBREVIATED PROVINCE NAME
PRO#     DS    X             PRIVONCE NUMBER
PROCC    DS    X             COUNTRY OCCUPYING
PROSC    DS    X             COUNTRY OWNING SUPPLY CENTER
PROF     DS    X             STATUS FLAG
PROFSUP  EQU   128           SUPPLY CENTER
PROFSEA  EQU   64            OCEAN
PROFTWO  EQU   32            2-COAST
PROFLAN  EQU   16            INLAND
PROFSOU  EQU   4             FLEET ON SOUTH COAST
PROFNAV  EQU   2             NAVY OCCUPYING
PROFARM  EQU   1             ARMY OCCUPYING
PROFHOM  DS    X             HOME COUNTRY
         SPACE
PROLAND  DS    XL12          VALID LAND MOVES FROM THIS PROVINCE
PROSEA   DS    0XL12         VALID NAVY MOVES FROM THIS PROVINCE
         DS    XL6           VALID MOVES FROM NORTH/WEST COAST
PROSOU   DS    XL6           VALID MOVES FROM EAST/SOUTH COAST
PROLEN   EQU   *-PROVINCE
         SPACE 2
STASECT  DSECT
STABIT   DS    X             COUNTRY ROUTING CODE
STASNAT  DS    CL7           NAME OF COUNTRY
STAFOR   DS    PL2           NO. OF FORCES
STACON   DS    PL2           NO. OF CONTROLLED SUPPLY CENTERS
STABUIL  DS    PL2           NO. AVAILABLE TO BUILD ON
STASLEN  EQU   *-STASECT
         SPACE 2
ORDSECT  DSECT
ORDCNT   DS    PL2'0'        ORDER NUMBER - INPUT SEQUENCE COUNT
ORDCO    DS    C             CURRENT COUNTRY
ORDUN    DS    X             UNIT TYPE
ORDLOC   DS    X             CURRENT LOCATION
ORDORD   DS    X             REQUEST TYPE
ORDST    EQU   1             STANDS
ORDMO    EQU   2             MOVE TO
ORDSUP   EQU   4             SUPPORTS
ORDCON   EQU   8             CONVOYS
ORDCONA  EQU   16            ATTACKED CONVOY
ORDRET   EQU   32            RETREAT REQUIRED
ORDBUIL  EQU   64            BUILD REQUESTED
ORDREM   EQU   128           REMOVAL
ORDSCO   DS    X             SUPPORTED/CONVOYED COUNTRY
ORDSUN   DS    X             -"- UNIT
ORDSLOC  DS    X             CURR. LOCATION
ORDSORD  DS    X             SUPP. REQUEST - STAND OR MOV
ORDNLOC  DS    X             NEW LOCATION
ORDPCON  DS    PL2           CONFLICT COUNT
ORDPSUP  DS    PL2           SUPPORT COUNT
ORDFP    DS    X             PLAY FLAGS
ORDALO   DS    X             CURR LOCATION - NO COAST
ORDALS   DS    X             ABSOLUTE LOCATION - SUPPORT
ORDALN   DS    X             ABSOLUTE - NEW LOCATION
ORDATC   DS    X             'OR' SUPPORTING AND THIS COUNTRY
ORDLEN   EQU   *-ORDSECT     LENGTH OF ONE ENTRY
         SPACE
NORSECT  DSECT
NORCNT   DS    PL2'0'        ORDER NUMBER - INPUT SEQUENCE COUNT
NORCO    DS    C             CURRENT COUNTRY
NORUN    DS    X             UNIT TYPE
NORLOC   DS    X             CURRENT LOCATION
NORORD   DS    X             REQUEST TYPE
NORSCO   DS    X             SUPPORTED/CONVOYED COUNTRY
NORSUN   DS    X             -"- UNIT
NORSLOC  DS    X             CURR. LOCATION
NORSORD  DS    X             SUPP. REQUEST - STAND OR MOV
NORNLOC  DS    X             NEW LOCATION
NORPCON  DS    PL2           CONFLICT COUNT
NORPSUP  DS    PL2           SUPPORT COUNT
NORFP    DS    X             PLAY FLAGS
NORALO   DS    X             CURR LOCATION - NO COAST
NORALS   DS    X             ABSOLUTE LOCATION - SUPPORT
NORALN   DS    X             ABSOLUTE - NEW LOCATION
NORATC   DS    X             'OR' SUPPORTING AND THIS COUNTRY
         TITLE 'DIPLOMACY --- COMMON WORK AREA'
DIPLCOMM CSECT
DB       DS    D             WORK AREA
MESS9    DS    F
GETBACK  DS    F
WARN9    DS    F
DOCS9    DS    F
ACC9     DC    F'0'
ZEROES   DC    2F'0'
         SPACE 2
APRO#BEG DC    V(PROV#BEG)   FIRST PROVINCE DEFINED
APRO#END DC    V(PROV#END)   LAST ENTRY + 1 OF PROVINCE TABLE
APRO#ARM DC    V(PROV#ARM,PROV#END)    EXTENT OF ARMY ONLY PROVINCES
APRO#NAV DC    V(PROV#NAV,PROV#INL)   EXTENT OF NAVY ONLY PROVINCES
CHECKPRO DC    V(DIPLPROV,DIPLWORK)   CHECKPOINT ADDRESS / LENGTH
ARET#BEG DC    V(PROV#END)
AORD#SIZ DC    A(35*ORDLEN)
AORD#BEG DC    V(PROV#END)   USE END OF PROV. FOR ORDERS
VXPRNT   DC    V(XPRNT)      PRINT ROUTINE
         SPACE 2
PARM1    DS    CL8           INPUT PARM
PARM2    DS    CL8
PARM3    DS    CL8
PARM4    DS    CL8
PARM5    DS    CL8
PARM6    DS    CL8
PARM7    DS    CL8
PARM8    DS    CL8
PARM9    DS    CL8
PARM10   DS    CL8
PARM11   DS    CL8
PARM12   DS    CL8
PARM13   DS    CL8
PARME    EQU   *             END OF PARMS
BLANKS   DC    CL80' '
         SPACE 2
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,EROPT=ACC,EODAD=ENDSYN,  *
               RECFM=FB,LRECL=80
         SPACE
FLAG     DC    X'0'          PROCESSING FLAG
STOP     EQU   128           SCREECHING HALT
SEND     EQU   64            MORE SEDATE END
STAT     EQU   32            SHOW STATUS OF PIECES
SAVE     EQU   16            TAKE A CHECKPOINT
DEBUG    EQU   1             DEBUG FLAG
         SPACE
SPRING   DC    C'SPRING'
FALL     DC    C' FALL '     SEE WHAT COMES AFTER SPRING-ING ?
STAF     DC    C'FLEET'
STAA     DC    C'ARMY '
STACOS   DC    C'-SC'
VERBTAB  DC    0A(0),AL1(PINIT),AL3(INITIAL),AL1(3),CL7'INIT'
         DC    AL1(255),AL3(MESSAGE),AL1(3),CL7'MESSAGE'
         DC    AL1(255),AL3(MESSAGE),AL1(2),CL7'MSG'
         DC    AL1(255),AL3(OFFMESS),AL1(3),CL7'NEWS'
VERBDEBG DC    AL1(255),AL3(BUGGER),AL1(2),CL7'DEBUG'   DEBUG SET
         DC    AL1(127),AL3(ENDEND),AL1(2),CL7'END'
         DC    AL1(PMOVE),VL3(DIPLMOVE),AL1(2),CL7'MOVE'
         DC    AL1(PBUIL),VL3(DIPLBUIL),AL1(2),CL7'BUILDS'
         DC    AL1(PRET),VL3(DIPLRETR),AL1(2),CL7'RETREATS'
VERBTABE EQU   *             END OF TABLE
VMESS    WTO   '-  PROPAGANDA :',MF=L
VMESS1   WTO   '-  PROPAGANDA FROM         :',MF=L
VOMESS   WTO   '-  MESSAGES FROM THE GAME KEEPER',MF=L
MSINCO   DC    C'INVALID CONTROL COMMAND'
PARMBACK DC    A(0,0)        RETURN ADDRESS, DISPLACEMENT
VSTAM    DC    0A(0),AL2(137,0),CL133'0'
         ORG   VSTAM+7
STAMCL   DS    0CL129
STAMCOUN DC    CL7' '
         DC    CL2' '
STAMUNIT DC    CL5' '
         DC    CL3' '
STAMPROV DC    CL4' '
STAMCOAS DC    CL3' '
         DC    CL4' '
STAMSTAT DC    CL40' '
         ORG
VSTAS    DC    0A(0),AL2(VSTAE-*,0),CL10'0'
STASCOU  DC    CL7' '
         DC    C' :  '
STASFOR  DC    CL4' ',C' ARMED FORCES;  ALLOWED '
STASCON  DC    CL4' ',C'. '
STASMSG  DC    CL30' '
VSTAE    EQU   *
         ORG   STASCOU
STASC    DS    0CL(VSTAE-STASCOU)
         ORG
STABUA   DC    C'AVAILABLE FOR BUILD'
STAOCN   DC    C'CONTROLLED'
STAOCC   DC    C'  OCCUPIED'
STAHO    DC    C'HOME CITY'
STASU    DC    C'SUPPLY CENTER'
STAMASK  DC    X'40202120'
H1       DC    H'1'
VSTATUS  WTO   '$   DISPOSITION OF ARMED FORCES',MF=L
VNULL    WTO   '$ ',MF=L
P0       DC    P'0'
P1       DC    P'1'
         SPACE 2
REOF     EQU   128           EOF READ
REND     EQU   64            EOF RECURSION FLAG
RFULL    EQU   32            BUFFER LOADED, SKIP NEXT GET
RCON     EQU   16            CONTROL CARD NEEDED, SKIP DATA
READ     DC    AL1(RCON)
         SPACE
VCARD    DC    0A(0),AL2(137,0),CL133'0'
         ORG   VCARD+10
PCARD    DS    0CL127
         DS    CL6           CARD NUMBER + SPACES
CARD     DS    CL80          CARD INPUT IMAGE
         DS    CL4
CAMSG    DS    CL37          MESSAGE AREA
         ORG
MSEXPL   DC    C'TOO LONG'
MSEXP    DC    C'EXCESSIVE PARAMETERS'
MSCONN   DC    C'SKIPPING FOR CONTROL CARD'
MSBADT   DC    C'COMMAND NOT VALID AT THIS TIME'
EOFMSG   DC    C'*****    END OF CARD INPUT    *****'
VSTO     DC    0A(0),AL2(VSTOE-*,0),CL3'$'
VSTOT    DC    CL8'MOVES',C' FOR '
VSTOY    DC    C'SPRING 1901'
VSTOE    EQU   *
VSTOM    WTO   '-PLEASE CIRCLE CHOICES AND COMPLETE UNDERLINED SECTIONS*
               ',MF=L
VSTOF    DC    0A(0),AL2(VSTOFE-*,0),CL3'0'
VSTOFU   DC    CL5' ',CL3' '
VSTOFN   DC    CL4' ',CL3' '
VSTOFCL  DC    CL2' '
         DC    C'STANDS | MOVES | SUPPORTS | CONVOYS'
         DC    C'  _______(CO.)  _ ____-__  STANDS | TO ____-__'
VSTOFE   EQU   *
         SPACE 2
NEWPRO   DC    XL(PROLEN)'0'   DUMMY PROVINCE WORK ENTRY
NEWORD   DC    XL(ORDLEN)'0'   NEW ORDER WORK AREA
TESTPMVC MVC   PARM0(0),0(R3)  MOVE PROV NAME
PARM0    DC    CL8' '        PARM CHECKING
CNTMOV   DC    PL2'0'
ORDCOUNT DC    H'0'
OLDCOUNT DC    H'0'          BACKUP COUNT
CONFG    DC    X'FF'         CONTROL FLAG FOR MOVE PROCESSING
CONCNT   DC    PL2'0'
CUCO     DS    X             CURRENT COUNTRY IN PROCESSING
CUUN     DS    X             CURRENT UNIT
CULOC    DS    X             CURRENT UNIT LOCATION
TESTSFLG DC    X'0'          MOVE PROCESSING FLAG
TSTCO    EQU   128           CHECK COUNTRY AGAINST CUCO
TST1     EQU   1             THIS IS PRIMARY SOURCE FIELD
TST2     EQU   2             THIS IS SECONDARY SOURCE
TST3     EQU   4             THIS IS DESTINATION FIELD
DCORDM   DC    AL1(ORDCON),CL3'CON'
DCORDMA  DC    AL1(ORDSUP),CL3'SUP'
DCORDM2  DC    AL1(ORDST),CL3'STA'
         DC    AL1(ORDMO),CL3'MOV'
         DC    AL1(ORDMO),CL3'-'   ACCEPT HYPHEN AS 'MOVE'
DCORDMX  DC    AL1(ORDMO),CL3'AT'
         DC    AL1(ORDMO),CL3'TO'
DCORDME  EQU   *
DCORDB   DC    AL1(ORDBUIL),CL3'BUI'
         DC    AL1(ORDREM),CL3'REM'
DCORDBE  EQU   *
DCORDR   DC    AL1(ORDRET),CL3'RET'   RETREATS
         SPACE
DCENDORD DC    C'$MESSAGE '
DCBDU1   DC    CL11'     SOURCE'
DCBDU2   DC    CL11'  SECONDARY'
DCBDU3   DC    CL11'DESTINATION'
MSBDUNPR DC    C'UNIT/PROVINCE BAD'
MSBDODU  DC    C'DUPLICATE ORDER'
DCONCIN  DC    C'INCOMPLETE CONVOY'
DCONCUN  DC    C'NOT REQUIRED'
MSBDORD  DC    C'UNACCEPTABLE ORDER'
MSBDMANY DC    C'EXCESSIVE PARAMETERS'
MSBDUNM  DC    C'UNIT TYPE MISSING'
MSBDONE  DC    C'DONE'
DCONOC   DC    C'REQUIRES CONVOY - BUT NOT ORDERED'
DCONRES  WTO   '1ORDER RESOLUTION',MF=L
DCONFAIL DC    C'FAILED'
VORDZERO WTO   ' ******** NO VALID ORDERS RECEIVED ********',MF=L
DCONTF   DC    C'INVALID UNIT'
DCATFAIL DC    C'FAILED; ATTACKS '
DCONAT   DC    C'CANCELLED; ATTACKED BY '
DCONMRT  DC    0C'MUST RETREAT'
DCONMRTO DC    C'MUST RETREAT TO :'
DCONMAN  DC    CL(L'DCONMRT)'ANNIHILATED'
DCONSUCS DC    0C'SUCCEEDS'
DCONSUC  DC    C'SUCCEEDS'
DCONDEL  DC    C'DELIVERED'
DCONSTAN DC    C'STAND-OFF'
DCBYDFLT DC    C'BY DEFAULT'
MAXSUP   DC    PL2'0'        SUPPORT COUNT - MAX
MAXSFG   DC    X'0'          1 IF STANDOFF
SOCFLAG  DC    X'0'          MOVE CHAIN PROCESSING
HEXTAB   DC    C'0123456789ABCDEF'
VORDHEAD DC    0H'0',AL2(VORDHEAN-*,128),C'0 '
 DC C' #### CO UN LO OR CO UN LO SO NL CONF SUPP FG MSGS'
VORDHEAN EQU   *
VORDHEXT DC    0H'0',AL2(VORDHEXN-*,128),C'  '
HEXTEXT  DC    CL131' '
VORDHEXN DC    CL9' '        FOR BLANKING TRAILER INFO
         SPACE 2
SUPWORK  DC    2D'0'
SUPFG    DC    X'0'
SUPCO    DC    X'0'
SUPUN    DC    X'0'
SUPLOC   DC    X'0'
SUPNLOC  DC    X'0'
         SPACE
         SPACE
TABORD   DC    X'1',CL8'STANDS'
         DC    X'2',CL8'MOVES TO'
         DC    X'4',CL8'SUPPORTS'
         DC    X'8',CL8'CONVOYS'
         DC    X'20',CL8'RETREATS'
         DC    X'40',CL8'BUILT'
         DC    X'80',CL8'REMOVED'
TABORDE  EQU   *
         SPACE
SUPUNTAB DC    X'01',CL13'AUSTRIAN'
         DC    X'02',CL13'ENGLISH'
         DC    X'04',CL13'FRENCH'
         DC    X'08',CL13'GERMAN'
         DC    X'10',CL13'ITALIAN'
         DC    X'20',CL13'RUSSIAN'
         DC    X'40',CL13'TURKISH'
         SPACE
CKUN     DS    X
CKLOC    DS    X
CKNLOC   DS    X
RETLOC   DC    X'0'          LOCATION INVALID FOR RETREAT
         SPACE
VORDACC  WTO   '1    ACCEPTED ORDERS',MF=L
VAORD    DC    0H'0',AL2(VAORDE-*,0),CL4' '
AORDER   DC    CL129' '
         ORG   AORDER-1
AORDNUMB DS    CL4
         DS    CL3
AORDCOUN DS    CL8
         DS    C
AORDPUN  DS    CL5
         DS    C
AORDPLOC DS    CL7
         DS    C
AORDACT  DS    CL8
         DS    C
AORDSCO  DS    CL8
         DS    C
AORDSUN  DS    CL5
         DS    C
AORDSLOC DS    CL7
         DS    C
AORDSACT DS    CL8
         DS    C
AORDNLOC DS    CL7
         DS    C
AORDMSG  DS    CL38
         ORG
VAORDE   EQU   *
         SPACE 2
STANDOFF DC    34X'0',X'0'   + SPARE
STANDAD  DC    A(STANDOFF)
DOMSAVP  DC    A(DOMSAVE)
DOMSAVE  DC    34F'0'
         DC    2F'0'         JUST IN CASE
         TITLE 'DIPLOMACY --- CHECKPOINT INFORMATION'
CHECKIN  DS    0D            SINGLE APPEARANCE TABLE
COPFX    DC    AL1(0),CL7'MESSAGE'   PREFIX TO 'STATAB'
COPTOT   DC    PL2'0'        TOTAL FORCES ON BOARD
         DC    2PL2'0'       OTHER JUNK
         DC    AL1(0),CL7'MSG',3PL2'0'   MESSAGE ENTRY - DUMMY CNTRS
STATAB   DC    AL1(001),CL7'AUSTRIA',3PL2'0'
         DC    AL1(002),CL7'ENGLAND',3PL2'0'
         DC    AL1(004),CL7'FRANCE ',3PL2'0'
         DC    AL1(008),CL7'GERMANY',3PL2'0'
         DC    AL1(016),CL7'ITALY  ',3PL2'0'
         DC    AL1(32),CL7'RUSSIA  ',3PL2'0'
         DC    AL1(064),CL7'TURKEY ',3PL2'0'
STATEND  EQU   *
         SPACE 2
CHECKIT  EQU   *             START OF DOUBLE APPEARACCE TABLE
PLAY     DC    X'80'         PLAY STATUS FLAG
PINIT    EQU   128           INIT. NECESSARY OR PREV. PLAY ENDED
PMOVE    EQU   64            ONLY 'MOVE' REQUESTS HONORED
PBUIL    EQU   32            ONLY 'BUILD'/'REMOVE'
PRET     EQU   8             RETREATS ONLY
PNBUIL   EQU   16            BUILD IS REQUIRED
PMSG     EQU   1             BIT TO ALLOW $MESSAGE, NEWS, ETC. PROCESSI
         ENTRY DATE
DATE     DC    C' FALL '     SEASON
         DC    C' 19'        YEAR-PREFIX
YEAR     DC    C'00'
HADMOVE  DC    X'0'          WHICH COUNTRY SUPPLIED MOVES ?
ROUTE    DC    X'FF'         FLAGS TO ALLOW OUTPUT PRINTING
         SPACE 2
CHECKNEW EQU   *
NEWPLAY  DC    AL1(PMOVE)
NEWDATE  DC    C'SPRING 1901'
DIDMOVE  DC    X'0'          WHICH COUNTRY SUPPLIED MOVES ?
NEWROUTE DC    X'FF'         FLAGS TO ALLOW OUTPUT PRINTING
CHECKLEN EQU   *-CHECKIN     LENGTH OF CHECKPOINT INFORMATION
         SPACE 2
         END
