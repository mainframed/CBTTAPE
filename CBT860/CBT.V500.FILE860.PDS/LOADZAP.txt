LZAP     TITLE 'L O A D Z A P  ***  LOAD, ZAP AND EXECUTE A PROGRAM'
         PUNCH '  ALIAS LOADZAPA '                              GP05060
         PUNCH '   SETCODE AC(1) '                              GP05060
         PUNCH '  ORDER LOADZAP(P) '   EASIER DUMPS             GP05060
         SPACE 1
*              AUTHOR.   DON HIGGINS.
*              DATE.     12/26/72.
*              REMARKS.  LOAD, SUPERZAP, AND EXECUTE A PROGRAM.
*
*                   THIS PROGRAM READS CARDS IDENTICAL TO
*                   SUPERZAP NAME, VERIFY, REP,DUMP,COMMENT CARDS FROM
*                   A SEQUENTIAL FILE NAMED 'ZAPIN'.
*
*                   THE PROGRAM ON THE NAME CARD IS 'LOADED'.
*                   THE VERIFY-REP CARDS ARE READ AND PROCESSED
*                   AGAINST THE LOADED PROGRAM IN CORE.
*                   THE MODIFIED PROGRAM IS EXECUTED.
*
*                        DUMP COMMAND CAUSES THE PROGRAM TO BE LOADED
*                        AND THEN AN ABEND DUMP 999 IS TAKEN.
*                        AN ABEND DUMP 999 IS ALSO TAKEN AFTER ALL
*                        VERIFY / REP CARDS HAVE BEEN PROCESSED IF
*                        ANY VERIFY FAILED.
*
*                   IF A VERIFY FAILS, THE PROGRAM IS NOT EXECUTED.
*
*                   ALL MESSAGES ARE PRINTED ON 'ZAPOUT'.
*                                                                83087
*        MODIFICATIONS BY G. POSTPISCHIL                         83087
*              EXPERT SYSTEM PROGRAMMING                        GP02253
*              176 OLD STAGE COACH ROAD                         GP02253
*              BRADFORD, VT 05033-8844                          GP02253
*        E-MAIL: GERHARD@postpischil.com                        GP02253
*                                                                83087
*        ALLOW 'LOAD MEMBER' CARD TO LOAD/MODIFY A DEPENDENT MODULE
*                                                                83087
*        ATTRIBUTES: RENT/REFR/REUS AC(1) PAGE                   83087
*        DEPENDENCIES: LOCAL MACROS AND SERVICE ROUTINES         83087
*        MINOR CHANGES FOR X/A, ESA, AND OS/390 SUPPORT         GP99188
*        THE USER NOW MUST SUPPLY A LOADLIB DD CARD INCLUDING   GP02253
*        ALL (NORMAL) STEPLIB LIBRARIES. THIS AVOIDS LOSING     GP02253
*        AUTHORIZATION FOR LOADZAPA. FOR LOADZAP, STEPLIB MAY   GP02253
*        BE USED FOR UNAUTHORIZED PROGRAMS.                     GP02253
*        WHEN LOADZAP IS INVOKED AUTHORIZED, THEN MODULES WITH  GP02253
*        THE RENT OR REFR ATTRIBUTE ARE LOADED TO SUBPOOL 252   GP02253
*        (STORE PROTECTED)                                      GP02253
         SPACE 1
**   THE ZAPIN FILE SUPPLIES THE MODULE NAME TO BE INVOKED, AND MAY  **
**   SPECIFY OPTIONAL MODULES TO BE LOADED. ANY MODULE MAY BE        **
**   FOLLOWED BY VERIFY AND REPLACE STATEMENTS:                      **
**                                                                   **
**   NAME module             SPECIFY PROGRAM TO BE EXECUTED          **
**   RENT module             -"- AND FORCE REFRESHABLE/REENTRANT     **
**   NAME module alias       -"- AND ASSIGN THE ALIAS TO THE ENTRY   **
**   VER  1234 ABCDEF          (OPTIONAL) VERIFY TEXT                **
**   REP  1234 9876            (OPTIONAL) REPLACE TEXT               **
**   LOAD module                                                     **
**                                                                   **
**   AN ASTERISK IN COLUMN 1, OR AFTER A MODULE NAME, DENOTES        **
**   COMMENTS.                                                       **
         SPACE 1                                                 83087
*              REGISTERS.
*                   R2  - LOAD MODULE ADDRESS
*                   R3  - LOAD MODULE SIZE
*                   R5  - DATA LENGTH FOR VERIFY/REP
*                   R6  - OFFSET FOR VERIFY/REP
*                   R8  - SCAN START
*                   R9  - SCAN LENGTH
*                   R10 - CURRENT PARSE ELEMENT
*                   R14 - LINK FOR SUBROUTINES
         SPACE 1                                                 83087
         COPY  OPTIONGB                                          83087
         SPACE 1                                                 83087
         SYSPARM LIST=YES    SET GLOBAL OPTIONS                  83087
         SPACE 2                                                 83087
         PRINT &PRTSOR                                           83087
LOADZAP  PGMHEAD ZERO12,BASE=R12,PARM=R2,AM=31,RM=24,LOC=BELOW,        *
               ENTRY=LOADZAPA,EID=SHORT,ENTNO=FLAGS
         MVC   BLDLLIST(4),=Y(1,78)                              83087
         LA    R0,256                                           GP11288
         ST    R0,DEFPATCH                                      GP11288
         SERVINIT ,          LOAD THE SERVICE ROUTINE            83087
         SERVLOAD @PRINTER,@PARSER,@INPREAD,SUBFETCH            GP11288
         DEVTYPE PATDCB+DCBDDNAM-IHADCB,DB  LOOK FOR LOADLIB    GP02253
         BXH   R15,R15,NOLOAD   NONE                            GP02253
         CLI   DB+2,UCB3DACC    DASD ?                          GP02253
         BNE   NOLOAD        NO; IGNORE                         GP02253
         MVC   STEPDCB(STEPDCBL),PATDCB  MOVE PATTERN DCB       GP02253
         LA    R2,STEPDCB    POINT TO ACTIVE DCB                GP02253
         OPEN  ((R2)),MF=(E,STEPOLST)  OPEN IT                  GP02253
         BXH   R15,R15,NOLOAD                                   GP02253
         ST    R2,@DCB       PASS DCB ADDRESS TO BLDL AND LOAD  GP02253
NOLOAD   DS    0H                                               GP02253
         SPACE 1                                                 79192
         AIF   ('&SYSTEM' EQ 'MFT' OR '&SYSTEM' EQ 'MVT').NOAPF  79192
         PUSH  USING                                            GP99188
         AIF   ('&LOCAL' NE 'DTS').NOLACCT                      GP02347
*        D.T.S. SVS CHANGES                                      79192
*          IF INVOKED BY AUTHORIZED ACCOUNT NUMBER, TURN ON APF  79192
*          BIT IN TASKLIB DEB (LOCAL IEAVLK01 MOD USES IT)       79192
*        IF UNAUTHORIZED, TURN APF BIT OFF IN JSCB               79192
         SPACE 1                                                 79192
         SERVCALL ACGET,DB   GET CURRENT ACCOUNT                 83087
         STC   R0,DB+7       SAVE PRIVILEGES                     83087
         TM    DB+7,VAASTC+VAASYS  SYSTEMS OR START TASK ?       83087
         BZ    *+8           NO                                  83087
.NOLACCT OI    FLAGS,FGAUTH  SET PRIVILEGED USER                 83087
         LTCB  R10,USE=R10   GET TCB                            GP02347
         ICM   R9,15,TCBJSCB  GET JSCB                          GP99188
         BZ    BOOBOO        HUH?                               GP99188
         USING IEZJSCB,R9    DECLARE IT                          83087
         TM    JSCBOPTS,JSCBAUTH       APF AUTHORIZED ?          79192
         BZ    BOOBOO        NO; SKIP ALL THIS                   79192
         MODESET KEY=ZERO    GET KEY ZERO                        79192
         TM    FLAGS,FGZAPA  SPECIAL ENTRY USED ?                83087
         BZ    APFRESET      NO; RESET AUTHORIZATION             83087
         TM    FLAGS,FGAUTH  PRIVILEGED REQUEST ?                83087
         BZ    BOOBOO        NO; FAIL                           GP02253
         SPACE 1                                                 83087
APFSET   ICM   R7,15,TCBJLB  TEST STEPLIB PRESENCE              GP99188
         BZ    APFQUIT       NO STEPLIB                         GP99188
         USING IHADCB,R7     DECLARE IT                          83087
         L     R7,DCBDEBAD   LOAD DEB FOR STEPLIB                83087
         N     R7,=X'00FFFFFF'  FIX HIGH BYTE                   GP99188
         USING DEBBASIC,R7                                       83087
         OI    DEBFLGS1,DEBAPFIN  TURN ON APF LIBRARY BIT        83087
         ICM   R7,15,@DCB    LOAD LIB DCB ALSO ?                GP02253
         BZ    NOLOAD1                                          GP02253
         USING IHADCB,R7     DECLARE IT                         GP02253
         L     R7,DCBDEBAD   LOAD DEB FOR LOADLIB                83087
         N     R7,=X'00FFFFFF'  FIX HIGH BYTE                   GP99188
         USING DEBBASIC,R7                                       83087
         OI    DEBFLGS1,DEBAPFIN  TURN ON APF LIBRARY BIT        83087
NOLOAD1  OI    JSCBSWT1,JSCBPASS  PERMIT LIBRARY ACCESS          87053
         B     APFQUIT                                           79192
APFRESET NI    JSCBOPTS,255-JSCBAUTH   ELSE TURN OFF AUTHORIZATION
APFQUIT  MODESET KEY=NZERO   RESET ME                            79192
         B     BOOHOO        WHEW                               GP02253
         POP   USING                                            GP99188
BOOBOO   TM    FLAGS,FGZAPA  FORGOT SETCODE IN LKED ?           GP02253
         BZ    BOOHOO        IF SO, NOT A PROBLEM               GP02253
         WTO   'LOADZAPA: NO APF'                               GP02253
BOOHOO   DC    0H'0'         FORGOT SETCODE IN LKED ?            79192
.NOAPF   SPACE 2                                                 79192
         INPOPEN ZAPIN,OPT=(ABEND)  NEED INPUT                   83087
         PRTOPEN ZAPOUT,OPT=(DUMMY,NOWTO)  OPEN OUTPUT FILE      83087
         CH    R15,=H'8'     SUCCESSFUL OPEN ?                   83087
         BL    PRTOPEND      YES                                 83087
         PRTOPEN CONSOLE,OPT=(DUMMY,NOWTO),DEV=2  OPEN WTO       83087
PRTOPEND PRTV  HEADER,TITLE=1  PRINT THE TITLE                   83087
         SPACE 2
MAINLOOP BAS   R14,DECODE    GET AN INPUT CARD                  GP11288
MAINLOOK CLI   VERBCODE,C'D'   DUMP REQUEST ?                    83087
         BE    LOAD
         CLI   VERBCODE,C'N'   NAME/MEMBER EXECUTE REQUEST ?     83087
         BE    LOAD
         CLI   VERBCODE,C'L'   LOAD A DEPENDENT MODULE ?         83087
         BE    LOAD          YES; PROCEED                        83087
         BAS   R10,DFLUSH                                       GP99188
         B     MAINLOOP                                          83087
         SPACE 2
*        THE OMISSION OF LPA LOOKUP IS INTENTIONAL               79192
LOAD     L     R2,@DCB       LOAD OPTIONAL LOADLIB DCB          GP02253
         BLDL  (R2),BLDLLIST    FIND THE PROGRAM                GP02253
         BXH   R15,R15,LOADFAIL  FLUSH WITH MESSAGE IF BLDL FAILED
         TM    PDS2ATR1,PDS2EXEC  EXECUTABLE ?                   83087
         BZ    LOADNEXE      NO; SKIP                            83087
         XC    FETPARM(FETPARML),FETPARM  CLEAR SUBFETCH PARMS  GP02255
         ST    R2,FT@DCB     SET OPEN DCB                       GP02255
         SPACE 1
         SR    R1,R1                                            GP11288
         ICM   R1,7,PDS2STOR   STORAGE REQUIRED/NO H1/ATL SPLT  GP11288
         ST    R1,MODSIZE    SAVE FOR LATER                     GP11288
         A     R1,DEFPATCH   SET PREFERRED PATCH SIZE           GP11288
         MIN   R1,=X'00FFFFFF'    MVS LIMIT                     GP11288
         S     R1,MODSIZE                                       GP11288
         ST    R1,MODPATCH   SAVE PATCH SIZE                    GP11288
         SPACE 1
         LA    R0,BLDLLIST+4  POINT TO BLDL OUTPUT              GP02255
         ST    R0,FT@NAME   STASH IN NAME FIELD                 GP02255
         OI    FT@NAME,FTMFBLD  SHOW BLDL SUPPLIED              GP02255
         OI    FTOFLGS1,FTOFBCDE  BUILD A CDE AND LOAD          GP02348
         OI    FTOFLGS2,FTOFREUS  ALWAYS REUSABLE               GP11288
         OI    FTOFLGS3,FTOFAPFL+FTOFAUTH FLAG AC=1 & APFLIB    GP02348
         MVC   DB(1),OPTFLAGS  COPY CLEAR/PAD FLAGS             GP11288
         NI    DB,OFCLR0+OFCLRC   KILL REST                     GP11288
         OC    FTOFLGS1,DB   SET PAD FLAGS                      GP11288
         TM    OPTFLAGS,OFFREUS   SET REUSABLE ?                GP11288
         BZ    NOPTREUS             NO                          GP11288
         OI    PDS2ATR1,PDS2REUS                                GP11288
         OI    FTOFLGS2,FTOFREUS                                GP11288
NOPTREUS TM    OPTFLAGS,OFFRENT   SET REENTRANT?                GP11288
         BZ    NOPTRENT             NO                          GP11288
         OI    PDS2ATR1,PDS2RENT                                GP11288
         OI    FTOFLGS2,FTOFRENT                                GP11288
NOPTRENT TM    PDS2ATR1,PDS2RENT  OLD STYLE RENT ?              GP11288
         BNZ   PAYRENT              YES; LOAD PROTECTED         GP11288
         TM    PDS2ATR2,PDS2REFR  NEW STYLE REFRESHABLE ?       GP11288
         BZ    SKIPRENT             NO; NOTHING SPECIAL         GP11288
PAYRENT  OI    FTOFLGS1,FTOFREFR+FTOFFREN  LOAD TO SUBPOOL 252  GP11288
         OI    FTOFLGS2,FTOFRENT+FTOFREUS  ALSO TO CDE          GP11288
SKIPRENT LA    R1,FETPARM    PASS PARM                          GP02255
         SUBCALL SUBFETCH    CALL FETCH ROUTINE                 GP05060
         STM   R15,R0,DB     SAVE RETURN AND REASON CODE
         BXH   R15,R15,LOADNFET  OOPS
         TM    FTOFLGS1,FTOFBCDE  CDE BUILD REQUESTED ?         GP06308
         BZ    NOSUBCDE      NO                                 GP06308
         TM    FTOFLGS1,FTOFBCDF  DID IT FAIL ?                 GP06308
         BZ    NOSUBALI      NO                                 GP06308
         PRTL  '         CDE BUILD FAILED'                      GP06308
         LA    R2,306        ABEND CODE
ABDUMP   SERVTERM ,                                             GP11288
         ABEND (R2),DUMP     *****DEBUG*****   AVERT LOOP       GP11288
NOSUBALI CLI   ALIAS,C' '    DID USER WISH AN ALIAS NAME ?      GP06310
         BNH   NOSUBCDE      NO; NORMAL                         GP06310
         L     R2,FT@ENTRY   GET THE ENTRY POINT                GP06310
         IDENTIFY EPLOC=ALIAS,ENTRY=(R2)   REQUEST SECOND NAME  GP06310
         LTR   R15,R15       DID IT WORK ?                      GP06310
         BZ    NOSUBOKI      YES                                GP06310
         PRTLIST MSGBDIDE    SHOW PROBLEM                       GP06310
         B     NOSUBCDE                                         GP06310
NOSUBOKI PRTLIST MSGGDIDE    SHOW SUCCESS                       GP06310
         B     NOSUBCDE                                         GP06310
MSGBDIDE FDPRT '         IDENTIFY FOR',NL                       GP06310
         FDPRT ALIAS,PAD                                        GP06310
         FDPRT 'FAILED'                                         GP06310
         FD    *END                                             GP06310
MSGGDIDE FDPRT '         IDENTIFY FOR',NL                       GP06310
         FDPRT ALIAS,PAD                                        GP06310
         FDPRT 'AT'                                             GP06310
         FDPRT FT@ENTRY,HEX,PAD                                 GP06310
         FD    *END                                             GP06310
         SPACE 1                                                GP06310
NOSUBCDE L     R1,FT@ENTRY  SAVE ENTRY ADDRESS AND MODE BIT     GP02255
         L     R2,FT@LOAD   ALSO GET LOAD ADDRESS               GP02255
         L     R3,FT#LOAD   AND MODULE LENGTH                   GP02255
         CLI   VERBCODE,C'N'  MEMBER NAME REQUEST ?              83087
         BNE   LOADONLY      NO; JUST LOAD/DUMP                  83087
         TM    FLAGS,FGHENT  PRIOR MEMBER REQUEST ?              83087
         BZ    LOADEXEC      NO                                  83087
         PRTV  MGTOOMAN,DEV=(1,2)  EXCESSIVE ENTRY               83087
         B     LOADONLY      BUT CONTINUE                        83087
LOADFAIL PRTLIST MGLODBAD,DEV=(1,2)                              83087
         OI    FLAGS,FGFAIL                                      83087
         B     MAINLOOP                                          83087
LOADNEXE PRTLIST MGEXEBAD,DEV=(1,2)
         OI    FLAGS,FGFAIL
         B     MAINLOOP
LOADNFET PRTLIST MGFETBAD,DEV=(1,2)
         OI    FLAGS,FGFAIL
         B     MAINLOOP
LOADEXEC ST    R1,ENTRY      SAVE ENTRY ADDRESS AND MODE        GP02255
         OI    FLAGS,FGHENT  SET ENTRY SET FLAG                  83087
LOADONLY LA    R2,0(,R2)     CLEAR MODE BIT                     GP02254
         OI    FLAGS,FGLOAD   AT LEAST ONE LOAD DONE             83087
         PRTLIST MGFETGUD,DEV=(1,2)                             GP02256
         CLI   VERBCODE,C'D'                                     83087
         BNE   MODIFYL
         SPACE 1                                                 83087
DUMP     SERVTERM ,          CLOSE AND FREE SERVICES             83087
         ABEND 999,DUMP            ABEND WITH DUMP
         SPACE 2
MODIFYL  BAS   R14,DECODE                                       GP11288
         CLI   VERBCODE,C'V'                                     83087
         BE    VERIFY              VERIFY DATA
         CLI   VERBCODE,C'R'                                     83087
         BE    REP                 REP DATA
         CLI   VERBCODE,C'N'   NAME ?                            83087
         BE    MAINLOOK      YES                                 83087
         CLI   VERBCODE,C'L'   LOAD ?                            83087
         BE    MAINLOOK                                          83087
         CLI   VERBCODE,C'D'   DUMP ?                            83087
         BE    MAINLOOK                                          83087
         BAS   R10,DFLUSH                                       GP99188
         B     MODIFYL
         SPACE 2
EOF      ICM   R2,15,@DCB    DID WE OPEN A LOADLIB DCB ?        GP02256
         BZ    EOFCLOSE      NO                                 GP02256
         CLOSE ((R2)),MF=(E,STEPOLST)  YES; CLOSE IT            GP02256
EOFCLOSE TM    FLAGS,FGHENT  ENTRY POINT RECEIVED ?              83087
         BNZ   EOFTEST       YES; NORMAL TERMINATION             83087
         PRTV  MGNOEP,DEV=(1,2)  COMPLAIN ABOUT NO ENTRY         83087
         OI    FLAGS,FGFAIL    FAIL                              83087
         B     EOFTEST                                           83087
INPERR   PRTV  MGINPERR,DEV=(1,2)                                83087
         OI    FLAGS,FGFAIL                                      83087
EOFTEST  SERVTERM ,          CLOSE AND FREE SERVICES             83087
         TM    FLAGS,FGFAIL   TERMINATION REQUIRED ?             83087
         BZ    EOFEXEC       NO; INVOKE DESIRED PROGRAM
         TM    FLAGS,FGLOAD  AT LEAST ONE LOAD DONE ?            83087
         BNZ   DUMP          YES; QUIT WITH A DUMP               83087
         ABEND 999,DUMP      NO DUMP                             83087
         SPACE 2
EOFEXEC  L     R9,ENTRY      GET ADDRESS TO BE EXECUTED          83087
         PUSH  PRINT
         PRINT ON,GEN
         PGMEXIT RC=(R9),RETADDR=(R15) INVOKE SIDEWAYS
         POP   PRINT
         SPACE 2
DECODE   ST    R14,SAVEDEC                                      GP11288
DECODER  INPGET ,            GET AN INPUT CARD                   83087
         BXH   R15,R15,INPERR   UNRECOVERABLE INPUT ERROR        83087
         LR    R8,R1         SAVE START                         GP11288
         LR    R9,R0         SAVE LENGTH                        GP11288
         PRTF  (R8),(R9),CC=NO  PRINT ECHO                      GP11288
         MINH  R9,=H'72'     IGNORE 73-80                       GP11288
         PARSE (R8),(R9),OPT=(KEYWORD,UNQUOTE),PARM=PARPARM     GP11288
         BXLE  R15,R15,PARSED     OK SO FAR                     GP11288
FAILPARS PRTDATA 'PARSE FAILURE'                                GP11288
         LA    R2,2540                                          GP11288
         B     ABDUMP        QUIT                               GP11288
         SPACE 1
PARSED   ICM   R10,15,PAR@TABL    GET FIRST PARAMETER           GP11288
         BZ    DECODER              NONE; IGNORE BLANK CARD     GP11288
         USING PRSDSECT,R10       DECLARE PARAMETER AREA        GP11288
         CLI   PRSKEYWD,0    ANY KEYWORD?                       GP11288
         BH    BADCARD         TOO BAD                          GP11288
         MVC   PRSKEYWD,PRS$TEXT  MOVE TEXT                     GP11288
         OC    PRSKEYWD,=CL8' '   MAKE UPPER CASE               GP11288
         MVC   VERBCODE,PRSKEYWD  COPY INITIAL                  GP11288
         LA    R0,PRSKEYWD   POINT TO TEXT                      GP11288
         LA    R1,VERBBXLE   POINT TO TABLE POINTERS            GP11288
         EXTRN SUBVERB                                          GP11288
         SUBCALL SUBVERB     MATCH                              GP11288
         LTR   R15,R15       DID IT WORK ?                      GP11288
         BNZR  R1              YES; INVOKE MATCHING ADDRESS     GP11288
         CLI   PRS$TEXT,C'*'      IGNORE COMMENTS               GP11288
         BE    DECODER                                          GP11288
         SPACE 1
BADCARD  PRTDATA 'UNRECOGNIZED COMMAND'                         GP11288
         BAS   R10,DFLUSH                                       GP99188
         B     DECODER                                          GP11288
         SPACE 1
VERBBXLE PARKEYBX VERBTAB    DEFINE VALID VERBS
VERBTAB  PARKEYAD 'NAME    ',DNAME     LOAD AND EXECUTE         GP11288
VERBTAB2 PARKEYAD 'RENT    ',DNAME252  LOAD AND EXECUTE         GP11288
         PARKEYAD 'MEMBER  ',DNAMEM    LOAD AND EXECUTE         GP11288
         PARKEYAD 'LOADR   ',DNAME252  LOAD AND EXECUTE         GP11288
         PARKEYAD 'LOAD    ',DNAMEM    LOAD AND EXECUTE         GP11288
         PARKEYAD 'VERIFY  ',DVERIFY   VERIFY LOADED TEXT       GP11288
         PARKEYAD 'REPLACE ',DVERIFY   REPLACE LOADED TEXT      GP11288
         PARKEYAD 'OPTIONS ',DOPTS     SET RUN OPTIONS          GP11288
         PARKEYAD '*OPTIONS',DOPTS     SET RUN OPTIONS          GP11288
VERBTABN PARKEYAD 'DUMP    ',DNAME     LOAD AND DUMP            GP11288
         SPACE 1
BADSYN   PRTDATA 'EXTRANEOUS FIELD',(PRS$TEXT,DEB,PAD)          GP11288
         BAS   R10,DFLUSH                                       GP99188
         B     DECODER                                          GP11288
         SPACE 1
MISSFD   PRTDATA 'REQUIRED FIELD NOT FOUND'                     GP11288
         BAS   R10,DFLUSH                                       GP99188
         B     DECODER                                          GP11288
         SPACE 1
LONGOP   PRTDATA 'OPERAND TOO LONG'                             GP11288
         BAS   R10,DFLUSH                                       GP99188
         B     DECODER                                          GP11288
         SPACE 1
NOTHEX   PRTDATA 'HEX OPERAND EXPECTED'                         GP11288
         BAS   R10,DFLUSH                                       GP99188
         B     DECODER                                          GP11288
         SPACE 1
NOTPAIR  PRTDATA 'HEX DATA LENGTH ODD'                          GP11288
         BAS   R10,DFLUSH                                       GP99188
         B     DECODER                                          GP11288
         SPACE 1
EXCESS   PRTDATA 'PATCH ADDRESS EXCEEDS MODULE SIZE'            GP11288
         BAS   R10,DFLUSH                                       GP99188
         B     DECODER                                          GP11288
         SPACE 1
DFLUSH   TM    FLAGS,FGFAIL  SET NOGO SWITCH                    GP11288
         BNZR  R10                                              GP11288
         OI    FLAGS,FGFAIL  SET NOGO SWITCH                     83087
         PRTV  NOGOMSG                                           83087
         BR    R10
         SPACE 1                                                 83087
DNAME252 OI    FLAGS,FGFREN  FORCE MODULE REENTRANT             GP11288
         B     DNAME2
DNAMEM   MVI   VERBCODE,C'N'  FORCE 'NAME ' CARD                GP11288
DNAME    ZI    FLAGS,FGFREN       RESET FORCED REENTRANCY       GP11288
DNAME2   ICM   R10,15,PRSLINK     GET NEXT OPERAND              GP11288
         BZ    MISSFD               NONE; FAIL                  GP11288
         MVC   PDS2NAME,=CL8' '   PRE-BLANK MEMBER NAME          83087
         MVC   ALIAS,=CL8' '   PRE-BLANK MEMBER'S ALIAS         GP06310
         LA    R14,PDS2NAME                                     GP11288
         ICM   R2,15,PRS#TEXT                                   GP11288
         BZ    MISSFD                                           GP11288
         CH    R2,=AL2(L'PDS2NAME)     VALID ?                  GP11288
         BH    LONGOP        NO; CHECK FOR ALIAS                GP11288
         BCTR  R2,0                                             GP11288
         EX    R2,EXMVCNAM   MOVE NAME                          GP11288
         ICM   R10,15,PRSLINK     ALIAS PRESENT ?               GP11288
         BZ    EXITDEC              NO                          GP11288
         CLI   PRS$TEXT,C'*'      COMMENT ?                     GP11288
         BE    EXITDEC              YES; IGNORE                 GP11288
         ICM   R2,15,PRS#TEXT                                   GP11288
         BZ    EXITDEC                                          GP11288
         CH    R2,=AL2(L'PDS2NAME)     VALID ?                  GP11288
         BH    LONGOP        NO; CHECK FOR ALIAS                GP11288
         BCTR  R2,0                                             GP11288
         LA    R14,ALIAS     COPY OPERAND TO ALIAS NAME         GP11288
         EX    R2,EXMVCNAM   MOVE ALIAS NAME                    GP11288
EXITDEC  L     R14,SAVEDEC                                      GP11288
         BR    R14           NOTHING MORE - NORMAL RETURN       GP11288
EXMVCNAM OC    0(0,R14),PRS$TEXT                                GP11288
         SPACE 1
*---------------------------------------------------------------------*
*   *OPTIONS CARD - SPECIFY OPTIONS FOR SUBSEQUENT VERBS              *
*---------------------------------------------------------------------*
DOPTS    ICM   R10,15,PRSLINK     GET OFFSET                    GP11288
         BZ    DECODER              NONE; IGNORE CARD           GP11288
         CLI   PRSKEYWD,0    KEYWORD FOUND ?                    GP11288
         BH    DOPTSK          YES; RETAIN IT                   GP11288
         MVC   PRSKEYWD,PRS$TEXT  MOVE TEXT                     GP11288
         OC    PRSKEYWD,=CL8' '   MAKE UPPER CASE               GP11288
DOPTSK   LA    R0,PRSKEYWD   POINT TO TEXT                      GP11288
         LA    R1,OPTSBXLE   POINT TO TABLE POINTERS            GP11288
         SUBCALL SUBVERB     MATCH                              GP11288
         LTR   R15,R15       DID IT WORK ?                      GP11288
         BNZR  R1              YES; INVOKE MATCHING ADDRESS     GP11288
         CLI   PRS$TEXT,C'*'      IGNORE COMMENTS               GP11288
         BE    DECODER                                          GP11288
         B     BADSYN        UNRECOGNIZED                       GP11288
         SPACE 1                                                GP11288
ORENT    OI    OPTFLAGS,OFFRENT   SET TO SET REENTRANCY         GP11288
         B     DOPTS                                            GP11288
         SPACE 1                                                GP11288
OREUS    OI    OPTFLAGS,OFFREUS   SET TO SET REUSABILITY        GP11288
         B     DOPTS                                            GP11288
         SPACE 1                                                GP11288
ONRENT   ZI    OPTFLAGS,OFFREUS+OFFRENT  RESET SPECIALS         GP11288
         B     DOPTS                                            GP11288
         SPACE 1                                                GP11288
OFILL0   OI    OPTFLAGS,OFCLR0    PAD WITH X'00'                GP11288
         B     DOPTS                                            GP11288
         SPACE 1                                                GP11288
OFILLC   OI    OPTFLAGS,OFCLRC    PAD WITH X'CC'                GP11288
         B     DOPTS                                            GP11288
         SPACE 1                                                GP11288
ONFILL   ZI    OPTFLAGS,OFCLRC+OFCLR0  RESET PADDING            GP11288
         B     DOPTS                                            GP11288
         SPACE 1                                                GP11288
OPATCH   TM    PRSFLAGS,PRSFGINT  INTEGER VALUE?                GP11288
         BZ    OPATCHH              NO; CHECK FOR HEX           GP11288
         LM    R0,R1,PRS#TINT     GET VALUE                     GP11288
         B     OPATCHCM                                         GP11288
OPATCHH  TM    PRSFLAGS,PRSFGHEX  HEXADECIMAL VALUE ?           GP11288
         BZ    BADSYN               NO; FAIL                    GP11288
         LM    R0,R1,PRS#THEX     GET VALUE                     GP11288
OPATCHCM LTR   R0,R0              USABLE ?                      GP11288
         BNZ   LONGOP               NO                          GP11288
         CL    R1,=X'00FFFFFF'    VALID ?                       GP11288
         BH    LONGOP               NO                          GP11288
         ST    R1,DEFPATCH        SET NEW DEFAULT               GP11288
         B     DOPTS                                            GP11288
         SPACE 1                                                GP11288
OPTSBXLE PARKEYBX OPTSTAB    DEFINE VALID OPTSS                 GP11288
OPTSTAB  PARKEYAD 'PATCH   ',OPATCH    SET PATCH SIZE           GP11288
OPTSTAB2 PARKEYAD 'RENT    ',ORENT     SET MODULES REENTRANT    GP11288
         PARKEYAD 'SIZE    ',OPATCH    SET PATCH SIZE           GP11288
         PARKEYAD 'REFR    ',ORENT     SET MODULES REENTRANT    GP11288
         PARKEYAD 'REUS    ',OREUS     SET MODULES REUSABLE     GP11288
         PARKEYAD 'NORENT  ',ONRENT    USE MODULES ATTRIBUTES   GP11288
         PARKEYAD 'NOREUS  ',ONRENT    USE MODULE ATTRIBUTES    GP11288
         PARKEYAD 'FILL00  ',OFILL0    PAD WITH X'00'           GP11288
         PARKEYAD 'FILLCC  ',OFILLC    PAD WITH X'CC'           GP11288
         PARKEYAD 'PAD00   ',OFILL0    PAD WITH X'00'           GP11288
         PARKEYAD 'PADCC   ',OFILLC    PAD WITH X'CC'           GP11288
         PARKEYAD 'NOFILL  ',ONFILL    NO PADDING               GP11288
OPTSTABN PARKEYAD 'NOPAD   ',ONFILL    NO PADDING               GP11288
         SPACE 1
*---------------------------------------------------------------------*
*   VER OR REP CARD - MUST HAVE HEX OFFSET + DATA                     *
*---------------------------------------------------------------------*
DVERIFY  DS    0H                                               GP11288
DREPLACE XC    LENPATCH,LENPATCH  RESET CURRENT PATCH LENGTH    GP11288
         ICM   R10,15,PRSLINK     GET OFFSET                    GP11288
         BZ    MISSFD               NONE; FAIL                  GP11288
         TM    PRSFLAGS,PRSFGHEX  HEX ADDRESS SUPPLIED?         GP11288
         BZ    NOTHEX               NO; INVALID                 GP11288
         ICM   R14,15,PRS#THEX    MORE THAN 32 BITS ?           GP11288
         BNZ   EXCESS               YES; INVALID                GP11288
         ICM   R4,15,PRS#THEX+4   GET LOW PORTION               GP11288
         CLR   R4,R3              LARGER THAN MODULE ?          GP11288
         BH    EXCESS               YES; FAIL                   GP11288
*---------------------------------------------------------------------*
*   HAVE OFFSET. NOW PARSE THE REMAINDER A SECOND TIME, ALLOWING      *
*   COMMA SEPARATION, AND HEX AS WELL AS QUOTED TEXT.                 *
*     DATA ARE ACCUMULATED IN ZAPDATA                                *
*---------------------------------------------------------------------*
         ICM   R10,15,PRSLINK     GET FIRST DATUM               GP11288
         BZ    MISSFD               NONE; FAIL                  GP11288
         L     R1,PRS@TEXT   ADDRESS OF TEXT START              GP11288
         BCTR  R1,0          ALLOW FOR POSSIBLE QUOTE           GP11288
         AR    R9,R8         END OF BUFFER + 1                  GP11288
         SR    R9,R1         LENGTH OF VER/REP TEXT             GP11288
         LR    R8,R1         SAVE IT                            GP11288
         PARSE (R8),(R9),OPT=(COMMA,UNQUOTE),PARM=PARPARM       GP11288
         BXH   R15,R15,FAILPARS     TOO BAD ???                 GP11288
         ICM   R10,15,PAR@TABL    GET FIRST HEX OR QUOTED TEXT  GP11288
         BZ    MISSFD               NONE; FAIL                  GP11288
         SPACE 1
LOOPDATA TM    PRSFLAGS,PRSFGUNQ  QUOTED,STRIPPED STRING?       GP11288
         BNZ   LOOPZSTR             YES; PROCESS AS TEXT        GP11288
         TM    PRSFLAGS,PRSFGHEX  HEX OPERAND SUPPLIED?         GP11288
         BZ    BADSYN               NO; USER ERROR?             GP11288
         ICM   R5,15,PRS#TEXT     R5=DATA LENGTH                GP11288
         BZ    MISSFD               ?                           GP11288
         EX    R5,EXISODD    EVEN STRING ?                      GP11288
         BNZ   NOTPAIR         NO; ODD                          GP11288
         LA    R6,PRS#THEX+L'PRS#THEX                           GP11288
         SRL   R5,1               GET NUMBER OF BYTES           GP11288
         SR    R6,R5              POINT TO HEX TEXT             GP11288
         LA    R1,ZAPDATA                                       GP11288
         L     R2,LENPATCH   GET LENGTH SO FAR                  GP11288
         AR    R1,R2                                            GP11288
         EX    R5,EXZAPMVC   ACCUMULATE ZAP DATA                GP11288
         AR    R5,R2                                            GP11288
         ST    R5,LENPATCH                                      GP11288
         B     BUMPDATA                                         GP11288
LOOPZSTR ICM   R5,15,PRS#TEXT     R5=DATA LENGTH                GP11288
         BZ    MISSFD               ?                           GP11288
         LA    R6,PRS$TEXT   STRIPPED TEXT LOCATION             GP11288
         LA    R1,ZAPDATA                                       GP11288
         L     R2,LENPATCH   GET LENGTH SO FAR                  GP11288
         AR    R1,R2                                            GP11288
         EX    R5,EXZAPMVC   ACCUMULATE ZAP DATA                GP11288
         AR    R5,R2                                            GP11288
         ST    R5,LENPATCH                                      GP11288
BUMPDATA TM    PRSSTOP,PRSSTBLK   STRING IS BLANK TERMINATED?   GP11288
         BNZ   REPVEREX      YES; DONE WITH THIS CARD           GP11288
         ICM   R10,15,PRSLINK     GET PATCH DATA                GP11288
         BNZ   LOOPDATA        MORE TO DO                       GP11288
REPVEREX L     R10,FT@LOAD   GET MODULE LOAD POINT              GP11288
         AR    R10,R4        GET OFFSET                         GP11288
         LR    R2,R5
         BCTR  R2,0
         CLI   VERBCODE,C'R' REPLACE?                           GP11288
         BE    REP             YES; GO TO IT                    GP11288
         SPACE 1
VERIFY   EX    R2,EXCLCDAT   COMPARE                            GP11288
         BE    MODIFYL
         ST    R10,DB
         ST    R5,DB+4
         PRTLIST BADVER                                         GP11288
         BAL   R10,DFLUSH    FAIL                               GP11288
         B     MODIFYL                                          GP11288
BADVER   FDPRT ' VERIFY FAILED AT',NL                           GP11288
         FDPRT DB,4,HEX,PADL                                    GP11288
         FDPRT ', LENGTH='                                      GP11288
         FDPRT DB+4,4,I                                         GP11288
         FDSNAP ZAPDATA,DUAL,NL,LEN=(R5)                        GP11288
         FDSNAP 0(R10),DUAL,NL,LEN=(R5)                         GP11288
         FDPRT *END                                             GP11288
         SPACE 1
REP      TM    FLAGS,FGFAIL   ANY ERROR ?                        83087
         BNZ   MODIFYL       YES; SKIP                           83087
         TM    FLAGS,FGZAPA  AUTHORIZED ?                       GP11288
         BZ    REP0          IF NO, NOT A PROBLEM               GP11288
         MODESET KEY=ZERO    TARGET MAY BE IN SP 252            GP11288
         EX    R2,EXMVCDAT                                      GP11288
         MODESET KEY=NZERO   RESTORE                            GP11288
         B     MODIFYL                                          GP11288
REP0     EX    R2,EXMVCDAT
         B     MODIFYL
EXCLCDAT CLC   0(0,R10),ZAPDATA                                 GP11288
EXMVCDAT MVC   0(0,R10),ZAPDATA                                 GP11288
EXZAPMVC MVC   0(0,R1),0(R6)                                    GP11288
EXISODD  TM    =X'01',*-*    TEXT FOR ODD BIT                   GP11288
         SPACE 1                                                GP02347
         LTORG ,                                                GP02347
         SPACE 1                                                 83087
ZAPIN    INPWORK ZAPIN,WIDTH=80,FILL=C' ',EDIT=128,EODAD=EOF     83087
ZAPOUT   PRTWORK ZAPOUT,TITLE=3                                  83087
CONSOLE  PRTWORK *CONSOLE                                        83087
HEADER   VCON  '#        FPC SUPERZAP AND EXECUTE MONITOR              *
                LOADZAP V5.0'                                   GP99188
NOGOMSG  VCON  ' NO GO SET DUE TO ABOVE CARD'                    83087
MGTOOMAN VCON  ' SUPERFLUOUS MEMBER REQUEST; TREATED AS LOAD'    83087
MGNOEP   VCON  ' REQUIRED MEMBER/NAME/DUMP CARD NOT RECEIVED'    83087
MGINPERR VCON  '0>>> UNRECOVERABLE INPUT ERROR <<<'              83087
MGLODBAD FDPRT '>>> BLDL/LOAD FAILED FOR',NL                     83087
         FDPRT PDS2NAME,DEB,PAD                                  83087
         FDPRT '<<<'                                             83087
         FDPRT *END                                              83087
MGEXEBAD FDPRT '>>> MODULE',NL
         FDPRT PDS2NAME,DEB,PAD
         FDPRT 'IS NOT EXECUTABLE <<<'
         FDPRT *END
MGFETBAD FDPRT '>>> MODULE',NL
         FDPRT PDS2NAME,DEB,PAD
         FDPRT 'FETCH FAILED. RC='
         FDPRT DB,4,I
         FDPRT ', REASON='
         FDPRT DB+4,4,HEX                                       GP02256
         FDPRT ' <<<'
         FDPRT *END
MGFETGUD FDPRT '>>> MODULE',NL                                  GP02256
         FDPRT PDS2NAME,DEB,PAD                                 GP02256
         FDPRT 'LOADED AT'                                      GP02256
         FDPRT FT@LOAD,HEX,PADL                                 GP02256
         FDPRT ', SIZE='                                        GP02256
         FDPRT MODSIZE,I                                        GP11288
         FDPRT '(',PADL                                         GP02256
         FDPRT MODSIZE+1,3,HEX                                  GP11288
         FDPRT '), SP='                                         GP11288
         FDPRT FTOFLGSP,IA,DEB                                  GP11288
         FDPRT ', ENTRY'                                        GP11288
         FDPRT FT@ENTRY,HEX,PADL                                GP02256
         FDPRT ', PATCH SIZE='                                  GP02256
         FDPRT MODPATCH,HEX                                     GP02256
         FDPRT ' <<<'                                           GP02256
         FDPRT *END                                             GP02256
         SPACE 1                                                 83087
PATDCB   DCB   DSORG=PO,DDNAME=LOADLIB,MACRF=(R)    1/2         GP02253
PATOLST  OPEN  (STEPDCB-STEPDCB,INPUT),MF=L         2/2         GP02253
PATDCBL  EQU   *-PATDCB                                         GP02253
         SPACE 1                                                 83087
TRTHEX   DC    256AL1(4)     ALL INVALID                        GP11288
         TRENT TRTHEX,0,(C'0',10)           DIGITS ARE VALID    GP11288
         TRENT ,0,(C'A',6),(X'81',6)        A-F, a-f VALID      GP11288
         SPACE 1                                                 83087
         PRINT &PRTSYS                                           83087
SAVE     DSECT ,                                                 83087
DB       DS    D             WORK WORD                           83087
         SERVDEFS ,          EXPAND GOODIES                     GP05060
ENTRY    DS    A             ENTRY ADDRESS FOR INVOKED PROGRAM   83087
DEFPATCH DS    F             DEFAULT PATCH SPACE                GP11288
MODSIZE  DS    F             MODULE SIZE BEFORE PATCH           GP11288
MODPATCH DS    F             MODULE PATCH SPACE                 GP02256
SAVEDEC  DS    A             DECODE RETURN SAVE                 GP11288
LENPATCH DS    F             PATCH LENGTH                       GP11288
FETPARM  FETWORK PFX=FT,DSECT=NO                                GP02255
FETPARML EQU   *-FETPARM                                        GP02255
         SPACE 1
         MAPPARSE DSECT=NO                                      GP11288
ALIAS    DC    CL8' '        USER REQUESTED ALIAS FOR NAME/LOAD GP06301
BLDLLIST DS    0A(0),Y(1,78)                                     83087
         IHAPDS PDSBLDL=YES,DSECT=NO                             83087
         ORG   PDS2+78                                           83087
         SPACE 1                                                 83087
FLAGS    DC    X'00'         PROCESSING FLAGS                    83087
FGHENT   EQU   X'80'           ENTRY POINT RECEIVED              83087
FGLOAD   EQU   X'40'           AT LEAST ONE LOAD DONE            83087
FGFAIL   EQU   X'20'           SUPPRESS EXECUTION DUE TO ERROR   83087
FGAUTH   EQU   X'10'           USER IS PRIVILEGED                83087
FGFREN   EQU   X'02'           FORCE NEXT LOAD RENT/REFR        GP11288
* NOTE THAT THE X'01' VALUE IS FIXED BY PGMHEAD ARCHITECTURE.
FGZAPA   EQU   X'01'           ENTERED AT LOADZAPA ENTRY
VERBCODE DS    C             FIRST LETTER OF LAST/CURRENT VERB   83087
OPTFLAGS DS    X             OPTION FLAGS                       GP11288
OFCLR0   EQU   FTOFCLR0        CLEAR/PAD WITH X'00'             GP11288
OFCLRC   EQU   FTOFCLRC        CLEAR/PAD WITH X'CC'             GP11288
OFFRENT  EQU   FTOFRENT        FLAG ALL MODULES RE-ENTRANT      GP11288
OFFREUS  EQU   FTOFREUS        FLAG ALL MODULES RE-USABLE       GP11288
ZAPDATA  DS    CL80          NEED 64, PLUS ??                   GP11288
         SPACE 1                                                 83087
@DCB     DS    F             0 OR ADDRESS OF OPENED STEPDCB     GP02253
STEPDCB  DCB   DSORG=PO,DDNAME=LOADLIB,MACRF=(R)    1/2         GP02253
STEPOLST OPEN  (STEPDCB,INPUT),MF=L                 2/2         GP02253
STEPDCBL EQU   *-STEPDCB                                        GP02253
         SPACE 1                                                 83087
SAVEEND  EQU   *             END OF WORK/SAVE AREA
         SPACE 2                                                 83087
         MAPPARST ,          DEFINE PARSE RESULTS               GP11288
         PRINT &PRTSYS                                           83087
CVTDSECT DSECT ,                                                 83087
         CVT   DSECT=YES                                         83087
         IHAPSA ,                                               GP02347
         IHACDE ,                                                83087
         IKJTCB ,                                                83087
         IEZJSCB ,                                               83087
         IEZDEB ,                                                83087
         DCBD  DEVD=DA,DSORG=PS                                  83087
         IEFUCBOB ,                                             GP02253
         END   ,
