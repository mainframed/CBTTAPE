DRIP     TITLE 'DISK RESIDENT IPL PROGRAM LOADER'
         MACRO
         CHECKUP
         GBLC  &CONSOLE(12),&CONTYPE(12),&START                 GP12046
         GBLB  &INSW
         GBLA  &POS,&ENTRY,&LENGTH,&WORKSZ,&LOADPT
         GBLA  &NUMCONS,&MAXCONS                                GP05202
         GBLC  &TEMP
         LCLA  &MULT                                            GP05202
         AIF   (&NUMCONS GT 0).CONLOOP                          GP05202
       MNOTE 8,'NO CONSOLES DEFINED - USING 3270/010 3215/009'  GP05202
&CONSOLE(1) SETC '010'       TURNKEY MASTER CRT                 GP05202
&CONTYPE(1) SETC '3270'                                         GP05202
&CONSOLE(2) SETC '009'       TURNKEY MASTER LINE MODE           GP05202
&CONTYPE(2) SETC '3215'                                         GP05202
&CONSOLE(3) SETC '011'       TURNKEY ALTERNATE                  GP10180
&CONTYPE(3) SETC '3270'                                         GP10180
&CONSOLE(4) SETC '01F'       TURNKEY ALTERNATE LINE MODE        GP10180
&CONTYPE(4) SETC '3215'                                         GP10180
&CONSOLE(5) SETC '0C0'       TURNKEY OPTIONAL                   GP10180
&CONTYPE(5) SETC '3270'                                         GP10180
&CONSOLE(6) SETC '1C0'       TURNKEY OPTIONAL                   GP10180
&CONTYPE(6) SETC '3270'                                         GP10180
&CONSOLE(7) SETC '700'       OS/390  MASTER                     GP10180
&CONTYPE(7) SETC '3270'                                         GP10180
&NUMCONS SETA  7                                                GP05202
.*
.CONLOOP AIF   (&MULT GE &NUMCONS).CONANY                       GP05202
&MULT    SETA  &MULT+1
         AIF   ('&CONSOLE(&MULT)' NE '').CONTYP
&CONTYPE(&MULT) SETC ''      SHOW NO TYPE - NO ADDRESS
.CONTYP  AIF   ('&CONTYPE(&MULT)' EQ '').CTYPEA
&TEMP    SETC  '&TEMP'.' '.'&CONSOLE(&MULT)'                    GP05202
         AIF   ('&CONTYPE(&MULT)' EQ '1052').CTYPEA
         AIF   ('&CONTYPE(&MULT)' EQ '3210').CTYPEA
         AIF   ('&CONTYPE(&MULT)' EQ '3215').CTYPEA
         AIF   ('&CONTYPE(&MULT)' EQ '3066').CTYPEB
         AIF   ('&CONTYPE(&MULT)' EQ '5450').CTYPEB
         AIF   ('&CONTYPE(&MULT)' EQ '3270').CTYPEC
         AIF   ('&CONTYPE(&MULT)' EQ '3158').CTYPEC
         AIF   ('&CONTYPE(&MULT)' EQ '4341').CTYPED              84134
         AIF   ('&CONTYPE(&MULT)' EQ '4381').CTYPED              92234
         MNOTE 4,'  CONSOLE DEVICE TYPE &CONTYPE(&MULT) NOT SUPPORTED,'
         MNOTE 4,'       1052 CONSOLE ASSUMED'
.CTYPEA  ANOP
&CONTYPE(&MULT) SETC  '0'    1052 BRANCH OFFSET
         AGO   .CONLOOP
.CTYPEC  ANOP  ,
&CONTYPE(&MULT) SETC '8'      3270 BRANCH OFFSET
         AGO   .CONLOOP
.CTYPEB  ANOP
&CONTYPE(&MULT) SETC  '4'    3066 BRANCH OFFSET
         AGO   .CONLOOP
.CTYPED  ANOP  ,                                                 84134
&CONTYPE(&MULT) SETC 'C'     4341 20-LINE 3278/79                84134
         AGO   .CONLOOP                                          84134
.*
.CONANY  MNOTE *,'  CONSOLE SUPPORT FOR : &TEMP'
         SPACE 1
         MNOTE *,'  THE SYSPUNCH DATASET CONTAINS IPL TEXT IN THE'
         MNOTE *,'      FORMAT REQUIRED FOR ICKDSF USING OPTION:'
         MNOTE *,' REFORMAT DDNAME() NOVFY VOLID() NOBOOTSTRAP IPLDD()'
         AIF   ('&START' EQ '').DEFLT
&START   SETC  '&START'.'        '
.*       THE FOLLOWING STATEMENTS CONVERT A HEX VALUE TO DECIMAL.
&POS     SETA  8
&LENGTH  SETA  8
&POS     SETA  8
&MULT    SETA  1
.CONT4A  ANOP
&POS     SETA  &POS-1
&TEMP    SETC  '&START'(&POS,1)
         AIF   ('&TEMP' GE '0').DEC
         AIF   ('&TEMP' EQ ' ').CONT4A
         AIF   ('&TEMP' NE 'A').HEXB
&TEMP    SETC  '10'
         AGO   .DEC
.HEXB    AIF   ('&TEMP' NE 'B').HEXC
&TEMP    SETC  '11'
         AGO   .DEC
.HEXC    AIF   ('&TEMP' NE 'C').HEXD
&TEMP    SETC  '12'
         AGO   .DEC
.HEXD    AIF   ('&TEMP' NE 'D').HEXE
&TEMP    SETC  '13'
         AGO   .DEC
.HEXE    AIF   ('&TEMP' NE 'E').HEXF
&TEMP    SETC  '14'
         AGO   .DEC
.HEXF    AIF   ('&TEMP' NE 'F').CONT4X
&TEMP    SETC  '15'
.DEC     AIF   ('&TEMP' LE '0').CONT4C
&ENTRY   SETA  (&TEMP*&MULT)+&ENTRY
.CONT4C  AIF   ('&POS' LE '1').CONT6
&MULT    SETA  &MULT*16
         AGO   .CONT4A
.CONT4X  MNOTE 4,'  START=&START INVALID, DEFAULT USED'          92234
.DEFLT   ANOP  ,             SET DEFAULT START PARMS
&START   SETC  '3B000'
&ENTRY   SETA  241664        236K
.CONT6   SPACE 1
         MNOTE *,'  DRIP WILL BE LOADED AT HEX &START'
         MEND
         SPACE 1
         MACRO ,                                          ADDED GP05202
&NM      CONDEF &CUU,&TYPE   DEFINE AN INPUT CONSOLE            GP05202
         GBLC  &CONSOLE(12),&CONTYPE(12)                        GP12046
         GBLA  &NUMCONS,&MAXCONS                                GP05202
         AIF   ('&CUU' NE '').ADDCON                            GP05202
         MNOTE 8,'CONSOLE ADDRESS REQUIRED'                     GP05202
         MEXIT ,                                                GP05202
.ADDCON  AIF   (&NUMCONS LE &MAXCONS).CONTEST                   GP12046
         MNOTE 4,'MORE THAN &MAXCONS CONSOLES - &CUU IGNORED'   GP05202
         MEXIT ,                                                GP05202
.CONTEST AIF   ('&TYPE' EQ '').CTYPED   **DEFAULT IS 3270**     GP05202
         AIF   ('&TYPE' EQ '1052').CTYPE                        GP05202
         AIF   ('&TYPE' EQ '3210').CTYPE                        GP05202
         AIF   ('&TYPE' EQ '3215').CTYPE                        GP05202
         AIF   ('&TYPE' EQ '3066').CTYPE                        GP05202
         AIF   ('&TYPE' EQ '5450').CTYPE                        GP05202
         AIF   ('&TYPE' EQ '3270').CTYPE                        GP05202
         AIF   ('&TYPE' EQ '3158').CTYPE                        GP05202
         AIF   ('&TYPE' EQ '4341').CTYPE                         84134
         AIF   ('&TYPE' EQ '4381').CTYPE                         92234
         MNOTE 4,'  CONSOLE DEVICE TYPE &TYPE NOT SUPPORTED,'   GP05202
         MNOTE 4,'       1052 CONSOLE ASSUMED'                  GP05202
&CONTYPE(&NUMCONS+1) SETC '1052'   GARBAGE CONSOLE TYPE         GP05202
         AGO   .CONADD                                          GP05202
.CTYPED  ANOP  ,                                                GP05202
&CONTYPE(&NUMCONS+1) SETC '3270'   DEFAULT CONSOLE TYPE         GP05202
         AGO   .CONADD                                          GP05202
.CTYPE   ANOP  ,                                                GP05202
&CONTYPE(&NUMCONS+1) SETC '&TYPE'  USER'S CONSOLE TYPE          GP05202
.CONADD  ANOP  ,                                                GP05202
&CONSOLE(&NUMCONS+1) SETC '&CUU'   CONSOLE ADDRESS              GP05202
&NUMCONS SETA  &NUMCONS+1                                       GP05202
         MEND  ,                                                GP05202
         EJECT
*
*        THIS PROGRAM IS INTENDED TO EASE THE PAINS OF RUNNING
*        PROGRAMS DESIGNED FOR CARD-READER OR TAPE IPL.
*
*        REQUIREMENTS :
*
*        ALLOCATE A SINGLE EXTENT PDS NAMED SYS1.IPLDECKS ON
*        ANY DISK NOT HAVING IPL TEXT.  FORMAT IS FIXED BLOCKED,
*        RECORD LENGTH OF 80, BLOCKSIZE TO OPTIMIZE (3520 ON 2314,
*        OR 3120 ON 3330; LARGER SIZES UP TO TRACK CAPACITY ARE
*        FULLY SUPPORTED).
*        MEMBERS OF THIS PDS WILL BE ALL DECKS TO BE IPL'D; NAMES
*        SHOULD BE MNEMONIC AND MEANINGFUL TO THE OPERATORS.
*        DECKS ARE PLACED IN THE PDS USING NORMAL OS BPAM/QSAM
*        IN THE FORM OF OBJECT DECKS FROM ASSEMBLIES, OR EXISTING
*        DECKS MAY BE USED.
*
*        EXISTING DECKS :
*        SEREP
*              THE SEREP DECKS REQUIRES SPECIAL PREPARATION
*              USE IEBGENER TO PLACE AN UNBLOCKED COPY OF SEREP ON A
*              DISK DATASET.  USE IEHDASDR OR IDCAMS TO PRINT HEX
*              DATA. (OR USE THE PRINTALL PROGRAM ALSO ON THIS TAPE).
*              UNLESS YOUR VERSION IS RADICALLY DIFFERENT, NOTE THAT
*              THE NEXT TO THE LAST CARD IS AN 'END' CARD CONTAINING
*              TWO LOW MEMORY ADDRESSES (IN OUR DECK, A030 AND 0850);
*              THE SECOND ADDRESS IS IN COLUMNS 30/31 - THIS IS THE
*              PROGRAM ENTRY POINT ADDRESS.  AFTER YOU HAVE COPIED
*              SEREP AS IS TO SYS1.IPLDECKS(SEREPXXX), SUPERZAP THE END
*              CARD COLUMNS 4-7 TO THIS ADDRESS (LEADING ZEROES).
*              NOTE THAT THE LAST CARD IN THE DECK CONTAINS EBCDIC
*              DATA; THE FIRST FOUR BYTES ARE THE PRINTER ADDRESS.
*              IN OUR DECK, THIS CARD IMAGE MUST BE ZAPPED INTO THE
*              TEXT CARDS LOADING ADDRESSES 0800-084F.  IF THESE
*              ADDRESSES ARE NOT CORRECT, THEY MAY BE FOUND BY UNDOING
*              THE ZAP, AND RUNNING DRIP REQUESTING SEREP.  SEREP WILL
*              BOMB DOING AN IPL READ FROM THE IPL DISK - CHECK THE
*              CAW IN LOW MEMORY TO GET THE CCW AND THE CORRESPONDING
*              INPUT ADDRESSES.  YOU MAY WISH TO MAKE MULTIPLE COPIES
*              OF THE SEREP DECK FOR EACH PRINTER (IN CASE YOUR PRIMARY
*              PRINTER IS DOWN) - JUST ZAP THE ADDRESS AFTER RENAMING
*              AN ADDITIONAL COPY.  NO DRIP SUPPORT FOR THIS IS PLANNED
*           SEREP NAMES ARE OBTAINED DYNAMICALLY - A STORE CPU ID
*              INSTRUCTION IS ISSUED - IF IT FAILS, INPUT OF 'SEREP'
*              IS TREATED AS SEREP360; ELSE THE CPU MODEL IS
*              USED IN PLACE OF 360, E.G. SEREP165.
*
*        CLIP, DEBE, STAND-ALONE DUMP/RESTORE AND DASDR CONTAIN IPL
*        BOOTSTRAP CARDS, A BPS LOADER DECK AND THE PROGRAM DECK.
*        REMOVE ALL CARDS UP TO, BUT NOT INCLUDING THE FIRST PROGRAM
*        TEXT CARD.  THE 'END' CARD MUST BE PRESENT AND SHOULD BE THE
*        LAST IN THE DECK; IF NECESSARY, AN ENTRY ADDRESS MUST BE
*        ZAPPED IN.  FOR MOST PROGRAMS, THIS ADDRESS WILL BE HEX 1000.
*              IF FINISHED WHEN THIS TAPE IS PREPARED, A VERSION OF
*              CLIP AND IMDSADMP WILL BE INCLUDED UNDER THE NAMES
*              DRIPCLIP AND DRIPDUMP.  THIS VERSION OF CLIP WILL
*              PROVIDE 3270 CONSOLE SUPPORT, UPPER CASE TRANSLATE ON
*              ALL INPUT, AND HOPEFULLY OTHER IMPROVEMENTS.
*
*        AS A TEMPORARY EXPEDIENT, WHEN A PROGRAM NAME BEGINS WITH
*        'SA', THE PROGRAM IS INVOKED IN OS/390 MODE (E.G., SACLIP)
*
*        FOR ASSEMBLY DECKS, 'START ADDR' MUST BE USED IN PLACE OF
*        'CSECT'; THE 'ADDR' BEING THE ABSOLUTE LOAD ADDRESS.
*        THIS ADDRESS, AND NONE OF THE PROGRAM, MAY BE HIGHER THAN
*        236K IN MEMORY.
*        THE ASSEMBLY 'END' CARD MUST SPECIFY AN ENTRY POINT NAME.
*        THE DRIP PROGRAM WILL NOT PERFORM LINKAGE-EDITOR NOR LOADER
*        FUNCTIONS; ALL TEXT IS LOADED INTO THE ADDRESSES SPECIFIED
*        ON TEXT CARDS, AND CONTROL WILL BE GIVEN TO THE ENTRY POINT
*        ON THE END CARD.  NO RELOCATION OF RLD RECORDS IS PERFORMED.
*
*        OPERATION :
*        THIS PROGRAM IS LOADED AT 236K; MEMORY IS NOT CLEARED.
*        THE OPERATOR IS PROMPTED FOR THE NAME OF THE PROGRAM TO BE RUN
*        DATASET 'SYS1.DRIPLOAD' IS LOCATED ON THE IPL VOLUME AND THE
*        REQUESTED MEMBER IS SCANNED FOR.  IF FOUND, TEXT CARDS ARE
*        READ INTO MEMORY UNTIL AN END CARD IS ENCOUNTERED.  LOW MEMORY
*        IS SET TO THE IPL UNIT ADDRESS (LOC 2-3), THE ENTRY POINT
*        (LOC 5-7), THE TIMER IS SET TO FFFFFFFF TO DISABLE IT, AND
*        TIMER+4 IS USED TO STORE THE CONSOLE ADDRESS/TYPE; THE
*        REMAINDER OF MEMORY OUTSIDE THE DRIP PROGRAM IS NOT CHANGED.
*        CONTROL IS GIVEN TO THE ENTRY POINT SPECIFIED ON THE END CARD.
*        IF 'WAIT' WAS SPECIFIED, CONTROL TRANSFER WILL BE DELAYED
*        UNTIL AN EXTERNAL INTERRUPT IS ISSUED.
*
*        PREPARATION :
*        ASSEMBLE THIS PROGRAM, OVERRIDING LOCAL OPTIONS WHERE DESIRED.
*        THE OBJECT DECK FROM THIS ASSEMBLY IS FED INTO AN ICKDSF STEP
*        THAT WRITES THE IPL TEXT TO THE SPECIFIED DISK. ALL PROGRAMS
*        TO BE LOADED ARE PLACED IN OBJECT DECK FORMAT INTO DATA SET
*        SYS1.IPLDECKS ON THAT VOLUME.
*        THE DISK PACK MAY BE USED ON ANY ADDRESS. THIS SECTION OF CODE
*        HAS BEEN LIFTED FROM IBM'S IMDSADMP PROGRAM, WHICH IS
*        DESCRIBED IN THE 'SERVICE AIDS' MANUAL.
*        MOST OF THE CODE HAS BEEN LIFTED FROM THE OS IPL PROGRAM
*        IEAIPL.  WARNING :  THIS PROGRAM IS LIMITED TO A MAXIMUM
*        SIZE OF HEX 'E29' TO FIT ON A 2311 IPL TRACK, AND SHOULD
*        NOT EXCEED THIS SIZE ON OTHER DEVICES UNLESS CHECKED BEFORE
*        CLOBBERING A GOOD DISK.
*
*        THE PROGRAM SUPPORTS MULTIPLE CONSOLES; THE BASIC TYPES
*        ALSO SUPPORTED BY IMDSADMP (1052/3215 AND 3066/5450), AND
*        THE ADDED 327x MOD 2 CRT.  CONSOLES WILL BE USED IN THE
*        ORDER IN WHICH THEY ARE DEFINED IN &CONS; A NOT-READY OR
*        PROLONGED 'BUSY' CONDITION WILL CAUSE ANOTHER CONSOLE TO BE
*        USED.
*                                                                84134
*        A CONSOLE TYPE OF '4341' SHOULD BE USED FOR A 20-LINE   84134
*          3278 OR 3279 ON A 43XX PROCESSOR.                     84134
*
*        PLEASE DIRECT COMMENTS AND QUESTIONS CONCERNING THIS PROGRAM
*        TO                  G. Postpischil
*                            Expert System Programming
*                            176 Old Stage Coach Road
*                            Bradford, VT 05033-8844
*                            gerhard@postpischil.com
*
         SPACE 2
*EXITS  -ERROR-    ERROR CONDITIONS RESULT IN A WAIT PSW BEING LOADED *
*    WITH THE FOLLOWING ERROR CODES STORED IN THE ADDRESS FIELD OF THE*
*    PSW FOR DISPLAY IN THE CONSOLE LIGHTS-                           *
*  ERROR CODE-  1  (0001)    I/O IS NOT OPERATIONAL.                  *
*               2  (0010)    I/O OPERATION IS NOT INITIATED. CSW IS   *
*                            STORED BUT UNIT IS NOT BUSY.             *
*               3  (0011)    I/O OPERATION IS NOT INITIATED. CSW IS   *
*                            NOT STORED AND THE CHANNEL IS NOT BUSY.  *
*               4  (0100)    CHANNEL IS NOT BUSY AND CSW IS NOT STORED*
*               5  (0101)    UNIT CHECK CONDITION IS INDICATED.       *
*               6  (0110)    ANY OF THE FOLLOWING CONDITIONS -        *
*                              PROGRAM CHECK                          *
*                              CHANNEL DATA CHECK                     *
*                              CHANNEL CONTROL CHECK                  *
*                              CHANNEL CHAINING CHECK                 *
*                              INTERFACE CONTROL CHECK                *
*              18  (00011000) AVAILABLE SPACE FOR RLD RCDS HAS BEEN
*                            EXCEEDED.                                *
*            C0XX  (NNNNNNNN) UNEXPECTED PGM CHK INTERRUPT, IPL
*                            CONTAMINATED.                            *
*                                                                     *
*            FFFF     NO CONSOLE OPERATIONAL
*
*        NUMBERED MESSAGES
*
*        'ERROR CODE X'  X=
*
*              2 - TEXT ADDRESS OVERLAPS DRIP MEMORY
*              3 - NO VALID TEXT CARD WAS READ
*              4 - END CARD NOT FOUND OR END CONTAINS NO ENTRY ADDRESS
*
*        IPL TRACK CREATION WAIT STATE CODES - C900XX
*
*              01 - NORMAL COMPLETION
*              04 - VTOC OVERLAPS IPL TRACK(S)
*              08 - INSUFFICIENT TRACK SPACE TO WRITE
*            F0F0 - I/O ERROR
         EJECT
         GBLC  &CONSOLE(12),&CONTYPE(12),&START                 GP12046
         GBLB  &INSW
         GBLA  &POS,&ENTRY,&LENGTH,&WORKSZ,&LOADPT,&CURCONS     GP12046
         GBLA  &NUMCONS,&MAXCONS  CONSOLES DEFINED/MAX ALLOWED  GP05202
         GBLC  &TEMP
&MAXCONS SETA  12            MATCHES GBLC FOR CONSOLE/CONTYPE   GP05202
*
***********************************************************************
*
*        ASSEMBLY OPTIONS
*
*
&START      SETC '3B000'     PROGRAM LOAD ADDRESS = 236K
*
         CONDEF 010,3270     HERCULES TURNKEY MVS DEFAULT       GP05202
         CONDEF 011,3270       -"- ALTERNATE CONSOLE            GP05202
         CONDEF 009,1052     COMMON LINE MODE CONSOLE           GP05202
         CONDEF 01F,3215     OTHER COMMON LINE MODE ADDRESS     GP05202
         CONDEF 0C0,3270     IN CASE CONSOLES DOWN ?            GP05202
         CONDEF 1C0,3270     IN CASE CONSOLES DOWN ?            GP05202
         CONDEF 0C1,3270     IN CASE CONSOLES DOWN ?            GP05202
         CONDEF 1C1,3270     IN CASE CONSOLES DOWN ?            GP05202
         CONDEF 700,3270     OS/390 MASTER                      GP10180
*
&WORKSZ  SETA  3625          MAXIMUM IPL PROGRAM/RECORD SIZE
*
***********************************************************************
         SPACE 2
         CHECKUP ,           CHECK ASSEMBLY OPTIONS
         EJECT
DRIPLOAD START 0             START AT ABSOLUTE ZERO, THEN RELOCATE
         SPACE 1
LOWPAGE  DS    0D ,          SET START ADDRESS FOR IPL DATA PGM
LP       EQU   LOWPAGE         SHORTER FORM OF LOWPAGE           92235
PSWRSTRT DS    D'0'          IPL/RESTART PSW
IPLCCW1  DS    D'0'          IPL CCW - FIRST
IPLCCW2  DS    D'0'          IPL CCW - SECOND
PSOEXT   DS    D'0'          OLD PSW - EXTERNAL INTERRUPT
PSOSVC   DS    D'0'          OLD PSW - SUPERVISOR CALL
PSOPGM   DS    D'0'          OLD PSW - PROGRAM CHECK
PSOMCC   DS    D'0'          OLD PSW - MACHINE CHECK
PSOIOP   DS    D'0'          OLD PSW - INPUT/OUTPUT INTERRUPTION
CSW      DS    D'0'          CHANNEL STATUS WORD
CAW      DS    F'0'          CHANNEL ADDRESS WORD
OSCVT    DS    F'0'          OS CVT REFRESH
TIMER    DS    F'0'          INTERVAL TIMER
TIMER2   DS    F'0'          UNUSED
PSWEXT   DS    D'0'          NEW PSW - EXTERNAL INTERRUPT
PSWSVC   DS    D'0'          NEW PSW - SUPERVISOR CALL
PSWPGM   DS    D'0'          NEW PSW - PROGRAM CHECK
PSWMCC   DS    D'0'          NEW PSW - MACHINE CHECK
PSWIOP   DS    D'0'          NEW PSW - INPUT/OUTPUT
LOGOUT   EQU   *            VARIABLE SIZE/LOCATION MACHINE CHECK LOGOUT
LOWLEN   EQU   *-LOWPAGE     SIZE OF MEMORY TO BE CLEARED
         SPACE 1
*   THE FOLLOWING ARE MISCELLANOUS EQUATES                      *
R0       EQU   0 .                 PLACE-MARK FOR STM TO SAVE ALL REGS
R1       EQU   1
R2       EQU   2 .                 LAST REAL LINE PRINTED
R3       EQU   3 .                 FALSE POINTER
R4       EQU   4 .                 INCREMENT FOR BXLE
R5       EQU   5 .                 LIMIT VALUE OF BXLE
R6       EQU   6 .                 OUTPUT AREA POINTER
R7       EQU   7 .                 SECONDARY LINKAGE REGISTER
R8       EQU   8 .                 WORK REG
R9       EQU   9 .                 COMPLETION CODE POINTER
R10      EQU   10 .                EVEN/ODD PAIR IN CONVER -
R11      EQU   11 .                I/O DEVICE ADDRESS
R12      EQU   12 .                WORK REG
R13      EQU   13 .                CCW ADDRESS - WORK REG
R14      EQU   14 .                LINKAGE REGISTER
R15      EQU   15 .                BASE REGISTER
Q0       EQU   0
Q1       EQU   1
Q2       EQU   2
Q3       EQU   3
Q4       EQU   4
Q5       EQU   5
Q6       EQU   6
Q7       EQU   7
Q8       EQU   8
Q12      EQU   12
Q16      EQU   16
Q17      EQU   17
Q20      EQU   20
Q24      EQU   24
ECFCNTOP EQU   X'10'               MASK FOR CCW COUNT OP CODE
ECFDTACK EQU   X'08'               MASKS DATA CHK BIT IN SENSE
ECFTIC   EQU   X'08'               RESETS TIC CCW COMMAND
SNSZEUSA EQU   X'35'               ZEUS-A SENSE BYTE ID
SNSZEUSC EQU   X'34'               ZEUS-C SENSE BYTE ID
         SPACE 1
DC       EQU   X'80'         CCW - DATA CHAINING BIT
CC       EQU   X'40'         CCW - COMMAND CHAINING
SLI      EQU   X'20'         CCW - SUPPRESS LENGTH INDICATION
NOR      EQU   X'10'         CCW - SUPPRESS READ / DO VERIFY ONLY
         EJECT
         USING LOWPAGE,R0    DECLARE LOW MEMORY
STARTDEV DS    0D            ENTRY FOR ICKDSF DECK               92218
         ORG   PSWPGM                                            92236
         DC    A(0,ENTRY-LP) START OUT ON THE RIGHT FOOT         92236
         ORG   STARTDEV                                          92236
         SPACE 1                                                 92218
ENTRY    ST    R15,TIMER2          SAVE REG 15 AT ABSOLUTE LOCATION
*                                  BEFORE ESTABLISHING ADDRESSABILITY
         BALR  R15,0 .             BASE DUMP PROGRAM
         USING *,R15         TEMPORARY
         STM   R0,R14,SAVE .       SAVE ALL REGS
         LA    R1,6          I LIKE BASE = LOAD ADDRESS
         SR    R15,R1        CORRECT BASE TO LOAD POINT
         USING ENTRY,R15     DECLARE IT
         MVC   SAVE+60(4),TIMER2   MOVE ORGINAL REG 15 CONTENT TO
         MVC   TIMER,FFFFFFFF  KILL THE TIMER
         MVC   MEMSAVER(LOWLEN),LOWPAGE  SAVE LO MEMORY
         XC    LOWPAGE+8(LOWLEN-8),LOWPAGE+8  CLEAR LOW MEMORY
         MVC   TIMER,FFFFFFFF  KILL THE TIMER
         LA    R0,MODE370    GET CONTINUATION POINT             GP05208
         ST    R0,GORUN370+4  SET ADDRESS                       GP05208
         LPSW  GORUN370                                         GP05208
GORUN370 DC    0D'0',A(0,MODE370)  SWITCH TO 370 MODE           GP05208
MODE370  LM    R2,R3,RELOADLN  GET RELOCATION ADDRESS AND LENGTH 92235
         LR    R6,R2         SAVE NEW BASE ADDRESS               92235
*OLD*    AIF   ('&IPL' NE 'ICKDSF').NORELO                       92235
         SLR   R4,R4         START AT 'LP'                       92235
         LR    R5,R3         COPY LENGTH TO MOVE                 92235
         MVCL  R2,R4         MOVE PROGRAM TO HIGHER STORAGE      92235
         LM    R3,R5,RELOC8  GET BXLE LIST FOR CCWS              92235
         BAL   R9,RELOCSUB   CALL SUBROUTINE                     92235
         LM    R3,R5,RELOCP  GET BXLE LIST FOR PSWS              92235
         BAL   R9,RELOCSUB   CALL SUBROUTINE                     92235
         LM    R3,R5,RELOC4  GET BXLE FOR OTHER RELOCATION DATA  92235
         BAL   R9,RELOCSUB   CALL SUBROUTINE                     92235
         ALR   R15,R6        SET NEW BASE                        92235
         B     REDO          START PROCESSING                    92235
RELOADLN DC    A(&ENTRY,IEABUFER-LP)  DESTINATION ADDRESS/LENGTH 92235
RELOC8   DC    A(RELCCW1-LP,8,RELCCWL-8-LP)  CCW RELOCATION      92235
RELOCP   DC    A(RELPSW1+4-LP,8,RELPSWL-4-LP)  PSW RELOCATION    92235
RELOC4   DC    A(RELADD1-LP,4,RELADDL-4-LP)  ADDRESS RELOCATION  92235
         SPACE 1                                                 92235
RELOCSUB CR    R3,R5         ANYTHING TO DO ?                    92235
         BHR   R9            RETURN IF NOTHING TO DO             92235
         ALR   R3,R6         RELOCATE THE LIST ENTRIES           92236
         ALR   R5,R6                                             92236
RELOCUP  L     R0,0(,R3)     LOAD VALUE                          92235
         ALR   R0,R6         ADD BASE OFFSET                     92235
         ST    R0,0(,R3)     STASH BACK                          92235
         BXLE  R3,R4,RELOCUP DO NEXT ENTRY                       92235
         BR    R9            RETURN WHEN DONE                    92235
         SPACE 1                                                 92235
REDO     MVC   PSWPGM+4(4),ADIEAINT   SET PROGRAM CHECK NEW PSW ADDRESS
         LA    R1,IOINRPT    SET UNSOLICITED I/O INTERRUPT PGM ADDR
         ST    R1,PSWIOP+4   SET IN I/O NEW PSW
         LA    R1,INTERUPT   SET EXTERNAL INTERRUPT HANDLER ADDRESS
         ST    R1,PSWEXT+4   SET EXTERNAL HANDLER
         MVI   WAITFLAG,0    CLEAR WAIT FLAG
         MVC   PSWINTER,PSWINNEW       SET EXT. INTERRUPT TO COME HERE
         LA    R14,REDO360   SET PGM CHK ADDR FOR 360
         DC    X'B202',S(MICPUID)  STIDP
         UNPK  MISER+5(4),MICPUID+4(3)    CPU MODEL INTO EBCDIC
REDO360  LA    R14,PGMCHK    SET BACK TO UNEXPECTED PROGRAM CHECK
         SSM   CONACCW       ENABLE EXT. INTERRUPTS ON FIRST ENTRY
         B     GETANAME      GET A PROGRAM NAME
         SPACE 2
NOTFOUND MVC   MESSAGE(PROMF),PROGNAM  SHOW PREVIOUS NAME NOT FOUND
         B     CONSCOM
         SPACE
GETANAME MVI   MESSAGE,C' '   CLEAR MESSAGE AREA OUT
         MVC   MESSAGE+1(L'MESSAGE-1),MESSAGE
         MVC   MESSAGE(L'PROMPT),PROMPT  WRITE STANDARD PROMPT
         MVI   CONACCW,1     REBUILD CCW, JUST IN CASE
         MVI   CONACCW+4,CC+SLI
         SPACE
CONSCOM  LH    R1,CONSPGM    GET OFFSET FOR THIS CONSOLE TYPE
         L     R13,CONWAD(R1) SET WRITE CCW BY TYPE
         CLI   CONSPGM+1,0   IS IT A 1052 ?
         BNE   CONSCOMY      NO - NEED ONLY CCW
         SPACE
         LA    R1,L'MESSAGE  FOR 1052, ELIMINATE TRAILING BLANKS
         LA    R2,MESSAGE+L'MESSAGE
CONSCOM1 BCTR  R2,0          GET PREVIOUS CHAR
         CLI   0(R2),C' '    TRAILING BLANK ?
         BNE   *+12          NO; R1 HAS CORRECT LENGTH
         BCT   R1,CONSCOM1   TRY AGAIN
         LA    R1,1          SET FOR A MINIMUM OF ONE BLANK
         STH   R1,CONACCW+6  SET NEW LENGTH
         SPACE
CONSCOMY ST    R13,CONSCAW   SAVE ADDRESS OF WRITE CCW
         SPACE
CONSPGMY L     R13,CONSCAW   GET WRITE CCW ADDRESS
         L     R1,CONDELAY   GET TIO DELAY BEFORE QUITTING THIS CONS
CONSPGMZ LH    R10,CONSLAD   GET CONSOLE ADDRESS
         LTR   R10,R10       IS THERE ONE ?
         BNP   KILLKILL      NO, FAIL
         BAL   R9,TIOLOOP    WAIT FOR CONSOLE AVAILABILITY       84134
         BC    1,CONSALT1    IF CC=3, WAIT FOR ACTION            84134
         B     CONSSIO       CSW STORED                          84134
CONSALT1 BCT   R1,CONSPGMZ   GIVE IT ANOTHER CHANCE
CONSALT  MVC   CONSTABL(CONSTABS-CONSTABL),CONSTABL+4  KILL PREVIOUS
         B     CONSCOM       TRY AGAIN
         SPACE
CONSSIO  XC    PROGNAM,PROGNAM   CLEAR INPUT AREA
         LA    R8,CONSIN     SET INLINE RETURN
CONSSIOS ST    R13,CAW       SET CHANNEL PROGRAM ADDRESS
         BAL   R9,TIOLOOP    WAIT FOR DEVICE AVAILABILITY        84134
         BC    1,CONSALT1    CC=3, COUNT                         84134
         SIO   0(R10)        START UP
         BC    3,CONSSIOS    BUSY - RETRY                        84134
         BZ    CONSTIO       OK - WAIT
         TM    CSW+4,X'10'   BUSY ?                              84134
         BNZ   CONSSIOS+4    YES, TRY AGAIN                      84134
         B     CONSCT        ELSE CHECK OTHER STATUS             84134
         SPACE 1
CONSTIO  BAL   R9,TIOLOOP    GET DEVICE CLEARED                  84134
         B     CONSTIO       NO CSW STORED - TRY AGAIN           84134
CONSCT   CLI   CSW+5,0       CHANNEL/IF DATA CHECK OR OTHER      84134
         BNE   CONSSIOS      CATASTROPHIC ERROR                  84134
         TM    CSW+4,X'02'   UNIT CHECK ?                        84134
         BNZ   CONSALT       YES - RETRY
         TM    CSW+4,X'85'   DEVICE END OR UNIT EXCEPTION ?      84134
         BNZR  R8            YES; HANDLE SPECIFICS               84134
         B     CONSTIO       WAIT FOR A CSW I LIKE               84134
         SPACE
CONSIN   LH    R1,CONSPGM    GET BRANCH OFFSET FOR DEVICE
         B     *+4(R1)
         B     CONSPOSA      1052
         B     CONSPOSB      3066
         B     CONSPOSC      3270
         B     CONSPOSC      4341 20-LINE 3278/79                84134
         SPACE
CONSPOSA TM    CSW+4,X'01'   UNIT EXCEPTION (CANCEL) ?
         BNZ   GETANAME      YES - RETRY
         B     DISKGET       ELSE LOOK AT INPUT
         SPACE
CONSPOSB TM    CSW+4,X'80'   ATTENTION ?
         BZ    CONSTIO       NO; WAIT FOR IT
         LA    R13,CONBCCWR  GET READ CHANNEL PROGRAM
         BAL   R8,CONSSIOS   READ INPUT
         LA    R13,CONBCCW   RESTORE CCW ADDRESS
         TM    CONBINP+2,X'80'   'ENTER' ?
         BZ    GETANAME      AND RETRY
         B     DISKGET       ELSE PRCESS NAME
         SPACE
CONSPOSC TM    CSW+4,X'80'   ATTENTION STATUS ?
         BZ    CONSTIO       NO; WAIT
CONSPOS3 LA    R13,CONCCCWR  GET READ CCW
         BAL   R8,CONSSIOS   READ MODIFIED INPUT
         LA    R13,CONCCCW   RESTORE CCW WRITE
         CLI   CONCINP,X'60'  ANY AID ?
         BE    CONSPOS3      NO; REDO THE READ
         CLI   CONCINP,X'7D'  'ENTER' ?
         BNE   GETANAME      NO; REDO
*  NOTE THAT 3270 INPUT MAY CONTAINS NULLS, OR ANOTHER SBA.
*  CHECK FOR CHARACTER LESS THAN SPACE AND CLEAR REMAINDER
         ICM   R8,15,PROGNAM    GET LEFT HALF                   GP05208
         ICM   R9,15,PROGNAM+4  GET RIGHT                       GP05208
         XC    PROGNAM,PROGNAM  CLEAR RESULT                    GP05208
         LA    R13,L'PROGNAM PROCESS ALL                        GP05208
         LA    R1,PROGNAM    POINT TO OUTPUT LOCATION           GP05208
LADJLOOP CLM   R8,8,BLANKS   LOWER THAN BLANK ?                 GP05208
         BL    DISKGET       YES; TRUNCATE INPUT HERE           GP05208
         BE    LADJSKIP      SKIP LEADING/TRAILING SPACE        GP05208
         STCM  R8,8,0(R1)    STASH IT                           GP05208
         LA    R1,1(,R1)     ADVANCE OUTPUT                     GP05208
LADJSKIP SLDL  R8,8          SHIFT INPUT                        GP05208
         BCT   R13,LADJLOOP  TRY AGAIN                          GP05208
         SPACE
DISKGET  OC    PROGNAM,BLANKS   MAKE UPPER CASE
         CLC   PROGNAM,BLANKS   ALL BLANK ?
         BE    CONSCOM       YES - RETRY
         MVI   MESSAGE,C' '  CLEAR MESSAGE AREA
         MVC   MESSAGE+1(L'MESSAGE-1),MESSAGE  CLEAR IT ALL
         SPACE 1
         CLC   MISER(5),PROGNAM   REQUEST FOR 'SEREP' ?
         BNE   *+10          NO
         MVC   PROGNAM(8),MISER   USE CPU DEPENDENT SEREP
         CLC   WAITYES,PROGNAM  REQUEST FOR WAIT ?
         BNE   DISKNWAT      NO
         OI    WAITFLAG,1    SET WAIT REQUEST
         B     GETANAME      GET THE PROGRAM
DISKNWAT CLC   WAITNO,PROGNAM   REQUEST TO KILL WAIT ?
         BNE   VOLREAD       NO - START READING DISK
         NI    WAITFLAG,254  TURN WAIT REQUEST OFF
         B     GETANAME
         SPACE 1                                                 84134
*        CODE ADDED TO SUPPORT 3270 CRTS ON BLOCK MPX CHANNELS   84134
*                                                                84134
TIOLOOP  LM    R5,R6,PSWIOP  SAVE OLD INTERRUPT ADDRESS          84134
         LA    R2,CONIOINT   GET MY INTERRUPT ADDRESS            84134
         ST    R2,PSWIOP+4   STASH IT                            84134
TIOLOOP1 XC    CSW,CSW       CLEAR CSW STATUS                    84134
         TIO   0(R10)        TRY TO ACCESS THE CONSOLE           84134
         BC    9,TIOLOOPZ    AVAILABLE (8) OR CC=3 (1)           84134
         BC    4,TIOLOOPX    CSW STORED                          84134
         SSM   FFFFFFFF      ENABLE FOR INTERRUPTS               84134
         SSM   CONSPOSA+1    DISABLE EXCEPT EXTERNAL             84134
         B     TIOLOOP1      LOOP UNTIL NOT BUSY                 84134
TIOLOOPZ STM   R5,R6,PSWIOP  RESTORE I/O INTERRUPT ADDRESS       84134
         BR    R9            RETURN TO CALLER                    84134
CONIOINT CH    R10,PSOIOP+2  INTERRUPT FROM OUR CONSOLE ?        84134
         BNE   TIOLOOP1      NO; CLEAR CSW AND TRY AGAIN         84134
TIOLOOPX TM    CSW+4,X'70'   SM/CU END/BUSY ?                    84135
         BZ    TIOLOOPY      NO; EXIT                            84135
         LA    R0,4095       GET A LARGE NUMBER                  84135
         SLL   R0,2          MAKE IT LARGER                      84135
TIOLOOPT SSM   FFFFFFFF      ENABLE FOR INTERRUPTS               84135
         SSM   CONSPOSA+1    DISBLE                              84135
         BCT   R7,TIOLOOPT   LOOP UNTIL NOT BUSY                 84135
TIOLOOPY STM   R5,R6,PSWIOP  RESTORE OLD I/O INTERRUPT           84134
         B     4(,R9)        RETURN WITH CSW CODE                84134
         EJECT
VOLREAD  LA    R14,ENDSSK    LOAD FOR PC ON STORAGE KEY          92218
         XR    R3,R3         SUPERVISOR KEY OF ZERO
         LA    R4,2048                STORAGE BLOCK NUMBER (1 IN BIT 20
         LA    R2,0(R4,R4)   SKIP FIRST BLOCK OF STORAGE         92218
IEAKYLP  SSK   R3,R2         SET STORAGE KEY
         AR    R2,R4
         ST    R2,MAXMEM     SET HIGHEST MEMORY ADDRESS + 1
         C     R2,=X'1000000'  MAX OF 16M REACHED ?             GP05202
         BL    IEAKYLP       NO; TRY AGAIN
IEAINT   BR    R14           RETURN FROM PC INTERRUPT
         SPACE 1                                                 92218
ENDSSK   ICM   R4,15,MAXMEM  WAS ANYTHING STORED ?               92218
         BNZ   IEAPCKEY      YES                                 92218
         MVI   MAXMEM,1      FAKE IT AS 16 MEGABYTES             92218
******************* IPL PDS LOCATION ROUTINE **************************
*                                                                     *
*  LOCATES THE IPL PDS ON THE IPL DEVICE BY READING THE
* STANDARD VOLUME LABEL TO FIND THE VTOC AND THEN SEARCHING THRU THE
* VTOC DSCB'S UNTIL IT FINDS THE ONE REFERING TO THE NAME OF THE
* IPL PDS. THE PDS DIRECTORY IS THEN READ AND THE PROGRAM WITH THE
* DESIRED NAME IS SEARCHED FOR. WHEN FOUND, THE FIRST RECORD'S COUNT
* FIELD IS READ; A SUBSEQUENT READ WILL BRING IN A BLOCK AND THE COUNT
* FIELD OF THE NEXT BLOCK (OR ZERO FOR EOF).  THE RECORD MAY BE UP TO
* FULL TRACK CAPACITY IN SIZE.  NOTE THAT I/O ERRORS MAY RESULT IF
* THE MEMBER IS NOT TERMINATED BY AN END-FILE OR AN END CARD. I/O
* ERRORS MAY ALSO RESULT ON A BAD TRACK; OR A BAD TRACK MAY RESULT IN
* PROGRAM ERRORS IF THE M/T READ COUNT SKIPS A BAD TRACK ENTIRELY.
*                                                                     *
IEAPCKEY LA    R14,PGMCHK    SET PROGRAM CHECK ADDRESS
         LH    R10,PSWRSTRT+2  SYS RES ADDR STORED BY IPL HARDWARE
*CLEAR PENDING IO CONDITIONS
IEAIOTST TIO   0(R10)        TEST IO
         BC    1,IEAERR1     IO NOT OPERATIONAL,ERROR1
         BC    2,IEAIOTST    CHANNEL BUSY, TEST IO
*READ STD VOLUME LABEL
         XC    IEANUCDS(8),IEANUCDS  CLEAR EXTENTS FOR VTOC
         MVI   IEANUCDS+4,X'FF'  SET PHONY END CC FOR VTOC
         MVC   IEASKADR(6),IEAOTRCK     SEEK ADDR=ZERO CYL,ZERO TRACK
         MVC   IEASRADR(5),IEAREC3      SRCH ADDR=RECORD3 OF ZERO TRACK
         MVC   CAW,IEALBLST           MOVE CHNL PROG ADDR INTO CAW
         BAL   R5,IEASTRIO     READ STD VOL LABEL INTO BUFFER
*READ VTOC DSCB DATA
         OI    IEARREC1,X'02'  SHOW MULTI-TRACK OP
         MVC   IEASKADR+2(4),IEABUFER+11  CCHH OF VTOC  INTO SEEK ADDR
         MVC   IEASRADR(5),IEABUFER+11    CCHHR OF VTOC INTO SRCH ADDR
         MVC   CAW,IEAVTLST           MOVE CHNL PROG ADDR INTO CAW
         BAL   R5,IEASTRIO     READ VTOC DSCB DATA INTO BUFFER
         TM    IEABUFER+27,X'04'        TEST FOR 2301 DEV TYPE
         BZ    IETRAKER               NOT 2301 - GET TRPCYL
         MVI   IEADRUM,X'FF'            INDICATE 2301
IETRAKER MVC   IEATRPCL(2),IEABUFER+20  SAVE TRKS PER CYL
         MVC   TRKCAP+2(2),IEABUFER+22  COPY TRACK CAPACITY
         MVI   IEASRADR+4,X'01'         SEARCH FOR REC ONE OF VTOC
         OI    IEARREC1,X'02'  SHOW MULTI-TRACK OP
*READ SYS1.IPLDECKS DSCB DATA
         MVC   CAW,IEADSLST           MOVE CHNL PROG ADDR INTO CAW
         MVI   IEABUFER,0    CLEAR FOR TEST                      79350
         BAL   R5,IEASTRIO     READ NUCL POD SET DSCB DATA
         CLI   IEABUFER,C'1'  WAS DSCB FOUND ?                   79350
         BE    GORDNUC       YES; READ DIRECTORY                 79350
         MVC   PROGNAM(8),NOPGMSG  SHOW PDS NOT FOUND            79350
         B     NOTFOUND      DISPLAY IT                          79350
*READ SYS1.IPLDECKS DIRECTORY
GORDNUC  MVC   IEANUCDS(8),IEABUFER+63  SAVE CCHH OF NUCL POD SET
         MVC   IEASKADR+2(4),IEABUFER+63  CCHH OF NUCL POD INTO SEEKADR
         MVC   IEASRADR(4),IEABUFER+63  CCHH OF NUC INTO SEEKADR
         MVI   IEASRADR+4,X'01'         RECORD ONE
         OI    IEARREC1,X'02'  SHOW MULTI-TRACK OP
         MVC   CAW,IEAPDLST           MOVE CHNL PROG ADDR INTO CAW
         BAL   R5,IEASTRIO     READ NUCL POD REC INTO BUFFER
*SEARCH FOR MEMBER ENTRY IN DIRECTORY
         LA    R1,IEABUFER-4 SET TO GET END
         AH    R1,IEABUFER     OF MEMBER LIST IN BLOCK
         LA    R13,IEABUFER+2        COUNT REG 13=ADDR OF BUFFER+2
IEACOMPR CLC   0(8,R13),PROGNAM        NAM FLD IN BUFFER=PROGRAM
         BE    IEAPROCD               YES,PROCEED
         BH    NOTFOUND       MEMBER NOT FOUND - TRY AGAIN
         IC    R8,11(,R13)   GET FLAGS AND HALFWORD LENGTH COUNT
         LA    R9,X'1F'                  REG9= MASK BITS
         NR    R9,R8                    MASK OFF FIRST 3 BITS IN REG8
         AR    R9,R9                    REG 9=SIZE OF VAR FLD IN BYTES
         LA    R13,12(R9,R13)           NEXT ENTRY
         CR    R13,R1        REACHED END OF BLOCK ?             GP05208
         BL    IEACOMPR      NO; CONTINUE COMPARING NAM FLD
         B     NOTFOUND      TOO BAD                            GP05208
         EJECT
IEAPROCD LH    R7,8(,R13)    REG 7 = TT OF FIRST TEXT RECORD
IEACOMLP TM    IEADRUM,X'FF'            TEST FOR 2301 DEVICE
         BO    IEADRMR                YES DO NOT CONVERT TRPCYL
         LH    R6,IEATRPCL               NO GET TRKS PER CYL
         LH    R9,IEANUCDS+2
         AR    R9,R7                    REG9=HH OF POD SET+TT OF TRTAB
         SR    R8,R8                    CLEAR REG 8
         DR    R8,R6                    REG8=HH OF TRTAB, REG9=CC
         LH    R7,IEANUCDS               REG7=CC OF POD SET
         AR    R7,R9                    REG7=CC OF TRTAB
*CONSTRUCT CCW FOR READING TRANSLATION TABLE AND SCATTER LIST
         STH   R7,IEASKADR+2         STORE CC OF TRTAB INTO SEEKADDR
         STH   R8,IEASKADR+4         STORE HH OF TRTAB INTO SEEKADDR
         B     IEAIEARD                 BRANCH AROUND DRUM CODE
IEADRMR  LH    R9,IEANUCDS+2             GET TTR OF NUCLEUS
         AR    R9,R7                    ADD REL TTR TO ABSL TTR
         SR    R8,R8                    CLEAR REG 8
         STH   R8,IEASKADR+2             SET CC TO ZERO
         STH   R9,IEASKADR+4             SET SEEK HH
         MVC   IEATRPCL(2),IEADRMLM     SET TRPCYL TO 200
IEAIEARD MVC   IEASRADR(4),IEASKADR+2   SET SEARCH CC HH
         SPACE
         SR    R1,R1
         IC    R1,10(,R13)   GET FIRST TEXT RECORD NUMBER
         BCTR  R1,0          LESS ONE
         LTR   R1,R1         SEE IF MINUS - ERROR IF SO
         BP    *+6           NO - OK
         SR    R1,R1         START WITH FIRST RECORD
         STC   R1,IEASRADR+4  SET RECORD IN SEARCH ARRGUMENT
         MVI   MEMFLAG,0     CLEAR MEMBER FLAG
         MVC   CAW,IEATRLST  SET CHANNEL PROGRAM ADDRESS
         OI    IEARREC1,X'02' SHOW MULTI-TRACK OP
         MVC   IEATRTRD(8),IEARDCNT FIRST I/O - GET COUNT ONLY
         BAL   R5,IEASTRIO   GO TO IT
         LA    R1,IEATRTBH   REBUILD READ CCW
         ST    R1,IEATRTRD   SET READ ADDDRESS
         MVI   IEATRTRD,6    READ DATA
         MVI   IEATRTRD+4,CC  SET CHAINING - NO SLI
         SPACE
MEMREAD  MVC   IEASRADR(5),IEAST  SET NEXT SEARCH ADDRESS
         MVC   IEASKADR+2(4),IEASRADR  ALSO UPDATE SEEK
         LH    R1,IEAST+6    GET DATA LENGTH OF NEXT RECORD
         LTR   R1,R1         IS IT ZERO (EOF) ?
         BZ    MEMEOF        YES - QUIT READING
         STH   R1,IEATRTRD+6 ELSE SET INTO RECORD
         OI    IEARREC1,X'02'  SHOW MULTI-TRACK OP
         MVC   CAW,IEATRLST  SET CHANNEL PROGRAM ADDRESS
         BAL   R5,IEASTRIO   AND READ THE RECORD
         LA    R3,IEATRTBH   GET BLOCK ADDRESS
         LA    R4,80         SET RCORD LENGTH
         LH    R5,IEATRTRD+6   GET BLOCK LENGTH
         AR    R5,R3         GET END OF BLOCK + 1
         BCTR  R5,0          END OF BLOCK
MEMREC   CLC   END2,0(R3)    END CARD ?
         BE    MEMEND        YES
         CLC   TEXT2,0(R3)   TEXT RECORD ?
         BNE   MEMINC        NO - IGNORE IT
         OI    MEMFLAG,2     SHOW TEXT CARD GOTTEN
         SR    R2,R2
         IC    R2,11(,R3)    GET TEXT LENGTH
         L     R1,4(,R3)     GET LOAD ADDRESS
         LA    R1,0(,R1)     TAKE A BYTE
         BCTR  R2,0          LESS ONE FOR EX
         LA    R6,0(R1,R2)   GET ADDRESS OF LAST BYTE
         C     R6,MAXMEM     IS IT IN AVAILABLE MEMORY ?
         BNL   MEMBADR       NO - KILL
         C     R1,HIBOUND    SEE IF IT OVERLAYS US
         BNL   MEMMOVE       NO; MOVE IT
         LA    R7,0(,R15)    SEE IF IT OVERLAYS US
         CR    R6,R7
         BNL   MEMBADR       YES -SHOW BAD ADDRESS
         LA    R7,LOWLEN     SEE IF IN LOW MEMORY
         CR    R1,R7         IS START IN LOW MEMORY ?
         BL    MEMLOW        YES - SPECIAL
MEMMOVE  EX    R2,MEMMVC     MOVE TEXT
MEMINC   BXLE  R3,R4,MEMREC  DO NEXT  RECORD
         B     MEMREAD       ELSE READ NEXT BLOCK
         SPACE
MEMLOW   CR    R6,R7         IS IT ENTIRELY IN LOW MEMORY ?
         BNL   MEMLOW2       NO; MIXED
         LA    R1,MEMSAVER(R1)  GET LOW MEMORY SAVE AREA
         B     MEMMOVE       AND MOVE TO IT
MEMLOW2  SR    R7,R1         LENGTH + 1 IN LOW MEMORY
         LR    R2,R7         FOR EX
         BCTR  R2,0
         LA    R1,MEMSAVER(R1)  IN MEMORY SAVE ADDRESS
         EX    R2,MEMMVC     MOVE TO FAKE LOW MEMORY
         SR    R6,R7         LENGTH REMAINING TO BE MOVED
         LA    R1,LOWLEN     GET NEW DESTINATION IN LOW MEMORY
         LA    R2,16(R3,R7)   NEW FROM ADDRESS
         STC   R6,*+5        CHEAT
         MVC   0(0,R1),0(R2)  MOVE REMAINDER TO ACTUAL MEMORY
         B     MEMINC
         SPACE 1
MEMBADR  LA    R1,C'2'       SET ERROR CODE 2 - BAD ADDRESS
         B     MEMBSTC       WRITE MESSAGE
         SPACE 1
MEMEND   LA    R1,C'4'       PROVISIONAL ERROR MESSAGE - BAD END ENTRY
         CLC   BLANKS(3),5(R3)  ANY ENTRY ?
         BE    MEMBSTC       NO - SIGNAL ERROR
         MVC   MEMSAVER+5(3),5(R3)  ELSE COPY THE ENTRY ADDRESS
         OI    MEMFLAG,1     SHOW ENTRY GOTTEN
         SPACE 1
MEMEOF   TM    MEMFLAG,3     TEXT + ENTRY PRESENT ?
         BNO   MEMBERR       NO - ERROR
         SPACE 1
         MVC   PSWMCC,PSWRSTRT     SAVE RESTART PSW
         MVC   PSWRSTRT,PSWINNEW   SET FOR REDO ON EXTERNAL
GOTOXCTL SSM   FFFFFFFF      ALLOW ALL INTERRUPTS
         NOPR  0               DELAY A BIT                       84134
         SSM   *+1           DISABLE ALL INTERRUPTS
         MVC   MESSAGE(L'EPMSG),EPMSG
         MVC   MESSAGE+4(L'PROGNAM),PROGNAM  ADD NAME
         UNPK  MESSAGE+19(7),MEMSAVER+5(4)  GET ENTRY POINT
         TR    MESSAGE+19(6),HEXTAB-C'0'  MAKE EBCDIC
         MVI   MESSAGE+25,C' '         MAKE TRAILING BLANK
         MVI   CONACCW+7,25  SET LENGTH FOR 1052
         MVI   CONACCW,X'09'   SET FOR CARRIAGE RETURN
         MVI   CONACCW+4,SLI   KILL COMMAND CHAINING
         LH    R13,CONSPGM   GET CONSOLE DEVICE OFFSET
         L     R13,CONWAD(R13)  GET WRITE CCW
         LH    R10,CONSLAD   GET CONSOLE ADDRESS
         TIO   0(R10)        WAIT FOR IT
         BNZ   *-4           LOOP IF NOT OK
         BAL   R8,CONSSIOS   WRITE MSG
         MVI   CONACCW,1     RESTORE WRITE CODE
         MVI   CONACCW+4,SLI+CC  RESTORE CHAINING
         MVI   MESSAGE,C' '  CLEAR MESSAGE AREA
         MVC   MESSAGE+1(L'MESSAGE-1),MESSAGE
         MVC   TIMER2-LOWPAGE+MEMSAVER,CONSLAD  PASS CONSOLE TYPE TO
         MVI   TIMER2+2-LOWPAGE+MEMSAVER,C'C'   INVOKED PROGRAM
         TM    WAITFLAG,1    WAIT REQUESTED ?
         BZ    GODOXCTL      NO; GO TO USER PROGRAM
         MVC   PSWXWAIT+4(4),MEMSAVER+4  DISPLAY ENTRY IN WAIT PSW
         MVC   PSWINTER,PSWINWAT       WAIT FOR EXTERNAL TO START EXEC
         LPSW  PSWXWAIT      WAIT FOR BUTTON
GODOXCTL CLC   PFX390,PROGNAM  REQUEST FOR 390 MODE PGM?        GP05208
         BNE   DOGOXCTL      NO; NORMAL                         GP05208
         OI    PSWRSTRT+1,X'08'  BIT 12 - 390 MODE              GP05208
         MVI   PSWRSTRT+4,X'80'  AMODE 31, RMODE 24             GP05208
         SPACE 1
DOGOXCTL NI    WAITFLAG,254  TURN OFF WAIT FLAG
         MVC   PSWRSTRT,PSWMCC   RESTORE LOW PSW
         MVC   LOWPAGE+4(LOWLEN-4),MEMSAVER+4  RESTORE LOW MEMORY
         LM    R0,R15,SAVE   RESTORE ALL REGISTERS
         LPSW  PSWRSTRT      ENTER THE PROGRAM
PFX390   DC    C'SA'         SA PREFIX - 390 MODE               GP05208
         SPACE 1
MEMBERR  LA    R1,C'3'       SET FOR TEXT MISSING
         TM    MEMFLAG,2     TEXT CARD ?
         BZ    MEMBSTC       NO
         LA    R1,C'4'       SHOW BAD ENTRY - NO END CARD
         SPACE 1
MEMBSTC  MVC   MESSAGE(L'MESSMEM),MESSMEM  MOVE BASIC MESSAGE
         MVC   MESSAGE+1+L'MESSMEM(L'PROMPT),PROMPT  ADD PROMPT
         STC   R1,MESSAGE+11   ADD ERROR CODE
         B     CONSCOM       PUT OUT THE MESSAGE
         SPACE 2
IOINRPT  LPSW  PSOIOP        IGNORE I/O INTERRUPTS
         SPACE 1
INTERUPT TM    PSOEXT+3,X'40'  INTERRUPT BUTTON PRESSED ?
         BNZ   INTERPSW      YES - LOAD INTERRUPT PSW
         LPSW  PSOEXT        ELSE IGNORE INTERRUPT
INTERPSW LPSW  PSWINTER      GO TO REQUESTED INTERRUPT POINT
         EJECT
***************************** IO ROUTINE ******************************
*                                                                     *
* THIS IS A COMMON I/O ROUTINE USED BY THE ROUTINES WHICH LOCATE AND  *
*   LOAD THE NUCLEUS. IT CONTAINS SUBROUTINES TO HANDLE CYLINDER AND  *
*   HEAD SWITCHING, AND DEFECTIVE AND ALTERNATE TRACKS.               *
*
***********************************************************************
*
* THE FOLLOWING INITIATES THE I/O REQUEST, AND IF THE CSW IS STORED,
*   ANALYZES THE STATUS BYTES. IF THE OPERATION IS SUCCESSFUL, CONTROL
*   RETURNS TO THE MAIN LINE; ON UNIT CHECK, THE SENSE SUBROUTINE IS
*   BRANCHED TO; AND ON ANY OTHER CHECK, THE MACHINE IS PUT IN WAIT
*   STATE.
*
IEASTRIO SIO   0(R10)                    START IO
         BC    8,IEATSTIO               IO OP INITIATED,TEST IO
         BC    4,ERRSIO2           I/O OP NOT INITIATED;CSW STORED
         BC    2,IEASTRIO               CHANNEL BUSY,START IO
         B     ERRSIO3             CHN NOT BUSY,CSW NOT STORED
IEATSTIO TIO   0(R10)                    TEST IO
         BC    2,IEATSTIO               CHANNEL BUSY,TEST IO
         BC    4,CSWTEST           CSW STORED; TEST STATUS
         B     ERRTIO              NEITHER OF ABOVE
CSWTEST  TM    CSW+4,X'02'            TEST FOR UNIT CHECK
         BC    1,SENSE
         TM    CSW+5,X'2F'                 TEST FOR OTHER CHECKS
         BNZ   ERRCSW            UNRECOVERABLE ERROR
         TM    CSW+4,X'04'                 TEST FOR DEVICE END
         BOR   R5                 RETURN TO CALLER
         B     IEATSTIO                 NONE OF ABOVE CONDITIONS,TESTIO
*
* SENSE SUBROUTINE-ISSUES SENSE COMMAND TO SYSRES. TESTS FOR THE VALID
*   CONDITIONS OF TRACK CONDITION CHECK, FILE MASK VIOLATION, OR END
*   OF CYLINDER. IF NONE OF THESE, AN ERROR CONDITION EXISTS.
*
SENSE    EQU   *
         BC    0,ERRSNS            ERROR IF UNIT CHK ON SENSE
         MVI   SENSE+1,X'F0'       MAKE THE BC AN UNCONDITIONAL BRANCH
         ST    R13,IEAFULWD        PROTECT REGISTER 13
         MVC   CSWCAW(Q12),CSW     SAVE CSW AND CAW
         L     R2,CSW              GET ADDR OF LAST CCW
         LA    R1,8                  BY SUBTRACTING 8 FROM THE CSW
         SLR   R2,R1                 TO POINT TO THE COMMAND
         LR    R1,R5               SAVE REG5 (RETURN REG) OVER BAL
         XC    SENSESAV(Q24),SENSESAV ZERO SENSE DATA AREA
         MVC   CAW,IEASNCAW      PLACE PTR TO SENSE CMD IN CAW
         BAL   R5,IEASTRIO          ISSUE SENSE
         MVI   SENSE+1,X'00'       RESET BC FOR NO BRANCH
* THE FOLLOWING STATEMENTS TEST THE SENSE BITS
         TM    SENSESAV,X'02'      TRK CONDITION CHECK
         BO    TRKCCK
         LA    R8,INITRTRY   LOAD BR ADDR FOR RETURN FROM EOTRACK
         TM    SENSESAV+1,X'04'    FILE MASK VIOLATION-EOTRK
         BO    EOTRACK
         TM    SENSESAV+1,X'20'    END OF CYLINDER
         BO    UPCYL
         TM    SENSESAV,ECFDTACK   TEST FOR DATA CHECK
         BZ    ERRUCK              IF NO-LOAD WAIT
***                                                           ***
*     THIS ROUTINE CORRECTS DATA CHECKS ON MERLIN/ZEUS DEV      *
*     SEVERAL ASSUMPTIONS HAVE BEEN MADE IN ECF CODING.
*     THEY ARE:
*            1.  NO CCWS ARE DATA CHAINED.
*            2.  NO READ CCW IS FOLLOWED BY A 'TIC'.
***                                                           ***
         OC    SENSESAV+Q7(Q17),SENSESAV+Q7 IS IT MER/ZEUS DEV
         BE    ERRUCK              NO-LOAD WAIT
         XC    SENSDISP(Q5),SENSDISP CLEAR SEC SENSE AREA
         OC    SENSDISP(Q5),SENSESAV+Q16 IS THERE DATA TO CONVT
         BE    ECFRESIO            NO-REALGN READ HEAD AND EXIT
         LA    R5,Q8               GET LGN OF CCW
         TM    Q4(R2),NOR      IS SKIP BIT ON IN CCW
         BO    ECFRESIO            YES-REALGN READ HEAD AND SIO
         CLI   SENSESAV+Q7,SNSZEUSA IS IT ZEUS-A
         BE    ECFNOMER            YES-USE CCW INFO FOR ERR LOC
         CLI   SENSESAV+Q7,SNSZEUSC IS IT ZEUS-C
         BE    ECFNOMER            YES-USE CCW INFO FOR ERR LOC
         L     R13,SENSESAV+Q20     GET MUMB BYTES CLOCKED
         LA    R13,Q0(,R13)        CLEAR HI ORDER BYTE
         B     ECFMERFX            BR TO CONTINUE ERROR LOCATE
ECFNOMER LH    R5,CSWCAW+Q6        GET RESID COUNT (ZEUS)
         LH    R13,Q6(R2)           GET LGN OF READ FROM FAIL CCW
         SLR   R13,R5               ADJUST ACTUAL READ LGN
ECFMERFX L     R3,Q0(R2)           GET DATA ADDRESS
         LA    R3,Q0(R3)            CLEAR HI BYTE
         ALR   R13,R3               ADD READ CT TO START OF DATA
         LH    R5,SENSDISP         GET DISP TO ERROR AREA
         SLR   R13,R5               FIND BAD AREA
         CLR   R13,R3               IS ERROR IN THIS DATA AREA
         BL    ERRUCK              NO- CANT RECOVER
         CLI   SENSESAV+Q7,SNSZEUSA  IS THIS A ZEUS-A
         BNE   ECFNOZA            NO FIX 3 CONTIG BYTES
         XC    Q0(Q1,R13),SENSOVLY    FIX BYTE ONE   *
         XC    Q2(Q1,R13),SENSOVLY+Q1 FIX BYTE TWO   * ON ZEUS-A
         XC    Q4(Q1,R13),SENSOVLY+Q2 FIX BYTE THREE *
         B     ECFIXMER            BR AROUND MERLIN FIX
ECFNOZA  XC    Q0(Q3,R13),SENSOVLY  FIX BAD AREA FOR MERLIN OR ZEUS-C
ECFIXMER EQU   *
ECFRESIO EQU   *
         TM    Q4(R2),CC           IS CHAIN ON IN FAILING CCW
         BZ    NOMTRET             NO TAKE NO I/O EXIT
         MVC   ECFTICCW+Q1(Q3),CSWCAW+Q1  PUT TIC ADDR IN CCW
         MVI   ECFTICCW,ECFTIC     RESTOR TIC CMD
         MVC   TIC(Q8),ECFSRC      CHNG RETRY TIC TO SRCH W/CHAIN
         MVC   CAW(Q4),ECFRESTR    SET RESTART CAW
         TM    Q8(R2),ECFCNTOP     IS USER CCW AT RESTRT A COUNT
         BO    ECFNOCNT            YES--DONT USE DUMMY COUNT CCW
         MVC   ECFTICRD(Q4),ECFDMYRD SET DMY RD COUNT IN CHAIN
         B     ECFRSEXT            LEAVE RESTART RTN
ECFNOCNT MVC   ECFTICRD(Q4),ECFTICCW TIC TO USER,NO RD COUNT
ECFRSEXT EQU   *
         B     INITRY25            REST REGS 1 AND 13 AND SIO
*        ORG   SENSE+X'52' RESET TO OVERLAY MER/ZEUS                MES
*        B     ERRUCK ERROR FOR THE TIME BEING                      RPR
         SPACE 3                                                    RPR
*
* ON A TRACK CONDITION CHECK, THE HOME ADDRESS AND RECORD 0 ARE READ.
*   THE ID OF RECORD 0 IS USED AS THE NEXT SEEK ARGUMENT, AND THE FLAG
*   BYTE OF HOME ADDRESS IS USED TO ROUTE CONTROL.
*
TRKCCK   EQU   *
         MVI   SENSE+1,X'F0'       ANY UNIT CHK IN TCC CHAIN IS ERROR;
*                                    IF SENSE REENTERED,BRANCH TO ERRTN
         MVC   CAW,TRKCKCAW      MOVE TO CAW A PTR TO TCC CMD CHAIN
         BAL   R5,IEASTRIO       READ HA INTO BUF,R0 INTO IEASKADR+2
         MVI   SENSE+1,X'00'       TURN OFF BRANCH TO ERROR ROUTINE
         TM    HABUFER,X'02'       TEST IF ON DEFECTIVE TRACK
         BO    INITRYDT      YES-TEST FURTHER
         LA    R8,INITRTRY   LOAD BR ADDR FOR RETURN FROM EOTRACK
*                              ROUTINE
*
* END OF TRACK ROUTINE UPDATES THE HH PORTION OF THE SEEK AND SEARCH
*   ARGUMENTS BY 1.
*
EOTRACK  EQU   *
         LH    R13,IEASKADR+4       GET HH PORTION OF SEEK ARGUMENT
         LA    R13,1(,R13)         UPDATE BY 1
         CH    R13,IEATRPCL         COMPARE TO MAX NO. OF TRKS PER CYL
         BNL   UPCYL
         STH   R13,IEASKADR+4       STORE NEW HH IN SEEK ARGUMENT
         STH   R13,IEASRADR+2         AND SEARCH ARGUMENT
         BR    R8
*
* THE FOLLOWING IS A COMMON SUBROUTINE USED TO SET UP THE COMMAND CHAIN
*   TO SEEK TO THE NEW TRACK AND TIC TO THE COMMAND WHICH CAUSED THE
*   UNIT CHECK.
*
INITRTRY EQU   *
         MVI   IEASRADR+4,X'01'    ENSURE SEARCH FOR REC 1 ON NEW TRK
INITRY2  ST    R2,TIC
         MVI   TIC,X'08'           TIC COMMAND
         MVC   CAW,RETRYCAW      MOVE TO CAW PTR TO RETRY CCW CHAIN
INITRY25 EQU   *                   ENTRY PT FROM ECF TO RESTART
         LR    R5,R1               RESET REG FOR RETURN FROM I/O RTN
         L     R13,IEAFULWD             RESTORE REGISTER 13
         B     IEASTRIO
INITRYDT TM    IEARREC1,X'02' *  IS THERE M-T?
         BNO   INITRY2           NO--TAKE NORM RETURN
         ST    R2,TIC             RESTORE TIC COMMAND AND
         MVI   TIC,X'08'         CCW ADDR
         MVC   CAW,RETRYCAW    RE-ENTER CCW CHAIN CAUSING U-CHK
         BAL   R5,IEASTRIO        START I/O
         CLC   IEASRADR(4),IEAST   IS NEXT COUNT ON SAME TRK
         BE    NOMTRET       YES-RETURN TO CALLER
         MVC   IEASKADR+2(4),IEASRADR RESTORE DEF TRK ADDR
         BAL   R8,EOTRACK      UPDATE TRK ADDR BY ONE
         MVI   IEASRADR+4,X'01' ENSURE READ OF REC ONE ON NXT TRK
         LA    R1,6(,R1)           SET RET ADR TO BYPAS INLIN TRK
*                              UPDATE INSTRUCTION
NOMTRET  LR    R5,R1             RESTORE ORIG CALLERS RET ADDR
         L     R13,IEAFULWD
         NI    IEARREC1,X'FD'    TURN OFF M-T SWIT IF ON
MTRET    BR    R5                 RET IS TO CALLER OR IF M-T
*                        RETURN IS PAST INLINE RESTORE OF SRADDR
*
* END OF CYLINDER ROUTINE UPDATES THE CYLINDER PORTION BY 1, AND
*   ZEROS THE TRACK PORTION OF THE SEEK AND SEARCH ARGUMENTS.
*
UPCYL    EQU   *
         SR    R13,R13
         STH   R13,IEASKADR+4       STORE 0 IN HH PART OF SEEK ARG
         STH   R13,IEASRADR+2       STORE 0 IN HH PART OF SEARCH ARG
         LH    R13,IEASKADR+2       GET CURRENT CC ARGUMENT
         LA    R13,1(,R13)         UPDATE BY 1
         STH   R13,IEASKADR+2       STORE NEW CC IN SEEK ARG
         STH   R13,IEASRADR         STORE NEW CC IN SEARCH ARG
         CLC   IEASRADR(4),IEANUCDS+4  EXTENT VALID ?
         BNHR  R8            YES
         XC    IEAST(8),IEAST  ELSE CLEAR NEXT COUNT FIELD
         BR    R8
*
* FOLLOWING ARE THE VARIOUS ERROR ROUTINES WHICH CAUSE A WAIT STATE
*   WITH AN ERROR CODE.
*
IEAERR1  MVI   IEAERR,X'01'        I/O NOT OPERATIONAL
         LPSW  IEAWAIT
         SPACE
ERRSIO2  TM    CSW+4,X'30'     TEST FOR CU BUSY OR CU END
         BNZ   IEASTRIO          IF EITHER, RESTART I/O
*    IF THE I/O OPERATION WAS NOT INITIATED DUE TO SOME CAUSE
*    OTHER THAN CONTROL UNIT BUSY OR CONTROL UNIT END, THE CSW
*    WAS STORED IN ERROR
         MVI   IEAERR,X'02'        CSW STORED IN ERROR
         LPSW  IEAWAIT
         SPACE
ERRSIO3  MVI   IEAERR,X'03'        I/O NOT INITIATED
         LPSW  IEAWAIT
         SPACE
ERRTIO   MVI   IEAERR,X'04'        ERROR ON TIO
         LPSW  IEAWAIT
         SPACE
IEAERR8  MVI   IEAERR,X'18'             EXCEEDED MEMORY AVAIL FOR RLD
         LPSW  IEAWAIT
         SPACE
PGMCHK   MVI   IEAERR-1,X'C0'           UNEXPECTED PGM CHECK
         MVC   IEAERR(1),PSOPGM+3  SHOW TYPE OF CHECK
         LPSW  IEAWAIT
         SPACE
ERRCSW   EQU   *                   PGM CHECK OR CATASTROPHIC ERROR
         MVI   IEAERR,X'06'        INDICATE UNDEFINED CHECK
         LPSW  IEAWAIT
         SPACE
ERRUCK   EQU   *                   UNIT CHK OTHER THAN TCC,EOT,EOCYL
         ST    R2,OSCVT            SAVE PTR TO CCW CAUSING UNIT CHK
         MVC   TIMER2(4),SENSESAV MOVE 4 SNSE BYTES TO AREA IN S/A DMP
         MVI   IEAERR,X'05'        INDICATE UNIT CHECK
         LPSW  IEAWAIT
         SPACE
ERRSNS   EQU   *                   UNIT CHECK ON SENSE COMMAND
         MVI   IEAERR,X'17'        INDICATE WAIT CODE
         LPSW  IEAWAIT
         SPACE 1
KILLKILL MVI   IEAERRX+2,X'FF'  MAKE ALL ONES - N O  CONSOLE
         MVI   IEAERRX+3,X'F7'  CHEAT - FOR MODEL 65 (+8 DISPLAY)
         LPSW  IEAWAIT       WAIT FOR NOTHING TO HAPPEN
         EJECT
MEMSAVER DC    XL(LOWLEN)'0'   SAVE LOW MEMORY HERE
CONSTABL DS    0F            CONSOLE TABLE
&CURCONS SETA  0                                                GP12050
.EXPLOOP AIF   (&CURCONS GE &NUMCONS).EXPDONE                   GP12050
&CURCONS SETA  &CURCONS+1                                       GP12050
         DC    XL2'0&CONSOLE(&CURCONS)'   CONSOLE ADDRESS       GP12050
         DC    XL2'0&CONTYPE(&CURCONS)'   OFFSET TO CONSOLE HANDLER
         AGO   .EXPLOOP                                         GP05202
.EXPDONE ANOP  ,                                                GP05202
CONSLAD  EQU   CONSTABL,2,C'X'     CONSOLE ADDRESS              GP05202
CONSPGM  EQU   CONSTABL+L'CONSLAD,2,C'H' CONSOLE HANDLER OFFSET GP05202
CONSTABS EQU   *             CONSOLE SCAN STOPPER               GP05202
NOPGMSG  DC    CL8'**** PDS '                                    79350
MISER    DC    CL8'SEREP360'  DEFAULT NAME FOR SEREP
MICPUID  DC    D'0'          370 CPU ID
         SPACE
RELCCW1  DS    0D            FIRST CCW TO RELOCATE               92235
CONACCW  CCW   1,MESSAGE-LP,CC+SLI,L'MESSAGE  1052 WRITE / VAR. LENGTH
         CCW   X'0A',PROGNAM-LP,SLI,8     READ
         SPACE
CONBCCW  CCW   X'07',0,CC+SLI,2     3066 ERASE SCREEN
         CCW   X'27',CONBSBA1-LP,CC+SLI,2     3066 SET BUFFER ADDRESS
         CCW   1,MESSAGE-LP,CC+SLI,L'MESSAGE       WRITE
CONBSBA1 EQU   *-3           USE 0,0 FOR CURSOR ADDRESS LINE 1
         CCW   X'0F',CONBSBA2-LP,SLI,2             SET CURSOR ADDRESS
CONBCCWR CCW   X'0E',CONBINP-LP,CC+SLI,3           READ CONTROL INPUT
         CCW   X'27',CONBSBA2-LP,CC+SLI,2          SET BUFFER ADDRESS
         CCW   6,PROGNAM-LP,SLI,8                  READ INPUT
         SPACE
CONCCCW  CCW   5,MESSAGEP-LP,CC+SLI,L'MESSAGE+L'MESSAGEP+L'MESSAGES CRT
         CCW   3,0,SLI,1     ADD AN EXTRA CCW FOR ZAPPING UNTIL I LEARN
CONCCCWR CCW   6,CONCINP-LP,SLI,L'PROGNAM+6   3277 READ MODIFIED
IEALBLSK CCW   7,IEASKADR-LP,CC,6       CONTROL SEEK REC 3, CYL 0, TR 0
IEALBLSR CCW   X'31',IEASRADR-LP,CC,5   SEARCH EQUAL ID STNDR VOL LABEL
         CCW   8,IEALBLSR-LP,0,0        TIC
         CCW   6,IEABUFER-LP,SLI,80     READ DATA STANDARD VOLUME LABEL
IEAVTCSK CCW   7,IEASKADR-LP,CC,6       CONTROL SEEK
IEAVTCSR CCW   X'31',IEASRADR-LP,CC,5   SEARCH EQUAL ID  VTOC
         CCW   8,IEAVTCSR-LP,0,0        TIC
         CCW   6,IEABUFER-LP,0,96     READ DATA  VTOC
IEADSTSK CCW   7,IEASKADR-LP,CC,6       CONTROL SEEK
IEASYNC  CCW   X'31',IEASRADR-LP,CC,5 *SEARCH FOR RECORD ONE
         CCW   8,IEASYNC-LP,0,0         TIC
IEADSTSR CCW   X'A9',IEADSNAM-LP,CC,44 T SEARCH KEY
         CCW   8,IEADSTSR-LP,0,0        TIC
         CCW   6,IEABUFER-LP,0,96     READ DATA  DATA SET DSCB
IEAPODSK CCW   7,IEASKADR-LP,CC,6       CONTROL SEEK
IEAPODSR CCW   X'31',IEASRADR-LP,CC,5   SEARCH EQUAL ID. PODS DIRECTORY
         CCW   8,IEAPODSR-LP,0,0        TIC
IEARECSR CCW   X'E9',PROGNAM-LP,CC,8 EARCH HI EQUAL KEY PODS M/T
         CCW   8,IEARECSR-LP,0,0        TIC
         CCW   6,IEABUFER-LP,0,256    READ DATA PODS
         SPACE 1
IEASNCCW CCW   4,SENSESAV-LP,SLI,24     SENSE COMD
         SPACE 1
IEATRTSK CCW   7,IEASKADR-LP,CC,6       SEEK FIRST TR TBL THEN TXT RECS
         CCW   X'1F',IEAMASK-LP,CC,1    SET FILE MASK
IEATRTSR CCW   X'31',IEASRADR-LP,CC,5   SEARCH EQUAL ID
         CCW   8,IEATRTSR-LP,0,0        TIC
IEATRTRD CCW   6,IEATRTBH-LP,SLI,1024   READ DATA
IEARDCNT CCW   X'92',IEAST-LP,SLI,8     READ COUNT MULTI TRACK
         SPACE 1
TRKCKCCW CCW   7,IEASKADR-LP,CC,6  SEEK
         CCW   X'1A',HABUFER-LP,CC,5  READ HOME ADDRESS
         CCW   X'16',IEASKADR+2-LP,0,4  READ R0 INTO THE SEEK ARGUMENT
         SPACE
RETRYCCW CCW   7,IEASKADR-LP,CC,6     SEEK
         CCW   X'1F',IEAMASK-LP,CC,1  SET FILE MASK
         CCW   X'1A',IEASKADR+1-LP,CC+NOR,5   READ HOME ADDRESS
TIC      CCW   8,0,0,0       TIC PATTERN
         CCW   8,TIC-LP,0,0      TIC BACK TO ECFSRC
ECFTICRD CCW   8,0,0,0             HOLDS 'ECFTICCW' IF RESTART
*                                   ON COUNT OR 'ECFTICRD' IF
*                                     FOR NON-COUNT RESTART
ECFTICCW CCW   8,0,0,0        TIC FILLED BY ECF RTN TO USERS CHAN
ECFDMYRD CCW   X'12',IEABUFER-LP,CC+SLI+NOR,1        ALIGN HEAD
ECFSRC   CCW   X'31',IEASRADR-LP,CC,5     THIS SEEK  WILL OVERLAY CCW
RELCCWL  EQU   *             LAST CCW TO RELOCATE                92235
         SPACE
CONSCAW  DC    F'0'          ADDRESS OF CCW CHAIN
CONDELAY DC    F'3000'       TIME DELAY FOR TIO RETRIES
CONBSBA2 DC    X'0100'       3066 BUFFER ADDRESS LINE 2
MESSAGEP DC    X'C71140401D68' 3277 WCC,SBA,(0,0),SF,(PROT,INT)
MESSAGE  DC    CL80' '       VARIABLE MESSAGE AREA
MESSAGES DC    X'11C1501D4013' 3277 SBA,(1,0),SF,0,IC SUFFIX
CONCINP  DC    XL3'0'        3270 INPUT PREFIX AID,CURS ADDR
CONBINP  DC    XL3'0'        3270 ATTR/ATTR ADDR | 3066 PREFIX
PROGNAM  DC    CL8' '        INPUT AREA
         DC    C' NOT FOUND.  '        MUST FOLLOW ANPUT FIELD
PROMPT   DC    C'SPECIFY PROGRAM :'    MUST FOLLOW 'NOT FOUND'
PROMF    EQU   *-PROGNAM     COMBINED MESSAGE LENGTH
EPMSG    DC    0CL18' ',C'PGM ' CONTINUED AFTER BLANKS
BLANKS   DC    CL8' '        SHORT BLANK FIELD
EPMSG2   DC    C' ENTRY'
         SPACE 1
MEMMVC   MVC   0(0,R1),16(R3)  MOVE TEXT FROM TEXT CARD TO MEMORY
END2     DC    0XL4'0',X'02',C'END'    ASSEMBLER END CARD
TEXT2    DC    0XL4'0',X'02',C'TXT'    ASSEMBLER TEXT CARD
MEMFLAG  DC    X'0'          PROCESSING FLAG
WAITFLAG DC    X'0'          SET TO 1 TO WAIT PRIOR TO EXEC
WAITNO   DC    0C'NOWAIT ',C'NO'  CANCEL WAIT REQUEST
WAITYES  DC    C'WAIT '      REQUEST FOR WAIT PRIOR TO XCTL
MESSMEM  DC    C'ERROR CODE X -'       PROCESSING ERROR MESSAGE
         SPACE 1
         SPACE 1
PSWINTER DC    D'0'          EXTERNAL INTERRUPT BUTTON PSW
RELPSW1  DS    0D            FIRST PSW TO RELOCATE               92235
PSWINNEW DC    X'01000000',A(REDO-LP)  RESTART WITH NAME REQUEST
PSWINWAT DC    X'00000000',A(GODOXCTL-LP)  START PROGRAM EXECUTION
PSWXWAIT DC    X'FF020000',A(GODOXCTL-LP)  WAIT FOR USER INTERRUPTS
RELPSWL  EQU   *             LAST PSW TO RELOCATE                92235
IEAWAIT  DC    X'00020000'          DISABLED WITH WAIT FLAG ON
IEAERRX  DC    X'00000000'          I/O ERROR STOP
IEAERR   EQU   IEAERRX+3                  ERROR STOP SIGNAL
         SPACE 1
MAXMEM   DC    A(0)          HIGHEST AVAILABLE MEMORY ADDRESS + 1
HIBOUND  DC    A(&ENTRY+20*1024) FIRST BYTE AFTER PROGRAM
RELADD1  DS    0F            FIRST ADDRESS TO RELOCATE           92235
CONWAD   DC    A(CONACCW-LP,CONBCCW-LP,CONCCCW-LP,CONCCCW-LP) BY TYPE
ADIEAINT DC    A(IEAINT-LP)    PCI HANDLER FOR IPL PRELUDE
IEASNCAW DC    A(IEASNCCW-LP)
TRKCKCAW DC    A(TRKCKCCW-LP)
RETRYCAW DC    A(RETRYCCW-LP)
ECFRESTR EQU   RETRYCAW            ECF RESTART CCW CHAIN ADDR
IEAPCFP  DC    A(PGMCHK-LP)
IEATRLST DC    A(IEATRTSK-LP)              SEEK COMD
IEALBLST DC    A(IEALBLSK-LP)         LOCATE-READ STANDARD VOLUME LABEL
IEAVTLST DC    A(IEAVTCSK-LP)         LOCATE-READ VTOC CCW CHAIN
IEADSLST DC    A(IEADSTSK-LP)         LOCATE-READ DATA SET DSCB CCW CHN
IEAPDLST DC    A(IEAPODSK-LP)         LOCATE-READ PODS CCW CHAIN
RELADDL  EQU   *              LAST ADDRESS TO RELOCATE           92235
FFFFFFFF DC    XL8'FFFFFFFFFFFFFFFF'   MEMBER SEARCH STOPPER
HEXTAB   DC    C'0123456789ABCDEF'     HEX TRANSLATE TABLE
IEANUCDS DC    2F'0'         START/END CCHH OF DS
TRKCAP   DC    F'0'          TRACK CAPACITY
IEADSNAM DC CL22'SYS1.IPLDECKS'         DATA SET NAME VTOC DSCB KEY
         DC CL22' '
         DS 0D
IEAOTRCK DC    X'000000000000'          ZERO CYL, ZERO TRACK
         DS 0D
IEAREC3  DC    X'0000000003'            REC3 ON ZERO CYL,ZERO TRACK
IEADRUM  DC    X'00'
IEADRMLM DC    X'00C8'
         SPACE 1
IEAMASK  DC    X'58'                       INHIBIT WRITES AND SEEKS
HABUFER  DS    5X'00'
IEARREC1 DC    X'00'             SWITCH USED TO TEST MULTITRK OPT
         DS    0D
IEASKADR DS    3H            SEEK ADDRESS  BBCCHH
         DC    X'0100'
         DS    0D
IEASRADR DC    5X'00'        SEARCH ADDRESS   CCHHR
         DS    0D
IEAST    DS    2F                          STORE RECORD ID
IEAFULWD DS    F
         SPACE 1                                                    RPR
CSWCAW   DS    3F                  SAVE AREA FOR CSW + CAW USED
*                                   IN SENSE RTN
SENSESAV DS    12H                 AREA TO HOLD SENSE DATA
SENSDISP DS    H                   ECF DISPLACEMENT DATA
SENSOVLY DS    2H                  ECF CORRECTION DATA
IEATRPCL DS    H                      2 BYTE AREA TO STOR TRACKS/CYL
         SPACE
SAVE     DS    16F'0'        SAVE REGISTERS AT IPL TIME
         SPACE 1
         DS    0D
ZAPSPACE EQU   *
PATLEN1  EQU   ZAPSPACE-ENTRY .       LENGTH OF USED LENGTH
PATLEN2  EQU   ((2*&WORKSZ+1-2*PATLEN1)/(2*&WORKSZ-2*PATLEN1)*(&WORKSZ-*
               PATLEN1))/4   SIZED TO PREVENT NEGATIVE LENGTH
PATCH    DC    (PATLEN2)C'ZAPS' .   ZAP SPACE
IEABUFER DS    0D .   WORK BUFFER & ROUNDED END OF IPL TEXT
IEATRTBH EQU   IEABUFER+264               ALLOW FOR TT HEADER
        END STARTDEV ADDRESS OF IPL PREPARE PROGRAM REQUIRED BY LOADER
