SUBDSCB1 TITLE 'S U B D S C B 1  ***  SEARCH FOR FORMAT 1 DSCB'
***********************************************************************
**                                                                   **
**   SUBROUTINE SUBDSCB1 DOES A VTOC SEARCH FOR THE REQUEESTED DSN   **
**   ON THE VOLUME, AND RETURNS THE RESULT IN THE USER'S WORK AREA.  **
**   44X'04' AND 'FORMAT4.DSCB' RETURN A FORMAT 4, WITH A CC OF 0.   **
**                                                                   **
**                                   AUTHOR: G. POSTPISCHIL          **
***********************************************************************
**
*   R1 POINTS TO PARAMETER LIST
*   0     POINTER TO CL44 - DATA SET NAME
*   4     CL6 - POINTER TO VOLSER
*   8     POINTER TO RETURN WORK AREA (184 BYTES)
*
*   RETURN CODES AS DOCUMENTED FOR OBTAIN SEARCH, EXC FMT4 8->0
*
*
SUBDSCB1 START 0
         STM   R14,R12,12(R13)
         LR    R8,R15        SET NEW BASE
         USING SUBDSCB1,R8
         LA    R7,20+9*4(,R13)  POINT INTO USER'S SAVE AREA (R9-R12)
         USING USERSAVE,R7   MAKE THIS CODE RE-ENTRANT/REFRESHABLE
         USING PRMDSECT,R1   DECLARE THE PARM LIST
         LM    R4,R6,PRM@DSN  LOAD DSN AND WORK AREA ADDRESSES
         LA    R6,0(,R6)     KILL POSSIBLE VL BIT
         USING WRKDSECT,R6
         MVC   DYNOBT(4),PATOBT   INITIALIZE OBTAIN AREA
         MVC   WRKDSN,0(R4)  PRESERVE USER'S DSN
         CLC   =C'FORMAT4.DSCB ',WRKDSN     SPECIAL ?
         BNE   NORMDSN
         MVI   WRKDSN,X'04'  START FORMAT 4 NAME
         MVC   WRKDSN+1(L'WRKDSN-1),WRKDSN  COMPLETE NAME
NORMDSN  ST    R6,DYNOBT+4   DSN ADDRESS
         ST    R5,DYNOBT+8   VOL ADDRESS
         LA    R6,WRKDSCB
         ST    R6,DYNOBT+12  SET RETURN WORK AREA
         OBTAIN DYNOBT       LOOK FOR IT
         CH    R15,=H'8'     ERROR ?
         BNE   PGMEXIT       DON'T CARE
         CLI   WRKDSN,X'04'  FMT4 LOOKUP ?
         BNE   PGMEXIT       NO
         CLI   WRKDSCB,C'4'  WAS IT FOUND ?
         BNE   PGMEXIT       NO
         SR    R15,R15       SET GOOD RETURN
PGMEXIT  LM    R1,R8,24(R13)  RELOAD SOME REGISTERS
         L     R14,12(,R13)  RELOAD RETURN ADDRESS
         BR    R14           RETURN
         SPACE 2
PATOBT   CAMLST SEARCH,1-1,2-2,3-3
         LTORG ,
         SPACE 1
         PRINT NOGEN
         SPACE 1
USERSAVE DSECT ,             MY WORK AREA MAPPED ON USER'S SAVE AREA
DYNOBT   CAMLST SEARCH,1-1,2-2,3-3
         SPACE 1
PRMDSECT DSECT ,             CALLER'S PARMS
PRM@DSN  DS    A             REQUESTED DATA SET NAME
PRM@VOL  DS    A             REQUESTED VOLUME SERIAL
PRM@WORK DS    A             ADDRESS OF WORK AREA FOR RETURN DATA
         SPACE 1
WRKDSECT DSECT ,             CALLER'S PARMS
WRKDSN   DS    CL44          ALTERED (?) DATA SET NAME
WRKDSCB  DS    CL96          RETURN DATA
WRKSIZE  EQU   *-WRKDSECT
         SPACE 1
         YREGS ,
         END   ,
