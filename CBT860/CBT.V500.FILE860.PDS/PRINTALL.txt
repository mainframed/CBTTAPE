PALL     TITLE 'P R I N T A L L  ***  PRINT OR DUMP ANY DATA SET'
         PUNCH ' ORDER PRINTALL(P) '  EASIER DUMPS              GP05035
         SPACE 1
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1981,1990  EXPERT SYSTEM PROGRAMMING, INC.         *
*        COPYRIGHT 1998,1999  GERHARD POSTPISCHIL                     *
*        COPYRIGHT 2002       EXPERT SYSTEM PROGRAMMING               *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*          E-MAIL : GERHARD@POSTPISCHIL.COM                           *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 1                                                GP99187
*   DEPENDENCIES - LOCAL AND IBM MACROS                         GP99187
*                                                               GP99187
*   JCL - 64K TO 512K REGION, DEPENDING ON BLOCKSIZE            GP99187
*   SYSPRINT DD - LISTING FBA/VBA/FBM/VBM, 133/137              GP99187
*                                                               GP99187
*   SYSUT1 TO SYSUT99 - INPUT FILES TO BE PROCESSED             GP99187
*     DCB PARAMETERS ARE REQUIRED FOR THE RECORD OPTIONS.       GP99187
*     DATASET MAY BE VSAM (PASSWORD REQUIRED IF PROTECTED)      GP99187
*     IN VSAM MODE, RECORDS ARE TREATED AS BLOCKS UNLESS A RECORD
*     OPTION IS CHOSEN; KEYS ARE IGNORED (PRINTED AS DATA); RDWS
*     ARE PART OF THE DATA.                                     GP99187
*                                                               GP99187
*   PARM FIELD:   0 TO 3 NUMERIC VALUES, SEPARATED BY BLANKS,   GP99187
*     COMMAS, OR PROCESSING OPTIONS. EXTRA COMMA INDICATES MISSING
*     OPERAND FIELD:                                            GP99187
*                                                               GP99187
*     1) FIRST NUMERIC IS NUMBER OF RECORDS/BLOCKS TO PRINT     GP99187
*     2) SECOND IS STARTING RECORD/BLOCK NUMBER                 GP99187
*     3) THIRD IS ENDING RECORD/BLOCK NUMBER                    GP99187
*                                                               GP99187
*   PRINT FORMATTING OPTIONS :                                  GP99187
*                                                               GP99187
*     L (DEFAULT) - PLAIN LISTING, 100 CHARACTERS PER LINE      GP99187
*     H - HEXADECIMAL DUMP, 48 BYTES PER LINE                   GP99187
*     D - HEXADECIMAL AND EBCDIC (SIDE BY SIDE), 32 BYTES PER LINE
*     V - VERTICAL DUMP, 100 BYTES PER LINE; TOP LINE SHOWS EBCDIC,
*         MIDDLE LINE SHOWS ZONES, BOTTOM LINE SHOWS NUMERICS.  GP99187
*     Y - HORIZONTAL DUMP, 48 BYTES PER LINE; TOP LINE SHOWS EBCDIC,
*         BOTTOM LINE SHOWS (TWO-BYTE) HEXADECIMAL.             GP99187
*                                                               GP99187
*   PROCESSING OPTIONS (DEFAULT IS BLOCK MODE) :                GP99187
*                                                               GP99187
*     X - USE EXCP ACCESS METHOD (TAPE ONLY)                    GP99187
*     K - KEYS ARE TREATED AS DATA -> FORCES BLOCK MODE         GP99187
*     W - PROCESS RECORDS RATHER THAN BLOCKS; DATA ASSUMED TO BE IN
*         NIH COMPATIBLE WYLBUR EDIT FORMAT (RECFM=U). OPTION TREATED
*         AS R FOR RECFM=F, D AND V, AND BLOCK MODE IF U, NOT WYLBUR.
*     R - PROCESS RECORDS RATHER THAN BLOCKS. VALID FOR RECFM=F, D,
*         AND V. FOR V FORMAT, SEGMENTS ARE PROCESSED AS RECORDS.
*     S - TREATED AS R EXCEPT FOR RECFM=VS AND VBS. FOR THESE,  GP99187
*         SEGMENTS ARE CONSOLIDATED (UP TO 32K) INTO SINGLE RECORDS.
*     I - WITH R AND S, RDW/SDW ARE PRINTED WITH DATA           GP99187
*     Q - FORCE ASCII TRANSLATE REGARDLESS OF OPTCD             GP99187
*     F - CONVERT FROM 9-BIT ASCII FIELD-DATA FORMAT (UNIVAC)   GP99187
*     A - CONVERTS PRINTABLE OUTPUT TO ASCII, BUT NOT HEX DATA; ALSO
*         CONVERTS SCAN STRINGS TO ASCII.                       GP99187
*     - - READ BACKWARDS (SUPPORTED FOR TAPE ONLY)              GP99187
*                                                               GP99187
*   SCAN OPTIONS :                                              GP99187
*                                                               GP99187
*     OPTIONS ARE SUPPLIED ON SYSIN, ONE REQUEST PER CARD.      GP99187
*     INPUT IS FREE FORMAT USING COLUMNS 1 TO 72.               GP99187
*     IF NOT SUPPLIED, ALL RECORDS WITHIN THE START/END RANGE ARE
*     PRINTED.  IF SUPPLIED, RECORDS ARE PRINTED WHEN ANY SCAN OPT
*     IS SATISFIED.                                             GP99187
*                                                               GP99187
*         FORMATS ARE:                                          GP99187
*                                                               GP99187
*         'STRING' (COL,COL)                                    GP99187
*                                                               GP99187
*        'STRING' MAY BE 'STRING', "STRING" OR X'STRING'        GP99187
*        (COL,COL) MAY BE OMITTED (ALL BYTES SCANNED)           GP99187
*        (COL) REQUESTS A SINGLE COLUMN TEST                    GP99187
*        (COL,COL) SPECIFIES THE START AND END COLUMNS IN WHICH THE
*              FIRST BYTE OF THE STRING MAY APPEAR.             GP99187
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
PRINTALL PGMHEAD ZERO12,BASE=(R11,R12),PARM=R7,BNDRY=PAGE       GP02338
         DROP  R13                                               81222
         LR    R10,R13       SET WORK AREA FOR DCB AND SYNAD EXITS
         USING SAVE,R10                                          81222
         L     R8,=A(INITCODE)  GET INITIALIZATION              GP99187
         BR    R8            AND INVOKE IT                      GP99187
         SPACE 2                                                 81222
NEXTDDNM BAS   R9,MAKEDD     INITIALIZE FOR NEW DD               81222
         SPACE 1
         RDJFCB MF=(E,OPUT1)   READ JFCB FOR INPUT FILE
         BXLE  R15,R15,PARMTEST  OK; MAKE TITLE AND CHECK PARM
         PRTLIST MSGNOUT1    SET FOR SYSUT1 MISSING              81222
QUIT8    MVI   CONDCODE+1,8  SET LEVEL 8 ERROR                   83277
         B     EOJCOM        AND EXIT                            83277
         SPACE 1
PARMTEST PRTLIST TITLE,TITLE=1   BUILD TITLE
         TM    PARMFG,FPDONE   RECURSION ?                      GP99187
         BNZ   PARMDONE      YES; SKIP PARM PROCESSING           81222
         L     R8,=A(INITCODE)  GET INITIALIZATION              GP99187
         B     INITPARM-INITCODE(,R8)  INVOKE PARM ANALYSIS     GP99187
PARMDONE MVC   OPTMODE,=C' BLOCK'  SET DEFAULT OF BLOCK MODE
         TM    FLAGC,FGASC9+FGASCII  FORCED ASCII ?              81253
         BZ    *+8           NO                                  81253
         OI    FLAGC,FGTRANS  YES; SET ASCII TRANSLATE ON        81253
         TM    FLAGS,FGREC   RECORD MODE ?
         BZ    *+10          NO
         MVC   OPTMODE,=C'RECORD'
         MVC   SAVSTART(12),START  SAVE COUNTERS                 81222
         MVC   SAVFLAGS(2),FLAGS       AND FLAGS                GP99187
         SR    R1,R1                                            GP99187
         IC    R1,FLAGS      GET FLAG BITS
         SRL   R1,5          RETAIN ONLY FORMAT OPTION BITS     GP99187
         MH    R1,=AL2(OPTFORM2-OPTFORM1)  MAKE OFFSET
         LA    R1,OPTFORM1(R1)  POINT TO OPTION LIST
         MVC   OPTSUB1(OPTFORM2-OPTFORM1),0(R1)  SET CURRENT OPTIONS
         L     R1,OPTSUB1    GET TOPT SUBTITLE
         PRTLIST (R1),TITLE=3  PRINT TOP
         L     R1,OPTSUB2    GET BOTTOM SUBTITLE
         PRTLIST (R1),TITLE=4  PRINT BOTTOM
         TM    PARMFG,FPDFLT   DEFAULT PARMS ?
         BZ    PARMSCON      NO
         PRTV  MSGDFPRM,CC=NO  SHOW DEFAULT PARM FIELD
PARMSCON TM    PARMFG,FPERR  PRIOR ERROR ?
         BZ    PARMSCOR      NO; NO MESSAGE
         BAS   R9,CLRLINE    CLEAR PRINT LINE                   GP99187
         MVI   LINECC,C'0'
         MVC   LINEHEAD(19),=C'ERROR IN PARM FIELD'
         LA    R3,LINETEXT
         L     R9,4(,R13)    GET CALLER'S SAVE AREA              86150
         L     R9,24(,R9)    GET ORIGINAL R1                     86150
         L     R9,0(,R9)     LOAD PARM ADDRESS                   86150
         LA    R5,2(,R9)     PARM ADDRESS
         LH    R4,0(,R9)     LENGTH
         BCTR  R4,0
         EX    R4,EXMVC35    MOVE TEXT
         BAS   R9,PRINT      PRINT THE LINE                     GP99187
PARMSCOR PRTLIST OPTMSG      PRINT OPTIONS
         TM    PARMFG,FPDONE   RECURSION ?                      GP99187
         BNZ   SCNDONE       YES; SKIP SCAN                      81222
         L     R8,=A(SCANINIT)  GET SCAN INITIALIZATION          90277
         BR    R8            INVOKE IT                           90277
         SPACE 1                                                 90277
SCNDONE  LA    R8,SYSUT1     SET FOR CONVENIENT ADDRESSING
         USING IHADCB,R8
         DEVTYPE DCBDDNAM,DB  GET BLOCKSIZE                      90277
         BXH   R15,R15,OPENBAD
         TM    DB+2,UCB3DACC+UCB3TAPE+UCB3UREC  USABLE DEVICE ?  83277
         BNM   OPENNUSE      NO; SKIP IT WITH WARNING MESSAGE    83277
         MVI   RECFM,DCBOPTQ   PRESET TO ALLOW OPTCD=Q
         MVI   LRECL,0       SET MASK FOR KEYLEN                 81222
         MVI   VSPREV,0      SET SEGMENT COMPLETE                81222
         CLI   DB+2,UCB3TAPE   TAPE DEVICE ?                     81222
         BNE   OPENNTA       NO                                  81222
         OI    RECFM,DCBOPTZ   YES; SET REDUCED ERROR RECOVERY   81222
         TM    FLAGS,FGEXCP  USER SELECTED EXCP ?                89365
         BZ    OPENNDA       NO                                  89365
         MVC   SYSUT1(PATEXCPL),PATEXCP  MOVE EXCP DCB           89365
         MVC   DCBDDNAM,CURRDDNM  UPDATE DDNAME                  90277
         LA    R1,EXITLIST                                       89365
         STCM  R1,7,DCBEXLST+1                                   90277
         MVI   READCCW,2     SET TAPE READ CODE                  89365
         L     R1,=A(65535)  GET MAXIMUM READ SIZE               89365
         ST    R1,READCCW+4  MAKE REST OF CCW                    89365
         OI    READCCW+4,X'20'  SILLY BIT                        89365
         XC    READIOB(IOBEXTEN-IOBSTDRD),READIOB  CLEAR         89365
         LA    R2,READIOB                                        89365
         USING IOBSTDRD,R2   TEMPORARY DECLARATION               89365
         LA    R1,READECB    GET ECB ADDRESS                     89365
         ST    R1,IOBECBPT   STASH POINTER                       89365
         LA    R1,READCCW    GET CCW                             89365
         ST    R1,IOBSTART   SET NORMAL START                    89365
         ST    R1,IOBRESTR   AND RESTART                         89365
         MVI   IOBINCAM+L'IOBINCAM-1,1  SET BLOCK COUNT = 1      89365
         ST    R8,IOBDCBPT   DCB POINTER                         90277
         MVI   IOBFLAG1,IOBUNREL  SET UNRELATED                  89365
         DROP  R2                                                89365
         B     OPENEXCP      SKIP AROUND DISK STUFF              89365
OPENNUSE PRTLIST MSGBDDVC    UNUSABLE INPUT DEVICE               83277
         OI    CONDCODE+1,4  SET WARNING                         83277
         B     EOJCOM        TRY FOR ANOTHER REQUEST             83277
OPENNTA  NI    FLAGC,255-FGBACK  RDBACK ONLY ON TAPE            GP99187
         OI    JFCBTSDM,JFCNWRIT   KEEP JOBQUEUE CHASTE
         CLI   DB+2,UCB3DACC  DASD ?                             81222
         BNE   OPENNDA       NO
         MVI   LRECL,X'FF'   PERMIT KEYS                         81222
         SERVCALL DSDJ1,INFMJFCB   GET DSCB 1 FOR DATASET        81222
         BXLE  R15,R15,OPENDSCB  OK                              81222
         CLI   JFCBDSNM,X'04'  VTOC ?                            81222
         BNE   OPENNDA       NO; OPEN WILL FAIL                  81222
OPENDSCB CLI   JFCBDSNM,X'04'   VTOC ?                           81223
         BNE   OPENNVTO      NO                                  81222
         MVI   DCBKEYLE,44   SET KEY LENGTH                      90277
         MVI   RECFM,0       KILL OPTCD                          81222
         LA    R0,96         SET DATA LENGTH                     81222
         STH   R0,DCBLRECL    INTO RECORD LENGTH                 90277
         LA    R0,44+96      SET KEY+DATA LENGTH                 81222
         STH   R0,DCBBLKSI                                       90277
         STH   R0,BLKSIZE    ALSO SAVE BLOCKSIZE                 81222
         B     OPENQIRK      GO TO OVERRIDE                      81222
RDBACK   OPEN  (OPENNVTO,(RDBACK)),MF=L                         GP99187
RDAHED   OPEN  (OPENNVTO,(INPUT,REREAD)),MF=L                   GP99187
         USING DS1FMTID,R1   DECLARE FORMAT 1 DSCB MAPPING       81223
OPENNVTO TM    DS1DSORG+1,X'08'   VSAM ?                         90277
         BZ    OPENNASM      NO                                  90277
         MVI   PFLAG,FGVSAM  SET VSAM MODE                       90277
         MVC   UT1ACB(PATACBLN),PATACB  REFRESH THE ACB          90277
         MVC   DCBDDNAM,CURRDDNM  RESTORE DDN                    90277
         OPEN  MF=(E,OPUT1)   OPEN ACB FOR INPUT                 90277
         TM    DCBOFLGS,DCBOFOPN  OPEN ?                         90277
         BZ    OPENBAD       NO                                  90277
         GENCB BLK=RPL,ACB=(S,UT1ACB),AM=VSAM,                         *
               OPTCD=(KEY,SEQ,ASY,KGE,LOC),                            *
               AREA=(S,UT1RECAD),AREALEN=4,ARG=(S,UT1RRN),             *
               MSGAREA=(S,LINETEXT),MSGLEN=132,MF=(G,UT1GNCB,UT1GNCBL)
         BXH   R15,R15,OPENBAD  FAILED                           90277
         XC    UT1RRN,UT1RRN   CLEAR, JUST IN CASE               90277
         LR    R15,R1        SWAP ADDRESS                        90277
         STM   R15,R0,BLOCKADE  SAVE RPL ADDRESS/LENGTH          90277
         MVI   RECFM,0       NO RECFM                            90277
         MVC   BLKSIZE,=H'32767'  SET MAXIMUM BLOCKSIZE          90277
         SHOWCB ACB=(S,SYSUT1),AREA=(S,DB),LENGTH=8,FIELDS=KEYLEN,     *
               MF=(G,UT1SHCB,UT1SHCBL)                           90351
         BXH   R15,R15,OPENOK2   MAY FAIL LATER ?                90277
         ICM   R15,15,DB     TEST KEY LENGTH                     90277
         BNZ   OPENOK2       PROCEED                             90277
         L     R2,BLOCKADE   RELOAD RPL ADDRESS                  90277
         MODCB RPL=(R2),OPTCD=(ADR,SEQ,ASY,NUP,LOC), NO KEY           R*
               MF=(G,UT1MDCB,UT1MDCBL)                           90351
         B     OPENOK2       START PROCESSING                    90277
OPENNASM TM    DS1DSORG,255-JFCORGPS-JFCORGPO-JFCORGU  FUNNY ?   81222
         BNZ   OPENNOPQ      YES; KILL OPTCD=Q
         CLI   DS1DSORG+1,0
         BE    OPENQOK       LEAVE IT SET TO Q
OPENNOPQ MVI   RECFM,0       KILL OPTCD
OPENQOK  TM    DS1DSORG,JFCORGPO  PARTITIONED ?
         BZ    OPENKEYT      NO; SEE IF KEYED                    81223
         CLI   JFCBELNM,C' '   MEMBER SPECIFIED ?
         BH    OPENKEYT      YES; SEE IF KEYED                   81223
         MVI   DCBKEYLE,8    SET KEY LENGTH                      90277
         OI    PFLAG,FGPDS   INDICATE PDS PROCESSING            GP99187
         MVI   DCBRECFM,DCBRECKL+DCBRECU  SET KEYLE+UNDEF       GP99187
         NI    FLAGS,255-FGSEG  RESET SEGMENTATION PROCESSING   GP99187
         B     OPENKEYT      DEBLOCK DIRECTORY WHEN FEASIBLE    GP99187
OPENQIRK NI    FLAGS,255-FGREC-FGSEG  NO DEBLOCKING FOR DIRECTORY
         MVI   DCBRECFM,DCBRECKL+DCBRECU  KEYLE/UNDEF            90277
         MVC   OPTMODE,=C' BLOCK'  SET TO BLOCK MODE
         AIF   (NOT &MVS).NOTMVSO                                83026
OPENKEYT DS    0H            FOR MVS, QSAM GETS MAXIMUM RATHER THAN
OPENNDA  DS    0H            TRUE BLOCKSIZES, HENCE BSAM IS FORCED.
OPENBSAM MVI   DCBMACR1,DCBMRRD  FORCE BSAM READ MODE            90277
         DROP  R1                                                83026
OPENEXCP L     R0,DB+4       GET DEVICE CAPACITY                 89365
         AGO   .NOTMVSP                                          83026
.NOTMVSO B     OPENBSAM      USE BSAM TO GET KEYS                83026
OPENKEYT CLI   DS1KEYL,0     ANY KEYS ?                          81223
         BNE   OPENBSAM      YES; USE BSAM TO GET KEYS           81223
         CLI   JFCKEYLE,0    DOES USER WANT TO FORCE KEYS ?      81223
         BE    OPENNDA       NO; USE QSAM                        81223
OPENBSAM MVI   DCBMACR1,DCBMRRD  SET BSAM READ MODE              90277
         DROP  R1
OPENEXCP DS    0H                                                89365
OPENNDA  L     R0,DB+4       GET DEVICE CAPACITY
.NOTMVSP LH    R1,=H'32760'  SET FOR VS OPEN LIMIT               83026
         CR    R0,R1         EXCEEDS SYSTEM MAX ?
         BNH   *+6           NO
         LR    R0,R1         TRUNCATE TO MAX
         STH   R0,BLKSIZE    TEMPORARY SAVE FOR OPEN EXIT
         MVC   OPUT1(1),RDAHED  MAKE OPEN OPTION BYTE           GP99187
         TM    FLAGC,FGBACK  READ BACKWARDS?                    GP99187
         BZ    *+10          NO                                 GP99187
         MVC   OPUT1(1),RDBACK  COPY OPEN OPTION BYTE           GP99187
         OPEN  MF=(E,OPUT1),TYPE=J   OPEN INPUT FILE            GP02338
         TM    DCBOFLGS,DCBOFOPN   OPENED OK ?
         BNZ   OPENOK        YES
OPENBAD  PRTLIST MSGBDOPN    EXPLAIN ABOUT BAD OPEN              81222
         B     QUIT8         QUIT WITH ERROR CODE 8              83277
OPENOK   SR    R3,R3         SET BSAM BUFFER SIZE (NONE)         81222
         TM    DCBMACF1,DCBMRRD   USING BSAM ?                   81222
         BZ    OPENNBSM      NO                                  81222
         LH    R3,BLKSIZE    GET KEY LENGTH+BLOCKSIZE            81222
         MVC   DECB(LENDECB),PATDECB  COMPLETE DECB FOR BSAM     81222
OPENNBSM SR    R2,R2         CLEAR RECORD INTERFACE AREA         81222
         TM    RECFM,DCBRECU   F, U, OR V ?                      81253
         BZ    *+8           NO                                  81253
         NI    RECFM,255-DCBRECTO  YES; RESET TRACK OVERFLOW     81253
         TM    RECFM,DCBRECLA-DCBRECV  ANY NON-V FORM ?          81253
         BNZ   OPENGETM      YES; NOT V                          81222
         TM    RECFM,DCBRECV+DCBRECSB  VARIABLE SPANNED ?        81222
         BNO   OPENGETM      NO                                  81222
         TM    FLAGS,FGSEG   DEBLOCK SPANNED OPTION ?            81222
         BZ    OPENGETM      NO                                  81222
         ICM   R2,3,LRECL    GET RECORD AREA SIZE                81222
         BNZ   OPENGETS      OK IF NOT ZERO                      81222
         LH    R2,DCBBLKSI   ELSE USE BLOCKSIZE AS DEFAULT       81222
         B     OPENGETS      RUN WITH SEGMENTS                   81222
OPENGETM NI    FLAGS,255-FGSEG  RESET SPANNED OPTION             81222
OPENGETS TM    DCBMACF1,DCBMRECP  EXCP MODE ?                    89365
         BZ    OPENGETX      NO                                  89365
         L     R2,=A(65535)  SET FOR MAXIMUM BLOCK               89365
OPENGETX ST    R2,RECADE+4   SAVE RECORD AREA SIZE               89365
         AR    R2,R3         GET TOTAL SIZE REQUIRED             81222
         BNP   OPENOK2       NONE                                81222
         LA    R2,4(,R2)     INSURANCE FOR XC                    83026
         GETMAIN R,LV=(R2)   GET STORAGE                         81222
         STM   R1,R2,BLOCKADE  SAVE ADDRESS AND LENGTH           81222
         AR    R1,R3         GET START OF RECORD INTERFACE AREA  81222
         ST    R1,RECADE     SAVE START                          81222
         XC    0(4,R1),0(R1)  CLEAR NEW RDW                      81222
OPENOK2  NI    RECFM,DCBRECLA   CLEAR USER'S RECORD FORMAT       81253
         CLI   RECFM,DCBRECU   U FORMAT ?                        81253
         BNE   *+8           NO                                  81253
         MVI   RECFM,0       CLEAR IT COMPLETELY
         BAS   R9,CLRLINE    CLEAR PRINT LINE                   GP99187
         DROP  R8            FINISHED WITH DCB MAPPING
         SPACE 2                                                 90277
READ     MVC   BLOCKMTX,BLOCKMTX-1   CLEAR MESSAGES              81222
         XC    BLOCKHED,BLOCKHED  RESET BLOCK MESSAGE
         TM    PFLAG,FGVSAM  VSAM MODE ?                         90277
         BNZ   READVSAM      YES                                 90277
         TM    DCBMACF1-IHADCB+SYSUT1,DCBMRECP  EXCP MODE ?      90277
         BZ    READBSAM      NO                                  89365
         L     R5,BLOCKADE   GET BUFFER ADDRESS                  89365
         STCM  R5,7,READCCW+1  STASH READ ADDRESS                89365
         LA    R2,READIOB                                        89365
         USING IOBSTDRD,R2   DECLARE IT                          89365
         XC    IOBSENS0(2),IOBSENS0  CLEAR IOB                   89365
         XC    IOBCSW,IOBCSW FOR DEBUGGING                       89365
         ST    R2,DCBIOBAD-IHADCB+SYSUT1                         89365
         EXCP  READIOB       SCHEDULE THE READ                   89365
         WAIT  ECB=READECB   WAIT FOR COMPLETION                 89365
         SR    R6,R6                                             89365
         ICM   R6,3,READCCW+6  GET READ LENGTH                   89365
         SR    R0,R0                                             89365
         ICM   R0,3,IOBCSW+5  GET UNREAD LENGTH                  89365
         SR    R6,R0         GET TRUE LENGTH READ                89365
         BNM   *+6                                               89365
         SR    R6,R6                                             89365
         STH   R6,DCBLRECL-IHADCB+SYSUT1   SET INTO FAKE POSITION
         CLI   READECB,X'7F' GOOD COMPLETION ?                   89365
         BE    READCOMM      JOIN COMMON                         89365
         CLI   READECB,X'41'   END-FILE ?                        89365
         BNE   SYNADX        NO; DIAGNOSE ERROR                  89365
         TM    IOBCSW+3,1    END-FILE (UNIT EXCEPTION) HIT ?     89365
         BNZ   EOJEOF        YES; TAKE END-FILE                  89365
         B     SYNADX        NO; PROCESS SYNAD                   89365
         DROP  R2                                                89365
READBSAM TM    DCBMACF1-IHADCB+SYSUT1,DCBMRRD    BSAM MODE ?     90277
         BZ    READQSAM      NO; GET A BLOCK                     81222
         L     R5,BLOCKADE   GET THE BUFFER ADDRESS              81222
         READ  DECB,SF,SYSUT1,(R5),MF=E   READ A BLOCK           81222
         CHECK DECB          WAIT FOR COMPLETION                 81222
         B     READCOMM      JOIN COMMON CODE                    81222
         SPACE 1                                                 90277
READVSAM L     R2,BLOCKADE   GET RPL ADDRESS                     90277
         USING IFGRPL,R2     DECLARE IT                          90277
         XC    LINETEXT(4),LINETEXT  CLEAR VSAM SYNAD MESSAGE    90277
         GET   RPL=(R2)      GET A RECORD                        90277
         BXH   R15,R15,VSERROR                                   90277
         CHECK RPL=(R2)      TAKE EXITS                          90277
         BXH   R15,R15,VSERROR                                   90277
         L     R6,RPLRLEN-IFGRPL(,R2)  GET RECORD LENGTH         90277
         L     R5,UT1RECAD   GET RECORD ADDRESS                  90277
         CH    R6,=H'14'     IS THIS AN SMF EOF RECORD ?         92279
         BNE   READCOML      NO                                  92279
         CLC   =C'SMFEOFMARK',4(R5)  LOGICAL EOF ?               92279
         BE    EOJEOFN       YES; QUIT NOW                       92279
         B     READCOML      JOIN COMMON                         90277
         SPACE 1                                                 90277
VSERROR  PRTLIST BADVSAM                                         90277
         INC   NUMERR        COUNT ERROR                         90277
         OI    CONDCODE+L'CONDCODE-1,8  SET ERROR                90277
         ICM   R0,3,LINETEXT  WAS A SYNAD MESSAGE WRITTEN ?      90277
         BZ    EOJCOM        NO                                  90277
         PRTLIST LNTXT27     WRITE THE ERROR MESSAGE            GP02338
         B     EOJCOM        AND QUIT                            90277
LNTXT27  FD    LINETEXT+27,101,NL  ERROR MESSAGE                GP02338
         FD    *END                                             GP02338
BADVSAM  FD    '***** VSAM ERROR AFTER BLOCK',NL                 90277
         FD    NUMBLOCK,I,PADL                                   90277
         FD    '; RC='                                           90277
         FD    RPLRTNCD,I                                        90277
         FD    ', FTNCD='                                        90277
         FD    RPLFDB2,I                                         90277
         FD    ', FDBK='                                         90277
         FD    RPLERRCD,I                                        90277
         FD    '('                                               90277
         FD    RPLERRCD,HEX                                      90277
         FD    ') *****'                                         90277
         FD    *END                                              90277
         DROP  R2                                                90277
         SPACE 1                                                 90277
READQSAM GET   SYSUT1        GET THE NEXT BLOCK                  81222
         LR    R5,R1         SAVE BLOCK ADDRESS                  81222
READCOMM SR    R6,R6                                             89365
         ICM   R6,3,DCBLRECL-IHADCB+SYSUT1   LRECL IS SIZE OF BLOCK
         SPACE 1
READCOML LA    R7,1          INCREASE                            81222
         A     R7,NUMBLOCK
         ST    R7,NUMBLOCK   UPDATE CURRENT BLOCK NUMBER
         ST    R6,CURLEN     SAVE FOR BLOCK HEADER               81253
         LA    R7,0(R5,R6)   GET LAST BYTE +
         BCTR  R7,0          SET LAST BYTE
         TM    FLAGC,FGASC9  FIELD DATA ?                        81253
         BZ    NOTFIELD      NO                                  81253
         LTR   R3,R6         COPY LENGTH                         81253
         BZ    NOTASCII      ZERO LENGTH RECORD ?                81253
         SR    R2,R2         CLEAR FOR DIVIDE                    81253
         SLL   R3,3          MULTIPLY BY 8 TO GET BITS           81253
         D     R2,=F'9'      GET FULL BYTES AFTER CONVERSION     81253
         LR    R6,R3         SET NEW LENGTH                      81253
         ST    R6,CURLEN     UPDATE LENGTH FOR BLOCK HEADER      81253
         LA    R7,0(R5,R6)   SET NEW END ADDRESS+1               81253
         BCTR  R7,0          SET NEW END ADDRESS                 81253
         LA    R2,1          SET BX INCREMENT                    81253
         LR    R3,R7         COPY END ADDRESS                    81253
         LR    R14,R5        COPY START ADDRESS                  81253
         LR    R15,R14       COPY START ADDRESS FOR OUTPUT       81253
FIELDGRP LM    R0,R1,0(R14)    LOAD 8 BYTES UNALIGNED            81253
         SLDL  R0,1          DELETE HIGH ORDER BIT               81253
         LA    R4,7          PROCESS SEVEN BYTES                 81253
FIELDBYT STCM  R0,8,0(R15)   STASH A BYTE                        81253
         BXH   R15,R2,NOTFIELD  QUIT IF ALL DONE                 81253
         SLDL  R0,9          POSITION NEXT BYTE                  81253
         BCT   R4,FIELDBYT   DO NEXT BYTE                        81253
         MVC   0(1,R15),8(R14)  DO LAST BYTE OF GROUP            81253
         LA    R14,9(,R14)   POSITION TO NEXT GROUP              81253
         BXLE  R15,R2,FIELDGRP  DO NEXT GROUP                    81253
         SPACE 1                                                 81253
NOTFIELD TM    FLAGC,FGTRANS   ASCII TRANSLATE REQUIRED ?        81253
         BZ    NOTASCII      NO                                  81253
         LR    R2,R5         COPY START ADDRESS                  81253
         LTR   R3,R6         CHECK LENGTH                        81253
         BNP   NOTASCII      NOTHING TO DO                       81253
         LA    R0,256        MAXIMUM TRANSLATABLE                81253
TRTOLOOP LR    R1,R0         SET LENGTH                          81253
         CR    R1,R3         COMPARE TO RESIDUAL LENGTH          81253
         BNH   *+6           MORE THAN ONE SEGMENT               81253
         LR    R1,R3         PARTIAL SEGMENT LENGTH              81253
         BCTR  R1,0          SET LENGTH FOR EXECUTE              81253
         EX    R1,TRTOTR     TRANSLATE                           81253
         AR    R2,R0         NEXT SEGMENT ADDRESS                81253
         SR    R3,R0         REMAINING LENGTH                    81253
         BP    TRTOLOOP                                          81253
         B     NOTASCII      PROCEED WITH PROCESSING             81253
TRTOTR   TR    0(0,R2),TRANSTAB  TRANSLATE TO EBCDIC             81253
         SPACE 1                                                 81253
NOTASCII LR    R14,R5        SAVE BLOCK START ADDRESS            81253
         SR    R15,R15       CLEAR PRESUMED KEY LENGTH           81222
         TM    PFLAG,FGVSAM  VSAM MODE ?                         90277
         BNZ   READNOKY      TREAT KEY AS DATA                   90277
         TM    DCBMACF1-IHADCB+SYSUT1,DCBMRRD  USING BSAM ?      90277
         BZ    READNOKY      NO; QSAM DOES NOT MAKE KEY AVAILABLE
         TM    FLAGS,FGKEY   USER WANTS KEYS AS DATA ?           81222
         BNZ   READNOKY      YES                                 81222
         IC    R15,DCBKEYLE-IHADCB+SYSUT1  GET KEY LENGTH        90277
         AR    R5,R15        ADJUST BLOCK START ADDRESS          81222
         SR    R6,R15        ADJUST RESIDUAL LENGTH              81222
READNOKY STM   R14,R15,KEYADE  SAVE FOR BLOCK FORMATTING         81222
         STM   R5,R7,BLOCKXLE  SET BLOCK BXLE POINTERS           81222
         LA    R1,BLOCKFD    GET PRT LIST FOR BLOCK HEADER       81222
         ST    R1,BLOCKHED   INDICATE MESSAGE PRESENT
         MVC   BLOCKMER(10),=C'BAD LENGTH'  PRESET
         LTR   R6,R6         ZERO LENGTH READ ?
         BNP   BLOCKED       VERRRRY INTERRRRESTING...
         CH    R6,BLKSIZE    LONGER THAN USER'S DCB SIZE ?
         BH    BLOCKED
         TM    FLAGS,FGREC   DEBLOCK OPTION ?
         BZ    BLOCKNER      NO; JUST DUMP THE BLOCK
         SR    R15,R15       SET BLOCK DESCRIPTOR LENGTH         81253
         CLI   RECFM,DCBRECTO  D FORMAT ?                        81253
         BE    VADCOMM       YES                                 81253
         CLI   RECFM,DCBRECV  CHECK FORMAT
         BH    FIXED         FIXED
         BL    TESTWYL       UNDEFINED (OR D) - SEE IF WYLBUR
         MVC   BLOCKMTX(4),=C'BDW='
         UNPK  BLOCKMTX+4(9),0(5,R5)  SHOW BLOCK HEADER
         TR    BLOCKMTX+4(8),TRANNUM
         MVI   BLOCKMTX+12,C' '
         CLM   R6,3,0(R5)    LENGTH=BDW LENGTH ?
         BL    BLOCKED       NO; DUMP FULL BLOCK                 81222
         BE    *+8           SAME                                81222
         LH    R6,0(,R5)     USE BDW LENGTH                      81222
         LA    R15,4         SET INITIAL OFFSET                  81253
VADCOMM  SR    R14,R14                                           81253
         MVC   BLOCKMER(11),=C'INVALID RDW'                      81253
         CH    R6,=H'8'      AT LEAST 8 BYTES ?
         BL    BLOCKED       NO; ERROR
VARCHECK CR    R15,R6        DONE ?
         BH    BLOCKED       BDW NOT = SUM OF RDW
         BE    VARCHEKT      OK
         LA    R1,0(R5,R15)  SPACE TO NEXT RDW
         CR    R1,R7         ROOM FOR 2 BYTES ?
         BNL   BLOCKED       NO; ERROR
         CLI   RECFM,DCBRECTO  D FORMAT ?                        81253
         BNE   VADCOMR       NO                                  81253
         MVC   DB(4),0(R1)   MOVE EBCDIC LENGTH                  81253
         NC    DB(4),=4C'0'  MASK ZONES                          81253
         CLC   DB(4),=4C'0'  NUMERIC ZONES ?                     81253
         BNE   BLOCKED       NO; BAD RDW                         81253
         CLI   0(R1),C'9'    VALID NUMERIC ?                     81253
         BH    BLOCKED       NO                                  81253
         CLI   1(R1),C'9'    VALID NUMERIC ?                     81253
         BH    BLOCKED       NO                                  81253
         CLI   2(R1),C'9'    VALID NUMERIC ?                     81253
         BH    BLOCKED       NO                                  81253
         CLI   3(R1),C'9'    VALID NUMERIC ?                     81253
         BH    BLOCKED       NO                                  81253
         PACK  DB,0(4,R1)    PACK IT                             81253
         CVB   R14,DB        GET LENGTH                          81253
         B     VADCOMRD                                          81253
VADCOMR  ICM   R14,3,0(R1)   GET LENGTH IN THIS RDW              81253
         BNP   BLOCKED       INVALID RDW
VADCOMRD CH    R14,=H'4'                                         81253
         BL    BLOCKED       ERROR ON SHORT RDW                  81253
         AR    R15,R14
         B     VARCHECK
VARCHEKT CLI   RECFM,DCBRECTO  D FORMAT ?                        81253
         BE    VARCHEKM      YES; SKIP BDW SKIP                  81253
         LA    R5,4(,R5)     SKIP BDW                            81253
VARCHEKM MVC   BLOCKMER,BLOCKMER-1  RESET ERROR MESSAGE          81253
VARLOOP  CLI   RECFM,DCBRECTO   D FORMAT ?                       81253
         BNE   VARLOOPV      NO                                  81253
         PACK  DB,0(4,R5)    GET RECORD LENGTH                   81253
         CVB   R6,DB                                             81253
         B     VARLOOPD                                          81253
VARLOOPV ICM   R6,3,0(R5)    GET LENGTH                          81253
VARLOOPD STM   R5,R7,BLOCKXLE  UPDATE BXLE POINTERS              81253
         TM    FLAGS,FGSEG   DOING SEGMENT CONSOLIDATION ?       81222
         BZ    VARNSPAN      NO; PROCESS INDIVIDUALLY            81222
         CLI   RECFM,DCBRECTO D FORMAT?                         GP99187
         BE    VARNSPAN      YES; NO CONTROL BITS               GP99187
         TM    VSPREV,1      ANY DATA STACKED ?                  81222
         BZ    VARSOFF       NO                                  81222
         TM    2(R5),2       ADDITIONAL SEGMENT ?                81222
         BZ    VARPERR       NO; SEQUENCE ERROR                  81222
         L     R4,RECADE     GET RECORD AREA                     81222
         LH    R2,0(,R4)     GET CURRENT LENGTH                  81222
         LA    R3,0(R2,R6)                                       81222
         SH    R3,=H'4'      LENGTH AFTER MOVE                   81222
         C     R3,RECADE+4   WILL IT FIT ?                       81222
         BH    VARPTRUN      NO; NEED TO TRUNCATE                81222
         LA    R14,0(R4,R2)  GET DESTINATION ADDRESS             81222
         LR    R15,R6        COPY LENGTH                         81222
         SH    R15,=H'4'     AFTER MOVE                          81222
         LA    R0,4(,R5)     GET TEXT START                      81222
         LR    R1,R15        COPY LENGTH                         81222
         MVCL  R14,R0        MOVE TO RECORD AREA                 81222
         STH   R3,0(,R4)     SET NEW LENGTH                      81222
         MVC   VSPREV,2(R5)  COPY SPAN CONTROL BITS              81222
         TM    VSPREV,1      LAST ?                              81222
         BNZ   VARBLE        NO; DO NEXT SEGMENT                 81222
         BAS   R9,SPANFORM   FORMAT CONSOLIDATED RECORD          81222
         B     VARBLE        GET ANOTHER                         81222
VARPERR  MVC   BLOCKMER(18),=C'SDW SEQUENCE ERROR'               81222
         B     VARPDUMP                                          81222
VARPTRUN MVC   BLOCKMER(18),=C'SDW LENGTH > LRECL'               81222
VARPDUMP BAS   R9,SPANFORM   FORMAT PRIOR RECORD                 81222
VARSOFF  TM    2(R5),3       SINGLE SEGMENT ?                    81222
         BZ    VARNSPAN      YES; DO NORMALLY                    81222
         TM    2(R5),2       OUT OF SEQUENCE ?                   81222
         BZ    VARSSAVE      NO; JUST SAVE                       81222
         MVC   BLOCKMER(18),=C'SDW SEQUENCE ERROR'               81222
         B     VARNSPAN                                          81222
VARSSAVE L     R4,RECADE     GET RECORD AREA                     81222
         LA    R2,4          SET CURRENT LENGTH                  81222
         LA    R3,0(R2,R6)                                       81222
         SR    R3,R2         LENGTH AFTER MOVE                   81222
         C     R3,RECADE+4   WILL IT FIT ?                       81222
         BH    VARSTRUN      NO; NEED TO TRUNCATE                81222
         LA    R14,0(R4,R2)  GET DESTINATION ADDRESS             81222
         LR    R15,R6        COPY LENGTH                         81222
         SR    R15,R2        AFTER MOVE                          81222
         LA    R0,4(,R5)     GET TEXT START                      81222
         LR    R1,R15        COPY LENGTH                         81222
         MVCL  R14,R0        MOVE TO RECORD AREA                 81222
         STH   R3,0(,R4)     SET NEW LENGTH                      81222
         MVC   VSPREV,2(R5)  COPY SPAN CONTROL BITS              81222
         B     VARBLE        GET ANOTHER SEGMENT                 81222
VARSTRUN MVC   BLOCKMER(18),=C'SDW LENGTH > LRECL'               81222
         B     VARNSPAN      DO NORMALLY                         81222
         SPACE 1                                                 81222
SPANFORM STM   R1,R9,SPANSAVE  SAGE REGISTERS                    81222
         L     R5,RECADE     GET SPANNED RECORD                  81222
         LH    R6,0(,R5)     AND CURRENT LENGTH                  81222
         TM    FLAGS,FGINDW  INCLUDE RDW WITH DATA ?             84165
         BNZ   VARINCL1      YES; DO NOT INDENT                  84165
         MVC   WORK(4),0(R5)  MAKE SIMULATED SDW                 81222
         UNPK  LINERDW+1(9),WORK(5)                              81222
         TR    LINERDW+1(8),TRANNUM   FORMAT RDW IN HEX          81222
         MVI   LINERDW,C'('                                      81222
         MVI   LINERDW+9,C')'                                    81222
         LA    R5,4(,R5)     SKIP RDW                            81222
         SH    R6,=H'4'      ADJUST TO TEXT LENGTH               81222
VARINCL1 BAS   R9,FORMLINE                                       84165
         L     R5,RECADE     GET RECORD AGAIN                    81222
         LA    R6,4          SET CURRENT LENGTH                  81222
         STH   R6,0(,R5)                                         81222
         MVI   VSPREV,0      RESET SEQUENCE BITS                 81222
         MVC   BLOCKMER,BLOCKMER-1  RESET ERROR MESSAGE          81222
         LM    R1,R9,SPANSAVE  RESTORE REGISTERS                 81222
         BR    R9            RETURN TO CALLER                    81222
         SPACE 1                                                 81222
VARNSPAN TM    FLAGS,FGINDW  INCLUDE RDW WITH DATA ?             84165
         BNZ   VARINCL2      YES; DO NOT INDENT                  84165
         MVC   WORK(4),0(R5) SAVE RDW                            84165
         UNPK  LINERDW+1(9),WORK(5)
         TR    LINERDW+1(8),TRANNUM   FORMAT RDW IN HEX
         MVI   LINERDW,C'('
         MVI   LINERDW+9,C')'
         LA    R5,4(,R5)     SKIP RDW
         SH    R6,=H'4'      ADJUST TO TEXT LENGTH               81222
VARINCL2 BAS   R9,FORMLINE                                       84165
VARBLE   LM    R5,R7,BLOCKXLE
         BXLE  R5,R6,VARLOOP   DO NEXT RECORD
         B     READ          ELSE READ ONE
         SPACE 1
FIXED    LH    R2,LRECL      GET USER'S RECORD LENGTH
         LTR   R2,R2
         BNP   BLOCKED       TOO BAD
         LR    R1,R6         GET BLOCK LENGTH
         SR    R0,R0                                            GP99187
         DR    R0,R2         SEE HOW MANY
         LTR   R0,R0         REMAINDER ?
         BZ    FIXDIVID      NO; PROCEED                         86014
         CH    R0,=H'7'      POSSIBLE DOUBLE-WORD ROUNDING ?     86014
         BH    BLOCKED       NO, TREAT AS BLOCK IN ERROR         86014
         SR    R7,R0         ADJUST END TO SKIP EXTRA            86014
         B     FIXOOPS       PROCEED, BUT WITH ERROR MSG         86014
FIXDIVID MVC   BLOCKMER,BLOCKMER-1  RESET ERROR MESSAGE
FIXOOPS  LR    R6,R2         SET RECORD LENGTH FOR INCREMENT     86014
FIXLOOP  BAS   R9,FORMLINE                                      GP99187
         TM    FLAGC,FGBACK  READING BACKWARDS?                 GP99187
         BNZ   FIXLOOPB      YES; APPROACH DIFFERENTLY          GP99187
         BXLE  R5,R6,FIXLOOP
         B     READ
FIXLOOPB SWAPR R5,R7         SET FIRST AS LOW LIMIT; END AS START
         SR    R5,R6         STOP AT THE LAST RECORD            GP99187
         SR    R7,R6         SET FOR BXH                        GP99187
FIXLOOPR LPR   R6,R6         AND SET INCREMENT                  GP99187
         BAS   R9,FORMLINE   PROCESS DATA                       GP99187
         LNR   R6,R6         AND CHANGE INCREMENT TO DECREMENT  GP99187
         BXH   R5,R6,FIXLOOPR  FOR ALL RECORDS                  GP99187
         B     READ          DO ANOTHER BLOCK                   GP99187
         SPACE 1
TESTWYL  ST    R5,DCMBUFAD   SET BUFFER ADDRESS
         MVC   BLOCKMER,BLOCKMER-1  RESET ERROR MESSAGE
         MVC   DCMBUFMX,0(R5)  MOVE BDW
         NI    DCMBUFMX,255-X'80'  RESET NIH FLAG
         CH    R6,DCMBUFMX   POSSIBLE WYLBUR RECORD ?
         BL    BLOCKNWY      NO; CHECK DIRECTORY BLOCK           81222
         STCM  R6,12,DCMBUFLN  CLEAR OFFSET
WYLLOOP  SERVCALL WDCOM,DECOMPRS  TRY TO DECOMPRESS THE RECORD
         CH    R15,=H'4'
         BH    BLOCKNWY      NOT WYLBUR RECORD                   81222
         BE    READ          END OF BLOCK
         L     R5,DCMRECAD   GET RECORD ADDRESS
         LH    R6,DCMRECLN   GET CURRENT LENGTH OF RECORD
         MVC   LINEWYL#+1(8),0(R5)  MOVE RETAINED PORTION OF LINE
         MVC   LINEWYL#(1),DCMLINEH  MOVE OVERFLOW BYTE          81253
         LA    R5,8(,R5)     SKIP LINE NUMBER                    81253
         SH    R6,=H'8'      ADJUST LENGTH                       81253
         BAS   R9,FORMLINE   PRINT IT                           GP99187
         B     WYLLOOP
         SPACE 1                                                GP99187
BLOCKNWY TM    PFLAG,FGPDS   DOING A PDS?                       GP99187
         BZ    BLOCKOFF      NO                                 GP99187
         TM    FLAGS,FGREC   USER WANTS INDIVIDUAL ENTRIES?     GP99187
         BZ    BLOCKOFF      NO                                 GP99187
         CH    R6,=H'256'    REAL DIRECTORY LENGTH ?            GP99187
         BNE   BLOCKOFF      NO; QUIT NOW                       GP99187
         CLC   0(2,R5),=H'256'   IS THE LENGTH <= 256 ?         GP02338
         BH    BLOCKOFF      SHOULD NOT HAPPEN ?                GP02338
         LR    R8,R6         SAVE ORIGINAL LENGTH               GP99187
         LA    R6,2          SET FOR LENGTH OF LENGTH           GP99187
         BAS   R9,FORMLINE   FORMAT LENGTH                      GP99187
         LH    R7,0(,R5)     LOAD LENGTH                        GP99187
         LA    R7,0(R5,R7)   POINT TO END OF BLOCK              GP99187
         BCTR  R7,0          LAST VALID BYTE                    GP99187
         LA    R5,2(,R5)     SKIP LENGTH                        GP99187
PDSLOOP  IC    R6,11(,R5)    INSERT FLAG/LENGTH BYTE            GP99187
         LA    R0,X'1F'      SET MAXIMUM LENGTH MASK            GP99187
         NR    R6,R0         STRIP ALIAS AND TTRN BITS          GP99187
         LA    R6,12(R6,R6)  CONVERT TO LENGTH                  GP99187
         BAS   R9,FORMLINE                                      GP99187
         BXLE  R5,R6,PDSLOOP  DO ALL                            GP99187
         L     R5,KEYADE     RELOAD ADDRESS                     GP99187
         CLC   =X'FFFFFFFFFFFFFFFF0000',0(R5)  END BLOCK ?      GP99187
         BNE   READ          NO; READ NEXT DIRECTORY BLOCK      GP99187
         NI    FLAGS,255-FGREC  DUMP REST AS BLOCKS             GP99187
         B     READ           AND GO TO IT                      GP99187
         SPACE 1                                                GP99187
BLOCKOFF NI    FLAGS,255-FGREC-FGSEG   NOT WYLBUR; USE BLOCK MODE
         MVC   OPTMODE,=C' BLOCK'  SET TO BLOCK MODE             81222
BLOCKNER CLC   BLOCKSYN,BLOCKSYN-1  SYNAD MESSAGE PRESENT ?
         BNE   BLOCKED       YES; PRINT WITH MESSAGE
         MVC   BLOCKMER,BLOCKMER-1  RESET ERROR MESSAGE
         ICM   R0,15,KEYADE+4  ANY KEY?                         GP99187
         BNZ   BLOCKED         YES; LEAVE MESSAGE               GP99187
         XC    BLOCKHED,BLOCKHED  ELSE KILL MESSAGE
BLOCKED  LM    R5,R7,BLOCKXLE  FOR ENTRY FROM FUNNY TESTS
         LA    R9,READ       RETURN TO READ AFTER FORMAT
         SPACE 1
*        SUBROUTINE TO FORMAT ONE RECORD SEGMENT
*        R5 ADDRESS; R6 LENGTH; R9 RETURN ADDRESS
*
FORMLINE CLC   BLOCKERR,BLOCKERR-1  ERROR ?                      86014
         BE    FORMLINK      NO                                  86014
         CLC   ERRBLOCK,NUMBLOCK  ALREADY COUNTED THIS ONE ?     86014
         BE    FORMLINK      YES; DON'T REPEAT                   86014
         MVC   ERRBLOCK,NUMBLOCK  SAVE LAST COUNTED ERROR BLOCK  86014
         OI    CONDCODE+L'CONDCODE-1,4  SET ERROR                86014
         INC   NUMERR        UP ERROR COUNT                      86014
FORMLINK STM   R5,R9,FORMSAVE   SAVE REGISTERS                   86014
         LA    R7,1
         A     R7,NUMREC     INCREMENT CURRENT RECORD NUMBER
         ST    R7,NUMREC
         C     R7,START      STARTING RECORD REACHED ?
         BL    FORMEXIT      NO; RETURN
         OI    PFLAG,FGFOUND   RECORD FOUND IN USER'S RANGE      81222
         ICM   R9,15,TABLE   ANY SCAN SELECTION ?
         BZ    FORMSELC      NO; DO UNCONDITIONALLY
         LH    R9,TABOFFLO   GET FIRST SCAN OFFSET
         CR    R9,R6         WILL IT FIT ?
         BNL   FORMOMIT      NO; SKIP TESTS AND RECORD
         BCTR  R9,0
TSTNEXT  LA    R9,1(,R9)     GO TO NEXT COLUMN
         LA    R14,0(R5,R9)  GET NEXT COMPARE COLUMN
         LH    R15,TABOFFHI  GET END OF SCAN
         CR    R15,R6        ANY ?
         BL    *+8           YES
         LR    R15,R6
         BCTR  R15,0         ELSE TRUNCATE TO CURRENT LENGTH
         SR    R15,R9        NO. OF COLUMNS TO SCAN-1
         BM    FORMOMIT      DONE WITH THIS CARD
         LA    R0,255        SET COMPARE AND INCREMENT VALUE     82248
         CR    R15,R0        TOO LONG FOR ONE EXECUTE ?          82248
         BNH   TSTNXTEX      NO                                  82248
         TRT   0(256,R14),TRTTAB  TEST ON FIRST CHARACTER        82248
         BNZ   TSTNXTFD      FOUND; CHECK FOR TABLE MATCH        82248
         AR    R9,R0         BUMP BY ONE SEGMENT                 82248
         B     TSTNEXT       AND TRY IT                          82248
TSTNXTEX EX    R15,SCANTRT   LOOK FOR SIGNIFICANT CHARACTER      82248
         BZ    FORMOMIT      NONE; NEXT CARD
TSTNXTFD LR    R9,R1         GET HIT COLUMN                      82248
         SR    R9,R5         GET EQUIVALENT OFFSET
         L     R14,TABLE     GET TABLE START ADDRESS
         USING TABSECT,R14   DECLARE TABLE ENTRY
         SR    R2,R2                                            GP99187
TSTNXTAB CLI   TABLEN,X'FF'  ALL DONE ?
         BE    TSTNEXT       TRY NEXT COLUMN
         IC    R2,TABLEN     GET ENTRY LENGTH - 1
         SR    R15,R15                                          GP99187
         ICM   R15,3,TABOFF  GET START COLUMN
         CR    R9,R15        IN RANGE ?
         BL    TSTNXCOL      NO; SKIP THIS ITEM
         ICM   R15,3,TABOFFE AND END COLUMN
         CR    R9,R15        IN RANGE ?
         BH    TSTNXCOL      NO; SKIP
         EX    R2,SCANCLC    DO CLC
         BE    FORMSELC      MATCH - PUT IT OUT
TSTNXCOL LA    R14,TABTEXT-TABSECT+1(R14,R2)  BUMP TO NEXT TABLE ENTRY
         B     TSTNXTAB
         DROP  R14                                               90277
         SPACE 1
FORMSELC MVC   LINERECL,=X'4020202020202120'                     81222
         CVD   R6,DB
         ED    LINERECL,DB+4   EDIT RECORD LENGTH
         CVD   R7,DB
         MVC   LINEREC#,=X'4020202020202120'
         ED    LINEREC#,DB+4   EDIT RECORD/BLOCK NUMBER
         LR    R8,R6         COPY LENGTH
         LA    R7,0(R5,R6)
         BCTR  R7,0          SET END ADDRESS FOR BXLE
         ICM   R2,15,BLOCKHED  BLOCK MESSAGE PRESENT ?
         BZ    FORMTLEN      NO; TEST LENGTH
         CLI   KEYADE+7,0    ANY KEY LENGTH ?                    81222
         BE    FORMNKEY      NO                                  81222
         MVI   KEYLINE,C' '                                      81222
         MVC   KEYLINE+1(L'KEYLINE-1),KEYLINE  CLEAR IT          81222
         MVC   KLKEYD,=C'KEYLEN='                                81222
         MVC   KLKEYH,=C'KEY='                                   81222
         LM    R14,R15,KEYADE                                    81222
         CVD   R15,DB        MAKE LENGTH DECIMAL                 81222
         MVC   WORK(6),=X'402021204040'  MAKE EDIT MASK + PADDING
         ED    WORK(4),DB+6  EDIT KEY LENGTH                     81222
         LA    R1,WORK+1     POINT TO FIRST BYTE                 81222
         LA    R0,3          MAX OF THREE                        81222
FORMKEYB CLI   0(R1),C' '    LEADING BLANK ?                     81222
         BNE   FORMKEYM      NO; MOVE                            81222
         LA    R1,1(,R1)                                         81222
         BCT   R0,FORMKEYB                                       81222
FORMKEYM MVC   KLKEYL,0(R1)  MOVE TO LINE                        81222
         LA    R0,L'KLKEYT   GET MAX EBCDIC TEXT LENGTH          81222
         CR    R15,R0        LARGER ?                            81222
         BNH   *+6           NO                                  81222
         LR    R15,R0        TRUNCATE IT                         81222
         BCTR  R15,0         LESS ONE FOR EXECUTE                81222
         L     R1,=A(TRANEBCD)                                   89365
         EX    R15,TESTEBCD  SEE IF EBCDIC                       81222
         BNZ   FORMHKEY      NO; FORMAT IN HEX                   81222
         EX    R15,TESTKEY   MOVE KEY TEXT                       81222
         MVI   KLKEYT-1,C''''  PUT FRAMING QUOTE IN              81222
         LA    R1,KLKEYT+2(R15)  ADDRESS OF TRAILING QUOTE       81222
         MVI   0(R1),C''''   ADD TRAILING FRAME                  81222
         B     FORMNKEY      GO TO PRINT BLOCK AND KEY           81222
TESTEBCD TRT   0(0,R14),0(R1)  SEE IF KEY IS EBCDIC              89365
TESTKEY  MVC   KLKEYT(0),0(R14)   MOVE KEY TEXT IN EBCDIC        81222
         SPACE 1                                                 81222
FORMHKEY LA    R2,1(,R15)    GET LENGTH BACK                     81222
         LA    R0,(L'KLKEYT+2+1)/9*4  GET NUMBER OF FIELDS*4     81222
         CR    R2,R0         WILL IT FIT ?                       81222
         BNH   *+6           YES                                 81222
         LR    R2,R0         ELSE TRUNCATE                       81222
         LR    R3,R14        SET TO START OF KEY                 81222
         LA    R1,KLKEYT-1   POINT TO OUTPUT LINE                81222
         LA    R0,4          SET CONSTANT                        81222
         LA    R15,8         SET ANOTHER CONSTANT                81222
FORMKEXW LA    R14,8*16+4    SET INPUT/OUTPUT LENGTH             81222
         CR    R2,R0         LESS THAN ONE WORD ?                81222
         BNL   FORMKEX4      NO                                  81222
         LR    R14,R2        GET LENGTH                          81222
         SLL   R14,5         GET LENGTH*2 IN HIGH NYBBLE         81222
         OR    R14,R2        GET UNPACK MASK                     81222
FORMKEX4 EX    R14,EXUNPK    UNPACK IT                           81222
         SRL   R14,4         ISOLATE THE OUTPUT LENGTH           81222
         BCTR  R14,0         LESS ONE                            81222
         EX    R14,EXTRHEX   TRANSLATE TO PRINTABLE EBCDIC       81222
         LA    R14,1(R14,R1) GET LAST BYTE+1                     81222
         MVI   0(R14),C' '   PUT TRAILING BLANK IN               81222
         LA    R1,1(R15,R1)  SET NEXT OUTPUT ADDRESS             81222
         AR    R3,R0         SET NEXT INPUT ADDRESS              81222
         SR    R2,R0         GET RESIDUAL LENGTH                 81222
         BP    FORMKEXW      DO NEXT WORD                        81222
         L     R2,BLOCKHED   RESTORE MESSAGE ADDRESS             81222
FORMNKEY PRTROOM 3           ROOM FOR THREE LINES ?              81222
         PRTSPACE 1          DOUBLE-SPACE                        81222
         PRTLIST (R2)        PRINT THE BLOCK MESSAGE             81222
         XC    BLOCKHED,BLOCKHED  KILL MESSAGE
         B     FORMLOOF      SINGLE SPACE RECORD                 81222
FORMTLEN TM    PFLAG,FGPRINT  FIRST TIME ?                       81222
         BZ    FORMTLEF      YES; DOUBLE-SPACE                   81222
         TM    PFLAG,FGSPC   FORCED SPACE ?                      81222
         BNZ   FORMTLEF      YES; FORCE SPACE                    81222
         CH    R6,OPTLEN     MORE THAN ONE LINE ?                81222
         BNH   FORMLOOF      NO                                  81222
FORMTLEF PRTROOM 3                                               81222
         MVI   LINECC,C'0'   DOUBLE-SPACE
FORMLOOF OI    PFLAG,FGPRINT   AT LEAST ONE RECORD PRINTED       81222
         NI    PFLAG,255-FGSPC   RESET FORCED SPACE FLAG         81222
         CH    R6,OPTLEN     MULTI-LINE PRINT ?                  81222
         BNH   FORMLOOP      NO                                  81222
         OI    PFLAG,FGSPC   YES; FORCE SPACE NEXT CALL          81222
FORMLOOP LTR   R6,R8         ANY LENGTH AT ALL ?
         BNP   FORMPUMP      NO; PRINT EMPTY LINE
         CLC   LINEHEAD,LINEHEAD+1  EMPTY LINE ?
         BNE   FORMNOFF      NO
         LR    R0,R5         GET CURRENT START ADDRESS
         S     R0,FORMSAVE     LESS STARTING ADDRESS
         ST    R0,OFFSET     SAVE LINE OFFSET
         CVD   R0,DB
         MVC   LINEOFFD,=X'4020202020202120'
         ED    LINEOFFD,DB+4
         UNPK  LINEOFFH(5),OFFSET+2(3)
         TR    LINEOFFH(4),TRANNUM   MAKE PRINTABLE
         MVI   LINEOFFH+L'LINEOFFH,C' '
FORMNOFF LH    R2,OPTLEN     GET LOGICAL SIZE OF ONE LINE
         CR    R6,R2         IS IT LESS THAN MAXIMUM ?
         BNL   *+6           YES
         LR    R2,R6         ELSE COPY BYTES TO DO IN THIS LINE
         LR    R6,R2         SET LENGTH PROCESSED
         TM    FLAGS,FGVERT  CHECK FORMATTING OPTION
         BO    FORMVERT      VERTICAL FORMAT
         BZ    FORMTEXT      PLAIN TEXT
         TM    FLAGS,FGDUAL  TEXT + HEX ?
         BZ    FORMTHEX      NO; JUST HEX
         SPACE 1
FORMDUAL LA    R3,LINETEXT+L'LINETEXT-2-((L'LINETEXT-2)/3)  TEXT
         MVI   0(R3),C'*'    FRAME
         LA    R3,1(,R3)
         MVI   LINETEXT+L'LINETEXT-1,C'*'
         LR    R15,R2
         BCTR  R15,0         EXECUTE LENGTH
         EX    R15,EXMVC35   MOVE TEXT TO END OF LINE
         LA    R1,LINETEXT-6  FIRST WORD
         TM    FLAGC,FGASCIT  PRINT-ONLY TRANSLATE ?             89171
         BZ    FORMDUHX      NO                                  89171
         EX    R15,EXTRANA   TRANSLATE PRINT ONLY                89171
         LR    R3,R5         SET HEX FROM ADDRESS                89171
         B     FORMDUHX      GO TO COMMON HEX FORMATTING
         SPACE 1
FORMTHEX TM    FLAGS,FGALT   ALTERNATE (HORIZ. TEXT/HEX?)       GP99187
         BZ    FORMOHEX      NO; HEX ONLY                       GP99187
         PRTROOM 3           NEED THREE CONTIGUOUS LINES        GP99187
         MVI   LINECC,C'0'   DOUBLE-SPACE FIRST                 GP99187
         LR    R15,R6        COPY LENGTH                        GP99187
         MVI   LINETEXT-8,C' '  DIAPER DUTY                     GP99187
         MVC   LINETEXT-7(L'LINETEXT+7),LINETEXT-8  CLEAR       GP99187
         LA    R3,LINETEXT-7  POINT TO OUTPUT                   GP99187
         LR    R1,R5         COPY INPUT ADDRESS                 GP99187
FORMYWRD LA    R0,4          SET BYTE/WORD COUNTER              GP99187
FORMYLUP MVC   0(1,R3),0(R1)  MOVE ONE CHARACTER                GP99187
         LA    R3,2(,R3)     DOUBLE-SPACE OUTPUT                GP99187
         LA    R1,1(,R1)     NEXT INPUT                         GP99187
         BCT   R15,FORMTWRD  TEST IF WORD BOUNDARY              GP99187
         BAS   R9,PRINT      PRINT AND RESET THIS LINE          GP99187
         B     FORMOHEX                                         GP99187
FORMTWRD BCT   R0,FORMYLUP   DO NEXT BYTE IN THIS WORD          GP99187
         LA    R3,1(,R3)     ELSE INSERT INTER-WORD SPACING     GP99187
         B     FORMYWRD      AND RESET                          GP99187
         SPACE 1                                                GP99187
FORMOHEX LR    R15,R6        COPY LENGTH                        GP99187
         BCTR  R15,0         LESS ONE FOR MOVE
         LA    R3,WORKLINE   SET TO START OF COPIED LINE
         EX    R15,EXMVC35   COPY TEXT TO WORK AREA
         LA    R1,LINETEXT-7  POINT TO TEXT
FORMDUHX LA    R0,4          SET CONSTANT
         LA    R15,8         SET ANOTHER CONSTANT
FORMHEXW LA    R14,8*16+4    SET INPUT/OUTPUT LENGTH
         CR    R2,R0         LESS THAN ONE WORD ?
         BNL   FORMHEX4      NO
         LR    R14,R2        GET LENGTH
         SLL   R14,5         GET LENGTH*2 IN HIGH NYBBLE
         OR    R14,R2        GET UNPACK MASK
FORMHEX4 EX    R14,EXUNPK    UNPACK IT
         SRL   R14,4         ISOLATE THE OUTPUT LENGTH
         BCTR  R14,0         LESS ONE
         EX    R14,EXTRHEX   TRANSLATE TO PRINTABLE EBCDIC
         LA    R14,1(R14,R1) GET LAST BYTE+1
         MVI   0(R14),C' '   PUT TRAILING BLANK IN
         LA    R1,1(R15,R1)  SET NEXT OUTPUT ADDRESS
         AR    R3,R0         SET NEXT INPUT ADDRESS
         SR    R2,R0         GET RESIDUAL LENGTH
         BP    FORMHEXW      DO NEXT WORD
         B     FORMPUMP      PRINT AND GET NEXT
         SPACE 1
EXTRHEX  TR    0(0,R1),TRANNUM   TRANSLATE TO PRINTABLE HEX
EXUNPK   UNPK  0(0,R1),0(0,R3)  UNPACK ONE WORD
EXMVC35  MVC   0(0,R3),0(R5)   MOVE DATA TO PRINT LINE
EXTRANZ  TR    0(0,R3),TRANZONE  TRANSLATE ZONES
EXTRANN  TR    0(0,R3),TRANNUM   TRANSLATE NUMERICS
EXTRANA  TR    0(0,R3),TRANSTAB  MAKE ASCII READABLE             89171
         SPACE 1
FORMVERT PRTROOM 4           SET FOR FOUR LINES
         MVI   LINECC,C'0'   DOUBLE-SPACE FIRST LINE OF THREE
FORMTEXT BCTR  R2,0          SET FOR EXECUTE
         LA    R3,LINETEXT   SET OUTPUT ADDRESS
         EX    R2,EXMVC35    MOVE TEXT TO PRINT LINE
         TM    FLAGC,FGASCIT  PRINT TRANSL. ONLY ?               89171
         BZ    *+8           NO                                  89171
         EX    R2,EXTRANA    YES; MAKE ASCII PRINTABLE           89171
         TM    FLAGS,FGVERT  VERTICAL FORMAT ?
         BNO   FORMPUMP      NO; PRINT AND INCREMENT
         BAS   R9,PRINT      PRINT THE LINE                     GP99187
         EX    R2,EXMVC35    MOVE THE DATA AGAIN
         EX    R2,EXTRANZ    TRANSLATE ZONES
         BAS   R9,PRINT      PRINT THE ZONES                    GP99187
         EX    R2,EXMVC35    MOVE THE DATA AGAIN
         EX    R2,EXTRANN    TRANSLATE NUMERICS
*        B     FORMPUMP      PRINT AND BUMP
         SPACE 1
FORMPUMP BAS   R9,PRINT      PRINT THE LINE                     GP99187
FORMBUMP SR    R8,R6         GET RESIDUAL LENGTH
         BXLE  R5,R6,FORMLOOP  DO ANOTHER LINE
FORMOMIT L     R1,LIMIT      GET SCAN LIMIT
         BCT   R1,*+8        NOT ZERO
         B     EOJLIM        QUIT
         ST    R1,LIMIT      SET REMAINING LIMIT
         L     R1,INTVAL     GET PRINT INTERVAL
         A     R1,START
         ST    R1,START      SET NEXT RECORD TO BE DONE
FORMEXIT LM    R5,R9,FORMSAVE  RESTORE THE REGISTERS
         BR    R9            AND RETURN
         SPACE 1
PRINT    PRTV  VLINE         PRINT A LINE
CLRLINE  MVI   LINECC,C' '
         MVC   LINE(132),LINECC
         BR    R9            RETURN
         SPACE 1
EOJLIM   PRTLIST MSGLIMIT    SHOW STOP DUE TO LIMIT
         B     EOJCOM
EOJEOF   TM    FLAGS,FGSEG   SEGMENT CONSOLIDATION ?             81222
         BZ    EOJEOFN                                           81222
         TM    VSPREV,1      ANY DATA STACKED ?                  81222
         BZ    EOJEOFN                                           81222
         BAS   R9,SPANFORM   PROCESS THE LAST RECORD             81222
EOJEOFN  PRTLIST MSGEOF      SHOW STOP DUE TO END-FILE           81222
EOJCOM   PRTLIST MSGBDERR    SEE IF ANY ERRORS                   86014
EOJCOMCO LH    R9,CONDCODE   SET RETURN CODE                     86014
         CLOSE (,DISP),MF=(E,OPUT1)   CLOSE SYSUT1               82248
         LM    R1,R2,BLOCKADE   GET GETMAIN PARAMETERS           81222
         LTR   R1,R1                                             81222
         BZ    QUITNFRI      NONE                                81222
         FREEMAIN R,LV=(R2),A=(R1)  FREE THE STORAGE             81222
         XC    BLOCKADE(4*4),BLOCKADE  CLEAR BLOCK AND RECORD AREA
QUITNFRI LA    R1,1                                              81222
         CLM   R1,7,DCBBUFCA-IHADCB+SYSUT1                       81222
         BNL   QUITNPUL                                          81222
         FREEPOOL SYSUT1                                         81222
QUITNPUL CH    R9,=H'4'      SEVERE ERROR ?                      81222
         BH    QUITTERM      YES; GET OUT                        81222
         PRTV  NULLLINE,TITLE=1   RESET TITLE                    81222
         PRTV  NULLLINE,TITLE=3   RESET SUB-TITLES               81222
         PRTV  NULLLINE,TITLE=4                                  81222
QUITNDD  LA    R3,1                                              81222
         AH    R3,CURRDD#    GET CURRENT DD NUMBER               81222
         CH    R3,=H'100'    PAST 99 ?                           81222
         BNL   QUITTERM      YES; QUIT                           81222
         STH   R3,CURRDD#    STASH BACK                          81222
         LA    R14,10                                            81222
         SR    R2,R2                                             81222
         DR    R2,R14        SEPARATE UNITS/TENS                 81222
         LA    R2,C'0'(,R2)  MAKE EBCDIC                         81222
         STC   R2,CURRDDNM+5  MAKE NEW NAME                      81222
         LTR   R3,R3         LESS THAN 10 ?                      81222
         BNP   QUITIOD       YES; LOOK FOR TIOT                  81222
         LA    R3,C'0'(,R3)                                      81222
         STC   R3,CURRDDNM+5                                     81222
         STC   R2,CURRDDNM+6                                     81222
QUITIOD  SERVCALL TIODD,CURRDDNM  SEE IF PRESENT                 81222
         LTR   R0,R0                                             81222
         BZ    QUITNDD       NO; LOOK FOR NEXT                   81222
         B     NEXTDDNM      GET THE JFCB                        81222
QUITTERM SERVTERM ,          CLOSE, FREE AND DELETE EVERYTHING   81222
         DROP  R10                                               81222
         USING SAVE,R13                                          81222
         PGMEXIT RC=(R9)     RETURN WITH CODE                   GP02338
         DROP  R13                                               81222
         USING SAVE,R10                                          81222
         SPACE 2
         USING IHADCB,R4     DECLARE DCB                         81222
INEXIT   LR    R4,R1         PRESERVE DCB ADDRESS                81222
         LR    R9,R14        SAVE RETURN OVER GETMAIN            81222
         TM    RECFM,DCBOPTZ REDUCED ERROR RECOVERY ?            87174
         BZ    *+8           NO; NO CHANGE                       87174
         OI    DCBOPTCD-IHADCB+SYSUT1,DCBOPTZ  ALSO SET IN DCB   81222
         NC    DCBOPTCD,RECFM  LEAVE ONLY OPTCD=Q (ASCII)        81222
         TM    DCBOPTCD,DCBOPTQ   ASCII ?                        81253
         BZ    INEXNOQ       NO                                  89167
         TM    JFCBLTYP,JFCBAL  LABEL=AL ?                       89167
         BNZ   INEXNOQ       YES; AVOID 013 IN IFG0196M          89167
         NI    DCBOPTCD,255-DCBOPTQ  RESET IT                    81253
         OI    FLAGC,FGTRANS   USE FASTER LOCAL CODE             81253
INEXNOQ  NC    DCBKEYLE,LRECL  KEEP KEYLEN FOR DASD ONLY         89167
         LH    R2,BLKSIZE    LOAD SYSTEM OR DEVICE MAXIMUM
         MVC   LRECL,DCBLRECL
         MVC   RECFM,DCBRECFM
         LH    R3,DCBBLKSI   GET DATASET SIZE
         LTR   R3,R3         IS IT VALID ?
         BNZ   *+6           LET'S HOPE SO
         LR    R3,R2         ELSE USE DEVICE MAXIMUM
         TM    FLAGC,FGASC9  FIELD-DATA OPTION ?                 81253
         BZ    INEXITN9      NO                                  81253
         LA    R3,7(,R3)     ROUND                               81253
         SRA   R3,3          DIVIDE BY 8                         81253
         MH    R3,=H'9'      MAKE FIELD-DATA LENGTH              81253
INEXITN9 LA    R1,264        SET MINIMUM FOR DIRECTORIES, ETC.   81253
         CR    R3,R1         SUFFICES ?
         BNL   *+6           YES
         LR    R3,R1         IF NOT, USE DIRECTORY SIZE
         LA    R3,8(,R3)     INCREASE FOR LONG BLOCK CHECKING
         CR    R3,R2         EXCEEDS CAPACITY OF DEVICE ?
         BNH   *+6           NO
         LR    R3,R2         ELSE TRUNCATE TO MAXIMUM
         STH   R3,DCBBLKSI   SET OUR SIZE INTO THE DCB
         SR    R0,R0                                             81222
         IC    R0,DCBKEYLE   GET KEY LENGTH                      81222
         AR    R3,R0         ADD IT IN                           81222
         STH   R3,BLKSIZE    SET FOR FORMATTING                  81222
         MVI   DCBRECFM,DCBRECU  SET FOR FULL BLOCK PROCESSING
         TM    DCBMACR1,DCBMRRD+DCBMRECP  BSAM OR EXCP READ ?    89365
         BNZR  R9            YES; RETURN                         81222
         OI    DCBOPTCD,DCBOPTC   REQUEST CHAINED SCHEDULING     81222
         SR    R2,R2         CLEAR RECORD INTERFACE AREA SIZE    81223
         TM    RECFM,DCBRECU-DCBRECV  ANY RECFM ?                81223
         BNZ   INEXSIZE      YES; NOT V                          81223
         TM    RECFM,DCBRECV+DCBRECSB  VARIABLE SPANNED ?        81223
         BNO   INEXSIZE      NO                                  81223
         TM    FLAGS,FGSEG   DEBLOCK SPANNED OPTION ?            81223
         BZ    INEXSIZE      NO                                  81223
         ICM   R2,3,LRECL    GET RECORD AREA SIZE                81223
         BNZ   INEXSIZE      OK IF NOT ZERO                      81223
         LH    R2,DCBBLKSI   ELSE USE BLOCKSIZE AS DEFAULT       81223
INEXSIZE LA    R2,4095(,R2)                                      81223
         N     R2,=X'0007FC00'  ROUND TO 4K MULTIPLE             81223
         AH    R2,=AL2(12*1024)  ALLOW FOR OVERHEAD              81223
         ST    R2,MAINTAB    SET IN GETMAIN LOWER LIMIT          81223
         GETMAIN VC,A=DB,LA=MAINTAB,MF=(E,GETMAIN)  GET SOME STORAGE
         LM    R1,R2,DB      GET SIZE GOTTEN                     81222
         FREEMAIN R,LV=(R2),A=(R1)  FREE IT                      81222
         SR    R6,R6                                             81222
         L     R7,DB+4       GET SIZE                            81222
         S     R7,MAINTAB    ALLOW FOR OVERHEAD                  81223
         AH    R7,=AL2(4*1024)  PLUS A LITTLE MORE               81223
         DR    R6,R3         GET BLOCKS                          81222
         LTR   R7,R7                                             81222
         BP    *+8                                               81222
         LA    R7,1                                              81222
         LA    R1,20         SET REASONABLE MAX ?                81222
         CR    R7,R1         REASONABLE ?                        81222
         BNH   *+6                                               81222
         LR    R7,R1         SET MAX                             81222
         STC   R7,DCBBUFNO                                       81222
         LR    R14,R9        RESTORE RETURN                      81222
         BR    R14
         DROP  R4                                                81222
         SPACE 1
SYNADX   SYNADAF ACSMETH=EXCP,PARM1=READIOB                      89365
         LA    R14,READCOMM                                      89365
         B     SYNADC        GO TO COMMON                        89365
         SPACE 1                                                 89365
SYNAD    TM    SYSUT1+DCBMACF1-IHADCB,DCBMRRD  BSAM ?            81222
         BZ    SYNADQ        NO, QSAM                            81222
         SYNADAF ACSMETH=BSAM   FORMAT ERROR                     81222
         B     SYNADC                                            81222
SYNADQ   SYNADAF ACSMETH=QSAM   FORMAT ERROR                     81222
SYNADC   MVC   BLOCKSYN,84(R1)   MOVE MESSAGE                    81222
         SYNADRLS ,          FREE UP
         BR    R14           RETURN
         SPACE 1                                                 90277
VSSYNAD  MVC   BLOCKSYN(5),=C'SYNAD'                             90277
         B     VSLERSYN                                          90277
VSLERAD  MVC   BLOCKSYN(5),=C'LERAD'                             90277
VSLERSYN MVC   BLOCKSYN+6(12),=C'EXIT ENTERED'                   90277
         BR    R14                                               90277
         SPACE 1                                                 81222
MAKEDD   MVC   SYSUT1(SYSUTLEN),PATUT1   REFRESH DCB             81222
         MVC   DECOMPRS(LENWYLDC),PATDECMP  REFRESH WYLDCOMP AREA
         MVC   SYSUT1+DCBDDNAM-IHADCB(8),CURRDDNM                81222
         LA    R1,SYNAD                                          81222
         STCM  R1,7,DCBSYNAD+1-IHADCB+SYSUT1                     81222
         LA    R1,EOJEOF                                         81222
         STCM  R1,7,DCBEODAD+1-IHADCB+SYSUT1                     81222
         LA    R1,EXITLIST                                       81222
         STCM  R1,7,DCBEXLST+1-IHADCB+SYSUT1                     81222
         MVC   START(12),SAVSTART  RESET COUNTERS                81222
         XC    NUMBLOCK(FLAGS-NUMBLOCK),NUMBLOCK  CLEAR COUNTERS 81222
         MVC   FLAGS(2),SAVFLAGS   RESET FLAGS                  GP99187
         MVI   PFLAG,0       RESET PRINT/SCAN FLAGS              81222
         NI    FLAGC,255-FGTRANS  RESET TRANSLATION              81253
         MVC   MAINTAB(8),TABMAIN  SET SIZE RANGE (AVOID 604 IN SVS)
         BR    R9            RETURN TO CALLER                    81222
         SPACE 2
         LTORG ,
         SPACE 1
OPTFORM1 DC    A(SUB1TOP,SUB1BOT),AL2(L'LINETEXT),C'L '  LINE FORMAT
OPTFORM2 DC    A(SUB2TOP,SUB2BOT),AL2(32),C'D ' DUAL HEX + EBCDIC
OPTFORM3 DC    A(SUB3TOP,SUB3BOT),AL2(48),C'H '  HEX FORMAT
OPTFORM4 DC    A(SUB4TOP,SUB4BOT),AL2(L'LINETEXT),C'V ' VERT. HEX
OPTFORM5 DC    A(SUB1TOP,SUB1BOT),AL2(L'LINETEXT),C'? '  AVAIL  GP99187
OPTFORM6 DC    A(SUB2TOP,SUB2BOT),AL2(32),C'? '          AVAIL  GP99187
OPTFORM7 DC    A(SUB3TOP,SUB3BOT),AL2(48),C'Y '  HOR. HEX + TEXT
OPTFORM8 DC    A(SUB4TOP,SUB4BOT),AL2(L'LINETEXT),C'? '  AVAIL  GP99187
TABMAIN  DC    A(20*1024,512*1024)  GETMAIN SIZES
GETMEAN  GETMAIN VC,LA=TABMAIN,MF=L                              81222
GETMEANL EQU   *-GETMEAN                                         81222
SCANCLC  CLC   0(0,R1),TABTEXT-TABSECT(R14)
SCANTRT  TRT   0(0,R14),TRTTAB  TEST ON FIRST CHARACTER
SCNWKMVC MVC   WORKLINE+1(0),TABTEXT-TABSECT(R4)  MOVE HEX TEXT
SSCOL    DC    C'....:....'
SSNUM    DC    C'1234567890'
SSHEX    DC    C'0 1 2 3  4 5 6 7  8 9 A B  C D E F  '
         SPACE 1
SYSIN    INPWORK SYSIN,WIDTH=80,EODAD=SCANEOJ,FILL=C' '          81222
PATUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL),SYNAD=SYNAD,          X
               EROPT=ACC,EODAD=EOJEOF,EXLST=123                  81222
PATUT1LN EQU   *-PATUT1                                          89365
         READ  PATDECB,SF,1,3,'S',MF=L   BSAM MODE DECB PATTERN  81222
SYSPRINT PRTWORK SYSPRINT,SYSOUT,TITLE=5,FILL=C'.'   OUTPUT DATASET
PATDECMP SERVCOMP DSECT=NO,PFX=PDC                               81222
         ORG   PDCFG1
         DC    AL1(DCMF1EDT+DCMF1TSO)  LEFT EDIT LINE NUMBERS    81253
         ORG   PDCRECMX
         DC    H'508'        SET FOR MAX NIH SIZE + LINE NUMBERS 81222
         ORG   ,
PATEXCP  DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(E), SYNAD=SYNADX,         *
               EODAD=EOJEOF,EXLST=123,DEVD=TA                    89365
         ORG   PATUT1LN+PATEXCP                                  89365
PATEXCPL EQU   *-PATEXCP     LENGTH                              89365
         SPACE 1                                                 90277
PATACB   ACB   DDNAME=SYSUT1,MACRF=(KEY,SEQ,CNV,DIR,IN),EXLST=EXLSTACB
PATACBLN EQU   *-PATACB      LENGTH TO MOVE                      90277
EXLSTACB EXLST AM=VSAM,EODAD=EOJEOF LERAD=VSLERAD,SYNAD=VSSYNAD  90277
         SPACE 1                                                 81222
NULLLINE VCON  '  '          NULL LINE                           81222
         SPACE 1
TITLE    FDOPT NL,CC=C'#'    AUTOMATIC DATE/TIME/PAGE OPTION     81278
         FDPRT 'PRINTALL LISTING OF',PADR                        81278
         FDCLI JFCBDSNM,X'04',BNE=TITLEDSN   VTOC ?              81222
         FDPRT 'VTOC'                                            81222
         FDGOTO TITLEVOL                                         81222
TITLEDSN FDPRT JFCBDSNM,DEBR                                     81222
         FDCLI JFCBELNM,C' ',BE=TITLEVOL,BL=TITLEVOL
         FDPRT '('           SHOW MEMBER NAME ALSO
         FDPRT JFCBELNM,DEBR
         FDPRT ')'
TITLEVOL FDPRT 'ON',PAD
         FDPRT JFCBVOLS,6
         FDPRT *END
         SPACE 1                                                 81222
BLOCKFD  FDPRT 'BLOCK',PADR,NL                                   81222
         FDPRT NUMBLOCK,I,RADJ,LEN=7                             81222
         FDPRT ',',PADR                                          81222
         FDPRT CURLEN,I,RADJ,PAD,LEN=5                           81253
         FDPRT 'BYTES  '                                         81222
         FDPRT BLOCKMTX,DEB                                      81222
         FDCLI KEYADE+7,0,BE=BLOCKFDX  SKIP KEY IF NONE          81222
         FDPRT KEYLINE,PAD,DEB   ELSE PRINT KEY                  81222
BLOCKFDX FDPRT *END                                              81222
         SPACE 1
SUB4TOP  EQU   *
SUB1TOP  EQU   *
SUBTPFX  FDPRT OPTMODE,RADJ,DEB,NL,LEN=7
         FDPRT OPTMODE,RADJ,DEB,PADL,LEN=6
         FDPRT '   OFFSET'
SUBTPFXX FDOPT SBA=(*,+33)
         FDPRT SSCOL         PRINT TOP COLUMN LINE
         FDPRT '1'
         FDPRT SSCOL
         FDPRT '2'
         FDPRT SSCOL
         FDPRT '3'
         FDPRT SSCOL
         FDPRT '4'
         FDPRT SSCOL
         FDPRT '5'
         FDPRT SSCOL
         FDPRT '6'
         FDPRT SSCOL
         FDPRT '7'
         FDPRT SSCOL
         FDPRT '8'
         FDPRT SSCOL
         FDPRT '9'
         FDPRT SSCOL
         FDPRT '1'
         FDPRT *END
         SPACE 1
SUB4BOT  EQU   *
SUB1BOT  EQU   *
SUBBPFX  FDPRT 'NUMBER',RADJ,NL,PADR,LEN=7
         FDPRT 'LENGTH',LEN=6
         FDPRT ' DEC   HEX'
SUBBPFXX FDOPT SBA=(*,+33)
         FDPRT SSNUM
         FDPRT SSNUM
         FDPRT SSNUM
         FDPRT SSNUM
         FDPRT SSNUM
         FDPRT SSNUM
         FDPRT SSNUM
         FDPRT SSNUM
         FDPRT SSNUM
         FDPRT SSNUM
         FDPRT *END
         SPACE 1
SUB2TOP  FDEXEC SUBTPFX,SUBTPFXX
         FDOPT SBA=(*,+27)
         FDPRT '0',LEN=36
         FDPRT '1',LEN=36
         FDPRT '0               1',PADL
         FDPRT *END
         SPACE 1
SUB2BOT  FDEXEC SUBBPFX,SUBBPFXX
         FDOPT SBA=(*,+27)
         FDPRT SSHEX
         FDPRT SSHEX
         FDPRT TRANNUM,32,PADL
         FDPRT *END
         SPACE 1
SUB3TOP  FDEXEC SUBTPFX,SUBTPFXX
         FDOPT SBA=(*,+26)
         FDPRT '0',LEN=36
         FDPRT '1',LEN=36
         FDPRT '2'
         FDPRT *END
         SPACE 1
SUB3BOT  FDEXEC SUBBPFX,SUBBPFXX
         FDOPT SBA=(*,+26)
         FDPRT SSHEX
         FDPRT SSHEX
         FDPRT SSHEX,35
         FDPRT *END
         SPACE 1
OPTMSG   FDPRT ' ',NL,LEN=18
         FDPRT 'ACCEPTED OPTIONS ARE :',PADR
         FDPRT OPTFORM
         FDPRT 'FORMAT,',PAD
         FDTM  FLAGS,FGSEG,BZ=OPTMNSP                            81222
         FDPRT 'SPANNED',PAD                                     81222
OPTMNSP  FDPRT OPTMODE,PAD,DEB                                   81222
         FDPRT 'MODE,',PAD
         FDPRT 'START',PAD
         FDPRT OPTMODE,PAD,DEB
         FDPRT START,I
         FDPRT ', INTERVAL',PADR
         FDPRT INTVAL,I
         FDPRT ', LIMIT',PADR
         FDPRT LIMIT,I
         FDCLI FLAGC,0,BE=OPTMSGEX  ANY TRANSLATION/CONVERSION ? 81253
         FDTM  FLAGC,FGASC9,BZ=OPTMSGC9                          81253
         FDPRT ' ',NL,LEN=20                                     81253
         FDPRT 'DATA WILL BE CONVERTED FROM UNIVAC FIELD-DATA FORMAT'
OPTMSGC9 FDTM  FLAGC,FGTRANS+FGASCII,BZ=OPTMSGEX                 81253
         FDPRT ' ',NL,LEN=20                                     81253
         FDPRT 'DATA WILL BE TRANSLATED FROM ASCII TO EBCDIC'    81253
OPTMSGEX FDPRT *END                                              81253
         SPACE 1
MSGCARD  FDOPT NL,SBA=(*,-80)
         FDPRT 0(R5),80      PRINT SCAN CARD IMAGE
         FDPRT *END
         SPACE 1
MSGLIMIT FDEXEC MSGEOF,MSGEOFX  DOUBLE-SPACE
         FDPRT '*** LIMIT COUNT REACHED AT'
         FDGOTO MSGEOFC      GO TO COMMON
MSGEOF   FDPRT ' ',NL
MSGEOFX  FDPRT ' ',NL
         FDPRT '*** END-FILE AFTER'
MSGEOFC  FDPRT 'BLOCK',PAD
         FDPRT NUMBLOCK,I,PAD
         FDPRT '***',PADL
         FDTM  PFLAG,FGFOUND+FGPRINT,BO=MSGEOFEX                 81222
         FDPRT 'NO',RADJ,LEN=10
         FDPRT OPTMODE,PADL,DEB
         FDPRT 'S',PADR
         FDTM  PFLAG,FGPRINT,BZ=MSGEOFMT                         81222
         FDPRT 'IN RANGE'
         FDGOTO MSGEOFEX
MSGEOFMT FDPRT 'MATCHING SCAN REQUEST'
MSGEOFEX FDPRT *END
         SPACE 1
MSGNOUT1 FDPRT CURRDDNM,DEBR,PADR,NL                             81222
         FDPRT 'DD CARD OMITTED'                                 81222
         FDPRT *END                                              81222
         SPACE 1                                                 86014
MSGBDERR FDCLC NUMERR,ZEROES,4,BE=MSGBDERX                       86014
         FDOPT NL,CC=C'0'    DOUBLE SPACE                        86014
         FDREPT 10,C'*'      FRAME                               86014
         FDPRT NUMERR,I,DEB,PAD                                  86014
         FDPRT 'BLOCKS WITH LOGICAL OR PHYSICAL ERRORS',PAD      86014
         FDREPT 10,C'*'                                          86014
MSGBDERX FDPRT *END                                              86014
         SPACE 1
MSGDFPRM VCON  'PARM DATA OMITTED; DEFAULTS WILL BE USED'
         SPACE 1                                                 83277
MSGBDDVC FDPRT 'DD CARD',NL,RADJ,LEN=12                          83277
         FDPRT CURRDDNM,PAD,DEB                                  83277
         FDPRT 'SPECIFIES AN INVALID INPUT DEVICE'               83277
         FDPRT *END                                              83277
         SPACE 1
MSGBDOPN FDPRT 'OPEN FAILED FOR',PADR,NL,RADJ,LEN=20             81222
         FDPRT CURRDDNM                                          81222
         FDPRT *END                                              81222
         SPACE 1
MSGBDCRD VCON  '&&CARD IN ERROR'
MSGBDSCN VCON  '-RUN TERMINATED DUE TO ABOVE ERRORS'
MSG2MANY VCON  '0 TOO MANY REQUESTS - RERUN IN LARGER REGION'
MSGHDSCN VCON  '0                  SCAN OPTIONS :'
         SPACE 1
TRANTOHX DC    X'000A0B0C0D0E0F00000000000000000000010203040506070809'
TRANZONE DC    16C'0',16C'1',16C'2',16C'3'   VERTICAL FORMAT
         DC    16C'4',16C'5',16C'6',16C'7'   ZONE TRANSLATION TABLE
         DC    16C'8',16C'9',16C'A',16C'B'
         DC    16C'C',16C'D',16C'E',16C'F'
TRANNUM  DC    16C'0123456789ABCDEF'   VERTICAL FORMAT - NUMERICS
         SPACE 1                                                 81222
TRANEBCD DC    256X'01'      TRT FOR PRINTABLE CHARACTERS        81222
         ORG   TRANEBCD+C' '                                     81222
         DC    X'00'         BLANK                               81222
         ORG   TRANEBCD+C'A'                                     81222
         DC    9X'00'        A-I                                 81222
         ORG   TRANEBCD+C'J'                                     81222
         DC    9X'00'        J-R                                 81222
         ORG   TRANEBCD+C'S'                                     81222
         DC    8X'00'        S-Z                                 81222
         ORG   TRANEBCD+C'0'                                     81222
         DC    10X'00'       0-9                                 81222
         ORG   TRANEBCD+C'.'                                     81222
         DC    6X'00'        . < ( + | &                         81222
         ORG   TRANEBCD+C'$'                                     81222
         DC    7X'00'        $ * ) ;  - /                       81222
         ORG   TRANEBCD+C','                                     81222
         DC    5X'00'        , % _ > ?                           81222
         ORG   TRANEBCD+C':'                                     81222
         DC    6X'00'        : # @ ' = "                         81222
         ORG   ,                                                 81222
         SPACE 1                                                 81253
*        TRANSLATE TABLE LIFTED FROM SVC 103 (PTF 77533 LEVEL)
TRASTOEB DC    X'00010203372D2E2F1605250B0C0D0E0F'               81253
         DC    X'101112133C3D322618193F271C1D1E1F'               81253
         DC    X'404F7F7B5B6C507D4D5D5C4E6B604B61'               81253
         DC    X'F0F1F2F3F4F5F6F7F8F97A5E4C7E6E6F'               81253
         DC    X'7CC1C2C3C4C5C6C7C8C9D1D2D3D4D5D6'               81253
         DC    X'D7D8D9E2E3E4E5E6E7E8E9ADE0BD5F6D'               81253
         DC    X'79818283848586878889919293949596'               81253
         DC    X'979899A2A3A4A5A6A7A8A9C06AD0A107'               81253
*        DC    X'3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F'
*        DC    X'3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F'
*        DC    X'3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F'
*        DC    X'3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F'
*        DC    X'3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F'
*        DC    X'3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F'
*        DC    X'3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F'
*        DC    X'3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F3F'
         SPACE 1                                                 89171
*        TRANSLATE TABLES LIFTED FROM SVC 103 (PTF 77533 LEVEL)  89171
TREBTOAS DC    X'000102031A091A7F1A1A1A0B0C0D0E0F'               89171
         DC    X'101112131A1A081A18191A1A1C1D1E1F'               89171
         DC    X'1A1A1A1A1A0A171B1A1A1A1A1A050607'               89171
         DC    X'1A1A161A1A1A1A041A1A1A1A14151A1A'               89171
         DC    X'201A1A1A1A1A1A1A1A1A1A2E3C282B21'               89171
         DC    X'261A1A1A1A1A1A1A1A1A1A242A293B5E'               89171
         DC    X'2D2F1A1A1A1A1A1A1A1A7C2C255F3E3F'               89171
         DC    X'1A1A1A1A1A1A1A1A1A603A2340273D22'               89171
         DC    X'1A6162636465666768691A1A1A1A1A1A'               89171
         DC    X'1A6A6B6C6D6E6F7071721A1A1A1A1A1A'               89171
         DC    X'1A7E737475767778797A1A1A1A1A1A1A'               89171
         DC    X'1A1A1A1A1A1A1A1A1A1A1A1A1A1A1A1A'               89171
         DC    X'7B4142434445464748491A1A1A1A1A1A'               89171
         DC    X'7D4A4B4C4D4E4F5051521A1A1A1A1A1A'               89171
         DC    X'5C1A535455565758595A1A1A1A1A1A1A'               89171
         DC    X'303132333435363738391A1A1A1A1A1A'               89171
         TITLE 'P R I N T A L L  ***  PROGRAM INITIALIZATION'   GP99187
*        ONE TIME CALL AS PART OF INITIALIZATION                 90277
*                                                                90277
         USING INITCODE,R8   SET BY CALLER                       90277
INITCODE SERVINIT ,          LOAD THE SERVICE ROUTINE           GP99187
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINTER ROUTINE
         ST    R0,@PRINTER                                      GP99187
         PRTOPEN SYSPRINT,OPT=(ABEND,NOWTO)                     GP99187
         SPACE 1                                                 81222
         LA    R1,SYSUT1                                         81222
         ST    R1,OPUT1      MAKE OPEN/CLOSE LIST                81222
         MVC   OPUT1(1),RDAHED   MAKE INPUT/REREAD              GP99187
         LA    R14,INFMJFCB                                      81222
         LA    R15,INEXIT    DCB EXIT                            81222
         STM   R14,R15,EXITLIST                                  81222
         MVI   EXITLIST,X'07'                                    81222
         MVI   EXITLIST+4,X'85'  EXIT LIST                       81222
         MVC   TABOFFLO,=H'32767'  INITIALIZE SCAN RANGE         81222
         LA    R1,VLINEX-VLINE                                   81222
         STH   R1,VLINE      MAKE VCON LENGTH                    81222
         MVI   BLOCKMTB,C' '                                     81222
         MVC   CURRDDNM,PATUT1+DCBDDNAM-IHADCB  INIT DDNAME      81222
         LA    R1,1                                              81222
         ST    R1,SAVSTART   SET START BLOCK(RECORD) TO 1        81222
         ST    R1,SAVSTART+8  SET INTERVAL                       81222
         STH   R1,CURRDD#    SET CURRENT DD NUMBER               81222
         MVC   GETMAIN,GETMEAN  MOVE GETMAIN PATTERN             81222
         MVI   TRANSTAB+128,X'3F'  MAKE SECOND HALF OF ASCII     81253
         MVC   TRANSTAB+129(127),TRANSTAB+128  INVALID           81253
         L     R1,=A(TRASTOEB)  GET LOW HALF PATTERN             89365
         MVC   TRANSTAB(128),0(R1)  INITIALIZE VALID HALF        89365
         B     NEXTDDNM      COMPLETED INITIALIZATION           GP99187
         SPACE 2                                                GP99187
INITPARM LA    R9,0(,R7)     DON'T TRUST ANYBODY                 81222
         LTR   R9,R9         IS THERE A PARM ?                  GP99187
         BZ    PARMDFLT      NO; SET DEFAULT                    GP99187
         LAT   R9,0(R9),PARMDFLT  GET PARM ADDRESS OR SET MESSAGE
         LH    R4,0(,R9)     GET PARM LENGTH                    GP99187
         LTR   R4,R4         ANY PARM LENGTH ?                  GP99187
         BNP   PARMDFLT      NO; PRINT DEFAULT MESSAGE          GP99187
         LA    R5,1(R4,R9)   POINT TO LAST BYTE OF PARM         GP99187
         LA    R6,1(,R9)     SET TO FIRST BYTE -1               GP99187
         LA    R4,1          SET BX INCREMENT                   GP99187
PARMLOOP BXH   R6,R4,INITPARX  GET NEXT CHARACTER OR OUT        GP99187
PARMLOOT CLI   0(R6),C'0'    NUMERIC FIELD ?                    GP99187
         BL    PARMNNUM      NO                                 GP99187
         CLI   0(R6),C'9'    REALLY ?                           GP99187
         BNH   PARMNUMB      CHECK FURTHER                      GP99187
PARMNNUM LA    R14,PARMCHAR  GET CHARACTER TABLE                 81222
         LA    R15,(PARMCHAT-PARMCHAR)/(PARMCHA2-PARMCHAR) NUMBER
PARMLOOK CLC   0(1,R6),0(R14)   MATCH ?                          81222
         BNE   PARMLUMP      NO; BUMP                            81222
         LA    R15,FLAGS     POINT TO FIRST FLAG BYTE            81253
         C     R14,=A(PARMCHB2)  DOING SECOND BYTE ?             81253
         BL    PARMLSET      NO; SET OPTION                      81253
         LA    R15,FLAGC     POINT TO SECOND BYTE                81253
PARMLSET NC    0(1,R15),2(R14)  RESET FLAGS                      81253
         OC    0(1,R15),1(R14)  OR IN FLAGS FOR CURRENT FUNCTION 81253
         B     PARMLOOP      SKIP TO NEXT ENTRY                  81222
         SPACE 1                                                 81222
PARMCHAR DC    C'L',AL1(0,255-FGFOFF)            EBCDIC LIST    GP99187
PARMCHA2 DC    C'D',AL1(FGDUAL,255-FGFOFF)       HEX+EBCDIC LIST
         DC    C'H',AL1(FGHEX,255-FGFOFF)    HEX ONLY           GP99187
         DC    C'V',AL1(FGVERT,255-FGFOFF)   VERTICAL HEX+EBCDIC
         DC    C'Y',AL1(FGHEX+FGALT,255-FGFOFF)   HORIZ. HEX+EBCDIC
         DC    C'R',AL1(FGREC,255-FGKEY)     DEBLOCK OPTION     GP99187
         DC    C'W',AL1(FGREC,255-FGKEY)     DEBLOCK (WYLBUR)   GP99187
         DC    C'K',AL1(FGKEY,255-FGREC-FGSEG) KEYS AS DATA; BLOCKS
         DC    C'S',AL1(FGSEG+FGREC,255-FGKEY) STACK SPANNED RECORDS
         DC    C'I',AL1(FGINDW,255)          RDW AS DATA        GP99187
         DC    C'E',AL1(FGEXCP,255)          USE EXCP           GP99187
         DC    C'X',AL1(FGEXCP,255)          USE EXCP           GP99187
PARMCHB2 DC    C'Q',AL1(FGASCII,255)             ASCII TRANSLATE
         DC    C'N',AL1(FGASC9+FGASCII,255)  9-BIT ASCII/FIELDDATA
         DC    C'A',AL1(FGASCII,255)         ASCII PRINT ONLY   GP99187
         DC    C'-',AL1(FGBACK,255)          READ BACKWARDS     GP99187
         DC    C' ',AL1(0,255)               SPACER             GP99187
PARMCHAT EQU   *             END OF PARM LIST                    81222
         SPACE 1                                                 81222
PARMLUMP LA    R14,PARMCHA2-PARMCHAR(,R14)                       81222
         BCT   R15,PARMLOOK  TRY ANOTHER                         81222
         SR    R0,R0                                            GP99187
         CLI   0(R6),C','    NUMBER SEPARATOR ?                 GP99187
         BE    PARMWNUM      YES; INCREMENT NUMERIC             GP99187
PARMERR  OI    PARMFG,FPERR  SET ERROR FLAG                     GP99187
         B     PARMLOOP                                         GP99187
         SPACE 1                                                GP99187
PARMNUMB SR    R0,R0         CLEAR ACCUMULATOR                  GP99187
         LA    R1,C'0'       PRESET FOR NULL                     82031
         LA    R14,7         SET MAXIMUM LENGTH ACCEPTABLE      GP99187
PARMNULP CLI   0(R6),C'0'    NUMERIC ?                          GP99187
         BL    PARMNULR      NO                                 GP99187
         CLI   0(R6),C'9'                                       GP99187
         BH    PARMNULR                                         GP99187
         SLDL  R0,8          SHIFT OVER                         GP99187
         IC    R1,0(,R6)     LOAD NEXT DIGIT                    GP99187
         BXH   R6,R4,PARMNULN  TRY NEXT ONE                     GP99187
         BCT   R14,PARMNULP                                     GP99187
PARMNULR BCTR  R6,0          FAKE FOR AUTO INCREMENT AFTER STORE
PARMNULN STM   R0,R1,WORK    STASH CHARACTER STRING             GP99187
         PACK  DB,WORK(8)    PACK IT                            GP99187
         CVB   R0,DB         GET BINARY VALUE                   GP99187
PARMWNUM TM    PARMFG,FPNUM1  LIMIT SUPPLIED ?                  GP99187
         BNZ   PARMWNM2      YES                                GP99187
         LA    R14,LIMIT     SET FOR LIMIT                      GP99187
         OI    PARMFG,FPNUM1  SET SUPPLIED                      GP99187
         B     PARMWCOM                                         GP99187
PARMWNM2 TM    PARMFG,FPNUM2   START SUPPLIED ?                 GP99187
         BNZ   PARMWNM3      YES                                GP99187
         LA    R14,START     SET START NUMBER                   GP99187
         OI    PARMFG,FPNUM2                                    GP99187
         B     PARMWCOM                                         GP99187
PARMWNM3 TM    PARMFG,FPNUM3  INTERVAL SUPPLIED ?               GP99187
         BNZ   PARMERR       YES; ERROR                         GP99187
         LA    R14,INTVAL    SET INCREMENT                      GP99187
         OI    PARMFG,FPNUM3                                    GP99187
PARMWCOM LTR   R0,R0                                            GP99187
         BNP   PARMLNUM      IGNORE IF ZERO                     GP99187
         ST    R0,0(,R14)    SET VALUE                          GP99187
PARMLNUM BXH   R6,R4,INITPARX  EXIT IF NO MORE                  GP99187
         CLI   0(R6),C','    COMMA AFTER FIELD ?                GP99187
         BNE   PARMLOOT      NO; SEE WHAT IT IS                 GP99187
         B     PARMLOOP      ELSE SKIP IT                       GP99187
         SPACE 1                                                GP99187
PARMDFLT OI    PARMFG,FPDFLT  INDICATE DEFAULT PARM             GP99187
INITPARX B     PARMDONE      RETURN TO CALLER                   GP99187
         TITLE 'P R I N T A L L  ***  SCAN REQUEST PROCESSING'   90277
*        ONE TIME CALL AS PART OF INITIALIZATION                 90277
*                                                                90277
         USING SCANINIT,R8   SET BY CALLER                       90277
SCANINIT OI    PARMFG,FPDONE   SHOW INITIALIZATION DONE         GP99187
         SERVCALL LPALD,=CL8'@INPREAD'  GET THE READ ROUTINE     81222
         ST    R0,@INPREAD   SAVE THE ADDRESS                    81222
         INPOPEN SYSIN,OPT=(DUMMY,NOWTO)  OPEN QUIETLY           81222
         BXH   R15,R15,SCANEOJ   DUMMY OR NOT FOUND              81222
SCANGET  INPGET DEV=1        GET A CARD                          81222
         BXH   R15,R15,SCANEOJ   TERMINATE ON ERROR              81222
         LR    R5,R1         SAVE TEXT POINTER
         ICM   R15,15,TABLE  FIRST TIME ?
         BNZ   SCNNTONE      NO
         PRTV  MSGHDSCN      PRINT SCAN HEADER
         GETMAIN VC,A=TABLE,LA=MAINTAB,MF=(E,GETMAIN)            81223
         BXLE  R15,R15,SCNSTRGE YES; HAVE STORAGE
         PRTV  MSG2MANY      TATTLE
         B     SCANEOJ
SCNSTRGE LM    R3,R4,TABLE   GET START/SIZE
         LH    R2,=AL2(16*1024)   LEAVE ROOM FOR ABENDS
         LA    R1,0(R3,R4)   GET END ADDRESS
         SR    R1,R2         MAKE ADDRESS TO FREE
         FREEMAIN R,LV=(R2),A=(R1)  FREE SOME
         SR    R4,R2         NEW LENGTH
         LR    R14,R3        COPY ADDRESS
         LR    R15,R4        COPY LENGTH
         SR    R0,R0                                            GP99187
         SR    R1,R1                                            GP99187
         MVCL  R14,R0        CLEAR IT
         SH    R4,=H'100'    PREVENT OVERRUN
         AR    R4,R3         MAKE END ADDRESS
         STM   R3,R4,TABLE   SET NEW LIMITS
         ST    R3,TABLECUR   SET CURRENT TABLE POSITION
         MVC   0(5,R3),=5X'FF'  INITIALIZE
         SPACE 1
         USING TABSECT,R4
SCNNTONE PRTLIST MSGCARD     LIST CARD
         LA    R7,71(,R5)    SCAN 72 COLUMNS
         LA    R6,1          SET BXH INCREMENT
         L     R4,TABLECUR   GET NEXT AVAILABLE ENTRY IN TABLE
         BAS   R9,NOBLANK    GET FIRST NON-BLANK                GP99187
         B     SCANGET       JUST PRINT BLANK CARDS
         CLI   0(R5),C'*'    COMMENTS CARD ?
         BE    SCANGET       YES - JUST GET ANOTHER CARD
         NI    PARMFG,255-FSHEX  RESET HEX STRING OPTION
SCNNEXT  CLI   0(R5),C'*'    TRAILING COMMENTS ?
         BE    SCNCDONE      YES - CARD DONE
         CLI   0(R5),C'('    CARD COLUMN SPECIFICATION ?
         BE    SCNCOLS
         CLI   0(R5),C'X'    POSSIBLE HEX REQUEST ?
         BNE   SCNNEXTQ      NO; TRY FOR QUOTE
         OI    PARMFG,FSHEX  SET HEX OPTIONS
         BAS   R9,NXBLANK    SKIP TO NEXT NON-BLANK             GP99187
         B     SCNBCARD      ERROR IF WIDOW
SCNNEXTQ CLI   0(R5),C''''   QUOTED STRING ?
         BE    SCNQUOTE
         CLI   0(R5),C'"'    ALTERNATE QUOTE ?
         BE    SCNQUOTE      YES
SCNBCARD PRTV  MSGBDCRD      BAD CARD
         OI    CONDCODE+1,8  SHOW BAD CARD
         MVC   0(5,R4),=5X'FF'  RESET CURRENT TABLE ENTRY
         B     SCANGET
         SPACE 1
SCNQUOTE MVC   QUOTE,0(R5)   SAVE DELIMITER
         CLI   TABLEN,X'FF'  BEEN HERE BEFORE ?
         BNE   SCNBCARD      YES - FAIL
         LA    R1,TABTEXT    POINT TO OUTPUT FIELD
         LR    R15,R1        SAVE START ADDRESS
SCNQUOTT BXH   R5,R6,SCNBCARD  SKIP A BYTE
         CLC   QUOTE,0(R5)   TRAILING/DOUBLED DELIMITER ?
         BNE   SCNQUOTM      NEITHER - MOVE IT
         BXH   R5,R6,SCNQUOTZ  GET NEXT BYTE
         CLC   QUOTE,0(R5)   DOUBLED DELIMITER ?
         BNE   SCNQUOTZ      ELSE END OF INPUT
SCNQUOTM MVC   0(1,R1),0(R5) MOVE ONE BYTE
         TM    PARMFG,FSHEX  DOING HEX STRING ?
         BZ    SCNQUOT1      NO
         CLI   0(R1),C'A'    VALID ?
         BL    SCNBCARD      NO
         CLI   0(R1),C'F'    IN LOW RANGE ?
         BNH   SCNQUOT1      YES; KEEP IT
         CLI   0(R1),C'0'    NUMERIC ?
         BL    SCNBCARD      NO; ERROR
         CLI   0(R1),C'9'
         BH    SCNBCARD      DITTO
SCNQUOT1 LA    R1,1(,R1)     SET NEXT O/P LOCATION
         B     SCNQUOTT      TRY NEXT BYTE
SCNQUOTZ SR    R1,R15        GET LENGTH OF STRING
         BNP   SCNBCARD      LESS THAN ONE ?
         BCTR  R1,0          - ONE FOR EXECUTE
         STC   R1,TABLEN     SAVE STRING LENGTH
         TM    PARMFG,FSHEX  HEX STRING ?                        89171
         BNZ   SCNSKIPB      YES; PROCESS NEXT FIELD             89171
         TM    FLAGC,FGASCIT ASCII MODE ?                        89171
         BZ    SCNSKIPB      NO                                  89171
         L     R14,=A(TREBTOAS)  TRANSLATE TO ASCII              89171
         EX    R1,EXTRASTA   TRANSLATE STRING TO ASCII           89171
         B     SCNSKIPB      GO TO NEXT FIELD PROCESSOR
EXTRASTA TR    TABTEXT(0),0(R14)  TRANS TO ANS                   89171
         SPACE 1
SCNCOLS  CLI   TABOFF,X'FF'  BEEN HERE BEFORE ?
         BNE   SCNBCARD      YES - TOO BAD
         MVC   TABOFFS,=AL2(0,32767)  SET FIRST/LAST VALID COLUMNS
         BAS   R9,NXBLANK    GET NEXT NON-BLANK CHARACTER       GP99187
         B     SCNBCARD      NONE ?
         BAS   R9,GETNUM     CONVERT TO NUMERIC                 GP99187
         B     SCNBCARD      TOO BAD
         LTR   R0,R0         TEST IT
         BNP   SCNBCARD      ZERO COLUMN ?
         CH    R0,=H'32767'  AFTER LAST ?
         BH    SCNBCARD
         BCTR  R0,0          SUBTRACT ONE
         STCM  R0,3,TABOFF   SET INTO START
         STCM  R0,3,TABOFFE  SET DEFAULT END COLUMN
         CLI   0(R5),C')'    SINGLE COLUMN ?
         BE    SCNSKIPL      YES; SKIP ONE AND TRY AGAIN
         CLI   0(R5),C','    ELSE COMMA TO NEXT ONE ?
         BNE   SCNBCARD      NO; TOO BAD
         BAS   R9,NXBLANK    NEXT CHAR                          GP99187
         B     SCNBCARD      TOO BAD - NONE
         BAS   R9,GETNUM     CONVERT TO NUMERICS                GP99187
         B     SCNBCARD      NONE ?
         LTR   R0,R0         VALID ?
         BNP   SCNBCARD      NO
         CH    R0,=H'32767'  IN RANGE ?
         BH    SCNBCARD      NO
         BCTR  R0,0          MINUS ONE
         STCM  R0,3,TABOFFE  SAVE IT
         CLC   TABOFF,TABOFFE  VALID RANGE ?
         BH    SCNBCARD      NO
         CLI   0(R5),C')'    PROPER TERMINATION ?
         BNE   SCNBCARD      NO; FAIL
SCNSKIPL BXH   R5,R6,SCNCDONE  SKIP A COLUMN
SCNSKIPB BAS   R9,NOBLANK                                       GP99187
         B     SCNCDONE      NO NON-BLANK
         B     SCNNEXT       TRY MORE PARMS
         SPACE 1
NOBLANK  BCTR  R5,0          BACKSPACE ONE
NXBLANK  BXH   R5,R6,0(R9)   RETURN ERROR IF ALL GONE
NOBLANK1 CLI   0(R5),C' '    BLANK ?
         BNE   4(,R9)        RETURN OK
         BXLE  R5,R6,NOBLANK1  ELSE TRY NEXT ONE
         BR    R9            NO MORE
         SPACE 1
GETNUM   BCTR  R5,0          ANY INPUT REMAINING ?
         BXH   R5,R6,0(R9)   TOO BAD
         CLI   0(R5),C'0'    FIRST DIGIT NUMERIC ?
         BLR   R9            TOO BAD, TOO
         CLI   0(R5),C'9'
         BHR   R9
         SR    R14,R14                                          GP99187
         SR    R15,R15       INITIALIZE ACCUMULATORS            GP99187
         LA    R1,8          DO MAX OF EIGHT TIMES
GETNUM1  SLDL  R14,8         MAKE ROOM FOR CURRENT DIGIT
         IC    R15,0(,R5)    TACK THE CURRENT ONE ON
         BXH   R5,R6,0(R9)   ERROR IF NO MORE
         CLI   0(R5),C'0'    NUMERIC ?
         BL    GETNUM2       NO
         CLI   0(R5),C'9'
         BH    GETNUM2
         BCT   R1,GETNUM1    TRY IT
         B     SCNBCARD
GETNUM2  STM   R14,R15,WORK  STASH NUMBERS
         PACK  DB,WORK       MAKE PACKED
         CVB   R0,DB         GET BINARY
         B     4(,R9)
         SPACE 1
SCNCDONE CLI   TABLEN,X'FF'  STRING SUPPLIED ?
         BE    SCNBCARD      NO
         CLI   TABOFF,X'FF'  COLUMNS SUPPLIED ?
         BNE   SCNHVCOL      OK
         MVC   TABOFFS,=AL2(0,32767)  SET DEFAULT COLUMNS
SCNHVCOL SR    R14,R14                                          GP99187
         IC    R14,TABLEN    GET STRING LENGTH - 1
         TM    PARMFG,FSHEX  HEX STRING SUPPLIED ?
         BZ    SCNHVNHX      NO
         EX    R14,SCNWKMVC  MOVE TEXT TO WORK AREA+1
         LA    R2,WORKLINE+1 POINT TO START OF TEXT
         TM    TABLEN,1      ODD LENGTH STRING ?
         BNZ   SCNHXEVN      NO (REMEMBER, L-1 IN TABLEN)
         BCTR  R2,0          BACK-SPACE ONE BYTE
         MVI   0(R2),C'0'    PUT A LEADING ZERO IN
SCNHXEVN LA    R14,1(,R14)   GET NUMBER OF INPUT BYTES
         LA    R15,1(,R14)
         SRL   R15,1         NUMBER OF OUTPUT BYTES
         LR    R14,R15       SAVE LENGTH
         BCTR  R14,0         LENGTH-1
         STC   R14,TABLEN    SET NEW LENGTH
         LA    R1,TABTEXT    GET START OUTPUT ADDRESS
SCNDOHEX NC    0(2,R2),=X'1F1F'  CREAM HIGH BITS
         TR    0(2,R2),TRANTOHX  MAKE NUMERICS ONLY
         PACK  0(2,R1),0(3,R2)  PACK
         LA    R2,2(,R2)     UP INPUT ADDRESS
         LA    R1,1(,R1)     UP OUTPUT
         BCT   R15,SCNDOHEX  REPEAT
SCNHVNHX NI    PARMFG,255-FSHEX  RESET HEX OPTION
         LA    R3,TABTEXT-TABSECT+1(R4,R14)  SET NEW START ADDRESS
         LH    R15,=AL2(32767-2)  GET LAST COLUMN - 2
         SR    R15,R14       COMPUTE LAST VALID COMPARE POSITION
         ICM   R14,3,TABOFFE  GET LAST REQUEST
         CR    R14,R15       WILL IT FIT ?
         BNH   *+8           YES
         STCM  R15,3,TABOFFE   ELSE SET NEW END LOCATION
         CLC   TABOFF,TABOFFE  VALID RANGE ?
         BH    SCNBCARD      NO; FAIL IT
         SR    R14,R14                                          GP99187
         ICM   R14,3,TABOFF
         CH    R14,TABOFFLO
         BNL   *+8
         STH   R14,TABOFFLO  SET LOW START SCAN COLUMN
         ICM   R14,3,TABOFFE   GET END SCAN
         CH    R14,TABOFFHI
         BNH   *+8
         STH   R14,TABOFFHI  SET HIGH END SCAN COLUMN
         SR    R14,R14                                          GP99187
         IC    R14,TABTEXT
         STC   R14,TRTTAB(R14)   SET TRT STOP
         LTR   R14,R14       USER LOOKING FOR HEX ZERO ?         90179
         BNZ   *+8           NO                                  90179
         OI    TRTTAB,X'FF'  TRIGGER TRT ON MATCH OF ZERO        90179
         C     R3,TABLEND    WILL IT FIT ?
         BNH   SCNCFITS      YES
         PRTV  MSG2MANY      NO MORE ROOM
         MVI   CONDCODE+1,12  SET ERROR
         L     R3,TABLE      RESET TO START - DIAGNOSE REMAINING INPUT
         SPACE 1
SCNCFITS ST    R3,TABLECUR   SET NEW TABLE LOCATION
         MVC   0(5,R3),=5X'FF'  INITIALIZE NEW TABLE
         B     SCANGET       GET THE NEXT CARD
         DROP  R4
         SPACE 1
SCANEOJ  CLI   CONDCODE+1,0  TEST FOR ERRORS
         BE    SCNCDSOK      OK
         PRTV  MSGBDSCN      TOO BAD
         B     QUIT8         EXIT UNGRACEFULLY
         SPACE 1
SCNCDSOK SERVCALL LPADL,=CL8'@INPREAD'  CLOSE AND FREE SYSIN     81222
         ICM   R4,15,TABLE+4 GET TABLE END ADDRESS-100
         BZ    SCNDONE       SKIP IF NONE
         LA    R4,100(,R4)   ADD SAFETY BACK IN
         L     R3,TABLE      GET START OF TABLE
         CLI   0(R3),X'FF'   ANY ENTRIES ?
         BNE   SCANFRET      YES; FREE SOME
         SR    R4,R3         MAKE LENGTH
         XC    TABLE(8),TABLE   CLEAR TABLE POINTERS
         B     SCANFREE      FREE ALL OF GOTTEN STORAGE
SCANFRET SRL   R4,3
         SLL   R4,3          ROUND TO DOUBLE WORD
         L     R3,TABLECUR   GET STOP ADDRESS
         LA    R3,TABTEXT-TABLEN+7(,R3)   ALLOW FOR END AND ROUND
         SRL   R3,3
         SLL   R3,3          ROUND TO NEXT HIGHER DOUBLE WORD
         SR    R4,R3         GET LENGTH TO FREE
         BNP   SCNDONE       NONE?  WILL BOMB ON 80A
SCANFREE FREEMAIN R,LV=(R4),A=(R3)  FREE IT UP
         B     SCNDONE       RETURN                              90277
         SPACE 1                                                 90277
         LTORG ,                                                 90277
         SPACE 3
TABSECT  DSECT ,             TABLE ENTRY MAPPING
TABLEN   DC    AL1(0)        LENGTH OF TEXT-1
TABOFFS  DC    0AL4(0)       START/END OFFSET
TABOFF   DC    AL2(0)        START OFFSET
TABOFFE  DC    AL2(0)        END OFFSET
TABTEXT  DC    0C' '         VARIABLE LENGTH TEXT STRING
         SPACE 1                                                 89365
         IEZIOB ,                                                89365
         EJECT ,                                                 81222
SAVE     DSECT ,             DYNAMIC WORK AREA                   81222
DB       DC    D'0'
WORK     DC    CL8' '        MUST FOLLOW DB
         SPACE 1
@SERVICE DC    A(0)          ADDRESS OF SERVICE ROUTINE
@INPREAD DC    A(0)          ADDRESS OF INPUT PROCESSOR          81222
@PRINTER DC    A(0)          ADDRESS OF PRINTER ROUTINE
@TRACE   DC    A(0)          ADDRESS OF DEBUG PGMTRACE ROUTINE
ZEROES   DC    F'0'          CONSTANT 0                          86014
BLOCKADE DC    A(0,0)        ADDRESS/LENGTH OF BSAM BUFFER       81222
RECADE   DC    A(0,0)        ADDRESS OF V(B)S RECORD INTERFACE AREA
KEYADE   DC    A(0,0)        ADDRESS/LENGTH OF KEY               81222
FORMSAVE DC    5A(0)         LINE FORMATTING SAVE AREA
SPANSAVE DC    9A(0)         SPANNING/FORMAT SAVE AREA           81222
BLOCKXLE DC    3A(0)         BXLE POINTERS FOR BLOCK PROCESSING
BLOCKHED DC    A(0)          0 OR ADDRESS OF BLOCK HEADER
START    DC    F'1'          STARTING RECORD TO DUMP
LIMIT    DC    F'0'          NUMBER OF RECORDS TO DO (0=ALL)
INTVAL   DC    F'1'          DUMP EVERY NTH RECORD
NUMBLOCK DC    F'0'          NUMBER OF CURRENT BLOCK
NUMREC   DC    F'0'          NUMBER OF CURRENT RECORD
NUMERR   DC    F'0'          NUMBER OF ERRORS                    86014
ERRBLOCK DC    F'0'          BLOCK # WITH LAST COUNTED ERROR     86014
OFFSET   DC    F'0'          OFFSET IN CURRENT RECORD
BLKSIZE  DC    H'0'          USER'S BLOCKSIZE
LRECL    DC    H'0'          USER'S RECORD LENGTH
CURLEN   DC    F'0'          LENGTH OF CURRENT BLOCK             81253
RECFM    DC    AL1(DCBOPTQ)  USER'S RECORD FORMAT (MASK BEFORE OPEN)
FLAGS    DC    X'00'         PROCESSING FLAGS
FGALT    EQU   X'80'           ALTERNATE FORMATTING             GP99187
FGHEX    EQU   X'40'           DUMP IN HEX ONLY                 GP99187
FGDUAL   EQU   X'20'           PRINT HEX AND TEXT               GP99187
FGLIST   EQU   FGHEX+FGDUAL    BOTH OFF - TEXT ONLY             GP99187
FGVERT   EQU   FGHEX+FGDUAL    BOTH ON - VERTICAL TEXT AND HEX  GP99187
FGFOFF   EQU   FGHEX+FGDUAL+FGALT  ALL FORMATTING BITS          GP99187
FGREC    EQU   X'10'           DEBLOCK INTO RECORDS             GP99187
FGKEY    EQU   X'08'           NO DEBLOCK; TREAT KEY AS DATA    GP99187
FGSEG    EQU   X'04'           VS SPANNED RECORD INTERFACE OPTION
FGINDW   EQU   X'02'           INCLUDE RDW/SDW WITH DATA DISPLAY
FGEXCP   EQU   X'01'           USE EXCP FOR TAPE                GP99187
FLAGC    DC    X'00'         CONVERSION PROCESSING               81253
FGASCIT  EQU   X'80'           ASCII ON TEXT DISPLAY LINE ONLY   89171
FGBACK   EQU   X'40'           READ FILE BACKWARDS              GP95208
FGASC9   EQU   X'04'           9-BIT ASCII INPUT                 81253
FGASCII  EQU   X'02'           ASCII TRANSLATE IN PARM           81253
FGTRANS  EQU   X'01'           OPTCD=Q SPECIFIED                 81253
PFLAG    DC    X'00'         PRINT/SCAN CONTROL FLAG             81222
FGVSAM   EQU   X'80'           PROCESSING A VSAM FILE            90277
FGPDS    EQU   X'40'           PROCESSING A DIRECTORY           GP99187
FGPDSE   EQU   FGPDS+FGVSAM    PROCESSING A PDSE (NOT YET)      GP99187
FGFOUND  EQU   X'08'           RECORD IN START/LIMIT RANGE FOUND 81222
FGPRINT  EQU   X'04'           RECORD PRINTED                    81222
FGSPC    EQU   X'01'           DOUBLE-SPACE NEXT RECORD          81222
PARMFG   DC    X'00'         PARM PROCESSING FLAG
FPDFLT   EQU   X'80'           DEFAULT PARM IN USE
FPERR    EQU   X'40'           ERROR IN PARM FIELD
FPDONE   EQU   X'04'           RECURSION SWITCH                 GP99187
FSHEX    EQU   X'08'           SCAN - HEX STRING REQUEST
FPNUM3   EQU   X'04'           THIRD NUMERIC PARM FOUND
FPNUM2   EQU   X'02'           SECOND NUMERIC PARM FOUND
FPNUM1   EQU   X'01'           FIRST NUMERIC PARM FOUND
OPTSUB1  DC    A(0)          CURRENT SUBTITLE TOP LINE
OPTSUB2  DC    A(0)          CURRENT SUBTITLE BOTTOM LINE
OPTLEN   DC    H'0'          DATA LENGTH OF A TEXT LINE
OPTFORM  DC    C' '          FORMAT CHARACTER
         DC    X'00'         RESERVED
SAVSTART DC    3F'0'         SAVE START/LIMIT/INTERVAL           81222
SAVFLAGS DC    2X'00'        SAVE FLAGS                         GP99187
VSPREV   DC    0X'00'        SDW CONTROL FLAG OF PRIOR SEGMENT   81222
QUOTE    DC    C''''         DELIMITER OF CURRENT STRING
OPTMODE  DC    CL6' BLOCK'   BLOCK OR RECORD MODE
OPUT1    OPEN  (SYSUT1,(INPUT,REREAD)),MF=L
EXITLIST DC    0A(0),X'07',AL3(INFMJFCB),X'85',AL3(INEXIT)
MAINTAB  DC    A(20*1024,512*1024)  GETMAIN SIZES                81223
TABLE    DC    A(0)          START OF SCAN REQUEST TABLE
TABLEND  DC    A(0)          END OF TABLE
TABLECUR DC    A(0)          NEXT AVAILABLE ENTRY ADDRESS
GETMAIN  DC    XL(GETMEANL)'0'  GETMAIN LIST                     81222
TABOFFLO DC    H'32767'      LOW SCAN OFFSET
TABOFFHI DC    H'0'          HIGH SCAN OFFSET
CONDCODE DC    H'0'          RETURN CODE
CURRDD#  DC    H'0'          CURRENT SYSUT NUMBER                81222
CURRDDNM DC    CL8'SYSUT1'   CURRENT DD NAME                     81222
VLINE    VCON  END=VLINEX    PRINT LINE
LINECC   DC    C' '
LINE     DC    0CL132' '
LINEHEAD DC    CL32' '       DATA PREFIX
LINERECL EQU   LINEHEAD+6,8,C'C'
LINEREC# EQU   LINEHEAD,8,C'C'
LINERDW  EQU   LINERECL+8,10,C'C'  (RDW)
LINEOFFD EQU   LINEHEAD+10,8,C'C'
LINEOFFH EQU   LINEOFFD+L'LINEOFFD+2,4,C'C'
LINEWYL# EQU   LINEOFFH-4-1,9,C'C'
LINETEXT DC    CL100' '      TEXT PORTION
         ORG   LINETEXT      RE-USE FOR KEY PROCESSING           81222
KEYLINE  DC    CL132' '                                          81222
         ORG   KEYLINE                                           81222
KLKEYD   DC    C'KEYLEN='                                        81222
KLKEYL   DC    CL3' '        LENGTH OF KEY                       81222
         DC    C' '                                              81222
KLKEYH   DC    C'KEY='                                           81222
         DC    C' '                                              81222
KLKEYT   DC    CL116' '      MAX TEXT LENGTH                     81222
         DC    C' '                                              81222
         ORG   ,                                                 81222
         DC    C' '          FOR OVERRUN OF UNPACK (HEX KEY)     81222
         VCON  ,
         DC    C' '          OVERFLOW FOR UNPACK OF LINETEXT
WORKLINE DC    CL(L'LINETEXT)' '   COPY OF USER'S TEXT
BLOCKMTB DC    C' '          BLOCKMTX-1                          81222
BLOCKMTX DC    CL80' '       EXTRA TEXT
BLOCKMER EQU   BLOCKMTX+13,20,C'C'  ERROR MESSAGE
BLOCKSYN EQU   BLOCKMTX+24,44,C'C'  SYNAD MESSAGE
BLOCKERR EQU   BLOCKMTX+13,55,C'C'  ANY ERROR MESSAGE            86014
         SPACE 1
SYSUT1   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(GL),SYNAD=123,            X
               EROPT=ACC,EODAD=123,EXLST=123                     81222
SYSUTLEN EQU   *-SYSUT1      LENGTH OF DCB FOR MOVE              81222
READIOB  DC    XL(IOBEXTEN-IOBSTDRD)'0'  IOB                     89365
READECB  DC    F'0'          EXCP ECB                            89365
READCCW  CCW   2,1-1,X'20',65535       TAPE CCW                  89365
         ORG   READIOB                                           89365
         READ  DECB,SF,SYSUT1,MF=L   BSAM MODE DECB              81222
LENDECB  EQU   *-DECB        LENGTH FOR MOVE                     81222
         ORG   SYSUT1                                            90277
UT1ACB   ACB   DDNAME=SYSUT1,MACRF=(ADR,SEQ,IN)                  90277
UT1RRN   DC    A(0)          RELATIVE RECORD NO. FEEDBACK        90277
UT1RECAD DC    A(0)          ADDRESS OF CURRENT RECORD           90277
UT1GNCB  DS    XL(UT1GNCBL)  SHOWCB WORK AREA                    90351
UT1MDCB  DS    XL(UT1MDCBL)  MODCB WORK AREA                     90351
UT1SHCB  DS    XL(UT1SHCBL)  SHOWCB WORK AREA                    90351
         ORG   ,                                                 89365
DECOMPRS SERVCOMP DSECT=NO,PFX=DCM
         ORG   DCMFG1
         DC    AL1(DCMF1EDT+DCMF1TSO)  LEFT EDIT LINE NUMBERS    81253
         ORG   DCMRECMX
         DC    H'500'        SET FOR MAX NIH SIZE
         ORG   ,
LENWYLDC EQU   *-DECOMPRS    LENGTH FOR PATTERN MOVE             81222
         SPACE 1
         IEFJFCBN ,          EXPAND JFCB IN-LINE
         SPACE 1
TRANSTAB DC    2XL128'0'     ASCII OR USER TRANSLATE TABLE       81253
         SPACE 1                                                 81253
TRTTAB   DC    16XL16'0'     TRT TABLE FOR SCAN
         SPACE 1                                                 81222
SAVEEND  EQU   *                                                GP02338
         SPACE 1
         DCBD  DSORG=PS      EXPAND DCB MAPPING
         SPACE 1
CVTDSECT DSECT ,
         CVT   DSECT=YES
         SPACE 1
         IHACDE ,
         SPACE 1
         IEFUCBOB ,
         SPACE 1
         IECSDSL1 1
         SPACE 1
FDSECT   FDSECT ,            MAP FD EXPANSIONS
         SPACE 1                                                 90277
         IFGRPL ,                                                90277
         END   ,
