KERMIT   TITLE 'K E R M I T  ***  TSO/VTAM/TCAM KERMIT'       ESP88150
         MACRO ,
&LABEL   BINCVRT &REG,&AREA,&PKVAR                            ESP88150
.*
.*  CONVERT THE CONTENTS OF &REG TO DECIMAL AND EDIT INTO &AREA.
.*  &AREA IS A FIELD OF LENGTH SIX THAT WILL CONTAIN THE INTEGER
.*  STRING WITH LEADING BLANKS SUPRESSED.  &PKVAR IS A DOUBLE ESP88150
.*  WORK SPACE.
.*
&LABEL   CVD   &REG,&PKVAR                                    ESP88150
         MVC   &AREA.(6),=X'402020202120'
         ED    &AREA.(6),&PKVAR+5                             ESP88150
         MEND  ,
         SPACE 1                                              ESP88150
         MACRO ,                                              ESP88150
&NM      CHARB &CH,&B                                         ESP88150
         AIF   ('&CH' EQ 'END').END                           ESP88150
&NM      DC    AL1(&CH,0),AL2(&B-KERMIT)                      ESP88150
         MEXIT ,                                              ESP88150
.END     ANOP  ,                                              ESP88150
&NM      DC    X'FF',AL3(0+&B)                                ESP88150
         MEND  ,                                              ESP88150
         SPACE 1                                              ESP88150
         MACRO ,                                              ESP88150
&NM      NINC  &N                                             ESP88150
&NM      LA    R0,1                                           ESP88150
         A     R0,&N                                          ESP88150
         N     R0,=X'0000003F'                                ESP88150
         ST    R0,&N                                          ESP88150
         MEND  ,                                              ESP88150
         SPACE 1                                              ESP88150
         MACRO ,                                              ESP88150
&NM      INC   &N                                             ESP88150
&NM      LA    R0,1                                           ESP88150
         A     R0,&N                                          ESP88150
         ST    R0,&N                                          ESP88150
         MEND  ,                                              ESP88150
         SPACE 1                                              ESP88150
         MACRO ,                                              ESP88150
&NM      MSG   &T,&L                                          ESP88150
         LCLA  &N                                             ESP88150
&N       SETA  K'&T                                           ESP88150
&NM      LA    R1,=C&T       LOAD THE MESSAGE ADDRESS         ESP88150
         AIF   ('&L' EQ '').DFL                               ESP88150
         LA    R0,0-1+&L     LOAD THE MESSAGE LENGTH          ESP88150
         MEXIT ,                                              ESP88150
.DFL     LA    R0,&N-2-1     LOAD THE MESSAGE LENGTH FOR EX   ESP88150
         MEND  ,                                              ESP88150
         SPACE 2                                              ESP88277
         COPY  OPTIONGB                                       ESP88277
         SPACE 1                                              ESP88277
         SYSPARM LIST=YES                                     ESP88277
         EJECT ,                                              ESP88150
*
*  KERMIT/TSO   -
*
*  Kermit - KL10 Error-free Reciprocal Micro Interface Transfer
*  IBM Version 1.0
*
*  This program is the IBM MVS/TSO side of a file transfer system.
*  It may be used to transfer files between a micro and a system
*  running under MVS/TSO. It MUST be run as a Command Processor.
*  See the KERMIT manual for the complete program specifications
*  to which this program and any other component of the system
*  must adhere.
*
*  Ronald J. Rusnak, University of Chicago Computation Center
*  BITNET address, SYSRONR at UCHIVM1
*  MAILNET address, SYSTEMS.RON@UCHICAGO.MAILNET
*  ARPA forwarding address, SYSTEMS.RON%UCHICAGO@MIT-MULTICS.ARPA
*  May 1984
*
*  Developed by the modification of the IBM CMS version written by
*  Daphne Tzoar, Columbia University Center for Computing Activities
*  March 1982
*
* Copyright (C) 1984 University of Chicago
*
* Permission is granted to any individual or institution to copy
* or use this program, except for explicitly commercial purposes.
*
*        MODIFICATIONS, ENHANCEMENTS AND ERROR CORRECTIONS FLAGGED ESP
*        OR DATED AS YYDDD BY G. POSTPISCHIL, EXPERT SYSTEM PROGRAMMING
*          PDS SUPPORT, GDG SUPPORT, SERVER CODE, VTAM, ETC.  ESP88150
*              NOTE THAT WE RUN VTAM AND TCAM WITH EVEN PARITY AND
*                X-ON FOR END-OF-LINE (X-ON IS PRECEDED BY PC'S EOL).
*        MORE CHANGES AS OF 11/23/89 :                           89327
*              MODULE NOW COMPLETELY RE-ENTRANT AND REFRESHABLE. 89327
*              ENTRY POINT KERMNET ADDED; USER IS PASSED A WORK AREA
*              MAPPED BY LOCAL MACRO MAPMTS - USED FOR VTAM AND WYLBUR
*              VERSION (MULTIPLE USERS IN ONE ADDRESS SPACE).    89327
*              I'M LAZY, SO SOME CODE CONVERTED TO USE LOCAL MACROS.
*        ENHANCEMENTS AS OF MARCH 1991:                          91084
*              SUPPORT FOR LONG PACKET PROTOCOL (ABOUT 30-50% FASTER)
*              ADDED 'SET UPDATE' MODE - FORCES NEW DCB OPTIONS ON
*                REPLACED SEQUENTIAL DATASET.                    91084
*              ADDED SCRATCH/DELETE COMMAND                      91084
*         ----------------------------------------
*   Correct assembly requires Dave Cole's macro library (CBT tape)
*   and MVS/SP or later S99FAIL
*         ----------------------------------------
*
* Note that this is an experimental version; all changes should
* be forwarded to the author.
*
         SPACE 1                                              ESP88150
         GBLB  &ZZ@BLUK      LOCAL GOODIE                     ESP88150
         GBLB  &INPREAD,&PRINTER                                 91315
         GBLC  &OPPAKSZ,&OPPAKLG   PACKET SIZE / LONG PACKET     91077
         GBLC  &OPBLKSZ,&OPLRECL,&OPTRACK,&MXLRECL            ESP88203
         GBLC  &OPPAKMN                                          91311
&OPBLKSZ SETC  '3860'        3380 BLOCKSIZE DEFAULT           ESP88150
&OPLRECL SETC  '255'         DEFAULT DATA LRECL               ESP88150
&MXLRECL SETC  '32752'       MAXIMUM SUPPORTED LRECL          ESP88203
&OPTRACK SETC  '15'          DEFAULT TRACKS                   ESP88150
&OPPAKMN SETC  '20'          REDUCED FROM 26 TO MATCH PROCOMM    91311
&OPPAKSZ SETC  '94'          MAXIMUM PACKET (26 <= SIZE <= 94) SP88150
&OPPAKLG SETC  '2048'        MAX LONG PACKET (LIMITED BY TP FRONTEND)
** SUPPORT FOR THE 'TAKE CMDFILE' COMMAND REQUIRES ESP SUPPORT CODE:
** @SERVICE, @INPREAD, @DCBEXIT AND LOTS OF MACROS.              91315
** THE SCRATCH/DELETE COMMAND REQUIRES MODULE DELETER(ALIAS DELETNP),
** AND THE @PRINTER SUPPORT MODULE.                              91315
&INPREAD SETB  1 SET 1 IF @INPREAD + OTHER GOODIES AVAILABLE     91315
&PRINTER SETB  ('&LOCAL' EQ 'PID')                               92320
         EJECT
         PRINT &PRTSOR                                           89327
KERMIT   SAVEM ZERO15,BASE=R12,PARM=R9,BNDRY=PAGE,                     *
               ENTRY=KERMNET,ENTNO=MODEFG                        89327
* REGISTER USAGE -
* R10 - BASE FOR GOTTEN STORAGE
* R11 - BASE REGISTER FOR GLOBAL READ-ONLY DATA
* R12 - PROGRAM BASE (LOCAL IN EACH ROUTINE)
* R13 - SAVE AREA
* R14 - SUBROUTINE LINKAGE
* R15 - SUBROUTINE LINKAGE
*
         SPACE
         PRINT     NOGEN
         IKJCPPL
         IKJPSCB ,                                            ESP84246
         IKJUPT
         DCBD  DSORG=(PS),DEVD=DA                             ESP88150
         SPACE
AA       EQU   65            FOR ALPHA RANGE CHECK            ESP88150
AB       EQU       66                  BREAK PACKET
AC       EQU   67            SERVER MODE - HOST COMMAND       ESP88150
AD       EQU       68                  DATA PACKET (ASCII 'D')
AE       EQU       69                  ERROR PACKET
AF       EQU       70                  FILE PACKET
AG       EQU   71            SERVER MODE - SERVER COMMAND     ESP88150
AI       EQU   73            SERVER INIT PACKET (PC GET)      ESP88150
AK       EQU   75            SERVER - KERMIT COMMAND          ESP88150
AL       EQU   76            SERVER - BYE                     ESP88150
AN       EQU       78                  NAK
AR       EQU   82            SERVER FILE RECEIVE              ESP88150
AS       EQU       83                  INIT PACKET
AT       EQU   84            SERVER - TYPE                    ESP88150
AX       EQU   88            CANCEL FILE RESPONSE             ESP88150
AY       EQU       89                  ACK
AZ       EQU       90                  EOF PACKET
A0       EQU   48            ASCII DIGIT 0                    ESP88150
A1       EQU   49            ASCII DIGIT 1                    ESP88150
A2       EQU   50             DEFINE A 1 AND A 2              ESP88150
A3       EQU   51                                             ESP88150
A9       EQU   57            ASCII DIGIT 9                    ESP88150
ASP      EQU   32            ASCII SPACE                      ESP88150
AQUO     EQU   34            ASCII QUOTE                         91290
         SPACE 1
FLG1     EQU       X'80'               IS FILE THE FIRST OR NOT
FLG2     EQU       X'40'               OVERWRITE SENT FILENAME?
FGBYE    EQU   X'20'         SERVER RECEIVED A BYE-BYE (LOGOFF) P88150
FGNAK    EQU   X'08'         NAK FROM MICRO(0) OR RPACK(1)?   ESP88150
FGSERVER EQU   X'04'         WE ARE IN SERVER MODE            ESP88150
FGHAZED  EQU   X'02'         INITIALIZATION COMPLETE          ESP88150
         EJECT
**********************************************************************
*                                                                    *
*        KERMIT-TSO PROGRAM                                          *
*                                                                    *
**********************************************************************
         PRINT &PRTSOR                                           89327
KERMIT   CSECT ,             RESUME MAIN PROGRAM                 89327
         LA    R10,DYNWORK   POINT TO SHARED WORK AREA           89327
         DROP  R13                                               89327
         USING DYNWORK,R10   DECLARE IT                       ESP88150
         LH    R11,=Y(RENTWORK-KERMIT)                        ESP88150
         AR    R11,R12       MAKE COMMON READ-ONLY WORK AREA  ESP88150
         USING RENTWORK,R11  AND DECLARE IT                   ESP88150
         STM   R11,R12,@RENTWRK  SAVE BASE ADDRESSES          ESP88150
         SUBCALL INITRENT    INITIALIZE EVERYTHING               91290
         LTR   R8,R15        IS THERE A COMMAND BUFFER ?         91290
         SPACE 1                                              ESP86307
         WTERM  ' KERMIT Version 1.99'                        ESP88150
         LTR   R8,R8         IS THERE A COMMAND BUFFER ?         91294
         BZ    PROMPT        NO                               ESP88150
         CH    R8,=H'20'     SPECIAL ERROR RETURN CODE ?         91290
         BH    INITTAKE      NO; DO INITIAL TAKE                 91290
         B     *(R8)         BRANCH BY RETURN CODE               91290
          B     RET     +4    RETURN WITHOUT ERROR?              91290
          B     BADDEV  +8    UNSUPPORTED DEVICE TYPE            91290
          B     DOABEND 12    INIT IS UNHAPPY ?                  91290
         SPACE 1                                                 91290
INITTAKE LH    R1,0(,R8)     LOAD THE BUFFER LENGTH           ESP88150
         SH    R1,2(,R8)     LESS PORTION ALREADY PROCESSED   ESP88150
         SH    R1,=H'4'      LESS LENGTH OF LENGTHS           ESP88150
         BNP   PROMPT        NOTHING                          ESP88150
         LA    R2,4(,R8)     START OF TEXT                    ESP88150
         AH    R2,2(,R8)     SKIP TO UNPROCESSED PORTION      ESP88150
         ST    R2,@INCOME    SAVE START                       ESP88150
         LR    R3,R1         COPY LENGTH                      ESP88150
         LA    R14,INPUT     GET COMMAND BUFFER               ESP88150
         LA    R15,L'INPUT     AND LENGTH                     ESP88150
         MIN   R1,(R15)      PASS SMALLER LENGTH TO PARSER       91301
         ICM   R3,1,BLANKS   SET FOR BLANK FILL               ESP88150
         MVCL  R14,R2        MOVE AND FILL COMMAND            ESP88150
         TR    INPUT,UPPER   MAKE UPPER CASE                  ESP88150
         CLC   INPUT,INPUT+1  ALL BLANK ?                     ESP88150
         BNE   PROMPTS       NO; PROCESS (LENGTH STILL IN R1) ESP88150
**********************************************************************
*                                                                    *
*        MAIN COMMAND PROCESSING ROUTINE                             *
*                                                                    *
**********************************************************************
PROMPT   TM    FLAGS,FGBYE   TERMINATION REQUESTED ?          ESP88150
         BNZ   RET           RETURN TO TMP                    ESP88150
         SUBCALL FREEFILE    FREE HANGING ALLOCATIONS         ESP85142
         LA    R1,=C' Kermit-TSO> '                              91084
         TM    MODEFG,FGNET  RUNNING UNDER VTAM APPLICATION ?    91084
         BZ    PROMPTA       NO; NATIVE TSO                      91084
         LA    R1,=C' Kermit-Net> '                              91084
         TM    MODEFG,FGWYSS  WYLBUR SUBSYSTEM ?                 91267
         BZ    PROMPTA       NO; VTAM                            91267
         LA    R1,=C' WYL-Kermit> '                              91267
PROMPTA  WTERM (R1),13,MODE=ASIS   PROMPT FOR MORE               91084
         SUBCALL TTGETC      GET CHARACTER-MODE TEXT (EDITED) ESP88150
         BXH   R15,R15,ERRTGET  QUIT ON ERROR                 ESP88150
         TM    MODEFG,FGNET  MULTI-TASKING MODE ?                89337
         BZ    PROMPTS       NO; NO INTERCEPT                    89337
         L     R14,FAKECPPL+8  GET MTS ADDRESS BACK              89337
         ICM   R15,15,MTSCOMND-MTS(R14)  ANY INTERCEPT ROUTINE ? 89337
         BZ    PROMPTS       NO                                  89337
         LR    R0,R1         COPY LENGTH                         89345
         LA    R1,INPUT      POINT TO INPUT BUFFER               89345
         BALSR R14,R15       R0,R1 STILL SET                     89337
         CH    R15,=H'4'     CHECK RETURN                        89337
         BH    RET         8 QUIT                                89337
         BE    PROMPT      4 GET ANOTHER RESPONSE                89337
         LR    R1,R0         COPY LENGTH BACK (!)                89345
PROMPTS  LR    R0,R1         COPY RECEIVED LENGTH             ESP85142
         OI        FLAGS,FLG1          SET FLG1 - IT'S THE FIRST FILE
         NI    FLAGS,X'FF'-FLG2-FGHAZED  RESET SPECIAL FLAGS  ESP88150
         TM    IOFLAGS,FGREP GLOBAL FILE REPLACE ?            ESP88150
         BZ    *+8           NO                               ESP88150
         OI    FLAGS,FLG2    YES; PERMIT FOR THIS OPERATION   ESP88150
         MVI   SERVFUN,0     RESET SERVER MODE                ESP88150
         MVC   LONGLEN,LONGUSR  PRESET NEGOTIATED LENGTH         91286
         CLC   RSOH,INPUT    START OF HEADER ?                ESP88150
         BE    GOSERV        YES; TREAT AS SERVER COMMAND     ESP88150
         CLI   INPUT,C' '    NON-BLANK ?                      ESP88150
         BNH   GOSCAN        NO                               ESP88150
         CLI   INPUT+1,C' '  BLANK ?                          ESP88150
         BNE   GOSCAN        NO                               ESP88150
         CLI   INPUT+2,C'A'  ALPHA CHARACTER ?                ESP88150
         BL    GOSCAN        NO                               ESP88150
         CLI   INPUT+2,C'Z'  ALPHA ?                          ESP88150
         BH    GOSCAN        NO; NOT SERVER COMMAND           ESP88150
GOSERV   SUBCALL SERVER      GET INTO SERVER MODE             ESP88150
         B     PROMPT                                         ESP88150
GOSCAN   SUBCALL PARSER,INPUT  TOKENIZE THE INPUT             ESP88150
*
         LM        R7,R9,PARSELST      SAVE ADDR OF TOKENIZED LIST
         L         R6,0(,R7)           GET THE PTR TO FIRST OPERAND
*OOPS*   MVI       ERRNUM,X'FF'        RESET ERROR FOR THIS TIME
         BLOOK T=CMDTABLE,R=INPUT,X=R6,Y=R5,B=,ERR=PROMPTN    ESP88150
*ERR*    B     PROMPTN     +0 NO MATCH                        ESP88278
         BALS  R14,0(R12,R15)  +4 MATCH - EXECUTE             ESP88278
         B     PROMPT        AND PROMPT AGAIN                 ESP88278
PROMPTN  CLI   0(R6),C' '    NULL INPUT ?                     ESP88150
         BE    PROMPT        YES; JUST RE-SOLICIT             ESP88150
DOWHAT   WTERM    ' Legal Commands are: '                     ESP88150
 WTERM  ' SERver, REceive, SEnd, HElp, Exit, Quit, SET, SHow, STatus.'
         AIF   (NOT &PRINTER).NODELCM                            91315
         WTERM ' DELete/SCRatch, MAke,'                          91315
.NODELCM AIF   (NOT &INPREAD).NOTAKCM                            91315
         WTERM ' TAke,'                                          91315
.NOTAKCM AIF   ('&LOCAL' NE 'PID').NOBYE                     ESP 92320
         WTERM  ' BYE (LOGOFF, SIGNOFF).'                        91315
.NOBYE   TM    MODEFG,FGWYSS  WYLBUR MODE ?                      91290
         BZ    PROMPT        NO                                  91290
         WTERM ' WYLBUR (to return to editor)'                   91290
         B         PROMPT
         SPACE 1                                              ESP88150
CMDTABLE BTAB  SET,SET,BASE=*   COMMAND TABLE                 ESP88150
         BTAB  SER,SETSERV   SERVER MODE                      ESP88150
         BTAB  SH,DOSHOW                                      ESP88150
         BTAB  E,LEAVE       END, EXIT                        ESP88150
         BTAB  Q,LEAVE       QUIT                             ESP88150
         BTAB  LOGOFF,GOODBYE  QUIT AND LOGOFF                ESP88150
         BTAB  SIGNOFF,GOODBYE  QUIT AND LOGOFF               ESP88150
         BTAB  BYE,GOODBYE   QUIT AND LOGOFF                  ESP88150
         BTAB  '?',DOWHAT    SHOW HELP                        ESP88150
         BTAB  HE,DOHELP                                      ESP88150
         BTAB  ABEND,DOABEND                                  ESP88150
         BTAB  ST,DOSTAT     STATUS                           ESP88150
         BTAB  RE,DORECV     RECEIVE                          ESP88150
         BTAB  SE,DOSEND     SEND                             ESP88150
         AIF   (NOT &INPREAD).NOCMTAK                            91315
         BTAB  TA,DOTAKE     TAKE (A COMMAND FILE)            ESP88150
.NOCMTAK AIF   (NOT &PRINTER).NOCMDEL                            91315
         BTAB  SCR,DODELETE  DELETE SPECIFIED DATASET            91084
         BTAB  DEL,DODELETE   ALTERNATE FORM                     91084
*DEFER*  BTAB  'MA',DOMAKE   MAKE A COMMAND FILE                 91315
.NOCMDEL BTAB  *END                                           ESP88150
         SPACE 1                                              ESP88150
DOABEND  EX    0,*           BOMB ON 0C3                      ESP85142
         SPACE 1                                              ESP85142
**********************************************************************
*        PROCESS RECEIVE COMMAND                                     *
**********************************************************************
DORECV   BXH       R7,R8,RR3           GET NEXT OPERAND       ESP88150
         L         R6,0(,R7)           GET POINTER TO NEXT OPERAND
         CLI       0(R6),C'?'          NEED HELP?
         BNE       RR2
         WTERM  ' Specify dsname to be created for RECEIVE.'
         B     PROMPT        TRY AGAIN
RR2      CLI       0(R6),C' '          MORE WORDS ?
         BNE   RRALLOC       YES; ALLOCATE                    ESP88150
         B     RR3           NO, THEN PROMPT                  ESP88150
RR4LONG  CLC   =C'* ',TUDSNAME  "RECEIVE *" REQUESTED ?          90050
         BNE   RR4LONGA      NO; FAIL THIS                       90050
         NI    FLAGS,255-FLG1  SHOW ALLOCATION REQUIRED          90050
         B     RRDELAY       ALLOCATE ON 'F' PACKET              90050
RR4LONGA WTERM  ' DSName malformed'                           ESP88150
RR3      WTERM  ' Enter data set name for RECEIVE; you may use * for gr*
               oups or to use P/C''s name.'                      91315
         SUBCALL TTGETC      GET DSNAME                       ESP88150
         BXH   R15,R15,ERRTGET  QUIT ON ERROR                 ESP88150
         LA    R6,INPUT      RESCAN                           ESP88150
         CLC   INPUT,INPUT+1  ALL BLANK ?                        91310
         BE    PROMPT        YES; PROMPT FOR COMMAND             94089
RRALLOC  OI    IOFLAGS,FGOUT  SET TO CREATE OUTPUT            ESP88150
         SUBCALL FILEMAKE    PARSE, ALLOCATE AND OPEN         ESP88150
         B     *+4(R15)      BRANCH ON RETURN CODE            ESP88150
         B     RRDELAY   0   OPEN AND READY                   ESP88150
         B     RR4LONG   4   DSN MALFORMED                    ESP88150
         B     RRDUPL    8   ALREADY EXISTS                   ESP88150
         B     SSFALLO  12   ALLOCATION FAILED                ESP88150
         B     SSFDCB   16   DCB EXIT OR PARMS BAD            ESP88150
         B     SSFOPEN  20   OPEN FAILED                      ESP88150
         B     SSFRACF  24   ACCESS DENIED BY SECURITY PACKAGE   91091
         SPACE 1                                              ESP88150
RRDUPL   WTERM  ' Dataset already exists; OK to overwrite ? ',MODE=ASIS
         SUBCALL TTGETC      GET A RESPONSE                   ESP88150
         BXH   R15,R15,ERRTGET  QUIT ON ERROR                 ESP88150
         CLC   =C'OK',INPUT                                   ESP88150
         BE    RRDUPE        YES                              ESP88150
         CLI   INPUT,C'Y'    YES; YEAH; YEP; YUP OR WHAT-NOT? ESP88150
         BNE   PROMPT        NO                               ESP88150
RRDUPE   OI    FLAGS,FLG2    ALLOW REPLACEMENT                ESP88150
         MVI   STATE,C'C'    RESET STATE                      ESP88150
         MVI   ERRNUM,X'FF'  RESET ERROR                      ESP88150
         OI    PTCHFLG,FGTGEXT  SHOW DSN ALREADY PARSED       ESP88150
         B     RRALLOC                                        ESP88150
         SPACE 1                                              ESP88150
RRDELAY  SLR   R6,R6         GET ZERO                         ESP88150
         ST    R6,N          HERE TOO                         ESP88150
         MVI   STATE,C'R'    SET TO RECEIVE STATE             ESP88150
         WTERM    ' Receive waiting...'
         TCLEARQ INPUT       CLEAR NAKS FROM BUFFER           ESP88150
         SUBCALL RECEIVE                                      ESP88150
         MVI   OLDERR,X'FF'  SET NO ERROR                     ESP88150
         BXLE  R15,R15,PROMPT  COMPLETE WITHOUT ERROR         ESP88150
         MVC   OLDERR,ERRNUM    ERROR SETTING OF THIS RUN     ESP88150
         WTERM    ' Error in receiving file. Try again.'
         B         PROMPT              ERROR - TRY AGAIN
         SPACE 1                                              ESP88150
**********************************************************************
*        PROCESS SEND COMMAND                                        *
**********************************************************************
DOSEND   BXH       R7,R8,SS3           NO MORE LEFT           ESP88150
         L     R6,0(,R7)            PICK UP  NEXT OPERAND
         CLI       0(R6),C'?'          NEED HELP?
         BNE       SS2
         WTERM    ' Specify dataset name.'
         B         PROMPT
SS2      CLI   0(R6),C' '    ANY DATASET NAME ?               ESP88150
         BNE   SSALLOC       YES; TRY TO ALLOCATE IT          ESP88150
         B     SS3           ELSE PROMPT FOR THE NAME         ESP88150
SS4LONG  WTERM  ' DSName malformed'                           ESP88150
SS3      WTERM    ' Enter name of dataset to send.'           ESP88150
         SUBCALL TTGETC      GET DSNAME                       ESP88150
         BXH   R15,R15,ERRTGET  QUIT ON ERROR                 ESP88150
         LA    R6,INPUT      SET SCAN START                   ESP88150
         CLC   INPUT,INPUT+1  ALL BLANK ?                        91310
         BE    PROMPT        ASK FOR A COMMAND                   94089
SSALLOC  NI    IOFLAGS,255-FGOUT  INPUT ONLY                  ESP88150
         SUBCALL FILEMAKE    PARSE, ALLOCATE AND OPEN         ESP88150
         B     *+4(R15)      BRANCH ON RETURN CODE            ESP88150
         B     SSDELAY   0   OPEN AND READY                   ESP88150
         B     SS4LONG   4   DSN MALFORMED                    ESP88150
         NOP   0         8   ..RESERVED                       ESP88150
         B     SSFALLO  12   ALLOCATION FAILED                ESP88150
         B     SSFDCB   16   DCB EXIT OR PARMS BAD            ESP88150
         B     SSFOPEN  20   OPEN FAILED                      ESP88150
         B     SSFRACF  24   ACCESS DENIED BY SECURITY PACKAGE   91091
         SPACE 1                                                 91091
SSFRACF  WTERM ' Access denied by security package.'             91091
         B     PROMPT                                            91091
         SPACE 1                                              ESP88150
SSFOPEN  WTERM  ' Open for dataset failed.'                   ESP88150
         B     PROMPT                                         ESP88150
SSFDCB   WTERM   ' Invalid or unsupported DCB parameters:'    ESP88150
         WTERM  ' e.g., bad access method, LRECL too big,'    ESP88150
         WTERM  '       or RECFM neither Fx nor Vx'           ESP88150
         B     PROMPT                                         ESP88150
SSFALLO  WTERM  ' Dynamic allocation failed'                  ESP88150
         B     PROMPT                                         ESP88150
SSDELAY  MVC   WRKBUFF(38),=C' Waiting ..... seconds before sending.'
         L         R1,DELAY
         SLR   R0,R0                                          ESP88150
         ST    R0,N          CLEAR PACKET NUMBER              ESP88150
         D         R0,=F'100'
         BINCVRT   R1,WRKBUFF+8,PKVAR                         ESP88150
         WTERM  WRKBUFF,38                                    ESP88150
         STIMER    WAIT,BINTVL=DELAY
         MVI   STATE,C'S'                                     ESP88108
         TCLEARQ INPUT       CLEAR NAKS FROM BUFFER           ESP88150
         SUBCALL SEND                                         ESP88150
         MVI   OLDERR,X'FF'  SET NO ERROR                     ESP88150
         BXLE  R15,R15,PROMPT   CONTINUE                      ESP88150
         MVC   OLDERR,ERRNUM    ERROR SETTING OF THIS RUN     ESP88150
         WTERM    ' Error in sending file. Try again.'
         B         PROMPT              ERROR - TRY AGAIN
         SPACE 1                                              ESP88150
**********************************************************************
*        PROCESS STATUS COMMAND                                      *
**********************************************************************
DOSTAT   SUBCALL STATUS                                       ESP88150
         BXLE  R15,R15,PROMPT  CHECK RETCODE                  ESP88150
MSGBADOP WTERM    ' Illegal operand'                          ESP88150
         B         PROMPT
**********************************************************************
*        PROCESS SHOW COMMAND                                        *
**********************************************************************
DOSHOW   BXH       R7,R8,GIVSHOW       NO MORE LEFT           ESP88150
         L         R6,0(R7)            PICK UP  NEXT OPERAND
         CLI       0(R6),C'?'          NEED HELP?
         BNE   GIVSHOW
         WTERM    ' Command is SHOW STATE'                    ESP88150
         B         PROMPT
GIVSHOW  SLR   R5,R5                                          ESP88150
         ICM   R5,1,ERRNUM   GET CURRENT ERROR MESSAGE           91112
         BNM   GIVSHOWR      SHOW ERROR                          91112
         ICM   R5,1,OLDERR   GET TRANSFER CODE                ESP88150
         BNM   GIVSHOWR      ERROR - DISPLAY MESSAGE          ESP88150
         CLI   ERRNUM,X'FE'  INITIAL MODE ?                      91267
         BE    GIVSHOWI                                          91267
         WTERM  ' KERMIT transfer completed successfully'     ESP88150
         B         PROMPT
GIVSHOWI WTERM ' Ready for setup/file transfer'                  91267
         B     PROMPT                                            91267
GIVSHOWR LA    R0,L'ERRTAB   GET TABLE LENGTH                 ESP88150
         MR    R4,R0         MAKE INDEX INTO OFFSET           ESP88150
         LA    R1,ERRTAB(R5)  POINT TO MESSAGE                ESP88277
         MVI   WRKBUFF,C' '  NO MESSAGE ID                    ESP88277
         MVC   WRKBUFF+1(L'WRKBUFF-1),WRKBUFF  CLEAR WHOLE       91112
         MVC   WRKBUFF+1(L'ERRTAB),0(R1)  BUFFER              ESP88277
         ICM   R1,15,IOCANCOD  DCB EXIT ENTERED ?                91112
         BNZ   GIVSHOWA      YES; LONGER MESSAGE                 91112
         WTERM  WRKBUFF,L'ERRTAB+1  DISPLAY THE MESSAGE       ESP88150
         B         PROMPT              AND LEAVE
GIVSHOWA MVC   WRKBUFF+1+L'ERRTAB+5(CANPATLN),CANPAT  MAKE PATTERN
         UNPK  WRKBUFF+1+L'ERRTAB+5+10(7),IOCANCOD(4)  GET HEX   91112
         TR    WRKBUFF+1+L'ERRTAB+5+10(6),TRHEXTAB  MAKE PRINTABLE
         MVI   WRKBUFF+1+L'ERRTAB+5+13,C'-'                      91112
         WTERM WRKBUFF,1+L'ERRTAB+5+CANPATLN                     91112
         B     PROMPT        GO FOR MORE                         91112
CANPAT   DC    C'DCB AbEnd xxx-XX'                               91112
CANPATLN EQU   *-CANPAT                                          91112
**********************************************************************
*        PROCESS HELP COMMAND                                        *
**********************************************************************
DOHELP   WTERM  ' Enter ? at prompt to receive list of commands.'
         WTERM  ' Enter ? after a command to receive list of operands'
         B         PROMPT
**********************************************************************
*        PROCESS EXIT COMMAND                                        *
**********************************************************************
LEAVE    BXH       R7,R8,RET         ANY MORE OPERANDS?
         L         R6,0(,R7)           GET ADDRESS OF OPERAND
         CLI       0(R6),C'?'          NEED HELP?
         BNE   RET                     NO, JUST LEAVE
         WTERM    ' Confirm with a carriage return'
         B         PROMPT
ERRTGET  WTERM  ' Error getting input - cancelled'            ESP88150
         B     RET           QUIT                             ESP88150
BADDEV   WTERM    ' An ASCII terminal must be used.'
*        B         RET
         SPACE 1                                              ESP85142
GOODBYE  SUBCALL SYSBYE      REQUEST LOGOFF AFTER EXIT        ESP88150
RET      SUBCALL FREEFILE    CLOSE, WRITE MSGS, FREE ALL      ESP88150
         AIF   (NOT &INPREAD).NODONE                             91315
         ICM   R0,15,@SERVICE  ANY SERVICE FACILITIES USED ?  ESP88277
         BZ    RETSERV       NO                               ESP88277
         SERVTERM ,          CLOSE AND FREE EVERYTHING        ESP88277
         LA    R2,MISTAKEN   POINT TO TAKE FILE BASE          ESP88277
         LA    R3,8          LOOP COUNT                       ESP88277
RETTAKE  ICM   R4,15,0(R2)   LOAD AND TEST TAKE BASE          ESP88277
         BZ    RETTLOOP      NONE                             ESP88277
         USING TAWSECT,R4    DECLARE IT                       ESP88277
         SUBCALL DDFREE,TAWPTR  FREE THE DDNAME ALLOCATION    ESP88277
         LA    R0,TAWSIZE    GET SIZE                         ESP88277
         FREEMAIN R,LV=(0),A=(R4)  FREE IT                    ESP88277
         XC    0(4,R2),0(R2)  CLEAR THE POINTER               ESP88277
RETTLOOP LA    R2,4(,R2)     ADVANCE                          ESP88277
         BCT   R3,RETTAKE    TRY AGAIN                        ESP88277
.NODONE  SPACE 1                                              ESP88277
RETSERV  CLOSE MF=(E,@DEBDCB)                                    89330
         ENDM  RC=0          RETURN TO CALLER WITH CODE 0        89327
         SPACE 2                                              ESP88150
*        BRANCH ROUTINE                                       ESP88150
*        R0 (24-31) CHARACTER TO BE MATCHED                   ESP88150
*        R1    CHARACTER/BRANCH OFFSET TABLE                  ESP88150
*        RETURN - R14 IF NO MATCH, ELSE OFFSET                ESP88150
*                                                             ESP88150
         PUSH  USING                                          ESP88150
CHARLOOK STM   R14,R12,12(R13)                                ESP88150
         USING CHARLOOK,R15  PASSED BASE                      ESP88150
CHARLOOP CLC   =X'FF',0(R1)  END OF LIST ?                    ESP88150
         BE    CHARLOOX      YES                              ESP88150
         CLM   R0,1,0(R1)    MATCH ?                          ESP88150
         BE    CHARLOOB      YES; BRANCH                      ESP88150
         LA    R1,4(,R1)     TRY NEXT ENTRY                   ESP88150
         B     CHARLOOK                                       ESP88150
CHARLOOX ICM   R14,7,1(R1)   ANY END ADDRESS ?                ESP88150
         BNZ   CHARLOOY      YES                              ESP88150
         L     R14,12(R13)                                    ESP88150
CHARLOOY LM    R15,R12,16(R13)                                ESP88150
         BR    R14           RETURN                           ESP88150
CHARLOOB LH    R14,2(,R1)    GET OFFSET                       ESP88150
         A     R14,@KERMIT   RELOCATE                         ESP88150
         LM    R15,R12,16(R13)                                ESP88150
         BR    R14           BRANCH TO CODE                   ESP88150
         POP   USING                                          ESP88150
         SPACE 1                                              ESP88150
         LTORG ,                                              ESP88150
         SPACE 2
**********************************************************************
*                                                                    *
*        PROGRAM INITIALIZATION                                      *
*                                                                    *
**********************************************************************
INITRENT SUBENT ,            SET LOCAL OPTIONS                   91290
         LA    R14,DYNRBPTR  GET ALLOCATION WORK AREA            89328
         LA    R15,PRMLNGTH  GET LENGTH                          89328
         L     R0,=A(PRMRBPTR)  GET DYNALLOC WORK SPACE          89327
         LR    R1,R15        COPY LENGTH                         89328
         MVCL  R14,R0        MOVE ALLOCATION PATTERN             89328
         LA    R14,DYNRB                                         89327
         ST    R14,DYNRBPTR                                      89327
         LA    R15,S99RC                                         89328
         LA    R0,DFZEROES                                       89328
         LA    R1,DFSWTCHS                                       89328
         SLR   R2,R2                                             89328
         LA    R3,DFBUFL1                                        89328
         STM   R14,R3,DFPARMS                                    89328
         OI    DYNRBPTR,X'80'                                    89327
         LA    R14,TUDDN                                         89327
         LA    R15,TUDSN                                         89327
         LA    R0,TUSTA                                          89327
         LA    R1,TUDIS                                          89327
         STM   R14,R1,TEXTOLD                                    89327
         STM   R14,R1,TEXTNEW                                    89327
         LA    R14,TUFRE                                         89327
         LA    R15,TUINO                                         89327
         LA    R0,TUMEM                                          89327
         STM   R14,R0,TEXTOLD+16                                 89327
         OI    TEXTOLDL,X'80'                                    89327
         OI    TEXTOLDL+4,X'80'                                  89327
         LA    R14,TUINO                                         89327
         LA    R15,TUFRE                                         89327
         LA    R0,TUUNT                                          89327
         LA    R1,TUTRK                                          89327
         LA    R2,TUPRI                                          89327
         LA    R3,TUSEC                                          89327
         STM   R14,R3,TEXTNEW+16                                 89327
         LA    R14,TUREL                                         89327
         LA    R15,TUMEM                                         89327
         LA    R0,TUDIR                                          89327
         STM   R14,R0,TEXTNEWL                                   89327
         OI    TEXTNEWL,X'80'                                    89327
         OI    TEXTNEWL+8,X'80'                                  89327
         MVC   TEXTWYL(TEXTWYL-TEXTNEW),TEXTNEW                  89327
         LA    R14,TUUWYL                                        89327
         ST    R14,TEXTWYL+24                                    89327
         MVI   FILNAM,ASP    MAKE ASCII BLANK                 ESP88150
         MVC   FILNAM+1(L'FILNAM+LABLANKS-1),FILNAM  ALL BLK  ESP88150
         MVI       ERRNUM,X'FE'        SET TO NO ERROR FOR NOW
         MVI       OLDERR,X'FE'        SAME HERE
         MVC   DELAY+L'DELAY-L'DDELAY(L'DDELAY),DDELAY        ESP88150
         MVC   LRECL,DLRECL        SET DEFAULTS, JUST IN CASE ESP85142
         MVC   BLKSIZE,DBLKSIZE  SET DEFAULT BLOCKSIZE        ESP88150
         MVI   EDFLAGS,FGDEBS+FGDEBG  STRIP TRAILING BLANKS   ESP88307
         LH    R0,DTRACK     PRIMARY/SECONDARY SPACE DEFAULT  ESP85142
         STCM  R0,7,TUPRIME  STASH PRIMARY REQUEST            ESP85142
         STCM  R0,7,TUSECOND  SET SECONDARY SPACE             ESP85142
         LH    R0,DSSIZ      GET MAXIMUM (AND DEFAULT) SEND SIZE 88150
         ST    R0,SPSIZ      SET SIZE EXPECTED                ESP88150
         ST    R0,RPSIZ      SET SIZE EXPECTED                ESP88150
         SH    R0,=H'3'      ALLOW FOR OVERHEAD               ESP88150
         ST    R0,SIZE       SET DATA SIZE IN PACKET          ESP88150
         MVC   RECFM,DRECFM  DEFAULT RECFM                    ESP88150
         MVC   SQUO,DQUOTE   SET SEND QUOTE                   ESP88150
         MVC   RQUO,DQUOTE                                    ESP88150
         MVC   REOL,DEOL                                      ESP88150
         MVC   SEOL,DEOL                                      ESP88150
         MVC   SSOH,DSOH                                      ESP88150
         MVC   RSOH,DSOH                                      ESP88150
         MVI   SCHECK,A2     SET DEFAULT CHECKSUM MODE        ESP88150
         MVC   RCHECK,SCHECK  COPY TO WORKING MODE BYTE       ESP88150
         MVI   BLANKS,C' '   1/2 MAKE A LINE OF BLANKS        ESP88150
         MVC   BLANKS+1(L'BLANKS-1),BLANKS  2/2               ESP88150
         L     R1,=A(PTCHPAT)  SET FOR PATTERN PTCH TRT          91315
         MVC   PTCHTAB(256),0(R1)  BUILD 'NOTABS' TABLE          91315
         L     R14,=A(ETOA)  EBCDIC TO ASCII                     94297
         ST    R14,@MAKASC   SET TRANSLATION ADDRESS             91315
         L     R15,=A(ATOE)  ASCII TO EBCDIC                     94297
         ST    R15,@MAKEBC   SET TRANSLATION ADDRESS             91315
         MVC   TABNAME,=CL6' ASCII'  SET DEFAULT                 94297
         MVI   CAPAFG,FGRLONG+FGAPACK  ABLE TO PROCESS LONG PACKETS
         MVC   LONGMAX,=Y(&OPPAKLG)  SET MY SIZE                 91077
         MVC   LONGLEN,LONGMAX  PRESET NEGOTIATED LENGTH         91286
         MVC   LONGUSR,LONGLEN  PRESET USER OVERRIDE             91286
         LA    R0,KERIN      GET DCB ADDRESS                  ESP88150
         ST    R0,@IODCB     STASH IT                         ESP88150
         OI    @IODCB,X'80'  SET END-LIST BIT FOR SINGLE DCB  ESP88150
         LA    R0,DYNALCRC   GET DYNAMIC ALLOCATION RETURN CODE P88150
         ST    R0,DYNAPARM   SET INTO ALLOCATION PARM         ESP88150
         ST    R0,DYNAFREE   SET INTO DE-ALLOCATION           ESP88150
         OI    DYNAFREE,X'80'  REQUEST DE-ALLOCATION          ESP88150
         MVC   DEBUG(PATBUGL),PATBUG  MAKE DEBUG DCB             89330
         LA    R14,DEBUG                                         89330
         ST    R14,@DEBDCB   MAKE ADDRESS                        89330
         OI    @DEBDCB,X'80' MAKE OPEN LIST                      89330
         MVC   INPUTRDW(9),=X'00590000C394847A40'  'CMD:'     ESP88150
         SERVINIT LPA=NO     INITIALIZE THE @SERVICE ROUTINE  ESP88312
         AIF   (NOT &INPREAD).NOLINIT                            91315
         SERVCALL LPALD,=CL8'@INPREAD'  LOAD THE READER       ESP88277
         ST    R0,@INPREAD   STASH IT, TOO                    ESP88277
.NOLINIT SPACE 1                                              ESP88203
         LH    R2,DLRECL     GET DEFAULT RECORD LENGTH        ESP88203
         LA    R2,4(,R2)     PLUS RDW LENGTH                  ESP88203
         GETMAIN R,LV=(R2)   GET SOME STORAGE                 ESP88203
         STM   R1,R2,@IORDW  SAVE I/O BUFFER ADDRESS          ESP88203
         LA    R1,4(,R1)     GET DATA PORTION                 ESP88203
         ST    R1,@IOBUF     SAVE IT                          ESP88203
         SPACE 1                                              ESP88150
         TM    MODEFG,FGNET  ARE WE RUNNING AS A SUB-TASK ?      89327
         BZ    NOTNET        NO; DO NORMAL TSO CODE              89327
         TM    8(R9),X'80'   CORRECT CALLING SEQUENCE ?          89327
         BZ    INITBEND      INCORRECT PASSED PARM               91290
         MVC   FAKECPPL(12),0(R9)  SAVE BUFFER, MTV, MTS ADDRESS 89327
         LA    R9,FAKECPPL                                       89327
         ST    R9,@CPPL      FAKE A BIT                          89327
         L     R3,FAKECPPL+8 LOAD MTS ADDRESS                    93342
         USING MTS,R3        DECLARE IT                          93342
*OOPS*   CLC   =C'MTS',MTSID  CORRECT CONTROL BLOCK ?            89327
*OOPS*   BNE   INITBEND      NO; QUIT THE HARD WAY               91290
         MVC   @GETLINE,MTSGETLN  COPY GETLINE ADDRESS           89327
         MVC   @PUTLINE,MTSPUTLN  DITTO FOR PUTLINE              89327
         TM    MTSTRMFG,MTSFWYLB  WYLBUR MODE ?                  91267
         BZ    *+8           NO                                  91267
         OI    MODEFG,FGWYSS   INDICATE WYLBUR SUBSYSTEM         91267
         ICM   R15,15,MTSTETOA  EBCDIC TO ASCII TRANSLATION ?    91315
         BZ    *+8           NO                                  91315
         ST    R15,@MAKASC   SET IT                              91315
         ICM   R15,15,MTSTATOE  ASCII TO EBCDIC ?                91315
         BZ    *+8           NO                                  91315
         ST    R15,@MAKEBC   SET IT                              91315
         ICM   R0,15,MTSKMXPK  USER PROVIDED MAXIMUM PACKET SIZE 91267
         BZ    NETNOMAX      NO                                  91267
         SH    R0,=H'3'      ALLOW FOR OVERHEAD                  91290
         CH    R0,=H'&OPPAKMN'     AT LEAST MINIMUM ?            91311
         BL    NETNOMAX      NO; NO OVERRIDE                     91290
         CH    R0,MAXPACK    MAXIMUM SHORT SIZE ?                91290
         BH    NETDOMAX      YES                                 91290
         XC    LONGMAX,LONGMAX                                   91290
         XC    LONGLEN,LONGLEN                                   91290
         XC    LONGUSR,LONGUSR                                   91290
         ST    R0,SPSIZ      SET SHORT LENGTH                    91290
         ST    R0,RPSIZ                                          91290
         SH    R0,=H'3'                                          91290
         ST    R0,SIZE       SET DATA SIZE                       91290
         NI    CAPAFG,255-FGRLONG  CANCEL LONG PROTOCOL SUPPORT  91290
         B     NETNOMAX                                          91290
NETDOMAX STMIN R0,LONGMAX,TYPE=H    SET MAX SIZE                 91267
         MVC   LONGLEN,LONGMAX  PRESET NEGOTIATED LENGTH         91286
         MVC   LONGUSR,LONGLEN  PRESET USER OVERRIDE             91286
NETNOMAX CLI   MTSUID,C'$'   IS THERE A USER ID ?                89327
         BL    INITBEND      NO; QUIT                            91290
         AIF   ('&LOCAL' NE 'PID').NOPFX1                        92320
         ICM   R4,15,MTSLOUD  HAVE A USER BLOCK ?                93342
         BZ    NOTALOUD      NO?                                 93342
         USING A$LOUD,R4                                         93342
         MVC   PFXFORM,A$UWYHIX  SET PREFERRED FORM INDICATOR    93342
         SUBCALL SETDSX      MAKE PREFIX FROM INDEX              93342
         ICM   R0,15,PREFIXL  WAS A LENGTH STORED ?              93342
         BNZ   NETDNPFX      YES; REJOIN COMMON                  93342
         DROP  R4                                                93342
         SPACE 2                                                 93342
NOTALOUD CLI   MTSUID+7,C' ' WYLBUR ID ?                         89327
         BH    NETWYLID      YES                                 89327
.NOPFX1  MVC   PREFIX,MTSUID SAVE USER ID                        89327
         LA    R0,8          SET MAXIMUM POSSIBLE LENGTH         89327
         LA    R15,MTSUID+7                                      89327
NETIDLOP CLI   0(R15),C' '   END OF ID ?                         89327
         BH    NETIDLEN      YES                                 89327
         BCTR  R15,0         BACKSPACE                           89327
         BCT   R0,NETIDLOP                                       89327
NETIDLEN ST    R0,PREFIXL    SET LENGTH OF PREFIX                89327
NETDNPFX DS    0H                                                93342
         AIF   ('&LOCAL' NE 'PID').NOPFX2                        92320
         MVC   TUUNIT,=CL8'TSO     '  SET DEFAULT SAVE GROUP     89327
         B     NETIDCOM      GO TO COMMON                        89327
NETWYLID MVC   PREFIX(4),=C'WYL.'  MAKE COMMON WYLBUR PREFIX     89327
         MVC   PREFIX+4(8),MTSACCT  COPY ACCOUNT NUMBER          89327
         MVI   PREFIX+12,C'.'  NEXT INDEX                        89327
         MVC   PREFIX+13(8),MTSUID  OWNER                        89327
         MVI   PREFIXL+L'PREFIXL-1,21  PREFIX LENGTH             89327
         NI    PREFIX+4,X'CF'  CONVERT ACCOUNT NUMBER TO INDEX   89327
         CLI   PREFIX+4,X'C0'  SYSTEMS ?                         89327
         BNE   *+8           NO                                  89327
         MVI   PREFIX+4,C'Z' SET SPECIAL                         89327
         MVC   TUUNIT,=CL8'WYLBUR  '  SET DEFAULT SAVE GROUP     89327
.NOPFX2  ANOP  ,                                                 91315
NETIDCOM TM    MTSSIMFG,MTSSICRT  RUNNING ON A CRT ?             89327
         BZ    NETIDCON      NO                                  89327
         OI    IOFLAG2,FGCRT YES ?                               89327
         MVI   PROTOCON,PCCX80  ONLY ONE WE HAVE                 89327
         NOP   INITBDEV      *****FOR DEBUGGING*****             91290
         DROP  R3                                                93342
NETIDCON L     R8,FAKECPPL   LOAD COMMAND BUFFER, IF ANY         89327
         B     VTAMHOST      JOIN COMMON                         89327
*        PREPARE TSO SPECIFIC FUNCTIONS                       ESP88150
*                                                             ESP88150
         USING CPPL,R9       DECLARE MAPPING                  ESP88150
NOTNET   TM    CPPLCBUF,X'80' INVOKED AS A COMMAND PROCESSOR? ESP88277
         BZ    DONECHEK      MAYBE                            ESP88277
         WTERM  MSG514A                                       ESP88277
         ICM   R1,15,ASCBASXB-ASCB(R2)  GET THE ASXB          ESP88277
         BZ    INITRET                                           91290
         ICM   R1,15,ASXBLWA-ASXB(R1)  GET THE LWA            ESP88277
         BZ    INITRET                                           91290
         L     R14,CPPLCBUF  LOAD INPUT PARAMETER             ESP88277
         ICM   R0,15,LWAPSCB-LWA(R1)                          ESP88277
         BZ    INITRET                                           91290
         LR    R15,R0                                         ESP88277
         ICM   R15,15,PSCBUPT-PSCB(R15)  GET UPT              ESP88277
         BZ    INITRET       TOO BAD                             91290
         ICM   R1,15,LWAPECT-LWA(R1)                          ESP88277
         BZ    INITRET       TOO BAD                             91290
         STM   R14,R1,FAKECPPL                                ESP88277
         LA    R9,FAKECPPL                                    ESP88277
         WTERM  MSG514B                                       ESP88277
DONECHEK ST    R9,@CPPL      SAVE CPPL FOR LATER              ESP88278
         LOAD  EP=IKJGETL    GET THE GETLINE LPA ADDRESS      ESP88150
         ST    R0,@GETLINE   SAVE IT                          ESP88150
         LOAD  EP=IKJPUTL    GET THE PUTLINE LPA ADDRESS      ESP88150
         ST    R0,@PUTLINE   SAVE IT                          ESP88150
         L     R1,CPPLECT    GET THE ECT ADDRESS              ESP88150
         L     R8,CPPLCBUF   GET THE COMMAND BUFFER ADDRESS   ESP88150
         ST    R1,MYIOPL+IOPLECT-IOPL  INTO THE IOPL          ESP88150
         LA    R1,MYIOPECB   GET DUMMY ECB                    ESP88150
         ST    R1,MYIOPL+IOPLECB-IOPL  DITTO                  ESP88150
         L     R2,CPPLUPT    GET TO UPT                       ESP88150
         ST    R2,MYIOPL+IOPLUPT-IOPL                         ESP88150
         SLR   R3,R3         CLEAR R3                         ESP88150
         ICM   R3,1,UPTPREFL-UPT(R2)  GET LENGTH              ESP85142
         ST    R3,PREFIXL    SAVE FOR LATER                   ESP85142
         BNP   SKIPPFX       NO PREFIX                        ESP85142
         BCTR  R3,0
         MVC       PREFIX(*-*),UPTPREFX-UPT(R2)  MOVE PREFIX
         EX        R3,*-6
SKIPPFX  ICM   R15,15,CPPLPSCB  PSCB ADDRESS ?                ESP88150
         BZ    NOPSCB        NO                               ESP84246
         CLI   PSCBGPNM-PSCB(R15),C' '  ANY GROUP NAME ?      ESP84246
         BNH   NOPSCB        NO; USE 'SYSALLDA' AS DEFAULT    ESP84246
         MVC   TUUNIT,PSCBGPNM-PSCB(R15)   GENERIC UNIT NAME  ESP84246
NOPSCB   GTSIZE ,                  GET TERMINAL INFO
         CH    R0,=H'1'      MORE THAN ONE LINE ON SCREEN ?   ESP88150
         BNH   OKDEVTTY      YES, THEN UNABLE TO USE (LAZY ?) ESP88150
         OI    IOFLAG2,FGCRT   CRT TERMINAL (327X...)         ESP88278
         B     INITBDEV      *****DEBUG***** ZAP BRANCH          91290
         MVI   PROTOCON,PCCX80  SET FOR OUR ONLY ONE          ESP88204
         B     VTAMTEST                                          91291
OKDEVTTY STSIZE SIZE=1920    MAKE TTY SIZE FOR LONG PACKETS      91291
VTAMTEST STATTN INPUT=TCAMATTN  SET ATTN TO CTRL-D            ESP88150
         CH    R15,=H'8'     NOT TCAM ?                       ESP88150
         BE    VTAMHOST      SET VTAM FLAG                    ESP88150
*        GTDEVSIZ                                             ESP88150
         SLR   R1,R1         CLEAR RESULT REGISTER            ESP88150
         L     R0,=AL1(21,0,0,0)  LOAD REQUEST CODE (GTDEVSIZ) SP88150
         SVC   94            TCAM DOESN'T RECOGNIZE THIS CALL ESP88150
         LTR   R1,R1         RUNNING UNDER VTAM ?             ESP88150
         BNP   *+8           NO                               ESP88150
VTAMHOST OI    IOFLAG2,FGVTAM  SET VTAM FLAG                  ESP88150
         MVC   DEFFIX,PREFIX  MAKE A BACKUP COPY              ESP88150
         MVC   DEFFIXL,PREFIXL                                ESP88150
         TM    MODEFG,FGNET  NETWORK VERSION ?                   89328
         BNZ   SKIPSTAE      YES; CALLER HAS STAI                89328
         L     R3,=A(ABNDEXIT)                                   89328
         ESTAE (R3),PARAM=(R10),MF=(E,LISTESTA) PASS WORK AREA   89328
         BXLE  R15,R15,GOODSTAE                               ESP85142
         WTERM  ' ESTAE failed'                               ESP85142
         SPACE 1                                              ESP85142
GOODSTAE LA    R15,ADCPPLP   GET ALLOC MSG CPPL               ESP88150
         ST    R9,0(,R15)    SAVE CPPL                        ESP84246
         DROP  R9            DONE WITH CPPL                   ESP88150
         NI    0(R15),X'7F'  CLEAR HIGH BIT                   ESP84246
         B     SKIPSTAE                                          91290
INITRET  LA    R8,4          SET FOR RETURN TO RET               91290
         B     SKIPSTAE                                          91290
INITBDEV LA    R8,8          SET FOR RETURN TO BADDEV            91290
         B     SKIPSTAE                                          91290
INITBEND LA    R8,12         SET FOR RETURN TO DOABEND           91290
*        B     SKIPSTAE                                          91290
SKIPSTAE SUBEX RC=(R8)       RETURN TO CALLER WITH BUFFER ADDR.  91290
         SPACE 1                                                 94296
         LTORG ,                                                 94296
         EJECT
**********************************************************************
*                                                                    *
*        ROUTINE TO PROCESS SET COMMAND                              *
*                                                                    *
**********************************************************************
SET      SUBENT ,            SET LOCAL OPTIONS                ESP88150
&ZZ@BLUK SETB  0             REQUEST BLOOK EXPANSION          ESP88150
         BXH   R7,R8,SETHLP  SHOW LIST IF NO MORE INPUT       ESP88150
         L     R6,0(,R7)     PICK UP NEXT OPERAND             ESP88150
*        L     R5,0(,R9)     LOAD END OF INPUT ADDRESS        ESP88150
         BLOOK T=SETTAB,R=INPUT,PFX=SETK,X=R6,Y=R5,B=,ERR=SETHLP 88150
         LA    R2,0(R15,R12)  GET THE BRANCH ADDRESS          ESP88150
         SH    R2,=H'8'      GET THE MESSAGE POINTER          ESP88150
         ST    R2,TEMPD+8    SAVE THE MESSAGE ADDRESS         ESP88150
         BXH   R7,R8,SETMSG  IDENTIFY MISSING OPERAND         ESP88150
         L     R6,0(,R7)     GET THE TEXT POINTER             ESP88150
         CLI   0(R6),C'?'    HELP REQUEST ?                   ESP88150
         BNE   0(R15,R12)    NO; EXECUTE THE ROUTINE          ESP88150
SETMSG   EX    0,0(,R2)      GET THE MESSAGE ADDRESS          ESP88150
         EX    0,4(,R2)      GET THE MESSAGE LENGTH           ESP88150
         LR    R15,R0        COPY THE LENGTH                  ESP88277
         EX    R15,SETMSGMV  MOVE                             ESP88277
         MVI   WRKBUFF,C' '                                   ESP88277
         WTERM  WRKBUFF,2(R15)  WRITE THE MESSAGE             ESP88277
         B     SETOK                                          ESP88150
SETMSGMV MVC   WRKBUFF+1(0),0(R1)                             ESP88277
         SPACE 1                                              ESP88150
SETHLP WTERM   ' BLKsize, CHEcksum, DEBug, DELay, END-of-line, LREcl,'
      WTERM  ' QUOte, PACket-size, PREfix, RECfm, SPAce, STArt-of-line'
         WTERM ' TABexpand, VOLser'                              91315
 WTERM ' ADD|REPlace|UPDate files, BINary|TEXt file mode, STRIP on|off'
         B         SETOK
         SPACE 1                                              ESP88150
SETTAB   BTAB  '?',SETHLP,BASE=SETZBASE   HELP REQUEST        ESP88150
         BTAB  HELP,SETHLP                                    ESP88150
         BTAB  RECL,SETLRECL ALTERNATE FORM                   ESP88150
         BTAB  REC,SETRECFM  RECFM                            ESP88150
         BTAB  QUO,SETQUOTE  QUOTE                            ESP88150
         BTAB  LRE,SETLRECL  LRECL                            ESP88150
         BTAB  BLK,SETBLKSI                                   ESP88150
         BTAB  BLO,SETBLKSI  BLOCKSIZE                        ESP88150
         BTAB  SPA,SETSPACE  SPACE                            ESP88150
         BTAB  VOL,SETVOLSR  VOLUME SERIAL FOR NEW DATASETS      91287
         BTAB  SER,SETVOLSR  DITTO                               91287
         BTAB  EOL,SETEOL    END-OF-LINE                      ESP88150
         BTAB  END,SETEOL                                     ESP88150
         BTAB  PAC,SETPACK   PACKET SIZE (RCV)                ESP88150
         BTAB  SIZ,SETPACK                                    ESP88150
         BTAB  DEB,SETDEBUG  DEBUG                            ESP88150
         BTAB  SOH,SETSOH    START OF HEADER                  ESP88150
         BTAB  STA,SETSOH                                     ESP88150
         BTAB  DEL,SETDELAY  DELAY TIME                       ESP88150
         BTAB  CHE,SETCHECK  CHECKSUM                         ESP88150
         BTAB  ADD,SETOPADD  ADD FILES ONLY                   ESP88150
         BTAB  REP,SETOPREP  REPLACE FILES                    ESP88150
         BTAB  UPD,SETOPUPD  REPLACE AND UPDATE FILES            91084
         BTAB  TIM,SETDELAY                                   ESP88150
         BTAB  PRE,SETPFIX   SET PREFIX                       ESP88150
         BTAB  CWD,SETPFIX   SET PREFIX (CHANGE WORKING DIRECTORY)
         BTAB  PFX,SETPFIX   SET PREFIX                       ESP88150
         BTAB  STR,SETSTRIP  DEBLANK                          ESP88307
         BTAB  TAB,SETTABEX  EXPAND RECEIVED TABS                91315
*        BTAB  FIL,SETTYPFX  FILE TYPE PREFIX                 ESP88150
*ETTABY1 BTAB  TYP,SETTYPF2  FILE TYPE PREFIX                 ESP88150
SETTABY2 BTAB  BIN,SETTYBIN  BINARY TEXT TRANSMISSION         ESP88150
         BTAB  TEX,SETTYTEX  TEXT TRANSMISSION                ESP88150
         BTAB  ASC,SETTYASC  TEXT TRANSMISSION - 7-BIT ASCII     94296
         BTAB  ISC,SETTYISC  TEXT TRANSMISSION - 8-BIT ISCII     94296
         BTAB  PC8,SETTYPC8  TEXT TRANSMISSION - 8-BIT PC-8      94296
         BTAB  PC-8,SETTYPC8 TEXT TRANSMISSION - 8 BIT PC-8      94297
         BTAB  LATIN1,SETTYLTN   TEXT MODE - 8-BIT LATIN 1       94297
         BTAB  LATIN,SETTYLTN TEXT TRANSMISSION - 8-BIT LATIN1   94297
         BTAB  SAS,SETTYSAS  SAS TRANSPORT FILE                  93095
         BTAB  *END                                           ESP88150
         SPACE 1                                              ESP88150
**********************************************************************
*                           SET RECFM                                *
**********************************************************************
 MSG 'Fixed (F), Variable (V) and Wylbur (W) files (V is default)'
SETRECFM CLI       0(R6),C'V'          REDUNDANT
         BE        FMSET
         CLI   0(R6),C'W'    WYLBUR (COMPRESSED) EDIT ?          89333
         BE    FMSET         YES                                 89333
         CLI       0(R6),C'F'          FIXED FORMAT?
         BE    FMSET                                             89333
         CLI   0(R6),C'U'    CAN'T READ ?                        89333
         BNE   SETERR
         MVI   0(R6),C'W'    CHANGE 'U' TO 'W'                   89333
FMSET    MVC   RECFM,0(R6)   PICK UP RECFM                    ESP88150
         B         SETOK
**********************************************************************
*                         SET QUOTE                                  *
**********************************************************************
         MSG   'A single character; ASCII value from 32 to 62 or 96 to *
               126'                                           ESP88150
SETQUOTE CLI       1(R6),C' '          IS IT ONLY ONE CHAR?
         BNE   SETERR                                         ESP88150
         L     R15,@MAKASC                                       91315
         TR    0(1,R6),0(R15)  GET ASCII FORM                    91315
         CLI   0(R6),X'21'   MUST NOT BE LESS THAN 32         ESP88150
         BL    SETERR                                         ESP88150
         CLI   0(R6),X'7E'   MUSTN'T BE LARGER THAN 126       ESP88150
         BH    SETERR                                         ESP88150
         CLI   0(R6),X'3E'   AND BE BETWEEN 32-63             ESP88150
         BNH   SETQUOTC                                       ESP88150
         CLI   0(R6),X'60'   OR BETWEEN 95-127                ESP88150
         BL    SETERR                                         ESP88150
SETQUOTC MVC   SQUO,0(R6)    SET NEW QUOTE CHAR               ESP88150
         B     SETOK         DONE                             ESP88150
**********************************************************************
*                         SET LRECL                                  *
**********************************************************************
         MSG   'Logical Record Length (default of &OPLRECL).' ESP88150
SETLRECL BALS  R14,SCNUMBER  PARSE AN INTEGER                 ESP85142
         BM    SETMSG        HELP REQUEST                     ESP88150
         BNZ   *+8           CHECK VALUE                      ESP88150
         LH    R0,DLRECL     0; RESTORE DEFAULT               ESP88150
SETRECOM CH    R0,=Y(&MXLRECL)  TEST FOR (CURRENT) MAXIMUM    ESP88203
         BH    SETERR        FAIL IT                          ESP88150
         STH   R0,LRECL      SET THE NEW VALUE                ESP85142
         AH    R0,=H'4'      LENGTH WITH RDW                  ESP88203
         C     R0,@IORDW+4   WILL IT FIT INTO BUFFER ?        ESP88203
         BNH   SETOK         YES                              ESP88203
         LR    R3,R0         SAVE IT                          ESP88203
         LM    R1,R2,@IORDW  LOAD OLD ADDRESS/LENGTH          ESP88203
         FREEMAIN R,A=(1),LV=(R2)  FREE OLD STORAGE           ESP88203
         LR    R2,R3         RESTORE SAVED LENGTH             ESP88203
         GETMAIN R,LV=(R2)   GET STORAGE                      ESP88203
         STM   R1,R2,@IORDW  STASH IT                         ESP88203
         LA    R1,4(,R1)     POINT TO DATA PORTION            ESP88203
         ST    R1,@IOBUF     SAVE POINTER                     ESP88203
         B         SETOK
**********************************************************************
*                         SET BLKSIZE                                *
**********************************************************************
         MSG   'Blocksize (default of &OPBLKSZ).'             ESP88150
SETBLKSI BALS  R14,SCNUMBER  PARSE AN INTEGER                 ESP85142
         BM    SETMSG        ?                                ESP88150
         BNZ   *+8           CHECK VALUE                      ESP85142
         LH    R0,DBLKSIZE   0; RELOAD DEFAULT                ESP88150
         CH    R0,=H'32760'  OS/VS MAXIMUM EXCEEDED ?         ESP85142
         BH    SETERR        TOO HIGH; TOO BAD                ESP88150
         STH   R0,BLKSIZE    SET NEW VALUE                    ESP85142
         B         SETOK
**********************************************************************
*                         SET TRACK ALLOCATION                       *
**********************************************************************
         MSG   'Dataset space allocation (default of &OPTRACK tracks).'
SETSPACE BALS  R14,SCNUMBER  PARSE AN INTEGER                 ESP85142
         BM    SETMSG        HELP                             ESP88150
         BNZ   *+8           CHECK                            ESP85142
         LH    R0,DTRACK     GET DEFAULT SPACE                ESP88150
         C     R0,=F'99999'  UNREASONABLE DEMAND ?            ESP85142
         BH    SETERR                                         ESP88150
         STCM  R0,7,TUPRIME   SET THE PRIMARY ALLOCATION      ESP85142
         STCM  R0,7,TUSECOND   AND THE SECONDARY              ESP85142
         B         SETOK     LOCAL SVC 99 EXIT WILL CHECK SOME MORE
**********************************************************************
*                         SET END-OF-LINE CHARACTER                  *
**********************************************************************
         MSG   'Must be a two-digit value less than 32 (dec).' SP88150
SETEOL   BALS  R14,SCNUMBER  PARSE INTEGER                    ESP85142
         BM    SETMSG                                         ESP85142
         BP    *+8                                            ESP88150
         IC    R0,DEOL       RESTORE DEFAULT                  ESP88150
         CH    R0,=H'31'     VALID ?                          ESP85142
         BH    SETERR                                         ESP88150
         STC   R0,SEOL        END-LINE VALUE TO SEND          ESP85142
         B         SETOK
**********************************************************************
*                         SET PACKET-SIZE                            *
**********************************************************************
 MSG   'Receive packet size (range: &OPPAKMN-&OPPAKLG decimal).' 91311
SETPACK  BALS  R14,SCNUMBER  PARSE AN INTEGER                 ESP85142
         BM    SETMSG        ?                                ESP88150
         BP    *+8           CHECK                            ESP88150
         LH    R0,LONGMAX    GET DEFAULT PACKET SIZE             91286
         CH    R0,MAXPACK    LONG OR SHORT ?                     91286
         BH    SETPACKL      LONG                                91286
         CH    R0,=H'&OPPAKMN'     AT LEAST MINIMUM ?            91311
         BL    SETERR                                         ESP88150
         ST    R0,RPSIZ      SET NEW VALUE                    ESP85142
         XC    LONGUSR,LONGUSR  CANCEL LONG PACKETS              91286
         NI    CAPAFG,255-FGRLONG  CANCEL LONG PACKETS           91290
         B         SETOK
SETPACKL CH    R0,=Y(&OPPAKLG)  LONGER THAN PROMPTED ?           91312
         BH    SETERR        YES; TOO BAD                        91286
         MIN   R0,LONGMAX,TYPE=H  ACCEPT BUT MAKE LEGAL          91312
         STH   R0,LONGUSR    SET DESIRED MAX                     91286
         MVI   CAPAFG,FGRLONG+FGAPACK  ABLE TO PROCESS LONG PACKETS
         B     SETOK                                             91286
**********************************************************************
*        SET DATASET PREFIX (WORKING DIRECTORY)                      *
**********************************************************************
         MSG   'Prefix of created dataset names or NONE'      ESP88150
SETPFIX  CLC   =C'NONE',0(R6)  RESTORE DEFAULT ?              ESP88150
         BNE   SETPFIXT      NO                               ESP88150
         MVC   PREFIX,DEFFIX                                  ESP88150
         MVC   PREFIXL,DEFFIXL  RESTORE DEFAULTS              ESP88150
         B     SETOK                                          ESP88150
SETPFIXT MVC   WRKBUFF(45),BLANKS  CLEAR                      ESP88150
         LA    R1,WRKBUFF    SET DESTINATION                  ESP88150
         LR    R15,R6        SET START                        ESP88150
         LA    R0,L'TUDSNAME MAXIMUM LENGTH                   ESP88150
SETPFIXL CLI   0(R15),C' '   END ?                            ESP88150
         BNH   SETPFIXN      YES                              ESP88150
         CLI   0(R15),C','   ALTERNATE END ?                  ESP88150
         BE    SETPFIXN                                       ESP88150
         MVC   0(1,R1),0(R15)  MOVE ONE BYTE                  ESP88150
         LA    R1,1(,R1)     INCREMENT                        ESP88150
         LA    R15,1(,R15)                                    ESP88150
         BCT   R0,SETPFIXL                                    ESP88150
SETPFIXN LA    R2,L'TUDSNAME MAXIMUM LENGTH                   ESP88150
         SR    R2,R0         TRUE LENGTH                      ESP88150
         ST    R2,PREFIXL    STASH IT                         ESP88150
         MVC   PREFIX,WRKBUFF  MOVE TEXT                      ESP88150
         B     SETOK                                          ESP88150
**********************************************************************
*        SET VOLUME SERIAL FOR NEW DATASETS (OVERRIDES CATLG)        *
**********************************************************************
         MSG   'Volume serial of authorized, mounted disk for new data'
SETVOLSR CLI   0(R6),C'?'    INFORMATION REQUEST ?               91287
         BE    SETMSG        YES; DISPLAY IT                     91287
         MVC   WRKBUFF(7),BLANKS  PREPARE WORK SPACE             91287
         LA    R14,WRKBUFF   OUTPUT BUFFER                       91287
         LA    R2,1          SET INCREMENT                       91287
         LA    R3,6(,R6)     MAX TO SCAN                         91287
SETVOLLP CLI   0(R6),C' '    TOO LOW ?                           91287
         BNH   SETVOLND      YES; END OF STRING                  91287
         CLI   0(R6),C','    ALTERNATE END ?                     91287
         BE    SETVOLND      YES                                 91287
         MVC   0(1,R14),0(R6)  MOVE CHARACTER                    91287
         LA    R14,1(,R14)   INCREMENT OUTPUT                    91287
         BXLE  R6,R2,SETVOLLP  TRY AGAIN                         91287
SETVOLND CLI   WRKBUFF,C' '  RESET TO DEFAULT ?                  91287
         BE    SETVOLNW      YES                                 91287
         CLI   WRKBUFF+6,C' '  TOO LONG ?                        91287
         BH    SETERR        NO; REJECT                          91287
         CLC   =C'NONE ',WRKBUFF  DITTO ?                        91287
         BE    SETVOLBK      YES                                 91287
         CLC   =C'CATLG ',WRKBUFF  WYLBUR CONVENTION ?           91287
         BE    SETVOLBK      YES                                 91287
         CLC   =C'CAT ',WRKBUFF                                  91287
         BE    SETVOLBK                                          91287
         SERVICE UCBDK,WRKBUFF  LOCATE UCB FOR NAME              91287
         LTR   R3,R0         RETURNED UCB ADDRESS ?              91287
         BZ    SETVOLNO      NO                                  91287
         TM    UCBSTAT-UCBOB(R3),UCBPRES+UCBRESV  MOUNTED ?      91287
         BZ    SETVOLNO      NO                                  91287
         TM    MODEFG,FGNET  MULTI-TASKING MODE ?                91293
         BZ    SETVOLNW      NO                                  91293
         L     R14,FAKECPPL+8  GET MTS ADDRESS BACK              91293
         XC    MTSDDB-MTS(8,R14),MTSDDB-MTS(R14)                 91293
         LA    R0,WRKBUFF    POINT TO DDNAME                     91293
         ST    R0,MTSDDB+4-MTS(,R14)                             91293
         MVI   MTSDDB+3-MTS(R14),X'C0'  SET VOLUME/WRITE MODE    91293
         LA    R1,MTSDDB-MTS(,R14)                               91293
         ICM   R15,15,MTSDDACC-MTS(R14)  ACCESS CHECK ?          91293
         BZ    SETVOLNW      NO; SKIP                            91293
         BALSR R14,R15       CHECK USER'S PRIVILEGES             91293
         BXLE  R15,R15,SETVOLNW  ACCESS ALLOWED                  91293
         WTERM ' Write access to volume denied.'                 91293
         B     SETERC        FAIL IT                             91293
SETVOLNO WTERM ' Operand does not specify a mounted direct access devic*
               e'                                                91287
         B     SETERC        RETURN WITH ERROR                   91287
SETVOLBK MVC   WRKBUFF(6),BLANKS  SET CATALOG MODE               91287
SETVOLNW MVC   TUVOLSER,WRKBUFF                                  91287
         B     SETOK         ACCEPT                              91287
**********************************************************************
*                         SET DEBUG ON|OFF                           *
**********************************************************************
         MSG   'Command is SET DEBUG ON | OFF'                ESP88150
SETDEBUG CLC       =C'ON',0(R6)        IS IT TIME TO TURN ON
         BE        DEBON               YES, OPEN FILE
         CLC       =C'OF',0(R6)       IS IT TIME TO TURN OFF
         BNE   SETERR        FAIL                             ESP88150
         CLOSE MF=(E,@DEBDCB)                                    89330
         B         SETOK
DEBON    OPEN  (,(OUTPUT)),MF=(E,@DEBDCB)                        89330
         TM        DEBUG+(DCBOFLGS-IHADCB),DCBOFOPN  IS IT OPEN?
         BO        SETOK
         WTERM    ' Unable to open DEBUG file; debug disabled.'
         B         SETERC
*********************************************************************
*                                                                   *
*        SET TRANSMISSION TYPE (TEXT -DEFAULT   VS. BINARY          *
*                                                                   *
*********************************************************************
         MSG   'SET FILE BINARY - transmit data as-is (no translate)'
SETTYBIN OI    EDFLAGS,FGASIS  SEND DATA AS-IS                ESP88150
         NI    EDFLAGS,255-FGSAS  RESET SAS MODE                 93095
         B     SETOK                                          ESP88150
         MSG   'SET FILE TEXT - data translated to/from EBCDIC'  94297
SETTYTEX NI    EDFLAGS,255-FGASIS-FGSAS TEXT WITH TRANSLATION    93095
         B     SETOK                                          ESP88150
         MSG   'SET FILE ASCII - 7-bit data with ASCII translation'
SETTYASC NI    EDFLAGS,255-FGASIS-FGSAS TEXT WITH TRANSLATION    94296
         MVC   TABNAME,=CL6' ASCII'                              94297
         L     R0,=A(ATOE)   ASCII TO EBCDIC                     94296
         ST    R0,@MAKEBC                                        94296
         L     R0,=A(ETOA)   EBCDIC TO ASCII                     94296
         ST    R0,@MAKASC                                        94296
         TM    MODEFG,FGNET  NETWORK/WYLBUR VERSION ?            94297
         BZ    SETOK         NO; NO MULTI-TASKING BLOCK          94297
         L     R3,FAKECPPL+8  GET MTS ADDRESS                    94297
         USING MTS,R3        DECLARE IT                          94297
         ICM   R15,15,MTSTETOA  EBCDIC TO ASCII TRANSLATION ?    94297
         BZ    *+8           NO                                  94297
         ST    R15,@MAKASC   SET IT                              94297
         ICM   R15,15,MTSTATOE  ASCII TO EBCDIC ?                94297
         BZ    *+8           NO                                  94297
         ST    R15,@MAKEBC   SET IT                              94297
         DROP  R3                                                94297
         B     SETOK                                             94296
         MSG   'SET FILE ISCII - 8-bit data with (tape) translation'
SETTYISC NI    EDFLAGS,255-FGASIS-FGSAS TEXT WITH TRANSLATION    94296
         MVC   TABNAME,=CL6' ISCII'                              94297
         L     R0,=A(ITOE)   ISCII TO EBCDIC                     94296
         ST    R0,@MAKEBC                                        94296
         L     R0,=A(ETOI)   EBCDIC TO ISCII                     94296
         ST    R0,@MAKASC                                        94296
         B     SETOK                                             94296
         MSG   'SET FILE TEXT - 8-bit data with PC-8 translation'
SETTYPC8 NI    EDFLAGS,255-FGASIS-FGSAS TEXT WITH TRANSLATION    94296
         MVC   TABNAME,=CL6'  PC-8'                              94297
         L     R0,=A(PTOE)   PC-8 TO EBCDIC                      94296
         ST    R0,@MAKEBC                                        94296
         L     R0,=A(ETOP)   EBCDIC TO PC-8                      94296
         ST    R0,@MAKASC                                        94296
         B     SETOK                                             94296
         MSG   'SET FILE TEXT - 8-bit data with LATIN1 translation'
SETTYLTN NI    EDFLAGS,255-FGASIS-FGSAS TEXT WITH TRANSLATION    94297
         MVC   TABNAME,=CL6'LATIN1'                              94297
         L     R0,=A(LTOE)   PC-8 TO EBCDIC                      94297
         ST    R0,@MAKEBC                                        94297
         L     R0,=A(ETOL)   EBCDIC TO PC-8                      94297
         ST    R0,@MAKASC                                        94297
         B     SETOK                                             94297
         MSG   'SET FILE SAS - transmits SAS transport files'    93095
SETTYSAS OI    EDFLAGS,FGASIS+FGSAS   SET FOR SAS MODE           93095
         MVI   RECFM,C'F'    REALLY FS                           93095
         LH    R0,BLKSIZE    USE BLKSIZE AS RECORD LENGTH        93095
         B     SETRECOM      DO IT                               93095
*********************************************************************
*                                                                   *
*        SET DEBLANKING - SET STRIP ON|BOTH SEND RECEIVE OFF        *
*                                                                   *
*********************************************************************
         MSG   'SET STRIP on|send|receive|off'                ESP88307
SETSTRIP CLC   =C'ON',0(R6)  BOTH ON ?                        ESP88307
         BE    STRIP2        YES                              ESP88307
         CLC   =C'BO',0(R6)  BOTH ON ?                        ESP88307
         BE    STRIP2        YES                              ESP88307
         CLC   =C'OF',0(R6)  OFF BOTH ?                       ESP88307
         BE    STRIP0        YES                              ESP88307
         CLC   =C'NO',0(R6)  NONE ?                           ESP88307
         BE    STRIP0                                         ESP88307
         CLC   =C'RE',0(R6)  RECEIVE ONLY ?                   ESP88307
         BE    STRIP10                                        ESP88307
         CLC   =C'? ',0(R6)  HELP ?                              91315
         BE    SETMSG        YES; NO ERROR                       91315
         CLC   =C'SE',0(R6)  SEND ONLY ?                      ESP88307
         BNE   SETERR        NO; INVALID                      ESP88307
STRIP01  NI    EDFLAGS,255-FGDEBG                             ESP88307
         OI    EDFLAGS,FGDEBS  STRIP ON SENDING               ESP88307
         B     SETOK                                          ESP88307
STRIP10  NI    EDFLAGS,255-FGDEBS                             ESP88307
         OI    EDFLAGS,FGDEBG  STRIP ON RECEIVE ONLY          ESP88307
         B     SETOK                                          ESP88307
STRIP2   OI    EDFLAGS,FGDEBG+FGDEBS  BOTH                    ESP88307
         B     SETOK                                          ESP88307
STRIP0   NI    EDFLAGS,255-FGDEBG-FGDEBS  NONE                ESP88307
         B     SETOK                                          ESP88307
*********************************************************************
*                                                                   *
*        SET TAB-EXPAND - SET TAB ON|EXPAND|OFF                     *
*                                                                   *
*********************************************************************
         MSG   'SET TABex on|off (ON expands received tabs to blanks)'
SETTABEX L     R1,=A(PTCHPAT)  GET PATTERN TABLE ADDRESS         91315
         CLC   =C'ON',0(R6)  EXPAND ON ?                         91315
         BE    TABEX2        YES                                 91315
         CLC   =C'EX',0(R6)  EXPAND ?                            91315
         BE    TABEX2        YES                                 91315
         CLC   =C'OF',0(R6)  OFF ?                               91315
         BE    TABEX0        YES                                 91315
         CLC   =C'NO',0(R6)  NONE ?                              91315
         BE    TABEX0                                            91315
         CLC   =C'? ',0(R6)  HELP ?                              91315
         BE    SETMSG        YES; NO ERROR                       91315
         B     SETERR        INVALID                             91315
TABEX2   MVC   PTCHTAB(256),0(R1)  REFRESH TABLE                 91315
         MVI   PTCHTAB+X'0C',12  INTERCEPT PAGE EJECT            91315
         MVI   PTCHTAB+X'08',16  INTERCEPT BACKSPACE             91315
         MVI   PTCHTAB+X'09',20  INTERCEPT AND EXPAND TABS       91315
         B     SETOK                                             91315
TABEX0   MVC   PTCHTAB(256),0(R1)  RESTORE PLAIN TABLE           91315
         B     SETOK                                             91315
*********************************************************************
*                                                                   *
*        SET FILE UPDATE/REPLACE OPTION VS. ADD-ONLY                *
*                                                                   *
*********************************************************************
         MSG   'Duplicate files replaced; new DCB options'       91084
SETOPUPD OI    IOFLAGS,FGREP+FGUPD SET REPLACE REQUESTED         91084
         B     SETOK                                             91084
         MSG   'Duplicate files replaced'                     ESP88150
SETOPREP OI    IOFLAGS,FGREP SET REPLACE REQUESTED            ESP88150
         B     SETOK                                          ESP88150
         MSG   'Duplicate files rejected'                     ESP88150
SETOPADD NI    IOFLAGS,255-FGREP-FGUPD  RESET REPLACE AND UPDATE 91084
         B     SETOK                                          ESP88150
**********************************************************************
*                         SET START-OF-HEADER CHARACTER              *
**********************************************************************
         MSG   'Must be a two-digit value less than 32 (dec).' SP88150
SETSOH   BALS  R14,SCNUMBER  PARSE AN INTEGER                 ESP85142
         BM    SETMSG        HELP                             ESP85142
         BP    *+8           VERIFY                           ESP88150
         IC    R0,DSOH       RESTORE DEFAULT VALUE            ESP88150
         CH    R0,=H'31'     IN CONTROL RANGE ?               ESP85142
         BH    SETERR        TOO BIG; TOO BAD                 ESP88150
         STC   R0,SSOH       SET SOH VALUE TO BE SENT         ESP85142
         STC   R0,RSOH       SET RECEIVE SOH VALUE            ESP85142
         B         SETOK
***********************************************************************
*        SET CHECKSUM PREFERRENCE (1 OR 2 BYTE SUM, OR 3-BYTE CRC     *
***********************************************************************
         MSG   'Must be 1 or 2(default) for checksum, or 3 for CRC'
SETCHECK BALS  R14,SCNUMBER  PARSE AN INTEGER                 ESP88150
         BM    SETMSG        HELP                             ESP88150
         BP    *+8           VERIFY                           ESP88150
         LA    R0,2          RESTORE DEFAULT VALUE            ESP88150
         CH    R0,=H'3'      IN RANGE ?                       ESP88150
         BH    SETERR        TOO BIG; TOO BAD                 ESP88150
         AH    R0,=Y(A0)     MAKE ASCII DIGIT                 ESP88150
         STC   R0,SCHECK     SET CHECKSUM MODE                ESP88150
         MVC   RCHECK,SCHECK  PROPAGATE TO CURRENT MODE, JUST IN CASE
         B     SETOK                                          ESP88150
**********************************************************************
*                      SET DELAY VALUE                               *
**********************************************************************
  MSG 'Delay time (secs) between SEnd/REceive command and first packet'
SETDELAY BALS  R14,SCNUMBER  PARSE AN INTEGER                 ESP85142
         BM    SETMSG        ?                                ESP88150
         BP    *+8                                            ESP88150
         LH    R0,DDELAY     RESTORE DEFAULT DELAY            ESP88150
         C     R0,=F'99999'  MAXIMUM DELAY ?                  ESP85142
         BH    SETERR        TOO HIGH; TOO BAD                ESP88150
         MH    R0,=H'100'    CONVERT TO 1/100THS OF A SECOND  ESP85142
         ST    R0,DELAY      SET TO USE IT                    ESP85142
         B         SETOK
         SPACE 1                                              ESP88150
SETERR   WTERM  ' Unacceptable operand; ',MODE=ASIS           ESP88150
         L     R2,TEMPD+8    RESTORE MESSAGE ADDRESS          ESP88150
         EX    0,0(,R2)      LOAD MESSAGE ADDRESS             ESP88150
         EX    0,4(,R2)      LOAD MESSAGE LENGTH              ESP88150
         LR    R15,R0                                         ESP88277
         EX    R15,SETMSGMV  MOVE TO BUFFER                   ESP88277
         MVI   WRKBUFF,C' '                                   ESP88277
         WTERM  WRKBUFF,2(R15)  WRITE THE ERROR MESSAGE       ESP88277
SETERC   LA        R15,4               SET A NON-ZERO RETCODE
         B         SETRET
SETOK    SLR   R15,R15       CLEAR RETURN CODE                ESP88150
*
SETRET   SUBEX RC=(R15)      RETURN TO CALLER WITH RC IN R15  ESP88150
         SPACE 2                                              ESP85142
*        SUBROUTINE TO EXTRACT AND CONVERT INTEGER            ESP85142
*        CALL ON R14, RETURNS VALUE IN R0 AND SETS CC         ESP85142
*        RETURNS TO SETERR AFTER MESSAGE IF NON-NUMERIC       ESP85142
*                                                             ESP85142
SCNUMBER STM   R1,R3,24(R13)  SAVE WORK REGISTERS             ESP85142
         LA    R0,1                                           ESP85142
         LNR   R0,R0         SET TO -1                        ESP85142
         CLC   =C'? ',0(R6)  HELP REQUEST ?                   ESP85142
         BE    SCNUMRET      YES; RETURN                      ESP85142
         SLR   R0,R0                                          ESP85142
         LA    R1,C'0'       SET FOR NULL PROCESSING          ESP85142
         CLI   0(R6),C' '    END OF PARM ?                    ESP85142
         BE    SCNUMNUL      YES; STOP                        ESP85142
         LA    R2,1          SET INCREMENT FOR LOCAL BXLE     ESP85142
         LA    R3,8(,R6)     SET FOR LAST ACCEPTED POSITION   ESP85142
         BCTR  R6,0          COMPENSATE FOR FIRST BXH         ESP85142
SCNUMLOP BXH   R6,R2,SCNUMDON  SKIP IF DONE                   ESP85142
         CLI   0(R6),C'0'    NUMERIC ?                        ESP85142
         BL    SCNUMDON      NO                               ESP85142
         CLI   0(R6),C'9'                                     ESP85142
         BH    SCNUMDON                                       ESP85142
         SLDL  R0,8                                           ESP85142
         IC    R1,0(,R6)     LOAD NEXT BYTE                   ESP85142
         B     SCNUMLOP      TRY AGAIN                        ESP85142
SCNUMDON CLI   0(R6),C' '    END OF VALUE ?                   ESP85142
         BNE   SCNUMERR                                       ESP85142
SCNUMNUL STM   R0,R1,TEMPD   STASH VALUE                      ESP85142
         PACK  PKVAR,TEMPD                                    ESP85142
         CVB   R0,PKVAR                                       ESP85142
SCNUMRET LTR   R0,R0         CHECK VALUE                      ESP85142
         LM    R1,R3,24(R13)  RELOAD WORK REGISTERS           ESP85142
         BR    R14           RETURN TO CALLER                 ESP85142
SCNUMERR LM    R1,R3,24(R13)  RELOAD WORK REGISTERS           ESP85142
         WTERM  ' Numeric operand expected, but not found'    ESP85142
         B     SETERR        SIGNAL ERROR                     ESP85142
         SPACE 1                                              ESP88150
         LTORG ,
         EJECT
**********************************************************************
*                                                                    *
*        ROUTINE TO PROCESS STATUS COMMAND                           *
*                                                                    *
**********************************************************************
STATUS   SUBENT ,            SHOW LOCAL OPTIONS               ESP88150
         BXH   R7,R8,STATUT  MORE OPERANDS ?                  ESP88150
         L     R6,0(,R7)     GET NEXT TOKEN                   ESP88150
         CLI   0(R6),C' '    END OF INPUT ?                   ESP88150
         BE    STATUT        YES; DISPLAY                     ESP88150
         WTERM    ' STATE requires no operands'
         CLI       0(R6),C'?'          NEED HELP ?
         BE    STATOK        HELP ONLY                        ESP88150
STATUT   MVC   WRKBUFF(80),BLANKS  CLEAR PRINT LINE           ESP88277
         MVC   WRKBUFF+1(17),=C'Record format is '            ESP88150
         MVC   WRKBUFF+18(1),RECFM                            ESP88150
         SLR   R4,R4         ZERO IT OUT                      ESP88150
         IC        R4,SSOH
  MVC WRKBUFF+35(44),=C'Start-Of-Header character is ..... (decimal)'
         BINCVRT   R4,WRKBUFF+28+35,PKVAR                     ESP88150
         WTERM  WRKBUFF,79                                    ESP88150
         MVC   WRKBUFF(80),BLANKS  CLEAR BUFFER               ESP88150
         LH    R4,LRECL                                       ESP88150
         MVC   WRKBUFF+1(8),=C'LRECL is'                      ESP88150
         BINCVRT   R4,WRKBUFF+9,PKVAR                         ESP88150
         MVC   WRKBUFF+35(18),=C'Quote character is'          ESP88150
         MVC   WRKBUFF+19+35(1),SQUO                          ESP88150
         L     R15,@MAKEBC   GET EBCDIC TRANSLATION              91315
         TR    WRKBUFF+19+35(1),0(R15)  GET EBCDIC VERSION       91315
         WTERM  WRKBUFF,20+35                                 ESP88150
         MVC   WRKBUFF(80),BLANKS                             ESP88150
         LH        R4,BLKSIZE
         MVC   WRKBUFF+1(10),=C'BLKSIZE is'
         BINCVRT   R4,WRKBUFF+11,PKVAR                        ESP88150
         SLR   R4,R4         ZERO IT OUT                      ESP88150
         IC        R4,SEOL
       MVC WRKBUFF+35(40),=C'End-Of-Line character is ..... (decimal)'
         BINCVRT   R4,WRKBUFF+24+35,PKVAR                     ESP88150
         WTERM  WRKBUFF,40+35                                 ESP88150
         MVC   WRKBUFF(80),BLANKS                             ESP88150
         SLR   R4,R4         JUST IN CASE                     ESP85142
         ICM   R4,7,TUPRIME  GET SPACE REQUEST                ESP85142
         MVC   WRKBUFF+1(32),=C'Space allocation is ..... tracks'
         BINCVRT   R4,WRKBUFF+20,PKVAR                        ESP88150
         MVC WRKBUFF+35(38),=C'Receive packet size is ..... (decimal)'
         L         R1,RPSIZ
         MAX   R1,LONGLEN,TYPE=H  OVERRIDE WITH LONG PACKET, IF ANY
         BINCVRT   R1,WRKBUFF+22+35,PKVAR                     ESP88150
         WTERM  WRKBUFF,38+35                                 ESP88150
         MVC   WRKBUFF(80),BLANKS                             ESP88150
         MVC   WRKBUFF+1(28),=C'Delay value is ..... seconds'
         L         R1,DELAY
         SLR   R0,R0                                          ESP88150
         D         R0,=F'100'
         BINCVRT   R1,WRKBUFF+15,PKVAR                        ESP88150
         MVC   WRKBUFF+35(9),=C'Debug is '                    ESP88150
         MVC   WRKBUFF+9+35(3),=C'off'                        ESP88150
         TM        DEBUG+(DCBOFLGS-IHADCB),DCBOFOPN  IS IT OPEN?
         BZ        STATDBG
         MVC   WRKBUFF+9+35(3),=C'on '                        ESP88150
STATDBG  WTERM  WRKBUFF,12+35                                 ESP88150
         MVC   WRKBUFF+1(22),=C'File transfer mode is '       ESP88150
         LA    R1,=CL34'binary (as-is) without translation'   ESP88150
         MVC   WRKBUFF+23(34),0(R1)                           ESP88150
         LA    R1,=CL40'text type (with  ASCII/EBCDIC translate)'
         TM    EDFLAGS,FGASIS+FGABIN  BINARY MODE ?              91310
         BNZ   STATTXM       NO                               ESP88150
         MVC   WRKBUFF+23(40),0(R1)  MOVE TEXT PATTERN           94297
         MVC   WRKBUFF+39(6),TABNAME  CURRENT TRANSLATE TABLE    94299
STATTXM  WTERM  WRKBUFF,23+39                                 ESP88150
         MVC   WRKBUFF(80),BLANKS  CLEAR AGAIN                ESP88307
         MVC   WRKBUFF+1(22),=C'STRIP trailing blanks:'       ESP88307
         LA    R1,=C'never   always  on send receive '        ESP88307
         TM    EDFLAGS,FGDEBS+FGDEBG  ANY STRIPPING ?         ESP88307
         BZ    STATSTRP      NO                               ESP88307
         LA    R1,8(,R1)     POINT TO ALWAYS                  ESP88307
         BO    STATSTRP      YES; BOTH                        ESP88307
         LA    R1,8(,R1)     POINT TO SEND                    ESP88307
         TM    EDFLAGS,FGDEBS  ON SEND ONLY ?                 ESP88307
         BNZ   STATSTRP      YES                              ESP88307
         LA    R1,8(,R1)     POINT TO RECEIVE                 ESP88307
STATSTRP MVC   WRKBUFF+24(8),0(R1)  MAKE MESSAGE              ESP88307
         LA    R0,32         SET DEFAULT MESSAGE LENGTH          91287
         CLI   TUVOLSER,C' '   USER SPECIFIED SERIAL ?           91287
         BNH   STATNVOL      NO                                  91287
         MVC   WRKBUFF+35(18),=C'Default VOLume is '             91287
         MVC   WRKBUFF+53(6),TUVOLSER                            91287
         LA    R0,59         NEW MESSAGE LENGTH                  91287
STATNVOL WTERM  WRKBUFF,(R0) WRITE STATUS                        91287
         MVC   WRKBUFF(80),BLANKS                                91315
         MVC   WRKBUFF+1(14),=C'TAB expansion:'                  91315
         LA    R1,=C'OFF'                                        91315
         CLI   PTCHTAB+X'09',0  TABS ?                           91315
         BE    *+8           NO                                  91315
         LA    R1,=C'ON '                                        91315
         MVC   WRKBUFF+1+14+1(3),0(R1)  COMPLETE                 91315
         WTERM WRKBUFF,19    WRITE IT                            91315
         MVC   WRKBUFF(80),BLANKS                             ESP88150
         MVC   WRKBUFF+1(32),=C'Duplicate files will be rejected' 8150
         TM    IOFLAGS,FGREP                                  ESP88150
         BZ    *+10                                           ESP88150
         MVC   WRKBUFF+27(4),=C'plac'  CHANGE TO 'REPLACED'   ESP88150
         TM    IOFLAGS,FGREP+FGUPD  UPDATE MODE ?                91084
         BNO   *+10                                              91084
         MVC   WRKBUFF+25(8),=C'updated.'                        91084
         MVC   WRKBUFF+35(18),=C'CHEcksum is x-byte'          ESP88150
         MVC   WRKBUFF+35+12(1),SCHECK  SET CURRENT CHECKSUM  ESP88150
         L     R15,@MAKEBC   GET EBCDIC TRANSLATION              91315
         TR    WRKBUFF+35+12(1),0(R15)    MAKE PRINTABLE         91315
         CLI   SCHECK,A3     CRC MODE ?                       ESP88150
         BNE   *+10          NO                               ESP88150
         MVC   WRKBUFF+35+19(3),=C'CRC'                       ESP88150
         WTERM  WRKBUFF,32+3+22                               ESP88150
         MVC   WRKBUFF+1(9),=C'Prefix : '                     ESP88150
         LA    R1,PREFIX     POINT TO DSN PREFIX              ESP88150
         ICM   R15,15,PREFIXL  GET THE LENGTH                 ESP88150
         SH    R15,=H'1'     ADJUST FOR EXECUTE               ESP88150
         BNM   STATCWD       MOVE                             ESP88150
         LA    R15,3                                          ESP88150
         LA    R1,=C'none'                                    ESP88150
STATCWD  EX    R15,STATCWDM  MOVE                             ESP88150
         LA    R0,10+1(,R15) LENGTH OF TEXT                   ESP88150
         WTERM  WRKBUFF,(R0) WRITE THE PREFIX                 ESP88150
         B         STATOK
STATCWDM MVC   WRKBUFF+10(0),0(R1)  COPY PREFIX               ESP88150
STATERR  LA        R15,4               SET A NON-ZERO RETCODE
         B         STATRET
STATOK   SLR   R15,R15       ZERO RETCODE                     ESP88150
*
STATRET  SUBEX RC=(R15)      RETURN WITH CC                   ESP88150
         SPACE 2                                                 93342
***********************************************************************
*                                                                     *
*   SET DSINDEX (SET DSX) - SET CURRENT PREFIX BY DSX TYPE            *
*                                                                     *
***********************************************************************
         SPACE 1                                                 93342
SETDSX   SUBENT ,            SUBROUTINE TO BUILD PREFIX          93342
         TM    MODEFG,FGNET  NETWORK/WYLBUR VERSION ?            93342
         BZ    SETDSXEX      NO; NO MULTI-TASKING BLOCK          93342
         L     R3,FAKECPPL+8  GET MTS ADDRESS                    93342
         USING MTS,R3        DECLARE IT                          93342
         SLR   R15,R15       CLEAR FOR INSERT                    93342
         ICM   R15,1,PFXFORM   LOAD INDEX TYPE                   93342
         BM    SETDSXBD                                          93342
         CH    R15,=Y((SHODSXTX-SHODSXTB)/(SHODSXT2-SHODSXTB))   93342
         BNH   SETDSXOK                                          93342
SETDSXBD WTERM 'INVALID DSINDEX BYTE; 0 ASSUMED'                 93342
         SLR   R15,R15       SET DEFAULT (UID ONLY)              93342
SETDSXOK SLR   R2,R2         SET LENGTH                          93342
         MVC   WRKBUFF(45),BLANKS  CLEAR PREFIX                  93342
         CH    R15,=H'1'     CHECK TYPE                          93342
         BL    SETDSXUI      USERID ONLY                         93342
         BE    SETDSXUA      USERID+ACCOUNT                      93342
         CH    R15,=H'3'     ACCT OR ACCTSUBA.                   93342
         BNL   SETDSXAC                                          93342
         MVC   WRKBUFF(4),MTSACCT  USE MAJOR ACCOUNT (LOCAL)     93342
         B     SETDSXAS                                          93342
SETDSXAC MVC   WRKBUFF(L'MTSACCT),MTSACCT   ACCT.                93342
SETDSXAS LA    R2,L'MTSACCT  PREFIX LENGTH                       93342
         B     SETDSXTM       JOIN COMMON                        93342
SETDSXUA MVC   WRKBUFF(7),MTSUID  GET USER                       93342
         LA    R2,L'MTSUID                                       93342
         BALS  R14,DSXTRIM                                       93342
         LA    R15,WRKBUFF(R2)                                   93342
         MVI   0(R15),C'.'                                       93342
         MVC   1(L'MTSACCT,R15),MTSACCT  APPEND ACCOUNT          93342
         LA    R2,1+L'MTSACCT(,R2)   PREFIX LENGTH               93342
         B     SETDSXTM                                          93342
SETDSXUI MVC   WRKBUFF(7),MTSUID  GET USER                       93342
         LA    R2,L'MTSUID     PREFIX LENGTH                     93342
         SPACE 1                                                 93342
SETDSXTM BALS  R14,DSXTRIM                                       93342
         MVI   1(R15),C'.'   MAKE INDEX LEVEL                    93342
         LA    R2,1(,R2)     MAKE TOTAL LENGTH                   93342
         ST    R2,PREFIXL    SET NEW LENGTH                      93342
         MVC   PREFIX,WRKBUFF                                    93342
SETDSXEX SUBEX ,             JOIN COMMON                         93342
         SPACE 1                                                 93342
DSXTRIM  LA    R15,WRKBUFF(R2)                                   93342
DSXTRIML BCTR  R15,0         POINT TO PRIOR BYTE                 93342
         TM    0(R15),255-C' '  BLANK OR NULL BYTE ?             93342
         BNZR  R14           NO; RETURN                          93342
         BCT   R2,DSXTRIML   TRY AGAIN                           93342
         BR    R14           BOO-BOO                             93342
         SPACE 2                                                 93342
***********************************************************************
*                                                                     *
*   SHOW DSINDEX (SHO DSX) - DISPLAY CURRENT FORMAT OF PREFIX         *
*                                                                     *
***********************************************************************
         SPACE 1                                                 93342
SHODSX   SUBENT ,            DISPLAY DSINDEX                     93342
         SLR   R15,R15       CLEAR FOR INSERT                    93342
         IC    R15,PFXFORM   GET FORMAT BYTE                     93342
         CH    R15,=Y((SHODSXTX-SHODSXTB)/(SHODSXT2-SHODSXTB))   93342
         BH    MSGNODSX                                          93342
         MH    R15,=Y(SHODSXT2-SHODSXTB)  CONVERT INDEX TO OFFSET
         LA    R3,SHODSXTB   GET TABLE BASE                      93342
         AR    R3,R15        POINT TO MESSAGE                    93342
         WTERM 'CURRENT DSX:',MODE=ASIS                          93342
         WTERM (R3),SHODSXT2-SHODSXTB,MODE=ASIS  SHOW TYPE       93342
         WTERM '  PREFIX:',MODE=ASIS                             93342
         L     R0,PREFIXL    LOAD PREFIX LENGTH                  93342
         WTERM PREFIX,(0)    DISPLAY PREFIX                      93342
SHODSXNU SUBEX ,             RETURN                              93342
         SPACE 1                                                 93342
MSGNODSX WTERM 'INVALID DSINDEX FOUND'                           93342
         B     SHODSXNU      ERROR RETURN                        93342
         SPACE 1                                                 93342
SHODSXTB DC    CL24'0 - USERID.NAME'                             93342
SHODSXT2 DC    CL24'1 - USERID.ACCTSUBA.NAME'                    93342
         DC    CL24'2 - ACCT.NAME'                               93342
         DC    CL24'3 - ACCTSUBA.NAME'                           93342
SHODSXTX EQU   *             END OF TABLE                        93342
         AIF   (NOT &INPREAD).NOTAKE2                            91315
         EJECT
**********************************************************************
*                                                                    *
*        ROUTINE TO PROCESS TAKE COMMAND                             *
*                                                                    *
**********************************************************************
DOTAKE   SUBENT ,            SHOW LOCAL OPTIONS               ESP88150
         SLR   R4,R4         SIGNAL NO TAKE WORK AREA GOTTEN     94069
         BXH   R7,R8,DOTAKE0 MORE OPERANDS ?                  ESP88150
         L     R6,0(,R7)     GET NEXT TOKEN                   ESP88150
         CLI   0(R6),C' '    END OF INPUT ?                   ESP88150
         BE    DOTAKE0       YES; PROMPT FOR DSN              ESP88150
         CLI       0(R6),C'?'          NEED HELP ?
         BNE   DOTAKEN       NO; PARSE NAME, ETC.             ESP88150
DOTAKEH  WTERM  ' TAKE REQUIRES A DSN OPERAND OR A DSN(MEMBER) -'
         WTERM  '   the file contents will be taken as KERMIT commands'
         WTERM  '   and data. Seven levels of nesting are supported.'
         B     DOTAKRET      RETURN                           ESP88277
DOTAKEN  CLI   NUMTAKEN,7    MAXIMUM STACK DEPTH REACHED ?    ESP88277
         BL    DOTAKEP       NO; GETMAIN AND PARSE            ESP88277
         WTERM  ' TAKE may be nested only 7 levels deep.'     ESP88277
         B     DOTAKRET                                       ESP88277
DOTAKEP  SLR   R2,R2                                          ESP88277
         IC    R2,NUMTAKEN   GET THE STACK INDEX              ESP88277
         LA    R2,1(,R2)     SET FOR NEXT FILE OFFSET            91112
         SLL   R2,2          MAKE INDEX TO WORK AREA          ESP88277
         LA    R5,TAWSIZE    LOAD WORK AREA SIZE              ESP88277
         L     R4,MISTAKEN(R2)  LOAD WORK AREA ADDRESS        ESP88277
         LTR   R4,R4         ANY ?                            ESP88277
         BP    DOTAKEW       YES                              ESP88277
         USING TAWSECT,R4                                     ESP88277
         GETMAIN R,LV=(R5)   GET IT                           ESP88277
         LR    R4,R1         COPY IT                          ESP88277
         ST    R1,MISTAKEN(R2)  REMEMBER IT                   ESP88277
DOTAKEW  CLRL  (R4),(R5)     CLEAR THE AREA                      94069
         MVC   TAWDDN(PATDDNLN),PATDDN  MOVE PATTERN          ESP88277
         MVC   TAWMASK,NUMTAKEN  MOVE OLD FILE NUMBER         ESP88277
         TR    TAWMASK,=AL1(1,2,4,8,16,32,64,128)             ESP88277
         LA    R15,TAWDDN    POINT TO EVENTUAL DD NAME        ESP88277
         ST    R15,TAWPTR    MAKE IKJPARSE-STYLE POINTER      ESP88277
         MVI   5+TAWPTR,8    SET DDNAME LENGTH                ESP88277
         MVI   6+TAWPTR,X'80'  SET DDN PRESENT                ESP88277
         L     R6,0(,R7)     LOAD DSN START                   ESP88277
DOTAPARS LA    R0,TAWPTR     POINT TO OUTPUT                  ESP88277
         SUBCALL DSNPARSE,TAWDSN  SET OUTPUT                  ESP88277
         B     *+4(R15)      BRANCH ON RETURN CODE            ESP88277
         B     DOTAKEOP    0 TRY TO OPEN                      ESP88277
         B     DOTALONG    4 DSN MALFORMED                    ESP88277
         SPACE 1                                              ESP88277
DOTALONG WTERM  ' DSName malformed'                           ESP88277
DOTAKE0  WTERM  ' Enter name of dataset containing commands.' ESP88277
         SUBCALL TTGETC      GET A NAME                       ESP88277
         BXH   R15,R15,DOTAKERR  TGET FAILED                  ESP88277
         LA    R6,INPUT      POINT TO INPUT                   ESP88277
         CLC   INPUT,INPUT+1  ANYTHING ?                         93239
         BE    DOTAKEH       NO; JUST PRINT HELP INFO            93239
         LTR   R4,R4         WORK AREA ALREADY GOTTEN ?          94069
         BZ    DOTAKEN       NO; GO GET ONE                      94069
         B     DOTAPARS                                       ESP88277
         SPACE 1                                              ESP88277
DOTAKERR ABEND 2741          TGET IN ERROR                    ESP88277
         SPACE 1                                              ESP88277
*    THE DSNAME ACCESS CHECK DONE HERE LACKS THE VOLUME SERIAL TO BE
*      ACCESSED, BUT SHORTENS THE CHECKING.                      92082
DOTAKEOP TM    MODEFG,FGNET  MULTI-TASKING MODE ?                92082
         BZ    DOTAKEOF      NO                                  92082
         L     R14,FAKECPPL+8  GET MTS ADDRESS BACK              92082
         XC    MTSDDB-MTS(8,R14),MTSDDB-MTS(R14)                 92082
         LA    R0,TAWDSN     POINT TO DSNAME                     92082
         ST    R0,MTSDDB+4-MTS(,R14)                             92082
         MVI   MTSDDB+3-MTS(R14),X'01'  SET INPUT, DSN MODE      92082
         LA    R1,MTSDDB-MTS(,R14)                               92082
         ICM   R15,15,MTSDDACC-MTS(R14)  ACCESS CHECK ?          92082
         BZ    DOTAKEOF      NO; SKIP                            92082
         BALSR R14,R15       CHECK USER'S PRIVILEGES             92082
         BXLE  R15,R15,DOTAKEOF  ACCESS GRANTED; PROCEED         92082
         WTERM ' TAKE file denied by security exit.'             92082
         B     DOTAKRET                                          92082
DOTAKEOF SUBCALL DSNALLOC,TAWDSN  TRY TO ALLOCATE IT          ESP88277
         BXLE  R15,R15,DOTAOPEN  OPEN AND READ                ESP88277
         CH    R15,=H'8'     RC > 4 ?                         ESP88278
         BNH   DOTAKRET      NO; MESSAGE ALREADY ISSUED       ESP88278
DOTALLOC WTERM  ' TAKE file allocation failed.'               ESP88277
         B     DOTAKRET      RETURN                           ESP88277
         SPACE 1                                              ESP88277
DOTAOPEN INPOPEN TAWDDN,DEV==TAWMASK  OPEN INPUT FILE         ESP88277
         BXLE  R15,R15,DOTAOPND  OK                           ESP88277
DOTAFAIL WTERM  ' TAKE file OPEN failed.'                     ESP88277
         SUBCALL DDFREE,TAWSECT  FREE ALLOCATION                 92082
         B     DOTAKRET                                       ESP88277
         SPACE 1                                              ESP88277
DOTAOPND SLR   R2,R2                                          ESP88277
         IC    R2,NUMTAKEN                                    ESP88277
         LA    R2,1(,R2)     SET NEXT FILE NUMBER             ESP88277
         STC   R2,NUMTAKEN                                    ESP88277
         STC   R2,TAWNUM     ALSO SAVE FILE NUMBER            ESP88277
         SPACE 1                                              ESP88277
DOTAKRET SUBEX ,                                              ESP88277
         DROP  R4                                             ESP88277
PATDDN   INPWORK INTAKE,,WIDTH=0,EDIT=128,EODAD=TTGETEOF      ESP88277
PATDDNLN EQU   *-PATDDN      LENGTH OF PATTERN                ESP88277
.NOTAKE2 AIF   (NOT &PRINTER).NODELT2                            91315
         EJECT ,                                                     *
**********************************************************************
*                                                                    *
*    ROUTINE TO SCRATCH (DELETE) A DATASETMEMBER,VOLSER          *
*        FOR DETAIL PARAMETER INFORMATION, CONSULT LOCAL 'DELETER'   *
*        WRITE-UP.                                                   *
*                                                                    *
**********************************************************************
DODELETE SUBENT ,            SCRATCH A DATASET                   91084
         BXH   R7,R8,DODEL0  NO OPERANDS - PROMPT                91084
         L     R6,0(,R7)     GET NEXT TOKEN                      91084
         CLI   0(R6),C' '    END OF INPUT ?                      91084
         BE    DODEL0        YES; PROMPT                         91084
         CLI   0(R6),C'?'    HELP ?                              91084
         BNE   DODELET       NO                                  91084
DODELH   WTERM ' SCRATCH(DELETE) REQUIRES A DSN SPECIFICATION -' 91084
         WTERM '   Entire cataloged dataset:   DEL datasetname'  91084
         WTERM '   Entire dataset on volume:   DEL datasetname,volser'
         WTERM '   Member of Partitioned DS:   DEL datasetname(member)'
         B     DODELEXT      RETURN TO USER                      91084
         SPACE 1                                                 91084
DODELET  LA    R4,SDAT+256+7 GET A WORK AREA                     91091
         SRL   R4,3          DIVIDE BY 8                         91084
         SLL   R4,3          NOW HAVE ADDRESS OF DOUBLE-WORD BOUNDARY
         USING SCRSECT,R4    DECLARE IT                          91084
         XC    SCRSECT(SCRWORKL),SCRSECT  CLEAN IT OUT           91084
DODELPAR LA    R1,SCRDSN     POINT TO START OF DSN               91084
         SUBCALL DSNPARSE    PARSE DS NAME                       91084
         BXH   R15,R15,DODEBADN  BAD NAME SYNTAX                 91084
         TM    MODEFG,FGNET  MULTI-TASKING MODE ?                91084
         BZ    DODELACF      NO                                  91084
         L     R14,FAKECPPL+8  GET MTS ADDRESS BACK              91084
         XC    MTSDDB-MTS(8,R14),MTSDDB-MTS(R14)                 91084
         LA    R0,SCRDSN     POINT TO DATASET NAME               91084
         ST    R0,MTSDDB+4-MTS(,R14)                             91084
         MVI   MTSDDB+3-MTS(R14),X'81'  SET OUTPUT MODE/DSN PASSED
         LA    R1,MTSDDB-MTS(,R14)                               91084
         ICM   R15,15,MTSDDACC-MTS(R14)  ACCESS CHECK ?          91084
         BZ    DODELACF      NO; SKIP                            91084
         BALSR R14,R15       CHECK USER'S PRIVILEGES             91084
         BXLE  R15,R15,DODELACF  ACCESS FAILED                   91084
         WTERM ' Access denied.'                                 91084
         B     DODELEXT      QUIT                                91084
DODELACF LA    R3,44         SET DATASET NAME LENGTH             91084
         LA    R2,SCRDSN+44  POINT TO END OF NAME                91084
DODELNLP BCTR  R2,0          GET PREVIOUS BYTE                   91084
         CLI   0(R2),C' '    TRAILING BLANK ?                    91084
         BNE   DODELHLN      NO; HAVE LENGTH IN R3               91084
         BCT   R3,DODELNLP   TRY AGAIN                           91084
         SPACE 1                                                 91084
DODEBADN WTERM  ' DSName malformed'                              91084
DODEL0   WTERM  ' Enter name of dataset to be deleted.'          91084
         SUBCALL TTGETC      GET A NAME                          91084
         BXH   R15,R15,DODELERR  TGET FAILED                     91084
         LA    R6,INPUT      POINT TO INPUT                      91084
         CLC   INPUT,INPUT+1  ANY ?                              93239
         BE    DODELH                                            93239
         B     DODELPAR                                          91084
         SPACE 1                                                 91084
DODELERR ABEND 2741          TGET IN ERROR                       91084
         SPACE 1                                                 91084
DODELHLN CLI   SCRDSN+44,C' '  IS THERE A MEMBER NAME ?          91084
         BNH   DODELNMB      NO                                  91084
         MVI   1(R2),C'('                                        91084
         MVC   2(8,R2),SCRDSN+44  MOVE MEMBER                    91084
         LA    R2,10(,R2)    POINT PAST                          91084
         LA    R3,10(,R3)    ADJUST LENGTH                       91084
DODELBLP BCTR  R2,0          POINT TO LAST BYTE                  91084
         CLI   0(R2),C' '    TRAILING BLANK ?                    91084
         BH    DODELTPR      NO; ADD PARENTHESIS                 91084
         BCT   R3,DODELBLP                                       91084
DODELTPR MVI   1(R2),C')'    MAKE END OF MEMBER                  91084
DODELNMB BXH   R7,R8,DODELNVL  SKIP IF NO VOLUME                 91084
         L     R6,0(,R7)     GET SERIAL ADDRESS                  91084
         CLI   0(R6),C' '    END OF SCAN ?                       91084
         BE    DODELNVL      YES; NO SERIAL                      91084
         MVI   1(R2),C','    MAKE SEPARATOR                      91084
         MVC   2(6,R2),0(R6)   TACK IT ON                        91084
         LA    R3,7(,R3)     ADJUST LENGTH                       91084
DODELNVL STH   R3,SCRDSNL    MAKE LENGTH OF PARM FIELD           91084
         LA    R1,SCRDSNL    GET LOCATION OF PARM                91084
         ST@   R1,SCRPARM,MVI=X'80'  MAKE PARM POINTER           91084
         LA    R1,SCRPARM    POINT TO PARM                       91084
         LA    R2,SCRECB                                         91084
         ATTACH EP=DELETNP,SF=(E,SCRATT),ECB=(R2)                91084
         ST    R1,SCRTASK                                        91084
         WAIT  ,ECB=SCRECB   WAIT FOR COMPLETION                 91084
         DETACH SCRTASK      GET RID OF IT                       91084
         SLR   R1,R1                                             91084
         ICM   R1,7,SCRECB+1 GET THE COMPLETION CODE             91084
         CH    R1,=H'4095'   ABEND ?                             91084
         BH    DODELABN      YES                                 91084
         CH    R1,=H'1'      GOOD ?                              91084
         BL    DODELGUD      YES                                 91084
         BE    DODELGON      NOT FOUND                           91084
         MVC   WRKBUFF(20),=C' Request failed - RC'              91084
         MVC   WRKBUFF+20(5),=X'402020202120'                    91084
         CVD   R1,SCRDB      CONVERT                             91084
         ED    WRKBUFF+20(5),SCRDB+5                             91084
         WTERM WRKBUFF,25                                        91084
         B     DODELEXT                                          91084
DODELGON WTERM ' Not done - requested name not found.'           91084
         B     DODELEXT                                          91084
DODELGUD WTERM ' Request completed.'                             91084
         B     DODELEXT                                          91084
DODELABN MVC   WRKBUFF(21),=C' Request abended - CC'             91084
         SRL   R1,12                                             91084
         STH   R1,SCRDB                                          91084
         UNPK  WRKBUFF+21(5),WRKBUFF(3)                          91084
         TR    WRKBUFF+22(3),TRHEXTAB                            91084
         MVI   WRKBUFF+21,C' '                                   91084
         WTERM WRKBUFF,25                                        91084
DODELEXT SUBEX ,             RETURN                              91084
         DROP  R4                                                91084
         SPACE 1                                                 91084
SCRSECT  DSECT ,                                                 91084
SCRDB    DS    D                                                 91084
SCRTASK  DS    F                                                 91084
SCRECB   DS    F                                                 91084
SCRPARM  DS    A                                                 91084
SCRDSNL  DS    H                                                 91084
SCRDSN   DS    CL(44+1+8+1+1+6)  DSN/MEM/VOL                     91084
SCRATT   ATTACH EP=DELETNP,SF=L                                  91084
         DS    2F            JUST IN CASE ?                      91084
SCRWORKL EQU   *-SCRSECT     SIZE TO CLEAR                       91084
KERMIT   CSECT ,             RESTORE                             91084
.NODELT2 EJECT ,
**********************************************************************
*                                                                    *
*        ROUTINE TO PARSE COMMANDS AND CREATE PARSE TABLE            *
*                                                                    *
**********************************************************************
PARSER   SUBENT ,                                             ESP88150
         LR        R3,R0               R3 = TEXT LENGTH
         BCTR      R1,0                R1 ==> BYTE BEFORE PARM
         LA        R3,0(R1,R3)         R3 ==> END OF LINE
         LA    R9,1(,R3)     SET END FOR SINGLE OR NO OPERAND ESP85142
         LA        R2,1                R2 = PARSING INCREMENT
         LA        R5,PTRTBL           R5 ==> TARGET AREA
         LA        R6,4                R6 = POINTER INCREMENT
         STM       R5,R6,PARSELST      SAVE FOR PARSING
         LA        R7,PTRTBL+PTRTBLL-4 R7 ==> END OF TARGET
*
SCNTOKEN BXH       R1,R2,SCNFINIS      SCAN FOR PARM START
         CLI       0(R1),C' '          FOUND A BLANK?
         BE        SCNTOKEN            YES, THEN KEEP LOOKING
         ST        R1,0(,R5)           SAVE PTR TO OPERAND
         BXH       R5,R6,SCNFINIS      BR ON END OF TARGET AREA
SCNLASTC BXH       R1,R2,SCNFINIS      SCAN TO END OF OPERAND
         CLI   0(R1),C'='    VARIABLE=VALUE ?                 ESP88204
         BE    SCNLASTB      YES; CHANGE TO BLANK SEPARATOR   ESP88204
         CLI       0(R1),C' '          IS THIS BLANK AT END OF OPERAND
         BNE       SCNLASTC            IF SO, MOVE TOKEN
SCNLASTB MVI   0(R1),C' '    REPLACE '=' BY ' '               ESP88204
         LR        R9,R1               REMEMBER JUST AFTER OPERAND
         B         SCNTOKEN            FIND START OF NEXT OPERAND
SCNFINIS MVI       0(R9),C' '          MARK THE END OF OPERANDS
         ST        R9,0(R5)            SAVE POINTER TO END
         ST        R5,PARSELST+8       SAVE END TARGET
         SUBEX ,             RESTORE THE REGISTERS AND RETURN ESP88150
         EJECT ,                                              ESP88150
***********************************************************************
*                                                                     *
*        (TRUNCATED) SERVER FUNCTIONS - UNSOLICITED SOH COMES         *
*              TO THIS ROUTINE.                                       *
*        ...I IS PC'S GET INITIALIZATION                              *
*        ...S IS PC'S SEND INITIALIZATION                             *
*              INITIALIZATION LOGIC IS REVERSE OF HOST'S.             *
*                                                                     *
***********************************************************************
         SPACE 1                                              ESP88150
SETSERV  OI    FLAGS,FGSERVER  PLACE INTO SERVER MODE         ESP88150
SERVER   SUBENT ,                                             ESP88150
         MVI   STATE,C'S'    CHEAT A LITTLE                   ESP88150
         NI    IOFLAGS,255-FGOUT  INPUT ONLY                  ESP88150
         TM    FLAGS,FGSERVER SERVER MODE ?                   ESP88150
         BNZ   SVACK0        YES; ACK                         ESP88150
         LR    R3,R0         COPY AND SAVE INPUT LENGTH       ESP88150
         SLR   R6,R6         GET ZERO                         ESP88150
         ST        R6,NUMTRY           ZERO THIS OUT          ESP88150
         ST        R6,N                HERE TOO               ESP88150
         LR    R15,R3        ALSO PASS LENGTH TO RPACK        ESP88150
         L     R1,=A(RPACK)  GET READ PACKET ADDRESS          ESP88150
         BALS  R14,RPACKGOT-RPACK(,R1)  CALL PACKET CONVERSION SP88150
SVSERV   IC    R0,RTYPE      GET RESPONSE                     ESP88150
         SUBCALL CHARLOOK,SRVTAB0  GET ENTRY TABLE            ESP88150
SRVTAB0  CHARB AE,SVABORT    ERROR PACKET                     ESP88150
         CHARB AN,SVNAK      NACK-NACK                        ESP88150
         CHARB AS,SVINIT                                      ESP88150
         CHARB AI,SVINIT                                      ESP88150
         CHARB AR,SVFR       JUMP TO FILE RECEIVE             ESP88150
         CHARB AG,SVSCMD     SERVER COMMAND                   ESP88150
         CHARB AC,SVHCMD     HOST COMMAND                     ESP88150
         CHARB END,SVNAK     NACK                             ESP88150
         SPACE 1                                              ESP88150
**********************************************************************
*        PROCESS INITIALIZATION PACKET                               *
**********************************************************************
SVINIT   IC    R0,RTYPE      GET RECEIVED PACKET TYPE         ESP88150
         SUBCALL CHARLOOK,SVITAB  GET BRANCH TABLE            ESP88150
SVITAB   CHARB AS,SVIR       PC SEND ?                        ESP88150
         CHARB AR,SVFR       JUMP TO FILE RECEIVE             ESP88150
         CHARB AI,SVII       PC RECEIVE (OR COMMAND OR ...)   ESP88150
         CHARB AN,SVNAK      NACK-NACK                        ESP88150
         CHARB AG,SVSCMD     SERVER COMMAND                   ESP88150
         CHARB AC,SVHCMD     HOST COMMAND                     ESP88150
         CHARB END,SVELSE    ELSE ILLEGAL                     ESP88150
         SPACE 1                                              ESP88150
SVIR     MVI   STATE,C'R'    PC'S 'S' IS OUR 'R'              ESP88150
         OI    IOFLAGS,FGOUT  OUTPUT MODE                     ESP88150
         B     SVICOM        GO TO COMMON                     ESP88150
SVII     NI    IOFLAGS,255-FGOUT  INPUT ONLY                  ESP88150
         MVI   STATE,C'S'    PC'S 'I' IS OUR 'S'              ESP88150
SVICOM   MVC   SERVFUN,RTYPE REMEMBER THE REQUEST TYPE        ESP88150
         SUBCALL RINDATA     PROCESS INITIALIZATION DATA      ESP88150
         SUBCALL SINDATA     SEND NEGOTIATED DATA             ESP88150
         SUBCALL SPACK       SEND A PACKET                    ESP88150
         CLI       STATE,C'A'                                 ESP88150
         BE    SVABORT                                        ESP88150
         MVI       STATE,C'F'          SET TO RECEIVE FILE STATE 88150
         MVC   OLDTRY,NUMTRY    SAVE TRIAL COUNTER            ESP88150
         XC        NUMTRY,NUMTRY       RESET COUNTER TO ZERO  ESP88150
         LA    R3,1          ADVANCE PACKET NUMBER            ESP88150
         ST    R3,NUM                                         ESP88150
         ST    R3,N          RESET                            ESP88150
         CLI   SERVFUN,AS    PC SEND MODE ?                   ESP88150
         BE    VGETPUT       YES; AWAIT FILE HEADER           ESP88150
         SLR   R3,R3                                          ESP88150
         ST    R3,NUM                                         ESP88150
         ST    R3,N          RESET                            ESP88150
         B         VLOOP                                      ESP88150
         SPACE 1                                              ESP88150
**********************************************************************
*        PROCESS FILE PACKET                                         *
**********************************************************************
SRVGET   CLC   NUMTRY,MAXTRY EXCEEDED NO. OF TRIALS ALLOWED?  ESP88150
         BNL   SVABORT       YES; ABORT                       ESP88150
         INC   NUMTRY        INCREMENT TRIAL COUNTER          ESP88150
         SUBCALL RPACK       GET A PACKET                     ESP88150
         CLI   RTYPE,AE      ERROR PACKET?                    ESP88150
         BNE   VLOOP2        NO; BRANCH BY STATE              ESP88150
         MVI   ERRNUM,ERCBDIED  MICRO DIED                    ESP88150
         B     SVABORT       ABORT                            ESP88150
         SPACE 1                                              ESP88150
SVFILE   IC    R0,RTYPE      LOAD PACKET TYPE                 ESP88150
         SUBCALL CHARLOOK,SRFTAB  LOAD FILE DECISION TABLE    ESP88150
SRFTAB   CHARB AS,SVFIGN     IGNORE REPEATED S                ESP88150
         CHARB AI,SVFIGN     IGNORE REPEATED I                ESP88150
         CHARB AZ,SVFZ       EOF                              ESP88150
         CHARB AF,SVFF       FILE HEADER                      ESP88150
         CHARB AR,SVFR       FILE HEADER (RECEIVE)            ESP88150
         CHARB AB,SVFB       BREAK                            ESP88150
         CHARB AN,SVNAK      NACK-NACK                        ESP88150
         CHARB AG,SVSCMD     SERVER COMMAND                   ESP88150
         CHARB AC,SVHCMD     HOST COMMAND                     ESP88150
         CHARB END,SVELSE    FAIL ALL ELSE                    ESP88150
         SPACE 1                                              ESP88150
SVFIGN   CLC   OLDTRY,MAXTRY MAY WE TRY AGAIN?                ESP88150
         BNL   SVABORT       NO; ABORT                        ESP88150
         INC   OLDTRY        INCREASE TRIAL COUNTER           ESP88150
         SLR   R3,R3                                          ESP88150
         ST    R3,N                                           ESP88150
         ST    R3,NUM        RESET                            ESP88150
SVACK6   XC    LSDAT,LSDAT   CLEAR LENGTH                        91311
         SUBCALL SINDATA     REBUILD CAPABILITIES PACKET         91311
         B     SVACKC        GO TO COMMON                     ESP88150
SVACK0   XC    LSDAT,LSDAT   CLEAR SEND DATA LENGTH           ESP88150
SVACKC   MVI   STYPE,AY      ACK PACKET                       ESP88150
         SUBCALL SPACK       SEND A PACKET                    ESP88150
         CLI   STATE,C'A'    OK ?                             ESP88150
         BE    SVABORT       NO                               ESP88150
         XC    NUMTRY,NUMTRY RESET COUNTER TO ZERO            ESP88150
         B     VLOOP                                          ESP88150
         SPACE 1                                              ESP88150
SVFZ     CLC   OLDTRY,MAXTRY MAY WE TRY AGAIN?                ESP88150
         BNL   SVABORT       NO                               ESP88150
         INC   OLDTRY        INCREMENT TRIAL COUNTER          ESP88150
         B     SVACK0        SEND ACK PACKET                  ESP88150
         SPACE 1                                              ESP88150
SVFR     NI    IOFLAGS,255-FGOUT  INPUT ONLY                  ESP88150
         MVI   STATE,C'F'    FOR SHORT-CUT ENTRY              ESP88150
SVFF     CLC   NUM,N         THEY HAVE TO BE EQUAL            ESP88150
         BE    VNUM3                                          ESP88150
SVMISS   MVI   ERRNUM,ERCBMISS  PREVIOUS PACKET MISSING       ESP88150
         B     SVNAK         SEND A NAK                       ESP88150
         SPACE 1                                              ESP88150
VNUM3    LA    R0,RDAT       POINT TO FILENAME                ESP88150
         L     R1,LRDAT      GET LENGTH OF NAME               ESP88150
SVFRCOM  LA    R14,INPUT                                      ESP88150
         LA    R15,L'INPUT-1  INTENDED DSNAME                 ESP88150
         ICM   R1,8,ABLANKS  FILL BYTE IS ASCII BLANK         ESP88150
         MVCL  R14,R0        COPY AND PAD DSNAME              ESP88150
         L     R15,@MAKEBC   GET EBCDIC TRANSLATION              91315
         TR    INPUT(L'INPUT-1),0(R15)  MAKE EBCDIC              91315
         TR    INPUT(L'INPUT-1),UPPER   UPPER-CASE DSNAME        91315
         SUBCALL FREEFILE    FREE PREVIOUS FILE               ESP88150
         LA    R6,INPUT      PASS START OF NAME               ESP88150
         SUBCALL FILEMAKE    BUILD AND ALLOCATE FILE          ESP88150
         CLI   STATE,C'A'    REQUEST FAILED ?                 ESP88150
         BE    SVABORT       YES; QUIT                        ESP88150
         OI    FLAGS,FGHAZED SHOW INITIALIZED                 ESP88150
         CLI   SERVFUN,AS    SENDING DATA ?                   ESP88150
         BNE   VGETPUT       YES; STATE=F SENDS 'F' PACKET    ESP88150
         MVI       STYPE,AY            ACK PACKET             ESP88150
         XC        LSDAT,LSDAT         NO DATA                ESP88150
         SUBCALL SPACK                                        ESP88150
         CLI       STATE,C'A'                                 ESP88150
         BE    SVABORT                                        ESP88150
         MVC   OLDTRY,NUMTRY    KEEP NUMTRY FOR LATER         ESP88150
         XC        NUMTRY,NUMTRY       RESET TO ZERO          ESP88150
         MVI       STATE,C'D'          DATA RECEIVE STATE     ESP88150
         B     VGETPUT       START DATA TRANSFER              ESP88150
         SPACE 1                                              ESP88150
SVFB     CLC       NUM,N                                      ESP88150
         BNE   SVMISS        NO; PACKET MISSING               ESP88150
         MVI       STYPE,AY            ACK PACKET             ESP88150
         XC        LSDAT,LSDAT         NO DATA                ESP88150
         SUBCALL SPACK                                        ESP88150
         CLI       STATE,C'A'                                 ESP88150
         BE    SVABORT                                        ESP88150
         MVI       STATE,C'C'          COMPLETE STATE         ESP88150
         B     VLOOP         QUIT ?                           ESP88150
         SPACE 1                                              ESP88150
SVNAK    MVI   STYPE,AN      SEND A NAK PACKET                ESP88150
         XC    LSDAT,LSDAT   NO DATA                          ESP88150
         SUBCALL SPACK                                        ESP88150
*        B     VLOOP         DO NOTHING ON A NAK              ESP88150
         SPACE 1                                              ESP88150
VLOOP    IC    R0,STATE      LOAD THE STATE STATE             ESP88150
         SUBCALL CHARLOOK,SRVTAB1  GET PRIMARY STATE TABLE    ESP88150
VLOOP2   IC    R0,STATE      GET CURRENT STATE                ESP88150
         SUBCALL CHARLOOK,SRVTAB2  GET SECONDARY STATE TABLE  ESP88150
         SPACE 1                                              ESP88150
SRVTAB1  CHARB C'F',SRVGET   GET A RESPONSE PACKET            ESP88150
         CHARB C'R',SRVGET   GET A RESPONSE PACKET            ESP88150
         CHARB C'S',SRVGET   GET A RESPONSE PACKET            ESP88150
         CHARB C'V',SRVGET   GET A SERVER COMMAND             ESP88150
SRVTAB2  CHARB C'F',SVFILE   PROCESS FILE HEADER              ESP88150
         CHARB C'R',SVINIT   PROCESS INITIALIZATION           ESP88150
         CHARB C'S',SVINIT   PROCESS INITIALIZATION           ESP88150
         CHARB C'V',SVSERV   SERVER COMMAND                   ESP88150
         CHARB C'A',SVABORT  ABORT STATE                      ESP88150
         CHARB C'B',SERVIFF  BREAK OFF                        ESP88150
         CHARB C'C',SERVIFF  COMPLETE                         ESP88150
         CHARB END,SVABORT   QUIT WITH PREJUDICE              ESP88150
         SPACE 1                                              ESP88150
***********************************************************************
*                                                                     *
*        SERVER COMMAND PROCESSING                                    *
*                                                                     *
***********************************************************************
SVSCMD   IC    R0,RDAT       GET REQUEST BYTE                 ESP88150
         SUBCALL CHARLOOK,SVCTAB                              ESP88150
         SPACE 1                                              ESP88150
SVCTAB   CHARB AF,SVCQUIT    EXIT FROM SERVER MODE (ONLY)     ESP88150
         CHARB AL,SVCBYE     EXIT, TERMINATE AND LOGOFF       ESP88150
         CHARB AT,SVCTYPE    TYPE A FILE                      ESP88150
* LOTS OF OTHER DEFINED FUNCTIONS NOT (YET?) IMPLEMENTED      ESP88150
         CHARB END,SVCNOSUP  FUNCTION NOT SUPPORTED           ESP88150
         SPACE 1                                              ESP88150
SVCNOSUP MVI   ERRNUM,ERCNOSUP  SET UNSUPPORTED               ESP88150
         B     SVABORT       AND SEND                         ESP88150
         SPACE 1                                              ESP88150
SVHCMD   EQU   SVCNOSUP      HOST COMMAND PROCESSING NOT DONE ESP88150
         SPACE 1                                              ESP88150
SVCBYE   SUBCALL SYSBYE      SET LOGOFF REQUEST               ESP88150
SVCQUIT  NI    FLAGS,255-FGSERVER  NO LONGER IN SERVER MODE   ESP88150
         MVI   STATE,C'C'    SET COMPLETE STATE               ESP88150
         B     SVACK0        ACKNOWLEDGE AND EXIT VIA VLOOP   ESP88150
         SPACE 1                                              ESP88150
SVCTYPE  LA    R0,RDAT+2     POINT TO PRESUMED FILENAME       ESP88150
         SLR   R1,R1                                          ESP88150
         IC    R1,RDAT+1     GET LENGTH                       ESP88150
         S     R1,SPACE      ADJUST                           ESP88150
         BNP   SVCBDNAM      SET INVALID FILENAME             ESP88150
         OI    IOFLAGS,FGTYPE  SET REQUEST TO 'TYPE'          ESP88150
         MVI   SERVFUN,AX    SET FOR INFO                     ESP88150
         MVI   STATE,C'F'    FOR SHORT-CUT ENTRY              ESP88150
         B     SVFRCOM       GO TO COMMON FILE PROCESSING     ESP88150
SVCBDNAM MVI   ERRNUM,ERCBDSN  SET BAD DSN                    ESP88150
         B     SVABORT       AND RETURN ERROR PACKET          ESP88150
         SPACE 1                                              ESP88150
SVELSE   MVI   ERRNUM,ERCBTYPE  ILLEGAL PACKET TYPE           ESP88150
*        B     SVABORT                                        ESP88150
         SPACE 1                                              ESP88150
SVABORT  SUBCALL SERRPACK    SEND ERROR PACKET                ESP88150
         LA    R15,4         SET NON-ZERO RETCODE             ESP88150
         CLI   ERRNUM,ERCBDIED  MICRO GONE BYE-BYE ?          ESP88150
         BE    SERVRET       YES; RETURN                      ESP88150
         TM    FLAGS,FGSERVER  FORMAL SERVER MODE ?           ESP88150
         BNZ   SERVSTAY      YES; STAY IN HERE                ESP88150
         B     SERVRET       PREPARE TO LEAVE                 ESP88150
         SPACE 1                                              ESP88150
VGETPUT  L     R15,=A(RECEIVE)  PC SENDING A FILE             ESP88150
         CLI   SERVFUN,AS    PC GET ?                         ESP88150
         BNE   VPUTPUT       YES                              ESP88150
         NI    FLAGS,255-FLG1  NEED FILE ALLOCATION           ESP88150
         OI    FLAGS,FGHAZED SET INITIALIZED                  ESP88150
         B     VGETCOM                                        ESP88150
VPUTPUT  L     R15,=A(SEND)  YES; SEND                        ESP88150
         CLI   SERVFUN,AX    TYPE REQUESTED ?                 ESP88150
         BE    VGETCOM       YES; LEAVE IN 'F' STATE          ESP88150
         MVI   STATE,C'S'    REVERT TO SEND INIT STATE        ESP88150
         NI    FLAGS,255-FGHAZED                              ESP88150
VGETCOM  SUBCALL (R15)       PROCESS IT                       ESP88150
         MVI   OLDERR,X'FF'  SET NO ERROR                     ESP88150
         BXLE  R15,R15,VGETCAME COMPLETE WITHOUT ERROR        ESP88150
         MVC   OLDERR,ERRNUM    ERROR SETTING OF THIS RUN     ESP88150
VGETCAME CLI   STATE,C'F'    READY FOR ANOTHER FILE ?         ESP88150
         BE    VLOOP         YES                              ESP88150
*        B     SERVIFF       TEST FOR SERVER MODE             ESP88150
SERVIFF  TM    FLAGS,FGSERVER  INVOKED WITH 'SERVER' COMMAND? ESP88150
         BZ    SERVOUT       NO                               ESP88150
SERVSTAY MVI   STATE,C'V'    SET SERVER                       ESP88150
         XC    NUMTRY,NUMTRY  RESET ERROR COUNTER             ESP88307
         MVC   OLDERR,ERRNUM  PRESERVE ERROR CODE             ESP88150
         MVI   ERRNUM,X'FF'  RESET ERROR NUMBER               ESP88150
         NI    FLAGS,255-FGHAZED  RE-INIT REQUIRED            ESP88150
         TM    MODEFG,FGNET  MULTI-TASKING MODE ?                90231
         BZ    VLOOP         NO; LOOK FOR ORDERS                 90231
         L     R14,FAKECPPL+8  GET MTS ADDRESS                   90231
         TM    MTSFLGT-MTS(R14),MTSFGOIN+MTSFGONE+MTSFXEND  QUITTING ?
         BZ    VLOOP         NO; GO AWAIT ORDERS                 90231
SERVOUT  SLR   R15,R15       SET GOOD RETURN                  ESP88150
SERVRET  SUBEX RC=(R15)      BACK-UP                          ESP88150
         SPACE 1                                              ESP88150
*        ERROR PACKET PROCESSING                              ESP88150
SERRPACK SUBENT ,            NO PARAMETERS (ERCODE IN ERRNUM) ESP88150
         MVI   STATE,C'A'    JUST IN CASE                     ESP88150
         SLR   R15,R15       PROVISIONALLY SET NO ERROR       ESP88150
         CLI   ERRNUM,ERCBDIED  DID THE MICRO DIE?            ESP88150
         BE    SERRNERR      NO ERROR PACKET IF SO            ESP88150
         MVI   STYPE,AE      ERROR PACKET                     ESP88150
         LA    R3,L'ERRTAB   ERROR MESSAGE LENGTH             ESP88150
         ST    R3,LSDAT      SET DATA LENGTH                  ESP88150
         MVC   N,NUM         SYNCH PACKET NUMBERS             ESP88150
         SLR   R5,R5                                          ESP88150
         IC    R5,ERRNUM     GET MESSAGE NUMBER               ESP88150
         MR    R4,R3         CONVERT TO OFFSET                ESP88150
         A     R5,=A(ERRTAB) GET MESSAGE ADDRESS              ESP88150
         MVC   SDAT(L'ERRTAB),0(R5) SPACK NEEDS THE DATA HERE ESP88150
         L     R15,@MAKASC   GET TRANSLATION TABLE               91315
         TR    SDAT(L'ERRTAB),0(R15)  MAKE ASCII                 91315
         SUBCALL SPACK                                        ESP88150
SERRNERR SUBEX RC=(R15)      PASS ERROR CODE                  ESP88150
         SPACE 1                                              ESP88150
*        COMMON CODE TO MERGE RECEIVED INITIALIZATION STRING  ESP88150
*                                                             ESP88150
RINDATA  SUBENT ,            SAVE A BIT                       ESP88150
         XC    TMPDATA,TMPDATA  PRE-CLEAR                     ESP88150
         XC    QUOTABLE,QUOTABLE  RESET QUOTE TRT TABLE       ESP88150
         XC    GTCHTAB(128),GTCHTAB  RESET SEND TABLE         ESP88150
         MVI   GTCHTAB+127,8  QUOTE DELETE                    ESP88150
         MVI   GTCHTAB,8     QUOTE CONTROL CHARACTERS FROM NULL P88150
         MVC   GTCHTAB+1(31),GTCHTAB  THROUGH X'1F'           ESP88150
         MVC   RQUO,DQUOTE   SET DEFAULT QUOTE                ESP88150
         MVC   RCHECK,SCHECK GET CHECKSUM PREFERRENCE         ESP88150
         MVI   RQATE,0       NO 8TH-BIT PREFIXING             ESP88150
         MVI   RQREP,0       NO REPEAT PREFIX                 ESP88150
         MVI   CAPAFG,0      RESET CAPABILITIES                  91091
         ICM   R4,15,LRDAT   GET RECEIVED DATA LENGTH         ESP88150
         BNP   RINDBADL      BAD LENGTH                       ESP88150
         LA    R0,L'TMPDATA  LOAD INTERIM DATA HOLDER LENGTH  ESP88150
         CR    R4,R0         WILL IT FIT ?                    ESP88150
         BL    *+6           YES                              ESP88150
         LR    R4,R0         ELSE TRUNCATE                    ESP88150
         BCTR  R4,0          MAKE EXECUTE LENGTH              ESP88150
         EX    R4,RINDMVC    MOVE TO MY WORK AREA             ESP88150
         IC    R4,TMPDATA    GET FIRST CHARACTER              ESP88150
         S     R4,SPACE      UNCHAR                           ESP88150
         CH    R4,=H'&OPPAKMN'     MANDATORY MINIMUM LENGTH ?    91311
         BL    RINDBSIZ      NO; TOO BAD                      ESP88150
         CH    R4,MAXPACK    NOT TOO LARGE ?                  ESP88150
         BNH   RINDGSIZ      OK                               ESP88150
RINDBSIZ MVI   ERRNUM,ERCBPACK  BAD PACKET LENGTH             ESP88150
         B     RINDFAIL                                       ESP88150
RINDGSIZ ST    R4,SPSIZ      USE THE VALUE AS SEND SIZE       ESP88150
         SH    R4,=H'3'      ALLOW FOR OVERHEAD               ESP88150
         ST    R4,SIZE       SET MAXIMUM DATA LENGTH IN PACKET SP88150
         ICM   R4,1,TMPDATA+4  TEST SEND-EOL                  ESP88150
         BZ    RINDDONE      NO MORE                          ESP88150
         S     R4,SPACE      UNCHAR (SUBTRACT ' ')            ESP88150
         STC   R4,SEOL                                        ESP88150
         ICM   R4,1,TMPDATA+5  QUOTE CHARACTER ?              ESP88150
         BZ    RINDDONE      NO MORE                          ESP88150
         STC   R4,RQUO       YES; STASH IT                    ESP88150
         ICM   R4,1,TMPDATA+6  8TH-BIT PREFIXING ?            ESP88150
         BZ    RINDDONE      NO                               ESP88150
         CLM   R4,1,=AL1(AY) JUST 'YES' ?                     ESP88150
         BNE   *+8           NO                               ESP88150
         IC    R4,DRQATE     SET MY DEFAULT PREFIXING         ESP88150
         CLM   R4,1,=AL1(AA) IS IT ALPHABETIC ?               ESP88150
         BL    RINDNATA      NO                               ESP88150
         CLM   R4,1,=AL1(AZ)                                  ESP88150
         BNH   RINDNATE      YES; NO PREFIXING                ESP88150
RINDNATA CLM   R4,1,=AL1(A0) IS IT NUMERIC ?                  ESP88150
         BL    RINDNATN      NO                               ESP88150
         CLM   R4,1,=AL1(A9)                                  ESP88150
         BNH   RINDNATE      YES; NO PREFIXING                ESP88150
RINDNATN CLM   R4,1,ABLANKS  LARGER THAN SPACE ?              ESP88150
         BNH   RINDNATE      NO                               ESP88150
         CLM   R4,1,RQUO     SAME AS QUOTE ?                  ESP88150
         BE    RINDNATE      YES; FAIL                        ESP88150
         STC   R4,RQATE      YES; SET IT                      ESP88150
         LA    R3,GTCHTAB(R4)  GET SEND TRANSLATION           ESP88150
         MVI   0(R3),4       QUOTE NATIVE PREFIX CHAR IN DATA ESP88150
         LA    R4,QUOTABLE(R4)                                ESP88150
         MVI   0(R4),OFFEIGHT                                 ESP88150
RINDNATE SLR   R4,R4                                          ESP88150
         ICM   R4,1,TMPDATA+7  BLOCK CHECK ?                  ESP88150
         BZ    RINDDONE      NO                               ESP88150
         LA    R0,A2         TEST CHECKSUM = 2-BYTE           ESP88150
         CR    R4,R0         COMPARE INPUT TO 2               ESP88150
         BL    RINDBLK1      IF LOWER, USE 1-BYTE             ESP88150
         BE    *+8           2-BYTE                           ESP88150
         LA    R0,A3         ELSE USE 3                       ESP88150
         LR    R4,R0         USE 2-BYTE CHECK                 ESP88150
         B     RINDBLKM      SKIP AROUND                      ESP88150
RINDBLK1 LA    R4,A1         SET LOWEST COMMON DENOMINATOR    ESP88150
RINDBLKM STC   R4,RCHECK     1 OR 2 CHECK BYTES               ESP88150
         L     R0,SIZE       GET DATA SIZE                    ESP88150
         SR    R0,R4         SUBTRACT CHECKBYTE LENGTH        ESP88150
         LA    R4,A1         SET BASE VALUE                   ESP88150
         AR    R0,R4         MAKE TRUE MAXIMUM LENGTH         ESP88150
         ST    R0,SIZE       SET BACK                         ESP88150
         ICM   R4,1,TMPDATA+8  REPEAT PREFIX ?                ESP88150
         BZ    RINDDONE      NO                               ESP88150
         CLM   R4,1,=AL1(AA) IS IT ALPHABETIC ?               ESP88150
         BL    RINDRATA      NO                               ESP88150
         CLM   R4,1,=AL1(AZ)                                  ESP88150
         BNH   RINDTOPT      YES; NO PREFIXING                ESP88150
RINDRATA CLM   R4,1,=AL1(A0) IS IT NUMERIC ?                  ESP88150
         BL    RINDRATN      NO                               ESP88150
         CLM   R4,1,=AL1(A9)                                  ESP88150
         BNH   RINDTOPT      YES; NO PREFIXING                ESP88150
RINDRATN CLM   R4,1,ABLANKS  LARGER THAN SPACE ?              ESP88150
         BNH   RINDTOPT      NO                               ESP88150
         CLM   R4,1,RQUO     SAME AS QUOTE ?                  ESP88150
         BE    RINDTOPT      YES; FAIL                        ESP88150
         CLM   R4,1,RQATE    SAME AS 8-BIT PREFIX ?           ESP88150
         BE    RINDTOPT      YES; FAIL                        ESP88150
         STC   R4,RQREP      NO; SET IT                       ESP88150
         LA    R3,GTCHTAB(R4)                                 ESP88150
         MVI   0(R3),4       QUOTE NATIVE PREFIX CHARACTER    ESP88150
         LA    R4,QUOTABLE(R4)                                ESP88150
         MVI   0(R4),OFFREPIT                                 ESP88150
RINDTOPT ICM   R0,15,LRDAT   RESTORE PACKET DATA LENGTH          91311
         CH    R0,=H'9'      LONGER THAN MINIMUM ?               91311
         BH    RINDTLNG      YES; PROCESS EXTRA                  91311
         MVI   CAPAFG,0      KILL ALL EXTENDED CAPABILITIES      91311
         B     RINDDONE      BYPASS LONG PACKET CODE             91311
         SPACE 1                                                 91077
*    LONG PACKET EXTENSION PROCESSING                            91077
RINDTLNG MVC   CAPAFG,TMPDATA+9  GET CAPABILITIES                91091
         NI    CAPAFG,FGAPACK  LEAVE ONLY SUPPORTED OPTIONS      91091
         TM    TMPDATA+9,FGRLONG  IS LONG PACKET BIT ON ?        91091
         BZ    RINDDONE      NO; USE SHORT PACKETS               91077
         TM    IOFLAG2,FGCRT CRT MODE ?                          91084
         BNZ   RINDDONE      YES; NO LONG PACKETS                91084
         LA    R3,TMPDATA+9  POINT TO PRESUMED LAST OPTION BYTE  91077
RINDTOPL TM    0(R3),FGCAPA2 ANOTHER OPTION BYTE FOLLOWING ?     91077
         BZ    RINDTOPD      NO                                  91077
         LA    R3,1(,R3)     NEXT BYTE                           91077
         CLI   0(R3),0       SENT ?                              91077
         BNZ   RINDTOPL      YES                                 91077
         B     RINDDONE      NO GO                               91077
RINDTOPD TM    TMPDATA+9,4   IS WINDOW BIT ON ?                  91077
         BZ    RINDNWIN      NO                                  91077
RINDNWIN LA    R3,1(,R3)     SKIP WINDOW SIZE                    91077
         SLR   R14,R14       CLEAR FOR IC                        91077
         IC    R14,1(,R3)    LOAD AND TEST FIRST LENGTH BYTE     91077
         S     R14,SPACE     MAKE RELATIVE                       91077
         BM    RINDDONE      INVALID                             91077
         SLR   R15,R15       LOAD AND TEST NEXT ONE              91077
         IC    R15,2(,R3)                                        91077
         S     R15,SPACE                                         91077
         BM    RINDDONE                                          91077
         MH    R14,=H'95'    MAKE ROOM FOR SECOND BYTE           91080
         AR    R14,R15       COMBINE THEM                        91080
         CH    R14,=H'95'    AT LEAST SMALL PACKET SIZE ?        91077
         BL    RINDDONE      NO; IGNORE                          91077
         MIN   R14,LONGLEN,TYPE=H  CLIP TO OUR MAXIMUM           91267
         STH   R14,LONGLEN   SAVE LONG PACKET SIZE               91077
         LTR   R14,R14       ANY LENGTH ?                        91269
         BNP   RINDDONE      YES; NO LONG PACKETS                91269
         OI    CAPAFG,FGRLONG  ENABLE LONG RECEIVE               91077
RINDDONE MVC   N,NUM         SYNCH PACKET NUMBERS             ESP88150
         SLR   R4,R4                                          ESP88150
         IC    R4,SQUO       GET SEND QUOTE                   ESP88150
         LA    R3,GTCHTAB(R4)                                 ESP88150
         MVI   0(R3),4       QUOTE QUOTE IN DATA              ESP88150
         IC    R4,RQUO       GET RECEIVE QUOTE                ESP88150
         LA    R4,QUOTABLE(R4)  POINT TO ITS ENTRY            ESP88150
         MVI   0(R4),OFFQUOTE  KICK OUT OF TRT                ESP88150
         B     RINDEXIT                                       ESP88150
RINDBADL MVI   ERRNUM,ERCBCNT  BAD CHARACTER COUNT            ESP88150
RINDFAIL MVI   STATE,C'A'    SET ERROR CODE                   ESP88150
RINDEXIT SUBEX ,             RETURN TO CALLER                 ESP88150
RINDMVC  MVC   TMPDATA(0),RDAT  MOVE BACK FOR EASY TESTING    ESP88150
         SPACE 1                                              ESP88150
*        INITIALIZATION DATA RETURNED AFTER NEGOTIATION       ESP88150
*                                                             ESP88150
SINDATA  SUBENT ,                                             ESP88150
         MVI   STYPE,AY      SET MESSAGE TYPE TO ACK          ESP88150
         MVI   LSDAT+L'LSDAT-1,9  RETURN NINE BYTES           ESP88150
         L     R5,RPSIZ      GET MAXIMUM PACKET SIZE          ESP88150
         A     R5,SPACE      MAKE IT PRINTABLE                ESP88150
         STC   R5,SDAT       PUT SIZE INTO BUFFER             ESP88150
         LA    R5,8          SET TIME-OUT VALUE               ESP88150
         A     R5,SPACE      MAKE IT PRINTABLE                ESP88150
         STC   R5,SDAT+1                                      ESP88150
         L     R5,SPACE      ZERO + " " FOR NUMBER OF PADCHARS SP88150
         STC   R5,SDAT+2     WE'RE THE SLOW GUYS (?)          ESP88150
         SLR   R5,R5         PAD WITH NULLS                   ESP88150
         X     R5,O100       XOR WITH 64                      ESP88150
         STC   R5,SDAT+3     DON'T NEED PADCHAR EITHER        ESP88150
         SLR   R5,R5         CLEAR AGAIN                      ESP88150
         IC    R5,REOL       EOL CHAR I NEED                  ESP88150
         A     R5,SPACE      MAKE PRINTABLE                   ESP88150
         STC   R5,SDAT+4                                      ESP88150
         IC    R5,SQUO       MY QUOTE CHAR                    ESP88150
         STC   R5,SDAT+5                                      ESP88150
         MVI   SDAT+6,AN     PRESET 8TH-BIT QUOTING TO NONE   ESP88150
         MVC   SDAT+7(1),RCHECK  SET CHECKSUM LENGTH (1 OR 2) ESP88150
         MVI   SDAT+8,ASP    NO REPEAT PREFIX                 ESP88150
         ICM   R5,1,RQATE    EIGHT-BIT PREFIX ?               ESP88150
         BZ    *+10          NO                               ESP88150
         MVC   SDAT+6(1),RQATE  YES; PROPAGATE IT             ESP88150
         ICM   R5,1,RQREP    REPEAT PREFIX ?                  ESP88150
         BZ    *+10          NO                               ESP88150
         MVC   SDAT+8(1),RQREP  YES; PROPAGATE IT             ESP88150
         SPACE 1                                                 91077
*    LONG PACKET EXTENSION                                       91077
         TM    IOFLAG2,FGCRT CRT MODE ?                          91084
         BNZ   SINDEXIT      YES; NO LONG PACKETS                91084
         ICM   R15,3,LONGLEN  VALID LONG LENGTH ?                91269
         BNP   SINDEXIT      NO; NO LONG PACKETS                 91269
         IC    R15,CAPAFG    GET CAPABILITIES                    91091
         N     R15,=A(X'3F'-FGRLONG)  SET CAPABILITIES           91091
         A     R15,SPACE     MAKE PRINTABLE                      91267
         STC   R15,SDAT+9                                        91091
         MVI   LSDAT+L'LSDAT-1,10  SET LENGTH FOR CAPA BYTE      91311
         TM    CAPAFG,FGCAWIN  WINDOW MODE ?                     91311
         BZ    *+8           NO                                  91311
         MVI   LSDAT+L'LSDAT-1,11  INCLUDE WINDOW COUNT          91311
         TM    CAPAFG,FGRLONG  LONG PACKETS DESIRED ?            91084
         BZ    SINDEXIT      NO                                  91084
         LH    R15,LONGLEN   GET LONG LENGTH                     91077
         CH    R15,=H'95'    AT LEAST MINIMUM ?                  91077
         BL    SINDEXIT      NO                                  91077
         OI    SDAT+9,FGRLONG  SET LONG PACKET OPTION            91091
         MVI   SDAT+10,ASP   SET WINDOW OPTION COUNT=0           91080
         SLR   R14,R14       CLEAR FOR DIVIDE                    91080
         D     R14,=F'95'    GET REMAINDER/QUOTIENT              91080
         A     R14,SPACE     CONVERT                             91077
         A     R15,SPACE     DITTO                               91077
         STC   R14,SDAT+12   REMAINDER                           91080
         STC   R15,SDAT+11   QUOTIENT                            91080
         MVI   LSDAT+L'LSDAT-1,13  RETURN 12 BYTES               91077
SINDEXIT SUBEX ,             RETURN TO CALLER                 ESP88150
         EJECT
**********************************************************************
*                                                                    *
*        ROUTINE TO PROCESS SEND COMMAND                             *
*                                                                    *
**********************************************************************
SEND     SUBENT ,                                             ESP88150
         SLR   R6,R6         GET ZERO                         ESP88150
         ST        R6,NUMTRY           ZERO THIS OUT
OKSND    TM        FLAGS,FLG1          IS THIS THE FIRST FILE?
         BNO   SNDLOOP                                        ESP88150
         NI        FLAGS,X'FF'-FLG1    TURN OFF FIRST FILE FLAG
**********************************************************************
*        MAIN SEND LOOP                                              *
**********************************************************************
SNDLOOP  IC    R0,STATE      GET CURRENT STATE                ESP88150
         SUBCALL CHARLOOK,SNDTAB   BRANCH BY TYPE             ESP88150
         SPACE 1                                              ESP88150
SNDTAB   CHARB C'D',SNDDATA  SEND DATA                        ESP88150
         CHARB C'F',SNDFILE  SEND FILE                        ESP88150
         CHARB C'S',SNDINIT  SEND INITIALIZATION              ESP88150
         CHARB C'Z',SNDEOF   SEND EOF                         ESP88150
         CHARB C'B',SNDEOB   SEND END-BATCH                   ESP88150
         CHARB C'C',SNDCOMP  COMPLETE                         ESP88150
         CHARB C'P',SNDAPACK ATTRIBUTE PACKET                    91290
         CHARB C'A',SNDABORT ERROR                            ESP88150
         CHARB END,SNDBSTAT  BAD STATE ?                      ESP88150
         SPACE 1                                              ESP88150
SNDBSTAT MVI   ERRNUM,ERCBSTAT  UNRECOGNIZED STATE            ESP88150
         B     SNDABORT                                       ESP88150
**********************************************************************
*        CREATE AND SEND INITIALIZATION PACKET                       *
**********************************************************************
SNDINIT  CLC       NUMTRY,IMXTRY       SEE IF CAN SEND        ESP88150
         BNL   SNDABORT      NO; ABORT                        ESP88150
         LH    R0,DSSIZ      GET MAXIMUM (AND DEFAULT) SEND SIZE 88150
         ST    R0,SPSIZ      SET SIZE EXPECTED                ESP88150
         ST    R0,RPSIZ      SET SIZE EXPECTED                ESP88150
         SH    R0,=H'3'      ALLOW FOR OVERHEAD               ESP88150
         ST    R0,SIZE       SET DATA SIZE IN PACKET          ESP88150
         MVC   SQUO,DQUOTE   SET QUOTE DEFAULT                ESP88150
         MVC   RQATE,DRQATE  EIGHT-BIT PREFIXING              ESP88150
         MVC   RCHECK,SCHECK SET CHECKSUM PREFERRENCE         ESP88150
         MVC   RQREP,DRQREP  REPEAT PREFIXING                 ESP88150
         OI    CAPAFG,FGRLONG+FGAPACK  VOLUNTEER LONG PACKETS    91091
         SUBCALL SINDATA     PREPARE INITIALIZATION PACKET    ESP88150
         MVI   STYPE,AS      BUT SET PACKET TYPE TO 'SEND'    ESP88150
         SUBCALL SPACK       SEND A PACKET                    ESP88150
         CLI       STATE,C'A'
         BE    SNDABORT                                       ESP88150
         SUBCALL RPACK       GET A PACKET                     ESP88150
         IC    R0,RTYPE      GET RETURN PACKET TYPE           ESP88150
         SUBCALL CHARLOOK,SNITAB  BRANCH BY RESPONSE          ESP88150
         SPACE 1                                              ESP88150
SNITAB   CHARB AE,SNDDIED    MICRO DIED                       ESP88150
         CHARB AY,SNIACK     ACK; CHECK PACKET NUMBER         ESP88150
         CHARB AN,SNINAK     NAK                              ESP88150
         CHARB END,SNDELSE   OTHER PACKET                     ESP88150
         SPACE 1                                              ESP88150
SNIACK   CLC   N,NUM         CHECK MESSAGE NUMBERS            ESP88150
         BNE   SNDMISS       MISSING A PACKET                 ESP88150
         SUBCALL RINDATA     PROCESS INITIALIZATION DATA      ESP88150
         CLI   STATE,C'A'    OK ?                             ESP88150
         BE    SNDABORT      NO                               ESP88150
         MVI       STATE,C'F'          PUT INTO SEND FILE STATE P88150
         OI    FLAGS,FGHAZED SHOW INITIALIZED                 ESP88150
         XC        NUMTRY,NUMTRY       RESET TO ZERO
         NINC  N             GET NEXT PACKET NUMBER           ESP88150
         B     SNDLOOP                                        ESP88150
SNINAK   TM    FLAGS,FGNAK   DID MICRO NAK OR I REJECTED?     ESP88150
         BO    SNDLOOP       LEAVE ERR MSG AS IS IF I DID     ESP88150
         MVI   ERRNUM,ERCBNAK  MICRO NAK'ED                   ESP88150
         B     SNDLOOP                                        ESP88150
**********************************************************************
*        CREATE AND SEND FILE PACKET                                 *
**********************************************************************
SNDFILE  CLC   NUMTRY,MAXTRY EXCEEDED NO. OF TRIES ALLOWED?   ESP88150
         BNL   SNDABORT      YES; QUIT                        ESP88150
         LH    R5,FILNAML    GET LENGTH OF FILENAME - 1
         MVC   SDAT(*-*),FILNAM    USE FOR EXECUTE
         EX    R5,*-6        GO MOVE FILENAME TO BUFFER
         L     R15,@MAKASC   GET TRANSLATION TABLE               91315
         TR    SDAT(L'FILNAM),0(R15)  TRANSLATE NAME TO ASCII    91315
         LA    R5,1(,R5)     GET CORRECT LENGTH OF NAME
         INC   NUMTRY        INCREMENT RETRY COUNTER          ESP88150
         MVI       STYPE,AF            PACKET TYPE = FILE HEADER
         TM    IOFLAGS,FGTYPE  TYPE REQUESTED ?               ESP88150
         BZ    *+8           NO                               ESP88150
         MVI   STYPE,AX      YES; RETURN AS TYPE/LIST FILE    ESP88150
         ST        R5,LSDAT            SET BUFFER SIZE
SNDFIL   SUBCALL SPACK       SEND A PACKET                    ESP88150
         CLI       STATE,C'A'
         BE    SNDABORT                                       ESP88150
         SUBCALL RPACK       GET A PACKET                     ESP88150
         IC    R0,RTYPE      GET RESPONSE                     ESP88150
         SUBCALL CHARLOOK,SNFTAB  BRANCH ACCORDINGLY          ESP88150
         SPACE 1                                              ESP88150
SNFTAB   CHARB AE,SNDDIED    MICRO DIED                       ESP88150
         CHARB AY,SNFACK     OK                               ESP88150
         CHARB AN,SNINAK     RE-USE INIT'S NAK CODE           ESP88150
         CHARB END,SNDELSE   UNKNOWN                          ESP88150
         SPACE 1                                              ESP88150
SNFACK   CLC   N,NUM         DO WE HAVE THE CORRECT ACK?      ESP88150
         BNE   SNDMISS       NO; PACKET MISSING               ESP88150
         MVI       STATE,C'D'          PREPARE FOR SEND-DATA STATE
         MVI   CANCODE,0     SET FOR PREMATURE TERMINATION    ESP88150
         XC        NUMTRY,NUMTRY       RESET COUNTER
         NINC  N             MAKE NEXT PACKET NUMBER          ESP88150
         TM    CAPAFG,FGAPACK  DOING ATTRIBUTE PACKETS ?         91290
         BZ    SNDFILL       NO; SEND DATA                       91290
         MVI   STATE,C'P'    SET ATTRIBUTE PACKET STATE          91290
**********************************************************************
*        CREATE AND SEND ATTRIBUTE PACKET(S)                         *
**********************************************************************
SNDAPACK CLC   NUMTRY,MAXTRY  OVER RETRY LIMIT ?                 91290
         BH    SNDABORT      YES; QUIT                           91290
         ICM   R0,15,NUMTRY  FIRST TIME ?                        91290
         BNZ   SNDAPNEW      NO                                  91290
         MVC   SDAT(L'ATTRPAT),ATTRPAT  MAKE DEFAULT             91290
         MVI   LSDAT+L'LSDAT-1,L'ATTRPAT  SET DATA LENGTH        91290
         TM    EDFLAGS,FGASIS+FGABIN  BINARY MODE ?              91310
         BZ    *+8           NO                                  91290
         MVI   SDAT+2,AB    CHANGE FILE TYPE TO BINARY           91310
         MVI   STYPE,AA      SET ATTRIBUTE PACKET                91290
SNDAPNEW SUBCALL SPACK       SEND PACKET                         91290
         CLI   STATE,C'A'    ACCEPTED ?                          91290
         BE    SNDABORT      NO                                  91290
         SUBCALL RPACK       GET RESPONSE                        91290
         IC    R0,RTYPE      GET RESPONSE TYPE                   91290
         SUBCALL CHARLOOK,SNPTAB  BRANCH BY RESPONSE             91290
         SPACE 1                                                 91290
SNPTAB   CHARB AE,SNDDIED                                        91290
         CHARB AY,SNPACK     OK; CHANGE TO DATA                  91290
         CHARB AN,SNINAK     COMMON NAK                          91290
         CHARB END,SNDELSE   DON'T KNOW ?                        91290
         SPACE 1                                                 91290
SNPACK   CLC   N,NUM         CORRECT SEQUENCE ?                  91290
         BNE   SNDMISS       NO; MISSING PACKET                  91290
         XC    LSDAT,LSDAT   JUST IN CASE                        91290
         MVI   STATE,C'D'    CHANGE TO DATA                      91290
         XC    NUMTRY,NUMTRY                                     91290
         NINC  N             NEW PACKET NUMBER                   91290
         B     SNDFILL       FILL AND SEND DATA PACKET           91290
         SPACE 1                                                 91290
ATTRPATS DC    AL1(AQUO,ASP+1,AA,46,ASP+2,AI,A2)  IBM/MVS TEXT   91290
ATTRPAT  EQU   ATTRPATS,*-ATTRPATS,C'X'                          91290
         SPACE 1                                                 91290
**********************************************************************
*        CREATE AND SEND DATA PACKETS                                *
**********************************************************************
SNDDATA  CLC   NUMTRY,MAXTRY MAY WE DO IT?                    ESP88150
         BNL   SNDABORT      NO; QUIT                         ESP88150
         INC   NUMTRY        COUNT TRIES                      ESP88150
         MVI       STYPE,AD            PACKET TYPE = DATA
         SUBCALL SPACK       SEND A PACKET                    ESP88150
         CLI       STATE,C'A'
         BE    SNDABORT                                       ESP88150
         SUBCALL RPACK       GET A PACKET                     ESP88150
         IC    R0,RTYPE      GET RESPONSE                     ESP88150
         SUBCALL CHARLOOK,SNATAB  BRANCH BY RESPONSE          ESP88150
         SPACE 1                                              ESP88150
SNATAB   CHARB AE,SNDDIED                                     ESP88150
         CHARB AY,SNAACK     CONTINUE                         ESP88150
         CHARB AN,SNINAK     COMMON NAK                       ESP88150
         CHARB END,SNDELSE   OTHER                            ESP88150
         SPACE 1                                              ESP88150
SNAACK   CLC   N,NUM         DO WE HAVE THE CORRECT ACK?      ESP88150
         BNE   SNDMISS       NO                               ESP88150
         XC        NUMTRY,NUMTRY       RESET COUNTER
         NINC  N             MAKE NEXT PACKET NUMBER          ESP88150
         ICM   R0,15,LRDAT   ANY DATA ON THE ACK ?            ESP88150
         BNP   SNDFILL       NO; FILL ANOTHER SEND PACKET     ESP88150
         CLI   RDAT,AX       CANCEL DATASET ?                 ESP88150
         BE    SNDCANC       YES                              ESP88150
         CLI   RDAT,AZ       CANCEL BATCH ?                   ESP88150
         BE    SNDCANC       YES; ELSE IGNORE                 ESP88150
SNDFILL  SUBCALL GTCHR                                        ESP88150
         ICM   R0,15,LSDAT   ARE WE SENDING ANY DATA ?        ESP88150
         BNZ   SNDLOOP       YES; CONTINUE                    ESP88150
         B     SNDLEOF       NO; FORCE EOF                    ESP88150
**********************************************************************
*        CREATE AND SEND EOF PACKET                                  *
**********************************************************************
SNDCANC  MVC   CANCODE,RDAT  SAVE THE CANCEL REQUEST TYPE     ESP88150
SNDLEOF  MVI   STATE,C'Z'    SET FORCED END                   ESP88150
         SPACE 1                                              ESP88150
SNDEOF   CLC   NUMTRY,MAXTRY MAY WE DO IT?                    ESP88150
         BNL   SNDABORT      NO MORE                          ESP88150
         MVC   N,NUM         SYNCHRONIZE PACKET NUMBERS       ESP88150
         INC   NUMTRY        COUNT TRIES                      ESP88150
         NINC  N             NEXT PACKET NUMBER               ESP88150
         MVI       STYPE,AZ            PACKET TYPE = EOF
         XC        LSDAT,LSDAT         LENGTH OF ZERO
         CLI   CANCODE,0     ANY CANCELLATION ?               ESP88150
         BE    SNDEOF2       NO                               ESP88150
         MVI   LSDAT+L'LSDAT-1,1  SEND ONE BYTE IN RESPONSE   ESP88150
         MVI   SDAT,AD       SET FOR DATASET DELETE           ESP88150
SNDEOF2  SUBCALL SPACK                                        ESP88150
         CLI       STATE,C'A'
         BE    SNDABORT                                       ESP88150
         SUBCALL RPACK                                        ESP88150
         IC    R0,RTYPE      GET RESPONSE                     ESP88150
         SUBCALL CHARLOOK,SNZTAB  BRANCH ACCORDINGLY          ESP88150
         SPACE 1                                              ESP88150
SNZTAB   CHARB AE,SNDDIED                                     ESP88150
         CHARB AY,SNZACK     OK                               ESP88150
         CHARB AN,SNINAK     JOIN COMMON NACK                 ESP88150
         CHARB END,SNDELSE                                    ESP88150
         SPACE 1                                              ESP88150
SNZACK   CLC   N,NUM         CORRECT ACK?                     ESP88150
         BNE   SNDMISS       NO                               ESP88150
         NINC  N             NEXT PACKET NUMBER               ESP88150
         MVI       STATE,C'F'          SET TO SEND FILE FOR NOW
*        CLI   CANCODE,AZ    CANCEL BATCH ?                   ESP88150
*        BE    SNZEOB        YES                              ESP88150
*WILD    CLI   SERVFUN,0     SERVER MODE ?                    ESP88150
*CARD?   BNE   COMPLETE      YES; LET IT DO ANOTHER FILE OR QUIT 88150
*
*
*  WE JUST PROCESS ONE FILE FOR NOW.
*
SNZEOB   MVI       STATE,C'B'          BREAK CONNECTION       ESP88150
         B     SNDLOOP                                        ESP88150
**********************************************************************
*        CREATE AND SEND BREAK PACKET                                *
**********************************************************************
SNDEOB   CLC   NUMTRY,MAXTRY OVER OUR LIMIT?                  ESP88150
         BNL   SNDABORT      YES; QUIT                        ESP88150
         INC   NUMTRY        COUNT TRIES                      ESP88150
         MVI       STYPE,AB            PACKET TYPE = BREAK
         XC        LSDAT,LSDAT         LENGTH = ZERO
         SUBCALL SPACK                                        ESP88150
         CLI       STATE,C'A'
         BE    SNDABORT                                       ESP88150
         SUBCALL RPACK                                        ESP88150
         IC    R0,RTYPE      GET RESPONSE                     ESP88150
         SUBCALL CHARLOOK,SNBTAB  BRANCH                      ESP88150
         SPACE 1                                              ESP88150
SNBTAB   CHARB AE,SNDDIED    NO RESPONSE                      ESP88150
         CHARB AY,SNBACK     ACK                              ESP88150
         CHARB AN,SNINAK     COMMON NAK                       ESP88150
         CHARB END,SNDELSE   OTHER                            ESP88150
         SPACE 1                                              ESP88150
SNBACK   CLC   N,NUM         CORRECT ACK?                     ESP88150
         BNE   SNDMISS       NO; PACKET MISSING               ESP88150
         MVI       STATE,C'C'          COMPLETED STATE
         NI    FLAGS,255-FGHAZED  RE-INIT REQUIRED            ESP88150
         B     SNDLOOP                                        ESP88150
         SPACE 1                                              ESP88150
SNDMISS  MVI   ERRNUM,ERCBMISS  PACKET MISSING                ESP88150
         B     SNDLOOP       RETRY MY REQUEST                 ESP88150
**********************************************************************
*        CREATE AND SEND ABORT PACKET                                *
**********************************************************************
SNDDIED  MVI   ERRNUM,ERCBDIED  SET MICRO DIED                ESP88150
         B     SNDABORT                                       ESP88150
SNDELSE  MVI   ERRNUM,ERCBTYPE  UNTIMELY OR INVALID PACKET TYPE P88150
*        B     SNDABORT      QUIT                             ESP88150
SNDABORT SUBCALL SERRPACK                                     ESP88150
NOERRP   LA        R15,4               SET NON-ZERO RETCODE
         B         SENDRET             PREPARE TO LEAVE
**********************************************************************
*        PROCESS COMPLETE                                            *
**********************************************************************
SNDCOMP  SLR   R15,R15       ZERO WILL BE RETCODE             ESP88150
SENDRET  NI    IOFLAGS,255-FGTYPE  RESET TYPE REQUEST         ESP88150
         NI    FLAGS,255-FGHAZED  RE-INIT REQUIRED            ESP88150
         NI    EDFLAGS,255-FGABIN  JUST IN CASE                  91310
         SUBEX RC=(R15)                                       ESP88150
         EJECT
**********************************************************************
*                                                                    *
*        ROUTINE TO LOAD SEND PACKET FROM HOST INPUT FILE            *
*                                                                    *
**********************************************************************
GTCHR    SUBENT ,                                             ESP88150
         XC    LSDAT,LSDAT   SET PACKET EMPTY                 ESP88150
         XC    SNLEN,SNLEN   ALSO CLEAR TRANSLATED LENGTH        91091
GTCHLOOP L     R6,CURRECAD   GET ADDRESS LEFT LAST TIME       ESP88150
         SLR   R7,R7         CLEAR FOR IC                     ESP88150
         ICM   R7,3,CURLRECL  GET RESIDUAL SPACE IN CURRENT RECORD
         BP    GTCHMORE      SOME LEFT, PROCESS               ESP88150
         TM    PTCHFLG,PTCHFCR  NEED TO ADD A CARRIAGE RETURN ? P88150
         BZ    GTCHNCR       NO                               ESP88150
         NI    PTCHFLG,255-PTCHFCR  RESET                     ESP88150
         LA    R6,=X'0D0A'   SET ASCII CR/LF SEQUENCE         ESP88150
         LA    R7,2          SET LENGTH OF CONTROL SEQUENCE   ESP88150
         B     GTCHAGIN      SAVE AND RESTART                 ESP88150
         SPACE 1                                              ESP88150
GTCHNCR  SUBCALL READX       READ A RECORD                    ESP88150
         BXH   R15,R15,GTCHEXIT  EXIT NORMALLY (NOTE LRECL=0 FOR EOF)
         TM    EDFLAGS,FGASIS+FGABIN  BINARY MODE ?              91310
         BNZ   GTCHLOOP      YES; PROCESS AS IS               ESP88150
         OI    PTCHFLG,PTCHFCR  TACK CR/LF AT END OF RECORD   ESP88150
         L     R6,CURRECAD   GET RECORD ADDRESS               ESP88150
         LH    R7,CURLRECL    AND RECORD LENGTH               ESP88150
         TM    EDFLAGS,FGDEBS  DELETE TRAILING BLANKS ?       ESP88307
         BZ    GTCHCVIN      NO                                  91315
         LA    R15,0(R6,R7)  GET LAST BYTE +1                 ESP88307
         CR    R15,R6        ANY DATA ?                       ESP88307
         BNH   GTCHCVIN      NO; GET ON WITH IT                  91315
GTCHNDEB BCTR  R15,0         BACK-UP ONE                      ESP88307
         CLI   0(R15),C' '   TRAILING BLANK ?                 ESP88307
         BNE   GTCHYDEB      NO; DONE                         ESP88307
         BCT   R7,GTCHNDEB   TRY AGAIN                        ESP88307
GTCHYDEB STH   R7,CURLRECL   SET ADJUSTED LENGTH              ESP88307
GTCHCVIN L     R15,@MAKASC   GET ASCII TRANSLATION               91315
GTCHCVLP LA    R1,256        SET MAXIMUM TO DO                ESP88150
         CR    R7,R1         LONG ENOUGH ?                    ESP88150
         BNL   *+6           YES                              ESP88150
         LR    R1,R7         ELSE SHORTEN                     ESP88150
         SR    R7,R1         LENGTH AFTER TRANSLATE           ESP88150
         BCTR  R1,0          LENGTH FOR EXECUTE               ESP88150
         LTR   R1,R1         LEGAL ?                          ESP88150
         BM    GTCHLOOP      OOPS                             ESP88150
         EX    R1,GTCHTR     MAKE ASCII                       ESP88150
         LA    R6,1(R1,R6)   NEXT SEGMENT                     ESP88150
         B     GTCHCVLP      REPEAT UNTIL EXHAUSTED (OR BORED) SP88150
         SPACE 1                                              ESP88150
GTCHMORE LA    R4,SDAT       POINT TO SEND DATA               ESP88150
         L     R0,LSDAT      GET LENGTH CURRENTLY FILLED      ESP88150
         L     R5,SIZE       GET SPACE IN THIS PACKET         ESP88150
         TM    CAPAFG,FGRLONG  SENDING LONG PACKETS ?            91091
         BNO   GTCHSHRT      NO; SHORT                           91091
         LH    R5,LONGLEN    GET MAXIMUM LONG SIZE               91091
         SH    R5,=H'9'      ALLOW FOR FIXED OVERHEAD            91290
         SLR   R14,R14                                           91091
         IC    R14,RCHECK    GET CHECKSUM TYPE                   91091
         N     R14,=F'3'     CLIP                                91091
         SR    R5,R14        SUBTRACT CHECKSUM SPACE ALSO        91091
         LTR   R0,R0         FIRST TIME IN PACKET ?              91091
         BNZ   GTCHSHRT      NO                                  91091
         LA    R0,3          LEAVE ROOM FOR LONG PROTOCOL LENGTH 91091
GTCHSHRT SR    R5,R0         GET ROOM LEFT                    ESP88150
         BNP   GTCHEXIT      HUH ?                            ESP88150
         AR    R4,R0         SPACE TO AVAILABLE LOCATION      ESP88150
         MVC   TEMP+2(1),0(R6)  MOVE THE BYTE                 ESP88150
         LA    R9,TEMP+2     SET FROM ADDRESS                 ESP88150
         LA    R8,1          SET LENGTH                       ESP88150
         TM    TEMP+2,X'80'  NEED 8-BIT QUOTE ?               ESP88150
         BZ    GTCHNATE      NO                               ESP88150
         NI    TEMP+2,X'7F'  RESET HIGH BIT                   ESP88150
         CLI   RQATE,ASP     QUOTING ?                        ESP88150
         BNH   GTCHNATE      NO; LOSE IT                      ESP88150
         BCTR  R9,0                                           ESP88150
         LA    R8,1(,R8)     MAKE SEQUENCE ONE LONGER         ESP88150
         MVC   0(1,R9),RQATE  MAKE 8TH-BIT QUOTE              ESP88150
GTCHNATE SLR   R2,R2                                          ESP88150
         TRT   TEMP+2(1),GTCHTAB  SEE WHAT ELSE TO DO         ESP88150
         B     *+4(R2)                                        ESP88150
         B     GTCHFITS      MOVE IF IT FITS                  ESP88150
         B     GTCHQUOT      QUOTE IT                         ESP88150
*        B     GTCHFLIP      QUOTE AND FLIP                   ESP88150
GTCHFLIP XI    TEMP+2,X'40'  FLIP CONTROL                     ESP88150
GTCHQUOT BCTR  R9,0          FIX THE MOVE SEQUENCE            ESP88150
         MVC   0(1,R9),RQUO  ADD QUOTE                        ESP88150
         LA    R8,1(,R8)     UP THE LENGTH                    ESP88150
         CH    R8,=H'3'      8-BIT QUOTE + CONTROL QUOTE ?       90212
         BNE   GTCHFITS      NO; CONTINUE                        90212
         MVC   0(2,R9),RQATE  8-BIT PREFIX, THEN CTL-QUOTE       90212
GTCHFITS SR    R5,R8         WILL THIS SEQUENCE FIT ?         ESP88150
         BM    GTCHEXIT      NO; SEND A PACKET, THEN REPEAT   ESP88150
         BZ    GTCHFITM      FULL - SKIP REPEAT CHECK         ESP88150
         LA    R1,3          LENGTH OF MINIMUM REPEAT TEXT    ESP88150
         CR    R1,R7         LONG ENOUGH TO TEST ?            ESP88325
         BNL   GTCHFITM      NO; DON'T 0C4 ON TEST            ESP88325
         BCTR  R1,0          ADJUST FOR EXECUTE               ESP88325
         EX    R1,GTCHCLC    DUPLICATE CHARACTERS > CHAR LENGTH ?
         BNE   GTCHFITM      NO                               ESP88150
         CLI   RQREP,ASP     ANY REPEAT PREFIX ?              ESP88150
         BNH   GTCHFITM      NO                               ESP88150
         IC    R0,0(,R6)     LOAD THIS CHARACTER              ESP88150
         CH    R5,=H'2'      ROOM FOR REPEAT PREFIX, TOO ?    ESP88150
         BL    GTCHFITM      NO; JUST MOVE BYTE               ESP88150
         LA    R1,&OPPAKSZ   IMPLEMENTATION LIMIT             ESP88150
         CR    R1,R7         LESS THAN LENGTH LEFT ?          ESP88150
         BNH   *+6           YES                              ESP88150
         LR    R1,R7         DON'T DO MORE THAN AVAILABLE     ESP88150
         BCTR  R1,0          FIRST BYTE EQUALS ITSELF         ESP88150
         LTR   R2,R1         SAVE FOR LENGTH CALCULATION      ESP88150
         BNP   GTCHFITM      OOPS                             ESP88150
GTCHRPLP CLM   R0,1,1(R6)    MATCH ?                          ESP88150
         BNE   GTCHRPND      NO                               ESP88150
         LA    R6,1(,R6)     GET NEXT                         ESP88150
         BCT   R1,GTCHRPLP   CONTINUE UP TO MAX               ESP88150
GTCHRPND MVC   0(1,R4),RQREP  EMIT THE REPEAT CHARACTER       ESP88150
         SR    R2,R1         NUMBER FOUND                     ESP88150
         LA    R1,1(,R2)     TRUE LENGTH                      ESP88150
         SR    R7,R2         REMAINDER - (L-1)                ESP88150
         A     R1,SPACE      MAKE PRINTABLE ASCII             ESP88150
         STC   R1,1(,R4)     ADD TO PACKET                    ESP88150
         LA    R4,2(,R4)     SKIP REPEAT PREFIX               ESP88150
         BCTR  R5,0          TAKE TWO                         ESP88150
         BCTR  R5,0          OFF THE PACKET LENGTH            ESP88150
GTCHFITM EX    R8,GTCHMVC    MOVE TO PACKET (NOTE R8 ONE TOO HIGH)
         AR    R4,R8         GET NEXT ADDRESS                 ESP88150
         LA    R0,SDAT       PACKET START                     ESP88150
         SR    R4,R0         LESS START                       ESP88150
         ST    R4,LSDAT      SET LENGTH IN PACKET             ESP88150
         LA    R6,1(,R6)     ADVANCE IN INPUT                 ESP88150
         BCTR  R7,0          FIX INPUT REMAINING              ESP88150
GTCHAGIN ST    R6,CURRECAD                                    ESP88150
         STH   R7,CURLRECL   AND SAVE IT                      ESP88150
         B     GTCHLOOP      TRY AGAIN                        ESP88150
GTCHEXIT SUBEX ,             RETURN TO CALLER                 ESP88150
GTCHTR   TR    0(0,R6),0(R15)  TRANSLATE TO ASCII                91315
GTCHMVC  MVC   0(0,R4),0(R9)  MOVE VARIABLE LENGTH SEQUENCE   ESP88150
GTCHCLC  CLC   0(0,R6),1(R6)  ANY REPETITIONS ?               ESP88150
         EJECT
**********************************************************************
*                                                                    *
*        HOST INPUT FILE READER                                      *
*                                                                    *
**********************************************************************
READX    SUBENT ,                                             ESP88150
         TM    KERIN+DCBOFLGS-IHADCB,DCBOFOPN  DCB OPEN ?     ESP85142
         BZ    READXEOF      NO; RETURN EOF                   ESP85142
         AIF   (NOT &INPREAD).NOLGET                             91315
         TM    IOFLAGS,FGWYL WYLBUR COMPRESSED MODE ?         ESP88312
         BNZ   READXWYL      YES; GET NEXT RECORD             ESP88312
.NOLGET  SPACE 1                                              ESP88312
READLOOP GET   KERIN         GET A BUFFER (LOCATE MODE)       ESP88150
         TM    IOFLAGS,FGABEND  DCB ABENDED ?                    91112
         BNZ   READXABN      YES; TREAT AS END OF FILE           91112
         LR    R4,R1         COPY THE RECORD ADDRESS          ESP88150
         TM    MODEFG,FGNET  MULTI-TASKING MODE ?                89337
         BZ    READNCNT      NO; NO I/O COUNTS                   89337
         L     R14,FAKECPPL+8  GET MTS ADDRESS BACK              89337
         ICM   R15,15,MTSNEXCP-MTS(R14)  GET OLD DISK INP COUNT  89337
         AH    R15,=H'1'     ADD 1                               89337
         STCM  R15,15,MTSNEXCP-MTS(R14)  SAVE NEW DISK INP COUNT 89337
READNCNT DS    0H                                                89337
         LH    R5,DCBLRECL+KERIN-IHADCB  RECORD LENGTH        ESP88150
         TM    DCBRECFM+KERIN-IHADCB,DCBRECF  VARIABLE ?      ESP88150
         BNZ   READXSET      NO                               ESP88150
         ICM   R5,3,0(R4)    GET THE LENGTH FROM THE RDW      ESP88150
         LA    R4,4(,R4)     SKIP THE RDW                     ESP88150
         SH    R5,=H'4'      ALLOW FOR LENGTH OF RDW          ESP88150
         BNP   READLOOP      SKIP IF GARBAGE                  ESP88150
READXSET DS    0H                                             ESP88312
         AIF   (NOT &INPREAD).NOLREAD                            91315
         TM    DCBRECFM+KERIN-IHADCB,DCBRECU  UNDEFINED ?     ESP88312
         BNO   READXREC      NO                               ESP88312
         TM    IOFLAGS,FGWYL WYLBUR COMPRESSED FORMAT ?       ESP88312
         BZ    READXREC      NO; PRIOR FAILURE                ESP88312
         ST    R4,DCMBUFAD   STASH CURRENT BUFFER             ESP88312
         STCM  R5,12,DCMBUFLN  ZERO BUFFER OFFSET             ESP88312
         STH   R5,DCMBUFMX   SET BUFFER LENGTH                ESP88312
READXWYL SERVCALL WDCOM,DECOMP  DECOMPRESS RECORD FROM BUFFER ESP88312
         CH    R15,=H'4'     CHECK RETURN                     ESP88312
         BL    READXWYR      HAVE RECORD                      ESP88312
         BE    READLOOP      BUFFER EMPTY; READ AGAIN         ESP88312
         ICM   R0,15,DCMBUFAD  FIRST TIME ?                      90050
         BZ    READLOOP      YES; PRIME THE BUFFER               90050
         NI    IOFLAGS,255-FGWYL  NOT WYLBUR RECORD           ESP88312
         L     R4,DCMBUFAD   GET BUFFER BACK                  ESP88312
         LH    R5,DCMBUFMX   GET BUFFER LENGTH                ESP88312
         B     READXREC      PROCESS BLOCK AS RECORD          ESP88312
READXWYR LH    R5,DCMRECLN   COPY RECORD LENGTH               ESP88312
         L     R4,DCMRECAD   GET RECORD ADDRESS               ESP88312
.NOLREAD SPACE 1                                              ESP88312
READXREC ST    R4,CURRECAD   SAVE RECORD ADDRESS              ESP88150
         STH   R5,CURLRECL   SAVE CURRENT RECORD LENGTH       ESP88150
         TM    DEBUG+DCBOFLGS-IHADCB,DCBOFOPN  IS IT OPEN?    ESP88150
         BZ    READXOK       EXIT WITH CODE 0                 ESP88150
         MVC   WRKBUFF+2(7),=X'0000C4C9D5D740'  'DINP '       ESP88150
         BCTR  R5,0          MAKE EXEC LENGTH                 ESP88150
         LA    R0,L'WRKBUFF-10  SET MAXIMUM                   ESP88150
         CR    R5,R0         WILL IT FIT ?                    ESP88150
         BNH   *+6           YES                              ESP88150
         LR    R5,R0         TRUNCATE                         ESP88150
         EX    R5,DBGMVC3    MOVE TEXT TO BUFFER              ESP88150
         LA    R5,10(,R5)                                     ESP88150
         STH   R5,WRKBUFF    SET RDW LENGTH                   ESP88150
         PUT   DEBUG,WRKBUFF  WRITE THE ENTRY                 ESP88150
READXOK  SLR   R15,R15       CLEAR THE RETURN CODE            ESP88150
         B     READEXIT      AND RETURN                       ESP88150
DBGMVC3  MVC   WRKBUFF+9(*-*),0(R4)                           ESP88150
         SPACE 1                                              ESP88150
READXABN MVI   ERRNUM,ERCIOERR  SHOW I/O ERROR                   91112
         MVI   STATE,C'A'    ABORT TRANSFER                      91112
READEOF  SUBCALL FREEFILE    RELEASE FOR CLOSE=FREE              91112
READXEOF LA    R15,4         SET EOF RETURN                   ESP88150
         XC    CURRECAD,CURRECAD  NO TEXT                     ESP88150
         XC    CURLRECL,CURLRECL  NO TEXT LENGTH              ESP88150
READEXIT SUBEX RC=(R15)      RETURN WITH CODE                 ESP88150
         EJECT
**********************************************************************
*                                                                    *
*        ROUTINE TO PROCESS RECEIVE COMMAND                          *
*                                                                    *
**********************************************************************
RECEIVE  SUBENT ,                                             ESP88150
         SLR   R6,R6         GET ZERO                         ESP88150
         ST        R6,NUMTRY           ZERO THIS OUT
         MVI   CANCODE,0     CLEAR FILE/BATCH CANCEL          ESP88150
**********************************************************************
*        MAIN RECEIVE PROCESSING LOOP                                *
**********************************************************************
RCVLOOP  IC    R0,STATE      BRANCH BY STATE                  ESP88150
         SUBCALL CHARLOOK,RCVTAB                              ESP88150
         SPACE 1                                              ESP88150
RCVTAB   CHARB C'D',RCVDATA  RECEIVE DATA STATE               ESP88150
         CHARB C'F',RCVFILE                                   ESP88150
         CHARB C'R',RCVINIT  RECEIVE INITIALIZATION           ESP88150
         CHARB C'C',RCVCOMP  RECEIVE COMPLETE                 ESP88150
         CHARB C'A',RCVABORT  ABORT                           ESP88150
         CHARB END,RCVBSTAT                                   ESP88150
         SPACE 1                                              ESP88150
RCVBSTAT MVI   ERRNUM,ERCBSTAT  UNRECOGNIZED STATE            ESP88150
         B     RCVABORT      QUIT                             ESP88150
**********************************************************************
*        PROCESS INITIALIZATION PACKET                               *
**********************************************************************
RCVINIT  CLC   NUMTRY,IMXTRY  SEE IF CAN RECEIVE              ESP88150
         BNL   RCVABORT      NO                               ESP88150
         INC   NUMTRY                                         ESP88150
         LH    R4,DSSIZ      DEFAULT SEND PACKET SIZE         ESP88150
         SH    R4,=H'3'      USE DEFAULT TO SET "SIZE"        ESP88150
         ST        R4,SIZE             IN CASE WE DIE BEFORE IT'S SET
         SUBCALL RPACK       GET INIT INFORMATION             ESP88150
         IC    R0,RTYPE      GET RETURN PACKET TYPE           ESP88150
         SUBCALL CHARLOOK,RCITAB  BRANCH BY RESPONSE          ESP88150
         SPACE 1                                              ESP88150
RCITAB   CHARB AE,RCVDIED    MICRO DIED                       ESP88150
         CHARB AS,RCISEND    SEND INIT                        ESP88150
         CHARB AN,RCINAK     NAK                              ESP88150
         CHARB END,RCVELSE   OTHER PACKET                     ESP88150
         SPACE 1                                              ESP88150
RCISEND  SUBCALL RINDATA     PROCESS INITIALIZATION DATA      ESP88150
         CLI   STATE,C'A'    OK ?                             ESP88150
         BE    RCVABORT      NO; FAIL                         ESP88150
         SUBCALL SINDATA     SEND NEGOTIATED DATA             ESP88150
         SUBCALL SPACK       SEND PACKET                      ESP88150
         CLI       STATE,C'A'
         BE    RCVABORT                                       ESP88150
         MVI       STATE,C'F'          SET TO RECEIVE FILE STATE
         OI    FLAGS,FGHAZED INITIALIZATION COMPLETE          ESP88150
         MVC   OLDTRY,NUMTRY    SAVE TRIAL COUNTER            ESP88150
         XC        NUMTRY,NUMTRY       RESET COUNTER TO ZERO
         NINC  N             GET NEXT PACKET NUMBER           ESP88150
         B     RCVLOOP                                        ESP88150
RCVSNAK  DS    0H            SEND A NAK                       ESP88150
RCINAK   MVI   STYPE,AN      SEND A NAK PACKET                ESP88150
         XC        LSDAT,LSDAT         NO DATA
         SUBCALL SPACK                                        ESP88150
         B     RCVLOOP                                        ESP88150
         SPACE 1                                              ESP88150
**********************************************************************
*        PROCESS FILE PACKET                                         *
**********************************************************************
RCVFILE  CLC       NUMTRY,MAXTRY       EXCEEDED NO. OF TRIALS ALLOWED
         BNL   RCVABORT      NO; QUIT                         ESP88150
         OI    FLAGS,FGHAZED INITIALIZATION COMPLETE          ESP88150
         INC   NUMTRY                                         ESP88150
         SUBCALL RPACK                                        ESP88150
         IC    R0,RTYPE      GET RESPONSE                     ESP88150
         SUBCALL CHARLOOK,RCFTAB  PROCESS IT                  ESP88150
         SPACE 1                                              ESP88150
RCFTAB   CHARB AE,RCVDIED    MICRO DIED                       ESP88150
         CHARB AS,RCFSEND    STILL INIT ?                     ESP88150
         CHARB AZ,RCFEOF     END-FILE                         ESP88150
         CHARB AF,RCFFILE    FILE NAME                        ESP88150
         CHARB AA,RCDATTR    FILE ATTRIBUTES                     91091
         CHARB AB,RCFEOB     BATCH END                        ESP88150
         CHARB AN,RCINAK     NAK-NAK                          ESP88150
         CHARB END,RCVELSE   OTHER                            ESP88150
         SPACE 1                                              ESP88150
RCFSEND  CLC   OLDTRY,MAXTRY  CAN WE TRY AGAIN?               ESP88150
         BNL   RCVABORT      NO                               ESP88150
         INC   OLDTRY        INCREMENT COUNTER                ESP88150
         L         R3,N                GET PACKET NUMBER SENT
         BCTR      R3,0                SUBTRACT ONE FROM IT
         N     R3,=X'0000003F'  TRUNCATE                      ESP88150
         C         R3,NUM              NUM MUST EQUAL N-1
         BE        RNUM
RCVMISS  MVI   ERRNUM,ERCBMISS  PREVIOUS PACKET MISSING       ESP88150
         B     RCVSNAK       SEND A NAK                       ESP88150
RNUM     MVI       STYPE,AY            ACK PACKET
         ST        R3,N                MAKE SEND SEQ NO. = N-1
         XC    LSDAT,LSDAT   CLEAR SEND LENGTH                   91311
         SUBCALL SINDATA     REBUILD CAPABILITIES PACKET         91311
         NI    FLAGS,255-FGHAZED  INIT. NOT YET FINISHED         91311
         SUBCALL SPACK                                        ESP88150
         CLI       STATE,C'A'
         BE    RCVABORT                                       ESP88150
         NINC  N             NEXT PACKET NUMBER               ESP88150
         XC        NUMTRY,NUMTRY       RESET COUNTER TO ZERO
         B     RCVLOOP                                        ESP88150
RCFEOF   CLC   OLDTRY,MAXTRY CAN WE TRY AGAIN?                ESP88150
         BNL   RCVABORT                                       ESP88150
         INC   OLDTRY        INCREMENT COUNTER                ESP88150
         L         R3,N                GET PACKET NUMBER SENT
         BCTR      R3,0                SUBTRACT ONE FROM IT
         N     R3,=X'0000003F'  TRUNCATE                      ESP88150
         C         R3,NUM              NUM MUST EQUAL N-1
         BNE   RCVMISS       NO; PACKET MISSING               ESP88150
         MVI       STYPE,AY            ACK PACKET
         ST        R3,N                SEND SEQ := N-1
         XC        LSDAT,LSDAT         NO DATA
         SUBCALL SPACK                                        ESP88150
         CLI       STATE,C'A'
         BE    RCVABORT                                       ESP88150
         NINC  N                                              ESP88150
         XC        NUMTRY,NUMTRY       RESET COUNTER TO ZERO
         B     RCVLOOP                                        ESP88150
RCFFILE  CLC   NUM,N         THEY HAVE TO BE EQUAL            ESP88150
         BNE   RCVMISS       NOPE; NACK                       ESP88150
         TM    FLAGS,FLG1    DID WE ALREADY ALLOCATE THIS ?   ESP88150
         BNZ   RNUM3DON      YES; DON'T REPEAT                ESP88150
         SUBCALL FREEFILE    FREE PREVIOUS FILE               ESP88150
         LA    R14,INPUT                                      ESP88150
         LA    R15,L'INPUT-1  INTENDED DSNAME                 ESP88150
         LA    R0,RDAT       POINT TO FILENAME                ESP88150
         L     R1,LRDAT      GET LENGTH OF FILENAME              93238
         ICM   R1,8,ABLANKS  FILL BYTE IS ASCII BLANK         ESP88150
         MVCL  R14,R0        COPY AND PAD DSNAME              ESP88150
         L     R15,@MAKEBC   GET ECBDIC TRANSLATION              91315
         TR    INPUT(L'INPUT-1),0(R15)  MAKE EBCDIC              91315
         TR    INPUT(L'INPUT-1),UPPER   UPPER-CASE DSNAME        91315
         CLC   INPUT(L'INPUT-1),BLANKS  ALL BLANK ?              91080
         BNE   RCFFILEM      OK ?                                91078
         TIME  DEC           GET TIME/DATE IN PACKED FORM        91080
         STM   R0,R1,TEMP    STASH FOR UNPACK                    91080
         UNPK  INPUT+3(5),TEMP+5(3)                              91080
         UNPK  INPUT+10(7),TEMP(4)  GET TIME                     91080
         MVC   INPUT(3),=C'KER'  MAKE KERYYDDD.THHMMSS           91080
         MVC   INPUT+8(2),=C'.T'                                 91080
         MVI   INPUT+16,C' '                                     91080
RCFFILEM LA    R6,INPUT      PASS START OF NAME               ESP88150
         SUBCALL FILEMAKE    BUILD AND ALLOCATE FILE          ESP88150
RNUM3DON NI    FLAGS,255-FLG1  DON'T ALLOCATE AGAIN           ESP88150
         CLI   STATE,C'A'    REQUEST FAILED ?                 ESP88150
         BE    RCVABORT      YES; QUIT                        ESP88150
         MVI       STYPE,AY            ACK PACKET             ESP88150
         XC        LSDAT,LSDAT         NO DATA
         SUBCALL SPACK                                        ESP88150
         CLI       STATE,C'A'
         BE    RCVABORT                                       ESP88150
         MVC   OLDTRY,NUMTRY    KEEP NUMTRY FOR LATER         ESP88150
         XC        NUMTRY,NUMTRY       RESET TO ZERO
         NINC  N             NEXT PACKET NUMBER               ESP88150
         MVI       STATE,C'D'          DATA RECEIVE STATE
         B     RCVLOOP                                        ESP88150
RCFEOB   CLC   NUM,N                                          ESP88150
         BNE   RCVMISS       PACKET MISSING                   ESP88150
         MVI       STYPE,AY            ACK PACKET
         XC        LSDAT,LSDAT         NO DATA
         SUBCALL SPACK                                        ESP88150
         CLI       STATE,C'A'
         BE    RCVABORT                                       ESP88150
         MVI       STATE,C'C'          COMPLETE STATE
         NI    FLAGS,255-FGHAZED  RE-INIT REQUIRED            ESP88150
         NI    EDFLAGS,255-FGABIN  JUST IN CASE                  91310
         B     RCVLOOP                                        ESP88150
**********************************************************************
*        RECEIVE DATA PACKETS                                        *
**********************************************************************
RCVDATA  CLC   NUMTRY,MAXTRY  HAVE WE EXCEEDED OUR LIMIT?     ESP88150
         BNL   RCVABORT      YES; QUIT                        ESP88150
         INC   NUMTRY        SAVE INCREMENTED COUNTER         ESP88150
         SUBCALL RPACK                                        ESP88150
         IC    R0,RTYPE      GET RESPONSE                     ESP88150
         SUBCALL CHARLOOK,RCDTAB  AND PROCESS                 ESP88150
         SPACE 1                                              ESP88150
RCDTAB   CHARB AE,RCVDIED    MICRO DIED ?                     ESP88150
         CHARB AD,RCDDATA    DATA                             ESP88150
         CHARB AF,RCDFILE    FILENAME AGAIN ?                 ESP88150
         CHARB AA,RCDATTR    FILE ATTRIBUTES                     91091
         CHARB AZ,RCDEOF     END-FILE                         ESP88150
         CHARB AN,RCVSNAK    NACK-NACK                        ESP88150
         CHARB END,RCVELSE   OTHER                            ESP88150
         SPACE 1                                                 91091
RCDATTR  CLC   N,NUM         CORRECT PACKET NUMBER ?             91091
         BNE   RCVMISS       MISSING A PACKET                    91091
         LA    R1,RDAT       GET INPUT BUFFER                    91290
         ICM   R15,15,LRDAT   LOAD AND TEST INPUT LENGTH         91290
         BZ    RCDYATTR      NONE ?                              91290
         SH    R15,=H'2'     LESS SHORTEST SEQUENCE              91290
         AR    R15,R1        LAST POSSIBLE FIELD                 91290
         SLR   R14,R14       FOR IC                              91290
RCDATLUP IC    R14,1(,R1)    LOAD LENGTH FIELD                   91290
         S     R14,SPACE     LESS BIAS                           91290
         CLI   0(R1),AQUO    ASCII QUOTE ? (FILE TYPE)           91290
         BNE   RCDATBMP      NO; BUMP                            91290
         CLI   2(R1),AB      BINARY ?                            91290
         BE    RCDATTRB      YES                                 91091
         CLI   2(R1),AI      IMAGE ?                             91290
         BNE   RCDYATTR      JOIN COMMON                         91091
RCDATTRB OI    EDFLAGS,FGABIN USE BINARY MODE                    91310
         B     RCDYATTR      DONE SCANNING (FOR NOW)             91290
RCDATBMP BXLE  R1,R14,RCDATLUP  TRY AGAIN                        91290
RCDYATTR XC    NUMTRY,NUMTRY                                     91091
         XC    LSDAT,LSDAT                                       91091
         MVI   STYPE,AY      SEND ACK                            91091
         SUBCALL SPACK       SEND ACK                            91091
         NINC  N             EXPECT NEXT PACKET                  91091
         B     RCVLOOP       GO AGAIN                            91091
         SPACE 1                                              ESP88150
RCDDATA  CLC   N,NUM         CHECK FOR RIGHT PACKET           ESP88150
         BNE   RCDDATAF      NO; CHECK IF ONE OFF             ESP88150
         SUBCALL DECODE      PROCESS CURRENT DATA LINE        ESP88150
         BXH   R15,R15,RCVABORT  QUIT ON FILE ERROR           ESP88150
         MVI       STYPE,AY            ACK PACKET
         XC        LSDAT,LSDAT         NO DATA
         SUBCALL SPACK                                        ESP88150
         CLI       STATE,C'A'
         BE    RCVABORT                                       ESP88150
         MVC   OLDTRY,NUMTRY    SAVE NUMTRY'S VALUE IN OLDTRY ESP88150
         XC        NUMTRY,NUMTRY       RESET NUMTRY
         NINC  N             UP PACKET NUMBER                 ESP88150
         B     RCVLOOP                                        ESP88150
RCDDATAF CLC       OLDTRY,MAXTRY       CAN WE DO IT?
         BNL   RCVABORT      NO                               ESP88150
         INC   OLDTRY        INCREMENT THIS COUNTER           ESP88150
         L         R4,N
         BCTR      R4,0
         N     R4,=X'0000003F'                                ESP88150
         C         R4,NUM              NUM MUST EQUAL N-1
         BNE   RCVMISS       NO; TOO BAD                      ESP88150
         XC        NUMTRY,NUMTRY       RESET COUNTER TO ZERO
         MVI       STYPE,AY            ACK PACKET
         XC        LSDAT,LSDAT         NO DATA
         ST        R4,N                SET N TO N-1 TO RESEND PACKET
         SUBCALL SPACK                                        ESP88150
         CLI       STATE,C'A'
         BE    RCVABORT                                       ESP88150
         NINC  N                                              ESP88150
         B     RCVLOOP                 AND RETURN             ESP88150
RCDFILE  CLC   OLDTRY,MAXTRY  CAN WE DO IT?                   ESP88150
         BNL   RCVABORT      NO                               ESP88150
         INC   OLDTRY        SAVE INCREMENTED VALUE           ESP88150
         L         R4,N
         BCTR      R4,0                NEED VALUE OF N-1
         N     R4,=X'0000003F'                                ESP88150
         C         R4,NUM              N-1 MUST EQUAL NUM
         BNE   RCVMISS       MISSED ONE                       ESP88150
         XC        NUMTRY,NUMTRY       RESET TO ZERO
         XC        LSDAT,LSDAT         NO DATA
         MVI       STYPE,AY            ACK PACKET AGAIN
         ST        R4,N                SET N TO N-1 FOR NOW
OVRWRT   SUBCALL SPACK                                        ESP88150
         CLI       STATE,C'A'
         BE    RCVABORT                                       ESP88150
         NINC  N                                              ESP88150
         B     RCVLOOP                                        ESP88150
RCDEOF   CLC   N,NUM         ARE THEY EQUAL                   ESP88150
         BNE   RCVMISS       NO                               ESP88150
         TM    KEROUT+(DCBRECFM-IHADCB),DCBRECU  WYLBUR ?        89332
         BNO   RECEOFVF      NO                                  89332
         OI    CURLRECL,X'80'  SET LENGTH NEGATIVE - EOF SIGNAL  89332
         B     RECEOFW       GO TO WRITE LAST BLOCK              89332
RECEOFVF ICM   R0,3,CURLRECL  ANY PARTIAL BUFFER DATA ?          89332
         BNP   RCDEOF1       NO                               ESP88150
RECEOFW  SUBCALL WRITEX      WRITE FINAL RECORD                  89332
RCDEOF1  ICM   R0,15,LRDAT   ANY DATA ON THE EOF ?            ESP88150
         BNP   RCDEOF2       NO                               ESP88150
         CLI   RDAT,AD       DELETE DATASET ?                 ESP88150
         BNE   RCDEOF2       NO                               ESP88150
         MVC   CANCODE,RDAT  SAVE THE DELETE REQUEST          ESP88150
RCDEOF2  MVI       STYPE,AY            ACK THE PACKET
         XC        LSDAT,LSDAT         NO DATA
         SUBCALL SPACK                                        ESP88150
         MVC   OLDTRY,NUMTRY    SAVE NUMTRY'S VALUE HERE      ESP88150
         XC        NUMTRY,NUMTRY       AND RESET COUNTER
         NINC  N             NEXT PACKET NUMBER               ESP88150
         MVI       STATE,C'F'          TRY FOR ANOTHER FILE
         NI    EDFLAGS,255-FGABIN  JUST IN CASE                  91310
         B     RCVLOOP                                        ESP88150
**********************************************************************
*        RECEIVE ABORT PROCESS                                       *
**********************************************************************
RCVDIED  MVI   ERRNUM,ERCBDIED  SET MICRO DIED                ESP88150
         B     RCVABORT                                       ESP88150
RCVELSE  MVI   ERRNUM,ERCBTYPE  UNTIMELY OR INVALID PACKET TYPE P88150
*        B     RCVABORT      QUIT                             ESP88150
RCVABORT SUBCALL SERRPACK                                     ESP88150
RNOERRP  LA        R15,4               SET A NON-ZERO RETCODE
         B         RECRET              PREPARE TO LEAVE
**********************************************************************
*        RECEIVE COMPLETE PROCESS                                    *
**********************************************************************
RCVCOMP  SLR   R15,R15       RETCODE OF ZERO                  ESP88150
RECRET   NI    FLAGS,255-FGHAZED  RE-INIT REQUIRED            ESP88150
         NI    EDFLAGS,255-FGABIN  JUST IN CASE                  91310
         SUBEX RC=(R15)                                       ESP88150
         EJECT ,                                                 91293
**********************************************************************
*                                                                    *
*        DECODE A DATA PACKET AND FEED OUTPUT TO PTCHR               *
*              THIS PROCESS TRADES CPU OVERHEAD BUT DOES             *
*        OBVIATE THE NEED FOR A HUMONGOUS BUFFER (48*INPUT LEN)      *
*                                                                    *
**********************************************************************
DECODE   SUBENT ,            DATA IN LRPOS, LENGTH IN LRDAT      91293
         L     R6,LRPOS      GET (NEXT) DATA ADDRESS             91293
         ICM   R7,15,LRDAT   LOAD AND TEST LENGTH                91293
         BNP   DECOEXIT      NULL BUFFER IS UNDEFINED CASE ???   91293
         MVI   CHARHIGH,0    RESET BIT FLIPPERS                  91293
         MVI   CHARSPEC,0                                        91293
         MVI   CHARREP,0                                         91293
DECORINN LR    R4,R7         COPY RESIDUAL LENGTH                91293
         LA    R0,256        MAX TO PROCESS                      91293
         CR    R4,R0         TOO LONG ?                          91293
         BNH   *+6           NO                                  91293
         LR    R4,R0         ELSE DO ONLY ONE SEGMENT            91293
         BCTR  R4,0          LESS ONE FOR EXECUTE                91293
         SLR   R2,R2         CLEAR FUNCTION RESULT BYTE          91293
         LA    R1,1(R6,R4)   PRESET ENDING ADDRESS PAST END      91293
         EX    R4,DECORINT   TRANSLATE AND TEST                  91293
DECORIMV LR    R0,R1         COPY END OF STRING+1                91293
         LR    R5,R1         SAVE FOR NEXT TIME                  91293
         SR    R0,R6         COMPUTE LENGTH OF 'NORMAL' STRING   91293
         LR    R1,R6         SET STRING START ADDRESS            91293
         LR    R6,R5         SET STRING START FOR NEXT TIME      91293
         SR    R7,R0         SET RESIDUAL STRING LENGTH          91293
         LTR   R9,R0         ANY NORMAL STRING ?                 91293
         BZ    DECORINB      NO                                  91293
         OC    0(1,R1),CHARHIGH  ADJUST HIGH BIT                 91293
         XC    0(1,R1),CHARSPEC  FLIP X'40' IF NECESSARY         91293
         MVI   CHARHIGH,0    RESET HIGH BIT TOGGLE               91293
         MVI   CHARSPEC,0    RESET SECOND BIT TOGGLE             91293
         SLR   R4,R4                                             91293
         ICM   R4,1,CHARREP  GET AND TEST REPEAT COUNT           91293
         BZ    DECORIMT      MOVE TEXT                           91293
         STM   R1,R3,TEMPD   SAVE OVER MOVE                      91293
         MVC   RDAT(1),0(R1)  COPY CHARACTER TO BE REPEATED      91293
         EX    R4,DECORDAT   REPLICATE (ONE TOO MANY)            91293
         LR    R0,R4         COPY LENGTH                         91293
         LA    R1,RDAT       POINT TO REPEATED FIELD             91293
         SUBCALL PTCHR       PUT STRING TO OUTPUT                91293
         LM    R1,R3,TEMP    RESTORE                             91293
         SLR   R3,R3         LENGTH IS ZERO (REPLICATE PATTERN)  91293
         STC   R3,CHARREP    CLEAR REPEAT COUNT                  91293
         LA    R1,1(,R1)     SKIP PATTERN CHARACTER              91293
         SH    R9,=H'1'      LENGTH TO MOVE AFTER THIS           91293
         BNP   DECORINB      NO MORE                             91293
DECORIMT LR    R0,R9         COPY LENGTH                         91293
         SUBCALL PTCHR       MOVE REMAINING STRING TO OUTPUT     91293
DECORINB LTR   R2,R2         NEED TO PROCESS QUOTE ?             91293
         BZ    DECOTEST      NO; JUST CONTINUE MOVING            91293
         LA    R6,1(,R6)     SKIP OVER THE QUOTE                 91293
         BCTR  R7,0          ADJUST LENGTH                       91293
         B     *(R2)         BRANCH ACCORDING TO FUNCTION CODE   91293
         B     DECOQUOT      PROCESS SIMPLE QUOTE                91293
         B     DECOEIGT      PROCESS EIGHT-BIT PREFIX SEGMENT    91293
*        B     DECOREPT      PROCESS REPEAT SEQUENCE             91293
DECOREPT CH    R7,=H'2'      AT LEAST TWO MORE ?                 91293
         BL    DECOERR1      NO; PUT OUT AS IS                   91293
         IC    R3,0(,R6)     GET NEXT BYTE                       91293
         N     R3,=X'0000007F'  MASK                             91293
         S     R3,SPACE      ISOLATE LENGTH                      91293
         BNP   DECOERR1      BAD REPEAT COUNT                    91293
         STC   R3,CHARREP    STASH REPEAT COUNT                  91293
         B     DECOBUMP      BUMP OVER COUNT FIELD               91293
         SPACE 1                                                 91293
DECOEIGT LTR   R7,R7         AT LEAST ONE MORE BYTE ?            91293
         BNP   DECOERR1      NO; OUTPUT THIS ONE                 91293
         OI    CHARHIGH,X'80'  SET FOR HIGH BIT                  91293
         B     DECOTEST      SEE IF MORE                         91293
         SPACE 1                                                 91293
DECOQUOT LTR   R7,R7         AT LEAST ONE MORE ?                 91293
         BNP   DECOERR1      NO; DO THIS ONE                     91293
         OI    CHARSPEC,X'40'  SET TO FLIP SECOND BIT            91293
         SLR   R2,R2         RESET FOR TRT NON-INSERT            91293
         TRT   0(1,R6),QUOTABLE  IS NEXT CHARACTER SPECIAL ?     91293
         LTR   R2,R2         SPECIAL ?                           91293
         BZ    DECOQONE      NO; DO SINGLE BYTE                  91293
DECOPUTC MVI   CHARSPEC,0    ELSE CANCEL BIT FLIP                91293
DECOQONE SLR   R2,R2         FAKE TRT INDEX                      91293
         LA    R1,1(,R6)     POINT TO NEXT CHARACTER             91293
         B     DECORIMV      GO TO MOVE DATA                     91293
         SPACE 1                                                 91293
DECOERR1 BCTR  R6,0          BACKSPACE 1                         91293
         LA    R7,1(,R7)     FINAGLE LENGTH                      91293
         B     DECOPUTC                                          91293
DECOBUMP LA    R6,1(,R6)     ADJUST NEW POINTER                  91293
         BCTR  R7,0          AND SET RESIDUAL LENGTH             91293
         SPACE 1                                                 91293
DECOTEST LTR   R7,R7                                             91293
         BP    DECORINN      DO NEXT SEGMENT OF STRING           91293
DECORINX ST    R9,LRDAT      SET NEW DATA LENGTH                 91293
DECOEXIT SUBEX ,             RETURN TO CALLER                    91293
DECORDAT MVC   RDAT+1(0),RDAT  PROPAGATE REPEATED CHARACTER      91293
DECORINT TRT   0(0,R6),QUOTABLE  TRANSLATE AND TEST              91293
         EJECT
**********************************************************************
*                                                                    *
*    ROUTINE TO MOVE INCOMING DATA TO HOST FILE                      *
*        CALLED FROM DECODE WITH R1=STRING ADDRESS, R0=LENGTH        *
*                                                                    *
**********************************************************************
PTCHR    SUBENT ,            DATA IN R1, LENGTH IN R0         ESP88150
         LR    R6,R1         COPY DATA ADDRESS                   91293
         LTR   R7,R0         COPY LENGTH                         91293
         BNP   PTCHEXIT      HUH ?                               91293
PTCHLOOP L     R4,@IOBUF     POINT TO OUTPUT LINE             ESP88203
         LH    R5,CURLRMAX   GET MAXIMUM MOVABLE              ESP88150
         LH    R3,CURLRECL   GET AMOUNT ALREADY FILLED        ESP88150
         AR    R4,R3         OFFSET                           ESP88150
         SR    R5,R3         GET REMAINING LENGTH             ESP88150
         BP    PTCHFILL      OK; HAVE SOME                    ESP88150
PTCHRITE SUBCALL WRITEX      WRITE CURRENT RECORD             ESP88150
         OI    PTCHFLG,PTCHFIO  SHOW RECORD WRITTEN           ESP88150
         XC    TXTPOS,TXTPOS  CLEAR TEXT LENGTH                  91315
         B     PTCHLOOP      TRY AGAIN                        ESP88150
         SPACE 1                                              ESP88150
PTCHFILL LA    R3,256        SET MAXIMUM PROCESSABLE IN ONE TRY P88150
         CR    R7,R3         THAT MUCH LEFT ?                 ESP88150
         BNL   *+6           YES                              ESP88150
         LR    R3,R7         USE ONLY WHAT'S LEFT             ESP88150
         LTR   R3,R3         ANYTHING LEFT TO DO ?            ESP88150
         BNP   PTCHEXIT      DONE WITH THIS SEGMENT           ESP88150
         CR    R3,R5         MORE THAN WILL FIT IN OUTPUT ?   ESP88150
         BNH   *+6           NO                               ESP88150
         LR    R3,R5         TEST ONLY WHAT WILL FIT          ESP88150
         BCTR  R3,0          MAKE EXECUTE LENGTH              ESP88150
         SLR   R2,R2         CLEAR FOR IC (DOUBLES AS FLAG FOR BINARY)
         TM    EDFLAGS,FGASIS+FGABIN  BINARY MODE ?              91310
         BNZ   PTCHMOVE      YES; MOVE AS IS                  ESP88150
         LA    R1,1(R3,R6)   POINT TO END (IF NO MATCH)       ESP88150
         EX    R3,PTCHTRT    LOOK FOR CR OR LF                ESP88150
         LR    R3,R1         GET END LOCATION                 ESP88150
         SR    R3,R6         LESS START LOCATION              ESP88150
         BNP   PTCHWHAT      NOTHING TO MOVE                  ESP88150
         BCTR  R3,0          LENGTH FOR EXECUTE               ESP88150
         L     R15,@MAKEBC   GET EBCDIC TRANSLATION              91315
         EX    R3,PTCHTR     TRANSLATE TO EBCDIC              ESP88150
PTCHMOVE EX    R3,PTCHMVC    MOVE TO OUTPUT BUFFER            ESP88150
         LA    R3,1(,R3)     RESTORE TRUE LENGTH              ESP88150
         AR    R6,R3         UPDATE INPUT                     ESP88150
         SR    R7,R3         AND INPUT LENGTH                 ESP88150
PTCHPOS  INCH  TXTPOS,INC=(R3),WORK=(R14)  INCREASE TEXT LENGTH  91315
PTCHUPP  AH    R3,CURLRECL   UPDATE LENGTH USED               ESP88150
         STH   R3,CURLRECL   AND STASH BACK                   ESP88150
         MVI   PTCHFLG,0     RESET FLAG                       ESP88150
PTCHWHAT LTR   R2,R2         SPECIAL CONDITION ?              ESP88150
         BNP   PTCHLOOP      NO; CONTINUE MOVING              ESP88150
         LA    R6,1(,R6)     ADVANCE OVER CONTROL CHARACTER   ESP88150
         BCTR  R7,0          SET RESIDUAL LENGTH              ESP88150
         B     *(R2)         BRANCH BY CONDITION              ESP88150
         B     PTCHCR        CARRIAGE RETURN                  ESP88150
         B     PTCHLF        LINE-FEED                        ESP88150
         B     PTCHFF        FORM FEED FOUND                     91315
         B     PTCHBS        BACK-SPACE                          91315
         B     PTCHTB        TAB                                 91315
         SPACE 1                                                 91315
PTCHFF   XC    TXTPOS,TXTPOS CLEAR TEXT POSITION                 91315
         B     PTCHCTL       COPY CONTROL CHARACTER              91315
PTCHBS   SLR   R14,R14       CLEAR FOR ICM                       91315
         ICM   R14,3,TXTPOS  LOAD (PRESUMED) TEXT POSITION       91315
         BNP   PTCHCTL       NOT PLUS; JUST COPY                 91315
         BCTR  R14,0                                             91315
         STH   R14,TXTPOS                                        91315
PTCHCTL  SLR   R2,R2         SIGNAL NO CONTROL CHARACTER         91315
         LR    R14,R6        COPY CURRENT LOCATION               91315
         BCTR  R14,0         BACKSPACE TO CONTROL                91315
         L     R15,@MAKEBC   GET EBCDIC TRANSLATION              91315
         TR    0(1,R14),0(R15)  MAKE EBCDIC                      91315
         MVC   0(1,R4),0(R14)  MOVE TO RECORD                    91315
         LA    R3,1          SET LENGTH MOVED                    91315
         B     PTCHUPP       INCREASE OUTPUT COUNT               91315
         SPACE 1                                                 91315
PTCHTB   LA    R14,7         MAKE MASK                           91315
         LR    R15,R14       COPY                                91315
         X     R14,TXTPOS-2  MAKE EXCLUSIVE OR                   91315
         NR    R14,R15       KEEP ONLY LOW BITS                  91315
         LA    R3,1(,R14)    MAKE BLANKS TO INSERT               91315
         LH    R15,CURLRMAX  GET MAX LENGTH                      91315
         SH    R15,CURLRECL  LESS AMOUNT USED                    91315
         BNP   PTCHLOOP      IGNORE TAB AT END OF LINE           91315
         SLR   R2,R2         NO MORE CONTROL                     91315
         MIN   R3,(R15)      MOVE ONLY WHAT FITS                 91315
         LR    R14,R3                                            91315
         BCTR  R14,0         MAKE EXECUTE LENGTH                 91315
         EX    R14,PTCHMVBL  MOVE BLANKS                         91315
         B     PTCHPOS       UPDATE AMOUNT USED                  91315
         SPACE 1                                              ESP88150
PTCHCR   TM    PTCHFLG,PTCHFIO PRIOR WRITE ?                  ESP88150
         BZ    PTCHCRW       NO                               ESP88150
         TM    PTCHFLG,PTCHFCR  PRIOR CR ?                    ESP88150
         BZ    PTCHCR1       NO; JUST SET CR                  ESP88150
PTCHCRW  MVI   PTCHFLG,PTCHFCR+PTCHFIO  YES; SHOW WRITTEN     ESP88150
         B     PTCHRITE      AND GO TO WRITE BUFFER           ESP88150
PTCHCR1  OI    PTCHFLG,PTCHFCR  SHOW CR FOUND                 ESP88150
         B     PTCHLOOP      AND CONTINUE                     ESP88150
         SPACE 1                                              ESP88150
PTCHLF   TM    PTCHFLG,PTCHFIO PRIOR WRITE ?                  ESP88150
         BZ    PTCHLFW       NO                               ESP88150
         TM    PTCHFLG,PTCHFLF  PRIOR LF ?                    ESP88150
         BZ    PTCHLF1       NO; JUST SET LF                  ESP88150
PTCHLFW  MVI   PTCHFLG,PTCHFLF+PTCHFIO  YES; SHOW WRITTEN     ESP88150
         B     PTCHRITE      AND GO TO WRITE BUFFER           ESP88150
PTCHLF1  OI    PTCHFLG,PTCHFLF  SHOW LF FOUND                 ESP88150
         XC    TXTPOS,TXTPOS  CLEAR TEXT LENGTH                  91315
         B     PTCHLOOP      AND CONTINUE                     ESP88150
         SPACE 1                                              ESP88150
PTCHEXIT SUBEX ,             RETURN TO CALLER                 ESP88150
PTCHTRT  TRT   0(0,R6),PTCHTAB  LOOK FOR CR AND LF            ESP88150
PTCHMVC  MVC   0(0,R4),0(R6)  MOVE DATA TO OUTPUT BUFFER      ESP88150
PTCHTR   TR    0(0,R6),0(R15)   TRANSLATE INPUT TO EBCDIC        91315
PTCHMVBL MVC   0(0,R4),BLANKS  MOVE BLANKS INSTEAD OF TAB        91315
PTCHPAT  DC    256X'0'       TRANSLATE AND TEST TABLE         ESP88150
         ORG   PTCHPAT+X'0A'  LINE-FEED                       ESP88150
         DC    AL1(8)                                         ESP88150
         ORG   PTCHPAT+X'0D'  CARRIAGE-RETURN                 ESP88150
         DC    AL1(4)                                         ESP88150
         ORG   PTCHPAT+X'8A'  LINE-FEED IN WRONG PARITY       ESP88150
         DC    AL1(8)                                         ESP88150
         ORG   PTCHPAT+X'8D'  CARRIAGE-RETURN (WORDSTAR?)     ESP88150
         DC    AL1(4)                                         ESP88150
*ADDED   ORG   PTCHTAB+X'0C' FORM-FEED                           91315
*LATER   DC    AL1(12)       JUST RESET CHAR. COUNT              91315
*FOR     ORG   PTCHTAB+X'08'  BACKSPACE/TAB                      91315
*TABEXP  DC    AL1(16,20)                                        91315
         ORG   ,                                              ESP88150
         EJECT
**********************************************************************
*                                                                    *
*  DISK FILE WRITE ROUTINE WITH DEBUGGING CODE                       *
*                                                                    *
**********************************************************************
WRITEX   SUBENT ,  WRITE RECORD OF LENGTH IN CURLRECL         ESP88150
         TM    IOFLAGS,FGABEND  PRIOR I/O PROBLEM ?              91112
         BZ    WRITEXOK      NO                                  91112
         MVI   ERRNUM,ERCIOERR  SET I/O ERROR                    91112
         MVI   STATE,C'A'    ABORT                               91112
         B     WRITEXIT      AND QUIT                            91112
WRITEXOK SLR   R9,R9         PRESET MOVE LENGTH (FILL CHARACTER) 88150
         TM    EDFLAGS,FGASIS+FGABIN  BINARY MODE ?              91310
         BNZ   *+8           YES                              ESP88150
         ICM   R9,8,BLANKS   USE A BLANK AS FILLER IN TEXT MODE P88150
         LH    R6,CURLRECL   GET CURRENT RECORD LENGTH        ESP88150
         L     R3,@IORDW     POINT TO OUTPUT RDW              ESP88203
         LA    R0,4(,R3)     PRESET FOR FIXED RECFM DATA      ESP88203
         TM    KEROUT+(DCBRECFM-IHADCB),DCBRECU  WYLBUR EDIT ?   89332
         BNO   WRITNWYL      NO; CHECK V VS. F                   89332
         LTR   R6,R6         CHECK TYPE OF CALL                  89332
         BM    WRITWYLX      WRITE LAST BLOCK                    89332
         SLR   R2,R2                                             89332
         ICM   R1,15,DCMBUFAD  BEEN HERE BEFORE ?                89332
         BNZ   WRITWYLR      YES; ADD RECORD                     89332
         ICM   R2,3,DCBBLKSI+KEROUT-IHADCB GET BLOCKSIZE         89332
         GETMAIN R,LV=(R2)   GET A BUFFER (FOR MOVE MODE)        89332
         ST    R1,DCMBUFAD   SAVE BUFFER ADDRESS                 89332
         STH   R2,DCMBUFMX   SET MAXIMUM BUFFER SIZE             89332
         STCM  R2,12,DCMBUFLN  SET LENGTH IN USE (NONE)          89332
WRITWYLR LA    R1,4(,R3)     GET RECORD ADDRESS                  89332
         ST    R1,DCMRECAD   PASS THE RECORD ADDRESS             89332
         STH   R6,DCMRECLN   SET THE RECORD LENGTH               89332
WRITWYLP SERVCALL WCOMP,DECOMP    COMPRESS THE RECORD            89332
         CH    R15,=H'4'     CHECK RETURN CODE                   89332
         BL    WRITEXIT      DONE; GET NEXT RECORD               89332
*HUH*    BH    COMPERR       ERROR                               89332
         STH   R0,DCBLRECL-IHADCB+KEROUT   SET THE BLOCK SIZE    89332
         L     R0,DCMBUFAD   GET BUFFER ADDRESS BACK             89332
         PUT   KEROUT,(R0)   WRITE THE BLOCK                     89332
         BALS  R14,WRITCOUN  UPDATE SMF I/O COUNT                89337
         B     WRITWYLP      COMPRESS CURRENT RECORD AGAIN       89332
WRITWYLX SLR   R1,R1                                             89332
         C     R1,DCMBUFAD   DID WE EVER WRITE ANYTHING ?        89332
         BE    WRITEXIT      NO                                  89332
         ST    R1,DCMRECAD   SIGNAL CLOSE CALL                   89332
         STH   R1,DCMRECLN   ZERO THE RECORD LENGTH              89333
         SERVCALL WCOMP,DECOMP    SEE IF FINAL WRITE NEEDED      89332
         LTR   R0,R0         ANYTHING TO WRITE ?                 89332
         BNP   WRITWYLZ      NO                                  89333
         STH   R0,DCBLRECL-IHADCB+KEROUT   SET FINAL BLOCK       89332
         L     R0,DCMBUFAD   GET BUFFER ADDRESS BACK             89332
         PUT   KEROUT,(R0)   AND WRITE THE LAST BLOCK            89332
         BALS  R14,WRITCOUN  UPDATE SMF I/O COUNT                89337
WRITWYLZ L     R1,DCMBUFAD                                       89333
         LH    R0,DCMBUFMX                                       89332
         FREEMAIN R,A=(1),LV=(0)  FREE THE BUFFER                89332
         B     WRITEXIT      AND QUIT                            89332
         SPACE 1                                                 89332
WRITNWYL TM    KEROUT+(DCBRECFM-IHADCB),DCBRECV VARIABLE?     ESP88150
         BNZ   WRITVAR       YES; FINAGLE A BIT               ESP88150
         LH    R5,CURLRMAX   GET DESIRED RECORD LENGTH        ESP88150
         LA    R4,4(R3,R6)   GET DATA END+1                   ESP88203
         SR    R5,R6         NUMBER OF BYTES TO PAD           ESP88150
         BNP   WRITCOM       NONE                             ESP88150
         B     WRITPAD       ELSE PAD                         ESP88150
         SPACE 1                                              ESP88150
WRITVAR  L     R0,@IORDW     POINT TO INTENDED RDW            ESP88203
         LTR   R6,R6         ANY DATA ?                       ESP88307
         BNP   WRITVNDB      NO                               ESP88307
         TM    EDFLAGS,FGDEBG  DEBLANK ?                      ESP88307
         BZ    WRITVNDB      NO                               ESP88307
         TM    EDFLAGS,FGASIS+FGABIN  BINARY MODE ?              91310
         BNZ   WRITVNDB      YES; LEAVE IT ALONE              ESP88307
         LA    R15,4(R3,R6)  POINT PAST LAST BYTE             ESP88307
WRITVDEB BCTR  R15,0         BACKSPACE                        ESP88307
         CLI   0(R15),C' '   TRAILING BLANK ?                 ESP88307
         BNE   WRITVNDB      NO; DONE                         ESP88307
         BCT   R6,WRITVDEB   ELSE TRY IT AGAIN                ESP88307
WRITVNDB LTR   R6,R6         ANY OUTPUT ?                     ESP88150
         BP    WRITVARD      YES; BUILD RDW                   ESP88150
         LTR   R9,R9         BINARY MODE ?                    ESP88150
         BZ    WRITEXIT      YES; NO PAD (DON'T INTRODUCE STRAY DATA)
*        A ZERO LENGTH HERE CORRESPONDS TO A RECORD CONSISTING ONLY
*        OF A CARRIAGE RETURN (BLANK LINE) (W/ OR W/O LINE-FEED).
         LA    R4,4(,R3)     POINT TO BUFFER                  ESP88203
         LA    R5,2          BUILD TWO BLANKS                 ESP88150
         LR    R6,R5         AND SET RECORD LENGTH            ESP88325
WRITPAD  SLR   R8,R8         CLEAR SOURCE ADDRESS             ESP88150
         MVCL  R4,R8         MOVE PADDING                     ESP88150
WRITVARD CH    R6,CURLRMAX   CHECK LENGTH FOR VALIDITY        ESP88150
         BNH   *+8           OK                               ESP88150
         LH    R6,CURLRMAX   TRUNCATE (HUH? HOW DID WE GET HERE) 88150
         LA    R6,4(,R6)     ADD RDW LENGTH                   ESP88150
         SLL   R6,16         FINAGLE                          ESP88150
         STCM  R6,15,0(R3)   MAKE RDW                         ESP88203
WRITCOM  PUT   KEROUT,(R0)   WRITE A RECORD                   ESP88150
         BALS  R14,WRITCOUN  UPDATE SMF I/O COUNT                89337
         TM    DEBUG+DCBOFLGS-IHADCB,DCBOFOPN  IS IT OPEN?    ESP88150
         BZ    WRITEXIT      NO; NO DEBUG NEEDED              ESP88150
         MVC   WRKBUFF+2(7),=X'0000C4D6E4E340'  'DOUT '       ESP88150
         SLR   R6,R6         CLEAR FOR LENGTH                 ESP88150
         ICM   R6,3,CURLRECL LOAD AND TEST CURRENT LENGTH     ESP88150
         BP    *+8           OK ?                             ESP88150
         LA    R6,1          JUST ONE                         ESP88150
         LA    R0,=Y(L'WRKBUFF-9)  GET BUFFER LENGTH          ESP88150
         MIN   R6,(R0)       TRUNCATE IF TOO LONG             ESP88150
         BCTR  R6,0          LESS ONE FOR EXECUTE             ESP88150
         EX    R6,DBGMVC4    MOVE BUFFER TO PRINT LINE        ESP88150
         LA    R14,10(,R6)   TOTAL LENGTH                     ESP88150
         STH   R14,WRKBUFF   MAKE RDW                         ESP88150
         PUT   DEBUG,WRKBUFF  WRITE THE DEBUG LINE            ESP88150
WRITEXIT XC    CURLRECL,CURLRECL  RESET CURRENT SIZE          ESP88150
         SUBEX ,             RETURN                           ESP88150
WRITCOUN TM    MODEFG,FGNET  MULTI-TASKING MODE ?                89337
         BZR   R14           NO; NO I/O COUNTS                   89337
         L     R1,FAKECPPL+8  GET MTS ADDRESS BACK               89337
         ICM   R15,15,MTSNEXCP+8-MTS(R1)  GET OLD DISK OUT COUNT 89337
         AH    R15,=H'1'     ADD 1                               89337
         STCM  R15,15,MTSNEXCP+8-MTS(R1)  SAVE NEW DISK OUT COUNT
         BR    R14           RETURN                              89337
DBGMVC4  MVC   WRKBUFF+9(*-*),4(R3)                           ESP88203
         EJECT
**********************************************************************
*                                                                    *
*        ROUTINE TO PROCESS SEND PACKET REQUEST                      *
*                                                                    *
**********************************************************************
SPACK    SUBENT ,                                             ESP88150
         ICM   R6,15,SNLEN   GET LENGTH OF CURRENT PACKET        91091
         BZ    SPAKNEW       NONE; START A NEW PACKET            91091
         CLI   STYPE,AD      SENDING A DATA PACKET ?             91091
         BE    SPAKNAKD      JUST REPEAT PRIOR PACKET            91091
SPAKNEW  XC    SNLEN,SNLEN   SET FLAG - NO OLD DATA              91091
         SLR   R9,R9                                          ESP88150
         MVC       PHDR,SSOH           ADD SOH TO PACKET
         SLR   R3,R3         CLEAR WORK REGISTER                 91091
         SLR   R4,R4                                             91091
         IC    R4,RCHECK     GET CHECKSUM LENGTH                 91091
         TM    FLAGS,FGHAZED PAST INITIALIZATION ?               91091
         BNZ   *+8           YES                                 91091
         LA    R4,A1         SET VALUE OF ONE-BYTE CHECKSUM      91091
         S     R4,=A(A1)     REMOVE BIAS; GET ADDEND TO OHD 3    91091
         ICM   R3,15,N       CHECK IF N IS VALID                 91091
         BM    SPAKBNUM      FAIL INVALID NUMBER                 91091
         C     R3,O100       SEE IF IS <= OCTAL 100              91091
         BNH   SPAKYNUM      YES                                 91091
SPAKBNUM MVI   ERRNUM,ERCBMSG#  ILLEGAL MESSAGE NUMBER           91091
         B     SPAKABRT      ABORT                               91091
SPAKYNUM A     R3,SPACE      OFFSET THE PACKET NUMBER            91091
         STC   R3,PNUM       PLACE INTO PACKET                   91091
         IC    R3,STYPE      LOAD PACKET TYPE                    91091
         CLM   R3,1,=AL1(AA) ASCII 'A'                           91091
         BL    SPAKBTYP      MUSTN'T BE LESS THAN THIS           91091
         CLM   R3,1,=AL1(AZ) ASCII 'Z'                           91091
         BNH   SPAKGTYP      OK IF ALPHA                         91091
SPAKBTYP MVI   ERRNUM,ERCBTYPE  ILLEGAL PACKET TYPE              91091
         B     SPAKABRT      ABORT                               91091
SPAKGTYP STC   R3,PTYPE      PLACE INTO PACKET                   91091
         L     R6,LSDAT      LOAD DATA LENGTH                    91091
         CLI   STYPE,AD      SENDING A DATA PACKET ?             91091
         BNE   SPAKCHKS      NO; CHECK SIZE                      91091
         TM    CAPAFG,FGRLONG  SENDING LONG PACKETS ?            91091
         BO    SPAKLONG      YES                                 91091
SPAKCHKS C     R6,SIZE       NEED DATA SIZE <= SPSIZ-3-(RCHECK-1)
         BNH   SPAKSZOK                                       ESP88150
SPAKBLEN MVI   ERRNUM,ERCBPACK  DATA SIZE EXCEEDS MAX LIMIT   ESP88150
         B     SPAKABRT      ABORT                            ESP88150
         SPACE 1                                                 91091
*    PROCESS LONG (0-1024) BYTE PACKETS HERE                     91091
SPAKLONG SH    R6,=H'3'      LENGTH INCLUDES OVERHEAD            91091
         C     R6,SIZE       WOULD THIS FIT IN A SHORT PACKET ?  91091
         BH    SPAKLONK      NO                                  91091
         MVC   PDATA(&OPPAKSZ),PLDATA  MOVE TO EXPECTED POSITION 91091
         B     SPAKSZOK      AND USE SHORT PACKET                91091
SPAKLONK LA    R3,1(R4,R6)   LEN=DAT.LEN+L'CHECKSUM              91091
         LA    R1,1+3+2(,R3)   FULL LENGTH WITH SOH /.../CR/XON  91290
         CH    R1,LONGLEN    NOT TOO LONG ?                      91091
         BH    SPAKBLEN      TOO BAD                             91091
         SLR   R2,R2                                             91091
         D     R2,=F'95'     PROTOCOL SIZE                       91091
         A     R3,SPACE      BIAS QUOTIENT                       91091
         A     R2,SPACE      BIAS REMAINDER                      91091
         STC   R2,PLLEN2     STASH REMAINDER                     91091
         STC   R3,PLLEN1     STASH QUOTIENT                      91091
         MVI   PLEN,ASP      MAKE ZERO LENGTH PACKET             91091
         SLR   R0,R0                                             91091
         LA    R14,PLEN      POINT TO FIRST BYTE                 91091
         LA    R15,PLCHK-PLEN  NUMBER TO DO                      91091
SPAKLLUP IC    R0,0(,R14)    LOAD NEXT BYTE                      91091
         AR    R9,R0         ADD TO CHECKSUM                     91091
         LA    R14,1(,R14)   NEXT BYTE                           91091
         BCT   R15,SPAKLLUP  GO FOR MORE                         91091
         LR    R8,R9         COPY SUM                            91091
         N     R9,=X'000000C0'  MOD 192                          91091
         SRL   R9,6                                              91091
         AR    R9,R8         PLUS ORIGINAL                       91091
         N     R9,=X'0000003F'  MOD 64                           91091
         A     R9,SPACE      OFFSET                              91091
         STC   R9,PLCHK      STASH IT                            91091
         SLR   R9,R9         RESET CHECKSUM ACCUMULATOR          91091
         L     R6,LSDAT      RESTORE TRUE DATA LENGTH (+LLEN/CK) 91290
         B     SPAKCOMM      JOIN COMMON                         91091
         SPACE 1                                                 91091
SPAKSZOK LA    R1,ASP+3(R4,R6)  2 + L'CHECKSUM + BIAS + DATA LENGTH
         STC   R1,PLEN       SET PACKET LENGTH                   91091
         SPACE 1                                                 91091
SPAKCOMM CH    R4,=H'2'      THREE BYTE CHECKSUM ?               91091
         BE    SPAKCHK3      YES; DON'T ADD                      91091
         LA    R1,PLEN       POINT TO LENGTH BYTE                91091
         LA    R15,PDATA-1(R6)  GET LAST BYTE                    91091
         LA    R14,1         SET INCREMENT                       91091
         SLR   R3,R3         USE TO HOLD DATA                 ESP88150
SPAKCLUP IC    R3,0(,R1)     GET CHARACTER                       91091
         AR    R9,R3         ADD TO CHECKSUM                     91091
         BXLE  R1,R14,SPAKCLUP  REPEAT FOR ALL                   91091
         LR    R8,R9         SAVE CHECKSUM                       91091
         SLL   R4,2          CONVERT CHECKSUM ADDEND TO OFFSET   91091
         B     *+4(R4)       BRANCH BY TYPE                      91091
         B     SPAKCHK1      USE 1-BYTE                          91091
         B     SPAKCHK2                                          91091
SPAKCHK3 LA    R0,3(,R6)     SET DATA LENGTH                  ESP88150
         SUBCALL CHK3,PLEN   CHECKSUM STARTS AT PACKET LENGTH FIELD
         LA    R1,PDATA(R6)  NEXT AVAILABLE BYTE IN PACKET    ESP88150
         MVC   0(3,R1),TEMP+4+1  MOVE 3-BYTE CHECKSUM         ESP88150
         LA    R6,2(,R6)     FIX LENGTH                       ESP88150
         B     SPAKCHKM      GO TO COMMON                     ESP88150
SPAKCHK2 SLR   R8,R8         CLEAR FOR DIVIDE                 ESP88150
         D     R8,O100       ISOLATE LOW SIX BITS IN R8       ESP88150
         N     R9,=X'0000003F'  KILL EXCESS HIGH BITS         ESP88150
         A     R8,SPACE      MAKE PRINTABLE ASCII             ESP88150
         A     R9,SPACE      DITTO                            ESP88150
         STC   R9,PDATA(R6)  STASH HIGH BYTE                  ESP88150
         LA    R6,1(,R6)     ADVANCE                          ESP88150
         STC   R8,PDATA(R6)  STASH SECOND BYTE                ESP88150
         B     SPAKCHKM      GO TO COMMON                     ESP88150
SPAKCHK1 N     R9,=X'000000C0'  GET MOD 192                   ESP88150
         SRL   R9,6          SHIFT TO UNITS POSITION          ESP88150
         AR    R9,R8         ADD ORIGINAL SUM BACK IN            91091
         N         R9,=X'0000003F'     GET MOD 64 OF CHECKSUM
         A         R9,SPACE            ADD OFFSET
         STC       R9,PDATA(R6)        ADD CHECKSUM AFTER DATA
SPAKCHKM LA    R6,1(,R6)     MOVE POINTER                     ESP88150
         IC        R9,SEOL             ADD SEND END OF PACKET CHAR
         STC       R9,PDATA(R6)
         LA    R6,5(,R6)     DATA LENGTH+5                       91091
         TM    IOFLAG2,FGVTAM  VTAM ?                         ESP88150
         BZ    SPAKTRAN      NO; TCAM ADDS X-ON ALL BY ITSELF ESP88150
         LA    R1,SNDPKT(R6)  POINT PAST LAST BYTE            ESP88102
         MVI   0(R1),X'11'   ADD X-ON THAT VTAM/NTO DOESN'T   ESP86307
         LA    R6,1(,R6)     ADJUST LENGTH                    ESP86307
         SPACE 1                                                 91091
*    TRANSLATE OUTGOING PACKET TO EBCDIC                         91091
SPAKTRAN LA    R15,SNDPKT    POINT TO OUTPUT PACKET START        91091
         LTR   R14,R6        COPY LENGTH                         91091
         BNP   SPAKBLEN      INVALID PACKET LENGTH               91091
         L     R8,@MAKEBC    GET EBCDIC TRANSLATION              91315
         LA    R0,256        MAXIMUM TRANSLATABLE                91091
SPAKTRLP LR    R1,R0         SET LENGTH                          91091
         CR    R1,R14        COMPARE TO RESIDUAL LENGTH          91091
         BNH   *+6           MORE THAN ONE SEGMENT               91091
         LR    R1,R14        PARTIAL SEGMENT LENGTH              91091
         BCTR  R1,0          SET LENGTH FOR EXECUTE              91091
         EX    R1,SPAKTRA6   TRANSLATE                           91091
         AR    R15,R0        NEXT SEGMENT ADDRESS                91091
         SR    R14,R0        REMAINING LENGTH                    91091
         BP    SPAKTRLP                                          91091
         CLI   STYPE,AD      SENDING A DATA PACKET ?             91091
         BNE   SPAKNAKD      NO; DO IT OVER AND OVER             91091
         ST    R6,SNLEN      ELSE RE-USE EXISTING PACKET ON NAK  91091
         SPACE 1                                                 91091
*    COME HERE TO RETRANSMIT A PRIOR TRANSLATED PACKET (DATA ONLY)
*                                                                91091
SPAKNAKD LR    R1,R6         COPY TEXT LENGTH                 ESP88150
         MIN   R1,=Y(120),TYPE=H  NOT TOO LONG                   91091
         BCTR  R1,0          LESS ONE FOR EXECUTE             ESP88150
         TM    DEBUG+DCBOFLGS-IHADCB,DCBOFOPN  IS IT OPEN?    ESP88150
         BZ    SPAKNDEB                                       ESP88150
         MVC   SNDBUG+2(7),=X'0000E2C5D5C440'  SET PREFIX AND 'SEND'
         LA    R1,10(,R1)    4-LEN, 5-TEXT, 1-EX              ESP88150
         STCM  R1,3,SNDBUG   RDW LENGTH                          91091
         PUT   DEBUG,SNDBUG                                      91091
SPAKNDEB WTERM SNDPKT,(R6),MODE=CONTROL  SEND PACKET             91091
         BXLE  R15,R15,SPRET  RETURN IF NO ERROR              ESP88150
         SRL   R15,1         FIX CODE FOR DEBUGGING           ESP88150
         MVI   ERRNUM,ERCBDIED  SET MICRO DIED                ESP88150
SPAKABRT MVI   STATE,C'A'    ABORT ON THIS                    ESP88150
SPRET    SUBEX RC=(R15)                                       ESP88150
         SPACE 1                                                 91091
SPAKTRA6 TR    SNDPKT-SNDPKT(0,R15),0(R8)   SEND IN EBCDIC       91315
         EJECT
**********************************************************************
*                                                                    *
*        ROUTINE TO PROCESS RECEIVE PACKET REQUEST                   *
*                                                                    *
**********************************************************************
RPACK    SLR   R15,R15       SET TGET REQUESTED               ESP88150
RPACKGOT SUBENT ,                                             ESP88150
         LTR   R3,R15        BUFFER ALREADY READ ?            ESP88150
         BP    RPAKIBUF      YES; SKIP TGET                   ESP88150
         SUBCALL TTGET       GET AN INPUT PACKET              ESP88150
         LR    R3,R1         SAVE LENGTH READ                 ESP85142
         BXLE  R15,R15,RPAKIBUF  SKIP IF NO ERROR             ESP88150
         SLR   R15,1         FIX ERROR CODE FOR DEBUGGING     ESP88150
RPAKABRT MVI   RTYPE,AE      SET AN ERROR                     ESP88108
         B     RPAKRET       RETURN WITH ERROR                ESP88150
RPAKIBUF L     R8,@INCOME    LOAD INPUT BUFFER ADDRESS        ESP88150
         XC    TEMP+8(4),TEMP+8  CLEAR                        ESP88150
         MVC   TEMP+8+3(1),RCHECK  SAVE CHECKSUM TYPE         ESP88150
         USING RECPKT,R8     DECLARE IT                       ESP88150
         TM    DEBUG+DCBOFLGS-IHADCB,DCBOFOPN  IS IT OPEN?    ESP88150
         BZ    RPAKNDEB                                       ESP88150
         MVC   WRKBUFF+2(7),=X'0000D9D7C1D240'  'RPAK '       ESP88150
         LTR   R14,R3        COPY LENGTH                      ESP88279
         BNP   RPAKPDEB      PUT DEBUG - NO TEXT              ESP88279
         MIN   R14,=Y(120),TYPE=H   NOT TOO LONG ?               91077
         LA    R1,10(,R14)   MAKE MESSAGE LENGTH                 91077
         STH   R1,WRKBUFF    SET RDW LENGTH                      91077
         BCTR  R14,0         EXECUTE LENGTH                   ESP88279
         EX    R14,DBGMVC2    MOVE INPUT DATA                 ESP88150
RPAKPDEB PUT   DEBUG,WRKBUFF  PRINT IT                        ESP88150
RPAKNDEB LTR   R14,R3        COPY LENGTH                      ESP88150
         BNP   RPAKABRT      INVALID PACKET LENGTH            ESP88150
         CH    R3,=H'4'      LONG ENOUGH ?                    ESP88279
         BL    RPAKNQIT      NO                               ESP88279
         CLC   =C'QUIT',RECPKT  USER TRYING TO GET OUT ?      ESP88150
         BE    RPAKABRT      YES; ABORT                       ESP88150
RPAKNQIT LR    R15,R8        COPY START ADDRESS                  91077
         L     R9,@MAKASC    GET ASCII TRANSLATION               91315
         LA    R0,256        MAXIMUM TRANSLATABLE                91077
RPAKTRLP LR    R1,R0         SET LENGTH                          91077
         CR    R1,R14        COMPARE TO RESIDUAL LENGTH          91077
         BNH   *+6           MORE THAN ONE SEGMENT               91077
         LR    R1,R14        PARTIAL SEGMENT LENGTH              91077
         BCTR  R1,0          SET LENGTH FOR EXECUTE              91077
         EX    R1,RPAKTRAA   TRANSLATE                           91077
         AR    R15,R0        NEXT SEGMENT ADDRESS                91077
         SR    R14,R0        REMAINING LENGTH                    91077
         BP    RPAKTRLP                                          91077
         SPACE 1                                                 91077
         NI    FLAGS,X'FF'-FGNAK  ASSUME MICRO'LL NAK-NOT RPACK P88150
         LA    R7,RECPKT(R3) LAST BYTE +1 OF PACKET           ESP88150
         BCTR  R7,0          END ADDRESS                      ESP88150
         LA    R6,1          FOR BXH LOGIC                    ESP88150
         TM    IOFLAG2,FGVTAM  RUNNING UNDER VTAM ?           ESP88150
         BZ    RPAKTCAM      NO SOH PREFIX UNDER TCAM         ESP88150
RPAKTRY  CLC   RSOH,RECPKT   IS IT START OF HEADER ?          ESP88150
         BE    RPAKREAD                YES; SO FAR, SO GOOD
         BXLE  R8,R6,RPAKTRY NO; TRY AGAIN IF MORE            ESP88150
         MVI   ERRNUM,ERCNOSOH  NO "SOH" ERROR                ESP88150
         B     RPAKBADP
RPAKREAD BXH   R8,R6,RPAKBLEN SKIP OVER THE SOH               ESP88150
RPAKTCAM ST    R8,@INCOME    SAVE FOR TYPE 3 BLOCKCHECK       ESP88150
RPAKRTRY SLR   R5,R5         CHECKSUM REGISTER                ESP88150
         XC    TMPDATA,TMPDATA  CLEAR IT                      ESP88150
         XC    RDAT(256),RDAT  CLEAR LONG DATA AREA, ALSO        91080
         CLI   RECPKT,ASP    LEN=0 ?                             91077
         BNE   RPAKNLNG      NO; NOT LONG                        91077
         CLI   RECPKT+2,AD   DATA PACKET ?                       91077
         BNE   RPAKNLNG      NO                                  91077
         TM    CAPAFG,FGRLONG  OK TO PROCESS LONG ?              91077
         BNZ   RPAKLONG      YES; DO SEPARATELY                  91077
RPAKNLNG BALS  R14,RPAKGETC  PROCESS LENGTH BYTE              ESP88150
         SH    R2,=Y(ASP+3)  LENGTH OF DATA WITH 1-BYTE CHECKSUM 88150
         BM    RPAKBLEN      INVALID - NAK                    ESP88150
         BZ    RPAKLONE      ZERO - CHECKSUM LENGTH IS ONE    ESP88150
         LA    R2,A1(,R2)    ELSE ADD A ONE                   ESP88150
         S     R2,TEMP+8     LESS CHECKSUM TYPE               ESP88150
         BNM   RPAKLST       STORE IT                         ESP88150
RPAKLONE MVI   TEMP+8+3,A1   SET FOR ONE-BYTE CHECKSUM        ESP88150
         SLR   R2,R2         ZERO DATA LENGTH                 ESP88150
RPAKLST  ST    R2,LRDAT      STASH NEW LENGTH (AND CLEAR HIGH BYTES)
         BALS  R14,RPAKNEXT  GET AND PROCESS NEXT BYTE        ESP88150
         S     R2,SPACE      SUBTRACT THE ' '                 ESP88150
         ST    R2,NUM        NUM := RECEIVED PACKET NO.       ESP88150
         BALS  R14,RPAKNEXT  SPACE TO MESSAGE TYPE            ESP88150
         STC   R2,RTYPE      PUT INTO RTYPE                   ESP88150
         SPACE 1                                                 91077
*    COMMON SHORT/LONG PACKET CODE                               91077
RPAKDATA LA    R0,1(,R8)     POINT TO DATA                       91077
         ST    R0,LRPOS      SAVE DATA START POSITION            91077
         ICM   R4,15,LRDAT   LOAD AND TEST DATA LENGTH        ESP88150
         BNP   RPAKFIN       DONE; VERIFY CHECKSUM            ESP88150
         SLR   R9,R9         CLEAR TMPDATA INDEX              ESP88150
         LA    R3,&OPPAKSZ-2  GET LENGTH OF TEXT PACKET          91081
         MIN   R3,(R4)       LESSER OF TWO EVILS                 91081
         SR    R4,R3         SET RESIDUAL, UNMOVED LENGTH        91083
RPAKCOPY BALS  R14,RPAKNEXT  GET NEXT DATA BYTE               ESP88150
         STC   R2,TMPDATA(R9)  PLACE IN INTERMEDIATE BUFFER   ESP88150
         LA    R9,1(,R9)     ADD ONE                          ESP88150
         BCT   R3,RPAKCOPY   TRY NEXT BYTE                       91081
         LTR   R4,R4         ANY MORE TO DO ?                    91081
         BNP   RPAKFIN       DONE; VERIFY CHECKSUM            ESP88150
RPAKSUMC BALS  R14,RPAKNEXT  GET NEXT DATA BYTE               ESP88150
         BCT   R4,RPAKSUMC   ADD NEXT BYTE TO CHECKSUM           91081
RPAKFIN  ST    R5,TEMP       SAVE CHARACTER TOTAL             ESP88150
         BALS  R14,RPAKNEXT  LOAD CHECKSUM BYTE               ESP88150
         L     R5,TEMP       RELOAD THE CHECKSUM              ESP88150
         CLI   TEMP+8+3,A2   TWO-BYTE CHECKSUM ?              ESP88150
         BL    RPAKCHK1      NO; USE ONE-BYTE                 ESP88150
         BE    RPAKCHK2      YES; 2-BYTE                      ESP88150
         ICM   R2,7,RECPKT   GET PACKET'S CHECKSUM            ESP88150
         CLM   R2,2,RSOH     CHOPPED PACKET ?                 ESP88150
         BE    RPAKREAD      YES ?                            ESP88150
         CLM   R2,1,RSOH     CHOPPED PACKET ?                 ESP88150
         BE    RPAKREAD      YES; RETRY                       ESP88150
         ICM   R4,15,LRDAT   GET DATA LENGTH                  ESP88150
         L     R1,@INCOME    GET START OF PACKET              ESP88150
         L     R0,LRPOS      GET DATA POSITION                   91077
         SR    R0,R1         LESS START (OVERHEAD)               91077
         AR    R0,R4         PLUS DATA LENGTH                    91077
         SUBCALL CHK3,PLEN-PLEN(R1)  PASS CHECK START         ESP88150
         ICM   R5,7,TEMP+4+1  GET CALCULATED CHECKSUM         ESP88150
         B     RPAKCHKR      SEE IF IT IS CORRECT             ESP88150
RPAKCHK2 ICM   R2,3,RECPKT   LOAD TWO-BYTE CHECKSUM           ESP88150
         CLM   R2,1,RSOH     CHOPPED PACKET ?                 ESP88150
         BE    RPAKREAD      YES; RETRY                       ESP88150
         CLM   R2,1,REOL     END-DATA ?                       ESP88150
         BE    RPAKCHKB      YES; TREAT AS 1-BYTE CHECKSUM    ESP88150
         SLR   R4,R4         CLEAR FOR DIVIDE                 ESP88150
         D     R4,O100       ISOLATE LOW SIX BITS IN R4       ESP88150
         N     R5,=X'0000003F'  CREAM EXCESS BITS IN QUOTIENT ESP88150
         A     R4,SPACE      ADD ASCII SPACE                  ESP88150
         A     R5,SPACE      DITTO                            ESP88150
         SLL   R5,8          MOVE LEFT                        ESP88150
         OR    R5,R4         COMBINE                          ESP88150
RPAKCHKR CR    R5,R2         MATCHES ?                        ESP88150
         BE    RPAKCHKM      CHECK RESULT                     ESP88150
RPAKCHKB MVI   TEMP+8+3,A1   SET TO TRY AGAIN AS 1-BYTE CHECKSUM 88150
         L     R8,@INCOME    LOAD RESTART ADDRESS             ESP88150
*THINK   TM    FLAGS,FGHAZED INITIALIZATION COMPLETE ?        ESP88150
*IT-OVER BNZ   RPAKCHKM      YES; FAIL                        ESP88150
         B     RPAKRTRY      RESTART                          ESP88150
RPAKCHK1 N     R5,=X'000000C0'     GET MOD 192                ESP88150
         SRL   R5,6          GET MOD 64                       ESP88150
         A     R5,TEMP       ADD THE TWO VALUES               ESP88150
         N     R5,=X'0000003F' GET MOD 64                     ESP88150
         A     R5,SPACE      ADD OFFSET                       ESP88150
RPAKCHKM CR    R5,R2         COMPUTED VS RECEIVED CHECKSUM ?  ESP88150
         BE    RPAKRET0      WHOOPPEE !!!                     ESP88150
RPAKCHKN TM        DEBUG+(DCBOFLGS-IHADCB),DCBOFOPN
         BZ    RPAKBSUM                                       ESP88150
         ST    R5,TEMP                                        ESP85142
         MVC   WRKBUFF(CHKSUMEL),CHKSUMER  MOVE PATTERN MESSAGE P88150
         UNPK  WRKBUFF+29(7),TEMP+1(4)  UNPACK                ESP88150
         TR    WRKBUFF+29(6),TRHEXTAB   MAKE VISIBLE          ESP88150
         MVI   WRKBUFF+35,C','                                ESP88150
         ST    R2,TEMP                                        ESP85142
         UNPK  WRKBUFF+46(7),TEMP+1(4)  UNPACK                ESP88150
         TR    WRKBUFF+46(6),TRHEXTAB   MAKE VISIBLE          ESP88150
         MVI   WRKBUFF+52,C'.'                                ESP88150
         PUT       DEBUG,WRKBUFF
         LA    R0,132                                         ESP85142
         LA    R3,4(R3,R3)   GET REQUIRED LENGTH FOR INPUT DATA P88150
         CR    R0,R3         USE THE SMALLER OF THE TWO       ESP85142
         BNH   *+6                                            ESP85142
         LR    R0,R3         USE SHORTER VALUE                ESP85142
         STH   R0,WRKBUFF                                     ESP85142
         LA    R0,64                                          ESP85142
         LA    R14,WRKBUFF+4                                  ESP85142
         L     R15,@INCOME                                    ESP88150
RPDEBALP PACK  0(1,R14),0(1,R15)  MOVE HIGH                   ESP85142
         MVC   1(1,R14),0(R15)    AND LOW                     ESP85142
         OC    0(2,R14),=C'00'    FORCE ZONES                 ESP85142
         LA    R14,2(,R14)                                    ESP85142
         LA    R15,1(,R15)                                    ESP85142
         BCT   R0,RPDEBALP                                    ESP85142
         TR    WRKBUFF+4(128),TRHEXTAB                        ESP85142
         PUT   DEBUG,WRKBUFF                                  ESP85142
RPAKBSUM MVI   ERRNUM,ERCBSUM  BAD CHECKSUM ERROR             ESP88150
RPAKBADP MVI       RTYPE,AN            RETURN A NAK
         OI    FLAGS,FGNAK   RPACK NAK'ED THE PACKET          ESP88150
         LA    R15,4         SET ERROR CODE                   ESP88150
         B     RPAKRET                                        ESP88150
         SPACE 1                                              ESP88150
RPAKNEXT BXLE  R8,R6,RPAKGETC  GET NEXT BYTE IF ONE SENT      ESP88150
RPAKBLEN MVI   ERRNUM,ERCBCNT  SET BAD LENGTH (L-BYTE OR TGET LEN)
         B     RPAKBADP      NAK                              ESP88150
RPAKGETC SLR   R2,R2         CLEAR FOR IC                     ESP88150
         IC    R2,RECPKT     GET NEXT BYTE                    ESP88150
         CLM   R2,1,RSOH     START OF PACKET AGAIN ?          ESP88150
         BE    RPAKREAD      YES; RESTART PROCESSING          ESP88150
         AR    R5,R2         ADD TO CHECKSUM                  ESP88150
         BR    R14           RETURN TO CALLER                 ESP88150
         SPACE 1                                                 91077
*    PROCESS LONG (95-9094) PACKETS                              91077
RPAKLONG BALS  R14,RPAKGETC  GET LENGTH INTO CHECKSUM            91080
         BALS  R14,RPAKNEXT  GET PACKET NUMBER                   91080
         S     R2,SPACE      SUBTRACT THE ' '                    91077
         ST    R2,NUM        NUM := RECEIVED PACKET NO.          91077
         BALS  R14,RPAKNEXT  GET PACKET TYPE                     91077
         STC   R2,RTYPE      PUT INTO RTYPE                      91077
         BALS  R14,RPAKNEXT  GET FIRST LENGTH BYTE               91080
         LR    R4,R2         SAVE IN R4                          91080
         BALS  R14,RPAKNEXT  GET SECOND LENGTH BYTE              91080
         LR    R3,R2         SAVE IN R3                          91080
         ST    R5,TEMP       SAVE CHECKSUM                       91077
         BALS  R14,RPAKNEXT  GET THE CHECKSUM BYTE               91077
         LR    R15,R5        SAVE PACKET CHECKSUM                91080
         L     R5,TEMP       RELOAD CHECKSUM FOR TESTING         91080
         N     R5,=X'000000C0'     GET MOD 192                   91077
         SRL   R5,6          GET MOD 64                          91077
         A     R5,TEMP       ADD THE TWO VALUES                  91077
         N     R5,=X'0000003F' GET MOD 64                        91077
         A     R5,SPACE      ADD OFFSET                          91077
         CR    R5,R2         COMPUTED VS RECEIVED CHECKSUM ?     91077
         BNE   RPAKCHKN      BAD CHECKSUM                        91080
         LR    R5,R15        RELOAD THE PACKET CHECKSUM          91080
         S     R3,SPACE      MAKE TRUE NUMBER (LENLX2)           91077
         BM    RPAKBLEN      TOO BAD                             91077
         S     R4,SPACE      MAKE TRUE NUMBER (LENLX1)           91077
         BM    RPAKBLEN                                          91077
         MH    R4,=H'95'     MAKE ROOM FOR LOW PORTION           91080
         AR    R4,R3         MAKE TRUE LENGTH                    91080
         S     R4,TEMP+8     SUBTRACT CHECKSUM-SPACE             91080
         LA    R4,A0(,R4)    ALLOW FOR ZONE IN CHECKSUM          91081
         ST    R4,LRDAT      SAVE DATA LENGTH                    91077
         B     RPAKDATA      GO TO COMMON DATA PROCESSING        91077
         SPACE 1                                              ESP88150
RPAKRET0 SLR   R15,R15       CLEAR THE RETURN CODE            ESP88150
RPAKRET  ICM   R7,15,LRDAT   ANY DATA TO RETURN ?             ESP88150
         BNP   RPAKEXIT      NO                               ESP88150
         CLI   RTYPE,AD      DATA PACKET ?                       91293
         BE    RPAKEXIT      YES; DEFER EXPANSION                91293
         L     R6,LRPOS      GET STRING START                    91077
         SLR   R9,R9         SET LENGTH OF OUTPUT             ESP88150
         LA    R8,RDAT       GET RECEIVE BUFFER               ESP88150
         MVI   CHARHIGH,0    RESET BIT FLIPPERS               ESP88150
         MVI   CHARSPEC,0                                     ESP88150
         MVI   CHARREP,0                                      ESP88150
DESTRINN LR    R4,R7         COPY RESIDUAL LENGTH             ESP88150
         LA    R0,256        MAX TO PROCESS (KEEP FOR LONG PACKETS)
         CR    R4,R0         TOO LONG ?                       ESP88150
         BNH   *+6           NO                               ESP88150
         LR    R4,R0         ELSE DO ONLY ONE SEGMENT         ESP88150
         BCTR  R4,0          LESS ONE FOR EXECUTE             ESP88150
         SLR   R2,R2         CLEAR FUNCTION RESULT BYTE       ESP88150
         LA    R1,1(R6,R4)   PRESET ENDING ADDRESS PAST END   ESP88150
         EX    R4,DESTRINT   TRANSLATE AND TEST               ESP88150
         LTR   R2,R2         SPECIAL CHARACTER FOUND ?        ESP88150
         BZ    DESTRIMV      NO; CONTINUE                     ESP88150
         TM    FLAGS,FGHAZED INITIALIZATION COMPLETE ?        ESP88150
         BNZ   DESTRIMV      YES; USE QUOTES                  ESP88150
         LA    R1,1(,R1)     PROCESS AS DATA                  ESP88150
         SLR   R2,R2         NO QUOTE PROCESSING IN S/I SEQUENCES
DESTRIMV LR    R0,R1         COPY END OF STRING+1             ESP88150
         LR    R5,R1         SAVE FOR NEXT TIME               ESP88150
         SR    R0,R6         COMPUTE LENGTH OF 'NORMAL' STRING SP88150
         LR    R1,R6         SET STRING START ADDRESS         ESP88150
         LR    R6,R5         SET STRING START FOR NEXT TIME   ESP88150
         SR    R7,R0         SET RESIDUAL STRING LENGTH       ESP88150
         LTR   R14,R0        ANY NORMAL STRING ?              ESP88150
         BZ    DESTRINB      NO                               ESP88150
         OC    0(1,R1),CHARHIGH  ADJUST HIGH BIT              ESP88150
         XC    0(1,R1),CHARSPEC  FLIP X'40' IF NECESSARY      ESP88150
         MVI   CHARHIGH,0    RESET HIGH BIT TOGGLE            ESP88150
         MVI   CHARSPEC,0    RESET SECOND BIT TOGGLE          ESP88150
         SLR   R4,R4                                          ESP88150
         ICM   R4,1,CHARREP  GET AND TEST REPEAT COUNT        ESP88150
         BZ    DESTRIMT      MOVE TEXT                        ESP88150
         STM   R1,R3,TEMPD   SAVE OVER MOVE                   ESP88150
         LA    R2,0(R9,R4)   LENGTH AFTER MOVE                   91293
         CH    R2,=Y(RDATLEN)  WILL IT FIT ?                     91293
         BH    DESTRINX      NO; CHOP                            91293
         LR    R0,R8         SET OUTPUT ADDRESS               ESP88150
         AR    R8,R4         SET NEW OUTPUT ADDRESS           ESP88150
         AR    R9,R4         SET NEW OUTPUT LENGTH            ESP88150
         LR    R2,R1         GET START OF MOVE                ESP88150
         SLR   R3,R3         LENGTH IS ZERO (REPLICATE PATTERN) P88150
         STC   R3,CHARREP    CLEAR REPEAT COUNT               ESP88150
         ICM   R3,8,0(R2)    LOAD PATTERN BYTE                ESP88150
         LR    R1,R4         DESTINATION LENGTH               ESP88150
         MVCL  R0,R2         MAKE REPEAT PATTERN              ESP88150
         LM    R1,R3,TEMPD   RESTORE                          ESP88150
         LA    R1,1(,R1)     SKIP PATTERN CHARACTER           ESP88150
         SH    R14,=H'1'     LENGTH TO MOVE AFTER THIS        ESP88150
         BNP   DESTRINB      NO MORE                          ESP88150
DESTRIMT BCTR  R14,0         SET EXECUTE LENGTH               ESP88150
         LA    R3,1(R9,R14)  SIZE AFTER MOVE                     91293
         CH    R3,=Y(RDATLEN)  FITS ?                            91293
         BH    DESTRINX      NO; CHOP SHORT                      91293
         EX    R14,DESTRINE  MOVE TO LONG BUFFER              ESP88150
         LA    R8,1(R8,R14)  UP OUTPUT ADDRESS                ESP88150
         LA    R9,1(R9,R14)  UP OUTPUT LENGTH                 ESP88150
DESTRINB LTR   R2,R2         NEED TO PROCESS QUOTE ?          ESP88150
         BZ    DESTTEST      NO; JUST CONTINUE MOVING         ESP88150
         LA    R6,1(,R6)     SKIP OVER THE QUOTE              ESP88150
         BCTR  R7,0          ADJUST LENGTH                    ESP88150
OFFBASE  B     *(R2)         BRANCH ACCORDING TO FUNCTION CODE SP88150
OFFQUOTE EQU   *-OFFBASE                                      ESP88150
         B     DESTQUOT      PROCESS SIMPLE QUOTE             ESP88150
OFFEIGHT EQU   *-OFFBASE                                      ESP88150
         B     DESTEIGT      PROCESS EIGHT-BIT PREFIX SEGMENT ESP88150
OFFREPIT EQU   *-OFFBASE                                      ESP88150
*        B     DESTREPT      PROCESS REPEAT SEQUENCE          ESP88150
DESTREPT CH    R7,=H'2'      AT LEAST TWO MORE ?              ESP88150
         BL    DESTERR1      NO; PUT OUT AS IS                ESP88150
         IC    R3,0(,R6)     GET NEXT BYTE                    ESP88150
         N     R3,=X'0000007F'  MASK                          ESP88150
         S     R3,SPACE      ISOLATE LENGTH                   ESP88150
         BNP   DESTERR1      BAD REPEAT COUNT                 ESP88150
         STC   R3,CHARREP    STASH REPEAT COUNT               ESP88150
         B     DESTBUMP      BUMP OVER COUNT FIELD            ESP88150
         SPACE 1                                              ESP88150
DESTEIGT LTR   R7,R7         AT LEAST ONE MORE BYTE ?         ESP88150
         BNP   DESTERR1      NO; OUTPUT THIS ONE              ESP88150
         OI    CHARHIGH,X'80'  SET FOR HIGH BIT               ESP88150
         B     DESTTEST      SEE IF MORE                      ESP88150
         SPACE 1                                              ESP88150
DESTQUOT LTR   R7,R7         AT LEAST ONE MORE ?              ESP88150
         BNP   DESTERR1      NO; DO THIS ONE                  ESP88150
         OI    CHARSPEC,X'40'  SET TO FLIP SECOND BIT         ESP88150
         SLR   R2,R2         RESET FOR TRT NON-INSERT         ESP88150
         TRT   0(1,R6),QUOTABLE  IS NEXT CHARACTER SPECIAL ?  ESP88150
         LTR   R2,R2         SPECIAL ?                        ESP88150
         BZ    DESTQONE      NO; DO SINGLE BYTE               ESP88150
DESTPUTC MVI   CHARSPEC,0    ELSE CANCEL BIT FLIP             ESP88150
DESTQONE SLR   R2,R2         FAKE TRT INDEX                   ESP88150
         LA    R1,1(,R6)     POINT TO NEXT CHARACTER          ESP88150
         B     DESTRIMV      GO TO MOVE DATA                  ESP88150
         SPACE 1                                              ESP88150
DESTERR1 BCTR  R6,0          BACKSPACE 1                      ESP88150
         LA    R7,1(,R7)     FINAGLE LENGTH                   ESP88150
         B     DESTPUTC                                       ESP88150
DESTBUMP LA    R6,1(,R6)     ADJUST NEW POINTER               ESP88150
         BCTR  R7,0          AND SET RESIDUAL LENGTH          ESP88150
         SPACE 1                                              ESP88150
DESTTEST LTR   R7,R7                                          ESP88150
         BP    DESTRINN      DO NEXT SEGMENT OF STRING        ESP88150
DESTRINX ST    R9,LRDAT      SET NEW DATA LENGTH              ESP88150
RPAKEXIT SUBEX RC=(R15)                                       ESP88150
DESTRINE MVC   0(0,R8),0(R1)  MOVE PLAIN TEXT TO OUTPUT       ESP88150
DESTRINT TRT   0(0,R6),QUOTABLE  TRANSLATE AND TEST           ESP88150
DBGMVC2  MVC   WRKBUFF+9(0),RECPKT                            ESP88150
RPAKTRAA TR    RECPKT-RECPKT(0,R15),0(R9)  TRANSLATE TO ASCII    91315
CHKSUMER WTO   'CHECKSUM ERROR; EXPECTED XXXXXX, RECEIVED XXXXXX.',MF=L
CHKSUMEL EQU   *-CHKSUMER                                     ESP85142
         DROP  R8                                             ESP88150
         EJECT ,                                              ESP88150
**********************************************************************
*                                                                    *
*        CHK3 - THREE-BYTE CHECKSUM CALCULATION                      *
*              THIS CODE WAS ADAPTED FROM THE ORIGINAL 'C' VERSION   *
*              AND NOT (YET) OPTIMIZED FOR 360 ARCHITECTURE.         *
*                                                                    *
**********************************************************************
CHK3     SUBENT ,            R1 - START ADDRESS; R0 - LENGTH  ESP88150
         SLR   R15,R15       CLEAR RETURNED CRC               ESP88150
         LTR   R0,R0         CHECK LENGTH                     ESP88150
         BNP   CHK3EXIT      BAD; QUIT WITH ZERO CRC          ESP88150
         LA    R7,15         MAKE MASK                        ESP88150
*SLOW*   LH    R8,=X'1081'   FINAGLE FACTOR FROM POLYNOMIAL   ESP88150
*SLOW*   LA    R9,X'7F'      ONLY 7-BITS ALLOWED              ESP88150
*HK3LOOP IC    R3,0(,R1)     GET THE CURRENT BYTE             ESP88150
*SLOW*   NR    R3,R9         MASK IT                          ESP88150
*SLOW*   STC   R3,0(,R1)     MAKE SURE                        ESP88150
*SLOW*   XR    R3,R15        CRC XOR CHAR                     ESP88150
*SLOW*   NR    R3,R7         AND O17                          ESP88150
*SLOW*   MR    R2,R8         * O10201                         ESP88150
*SLOW*   SRL   R15,4         CRC >> 4                         ESP88150
*SLOW*   XR    R15,R3        CRC =                            ESP88150
*SLOW*   IC    R3,0(,R1)     CHAR AGAIN                       ESP88150
*SLOW*   NR    R3,R9         QUICKY REGISTER CLEAR            ESP88150
*SLOW*   SRL   R3,4          CHAR >> 4                        ESP88150
*SLOW*   XR    R3,R15        XOR CRC                          ESP88150
*SLOW*   NR    R3,R7         AND O17                          ESP88150
*SLOW*   MR    R2,R8         * O10201                         ESP88150
*SLOW*   SRL   R15,4         CRC >> 4                         ESP88150
*SLOW*   XR    R15,R3        XOR Q                            ESP88150
         SLR   R4,R4                                             89289
CHK3LOOP IC    R4,0(,R1)     GET NEXT BYTE                       89289
         XR    R4,R15        XOR CHAR AND CRC LOW BYTE           89289
         LR    R5,R4         COPY                                89289
         SRL   R5,4          ISOLATE HIGH NYBBLE                 89289
         NR    R4,R7         MASK ONLY NYBBLE                    89289
         NR    R5,R7                                             89289
         ALR   R4,R4         DOUBLE                              89289
         LH    R4,CRCTAB2(R4)  GET CRC FOR LOW 4 BITS            89289
         ALR   R5,R5                                             89289
         LH    R5,CRCTAB1(R5)  GET CRC FOR HIGH 4 BITS           89289
         XR    R4,R5         XOR                                 89289
         SRL   R15,8         SHIFT PRIOR CRC                     89289
         XR    R15,R4        XOR CURRENT                         89289
         N     R15,=X'0000FFFF'  TRUNCATE TO LOW TWO BYTES       89289
         LA    R1,1(,R1)                                      ESP88150
         BCT   R0,CHK3LOOP   GO BACK FOR MORE PUNISHMENT      ESP88150
CHK3EXIT LR    R2,R15        COPY CRC                         ESP88150
         SRDL  R2,6          ISOLATE LOWEST BYTE              ESP88150
         SRL   R3,2          INSERT GAP FOR ZONES             ESP88150
         SRDL  R2,6          MOVE SECOND BYTE                 ESP88150
         SRL   R3,2          NEXT GAP                         ESP88150
         SLDL  R2,16         BACK INTO 2                      ESP88150
         ICM   R3,7,ABLANKS  LOAD ASCII BLANKS                ESP88150
         AR    R2,R3         CONVERT TO PRINTABLES            ESP88150
         ST    R2,TEMP+4     PASS IT BACK TO CALLER           ESP88150
         SUBEX RC=(R15)      RETURN CRC                       ESP88150
         SPACE 1                                                 89289
*        CRC TABLES                                              89289
CRCTAB1  DC    X'0000,1081,2102,3183,4204,5285,6306,7387'        89289
         DC    X'8408,9489,A50A,B58B,C60C,D68D,E70E,F78F'        89289
CRCTAB2  DC    X'0000,1189,2312,329B,4624,57AD,6536,74BF'        89289
         DC    X'8C48,9DC1,AF5A,BED3,CA6C,DBE5,E97E,F8F7'        89289
         EJECT ,                                              ESP88150
***********************************************************************
*                                                                     *
*        GET INPUT FROM TERMINAL (OR BATCH TMP SIMULATION FILE)       *
*           ALSO NOTE THAT INPUT MAY BE FROM 'TAKE' FILE              *
*                                                                     *
*        INPUT ADDRESS RETURNED IN R0; LENGTH IN R1                   *
*           LEADING ZEROES AND BLANKS ARE SKIPPED                     *
*        FOR A TEXT MODE CALL (TTGETC), THE INPUT IS ALSO COPIED TO   *
*        'INPUT', CONVERTED TO UPPER CASE AND PADDED WITH BLANKS.     *
*                                                                     *
***********************************************************************
TTGETC   OI    PTCHFLG,FGTGEXT  GET TEXT (COMMAND)            ESP88150
TTGET    SUBENT ,            ENTRY TO GET INPUT BUFFER        ESP88150
         TM    MODEFG,FGNET  MULTI-TASKING MODE ?                89337
         BZ    TTGENCNT      NO; NO I/O COUNTS                   89337
         L     R14,FAKECPPL+8  GET MTS ADDRESS BACK              89337
         ICM   R15,15,MTSINCT-MTS(R14)  GET OLD TERM INP COUNT   89337
         AH    R15,=H'1'     ADD 1                               89337
         STCM  R15,15,MTSINCT-MTS(R14)  SAVE NEW DISK INP COUNT  89337
TTGENCNT DS    0H                                                89337
         AIF   (NOT &INPREAD).NOTAKE3                            91315
TTGETTAK SLR   R2,R2         CLEAR FOR LOAD                   ESP88277
         ICM   R2,1,NUMTAKEN  ANY TAKE FILE IN EFFECT ?       ESP88277
         BZ    TTGETNTK      NO                               ESP88277
         SLL   R2,2          MAKE INDEX                       ESP88277
         L     R4,MISTAKEN(R2)  LOAD THE WORK AREA            ESP88277
         USING TAWSECT,R4    DECLARE IT                       ESP88277
         INPGET DEV==TAWMASK  GET A RECORD                    ESP88277
         BXH   R15,R15,TTGETTAB  ERROR                        ESP88277
         LR    R2,R1         COPY THE TEXT ADDRESS            ESP88277
         LTR   R3,R0         COPY THE LENGTH                  ESP88277
         BNM   *+6           IF NOT POSITIVE, ENSURE ZERO     ESP88277
         SLR   R3,R3         MAKE NON-NEGATIVE                   91084
         TM    PTCHFLG,FGTGEXT  TEXT REQUEST ?                   91084
         BZ    TTGETFCV      NO; JUST RETURN AS-IS               91084
         IC    R5,PTCHFLG    PRESERVE OPTION (TEXT)              91084
         WTERM (R2),(R3)     ECHO TO USER                        91084
         STC   R5,PTCHFLG    RESTORE OPTION FLAG                 91084
         B     TTGETFCV      JOIN COMMON                         91084
TTGETTAB WTERM  ' TAKE file read error. File ignored.'        ESP88277
TTGETEOF INPCLOSE DEV==TAWMASK  CLOSE THE FILE                ESP88277
         SUBCALL DDFREE,TAWPTR  FREE THE ALLOCATION           ESP88277
         SLR   R2,R2                                          ESP88277
         IC    R2,NUMTAKEN                                    ESP88277
         BCTR  R2,0          SET NUMBER OF PREVIOUS FILE      ESP88277
         STC   R2,NUMTAKEN                                    ESP88277
         B     TTGETTAK      TRY PREVIOUS FILE                ESP88277
         DROP  R4                                             ESP88277
TTGETNTK DS    0H            NO TAKE IN EFFECT                ESP88277
.NOTAKE3 ICM   R1,15,MYGTPB+GTPBIBUF-GTPB  TEST PRIOR BUFFER  ESP88150
         BZ    TTGET0        NONE                             ESP88150
         TM    MODEFG,FGNET  NETWORK VERSION ?                   89328
         BNZ   TTGET0        DON'T FREE MTSINBUF                 89328
         LH    R0,0(,R1)     GET BUFFER LENGTH                ESP88150
         ICM   R0,8,=AL1(1)  SUBPOOL=1                        ESP88150
         FREEMAIN R,LV=(0),A=(1)  FREE THE LINE               ESP88150
         XC    GTPBIBUF+MYGTPB-GTPB,GTPBIBUF+MYGTPB-GTPB      ESP88150
TTGET0   L     R15,@GETLINE  LOAD THE IKJGETL SERVICE ROUTINE ADDRESS
         LA    R1,MYIOPL     GET MY LIST ADDRESS              ESP88150
         LA    R0,MYGTPB     GET THE GETLINE PARAMETER BLOCK  ESP88150
         ST    R0,MYIOPL+IOPLIOPB-IOPL  STASH IT IN IOPL      ESP88150
         TM    PTCHFLG,FGTGEXT  GETTING TEXT ?                ESP88150
         BNZ   TTGETT        YES                              ESP88150
         GETLINE INPUT=PHYSICAL,TERMGET=ASIS,  GET DATA                *
               ENTRY=(15),MF=(E,(1))                          ESP88150
         B     TTGETR        CHECK RETURN CODE                ESP88150
TTGETT   GETLINE INPUT=ISTACK,TERMGET=EDIT,    GET TEXT                *
               ENTRY=(15),MF=(E,(1))                          ESP88150
TTGETR   CH    R15,=H'4'     NORMAL RETURN ?                  ESP88150
         BH    TTGETS        NO; HANDLE SPECIAL PROCESSING    ESP88150
         L     R5,MYGTPB+GTPBIBUF-GTPB  GET THE INPUT BUFFER  ESP88150
         LA    R2,4(,R5)     POINT TO THE TEXT                ESP88150
         LH    R3,0(,R5)     GET THE TEXT LENGTH              ESP88150
         SH    R3,=H'4'      ALLOW FOR LENGTH OF LENGTH       ESP88150
         BNP   TTGETM0       RETURN AS IS                     ESP88150
TTGETB   TM    0(R2),255-C' '    LEADING BLANK OR NULL ?      ESP88150
         BNZ   TTGETM        NO; DONE                         ESP88150
         LA    R2,1(,R2)     SKIP OVER IT                     ESP88150
         BCT   R3,TTGETB     AGAIN                            ESP88150
TTGETM0  SLR   R3,R3         MAKE NON-NEGATIVE                ESP88150
TTGETM   SLR   R15,R15                                        ESP88204
         IC    R15,PROTOCON  GET CONVERTER INDEX              ESP88204
         B     *+4(R15)      PRE-PROCESS INPUT BUFFER         ESP88204
         B     TTGETNPC      0 - NO CONVERSION                ESP88204
         B     TTGETC80      4 - COMMTEX CX-80                ESP88204
*        B     TTGET717      8 - IBM 7171                     ESP90086
         SPACE 1                                              ESP88204
TTGETC80 LTR   R3,R3         ANY DATA ?                       ESP88204
         BNP   TTGETNPC      NO ?                             ESP88204
         TM    PTCHFLG,FGTGEXT  TEXT MODE ?                   ESP88277
         BNZ   TTGETNPC      YES; NO ASCII RESPONSE EXPECTED  ESP88277
         CLI   0(R2),X'E8'   NO ATTN AID ?                    ESP88277
         BE    TTGETASC      YES; STRIP                       ESP88277
         CLI   0(R2),X'70'   ASCII RESPONSE (PASS-THROUGH MODE) ?
         BNE   TTGETNPC      NO; LEAVE AS IS                  ESP88204
TTGETASC LA    R2,1(,R2)     SKIP OVER IT                     ESP88204
         BCTR  R3,0          ADJUST THE LENGTH                ESP88204
         LR    R14,R2        COPY START ADDRESS               ESP88204
         LR    R15,R3        COPY LENGTH                      ESP88204
         L     R9,@MAKEBC    GET EBCDIC TRANSLATION              91315
TTGETC81 CH    R14,=H'256'   MORE THAN ONE CHUNK ?            ESP88204
         BNH   TTGETC82      NO                               ESP88204
         TR    0(256,R14),0(R9)  CONVERT ZONED ASCII (+X'80')    91315
         LA    R14,256(,R14)         TO EBCDIC                ESP88204
         SH    R15,=H'256'                                    ESP88204
         B     TTGETC81                                       ESP88204
TTGETC82 SH    R14,=H'1'     GET EXECUTE LENGTH               ESP88204
         BM    TTGETFCV      DONE                             ESP88204
         EX    R14,TTGETC89  CONVERT LAST FRAGMENT            ESP88204
         B     TTGETFCV      DONE                             ESP88204
TTGETC89 TR    0(0,R14),0(R9)  CONVERT TO EBCDIC                 91315
         SPACE 1                                              ESP88204
TTGETNPC TM    IOFLAG2,FGCRT  CRT MODE ?                      ESP88278
         BZ    TTGETFCV      NO                               ESP88278
         TM    PTCHFLG,FGTGEXT  TEXT MODE ?                   ESP88278
         BNZ   TTGETFCV      YES; NO AID, ETC.                ESP88278
         CLI   0(R2),X'01'   TEST-REQUEST/SYSTEM REQUEST ?    ESP88278
         BNE   TTGETNTR      NO                               ESP88278
         LA    R2,1(,R2)     LONGER PREFIX                    ESP88278
         BCTR  R3,0                                           ESP88278
TTGETNTR CLI   3(R2),X'11'   FORMATTED SCREEN ?               ESP88278
         BNE   *+8           NO                               ESP88278
         LA    R0,6          SET LONGER PREFIX TO DATA        ESP88278
         AR    R2,R0         SKIP OVER CONTROL INFO           ESP88278
         SR    R3,R0         GET RESIDUAL LENGTH              ESP88278
         BNM   TTGETFCV      OK ?                             ESP88278
         SLR   R3,R3         ELSE NULL INPUT                  ESP88278
TTGETFCV ST    R2,@INCOME    SAVE THE TEXT ADDRESS            ESP88150
         L     R15,4(,R13)   GET THE PREVIOUS SAVE AREA       ESP88150
         STM   R2,R3,20(R15)  RETURN ADDRESS/LENGTH IN R0/R1  ESP88150
         TM    PTCHFLG,FGTGEXT  TEXT MODE ?                   ESP88150
         BZ    TTGETQ        NO                               ESP88150
         LA    R14,INPUT                                      ESP88150
         LA    R15,L'INPUT                                    ESP88150
         ICM   R3,8,BLANKS   BLANK FILL                       ESP88150
         MVCL  R14,R2        MOVE TEXT TO INPUT BUFFER        ESP88150
         TR    INPUT,UPPER   AND MAKE UPPER-CASE              ESP88150
         TM    DEBUG+DCBOFLGS-IHADCB,DCBOFOPN  DEBUG OPEN ?   ESP88150
         BZ    TTGETQ        NO                               ESP88150
         PUT   DEBUG,INPUTRDW  WRITE INPUT LINE               ESP88150
TTGETQ   SLR   R15,R15       CLEAR THE RETURN                 ESP88150
         B     TTGETEX       AND EXIT                         ESP88150
TTGETS   XC    GTPBIBUF+MYGTPB-GTPB,GTPBIBUF+MYGTPB-GTPB      ESP88150
         CH    R15,=H'12'    END OF INPUT ?                   ESP88150
         BE    TTGETF        YES                              ESP88150
         CH    R15,=H'40'    BARRIER ?                        ESP88150
         BNE   TTGETBD       NO; ERROR                        ESP88150
TTGETF   LA    R2,=C'QUIT '                                   ESP88150
         LA    R3,5          SET FOR END REQUEST              ESP88150
         B     TTGETM        AND RETURN IT                    ESP88150
TTGETBD  LA    R15,8         SET MAJOR ERROR                  ESP88150
TTGETEX  NI    PTCHFLG,255-FGTGEXT  RESET TEXT MODE FLAG      ESP88150
         SUBEX RC=(R15)      RETURN WITH ERROR CODE           ESP88150
         EJECT ,                                              ESP88150
***********************************************************************
*                                                                     *
*        PUT OUTPUT ON THE TERMINAL (OR BATCH TMP SIMULATION FILE)    *
*                                                                     *
*        ADDRESS SUPPLIED IN R1; LENGTH IN R0                         *
*                                                                     *
***********************************************************************
TPUTCONT OI    PTCHFLG,FGTCONT  CONTROL MODE                  ESP88150
TPUTASIS OI    PTCHFLG,FGTTEXT  ASIS (PROMPT) MODE            ESP88150
TPUTEDIT SUBENT ,            ENTRY TO WRITE LINE TO TERMINAL  ESP88150
         LR    R5,R0         COPY TEXT LENGTH                 ESP88150
         LR    R4,R1         COPY TEXT START                  ESP88150
         SLR   R2,R2                                             91291
         IC    R2,PROTOCON   GET PROTOCOL CONVERTER INDEX        91291
         TM    MODEFG,FGNET  MULTI-TASKING MODE ?                89337
         BZ    TPUTNCNT      NO; NO I/O COUNTS                   89337
         L     R14,FAKECPPL+8  GET MTS ADDRESS BACK              89337
         ICM   R15,15,MTSOUTCT-MTS(R14)  GET OLD TERM OUT COUNT  89337
         AH    R15,=H'1'     ADD 1                               89337
         STCM  R15,15,MTSOUTCT-MTS(R14)  SAVE NEW TERM OUT COUNT 89337
TPUTNCNT TM    PTCHFLG,FGTCONT  CONTROL BUFFER ?                 91291
         BNZ   TPUTLONG      YES; SPECIAL HANDLING               91291
         LA    R14,PUTBUFF   GET OUTPUT BUFFER                ESP88150
         LA    R15,L'PUTBUFF  MAXIMUM LENGTH                  ESP88150
         MIN   R15,(R5)      TRUNCATE TO MINIMUM              ESP88309
         LA    R0,4(,R15)    LENGTH WITH RDW LENGTH           ESP88309
         LTR   R2,R2         PROTOCOL CONVERSION ?               91291
         BZ    TPUTNPC       NO CONVERSION                    ESP88204
         SPACE 1                                              ESP88204
         TM    PTCHFLG,FGTCONT  CONTROL MODE ?                ESP88204
         BZ    TPUTNPC       NO                               ESP88204
         LTR   R5,R5         ANY DATA ?                       ESP88204
         BNP   TPUTNPC       NO                               ESP88204
         B     *(R2)         BRANCH BY TYPE                   ESP88204
         B     TPUTCX80      COMMTEX CX-80                    ESP88204
*        B     TPUT7171      IBM 7171                         ESP90086
         SPACE 1                                              ESP90086
TPUT7171 MVC   PUTBUFF(L'PFX7171),PFX7171  TRANSPARENCY REQ.  ESP88204
         LA    R14,L'PFX7171(,R14)  SKIP OVER PREFIX          ESP88204
         LA    R0,4+L'PFX7171(,R5)  SET NEW LENGTH            ESP88204
         B     TPUTNPC       JOIN COMMON                      ESP88204
         SPACE 1                                              ESP90086
TPUTCX80 MVC   PUTBUFF(L'PFXCX80),PFXCX80  TRANSPARENCY REQ.  ESP88204
         LA    R14,L'PFXCX80(,R14)  SKIP OVER PREFIX          ESP88204
         LA    R0,4+L'PFXCX80(,R5)  SET NEW LENGTH            ESP88204
*        B     TPUTNPC       JOIN COMMON                      ESP88204
         SPACE 1                                              ESP88204
TPUTNPC  STH   R0,PUTRDW                                      ESP88150
         MVCL  R14,R4        MOVE TEXT TO OUTPUT              ESP88150
         LA    R4,PUTRDW     RELOAD BUFFER START                 91291
         LH    R5,PUTRDW     AND LENGTH                          91291
         B     TPUTSHRT      BRANCH AROUND                       91291
TPUTLONG LA    R0,4                                              91291
         AR    R5,R0         LENGTH WITH RDW                     91291
         SR    R4,R0         RDW LOCATION                        91291
         STH   R5,0(,R4)     MAKE RDW                            91291
         STCM  R0,IIOO,2(R4)  CLEAR LOW PORTION                  91291
TPUTSHRT L     R15,@PUTLINE  LOAD THE IKJPUTL SERVICE ROUTINE ADDRESS
         LA    R1,MYIOPL     GET MY LIST ADDRESS              ESP88150
         LA    R0,MYPTPB     GET THE GETLINE PARAMETER BLOCK  ESP88150
         ST    R0,MYIOPL+IOPLIOPB-IOPL  STASH IT IN IOPL      ESP88150
         TM    PTCHFLG,FGTCONT  CONTROL (SEND PACKET) MODE ?  ESP88150
         BZ    TTPUTW        NO; CHECK DEBUG MODE             ESP88150
         B     *+4(R2)       BRANCH BY TYPE                   ESP88204
         B     TTPUTNPC      NO CONVERSION                    ESP88204
         B     TTPUTC80      COMMTEX CX-80                    ESP88204
         B     TTPUTI71      IBM 7171                         ESP90086
         SPACE 1                                              ESP88204
TTPUTC80 L     R15,@MAKASC   GET ASCII TRANSLATION               91315
         TR    PUTBUFF+L'PFX7171(L'PUTBUFF-L'PFX7171),0(R15)  ASCII
         TR    PUTBUFF+L'PFX7171(L'PUTBUFF-L'PFX7171),ZONASCII SP88279
TTPUTI71 LR    R3,R5         GET BUFFER LENGTH BACK              91291
         SH    R3,=H'4'      GET TEXT LENGTH                  ESP88206
         TM    MODEFG,FGNET  NET VERSION ?                       89331
         BNZ   TTPUTC8N      YES; USE FAKE PUTLINE               89331
         TPUT  4(R4),(R3),FULLSCR,WAIT,HOLD  WAIT FOR DELIVERY   91291
         B     TTPUTR                                            89331
TTPUTC8N PUTLINE OUTPUT=(0(R4),DATA),TERMPUT=(CONTROL,HOLD),           *
               ENTRY=(15),MF=(E,(1))                          ESP88204
         B     TTPUTR        JOIN COMMON                      ESP88204
         SPACE 1                                              ESP88204
TTPUTNPC PUTLINE OUTPUT=(0(R4),DATA),TERMPUT=(CONTROL,HOLD),           *
               ENTRY=(15),MF=(E,(1))                          ESP88150
         B     TTPUTR        CHECK RETURN CODE                ESP88150
TTPUTW   TM    DCBOFLGS+DEBUG-IHADCB,DCBOFOPN  DEBUGGING ?    ESP88150
         BZ    TTPUTWB       NO                               ESP88150
         STM   R15,R1,TEMP   SAVE REGISTERS                   ESP88150
         PUT   DEBUG,(R4)    WRITE BUFFER (NO EMBELLISHMENTS)    91291
         LM    R15,R1,TEMP   RESTORE                          ESP88150
TTPUTWB  TM    PTCHFLG,FGTTEXT  DOING TEXT ?                  ESP88150
         BZ    TTPUTT        YES                              ESP88150
         PUTLINE OUTPUT=(0(R4),DATA),TERMPUT=ASIS,                     *
               ENTRY=(15),MF=(E,(1))                          ESP88150
         B     TTPUTR        CHECK RETURN CODE                ESP88150
TTPUTT   PUTLINE OUTPUT=(0(R4),DATA),TERMPUT=EDIT,                     *
               ENTRY=(15),MF=(E,(1))                          ESP88150
TTPUTR   CH    R15,=H'4'     NORMAL RETURN ?                  ESP88150
         BL    TTPUTEX       YES                              ESP88150
         STM   R15,R0,TEMP   SAVE FOR DEBUGGING               ESP88325
         BH    TTPUTAB       NO; TERMINATE WITH PREJUDICE     ESP88150
*LATER DO SOMETHING ABOUT ATTENTIONS ?                        ESP88150
         B     TTPUTEX       IGNORE ATTENTIONS (PASS ERROR CODE) 88325
TTPUTAB  TM    MODEFG,FGNET  MULTI-TASKING MODE ?                89345
         BZ    TTPUTABE      NO                                  89345
         OI    FLAGS,FGBYE   FORCE TERMINATION                   91269
         TM    MODEFG,FGWYSS   WYLBUR ?                          91269
         BNZ   TTPUTEX       YES; QUIT GENTLY                    91269
         ABEND 522           JUST GO QUIETLY WITHOUT A DUMP      89345
TTPUTABE ABEND 2741,DUMP     MY TERMINAL IS BROKEN            ESP88150
TTPUTEX  NI    PTCHFLG,255-FGTTEXT-FGTCONT  RESET MODE FLAGS  ESP88150
         SUBEX RC=(R15)      RETURN WITH ERROR CODE           ESP88150
         SPACE 1                                              ESP88204
ZONASCII DC    127AL1(128+*-ZONASCII),128AL1(*-ZONASCII),X'7F' SP88204
PFX7171  DC    X'27F1C3115D7F110001'  IBM 7171 TRANSPARENCY   ESP88277
PFXCX80  DC    X'27F1C370'   CX-80 PREFIX                     ESP88277
*FXYALE  DC    X'27F1C3110001'  YALE SERIES/1 PREFIX          ESP88277
         EJECT ,                                              ESP85142
*********************************************************************
*                                                                   *
*        DCB OPEN EXIT FOR OUTPUT                                   *
*                                                                   *
*********************************************************************
         SPACE 1                                              ESP85142
         USING OUTEXIT,R15                                    ESP85142
         USING IHADCB,R1                                      ESP85142
OUTEXIT  TM    DCBRECFM,DCBRECU  CHECK RECORD FORMAT          ESP85142
         BM    OUTEXIT1      SET TO F OR V, LEAVE IT          ESP85142
         BZ    OUTEXIT0      NOT SET, USE USER'S              ESP85142
OUTEXITH SLR   R0,R0                                             89333
         ICM   R0,3,LRECL    GET USER'S RECORD LENGTH            89332
         CH    R0,=H'500'    MORE THAN WYLBUR MAXIMUM ?          89332
         BH    OUTXFAIL      YES; REJECT IT                      89332
         CH    R0,=H'232'    MORE THAN O.B.S. WYLBUR ?           89332
         BNH   OUTEXITU      NO                                  89332
*DEFER*  OI    DCMFG1,DCMF1NIH  SUPPORT NIH FORMAT               89332
         B     OUTXFAIL      UNTIL WYLBUR FIXED ?                89333
OUTEXITU STH   R0,CURLRMAX   SET MAXIMUM RECORD SIZE             89333
         ICM   R0,3,DCBBLKSI  ANY BLOCK SIZE ?                   89332
         BNZ   OUTEXITW      YES                                 89332
         LH    R0,BLKSIZE    GET USER'S PREFERENCE               89332
         STH   R0,DCBBLKSI   SET TO USER'S SIZE                  89332
         ICM   R2,3,DCBLRECL ANY LRECL ?                         89332
         BNZ   OUTEXITW      YES                                 89332
         LR    R2,R0         USE BLOCKSIZE                       89332
         STH   R2,DCBLRECL                                       89332
OUTEXITW CH    R0,DCBLRECL   BLKSIZE=LRECL ?                     89332
         BER   R14           HOPE FOR THE BEST                   89332
OUTXFAIL OI    IOFLAGS,FGOPF   SHOW NOT SUPPORTED             ESP85142
         B     OUTEXIT1      CHECK REST                       ESP85142
OUTEXIT0 CLI   RECFM,C'V'    VARIABLE ?                       ESP85142
         BE    OUTEXITV      YES                              ESP85142
         OI    DCBRECFM,DCBRECF+DCBRECBR  SET FIXED, BLOCKED  ESP85142
         CLI   RECFM,C'W'    WYLBUR EDIT ?                       89333
         BNE   OUTEXIT1                                          89333
         MVI   DCBRECFM,DCBRECU  SET RECFM=U                     89333
         B     OUTEXITH      JOIN COMMON                         89333
OUTEXITV OI    DCBRECFM,DCBRECV+DCBRECBR  SET VARIABLE, BLOCKED P88150
OUTEXIT1 TM    DCBRECFM,DCBRECF  FIXED ?                         93095
         BZ    OUTEXITF      NO                                  93095
         TM    EDFLAGS,FGSAS  SAS MODE ?                         93095
         BZ    OUTEXITF      NO                                  93095
         OI    DCBRECFM,DCBRECSB  STANDARD BLOCKS                93095
OUTEXITF SLR   R0,R0                                          ESP85142
         SLR   R2,R2                                          ESP85142
         SLR   R3,R3                                          ESP85142
         ICM   R0,3,DCBLRECL RECORD LENGTH SET ?              ESP85142
         BNZ   OUTEXIT2      YES; USE IT                      ESP85142
         ICM   R0,3,LRECL    GET USER'S RECORD LENGTH         ESP85142
         TM    DCBRECFM,DCBRECF  RECFM NOT V ?                ESP85142
         BNZ   *+8           CORRECT                          ESP85142
         AH    R0,=H'4'      ELSE ADJUST FOR RDW              ESP85142
         STH   R0,DCBLRECL   SET IT BACK                      ESP85142
OUTEXIT2 STH   R0,CURLRMAX   SAVE MAXIMUM LRECL FOR THIS FILE ESP88150
         ICM   R3,3,DCBBLKSI GET BLOCKSIZE                    ESP85142
         BNZ   OUTEXIT3      HAVE IT                          ESP85142
         ICM   R3,3,BLKSIZE  ELSE USE USER'S                  ESP85142
         STH   R3,DCBBLKSI   SET FOR RECFM=V                  ESP85142
         DR    R2,R0         TRY IT FOR SIZE                  ESP85142
         LTR   R3,R3         ANY QUOTIENT ?                   ESP85142
         BP    *+12          YES                              ESP85142
         LA    R3,1          DEFAULT TO UNBLOCKED             ESP85142
         STH   R0,DCBBLKSI   SET BLKSIZE=LRECL                ESP85142
OUTEXIT3 TM    DCBRECFM,DCBRECV  OTHER THAN FIXED ?           ESP85142
         BNZR  R14           YES                              ESP85142
         ICM   R3,3,DCBBLKSI GET BLOCKSIZE BACK               ESP85142
         SLR   R2,R2                                          ESP85142
         DR    R2,R0                                          ESP85142
         LTR   R3,R3         ANY AT ALL ?                     ESP85142
         BNZ   OUTEXIT4      YES                              ESP85142
         LA    R3,1          FORCE SINGLE BLOCK               ESP85142
         ICM   R0,3,DCBLRECL FORCE LRECL=BLKSIZE              ESP85142
OUTEXIT4 MR    R2,R0         MAKE MULTIPLE SIZE               ESP85142
         STH   R3,DCBBLKSI   ADJUST                           ESP85142
         CR    R0,R3         UNBLOCKED ?                      ESP85142
         BNER  R14           NO                               ESP85142
         NI    DCBRECFM,255-DCBRECBR  RESET BLOCKING          ESP85142
         BR    R14           RETURN                           ESP85142
         DROP  R1,R15                                         ESP85142
         SPACE 2                                                 91112
***********************************************************************
*                                                                     *
*        DCB ABEND EXIT FOR INPUT/OUTPUT DATA FILE                    *
*                                                                     *
***********************************************************************
         PUSH  USING                                             91112
         DROP  ,                                                 91112
         USING ABDCBEX,R15   DECLARE IT                          91112
ABDCBEX  L     R2,4(,R1)     GET DCB                             91112
         USING KERIN,R2      DECLARE DCB (SAME AS KEROUT)        91112
         OI    IOFLAGS,FGABEND  SHOW DCB ABEND EXIT ENTERED      91112
         MVC   IOCANCOD(3),0(R1)  COPY SYSTEM ABEND CODE         91112
         MVI   3(R1),4       KILL FLAGS; SET IGNORE OR CONTINUE  91112
         BR    R14           RETURN TO SAM                       91112
         POP   USING                                             91112
         SPACE 2                                                 91112
***********************************************************************
*                                                                     *
*        DCB SYNAD EXIT FOR INPUT/OUTPUT DATA FILE                    *
*                                                                     *
***********************************************************************
         PUSH  USING                                             94081
         DROP  ,                                                 94081
         USING SYNADEX,R15   DECLARE IT                          94081
         USING KERIN,R1      DECLARE DCB (SAME AS KEROUT)        94081
SYNADEX  OI    IOFLAGS,FGABEND  SHOW DCB SYNAD/ABEND EXIT ENTERED
         MVI   IOCANCOD+1,X'1'  SET SYSTEM ABEND CODE TO 001000  94081
         USING IHADCB,R1                                         94081
         NI    DCBEROPT,255-DCBERACC-DCBERABE  RESET OTHERS      94081
         OI    DCBEROPT,DCBERSKP  SKIP THIS AND FUTURE ERRORS    94081
         BR    R14           RETURN TO SAM                       94081
         POP   USING                                             94081
         EJECT ,                                              ESP88150
***********************************************************************
*                                                                     *
*        FILEMAKE - SERVER ROUTINE TO PARSE, ALLOCATE AND             *
*              OPEN FILES SPECIFIED BY THE OTHER SIDE                 *
*                                                                     *
***********************************************************************
FILEMAKE SUBENT ,                                             ESP88150
         XC    IOCANCOD,IOCANCOD  CLEAR DCB ABEND CODE           91112
         TM    PTCHFLG,FGTGEXT  ALREADY PARSED ?              ESP88150
         BNZ   FILEPAST      YES; JUST ALLOCATE               ESP88150
         MVC   DSNAMEX,BLANKS    CLEAR DSN                    ESP88150
         MVC   TUMEMBER,BLANKS           AND MEMBER           ESP88150
         LA    R1,DSNAMEX    POINT TO DSNAME BUFFER           ESP88150
         LA    R2,L'TUDSNAME MAX LENGTH OF DSNAME             ESP88150
         SLR   R5,R5         CLEAR LENGTH                     ESP88150
FMDSNLP  CLI   0(R6),C' '    IS THIS END OF FIELD ?           ESP88150
         BE    FMMEMND       YES; PROCESS                     ESP88150
         CLI   0(R6),C'('    START OF MEMBER ?                ESP88150
         BNE   FMDMVC        NO                               ESP88150
         CLI   1(R6),C'-'    OLD GDG ENTRY                    ESP88150
         BE    FMDMVC        YES; CONTINUE                    ESP88150
         CLI   1(R6),C'+'    NEW GDG ENTRY ?                  ESP88150
         BE    FMDMVC        YES (FAILS IF PC RECEIVE)        ESP88150
         CLI   1(R6),C'0'    NUMERIC ARGUMENT ?               ESP88150
         BL    FMMEMLP       NO; BRANCH TO MEMBER PROCESSING  ESP88150
FMDMVC   MVC   0(1,R1),0(R6) MOVE A CHARACTER                 ESP88150
         LA    R6,1(,R6)     MOVE ALONG INPUT BUFFER          ESP88150
         LA    R1,1(,R1)     MOVE ALONG DSNAME BUFFER         ESP88150
         LA    R5,1(,R5)     UP THE LENGTH COUNT              ESP88150
         BCT   R2,FMDSNLP    KEEP LOOKING FOR END             ESP88150
         CLI   0(R6),C'('    START OF MEMBER ?                ESP88150
         BE    FMMEMLP       YES                              ESP88150
FM4LONG  LA    R15,4         SET FOR DSN MALFORMED            ESP88150
         B     FILEMAKX      QUIT NOW                         ESP88150
FMMEMLP  LA    R1,TUMEMBER                                    ESP88150
         LA    R0,8                                           ESP88150
         LA    R6,1(,R6)     SKIP OVER THE '('                ESP88150
FMMEMLO  CLI   0(R6),C')'    END OF MEMBER ?                  ESP88150
         BE    FMMEMND       YES                              ESP88150
         MVC   0(1,R1),0(R6)    COPY A BYTE                   ESP88150
         LA    R1,1(,R1)                                      ESP88150
         LA    R6,1(,R6)                                      ESP88150
         BCT   R0,FMMEMLO    DO MORE                          ESP88150
         CLI   0(R6),C')'    CORRECT TERMINATION ?            ESP88150
         BNE   FM4LONG       NO; SET SYNTAX ERROR             ESP88150
         LA    R6,1(,R6)     SKIP                             ESP88150
FMMEMND  CLI   DSNAMEX,C''''   QUOTED STRING ?                ESP88150
         BNE   FMDSNND       NO                               ESP88150
         BCTR  R6,0          CHECK LAST BYTE                  ESP88153
         CLC   0(1,R6),DSNAMEX  TRAILING QUOTE ?              ESP88153
         BE    FMDSNND       YES                              ESP88153
         LA    R5,1(,R5)     FINAGLE FOR LATER SUBTRACT       ESP88150
FMDSNND  LA    R6,DSNAMEX    SOURCE                           ESP88150
         MVC   TUDSNAME,BLANKS   CLEAR DSN                    ESP88150
         LA    R2,TUDSNAME   PLACE TO STUFF DSNAME            ESP88150
         CLI   DSNAMEX,C'''' TEST IF QUOTED                   ESP88150
         BE    FMDSNQUO      BR IF SO                         ESP88150
         CLC   =C'* ',DSNAMEX  SPECIAL NAME ?                    90050
         BE    FMDSNNQO      YES; LEAVE AS IS                    90050
         AIF   ('&LOCAL' NE 'PID').NODOLL                        92320
         CLI   DSNAMEX,C'$'  DOLLAR-SIGN PREFIX ?                91310
         BE    FMDSNDOL      YES; LOCAL NO-PREFIX CONVENTION     91310
.NODOLL  SPACE 1                                              ESP88150
*        PREFIX DSNAME WITH PREFIX (IF ANY)                   ESP88150
         ICM   R3,15,PREFIXL  LOAD AND TEST PREFIX LENGTH     ESP88150
         BNP   FMDSNNQO      SKIP IF NONE                     ESP88150
         BCTR  R3,0          ADJUST FOR EX                    ESP88150
         MVC   0(*-*,R2),PREFIX    MOVE PREFIX TO BUFFER      ESP88150
         EX    R3,*-6        MOVE IT                          ESP88150
         LA    R2,0(R3,R2)   ALMOST NEXT POS IN BUFFER        ESP88150
         CLI   0(R2),C'.'    INDEX POINT PRESENT ALREADY ?    ESP88150
         BNE   FMDSNDT       NO; ADD ONE                      ESP88150
         LA    R2,1(,R2)     SET TO START OF ADDITION         ESP88150
         B     FMDSNNQO      APPEND REMAINDER                 ESP88150
FMDSNDT  MVI   1(R2),C'.'    PUT A DOT IN THERE               ESP88150
         LA    R2,2(,R2)     PLACE FOR REST OF DSNAME         ESP88150
         B     FMDSNNQO      CONTINUE                         ESP88150
         SPACE 1                                              ESP88150
FMDSNDOL LA    R5,1(,R5)     REMOVE SINGLE PREFIX CHARACTER      91310
FMDSNQUO LA    R6,1(,R6)     PAST QUOTE                       ESP88150
         BCTR  R5,0          REDUCE LENGTH BY 2               ESP88150
         BCTR  R5,0                                           ESP88150
*                                                             ESP88150
FMDSNNQO LA    R0,TUDSNAME+L'TUDSNAME  LAST CHARACTER+1       ESP88150
         SR    R0,R2         LESS STARTING POSITION           ESP88150
         CR    R5,R0         WILL STRING FIT ?                ESP88150
         BH    FM4LONG       NO; FAIL IT                      ESP88150
         SH    R5,=H'1'      DECREASE LENGTH FOR EXECUTE         91310
         BM    FM4LONG       DSNAME NULL                         91310
         MVC   0(*-*,R2),0(R6)     COMPLETE DSNAME            ESP88150
         EX    R5,*-6                                         ESP88150
         LA    R5,TUDSNAME+L'TUDSNAME-1    POINT TO END OF DSN SP88150
         LA    R4,L'TUDSNAME       LENGTH OF DSNAME           ESP88150
         MVC   TEMP+8(8),BLANKS  CLEAR INDEXES                ESP88150
         MVC   TEMP(8),TUDSNAME  PRESET INDEX                 ESP88150
FMFINDL1 CLI   0(R5),C' '    IS IT BLANK?                     ESP88150
         BNE       FMFINDL2            NO, THEN FOUND END OF DSN 88150
         BCTR      R5,0                DECREMENT PTR          ESP88150
         BCT       R4,FMFINDL1         LOOP TILL FOUND        ESP88150
         B     FM4LONG       DSN MALFORMED                    ESP88150
FMFINDL2 LR        R3,R5               REMEMBER END OF DSN    ESP88150
         LA        R2,2                TRY TO FIND 2 LEVELS   ESP88150
FMFINDL3 CLI       0(R5),C'.'          IS IT A DOT?           ESP88150
         BE        FMFINDL4            YES, THEN HANDLE IT    ESP88150
FMFINDL5 BCTR      R5,0                DECREMENT PTR          ESP88150
         BCT       R4,FMFINDL3         LOOP TILL FOUND        ESP88150
         B         FMFINDE             BR IF FRONT OF DSN     ESP88150
FMFINDL4 CLI   TEMP+8,C' '   BEEN HERE BEFORE ?               ESP88150
         BNE   *+10          YES; DON'T CLOBBER LOWEST PORTION SP88150
         MVC   TEMP+8(8),1(R5)  MOVE LOW PORTION              ESP88150
         BCT       R2,FMFINDL5         FIND ANOTHER LEVEL     ESP88150
FMFINDE  MVC   FILNAM,BLANKS  CLEAR FILE NAME                 ESP88150
         CLI   TUMEMBER,C' ' ANY MEMBER NAME ?                ESP88150
         BE    FMFIND5       NO; DO AS BEFORE                 ESP88150
         CLI   TEMP+8,C' '   DSN ?                            ESP88150
         BE    *+10          NO                               ESP88150
         MVC   TEMP(8),TEMP+8  SLIDE INDEX BACK               ESP88150
         MVC   FILNAM(8),TUMEMBER  MAKE MEMBER NAME FIRST     ESP88150
         SLR   R3,R3                                          ESP88150
         LA    R5,FILNAM                                      ESP88150
FMFINDML CLI   0(R5),C' '    DONE ?                           ESP88150
         BE    FMFINDMN      YES                              ESP88150
         LA    R3,1(,R3)     LAZY                             ESP88150
         LA    R5,1(,R5)                                      ESP88150
         B     FMFINDML                                       ESP88150
FMFINDMN CLI   TEMP,C' '     ANY INDEX/DSN ?                  ESP88150
         BE    FMFIND3       HUH ?                            ESP88150
         MVI   0(R5),C'.'    MAKE FAKE INDEX                  ESP88150
         LA    R2,TEMP                                        ESP88150
         LA    R0,8                                           ESP88150
FMFINDNL CLI   0(R2),C' '    END OF DSN ?                     ESP88150
         BE    FMFIND3       YES                              ESP88150
         CLI   0(R2),C'.'    INDEX POINT ?                    ESP88150
         BE    FMFIND3       YES; QUIT                        ESP88150
         CLI   0(R2),C'('    GDG LEVEL ?                      ESP88150
         BE    FMFIND3       YES; QUIT                        ESP88150
         MVC   1(1,R5),0(R2)                                  ESP88150
         LA    R2,1(,R2)                                      ESP88150
         LA    R5,1(,R5)                                      ESP88150
         LA    R3,1(,R3)                                      ESP88150
         BCT   R0,FMFINDNL                                    ESP88150
         B     FMFIND3                                        ESP88150
FMFMVCF  MVC   FILNAM(*-*),0(R5)  EXECUTED MOVE               ESP88150
FMFIND5  LA    R5,1(,R5)     MOVE TO FRONT OF LEVEL           ESP88150
         SR    R3,R5         FIND LENGTH TO MOVE              ESP88150
         LA    R0,L'FILNAM-1 SET MAXIMUM LENGTH               ESP88150
         CR    R3,R0         TOO LONG ?                       ESP88150
         BNH   *+6           MO                               ESP88150
         LR    R3,R0         ELSE TRUNCATE                    ESP88150
         EX    R3,FMFMVCF    MOVE NAME                        ESP88150
FMFIND3  STH   R3,FILNAML    SAVE LENGTH - 1                  ESP88150
FILEPAST NI    PTCHFLG,255-FGTGEXT  RESET PARSE SWITCH        ESP88150
         LA    R15,L'TUDSNAME                                    90056
         LA    R14,TUDSNAME+L'TUDSNAME                           90056
NAMELENT BCTR  R14,0         GET PREVIOUS BYTE                   90056
         CLI   0(R14),C' '   IS IT TRAILING BLANK ?              90056
         BNE   NAMELENZ      NO                                  90056
         BCT   R15,NAMELENT                                      90056
NAMELENZ STH   R15,DSNAMEL   SET LENGTH OF EXPANDED NAME         90331
         TM    IOFLAGS,FGOUT  OUTPUT FILE ?                   ESP88150
         BZ    FMFINP        NO                               ESP88150
         MVC   KEROUT(MODOUTL),MODDCBO  REFRESH THE DCB       ESP88150
         MVC   TUDDNAME,DCBDDNAM-IHADCB+KEROUT                ESP88150
         MVI   TUSTAT,4      SET DISP=NEW                     ESP88150
         MVI   TUDISP,2      ,CATLG                           ESP88150
         MVI   TUINOUT,X'40'  SET OUTPUT                      ESP88153
         CLC   =C'* ',TUDSNAME  SPECIAL RECEIVE 'NAME' ?         90050
         BE    FM4LONG       RETURN ERROR AS MALFORMED DSNAME    90050
         SPACE 1                                                 90056
*        CHECK FOR VALID OS CATALOGABLE NAME.  INVALID CHARACTERS
*        ARE CHANGED TO DOLLAR SIGNS. NUMERICS AFTER A PERIOD ARE
*        CHANGED TO A LETTER (0=>Z, 1-9=>A-I). NINTH CHARACTER   90056
*        IN A ROW CHANGED TO PERIOD. SECOND OF CONSECUTIVE PERIODS
*        CHANGED TO DOLLAR SIGN.                                 90056
*                                                                90056
         SLR   R3,R3                                             90056
         ICM   R3,3,DSNAMEL  GET LENGTH OF FILE NAME             90331
         BZ    FM4LONG       NONE AT ALL - MALFORMED             90056
         BCTR  R3,0          SET EXECUTE LENGTH                  90056
         EX    R3,TRNAMTAB   MAKE ALL CHARACTERS VALID           90056
         SERVCALL DSTST,TUDSNAME  VALIDITY-CHECK THE NAME        90056
         BXLE  R15,R15,NAMEDONE   ALL DONE                       90056
         LA    R14,TUDSNAME                                      90056
         LA    R15,L'TUDSNAME                                    90056
NAMEINDX CLI   0(R14),C'.'   DOUBLED PERIOD ?                    90056
         BNE   *+8           NO                                  90056
         MVI   0(R14),C'$'   CREAM IT                            90056
         CLI   0(R14),C'0'   ALPHABETIC ?                        90056
         BL    NAMELEAD      YES                                 90056
         NI    0(R14),X'CF'  MAKE F0-F9 TO C0-C9                 90056
         CLI   0(R14),X'C0'  REALLY C0 ?                         90056
         BNE   *+8           NO                                  90056
         MVI   0(R14),C'Z'   LOCAL PREJUDICE                     90056
NAMELEAD LA    R0,8          SCAN FOR MAXIMUM OF 8 ALPHA         90056
NAMELEVL CLI   0(R14),C'.'   PERIOD ?                            90056
         BE    NAMELEVN      YES                                 90056
         LA    R14,1(,R14)                                       90056
         BCT   R15,*+8       COUNT DOWN THE LENGTH               90056
         B     NAMEENDS      END OF NAME                         90056
         BCT   R0,NAMELEVL   EXHAUST ALL                         90056
NAMELEVN MVI   0(R14),C'.'   MAKE INDEX LEVEL                    90056
         LA    R14,1(,R14)                                       90056
         BCT   R15,NAMEINDX  CHECK NEXT INDEX LEVEL              90056
         LH    R3,DSNAMEL    GET NAME LENGTH BACK                90331
         CH    R3,=Y(L'TUDSNAME)  AT MAXIMUM ?                   90056
         BL    NAMELEVA      NO; ADD A BYTE                      90056
         BCTR  R14,0         BACK-SPACE ONE                      90056
         MVI   0(R14),C' '   REMOVE LAST BYTE                    90056
         BCTR  R3,0          FIX LENGTH                          90056
         B     NAMEENDL      ADJUST LENGTH                       90056
NAMELEVA MVI   0(R14),C'$'   MAKE NEW NAME                       90056
         LA    R3,1(,R3)     FIX LENGTH                          90056
NAMEENDL STH   R3,DSNAMEL    FIX LENGTH OF NAME                  90331
NAMEENDS DS    0H                                                91315
         AIF   ('&LOCAL' NE 'PID').NOLIDOL                       92320
         CLI   TUDSNAME,C'A' STARTS WITH SPECIAL CHARACTER ?     91315
         BNL   NAMEDONE                                          90056
         MVI   TUDSNAME,C'A' FORCE ALPHABETIC FIRST BYTE         90056
.NOLIDOL ANOP  ,                                                 91315
NAMEDONE L     R14,CATLOOK   GET CAMLST BASE                  ESP88150
         LA    R15,TMPDATA   GET DATASET NAME                 ESP88150
         MVC   TMPDATA(L'TUDSNAME),TUDSNAME NAME MAY GET CHANGED 88150
         SLR   R0,R0                                          ESP88150
         LA    R1,CAMBUFF    WORKSPACE                        ESP88150
         STM   R14,R1,TEMP                                    ESP88150
         LOCATE TEMP         CHECK THE CATALOG                ESP88150
         CLC   TUDSNAME,TMPDATA  DID NAME GET CHANGED ?       ESP88150
         BNE   FMFOAL        YES; IGNORE ALIAS NAME           ESP88150
         BXH   R15,R15,FMFALOC  NOT FOUND - DO ALLOCATION     ESP88150
FMFOAL   CLI   TUMEMBER,C' '   DID USER SPECIFY A MEMBER      ESP88150
         BNE   FMFOOLD       YES; QUICK+DIRTY BYPASS          ESP88150
         LA    R15,8         SET NON-REPLACE ERROR CODE       ESP88150
         TM    FLAGS,FLG2    ALLOWED TO REPLACE ?             ESP88150
         BZ    FILEMAKX      NO; RETURN ERROR                 ESP88150
         TM    IOFLAGS,FGUPD UPDATE DCB OPTIONS ?                91084
         BZ    FMFOOLD       NO; LET OLD ONES MERGE IN           91084
         CLI   RECFM,C'V'    VARIABLE ?                          91084
         BE    FMFOVAR       YES                                 91084
         CLI   RECFM,C'F'    FIXED ?                             91084
         BE    FMFOFIX       YES                                 91084
         CLI   RECFM,C'W'    WYLBUR EDIT ?                       91084
         BNE   FMFOOLD       NO; IGNORE                          91084
         MVI   DCBRECFM-IHADCB+KEROUT,DCBRECU  SET UNDEFINED     91084
         MVC   DCBLRECL-IHADCB+KEROUT,BLKSIZE  USE USER'S        91084
         MVC   DCBBLKSI-IHADCB+KEROUT,BLKSIZE  USE USER'S        91084
         B     FMFOOLD                                           91084
FMFOFIX  MVI   DCBRECFM-IHADCB+KEROUT,DCBRECF+DCBRECBR  FB       91084
         MVC   DCBLRECL-IHADCB+KEROUT,LRECL  USE USER'S          91084
         MVC   DCBBLKSI-IHADCB+KEROUT,BLKSIZE                    91084
         B     FMFOOLD                                           91084
FMFOVAR  MVI   DCBRECFM-IHADCB+KEROUT,DCBRECV+DCBRECBR  VB       91084
         MVC   DCBLRECL-IHADCB+KEROUT,LRECL                      91084
         MVC   DCBBLKSI-IHADCB+KEROUT,BLKSIZE                    91084
FMFOOLD  MVI   TUSTAT,1      DISP=OLD                         ESP88150
         MVI   TUDISP,8      ,KEEP                            ESP88150
*    THE DSNAME ACCESS CHECK DONE HERE LACKS THE VOLUME SERIAL TO BE
*      ACCESSED, BUT SHORTENS THE CHECKING.                      92082
*    THE DDNAME CHECK PERFORMED LATER (POST-ALLOCATION) HAS ALL THE
*      INFORMATION, BUT CANNOT UNDO THE EFFECT OF ALLOCATION.    92082
FMFALOC  TM    MODEFG,FGNET  MULTI-TASKING MODE ?                92082
         BZ    FMFALOC2      NO                                  92082
         L     R14,FAKECPPL+8  GET MTS ADDRESS BACK              92082
         XC    MTSDDB-MTS(8,R14),MTSDDB-MTS(R14)                 92082
         LA    R0,TUDSNAME   POINT TO DSNAME                     92082
         ST    R0,MTSDDB+4-MTS(,R14)                             92082
         MVI   MTSDDB+3-MTS(R14),X'81'  SET OUTPUT, DSN MODE     92082
         LA    R1,MTSDDB-MTS(,R14)                               92082
         ICM   R15,15,MTSDDACC-MTS(R14)  ACCESS CHECK ?          92082
         BZ    FMFALOC2      NO; SKIP                            92082
         BALSR R14,R15       CHECK USER'S PRIVILEGES             92082
         BXH   R15,R15,FILEMACF  ACCESS FAILED                   92082
FMFALOC2 SUBCALL DYNALC,DYNAPARM  ALLOCATE                    ESP88150
         LA    R15,12        PRESET FOR ALLOCATION FAILURE    ESP88150
         ICM   R1,B'1111',DYNALCRC GET RETURN CODE            ESP88150
         BNZ   FILEMAKX      BRANCH IF IT FAILED              ESP88150
         MVC   DCBDDNAM-IHADCB+KEROUT,TUDDNAME  GET CURRENT NAME 89331
         MVC   DECOMP(DECOMPLN),PATDECOM  RE-INIT DECOMPRESS LIST
         TM    MODEFG,FGNET  MULTI-TASKING MODE ?                89337
         BZ    FILENACO      NO                                  89337
         L     R14,FAKECPPL+8  GET MTS ADDRESS BACK              89337
         XC    MTSDDB-MTS(8,R14),MTSDDB-MTS(R14)                 89337
         LA    R0,TUDDNAME   POINT TO DDNAME                     89337
         ST    R0,MTSDDB+4-MTS(,R14)                             89337
         MVI   MTSDDB+3-MTS(R14),X'80'  SET OUTPUT MODE          89337
         LA    R1,MTSDDB-MTS(,R14)                               89337
         ICM   R15,15,MTSDDACC-MTS(R14)  ACCESS CHECK ?          89337
         BZ    FILENACO      NO; SKIP                            89337
         BALSR R14,R15       CHECK USER'S PRIVILEGES             89337
         BXH   R15,R15,FILEMACF  ACCESS FAILED                   89337
FILENACO SUBCALL CHECKDSN,TUDDNAME                               94089
         BXH   R15,R15,FM4LONG         MALFORMED DSN             94089
         OPEN  (,(OUTPUT)),MF=(E,@IODCB)  OPEN FOR OUTPUT     ESP88150
         B     FMFPOP        GO TO POST-OPEN CHECK            ESP88150
         SPACE 1                                              ESP88150
*    THE DSNAME ACCESS CHECK DONE HERE LACKS THE VOLUME SERIAL TO BE
*      ACCESSED, BUT SHORTENS THE CHECKING.                      92082
*    THE DDNAME CHECK PERFORMED LATER (POST-ALLOCATION) HAS ALL THE
*      INFORMATION, BUT CANNOT UNDO THE EFFECT OF ALLOCATION.    92082
FMFINP   TM    MODEFG,FGNET  MULTI-TASKING MODE ?                92082
         BZ    FMFINP2       NO                                  92082
         L     R14,FAKECPPL+8  GET MTS ADDRESS BACK              92082
         XC    MTSDDB-MTS(8,R14),MTSDDB-MTS(R14)                 92082
         LA    R0,TUDSNAME   POINT TO DSNAME                     92082
         ST    R0,MTSDDB+4-MTS(,R14)                             92082
         MVI   MTSDDB+3-MTS(R14),X'01'  SET INPUT, DSN MODE      92082
         LA    R1,MTSDDB-MTS(,R14)                               92082
         ICM   R15,15,MTSDDACC-MTS(R14)  ACCESS CHECK ?          92082
         BZ    FMFINP2       NO; SKIP                            92082
         BALSR R14,R15       CHECK USER'S PRIVILEGES             92082
         BXH   R15,R15,FILEMACF  ACCESS FAILED                   92082
FMFINP2  MVC   KERIN(MODINL),MODDCBI  REFRESH DCB FROM PATTERN SP88150
         AIF   (NOT &INPREAD).NOLINOP                            91315
         MVC   DECOMP(DECOMPLN),PATDECOM  RE-INIT DECOMPRESS LIST
.NOLINOP MVC   TUDDNAME,DCBDDNAM-IHADCB+KERIN                 ESP88150
         MVI   TUSTAT,8      DISP=SHR                         ESP88150
         MVI   TUDISP,8      ,KEEP                            ESP88150
         MVI   TUINOUT,X'80'  INPUT                           ESP88153
         SUBCALL DYNALC,DYNAPARM  CALL ALLOCATION             ESP88150
         LA    R15,12        SET FOR ALLOCATION FAILED        ESP88150
         ICM   R1,B'1111',DYNALCRC GET RETURN CODE            ESP88150
         BNZ   FILEMAKX      RETURN ALLOCATION FAILURE        ESP88150
         MVC   DCBDDNAM-IHADCB+KERIN,TUDDNAME  GET CURRENT NAME  89331
         TM    MODEFG,FGNET  MULTI-TASKING MODE ?                89337
         BZ    FILENACI      NO                                  89337
         L     R14,FAKECPPL+8  GET MTS ADDRESS BACK              89337
         XC    MTSDDB-MTS(8,R14),MTSDDB-MTS(R14)                 89337
         LA    R0,TUDDNAME   POINT TO DDNAME                     89337
         ST    R0,MTSDDB+4-MTS(,R14)                             89337
*        MVI   MTSDDB+3-MTS(R14),X'00'  SET INPUT MODE           89337
         LA    R1,MTSDDB-MTS(,R14)                               89337
         ICM   R15,15,MTSDDACC-MTS(R14)  ACCESS CHECK ?          89337
         BZ    FILENACI      NO; SKIP                            89337
         BALSR R14,R15       CHECK USER'S PRIVILEGES             89337
         BXH   R15,R15,FILEMACF  ACCESS FAILED                   89337
FILENACI SUBCALL CHECKDSN,TUDDNAME                               94089
         BXH   R15,R15,FM4LONG         MALFORMED DSN             94089
*                                                             ESP88150
*  open the user's data set                                   ESP88150
*                                                             ESP88150
         OPEN  (,(INPUT)),MF=(E,@IODCB)   OPEN FOR INPUT      ESP88150
         MVC   CURLRMAX,DCBLRECL+KERIN-IHADCB  SAVE LRECL     ESP88150
FMFPOP   LA    R15,16        SET OPEN FAILED                  ESP88150
         TM    IOFLAGS,FGABEND  MEMBER MISSING, ETC. ?           91112
         BNZ   FMACFAIL      YES; OPEN FAILED                    91112
         TM    IOFLAGS,FGOPF   DCB EXIT SUCCESSFUL ?          ESP88150
         BNZ   FILEMAKX      NO; SET INDICATOR                ESP88150
         TM    KERIN+(DCBOFLGS-IHADCB),DCBOFOPN               ESP88150
         BO    FMOPEND                                        ESP88150
FMACFAIL LA    R15,20        OTHER BAD OPEN                   ESP88150
         B     FILEMAKX                                       ESP88150
FMOPEND  SLR   R15,R15       SET ALLOCATION SUCCESSFUL        ESP88150
         TM    KERIN+DCBRECFM-IHADCB,DCBRECF OTHER THAN V?    ESP88150
         BNZ   FMOPNF        YES                              ESP88150
         LH    R0,CURLRMAX   GET MAXIMUM LRECL                ESP88150
         SH    R0,=H'4'      ADJUST FOR RDW                   ESP88150
         BNP   FILEMR16      TOO BAD                          ESP88150
         STH   R0,CURLRMAX   SET MAXIMUM DATA LENGTH          ESP88150
FMOPNF   STH   R15,CURLRECL  CLEAR CURRENT RECORD SIZE        ESP88150
         STH   R15,TXTPOS    CLEAR TEXT LENGTH                   91315
         LH    R0,DCBLRECL+KERIN-IHADCB  GET LRECL               91059
         AH    R0,=H'4'      LENGTH WITH RDW                     91059
         C     R0,@IORDW+4   WILL IT FIT INTO BUFFER ?           91059
         BNH   FMOPBUFY      YES                                 91059
         LR    R3,R0         SAVE IT                             91059
         LM    R1,R2,@IORDW  LOAD OLD ADDRESS/LENGTH             91059
         FREEMAIN R,A=(1),LV=(R2)  FREE OLD STORAGE              91059
         LR    R2,R3         RESTORE SAVED LENGTH                91059
         GETMAIN R,LV=(R2)   GET STORAGE                         91059
         STM   R1,R2,@IORDW  STASH IT                            91059
         LA    R1,4(,R1)     POINT TO DATA PORTION               91059
         ST    R1,@IOBUF     SAVE POINTER                        91059
         SLR   R15,R15       DON'T TRUST NOBODY, NOHOW           91059
FMOPBUFY MVI   PTCHFLG,0     RESET OUTPUT LOGIC FLAG          ESP88150
         TM    KERIN+DCBRECFM-IHADCB,DCBRECU  UNDEFINED ?     ESP88150
         BNO   FILEMAKX      NO; F OR V - SET RC=0            ESP88150
         OI    IOFLAGS,FGWYL SHOW WYLBUR RECORD               ESP88312
         B     FILEMAKX      GO TO NORMAL RETURN                 89333
FILEMACF LA    R15,24        ACCESS DENIED BY SECURITY           91091
         B     FILEMAKX      AND EXIT                            91091
FILEMR16 LA    R15,16        DCB ERROR                        ESP88150
FILEMAKX LTR   R1,R15        NORMAL RETURN ?                  ESP88150
         BNP   FILEMAKZ      YES                              ESP88150
         MVI   STATE,C'A'    ABORT TRANSFER                   ESP88150
         SRA   R1,2          MAKE RETURN CODE INTO OFFSET     ESP88150
         LA    R1,FILEMERR-1(R1)  LOAD ERROR MESSAGE CODE     ESP88150
         MVC   ERRNUM,0(R1)  RETURN IT                        ESP88150
         LR    R2,R15        PRESERVE RETURN CODE                91091
         SUBCALL FREEFILE    FREE ALLOCATION NOW                 91091
         LR    R15,R2        RESTORE RETURN                      91091
FILEMAKZ SUBEX RC=(R15)      RETURN WITH RC                   ESP88150
         SPACE 1                                              ESP88150
FILEMERR DC    AL1(11,17,16,15,18,20)                            91091
CATLOOK  CAMLST NAME,1,,3    CATALOG LOOK-UP PATTERN          ESP88150
         ORG   CATLOOK+4     KEEP ONLY FLAGS                  ESP88150
         SPACE 1                                                 90056
TRNAMTAB TR    TUDSNAME(0),OSNAMTAB  MAKE NAME BYTES VALID       90056
OSNAMTAB DC    256C'$'       DEFAULT INVALID AS DOLLAR SIGNS     90056
         ORG   OSNAMTAB+X'81'  LOWER CASE A-I                    90056
         DC    C'ABCDEFGHI'                                      90056
         ORG   OSNAMTAB+X'91'  LOWER CASE J-R                    90056
         DC    C'JKLMNOPQR'                                      90056
         ORG   OSNAMTAB+X'A2'  LOWER CASE S-Z                    90056
         DC    C'STUVWXYZ'                                       90056
         ORG   OSNAMTAB+C'$'   DOLLAR SIGN                       90056
         DC    C'$'                                              90056
         ORG   OSNAMTAB+C'#'   POUND SIGN                        90056
         DC    C'#'                                              90056
         ORG   OSNAMTAB+C'@'   AT SIGN                           90056
         DC    C'@'                                              90056
         ORG   OSNAMTAB+C'A'   UPPER CASE A-I                    90056
         DC    C'ABCDEFGHI'                                      90056
         ORG   OSNAMTAB+C'J'   UPPER CASE J-R                    90056
         DC    C'JKLMNOPQR'                                      90056
         ORG   OSNAMTAB+C'S'   UPPER CASE S-Z                    90056
         DC    C'STUVWXYZ'                                       90056
         ORG   OSNAMTAB+C'0'   DIGITS 0-9                        90056
         DC    C'0123456789'                                     90056
         ORG   OSNAMTAB+C'.'   INDEX POINT                       90056
         DC    C'.'                                              90056
         ORG   ,                                                 90056
         SPACE 1                                                 94089
CHECKDSN SUBENT ,            CHECK DSN: SEQ  MEMBER; PDS + MEM  94089
         LR    R3,R1         PRESERVE DDNAME PARAMETER           94089
         DEVTYPE (1),TEMP    GET UCBTBYT1-4                      94089
         BXH   R15,R15,CHECKDSX  HUH?                            94089
         CLI   TEMP+2,UCB3DACC  DIRECT ACCESS?                   94089
         BNE   CHECKDSX      NO; SHOULD FAIL LATER?              94089
         SERVCALL RJFCB,(R3)  GET JFCB FROM DDNAME               94089
         BXH   R15,R15,CHECKDSX  HUH?                            94089
         USING INFMJFCB,R1                                       94089
         IC    R2,JFCBIND1   SAVE JFCB'S PDS FLAG                94089
         SERVCALL DSDJ1,(R1)   GET DSCB FOR JFCB                 94089
         USING DS1FMTID,R1                                       94089
         BXH   R15,R15,CHECKDSX  ?  TAPE NOT SUPPORTED           94089
         CLI   DS1DSORG+1,0       PDS ?                         GP09186
         BNE   CHECKDSF      NO; IGNORE                         GP09186
         ICM   R0,3,DS1DSORG  ANY DSORG ASSIGNED ?               94091
         BNZ   CHECKDNN      YES; CONTINUE                       94091
         TM    IOFLAGS,FGOUT  OUTPUT FILE ?                      94091
         BNZ   CHECKDSX      YES; LET OPEN SET DSCB1             94091
CHECKDNN N     R2,=A(JFCPDS) PDS MEMBER REQUESTED ?              94089
         BNZ   CHECKDSP      YES; NEED PDS                       94089
CHECKDSQ TM    DS1DSORG,DS1DSGPS  SEQUENTIAL?                    94089
         BNZ   CHECKDSX      YES; PROCEED                        94089
         B     CHECKDSF      NO; FAIL                            94089
CHECKDSP TM    DS1DSORG,DS1DSGPO  PARTITIONED DATASET?           94089
         BNZ   CHECKDSX      YES; CONTINUE                       94089
CHECKDSF LA    R15,16        FAIL INVALID REQUEST                94089
CHECKDSX SUBEX RC=(R15)      RETURN WITH CODE                    94089
         DROP  R1                                                94089
         EJECT ,                                              ESP85142
FREEFILE SUBENT ,            CLOSE AND FREE DATA FILE         ESP88150
         TM    IOFLAGS,FGOPF   OPEN FAILED ?                  ESP85142
         BZ    FREEFILM      NO                               ESP85142
         NI    IOFLAGS,255-FGOPF                              ESP85142
         WTERM  ' Request cancelled due to inconsistent DCB parameters'
FREEFILM TM    KERIN+DCBOFLGS-IHADCB,DCBOFOPN  DCB OPEN ?     ESP85142
         BZ    FREEFILD      NO                               ESP85142
         CLOSE MF=(E,@IODCB) CLOSE IT                         ESP88150
         FREEPOOL KERIN      AND FREE BUFFERS                 ESP85142
FREEFILD TM    IOFLAGS,FGALLO  NEED TO FREE ?                 ESP85142
*DEFER*  BZ    FREEFILX      NO; JUST RETURN                  ESP85142
         SUBCALL DYNALC,DYNAFREE  FREE ALLOCATION             ESP88150
FREEFILX NI    IOFLAGS,255-FGDONE  RESET                      ESP85142
         SUBEX ,             RETURN TO CALLER                 ESP88150
         SPACE 1                                              ESP88312
PATDECOM SERVCOMP PFX=PCM,DSECT=NO    WYLBUR DECOMPRESS PARMS ESP88312
DECOMPLN EQU   *-PATDECOM                                     ESP88312
         SPACE 1                                              ESP88150
***********************************************************************
*                                                                     *
*        BYE COMMAND - REQUEST TSO LOGOFF AND QUIT                    *
*                                                                     *
***********************************************************************
SYSBYE   SUBENT ,                                             ESP88150
         TM    MODEFG,FGWYSS   WYLBUR ?                          91267
         BNZ   SYSBYECM      YES; NO HANG-UP                     91267
         SLR   R2,R2                                          ESP88277
         IC    R2,PROTOCON   GET INTERFACE TYPE               ESP88277
         B     *+4(R2)       AND BRANCH ACCORDINGLY           ESP88277
         B     SYSBYECM      NONE                             ESP88277
         B     SYSBCX80      CX-80                            ESP88277
*        B     SYSB7171      IBM 7171 DISCONNECT ?            ESP90086
SYSBCX80 TPUT  HANGCX80,L'HANGCX80,FULLSCR,WAIT,HOLD          ESP88277
         SPACE 1                                              ESP88277
SYSBYECM OI    FLAGS,FGBYE   SET LOGOFF REQUEST               ESP88150
         TM    MODEFG,FGNET  NON-TSO VERSION ?                   89330
         BNZ   SYSBEXIT      YES; DON'T ABEND                    89330
         GETMAIN R,LV=16+8,SP=78  GET TMP STACK SPACE         ESP88150
         LA    R2,16(,R1)    COMMAND ADDRESS                  ESP88150
         LR    R4,R2         FIRST COMMAND                    ESP88150
         L     R3,=X'00080008'  RECLEN / TOTAL LENGTH         ESP88150
         SLR   R5,R5                                          ESP88150
         STM   R2,R5,0(R1)   COMPLETE LSD                     ESP88150
         MVC   16(8,R1),=CL8'LOGOFF  '  MAKE LOGOFF COMMAND   ESP88150
         AIF   ('&LOCAL' NE 'PID').NOSIGN                        92320
         ORG   *-6                                            ESP88277
         MVC   16(8,R1),=CL8'SIGNOFF '  POSITIVE DISCONNECT   ESP88277
.NOSIGN  LR    R2,R1         COPY LSD ADDRESS                 ESP88150
         LA    R0,MYSTACK    POINT TO MY STACK WORK AREA      ESP88150
         ST    R0,IOPLIOPB-IOPL+MYIOPL  PLACE IN LIST         ESP88150
         STACK STORAGE=((R2),SOURCE),MF=(E,MYIOPL)            ESP88150
SYSBEXIT SUBEX ,             RETURN TO CALLER                 ESP88150
         SPACE 1                                              ESP88277
HANGCX80 DC    X'27F1C37082A8852B5B4B'  CX-80 DISCONNECT      ESP88277
         EJECT ,                                              ESP88277
***********************************************************************
*                                                                     *
*        DSNPARSE - ROUTINE TO PARSE DSN + OPTIONAL GDG OR MEMBER     *
*              R1 - DSN/MEMBER OUTPUT     R6 - INPUT POINTER          *
*                                                                     *
***********************************************************************
DSNPARSE SUBENT ,                                             ESP88277
         LR    R9,R1         SAVE OUTPUT                      ESP88277
         SLR   R7,R7         SET NON-QUOTE FLAG               ESP88277
         LA    R2,L'TUDSNAME MAX LENGTH OF DSNAME             ESP88277
         LR    R14,R1        SET OUTPUT ADDRESS               ESP88277
         LA    R15,L'TUDSNAME+L'TUMEMBER  LENGTH FOR DSN+MEMBER P88277
         LA    R0,PREFIX     SET FOR PREFIX                   ESP88277
         L     R1,PREFIXL    GET PREFIX LENGTH                ESP88277
         AIF   ('&LOCAL' NE 'PID').NODOLQ                        92320
         CLI   0(R6),C'$'    UNQUOTED STRING ?                   91315
         BNE   DSNPARND      NO                                  91315
         SLR   R1,R1         CLEAR QUOTE LENGTH                  91315
         LA    R6,1(,R6)     SKIP UNQUOTE                        91315
         B     DSNPUNQT      SKIP OTHER TEST                     91315
.NODOLQ  ANOP  ,                                                 91315
DSNPARND CLI   0(R6),C''''   IS THIS A QUOTED STRING ?        ESP88277
         BE    DSNPQUOT      YES                              ESP88277
         CLI   0(R6),C'"'    ALTERNATE QUOTE ?                ESP88277
         BNE   DSNPUNQT      NO                               ESP88277
DSNPQUOT LR    R7,R6         SAVE ADDRESS OF QUOTE            ESP88277
         LA    R6,1(,R6)     SKIP OVER IT                     ESP88277
         SLR   R1,R1         SET PREFIX LENGTH TO ZERO        ESP88277
DSNPUNQT LA    R5,0(R14,R1)  START OF TEXT INSERTION          ESP88277
         ICM   R1,8,BLANKS   SET BLANK FILL                   ESP88277
         MVCL  R14,R0        MOVE PREFIX AND BLANK FILL REST  ESP88277
         LR    R1,R5         SET SCAN START                   ESP88277
         CR    R1,R9         ANY PREFIX ?                     ESP88277
         BE    DSNPDSNL      NO; START SCANNING               ESP88277
         S     R2,PREFIXL    MAKE LENGTH AFTER PREFIX         ESP88277
         BCTR  R5,0          BACK-SPACE ONE                   ESP88277
         CLI   0(R5),C'.'    PREFIX+INDEX ?                   ESP88277
         BE    DSNPDSNL      YES; START SCANNING              ESP88277
         CLI   0(R6),C'.'    DSN FRACTION WITH INDEX ?        ESP88277
         BE    DSNPDSNL      YES; MOVE IT                     ESP88277
         CLI   0(R6),C'('    GDG OR MEMBER NAME ?             ESP88277
         BE    DSNPDSNL      YES; NO INDEX LEVEL              ESP88277
         BCTR  R2,0          NEW LENGTH                       ESP88277
         MVI   1(R5),C'.'    SET INDEX POINT                  ESP88277
         LA    R1,2(,R5)     SET FILL START                   ESP88277
DSNPDSNL LTR   R7,R7         QUOTED STRING ?                  ESP88277
         BZ    DSNPDSNQ      NO                               ESP88277
         CLC   0(1,R6),0(R7)  QUOTE CHARACTER ?               ESP88277
         BNE   DSNPDSYQ      NO; DO NORMALLY                  ESP88277
         LA    R6,1(,R6)     SKIP FIRST OF PAIR               ESP88277
         CLC   0(1,R6),0(R7) DOUBLED QUOTE ?                  ESP88277
         BNE   DSNPDSND      NO; END OF INPUT                 ESP88277
DSNPDSNQ CLI   0(R6),C' '    IS THIS END OF FIELD ?           ESP88277
         BE    DSNPDSND      YES; PROCESS                     ESP88277
DSNPDSYQ CLI   0(R6),C'('    START OF MEMBER ?                ESP88277
         BNE   DSNPMVC       NO                               ESP88277
         CLI   1(R6),C'-'    OLD GDG ENTRY                    ESP88277
         BE    DSNPMVC       YES; CONTINUE                    ESP88277
         CLI   1(R6),C'+'    NEW GDG ENTRY ?                  ESP88277
         BE    DSNPMVC       YES (FAILS IF PC RECEIVE)        ESP88277
         CLI   1(R6),C'0'    NUMERIC ARGUMENT ?               ESP88277
         BL    DSNPMEM       NO; BRANCH TO MEMBER PROCESSING  ESP88277
DSNPMVC  MVC   0(1,R1),0(R6) MOVE A CHARACTER                 ESP88277
         LA    R6,1(,R6)     MOVE ALONG INPUT BUFFER          ESP88277
         LA    R1,1(,R1)     MOVE ALONG DSNAME BUFFER         ESP88277
         BCT   R2,DSNPDSNL   KEEP LOOKING FOR END             ESP88277
         CLI   0(R6),C'('    START OF MEMBER ?                ESP88277
         BE    DSNPMEM       YES                              ESP88277
DSNPLONG LA    R15,4         SET FOR DSN MALFORMED            ESP88277
         B     DSNPRETX      QUIT NOW                         ESP88277
DSNPMEM  LA    R1,L'TUDSNAME(,R9)  POINT PAST DSN TO MEMBER   ESP88277
         LA    R0,8          MAXIMUM MEMBER NAME LENGTH       ESP88277
         LA    R6,1(,R6)     SKIP OVER THE '('                ESP88277
DSNPMEML CLI   0(R6),C')'    END OF MEMBER ?                  ESP88277
         BE    DSNPDSND      YES                              ESP88277
         MVC   0(1,R1),0(R6)    COPY A BYTE                   ESP88277
         LA    R1,1(,R1)                                      ESP88277
         LA    R6,1(,R6)                                      ESP88277
         BCT   R0,DSNPMEML   DO MORE                          ESP88277
         CLI   0(R6),C')'    CORRECT TERMINATION ?            ESP88277
         BNE   DSNPLONG      NO; SET SYNTAX ERROR             ESP88277
         LA    R6,1(,R6)     SKIP                             ESP88277
DSNPDSND LTR   R7,R7         QUOTED STRING ?                  ESP88277
         BZ    DSNPDSNN      NO                               ESP88277
         BCTR  R6,0          CHECK LAST BYTE                  ESP88277
         CLC   0(1,R6),0(R7) TRAILING QUOTE ?                 ESP88277
         BNE   DSNPLONG      NO; SET MALFORMED                ESP88277
DSNPDSNN SLR   R15,R15       SET PARSED                       ESP88277
DSNPRETX SUBEX RC=(R15)      RETURN WITH CODE                 ESP88277
         AIF   (NOT &INPREAD).NOTAKE4                            91315
         SPACE 1                                              ESP88277
***********************************************************************
*                                                                     *
*        DYNAMIC ALLOCATE FOR TAKE FILES                              *
*                                                                     *
***********************************************************************
DSNALLOC SUBENT ,            ALLOCATE DSN IN TAW              ESP88277
         USING TAWSECT,R4    DECLARE MAPPING                  ESP88277
         LA    R1,TAWDSN     IS THERE A DSN ?                 ESP88277
         ST    R1,TEMP       BUILD IKJPARSE TYPE INFO         ESP88277
         MVC   TEMP+4(4),=AL1(0,L'TAWDSN,X'80',0)  BUILD LENGTH/FLAGS
         LA    R1,TAWMEM     POINT TO MEMBER                  ESP88277
         ST    R1,TEMP+8     STASH MEMBER                     ESP88277
         MVC   TEMP+12(4),=AL1(0,L'TAWMEM,X'80',0) BUILD LENGTH/FLAGS
         CLI   TAWMEM,C' '   ANY MEMBER NAME ?                ESP88277
         BH    *+10          YES                              ESP88277
         XC    TEMP+12(4),TEMP+12  CLEAR IT AGAIN             ESP88277
         ALLOC DDNTO=TAWDDN,DSN=TEMP,MEMBER=TEMP+8,DISP=SHR      91091
         LTR   R9,R15                                         ESP88277
         BNZ   DSNAFAIL      FAILED; GET MESSAGE                 90050
         TM    MODEFG,FGNET  MULTI-TASKING MODE ?                90050
         BZ    DSNARETC      NO                                  90050
         L     R14,FAKECPPL+8  GET MTS ADDRESS                   90050
         MVC   MTSDDNCT-MTS(8,R14),TAWDDN  SAVE DDNM FOR CLEANUP 90050
         B     DSNARETC      SUCCESSFUL ALLOCATION               90050
         S99FAIL RB=DYN1RB,CPPL=@CPPL,MF=(E,S99FAIL,S99FAILL)    91081
*DEFER   LA    R9,4          SET GOOD COMPLETION              ESP88278
*DEFER*  BXLE  R15,R15,DSNARETC  MESSAGE(S) WRITTEN           ESP88278
DSNAFAIL LA    R9,8          REQUEST NON-SPECIFIC MESSAGE     ESP88278
DSNARETC SUBEX RC=(R9)       RETURN                              90050
         SPACE 2
***********************************************************************
*                                                                     *
*        DYNAMIC FREE FOR TAKE FILES                                  *
*                                                                     *
***********************************************************************
DDFREE   SUBENT ,            FREE DDNAME IN R1                ESP88277
         LA    R4,TAWDDN-TAWPTR(,R1)  POINT TO DDNAME            91091
         SERVICE ALCFD,(R4)  FREE IT                             91091
         LTR   R9,R15        SAVE AND TEST RETURN CODE           90050
         BNZ   DDFRFAIL      FAILED                              90050
         TM    MODEFG,FGNET  MULTI-TASKING MODE ?                90050
         BZ    DDFRFAIL      NO                                  90050
         L     R14,FAKECPPL+8  GET MTS ADDRESS                   90050
         LA    R15,MTSDDNIO-MTS(,R14)  POINT TO I/O NAME         90050
         CLC   0(8,R4),0(R15)  I/O DD NAME ?                     90050
         BE    DDFREEZ       YES; ZERO FIELD                     90050
         LA    R15,MTSDDNCT-MTS(,R14)  ELSE TRY CONTROL DD       90050
         CLC   0(8,R4),0(R15)  CONTROL DD ?                      90050
         BNE   DDFREEZ       HUH ?                               90050
DDFREEZ  XC    0(8,R15),0(R15)  CLEAR IT                         90050
DDFRFAIL SUBEX RC=(R9)       RETURN                              90050
.NOTAKE4 EJECT ,                                              ESP85142
***********************************************************************
*                                                                     *
*        DYNAMIC ALLOCATION CODE (WAS SEPARATE MODULE)        ESP85142
*                                                                     *
***********************************************************************
         SPACE 1
WTPTPUT  EQU   64            0 - TPUT (REQ. CPPL); 128 - WTP  ESP85142
*                                                             ESP85142
DYNALC   SUBENT ,            R1 => TO REQUEST                    89345
         LR    R9,R1                                             89345
         USING ARGADDS,R9                                        89345
         OI    TEXTOLD,X'80'  SET SHORT LIST                  ESP85142
         MVI   DYNRB+1,S99VRBUN  SET FOR UN-ALLOCATION        ESP85142
         MVI   TUDDN+1,DUNDDNAM  DDNAME TO BE REMOVED            91112
         LA    R4,TEXTOLD    PRESET FOR OLD DATASET           ESP85142
         TM    AIRETCD,X'80'  SHORT ENTRY ?                   ESP85142
         BNZ   DYNALCOM      YES; GO TO UNALLOCATE IT         ESP85142
         NI    TEXTOLD,255-X'80'  MAKE LONG LIST              ESP85142
         MVI   DYNRB+1,S99VRBAL  SET FOR ALLOCATION           ESP85142
         MVI   TUDDN+1,DALRTDDN  DDNAME TO BE RETURNED           91112
         OI    TEXTOLDL,X'80'    SET FOR NO MEMBER NAME       ESP85142
         OI    TEXTNEWL,X'80'                                 ESP85142
         CLI   TUMEMBER,C' '   ANY MEMBER NAME ?              ESP85142
         BNH   DYNAMEM       NO                               ESP85142
         NI    TEXTOLDL,255-X'80'  ADD MEMBER NAME TO LIST    ESP85142
         NI    TEXTNEWL,255-X'80'                             ESP85142
DYNAMEM  TM    TUSTAT,4      DISP=NEW ?                       ESP85142
         BNO   DYNALCOM      NO; USE OLD
         LA    R4,TEXTNEW    SET FOR NEW LIST                 ESP85142
         LA    R0,TUVSR      SET FOR EXPLICIT SERIAL             91287
         ST    R0,TEXTNEWU                                       91287
         CLI   TUVOLSER,C' '   USER SPECIFIED VOLUME ?           91287
         BH    DYNALCOM      YES                                 91287
         LA    R0,TUUNT      ELSE SET FOR UNIT                   91287
         ST    R0,TEXTNEWU                                       91287
         AIF   ('&LOCAL' NE 'PID').NOWYLTS                       92320
         CLC   =C'TSO',TUUNIT  RESTRICTED TO OUR PACK(S) ?    ESP89279
         BNE   DYNALCOM      NO                               ESP89279
         CLC   =C'WYL.',TUDSNAME  WYLBUR PREFIX ?             ESP89279
         BNE   DYNALCOM      NO                               ESP89279
         LA    R4,TEXTWYL    FORCE TO WYLBUR GROUP            ESP89279
         MVC   TEXTWYLL(TEXTWYL-TEXTNEWL),TEXTNEWL  COPY BITS ESP89279
.NOWYLTS ANOP  ,                                                 91315
DYNALCOM ST    R4,DYNTXTPP   MAKE LIST                        ESP85142
         LA    R1,DYNRBPTR                                    ESP85142
         DYNALLOC ,
         LTR   R15,R15
         BZ    EXITOK
DYNFAIL  ST    R15,S99RC
         CLI   DYNRB+1,S99VRBUN  UNALLOCATE ?                 ESP85142
         BE    EXITOKUN      DON'T CARE ABOUT ERRORS             90050
         MVI   ERRNUM,ERCBALLO  ALLOCATION FAILED             ESP88150
         CLI   SERVFUN,0     SERVER MODE ?                    ESP88150
         BNE   DYNAEXBD      YES; SKIP TPUT                   ESP88150
         SLR   R15,R15                                        ESP85142
         ST    R15,DFBUFL1   JUST IN CASE - CLEAR RETURN LENGTH P88150
         ST    R15,DFBUFL2     AND FOR SECOND ENTRY           ESP85142
         LA    R1,DFPARMS
         LINK  EP=IKJEFF18
         ICM   R15,3,DFBUFL1 IS THERE A MESSAGE TO BE WRITTEN ? P88150
         SH    R15,=H'4'     ALLOW FOR LENGTH LENGTH          ESP85142
         BNP   DYNAEXIT      NO; EXIT                         ESP85142
         WTERM  DFBUFT1,(R15)  WRITE THE PRIMARY MESSAGE      ESP88150
         ICM   R15,3,DFBUFL2  IS THERE A SECONDARY MESSAGE ?  ESP85142
         SH    R15,=H'4'     ALLOW FOR LENGTH LENGTH          ESP85142
         BNP   DYNAEXBD      NO; JUST SET RETURN CODE         ESP85142
         WTERM  DFBUFT2,(R15)  WRITE SECONDARY                ESP88150
         B     DYNAEXBD                                       ESP85142
DYNAEXIT WTERM  ' Request cancelled - allocation failed.'     ESP85142
DYNAEXBD LA    R15,1                                          ESP85142
         B     EXITBAD
         SPACE 2                                              ESP85142
EXITOK   TM    MODEFG,FGNET  MULTI-TASKING MODE ?                90050
         BZ    EXITOKUN      NO                                  90050
         L     R14,FAKECPPL+8  RELOAD MTS ADDRESS                90050
         MVC   MTSDDNIO-MTS(8,R14),TUDDNAME  GET RETURNED DDNAME 90050
         CLI   DYNRB+1,S99VRBUN  UNALLOCATE ?                    90050
         BNE   EXITOKUN      NO; RETURN                          90050
         XC    MTSDDNIO-MTS(8,R14),MTSDDNIO-MTS(R14)  CLEAR IT   90050
EXITOKUN SLR   R15,R15       SET GOOD RETURN                     90050
EXITBAD  L     R1,AIRETCD
         ST    R15,0(,R1)
         OI    IOFLAGS,FGALLO  SHOW SUCCESSFUL ALLOCATION     ESP85142
         SUBEX RC=(R15)      RETURN WITH R15                     89345
         SPACE 2                                              ESP85142
*        THIS IS THE ERROR RECOVERY EXIT (SPECIFIED VIA ESTAE). P88150
*----------------------------------------------------------------
         SPACE 1                                              ESP85142
         PUSH  USING                                          ESP85142
         DROP  ,                                              ESP85142
         USING ABNDEXIT,R15                                   ESP85142
ABNDEXIT CH    R0,=H'12'     WORK AREA PRESENT ?              ESP85142
         BNE   ABNDEXT2      YES                              ESP85142
ABNDEXT1 SLR   R15,R15                                        ESP85142
         BR    R14           CONTINUE WITH ABEND              ESP85142
         USING SDWA,R1       DECLARE THE PASSED WORK AREA     ESP85142
ABNDEXT2 STM   R14,R12,12(R13)  JUST IN CASE ...              ESP85142
         LR    R12,R15       COPY BASE                        ESP85142
         DROP  R15                                            ESP85142
         USING ABNDEXIT,R12   BASE NEEDED FOR SETRP EXPANSION ESP85142
         L     R10,SDWAPARM  GET DYNWORK AREA                 ESP85142
         USING DYNWORK,R10   DECLARE WORK AREA                ESP85142
         L     R11,@RENTWRK  RESTORE ORIGINAL R11             ESP85142
         USING RENTWORK,R11  DECLARE IT                       ESP85142
         TM    DEBUG+DCBOFLGS-IHADCB,DCBOFOPN  DEBUGGING ?    ESP85142
         BNZ   ABNDEXDB      YES; FORCE ABEND DUMP            ESP85142
         NI    SDWACMPF,255-SDWAREQ  KILL DUMP                ESP85142
         LM    R14,R12,12(R13)  ELSE RELOAD REGISTERS         ESP85142
         B     ABNDEXT1      CONTINUE ABEND (NO DUMP)         ESP85142
ABNDEXDB SETRP REGS=(14,12),DUMP=YES,DUMPOPT=ABNDSNAP,RC=0,            *
               COMPCOD=(X'1F9',SYSTEM)  RETURN AND DUMP       ESP85142
ABNDSNAP SNAP  SDATA=(SQA,LSQA,SWA,CB,Q,TRT,DM,ERR,IO), PCDATA         *
               PDATA=ALL,MF=L                                 ESP85142
         POP   USING         RESTORE MAPPINGS                 ESP85142
         SPACE 2
RENTWORK DS   0D             READ-ONLY DATA AREA              ESP85142
         SPACE 1                                              ESP88150
         LTORG ,                                              ESP88150
         SPACE 1                                              ESP88150
PRMRBPTR DC   0A(0),X'80',AL3(PRMRB)                          ESP84246
PRMRB    DC   AL1(20,S99VRBAL)
         DC   AL2(0,0,0)     ,RETURN,CODES
PRMTXTPP DC   AL4(*-*)
         DC   AL4(0,0)
         DC   F'0'           FLAGS2
PRPTOLD  DC   A(PUDDN,PUDSN,PUSTA,PUDIS,PUFRE)                ESP85142
PRPTOLDL DC   X'80',AL3(PUINO),X'80',AL3(PUMEM)               ESP85142
PRPTNEW  DC   A(PUDDN,PUDSN,PUSTA,PUDIS,PUINO,PUFRE)          ESP85142
         DC   A(PUUNT,PUTRK,PUPRI,PUSEC)
PRPTNEWL DC   X'80',AL3(PUREL),A(PUMEM),X'80',AL3(PUDIR)
PRPTWYL  DC   A(PUDDN,PUDSN,PUSTA,PUDIS,PUINO,PUFRE)          ESP89279
         DC   A(PUUWYL,PUTRK,PUPRI,PUSEC)                     ESP89279
PRPTWYLL DC   X'80',AL3(PUREL),A(PUMEM),X'80',AL3(PUDIR)      ESP89279
         SPACE 1
PUDDN    DC    AL2(DALRTDDN,1)  RETURN DDNAME                    89331
PUDDNLEN DC   AL2(8)
PUDDNAME DC   CL8' '
PUDSN    DC   AL2(DALDSNAM,1)    DSNAME
PUDSNLEN DC   AL2(44)
PUDSNAME DC   CL44' '
PUMEM    DC    AL2(DALMEMBR,1) MEMBER
PUMEMLEN DC    AL2(8)
PUMEMBER DC    CL8' '
PUDIR    DC    AL2(DALDIR,1,3)  DIR BLKS
PUDIRECT DC    AL3(5)
PUDIS    DC   AL2(DALNDISP,1,1)  DISP
PUDISP   DC   X'00'
PUSTA    DC   AL2(DALSTATS,1,1)  STAPUS
PUSTAT   DC   X'00'
PUINO    DC    AL2(DALINOUT,1,1) INPUT/OUTPUT
PUINOUT  DC    X'00'
PUUNT    DC   AL2(DALUNIT,1)     UNIT
PUUNTLEN DC   AL2(8)
PUUNIT   DC    CL8'SYSALLDA'                                  ESP85142
PUVSR    DC    AL2(DALVLSER,1,6)   VOLUME SERIAL                 91287
PUVOLSER DC    CL6' '        SERIAL TO BE USED FOR NEW DATA      91287
PUUWYL   DC   AL2(DALUNIT,1)     UNIT                         ESP89279
PUUWYLEN DC   AL2(8)                                          ESP89279
PUUWYLT  DC    CL8'WYLBUR  '  SPECIAL GROUP                   ESP89279
PUTRK    DC   AL2(DALTRK,0)      TRACKS
PUPRI    DC   AL2(DALPRIME,1,3)  PRIMARY
PUPRIME  DC   AL3(&OPTRACK)                                   ESP85142
PUSEC    DC   AL2(DALSECND,1,3)  SECONDARY
PUSECOND DC   AL3(&OPTRACK)                                   ESP85142
PUREL    DC   AL2(DALRLSE,0)     RELEASE
PUFRE    DC   AL2(DALCLOSE,0)    FREE=CLOSE
PRMLNGTH EQU   *-PRMRBPTR                                        89327
DFZEROES DC   A(0)
DFSWTCHS DC    AL1(WTPTPUT),X'32'  WTP OR TPUT / DYNALLOC     ESP84246
         SPACE 2
MAXTRY   DC        F'5'                NO. OF TIMES TO RETRY PACKET
IMXTRY   DC        F'16'               NO. OF INITIAL TRIALS ALLOWED
SPACE    DC    A(ASP)        ASCII SPACE                      ESP88150
O100     DC        F'64'               OCTAL 100
MAXPACK  DC    H'&OPPAKSZ'   MAXIMUM PACKET SIZE              ESP88150
DSSIZ    DC    H'&OPPAKSZ'   DEFAULT MAX SEND PACKET SIZE     ESP88150
DQUOTE   DC        X'23'               DEFAULT QUOTE CHARACTER = #
DRQATE   DC    X'26'         8TH-BIT PREFIX = AMPERSAND       ESP88150
DRQREP   DC    X'7E'         REPEAT PREFIX = TILDE            ESP88150
DEOL     DC        X'0D'               DEFAULT END OF PACKET (CR)
DSOH     DC        X'01'               DEFAULT START OF HEADER (CTL A)
DRECFM   DC    C'V'          DEFAULT TO VARIABLE RECORDS      ESP85142
DLRECL   DC    H'&OPLRECL'   DEFAULT LRECL SIZE OF &OPLRECL   ESP88150
DBLKSIZE DC    H'&OPBLKSZ'   DEFAULT BLKSIZE FOR 3380         ESP88150
DTRACK   DC    H'&OPTRACK'   DEFAULT SPACE ALLOCATION         ESP88150
DDELAY   DC    Y(10*100)     TEN SECOND SEND DELAY               91311
PATBUG DCB DDNAME=DEBUG,DSORG=PS,MACRF=(PM),LRECL=260,BLKSIZE=2048,    X
               RECFM=VB
PATBUGL  EQU   *-PATBUG      LENGTH OF DCB                       89330
MODDCBI  DCB   DDNAME=KERIN,DSORG=PS,MACRF=(GL),EODAD=READEOF,   85142 *
               EXLST=INEXLIST,SYNAD=SYNADEX                      91112
MODINL   EQU   *-MODDCBI     INPUT DCB PATTERN                ESP85142
MODDCBO  DCB   DDNAME=KEROUT,DSORG=PS,MACRF=(PM),EXLST=EXLIST,         *
               SYNAD=SYNADEX                                     94081
MODOUTL  EQU   *-MODDCBO     OUTPUT DCB PATTERN               ESP85142
EXLIST   DC    0A(0),X'5',AL3(OUTEXIT)                        ESP85142
INEXLIST DC    X'91',AL3(ABDCBEX)  ABEND EXIT                    91112
TCAMATTN DC    X'04',CL3' '  TCAM ATTENTION 'STRING' - CTL-D  ESP88150
MSG514A  DC    C'KER514W Module not invoked as a Command Processor'
MSG514B  DC    C'KER514I Defaults will be used for all options' P88277
*
* TABLE TO TRANSLATE TO UPPER CASE
*
UPPER    DC    C' ',255AL1(*-UPPER)                           ESP88150
         ORG   UPPER+X'81'
         DC    C'ABCDEFGHI'
         ORG   UPPER+X'91'
         DC    C'JKLMNOPQR'
         ORG   UPPER+X'A2'
         DC    C'STUVWXYZ'
         ORG
HEXTAB   DC    C'0123456789ABCDEF'                            ESP85142
TRHEXTAB EQU   HEXTAB-C'0',256,C'C'                           ESP85142
         SPACE 1                                                 91293
* TABLE OF ERROR MESSAGES (IN CASE WE ABORT)
ERRTAB   DC        CL20'Bad send-packet size'    ERR MSG #0
ERCBPACK EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Bad message number'      ERR MSG #1
ERCBMSG# EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Unrecognized state'      ERR MSG #2
ERCBSTAT EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'No SOH encountered'      ERR MSG #3
ERCNOSOH EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Bad character count'     ERR MSG #4
ERCBCNT  EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Bad checksum'            ERR MSG #5
ERCBSUM  EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Disk is full'            ERR MSG #6
ERCBFULL EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Illegal packet type'     ERR MSG #7
ERCBTYPE EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Lost a packet'           ERR MSG #8
ERCBMISS EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Micro sent a NAK'        ERR MSG #9
ERCBNAK  EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Micro aborted'           ERR MSG #10
ERCBDIED EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Illegal file name'       ERR MSG #11
ERCBDSN  EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Invalid lrecl'           ERR MSG #12
ERCBLEN  EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Permanent I/O error'     ERR MSG #13
ERCIOERR EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Disk is read-only'       ERR MSG #14
ERCBRITE EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Recfm conflict'          ERR MSG #15
ERCBRECF EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Error allocating DSN'    ERR MSG #16  ESP88150
ERCBALLO EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Replace not permittd'    ERR MSG #17  ESP88150
ERCNOREP EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Dataset OPEN failed '    ERR MSG #18  ESP88150
ERCNOPEN EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Unsupported request '    ERR MSG #19  ESP88150
ERCNOSUP EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                 ESP88150
         DC        CL20'Security - denied.  '    ERR MSG #20     91091
ERCNOACF EQU   (*-ERRTAB-1)/(L'ERRTAB)  INDEX                    91091
         SPACE 1                                                 94296
         AIF   ('&LOCAL' EQ 'RTI').TRANCCS                       92320
         PUSH  PRINT         *****DEBUG*****                     94296
         PRINT ON,GEN        *****DEBUG*****                     94296
* ASCII to EBCDIC translate table                                91293
*                 0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F 91293
ATOE     DC    X'00,01,02,03,37,2D,2E,2F,16,05,25,0B,0C,0D,0E,0F' 00
         DC    X'10,11,12,13,3C,3D,32,26,18,19,3F,27,1C,1D,1E,1F' 10
         DC    X'40,5A,7F,7B,5B,6C,50,7D,4D,5D,5C,4E,6B,60,4B,61' 20
         DC    X'F0,F1,F2,F3,F4,F5,F6,F7,F8,F9,7A,5E,4C,7E,6E,6F' 30
         DC    X'7C,C1,C2,C3,C4,C5,C6,C7,C8,C9,D1,D2,D3,D4,D5,D6' 40
         DC    X'D7,D8,D9,E2,E3,E4,E5,E6,E7,E8,E9,AD,E0,BD,5F,6D' 50
         DC    X'79,81,82,83,84,85,86,87,88,89,91,92,93,94,95,96' 60
         DC    X'97,98,99,A2,A3,A4,A5,A6,A7,A8,A9,C0,4F,D0,A1,07' 70
         DC    X'00,01,02,03,37,2D,2E,2F,16,05,25,0B,0C,0D,0E,0F' 80
         DC    X'10,11,12,13,3C,3D,32,26,18,19,3F,27,1C,1D,1E,1F' 90
         DC    X'40,5A,7F,7B,5B,6C,50,7D,4D,5D,5C,4E,6B,60,4B,61' A0
         DC    X'F0,F1,F2,F3,F4,F5,F6,F7,F8,F9,7A,5E,4C,7E,6E,6F' B0
         DC    X'7C,C1,C2,C3,C4,C5,C6,C7,C8,C9,D1,D2,D3,D4,D5,D6' C0
         DC    X'D7,D8,D9,E2,E3,E4,E5,E6,E7,E8,E9,AD,E0,BD,5F,6D' D0
         DC    X'79,81,82,83,84,85,86,87,88,89,91,92,93,94,95,96' E0
         DC    X'97,98,99,A2,A3,A4,A5,A6,A7,A8,A9,C0,4F,D0,A1,07' F0
* EBCDIC to ASCII translate table                                91293
*                 0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F 91293
ETOA     DC    X'00,01,02,03,00,09,00,7F,00,00,00,0B,0C,0D,0E,0F' 00
         DC    X'10,11,12,13,00,00,08,00,18,19,00,00,1C,1D,1E,1F' 10
         DC    X'00,00,00,00,00,0A,17,1B,00,00,00,00,00,05,06,07' 20
         DC    X'00,00,16,00,00,00,00,04,00,00,00,00,14,15,00,1A' 30
         DC    X'20,00,00,00,00,00,00,00,00,00,5C,2E,3C,28,2B,7C' 40
         DC    X'26,00,00,00,00,00,00,00,00,00,21,24,2A,29,3B,5E' 50
         DC    X'2D,2F,00,00,00,00,00,00,00,00,7C,2C,25,5F,3E,3F' 60
         DC    X'00,00,00,00,00,00,00,00,00,60,3A,23,40,27,3D,22' 70
         DC    X'00,61,62,63,64,65,66,67,68,69,00,7B,00,00,00,00' 80
         DC    X'00,6A,6B,6C,6D,6E,6F,70,71,72,00,7D,00,00,00,00' 90
         DC    X'00,7E,73,74,75,76,77,78,79,7A,00,00,00,5B,00,00' A0
         DC    X'00,00,00,00,00,00,00,00,00,00,00,00,00,5D,00,00' B0
         DC    X'7B,41,42,43,44,45,46,47,48,49,00,00,00,00,00,00' C0
         DC    X'7D,4A,4B,4C,4D,4E,4F,50,51,52,00,00,00,00,00,00' D0
         DC    X'5C,00,53,54,55,56,57,58,59,5A,00,00,00,00,00,00' E0
         DC    X'30,31,32,33,34,35,36,37,38,39,7C,00,00,00,00,00' F0
         SPACE 2                                                 94297
ITOE     TRTAB TYPE=ITOE     ISCII TO EBCDIC                     94296
         SPACE 2                                                 94297
ETOI     TRTAB TYPE=ETOI     EBCDIC TO ISCII                     94296
         SPACE 2                                                 94297
*   PC-8 TO EBCDIC TRANSLATION (NOT DONE YET)                    94297
*** 11 14    * PRIMARY CHANGE BYTES                              94297
*** 5E 4A    *                                                   94297
*** 7B 8B    *                                                   94297
*** 7D 9B    *                                                   94297
*** 7E 5F    *                                                   94297
*** 3B A1  *    DERIVED CHANGES TO KEEP TABLE REVERSIBLE         94297
*** 9D 11  *                                                     94297
*** A2 5E  *                                                     94297
*** BA D0  *                                                     94297
*** BB C0  *                                                     94297
*                 0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F 94299
PTOE     DC    X'00,01,02,03,37,2D,2E,2F,16,05,25,0B,0C,0D,0E,0F'  0X
         DC    X'10,14,12,13,3C,3D,32,26,18,19,3F,27,1C,1D,1E,1F'  1X
         DC    X'40,5A,7F,7B,5B,6C,50,7D,4D,5D,5C,4E,6B,60,4B,61'  2X
         DC    X'F0,F1,F2,F3,F4,F5,F6,F7,F8,F9,7A,5E,4C,7E,6E,6F'  3X
         DC    X'7C,C1,C2,C3,C4,C5,C6,C7,C8,C9,D1,D2,D3,D4,D5,D6'  4X
         DC    X'D7,D8,D9,E2,E3,E4,E5,E6,E7,E8,E9,AD,E0,BD,4A,6D'  5X
         DC    X'79,81,82,83,84,85,86,87,88,89,91,92,93,94,95,96'  6X
         DC    X'97,98,99,A2,A3,A4,A5,A6,A7,A8,A9,8B,4F,9B,5F,07'  7X
         DC    X'20,21,22,23,24,15,06,17,28,29,2A,2B,2C,09,0A,1B'  8X
         DC    X'30,31,1A,33,34,35,36,08,38,39,3A,6A,04,11,3E,FF'  9X
         DC    X'41,AA,3B,B1,49,62,63,B5,64,B4,A1,BA,65,CA,66,67'  AX
         DC    X'90,70,EA,FA,EC,A0,B6,B3,9D,DA,D0,C0,B7,B8,B9,BC'  BX
         DC    X'AB,CB,CC,EB,BF,8F,8D,68,74,71,72,73,78,75,76,77'  CX
         DC    X'9A,69,ED,EE,BE,EF,CD,CE,80,BB,AC,FB,FC,8A,DD,59'  DX
         DC    X'44,45,42,46,43,47,9C,48,54,51,52,53,58,55,56,57'  EX
         DC    X'DE,9E,AE,8C,FD,CF,FE,E1,B0,AF,8E,DB,DC,B2,9F,DF'  FX
*                 0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F 94299
ETOP     DC    X'00,01,02,03,9C,09,86,7F,97,8D,8E,0B,0C,0D,0E,0F'  0X
         DC    X'10,9D,12,13,11,85,08,87,18,19,92,8F,1C,1D,1E,1F'  1X
         DC    X'80,81,82,83,84,0A,17,1B,88,89,8A,8B,8C,05,06,07'  2X
         DC    X'90,91,16,93,94,95,96,04,98,99,9A,A2,14,15,9E,1A'  3X
         DC    X'20,A0,E2,E4,E0,E1,E3,E5,E7,A4,5E,2E,3C,28,2B,7C'  4X
         DC    X'26,E9,EA,EB,E8,ED,EE,EF,EC,DF,21,24,2A,29,3B,7E'  5X
         DC    X'2D,2F,A5,A6,A8,AC,AE,AF,C7,D1,9B,2C,25,5F,3E,3F'  6X
         DC    X'B1,C9,CA,CB,C8,CD,CE,CF,CC,60,3A,23,40,27,3D,22'  7X
         DC    X'D8,61,62,63,64,65,66,67,68,69,DD,7B,F3,C6,FA,C5'  8X
         DC    X'B0,6A,6B,6C,6D,6E,6F,70,71,72,D0,7D,E6,B8,F1,FE'  9X
         DC    X'B5,AA,73,74,75,76,77,78,79,7A,A1,C0,DA,5B,F2,F9'  AX
         DC    X'F8,A3,FD,B7,A9,A7,B6,BC,BD,BE,AB,D9,BF,5D,D4,C4'  BX
         DC    X'BB,41,42,43,44,45,46,47,48,49,AD,C1,C2,D6,D7,F5'  CX
         DC    X'BA,4A,4B,4C,4D,4E,4F,50,51,52,B9,FB,FC,DE,F0,FF'  DX
         DC    X'5C,F7,53,54,55,56,57,58,59,5A,B2,C3,B4,D2,D3,D5'  EX
         DC    X'30,31,32,33,34,35,36,37,38,39,B3,DB,DC,F4,F6,9F'  FX
         POP   PRINT                                             94296
         AGO   .TRANCOM                                          91293
.TRANCCS SPACE 1                                                 91293
* THIS IS THE ASCII TO EBCDIC TABLE
*                 0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F
ATOE     DC    X'00,01,02,03,37,2D,2E,2F,16,05,25,0B,0C,0D,0E,0F' 00
         DC    X'10,14,12,13,3C,3D,32,26,18,19,3F,27,1C,1D,1E,1F' 10
         DC    X'40,5A,7F,7B,5B,6C,50,7D,4D,5D,5C,4E,6B,60,4B,61' 20
         DC    X'F0,F1,F2,F3,F4,F5,F6,F7,F8,F9,7A,5E,4C,7E,6E,6F' 30
         DC    X'7C,C1,C2,C3,C4,C5,C6,C7,C8,C9,D1,D2,D3,D4,D5,D6' 40
         DC    X'D7,D8,D9,E2,E3,E4,E5,E6,E7,E8,E9,AD,E0,BD,4A,6D' 50
         DC    X'79,81,82,83,84,85,86,87,88,89,91,92,93,94,95,96' 60
         DC    X'97,98,99,A2,A3,A4,A5,A6,A7,A8,A9,8B,4F,9B,5F,07' 70
*    SECOND HALF OF TABLE FOR 8TH-BIT QUOTING WHEN BINARY TRANSMISSION
*        IS NOT TURNED ON.                                    ESP88150
         DC    X'00,01,02,03,37,2D,2E,2F,16,05,25,0B,0C,0D,0E,0F' 80
         DC    X'10,14,12,13,3C,3D,32,26,18,19,3F,27,1C,1D,1E,1F' 90
         DC    X'40,5A,7F,7B,5B,6C,50,7D,4D,5D,5C,4E,6B,60,4B,61' A0
         DC    X'F0,F1,F2,F3,F4,F5,F6,F7,F8,F9,7A,5E,4C,7E,6E,6F' B0
         DC    X'7C,C1,C2,C3,C4,C5,C6,C7,C8,C9,D1,D2,D3,D4,D5,D6' C0
         DC    X'D7,D8,D9,E2,E3,E4,E5,E6,E7,E8,E9,AD,E0,BD,4A,6D' D0
         DC    X'79,81,82,83,84,85,86,87,88,89,91,92,93,94,95,96' E0
         DC    X'97,98,99,A2,A3,A4,A5,A6,A7,A8,A9,8B,4F,9B,5F,07' F0
*THIS IS THE EBCDIC TO ASCII CONVERSION TABLE
*CHARACTERS NOT REPRESENTABLE IN ASCII ARE REPLACED BY A NULL
*        BRACES AND NOT SIGN (CARET) CHANGED TO MATCH TN TRAIN SP88150
*          AND O.B.S. WYLBUR CONVENTIONS (AND OUR TCAM)       ESP85142
*                 0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F
ETOA     DC    X'00,01,02,03,00,09,00,7F,00,00,00,0B,0C,0D,0E,0F' 00
         DC    X'10,11,12,13,11,0D,08,00,18,19,00,00,1C,1D,1E,1F' 10
         DC    X'00,00,00,00,00,0A,17,1B,00,00,00,00,00,05,06,07' 20
         DC    X'00,00,16,00,00,00,00,04,00,00,00,00,14,15,00,1A' 30
         DC    X'20,00,00,00,00,00,00,00,00,00,5E,2E,3C,28,2B,7C' 40
         DC    X'26,00,00,00,00,00,00,00,00,00,21,24,2A,29,3B,7E' 50
         DC    X'2D,2F,00,00,00,00,00,00,00,00,7C,2C,25,5F,3E,3F' 60
         DC    X'00,00,00,00,00,00,00,00,00,60,3A,23,40,27,3D,22' 70
         DC    X'00,61,62,63,64,65,66,67,68,69,00,7B,00,00,00,00' 80
         DC    X'00,6A,6B,6C,6D,6E,6F,70,71,72,00,7D,00,00,00,00' 90
         DC    X'00,7E,73,74,75,76,77,78,79,7A,00,00,00,5B,00,00' A0
         DC    X'00,00,00,00,00,00,00,00,00,00,00,00,00,5D,00,00' B0
         DC    X'7B,41,42,43,44,45,46,47,48,49,00,00,00,00,00,00' C0
         DC    X'7D,4A,4B,4C,4D,4E,4F,50,51,52,00,00,00,00,00,00' D0
         DC    X'5C,00,53,54,55,56,57,58,59,5A,00,00,00,00,00,00' E0
         DC    X'30,31,32,33,34,35,36,37,38,39,7C,00,00,00,00,00' F0
         SPACE 2                                                 94297
*   ISCII TO EBCDIC TRANSLATION                                  94297
*** 11 14    * PRIMARY CHANGES                                   94297
*** 5E 4A    *                                                   94297
*** 7B 8B    *                                                   94297
*** 7D 9B    *                                                   94297
*** 7E 5F    *                                                   94297
*** 5B A1  *    DERIVED CHANGES TO KEEP TABLE REVERSIBLE         94297
*** 9D 11  *                                                     94297
*** C5 C0  *                                                     94297
*** CC D0  *                                                     94297
*                 0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F 94297
ITOE     DC    X'00,01,02,03,37,2D,2E,2F,16,05,25,0B,0C,0D,0E,0F'  0X
         DC    X'10,14,12,13,3C,3D,32,26,18,19,3F,27,1C,1D,1E,1F'  1X
         DC    X'40,4F,7F,7B,5B,6C,50,7D,4D,5D,5C,4E,6B,60,4B,61'  2X
         DC    X'F0,F1,F2,F3,F4,F5,F6,F7,F8,F9,7A,5E,4C,7E,6E,6F'  3X
         DC    X'7C,C1,C2,C3,C4,C5,C6,C7,C8,C9,D1,D2,D3,D4,D5,D6'  4X
         DC    X'D7,D8,D9,E2,E3,E4,E5,E6,E7,E8,E9,A1,E0,5A,4A,6D'  5X
         DC    X'79,81,82,83,84,85,86,87,88,89,91,92,93,94,95,96'  6X
         DC    X'97,98,99,A2,A3,A4,A5,A6,A7,A8,A9,8B,6A,9B,5F,07'  7X
         DC    X'20,21,22,23,24,15,06,17,28,29,2A,2B,2C,09,0A,1B'  8X
         DC    X'30,31,1A,33,34,35,36,08,38,39,3A,3B,04,11,3E,E1'  9X
         DC    X'41,42,43,44,45,46,47,48,49,51,52,53,54,55,56,57'  AX
         DC    X'58,59,62,63,64,65,66,67,68,69,70,71,72,73,74,75'  BX
         DC    X'76,77,78,80,8A,C0,8C,8D,8E,8F,90,9A,D0,9C,9D,9E'  CX
         DC    X'9F,A0,AA,AB,AC,AD,AE,AF,B0,B1,B2,B3,B4,B5,B6,B7'  DX
         DC    X'B8,B9,BA,BB,BC,BD,BE,BF,CA,CB,CC,CD,CE,CF,DA,DB'  EX
         DC    X'DC,DD,DE,DF,EA,EB,EC,ED,EE,EF,FA,FB,FC,FD,FE,FF'  FX
*                 0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F 94297
ETOI     DC    X'00,01,02,03,9C,09,86,7F,97,8D,8E,0B,0C,0D,0E,0F'  0X
         DC    X'10,9D,12,13,11,85,08,87,18,19,92,8F,1C,1D,1E,1F'  1X
         DC    X'80,81,82,83,84,0A,17,1B,88,89,8A,8B,8C,05,06,07'  2X
         DC    X'90,91,16,93,94,95,96,04,98,99,9A,9B,14,15,9E,1A'  3X
         DC    X'20,A0,A1,A2,A3,A4,A5,A6,A7,A8,5E,2E,3C,28,2B,21'  4X
         DC    X'26,A9,AA,AB,AC,AD,AE,AF,B0,B1,5D,24,2A,29,3B,7E'  5X
         DC    X'2D,2F,B2,B3,B4,B5,B6,B7,B8,B9,7C,2C,25,5F,3E,3F'  6X
         DC    X'BA,BB,BC,BD,BE,BF,C0,C1,C2,60,3A,23,40,27,3D,22'  7X
         DC    X'C3,61,62,63,64,65,66,67,68,69,C4,7B,C6,C7,C8,C9'  8X
         DC    X'CA,6A,6B,6C,6D,6E,6F,70,71,72,CB,7D,CD,CE,CF,D0'  9X
         DC    X'D1,5B,73,74,75,76,77,78,79,7A,D2,D3,D4,D5,D6,D7'  AX
         DC    X'D8,D9,DA,DB,DC,DD,DE,DF,E0,E1,E2,E3,E4,E5,E6,E7'  BX
         DC    X'C5,41,42,43,44,45,46,47,48,49,E8,E9,EA,EB,EC,ED'  CX
         DC    X'CC,4A,4B,4C,4D,4E,4F,50,51,52,EE,EF,F0,F1,F2,F3'  DX
         DC    X'5C,9F,53,54,55,56,57,58,59,5A,F4,F5,F6,F7,F8,F9'  EX
         DC    X'30,31,32,33,34,35,36,37,38,39,FA,FB,FC,FD,FE,FF'  FX
         SPACE 2                                                 94297
*   PC-8 TO EBCDIC TRANSLATION (NOT DONE YET)                    94297
*** 11 14    * PRIMARY CHANGE BYTES                              94297
*** 5E 4A    *                                                   94297
*** 7B 8B    *                                                   94297
*** 7D 9B    *                                                   94297
*** 7E 5F    *                                                   94297
*** 3B A1  *    DERIVED CHANGES TO KEEP TABLE REVERSIBLE         94297
*** 9D 11  *                                                     94297
*** A2 5E  *                                                     94297
*** BA D0  *                                                     94297
*** BB C0  *                                                     94297
*                 0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F 94299
PTOE     DC    X'00,01,02,03,37,2D,2E,2F,16,05,25,0B,0C,0D,0E,0F'  0X
         DC    X'10,14,12,13,3C,3D,32,26,18,19,3F,27,1C,1D,1E,1F'  1X
         DC    X'40,5A,7F,7B,5B,6C,50,7D,4D,5D,5C,4E,6B,60,4B,61'  2X
         DC    X'F0,F1,F2,F3,F4,F5,F6,F7,F8,F9,7A,5E,4C,7E,6E,6F'  3X
         DC    X'7C,C1,C2,C3,C4,C5,C6,C7,C8,C9,D1,D2,D3,D4,D5,D6'  4X
         DC    X'D7,D8,D9,E2,E3,E4,E5,E6,E7,E8,E9,AD,E0,BD,4A,6D'  5X
         DC    X'79,81,82,83,84,85,86,87,88,89,91,92,93,94,95,96'  6X
         DC    X'97,98,99,A2,A3,A4,A5,A6,A7,A8,A9,8B,4F,9B,5F,07'  7X
         DC    X'20,21,22,23,24,15,06,17,28,29,2A,2B,2C,09,0A,1B'  8X
         DC    X'30,31,1A,33,34,35,36,08,38,39,3A,6A,04,11,3E,FF'  9X
         DC    X'41,AA,3B,B1,49,62,63,B5,64,B4,A1,BA,65,CA,66,67'  AX
         DC    X'90,70,EA,FA,EC,A0,B6,B3,9D,DA,D0,C0,B7,B8,B9,BC'  BX
         DC    X'AB,CB,CC,EB,BF,8F,8D,68,74,71,72,73,78,75,76,77'  CX
         DC    X'9A,69,ED,EE,BE,EF,CD,CE,80,BB,AC,FB,FC,8A,DD,59'  DX
         DC    X'44,45,42,46,43,47,9C,48,54,51,52,53,58,55,56,57'  EX
         DC    X'DE,9E,AE,8C,FD,CF,FE,E1,B0,AF,8E,DB,DC,B2,9F,DF'  FX
*                 0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F 94299
ETOP     DC    X'00,01,02,03,9C,09,86,7F,97,8D,8E,0B,0C,0D,0E,0F'  0X
         DC    X'10,9D,12,13,11,85,08,87,18,19,92,8F,1C,1D,1E,1F'  1X
         DC    X'80,81,82,83,84,0A,17,1B,88,89,8A,8B,8C,05,06,07'  2X
         DC    X'90,91,16,93,94,95,96,04,98,99,9A,A2,14,15,9E,1A'  3X
         DC    X'20,A0,E2,E4,E0,E1,E3,E5,E7,A4,5E,2E,3C,28,2B,7C'  4X
         DC    X'26,E9,EA,EB,E8,ED,EE,EF,EC,DF,21,24,2A,29,3B,7E'  5X
         DC    X'2D,2F,A5,A6,A8,AC,AE,AF,C7,D1,9B,2C,25,5F,3E,3F'  6X
         DC    X'B1,C9,CA,CB,C8,CD,CE,CF,CC,60,3A,23,40,27,3D,22'  7X
         DC    X'D8,61,62,63,64,65,66,67,68,69,DD,7B,F3,C6,FA,C5'  8X
         DC    X'B0,6A,6B,6C,6D,6E,6F,70,71,72,D0,7D,E6,B8,F1,FE'  9X
         DC    X'B5,AA,73,74,75,76,77,78,79,7A,A1,C0,DA,5B,F2,F9'  AX
         DC    X'F8,A3,FD,B7,A9,A7,B6,BC,BD,BE,AB,D9,BF,5D,D4,C4'  BX
         DC    X'BB,41,42,43,44,45,46,47,48,49,AD,C1,C2,D6,D7,F5'  CX
         DC    X'BA,4A,4B,4C,4D,4E,4F,50,51,52,B9,FB,FC,DE,F0,FF'  DX
         DC    X'5C,F7,53,54,55,56,57,58,59,5A,B2,C3,B4,D2,D3,D5'  EX
         DC    X'30,31,32,33,34,35,36,37,38,39,B3,DB,DC,F4,F6,9F'  FX
.TRANCOM SPACE 1                                              ESP88150
*   LATIN1 TO EBCDIC TRANSLATION                                 94297
*** 11 14    * PRIMARY CHANGE BYTES                              94297
*** 5E 4A    *                                                   94297
*** 7B 8B    *                                                   94297
*** 7D 9B    *                                                   94297
*** 7E 5F    *                                                   94297
*** 3B A1  *    DERIVED CHANGES TO KEEP TABLE REVERSIBLE         94297
*** 9D 11  *                                                     94297
*** A2 5E  *                                                     94297
*** BA D0  *                                                     94297
*** BB C0  *                                                     94297
*                 0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F 94297
LTOE     DC    X'00,01,02,03,37,2D,2E,2F,16,05,25,0B,0C,0D,0E,0F'  0X
         DC    X'10,14,12,13,3C,3D,32,26,18,19,3F,27,1C,1D,1E,1F'  1X
         DC    X'40,5A,7F,7B,5B,6C,50,7D,4D,5D,5C,4E,6B,60,4B,61'  2X
         DC    X'F0,F1,F2,F3,F4,F5,F6,F7,F8,F9,7A,A1,4C,7E,6E,6F'  3X
         DC    X'7C,C1,C2,C3,C4,C5,C6,C7,C8,C9,D1,D2,D3,D4,D5,D6'  4X
         DC    X'D7,D8,D9,E2,E3,E4,E5,E6,E7,E8,E9,AD,E0,BD,4A,6D'  5X
         DC    X'79,81,82,83,84,85,86,87,88,89,91,92,93,94,95,96'  6X
         DC    X'97,98,99,A2,A3,A4,A5,A6,A7,A8,A9,8B,4F,9B,5F,07'  7X
         DC    X'20,21,22,23,24,15,06,17,28,29,2A,2B,2C,09,0A,1B'  8X
         DC    X'30,31,1A,33,34,35,36,08,38,39,3A,3B,04,11,3E,FF'  9X
         DC    X'41,AA,5E,B1,9F,B2,6A,B5,BB,B4,9A,8A,B0,CA,AF,BC'  AX
         DC    X'90,8F,EA,FA,BE,A0,B6,B3,9D,DA,D0,C0,B7,B8,B9,AB'  BX
         DC    X'64,65,62,66,63,67,9E,68,74,71,72,73,78,75,76,77'  CX
         DC    X'AC,69,ED,EE,EB,EF,EC,BF,80,FD,FE,FB,FC,BA,AE,59'  DX
         DC    X'44,45,42,46,43,47,9C,48,54,51,52,53,58,55,56,57'  EX
         DC    X'8C,49,CD,CE,CB,CF,CC,E1,70,DD,DE,DB,DC,8D,8E,DF'  FX
*                 0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F 94297
ETOL     DC    X'00,01,02,03,9C,09,86,7F,97,8D,8E,0B,0C,0D,0E,0F'  0X
         DC    X'10,9D,12,13,11,85,08,87,18,19,92,8F,1C,1D,1E,1F'  1X
         DC    X'80,81,82,83,84,0A,17,1B,88,89,8A,8B,8C,05,06,07'  2X
         DC    X'90,91,16,93,94,95,96,04,98,99,9A,9B,14,15,9E,1A'  3X
         DC    X'20,A0,E2,E4,E0,E1,E3,E5,E7,F1,5E,2E,3C,28,2B,7C'  4X
         DC    X'26,E9,EA,EB,E8,ED,EE,EF,EC,DF,21,24,2A,29,A2,7E'  5X
         DC    X'2D,2F,C2,C4,C0,C1,C3,C5,C7,D1,A6,2C,25,5F,3E,3F'  6X
         DC    X'F8,C9,CA,CB,C8,CD,CE,CF,CC,60,3A,23,40,27,3D,22'  7X
         DC    X'D8,61,62,63,64,65,66,67,68,69,AB,7B,F0,FD,FE,B1'  8X
         DC    X'B0,6A,6B,6C,6D,6E,6F,70,71,72,AA,7D,E6,B8,C6,A4'  9X
         DC    X'B5,3B,73,74,75,76,77,78,79,7A,A1,BF,D0,5B,DE,AE'  AX
         DC    X'AC,A3,A5,B7,A9,A7,B6,BC,BD,BE,DD,A8,AF,5D,B4,D7'  BX
         DC    X'BB,41,42,43,44,45,46,47,48,49,AD,F4,F6,F2,F3,F5'  CX
         DC    X'BA,4A,4B,4C,4D,4E,4F,50,51,52,B9,FB,FC,F9,FA,FF'  DX
         DC    X'5C,F7,53,54,55,56,57,58,59,5A,B2,D4,D6,D2,D3,D5'  EX
         DC    X'30,31,32,33,34,35,36,37,38,39,B3,DB,DC,D9,DA,9F'  FX
         SPACE 2                                                 94297
         IKJIOPL ,           DEFINE LISTS FOR TSO SERVICES    ESP88150
IOPLSIZE EQU   *-IOPL                                         ESP88150
         SPACE 1                                              ESP88150
         IKJSTPB ,                                            ESP88150
STPBSIZE EQU   *-STPB                                         ESP88150
         SPACE 1                                              ESP88150
         IKJGTPB ,                                            ESP88150
GTPBSIZE EQU   *-GTPB                                         ESP88150
         SPACE 1                                              ESP88150
         IKJPTPB ,                                            ESP88150
PTPBSIZE EQU   *-PTPB                                         ESP88150
         SPACE 1                                              ESP88150
INPACK   DSECT ,             MAP INPUT PACKET                 ESP88150
RECPKT   DS    C             INPUT BUFFER                     ESP88150
         SPACE 2                                              ESP88150
SAVE     DSECT ,             GOTTEN SAVE AND WORK AREA           89327
         DS    (14*18)A(0)   SAVE AREA PUSH-DOWN STACK        ESP88150
DYNWORK  DS    0D            WORK AREA                        ESP88150
@RENTWRK DS    A        1/2  POINTER TO R11 MAPPING           ESP88150
@KERMIT  DS    A        2/2  KERMIT MODULE START ADDRESS      ESP88150
@MAKASC  DC    A(0)          ADDRESS OF EBCDIC=>ASCII TRANSLATION
@MAKEBC  DC    A(0)          ADDRESS OF ASCII=>EBCDIC TRANSLATION
PKVAR    DC    D'0'                    USE FOR PICKING UP INTEGER
TEMPD    DC    0D'0'         WORK SPACE                       ESP85142
TEMP     DC    4F'0'         WORK SPACE                       ESP85142
CAMBUFF  DC    34D'0'        CAMLIST WORK/RETURN AREA         ESP88150
         ORG   CAMBUFF                                        ESP88150
WRKBUFF  DC    CL132' ',CL8' '  DISPLAY BUFFER + OVERFLOW     ESP88150
         ORG   ,                                              ESP88150
PUTRDW   DC    AL2(0,0)      PUTLINE RDW                      ESP88150
PUTBUFF  DC    CL130' '      PUTLINE DATA                     ESP88150
         DC    C' '          OVERFLOW                         ESP88206
@DEBDCB  DC    A(0)          ADDRESS OF DEBUG DCB                89330
DEBUG  DCB DDNAME=DEBUG,DSORG=PS,MACRF=(PM),LRECL=260,BLKSIZE=2048,    X
               RECFM=VB
         SPACE 1
@IODCB   OPEN  (KERIN),MF=L  OPEN LIST                        ESP88150
KERIN    DCB   DDNAME=KERIN,DSORG=PS,MACRF=(GL),EODAD=READEOF ESP85142
         ORG   KERIN         REDEFINE FOR CLARITY             ESP85142
KEROUT   DCB   DDNAME=KEROUT,DSORG=PS,MACRF=(PM),EXLST=EXLIST ESP85142
         ORG   ,                                              ESP85142
MYIOPL   DC    (IOPLSIZE/4)F'0'  IOPL EXPANSION               ESP88150
MYIOPECB DC    F'0'          IOPL ECB                         ESP88150
MYSTACK  STACK MF=L          STACK SERVICE                    ESP88150
MYGTPB   GETLINE MF=L        GETLINE SERVICE                  ESP88150
MYPTPB   PUTLINE MF=L        PUTLINE SERVICE                  ESP88150
@INCOME  DC    A(0)          ADDRESS OF GETLINE'S RETURN BUFFER(+4)
@GETLINE DC    A(0)          ADDRESS OF GETLINE ROUTINE       ESP88150
@PUTLINE DC    A(0)          ADDRESS OF PUTLINE ROUTINE       ESP88150
@CPPL    DC    A(0)          ADDRESS OF CPPL                  ESP88278
@IOBUF   DC    A(0)      1/3   I/O BUFFER DATA ADDRESS        ESP88203
@IORDW   DC    A(0,0)    2/3   I/O BUFFER RDW ADDRESS / LENGTH SP88203
         DYNSPACE ,          ROOM FOR SVC 99 WORK AREA        ESP88277
DSNAMEL  DC    H'44'         TEMPORARY DSN LENGTH                90331
S99FAIL  DC    (S99FAILL/4)A(0)  S99FAIL PLIST SPACE             91081
FAKECPPL DC    4A(0)         HOME-GROWN CPPL                  ESP88277
LISTESTA ESTAE 1,PARAM=2,MF=L                                    89327
DYNRBPTR DC   0A(0),X'80',AL3(DYNRB)                          ESP84246
DYNRB    DC   AL1(20,S99VRBAL)
         DC   AL2(0,0,0)
DYNTXTPP DC   AL4(*-*)
         DC   AL4(0,0)
S99RC    DC   F'0'
TEXTOLD  DC   A(TUDDN,TUDSN,TUSTA,TUDIS,TUFRE)                ESP85142
TEXTOLDL DC   X'80',AL3(TUINO),X'80',AL3(TUMEM)               ESP85142
TEXTNEW  DC   A(TUDDN,TUDSN,TUSTA,TUDIS,TUINO,TUFRE)          ESP85142
TEXTNEWU DC   A(TUUNT,TUTRK,TUPRI,TUSEC)
TEXTNEWL DC   X'80',AL3(TUREL),A(TUMEM),X'80',AL3(TUDIR)
TEXTWYL  DC   A(TUDDN,TUDSN,TUSTA,TUDIS,TUINO,TUFRE)          ESP89279
         DC   A(TUUWYL,TUTRK,TUPRI,TUSEC)                     ESP89279
TEXTWYLL DC   X'80',AL3(TUREL),A(TUMEM),X'80',AL3(TUDIR)      ESP89279
         SPACE 1
TUDDN    DC    AL2(DALRTDDN,1)  RETURN DDNAME                    89331
TUDDNLEN DC   AL2(8)
TUDDNAME DC   CL8' '
TUDSN    DC   AL2(DALDSNAM,1)    DSNAME
TUDSNLEN DC   AL2(44)
TUDSNAME DC   CL44' '
TUMEM    DC    AL2(DALMEMBR,1) MEMBER
TUMEMLEN DC    AL2(8)
TUMEMBER DC    CL8' '
TUDIR    DC    AL2(DALDIR,1,3)  DIR BLKS
TUDIRECT DC    AL3(5)
TUDIS    DC   AL2(DALNDISP,1,1)  DISP
TUDISP   DC   X'00'
TUSTA    DC   AL2(DALSTATS,1,1)  STATUS
TUSTAT   DC   X'00'
TUINO    DC    AL2(DALINOUT,1,1) INPUT/OUTPUT
TUINOUT  DC    X'00'
TUUNT    DC   AL2(DALUNIT,1)     UNIT
TUUNTLEN DC   AL2(8)
TUUNIT   DC    CL8'SYSALLDA'                                  ESP85142
TUVSR    DC    AL2(DALVLSER,1,6)   VOLUME SERIAL                 91287
TUVOLSER DC    CL6' '        SERIAL TO BE USED FOR NEW DATA      91287
TUUWYL   DC   AL2(DALUNIT,1)     UNIT                         ESP89279
TUUWYLEN DC   AL2(8)                                          ESP89279
TUUWYLT  DC    CL8'WYLBUR  '  SPECIAL GROUP                   ESP89279
TUTRK    DC   AL2(DALTRK,0)      TRACKS
TUPRI    DC   AL2(DALPRIME,1,3)  PRIMARY
TUPRIME  DC   AL3(&OPTRACK)                                   ESP85142
TUSEC    DC   AL2(DALSECND,1,3)  SECONDARY
TUSECOND DC   AL3(&OPTRACK)                                   ESP85142
TUREL    DC   AL2(DALRLSE,0)     RELEASE
TUFRE    DC   AL2(DALCLOSE,0)    FREE=CLOSE
         SPACE 1                                              ESP85142
         IKJEFFDF ,          EXPAND IN-LINE                   ESP85142
         ORG   DFPARMS           DAIR FAIL PLIST              ESP85142
         DC   A(DYNRB)           ADDRESS OF SVC 99 REQ BLK
         DC   A(S99RC)           ADDRESS OF SVC 99 RET CODE
         DC   A(DFZEROES)        ADDR OF UNKNOWN WRITER
         DC   A(DFSWTCHS)        ADDR OF DAIRFAIL OPTIONS
ADCPPLP  DC   A(0)               UNKNOWN CPPL ADDRESS         ESP85142
         DC    A(DFBUFL1)    FOR RETURNED MESSAGE, IF ANY     ESP85142
         ORG   ,                                              ESP85142
FLAGS    DC        X'00'               USE TO TEST OUR FLAGS
@SERVICE DC    A(0)           ADDRESS OF LOCAL SERVICE ROUTINE (SVC)
@INPREAD DC    A(0)           INPUT READER                    ESP88277
MISTAKEN DC    8A(0)          TAKE FILE POINTER STACK         ESP88277
NUMTAKEN DC    AL1(0)         NUMBER OF CURRENT TAKE FILE (0-NONE, 1-7)
PFXFORM  DC    AL1(0)        0-UID  1-UID.ACCT  2-ACCT           93342
TXTPOS   DC    H'0'          CURRENT TEXT LENGTH (RCV PTCH)      91315
LSDAT    DC    F'0'                    SEND PACKET SIZE
SNLEN    DC    F'0'          LENGTH OF TRANSLATED SEND PACKET OR 0
LRDAT    DC    F'0'                    RECEIVE PACKET SIZE
LRPOS    DC    A(0)          RECEIVE PACKET DATA ADDRESS         91077
N        DC        F'0'                SEND PACKET NUMBER
NUM      DC        F'0'                RECEIVE PACKET NUMBER
NUMTRY   DC        F'0'                TRIAL COUNTER FOR TRANSFERS
OLDTRY   DC    F'0'                    COUNTER FOR PREVIOUS PACKET
CURRECAD DC    A(0)          ADDRESS OF CURRENT INPUT RECORD  ESP88150
DECOMP   SERVCOMP PFX=DCM,DSECT=NO    WYLBUR DECOMPRESS PARM LIST
FILNAML  DC    H'0'                LENGTH OF FILENAME
FILNAM   DC    CL18' '                 SEND/REC FILENAME
ABLANKS  DC    (L'FILNAM)AL1(ASP)  ASCII BLANKS               ESP88150
LABLANKS EQU   *-ABLANKS                                      ESP88309
DSNAMEX  DC        CL80' '             WRKBUFFER
INPUTRDW DC    Y(80+4+5,0),C'CMD: '  1/3  INPUT BUFFER PREFIX ESP88150
INPUT    DC    CL80' '  2/3  COMMAND INPUT BUFFER             ESP88150
BLANKS   DC    CL(L'DSNAMEX)' ' 3/3  MANY BLANKS              ESP88150
CANCODE  DC    X'0'          FILE/BATCH CANCEL CODE IF NOT 0  ESP88150
CURLRECL DC    H'&OPLRECL'   RECORD LEN (-4 IF RECFM = V)     ESP88150
CURLRMAX DC    H'0'          MAXIMUM LRECL FOR OUTPUT (-4 IF V) P88150
RPSIZ    DC    F'&OPPAKSZ'   MAX RECEIVE PACKET SIZE          ESP88150
SPSIZ    DC    F'0'                    SEND PACKET SIZE
SIZE     DC    F'0'                    MAX SIZE FOR SEND DATA
IOCANCOD DC    XL4'0'        CODE FROM DCB ABEND EXIT            91112
         SPACE 1                                                 91091
MODEFG   DC    X'00'         RUNNING ENVIRONMENT                 89327
FGNET    EQU   X'01'           NETWORK (MULTI-TASKING) VERSION   89327
FGWYSS   EQU   X'02'           WYLBUR SUBSYSTEM MODE             91267
         SPACE 1                                                 91091
EDFLAGS  DC    X'00'         EDITING CONTROL FLAGS            ESP88307
FGDEBS   EQU   X'40'           DON'T SEND TRAILING BLANK TEXT ESP88307
FGDEBG   EQU   X'20'           DON'T WRITE (V/U) TRAILING BLANKS 88307
FGEXTAB  EQU   X'08'           EXPAND RECEIVED TABS              91315
FGSAS    EQU   X'04'           SAS TRANSPORT FILE FS,LRECL=BLKSZ, BIN
FGABIN   EQU   X'02'           BINARY AS PER 'A' PACKET TYPE     91310
FGASIS   EQU   X'01'         TRANSFER FILES 'AS-IS' (NO XLATE) SP88150
         SPACE 1                                                 91091
PROTOCON DC    X'00'         PROTOCOL CONVERTER INDEX         ESP88204
PCCX80   EQU   X'04'           COMMTEX CX-80                  ESP88204
PCC7171  EQU   X'08'           IBM 7171, ETC. (UNTESTED)      ESP90086
         SPACE 1                                                 91091
IOFLAGS  DC    X'00'         MORE FLAGS                       ESP85142
FGALLO   EQU   X'80'           ALLOCATION WAS MADE            ESP85142
FGOPF    EQU   X'40'           OPEN FAILED; BAD DCB PARAMETERS SP88150
FGWYL    EQU   X'20'           WYLBUR COMPRESSION             ESP88312
FGABEND  EQU   X'10'           DCB ABEND EXIT ENTERED            91112
FGDONE   EQU   FGALLO+FGWYL+FGABEND  RESET AT CLOSE/FREE      ESP88312
FGOUT    EQU   X'08'           WRITING OUTPUT                 ESP88150
FGREP    EQU   X'04'           REPLACE PERMITTED              ESP88150
FGUPD    EQU   X'02'           UPDATE SEQ. DCB PARAMETERS        91084
FGTYPE   EQU   X'01'           SEND X (TYPE) INSTEAD OF F (FILE) 88150
         SPACE 1                                                 91091
IOFLAG2  DC    X'00'         MORE FLAGS                          91084
FGCRT    EQU   X'02'           TERMINAL IS 3270 (OR SIMILAR)  ESP88278
FGVTAM   EQU   X'01'           ON IF VTAM RATHER THAN TCAM    ESP88150
         SPACE 1                                                 91091
PTCHFLG  DC    X'00'         PROCESSING FLAG                  ESP88150
PTCHFIO  EQU   X'80'           RECORD WRITTEN                 ESP88150
PTCHFCR  EQU   X'40'           CARRIAGE RETURN FOUND          ESP88150
PTCHFLF  EQU   X'20'           LINE-FEED FOUND                ESP88150
FGTGEXT  EQU   X'04'           NEXT TGET IS TEXT MODE            92082
FGTTEXT  EQU   X'02'           NEXT TPUT IN TEXT MODE            92082
FGTCONT  EQU   X'01'           NEXT TPUT IS IN CONTROL MODE   ESP88150
         SPACE 1                                                 91091
CAPAFG   DC    X'00'         CAPABILITIES FLAG                   91077
FGXLONG  EQU   X'10'           EXTRA LONG PACKETS (NO)           91091
FGAPACK  EQU   X'08'           ACCEPT (AND IGNORE) 'A' PACKETS   91091
FGCAWIN  EQU   X'04'           WINDOWS                           91091
FGRLONG  EQU   X'02'           RECEIVE LONG PACKETS              91077
FGCAPA2  EQU   X'01'           ANOTHER CAPABILITIES BYTE FOLLOWS 91091
         SPACE 1                                                 91091
ERRNUM   DC    AL1(0)                  ERROR NUMBER,IN CASE WE DIE
OLDERR   DC    AL1(0)                  ERROR OF PREVIOUS EXECUTION
STYPE    DC    C' '                    TYPE OF PACKET SENT
RTYPE    DC    C' '                    TYPE OF PACKET RECEIVED
STATE    DC    C' '                    OUR CURRENT STATE
REOL     DC    C' '                    EOL CHAR I NEED (CR)
SEOL     DC    C' '                    EOL I'LL SEND
RSOH     DC    C' '                    RECEIVE START OF HEADER
SSOH     DC    C' '                    SEND START OF HEADER
SQUO     DC    C' '                    QOUTE CHAR WE'LL SEND  ESP88150
RQATE    DC    X'00'  1/2    EIGHT-BIT QUOTE OR 0             ESP88150
RQUO     DC    C' '   2/2              MICRO'S QUOTE CHAR
RCHECK   DC    AL1(A1)       CHECKSUM (1 OR 2 SUPPORTED)      ESP88150
SCHECK   DC    AL1(A3)       PROFFERED CHECKSUM TYPE          ESP88150
RQREP    DC    X'00'         REPEAT COUNT QUOTE OR ZERO       ESP88150
SERVFUN  DC    C' '          SERVER FUNCTION (ASCII)          ESP88150
RECFM    DC    C'V'          RECFM PROGRAM WILL USE           ESP85142
LRECL    DC    H'&OPLRECL'   LRECL PROGRAM WILL USE           ESP88150
BLKSIZE  DC    H'&OPBLKSZ'   BLKSIZE PROGRAM WILL USE         ESP88150
LONGLEN  DC    H'&OPPAKLG'   NEGOTIATED LONG PACKET LENGTH       91077
LONGMAX  DC    H'&OPPAKLG'   MAXIMUM LENGTH (EVER)               91286
LONGUSR  DC    H'&OPPAKLG'   USER SPECIFIED LENGTH               91286
TMPDATA  DC    CL(&OPPAKSZ-2)' ' TEMP PLACE FOR SEND/RECEIVE DATA
*
TABNAME  DC    CL6' '        NAME OF TRANSLATION TABLE IN USE    94297
PARSELST DC    3F'0'                   PTRS TO OPERAND STACK
PTRTBL   DC    15F'0'                  OPERAND STACK
PTRTBLL  EQU       *-PTRTBL            LENGTH OF PTRTBL
DYNAFREE DC    A(0)  X'80',AL3(DYNALCRC)    DYN.FREE ONLY     ESP85142
DYNAPARM DC    A(0) DYNALCRC NOT LAST ENTRY (!)               ESP85142
DYNALCRC DC        F'0'                RETURN CODE FROM FUNCTION
PREFIX   DC    CL44' '                 USERS DSET PREFIX FROM UPT
PREFIXL  DC    F'0'          LENGTH OF PREFIX                 ESP85142
DEFFIX   DC    CL44' '       USER'S PREFIX FROM UPT           ESP88150
DEFFIXL  DC    F'0'          LENGTH OF PREFIX                 ESP88150
DELAY    DC    F'0'                    DELAY TIME
CHARREP  DC    X'00'         REPEAT COUNT                     ESP88150
CHARHIGH DC    X'00'         FOR X'80' OR                     ESP88150
CHARSPEC DC    X'00'         FOR X'40' FLIP                   ESP88150
QUOTABLE DC    256X'00'      TRT TABLE FOR QUOTE CHARACTERS   ESP88150
GTCHTAB  DC    32AL1(8)      CONTROL CHARACTERS - QUOTE AND FLIP 88150
         DC    95AL1(0)      PRINTABLES - AS IS               ESP88150
         DC    AL1(8)        QUOTE AND FLIP                   ESP88150
*        4 (QUOTE ONLY) ADDED DYNAMICALLY AS PART OF NEGOTIATION 88150
         ORG   GTCHTAB+256   SPACE PAST TABLE                    91077
PTCHTAB  DC    XL256'0'      TRANSLATE AND TEST FOR RECEIVING    91315
         SPACE 1                                              ESP88277
*        NOTE THAT THE RECEIVED DATA MAY EXPAND TO 48 TIMES THE P88150
*        INPUT PACKET SIZE AFTER PREFIXING. THIS AREA IS FOR  ESP88150
*        SHORT (NON-DATA) RECEPTION ONLY.                     ESP88150
RDAT     DC    CL256' '      DECODED RECEIVE DATA             ESP88150
RDATLEN  EQU   *-RDAT        SIZE TO CLEAR                    ESP88150
         SPACE 2                                                 91091
*    SEND PACKET DATA (PLUS OVERHEAD BYTES) LONG FORMAT        91091
SNDBUG   DS    XL9           V-FORMAT DEBUG PREFIX               91091
SNDPKT   DS    CL(&OPPAKLG)' '   SEND THIS TO MICRO              91091
         DS    CL(3+2)       ROOM FOR PREFIX AND CR/X-ON         91312
         ORG       SNDPKT
PHDR     DS        X
PLEN     DS        X         IF LONG AND L=0
PNUM     DS        X
PTYPE    DS        X         IF LONG AND TYPE D
PDATA    DS        0C
SDAT     DS    0CL(&OPPAKSZ)  SEND DATA                          91091
PLLEN1   DS    X             LONG LENGTH - BYTE 1                91091
PLLEN2   DS    X             LONG LENGTH - BYTE 2                91091
PLCHK    DS    X             LONG LENGTH - CHECKSUM              91091
PLDATA   DS    0C            LONG PROTOCOL, DATA                 91091
         ORG       ,
SAVEND   EQU   *             END OF GOTTEN AREA                  89327
         IHASDWA ,            FOR ERROR RECOVERY              ESP85142
ARGADDS  DSECT
AIRETCD  DS   A
         PRINT NOGEN
         IEFZB4D0 ,
         IEFZB4D2 ,
         IEFUCBOB ,                                              91287
         IECSDSL1 1          MAP FORMAT 1 DSCB                   94089
         IEFJFCBN ,          MAP JFCB                            94089
         IKJEFLWA ,                                           ESP88277
         IHAASCB ,                                            ESP88277
         IHAASXB ,                                            ESP88277
MTS      MAPMTS FLAVOR=KERMIT                                    89327
         AIF   ('&LOCAL' NE 'PID').QUIET                         93342
         A$LOUD ,            MAP LOCAL ONLINE USER DATA          93342
.QUIET   AIF   (NOT &INPREAD).BIGEND                             91315
TAWSECT  DSECT ,                                              ESP88277
TAWDSN   DS    CL44          TAKE FILE NAME                   ESP88277
TAWMEM   DS    CL8           TAKE MEMBER NAME                 ESP88277
TAWPTR   DS    A(TAWDDN),AL1(0,8,128,0)  IKJPARSE-STYLE PTR   ESP88277
TAWDDN   DS    CL(PATDDNLN)  TAKE DDNAME/@INPREAD WORK AREA   ESP88277
TAWNUM   DS    X             TAKE FILE NUMBER                 ESP88277
TAWMASK  DS    X             @INPREAD FILE NUMBER MASK        ESP88277
TAWSIZE  EQU   *-TAWSECT     SIZE OF GETMAIN                  ESP88277
.BIGEND  END   ,                                              ESP88150
