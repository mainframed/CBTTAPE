DISF     TITLE 'D I S T  ***  PREPROCESSOR TO LOCATE DISTRIBUTION TAPE'
         COPY  OPTIONGB                                          86164
         SPACE 1                                                 86164
         SYSPARM LIST=YES                                        86164
         EJECT ,                                                 86164
         PRINT &PRTSOR                                           86164
*
*        THIS MODULE ACCEPTS INPUT IN 'ASMANY' INPUT FORMAT
*        INPUT IS COPIED AS IS TO TEMP DSN &&ASMIN FOR ASMANY INPUT
*        MEMBER NAMES ARE EXTRACTED AND SORTED IN-STORAGE        86164
*        THE SORTED MEMBERS ARE THEN CHECKED AGAINST THE SORTED INDEX
*        OF THE OS OPTIONAL SOURCE TAPES, BUILT EARLIER BY ANOTHER
*        VERSION OF 'DIST'.
*        THE RESULTING OUTPUT IS SORTED AGAIN ON VOLUME/FILE AND
*        WRITTEN TO A TEMPORARY DSN &&DISTIN FOR INPUT TO 'DIST'
         SPACE 1
DISTFIND PGMHEAD ZERO,BASE=R12,PARM=R11                         GP03027
         SERVINIT ,          PREPARE FOR @SERVICE                86164
         SERVCALL LPALD,=CL8'@INPREAD'  GET THE INPUT ROUTINE    86164
         ST    R0,@INPREAD                                       86164
         SERVCALL LPALD,=CL8'@PRINTER'  GET THE PRINT/PUNCH CODE 86164
         ST    R0,@PRINTER                                       86164
         INPOPEN SYSIN,OPT=(ABEND)  OPEN CONTROL INPUT           86164
         PRTOPEN SYSPRINT,OPT=(DUMMY)  OPEN; ALLOW IF MISSING    86164
         PRTLIST HEADER,TITLE=1  MAKE A TITLE                    86164
         PRTOPEN ASMOUT,OPT=ABEND,DEV=2  OPEN ASMANY INPUT       86164
         PRTOPEN DISTOUT,OPT=ABEND,DEV=3   OPEN DIST'S PULL FILE 86164
         INPOPEN INDEX,DEV=2,OPT=ABEND   OPEN THE INDEX FILE     86164
         GETMAIN VC,LA=MUCHSTOR,A=GOTNSTOR                       86164
         BXH   R15,R15,NOSTOR2                                   86164
         LM    R2,R3,GOTNSTOR      FIND HOW MUCH AND WHERE       86164
         L     R0,=A(16*1024)  GIVE 16K BACK TO OS (ABENDS,...)  86164
         ST    R2,TABEGIN    SAVE START ADDRESS
         LR    R4,R3         GET AMOUNT
         SR    R4,R0         GET REMAINDER AFTER RETURN
         BNP   NOSTOR        BUT HOW ?                           86164
         LA    R1,0(R2,R4)   END ADDRESS+1 GOTTEN; ADDRESS TO FREE
         LR    R5,R1         COPY FOR SAVING
         SH    R5,=Y(MEMSIZE)   TAKE OUT LAST ENTRY              86164
         BNP   NOSTOR        HOW ?                               86164
         ST    R5,TABEND     SAVE END ADRESS
       FREEMAIN R,LV=(0),A=(1)  FREE IT UP
         SPACE 2
         L     R10,TABEGIN   GET START OF NAME TABLE             86164
PROCESS  INPGET ,            GET AN ASMANY TYPE INPUT RECORD     86164
         CH    R15,=H'4'     TEST RETURN                         86164
         BL    GOTIN         EXPECTED RETURN                     86164
         BE    SYNEOF        END-FILE                            86164
GETSYERR PRTF  '0***** I/O ERROR ON SYSIN *****'                 86164
         ABEND 001,DUMP      NO, COMPLAIN AND QUIT               86164
GOTIN    LR    R3,R1         SAVE NEW CARD ADDRESS               86164
         USING CARDSECT,R3   DECLARE MAPPING                     86164
         PRTF  CARD,L'CARD,DEV=(1,2),CC=NO  MAKE A COPY FOR ASMANY
         LA    R6,1          INITIALIZE THE BXH INCREMENT        86164
         LA    R7,CARD+70    SET THE STOP ADDRESS                86164
         LR    R5,R3         SET THE SCAN START ADDRESS          86164
         NI    FG,255-FG2STACK  RESET STACKED ASSEMBLIES         86164
         LA    R4,OPTTAB     USE GENERAL OPTION TABLE            86164
PROCHK   BAL   R8,VERBLOOK   CHECK AND BRANCH ON VERB CODE       86164
         B     NEWMEM        TREAT AS NEW MEMBER NAME            86164
         SPACE 1                                                 86164
SETOPT   CLI   CARD+1,C' '   CHECK FOR ALL BLANK CARD            86164
         BNE   SETOPTT       NO; CHECK INDIVIDUAL OPTIONS        86164
         CLC   CARD+2(70),CARD+1  ALL BLANK ?                    86164
         BE    PROCESS       YES; SKIP                           86164
SETOPTT  LA    R4,MODTABAL   ELSE USE FULL TABLE                 86164
         BAL   R8,VERBSCAN   SCAN OUT INDIVIDUAL OPTIONS         86164
         B     PROCESS       GET ANOTHER CARD                    86164
         SPACE 1                                                 86164
VERBSCAN LA    R7,CARD+70    SET STOP SCAN ADDRESS               86164
VERBSCAM LA    R6,1          MAKE INCREMENT                      86164
VERBSCAL CLI   0(R5),C' '    LEADING BLANK ?                     86164
         BE    VERBSCAP                                          86164
         CLI   0(R5),C','    COMMA ?                             86164
         BNE   VERBLOOK      NO; LOOK FOR MATCH                  86164
VERBSCAP BXLE  R5,R6,VERBSCAL  TRY NEXT COLUMN                   86164
         SR    R5,R6         POINT TO LAST BLANK (FOR USER TEST) 86164
         BR    R8            RETURN NOT FOUND                    86164
VERBLUMP LA    R4,4(R4,R15)  SPACE TO NEXT TABLE ENTRY           86164
VERBLOOK SLR   R15,R15       CLEAR IC REGISTER                   86164
         ICM   R15,1,0(R4)   INSERT TABLE TEXT LENGTH-1          86164
         BMR   R8            RETURN NOT FOUND - END OF TABLE     86164
         EX    R15,VERBCLC   MATCHING VERB ?                     86164
         BNE   VERBLUMP      NO; TRY NEXT                        86164
         LA    R5,1(R5,R15)  POINT SCAN REGISTER PAST VERB       86164
         ICM   R15,3,1(R4)   GET BRANCH OFFSET                   86164
         B     DISTFIND(R15)   INVOKE THE VERB ROUTINE           86164
VERBCLC  CLC   0(0,R5),3(R4)   COMPARE TEXT TO VERB              86164
         SPACE 2                                                 86164
SETLINK  LA    R4,NULLTAB    GET DUMMY TABLE                     86164
         BAL   R8,VERBSCAN   LOOK FOR ANYTHING                   86164
         CLI   0(R5),C' '    BLANK ?                             86164
         BH    NEWMEM        NO; PROCESS ASSEMBLY/LINK REQUEST   86164
         B     PROCESS       IGNORE LINK-ONLY REQUEST            86164
         SPACE 1                                                 86164
*        UNIDENTIFIED CARD - TREAT AS NEW MEMBER NAME (ASM REQ.) 86164
*        IF MORE THAN ONE WORD, THE EXTRA WORDS ARE ASSEMBLIES   86164
*        TO BE INCLUDED WITH THE FIRST ONE -                     86164
*                                                                86164
*        'DISASM DISASM1 DISASM2' ASSEMBLES THREE MODULES, WITH  86164
*            THE MAIN MEMBER BEING DISASM                        86164
*        'IEHVTOC: IEHVMAIN IEHVINDX IEHVFMT1,IEHVFMT2 +  '      86164
*        'IEHVFMT3 IEHVFMT4 IEHVFMT5 IEHVFMT6             '      86164
*            ASSEMBLES 8 MODULES (EXCL. IEHVTOC), AND USES THE   86164
*            MEMBER NAME IEHVTOC ON THE LKED NAME CARD.          86164
*        A) COMMAS OR BLANKS MAY SEPARATE MEMBER NAMES           86164
*        B) COLON AFTER FIRST INDICATES LKED NAME, NOT ASSEMBLY  86164
*        C) TRAILING PLUS OR MINUS (OR NON-BLANK COL. 72) INDICATES
*             CONTINUATION                                       86164
*        D) * STARTS A COMMENTS FIELD                            86164
         SPACE 1                                                 86164
NEWMEM   NI    FG,255-FG2STACK  RESET STACKED ASSEMBLIES         86164
         MVC   MAINNAME,BLANK8  CLEAR MAIN MEMBER NAME           86164
POSTMEM2 MVC   CURRNAME,BLANK8  CLEAR ASSEMBLY NAME              86164
         CLI   0(R5),C'0'    NUMERIC OR INVALID START ?          86164
         BNL   BADCARD       YES; SYNTAX ERROR                   86164
         LR    R15,R7        GET LAST COLUMN TO SCAN             86164
         SR    R15,R5        GET LENGTH-1 TO EXAMINE             86164
         BM    BADCARD       EMPTY CARD ?                        86164
         LA    R1,1(,R7)     SET END IN CASE EXHAUSTED           86164
         SLR   R2,R2         CLEAR FOR IC BY TRT                 86164
         EX    R15,MEMTRT    SCAN FOR END OF NAME                86164
         LR    R15,R1        GET FIRST NON-ZERO ADDRESS          86164
         SR    R15,R5        LESS START ADDRESS                  86164
         BNP   BADCARD       INVALID TEXT                        86164
         BCTR  R15,0         EXECUTE LENGTH                      86164
         CH    R15,=Y(L'CURRNAME)  NOT TOO LONG ?                86164
         BNL   BADCARD       TOO BAD                             86164
         EX    R15,MEMMVC    MOVE                                86164
         LR    R5,R1         SET NEW START SCAN POINTER          86164
NEWMEMND STM   R5,R7,NAMESTAK  SAVE SCAN REGISTERS               86164
         TM    FG,FG2STACK   FIRST TIME AROUND ?                 86164
         BNZ   NEWMEMF2      NO; SPECIAL CHECKS                  86164
         OI    FG,FG2STACK   SET STACK ON                        86164
         MVC   MAINNAME,CURRNAME  COPY NAME                      86164
         CLM   R2,1,NAMETRT+C':'  LKED NAME ONLY ?               86164
         BNE   NEWMEMDO      NO; ASSEMBLE THIS ONE               86164
         MVC   CURRNAME,BLANK8  CLEAR ASSEMBLY NAME FOR CHECK    86164
         B     POSTNAME      SCAN FOR NEXT NAME                  86164
MEMTRT   TRT   0(0,R5),NAMETRT  CHECK FOR END OF NAME            86164
MEMMVC   MVC   CURRNAME(0),0(R5)  MOVE NAME TO JFCB              86164
         SPACE 1                                                 86164
NEWMEMF2 CLI   CURRNAME,C' '  ANY MEMBER EVER PROCESSED ?        86164
         BNH   BADCARD       SEQUENCE OR LOGIC ERROR             86164
         SPACE 1                                                 86164
NEWMEMDO SERVCALL DSMEM,CURRNAME   VALIDITY CHECK THE NAME       86164
         BXLE  R15,R15,NEWMEMOK  OK                              86164
BADCARD  PRTF  '*** INVALID CARD ***',CC=NO                      86164
LVL8ERR  OI    RETCH+L'RETCH-1,8    SET COND.CODE=8              86164
         B     PROCESS       ?                                   86164
         SPACE 1                                                 86164
*        PLACE AN ASSEMBLY MODULE NAME INTO THE TABLE            86164
*        THE NAME IS LOCATED IN CURRNAME                         86164
NEWMEMOK MVC   0(8,R10),CURRNAME    SAVE PRESUMED MEMBER NAME    86164
         MVC   8(8,R10),0(R10)   SAVE TWICE TO USE SAME 'SORT' CODE
         LA    R10,MEMSIZE(,R10)  BUMP TO NEXT ENTRY             86164
         C     R10,TABEND    SEE IF ROOM FOR MORE                86164
         BL    POSTNAME      YES                                 86164
NOSTOR2 WTO 'INSUFFICIENT STORAGE FOR MEMBER NAMES; INCREASE REGION',  *
               ROUTCDE=11
         ABEND 804,DUMP
POSTNAME LM    R5,R7,NAMESTAK  SEE IF ANY MORE ASSEMBLIES/MEMBER 86164
         BCTR  R5,0          BACK UP ONE                         86164
POSTLOOP BXH   R5,R6,POSTFULL  END OF CARD                       86164
         SLR   R2,R2         CLEAR FOR IC                        86164
         TRT   0(1,R5),NAMETRT  CHECK CURRENT CHARACTER          86164
         BIX   VAL=(R2),ERR=BADCARD,PFX=POST,                          *
               LOC=(MEM2,LOOP,LOOP,LOOP,FULL,GET,LKNM)           86164
POSTLKNM CLI   CURRNAME,C' '  FIRST TIME ?                       86164
         BH    BADCARD       NO, ERROR                           86164
         B     POSTLOOP                                          86164
POSTFULL CLI   CARD+71,C' '  CONTINUATION ?                      86164
         BNH   PROCESS       NO; GET NEXT CARD                   86164
POSTGET  INPGET ,            GET ANOTHER CARD (CONTINUATION)     86164
         CH    R15,=H'4'     CHECK RETURN                        86164
         BL    POSTGOT       OK                                  86164
         BH    GETSYERR      BOO                                 86164
         PRTF  '*** EXPECTED CONTINUATION NOT FOUND ***',CC=NO   86164
         SERVTERM ,          FORCE MESSAGE OUT                   86164
         B     GETSYERR      ABEND                               86164
POSTGOT  LR    R3,R1         SAVE CARD START                     86164
         LR    R5,R3         DITTO                               86164
         LA    R7,CARD+70    BXLE END                            86164
         PRTF  CARD,L'CARD,CC=NO,DEV=(1,2)  PUNCH THE CARD       86164
         B     POSTLOOP      TRY FOR MORE                        86164
         SPACE 2
SYNEOF   MVC   0(8,R10),=8X'FF'   SET END OF TABLE               86164
         MVC   8(8,R10),=8X'FF'    TWICE                         86164
         ST    R10,TABEND    SAVE LOGICAL TABLE END              86164
         INPCLOSE ,          CLOSE SYSIN                         86164
         PRTCLOSE DEV=2      CLOSE ASMOUT                        86164
         BAL   R9,SORT       SORT BY NAME
         L     R4,TABEGIN    GET FIRST ENTRY
         USING MEMSECT,R4    DECLARE THE MAPPING                 86164
         CLI   0(R4),X'FF'   ANY ENTRIES ?
         BL    FINDIT        YES
         WTO   'NO MEMBER NAMES FOUND',ROUTCDE=11
         ABEND 0,DUMP
         SPACE 2
FINDIT   MVI   FG,IGET       READ INDEX RECORD
FLOOP    TM    FG,EOF3       DOUBLE END-FILE ?
         BO    PUTDIST       YES; FINISHED
         TM    FG,IEOF       TAPE EOF ?
         BNZ   FLOOP1        YES; JUST RESET IGET
         TM    FG,IGET       GET A RECORD ?
         BZ    FLOOP2
         INPGET DEV=2        GET AN INDEX RECORD                 86164
         LR    R10,R1        COPY OVER                           86164
         USING INDXSECT,R10                                      86164
         CH    R15,=H'4'     TEST RETURN                         86164
         BL    FLOOP1        EXPECTED RETURN                     86164
         BE    INDEOF        END-FILE                            86164
GETIXERR PRTF  '0***** I/O ERROR ON INDEX *****'                 86164
         ABEND 001,DUMP      NO, COMPLAIN AND QUIT               86164
FLOOP1   NI    FG,255-IGET   TURN OFF GET FLAG
FLOOP2   TM    FG,MGET       GET A NAME ?
         BZ    FLOOP3        NO
         TM    FG,MEOF       EOF ?
         BNZ   PUTDIST       YES; NO FURTHER INDEX GETS NECESSARY
         CLI   MEMSIZE(R4),X'FE'  NEXT RECORD THE LAST ?         86164
         BL    FLOOP2A       NO
         OI    FG,MEOF       SET EOF FLAG
         B     PUTDIST       AND GET OUT
FLOOP2A  LA    R4,MEMSIZE(,R4)   GET NEXT ENTRY                  86164
         NI    FG,255-MGET
FLOOP3   CLC   DISMEMBR,INDMEM  MATCHES ?                        86321
         BL    FNOMEM        LOW; MEMBER NOT FOUND
         BE    FMATCH        MATCH
         OI    FG,IGET       HIGH; NEED TO SPACE TAPE FORWARD
         B     FLOOP
         SPACE 1
FNOMEM   OI    FG,MGET       GET NEXT MEMBER
         CLC   DISMEMBR,DISVOL  PRISTINE ENTRY ?                 86164
         BNE   FLOOP         NO; ALREADY PROCESSED               86164
         PRTLIST MISSMEM     PRINT NAME OF MISSING MEMBER        86164
         MVC   0(8,R4),=8X'FE'  SET FOR MISSING MEMBER
         OI    RETCH+L'RETCH-1,12    SET ERROR FLAGS IN RETURN CODE
         B     FLOOP         GET ANOTHER MEMBER
         SPACE 1
FMATCH   OI    FG,IGET       COMPARE NEXT INDEX RECORD ALSO      86164
         PRTF  INDXSECT,80,CC=NO  PRINT THE MATCH                86164
         MVC   DISVOL,INDVOL   SAVE THE SERIAL                   86164
         MVC   DISFILE,INDFIL    DITTO WITH FILE                 86164
         B     FLOOP
         SPACE
INDEOF   OI    FG,IEOF       SHOW END FILE
         B     FLOOP         FAIL ALL REMAINING MEMBERS
         SPACE 2
PUTDIST  BAL   R9,SORT       SORT BY VOLSER/FILE
         INPCLOSE DEV=2      CLOSE INDEX FILE                    86164
         L     R4,TABEGIN    GET FIRST ENTRY AGAIN
         USING MEMSECT,R4    DECLARE THE MAPPING                 86164
PUTLOOP  CLI   0(R4),X'FE'   TRAILING ENTRY ?
         BNL   EOJ           YES; DONE
         NI    RETCH+1,255-8  SHOW AT LEAST ONE FOUND
         PRTLIST DISTMSG,DEV=3  MAKE A DIST CONTROL CARD         86164
         LA    R4,MEMSIZE(,R4)  GET NEXT ENTRY                   86164
         B     PUTLOOP
         SPACE 2
EOJ      SERVTERM ,          CLOSE AND FREE                      86164
         LH    R9,RETCH      GET RETURN CODE                     86164
         PGMEXIT RC=(R9)     RETURN WITH CODE                   GP03027
         SPACE 2
NOSTOR   L     R0,GOTNSTOR+4                                     86164
         ICM   R1,15,GOTNSTOR                                    86164
         BZ    NOSTOR2                                           86164
       FREEMAIN R,LV=(0),A=(1)
         B     NOSTOR2                                           86164
         SPACE 3
SORT     LM    R4,R5,TABEGIN GET TABLE EXTENTS
         LA    R6,MEMSIZE    TABLE INCREMENT                     86164
         SR    R5,R6         LAST DATA ENTRY
         CR    R4,R5          SEE IF AT LEAST TWO
         BNLR  R9            NO; RETURN
         LA    R7,SORT1      REVERSE FLIP ADDRESS
         LA    R8,SORT2      UPWARD STEP
         LR    R3,R4         INITIALIZE FOR FIRST TIME IN
         LR    R2,R3         DITTO
SORT1    CLC   0(MEMSIZE,R2),MEMSIZE(R2)  ELEMENT IN SEQUENCE ?  86164
         BNHR  R8            YES
         LM    R14,R1,0(R2)   GET LOWER ONE
         MVC   0(MEMSIZE,R2),MEMSIZE(R2) AND PUT IT IN           86164
         STM   R14,R1,MEMSIZE(R2)   INTO PROPER SEQUENCE         86164
         SR    R2,R6         POINT TO PREVIOUS ONE
         CR    R2,R4          REVERSE FLIP DONE ?
         BNLR  R7            NO; CHECK AGAIN
SORT2    AR    R3,R6         GO TO NEXT ONE
         LR    R2,R3
         CR    R3,R5         DONE YET ?
         BNHR  R7            NO; DO AGAIN
         BR    R9            YES; RETURN
         EJECT ,
MUCHSTOR DC    A(8*1024,500*1024)  STORAGE REQUEST LIST          86164
GOTNSTOR DC    2A(0)         GETMAIN RETURN VALUES (ADDR/SIZE)   86164
SYSIN    INPWORK SYSIN,WIDTH=80,FILL=C' ',EDIT=128               86164
INDEX    INPWORK INDEX,WIDTH=80,FILL=C' ',EDIT=128               86164
ASMOUT   PUNWORK ASMOUT      ASMANY INPUT FILE (ASM REQUESTS)    86164
DISTOUT  PUNWORK DISTOUT     DIST INPUT FILE (MODULE NAMES)      86164
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                86164
BLANK8   EQU   SYSIN+8,8,C'C'  BLANKS (2ND DD NAME FIELD)        86164
         SPACE 1                                                 86164
HEADER   FDOPT NL,CC=C'#'    AUTO PAGE/DATE                      86164
         FDPRT 'D I S T F I N D   ***  DISTRIBUTION TAPE LOCATOR SERVIC*
               E'                                                86164
         FDPRT *END                                              86164
         SPACE 1                                                 86164
MISSMEM  FDOPT NL,CC=C'0'                                        86164
         FDPRT '***** MEMBER',PAD,RADJ,LEN=20                    86164
         FDPRT DISMEMBR,PAD   MEMBER NAME FROM TABLE             86164
         FDPRT 'NOT FOUND IN INDEX *****'                        86164
         FDPRT *END                                              86164
         SPACE 1                                                 86164
DISTMSG  FDPRT DISVOL,NL,LEN=L'DISVOL+2                          86164
         FDPRT DISFILE,LEN=L'DISFILE+2                           86164
         FDPRT DISMEMBR                                          86164
         FDPRT *END                                              86164
         SPACE 1                                                 86164
*        INPUT VERB TABLE                                        86164
OPTTAB VERBTAB ' ',PROCESS,LEN=2,BASE=*    LKED CONTROL CARD     86164
       VERBTAB '+',PROCESS             POST-ASM CONTROL CARD     86164
       VERBTAB *02,PROCESS             OBJECT DECK CARD          86164
       VERBTAB 'DSN=',PROCESS        NEW DDN OR DSN REQUIRED     86164
       VERBTAB 'DDN=',PROCESS        TABLE FOR NEW DDN           86164
       VERBTAB 'DDMOD=',PROCESS        SET NEW SYSLMOD NAME      86164
       VERBTAB 'DDLIB=',PROCESS        SET (LKED) SYSLIB DDN     86164
       VERBTAB '%',SETOPT              OPTION CARD               86164
       VERBTAB '*',PROCESS             TURN WTOR ON/OFF          86164
       VERBTAB 'SSI=',PROCESS          SET/RESET SSI             86164
       VERBTAB 'SEP=',PROCESS          SET/RESET SEPARATOR OPTION
       VERBTAB 'CODE=',PROCESS         SET STOP CONDITION CODE   86164
       VERBTAB 'COND=',PROCESS           DITTO                   86164
       VERBTAB 'LKEDCODE=',PROCESS     LKED CALL CODE            86164
       VERBTAB 'LKEDCOND=',PROCESS                               86164
       VERBTAB 'ID=',PROCESS           SET THIRD SEPARATOR LINE  86164
NULLTAB VERBTAB *END                                             86164
         SPACE 1                                                 86164
MODTABAL VERBTAB LINK,SETLINK          LKED MEMBER ONLY (Â¬ALIAS) 86164
       VERBTAB RELINK,SETLINK          RELINK ENTIRE MEMBER      86164
       VERBTAB *END                                              86164
         SPACE 2                                                 86164
*        TRT TABLE FOR NAME SCANNING                             86164
*                                                                86164
NAMETRT  DC    256AL1(255)                                       86164
         ORG   NAMETRT+C' '                                      86164
         DC    AL1(1)                                            86164
         ORG   NAMETRT+C'$'                                      86164
         DC    AL1(0)                                            86164
         ORG   NAMETRT+C'#'                                      86164
         DC    AL1(0)                                            86164
         ORG   NAMETRT+C'@'                                      86164
         DC    AL1(0)                                            86164
         ORG   NAMETRT+C'A'                                      86164
         DC    9AL1(0)       A-I                                 86164
         ORG   NAMETRT+C'J'                                      86164
         DC    9AL1(0)       J-R                                 86164
         ORG   NAMETRT+C'S'                                      86164
         DC    8AL1(0)       S-Z                                 86164
         ORG   NAMETRT+C'0'                                      86164
         DC    10AL1(0)      0-9                                 86164
         ORG   NAMETRT+C','                                      86164
         DC    AL1(2)                                            86164
         ORG   NAMETRT+C';'                                      86164
         DC    AL1(3)                                            86164
         ORG   NAMETRT+C'*'                                      86164
         DC    AL1(4)                                            86164
         ORG   NAMETRT+C'-'                                      86164
         DC    AL1(5)                                            86164
         ORG   NAMETRT+C'+'                                      86164
         DC    AL1(5)        CONTINUATION                        86164
         ORG   NAMETRT+C':'                                      86164
         DC    AL1(6)        END OF LINK-EDIT NAME               86164
         ORG   ,                                                 86164
         SPACE 1                                                 86164
SAVE     DSECT ,             SAVE/WORK AREA                      86164
@SERVICE DS    A             @SERVICE ADDRESS                    86164
@INPREAD DS    A             @INPREAD ADDRESS                    86164
@PRINTER DS    A             @PRINTER ADDRESS                    86164
TABEGIN  DC    F'0'
TABEND   DC    F'0'
RETCH    DC    H'0'
         SPACE 1
IEOF     EQU   X'80'
MEOF     EQU   X'40'
EOF3     EQU   IEOF+MEOF
IGET     EQU   X'20'
MGET     EQU   X'10'
FG2STACK EQU   X'02'           STACKED CARD                      86164
FG       DC    AL1(IGET)     SET TO READ INDEX RECORD
         SPACE 1                                                 86164
MAINNAME DC    CL8' '        MAIN MEMBER NAME                    86164
CURRNAME DC    CL8' '        CSECT NAME                          86164
NAMESTAK DS    3A            BXLE POINTERS FOR NAME SCANNING     86164
SAVEEND  EQU   *                                                GP03027
         SPACE 2                                                 86164
         PRINT &PRTMAC                                           86164
CARDSECT DSECT ,             INPUT RECORD GOING TO ASMANY        86164
CARD     DS    CL80                                              86164
         SPACE 1                                                 86164
MEMSECT  DSECT ,                                                 86164
DISVOL   DS    CL6           VOL-SER                             86164
DISFILE  DS    CL2           FILE                                86164
DISMEMBR DS    CL8           MODULE NAME                         86164
MEMSIZE  EQU   *-MEMSECT     ENTRY SIZE                          86164
         SPACE 1                                                 86164
INDXSECT DSECT ,                                                 86164
INDMEM   DC    CL8' '        MEMBER NAME
         DC    CL2' '
INDVOL   DC    CL6' '        VOL-SER
         DC    CL2' '
INDFIL   DC    CL2' '        FILE NUMBER
         SPACE 2
         PRINT &PRTSYS                                           86164
CVTDSECT DSECT ,                                                 86164
         CVT   DSECT=YES                                         86164
         IHACDE ,                                                86164
         END
