CATEPRO  TITLE '   CATALOG UPDATER                                     X
               CATEDIT --- UICC/GTEL CATALOG EDIT PROGRAM'         DCL
         MACRO ,                                                 85230
&NM      DETEST &B,&CC=0                                         85230
&NM      LA    R15,&CC       SET FAKE RETURN CODE                85230
         TM    MYFLAGS,FGDEL ALLOWED TO CHANGE CATALOG ?         85230
         BZ    &B            NO; BRANCH AROUND REAL MACRO        85230
         MEND  ,                                                 85230
         SPACE 1                                                 85230
*                                                                  DCL
*   CATEPRO                          -------- UICC/B.SCHRAGER      DCL
*                                                 /T.KOLE          DCL
*                                    -------- BOEING COMP SERV     DCL
*                                    07/16/76 UICC/E.KLEMENS       DCL
*                                    01/29/79 GTEL/D.C.LIVINGSTON  DCL
*                                                                  DCL
*   * CATALOG UPDATER *                                            DCL
*                                                                  DCL
*                                                                  DCL
*      THE PRESENT VERSION OF THIS CSECT WAS DEVELOPED FROM A      DCL
*      VERSION DISTRIBUTED BY UNIVERSITY OF ILLINOIS AT CHICAGO    DCL
*      CIRCLE (UICC) ON 07/16/76.  CHANGES INCLUDE PROVISION FOR   DCL
*      PROCESSING NONVSAM RECORDS IN THE VSAM MASTER CATALOG IN    DCL
*      THE SAME MANNER AS CATALOG ENTRIES IN NONVSAM 'USER' CATA-  DCL
*      LOGS, PROVISION FOR SEARCHING THE VSAM MASTER CATALOG TO    DCL
*      LOCATE ALIAS RECORDS CORRESPONDING TO HIGH-LEVEL INDEXES    DCL
*      APPEARING IN THE NONVSAM 'USER' CATALOGS TO VERIFY CONNEC-  DCL
*      TION OF THESE INDEXES TO THE NONVSAM CATALOGS IN WHICH      DCL
*      THEY APPEAR, AND EDITING OF DOCUMENTATION IN GENERAL TO     DCL
*      IMPROVE READABILITY OF SOURCE LISTINGS.  THE REMAINDER OF   DCL
*      THIS OPENING DOCUMENTATION IS ADAPTED FROM THAT ACCOMPANY-  DCL
*      ING THE 07/16/76 VERSION OF THE CSECT:                      DCL
*                                                                  DCL
*        - - - - - - - - - - - - - - - - - - - - - - - - - - - -   DCL
*
*   ACTION OF THIS CSECT CAN BE MODIFIED BY SUPPLYING 'RECAT' AND  DCL
*   'UNCAT' CONTROL STATEMENTS VIA SYSIN.  'RECAT' CONTROL STATE-  DCL
*   MENT WILL CAUSE DATASETS CATALOGED AS RESIDING ON A FIRST      DCL
*   SPECIFIED VOLUME TO BE RECATALOGED AS RESIDING ON A SECOND     DCL
*   SPECIFIED VOLUME OF SPECIFIED DEVICE TYPE.  'UNCAT' CONTROL    DCL
*   STATEMENT WILL CAUSE ALL DATASETS CATALOGED AS RESIDING ON A   DCL
*   SPECIFIED VOLUME OF ANY DEVICE TYPE OR ON ANY VOLUME OF A      DCL
*   SPECIFIED DEVICE TYPE TO BE UNCATALOGED.  CONTROL STATEMENTS   DCL
*   MUST CONFORM TO FOLLOWING FORMATS:                             DCL
*                                                                  DCL
*   RECAT   COMMAND NAME 'RECAT' IN COLS 1-5, OLD VOLUME SERIAL    DCL
*           NUMBER IN COLS 9-14, NEW VOLUME SERIAL NUMBER IN COLS  DCL
*           15-20, NEW DEVICE TYPE IN HEXADECIMAL IN COLS 21-24    DCL
*                                                                  DCL
*   UNCAT   COMMAND NAME 'UNCAT' IN COLS 1-5, VOLUME SERIAL NUM-   DCL
*           BER TO BE UNCATALOGED IN COLS 9-14, OR DEVICE TYPE IN  DCL
*           HEXADECIMAL TO BE UNCATALOGED IN COLS 9-12             DCL
*                                                                  DCL
*                                                                  DCL
         SPACE 1                                                   DCL
         EXTRN MCBUF          PROVIDES ACCESS TO VSAM MASTER CAT-  DCL
         EXTRN RPL1             ALOG BUFFER AND REQUEST PARAMETER  DCL
         EXTRN RPL2             LIST ADDRESSES                     DCL
         SPACE 1                                                   DCL
CATEPRO  CSECT
         PRINT NOGEN                                             85230
         REGEQU ,
         SPACE 1                                                   DCL
*                                                                  DCL
*   PERFORMS STANDARD INWARD LINKAGE, ESTABLISHES BASE REGISTERS,  DCL
*   LINKS SAVE AREAS, OPENS SYSUT1 AND SYSPRINT.  ATTEMPTS TO      DCL
*   OPEN SYSIN, BRANCHES TO LABEL 'NOSYS' IF SYSIN OPEN FAILS,     DCL
*   PRESUMING SYSIN OPEN FAILURE CAUSED BY ABSENCE OF SYSIN DD     DCL
*   STATEMENT                                                      DCL
*                                                                  DCL
         SPACE 1                                                   DCL
         SAVE  (14,12),,*     PERFORM STANDARD INWARD LINKAGE
         BALR  R12,0          ESTABLISH REG 12 AS BASE REGISTER
         USING *,R12
         USING IHADCB,15      ESTABLISH REGS 15 AND 1 AS DCB AND   DCL
         USING CVT,1            CVT ADDRESS BASES, RESP            DCL
         LM    8,10,VPRMS     ESTABLISH REGS 8, 9, 10 AS MCBUF     DCL
         USING MCBUF,8          AND VSAM CATALOG REQUEST PARAME-   DCL
         USING RPL1,9           TER LIST ADDRESS BASES, RESP       DCL
         USING RPL2,10                                             DCL
         LA    R11,SAVEAREA   LOAD NEW SAVE AREA ADDRESS
         ST    R13,4(,R11)    CROSS-LINK OLD AND NEW SAVE AREAS
         ST    R11,8(,R13)
         LR    R13,R11        LOAD NEW SAVE AREA ADDRESS
         L     R1,=V(FLAGS)  GET FLAGS IN CATEDIT                85230
         MVC   MYFLAGS,0(R1) COPY THEM                           85230
         OPEN  (SYSUT1,(INPUT),   OPEN SYSUT1 AND SYSIN FOR IN-    DCL X
               SYSPRINT,(OUTPUT),   PUT, SYSPRINT FOR OUTPUT       DCL X
               SYSIN,(INPUT))                                      DCL
         XR    R4,R4          SET CARD COUNT = 0
         TM    SYSIN+48,X'10' SYSIN OPENED OK?
         BZ    NOSYS          NO; GO TO NOSYS                      DCL
         LA    R3,OPTAB+4     YES; LOAD ADDR OF START OF TABLE
         SPACE 1                                                   DCL
*                                                                  DCL
*   IF SYSIN OPEN SUCCEEDS, READS UP TO 5 CONTROL STATEMENTS INTO  DCL
*   BUFFER 'OPTAB', GETS STORAGE FOR 5 ADDITIONAL CONTROL STATE-   DCL
*   MENTS, READS UP TO 5 MORE STATEMENTS, ETC., UNTIL ALL STATE-   DCL
*   MENTS READ, BRANCHES TO LABEL 'NOSYS'                          DCL
*                                                                  DCL
         SPACE 1                                                   DCL
RECYC    LA    R2,5           SET TO GET 5 CARDS BEFORE NEXT GETMAIN
GET      GET   SYSIN          GET CNTL CARD
         MVC   0(24,R3),0(R1) MOVE CNTL CARD TO TABLE
         LA    R3,24(R3)      SELECT NEXT TABLE ENTRY POSITION
         LA    R4,1(R4)       INCREMENT CARD COUNTER
         BCT   R2,GET         5 CARDS READ SINCE LAST GETMAIN?
*                             NO; GO TO GET                        DCL
         GETMAIN R,LV=124     YES; GET STORAGE FOR 5 MORE CARDS
         ST    R1,0(R3)       SAVE POINTER TO NEW ADDRESS IN OLD TABLE,
         LR    R3,R1            SWITCH REGISTERS, KEEP LOOPING
         B     RECYC          GO TO RECYC                          DCL
         SPACE 1                                                   DCL
*                                                                  DCL
*   WRITES HEADING 'UICC/GTEL CATALOG CLEANUP ROUTINE' ON FIRST    DCL
*   LINE OF SYSPRINT DATASET, SKIPS LINE, SETS SINGLE-SPACING,     DCL
*   CONDITIONS VSAM MASTER CATALOG REQUEST PARAMETER LIST RPL2 IN  DCL
*   CSECT 'CATEDIT ' FOR DIRECT-ACCESS PROCESSING VIA MCKEY VALUE  DCL
*   IN CSECT 'CATEPRO'                                             DCL
*                                                                  DCL
         SPACE 1                                                   DCL
NOSYS    MVC   LINE+1(132),LINE CLEAR SYSPRINT BUFFER
         MVC   LINE+40(L'TITLE),TITLE WRITE SYSPRINT DATASET HEADING:
         ST    R4,OPTAB                 'UICC/GTEL CATALOG CLEANUP ROU-
         PUT   SYSPRINT,LINE            TINE', FOLLOWED BY BLANK LINE
         MVC   LINE+1(132),LINE
         MVI   LINE,C'0'
         PUT   SYSPRINT,LINE
         MVI   LINE,C' '      SET SINGLE-SPACING
         MODCB RPL=RPL1,      MOVE MCKEY ADDRESS INTO REQUEST      DCL X
               ARG=MCKEY        PARAMETER LISTS, SET FOR DIRECT-   DCL
         MODCB RPL=RPL2,        ACCESS PROCESSING OF VSAM MASTER   DCL X
               ARG=MCKEY,       CATALOG                            DCL X
               OPTCD=DIR                                           DCL
         SPACE 1                                                   DCL
*                                                                  DCL
*   READS RECORD FROM SYSUT1 DATASET, DELETES CORRESPONDING NON-   DCL
*   VSAM CATALOG ENTRY IF RECORD DESCRIBES EMPTY INDEX LEVEL,      DCL
*   SKIPS RECORD WITHOUT PROCESSING IF REPRESENTS DATASET NOT ON   DCL
*   DIRECT-ACCESS DEVICE, PERFORMS SPECIAL CONTROL STATEMENT       DCL
*   FUNCTIONS, IF ANY                                              DCL
*                                                                  DCL
         SPACE 1                                                   DCL
READ     GET   SYSUT1,CARD    READ RECORD FROM SYSUT1
         MVC   LINECVOL,CCVOL MOVE SOURCE CATALOG VOLSER TO OUTPUT LINE
         MVI   DX12+1,X'00'   INDICATE MSG IF DLTX FAILS
         CLI   CDEVCLS,DINDX  EMPTY INDEX LEVEL?
         BE    DELETE         YES; GO TO DELETE: DELETE EMPTY INDEXES
         CLI   CDEVCLS,DASD   NO; DIRECT-ACCESS DEVICE?
         BNE   READ               NO; GO TO READ
         MVC   LINEVOL,CVOLSER    YES; MOVE DATASET VOLSER TO OUTPUT
*                                   LINE
         MVC   LINEDSN,CDSN   MOVE DSNAME TO OUTPUT LINE
         BAL   R5,VSWTCH      PERFORM SYSIN CNTL FUNCTIONS IF ANY
         SPACE 1                                                   DCL
****************************************************************** DCL
*                                                                * DCL
*   MODIFICATION BY DCL:  PROGRAM ORIGINALLY CONTINUED FROM      * DCL
*                         COMMENT 'FIND THE ASSOCIATED UCB'.     * DCL
*     NEW CODE TO SAME POINT CHECKS WHETHER CURRENT HIGH-LEVEL   * DCL
*     INDEX IS BEING ENCOUNTERED FOR FIRST TIME.  IF SO, READS   * DCL
*     VSAM MASTER CATALOG TO DETERMINE SERIAL NUMBER OF VOLUME   * DCL
*     CONTAINING CATALOG ASSOCIATED WITH THAT INDEX THROUGH      * DCL
*     MASTER CATALOG.  OTHERWISE, PROCESSING CONTINUES AT COM-   * DCL
*     MENT 'FIND THE ASSOCIATED UCB'.                            * DCL
*                                                                * DCL
*     IF MASTER CATALOG ENTRY FOR GIVEN INDEX POINTS TO VOLUME   * DCL
*     WITH SERIAL NUMBER SPECIFIED AT CCVOL, PROCESSING CONTIN-  * DCL
*     UES AT COMMENT 'FIND THE ASSOCIATED UCB'.  OTHERWISE, ALL  * DCL
*     CATALOG ENTRIES ON THAT VOLUME WITH THAT HIGH-LEVEL INDEX  * DCL
*     ARE DELETED.                                               * DCL
*                                                                * DCL
****************************************************************** DCL
         SPACE 1                                                   DCL
*                                                                  DCL
*   TESTS WHETHER RECORD DERIVED FROM VSAM MASTER CATALOG OR FROM  DCL
*   NONVSAM 'USER' CATALOG.  IF FORMER, AND IF DSNAME DOES NOT     DCL
*   IMPLY THAT REFERENCED DATASET IS NONVSAM CATALOG, PREPARES TO  DCL
*   SEARCH MASTER CATALOG FOR RECORD WITH CONTROL INTERVAL NUMBER  DCL
*   MATCHING VALUE AT LOCATION 'CCI', BRANCHES TO LABEL 'HLT07'.   DCL
*   IF DSNAME IS VALID NONVSAM CATALOG NAME, BRANCHES TO LOCATION  DCL
*   'READ'.  IF DSNAME IS INVALID NONVSAM CATALOG NAME, ISSUES     DCL
*   INFORMATIONAL MESSAGE TO SYSPRINT.  IF RECORD IS FROM NONVSAM  DCL
*   'USER' CATALOG, ISOLATES HIGH-LEVEL INDEX FROM DSNAME IN CUR-  DCL
*   RENT SYSUT1 RECORD, COMPARES WITH INDEX SAVED EARLIER AT LO-   DCL
*   CATION 'HLNAME'.  IF CURRENT AND SAVED INDEXES DIFFERENT,      DCL
*   BRANCHES TO LABEL 'HLT04' TO SEARCH VSAM MASTER CATALOG FOR    DCL
*   NEW HIGH-LEVEL INDEX.  IF SAME AND IF UNCATALOG FLAG           DCL
*   'UNCTGFL' SET, BRANCHES TO LABEL 'UNCAT' TO DELETE NONVSAM     DCL
*   CATALOG ENTRY SPECIFIED IN CURRENT SYSUT1 RECORD.  IF SAME     DCL
*   AND IF FLAG 'UNCTGFL' NOT SET, BRANCHES TO LABEL 'HLTEND' TO   DCL
*   ANALYZE NAMED NONVSAM CATALOG ENTRY                            DCL
*                                                                  DCL
         SPACE 1                                                   DCL
         CLI   CCI,C' '             SYSUT1 RECORD FROM MASTER      DCL
*                                     CATALOG?                     DCL
         BE    HLTEST               NO; GO TO HLTEST               DCL
         CLC   CDSN(8),=C'SYSCTLG.' YES; DSNAME IN RECORD IMPLY    DCL
*                                          NONVSAM CATALOG?        DCL
         BNE   REA1                      NO; GO TO REA1            DCL
         CLI   CDSN+8,C'V'               YES; VALID CATALOG NAME?
         BE    READ                           YES; GO TO READ      DCL
*                                             NO:                  DCL
         MVC   LINEDISP,ICN   SET 'INVALID CATALOG NAME' INTO      DCL
*                               SYSPRINT MESSAGE                   DCL
         B     PUTLINE        GO TO PUTLINE                        DCL
REA1     MVC   NEWKEY(3),CCI+1 SET TO SEARCH MASTER CATALOG FOR    DCL
         MVI   NEWKEY+3,0        RECORD WITH CONTROL INTERVAL      DCL
         MVI   MCSW,4            NUMBER SPECIFIED AT CCI           DCL
         B     HLT07          GO TO HLT07                          DCL
HLTEST   LA    15,CDSN        SELECT FIRST DSNAME CHARACTER        DCL
HLT01    CLI   1(15),C'.'     NEXT CHARACTER PERIOD OR BLANK?      DCL
         BE    HLT02                                               DCL
         CLI   1(15),C' '                                          DCL
         BE    HLT02          YES; GO TO HLT02                     DCL
         LA    15,1(15)       NO; SELECT NEXT CHARACTER            DCL
         B     HLT01          GO TO HLT01                          DCL
HLT02    S     15,=A(CDSN)    FORM MVC INSTRUCTION PARAMETER       DCL
         STC   15,HLT03+1     STORE PARAMETER                      DCL
         STC   15,HLT05+1                                          DCL
HLT03    CLC   HLNAME(0),CDSN CURRENT HIGH-LEVEL INDEX ENCOUN-     DCL
*                               TERED PREVIOUSLY?                  DCL
         BNE   HLT04          NO; GO TO HLT04                      DCL
         CLI   UNCTGFL,4      YES; UNCATALOG FLAG SET?             DCL
         BE    UNCAT               YES; GO TO UNCAT                DCL
         B     HLTEND              NO; GO TO HLTEND                DCL
         SPACE
*                                                                  DCL
*   SAVES NEW HIGH-LEVEL INDEX AT LABEL 'HLNAME', SEARCHES VSAM    DCL
*   MASTER CATALOG FOR TRUE-NAME RECORD CONTAINING SPECIFIED       DCL
*   HIGH-LEVEL INDEX NAME                                          DCL
*                                                                  DCL
         SPACE 1                                                   DCL
HLT04    MVI   UNCTGFL,0      RESET UNCATALOG FLAG                 DCL
         MVC   MCKEY(8),=CL8' ' CLEAR SEARCH KEY FIELD             DCL
HLT05    MVC   MCKEY(0),CDSN SET HIGH-LEVEL NAME AS SEARCH KEY     DCL
         MVC   HLNAME(8),MCKEY SAVE NEW HIGH-LEVEL NAME            DCL
         MVC   MCA1(L'TRRF),TRRF SET ERROR MESSAGE 'TRUE-NAME      DCL
*                                  RECORD READ FAILED'             DCL
*                             GET TRUE-NAME RECORD FROM VSAM MAS-  DCL
         GET   RPL=RPL1         TER CATALOG                        DCL
         CHECK RPL=RPL1          GET RECORD OK?                    DCL
         LTR   15,15                                               DCL
         BE    HLT06             YES; GO TO HLT06                  DCL
         CH    15,=H'8'          NO; RECORD NOT FOUND?             DCL
         BNE   MCABEND               NO; GO TO MCABEND             DCL
         TESTCB RPL=RPL1,FDBK=16     YES; CHECK FEEDBACK CODE      DCL
*                                FEEDBACK CODE = 16?               DCL
         BNE   MCABEND           NO; GO TO MCABEND                 DCL
         B     HLT18             YES; GO TO HLT18                  DCL
HLT06    CLC   MCBUF(8),MCKEY     CORRECT RECORD FOUND?            DCL
         BNE   MCABEND            NO; GO TO MCABEND                DCL
         MVC   NEWKEY(3),MCBUF+44 YES; SAVE ALIAS RECORD KEY       DCL
         MVI   MCSW,0         INDICATE SEEKING ALIAS RECORD        DCL
         SPACE 1                                                   DCL
*                                                                  DCL
*   SEARCHES VSAM MASTER CATALOG FIRST FOR ALIAS RECORD IDENTI-    DCL
*   FIED BY TRUE-NAME RECORD, THEN FOR NONVSAM DATASET RECORD      DCL
*   IDENTIFIED BY ALIAS RECORD.  FROM LATTER RECORD, OBTAINS NAME  DCL
*   AND LOCATION OF NONVSAM CATALOG THAT SHOULD CONTAIN DATASET    DCL
*   ENTRIES WITH SPECIFIED HIGH-LEVEL INDEX.  IF SAME AS CATALOG   DCL
*   CONTAINING CURRENT SYSUT1 RECORD ENTRY, BRANCHES TO LABEL      DCL
*   'HLTEND' TO CONTINUE PROCESSING OF CURRENT SYSUT1 RECORD.      DCL
*   OTHERWISE, SETS UNCATALOG FLAG 'UNCTGFL' TO DELETE ALL SUBSE-  DCL
*   QUENT CONSECUTIVE CATALOG ENTRIES WITH SAME HIGH-LEVEL INDEX,  DCL
*   BRANCHES TO LABEL 'UNCAT' TO DELETE ENTRY CORRESPONDING TO     DCL
*   CURRENT SYSUT1 RECORD                                          DCL
*                                                                  DCL
         SPACE 1                                                   DCL
HLT07    L     15,NEWKEY      SET TO USE CONTROL INTERVAL NUMBER   DCL
         SRA   15,8             AS NEW SEARCH KEY                  DCL
         ST    15,MCKEY                                            DCL
         GET   RPL=RPL2       GET SPECIFIED RECORD                 DCL
         CHECK RPL=RPL2                                            DCL
         LTR   15,15             GET RECORD OK?                    DCL
         BE    HLT08             YES; GO TO HLT08                  DCL
         MVC   MCA1(L'ARRF),ARRF NO; SET ERROR MESSAGE 'ALIAS      DCL
*                                  RECORD READ FAILED'             DCL
         CLI   MCSW,0            SEEKING ALIAS RECORD?             DCL
         BE    MCABEND           YES; GO TO MCABEND                DCL
         MVC   MCA1(L'NRRF),NRRF NO; SET ERROR MESSAGE 'NONVSAM    DCL
*                                  RECORD READ FAILED'             DCL
         B     MCABEND        GO TO MCABEND                        DCL
HLT08    CLC   NEWKEY(3),MCBUF+1 KEY CONFIRMED?                    DCL
         BE    HLT09             YES; GO TO HLT09                  DCL
         MVC   MCA1(L'ARKE),ARKE NO; SET ERROR MESSAGE 'ALIAS      DCL
*                                  RECORD KEY ERROR'               DCL
         CLI   MCSW,0            SEEKING ALIAS RECORD?             DCL
         BE    MCABEND           YES; GO TO MCABEND                DCL
         MVC   MCA1(L'NRKE),NRKE NO; SET ERROR MESSAGE 'NONVSAM    DCL
*                                  RECORD KEY ERROR'               DCL
         B     MCABEND        GO TO MCABEND                        DCL
HLT09    CLI   MCSW,0         SEEKING ALIAS RECORD?                DCL
         BNE   HLT13          NO; GO TO HLT13                      DCL
         CLI   MCBUF+44,C'X'  YES; ALIAS RECORD FOUND?             DCL
         BNE   HLT12               NO; GO TO HLT12                 DCL
         SR    15,15               YES; FORM ADDRESS BASE FOR      DCL
         IC    15,MCBUF+X'62'        ALIAS RECORD SETS OF FIELDS   DCL
         MH    15,=H'5'                                            DCL
         LA    15,MCBUF+X'63'(15)                                  DCL
         LR    14,15          SELECT LAST SET OF FIELDS POINTER    DCL
HLT10    SH    14,=H'5'         IN ALIAS RECORD                    DCL
         CLI   3(14),0             VALID SET OF FIELDS POINTER?    DCL
         BNE   HLT11               YES; GO TO HLT11                DCL
         MVC   MCA1(L'ARFNR),ARFNR NO; SET ERROR MESSAGE 'ALIAS    DCL
*                                    RECORD FORMAT NOT RECOG-      DCL
*                                    NIZED'                        DCL
         CLI   MCSW,0              SEEKING ALIAS RECORD?           DCL
         BE    MCABEND             YES; GO TO MCABEND              DCL
         MVC   MCA1(L'NRFNR),NRFNR NO; SET ERROR MESSAGE 'NONVSAM  DCL
*                                    RECORD FORMAT NOT RECOG-      DCL
*                                    NIZED'                        DCL
         B     MCABEND        GO TO MCABEND                        DCL
HLT11    LR    1,15             FORM POINTER TO CORRESPONDING SET  DCL
         MVC   MCBUF+8(2),1(14)   OF FIELDS                        DCL
         AH    1,MCBUF+8                                           DCL
         CLI   2(1),C'A'      POINTER TO NONVSAM SET OF FIELDS?    DCL
         BNE   HLT10          NO; SELECT PREVIOUS SET OF FIELDS    DCL
*                               POINTER, GO TO HLT10               DCL
         MVC   NEWKEY(3),3(1) YES; SAVE POINTER TO NONVSAM RECORD  DCL
         MVI   MCSW,4         INDICATE SEEKING NONVSAM RECORD      DCL
         B     HLT07          GO TO HLT07                          DCL
HLT12    CLI   MCBUF+44,C'A'  NONVSAM RECORD FOUND?                DCL
         BE    HLT18          YES; GO TO HLT18                     DCL
         B     HLT14          NO; GO TO HLT14                      DCL
HLT13    CLI   MCBUF+44,C'A'     NONVSAM RECORD FOUND?             DCL
         BE    HLT15             YES; GO TO HLT15                  DCL
HLT14    B     UNCAT    *** IF VSAM/Â¬VSAM MISMATCH, UNCATALOG IT 85230
         MVC   MCA1(L'NRNF),NRNF NO; SET ERROR MESSAGE 'NONVSAM  85230
*                                  RECORD NOT FOUND'               DCL
         B     MCABEND        GO TO MCABEND                        DCL
HLT15    MVC   MCA1(L'NRFNR),NRFNR SET ERROR MESSAGE 'NONVSAM      DCL
*                                    RECORD FORMAT NOT RECOG-      DCL
*                                    NIZED'                        DCL
         SR    15,15          FORM ADDRESS BASE FOR NONVSAM REC-   DCL
         IC    15,MCBUF+X'70'   ORD SET OF FIELDS                  DCL
         MH    15,=H'5'                                            DCL
         LA    15,MCBUF+X'71'(15)                                  DCL
         LR    14,15          SELECT LAST SET-OF-FIELDS POINTER    DCL
*                               IN NONVSAM RECORD                  DCL
HLT16    SH    14,=H'5'                                            DCL
         CLI   3(14),0        VALID SET-OF-FIELDS POINTER?         DCL
         BE    MCABEND        NO; GO TO HLT17                      DCL
HLT17    CLI   3(14),3        YES:                                 DCL
*                               VOLUME INFORMATION SET OF FIELDS?  DCL
         BNE   HLT16            NO; SELECT PREVIOUS SET-OF-FIELDS  DCL
*                                 POINTER, GO TO HLT16             DCL
         LR    1,15             YES; FORM POINTER TO CORRESPOND-   DCL
         MVC   MCBUF+8(2),1(14)   ING SET OF FIELDS                DCL
         AH    1,MCBUF+8                                           DCL
         CLI   1(1),X'12'     POINTER TO NONVSAM DATASET SET OF    DCL
*                               FIELDS?                            DCL
         BNE   MCABEND        NO; GO TO MCABEND                    DCL
         CLI   CCI,C' '       YES; SYSUT1 RECORD FROM MASTER CAT-  DCL
*                                    ALOG?                         DCL
         BNE   HLTEND              YES; GO TO HLTEND               DCL
         CLC   CCVOL(6),6(1)       NO; VOLSER IN NONVSAM RECORD    DCL
*                                        MATCH VALUE AT CCVOL?     DCL
         BE    HLT19                   YES; GO TO HLT19            DCL
HLT18    MVC   INCINX,HLNAME MOVE INDEX NAME                     85231
         MVC   INCIND+LINECVOL-LINE(6),CCVOL  COPY CVOL          85231
         PUT   SYSPRINT,INCIND  SHOW INCONSISTENT STRUCTURE      85231
*        DO NOT SET FLAG - CATALOG MAY BE VALID FROM OTHER SYSTEM
         B     HLTEND        JUST CHECK IF THE DATASET EXISTS    85231
*HLT18   MVI   UNCTGFL,4               NO; INDICATE ENTRIES WITH   DCL
*                                        SPECIFIED HIGH-LEVEL IN-  DCL
*                                        DEX TO BE DELETED FROM    DCL
*                                        USER CATALOG              DCL
         B     UNCAT          GO TO UNCAT                          DCL
HLT19    LA    1,15(1)        LOAD ADDRESS OF TTR FIELD IN NON-    DCL
         ST    1,MCBTTR         VSAM RECORD, SAVE AT MCBTTR        DCL
         B     HLTEND         GO TO HLTEND                         DCL
         SPACE 1                                                   DCL
*                                                                  DCL
*   FOLLOWING CODE ENTERED WHEN ERROR CONDITIONS DETECTED DURING   DCL
*   VSAM MASTER CATALOG SEARCH.  ISSUES MESSAGE TO OPERATOR CON-   DCL
*   SOLE IDENTIFYING NATURE OF ERROR CONDITION, ENDS EXECUTION     DCL
*   WITH DUMP                                                      DCL
*                                                                  DCL
         SPACE 1                                                   DCL
MCABEND  WTO   MF=(E,MCAMSG)  ISSUE ERROR MESSAGE TO CONSOLE       DCL
         ABEND 100,DUMP       ABEND WITH DUMP                      DCL
         SPACE 1                                                 85230
*        THE PEOPLE WORKING ON THIS PROGRAM PREVIOUSLY FAILED TO 85230
*        NOTICE (?) AND CORRECT THE CATALOG VS. UCB DEVICE TYPE  85230
*        DISCREPANCY. ANY ENTRY WITH POWER WARNING, SHARED DASD OR
*        3340 RPS WOULD GET RECATALOGED INCORRECTLY.             85230
*                                                                85230
CATUNIT  N     R0,=AL1(255,255-3,255,255)  REMOVE POWER WARNING  85230
         CLM   R0,2,=X'20'   DASD UCB ?                          85230
         BNER  R14             NO; RETURN                        85230
         N     R0,=AL1(255,255-X'20',255,255)  REMOVE SHARED DASD
         CLM   R0,1,=X'0A'   3340 ?                              85230
         BNER  R14             NO, RETURN                        85230
         N     R0,=AL1(255,255-X'10',255,255)  REMOVE RPS        85230
         BR    R14           RETURN                              85230
         SPACE 1                                                 85230
HLTEND   EQU   *              RESUME NORMAL PROCESSING             DCL
         SPACE 1                                                   DCL
*                                                                  DCL
*   USES UCB TABLE TO LOCATE UCB FOR DEVICE, IF ANY, ON WHICH      DCL
*   VOLUME WITH SERIAL NUMBER AT LOCATION 'CVOLSER' MOUNTED, PRO-  DCL
*   VIDED DEVICE IS DIRECT-ACCESS.  IF NOT FOUND, OR IF DEVICE     DCL
*   LOCATED NOT OF SAME TYPE AS SPECIFIED AT LOCATION 'CVOLTYP',   DCL
*   BRANCHES TO LABEL 'NOTMOUNT'                                   DCL
*                                                                  DCL
         SPACE 1                                                   DCL
         LA    1,CVOLSER      LOCATE UCB FOR DEVICE ON WHICH VOL-  DCL
         L     15,=V(UCBSRCH)   UME WITH SPECIFIED SERIAL NUMBER   DCL
         BALR  14,15            MOUNTED                            DCL
         LTR   2,2             UCB FOUND?                          DCL
         BE    NOTMOUNT        NO; GO TO NOTMOUNT                  DCL
         L     R0,16(,R2)    GET UCBTBYT1-4                      85230
         BAL   R14,CATUNIT   CONVERT TO CATALOG TYPE             85230
         CLM   R0,15,CVOLTYP DEVICE TYPE MATCH SYSUT1 RECORD ?   85230
         BNE   NOTMOUNT      NO; TOO BAD                         85230
         SPACE 1                                                   DCL
*                                                                  DCL
*   OBTAINS TRKS/CYL FOR SELECTED DEVICE FROM DEVICE CHARACTERIS-  DCL
*   TICS TABLE (DVCT), DETERMINES WHETHER SYSUT1 RECORD CATALOG    DCL
*   ENTRY CONTAINS TTR.  IF NOT, BRANCHES TO LABEL 'OBDSN'.  OTH-  DCL
*   ERWISE, CONVERTS TTR TO CCHHR                                  DCL
*                                                                  DCL
         SPACE 1                                                   DCL
         L     R1,CVTPTR      LOAD CVT ADDRESS                     DCL
         L     R1,CVTZDTAB-CVT(R1) LOAD DEVICE CHARACTERISTICS TABLE
*                                    (DVCT) ADDRESS
         LA    R6,DVCTYPMK    EXTRACT LAST HEX DIGIT FROM SELECTED UCB
         N     R6,UCBTYPE(R2)   DEVTYPE INTO REG R6
         IC    R6,DVCTIOFF-DVCTI(R1,R6) LOAD POINTER TO DVCT ENTRY FOR
*                                         SELECTED DEVTYPE
         LTR   R6,R6                 VALID POINTER?
         BZ    DVCTERR               NO;  GO TO DVCTERR
         LA    R6,DVCTRK-DVCT(R1,R6) YES; LOAD ENTRY ADDRESS
         CLC   CDSCBTTR,=XL3'000000' TTR IN CATALOG ENTRY?
         BE    OBDSN                 NO; GO TO OBDSN
         CLI   CCI,C' '      IS THIS A NON-VSAM ENTRY ?          85230
         BE    OBDSN         YES; HANG-OVER FROM DATE STAMP CODE 85230
         XC    FWORK,FWORK           YES; CLEAR WORK AREA
         MVC   FWORK+1(3),CDSCBTTR MOVE TTR INTO WORK AREA
         L     R5,FWORK       LOAD TTR INTO REG R5
         SRL   R5,8           SHIFT RECORD NUMBER OUT, LEAVING TT
         SR    R4,R4          CLEAR REG R4
         LH    R15,0(,R6)     LOAD NUMBER OF TRKS/CYL FOR THIS DEVICE
         DR    R4,R15         CONVERT TT TO CYLS AND TRKS
         STH   R4,HH          STORE REMAINDER AS HEAD NUMBER
         STH   R5,CC          STORE RESULT AS CYLINDER NUMBER
         MVC   R,FWORK+3      MOVE IN RECORD NUMBER
         SPACE 1                                                   DCL
*                                                                  DCL
*   READS DSCB AT LOCATION SPECIFIED BY CCHHR DERIVED FROM TTR IN  DCL
*   SYSUT1 RECORD, COMPARES DSNAME IN DSCB WITH DSNAME IN SYSUT1   DCL
*   RECORD.  IF SAME, BRANCHES TO LABEL 'READ'.                    DCL
*                                                                  DCL
         SPACE 1                                                   DCL
*                             READ DSCB AT LOCATION SPECIFIED BY   DCL
         OBTAIN BLOCK           TTR IN CATALOG ENTRY               DCL
         LA    R5,0           SET SECONDARY BRANCH CODE
         BAL   R14,OBRET      GO TO SUBROUTINE TO CHECK RETURN CODE
         CLC   CDSN,WORK1DSN  DSCB, SYSUT1 RECORD DSNAMES SAME?
         BE    READ           YES; GO TO READ
*                             NO:
         SPACE 1                                                   DCL
*                                                                  DCL
*   OBTAINS DSCB FOR SYSUT1 RECORD DSNAME FROM VTOC ON VOLUME IN-  DCL
*   DICATED IN SYSUT1 RECORD AS CONTAINING DATASET, REPLACES OB-   DCL
*   SOLETE TTR IN CATALOG ENTRY BY CURRENT VALUE.  WRITES LINE TO  DCL
*   SYSPRINT, EITHER 'RECATALOGED' OR 'ERROR NO #', DEPENDING ON   DCL
*   RECATALOGING SUCCEEDED                                         DCL
*                                                                  DCL
         SPACE 1                                                   DCL
OBDSN    OBTAIN DSNAME        READ DSCB SYSUT1 RECORD DATASET DSCB
         LA    R5,4           SET SECONDARY BRANCH CODE
         BAL   R14,OBRET      GO TO SUBROUTINE TO CHECK RETURN CODE
         MVC   CCHHR,WORK2CHR MOVE CCHHR TO WORK AREA, CONVERT TO TTR
         LH    R1,CC
         MH    R1,0(R6)
         AH    R1,HH
         SLL   R1,8
         IC    R1,R
         ST    R1,FWORK       SAVE RESULT AT CDSCBTTR
*        ICM   R0,7,CDSCBTTR  ANY TTR STASHED ?                  85230
*        BZ    READ          NO; JUST GET ANOTHER                85230
         CLC   CDSCBTTR,FWORK+1  ALREADY SET CORRECTLY ?         85230
         BE    READ          YES; LEAVE IT                       85230
         MVC   CDSCBTTR,FWORK+1
         B     TESTVSAM       GO TO TESTVSAM
OBRET    DS    0H             OBTAIN RETURN CODE SUBROUTINE
         B     *+4(R15)       BRANCH ON RETURN CODE
         B     0(R14)         NORMAL RETURN FROM OBTAIN
         B     READ           VOLUME NOT MOUNTED
         B     UNCAT          DSCB NOT IN VTOC - UNCATALOG THE DATASET
         B     SECBR(R5)      GO INTO SECONDARY BRANCH TABLE
         DC    AL2(0,0)       INVALID WORK AREA ADDRESS
         B     OBDSN          BAD TTR IN CATALOG, RECATALOG IT
SECBR    B     OBDSN          IF I/O ERROR ON READ BY TTR, TRY OBDSN
         MVC   LINEDISP,OE    SET 'OBTAIN ERR #' INTO SYSPRINT MESSAGE
         MVC   IOERMS,CVOLSER MOVE VOLSER TO CONSOLE OUTPUT BUFFER
         MVC   IOERMSD,CDSN   MOVE DSN TO CONSOLE OUTPUT BUFFER
         WTO   MF=(E,IOERM)   OUTPUT ERROR MESSAGE TO CONSOLE
         LA    R15,12         RESTORE RETURN CODE
         B     ERRCODE        PUT ERROR CONDITION ON OUTPUT ALSO
VSWTCH   MVC   NVOLSER(6),NVOLSER-6 CLEAR SYSPRINT BUFFER
         L     R2,OPTAB       LOAD NUMBER OF RECORDS IN FIRST SYSIN
*                               DATA TABLE
         LTR   R2,R2          ANY SYSIN CARDS READ?
         BER   R5             NO; RETURN VIA REG 5                 DCL
         LA    R1,OPTAB+4     YES; LOAD ADDR OF TABLE
VSWT1    LA    R3,5           SET TO READ FIRST SYSIN DATA TABLE   DCL
*                               ENTRY                              DCL
CODE     CLC   8(6,R1),CVOLSER VOLSERS MATCH?
         BE    CATVOL          YES; GO TO CATVOL                   DCL
         CLC   8(4,R1),CVOLTYP NO; DEVTYPES MATCH?
         BNE   LOOP1               NO; GO TO LOOP1
         CLC   0(5,R1),=C'UNCAT'   YES; 'UNCAT' SYSIN CARD?
         BE    UNCAT                     YES; GO TO UNCAT          DCL
LOOP1    LA    R1,24(R1)                 NO; SELECT NEXT SYSIN CARD
*                                          DATA ENTRY IF ANY
         BCT   R2,LOOP2       ANOTHER ENTRY EXIST?                 DCL
*                             YES; TO TO LOOP2                     DCL
         BR    R5             NO; RETURN VIA REG 5
LOOP2    BCT   R3,CODE        SET TO READ NEXT SYSIN DATA TABLE ENTRY
*                             ANOTHER ENTRY EXIST?
*                             YES; GO TO CODE
         L     R1,0(R1)       NO; LOAD ADDRESS OF NEXT SYSIN DATA TABLE
         B     VSWT1          GO TO VSWT1                          DCL
CATVOL   CLC   0(5,R1),=C'RECAT' 'RECAT' SYSIN CARD BEING PROCESSED?
         BE    RECATVOL          YES; GO TO RECATVOL
         CLC   0(5,R1),=C'UNCAT' NO; 'UNCAT' SYSIN CARD?
         BE    UNCAT                 YES; GO TO UNCAT
         B     LOOP1                 NO; GO TO LOOP1
RECATVOL MVC   NVOLSER(6),14(R1) SAVE NEW VOLSER AND DEVTYPE
         MVC   CVOLSER(6),14(R1)
         MVC   CVOLTYP(4),20(R1)
         MVC   CDSCBTTR(3),=X'000000'
         B     RECATIT        GO TO RECATIT
IOERM    DC    0F'0',AL2(EIOERM-*,0) WTO MESSAGE TEXT FOR I/O ERROR
         DC    C'I/O ERROR READING VTOC ON VOL '
IOERMS   DC    CL6'XXXXXX'
         DC    C' DSN '
IOERMSD  DC    CL44' '        DSNAME ASSOCIATED WITH I/O ERROR
EIOERM   EQU   *
UNCAT    DETEST UNCAT2,CC=69                                     85230
         CLI   CCI,C' '       SYSUT1 RECORD FROM MASTER CATALOG?   DCL
         BE    UNCAT1         NO; GO TO UNCAT1                     DCL
         CATALOG UCATDXM      YES; DELETE MASTER CATALOG ENTRY     DCL
         B     UNCAT2         GO TO UNCAT2                         DCL
UNCAT1   CATALOG UCATDX       DELETE NONVSAM CATALOG ENTRY         DCL
UNCAT2   LTR   R15,R15        DELETED OK?                          DCL
         BNZ   UNCATU         NO; GO TO UNCATU                     DCL
         MVC   LINEDISP,UC    YES; SET 'UNCATALOGED' INTO SYS-     DCL
*                               PRINT MESSAGE                      DCL
         B     PUTLINE        GO TO PUTLINE
UNCATU   CH    1,=H'8'        UNCATALOG FAIL BECAUSE CATALOG EN-   DCL
*                               TRY NOT FOUND?                     DCL
         BE    READ           YES; GO TO READ                      DCL
*                             NO; MOVE UNCATALOG RETURN CODE INTO  DCL
*                               SYSPRINT MESSAGE                   DCL
         MVC   LINEDISP,UCE   SET 'UNCAT ERR XX' INTO SYSPRINT     DCL
*                               MESSAGE                            DCL
         B     ERRCODE        GO TO ERRCODE
NOTMOUNT MVC   LINEDISP,VNM   SET 'VOL NOT MOUNTED' INTO SYSPRINT  DCL
*                               MESSAGE                            DCL
         B     PUTLINE        GO TO PUTLINE
DVCTERR  MVC   LINEDISP,DCTE  SET 'DEV CHAR TABLE ERROR' INTO      DCL
*                               SYSPRINT MESSAGE                   DCL
         B     PUTLINE        GO TO PUTLINE
DELETE   MVC   LINEVOL,=CL6' ' BLANK OUT VOLSER
         MVC   LINEDSN,CDSN   MOVE IN INDEX LEVEL
         DETEST READ,12      *BYPASS LOTS OF MESSAGES ?*         85230
         INDEX DLTINDX        DELETE INDEX
         LTR   R15,R15        DELETED OK?
         BZ    DELNXT         YES; GO TO DELNXT
         C     R15,=F'12'     NO; RETURN CODE 12?
DX12     BE    READ               YES; GO TO READ
         MVC   LINEDISP,DE        NO; SET 'DLTX ERR XX' INTO SYS-  DCL
*                                   PRINT MESSAGE                  DCL
         B     ERRCODE        GO TO ERRCODE
DELNXT   MVC   LINEDISP,ID    SET 'INDEX DELETED' INTO SYSPRINT    DCL
*                               MESSAGE                            DCL
         PUT   SYSPRINT,LINE  ISSUE MESSAGE TO SYSPRINT
         LA    R1,CDSN-1      SET POINTER AHEAD OF FIRST CHARACTER IN
*                               INDEX NAME
DELBMP   LA    R1,1(,R1)      STEP POINTER TO NEXT CHARACTER POSITION
         CLI   0(R1),C' '     SELECTED CHARACTER BLANK?
         BNE   DELBMP         NO; GO TO DELBMP
         LA    R0,CDSN        YES; FORM INDEX LENGTH IN REG 2
         LR    R2,R1
         SR    R2,R0
DELPER   CLI   0(R1),C'.'     SELECTED CHARACTER PERIOD ('.')?
         BE    DELGOTP        YES; GO TO DELGOTP
         MVI   0(R1),C' '     NO; DELETE CHARACTER
         BCTR  R1,0           BACKSPACE POINTER
         BCT   R2,DELPER      START OF INDEX NAME REACHED?
*                             NO; GO TO DELPER
         B     READ           YES; GO TO READ
DELGOTP  MVI   0(R1),C' '     DELETE PERIOD
         MVI   DX12+1,X'80'   SUPPRESS DLTX-FAIL ERROR MESSAGE     DCL
         B     DELETE         GO TO DELETE
TESTVSAM CLI   CCI,C' '       SYSUT1 RECORD DERIVED FROM MASTER    DCL
*                               CATALOG?                           DCL
         BE    READ           NO; GO TO READ ANOTHER RECORD      85230
RECATIT  MVC   VOLTYP,CVOLTYP YES; MOVE DEVICE TYPE AND VOLUME SERIAL
         MVC   VOLSER,CVOLSER   NUMBER INTO CATALOG MACRO ARGUMENT
*                               LIST
         DETEST RECAT1,CC=3                                      85230
         CATALOG RECAT        RECATALOG DATA SET
RECAT1   LTR   R15,R15        RECATALOGED OK?                      DCL
         BNE   RECATU         NO; GO TO RECATU                     DCL
         MVC   LINEDISP,RC    YES; SET 'RECATALOGED' INTO SYS-     DCL
*                               PRINT MESSAGE                      DCL
         B     PUTLINE        GO TO PUTLINE
RECATU   MVC   LINEDISP,RE    SET 'RECAT ERR #' INTO SYSPRINT      DCL
*                               MESSAGE                            DCL
ERRCODE  CVD   R15,DWORK      CONVERT ERROR CODE TO PRINTABLE CHARAC-
*                               TERS                               DCL
         UNPK  LINEDISP+15(3),DWORK+6(2)
         OI    LINEDISP+17,C'0'
PUTLINE  PUT   SYSPRINT,LINE  WRITE LINE TO SYSPRINT
         B     READ           GO TO READ
         EJECT
EOF      MVC   LINE+1(132),LINE CLEAR LINE
         MVI   LINE,C'0'      SET DOUBLE SPACE
         MVC   LINE+5(11),=C'UTILITY END'
         PUT   SYSPRINT,LINE
         CLOSE (SYSUT1,,SYSPRINT)
         TM    SYSIN+48,X'10'
         BC    8,EXIT
         CLOSE SYSIN
EXIT     L     R13,4(,R13)
         RETURN (14,12),RC=0
         EJECT
SAVEAREA DS    9D
MCBTTR   DC    F'0'        MASTER CATALOG NONVSAM RECORD TTR       DCL
*                            FIELD ADDRESS                         DCL
CARD     DS    0CL80          CARD INPUT AREA
CDSN     DS    CL44           DATA SET NAME
CVOLSER  DS    CL6            VOLUME
CVOLTYP  DS    0XL4           UCBTYP
         DS    XL1            IOS FLAGS
         DS    XL1            OPTIONAL FEATURES
CDEVCLS  DS    XL1            DEVICE CLASS
DASD     EQU   X'20'          DASD STORAGE DEVICE
DINDX    EQU   X'FF'          DELETE INDEX SIGNAL FROM MAINLINE
CUNITTYP DS    XL1            UNIT TYPE
CDSCBTTR DS    XL3            RELATIVE TTR OF DSCB IN VTOC
CCVOL    DS    CL6
CCI      DS    XL4            FOR MASTER CATALOG ENTRIES, MASTER   DCL
*                               CATALOG RECORD CONTROL INTERVAL    DCL
*                               NUMBER                             DCL
*                             FOR NONVSAM CATALOG ENTRIES, BLANK   DCL
         DS    CL17           UNUSED
FWORK    DS    F              FULL WORD WORK AREA
DWORK    DS    D              DOUBLE WORD WORK AREA
         DS    0H
CCHHR    DS    0XL5           CCHHR FOR VTOC SEARCH
CC       DS    H              CYLINDER NUMBER
HH       DS    H              HEAD NUMBER
R        DS    XL1            RECORD NUMBER
         SPACE 1                                                 85230
MYFLAGS  DC    X'00'         PROCESSING FLAGS                    85230
FGDEL    EQU   X'01'           CATALOG ALTERATION IS PERMITTED   85230
         DS    0D
WORK1    DS    CL148          WORK AREA FOR OBTAIN
WORK1DSN EQU   WORK1          START OF DSCB
WORK2    EQU   WORK1
WORK2CHR EQU   WORK1+96
VOLIST   DC    H'1'           VOLUME LIST FOR RECAT
VOLTYP   DS    XL4            UCBTYPE INFORMATION
VOLSER   DS    CL6            VOLUME SERIAL NUMBER
         DC    H'0'           DATA SET SEQUENCE NUMBER
LINE     DS    0CL133         OUTPUT LINE
         DC    C' '           CARRIAGE CONTROL
LINEDSN  DS    CL44           DSNAME
         DS    C' '
LINEDISP DS    CL20' '        DISPOSITION
         DS    CL1
LINEVOL  DS    CL6            VOLUME SERIAL NUMBER
         DS    CL12' '
NVOLSER  DS    CL6
         DS    CL6' '         FILLER
LINECVOL DS    CL6' '
         DS    CL30' '
TITLE    DC    C'UICC/GTEL CATALOG CLEANUP ROUTINE'
         SPACE 2
OPTAB    DS    F
         DS    124XL1
         SPACE 2
UCBTYPE  EQU   16             OFFSET OF UCBTYPE FIELD INTO UCB
SRTEVOLI EQU   28             OFFSET OF VOLUME SERIAL NUMBER IN UCB
         EJECT
BLOCK    CAMLST SEEK,CCHHR,CVOLSER,WORK1
DSNAME   CAMLST SEARCH,CDSN,CVOLSER,WORK2
RECAT    CAMLST RECAT,CDSN,,VOLIST,DSCBTTR=CDSCBTTR
UCATDX   CAMLST UCATDX,CDSN,CCVOL
UCATDXM  CAMLST UCATDX,CDSN                                        DCL
DLTINDX  CAMLST DLTX,CDSN,CCVOL
         EJECT
MCKEY    DC    CL44' '        SEARCH KEY                           DCL
NEWKEY   DC    XL4'0'         NEW SEARCH KEY                       DCL
HLNAME   DC    CL8' '         DSNAME                               DCL
TCBADDR  DC    F'0'           TCB ADDRESS                          DCL
MCSW     DC    X'0'           MASTER CATALOG SEARCH CONTROL        DCL
*                               SWITCH                             DCL
*                                 X'0'  SEEKING ALIAS RECORD       DCL
*                                 X'4'  SEEKING NONVSAM RECORD     DCL
UNCTGFL  DC    X'0'           HIGH-LEVEL INDEX UNCATALOG FLAG      DCL
*                               X'0'  DO NOT DELETE CATALOG EN-    DCL
*                                       TRIES WITH SPECIFIED       DCL
*                                       HIGH-LEVEL INDEX           DCL
*                               X'4'  DELETE CATALOG ENTRIES WITH  DCL
*                                       SPECIFIED HIGH-LEVEL IN-   DCL
*                                       DEX                        DCL
MCAMSG   DC    0F'0',AL2(EMCAMSG-*,0)                              DCL
MCA1     DC    CL37' '        MCABEND MESSAGE BUFFER               DCL
EMCAMSG  EQU   *                                                   DCL
TRRF     DC    CL37'TRUE-NAME RECORD READ FAILED'                  DCL
ARRF     DC    CL37'ALIAS RECORD READ FAILED'                      DCL
ARKE     DC    CL37'ALIAS RECORD KEY ERROR'                        DCL
ARNF     DC    CL37'ALIAS RECORD NOT FOUND'                        DCL
ARFNR    DC    CL37'ALIAS RECORD FORMAT NOT RECOGNIZED'            DCL
NRRF     DC    CL37'NONVSAM RECORD READ FAILED'                    DCL
NRKE     DC    CL37'NONVSAM RECORD KEY ERROR'                      DCL
NRNF     DC    CL37'NONVSAM RECORD NOT FOUND'                      DCL
NRFNR    DC    CL37'NONVSAM RECORD FORMAT NOT RECOGNIZED'          DCL
NCAF     DC    CL37'NONVSAM CATALOG ALLOCATION FAILED'             DCL
NCOF     DC    CL37'NONVSAM CATALOG OPEN FAILED'                   DCL
ICN      DC    CL20'INVALID CATALOG NAME'                          DCL
OE       DC    CL20'OBTAIN ERR #'                                  DCL
UC       DC    CL20'UNCATALOGED'                                   DCL
UCE      DC    CL20'UNCAT ERR #'                                   DCL
VNM      DC    CL20'VOLUME NOT MOUNTED'                            DCL
DCTE     DC    CL20'DEV CHAR TABLE ERROR'                          DCL
DE       DC    CL20'DLTX ERR #'                                    DCL
ID       DC    CL20'INDEX DELETED'                                 DCL
RC       DC    CL20'RECATALOGED'                                   DCL
RE       DC    CL20'RECAT ERR #'                                   DCL
INCIND   DC    CL37'0INCONSISTENT CATALOG, INDEX XXXXXXXX'       85231
INCINX   EQU   *-8,8,C'C'    INDEX INSERTION                     85231
         DC    CL(133-L'INCIND)' '                               85231
VPRMS    DC    V(MCBUF)       VSAM CATALOG BUFFER AND REQUEST      DCL
         DC    V(RPL1)          PARAMETER LIST ADDRESSES           DCL
         DC    V(RPL2)                                             DCL
         EJECT
SYSUT1   DCB   DDNAME=SYSUT1,DEVD=DA,DSORG=PS,EODAD=EOF,LRECL=80,      X
               MACRF=(GM)
SYSPRINT DCB   DDNAME=SYSPRINT,DEVD=DA,DSORG=PS,LRECL=133,BLKSIZE=133, X
               MACRF=(PM),RECFM=FA
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=(GL),RECFM=FB,              X
               LRECL=80,EODAD=NOSYS
         SPACE 2                                                   DCL
         EJECT                                                     DCL
         PRINT NOGEN          EXPANDED LISTINGS FOR FOLLOWING      DCL
*                               CONTROL BLOCK MACROS APPEAR AT     DCL
*                               END OF LISTING FOR CSECT 'CTLG-    DCL
*                               DRVR'                              DCL
         DCBD  DSORG=DA                                            DCL
         SPACE 2                                                   DCL
         IHADVCT
         SPACE 2                                                   DCL
         CVT   DSECT=YES,LIST=NO                                   DCL
         END
