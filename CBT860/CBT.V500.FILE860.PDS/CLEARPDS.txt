CLRP     TITLE 'C L E A R P D S  ***  RE-INITIALIZE PDS DIRECTORY'
         COPY  OPTIONGB                                          84150
         SPACE 2                                                 84150
         SYSPARM LIST=YES                                        84150
         SPACE 1
         PUNCH '   ORDER CLEARPDS(P) '      EASIER DUMPS
         EJECT ,                                                 84150
***********************************************************************
*                                                                     *
* FUNCTION : THIS UTILITY CLEARS THE DIRECTORY OF AN ALLOCATED PDS    *
*   LIBRARY  WITH THE ADDITIONAL TWO OPTIONS:                         *
*   A. RESETTING THE NUMBER OF THE DIRECTORY ENTRIES TO A NEW         *
*      DESIRED VALUE SPECIFIED VIA 'PARM=NN' IN THE EXEC CARD.        *
*   B. MULTIPLE CLEARING OF AS MANY LIBRARIES AS DESIRED IN ONE       *
*      JOB/STEP. THE FIRST DD CARD SHOULD HAVE THE NAME //SYSLIB      *
*      WHILE ALL THE ADDITIONAL DD CARDS SHOULD HAVE THEIR NAMES      *
*      //SYSLIBXX. XX IS ANY LEGAL CONSTANT AND SHOULD NOT BE         *
*      THE SAME FOR TWO DD CARDS.                                     *
*   NOTE : IN THE MULTIPLE LIBRARIES MODE, THE NN PARAMETER IN        *
*          THE PARM FIELD RELATES ONLY TO THE FIRST DD CARD I.E.      *
*          THE CARD HAVING ITS DDNAME //SYSLIB DD ....                *
*   NOTE : UNFORTUNATELY THERE SEEMS TO BE STILL A BUG IN THIS        *
*          UTILITY WHICH APPEARS AS ABEND ON SD37 CODE. WE            *
*          ENCOUNTERED WITH THIS PROBLEM ONLY ON THE SYSUADS LIB.     *
*          OF TSO. WHEN HAPPENDS TRY TO CHANGE THE NUMBER OF THE      *
*          DIRECTORY BLOCKS OR TO MOVE THE LIBRARY TO A DIFFERENT     *
*          PLACE. FOR THE ONE WHO WISHES TO REWRITE THIS UTILITY      *
*          WE CAN RECOMMEND THAT HE TRIES TO FIND THE APPROPRIATE     *
*          MODULE IN THE SYSTEM WHICH NORMALLY DOES THE BUILDING      *
*          OF THE DIRECTORY OF A NEW PDS DATA SET. THIS SHOULD BE     *
*          ONE OF THE MODULES OF SVC 32 (ALLOCATE).                   *
* (THE PROBLEM IS DUE TO DIFFERENCES IN VARIOUS SYSTEMS' VERSIONS OF  *
*  TRACK CAPACITY CALCULATIONS WHICH ARE NOT APARABLE.  SEE COPYVOL   *
*  IN THIS LIBRARY FOR EXCP LEVEL REFORMATTING).                 84150*
*  JCL :   //STEP1 EXEC PGM=CLEARPDS,PARM=NN  # OF DIR. BLOCKS        *
*              CHANGES: +NN ADDS NN BLOCKS TO THE DIRECTORY 84150     *
*                       -NN REDUCES BY NN BLOCKS            84150     *
*          //* IF PARM IS MISSING # OF DIR-BLKS REMAINS UNCHANGD      *
*          //* ALTERNATE FORM:  PARM='DEBUG,NN' WRITES TO SYSPRINT    *
*          //SYSPRINT DD SYSOUT=*                                     *
*          //SYSLIB   DD DSN,DISP=SHR...                              *
*          //SYSLIBXX DD DSN,DISP=SHR... OPTIONALLY MORE LIBS         *
***********************************************************************
*                                                                     *
*   CODE CHANGED TO RUN IN 31-BIT ADDRESSING MODE, AND TO DETECT      *
*   PDS/E DATA SETS. THE DIRECTORY IS CLEARED WITH STOW ,,,I.  IF     *
*   THAT FAILS, THE PDS/E DIRECTORY IS READ IN ITS ENTIRETY,          *
*   THEN WE DELETE THE ALIAS ENTRIES, AND THEN THE MEMBERS            *
*   IN REVERSE ALPHABETICAL ORDER. MEMBER COUNT REQUEST IS IGNORED.   *
*                                                               GP03075
***********************************************************************
         EJECT ,
         PRINT &PRTSOR                                           84150
CLEARPDS PGMHEAD ZERO,BASE=R12,PARM=R8                          GP03075
         SERVINIT ,          INITIALIZE SERVICE ROUTINE         GP03075
         SERVLOAD @PRINTER                                      GP03078
*DEBUG*  BANDAID INIT        ****DEBUG*****                     GP03077
*DEBUG*  BANDAID SPIE        ****DEBUG*****                     GP03077
         SPACE 1
         LA    R9,PDS               ADDRESS OF DCB              GP03075
         USING IHADCB,R9                                        GP03075
         LA    R15,PDSEXLST  EXIT LIST FOR BPAM DCB             GP03075
         STCM  R15,7,DCBEXLSA  SET INTO PDS                     GP03075
         LA    R0,EXITDCB                                       GP03075
         LA    R1,MYJFCB     POINT TO LOCAL JFCB COPY           GP03075
         STM   R0,R1,0(R15)  MAKE EXIT LIST ADDRESSES           GP03075
         MVI   0(R15),X'05'  SET DCB EXIT ADDRESS               GP03075
         MVI   4(R15),X'87'  SET END/JFCB ADDRESS               GP03075
         DROP  R9                                               GP03077
         SPACE 1                                                GP03075
         SR    R7,R7         CLEAR DIRECTORY BLOCK COUNT         84150
         L     R1,0(,R8)     GET ADDRESS OF PARM HEADER          84150
         SR    R2,R2         CLEAR LENGTH                        84150
         ICM   R2,3,0(R1)    LOAD AND TEST PARM FIELD LENGTH     84150
         BNP   DONEPARM      GET OLD COUNT                       84150
         CH    R2,=H'5'      LONG ENOUGH FOR DEBUG ?            GP03075
         BL    PARMNBUG      NO                                 GP03075
         LA    R0,5          SET FOR DEBUG                      GP03075
         CLC   =C'DEBUG',2(R1)  DEBUG REQUEST ?                 GP03075
         BE    PARMYBUG      YES; SET IT                        GP03075
         CLC   =C'PRINT',2(R1)  DEBUG REQUEST ?                 GP03075
         BE    PARMYBUG      YES; SET IT                        GP03075
         LA    R0,4          SET FOR TEST                       GP03075
         CLC   =C'DEBUG',2(R1)  DEBUG REQUEST ?                 GP03075
         BNE   PARMNBUG      NO; CHECK FOR COMMA                GP03075
PARMYBUG OI    PROFLAGS,FGDEBUG  SET DEBUG MODE                 GP03075
         SR    R2,R0         ADJUST RESIDUAL LENGTH             GP03075
         BNP   DONEPARM      NOTHING LEFT TO CHECK              GP03075
         AR    R1,R0         AND SKIP TEXT                      GP03075
PARMNBUG CLI   2(R1),C','    SEPARATING COMMA ?                 GP03075
         BNE   PARMTCNT      NO; CHECK DIR BLOCK COUNT PARM     GP03075
         LA    R1,1(,R1)                                        GP03075
         BCT   R2,PARMTCNT   GET RESIDUAL LENGTH                GP03075
         B     DONEPARM      USE OLD COUNT                      GP03075
         SPACE 1                                                GP03075
PARMTCNT LA    R15,C'0'      MAKE A ZERO                         84150
         SR    R14,R14                                           84150
TSTLDBLK CLI   2(R1),C' '    LEADING BLANK ?                     84150
         BNE   NOTLDBLK      NO                                  84150
         LA    R1,1(,R1)     SKIP TO NEXT FIELD                  84150
         BCT   R2,TSTLDBLK   REPEAT THE TEST                     84150
         B     DONEPARM      NULL PARM FIELD                     84150
NOTLDBLK SR    R4,R4         SET FLAG - PARM=NNNN TYPE           84150
         CLI   2(R1),C'0'    NUMERIC ?                           84150
         BNL   ADDITUP       YES; GO TO ADD IT                   84150
         LA    R4,1          SET FOR POSITIVE (PARM=+NN)         84150
         CLI   2(R1),C'+'    LEADING PLUS ?                      84150
         BE    SETLDSGN      YES; SET IT                         84150
         BCTR  R4,0                                              84150
         BCTR  R4,0          SET NEGATIVE                        84150
         CLI   2(R1),C'-'    MINUS ?                             84150
         BE    SETLDSGN                                          84150
BADPARM  WTO   'CLEARPDS - INVALID PARM FIELD'                   84150
         ABEND 666                                               84150
         SPACE 1                                                 84150
SETLDSGN LA    R1,1(,R1)     SKIP SIGN                           84150
         BCT   R2,ADDITUP    GO TO ADD NUMBERS                   84150
         SR    R4,R4         SIGNAL PARM=NN                      84150
         B     DONEPARM      BUT USE OLD COUNT                   84150
ADDITUP  CLI   2(R1),C'0'    NUMERIC DIGIT ?                     84150
         BL    BADPARM       NO; FAIL REQUEST                    84150
         CLI   2(R1),C'9'    DITTO ?                             84150
         BH    BADPARM       DITTO                               84150
         SLDL  R14,8         SHIFT ACCUMULATORS                  84150
         IC    R15,2(,R1)    GET LATEST DIGIT                    84150
         LA    R1,1(,R1)                                         84150
         BCT   R2,ADDITUP    DO NEXT DIGIT                       84150
         STM   R14,R15,DB2   STASH THE RIGHT-JUSTIFIED NUMBER    84150
         PACK  DB,DB2        MAKE IT PACKED                      84150
         CVB   R7,DB         MAKE IT BINARY                      84150
         LTR   R4,R4         TEST PROCESSING FLAG                84150
         BP    SAVECNT       + - ADD TO OLD COUNT               GP03075
         BZ    NEWDIRCT      NN - USE SUPPLIED VALUE             84150
         LNR   R7,R7         - - SUBTRACT FROM OLD COUNT         84150
SAVECNT  ST    R7,ADJCOUNT   SAVE ADJUSTMENT AMOUNT             GP03075
         B     DONEPARM                                         GP03075
NEWDIRCT ST    R7,EQUCOUNT   SAVE EXACT NEW COUNT               GP03075
         SPACE 1                                                GP03075
DONEPARM TM    PROFLAGS,FGDEBUG  RUNNING IN DEBUG MODE ?        GP03075
         BZ    SKIPPROP      NO; NO PRINTER OPEN                GP03075
         PRTOPEN SYSPRINT,DEV=1,OPT=(ABEND,WTO)                 GP03075
SKIPPROP PRTOPEN CONSOLE,DEV=2,OPT=(NOWTO)                      GP03075
         SPACE 1                                                 84150
***********************************************************************
**                                                                   **
**  EXTRA CODING ADDED LATER TO ALLOW MULTIPLE CLEARING IN           **
**  ONE JOB/STEP. ALL DD CARDS OF THE LIBRARIES TO BE CLEARED        **
**  SHOULD HAVE A DD NAME OF //SYSLIBXX  .                           **
**                                                                   **
***********************************************************************
         PUSH  USING                                            GP03077
LOOPDD   L     R1,@TIOTPTR   LOAD POINTER WITH LAST TIOT ADDRESS
         SERVCALL TIOLK,(R1) GET NEXT ENTRY                     GP03075
         BXH   R15,R15,PGMEXIT  ALL DONE                        GP03077
         LTR   R8,R0         ANY FOUND ?                        GP03075
         BZ    PGMEXIT       ALL DONE - GET OUT                 GP03075
         ST    R8,@TIOTPTR   UPDATE POINTER                     GP03075
         USING TIOELNGH,R8                                      GP03075
         CLC   =C'SYSLIB',TIOEDDNM  ONE OF OURS ?               GP03075
         BNE   LOOPDD        NO; GET ANOTHER                    GP03075
         SERVCALL TIODD,TIOEDDNM  DUPLICATE DD NAME ?           GP03075
         CR    R0,R8                                            GP03075
         BNE   DUPEDD        YES; ERROR                         GP03075
         MVI   OPTFLAGS,0    RESET FLAGS                        GP03075
         MVC   CURRDDNM,TIOEDDNM  SAVE FOR CLOSE                GP10214
         ICM   R1,7,TIOEFSRT  LOAD UCB ADDRESS                  GP03077
         BZ    JUNKDD        DUMMY ?                            GP03077
         SERVCALL SWAAB,(R1)  GET REAL UCB ADDRESS              GP03077
         LR    R6,R1                                            GP03077
         USING UCBOB,R6                                         GP03077
         CLI   UCBTBYT3,UCB3DACC  DASD ?                        GP03077
         BNE   JUNKDD        NO; JUST MESSAGE AND SKIP          GP03077
         ICM   R1,7,TIOEJFCB                                    GP03077
         SERVCALL SWAAD,(R1)                                    GP03077
         LR    R2,R1                                            GP03077
         USING INFMJFCB,R2                                      GP03077
         TM    JFCBIND1,JFCPDS  MEMBER SPECIFIED ?              GP03077
         BNZ   JUNKDD        YES; MESSAGE AND SKIP              GP03077
         MVC   MYJFCB(JFCBLGTH),INFMJFCB SAVE IN MY AREA        GP03077
         TM    JFCBIND2,JFCDISP   CHECK DISPOSITION             GP10214
         BNM   DISPSOSO      NEW OR NONE                        GP10214
         TM    JFCBIND2,JFCMOD    IS IT MOD?                    GP10214
         BO    BADDISP       YES; WOULD FAIL, ANYWAY            GP10214
         TM    JFCBIND2,JFCSHARE  IS IT SHARE ?                 GP10214
         BZ    DISPSOSO      NO; IT'S OLD                       GP10214
         PRTLIST MSGBDISP    SHOW UNWANTED DISP (NEW,SHR)       GP10214
         OICC  4             BUT KEEP RUNNING                   GP10214
DISPSOSO SERVCALL DSDJ1,INFMJFCB  GET DSCB 1 FOR DSN            GP03077
         BXH   R15,R15,JUNKDD   NONE SUCH                       GP03077
         USING DS1FMTID,R1   DECLARE RETURNED AREA              GP03077
         TM    DS1DSORG,JFCORGPO  PARTITIONED ?                 GP03077
         BZ    JUNKDD        NO                                 GP03077
         CLI   DS1DSORG+1,0       VSAM OR OTHER FUNNY?          GP09186
         BNE   JUNKDD        NO                                 GP09186
         TM    DS1SMSFG,DS1PDSEX  HFS DATA ?                    GP03077
         BNZ   JUNKDD        YES; DON'T PROCESS                 GP03077
         MVC   OLDLSTAR,DS1LSTAR  SAVE LSTAR                    GP10214
         TM    DS1SMSFG,DS1PDSE   PDS/E ?                       GP03077
         BZ    PLAINPDS                                         GP03077
         OI    OPTFLAGS,FGPDSE   YES - REMEMBER EXTENDED        GP03077
         LA    R2,1000       PRESET - 1000 MEMBERS              GP03078
         CPOOL BUILD,PCELLCT=(R2),SCELLCT=(R2),CSIZE=CELSIZE,          *
               LOC=BELOW,CPID=MEMPOOL,MF=(E,MFPOOL)             GP03078
         CPOOL BUILD,PCELLCT=(R2),SCELLCT=(R2),CSIZE=CELSIZE,          *
               LOC=BELOW,CPID=ALIPOOL,MF=(E,MFPOOL)             GP03078
*---------------------------------------------------------------------*
*  (RE)INITIALIZE DCBS; READ DIRECTORY; CLEAR OR DELETE MEMBERS       *
*---------------------------------------------------------------------*
PLAINPDS MVC   LIVEDCB(LIVELEN),PATDCB  REFRESH DCBS            GP03075
         MVC   INPDS+DCBDDNAM-IHADCB(8),TIOEDDNM MOVE NEW DDNAME
         MVC   PDS+DCBDDNAM-IHADCB(8),TIOEDDNM INTO DCBS        GP03075
         POP   USING                                            GP03077
         PUSH  USING                                            GP03078
         SR    R7,R7         ZERO MEMBER COUNT                  GP03077
         ST    R7,CURCOUNT   RESET DIRECTORY BLOCK COUNT        GP03077
         OPEN  (INPDS,INPUT),MF=(E,DCBOLIST) GET OLD DIRECTORY'S BLOCKS
         TM    OPTFLAGS,FGPDSE  PROCESSING A PDS/E ?            GP03078
         BNZ   GETLOOP       YES; ACCUMULATE MEMBER AND ALIAS NAMES
         SPACE 1                                                GP03078
*---------------------------------------------------------------------*
*  FOR A PDS, AND AFTER LAST MEMBER ON PDSE, COUNT REMAINING BLOCKS   *
*---------------------------------------------------------------------*
COUNTMEM GET   INPDS         READ DIRECTORY BLOCK AND COUNT      84150
         LA    R7,1(,R7)     IT                                  84150
         B     COUNTMEM                                         GP03078
         SPACE 1                                                GP03078
*---------------------------------------------------------------------*
*  FOR A PDS/E, ACCUMULATE MEMBER AND ALIAS NAMES FOR LATER DELETION  *
*---------------------------------------------------------------------*
GETLOOP  GET   INPDS         READ DIRECTORY BLOCK AND COUNT     GP03078
         LA    R7,1(,R7)     IT                                 GP03078
         LR    R3,R1         COPY DIRECTORY BLOCK ADDRESS       GP03078
         LA    R4,2          SET INCREMENT                      GP03078
         LH    R5,0(,R3)     GET DATA LENGTH IN BLOCK           GP03078
         AR    R5,R3         LENGTH+START                       GP03078
         BCTR  R5,0          LESS A LITTLE MORE                 GP03078
ENTLOOP  BXH   R3,R4,GETLOOP  GET NEXT BLOCK                    GP03078
         USING PDS2,R3       DECLARE DIRECTORY ENTRY MAPPING    GP03078
         IC    R4,PDS2INDC   GET ATTR/LEN BYTE                  GP03078
         N     R4,=A(PDS2LUSR)  KEEP ONLY LENGTH BITS           GP03078
         SLL   R4,1          CONVERT TO HALFWORDS               GP03078
         LA    R4,PDS2USRD-PDS2(,R4)    ADD OVERHEAD            GP03078
         CLC   PDS2NAME,DIRENT1   END OF LOGICAL LIST ?         GP03078
         BE    COUNTMEM      YES; REVERT TO PLAIN BLOCK COUNT   GP03078
         TM    PDS2INDC,PDS2ALIS  ALIAS ENTRY ?                 GP03078
         BNZ   ENTALIAS      YES; SAVE IT                       GP03078
         CPOOL GET,CPID=MEMPOOL,REGS=SAVE                       GP03078
         USING CELERY,R1                                        GP03078
         MVC   CELNAME,PDS2NAME  SAVE MEMBER NAME               GP03078
         MVC   CELLINK,MEMPOINT  MAKE BACK LINK                 GP03078
         ST    R1,MEMPOINT   MAKE NEW ADDRESS                   GP03078
         B     ENTLOOP                                          GP03078
         SPACE 1                                                GP03078
ENTALIAS CPOOL GET,CPID=MEMPOOL,REGS=SAVE                       GP03078
         MVC   CELNAME,PDS2NAME  SAVE MEMBER NAME               GP03078
         MVC   CELLINK,ALIPOINT  MAKE BACK LINK                 GP03078
         ST    R1,ALIPOINT   MAKE NEW ADDRESS                   GP03078
         B     ENTLOOP                                          GP03078
         POP   USING                                            GP03078
         SPACE 1                                                GP03075
DUPEDD   PRTLIST MSGDUPDD    WRITE EXTRANEOUS DD MESSAGE        GP03075
         OICC  4                                                GP10214
         B     LOOPDD        GET ANOTHER                        GP03075
         SPACE 1                                                GP03077
JUNKDD   PRTLIST MSGJNKDD,DEV=(1,2)  UNUSABLE DD MESSAGE        GP03077
         OICC  8                                                GP10214
         B     LOOPDD        GET ANOTHER                        GP03077
         SPACE 1                                                GP03077
BADDISP  PRTLIST MSGBDISP,DEV=(1,2)  WRITE UNUSABLE DD MESSAGE  GP10214
         OICC  8                                                GP10214
         B     LOOPDD        GET ANOTHER                        GP10214
         SPACE 1                                                GP03077
LOWDD    PRTLIST MSGCNTDD                                       GP03078
DIREDD   PRTLIST MSGERRDD,DEV=(1,2)    WRITE DIRECTORY ERROR    GP03078
         OICC  4                                                GP10214
         B     LOOPDD        GET ANOTHER                        GP03077
         SPACE 1                                                GP03075
*---------------------------------------------------------------------*
*                                                                     *
*   END OF FILE READING THE OLD DIRECTORY                             *
*   GET NEW BLOCK COUNT, AND BRANCH TO PDS OR PDSE PROCESSING         *
*                                                                     *
*---------------------------------------------------------------------*
EODADIR  CLOSE ,MF=(E,DCBOLIST)                                 GP03075
         LTR   R1,R7         DID WE OBTAIN A VALID COUNT ?       84150
         BNP   DIREDD        NO; INVALID DATA SET OR PARM        84150
         ST    R7,CURCOUNT   SAVE EXISTING COUNT                GP03077
         ICM   R7,15,EQUCOUNT  EXACT NEW COUNT REQUESTED ?      GP03077
         BNZ   HAVECNT       YES; USE IT                        GP03077
         L     R7,ADJCOUNT   GET NEW COUNT INCREASE/DECREASE    GP03077
         AR    R7,R1         ADD EXISTING COUNT                 GP03077
         BM    LOWDD         INVALID PARM                       GP03077
         BP    HAVECNT       POSITIVE                           GP03078
         LA    R7,1(,R7)     WRITE AT LEAST ONE BLOCK           GP03077
HAVECNT  ST    R7,NEWCOUNT   SET NEW COUNT                      GP03078
         TM    OPTFLAGS,FGPDSE  PDS/E ?                         GP03077
         BNZ   DELDIR        YES; DELETE THE DIRECTORY THE HARD WAY
         SPACE 1
***********************************************************************
**                                                                   **
**   PDS, NOT PDSE                                                   **
**     1) READ JFCB AND CHANGE TO DIRECTORY FORMAT U,256,256         **
**     2) CLOBBER IOB CCWS TO SUPPORT 8 BYTE KEY                     **
**     3) WRITE REQUESTED NUMBER OF DIRECTORY BLOCKS                 **
**                                                                   **
***********************************************************************
         LA    R9,PDS               ADDRESS OF DCB              GP03075
         USING IHADCB,R9                                        GP03075
         LA    R15,PDSEXLST  EXIT LIST FOR BPAM DCB             GP03075
         STCM  R15,7,DCBEXLSA  SET INTO PDS                     GP03075
         RDJFCB (PDS),MF=(E,PDSOLIST)                           GP03075
         PRTLIST MSGSHODD    WRITE BASIC INFORMATION            GP03078
         LA    R15,MYJFCB    FAKE FOR (OLD) USING               GP05033
         USING INFMJFCB,R15                                     GP05033
         MVI   JFCDSORG,JFCORGPO    CHANGE DSORG FROM PS TO PO IN JFCB
         XC    JFCRECFM,JFCRECFM    ZERO RECFM,BLKSI,LRECL,KEYLE
         XC    JFCBLKSI,JFCBLKSI
         XC    JFCLRECL,JFCLRECL
         XC    JFCKEYLE,JFCKEYLE
         MVI   JFCOPTCD,0    CLEAR OPTCD (=W BOMBS ON D37)       84150
         OI    JFCBTSDM,JFCNWRIT+JFCNDCB                        GP03078
         NI    JFCBTSDM,255-JFCNDSCB ALLOW MERGE                GP03078
         DROP  R15                                              GP05033
         MVI   DCBOPTCD,0    DITTO                               84150
         MVI   DCBNCP,X'02'         2 CHANNEL PROGRAMS
         MVI   DCBRECFM,X'00'
         MVI   DCBKEYLE,X'00'
         XC    DCBLRECL(2),DCBLRECL
         XC    DCBBLKSI(2),DCBBLKSI
         OPEN  (PDS,(OUTPUT)),TYPE=J,MF=(E,PDSOLIST)            GP03075
         MVI   DCBRECFM,X'81'       FIXED AND KEYLENGTH SPECIFIED
         MVI   DCBKEYLE,X'08'       KEYLENGTH=8
         MVI   DCBKEYCN,X'00'       KEYCN=0
         MVC   DCBLRECL,=H'256'     LRECL=BLKSI=256             GP03075
         MVC   DCBBLKSI,=H'256'                                 GP03075
         SR    R2,R2                                            GP03078
         IC    R2,DCBWCPO           WRITE CHANNEL PROGRAM OFFSET
         L     R3,DCBIOBA           IOB ADDRESS OF CHANNEL PROGRAM 1
         N     R3,=X'00FFFFFF'                                  GP03077
         L     R4,0(,R3)            IOB ADDRESS OF CHANNEL PROGRAM 2
         N     R4,=X'00FFFFFF'                                  GP03077
         AR    R3,R2
         AR    R4,R2
         IC    R2,DCBWCPL           WRITE CHANNEL PROGRAM LENGTH
         SLL   R2,3                 *8=NO. OF BYTES
         AR    R3,R2
         AR    R4,R2
         MVI   5(R3),X'08'          KEY LENGTH=8 IN COUNTS
         MVI   5(R4),X'08'
         SPACE 1
         LA    R5,DECB1      POINT TO FIRST DECB                GP03077
         LA    R6,DECB2      POINT TO SECOND DECB               GP03077
         ST    R9,8(,R5)     SET PDS DCB ADDRESS, ONCE          GP03078
         ST    R9,8(,R6)     SET PDS DCB ADDRESS, ONCE          GP03078
         LA    R8,DIRENT1    FIRST DIRECTORY ENTRY              GP03077
         MVI   DECB2,X'7F'   FAKE COMPLETE                      GP03077
         B     RITESKIP                                         GP03077
RITELOOP CHECK (R5)          CHECK FIRST DECB                   GP03077
RITESKIP XC    0(4,R5),0(R5)   CLEAR ECB                        GP03077
         WRITE (R5),SF,,(R8),'S',MF=E                           GP03078
         BCT   R7,RITEMORE                                      GP03077
         B     RITEDONE      ALL DONE                           GP03077
RITEMORE SWAPR R5,R6         EXCHANGE DECBS                     GP03077
         LA    R8,DIRENT2    SECOND/SUBSEQUENT DIRECTORY ENTRY  GP03077
         B     RITELOOP      PROCESS IT                         GP03077
         SPACE 2
RITEDONE CHECK (R5)          CHECK LAST WRITE                   GP03077
         MVI   5(R3),X'00'          KEY LENGTH=0 IN BOTH COUNTS
         MVI   5(R4),X'00'
         SR    R2,R2                                            GP03078
         IC    R2,DCBOFFSW          WRITE CCW OFFSET
         L     R3,DCBIOBA           IOB ADDRESSES- 2 CHAN. PROGS.
         N     R3,=X'00FFFFFF'                                  GP03077
         L     R4,0(,R3)
         N     R4,=X'00FFFFFF'                                  GP03077
         AR    R3,R2
         AR    R4,R2
         OI    4(R3),X'20'          LIGHT SLI BIT IN BOTH CCW'S
         OI    4(R4),X'20'
         XC    DCBBLKSI,DCBBLKSI    ZERO OUT BLOCKSIZE
         WRITE (R6),SF,,DIRENT2,'S',MF=E  WRITE EOF BLOCK       GP03078
         CHECK (R6)                                             GP03077
         MVC   DCBOPTCD,SAVOPTCD  RESTORE OPTCD PRIOR TO CLOSE
         B     DONEGOOD      WRITE FINAL MESSAGE                GP03078
         SPACE 2                                                GP03078
***********************************************************************
**                                                                   **
**   PDSE PROCESSING                                                 **
**     1) DELETE ALL ALIAS ENTRIES (IF ANY)                          **
**     2) DELETE ALL MEMBER ENTRIES                                  **
**                                                                   **
***********************************************************************
         PUSH  USING                                            GP03078
DELDIR   LA    R9,PDS               ADDRESS OF DCB              GP03075
         USING IHADCB,R9                                        GP03075
         MVC   PDS(PATPDSEL),PATPDSE  MAKE PO                   GP03078
         LA    R15,PDSEXLST  EXIT LIST FOR BPAM DCB             GP03075
         STCM  R15,7,DCBEXLSA  SET INTO PDS                     GP03075
         RDJFCB (PDS),MF=(E,PDSOLIST)                           GP03075
*DUPE*   USING INFMJFCB,MYJFCB                                  GP03077
         PRTLIST MSGSHODD    WRITE BASIC INFORMATION            GP03078
         OPEN  (PDS,(OUTPUT)),TYPE=J,MF=(E,PDSOLIST)            GP03075
         AIF   (NOT &MVSXA).FAILIN                              GP05033
         STOW  PDS,,I        (RE)INITIALIZE THE DIRECTORY       GP05033
         BXLE  R15,R15,DONEGOOD  SUCCESSFUL                     GP05033
.FAILIN  USING CELERY,R3     DECLARE THE MEMBER/ALIAS MAPPING   GP03078
DELSLOW  ICM   R3,15,ALIPOINT  IS THERE AN ALIAS CHAIN ?        GP03078
         BZ    DELMEMPF                                         GP03078
         PRTLIST MSGPALI     MAKE ALIAS HEADER                  GP03078
DELALIAS ICM   R3,15,ALIPOINT  IS THERE AN ALIAS CHAIN ?        GP03078
         BZ    DELMEMPF                                         GP03078
         MVC   ALIPOINT,CELLINK  UNCHAIN THIS ONE               GP03078
         BAS   R14,SCRATCH   DELETE AN ALIAS                    GP03078
         B     DELALIAS      DO THE NEXT ONE                    GP03078
DELMEMPF ICM   R3,15,MEMPOINT  IS THERE A MEMBER CHAIN ?        GP03078
         BZ    DONEGOOD                                         GP03078
         PRTLIST MSGPMEM     MAKE MEMBER HEADER                 GP03078
DELMEMBR ICM   R3,15,MEMPOINT  IS THERE A MEMBER CHAIN ?        GP03078
         BZ    DONEGOOD                                         GP03078
         MVC   MEMPOINT,CELLINK  UNCHAIN THIS ONE               GP03078
         BAS   R14,SCRATCH   DELETE A MEMBER                    GP03078
         B     DELMEMBR      DO THE NEXT ONE                    GP03078
         SPACE 1                                                GP03078
SCRATCH  STM   R14,R2,BAKRSAVE  SAVE A LITTLE                   GP05033
         FIND  (R9),CELNAME,D                                   GP03078
         STOW  (R9),CELNAME,D                                   GP03078
         PRTLIST MSGNMEM     ADD NAME TO LIST                   GP03078
         LM    R14,R2,BAKRSAVE  RESTORE                         GP05033
         BR    R14           RETURN TO CALLER                   GP05033
MSGPALI  FDPRT '  DELETING ALIAS  :',NL                         GP03078
         FDPRT *END                                             GP03078
MSGPMEM  FDPRT '  DELETING MEMBER :',NL                         GP03078
         FDPRT *END                                             GP03078
MSGNMEM  FDOPT IND=10                                           GP03078
         FDPRT CELNAME,PAD                                      GP03078
         FDPRT *END                                             GP03078
         POP   USING                                            GP03078
         SPACE 1                                                GP03078
*---------------------------------------------------------------------*
*  COMMON END POINT FOR PDS AND PDSE PROCESSING FOR ONE DATA SET      *
*---------------------------------------------------------------------*
DONEGOOD PRTLIST MSGSUCC,DEV=(1,2)  WRITE SUCCESSFUL COMPLETION GP03075
         ICM   R0,15,MEMPOOL IS THERE A CELL POOL ?             GP03078
         BZ    NOMEMPUL                                         GP03078
         CPOOL DELETE,CPID=(0)                                  GP03078
         XC    MEMPOOL,MEMPOOL                                  GP03078
         XC    MEMPOINT,MEMPOINT                                GP03078
NOMEMPUL ICM   R0,15,ALIPOOL IS THERE A CELL POOL ?             GP03078
         BZ    NOALIPUL                                         GP03078
         CPOOL DELETE,CPID=(0)                                  GP03078
         XC    ALIPOOL,ALIPOOL                                  GP03078
         XC    ALIPOINT,ALIPOINT                                GP03078
NOALIPUL CLOSE ,MF=(E,PDSOLIST)                                 GP03075
         SPACE 1                                                GP03075
         LA    R2,INPDS                                          86121
         BAL   R7,DIRTY                                          86121
         LA    R2,PDS        JUST IN CASE                        86121
         BAL   R7,DIRTY                                          86121
         SERVCALL DSDJ1,MYJFCB    GET DSCB 1 FOR DSN            GP10214
         BXH   R15,R15,LOOPDD   NONE SUCH                       GP10214
         USING DS1FMTID,R1   DECLARE RETURNED AREA              GP10214
         CLC   OLDLSTAR,DS1LSTAR  DID SOMETHING ?               GP10214
         BNE   LOOPDD        GOOD?                              GP10214
         ICM   R0,15,ADJCOUNT     REQUEST TO CHANGE BLOCKS?     GP11298
         BZ    LOOPDD               NO; EMPTY ALREADY           GP11298
         PRTDATA CURRDDNM,('LSTAR UNCHANGED',PAD),DEV=(1,2)     GP10214
         OICC  4                                                GP10214
         B     LOOPDD                                           GP03075
         SPACE 2
PGMEXIT  L     R9,RETCODE    GET THE RETURN CODE                GP10214
         PGMEXIT RC=(R9)     AND QUIT                           GP03075
         SPACE 1                                                 86121
*---------------------------------------------------------------------*
*   DIRTY - CONDITIONAL FREEPOOL                                      *
*---------------------------------------------------------------------*
DIRTY    TM    DCBBUFCB+3-IHADCB(R2),1 ODD BUFFER ADDRESS ?      86121
         BNZR  R7            YES; NO BUFFER                      86121
         ICM   R0,7,DCBBUFCB+1-IHADCB(R2)  ALL ZERO ?            86121
         BZR   R7            DITTO                               86121
         FREEPOOL (R2)       ELSE FREE IT                        86121
         BR    R7            RETURN TO CALLER                    86121
         SPACE 2
         LTORG ,
         SPACE 2                                                GP03075
SYSPRINT PRTWORK SYSPRINT,TITLE=3                               GP03075
CONSOLE  PRTWORK *CONSOLE                                       GP03075
         SPACE 1                                                GP03075
MSGSUCC  FDPRT '-> CLEARPDS SUCCESSFUL FOR',NL                  GP03078
         FDPRT JFCBDSNM-INFMJFCB+MYJFCB,L'JFCBDSNM,PAD,DEB
         FDPRT 'ON'                                             GP03078
         FDPRT JFCBVOLS-INFMJFCB+MYJFCB,6,DEB,PAD
         FDOPT NL            FORCE WTO                          GP03078
         FD    *END                                             GP03075
         SPACE 1                                                GP03075
         USING TIOELNGH,R8                                      GP03075
MSGDUPDD FDPRT '>>> MULTIPLE OCCURRENCES OF DD NAME',NL         GP03075
         FDPRT TIOEDDNM,PADL,DEB                                GP03075
         FDPRT '; ONLY FIRST ONE USED'                          GP03075
         FD    *END                                             GP03075
         SPACE 1                                                GP03075
MSGJNKDD FDPRT '>>> DD',NL                                      GP03077
         FDPRT TIOEDDNM,PADL,DEB                                GP03077
         FDPRT 'NOT PDS OR SPECIFIES A MEMBER NAME'             GP03077
         FDOPT NL                                               GP03078
         FD    *END                                             GP03077
         SPACE 1                                                GP10214
MSGBDISP FDPRT '>>> DD',NL                                      GP10214
         FDPRT TIOEDDNM,PADL,DEB                                GP10214
         FDPRT 'DISPOSITION NOT OLD',PADL                       GP10214
         FDOPT NL                                               GP03078
         FD    *END                                             GP10214
         SPACE 1                                                GP03075
MSGCNTDD FDPRT '>>> DIRECTORY BLOCK DECREASE NOT POSSIBLE <<<',NL
         FD    *END                                             GP03077
MSGERRDD FDPRT '>>> ERROR PROCESSING DIRECTORY OF DD',NL        GP03078
         FDPRT TIOEDDNM,PADL,DEB                                GP03077
         FDPRT ', DSN'                                          GP03078
         FDPRT JFCBDSNM-INFMJFCB+MYJFCB,L'JFCBDSNM,PAD,DEB
         FDPRT 'ON'                                             GP03078
         FDPRT JFCBVOLS-INFMJFCB+MYJFCB,6,DEB,PAD
         FDPRT '<<<'                                            GP03078
         FDOPT NL                                               GP03078
         FD    *END                                             GP03077
         SPACE 1                                                GP03075
MSGSHODD FDOPT NL,CC=C'0'  DOUBLE SPACE                         GP03078
         FDPRT 'PROCESSING DIRECTORY OF DD'                     GP03078
         FDPRT TIOEDDNM,PADL,DEB                                GP03077
         FDPRT ', DSN'                                          GP03078
         FDPRT JFCBDSNM-INFMJFCB+MYJFCB,L'JFCBDSNM,PAD,DEB
         FDPRT 'ON'                                             GP03078
         FDPRT JFCBVOLS-INFMJFCB+MYJFCB,6,DEB,PAD
         FDPRT '    OLD DIRECTORY BLOCK COUNT WAS',NL           GP03078
         FDPRT CURCOUNT,I,DEB,PADL                              GP03078
         FDPRT '; NEW COUNT IS'                                 GP03078
         FDPRT NEWCOUNT,I,DEB,PADL                              GP03078
         FD    *END                                             GP03077
         SPACE 2
DIRENT1  DC    X'FFFFFFFFFFFFFFFF' FIRST-KEY HIGHEST NAME
         DC    X'000E'             FIRST BLK LENGH
         DC    X'FFFFFFFFFFFFFFFF' FIRST ENTRY HIGHEST NAME
DIRENT2  DC    264X'00'
         SPACE 1                                                 86121
*---------------------------------------------------------------------*
*                                                                     *
*   PATTERN DCB/PDS DEFINITIONS - DCB FOR DIRECTORY READ; PDS FOR     *
*     DIRECTORY REWRITE (FOR PDS/E, USED ONLY FOR MEMBER DELETES)     *
*                                                                     *
*---------------------------------------------------------------------*
PATDCB   DS    0F            START OF COPY SOURCE                86121
PATOLSTD OPEN  (PATIN,INPUT),MF=L                               GP03075
PATOLSTP OPEN  (PATPDS,OUTPUT),MF=L                             GP03075
PATIN    DCB DSORG=PS,DDNAME=SYSLIB,MACRF=GL,RECFM=F,BLKSIZE=256,      X
               LRECL=256,KEYLEN=8,EODAD=EODADIR                 GP03077
PATPDS   DCB   DSORG=PS,DDNAME=SYSLIB,MACRF=W,EXLST=123         GP03075
         WRITE PATDECB1,SF,PDS-PDS,DIRENT1,'S',MF=L             GP03075
         WRITE PATDECB2,SF,PDS-PDS,DIRENT1,'S',MF=L             GP03075
PATOPTCD DC    X'00'                                            GP03075
         SPACE 1                                                GP03078
PATPDSE  DCB   DSORG=PO,DDNAME=SYSLIB,MACRF=W,EXLST=123         GP03078
PATPDSEL EQU   *-PATPDSE                                        GP03078
         SPACE 2
         PUSH  USING                                            GP03075
         DROP  ,             DROP EVERYTHING
         USING EXITDCB,R15                                       84150
         USING IHADCB,R1
EXITDCB  MVC   SAVOPTCD-PDS(1,R15),DCBOPTCD   SAVE USER'S OPTCD GP03077
         MVI   DCBOPTCD,0    PREVENT D37 CAUSED BY =W           GP03075
         BR    R14
         POP   USING                                            GP03075
         SPACE 1                                                GP03078
CELERY   DSECT ,                                                GP03078
CELLINK  DS    A             LINK TO NEXT MEMBER                GP03078
CELNAME  DS    CL8           NAME OF MEMBER OR ALIAS            GP03078
CELSIZE  EQU   *-CELERY        SIZE OF ENTRY                    GP03078
         SPACE 1                                                GP03075
         IEFJFCBN ,                                             GP03075
         SPACE 2                                                 84150
SAVE     DSECT ,                                                 80006
BAKRSAVE DS    6A            UNTIL BAKR/PR BACK                 GP05033
DB       DS    D             WORK SPACE                          84150
DB2      DS    D               MORE OF SAME                      84150
CURRDDNM DS    CL8           CURRENT DD NAME                    GP10214
         SERVDEFS ,          SERVICE ROUTINE ADDRESSES          GP10214
ZEROES   DC    XL4'0'                                           GP03075
ADJCOUNT DS    F             NUMBER OF DIRECTORY BLOCKS ADD/SUB GP03075
EQUCOUNT DS    F             NUMBER OF NEW BLOCKS IF NON-ZERO   GP03075
CURCOUNT DS    F             NUMBER OF BLOCKS READ              GP03075
NEWCOUNT DS    F             NUMBER OF BLOCKS WRITTEN           GP03075
@TIOTPTR DS    A             ADDRESS OF CURRENT TIOT ENTRY      GP03075
ALIPOINT DS    A             CHAIN OF ALIAS NAMES               GP03077
MEMPOINT DS    A             CHAIN OF MEMBER NAMES              GP03077
ALIPOOL  DS    A             CPOOL ID FOR ALIAS ENTRIES         GP03078
MEMPOOL  DS    A             CPOOL ID FOR MEMBER ENTRIES        GP03078
PROFLAGS DS    X             PROCESSING FLAGS                   GP03075
FGHIT    EQU   X'40'           AT LEAST ONE DD PROCESSED        GP03075
FGDEBUG  EQU   X'01'           RUNNING IN DEBUG MODE            GP03075
OPTFLAGS DS    X             PROCESSING FLAGS                   GP03075
FGPDSE   EQU   X'80'           TARGET IS A PDS/E                GP03075
OLDLSTAR DS    XL3           STARTING LSTAR VALUE               GP10214
         SPACE 1                                                 86121
LIVEDCB  DS    0F            START OF COPY DESTINATION           86121
DCBOLIST OPEN  (INPDS,INPUT),MF=L                               GP03075
PDSOLIST OPEN  (PDS,OUTPUT),MF=L                                GP03075
INPDS    DCB DSORG=PS,DDNAME=SYSLIB,MACRF=GL,RECFM=F,BLKSIZE=256,      X
               LRECL=256,KEYLEN=8,EODAD=EODADIR                 GP03075
PDS      DCB   DSORG=PS,DDNAME=SYSLIB,MACRF=W,EXLST=PDSEXLST-PDSEXLST
         WRITE DECB1,SF,PDS-PDS,DIRENT1,'S',MF=L                GP03075
         WRITE DECB2,SF,PDS-PDS,DIRENT1,'S',MF=L                GP03075
SAVOPTCD DC    X'0'          SAVE OPTCD                         GP03075
LIVELEN  EQU   *-LIVEDCB                                         86121
PDSEXLST DC    0A(0),X'05',AL3(EXITDCB)  DCB EXIT               GP03075
         DC    X'87',AL3(INFMJFCB)      EXLST FOR RDJFCB        GP03075
MYJFCB   DS    0F,XL(JFCBLGTH)  COPY OF JFCB                    GP03075
MFPOOL   CPOOL BUILD,MF=L                                       GP03078
SAVEEND  EQU   *                                                GP03075
         SPACE 2                                                 84150
         DCBD  DSORG=PS
         PRINT &PRTSYS                                          GP03075
         CVT   DSECT=YES,LIST=NO                                GP03075
         IHACDE ,                                               GP03075
MYDSCB   DSECT ,                                                GP03077
         IECSDSL1 1          FORMAT 1 DSCB                      GP03075
         AIF   (&MVSXA).SYSDEF                                  GP04234
DS1FLAG1 EQU   DS1NOBDB+1,1,C'X'  MORE FLAGS
DS1COMPR EQU   X'80'           COMPRESSABLE EXTENDED IF DS1STRP
DS1CPOIT EQU   X'40'           CHECKPOINTED D S
DS1SMSFG EQU   DS1FLAG1+17,1,C'X'  SMS FLAG
DS1SMSDS EQU   X'80'           SMS D S
DS1SMSUC EQU   X'40'           NO BCS ENTRY
DS1REBLK EQU   X'20'           MAY BE REBLOCKED
DS1CRSDB EQU   X'10'           BLKSZ BY DADSM
DS1PDSE  EQU   X'08'           PDS/E
DS1STRP  EQU   X'04'           EXTENDED FORMAT D S
DS1PDSEX EQU   X'02'           HFS D S
DS1DSAE  EQU   X'01'           EXTENDED ATTRIBUTES EXISY
DS1SCEXT EQU   DS1SMSFG+1,3,C'X'  SECONDARY SPACE EXTENSION
DS1SCXTF EQU   DS1SCEXT,1,C'X'  -"- FLAG
DS1SCAVB EQU   X'80'           SCXTV IS AVG BLOCK LEN
DS1SCMB  EQU   X'40'                 IS IN MEGBYTES
DS1SCKB  EQU   X'20'                 IS IN KILOBYTES
DS1SCUB  EQU   X'10'                 IS IN BYTES
DS1SCCP1 EQU   X'08'           SCXTV COMPACTED BY 256
DS1SCCP2 EQU   X'04'                 COMPACTED BY 65536
DS1SCXTV EQU   DS1SCXTF+1,2,C'X'  SEC SPACE EXTNSION VALUE
DS1ORGAM EQU   DS1ACBM         CONSISTENT NAMING - VSAM D S
DS1RECFF EQU   X'80'           RECFM F
DS1RECFV EQU   X'40'           RECFM V
DS1RECFU EQU   X'C0'           RECFM U
DS1RECFT EQU   X'20'           RECFM T   001X XXXX IS D
DS1RECFB EQU   X'10'           RECFM B
DS1RECFS EQU   X'08'           RECFM S
DS1RECFA EQU   X'04'           RECFM A
DS1RECMC EQU   X'02'           RECFM M
*   OPTCD DEFINITIONS   BDAM    W.EFA..R
*                       ISAM    WUMIY.LR
*             BPAM/BSAM/QSAM    WUCHBZTJ
DS1OPTIC EQU   X'80'  FOR DS1ORGAM - CATLG IN ICF CAT
DS1OPTBC EQU   X'40'           ICF CATALOG
DS1RACDF EQU   DS1IND40
DS1SECTY EQU   DS1IND10
DS1WRSEC EQU   DS1IND04
DS1SCAL1 EQU   DS1SCALO,1,C'X'    SEC. ALLOC FLAGS
DS1DSPAC EQU   X'C0'         SPACE REQUEST MASK
DS1CYL   EQU   X'C0'           CYLINDER BOUND
DS1TRK   EQU   X'80'           TRACK
DS1AVRND EQU   X'41'           AVG BLOCK + ROUND
DS1AVR   EQU   X'40'           AVG BLOCK LEN
DS1MSGP  EQU   X'20'
DS1EXT   EQU   X'10'           SEC. EXTENSION EXISTS
DS1CONTG EQU   X'08'           REQ. CONTIGUOUS
DS1MXIG  EQU   X'04'           MAX
DS1ALX   EQU   X'02'           ALX
DS1DSABS EQU   X'00'           ABSOLUTE TRACK
DS1SCAL3 EQU   DS1SCAL1+1,3,C'X'  SEC ALLOC QUANTITY
.SYSDEF  SPACE 1
MYTIOT   DSECT ,                                                GP03075
         IEFTIOT1 ,          TIOT                               GP03075
         IKJTCB ,                                               GP03075
MYUCB    DSECT ,                                                GP03077
         IEFUCBOB ,                                             GP03077
         IHAPDS PDSBLDL=NO   DIRECTORY ENTRY MAPPING            GP03078
         END   ,
