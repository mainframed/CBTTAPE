SHOWTCT  TITLE 'S H O W T C T  ***  DISPLAY TCT COUNTERS'       GP04115
         MACRO ,                                                GP04115
&NM      DYNMAC &PFX=?                                          GP04115
         GBLC  &MACPLAB                                         GP04115
         LCLC  &P            SHORTEN                            GP04115
&MACPLAB SETC  '&NM'                                            GP04115
&P       SETC  '&PFX'                                           GP04115
         MACPARM MODE=LBL                                       GP04115
&P.EB4UV LINK  EP=IEFEB4UV,SF=L                                 GP04115
&P.OPDCB DCB   DSORG=PS,MACRF=PL,DDNAME=SYSCOUNT,RECFM=FBA,LRECL=121
&P.OPLST OPEN  (&P.OPDCB,OUTPUT),MF=L                           GP04115
&P.S99PT DC    A(0)                                             GP04115
&P.S99RB DC    A(X'14010000',0,*+12,0,0)                        GP04115
&P.S99TU DC    A(S99TXT01,X'80000000'+S99TXT18)                 GP04115
         MEND  ,                                                GP04115
         SPACE 2                                                GP04115
         PUNCH ' ORDER SHOWTCT(P) '   EASY DUMPS                GP04115
         COPY  OPTIONGB                                         GP04115
         SPACE 1                                                GP04115
         SYSPARM LIST=YES                                       GP04115
         SPACE 2                                                GP04115
         PRINT &PRTSOR                                          GP04115
***********************************************************************
**                                                                   **
**   FROM CBT 451, FILE 183, MEMBER TCTDCTR                          **
**                                                                   **
**       EDITION DU NOMBRE D'EXCP SUR TOUS LES FICHIERS DU STEP      **
**                                                                   **
**       VERSION CHAPEAU DE TCTDCTR, XA 2.2                          **
**                                                                   **
***********************************************************************
**                                                                   **
**   1) ISSUE LINK EP=SHOWTCT AT INTERSTING PLACES IN YOUR PROGRAM   **
**                                                                   **
**   2) OUTPUT TO SYSCOUNT DD - FBA/121/0 OUTPUT FILE                **
**                                                                   **
***********************************************************************
*
SHOWTCT  PGMHEAD ZERO12,NOREG,BASE=R12,AM=31,RM=24,SETAM=31,PARM=R9,   *
               PFX=SAWE,END=SAWEEND                             GP04115
         LA    R14,DYNMACS                                      GP04115
         LA    R15,DYNMSIZE                                     GP04115
         L     R0,=A(PATMACS)                                   GP04115
         LR    R1,R15                                           GP04115
         MVCL  R14,R0        INITIALIZE SYSTEM MACROS           GP04115
         LA    R0,DYNS99RB                                      GP04115
         ST    R0,DYNS99PT                                      GP04115
         OI    DYNS99PT,X'80'  SVC 99 POINTER                   GP04115
         LA    R0,DYNS99TU   TEXT UNITS                         GP04115
         ST    R0,DYNS99RB+8    STASH POINTER                   GP04115
         SPACE 1                                                GP04115
         SERVINIT ,          INITIALIZE MY SERVICES             GP04115
         SPACE 1                                                GP04115
*---------------------------------------------------------------------*
*   DETERMINE THE ENVIRONMENT:                                        *
*     1) IF UNDER TSO, USE PUTLINE FOR OUTPUT                         *
*     2) IF UNDER JES2, TRY TO GET PDDB COUNTS FOR SPOOLED FILES      *
*---------------------------------------------------------------------*
         LTCB  R4,USE=YES                                       GP04115
         PUSH  USING                                            GP04115
         ICM   R3,15,TCBJSCB GET THE JSCB                       GP04115
         BZ    ENVDONE       HUH?  WILL FAIL, ANYWAY            GP04115
         ICM   R2,15,JSCBSSIB-IEZJSCB(R3)  GET SSIB             GP04115
         BZ    ENVDONE       ??                                 GP04115
         USING SSIB,R2                                          GP04115
         CLI   SSIBSSID,SSIBJES2  UNDER JES2 ?                  GP04115
         BNE   ENVDONE       NO; NOT WRITTEN (YET?)             GP04115
         OI    PROFLAGS,PFJES2  SHOW JES2 ENVIRONMENT           GP04115
         CLI   SSIBJBID,C'T' TIME SHARING THE ODD WAY?          GP04115
         BNE   ENVNOTSO                                         GP04115
         OI    PROFLAGS,PFTSO   SHOW TSO ENVIRONMENT            GP04115
ENVNOTSO ICM   R5,15,SSIBSUSE   GET SJB ADDRESS                 GP04115
         BZ    ENVDONE       HUH ?                              GP04115
         USING SJB,R5        DECLARE IT                         GP04115
         ICM   R3,15,SJBSDB  SDB FOR PDDB CHAIN                 GP04115
         BZ    ENVDONE                                          GP04115
         ST    R3,@SDB       SAVE FIRST SDB ADDRESS             GP04115
         POP   USING                                            GP04115
ENVDONE  TM    PROFLAGS,PFTSO  UNDER TSO ?                      GP04115
         BNZ   SKIPOPEN                                         GP04115
         LA    R1,DYNS99PT   SVC99 REQUEST BLOCK HEADER         GP04115
         SVC   99                      ALLOCATE SYSCOUNT
         OPEN  (DYNOPDCB,OUTPUT),MF=(E,DYNOPLST)                GP04115
         ST    R15,RETCODE                                      GP04115
         TM    DYNOPDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPENED ?      GP04115
         BNZ   INITDONE      GOOD OPEN                          GP04115
         B     GOBACK                  QUIT IF OPEN FAILED      GP04115
         SPACE 1                                                GP04115
SKIPOPEN STLINENO LINE=1,CLEAR=YES     TOP OF SCREEN            GP04115
INITDONE L     R5,TCBTIO               MY TIOT                  GP04115
         USING TIOT1,R5
         L     R6,PSAAOLD-PSA          MY ASCB                  GP04115
         L     R6,ASCBOUCB-ASCB(,R6)   MY OUCB
         USING OUCB,R6
         L     R7,TCBJSCB              MY JSCB
         USING IEZJSCB,R7
         BAS   R14,OUTLINE         <== LOCATE OUTPUT RECORD     GP04115
         STRING '1 JOB=',TIOCNJOB,'  STEP=',(TIOCSTEP+0,8),            X
               '  PROCSTEP=',(TIOCSTEP+8,8),                           X
               '  PGM=',JSCBPGMN,                                      X
               '  PERFORM=',(OUCBNPG,H,L),                             X
               INTO=((R1),121)
         BAS   R14,OUTLINE             WRITE BLANK LINE         GP04115
         BAS   R14,OUTLINE                                      GP04115
         STRING '   DDNAME     DISP  VOLSER    EXCP   DATA SET NAME',  X
               34X,'DEVICE     DEV#    LABEL',                         X
               INTO=((R1),121)
         BAS   R14,OUTLINE                                      GP04115
         MVI   1(R1),C'-'              LINE OF DASHES
         MVC   2(110,R1),1(R1)         LINE OF DASHES           GP04122
*LOOP
TIOT_GET CLI   TIOEDDNM,C' '           FREED DD?
         BL    TIOT_NXT                YES, JUMP
         LA    R6,TENWORDS+4           WORK AREA
         ST    R6,TENWORDS             WORK AREA
         USING ZB505,R6
         XC    SWAEPAX,SWAEPAX
         MVC   SWVA,TIOEJFCB           SVA OF THE JFCB
         SWAREQ FCODE=RL,EPA=TENWORDS,MF=(E,SWAREQL1),UNAUTH=YES
         L     R6,SWBLKPTR             31-BIT ADDRESS OF JFCB
         USING INFMJFCB,R6
         MVC   WKCELL1,BLANKS          CLEAR WORK AREA
         MVC   STATUS,BLANKS           CLEAR WORK AREA
         MVC   UNITNAME,BLANKS         CLEAR WORK AREA
         MVC   VOLSER,=C'*JES*    '    SYSIN/SYSOUT
         SLR   R1,R1                   EXCP=0
         SLR   R7,R7                   NO UCB
         TM    TIOELINK,TIOESYIN+TIOESYOT           SPOOLED?    GP04115
         BNZ   TIOTJES2                YES, JUMP                GP04115
         MVC   VOLSER,=C'DUMMY   '     DUMMY DATA SET
         ICM   R7,B'0111',TIOEFSRT     UCB ADDRESS
         BZ    TIOT64                  NO UCB, SKIP TCT SCAN
         USING UCBOB,R7
         MVC   UNITNAME+8(4),UCBTYP    DEVICE TYPE
         BAL   R14,GETUNIT         <== GET UNITNAME
         MVC   VOLSER,=C'-VIO-     '   UNIT=VIO
         TM    UCBOB,UCBVRDEV          IS THIS A VIO DATA SET?
         BO    TIOT41                  YES, JUMP
         MVC   VOLSER,JFCBVOLS         NO, MOVE FIRST VOLSER
*
TIOT41   BAS   R1,TIOT41IC
         DC    C'NEW',AL1(JFCNEW)
         DC    C'MOD',AL1(JFCMOD)
         DC    C'SHR',AL1(JFCOLD+JFCSHARE)
         DC    C'OLD',AL1(JFCOLD)
         DC    C'???',0H'0'
TIOT41TM TM    JFCBIND2,*-*            TEST LABEL TYPE FLAGS
*--LOOP
TIOT41IC IC    R15,3(,R1)              PICK UP MASK FOR "TM"
         EX    R15,TIOT41TM
         BO    TIOT41X
         LA    R1,3+1(,R1)             BUMP TABLE PTR
         CLI   0(R1),C'?'              END OF TABLE?
         BNE   TIOT41IC                NEXT TABLE ENTRY
*--ENDLOOP
TIOT41X  MVC   STATUS,0(R1)            DISP
*
         TM    UCBFL5,UCBSMS           SMS VOL?     X'20'
         BZ    TIOT46                  NO, JUMP
         MVC   WKCELL1(3),=C'SMS'      SMS-MANAGED
*
TIOT46   CLI   UCBTBYT3,UCB3TAPE       TAPE DEVICE?
         BNE   TIOT61                  NO, JUMP
         BAS   R1,TIOT46IC
         DC    C'LTM',AL1(JFCBLTM)
         DC    C'BLP',AL1(JFCBLP)
         DC    C'SUL',AL1(JFCSUL)
         DC    C'AUL',AL1(JFCSUL+JFCBAL)
         DC    C'AL ',AL1(JFCBAL)
         DC    C'NSL',AL1(JFCNSL)
         DC    C'SL ',AL1(JFCSL)
         DC    C'NL ',AL1(JFCNL)
         DC    C'???',0H'0'
TIOT46TM TM    JFCBLTYP,*-*            TEST LABEL TYPE FLAGS
*--LOOP
TIOT46IC IC    R15,3(,R1)              PICK UP MASK FOR "TM"
         EX    R15,TIOT46TM
         BO    TIOT46X
         LA    R1,3+1(,R1)             BUMP TABLE PTR
         CLI   0(R1),C'?'              END OF TABLE?
         BNE   TIOT46IC                NEXT TABLE ENTRY
*--ENDLOOP
TIOT46X  LH    R0,JFCBFLSQ             PICK UP FILE SEQUENCE NUMBER
         LTR   R0,R0                   LABEL=0?
         BNZ   TIOT46Y                 NO, JUMP
         LA    R0,0001                 YES, CHANGE TO LABEL=1
TIOT46Y  STRING ((R0),,L),',',((R1),3),INTO=WKCELL1
         B     TIOT61        JUMP AROUND JES2 CODE              GP04115
         SPACE 1                                                GP04115
*---------------------------------------------------------------------*
*   GET JES I/O COUNTS BY DD NAME FROM SDB CHAIN                      *
*---------------------------------------------------------------------*
TIOTJES2 LA    R7,=XL32'00'  FAKE UCB                           GP04115
         ICM   R14,15,@SDB   HAVE AN SDB CHAIN?                 GP04115
         BZ    TIOT61        NO                                 GP04115
         USING SDB,R14                                          GP04115
TIOJLOOP CLC   SDBDDNM,TIOEDDNM  FOUND MATCH ?                  GP04115
         BE    TIOJHAVE      YES                                GP04115
         ICM   R14,15,SDBSDB GET NEXT SDB                       GP04115
         BNZ   TIOJLOOP                                         GP04115
         B     TIOT61        NO MORE                            GP04115
         SPACE 1                                                GP04115
TIOJHAVE TM    SDBDRECD+L'SDBDRECD-1,X'0C' EVER USED ?          GP04115
         BNO   TIOT61        APPARENTLY NOT                     GP04115
         MVC   DB,SDBDRECD   COPY TO ALIGNED FIELD              GP04115
         CVB   R1,DB         GET COUNT                          GP04115
         LA    R0,4          PROCESS FOUR FLAGS                 GP04122
         LR    R15,R0        MAKE LOW NON-ZERO                  GP04122
         ICM   R15,8,SDBFLG4  LOAD IN/OUT FLAG                  GP04122
         SRL   R15,1         ADJUST FOR FIRST TIME              GP04122
         LA    R14,=C'OUTOUTIN SPL'                             GP04122
TIOJTPLP BXH   R15,R15,TIOJTPMV  MATCH                          GP04122
         LA    R14,3(,R14)   NEXT                               GP04122
         BCT   R0,TIOJTPLP                                      GP04122
         B     TIOT62        COUNT IT                           GP04115
TIOJTPMV MVC   STATUS,0(R14) IDENTIFY                           GP04122
         B     TIOT62        COUNT IT                           GP04115
         DROP  R14                                              GP04115
*
TIOT61   LA    R0,TIOENTRY             POINT TO CURRENT TIOT ENTRY
         SL    R0,TCBTIO               CHANGE ADDRESS TO OFFSET
         BAL   R14,EXCP00          <== GET EXCP COUNT IN R1
TIOT62   STRING ((R1),,R5B),INTO=WKCELL2                        GP04115
         CL    R1,=F'100000'           OVER 100K?
         BL    TIOT64                  NO, JUMP
         SLR   R0,R0                   YES, DIVIDE BY 1000
         LA    R1,500(,R1)             YES, DIVIDE BY 1000
         D     R0,=F'1000'             YES, DIVIDE BY 1000
         STRING ((R1),,R4B),'K',INTO=WKCELL2
*
TIOT64   LTR   R7,R7                   DO WE HAVE AN UCB?
         BNZ   TIOT_PRT                YES, JUMP
         LA    R7,BLANKS               NO, POINT TO A DUMMY UCB
TIOT_PRT BAS   R14,OUTLINE         <== LOCATE OUTPUT RECORD     GP04115
         STRING $$$,TIOEDDNM,$$$,STATUS,$$$,                          XX
               VOLSER,$$$,(WKCELL2,5),$$$,JFCBDSNM,$$$,               XX
               UNITNAME,$$$,(UCBCHAN,2,X),$$$,WKCELL1,$$$,            XX
               INTO=((R1),121)
*
TIOT_NXT SLR   R0,R0                   PREPARE IC
         IC    R0,TIOELNGH             LOAD LENGTH OF CURRENT ENTRY
         ALR   R5,R0                   BUMP UP TO NEXT ENTRY
         CLI   TIOELNGH,0              IS THIS THE END?
         BNZ   TIOT_GET                NOT YET, LOOP THROUGH TIOT
*ENDLOOP
         BAS   R14,OUTLINE             WRITE BLANK LINE         GP04115
         MVI   1(R1),C'-'              LINE OF DASHES
         MVC   2(110,R1),1(R1)         LINE OF DASHES           GP04122
         BAS   R14,OUTLINE             WRITE BLANK LINE         GP04115
         TM    PROFLAGS,PFTSO  UNDER TSO?                       GP04115
         BNZ   GOBACK        YES; ALL DONE                      GP04115
         CLOSE MF=(E,DYNOPLST)         CLOSE SYSCOUNT (IF NECESSARY)
         LA    R2,DYNOPDCB                                      GP04115
         BAS   R14,CLRPOOL   FREEPOOL                           GP04115
*
GOBACK   SERVTERM ,          CLOSE AND FREE ALL                 GP04115
         PGMEXIT RC=RETCODE  RETURN WITH CODE                   GP04115
         SPACE 2                                                GP04115
*----------------------------------------------------------------------
OUTLINE  STM   R14,R2,DB     SAVE SOME                          GP12045
         TM    PROFLAGS,PFTSO  UNDER TSO ?                      GP04115
         BZ    OUTLINEP                AMODE24                  GP04115
         LA    R2,TPUTLINE   USE OUR LINE INSTEAD OF QSAM BUFF  GP04115
         TM    PROFLAGS,PFTSO1  FIRST TIME?                     GP04115
         BZ    OUTLINE1      YES; SKIP TPUT                     GP04115
         TPUT  1(R2),79      TRUNCATE A BIT                     GP04115
OUTLINE1 OI    PROFLAGS,PFTSO1                                  GP04115
         B     OUTLINEX                                         GP04115
OUTLINEP PUT   DYNOPDCB                                         GP04115
OUTLINEX MVI   0(R2),C' '              ALL SPACES               GP04115
         MVC   1(120,R2),0(R2)         ALL SPACES               GP04115
         ST    R1,DB+3*4     PASS RECORD ADDRESS BACK IN R1     GP12045
         LM    R14,R2,DB     RESTORE ALL BUT R1 - BUFFER        GP12045
         BR    R14           RETURN                             GP12045
         SPACE 2                                                GP04115
*----------------------------------------------------------------------
EXCP00   LTCB  R1            MY TCB                             GP04115
         L     R1,TCBTCT-TCB(,R1)      TCBTCT
         L     R1,TCTIOTBL-SMFTCT(,R1) START OF I/O MEASUREMENT TABLE
         LA    R1,TCTIODSP-TCTTIOT(,R1) FIRST DD ENTRY
         USING TCTDCBTD,R1
*LOOP
EXCP11   CL    R0,TCTDCBTD             SAME TIOT OFFSET?
         BE    EXCP21                  YES, EXIT LOOP
         LA    R1,TCTDCBLE             NEXT LOOKUP TABLE ENTRY
         ICM   R15,B'1111',TCTDCBLE    END OF TABLE ?
         BNZ   EXCP11                  NOT YET, TRY NEXT DD ENTRY
*ENDLOOP
         SLR   R0,R0                   EXCP COUNT IS ZERO
         B     EXCP99
*
EXCP21   L     R1,TCTIOTSD             OFFSET IN I/O MEASURE. TABLE
         LTCB  R15           MY TCB                             GP04115
         L     R15,TCBTCT-TCB(,R15)    MY TCT
         A     R1,TCTIOTBL-SMFTCT(,R15) START OF I/O MEASUREMENT TABLE
         USING TCTDDENT,R1
         ICM   R0,B'1111',TCTDCTR      EXCP COUNT IN R0
EXCP99   LR    R1,R0                   EXCP COUNT IN R1 TOO
         BR    R14
         DROP  R1
         SPACE 2                                                GP04115
*----------------------------------------------------------------------
*        CONVERT UCBTYP TO UNITNAME
*----------------------------------------------------------------------
GETUNIT  ST    R14,TENWORDS+32         SAVE R14
         MVI   TENWORDS+20,X'01'       RETURN A LOOK-UP VALUE (BIT7)
         MVI   TENWORDS+21,X'00'       CLEAR UNUSED BYTE
         LA    R14,UNITNAME            UNITNAME+DEVTYPE
         LA    R15,TENWORDS+20         FLAGS
         STM   R14,R15,TENWORDS
         OI    TENWORDS+4,X'80'        END-OF-LIST FLAG
         LA    R1,TENWORDS             PARM LIST ADDRESS
         L     R14,CVTPTR              POINT TO CVT
         L     R14,CVTLINK-CVTMAP(,R14) POINT TO LINKLIST DCB
         LINK  SF=(E,DYNEB4UV),DCB=(R14)   GET UNIT NAME        GP04115
         LTR   R15,R15
         BNZ   GETUNIT6                BAD RETURN CODE, QUIT
         MVI   TENWORDS+20,X'20'       RETURN A UNIT NAME (BIT2)
         LA    R1,TENWORDS             PARM LIST ADDRESS
         LINK  SF=(E,DYNEB4UV)         GET UNIT NAME            GP04115
         LTR   R15,R15
         BZ    GETUNIT9                GOOD RETURN CODE, GOBACK
GETUNIT6 MVC   UNITNAME,BLANKS         CONVERSION DID NOT WORK
GETUNIT9 L     R14,TENWORDS+32         RESTORE R14
         BR    R14
         SPACE 2                                                GP04115
*----------------------------------------------------------------------
*    FREEPOOL THE HARD WAY                                      GP04115
*      DCB (R2)                                                       *
*----------------------------------------------------------------------
         PUSH  USING                                            GP04115
CLRPOOL  BAKR  R14,0                                            GP04115
         USING IHADCB,R2     MAP THE DCB                        GP04115
         LA    R5,1          MAKE A CONSTANT                    GP04115
         SLR   R4,R4                                            GP04115
         ICM   R4,7,DCBBUFCA  GET THE POOL ADDRESS              GP04115
         BZ    CLRPOOLX      NONE?                              GP04115
         CR    R4,R5         GREATER?                           GP04115
         BNH   CLRPOOLX      NO; NOTHING TO DO                  GP04115
         FREEPOOL (R2)       FREE THE POOL FOR THIS DCB         GP04115
CLRPOOLX STCM  R5,7,DCBBUFCA  AND MAKE SURE IT SAYS SO          GP04115
         PR    ,                                                GP04115
         POP   USING                                            GP04115
         SPACE 2                                                GP04115
*----------------------------------- WORK-AREA ----------------------
BLANKS   DC    CL40' '
$$$      DC    C' | '
         PRINT &PRTSYS                                          GP04115
S99TXT01 DC    AL2(X'01',1,8),CL8'SYSCOUNT '  DDNAME            GP04115
S99TXT18 DC    AL2(X'18',0)                   SYSOUT=*
         SPACE 2                                                GP04115
SAWE     DSECT ,             DYNAMIC SAVE AND WORK AREA         GP04115
DB       DS    D                                                GP04115
WKCELL1  DS    D                                                GP04115
WKCELL2  DS    D
@SDB     DS    A                                                GP04115
         SERVDEFS ,          SERVICE AREA                       GP04115
         SPACE 1                                                GP04115
DYNMACS  DYNMAC PFX=DYN                                         GP04115
DYNMSIZE EQU   *-DYNMACS                                        GP04115
         SPACE 1                                                GP04115
PROFLAGS DS    X             PROCESSING FLAGS                   GP04115
PFJES2   EQU   X'80'           UNDER JES2                       GP04115
PFTSO    EQU   X'40'           TSO TASK                         GP04115
PFTSO1   EQU   X'20'           OUTLINE PRIMED                   GP04115
STATUS   DS    C'OLD'
VOLSER   DS    CL6
UNITNAME DS    CL8,XL4
SWAREQL1 SWAREQ MF=L                   WORK AREA FOR SWAREQ
TENWORDS DS    10F
TPUTLINE DS    CL121         FOR TSO                            GP04115
SAWEEND  EQU   *                                                GP04115
         SPACE 2                                                GP04115
*-------------------------------------------------------------------
*        DSECTS FOR MVS SYSTEM CONTROL BLOCKS
*               SOME OF THESE MACRO INSTRUCTION WILL BE FOUND
*               IN SYS1.MODGEN INSTEAD OF SYS1.MACLIB.
*-------------------------------------------------------------------
         DCBD  DSORG=PS                                         GP04115
         IKJTCB LIST=NO    TCB MAPPING       TASK CONTROL BLOCK
         IEFTCT ,          TCT MAPPING       TIMING CONTROL TABLE
TIOT     DSECT ,                                                GP04115
         IEFTIOT1 ,        TIOT MAPPING      TASK INPUT-OUTPUT TABLE
         IKJRB ,           PRB DSECT         PROGRAM REQUEST BLOCK
         IHACDE ,          CDE DSECT         CONTENTS DIRECTORY ENTRY
         IEFZB505 LOCEPAX=YES          SWAREQ
         IEFJFCBN ,                    JOB FILE CONTROL BLOCK   GP04115
         IEFJESCT ,                    JES COMMUNICATION TABLE  GP04115
         IEFJSSIB ,          JES2 SUBSYSTEM INFORMATION BLOCK   GP04115
         IEZJSCB ,                     JOB STEP CONTROL BLOCK   GP04115
         CVT   DSECT=YES
         IHAPSA ,                                               GP04115
         IEFUCBOB LIST=NO,PREFIX=NO    UCB DSECT
         IHAASCB ,                     ADDRESS SPACE CONTROL BLOCK
         IRAOUCB ,                     SRM PARMS                GP04115
SHOWTCT  CSECT ,                                                GP04115
         STRING GENERATE
SHOWTCT  CSECT ,                                                GP04115
PATMACS  DYNMAC PFX=PAT                                         GP04115
         PUSH  PRINT                                            GP04086
         PRINT NOGEN                                            GP04086
         $MODULE TITLE='S H O W T C T  ***  JES2 BLOCKS',              *
               ($PDDB,GEN),        PERIPHERAL DATA DEFINITION BLOCK    C
               ($SDB,GEN),         SPOOL DATA BLOCK                    C
               ($SJB,GEN),         JOB BLOCK                           C
               ($TQE,NOGEN)        TIMER                        GP04115
         POP   PRINT                                            GP04115
         END   ,
