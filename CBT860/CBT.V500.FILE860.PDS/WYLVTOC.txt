WVOL     TITLE 'WYLBUR/TSO SELECTIVE VTOC DISPLAY AND DSN CATLG/RENAME'
         PUNCH '  ORDER WYLVTOC(P) '   EASIER DUMP READING      GP12061
         PUNCH ' ALIAS WYLCATLG '      DEFINE EXTRA ENTRY       GP12061
         SPACE 1
         COPY  OPTIONGB
         GBLB  &LOCO         QUICK BRANCH AROUND LOCAL STUFF    GP12061
         SPACE 1
         SYSPARM LIST=YES
&LOCO    SETB  ('&LOCAL' EQ 'CCSI')                             GP12061
         SPACE 2
***********************************************************************
*                                                                     *
*        COPYRIGHT 1981  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003,2012  EXPERT SYSTEM PROGRAMMING               *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                e-mail: gerhard@postpischil.com                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1
*---------------------------------------------------------------------*
*   THIS PROGRAM IS BASED ON THE MAPDISK PROGRAM DISTRIBUTED BY       *
*   SHARE, INC. AND MEMBERS; BUT ONLY THE CONCEPT AND PARM OPTIONS    *
*   WERE RETAINED. MODIFICATIONS DUE TO R. OXLEY AND G. P.            *
*---------------------------------------------------------------------*
         EJECT ,                                                 79251
         PRINT &PRTSOR
WYLVTOC  PGMHEAD BIGZERO,BASE=(R10,R11,R12),PARM=R6,    ADDED ON 81236 *
               ENTRY=(WYLCATLG),ENTNO=PROP                      GP12050
         SPACE 1
*---------------------------------------------------------------------*
*   ENTRY WYLVTOC IS INTENDED TO PROVIDE RAPID AND INEXPENSIVE        *
*   DATA SET SUMMARY INFORMATION.  QUICK RETRIEVAL IS PERFORMED       *
*   WITH BUFFERED FULL-TRACK EXCP.  MASK PROCESSING ASSUMES LOCAL     *
*   DATA SET NAMING CONVENTION.                                       *
*                                                                     *
*   WYLVTOC PARM OPTIONS :  DEFAULT IS PDS,SUBA (local)               *
*   WYLVTOC PARM OPTIONS :  DEFAULT IS PDS      (non-local)           *
*                                                                84086*
*   SEARCH MASK - (non-LOCAL)  PARM='opt,prefix'                      *
*         prefix             - 1-44 bytes                             *
*                                                                     *
*   OUTPUT MODE -  VTOC     PDS    DIR     SNAP                       *
*         VTOC PRODUCES A ONE LINE SUMMARY PER DATA SET               *
*         PDS  ADDITIONALLY COUNTS DIRECTORY BLOCKS                   *
*         DIR  ADDITIONALLY LISTS MEMBER NAMES                        *
*         SNAP LISTS FIRST FIVE RECORDS OF EACH DATA SET/MEMBER       *
*---------------------------------------------------------------------*
         SPACE 1                                                 82137
*---------------------------------------------------------------------*
*   ENTRY WYLCATLG IS INTENDED TO PROVIDE RAPID CATALOGING OF         *
*   ALL OR SELECTED DATA SETS ON ONE OR MORE PACKS.  NOTE THAT        *
*(PRIOR) CVOL CONNECT POINTERS MUST BE ESTABLISHED BY THE USER.  82137*
*
* (MVS)  NO CATALOG DD CARDS ARE DESIRED                        GP12061
* (MVT)  THE CATALOG (CVOL) PACK IS SPECIFIED VIA A 'CATXXX DD'. 82137*
*
*   OPTIONAL SYSIN CONTROL CARDS MAY BE SUPPLIED TO LIMIT SEARCH      *
*   BY DATA SET NAME MASK, AND TO RENAME SELECTED DATA SETS PRIOR     *
*   TO CATALOGUING THEM.                                         82137*
*
*   IN NON-LOCAL MODE, A PREFIX (INDEX) MASK MUST BE SUPPLIED, AND
*   MUST CONTAIN AT LEAST ONE INDEX LEVEL + PERIOD
*
*                                                                82137*
SYNSIZE  EQU   4096          SIZE OF RENAME OPTION LIST          82137
*                                                                82137*
*   WYLCATLG PARM OPTIONS :  DEFAULT IS ADD,SUBA                 82137*
*                                                                82137*
*   PROCESS MODE -  <TEST,>  ADD|MOD|NULL  OR WYL  OR NEW|REP         *
*         TEST, (OPTIONAL) RUNS IN TEST MODE AND ONLY LISTS      82137*
*           SIMULATED PROCESSING ACTIONS. NO CATALOGING          82137*
*           IS DONE; SOME CATALOG FAILURES WILL NOT BE DETECTED.      *
*         ADD, MOD,  OR NOTHING ONLY ADDS CATALOG ENTRIES        82137*
*         NEW, REP  REPLACE EXISTING ENTRIES AND ADD NEW ONES         *
* (local) WYL  REPLACES ONLY OLD ENTRIES POINTING TO WYLXXX      82137*
*                                                                     *
*   PRIVILEGED SECOND OPERAND: A=ACCT OR ACCT=ACCT(...)          84086*
*      CAUSES SPECIFIED ACCOUNT TO BE USED INSTEAD.              84086*
*      AFTER "ACCT" IS PROCESSED, REST OF FIELD IS               84086*
*      TREATED LIKE UNPRIVILEGED INPUT (SEARCH MASK).            84086*
*                                                                84086*
*   SEARCH MASK - (local only)                                        *
*         SUBA.USERNAME.MASK - 5 BYTES MINIMUM                        *
*         USERNAME.MASK      - 9 BYTES, ANY SUBACCOUNT                *
*         *ACCT              - ALL DATA SETS PER ACCOUNT              *
*         *VTOC              - ALL DATA SETS (TREATED AS *ACCT ON     *
*                                 SHARED STORAGE PACKS)               *
*                                                                84086*
*   SEARCH MASK - (non-LOCAL)                                         *
*         prefix             - 1-44 bytes                             *
*                                                                     *
*   MACROS - LOCAL SYSTEM MACRO LIBRARY REQUIRED                      *
*   EXTERNAL REFERENCES - @SERVICE, @PRINTER, @VOLREAD ROUTINES       *
*   REFRESHABLE, RE-ENTRANT, AUTHORIZED                               *
*                                                                     *
*   DISKS TO BE SEARCHED ARE SPECIFIED WITH ANY DDNAME OTHER          *
*     THAN ONES STARTING WITH JOB, STEP, PGM OR SYS                   *
*   THE PRESENCE OF A VOLMOUNT DD CARD CAUSES AUTOMATIC UCB LOOP      *
*   MODE FOR (LOCAL) PERMANENTLY RESIDENT WYLBUR VOLUMES.        82137*
*---------------------------------------------------------------------*
         SPACE 1
         SERVINIT ,          LOAD THE SERVICE ROUTINE
         SERVLOAD @PRINTER,@PARSER,SUBVERB  SUPPORT ROUTINES    GP12061
         PRTOPEN SYSPRINT,OPT=(ABEND,DUMMY),DEV=1  OPEN PRINT FILE
         PRTOPEN SUMPRINT,OPT=(NOWTO,DUMMY),DEV=2  OPTIONAL     GP12061
         VOLREAD LOAD        LOAD THE VOLUME READ ROUTINE
         MVI   BLANKONE,C' '   INITIALIZE BLANKS
         MVI   LINECARD+1,LINECARN-LINECARD  MAKE V HEADER       82137
         MVI   LINECARD+4,C' '                                   82137
         MVC   LINECARD+5(LINECARN-LINECARD-5),LINECARD+4        82137
         MVI   CARDLINE-1,C''''                                  82137
         MVI   CARDLINE+L'CARDLINE,C''''                         82137
         MVC   CVOL,CARDLINE                                     82137
         MVI   MASKLEN,X'FF' DISABLE MASK TEST                  GP12061
         SERVCALL ACGET,DSNACCT  GET CURRENT ACCOUNT AND PRIVILEGES
         AIF   (NOT &LOCO).NLOC1                                GP12061
         LA    R0,VAASYS+VAASUP  FAKE ME OUT                    GP98109
         STC   R0,ACCTPRIV   SAVE PRIVILEGES
         SERVCALL ACTST,DSNACCT  GET INTERNAL ACCOUNT NUMBER
         SRDL  R0,32+16      SHIFT TO R1
         SERVCALL ACCNX      GET EXTERNAL FORM
         STCM  R0,15,DSNACCT   SAVE EXTERNAL ACCOUNT NUMBER
         NI    ACCTPRIV,VAASYS+VAASUP  DELETE EXTRANEOUS BITS
         OI    ACCTPRIV,VAASYS+VAASUP  ***HERCULES/NON-LOCAL*** GP12061
         MVC   LIBCLC,LIBPAT MAKE PATTERN CLC FOR LIBPAK        GP12061
         MVC   WYLCLC,WYLPAT MAKE PATTERN FOR WYLBUR            GP12061
.NLOC1   MVC   DSNMASK+1(L'DSNMASK-1),BLANKS   CLEAR MASK       GP12061
         MVI   DSNMASK,C'*'  BUT SET FOR EVERYTHING             GP12061
         MVC   TSOCLC,TSOPAT MAKE PATTERN FOR TSO/GENERIC       GP12061
         TM    PROP,PCATLG   SET FOR WYLCATLG ?                 GP12050
         BZ    INITVTOC        NO                               GP12050
         OI    ACCTPRIV,VAASYS+VAASUP  ***HERCULES/NON-LOCAL*** GP12061
         NI    PROP,255-(PDIR+PPDS+PSNAP+PDSCB)  NO PDS ACCESS   94281
         TM    ACCTPRIV,VAASYS+VAASUP  AUTHORIZED USER ?         82137
         BNZ   INITVTOC      YES                                 82137
         PRTV  MSGUNAUT,DEV=(1,2)  FAIL                          84086
         AIF   (NOT &LOCO).NCSYS                                GP12061
         PRTV  SYNSYSMG,DEV=(1,2)  CONTACT SYSTEMS               82137
.NCSYS   B     JOBABEND                                          82137
INITVTOC PRTLIST TITLE,TITLE=1,DEV=(1,2)  PRELIMINARY TITLE      82137
         SPACE 1
         PARMLOAD R8,PTR=R6                                     GP12061
         LTR   R9,R9                                            GP12061
         BZ    PARMDFLT      NONE; USE DEFAULT ACCT/SUBACCT     GP12061
         PARSE (R8),(R9),OPT=COMMA                              GP12061
         LTR   R15,R15                                          GP12061
         BZ    GOTPARMS                                         GP12061
         STM   R15,R0,DB                                        GP12061
         PRTDATA 'PARM parsing failed, RC=',(DB,4,I,DEB),DEV=(1,2)
         SERVTERM ,                                             GP12061
         ABEND 100,DUMP                                         GP12061
         SPACE 1                                                GP12061
GOTPARMS LA    R3,PAR@TABL   POINT TO RESULTS                   GP12061
         TM    PROP,PCATLG   WYLCATLG ?                         GP12061
         BNZ   PCATPARM        YES; SEPARATE PROCESSING         GP12061
         USING PRSDSECT,R3     DECLARE ENTRY MAPPING            GP12061
         ICM   R3,15,PRSLINK      ANY PARAMETERS AT ALL?        GP12061
         BZ    PARMDFLT             NO; USE DEFAULTS            GP12061
         ICM   R2,15,PRS#TEXT     NULL FIRST PARM ?             GP12061
         BZ    PARM1DON             YES; KEEP DEFAULT           GP12061
         SPACE 1                                                GP12061
         LA    R0,PRS$TEXT   POINT TO TEXT                      GP12061
         OC    PRS$TEXT(8),BLANKS  UPPER CASE                   GP12061
         LA    R1,RUNOPTP    GET KEYWORD TABLE POINTER          GP12061
         SUBCALL SUBVERB     DO TABLE LOOKUP                    GP12061
         LTR   R15,R15       DID IT WORK ?                      GP12061
         BNZ   PARM1SET      NOW LOOK FOR OPTIONAL VALUE        GP12061
         SPACE 1                                                GP12061
PARMBAD  PRTDATA 'INVALID PARM -',(PRS$TEXT,PAD,DEB),DEV=(1,2)  GP12061
         SERVTERM ,                                             GP12061
         ABEND 123,DUMP                                         GP12061
         SPACE 1                                                GP12061
         PARKEYBX RUNOPT                                        GP12061
RUNOPT   PARKEYFG VTOC,PROP,0,PDSCB,LEN=1    PRINT DSCB INFO    GP12061
RUNOPT2  PARKEYFG DSCB,PROP,0,PDSCB          PRINT DSCB INFO    GP12061
         PARKEYFG PDS,PROP,0,PPDS            PRINT MEMBER NAMES GP12061
         PARKEYFG DIRECTORY,PROP,0,PPDS+PDIR  MEMBER NAMES      GP12061
         PARKEYFG MEMBERS,PROP,0,PPDS+PDIR    MEMBER NAMES      GP12061
RUNOPTN  PARKEYFG SNAP,PROP,0,PSNAP+PPDS+PDIR 5 LINES DATA      GP12061
         SPACE 1                                                GP12061
PARM1SET OC    PROP,OFOFONN(R15)  SET PARM1 FLAG VALUE          GP12061
PARM1DON ICM   R3,15,PRSLINK      ANY PARAMETERS AT ALL?        GP12061
         BZ    PARM2NUL             NO; TRY FOR DEFAULT         GP12061
         ICM   R2,15,PRS#TEXT     NULL PARM ?                   GP12061
         BZ    PARM2NUL             YES; KEEP DEFAULT           GP12061
         SPACE 1                                                GP12061
         OC    PRS$TEXT,BLANKS  UPPER CASE                      GP12061
         LA    R9,PRS$TEXT   MAKE EASIER                        GP12061
         AIF   (NOT &LOCO).NLOC3                                GP12061
         LA    R1,2          MINIMUM LENGTH FOR A=
         CLC   =C'A=',PRS$TEXT  ACCOUNT REQUEST ?
         BE    PARMCRAC      YES; CHECK IT
         LA    R1,5          ELSE TRY FOR ACCT=
         CLC   =C'ACCT=',PRS$TEXT  IS IT ?
         BNE   PARMNACT      NO
PARMCRAC SR    R2,R1         RESIDUAL LENGTH                    GP12061
         BNP   PARM1DON      IGNORE NULL KEYWORD                GP12061
         AR    R9,R1          RESIDUAL TEXT                     GP12061
         CH    R2,=H'8'      ACCOUNT/SUBACCT LENGTH ?           GP12061
         BE    PARMASUB        YES                              GP12061
         CH    R2,=H'4'      ACCOUNT LENGTH ?                   GP12061
         BNE   BADPARM       NO; ERROR                          GP12061
PARMASUB MVC   DSNACCT(8),0(R9)  MOVE ACCOUNT AND BLANKS OR SUBACCT
         BCTR  R2,0          DECREMENT LENGTH FOR CLC           GP12061
         STC   R2,WYLCLC     WYLBUR COMPARES ACCT OR SUBACCT    GP12061
         SERVCALL FMTAC,DSNACCT,LEN=4   VALID ?
         BXH   R15,R15,BADPARM  NO
         SERVCALL ACTST,DSNACCT,LEN=4  GET BINARY EQUIVALENT
         BXH   R15,R15,BADPARM  NO
         SRDL  R0,32+16      PLACE INTO R1
         SERVCALL ACCNX      GET EXTERNAL FORM
         STCM  R0,15,DSNACCT  STASH IT BACK
         ST    R2,MASKLEN    PREVENT BYPASS OF CLC TESTS        GP12061
PARMCRAN TM    ACCTPRIV,VAASYS+VAASUP  AUTHORIZED USER ?
         BZ    PARMNAUT      NO; FAIL
         B     PARMDFLT      NOW GO TO CHECK SYSIN
.NLOC3   SPACE 1                                                GP12061
PARMNACT CH    R2,=AL2(L'DSNMASK)  OVER MAXIMUM LENGTH ?        GP12061
         BH    BADPARM                 ERROR IF SO              GP12061
         BCTR  R2,0          GET EXECUTE LENGTH                 GP12061
         EX    R2,EXMVCUID   MOVE TO MASK                       GP12061
         CLI   DSNMASK,C'*'  GENERAL MATCH?                     GP12061
         BE    PARMDFLT        YES; LENGTH SET TO MATCH ALL     GP12061
         ST    R2,MASKLEN    SET COMPARE LENGTH                 GP12061
         PRTDATA 'Data sets beginning with',(DSNMASK,DEB,PAD),'will be *
               processed',DEV=2                                 GP12061
         B     PARMDFLT      IGNORE OTHER PARMS                 GP12061
         SPACE 1                                                GP12061
PARM2NUL LTCB  R1                                               GP12061
         AIF   (NOT &LOCO).DEFALL      PROCESS ALL DATA SETS    GP12061
         L     R1,TCBTIO-TCB(,R1)                               GP12061
         LA    R9,0(,R1)     POINT TO JOB NAME                  GP12061
         LA    R2,8          MAXIMUM NAME LENGTH                GP12061
         LA    R14,0(R2,R9)  LAST BYTE +1                       GP12061
PARM2NUC BCTR  R14,0         BACK UP                            GP12061
         CLI   0(R14),C' '   TRAILING BLANK ?                   GP12061
         BNE   PARM2NUP        NO; END                          GP12061
         BCT   R2,PARM2NUC   DO AGAIN                           GP12061
PARM2NUP LA    R5,DSNMASK-1(R2)                                 GP12061
         MVI   0(R5),C'.'    COMPLETE INDEX                     GP12061
         CH    R2,=AL2(L'DSNMASK)  OVER MAXIMUM LENGTH ?        GP12061
         BH    BADPARM                 ERROR IF SO              GP12061
         BCTR  R2,0          GET EXECUTE LENGTH                 GP12061
         EX    R2,EXMVCUID   MOVE TO MASK                       GP12061
         ST    R2,MASKLEN    SET COMPARE LENGTH                 GP12061
         PRTDATA 'Data sets beginning with',(DSNMASK,DEB,PAD),'will be *
               processed',DEV=2                                 GP12061
.DEFALL  SPACE 1
PARMDFLT TM    PROP,PDSCB+PPDS+PDIR+PSNAP   ANY USER PARM?      GP12061
         BNZ   PARMDSCB        YES                              GP12061
         OI    PROP,PPDS+PDIR    SET DEFAULT                    GP12061
PARMDSCB OI    PROP,PDSCB              SET FOR OPTION FORMAT    GP12061
         B     PARMDONE      SET MASKS                          GP12061
EXMVCUID MVC   DSNMASK(0),0(R9)      MOVE USER ID               GP12061
         SPACE 1
PARMNAUT PRTV  MSGUNAUT,DEV=(1,2)   FAIL - NOT AUTHORIZED       GP12061
         AIF   (NOT &LOCO).SKIPSYS                              GP12061
         PRTV  SYNSYSMG,DEV=(1,2)      CONTACT SYSTEMS
*        B     JOBABEND
.SKIPSYS SPACE 1
JOBABEND SERVTERM ,          CLOSE AND FREE EVERYTHING
         ABEND 666
         SPACE 1
OPTSHOW  FDPRT 'Options for current run: process',NL,PAD        GP12061
         FDTM  PROP,PCATLG,BO=OPTSHCAT                          GP12061
         FDFLAG PROP,1,TABLE=DCPROP,LEN=96,SEP=C',',SPACE=1     GP12061
         FD    *END                                             GP12061
OPTSHCAT FDFLAG POPS,1,TABLE=DCPOPS,LEN=96,SEP=C',',SPACE=1     GP12061
         FD    *END                                             GP12061
         SPACE 1
DCPROP   FLGTAB PDSCB,'DSCB1/3',MLEN=1                          GP12061
         FLGTAB PPDS,'PDS counts'                               GP12061
         FLGTAB PDIR,'PDS members'                              GP12061
         FLGTAB PSNAP,'5 lines of data'                         GP12061
         FLGTAB *END                                            GP12061
         SPACE 1
DCPOPS   FLGTAB PTEST,'Test mode',MLEN=1                        GP12061
         FLGTAB PREP,'replace'                                  GP12061
         FLGTAB PWYLREP,'on WYL packs only'                     GP12061
         FLGTAB *END                                            GP12061
         SPACE 1
         USING PRSDSECT,R3     DECLARE ENTRY MAPPING            GP12061
PCATPARM ICM   R3,15,PRSLINK      ANY PARAMETERS AT ALL?        GP12061
         BZ    PCATDFLT             NO; USE DEFAULTS            GP12061
         ICM   R2,15,PRS#TEXT     NULL PARM?                    GP12061
         BZ    PCATPARM             YES; IGNORE                 GP12061
         SPACE 1                                                GP12061
         LA    R0,PRS$TEXT   POINT TO TEXT                      GP12061
         OC    PRS$TEXT(8),BLANKS  UPPER CASE                   GP12061
         LA    R1,CATOPTP    GET KEYWORD TABLE POINTER          GP12061
         SUBCALL SUBVERB     DO TABLE LOOKUP                    GP12061
         LTR   R15,R15       DID IT WORK ?                      GP12061
         BZ    PCAT2SET        NO; MUST BE INDEX MASK ?         GP12061
PCAT1SET OC    POPS,OFOFONN(R15)  SET FLAG VALUE                GP12061
         B     PCATPARM             AND LOOK FOR MORE           GP12061
         SPACE 1                                                GP12061
         PARKEYBX CATOPT                                        GP12061
CATOPT   PARKEYFG ADD,POPS,0,0,LEN=1   ADD IS DEFAULT           GP12061
CATOPT2  PARKEYFG MODIFY,POPS,0,0      DITTO FOR MODIFY         GP12061
         PARKEYFG REPLACE,POPS,0,PREP  REPLACE                  GP12061
         PARKEYFG NEW,POPS,0,PREP      REPLACE                  GP12061
         PARKEYFG WYLBUR,POPS,0,PREP+PWYLREP ON WYLBUR PACKS    GP12061
CATOPTN  PARKEYFG TEST,POPS,0,PTEST    TEST MODE                GP12061
         SPACE 1                                                GP12061
PCAT2SET CH    R2,=AL2(L'DSNMASK)  OVER MAXIMUM LENGTH ?        GP12061
         BH    BADPARM                 ERROR IF SO              GP12061
         LA    R9,PRS$TEXT   POINT TO TEXT                      GP12061
         BCTR  R2,0          GET EXECUTE LENGTH                 GP12061
         EX    R2,EXMVCUID   MOVE TO MASK                       GP12061
         ST    R2,MASKLEN    SET COMPARE LENGTH                 GP12061
PCATDFLT CLI   DSNMASK,C'*'  GENERAL MATCH?                     GP12061
         BE    PCATPERR        YES; WILL FAIL                   GP12061
         TM    MASKLEN,X'80' MASK LENGTH SET ?                  GP12061
         BZ    PCATPRM                                          GP12061
PCATPERR PRTDATA 'PARM is missing high index - run aborted',DEV=(1,2)
         B     JOBABEND                                         GP12061
         SPACE 1
PCATPRM  PRTDATA 'Data sets beginning with',(DSNMASK,DEB,PAD),'will be *
               processed',DEV=2                                 GP12061
         OI    PROP,PDSCB    PROCESS DSCBS                      GP12061
         ZI    PROP,PDIR+PPDS+PSNAP  BUT NOTHING ELSE           GP12061
         SPACE 1
* (MVT)  FOR WYLCATLG/RENAME, FIND CATALOG DD CARD               82137
* (MVT)        DD DUMMY REQUESTS USE OF THE SYSTEM CATALOG       82137
*                                                                82137
         SPACE 1
PARMDONE PRTLIST OPTSHOW,DEV=2       RUN OPTIONS                GP12061
         MVC   DSNACCN,DSNACCT  MOVE EXTERNAL ACCOUNT NUMBER
         CLI   DSNACCN,C'Z'  SPECIAL ?
         BNE   *+8           NO
         MVI   DSNACCN,C'0'
         OI    DSNACCN,C'0'  MAKE INTERNAL FORMAT
         MVC   WYLMASK+4(8),DSNACCT    COMPLETE WYLBUR COMPARE  GP12061
         MVC   LIBMASK(4),DSNACCT      COMPLETE LIBPAK COMPARE  GP12061
         SPACE 1
*   LOOK FOR SYSIN INCLUDE/EXCLUDE/RENAME(WYLCATLG ONLY) CONTROL CARDS
*     RETURNS TO LABEL INITDONE OR JOBABEND
*
         B     PARMLOOK                                          82137
INITDONE TM    PROP,PDIR+PPDS+PSNAP    NEED DATA SET ACCESS?
         BZ    INITAUTH                                          94281
         TESTAUTH FCTN=1                                         94281
         BXLE  R15,R15,INITAUTH                                  94281
         NI    PROP,255-PDIR-PPDS-PSNAP                          94281
         PRTF  '***** PROGRAM NOT AUTHORIZED - DATA AND DIRECTORIES NOT*
                ACCESSED *****',CC=NO,DEV=(1,2)                  94281
         PRTSPACE 1                                              94281
INITAUTH MVC   DSNACCN,DSNACCT  COPY ACCOUNT                     85252
         AIF   ('&LOCAL' NE 'CCSI').NOINT                        94281
         CLI   DSNACCN,C'Z'  INTERNAL ?                          85252
         BNE   *+8           NO                                  85252
         MVI   DSNACCN,C'0'  MAKE INTERNAL INTERNAL              85252
         OI    DSNACCN,C'0'  MAKE INTERNAL FORMAT                85252
.NOINT   TM    PROP,PCATLG   WYLCATLG ?                          82137
         BZ    INITTIOT      NO; PROCESS NORMALLY                82137
         AIF   (&MVS).NOCVOL1                                   GP12061
LOOPTCAT L     R1,ADDRTIOT   GET PRIOR TIOT ADDRESS              82137
         SERVCALL TIOLK      LOOK FOR PLAIN TIOT ENTRIES         82137
         ST    R0,ADDRTIOT   SAVE RETURN FOR NEXT TIME           82137
         LTR   R2,R0         ANY FOUND ?                         82137
         BZ    ENDTCAT       NO MORE; CHECK                      82137
         CLI   0(R2),20      STANDARD LENGTH ?                  GP12061
         BNE   LOOPTCAT        NO; GET ANOTHER                  GP12061
         CLI   20(R2),0      LAST ENTRY ?                       GP12061
         BE    LOOPACCP        YES; SKIP CONCATENATION TEST     GP12061
         CLI   4+20(R2),C' ' CONCATENATION ?                    GP12061
         BE    LOOPTCAT        NO; GET ANOTHER                  GP12061
LOOPACCP CLC   =C'CAT',4(R2)   CAT--- DD NAME ?                  82137
         BNE   LOOPTCAT                                          82137
         CLI   CVOL,C' '     FOUND PREVIOUSLY ?                  82137
         BNE   DUPCATDD      YES; ERROR                          82137
         MVI   CVOL,C'*'     INDICATE SYSRES OPTION              82137
         ICM   R4,7,17(R2)   GET UCB ADDRESS                     82137
         BZ    LOOPTCAT      DUMMY; ALL SET FOR SYSRES           82137
         CLI   UCBTBYT3-UCBOB(R4),UCB3DACC  DIRECT ACCESS ?      82137
         BNE   BADCATDD      NO; NOT USABLE                      82137
         MVC   CVOL,UCBVOLI-UCBOB(R4)  SAVE VOLUME SERIAL        82137
         B     LOOPTCAT      TRY FOR ANOTHER                     82137
DUPCATDD PRTF  =C'DUPLICATE CATALOG DD CARDS FOUND',32,CC=NO,DEV=(1,2)
         B     JOBABEND                                          82137
BADCATDD PRTF =C'CATALOG DD CARD MISSING OR INVALID',34,CC=NO,DEV=(1,2)
         B     JOBABEND                                          82137
         SPACE 1
ENDTCAT  CLI   CVOL,C' '     CVOL FOUND ?                        82137
         BE    BADCATDD      NO; FAIL                            82137
         AGO   .CMCVOL1      NO CVOL REQUIRED IN MVS            GP12061
.NOCVOL1 MVI   CVOL,C'*'     INDICATE SYSRES OPTION              82137
.CMCVOL1 MVI   CATLIST+1,1   SET NUMBER OF VOLUMES TO BE CATALOGED
*   BUILD LISTS FOR NEW (OR SAME) DATA SET NAME
         LA    R15,DS1DSNAM  POINT TO NEW DATA SET NAME         GP12061
         SLR   R0,R0         CLEAR CVOL                          82137
         LA    R1,CATLIST    POINT TO CATALOG REQUEST LIST       82137
         LA    R2,PATSCAT    POINT TO FLAGS; NO CVOL             82137
         CLI   CVOL,C'*'     CVOL ?                              82137
         BE    ENDTCAT2      NO; OK AS IS                        82137
         LA    R0,CVOL       POINT TO CVOL                       82137
         LA    R2,PATCCAT    POINT TO FLAGS WITH CVOL            82137
ENDTCAT2 L     R14,PATSCAT-PATSCAT(,R2)  GET CATBX FLAGS         82137
         STM   R14,R1,DOCAT  MAKE CATBX REQUEST                  82137
         L     R14,PATSRCT-PATSCAT(,R2)  GET RECATLG FLAGS       82137
         STM   R14,R1,RECAT  MAKE RECATALOG REQUEST              82137
*   BUILD LISTS FOR OLD (OR SAME) DATA SET NAME
         LA    R15,DSNAME    POINT TO OLD DATA SET NAME         GP12061
         L     R14,PATSLOC-PATSCAT(,R2)  GET LOCATE FLAGS        82137
         LA    R1,CATWORK    POINT TO WORK AREA                  82137
         STM   R14,R1,LOOCAT  MAKE LOCATE REQUEST                82137
         L     R14,PATSUNC-PATSCAT(,R2)  GET UNCATALOG FLAGS     82137
         STM   R14,R0,UNCAT  MAKE UNCATALOG REQUEST              82137
*   BUILD LISTS FOR OLD AND NEW - RENAME
         L     R14,PATSREN-PATSCAT(,R2)  GET RENAME REQUEST      82137
         LA    R0,DS1DSNAM   POINT TO NEW NAME                   82137
         LA    R1,CATLIST    POINT TO VOLUME LIST                82137
         STM   R14,R1,RENLIST  MAKE RENAME REQUEST               82137
         SPACE 1                                                 82137
INITTIOT SERVCALL TIODD,=CL8'VOLMOUNT'  DYNAMIC MOUNTING ?
         LTR   R0,R0         ANY DD FOUND ?
         BZ    LOOKTIOT      NO; SKIP AUTOMATIC MODE
         OI    PROP,PAUTO    SET FOR UCB LOOP MODE
         B     AUTOLOOK      START UCB SCAN
         SPACE 1
LOOPTIOT VOLREAD TCLOSE      CLOSE PRIOR VOLUME
         TM    PROP,PAUTO    ARE WE IN UCB LOOKUP MODE ?
         BZ    LOOKTIOT      NO; GET NEXT TIOT ENTRY
AUTOLOOK L     R1,STARUCBA   GET FIRST/NEXT UCB ADDRESS          82137
         SERVCALL UCBUM,(R1)   GET NEXT UCB
         BXH   R15,R15,AUTOEND  ERROR                           GP02245
         LTR   R4,R0         WAS A UCB FOUND ?
         BZ    AUTOEND       NO; END OF CHAIN
         STCM  R4,7,STARUCBA+1  SAVE IT                          82137
         USING UCBOB,R4      DECLARE IT
         CLI   UCBTBYT3,UCB3DACC  DASD DEVICE ?
         BNE   AUTOLOOK      NO; TRY AGAIN
         TM    UCBSTAT,UCBONLI  ON-LINE ?
         BZ    AUTOLOOK      NO
         TM    UCBSTAT,UCBCHGS+UCBUNLD  COMING OFF ?
         BNZ   AUTOLOOK      EITHER; SKIP IT
         ICM   R0,3,UCBVOLI  DOS OR BAD VTOC ?                  GP13227
         BZ    AUTOLOOK        YES; IGNORE IT                   GP13227
         SERVCALL VSNFG,UCBVOLI  GET PRIVILEGE BITS
         STH   R0,VOLFLAGS   STASH FLAGS
         AIF   ('&LOCAL' NE 'CCSI').NOLO1                       GP12050
         TM    VOLFLAGS+1,VF2DSNA+VF2DSNT  WYL/TSO/LIB?          94240
         BZ    AUTOLOOK      NO; SKIP IT
.NOLO1   SERVCALL TIOUA,(R4)  SEE IF USER DD CARD               GP12050
         LTR   R0,R0
         BNZ   AUTOLOOK      YES; DO LATER ON
         MVC   VOLDDNM,=CL8'VOLMOUNT'
         B     LOOKCOMM      GO TO COMMON CODE
AUTOEND  NI    PROP,255-PAUTO  RESET AUTO MODE
         B     ALLDONE       NO NEED TO SCAN MORE               GP12061
         DROP  R4
LOOKTIOT L     R1,ADDRTIOT   GET PRIOR ENTRY ADDRESS OR 0
         SERVCALL TIOLK      LOOK FOR PLAIN TIOT ENTRIES
         LTR   R2,R0         ANY MORE TO DO ?
         BZ    ALLDONE       NO; QUIT
         ST    R2,ADDRTIOT   SET FOR NEXT CALL
         LAT   R4,16(R2),LOOKTIOT     SKIP DUMMY UCB
         CLI   0(R2),20      STANDARD LENGTH ?                  GP12061
         BNE   LOOKTIOT        NO; GET ANOTHER                  GP12061
         CLI   20(R2),0      LAST ENTRY ?                       GP12061
         BE    LOOKACCP        YES; SKIP CONCATENATION TEST     GP12061
         CLI   4+20(R2),C' ' CONCATENATION ?                    GP12061
         BE    LOOKTIOT        NO; GET ANOTHER                  GP12061
         USING UCBOB,R4
LOOKACCP CLI   UCBTBYT3,UCB3DACC       IS IT DASD ?
         BNE   LOOKTIOT      IGNORE NON-DA
         SERVCALL TIODD,4(R2)   LOCATE TIOT ENTRY BY DDNAME
         CR    R2,R0         IF NOT EQUAL, DUPLICATE DDNAME
         BNE   LOOKTIOT      WHICH SHOULD BE IGNORED
         CLC   =CL8'VOLMOUNT',4(R2)  GENERIC REQUEST CARD ?      82137
         BE    LOOKTIOT      YES; ALREADY PROCESSED              82137
         CLC   =C'SYS',4(R2)   DYNAMIC DD CARD ?                 88234
         BE    LOOKTIOT      YES; SKIP IT                        88234
         TM    PROP,PCATLG   WYLCATLG ?                          82137
         BZ    LOOKNCAT      NO                                  82137
         CLC   =C'CAT',4(R2)   CATALOG DEFINITION ?              82137
         BE    LOOKTIOT      YES; SKIP IT                        82137
LOOKNCAT MVC   VOLDDNM,4(R2)  SAVE DDNAME                        82137
         SERVCALL VSNFG,UCBVOLI   GET ATTRIBUTE FLAGS FOR VOLUME
         STH   R0,VOLFLAGS   SAVE FOR LATER TEST
LOOKCOMM STCM  R4,7,STARUCBA+1  SAVE UCB FOR EXTENT FORMATTING   85252
         SERVCALL UCBGN,(R4)   GET GENERIC NAME
         MVC   VOLTYPE,0(R1)   STASH GENERIC
         MVC   VOLUNIT,UCBNAME   SAVE UCB NAME
         MVC   VOLSER,UCBVOLI   SAVE VOL-SER
         MVC   CATTYPE,UCBTBYT1  SET CATALOG TYPE                82137
         NI    CATTYPE+1,255-UCBRR-UCBVLPWR-UCBDVPWR  CLEAR SHR/PWR
         CLI   CATTYPE+4,X'0A'   3340 ?                          82137
         BNE   *+8           NO                                  82137
         NI    CATTYPE+1,255-UCBRPS    IBM DID IT TO ME AGAIN    82137
         MVC   CATSER,UCBVOLI  MOVE TO CATALOG                   82137
         MVC   ALTCAT,CATSER  COPY TO SECOND                     82137
         CLC   =C'WYL0',CATSER  PRIMARY WYLBUR PACK ?            82137
         BNE   *+8           NO                                  82137
         MVI   ALTCAT+3,C'B'  ALSO DO BACKUP PACK                82137
         DROP  R4            DONE WITH UCB
         NI    PROP,255-PDATA-PHEAD  SET NO DATA FOUND ON VOLUME
         XC    CLEARVOL(CLEARDSN-CLEARVOL),CLEARVOL  VOLUME COUNTERS
         SPACE 1
         VOLREAD OPEN,VOLSER   INITIALIZE VOLUME PROCESSING
         BXH   R15,R15,SKIPVOLE  IGNORE IF BAD                  GP12061
         VOLREAD DSCB        READ THE FORMAT 4
         BXH   R15,R15,SKIPVOLE  ERROR                          GP12061
         B     NEXTDSC4      JOIN NORMAL PROCESSING              82137
         SPACE 1
NEXTDSCB VOLREAD DSCB        GET ANOTHER DSCB
         CH    R15,=H'4'     DID WE GET ONE ?
         BE    DOTOTALS      NO; END OF VOLUME
         BH    DSCBIOER      NO; I/O ERROR
NEXTDSC4 LR    R1,R0         COPY DSCB ADDRESS                   82137
         CLI   DS1FMTID-DS1DSNAM(R1),C'1'   REALLY A FORMAT 1 ?
         BNE   NEXTDSCB      IF NOT, SKIP
         TM    ACCTPRIV,MASKVTOC   FULL VTOC RUN ?
         BNZ   MASKDONE      YES; PROCESS
         ICM   R2,15,MASKLEN    GET CLC LENGTH                  GP12061
         BM    MASKDONE      NONE; ACCEPT THE DATA SET          GP12061
         AIF   (NOT &LOCO).NLOC7
         TM    VOLFLAGS+1,VF2DSNA   LIBPAK ?
         BNZ   MASKTSTL      YES; TEST IT
         TM    VOLFLAGS+1,VF2DSNT   TSO PACK ?
         BNZ   MASKTSTS      YES; TEST IT
         TM    ACCTPRIV,MASKVTU  USER *VTOC REQUEST ?
         BNZ   MASKDONE      YES; PROCESS
         EX    0,WYLCLC      SEE IF MATCH ON WYLBUR NAME        GP12061
         BE    MASKDONE
MASKTSTL EX    0,LIBCLC      LIBPAK ACCOUNT ?                   GP12061
         BE    MASKDONE      MATCH; PROCESS
         B     NEXTDSCB
.NLOC7   SPACE 1
MASKTSTS EX    R2,TSOCLC     SEE IF MATCH ON TSO ID/GENERIC     GP12061
         BNE   NEXTDSCB        NO; SKIP
MASKDONE MVC   DS1DSNAM(LENDSCB1),DS1DSNAM-DS1DSNAM(R1)  COPY
         BAL   R14,DSNCHOSE   SEE IF DATA SET TO BE PROCESSED    82137
         B     NEXTDSCB      NO; SKIP IT                         82137
         LA    R1,1
         AH    R1,TOTDSNS
         STH   R1,TOTDSNS    INCREMENT MATCH COUNT
         OI    PROP,PDATA    INDICATE DATA SET FOUND
         BAL   R9,PAGEHEAD   PRINT PAGE HEADER
         XC    CLEARDSN(CLEARLEN),CLEARDSN  CLEAR DSN COUNTERS
         MVC   BLANKDSN(BLANKDSL),BLANKDSN-1  BLANK DSN FIELDS
         TM    PROP,PCATLG   CATALOG PROCESSING ?                82137
         BZ    LISTVTOC      NO; VTOC LISTING                    82137
         SPACE 1                                                 82137
         CLI   DS1DSORG+1,0  IS THIS VSAM OR OTHER FUNNY?        92237
         BNE   NEXTDSCB      YES; IGNORE IN LISTING              92237
         MVC   DSNAME,DS1DSNAM         MOVE DSNAME               82137
         BAL   R14,DSNCONV   CHECK FOR RENAME OPTION             82137
         CLC   DSNAME,DS1DSNAM  DSN CHANGED FOR RENAME ?         82137
         BE    NOTRENAM      NO                                  82137
         PRTROOM 2           ROOM FOR TWO CONSECUTIVE LINES      82137
         PRTLIST MSGRENAM    PRINT RENAME MESSAGE                82137
         SERVCALL DSCAT,DS1DSNAM  VALIDITY CHECK THE NAME        82137
         BXLE  R15,R15,NOTBADNM  NAME OK                         82137
         LA    R15,100       MAKE FAKE ERROR NUMBER              82137
         AR    R15,R0        ADD RETURN CODE IN                  82137
         B     CATFAIL       DISPLAY FAKE ERROR CODE             82137
NOTBADNM CATALOG LOOCAT      SEE IF OLD CATALOGED                82137
         BXH   R15,R15,PRORENAM  NO; IGNORE ERROR                82137
         CLC   =H'1',CATWORK      MULTI-VOLUME DATA SET ?       GP12061
         BE    PRORENAM      NO; PROCEED                        GP12061
         PRTDATA 'Multi-volume data set not changed'            GP12061
         OICC  1             SET MINOR ERROR                    GP12050
         B     NEXTDSCB      GO ON                               82137
PRORENAM TM    POPS,PTEST    TEST MODE ?                         82137
         BNZ   COMRENAM      FAKE IT OUT                         82137
         SLR   R0,R0         CLEAR THE 'UCB' POINTER             82137
         RENAME RENLIST      RENAME THE DATA SET                 82137
         BXLE  R15,R15,COMRENAM                                  82137
         SRL   R15,1         MAKE RETURN CODE AGAIN              82137
         ST    R15,DB        SAVE IT                             82137
         PRTLIST MSGRENFL    SHOW FAILED                         82137
         OICC  8             SET ERROR                          GP12050
         B     NEXTDSCB      GO ON                               82137
         SPACE 1
COMRENAM CLC   CATWORK+CATSER-CATLIST(6),CATSER  CATL'D ON OLD VOL ?
         BE    RENUNCAT      YES; UNCATALOG IT                   82137
         CLC   CATWORK+CATSER-CATLIST(6),ALTCAT  ALTERNATE ?     82137
         BNE   NOTRENAM      NO; LEAVE IT ALONE                  82137
RENUNCAT CATALOG UNCAT       UNCATALOG THE OLD NAME              82137
         SPACE 1
NOTRENAM TM    POPS,PTEST    TEST MODE ?                         82137
         BNZ   GOODCAT       FAKE IT OUT                         82137
         TM    POPS,PREP     REPLACE OPTION ?                    82137
         BZ    DOCATLG       NO; JUST CATALOG                    82137
         CATALOG DOCAT       TRY TO CATALOG                      82137
         BXLE  R15,R15,GOODCAT   OK                              82137
         TM    POPS,PWYLREP  CONDITIONAL REPLACE ?               82137
         BZ    DORECAT       NO; UNCONDITIONAL                   82137
         CATALOG LOOCAT      ELSE TRY FOR AN OLD ENTRY           82137
         BXH   R15,R15,DOCATLG                                   82137
         CLC   =H'1',CATWORK      MULTI-VOLUME DATA SET ?       GP12061
         BNE   DOCATLG       SKIP RECAT (LET IT FAIL W/MSG)     GP12061
         CLC   CATWORK+CATSER-CATLIST(5),CATSER  ALREADY ON ?    82137
         BE    GOODCAT                                           82137
         CLC   CATWORK+CATSER-CATLIST(5),ALTCAT  OLD FROM VOL ?  82137
         BNE   DOCATLG       NO; FAIL IT                         82137
DORECAT  CATALOG RECAT       RECATALOG                           82137
         LTR   R15,R15       GOOD RETURN ?                       82137
         BZ    GOODCAT       YES                                 82137
DOCATLG  CATALOG DOCAT       ELSE JUST CATALOG                   82137
         LTR   R15,R15                                           82137
         BZ    GOODCAT       OK, DONE                            83178
         TM    POPS,PREP     RUNNING WITH REPLACE OPTION ?       83178
         BNZ   CATFAIL       NOT CATALOGED                       82137
         CH    R15,=H'8'     PRESUMED DUPLICATE ?                83178
         BE    NEXTDSCB      YES; SKIP MESSAGE                   83178
GOODCAT  SLR   R15,R15       SET GOOD RETURN CODE                82137
CATFAIL  ST    R15,DB        STASH RETURN CODE                   82137
         PRTLIST MSGCATLG    PRINT RESULT                        82137
         B     NEXTDSCB      GO GET ANOTHER DATA SET             82137
         SPACE 1
*        PROCESS ALLOCATION AND LAST USE DATE
         SPACE 1
LISTVTOC LA    R1,DS1CREDT-1  POINT TO CREATION DATE             82137
         LA    R7,ALODATE    OUTPUT FIELD                        82137
         BAL   R9,DS1DATE    CONVERT DSCB DATE                   82137
         MVC   LASTUSE+2(4),=C'-NA-'   PRESET FOR NO ACCESS DATE
         LA    R1,DS1REFD-1  POINT TO REFERENCE DATE -1
         LA    R7,LASTUSE    SET OUTPUT FIELD                    82137
         BAL   R9,DS1DATE    CONVERT DSCB DATE                   82137
         AIF   ('&LOCAL' NE 'CCSI').NODTSDT
         CLI   LASTUSE+1,C' '   WAS A DATE CONVERTED ?
         BNE   DODSORG       YES; DO DSORG
         TM    DS1REFD+2,X'0F'  VALID SIGN ?
         BNO   DODSORG       NO; SKIP
         SLR   R1,R1         CLEAR HIGH BYTE
         ICM   R1,7,DS1REFD  LOAD IT
         BAL   R9,GETDATE    CONVERT PACKED DATE                 82137
.NODTSDT SPACE 1
*        PROCESS DSORG, RECFM, ETC.
         SPACE 1
DODSORG  SERVCALL DSFMT,DS1DSORG   FORMAT COMMON FIELDS          82137
         MVC   DSORG(LENFMT),0(R1)  MOVE RESULTS                 82137
         CLI   DSORG,C' '    ANY DSORG ?                         82137
         BNE   *+8           YES                                 82137
         MVI   DSORG,C'?'      TOO BAD ?                         82137
         CLI   RECFM,C' '    ANY RECFM ?                         82137
         BNE   *+8           YES                                 82137
         MVI   RECFM,C'?'      TOO BAD ?                         82137
         SPACE 1
*        PROCESS SECONDARY ALLOCATION
         SPACE 1
         SLR   R1,R1
         IC    R1,DS1SCALO   GET SECONDARY ALLOCATION OPTIONS
         SRL   R1,6
         MH    R1,=H'3'
         LA    R1,PATSCALO(R1)   GET ABBREVIATION
         MVC   ALLOCTYP,0(R1)
         SPACE 1
*        START COUNTING TRACKS
*        IBM DOES NOT DOCUMENT HANDLING OF THE USER LABEL TRACK. IT
*        OCCUPIES ONE TRACK. MAPPED BY DS1EXT1, BUT IS NOT COUNTED
*        IN DS1EXT1, SO WE NEED TO FINAGLE.
         SPACE 1
         LA    R14,IECSDSL1  POINT TO FORMAT 1                   82137
         SLR   R15,R15       CLEAR FORMAT 3 POINTER              82137
         STM   R14,R15,STARUCBA+4   MAKE CALLING LIST            82137
         CLI   DS1NOEPV,3    MORE THAN THREE EXTENTS ?
         BL    FMTRACK       NO; DONE                            81245
         BH    FMTRD         YES; READ EXTENSION                 83156
         CLI   DS1EXT1,X'40'   LABEL TRACK ?                     83156
         BNE   FMTRACK       NO; NO FMT3 EXPECTED                83156
FMTRD    VOLREAD DSC3        GET CHAINED DSCB
         BXH   R15,R15,FMT3ERR  NO; SIGNAL ERROR
         LR    R1,R0         LOAD THE DSCB ADDRESS               82137
         USING IECSDSL3,R1                                       82137
         CLI   DS3FMTID,C'3'           REALLY A FORMAT 3 ?
         BNE   FMTRD                   BRANCH IF FORMAT 2
         ST    R1,STARUCBA+8  PLACE ADDRESS IN CALLING SEQUENCE  82137
         DROP  R1                                                82137
         B     FMTRACK       DONE
         SPACE 1
FMT3ERR  OI    FLAGDSN,FGFMT3  SHOW ERROR GETTING FORMAT 3 DSCB
         SPACE 1
*        FORMAT TRACKS AND LSTAR
         SPACE 1
FMTRACK  SERVCALL DVEXT,STARUCBA  GET EXTENT SIZE IN TRACKS      82137
         BXLE  R15,R15,FMTRACKS  SKIP IF NO ERROR                82137
         OI    FLAGDSN,FGFMT3  SHOW ERROR GETTING EXTENTS        82137
FMTRACKS ST    R0,TRACKS     SAVE FOR FORMATTING                 82137
         SLR   R2,R2         CLEAR FOR ICM
         ICM   R2,7,DS1LSTAR  GET TRACKS FROM LSTAR             GP09186
         BE    FMTRACKC      NONE                               GP09186
         SRL   R2,8          00TT                               GP09186
         LA    R2,1(,R2)     ELSE INCREASE TRACK COUNT
FMTRACKC CR    R2,R0         MORE THAN 100% USED ?               82137
         BNH   *+6           NO
         LR    R2,R0         YOU'RE KIDDING ?                    82137
         ST    R2,TRACKUSE   SET TRACKS IN USE
         A     R0,TOTRACKS   ADD TO VOLUME TOTAL                 82137
         ST    R0,TOTRACKS             PUT IT BACK               82137
         TM    DSLFG,PSNAP   SNAP REQUESTED ?                    82137
         BZ    SNAPTEST      NO
         NI    DSLFG,255-PSNAP  PROVISIONALLY RESET SNAP OPTION  82137
SNAPCHEK CLC   =H'72',DS1BLKL  MINIMUM BLOCKSIZE ?              GP12061
         BH    SNAPTEST      NO; SKIP
*OLD*    CLC   =H'4100',DS1BLKL  MAXIMUM BLOCKSIZE ?
*OLD*    BL    SNAPTEST      TOO BIG; TOO BAD
         TM    DS1DSIND,X'40'    RACF PROTECTED ?
         BNZ   SNAPCACC      YES; CHECK ACCESS PRIVILEGES
         TM    DS1DSIND,X'14'  READ PROTECT PASSWORD ?
         BNM   SNAPCANP      NO; OK
SNAPCACC TM    ACCTPRIV,VAASYS+VAASUP  PRIVILEGED ?
         BZ    SNAPTEST      NO; REJECT IT
SNAPCANP CLI   DS1KEYL,0     NO KEYS ?
         BNE   SNAPTEST      TOO BAD
         TM    DS1RECFM,DCBRECU  CHECK RECFM
         BZ    SNAPTEST      NONE - NO SNAP
         BO    SNAPCHEW      U - LRECL MUST MATCH BLOCKSIZE
*   TEST FOR KERMIT DEFAULT RECLEN RATHER THAN PRINTABLE.        92254
         CLC   =H'260',DS1LRECL  PRINTABLE LENGTH ?              92254
         BL    SNAPTEST      NO; TOO BAD
         OC    DS1LRECL(2),DS1LRECL    ANY RECORD LENGTH ?
         BZ    SNAPTEST      NO; TOO BAD
         B     SNAPSNAP      ELSE TRY IT
SNAPCHEW CLC   DS1LRECL(2),DS1BLKL  LRECL=BLKSIZE ?
         BNE   SNAPTEST      NO; TOO BAD
SNAPSNAP OI    DSLFG,PSNAP   YES                                 82137
SNAPTEST TM    DSLFG,PPDS+PDIR+PSNAP   ANY OPTIONS ENABLED ?     82137
         BZ    DSNFINIS      NO; JUST PRINT
         TM    DS1NOEPV,255  PATTERN DSCB ?                      85252
         BZ    DSNFINIS      YES; SKIP                           85252
         CLC   DS1LSTAR(7),ZEROES  INITIALIZED ?                GP09186
         BE    DSNFINIS      NO; SKIP                            85252
         PRTROOM 2           ALLOW FOR TWO LINES
         SPACE 1
         TM    DS1DSORG,DCBDSGPO  PARTITIONED DATA SET ?
         BZ    DSNGO         NO; CHECK FOR SNAP OPTION
         TM    DSLFG,PPDS    COUNT DIRECTORY BLOCKS ?           GP12061
         BZ    DSNFINIS        NO; JUST PRINT                   GP12061
         SLR   R8,R8         CLEAR DIRECTORY BLOCKS USED
         SLR   R9,R9         CLEAR DIRECTORY BLOCKS READ
DIRLOOP  VOLREAD READ        READ A BLOCK
         CH    R15,=H'4'
         BE    DIREOF        END-FILE
         BH    DIRROR        ERROR
         LR    R6,R0         COPY BLOCK ADDRESS
         CLC   =X'080100',5(R6)  DIRECTORY BLOCK COUNT FIELD ?
         BNE   DIRROR        NO; ERROR
         LA    R9,1(,R9)     UP ALLOCATED COUNT
         TM    FLAGDSN,FGDEOF  LOGICAL END OF DIRECTORY PASSED ?
         BNZ   DIRLOOP       YES; JUST COUNT ALLOCATED BLOCKS
         LA    R8,1(,R8)     UP USED COUNT
         CLI   8(R6),X'FF'   END-KEY ?
         BNE   DIRLOOP       NO; DO IT AGAIN
         OI    FLAGDSN,FGDEOF  SET LOGICAL END
         B     DIRLOOP       COUNT ALLOCATED BLOCKS ONLY
         SPACE 1
DIRROR   OI    FLAGDSN,FGERR  SET ERROR FLAG
DIREOF   STH   R8,DIRUSE     SET BLOCKS USED
         STH   R9,DIRALL     SET DIRECTORY BLOCKS ALLOCATED
         TM    FLAGDSN,FGERR  ERROR ?
         BZ    PDSDOMEM      NO
DERROR   TM    FLAGDSN,FGDSN  WAS DSN LINE PRINTED PREVIOUSLY ?
         BNZ   DERROR1       YES
         PRTROOM 3           ROOM FOR THREE LINES
         PRTSPACE 1          DOUBLE-SPACE
         BAL   R9,PRINTDSN   PRINT DSN INFO
DERROR1  PRTLIST DIRIOERR    ELSE SIGNAL ERROR
         PRTDATA 'Directory I/O error on',(VOLSER,PAD),'in',(DS1DSNAM,P*
               AD),DEV=2
         OICC  4             SET MINOR ERROR                    GP12050
         PRTSPACE 1
         B     NEXTDSCB
         SPACE 1
PDSDOMEM TM    DSLFG,PDIR    DIRECTORY LISTING DESIRED ?        GP12061
         BZ    DSNFINIS      NO; JUST PRINT TOTALS
         MVC   PDDRECFM,DS1RECFM  SET RECFM FOR PDSDE            82137
         SLR   R0,R0
         LR    R1,R8         GET NUMBER OF DIRECTORY ENTRIES
         D     R0,=F'6'      GET APPROXIMATE NUMBER OF LINES
         LTR   R0,R0         PARTIAL LINE ?
         BZ    *+8           NO
         LA    R1,1(,R1)     ALLOW FOR PARTIAL LINE
         LA    R1,3(,R1)     ALLOW FOR COMMON HEADER
         TM    DSLFG,PSNAP   SNAP OPTION ?                       82137
         BZ    *+8           NO
         LA    R1,3+5        SET FOR FIRST ENTRY ONLY
         PRTROOM (R1)        RESERVE CONTIGUOUS SPACE FOR HEADER
         PRTSPACE 1          DOUBLE SPACE
         BAL   R9,PRINTDSN   PRINT THE DATA SET INFORMATION
         LTR   R9,R8         ANY BLOCKS USED ?
         BNP   PDSFINIS      NO; SAY SO
         TM    DS1RECFM,DCBRECU  RECFM=U ?
         BZ    PDSLOOP       SKIP IF NONE
         BNO   PDSDOSF                                           79322
*OLD*    CLC   DS1BLKL,=H'4100'  REASONABLE ?
*OLD*    BH    PDSLOOP
         B     PDSDOSSI                                          79322
PDSDOSF  TM    DS1RECFM,DCBRECF  F ?
         BZ    PDSLOOP
         CLC   DS1LRECL,=H'80'  CARD ?                           79322
         BNE   PDSLOOP       NO
PDSDOSSI OI    FLAGDSN,FGWYL  SET WYLIB SWITCH
PDSLOOP  VOLREAD GETDE       GET A DIRECTORY ENTRY
         CH    R15,=H'4'     CHECK IT
         BH    DERROR        REPORT ERROR
         BE    PDSFINIS      DONE
         LR    R4,R0         SAVE ADRESS OF THE ENTRY
         TM    DSLFG,PSNAP   SNAP OPTION FOR THIS MEMBER ?       82137
         BZ    PDSNSNAP      NO
         PRTROOM 6           MAKE CONTIGUOUS SPACE
PDSNSNAP MVC   MEMLONG,MEMLONG-1  CLEAR MEMBER PRINT LINE
         ST    R4,PDDNEXT    SET DIRECTORY ADDRESS FOR FORMATTING
         SERVCALL PDSDE,PDDNEXT  FORMAT THE DIRECTORY ENTRY      82137
         LR    R1,R0         COPY RETURN ADDRESS                 82137
         MVC   PDDRALIS(PDDRBLNK),0(R1)  SAVE RETURN             82137
         MVC   MEMALIAS(L'MEMALIAS+L'MEMNAME),PDDRALIS  COPY MEMBER
         CLI   MEMALIAS,C'*' ALIAS ENTRY ?                       82137
         BNE   *+8           NO                                  82137
         MVI   MEMALIAS,C'-'   YES; CONVERT TO OLD FORMAT        82137
         OI    FLAGDSN,FGMEM  SHOW AT LEAST ONE MEMBER DONE
         LA    R3,MFDSHORT   SET FOR SHORT PRINT ENTRY
         TM    FLAGDSN,FGWYL  WYLIB ?
         BNZ   PDSWYLDT      YES; SEE IF DATE PRESENT            82137
         CLI   PDDRDATE,C' '   DATE RETURNED ?                   82137
         BE    PDSNWLIB      NO; PRINT SHORT ENTRY               82137
         OI    FLAGDSN,FGWYL   USE LONG MODE FROM NOW ON         82137
PDSWYLDT CLI   PDDRDATE,C' '   DATE RETURNED ?                   82137
         BE    PDSWYLD       NO; LEAVE BLANK                     82137
         PACK  DB(4),PDDRDATE   PACK THE DATE                    82137
         L     R1,DB         LOAD IT                             82137
         LA    R7,MEMDATE    POINT TO OUTPUT FIELD               82137
         BAL   R9,GETDATE    CONVERT TO MM/DD/YY FORM            82137
PDSWYLD  LA    R3,MFDLONG    SET FOR LONG MEMBER ENTRY
PDSNWLIB PRTLIST (R3)        PRINT THE MEMBER ENTRY
         TM    DSLFG,PSNAP   PRINT MEMBER TEXT ALSO ?            82137
         BZ    PDSLOOP       NO; DO NEXT MEMBER
         BAL   R9,SNAPSHOT   READ THE TEXT
         B     PDSLOOP       GET NEXT MEMBER
         SPACE 1
PDSFINIS TM    FLAGDSN,FGMEM  ANY MEMBERS PRINTED ?
         BNZ   PDSFINIX      YES
         LA    R3,NOMEMB     SET ERROR MESSAGE
         BAL   R9,SNAPQUIT                                       81243
PDSFINIX PRTSPACE 1          INSERT EXTRA SPACE
         B     NEXTDSCB      DO NEXT DSN
         SPACE 2
DSNGO    TM    DS1DSORG,DCBDSGPS  PS ?
         BZ    DSNFINIS      NO
         TM    DSLFG,PSNAP   SNAP OPTION ?                       82137
         BZ    DSNFINIS      NO
         PRTROOM 7           LEAVE SPACE FOR 7 LINES
         PRTSPACE 1          DOUBLE SPACE
         BAL   R9,PRINTDSN   PRINT DATA SET INFORMATION
         BAL   R9,SNAPSHOT   READ TEXT
         PRTSPACE 1          SPACE AFTER
         B     NEXTDSCB      DONE WITH DATA SET
         SPACE 1
DSNFINIS LA    R9,NEXTDSCB   SET RETURN FROM PRINT
PRINTDSN TM    FLAGDSN,FGDSN   DATA SET LINE PRINTED PREVIOUSLY ?
         BNZR  R9            YES; RETURN TO CALLER
         PRTLIST DSNFD       PRINT THE DATA SET INFORMATION
         OI    FLAGDSN,FGDSN   SHOW DSN LINE PRINTED
         TM    FLAGDSN,FGFMT3  ERROR GETTING FORMAT THREE ?
         BZR   R9            NO; RETURN
         PRTLIST OBT3MSG     PRINT THE ERROR MESSAGE
         PRTDATA 'DSCB3 I/O error on',(VOLSER,PAD),'in',(DS1DSNAM,PAD),*
               DEV=2
         OICC  4             SET MINOR ERROR                    GP12050
         PRTSPACE 0          FORCE NEW LINE (FOR MEMBER LIST)    82137
         BR    R9            RETURN TO CALLER
         SPACE 1
PAGEHEAD TM    PROP,PHEAD    TITLE PRINTED PREVIOUSLY ?
         BNZR  R9            YES; RETURN TO CALLER
         PRTLIST TITLE,TITLE=1
         PRTLIST SUBHEAD,TITLE=3                                 82137
         OI    PROP,PHEAD    SHOW PAGE HEADER DONE
         BR    R9            RETURN
         SPACE 1
*        DO VOLUME TOTALS
         SPACE 1
DOTOTALS TM    PROP,PDATA    ANY DATA FOUND ?
         BO    DOTOT2                  BRANCH IF SO
         TM    PROP,PAUTO    AUTOMATIC LOOK-UP MODE ?
         BNZ   LOOPTIOT      YES; SKIP PRINTOUT
         BAL   R9,PAGEHEAD   FORCE PAGE HEADER IF NOT DONE BEFORE
         PRTLIST NODATA      ELSE INDICATE NONE
         B     LOOPTIOT      GET NEXT VOLUME
DOTOT2   L     R0,TOTRACKS
         A     R0,ALLTRACK   TOTAL PER JOB
         ST    R0,ALLTRACK   STASH BACK
         LH    R0,TOTDSNS    GET DSN COUNT
         A     R0,ALLDSNS
         ST    R0,ALLDSNS    UPDATE DSN COUNT
         PRTLIST TOTALMSG,DEV=(1,2)
         B     LOOPTIOT                GO GET A NEW VOLUME
         SPACE 1
*        FORMAT 4 OBTAIN ERROR ROUTINE
         SPACE 1
SKIPVOLE SRL   R15,1         COMPENSATE FOR BXH
         ST    R15,DB        SAVE CODE FOR FORMATTING
         BAL   R9,PAGEHEAD   FORCE PAGE HEADER IF NOT DONE BEFORE
         PRTLIST OBT4MSG     VOLUME SKIPPED MESSAGE
         PRTDATA 'DSCB4 I/O error on',(VOLSER,PAD),DEV=2
         OICC  8             SET NOT SO MINOR ERROR             GP12050
SKIPVOL  B     LOOPTIOT                GET NEXT VOLUME          GP12061
         SPACE 1
DSCBIOER SRL   R15,1         ADJUST FOR BXH
         ST    R15,DB        SAVE FOR FORMATTING
         BAL   R9,PAGEHEAD   FORCE PAGE HEADER IF NOT DONE BEFORE
         PRTLIST IOERMSG
         PRTDATA 'DSCB1 I/O error on',(VOLSER,PAD),DEV=2        GP12061
         OICC  8             SET NOT SO MINOR ERROR             GP12050
         B     LOOPTIOT
         SPACE 1
ALLDONE  PRTLIST DONEMSG,DEV=(1,2)
ENDOJOB  SERVTERM ,          CLOSE AND FREE EVERYTHING
         L     R9,RETCODE    LOAD COMPLETION CODE               GP12050
         PGMEXIT RC=(R9)     QUIT                               GP12050
         SPACE 1
BADPARM  PRTV  BADPARMG      SHOW BAD PARM LIST
         SERVTERM ,          CLOSE PRINT FILE
         ABEND 666           AND QUIT
         SPACE 3
SNAPSHOT LA    R5,5          SET FOR FIVE LINES
SNAPREAD VOLREAD READ        READ A BLOCK
         LR    R8,R0         COPY BLOCK ADDRESS
         CH    R15,=H'4'     CHECK RETURN
         BH    SNAPPER       ?
         BE    SNAPEOF       END-FILE
SNAPBLOK CLI   5(R8),0       ZERO KEY ?
         BNE   SNAPPER       NO; ERROR
         LH    R6,6(,R8)     GET BLOCK LENGTH
         LA    R7,8(R6,R8)   GET LAST BYTE +1
         BCTR  R7,0          POINT TO LAST BYTE
         TM    DS1RECFM,DCBRECU  CHECK RECFM
         BO    SNAPWYL       PRESUMED WYLBUR FORMAT
         TM    DS1RECFM,DCBRECF  FIXED ?
         BZ    SNAPVARB      NO, VARIABLE
         LA    R8,8(,R8)     SKIP COUNT FIELD
         LH    R6,DS1LRECL   GET RECORD LENGTH
         LA    R7,1(,R7)     GET LAST+1 BYTE AGAIN
         SR    R7,R6         POINT TO LAST POSSIBLE RECORD
         CR    R8,R7         ANY RECORDS AT ALL ?
         BH    SNAPPER       NO; ERROR
SNAPFIXL LR    R1,R6         COPY RECORD LENGTH
         LR    R2,R8         COPY RECORD ADDRESS
         BAL   R4,SNAPLINT   PRINT A LINE
         BXLE  R8,R6,SNAPFIXL  DO ANOTHER LINE
         B     SNAPREAD      GET ANOTHER BLOCK
         SPACE 1
SNAPVARB CLC   6(2,R8),8(R8)  BLOCK SIZE < DATA LENGTH ?
         BL    SNAPPER       YES; ERROR
         LA    R8,8(,R8)     SKIP OVER COUNT HEADER
         ICM   R6,3,0(R8)    GET BDW LENGTH
         BNP   SNAPPER       INVALID IF NOT POSITIVE
         LA    R7,0(R6,R8)   SET LAST BYTE +1
         BCTR  R7,0          LAST BYTE                          GP12061
         LA    R6,4          SET LENGTH OF FIRST ENTRY (BDW)
         BXH   R8,R6,SNAPPER  ERROR IF SHORT BLOCK
SNAPVARL ICM   R6,3,0(R8)    GET RDW OF NEXT RECORD
         BNP   SNAPPER       INVALID IF NOT POSITIVE
         LA    R0,4          SET OVERHEAD
         LR    R1,R6         COPY LENGTH
         SR    R1,R0         SET TEXT LENGTH
         BM    SNAPPER       TOO BAD
         LA    R2,4(,R8)     POINT TO TEXT
         LA    R14,0(R1,R2)  GET NEXT RECORD ADDRESS
         CR    R14,R7        STILL IN BLOCK ?
         BH    SNAPPER       NO; ERROR
         BAL   R4,SNAPLINT   PRINT THE LINE
         BXLE  R8,R6,SNAPVARL  DO ANOTHER LINE
         B     SNAPREAD      ELSE READ ANOTHER BLOCK
         SPACE 1
SNAPWYL  MVC   WYLDCOMP(PATDCOML),PATDCOMP  MOVE DECOMPRESS CODE
         MVC   DCMBUFMX,6(R8)   MOVE BLOCK LENGTH
         LA    R1,8(,R8)     GET BUFFER ADDRESS
         ST    R1,DCMBUFAD   STASH IN BLOCK
SNAPWYLR SERVCALL WDCOM,WYLDCOMP   DECOMPRESS
         CH    R15,=H'4'     CHECK RETURN
         BH    SNAPNEDT      NOT EDIT FORMAT
         BE    SNAPREAD      END OF BLOCK; GET ANOTHER
         MVC   INT(1),DCMLINEH  GET OVERFLOW BYTE OF LINE NUMBER 81245
         MVC   INT+1(8),DCMLINEB  MOVE REST OF NUMBER            81245
         L     R2,DCMRECAD   GET THE TEXT LINE
         LH    R1,DCMRECLN   GET THE LENGTH
         LA    R0,8          CONSTANT
         AR    R2,R0         SKIP THE LINE NUMBER
         SR    R1,R0         ADJUST LENGTH
         LA    R3,WYLFD      POINT TO FD LIST
         BAL   R4,SNAPLINE   PRINT A LINE
         B     SNAPWYLR      DO ANOTHER RECORD
         SPACE 1
SNAPLINT LA    R3,LINEFD     SET FOR NON-WYLBUR PRINT LINE
SNAPLINE SLR   R0,R0
         IC    R0,FDILEN-2-FDSECT+PATFD  LOAD MAX LENGTH
         CR    R1,R0         GREATER THAN MAX LENGTH ?
         BNH   *+6           NO
         LR    R1,R0         SET FOR MAX
         MVC   VARFD(PATFDLEN),PATFD  MOVE PATTERN FD ENTRY
         STC   R1,FDILEN-FDSECT-2+VARFD   SET FD LENGTH
         PRTLIST (R3)        PRINT ONE DATA LINE
         BCTR  R5,R4         RETURN TO CALLER
         BR    R9            RETURN TO MAIN-LINE
         SPACE 1
SNAPPER  LA    R3,ERRSNAP
         B     SNAPQUIT      AND EXIT
SNAPEOF  CH    R5,=H'5'      ANY DATA PRINTED ?
         BNER  R9            YES; NORMAL EOF - GO TO MAIN-LINE
         LA    R3,EOFSNAP
         B     SNAPQUIT
SNAPNEDT LA    R3,NOTEDIT    DATA NOT EDIT
SNAPQUIT PRTLIST SNAPMSG     PRINT ERROR LINE
         BR    R9            RETURN TO MAIN-LINE
         SPACE 1
*        ROUTINES FOR DATE CONVERSION
         SPACE 1
DS1DATE  XC    DB,DB         DSCB FORMAT DATE CONVERSION
*Y2K*    CLI   1(R1),99      VALID YEAR ?                       GP12061
*Y2K*    BHR   R9            NO; RETURN                         GP12061
         CLC   DB(2),2(R1)   VALID DAY ?
         BER   R9            NO                                  82137
         CLC   2(2,R1),=H'366'
         BHR   R9            TOO HIGH; TOO BAD                   82137
         SLR   R0,R0
         IC    R0,1(,R1)     LOAD YEAR
         MH    R0,=H'1000'
         AH    R0,2(,R1)     ADD DAY
         CVD   R0,DB         CONVERT TO PACKED
         OI    DB+7,X'0F'    MAKE SURE
         L     R1,DB+4       LOAD FOR GETDATE
         SPACE 1
GETDATE  STM   R1,R5,SAVE1T5
         XC    DB,DB
         LR    R3,R1         COPY FOR CHECK
         LA    R5,9          LARGEST VALID DIGIT
         LA    R4,7          CHECK SEVEN DIGITS
GETDATEC SLR   R2,R2         CLEAR
         SLDL  R2,4          SHIFT A DIGIT
         CR    R2,R5         VALID ?
         BH    GETDATEX      INVALID; QUIT
         BCT   R4,GETDATEC   TRY NEXT
         LR    R2,R1         RELOAD
         STH   R1,DB+6
         OI    DB+7,X'0F'
         CVB   R1,DB
         LTR   R1,R1         ANY DAY ?
         BZ    GETDATEX      NO; QUIT
         CH    R1,=H'366'    REASONABLE ?
         BH    GETDATEX      NO; QUIT
         SRA   R2,12
         STH   R2,DB+6
         OI    DB+7,X'0F'
         CVB   R2,DB                   YEAR
         SLR   R0,R0
         SLR   R3,R3
         SRDL  R2,2
         LTR   R3,R3
         SLDL  R2,2
         BNZ   D3
         CH    R1,=H'60'               FEB. 29TH. ?
         BL    D3                      IF LOW IGNORE LEAP YEAR
         BH    D2
         LA    R0,1
D2       BCTR  R1,R0
D3       LA    R4,TABL
         LR    R5,R4
DLP      CH    R1,2(,R4)
         BNH   DF
         LA    R4,2(,R4)
         B     DLP
DF       SH    R1,0(,R4)
         AR    R1,R0                   ADD 1 IF LEAP YEAR
         LA    R4,2(,R4)
         SR    R4,R5
         SRL   R4,1
         MH    R4,=H'100'
         AR    R1,R4
         MH    R1,=H'100'
         AR    R1,R2
         CVD   R1,DB
         MVC   INT(10),PATDATE
         ED    INT(10),DB+4
         MVC   0(8,R7),INT+2                                     82137
GETDATEX LM    R1,R5,SAVE1T5
         BR    R9                                                82137
         EJECT
*        CONSTANTS AND STUFF
         SPACE 1
PATSCAT  CAMLST CATBX,1,,3   INDEX AND CATALOG; NO CVOL          82137
         ORG   *-12          DEFINE FLAGS ONLY                   82137
PATSRCT  CAMLST RECAT,1,,3   RECATALOG; NO CVOL                  82137
         ORG   *-12          DEFINE FLAGS ONLY                   82137
PATSLOC  CAMLST NAME,1,,3    LOCATE; NO CVOL                     82137
         ORG   *-12          DEFINE FLAGS ONLY                   82137
PATSUNC  CAMLST UCATDX,1     UNCATLOG; NO CVOL                   82137
         ORG   *-8           DEFINE FLAGS ONLY                   84188
PATSREN  CAMLST RENAME,1,2,3   RENAME                            82137
         ORG   *-12          DEFINE FLAGS ONLY                   82137
PATCCAT  CAMLST CATBX,1,2,3  INDEX AND CATALOG; CVOL             82157
         ORG   *-12          DEFINE FLAGS ONLY                   82137
PATCRCT  CAMLST RECAT,1,2,3  RECATALOG; CVOL                     82157
         ORG   *-12          DEFINE FLAGS ONLY                   82137
PATCLOC  CAMLST NAME,1,2,3   LOCATE; CVOL                        82157
         ORG   *-12          DEFINE FLAGS ONLY                   82137
PATCUNC  CAMLST UCATDX,1,2   UNCATLOG; CVOL                      82157
         ORG   *-8           DEFINE FLAGS ONLY                   84188
PATCREN  CAMLST RENAME,1,2,3   RENAME                            82137
         ORG   *-12          DEFINE FLAGS ONLY                   82137
         SPACE 1                                                 82137
ZEROES   DC    D'0'          CONSTANT
PATSCALO DC    C'?  BLKTRKCYL'
SYSPRINT PRTWORK SYSPRINT,TITLE=5
SUMPRINT PRTWORK SUMPRINT,TITLE=4                               GP12061
         SPACE 1
         PRINT NOGEN                                             82137
PATDCOMP SERVCOMP DSECT=NO,PFX=PCM,FG1=(PCMF1EDT+PCMF1TSO)       81278
PATDCOML EQU   *-PATDCOMP
         PRINT &PRTSYS                                           81278
         SPACE 1
BLANKS   DC    CL80' '       QUICK BLANKER                      GP12061
LIBPAT   CLC   LIBMASK(5),DS1DSNAM-IECSDSL1(R1)  LIBPAK ACCOUNT GP12061
WYLPAT   CLC   WYLMASK(8),DS1DSNAM-IECSDSL1(R1)  WYLBUR 4/8 ACC GP12061
TSOPAT   CLC   DSNMASK(0),DS1DSNAM-IECSDSL1(R1)  TSO/GENERIC    GP12061
         SPACE 1
AST30    DC    C'******************************'
         SPACE 1
TABL     DC    H'0,031,059,090,120,151,181,212,243,273,304,334,365'
PATDATE  DC    X'F0212020612020612020'
         SPACE 2
         LTORG
         EJECT
*        MESSAGES - HEADINGS, ETC.
         SPACE 1
         PRINT NOGEN                                             82137
TITLE    FDOPT NL,CC=C'#'    AUTOMATIC DATE/TIME/PAGE OPTION     81278
         FDSPACE 4                                               82137
         FDTM  PROP,PCATLG,BZ=TITLEVT  SKIP IF WYLVTOC           82137
         FDPRT 'DATA SET CATALOG/RENAME'                         82137
         FDGOTO TITLECM                                          82137
TITLEVT  FDPRT 'WYLBUR DATA SET SUMMARY'                         82137
TITLECM  FDCLI VOLSER,0,BE=TITLECX  SKIP REST ON SETUP PAGE      82137
         FDPRT 'FOR VOLUME',PAD                                  82137
         FDPRT VOLSER,DEB,PAD          VOLSER
         FDPRT '('
         FDPRT VOLTYPE,DEB
         FDPRT ') ON UNIT',PADR
         FDPRT VOLUNIT
         FDPRT ', DD',PADR
         FDPRT VOLDDNM,DEB
TITLECX  FDPRT *END                                              82137
         SPACE 1
SUBHEAD  FDOPT NL,CC=C' '                                        82137
         FDSPACE 8                                               82137
         FDPRT 'DATA SET NAME',LEN=36                            82137
         FDTM  PROP,PCATLG,BNZ=SUBHEADX                          82137
         FDPRT 'ALLOCATED  LAST USE  EXT  TRACKS USED  SECONDARY'
         FDPRT ' ORG  RECFM  LRECL  BLKSIZE    DIR USED',PADL    82137
         FDPRT *END                                              82137
SUBHEADX FDTM  POPS,PTEST,BZ=SUBHEADY                            82137
         FDPRT 'TEST MODE  *** ',PAD                             82137
SUBHEADY FDPRT 'STATUS'                                          82137
         FDPRT *END                                              82137
         SPACE 1
DSNFD    FDPRT DS1DSNAM,NL   DSN
         FDPRT ALODATE,PADL
         FDPRT ' ',LEN=2
         FDPRT LASTUSE,PADR
         FDPRT DS1NOEPV,I,RADJ,LEN=3
         FDPRT TRACKS,I,RADJ,LEN=8
         FDPRT TRACKUSE,I,RADJ,PADL,LEN=5
         FDPRT DS1SCALO+1,3,I,RADJ,LEN=7
         FDPRT ALLOCTYP,PADL,LEN=L'ALLOCTYP+2                    82137
         FDPRT DSORG,LEN=L'DSORG+2                               82137
         FDPRT RECFM,PADR,LEN=5                                  82137
         FDPRT LRECL,RADJ,LEN=6                                  82137
         FDPRT BLKSIZE,RADJ,LEN=9                                82137
         FDCLC DIRALL,ZEROES,2,BE=DSNFDX  QUIT IF NO DIRECTORY BLOCKS
         FDPRT DIRALL,I,RADJ,LEN=6
         FDPRT DIRUSE,I,RADJ,LEN=6
DSNFDX   FDPRT *END
         SPACE 1
MFDLONG  FDPRT MEMLONG       PRINT LONG FORMAT MEMBER ENTRY
         FDPRT *END
MFDSHORT FDPRT MEMSHORT      PRINT SHORT FORMAT MEMBER ENTRY
         FDPRT *END
         SPACE 1
PATFD    FDPRT 0(R2),116     PATTERN FD ENTRY (ILEN REPLACED IN COPY)
PATFDLEN EQU   *-PATFD
         SPACE 1
LINEFD   FDPRT ' ',NL,LEN=16
         FDGOTO WYLNCOM
WYLFD    FDPRT ' ',NL,LEN=6
         FDPRT INT,PADR,LEN=9
WYLNCOM  FDEXEC VARFD        DO VARIABLE LENGTH TEXT
         FDPRT *END
         SPACE 1
OBT4MSG  FDPRT AST30,NL,LEN=10
         FDPRT 'VOLUME',PAD
         FDPRT VOLSER,DEB,PAD
         FDPRT 'SKIPPED DUE TO FORMAT 4 PROCESSING ERROR.',PAD
OBTCOM   FDPRT 'CONDITION CODE=',PADL
         FDPRT DB,4,I,PADR
OBTCOMAS FDPRT AST30,LEN=10
OBTCOMX  FDPRT *END
         SPACE 1
OBT3MSG  FDPRT AST30,NL,PADR,LEN=10
         FDPRT 'ERROR PROCESSING EXTENTS; TRACK COUNT INCORRECT.'
         FDGOTO OBTCOMAS
         SPACE 1
IOERMSG  FDOPT NL,CC=C'0'    DOUBLE-SPACE
         FDPRT AST30,PADR,LEN=10
         FDPRT 'VOLUME SKIPPED DUE TO OBTAIN ERROR DURING SEQUENTIAL VT*
               OC SEARCH.',PADR
         FDGOTO OBTCOM
         SPACE 1
DIRIOERR FDPRT AST30,NL,LEN=L'AST30+15                           82137
         FDPRT 'DIRECTORY I/O ERROR',LEN=34                      82137
         FDPRT AST30
         FDPRT *END
         SPACE 1
DONEMSG  FDOPT NL,CC=C'-'    TRIPLE-SPACE
         FDPRT AST30
         FDTM  PROP,PCATLG,BZ=DONEMSGV                           82137
         FDPRT 'WYLCATLG PROCESSING DONE;',PAD                   82137
         FDGOTO DONEMSGC                                         82137
DONEMSGV FDPRT 'WYLVTOC PROCESSING DONE',PADL                    82137
         FDPRT ALLTRACK,I,RADJ,PAD,LEN=10
         FDPRT 'TRACKS LISTED FOR',PAD
DONEMSGC FDPRT ALLDSNS,I                                         82137
         FDPRT 'DATA SETS',PAD
         FDPRT AST30
         FDOPT NL
         FDPRT *END
         SPACE 1
NODATA   FDOPT NL,CC=C'-'
         FDPRT AST30,PADR,LEN=15
        FDPRT 'NO DATA SETS FOUND ON THIS VOLUME MATCHING THE REQUEST.'
         FDGOTO MASKFD       GO TO DISPLAY THE MASK
         SPACE 1
TOTALMSG FDOPT NL,CC=C'-'    TRIPLE-SPACE
         FDPRT ' ',LEN=30
         FDPRT 'VOLUME'                                         GP12061
         FDPRT VOLSER,PAD                                       GP12061
         FDPRT 'HAS',PAD                                        GP12061
         FDTM  PROP,PCATLG,BNZ=TOTALMSC                          82137
         FDPRT TOTRACKS,I,RADJ,PAD,LEN=6
         FDPRT 'TRACKS LISTED FOR',PAD
TOTALMSC FDPRT TOTDSNS,I                                         82137
         FDPRT 'DATA SETS.',PAD
MASKFD   FDTM  ACCTPRIV,MASKVTOC,BO=MASKFDX   NO MASK
         AIF   ('&LOCAL' NE 'CCSI').NOLO4                       GP12050
         FDTM  ACCTPRIV,MASKVTU,BZ=MASKFDAT  NOT VTOC
         FDTM  VOLFLAGS+1,VF2DSNA+VF2DSNT,BZ=MASKFDX  NO MASK ON USRVOL
MASKFDA  FDPRT 'MASK: ACCT=',PADL
         FDPRT DSNACCT,4
         FDPRT *END
MASKFDAT FDTM  ACCTPRIV,MASKACCT,BO=MASKFDA
.NOLO4   ANOP  ,                                                GP12050
MASKFDN  FDPRT 'MASK:',PAD
         FDPRT DSNMASK,DEBR
MASKFDX  FDPRT *END
         SPACE 1
SNAPMSG  FDPRT ' ',LEN=20
         FDPRT AST30,PADR,LEN=5
         FDPRT 0(R3),24      VARIABLE TEXT
         FDPRT AST30,PADL,LEN=5
         FDPRT *END
ERRSNAP  DC    CL24'ERROR IN TEXT PROCESSING'
EOFSNAP  DC    CL24'END FILE READ - NO TEXT'
NOTEDIT  DC    CL24'DATA NOT IN EDIT FORMAT'
NOMEMB   DC    CL24'NO MEMBERS IN THIS PDS'
         SPACE 1
BADPARMG VCON  '-******* INCORRECT PARM FIELD *******'
         SPACE 1                                                 82137
MSGRENAM FDPRT DSNAME,PADR,NL                                    82137
         FDPRT RENAMSGQ                                          82137
         FDPRT 'ED TO'                                           82137
         FDPRT DS1DSNAM,PADL                                     82137
         FDPRT *END                                              82137
         SPACE 1                                                 82137
MSGRENFL FDPRT AST30,NL,LEN=L'AST30+15                           82137
         FDPRT 'RENAME FAILED; RETURN CODE'                      82137
         FDPRT DB,4,I                                            82137
         FDPRT AST30,RADJ,LEN=45                                 82137
         FDPRT *END                                              82137
         SPACE 1                                                 82137
MSGCATLG FDPRT DS1DSNAM,NL,LEN=L'DSNAME+8                       GP12061
         FDCLC DB,ZEROES,4,BE=MSGCATGD                           82137
         FDPRT 'NOT CATALOGED; RETURN CODE',PAD                  82137
         FDPRT DB,4,I                                            82137
         FDGOTO MSGCATCV                                         82137
MSGCATGD FDPRT 'CATALOGED',PADL,RADJ,LEN=9+4                     82137
MSGCATCV FDOPT SBA=(*,-12)                                       82137
         FDCLI CVOL,C'*',BE=MSGCATCX                             82137
         FDPRT 'CVOL='                                           82137
         FDPRT CVOL                                              82137
MSGCATCX FDPRT *END                                              82137
         SPACE 1                                                 82137
MSGUNAUT VCON  ' RESTRICTED PROGRAM'                             82137
         SPACE 2
         PRINT &PRTMAC       EXPAND LOCAL MACROS                 82137
SAVE     DSECT ,             SAVE/WORK AREA
DB       DC    D'0'
         SERVDEFS ,          SERVICE ROUTINE ADDRESSES, ETC.    GP12050
@VOLREAD DC    A(0)          VTOC READ ROUTINE
@INPREAD DC    A(0)          CARD READ ROUTINE
SAVE1T5  DC    5A(0)
ADDRTIOT DC    A(0)
         SPACE 1                                                 82137
         SERVCALC MF=D       EXPAND MAPPING FOR TRKCALC          82137
         SPACE 1                                                GP12061
         MAPPARSE DSECT=NO   EXPAND MAPPING FOR @PARSER         GP12061
         SPACE 1                                                 82137
ALLTRACK DC    F'0'          TRACKS LISTED FOR THIS RUN
ALLDSNS  DC    F'0'          NUMBER OF DATA SETS IN THIS RUN
MASKLEN  DC    F'0'
         SPACE 1                                                 82137
POPS     DC    AL1(0)        PROCESS OPTIONS                     82137
PREP     EQU   1               REPLACE CATALOG ENTRIES           82137
PWYLREP  EQU   2               REPLACE WYLXXX CATALOG ENTRIES    82137
PTEST    EQU   8               RUNNING IN TEST MODE ONLY         82137
OPTRENAM EQU   16              RENAME OPTION PRESENT             82137
OPTEXC   EQU   32              EXCLUDE CARDS PRESENT             82137
OPTINC   EQU   64              INCLUDE CARDS PRESENT             82137
OPTINCL  EQU   128             INCLUDE WAS FIRST OPTION          82137
PROP     DC    AL1(0)          PROCESS OPTIONS                  GP12061
PCATLG   EQU   1  (PGMHEAD)    WYLCATLG RATHER THAN WYLVTOC ENTRY
PSNAP    EQU   2               PRINT 5 LINES OF DATA             82137
PDIR     EQU   4               COUNT DIRECTORY BLOCKS            82137
PPDS     EQU   8               PRINT MEMBER NAMES                82137
PDSCB    EQU   16              PRINT DSCB LINE ONLY              82137
PAUTO    EQU   32              AUTOMATIC VOLUME RETRIEVAL (UCB LOOP)
PHEAD    EQU   64              PAGE HEADER PRINTED
PDATA    EQU   128             DATA SET FOUND ON THIS VOLUME
DSLFG    DC    X'00'         LOCAL COPY OF SNAP OPTIONS          82137
ACCTPRIV DC    X'00'         SAVE ACCOUNT PRIVILEGES
MASKVTOC EQU   X'10'         ENTIRE VTOC - NO MASK TESTING
MASKVTU  EQU   X'08'         ENTIRE VTOC, EXCEPT ON PUBLIC VOLUMES
MASKACCT EQU   X'04'         CHECK ACCOUNT NUMBER ONLY
MASKSUBA EQU   X'02'         CHECK WITH SUB-ACCOUNT MASK
MASKUID  EQU   X'01'         CHECK BY USER-ID
         SPACE 1                                                 82137
LIBCLC   CLC   LIBMASK(5),DS1DSNAM-IECSDSL1(R1)  LIBPAK ACCOUNT GP12061
WYLCLC   CLC   WYLMASK(8),DS1DSNAM-IECSDSL1(R1)  WYLBUR 4/8 ACC GP12061
TSOCLC   CLC   DSNMASK(0),DS1DSNAM-IECSDSL1(R1)  TSO/GENERIC    GP12061
VOLFLAGS DC    H'0'          VOLUME ATTRIBUTE FLAGS
VOLSER   DC    CL6' '        CURRENT VOLUME SERIAL
VOLUNIT  DC    CL3' '        CURRENT UCB NAME
VOLDDNM  DC    CL8' '        CURRENT DD NAME
VOLTYPE  DC    CL8' '        DEVICE TYPE
DSNACCT  DC    CL8' '
DSNACCN  DC    CL8' '        INTERNAL ACCOUNT                    85252
WYLMASK  DC    C'WYL.acctsuba'    WYLBUR MASK L=8 or 12
LIBMASK  DC    C'acct.'           LIBPAK MASK L=5
DSNMASK  DC    CL44' '       TSO/GENERIC MASK
DSNAME   DC    CL44'DSNAME'  CURRENT DSN (BEFORE RENAME)         82137
         SPACE 1
CLEARVOL EQU   *             START OF CLEAR FOR NEW VOLUME
TOTRACKS DC    F'0'          TRACKS LISTED FOR THIS VOLUME
TOTDSNS  DC    H'0'          NUMBER OF DATA SETS LISTED
CLEARDSN EQU   *             START OF CLEAR FOR NEW DATA SET
TRACKS   DC    F'0'          TRACKS IN CURRENT DATA SET
TRACKUSE DC    F'0'          TRACKS USED IN CURRENT DATA SET
DIRALL   DC    H'0'          DIRECTORY BLOCKS ALLOCATED
DIRUSE   DC    H'0'          DIRECTORY BLOCKS IN USE
FLAGDSN  DC    X'00'         DSN PROCESSING FLAG
FGDSN    EQU   X'80'           DSN PRINT LINE DONE
FGFMT3   EQU   X'40'           ERROR GETTING FORMAT 3
FGDEOF   EQU   X'10'           DIRECTORY END
FGMEM    EQU   X'08'           MEMBER FOUND
FGWYL    EQU   X'04'           WYLBUR LIBRARY DIRECTORY FORMAT
FGERR    EQU   X'01'           DIRECTORY ERROR
CLEARLEN EQU   *-CLEARDSN    LENGTH TO CLEAR FOR NEW DATA SET
         SPACE 1
BLANKONE DC    C' '          BLANKDSN-1
BLANKDSN EQU   *             START OF AREA TO BLANK FOR NEW DSN
INT      DC    CL10' '       DATE & WYLBUR LINE EDITING
ALODATE  DC    C'00/00/00'
LASTUSE  DC    C'00/00/00'
DSORG    DC    CL3' '   1/5  DSORG                               82137
RECFM    DC    CL6' '   2/5  RECFM                               82137
OPTCD    DC    CL8' '   3/5  OPTCD                               82137
BLKSIZE  DC    CL5' '   4/5  BLKSIZE                             82137
LRECL    DC    CL5' '   5/5  LRECL                               82137
LENFMT   EQU   *-DSORG       LENGTH TO MOVE                      82137
ALLOCTYP DC    CL3' '
         DC    C' '          MEMLONG-1 BLANK
MEMLONG  DS    0CL21         MAPPING FOR LONG MEMBER ENTRY
         DC    CL2' '
MEMSHORT DS    0CL10         SHORT MEMBER ENTRY
         DC    C' '          SEPARATOR
MEMALIAS DC    C' '          - FOR ALIAS
MEMNAME  DC    CL8' '        MEMBER NAME
         DC    C' '
MEMDATE  DC    CL8' '        ADD/CHANGE DATE OF MEMBER
BLANKDSL EQU   *-BLANKDSN    LENGTH TO BLANK
VARFD    DC    XL(PATFDLEN)'0'  VARIABLE LENGTH PRINT FD
         SPACE 1                                                 82137
         SERVPDS DSECT=NO,RETURN=YES,VER=1                      GP12050
         SPACE 1                                                 82137
WYLDCOMP SERVCOMP DSECT=NO,PFX=DCM
         SPACE 1                                                 82137
         PRINT &PRTSYS                                           82137
DSNAMES  DC    A(0)          RENAME/OPTION LIST ADDRESS          82137
CVOL     DC    CL6' '        CATALOG VOLSER                      82137
CATLIST  DC    H'1'          CATALOG VOLUME LIST                 82137
CATTYPE  DC    XL4'0'        VOLUME TYPE                         82137
CATSER   DC    CL6' '        RESIDENCE VOLUME                    82137
         DC    H'0'          SEQUENCE #                          82137
ALTCAT   DC    CL6' '        ALTERNATE UNCATALOG VOLUME          82137
RENAMSGQ DC    C'TRUNCAT'    TRUNCATE OR RENAME MESSAGE          82137
         SPACE 1                                                 82137
LINECARD VCON  ' ',END=LINECARN                                  82137
WORKDSN  DC    0CL88' '      RENAME WORK AREA                    82137
CARDMSG  DC    CL25' '                                           82137
         DC    CL5' '                                            82137
         DC    C''''                                             82137
CARDLINE DC    CL80' '                                           82137
         DC    C''''                                             82137
         VCON  ,                                                 82137
         SPACE 1                                                 82137
DOCAT    CAMLST  CATBX,DSNAME,CVOL,CATLIST  INDEX AND CATALOG    82137
RECAT    CAMLST  RECAT,DSNAME,CVOL,CATLIST  RECATALOG            82137
LOOCAT   CAMLST  NAME,DSNAME,CVOL,CATWORK                        82137
UNCAT    CAMLST UCATDX,DSNAME,CVOL  DELETE OLD NAME              82137
RENLIST  CAMLST RENAME,DSNAME,DS1DSNAM,CATLIST  RENAME           82137
CATWORK  DC    35D'0'        CATALOG AND DSCB WORK AREA          82137
         DS    0D
         IECSDSL1 1          FORMAT 1
CCHHFMT1 DS    XL5           ADDRESS OF FORMAT 1 DSCB            82137
LENDSCB1 EQU   *-DS1DSNAM    LENGTH TO MOVE
SAVEEND  EQU   *                                                GP12050
         EJECT ,                                                 82137
WYLVTOC  CSECT ,             SYSIN CARD PROCESSING               82137
         PRINT &PRTSOR                                           82137
*                                                                82137
* SYSIN CARDS (FREE FORMAT, COLUMNS 1-80,                        82137
*              OR COLUMNS 1-72 WHEN 73-80 ARE SEQUENCED)         82137
*   GENERAL FORMAT:                                              82137
*   FIRST NON-BLANK FIELD - OPTIONS                              82137
*   SECOND NON-BLANK FIELD - DATA SET NAME, DSN PREFIX, OR MASK  82137
*   THIRD NON-BLANK FIELD - RENAME OPTION REPLACEMENT STRING     82137
*                                                                82137
*   OPTIONS                                                      82137
*    * - COMMENTS CARD - PRINTED AS READ                         82137
*    S - SELECT DSN   (ALTERNATE FORM)                           82137
*    I - INCLUDE DATA SET                                        82137
*    X - EXCLUDE DSN  (ALTERNATE FORM)                           82137
*    E - EXCLUDE DATA SET                                        82137
*    R - RENAME DATA SET                                         82137
*    D - PRINT DSCB ONLY; NO COUNTS, NO MEMBERS, NO SNAP         82137
*    C - PRINT DSCB AND COUNT DIRECTORY BLOCKS                   82137
*    M - D+C AND LIST MEMBER NAMES                               82137
*    P - D+C+M AND PRINT 5 LINES PER DATA SET/MEMBER             82137
         DS    0H                                                82137
         SPACE 1                                                 82137
PARMLOOK PRTV  SUBPARM,TITLE=3                                   82137
         TM    PROP,PCATLG   WYLCATLG ?                          82137
         BZ    TESTSYDD      NO; NO JSCBPASS                     82137
         SPACE 1                                                 82137
*   GET RID OF PASSWORD PROTECTION TO BYPASS CATLG/RENAME CHECKS 82137
         TESTAUTH FCTN=1     PRIVILEGED ?                        82137
         BXH   R15,R15,TESTSYDD  NO; SKIP                        82137
         L     R2,CVTPTR              CVT                        82137
         USING CVTMAP,R2                                         82137
         L     R2,CVTTCBP    GET TCB POINTERS                    82137
         L     R2,4(,R2)     OUR TCB                             82137
         USING TCB,R2                                            82137
         MODESET KEY=ZERO    ENTER PROTECTION KEY ZERO           82137
         AIF   ('&LOCAL' NE 'IIH' AND '&LOCAL' NE 'CCSI').NOUSE  82137
         OI    TCBUSER,TUFPASS   DTS/TECHNION MODIFICATION       82137
.NOUSE   L     R2,TCBJSCB    JSCB                                82137
         USING IEZJSCB,R2                                        82137
         OI    JSCBSWT1,JSCBPASS **MVT/SVS ZAP TO IFG0195T REQUIRED**
         DROP  R2                                                82137
         MODESET KEY=NZERO                                       82137
         SPACE 1                                                 82137
TESTSYDD SERVLOAD @INPREAD   GET THE READ ROUTINE               GP12050
         INPOPEN SYSIN,OPT=(DUMMY,NOWTO)  TEST FOR SYSIN         82137
         BXH   R15,R15,SQUIT     NO; IGNORE                      82137
         PRTSPACE 1          SPACER                              82137
         L     R9,=A(SYNSIZE)  GET USER REQUESTED SPACE          82137
         GETMAIN R,LV=(R9)                                       82137
         LR    R4,R1         COPY ADDRESS                        82137
         LR    R5,R9         COPY LENGTH                         82137
         SLR   R14,R14                                           82137
         SLR   R15,R15                                           82137
         MVCL  R4,R14        CLEAR GOTTEN STORAGE                82137
         ST    R1,DSNAMES    SAVE DSN LIST AND CLEAR HIGH BYTE   82137
         XC    0(8,R1),0(R1)  CLEAR START/CHAIN BYTE             82137
         LR    R8,R1         SET NEXT ENTRY ADDRESS              82137
         USING RIXLIST,R8                                        82137
         SPACE 1                                                 82137
SYNLOOP  INPGET ,            TAKE A CARD, ANY CARD               82137
         BXH   R15,R15,SYNEOF   QUIT IF END OR ERROR             82137
         MVC   CARDLINE,0(R1) COPY CARD                          82137
         LA    R6,80         SET LENGTH OF CARD                  82137
         CLI   72(R1),C' '   BLANK SEQUENCE FIELD ?              82137
         BE    SYNSHORT      HOPE SO                             82137
         MVC   DB,=8C'0'                                         82137
         NC    DB,72(R1)                                         82137
         CLC   DB,=8C'0'     ALL NUMERIC SEQUENCE FIELD ?        82137
         BE    SYNSHORT      YES; SKIP IT                        82137
         CLI   72(R1),C'0'   NUMERIC FIRST DIGIT ?               82137
         BL    SYNLONG       NO                                  82137
         CLI   76(R1),C'.'   WYLBUR SEQUENCE ?                   82137
         BNE   SYNLONG       NO                                  82137
SYNSHORT MVI   72(R1),C' '   SKIP SEQUENCE FIELD                 82137
         MVC   73(7,R1),72(R1)  BLANK IT OUT                     82137
SYNLONG  LR    R2,R1         POINT TO FIRST BYTE                 82137
         SLR   R3,R3         SET CURRENT STRING LENGTH           82137
         SLR   R5,R5         SET LENGTH OF SECOND STRING         82137
SYNOPT   CLI   0(R2),C' '    LEADING BLANK ?                     82137
         BNE   SYNOLOOK                                          82137
         LA    R2,1(,R2)                                         82137
         BCT   R6,SYNOPT                                         82137
         B     CARDPUT       COPY BLANK CARD                     82137
SYNOLOOK CLI   0(R2),C'*'    COMMENT ?                           82137
         BE    CARDPUT       YES; PRINT IT                       82137
SYNOTAB  LA    R14,OPTTAB    GET OPTION ABBREVIATIONS            82137
         LA    R15,(OPTTABE-OPTTAB)/2  AND ENTRIES               82137
         TM    PROP,PCATLG   WYLCATLG OPTION ?                   82137
         BZ    SYNOOPT       NO                                  82137
         LA    R14,OPTTABC   POINT TO WYLCATLG OPTIONS           82137
         LA    R15,(OPTTABCE-OPTTABC)/2  AND NUMBER              82137
SYNOOPT  CLC   0(1,R2),0(R14)  MATCHING OPTION ?                 82137
         BE    SYNOMACH      YES                                 82137
         LA    R14,2(,R14)                                       82137
         BCT   R15,SYNOOPT                                       82137
SYNOOPTI MVC   CARDMSG(1),0(R2)  SHOW BAD VALUE                  82137
         MVC   CARDMSG+2(14),=C'INVALID OPTION'                  82137
         B     CARDERRQ      PRINT AND QUIT                      82137
SYNOMACH OC    RIXFG,1(R14)  COPY OPTION BIT                     82137
SYNOMAC2 LA    R2,1(,R2)                                         82137
         BCT   R6,*+8                                            82137
         B     CARDMIS1      SHOW FIRST NAME FIELD MISSING       82137
         CLI   0(R2),C' '    END OF OPTIONS ?                    82137
         BNE   SYNOTAB                                           82137
         MVC   CARDMSG(3),=C'R X'   PRESET OPTIONS               82137
         TM    RIXFG,RIXREN+RIXEXC  RENAME+EXCLUDE ?             82137
         BO    CARDEXCL      SHOW EXCLUSIVE                      82137
         MVC   CARDMSG(3),=C'I X'                                82137
         TM    RIXFG,RIXINC+RIXEXC                               82137
         BO    CARDEXCL                                          82137
         BNZ   SYNONEXC      OK IF ONE OR THE OTHER              82137
         CLI   RIXFG,0       ANY OPTIONS ?                       82137
         BNZ   SYNONEXC      YES                                 82137
         MVC   CARDMSG(23),=C'INCLUDE/EXCLUDE MISSING'           82137
         B     CARDERRQ                                          82137
SYNONEXC MVC   CARDMSG(3),CARDMSG-1  CLEAR AGAIN                 82137
         TM    RIXFG,RIXINC  INCLUDE OPTION ?                    82137
         BZ    SYNOEXC       NO                                  82137
         TM    POPS,OPTEXC+OPTINC  FIRST TIME ?                  82137
         BNZ   *+8           NO                                  82137
         OI    POPS,OPTINCL  SHOW INCLUDE FIRST                  82137
         OI    POPS,OPTINC   SHOW AT LEAST ONE INCLUDE           82137
         B     SYN1LOOP                                          82137
SYNOEXC  TM    RIXFG,RIXEXC  EXCLUDE ?                           82137
         BZ    SYN1LOOP      NO                                  82137
         OI    POPS,OPTEXC   SHOW EXCLUDE PRESENT                82137
SYN1LOOP CLI   0(R2),C' '    LEADING BLANK ?                     82137
         BNE   SYN1TEXT      NO                                  82137
         LA    R2,1(,R2)                                         82137
         BCT   R6,SYN1LOOP                                       82137
CARDMIS1 MVC   CARDMSG(25),=C'DSNAME/MASK FIELD MISSING'         82137
         B     CARDERRQ      PRINT AND QUIT                      82137
SYN1TEXT LR    R4,R2         SAVE START OF FIRST TEXT STRING     82137
         CLI   0(R4),C' '    END OF STRING ?                     82137
         BE    SYN2LOOK      YES; SCAN FOR NEXT                  82137
         LA    R3,1(,R3)     UP TEXT LENGTH                      82137
         LA    R4,1(,R4)     UP TEXT LOCATION                    82137
         BCT   R6,SYN1TEXT+2  TRY AGAIN                          82137
         B     CARDERR                                           82137
SYN2LOOK TM    RIXFG,RIXREN  TWO STRING OPTION ?                 82137
         BZ    SYN3TEST      NO; SKIP SECOND ONE                 82137
SYN2LOOP CLI   0(R4),C' '    START OF SECOND STRING              82137
         BNE   SYN2TEXT                                          82137
         LA    R4,1(,R4)                                         82137
         BCT   R6,SYN2LOOP                                       82137
         B     CARDERR                                           82137
SYN2TEXT LR    R15,R4        SAVE SECOND STRING ADDRESS          82137
         CLI   0(R15),C' '   END OF SECOND STRING ?              82137
         BE    SYN3TEST      YES; CHECK EVERYTHING               82137
         LA    R15,1(,R15)                                       82137
         LA    R5,1(,R5)     UP STRING LENGTH                    82137
         BCT   R6,SYN2TEXT+2  TRY AGAIN                          82137
SYN3TEST CH    R3,=H'44'     SHORT ENOUGH ?                      82137
         BH    CARDERR                                           82137
         CH    R5,=H'44'                                         82137
         BH    CARDERR                                           82137
         LA    R15,RIXFIX+1(R3,R5)  LENGTH REQUIRED IN TABLE     82137
         CR    R15,R9        WILL IT FIT ?                       82137
         BH    CARDFULL      NO; QUIT WITH ERROR                 82137
         STC   R3,RIXL1      SET LENGTH OF FIRST STRING          82137
         LA    R15,RIXMASK   SET LOCATION OF FIRST STRING        82137
         EX    R3,MVCSTR1    MOVE FIRST STRING                   82137
         STC   R5,RIXL2      SET LENGTH OF SECOND STRING         82137
         LR    R14,R3        GET LENGTH OF FIRST STRING          82137
         SLR   R0,R0         INIT COUNT OF REPLACABLE MATCH BYTES
SYN1REPL CLI   0(R15),C'?'   GENERAL MATCH BYTE ?                82137
         BE    SYN1REPM      MASK BYTE                           82137
         CLI   0(R15),X'7F'  ALTERNATE MASK BYTE ?               82137
         BNE   SYN1REPN      NO                                  82137
         AH    R0,=H'1'      UP COUNT OF COPYABLE MATCH BYTES    82137
SYN1REPM OI    RIXF2,RIXCMSK  SET COMPARE MASK PRESENT           82137
         B     SYN1REPX      SKIP TO NEXT BYTE                   82137
SYN1REPN CLI   0(R15),C';'   PRECISE STRING ?                    82137
         BE    SYN1REPB      YES; REPLACE BY BLANK               82137
         CLI   0(R15),C'!'   EXCLAMATION ?                       82137
         BNE   SYN1REPX      NO; BUMP                            82137
SYN1REPB MVI   0(R15),C' '   FORCE EXACT MATCH                   82137
SYN1REPX LA    R15,1(,R15)                                       82137
         BCT   R14,SYN1REPL  DO ALL                              82137
         TM    RIXFG,RIXREN  TWO STRING OPTION ?                 82137
         BZ    SYNTINC       NO; SKIP SECOND STRING              82137
         LR    R2,R4                                             82137
         LA    R15,RIXMASK(R3)  POINT TO SECOND STRING           82137
         EX    R5,MVCSTR1    MOVE SECOND STRING                  82137
         LA    R14,1(,R5)    GET NUMBER OF BYTES TO CHECK        82137
SYN3REPM CLI   0(R15),C'?'   BAD REPLACE PARM ?                  82137
         BE    CARDERR       YES; FAIL                           82137
         CLI   0(R15),X'7F'  REPLACEMENT MASK ?                  82137
         BNE   SYN3REPX      NO                                  82137
         BCTR  R0,0          SUBTRACT ONE                        82137
         OI    RIXF2,RIXRMSK  SET REPLACEMENT MASK PRESENT       82137
SYN3REPX LA    R15,1(,R15)   NEXT BYTE                           82137
         BCT   R14,SYN3REPM  REPEAT                              82137
         LTR   R0,R0         EXCESSIVE REPLACES ?                82137
         BNM   SYNTINC       NO                                  82137
         MVC   CARDMSG(25),=C'REPLACE STRING - EXCESS "'         82137
         B     CARDERRQ      FAIL                                82137
SYNTINC  OC    DSNAMES(1),RIXFG  COLLECT GLOBAL FLAGS            82137
         LA    R8,RIXFIX(R8,R5) POINT TO NEXT ENTRY              82137
         AR    R8,R3                                             82137
         XC    RIXFG(3),RIXFG SET END OF CHAIN                   82137
         LA    R15,RIXFIX(R3,R5)  SET ENTRY LENGTH               82137
         SR    R9,R15        INTO SPACE REMAINING                82137
CARDPUT  PRTV  LINECARD      PRINT THE LINE                      82137
         MVC   CARDMSG,CARDMSG-1  CLEAR MESSAGE AREA             82137
         B     SYNLOOP       GET ANOTHER CARD                    82137
         SPACE 1                                                 82137
CARDEXCL MVC   CARDMSG+4(21),=C'ARE EXCLUSIVE OPTIONS'           82137
         B     CARDERRQ                                          82137
CARDERR  MVC   CARDMSG(12),=C'INVALID TEXT'                      82137
CARDERRQ OICC  8             SET ERROR                          GP12050
         B     CARDPUT                                           82137
         SPACE 1                                                 82137
CARDFULL MVC   CARDMSG(14),=C'DSN TABLE FULL'                    82137
         PRTV  LINECARD                                          82137
         PRTV  SYNSYSMG                                          82137
         B     SQUITX                                            82137
         SPACE 1                                                 82137
SYNEOF   MVC   CARDMSG(LINECARN-CARDMSG),CARDMSG-1  CLEAR FOR WORK
         L     R1,DSNAMES    GET DSN TABLE                       82137
         CLI   0(R1),0       ANY ENTRIES ?                       82137
         BNE   SQUIT         YES; LEAVE IT                       82137
         L     R0,=A(SYNSIZE)  GET USER REQUESTED SPACE          82137
         FREEMAIN R,A=(1),LV=(0)   FREE THE TABLE SPACE          82137
         XC    DSNAMES,DSNAMES  AND CLEAR POINTER                82137
SQUIT    PRTSPACE 1          PRINT SPACER                        82137
         SERVCALL LPADL,=CL8'@INPREAD'   CLOSE AND FREE SYSIN    82137
SQUITB   ICM   R15,15,RETCODE  GET RETURN CODE
         BZ    INITDONE      ZERO - COMPLETED INITIALIZTION      82137
SQUITX   B     JOBABEND      QUIT                                82137
         SPACE 1                                                 82137
SYSIN    INPWORK SYSIN,FILL=C' ',WIDTH=80,EDIT=128               82137
MVCSTR1  MVC   0(0,R15),0(R2)                                    82137
SUBPARM  VCON  ' SYSIN REQUESTS'                                 82137
         DROP  R8                                                82137
         SPACE 1                                                 82137
SYNSYSMG VCON  '0CONTACT THE SYSTEM GROUP FOR ASSISTANCE'        82137
         SPACE 1                                                 82137
OPTTABC  DC    C'R',AL1(RIXREN)  RENAME                          82137
OPTTAB   DC    C'X',AL1(RIXEXC)  EXCLUDE                         82137
         DC    C'E',AL1(RIXEXC)  EXCLUDE                         82137
         DC    C'I',AL1(RIXINC)  INCLUDE                         82137
         DC    C'S',AL1(RIXINC)  INCLUDE(SELECT)                 82137
OPTTABCE EQU   *             END OF TABLE FOR WYLCATLG           82137
         DC    C'D',AL1(RIXDSCB)  PRINT DSCB LINE ONLY           82137
         DC    C'C',AL1(RIXDIR+RIXDSCB)  COUNT DIRECTORY BLOCKS  82137
         DC    C'M',AL1(RIXPDS+RIXDIR+RIXDSCB)  PRINT MEMBERS ALSO
         DC    C'P',AL1(RIXSNAP+RIXPDS+RIXDIR+RIXDSCB)  PRINT TEXT
OPTTABE  EQU   *                                                 82137
         EJECT ,                                                 82137
DSNCHOSE STM   R14,R12,12(R13)  EXCLUSION/SELECTION TESTS        82137
         MVC   DSLFG,PROP    COPY BASIC FORMATTING OPTIONS      GP12061
         NI    DSLFG,PDSCB+PPDS+PDIR+PSNAP  LEAVE ONLY OUTPUT   GP12061
         MVI   DB,RIXINC     PROVISIONALLY SET COPY ON           82137
         TM    DSNAMES,255-RIXREN  ANY OTHER THAN RENAME ?       82137
         BZ    DSCCONX       NO; SKIP                            82137
         LAT   R6,DSNAMES,DSCCONX  GET TABLE OR QUIT             82137
         USING RIXLIST,R6    DECLARE IT                          82137
         TM    DSNAMES,RIXINC+RIXEXC  EITHER SELECTION ?         82137
         BZ    DSNCHOSF      NO; LEAVE ON                        82137
         TM    POPS,OPTINCL  INCLUDES ONLY ?                     82137
         BZ    DSNCHOSF      NO; LEAVE ON                        82137
         MVI   DB,0          RESET COPY FLAG                     82137
DSNCHOSF LA    R1,DS1DSNAM+43    POINT TO LAST BYTE OF DSN       82137
         LA    R8,44         AND MAX LENGTH                      82137
         LR    R14,R8        SAVE MAXIMUM                        82137
DSCDSCL  CLI   0(R1),C' '    NON-BLANK ?                         82137
         BNE   DSCDSCHL      YES; HAVE LENGTH                    82137
         BCTR  R1,0          TRY PRECEDING BYTE                  82137
         BCT   R8,DSCDSCL    AND CHOP LENGTH                     82137
         B     DSCCONX       DON'T LIKE SHORT NAMES              82137
DSCDSCHL CR    R8,R14        FULL LENGTH NAME ?                  82137
         BNL   *+8           YES; LEAVE IT                       82137
         LA    R8,1(,R8)     LEAVE ONE TRAILING BLANK FOR EXACT MATCH
DSCDSCLP CLI   RIXFG,0       END OF LIST ?                       85253
         BE    DSCCONX       YES; EXIT                           82137
         SLR   R14,R14       LENGTH OF MASK                      82137
         IC    R14,RIXL1     GET LENGTH OF MASK                  82137
         SLR   R1,R1         FOR LENGTH OF REPLACEMENT           82137
         IC    R1,RIXL2      GET LENGTH OF REPLACEMENT           82137
         TM    RIXFG,255-RIXREN  ANY OTHER THAN RENAME ?         82137
         BZ    DSCNDSC       NO; SKIP IT                         82137
         CR    R14,R8        IS MASK LONGER THAN DSN ?           82137
         BH    DSCNDSC       YES; TRY NEXT ENTRY                 82137
         LA    R2,RIXMASK    GET MASK ADDRESS                    82137
         LR    R3,R14        AND MASK LENGTH                     82137
         LA    R9,DS1DSNAM       AND DSNAME                      82137
DSCMDSC  CLI   0(R2),X'7F'   NON-COMPARE ?                       82137
         BE    *+14          YES; SKIP TEST                      82137
         CLC   0(1,R2),0(R9) .  EQUAL ?                          82137
         BNE   DSCNDSC       NO - RETURN UNEQUAL                 82137
         LA    R2,1(,R2)     NEXT MASK BYTE                      82137
         LA    R9,1(,R9)     NEXT DATA BYTE                      82137
         BCT   R3,DSCMDSC    NEW MASK LENGTH                     82137
         TM    RIXFG,RIXDSCB+RIXDIR+RIXPDS+RIXSNAP  FORMATTING OPTIONS?
         BZ    *+10          NO; NO CHANGE                       82137
         MVN   DSLFG,RIXFG   SET NEW OPTIONS                     82137
         TM    RIXFG,RIXINC  INCLUDE REQUEST ?                   82137
         BZ    DSCEXCT       NO; CHECK EXCLUDE                   82137
         OI    DB,RIXINC     SET INCLUDE FLAG                    82137
         TM    DSNAMES,255-RIXREN-RIXINC   ANY MORE ?            82137
         BZ    DSCCONX       NO; EXIT NOW                        82137
         B     DSCNDSC       ELSE CHECK REMAINING ENTRIES        82137
DSCEXCT  TM    RIXFG,RIXEXC  EXCLUDE ?                           82137
         BZ    DSCNDSC       NO; TRY AGAIN                       82137
         MVI   DB,0          RESET COPY FLAG                     82137
         TM    DSNAMES,255-RIXREN-RIXEXC  ANY MORE ?             82137
         BZ    DSCCONX       NO; EXIT                            82137
         SPACE 1                                                 82137
DSCNDSC  LA    R6,RIXFIX(R14,R6)                                 82137
         AR    R6,R1         GET NEXT DSC LOCATION               82137
         B     DSCDSCLP      AND DO IT                           85253
         SPACE 1                                                 82137
DSCCONX  TM    DB,RIXINC     COPY THIS DATA SET ?                82137
         BNZ   DSCCON4       YES; COPY THIS ONE                  82137
DSCCON0  LM    R14,R12,12(R13)  RESTORE REGISTERS                82137
         BR    R14           RETURN DATA SET TO BE SKIPPED       82137
DSCCON4  LM    R14,R12,12(R13)  RESTORE REGISTERS                82137
         B     4(,R14)       RETURN DATA SET TO BE PROCESSED     82137
         SPACE 3                                                 82137
DSNCONV  STM   R14,R12,12(R13)  RENAME CHECK                     82137
*        DATA SET RENAME TEST AND PROCESSING                     82137
         SPACE 1                                                 82137
         TM    DSNAMES,RIXREN  ANY RENAME REQUESTS ?             82137
         BZ    DSNCONX       NO; RETURN                          82137
         LAT   R6,DSNAMES,DSNCONX  GET DSN NAME LIST OR EXIT     82137
         USING RIXLIST,R6    DECLARE MAPPING                     82137
         LA    R1,DS1DSNAM+43    POINT TO LAST BYTE OF DSN       82137
         LA    R8,44         AND MAX LENGTH                      82137
         LR    R14,R8        SAVE MAXIMUM                        82137
DSNDSNL  CLI   0(R1),C' '    NON-BLANK ?                         82137
         BNE   DSNDSNHL      YES; HAVE LENGTH                    82137
         BCTR  R1,0          TRY PRECEDING BYTE                  82137
         BCT   R8,DSNDSNL    AND CHOP LENGTH                     82137
         B     DSNCONX       DON'T LIKE SHORT NAMES              82137
DSNDSNHL CR    R8,R14        FULL LENGTH NAME ?                  82137
         BNL   *+8           YES; LEAVE IT                       82137
         LA    R8,1(,R8)     LEAVE ONE TRAILING BLANK FOR EXACT MATCH
DSNDSNLP CLI   RIXFG,0       END OF LIST ?                       85253
         BE    DSNCONX       YES; EXIT                           82137
         SLR   R14,R14       LENGTH OF MASK                      82137
         IC    R14,RIXL1     GET LENGTH OF MASK                  82137
         SLR   R1,R1         FOR LENGTH OF REPLACEMENT           82137
         IC    R1,RIXL2      GET LENGTH OF REPLACEMENT           82137
         TM    RIXFG,RIXREN  RENAME ENTRY ?                      82137
         BZ    DSNNDSN       NO; SKIP IT                         82137
         CR    R14,R8        IS MASK LONGER THAN DSN ?           82137
         BH    DSNNDSN       YES; TRY NEXT ENTRY                 82137
         LA    R2,RIXMASK    GET MASK ADDRESS                    82137
         LR    R3,R14        AND MASK LENGTH                     82137
         LA    R4,DS1DSNAM       AND DSNAME                      82137
         TM    RIXF2,RIXCMSK  COMPARE MASK ?                     82137
         BNZ   DSNMDSN       YES; COMPARE BYTE BY BYTE           82137
         BCTR  R3,0          DECREASE MASK LENGTH                82137
         EX    R3,MASKCLC    COMPARE                             82137
         BNE   DSNNDSN       NOT EQUAL                           82137
         B     DSNQDSN       EQUAL - MOVE IT                     82137
MASKCLC  CLC   0(0,R2),0(R4)  COMPARE PREFIX TO DSN              82137
DSNMDSN  CLI   0(R2),X'7F'   NON-COMPARE ?                       82137
         BE    DSNMDSNX      YES; SKIP TEST                      82137
         CLI   0(R2),C'?'    NON-COPY MASK BYTE ?                82137
         BE    DSNMDSNX      YES; ACCEPT                         82137
         CLC   0(1,R2),0(R4)   EQUAL ?                           82137
         BNE   DSNNDSN       NO - RETURN UNEQUAL                 82137
DSNMDSNX LA    R2,1(,R2)     NEXT MASK BYTE                      82137
         LA    R4,1(,R4)     NEXT DATA BYTE                      82137
         BCT   R3,DSNMDSN    NEW MASK LENGTH                     82137
DSNQDSN  MVC   WORKDSN(88),WORKDSN-1  CLEAR NEW DSNAME WORK FIELD
         LA    R2,43                                             82137
         SR    R2,R14        LENGTH TO MOVE                      82137
         BM    DSNNTRAL      SKIP TRAILER                        82137
         LA    R3,DS1DSNAM(R14)  GET FIRST BYTE OF TRAILER       82137
         LA    R4,WORKDSN(R1)  NEW LOCATION                      82137
         EX    R2,TRAILMVC   MOVE IT                             82137
DSNNTRAL LA    R4,WORKDSN    GET HEADER DEST                     82137
         LA    R3,RIXREP(R14)  REPLACEMENT HEADER                82137
         LR    R2,R1         HEADER LENGTH                       82137
         BCTR  R2,0          LESS ONE                            82137
         LTR   R2,R2         ANY ?                               82137
         BM    DSNQMVC       NO; JUST PUT IT BACK                82137
         EX    R2,TRAILMVC   MOVE HEADER                         82137
DSNQMVC  MVC   DSNAME,DS1DSNAM  SAVE OLD DSNAME                  82137
         TM    RIXF2,RIXRMSK  REPLACEMENT STRING IS MASK ?       82137
         BZ    DSNQMDON      NO; SKIP                            82137
         LR    R15,R14       GET MASK LENGTH                     82137
         LR    R0,R1         GET REPLACEMENT LENGTH              82137
         LA    R2,RIXMASK    MASK ADDRESS                        82137
         LA    R3,DSNAME     OLD DSN                             82137
         LA    R4,WORKDSN    NEW DSN                             82137
DSNQMLOP CLI   0(R2),X'7F'   INPUT MASK BYTE ?                   82137
         BNE   DSNQMBMP      NO                                  82137
DSNQMLOO CLI   0(R4),X'7F'   OUTPUT REPLACE BYTE ?               82137
         BE    DSNQMEQ       YES; REPLACE                        82137
         LA    R4,1(,R4)                                         82137
         BCT   R0,DSNQMLOO   TRY AGAIN                           82137
         B     DSNQMDON                                          82137
DSNQMEQ  MVC   0(1,R4),0(R3)  REPLACE THE BYTE                   82137
         LA    R4,1(,R4)                                         82137
         BCT   R0,DSNQMBMP   BUMP IF MORE                        82137
         B     DSNQMDON      ELSE QUIT                           82137
DSNQMBMP LA    R2,1(,R2)                                         82137
         LA    R3,1(,R3)                                         82137
         BCT   R15,DSNQMLOP  REPEAT                              82137
DSNQMDON MVC   DS1DSNAM(44),WORKDSN  SET NEW NAME                82137
         MVC   RENAMSGQ,=CL7'  RENAM' 'RENAMED' MESSAGE          82137
         CLI   WORKDSN+44,C' '  OVERFLOW ?                       82137
         BNE   DSNQSPIL      YES; TOO BAD                        82137
         CLC   WORKDSN+45(43),WORKDSN+44  ALL BLANK ?            82137
         BE    DSNCONX       YES; LEAVE IT                       82137
DSNQSPIL MVC   RENAMSGQ,=CL7'TRUNCAT'  SET 'TRUNCATED'           82137
         OICC  2             SET MINOR ERROR                    GP12050
DSNCONX  LM    R14,R12,12(R13)  RESTORE                          82137
         BR    R14           AND RETURN                          82137
         SPACE 1                                                 82137
DSNNDSN  LA    R6,RIXFIX(R14,R6)                                 82137
         AR    R6,R1         GET NEXT DSN LOCATION               82137
         B     DSNDSNLP      AND DO IT                           85253
         DROP  R6                                                82137
         SPACE 1                                                 82137
TRAILMVC MVC   0(0,R4),0(R3)  MOVE DSN PORTION                   82137
         SPACE 1                                                 82137
         LTORG ,                                                 82137
         SPACE 1                                                 82137
RIXLIST  DSECT ,     (OPTIONAL) RENAME/INCLUDE/EXCLUDE DSN LIST  82137
RIXFG    DC    X'0'          ENTRY TYPE(S)                       82137
RIXREN   EQU   OPTRENAM        RENAME DSN / INPUT MASK / OUTPUT PFX
RIXINC   EQU   OPTINC          INCLUDE DSN / INPUT MASK          82137
RIXEXC   EQU   OPTEXC          EXCLUDE DSN / INPUT MASK          82137
RIXDSCB  EQU   PDSCB           PRINT DSCB LINE ONLY              82137
RIXPDS   EQU   PPDS            COUNT DIRECTORY BLOCKS ONLY       82137
RIXDIR   EQU   PDIR            PRINT MEMBER NAMES                82137
RIXSNAP  EQU   PSNAP           PRINT 5 LINES OF TEXT             82137
RIXF2    DC    X'0'          SECOND FLAG BYTE                    82137
RIXCMSK  EQU   X'80'           COMPARISON IS MASKED              82137
RIXRMSK  EQU   X'40'           REPLACEMENT IS MASKED             82137
RIXL1    DC    X'0'          LENGTH OF FIRST ENTRY               82137
RIXL2    DC    X'0'          RENAME ONLY / LENGTH OF REPLACE STRING
RIXMASK  DC    0X'0'         1-44 BYTE MASK                      82137
RIXREP   DC    0X'0'         REPLACEMENT STRING                  82137
RIXFIX   EQU   *-RIXLIST     FIXED LENGTH OVERHEAD PER ENTRY     82137
         SPACE 1
         PRINT &PRTMAC                                          GP12061
         MAPPARST ,          EXPAND PARSE RETURN                GP12061
         SPACE 1
         PRINT &PRTSYS                                           82137
CVTDSECT DSECT ,
         IHAPSA ,
         CVT   DSECT=YES                                         82137
         IKJTCB ,                                                82137
         SPACE 1                                                 82137
         IEZJSCB ,                                               82137
         SPACE 1
         IEFUCBOB ,
         AIF   (&MVS).MVSUCB                                     82137
UCBRR    EQU   X'20'         DEVICE SHARABLE                     82137
UCBRPS   EQU   X'10'         DEVICE HAS RPS                      82137
         AIF   (&SVS).MVSUCB                                     82137
UCBVLPWR EQU   X'02'         DEVICE REQUIRES ALTERNATE POWER     82137
UCBDVPWR EQU   X'01'         DEVICE HAS ALTERNATE POWER          82137
.MVSUCB  SPACE 1                                                 82137
         DCBD  DEVD=DA,DSORG=PS
         SPACE 1
DSCB3    DSECT ,
         IECSDSL1 3          FORMAT 3
         SPACE 1
FDSECT   FDSECT ,
         SPACE 1
         AIF   (NOT &LOCO).BIGEND                               GP12061
USERVOLT DSECT ,
VOLTUSER USERVOLT MAP=NO     GET FLAGS ONLY
         SPACE 1                                                 82137
USERCVT  USERCVT ,                                               85252
         SPACE 1                                                 82137
USERTCB  USERTCB ,                                               82137
         SPACE 1
.BIGEND  END   ,                                                GP12050
