ASMANY   TITLE 'A S M A N Y  ---  ASSEMBLE MANY DECKS'
         MACRO                                                   84275
&NM      SUBENT &SA,&BASE=,&BASEREG=R12                          84275
         AIF   (T'&SA EQ 'O').NOSA                               84275
&NM      STM   R14,R12,&SA                                       84275
         AGO   .TSTBASE                                          84275
.NOSA    ANOP  ,                                                 84275
&NM      STM   R14,R12,SUBSAVE                                   84275
.TSTBASE AIF   (T'&BASE EQ 'O').MEND
         BASR  &BASEREG,0
         USING *,&BASEREG
.MEND    MEND  ,                                                 84275
         SPACE 1                                                 84275
         MACRO ,                                                 84275
&NM      SUBEX &SA,&RET=R14                                      85083
         AIF   (T'&SA EQ 'O').NOSA                               84275
&NM      LM    R14,R12,&SA                                       84275
         AGO   .COM                                              84275
.NOSA    ANOP  ,                                                 84275
&NM      LM    R14,R12,SUBSAVE                                   84275
.COM     BR    &RET                                              85083
         MEND  ,                                                 84275
         SPACE 1
         PUNCH ' ORDER ASMANY(P) '
         PUNCH ' ALIAS ASMANYHL,ASMANYL,ASMANYG,ASMANYH,ASMANYF'
         PUNCH ' ALIAS ASMANYX,ASMANYXF,ASMANYT,COBMANY,FORMANY'
         PUNCH ' SETCODE AC(1) '
         SPACE 2                                                 84275
         COPY  OPTIONGB                                          81167
         SPACE 1                                                 81167
         LCLC  &ASMF,&ASMG,&ASMH,&ASML,&ASMT   ASSEMBLER NAMES  GP05336
         SPACE 1                                                 81167
         SYSPARM LIST=YES    MERGE AND SHOW OPTIONS              81167
         SPACE 1                                                 81167
&ASMF    SETC  'IFOX00'      ASM F EXTENDED                      81167
&ASMG    SETC  'ASMGWYL'     U.OF WATERLOO - ASM WITH WYLBUR SUPPORT
&ASMH    SETC  'IEV90'       ASM H                               81320
&ASML    SETC  'ASMA90'      ASM HL                             GP97206
&ASMT    SETC  'X390'        TACHYON CROSS ASSEMBLER            GP05336
***********************************************************************
*                                                                     *
*                                                                     *
*   COPYRIGHT 1981, 1987, 1997  EXPERT SYSTEM PROGRAMMING, INC.       *
*   COPYRIGHT 2002, 2003, 2008  EXPERT SYSTEM PROGRAMMING             *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                 81169
*        ASMANY - PERFORMS MULTIPLE ASSEMBLIES OF SOURCE DECKS   81167
*              RESIDING IN ONE OR MORE PARTITIONED DATASETS.     81167
*        THE PRIMARY DATASET IS SPECIFIED ON AN ASMIN DD CARD    81167
*              (WITHOUT A MEMBER NAME); CONCATENATION IS NOT     81167
*              SUPPORTED.  ADDITIONAL DATASETS MAY BE SPECIFIED  81167
*              WITH ANY NON-RESERVED DDNAME, AND INVOKED USING THE
*              DDN= OPTION.                                      81167
*        CONTROL INPUT IS SUPPLIED ON SYSIN (MAY BE CONCATENATED).
*        BY DEFAULT, ANY CARD NOT RECOGNIZED CAUSES THE FIRST EIGHT
*              BYTES TO BE USED AS A MEMBER NAME TO BE ASSEMBLED.
*        WHEN THE OPTIONS ARE NOT OVERRIDDEN, A LINK-EDIT NAME (R)
*              AND SSI=000DATE ARE GENERATED AFTER THE ASSEMBLY. 81167
*        OTHER ASSEMBLER FILES MAY BE SPECIFIED EITHER AS SYS.....
*              OR WITH A DDNAME OF ASM.....                      81167
*        SYSPRINT AND SYSPUNCH MAY SPECIFY PARTITIONED DATASETS. 81167
*              WHEN NO MEMBER IS SPECIFIED, ASMANY WILL SUPPLY THE
*              APPROPRIATE MEMBER NAME FOR EACH ASSEMBLY.  THE PAGE
*              SEPARATOR ROUTINE IS NOT USED FOR PDS MEMBERS.    81167
*              THIS PDS SUPPORT REQUIRES THE CODE IN LOCAL SVC 240,
*              DISTRIBUTED WITH ASMANY AS IGC00240. IF YOU DO NOT
*              INSTALL THIS SVC, ONLY SEQUENTIAL SUPPORT IS PROVIDED,
*              AND &SVCJFCB (IN MACRO SYSPARM) MUST BE ZERO.     82102
*        SPECIFICATION CHANGE:  IN MVS AND LATER, IF ASMANY IS
*              RUNNING AUTHORIZED, SVCJFCB MAY BE ZERO; THE SVC MODULE
*              WILL BE INVOKED AT ENTRY POINT SVCMJFCB INSTEAD. GP99053
*          SYSADATA NOW SUPPORTED AS PDS OR DISP=MOD SEQUENTIAL GP08288
*
*        ONE OF TWO OTHER DD CARDS MAY BE SUPPLIED:              81167
*          MSGPRINT WILL RECEIVE A LISTING OF CONTROL CARDS AND ERROR
*              MESSAGES GENERATED BY ASMANY.                     81167
*          WHEN MSGPRINT IS OMITTED OR DUMMY, AND A CONPRINT DD IS
*              SUPPLIED INSTEAD, IT WILL RECEIVE CONTROL AND MESSAGE
*              OUTPUT, AND SELECTED MESSAGES WILL APPEAR ON THE CONSOLE
*
*              ADDITIONAL INPUT OPTIONS:
*        * IN COL. 1         OPERATOR MAY CANCEL JOB BETWEEN ASSEMBLIES
*                       (A WTOR IS ISSUED; A REPLY OF STOP TERMINATES)
*        DSN=.. IN 1-4       SWITCH TO NEW DSNAME ON CURRENT DDNAME
*        DDN=.. IN 1-4       SWITCH TO NEW DDNAME (REPLACE 'ASMIN')
*        % IN COL. 1         FOR EACH MEMBER OUTPUTS 'NAME MEMBER(R)'
*                                      (DEFAULT MODE)            82249
*        %OFF                STOPS NAME AND SSI CARDS
*        %XEROX  {OPT}       SET XEROX (4050, 9700) OPTIONS      90101
*              APPEARANCE OF CARD FORCES SHEET EJECTS            90101
*              "OPT" OF AUX, FEED OR TRAY CAUSES AUX. TRAY SELECT
*        %9700   (OLD STYLE) SETS XEROX 9700 OPTION              81131
*        %NOSVC              INHIBITS SVC NAME CONVERSION. IF NOT USED,
*                            A MEMBER NAME OF THE FORM IGC--NN0 HAS
*                            THE ZERO CHANGED TO X'C0' ON THE NAME CARD
*                            CREATED BY ASMANY.                  81169
*        %SEP                TURN THE SEPARATOR PAGES ON (DEFAULT)
*        %NOSEP              TURN SEPARATOR PAGE GENERATION OFF  81169
*        %PAGE               SETS AUTO PAGE AND NAME CONTROL CARDS
*        %NOPAGE             RESETS AUTOMATIC PAGE CONTROL CARDS 81169
*        BLANK IN COL. 1     COPY AS LKED CONTROL CARD
*        HEX '02' IN COL. 1  COPY LKED OBJECT DECK IN SYSIN STREAM
*        + IN COLUMN 1: PLACED ON STACK (DEPTH 128); + CHANGED TO BLANK
*                            STACK WRITTEN TO SYSGO AFTER ASSEMBLY
*                            (BUT BEFORE NAME(R) CARD) AND CLEARED.
*        SEP=2 OR SEP=3 IN COLUMN 1 - SET NUMBER OF BIG LINES ON 81167
*              PAGE SEPARATOR. DEFAULT IS 3 EXCEPT AT CCSI.      81167
*        SEP=NO AND SEP=YES SET/RESET THE SEPARATOR PAGE OPTION  81169
*        ID=                 UP TO 8 BYTES OF DATA TO BE PRINTED ON
*                            THE THIRD LINE OF THE SEPARATOR.    81169
*                            RESET BY DSN= AND DDN=              81169
*        SSI=  IN COL 1      WITH BLANK OPERAND - CANCELS SSI CARDS
*                            WITH NON-BLANK : VALUE TO BE USED   81167
*                            DEFAULT IS CURRENT DATE: 0CCYYDDD   99053
*        COND= (OR CODE=) SETS THE LOWEST UNACCEPTABLE RETURN CODE.
*                            FOR BOTH COMPILERS AND LINKER/BINDER
*                            THE VALUE IS 1-8 DIGITS. WHEN THIS VALUE
*                            IS REACHED OR EXCEEDED, ASMANY STOPS.
*                            ASMANY USES 4095 FOR ALL ABEND CODES.
*              COND=RESET SET CURRENT CC=0, SAVES PRIOR CODE     91003
*        ASMCOND= (OR ASMCODE=) SETS THE LOWEST VALUE AT WHICH  GP04064
*                            THE COMPILER/ASSEMBLER WILL NOT BE CALLED
*        COMPCOND= (OR COMPCODE=) SETS THE LOWEST VALUE AT WHICH
*                            THE COMPILER/ASSEMBLER WILL NOT BE CALLED
*        LKEDCOND= (OR LKEDCODE=) SETS THE LOWEST VALUE AT WHICH 84275
*                            THE LINKAGE EDITOR WILL NOT BE CALLED.
*                            DEFAULT IS 4.                       84275
*        %LKEDPARM STRING    DEFAULT LKED PARM PREFIX (ALL LKEDS)
*        %LKEDDFLT OPTS,...  DEFAULT LKED OPTIONS FOR %NEWLINK   87074
*        %LKEDON   OPTS,...  LKED OPTIONS TO TURN ON IN LINK/RELINK
*        %LKEDOFF  OPTS,...  OPTIONS TO TURN OFF IN RELINK MEMBERS
*              "OPTS" : RENT, REUS, REFR, OVLY, TEST, SCTR, LOAD, DC
*        NAME (NAME2 NAME3...)  ASSEMBLE THE MODULES SPECIFIED,  84275
*              CREATE LKED CONTROL CARDS (AFTER) FOR NAME ONLY.  84275
*        NAME: NAME2 (NAME3...) ASSEMBLE NAME2, ETC., BUT USE 'NAME'
*              FOR LKED CONTROL CARDS.                           84275
*        %LINK NAME(:) (NAME2...) ASSEMBLE NAME, ETC. AS ABOVE, BUT
*              ALSO USE ENTRY, ORDER, OVERLAY, ETC. INFORMATION  84275
*              FROM MODULE 'NAME' IN DD LNKLIB. ALIAS ENTRIES ARE
*              NOT COPIED.                                       84275
*        %NEWLINK NAME ...  AS FOR %LINK, BUT DEFAULTS OPTIONS   87074
*              WHEN MEMBER 'NAME' IS NOT FOUND.                  87074
*        %RELINK NAME(:) (NAME2...) ASSEMBLE NAME,ETC. AS ABOVE, THEN
*              RELINK WITH SAME ALIASES AND ATTRIBUTES AS EXISTING
*              MODULE 'NAME' IN DD LNKLIB.                       84275
*        %RELINK NAME:NAM2 NAME3 (NAME4...)  ASSEMBLE NAME3, ETC., USE
*              'NAME' FOR LKED NAME CARD, USE LKED OPTIONS FROM 'NAM2'.
*
*        DDLMOD=DDNAME|'DYNALLOC'
*          THE %LINK OPTIONS ACCESS DD NAMES LNKLMOD (OUTPUT) AND
*          LNKLIB (INCLUDE, ETC). DDLMOD=DYNALLOC WILL DYNAMICALLY
*          ALLOCATE AND USE AS LNKLMOD THE LNKLIB THE MEMBER WAS
*          PREVIOUSLY FOUND IN (FOR %LINK AND %NEWLINK, FIRST ENTRY
*          IN LNKLIB CONCATENATION WHEN NOT FOUND).
*
*        %LLA | %REFRESH   DO LLACOPY AFTER CC<5 LINK-EDIT
*        %NOLLA | %NOREFRESH  DON'T DO LLACOPY
*          THESE OPTIONS REQUIRE AUTHORIZATION. %NOLLA IS THE DEFAULT
*
***********************************************************************
*
*        ATTRIBUTES: REFR, RENT (DIFFERENT TIOTS ONLY)           81169
*              LOGICALLY REUSABLE ONLY FOR SINGLE JOBSTEP FAMILY 81169
*          AC=1 RECOMMENDED FOR SPECIAL FUNCTIONS
*                                                                81167
*        ENTRY POINT ASMANY INVOKES THE ASSEMBLER SPECIFIED BY &ASM
*              (DEFAULT: HIGH-LEVEL ASSEMBLER ASMA90)           GP97217
*        ENTRY POINT ASMANYG INVOKES THE ASSEMBLER SET BY &ASMG  81167
*              (DEFAULT IS ASMGWYL - U. OF WATERLOO ASM WITH WYLBUR)
*        ENTRY POINT ASMANYH INVOKES THE ASSEMBLER SET BY &ASMH  81320
*        ENTRY POINT ASMANYF INVOKES THE ASSEMBLER SET BY &ASMF GP97217
*        ENTRY POINT ASMANYT INVOKES THE TACHYON CROSS ASSEMBLER  05336
*                                                                81167
*        EXTERNAL ROUTINES REQUIRED :                            81167
*              SVC 240 - SEE ABOVE COMMENTS FOR SYSPRINT         81167
*        SERVICE ROUTINES @DCBEXIT, @PRINTER AND @SERVICE        81167
*                         @INPREAD (SEQ, CONCAT, WYLBUR I/P)     84261
         SPACE 3                                                 81167
         PRINT &PRTSOR                                           81167
ASMANY   PGMHEAD ZERO15,BASE=(R11,R12),PARM=R10,BNDRY=PAGE,            *
               ENTRY=(ASMANYHL,ASMANYL,ASMANYG,ASMANYH,ASMANYF,ASMANYX,*
               ASMANYXF,ASMANYT,COBMANY,FORMANY),EID=NONE,      GP05336*
               AM=31,RM=24   OLD CODE (JFCB, EODAD, ETC.)       GP02251
         SPACE 1                                                 81169
         MVC   PDSDCB(PDSDCBLN),PDSPAT  MOVE DCB PATTERNS        84261
         LA    R1,PDSEXLST                                       81169
         STCM  R1,7,DCBEXLST+1-IHADCB+PDSDCB                     81169
         LA    R1,INFMJFCB                                       81169
         ST    R1,PDSEXLST+4                                     81169
         MVI   PDSEXLST,5                                        81169
         MVI   PDSEXLST+4,X'87'                                  81169
         MVI   STOPCODE,X'7F'  MAKE LARGE POSITIVE               81169
         MVI   STOPLINK+L'STOPLINK-1,4  SET LKED STOP CODE       84275
         TESTAUTH FCTN=1     ARE WE AUTHORIZED ?                GP99053
         BXH   R15,R15,PGMAUNOT NO                              GP99053
         MVI   FLAG3,FGAUTH  SHOW AUTHORIZATION                 GP99053
PGMAUNOT L     R0,=X'0000FFFF'  SET LKED OR/AND OPTION BITS      87074
         STCM  R0,15,LOPTOR  SET OR AND AND OPTIONS              87074
         STCM  R0,12,LOPTDFLT  ALSO SET DEFAULT OPTIONS          87074
         MVI   DFLLPART,C' '                                     87074
         MVC   DFLLPART+1(L'DFLLPART-1),DFLLPART  CLEAR TEXT     87074
         MVC   DFLLPARM(PATLPARX-PATLPARM),PATLPARM  PARM.LKED   87074
         L     R14,=A(PATRBPTR)                                  87081
         MVC   DYNRBPTR(PATRBEND-PATRBPTR),0(R14)  SVC 99 PARMS  87081
         LA    R14,TUDSN                                         87081
         LA    R15,TUVOL                                         87081
*FAILS   LA    R0,TUUNT      MVS 3.8J FAILS ON 21C              GP09274
         LA    R0,TUDIS                                          87081
         LA    R1,TUDDN                                          87081
         O     R1,=X'80000000'   END OF LIST                    GP09274
         STM   R14,R1,TEXTNEW  BUILD TEXT UNIT POINTERS         GP09274
         LA    R1,DYNRB                                          87081
         ST    R1,DYNRBPTR                                       87081
         OI    DYNRBPTR,X'80'                                    87081
         LA    R15,PDSDCB                                        81169
         ST    R15,OPLASMIN  MAKE OPEN LIST                      84261
         MVI   OPLASMIN,X'90'  INPUT, REREAD                     81169
         AIF   (NOT &MVS AND &SVCJFCB EQ 0).S240IN              GP08288
         TM    FLAG3,FGAUTH  ARE WE AUTHORIZED?                 GP99053
         BZ    SHRTPARM      NO                                 GP99053
         LA    R14,DDNPDS                                        81169
         LA    R15,JFCBELNM                                      81169
         ICM   R14,8,=X'01'                                      81169
         ICM   R15,8,=X'84'  SVC 240 MEMBER NAME UPDATE OPTION   81169
         LA    R0,JFCBDSNM   PARM FOR DSN= OPTION                81169
         ICM   R0,8,=X'86'                                       81169
         STM   R14,R0,PRM240IN                                   81169
         LA    R14,DDNPRINT                                      81169
         ICM   R14,8,=X'01'                                      81169
         STM   R14,R15,PRM240PR                                  81169
         ST    R14,PRMMODPR                                      81169
         MVI   PRMMODPR+4,X'8E'   DISP=MOD                       81169
         LA    R14,DDNPUNCH                                      81169
         ICM   R14,8,=X'01'                                      81169
         STM   R14,R15,PRM240PU                                  81169
         ST    R14,PRMMODPU                                      81169
         MVI   PRMMODPU+4,X'8E'                                  81169
         LA    R14,DDNADATA                                     GP04065
         ICM   R14,8,=X'01'                                     GP04065
         STM   R14,R15,PRM240AD                                 GP04065
         ST    R14,PRMMODAD                                     GP04065
         MVI   PRMMODAD+4,X'8E'                                 GP04065
         LA    R14,DDNLIN                                       GP97241
         ICM   R14,8,=X'01'                                     GP97241
         STM   R14,R15,PRM240OB                                 GP97241
         ST    R14,PRMMODOB                                     GP97241
         MVI   PRMMODOB+4,X'8E'                                 GP97241
SHRTPARM DS    0H                                               GP99053
.S240IN  MVC   LISTFLAG(4),=AL1(LFINIT,8,8,3)                    81169
         MVC   LISTCOMP(4),=C'CC #'                              81169
         MVC   LISTVART,LISTDATE  SET SEPARATOR TO DATE          81169
         LA    R1,DDLIST                                         81169
         ST    R1,MYPARM+4                                       81169
         MVI   MYPARM+4,X'80'                                    81169
         MVI   FLAGS,FGAUTONM+FGSVCGO  AUTO PAGE/SSI/NAME; SYSGO=MOD
         OI    FLAG3,FGSOME  SET FOR MOST OUTPUT                 84275
PGMNOTAU MVC   WTOR(PATWTOLN),PATWTOR                            81169
         MVC   ATTALIST(ATTALEN),ATTAPAT                         81169
         LA    R14,SAVE                                         GP11217
         A     R14,=A(POSTCARD-SAVE)                            GP11217
         LA    R15,80                                            81169
         LR    R0,R14                                            81169
         LA    R1,SAVE                                          GP11217
         A     R1,=A(CARDPOST-SAVE-80)       SET END            GP11217
         STM   R14,R1,MAILBAG INIT BXH POINTERS                  81169
         MVC   MYPARM+1(3),1(R10)       SAVE PARM FOR ASSEMBLER  81167
         LA    R2,ASMANY     GET CLEARED BASE ADDRESS            88111
         L     R14,SAVE13    CALLER'S SAVE AREA                  88111
         L     R15,SAVE15-SAVE(,R14)  GET ENTRY ADDRESS          88111
         LA    R15,0(,R15)   CLEAR HIGH BYTE                     88111
         SR    R15,R2        CONVERT TO ENTRY OFFSET             88111
         LA    R0,ASMPGMLN   ENTRY LENGTH                       GP05336
         LA    R1,ENDPGM-ASMPGMLN  LAST ENTRY                   GP05336
         LA    R14,ASMPGM    FIRST ENTRY                         88111
PGMLOOP  CH    R15,0(,R14)   MATCHING ENTRY ?                    88111
         BE    PGMSET        YES                                 88111
         BXLE  R14,R0,PGMLOOP  ELSE TRY AGAIN                    88111
         LA    R14,ASMPGM    DEFAULT FOR UNMATCHED ?             88111
PGMSET   MVC   ASMPGMNM,4(R14)  MOVE PROGRAM ENTRY NAME          88111
         MVC   ASMPGTYP,12(R14)  MOVE PROGRAM TYPE (A/ASM, C/COBOL...
         ICM   R0,3,2(R14)   GET OFFSET TO DDNAME LIST           88111
         AR    R2,R0         DDNAME LIST ADDRESS                 88111
         LH    R1,0(,R2)     GET LENGTH OF DDNAMES               88111
         LA    R1,1(,R1)     GET LENGTH OF LIST - 1              88111
         EX    R1,PGMDDNS    MOVE LIST                           88111
         MVC   DCBDDNAM-IHADCB+PDSDCB(8),DDNPDS  MAKE ---IN      88111
         CLI   ASMPGTYP,C'A'  ASSEMBLER ?                        88329
         BE    *+8           YES                                 88111
         MVI   STOPLINK+L'STOPLINK-1,8  DEFAULT STOP CODE HIGHER 88111
         SPACE 1                                                 81167
         SERVINIT ,          FETCH THE SERVICE ROUTINE           81169
         AIF   ('&LOCAL' NE 'CCSI').NOCHAP                       81167
         AIF   (&MVS).CHAPT2                                     84275
         SERVCALL ACGET,DEVT   GET ACCOUNT PRIVILEGES            81167
         STC   R0,DEVT       STASH IT                            81167
.CHAPT2  MVI   LISTNUM,2     DEFAULT TO TWO LINES PER SEPARATOR PAGE
.NOCHAP  SERVLOAD @PRINTER,@INPREAD,SUBDSERV                    GP04064
         SPACE 1                                                 81167
         PRTOPEN MSGPRINT,DEV=2,OPT=(DUMMY,NOWTO)   OPEN MESSAGE FILE
         BXLE  R15,R15,INITMSG  OK                               81167
         PRTOPEN CONSOLE,DEV=3  ELSE OPEN CONSOLE (WTO) FILE ALSO
INITMSG  SLR   R9,R9         CLEAR (GLOBAL) RETURN CODE          81167
         LA    R3,DDFIRST    POINT TO DDNAME LIST                81167
         LH    R4,DDLIST     GET LENGTH OF DDNAMES               88111
         SRA   R4,3          GET NUMBER OF ENTRIES (/8)          88111
         LA    R5,DDNPDS     SET ADDRESS OF SPECIAL              81167
INITDDLP CLI   0(R3),0       DUMMY ENTRY ?                       81167
         BE    INITDDBP      YES; SKIP IT                        81167
         CR    R3,R5         SPECIAL ENTRY ?                     81167
         BE    INITDDBP      YES; SKIP IT ALSO                   81167
         DEVTYPE (R3),DEVT   SEE IF PRESENT                      81167
         BXH   R15,R15,INITDDAL  NO; CHECK FOR ALTERNATE         81167
         MVC   JFCB(8),0(R3)  COPY THE NAME                      81167
         MVC   JFCB(3),=C'SYS'  MAKE ALTERNATE NAME              81167
         DEVTYPE JFCB,WORK   SEE IF IT IS PRESENT                81167
         BXH   R15,R15,INITDDBP   NO; OK                         81167
         PRTLIST TWODDN,DEV=(2,3)  SHOW TWO DD CARDS PRESENT     81167
         LA    R9,4          SET (MINOR) ERROR                   81167
         B     INITDDBP      TRY NEXT FILE                       81167
INITDDAL MVC   0(3,R3),=C'SYS'  SET FOR OLD NAME                 81167
         DEVTYPE (R3),DEVT   PRESENT ?                           81167
         BXLE  R15,R15,INITDDBP  YES                             81167
         CLC   =C'AOPT',3(R3)    TACHYON OPTIONS DD ?           GP08286
         BE    INITDDBP      IT'S OPTIONAL, SO BYPASS MESSAGE   GP08286
         PRTLIST NONEDDN,DEV=(2,3)                               81167
*NONONO* XC    0(8,R3),0(R3)   INDICATE DD MISSING               81167
*        DON'T SET R9 - MAY BE OPTIONAL (E.G. SYSPUNCH)          81167
INITDDBP LA    R3,8(,R3)                                         81167
         BCT   R4,INITDDLP                                       81167
INITDDNM CLI   DDNPRINT,0    MISSING ?                           81167
         BE    INITDDP0      YES; DON'T RESET IT                 81167
         MVC   SYSPRINT(8),DDNPRINT   COPY AVAILABLE SYSPRINT NAME
INITDDP0 LA    R3,DDNLIN     SET TO SYSLIN                       88111
         CLI   DDNLIN,0      IS THIS A SYSLIN COMPILER ?         88111
*TEST*   BNE   INITDDG1      YES; USE XXXLIN, NOT XXXGO          88111
         BNE   INITDDG0      ALLOW IT IF IT WORKS               GP97241
         CLI   DDNGO,0       SYSGO MISSING ?                     81167
         BE    INITDDG0      YES; DO NOT RESET NAME              81167
         LA    R3,DDNGO      POINT TO SYSGO DD NAME              84275
INITDDG1 MVC   SYSGO(8),0(R3)  SET FOR SYSGO/SYSLIN              88111
         BAS   R8,TESTPDS    SEE IF IT IS A PDS                  84275
         B     UNUSDD        ISSUE ERROR MESSAGE - PDS           84275
         B     INITDDG0      NOT PDS - CONTINUE                  84275
UNUSDD   PRTLIST UNUSDDFD,DEV=(2,3)  SHOW UNUSABLE               84275
         LA    R15,20        SET MAJOR ERROR                     84275
         B     ENDFILE       QUIT                                84275
PGMDDNS  MVC   DDLIST(0),0(R2)  MOVE DDNAME LIST                 88111
         SPACE 1                                                 84275
         PUSH  USING
INITDDG0 LTCB  R1,USE=YES    GET AND DECLARE TCB                 87081
         L     R1,TCBJSCB    GET THE JSCB                        81169
         USING IEZJSCB,R1                                        81169
         L     R1,JSCBCSCB   GET THE CSCB                        81169
         USING CHAIN,R1                                          81169
         AIF   (NOT &MVSESA).CMESACH                            GP97206
         L     R2,CHCSCXP    GET THE CSCB EXTENSION             GP97206
         USING CSCX,R2       DECLARE IT                         GP97206
.CMESACH MVC   CIBPTR,CHECBP SAVE STOP ECB ADDRESS              GP97206
         POP   USING
         SERVCALL LPALD,=CL8'@DCBEXIT'  LOAD DCB EXIT ROUTINE    81167
         STCM  R0,7,PDSEXLST+1                                   81167
         MVC   WORK,SYSIN    MAKE NAME FOR NODD MSG              84261
         INPOPEN SYSIN       OPEN THE CONTROL CARD FILE          84261
         CH    R15,=H'4'     DID IT OPEN ?                       84261
         BNH   SYNOPEN       YES                                 84261
         PRTLIST NODDFD,WORK,DEV=(2,3)  WRITE MISSING DD MESSAGE 81169
         SERVTERM ,          CLOSE ALL FILES                     81169
         ABEND 2540                                              81167
         SPACE 1                                                 81167
SYNOPEN  TIME  ,
         ST    R1,WORK+4     STASH DATE
         OI    WORK+7,X'0F'  MAKE PROPER ZONE                   GP99053
         CLI   WORK+4,X'18'  ANY CENTURY SET ?                  GP99053
         BH    SYNOPEN2      PERHAPS                            GP99053
         AP    WORK+4(4),=P'1900000'  MAKE APPROPRIATE CENTURY  GP99053
         OI    WORK+7,X'0F'  MAKE PROPER ZONE                   GP99053
SYNOPEN2 UNPK  SSITEXT,WORK+4(4)   UNPACK IT                     81167
         L     R1,=A(TRHEX-C'0')   TRANSLATE TABLE              GP99053
         TR    SSITEXT,0(R1)   MAKE DEFAULT SSI PRINTABLE       GP99053
         MVC   SSIDATE,SSITEXT  SAVE DEFAULT DATE SSI            84275
         AIF   (NOT &MVS AND &SVCJFCB NE 0).NOIDENT             GP08288
       IDENTIFY EP=ASMANY1,ENTRY=DOOPEN   FAKE FOR ATTACH
.NOIDENT LA    R3,DDNPRINT   POINT TO SYSPRINT                   81167
         BAS   R8,TESTPDS    SEE IF PDS WITHOUT MEMBER           81167
         OI    FLAG4,FGSVCPR   YES; SET JFCB MODIFICATION        81167
         TM    DEVT+2,X'A0'  IS PRINT OUTPUT DISK OR TAPE ?      81167
*TEST*   BNM   TSTPUNCH      NEITHER                             81167
         NI    LISTFLAG,LF9700+LFXRXAUX  YES; SKIP INITIAL EJECT 90101
TSTPUNCH LA    R3,DDNPUNCH   NOW TRY SYSPUNCH                    81167
         BAS   R8,TESTPDS                                        81167
         OI    FLAG4,FGSVCPU   SET JFCB MODIFICATION             81167
         LA    R3,DDNLIN     NOW TRY SYSLIN                     GP97241
         BAS   R8,TESTPDS                                       GP97241
         OI    FLAG4,FGSVCOB   SET JFCB MODIFICATION            GP97241
         LA    R3,DDNADATA   NOW TRY SYSADATA                   GP04065
         BAS   R8,TESTPDS                                       GP04065
         OI    FLAG4,FGSVCAD   SET JFCB MODIFICATION            GP04065
         AIF   (NOT &MVS AND &SVCJFCB NE 0).CMIDENT             GP08288
         TM    FLAG4,FGSVCPR+FGSVCPU+FGSVCOB+FGSVCAD            GP04065
         BZ    NEWDD                                             81167
         TM    FLAG3,FGAUTH  RUNNING AUTHORIZED?                GP99053
         BNZ   NEWDD         YES; WILL PROCESS                  GP99053
         PRTV  MSGNOSTW,CC=NO,DEV=(2,3)                         GP02251
         LA    R9,20                                             81167
         B     ENDFILE       AND QUIT                            81167
MSGNOSTW VCON  ' PRINT/PUNCH/LIN/ADATA PDS NOT SUPPORTED'       GP04065
.CMIDENT SPACE 1                                                 81167
NEWDD    RDJFCB MF=(E,OPLASMIN)         READ IN JFCB FOR DDNAME  81167
         BXLE  R15,R15,NEWDSN    CONTINUE IF GOTTEN              81167
         SPACE 1
         MVC   WORK,DDNPDS   SHOW DDNAME IN MESSAGE              81169
DDNSRCH  PRTLIST NODDFD,DEV=(2,3)  PRINT/WTO ERROR MESSAGE       81169
         MVI   ACB,NEEDDDN   SET FOR DDN SEARCH ONLY             81167
         SPACE 1                                                 81167
NEWDSN   SERVCALL DSABB,JFCBDSNM   GET ABBREVIATED FORM OF DSN   81167
         MVC   LISTDSN,0(R1)  STASH INTO SEPARATOR LIST          81167
         EJECT ,
*---------------------------------------------------------------------*
*    MAIN PROCESSING LOOP                                             *
*---------------------------------------------------------------------*
PROCESS  BASR  R10,0         SET FAST RETURN REGISTER            81169
         INPGET ,            READ A CONTROL CARD                 84261
         CH    R15,=H'4'     CHECK RETURN                        84261
         BL    GOTIN         GOOD                                84261
         BE    ENDFILE       ALL DONE                            84261
GETINERR ABEND 001,DUMP      NO, COMPLAIN AND QUIT               84261
GOTIN    LR    R3,R1         SAVE NEW CARD ADDRESS               84261
         USING CARDSECT,R3   DECLARE MAPPING                     81167
         PRTLIST CARDLIST,DEV=2        ECHO THE INPUT CARD       81167
         CLC   CARD(71),CARD+1    ALL BLANK?                    GP10030
         BE    PROCESS       YES; IGNORE AFTER PRINTING         GP10030
         LA    R6,1          INITIALIZE THE BXH INCREMENT        81169
         LA    R7,CARD+70    SET THE STOP ADDRESS                81169
         LR    R5,R3         SET THE SCAN START ADDRESS          81169
         MVI   LKFLAG,0      RESET THE LINK-EDIT CARD FLAGS      84275
         NI    FLAG2,255-FG2STACK  RESET STACKED ASSEMBLIES      84275
         LA    R4,OPTTABDD   SET FOR DDNAME REQUIRED             81169
         TM    ACB,NEEDDDN   DDN REQUIRED ?                      81169
         BNZ   PROCHK        YES; USE IT                         81169
         LA    R4,OPTTABDS   ELSE SET FOR DSN= TABLE             81169
         TM    ACB,NEEDDSN   DSN REQUIRED ?                      81169
         BNZ   PROCHK        YES; USE IT                         81169
         LA    R4,OPTTAB     ELSE USE GENERAL OPTION TABLE       81169
PROCHK   BAS   R8,VERBLOOK   CHECK AND BRANCH ON VERB CODE       81169
         TM    ACB,NEEDDSN+NEEDDDN  SEARCHING FOR DSN ?          81169
         BZ    NEWMEM        NO; TREAT AS NEW MEMBER NAME        81169
         BR    R10           IGNORE IF NOT ACCEPTABLE            81169
         SPACE 1                                                 81169
PROPOST  LM    R0,R2,MAILBAG+4   GET BXH POINTERS                81169
         XR    R1,R2                                             81169
         XR    R2,R1                                             81169
         XR    R1,R2         FLIP FOR BXH                        81169
         BXH   R2,R0,PROPOSTF  BRANCH IF FULL                    81169
         MVC   0(80,R2),CARD   SAVE CARD                         81169
         MVI   0(R2),C' '    REPLACE PLUS BY BLANK               81169
         ST    R2,MAILBAG+8  SET NEW LAST CARD ADDRESS           81169
         BR    R10           DO ANOTHER                          81169
PROPOSTF PRTV  MSGSTKFL,CC=NO,DEV=(2,3)                         GP02251
         B     LVL8ERR       SET COND.CODE=8                     84261
MSGSTKFL VCON  ' POST-ASM CARD STACK IS FULL'                   GP02251
         SPACE 1                                                 81167
DODSN    MVC   JFCBDSNM,CARD+4  MOVE IN NEW DSNAME               81167
         NI    ACB,255-NEEDDSN   RESET SEARCH FLAG               81167
         AIF   (NOT &MVS AND &SVCJFCB EQ 0).N240LCH             GP08288
         TM    FLAG3,FGAUTH  AUTHORIZED?                        GP99053
         BZ    NEWDSN        NO; NO CHANGE                      GP99053
         NI    PRM240IN+4,255-X'80'  EXTEND LIST TO DSN          81169
.N240LCH B     NEWDSN        GET DSN SHORT FORM AND PROCESS INPUT
         SPACE 1
SETNEWDD MVI   ACB,0         RESET SEARCHING FOR DDN/DSN FLAG    81167
         MVC   PDSDCB+DCBDDNAM-IHADCB(8),CARD+4  COPY THE DD NAME
         MVC   DDNPDS,CARD+4   PASS NEW NAME TO THE ASSEMBLER    81167
         B     NEWDD         NOW SET UP FOR IT
         SPACE 1                                                 85083
SETLLMOD LA    R3,1(,R3)     DDLMOD IS ONE LONGER THAN OTHERS    87081
SETLMOD  LA    R5,USERLMOD   POINT TO SYSLMOD OVERRIDE           85083
         NI    ALLOFLAG,255-ALFREQ  RESET DYNAMIC ALLOCATION REQUEST
         CLC   =C'DYNALLOC',CARD+6  DYNAMIC ALLOCATION ?         87081
         BNE   SETLCOM       NO; PROCESS LEGITIMATE REQUEST (?)  87081
         OI    ALLOFLAG,ALFREQ+ALFNEED  REQUEST DYNALLOC         87081
         BR    R10           GET NEXT CARD                       87081
SETLLIB  LA    R5,USERLLIB   POINT TO SYSLIB OVERRIDE            85083
SETLCOM  LA    R3,CARD+6     POINT TO INPUT                      85083
         CLI   0(R3),C' '    NEGATION ?                          85083
         BE    SETLMOV       YES; MOVE NAME                      85083
         BAS   R8,TESTLMOD   CHECK FOR LOAD-MODULE PDS           87291
         B     SETLMOV       OK ?                                85083
         B     UNUSDD        SHOW INVALID DD                     85083
SETLMOV  MVC   0(L'USERLMOD,R5),0(R3)  COPY USER'S NAME          85083
         BR    R10           GET ANOTHER CARD                    85083
         SPACE 1
SETWTOR  CLC   CARD+1(3),DCOFF+3   IGNORE REPLIES ?              81169
         BNE   SETWTORN      NO, SET REPLY ON                    84275
         NI    FLAGS,255-FGWTOR  RESET WTOR OPTION               81167
         BR    R10                                               81169
         SPACE 1
SETWTORN OI    FLAGS,FGWTOR  SET TO PRODUCE/HONOR WTOR STOP      84275
         TM    FLAGS,FGWTORED IS THERE A WTOR ALREADY ?          81167
         BNZR  R10           YES - DON'T NEED NEW ONE            81169
         OI    FLAGS,FGWTORED          SET SWITCH TO SHOW WTOR GIVEN
         WTOR  ,REPLY,4,ECB,MF=(E,WTOR)                          81169
         BR    R10                                               81169
         SPACE 1
SETSSI   MVC   SSITEXT,CARD+4   MOVE NEW SSI TEXT
         BR    R10           GET NEXT CARD                       81169
         SPACE 1                                                 81167
PROSEP   CLI   CARD+4,C'2'   VALID RANGE ?                       81167
         BL    PROSEPT       NO; CHECK FOR YES/NO                81167
         CLI   CARD+4,C'3'                                       81167
         BHR   R10           DITTO                               81169
         MVN   LISTNUM,CARD+4  SET NUMBER OF BIG LINES ON SEP. PAGE
PROSEPY  NI    FLAG2,255-FG2NOHED  ALLOW SEPARATORS AGAIN        81167
         BR    R10                                               81169
PROSEPT  CLC   =C'YES',CARD+4   ENABLE SEPARATORS ?              81167
         BE    PROSEPY       YES; SET                            81167
         CLC   CARD+4(2),DCNO+3   DISABLE ?                      81169
         BNER  R10           NO; IGNORE                          81169
PROSETN  OI    FLAG2,FG2NOHED  DISABLE SEPARATOR PAGES           81167
         BR    R10                                               81169
         SPACE 1                                                 81167
PROSETY  ZI    FLAG2,FG2NOHED  ENABLE SEPARATOR PAGES           GP09274
         BR    R10                                              GP09274
         SPACE 1                                                 81167
SETID3   MVC   LISTDSN,CARD+3  SET USER'S NAME                   81167
         MVI   LISTNUM,3     SET FOR THREE LINES                 81167
         B     PROSEPY       ENABLE SEPARATORS                   81167
         SPACE 1                                                 81167
SETCLINK BAS   R8,GETINT     GET INTEGER VALUE                   84275
         ST    R0,STOPLINK   SET LINK-EDIT STOP CODE             84275
         BR    R10           GET MORE                            84275
         SPACE 1                                                 91003
*   COND=RESET OR CODE=RESET CAUSES THE CURRENT COMPLETION CODE TO BE
*        SAVED, THEN ZEROED.  THIS ALLOWS ADDITIONAL ASSEMBLY/LINK
*        CYCLES IN A RUN, PROVIDING CODE AND LKEDCODE ARE HIGH ENOUGH.
SETCOND  CLC   =C'RESET',0(R5)  RESET REQUEST ?                  91003
         BNE   SETCOND#      NO; EXPECT A NUMBER                 91003
         STMAX R9,PREVCODE   SAVE HIGHEST CODE TO DATE           91003
         SLR   R9,R9         CLEAR THE CURRENT CODE              91003
         BR    R10           GO TO SCANNER                       91003
         SPACE 1                                                 91003
SETCOND# BAS   R8,GETINT     GET INTEGER VALUE                   84275
         ST    R0,STOPCODE   SET CONDITION CODE - COMPILER       84275
         BR    R10           RETURN FOR MORE                     84275
         SPACE 1                                                 91003
SETCODES BAS   R8,GETINT     GET INTEGER VALUE                   84275
         ST    R0,STOPCODE   SET CONDITION CODE - COMPILER       84275
         ST    R0,STOPLINK   SET CONDITION CODE - LINKER        GP04064
         BR    R10           RETURN FOR MORE                     84275
         SPACE 1                                                 84275
GETINT   SLR   R14,R14       SUBROUTINE TO RETURN INTEGER VALUE  84275
         LA    R15,C'0'      DEFAULT VALUE (FOR ALL BLANK)       84275
         LA    R0,8          LOOP FOR MAX OF 8 DIGITS            81167
GETINTL  CLI   0(R5),C'0'    NUMERIC ?                           84275
         BL    GETINTX       NO; STOP                            84275
         SLDL  R14,8         SHIFT OVER                          81167
         IC    R15,0(,R5)    ADD IT IN                           84275
         BXH   R5,R6,GETINTY           BUMP SCAN POINTER         84275
         BCT   R0,GETINTL    TRY ANOTHER                         84275
GETINTX  CLI   0(R5),C' '    TRAILING BLANK ?                    84275
         BNE   BADCARD       NO; ERROR                           84275
GETINTY  STM   R14,R15,DEVT  STASH IT                            84275
         PACK  WORK,DEVT     PACK IT                             81167
         CVB   R0,WORK       MAKE BINARY                         81167
         BR    R8            RETURN TO CALLER                    84275
         SPACE 1                                                 87074
*        SET DEFAULT FOR COMMON LKED.PARM (SIZE, ETC.)           87074
*                                                                87074
SETLKPRM LA    R4,NULLTAB    POINT TO DUMMY SCAN TABLE           87074
         BAS   R8,VERBSCAN   LOCATE OPERAND START                87074
         MVI   DFLLPART,C' '                                     87074
         MVC   DFLLPART+1(L'DFLLPART-1),DFLLPART  CLEAR TEXT     87074
         CLI   0(R5),C' '    NONE (RESET REQUEST ?) ???          87074
         BNE   SETLKPRD      FIND END OF OPERAND                 87074
         MVC   DFLLPARM(PATLPARX-PATLPARM),PATLPARM  DEFAULT     87074
         B     PROCESS       READ NEXT CARD                      87074
SETLKPRD LA    R1,DFLLPARM+2 GET START OF OPERAND                87074
         SLR   R2,R2         NEW LENGTH FIELD                    87074
SETLKPRL CLI   0(R5),C' '    END ?                               87074
         BE    SETLKPRX      YES                                 87074
         MVC   0(1,R1),0(R5) MOVE ONE BYTE                       87074
         AR    R1,R6         POINT TO NEXT ONE                   87074
         AR    R2,R6         INCREASE FIELD LENGTH               87074
         BXLE  R5,R6,SETLKPRL                                    87074
SETLKPRX STH   R2,DFLLPARM   SET PARM LENGTH                     87074
         B     PROCESS       GET ANOTHER CARD                    87074
         SPACE 1                                                 87074
SETLKDFL LA    R3,LOPTDFLT   POINT TO OUTPUT                     87074
         B     *+8           SET COMMON 'OR' PROCESSING          87074
         SPACE 1                                                 87074
SETLKOR  LA    R3,LOPTOR     SET OPTIONS TO ADD                  87074
         SLR   R4,R4         SET 'OR' FLAG                       87074
         STH   R4,0(,R3)     INITIALIZE                          87074
         B     SETLKCOM                                          87074
         SPACE 1                                                 87074
SETLKAND MVC   LOPTAND,=X'FFFF'  RESET - KEEP ALL OPTIONS        87074
         LA    R3,LOPTAND    SET TO OUTPUT FIELD                 87074
         LA    R4,4          SET AND FLAG                        87074
         SPACE 1                                                 87074
SETLKCOM ST    R4,DB+4       SAVE BRANCH OFFSET                  87074
         LA    R4,NULLTAB    GET NULL TABLE                      87074
         BAS   R8,VERBSCAL   SCAN FOR NEXT OPTION                87074
         CLI   0(R5),C' '    END ?                               87074
         BE    PROCESS       YES; PROCESS NEXT CARD              87074
         L     R4,DB+4       RESTORE BRANCH OFFSET               87074
         LR    R15,R7        GET LAST COLUMN TO SCAN             87074
         SR    R15,R5        GET LENGTH-1 TO EXAMINE             87074
         BM    PROCESS       EMPTY CARD ?                        87074
         LA    R1,1(,R7)     SET END IN CASE EXHAUSTED           87074
         SLR   R2,R2         CLEAR FOR IC BY TRT                 87074
         EX    R15,MEMTRT    SCAN FOR END OF NAME                87074
         LR    R15,R1        GET FIRST NON-ZERO ADDRESS          87074
         SR    R15,R5        LESS START ADDRESS                  87074
         BNP   BADCARD       INVALID TEXT                        87074
         BCTR  R15,0         EXECUTE LENGTH                      87074
         CH    R15,=Y(4)     NOT TOO LONG ?                      87074
         BNL   BADCARD       TOO BAD                             87074
         MVC   DB(4),BLANK8  PREPARE FOR OPTION MOVE             87074
LOPMVC   MVC   DB(0),0(R5)   MOVE LKED OPTION                    87074
         EX    R15,LOPMVC    MOVE                                87074
         LR    R5,R1         SET NEW START SCAN POINTER          87074
         L     R14,=A(SCANATTR)  GET ATTRIBUTE NAME/MASK TABLE   87074
         LA    R0,ATTRPAR2-ATTRPARM  SET ENTRY LENGTH            87074
         LA    R1,ATTRPARX-SCANATTR-(ATTRPAR2-ATTRPARM)(,R14) LAST ONE
SETLKCOP CLC   DB(4),4(R14)  MATCHING OPTION ?                   87074
         BE    SETLKCOF      YES; SET BITS                       87074
         BXLE  R14,R0,SETLKCOP                                   87074
         B     BADCARD       INVALID OPTION                      87074
SETLKCOF OC    0(2,R3),0(R14)  SET THE REQUESTED OPTION ON       87074
         LTR   R4,R4         ON ONLY ?                           87074
         BZ    SETLKCOM      YES; SCAN FOR ANOTHER OPTION        87074
         XC    0(2,R3),0(R14)  AND FLIP IT OFF                   87074
         B     SETLKCOM                                          87074
         SPACE 1
SETOPT   CLI   CARD+1,C' '   CHECK FOR ALL BLANK CARD            81169
         BNE   SETOPTT       NO; CHECK INDIVIDUAL OPTIONS        81169
         CLC   CARD+2(70),CARD+1  ALL BLANK ?                    81169
         BE    SETAUNM       YES; TURN NAME/SSI ON               81169
SETOPTT  BASR  R10,0         SET NEW RETURN POINT                81169
         LA    R4,MODTAB     SET MODE OPTION TABLE               81169
         TM    ACB,NEEDDSN+NEEDDDN  JCL REQUIRED ?               84275
         BNZ   *+8           YES; USE SHORT TABLE                84275
         LA    R4,MODTABAL   ELSE USE FULL TABLE                 84275
         BAS   R8,VERBSCAN   SCAN OUT INDIVIDUAL OPTIONS         81169
         LA    R10,PROCESS   SET RETURN ADDRESS                  82249
         CLI   0(R5),C' '    NORMAL END ?                        81169
         BNE   BADCARD       NO; INVALID OPTION                  81169
         BR    R10           GET ANOTHER CARD                    82249
         SPACE 1                                                 89282
SETINCL  NI    FLAG3,255-FGNOINC  ALLOW 'INCLUDE' CARD           89282
         BR    R10                                               89282
SETNINCL OI    FLAG3,FGNOINC   INHIBIT 'INCLUDE SYSLMOD' CARDS   89282
         BR    R10                                               89282
         SPACE 1                                                 81169
SETYLLA  TM    FLAG3,FGAUTH  AUTHORIZED?                        GP97289
         BZ    SETYLLAF          OOPS                           GP97289
         OI    FLAG3,FGLLA   REQUEST LLACOPY                    GP97289
         BR    R10                                              GP97289
SETYLLAF PRTV  MSGLLAUT,CC=NO,DEV=2                             GP02251
SETNLLA  NI    FLAG3,255-FGLLA  NO LLA COPY                     GP97289
         BR    R10                                              GP97289
MSGLLAUT VCON  ' LLA OPTION REQUIRES AUTHORIZATION'             GP02251
         SPACE 1                                                 81169
SETPGON  OI    FLAGS,FGAUTOPG   SET PAGE CARD GENERATION         81167
SETAUNM  OI    FLAGS,FGAUTONM  SET LKED NAME CARD CREATION ON    81167
         BR    R10           GET NEXT CARD                       81169
         SPACE 1                                                 81167
SETPGNO  NI    FLAGS,255-FGAUTOPG  RESET PAGE CARDS              81167
         BR    R10           GET ANOTHER                         81169
         SPACE 1
NAMPR0   NI    FLAGS,255-FGAUTONM  RESET AUTOMATIC NAME CARD OPTION
         BR    R10           GET MORE                            81169
         SPACE 1
X9700ON  OI    LISTFLAG,LF9700  TURN XEROX 9700 OPTION ON        81167
         LA    R4,XERTAB     GET SUB-OPTIONS                     90101
         BAS   R8,VERBSCAL   GET NEXT OPTION                     90101
         BR    R10           GET MORE                            81169
XERTRAY  OI    LISTFLAG,LFXRXAUX  SET AUXILIARY TRAY FEED        90101
         BR    R10           END OF CARD ?                       90101
         SPACE 1                                                 81167
SETNOSVC OI    FLAGS,FGNOSVC   SET TO BYPASS NAME CHECK ON NAME CARD
         BR    R10           GET ANOTHER                         81169
         SPACE 1                                                 81169
VERBSCAN LA    R7,CARD+70    SET STOP SCAN ADDRESS               81169
VERBSCAM LA    R6,1          MAKE INCREMENT                      81169
VERBSCAL CLI   0(R5),C' '    LEADING BLANK ?                     81169
         BE    VERBSCAP                                          81169
         CLI   0(R5),C','    COMMA ?                             81169
         BNE   VERBLOOK      NO; LOOK FOR MATCH                  81169
VERBSCAP BXLE  R5,R6,VERBSCAL  TRY NEXT COLUMN                   81169
         SR    R5,R6         POINT TO LAST BLANK (FOR USER TEST) 81169
         BR    R8            RETURN NOT FOUND                    81169
VERBLUMP LA    R4,4(R4,R15)  SPACE TO NEXT TABLE ENTRY           81169
VERBLOOK SLR   R15,R15       CLEAR IC REGISTER                   81169
         ICM   R15,1,0(R4)   INSERT TABLE TEXT LENGTH-1          81169
         BMR   R8            RETURN NOT FOUND - END OF TABLE     81169
         EX    R15,VERBCLC   MATCHING VERB ?                     81169
         BNE   VERBLUMP      NO; TRY NEXT                        81169
         LA    R5,1(R5,R15)  POINT SCAN REGISTER PAST VERB       81169
         ICM   R15,3,1(R4)   GET BRANCH OFFSET                   81169
         B     ASMANY(R15)   INVOKE THE VERB ROUTINE             81169
VERBCLC  CLC   0(0,R5),3(R4)   COMPARE TEXT TO VERB              81169
         SPACE 2                                                 84275
SETNLINK MVI   LKFLAG,LKFGNEW+LKFGPARM+LKFGLKED  NEW OR OLD MBR  87074
         B     SETLINKS                                          87074
SETRLINK MVI   LKFLAG,LKFGNAME+LKFGALII+LKFGSSI    RE-LINK       84275
SETLINK  OI    LKFLAG,LKFGPARM+LKFGINS+LKFGLKED    LINK          84275
SETLINKS BAS   R8,LINKINIT   INITIALIZE FOR EVENTUAL LINK-EDIT   84275
         LA    R10,PROCESS   HOG REST OF CARD (NOT SETOPT)       84275
         LA    R4,NULLTAB    GET DUMMY TABLE                     84275
         BAS   R8,VERBSCAN   LOOK FOR ANYTHING                   84275
         CLI   0(R5),C' '    BLANK ?                             84275
         BH    NEWMEM        NO; PROCESS ASSEMBLY/LINK REQUEST   85083
         MVC   MAINNAME,BLANK8  SET LKED ONLY                    85083
         MVC   DB(2),LOPTDFLT  GET DEFAULT OPTIONS               89081
         XI    DB+1,X'80'    FLIP THE 'DC' BIT                   89081
         L     R15,=A(MAKLPARM)                                 GP99053
         BASR  R3,R15        BUILD LKED PARM (IF NOT DONE)       89081
         NI    LKFLAG,LKFGLKED  SET FOR LINK-EDIT ONLY           85083
         B     NAMECARD      CHECK FOR LINK-EDIT ONLY            85083
         SPACE 1                                                 84261
*        UNIDENTIFIED CARD - TREAT AS NEW MEMBER NAME (ASM REQ.) 84261
*        IF MORE THAN ONE WORD, THE EXTRA WORDS ARE ASSEMBLIES   84261
*        TO BE INCLUDED WITH THE FIRST ONE -                     84261
*                                                                84261
*        'DISASM DISASM1 DISASM2' ASSEMBLES THREE MODULES, WITH  84261
*            THE MAIN MEMBER BEING DISASM                        84261
*        'IEHVTOC: IEHVMAIN IEHVINDX IEHVFMT1,IEHVFMT2 +  '      84261
*        'IEHVFMT3 IEHVFMT4 IEHVFMT5 IEHVFMT6             '      84261
*            ASSEMBLES 8 MODULES (EXCL. IEHVTOC), AND USES THE   84261
*            MEMBER NAME IEHVTOC ON THE LKED NAME CARD.          84261
*        A) COMMAS OR BLANKS MAY SEPARATE MEMBER NAMES           84261
*        B) COLON AFTER FIRST INDICATES LKED NAME, NOT ASSEMBLY  84261
*        C) TRAILING PLUS OR MINUS (OR NON-BLANK COL. 72) INDICATES
*             CONTINUATION                                       84261
*        D) * STARTS A COMMENTS FIELD                            84261
         SPACE 1                                                 84261
NEWMEM   NI    FLAG2,255-FG2STACK  RESET STACKED ASSEMBLIES      84261
         MVC   MAINNAME,BLANK8  CLEAR MAIN MEMBER NAME           84261
NEWMEM2  MVC   JFCBELNM,BLANK8  CLEAR ASSEMBLY NAME              84261
         CLI   0(R5),C'0'    NUMERIC OR INVALID START ?          84261
         BNL   BADCARD       YES; SYNTAX ERROR                   84261
         LR    R15,R7        GET LAST COLUMN TO SCAN             84261
         SR    R15,R5        GET LENGTH-1 TO EXAMINE             84261
         BM    BADCARD       EMPTY CARD ?                        84261
         LA    R1,1(,R7)     SET END IN CASE EXHAUSTED           84261
         SLR   R2,R2         CLEAR FOR IC BY TRT                 84261
         EX    R15,MEMTRT    SCAN FOR END OF NAME                84261
         LR    R15,R1        GET FIRST NON-ZERO ADDRESS          84261
         SR    R15,R5        LESS START ADDRESS                  84261
         BNP   BADCARD       INVALID TEXT                        84261
         BCTR  R15,0         EXECUTE LENGTH                      84261
         CH    R15,=Y(L'JFCBELNM)  NOT TOO LONG ?                84261
         BNL   BADCARD       TOO BAD                             84261
         EX    R15,MEMMVC    MOVE                                84261
         LR    R5,R1         SET NEW START SCAN POINTER          84261
NEWMEMND STM   R5,R7,NAMESTAK  SAVE SCAN REGISTERS               84261
         TM    FLAG2,FG2STACK  FIRST TIME AROUND ?               84261
         BNZ   NEWMEMF2      NO; SPECIAL CHECKS                  84261
         OI    FLAG2,FG2STACK  SET STACK ON                      84261
         MVC   MAINNAME,JFCBELNM  COPY NAME                      84261
         MVC   PDS2NAME,MAINNAME  SET DEFAULT LKED COPY NAME     89281
         CLM   R2,1,NAMETRT+C':'  LKED NAME ONLY ?               84261
         BNE   NEWMEMDO      NO; ASSEMBLE THIS ONE               84261
         MVC   JFCBELNM,BLANK8  CLEAR ASSEMBLY NAME FOR CHECK    84261
         BXH   R5,R6,POSTNAME  SKIP IF NO ROOM                   89281
         CLI   0(R5),C'0'    NUMERIC OR INVALID START ?          89281
         BNL   POSTNAME      YES; SYNTAX ERROR                   89281
         LR    R15,R7        GET LAST COLUMN TO SCAN             89281
         SR    R15,R5        GET LENGTH-1 TO EXAMINE             89281
         BM    POSTNAME      EMPTY CARD ?                        89281
         LA    R1,1(,R7)     SET END IN CASE EXHAUSTED           89281
         SLR   R2,R2         CLEAR FOR IC BY TRT                 89281
         EX    R15,MEMTRT    SCAN FOR END OF NAME                89281
         LR    R15,R1        GET FIRST NON-ZERO ADDRESS          89281
         SR    R15,R5        LESS START ADDRESS                  89281
         BNP   POSTNAME      INVALID TEXT                        89281
         BCTR  R15,0         EXECUTE LENGTH                      89281
         CH    R15,=Y(L'JFCBELNM)  NOT TOO LONG ?                89281
         BNL   POSTNAME      TOO BAD                             89281
         MVC   PDS2NAME,BLANK8  CLEAR REFERENCE NAME             89281
         EX    R15,MEMMVC2   MOVE                                89281
         ST    R1,NAMESTAK   RESAVE SCAN REGISTERS               89281
         B     POSTNAME      SCAN FOR NEXT NAME                  84261
MEMTRT   TRT   0(0,R5),NAMETRT  CHECK FOR END OF NAME            84261
MEMMVC   MVC   JFCBELNM(0),0(R5)  MOVE NAME TO JFCB              84261
MEMMVC2  MVC   PDS2NAME(0),0(R5)  MOVE NAME TO LKED REFERENCE    89281
         SPACE 1                                                 84261
NEWMEMF2 CLI   JFCBELNM,C' '  ANY MEMBER EVER PROCESSED ?        84261
         BNH   BADCARD       SEQUENCE OR LOGIC ERROR             84261
         SPACE 1
NEWMEMDO SERVCALL DSMEM,JFCBELNM   VALIDITY CHECK THE NAME       84261
         BXLE  R15,R15,NEWMEMOK  OK                              84261
BADCARD  PRTV  MSGBDCRD,CC=NO,DEV=(2,3)                         GP02251
LVL8ERR  LA    R15,8         SET COND.CODE=8                     84261
         CR    R9,R15                                            81169
         BNLR  R10                                               81169
         LR    R9,R15        SET COND CODE                       81169
         BR    R10           ?                                   81169
MSGBDCRD VCON  ' *** INVALID CARD ***'                          GP02251
NEWMEMOK MVC   LISTMEM,JFCBELNM  MOVE MEMBER NAME TO SEPARATOR   84261
         CLI   JFCDSRG2,0    NON-PDS ?                           88329
         BNE   NEWMEMPS      WHAT IS IT ?                        88329
         TM    JFCDSRG1,255-(JFCORGPO+JFCORGU) NON-PDS ?         88329
         BNZ   NEWMEMPS      YES; TREAT AS SEQUENTIAL            88329
         TM    JFCBIND1,JFCGDG  GENERATION DATA ?                88329
         BNZ   NEWMEMPS      YES; NO PDS MEMBER ALLOWED          88329
         OI    JFCBIND1,JFCPDS  INDICATE MEMBER NAME PRESENT     88329
NEWMEMPS TM    FLAGS,FGWTOR  REPLIES TO BE IGNORED ?             81167
         BZ    NOREPLY       YES                                 81167
         TM    ECB,X'40'                WAS REPLY ECB POSTED?
         BZ    NOREPLY                   NOT YET
         NI    FLAGS,FGWTORED     RESET WTOR CONTROL FLAG        81167
         XC    ECB,ECB
         OC    REPLY,BLANK8  TRANSLATE REPLY TO UPPER CASE       84261
         CLC   DCSTOP,REPLY                                      84275
         BE    POSTNAME                                          84261
         CLC   =C'U ',REPLY   DID OPERATOR GOOF ?
         BNE   BADREPLY
         WTO   'WHO NEEDS U ?',ROUTCDE=(1),DESC=(1)
BADREPLY WTOR  MF=(E,WTOR)                                       81169
         XC    REPLY,REPLY                                       81167
         OI    FLAGS,FGWTORED   SET FOR WTOR ISSUED              81167
NOREPLY  PRTCLOSE DEV=4      CLOSE THE SYSGO FILE                81167
         NI    FLAGS,255-FGSYSGO   RESET OPEN BIT                81167
         AIF   (NOT &MVS AND &SVCJFCB EQ 0).DOSVCAT             GP08288
         TM    FLAG3,FGAUTH  AUTHORIZED?                        GP99053
         BZ    ATTASMIN      NO; USE ATTACH                     GP99053
         NI    PRM240IN+4,X'7F'  APPEND TO PLAIN LIST           GP99053
         SVCJFCB PRM240IN    SET ASMIN MEMBER NAME               84275
         OI    PRM240IN+4,X'80'  RESTORE END OF LIST AFTER NAME  81169
ATTASMIN LTR   R15,R15       DID THIS WORK?                     GP99053
         BZ    DIDASMIN      YES                                GP99053
         LA    R2,=CL8'ASMANY1'  NAME OF OPEN ROUTINE           GP99053
         LR    R1,R13        PASS WORK AREA ADDRESS AS PARM     GP99053
         BAS   R8,ATTACK     ATTACH OPEN ROUTINE                GP99053
         TM    FLAG3,FGAUTH  AUTHORIZED?                        GP99053
         BZ    GOASMBL       NO; JUST GO ASSEMBLE               GP99053
DIDASMIN SLR   R15,R15       ***PERMIT SEQUENTIAL INPUT FILES*** 84261
         TM    FLAG4,FGSVCPR   IS PRINTER A PDS ?                81167
         BZ    NOSVCPR       NO                                  81167
         CLI   DDNPRINT,0    MISSING PRINT DD ?                  81167
         BE    NOSVCPR       YES; SKIP MEMBER CHANGE             81167
         SVCJFCB PRM240PR,ERR=MODFAIL  SET SYSPRINT MEMBER NAME  84275
NOSVCPR  TM    FLAG4,FGSVCPU  IS PUNCH A PDS ?                   81167
         BZ    NOSVCPU       NO                                  81167
         CLI   DDNPUNCH,0    PUNCH DD MISSING ?                  81167
         BE    NOSVCPU       YES; SKIP CHANGE                    81167
         SVCJFCB PRM240PU,ERR=MODFAIL  SET SYSPUNCH MEMBER NAME  84275
NOSVCPU  TM    FLAG4,FGSVCAD  IS ADATA A PDS ?                  GP04065
         BZ    NOSVCAD       NO                                 GP04065
         CLI   DDNADATA,0    ADATA DD MISSING ?                 GP04065
         BE    NOSVCAD       YES; SKIP CHANGE                   GP04065
         SVCJFCB PRM240AD,ERR=MODFAIL  SET SYSADATA MEMBER NAME GP04065
NOSVCAD  TM    FLAG4,FGSVCOB  IS SYSLIN A PDS ?                 GP97241
         BZ    NOSVCOB       NO                                 GP97241
         CLI   DDNLIN,0      SYSLIN DD MISSING ?                GP97241
         BE    NOSVCOB       YES; SKIP CHANGE                   GP97241
         SVCJFCB PRM240OB,ERR=MODFAIL  SET SYSPUNCH MEMBER NAME GP97241
NOSVCOB  B     GOASMBL       GO TO ASSEMBLY                      84275
         AGO   .CMSVCAT                                          81167
.DOSVCAT LA    R2,=CL8'ASMANY1'  NAME OF OPEN ROUTINE            81167
         LR    R1,R13        PASS WORK AREA ADDRESS AS PARM      81169
         BAS   R8,ATTACK     ATTACH OPEN ROUTINE                GP99053
         B     GOASMBL       GOOD RETURN, CALL ASSEMBLER
.CMSVCAT SPACE 1                                                 81167
MODFAIL  CR    R9,R15        HIGHER RETURN CODE THAN BEFORE ?    81167
         BNL   *+6           NO, LEAVE IT
         LR    R9,R15        USE HIGHER CODE
         PRTV  MSGJFCBD,CC=NO,DEV=(2,3)                         GP02251
         OI    FLAGS,FGSTOP  TERMINATE                           81167
         B     ABEND         BAD RETURN CODE
MSGJFCBD VCON  ' JFCB MODIFY FAILED'                            GP02251
         SPACE 1
*COME HERE TO DO ONE ASSEMBLY
GOASMBL  PRTLIST MSGASMBL,DEV=(2,3)  PRODUCE 'ASSEMBLING ...' MSG
         TM    FLAG2,FGNOSEP+FG2NOHED  SKIP SEPARATORS ?        GP04065
         BNZ   SKPHEAD       YES - PREVENT B14 - SKIP SEPARATOR  81167
         TM    FLAG4,FGSVCPR  SKIP SEPARATORS ?                 GP04065
         BNZ   SKPHEAD       YES - PREVENT B14 - SKIP SEPARATOR  81167
         PRTOPEN SYSPRINT,OPT=(DUMMY,NOWTO)  OPEN ASM PRINT FILE 81167
         PRTBIG LIST=LISTFLAG         WRITE THE PAGE SEPARATOR   81167
         PRTCLOSE , DEV=1    CLOSE THE FILE AGAIN                81167
SKPHEAD  NI    LISTFLAG,LF9700+LFXRXAUX   RESET FLAG BYTE        90101
         AIF   (NOT &MVS AND &SVCJFCB EQ 0).SKPMODJ             GP08288
         TM    FLAG2,FG2ONE+FG2TWO  BEEN HERE BEFORE ?           81167
         BO    SKPMODJF      SKIP DISP MODIFICATION              81167
         TM    FLAG3,FGAUTH  AUTHORIZED?                        GP99053
         BZ    SKPMODJF      NO; NO CHANGE                      GP99053
         TM    FLAG2,FG2ONE  FIRST TIME ?                        81167
         BZ    SKPMODJ1      NO; JUST SET FLAG                   81167
         CLI   DDNPRINT,0    PRINTER DD MISSING ?                81167
         BE    SKPMODPR      YES; SKIP                           81167
         SVCJFCB PRMMODPR    GET DISP=MOD LIST                   84275
SKPMODPR CLI   DDNPUNCH,0    PUNCH DD MISSING ?                  81167
         BE    SKPMODPU      YES; SKIP                           81167
         SVCJFCB PRMMODPU    GET DISP=MOD FOR PUNCH              84275
SKPMODPU CLI   DDNLIN,0      SYSLIN DD MISSING ?                GP97241
         BE    SKPMODOB      YES; SKIP                          GP97241
         SVCJFCB PRMMODOB    GET DISP=MOD FOR SYSLIN            GP97241
SKPMODOB CLI   DDNADATA,0    SYSADATA DD MISSING ?              GP08288
         BE    SKPMODAD      YES; SKIP                          GP08288
         SVCJFCB PRMMODAD    GET DISP=MOD FOR SYSADATA          GP08288
SKPMODAD OI    FLAG2,FG2TWO  SET MOD ISSUED                      81167
SKPMODJ1 OI    FLAG2,FG2ONE  SHOW FIRST ASSEMBLY CALLED          81167
SKPMODJF EQU   *                                                 81167
.SKPMODJ SPACE 1                                                 81167
         LA    R1,MYPARM     SUPPLY USER PARM, PLUS MY DDNAMES
         LA    R2,ASMPGMNM   SET CURRENT ASSEMBLER NAME          81169
         BAS   R8,ATTACK     ASSEMBLE                           GP99053
         B     POSTGOMD      GOOD RETURN                         84275
         LA    R14,4095                                          81169
         CR    R15,R14       ABEND ?                             81169
         BNL   *+6           YES                                 81169
         LR    R14,R15       LOAD LOWER CODE                     81169
         CR    R14,R9                   TEST RETURN CODE         81169
         BNH   *+6           RETAIN ONLY HIGHEST RETURN CODE
         LR    R9,R14                   THIS ONE GREATER, SAVE IT
         CR    R15,R14       ABEND CODE ?                        81169
         BH    ABEND
         CH    R15,=H'20'    ASSEMBLER I/O ERROR ?               81169
         BE    ASCLEAN       YES, CLEAN UP SYSPRINT AFTER IT
         PRTLIST MSGASMER,DEV=(2,3)    PRINT THE ASMBLR ERROR MESSAGE
         SPACE 1                                                 84275
POSTGOMD TM    FLAGS,FGSVCGO NEED TO SET DISP=MOD ON SYSGO ?     84275
         BZ    POSTNAME      NO                                  84275
         NI    FLAGS,255-FGSVCGO  RESET IT                       84275
         AIF   (NOT &MVS AND &SVCJFCB EQ 0).GOMOD               GP08288
         TM    FLAG3,FGAUTH  AUTHORIZED?                        GP99053
         BZ    POSTNAME      NO; NO CHANGE                      GP99053
         MVC   DEVT(8),PRMMODOB  COPY JFCBMOD SVC PARM LIST     GP97241
         TM    FLAGS,FGSVCOB  IS SYSLIN A PDS?                  GP97241
         BNZ   POSTGOGO      YES; DONE ELSEWHERE                GP97241
         LA    R14,DDNLIN    SET FOR SYSLIN                      88111
         CLI   0(R14),0      ANY ?                               88111
         BH    POSTGOMG      YES; USE IT                         88111
POSTGOGO LA    R14,DDNGO                                         84275
POSTGOMG STCM  R14,7,DEVT+1  BUT SET SYSGO DDNAME POINTER        88111
         SVCJFCB DEVT        SET DISP=MOD, IGNORE ERROR ?        84275
.GOMOD   SPACE 1                                                 84275
POSTNAME LM    R5,R7,NAMESTAK  SEE IF ANY MORE ASSEMBLIES/MEMBER 84261
         BCTR  R5,0          BACK UP ONE                         84261
POSTLOOP BXH   R5,R6,POSTFULL  END OF CARD                       84261
         SLR   R2,R2         CLEAR FOR IC                        84261
         TRT   0(1,R5),NAMETRT  CHECK CURRENT CHARACTER          84261
         BIX   VAL=(R2),ERR=BADCARD,PFX=POST,                          *
               LOC=(MEM2,LOOP,LOOP,LOOP,FULL,GET,LKNM)           84261
POSTLKNM CLI   JFCBELNM,C' '  FIRST TIME ?                       84261
         BH    BADCARD       NO, ERROR                           84261
         B     POSTLOOP                                          84261
POSTFULL CLI   CARD+71,C' '  CONTINUATION ?                      84261
         BNH   NAMECARD      NO; WRITE NAME CARD, ETC.           84261
POSTGET  INPGET ,            GET ANOTHER CARD                    84261
         CH    R15,=H'4'     CHECK RETURN                        84261
         BL    POSTGOT       OK                                  84261
         BH    GETINERR      BOO                                 84261
         PRTV  MSGNOCNT,CC=NO,DEV=(2,3)  PRINT/WTO ERROR MSG    GP02251
         SERVTERM ,          FORCE MESSAGE OUT                   84261
         B     GETINERR      ABEND                               84261
MSGNOCNT VCON  ' *** EXPECTED CONTINUATION NOT FOUND ***'       GP02251
POSTGOT  LR    R3,R1         SAVE CARD START                     84261
         LR    R5,R3         DITTO                               84261
         LA    R7,CARD+70    BXLE END                            84261
         PRTLIST CARDLIST,DEV=2   ECHO THE INPUT                 84261
         CLC   CARD(70),CARD+1    ALL BLANK?                    GP10030
         BE    POSTGET       YES; IGNORE AFTER PRINTING         GP10030
         B     POSTLOOP      TRY FOR MORE                        84261
         SPACE 1                                                 81167
POSTMEM2 C     R9,STOPCODE   REACHED TERMINAL ERROR CONDITION ?  84261
         BL    NEWMEM2       NO; CONTINUE                        84261
NAMECARD MVC   JFCBELNM,MAINNAME  RESTORE MEMBER NAME            84261
         LM    R3,R5,MAILBAG   GET BXH POINTERS FOR POST-ASM CARDS
NAMEPOLP BXH   R3,R4,NAMEPOST                                    81169
         L     R15,=A(PUTLCARD)                                 GP99053
         BASR  R8,R15        ADD TO SYSGO                       GP99053
         B     NAMEPOLP      SEE IF MORE                         81169
         SPACE 1
NAMEPOST MVC   MAILBAG+8(4),MAILBAG  RE-INITIALIZE               81169
         TM    LKFLAG,LKFGINS+LKFGPARM+LKFGSSI+LKFGALII+LKFGNAME 84275
         BZ    LOADPOST      BRANCH AROUND IF MAP UNNEEDED       84275
         BAS   R14,LOADMAPS  ELSE BUILD CONTROL CARDS            84275
         ST    R15,DB                                           GP02265
         BXLE  R15,R15,LOADPOST   YES                            84275
         PRTLIST MSGMAPER,DEV=(2,3)                             GP02265
         LA    R15,4095      SET MAJOR ERROR                     84275
         B     ENDFILE       QUIT                                84275
MSGMAPER FDOPT NL,CC=C'0'    DOUBLE SPACE                       GP02265
         FDPRT '*** ERROR'                                      GP02265
         FDPRT DB,4,I,DEB,PAD                                   GP02265
         FDPRT 'IN MAP PROCESSING ***'                          GP02265
         FDPRT *END                                             GP02265
         SPACE 1                                                GP02265
LOADPOST TM    FLAGS,FGAUTONM   LKED NAME OPTION ON ?            81169
         BZ    NAMEPAST      NO; SKIP CARD GENERATION            85083
         MVC   WORK,JFCBELNM   COPY NAME TO FD WORK AREA         81169
         CLI   JFCBELNM,C' '  LINK-EDIT ONLY ?                   85083
         BE    NAMEPAST      YES; JUST CLOSE                     85083
         LA    R1,WORK       SET LOCATION OF NAME                89081
         L     R15,=A(TESTSVCN)                                 GP99053
         BASR  R14,R15       CHECK FOR SVC NAME AND FIX         GP99053
         TM    LKFLAG,LKFGNAME  NAME ALREADY DONE ?              84275
         BNZ   NAMEPAST      YES; DON'T DO IT AGAIN              84275
         LA    R6,AUTOCARD   GET LIST FOR PAGE/SSI/NAME CARDS    84275
         L     R15,=A(PUTLKED)                                  GP99053
         BASR  R8,R15        PROCESS 1-3 CONTROL CARDS          GP99053
NAMEPAST PRTCLOSE DEV=4      CLOSE SYSGO                         84275
         NI    FLAGS,255-FGSYSGO  SHOW CLOSED                    81167
         TM    FLAGS,FGSTOP  SEVERE ERROR ?                      81167
         BNZ   ENDFILE       YES; QUIT                           81167
         L     R1,CIBPTR     GET COMM ECB POINTER                81167
         L     R1,0(,R1)     GET COMM ECB ADDRESS                81167
         TM    0(R1),X'50'   STOP POSTED ?                       81167
         BNO   CHKSTOP       NO; CHECK REPLY                     81167
         PRTV  MSGSTOPD,CC=NO,DEV=2                             GP02251
         B     ENDFILE       QUIT                                81167
MSGSTOPD VCON  ' STOP COMMAND ISSUED'                           GP02251
         SPACE 1                                                GP97206
CHKSTOP  CLC   DCSTOP,REPLY  GOT HERE BECAUSE OF STOP ?          84275
         BE    ENDFILE       YES; QUIT                           81167
         C     R9,STOPCODE   TERMINATION CODE REACHED ?          81167
         BNL   CONDSTOP      YES; STOP                           84275
         TM    LKFLAG,LKFGLKED  INVOKED LINKAGE-EDITOR ?         84275
         BZR   R10           NO; GET NEXT CONTROL CARD           84275
         C     R9,STOPLINK   ALLOWED TO INVOKE LINK-EDIT ?       84275
         BNL   CONDSTOP      NO; SAY SO                          84275
         TM    FLAGS,FGSYSGO  IS SYSGO OPEN ?                    84275
         BZ    GOLINK                                            84275
         NI    FLAGS,255-FGSYSGO  RESET IT                       84275
         PRTCLOSE DEV=4      CLOSE IT                            84275
GOLINK   SERVCALL RJFCB,LKEDDMOD  GET THE DSN OF THE LOAD LIBRARY 97241
         LR    R8,R1         PRESERVE IT FOR FORMATTING         GP97241
         PRTLIST LINKMSG,DEV=(2,3)                               84275
         PRTSPACE 0,DEV=(2,3)  FORCE ANY PARTIAL MESSAGES OUT    84275
         LA    R1,LKEDPARM   GET LINK-EDIT PARAMETERS            84275
         LA    R2,LINKEP     AND NAME                            84275
         BAS   R8,ATTACK     INVOKE LINK-EDIT                    84275
         NOP   LINKOK0       ZERO RETURN
         LTR   R2,R15        SAVE RETURN CODE                   GP97289
         BZ    LINKOK        GOOD                               GP97289
         LR    R14,R15       COPY COMPLETION CODE                84275
         SRA   R14,12        TEST RETURN FOR ABEND               84275
         BZ    LINKERR       CONTINUE CONDITIONALLY              81169
         ST    R14,WORK                                          84275
         PRTLIST LINKAMSG,DEV=(2,3)                              84275
         LA    R15,4095      SET FOR ABEND CODE                  84275
         B     ENDFILE       AND QUIT                            84275
LINKOK0  SLR   R2,R2         SET ZERO RETURN IN R2, ALSO
LINKERR  CR    R9,R15        HIGHER THAN PRIOR ?                 84275
         BNL   *+6           NO                                  84275
         LR    R9,R15        KEEP HIGHEST                        84275
         PRTLIST LINKEMSG,DEV=(2,3)                              84275
LINKOK   C     R9,STOPCODE   TIME TO STOP ?                      84275
         BNL   CONDSTOP      YES                                 84275
         TM    FLAG3,FGLLA   LLACOPY REQUESTED?                 GP97289
         BZ    NOFRESH                                          GP97289
         CH    R2,=H'4'      ACCEPTABLE RETURN CODE?            GP97289
         BH    NOFRESH       NO                                 GP97289
         SUBCALL REFRESH     GO TO LLA REFRESH CODE             GP04065
NOFRESH  LA    R14,DDNLIN                                        88111
         CLI   0(R14),0      HAVE A SYSLIN ?                     88111
         BH    LINKOKGO      YES; USE IT                         88111
         CLI   DDNGO,0       SYSGO MISSING ?                     84275
         BE    LINKEND       YES; DON'T KNOW WHAT TO DO          84275
         LA    R14,DDNGO     POINT TO DDN                        84275
LINKOKGO OI    FLAGS,FGSVCGO   REQUEST DISP=MOD AFTER NEXT OPEN  88111
         AIF   (NOT &MVS AND &SVCJFCB EQ 0).LNKONZE             GP08288
         TM    FLAG3,FGAUTH  ARE WE AUTHORIZED?                 GP99053
         BZ    LINKOKAU                                         GP99053
         SLR   R15,R15       ZERO THE PARAMETER                  84275
         STM   R14,R15,WORK  STASH THE PARAMETERS                84275
         MVI   WORK,X'01'    IDENTIFY DDNAME                     84275
         MVI   WORK+4,X'8F'  SET END OF LIST; DISP=OLD           84275
         SVCJFCB WORK        SET DISP=OLD                        84275
         B     LINKOKAD                                         GP99053
LINKOKAU DS    0H                                               GP99053
         AIF   (NOT &MVS).LNKONCE                               GP08288
.LNKONZE SERVCALL DDCLR,(R14)  RE-INITIALIZE SYSLIN/SYSGO        90218
.LNKONCE ANOP  ,                                                GP99053
LINKOKAD LTR   R15,R15       DID IT WORK ?                       90218
         BZR   R10           YES; CONTINUE                       84275
         SPACE 1                                                 90218
LINKEND  INPGET ,            SEE IF THERE IS ANOTHER CARD        84275
         CH    R15,=H'4'     END-FILE RETURN CODE ?              84275
         BE    ENDFILE       YES; SKIP WARNING MESSAGE           84275
         PRTV  MSGXNOGO,CC=ASA,DEV=(2,3)   OOPS                 GP02251
         LA    R9,20         SET ERROR CODE                      84275
         B     ENDFILE                                           84275
MSGXNOGO VCON  '0EXECUTION TERMINATED; UNABLE TO CLEAR SYSGO'   GP02251
MSGSTPCC VCON  ' STOPPED DUE TO CONDITION CODE'                 GP03074
         SPACE 1                                                GP02251
CONDSTOP PRTV  MSGSTPCC,CC=NO,DEV=(2,3)                         GP02251
         SPACE 1
*COME HERE AFTER DOING ALL ASSEMBLIES REQUESTED...
ENDFILE  TM    FLAGS,FGSVCGO IS SYSGO FILE FULL OF TRASH ?       85083
         BZ    ENDFILED      NO ?                                85083
         PRTOPEN SYSGO,OPT=DUMMY,DEV=4  OPEN AND RESET           85083
         PRTCLOSE DEV=4      CLOSE AND CLEAR FILE                85083
ENDFILED SERVTERM ,          CLOSE AND FREE EVERYTHING ELSE      85083
         MAX   R9,PREVCODE   USE HIGHEST CODE TO DATE            91003
         LA    R14,4095      SET MAXIMUM                         84261
         CR    R9,R14        HIGHER THAN MAX OS CONDITION CODE ? 84261
         BNH   *+6           NO                                  84261
         LR    R9,R14        YES; SET MAXIMUM                    84261
         PGMEXIT RC=(R9)     SET HIGHEST RC FROM ASMBLR         GP02251
         SPACE 2
*---------------------------------------------------------------------*
*    ATTACH SUBROUTINE                                  *
*---------------------------------------------------------------------*
ATTACK   MVC   EPLOC(8),0(R2)  COPY THE MODULE NAME
         XC    ACB,ACB
         ATTACH EPLOC=EPLOC,ECB=ACB,SF=(E,ATTALIST)              81169
         ST    R1,WORK
         WAIT  ECB=ACB
         DETACH WORK
         MVI   ACB,0         CLEAR POST BITS (USED BY PRTLIST)
         ICM   R15,15,ACB    LOAD AND TEST COMPLETION CODE
         BNZ   4(,R8)        NO GOOD
         BR    R8
         SPACE 1
DOOPEN   DS    0H            DEFINE FOR IDENTIFY                GP02251
         AIF   (&SVCJFCB NE 0).NOJFCAT                           82102
*        THIS ROUTINE IS ATTACHED AS 'ASMANY1' PRIOR TO
*        ATTACHING THE ASSEMBLER TO PLACE THE MEMBER NAME INTO
*        THE JFCB OF THE PDS TO BE USED AS ASSEMBLER INPUT.      81167
*        THIS FUNCTION COULD BE PERFORMED BY MODIFYING THE JFCB  81167
*        DIRECTLY ON THE JOBQUEUE (SEE NOTES FOR &SVCJFCB)       81167
*
         PUSH  USING                                             84261
         DROP  ,             DROP BASE AND WORK REGISTERS        84261
         STM   R14,R12,12(R13)                                  GP02251
         LR    R12,R15       MAKE LOCAL BASE                     81169
         USING DOOPEN,R12    USE NEW BASE                        81169
         LR    R8,R1         COPY WORK AREA ADDRESS              81169
         USING SAVE,R8       DECLARE LOCAL WORK AREA             81169
         LA    R5,IOSAVE     GET LOWER LEVEL SAVE AREA           90205
         ST    R13,4(,R5)    MAKE RETURN POINTER                 90205
         LR    R13,R5        USE IT FOR @DCBEXIT                 90205
         LM    R3,R4,JFCBELNM  PRESERVE THE MEMBER/GDG NAME      88329
         TM    JFCBIND1,JFCPDS+JFCGDG  PDS OR GDG ?              88329
         BNZ   *+10          YES (WOULD YOU BELIEVE *+8)         90206
         XC    JFCBELNM,JFCBELNM  CLEAR OVER OPEN                88329
         OPEN  (,INPUT),TYPE=J,MF=(E,OPLASMIN)                   81167
         CLOSE (,REREAD),MF=(E,OPLASMIN)  CLOSE FILE AGAIN       81167
         L     R13,4(,R13)   GET OS SAVE AREA BACK               90205
         STM   R3,R4,JFCBELNM                                    88329
         TM    PDSDCB+DCBBUFCB+3-IHADCB,1   BUFFER CONTROL BLOCK ?
         BO    POLLUTED      NO, SKIP FREE POOL
         FREEPOOL PDSDCB
POLLUTED LM    R14,R12,12(R13)   RESTORE REGISTERS               81169
         SLR   R15,R15
         BR    R14
         POP   USING         RESTORE BASE REGISTERS              84261
.NOJFCAT SPACE 1                                                 81167
*        THIS CLEANS UP SYSPRINT DCB ATTR. AFTER ASSEMBLER ERROR
ASCLEAN  MVC   COMPC,=C'U20'  SET FINAGLED CODE                  81167
         B     BADMEMCM
         SPACE 1
*        PROCESS A SUB-TASK ABEND HERE
*
ABEND    UNPK  WORK(5),ACB+1(3)  GET ABEND CODE                  81167
         L     R1,=A(TRHEX-C'0')                                GP99053
         TR    WORK(3),0(R1)                                    GP99053
         MVC   COMPC,WORK    COPY ABEND CODE                     81167
BADMEMCM PRTLIST BENDLIST,DEV=(2,3)  PRINT ABEND MESSAGE         81167
         MVC   LISTVART,LISTCOMP  SET FOR COMPLETION CODE LINE   81167
         TM    FLAG2,FGNOSEP+FG2NOHED  SKIP SEPARATORS ?        GP04065
         BNZ   SKPBADHD      YES; AVOID POSSIBLE B14             81167
         TM    FLAG4,FGSVCPR  SKIP SEPARATORS ?                 GP04065
         BNZ   SKPBADHD      YES; AVOID POSSIBLE B14             81167
         PRTOPEN SYSPRINT,OPT=(DUMMY,NOWTO)  OPEN ASM PRINT FILE 81167
         PRTBIG LIST=LISTFLAG DEV=1   MAKE TWO SEPARATOR PAGES   81167
SKPBADHD NI    LISTFLAG,LF9700+LFXRXAUX   RESET FLAG BYTE        90101
         MVC   LISTVART,LISTDATE  SET BACK FOR DATE LINE         81167
         CLC   =C'13',COMPC+1   BAD SOMETHING ?                  81167
         BNE   ENDFILE       TERMINATE
         MVC   8(52,R13),JFCBDSNM  SAVE DSNAME, MEMBER           81167
         RDJFCB MF=(E,OPLASMIN)   RE-READ JFCB                   81167
         MVC   JFCBDSNM(52),8(R13)   RESTORE DSN AND MEMBER      81167
         MVC   PDSDCB(PDSPATLN),PDSPAT  COPY CLEAN DCB           81169
         MVC   PDSDCB+DCBDDNAM-IHADCB(8),DDNPDS  SET CORRECT DDN 81169
         LA    R1,PDSEXLST                                       81169
         STCM  R1,7,DCBEXLST+1-IHADCB+PDSDCB                     81169
         LA    R9,4095       SET MAXIMUM ERROR CODE              81320
         B     POSTNAME      KEEP PROCESSING                     84261
         SPACE 1                                                 81167
*        CHECK SYSPRINT/SYSPUNCH FOR PDS SPECIFICATION.          81169
*        CALLER MUST HAVE OI AT RETURN ADDRESS                   81169
*                                                                81169
TESTLMOD O     R8,=X'80000000'  SET LMOD FLAG                    87291
         B     TESTPDSC                                          87291
TESTPDS  SLL   R8,1                                              87291
         SRL   R8,1          KILL HIGH BIT                       87291
TESTPDSC DEVTYPE (R3),DEVT   SEE IF DISK                         81167
         BXH   R15,R15,4(R8)   SHOULD NOT HAVE HAPPENED          81167
         CLI   DEVT+2,X'20' (UCB3DACC)  DIRECT ACCESS ?          81167
         BNE   4(,R8)        NO; RETURN                          81167
         SERVCALL RJFCB,(R3)  READ THE JFCB                      84275
         BXH   R15,R15,4(R8)   RETURN IF ERROR                   81167
         TM    JFCBIND1-INFMJFCB(R1),JFCPDS  MEMBER ON DD ?      84275
         BZ    TESTPDSD      NO; GET DSCB                        81167
         CLI   1(R8),FGSVCPR  DOING PRINTER ?                    81167
         BNE   4(,R8)        NO; JUST RETURN                     81167
         OI    FLAG2,FGNOSEP  NO SEP; WILL B14 ON >1 MEMBER ANYWAY
         B     4(,R8)        RETURN NOW                          81167
TESTPDSD MVC   READBFR(JFCBLGTH),0(R1)  COPY JFCB                84275
         SERVCALL DSDJ1,READBFR  READ THE DSCB1                  84275
         BXH   R15,R15,4(R8)   SKIP IF FAILED                    81167
         PUSH  USING                                            GP97206
         DROP  R13                                              GP97206
         USING DS1FMTID,R1   DECLARE IT                          81167
         CLI   DS1DSORG+1,0   ANY FUNNY DSORG ?                  81167
         BNE   4(,R8)        NO; FAIL                            81167
         TM    DS1DSORG,255-JFCORGPO-JFCORGU  NON-PDS ?          81167
         BNZ   4(,R8)        YES; FAIL                           81167
         TM    DS1DSORG,JFCORGPO   PDS ?                         81167
         BNO   4(,R8)        NO; RETURN APPROPRIATELY            87291
         LTR   R8,R8         PDS OR LMOD ENTRY ?                 87291
         BNMR  R8            PDS; RETURN WITH PDS OFFSET         87291
         TM    DS1RECFM,DCBRECU  IS THE RECFM=U ?                87291
         BNMR  R8            YES; PROCEED (OR NEVER SET)        GP99055
         POP   USING                                            GP97206
         PRTLIST NOTLMODD,DEV=(2,3)  WRITE AN ERROR MESSAGE      87291
         B     4(,R8)        TAKE ERROR RETURN                   87291
NOTLMODD FDPRT '** DD',NL,PADR                                   87291
         FDPRT 0(R3),8,DEB,PAD                                   87291
         FDPRT 'IS NOT A LOAD-MODULE PDS **',PAD                 87291
         FDPRT *END                                              87291
         SPACE 2                                                 84275
LINKINIT ST    R8,DB         SAVE RETURN ADDRESS                 84275
         ST    R3,DB+4       PRESERVE CARD POINTER               84275
         TM    ALLOFLAG,ALFHAVE  HAVE A SYSLMOD DD ALLOCATED ?   87081
         BZ    LINKINDA      NO                                  87081
         L     R15,=A(DYNALC)  GET DYNAMIC ALLOCATION            87081
         BASR  R14,R15       AND RELEASE CURRENT SYSLMOD         87081
LINKINDA LA    R14,LKEDOPTL  POINT TO OPTION LIST                84275
         LA    R15,LKEDDDNL    AND DDNAME LIST                   84275
         C     R14,LKEDPARM  ALREADY INITIALIZED ?               84275
         BE    LINKLMOD      YES; BUT CHECK FOR DD OVERRIDES     85083
         STM   R14,R15,LKEDPARM  MAKE ADDRESSES                  84275
         OI    LKEDPARM+4,X'80'  SET END OF LIST                 84275
         LA    R0,LKEDOPTX-LKEDOPT  LENGTH OF OPTION LIST        84275
         STH   R0,LKEDOPTL                                       84275
         LA    R0,LKEDDDNX-LKEDDDN  LENGTH OF DDNAME LIST        84275
         STH   R0,LKEDDDNL                                       84275
         MVC   LKEDDMOD(DDLKPATL),DDLKPAT  SET DEFAULT DDNS      84275
         MVC   LKEDDUT1,DDNUT1   UTILITY                         84275
         LA    R3,DDNUT2     LOOK AT EARLIEST ENTRY              88111
         LA    R0,(DDNTERM+8-DDNUT2)/8  NUMBER OF ENTRIES        88111
LINKINDL CLC   =C'TERM',3(R3)  FOUND AN XXXTERM ENTRY ?          88111
         BE    LINKINDH      YES; USE IT                         88111
         LA    R3,8(,R3)     TRY AGAIN                           88111
         BCT   R0,LINKINDL                                       88111
         LA    R3,=CL8'SYSTERM'  SET FOR DEFAULT                 88330
LINKINDH MVC   LKEDDTRM,0(R3)  ASMTERM/COBTERM/FORTERM/SYSTERM   88330
         LA    R3,DDNLIN     POINT TO SYSLIN                     88111
         CLI   0(R3),0       ANY ?                               88111
         BH    LINKINDG      YES; USE IT                         88111
         LA    R3,SYSGO      POINT TO SYSGO                      88111
LINKINDG MVC   LKEDDDN,0(R3)  ASM SYSGO IS LKED SYSLIN           88111
         CLI   0(R3),0       IS THERE ONE ?                      88111
         BE    UNUSDD        NO; ISSUE ERROR MESSAGE             84275
         LA    R3,LKEDDPRT   POINT TO PRINT FILE                 84275
         DEVTYPE 0(R3),DEVT  IS IT AVAILABLE ?                   84275
         BXLE  R15,R15,LINKINPR  YES; USE IT                     84275
         TM    FLAG4,FGSVCPR  IS ASSEMBLER PRINT FILE A PDS ?    84275
         BNZ   UNUSDD        SHOW LNKPRINT NO GOOD               84275
         MVC   LKEDDPRT,DDNPRINT  USE ASSEMBLER'S PRINT FILE     84275
LINKLMOD TM    ALLOFLAG,ALFREQ+ALFNEED  USE DYNAMIC ALLOCATION ? 87081
         BO    LINKINTD      YES; LKED SYSLIB REQUIRED           87081
         LA    R3,USERLMOD   POINT TO USER SYSLMOD OVERRIDE      85083
         CLI   0(R3),C' '    WAS THERE ONE ?                     85083
         BH    LINKINLT      YES; TEST IT                        85083
LINKINPR LA    R3,LKEDDMOD   GET SYSLMOD                         84275
LINKINLT BAS   R8,TESTLMOD   HAVE WE A LOAD-MODULE PDS ?         87291
         B     LINKINTD      YES; TEST SYSLIB                    84275
         CLC   =C'SYS',0(R3)  SECOND TIME AROUND ?               84275
         BE    LINKFAIL      YES; TOO BAD                        84275
         LA    R15,USERLMOD  SET USER OVERRIDE ADDRESS           85083
         CR    R15,R3        USER OVERRIDE REQUEST ?             85083
         BE    LINKINPR      YES; USE NORMAL FIRST TRY           85083
         MVC   0(3,R3),=C'SYS'  CHANGE TO SYSLMOD                84275
         B     LINKINLT      TRY AGAIN                           84275
LINKINTD LA    R3,LKEDDLIB   POINT TO SYSLIB DDNAME              84275
         CLI   USERLLIB,C' '  USER OVERRIDE ?                    85083
         BNH   LINKINLD      NO                                  85083
         LA    R3,USERLLIB   TRY IT                              85083
LINKINLD BAS   R8,TESTPDS    IS IT A PDS ?                       84275
         B     LINKINDN      YES; ALL SET ?                      84275
         CLC   =C'SYS',0(R3)  SECOND TIME AROUND ?               84275
         BE    LINKINDF      YES; USE SYSLMOD INSTEAD            84275
         MVC   0(3,R3),=C'SYS'  SET FOR SYSLIB                   84275
         CLC   DDNLIB,0(R3)  IS ASSEMBLER USING IT ?             84275
         BNE   LINKINLD      NO; TRY IT                          84275
LINKINDF TM    ALLOFLAG,ALFREQ+ALFNEED  DYNAMIC ALLOCATION ?     87081
         BO    UNUSDD        YES; REQUIRED DD MISSING            87081
         MVC   LKEDDLIB,LKEDDMOD  USE SYSLMOD INSTEAD            84275
LINKINDN MVI   LKEDOPT-1,C' '                                    85083
         MVC   LKEDOPT,LKEDOPT-1   CLEAR OPTION FIELD            85083
         MVC   LKEDOPTL(DFLLPARX-DFLLPARM),DFLLPARM  PARM.LKED   87074
         L     R8,DB         RESTORE RETURN                      84275
         L     R3,DB+4       PRESERVE CARD POINTER               84275
         BR    R8            RETURN                              84275
         SPACE 1                                                 84275
LINKFAIL PRTLIST NONEDDL,DEV=(2,3)                               84275
         LA    R9,20                                             84275
         B     CONDSTOP                                          84275
         EJECT ,
SYSIN    INPWORK SYSIN,WIDTH=80,FILL=C' ',EDIT=128   (EODAD=ENDFILE)
BLANK8   EQU   SYSIN+8,8,C'C'  USE EMPTY SYSIN ALIAS NAME        84261
*        BEGINNING OF BLOCK MOVE AREA                            81169
PDSPAT   DCB   DDNAME=ASMIN,DSORG=PS,EROPT=ACC,EXLST=1,MACRF=GL  81169
PATSYPRT PRTWORK ASMPRINT,RECFM=FBA,WIDTH=121   PAGE SEPARATOR FILE 1
PATSYSGO PUNWORK ASMGO,WIDTH=80,RECFM=FB  DEFINE SYSGO FILE 4    81167
*        END OF BLOCK MOVE AREA                                  81169
PATLPARM DC    Y(PATLPARX-*-2)                                   84275
         DC    C'TERM,LET,LIST,MAP,SIZE=(400K,100K)'             84275
PATLPARX EQU   *                                                 84275
         SPACE 1                                                 81169
MSGPRINT PRTWORK MSGPRINT,CONPRINT,RECFM=VB   MESSAGE FILE 2     81167
CONSOLE  PRTWORK *USER       TPUT OR WTO, PRINT FILE 3           84275
         SPACE 1                                                 81167
         AIF   (&MVSESA).DEFHL                                  GP05013
         AIF   (&MVSXA).DEFHA                                   GP05013
ASMPGM   DC    Y(ASMANYF-ASMANY,DDNASM-ASMANY),CL8'&ASMF',C'A' XF
         AGO   .DEFCOM                                          GP05013
.DEFHL   ANOP  ,                                                GP05013
ASMPGM   DC    Y(ASMANY-ASMANY,DDNASML-ASMANY),CL8'&ASML',C'A' HL
         AGO   .DEFCOM                                          GP05013
.DEFHA   ANOP  ,                                                GP05013
ASMPGM   DC    Y(ASMANYH-ASMANY,DDNASMH-ASMANY),CL8'&ASMH',C'A' H
.DEFCOM  ANOP  ,                                                GP05013
ASMPGMHL DC    Y(ASMANYHL-ASMANY,DDNASML-ASMANY),CL8'&ASML',C'A' HL
ASMPGML  DC    Y(ASMANYL-ASMANY,DDNASML-ASMANY),CL8'&ASML',C'A' HL
ASMPGMG  DC    Y(ASMANYG-ASMANY,DDNASM-ASMANY),CL8'&ASMG',C'A' G 88329
ASMPGMH  DC    Y(ASMANYH-ASMANY,DDNASMH-ASMANY),CL8'&ASMH',C'A' H
ASMPGMF  DC    Y(ASMANYF-ASMANY,DDNASM-ASMANY),CL8'&ASMF',C'A' XF 88329
ASMPGMX  DC    Y(ASMANYF-ASMANY,DDNASM-ASMANY),CL8'&ASMF',C'A' XF 88329
ASMPGMFX DC    Y(ASMANYF-ASMANY,DDNASM-ASMANY),CL8'&ASMF',C'A' XF 88329
ASMPGMT  DC    Y(ASMANYT-ASMANY,DDNAST-ASMANY),CL8'&ASMT',C'A' TACHYON
COBPGM   DC    Y(COBMANY-ASMANY,DDNCOB-ASMANY),CL8'IKFCBL00',C'C' COB
FORPGM   DC    Y(FORMANY-ASMANY,DDNFOR-ASMANY),CL8'IEYFORT',C'F' FORT
ENDPGM   DS    0H            NOTE ALIGNMENT                     GP05336
ASMPGMLN EQU   *-FORPGM      LENGTH OF ONE ENTRY                GP05336
LINKEP   DC    CL8'IEWL'     LARGEST LINKAGE EDITOR ?            84275
         SPACE 1                                                 81167
LISTDATE DC    C'##/##/##'   INSERTION PATTERN - DATE            81167
         SPACE 1
DDLKPAT  DC    CL8'LNKLMOD',CL8'LNKLIB'  SUBSET OF LKED DDS      84275
         DC    XL8'0',CL8'LNKPRINT'                              84275
DDLKPATL EQU   *-DDLKPAT                                         84275
         SPACE 1                                                 81169
PATWTOR  WTOR  'REPLY STOP TO TERMINATE CURRENT ASSEMBLY',MF=L   81169
PATWTOLN EQU   *-PATWTOR                                         81169
DCSTOP   EQU   PATWTOR+8+6,4,C'C'   'STOP' CONSTANT              84275
ATTAPAT  ATTACH SZERO=NO,DPMOD=+4,SF=L                           81292
         SPACE 1                                                 81167
CARDLIST FDCLI CARD+1,C'E',BE=CARDLIS2  PRINT CARD IF 'E' IN COL 2
         FDCLI CARD,X'02',BE=CARDLISX  ELSE SKIP OBJECT CARD     81169
CARDLIS2 FDPRT '''',RADJ,NL,LEN=11                               81167
         FDPRT CARD,80      PRINT CARD IMAGE                     81167
         FDPRT ''''                                              81167
CARDLISX FDPRT *END                                              81167
         SPACE 1                                                 84275
LINKMSG  FDPRT '---LINKING',NL                                   84275
         FDPRT JFCBELNM,DEB,PAD                                  84275
         FDPRT 'INTO',PAD                                       GP97241
         FDPRT JFCBDSNM-INFMJFCB(R8),L'JFCBDSNM                 GP97241
         FDGOTO FDNL                                             84275
         SPACE 1                                                 84275
LINKAMSG FDPRT 'LINK-EDIT ABEND S',NL                            84275
         FDPRT WORK+6,2,HEX,LEN=3                                84337
         FDGOTO FDNL                                             84275
         SPACE 1                                                 84275
LINKEMSG FDPRT 'LINK-EDIT COMPLETION CODE',NL,PADR               84275
         FDPRT ACB,I                                             84275
         FDGOTO FDNL                                             84275
         SPACE 1                                                 81167
MSGASMBL FDCLI ASMPGTYP,C'A',BE=MSGASMBA                         88329
         FDPRT ' COMPILING',NL,PADR                              88111
         FDGOTO MSGASMBC                                         88111
MSGASMBA FDPRT 'ASSEMBLING',NL,PADR                              81167
MSGASMBC FDPRT JFCBELNM                                          81167
         FDGOTO FDNL         FORCE NEW LINE FOR CONSOLE          81169
         SPACE 1                                                 81167
MSGASMER FDPRT 'LEVEL',NL,PADR                                   81167
         FDPRT ACB,I,DEB                                         81169
         FDPRT 'ERRORS IN',PAD                                   81167
         FDPRT JFCBELNM                                          81167
         FDGOTO FDNL         NEW LINE FOR CONSOLE WTO            81169
         SPACE 1                                                 81167
BENDLIST FDPRT 'SYSTEM COMPLETION CODE',NL,PADR                  81167
         FDPRT COMPC,PAD                                         81167
         FDPRT 'IN MEMBER',PAD                                   81167
         FDPRT JFCBELNM,DEBR                                     81167
         FDCLI JFCBVOLS,C' ',BE=BENDNVOL,BL=BENDNVOL             81169
         FDPRT ', VOLUME',PADR                                   81167
         FDPRT JFCBVOLS,6,DEBR                                   81167
BENDNVOL FDPRT ', DSN',PADR                                      81167
         FDPRT JFCBDSNM,DEBR                                     81167
         FDGOTO FDNL         FORCE NEW LINE FOR WTO              81169
         SPACE 1                                                 81167
TWODDN   FDPRT 'DUPLICATE DDNAME SUPPLIED FOR',NL,PADR           81167
         FDPRT 0(R3),8,PAD,DEBR                                  81167
         FDPRT 'AND',PAD                                         81167
         FDPRT JFCB,8,PADL,DEBR                                  81167
         FDPRT ';',PADR                                          81167
         FDPRT JFCB,8,PAD,DEBR                                   81167
         FDPRT 'WILL BE IGNORED'                                 81167
         FDGOTO FDNL                                             81169
         SPACE 1                                                 81167
NONEDDL  FDPRT 'NEITHER LNK',NL                                  84275
         FDGOTO NONEDDNX                                         84275
NONEDDN  FDPRT 'NEITHER ASM',NL                                  81167
NONEDDNX FDPRT 3(R3),5,PADR,DEBR                                 84275
         FDPRT 'NOR',PAD                                         81167
         FDPRT 0(R3),8,PAD,DEBR                                  81167
         FDPRT 'DD CARD SUPPLIED'                                81167
         FDGOTO FDNL                                             81169
         SPACE 1                                                 81167
AUTOCARD FDTM  FLAGS,FGAUTOPG,BZ=AUTONPAG  SKIP IF NOT PAGE CARD 81169
         FDTM  LKFLAG,LKFGINS,BO=AUTONPAG  GEN'ED CONTROL CARDS  84275
         FDPRT '  PAGE',NL,PADR                                  81169
         FDPRT JFCBELNM      MEMBER NAME                         81169
AUTONPAG FDCLI SSITEXT,C' ',BE=AUTONSSI    SKIP IF NO SSI        81169
         FDTM  LKFLAG,LKFGSSI,BO=AUTONSSI                        84275
         FDPRT '  SETSSI',NL,PADR                                81172
         FDPRT SSITEXT       SSI VALUE                           81169
AUTONSSI FDTM  FLAGS,FGAUTONM,BZ=AUTONONE                        81169
         FDPRT ' NAME',NL,PADR                                   81169
         FDPRT WORK,DEBR     (MODIFIED) MEMBER NAME              81169
         FDPRT '(R)'         REPLACE OPTION                      81169
AUTONONE FDPRT *END                                              81169
         SPACE 1                                                 81169
NODDFD   FDPRT 'NO DD CARD FOR',NL,PADR                          81169
         FDPRT WORK          DDNAME                              81169
FDNL     FDOPT NL                                                81169
         FDPRT *END                                              81169
         SPACE 1                                                 84275
UNUSDDFD FDPRT 'DD CARD',NL                                      84275
         FDPRT 0(R3),8,DEB,PAD                                   84275
         FDPRT 'IS NOT USABLE'                                   84275
         FDGOTO FDNL                                             84275
         SPACE 1                                                 81169
*        INPUT VERB TABLE                                        81169
OPTTAB VERBTAB ' ',PUTSYSGO,LEN=2,BASE=*    LKED CONTROL CARD    81169
       VERBTAB '+',PROPOST             POST-ASM CONTROL CARD     81169
       VERBTAB *02,PUTSYSGO            OBJECT DECK CARD          81169
*                                                                81169
OPTTABDS VERBTAB 'DSN=',DODSN          NEW DDN OR DSN REQUIRED   81169
OPTTABDD VERBTAB 'DDN=',SETNEWDD       TABLE FOR NEW DDN         81169
       VERBTAB 'DDMOD=',SETLMOD        SET NEW SYSLMOD NAME      85083
       VERBTAB 'DDLMOD=',SETLLMOD      SET NEW SYSLMOD NAME      87081
       VERBTAB 'DDLIB=',SETLLIB        SET (LKED) SYSLIB DDN     85083
       VERBTAB '%',SETOPT              OPTION CARD               81169
       VERBTAB '*',SETWTOR             TURN WTOR ON/OFF          81169
       VERBTAB 'SSI=',SETSSI           SET/RESET SSI             81169
       VERBTAB 'SEP=',PROSEP           SET/RESET SEPARATOR OPTION
       VERBTAB 'CODE=',SETCODES        SET STOP CONDITION CODE   81169
       VERBTAB 'COND=',SETCODES          DITTO                   81169
       VERBTAB 'LKEDCODE=',SETCLINK    LKED CALL CODE            84275
       VERBTAB 'LKEDCOND=',SETCLINK                              84275
       VERBTAB 'COMPCODE=',SETCOND     COMPILER                 GP04064
       VERBTAB 'COMPCOND=',SETCOND                              GP04064
       VERBTAB 'ASMCODE=',SETCOND      COMPILER                 GP04064
       VERBTAB 'ASMCOND=',SETCOND                               GP04064
       VERBTAB 'ID=',SETID3            SET THIRD SEPARATOR LINE  81169
NULLTAB VERBTAB *END                                             81169
         SPACE 1                                                 81169
MODTABAL VERBTAB LINK,SETLINK          LKED MEMBER ONLY (ALIAS) 84275
       VERBTAB RELINK,SETRLINK         RELINK ENTIRE MEMBER      84275
       VERBTAB NEWLINK,SETNLINK   LINK POSSIBLY NEW MEMBER       87074
       VERBTAB LLA,SETYLLA   DO LLACOPY AFTER GOOD LINK-EDIT    GP97289
       VERBTAB REFRESH,SETYLLA   LONG FORM                      GP97289
         SPACE 1                                                 84275
MODTAB VERBTAB 'ON',SETAUNM            AUTO NAME CARD            81169
DCOFF  VERBTAB 'OFF',NAMPR0            RESET AUTO NAME           81169
       VERBTAB 'XEROX',X9700ON         SET XEROX (4050,...)      90101
       VERBTAB '9700',X9700ON          SET XEROX 9700 MODE       81169
       VERBTAB DUPLEX,X9700ON          SET DUPLEX PRINT MODE     89281
DCNO   VERBTAB NOSVC,SETNOSVC          NO IGC NAME CHECK/FIXUP   81169
       VERBTAB NOSEP,PROSETN           NO SEPARATOR              81169
       VERBTAB SEP,PROSETY             DO SEPARATOR             GP09274
       VERBTAB NOPAGE,SETPGNO          NO PAGE CARD WITH NAME    81169
       VERBTAB PAGE,SETPGON            SET PAGE AND NAME CARDS   81169
       VERBTAB NOLLA,SETNLLA    TURN LLACOPY OFF                GP97289
       VERBTAB NOREFRESH,SETNLLA  DITTO                         GP97289
       VERBTAB LKEDDFLT,SETLKDFL  SET LINK-EDIT DEFAULTS         87074
       VERBTAB LKEDON,SETLKOR     OPTIONS TO OR TO EXISTING      87074
       VERBTAB LKEDOR,SETLKOR        MODULES ON LINK/RELINK      87074
       VERBTAB LKEDAND,SETLKAND   OPTIONS TO AND OUT ON EXISTING 87074
       VERBTAB LKEDNOT,SETLKAND      MODULES ON LINK/RELINK      87074
       VERBTAB LKEDOFF,SETLKAND                                  87074
       VERBTAB LKEDPARM,SETLKPRM  SET DEFAULT COMMON PARM        87074
       VERBTAB LKEDPRM,SETLKPRM   SET DEFAULT COMMON PARM       GP99148
       VERBTAB INCLUDE,SETINCL    GENERATE 'INCLUDE SYSLMOD'     89282
       VERBTAB INC,SETINCL                                       89282
       VERBTAB NOINCLUDE,SETNINCL  INHIBIT 'INCLUDE' CARDS       89282
       VERBTAB NOINC,SETNINCL                                    89282
       VERBTAB *END                                              81169
         SPACE 1                                                 90101
XERTAB VERBTAB 'AUX',XERTRAY           XEROX AUXILIARY TRAY      90101
       VERBTAB 'FEED',XERTRAY                                    90101
       VERBTAB 'TRAY',XERTRAY                                    90101
       VERBTAB *END                                              90101
         AIF   (&SVCJFCB NE 0).NODCM                            GP99055
DCMJFCB  DC    CL8'SVCMJFCB'  INVOKE MOD JFCB SERVICE           GP99055
.NODCM   SPACE 1                                                GP99055
         LTORG ,
         SPACE 2                                                 84261
*---------------------------------------------------------------------*
*        TRT TABLE FOR NAME SCANNING                             84261
*---------------------------------------------------------------------*
NAMETRT  DC    256AL1(255)                                       84261
         ORG   NAMETRT+C' '                                      84261
         DC    AL1(1)                                            84261
         ORG   NAMETRT+C'$'                                      84261
         DC    AL1(0)                                            84261
         ORG   NAMETRT+C'#'                                      84261
         DC    AL1(0)                                            84261
         ORG   NAMETRT+C'@'                                      84261
         DC    AL1(0)                                            84261
         ORG   NAMETRT+C'A'                                      84261
         DC    9AL1(0)       A-I                                 84261
         ORG   NAMETRT+C'J'                                      84261
         DC    9AL1(0)       J-R                                 84261
         ORG   NAMETRT+C'S'                                      84261
         DC    8AL1(0)       S-Z                                 84261
         ORG   NAMETRT+C'0'                                      84261
         DC    10AL1(0)      0-9                                 84261
         ORG   NAMETRT+C','                                      84261
         DC    AL1(2)                                            84261
         ORG   NAMETRT+C';'                                      84261
         DC    AL1(3)                                            84261
         ORG   NAMETRT+C'*'                                      84261
         DC    AL1(4)                                            84261
         ORG   NAMETRT+C'-'                                      84261
         DC    AL1(5)                                            84261
         ORG   NAMETRT+C'+'                                      84261
         DC    AL1(5)        CONTINUATION                        84261
         ORG   NAMETRT+C':'                                      84261
         DC    AL1(6)        END OF LINK-EDIT NAME               84261
         ORG   ,                                                 84261
         TITLE 'A S M A N Y  ---  ASSEMBLE MANY DECKS  ---  LOAD MAPS'
*---------------------------------------------------------------------*
*        THIS SEGMENT WAS ADAPTED FROM MODULE OVLYTREE DISTRIBUTED BY
*        SOME SHARE, INC. MEMBER INSTALLATIONS.  AUTHOR UNKNOWN. 84275
*                                                                84275
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*    THE OVERLAY TREE PROCESSOR READS IN THE CESD AND SEGTAB FOR A    *
*     LOAD MODULE IN OVERLAY STRUCTURE, AND PRODUCES A GRAPHIC        *
*     REPRESENTATION OF THE OVERLAY STRUCTURE.  OPTIONALLY, IT WILL   *
*     PUNCH THE OVERLAY AND INSERT CARDS REQUIRED TO RECREATE THE     *
*     SAME OVERLAY STRUCTURE.                                         *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*     E.S.P. CHANGES - FOR MODULES NOT IN OVERLAY, THIS PROGRAM
*     PUNCHES ORDER CARDS. THE PDS CONTAINING THE MEMBER IS SEARCHED
*     FOR OTHER MEMBERS WITH THE SAME TTR, AND THE APPROPRIATE ALIAS
*     CARDS ARE GENERATED.                                       84275
*---------------------------------------------------------------------*
         SPACE 2                                                 84275
         PRINT &PRTSOR                                           84275
LOADMAPS LA    R15,16        SET PROVISIONAL RETURN CODE         84275
         STM   R0,R15,SAVELOAD  SAVE REGISTERS                   84275
         BASR  R10,0         MAKE A BASE                         85083
LOADMAPB LA    R12,2048      MAKE AN INCREMENT                   85083
         LA    R11,2048(R10,R12)  MAKE SECOND BASE               85083
         LA    R12,2048(R11,R12)  MAKE THIRD BASE                85083
         USING LOADMAPB,R10,R11,R12  DECLARE THEM                85083
         SPACE 1                                                 84275
         LM    R2,R3,PDS2NAME  SAVE REFERENCE NAME               89281
         CLRL  SUBSAVE,MAPCLEAR,OPLEN=LA  RE-INITIALIZE          84275
         STM   R2,R3,PDS2NAME  RESTORE REFERENCE NAME            89281
         MVC   WORK,LKEDDLIB FIRST TRY SYSLIB(LNKLIB)            84275
         TM    LKFLAG,LKFGLMOD  SYSLMOD ONLY ?                   84275
         BZ    LOADMAPI      NO; START WITH SYSLIB               84275
LOADMAP2 CLC   WORK,LKEDDMOD DID WE JUST TRY SYSLMOD ?           84275
         BE    LOADMAP3      QUIT WITH CC=16                     84275
         CLC   LKEDDMOD,LKEDDLIB  SAME DD CARD ?                 84275
         BE    LOADMAP3      YES; DON'T WASTE TIME               84275
         MVC   WORK,LKEDDMOD TRY SYSLMOD TOO                     84275
LOADMAPI MVC   MODDCB(MODELLEN),MODELDCB  RE-INIT DCB            84275
         MVC   DCBDDNAM-IHADCB+MODDCB(L'DCBDDNAM),WORK  SET DDNAME
         LA    R15,BPAMERR                                       84275
         STCM  R15,7,1+DCBSYNAD-IHADCB+MODDCB                    84337
         STCM  R15,7,1+DCBEODAD-IHADCB+MODDCB                    84337
         LA    R15,MODDCB                                        84275
         ST    R15,OPLMOD                                        84275
         MVI   OPLMOD,X'90'  MAKE INPUT, REREAD                  84275
         MVC   BPAMDECB(PATDECBL),PATDECB                        84275
         XC    READBFR,READBFR     CLEAR READ BUFFER             84275
         MVI   PDORECFM,DCBRECU  SET RECFM=U                     84275
         MVI   MESSBLNK,C' '                                     84275
         BAS   R14,MESSCLR   BLANK OUT LINE                      84275
NOPNPUN  MVC   BLDLLIST(4),PATBLDL    INITIALIZE BLDL LENGTHS    89281
         OPEN  MF=(E,OPLMOD)    LOOK FOR SYSLMOD                 84275
         TM    MODDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN ?           84275
         BNZ   TRYBLDL       YES; LOOK FOR THE MEMBER            84275
         PRTLIST NOOPDDFD,DEV=(2,3)                              84275
         B     LOADMAP2      TRY OTHER DD CARD                   84275
TRYBLDL  LA    R1,PDS2NAME   POINT TO NAME LOCATION              89081
         BAS   R14,TESTSVCN  CHECK FOR SVC NAME AND ADJUST       89081
         MVI   BLKSTART,C' '                                     84275
         MVC   BLKSTART+1(BLKLEN-1),BLKSTART  BLANK OPTIONAL FIELDS
         BLDL  MODDCB,BLDLLIST    LOOK FOR MEMBER                84275
         BXLE  R15,R15,TESTALLO  SUCCESSFUL; GET STORAGE         87081
BLDLERR  TM    ALLOFLAG,ALFREQ+ALFNEED DYNAMIC ALLOCATION ?      87081
         BNO   BLDLERRM      NO                                  87081
         TM    LKFLAG,LKFGNEW  NEWLINK ?                         87081
         BZ    BLDLERRM      NO                                  87081
         PRTLIST BADBLDL,DEV=(2,3)                               87081
         MVI   PDS2TTRP+2,0  ZERO R IN TTR AS FLAG               92285
         B     LOADMAP3      PUT IN IN FIRST LNKLIB LIBRARY      87081
BLDLERRM CLOSE MF=(E,OPLMOD)  CLOSE UNSUCCESSFUL LIBRARY         87074
         TM    ALLOFLAG,ALFREQ+ALFNEED  NEED IN SYSLIB ?         87081
         BO    BLDLERRA      YES; QUIT NOW                       87081
         TM    LKFLAG,LKFGNEW  ADDING A MEMBER ?                 87074
         BNZ   LOADMAP2      YES; TRY AGAIN, BUT SKIP MESSAGE    87074
BLDLERRA PRTLIST BADBLDL,DEV=(2,3)                               84275
         B     LOADMAP2      LOOK IN OTHER DD                    84275
LOADMAP3 TM    LKFLAG,LKFGNEW  ADDING A MEMBER ?                 87074
         BZ    IEWLDONE      NO; QUIT WITH CC=16                 87074
         NI    LKFLAG,LKFGLKED+LKFGLMOD+LKFGNEW                  87074
         OI    LKFLAG,LKFGPARM  MAKE PARM                        87074
         MVC   PDS2ATR+2(2),LOPTDFLT  SET DEFAULT ATTRIBUTES (2+ BLDL)
         XI    PDS2ATR+2+1,X'80'    FLIP THE DC BIT              87081
         PRTLIST NEWBLDL,DEV=2  WRITE WARNING TO MSG FILE        87074
         B     TESTALLO      FAKE IT                             87081
         SPACE 1                                                 81167
*        COMMON PUNCH ROUTINE WITH SHARED ADDRESSABILITY         85083
*                                                                85083
         PUSH  USING         SAVE DEFINITIONS                    85083
         DROP  ,                                                 85083
PUTSYSGO LR    R8,R10        SET RETURN ADDRESS                  81169
         USING SAVE,R13      DECLARE COMMON WORK AREA            85083
PUTLCARD SUBENT PUTSAVE      SAVE REGISTERS                      85083
         BASR  R9,0          ESTABLISH ONE LOCAL BASE            87075
         USING *,R9          MAKE TEMPORARY BASE                 87075
         LR    R6,R3         COPY CARD ADDRESS                   85083
         O     R6,=X'80000000'  SIGNAL 80-BYTE CARD IMAGE       GP02251
         B     PUTLCCOM      GO TO COMMON                        85083
         DROP  R9                                                87075
         SPACE 1                                                 81167
PUTLKED  SUBENT PUTSAVE      SAVE REGISTERS                      85083
PUTLCCOM BASR  R9,0          ESTABLISH LOCAL BASE                87075
         USING *,R9          MAKE TEMPORARY BASE                 87075
         TM    FLAGS,FGSYSGO   IS SYSGO FILE OPEN ?              85083
         BNZ   PUTLKED2      YES                                 81167
         PRTOPEN SYSGO,OPT=DUMMY,DEV=4   ELSE OPEN IT            81167
         OI    FLAGS,FGSYSGO   SHOW OPEN ATTEMPTED               81167
         NI    FLAGS,255-FGSVCGO  @PRINTER SETS DISP=MOD         84275
PUTLKED2 LTR   R6,R6         CARD IMAGE REQUEST ?               GP02251
         BNM   PUTLKEDV      NO; USE V-FORMAT                   GP02251
         PRTLIST MSGCARDF,DEV=4  'PUNCH' THE CARD               GP02251
         B     PUTLKEDX      RETURN TO CALLER                    85083
MSGCARDF FDPRT 0(R6),80,NL  'PUNCH' THE CONTROL CARD            GP02251
         FDPRT *END                                             GP02251
PUTLKEDV PRTLIST 0(R6),DEV=4  'PUNCH' THE CONTROL CARD          GP02251
PUTLKEDX SUBEX PUTSAVE,RET=R8  RETURN TO CALLER                  85083
         SPACE 1                                                 85083
         LTORG ,                                                 85083
         POP   USING                                             85083
         SPACE 1                                                 89081
         PUSH  USING                                             89081
         DROP  ,                                                 89081
         USING SAVE,R13                                          89081
TESTSVCN BASR  R15,0                                             89081
         USING *,R15                                             89081
*    CHECK FOR SVC NAME AND CONVERT TO INTERNAL (LOAD-PDS) FORM
*                                                                89081
         TM    FLAGS,FGNOSVC   BYPASS SVC NAME CHECK ?           89081
         BNZR  R14           YES                                 89081
         CLI   7(R1),C'+'    OLD CONVENTION ?                    89281
         BE    TESTSVCO      YES                                 89281
         CLI   7(R1),C'0'    TRAILING ZERO IN NAME ?             89081
         BNER  R14           NO                                  89081
TESTSVCO CLC   =C'IGC',0(R1) POSSIBLE SVC NAME ?                 89081
         BNER  R14           NO                                  89081
         CLI   6(R1),C'0'    NUMERIC NEXT TO LAST BYTE ?         89081
         BLR   R14           NO                                  89081
         CLI   5(R1),C'0'    SVC NUMBER IN RANGE ?               89081
         BLR   R14           NO                                  89081
         CLI   5(R1),C'2'                                        89081
         BHR   R14           DITTO                               89081
         MVI   7(R1),X'C0'   MAKE CORRECT SVC NAME               89081
         BR    R14           RETURN TO CALLER                    89081
         POP   USING                                             89081
         SPACE 2                                                 89081
*        BUILD LKED PARM FROM DB(2), THEN AND AND OR OPTIONS.    89081
*                                                                89081
         PUSH  USING                                             89081
         DROP  ,                                                 89081
         USING SAVE,R13                                          89081
MAKLPARM BASR  R2,0          MAKE LOCAL BASE                     89081
         USING *,R2                                              89081
         TM    LKFLAG,LKFGPARM  NEED TO BUILD LKED PARM ?        89081
         BZR   R3            NO                                  89081
         NI    LKFLAG,255-LKFGPARM  RESET                        89081
         L     R1,=A(ATTRPARM)  POINT TO PARM ATTRIBUTE TABLE    89081
         LA    R0,(ATTRPARX-ATTRPARM)/(ATTRPAR2-ATTRPARM) NUMBER 89081
         LH    R15,DFLLPARM  LOAD DEFAULT LKED PARM LENGTH       89081
         NC    DB(2),LOPTAND  DELETE USER'S UNWANTED ATTRS       89081
         OC    DB(2),LOPTOR   AND OR IN DESIRED ONES             89081
MAKLPARL MVC   DB+2(2),DB    COPY ATTRIBUTES                     89081
         NC    DB+2(2),0(R1)  KILL BITS                          89081
         CLC   DB+2(2),0(R1)  MATCH ?                            89081
         BNE   MAKLPARF      NO; TRY NEXT                        89081
         NC    DB(2),2(R1)   KILL CONFLICTING OPTIONS            89081
         LA    R14,LKEDOPT(R15)  POINT TO NEXT AVAILABLE BYTE    89081
         LTR   R15,R15       FIRST PARM ?                        89081
         BZ    MAKLPAR1      YES                                 89081
         MVI   0(R14),C','   MOVE A SEPARATOR                    89081
         LA    R14,1(,R14)                                       89081
         LA    R15,1(,R15)   FIX LENGTH                          89081
MAKLPAR1 MVC   0(4,R14),4(R1)  MOVE OPTION                       89081
         LA    R14,4(,R14)   SET NEXT ADDRESS                    89081
         LA    R15,4(,R15)   SET NEW LENGTH                      89081
MAKLPAR0 BCTR  R14,0         BACKSPACE                           89081
         CLI   0(R14),C','   TRAILING COMMA ?                    89081
         BE    MAKLPARC      YES; SKIP IT                        89081
         CLI   0(R14),C' '   TRAILING BLANK ?                    89081
         BH    MAKLPARF      NO; QUIT                            89081
MAKLPARC BCT   R15,MAKLPAR0  FIX LENGTH AND TRY AGAIN            89081
MAKLPARF LA    R1,ATTRPAR2-ATTRPARM(,R1)  GO TO NEXT OPTION      89081
         BCT   R0,MAKLPARL                                       89081
         STH   R15,LKEDOPTL  SET UPDATED LENGTH                  89081
         BR    R3            RETURN TO CALLER                    89081
         LTORG ,                                                GP99053
         POP   USING         RESTORE                             89081
         SPACE 1                                                 84275
         USING ESDDATA,R4                                        84275
TESTALLO TM    ALLOFLAG,ALFREQ+ALFNEED NEED DYNAMIC ALLOCATION ? 87081
         BNO   GETMEAN       NO; GET WORKING STORAGE             87081
         LTCB  R2            GET THE CURRENT TCB                 87081
         L     R2,TCBTIO-TCB(,R2)  GET THE TIOT                  87081
         SR    R15,R15       CLEAR HIGH BYTE                    GP02264
         ICM   R15,7,OPLMOD+1  GET THE DCB ADDRESS BACK         GP02264
         TM    DCBOFLGS-IHADCB(R15),DCBOFOPN  DCB OPEN ?         87081
         BZ    TESTALLF      NO; FAIL                            87081
         SLR   R1,R1                                             87081
         ICM   R1,3,DCBTIOT-IHADCB(R15) GET TIOT OFFSET          93051
         AR    R2,R1         ADDRESS OF FIRST ENTRY              93051
         SLR   R1,R1                                             87081
         IC    R1,PDS2TTRP+3 UNDECLARED PDS2CNCT CONCATENATION # 87081
         SLR   R15,R15       FOR IC                              87081
TESTALLP SH    R1,=H'1'      DECREMENT CONCATENATION COUNTER     87081
         BM    TESTALLJ      DONE; GET JFCB                      87081
         USING TIOENTRY,R2   DECLARE THE TIOT ENTRY              87081
         ICM   R15,1,TIOELNGH  LENGTH OF THIS ENTRY              87081
         BZ    TESTALLF      HUH ?  PROGRAM ERROR                87081
         AR    R2,R15        BUMP                                87081
         B     TESTALLP      TRY IT                              87081
TESTALLJ SR    R1,R1
         ICM   R1,7,TIOEJFCB  GET THE JFCB                       92285
         BZ    TESTALLF      HUH ?                               87081
         SERVCALL SWARL,(R1) PASS JFCB TOKEN TO GET 24-BIT COPY  92285
         BXH   R15,R15,TESTALLF  FAILED ?                        92285
         CLC   =C'JFCB',12(R1)  HAVE A HEADER?                  GP02251
         BNE   *+8           NO                                 GP02251
         LA    R1,16(,R1)    SKIP QUEUE HEADER                  GP02251
         MVC   TVDSN,JFCBDSNM-INFMJFCB(R1)  MOVE DSN             92285
         MVC   READBFR(JFCBLGTH),0(R1) GET JFCB FOR DSCB1 CALL   92285
         ICM   R1,7,TIOEFSRT  GET THE UCB ADDRESS                87081
         BZ    TESTALLF      HUH ?                               87081
         SERVCALL SWAAB,(R1) UCB TOKEN FOR ADDRESS OR COPY      GP02251
         BXH   R15,R15,TESTALLF  FAILED ?                       GP02251
         USING UCBOB,R1                                         GP04065
TESTALLU INHEX TVUNT,UCBCHAN   MOVE DEVICE                      GP04065
         MVC   TVVOL,UCBVOLI   MOVE SERIAL                      GP04065
         DROP  R1                                               GP04065
         PRTLIST MSGDYNOK,DEV=2                                  87081
         L     R15,=A(DYNALC)                                    87081
         BASR  R14,R15       CALL THE ALLOCATION ROUTINE         87081
         MVC   LKEDDMOD,TVDDN  SET NEW DDNAME (ANYWAY)           87081
         CH    R15,=H'1'     TEST RETURN                         87081
         BL    TESTFMT1      PROCEED                             87291
         BE    IEWLDONE      ERROR 16; QUIT                      87081
TESTALLF PRTV  MSGDYNBD,CC=NO,DEV=(2,3)                         GP02251
         B     IEWLDONE      SET CC 16                           87081
MSGDYNBD VCON  ' ** DYNAMIC ALLOCATION FAILED'                  GP02251
         SPACE 1                                                 87291
TESTFMT1 SERVCALL DSDJ1,READBFR  READ THE DSCB1                  87291
         BXH   R15,R15,FAILFMT1   SKIP IF FAILED                 87291
         PUSH  USING                                            GP97206
         DROP  R13                                              GP97206
         USING DS1FMTID,R1   DECLARE IT                          87291
         CLI   DS1DSORG+1,0   ANY FUNNY DSORG ?                  87291
         BNE   FAILFMT1      NO; FAIL                            87291
         TM    DS1DSORG,255-JFCORGPO-JFCORGU  NON-PDS ?          87291
         BNZ   FAILFMT1      YES; FAIL                           87291
         TM    DS1DSORG,JFCORGPO   PDS ?                         87291
         BNO   FAILFMT1      NO; RETURN APPROPRIATELY            87291
         TM    DS1RECFM,DCBRECU  IS THE RECFM=U ?                87291
         BO    GETMEAN       JOIN COMMON CODE                    87291
         OC    DS1RECFM(6),DS1RECFM  INITIALIZED?               GP99055
         BNZ   GETMEAN       YES; USE IT(?)                     GP09274
         POP   USING                                            GP97206
FAILFMT1 PRTLIST NOTLMOD2,DEV=(2,3)  WRITE AN ERROR MESSAGE      87291
         B     IEWLDONE      TAKE ERROR RETURN                   87291
NOTLMOD2 FDPRT '** DD',NL,PADR                                   87291
         FDPRT TVDDN,DEB,PAD                                     87291
         FDPRT 'IS NOT A LOAD-MODULE PDS **',PAD                 87291
         FDPRT *END                                              87291
         SPACE 1                                                GP97206
MSGDYNOK FDCLI PDS2TTRP+2,0,BNE=MSGDYNFD                         92285
         FDPRT 'ADDING',RADJ,NL,LEN=10                           92285
         FDGOTO MSGDYNCM                                         92285
MSGDYNFD FDPRT 'FOUND',RADJ,NL,LEN=10                            87081
MSGDYNCM FDPRT PDS2NAME,DEB,PAD                                  87081
         FDPRT 'IN'                                              87081
         FDPRT TVDSN,PAD,DEB                                     87081
         FDPRT 'ON'                                              87081
         FDPRT TVVOL,PAD,DEB                                     87081
         FDPRT *END                                              87081
         DROP  R2                                                87081
GETMEAN  LM    R6,R7,MEMYWANT  GET MIN/MAX OF MEMORY DESIRED     84275
         A     R6,STORESVD   PLUS STORAGE RESERVED FOR OS USE    84275
         C     R6,MEMYWANT+4                                     84275
         BNH   *+6                                               84275
         LR    R7,R6         SET TOO LOW MAX AT LEAST TO MIN     84275
         STM   R6,R7,COREWANT  SET GETMAIN LIMITS                84275
         MVC   READBFR(PATMAINL),PATMAIN  COPY GETMAIN PATTERN   84275
         GETMAIN VU,LA=COREWANT,A=COREGOT,MF=(E,READBFR)         84275
         LM    R7,R8,COREGOT       R7=ADDRESS, R8=LENGTH         84275
         L     R6,STORESVD   GET STORAGE RESERVED FOR OS USE     84275
         SR    R8,R6         RETURN SOME FOR OS                  84275
         LR    R9,R8                                             84275
         AR    R9,R7         R9=ADDRESS AT WHICH TO FREE RESERVE 84275
         ST    R8,COREGOT+4        SAVE CORE KEPT                84275
         LTR   R6,R6         ANYTHING TO FREE ?                  84275
         BNP   NOFREEMN      NO                                  84275
         FREEMAIN R,LV=(R6),A=(R9)                               84275
NOFREEMN CLRL  (R7),(R8)     CLEAR THE AREA                      84275
         SPACE 1                                                 84275
         LA    R14,0(,R7)          BEGIN OF AVAIL. CORE; ZERO HI BYTE
         LA    R9,ALLOCTAB         ALLOCATION TABLE              84275
         LA    R6,ALLOC2-ALLOCTAB  INCREMENT                     84275
         LA    R7,ALLOCLST         END FOR BXLE                  84275
ALLOCATE LR    R5,R8               LENGTH OF CORE GOTTEN         84275
         SLR   R4,R4                                             84275
         MH    R5,0(,R9)           % TO ALLOCATE TO THIS TABLE   84275
         D     R4,=F'100'          R5 = AMT. OF CORE IN BYTES FOR TBL.
         CH    R5,2(,R9)           R5 : MINIMUM                  84275
         BH    ALLOC01             >                             84275
         LH    R5,2(,R9)           DEFAULT TO MIN.               84275
ALLOC01  CH    R5,4(,R9)           R5 : MAXIMUM                  84275
         BL    ALLOC02             <                             84275
         LH    R5,4(,R9)           USE MAX.                      84275
ALLOC02  LH    R3,6(,R9)           PUT BEGIN ADDRESS             84275
         ST    R14,SAVE(R3)        WHERE IT GOES.                84275
         LA    R5,3(,R5)           ROUND UP                      84275
         SRL   R5,2                TO NEXT                       84275
         SLL   R5,2                FULLWORD BDRY.                84275
         AR    R14,R5              NEXT CORE AVAILABLE.          84275
         LH    R3,8(,R9)           END ADDRESS                   84275
         LTR   R3,R3               NEEDED?                       84275
         BZ    ALLOC03             (NO)                          84275
         ST    R14,SAVE(R3)        SAVE IT.                      84275
ALLOC03  BXLE  R9,R6,ALLOCATE      FOR NEXT ENTRY.               84275
         LH    R3,6(,R9)           TAKE CARE OF CESD NOW         84275
         ST    R14,SAVE(R3)        START ADDRESS                 84275
         LH    R3,8(,R9)           END LOCATION                  84275
         LM    R7,R8,COREGOT                                     84275
         AR    R7,R8               R7 = END ADDRESS              84275
         ST    R7,SAVE(R3)         SAVE ADDRESS                  84275
         SPACE 1                                                 84275
         LA    R1,READBFR    GET THE READ BUFFER                 87081
         LA    R1,TABLLN(,R1)  GET END OF BUFFER                 87081
         ST    R1,RCBFEND          END OF READ BUFFER            84275
         PRTLIST MEMHEAD,DEV=2  IDENTIFY WHAT WE ARE DOING       84275
         NI    INTERN1,255-PCFIRST-NUCMOD                        84275
         OI    SWITCHES,ERRLVL4    IF LEFT ON, RETURN CODE = 4   84275
         MVC   DECKSEQ,ZEROES  INITIALIZE DECK OPTION COUNTER    84275
         MVC   DECKC73,PDS2NAME     AND EBCDIC NAME              84275
         SPACE 1                                                 84275
         MVC   FINDTTR,PDS2TTRP  SAVE TTR/Z FOR POINTS           84275
         MVC   PDS2INDC(65),PDS2INDC+2  ERASE BLDL BYTES         84275
         MVC   MEMNAME,PDS2NAME  SAVE USER'S NAME                84275
         TM    LKFLAG,LKFGPARM  NEED TO BUILD LKED PARM ?        84275
         BZ    NOTLPARM      NO                                  84275
         MVC   DB(2),PDS2ATR  COPY ATTRIBUTES                    84275
         XI    DB+1,X'80'    FLIP THE DC BIT                     87081
         BAS   R3,MAKLPARM   BUILD NEW LKED PARM                 89081
         PRTLIST NEWLPARM,DEV=2  PRINT NEW PARM INFO             84275
         TM    LKFLAG,LKFGNAME+LKFGALII+LKFGINS+LKFGSSI  MORE TO DO ?
         BZ    NEXTMBR       NO; TAKE FAST EXIT                  84275
NOTLPARM MVC   FINDTTR(3),=X'000001'  SET TO DIRECTORY TTRZ(001Z)
         FIND  MODDCB,FINDTTR,C  FANCY POINT TO ZTH DIRECTORY    84275
         LA    R6,ALIASNAM   POINT TO FIRST ALIAS SLOT           84275
DIRBLOCK LA    R4,READBFR    POINT TO BUFFER                     84275
         LA    R3,TABLLN     SET EXPECTED READ LENGTH (NO KEY)   84275
         BAS   R14,READRTN   READ                                84275
         LH    R3,0(,R4)     GET SIZE OF BLOCK READ              84275
         AR    R3,R4         GET LAST BYTE +1                    84275
         BCTR  R3,0          ADJUST                              84275
         LA    R2,2          SET LENGTH OF LENGTH                84275
DIRNEXT  BXH   R4,R2,DIRBLOCK  READ ANOTHER BLOCK IF EXHAUSTED   84275
         LR    R1,R4         FINAGLE                             84275
         STM   R1,R3,DIRBXLE  UPDATE POINTERS                    84275
         CLI   0(R4),X'FF'   LAST MEMBER PROCESSED ?             84275
         BE    ENDDIRP       YES; SKIP OUT                       84275
         SERVCALL PDSDE,DIRBXLE   DECODE CURRENT BLOCK           84275
         LR    R15,R0                                            84275
         MVC   PDORALIS(PDORBLNK),0(R15)  SAVE RETURNED DATA     84275
         CLC   PDS2TTRP,PDS2TTRP-PDS2NAME(R4)  SAME TTR ?        84275
         BNE   DIRBUMP       NO; SKIP                            84275
         TM    PDS2INDC-PDS2NAME(R4),PDS2ALIS  ALIAS ENTRY ?     84275
         BNZ   DIRPALIS      YES; CHECK AGAINST USER'S NAME      84275
         MVC   MEMNAME,0(R4) SAVE THIS NAME FOR LATER VERIFICATION
         MVC   MEMEPA,PDS2EPA-PDS2(R4)   SAVE OFFSET, ALSO       84275
         B     DIRBUMP                                           84275
DIRPALIS MVC   0(8,R6),0(R4) SAVE THE ALIAS NAME                 84275
         MVC   8+1(3,R6),PDS2EPA-PDS2(R4)  SAVE ENTRY FOR ALIAS  84275
         LA    R6,12(,R6)    POINT TO NEXT SLOT                  84275
DIRBUMP  LM    R1,R3,DIRBXLE   RELOAD                            84275
         LR    R4,R1         UNFINAGLE                           84275
         B     DIRNEXT       GET NEXT MEMBER                     84275
ENDDIRP  SH    R6,=H'12'     POINT TO LAST ALIAS ENTRY           84275
         ST    R6,ALIASPTR   SAVE POINTER                        84275
         SLR   R3,R3                                             84275
         IC    R3,FINDTTR+3  GET CONCATENATION NUMBER            84275
GONETIOT SERVCALL TIODD,LKEDDLIB  SET LNKLIB OR LNKLMOD          84275
         LR    R4,R0         COPY THE TIOT ADDRESS               84275
BUMPTIOT SH    R3,=H'1'      COUNT DOWN                          84275
         BM    HAVETIOT      HAVE TIOT ADDRESS                   84275
         SERVCALL TIOLP,(R4)   GET NEXT ENTRY                    84275
         LTR   R4,R0         ANY ?                               84275
         BNZ   BUMPTIOT      YES                                 84275
         B     GONETIOT      USE DEFAULT                         84275
         SPACE 1                                                 84275
         PUSH  USING                                            GP02265
         USING TIOENTRY,R4   DECLARE TIOT                        84275
HAVETIOT SR    R3,R3
         ICM   R3,7,TIOEJFCB  GET JFCB ADDRESS OR TOKEN          84275
         SERVCALL SWARL,(R3) COPY JFCB TO 24-BIT AREA            92285
         BXH   R15,R15,HAVETIOF  SKIP IF BAD                    GP02251
         CLC   =C'JFCB',12(R1)  HAVE A HEADER?                  GP02251
         BNE   *+8           NO                                 GP02251
         LA    R1,16(,R1)    SKIP QUEUE HEADER                  GP02251
         MVC   CURDSN,JFCBDSNM-INFMJFCB(R1)  MOVE DSN TO SUB-HEADER
         ICM   R1,7,TIOEFSRT  LOAD THE UCB ADDRESS              GP02265
         SERVCALL SWAAB,(R1) UCB TOKEN FOR ADDRESS OR COPY      GP02251
         BXH   R15,R15,TESTALLF  FAILED ?                       GP02251
         USING UCBOB,R1      DECLARE IT                         GP02251
HAVETIOU INHEX CURUCB,UCBCHAN  GET DEVICE NUMBER                GP04065
         MVC   CURVOL,UCBVOLI    AND VOLUME SERIAL               84275
         SERVCALL UCBGN,UCBOB   GET GENERIC NAME                 84275
         MVC   CURUNIT,0(R1)                                     84275
         POP   USING                                            GP02265
HAVETIOF CLC   MEMNAME,PDS2NAME   DID USER SPECIFY ALIAS ENTRY ? 84275
         BE    ENDDIRD       NO; USE AS IS                       84275
         PRTLIST MEMISALI,DEV=2                                  84275
         MVC   DB,MAINNAME   GET ORIGINAL (MEMBER) NAME          89281
         LA    R1,DB         POINT TO WORK SPACE                 89281
         BAS   R14,TESTSVCN  SEE IF NATURAL FORM                 89281
         CLC   PDS2NAME,DB   IS THIS THE NAME THE USER USED ?    89281
         BNE   *+10          NO; LEAVE AS IS                     89281
         MVC   MAINNAME,MEMNAME  UPDATE TO USE MAIN MEMBER NAME  89281
         MVC   PDS2NAME,MEMNAME   UPDATE NAME                    84275
         MVC   PDS2EPA,MEMEPA          AND ENTRY OFFSET          84275
         BLDL  MODDCB,BLDLLIST   GET UPDATED INFORMATION         84275
         BXH   R15,R15,BLDLERR                                   84275
         MVC   PDS2INDC(65),PDS2INDC+2  ERASE BLDL BYTES         84275
ENDDIRD  MVC   FINDTTR(3),PDS2TTRP  SET FOR FIRST RECORD         84275
         FIND  MODDCB,FINDTTR,C   POSITION TO IT                 84275
         LA    R14,PDS2NAME  POINT TO MAIN MEMBER DATA           84275
         LA    R15,64        PHONY LENGTH                        84275
         LA    R0,0(R14,R15)  ANY OLD END ADDRESS                84275
         STM   R14,R0,DIRBXLE  SET BXLE POINTERS FOR SERVICE     84275
         SERVCALL PDSDE,DIRBXLE  CONVERT, EXPAND, ETC.           84275
         LR    R15,R0                                            84275
         MVC   PDORALIS(PDORBLNK),0(R15)  SAVE RETURNED DATA     84275
         TM    FLAG3,FGSOME+FGLOTS  PRINT-OUT DESIRED ?          84275
         BZ    *+8           NO; SKIP IT                         84275
         BAS   R14,@IEBPDS   PRINT FORMATTED DIRECTORY INFORMATION
         TM    PDOTYPE,PDOTLKED  LINKAGE-EDITOR DIRECTORY ENTRY ?
         BZ    NOTLOAD       NO; CANNOT PROCESS                  87291
         TM    PDS2ATR2,PDS2NREP   IS THERE A CESD?              84275
         BO    NOTEDTBL                                          84275
         NI    SWITCHES,255-ERRLVL4  TURN OFF SWITCH             84275
         TM    PDS2ATR1,PDS2OVLY   OVERLAY?                      84275
         BZ    NOTOVLY                                           84275
         SPACE 3                                                 84275
***** READ IN CESD; KEEP SD, CM, PC, PD; FIND EP. *****          84275
         SPACE 1                                                 84275
         LM    R6,R7,CESDADR                                     84275
READ     LA    R4,READBFR          READ IN CESD                  84275
         LA    R3,TABLLN                                         84275
         BAS   R14,READRTN                                       84275
         SPACE 1                                                 84275
************************* BLOCK CONTROL BYTES ***********************
IDRREC   EQU   X'80'                                             84275
SYMREC   EQU   X'40'         SYMBOL TABLE ENTRIES                84275
CESDREC  EQU   X'20'         CESD RECORD                         84275
SCTRREC  EQU   X'10'         SCATTER/TRANSLATION RECORD          84275
CNTRLREC EQU   X'01'                                             84275
         SPACE 1                                                 84275
         CLI   0(R4),CNTRLREC      CONTROL RECORD?               84275
         BE    CESDFULL      YES; GET $SEGTAB SIZE FROM IT       84275
         CLI   0(R4),CESDREC   CESD INFORMATION ?                84275
         BNE   READ          NO; READ SOME MORE.                 84275
         LH    R9,6(,R4)           LENGTH OF ESD FIELDS          84275
         LA    R8,16         INCREMENT                           84275
         AR    R9,R4               ADDRESS + LENGTH              84275
         LA    R4,8(,R4)           SKIP CONTROL INFORMATION      84275
         USING ESDNAME,R4                                        84275
CHECK    CLI   ESDTYPE,CODESEG     PD?                           84275
         BNE   PVT                 NO; CHECK FOR PC.             84275
         TM    INTERN1,PCFIRST     FIRST PD ITEM?                84275
         BO    ENDTAB              YES; GO.                      84275
         OI    INTERN1,PCFIRST     SET SWITCH                    84275
         MVC   ESDNAME,=CL8'$SEGTAB'  NAME IS $SEGTAB            84275
         B     MOVE                                              84275
ENDTAB   MVC   ESDNAME,=CL8'$ENTAB'  NAME IS $ENTAB              84275
         B     MOVE                                              84275
PVT      CLI   ESDTYPE,CODEPC      PC?                           84275
         BNE   CLRBITS                                           84275
         MVC   ESDNAME,=CL8'$PRIVATE'  NAME IS $PRIVATE          84275
         B     MOVE                                              84275
CLRBITS  NI    ESDTYPE,CODEMASK    TURN OFF LKED FLAGS           84275
         BZ    TESTEP              ZERO IF SD                    84275
         CLI   ESDTYPE,CODELR      LR?                           84275
         BE    TESTEP                                            84275
         CLI   ESDTYPE,CODECM      CM?                           84275
         BNE   UPDATE                                            84275
         CLI   ESDNAME,C' '        BLANK COMMON?                 84275
         BNE   MOVE                NO; OK.                       84275
         MVC   ESDNAME,=CL8'$BLKCOM'  YES; NAME IS $BLKCOM       84275
         B     MOVE                                              84275
TESTEP   CLC   ESDADDR,PDS2EPA      IS THIS THE EP?              84275
         BNE   NOTTHEEP                                          84275
         CLI   ENTRY,C' '    PREVIOUSLY STASHED ?                84275
         BH    *+10          YES; USE SD RATHER THAN LR NAME     84275
         MVC   ENTRY,ESDNAME       YES; SAVE NAME.               84275
NOTTHEEP CLI   ESDTYPE,CODESD                                    84275
         BNE   UPDATE              IF LR.                        84275
MOVE     MVC   0(16,R6),ESDENTRY   SAVE IN CESD BUFFER.          84275
*WHY?*   XC    10(2,R6),10(R6)     SET TO 0 FOR PRINT POS.       84275
         LA    R6,LENESD(,R6)  POINT TO NEXT SAVE ENTRY          84275
         CR    R6,R7               NEXT SPACE IN CESD BUFFER     84275
         BNL   MBOVRFLW            CESD BUFFER OVERFLOW.         84275
UPDATE   BXLE  R4,R8,CHECK                                       84275
         B     READ                READ IN ANOTHER RECORD.       84275
         SPACE 1                                                 84275
*****   READ IN SEGTAB; BUILD AND SORT ENTRIES   *****           84275
         SPACE 1                                                 84275
CESDFULL ST    R6,CESDUSED         LENGTH USED.                  84275
         LH    R3,14(R4)           GET LENGTH OF SEGTAB FROM CCW.
         LM    R4,R5,SEGBFBEG                                    84275
         LR    R8,R4               SEGTAB BUFFER BEGIN           84275
         AR    R8,R3               + SEGTAB LENGTH               84275
         CR    R8,R5               -- LARGE ENOUGH?              84275
         BNL   SGBOVFLW            NO.                           84275
         BAS   R14,READRTN         READ IT IN.                   84275
         ST    R8,SEGUSED          SAVE UPPER LIMIT.             84275
         DROP  R4                                                84275
         L     R3,CESDADR          FOR CESD                      84275
         USING ESDNAME,R3                                        84275
         LA    R4,LENESD     INCREMENT FOR CESD                  84275
         L     R5,CESDUSED         END OF CESD                   84275
         SR    R5,R4               - 16 FOR BXLE                 84275
         STM   R3,R5,ESDPTR  SET FOR SORT                        84275
         B     ESDSOSRT      GO TO SORT THE ENTRIES              84275
OLAYPRT  TM    FLAG3,FGLOTS  PRINT EVERYTHING ?                  84275
         BZ    OLAYDECK      NO; JUST PUNCH                      84275
OLAYLOOP CLI   ESDTYPE,CODELR    LR ?                            84275
         BNE   OLAYSD        SKIP IF NOT LR.                     84275
         PRTLIST ORDLRPRT,DEV=2    PRINT LR INFO                 84275
         B     OLAYBUMP                                          84275
OLAYSD   PRTLIST ORDOLPRT,DEV=2    PRINT THE CONTROL CARD INFO   84275
OLAYBUMP BXLE  R3,R4,OLAYLOOP                                    84275
         DROP  R3                                                84275
OLAYDECK BAS   R14,OVLYDECK        PRODUCE DECK.                 84275
         SPACE 1                                                 84275
IEWLQUIT TM    SWITCHES,ERRLVL4    LEVEL 4 ERROR?                84275
         BZ    NEXTMBR             NO.                           84275
         OI    LMPCODE+L'LMPCODE-1,4   YES. SET IT.              84275
NEXTMBR  NI    LMPCODE+L'LMPCODE-1,15  REMOVE LEVEL 16 ERROR     84275
IEWLDONE CLOSE MF=(E,OPLMOD)       CLOSE                         84275
         LM    R1,R2,COREGOT RELOAD GOTTEN MEMORY ADDR/SIZE      84275
         LTR   R0,R2         ANY STORAGE GOTTEN ?                84275
         BZ    IEWLDONT      NO; SKIP FREEMAIN                   84275
         FREEMAIN R,A=(1),LV=(0)  FREE TABLE STORAGE             84275
IEWLDONT LM    R0,R15,SAVELOAD  RESTORE REGISTERS + RETURN CODE  84275
         BR    R14           GO BACK TO MAIN PROGRAM             84275
         SPACE 2                                                 84275
***** READ ROUTINE *****                                         84275
         SPACE 1                                                 84275
READRTN  SUBENT IOSAVE                                           84275
         LR    R5,R13        SET WORK AREA ADDRESS FOR SYNAD     84275
         READ  BPAMDECB,SF,MODDCB,(R4),(R3),MF=E                 84275
         CHECK BPAMDECB                                          84275
         TM    OPTSW,IOERR   PERMANENT I/O ERROR ON READ ?       84275
         BZ    EXITRTN       NO; RETURN TO CALLER                84275
         OI    LMPCODE+L'LMPCODE-1,8   SET I/O ERROR LEVEL       84275
WARNPRNT PRTLIST MSGWARNP,DEV=(2,3)  PRINT ERROR MESSAGE        GP02251
         BAS   R14,MESSCLR                                       84275
         B     IEWLQUIT      TERMINATE THIS MEMBER               84275
         SPACE 1                                                 84275
***** PRINT INTERFACE *****                                      84275
         SPACE 1                                                 84275
PRTRTN   SUBENT IOSAVE                                           84275
         PRTLIST MSGWARNP,DEV=2                                 GP02251
         BAS   R14,MESSCLR   CLEAR THE PRINT LINE                84275
EXITRTN  SUBEX IOSAVE                                            84275
         SPACE 1                                                 84275
MESSCLR  MVC   MESSAGE,MESSBLNK   CLEAR THE PRINT LINE           84275
         BR    R14           RETURN TO CALLER                    84275
MSGWARNP FDPRT MESSAGE,NL                                       GP02251
         FDPRT *END                                             GP02251
         SPACE 2                                                 84275
***** ERROR MESSAGES *****                                       84275
         SPACE 1                                                 84275
MBOVRFLW LA    R2,FDXCESD                                        84275
         B     PRTOVFLW                                          84275
SGBOVFLW LA    R2,FDXSEGT                                        84275
PRTOVFLW PRTLIST MSGOVFLW,DEV=(2,3)                              84275
         OI    LMPCODE+L'LMPCODE-1,12                            84275
         B     IEWLDONE            GIVE UP.                      84275
         SPACE 1                                                 84275
NOTEDTBL PRTLIST MSGNEDIT,DEV=(2,3)  SET NOT EDITABLE MESSAGE    84275
         OI    LMPCODE+L'LMPCODE-1,4   MINOR ERROR               84275
         B     IEWLQUIT                                          84275
         SPACE 1                                                 87291
NOTLOAD  PRTLIST MSGNLOAD,DEV=(2,3)  SET NOT LOADLIB/LOADMOD     87291
         OI    LMPCODE+L'LMPCODE-1,20  MAJOR ERROR               87291
         B     IEWLDONE                                          87291
         SPACE 1                                                 84275
FDXCESD  FDPRT 'CESD',PAD                                        84275
FDXSEGT  FDPRT 'SEGTAB',PAD                                      84275
MSGOVFLW FDPRT '** OVERFLOW IN'                                  84275
         FDEXEC 0(R2)        INSERT FDX FRAGMENT                 84275
         FDPRT 'TABLE.  INCREASE REGION.  **'                    84275
         FDPRT *END                                              84275
         SPACE 1                                                 84275
MSGNEDIT FDPRT '** MODULE',NL                                    84275
         FDPRT PDS2NAME,DEB,PAD                                  84275
         FDPRT 'IS NOT EDITABLE **'                              84275
         FDPRT *END                                              84275
         SPACE 1                                                 87291
MSGNLOAD FDPRT '** MODULE',NL                                    87291
         FDPRT PDS2NAME,DEB,PAD                                  87291
         FDPRT 'IN'                                              87291
         FDPRT TVDSN,PAD,DEB                                     87291
         FDPRT 'ON'                                              87291
         FDPRT TVVOL,PAD,DEB                                     87291
         FDPRT 'IS NOT A LOAD-MODULE **'                         87291
         FDPRT *END                                              87291
         SPACE 2                                                 84275
*        PROCESSING FOR NORMAL (NON-OVERLAY) MODULE              84275
         SPACE 1                                                 84275
NOTOVLY  TM    PDS2ATR2,PDS2NREP DOES MODULE CONTAIN ESD ENTRIES?
         BNZ   NOTEDTBL      NO, EXIT WITH ERROR                 84275
         CLC   PDS2TTRP,PDS2TTRT  START TTR SAME AS TEXT TTR ?   84275
         BNL   NOTEDTBL      YES; NO ESD ENTRIES                 84275
         L     R14,CESDADR   GET ESD STORAGE TABLE               84275
         LA    R15,LENESD    ENTRY LENGTH                        84275
         LR    R0,R14        COPY ADDRESS                        84275
         SR    R0,R15        SET FOR FIRST GETESD                84275
         STM   R14,R0,ESDPTRS  SET BXLE POINTERS                 84275
         LR    R6,R14        POINT TO FIRST ENTRY JUST IN CASE   84275
         SPACE 1                                                 84275
ESDEXCP  LA    R4,READBFR    GET INPUT BUFFER                    84275
         LA    R3,TABLLN     EXPECTED MAXIMUM LENGTH             84275
         BAS   R14,READRTN   READ A DISK BLOCK                   84275
         CLI   READBFR,SYMREC   TEST SYM RECORD?                 84275
         BE    ESDEXCP        YES, SKIP RECORD                   84275
         CLI   READBFR,CESDREC  CESD RECORD?                     84275
         BNE   ESDLAST        NO                                 84275
         LH    R2,READBFR+4   RELATIVE # OF 1ST ESD ID           84275
         LA    R3,READBFR+8   START OF ESD DATA                  84275
         LH    R5,READBFR+6   LENGTH OF DATA IN BUFFER           84275
         AR    R5,R3                                             84275
         LA    R4,LENESD1     LENGTH OF ONE ENTRY                84275
         SR    R5,R4                                             84275
         SPACE 1                                                 84275
         USING ESDNAME,R3                                        84275
ESDSCAN  IC    R0,ESDTYPE                                        84275
         LA    R1,CODESEG     CHECK FOR SEGTAB/ENTAB             84275
         NI    ESDTYPE,CODEMASK                                  84275
         NR    R0,R1                                             84275
         CLI   ESDTYPE,CODEPC   PRIVATE COMMON?                  84275
         BE    ESDSCANF      YES; FINAGLE                        84275
         CLI   ESDTYPE,CODECM   COMMON ?                         84275
         BNE   *+8           NO                                  84275
ESDSCANF MVI   ESDTYPE,CODESD  YES; TREAT AS SD                  84275
ESDNUCX  CR    R0,R1          THIS SEGTAB/ENTAB ENTRY?           84275
         BE    ESDUSE1        YES                                84275
         CLI   ESDTYPE,CODESD SD ENTRY?                          84275
         BE    ESDUSE         YES                                84275
         CLI   ESDTYPE,CODELR LR ENTRY?                          84275
         BE    ESDUSE                                            84275
NEXTESD  LA    R2,1(,R2)     UP ESDID                            84275
         BXLE  R3,R4,ESDSCAN                                     84275
         B     ESDEXCP        GET NEXT ESD RECORD                84275
         DROP  R3                                                84275
         USING ESDDATA,R6                                        84275
         SPACE 1                                                 84275
ESDUSE1  STC   R0,ESDTYPE-ESDDATA(,R3)  RESTORE SEG/ENTAB        84275
ESDUSE   BAS   R14,GETESD                                        84275
         STH   R2,ESDID      SET ESD ID                          84275
         MVC   ESDNAME(LENESD1),0(R3) SAVE ESD ENTRY             84275
         B     NEXTESD                                           84275
         SPACE 1                                                 84275
ESDLAST  CLC   ESDPTR,ESDPTR2  ANY ENTRIES STASHED ?             84275
         BH    NOTEDTBL       EXIT - NO ESD DATA AVAILABLE       84275
         CLC   =C'IEANUC0',PDS2NAME  NUCLEUS ?                   84275
         BNE   ESDSOSRT      NO                                  84275
         TM    PDS2ATR1,PDS2SCTR  SCATTER LOAD ?                 84275
         BZ    ESDNPROB      NO                                  84275
         SPACE 1                                                 84275
*        SPECIAL RELOCATION CODE FOR NUCLEUS MEMBERS             84275
*                                                                84275
         L     R6,ESDPTR     GET FIRST ESD ENTRY                 84275
         CLC   =C'IEAVNIP0',ESDNAME  CORRECT MVS ENTRY ?         84275
         BE    ESDNUCVS      YES                                 84275
         CLC   =C'IEAANIP0',ESDNAME  CORRECT MVT/SVS ENTRY ?     84275
         BNE   ESDNPROB      NO; CANNOT RELOCATE                 84275
ESDNUCVS XC    WORKWORK,WORKWORK  CLEAR DOUBLE-WORD              84275
         MVC   WORKWORK+2(2),PDS2SLSZ  GET SCATTER SIZE          84275
         MVC   WORKWORK+6(2),PDS2TTSZ  AND TRTABLE SIZE          84275
         MVC   ENTRY,ESDNAME   SET NIP AS ENTRY POINT            84275
         LM    R4,R5,WORKWORK  GET COMBINED SIZE                 84275
         LA    R3,0(R4,R5)   SAVE LENGTH OF DATA TO BE READ      84275
         LA    R4,4(R4,R4)   SCTR+ADDRESS TABLE+4 ON TOP         84275
         AR    R4,R5         ALLOCATE TWO ADDRESS TABLES         84275
         LR    R5,R4         COPY                                84275
         LM    R0,R1,TRANGET   GET SIZE/ADDRESS OF PRIOR GETMAIN 84275
         CR    R4,R0         WILL IT FIT EXISTING AREA ?         84275
         BNH   ESDNRGTN      YES; USE IT                         84275
         LTR   R0,R0         WAS THERE A PRIOR GETMAIN ?         84275
         BZ    ESDNRGNW      NO; GET NEW ONE                     84275
         FREEMAIN R,LV=(0),A=(1)  FREE OLD AREA                  84275
         XC    TRANGET(8),TRANGET  JUST IN CASE                  84275
ESDNRGNW GETMAIN R,LV=(R4)   GET STORAGE                         84275
         LR    R0,R4         COPY SIZE AGAIN                     84275
         STM   R0,R1,TRANGET  SAVE SIZE AND ADDRESS              84275
ESDNRGTN LTR   R6,R1         TEST IF GOTTEN                      84275
         BNP   ESDNPROB                                          84275
         AR    R5,R6         MAKE TOP BYTE+1                     84275
         SH    R5,=H'4'      ALLOW FOR DUMMY WORD AT END         84275
         ST    R5,ESDTAB     SET TO FAIL IF UNREAD               84275
         MVC   FINDTTR(3),PDS2TTRN  SET TTR OF SCTR TABLE        84275
         FIND  MODDCB,FINDTTR,C   POINT TO FIRST SCTR/TRAN RECORD
ESDNREAD STM   R3,R4,DB      SAVE RESIDUAL SIZE                  84275
         LA    R4,READBFR    GET THE READ BUFFER                 84275
         LA    R3,1024       SET FOR LARGE BLOCKS                84275
         BAS   R14,READRTN   READ A BLOCK                        84275
         CLI   READBFR,SCTRREC  SCATTER/TRANSLATE BLOCK ?        84275
         BNE   ESDNRED       NO; ASSUME END                      84275
         LM    R3,R4,DB      RESTORE RESIDUAL SIZE               84275
         SLR   R0,R0                                             84275
         ICM   R0,7,READBFR+1  GET LENGTH OF DATA IN BLOCK       84275
*WHY?*   AH    R0,=H'4'      FOR OLD CODE COMPATIBILITY          84275
         SR    R5,R0         GET NEW INPUT ADDRESS               84275
         CR    R5,R6         VALID ?                             84275
         BL    ESDNRED       NO; ASSUME DONE                     84275
         SR    R3,R0         GET RESIDUAL SIZE OF TABLE          84275
*WHY?*   AH    R3,=H'4'      ALLOW FOR CONTROL BYTES             84275
         BM    ESDNRED       DONE ?                              84275
         LR    R14,R5        GET BOTTOM ADDRESS                  84275
         LR    R15,R0        SIZE                                84275
         LA    R0,READBFR+4  INPUT DATA                          84275
         LR    R1,R15        SIZE                                84275
         MVCL  R14,R0        MOVE INPUT                          84275
*WHY?*   LA    R5,4(,R5)                                         84275
         ST    R5,ESDTAB     SAVE LAST ADDRESS PROCESSED         84275
         LTR   R3,R3         ANY MORE TO READ ?                  84275
         BP    ESDNREAD      YES; READ AGAIN                     84275
ESDNRED  L     R5,ESDTAB     GET LAST ADDRESS PROCESSED          84275
         A     R5,WORKWORK+4 GET SCTR TABLE                      84275
         ST    R5,ESDSCTR    SET IT                              84275
         A     R5,WORKWORK    GET END BYTE                       84275
         LM    R0,R1,TRANGET GET GETMAIN ADDRESS                 84275
         AR    R1,R0         GET HIGH ADDRESS                    84275
         SH    R1,=H'4'      GET END LESS 4 BYTE DUMMY ENTRY     84275
         CR    R1,R5         MATCHES END ?                       84275
         BNE   ESDNPROB      NO; SOME ERROR                      84275
         L     R6,ESDPTR     GET NIP ENTRY BACK                  84275
         XC    DB,DB                                             84275
         MVC   DB+1(3),PDS2STOR  GET LOAD-MODULE LENGTH          84275
         MVC   DB+5(3),ESDLEN    GET NIP LENGTH                  84275
         LM    R14,R15,DB                                        84275
         LA    R14,0(,R14)   CLEAR SIZE                          84275
         ST    R14,0(,R1)    STORE IN DUMMY ENTRY                84275
         LA    R15,7(,R15)   ROUND NIP LENGTH                    84275
         N     R15,=X'00FFFFF8'                                  84275
         SR    R14,R15       GET NIP ADDRESS                     84275
         ST    R14,DB                                            84275
         MVC   ESDADDR,DB+1  SET NIP ADDRESS                     84275
         MVC   PDS2EPA,DB+1  ALSO SET NIP AS ENTRY ADDRESS       84275
         L     R14,TRANGET+4 GET GETMAINED STORAGE               84275
         L     R15,WORKWORK  GET SIZE OF ADDRESS TABLE           84275
         SRL   R15,2         CHANGE TO NUMBER OF ENTRIES         84275
         SLR   R0,R0                                             84275
         BCTR  R0,0          MAKE CONSTANT FFFFFFFF              84275
ESDADCLR ST    R0,0(,R14)    INITIALIZE - NO ADDRESS             84275
         LA    R14,4(,R14)                                       84275
         BCT   R15,ESDADCLR  DO ALL                              84275
*        HOUSEKEEPING; THEN BUILD ADDRESS TABLE                  84275
         LM    R4,R5,ESDSCTR  GET SCATTER AND TRANSLATE ADDR     84275
         LA    R6,4          SET FIRST OFFSET TO PROCESS         84275
         L     R3,TRANGET+4  GET BASE OF ADDRESS LIST            84275
         LH    R1,2(,R5)     GET NIP INDEX                       84275
         SLL   R1,2          MAKE INTO OFFSET                    84275
         AR    R1,R3         GET ADDRESS ENTRY                   84275
         MVC   0(4,R1),DB    PRESET NIP ADDRESS                  84275
         SLR   R14,R14       CLEAR 'NEXT AVAILABLE' ADDRESS      84275
ESDADLOP LH    R2,0(R6,R5)   GET NEXT TT INDEX                   84275
         SLA   R2,2          CHANGE INTO OFFSET                  84275
         BNP   ESDADINC      SKIP IF NULL                        84275
         L     R0,0(R2,R3)   GET ADDRESS TABLE ENTRY             84275
         LTR   R0,R0         ALREADY SET ?                       84275
         BNM   ESDADINC      YES; SKIP                           84275
         ST    R14,0(R2,R3)  SET NEW ADDRESS FOR THIS SD         84275
         A     R14,4(R2,R4)  ADD NEXT SD ADDRESS                 84275
         S     R14,0(R2,R4)  LESS CURRENT OLD SD = NEW SD ADDRESS
ESDADINC LA    R6,2(,R6)     BUMP TT OFFSET                      84275
         C     R6,WORKWORK+4 DONE ?                              84275
         BL    ESDADLOP      NO; DO AGAIN                        84275
*        NOW RELOCATE ALL ENTRIES                                84275
         LM    R3,R5,ESDPTR  GET ESD POINTERS                    84275
         TM    LKFLAG,LKFGINS  BASIC CONTROL CARDS ?             84275
         BZ    NONIPCRD      NO                                  84275
         BAS   R14,CARDSEQ   GET NEXT CARD SEQUENCE NUMBER       84275
         LR    R2,R3         COPY NIP ENTRY                      89281
         LA    R1,INSPUN     SET FOR INSERT IEAANIP0             84275
         BAS   R14,PUNCHLST  PUNCH NIP INSERT CARD               84275
         AR    R2,R4         POINT TO QFX/VFX                    89281
         BAS   R14,CARDSEQ   COUNT CARDS                         84275
         LA    R1,INSPUN     SET FOR INSERT OF IOS               84275
         BAS   R14,PUNCHLST  PUNCH QFX/VFX INSERT CARD           84275
NONIPCRD OI    INTERN1,NUCMOD    SHOW WE'RE DOING NUCLEUS        84275
         LA    R6,0(R3,R4)   SKIP NIP; START WITH QFX            84275
         LM    R2,R3,ESDSCTR  LOAD SCTR AND TT ADDRESSES         84275
         L     R9,TRANGET+4  AND ADDRESS TABLE ADDRESS           84275
ESDADHOC LH    R1,ESDID      GET CESD ID                         84275
         SLL   R1,1          MAKE SCATTER TABLE OFFSET           84275
         LH    R1,0(R1,R3)   GET INDEX TO TRANSLATE TABLE        84275
         SLL   R1,2          MAKE INTO OFFSET                    84275
         L     R0,0(R1,R9)   GET NEW ADDRESS                     84275
         CLI   ESDTYPE,CODELR  SD OR LR ENTRY ?                  84275
         BNE   ESDADST       SD; HAVE NEW ADDRESS                84275
         L     R15,ESDADDR-1  GET OLD LR ADDRESS                 84275
         SL    R15,0(R1,R2)   LESS OLD SD BASE                   84275
         ALR   R0,R15        NEW ADDRESS                         84275
ESDADST  ST    R0,DB                                             84275
         MVC   ESDADDR,DB+1  SET NEW ADDRESS                     84275
         BXLE  R6,R4,ESDADHOC                                    84275
         B     ESDNUCOK                                          84275
ESDNPROB OI    LMPCODE+L'LMPCODE-1,8                             84275
         PRTV  MSGNSCTR,CC=ASA,DEV=(2,3)                        GP02251
ESDNUCOK LM    R0,R1,TRANGET   LOAD GETMAINED STORAGE            84275
         LTR   R0,R0         ANY STORAGE TO FREE ?               84275
         BZ    ESDSOSRT      NO                                  84275
         FREEMAIN R,LV=(0),A=(1)                                 84275
         XC    TRANGET(8),TRANGET                                84275
ESDSOSRT L     R6,ESDPTR     GET FIRST ENTRY                     84275
         LM    R4,R5,ESDPTR+4  GET LENGTH AND LAST ENTRY         84275
         LR    R15,R5        COPY LAST ENTRY                     84275
         SR    R5,R4                                             84275
         LR    R14,R4        COPY LENGTH                         84275
         CR    R15,R6        ONLY ONE ENTRY ?                    84275
         BNH   ESDOUT        YES; SKIP SORT                      84275
         LA    R2,ESDSWAP    SET SWAP ADDRESS FOR FAST BRANCH    84275
         LA    R3,ESDSBUMP   SET BUMP ADDRESS                    84275
         LA    R9,ESDSCLC    SET CLC ADDRESS FOR NORMAL SORT     84275
         TM    PDS2ATR1,PDS2OVLY  OVERLAY ?                      84275
         BNZ   *+8           YES                                 84275
         LA    R9,ESDSCLAD   ELSE IGNORE REGIONS ON SORT         84275
*        TM    ESDFLAG,ESDFNAME  SORT BY NAME ?                  84275
*        BZ    ESDSORT       NO                                  84275
*        LA    R9,ESDSCLNM   SORT BY CSECT NAME ONLY             84275
ESDSORT  LA    R1,0(R4,R6)   SET NEXT ENTRY                      84275
         BR    R9            DO ESDSCLC OR ESDSCLNM              84275
MSGNSCTR VCON  '0**ERROR PROCESSING SCATTER/TRANSLATION DATA **'
ESDSCLC  CLC   ESDSEG#,ESDSEG#-ESDDATA(R1) CHECK ENTRY SEGMENT #S
         BLR   R3                                                84275
         BHR   R2                                                84275
ESDSCLAD CLC   ESDADDR,ESDADDR-ESDDATA(R1)                       84275
         BLR   R3                                                84275
         BHR   R2                                                84275
         CLC   ESDTYPE,ESDTYPE-ESDDATA(R1)                       84275
         BLR   R3                                                84275
         BHR   R2                                                84275
ESDSCLNM CLC   ESDNAME,ESDNAME-ESDDATA(R1)                       84275
         BNHR  R3                                                84275
ESDSWAP  XC    ESDNAME(LENESD),ESDNAME-ESDDATA(R1)               84275
         XC    ESDNAME-ESDDATA(LENESD,R1),ESDNAME                84275
         XC    ESDNAME(LENESD),ESDNAME-ESDDATA(R1)               84275
ESDSBUMP BXLE  R1,R14,0(R9)  SORT ANOTHER BY ADDR OR NAME        84275
         BXLE  R6,R4,ESDSORT                                     84275
         B     ESDOUT                                            84275
         SPACE 1                                                 84275
GETESD   L     R6,ESDPTR2    GET CURRENT ENTRY                   84275
         L     R0,CESDADR+4  GET BASE                            84275
         A     R6,ESDPTR+4   MAKE NEXT ENTRY                     84275
         S     R0,ESDPTR+4   MAKE LAST FITTING ENTRY             84275
         S     R0,ESDPTR+4   MAKE LAST FITTING ENTRY             84275
         CR    R6,R0         WILL THIS ONE FIT ?                 84275
         BH    MBOVRFLW      NO; QUIT WITH MESSAGE               84275
         ST    R6,ESDPTR2    SET NEW LAST ENTRY                  84275
         BR    R14           RETURN TO CALLER                    84275
         DROP  R6                                                84275
         SPACE 2                                                 84275
*        ESD TABLE FORMAT                                        84275
         SPACE 1                                                 84275
ESDDATA  DSECT ,                                                 84275
ESDENTRY DS    0XL16         ONE LINK-EDIT ENTRY                 84275
ESDNAME  DS    CL8            NAME OF EXTERNAL SYMBOL            84275
ESDTYPE  DS    X              ESD TYPE                           84275
CODESD   EQU   X'00'          ENTERNAL DEFINATION                84275
CODELR   EQU   X'03'          EXTERNAL REFERENCE                 84275
CODEPC   EQU   X'04'          PRIVATE COMMON                     84275
CODECM   EQU   X'05'          COMMON COMMON                      84275
CODEMASK EQU   X'07'         MASK FOR ABOVE                      84275
CODESEG  EQU   X'14'          OVERLAY SEGMENT TABLE              84275
CODEENTB EQU   CODESEG        OVERLAY ENTRY TABLE                84275
ESDADDR  DS    XL3            RELATIVE OFFSET                    84275
ESDSEG#  DS    X              SEGMENT NUMBER                     84275
ESDLEN   DS    XL3            LENGTH OF SD ENTRY                 84275
LENESD1  EQU   *-ESDNAME      LENGTH OF ESD DATA                 84275
ESDID    DS    XL2            RELATIVE ESD ID #                  84275
LENESD   EQU   *-ESDDATA      LENGTH OF ENTRY                    84275
ASMANY   CSECT ,                                                 84275
         SPACE 1                                                 84275
ESDOUT   LM    R3,R5,ESDPTRS  LOAD THE ESD TABLE POINTERS        84275
         TM    PDS2ATR1,PDS2OVLY   CALLED FROM OVERLAY ?         84275
         BNZ   OLAYPRT       YES; PRINT THE MAP                  84275
         XC    ORDCOUNT,ORDCOUNT  CLEAR ORDER COUNTER            84275
         USING ESDDATA,R3                                        84275
NXTNOTO  CLI   ESDTYPE,CODESEG     PD?                           84275
         BE    UPDPRNT       YES; IGNORE                         84275
         CLI   ESDTYPE,CODEPC    PC?                             84275
         BE    UPDPRNT       YES; SKIP NAME                      84275
         NI    ESDTYPE,CODEMASK    TURN OFF LKED FLAGS           84275
         BZ    EPTEST              ZERO IF SD                    84275
         CLI   ESDTYPE,CODELR      LR?                           84275
         BE    EPTEST                                            84275
         CLI   ESDTYPE,CODECM      CM?                           84275
         BNE   UPDNOTO                                           84275
         CLI   ESDNAME,C' '        BLANK COMMON?                 84275
         BE    UPDPRNT       YES; IGNORE                         84275
EPTEST   CLC   ESDADDR,PDS2EPA      IS THIS THE EP?              84275
         BNE   EPNOTFND                                          84275
         CLI   ENTRY,C' '    PREVIOUSLY STASHED ?                84275
         BH    *+10          YES; USE SD RATHER THAN LR NAME     84275
         MVC   ENTRY,ESDNAME       YES; SAVE NAME.               84275
EPNOTFND CLI   ESDTYPE,CODELR    LR ?                            84275
         BNE   EPNOTLR       SKIP IF NOT LR.                     84275
         TM    FLAG3,FGLOTS  LOTS OF OUTPUT ?                    84275
         BZ    UPDNOTO       NO; SKIP                            84275
         PRTLIST ORDLRPRT,DEV=2    PRINT LR INFO                 84275
         B     UPDNOTO                                           84275
EPNOTLR  TM    INTERN1,NUCMOD   DOING NUCLEUS ?                  84275
         BNZ   UPDPRNT       YES; NO ORDER - ALREADY DID INSERTS 84275
         TM    LKFLAG,LKFGINS  BASIC CONTROL CARDS ?             84275
         BZ    UPDPRNT       NO; SKIP PUNCHING                   84275
         LA    R2,1                                              84275
         AH    R2,ORDCOUNT   GET CURRENT CARD COUNT              84275
         CH    R2,=H'1'      ANY PREVIOUS DATA PUNCHED ?         84275
         BNH   PORNPREV      NO                                  84275
         TM    PDS2FTB1,PDS2PAGA+PDSAOSLE  VS EDITOR/PAGE ALIGN ?
         BNO   PORNPAGE      NEITHER                             84275
         CLI   ESDADDR+2,0   POSSIBLE PAGE BOUNDARY ?            84275
         BNE   PORNPAGE      NO                                  84275
         TM    ESDADDR+1,X'0F'                                   84275
         BNZ   PORNPAGE                                          84275
         LA    R1,COMPUN                                         84275
         BAS   R14,PUNCHLST  FINISH OF THE CARD SEQUENCE         84275
PORNPREV LA    R2,1          FORCE PAGE BOUNDARY ON NEW CARD     84275
PORNPAGE CH    R2,=H'1'      STARTING NEW ?                      84275
         BNH   PORPUN1       YES; PUNCH NEW                      84275
         LA    R1,ORDPUNM    SET FOR MIDDLE OF CARD              84275
         CH    R2,=H'6'      MIDDLE ?                            84275
         BL    PORPUN        YES                                 84275
         LA    R1,ORDPUNT    PUNCH LAST AND SEQUENCE             84275
         SLR   R2,R2         SHOW NO PARTIAL CARD IN BUFFER      84275
         B     PORPUN                                            84275
PORPUN1  BAS   R14,CARDSEQ   UPDATE CARD SEQUENCE COUNT          84275
         LA    R1,ORDPUN     START NEW FD                        84275
PORPUN   STH   R2,ORDCOUNT   UPDATE COUNT                        84275
         BAS   R14,PUNCHLST  PUNCH A CARD FRAGMENT               84275
UPDPRNT  TM    FLAG3,FGLOTS  LOTS OF OUTPUT ?                    84275
         BZ    UPDNOTO       NO; SKIP                            84275
         PRTLIST ORDPRT,DEV=2      PRINT THE CONTROL CARD INFO   84275
UPDNOTO  BXLE  R3,R4,NXTNOTO                                     84275
EOFNOTO  CLI   ORDCOUNT+L'ORDCOUNT-1,0  PARTIAL CARD PUNCHED ?   84275
         BE    DECKNOTE      NO                                  84275
         LA    R1,COMPUN                                         84275
         BAS   R14,PUNCHLST  FINISH OFF THE CARD                 84275
DECKNOTE BAS   R14,NOTODECK  PUNCH ENTRY AND NAME CARDS          84275
         B     IEWLQUIT      DONE WITH THIS MEMBER               84275
         EJECT ,                                                 84275
@IEBPDS  SUBENT ,                                                84275
         XC    CURAPF,CURAPF  CLEAR APF CODE                     84275
         SLR   R15,R15                                           84275
         ICM   R15,3,PDOOAPF  APF PRESENT ?                      84275
         BZ    NOSKIP        NO                                  84275
         LA    R1,PDS2(R15)  POINT TO APF CODE                   84275
         MVC   CURAPF,0(R1)  SAVE IT                             84275
NOSKIP   LA    R1,MEMTTR     FORMAT TTR                          84275
         LA    R2,PDS2TTRP                                       84275
         BAS   R9,HEXTTR                                         84275
         TM    PDOTYPE,PDOTLKED   LKED ENTRY ?                   84275
         BZ    NOSKTDAT      NO; CHECK FOR DATA                  84275
         LA    R1,3                                              84275
         LA    R3,MEMENT     SHOW ENTRY ADDRESS                  84275
         LA    R4,PDS2EPA                                        84275
         BAS   R9,HEXCNVT                                        84275
NOSKTDAT BAS   R9,ALOUT      PRINT MEMBER AND ALIAS LINES        84275
         CLI   PDOUDLEN,2    NONE, SHORT, OR SSI ONLY ?          84275
         BNH   PRTINC        YES; DONE WITH THIS MEMBER          84275
         TM    PDOTYPE,PDOTLKED  LKED ENTRY ?                    84275
         BZ    DODATA        NO; FORMAT THE DATA                 84275
         SPACE 1                                                 84275
*        FORMAT THE LKED DATA IN HEX                             84275
*                                                                84275
         BAS   R14,MESSCLR   CLEAR THE MESSAGE/WORK AREA         84275
         LA    R3,MESSAGE    POINT TO LKED OUTPUT DATA           84275
         LA    R4,PDS2USRD   POINT TO INPUT DATA                 84275
         LA    R14,FORLCOM   GET COMMON FORMATTING LIST          84275
         BAS   R8,HEXLIST    FORMAT THE COMMON AREA              84275
         LA    R3,1(,R3)     SPACE BETWEEN LISTS                 84275
         SPACE 1                                                 84275
         TM    PDOFLAGS,PDOFSCTR  SCATTER DATA ?                 84275
         BZ    LFORNSCT      NO                                  84275
         LA    R14,FORLSCTR  SET SCATTER FORMATTING LIST         84275
         BAS   R8,HEXLIST                                        84275
         LA    R3,1(,R3)     SPACE BETWEEN LISTS                 84275
         SPACE 1                                                 84275
LFORNSCT TM    PDOFLAGS,PDOFREAL  REAL MEMBER NAME PRESENT ?     84275
         BZ    LFORNMEM      NO                                  84275
         PRTLIST PUTREAL,DEV=2     DISPLAY MEMBER OF THIS 'ALIAS'
         LA    R14,FORLREAL  SET EPA/NAME MAIN MEMBER            84275
         BAS   R8,HEXLIST                                        84275
         LA    R3,1(,R3)     SPACE BETWEEN LISTS                 84275
         SPACE 1                                                 84275
LFORNMEM TM    PDOFLAGS,PDOFSSI  SSI PRESENT ?                   84275
         BZ    LFORNSSI      NO                                  84275
         LA    R14,FORLSSIE  SET FOR SSI DATA ON EVEN BOUNDARY   84275
         LR    R8,R4         COPY CURRENT INPUT ADDRESS          84275
         SRL   R8,1                                              84275
         SLL   R8,1          TRUNCATE                            84275
         CR    R8,R4         EVEN ADDRESS ?                      84275
         BE    LFORYSSI      YES; DO IT                          84275
         BCTR  R14,0         BACKSPACE TO ODD SSI LIST           84275
LFORYSSI BAS   R8,HEXLIST                                        84275
         LA    R3,1(,R3)     SPACE BETWEEN LISTS                 84275
         SPACE 1                                                 84275
LFORNSSI TM    PDOFLAGS,PDOFAPF   APF DATA ?                     84275
         BZ    LFORNAPF      NO                                  84275
         LA    R14,FORLAPF   FORMAT APF BYTES                    84275
         BAS   R8,HEXLIST                                        84275
         LA    R3,1(,R3)     SPACE BETWEEN LISTS                 84275
         SPACE 1                                                 84275
LFORNAPF PRTLIST PUTLINK2,DEV=2    PRINT THE SECOND LINE         84275
         SPACE 2                                                 84275
*        THIS SECTION OF CODE REORDERS THE ATTRIBUTE BITS INTO A 84275
*        PREFERRED SEQUENCE, THEN PRINTS EITHER THE ATTRIBUTE OR 84275
*        ITS NEGATION (IF PRESENT).                              84275
*                                                                84275
         BAS   R14,MESSCLR   CLEAN UP                            84275
         LH    R1,PDS2ATR    GET PRESENT ATTRIBUTES              84275
         SLL   R1,15         MAKE HIGH BIT NEXT TO SIGN          84275
         LA    R2,16         SIXTEEN ATTRIBUTES TO DO            84275
         OR    R1,R2         FORCE A LOW BIT ON FOR BXH          84275
         XC    DB,DB         ZERO WORK AREA                      84275
         LA    R3,ATTRBITS   GET TABLE OF PREFERRED SEQUENCE BITS
SHUFFLE  BXH   R1,R1,SHUFINC   BRANCH IF BIT IS ZERO             84275
         OC    DB(2),0(R3)  ELSE SET THE ATTRIBUTE FLAG IN DIFF POSITN
SHUFINC  LA    R3,2(,R3)     POINT TO NEXT POSITION IN TABLE     84275
         BCT   R2,SHUFFLE    DO FOR ALL 16                       84275
         LH    R1,DB         GET THE RESULT                      84275
         SLL   R1,15         MAKE HIGH BIT NEXT TO SIGN          84275
         LA    R2,16         DO FOR 16 ATTRIBUTES                84275
         OR    R1,R2         FORCE LOW BIT ON FOR BXH            84275
         LA    R4,ATTRBUTE   GET TABLE OF ATTRIBUTES             84275
         SLR   R15,R15       ZERO IC REGISTER                    84275
         LA    R3,MESSAGE    GET O/P AREA                        84275
         TM    PDOFLAGS,PDOFAPF  APF OPTION ?                    84275
         BZ    OPTNAC        NO                                  84275
         CLI   CURAPF,1      AC LENGTH=1 ?                       84275
         BNE   OPTNAC        NO; IGNORE IT                       84275
         CLI   CURAPF+1,1    AC=1 ?                              84275
         BL    OPTNAC        NO                                  84275
         CLI   CURAPF+1,9    SOMETHING NEW ?                     84275
         BH    OPTNAC        NO; SKIP                            84275
         MVC   MESSAGE+2(4),=C'AC=0'  MOVE BASIC PORTION         84275
         MVN   MESSAGE+2+3(1),CURAPF+1  COMPLETE AC CODE         84275
         LA    R3,6(,R3)                                         84275
OPTNAC   TM    PDS2FTB1,PDS2PAGA  PAGE ALIGNMENT ?               84275
         BZ    OPTLOOP       NO                                  84275
         MVC   0(6,R3),=C', PAGE'  SHOW ALIGNMENT                84275
         LA    R3,6(,R3)                                         84275
OPTLOOP  BXH   R1,R1,OPTNEG    GO TO NEGATION IF BIT IS OFF      84275
         BAS   R14,OPTMOVE   BIT IS ON, PUT MESSAGE IN           84275
         BAS   R14,OPTSKIP   SKIP OVER NEGATION                  84275
         B     OPTINC                                            84275
OPTNEG   BAS   R14,OPTSKIP   SKIP OVER AFFIRMATIVE               84275
         BAS   R14,OPTMOVE   MOVE IN TEXT FOR NEGATION OF ATTRIBUTE
OPTINC   BCT   R2,OPTLOOP    DO FOR ALL ATTRIBUTES               84275
         MVI   MESSAGE,C' '  RESET FIRST COMMA                   84275
         PRTLIST MSGWARNP,DEV=2   ATTRIBUTE LINE                GP02251
         B     PRTINC                                            84275
         SPACE 2                                                 84275
*        FORMAT USER DATA FOR NON-LKED ENTRIES                   84275
*                                                                84275
DODATA   BAS   R14,MESSCLR                                       84275
         LA    R4,PDS2USRD                                       84275
         LA    R3,MESSAGE                                        84275
         SLR   R1,R1                                             84275
         IC    R1,PDOUDLEN   GET DATA LENGTH IN HALFWORDS        84275
         SLL   R1,1          DOUBLE TO GET BYTE LENGTH           84275
         BAS   R9,HEXCNVT                                        84275
         SLR   R2,R2         CLEAR AGAIN                         84275
         IC    R2,PDOUDLEN   GET HALFWORD COUNT BACK             84275
         SLL   R2,2          GET BYTE COUNT FOR OUTPUT           84275
         LR    R3,R2         COPY OVER                           84275
         LA    R1,28*4       SET MAXIMUM LENGTH ON ONE LINE      84275
         CR    R2,R1         MORE THAN ONE LINE REQUIRED ?       84275
         BNH   DODATAP       NO                                  84275
         SR    R3,R1         MAKE LENGTH ON SECOND LINE          84275
         LR    R2,R1         AND LENGTH ON FIRST LINE            84275
DODATAP  PRTLIST PUTDATA,DEV=2     PUT THE DATA LINES            84275
         SPACE 1                                                 84275
PRTINC   BAS   R14,MESSCLR   CLEAR PRINT LINE                    84275
         SUBEX ,                                                 84275
         SPACE 2                                                 84275
*        PRINT THE MEMBER LINE, ALIAS ENTRIES AND SET SPACING    84275
*                                                                84275
ALOUT    SERVCALL DSMEM,PDS2NAME  VALID MEMBER NAME ?            89081
         BXLE  R15,R15,ALOUTM   YES; PRINT IT NORMALLY           84275
         PRTLIST PUTMEMH,DEV=2     ELSE DISPLAY MEMBER IN HEX    84275
         B     ALOUTN        GO TO COMMON                        84275
ALOUTM   PRTLIST PUTMEM,DEV=2                                    84275
ALOUTN   LA    R3,ALIASNAM-12   GET ADDRESS OF FIRST ALIAS ENTRY 84275
         LA    R4,12         SET ENTRY SIZE                      84275
         L     R5,ALIASPTR   GET LAST ENTRY ADDRESS              84275
ALOUTL   BXH   R3,R4,0(R9)   QUIT IF NONE OR NO MORE             84275
         SERVCALL DSMEM,0(R3)  VALID NAME ?                      84275
         BXLE  R15,R15,ALOUTO  YES; PRINT IT                     84275
         PRTLIST PUTALIAH,DEV=2   ELSE PRINT IN HEX              84275
         B     ALOUTQ                                            84275
ALOUTO   CLI   7(R3),X'C0'   SVC NAME ?                          84275
         BNE   ALOUTP        NO                                  84275
         CLC   =C'IGC',0(R3)  REALLY ?                           84275
         BNE   ALOUTP        NO                                  84275
         MVI   7(R3),C'+'    MAKE IT PRETTY                      84275
ALOUTP   PRTLIST PUTALIAS,DEV=2    DISPLAY ALIAS NAMES           84275
ALOUTQ   B     ALOUTL        DO NEXT ENTRY                       84275
         SPACE 2                                                 84275
*                                                                84275
*        CONVERT BINARY TTR TO TTTT.RR                           84275
*                                                                84275
HEXTTR   UNPK  1(7,R1),0(4,R2)                                   84275
         MVI   7(R1),C' '                                        84275
         TR    1(6,R1),TRHEX-C'0'                                84275
         MVC   0(4,R1),1(R1)                                     84275
         MVI   4(R1),C'.'                                        84275
         BR    R9                                                84275
         SPACE 2                                                 84275
*                                                                84275
*        CALL HEXCNVT FROM LIST                                  84275
*                                                                84275
HEXLIST  SLR   R1,R1                                             84275
         IC    R1,0(,R14)                                        84275
HEXLOOP  BAS   R9,HEXCNVT                                        84275
         LA    R14,1(,R14)                                       84275
         ICM   R1,1,0(R14)   LOAD AND TEST NEXT LENGTH BYTE      84275
         BZR   R8            END OF LIST; RETURN                 84275
         LA    R3,1(,R3)                                         84275
         B     HEXLOOP                                           84275
         SPACE 2                                                 84275
*                                                                84275
*        CONVERT FROM INTERNAL HEX TO BCD HEX DATA               84275
*                                                                84275
HEXCNVT  CH    R1,H7                                             84275
         BL    HEX1                                              84275
         UNPK  0(15,R3),0(8,R4)                                  84275
         TR    0(14,R3),TRHEX-C'0'                               84275
         LA    R3,14(,R3)                                        84275
         LA    R4,7(R4,0)                                        84275
H7       EQU   *-2                                               84275
         SH    R1,H7                                             84275
         BP    HEXCNVT                                           84275
         B     HEX2                                              84275
HEX1     LA    R2,0(R1,R1)                                       84275
         SLA   R2,4                                              84275
         LA    R2,0(R1,R2)                                       84275
         EX    R2,UNPK                                           84275
         LA    R2,0(R1,R1)                                       84275
         BCTR  R2,0                                              84275
         EX    R2,TRT                                            84275
         LA    R3,1(R2,R3)                                       84275
         LA    R4,0(R1,R4)                                       84275
HEX2     MVI   0(R3),C' '                                        84275
         BR    R9                                                84275
TRHEX    DC    C'0123456789ABCDEF'  HEX TRANSLATE TABLE         GP99053
HEXTAB   EQU   TRHEX-C'0',256,C'C'                              GP04065
         SPACE 2                                                 84275
OPTMOVE  CLI   0(R4),0       IS THERE AN ATTRIBUTE IN THE TABLE ?
         BE    OPTSKIP       NO, SKIP OVER IT                    84275
         IC    R15,0(,R4)    GET LENGTH - 1 OF ATTRIBUTE         84275
         MVI   0(R3),C','    PUT SEPARATOR CHAR. INTO BUFFER     84275
         EX    R15,OPTMVC    MOVE THE ATTRIBUTE                  84275
         LA    R3,3(R3,R15)     POINT TO NEW OUTPUT AREA         84275
OPTSKIP  IC    R15,0(,R4)    GET LENGTH OF ATTR. - 1             84275
         LA    R4,2(R4,R15)     SKIP TO NEXT TABLE ENTRY         84275
         BR    R14           RETURN                              84275
OPTMVC   MVC   2(0,R3),1(R4)   MOVE LKED ATTRIBUTE FROM TABLE TO O/P
         SPACE 1                                                 84275
PUTMEMH  FDOPT NL                                                84275
         FDTM  PDS2INDC,PDS2ALIS,BZ=PUTMEMHM                     84275
         FDPRT 'ALIAS',PADR                                      84275
PUTMEMHM FD    PDORNAME,HEX,INTENSE  PRINT MEMBER NAME IN HEX    84275
         FDGOTO PUTMEMCM     GO TO COMMON                        84275
         SPACE 1                                                 84275
PUTMEM   FDOPT NL                                                84275
         FDTM  PDS2INDC,PDS2ALIS,BZ=PUTMEMNM                     84275
         FDPRT 'ALIAS',PADR                                      84275
PUTMEMNM FD    PDORNAME,INTENSE      OVERPRINT MEMBER NAME       84275
PUTMEMCM FDPRT MEMTTR,PAD    TTTT.RR OF MEMBER                   84275
         FDPRT MEMENT,PAD    (LKED) ENTRY POINT ADDRESS          84275
         FDCLI PDOUDLEN,1,BE=PUTMEMD,BL=PUTMEMB  NO SSI; SHORT DATA
         FDTM  PDOFLAGS,PDOFSSI,BZ=PUTMEMB  SKIP IF NO SSI       84275
         FDPRT 'SSI=',PADL                                       84275
         FDPRT PDORSSI       FORMAT SSI                          84275
         FDPRT *END                                              84275
PUTMEMB  FDPRT ' ',LEN=12    SPACE FOR MISSING SSI               84275
         FDPRT *END                                              84275
PUTMEMD  FDPRT 'INDC=',PADL                                      84275
         FDPRT PDS2INDC,HEX,PADR                                 84275
         FDPRT ' DATA =',PAD                                     84275
         FDPRT PDS2USRD,2,HEX,PADL                               84275
         FDPRT *END                                              84275
         SPACE 1                                                 84275
PUTALIAS FDOPT IND=38        SET INDENTATION IF NEW LINE FORCED  84275
         FDTM  PDOTYPE,PDOTLKED,BZ=PUTALNLK  SKIP IF NOT LKED    84275
         FDPRT 9(R3),3,HEX,RADJ,LEN=9                            84275
         FD    0(R3),8,PADL,INTENSE                              84275
         FDPRT *END                                              84275
PUTALNLK FD    0(R3),8,RADJ,PADL,INTENSE,LEN=10  ALIAS NAME AND SPACES
         FDPRT *END                                              84275
         SPACE 1                                                 84275
PUTALIAH FDOPT IND=38        SET INDENTATION IF NEW LINE FORCED  84275
         FDTM  PDOTYPE,PDOTLKED,BZ=PUTALNLH  SKIP IF NOT LKED    84275
         FDPRT 9(R3),3,HEX,RADJ,LEN=9                            84275
         FD    0(R3),8,HEX,PADL,INTENSE                          84275
         FDPRT *END                                              84275
PUTALNLH FD    0(R3),8,HEX,PADL,INTENSE  ALIAS NAME              84275
         FDPRT *END                                              84275
         SPACE 1                                                 84275
PUTREAL  FDPRT 'ALIAS OF',RADJ,PADR,LEN=11                       84275
         FD    PDS2MNM-PDSS02(R4),L'PDS2MNM,INTENSE              84275
         FDPRT *END                                              84275
         SPACE 1                                                 84275
PUTLINK2 FDPRT 'LENGTH=',RADJ,NL,LEN=9                           84275
         FDPRT PDS2STOR,I,PADR,RADJ,LEN=7                        84275
         FDPRT '('                                               84275
         FDPRT PDS2STOR,HEX                                      84275
         FDPRT ')',PADR                                          84275
         FDPRT ' INDC='                                          84275
         FDPRT PDS2INDC,HEX,PADR                                 84275
         FDPRT ' DATA =',PAD                                     84275
         FDPRT MESSAGE,DEB                                       84275
         FDPRT *END                                              84275
         SPACE 1                                                 84275
PUTDATA  FDPRT 'INDC=',RADJ,NL,LEN=7                             84275
         FDPRT PDS2INDC,HEX,PADR                                 84275
         FDPRT ' DATA =',PAD                                     84275
         FDPRT MESSAGE,(R2)           VARIABLE LENGTH HEX DATA   84275
         FDOPT IND=18        SET FOR AUTO INDENTATION            84275
         FDOPT NL,CC=C'+'    REQUEST CONDITIONAL OVERPRINTING    84275
         FDPRT UNDERLIN,(R2)           MAKE MATCHING UNDERLINES  84275
         FDCLI PDOUDLEN,29,BL=PUTDATAX    QUIT IF ONLY ONE LINE  84275
         FDPRT MESSAGE+112,(R3),NL    PRINT SECOND LINE          84275
         FDOPT NL,CC=C'+'    REQUEST CONDITIONAL OVERPRINTING    84275
         FDPRT UNDERLIN,(R3)           UNDERLINE IT              84275
PUTDATAX FDPRT *END                                              84275
         SPACE 1                                                 84275
ORDOLPRT FDPRT ESDSEG#,I,RADJ,PADR,NL,LEN=5                      84275
         FDGOTO ORDPRTOL                                         84275
ORDPRT   FDOPT NL                                                84275
ORDPRTOL FDPRT ESDNAME,PADR                                      84275
         FDPRT ESDTYPE,HEX,PADR                                  84275
         FDPRT ESDADDR,HEX,RADJ,LEN=10                           84275
         FDPRT ESDLEN,HEX,RADJ,LEN=8                             84275
         FDPRT *END                                              84275
ORDLRPRT FDOPT IND=30                                            84275
         FDROOM 19                                               84275
         FDPRT ESDADDR,HEX,RADJ,LEN=9                            84275
         FDPRT ESDNAME,PADL                                      84275
         FDPRT *END                                              84275
         SPACE 1                                                 84275
INSPUN   FDPRT 'INSERT',NL,RADJ,LEN=10                           84275
         FDPRT ESDNAME-ESDDATA(R2),8,PADL                        89281
         FDGOTO COMPUN                                           84275
ORDPUN   FDPRT 'ORDER',PADR,NL,RADJ,LEN=7                        84275
         FDTM  ESDADDR+2,X'FF',BM=ORDPUNX,BO=ORDPUNX             84275
         FDTM  PDS2FTB1,PDS2PAGA+PDSAOSLE,BZ=ORDPUNX,BM=ORDPUNX  84275
         FDTM  ESDADDR+1,X'0F',BM=ORDPUNX,BO=ORDPUNX             84275
         FDPRT ESDNAME,DEB                                       84275
         FDPRT '(P)'                                             84275
         FDPRT *END                                              84275
ORDPUNX  FDPRT '   '                                             84275
         FDGOTO ORDPUNMC                                         84275
ORDPUNM  FDPRT ','                                               84275
ORDPUNMC FDPRT ESDNAME,DEB                                       84275
         FDPRT *END                                              84275
ORDPUNT  FDPRT ','                                               84275
         FDPRT ESDNAME,DEB                                       84275
         FDGOTO COMPUN                                           84275
MEMISALI FDPRT ' ',NL,LEN=5                                      84275
         FDREPT 5,C'*'       FRAME                               84275
         FDPRT ' REQUESTED MODULE'                               84275
         FDPRT PDS2NAME,DEB,PAD                                  84275
         FDPRT 'IS AN ALIAS OF'                                  84275
         FDPRT MEMNAME,DEB,PADL                                  84275
         FDPRT ', WHICH WILL BE PROCESSED INSTEAD. '             84275
         FDREPT 5,C'*'       FRAME                               84275
         FDPRT *END                                              84275
         SPACE 2                                                 84275
***** CONSTANTS, ETC. *****                                      84275
         SPACE 1                                                 84275
STORESVD DC    A(12*1024)                                        84275
MEMYWANT DC    A(4*1024,128*1024)                                84275
PATBLDL  DC    H'1,78'  1/2  CONSTANTS FOR BLDL                  84275
BLANKS   DC    CL8' '   2/2                                      84275
         SPACE 1                                                 84275
***** ALLOCATION TABLE *****                                     84275
         SPACE 1                                                 84275
         DROP  R13                                               84275
         USING SAVE,R0             -- FOR S-TYPE CONSTANTS       84275
         SPACE 1                                                 84275
ALLOCTAB DC    H'25,768,4096',SL2(SEGBFBEG,ENDSGBF)  SEGTAB      84275
ALLOC2   EQU   *                                                 84275
ALLOCLST EQU   *-(ALLOC2-ALLOCTAB)                               84275
         DC    H'75,5800,32767',SL2(CESDADR,CESDEND)  CESD BUFFER
         SPACE 2                                                 84275
         DROP  R0,R3                                             84275
         USING SAVE,R13                                          84275
         SPACE 1                                                 84275
PATMAIN  GETMAIN VU,LA=2,A=1,MF=L  GETMAIN PATTERN               84275
PATMAINL EQU   *-PATMAIN                                         84275
         SPACE 2                                                 84275
*        DATA                                                    84275
*                                                                84275
MVCMEM   MVC   0(0,R3),0(R4)                                     84275
UNPK     UNPK  0(0,R3),0(0,R4)                                   84275
TRT      TR    0(0,R3),TRHEX-C'0'                                84275
FORLCOM  DC    AL1(3,1,3,1,2,3,2,3,3,0)  BASIC LKED ENTRY        84275
FORLSCTR DC    AL1(2,2,2,2,0)            SCATTER LOAD DATA       84275
FORLREAL DC    AL1(3,8,0)                MEMBER FOR ALIAS        84275
FORLSSIO DC    AL1(1)        1/2       SSI ON ODD BOUNDARY       84275
FORLSSIE DC    AL1(4,0)      2/2       SSI ON EVEN BOUNDARY      84275
FORLAPF  DC    AL1(2,0)                  APF AC CODE             84275
UNDERLIN DC    14CL8'_'      UNDERLINES AT WORD SPACING          84275
         SPACE 2                                                 84275
*        THIS TABLE IS USED TO REORDER THE LKED ATTRIBUTE BITS   84275
*                                                                84275
ATTRBITS EQU   *             TABLE POSITION IS LKED, NEW BIT GIVES SEQ
         DC    X'8000'       RENT                                84275
         DC    X'4000'       REUS                                84275
         DC    X'0800'       OVLAY                               84275
         DC    X'0400'       TESTRAN                             84275
         DC    X'0100'       LOAD ONLY                           84275
         DC    X'1000'       SCATTER LOAD                        84275
         DC    X'0080'       EXECUTABLE                          84275
         DC    X'0040'       1 BLOCK                             84275
         DC    X'0001'       NOT D.C.                            84275
         DC    X'0020'       ORG = 0                             84275
         DC    X'0010'       EP = 0                              84275
         DC    X'0008'       NO RLD                              84275
         DC    X'0004'       NO RELINK                           84275
         DC    X'0200'       TESTRAN SYMBOL TABLE                84275
         DC    X'0002'       PRODUCED BY F LEVEL                 84275
         DC    X'2000'       REFRESHABLE                         84275
         SPACE 1                                                 84275
*        TABLE OF LKED ATTRIBUTES AND THEIR NEGATION, IF ANY     84275
*                                                                84275
ATTRBUTE EQU   *             TABLE IN SEQ CORR. TO ATTRBITS SETTINGS
         DC    X'3',C'RENT',X'0',C' '                            84275
         DC    X'3',C'REUS',X'0',C' '                            84275
         DC    X'3',C'REFR',X'0',C' '                            84275
         DC    X'6',C'SCTR LD',X'5',C'BLK LD'                    84275
         DC    X'4',C'OVLAY',X'0',C' '                           84275
         DC    X'4',C'TESTR',X'0',C' '                           84275
         DC    X'5',C'T-SYMB',X'0',C' '                          84275
         DC    X'8',C'LOAD ONLY',X'0',C' '                       84275
         DC    X'3',C'EXEC',X'D',C'NOT EXECUTABLE'               84275
         DC    X'4',C'1 BLK',X'0',C' '                           84275
         DC    X'4',C'ORG=0',X'0',C' '                           84275
         DC    X'3',C'EP=0',X'0',C' '                            84275
         DC    X'5',C'NO RLD',X'0',C' '                          84275
         DC    X'8',C'NO RELINK',X'0',C' '                       84275
         DC    X'4',C'F-LVL',X'0',C' '                           84275
         DC    X'5',C'NOT DC',X'1',C'DC'                         84275
         SPACE 1                                                 84275
SCANATTR DC    X'C000',X'1BFE',C'RENT' REQUEST IMPLIES REUS      87074
         DC    X'1004',X'EFFB',C'TEST'  TWO BITS                 87074
         DC    X'0800',X'1BFE',C'LOAD' ONLY LOADABLE             87074
         DC    X'2000',X'1BFE',C'OLAY' SCTR,RENT,REUS,REFR      87278
ATTRPARM DC    X'0400',X'1BFE',C'SCTR' OVLY,RENT,REUS,REFR      84275
ATTRPAR2 DC    X'2000',X'1BFE',C'OVLY' SCTR,RENT,REUS,REFR      84275
OVLYMSG  EQU   *-4,4,C'C'    CONSTANT 'OVLY'                     84275
         DC    X'0001',X'FFFE',C'REFR' REFR                     84275
         DC    X'8000',X'1BFE',C'RENT' REUS                     84275
         DC    X'4000',X'1BFE',C'REUS'                           84275
         DC    X'0800',X'F7FF',C'OL  '                           87074
         DC    X'0080',X'FFFE',C'DC  ' DOWN-WARD INCOMPATIBLE    87074
         DC    X'1000',X'EFFB',C'TEST' TEST SYMBOL TABLE        84275
         DC    X'0004',X'EFFB',C'TEST' TEST                     84275
ATTRPARX EQU   *                                                 84275
         SPACE 1                                                 84275
NEWLPARM FDPRT 'NEW LKED.PARM=',RADJ,NL,LEN=20                   84275
         FDPRT LKEDOPT,DEBR                                      84275
         FDPRT *END                                              84275
         EJECT ,                                                 84275
OVLYDECK SUBENT ,                                                84275
         USING ESDDATA,R4                                        84275
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*      DECK OPTION ROUTINE: TO PRODUCE A DECK FOR THE LINKAGE EDITOR  *
*       DESCRIBING THE OVERLAY STRUCTURE.                             *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 1                                                 84275
         TM    LKFLAG,LKFGINS  PUNCH BASIC CONTROL CARDS ?       84275
         BZ    ENDDECK       NO; SKIP THIS SECTION               84275
         TM    LOPTOR,PDS2OVLY  WILL WE OR IN OVERLAY OPTION ?   87278
         BNZ   OVLYKEPT      YES                                 87278
         TM    LOPTAND,PDS2OVLY  ARE WE KILLING OVERLAY OPTION ? 87278
         BZ    ENDDECK       YES; SKIP OVERLAY AND INSERT CARDS  87278
OVLYKEPT MVC   PASTREG,LASTREG  INITIALIZE REGION 'CONSTANT'     84275
         L     R5,SEGBFBEG         BEGINNING OF CESD BUFFER      84275
         LA    R3,24(,R5)          + 24 -> FIRST SEGMENT         84275
         LA    R6,4                INCREMENT FOR BXLE            84275
         L     R7,SEGUSED          END OF SEGTAB BUFFER          84275
         SR    R7,R6               - R4 FOR END LIMIT ON BXLE    84275
         SLR   R4,R4                                             84275
LINEAD   AH    R4,=H'1'            NEXT SEGMENT                  84275
         STC   R4,3(,R3)           STORE IN SEGTAB               84275
         BXLE  R3,R6,LINEAD                                      84275
         MVC   0(2,R3),ENDTBL      END-OF-TABLE DELIMITER        84275
         MVC   REGION#,=H'1'                                     84275
         LR    R8,R6               = 4                           84275
         LA    R15,24(,R5)         BEGIN OF SEGMENT 1            84275
         LA    R2,8(,R5)          BEGIN OF REGION 1              84275
NEXTRGN  SLR   R1,R1                                             84275
         ICM   R1,1,0(R2)         # OF LAST SEGMENT              84275
         BZ    ENDDECK             END OF STRUCTURE              84275
         SLL   R1,2                X 4                           84275
         LA    R9,20(R1,R5)        -> LAST SEGMENT FOR BXLE      84275
PUNCHPAS L     R4,CESDADR          FOR CESD                      84275
         LA    R6,LENESD           INCREMENT FOR CESD            84275
         L     R7,CESDUSED         END OF CESD                   84275
         SR    R7,R6               - 16 FOR BXLE                 84275
         LA    R1,MESSAGE+8        1ST POSITION FOR CSECT NAME   84275
         LA    R0,MESSAGE+62       END OF INSERT CARD            84275
         LR    R3,R1               FOR COMPARE                   84275
SEGSRCH  CLC   ESDSEG#,3(R15)      CURRENT SEGMENT #?            84275
         BNE   BXLE1                                             84275
         CLI   ESDTYPE,CODESD      SD?                           84275
         BE    YESTHIS                                           84275
         CLC   ESDNAME,BLKCOMM                                   84275
         BE    BXLE1                                             84275
         CLI   ESDTYPE,CODECM      CM?                           84275
         BNE   BXLE1                                             84275
YESTHIS  MVC   1(8,R1),ESDNAME     MOVE ESD NAME TO INSERT CARD  84275
         LA    R14,8(,R1)          FOR BCT LOOP                  84275
BCTLOOP  CLI   0(R14),C' '                                       84275
         BNE   YESYES                                            84275
         BCT   R14,BCTLOOP                                       84275
YESYES   LA    R1,1(,R14)          NEW END OF CARD               84275
         CR    R1,R0               END OF CARD?                  84275
         BNH   NOPUNCH                                           84275
         MVC   MESSAGE+2(6),INSMSG                               84275
         BAS   R14,PUNCHRTN                                      84275
         LR    R1,R3               RE-INITIALIZE                 84275
         LA    R0,MESSAGE+62                                     84275
         B     BXLE1                                             84275
NOPUNCH  MVI   0(R1),C','          COMMA AFTER ESD ITEM          84275
BXLE1    BXLE  R4,R6,SEGSRCH       TO NEXT CESD ITEM             84275
         CR    R1,R3               PUNCH INSERT CARD?            84275
         BE    NOPUNCH2            NO.                           84275
         MVI   0(R1),C' '          BLANK TRAILING ,              84275
         MVC   MESSAGE+2(6),INSMSG                               84275
         BAS   R14,PUNCHRTN                                      84275
NOPUNCH2 CLC   4(2,R15),ENDTBL     END OF SEGTAB?                84275
         BE    ENDDECK             YES; DONE.                    84275
         CR    R15,R9              END OF THIS REGION?           84275
         BE    BXLE2                                             84275
         MVC   MESSAGE+1(7),OVERLAY  PRODUCE 'OVERLAY' CARD.     84275
         SLR   R3,R3                                             84275
         ICM   R3,1,4(R15)         PREVIOUS SEGMENT #            84275
         BZ    OVLYRGN       IF ZERO, USE REGION #               84275
         MVC   MESSAGE+9(4),OVLYMSG  'OVLY'                      84275
         CVD   R3,DB                                             84275
         OI    DB+7,X'0F'    FIX SIGN                            84275
         UNPK  MESSAGE+13(3),DB+6(2)                             84275
         B     PCHCALL             & PUNCH IT OUT.               84275
OVLYRGN  MVC   MESSAGE+9(7),PASTREG  NODE PT. IS LAST REGION#    84275
PCHCALL  BAS   R14,PUNCHRTN                                      84275
BXLE2    BXLE  R15,R8,PUNCHPAS     FOR NEXT SEGMENT              84275
         LA    R2,2(,R2)                                         84275
         CLI   0(R2),X'00'        ANY SEGMENTS IN THIS REGION?   84275
         BE    ENDDECK             NO; DONE.                     84275
         LH    R3,REGION#          REGION #                      84275
         CH    R3,=H'4'            DONE IF 4TH REGION PROC.      84275
         BNL   ENDDECK                                           84275
         AH    R3,=H'1'            + 1                           84275
         STH   R3,REGION#                                        84275
         MVC   MESSAGE+9(6),REGMSG+1  OVERLAY REGION_(REGION)    84275
         STC   R3,MESSAGE+15                                     84275
         OI    MESSAGE+15,X'F0'                                  84275
         MVC   PASTREG,MESSAGE+9   SAVE FOR USE LATER            84275
         MVC   MESSAGE+16(8),REGMSG                              84275
         MVC   MESSAGE+1(7),OVERLAY                              84275
         BAS   R14,PUNCHRTN                                      84275
         B     NEXTRGN                                           84275
         SPACE 2                                                 84275
NOTODECK SUBENT ,            SECONDARY ENTRY - NOT IN OVERLAY    84275
         SPACE 1                                                 84275
ENDDECK  TM    LKFLAG,LKFGALII  PUNCH ALIAS CARDS ?              84275
         BZ    ENDALIAS      NO; SKIP                            84275
         L     R3,ALIASPTR   GET POINTER TO LAST ALIAS STORED    84275
         LA    R2,12           ENTRY LENGTH                      84275
         LA    R4,ALIASNAM-12  GET FIRST ENTRY                   84275
NEXTALIS BXH   R4,R2,ENDALIAS  SKIP OUT IF NONE OR ALL DONE      84275
         BAS   R14,CARDSEQ   DECK OPTION ?                       84275
         TM    FLAG3,FGLOTS  LOTS OF OUTPUT ?                    84275
         BZ    NEXTALIP      NO                                  84275
         PRTLIST ALIPUN,DEV=2      PRINT THE ALIAS ENTRY         84275
NEXTALIP LA    R1,ALIPUN     SET FOR ALIAS CARD                  84275
         BAS   R14,PUNCHLST  PUNCH THE CARD                      84275
         B     NEXTALIS      GET NEXT ENTRY                      84275
         SPACE 1                                                 84275
ENDALIAS TM    LKFLAG,LKFGSSI  PUNCHING SSI ?                    84275
         BZ    ENDSSI        NO                                  84275
         CLI   SSITEXT,C' '  DID USER CANCEL SSI ?               84275
         BE    ENDSSI        YES; RESPECT THE WISH               84275
         CLI   PDORDATE,C' '  WAS THE OLD SSI IN DATE FORM ?     84275
         BE    NDTSSI        NO                                  84337
         MVC   PDORSSI,SSIDATE  UPDATE WITH CURRENT DATE         84275
         B     DATSSI                                            84275
NDTSSI   CLC   SSITEXT,SSIDATE  DEFAULT SSI ?                    84275
         BE    OLDSSI        YES; USE MODULE'S OLD SSI           84275
         MVC   PDORSSI,SSITEXT  ELSE USE WHATEVER                84275
         B     DATSSI                                            84275
OLDSSI   TM    PDOFLAGS,PDOFSSI  SSI SUPPLIED ?                  84275
         BZ    ENDSSI        NO; SKIP                            84275
DATSSI   BAS   R14,CARDSEQ   TEST DECK OPTION; UP CARD COUNT     84275
         TM    FLAG3,FGSOME+FGLOTS  LOTS OF OUTPUT ?             84275
         BZ    DATSSIP       NO                                  84275
         PRTLIST SSIPUN,DEV=2      PRINT THE SSI                 84275
DATSSIP  LA    R1,SSIPUN     SET FOR SSI                         84275
         BAS   R14,PUNCHLST  PUNCH THE CARD                      84275
ENDSSI   TM    PDOFLAGS,PDOFAPF  APF SUPPLIED ?                  84275
         BZ    ENDOTHER      NO; SKIP                            84275
         CLI   PDORAPF+L'PDORAPF-1,C'1'  KNOWN APF VALUE ?       84275
         BL    ENDOTHER      YES; OMIT AC=0                      84275
         BH    ENDOTHER      DON'T KNOW HOW TO HANDLE AC=2,3,ETC.
         TM    LKFLAG,LKFGPARM+LKFGNAME  DOING PARM OR NAME ?    84275
         BZ    ENDOTHER      NEITHER, SKIP AUTHORIZATION         84275
         BAS   R14,CARDSEQ   DECK OPTION ?                       84275
         TM    FLAG3,FGSOME+FGLOTS  LOTS OF OUTPUT ?             84275
         BZ    ENDAPFP                                           84275
         PRTLIST APFPUN,DEV=2      PRINT THE APF                 84275
ENDAPFP  LA    R1,APFPUN     SET FOR AC                          84275
         BAS   R14,PUNCHLST  PUNCH THE CARD                      84275
         SPACE 1                                                 84275
ENDOTHER TM    LKFLAG,LKFGNAME  PUNCHING NAME CARD ?             84275
         BZ    ENDNAMES      NO; SKIP                            84275
         TM    PDS2ATR1,PDS2SCTR  DOING SCATTER LOAD ?           89281
         BNZ   PUTNAME       YES; SKIP ENTRY CARD                89281
         NC    ENTRY,ENTRY         ENTRY FOUND?                  84275
         BZ    ENTRYNOT                                          84275
         BAS   R14,CARDSEQ   UP CARD SEQUENCE                    84275
         TM    FLAG3,FGSOME+FGLOTS  LOTS OF OUTPUT ?             84275
         BZ    ENDENTP       NO                                  84275
         PRTLIST ENTPUN,DEV=2      PRINT THE ENTRY CARD          84275
ENDENTP  LA    R1,ENTPUN     SET FOR ENTRY                       84275
         BAS   R14,PUNCHLST  PUNCH THE ENTRY                     84275
         B     PUTNAME                                           84275
         SPACE 1                                                GP02251
MSGNOENT VCON  '  **ENTRY POINT NOT FOUND, MUST BE SUPPLIED**'  GP02251
ENTRYNOT PRTV  MSGNOENT,CC=NO,DEV=(2,3)                         GP02251
PUTNAME  TM    FLAG3,FGNOINC  SKIP INCLUDE ?                     89282
         BNZ   PUTNAMNM      YES                                 89282
         BAS   R14,CARDSEQ   UP THE CARD SEQUENCE                84275
         TM    FLAG3,FGSOME+FGLOTS  PRINTING ?                   84275
         BZ    PUTNAMPI      NO                                  84275
         PRTLIST INCPUN,DEV=2  PRINT INCLUDE SYSLMOD             84275
PUTNAMPI LA    R1,INCPUN     GET INCLUDE SYSLMOD(MEMBER)         84275
         BAS   R14,PUNCHLST  PUNCH IT                            84275
PUTNAMNM LA    R1,MAINNAME   POINT TO LKED NAME                  89281
         BAS   R14,TESTSVCN  MAKE INTERNAL FORM                  89281
         BAS   R14,CARDSEQ   INCREASE CARD COUNT                 84275
         TM    FLAG3,FGSOME+FGLOTS  LOTS OF OUTPUT ?             84275
         BZ    PUTNAMEP      NO                                  84275
         PRTLIST NAMPUN,DEV=2      PRINT THE NAME CARD           84275
PUTNAMEP LA    R1,NAMPUN     SET FOR NAME CARD                   84275
         BAS   R14,PUNCHLST  PUNCH THE NAME CARD                 84275
         PRTLIST MEMHSUM,DEV=2                                   84275
ENDNAMES SUBEX ,              RETURN.                            84275
         SPACE 1                                                 84275
MEMHSUM  FDPRT ' ',LEN=10                                        84275
         FDCLC DECKSEQ,ZEROES,4,BE=MEMHEMP                       84275
         FDPRT DECKSEQ,4,I,DEB                                   84275
         FDGOTO MEMHCOM                                          84275
MEMHEMP  FDPRT 'NO'                                              84275
MEMHCOM  FDPRT 'CARDS GENERATED',PADL                            84275
         FDPRT *END                                              84275
         SPACE 2                                                 84275
***** PUNCH INTERFACE / SEQUENCING ROUTINE *****                 84275
         SPACE 1                                                 84275
PUNCHRTN SUBENT IOSAVE                                           84275
         BAS   R14,CARDSEQ   GET NEXT CARD NUMBER                84275
         UNPK  MESSAGE+75(5),DB+5(3)  CONVERT SEQ # TO PRINTABLE 84275
         MVC   MESSAGE+72(4),DECKC73   AND MOVE IN NAME          84275
         LA    R3,MESSAGE    SET CARD ADDRESS                    84275
         BAS   R8,PUTLCARD   GO TO PUT CARD                      84275
PUNCHRTX BAS   R14,MESSCLR                                       84275
         SUBEX IOSAVE        RETURN TO CALLER                    84275
         SPACE 1                                                 84275
PUNCHLST SUBENT IOSAVE                                           84275
         LR    R6,R1         SET PRTLIST ADDRESS                 84275
         BAS   R8,PUTLKED    GO TO PUT CARD                      84275
PUNCHLSX SUBEX IOSAVE        RETURN TO CALLER                    84275
         SPACE 1                                                 84275
CARDSEQ  INC   DECKSEQ,WORK=R0  UPDATE CURRENT CARD COUNT        84275
         CVD   R0,DB         CONVERT TO PACKED                   84275
         OI    DB+7,X'0F'                                        84275
         BR    R14           RETURN TO CALLER WITH COND. CODE    84275
         SPACE 1                                                 84275
INCPUN   FDPRT 'INCLUDE',NL,RADJ,PADR,LEN=9                      84275
         FDPRT WORK,DEB      REFERENCE DD NAME                   84275
         FDPRT '('                                               84275
         FDPRT MEMNAME,DEB                                       84275
         FDGOTO APFPUNC                                          84275
         SPACE 1                                                 84275
NAMPUN   FDPRT 'NAME',NL,RADJ,LEN=5                              84275
         FDPRT MAINNAME,DEB,PADL                                 89281
         FDPRT '(R)'                                             84275
         FDGOTO COMPUN                                           84275
         SPACE 1                                                 84275
ENTPUN   FDPRT 'ENTRY',NL,RADJ,LEN=7                             84275
         FDPRT ENTRY,PADL                                        84275
         FDGOTO COMPUN                                           84275
         SPACE 1                                                 84275
SSIPUN   FDPRT 'SETSSI',NL,RADJ,LEN=10                           84275
         FDPRT PDORSSI,PADL                                      84275
         FDGOTO COMPUN                                           84275
         SPACE 1                                                 84275
APFPUN   FDPRT 'SETCODE AC(',NL,RADJ,LEN=15                      84275
         FDPRT PDORAPF+L'PDORAPF-1,1                             84275
APFPUNC  FDPRT ')'                                               84275
         FDGOTO COMPUN                                           84275
         SPACE 1                                                 84275
ALIPUN   FDPRT 'ALIAS',NL,RADJ,LEN=10                            84275
         FDPRT 0(R4),8,PADL                                      84275
COMPUN   FDOPT SBA=(*,73)                                        84275
         FDPRT DECKC73                                           84275
         FDPRT DECKSEQ,IZ,RADJ,LEN=4                             84275
         FDPRT *END                                              84275
         SPACE 1                                                 84275
NOOPDDFD FDPRT '**UNABLE TO OPEN DD',NL                          84275
         FDPRT WORK,DEB,PAD                                      84275
         FDPRT *END                                              84275
BADBLDL  FDPRT '** BLDL FAILED FOR',NL,PADR                      84275
         FDGOTO MEMHEADC                                         84275
MEMHEAD  FDPRT 'ANALYZING LOAD-MODULE',NL,PADR                   84275
MEMHEADC FDPRT PDS2NAME,DEB,PAD                                  84275
         FDPRT 'IN DD',PAD                                       84275
         FDPRT WORK,DEB,PAD                                      84275
         FDPRT *END                                              84275
NEWBLDL  FDPRT '** BLDL FAILED FOR',NL                           87074
         FDPRT PDS2NAME,DEB,PADL                                 87074
         FDPRT ', DEFAULTS WILL BE USED'                         87074
         FDPRT *END                                              87074
         SPACE 2                                                 84275
***** CONSTANTS *****                                            84275
         SPACE 1                                                 84275
INSMSG   DC    C'INSERT'                                         84275
REGMSG   DC    C'(REGION)'                                       84275
BLKCOMM  DC    CL8'$BLKCOM'                                      84275
OVERLAY  DC    C'OVERLAY'                                        84275
ENDTBL   DC    X'FFFF'             END-OF-SEGTAB FLAG            84275
LASTREG  DC    C'REGION#'                                        84275
         EJECT ,                                                GP04065
*---------------------------------------------------------------------*
*   REFRESH OPENS THE SYSLMOD LIBRARY AGAIN, AND ISSUES AN LLACOPY    *
*   TO REFRESH THE LLA FOR THE NEW MEMBER. IT THEN CALLS SUBDSERV     *
*   TO GET A LIST OF ALL MEMBERS, AND REPEATS THE LLACOPY FOR ALIAS   *
*   ENTRIES OF THE MEMBER.                                            *
*---------------------------------------------------------------------*
         PUSH  USING                                            GP04065
REFRESH  SUBENT BASE=*                                          GP04065
         L     R15,=A(MODELDCB)  GET PATTERN BPAM DCB           GP97289
         MVC   MODDCB(MODELLEN),0(R15)    RE-INIT DCB           GP97289
         MVC   DCBDDNAM-IHADCB+MODDCB(L'DCBDDNAM),LKEDDMOD      GP97289
         LA    R15,REFRESHX  JUST IN CASE; SHOULD NEVER GET HEREGP97289
         STCM  R15,7,1+DCBEODAD-IHADCB+MODDCB                   GP97289
*NOSYN   STCM  R15,7,1+DCBSYNAD-IHADCB+MODDCB                   GP97289
         LA    R15,MODDCB                                       GP97289
         ST    R15,OPLMOD                                       GP97289
         MVI   OPLMOD,X'90'  MAKE INPUT, REREAD                 GP97289
         OPEN  MF=(E,OPLMOD) OPEN THE LNKLMOD DATASET           GP97289
         TM    MODDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPENED?         GP97289
         BZ    REFRESHX      NO?                                GP97289
         MVC   BLDLNAME,JFCBELNM    MOVE MODULE NAME            GP97289
         AIF   (NOT &MVSESA).NOLLA1                             GP05019
         MODESET MODE=SUP                                       GP97289
         LLACOPY BLDLLIST=BLDLLIST,DCB=MODDCB,MF=(E,MYLLAPRM)   GP02243
.NOLLA1  STM   R15,R0,DB     SAVE FOR LATER                     GP97289
         MODESET MODE=PROB                                      GP97289
         PRTLIST LLAUP,DEV=(2,3)                                GP97289
         ICM   R15,15,DB     DID IT WORK ?                      GP04065
         BNZ   REFRESHX      NO; GET OUT NOW                    GP04065
         ICM   R3,7,BLDLNAME+PDS2TTRP-PDS2    GET MEMBER TTR    GP04065
         XC    DS@WORK,DS@WORK  CLEAR FOR RE-USE                GP04065
         SUBCALL SUBDSERV,('INIT',DS@ROOT,MODDCB),VL,MF=(E,CALLPARM)
REFRLOOP SUBCALL SUBDSERV,('LOOP',DS@ROOT,DS@BXLE),VL,MF=(E,CALLPARM)
         LTR   R5,R15        FOUND AN SMDE ?                    GP04065
         BZ    REFRESHT      NO - LOGICAL END OF DIRECTORY      GP04065
         USING SMDE,R5                                          GP04065
         CLM   R3,7,SMDEMLT  SAME TTR ?                         GP05019
         BNE   REFRLOOP      NO; IGNORE                         GP04065
         MVC   BLDLNAME,BLANKS    CLEAR PRIOR TO MOVE           GP04065
         SR    R6,R6                                            GP04065
         ICM   R6,3,SMDENMOF OFFSET TO NAME SECTION             GP05019
         BZ    REFRLOOP      HUH ?                              GP04065
         AR    R6,R5                                            GP04065
         USING SMDENAME,R6                                      GP05019
         SR    R15,R15                                          GP04065
         ICM   R15,3,SMDENMLN  GET LENGTH OF NAME               GP05019
         BZ    REFRLOOP      SHOULD NOT HAPPEN ?                GP04065
         MIN   R15,=AL2(L'JFCBELNM),TYPE=H  NOT TOO LONG ?      GP04065
         BCTR  R15,0         FOR EX                             GP04065
         EX    R15,EXMVCBLD  MAKE NAME                          GP04065
         CLC   JFCBELNM,BLDLNAME    SAME AS MAIN MEMBER ?       GP04065
         BE    REFRLOOP      YES; SKIP                          GP04065
         AIF   (NOT &MVSESA).NOLLA2                             GP05019
         MODESET MODE=SUP                                       GP04065
         LLACOPY BLDLLIST=BLDLLIST,DCB=MODDCB,MF=(E,MYLLAPRM)   GP04065
         STM   R15,R0,DB     SAVE FOR LATER                     GP04065
         MODESET MODE=PROB                                      GP04065
         PRTLIST LLAUP,DEV=(2,3)                                GP04065
.NOLLA2  B     REFRLOOP      IGNORE                             GP04065
REFRESHT ICM   R2,15,DS@ROOT  LOOK FOR DESERV BUFFERS           GP04065
         BZ    REFRESHX      NO; NOTHING TO FREE                GP04065
         SUBCALL SUBDSERV,('END',DS@ROOT),VL,MF=(E,CALLPARM)    GP04065
REFRESHX CLOSE MF=(E,OPLMOD) CLOSE AGAIN                        GP97289
         SUBEX ,                                                GP04065
EXMVCBLD MVC   BLDLNAME(0),SMDENMVL  MOVE NAME                  GP05019
         SPACE 1                                                GP97289
LLAUP    FDPRT '---LLA COPY FOR',NL                             GP04127
         FDPRT BLDLNAME,PAD                                     GP97289
         FDCLC DB,ZEROES,4,BE=LLAUPPED                          GP97289
         FDPRT 'FAILED; R15='                                   GP97289
         FDPRT DB,4,I,DEB                                       GP97289
         FDPRT ', R0='                                          GP97289
         FDPRT DB+4,4,I,DEB                                     GP97289
         FDPRT *END                                             GP97289
LLAUPPED FDPRT 'SUCCESSFUL'                                     GP97289
         FDPRT *END                                             GP97289
         POP   USING                                             84275
         SPACE 2                                                 84275
         PUSH  USING                                             84275
         DROP  ,                                                 84275
         USING BPAMERR,R15                                       84275
BPAMERR  SYNADAF ACSMETH=BPAM                                    84275
         DROP  R15                                               84275
         BASR  R12,0         DON'T TRUST ANYBODY                 84275
         USING *,R12               GET NEW BASE REGISTER...      84275
         LR    R8,R14              SAVE RETURN ADDRESS.          84275
         USING SAVE,R5       PASSED BY PRE-READ CODE             84275
         MVC   MESSAGE(15),=CL15'-** I/O ERROR: '                84275
         MVC   MESSAGE+15(78),50(R1)  MOVE IN SYNAD INFORMATION  84275
         SYNADRLS ,                RELEASE SYNAD AREA.           84275
         OI    OPTSW,IOERR   SET PERMANENT I/O ERROR SWITCH      84275
         LR    R14,R8                                            84275
         BR    R14                 GO.                           84275
         POP   USING                                             84275
         SPACE 2                                                 84275
***** DATA CONTROL BLOCKS. *****                                 84275
         SPACE 1                                                 84275
MODELDCB DCB   DDNAME=LNKLIB,DSORG=PO,MACRF=R,SYNAD=1  BPAM      84275
MODELLEN EQU   *-MODELDCB    LENGTH OF SYSLMOD PATTERN DCB       84275
         READ  PATDECB,SF,MF=L                                   84275
PATDECBL EQU   *-PATDECB                                         84275
         SPACE 1                                                 84275
         LTORG ,                                                 84275
         EJECT ,                                                 87081
*                                                                87081
*        DYNAMIC ALLOCATION CODE (WAS SEPARATE MODULE)           87081
         SPACE 1                                                 87081
WTPTPUT  EQU   64            0 - TPUT (REQ. CPPL); 128 - WTP     87081
*                                                                87081
DYNALC   STM   R0,R15,PUTSAVE  USE INNOCUOUS SAVE AREA           87081
         BASR  R9,0          MAKE NEW BASE                       87081
         DROP  ,                                                 87081
         USING *,R9                                              87081
         USING SAVE,R13                                          87081
         LA    R1,TEXTOLD    SET FOR UNALLOCATION                87081
         MVI   DYNRB+1,S99VRBUN  SET FOR UN-ALLOCATION           87081
         MVI   TUDDN+1,DUNDDNAM  SET DDNAME TO FREE              87081
         TM    ALLOFLAG,ALFHAVE  NEED TO UNALLOCATE ?            87081
         BNZ   DYNUNA        YES; GO TO UNALLOCATE IT            87081
         LA    R1,TEXTNEW    SET FOR ALLOCATION                  87081
         MVI   DYNRB+1,S99VRBAL  SET FOR ALLOCATION              87081
         MVI   TUDDN+1,DALRTDDN  REQUEST DDNAME RETURN           87081
DYNUNA   ST    R1,DYNTXTPP   MAKE LIST HEAD                      87081
         NI    ALLOFLAG,255-ALFHAVE  NO LONGER HAVE ALLOCATION   87081
         LA    R1,DYNRBPTR                                       87081
         DYNALLOC ,                                              87081
         LTR   R15,R15                                           87081
         BZ    EXITOK                                            87081
DYNFAIL  ST    R15,S99RC                                         87081
         CLI   DYNRB+1,S99VRBUN  UNALLOCATE ?                    87081
         BE    EXITOK        DON'T CARE ABOUT ERRORS             87081
         LR    R8,R13        LOAD SAVE AREA                      87081
         AH    R8,=Y(DFPARMS-SAVE)                               87081
         USING DFPARMS,R8                                        87081
         LA    R14,DYNRB     GET THE RB ADDRESS                  87081
         LA    R15,S99RC     AND THE RETURN CODE                 87081
         LA    R0,DFZEROES   ZERO FOR WRITER ADDRESS             87081
         LA    R1,DFSWTCHS   OPTIONS                             87081
         SLR   R2,R2         CPPL ADDRESS                        87081
         LA    R3,DFBUFL1    RETURNED MESSAGE                    87081
         STM   R14,R3,DFPARMS  MAKE COMPLETE PARM LIST           87081
         SLR   R15,R15                                           87081
         ST    R15,DFBUFL1   JUST IN CASE - CLEAR RETURN LENGTH  87081
         ST    R15,DFBUFL2     AND FOR SECOND ENTRY              87081
         LA    R1,DFPARMS                                        87081
         LINK  EP=IKJEFF18                                       87081
         ICM   R15,3,DFBUFL1 IS THERE A MESSAGE TO BE WRITTEN ?  87081
         BNP   DYNAEXIT      NO; EXIT                            87081
         PRTV  DFBUFT1,DEV=(2,3),CC=NO  PRIMARY MESSAGE          87081
         ICM   R15,3,DFBUFL2  IS THERE A SECONDARY MESSAGE ?     87081
         BNP   DYNAEXBD      NO; JUST SET RETURN CODE            87081
         PRTV  DFBUFT2,DEV=(2,3),CC=NO  WRITE SECONDARY          87081
         B     DYNAEXBD                                          87081
         DROP  R8                                                87081
DYNAEXIT LA    R15,2         SET ERROR EXIT; NO MESSAGE          87081
         B     EXITBAD                                           87081
DYNAEXBD LA    R15,1                                             87081
         B     EXITBAD                                           87081
         SPACE 2                                                 87081
EXITOK   SR    R15,R15                                           87081
         CLI   DYNRB+1,S99VRBUN  UNALLOCATE ?                    87081
         BE    EXITBAD       DON'T CARE ABOUT ERRORS             87081
         OI    ALLOFLAG,ALFHAVE  SHOW SUCCESSFUL ALLOCATION      87081
EXITBAD  LM    R0,R14,PUTSAVE  RESTORE EXCEPT RETURN CODE        87081
         BR    R14           RETURN WITH CODE IN R15             87081
         SPACE 2                                                 87081
         LTORG ,                                                 87081
         SPACE 2                                                 87081
PATRBPTR DC    A(X'80000000'+PATRB)                             GP02251
PATRB    DC    AL1(20,S99VRBAL)                                  87081
         DC    AL2(0,0,0)                                        87081
PATTXTPP DC    AL4(*-*)                                          87081
         DC    AL4(0,0)                                          87081
         DC    F'0'          RC                                  87081
PUDDN    DC    AL2(DALRTDDN,1)    DDNAME (REPURNED)              87081
PUDDNLEN DC    AL2(8)                                            87081
PVDDN    DC    CL8' '                                            87081
PUDSN    DC    AL2(DALDSNAM,1)    DSNAME                         87081
PUDSNLEN DC    AL2(44)                                           87081
PVDSN    DC    CL44' '                                           87081
PUVOL    DC    AL2(DALVLSER,1)  VOL=SER=                         87081
         DC    AL2(6)                                            87081
PVVOL    DC    CL6' '                                            87081
PUDIS    DC    AL2(DALSTATS,1,1)  DISP                           87081
PVDISP   DC    X'08'         DISP=SHR                            87081
PUUNT    DC    AL2(DALUNIT,1)     UNIT                           87081
PUUNTLEN DC    AL2(4)                                            87081
PVUNT    DC    CL4' '                                            87081
PATRBEND EQU   *                                                 87081
DFZEROES DC    A(0)                                              87081
DFSWTCHS DC    AL1(WTPTPUT),X'32'  WTP OR TPUT / PATALLOC        87081
         SPACE 1                                                 88111
DDNASM   DC    Y(DDPASMX-DDPASMF)  LENGTH OF DDNAME ENTRIES      88111
DDPASMF  DC    3XL8'0'                                           88111
         DC    CL8'ASMLIB'                                       88111
         DC    CL8'ASMIN'                                        88111
         DC    CL8'ASMPRINT'                                     88111
         DC    CL8'ASMPUNCH'                                     88111
         DC    CL8'ASMUT1'                                       88111
         DC    CL8'ASMUT2'                                       88111
         DC    CL8'ASMUT3'                                       88111
         DC    CL8'ASMGO'                                        88111
         DC    CL8'ASMTERM'                                      88111
DDPASMX  EQU   *             END OF ASSEMBLER DDNAME LIST        88111
         SPACE 1                                                 88111
DDNASMH  DC    Y(DDPASHX-DDPASHF)  LENGTH OF DDNAME ENTRIES      88111
DDPASHF  DC    CL8'ASMGO',2XL8'0'                                88111
         DC    CL8'ASMLIB'                                       88111
         DC    CL8'ASMIN'                                        88111
         DC    CL8'ASMPRINT'                                     88111
         DC    CL8'ASMPUNCH'                                     88111
*   SOME RELEASES OF ASM H EXPECT SYSTERM JUST AFTER SYSUT1     GP10149
         DC    CL8'ASMUT1' 3XL8'0' (NO GAP)                      88111
         DC    CL8'ASMTERM',2XL8'0'   SUPPORT EITHER FORM       GP10149
         DC    CL8'ASMTERM'                                      88111
DDPASHX  EQU   *             END OF ASSEMBLER DDNAME LIST        88111
         SPACE 1                                                 88111
DDNASML  DC    Y(DDPASLX-DDPASLF)  LENGTH OF DDNAME ENTRIES      88111
DDPASLF  DC    CL8'ASMGO',2XL8'0'                               GP97217
         DC    CL8'ASMLIB'                                      GP97217
         DC    CL8'ASMIN'                                       GP97217
         DC    CL8'ASMPRINT'                                    GP97217
         DC    CL8'ASMPUNCH'                                    GP97217
         DC    CL8'ASMUT1',3XL8'0'                              GP97217
         DC    CL8'ASMTERM',3XL8'0'                             GP97217
         DC    CL8'ASMADATA'                                    GP97217
DDPASLX  EQU   *             END OF ASSEMBLER DDNAME LIST       GP97217
         SPACE 1                                                 88111
DDNAST   DC    Y(DDPASTX-DDPASTF)  LENGTH OF DDNAME ENTRIES
DDPASTF  DC    CL8'ASMGO'    SYSLIN                             GP05336
         DC    2XL8'0'                                          GP05336
         DC    CL8'ASMLIB'                                      GP05336
         DC    CL8'ASMIN'                                       GP05336
         DC    CL8'ASMPRINT'                                    GP05336
         DC    CL8'ASMPUNCH'                                    GP05336
         DC    CL8'ASMUT1'                                      GP05336
         DC    CL8'ASMUT2'                                      GP05336
         DC    CL8'ASMUT3'                                      GP05336
         DC    XL8'0'                                           GP05336
         DC    CL8'ASMTERM',3XL8'0'                             GP05336
         DC    CL8'ASMADATA'                                    GP05336
         DC    3XL8'0',CL8'ASMAOPT'                             GP05336
DDPASTX  EQU   *             END OF ASSEMBLER DDNAME LIST        88111
         SPACE 1                                                 88111
DDNCOB   DC    Y(DDPCOBX-DDPCOBF)  LENGTH OF DDNAME ENTRIES      88111
DDPCOBF  DC    CL8'COBLIN',2XL8'0'                               88111
         DC    CL8'COBLIB'                                       88111
         DC    CL8'COBIN'                                        88111
         DC    CL8'COBPRINT'                                     88111
         DC    CL8'COBPUNCH'                                     88111
         DC    CL8'COBUT1'                                       88111
         DC    CL8'COBUT2'                                       88111
         DC    CL8'COBUT3'                                       88111
         DC    CL8'COBUT4'                                       88111
         DC    CL8'COBTERM'                                      88111
DDPCOBX  DC    C'COBUT5  COBUT6  '  END OF COBOL LIST            88111
         SPACE 1                                                 88111
DDNFOR   DC    Y(DDPFORX-DDPFORF)  LENGTH OF DDNAME ENTRIES      88111
DDPFORF  DC    CL8'FORLIN',3XL8'0'                               88111
         DC    CL8'FORIN'                                        88111
         DC    CL8'FORPRINT'                                     88111
         DC    CL8'FORPUNCH'                                     88111
         DC    2XL8'0'                                           88111
         DC    CL8'FORTERM'                                      88111
         DC    CL8'FORLIB'                                       88111
DDPFORX  EQU   *             END OF FORTRAN DDNAME LIST          88111
         EJECT ,                                                 84275
***** DSECT TO DEFINE SHARED CORE. *****                         84275
         SPACE 1                                                 84275
SAVE     DSECT ,             SAVE/WORK AREA                      84275
SAVELOAD DS    15F   1/2     LOADMAPS SAVE AREA                  84275
         DS    H     2/2     -"- R15                             84275
LMPCODE  DS    H'16' 3/2     -"- R15 RETURN CODE                 84275
SUBSAVE  DC    18F'0'              SAVE AREA FOR SUBROUTINES     84275
IOSAVE   DC    18F'0'              SAVE AREA FOR I/O ROUTINE     84275
PUTSAVE  DC    18F'0'        PUT ROUTINE SAVE                    85083
MODDCB   DCB   DDNAME=LNKLIB,DSORG=PO,MACRF=R,SYNAD=1  BPAM      84275
OPLMOD   OPEN  (MODDCB,(INPUT,REREAD)),MF=L  OPEN/CLOSE LIST     84275
         READ  BPAMDECB,SF,MODDCB,MF=L                           84275
UPD      DS    0D                                               GP04065
DB       DS    D                                                 84275
ZEROES   DC    D'0'          CONSTANT ZEROES                     84275
FINDTTR  DS    XL4           TTRZ FOR FIND AND POINTS            84275
ESDPTRS  DS    0XL12         POINTERS TO STASHED ESD INFORMATION 84275
ESDPTR   DS    A             FIRST ESD ENTRY                     84275
ESDPTRL  DS    A               LENGTH OF ONE ENTRY               84275
ESDPTR2  DS    A             LAST ENTRY                          84275
TRANGET  DS    2A(0)         GETMAIN FOR SCATTER/TRANSLATE       84275
WORKWORK DS    2A(0)         SCTR/TRAN SIZES                     84275
         ORG   TRANGET                                          GP04065
DS@ROOT  DS    A        2/3  SUBDSERV ROOT POINTER - REUSE UPD  GP04065
DS@BXLE  DS    A,F,A,A  3/3  SUBDSERV LIST BXLE                 GP04065
DS@WORK  EQU   DS@ROOT,*-DS@ROOT,C'X'  SIZE TO CLEAR            GP04065
         ORG   ,                                                GP04065
ESDSCTR  DS    A                                                 84275
ESDTAB   DS    A                                                 84275
COREWANT DC    A(4*1024,128*1024)                                84275
COREGOT  DC    2F'0'                                             84275
DECKC73  DS    CL4           COLUMNS 73-76                       84275
DECKSEQ  DS    F             SEQUENCE NUMBER OF CARD PUNCHED     84275
MEMNAME  DS    CL8           NAME OF CURRENT MEMBER              84275
MEMEPA   DS    XL3           ENTRY OFFSET OF MEMBER              84275
MESSBLNK DC    C' '          BYTE FOR QUICK MESSAGE BLANKING     84275
MESSAGE  DC    CL132' '            MESSAGE FOR O/P               84275
MSAVE    DC    CL132' '                                          84275
BLKSTART EQU   *             START OF BLANKED AREA               84275
CURDSN   DC    CL44' '       CURRENT DSN                         84275
CURVOL   DC    CL6' '         -"- VOLUME SERIAL                  84275
CURUCB   DC    CL4' '         -"- DEVICE NUMBER                 GP04065
CURUNIT  DC    CL8' '         -"- UNIT TYPE                      84275
MEMTTR   DC    CL8' '        TTTT.RR + OVERFLOW                  84275
MEMENT   DC    CL6' ',C' '   EPA + UNPK OVERFLOW                 84275
BLKLEN   EQU   *-BLKSTART    LENGTH TO BLANK                     84275
ORDCOUNT DC    H'0'          ORDER STACK COUNTER                 84275
CURAPF   DC    XL2'0'        APF (LEN/VALUE)                     84275
CESDADR  DC    F'0'                BEGIN OF CESD BUFFER          84275
CESDEND  DC    F'0'                END OF CESD BUFFER            84275
CESDUSED DC    F'0'                END OF CESD BUFFER ACTUALLY USED.
RCBFEND  DC    F'0'                READ CESD BUFFER END          84275
SEGBFBEG DC    F'0'                BEGIN ADDR OF SEGTAB          84275
ENDSGBF  DC    F'0'                END ADDR OF SEGTAB            84275
SEGUSED  DC    F'0'                END OF SEGTAB ACTUALLY USED   84275
CEXTPTR  DC    F'0'                                              84275
EXTPTR   DC    F'0'                EXTENSION TABLE PTR           84275
         DC    A(0)                ADDRESS OF DEPEND WILL BE INSERTED
HSNOPRCS DC    X'00'               HIGHEST SEG # CURRENTLY PROCESSING
PSGT     DC    X'00'               PREVIOUS SEG # TEST AREA      84275
PASTREG  DC    C'REGION#'                                        84275
         SPACE 2                                                 84275
SWITCHES DC    X'00'               BUFFER SWITCHES               84275
BUFUSED  EQU   1                                                 84275
LINFULL  EQU   2                                                 84275
SEGIN    EQU   4                                                 84275
ERRLVL4  EQU   8                                                 84275
FRSTRCVD EQU   16                                                84275
CONLN    EQU   32                                                84275
LOOKAHED EQU   64                                                84275
SPECON   EQU   128                                               84275
DEPTEST  DC    X'00'               DEPENDENCY TEST AREA          84275
HEXTCNT  DC    H'0'                                              84275
REGION#  DC    H'1'                CURRENT REGION #              84275
         SPACE 2                                                 84275
***** INTERNAL SWITCHES *****                                    84275
         SPACE 1                                                 84275
INTERN1  DC    X'00'               SWITCHES 1                    84275
NMSRCH   EQU   1                                                 84275
PCFIRST  EQU   32                                                84275
NUCMOD   EQU   128           MODULE IS SCATTER-LOADED NUCLEUS    84275
OPTSW    DC    X'00'               SWITCHES 2                    84275
NMRCVD   EQU   2                                                 84275
IOERR    EQU   128           I/O ERROR ON READ                   84275
         SPACE 1                                                 84275
ENTRY    DC    CL8' '              ENTRY POINT SD OR LR          84275
         PRINT &PRTMAC                                           84275
DIRBXLE  SERVPDS PFX=PDO,DSECT=NO,RETURN=YES,VER=1  DIRECTORY WORK AREA
         SPACE 2                                                 84275
***** BLDL LIST *****                                            84275
         SPACE 1                                                 84275
BLDLLIST DC    0A(0),Y(1,78) BLDL ENTRY PREFIX                   84275
BLDLNAME DS    CL8           MEMBER/ALIAS NAME                  GP04065
         DS    XL70          ENSURE SUFFICIENT SPACE FOR LONG ENTRY
         ORG   BLDLLIST+4    REDEFINE                            84275
         PRINT &PRTSYS                                           84275
         IHAPDS PDSBLDL=NO,DSECT=NO                              84275
         ORG   ,                                                 84275
         PRINT &PRTSYS                                           84275
         SPACE 1                                                 84275
         MAPPDS ,            EXPAND SPECIAL PDS MAPPINGS         84275
         SPACE 1                                                 84275
ALIASPTR DS    A             LOCATION OF NEXT AVAILABLE ALIAS NAME
ALIASNAM DS    21CL12        LOTS OF NAMES                       84275
ALIASCLR EQU   *-ALIASNAM      LENGTH TO CLEAR                   84275
MAPCLEAR EQU   *-SUBSAVE     LENGTH CLEARED BY LOADMAPS EVERY CALL
         SPACE 2
PDSDCB   DCB   DDNAME=ASMIN,DSORG=PS,EROPT=ACC,EXLST=PDSEXLST,         *
               MACRF=GL
PDSPATLN EQU   *-PDSDCB      LENGTH FOR RE-INITIALIZATION        81169
SYSPRINT PRTWORK ASMPRINT,RECFM=FBA,WIDTH=121   PAGE SEPARATOR FILE 1
SYSGO    PUNWORK ASMGO,WIDTH=80,RECFM=FB  DEFINE SYSGO FILE 4    81167
PDSDCBLN EQU   *-PDSDCB      LENGTH OF INITIALIZATION MOVE       81169
PDSEXLST DC    0A(0),X'05',AL3(0),X'87',AL3(INFMJFCB)            81167
         SPACE 1                                                 81167
DEVT     DC    D'0'                    FOR DEVTYPE CHECK
WORK     DC    D'0'                    WORK AREA FOR CVD
ECB      DC    F'0'
CIBPTR   DC    A(0)          COMM ECB/CIB PTR
ACB      DC    F'0'          ATTACH ECB / SCAN FLAG FIELD
NEEDDDN  EQU   X'80'           USER DDN= REQUIRED                81167
NEEDDSN  EQU   X'40'           USER DSN= OR DDN= REQUIRED        81167
STOPCODE DC    X'7FFFFFFF'   LOWEST UNSATISFACTORY RETURN CODE   81167
STOPLINK DC    F'4'          CODE PREVENTING LINK-EDIT CALL.     84275
OPLASMIN OPEN  (PDSDCB,(INPUT,REREAD)),MF=L                      81167
SSITEXT  DC    CL8' '        LKED SSI CARD                       81167
SSIDATE  DC    CL8' '        DATE FORMAT SSI                     84275
EPLOC    DC    CL8' '        PROGRAM NAME  (ASMANY1/ASSEMBLER)   81167
ASMPGMNM DC    CL8'&ASML'    CURRENT ASSEMBLER NAME             GP97217
REPLY    DC    CL4' '                                            81167
         SERVDEFS ,          SERVICE AREA                       GP04064
PREVCODE DC    F'0'          PRIOR CONDITION CODE                91003
NAMESTAK DC    3A(0)         ASM NAME CARD BXLE SAVE AREA        84261
DFLLPARM DC    Y(0)          COMMON LKED PARM LENGTH             87074
DFLLPART DC    CL62' '       COMMON LKED PARM TEXT               87074
DFLLPARX EQU   *             END OF PARM FIELD                   87074
LKEDPARM DC    A(LKEDOPTL,X'80000000'+LKEDDDNL)                 GP02251
LKEDOPTL DC    Y(LKEDOPTX-LKEDOPT)                               84275
LKEDOPT  DC    CL100' '                                          84275
LKEDOPTX EQU   *             END OF LKED PARM FIELD              84275
LKEDDDNL DC    Y(LKEDDDNX-LKEDDDN)                               84275
LKEDDDN  DC    CL8'SYSLIN'                                       84275
MAINNAME DC    CL8' '        (LKED) NAME OF CURRENT MEMBER       84261
LKEDDMOD DC    CL8'LNKLMOD'                                      84275
LKEDDLIB DC    CL8'LNKLIB'                                       84275
         DC    XL8'0'                                            84275
LKEDDPRT DC    CL8'SYSPRINT'                                     84275
         DC    XL8'0'                                            84275
LKEDDUT1 DC    CL8'SYSUT1'                                       84275
         DC    3XL8'0'                                           84275
LKEDDTRM DC    CL8'SYSTERM'                                      84275
LKEDDDNX EQU   *                                                 84275
USERLMOD DC    CL8' '        USER DDNAME OVERRIDE FOR SYSLMOD    85083
USERLLIB DC    CL8' '        USER DDNAME OVERRIDE FOR SYSLIB     85083
LOPTOR   DC    XL2'0000'     LINK-EDIT OPTIONS TO OR             87074
LOPTAND  DC    XL2'FFFF'     OPTIONS TO AND AGAINST OLD MODULE   87074
LOPTDFLT DC    XL2'0000'     DEFAULT OPTIONS                     87074
         SPACE 1                                                 81167
PRM240IN DC    0A(0),X'01',AL3(DDNPDS),X'84',AL3(JFCBELNM),X'86',AL3(0)
PRM240PR DC    X'01',AL3(DDNPRINT),X'84',AL3(JFCBELNM)           81167
PRM240PU DC    X'01',AL3(DDNPUNCH),X'84',AL3(JFCBELNM)           81167
PRM240AD DC    X'01',AL3(DDNADATA),X'84',AL3(JFCBELNM)          GP04065
PRM240OB DC    X'01',AL3(DDNLIN),X'84',AL3(JFCBELNM)            GP97241
PRMMODPR DC    X'01',AL3(DDNPRINT),X'8E',AL3(0)  DISP=MOD REQUEST
PRMMODPU DC    X'01',AL3(DDNPUNCH),X'8E',AL3(0)                  81167
PRMMODAD DC    X'01',AL3(DDNADATA),X'8E',AL3(0)  SYSADATA       GP04065
PRMMODOB DC    X'01',AL3(DDNLIN),X'8E',AL3(0)                   GP97241
         SPACE 1                                                 81167
LISTFLAG DC    AL1(1,8)      4X,3CL8 BIG LETTER PARM LIST        81167
LF9700   EQU   X'40'           XEROX 9700 SPECIAL EJECT (LOCAL EPA/CNW)
LFXRXAUX EQU   X'20'           XEROX AUXILIARY TRAY SELECT       90101
LFNOEJE  EQU   X'02'           SKIP PAGE EJECT                   81167
LFINIT   EQU   X'01'           EXTRA EJECT ON FIRST PAGE         81167
         DC    AL1(8)        LENGTH OF EACH ENTRY                81167
LISTNUM  DC    AL1(3)        NUMBER OF DATA LINES FOLLOWING      81167
LISTMEM  DC    CL8' '        MEMBER NAME                         81167
LISTVART DC    CL8'##/##/##'   DATE OR ERROR MESSAGE             81167
LISTDSN  DC    CL8' '        DATASET NAME (PORTION)              81167
         SPACE 1                                                 81167
LISTCOMP DC    C'CC # '      INSERTION FOR COMPLETION CODE MSG   81167
COMPC    DC    C'XXX'          COMPLETION CODE                   81167
         SPACE 1                                                 81167
MYPARM   DC    A(0,X'80000000'+DDLIST)                          GP02251
         DC    AL2(0)
DDLIST   DC    Y(DDLAST-DDFIRST)   LENGTH OF DDNAME ENTRIES      81167
DDFIRST  DS    0CL8          FIRST ENTRY                         88111
DDNLIN   DC    3XL8'0'                                           88111
DDNLIB   DC    CL8'ASMLIB'
DDNPDS   DC    CL8'ASMIN'                                        81167
DDNPRINT DC    CL8'ASMPRINT'                                     81167
DDNPUNCH DC    CL8'ASMPUNCH'                                     81167
DDNUT1   DC    CL8'ASMUT1'                                       81167
DDNUT2   DC    CL8'ASMUT2'                                       81167
DDNUT3   DC    CL8'ASMUT3'                                       81167
DDNGO    DC    CL8'ASMGO'                                        81167
DDNTERM  DC    CL8'ASMTERM'                                      81167
         DC    3XL8'0'                                          GP04065
DDNADATA DC    CL8'ASMADATA',3XL8'0'  ROOM FOR EXPANSION        GP04065
DDLAST   EQU   *             END OF ASSEMBLER DDNAME LIST        81167
         SPACE 1                                                 81169
WTOR     WTOR  'REPLY STOP TO TERMINATE CURRENT ASSEMBLY',REPLY,4,ECB,M*
               F=L                                               81169
ATTALIST ATTACH SF=L                                             81169
ATTALEN  EQU   *-ATTALIST                                        81169
         SPACE 1                                                 81167
FGAUTONM EQU   X'80'         CREATE LKED NAME (R) CARD AUTOMATICALLY
FGWTOR   EQU   X'40'           ISSUE WTOR FOR OPERATOR STOP      81167
FGWTORED EQU   X'20'           WTOR ISSUED                       81167
FGSTOP   EQU   X'10'           STOP/CANCEL/ABEND TERMINATION     81167
FGNOSVC  EQU   X'08'           BYPASS NAME CARD SVC NAME CHECK   81167
FGAUTOPG EQU   X'04'           GENERATE PAGE CARD WITH AUTO NM   81167
FGSVCGO  EQU   X'02'           SET SYSGO DISP=MOD AFTER NEXT OPEN
FGSYSGO  EQU   X'01'         SYSLIN(SYSGO) IS OPEN               81167
FLAGS    DC    AL1(FGAUTONM)  DEFAULT FLAGS                      81167
         SPACE 1                                                 84275
FLAG2    DC    X'00'         SECOND FLAG BYTE                    81167
FG2ONE   EQU   X'80'           FIRST TIME SWITCH                 81167
FG2TWO   EQU   X'40'           SECOND TIME SWITCH                81167
FG2STACK EQU   X'20'           STACKED ASSEMBLIES/MEMBER         84261
FG2NOHED EQU   X'10'           BYPASS SEPARATOR PAGES            81167
FGNOSEP  EQU   X'08'           BYPASS PRINT SEPARATOR PAGE       81167
         SPACE 1                                                 84275
FLAG3    DC    X'20'         MORE PROCESSING BITS                84275
FGLOTS   EQU   X'80'           PRINT EXCRUCIATING DETAILS        84275
FGSOME   EQU   X'40'           PRINT MOST MESSAGES               84275
FGLLA    EQU   X'20'           AFTER LKED<5, REFRESH W/LLACOPY  GP97289
FGAUTH   EQU   X'10'           RUNNING AUTHORIZED               GP99053
FGNOINC  EQU   X'02'           NO AUTOMATIC INCLUDE SYSLMOD()    89282
FGDEBUG  EQU   X'01'           DEBUG MODE                        84275
         SPACE 1                                                 84275
FLAG4    DC    X'00'         FOURTH FLAG BYTE                   GP04065
FGSVCAD  EQU   X'08'           STOW SYSADATA MEMBER NAME        GP04065
FGSVCPR  EQU   X'04'           STOW SYSPRINT MEMBER NAME         81167
FGSVCPU  EQU   X'02'           STOW SYSPUNCH MEMBER NAME         81167
FGSVCOB  EQU   X'01'           STOW SYSLIN   MEMBER NAME        GP97241
         SPACE 1                                                 84275
LKFLAG   DC    X'00'         LKED/LOADMAPS FLAG                  84275
LKFGLKED EQU   X'80'           INVOKE LINKAGE EDITOR AFTER ASM   84275
LKFGNAME EQU   X'40'           REPRODUCE NAME/ENTRY/ETC.         84275
LKFGALII EQU   X'20'           REPRODUCE SAME ALIAS NAMES        84275
LKFGSSI  EQU   X'10'           REPRODUCE SSI (UNLESS DATE)       84275
LKFGINS  EQU   X'08'           PRODUCE OVERLAY/ORDER CARDS       84275
LKFGPARM EQU   X'04'           SET THE LINK-EDIT PARMS           84275
LKFGNEW  EQU   X'02'           CONTINUE IF MEMBER IS NOT FOUND   87074
LKFGLMOD EQU   X'01'           BLDL/FIND FROM SYSLMOD, NOT SYSLIB
         SPACE 1                                                 81169
         PRINT NOGEN
JFCB     DS    0D
         IEFJFCBN ,
         SPACE 1                                                 81169
MAILBAG  DC    A(POSTCARD-80,80,POSTCARD-80,CARDPOST)            81169
         SPACE 2                                                 87081
DYNRBPTR DC    A(X'80000000'+DYNRB)                             GP02251
DYNRB    DC    AL1(20,S99VRBAL)                                  87081
         DC    AL2(0,0,0)                                        87081
DYNTXTPP DC    AL4(*-*)                                          87081
         DC    AL4(0,0)                                          87081
S99RC    DC    F'0'                                              87081
TUDDN    DC    AL2(DALRTDDN,1)    DDNAME (RETURNED)              87081
TUDDNLEN DC    AL2(8)                                            87081
TVDDN    DC    CL8' '                                            87081
TUDSN    DC    AL2(DALDSNAM,1)    DSNAME                         87081
TUDSNLEN DC    AL2(44)                                           87081
TVDSN    DC    CL44' '                                           87081
TUVOL    DC    AL2(DALVLSER,1)  VOL=SER=                         87081
         DC    AL2(6)                                            87081
TVVOL    DC    CL6' '                                            87081
TUDIS    DC    AL2(DALSTATS,1,1)  DISP                           87081
TVDISP   DC    X'08'         DISP=SHR                            87081
TUUNT    DC    AL2(DALUNIT,1)     UNIT                           87081
TUUNTLEN DC    AL2(4)                                            87081
TVUNT    DC    CL4' '                                            87081
         SPACE 1                                                 87081
ALLOFLAG DC    X'00'         SYSLMOD DYNALLOC FLAG               87081
ALFREQ   EQU   X'80'           USER REQUESTED DYNAMIC ALLOCATION 87081
ALFNEED  EQU   X'40'           NEED DYNALLOC FOR THIS LINK-EDIT  87081
ALFHAVE  EQU   X'01'           NEED TO UNALLOCATE                87081
         SPACE 1                                                 88329
ASMPGTYP DC    C'A'          A - ASM; F - FORT; C - COBOL        88329
         SPACE 1                                                 87081
TEXTNEW  DC    A(TUDSN,TUVOL,TUDIS)    HAD TUUNT                GP09274
TEXTOLD  DC    A(X'80000000'+TUDDN)                             GP02251
         SPACE 1                                                GP02243
         AIF   (NOT &MVSESA).NOLLA3                             GP05019
MYLLA    LLACOPY MF=(L,MYLLAPRM,0F)                             GP02243
         ORG   MYLLA         SAVE A BIT                         GP02243
.NOLLA3  DC    0F'0'                                             84275
READBFR  DC    XL256'00'           ALSO TRT AND TR TABLE         84275
         DC    3XL256'00'    EXTRA FOR SCATTER/TRANSLATION READS 84275
TABLLN   EQU   256           LENGTH OF TR AND TRT TABLES         84275
         ORG   ,                                                GP02243
         SPACE 1                                                 87081
POSTCARD DC    128CL80' '    POST-ASSEMBLY CONTROL CARDS         81169
CARDPOST DC    CL80' '         LAST CARD                         81169
         SPACE 1                                                 87081
*        LAST ENTRY IN SAVE AREA (FUNNY ADDRESSING)              87081
         IKJEFFDF ,          EXPAND IN-LINE                      87081
SAVEEND  EQU   *                                                GP02251
         SPACE 2
CARDSECT DSECT ,             DECLARE CARD IMAGE ON INPUT         81167
CARD     DS    CL80          INPUT CARD                          81167
         SPACE 1                                                 81167
         DCBD  DSORG=PS
         SPACE 1                                                 81167
CVT      DSECT ,                                                 81167
         CVT   DSECT=YES                                         81167
         SPACE 1                                                 81169
         IKJTCB ,                                                81169
         SPACE 1                                                 81169
         IEZJSCB ,                                               81169
         SPACE 1                                                 81169
CSCBSECT DSECT ,                                                 81169
         IEECHAIN ,                                              81169
         SPACE 1                                                 81167
         IHACDE ,                                                81167
         SPACE 1                                                 81167
DSCBS    DSECT ,                                                 81167
         IECSDSL1 1          MAP FORMAT 1 DSCB                   81167
         SPACE 1
TIOTSECT DSECT ,                                                GP97206
         IEFTIOT1 ,                                              84275
         SPACE 1                                                 84275
DSECTUCB DSECT ,                                                GP97206
         IEFUCBOB ,                                              84275
         SPACE 1                                                 87081
         IHAPSA ,                                                87081
         SPACE 1                                                 87081
         IEFZB4D0 ,                                              87081
         IEFZB4D2 ,                                              87081
         SPACE 1                                                GP97224
         MVSSMDE ,                                              GP03241
         END   ,
