DFORM    TITLE 'D E F O R M A T  ***  CONVERT FORMAT TO SCRIPT'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         EJECT ,
         PRINT &PRTSOR
*        THIS PROGRAM CONVERTS TEXT WRITTEN FOR THE FORMAT PROGRAM
*        (SEE IBM WASHINGTON SCIENTIFIC CENTER REPORT 320-3501)
*        INTO (WATERLOO) SCRIPT FORMAT.
*
*        NOTE THAT THE CONVERSION IS NOT PERFECT, BUT ALLOWS
*        FOR SATISFACTORY FIX-UP BY SIMPLE POST-PROCESSING.
*
*        INPUT FILE - SYSIN.
*        OUTPUT - SYSPRINT (LISTING); SYSPUNCH - SCRIPT DOCUMENT.
         SPACE 1
DEFORMAT PGMHEAD ZERO12,BASE=R12,PARM=R9,BNDRY=PAGE             GP03027
         MVI   OUTLINE-1,C' '  FOR QUICK BLANKING
         MVC   NPARIN,=CL3'0'
         MVC   COMMENT(4),=C'.CM '  COMMENT PREFIX
         LA    R7,OUTLINE
         ST    R7,@OUTCARD   SAVE ADDRESS
         SERVINIT ,          LOAD THE @SERVICE ROUTINE
         SERVCALL LPALD,=CL8'@INPREAD'
         ST    R0,@INPREAD
         SERVCALL LPALD,=CL8'@PRINTER'
         ST    R0,@PRINTER
         PRTOPEN SYSPRINT,OPT=(NOWTO)
         PRTOPEN SYSPUNCH,DEV=2,OPT=(NOWTO)
         BAL   R9,OUTINIT
         OI    MODEFG,MODE026  DEFAULT - UPPER TO LOWER CASE CONV.
         MVC   OUTLINE(9),=C'.IM ALIGN'  SET STANDARD CONVENTIONS
         BAL   R9,OUTCARD    WRITE IT
         MVC   OUTLINE(8),=C'.TR \ 40'   'FORMAT' SIGNIFICANT BLK
         BAL   R9,OUTCARD
         INPOPEN SYSIN
         CH    R15,=H'4'     OPEN ?
         BNH   PROCEED
         ABEND 2504          NO SYSIN
         SPACE 1
*        INITIAL STATUS - COMMAND MODE
*
PROCEED  INPGET ,            GET A RECORD
         BXH   R15,R15,ENDSYN  NO MORE
         LR    R5,R1         COPY THE CARD ADDRESS (L=80 FORCED)
         USING CARD,R5       DECLARE IT
         PRTF  CARD,CC=NO    PRINT THE INPUT
         MVC   COMCARD,CARD  MAKE A COMMENT
         PRTF  COMMENT,CC=NO,DEV=2  PUNCH A COMMENT CARD
         BLOOK T=LINETAB,R=CARD  LOOK FOR COMMAND LINES
         B     PROCEED       IGNORE IF NOT SPECIAL
         SPACE 1
LINETAB  BTAB  'GO',NEWCOUT,BASE=*  GO (TEXT PROCESSING)
         BTAB  '026',SETUP   DO UPPER CASE CONVERSION
         BTAB  '029',SETUP   DO UPPER CASE CONVERSION
         BTAB  'SPECIAL',SETDOWN  NO CASE CONVERSION
         BTAB  'PARAGRAPH INDENT IS ',SETPARA
         BTAB  *END
         SPACE 1
SETUP    OI    MODEFG,MODE026  SET FOR CASE CONVERSION
         B     PROCEED
SETDOWN  NI    MODEFG,255-MODE026
         B     PROCEED
SETPARA  MVC   NPARIN,0(R5)  SET PARAGRAPH INDENT
         B     PROCEED
         SPACE 1
NEWCOUT  BAL   R9,OUTCARD    CLEAR BUFFER
NEWCARD  INPGET ,            GET A NEW CARD
         BXH   R15,R15,ENDSYN  NO MORE TO DO
         LR    R5,R1         SAVE CARD ADDRESS
         LA    R6,L'CARD-1(,R5)  LAST INPUT BYTE
         PRTF  CARD,CC=NO
         TM    MODEFG,MODE026  ARTIFICIAL LOWER CASE ?
         BZ    *+10          NO
         TR    CARD,LOWTRAN  YES
         TM    MODEFG,AMODE  AS-IS MODE ?
         BNZ   NEWOPERA      YES; KEEP LEADING BLANKS
         BLOOK T=SPECTAB,R=R6  SKIP LEADING BLANKS
         B     NEWWORD       IF NO MATCH, CHECK OPERANDS
NEWOPERA CLC   =C') ',CARD   END OF AS-IS MODE ?
         BNE   NEWOPER       NO
         LA    R5,CARD+2     SKIP OVER
         NI    MODEFG,255-AMODE  RESET AS-IS
NEWOPER  BLOOK T=SPECTAB,R=R6,STRIP=NO  LOOK FOR SPECIAL CHARACTER
NEWOPER2 CR    R5,R6         ALL DONE ?
         BH    CARDEND       YES; END OF CARD
ADDCHAR  C     R7,@OUTCARD   FIRST POSITION ?
         BNE   ADDCHAR2      NO
         CLI   BYTE,C'.'     ADDING A CONTROL WORD INDICATOR ?
         BNE   ADDCHAR2      NO
         MVI   1(R7),X'16'   MAKE BLANK/BACKSPACE/PERIOD
         LA    R7,2(,R7)     THIS IS SHORTER THAN .LI
ADDCHAR2 MVC   0(1,R7),CARD  COPY
         TM    MODEFG,$MODE+#MODE  UPPER-CASE ?                  90023
         BZ    ADDUP         NO                                  90023
         CLI   0(R7),C' '    BLANK ?                             90023
         BE    ADDUP2        YES; DON'T RESET                    90023
         CLI   0(R7),C'a'    ALPAHABETIC ?                       90023
         BL    ADDUP         NO                                  90023
         CLI   0(R7),C'z'    AND LOWER CASE ?                    90023
         BH    ADDUP                                             90023
         OI    0(R7),C' '    UPPER-CASE                          90023
ADDUP    NI    MODEFG,255-#MODE  RESET ONE-TIME FLAG             90023
ADDUP2   TM    MODEFG,MODE026  CASE CONVERSION ?                 90023
         BZ    ADDLOW        NO                                  90023
         CR    R5,R6         END OF CARD ?                       90023
         BNL   ADDUP3        YES; ASSUME BLANK                   90023
         CLI   BYTE+1,C' '   END OF SENTENCE ?                   90023
         BNE   ADDLOW        NO                                  90023
ADDUP3   CLI   BYTE,C'.'     MOST FREQUENT ?                     90023
         BE    ADD1UP        YES                                 90023
         CLI   BYTE,C'?'     QUESTIONABLE ?                      90023
         BE    ADD1UP                                            90023
         CLI   BYTE,C'!'     EXCLAIM ?                           90023
         BNE   ADDLOW        NO                                  90023
ADD1UP   OI    MODEFG,#MODE  SET ONE-TIME TRIGGER                90023
ADDLOW   NI    SCANFG,255-FGWORD
         TM    MODEFG,AMODE  AS-IS ?
         BNZ   ADDCHART      YES
         CLI   BYTE,C' '     BLANK ?
         BNE   ADDCHART      NO
         BLOOK T=NULLTAB,R=R6  SKIP EXTRANEOUS BLANKS
         BCTR  R5,0          FIX UP FOR NEXT INCREMENT
         OI    SCANFG,FGWORD  LOOK FOR WORDS
         CLC   OUTLINE,OUTLINE-1  ALL BLANK ?
         BNE   ADDCHART      NO
         LA    R7,OUTLINE    REPOSITION
         LA    R5,CARD+1     GET NEXT
         B     CARDTEST      CHECK FOR END OF INPUT
ADDCHART LA    R5,CARD+1
         LA    R7,1(,R7)
         CR    R7,R8         FULL ?
         BNH   *+8           NO
CARDEND  BAL   R9,OUTCARD    WRITE OUTPUT
CARDTEST CR    R5,R6         FULL ?
         BH    NEWCOUT       YES; GET ANOTHER CARD
         TM    SCANFG,FGWORD  PRIOR BLANK ?
         BNZ   NEWOPER       YES; RESUME SCAN FOR COMMANDS
NEWWORD  BLOOK T=SPEC2TAB,R=R6,ERR=ADDCHAR,STRIP=NO  SPECIAL BYTE?
         SPACE 1
SPECTAB  BTAB  ')',CMDLOOK,BASE=*  START OF COMMAND WORD ?
SPEC2TAB BTAB  '¢',OPERCENT  CENT OPERAND
         BTAB  '!',OPEREXC   EXCLAMATION POINT OPERAND
         BTAB  *9D,INDXTEST  SUPER RIGHT PARENT - INDEX ?
NULLTAB  BTAB  *END
         SPACE 1
CMDTAB   BTAB  'A',CMDA,BASE=*  COMMAND BYTE TABLE
         BTAB  'C',CMDC
         BTAB  'D',CMDD
         BTAB  'E',CMDE
         BTAB  'F',CMDF
         BTAB  'H',CMDH
         BTAB  'I',CMDI
         BTAB  'J',CMDJ
         BTAB  'K',CMDK
         BTAB  'L',CMDL
         BTAB  'M',CMDM
         BTAB  'P',CMDP
         BTAB  'S',CMDS
         BTAB  'T',CMDT
         BTAB  'U',CMDU
         BTAB  'V',CMDV
         BTAB  'W',CMDW
         BTAB  'a',CMDA         LOWER-CASE MODE                  90022
         BTAB  'c',CMDC                                          90022
         BTAB  'd',CMDD                                          90022
         BTAB  'e',CMDE                                          90022
         BTAB  'f',CMDF                                          90022
         BTAB  'h',CMDH                                          90022
         BTAB  'i',CMDI                                          90022
         BTAB  'j',CMDJ                                          90022
         BTAB  'k',CMDK                                          90022
         BTAB  'l',CMDL                                          90022
         BTAB  'm',CMDM                                          90022
         BTAB  'p',CMDP                                          90022
         BTAB  's',CMDS                                          90022
         BTAB  't',CMDT                                          90022
         BTAB  'u',CMDU                                          90022
         BTAB  'v',CMDV                                          90022
         BTAB  'w',CMDW                                          90022
         BTAB  '¢',CMDCENT
         BTAB  *END
         SPACE 1
CMDLOOK  BLOOK T=CMDTAB,R=R6,STRIP=NO  REALLY CMD ?
         BCTR  R5,0          BACK-UP TO PARENTHESIS
         CLI   BYTE,C')'     IS THIS THE PARENTHESIS ?
         BE    ADDCHAR       YES
         LA    R5,CARD+1     ELSE JUST DO THE FUNNY BYTE
         CLI   0(R5),C' '    BLANK ?                             90023
         BNE   ADDCHAR       NO; PROCESS IT (ERROR?)             90023
         BLOOK T=NULLTAB,R=R6  SKIP REMAINING BLANKS             90023
         OI    SCANFG,FGWORD  LOOK FOR NEXT WORD                 90023
         B     CARDTEST      PROCEED                             90023
         SPACE 1
ADDBACK  BCTR  R5,0          BACK-UP TO PARENTHESIS
         B     ADDCHAR       AND ADD IT
         SPACE 1
*        OPERAND WAS A CENT SIGN. UPPER-CASE NEXT CHARACTER IF
*        A-Z OR 0-9, ELSE BACK-SPACE AND PUT CENT SIGN.
*
OPERCENT TM    MODEFG,MODE026  CASE CONVERSION ?                 90022
         BZ    OPERCENZ      NO; DO AS-IS                        90022
         CLI   BYTE,C'a'     LETTER ?                            90022
         BL    ADDBACK       NO; PUT OUT CENT SIGN               90022
         CLI   BYTE,C'i'     FIRST RANGE ?                       90022
         BNH   UPPCASE       YES                                 90022
         CLI   BYTE,C'j'     SECOND RANGE ?                      90022
         BL    ADDBACK       NO                                  90022
         CLI   BYTE,C'r'                                         90022
         BNH   UPPCASE       YES                                 90022
         CLI   BYTE,C's'     THIRD RANGE ?                       90022
         BL    ADDBACK       NO                                  90022
         CLI   BYTE,C'z'                                         90022
         BNH   UPPCASE       YES                                 90022
         B     OPERCENU                                          90022
OPERCENZ CLI   BYTE,C'A'     LETTER ?
         BL    ADDBACK       NO; PUT OUT CENT SIGN
         CLI   BYTE,C'I'     FIRST RANGE ?
         BNH   UPPCASE       YES
         CLI   BYTE,C'J'     SECOND RANGE ?
         BL    ADDBACK       NO
         CLI   BYTE,C'R'
         BNH   UPPCASE       YES
         CLI   BYTE,C'S'     THIRD RANGE ?
         BL    ADDBACK       NO
         CLI   BYTE,C'Z'
         BNH   UPPCASE       YES
OPERCENU CLI   BYTE,C'0'     NUMERIC ?
         BL    ADDBACK
         CLI   BYTE,C'9'
         BH    ADDBACK
UPPCASE  XI    BYTE,X'40'    CHANGE CASE
         B     ADDCHAR       PUT CURRENT BYTE OUT
         SPACE 1
*        EXCLAMATION POINT - IF FOLLOWED BY TWO DIGITS IN 10-51
*        RANGE, REPLACE BY SINGLE BYTE; ELSE PUT EXCLAMATION.
*
OPEREXC  CLI   BYTE,C'1'     NUMERIC OPERAND ?
         BL    ADDBACK       NO; EXCLAMATION
         CLI   BYTE,C'5'     IN 10-51 RANGE ?
         BH    ADDBACK
         CLI   BYTE+1,C'0'   SECOND BYTE ?
         BL    ADDBACK
         CLI   BYTE+1,C'9'
         BH    ADDBACK
         SLR   R14,R14
         SLR   R15,R15
         ICM   R15,3,BYTE    LOAD BYTES
         STM   R14,R15,DB+8  STASH IN WORK AREA
         PACK  DB,DB+8(8)    PACK TO DECIMAL
         CVB   R1,DB         GET NUMERICS
         CH    R1,=H'51'     NOT TOO LARGE ?
         BH    ADDBACK       TOO BAD
         LA    R5,CARD+1     SKIP OVER FIRST DIGIT
         LA    R1,SPECHARS-10(R1)  POINT TO SPECIAL BYTE
         MVC   BYTE,0(R1)    REPLACE IT
         CLI   BYTE,X'9D'    SUBSCRIPT RIGHT PARENTHESIS ?
         BNE   ADDCHAR       NO
         B     INDXCHEK
INDXTEST BCTR  R5,0          BACK-UP TO X'9D'
INDXCHEK CLI   BYTE+1,X'E0'  SIGNIFICANT BLANK ?
         BE    ADDCHAR       YES; JUST COPY
         CLI   BYTE+1,X'81'  TEXT FOLLOWS ?
         BNL   INDEXER
         CLI   BYTE+1,C'@'   TEXT ?
         BE    INDEXER
         CLI   BYTE+1,C'#'
         BE    INDEXER
         CLI   BYTE+1,C'$'
         BNE   ADDCHAR       NO; JUST PROCESS
INDEXER  BAL   R9,OUTCARD    FLUSH
         LA    R1,OUTLINE+5  START OF WORD
         LA    R5,BYTE+1     SKIP SPECIAL BYTE
         LR    R15,R5        COPY
         LA    R14,1(,R6)
         SR    R14,R5        NUMBER TO COPY
         BNP   NEWOPER
         MVC   OUTLINE(5),=C'.IX '''
INDXLOOP CLI   BYTE-BYTE(R15),X'E0'    SIGNIFICANT BLANK ?
         BE    INDXEND       YES; QUIT
         CLI   BYTE-BYTE(R15),X'81'    LETTER/NUMBER ?
         BNL   INDXCOPY      YES
         CLI   BYTE-BYTE(R15),C'@'     TEXT ?
         BE    INDXCOPY
         CLI   BYTE-BYTE(R15),C'#'
         BE    INDXCOPY
         CLI   BYTE-BYTE(R15),C'$'
         BNE   INDXEND       NO; JUST PROCESS
INDXCOPY MVC   0(1,R1),0(R15)  MOVE ONE
         LA    R1,1(,R1)
         LA    R15,1(,R15)
         BCT   R14,INDXLOOP
INDXEND  MVI   0(R1),C''''   MAKE CLOSING QUOTE
         BAL   R9,OUTCARD    MAKE THE INDEX REQUEST
         B     NEWWORD       LOOK FOR OPERAND
SPECHARS DC    X'8D9D8EA08B9BADBD8CAE9EBEABBBACBC8FBF9C9FAF'
         DC    X'A1504F5F4C7E6E4E4D5D7F7D4A7B6C7C6D5E7A6F5A'
         SPACE 1
*        SPECIAL COMMANDS
*
CMDA     BAL   R9,OUTCARD    CLEAR THE OUTPUT BUFFER
         LA    R1,=C'.FO OFF'  ENTER AS-IS MODE
         TM    MODEFG,AMODE  AS-IS ON ?
         BNZ   *+8           YES
         LA    R1,=C'.FO ON '
         XI    MODEFG,AMODE  RESET AS-IS MODE
         B     CMDSEVEN      GO FOR MORE
         SPACE 1
CMDC     BAL   R9,OUTCARD    FLUSH
         MVC   OUTLINE(3),=C'.CB'  TEXT COLUMN
         B     CMDFLUSH
         SPACE 1
         SPACE 1
CMDE     BAL   R9,OUTCARD
DOBREAK  MVC   OUTLINE(3),=C'.BR'  TAKE A BREAK
         B     CMDFLUSH
         SPACE 1
*CMDF    BAL   R9,OUTCARD
*        XI    MODEFG,FMODE  CHANGE CAPITALIZATION MODE
*        LA    R1,=C'.UC OFF'
*        TM    MODEFG,FMODE  IS IT OFF NOW ?
*        BZ    *+8           YES
*        LA    R1,=C'.UC ON '
*        B     CMDSEVEN
CMDF     B     CMDCENT       TREAT AS FULL UPPER-CASE            90025
         SPACE 1
CMDH     BAL   R9,OUTCARD
         CLI   BYTE,C'0'     NUMERIC OPERAND ?
         BL    CMDHBAK       NO; PRESERVE IT FOR LATER
         BE    CMDHOFF       ZERO - RESET
         CLI   BYTE,C'9'     NOT TOO LARGE ?
         BH    CMDHBAK
         CLC   NHANG,BYTE    SAME AS BEFORE ?
         BE    CMDHOFF       YES; KILL IT
         MVC   NHANG,BYTE    ELSE SAVE IT
         SLR   R1,R1
         IC    R1,BYTE
         N     R1,=F'15'     MASK ALL BUT LOWEST NIBBLE
         MH    R1,=H'5'      DEFAULT
         CVD   R1,DB
         OI    DB+7,X'0F'    MAKE PLUS, POSITIVELY
         UNPK  OUTLINE+3(3),DB+6(2)  UNPACK
         MVC   OUTLINE(4),=C'.HI 0'  SET HANGING INDENT
         LA    R5,CARD+1     SKIP COUNTER
         B     CMDFLUSH      GO TO FLUSH COMMAND, GET NEXT COMMAND
CMDHBAK  BCTR  R5,0          REPROCESS THIS BYTE
CMDHOFF  MVC   OUTLINE(5),=C'.HI 0'
         MVI   NHANG,C'0'    RESET HANG FLAG
         LA    R5,CARD+1     SKIP THE OPERAND
         B     CMDFLUSH
         SPACE 1
CMDI     BAL   R9,OUTCARD
         CLI   BYTE,C'0'     NUMERIC OPERAND ?
         BL    CMDIBAK       NO; PRESERVE IT FOR LATER
         BE    CMDIOFF       ZERO - RESET
         CLI   BYTE,C'9'     NOT TOO LARGE ?
         BH    CMDIBAK
         CLC   NINDN,BYTE    SAME AS BEFORE ?
         BE    CMDIOFF       YES; KILL IT
         MVC   NINDN,BYTE    ELSE SAVE IT
         SLR   R1,R1
         IC    R1,BYTE
         N     R1,=F'15'     MASK ALL BUT LOWEST NIBBLE
         MH    R1,=H'5'      DEFAULT
         CVD   R1,DB
         OI    DB+7,X'0F'    MAKE PLUS, POSITIVELY
         UNPK  OUTLINE+3(3),DB+6(2)  UNPACK
         MVC   OUTLINE(4),=C'.IN 0'  SET INDENT
         LA    R5,CARD+1     SKIP COUNTER
         B     CMDFLUSH      GO TO FLUSH COMMAND, GET NEXT COMMAND
CMDIBAK  BCTR  R5,0          REPROCESS THIS BYTE
CMDIOFF  MVC   OUTLINE(5),=C'.IN 0'
         MVI   NINDN,C'0'    RESET HANG FLAG
         LA    R5,CARD+1     SKIP THE OPERAND
         B     CMDFLUSH
         SPACE 1
CMDJ     BAL   R9,OUTCARD
         LA    R1,1          SET FOR ONE UNCONDITIONAL SPACE
         MVC   OUTLINE(3),=C'.SP'
         B     CMDJL         JOIN J/L COMMON PROCESSING
         SPACE 1
CMDK     BAL   R9,OUTCARD
         XI    MODEFG,KMODE  FLIP KEEP MODE
         LA    R1,=C'.CP OFF'
         TM    MODEFG,KMODE  IS IT OFF ?
         BZ    *+8           YES
         LA    R1,=C'.CP ON '
         MVC   OUTLINE(7),0(R1)
         B     CMDFLUSH
         SPACE 1
CMDL     BAL   R9,OUTCARD
         MVC   OUTLINE(3),=C'.SK'  CONDITIONAL LINE ADVANCE
         LA    R1,1
CMDJL    CLI   BYTE,C'L'     ANOTHER L ?
         BE    CMDJLUP       YES
         CLI   BYTE,C'J'     ANOTHER J ?
         BE    CMDJLUP       YES
         CLI   BYTE,C'l'     ANOTHER L ?                         90022
         BE    CMDJLUP       YES                                 90022
         CLI   BYTE,C'j'     ANOTHER J ?                         90022
         BE    CMDJLUP       YES                                 90022
CMDJLEND BCT   R1,*+8        DECREASE COUNT BY ONE
         B     DOBREAK       )L IS LINE BREAK ONLY
         CVD   R1,DB         PACK IT
         OI    DB+7,X'0F'    MAKE PLUS, POSITIVELY
         UNPK  OUTLINE+3(3),DB+6(2)  MAKE EBCDIC
         MVI   OUTLINE+3,C' '  KILL LEADING ZERO
         B     CMDFLUSH
CMDJLUP  LA    R1,1(,R1)     INCREASE SPACE COUNT
         LA    R5,CARD+1     BUMP
         CR    R5,R6
         BNH   CMDJL         KEEP COUNTING
         B     CMDJLEND      ELSE FORCE OUT
         SPACE 1
CMDM     BAL   R9,OUTCARD    FLUSH
         XI    MODEFG,MMODE  FLIP CENTER MODE
         LA    R1,=CL7'.CE ON'
         TM    MODEFG,MMODE
         BO    *+8
         LA    R1,=CL7'.CE OFF'
         B     CMDSEVEN      ISSUE
         SPACE 1
CMDP     BAL   R9,OUTCARD
         NI    MODEFG,255-$MODE-FMODE-MMODE-UMODE
         TM    MODEFG,MODE026  AUTO CAP ?                        90023
         BZ    *+8           NO                                  90023
         OI    MODEFG,#MODE  CAPITALIZE NEXT CHARACTER           90023
         MVC   OUTLINE(17),=CL17'.SK 2;.CC 2;.LI +'
         MVC   OUTLINE+17(L'NPARIN),NPARIN  PARAGRAPH INDENT
         B     CMDFLUSH
         SPACE 1
CMDS     BAL   R9,OUTCARD
         NI    MODEFG,255-$MODE-FMODE-MMODE-UMODE
         TM    MODEFG,MODE026  AUTO CAP ?                        90023
         BZ    *+8           NO                                  90023
         OI    MODEFG,#MODE  CAPITALIZE NEXT CHARACTER           90023
         CLI   NHANG,C'0'    ANY HANGING INDENT ?
         BNH   *+10          NO
         MVC   OUTLINE+7(6),=C';.HI 0'
         CLI   NINDN,C'0'    REGULAR INDENT ?
         BNH   *+10          NO
         MVC   OUTLINE+13(6),=C';.IN 0'
         MVI   NHANG,0
         MVI   NINDN,0
         LA    R1,=CL7'.PA'
         B     CMDSEVEN
         SPACE 1
CMDD     DS    0H            NO .....TAB OPTION
CMDT     MVI   0(R7),X'05'   MAKE A TAB
         LA    R7,1(,R7)
         B     CMDLOOK       LOOK FOR MORE
         SPACE 1
*CMDU    BAL   R9,OUTCARD
*        XI    MODEFG,UMODE
*        LA    R1,=CL7'.UL ON '
*        TM    MODEFG,UMODE  ON ?
*        BNZ   *+8
*        LA    R1,=CL7'.UL OFF'
*        B     CMDSEVEN
CMDU     BAL   R9,OUTCARD    FORCE A BREAK                       90025
         XI    MODEFG,UMODE  FLIP UNDERLINE MODE                 90025
         TM    MODEFG,UMODE  IS IT ON NOW ?                      90025
         BZ    CMDLOOK       NO; LOOK FOR ANOTHER COMMAND        90025
         LA    R1,=CL7'.UL 1  '  UNDERSCORE NEXT DATA LINE       90025
         B     CMDSEVEN                                          90025
         SPACE 1
CMDV     BAL   R9,OUTCARD
         NI    MODEFG,255-$MODE-FMODE-#MODE                      90023
         B     PROCEED       GO TO COMMAND SCAN
         SPACE 1
CMDW     BAL   R9,OUTCARD
         SLR   R14,R14
         LA    R15,C'0'
CMDWTEST CLI   BYTE,C'0'     VALID ?
         BL    CMDWEXP
         CLI   BYTE,C'9'
         BH    CMDWEXP
         SLDL  R14,8
         IC    R15,BYTE
         LA    R5,CARD+1
         B     CMDWTEST
CMDWEXP  STM   R14,R15,DB+8
         PACK  DB,DB+8(8)
         UNPK  OUTLINE+3(3),DB+6(2)
         OI    OUTLINE+5,C'0'
         MVC   OUTLINE(4),=C'.CC '
         B     CMDFLUSH
         SPACE 1
CMDCENT  XI    MODEFG,$MODE
         B     CMDLOOK       LOOK FOR MORE                       90023
         SPACE 1
CMDSEVEN MVC   OUTLINE(7),0(R1)  MAKE COMMAND
CMDFLUSH BAL   R9,OUTCARD    WRITE COMMAND CARD
         B     CMDLOOK       SEE IF ANOTHER COMMAND BYTE
         SPACE 1
OUTCARD  CLC   OUTLINE,OUTLINE-1  ANYTHING TO PUNCH ?
         BE    OUTINIT       NO; JUST RE-INITIALIZE
         PRTF  OUTLINE,CC=NO,DEV=2   PUNCH IT
         PRTLIST OUTFD       PRINT FOR DEBUGGING
OUTINIT  LA    R7,OUTLINE
         LA    R8,OUTLINE+80-1  END OF OUTPUT
         MVC   OUTLINE,OUTLINE-1  CLEAR IT
         BR    R9            RETURN
         SPACE 1
ENDSYN   SERVTERM ,          CLOSE AND FREE
         PGMEXIT RC=0        RETURN                             GP03027
         SPACE 1
SYSPRINT PRTWORK SYSPRINT,TITLE=3
SYSPUNCH PUNWORK SYSPUNCH,SYSUT2
SYSIN    INPWORK SYSIN,SYSUT1,FILL=C' ',EDIT=128
         SPACE 1
OUTFD    FD    '----->',NL
         FD    OUTLINE
         FD    '<-----'
         FD    *END
         SPACE 1
LOWTRAN  DC    256AL1(*-LOWTRAN)
         ORG   LOWTRAN+C'A'
         DC    X'818283848586878889'
         ORG   LOWTRAN+C'J'
         DC    X'919293949596979899'
         ORG   LOWTRAN+C'S'
         DC    X'A2A3A4A5A6A7A8A9'
         ORG   ,
         SPACE 1
CARDSECT DSECT ,
CARD     DS    0CL80         INPUT RECORD
BYTE     DS    C             REDEFINED
         SPACE 1
SAVE     DSECT ,             SAVE/WORK AREA
@SERVICE DS    A
@INPREAD DS    A
@PRINTER DS    A
@OUTCARD DS    A
DB       DS    2D            WORK AREA
SCANFG   DS    X             SCANNING CONTROL
FGWORD   EQU   X'80'           SCAN FOR WORDS )XXX
MODEFG   DS    X             MODE (STATE) FLAGS
AMODE    EQU   X'80'           AS-IS PROCESSING
FMODE    EQU   X'40'           BEGIN CAPITALIZATION (LEADING)
KMODE    EQU   X'20'           KEEP MODE
MMODE    EQU   X'10'           CENTER
UMODE    EQU   X'08'           UNDERLINE
$MODE    EQU   X'04'           UPPER-CASE MODE
#MODE    EQU   X'02'           UPCASE NEXT CHARACTER             90023
MODE026  EQU   X'01'           MAKE UPPER INTO LOWER CASE
NHANG    DS    C             HANGING INDENT SUBSCRIPT
NINDN    DS    C             NORMAL INDENT SUBSCRIPT
NPARIN   DC    CL3'0  '      PARAGRAPH INDENT
         DS    C' '          OUTLINE-1 BLANK
OUTLINE  DS    CL80          OUTPUT WORK AREA
COMMENT  DS    0CL80,C'.CM '
COMCARD  DS    CL76
SAVEEND  EQU   *                                                GP03027
         SPACE 1
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         SPACE 1
         END   ,
