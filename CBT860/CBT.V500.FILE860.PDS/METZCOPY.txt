CPY      TITLE 'COPY DATA SET WITH MINIMAL JCL'
         MTITL 'COPY DATA SET WITH MINIMAL JCL'                  *TSM*
         PRINT NOGEN                                             80050
METZCOPY PGMHEAD ZERO12,BASE=R12,AM=24,RM=24 *O*L*D* CODE        90280
         EXTRN @DCBXLST      (IN @DCBEXIT)                       90280
         LA    R0,SYSUT1
         LA    R1,SYSUT2                                         *TSM*
         STM   R0,R1,OPL1                                        *TSM*
         OC    OPL1(MODEND-MODOPL),MODOPL                        *TSM*
         LA    R0,INFMJFCB                                       80050
         ST@   R0,XLIST+4,MVI=X'87'  SET JFCB ENTRY              90280
         LA    R0,XLIST                 POINT AT EXIT LIST       80050
         ST@   R0,DCBEXLST-IHADCB+SYSUT2                         90280
       DEVTYPE DCBDDNAM-IHADCB+SYSUT2,DEVTYPE                    80050
         BXH   R15,R15,BADUT2                                    90280
        RDJFCB MF=(E,OPL2)                                       80050
         CLI   DEVTYPE+2,UCB3DACC       DIRECT ACCESS DEVICE ?   80050
         BNE   NOTPO                     NO - DON'T NEED OBTAIN  80050
         LA    R14,JFCBDSNM             FILL IN CAMLST           80050
         LA    R15,JFCBVOLS                                      80050
         LA    R0,DS1FMTID                                       80050
         STM   R14,R0,OBTLIST+4                                  80050
        OBTAIN OBTLIST                                           80050
         BXH   R15,R15,BADUT2                                    90280
         TM    DS1DSORG,JFCORGPO        IS THIS A PDS ?          80050
         BZ    NOTPO                     NO  - DON'T NEED MEMBER 80050
         TM    JFCBIND1,JFCPDS          MEMBER SPECIFIED ?       80050
         BO    NOTPO                     YES - NO SWEAT          80050
         MVC   JFCBELNM,DOWTO+8+12      MEMBER SPECIFIED NOW !   90280
         OI    JFCBIND1,JFCPDS                                   80050
DOWTO    WTO   'MEMBER NAME TEMPNAME ASSUMED',ROUTCDE=11,DESC=7  90289
* TSM :  WHY USE A NAME THAT CAN'T BE SCRATCHED WITH IEHPROGM ?  90280
NOTPO    OPEN  MF=(E,OPL1)                                       80050
         TM    DCBOFLGS+SYSUT1-IHADCB,DCBOFOPN                   90280
         BZ    BADUT1                                            90280
         OPEN  MF=(E,OPL2),TYPE=J                                80050
         TM    DCBOFLGS+SYSUT2-IHADCB,DCBOFOPN                   90280
         BZ    BADUT2                                            90280
         TM    SYSUT2+DCBRECFM-IHADCB,DCBRECCM
*                                       IS AN INITIAL EJECT NEEDED ?
         BZ    LOOP                      NO    - NOT RECFM=M
         GET   SYSUT1                    MAYBE - INSPECT 1ST RECORD
         LR    R2,R1
         TM    DCBRECFM+SYSUT2-IHADCB,DCBRECF
         BO    *+8                      IF F OR U, R1 IS OK
         LA    R1,4(,R1)                SKIP PREFIX IF RECFM=V|RECFM=D
         TM    0(R1),X'80'              ONLY  CARRIAGE CONTROL ?
         BO    PUT                       YES - NO DUMMY RECORD
         LA    R0,VHEAD
         TM    DCBRECFM+SYSUT1-IHADCB,DCBRECF
         BZ    DUMPUT                    V OR D
         LA    R0,LINE
         TM    DCBRECFM+SYSUT2-IHADCB,DCBRECU                    78271
         BNO   DUMPUT                    F - ONLY U IS SPECIAL   *TSM*
         LA    R15,133                   USE LRECL=133 IF OK     *TSM*
         CH    R15,DCBBLKSI+SYSUT2-IHADCB                        *TSM*
         BNH   *+8                                               78271
         LH    R15,DCBBLKSI+SYSUT2-IHADCB                        *TSM*
         STH   R15,DCBLRECL+SYSUT2-IHADCB                        78271
DUMPUT   PUT   SYSUT2
         LR    R1,R2                    RESTORE 1ST RECORD TO R0
         B     PUT
         SPACE 1
LOOP     GET   SYSUT1
PUT      TM    DCBRECFM+SYSUT1-IHADCB,DCBRECU                    *TSM*
         BNO   PUTNOTU                   F, V, OR D              *TSM*
         MVC   DCBLRECL+SYSUT2-IHADCB,DCBLRECL+SYSUT1-IHADCB     *TSM*
PUTNOTU  LR    R0,R1                                             *TSM*
         PUT   SYSUT2
         B     LOOP
         SPACE 2
CLOSE    CLI   INTRDRFL,0               INTERNAL READER ?        78271
         BE    NOTINTCL                  NO  - CLOSE NORMALLY    78271
         LA    R0,VEOF                  ASSUME RECFM=V           78271
         TM    DCBRECFM+SYSUT2-IHADCB,DCBRECF                    78271
         BZ    EOFPUT                   V OR D                   78271
         LA    R0,INTEOF                U OR F                   78271
         TM    DCBRECFM+SYSUT2-IHADCB,DCBRECU                    78271
         BNO   EOFPUT                   F - ONLY U IS SPECIAL    78271
         LA    R15,80                   USE LRECL=80 IF OK       78271
         CH    R15,DCBBLKSI+SYSUT2-IHADCB                        78271
         BNH   *+8                                               78271
         LH    R15,DCBBLKSI+SYSUT2-IHADCB                        78271
         STH   R15,DCBLRECL+SYSUT2-IHADCB                        78271
EOFPUT   PUT   SYSUT2                   WRITE /*EOF TO INT. READER78271
         SPACE 1                                                 78271
NOTINTCL NI    OPL1,X'7F'                                        78271
         CLOSE (,DISP,,DISP),MF=(E,OPL1)
      FREEPOOL SYSUT1
      FREEPOOL SYSUT2
         PGMEXIT RC=0
         SPACE 1                                                 90280
BADUT1   WTO   'UNABLE TO OPEN SYSUT1',ROUTCDE=11,DESC=7         90280
         ABEND 101                                               90280
BADUT2   WTO   'UNABLE TO OPEN SYSUT2',ROUTCDE=11,DESC=7         90280
         ABEND 102                                               90280
         STITL 'DCB EXIT FOR SYSUT2'                             *TSM*
*              SINCE THE DCBDEVT FIELD IS NOT FILLED IN BY OPEN  *TSM*
*              PRIOR TO CALLING THE DCBEXIT FOR TAPE FILES,      *TSM*
*              IT IS NECESSARY TO ACCESS THE UCB DIRECTLY.       *TSM*
         USING DCBFILL,R15                                       90280
         USING IHADCB,R1
DCBFILL  L     R3,CVTPTR                                         *TSM*
         USING CVTMAP,R3                                         *TSM*
         L     R3,CVTTCBP                                        *TSM*
         L     R3,4(,R3)                                         *TSM*
         USING TCB,R3                                            *TSM*
         L     R3,TCBTIO                                         *TSM*
         USING TIOT,R3                                           *TSM*
         SLR   R4,R4                                             93051
         ICM   R4,3,DCBTIOT                                      93051
         AR    R3,R4         GET TIOT ENTRY                      93051
         USING TIOELNGH,R3                                       *TSM*
         LAT   R4,TIOEFSRT-1,LA=0  LOAD 24-BIT ADDRESS           90280
         USING UCBOB,R4                                          91329
         CLI   DCBRECFM,0               RECFM DEFAULTED ?        *TSM*
         BNZ   *+10                      NO - LET IT BE
         MVC   DCBRECFM,DCBRECFM-IHADCB+SYSUT1
         OC    DCBLRECL,DCBLRECL        LRECL DEFAULTED ?
         BNZ   *+10                      NO - LET IT BE
         MVC   DCBLRECL,DCBLRECL-IHADCB+SYSUT1
         OC    DCBBLKSI,DCBBLKSI        BLKSIZE DEFAULTED ?
         BNZ   *+10                      NO - LET IT BE
         MVC   DCBBLKSI,DCBBLKSI-IHADCB+SYSUT1
         CLI   DCBDEVT,X'4F'            DA(*) (TERM=TS)?         *TSM*
         BE    TSOTERM                   YES - TREAT AS UR       *TSM*
         LTR   R4,R4         IS THERE A UCB ?                    *TSM*
         BZR   R14                       NO - LEAVE ALONE        *TSM*
         CLI   UCBTBYT3,UCB3UREC        UNIT-RECORD DEVICE ?     78271
         BNE   NOTINTRD                  NO  - NOT INT. READER   78271
         MVI   DCBBUFNO,1                                        78271
         CLI   DCBDEVT,DCBDVCPR         2520 READER-PUNCH ?      78271
         BNE   NOTINTRD                  NO  - NOT INT. READER   78271
         MVI   INTRDRFL-SYSUT2(R1),1     YES - SET INT. RDR FLAG 78271
NOTINTRD CLI   DCBRECFM,DCBRECV+DCBRECSB                         78271
         BNE   NOTVS
         TM    UCBTBYT3,UCB3TAPE        IS IT TAPE WITH RECFM=VS?*TSM*
         BOR   R14                       YES - MAY BE IEBCOPY    *TSM*
NOTVS    TM    DCBRECFM,DCBRECU         RECFM=U ?                *TSM*
         BOR   R14                       YES - LEAVE UNBLOCKED   *TSM*
         OI    DCBRECFM,DCBRECBR        FORCE BLOCKED            *TSM*
         TM    UCBTBYT3,UCB3UREC        UNIT RECORD ?            *TSM*
         BZR   R14
TSOTERM  NI    DCBRECFM,255-DCBRECBR    TURN OFF BLOCKED         *TSM*
         LH    R2,DCBLRECL
         TM    DCBRECFM,DCBRECF
         BO    *+8
         LA    R2,4(,R2)
         STH   R2,DCBBLKSI
         BR    R14
         SPACE 2
VHEAD    DC    H'6',H'0'
LINE     DC    X'88',CL132' '
         SPACE 1                                                 78271
VEOF     DC    H'80',H'0'                                        78271
INTEOF   DC    CL80'/*EOF'                                       78271
         LTORG
         SPACE 2
MODOPL   OPEN  (0,(INPUT,REREAD)),MF=L                           90280
         OPEN  (0,OUTPUT),MF=L                                   *TSM*
MODUT1   DCB   DDNAME=SYSUT1,MACRF=GL,DSORG=PS,EODAD=CLOSE,      78271 *
               EXLST=@DCBXLST                                    90280
MODUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,                  80050 *
               EXLST=MODXLIST                                    80050
         SPACE 1                                                 78271
MODOBT  CAMLST SEARCH,0,0,0                                      80050
         SPACE 1                                                 80050
MODXLIST DC    0A(0),X'05',AL3(DCBFILL)                          80050
         SPACE 1                                                 80050
MODEND   EQU   *
         STITL 'WORK AREA'                                       *TSM*
SAVE     DSECT
         PRINT GEN                                               80050
OPL1     OPEN  (SYSUT1,INPUT),MF=L
OPL2     OPEN  (SYSUT2,OUTPUT),MF=L
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GL,DSORG=PS,EODAD=CLOSE
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,EXLST=XLIST
         SPACE 1                                                 78271
OBTLIST CAMLST SEARCH,JFCBDSNM,JFCBVOLS,DS1FMTID                 80050
         SPACE 1                                                 78271
XLIST    DC    0A(0),X'05',AL3(DCBFILL)                          80050
         DC    0A(0),X'87',AL3(INFMJFCB)                         80050
         SPACE 1                                                 78271
DEVTYPE  DS    D                                                 80050
         SPACE 1                                                 78271
INTRDRFL DC    X'0'                     SET IF INTERNAL READER   78271
         STITL 'WORK AREA - JFCB'                                80050
      IEFJFCBN LIST=YES                                          80050
         STITL 'WORK AREA - DSCB 1'                              80050
      IECSDSL1 1                                                 80050
         DS    XL5                     CCHHR OF DSCB             80050
         ORG   DS1FMTID+140            "140 BYTE WORKAREA"       80050
SAVEEND  EQU   *
         STITL 'DCB DSECT'                                       *TSM*
         PRINT NOGEN                                             *TSM*
         DCBD  DSORG=PS,DEVD=PC                                  78271
         STITL 'CVT DSECT'                                       *TSM*
         CVT   DSECT=YES,PREFIX=YES                              *TSM*
         STITL 'TCB DSECT'                                       *TSM*
        IKJTCB ,                                                 *TSM*
         STITL 'TIOT DSECT'                                      *TSM*
TIOT     DSECT ,                                                 *TSM*
      IEFTIOT1 ,                                                 *TSM*
         STITL 'UCB DSECT'                                       *TSM*
UCB      DSECT ,                                                 *TSM*
      IEFUCBOB ,                                                 *TSM*
         END   METZCOPY
