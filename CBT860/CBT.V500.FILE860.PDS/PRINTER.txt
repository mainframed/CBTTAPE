PRINTER  TITLE 'P R I N T E R  ***  LIST FULL OR SELECTED DATA SET'
 PUNCH ' ORDER PRINTER(P) '  MAKE DUMPS EASIER TO READ
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
***********************************************************************
**                                                                   **
**  PRINTER PRINTS A WHOLE OR PARTIAL SEQUENTIAL DATA SET            **
**    (VARIANT OF LISTER, WITH PRINT ORIENTATION AND TITLE SUPPORT)  **
**                                                                   **
**  SYSUT1 - INPUT DATA SET                                          **
**  SYSUT2 - OUTPUT DATA SET                                         **
**                                                                   **
**  PARM=LIMIT          (E.G., PARM=123 PRODUCES 123 RECORDS)        **
**  PARM='LIMIT,START'  (E.G., PARM='123,5' SKIPS 5, THEN PRINTS 123)**
**  PARM=-"-,LAND       SET LANDSCAPE MODE; WIDTH 132                **
**  PARM=-"-,PORT       SET PORTRAIT MODE; WIDTH 80                  **
**  PARM=-"-,CC         INPUT HAS CARRIAGE CONTROLS                  **
**                                                                   **
***********************************************************************
**  THIS PROGRAM IS INVOKED BY REXX COMMAND FILE LISTOFF IN .CLIST   **
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 2004  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 2
         PRINT &PRTSOR
         EXTRN @DCBXLST      (IN @DCBEXIT)                       90280
PRINTER  PGMHEAD ZERO12,BASE=R12,AM=31,RM=24, *O*L*D* CODE       90280 *
               PARM=R9
*DEFER*  BANDAID INIT                                           GP03114
         MVC   SYSUT2(SYSUT2LN),PATUT2  PREPARE DEFAULT OUTPUT  GP04082
         SERVINIT ,                                             GP03114
         SERVLOAD @PARSER,@PRINTER,@INPREAD,SUBVERB             GP04082
         #OPMSG 'PRINTER',TYPE=INIT (FOR OPTIONAL WTO MSGS)     GP03114
*---------------------------------------------------------------------*
*  ANALYZE PARM FIELD - OK IF NONE; ELSE WANT 1 OR 2 NUMERICS         *
*---------------------------------------------------------------------*
         MVC   LIMCNT,=X'7FFFFFFF'  NO END LIMIT (SKIP IS ZERO) GP03114
         LA    R9,0(,R9)                                        GP03114
         LTR   R9,R9                                            GP03114
         BZ    DONEPARM      NO PARM POINTER ADDRESS            GP03114
         L     R9,0(,R9)                                        GP03114
         LA    R9,0(,R9)                                        GP03114
         LTR   R9,R9                                            GP03114
         BZ    DONEPARM      NO PARM POINTER                    GP03114
         LH    R8,0(,R9)     GET PARM LENGTH                    GP03114
         LTR   R8,R8         AND TEST IT                        GP03114
         BNP   DONEPARM                                         GP03114
         PARSE 2(,R9),(R8),OPT=COMMA  (NO KEYWORDS)             GP03114
         BXH   R15,R15,BADPARSE                                 GP03114
         LA    R3,PAR@TABL   GET RESULT(S)                      GP04082
         LA    R4,LIMCNT     SET FOR LIMIT COUNTER              GP04082
         USING PRSDSECT,R3   DECLARE IT                         GP03114
LOOPWORD ICM   R3,15,PRSLINK  ANY TEXT AT ALL ?                 GP04082
         BZ    DONEPARM      NO; DONE                           GP04082
         ICM   R2,15,PRS#TEXT  ANY TEXT AT ALL ?                GP03114
         BZ    LOOPWORD      NO; SKIP NULLS                     GP04082
         TM    PRSFLAGS,PRSFGINT  VALID INTEGER ?               GP03114
         BZ    LOOKWORD      TRY KEYWORD                        GP04082
         ICM   R0,15,PRS#TINT  MORE THAN FOUR BYTES ?           GP03114
         BNZ   PARMBAD                                          GP03114
         LA    R0,SKIPCNT                                       GP04082
         CR    R4,R0         BEEN HERE BEFORE ?                 GP04082
         BH    PARMBAD       YES; TOO BAD                       GP04082
         MVC   LIMCNT-LIMCNT(4,R4),PRS#TINT+4  USE LOW BYTES    GP04082
         LA    R4,SKIPCNT-LIMCNT(,R4)  NEXT COUNTER             GP04082
         B     LOOPWORD                                         GP04082
         SPACE 1                                                GP04082
LOOKWORD LA    R0,PRS$TEXT   POINT TO TEXT                      GP04082
         OC    PRS$TEXT(8),BLANKS  UPPER CASE                   GP04082
         LA    R1,KEYWORDP   GET KEYWORD TABLE POINTER          GP04082
         SUBCALL SUBVERB     DO TABLE LOOKUP                    GP04082
         LTR   R15,R15       DID IT WORK ?                      GP04082
         BZ    PARMBAD       NO                                 GP04082
         OC    OPTFLAGS,OFOFONN(R15)  SET FLAG ON               GP04082
         TM    OPTFLAGS,FGPORT+FGLAND  NOT TOO MANY ?           GP04082
         BNO   LOOPWORD      LOOK AT NEXT PARM                  GP04082
         SPACE 1                                                GP04082
PARMBAD  L     R4,PRS@TEXT   LOAD TEXT ADDRESS                  GP03114
         #OPMSG 'INVALID PARM - |',LIST=((R4),(R2))             GP03114
         ABEND 123,DUMP                                         GP03114
BADPARSE #OPMSG 'PARSE FAILED'                                  GP03114
         ABEND 122,DUMP                                         GP03114
*---------------------------------------------------------------------*
*  CHECK FOR OPTIONAL TITLE FILE                                      *
*---------------------------------------------------------------------*
DONEPARM LA    R0,80         SET FOR PORTRAIT MODE              GP04082
         TM    OPTFLAGS,FGPORT  IS IT ?                         GP04082
         BNZ   PRNTSIZE      YES                                GP04082
         LA    R0,132        SET FOR LANDSCAPE                  GP04082
         TM    OPTFLAGS,FGLAND  IS IT ?                         GP04082
         BZ    PRNTOPEN      NO; USE DEFAULT                    GP04082
PRNTSIZE STC   R0,SYSUT2+PUWIDTH-PUPARM  SET DATA WIDTH         GP04082
PRNTOPEN LA    R2,SYSUT2     POINT TO DD NAME                   GP04081
         PRTOPEN SYSUT2      OPEN OUTPUT FILE                   GP04081
         CH    R15,=H'4'     AVAILABLE ?                        GP04081
         BH    BADUTDD       NO                                 GP04081
         INPOPEN TITLE,OPT=(NOWTO)                              GP04081
         INPGET ,            GET TITLE CARD, IF ANY             GP04081
         BXH   R15,R15,DONETITL                                 GP04081
         LR    R4,R1         COPY TEXT ADDRESS                  GP04081
         LR    R5,R0         COPY LENGTH                        GP04081
         LOCBYTE (R4),LEN=(R5)     FIND A BLANK                 GP04081
         SR    R15,R14       LENGTH OF NON-BLANK TEXT           GP04081
         BNP   DONETITL      HUH ?                              GP04081
         CH    R15,=H'8'     REASONABLE FOR USER ID ?           GP04081
         BH    DONETITL      NO                                 GP04081
         MVI   VTITEXT,C' '                                     GP04081
         MVC   VTITEXT+1(L'VTITEXT-1),VTITEXT  BLANK LINE BUFFER
         LA    R14,VTITEXT+1  USER ID                           GP04081
         EX    R15,EXMVCTIT  MOVE USERID OR DSN                 GP04081
         LA    R4,1(R15,R4)  ADJUST START                       GP04081
         BCTR  R5,0          ADJUST FOR SPACER                  GP04081
         SR    R5,R15        ADJUST LENGTH                      GP04081
         BNP   MAKETITL      ISSUE TITLE REQUEST                GP04081
         LOCBYTE (R4),LEN=(R5)     FIND END OF DSN              GP04081
         SR    R15,R14       GET LENGTH                         GP04081
         BNP   MAKETITL      NONE                               GP04081
         LR    R5,R15        SAVE LENGTH                        GP04081
         BCTR  R15,0         EXECUTE LENGTH                     GP04081
         LA    R14,VTITEXT+80/2  POINT TO MIDDLE OF LINE        GP04081
         SRL   R5,1          HALVE LENGTH                       GP04081
         SR    R14,R5        CENTER, MORE OR LESS               GP04081
         EX    R15,EXMVCTIT  MOVE DSN                           GP04081
MAKETITL LA    R0,L'VTITEXT+4                                   GP04082
         STH   R0,VTITLE1    COMPLETE THE RDW                   GP04082
         PRTV  VTITLE1,TITLE=1  MAKE TITLE                      GP04081
         PRTL  '#',TITLE=2   REQUEST DATE/TIME/PAGE #           GP04082
*---------------------------------------------------------------------*
*  CHECK INPUT AND OUTPUT SPECIFICATIONS                              *
*---------------------------------------------------------------------*
DONETITL INPCLOSE ,          CLOSE TITLE FILE                   GP04081
         LA    R2,SYSUT1     POINT TO DD NAME                   GP04081
         INPOPEN SYSUT1      OPEN INPUT FILE                    GP04081
         CH    R15,=H'4'     AVAILABLE ?                        GP04081
         BH    BADUTDD       NO                                 GP04081
*---------------------------------------------------------------------*
*  CHECK FOR RECORDS TO BE SKIPPED                                    *
*---------------------------------------------------------------------*
         L     R7,LIMCNT     RESET RECORD NUMBER TO LIMIT       GP04081
         ICM   R6,15,SKIPCNT    ANY RECORDS TO BE SKIPPED ?     GP03114
         BZ    DONESKIP                                         GP03114
         CVD   R6,DB         CONVERT BINARY TO PACKED           GP03114
         MVC   PRTCOUNT,=X'40202020202020202020202020202120'    GP03114
         EDMK  PRTCOUNT,DB   MAKE PRINTABLE                     GP03114
         LR    R4,R1                                            GP03114
         #OPMSG 'SKIPPING | RECORDS',LIST=((R4))  ZERO STOPPER  GP03114
SKIPPING INPGET ,            GET A RECORD                       GP03114
         BXH   R15,R15,CLOSE  NO GOOD                           GP04081
*CHECK*  BCT   R7,*+8        BUMP RECORD COUNTER                GP04081
*CHECK*    B   CLOSE         REACHED LIMIT WHILE SKIPPING       GP04081
         BCT   R6,SKIPPING   AND IGNORE IT                      GP03114
         SPACE 1                                                GP03114
DONESKIP TM    OPTFLAGS,FGCC  INPUT CARRIAGE CONTROLS ?         GP04082
         BNZ   LOOPINCC      YES; DO IN SEPARATE LOOP           GP04082
         SPACE 1                                                GP03114
LOOPNOCC INPGET ,            GET A RECORD                       GP03114
         BXH   R15,R15,CLOSE  NO GOOD                           GP04081
         LR    R4,R1         COPY TEXT ADDRESS                  GP04081
         LR    R5,R0         COPY LENGTH                        GP04081
         PRTF  (R4),(R5),CC=NO                                  GP04081
         BCT   R7,LOOPNOCC   WRITE UNDER COUNT CONTROL          GP03114
         B     CLOSE                                            GP04082
         SPACE 1                                                GP03114
LOOPINCC INPGET ,            GET A RECORD                       GP03114
         BXH   R15,R15,CLOSE  NO GOOD                           GP04081
         LR    R4,R1         COPY TEXT ADDRESS                  GP04081
         LR    R5,R0         COPY LENGTH                        GP04081
         PRTF  (R4),(R5),CC=ASA                                 GP04081
         BCT   R7,LOOPINCC   WRITE UNDER COUNT CONTROL          GP03114
         SPACE 2
CLOSE    C     R7,LIMCNT     ANY ?                              GP03114
         BNE   CLOSEM                                           GP03114
         #OPMSG 'END-FILE BEFORE COPYING STARTED'               GP03114
         MVICC 4,0                                              GP03114
         B     CLOSER                                           GP03114
CLOSEM   L     R0,LIMCNT                                        GP03114
         SR    R0,R7                                            GP03114
         CVD   R0,DB         CONVERT BINARY TO PACKED           GP03114
         MVC   PRTCOUNT,=X'40202020202020202020202020202120'    GP03114
         EDMK  PRTCOUNT,DB   MAKE PRINTABLE                     GP03114
         LR    R2,R1
         #OPMSG '| RECORDS COPIED',LIST=((R2)) ZERO STOPPER     GP03114
         SPACE 1                                                 78271
CLOSER   #OPMSG TYPE=END     CLEANUP                            GP03114
         SERVTERM ,                                             GP03114
         PGMEXIT COPYRET=(RETCODE,8)                            GP03114
         SPACE 1                                                 90280
BADUTDD  #OPMSG 'UNABLE TO OPEN DD |',LIST=((R2),8)             GP03114
         ABEND 113                                               90280
EXMVCTIT MVC   0(0,R14),0(R4)  MOVE USERID OR DSN TO TITLE      GP04081
         SPACE 2
         LTORG ,                                                GP04081
         SPACE 2                                                GP04081
SYSUT1   INPWORK SYSUT1,INFILE,EDIT=128                         GP04081
TITLE    INPWORK TITLE,EDIT=128                                 GP04081
BLANKS   EQU   TITLE+8,8,C'C'  RE-USE                           GP04082
PATUT2   PRTWORK SYSUT2,SYSPRINT,TITLE=3                        GP04081
         SPACE 1                                                GP04082
         PARKEYBX KEYWORD                                       GP04082
KEYWORD  PARKEYFG 'LANDSCAPE',OPTFLAGS,FGPORT,FGLAND,LEN=1      GP04082
KEYWORD2 PARKEYFG 'PORTRAIT',OPTFLAGS,FGLAND,FGPORT             GP04082
         PARKEYFG 'CC',OPTFLAGS,0,FGCC,MIN=2                    GP04082
KEYWORDN PARKEYFG 'DEBUG',OPTFLAGS,0,FGDEBUG                    GP04082
         STITL 'WORK AREA'                                       *TSM*
SAVE     DSECT
DB       DS    D                                                GP03114
         SERVDEFS ,          DEFINE NEWFANGLED STUFF            GP03114
LIMCNT   DC    X'7FFFFFFF' 1 NUMBER OF RECORDS TO WRITE         GP03114
SKIPCNT  DC    F'0'      2/2 NUMBER OF RECORDS TO SKIP          GP03114
OPTFLAGS DS    X                                                GP04081
FGLAND   EQU   X'80'           LANDSCAPE MODE; WITH 132         GP04082
FGPORT   EQU   X'40'           PORTRAIT MODE; WITH 80           GP04082
FGCC     EQU   X'20'           INPUT FILE HAS CONTROL CHARACTERS
FGDEBUG  EQU   X'01'           DEBUG MODE                       GP04082
         SPACE 1                                                GP03114
SYSUT2   PRTWORK SYSUT2,SYSPRINT,TITLE=3                        GP04082
SYSUT2LN EQU   *-SYSUT2                                         GP04082
         SPACE 1                                                GP03114
         MAPPARSE DSECT=NO                                      GP03114
         SPACE 1                                                GP03114
PRTCOUNT DS    CL16,X'00'    PRINTABLE COPY COUNT (0 STOPPER)   GP03114
         SPACE 1                                                GP03114
VTITLE1  VCON  END=VTITLE1N                                     GP04081
VTITEXT  DC    CL133'#'                                         GP04081
         VCON  *END                                             GP04081
SAVEEND  EQU   *
         SPACE 2                                                GP03114
         MAPPARST ,          DEFINE PARSE RESULTS               GP03114
         MAPPRT ,            MAP PRINTER PARM AREA              GP04082
         END   ,                                                GP03114
