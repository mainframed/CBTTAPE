SUBTRAP  TITLE 'S U B T R A P   ***  #TRAP FORMATTING ROUTINE'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*   R1 --> A(data) :  PSW(8),LNIC(4),REGS(R0-R15)                     *
*   Not reentrant; partially reusable.                                *
*   Expects external error reovery (restart at address in             *
*     SUBTREST if non-zero)                                           *
*   The PSW may point to the trace instruction (83 or B2), or just    *
*     past it (default IC).                                           *
*                                                                     *
*   PROCESS A REQUEST LIST TO FORMAT USER DATA (at PSW address)       *
*     B2FF0000,nnnnnnnn ->  addr of list (AL4 or FFFFbddd)            *
*     83CD0000,nnnnnnnn ->  addr of list (AL4 or FFFFbddd)            *
*     list: fg nn ->  flags; item count (<256)                        *
*      0-255:     type, subtype, bddd, len   (FORMAT ITEM)            *
*                 may have flag and CL8 label                         *
*                                                                     *
*---------------------------------------------------------------------*
*   The subroutine interacts with the caller :                        *
*   1) If a SYSDEBUG DD is present, then output will be written to it.*
*   2) If SUBTRAP+28 is set to the @PRINTER address, and DEV=1 has    *
*      been opened, then output will go there.                        *
*   3) If neither, output goes to console via WTO.                    *
*                                                                     *
*   SUBTRAP sets a recovery address into the +24 slot. Caller may     *
*   use an (E)SPIE exit to set the IC to that address for continue    *
*   processing (the entry will be marked bad).                        *
***********************************************************************
SUBTRAP  START 0
SUBTRAP  AMODE ANY
SUBTRAP  RMODE ANY
         B     BEGIN-SUBTRAP(,R15)    SKIP AROUND
         DC    AL1(19),CL19'SUBTRAP &SYSDATE'
SUBTREST DC    AL4(0)        0 OR RESTART ADDRESS AFTER PIC     GP12327
SUBT@PRT DC    AL4(0)        ADDRESS OF @PRINTER (DEV=1 OPEN)   GP12327
         SPACE 1
BEGIN    BSM   R14,0         SAVE AMODE
         STM   R14,R12,12(R13)
         LR    R12,R15       AND NEW BASE
         USING SUBTRAP,R12
         LA    R9,SUBSAVE    SAVE AREA
         ST    R9,8(,R13)
         ST    R13,4(,R9)
         LR    R13,R9        NEW SAVE AREA
         SPACE 1
         SR    R15,R15                                          GP99116
         LTR   R14,R14       CALLER IN AM31?                    GP99116
         BNM   SETAMASK      NO                                 GP99116
         LA    R15,X'7F'     MAKE MASK FOR AMODE 31             GP99116
SETAMASK STC   R15,ADDRMASK                                     GP99116
         TM    PROFLAGS,PFCANC  PERMANENT DISABLE?              GP00162
         BNZ   SUBTREXT      YES; JUST TAKE QUICK RETURN        GP10094
         LA    R6,DBTEXITS   SET FOR QUICK EXIT (LABEL ONLY)
         LR    R10,R1        COPY PARM REGISTER                 GP95235
         N     R10,ADDRMASK  CLEAN PARAMETER POINTER            GP99116
         LA    R8,FORMREGS-FORMPSW(,R10)  POINT TO USER'S REGS  GP09277
         L     R9,4(,R10)    GET IC                             GP09277
         N     R9,ADDRMASK   CLEAN IT TO GET LIST POINTER       GP09277
         LR    R6,R9         SAVE START                         GP12327
         CLI   0(R6),X'B2'   TRAP4 ?                            GP12327
         BE    ADJUST9         YES; ADDRESS POINTER IS NEXT     GP12327
         CLI   0(R6),X'83'   DIAG ?                             GP12327
         BE    ADJUST9         YES                              GP12327
         SH    R6,=H'4'      DOUBLE-CHECK                       GP12327
         CLI   0(R6),X'B2'   TRAP4 ?                            GP12327
         BE    ADJUSTED        YES; ADDRESS POINTER IS NEXT     GP12327
         CLI   0(R6),X'83'   DIAG ?                             GP12327
         BNE   SUBTREXT      NO; MAJOR ERROR IN CALL            GP12327
ADJUST9  LA    R9,4(,R9)     SKIP OVER THE TRAP INSTRUCTION     GP12327
ADJUSTED ZI    PROFGMOD,PROFDIAG  NOT OLD DIAGNOSE              GP12327
         CLI   1(R6),X'0E'   830E ?                             GP12327
         BNE   NOTODIAG        NO                               GP12327
         OI    PROFGMOD,PROFDIAG  DIFFERENT LIST STRUCTURE      GP12327
         MVC   CALFLAGS(2),2(R6)  SAVE FORMATTING REQUESTS      GP12327
         LA    R9,8(,R6)     POSITION TO BRANCH                 GP12327
         B     HAVEADDR        AND USE LIST                     GP12327
NOTODIAG ICM   R9,15,0(R9)   LOAD REQUEST LIST                  GP09277
         CLM   R9,12,=X'FFFF'   SL2 FORMAT ?                    GP11351
         BNE   HAVEADDR           NO; IT'S AN ADDRESS           GP11351
         LA    R0,X'FFF'     MAKE DISPLACEMENT MASK             GP11351
         NR    R0,R9         ISOLATE DISPLACEMENT               GP11351
         SRL   R9,12-2       CHANGE REGISTER TO OFFSET          GP11351
         N     R9,=X'0000003C'  CLEAN OFFSET                    GP11351
         L     R9,FORMREGS-FORMPSW(R9,R10)  GET BASE            GP11351
         N     R9,ADDRMASK        CLEAN IT                      GP11351
         AR    R9,R0              GET LIST ADDRESS              GP11351
HAVEADDR N     R9,ADDRMASK   CLEAN IT TO GET LIST ADDRESS       GP09277
         SPACE 1                                                GP99113
         TM    PROFLAGS,PFCANC  DISABLED ?                      GP10159
         BNZ   DBTEXITS      PROD & NO DD - BYPASS              GP10159
         SPACE 1                                                GP99113
         TM    PROFGMOD,PROF@LCL   IS THIS THE FIRST CALL ?     GP12327
         BNZ   DBTNONCE      NO                                 GP12327
         OI    PROFGMOD,PROF@LCL+PROF@WTO  INIT; WTO            GP12327
         SPACE 1                                                GP00162
         ICM   R0,15,SUBT@PRT      USE @PRINTER ?               GP12327
         BNZ   DBTUSE@P              YES                        GP12327
         #PRT  PRINTWRK,TYPE=OPEN DEV=1 OPEN IT                 GP10159
         CH    R15,=H'8'     OPEN NOW?                          GP09277
         BNL   DBTBYPED      NO                                 GP09277
         OI    PROFGMOD,PROF@DCB  SIGNAL OPEN DCB               GP99113
DBTUSE@P OI    PROFLAGS,PROFLWID  SET WIDE MODE                 GP99113
         ZI    PROFGMOD,PROF@WTO  PRINT, NOT WTO                GP99113
         B     DBTNONCE                                         GP99113
DBTBYPED TM    PROFLAGS,PROFLPRO  PRODUCTION MODE?              GP99113
         BZ    DBTGOWTO      NO; USE WTO                        GP10159
         OI    PROFLAGS,PFCANC  DISABLE COMPLETELY              GP00162
         B     DBTEXITS      PROD & NO DD - BYPASS              GP99113
DBTGOWTO OI    PROFLAGS,PROFLWTO  FORCE WTO                     GP10159
DBTNONCE TM    PROFLAGS,PROFLWTO  IS WTO FORCED?                GP99113
         BNZ   INITSHRT      YES; NARROW MODE                   GP99113
         OI    PROFLAGS,PFWIDE  SET WIDE MODE                   GP99113
         LA    R0,MSGRTDS2-MSGRDW  WIDE SIZE                    GP99113
         STH   R0,MSGRDW     SET WIDE                           GP99113
         USING LSTWORK,R9    DECLARE MAPPING OF REQUESTS        GP95235
         SPACE 1                                                GP99113
INITSHRT MVC   MSGLABL(MSGRTDS-MSGLABL),MSGLABL-1   BLANK THE LINE
         MVC   MSGHEAD,=CL8'ESP666'  DEBUG HEADER
         SPACE 1                                                GP99113
         TM    PROFGMOD,PROFDIAG  DIAG TYPE LIST ?
         BNZ   INITDIAG
INITTRAP MVC   CALFLAGS(2),LSTTYPE INIT FLAGS                   GP12327
         LA    R9,2(,R9)     SKIP FLAGS AND COUNT               GP09277
INITDIAG SR    R1,R1                                            GP09277
         IC    R1,CALCOUNT   GET ENTRY COUNT                    GP12327
         ST    R1,NUMTODO    SAVE IT                            GP09277
         MVC   CURFLAGS,CALFLAGS  SAVE FLAGS                    GP12327
         NI    CURFLAGS,X'3F'     RESET LABEL, PSW              GP10092
         TM    CALFLAGS,X'80'  IS THERE A LABEL?                GP02364
         BNZ   DBTLUP        YES; USE IT                        GP02364
         UNPK  MSGLABL+1(7),5(4,R10)                            GP09277
         TR    MSGLABL+1(6),DBTHEXTR  MAKE PRINTABLE            GP09277
         MVI   MSGLABL+7,C' '                                   GP09277
         B     TESTPSW                                          GP09277
DBTLUP   MVC   MSGLABL,LSTTYPE   MOVE USER'S LABEL              GP95235
         LA    R9,8(,R9)     BUMP PAST LABEL                    GP95235
TESTPSW  TM    CALFLAGS,X'40'     WANT PSW ?                    GP09277
         BZ    SKIPPSW
         MVC   MSGLABL+20(4),=C'PSW='                           GP09277
         UNPK  MSGLABL+24(9),0(5,R10)                           GP09277
         UNPK  MSGLABL+33(9),4(5,R10)                           GP09277
         TR    MSGLABL+24(8+1+8),DBTHEXTR  MAKE PRINTABLE       GP09277
         MVI   MSGLABL+32,C'-'                                  GP09277
         MVI   MSGLABL+41,C' '                                  GP09277
SKIPPSW  MVC   MSGLABL+50(4),=C'TCB='                           GP09277
         UNPK  MSGLABL+54(9),PSATOLD-PSA(5)                     GP09277
         TR    MSGLABL+54(8),DBTHEXTR                           GP09277
         MVI   MSGLABL+62,C' '                                  GP09277
         TM    CALFLAGS,X'3F'     ANY OTHER REQUESTS?           GP10092
         BNZ   DBTINIT            YES; CONTINUE                 GP10092
         ICM   R0,15,NUMTODO ANY AT ALL ?                       GP10094
         BP    DBTINIT       YES; PROCESS NORMALLY              GP10094
         SR    R3,R3         CLEAR PARTIAL COUNT                GP10094
         OI    REQTYPE,RQTEOL     INDICATE END OF REQUEST       GP09277
         B     DBTLAST       PRINT IT                           GP09277
         SPACE 1
DBTINIT  LPR   R3,R12        ANY POSITIVE VALUE                 GP09277
         NI    CURFLAGS,X'3F'   RETAIN ONLY UNPROCESSED FLAGS   GP10092
         BAS   R6,DBTLAST    PRINT HEADER LINE                  GP09277
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  LOOP TO PROCESS SPECIAL REQUESTS (CURFLAGS - GPR, FPR,.....)     **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DBTLOOP  OI    CURFLAGS,X'40'     RETURN HERE UNTIL DONE        GP10092
         TM    CURFLAGS,X'20'     FORMAT GENERAL REGISTERS?     GP10092
         BZ    DBTLUP20           NO                            GP10092
         ZI    CURFLAGS,X'20'     RESET IT                      GP10092
         B     DOGPRS             AND FORMAT GENERAL REGISTERS  GP10092
         SPACE 1
DBTLUP20 TM    CURFLAGS,X'10'     FORMAT FLOATING POINT ?       GP10092
         BZ    DBTLUP10           NO                            GP10092
         ZI    CURFLAGS,X'10'     RESET IT                      GP10092
         B     DOFPRS             AND FORMAT FLOATING REGISTERS GP10092
         SPACE 1
DBTLUP10 TM    CURFLAGS,X'08'     FORMAT CONTROL REGISTERS?     GP10092
         SR    R3,R3         CLEAR PARTIAL COUNT                GP10094
         MVI   CURFLAGS,0     --- REST NOT IMPLEMENTED ---      GP10092
         BALR  R6,0          NEW RETURN POINT                   GP12327
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  LOOP TO PROCESS CURRENT REQUEST (POINTED TO BY R9)               **
**                                                                   **
***********************************************************************
         SPACE 1
ITEMLOOP INC   NUMTODO,WORK=R1,INC=-1   NUMBER LEFT TO DO       GP09277
         LTR   R1,R1         ANY MORE ?                         GP09277
         BM    SUBTREXT      NO; QUIT                           GP09277
         TM    PROFGMOD,PROFDIAG  DIAG TYPE LIST ?              GP12327
         BZ    MOVETRAP      NO                                 GP12327
         MVC   REQTYPE(2),=AL1(RTHEX,0)  SIMPLE HEX ITEM        GP12327
         CLI   0(R9),X'40'   NAME ?                             GP12327
         BNH   MOVEDNNM        NO; LEAVE BLANK                  GP12327
         MVC   MSGLABL,0(R9) SHOW NAME                          GP12327
         LA    R9,8(,R9)     SKIP NAME                          GP12327
MOVEDNNM MVC   REQADDR,0(R9) MOVE S-FORM ADDRESS                GP12327
         MVC   REQLEN,2(R9)  MOVE LENGTH                        GP12327
         LA    R9,4(,R9)     SKIP TO NEXT ITEM                  GP12327
         B     SKIPNAME                                         GP12327
         SPACE 1
MOVETRAP MVC   REQTYPE(2),LSTTYPE     MOVE FLAGS AND COUNT      GP12327
         MVC   REQTYPE,LSTTYPE                                  GP12327
         MVC   REQADDR,LSTADDR                                  GP12327
         MVC   REQLEN,LSTLEN                                    GP12327
         LA    R9,6(,R9)     SKIP FLAGS AND LENGTH
         TM    REQFG,RFNONAM   ANY NAME FIELD ?                 GP09277
         BNZ   SKIPNAME                                         GP09277
         MVC   MSGLABL,LSTNAME-LSTNAME(R9)                      GP09277
         LA    R9,8(,R9)     SKIP NAME
SKIPNAME LA    R0,RQTMSK     LOAD END-LIST MASK                 GP02364
         IC    R2,REQTYPE    GET TYPE OF ITEM                   GP12327
         NR    R2,R0         IGNORE END-LIST BIT                GP02364
         CH    R2,=Y((PROCEND-PROCONE)/(PROCTWO-PROCONE))       GP97265
         BH    DBTLAST       TOO HIGH, TOO BAD                  GP97265
         SLL   R2,1          MAKE PROCESSOR ROUTINE OFFSET      GP97265
         LA    R0,FORMFAIL   SET RECOVERY ADDRESS               GP10092
         ST    R0,SUBTREST     INTO SPIE EXIT                   GP10092
         LH    R7,PROCONE(R2)  LOAD PROCESSING ROUTINE          GP97265
         LA    R15,1                                            GP97265
         NR    R15,R7        SEE WHETHER ADDRESS WAS ODD        GP97265
         BZ    SUBTRAP(R7)   NO; INVOKE IT DIRECTLY             GP97265
         SRL   R7,1                                             GP97265
         SLL   R7,1          ELSE MAKE IT EVEN                  GP97265
         B     RESADDR                                          GP97265
         SPACE 1                                                GP97265
PROCONE  DC    Y(DOREGS-SUBTRAP)         REGISTER FORMATTING    GP97265
PROCTWO  DC    Y(FUNFTEXT-SUBTRAP+1)     TEXT                   GP97265
         DC    Y(FUNFCHEX-SUBTRAP+1)     TEXT OR HEX            GP97265
         DC    Y(FUNFHEX-SUBTRAP+1)      HEX                    GP97265
         DC    Y(FUNPACK-SUBTRAP+1)      PACKED                 GP97265
         DC    Y(FUNFHEX-SUBTRAP+1)        SPARE                GP97265
         DC    Y(FUNFHEX-SUBTRAP+1)        SPARE                GP97265
         DC    Y(FUNFHEX-SUBTRAP+1)        SPARE                GP97265
         DC    Y(DBTLIST-SUBTRAP)        LIST FORM              GP97265
PROCEND  EQU   *                                                GP97265
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PROCESS ADDRESS, LENGTH, AND FLAG OPTION FOR COMMON FUNCTIONS    **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
RESADDR  SLR   R2,R2                                            GP97265
         IC    R2,REQADDR    GET S(ADDR)
         SRL   R2,4          DELETE LOW BITS OF OFFSET
         SLA   R2,2          *4 => INDEX INTO ERRREGS
         BZ    *+8           R0 = 0
         L     R2,ERRREGS-ERRREGS(R2,R8)  GET USER'S REGISTER   GP09277
         LA    R0,4095       MAKE MASK
         ICM   R15,3,REQADDR  GET LOW PORTION OF ADDRESS        GP95235
         NR    R0,R15        MASK OFFSET OF ADDRESS             GP95235
         ALR   R2,R0         MAKE EFFECTIVE ADDRESS
         TM    REQFG,RFA24   FORCED 24-BIT ADDRESSING MODE?     GP96101
         BZ    *+8           NO                                 GP96101
         N     R2,=X'00FFFFFF'  FORCE 24-BIT ADDRESS            GP96101
         TM    REQFG,RFA31   FORCED 31-BIT ADDRESSING MODE?     GP99116
         BNZ   NOTBAD31      NO                                 GP99116
         N     R2,ADDRMASK   USE MASK IN USER'S AMODE           GP99116
NOTBAD31 TM    REQFG,RFIAD   INDIRECT ADDRESSING?               GP96101
         BZ    NOTIAD31      NO                                 GP96101
         L     R2,0(,R2)     LOAD ADDRESS FROM POINTER          GP96101
         TM    REQFG,RFA24   FORCED 24-BIT ADDRESSING MODE?     GP96101
         BZ    NOTIAD        NO                                 GP96101
         N     R2,=X'00FFFFFF'  FORCE 24-BIT ADDRESS            GP96101
NOTIAD   TM    REQFG,RFA31   FORCED 31-BIT ADDRESSING MODE?     GP99116
         BNZ   NOTIAD31      NO                                 GP99116
         N     R2,ADDRMASK   USE MASK IN USER'S AMODE           GP99116
NOTIAD31 SLR   R3,R3                                            GP96101
         ICM   R3,3,REQLEN                                      GP09277
         BNM   LENINT                                           GP09277
         LA    R0,15         REGMASK                            GP09277
         NR    R3,R0         REGISTER NUMBER                    GP09277
         SLL   R3,2          MAKE OFFSET                        GP09277
         L     R3,ERRREGS-ERRREGS(R3,R8)  GET USER'S REGISTER   GP09277
         LTR   R3,R3         USABLE ?                           GP09277
         BM    DBTLAST       NO; SKIP THIS                      GP09277
LENINT   ST    R2,@BASE                                         GP09277
         UNPK  MSGHEAD+2(7),@BASE+1(4)                          GP09277
         TR    MSGHEAD+2(6),DBTHEXTR                            GP09277
         MVI   MSGHEAD+8,C' '                                   GP09277
         B     SUBTRAP(R7)   GO TO PROCESSING ROUTINE           GP97265
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  FORMAT ALL GENERAL REGISTERS                                     **
**    REGISTERS ARE IN R8                                            **
**    THEN PROCESSED AS A HEX DISPLAY                                **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP10092
DOGPRS   MVC   MSGFLAG(2),=C'r0'  IDENTIFY FIRST REGISTER       GP10092
         LR    R2,R8         POINT TO FIRST REGISTER            GP10092
         ST    R2,@BASE                                         GP10092
         LA    R3,4*16       SET LENGTH                         GP10092
         B     PROFHEX       FORMAT HEX                         GP10092
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  FORMAT FLOATING POINT REGISTERS                                  **
**    REGISTERS ARE MOVED TO REGWORK                                 **
**    THEN PROCESSED AS A HEX DISPLAY                                **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP10092
DOFPRS   MVC   MSGFLAG(2),=C'f0'  IDENTIFY FIRST REGISTER       GP10092
         STD   R0,REGWORK    SAVE FIRST ONE                     GP10092
         STD   R2,REGWORK+8                                     GP10092
         STD   R4,REGWORK+16                                    GP10092
         STD   R6,REGWORK+24                                    GP10092
         LA    R2,REGWORK    POINT TO FIRST REGISTER            GP10092
         ST    R2,@BASE                                         GP10092
         LA    R3,4*8        SET LENGTH                         GP10092
         B     PROFHEX       FORMAT HEX                         GP10092
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PROCESS REGISTER REQUESTS                                        **
**    REGISTERS ARE MOVED TO REGWORK (TO HANDLE WRAP-AROUND) AND     **
**    THEN PROCESSED AS A HEX DISPLAY                                **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DOREGS   TM    CALFLAGS,X'20'  ALREADY FORMATTED ?              GP10092
         BNZ   DBTLAST       YES; SKIP                          GP10092
         LA    R0,15         MAKE MASK                          GP97261
         LH    R14,REQADDR   GET START REGISTER                 GP97261
         LH    R15,REQLEN    GET END REGISTER                   GP97261
         NR    R14,R0        KEEP ONLY LOW NIBBLE (START REG)   GP96101
         NR    R15,R0          END REGISTER                     GP96101
         LA    R1,DBTHEXTB(R14)  POINT TO PRINTABLE EQUIVALENT  GP97265
         MVC   MSGFLAG+1(1),0(R1)  IDENTIFY IT                  GP97265
         MVI   MSGFLAG,C'R'-X'40'  AS REGISTER                  GP97265
         CR    R14,R15       IS LOW SAME AS HIGH?               GP96101
         BNE   DOREGWRP      NO                                 GP96101
         BCTR  R15,0         MAKE RANGE OF 15 REGISTERS         GP96101
         NR    R15,R0        CLEAN IT UP                        GP96101
DOREGWRP SLDL  R14,2         CONVERT REGISTER NUMBER TO OFFSET  GP96101
         CR    R14,R15       IS LOW LESS THAN HIGH?             GP96101
         BH    DOREGSPL      NO; SPLIT                          GP96101
         LA    R2,0(R14,R8)     POINT TO FIRST CONTIGUOUS REGISTER
         ST    R2,@BASE                                         GP09284
         LR    R3,R15                                           GP96101
         SR    R3,R14                                           GP96101
         LA    R3,4(,R3)     MAKE HEX LENGTH                    GP96101
         B     PROFHEX       FORMAT HEX                         GP96101
DOREGSPL LA    R3,(16+1)*4   MAX LENGTH TO FORMAT + ONE         GP96101
         AR    R3,R15        PLUS END OFFSET                    GP96101
         SR    R3,R14        LESS START OFFSET = REQ. LENGTH    GP96101
         LA    R4,0(R14,R8)     POINT TO FIRST (LOW) SEGMENT    GP96101
         LA    R5,REGWORK    SET DESTINATION ADDRESS            GP96101
         LA    R1,64-1       MAX MOVABLE                        GP96101
         SR    R1,R14        LESS DISPLACEMENT TO LOW           GP96101
         EX    R1,DOREGMVC   MOVE FIRST PORTION                 GP96101
         LR    R4,R8         SET FROM ADDRESS                   GP96101
         LA    R5,REGWORK+1(R1)  OFFSET TO SECOND PART          GP96101
         LA    R1,4-1(,R15)    LENGTH TO MOVE                   GP96101
         EX    R1,DOREGMVC   MOVE SECOND SECTION                GP96101
         LA    R2,REGWORK    POINT TO CONSOLIDATED FIELD        GP96101
         ST    R2,@BASE                                         GP09284
         B     PROFHEX       GO TO FORMAT                       GP96101
DOREGMVC MVC   0(0,R5),0(R4)  MOVE                              GP96101
         SPACE 1                                                GP97265
***********************************************************************
**                                                                   **
**  FOR PACKED DECIMAL, PERFORM VALIDITY CHECK. MAKE SURE LENGTH     **
**   MATCHES USER'S REQUEST IF NON-ZERO                              **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
FUNPACK  MVI   MSGFLAG,C'L'-X'40' PROVISIONALLY FLAG BAD LENGTH GP97265
         LA    R4,16         SET MAXIMUM LENGTH                 GP96081
         CR    R3,R4         IS USER'S LENGTH VALID?            GP96081
         BH    PROFHEX       NO; FORMAT HEX WITH 'L' FLAG       GP96081
         MVI   MSGFLAG,C'P'-X'40' FLAG BAD PACKED (PRELIMINARY) GP97265
         LA    R1,0(,R2)     COPY ADDRESS                       GP96081
         LA    R7,9          CONSTANT                           GP96081
         LA    R0,X'0F'      MASK                               GP96081
         LA    R5,1(,R4)     REMEMBER THE LENGTH                GP96081
         SLR   R14,R14       CLEAR                              GP96081
         LTR   R6,R3         LOAD USER'S LENGTH                 GP96081
         BNZ   DBTPACKL      OK; UP AND AT 'EM                  GP96081
         LR    R6,R4         USE THIS ONE                       GP96081
DBTPACKL IC    R14,0(,R1)    LOAD NEXT BYTE                     GP96081
         LR    R15,R14       COPY                               GP96081
         SRL   R14,4         ISOLATE HIGH NYBBLE                GP96081
         CR    R14,R7        IS IT LEGAL ?                      GP96081
         BH    PROFHEX       NO; DISPLAY AS HEX                 GP96081
         NR    R15,R0        ISOLATE LOW NYBBLE                 GP96081
         CR    R15,R7        IS IT NUMERIC?                     GP96081
         BH    DBTPACKD      NO; SIGN ENDS THE FIELD            GP96081
         LA    R1,1(,R1)     SPACE                              GP96081
         BCT   R6,*+8        OK IF USER'S COUNT NOT EXHAUSTED   GP96081
         B     PROFHEX       ELSE INVALID PACKED                GP96081
         BCT   R4,DBTPACKL   TRY AGAIN                          GP96081
         B     PROFHEX       TOO BAD                            GP96081
         SPACE 1                                                GP96081
*  COPY NUMBER TO MAXIMUM LENGTH PACKED WORK AREA AND CHECK SIGN
*    NOTE THAT SIGN IS STILL IN R15                             GP96081
DBTPACKD SR    R5,R4         INPUT LENGTH                       GP96081
         LTR   R3,R3         DID USER SPECIFY AN INPUT LENGTH?  GP96081
         BZ    DBTPACK0      NO; USE OURS                       GP96081
         MVI   MSGFLAG,C'L'-X'40' PROVISIONALLY FLAG BAD LENGTH AGAIN
         CR    R3,R5         SAME AS USER'S ?                   GP96081
         BNE   PROFHEX       NO; FORMAT AS HEX                  GP96081
DBTPACK0 BCTR  R5,0          EXECUTE LENGTH                     GP96081
         XC    REGWORK(16),REGWORK  USE AS WORK SPACE           GP96081
         LA    R7,REGWORK+16-1                                  GP96081
         SR    R7,R5         LOCATION TO MOVE TO                GP96081
         EX    R5,DBTPACKM   MOVE TO WORK SPACE                 GP96081
         MVC   MSGTEXT-1(37),DBTPAMSK                           GP96081
         LA    R1,MSGTEXT+L'MSGTEXT-1   POINT TO SIGNIFICANCE   GP96081
         EDMK  MSGTEXT+L'MSGTEXT-32(32),REGWORK  FORMAT         GP96081
         BCTR  R1,0          INSIGNIFICANT LOCATION             GP96081
         STC   R15,0(,R1)    STORE HEX SIGN (0A-0F)             GP96081
         TR    0(1,R1),DBTPACKS-X'0A'  MAKE PRINTABLE SIGN      GP96081
         OI    MSGTEXT+L'MSGTEXT-1,C'0'  MAKE F ZONE            GP96081
         CLI   0(R1),C'?'    UNUSUAL SIGN?                      GP96081
         BE    DBTPACKF      NO                                 GP96081
         SLR   R3,R3         NO MORE DATA                       GP96081
         B     DBTLAST       PRINT THIS LINE                    GP96081
DBTPACKM MVC   0(0,R7),0(R2)  RIGHT ADJUST USER'S NUMBER        GP96081
DBTPACKS DC    C'??+-? '     F - BLANK, C - PLUS, D - MINUS     GP96081
DBTPAMSK DC    C' PD   ',30X'20',X'21,20'                       GP96081
DBTPACKF MVI   MSGTEXT-1,C' '                                   GP96081
         MVC   MSGTEXT,MSGTEXT-1  CLEAR TEST PORTION AGAIN      GP96081
         MVI   MSGFLAG,C'S'-X'40' SHOW BAD SIGN AND DO AS HEX   GP97265
         B     PROFHEX                                          GP97266
         SPACE 1
***********************************************************************
**                                                                   **
**  CONDITIONAL TEXT :  COMPARE TEXT TO OUR TRANSLATE TABLE. WHEN    **
**  UNPRINTABLE CHARACTERS OCCUR, FORMAT THE STRING IN HEXADECIMAL,  **
**  OTHERWISE PRINT IT.                                              **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
FUNFCHEX MVI   MSGFLAG,C'C'-X'40'                               GP97266
PROFCHEX LTR   R15,R3        COPY LENGTH                        GP96101
         BNP   FUNFTEXT      INVALID; GET OUT                   GP96101
         BCTR  R15,0         SET FOR EXECUTE                    GP96101
         LR    R4,R2         SAVE                               GP96101
         EX    R15,CHEXTRT   IS THIS A PRINTABLE TEXT STRING?   GP96101
         LR    R2,R4         RESTORE                            GP96101
         BZ    FUNFTEXT      YES; PRINT AS TEXT                 GP96101
         OI    PROFLAGS,PFDUAL  ENABLE DUAL HEX/TEXT DISPLAY    GP00162
         B     FUNFHEX       ELSE FORMAT IN HEX                 GP96101
CHEXTRT  TRT   0(0,R2),TRTCHAR  CHECK FOR PRINTABLES            GP96101
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  HEXADECIMAL FORMATTING                                           **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP96101
FUNFHEX  MVI   MSGFLAG+1,C'H'-X'40'    MAKE LOWER CASE H        GP97266
         OI    PROFLAGS,PFDUAL  ENABLE DUAL HEX/TEXT DISPLAY    GP00162
PROFHEX  BASR  R6,0          SET RETURN ADDRESS                  92271
         LR    R0,R2         NEXT DISPLAY ADDRESS               GP09277
         S     R0,@BASE      LESS START                         GP09277
         BNP   PROFHEX1      SKIP FIRST TIME                    GP09277
         ST    R0,DB         SAVE                               GP09277
         UNPK  MSGLABL+2(7),DB+1(4)                             GP09277
         TR    MSGLABL+2(6),DBTHEXTR                            GP09277
         MVI   MSGLABL+8,C' '                                   GP09277
         MVI   MSGLABL+1,C'+'
PROFHEX1 SLR   R4,R4
         LA    R1,MSGTEXT    POINT TO TEXT
         LA    R5,16         MAX INPUT CHARACTERS ON LINE        92093
         TM    PROFLAGS,PFWIDE  DOING WIDE?                     GP99113
         BZ    PROFHEX3      NO                                 GP99113
         LA    R5,32         PRINT TWO GROUPS                   GP99113
         TM    PROFLAGS,PFDUAL  DUAL MODE REQUEST?              GP00162
         BZ    PROFHEX3      NO                                 GP00162
         LTR   R3,R3         IS LENGTH VALID ?                  GP00162
         BNP   PROFHEX3      NO                                 GP00162
         LA    R15,L'MSGHTEX  MAX LENGTH ON ONE LINE            GP00162
         MIN   (R15),(R3)    USE SHORTER                        GP00162
         BCTR  R15,0         SET FOR EXECUTE                    GP00162
         EX    R15,EXMVCHTX  MOVE TO OUTPUT LINE                GP00162
         EX    R15,EXTRHTX   AND MAKE IT PRINTABLE              GP00162
         MVI   MSGHTEX-1,C'*'  FRAME THE TEXT                   GP00162
         MVI   MSGHTEX+L'MSGHTEX,C'*'                           GP00162
PROFHEX3 LTR   R3,R3         VALID LENGTH?                      GP96081
         BP    PROFHEXL      YES                                GP96081
         MVC   MSGTEXT(3),=C'L=?'   MAKE AN ERROR MESSAGE       GP96081
PROFHEXL LTR   R3,R3         ANY MORE TO DO ?
         BNP   DBTLAST
         MVC   DB(1),0(R2)   MOVE SINGLE BYTE                   GP98222
         UNPK  0(3,R1),DB(2)  UNPACK ONE BYTE (NO 0C4)          GP98222
         TR    0(2,R1),DBTHEXTR   MAKE IT PRINTABLE             GP97265
         MVI   2(R1),C' '    BLANK NEXT
         LA    R2,1(,R2)
         LA    R1,2(,R1)
         BCTR  R3,0          ADJUST RESIDUAL COUNT
         LA    R4,1(,R4)     ADD ONE TO COUNT DONE
         LA    R15,3
         NR    R15,R4        END OF A WORD ?
         BNZ   *+8           NO
         LA    R1,1(,R1)     LEAVE A GAP BETWEEN WORDS
         CR    R4,R5         DONE ONE LINE ?                     92093
         BNL   DBTLAST       YES; PROCESS IT
         CH    R4,=H'16'     DONE ONE GROUP?                    GP99113
         BNE   PROFHEXL      NO; NOT YET, OR NEVER              GP99113
         LA    R1,1(,R1)     LEAVE A LARGER GAP BETWEEN GROUPS  GP99113
         B     PROFHEXL      SEE IF MORE TO DO
EXMVCHTX MVC   MSGHTEX(0),0(R2)  COPY USER'S TEXT               GP00162
EXTRHTX  TR    MSGHTEX(0),TRTABTN   LIMIT TO PRINTABLE STUFF    GP00162
         SPACE 1                                                GP97266
***********************************************************************
**                                                                   **
**  TEXT PRINTING                                                    **
**                                                                   **
***********************************************************************
FUNFTEXT MVI   MSGFLAG+1,C'T'-X'40'  L.C. T                     GP97266
PROFTEXT LA    R4,L'MSGTEXT  SET MAXIMUM TEXT PRINTABLE
         TM    PROFLAGS,PFWIDE  DOING WIDE?                     GP99113
         BZ    PROFTEX3      NO                                 GP99113
         LA    R4,2*(1+L'MSGTEXT)  WIDER IS BETTER?             GP99113
PROFTEX3 LTR   R3,R3         VALID LENGTH?                      GP96081
         BP    PROFTEXL      YES                                GP96081
         MVC   MSGTEXT(3),=C'L=?'   MAKE AN ERROR MESSAGE       GP96081
PROFTEXL BASR  R6,(0)        SET RETURN POINT                    92271
         LTR   R3,R3         ANY MORE ?
         BNP   DBTLAST       NO; EXIT
         LR    R0,R2         NEXT DISPLAY ADDRESS               GP09277
         S     R0,@BASE      LESS START                         GP09277
         BNP   PROFTXT1      SKIP FIRST TIME                    GP09277
         ST    R0,DB         SAVE                               GP09277
         UNPK  MSGLABL+2(7),DB+1(4)                             GP09277
         TR    MSGLABL+2(6),DBTHEXTR                            GP09277
         MVI   MSGLABL+8,C' '                                   GP09277
         MVI   MSGLABL+1,C'+'
PROFTXT1 LR    R15,R4        SET TEXT LENGTH
         CR    R3,R15        IS REQUEST LONGER ?
         BNL   *+6           YES
         LR    R15,R3        ELSE USE SHORTER
         BCTR  R15,0         SET EXECUTE LENGTH
         EX    R15,PROFTMVC  MOVE TO LINE
         EX    R15,PROFTTR   MAKE PRINTABLE                     GP00162
         LA    R1,MSGTEXT+1(R15)  GET END OF LINE+1             GP00162
         MVI   0(R1),C'<'    SHOW WHERE TEXT ENDS               GP00162
         AR    R2,R4         UP TEXT ADDRESS
         SR    R3,R4         SET RESIDUAL PRINT LENGTH
         B     DBTLAST       PRINT IT
PROFTMVC MVC   MSGTEXT(0),0(R2)  MOVE USER'S TEXT
PROFTTR  TR    MSGTEXT(0),TRTABTN  LIMIT TO PRINTABLES          GP00162
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  ADJUST FOR MULTIPLE (LIST) ENTRIES IN PARAMETER LIST             **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DBTLIST  LA    R3,REQSIZE    SET POSITIVE                       GP95235
         BAS   R6,DBTLAST    PRINT PRIOR LINE, IF ANY           GP95235
         ICM   R9,15,2(R9)   LOAD NEW LIST ADDRESS              GP95235
         B     INITSHRT      SHOW NAME                          GP95235
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PROCESS THE CURRENT RECORD PER USER'S REQUEST                    **
**    SKIP COMPLETELY BLANK LINES                                    **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
FORMFAIL MVC   MSGTEXT+20(21),=C' ** PGM EXCEPTION ** '         GP10092
         SR    R3,R3         TERMINATE THE FUNCTION             GP10092
         SPACE 1                                                GP99113
DBTLAST  CLC   MSGLABL(MSGRTDS-MSGLABL),MSGLABL-1  ANYTHING TO PRINT ?
         BE    DONEOUT       NO; SKIP WHEN NO DATA              GP99113
         TM    PROFLAGS,PROFLWTO  USE WTO RATHER THAN PRINT?    GP95240
         BNZ   DOWTO         YES                                GP95240
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  PRINT RECORD TO ANY DCB OPEN FOR QSAM OUTPUT                     **
**    (CURRENTLY DATA MODE IS NOT SUPPORTED)                         **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DOPUT    ICM   R0,15,SUBT@PRT      USE @PRINTER ?               GP12327
         BZ    DOPUT#P               NO                         GP12327
*LONG    PRTV  MSGRDW                                           GP12327
         LA    R0,6                                             GP12327
         LA    R1,MSGRDW                                        GP12327
         L     R15,SUBT@PRT  GET PRINTER SERVICE                GP12327
         BASR  R14,R15                                          GP12327
         B     DONEOUT                                          GP12327
DOPUT#P  #PRT  MSGRDW,TYPE=VCON  PRINT RECORD                   GP09275
         B     DONEOUT       JOIN COMMON                        GP99113
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  WRITE RECORD WITH WTO                                            **
**    NOTE THAT WE DON'T REMOVE THE CARRIAGE CONTROL CHARACTER       **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DOWTO    MVC   MSGRTDS(4),=X'01000020'   DESCRIPTOR 8; ROUTCDE 11
         WTO   MF=(E,MSGRDW)   WRITE TO CONSOLE                  95067
         SPACE 1                                                GP99113
***********************************************************************
**                                                                   **
**  DONE PRINTING.                                                   **
**                                                                   **
**  1) WHEN R3 IS NON-ZERO (RESIDUAL INPUT LENGTH), CONTINUE PROCESS **
**                                                                   **
**  2) BUMP ITEM POINTER (R9) AND FORMAT IT IF VALID                 **
**                                                                   **
**  OTHERWISE CLEAN UP AND RETURN TO CALLER                          **
**                                                                   **
***********************************************************************
         SPACE 1                                                GP99113
DONEOUT  MVC   MSGLABL(MSGRTDS2-MSGLABL),MSGLABL-1  BLANK IT    GP09277
         MVC   MSGHEAD,MSGLABL  CLEAR MESSAGE NUMBER             93357
*OLD*    MVI   MSGHEAD+5,C'+' INDICATE CONTINUATION              93357
         SPACE 1                                                GP99113
LASTSHRT LTR   R3,R3         ANY MORE TO DO ?                   GP95235
         BPR   R6            RESTART
DBTEXIT  LA    R0,0          RESET RECOVERY ADDRESS             GP10092
         ST    R0,SUBTREST     INTO SPIE EXIT                   GP10092
         TM    REQFLAGS,RQTEOL  END OF LIST FLAG ?              GP02364
         BNZ   DBTEXITS      YES; STOP NOW                      GP02364
         CLI   CURFLAGS,0    ANY SPECIAL REQUESTS LEFT?         GP10092
         BNE   DBTLOOP       YES; DO THEM NOW                   GP10092
*OLD*    TM    REQTYPE+1,RFNONAM NAME PRESENT ?                 GP09277
*OLD*    BNZ   *+8           YES; NORMAL SIZE                   GP09277
*OLD*    LA    R9,8(,R9)     ALLOW FOR LABEL                    GP09277
*OLD*    LA    R9,REQSIZE(,R9)    SKIP TO NEXT OPTION ENTRY     GP09277
         B     ITEMLOOP      AND DO IT                          GP10092
DBTEXITS TM    PROFLAGS,PROFLEND  DID WE HAVE A CLOSE CALL ?    GP98222
         BZ    SUBTREXT      NO                                 GP98222
         ICM   R0,15,SUBT@PRT      USE @PRINTER ?               GP12327
         BNZ   SUBTREXT              YES; BUT NO CLOSE          GP12327
         #PRT  TYPE=CLOSE,DEV=1 CLOSE DCB                       GP98222
SUBTREXT L     R13,4(,R13)   UPLEVEL                            GP12327
         LM    R14,R12,12(R13)    RESTORE ALL REGISTERS         GP12327
         BSM   0,R14                                            GP12327
         SPACE 1                                                GP99113
         LTORG ,                                                GP99113
         SPACE 1             NOTE X'80' IN SPICA TO SUPPRESS STAE MSGS
DB       DC    D'0'          WORK SPACE
REGWORK  DC    16A(0)        WORK SPACE FOR REG OPTION          GP96101
PRINTWRK #PRTWRK DDNAME=SYSDEBUG,REPCH=C'.'                     GP99113
@HEADERS DC    A(PAGE#,TIT1,X'80000000'+TIT2,0) TITLES          GP09277
TIT1     VCON  '1',END=TIT1END
         DC    CL48' '
         DC    CL73'TRAP - Diagnostic output'
         DC    C'PAGE',C' '
PAGE#    DC    C'0000'       CURRENT PAGE NUMBER
         VCON  *END
TIT2     VCON  '0    '
         SPACE 1                                                GP96109
BLANKS   DC    CL80' '                                          GP09277
ADDRMASK DC    0A(0),X'00FFFFFF'  ADDRESS MODE MASK             GP12327
SUBSAVE  DC    18A(0)                                           GP12327
FORMPSW  DC    D'0'          FORMAT: PSW                        GP12327
FORMLNIC DC    F'0'          FORMAT: LEN / INT. CODE            GP12327
FORMREGS DC    (18*4)A(0)    FORMAT: REGISTERS                  GP12327
ERRREGS  DC    16A(0)        (USR) REGISTERS                    GP12327
NUMTODO  DC    F'0'          NUMBER OF ENTRIES                  GP09277
@BASE    DC    A(0)          START ADDRESS FOR OFFSET CALC      GP09277
         SPACE 1
CALFLAGS DC    X'00'         CALL OPTIONS                       GP10092
CALCOUNT DC    X'00'         NUMBER OF ITEMS TO FORMAT          GP12327
CURFLAGS DC    X'00'         COPY OF CALFLAGS                   GP10092
REQFLAGS DC    X'0000'       ITEM REQUEST FLAGS                 GP09277
REQTYPE  DS    X             REQUEST TYPE                       GP95235
REQFG    DC    AL1(0)        OPTION FLAGS                       GP95235
REQADDR  DS    S             ADDRESS OF VARIABLE                GP95235
REQLEN   DS    Y             LENGTH OF VARIABLE                 GP95235
REQNAME  DS    0CL8          VARIABLE NAME FOR RTVAR TYPE       GP95235
REQSIZE  EQU   *-REQTYPE     LENGTH OF ONE ENTRY (EXC VARIABLE AND TAG)
         SPACE 1
PROFLAGS DC    X'00'         LOCAL PROCESSING FLAGS             GP99113
PFWIDE   EQU   X'80'           PROCESSING WIDE LINES            GP99113
PROFLWID EQU   X'80'           PROCESSING WIDE LINES            GP99113
PFDUAL   EQU   X'40'           SHOW HEX + TEXT                  GP00162
PROFLPRO EQU   X'20'           PRODUCTION MODE                  GP00162
PROFLWTO EQU   X'10'           WTO FORCED                       GP00162
PROFLEND EQU   X'02'           CLOSE CALL                       GP00162
PFCANC   EQU   X'01'           GLOBAL CANCEL OF FUNCTION        GP00162
PFGBL    EQU   (PFCANC)        FLAGS RETAINED GLOBALLY          GP99113
         SPACE 1
PROFGMOD DC    X'00'                                            GP09277
PROF@LCL EQU   X'01'           PRT OPEN DONE
PROF@WTO EQU   X'10'           NO PRT; USE WTO
PROF@DCB EQU   X'40'           #PRT DCB IS OPEN
PROFDIAG EQU   X'80'           LIST IS 'name',SL(addr),AL2(len) GP12327
         SPACE 2
DBTHEXTB DC    C'0123456789ABCDEF'   1/2                        GP97265
DBTHEXTR EQU   DBTHEXTB-C'0'         2/2                        GP97265
MSGRDW   DC    Y(MSGRTDS-*),X'8000'   WTO HEADER                GP96081
MSGMAX   DC    CL133' '      ASSURE REQUIRED SPACE              GP99113
         ORG   MSGMAX        REDEFINE                           GP99113
MSGRTDSC DC    C' '          CARRIAGE CONTROL                   GP99113
MSGHEAD  DC    CL8'ESP666',C' '  DEBUG HEADER
MSGLABL  DC    CL8' ',C' '   USER'S LABEL                       GP97265
MSGFLAG  DC    CL2' ',C' '   ANNOTATION/ERROR MSGS              GP97265
MSGTEXT  DC    CL36' '       USER'S HEX OR EBCDIC TEXT          GP97265
         DC    C' '          EXTRA FOR UNPACK                    94011
MSG@TCB  DC    CL8' ',C' '   CURRENT TCB ADDRESS+PADDING FOR UNPK
MSGRTDS  DC    X'01000020'   DESCRIPTOR 8; ROUTCDE 11
         ORG   MSG@TCB         REDEFINE FOR WIDE OPTION         GP99113
         DC    C' '          GROUP SEPARATOR                    GP99113
MSGTEXT2 DC    CL36' '       ANOTHER 4 HEX WORDS                GP99113
         DC    C' '          EXTRA FOR UNPACK                    94011
MSG@TCB2 DC    CL8' ',C' '   CURRENT TCB ADDRESS+PADDING FOR UNPK
         ORG   MSG@TCB2      RE-USE FOR WIDE DUAL OPTION        GP00162
         DC    C' '          LEFT FRAME *                       GP00162
MSGHTEX  DC    CL32' '       TEXT FOR HEX DUAL OPTION           GP00162
         DC    C' '          RIGHT FRAME *                      GP00162
         ORG   ,                                                GP99113
MSGRTDS2 DC    X'01000020'   DESCRIPTOR 8; ROUTCDE 11           GP99113
         SPACE 1
*   THIS TABLE DEFINES ALL TN/T11 CHARACTERS                    GP00162
*                                                               GP96101
TRTCHAR  TRTAB CODE=TN,FILL=X'FF'  0 FOR VALID CHARS            GP00162
TRTABTN  TRTAB CODE=TN,FILL=C'.'   VALID CHARS                  GP09277
         SPACE 1                                                GP95235
LSTWORK  DSECT ,             INPUT REQUEST LIST MAPPING         GP95235
LSTTYPE  DS    X             REQUEST TYPE                       GP95235
RTREGS   EQU   0               REGISTER LISTING                 GP96101
RTTEXT   EQU   1               TEXT STRING                      GP95235
RTCHEX   EQU   2               PRINTABLE TEXT, ELSE HEX         GP96101
RTHEX    EQU   3               HEXADECIMAL                      GP95235
RTPACK   EQU   4               PACKED DECIMAL                   GP96081
RTVAR    EQU   8               VARIABLE NAME (+ DATA REQUEST)   GP95235
RQTEOL   EQU   X'80'           END OF LIST FLAG                 GP02364
RQTMSK   EQU   X'0F'           END OF LIST MASK                 GP02364
         SPACE 1                                                GP95235
LSTFG    DC    AL1(0)        OPTION FLAGS                       GP95235
RFIAD    EQU   1               INDIRECT ADDRESS SUPPLIED        GP96101
RFA24    EQU   2               FORCE 24-BIT MODE ADDRESSING     GP96101
RFA31    EQU   4               FORCE 31-BIT ADDRESSING MODE     GP96101
RFNONAM  EQU   X'80'           NAME FIELD OMITTED               GP09277
         SPACE 1                                                GP95235
LSTADDR  DS    S             ADDRESS OF VARIABLE                GP95235
LSTLEN   DS    Y             LENGTH OF VARIABLE                 GP95235
LSTNAME  DS    0CL8          VARIABLE NAME FOR RTVAR TYPE       GP95235
         PRINT NOGEN                                            GP95235
         SPACE 1                                                 93185
         AIF   (&MVSXA).XAPIE                                   GP09052
         IHAPIE ,                                               GP09052
EPIEPSW  EQU   PIEPSW,8,C'X'                                    GP09277
         AGO   .CMPIE                                           GP09052
.XAPIE   IHAEPIE ,           MAP EPIE                            93185
.CMPIE   SPACE 1                                                 93185
SUBTRAP  CSECT ,                                                GP09277
SPMTXT1  VCON  'XXXXXXXX TEXT @ ',END=SPMTXT1X                  GP97316
SPMTXT1A DC    CL8' ',C' '
SPMTXT1B DC    4CL(8+1)' '   FOUR REGS + PAD                     93185
         VCON  ,                                                 93185
         SPACE 1                                                 93185
SPMREG1  VCON  '   GPR 0-3  ',END=SPMREG1X                       93185
SPMREG1A DC    4CL(8+1)' '   FOUR REGS + PAD                     93185
         VCON  ,                                                 93185
SPMREG2  VCON  '   GPR 4-7  ',END=SPMREG2X                       93185
SPMREG2A DC    4CL(8+1)' '   FOUR REGS + PAD                     93185
         VCON  ,                                                 93185
SPMREG3  VCON  '   GPR 8-B  ',END=SPMREG3X                       93185
SPMREG3A DC    4CL(8+1)' '   FOUR REGS + PAD                     93185
         VCON  ,                                                 93185
SPMREG4  VCON  '   GPR C-F  ',END=SPMREG4X                       93185
SPMREG4A DC    4CL(8+1)' '   FOUR REGS + PAD                     93185
         VCON  ,                                                 93185
         SPACE 1                                                 *TSM*
         LTORG ,                                                 93221
         IHAPSA ,                                               GP95235
         IKJTCB ,
         IHARB ,
         IHACDE ,
         DCBD  DEVD=DA,DSORG=PS                                 GP95235
         IEFUCBOB ,                                             GP99113
         MAPMTS ,                                                93185
         IHAASCB ,                                               83324
         IEFJESCT ,                                              87280
         IEFJSCVT ,                                              87280
         IEFJSSIB ,                                              87280
         YREGS ,
         END   ,
