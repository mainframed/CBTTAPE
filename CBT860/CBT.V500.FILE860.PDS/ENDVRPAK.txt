ENDVRPAK TITLE 'E N D V R P A K  ***  ENDEVOR GLOBAL PACKAGE SUMMARY'
***********************************************************************
*                                                                     *
*   THIS MODULE READS THE EXTRACT DATASET PRODUCED BY ENDEVOR         *
*   PACKAGE EXTRACT #70, AND PRODUCES A SUMMARY FOR THIS SECTION'S    *
*   PACKAGES.                                                         *
*                                                                     *
*   EXTRACT INPUT IS ON DD EXTRACT; SUMMARY OUTPUT ON SYSPRINT        *
*     OPTIONAL OUTPUT ON INAPP (PACKAGES AWAITING APPROVAL)           *
*     OPTIONAL OUTPUT ON INEDIT  (PACKAGES WAITING TO BE CAST)        *
*     OPTIONAL OUTPUT ON DENIED  (PACKAGES REJECTED, WITH NOTES)      *
*   ALL OUTPUT FILES IN PRINT FORMAT FB/VB A/M,133/137                *
*                                                                     *
***********************************************************************
         MACRO ,
&NM      ENDEXT70 &DSECT=,&PFX=X70,&RECNAME=X70REC,&LIST=NO
.*    MAPPING OF ENDEVOR REPORT OUTPUT FOR ELEMENT CATALOG CONRPT03
.*
         LCLC  &N,&SECT,&P
         PUSH  PRINT
         PRINT &LIST.GEN
&N       SETC  '&NM'
&P       SETC  '&PFX'
         AIF   ('&N' NE '').HAVENM
&N       SETC  '&P'.'DSECT'
.HAVENM  AIF   ('&DSECT' EQ 'NO').NOSECT
&SECT    SETC  '&SYSECT'
&N       DSECT ,
         AGO   .NONAME
.NOSECT  AIF   ('&N' EQ '').NONAME
&N       DS    0C
.NONAME  AIF   ('&RECNAME' EQ '').NORECNM
&RECNAME DS    0CL981        PACKAGE EXTRACT RECORD
.NORECNM ANOP  ,
&P.RDW   DC    Y(981,0)      RECORD DESCRIPTOR WORD
&P.LEN   DC    Y(977,0)      ANOTHER RECORD DESCRIPTOR WORD
&P.RCTYP DC    X'14'         RECORD TYPE?
         DC    XL8'0'          ?
&P.PACK  DC    CL8' ',CL8' '   PACKAGE ID
         DC    XL11'0'         ?
         DC    XL08'0'         ?  TIME STAMP OR FOOTPRINT
&P.UID1  DC    CL8' '          FIRST USER ID
         DC    XL08'0'         ?  TIME STAMP OR FOOTPRINT
&P.UID2  DC    CL8' '          SECOND USER ID
         DC    XL04'0'         ?
         DC    XL08'0'         ?  TIME STAMP OR FOOTPRINT
&P.UID3  DC    CL8' '          THIRD USER ID
         DC    XL08'0'         ?  TIME STAMP OR FOOTPRINT
         DC    XL08'0'         ?  TIME STAMP OR FOOTPRINT
         DC    XL08'0'         ?  TIME STAMP OR FOOTPRINT
         DC    XL48'0'         ?
&P.DESC  DC    CL50' '          PACKAGE DESCRIPTION
         DC    CL01' '         ?
         DC    XL05'0'         ?
         DC    XL08'0'         ?  TIME STAMP OR FOOTPRINT
         DC    XL22'0'         ?
         DC    CL05' '         ?
&P.DTE1  DC    CL7' '          FIRST DATE
&P.TME1  DC    CL5' '          FIRST TIME
         DC    CL10' '         ?
&P.STAT  DC    CL32' '         PACKAGE STATUS
&P.BACK  DC    CL10' '         BACKOUT OPTION
&P.DTE2  DC    CL7' '          SECOND DATE
&P.TME2  DC    CL5' '          SECOND TIME
         DC    CL05' '         ?
&P.DTE3  DC    CL7' '          THIRD DATE
&P.TME3  DC    CL5' '          THIRD TIME
&P.DTE4  DC    CL7' '          FOURTH DATE
&P.TME4  DC    CL5' '          FOURTH TIME
&P.DTE5  DC    CL7' '          FIFTH DATE
&P.TME5  DC    CL5' '          FIFTH TIME
&P.DTE6  DC    CL7' '          EXEC WINDOW - START DATE
&P.TME6  DC    CL5' '          EXEC WINDOW - START TIME
&P.DTE7  DC    CL7' '          EXEC WINDOW - END DATE
&P.TME7  DC    CL5' '          EXEC WINDOW - END TIME
         DC    CL48' '         ?
         DC    CL01' '         ?
&P.DTE8  DC    CL7' '          MORE DATE
&P.TME8  DC    CL5' '            AND TIME
         DC    XL44'0'         ?
&P.NOT1  DC    CL60' '         NOTE FIELD LINE 1
&P.NOT2  DC    CL60' '         NOTE FIELD LINE 2
&P.NOT3  DC    CL60' '         NOTE FIELD LINE 3
&P.NOT4  DC    CL60' '         NOTE FIELD LINE 4
&P.NOT5  DC    CL60' '         NOTE FIELD LINE 5
&P.NOT6  DC    CL60' '         NOTE FIELD LINE 6
&P.NOT7  DC    CL60' '         NOTE FIELD LINE 7
&P.NOT8  DC    CL60' '         NOTE FIELD LINE 8
         AIF   ('&RECNAME' EQ '').NORECN2
&P.LGNTH EQU   *-&RECNAME   LENGTH OF RECORD
.NORECN2 AIF   ('&SECT' EQ '').NOREST
&SECT    CSECT ,
.NOREST  POP   PRINT
         MEND  ,
         SPACE 2
         PUNCH ' ORDER ENDVRPAK(P) '
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
         PRINT NOGEN
ENDVRPAK PGMHEAD ZERO,BASE=(R11,R12)   DEFINE BASE, SAVE AREA, ETC.
         LA    R0,OUTPUTND-OUTPUT  LENGTH OF OUTPUT LINE
         STH   R0,OUTPUT     INITIALIZE
         MVI   OUTLINE,C' '
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE
         SERVINIT ,          INITIALIZE SERVICES
         SERVLOAD @PRINTER
         LA    R3,=CL8'REPORT'                                  GP03109
         PRTOPEN PR1WORK     OPEN SUMMARY FILE FOR OUTPUT
         BXLE  R15,R15,HAVEOUT1  SKIP ERROR IF OK
         LA    R2,PR1WORK                                       GP03109
BADOUWTO #OPMSG BADOUMSG,LIST=((R3),8,(R2),8)  WTO + INSERT DDN GP03109
         ABEND 1403
BADOUMSG VCON  'UNABLE TO OPEN | FILE |'                        GP03109
         SPACE 1
HAVEOUT1 PRTOPEN PR2WORK,DEV=2  OPEN INAPPROVAL FILE
         BXLE  R15,R15,HAVEOUT2 SKIP ERROR IF OK
         LA    R2,PR2WORK                                       GP03109
         #OPMSG BADOUMSG,LIST=((R3),8,(R2),8)  WTO + INSERT DDN GP03109
HAVEOUT2 PRTOPEN PR3WORK,DEV=4  OPEN IN-EDIT FILE
         BXLE  R15,R15,HAVEOUT3 SKIP ERROR IF OK
         LA    R2,PR3WORK                                       GP03109
         #OPMSG BADOUMSG,LIST=((R3),8,(R2),8)  WTO + INSERT DDN GP03109
HAVEOUT3 PRTOPEN PR4WORK,DEV=4  OPEN DENIED FILE
         BXLE  R15,R15,HAVEOUT4 SKIP ERROR IF OK
         LA    R2,PR4WORK                                       GP03109
         #OPMSG BADOUMSG,LIST=((R3),8,(R2),8)  WTO + INSERT DDN GP03109
         SPACE 1                                                GP03109
HAVEOUT4 PRTV  HEADPAGE,TITLE=1,DEV=1
         PRTV  HED2PAGE,TITLE=1,DEV=2
         PRTV  HED3PAGE,TITLE=1,DEV=3
         PRTV  HED4PAGE,TITLE=1,DEV=4
         PRTV  PHD1,TITLE=2,DEV=1
         PRTV  PHD1B,TITLE=2,DEV=2
         PRTV  PHD1C,TITLE=2,DEV=3
         PRTV  PHD1D,TITLE=2,DEV=4
         PRTV  PHD2,TITLE=3,DEV=(1,2,3,4)
         PRTV  PHD3,TITLE=3,DEV=(1,2,3,4)
         SPACE 1
         DEVTYPE DCBDDNAM-IHADCB+EXTRACT,DB IS THERE A EXTRACT DD ?
         LA    R2,DCBDDNAM-IHADCB+EXTRACT  GET DDN              GP03109
         LA    R3,=CL8'INPUT'                                   GP03109
         BXH   R15,R15,BADOUWTO  NO; WRITE MESSAGE AND QUIT
         OPEN  MF=(E,DCBLIST)  OPEN INPUT
         CALL  SUBDTE,(HEADDATE,HEADTIME),VL
         MVC   HED2DATE,HEADDATE  PROPAGATE TO ALL TITLES
         MVC   HED3DATE,HEADDATE  PROPAGATE TO ALL TITLES
         MVC   HED4DATE,HEADDATE  PROPAGATE TO ALL TITLES
         MVC   HED2TIME,HEADTIME  PROPAGATE TO ALL TITLES
         MVC   HED3TIME,HEADTIME  PROPAGATE TO ALL TITLES
         MVC   HED4TIME,HEADTIME  PROPAGATE TO ALL TITLES
         L     R1,PSATOLD-PSA  LOAD CURRENT TCB
         L     R1,12(,R1)    LOAD TCBTIO
         MVC   HEADSECT,0(R1)  COPY JOB SECTION FROM (NON-PRODUCTION)
         MVC   HED2SECT,HEADSECT  PROPAGATE TO ALL TITLES
         MVC   HED3SECT,HEADSECT  PROPAGATE TO ALL TITLES
         MVC   HED4SECT,HEADSECT  PROPAGATE TO ALL TITLES
         SPACE 1
GETSOME  BAS   R9,GETREC     GET NEXT EXTRACT RECORD
         USING INSECT,R10    DECLARE MAPPING
         SPACE 1
         MVC   OUTPACK,INPACK  COPY THE PACKAGE ID
         MVC   OUTCUID,INUID1  CASTER
         MVC   OUTCDTE,INDTE1
         MVC   OUTCTME,INTME1
         MVC   OUTSTAT,INSTAT  PACKAGE STATUS
         MVC   OUTDESC,INDESC  PACKAGE DESCRIPTION
         PRTV  OUTPUT        PROCESS OUTPUT LINE
         CLC   =C'IN-APP',INSTAT  AWAITING APPROVAL?
         BE    PUTAPP        YES; PRINT TOO
         CLC   =C'IN-EDI',INSTAT  AWAITING EDITING/CASTING?
         BE    PUTEDIT       YES; PRINT TOO
         CLC   =C'DENIED',INSTAT  SHOT DOWN?
         BNE   GETSOME       NO; JUST GO FOR MORE
         LA    R2,2          SET FOR KEEP VALUE (SPACER; DATA LINE)
         LA    R3,INNOT1     POINT TO NOTE SECTION
         LA    R4,L'INNOT1   LENGTH OF ONE NOTE
         LA    R5,INNOT8     MAXIMUM NOTES
NOTBLOOP MVC   OUTNOTE,0(R3)  MOVE NOTE TO OUTPUT
         CLC   OUTNOTE,OUTNOTE-1  ALL BLANK ?
         BE    NOTBBUMP      YES; DON'T PRINT
         LA    R2,1(,R2)     INCREASE LINE COUNT
NOTBBUMP BXLE  R3,R4,NOTBLOOP  TRY AGAIN
         PRTROOM (R2)        KEEP STATUS AND NOTES ON SAME PAGE
         PRTSPACE 1,DEV=4    SET FOR DOUBLE-SPACE
         PRTV  OUTPUT,DEV=4  WRITE TO DENY FILE
         LA    R3,INNOT1     POINT TO NOTE SECTION
         LA    R4,L'INNOT1   LENGTH OF ONE NOTE
         LA    R5,INNOT8     MAXIMUM NOTES
NOTELOOP MVC   OUTNOTE,0(R3)  MOVE NOTE TO OUTPUT
         CLC   OUTNOTE,OUTNOTE-1  ALL BLANK ?
         BE    NOTEBUMP      YES; DON'T PRINT
         PRTV  NOTELINE,DEV=4  WRITE TO DENIED FILE
NOTEBUMP BXLE  R3,R4,NOTELOOP  TRY AGAIN
         B     GETSOME       GO FOR MORE
         SPACE 1
PUTAPP   PRTSPACE 1,DEV=2    SET FOR DOUBLE-SPACE
         PRTV  OUTPUT,DEV=2  WRITE TO APPROVAL FILE
         B     GETSOME       GO FOR MORE
         SPACE 1
PUTEDIT  PRTSPACE 1,DEV=3    SET FOR DOUBLE-SPACE
         PRTV  OUTPUT,DEV=4  WRITE TO EDIT FILE
         B     GETSOME       GO FOR MORE
         SPACE 1
EODAD    CLOSE MF=(E,DCBLIST)
         SERVTERM ,          CLOSE AND FREE EVERYTHING
         L     R9,RETCODE    LOAD RETURN CODE
         PGMEXIT RC=(R9)     RETURN WITH CC
         SPACE 1
GETREC   GET   EXTRACT       READ A RECORD FROM THE PRODUCTION FILE
         LR    R10,R1        COPY TO DEFINED REGISTER
         CLI   INRCTYP,X'14' IS IT THE EXPECTED PACKAGE RECORD?
         BNE   GETREC        NO; SKIP IT
         CLC   HEADSECT,INPACK+3  IS THE PACKAGE FOR THIS SECTION?
         BNE   GETREC        NO; JUST GET NEXT RECORD
         BR    R9            AND RETURN
         SPACE 2
         LTORG ,
         SPACE 1
DCBLIST  OPEN  (EXTRACT,(INPUT,REREAD)),MF=L
HEXTAB   DC    C'0123456789ABCDEF'     1/2
HEXTRTAB EQU   HEXTAB-C'0'             2/2
         SPACE 1
EXTRACT  DCB   DDNAME=EXTRACT,DSORG=PS,MACRF=GL,EODAD=EODAD
         SPACE 1
PR1WORK  PRTWORK SYSPRINT,TITLE=5
PR2WORK  PRTWORK INAPP,TITLE=5
PR3WORK  PRTWORK INEDIT,TITLE=5
PR4WORK  PRTWORK DENIED,TITLE=5
         SPACE 1
PHD1     VCON  '1',END=PHD1X
         DC    CL30' '
         DC    C'SECTION '
HEADSECT DC    C'GP'
         DC    CL48' PACKAGE SUMMARY'
         DC    CL6' '
HEADDATE DC    CL10' ',X'4081A340'
HEADTIME DC    CL8' ',C'  PAGE' RUN DATE
HEADPAGE DC    CL4' '
         VCON  *END
         SPACE 1
PHD1B    VCON  '1',END=PHD1BX
         DC    CL30' '
         DC    C'SECTION '
HED2SECT DC    C'GP'
         DC    CL48' PACKAGES AWAITING APPROVAL'
         DC    CL6' '
HED2DATE DC    CL10' ',X'4081A340'
HED2TIME DC    CL8' ',C'  PAGE' RUN DATE
HED2PAGE DC    CL4' '
         VCON  *END
         SPACE 1
PHD1C    VCON  '1',END=PHD1CX
         DC    CL30' '
         DC    C'SECTION '
HED3SECT DC    C'GP'
         DC    CL48' PACKAGES AWAITING CAST'
         DC    CL6' '
HED3DATE DC    CL10' ',X'4081A340'
HED3TIME DC    CL8' ',C'  PAGE' RUN DATE
HED3PAGE DC    CL4' '
         VCON  *END
         SPACE 1
PHD1D    VCON  '1',END=PHD1DX
         DC    CL30' '
         DC    C'SECTION '
HED4SECT DC    C'GP'
         DC    CL48' PACKAGES DENIED'
         DC    CL6' '
HED4DATE DC    CL10' ',X'4081A340'
HED4TIME DC    CL8' ',C'  PAGE' RUN DATE
HED4PAGE DC    CL4' '
         VCON  *END
         SPACE 1
PHD2     VCON  '-',END=PHD2X
         DC    CL10'PACKAGE'  PACKAGE ID
         DC    CL6'USER'     CAST USER
         DC    CL8'DATE CAS' CAST DATE
         DC    CL7'T TIME'   CAST TIME
         DC    CL34'STATUS'  PACKAGE STATUS
         DC    CL52'DESCRIPTION'
         VCON  *END
         SPACE 1
PHD3     VCON  '0'
         SPACE 1
NOTELINE VCON  ' ',END=NOTELINX
         DC    CL40' '         INDENT
OUTNOTE  DC    CL(L'INNOT1)' '   OUTPUT NOTE LINE
         VCON  *END
         SPACE 1
INSECT   ENDEXT70 DSECT=,LIST=,PFX=IN,RECNAME=INREC  MAP INPUT RECORD
         SPACE 1
PATCHER  DC    30S(*)        MINI-PATCH
         SPACE 1
SAVE     DSECT ,             SAVE/WORK AREA
DB       DC    D'0'          PL8
RETCODE  DC    F'0'          PROGRAM RETURN CODE
@SERVICE DS    A             SERVICE ROUTINE
@PRINTER DS    A             PRINTING ROUTINE
CALLPARM DS    10A           SUBWTO CALLING PARAMETER LIST      GP03109
         SPACE 1
PROFG    DC    X'00'         GROUP FLAG
FGBUG    EQU   X'20'           DEBUG FLAG
         SPACE 1
OUTPUT   VCON  ' ',END=OUTPUTND
OUTLINE  DC    CL132' '      OUTPUT LINE
         ORG   OUTLINE
OUTPACK  DS    CL8,CL2       ELEMENT NAME
OUTCUID  DS    CL5,C         CAST USER
OUTCDTE  DS    CL7,CL1       CAST DATE
OUTCTME  DS    CL5,CL2       CAST TIME
OUTSTAT  DS    CL32,CL2      PACKAGE STATUS
OUTDESC  DS    CL50,CL2      PACKAGE DESCRIPTION
         ORG   ,
         VCON  *END
SAVEEND  EQU   *             SAVE/WORK AREA END
         SPACE 1
         PRINT NOGEN
         CVT   DSECT=YES
         DCBD  DEVD=DA,DSORG=PS
         IHAPSA ,
         IHACDE ,
         END   ,
