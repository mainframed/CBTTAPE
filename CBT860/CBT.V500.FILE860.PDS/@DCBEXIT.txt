@DCBEXIT TITLE 'GENERAL PURPOSE DCB EXIT'                        82178
         SPACE 2
***********************************************************************
*                                                                     *
*              COPYRIGHT 1978 BY SHMUEL (SEYMOUR J.) METZ             *
*                          ALL RIGHTS RESERVED.                       *
*                                                                     *
***********************************************************************
*                                                                     *
*  @DCBEXIT SERVES AS A GENERALIZED DCB EXIT, COMPLETING DCB FIELDS.  *
*    REQUIRES DCB EXIT LIST, WITH POINTER TO PARAMETER LIST MAPPED    *
*    BY MACRO DCBEXITD.                                               *
*                                                                     *
*  ATTRIBUTES:  REUS, RENT, REFR, AM24, RM24                          *
*                                                                     *
*  ALII: @DCBVECT, @DCBXLST, DCBEXIT (OLD NAME)                       *
*                                                                     *
***********************************************************************
*  MODIFICATIONS:                                                     *
*                                                                     *
*  2000/01/06  GYP  CORRECTED TRKCALC CALL (R=0 FAILS; NEEDS R=1)     *
*                   STARTED ADDING AM31 SUPPORT, BUT NOT TESTED YET.  *
*                                                                     *
***********************************************************************
         SPACE 1
*             THIS ROUTINE IS NOT TO BE DISTRIBUTED WITHOUT PERMISSION,
*             AS DESCRIBED IN MEMBER $$RIGHTS.
         SPACE 2
         MACRO ,
&L       SAVEX &R1,&R3,&LOC,&TYPE=*,&SETAM=,&WORK=R14           GP99018
         GBLB  &ZZSVBSM,&MVSESA
         LCLC  &NM
&NM      SETC  '&L'
.*
.*     COPYRIGHT 1981 BY EXPERT SYSTEMS PROGRAMMING INC.
.*     COPYRIGHT 1998-2005 EXPERT SYSTEMS PROGRAMMING
.*                       176 OLD STAGE COACH ROAD
.*                       BRADFORD, VT 05033-8844
.*
.*                        ALL RIGHTS RESERVED.
.*
.*             THIS MACRO IS NOT TO BE DISTRIBUTED WITHOUT PERMISSION,
.*             AS DESCRIBED IN MEMBER $$RIGHTS.
.*
.*       CODE ADDED TO PRESERVE CALLER'S AMODE AND OPTIONALLY SET AMODE
.*                                                              GP99018
         AIF   ('&TYPE' EQ 'BSM').BSM                           GP98322
         AIF   ('&TYPE' EQ 'STM').STM
         MNOTE 8,'SAVEX: TYPE=&TYPE UNKNOWN - TYPE=STM ASSUMED'
         AGO   .STM
.BSM     ANOP  ,                                                GP98322
&ZZSVBSM SETB  1             SET FLAG FOR ENDM
&NM      BSM   R14,0         GET CALLER'S AMODE                 GP98322
&NM      SETC  ''            DONE WITH LABEL
.STM     ANOP  ,
         USING &SYSECT,R15
&NM      SLL   R15,8         KILL GARBAGE BYTES IN ADDRESS REG  GP99034
&NM      SETC  ''            DONE WITH LABEL
         SRL   R15,8         FIX UP FOR AM24 AND AM31           GP99034
         SLL   R1,8          REMOVE OPEN BITS FROM DCB          GP05017
         SRL   R1,8                                             GP05017
         TM    DCBOFLGS-IHADCB(R1),DCBOFEOV  EOV - UNLIKE ATTR? GP05017
         BO    *+8                       YES - SAVE ONLY RETURN
         STM   &R1,&R3,&LOC              NO  - SAVE ALL
         DROP  R15                                              GP05017
         AIF   ('&SETAM' EQ '' OR '&SETAM' EQ 'ANY').MEND       GP04234
         AIF   (NOT &MVSESA).MEND                               GP04234
         BASR  R14,0                                            GP04050
         USING *,R14                                            GP04050
         LA    &WORK,ZZSV&SYSNDX                                GP99018
         AIF   ('&SETAM' EQ '24' OR '&SETAM' EQ 'AM24').SETCM   GP99018
         LA    R0,1                                             GP99018
         SLL   R0,31         MAKE 80000000                      GP99018
         OR    &WORK,R0                                         GP99018
         AIF   ('&SETAM' EQ '31' OR '&SETAM' EQ 'AM31').SETCM   GP99018
   MNOTE 8,'SAVEX: UNSUPPORTED SETAM VALUE: &SETAM - AM31 ASSUMED'
.SETCM   BSM   0,&WORK       CHANGE TO REQUIRED MODE            GP99018
         DROP  R14                                              GP04050
ZZSV&SYSNDX DS 0H                                               GP99018
.MEND    MEND  ,                                                GP99018
         SPACE 1                                                GP04234
         PUNCH '  ALIAS @DCBXLST,@DCBVECT,DCBEXIT '             GP04234
         SPACE 1                                                GP04234
         GBLB  &ZZSVBSM       FLAG FOR PGMEXIT                  GP04234
         SPACE 2                                                 81203
         COPY  OPTIONGB                                          81146
         SYSPARM LIST=YES                                        81158
*DEBUG*  PRINT &PRTSOR                                           81146
         MTITL 'GENERAL PURPOSE DCB EXIT'
*              THIS ROUTINE IS INVOKED BY OPEN AS A DCB EXIT,
*              ENTRY CODE 05 OR 85 IN THE EXIT LIST.
*              PARAMETERS, IF ANY, ARE POINTED TO BY A NOP,
*              ENTRY CODE 00 OR 80 IN THE EXIT LIST.
*              THE PARAMETER POINTER IS DISTINGUISHED FROM A
*              TRUE NOP ENTRY BY THE FACT THAT IT POINTS TO THE
*              EBCDIC CONSTANT 'DCBEXITP'; AS A CONSEQUENCE,
*              THE ADDRESS IN A NOP ENTRY MUST STILL BE VALID.
@DCBEXIT PGMHEAD COPYREGS,ZERO,BASE=R12,PARM=R11,AM=24,RM=24    GP99034
DCBEXIT  EQU   @DCBEXIT                                          82214
         ENTRY @DCBVECT                                          83069
         ENTRY DCBEXIT                                           82214
         ENTRY @DCBXLST
         USING IHADCB,R11
         AM24  WORK=R15      OLD, OLD, OLD CODE                 GP99026
         LTCB  R3,USE=YES                                        90278
         SPACE 2                                                 81203
         TM    DCBOFLGS,DCBOFEOV        EOV - UNLIKE ATTR. ?     81203
         BZ    NOTEOV                    NO  - SAVE IS OK        81203
         L     R5,TCBRBP                 YES - GET REGS          83069
         USING RBSECT,R5                                         83069
         L     R5,RBLINK                                         83069
         MVC   SAVE2(SAVE12+4-SAVE2),RBGRS2                      81203
         DROP  R5                                                83069
         SPACE 1
*              DCBDEVT IS NOT FILLED IN PRIOR TO DCBEXIT,
*              EXCEPT FOR CERTAIN SPECIFIC CASES.
*              FOR THIS REASON, DEVICE TYPE INFORMATION IS
*              OBTAINED FROM THE TIOT AND UCB (IF ANY).
         SPACE 1
NOTEOV   L     R4,TCBTIO                                         83069
         USING TIOT,R4                                           83069
         LH    R3,DCBTIOT              ASSUME OPEN               83069
         N     R3,=X'0000FFFF'         X/A WITH MANY DD ENTRIES  93051
         ALR   R3,R4                                             83069
         TM    DCBOFLGS,DCBOFUEX       FROM OPEN ?               83069
         BZ    ISOPEN                   YES - GOT TIOT ENTRY     83069
         LA    R3,TIOELNGH              NO  - SEARCH TIOT        83069
         DROP  R4                                              GP97202
         USING TIOELNGH,R3                                       83069
         SLR   R0,R0                   FOR ICM/AR                83069
TIOTLOOP ICM   R0,1,TIOELNGH           DUMMY ENTRY ?             83069
         BZ    NODD                     YES - BLETCH             83069
         CLC   TIOEDDNM,DCBDDNAM       RIGHT ENTRY               83069
         BE    ISOPEN                   YES - GET UCB            83069
         AR    R3,R0                    NO  - SKIP IT            83069
         B     TIOTLOOP                       AND TRY NEXT       83069
         SPACE 1                                                 83069
NODD     SLR   R2,R2                   FLAG NO UCB               83069
         SLR   R3,R3                   FLAG NO TIOT ENTRY        83069
         MVC   DEVBLKSI,MAXOSBLK       SET MAX BLKSI TO MAX      83069
         B     GETEXLST                SKIP UCB LOAD             83069
ISOPEN   LAT   R2,TIOEFSRT-1,LA=0                                83069
         USING UCBOB,R2                                          91329
       DEVTYPE TIOEDDNM,DEVTYPE        GET MAXIMUM BLOCK SIZE    83069
GETEXLST LAT   R5,DCBEXLST,LA=0                                  90278
TESTNTRY TM    0(R5),X'7F'             NOP ENTRY ?
         BNZ   SKIPNTRY                 NO  - TRY NEXT
         LAT   R8,0(R5),SKIPNTRY,LA=0   YES - CHECK FURTHER      90278
         USING DCBEXITD,R8                                       83275
         TM    3(R5),7                 DOUBLEWORD BOUNDARY ?
         BNZ   SKIPNTRY                 NO  - TREF
         LA    R6,MODEL      GET OUR MODEL'S ADDRESS             86296
         CR    R6,R8         IS USER POINTING TO MODEL ?         86296
         BE    MODLNTRY      YES; USE COPY TO STAY REFRESHABLE   86296
         CLC   MDDCBXID,DXDCBXID       ONE OF OURS ?             83069
         BE    GOTENTRY                 YES - USE IT             83069
SKIPNTRY TM    0(R5),X'80'              NO  - TRY NEXT           83069
         LA    R5,4(,R5)               NOT LAST, I HOPE          83069
         BZ    TESTNTRY                 NO  - TRY IT, YOU'LL LIKE IT
MODLNTRY MVC   FAKEX(MODELLEN),MODEL    YES - USE FAKE           86296
         LA    R8,FAKEX                                          83275
         SPACE 1                                                 83069
GOTENTRY ST    R11,DXDCBPTR            SAVE DCB ADDRESS FOR USER
         STM   R2,R3,DXDOUBLE  SAVE UCB AND TIOT ADDRESSES       86296
         ST    R2,STARUCBA   MAKE TRKCALC PARM LIST              86296
         MVI   STARFLGS,STARMAXS+4  MAX BLK + UCB ADDRESS        86296
*        XC    STARRKDD,STARRKDD  CREAM RECORD NUMBER AND LENGTHS
         MVI   STARRKDD,1    REQUEST MAX SIZE IF NO FIT         GP00006
         MVC   STARKL,DCBKEYLE  COPY KEY LENGTH                  86296
         SPACE 1                                                 82178
*        CODE ADDED TO SUPPORT A PRE-@DCBEXIT EXIT. OPLIST=..TPREX
*        WHEN PRESENT, TAKES USER EXIT PRIOR TO ANYTHING ELSE.   82178
*        USED FOR SETTING DCBOPTCD, ETC. AFTER JFCB/DCB MERGE, BUT
*        BEFORE @DCBEXIT RESETS AND CHECKS THINGS. MAY ALSO BE USED
*        TO UPDATE THE DCBEXITD PARAMETER LIST.                  82178
*                                                                82178
         LA    R5,DXOPLIST             POINT TO OPTION LIST      82178
PRELOOP  LAT   R6,0(R5),PRELATE,LA=0   GET EXIT/TABLE LIST ENTRY 90278
         IC    R1,0(,R5)               GET 2ND LEVEL EXIT TYPE   82178
         N     R1,=A(127)                                        82178
         CLM   R1,1,=AL1(DXTPREX)  PRE-DCBEXIT EXIT ?            82178
         BNE   PRELATE       NO; CHECK SOME MORE                 82178
         LR    R1,R11        (DCB)     STANDARD PARM REG         82178
         LR    R0,R8         DCBEXITP WORK AREA                  83275
         LR    R15,R6        COPY EXIT ADDRESS                   82178
         STM   R2,R12,MYR2             SAVE DCBEXIT REGISTERS    82178
         LM    R2,R12,SAVE2            LOAD USER REGISTERS       82178
         BASR  R14,R15       INVOKE USER PRE-EXIT               GP99026
         LM    R2,R12,MYR2             RESTORE DCBEXIT REGISTERS 82178
         B     PREDONE       QUIT CHECKING LIST                  82178
         SPACE 1                                                 82178
PRELATE  TM    0(R5),X'80'             LAST ?                    82178
         LA    R5,4(,R5)                MAYBE                    82178
         BZ    PRELOOP                  NO  - TRY NEXT           82178
         SPACE 1                                                 82178
PREDONE  TM    DXFLAG1,DX1NOPCI        SUPPRESS CHAINED SCHEDULING ?
         BZ    *+8                      NO
         NI    DCBOPTCD,255-DCBOPTC     YES - OFF IT
         LTR   R2,R2                   ANY UCB ?
         BZ    SUPWYLB                  NO; ALSO RESET ISWYL     91025
         TM    DXFLAG3,DX3DFWYL         YES - WYLBUR DEFAULTED ? 81210
         BZ    *+8                          - NO                 81210
         OI    DXFLAG3,DX3ISWYL               YES - ASSUME       81210
         CLI   UCBTBYT3,UCB3DACC       IS IT DASD ?              81210
         BNE   NOSUPSD                  NO                       81210
         MVC   DEVKEYLE+L'DEVKEYLE-1(1),DCBKEYLE  SET KEY LENGTH 86296
         MVC   STARDL,DEVBLKSI  SET TO MAXIMUM RECORD            86296
         AIF   (&SVC@SVC GT 0).MYSVC  USE MY SERVICE ROUTINE     90154
         TRKCALC MF=(E,STARTLE),REGSAVE=YES  DO IT THE HARD WAY  90154
         AGO   .COMCALC                                          90154
.MYSVC   SERVICE DVCAP,STARTLE  GET DEVICE MAXIMUM               86296
.COMCALC LTR   R15,R15       DID THE RECORD FIT ?                86296
         BZ    *+8           YES; LEAVE SIZE ALONE               86296
         STH   R0,DEVBLKSI   SET MAXIMUM SIZE FOR THIS REQUEST   86296
         TM    DXFLAG1,DX1NOSD          YES - SUPPRESS OPTCD=Z ? 81210
         BZ    DEFRECFM                       NO                 81210
         NI    DCBOPTCD,255-DCBSRCHD        - YES - OFF IT       81210
         B     DEFRECFM                                          81210
NOSUPSD  CLI   UCBTBYT3,UCB3TAPE       IS IT TAPE ?              81354
         BNE   SUPWYLB                  NO; RER IRRELEVANT       82109
         TM    DXFLAG2,DX2TARER        RESET REDUCED ERROR RECOVERY ?
         BZ    DEFRECFM                 NO                       81210
         NI    DCBOPTCD,255-DCBOPTZ    RESET REDUCED ERROR RECOVERY
         B     DEFRECFM                                          81354
SUPWYLB  NI    DXFLAG3,255-DX3ISWYL    NEITHER TAPE NOR DASD     82109
DEFRECFM SLR   R1,R1                   CLEAR FOR TRANSLATE       83069
         ICM   R1,1,DCBRECFM           RECFM DEFAULTED ?         83069
         BNZ   RECFMND                  NO                       83080
         ICM   R1,1,DXDRECFM            NO   - ANY DEFAULT ?    GP06290
         BNZ   SETRECFM                 YES; HONOR              GP06290
         CLI   DEVTYPB3,UCB3UREC  UNIT RECORD DEVICE ?          GP06290
         BNE   DFLRECFM      NO                                 GP06290
         LA    R1,DCBRECF    YES; DEFAULT TO RECFM=F            GP06290
         B     SETRECFM      STASH IT                           GP06290
DFLRECFM MVI   DCBRECFM,DCBRECU         YES - ASSUME WYLBUR
         TM    DXFLAG3,DX3DFWYL        IS DEFAULT WYLBUR ?
         BO    RECFMND                  YES  - DEFAULT TO U
         ICM   R1,1,DXDRECFM            NO   - ANY DEFAULT ?     83069
         BZ    *+8                             NO  - LEAVE AS U
SETRECFM STC   R1,DCBRECFM                     YES - SET DEFAULT 83069
         SPACE 1                                                 83069
RECFMND  SRL   R1,5                    TRANSLATE RECFM TO FLAGS  83080
         IC    R0,RECFMTAB(R1)                                   83069
         STC   R0,MYFLAG1                                        83080
         TM    DCBRECFM,DCBRECSB       STANDARD/SPANNED ?        83069
         BZ    *+8                                               83069
         OI    MYFLAG1,F1RECFMS                                  83080
         SPACE 1                                                 83069
         IC    R1,LROFFTAB(R1)         TRANSLATE RECFM TO OFFSET 83069
         TM    DCBRECFM,DCBRECU        RECFM=U ?                 83069
         BO    *+8                      YES                      81218
         NI    DXFLAG3,255-DX3ISWYL     NO  - OFF WYLBUR         81210
         LH    R0,DCBLRECL             IS LRECL DEFAULTED ?      81210
         LTR   R0,R0
         BNZ   LRECLND                  NO
         SLR   R15,R15       SET NOCC/CC OR CC/NOCC ADJUSTMENT   81158
         TM    DCBRECFM,DCBRECCC       DCB CONTROL CHARACTERS ?  81158
         BZ    *+8           NO                                  81158
         LA    R15,1         YES; ADJUST USER'S DEFAULT BY 1     81158
         TM    DXDRECFM,DCBRECCC       USER SPECIFIED CC ?       81158
         BZ    *+6           NO                                  81158
         BCTR  R15,0         YES-ADJUST DEFAULT LRECL DOWN BY 1  81158
*        IF CC=CC OR NOCC=NOCC, R15=0; ELSE + OR - 1             81158
         LTR   R1,R1                   WEIRD DCBRECFM ?          83069
         BZ    LRECLND                  YES - NO  LRECL DEFAULT  83069
         LH    R0,DXBLOCKF(R1)          NO  - GET LRECL DEFAULT  83069
         LTR   R0,R0         ANY ?                              GP06290
         BNZ   DFLLRECC      YES; PROCEED                       GP06290
         CLI   DEVTYPB3,UCB3UREC  UNIT RECORD DEVICE ?          GP06290
         BNE   DFLLRECC      NO                                 GP06290
         L     R0,DEVBLOCK   USE BLOCK AS RECORD SIZE           GP06290
DFLLRECC LTR   R15,R15       ANY CONTROL CHARACTER ADJUSTMENT ?  83069
         BZ    SETLRECC      NO                                  81158
         TM    DXFLAG2,DX2CCERR   SUPPORTED BY USER ?            81158
         BNZ   ABEND         NO - (TEMPORARY) ABEND              81158
         TM    DXFLAG2,DX2CCADJ   ADJUSTMENT REQUESTED ?         81158
         BZ    SETLRECC      NO                                  81158
         LTR   R0,R0         ANY LENGTH SUPPLIED ?               81158
         BZ    SETLRECC      NO; DON'T CLOBBER                   81158
         AR    R0,R15        ADJUST USER'S DEFAULT LENGTH FOR CC 81158
SETLRECC STH   R0,DCBLRECL             DEFAULT LRECL             81158
LRECLND  TM    DCBRECFM,DCBRECU        RECFM = F | V ?
         BNM   OFFRCINT                 NO  - CAN'T BE FBS | VBS
         TM    DCBRECFM,DCBRECV         YES - WHICH ?
         BO    TESTVBS                  V   - DO BFTEK=A TESTS
         TM    DXFLAG1,DX1NOFBS        SHUT OFF FBS ?
         BZ    OFFRCINT                 NO  - REMOVE ANY BFTEK=A
         NI    DCBRECFM,255-DCBRECSB    YES
         NI    MYFLAG1,255-F1RECFMS                              83080
         B     OFFRCINT                 NOW - REMOVE ANY BFTEK=A
TESTVBS  TM    DXFLAG1,DX1RCINT        RECORD INTERFACE REQUESTED ?
         BZ    NOTVBS                   NO  - DON'T TEST FOR VBS
         OI    DCBBFTEK,DCBBFTA        TURN ON BFTEK=A
         TM    DCBRECFM,DCBRECSB       RECFM = VS ?
         BNO   OFFRCINT                 NO  - CAN'T USE REC. INT.
         TM    DCBLRECL,X'80'          LRECL=X ?
         BO    OFFRCINT                 YES - CAN'T USE REC. INT.
         TM    DCBMACR1,DCBMRGET+DCBMRLCG
         BO    USERCINT                ONLY WORKS WITH MACRF=GL  82178
OFFRCINT NI    DCBBFTEK,255-DCBBFTA    TURN OFF BFTEK=A
USERCINT EQU   *
NOTVBS   LA    R5,DXOPLIST             2ND LEVEL EXIT LIST
         CLC   DEVBLKSI,MAXOSBLK       AFFECTED BY BUFL BUG ?    81158
         BL    *+10                     NO  - OK
         MVC   DEVBLKSI,MAXOSBLK        YES - TRUNCATE           81158
THIS2LVL LAT   R6,0(R5),NEXT2LVL,LA=0  GET 2ND LEVEL EXIT ADDRESS
         IC    R1,0(,R5)               GET 2ND LEVEL EXIT TYPE
         N     R1,=A(127)
         CLM   R1,1,=AL1(LVL2MAX)      IN RANGE ?                81158
BUMCODE  BNL   BUMCODE                  NO  - SPIE IN THE SKIE   81158
         ORG   *-2
         DC    S(*-1)
         SLL   R1,2                    SCALE FOR BRANCH TABLE    81158
         B     *+4(R1)
LVL2BRTB B     NEXT2LVL                NOP - GET NEXT EXIT IN LIST
         B     CALL2LVL                2ND LEVEL EXIT
         B     DEFBLKDV                BLOCK SIZE    BY DEVICE   83069
         B     WYLBLKDV                WYLBUR TABLE              83069
         B     NEXT2LVL                PRE-EXIT ALREADY TAKEN    83069
         B     MAXBLKDV                BLOCK MAXIMUM BY DEVICE   83069
         B     EXITFSAV                POST-DCBEXIT EXIT         83275
LVL2MAX  EQU   (*-LVL2BRTB)/4          ONE HIGHER THAN MAX LEVEL2 FLAG
         SPACE 1                                                 83275
EXITFSAV LTR   R6,R6         ANY EXIT ADDRESS ?                  83275
         BZ    NEXT2LVL      NO; IGNORE                          83275
         ST    R6,EXITFADR   SAVE IT                             83275
         OI    MYFLAG3,F3EXITF   SET FINAL EXIT FLAG             83275
         B     NEXT2LVL                                          83275
         SPACE 1
CALL2LVL LR    R1,R11        (DCB)     STANDARD PARM REG         81158
         LR    R0,R8         DCBEXITP WORK AREA                  83275
         LR    R15,R6
         STM   R2,R12,MYR2             SAVE DCBEXIT REGISTERS    81147
         LM    R2,R12,SAVE2            LOAD USER REGISTERS       81147
         BALSR R14,R15                                          GP99026
         LM    R2,R12,MYR2             RESTORE DCBEXIT REGISTERS 81147
*        B     NEXT2LVL
         SPACE 1
NEXT2LVL TM    0(R5),X'80'             LAST ?
         LA    R5,4(,R5)                MAYBE
         BZ    THIS2LVL                 NO  - TRY NEXT
         SPACE 1
         ICM   R0,3,DCBBLKSI           BLKSIZE SPECIFIED ?
         BNZ   NOTFULLT                 YES - DON'T CLOBBER
         TM    DXFLAG3,DX3BLKTB+DX3BLKWY  CANNED BLOCKSIZE TABLES ?
         BZ    NOBLKTB       NO                                  81158
         LA    R6,TABWYBLK   USE WYLBUR SIZE TABLE               81158
         LA    R15,8         SET TABLE ENTRY LENGTH              81158
         BALS  R14,DEFBLKSI  SET BLOCKSIZE BY DEVICE TYPE       GP99026
         STH   R0,DCBBLKSI   SET ENTRY FOR THIS DEVICE           81158
         LTR   R0,R0         BLKSIZE SPECIFIED ?                 86296
         BP    NOTFULLT                 YES - DON'T CLOBBER
         SPACE 1
NOBLKTB  TM    DXFLAG1,DX1FULLT        DEFAULT TO FULL TRACK ?
         BZ    NOTFULLT                 NO
         LA    R6,TABFULLT   GET THE FULL-TRACK PREFERENCE TABLE 86296
         LA    R15,4         SET THE ENTRY SIZE                  86296
         BALS  R14,DEFBLKSI  DO BLOCKSIZE LOOKUP                GP99026
         STH   R0,DCBBLKSI   STASH IT                            86296
NOTFULLT TM    DXFLAG1,DX1TRUNC        TRUNCATE TO VALID ?
         BZ    TRYCONCT                 NO
         LH    R7,DCBBLKSI             MIN(BLKSIZE,TRACKSIZE)
         C     R7,DEVBLOCK             BLKSIZE TOO BIG ?
         BNH   *+8                      NO  - USE IT FOR DR
         L     R7,DEVBLOCK              YES - USE TRACK SIZE FOR DR
         TM    DCBRECFM,DCBRECU        RECFM = F | V ?
         BNM   DOTRUNC                  NO  - JUST STORE NEW BLKSIZE
         TM    DCBRECFM,DCBRECF        RECFM = F ?
         BZ    DOTRUNC                  NO  - V
         LH    R0,DCBLRECL             GET LRECL FOR DR
         LTR   R0,R0                   DCBLRECL=0 ?
         BNP   TRYCONCT                 YES - DON'T 0C9          83059
         SLR   R6,R6                   FOR DR: BLKSI/LRECL
         DR    R6,R0                   R6=REMAINDER R7=QUOTIENT
         MR    R6,R0                   R6=LRECL*QUOTIENT
DOTRUNC  STH   R7,DCBBLKSI             DCBBLKSI=TRUNCATED BLKSIZE
         SPACE 1
TRYCONCT TM    DXFLAG2,DX2CONCT        CONCATENATION OF UNLIKE ?
         BZ    NOPARMS                  NO
         TM    DCBOFLGS,DCBOFPPC       FIRST TIME ?
         BZ    *+8                      YES
         OI    DXFLAG2,DX2REGET         NO  - SET REISSUE FLAG
         OI    DCBOFLGS,DCBOFPPC       SET UNLIKE ATTRIBUTES
         B     POSTPARM                                          81158
         SPACE 1
*              R2-> UCB (0 IF NONE)
*              R6-> BLOCKSIZE OR BLOCKSIZE LIMIT TABLE ENTRY     81158
         SPACE 1
DEFBLKDV TM    DXFLAG3,DX3ISWYL    WYLBUR EDIT FORMAT ?          81210
         BO    NEXT2LVL             YES - IGNORE ENTRY           81184
         LA    R15,4         SET BLOCKSIZE TABLE ENTRY SIZE      81210
         B     COMBLKDV      GO TO SET BLOCKSIZE BY DEVICE TYPE  81158
         SPACE 1                                                 81158
WYLBLKDV TM    DXFLAG3,DX3ISWYL    WYLBUR EDIT FORMAT ?          81210
         BZ    NEXT2LVL             NO  - IGNORE ENTRY           81184
         OI    MYFLAG2,F2WYLBLK     YES - FLAG AS WYLBUR TABLE   83080
         B     COMBLKDV                 - PROCESS AS MAX TABLE   83069
         SPACE 1                                                 83069
MAXBLKDV OI    MYFLAG2,F2MAXBLK                                  83080
         LA    R15,8               SET TABLE ENTRY LENGTH        83069
*        B     COMBLKDV                                          83069
         SPACE 1                                                 83069
COMBLKDV LTR   R6,R6         ANY TABLE ADDRESS SUPPLIED ?        86296
         BNZ   COMBLKDF      YES                                 86296
         LA    R6,TABWYBLK   ELSE USE WYLBUR TABLE               86296
COMBLKDF BALS  R14,DEFBLKSI  FIND TABLE ENTRY FOR THIS DEVICE   GP99026
         STH   R0,DEVDEF                                         83069
         TM    MYFLAG2,F2MAXBLK    FULL-SIZE ENTRIES ?           83080
         BZ    COMBLKMX             NO                           86296
         LH    R14,4(,R6)    GET PREFERRED MINIMUM               86296
         MAX   R14,DEVKEYLE,TYPE=H  MAKE SURE OF KEY             86296
         SH    R14,DEVKEYLE  GET RESIDUAL AFTER KEYLENGTH        86296
         STH   R14,DEVMIN    SET DEVICE MINIMUM                  86296
         LH    R14,6(,R6)    GET PREFERRED MAXIMUM               86296
         MAX   R14,DEVKEYLE,TYPE=H  ROOM FOR KEY ?               86296
         SH    R14,DCBKEYLE  SUBTRACT KEY LENGTH                 86296
         MIN   R14,DEVBLOCK  NO LARGER THAN DEVICE               86296
         STH   R14,DEVMAX    SAVE DESIRED MAXIMUM                86296
COMBLKMX ICM   R14,3,DCBBLKSI  BLOCKSIZE ALREADY SET ?           86296
         BNZ   NEXT2LVL             YES - NO DEFAULT NECESSARY   83069
         STH   R0,DCBBLKSI          NO  - SET DEFAULT FOR DEVICE 83069
         B     NEXT2LVL            GO FOR MORE                   81158
         SPACE 1                                                 81158
DEFBLKSI L     R0,0(,R6)               AL1(CLASS,TYPE),H'BLK'    81158
         CLI   0(R6),X'FF'             UCB TEST REQUESTED ?
         BE    DEFBLKDC                 NO  - DCBDEVT OR DEFAULT
         LTR   R2,R2                   IS THERE A UCB ?
         BZ    DEFBLKNX                 NO  - SEARCH FOR 'FF' ENTRY
         CLI   1(R6),X'FF'             IS TEST CLASS ONLY ?
         BNE   *+8                      NO
         ICM   R0,B'0100',UCBTBYT4      YES - FORCE EQUAL TYPE
         CLM   R0,B'1100',UCBTBYT3     SAME CLASS AND TYPE ?
         BNE   DEFBLKNX                 NO  - TRY NEXT ENTRY
DEFBLKST N     R0,=X'00007FFF'  GET RID OF DEVICE TYPE           86296
         MAX   R0,DEVKEYLE,TYPE=H  ENOUGH FOR KEY LENGTH ?       86296
         SH    R0,DEVKEYLE   ALLOW FOR LENGTH OF KEY             86296
         MIN   R0,DEVBLOCK   LIMIT TO DEVICE CAPACITY            86296
         BR    R14                      YES - RETURN TO CALLER   81158
DEFBLKDC CLI   1(R6),X'FF'             DCBDEVT TEST REQUESTED ?
         BE    DEFBLKST                 NO  - RETURN TO CALLER   81158
         CLM   R0,B'0100',DCBDEVT      DCBDEVT=TYPE ?
         BE    DEFBLKST                 YES - RETURN TO CALLER   86296
DEFBLKNX AR    R6,R15                  NEXT DEVICE DEFAULT ENTRY 81158
         B     DEFBLKSI                AND TRY AGAIN
         SPACE 2
POSTPARM LH    R0,DCBBLKSI              MERGED BLOCK SIZE
         TM    MYFLAG2,F2MAXBLK         MAXIMUM TABLE SUPPLIED ? 83080
         BO    DOMAXBK                   YES - USE IT            83069
         TM    DXFLAG3,DX3BLKWY         USE WYLBUR TABLE ?       83069
         BZ    NOMAXBK                   NO                      83069
         LA    R6,TABWYBLK               YES - USE WYLBUR TABLE  83069
         LA    R15,8                    SET TABLE ENTRY LENGTH   83069
         BALS  R14,DEFBLKSI             LOCATE DEVICE ENTRY     GP99026
*        B     DOMAXBK                                           83069
         SPACE 1                                                 81158
DOMAXBK  LH    R0,DCBBLKSI              LOAD BLOCKSIZE FOR TEST  83069
         TM    DXFLAG3,DX3ISWYL         IS THIS WYLBUR ?         83069
         BZ    *+12                      NO  - IGNORE MINIMUM    83069
         CH    R0,DEVMIN                 YES - TOO SMALL ?       83069
         BL    ABEND                    (TEMPORARY) FAIL IT      83069
         CH    R0,DEVMAX                TOO BIG ?                83069
         BH    ABEND                    (TEMPORARY) FAIL IT      83069
         SPACE 1                                                 81158
NOMAXBK  LTR   R0,R0         ANY BLKSIZE ?                       83069
         BNZ   NOPARMS       YES; SKIP U TEST                    81158
         TM    DCBRECFM,DCBRECU  RECFM=U.. ?                     81158
         BNO   NOPARMS       NO                                  81158
         LH    R4,DCBLRECL   GET RECORD LENGTH                   81158
         LTR   R4,R4         ANY ?                               81158
         BNZ   NOPARMS       YES                                 81158
         LH    R4,DXLRECLU   GET USER'S DEFAULT                  81158
         STH   R4,DCBLRECL   AND SET IT                          81158
         SPACE 1                                                 81158
NOPARMS  LH    R0,DCBBLKSI              MERGED BLOCK SIZE
         LH    R4,DCBLRECL              START WITH RECORD LENGTH
         LTR   R0,R0         ANY BLOCKSIZE ?                     81158
         BNZ   NOPARMSU      YES                                 81158
*        LTR   R4,R4         ANY RECORD LENGTH ?                 81158
*        BZ    NOPARMSU      NO                                  81158
         TM    DCBRECFM,DCBRECU  RECFM=U.. ?                     81158
         BNO   NOPARMSU      NO                                  81158
         STH   R4,DCBBLKSI   DEFAULT BLKSIZE TO LRECL            81158
         LR    R0,R4                                             81158
NOPARMSU LTR   R5,R4                    LRECL = X ?              81158
         BNP   BLKOK                     YES, OR NOT SET         83065
         TM    DCBRECFM,DCBRECU         RECFM=U?
         BO    NOTV                      YES - USE BLKSIZE=LRECL
         BM    *+12                      NO  - F OR V
         TM    DCBRECFM,DCBRECD         RECFM=D?
         BZ    NOTV                      NO  - WILL BECOME U
         SLR   R0,R0         FORCE BLOCKSIZE REPLACEMENT AT NOTV 83065
         LTR   R2,R2                    IS THERE A UCB ?
         BZ    UNBLOCK1                  NO  - USE BLKSIZE=LRECL
         CLI   UCBTBYT4,X'44'           IS DEVICE 3540 ? (OR 2955)
         BE    *+12                      YES - OR NOT UNIT RECORD
         CLI   UCBTBYT3,UCB3UREC        IS DEVICE UNIT RECORD?   83080
         BE    UNBLOCK1                  YES - USE BLKSIZE=LRECL 83080
         LH    R0,DCBBLKSI   RELOAD BLOCKSIZE                    83065
         SLR   R14,R14                                           83065
         ICM   R14,3,DXBLOCKF    GET USER'S BLOCKING FACTOR      83065
         BP    *+8           PLUS                                83065
         LA    R14,10        ELSE DEFAULT TO 10:1 BLOCKING       83065
         LR    R7,R4         COPY THE RECORD LENGTH              83065
         MR    R6,R14        LRECL*BLOCKING FACTOR               83065
         SLR   R15,R15       RECFM=V ADJUSTMENT EVENTUALLY       83065
         TM    DCBRECFM,DCBRECF  V OR D ?                        83065
         BNZ   *+8           NO; F                               83065
         LA    R15,4         SET ADJUSTMENT FOR RECFM=V          83065
         AR    R7,R15        REQUIRED BLOCKSIZE                  83065
         LR    R5,R7         SET FOR NOTV                        83065
         C     R7,DEVBLOCK   WILL IT FIT ?                       83065
         BNH   NOTV          YES; SEE IF IT SHOULD BE USED       83065
         L     R7,DEVBLOCK   ELSE TRUNCATE                       83065
         LR    R5,R7         SET FOR NOTV                        83065
         LTR   R15,R15       RECFM=V OR D ?                      83065
         BNZ   NOTV          YES; SEE IF IT SHOULD BE STASHED    83065
         SLR   R6,R6         CLEAR FOR DIVIDE, JUST IN CASE      83065
         DR    R6,R4         NUMBER OF RECORDS                   83065
         SLR   R6,R6         CLEAR AGAIN                         83065
         LTR   R7,R7         ANY ?                               83065
         BP    *+8                                               83065
         LA    R7,1          PREVENT HORRIBLE ERROR ?            83065
         MR    R6,R4         GET NEW BLOCKSIZE AS MULTIPLE       83065
         LR    R5,R7         COPY OVER                           83065
UNBLOCK1 TM    DCBRECFM,DCBRECF         IS IT FB?
         BO    NOTV                      YES
         TM    DCBRECFM,DCBRECSB        IS IT VB  (OR DB)
         BZ    NOTVS                     NO - TRY TO ADJUST BLK
         LTR   R5,R0                     YES; NEED ADJUSTMENT ?  83065
         BP    BLKOK                    DON'T RESET IF SUPPLIED  83069
NOTVS    LA    R4,4(,R4)                ADD BDW FOR V OR D REC.
         LA    R5,4(,R5)                ADD BDW FOR V OR D REC.
NOTV     CR    R0,R4                    VALID BLKSIZE?
         BNL   BLKOK                     YES - ACCEPT IT
         STH   R5,DCBBLKSI              STORE CALCULATED BLKSIZE
         LR    R0,R5
         SPACE 1                                                 83065
*        CHECK FOR RECFM ADJUSTMENTS                             83065
*             NOTE: BLKSIZE IN R0, LRECL IN R4                   83065
*                                                                83065
BLKOK    TM    DCBRECFM,DCBRECU        RECFM=U ?                 83065
         BO    FRECFMU       YES                                 83065
         BZ    FRECFMUD      D OR EVENTUAL U                     83065
         TM    DCBRECFM,DCBRECF        RECFM=F ?                 83065
         BZ    FRECFMV       NO; RECFM=V                         83065
         NI    DCBRECFM,255-DCBRECBR   RESET BLOCKED             83065
         CR    R0,R4         LRECL=BLKSIZE ?                     83065
         BE    FRECFMT       YES; UNBLOCKED                      83065
         OI    DCBRECFM,DCBRECBR   SET IT BLOCKED                83065
         B     FRECFMT                                           83065
FRECFMV  LA    R4,4(,R4)     ALLOW FOR BDW                       83065
         CR    R0,R4         IF LRECL+4=BLKSIZE, NO CHANGE       83065
         BE    BUFNOFIX      QUIT RECFM CHECKS                   83065
         BL    FRECFMVS      BLKSIZE LESS; SET SPANNED           83065
         OI    DCBRECFM,DCBRECBR  BLKSIZE GREATER; SET BLOCKED   83065
         B     BUFNOFIX                                          83065
FRECFMVS OI    DCBRECFM,DCBRECSB  SET SPANNED                    83065
         B     BUFNOFIX                                          83065
FRECFMUD TM    DCBRECFM,DCBRECD  RECFM=D ?                       83065
         BNZ   FRECFMV       YES; TREAT AS V                     83065
FRECFMU  NI    DCBRECFM,255-DCBRECBR-DCBRECSB                    83065
         MVC   DCBLRECL,DCBBLKSI  SET LRECL=BLKSIZE (WYLBUR)     86296
FRECFMT  C     R0,DEVBLOCK   WILL BLOCK FIT ON DEVICE ?          83065
         BNH   BUFNOFIX      YES                                 83065
         LTR   R2,R2         UCB KNOWN ?                         83065
         BZ    BUFNOFIX      NO                                  83065
         CLI   UCBTBYT3,UCB3DACC  DASD ?                         83065
         BNE   BUFNOFIX      NO                                  83065
         IC    R14,UCBTBYT4  GET THE DASD SUB-TYPE               86296
         N     R14,=X'0000000F'  MASK FOR (CURRENTLY) KNOWN      86296
         LA    R14,TABOVER(R14)  GET OVERFLOW BIT TABLE          86296
         OC    DCBRECFM,0(R14)  SET TRACK OVERFLOW AND HOPE      86296
BUFNOFIX CLI   DCBBUFNO,0     DID USER SUPPLY # OF BUFFERS?      83065
         BNE   KANT                      YES - EXIT              83065
         LA    R7,1                     DCBBUFNO=1 IF DA(*) (TERM=TS)
*        CLI   DCBDEVT,DCBDVTRM          IS IT TSO TERMINAL?     83069
*        BE    SETBUFNO                   YES - BUFNO=1          83069
         LTR   R2,R2         UCB AND TIOT ENTRY AVAILABLE ?      83080
         BZ    SETBUFNO      NO; FORCE SINGLE BUFFER             83080
         AIF   (&MVS).NOATI                                      82109
         TM    UCBATI,UCBHPDV            HASP PSEUDO-DEVICE ?
         BO    SETBUFNO                   YES - SET UNBUFFERED
.NOATI   TM    TIOELINK,TIOUNBUF         IS IT TSO, JES, ETC. ?  83080
         BNZ   SETBUFNO                   YES - BUFNO=1
         LTR   R0,R0         BLOCKSIZE USABLE ?                  83080
         BNP   KANT          NO; SKIP SETTING                    83065
         SLR   R6,R6          DCBBUFNO=MAX(2,4096/DCBBLKSI)      83065
         LA    R7,2048
         AR    R7,R7
         DR    R6,R0
         CH    R7,TWO
         BH    SETBUFNO
         LA    R7,2
TWO      EQU   *-2
SETBUFNO STC   R7,DCBBUFNO
KANT     TM    DCBOPTCD,DCBSRCHD  RER/SD OPTION ?                83065
         BZ    FIXDSD        NO; CONTINUE NORMALLY               83065
         LTR   R2,R2         ANY UCB ?                           83065
         BZ    FIXDSD        YES; PASS IT                        83065
         CLI   UCBTBYT3,UCB3DACC  DISK ?                         83065
         BNE   FIXDSD        NO; NO PROBLEM                      83065
         AIF   (NOT &SVS).SVSUERR                                83065
         TM    DCBRECFM,DCBRECU  RECFM=U ?                       83065
         BO    KILLSD        YES; ERROR ON SHORT WYLBUR FILES    83065
.SVSUERR TM    DCBRECFM,DCBRECTO+DCBRECSB  FS/FBS/VS/VBS/UT/FT, ETC.
         BZ    FIXDSD        NO; SEARCH DIRECT IS SUPPORTED ?    83065
KILLSD   NI    DCBOPTCD,255-DCBSRCHD  PREVENT ERRONEOUS ERRORS   83065
FIXDSD   TM    MYFLAG3,F3EXITF   TAKE FINAL EXIT ?               83275
         BZ    SARTRE        NO                                  83275
         ICM   R6,15,EXITFADR  GET AND CHECK ADDRESS             83275
         BZ    SARTRE        SHOULD BE UNNECESSARY ?             83275
*                                                                83275
*        THE POST-PROCESSING EXIT IS USUALLY REQUESTED FOR GETPOOL
*        WHEN THE PROGRAM IS IN KEY ZERO AND A FREEPOOL MAY BE DONE.
*                                                                83275
         LR    R1,R11        (DCB)     STANDARD PARM REG         83275
         LR    R0,R8         DCBEXITP WORK AREA                  83275
         LR    R15,R6                                            83275
         STM   R2,R12,MYR2             SAVE DCBEXIT REGISTERS    83275
         LM    R2,R12,SAVE2            LOAD USER REGISTERS       83275
         BALSR R14,R15       INVOKE THE EXIT                    GP99026
         LM    R2,R12,MYR2             RESTORE DCBEXIT REGISTERS 83275
SARTRE   PGMEXIT RC=0                                           GP99026
         SPACE 1                                                 81158
ABEND    ABEND 019,DUMP      ****TEMPORARY ERROR RETURN****      81158
         SPACE 1                                                 81158
         LTORG ,
         SPACE 2
MODEL DCBEXITD DSECT=NO,PREFIX=MD,                               83069 *
               FLAG1=(DX1RCINT+DX1TRUNC+MD1NOPCI+MD1NOSD),             *
               FLAG3=(MD3BLKTB+MD3BLKWY)                         86296
MODELLEN EQU   *-MODEL                                           83069
         SPACE 2                                                 83069
@DCBVECT DC    A(DCBEXIT,@DCBXLST,TABWYBLK,MODEL)                83069
         SPACE 1                                                 83069
@DCBXLST DC    0F'0',X'85',AL3(@DCBEXIT)                         82178
         SPACE 1                                                 81158
*        WYLBUR DEFAULT BLOCKSIZE TABLE; ALSO MIN/MAX BLKSIZES   81158
*                                                                81158
TABWYBLK DS    0F            FORCE ALIGNMENT                     81158
         DC    X'80FF',H'8000,0,32760'           DUMMY TAPE ENTRY
         DC    X'2001',H'3625,512,3625'          2311            81158
         DC    X'2006',H'3210,512,14136'         2305-1          83069
         DC    X'2007',H'3516,512,14660'         2305-2          83069
         DC    X'2008',H'3520,512,7294'          2314            81158
         DC    X'2009',H'3156,512,13030'         3330            81158
         DC    X'200A',H'4100,512,8368'          3340            81158
         DC    X'200B',H'3665,512,19069'         3350            83069
         DC    X'200C',H'4096,512,32760'         3375            83069
         DC    X'200D',H'3156,512,13030'         3330-11         83069
         DC    X'200E',H'6356,512,32760'         3380            91025
         DC    X'200F',H'6518,512,32760'         3390            91025
         DC    X'20FF',H'4000,512,32760'         OTHER DASD ?    83069
TABWLAST DC    X'FFFF',H'0,0,32760'    ALL ELSE                  81158
MAXOSBLK EQU   TABWLAST+6,2,C'H'       DEFINE MAX VS BLKSIZE     81158
         SPACE 1                                                 86296
TABFULLT DC    X'200C',H'17600'        3375                      86296
         DC    X'200E',H'23476'        3380                      86296
         DC    X'200F',H'27998'        3390                      91025
         DC    X'FFFF',H'32760'  NO MATCH - USE DEVBLKSI         86296
         SPACE 1                                                 83069
*              USED TO TRANSLATE 3 HIGH BITS OF DCBRECFM         83069
*              TO BITS IN FLAG1                                  83069
RECFMTAB DC    AL1(0)                                            83069
         DC    AL1(F1RECFMD)                                     83069
         DC    AL1(F1RECFMV)                                     83069
         DC    AL1(F1RECFMV)            TRACK OVERFLOW           83069
         DC    AL1(F1RECFMF)                                     83069
         DC    AL1(F1RECFMF)            TRACK OVERFLOW           83069
         DC    AL1(F1RECFMU)                                     83069
         DC    AL1(F1RECFMU)            TRACK OVERFLOW           83069
         SPACE 1                                                 83069
*              USED TO TRANSLATE 3 HIGH BITS OF DCBRECFM         83069
*              TO DEFAULT LRECL OFFSET                           83069
LROFFTAB DC    AL1(0)                                            83069
         DC    AL1(DXLRECLD-DXBLOCKF)                            83069
         DC    AL1(DXLRECLV-DXBLOCKF)                            83069
         DC    AL1(DXLRECLV-DXBLOCKF)                            83069
         DC    AL1(DXLRECLF-DXBLOCKF)                            83069
         DC    AL1(DXLRECLF-DXBLOCKF)                            83069
         DC    AL1(DXLRECLU-DXBLOCKF)                            83069
         DC    AL1(DXLRECLU-DXBLOCKF)                            83069
         SPACE 1                                                 86296
TABOVER  DC    X'00'         TRACK OVERFLOW FLAG TABLE           86296
         DC    11AL1(DCBRECTO) 2311-3350                         86296
         DC    X'00'         3375                                86296
         DC    AL1(DCBRECTO) 3330-1                              86296
         DC    2X'00'        3380, 3390                          86296
         STITL 'WORK AREA'
SAVE     DSECT ,
         DS    0D
DEVTYPE  DS    0XL4
DEVTYPB1 DS    X
DEVTYPB2 DS    X
DEVTYPB3 DS    X
DEVTYPB4 DS    X
DEVBLOCK DS    F                       MAXIMUM BLOCK SIZE
DEVBLKSI EQU   DEVBLOCK+2,2,C'H'
MYFLAG1  DS    X                                                 83080
F1RECFMD EQU   X'80'         RECFM=D...                          83069
F1RECFMF EQU   X'40'         RECFM=F...                          83069
F1RECFMU EQU   X'20'         RECFM=U...                          83069
F1RECFMV EQU   X'10'         RECFM=V...                          83069
F1RECFMS EQU   X'08'         RECFM=...S                          83069
MYFLAG2  DS    X                                                 83080
F2WYLBLK EQU   X'80'         WYLBUR  TABLE USED                  83069
F2MAXBLK EQU   X'40'         MAXIMUM TABLE USED                  83069
MYFLAG3  DS    X                                                 83080
F3EXITF  EQU   X'80'         TAKE POST-PROCESSING EXIT           83275
MYFLAG4  DS    X                                                 83080
DEVDEF   DS    H                                                 83069
DEVMIN   DS    H                                                 83069
DEVMAX   DS    H                                                 83069
DEVKEYLE DS    H             DEVICE ADJUSTED KEY LENGTH          86296
EXITFADR DS    A             ADDRESS OF POST-PROCESSING EXIT     83275
MYR2     DS    A                                                 81147
MYR3     DS    A                                                 81147
MYR4     DS    A                                                 81147
MYR5     DS    A                                                 81147
MYR6     DS    A                                                 81147
MYR7     DS    A                                                 81147
MYR8     DS    A                                                 81147
MYR9     DS    A                                                 81147
MYR10    DS    A                                                 81147
MYR11    DS    A                                                 81147
MYR12    DS    A                                                 81147
         SPACE 1                                                 86296
STARTLE  SERVCALC MF=D       EXPAND THE TRKCALC AREA             86296
         SPACE 2                                                 83069
FAKEX DCBEXITD DSECT=NO,PREFIX=FX                                83069
         ORG   FAKEX+MODELLEN          ALLOW FOR MODEL EXPANSION 83080
         SPACE 1                                                 83080
SAVEEND  EQU   *                                                GP99026
         STITL 'PARAMETER BLOCK'
         PRINT &PRTMAC                                           81146
      DCBEXITD DSECT=YES                                         81158
         STITL 'CONTROL BLOCK MAPPINGS'
         PRINT &PRTSYS                                           81146
         AIF   (NOT &MVS).NOPSA                                  83069
        IHAPSA ,                                                 83069
.NOPSA   CVT   DSECT=YES                                         83069
         DCBD  DSORG=PS,DEVD=DA
        IKJTCB ,
         IKJRB DSECT=YES                                         81347
TIOT     DSECT ,
      IEFTIOT1 ,
TIOUNBUF EQU   TIOTTERM+TIOEQNAM+TIOESYIN+TIOESYOT+TIOTREM       81135
*              TSO      TCAM     JES1     JES1     JES1          81135
*                                        +TIOESSDS               81135
*                                         OS/VS2                 81135
UCB      DSECT ,
      IEFUCBOB ,
         END   ,
