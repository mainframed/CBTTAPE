FUJV     TITLE 'I E F U J V  ***  JCL VALIDITY CHECKING'
         MACRO                                                   81334
&NM      SETMSG &N                                               81334
         LCLA  &I,&J                                             81334
         LCLC  &F                                                81334
&F       SETC  '8040201008040201'                                81334
         AIF   (T'&N NE 'N').FAIL                                81334
         AIF   (&N GE 1 AND &N LT 31).OK                         81334
.FAIL    MNOTE 8,'SETMSG REQUIRES NUMERIC FROM 1 TO 30; NOT &N'  81334
         MEXIT ,                                                 81334
.OK      ANOP  ,                                                 81334
&I       SETA  31-&N                                             81334
&J       SETA  &I/8          GET BYTE OFFSET                     81334
&I       SETA  2*(&I-&J*8)+1   MAKE SUBSTRING OFFSET             81334
&F       SETC  '&F'(&I,2)                                        81334
&NM      OI    JOBFLAG+&J,X'&F'  SET MESSAGE FLAG                81334
         MEND  ,                                                 81334
         SPACE 2                                                 81334
         MACRO                                                   81334
&NM      TESTMSG &N                                              81334
         LCLA  &I,&J                                             81334
         LCLC  &F                                                81334
&F       SETC  '8040201008040201'                                81334
         AIF   (T'&N NE 'N').FAIL                                81334
         AIF   (&N GE 1 AND &N LT 31).OK                         81334
.FAIL    MNOTE 8,'TESTMSG REQUIRES NUMERIC FROM 1 TO 30; NOT &N' 81334
         MEXIT ,                                                 81334
.OK      ANOP  ,                                                 81334
&I       SETA  31-&N                                             81334
&J       SETA  &I/8          GET BYTE OFFSET                     81334
&I       SETA  2*(&I-&J*8)+1   MAKE SUBSTRING OFFSET             81334
&F       SETC  '&F'(&I,2)                                        81334
&NM      TM    JOBFLAG+&J,X'&F'  TEST MESSAGE FLAG               81334
         MEND  ,                                                 81334
         SPACE 2                                                 81334
         MACRO ,                                                 81334
&NM      LOCKEY &KEY,&NOT,&NONE=,&NOTONE=                        81334
&NM      MVI   FINDKEY,&KEY  SET REQUESTED KEY/SUBKEY
         BAL   R9,KEYLOOK    LOOK FOR REQUESTED KEY              81334
         AIF   (T'&NOT EQ 'O').MEND                              81334
         B     &NOT          KEY NOT FOUND
         AIF   (T'&NOTONE EQ 'O').SOME                           81334
         CLI   KKNUM,1       ONE SUBPARAMETER ?                  81334
         BNE   &NOTONE       NO
         AGO   .MEND                                             81334
.SOME    AIF   (T'&NONE EQ 'O').MEND                             81334
         CLI   KKNUM,1       AT LEAST ONE SUBPARM ?              81334
         BL    &NONE         NO
.MEND    MEND  ,                                                 81334
         SPACE 2                                                 81334
         MACRO ,                                                 81334
&NM      BUGME &A                                                81334
         GBLB  &BUGBEAR                                          85112
         LCLA  &I                                                81334
         AIF   (NOT &BUGBEAR).MEND                               85112
&I       SETA  K'&A                                              81334
         STM   R14,R12,12(R13)                                   81334
         MVC   LENGTH(32),=X'001C8000C9C5C6E4D1E540C2E4C7E240D4C54B4040*
               4040404040404080004000'  MAKE MESSAGE             90197
         AIF   (&I LT 1).NOTEXT                                  81334
         AIF   (&I LE 8).TEXT                                    81334
&I       SETA  8                                                 81334
.TEXT    MVC   LENGTH+20(&I),=CL(&I)'&A'  MOVE USER TEXT         90197
.NOTEXT  XC    ECB(LENGTH-ECB),ECB  CLEAR REPLY, ECB, ETC.       90197
         WTOR  ,REPLY,9,ECB,MF=(E,FWIT)                          81334
         WAIT  ECB=ECB                                           81334
         LM    R14,R12,12(R13)                                   81334
.MEND    MEND  ,                                                 81334
         SPACE 2                                                 81334
         COPY  OPTIONGB                                          81334
         SPACE 1                                                 81334
         SYSPARM LIST=YES                                        81334
         SPACE 2                                                 81334
IEFUJV   START 0
         SPACE 2                                                 85112
         PRINT &PRTSOR
*              R1 POINTS TO A PARAMETER LIST:
*        0     JMRJOB
*        4     JCL STATEMENT
*        8     VERB CODE
         SPACE 1
*              R2 WORK REGISTER
         SPACE 1
*              R3 WAS THE BASE - NOW UNUSED                      85112
         SPACE 1
*              R4 IS THE WORK AREA REGISTER
         SPACE 1
*              R5 IS THE JMR POINTER
         SPACE 1
*              R7 WAS THE SCT POINTER  - NOW USED FOR OTHER THINGS
         SPACE 1
*              R8 BASE REGISTER - WAS THE JCT POINTER            85112
         SPACE 1
*              R9 IS THE STATEMENT POINTER
         SPACE 1
*              R12 POINTS TO THE INTERPRETER WORK AREA
         SPACE 1
*              JCL VERB CODES:
         SPACE 1
*              ALL BUT ONE OF THESE COMES FROM IEFVHEB
*        0     NULL   (NOTE: FORCED BY LOCAL CHANGE TO IEFVHL)   81334
*        1     JOB
*        2     EXEC
*        4     DD
*        8     PROC
         SPACE 1
*              ENTRY FROM IEFVHH HAS DIFFERENT REGISTER SETUP
*        16    CONVERTER COMPLETE
*        32    INTERPRETER COMPLETE                              90197
         SPACE 1
IEFUJV   CSECT ,
         SPACE 2                                                 81334
MSGNO    EQU   2
BASE     EQU   8                                                 85112
INITREG  EQU   4
STMENT   EQU   9
*
*        JOBFLAG + 3
*
*MSG1    EQU   X'02'         NON-SETUP JOB WITH SETUP            81334
*MSG2    EQU   X'04'         SYS1. DATASET WITH DISP NOT SHR     81334
*MSG3    EQU   X'08'         EXCESSIVE JOB REGION REQUEST        81334
*MSG4    EQU   X'10'         INVALID PRIORITY                    81334
*MSG5    EQU   X'20'         EXCESSIVE TIME SPECIFICATION        81334
*MSG6    EQU   X'40'         UNSUPPORTED ROLLOUT REQUEST         81334
*MSG7    EQU   X'80'         UNSUPPORTED JOB CARD CONTINUATION   81334
*
*        JOBFLAG + 2
*
*MSG8    EQU   X'01'         INVALID SYSOUT FORM/CLASS/WRITER    81334
*MSG9    EQU   X'02'         INVALID/REJECTED ACCOUNT INFO       81334
*MSG10   EQU   X'04'         REJECTED RERUN OR BENCHMARK JOB     81334
*MSG11   EQU   X'08'         AUTOMATIC RESTART UNDER HASP ?      81334
MSG12F   EQU   X'10'         JCL SCAN ONLY
*MSG13   EQU   X'20'         EXCESSIVE STEP REGION REQUEST       81334
*MSG14   EQU   X'40'         DISK WORK0N FOR NON-F JOB           81334
*MSG15   EQU   X'80'         DISK WORK3N FOR NON-I JOB           81334
*
*        JOBFLAG + 1
*
*MSG16   EQU   X'01'         ACCOUNT DISABLED                    81334
*MSG17   EQU   X'02'         UNIT=3330, ETC. WITHOUT VOLSER      81334
*MSG18   EQU   X'04'         UNIT=SYSDA AND EXPLICIT VOLSER      81334
MSG19F   EQU   X'08'         VOL=REF=*. AND UNIT                 90219
*
*        JOBFLAG
*
         SPACE 1                                                 92299
*   NOTE THAT THIS CODE USES 24-BIT ADDRESSING (JMRUCOM/INITREG) 92299
*                                                                92299
         USING JMR,R5
         USING COMWA,R12                                         85112
         USING IEFUJV,BASE        (R8)                           85112
         STM   R14,R12,12(R13)  SAVE ALL
         LR    BASE,R15        LOAD BASE
         SLR   R15,R15       SET PROV. RETURN CODE               85168
         ICM   R5,15,0(R1)   GET JMR                             90197
         BZ    QUIKEXIT      ELSE HAVE TO QUIT                   85168
         LM    R9,R10,4(R1)  GET TEXT AND VERB CODE POINTER      90197
         TM    VERB-VERB(R10),X'1F'   KNOWN VERB ?               90197
         BZ    QUIKEXIT      SHOULD NEVER HAPPEN ?               90197
         ICM   INITREG,7,JMRUCOM+1   WORK AREA PREVIOUSLY GOTTEN ?
         BNZ   CRANK         YES
*              THE NEXT TWO TESTS ARE FOR IMPOSSIBLE EVENTS:
*              THEY "NOCAN" HAPPEN
*              CALL DEAR ABBY (0C1) IF THEY DO
         TM    VERB-VERB(R10),1   IGNORE IT IF VERB NOT JOB      90197
         BZ    NOCAN
         TM    SWB,CXP
         BZ    DOITONCE
NOCAN    DC    H'0'                     IF I GET HERE, CHECK THE PGM
DOITONCE LH    R0,=Y(INITLEN)  GET WORK AREA SIZE                90197
         GETMAIN R,LV=(0)    GET SEMI-PERMANENT WORK AREA        90197
         LR    INITREG,R1    COPY                                81334
         USING INITSECT,INITREG                                  81334
         MVC   OLDTXTEX,CWATXTEX                                 85112
         ST    INITREG,JMRUCOM    SAVE FOR LATER ENTRANCES       81334
         LA    R0,INITACCT              SET PHAQUE IEFACT
         ST    R0,CWATXTEX                                       85112
         CLRL  XCBEGIN,INITCLR         CLEAR THE WORK AREA       81334
         MVI   JOBFLAG+3,1   INITIALIZE MSG FLAG
         MVI   SMF5JPTY,14   PRTY=14 IS JCL ERROR                81334
         MVC   FLAGMASK,MASKFLAG  SET BITS FOR WARNING MSGS      79056
         SPACE 1
CRANK    MVC   PTRSAVE,SAVEPTR                                   81334
         LA    R1,SAVER
         ST    R1,8(,R13)
         ST    R13,4(,R1)
         LR    R13,R1
         BUGME CARDEXIT                                          81334
         AIF   (&MVSSP).NOGM1                                    92299
         LAT   R1,VIEFVGM,,NOLOADS    VGM LOADED YET ?
       LPALOOK EPLOC=CIEFVGM,DCB=4 MESSAGE WRITER (ALIAS OF IEFVH1)
         ST    R0,VIEFVGM    REMEMBER THE MAIN STORAGE ADDRESS
.NOGM1   L     R2,CVTPTR     GET THE CVT                         85112
         ICM   R2,15,CVTUSER-CVTMAP(R2)  GET THE USER CVT EXTENSION
         BZ    NOLOADS       CAN DO WITHOUT IT ? NOT !           92347
         USING USERCVT,R2                                        85112
         ICM   R0,15,UCLACCT  GET ACCOUNT TABLE                  85112
         ST    R0,@TABACCT   SAVE IT                             85112
         ICM   R0,15,UCLVOLT GET VOLUME TABLE                    85112
         ST    R0,@LEXVOLT   SAVE IT                             85112
         SPACE 1                                                 81334
NOLOADS  TM    VERB-VERB(R10),16+32  CONV/INT. END ?             90197
         BNZ   ENQENTRY      IEFVHH HAS ITS OWN WAY OF DOING THINGS
         TM    JOBSW1,JOBSKIP  TO BE SKIPPED ?                   85154
         BNZ   RETURNS       YES; JUST EXIT                      85154
         SLR   R1,R1         SET START OFFSET FOR OPERAND SCAN   81334
         LA    R14,RSTMT     ASSUME NORMAL, READ STATEMENT       92360
         TM    SWE,PROC+GPI  ARE WE PROCESSING A PROC STATEMENT ?
         BNO   *+8           NO                                  92360
         LA    R14,PSTMT     YES; POINT TO PROC CARD             92360
         IC    R1,LISTPTR(,R14)  GET OFFSET TO OPERAND           92360
         LA    R1,0(R1,R9)   GET INPUT STATEMENT ADDRESS         90197
         LA    R14,1         SET BXLE INCREMENT                  81334
         LA    R15,70(,R9)   AND END COLUMN                      90197
         LR    R0,R9         SET SCAN START                      90197
         STM   R14,R1,CARDBXLE  MAKE BXLE POINTERS FOR CARD SCAN 81334
         TM    VERB-VERB(R10),1   JOB ?                          92360
         BO    JOBCARD
         SPACE 1
         AIF   ('&LOCAL' NE 'CCSI').NOSOUT                       92299
         LAT   R15,ADJQE,PREVSOUT   SKIP IF NOT A HASP JOB       81334
         TM    JQEFLAG3-JQEDSECT(R15),QUEINVFO  INVALID SYSOUT ? 85112
         BZ    PREVSOUT      NO                                  81334
         NI    JQEFLAG3-JQEDSECT(R15),255-QUEINVFO  RESET IT     85112
         SETMSG 8            FAIL INVALID SYSOUT SPECIFICATION   81334
.NOSOUT  SPACE 1                                                 92299
PREVSOUT BAL   R11,YAKETY    PRINT ERROR MESSAGES                81334
         OI    RETCH,BUM     FAIL IF ANY
         SPACE 1
         LM    R14,R1,CARDBXLE  RESTORE SCAN POINTERS            81334
         TM    VERB-VERB(R10),2   EXEC CARD ?                    92360
         BO    XEQCARD
         TM    VERB-VERB(R10),4   DD CARD ?                      92360
         BO    DDCARD
         CLI   VERB-VERB(R10),8   PROC CARD ?                    92360
         BE    PROCCARD      YES, PROCESS
         AIF   ('&LOCAL' NE 'CCSI').NONULL   SKIP SCAN MESSAGE   92299
*        NULL STATEMENT - PROCESS FINAL MESSAGES ONLY
         CLI   VERB-VERB(R10),0   REALLY A NULL STATEMENT ?      92360
         BNE   RETURN        NO
         TM    RETCH,BUM     FAILED ?
         BNZ   RETURN        YES; NO SPECIAL TEST
         ICM   R0,3,CWARET   BAD RETURN SET ?                    85112
         BNZ   RETURN        YES; NO FURTHER TESTS
         LAT   R15,ADJQE,RETURN   TEST FOR HASP JOB              81334
         TM    JQEOCLAS-JQEDSECT(R15),X'F0'  JCL-SCAN ONLY ?     85112
         BNO   RETURN        NO, LET IT PASS
         SETMSG 12           SHOW PSEUDO-FAIL                    81334
.NONULL  SPACE 1                                                 92299
RETURN   BAL   R11,YAKETY    PRINT ERROR MESSAGES, IF ANY
         OI    RETCH,BUM     SHOW FAILED BY UJV
RETURNS  L     R15,RETCODE   SET CURRENT RETURN CODE
         NI    MGFLAG,255-MGSKP   RESET FLAG
         L     R13,SAVER+4   RESTORE
         MVC   SAVEPTR,PTRSAVE
QUIKEXIT RETURN (14,12),RC=(15)                                  85168
         SPACE 3
YAKETY   STM   R9,R1,LINKWORK+16  SAVE VITAL REGISTERS           85160
         LA    R2,4          PROVISIONALLY SET GOOD EXIT
         CLC   JOBFLAG,DCF1    ANY ERROR FLAGS SET ?
         BE    YAKEXIT       IF NO ERRORS THEN RETURN
         TM    MGFLAG,MGSKP  IS SKIP FLAG ON ?
         BNZ   YAKEXIT2      NO MESSAGES; DEFER UNTIL TEXT EXIT
         SLR   R2,R2         SET FOR BAD EXIT
         L     R11,=A(MSGTAB)
         LA    R1,(MSGTEND-MSGTAB)/4
         L     R7,JOBFLAG
         SLL   R7,30-(MSGTEND-MSGTAB)/4
YAKLOOP  BXH   R7,R7,YAKKINC
         L     R15,=A(IEFVGMSG)                                  85154
         BALR  R14,R15
YAKKINC  LA    R11,4(,R11)
         BCT   R1,YAKLOOP
         L     R15,JOBFLAG   SAVE OLD FLAG VALUE
         N     R15,FLAGMASK  DELETE WARNING MSG BITS             79056
         BXH   R15,R15,YAKEXIT  FAIL IF ANY OTHERS ON            79056
         LA    R2,4          ELSE SET GOOD EXIT
YAKEXIT  MVC   JOBFLAG,DCF1   RESET MESSAGE FLAGS
YAKEXIT2 NI    MGFLAG,255-MGSKP  TURN OFF DELAY FLAG
         LM    R9,R1,LINKWORK+16   RESTORE REGISTERS             85160
         B     0(R2,R11)     TAKE BAD (0)  OR GOOD (4) EXIT
         SPACE 1
MASKFLAG DC    AL1(127,255-MSG19F,255-MSG12F,254)                90219
         EJECT
JOBCARD  TM    SWB,CXP                  IS IT CONTINUATION?
         BO    JPGMTEST                 YES - TEST FOR PROGRAMMER NAME
         OI    MGFLAG,MGSKP  SKIP MESSAGES BEFORE JOB CARD
         CLI   JMRCLASS,0    START TASK UNDER MSTR ?             92339
         BE    DONTFAIL                                          92339
         CLI   JMRCLASS,X'D0'   START TASK ?                     85154
         BE    DONTFAIL      YES                                 85154
         CLI   JMRCLASS,X'E0'   TIME-SHARING USER ?              85154
         BNE   DONTSKIP      NO                                  85154
SETSKIP  OI    JOBSW1,JOBSKIP  SKIP THIS                         85154
         B     RETURN        AND EXIT                            85154
DONTFAIL OI    JOBSW1,JOBCAN  SET PRIVILEGED                     92347
         CLI   0(R1),C'('    DID JES MASSAGE THIS CARD ?         92347
         BE    DONTSKIP      YES; DON'T BYPASS                   92347
         NI    JOBSW1,255-WHORU                                  92347
         OI    JOBSW1,JOBCHKD  FAKE JOB CARD CHECKED             92347
         B     RETURN        BUT SKIP JOB CARD                   92347
DONTSKIP ST    R1,ACCTSTRT             SAVE BEGINNING OF ACCT FIELD(S)
JOBQACCT CLI   1(R1),C'?'               IS IT A SPECIAL?         81334
         BNE   JOBPACCT                                          81334
         STM   R14,R1,SAVER+12
         MVI   MSGWORK,C' '
         MVC   MSGWORK+1(L'MSGWORK-1),MSGWORK
         MVC   MSGWORK(L'HWIT),HWIT
         MVC   MSGWORK+22(4),JMRUSEID  PUT OUT USER'S ACCOUNT
         MVC   MSGWORK+27(4),JMRUSEID+4  AND SUB-ACCOUNT FOR FIXING
*              IF I GET HERE A SECOND TIME, BELLS ARE RINGING...
         BAL   R14,WTOR      SOLICIT THE TRUE ACCOUNT NUMBER
         CLI   REPLY+4,C','            REMEMBER THE COMMA?
         BNE   ATANDT                    NO - WAKE FROM COMA
         CLI   REPLY+8,C' '             IS IT ALL THERE?
         BE    ATANDT                    NO - GIVE A MUSIC LESSON
         LM    R14,R1,SAVER+12
         MVC   1(9,R1),REPLY
         SPACE 2
JOBPACCT CLI   0(R1),C'('    IS THERE A LIST OF ACCT FIELDS ?    81334
         BNE   JACKPOT                                           81334
         CLI   5(R1),C','    TWO BY FOUR ?                       92299
         BE    JACQACCT      YES (?)                             92299
         MVC   LINKWORK(8),1(R1)  COPY FIRST EIGHT               92299
         NC    LINKWORK(8),=X'C0F0F0F0C0C0C0C0'  BASIC ZONE MASK 92299
         CLC   LINKWORK(8),=X'C0F0F0F0C0C0C0C0'  CORRECT ZONES ? 92299
         BNE   JACQACCT      NO; CONTINUE (AND FAIL)             92299
         MVC   JMRUSEID,1(R1)  MOVE PRESUMED FULL ACCOUNT        92299
         BCTR  R1,0          BACK-UP TO MAKE DISPLACEMENT RIGHT  92299
         B     JACQENDA      JOIN COMMON CODE                    92299
         SPACE 1                                                 79049
JACBDSUB SETMSG 9            SET INVALID ACCOUNT                 81334
         SLR   R0,R0         SET NUMBER OF UNSCANNED FIELDS      81334
JACBDSOP CLI   0(R1),C')'    END OF ACCOUNT FIELD ?              81334
         BE    JACQDONE      YES; CONTINUE                       81334
         BXLE  R1,R14,JACBDSOP                                   81334
         B     JACKPOT       ELSE FAIL IF NO PROGRAMMER NAME FIELD
         SPACE 1                                                 81334
JACQACCT CLI   5(R1),C','    SUB-ACCOUNT FOLLOWS ?               81334
         BNE   JACKPOT                                           81334
JACQACNM MVC   JMRUSEID(4),1(R1)        SAVE ACCT #              81334
         MVC   JMRUSEID+4(4),6(R1)     SAVE SUB-ACCOUNT          81334
JACQENDA CLI   10(R1),C','             MORE THAN TWO FIELDS?
         BE    JACQALPH                                          81334
         CLI   10(R1),C')'   NON-HASP END OF FIELD ?
         BNE   JACBDSUB      NO; INVALID SUB-ACCOUNT FIELD       81334
JACQALPH LA    R0,2          NUMBER OF UNCHECKED FIELDS          81334
         LR    R2,R0         CHECK TWO SETS OF FOUR BYTES        81334
         LA    R7,JACKPOT    FAIL ERROR IN MAJOR ACCOUNT FIELD   81334
         LA    R9,JMRUSEID   CHECK FOR NON-ALPHABETICS
JACQWORD LA    R10,4         CHECK FOUR BYTES AT A TIME          81334
JACQBYTE CLI   0(R9),C'A'    LOWER THAN ALPHA ?                  81334
         BLR   R7            EXIT TO BAD ACCOUNT OR SHORT SUBACCT
         LA    R9,1(,R9)
         BCT   R10,JACQBYTE  TRY AGAIN                           81334
         LA    R7,JACBDSUB   ON SUBACCOUNT ERROR DO NOT BOTHER OPSIE
         BCT   R2,JACQWORD   AND TRY AGAIN                       81334
         TESTMSG 9           ACCOUNT NUMBER ERROR?               81334
         BNZ   CHECKACT      IF YES, THEN WE'VE BEEN HERE ALREADY
JACQMORE BXH   R1,R14,JACBDSUB  LOOK FOR MORE                    81334
         CLI   0(R1),C''''   SPECIAL CHARACTERS ILLEGAL          81334
         BE    JACBDSUB                                          81334
         CLI   0(R1),C' '
         BE    JACBDSUB                                          81334
         CLI   0(R1),C')'    NORMAL TERMINATION ?                81334
         BE    JACQDONE      YES                                 81334
         CLI   0(R1),C','               END OF SUBFIELD?
         BNE   JACQMORE                                          81334
         BCT   R0,JACQMORE   REPEAT FOR EXPECTED SUBFIELD COUNT  81334
         BXH   R1,R14,JACBDSUB   MORE ON THIS CARD ?             81334
         TM    JOBSW1,JOBCAN    SPECIAL JOB ?
         BO    JACALLOW                                          81334
         CLI   0(R1),C')'    END OF FIELD ?                      81334
         BE    JACQDONE      YES; PERMIT IT                      81334
*
*   CHECK FOR 'PASS', 'ZERO', OR 'CONS' WHICH WILL ACT AS ACCT=????
*
         CLC   =C'PASS',0(R1)  ONLY LEGAL IN CANNED JOB JOBS
         BE    JACBDSUB                                          81334
         CLC   =C'ZERO',0(R1)  ONLY LEGAL IN INTERNAL TEXT
         BE    JACBDSUB                                          81334
JACALLOW BCT   R0,JACQMORE   SET SUBFIELD COUNT NEGATIVE         81334
JACQDONE TM    JOBSW1,HASPJOB    HASP JOB ?                      81334
         BO    JPGMLOOK      YES, ALLOWED MULTIPLE FIELDS        81334
         SRA   R0,1          TEST FOR MORE THAN 2
         BP    JACBSUB       BAD SUBACCOUNT FIELD                81334
         ORG   *-4           *****TEMPORARY BYPASS*****          85154
JPGMLOOK BXH   R1,R14,JACBSUB    BUMP PAST RIGHT PARENTHESIS     81334
         CLI   0(R1),C' '    MISSING PROGRAMMER NAME ?           81334
         BE    JACBSUB       YES - THEN BLANK THE JOB            81334
         OI    JOBSW1,WHORU  SET PROG. NAME EXPECTED SWITCH      81334
         CLI   0(R1),C','               COMMA?
         BNE   CHECKACT                 DON'T TELL -
*                                        LET USER PAY THE SIN-TAX
         BXH   R1,R14,CHECKACT          COMMA IN COL 71 IS KOSHER
         CLI   0(R1),C' '               CONTINUATION?
         BE    CHECKACT
JPGMTEST TM    JOBSW1,WHORU             IS PROGRAMMER NAME EXPECTED?
         BZ    JMSGCLAS                  NO - LOOK FOR ALLCLASS=X
         NI    JOBSW1,255-WHORU
         MVC   SMF5PRGN,0(R1)  BRUTAL WAY TO GET NAME FIELD      81334
         CLI   0(R1),C''''             IS THIS A REALLY SPECIAL
         BNE   JMSGCLAS                  CHARACTER?              81334
         MVC   SMF5PRGN,1(R1)   SAVE PROGRAMMER NAME LESS QUOTE  81334
JPGMQUOT BXH   R1,R14,JPGMBCON                                   81334
         CLI   0(R1),C''''   TRAILING OR DOUBLED QUOTE ?
         BNE   JPGMQUOT      NO; ANY OLD CHARACTER               81334
         CLI   1(R1),C''''   IS IT DOUBLED ?
         BNE   JMSGCLAS      NO; END OF FIELD                    81334
         BXLE  R1,R14,JPGMQUOT                                   81334
JPGMBCON SETMSG 7            TAX THE SYN                         81334
         B     CHECKACT
         SPACE 1
JMSGCLAS LA    R2,JOBVERBS                                       81334
         BAL   R6,VERBLOOK   LOOK FOR SPECIALS
*        ALLCLASS=X CAUSES ALL NON-PUNCH FILES TO BE CHANGED TO  81334
*              SYSOUT=X                                          81334
*        PRTCLASS=X CAUSES ALL SYSOUT=A TO BE CHANGED TO X       81334
         B     CHECKACT      THEN CHECK ACCOUNT NUMBER
         SPACE 1
EUREKA1  MVC   JOBPRT,0(R1)  SAVE REQUESTED OUTPUT CLASS
         NI    JOBPRT,255-C' '  ONLY SYSOUT=A IS CHANGED
         B     WINE          GET IT ?
EUREKA   MVC   JOBPRT,0(R1)  ALL BUT SYSOUT=B CHANGED TO REQ. CLASS
WINE     LR    R6,R1         POINT TO CLASS
         SH    R6,=H'9'      POINT TO TYPE
         MVC   0(3,R6),=C'MSG'
         B     CHECKACT
         SPACE 1
JACKPOT  TM    MGFLAG,MGSKP  BEEN HERE BEFORE ?                  81334
         BZ    JACKPOT2      YES; JUST RESET ERROR FLAG          81334
         SPACE 1
         L     R9,CARDC01    GET CARD START ADDRESS
         LA    R10,80        SET LENGTH                          85161
         L     R15,=A(IEFVGMSG)  GET WRITER ADDRESS              85154
         BAL   R14,IEFVGM80-IEFVGMSG(,R15)  CALL CARD WRITER     85161
         MVI   1(R9),C'&&'   MAKE REPLACEMENT CARD ID NEXT TIME
         NI    MGFLAG,255-MGSKP  NO NEED TO DELAY MESSAGES
         SPACE 1
JACKPOT2 SETMSG 9                                                81334
         L     R1,ACCTSTRT             BEGINNING OF ACCT FIELD
         MVI   0(R1),C'('
         MVI   1(R1),C'?'              SET UP FOR QUESTIONING
         CLI   10(R1),C','   THIRD SUBFIELD ?
         BE    JOBQACCT      YES; LEAVE IT                       81334
         MVI   10(R1),C')'
         MVI   11(R1),C','
         B     JOBQACCT                                          81334
         EJECT
JACBSUB  SETMSG 9            BAD SUBACCOUNT FIELD OR SYNTAX      81334
         SPACE 1                                                 81334
CHECKACT TM    SWB,CXP                 IS THIS A CONTINUATION?
         BO    SETCLS                  EXIT IF SO                90211
         LA    R1,JMRUSEID             ADDRESS OF USER I.D.
         CLC   ACCTFLIP(4),JMRUSEID       IS THIS A BIG VOLUME JOB ?
         BNE   *+8                     BRANCH IF NOT
         LA    R1,4(,R1)             INCREMENT TO SECOND USERID FIELD
         LA    R2,3          LOOP COUNTER
CHKANUM  LA    R10,0(R1,R2)  ADDR OF DIGIT IN ACCT. NO.          92299
         CLI   0(R10),C'0'             NUMERIC?
         BL    JACKPOT                 ERROR IF NOT NUMERIC      81334
         CLI   0(R10),C'9'             NUMERIC?
         BH    JACKPOT                 ERROR IF NOT NUMERIC      81334
         BCT   R2,CHKANUM    CHECK PREVIOUS DIGIT                79068
         LA    R2,ALPHAS     GET NUMERIC/ALPHA TABLE
         LA    R10,L'ALPHAS  MAX NUMBER
         LR    R9,R10        SAVE NUMBER                         81334
ALPO     CLC   0(1,R1),0(R2)   DID WE HIT IT YET ?               92299
         BE    ALPINE        OK
         LA    R2,1(,R2)     NEXT TABLE ENTRY
         BCT   R10,ALPO      CHECK IT
         B     JACKPOT       TOO BAD                             81334
ALPINE   SR    R9,R10        GET RELATIVE VALUE                  81334
         MH    R9,=H'1000'   LEAVE ROOM FOR LOW DIGITS           92299
         LR    R10,R1                  ADDR OF ACCOUNT NUMBER
         ICM   R1,15,@TABACCT   GET ACCOUNT TABLE                92299
         BZ    JACNBATS      NO TABLE; TREAT AS VALID            92299
         PACK  LINKWORK(8),1(3,R10)  CONVERT LAST TWO DIGITS     92299
         CVB   R11,LINKWORK
         AR    R11,R9        ADD IN OTHER STUFF
         SLR   R10,R10
         D     R10,=F'8'               DIVIDE BY 8 FOR BYTE AND MASK
         LA    R1,0(R1,R11)            POINT TO BYTE IN TABLE
         LA    R10,MASKTAB(R10)        AND POINT TO MASK
         MVC   MSGWORK(1),0(R1)        MOVE BYTE TO WORKING STORAGE
         LA    R1,2250(,R1)   UP BY HALF-TABLE SIZE              79049
         MVC   MSGWORK+1(1),2250(R1) ALSO COPY SUBMIT FLAG       79049
         NC    MSGWORK(1),0(R10)       CLEAR OFF BITS
         BZ    JACKPOT       IF BIT NOT ON, THEN ACCOUNT IS INVALID
         NC    MSGWORK+1(1),0(R10)  TEST TEMPORARY DISABLE BIT   81334
         BNZ   JACNBATS      BATCH SUBMISSION ALLOWED            81334
         SETMSG 16           SHOW BATCH NOT ALLOWED              81334
         B     SETCLS        QUIT FURTHER CHECKING               90211
JACNBATS CLC   JMRUSEID(3),ACCTFLIP  PRIVILEGED RANGE ?
         BNE   SETCLS        NO; EXIT                            90211
         CLI   JMRUSEID+3,C'0'   BENCH-MARK ?
         BE    RERUNT       YES - SEE IF AUTHORIZED              81334
         CLI   JMRUSEID+3,C'4'     IS IT SYSTEM JOB ?
         BL    SETCLS                  EXIT IF NOT               90211
         BE    *+12          YES; TEST IT
         OI    JOBSW1,JOBSOME   ALLOWED SOME PRIVILEGES
         B     SETCLS                                            90211
         TM    JOBSW1,JOBCAN    SPECIAL JOB ?
         BO    SETCLS        OK                                  90211
         OI    JOBSW1,JOBSYS    SHOW SEMI-SPECIAL JOB
         CLC   SUBSYS(3),JMRUSEID+4   CORRECT SUB-ACCOUNT ?
         BE    SETCLS        OK                                  90211
         NI    JOBSW1,255-JOBSYS
         SETMSG 9            BAD ACCT. INFO                      81334
         B     SETCLS        AND WRITE MESSAGE                   90211
         SPACE 1
RERUNT   DS    0H                                                92299
         AIF   ('&LOCAL' NE 'CCSI').NORRUN                       92299
         ICM   R9,15,ADJQE   JQE PRESENT ?                       81334
         BZ    RERUNTN       NO                                  81334
         TM    QUETYPE(R9),X'F0'   JCL-SCAN QUEUE ?              81334
         BNO   RERUNTN       NO; QUERY OPSIE                     81334
         TM    JQEOCLAS-JQEDSECT(R9),X'F0'  JCL-SCAN ONLY ?      85112
         BNO   SETCLS        NO; CATCH IT WHEN IT EXECUTES FOR REAL
.NORRUN  SPACE 1                                                 92299
RERUNTN  LA    R9,TESTRUN    ASK OPERATOR IF BENCHMARK IS AUTHORIZED
         BAL   R14,PERMIT              ASK PERMISSION
         SETMSG 10           **ERROR** - RERUN REFUSED           81334
         B     SETCLS                  O.K. ON RERUN, EXIT       90211
         SPACE 1
ALPHAS   DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789'           92299
NUMBAS   EQU   ALPHAS+26,10,C'C'  DIGITS                         92299
         SPACE 1                                                 90211
SETCLS   MVC   JBCLASS,JMRCLASS   COPY CURRENT CLASS             90211
         CLI   JBCLASS,C'0'  JCL SCAN REQUEST ?                  90211
         BNH   SETJCLSC      NO; LEAVE 0 AS 0                    90211
         NI    JBCLASS,255-X'30' CONVERT TO A-I RANGE            90211
SETJCLSC IC    R10,JBCLASS                                       90211
         STC   R10,JMRUCOM   SAVE FOR ALLOCATE AND SCRATCH/EXTEND
         CLI   JMRUCOM,C'0'  JCL-SCAN ONLY ?                     90211
         BNH   SETJCLNS      NO; LEAVE IT                        90211
         NI    JMRUCOM,255-X'30'  MAKE EQUIVALENT EXECUTION CLASS
SETJCLNS MVI   XTIME,20      SET MAXIMUM VALID TIME INDEX        90211
         MVI   XREGN,12      SET MAXIMUM VALID REGION REQUEST    90211
         ICM   R10,15,@LEXVOLT  GET VOLUME/SMF TABLE             92299
         BZ    SETCLSND      NO TABLE, NO FAILURE                92299
         USING USERVOLT,R10  TEMP. DECLARE                       90211
         TM    JOBSW1,JOBCAN   PRIVILEGED ?                      90211
         BNZ   SETCLSND      YES                                 90211
         L     R1,APCLASS    GET CLASS LOOKUP TABLE              90211
SETLKCLS CLC   JMRUCOM(1),0(R1)   MATCHING CLASS ?               90211
         BE    SETFNCLS      YES, PROCESS VALUES                 90211
         LA    R1,3(,R1)     TRY NEXT ENTRY                      90211
         C     R1,APCLASS+4  ON LAST ENTRY ?                     90211
         BL    SETLKCLS      NO; LOOK AGAIN                      90211
SETFNCLS TM    JOBSW1,JOB    ANY SPECIAL PRIVILEGES ?            90211
         BNZ   SETCLSND      YES; DON'T RESET                    90211
         MVC   XTIME(2),1(R1)   SET TIME/REGION INDEX BYTES FOR C
SETCLSND B     RETURN        GO FOR MORE                         90211
         EJECT ,
XEQCARD  TM    JOBSW1,JOBCHKD  JOB CARD CHECKED ALREADY ?
         BO    CHKXEQ        YES, DO ANY OTHER CHECKING
         MVC   JBCLASS,JMRCLASS   COPY CURRENT CLASS             85112
*        MVN   SMF5JPTY,JCTJPRTY    COPY OS PRIORITY             81334
         MVI   SMF5JPTY,4    SET DFLT PRTY *****TEMPORARY*****   85168
         AIF   ('&LOCAL' NE 'CCSI').NOXEQS                       92299
         ICM   R1,15,ADJQE   HASP JOB ?                          81334
         BZ    XEQSET        NO; PRIORITY AND CLASS ARE SET      81334
         MVN   SMF5JPTY,JQEOPRTY-JQEDSECT(R1) USER'S PRIORITY    85112
         MVC   JBCLASS,JQEOCLAS-JQEDSECT(R1)  SET SUBMISSION CLASS
.NOXEQS  SPACE 1                                                 92299
XEQSET   CLI   JBCLASS,C'0'  JCL SCAN REQUEST ?
         BNH   XEQJCLSC      NO; LEAVE 0 AS 0
         NI    JBCLASS,255-X'30' CONVERT TO A-I RANGE
XEQJCLSC IC    R10,JBCLASS                                       90197
         STC   R10,SMF5JICL                                      81334
         STC   R10,JMRUCOM   SAVE FOR ALLOCATE AND SCRATCH/EXTEND SVC
         CLI   JMRUCOM,C'0'  JCL-SCAN ONLY ?                     81334
         BNH   XEQJCLNS      NO; LEAVE IT                        81334
         NI    JMRUCOM,255-X'30'  MAKE EQUIVALENT EXECUTION CLASS
XEQJCLNS MVI   XTIME,20      SET MAXIMUM VALID TIME INDEX        81334
         MVI   XREGN,12      SET MAXIMUM VALID REGION REQUEST    81334
         TM    JOBSW1,JOBCHKD   JOB CARD ALREADY CHECKED?        81334
         BO    CHKXEQ                  YES-- GO CHECK EXEC CARD
         OI    JOBSW1,JOBCHKD   SET JCT CHECK COMPLETE           81334
         ICM   R10,15,@LEXVOLT  GET VOLUME/SMF TABLE             92299
         BZ    DOSETIME      NO TABLE; USE DEFAULT               92299
         USING USERVOLT,R10  TEMP. DECLARE                       81334
         TM    JOBSW1,JOBCAN   PRIVILEGED ?                      79079
         BNZ   DOSETIME      YES; SET DEFAULT TIME               79079
         L     R1,APCLASS    GET CLASS LOOKUP TABLE
LOOKCLAS CLC   JMRUCOM(1),0(R1)   MATCHING CLASS ?               81334
         BE    FNDCLASS      YES, PROCESS VALUES
         LA    R1,3(,R1)     TRY NEXT ENTRY
         C     R1,APCLASS+4  ON LAST ENTRY ?
         BL    LOOKCLAS      NO; LOOK AGAIN
FNDCLASS TM    JOBSW1,JOB    ANY SPECIAL PRIVILEGES ?
         BNZ   *+10          YES; DON'T RESET
         MVC   XTIME(2),1(R1)   SET TIME/REGION INDEX BYTES FOR CLASS
DOSETIME SLR   R1,R1         CLEAR FOR ICM                       81334
*        ICM   R1,7,JCTJMRTL   GET JOB CARD TIME SPECIFICATION   81334
*        BZ    DEFTIME       NONE; USE DEFAULT TIME              81334
*        M     R0,AMMPCPU    GET CPU CONVERSION MULTIPLIER
*        D     R0,AMDVCPU    AND DIVISOR
*        STCM  R1,7,JCTJMRTL  SET NEW VALUE                      81334
*        B     DOROLL        NOW CHECK ROLL OPTION
*        SPACE 1
*EFTIME  SLR   R1,R1         CLEAR FOR IC                        81334
*        IC    R1,XTIME      GET TIME ENTRY FOR THIS CLASS
*        AL    R1,APTIMLM    GET ADDRESS OF VALUE
*        C     R1,APTIMDF    MORE THAN DEFAULT TIME ?
*        BNH   DEFTIMER      NO; USE IT                          81334
*        L     R1,APTIMDF    ELSE USE DEFAULT
*EFTIMER MVC   JCTJMRTL,1(R1)   SET JOB TIME BY CLASS DEFAULT    81334
         DROP  R10
         SPACE 1
*OROLL   MVC   XQTIMVAL,JCTJMRTL  JUST IN CASE - SET STEP TIME   82283
*        TM    SWD,JOBROLLF  WAS ROLL REQUESTED ?
*        BZ    ISITREG       BRANCH IF NO ROLLY-POLY             81334
*        ROLL OPTION NOT ALLOWED
*        SETMSG 6            SET ERROR FLAG                      81334
*
ISITREG  TM    JOBSW1,JOBCAN   PRIVILEGED ?                      81334
         BNZ   RETURN        YES; NO FURTHER CHECKS              81334
*        TM    SWD,JOBREGNS  WAS REGION SPECIFIED ?              81334
*        BZ    ISITPRTY                NO - THEN IGNORE
*        CLC   =H'128',IWAJOBS6         WAS IT .GT. 128K         79049
*        BNL   ISITPRTY                IF NOT THEN CHECK PRIORITY
*        CLC   =H'7168',IWAJOBS6  GREATER THAN MAX ?             82141
*        BL    ISITBREG      YES; INVALID REGION REQUEST         81334
*        CLI   JBCLASS,C'F'  IS IT CLASS F ?                     90197
*        BE    ISITPRTY      YES, ALLOW IT
*        CLI   JBCLASS,C'I'  OR CLASS=I ?                        90197
*        BE    ISITPRTY      OK
*SITBREG SETMSG 3            ILLEGAL REGION REQUEST ON JOB CARD  81334
ISITPRTY CLI   SMF5JPTY,6    PRIORITY = 6?                       81334
*        BNH   ISITPLOW                IF SO THEN CONTINUE
*        TM    JOBSW1,JOB+JOBSOME    ANY PRIVILEGES ?
*        BNZ   ISITIME       YES; ALLOW
*        B     PRTYERR       ELSE FAIL
ISITPLOW CLI   SMF5JPTY,3   PRIORITY 4 ?                         81334
*        BNL   ISITIME       OK
*        ORG   *-4                                               82017
         B     ISITIME       ***BYPASS CHECK***                  82017
*        ICM   R1,15,ADJQE   SEE IF HASP JOB                     81334
*        BZ    ISITPLOH      NO; NO CHECK FOR JCL-SCAN           81334
*        TM    QUETYPE(R1),X'F0'  JCL-SCAN ?                     81334
*        BO    ISITIME       YES; WHO CARES ?                    81334
*SITPLOH TIME  BIN           GET TIME                            81334
*        ST    R1,LINKWORK+4  SAVE DATE FOR LATER
*        C     R0,=A(8*60*60*100)      8 O'CLOCK IN THE MORNING
*        BL    ISITIME                 IF LOWER THEN EARLIER
*        C     R0,=A(16*60*60*100)     4 PM OR LATER ?
*        BH    ISITIME                 BRANCH IF ALMOST MIDNIGHT
*        SLR   R0,R0         INSERT CLEAR
*        IC    R0,LINKWORK+5  INSERT YEAR
*        SRDL  R0,4          PUT DECADES IN R0
*        SRL   R1,28         AND UNITS IN R1
*        SLL   R0,1          DECADES*2
*        AR    R1,R0         ADD IN
*        SLL   R0,2          DECADES*2*4
*        AR    R1,R0         = UNITS + (2+8)*DECADES
*        XC    LINKWORK(6),LINKWORK   CLEAR YEAR
*        LA    R1,6(,R1)     ADD FINAGLE FACTOR
*        LA    R9,1(,R1)     SAVE YEAR + SECOND FINAGLE
*        SRA   R9,2          GET NUMBER OF LEAP YEARS SINCE 1900
*        MH    R1,=H'365'    DAYS SINCE 1900
*        AR    R1,R9         ADD LEAP DAYS
*        CVB   R0,LINKWORK   GET DAYS
*        AR    R1,R0         ADD DAY IN THIS YEAR
*        SLR   R0,R0
*        D     R0,=F'7'      GET DAY OF WEEK
*        C     R0,DCF1       SATURDAY IS 0, SUNDAY=1
*        BNH   ISITIME       NO CHECK IF WEEKEND
*        LA    R9,LOWPRTY    ASK OPERATOR IF LOW PRIORITY VALID
*        BAL   R14,PERMIT
*        SETMSG 4            SET PRIORITY ERROR                  81334
         B     ISITIME                 GO CHECK TIME ANYWAY
PRTYERR  SETMSG 4            SET PRIORITY ERROR FLAG             81334
*        NI    JCTJPRTY,X'F0'          SET PRTY TO ZERO
ISITIME  SLR   R1,R1         CLEAR                               81334
*        IC    R1,XTIME      GET TIME VALUE INDEX
*        ICM   R9,15,@LEXVOLT   GET VOLUME TABLE                 92347
*        BZ    CHKXEQ        ALLOW IF I CAN'T CHECK IT           92347
*        AL    R1,APTIMLM-USERVOLT(,R9)  GET MAX ALLOWED TIME    81334
*        CLC   JCTJMRTL,1(R1)   EXCEEDS MAX FOR CLASS ?
*        BNH   CHKXEQ        NO, USE IT
*SITIME3 MVC   JCTJMRTL,DCF1+1         MOVE IN 1
*        SETMSG 5            SET TIME ERROR FLAG                 81334
         SPACE 2
*        EXEC CARD CHECKING
CHKXEQ   LM    R14,R1,CARDBXLE  RESTORE BXLE POINTERS FOR SCANNING
         TM    SWB,CXP       CONTINUATION CARD ?                 81334
         BNZ   CHKXEQS       YES; NO INCREMENTATION
         TM    SWE,PROC      PROC EXEC ?                         82283
         BNZ   CHKXEQNP      YES; NO RESET                       82283
         MVI   XQTIMFG,XQTIMCK  SET FOR TIME= CHECK IN TEXT EXIT 82283
*        MVC   XQTIMVAL,JCTJMRTL-IEFJCT+JCT  COPY JOB TIME       82283
CHKXEQNP BAL   R6,XEQREGN    GO TO REGION CHECK FOR PREVIOUS EXEC
         OI    MGFLAG,DOEXEC   SET FOR EXEC CARD TEST
         TM    MGFLAG,SKP#UP  SKIP STEP UPDATE ?
         BZ    CHKXEQU       NO
         NI    MGFLAG,255-SKP#UP  RESET
         B     CHKXEQS       CHECK FOR REPLACEMENT
CHKXEQU  SLR   R9,R9
         IC    R9,SMF5NST    GET STEP NUMBER                     81334
         LA    R9,1(,R9)     UP BY ONE
         STC   R9,SMF5NST                                        81334
         B     CHKXEQS       GO TO TEXT REPLACEMENT SCAN         81334
         SPACE 1
PROCCARD OI    MGFLAG,SKP#UP SET TO SKIP NEXT EXEC STEP # INCREMENT
CHKXEQS  LM    R14,R1,CARDBXLE  RESTORE SCAN POINTERS            81334
         B     DDXEQ         GO TO COMMON PARAMETER REPLACE ROUTINE
         SPACE 2
STEPSUB  SLR   R10,R10                                           81334
         IC    R10,SMF5NST                                       81334
         CVD   R10,LINKWORK
         MVC   1(4,R1),=X'F0202120'  MOVE EDIT MASK
         ED    1(4,R1),LINKWORK+6    REPLACE BY STEP NUMBER
         BR    R9            RETURN TO CALLER
         SPACE 1                                                 81334
STEPRVOL L     R10,CVTPTR    LOAD CVT ADDRESS                    81334
         USING CVTMAP,R10                                        81334
         L     R10,CVTSYSAD  POINT TO UCB FOR SYSRES PACK        81334
         USING UCBOB,R10                                         81334
         CLI   4(R1),C'#'    REQUEST FOR CURRENT SYSTEM'S PACK ? 81334
         MVC   0(5,R1),UCBVOLI  COPY VOLSER PREFIX OF IPL PACK   81334
         BER   R9            YES; RETURN TO CALLER               81334
         XI    3(R1),1       ELSE MAKE OTHER SYSTEM'S PREFIX     85168
         BR    R9            RETURN TO CALLER                    81334
         DROP  R10           DONE WITH UCB                       81334
         EJECT ,
DDCARD   LA    R6,DDKARD     SET RETURN AFTER EXEC REGION CHECK
XEQREGN  TM    MGFLAG,DOEXEC  PREVIOUS EXEC CARD CHECK ?
         BZR   R6            NO
         NI    MGFLAG,255-DOEXEC   TURN TEST REQ. OFF
*        ICM   R9,15,@LEXVOLT   GET VOLUME TABLE                 92347
*        BZ    XEQTIMNO                                          92347
*        TM    SWC,PEXP      PROC EXEC ?
*        BNZ   XEQTIMNO      YES-ASSUME PROC EXEC - DON'T TIME TWICE
*        LR    R2,R1         SAVE R1
*        SLR   R1,R1         CLEAR FOR ICM                       81334
*        ICM   R1,7,SCTSTIME-IEFSCT+SCT  GET STEP TIME           81334
*        BZ    XEQTIMDF      NONE - USE JOB TIME AS DEFAULT
*        M     R0,AMMPCPU-USERVOLT(,R9)  MULTIPLY                81334
*        D     R0,AMDVCPU-USERVOLT(,R9)  AND DIVIDE TO ADJUST TO CPU
*        STCM  R1,7,SCTSTIME-IEFSCT+SCT   PUT NEW TIME BACK      81334
*        B     XEQTIMDN
*EQTIMDF MVC   SCTSTIME-IEFSCT+SCT(3),XQTIMVAL  USE JOB/PROC TIME
*EQTIMDN LR    R1,R2         RESTORE R1
*EQTIMNO TM    XQTIMFG,XQTIMCK+XQTIMON  PROC TIME= FOUND ?       82283
*        BNO   XEQTIMNF      NO                                  82283
*        MVC   XQTIMVAL,SCTSTIME-IEFSCT+SCT  SAVE VALUE          82283
*EQTIMNF MVI   XQTIMFG,0     RESET THE FLAGS                     82283
*        TM    JOBSW1,JOBCAN   CANNED JOB PRIVILEGES ?           82283
*        BNZR  R6            YES; ALLOW EVERYTHING ELSE          81334
*        SLR   R10,R10       CLEAR                               81334
*        IC    R10,XREGN     GET INDEX TO REGION TABLE           81334
*        AL    R10,APREGLM-USERVOLT(,R9)  GET MAX REGION PERMITTED
*CTEQREG EQU   X'58'         SEE SCT EXPANSION
*        CLC   SCTEQREG+SCT(2),0(R10)    VALID REQUEST ?
*        BNHR  R6            OK, USE IT
*        CLC   SCTEQREG+SCT(2),=H'7168'  GREATER THAN PREFERRED MAX ?
*        BNHR  R6            NO; PERMIT IT                       79049
*        SETMSG 13           SHOW BOOBOO                         81334
         BR    R6            RETURN TO CALLER
         SPACE 1
DDKARD   TM    SWB,CXP       CONTINUATION ?
         BO    DDLOOP                  YES - THEN CONTINUE
*        TM    SWB,DDAST+DDATA  LIKE DD *  OR  DD DATA ?
*        BNZ   RETURN        IF SO THEN IGNORE THE WHOLE THING
         SPACE 1
DDLOOP   BALR  R7,0          SET RETURN ADDRESS FOR DD SCAN      81334
         LA    R2,DDTVERBS   SET FOR FULL TEST                   81334
         CLI   JOBPRT,0      IS PRT/ALL CLASS OPTION REQUESTED
         BNE   DDVLOOK                                           81334
         BALR  R7,0          SET RETURN POINT                    81334
         LA    R2,DDTDSNT    SET TO LOOK FOR DSN=                81334
         TM    FDD,FDDFOUND  PRIOR DSN= FOUND ?                  81334
         BZ    DDVLOOK       NO; USE LONGER LIST                 81334
DDXEQ    BALR  R7,0          SET RETURN POINT FROM SCAN          81334
         LA    R2,DDTVERB    USE SHORTER TABLE (ALSO EXEC/PROC)  81334
DDVLOOK  BAL   R6,VERBLOOK   GO TO LOOKUP ROUTINE                81334
         B     RETURN        EXIT WHEN DONE
         SPACE 1
DDTRESVL LA    R9,STEPRVOL   SET FOR VOLSER REPLACEMENT          81334
         B     DDTSTEPC                                          81334
DDTSTEP  LA    R9,STEPSUB    SET FOR STEP NUMBER REPLACEMENT     81334
DDTSTEPC SH    R1,H5         BUMP BACK TO START OF FIELD         81334
         BALR  R9,R9         REPLACE BY STEP NUMBER OR VOLSER    81334
         LA    R1,5(R1,0)    FORWARD SPACE AGAIN                 81334
H5       EQU   *-2           KLUTZY CONSTANT
         BR    R7            RETURN TO DDLOOP OR DDXEQ           81334
         SPACE 1                                                 92299
DDTSYSID SH    R1,H4         BACK-UP TO START OF TEXT            92299
         BAL   R9,SETSYSID   MAKE SYSTEM ID OUT OF ##ID          92349
         BR    R7            RETURN                              92299
SETSYSID L     R10,CVTPTR                                        92299
         L     R10,CVTSMCA-CVTMAP(R10)  GET SMCA                 92299
         MVC   0(4,R1),16(R10)                                   92299
         LA    R1,4(R1,0)    FORWARD HO                          92299
H4       EQU   *-2,2,C'H'    KLUTZY CONSTANT                     92299
         BR    R9            RETURN TO CALLER                    92349
         SPACE 1
DDTSOUT  CLI   0(R1),C'('    LIST ?
         BNE   DDTSOUTA      NO
         LA    R1,1(,R1)     SKIP IT
DDTSOUTA CLI   0(R1),C'A'    VALID FIELD (OR SYMBOLIC) ?
         BLR   R7            NO                                  81334
         BE    DDSOUTS       A - REPLACE
         TM    JOBPRT,X'40'  ALLCLASS ?
         BZR   R7            NO; NO CHANGE OTHER THAN A          81334
         CLI   0(R1),C'B'    POSSIBLY PUNCH CLASS ?
         BER   R7                      IGNORE PUNCHED O/P        81334
         CLI   0(R1),C'K'    HASP SPECIAL ?
         BER   R7                      IGNORE THIS ONE TOO       81334
DDSOUTS  MVC   0(1,R1),JOBPRT          MOVE IN PRINT CLASS
         OI    0(R1),X'40'             MAKE IT A CHARACTER
         BR    R7                      CONTINUE SCANNING         81334
         SPACE 1                                                 81334
DDTDSN   TM    FDD,FDDFOUND  PRIOR DSN= FOUND ?                  81334
         BNZ   DDTDSNTQ      YES; CHECK MEMBER NAME              90210
         CLI   0(R1),C' '    NULL ?                              81334
         BER   R7            YES; SKIP                           81334
         CLI   0(R1),C','    ALTERNATE NULL ?                    81334
         BER   R7            YES; SKIP                           81334
         CLI   0(R1),C''''   QUOTED ?                            81334
         BNE   *+8           NO                                  81334
         OI    FDD,FDDQUOTE  SET QUOTED                          81334
         OI    FDD,FDDFOUND  SHOW DSN= FOUND                     81334
DDTDSNTQ CLI   0(R1),C''''   QUOTED STRING ?                     90210
         BER   R7            YES - NO CHANGE                     90210
DDTDSNLP CLI   0(R1),C' '    END ?                               90210
         BER   R7            YES                                 90210
         CLI   0(R1),C','    ALTERNATE END ?                     90210
         BER   R7                                                90210
         CLI   0(R1),C'#'    POSSIBLE STEP NUMBER ?              90210
         BNE   DDTDSNML      NO; CHECK FOR MEMBER START          90210
         CLC   0(4,R1),DDTVID+4  SYSTEM ID REQUEST ?             92349
         BE    DDTDSNID                                          92349
         CLC   1(3,R1),0(R1)  STEP NUMBER REQUEST ?              90210
         BNE   DDTDSNBP      NO                                  90210
         BCTR  R1,0          FINAGLE                             90210
         BAL   R9,STEPSUB    REPLACE BY STEP NUMBER              90210
         LA    R1,5(,R1)                                         90210
         B     DDTDSNML                                          92349
DDTDSNID BAL   R9,SETSYSID   REPLACE BY SYSTEM ID                92349
DDTDSNML CLI   0(R1),C'('    START OF MEMBER/GDG NAME ?          90210
         BE    DDTDSNLM      YES                                 90210
DDTDSNBP BXLE  R1,R14,DDTDSNLP  KEEP LOOKING                     90210
DDTDSNLM BXH   R1,R14,0(R7)  KEEP LOOKING OR RETURN TO SCAN      90211
         CLI   0(R1),C' '    ERROR ?                             90210
         BER   R7            YES                                 90210
         CLI   0(R1),C','    OTHER ERROR ?                       90210
         BER   R7                                                90210
         CLI   0(R1),C'#'    STEP NUMBER ?                       90210
         BNE   DDTDSNST      NO                                  90210
         CLC   0(4,R1),DDTVID+4  SYSTEM ID REQUEST ?             92349
         BE    DDTDSNMD      YES                                 92349
         CLC   1(3,R1),0(R1)  STEP REPLACE ?                     90210
         BNE   DDTDSNST      NO                                  90210
         BCTR  R1,0                                              90210
         BAL   R9,STEPSUB    REPLACE                             90210
         LA    R1,4(,R1)     SKIP OVER                           90210
         B     DDTDSNLM      LOOK AGAIN                          90210
DDTDSNMD BAL   R9,SETSYSID   REPLACE ID                          92349
DDTDSNST CLI   0(R1),C')'    MEMBER END ?                        90210
         BNE   DDTDSNLM      NO; CONTINUE                        90210
         LR    R10,R1        COPY CURRENT POSITION               90210
         LA    R9,8          SET MAX TO CHECK                    90210
         MVI   FDDMIN,0      RESET TRAILING - COUNT              90218
DDTDSNMN BCTR  R10,0         PREVIOUS BYTE                       90210
         CLI   0(R10),C'-'   TRAILING MINUS ?                    90210
         BNER  R7            NO; GET OUT                         90210
         STC   R9,FDDMIN     SAVE TRAILING COUNT                 90218
         MVI   0(R10),C'#'   REPLACE BY PUNK/PDSSCAN MASK        90210
         BCT   R9,DDTDSNMN   TRY AGAIN                           90210
         BR    R7            RETURN TO SCAN                      81334
         TITLE 'I E F U J V  ***  ENQUEUE EXIT - FINAL RITES'
ENQENTRY TM    RETCH,BUM     FAILED ?                            85112
         BNZ   DO5SET        YES; FORCE OUT TYPE 5 RECORD
         ICM   R0,3,CWARET        JOB FAILED ?
         BNZ   DO5SETH       YES, CHARGE                         79149
         AIF   ('&LOCAL' NE 'CCSI').NOBUM                        92299
         ICM   R15,15,ADJQE   SEE IF HASP JOB                    81334
         BZ    REALYDON      NO;  EXIT
         TM    JQEOCLAS-JQEDSECT(R15),X'F0'   JCL-SCAN ONLY ?    85112
         BO    DO5           YES - WRITE TYPE 5 AND BILL
         TM    QUETYPE(R15),X'F0'  DOING JCL SCAN ?              81334
         BNO   REALYDON      NO, NO CHARGE (YET)                 81334
         OI    RETCH,BUM     DO PSEUDO-FAIL ON NO ERROR
.NOBUM   B     REALYDON      BUT EASIEST JCL SCAN ?
         SPACE
DO5SET   OI    SMF5JBTI,X'40'  TERMINATED BY UJV EARLIER ?       81334
DO5SETH  ICM   R1,15,ADJQE   SEE IF HASP JOB                     81334
         BZ    DO5           NO, OK                              79149
         AIF   ('&LOCAL' NE 'CCSI').NOBUM2                       92299
         OI    QUEFLAG3(R1),QUEJCLER  SET JCL ERROR BIT          79149
         NI    QUEFLAGS(R1),255-QUEHOLD1  JUST IN CASE - UNHOLD  79149
.NOBUM2  ANOP  ,                                                 92299
DO5      L     R5,CWAJMRPT   MAKE SURE OF JMR                    85112
         OI    RETCH,BUM     FORCE ERROR
*        TM    JOBSW1,RJEJOB   REMOTE JOB ?
*        BO    REALYDON      BYPASS CHARGE, MAY BE LINE ERROR
         MVI   SMF5LEN+1,S05END-SMFRCD5   SET LENGTH             81334
         MVI   SMF5FLG,2     SET VS FLAG                         81334
         MVI   SMF5RTY,5     SET FOR TYPE 5 RECORD               81334
         TIME  BIN           GET THE TIME
         STM   R0,R1,LINKWORK  TIME RECORDED
         MVC   SMF5TME(8),LINKWORK  SET TIME WRITTEN             81334
         MVC   SMF5RSTT(8),LINKWORK  READER STOP TIME            81334
         MVC   SMF5JIT(8),LINKWORK  JOB INIT TIME                81334
         MVC   SMF5SID(4),JMRCPUID  GET SMCA ID/MDL              81334
         MVC   SMF5JBN(16),JMRJOB  COPY PARTIAL JOB LOG, LEAVE STEP=0
         MVC   SMF5UIF,JMRUSEID  COPY ACCOUNT NUMBER             81334
         MVC   SMF5NCI,JMRJOBIN   SYSIN COUNT                    81334
         MVC   SMF5JCC,=X'FFFF'   COMP. CODE - JCL ERROR         81334
         MVC   SMF5SMCI,JMRSYSOC   SYSOUT CLASSES, SHOULD BE 0   81334
         MVC   SMF5RDCL(2),JMRRDR   READER TYPE                  81334
         MVI   SMF5TLEN,S05END-SMF5PRGN  LENGTH OF REMAINDER     81334
         MVI   SMF5ACTF,2    FAKE TWO ACCOUNT FIELDS             81334
         MVI   S05ACT1L,L'S05ACT1  SET LENGTH OF ACCT #
         MVI   S05ACT2L,L'S05ACT2   LENGTH OF SUB-ACCT #
         MVC   S05ACT1,JMRUSEID  MOVE ACCT #
         MVC   S05ACT2,JMRUSEID+L'S05ACT1  SUB-ACCT #
         LA    R1,SMFRCD5    GET SMF RECORD                      81334
*DEFER SMFWTM  (1)           WRITE TYPE 5 SMF RECORDS FOR FAILED JOB
REALYDON MVC   CWATXTEX,OLDTXTEX                                 85112
         AIF   (&MVSSP).NOGM2                                    92299
         ICM   R0,15,VIEFVGM  WAS IEFVGM LOADED ?                85112
         BNP   REALYDEL      NO; OR FOUND IN LPA - SKIP DELETE   85112
         DELETE EPLOC=CIEFVGM
.NOGM2   ANOP  ,                                                 92299
REALYDEL L     R13,SAVER+4              RESTORE 13 WHILE YOU CAN
         L     R2,RETCODE    LAST CHANCE TO FAIL - SET RETURN CODE
         SPACE 2                                                 81334
*                            HERE THE HANGMAN STOPS HIS CART:
*                            NOW THE BEST OF FRIENDS MUST PART.
         SPACE
*              THE CARPENTER'S SON
*              A SHROPSHIRE LAD - XLVII
*              A. E. HOUSMAN
         SPACE
         ST    R2,16(,R13)              WE, THE GENTLEMEN OF THE JURY,
         XC    JMRUCOM+1(3),JMRUCOM+1 CLEAR WORK AREA POINTER
         LH    R0,=Y(INITLEN)                                    90197
         LR    R1,R4
         FREEMAIN R,LV=(0),A=(1)
         LM    R14,R12,12(R13)
         BR    R14
         EJECT
INITSECT DSECT
OLDTXTEX DS    A             SAVE CWATXTEX (INTERNAL TEXT X)     85112
XCBEGIN  EQU   *             START OF XC CLEAR AREA
PTRSAVE  DS    F
SAVER    DS    18F
LINKWORK DS    D
         ORG   LINKWORK
ECB      DS    F
REPLY    DS    CL9
FWIT     WTOR  'MSG903D XXXXXXXX NEEDS ACCOUNTING INFO',,9,            *
               ROUTCDE=1,DESC=4,MF=L
         ORG   FWIT+8
LENGTH   DS    H
FLAGS    DS    XL2
MSGWORK  DS    CL80
         DS    XL4           SPACE FOR ACTUAL DESC/ROUT FOR FULL LENGTH
DESC     DS    XL2
ROUT     DS    XL2
         ORG   MSGWORK
         DS    C'MSGXXXD '
MSGJOB   DS    CL8
         DS    CL4
MSGVAR   DS    CL8
         ORG
JOBFLAG  DS    F
FLAGMASK DC    F'0'          BITS FOR WARNING MSGS ONLY         79056
CARDBXLE DS    2F    1-2/4   SAVE R14/R15 FOR BX CARD SCAN       81334
CARDC01  DS    F       3/4   ADDRESS OF CARD COLUMN 1
CARDCURR DS    F       4/4   CURRENT SCAN POSITION               81334
ACCTSTRT DS    F                       ACCOUNTING FIELD START ADDR.
VIEFVGM  DS    F                       ADDRESS OF IEFVGM
@LEXVOLT DS    A             ADDRESS OF VOLUME TABLE LOOKUPS
@TABACCT DS    A             ADDRESS OF USERCVT ROOTED ACCOUNTS  81334
RETCODE  DS    F .           RETURN CODE
RETCH    EQU   *-1           CODE = 0 OR 4
BUM      EQU   4             KILL, KILL
ADJQE    DS    F             JQE ADDRESS FOR HASP JOB
XQTIMFG  DS    X             TIME= ON PROC INVOKATION IF ON      82283
XQTIMON  EQU   X'01'           RESET BY TEXT EXIT                82283
XQTIMCK  EQU   X'02'           SET FOR TEXT EXIT TIME= CHECK     82283
XQTIMVAL DS    XL3           VALUE OF TIME=                      82283
MGFLAG   DS    X             SPECIAL MESSAGE PROCESSING FLAGS
MGSKP    EQU   X'80'         SKIP MESSAGES THIS TIME ROUND (JOB CARD)
SKP#UP   EQU   X'20'         DON'T UP STEP NUMBER NEXT TIME
DOEXEC   EQU   X'02'         EXEC CARD TO BE LOOKED AT AFTER INTERPRET
JBCLASS  DS    X             JOB CLASS, EITHER OS OR HASP
XTIME    DS    X             TIME VALUE INDEX FIELD
XREGN    DS    X             REGION VALUE INDEX FIELD            81334
FINDKEY  DS    X             TEXT EXIT - KEY/SUBKEY TO LOCATE
JOBPRT   DS    C                        ALLCLASS PARAMETER
JOBSW1   DS    X                        JOB SWITCHES
WHORU    EQU   128                      WHO ARE YOU?
HASPJOB  EQU   64            FLAG ID RUNNING HASP JOB
JOBCAN   EQU   32            CANNED JOB JCL - ANYTHING GOES
JOBCHKD  EQU   16                       JOB CARD OPTIONS CHECKED
RJEJOB   EQU   8             SET IF RUNNING UNDER RJE
JOBSOME  EQU   4             SOME PRIVILEGES - CUST.SUPPORT, ETC.
JOBSKIP  EQU   2             DON'T DO ANYTHING WITH(TO) JOB      85154
JOBSYS   EQU   1             SYSTEM'S JOB - ALMOST ALL STUFF GOES
JOB      EQU   JOBSYS+JOBCAN ALMOST ANYTHING GOES
FDDMIN   DS    X             # OF TRAILING - SIGNS REPLACED      90218
FDD      DS    X             DSN/TEXT PROCESSING FLAGS           81334
FDDFOUND EQU   X'80'           DSN= FOUND                        81334
FDDQUOTE EQU   X'40'           DSN=' FOUND                       81334
FDDFAIL  EQU   X'02'           WYL.ACCT.UID NOT VALID            81334
FDDPFX   EQU   X'01'           PREFIX PROCESSED                  81334
KKNUM    DS    X             KLNUM SAVED IN CASE OF L/KCONT      81334
TEXTWORK DS    0F            TEXT EXIT WORK AREA                 81334
TXTSER   DS    F             ADDRESS OF SER= PARAMETER OR 0      81334
TXTREF   DS    F             ADDRESS OF REF= PARAMETER OR 0      81334
TXTUNI   DS    F             ADDRESS OF UNIT= PARAMETER OR 0     81334
TXTDSN   DS    F             ADDRESS OF DSN= PARAMETER OR 0      81334
TXTCLEAR EQU   *-TEXTWORK    LENGTH TO CLEAR                     81334
         SPACE 2
         ORG   MSGWORK       RE-USE WORK AREA
USERVLST USERVOLT ,          EXPAND CALL LIST AND MAPPING        81334
INITSECT DSECT ,                                                 81334
         ORG   ,             BUMP AHEAD
         SPACE 2
         PUSH  PRINT                                             85112
         PRINT ON,GEN        SHOW SMF EXPANSION                  85112
         IFASMFR 5           DEFINE SMF TYPE 5 RECORD            81334
S05ACT1L DS    X             LENGTH OF FIRST FIELD = 4
S05ACT1  DS    CL4           ACCOUNT FIELD
S05ACT2L DS    X             LENGTH OF SUB-ACCOUNT
S05ACT2  DS    CL4           SUB-ACCOUNT #
         SPACE
S05END   EQU   *
         POP   PRINT         RESTORE PRINT OPTIONS               85112
         SPACE 2                                                 81334
LPREFIX  DC    AL1(0)        LENGTH OF PREFIX DATA               92299
LDSNFIX  DC    AL1(0)        LENGTH OF COMPLETED DSN             92299
PREFIX   DC    C'USERID@@.ACCTSUBA'  WYLBUR PREFIX  1/2          92299
INITCLR  EQU   *-XCBEGIN     LENGTH TO BE CLEARED                81334
PRETEXT  DS    XL(4004-L'PREFIX)  REST OF TEXT BUFFER  2/2       90197
INITLEN  EQU   *-INITSECT    LENGTH OF GOTTEN STORAGE            81334
         SPACE 1
IEFUJV   CSECT ,
INITACCT DC    0A(0),AL1(NELEXADD,NELTXTEX,0,0),A(IEFACT)        81334
         EJECT ,
IEFUJV   CSECT ,
*              ENTRY IS WITH R9 POINTING TO A FIELD NAME
*              RETURN IS TO 0(R14) IF PERMISSION REFUSED
*              RETURN IS TO 4(R14) IF PERMISSION GRANTED
PERMIT   STM   R14,R0,12(R13)
         MVI   MSGWORK,C' '
         MVC   MSGWORK+1(L'MSGWORK-1),MSGWORK
         MVC   MSGWORK(L'SIMONSEZ),SIMONSEZ
         MVC   MSGVAR,0(R9)
         BAL   R14,WTOR
         L     R15,12(,R13)
         CLI   REPLY,C'Y'
         BE    4(,R15)
         CLI   REPLY,C'N'
         BNE   ATANDT
         MVC   MSGVAR+21,REPLY
         LR    R14,R15
         BR    R14           RETURN
         SPACE 2
*        EBCDIC VERB LOOKUP ROUTINE
*        R1,R14,R15 AS FOR BXLE
*        R2 - VERB TABLE ADDRESS
*        R6    RETURN REGISTER
*
VERBLOOK ST    R2,LINKWORK+4   SAVE TABLE ADDRESS
         ST    R5,LINKWORK   FREE REGISTER FOR IC
VERBLOOP L     R5,LINKWORK   RESTORE IN CASE OF EXIT
         CLI   0(R1),C' '    END OF INPUT ?
         BER   R6            YES
         L     R2,LINKWORK+4  RELOAD TABLE ADDRESS
         SLR   R5,R5         CLEAR FOR IC
VERBENT  ICM   R5,1,0(R2)    GET NEXT LENGTH BYTE                81334
         BNM   VERBIC        MORE TO DO                          81334
         BXLE  R1,R14,VERBLOOP  GET NEXT INPUT BYTE, IF ANY
         L     R5,LINKWORK   ELSE RESTORE
         BR    R6            AND RETURN
VERBIC   EX    R5,VERBCHEK   COMPARE                             81334
         BE    VERBFUND      FOUND
         LA    R2,5(R5,R2)   POINT TO NEXT ENTRY
         B     VERBENT       AND TRY IT
VERBFUND MVC   LINKWORK+5(3),1(R2)  MOVE ROUTINE ADDRESS
         LA    R1,1(R5,R1)   POINT TO FIRST BYTE PAST VERB
         LM    R5,R6,LINKWORK  RESTORE REG AND SET NEW 'RETURN'
         BR    R6            RETURN TO CALLER
         EJECT
WTOR     MVC   LENGTH(4),=X'00548000'   LENGTH=80, ROUTING EXISTS
         MVC   DESC(4),=X'10008000'     ROUTCDE=1,DESC=4
         MVC   MSGJOB,JMRJOB
ATANDT   XC    REPLY,REPLY
         XC    ECB,ECB
         LA    R0,ECB         DON'T LET WTOR EXPANSION KILL 14
         ST    R0,FWIT+4
         LA    R1,LENGTH-1   GET START OF WTO PORTION - 1
         LH    R15,LENGTH    GET LENGTH OF MESSAGE
         AR    R1,R15        + LENGTH = LAST BYTE OF MESSAGE
ATANDTRY CLI   0(R1),C' '    IS TRAILING BLANK ?
         BNE   ATANDTZ       NO, STORE NEW LENGTH
         BCTR  R1,0          PREVIOUS BYTE
         BCT   R15,ATANDTRY  TRY AGAIN
ATANDTZ  STH   R15,LENGTH    STASH NEW MESSAGE LENGTH
         MVC   1(4,R1),DESC   MOVE DESC/ROUT CODE AFTER MSG
         WTOR  ,REPLY,9,,MF=(E,FWIT)
         WAIT  ECB=ECB
         OC    REPLY,=CL9' '            UPPER CASE
         MVI   DESC,X'80'               SET DESCRIPTOR TO 1 FOR AT&T
         CLI   REPLY,C'U'               IS IT YOU?
         BNER  R14                       DOESN'T RING A BELL
         B     ATANDT
         SPACE 2
VERBCHEK CLC   4(0,R2),0(R1) COMPARE TABLE ENTRY TO CURRENT
DCF1     DC    F'1'          CONSTANT
CIEFVGM  DC    CL8'IEFVGM'
HWIT DC C'MSG801D JOBFAIL  ACCT=Z909,001X INVALID; SUPPLY CORRECT ACCT'
ACCTFLIP EQU   HWIT+22       ACCOUNT CONSTANT
SUBSYS   EQU   HWIT+27       SYSTEM'S SUBACCOUNTS
SIMONSEZ DC    C'MSG802D TEST-RUN IS LOW PRTY AUTHORIZED ?'
TESTRUN  EQU   SIMONSEZ+8    CONSTANT FOR TEST RUN
LOWPRTY  EQU   SIMONSEZ+20   CONSTANT FOR PRTY 0, 1
MASKTAB  DC    AL1(128,64,32,16,8,4,2,1)
         SPACE 2
JOBVERBS VERBTAB 'ALLCLASS=',EUREKA,BASE=0,LEN=3  SPECIAL MSGCLASS FORM
         VERBTAB 'PRTCLASS=',EUREKA1  REPLACE SYSOUT=A ONLY      81334
         VERBTAB *END                                            81334
         SPACE 2
DDTVERBS VERBTAB 'SYSOUT=',DDTSOUT,BASE=0,LEN=3                  81334
DDTDSNT  VERBTAB 'DSNAME=',DDTDSN  LOOK FOR DSN=                 81334
         VERBTAB 'DSN=',DDTDSN         DITTO                     81334
DDTVERB  VERBTAB '#####',DDTRESVL  RESIDENT VOLSER               81334
         VERBTAB '####$',DDTRESVL  ALTERNATE FORM                81334
DDTVID   VERBTAB '##ID',DDTSYSID   SYSTEM SMF ID                 92299
DDTVSTEP VERBTAB '####',DDTSTEP                                  81334
         VERBTAB *END                                            81334
         SPACE 1
         LTORG
         TITLE 'I E F U J V  ***  TEXT EXIT PROCESSING'
*              R1 POINTS TO A PARAMETER LIST:
*        0     FIRST TEXT BUFFER
*        4     QUEUE MANAGER PARAMETER AREA
*        8     QUEUE MANAGER ENTRY POINT
         SPACE 1
*              R12 POINTS TO THE INTERPRETER WORK AREA
IEFUJVXT CSECT ,             TEXT EXIT CSECT
IEFACT   STM   R14,R12,12(R13)                                   85112
         LR    BASE,R15      GET BASE
         USING IEFACT,BASE
         SLR   R15,R15       SET PROVISIONAL RETURN CODE         85168
         ICM   R5,15,CWAJMRPT    GET JMR                         85168
         BZ    QUITEXIT      EXIT IMMEDIATELY                    85168
         ICM   INITREG,7,JMRUCOM+1   GET WORK AREA ADDR          85168
         BZ    QUITEXIT                                          85168
         USING INITSECT,INITREG
         LA    R1,SAVER
         ST    R1,8(,R13)
         ST    R13,4(,R1)
         LR    R13,R1
         MVC   PTRSAVE,SAVEPTR
         L     R3,TEXTBUFP   GET POINTER TO TEXT BUFFER          85112
         USING TEXT,R3       MAP BUFFER                          85154
         TM    JOBSW1,JOBSKIP   SKIP THIS ?                      85154
         BNZ   EXITMSGS      YES                                 85154
         SPACE 1                                                 85112
         PRINT GEN                                               90197
         IEFTXTFT ,          EXPAND MAPPING                      85154
         PRINT &PRTSOR                                           90197
         SPACE 1                                                 85112
IEFUJVXT CSECT ,             RESUME                              85112
         SPACE 2
*              FOR A SPEEDY LOOK AT A SAMPLE TEXT EXIT,
*              SEE IKASPD(GJP) OR IHKCEDIT & IHKXEDIT (RJE)
         SPACE 1
*              THE INTERNAL TEXT BUFFER IS KEPT ON THE JOB QUEUE
*              THE FIRST 176 BYTES IS KEPT IN STORAGE, IN THE IWA,
*              WITH LABEL TEXTBUF.
         SPACE 1
*              THE LOCAL WORK AREA CONTAINS A READ-IN AREA WHICH MAY BE
*              USED TO READ TEXT BUFFER OVERFLOW RECORDS.
*              THE LABEL IS AUXBUF
         SPACE 1
*              THE EXIT MUST GUARANTEE THAT THE LAST TEXT OVERFLOW
*              RECORD IS RESTORED TO AUXBUF, PRIOR TO EITHER CALLING
*              ORIGINAL TEXT EXIT, OR RETURNING TO IEFVFA.
         SPACE 1
*              THE QMPA AND EXTERNAL PARAMETER AREA POINTED TO BY QMPCL
*              MAY BOTH BE USED BY THE TEXT EXIT.
         SPACE 1
*              THE TEXT BUFFER FORMAT IS:
*        0-2   TTR OF CONTINUATION
*        3     00 IF FIRST PART, 0B IF CONTINUATION
*        4-175 TEXT
         SPACE 2
*              WITHIN THE TEXT PORTION OF AN INTERNAL TEXT BUFFER,
*              EACH ENTRY STARTS WITH A KEY.
*              THE FIRST ENTRY IN THE TEXT BUFFER IS FOR THE VERB.
*              EACH KEYWORD SUB-PARAMETER HAS ITS OWN ENTRY.
*              THERE ARE THREE SPECIAL SITUATIONS WHICH CAN OCCUR:
*                                       A LENGTH BYTE OF LCONT
*                                       A KEY BYTE OF KCONT
*                                       A KEY BYTE OF ENDK
*              IN THE FIRST TWO CASES, BYTE 4 OF THE OVERFLOW RECORD
*              CONTAINS THE EXPECTED FIELD.
*              IN THE THIRD CASE, ALL DONE.
         SPACE 2
***********************************************************************
*       *                                                             *
* BIT 0 * BITS 1-7                                                    *
*       *                                                             *
***********************************************************************
*                                                                     *
*              KEY                                                    *
*                                                                     *
***********************************************************************
*       *                                                             *
*     0 *      NUMBER OF POSITIONAL PARAMETERS                        *
*       *                                                             *
***********************************************************************
*       *                                                             *
*     0 *      LENGTH OF 1ST POSITIONAL PARAMETER                     *
*       *                                                             *
*       *      THIS FORMAT IS USED FOR A SIMPLE PARAMETER             *
*       *                                                             *
***********************************************************************
*                                                                     *
*              VALUE OF 1ST POSITIONAL PARAMETER                      *
*                                                                     *
***********************************************************************
*       *                                                             *
*       *      ...                                                    *
*       *                                                             *
***********************************************************************
*       *                                                             *
*     0 *      LENGTH OF NTH POSITIONAL PARAMETER                     *
*       *                                                             *
***********************************************************************
*       *                                                             *
*     0 *      VALUE OF NTH POSITIONAL PARAMETER                      *
*       *                                                             *
***********************************************************************
*       *                                                             *
*     1 *      NUMBER OF SUB-PARAMETERS IN THIS PARAMETER             *
*       *                                                             *
*       *      THIS FORMAT IS USED FOR A PARAMETER WITH SUBPARAMETERS *
*       *      IT MAY BE RECOGNIZED BY THE HIGH ORDER BIT (X'80')     *
*       *                                                             *
***********************************************************************
*       *                                                             *
*     0 *      LENGTH OF 1ST SUBPARAMETER                             *
*       *                                                             *
***********************************************************************
*                                                                     *
*              TEXT OF 1ST SUBPARAMETER                               *
*                                                                     *
***********************************************************************
*       *                                                             *
*       *      ...                                                    *
*       *                                                             *
***********************************************************************
*       *                                                             *
*     0 *      LENGTH OF NTH SUBPARAMETER                             *
*       *                                                             *
***********************************************************************
*                                                                     *
*              TEXT OF NTH SUBPARAMETER                               *
*                                                                     *
***********************************************************************
         EJECT
         BUGME TEXTEXIT                                          81334
         TM    STRINDCS,JOBSTR          IS THIS A JOB CARD?      85154
         BZ    TEXTWHAT      NO                                  85154
*        THE JOB CARD WILL ALLWAYS HAVE AT LEAST ONE POSITIONAL PARAM:
*              TO WIT, THE JOB NAME (HALF-WIT)
         USING KEYLIST,R11   DEFINE TEXT BUFFER FORMAT           81334
         SPACE 1                                                 81334
TEXTJOB  SLR   R10,R10       CLEAR FOR IC                        81334
         LA    R11,STRJKEY   POINT TO JOB KEY                    85154
         CLI   KLNUM,3       PROGRAMMER NAME PRESENT ?           81334
         BL    TJNTIME       NO; LEAVE IT AS IS                  90210
         IC    R10,KLLEN     GET LENGTH OF JOBNAME               81334
         LA    R11,1(R10,R11)   SKIP JOBNAME                     81334
         LA    R9,1          SET DEFAULT 'SUBLIST' COUNT         81334
         TM    KLLEN,X'80'   IS IS A SUBLIST ?                   81334
         BZ    TJPSUB        HOW DID WE GET HERE ?               81334
         IC    R9,KLLEN      GET SUBLIST FLAG AND COUNT          81334
         LA    R15,X'7F'                                         81334
         NR    R9,R15        REMOVE FLAG                         81334
         LA    R11,L'KLLEN(,R11)  SKIP SUBLIST COUNT             81334
TJPSUB   IC    R10,KLLEN     GET PARAMETER LENGTH                81334
         LA    R11,1(R10,R11)  SKIP THIS SUBPARAMETER            81334
         BCT   R9,TJPSUB     UNTIL ALL DONE WITH ACCT FIELD      81334
         ICM   R10,1,KLLEN   GET LENGTH OF PROGRAMMER NAME       81334
         BZ    TJNTIME                                           90210
         CH    R10,=H'20'    TOO LONG ?                          81334
         BH    TJNTIME       YES; WILL FAIL ANYWAY               90210
         BCTR  R10,0         ADJUST LENGTH FOR EXECUTE           81334
         MVI   SMF5PRGN,C' '                                     81334
         MVC   SMF5PRGN+1(L'SMF5PRGN-1),SMF5PRGN  BLANK IT       81334
         EX    R10,MVCPRGN   MOVE SUPPLIED NAME                  81334
         SPACE 1                                                 90210
TJNTIME  TM    JOBSW1,JOBSOME+JOB  PRIVILEGED ?                  90211
         BNZ   TJNPGM        ALLOW ANY TIME                      90211
         LOCKEY PRTYJK,TJNPRTY  LOOK FOR PRTY=                   90218
         CLI   KLLEN,1       ANY AT ALL ?                        90218
         BL    TJNPRTY       NO; ACCEPT                          90218
         LA    R9,KLPARM     POINT TO DIGIT                      90218
         CLI   KLLEN,2       VALID ?                             90218
         BH    TJBPRTY       NO                                  90218
         BL    TJTPRTY       CHECK SINGLE DIGIT                  90218
         LA    R9,KLPARM+1   POINT TO DIGIT                      90218
         CLI   KLPARM,C'0'   LEADING ZERO ?                      90218
         BNE   TJBPRTY       NO; INVALID PRTY                    90218
TJTPRTY  CLI   0(R9),C'6'    NOT TOO HIGH ? (0-6 VALID)          90218
         BNH   TJNPRTY       NO; ACCEPT                          90218
TJBPRTY  SETMSG 4            SET INVALID PRIORITY                90218
TJNPRTY  LOCKEY TIMEJK,TJNPGM  LOOK FOR TIME=                    90210
         LA    R9,KLPARM     POINT TO TEXT                       90210
         ICM   R10,1,KLLEN   GET PARM LENGTH                     90210
         BNM   TJNTIMES      NOT A SUBLIST                       90210
         IC    R10,KLLEN+1   GET FIRST OPERAND LENGTH            90210
         LA    R9,1(,R9)     SKIP OVER LENGTH                    90210
TJNTIMES SH    R10,=H'1'     ANYTHING TO CHECK ?                 90210
         BM    TJNPGM        NO                                  90210
         XC    LINKWORK(16),LINKWORK  CLEAR WORK SPACE           90210
         LA    R11,LINKWORK+15                                   90210
         SR    R11,R10       GET LOCATION TO MOVE TO             90210
         EX    R10,MVCTIME   MOVE PARAMETER                      90210
         PACK  LINKWORK(8),LINKWORK+8(8)  MAKE PACKED            90210
         CVB   R0,LINKWORK   MAKE BINARY                         90210
         CH    R0,=H'1440'   LARGER THAN LEGAL ?                 90211
         BH    TJNPGM        YES; LET THE SYSTEM FAIL IT         90211
         MH    R0,=A(60*100) CONVERT TO 1/100 SECONDS            90211
         SLR   R1,R1         CLEAR                               90210
         IC    R1,XTIME      GET TIME VALUE INDEX                90210
         ICM   R9,15,@LEXVOLT   GET VOLUME TABLE                 92299
         BZ    TJNPGM        NONE - ALLOW                        92299
         AL    R1,APTIMLM-USERVOLT(,R9)  GET MAX ALLOWED TIME    90210
         C     R0,0(,R1)     EXCEEDS MAX FOR CLASS ?             90211
         BNH   TJNPGM        NO, USE IT                          90210
         SETMSG 5            SET TIME ERROR FLAG                 90210
         SPACE 1                                                 81334
TJNPGM   LOCKEY RDJK,EXITMSGS  LOOK FOR RD=                      81334
         CLI   KLPARM,C'R'   AUTOMATIC RESTART REQUESTED ?       81334
         BNE   EXITMSGS      NO
         SETMSG 11           SET ERROR FLAG                      81334
         B     EXITMSGS
         SPACE 1                                                 81334
MVCPRGN  MVC   SMF5PRGN(0),KLPARM  MOVE PROGRAMMER NAME FIELD    81334
MVCTIME  MVC   0(0,R11),0(R9)  MOVE TEXT - LENGTH IN R10         90210
         SPACE 5
TEXTWHAT TM    STRINDCS,DDSTR   DD CARD ?                        85154
         BNZ   TEXTDD        CHECK DD CARD PARAMETERS            85154
         TM    STRINDCS,EXECSTR  DOUBLE-CHECK, JUST IN CASE      85154
         BZ    EXITMSGS                                          85154
         SPACE 1
*        EXEC CARD CHECKING
*
TEXTEXEC TM    XQTIMFG,XQTIMCK+XQTIMON  CHECK FOR TIME ?         82283
         BNM   TXQNTIME      NO; SKIP                            82283
         MVI   XQTIMFG,0     RESET TIME= FLAG                    82283
         LOCKEY TIMEEEK,TXQNTIME  SKIP IF TIME= NOT FOUND        82283
         MVI   XQTIMFG,XQTIMCK+XQTIMON   SET TIME= FOUND         82283
TXQNTIME TM    JOBSW1,JOB    ANY PRIVILEGES ?                    82283
         BNZ   TXQSPRTY      YES - PERMIT DPRTY
         LOCKEY SDPPEK,TXQDPRTY  DPRTY ?                         81334
         B     TXQBPRTY      YES; FAIL                           81334
TXQDPRTY LOCKEY SDPEEK,TXQSPRTY  DPRTY ?                         81334
TXQBPRTY SETMSG 4            YES; NOT PERMITTED                  81334
TXQSPRTY LOCKEY RDEEK,TXQDRD   LOOK FOR RD=R                     81334
         CLI   KLPARM,C'R'   AUTO RESTART ?                      81334
         BE    TXQBRD        YES; FAIL IT
TXQDRD   LOCKEY RDPEK,TXQGRD  LOOK FOR RD. =R                    81334
         SLR   R10,R10       CLEAR FOR IC
         IC    R10,KLLEN     LENGTH OF STEPNAME                  81334
         AR    R11,R10       BUMP BY LENGTH OF NAME
         CLI   KLPARM+L'KLLEN,C'R'   AUTO RESTART ?              81334
         BNE   TXQGRD        NO
TXQBRD   SETMSG 11           AUTO-RESTART NOT SUPPORTED UNDER HASP
TXQGRD   B     EXITMSGS                                          81334
         EJECT ,
*        DD CARD CHECKING
*
TEXTDD   CLI   STRDKEY+1,2   MORE THAN ONE POSITIONAL PARM ?     85154
         BNL   EXITMSGS      YES; SKIP DD *, DUMMY, DYNAM, ETC.
TDDSOUTA LOCKEY SYSOUTK,TDDNSOUT,NONE=TDDNSOUT  LOCATE SYSOUT=   81334
         CLI   KLLEN,1       ONE BYTE IN CLASS ?                 81334
         BE    TDDSOUTN      YES; GOOD                           81334
TDDSOUT8 SETMSG 8            INVALID SYSOUT                      81334
         B     EXITMSGS      AND EXIT WITH MESSAGE               81334
TDDSOUTN CLI   KLPARM,C'*'   VALID ?                             90197
         BE    TDDSOUTX      YES; DON'T NEED TO CHANGE           90197
         CLI   KLPARM,C'$'   VALID ?                             90210
         BE    TDDSOUTX      YES; DON'T TOUCH IT                 90210
         CLI   KLPARM,C'A'   VALID ?                             81334
         BL    TDDSOUT8      NO; TOO BAD                         81334
         CLI   JOBPRT,0      PRTCLASS OR ALLCLASS OPTION USED ?  81334
         BE    TDDSOUTX      NO; QUIT                            81334
         CLI   KLPARM,C'A'    VALID FIELD ?                      81334
         BL    TDDSOUTX      NO; QUIT                            81334
         BE    TDDSOUTS      A - REPLACE
         TM    JOBPRT,X'40'  ALLCLASS ?
         BZ    TDDSOUTX      NO; NO CHANGE OTHER THAN A          81334
         CLI   KLPARM,C'B'   POSSIBLY PUNCH CLASS ?              81334
         BE    TDDSOUTX      IGNORE PUNCHED O/P                  81334
         CLI   KLPARM,C'K'   HASP SPECIAL PUNCH ?                81334
         BE    TDDSOUTX      IGNORE THIS ONE TOO                 81334
TDDSOUTS MVC   KLPARM(1),JOBPRT  REPLACE THE PRINT CLASS         81334
         OI    KLPARM,X'40'  MAKE IT UPPER CASE                  81334
TDDSOUTX B     EXITMSGS      NOTHING ELSE FOR NOW                81334
         SPACE 1
TDDNSOUT XC    TEXTWORK(TXTCLEAR),TEXTWORK  CLEAR WORK AREA      81334
         LOCKEY DSNAMEK,TWNDSN,NONE=TWNDSN  SKIP IF NO DSN=      81334
         TM    KLLEN,X'80'   SUBLIST FORMAT ?                    81334
         BZ    TDDNMEM       NO                                  90218
         CLI   FDDMIN,0      MEMBER NAME REPLACED ?              90218
         BE    TDDYMEM       NO; SKIP                            90218
         TM    KLLEN,255-X'81'  MORE THAN ONE ENTRY IN SUBLIST ? 90218
         BZ    TDDYMEM       NO; NO MEMBER PROCESSING            90218
         LA    R0,9                                              90218
         SLR   R15,R15                                           90218
         IC    R15,FDDMIN    GET NUMBER OF TRAILING MINUSES      90218
         SR    R0,R15        = NUMBER TO DO                      90218
         LA    R9,KLPARM+1   POINT TO PARM+L'KLSUBL              90218
         IC    R15,KLLEN+1   GET LENGTH OF DSN                   90218
         AR    R9,R15        SKIP OVER IT                        90218
         IC    R15,0(,R9)    INSERT KLLEN OF MEMBER NAME         90218
         CR    R15,R0        LONG ENOUGH ?                       90218
         BL    TDDYMEM       NO; SKIP THIS                       90218
         AR    R9,R15        POINT TO LAST BYTE                  90218
TDDLMEM  CLI   0(R9),C'#'    TRAILING POUND SIGN ?               90218
         BNE   TDDYMEM       NO; WHAT WENT WRONG ?               90218
         MVI   0(R9),C'-'    PUT IT BACK THE WAY IT WAS          90218
         BCTR  R9,0                                              90218
         BCT   R0,TDDLMEM    DO ALL                              90218
TDDYMEM  LA    R11,L'KLLEN(,R11)  SKIP SUBLIST BYTE              90218
TDDNMEM  CLI   KLLEN,1       AT LEAST ONE BYTE ?                 90218
         BL    TWNDSN        NO; NO DSN                          81334
         ST    R11,TXTDSN    SAVE DSN ADDRESS                    81334
         MVC   TXTDSN(1),KKNUM  SAVE NUMBER OF PARMS             81334
TWNDSN   LOCKEY SERMK,TWNSER,NONE=TWNSER  SKIP IF NO SER=        81334
         CLI   KLLEN,1       ANY LENGTH ?                        81334
         BNL   TWYSER        YES; SAVE IT                        81334
         CLI   KKNUM,1       MORE THAN 1 ?                       81334
         BNH   TWNSER        NO; ASSUME NO VOLSER                81334
TWYSER   ST    R11,TXTSER    SAVE VOLSER KEY ADDRESS             81334
         MVC   TXTSER(1),KKNUM  SAVE NUMBER OF PARMS             81334
TWNSER   LOCKEY REFMK,TWNREF,NONE=TWNREF  SKIP IF NO VOL=REF     81334
         TM    KLLEN,X'80'   SUBLIST FORMAT ?                    81334
         BZ    *+8           NO                                  81334
         LA    R11,L'KLLEN(,R11)  SKIP SUBLIST COUNT             81334
         CLI   KLLEN,1       AT LEAST ONE BYTE ?                 81334
         BL    TWNREF        NO; NO REF=                         81334
         ST    R11,TXTREF    SAVE REF= ADDRESS                   81334
         MVC   TXTREF(1),KKNUM  SAVE NUMBER OF PARMS             81334
TWNREF   LOCKEY UNITK,TWNUNIT,NONE=TWNUNIT  SKIP IF NO UNIT=     81334
         CLI   KLLEN,1       AT LEAST ONE BYTE ?                 81334
         BL    TWNUNIT       NO                                  81334
         ST    R11,TXTUNI    SAVE UNIT KEY ADDRESS               81334
         MVC   TXTUNI(1),KKNUM  SAVE NUMBER OF PARMS             81334
TWNUNIT  DS    0H            END OF INITIALIZATION CODE          81334
         SPACE 2                                                 81334
         ICM   R11,15,TXTSER  CHECK FOR VOL=SER KEY              81334
         BZ    TDDSER        NONE                                81334
         SLR   R10,R10
         IC    R10,TXTSER    INSERT THE NUMBER OF SERIALS TO CHECK
         LA    R9,1          SET TO SKIP KEY ENTRY
TDDSERLP LA    R11,KLLEN-1(R9)  POINT TO NEXT LENGTH FIELD       81334
         ICM   R9,1,KLLEN-KLLEN(R11)  INSERT NEXT LENGTH FIELD   81334
         BZ    TDDSERIN      IGNORE NULL                         81334
         CH    R9,=H'6'      LONGER THAN VALID ?
         BH    TDDSERIN      YES
         MVC   INVOL(6),=CL9' '
         LR    R1,R9         COPY LENGTH
         BCTR  R1,0          LESS ONE FOR EXECUTE
         EX    R1,MVCSER     MOVE VOL-SER TO TEST FIELD
         XC    INFLTEST,INFLTEST  CLEAR RETURN FLAGS
         ICM   R15,15,@LEXVOLT  GET ROUTINE ADDRESS              92299
         BZ    TDDSERIN      NONE; ALLOW IT                      92299
         SLR   R0,R0         SET REQUEST CODE - ANY MATCH
         LA    R1,INVOL      GET LIST ADDRESS
         BALR  R14,R15       CALL IT
         CLI   JBCLASS,C'E'  IS THIS A CLASS E JOB ?             90197
         BNE   TDDSER1A      NO
         TM    JOBSW1,JOB+JOBSOME  ANY PRIVILEGES ?
         BNZ   TDDSERIN
         TM    INFLTEST,VF1JOBE  CLASS E PACK ?
         BNZ   TDDSERIN      OK; ALLOW IT
         SETMSG 1            NON-SETUP JOB WITH VOLUME           81334
         B     TDDSER        THEN GET OUT
MVCSER   MVC   INVOL(0),KLPARM-KLLEN(R11)  COPY VOLSER           81334
TDDSER1A TM    INFLTEST,VF1JOBF+VF1JOBI RESTRICTED PACK ?
         BZ    TDDSERIN      NO
         TM    INFLTEST,VF1JOBF  CLASS F ?
         BNZ   TDDSER1B      YES
         CLI   JBCLASS,C'I'  CLASS I ?                           90197
         BE    TDDSERIN      OK
         SETMSG 15           CLASS INVALID FOR PACK              81334
         B     TDDSER
TDDSER1B CLI   JBCLASS,C'F'  CLASS F ?                           90197
         BE    TDDSERIN
         SETMSG 14           CLASS INVALID FOR PACK              81334
         B     TDDSER
TDDSERIN BCT   R10,TDDSERLP  DO NEXT VOL-SER, IF ANY
         SPACE 1                                                 81334
*        SCAN FOR DSN=$.XXX AND DSN=@.XXX - IF FOUND,            90210
*        THE TEXT IS PREFIXED BY THE FULL WYLBUR NAME, TAKEN     81334
*        FROM THE CURRENT ACCOUNT NUMBER AND PROGRAMMER NAME.    81334
*                                                                81334
TDDSER   ICM   R11,15,TXTDSN   DSN= FOUND ?                      81334
         BZ    TDDWYL        NO                                  81334
         TM    FDD,FDDQUOTE+FDDFAIL  FAILED OR NOT TO BE DONE ?  81334
         BNZ   TDDWYL        NO; SKIP IT                         81334
         CLI   KLPARM+1,C'.'   LEADING PERIOD ?                  90210
         BNE   TDDWYL        NO; NO CONVERSION                   81334
         CLI   KLPARM,C'@'   GROUP REQUEST ?                     90210
         BE    TDDWGRP       YES                                 90210
         CLI   KLPARM,C'$'   USER REQUEST ?                      90210
         BNE   TDDWYL        NO; NO PREFIXING                    90210
*FAILS*  TM    FDD,FDDPFX    HAS THE PREFIX CODE BEEN DONE ?     81334
*ON @ *  BNZ   TDDWYLB       YES; JUST BUILD NEW NAME            81334
TDDWGRP  MVC   PREFIX(8),SMF5PRGN  MOVE USER ID                  92299
         LA    R14,PREFIX    POINT TO PREFIX                     92299
         LA    R15,8         MAX LENGTH=8                        92299
         LR    R0,R15        COPY LENGTH                         92299
TTDWPLN  CLI   0(R14),C' '   TRAILING BLANK ?                    92299
         BE    TTDWPLT       YES                                 92299
         LA    R14,1(,R14)                                       92299
         BCT   R0,TTDWPLN                                        92299
TTDWPLT  SR    R15,R0        PREFIX LENGTH                       92299
         MVI   0(R14),C'.'   PERIOD                              92299
         L     R5,CWAJMRPT   MAKE SURE WE HAVE JMR               85112
         MVC   1(8,R14),JMRUSEID  ADD ACCOUNT NUMBER             92299
         LA    R15,9(,R15)   TOTAL PREFIX LENGTH                 92299
         STC   R15,LPREFIX   SAVE PREFIX LENGTH                  92299
         CLI   KLPARM,C'@'   GROUP REQUEST ?                     90210
         BNE   *+10          NO                                  90210
         MVC   5(8,R14),=4C'0'  MAKE GROUP SUBACCOUNT            92299
         OI    FDD,FDDPFX    SHOW PREFIX SET                     81334
*        LATER USE PROTECT SVC TO GET INDEX AND CHECK VALIDITY   81334
TDDWYLB  SLR   R9,R9                                             81334
         IC    R9,KLLEN      GET LENGTH                          81334
         BCTR  R9,0          DELETE THE $ OR @ PREFIX            90210
         SLR   R14,R14                                           92299
         IC    R14,LPREFIX   LOAD PREFIX LENGTH                  92299
         AR    R9,R14        GET NEW LENGTH                      92299
         CH    R9,=H'44'     VALID NEW LENGTH ?                  81334
         BH    TDDWYL        NO; JUST LEAVE IT AS IS             81334
         STC   R9,LDSNFIX    STASH FOR QUICKY MOVE               92299
         LA    R10,KLPARM+1  POINT TO INDEX POINT IN TEXT        90210
         LR    R0,R14        LENGTH OF 'USER.ACCT' PFX ADJUST    92299
         BCTR  R0,0          LESS ADJUST FOR $ OR @              92299
         LH    R11,STRLTH    GET OLD STRING LENGTH               85154
         AR    R0,R11        GET CURRENT TEXT LENGTH             85154
         STH   R0,STRLTH     SET NEW LENGTH                      85154
         AR    R11,R3        ADD TEXT BASE TO LENGTH             85154
         SR    R11,R10       LESS TEXT POSITION - LENGTH TO MOVE 85154
         LA    R14,PREFIX(R14)  PLACE OLD CODE AFTER PREFIX      92299
         LA    R15,L'PRETEXT  AND LENGTH                         81334
         LR    R0,R10        COPY SOURCE ADDRESS                 81334
         LR    R1,R11        COPY SOURCE LENGTH                  81334
         MVCL  R14,R0        MOVE REST OF BUFFER                 81334
         LA    R14,LDSNFIX   GET NEW SOURCE                      81334
         SLR   R15,R15       JUST IN CASE                        92299
         IC    R15,LPREFIX   GET PREFIX LENGTH                   92299
         BCTR  R10,0         BUMP BACK TO KLLEN BYTE             81334
         BCTR  R10,0         BUMP BACK TO KLLEN BYTE             81334
         LA    R11,2(R15,R11)   AND ADJUST THE LENGTH            92299
         LA    R15,PRETEXT(R15)  AND SOURCE LENGTH               92299
         MVCL  R10,R14       MOVE BACK TO TEXT/AUX BUFFER        81334
*        CODE ASSUMES NO OVERFLOW; THIS IS TRUE WITH 'NORMAL'    81334
*        DD CARDS, INCLUDING ALL DCB/SPACE/LABEL/UNIT/VOL/DISP   81334
*        PARAMETERS.                                             81334
         MVC   LINKWORK+16(20),=CL20'&&&&  DSN REPLACED BY'      85161
         MVC   LINKWORK+36(44),LINKWORK+35  BLANK REST           85161
         SLR   R10,R10                                           81334
         IC    R10,LDSNFIX   GET LENGTH OF DSN                   92299
         BCTR  R10,0         ADJUST FOR EXECUTE                  81334
         EX    R10,MVCWYL    MOVE IT                             81334
         LA    R10,20+1(,R10)  NEW LENGTH                        81334
         LA    R9,LINKWORK+16  MESSAGE ADDRESS                   85161
         L     R15,=A(IEFVGMSG)  MESSAGE ROUTINE                 85161
         BAL   R14,IEFVGM80-IEFVGMSG(,R15)  WRITE CANNED MESSAGE 85161
         B     TDDWYL        PROCESS SOME MORE                   81334
MVCWYL   MVC   LINKWORK+36(0),PREFIX  MOVE DSN                   92299
         SPACE 1
TDDWYL   TM    JOBSW1,JOB    PRIVILEGED JOB ?                    81334
         BNZ   TDDDSN        YES; SKIP DSN/DISP CHECKING
         ICM   R11,15,TXTDSN   LOOK FOR DSN=                     81334
         BZ    TDDDSN        NOT PRESENT                         81334
         CLI   KLLEN,6       AT LEAST SIX BYTES ?                81334
         BL    TDDDSN        NO
         CLC   =C'SYS1.',KLPARM  RESTRICTED NAME ?               81334
         BNE   TDDDSN        NO
NOTUSDSN LOCKEY DISPK,TDDDSP,NONE=TDDDSP  SCAN FOR DISP=; FAIL IF NONE
         CLI   KKNUM,2       HOW MANY SUBPARMS ?                 81334
         BH    TDDDSP        MORE THAN TWO - FAIL
         BL    TDDDS1        ONE OR LESS - CHECK FOR SHR
         CLI   KLPARM+L'KLLEN+3,C'P'   CHECK SECOND FIELD FOR PASS
         BNE   TDDDSP        TOO BAD
TDDDS1   CLI   KLLEN,3       SHR AND OLD ARE THREE BYTES         81334
         BNE   TDDDSP        TOO BAD
         CLI   KLPARM,C'S'   SHARE ?                             81334
         BE    TDDDSN        OK; ALLOW IT
         TM    JOBSW1,JOBSOME   SEMI-PRIVILEGED USER ?
         BZ    TDDDSP        NO; FAIL IT
         CLI   KLPARM,C'O'   OLD ?                               81334
         BE    TDDDSN        YES; BUT WHY DO I HAVE TO TRUST THEM ?
TDDDSP   SETMSG 2            DSN=SYS1. WITH DISP NOT SHR         81334
         SPACE 1                                                 81334
TDDDSN   ICM   R11,15,TXTUNI   CHECK FOR UNIT=                   81334
         BZ    EXITMSGS      NOTHING ELSE IF NOT PRESENT         81334
         CLI   KLLEN,8       TOO LONG FOR GENERIC ?              81334
         BH    EXITMSGS      YES; SKIP                           81334
         LR    R10,R11       SAVE FOR LATER                      81334
         SLR   R1,R1         CLEAR FOR ICM                       81334
         ICM   R9,15,@LEXVOLT   GET VOLUME TABLE                 92299
         BZ    TSPDONE       NONE; GET OUT                       92299
         L     R9,VTSPUNIT-USERVOLT(,R9)  GET SPECIFIC LIST      81334
*                                                                81334
*        SCAN FOR UNITS WHICH MUST BE SPECIFIED WITH A VOLSER    81334
*                                                                81334
TSPLOOP  ICM   R1,1,0(R9)    GET LENGTH OF UNIT                  81334
         BNP   TSPDONE       END OF LIST                         81334
         EX    R1,CLCLNUNI   COMPARE LENGTH AND VOLSER           81334
         BNE   TSPBUMP       NO MATCH; TRY NEXT                  81334
         ICM   R9,15,TXTSER  VOL=SER PRESENT ?                   81334
         BNZ   EXITMSGS      YES; VALID REQUEST ?                81334
         ICM   R9,15,TXTREF  OR VOL=REF= PRESENT ?               81334
         BNZ   EXITMSGS      YES; VALID                          81334
BADVLSER SETMSG 17           CONFLICTING UNIT AND VOLSER         81334
         B     EXITMSGS      QUIT WITH ERROR                     81334
CLCLNUNI CLC   0(0,R9),KLLEN   COMPARE LENGTH AND UNIT           81334
         SPACE 1                                                 81334
TSPBUMP  LA    R9,9(,R9)     SKIP LENGTH AND MAX UNIT            81334
         B     TSPLOOP       CHECK AGAIN                         81334
         SPACE 1                                                 81334
*        FOR CLASS E JOBS, SCAN FOR UNIT=2400, NO VOLSER         81334
*          AND SIMILAR REFERENCES NOT DETECTED EARLIER           81334
*                                                                81334
TSPDONE  CLI   JBCLASS,C'E'  CLASS E JOB ?                       90197
         BNE   TNSDONE       NO; NO NON-SETUP CHECK              81334
         TM    JOBSW1,JOBSOME+JOB  PRIVILEGED ?                  81334
         BNZ   TNSDONE       YES                                 81334
         LR    R11,R10       RESTORE UNIT KEY ADDRESS            81334
         SLR   R1,R1         CLEAR FOR ICM                       81334
         ICM   R9,15,@LEXVOLT   GET USERVOLT BACK                92299
         BZ    TNSDONE                                           92299
         L     R9,VTNSUNIT-USERVOLT(,R9)  GET TABLE OF GENERICS  81334
TNSLOOP  ICM   R1,1,0(R9)    GET LENGTH OF NAME                  81334
         BNP   TNSDONE       NONE; PASS IT                       81334
         EX    R1,CLCLNUNI   COMPARE LENGTH AND UNIT             81334
         BNE   TNSBUMP       TRY NEXT ENTRY                      81334
         SETMSG 1            SETUP FOR NON-SETUP CLASS           81334
         B     EXITMSGS                                          81334
TNSBUMP  LA    R9,9(,R9)     SKIP LENGTH AND UNIT NAME           81334
         B     TNSLOOP       CHECK IT                            81334
         SPACE 2                                                 81334
*        CHECK FOR UNITS WHICH MUST NOT HAVE A VOL=REF OR VOL=SER
*                                                                81334
TNSDONE  LR    R11,R10       RESTORE UNIT KEY ADDRESS            81334
         SLR   R1,R1         CLEAR FOR ICM                       81334
         ICM   R9,15,@LEXVOLT   GET VOLUME TABLE                 92299
         BZ    EXITMSGS                                          92299
         L     R9,VTNVUNIT-USERVOLT(,R9)  GET ADDRESS            81334
TNVLOOP  ICM   R1,1,0(R9)    GET NEXT UNIT NAME                  81334
         BNP   EXITMSGS      NO MATCH; ALLOWED                   81334
         EX    R1,CLCLNUNI   COMPARE                             81334
         BNE   TNVBUMP       CHECK NEXT                          81334
         ICM   R11,15,TXTREF   CHECK FOR VOL=REF=                81334
         BNZ   TNVCOMP       PRESENT; COMPARE NAMES              81334
TNVREF   ICM   R11,15,TXTSER   CHECK FOR VOL=SER=                81334
         BZ    EXITMSGS      NONE; ACCEPT                        81334
TNVCOMP  CLC   KLPARM(3),KLPARM-KEYLIST(R10)  IF UNIT(3)=VOLSER  81334
         BE    EXITMSGS      OR UNIT(3)=REFDSN, ALLOW WITH DOUBTS
*        NOTE THAT THIS PERMITS UNIT=SYSDA,VOL=REF=SYS1  AND     81334
*        UNIT=WYLBUR,VOL=SER=WYL00X, AND VOL=REF=WYL.            81334
*        BUT FAILS FOR OTHERS.                                   81334
         ICM   R11,15,TXTREF  VOL=REF= ?                         90219
         BNZ   TURFWARN      YES; JUST ISSUE WARNING             90219
         SETMSG 18           ELSE SET CONFLICT                   81334
         B     EXITMSGS      QUIT WITH MESSAGE                   81334
TURFWARN SETMSG 19           UNIT AND VOL=REF= ARE A NO-NO       90219
         B     EXITMSGS                                          90219
TNVBUMP  LA    R9,9(,R9)     TRY AGAIN                           81334
         B     TNVLOOP                                           81334
         SPACE 2
EXITMSGS NI    FDD,255-FDDFOUND-FDDQUOTE  RESET DSN=             81334
         MVI   FDDMIN,0      RESET MEMBER COUNT                  90218
         BAL   R11,YAXETY    WRITE ANY MSGS                      81334
         OI    RETCH,BUM     SET FAIL IF ANY
EXITEXIT ICM   R1,15,OLDTXTEX  WAS CWATXTEX ORIGINALLY 0?        85112
         BZ    EXTURNS
         TM    0(R1),X'C0'              IS PHAQUE REDUNDANT?
         BNM   EXTURNS
         TM    0(R1),NELEXNAM           IS TEXT EXIT ADDCON OR NAME?
         BZ    TXTADD                    ADDCON
         MVI   LINKWORK,C' '                                     85112
         MVC   LINKWORK+1(7),LINKWORK                            85112
         MVC   LINKWORK(6),2(R1)                                 85112
         L     R1,4(,R13)    GET ORIGINAL SAVE AREA              85112
         L     R1,24(,R1)    GET ORIGINAL R1                     85112
         LINK  EPLOC=LINKWORK,MF=(E,(1)),SF=(E,LINKWORK+8)       85112
         B     EXTURNS
TXTADD   L     R13,SAVER+4
         MVC   SAVEPTR,PTRSAVE
         MVC   16(4,R13),4(R1)          SET R15 TO ENTRY POINT
         LM    R14,R12,12(R13)
         BR    R15
         EJECT
*        SUBROUTINE FOR TEXT BUFFER LOOKUP
*        THIS ROUTINE FAILS IF MORE THAN TWO TEXT BUFFERS ARE REQUIRED
*
KEYLOOK  LA    R11,STRDKEY   POINT TO DD CARD KEY                85154
         TM    STRINDCS,DDSTR  DD CARD ?                         85154
         BNZ   KEYL5         YES                                 85154
         LA    R11,STREKEY   ELSE SET FOR EXEC CARD              85154
         TM    STRINDCS,EXECSTR  IS IT EXEC ?                    85154
         BNZ   KEYL5         YES                                 85154
         LA    R11,STRJKEY   ELSE SET FOR JOB                    85154
KEYL5    LA    R11,L'KLKEY(,R11)  SKIP THE MAJOR KEY (EXEC/DD..) 81334
         SLR   R14,R14
         IC    R14,KLNUM-KLNUM(,R11)   NUMBER OF SUBPARAMETERS   81334
KEYL1    LA    R11,L'KLNUM(,R11)   SKIP NUMBER                   81334
         LTR   R14,R14       DONE WITH THIS KEY ENTRY ?
         BZ    KEYL2         YES - GO TO CHECK NEXT KEY
         TM    KLNUM-KLNUM(R11),X'80'  SUBSIDIARY ENTRIES ?      81334
         BNZ   KEYLSUB       YES; PROCESS SUBLIST                85112
KEYL1A   SLR   R15,R15
         IC    R15,KLLEN-KLLEN(,R11)   LENGTH OF THIS ENTRY      81334
         AR    R11,R15       SKIP OVER
KEYL4    BCT   R14,KEYL1     CHECK NEXT SUBPARAMETER
         LA    R11,L'KLLEN(,R11)   SKIP OVER                     81334
KEYL2    CLI   KLKEY,ENDK    END ?                               81334
         BER   R9            YES - RETURN NOT FOUND
KEYL3    CLC   KLKEY,FINDKEY  IS THIS THE DESIRED KEY ?          81334
         BNE   KEYL5         NO
         MVC   KKNUM,KLNUM   SAVE THE NUMBER                     81334
         B     4(,R9)        RETURN WITH KEY FOUND
         SPACE 1
KEYLSUB  SLR   R1,R1
         IC    R1,KLNUM-KLNUM(,R11)    NUMBER OF SUBLIST ENTRIES 81334
         LA    R15,X'7F'     FLAG MASK
         NR    R1,R15        MASK OUT FLAG BIT
KEYLSUB1 LA    R11,KLLEN-KLNUM(,R11)   POINT TO LENGTH           81334
KEYLSUB2 SLR   R15,R15
         ICM   R15,1,KLLEN-KLLEN(R11)   LENGTH                   82017
         BM    KEYLSUB       SUBLIST OF SUBLIST ?  WILL FAIL     82017
         AR    R11,R15
         BCT   R1,KEYLSUB1   TRY NEXT ONE
         B     KEYL4
         DROP  R11,R3                                            85154
         EJECT ,
YAXETY   STM   R9,R1,LINKWORK+16  SAVE VITAL REGISTERS           85160
         LA    R2,4          PROVISIONALLY SET GOOD EXIT
         CLC   JOBFLAG,DXF1    ANY ERROR FLAGS SET ?
         BE    YAXEXIT       IF NO ERRORS THEN RETURN
         TM    MGFLAG,MGSKP  IS SKIP FLAG ON ?
         BNZ   YAXEXIT2      NO MESSAGES; DEFER PROCESSING ?
         SLR   R2,R2         SET FOR BAD EXIT
         L     R11,=A(MSGTAB)
         LA    R1,(MSGTEND-MSGTAB)/4
         L     R7,JOBFLAG
         SLL   R7,30-(MSGTEND-MSGTAB)/4
YAXLOOP  BXH   R7,R7,YAXKINC
         L     R15,=A(IEFVGMSG)                                  85160
         BALR  R14,R15
YAXKINC  LA    R11,4(,R11)
         BCT   R1,YAXLOOP
         L     R15,JOBFLAG   SAVE OLD FLAG VALUE
         N     R15,FLAGMASK  DELETE WARNING MSG BITS             79056
         BXH   R15,R15,YAXEXIT  FAIL IF ANY OTHERS ON            79056
         LA    R2,4          ELSE SET GOOD EXIT
YAXEXIT  MVC   JOBFLAG,DXF1   RESET MESSAGE FLAGS
YAXEXIT2 NI    MGFLAG,255-MGSKP  TURN OFF DELAY FLAG
         LM    R9,R1,LINKWORK+16   RESTORE REGISTERS             85160
         B     0(R2,R11)     TAKE BAD (0)  OR GOOD (4) EXIT
         SPACE 1
EXTURNS  L     R15,RETCODE   SET CURRENT RETURN CODE
         NI    MGFLAG,255-MGSKP   RESET FLAG
         L     R13,SAVER+4   RESTORE
         MVC   SAVEPTR,PTRSAVE
QUITEXIT RETURN (14,12),RC=(15)                                  85168
         SPACE 2
DXF1     DC    F'1'
FAILJOB  DC    CL8'JOBFAIL '  READER/INTERPRETER NAME FOR FAILED JOB
         SPACE 2
         LTORG ,
         SPACE 2
MSGCSECT CSECT ,             MESSAGE POINTERS / TEXT CONTROL SECTION
MSGTAB   EQU   *
         DC    AL1(L'MSG19-1),AL3(MSG19)  UNIT WITH VOL=REF=     90219
         DC    AL1(L'MSG18-1),AL3(MSG18)  INCONSISTENT UNIT/VOLSER
         DC    AL1(L'MSG17-1),AL3(MSG17)  INCONSISTENT UNIT/VOLSER
         DC    AL1(L'MSG16-1),AL3(MSG16)  ACCOUNT DISABLED       81334
         DC    AL1(L'MSG15-1),AL3(MSG15)
         DC    AL1(L'MSG14-1),AL3(MSG14)
         DC    AL1(L'MSG13-1),AL3(MSG13)
         DC    AL1(L'MSG12-1),AL3(MSG12)
         DC    AL1(L'MSG11-1),AL3(MSG11)
         DC    AL1(L'MSG10-1),AL3(MSG10)
         DC    AL1(L'MSG9-1),AL3(MSG9)
         DC    AL1(L'MSG8-1),AL3(MSG8)
         DC    AL1(L'MSG7-1),AL3(MSG7)
         DC    AL1(L'MSG6-1),AL3(MSG6)
         DC    AL1(L'MSG5-1),AL3(MSG5)
         DC    AL1(L'MSG4-1),AL3(MSG4)
         DC    AL1(L'MSG3-1),AL3(MSG3)
         DC    AL1(L'MSG2-1),AL3(MSG2)
         DC    AL1(L'MSG1-1),AL3(MSG1)
MSGTEND  EQU   *
MSG1     DC    C'00E VOLUME/UNIT NOT LEGAL FOR CLASS=E JOB'      81334
MSG2     DC    C'01E SYS1. DATA SET DISP = SHR'
MSG3     DC    C'02E REGION ON JOBCARD MUST BE LESS THAN 128K'
MSG4     DC    C'03E BAD PRIORITY'
MSG5     DC    C'04E BAD TIME SPECIFICATION'
MSG6     DC    C'05E BAD ROLL OPTION'
MSG7     DC    C'06E BAD CONTINUATION'
MSG8     DC    C'07E BAD SYSOUT, BAD FORM OR EXCESSIVE SYSIN/SYSOUT'
MSG9     DC    C'08E BAD ACCOUNTING INFORMATION'
MSG10    DC    C'09E UNAUTHORIZED BENCHMARK OR RERUN JOB'        81334
MSG11    DC    C'10E AUTOMATIC RESTART NOT SUPPORTED'
MSG12    DC    C'05I JOB FAILED DURING JCL SCAN; NO ERROR DETECTED'
MSG13    DC    C'11E BAD REGION REQUEST'
MSG14    DC    C'12E VOL-SER VALID FOR CLASS F JOB ONLY'
MSG15    DC    C'12E VOL-SER VALID FOR CLASS I JOB ONLY'
MSG16    DC    C'13E JOB SUBMISSION NOT PERMITTED FOR THIS ACCOUNT'
MSG17    DC    C'31E THIS UNIT MUST BE SPECIFIED WITH A VOLUME'  81334
MSG18    DC    C'31E VOLUME PARAMETER INVALID FOR THIS UNIT TYPE'
MSG19    DC    C'33W UNIT= IS NOT MEANINGFUL WITH VOL=REF='      90219
         EJECT                                                   85154
***********************************************************************
*                                                                     *
* IEFVGMSG                                                            *
* *******                                                             *
*                                                                     *
*                                                                     *
* INPUT:                                                              *
*        MESSAGE IN MESSAGE BUFFER (WAMSGBUF=>)                       *
*                                                                     *
* OUTPUT:                                                             *
*        MESSAGE TO THE JCL MESSAGE DATA SET.                         *
*                                                                     *
* EXTERNALS:                                                          *
*        PUT RPL TO SYSTEM MESSAGE DATA SET.                          *
*                                                                     *
* EXIT:                                                               *
*        RETURN TO CALLER OF THIS ROUTINE.                            *
*                                                                     *
* SVC:                                                                *
*        NONE                                                         *
*                                                                     *
***********************************************************************
         SPACE 1                                                 85154
IEFVGMSG STM   R14,R12,12(R13)     SAVE CALLERS' REGISTERS       85154
         LR    R7,R15        COPY BASE                           85161
         USING IEFVGMSG,R7   DECLARE THE BASE                    85161
         L     R1,WAMSGBUF   GET CWA MESSAGE BUFFER              85154
         MVI   0(R1),C' '                                        85154
         MVC   1(159,R1),0(R1)  BLANK IT                         85154
         SLR   R10,R10
         IC    R10,0(,R11)   GET MSG. LENGTH
         MVC   10(5,R1),=C'MSG9 '  PRESET MESSAGE BUFFER         85154
         L     R15,0(,R11)   LOAD MESSAGE ADDRESS                85154
         EX    R10,YAKMVC    MOVE IN ERROR MESSAGE
         LA    R10,15(,R10)  TOTAL MESSAGE LENGTH
         B     IEFVGCOM      JOIN COMMON CODE                    85161
         SPACE 2                                                 85161
IEFVGM80 STM   R14,R12,12(R13)     SAVE CALLERS' REGISTERS       85154
         LR    R7,R15        COPY BASE                           85161
         L     R1,WAMSGBUF   GET CWA MESSAGE BUFFER              85161
         MVI   0(R1),C' '                                        85161
         MVC   1(159,R1),0(R1)  BLANK IT                         85161
         BCTR  R10,0         ADJUST LENGTH FOR EXECUTE           85161
         EX    R10,YAKM80    MOVE CANNED MESSAGE                 85161
         LA    R10,11(,R10)  SET LENGTH FOR WHOLE MESSAGE
IEFVGCOM L     R0,WASTMTNO   GET STATEMENT NUMBER                92347
         CVD   R0,LINKWORK                                       85154
         MVC   LINKWORK+8(6),=X'402020202120'  EDIT MASK         85154
         ED    LINKWORK+8(6),LINKWORK+5     EDIT                 85154
         MVC   0(5,R1),LINKWORK+9                                85154
         LA    R8,WAJOBPFX         LOAD ADDRESS OF JOB PREFIX    85161
         USING TEXT,R8                                           85154
         TM    STRJINDC,JTXMHEDR   TEST IF JOB HEADER PRESENT    85154
         BO    PPMHDRDN            BRANCH IF SO                  85154
         OI    STRJINDC,JTXMHEDR   SET HEADER DONE               85154
         DROP  R8                                                85154
*                                                                85154
         LA    R15,PPMHEAD1        POINT AT FIRST HEADER         85154
         LA    R14,L'PPMHEAD1      GET LENGTH OF HEADER          85154
         BAL   R9,PPMPUTLN         PUT OUT LINE 1 OF HEADER      85154
         LA    R15,PPMHEAD2        POINT AT SECOND HEADER        85154
         LA    R14,L'PPMHEAD2      GET LENGTH OF HEADER          85154
         BAL   R9,PPMPUTLN         PUT OUT LINE 2 OF HEADER      85154
*                                                                85154
PPMHDRDN L     R15,WAMSGBUF        RELOAD BUFFER ADDRESS         85154
         LR    R14,R10       SET MESSAGE LENGTH
         BAL   R9,PPMPUTLN         GO PUT THE LINE               85154
*                                                                85154
         LM    R14,R12,12(R13)     RESTORE CALLERS' REGISTERS    85154
         BR    R14                 RETURN TO CALLER              85154
         SPACE 1                                                 85154
******************** PUT MESSAGE SUBROUTINE ***************************
PPMPUTLN DS    0H                                                85154
         LA    R1,RPLADDR          LOAD THE MESSAGE RPL ADDRESS  85154
         USING IFGRPL,R1                                         85154
         MVI   RPLREQ,RPLPUT       SET PUT REQUEST               85154
         ST    R15,RPLAREA         SET MESSAGE ADDRESS           85154
         XC    RPLARG,RPLARG       ZERO UNNECESSARY FIELD        85154
         ST    R14,RPLRLEN         SET RECORD LENGTH             85154
         MVI   RPLOPT1,RPLSEQ      SET SEQUENTIAL REQUEST        85154
         XC    RPLBUFL,RPLBUFL     ZERO UNNECESSARY FIELD        85154
         XC    RPLDDDD,RPLDDDD     ZERO UNNECESSARY FIELD        85154
         L     R15,WANELPTR        LOAD NEL ADDRESS              85154
         USING NEL,R15                                           85154
         MVC   RPLDACB,NELMSGCB    SET MSG ACB ADDRESS           85154
         DROP  R15                                               85154
*                                                                85154
         ST    R13,IWASACHN        SAVE CURRENT SAVE AREA ADDRESS
         ST    R13,IWAIOSA   IWASACHN GETS CREAMED !             90211
         LA    R13,IWAIOSA         SET I/O SAVE AREA             85154
         PUT   RPL=(1)             DO IT                         85154
         DROP  R1                                                85154
         L     R13,IWAIOSA   RESTORE SAVE AREA ADDRESS           90197
*                                                                85154
         TM    AOSW1,AOIOERR       WAS IT BAD                    85154
         BO    PPMBADNW            BRANCH IF SO                  85154
         LTR   R15,R15             ANOTHER TEST FOR BAD          85154
         BZR   R9                  RETURN IF OK                  85154
*                                                                85154
PPMBADNW MVC   CWARET,=X'0024'     SET I/O ERROR RETURN CODE     85154
         OI    AOSW4,CWATERM       INDICATE TERMINATION          85154
         L     R15,TERMRTN         LOAD TERMINATION ROUTINE ADDRESS
         BR    R15                 AND GO TO IT                  85154
         SPACE 2                                                 85154
YAKMVC   MVC   14(0,R1),0(R15)       MOVE ERROR MESSAGE          85161
YAKM80   MVC   10(0,R1),0(R9)  MOVE USER'S MESSAGE               85161
PPMHEAD1 DC    CL17' STMT NO. MESSAGE'   JCL MESSAGE HEADER      85154
PPMHEAD2 DC    CL17'-'       SECOND HEADER                       85154
         SPACE 1                                                 85154
         LTORG ,                                                 85154
         SPACE 1                                                 85154
         PUSH  PRINT                                             85154
         PRINT &PRTSYS                                           85154
         IFGRPL ,            MAP THE MSG REQUEST BLOCK           85154
         POP   PRINT                                             85154
         TITLE 'I E F U J V  ***  LOCAL CONTROL BLOCKS'          81334
KEYLIST  DSECT ,             SHORT MAPPING FOR TEXT BUFFER FIELDS
KLKEY    DS    X             KEY VALUE                           81334
KLNUM    DS    X             NUMBER OF PARAMETERS (X'80' SUBLIST)
KLLEN    DS    X             LENGTH OF FIRST PARM (IF ANY)       81334
KLPARM   DS    0CL8          VARIABLE LENGTH PARAMETER           81334
         SPACE 2                                                 81334
         PRINT &PRTMAC
USERTCB  USERTCB ,
         SPACE 2                                                 81334
USERCVT  USERCVT ,           LOCAL CVT EXTENSION                 81334
         TITLE 'I E F U J V  ***  SYSTEM CONTROL BLOCKS'         81334
         IEFJMR ,
         SPACE 3
NEL      DSECT ,
         IEFNEL SUBCOM=C                                         85112
         SPACE 3
         IEFCOMWA ,                                              83261
         IEFCVRWA ,                                              85112
         SPACE 1
R10      EQU   10            WHY RA ?
R11      EQU   11            WHY RB ?
R12      EQU   12            WHY RC ?
R13      EQU   13            WHY RD ?
R14      EQU   14            WHY RE ?
R15      EQU   15            WHY RF ?
         SPACE 3
*              MESSAGE KEYS ARE OF TWO TYPES, PRIMARY AND SECONDARY.
*              PRIMARY MESSAGES GENERALLY START WITH A MESSAGE NUMBER,
*              IEFXXXI, WHILE SECONDARY MESSAGES ARE APPENDED TO
*              PRIMARY MESSAGES.
*              THE KEY VALUES FOR PRIMARY AND SECONDARY MESSAGES ARE
*              TOTALLY INDEPENDENT, THEY ARE DISTINGUISHED BY LOCATION.
*              IEFVGM LOOKS FOR PRIMARY MESSAGE KEYS IN R2, AND FOR
*              SECONDARY MESSAGES IN MSGKEY, A BYTE IN THE LOCAL WORK
*              AREA.
         SPACE 2
*              ALL OF THE KEYWORDS IN THE RANGES 2C-6E, 81-94, A8-B4,
*                    HAVE MESSAGE KEYS CORRESPONDING TO THEIR INTERNAL
*              TEXT KEYS, AS GIVEN BY MACRO IEFVKEYS.
         SPACE 2
*              THE FOLLOWING MESSAGE KEYS MAY BE OF USE
*              IT IS OBVIOUS WHICH ARE PRIMARY AND WHICH SECONDARY.
*        HEX   MESSAGE
         SPACE 1
*        22    IEF634I ACCOUNT NUMBER MISSING
*        25    IEF637I EXCESSIVE ACCOUNT FIELD LENGTH
*        26    IEF638I SPECIFIED NUMERIC EXCEEDS MAXIMUM ALLOWED
*        27    IEF639I INVALID CLASS DESIGNATION
*        28    IEF640I EXCESSIVE NUMBER OF POSITIONAL PARAMETERS
*        29    IEF641I IMPROPER SUBPARAMETER LIST
*        2A    IEF642I EXCESSIVE PARAMETER LENGTH
*        2B    IEF643I UNIDENTIFIED POSITIONAL PARAMETER
*        2E    IEF646I REQUIRED POSITIONAL PARAMETER MISSING
*        30    IEF648I INVALID DISP FIELD
*        46    IN THE DISP FIELD
*        4B    IN THE SYSOUT FIELD
*        6E    ON THE DD STATEMENT
*        81    IN THE DPRTY OVERRIDE FIELD
*        82    IN THE DPRTY FIELD
*        83    IN THE EXEC STATEMENT  (SYSFLGS)
*        86    IN THE ROLL OVERRIDE FIELD
*        87    IN THE ROLL FIELD
*        88    IN THE REGION OVERRIDE FIELD
*        89    IN THE REGION FIELD
*        8F    IN THE TIME OVERRIDE FIELD
*        90    IN THE ACCOUNT FIELD
*        94    IN THE EXEC STATEMENT
*        A9    IN THE TIME FIELD
*        AC    IN THE ROLL FIELD
*        AD    IN THE CLASS FIELD
*        AE    IN THE REGION FIELD
*        B0    IN THE PRTY FIELD
*        B4    ON THE JOB STATEMENT
         SPACE 2
*              ADDITIONAL MESSAGE KEYS ARE ALSO AVAILABLE
*              SEE THE LISTINGS OF IEFVGMX, FOR X NUMERIC
         IEFVKEYS ,
         SPACE 3
         PRINT &PRTSYS       SKIP THE MUNDANE EXPANSION          81334
CVT      DSECT ,                                                 81334
         CVT   DSECT=YES                                         81334
         SPACE 1                                                 85112
         IHACDE ,                                                85112
         SPACE 1                                                 81334
         IKJTCB ,                                                81334
         SPACE 1                                                 81334
         IEFUCBOB ,                                              81334
         SPACE 1                                                 81334
         END
