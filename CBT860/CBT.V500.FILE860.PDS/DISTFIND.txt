DISTF    TITLE 'D I S T  ***  PREPROCESSOR TO LOCATE DISTRIBUTION FILE'
         PUNCH '  ORDER DISTFIND(P)  '                          GP10189
         COPY  OPTIONGB                                          86164
         SPACE 1                                                 86164
         SYSPARM LIST=YES                                        86164
         EJECT ,                                                 86164
         PRINT &PRTSOR                                           86164
*
*        THIS MODULE ACCEPTS INPUT IN 'ASMANY' INPUT FORMAT
*        INPUT IS COPIED AS IS TO TEMP DSN &&ASMIN FOR ASMANY INPUT
*        MEMBER NAMES ARE EXTRACTED AND SORTED IN-STORAGE        86164
*        THE SORTED MEMBERS ARE THEN CHECKED AGAINST THE SORTED INDEX
*        OF THE OS OPTIONAL SOURCE FILES, BUILT EARLIER BY A RELATED
*        PROGRAM DISTBLDL.
*        THE RESULTING OUTPUT IS SORTED AGAIN ON VOLUME/FILE AND
*        WRITTEN TO A TEMPORARY DSN &&DISTIN FOR INPUT TO DIST
*        BOTH THE INDEX FILE AND &&DISTIN USE THE FORMAT DESCRIBED
*        BY MACRO MAPINDEX.
         SPACE 1
DISTFIND PGMHEAD ZERO,BASE=R12,PARM=R11,BNDRY=PAGE              GP03027
         OICC  4             SHOW NO ASSEMBLY NAMES FOUND       GP10189
         SERVINIT ,          PREPARE FOR @SERVICE                86164
         SERVLOAD @INPREAD,@PRINTER  LOAD SUPPORT PROGRAMS      GP10189
         INPOPEN SYSIN,OPT=(ABEND)  OPEN CONTROL INPUT           86164
         PRTOPEN SYSPRINT,OPT=(DUMMY)  OPEN; ALLOW IF MISSING    86164
         PRTLIST HEADER,TITLE=1  MAKE A TITLE                    86164
         PRTOPEN ASMOUT,OPT=ABEND,DEV=2  OPEN ASMANY INPUT       86164
         PRTOPEN DISTOUT,OPT=ABEND,DEV=3   OPEN DIST'S PULL FILE 86164
         INPOPEN INDEX,DEV=2,OPT=ABEND   OPEN THE INDEX FILE     86164
         GETMAIN VC,LA=MUCHSTOR,A=GOTNSTOR                       86164
         BXH   R15,R15,NOSTOR2                                   86164
         LM    R2,R3,GOTNSTOR      FIND HOW MUCH AND WHERE       86164
         L     R0,=A(16*1024)  GIVE 16K BACK TO OS (ABENDS,...)  86164
         ST    R2,TABEGIN    SAVE START ADDRESS
         LR    R4,R3         GET AMOUNT
         SR    R4,R0         GET REMAINDER AFTER RETURN
         BNP   NOSTOR        BUT HOW ?                           86164
         LA    R1,0(R2,R4)   END ADDRESS+1 GOTTEN; ADDRESS TO FREE
         LR    R5,R1         COPY FOR SAVING
         SH    R5,=Y(MEMSIZE)   TAKE OUT LAST ENTRY              86164
         BNP   NOSTOR        HOW ?                               86164
         ST    R5,TABEND     SAVE END ADRESS
       FREEMAIN R,LV=(0),A=(1)  FREE IT UP
         SPACE 2
         L     R10,TABEGIN   GET START OF NAME TABLE             86164
         USING MEMSECT,R10   DECLARE THE MAPPING                GP10189
PROCESS  INPGET ,            GET AN ASMANY TYPE INPUT RECORD     86164
         CH    R15,=H'4'     TEST RETURN                         86164
         BL    GOTIN         EXPECTED RETURN                     86164
         BE    SYNEOF        END-FILE                            86164
GETSYERR PRTF  '0***** I/O ERROR ON SYSIN *****'                 86164
         SERVTERM ,                                             GP10189
         ABEND 001,DUMP      NO, COMPLAIN AND QUIT               86164
         SPACE 1
GOTIN    LR    R3,R1         SAVE NEW CARD ADDRESS               86164
         USING CARDSECT,R3   DECLARE MAPPING                     86164
         PRTF  CARD,L'CARD,DEV=(1,2),CC=NO  MAKE A COPY FOR ASMANY
         LA    R6,1          INITIALIZE THE BXH INCREMENT        86164
         LA    R7,CARD+70    SET THE STOP ADDRESS                86164
         LR    R5,R3         SET THE SCAN START ADDRESS          86164
         ZI    PROFLAGS,FGSTACK RESET STACKED ASSEMBLIES        GP10189
         LA    R4,OPTTAB     USE GENERAL OPTION TABLE            86164
PROCHK   BAL   R8,VERBLOOK   CHECK AND BRANCH ON VERB CODE       86164
         B     NEWMEM        TREAT AS NEW MEMBER NAME            86164
         SPACE 1                                                 86164
SETOPT   CLI   CARD+1,C' '   CHECK FOR ALL BLANK CARD            86164
         BNE   SETOPTT       NO; CHECK INDIVIDUAL OPTIONS        86164
         CLC   CARD+2(70),CARD+1  ALL BLANK ?                    86164
         BE    PROCESS       YES; SKIP                           86164
SETOPTT  LA    R4,MODTABAL   ELSE USE FULL TABLE                 86164
         BAL   R8,VERBSCAN   SCAN OUT INDIVIDUAL OPTIONS         86164
         B     PROCESS       GET ANOTHER CARD                    86164
         SPACE 1                                                 86164
VERBSCAN LA    R7,CARD+70    SET STOP SCAN ADDRESS               86164
VERBSCAM LA    R6,1          MAKE INCREMENT                      86164
VERBSCAL CLI   0(R5),C' '    LEADING BLANK ?                     86164
         BE    VERBSCAP                                          86164
         CLI   0(R5),C','    COMMA ?                             86164
         BNE   VERBLOOK      NO; LOOK FOR MATCH                  86164
VERBSCAP BXLE  R5,R6,VERBSCAL  TRY NEXT COLUMN                   86164
         SR    R5,R6         POINT TO LAST BLANK (FOR USER TEST) 86164
         BR    R8            RETURN NOT FOUND                    86164
VERBLUMP LA    R4,4(R4,R15)  SPACE TO NEXT TABLE ENTRY           86164
VERBLOOK SLR   R15,R15       CLEAR IC REGISTER                   86164
         ICM   R15,1,0(R4)   INSERT TABLE TEXT LENGTH-1          86164
         BMR   R8            RETURN NOT FOUND - END OF TABLE     86164
         EX    R15,VERBCLC   MATCHING VERB ?                     86164
         BNE   VERBLUMP      NO; TRY NEXT                        86164
         LA    R5,1(R5,R15)  POINT SCAN REGISTER PAST VERB       86164
         ICM   R15,3,1(R4)   GET BRANCH OFFSET                   86164
         B     DISTFIND(R15)   INVOKE THE VERB ROUTINE           86164
VERBCLC  CLC   0(0,R5),3(R4)   COMPARE TEXT TO VERB              86164
         SPACE 2                                                 86164
SETLINK  LA    R4,NULLTAB    GET DUMMY TABLE                     86164
         BAL   R8,VERBSCAN   LOOK FOR ANYTHING                   86164
         CLI   0(R5),C' '    BLANK ?                             86164
         BH    NEWMEM        NO; PROCESS ASSEMBLY/LINK REQUEST   86164
         B     PROCESS       IGNORE LINK-ONLY REQUEST            86164
         SPACE 1                                                 86164
*        UNIDENTIFIED CARD - TREAT AS NEW MEMBER NAME (ASM REQ.) 86164
*        IF MORE THAN ONE WORD, THE EXTRA WORDS ARE ASSEMBLIES   86164
*        TO BE INCLUDED WITH THE FIRST ONE -                     86164
*                                                                86164
*        'DISASM DISASM1 DISASM2' ASSEMBLES THREE MODULES, WITH  86164
*            THE MAIN MEMBER BEING DISASM                        86164
*        'IEHVTOC: IEHVMAIN IEHVINDX IEHVFMT1,IEHVFMT2 +  '      86164
*        'IEHVFMT3 IEHVFMT4 IEHVFMT5 IEHVFMT6             '      86164
*            ASSEMBLES 8 MODULES (EXCL. IEHVTOC), AND USES THE   86164
*            MEMBER NAME IEHVTOC ON THE LKED NAME CARD.          86164
*        A) COMMAS OR BLANKS MAY SEPARATE MEMBER NAMES           86164
*        B) COLON AFTER FIRST INDICATES LKED NAME, NOT ASSEMBLY  86164
*        C) TRAILING PLUS OR MINUS (OR NON-BLANK COL. 72) INDICATES
*             CONTINUATION                                       86164
*        D) * STARTS A COMMENTS FIELD                            86164
         SPACE 1                                                 86164
NEWMEM   ZI    PROFLAGS,FGSTACK RESET STACKED ASSEMBLIES        GP10189
         MVC   MAINNAME,BLANKS  CLEAR MAIN MEMBER NAME          GP10189
POSTMEM2 MVC   CURRNAME,BLANKS  CLEAR ASSEMBLY NAME             GP10189
         CLI   0(R5),C'0'    NUMERIC OR INVALID START ?          86164
         BNL   BADCARD       YES; SYNTAX ERROR                   86164
         LR    R15,R7        GET LAST COLUMN TO SCAN             86164
         SR    R15,R5        GET LENGTH-1 TO EXAMINE             86164
         BM    BADCARD       EMPTY CARD ?                        86164
         LA    R1,1(,R7)     SET END IN CASE EXHAUSTED           86164
         SLR   R2,R2         CLEAR FOR IC BY TRT                 86164
         EX    R15,MEMTRT    SCAN FOR END OF NAME                86164
         LR    R15,R1        GET FIRST NON-ZERO ADDRESS          86164
         SR    R15,R5        LESS START ADDRESS                  86164
         BNP   BADCARD       INVALID TEXT                        86164
         BCTR  R15,0         EXECUTE LENGTH                      86164
         CH    R15,=Y(L'CURRNAME)  NOT TOO LONG ?                86164
         BNL   BADCARD       TOO BAD                             86164
         EX    R15,MEMMVC    MOVE                                86164
         LR    R5,R1         SET NEW START SCAN POINTER          86164
NEWMEMND STM   R5,R7,NAMESTAK  SAVE SCAN REGISTERS               86164
         TM    PROFLAGS,FGSTACK   FIRST TIME?                   GP10189
         BNZ   NEWMEMF2      NO; SPECIAL CHECKS                  86164
         OI    PROFLAGS,FGSTACK   SET STACK ON                  GP10189
         MVC   MAINNAME,CURRNAME  COPY NAME                      86164
         CLM   R2,1,NAMETRT+C':'  LKED NAME ONLY ?               86164
         BNE   NEWMEMDO      NO; ASSEMBLE THIS ONE               86164
         MVC   CURRNAME,BLANKS  CLEAR ASSEMBLY NAME FOR CHECK   GP10189
         B     POSTNAME      SCAN FOR NEXT NAME                  86164
MEMTRT   TRT   0(0,R5),NAMETRT  CHECK FOR END OF NAME            86164
MEMMVC   MVC   CURRNAME(0),0(R5)  MOVE NAME TO JFCB              86164
         SPACE 1                                                 86164
NEWMEMF2 CLI   CURRNAME,C' '  ANY MEMBER EVER PROCESSED ?        86164
         BNH   BADCARD       SEQUENCE OR LOGIC ERROR             86164
         SPACE 1                                                 86164
NEWMEMDO SERVCALL DSMEM,CURRNAME   VALIDITY CHECK THE NAME       86164
         BXLE  R15,R15,NEWMEMOK  OK                              86164
BADCARD  PRTF  '*** INVALID CARD ***',CC=NO                      86164
LVL8ERR  OICC  8                    SET COND.CODE=8             GP10189
         B     PROCESS       ?                                   86164
         SPACE 1                                                 86164
*        PLACE AN ASSEMBLY MODULE NAME INTO THE TABLE            86164
*        THE NAME IS LOCATED IN CURRNAME                         86164
NEWMEMOK MVC   DISMEMBR,CURRNAME    SAVE PRESUMED MEMBER NAME    86164
         LA    R10,MEMSIZE+MEMSECT  BUMP TO NEXT ENTRY          GP10189
         C     R10,TABEND    SEE IF ROOM FOR MORE                86164
         BL    POSTNAME      YES                                 86164
NOSTOR2  PRTL '0INSUFFICIENT STORAGE FOR MEMBER NAMES; INCREASE REGION *
               AND TABLE SIZE'                                  GP10189
         SERVTERM ,                                             GP10189
         ABEND 804,DUMP
         SPACE 1
POSTNAME LM    R5,R7,NAMESTAK  SEE IF ANY MORE ASSEMBLIES/MEMBER 86164
         BCTR  R5,0          BACK UP ONE                         86164
POSTLOOP BXH   R5,R6,POSTFULL  END OF CARD                       86164
         SLR   R2,R2         CLEAR FOR IC                        86164
         TRT   0(1,R5),NAMETRT  CHECK CURRENT CHARACTER          86164
         BIX   VAL=(R2),ERR=BADCARD,PFX=POST,                          *
               LOC=(MEM2,LOOP,LOOP,LOOP,FULL,GET,LKNM)           86164
POSTLKNM CLI   CURRNAME,C' '  FIRST TIME ?                       86164
         BH    BADCARD       NO, ERROR                           86164
         B     POSTLOOP                                          86164
POSTFULL CLI   CARD+71,C' '  CONTINUATION ?                      86164
         BNH   PROCESS       NO; GET NEXT CARD                   86164
POSTGET  INPGET ,            GET ANOTHER CARD (CONTINUATION)     86164
         CH    R15,=H'4'     CHECK RETURN                        86164
         BL    POSTGOT       OK                                  86164
         BH    GETSYERR      BOO                                 86164
         PRTF  '*** EXPECTED CONTINUATION NOT FOUND ***',CC=NO   86164
         SERVTERM ,          FORCE MESSAGE OUT                   86164
         B     GETSYERR      ABEND                               86164
POSTGOT  LR    R3,R1         SAVE CARD START                     86164
         LR    R5,R3         DITTO                               86164
         LA    R7,CARD+70    BXLE END                            86164
         PRTF  CARD,L'CARD,CC=NO,DEV=(1,2)  PUNCH THE CARD       86164
         B     POSTLOOP      TRY FOR MORE                        86164
         SPACE 2
SYNEOF   MVC   MEMSECT(MEMSIZE),=80X'FF'   MAKE ONE MORE        GP10189
         SH    R10,=AL2(MEMSIZE)  GET END OF TABLE              GP10189
         ST    R10,TABLAST   SAVE LOGICAL TABLE END             GP10189
         DROP  R10                                              GP10189
         INPCLOSE ,          CLOSE SYSIN                         86164
         PRTCLOSE DEV=2      CLOSE ASMOUT                        86164
         SPACE 1
         L     R14,TABEGIN                                      GP10189
         L     R15,TABLAST                                      GP10189
         L     R0,=AL1(C'A',DISMEMBR-MEMSECT,L'DISMEMBR,MEMSIZE)
         STM   R14,R0,SORTPARM    MAKE SORT PARM LIST           GP10189
         OI    SOPHIGH,SOPFAD     PASSING END ADDRESS           GP10189
         SERVCALL SORTH,SORTPARM  SORT BY MEMBER                GP10189
         PRTSPACE 2          SEPARATE INPUT FROM OUTPUT         GP10189
         SPACE 1
         L     R10,TABEGIN   GET FIRST ENTRY                    GP10189
         USING MEMSECT,R10   DECLARE THE MAPPING                GP10189
         CLI   DISMEMBR,X'FF'   ANY ENTRIES ?                   GP10189
         BL    FINDONE       YES
         PRTL  '0NO MEMBER NAMES FOUND'                         GP10189
         SERVTERM ,                                             GP10189
         ABEND 0,DUMP
         SPACE 1
FINDONE  INPGET DEV=2        GET AN INDEX RECORD                 86164
         LR    R4,R1         COPY OVER                          GP10189
         BXLE  R15,R15,MATCHER    PROCEED                       GP10189
         USING INDXSECT,R4                                      GP10189
         PRTL  '0INDEX FILE EMPTY'                              GP10189
         SERVTERM ,                                             GP10189
         ABEND 0,DUMP
         SPACE 2
MATCHER  CLC   DISMEMBR,INDMEM    WANTED MEMBER ?               GP10189
         BL    NOTFOUND      NO; DOESN'T EXIST ON FILE          GP10189
         BE    MATCH         MATCH; PROCESS                     GP10189
         INPGET DEV=2        GET AN INDEX RECORD                 86164
         LR    R4,R1         COPY OVER                          GP10189
         USING INDXSECT,R4                                      GP10189
         CH    R15,=H'4'     TEST RETURN                         86164
         BL    MATCHER       EXPECTED RETURN                    GP10189
         BE    INDEOF        END-FILE                            86164
GETIXERR PRTF  '0***** I/O ERROR ON INDEX *****'                 86164
         ABEND 001,DUMP      NO, COMPLAIN AND QUIT               86164
         SPACE 1
NOTFOUND PRTLIST MISSMEM     PRINT NAME OF MISSING MEMBER        86164
         MVI   DISMEMBR,X'FE'     SHOW NOT FOUND                GP10189
         OICC  12            SET ERROR FLAGS IN RETURN CODE     GP10189
         SPACE 1
NEXTMEM  LA    R10,MEMSIZE+MEMSECT  BUMP TO NEXT ENTRY          GP10189
         CLI   DISMEMBR,X'FF'    LAST ENTRY  ?                  GP10189
         BNL   PUTDIST       YES; ALL DONE                      GP10189
         CLC   DISMEMBR,PREVMEM   DUPLICATE INPUT ?             GP10189
         BE    NEXTMEM       YES; IGNORE IT                     GP10189
         MVC   PREVMEM,DISMEMBR   REMEMBER IT                   GP10189
         B     MATCHER       AND PROCESS                        GP10189
         SPACE 1
MATCH    CLI   DISMEMBR,X'FF'     END OF PROCESSING ?           GP10189
         BE    PUTDIST            YES; GET OUT                  GP10189
         PRTLIST MSGINDEX    PRINT THE MATCH                    GP10189
         MVC   MEMSECT(MEMSIZE),BLANKS  CLEAR FOR COPY          GP10189
         MVC   DISVOL,INDVOL   SAVE THE SERIAL                   86164
         MVC   DISFIL,INDFIL      FILE NUMBER (0 FOR DASD)      GP10189
         MVC   DISDSN,INDDSN      DATA SET NAME                 GP10189
         MVC   DISLBL,INDLBL      AND LABEL TYPE                GP10189
         MVC   DISMEMBR,INDMEM    AND MEMBER                    GP10189
         B     NEXTMEM                                          GP10189
         SPACE
INDEOF   LA    R10,=80X'FF'  FAKE END FILE                      GP10189
         B     MATCHER       FAIL ALL REMAINING MEMBERS         GP10189
         SPACE 2
PUTDIST  INPCLOSE DEV=2      CLOSE INDEX FILE                    86164
         L     R14,TABEGIN                                      GP10189
         L     R15,TABLAST                                      GP10189
         L     R0,=AL1(C'A',0,MEMSIZE,MEMSIZE)  SORT IMAGE      GP10189
         STM   R14,R0,SORTPARM    MAKE SORT PARM LIST           GP10189
         OI    SOPHIGH,SOPFAD     PASSING END ADDRESS           GP10189
         SERVCALL SORTH,SORTPARM  SORT BY MEMBER                GP10189
         L     R10,TABEGIN    GET FIRST ENTRY AGAIN             GP10189
         USING MEMSECT,R10    DECLARE THE MAPPING               GP10189
PUTLOOP  CLI   DISMEMBR,X'FE'   LAST ENTRY; OR MISSING ?        GP10189
         BH    EOJ           LAST; DONE
         BE    PUTBUMP       MISSING; IGNORE                    GP10189
         NI    RETCODE+3,255-4  SHOW AT LEAST ONE FOUND         GP10189
         PRTLIST DISTMSG,DEV=3  MAKE A DIST CONTROL CARD         86164
PUTBUMP  LA    R10,MEMSIZE+MEMSECT  BUMP TO NEXT ENTRY          GP10189
         B     PUTLOOP
         SPACE 2
EOJ      SERVTERM ,          CLOSE AND FREE                      86164
         L     R9,RETCODE    GET RETURN CODE                    GP10189
         PGMEXIT RC=(R9)     RETURN WITH CODE                   GP03027
         SPACE 2
NOSTOR   L     R0,GOTNSTOR+4                                     86164
         ICM   R1,15,GOTNSTOR                                    86164
         BZ    NOSTOR2                                           86164
       FREEMAIN R,LV=(0),A=(1)
         B     NOSTOR2                                           86164
         EJECT ,
MUCHSTOR DC    A(8*1024,500*1024)  STORAGE REQUEST LIST          86164
GOTNSTOR DC    2A(0)         GETMAIN RETURN VALUES (ADDR/SIZE)   86164
SYSIN    INPWORK SYSIN,WIDTH=80,FILL=C' ',EDIT=128               86164
INDEX    INPWORK INDEX,WIDTH=80,FILL=C' ',EDIT=128               86164
ASMOUT   PUNWORK ASMOUT      ASMANY INPUT FILE (ASM REQUESTS)    86164
DISTOUT  PUNWORK DISTOUT     DIST INPUT FILE (MODULE NAMES)      86164
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                86164
BLANKS   DC    CL80' '                                          GP10189
         SPACE 1                                                 86164
HEADER   FDOPT NL,CC=C'#'    AUTO PAGE/DATE                      86164
         FDPRT 'D I S T F I N D   ***  DISTRIBUTION FILE LOCATOR SERVIC*
               E'                                                86164
         FDPRT *END                                              86164
         SPACE 1                                                 86164
MISSMEM  FDPRT '***** Member',PAD,RADJ,NL,LEN=20                GP10189
         FDPRT DISMEMBR,PAD   MEMBER NAME FROM TABLE             86164
         FDPRT 'not found in INDEX *****'                        86164
         FDPRT *END                                              86164
         SPACE 1                                                 86164
DISTMSG  FDPRT DISMEMBR,NL   SAME FORMAT AS MAPINDEX            GP10189
         FDPRT DISVOL                                           GP10189
         FDPRT DISLBL                                           GP10189
         FDPRT DISFIL                                           GP10189
         FDPRT DISDSN                                           GP10189
         FDPRT *END                                              86164
         SPACE 1                                                 86164
MSGINDEX FDPRT 'Found',NL                                       GP10189
         FDPRT INDMEM,PAD                                       GP10189
         FDPRT 'on'                                             GP10189
         FDPRT INDVOL,PAD                                       GP10189
         FDPRT INDLBL                                           GP10189
         FDPRT 'L file'                                         GP10189
         FDPRT INDFIL,PAD,LADJ,LEN=3                            GP10189
         FDPRT 'in'                                             GP10189
         FDPRT INDDSN,PAD                                       GP10189
         FDPRT *END                                             GP10189
         SPACE 1                                                 86164
*        INPUT VERB TABLE                                        86164
OPTTAB VERBTAB ' ',PROCESS,LEN=2,BASE=*    LKED CONTROL CARD     86164
       VERBTAB '+',PROCESS             POST-ASM CONTROL CARD     86164
       VERBTAB *02,PROCESS             OBJECT DECK CARD          86164
       VERBTAB 'DSN=',PROCESS        NEW DDN OR DSN REQUIRED     86164
       VERBTAB 'DDN=',PROCESS        TABLE FOR NEW DDN           86164
       VERBTAB 'DDMOD=',PROCESS        SET NEW SYSLMOD NAME      86164
       VERBTAB 'DDLIB=',PROCESS        SET (LKED) SYSLIB DDN     86164
       VERBTAB '%',SETOPT              OPTION CARD               86164
       VERBTAB '*',PROCESS             TURN WTOR ON/OFF          86164
       VERBTAB 'SSI=',PROCESS          SET/RESET SSI             86164
       VERBTAB 'SEP=',PROCESS          SET/RESET SEPARATOR OPTION
       VERBTAB 'CODE=',PROCESS         SET STOP CONDITION CODE   86164
       VERBTAB 'COND=',PROCESS           DITTO                   86164
       VERBTAB 'LKEDCODE=',PROCESS     LKED CALL CODE            86164
       VERBTAB 'LKEDCOND=',PROCESS                               86164
       VERBTAB 'COMPCODE=',PROCESS                              GP04064
       VERBTAB 'ASMCODE=',PROCESS                               GP04064
       VERBTAB 'ASMCOND=',PROCESS                               GP04064
       VERBTAB 'ID=',PROCESS           SET THIRD SEPARATOR LINE  86164
NULLTAB VERBTAB *END                                             86164
         SPACE 1                                                 86164
MODTABAL VERBTAB LINK,SETLINK          LKED MEMBER ONLY (Â¬ALIAS) 86164
       VERBTAB RELINK,SETLINK          RELINK ENTIRE MEMBER      86164
       VERBTAB NEWLINK,SETLINK         LINK NEW MEMBER           87074
       VERBTAB LLA,PROCESS                                      GP97289
       VERBTAB REFRESH,PROCESS                                  GP97289
       VERBTAB *END                                              86164
         SPACE 2                                                 86164
*        TRT TABLE FOR NAME SCANNING                             86164
*                                                                86164
NAMETRT  DC    256AL1(255)                                       86164
         ORG   NAMETRT+C' '                                      86164
         DC    AL1(1)                                            86164
         ORG   NAMETRT+C'$'                                      86164
         DC    AL1(0)                                            86164
         ORG   NAMETRT+C'#'                                      86164
         DC    AL1(0)                                            86164
         ORG   NAMETRT+C'@'                                      86164
         DC    AL1(0)                                            86164
         ORG   NAMETRT+C'A'                                      86164
         DC    9AL1(0)       A-I                                 86164
         ORG   NAMETRT+C'J'                                      86164
         DC    9AL1(0)       J-R                                 86164
         ORG   NAMETRT+C'S'                                      86164
         DC    8AL1(0)       S-Z                                 86164
         ORG   NAMETRT+C'0'                                      86164
         DC    10AL1(0)      0-9                                 86164
         ORG   NAMETRT+C','                                      86164
         DC    AL1(2)                                            86164
         ORG   NAMETRT+C';'                                      86164
         DC    AL1(3)                                            86164
         ORG   NAMETRT+C'*'                                      86164
         DC    AL1(4)                                            86164
         ORG   NAMETRT+C'-'                                      86164
         DC    AL1(5)                                            86164
         ORG   NAMETRT+C'+'                                      86164
         DC    AL1(5)        CONTINUATION                        86164
         ORG   NAMETRT+C':'                                      86164
         DC    AL1(6)        END OF LINK-EDIT NAME               86164
         ORG   ,                                                 86164
         SPACE 1                                                 86164
SAVE     DSECT ,             SAVE/WORK AREA                      86164
DB       DS    D             WORK SPACE                         GP10189
PREVMEM  DS    CL8           PROCESSED MEMBER NAME              GP10189
         SERVDEFS ,          SERVICE ROUTINE DATA               GP10189
TABEGIN  DC    A(0)          FIRST ENTRY                        GP10189
TABLAST  DC    A(0)          LAST USED ENTRY                    GP10189
TABEND   DC    A(0)          END PF TABLE SPACE                 GP10189
         SPACE 1
SORTPARM SERVSORT DSECT=NO,PFX=SOP
         SPACE 1
PROFLAGS DC    AL1(0)        STACK FLAG                         GP10189
FGSTACK  EQU   X'02'           STACKED CARD                     GP10189
         SPACE 1                                                 86164
MAINNAME DC    CL8' '        MAIN MEMBER NAME                    86164
CURRNAME DC    CL8' '        CSECT NAME                          86164
NAMESTAK DS    3A            BXLE POINTERS FOR NAME SCANNING     86164
SAVEEND  EQU   *                                                GP03027
         SPACE 2                                                 86164
         PRINT &PRTMAC                                           86164
CARDSECT DSECT ,             INPUT RECORD GOING TO ASMANY        86164
CARD     DS    CL80                                              86164
         SPACE 1                                                 86164
MEMSECT  DSECT ,                                                 86164
DISVOL   DS    CL6           VOL-SER                             86164
DISFIL   DS    CL3           FILE                               GP10189
DISDSN   DS    CL44          DATA SET NAME                      GP10189
DISLBL   DS    C             LABEL TYPE                         GP10189
DISMEMBR DS    CL8           MODULE NAME                         86164
MEMSIZE  EQU   *-MEMSECT     ENTRY SIZE                          86164
         SPACE 1                                                 86164
         PRINT &PRTMAC                                          GP10189
INDXSECT MAPINDEX PFX=IND                                       GP10189
         SPACE 2
         PRINT &PRTSYS                                           86164
CVTDSECT DSECT ,                                                 86164
         CVT   DSECT=YES                                         86164
         IHACDE ,                                                86164
         END
