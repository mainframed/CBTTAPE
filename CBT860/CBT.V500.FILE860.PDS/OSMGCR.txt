OSMGCR  TITLE 'O S M G C R  ***  ISSUE OS COMMANDS FROM PARM AND SYSIN'
         PUNCH  '  SETCODE AC(1) '   NOT MUCH GOOD WITHOUT IT
         PUNCH  ' ORDER OSMGCR(P) '  MAKE DUMPS EASIER TO READ
         SPACE 1
         COPY  OPTIONGB      DEFINE GLOBALS
         SPACE 1
         SYSPARM LIST=YES    SET GLOBALS
         EJECT ,
         PRINT &PRTSOR
OSMGCR   PGMHEAD ZERO,PARM=R8  ENTRY SEQUENCE; STORAGE; USINGS
         MVICC 8,RESULT=RETCODE  PROVISIONALL SET COND CODE 8
         SERVINIT ,          INITIALIZE SERVICES
         SERVLOAD @INPREAD,@PRINTER  LOAD SERVICE ROUTINES
         PRTOPEN SYSPRINT,OPT=(NOWTO)  RUN WITHOUT
         PRTLIST TITLE1,TITLE=1
         LA    R8,0(,R8)     CLEAN PARM POINTER
         LTR   R8,R8         ANY ?
         BZ    DONEPARM      NO; LOOK FOR SYSIN
         L     R8,0(,R8)     GET PARM
         LA    R8,0(,R8)     CLEAN PARM ADDRESS
         LTR   R8,R8         ANY ?
         BZ    DONEPARM      NO; LOOK FOR SYSIN
         SR    R9,R9
         ICM   R9,3,0(R8)    ALLOW FOR > 32K
         LA    R8,2(,R8)     POINT TO TEXT
         LA    R14,COMMAND   GET COMMAND ENTRY
         LA    R15,L'COMMAND  AND MAX LENGTH
         ICM   R9,8,=C' '    BLANK FILL
         MVCL  R14,R8        MOVE AND PAD
         BAL   R14,COMMISS   ISSUE THE COMMAND
         SPACE 1
DONEPARM INPOPEN SYSIN,OPT=(NOWTO)  LOOK FOR SYSIN
         CH    R15,=H'4'     INPUT AVAILABLE ?
         BH    PGMEXIT       NO; QUIT
LOOPIN   INPGET ,            READ NEXT CARD INTO COMMAND TEXT
         BXH   R15,R15,PGMEXIT  QUIT ON EOF OR ERROR
         LR    R14,R1        COPY TEXT ADDRESS
         LR    R15,R0        COPY TEXT LENGTH
         TRT   72(8,R14),TRTDIG   INTEGER - SEQUENCE FIELD?     GP11163
         BNZ   *+8             NO; USE ALL                      GP11163
         LA    R15,72          YES; IGNORE IT                   GP11163
         LA    R2,COMMAND    COMMAND FIELD
         LA    R3,L'COMMAND
         ICM   R15,8,=C' '   BLANK PADDING
         MVCL  R2,R14
         BAL   R14,COMMISS   ISSUE THE COMMAND
         B     LOOPIN        GET ANOTHER CARD
         SPACE 1
PGMEXIT  SERVTERM ,          SHUT DOWN SERVICES
         PGMEXIT COPYRET=(RETCODE,4) QUIT WITH APPROPRIATE CC
         EJECT ,
COMMISS  STM   R0,R15,SUBSAVE  SAVE STUFF
         LA    R15,COMMAND+L'COMMAND
         LA    R14,L'COMMAND+(COMMAND-MGCRPFX)  LENGTH + PREFIX LENGTH
         STH   R14,MGCRPFX   SET LENGTH
COMMISTB BCTR  R15,0         GET PRIOR BYTE
         CLI   0(R15),C' '   IS IT BETTER THAN BLANK?
         BH    COMMISFN      YES
         BCT   R14,COMMISTB  TRY AGAIN
COMMISFN MVC   TEXTCOPY,MGCRPFX  MAKE A COPY
         CH    R14,=AL2(COMMAND-MGCRPFX)  ANY DATA AT ALL ?
         BNH   COMMISPR      NO; JUST PRINT
         STH   R14,MGCRPFX   SET LENGTH
         STH   R14,TEXTCOPY  SET LENGTH
         MODESET KEY=ZERO    GET PRIVIED
         SR    R0,R0         INDICATE COMMAND REQUEST
         CLI   COMMAND,C'$'  POSSIBLE HASP COMMAND ?
         BE    COMMMAST      YES
         CLI   COMMAND,C'Â¢'  POSSIBLE SECONDARY JES COMMAND ?
         BNE   COMMNJES      NO
COMMMAST LA    R0,1          SET FOR (FUNCTIONAL?) MASTER CONSOLE
COMMNJES DS    0H            NO NAME ON MGCR !
         MGCR  MGCRPFX       ISSUE THE COMMAND
         MODESET KEY=NZERO   GET DULL
         MVICC 0             RESET RETURN CODE
COMMISPR PRTLIST CMDORIG     PRINT COMMAND
         CLC   TEXTCOPY,MGCRPFX  DID SYSTEM CHANGE IT ?
         BE    COMMISND      NO
         PRTLIST CMDBACK     PRINT RESPONSE
COMMISND MVI   COMMAND,C' '  BLANK FOR NEXT TIME
         MVC   COMMAND+1(L'COMMAND-1),COMMAND  CLEAR
         LM    R0,R15,SUBSAVE  RESTORE REGS
         BR    R14           GET BACK
         SPACE 2
         LTORG ,
         SPACE 2
SYSPRINT PRTWORK SYSPRINT,SUMPRINT,TITLE=5
SYSIN    INPWORK SYSIN,SYSUT1,EDIT=128,EODAD=PGMEXIT,                  *
               WIDTH=(L'TEXTCOPY),FILL=C' '
         SPACE 1
TITLE1   FDOPT NL,CC=C'#'    AUTO PAGE/DATE/TIME PROCESSING
TITLE1SQ FDOPT SBA=(*,56)
         FDPRT 'O S   C O M M A N D S'
         FD    *END
         SPACE 1
CMDORIG  FDOPT NL,CC=C'0'    DOUBLE SPACE
         FDPRT 'Command:',RADJ,LEN=20
         FDPRT TEXTCOPY+(COMMAND-MGCRPFX),L'COMMAND,DEBR,PAD
         FDPRT *END
         SPACE 1
CMDBACK  FDOPT NL,CC=C'0'    DOUBLE SPACE
         FDPRT 'Response:',RADJ,LEN=20
         FDPRT COMMAND,DEBR,PAD    SYSTEM RESPONSE, IF ANY
         FDPRT *END
         SPACE 2
TRTDIG   DC    256X'08'      ALL CHARACTERS INVALID             GP11163
         TRENT TRTDIG,4,C' '    BLANKS ARE DIFFERENT            GP11163
         TRENT TRTDIG,0,(C'0',10)  BUT ACCEPT DIGITS            GP11163
         SPACE 2
SAVE     DSECT ,             SAVE AND WORK AREA
SUBSAVE  DS    16A           SUBROUTINE SAVE AREA
DB       DS    2D            CONVERSIONS
         SERVDEFS ,          SERVICE ROUTINE ADJUNCTS
         SPACE 1
MGCRPFX  VCON  END=MGCRTEXX
COMMAND  DS    CL100         SET FOR CURRENT PARM LENGTH
         VCON  *END          GENERATE END LABEL FOR LENGTH
TEXTCOPY DS    XL(MGCRTEXX-MGCRPFX)  COPY FOR COMPARE
SAVEEND  EQU   *             END OF WORK AREA
         END   ,
