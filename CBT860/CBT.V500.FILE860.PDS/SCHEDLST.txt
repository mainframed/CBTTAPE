SCHDL    TITLE 'S C H E D L S T  ***  LIST SCHEDULE DATASET'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES    SET GLOBAL OPTIONS
         SPACE 2
***********************************************************************
*                                                                     *
*        THIS QUICKY WAS WRITTEN TO DO A BATCH LIST OF THE SCHEDULE   *
*        FACILITY VSAM DATASET. THE SCHEDULE FACILITY IS KEPT IN DSN  *
*        Z904.SCHEDSRC, AND WAS ADAPTED FROM DAVE COLE'S SUBMISSION   *
*        ON THE CB&T MODS TAPE, FILE 388                              *
*                                                                     *
***********************************************************************
*                                                                     *
*    INPUT DD:  SYSUT1 - SCHEDULE FACILITY VSAM FILE                  *
*                                                                     *
*   OUTPUT DD:  SYSPRINT OR SUMPRINT - FORMATTED REQUEST RECORDS      *
*               HEXPRINT - FORMATTED PLUS HEX DATA                    *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
SCHEDLST SAVEM ZERO12,BASE=(R12),BNDRY=PAGE             ADDED ON 90253
         SPACE 1
         SERVINIT ,
         SPACE 1
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD PRINTER SERVICES
         ST    R0,@PRINTER
         SERVCALL LPALD,=CL8'@INPREAD'  LOAD READER SERVICES
         ST    R0,@INPREAD
         PRTOPEN SYSPRINT,OPT=NOWTO  OPEN PREFERRED FILE        GP02323
         LR    R2,R15        SAVE RETURN CODE                   GP02323
         PRTOPEN HEXPRINT,OPT=NOWTO,DEV=2  OPEN OPTIONAL FILE   GP02323
         OR    R2,R15        COMBINE RETURN CODES               GP02323
         CH    R2,=H'4'      AT LEAST ONE OPEN ?                GP02323
         BNH   HAVEPRNT      YES                                GP02323
         WTO   'SCHEDLST: NO USABLE PRINT DD'                   GP02323
         SERVTERM ,                                             GP02323
         ABEND 1403,DUMP                                        GP02323
         SPACE 1                                                GP02323
HAVEPRNT PRTV  HEADER1,TITLE=1,DEV=(1,2)                        GP02323
         PRTLIST HEADER2,TITLE=2,DEV=(1,2)                      GP02301
         SPACE 1
         INPOPEN SYSUT1,OPT=ABEND  OPEN THE (VSAM) INPUT FILE
         SPACE 1
         INPGET ,            GET MASTER RECORD
         BXH   R15,R15,MISSMAST  MINOR ERROR IF NOT INITIALIZED
         B     NEXTRECD      JOIN COMMON
         SPACE 1
NEXTREC  INPGET ,            GET A RECORD
         BXH   R15,R15,EODAD   ALL DONE
NEXTRECD LR    R5,R1         COPY
         USING SFR,R5        DECLARE SCHEDULE MAPPING
         ICM   R0,3,SFRID    CONTROL RECORD ?
         BM    MIXMAST       YES; SEE IF MASTER RECORD
         MVC   DMDATE(8),=C'--/--/--' JUNK
         SR    R1,R1
         ICM   R1,3,SFRMO          EXPLICIT MONTH?
         BZ    DSTNOMO             NO, SKIP
         CVD   R1,DB          YES, CNVRT TO DECIMAL
         UNPK  DMDATE(2),DB+6(2) CNVRT TO EBCDIC
         OI    DMDATE+1,B'11110000' FIX THE STUPID SIGN
DSTNOMO  ICM   R1,3,SFRDY          EXPLICIT DAY?
         BZ    DSTNODY             NO, SKIP
         CVD   R1,DB          YES, CNVRT TO DECIMAL
         UNPK  DMDATE+3(2),DB+6(2) CNVRT TO EBCDIC
         OI    DMDATE+4,B'11110000' FIX THE STUPID SIGN
DSTNODY  ICM   R1,3,SFRYR          EXPLICIT YEAR?
         BZ    DSTNOYR             NO, SKIP
         CVD   R1,DB          YES, CNVRT TO DECIMAL
         UNPK  DMDATE+6(2),DB+6(2) CNVRT TO EBCDIC
         OI    DMDATE+7,B'11110000' FIX THE STUPID SIGN
DSTNOYR  DS    0H
         SPACE 1
         MVC   DMDAY,BLANKS
         SLR   R3,R3         CLEAR FOR IC                        90259
         ICM   R3,3,SFRWK    EXPLICIT WEEK?
         BZ    DSTNOWK       NO, SKIP
         CLI   SFRWK,0       LIST OF DAYS ?                      90259
         BE    DSCDAY        NO; DISCRETE                        90259
         LA    R0,7          UP TO SEVEN DAYS POSSIBLE           90259
         LA    R14,DMDAY     POINT TO FIRST DISPLAY POSITION     90259
         SLL   R3,16         ALIGN FOR TEST                      90259
         OR    R3,R0         MAKE NON-ZERO LOW BITS              90259
         LA    R15,=C'SMTWTFS'  USE INITIALS AND POSITION        90259
DSDAYLP  BXH   R3,R3,DSDAYUP                                     90259
         MVC   0(1,R14),0(R15)                                   90259
DSDAYUP  LA    R14,1(,R14)                                       90259
         LA    R15,1(,R15)                                       90259
         BCT   R0,DSDAYLP    TRY AGAIN                           90259
         B     DSTNOWK       SKIP DISCRETE DAY CODE              90259
         SPACE 1                                                 90259
DSCDAY   LA    R15,WEEKDAYN-(WEEKDAY2-WEEKDAYS)  SET LAST ENTRY  90259
         LA    R14,WEEKDAY2-WEEKDAYS  ENTRY LENGTH
         LA    R1,WEEKDAYS   POINT TO FIRST ENTRY
         SR    R1,R14        ADJUST FOR FIRST BXH
DSTWKLP  BXH   R1,R14,DSTNOWK  NO MATCH ?
         CLM   R3,1,0(R1)    MATCHING DAY ?
         BNE   DSTWKLP       NO, KEEP LOOKING
         MVC   DMDAY(4),1(R1)   YES, COPY TO MSG                 90259
DSTNOWK  DS    0H
         SPACE 1
         LH    R1,SFRWHR           GET WINDOW HOURS
         MH    R1,=H'100'          DECIMAL SHIFT 2 PLACES
         AH    R1,SFRWMN           INCLUDE WINDOW MINUTES
         CVD   R1,DB          CNVRT TO DECIMAL
         MVC   DMWINDOW,=X'4020202021207A2020' EDIT MASK
         LA    R1,DMWINDOW+5 PRESET FOR (IN)SIGNIFICANCE
         EDMK  DMWINDOW,DB+4 CNVRT TO EBCDIC
         BCTR  R1,0                BACK OFF
         MVI   0(R1),C'+'          DECORATION
         SPACE 1
         LH    R1,SFRHR            GET HOUR
         MH    R1,=H'100'          DECIMAL SHIFT 2 PLACES
         AH    R1,SFRMN            INCLUDE MINUTES
         CVD   R1,DB          CNVRT TO DECIMAL
         UNPK  DMTIME+1(4),DB+5(3) CNVRT TO EBCDIC
         OI    DMTIME+4,B'11110000' FIX THE STUPID SIGN
         MVC   DMTIME(2),DMTIME+1  CORRECT POSITION
         MVI   DMTIME+2,C':'       DECORATION
         SPACE 1
         LA    R1,DMFLAGS          --> FLAGS FIELD
         MVC   DMFLAGS,BLANKS  CLEAR
         TM    SFRFLAG,SFRFIPLF    IF?
         BZ    DSTNOIF             NO, SKIP
         MVI   0(R1),C'I'          YES, SAY SO
         LA    R1,2(,R1)           ADVANCE
DSTNOIF  TM    SFRFLAG,SFRFOVRD    OVERRIDE?
         BZ    DSTNOOV             NO, SKIP
         MVI   0(R1),C'O'          YES, SAY SO
         LA    R1,2(,R1)           ADVANCE
DSTNOOV  TM    SFRFLAG,SFRFOBSO    OBSOLETE COMMAND?
         BZ    DSTNOOBS            NO, SKIP
         MVI   0(R1),C'N'          YES, SAY SO
DSTNOOBS DS    0H
         PRTLIST FDSCHED,DEV=(1,2)   PRINT THE RECORD           GP02323
         PRTLIST FDHEX,DEV=2  PRINT RECORD IN HEX TO HEXPRINT   GP02323
         B     NEXTREC
         SPACE 1
MIXMAST  CLC   =C'SCHEDULE',SFMRFID  CORRECT KEY ?
         BNE   BADMAST       NO
         CLI   SFMRKEY,X'FF'  CORRECT CONTENTS ?
         BNE   BADMAST       NO; REJECT
         CLC   SFMRKEY(L'SFMRKEY-1),SFMRKEY+1   ALL FOXES ?
         BNE   BADMAST
         PRTSPACE 1          DOUBLE-SPACE
         PRTLIST FDMAST,DEV=(1,2)      PRINT MASTER RECORD      GP02323
         B     EODAD
         SPACE 1
MISSMAST OI    RETCH+L'RETCH-1,4  NO MASTER RECORD
         PRTL  '0***** DATASET NOT INITIALIZED *****',DEV=(1,2) GP02301
         B     EODAD
BADMAST  OI    RETCH+L'RETCH-1,8  INVALID MASTER RECORD
         PRTL  '0***** MASTER RECORD NOT VALID *****',DEV=(1,2) GP02301
         SPACE 1
EODAD    SERVTERM ,          CLOSE AND FREE EVERYTHING
         L     R9,RETCH      LOAD RETURN CODE
         ENDM  RC=(R9)       TERMINATE WITH CC
         SPACE 1
SYSPRINT PRTWORK SYSPRINT,SUMPRINT,TITLE=3
HEXPRINT PRTWORK HEXPRINT,TITLE=3                               GP02323
SYSUT1   INPWORK SYSUT1,SCHEDULE,EDIT=128,WIDTH=SFRLEN
         SPACE 1
HEADER1  VCON  '#SCHEDULE FACILITY  -  CURRENT REQUESTS'
HEADER2  FDPRT ' ID SYSID  DATE    DAY    TIME  WINDOW FLAGS',NL,PADR
         FDPRT 'NAME',LEN=L'SFRNAME                             GP02301
         FDPRT ' COMMAND',PADL                                  GP02301
         FDPRT *END                                             GP02301
BLANKS   DC    CL8' '
         SPACE 1
FDMAST   FD    'LAST ASSIGNED RECORD ID IS',NL
         FD    SFMRNXID,I,PAD
         FD    *END
         SPACE 1
FDSCHED  FD    SFRID,I,NL,RADJ,LEN=3
         FD    SFRSYSID,PAD
         FD    DMDATE,PAD     DATE OR -/-/-
         FD    DMDAY,PAD     WEEKDAY
         FD    DMTIME,PAD
         FD    DMWINDOW+L'DMWINDOW-6,6,PAD
         FD    DMFLAGS,PAD,LEN=L'DMFLAGS+1
         FD    SFRNAME,PAD
         FD    SFRCMD,DEB
         FD    *END
         SPACE 1                                                GP02323
FDHEX    FDOPT NL                                               GP02323
         FDFD  SFRYR                                            GP02323
         FDFD  SFRMO                                            GP02323
         FDFD  SFRDY                                            GP02323
         FDFD  SFRHR                                            GP02323
         FDFD  SFRMN                                            GP02323
         FDFD  SFRID                                            GP02323
         FDFD  SFRNAME,TYPE=CHAR                                GP02323
         FDFD  SFRWK                                            GP02323
         FDFD  SFRXYR                                           GP02323
         FDFD  SFRXMO                                           GP02323
         FDFD  SFRXDY                                           GP02323
         FDFD  SFRXHR                                           GP02323
         FDFD  SFRXMN                                           GP02323
         FDFD  SFRWHR                                           GP02323
         FDFD  SFRWMN                                           GP02323
         FDFD  SFRSYSID,OPTS=CT                                 GP02323
         FDFD  SFRFLAG                                          GP02323
         FDFD  SFRCMDL,TYPE=I                                   GP02323
         FDFD  SFRCMD,OPTS=DEB,TYPE=CHAR                        GP02323
         FD    *END                                             GP02323
         SPACE 1
WEEKDAYS DC    AL1(6),CL4'+FRIDAY'
WEEKDAY2 DC    AL1(2),CL4'+MONDAY'
         DC    AL1(7),CL4'+SATURDAY'
         DC    AL1(1),CL4'+SUNDAY'
         DC    AL1(5),CL4'+THURSDAY'
         DC    AL1(3),CL4'+TUESDAY'
         DC    AL1(4),CL4'+WEDNESDAY'
         DC    AL1(6+SFRWKNOT),CL4'¬FRIDAY'
         DC    AL1(2+SFRWKNOT),CL4'¬MONDAY'
         DC    AL1(7+SFRWKNOT),CL4'¬SATURDAY'
         DC    AL1(1+SFRWKNOT),CL4'¬SUNDAY'
         DC    AL1(5+SFRWKNOT),CL4'¬THURSDAY'
         DC    AL1(3+SFRWKNOT),CL4'¬TUESDAY'
         DC    AL1(4+SFRWKNOT),CL4'¬WEDNESDAY'
         DC    AL1(0),CL4'-'
         DC    AL1(-6),CL4'-FRIDAY'
         DC    AL1(-2),CL4'-MONDAY'
         DC    AL1(-7),CL4'-SATURDAY'
         DC    AL1(-1),CL4'-SUNDAY'
         DC    AL1(-5),CL4'-THURSDAY'
         DC    AL1(-3),CL4'-TUESDAY'
         DC    AL1(-4),CL4'-WEDNESDAY'
         DC    AL1(0),CL4'ANY'
         DC    AL1(6+SFRWKXCT),CL4' FRIDAY'
         DC    AL1(2+SFRWKXCT),CL4' MONDAY'
         DC    AL1(7+SFRWKXCT),CL4' SATURDAY'
         DC    AL1(1+SFRWKXCT),CL4' SUNDAY'
         DC    AL1(5+SFRWKXCT),CL4' THURSDAY'
         DC    AL1(3+SFRWKXCT),CL4' TUESDAY'
         DC    AL1(4+SFRWKXCT),CL4' WEDNESDAY'
WEEKDAYN DC    B'10000000'         DELIMITER
         SPACE 2
SAVE     DSECT ,             SAVE/WORK AREA
DB       DS    D             WORK
@SERVICE DS    A
@PRINTER DS    A
@INPREAD DS    A
RETCH    DS    F             RETURN CODE
DMDATE   DS    CL8           DATE -/-/-
DMDAY    DS    CL7           FLAG/WEEKDAY/LIST                   90259
DMTIME   DS    CL5           ISSUE TIME
DMWINDOW DS    CL9           WINDOW
DMFLAGS  DS    CL4           FLAGS
SAVEND   EQU   *
         SPACE 2
         PRINT &PRTSYS
         CVT   DSECT=YES
         IHACDE ,
         SPACE 2
         PRINT &PRTMAC
         SFR   ,
         SPACE 1
         PRINT &PRTSYS
         END   ,
