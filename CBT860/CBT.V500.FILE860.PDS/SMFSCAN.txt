SMFSCAN  TITLE 'FAST SCANNER FOR SMF DATASET'
*          DATA SET CBT276     AT LEVEL 001 AS OF 02/25/75
*              CBT TAPE 249 FILE 195
*********************************************************************
*                                                                    *
*                      D I S C L A I M E R                           *
*                                                                    *
*                                                                    *
*   THE PROGRAMS HAVE BEEN USED SUCCESSFULLY BY CBT AND TO THE BEST  *
*   OF OUR KNOWLEDGE THE SYSTEM IS OPERATIONAL, HOWEVER NO WARRANTY  *
*   IS MADE TO THE ACCURACY OF THE PROGRAMS OR RELATED MATERIAL AND  *
*   NO RESPONSIBILITY IS ASSUMED  FOR ANY MODIFICATION  DIRECTLY OR  *
*   INDIRECTLY  CAUSED BY THE USE OF THE PROGRAMS.  IT IS THE USERS  *
*   RESPONSIBILITY  TO  EVALUATE THE  USEFULLNESS  OF THE  MATERIAL  *
*   SUPPLIED.                                                        *
*                                                                    *
*   CBT DOES NOT GUARANTEE TO KEEP ANY MATERIAL PROVIDED UP TO DATE  *
*   NOR DOES IT GUARANTEE TO PROVIDE ANY CORRECTIONS OR  EXTENSIONS  *
*   DESCRIBED BY THE USER OR  CORRECTIONS OR EXTENSIONS MADE IN THE  *
*   FUTURE BY CBT ITSELF.                                            *
*                                                                    *
**********************************************************************
*   MODIFIED TO USE LOCAL SERVICES AND MACROS           G.POSTPISCHIL*
*   MINOR CLEANUP AND LOGIC CHANGES.                    2012-03-04   *
*--------------------------------------------------------------------*
*   SYSIN INPUT (REQUIRED!; STOPS AFTER FIRST BLANK CARD)            *
*   sssseeee - START AND END TIME hhmm FORMAT (NO DATE SELECTION?)   *
*   nn nn nn nn - LIST OF RECORD TYPES TO IGNORE, OR WORD TEST       *
*   pgmname OR pfx* - PROGRAM NAME TO SELECT                         *
*   jobname OR pfx* - JOB NAME TO SELECT                             *
**********************************************************************
         SPACE 1
         MACRO ,                                                GP12064
&NM      HANDLE &N,&LOC                                         GP12064
         GBLA  &TYP#,&SLOT#                                     GP12064
         AIF   ('&N' EQ '').NEXT                                GP12064
&TYP#    SETA  &N            USE USER'S NUMBER                  GP12064
         AGO   .SET                                             GP12064
.NEXT    ANOP  ,                                                GP12064
&TYP#    SETA  &TYP#+1       NEXT SEQUENTIAL NUMBER             GP12064
.SET     ANOP  ,                                                GP12064
&SLOT#   SETA  &SLOT#+1      NEXT ADDRESS SLOT                  GP12064
         ORG   HANDLERS+&TYP#                                   GP12064
         DC    AL1(&SLOT#)   SHOW ACTIVE ENTRY                  GP12064
         ORG   ,                                                GP12064
&NM      DC    S(&LOC)       POINTER TO PROCESSING ROUTINE      GP12064
         MEND  ,                                                GP12064
         SPACE 2
         MACRO
&NM      JOBCHEK &PLACE
&NM      TM    FLAG,X'88'
         BNO   @J&SYSNDX
         L     R14,@JOBLEN                                      GP12064
         LA    R1,&PLACE
         EX    R14,@JOB
         BNE   NEXREC
*        TEST FOR JOBNAME EQUIVALENCE IF JOB SELECTION ACTIVE ***
         SPACE
@J&SYSNDX DS  0H .
         MEND
         MACRO
&NM      PGMCHEK &PLACE
&NM      TM    FLAG,X'84'
         BNO   @P&SYSNDX
         L     R14,@PGMLEN                                      GP12064
         LA    R1,&PLACE
         EX    R14,@PGM
         BNE   NEXREC
*        TEST FOR PGMNAME EQUIVALENCE IF PGM SELECTION ACTIVE ***
         SPACE
@P&SYSNDX DS  0H .
         MEND
         MACRO
&NM      NOTJOB
&NM      TM    FLAG,X'80'
         BO    NEXREC
*        DO NOT PROCESS THIS RECORD TYPE IF JOB SELECTION ACTIVE ***
         SPACE
         MEND
         MACRO
&NAME    JESTYPE &N
         LA    R1,SMF&N.SBS
         L     R15,=A(SUBSYS)
         BALR  R14,R15
*              NAME OF SUBSYSTEM IS IN R0 AT THIS TIME
         SPACE
         MEND
         SPACE 1                                                GP12064
         COPY  OPTIONGB                                         GP12064
         SYSPARM LIST=YES                                       GP12064
         PUNCH ' ORDER SMFSCAN(P) '    ALIGN FOR DUMP READING   GP12064
         SPACE 2
SMFSCAN  PGMHEAD ZERO12,BASE=(R12,R11),PARM=R1                  GP12064
*
* THIS PROGRAM NEEDS 'SYS1.AMODGEN' AS PART OF SYSLIB
*
         SERVINIT ,          LOAD AND INITIALIZE SERVICES       GP12064
         SERVLOAD @INPREAD,@PRINTER                             GP12064
         SPACE 1
*DEBUG*  BANDAID INIT,SPIE *************************DEBUG*************
         OPEN  MF=(E,OCLIST)       OPEN THE TAPE FILE.          GP12064
         PRTOPEN SYSPRINT,OPT=(ABEND)                           GP12064
         INPOPEN SYSIN       OPEN THE INPUT FILE                GP12064
         PRTV  TITLE1,TITLE=1  MAKE A PAGE HEADER               GP12064
         PRTV  MSG1                                             GP12064
         MVC   QTIME,=A(2400*3600)  DEFAULT ALL DAY             GP12064
         LA    R1,LINEND-LINE                                   GP12064
         STH   R1,LINE       MAKE V-FORMAT HEADER               GP12064
         MVI   LINE+4,C' '                                      GP12064
         MVC   LINE+5(LINEND-LINE-5),LINE+4 CLEAR BUFFER        GP12064
         INPGET ,            GET A RECORD                       GP12064
         BXH   R15,R15,SYNEND     NO INPUT                      GP12064
         LR    R5,R1
         PACK  WORK,0(4,R5)
         PACK  WORK2,4(4,R5)
         CVB   R10,WORK
         MH    R10,=H'3600'
         ST    R10,STIME
         CVB   R10,WORK2
         MH    R10,=H'3600'
         ST    R10,QTIME
         PRTV  MSG2                                             GP12064
         PRTV  MSG3                                             GP12064
         PRTV  MSG3A                                  AXC       GP12064
         INPGET ,            GET A RECORD                       GP12064
         BXH   R15,R15,SYNEND     NO INPUT                      GP12064
         LR    R5,R1
NEXLOOP  CLI   0(R5),C'A'                                AXC
         BL    NEXEND                                    AXC
         CLC   0(4,R5),=C'TEST'
         BNE   NMBRS
         LA    R5,TEST
*
NMBRS    PACK  WORK,0(2,R5)
         CVB   R10,WORK
         LTR   R10,R10
         BZ    NEXEND
         LA    R9,HANDLERS(R10)
         MVI   0(R9),0       SKIP THIS RECORD                   GP12064
         LA    R5,3(,R5)
         B     NEXLOOP
         SPACE 2
NEXEND   PRTV  MSG4
         INPGET ,            GET A RECORD                       GP12064
         BXH   R15,R15,SYNEND     NO INPUT                      GP12064
         LR    R5,R1
*
*
NAMEND   PRTV  MSG5
         INPGET ,            GET A RECORD                       GP12064
         BXH   R15,R15,SYNEND     NO INPUT                      GP12064
         LR    R5,R1
         MVC   PGMNAME,0(R5)
         LA    R1,L'PGMNAME(R5)   SET STOPPER FOR NO MATCH      GP12064
         SR    R2,R2         CLEAR FOR FLAG                     GP12064
         LA    R15,L'PGMNAME      PRESET FOR MAXIMUM LENGTH     GP12064
         TRT   0(L'PGMNAME,R5),TRTNAME LOOK FOR STOPPER         GP12064
         B     *+4(R2)       BRANCH BY RESULT                   GP12064
           B   SETPLEN       NO WILD CARDS, NO BLANK            GP12064
           B   PGMLANK       STOPPED ON BLANK - GET LENGTH      GP12064
           B   PGMMASK       STOPPED ON MASK - GET LENGTH       GP12064
           SPACE 1
PGMLANK  LR    R14,R1        COPY STOP ADDRESS                  GP12064
         SR    R14,R5        LESS START                         GP12064
         BNP   SYNEND        NULL - NO SELECTION                GP12064
         B     SETPLEN       ELSE COMPARE FULL LENGTH (NO MASK) GP12064
           SPACE 1
PGMMASK  LR    R15,R1        COPY STOP ADDRESS                  GP12064
         SR    R15,R5        LESS START                         GP12064
         BNP   SYNEND        NULL - NO SELECTION                GP12064
*NEXT*   B     SETPLEN       ELSE COMPARE FULL LENGTH (NO MASK) GP12064
SETPLEN  BCTR  R15,0          MINUS ONE FOR EXECUTE CLC LENGTH  GP12064
         OI    FLAG,X'84'
         ST    R14,@JOBLEN                                      GP12064
*
*
SYNEND   INPCLOSE ,          CLOSE THE SYSIN FILE               GP12064
         PRTLIST TITLE2,TITLE=2   PRINT SUBHEADER               GP12064
         MVI   TRTNAME+C'*',0      CHANGE TO BLANK STOPPER ONLY GP12064
         B     NEXREC                                           GP12064
         SPACE 2
NEXRECP  PRTV  LINE          PRINT DATA LINE                    GP12064
NEXREC   GET   SMFTP               READ THE TAPE.
         USING SMFRCD0,R5          COMMON RECORD SEGMENT.
         LR    R5,R1               SAVE THE BUFFER ADDRESS.
*        CLC   SMF0SID,=C'H158'    CHECK FOR ACTUAL RECORD.
*        BNE   NEXREC              READ AGAIN IF NOT.
         SPACE 1
         SLR   R10,R10             CLEAR REG 10.                GP12064
         IC    R10,SMF0RTY         LOAD RECTYP.                 GP12064
         SLL   R10,2         CONVERT TO OFFSET                  GP12064
         LA    R10,RECCOUNT(R10)   POINT TO COUNTER             GP12064
         INC   0(,R10)       COUNT TYPE ON THIS CPU             GP12064
         SPACE
         CLI   SMF0RTY,X'02'       CHECK FOR HEADER REC.
         BE    NEXREC              IF SO, SKIP IT.
         CLI   SMF0RTY,X'03'       CHECK FOR TRAILER REC.
         BE    NEXREC              IF SO, SKIP IT.
         CLC   SMF0TME,STIME                                    GP12064
         BL    NEXREC
         CLC   SMF0TME,QTIME                                    GP12064
         BH    EOF
         MVC   TIME,SMF0TME
         SPACE 3
         SLR   R1,R1         CLEAR FOR IC                       GP12064
         IC    R1,SMF0RTY    GET RECTYP                         GP12064
         LA    R2,HANDLERS(R1)    POINT TO ROUTINE OFFSET       GP12064
         ICM   R1,1,0(R2)    IS THIS TYPE WANTED ?              GP12064
         BZ    NEXREC        NO                                 GP12064
         SLL   R1,1          OFFSET INTO ADDRESS SLOTS          GP12064
         LA    R2,SLOTS(R1)  POINT TO ROUTINE POINTER           GP12064
         MVC   WORK+2(2),0(R2)    COPY bddd                     GP12064
         MVC   WORK(2),=X'4190'   LA R9,bddd                    GP12064
         EX    0,WORK        LOAD R9                            GP12064
         BAL   R14,TIMED           CLEAR LINE; TIME STAMP       GP12064
         BR    R9            BRANCH TO PROCESSOR                GP12064
         SPACE 3
EOF      CLOSE MF=(E,OCLIST)       CLOSE THE TAPE FILE.         GP12064
         SPACE 1
         PRTL  ' Records read by type: '                        GP12064
         LA    R7,RECCOUNT   FIRST COUNTER                      GP12064
         SR    R3,R3         FIRST RECORD #                     GP12064
         LA    R4,1            COUNT INCREMENT                  GP12064
         LA    R5,255        LAST RECORD NUMBER                 GP12064
COUNTLUP ICM   R6,15,0(R7)   GET COUNT                          GP12064
         BZ    COUNTBMP        SKIP IF ZERO                     GP12064
         ST    R3,WORK       FOR PRTFD                          GP12064
         ST    R6,WORK+4     FOR PRTFD                          GP12064
         PRTLIST COUNTFD     FORMAT AND PRINT PARTIAL           GP12064
COUNTBMP LA    R7,4(,R7)     NEXT COUNTER                       GP12064
         BXLE  R3,R4,COUNTLUP  DO FOR ALL                       GP12064
         SPACE 1
PGMEXIT  SERVTERM ,                ....AND EXIT.                GP12064
         PGMEXIT RC=RETCODE                                     GP12064
         SPACE 2
TITLE2   FDPRT 'RECORDS SELECTED FROM'                          GP12064
         FDPRT STIME,TIME,PAD                                   GP12064
         FDPRT 'TO'                                             GP12064
         FDPRT QTIME,TIME,PAD                                   GP12064
         FDPRT *END                                             GP12064
         SPACE 2
COUNTFD  FDPRT WORK,4,I,RADJ,PAD,LEN=2                          GP12064
         FDPRT WORK+4,4,I,PAD,LEN=10                            GP12064
         FDPRT *END                                             GP12064
         SPACE 3
SUBSYS   LA    R0,BLANKS                                        GP12064
         CLC   0(2,R1),=XL2'0005'
         BNE   SUBSYSJ2
         L     R0,=CL4'JES3'
         BR    R14
SUBSYSJ2 CLC   0(2,R1),=XL2'0002'
         BNER  R14
         L     R0,=CL4'JES2'
         BR    R14
         EJECT
IPLREC   NOTJOB
         MVC   MESG(15),=C'SYSTEM WAS IPLD'
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMFRCD4,R5
STPREC   JOBCHEK SMF4JBN
         MVC   MESG(L'STPMSG1),STPMSG1
         MVC   MESG+16(8),SMF4JBN
         MVC   MESG+31(8),SMF4PGMN
         SR    R10,R10
         IC    R10,SMF4STN
         CVD   R10,WORK
         MVC   MESG+4(4),PAT1
         ED    MESG+4(4),WORK+6
         SR    10,10
         L     10,=A(SMF4PGNO-SMF4PGIN)
         LA    10,4(10)
         AH    10,SMF4RLCT
         AR    10,5
         LR    1,10
         LH    10,0(10)
         CVD   R10,WORK
         MVC   MESG+44(4),PAT1
         ED    MESG+44(4),WORK+6
         XC    WORK,WORK
         MVC   WORK+3(1),SMF4PRTY
         BAL   R14,CONV
         MVC   MESG+54(2),WORK2+2
*
*        SR    10,10
*        IC    10,SMF4PRTY
*        CVD   R10,WORK
*        MVC   MESG+53(4),PAT1
*        ED    MESG+53(4),WORK+6
*        CLI   0(1),X'03'
*        BH    STPOUT
*        CLI   SMF4PRTY,X'7E'
*        BL    STPOMIT
STPOUT   B     NEXRECP                                          GP12064
STPOMIT  B     NEXREC
STPMSG1 DC C'STEP XXX OF JOB XXXXXXXX PGM = XXXXXXXX PG = XXX DP =    '
         SPACE 1
         USING SMFRCD5,R5
JOBREC   JOBCHEK SMF5JBN
         MVC   MESG(22),=C'JOB XXXXXXXX HAS ENDED'
         MVC   MESG+4(8),SMF5JBN
         B     NEXRECP                                          GP12064
         SPACE 3
*FAILS*  USING SMF20HDR,R5                                      GP12064
         USING SMFRCD20,R5                                      GP12064
NUJOB    JOBCHEK SMF20JBN
         MVC   MESG(24),=C'JOB XXXXXXXX HAS STARTED'
         MVC   MESG+4(8),SMF20JBN
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMF6LEN,R5
SYSOUT   JOBCHEK SMF6JBN
         MVC   MESG(34),=C'JOB XXXXXXXX HAS SYSOUT IN CLASS X'
         MVC   MESG+33(1),SMF6OWC
         MVC   MESG+4(8),SMF6JBN
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMF7LEN,R5
LOSTD    MVC   MESG(13),=C'SMF DATA LOST'
         B     NEXRECP                                          GP12064
         SPACE 3
VONLINE  NOTJOB
         USING SMF9LEN,R5
         MVC   MESG(26),=C'UNIT XXX WAS VARIED ONLINE'
         MVC   WORK+2(2),SMF9DVAD+2
         BAL   R14,CONV
         MVC   MESG+5(3),WORK2+1
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMF10LEN,R5
ALLREC   JOBCHEK SMF10JBN
         MVC   MESG(42),=C'JOB XXXXXXXX IN ALLOC RECOVERY ON UNIT XXX'
         MVC   MESG+4(8),SMF10JBN
         MVC   WORK+2(2),SMF10DEV+2
         BAL   R14,CONV
         MVC   MESG+39(3),WORK2+1
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMF25LEN,R5
JESALC   JOBCHEK SMF25JBN
         CLC   SMF25NTF+2(2),=X'0000'
         BNE   CONT
         CLC   SMF25NTM+2(2),=X'0000'
         BE    NEXREC
CONT     MVC   MESG(42),=C'JOB XXXXXXXX SETUP XXX TAPES MOUNTED XXX  '
         MVC   MESG+4(8),SMF25JBN                 |         |       |
         MVC   WORK+2(2),SMF25NTF+2               23        33      42
         BAL   R14,CONV
         MVC   MESG+19(3),WORK2+1
         MVC   WORK+2(2),SMF25NTM+2
         BAL   R14,CONV
         MVC   MESG+37(3),WORK2+1
         B     NEXRECP                                          GP12064
         SPACE 3
VOFFLINE NOTJOB
         USING SMF11LEN,R5
         MVC   MESG(23),=C'UNIT XXX VARIED OFFLINE'
         MVC   WORK+2(2),SMF11DEV+2
         BAL   R14,CONV
         MVC   MESG+5(3),WORK2+1
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMF14LEN,R5
DASIN    JOBCHEK SMF14JBN
         TM    SMF14RIN,X'10'
         BO    NEXREC
         MVC   MESG(32),=C'JOB XXXXXXXX HAS READ FROM NAME='
         MVC   MESG+4(8),SMF14JBN
         MVC   MESG+32(44),SMFJFCB1
         B     NEXRECP                                          GP12064
         SPACE 3
DASOUT   TM    SMF14RIN,X'10'
         BO    NEXREC
         JOBCHEK SMF14JBN
         MVC   MESG(33),=C'JOB XXXXXXXX HAS WRITTEN TO NAME='
         MVC   MESG+4(8),SMF14JBN
         MVC   MESG+33(44),SMFJFCB1
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMF17LEN,R5
DASCR    JOBCHEK SMF17JBN
         MVC   MESG(27),=C'JOB XXXXXXXX HAS SCRATCHED '
         MVC   MESG+4(8),SMF17JBN
         MVC   MESG+27(44),SMF17DSN
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMF18LEN,R5
RENAME   JOBCHEK SMF18JBN
         MVC   MESG(22),=C'JOB XXXXXXXX, DATASET '
         MVC   MESG+4(8),SMF18JBN
         MVC   MESG+22(44),SMF18ODS
         LA    R10,MESG+22
         LA    R1,44(,R10)   STOPPER                            GP12064
         TRT   0(44,R10),TRTNAME                                GP12064
         MVC   0(12,R10),=C' WAS RENAMED'                       GP12064
         MVC   13(44,R10),SMF18NDS                              GP12064
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMF26LEN,R5
PURGE    JOBCHEK SMF26JBN
         MVC   MESG(28),=C'XXXXXXXX XXXXXXXX WAS PURGED'        GP12064
         MVC   MESG(8),SMF26JID
         MVC   MESG+9(8),SMF26JBN
         B     NEXRECP                                          GP12064
         SPACE 3
TIOCIN   NOTJOB
         MVC   MESG(15),=C'TSO WAS STARTED'
         B     NEXRECP                                          GP12064
         SPACE 3
         USING TIVRLEN,R5
TSOCOM   JOBCHEK TIVUIF
         MVC   MESG(6),=C'TSOCOM'
         MVC   MESG+7(8),TIVUIF
         MVC   MESG+16(8),TIVPRGNM
         MVC   MESG+25(8),TIVINVNM
         B     NEXRECP                                          GP12064
         SPACE 3
         USING TLGRLEN,R5
LOGOFF   JOBCHEK TLGUIF
         MVC   MESG(10),=C'TSO LOGOFF'
         MVC   MESG+11(8),TLGUIF
         B     NEXRECP                                          GP12064
         SPACE 3
JES2ST   NOTJOB
         USING SMF43LEN,R5
         MVC   MESG(12),=C'JES2 STARTED'
         JESTYPE 43
         STCM  R0,15,MESG                                       GP12064
         B     NEXRECP                                          GP12064
         SPACE 3
JESTOP   NOTJOB
         USING SMF45LEN,R5
         MVC   MESG(14),=C'JES2 WITHDRAWN'
         JESTYPE 45
         STCM  R0,15,MESG                                       GP12064
         B     NEXRECP                                          GP12064
         SPACE 3
RJEON    NOTJOB
         USING SMF47LEN,R5
         MVC   MESG(10),=C'RJE SIGNON'
         MVC   MESG+11(8),SMF47RMT
         MVC   MESG+20(8),SMF47LIN
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMF48LEN,R5
RJEOFF   NOTJOB
         MVC   MESG(11),=C'RJE SIGNOFF'
         MVC   MESG+12(16),SMF48RMT
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMF62LEN,R5
VSAMOPEN JOBCHEK SMF62JBN
         MVC   MESG(8),=C'VSAMOPEN'
         MVC   MESG+9(8),SMF62JBN
         MVC   MESG+18(44),SMF62DNM
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMF63LEN,R5
VSAMDEFN JOBCHEK SMF63JBN
         MVC   MESG(10),=C'VSAMDEFINE'
         MVC   MESG+11(8),SMF63JBN
         MVC   MESG+20(44),SMF63ENM
         LA    R10,MESG+20
         LA    R1,44(,R10)   STOPPER                            GP12064
         TRT   0(44,R10),TRTNAME                                GP12064
         MVC   0(12,R10),=C' IN CATALOG '                       GP12064
         MVC   13(44,R10),SMF63CNM                              GP12064
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMF67LEN,R5
VSAMDEL  JOBCHEK SMF67JBN
         MVC   MESG(10),=C'VSAMDELETE'
         MVC   MESG+11(8),SMF67JBN
         MVC   MESG+20(44),SMF67DEN
         LA    R10,MESG+20
         LA    R1,44(,R10)   STOPPER                            GP12064
         TRT   0(44,R10),TRTNAME                                GP12064
         MVC   0(12,R10),=C' IN CATALOG '                       GP12064
         MVC   13(44,R10),SMF67CNM                              GP12064
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SMF68LEN,R5
VSAMRNAM JOBCHEK SMF68JBN
         MVC   MESG(10),=C'VSAMRENAME'
         MVC   MESG+11(8),SMF68JBN
         MVC   MESG+20(44),SMF68ONM
         LA    R10,MESG+20
         LA    R1,44(,R10)   STOPPER                            GP12064
         TRT   0(44,R10),TRTNAME                                GP12064
         MVC   0(12,R10),=C' WAS RENAMED'                       GP12064
         MVC   13(44,R10),SMF68NNM
         B     NEXRECP                                          GP12064
         SPACE 3
         AIF   ('&LOCAL' NE 'AMOCO').OUTFGAS                    GP12064
         USING TMR,R5
STSREC   MVC   MESG(8),TMRJOB
         MVC   MESG+8(9),=C' SENT TO '
         MVC   MESG+17(8),TMRTDEST
         MVC   MESG+25(1),=C' '
         MVC   MESG+26(6),TMRFVOLS
         MVC   MESG+32(1),=C':'
         MVC   MESG+33(44),TMRFDSN
         B     NEXRECP                                          GP12064
         SPACE 3
         USING SAFTYP40,R5
DYNALOC  MVC   MESG(8),SAF40JBN
         TM    SAF40FUN,X'02'
         BE    USAF40
         MVC   MESG+8(11),=C' ALLOCATED '
         MVC   MESG+19(8),SAF40DDN
         MVC   MESG+27(1),=C' '
         MVC   MESG+28(6),SAF40UNT
         MVC   MESG+34(1),=C':'
         MVC   MESG+35(44),SAF40DSN
         B     PSAF40
USAF40   MVC   MESG+8(13),=C' UNALLOCATED '
         MVC   MESG+21(8),SAF40DDN
         MVC   MESG+29(1),=C' '
         MVC   MESG+30(6),SAF40UNT
         MVC   MESG+36(1),=C':'
         MVC   MESG+37(44),SAF40DSN
PSAF40   B     NEXRECP                                          GP12064
.OUTFGAS SPACE 3
TIMED    MVC   SYSID(4),SMF0SID-SMFRCD0(R5)
         L     R1,TIME       GET BINARY TIME 1/100 SECONDS      GP12064
         SR    R0,R0         CLEAR FOR DIVIDE                   GP12064
         D     R0,=F'100'    CONVERT TO SECONDS                 GP12064
         SR    R0,R0         CLEAR FOR DIVIDE                   GP12064
         D     R0,=F'60'     SECONDS / REST                     GP12064
         LR    R15,R0        SAVE SECS                          GP12064
         SR    R0,R0         CLEAR FOR DIVIDE                   GP12064
         D     R0,=F'60'     MINUTES / HOURS                    GP12064
         MH    R1,=H'100'    SCALE                              GP12064
         AR    R1,R0         ADD MINUTES                        GP12064
         MH    R1,=H'100'    SCALE                              GP12064
         AR    R1,R15        ADD SECONDS                        GP12064
         CVD   R1,WORK       CHANGE TO PACKED                   GP12064
         MVC   LTIME,PAT2
         ED    LTIME,WORK+4
         MVC   MESG(120),BLANKS
         BR    R14
         SPACE 3
*   CONVERT DATA IN WORK TO PRINTABLE HEX
*
CONV     UNPK  WORK2(5),WORK(5)   NASTY
         TR    WORK2(4),CHARS-C'0'
         BR    R14
         SPACE 1
         LTORG ,
         SPACE 2
HANDLERS DC    XL256'0'      NON-ZERO FOR ACTIVE SMF TYPE       GP12064
SLOTS    DC    H'0'          PROCESSOR POINTER LIST             GP12064
         HANDLE 00,IPLREC          0 IPL.                       GP12064
         HANDLE 02,NEXREC          2 DUMP HEADER.               GP12064
         HANDLE 03,NEXREC          3 DUMP TRAILER.              GP12064
         HANDLE 04,STPREC          4 STEP TERMINATION.          GP12064
         HANDLE 05,JOBREC          5 JOB TERMINATION.           GP12064
         HANDLE 06,SYSOUT          6 OUTPUT WRITER.             GP12064
         HANDLE 07,LOSTD           7 SMF DATA LOST.             GP12064
         HANDLE 08,NEXREC          8 I/O CONFIGURATION.         GP12064
         HANDLE 09,VONLINE         9 VARY ONLINE.               GP12064
         HANDLE 10,ALLREC          10 ALLOCATION RECOVERY.      GP12064
         HANDLE 11,VOFFLINE        11 VARY OFFLINE.             GP12064
         HANDLE 12,NEXREC          12 DOES NOT EXIST.           GP12064
*DEFER*  HANDLE 13,NEXREC          13 DOES NOT EXIST.           GP12064
         HANDLE 14,DASIN           14 INPUT DATA SET ACTIVITY.  GP12064
         HANDLE 15,DASOUT          15 OUTPUT DATA SET ACTIVITY. GP12064
*DEFER*  HANDLE 16,NEXREC          16 DOES NOT EXIST.           GP12064
         HANDLE 17,DASCR           17 SCRATCH DATA SET STATUS.  GP12064
         HANDLE 18,RENAME          18 RENAME DATA SET STATUS.   GP12064
         HANDLE 19,NEXREC          19 DIRECT-ACCESS VOLUME.     GP12064
         HANDLE 20,NUJOB           20 JOB COMMENCEMENT.         GP12064
*DEFER*  HANDLE 21,NEXREC          21 ERROR STATISTICS BY VOLUME.
*DEFER*  HANDLE 22,NEXREC          22 CONFIGURATION.            GP12064
         HANDLE 25,JESALC          25 JES3 DEVICE ALLOCATION.   GP12064
         HANDLE 26,PURGE           26 JES2 PURGE .              GP12064
         HANDLE 31,TIOCIN          31 TIOC INITIALIZATION.      GP12064
         HANDLE 34,TSOCOM          34 TS STEP TERMINATION.      GP12064
         HANDLE 35,LOGOFF          35 TS JOB TERMINATION (LOGOFF).
         HANDLE 43,JES2ST          43 JES2 START.               GP12064
         HANDLE 44,NEXREC          44 JES2 WITHDRAWAL.          GP12064
         HANDLE 45,JESTOP          45 JES2 WITHDRAWAL.          GP12064
         HANDLE 47,RJEON           47 RJE SIGNON.               GP12064
         HANDLE 48,RJEOFF          48 RJE SIGNOFF.              GP12064
         HANDLE 49,NEXREC          49 JES2 INTEGRITY.           GP12064
         HANDLE 62,VSAMOPEN        62 VSAM CLUSTER OR COMP OPENED.
         HANDLE 63,VSAMDEFN        63 VSAM CLUSTER OR COMP CATLGD.
         HANDLE 64,NEXREC          64 VSAM COMPONENT STATUS.    GP12064
         HANDLE 67,VSAMDEL         67 VSAM ENTRY DELETED.       GP12064
         HANDLE 68,VSAMRNAM        68 VSAM ENTRY RENAMED.       GP12064
         HANDLE 69,NEXREC          69 VSAM DATA SPC DEFD OR DELD.
         HANDLE 70,NEXREC          70 CPU ACTIVITY.             GP12064
         HANDLE 71,NEXREC          71 PAGING ACTIVITY.          GP12064
         HANDLE 72,NEXREC          72 WORKLOAD ACTIVITY.        GP12064
         HANDLE 73,NEXREC          73 CHANNEL ACTIVITY.         GP12064
         HANDLE 74,NEXREC          74 DEVICE ACTIVITY.          GP12064
         HANDLE 75,NEXREC          75 PAG/SWAP DASET ACTIVITY.  GP12064
         HANDLE 76,NEXREC          76 TRACE ACTIVITY.           GP12064
         HANDLE 77,NEXREC          77 ENQUEUE ACTIVITY.         GP12064
         HANDLE 79,NEXREC          79 MONITOR II ACTIVITY.      GP12064
         AIF   ('&LOCAL' NE 'AMOCO').SKIPS                      GP12064
         HANDLE 203,NEXREC         203 STEP INITILIZATION.      GP12064
         HANDLE 204,NEXREC         204 STEP TERMINATION.        GP12064
         HANDLE 205,NEXREC         205 SECURITY RECORD.         GP12064
*        HANDLE 206,STSREC         206 STSTMR FOR WRITERS.      GP12064
         HANDLE 207,NEXREC         207 VOLTS ERROR.             GP12064
         HANDLE 222,NEXREC         222 COMBINED JOB.            GP12064
         HANDLE 225,NEXREC         225 JOB ALLOCATION.          GP12064
         HANDLE 229,NEXREC         229 COMBINED STEP.           GP12064
         HANDLE 231,NEXREC         231 COMBINED JOB PURGE.      GP12064
         HANDLE 234,NEXREC         234 TSO TRANSACTION.         GP12064
*        HANDLE 240,DYNALOC        240 DYNAMIC ALLOCATION.      GP12064
         HANDLE 253,NEXREC         253 MEMIC (WAITING).         GP12064
         HANDLE 254,NEXREC         254 SOCRATES DATA.           GP12064
         HANDLE 255,NEXREC         255 LOOKER DATA.             GP12064
.SKIPS   SPACE 3                                                GP12064
@JOB     CLC   JOBNAME(0),0(R1)
@PGM     CLC   PGMNAME(0),0(R1)
NEXAD    DC    S(NEXREC)
PAT1     DC    X'40202120'
PAT2     DC    X'402021207A20207A2020'                          GP12064
CHARS    DC    C'0123456789ABCDEF'
MSG1     VCON  ' ENTER TIMES, SSSSEEEE  (MILITARY TIME FORMAT)' GP12064
MSG2     VCON  END=MSG2ND                                       GP12064
         DC    C' RECS PROC''D ARE:0,4-7,9-11,14,15,'
         DC    C'17,18,20,25,26,31,34,35,43,45,47,48,'
         DC    C'62,63,67,68'
MSG2ND   VCON  *END
TITLE1   VCON  '#                   SMF SCAN'                   GP12064
BLANKS   DC    CL120' '
MSG3     VCON  ' TO SUPPRESS RECS ENTER NUMBERS SEP''D BY BLANKS' 12064
MSG3A    VCON  ' OR ENTER A SINGLE BLANK FOR NO SUPRESSION'     GP12064
MSG4     VCON  ' ENTER A SPECIFIC JOBNAME OR A SINGLE BLANK'    GP12064
MSG5     VCON  ' ENTER A SPECIFIC PROGRAM NAME OR A SINGLE BLANK' 12064
SYSIN    INPWORK SYSIN,EODAD=SYNEND,EDIT=128                    GP12064
SYSPRINT PRTWORK SYSPRINT,TITLE=4                               GP12064
OCLIST   OPEN  (SMFTP,INPUT),MF=L                               GP12064
SMFTP    DCB   DDNAME=SMFTPE,DSORG=PS,MACRF=GL,EODAD=EOF,              X
               BFTEK=A,BLKSIZE=32756,LRECL=32756,RECFM=VBS
         SPACE 3
         DC    C'0 4 5 6 7 9 10 11 14 15 17 18 20 26 31 34 35 43 45 47'X
               '48 62 63 67 68'
TEST     DC    C'04 04 05 06 07 09 10 11 14 15 17 18 20 26 31 34 35 43'X
               '45 47 48 62 63 67 68                                  '
         SPACE 1
TRTNAME  TRENT FILL=0        DEFINE TABLE - ALL VALID           GP12064
         TRENT TRTNAME,4,C' ',    STOP ON BLANK                 GP12064
         TRENT ,8,C'*'       TERMINAL WILD CARD                 GP12064
         SPACE 1
SAVE     DSECT ,             DYNAMIC STORAGE
WORK     DS    D
WORK2    DS    D
WORK3    DS    D
TIME     DS    F
STIME    DS    F
QTIME    DS    F
JOBNAME  DS    CL8
PGMNAME  DS    CL8
         SERVDEFS ,                                             GP12064
LINE     VCON  ' ',END=LINEND                                   GP12064
SYSID    DS    CL4
LTIME    DS    CL10
         DC    CL2' '
MESG     DS    CL120
         VCON  *END                                             GP12064
         DS    CL44          ALLOW FOR OVERRUN OF MSG           GP12064
         SPACE
@JOBLEN  DS    F                                                GP12064
@PGMLEN  DS    F                                                GP12064
FLAG     DC    X'00'
         SPACE 1
RECCOUNT DS    256F'0'       COUNT RECORDS READ                 GP12064
SAVEEND  EQU   *
         SPACE 1
RECDEF   DSECT
RECS     IFASMFR (0,4,5,6,7,9,10,11,14,17,18,20,25,26,31,34,35)
         IFASMFR (43,45,47,48,62,63,64,67,68,69)
*        IFASAVR ALL,DSECT=YES,BITS=YES
         END
