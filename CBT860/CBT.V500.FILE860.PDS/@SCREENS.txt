@SCR     TITLE '@ S C R E E N S  ***  FULL-SCREEN FORMATTING'
         MACRO ,                                                 82102
&NM      COBAL &FUN                                              82102
&NM      L     R14,=A(CONBASE) BASE FOR CONVERSION FUNCTIONS
         BALS  R14,&FUN-CONBASE(,R14)  INVOKE FUNCTION           82102
         MEND  ,                                                 82102
         SPACE 1
         MACRO ,                                                 82102
&NM      CONENT ,            CONVERSION ENTRY CODE               82102
&NM      STM   R14,R12,SAVE14   SAVE CALLER'S REGISTERS          82102
         BASR  R11,0         GET LOCAL ADDRESSABILITY            87307
         L     R11,BASECON-*(R11)   SET LOCAL BASE               87307
         MEND  ,                                                 82102
         SPACE 1
         MACRO ,
&NM      SCHEAD &SAP=
&NM      BASR  R12,0         MAKE LOCAL BASE                    GP99026
         PUSH  USING                                            GP97231
         USING *,R12         DECLARE IT
         MEND  ,
         SPACE 1
         MACRO ,
&NM      SCEXIT ,
&NM      O     R0,RETCODE                                        87315
         ST    R0,RETCODE    SET NEW RETURN CODE                 87315
         L     R12,=A(@SCREENS)  GET MAIN BASE BACK
         B     RECURSE-@SCREENS(,R12)
         MEND  ,
         SPACE 1
         MACRO ,
&NM      LADD  &R,&A
         LCLA  &I
&I       SETA  &SYSNDX
         AIF   ('&A'(1,1) EQ '(').REGFORM
&NM      TM    &A,X'C0'      2*6 OR 14 BIT ADDRESS
         BZ    Z@&I.L
         IC    &R,1+&A
         SLL   &R,2
         ICM   &R,2,&A
         SRL   &R,2
         N     &R,=X'00000FFF'
         B     Z@&I.X
Z@&I.L   ICM   &R,3,&A
         AGO   .COMMON
.REGFORM ANOP  ,
&NM      TM    0&A,X'C0'     2*6 OR 14 BIT ADDRESS
         BZ    Z@&I.L
         IC    &R,1&A
         SLL   &R,2
         ICM   &R,2,0&A
         SRL   &R,2
         N     &R,=X'00000FFF'
         B     Z@&I.X
Z@&I.L   ICM   &R,3,0&A
.COMMON  N     &R,=X'00003FFF'
Z@&I.X   DS    0H
         MEND  ,
         SPACE 1
         MACRO ,
&NM      STADD &R,&A,&DB=DB
         LCLA  &I
&I       SETA  &SYSNDX
         STH   &R,&DB
         TM    &DB,X'F0'
         BNZ   Z@&I.L
         STH   &R,&DB+2
         STC   &R,&DB+1
         SLL   &R,2
         STCM  &R,2,&DB
         LH    &R,&DB+2
         NC    &DB.(2),=X'3F3F'
         TR    &DB.(2),TRADDR
         AIF   ('&A' EQ '').USER
         AIF   ('&A'(1,1) EQ '(').REGFORM
Z@&I.L   MVC   &A.(2),&DB
         MEXIT ,
.USER    ANOP  ,
Z@&I.L   DS    0H
         MEXIT ,
.REGFORM ANOP  ,
Z@&I.L   MVC   0(2,&R(1)),&DB
.MEND    MEND  ,
         SPACE 2                                                 82102
         COPY  OPTIONGB                                          81159
         SPACE 2                                                 82102
         LCLC  &DEFUCS       DEFAULT UCS ID                      81159
         SPACE 1                                                 82102
         SYSPARM LIST=YES    MERGE AND SHOW OPTIONS              81159
         EJECT ,
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1981  EXPERT SYSTEM PROGRAMMING INC.               *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1
*        FULL-SCREEN SERVICE ROUTINE
*          TO USE WITH MACROS, LOAD EP=@SCREENS / ST R0,@SCREENS
*          ALSO NOTE THE LPALD OPTION OF THE SERVCALL MACRO.     82102
*
*        FUNCTIONS :
*
* &NM    SCRWORK  DDNAME,ALTDDNM,WIDTH=,LPP=,TITLE=,FOOTER=,RECFM=
*         DEFINITION OF SCREENS WORK AREA. ASSOCIATION BETWEEN A WORK
*         AREA AND A 'DEVICE NUMBER' (1-8) IS MADE AT OPEN TIME.
*
* &NM    SCROPEN  WORK,DEV=N   OPENS PRINT/PUNCH FILE 'N'
*
* &NM    SCRCLOSE DEV=N OR DEV=ALL  CLOSES FILE 'N' OR ALL FILES
*
* &NM    SCRLIST LISTAD,DEV=  PROCESSES A LIST OF REQUESTS SPECIFIED
*          BY FDXXX DEFINITIONS.
*                                                                83331
         PRINT &PRTSOR
@SCREENS CSECT ,                                                 82031
         SPACE 1
MAXWIDTH EQU   132           MAXIMUM TERMINAL WIDTH TO SUPPORT
MAXWAIT  EQU   10*60         MAXIMUM INPUT WAIT TIME (IN SECONDS)
DEFUNPRT EQU   C'_'          USE UNDERLINE FOR UNPRINTABLES
DEFLPP   EQU   24            DEFAULT LPP
         SPACE 1
         USING @SCREENS,R15                                      82031
         ENTRY SCRNTEST      ****DEBUG*****                      83331
SCRNTEST B     SCREENR       SKIP ID                             82031
         DC    AL1(L'SCRNSID)   LENGTH FOR SNAP                  82031
SCRNSID  DC    C'@SCREENS - &SYSDATE'                            82031
SCREENR  BSM   R14,0         SAVE CALLER'S AMODE                GP99026
         STM   R14,R12,12(R13)                                   82031
         LR    R12,R15       SET PRIMARY BASE REGISTER           82102
         DROP  R15                                               82031
         USING @SCREENS,R12                                      82102
         AM24  WORK=R15      OLD-FASHIONED CODE                 GP99026
         SPACE 1                                                 82031
*        LOCATE OR ESTABLISH WORK AREA                           82031
*                                                                82031
         ICM   R4,15,SCRNSID+1  ID FIELD                         82031
         BALS  R9,LOOKFSA    FIND THE FSA ENTRY                  82031
         B     CURSTEST      FOUND IT, BUT CHECK RECURSION       84176
         LTCB  R5            GET OUR TCB ADDRESS BACK            87310
         LA    R3,FSWLEN     GET LENGTH                          82031
         BALS  R9,GETFSA     GET SPACE FOR PERMANENT WORK AREA   82031
         USING SCR0WORK,R8                                       82031
*        L     R7,TCBFSA-TCB(,R5)  CHAIN AS FIRST ENTRY          82031
         MVC   FSWLINK,FSWLINK-SCR0WORK(R7)  CHAIN OLD AREA      82031
         ST    R8,FSWLINK-SCR0WORK(,R7)  CHAIN THIS AHEAD OF OLD ONE
         USING SCR0WORK,R8                                       82031
         SLR   R6,R6         CLEAR MASTER WORK AREA POINTER      84176
         B     PRTPREP       PREPARE COMMON PREFIX ENTRIES       84176
CURSTEST TM    USEXFLAG,USXFACT  CALLED FROM USER EXIT ?         84176
         BZ    PRTHAVE       NO; USE FIXED AREA                  84176
         LR    R5,R3         SAVE TCB ADDRESS                    84176
         LA    R3,FSWLEN     SET LENGTH                          84176
         LR    R6,R8         SAVE OLD ENTRY                      84176
         BALS  R9,GETFSA     GET ANOTHER AREA                    84176
PRTPREP  ST    R6,USEX@WRK   SAVE MASTER WORK ADDRESS            84176
         MVI   USEXFLAG,0                                        84176
         ST    R5,FSWTCB     SAVE TCB ADDRESS                    84176
PRTHAVE  LR    R10,R13       COPY USER'S SAVE AREA ADDRESS       82031
         LA    R13,SAVE       AND ESTABLISH OURS                 82031
         DROP  R8            FSA HEADER NO LONGER ACCESSIBLE    GP97231
         USING SAVE,R13                                         GP97231
         ST    R10,SAVE13-SAVE(,R13)  MAKE BACK CHAIN            82031
         ST    R13,SAVEFWD-SAVE(,R10)    AND FORWARD CHAIN       82031
         XC    SCR0CLR(SCR0CLRL),SCR0CLR  CLEAR MOST OF GOTTEN AREA
         MVC   CALLPARM(4*13),SAVE0-SAVE(R10)  COPY R0 TO R12
         MVC   CALLPARM+4*13(4),SAVE13-SAVE(R13)  USER'S R13
         MVC   CALLPARM+4*14(8),SAVE14-SAVE(R10)  USER'S R14-R15
         CLI   CALLDEV,0     USER SPECIFIED DEVICE NUMBERS ?
         BNE   RECURSED      YES
         MVI   CALLDEV,1     ELSE DEFAULT TO FILE 1
         B     RECURSED      SKIP TEST                           82102
         SPACE 1
RECURSE  CLI   CALLDEV,0     ALL REQUESTS DONE ?
         BE    EXITSET       YES; QUIT
RECURSED LA    R1,1          SET TM BIT
         LA    R2,8          TEST 8 BITS
LOOPPW   STC   R1,SCR0OMSK   SET MASK BIT                        82031
         STC   R1,DB         SET FOR LOOKPW                      82031
         NC    SCR0OMSK,CALLDEV  IN USER'S REQUEST               82031
         BNZ   LOOKPW        ON - FIND ASSOCIATED WORK AREA
BUMPPW   SLL   R1,1          SET NEXT BIT
         BCT   R2,LOOPPW     TRY AGAIN
EXITSET  ICM   R15,15,IEFSD095  BIG LETTER ROUTINE LOADED ?     GP99026
         BZ    EXITSETD      NO                                  81159
         DELETE EP=IEFSD095  DELETE IT
EXITSETD L     R7,RETCODE    GET RETURN CODE                     87315
         XC    RETCODE,RETCODE  CLEAR FOR NEXT CALL              93361
         LR    R8,R13        SAVE OLD SAVE AREA                  82031
EXITSETT L     R13,SAVE13-SAVE(,R13)  RESTORE OLD ONE            84176
         ICM   R15,15,USEX@WRK-SAVE(R8)  RECURSION RETURN ?      84176
         BZ    EXITSETO      NO                                  84176
         MVC   SCR0OPEN-SCR0WORK(2,R15),SCR0OPEN-SAVE(R8)  SAVE OFLGS
         B     EXITSETF      FORCE FREE GETMAINED SAVE AREA      84176
EXITSETO CLI   SCR0OPEN-SAVE(R8),0    ANY DCBS OPEN ?            84176
         BNE   EXITFREE      YES; DON'T FREE WORK AREA           82031
EXITSETF SH    R8,=Y(SAVE-SCR0WORK)  GET PREFIX BACK             84176
         L     R3,FSWTCB-SCR0WORK(,R8)  SET TCB ADDRESS          82031
         BALS  R9,FREEFSA    UNCHAIN AND FREE AREA               82031
EXITFREE LR    R15,R7        COPY RETURN CODE                    82031
EXITFREF LM    R0,R12,SAVE0-SAVE(R13)  RELOAD REGISTERS          82031
         L     R14,SAVE14-SAVE(,R13)  RESTORE RETURN             82031
         LTR   R15,R15       SET CONDITION CODE                  87314
         BSM   0,R14         RETURN TO CALLER                   GP99026
         SPACE 1
EXIT16   MVI   RETCC,16      SET DISASTER
         B     EXITSET       AND QUIT
         SPACE 1
         USING SCRNWORK,R8   DECLARE DYNAMIC PRINT WORK AREA
LOOKPW   XI    DB,X'FF'      COMPLEMENT IT                       81273
         NC    CALLDEV,DB    AND COMPLIMENT IT                   81273
         LA    R4,9
         SR    R4,R2         GET RELATIVE DEVICE NUMBER
         O     R4,=C'SCR0'   MAKE PW ID FIELD NAME
         BALS  R9,LOOKFSA    FIND THE WORK AREA                  82031
         B     BRANCHPW      FOUND; USE IT                       82031
         USING TCB,R3
BUMPTCB  CL    R3,TCBJSTCB   TOP OF TREE ?                      GP99026
         BE    NONEPW        YES; SEE IF OPEN
         ICM   R3,15,TCBOTC  GET PARENT                         GP99026
         BZ    NONEPW        NONE ?                              82031
         BALS  R9,LOOTFSA    TRY AGAIN                           82031
         B     BRANCHPW      FOUND; USE IT                       82031
         B     BUMPTCB       ELSE TRY AGAIN                      82031
         DROP  R3                                                82031
NONEPW   SLR   R8,R8         JUST IN CASE
         CLI   CALLFUN,2     OPEN ?
         BE    BRANCHPW      YES; PROCESS
SET4     OI    RETCC,RETC4   SET MINOR ERROR - NOT OPEN
         B     RECURSE       SEE IF MORE
         SPACE 1
BRANCHPW IC    R0,CALLFUN    GET REQUESTED FUNCTION              81193
         L     R10,CALLR1    LOAD USER'S PARAMETER
         USING FDW,R10       DECLARE IT
         BIX   ERR=EXIT16,             BRANCH BY INDEX OFFSET          *
               LOC=(SCCLOSE,,SCOPEN,SCINIT,SCLIST,SCANAL,SCMOVE,SCPAGE,*
               SCLOOP,SCSCAN,SCMARK,SCEDIT,SCITEM) JUMP TO REQUEST
         SPACE 1                                                 82031
LOOKFSA  LTCB  R3            GET THE TCB                         86288
LOOTFSA  LA    R7,TCBFSA-TCB(,R3)  POINT TO CHAIN HEAD           87311
LOOPFSA  ICM   R8,15,FSWLINK-SCR0WORK(R7)  GET NEXT ENTRY       GP99026
         BZ    4(,R9)        EXIT; NO ENTRY FOUND                82031
         USING SCR0WORK,R8   DECLARE IT                          82031
         CL    R4,FSWID      MATCH ?                             82031
         BNE   LOORFSA       NO; TRY SOME MORE                   86288
         CLI   CALLFUN,2     OPEN ?                              86288
         BNER  R9            NO; RETURN FOUND                    86288
*        THIS INSERTION WAS MADE TO HANDLE MULTI-TASKING WITH    86288
*        INDEPENDENT PROGRAMS.  THE SAME DEVICE NUMBER MAY BE    86288
*        USED MORE THAN ONCE FOR DISTINCT DDNAMES, E.G., WHEN    86288
*        COPYVOL ATTACHES WYLCOPY.                               86288
         LTCB  R15           GET MY TCB BACK                     86288
         C     R15,FSWTCB    SAME TCB ?                          86288
         BER   R9            YES; MUST ACCEPT MATCH              86288
         SR    R15,R15       JUST IN CASE                       GP99026
         ICM   R15,7,1+CALLR1    LOAD THE USER'S AREA            86288
         BZ    LOORFSA       INVALID; IGNORE TO FORCE OPEN ERROR 86288
         USING SCRNWORK,R8                                       86288
         USING SCRMAP,R15    MAP USER'S AREA
         CLC   SWDDNAM,SUDDNAM  MATCH ?                          86288
         BER   R9            YES; PROCESS                        86288
         CLC   SWDDNAM,SUDDALT  ALTERNATE ?                      86288
         BER   R9                                                86288
         CLI   SWDDNAM,C'*'  CONSOLE OR TPUT ?                   86288
         BNE   LOORFSA       NO                                  86288
         CLC   SWDDNAM(2),SUDDNAM  MATCH ?                       86288
         BER   R9            YES                                 86288
         CLC   SWDDNAM(2),SUDDALT                                86288
         BER   R9            YES                                 86288
LOORFSA  LR    R7,R8                                             82031
         B     LOOPFSA       TRY AGAIN                           82031
         DROP  R15                                               86288
         USING SCR0WORK,R8   RESTORE BASE WORK AREA              86288
         SPACE 1                                                 82031
GETFSA   GETMAIN R,LV=(R3)   GET THE WORK AREA                   82031
         LR    R8,R1         SAVE THE ADDRESS                    82031
         LR    R0,R1         SET ADDRESS FOR CLEAR               82031
         LR    R1,R3         COPY THE LENGTH                     82031
         SLR   R14,R14       CLEAR ADDRESS                       82031
         SLR   R15,R15       FROM LENGTH                         82031
         MVCL  R0,R14        CLEAR THE AREA                      82031
         ST    R4,FSWID      SET THE CONTROL BLOCK ID FIELD      82031
         ST    R3,FSWSPLEN   SET THE SUBPOOL/LENGTH FIELD        82031
         BR    R9            RETURN TO CALLER                    82031
         USING SCRNWORK,R8   SET FOR DEVICE WORK AREA            82031
         SPACE 1                                                 82031
FREEFSA  L     R2,TCBFSA-TCB(,R3)  GET HIGH-LEVEL SAVE AREA      82102
FREEFSAL CL    R8,0(,R2)     THIS ONE ?                         GP99026
         BE    FREEFSAU      YES; UNCHAIN
         ICM   R2,15,0(R2)   GET NEXT                           GP99026
         BNZ   FREEFSAL      TRY AGAIN
         B     FREEFSAM      ELSE FREE
FREEFSAU MVC   0(4,R2),SWLINK  UNCHAIN THIS ONE
FREEFSAM L     R0,SWSPLEN    GET GETMAIN LENGTH/SUBPOOL
         FREEMAIN R,LV=(0),A=(R8)
         BR    R9            RETURN TO CALLER                    82031
         SPACE 1                                                GP99026
         LTORG ,                                                GP99026
         EJECT
*        OPEN PROCESSING: R1 POINTS TO WORK AREA CREATED BY SCRWORK
*          MACRO.  IF PRIMARY DDNAME BEGINS WITH A PERCENT SIGN,
*          2ND WORD OF NAME IS IN-STORAGE PUT ADDRESS (NO I/O DONE).
*          REGISTER 0 MUST SPECIFY ONE DEVICE NUMBER ONLY.
*        RETURN CODES :
*        R15=0  REQUEST COMPLETED
*        R15=4  REQUEST COMPLETED, BUT ALTERNATE DDNAME USED
*        R15=8  DD DUMMY; ALL CALLS WILL BE IGNORED.
*        R15=12 NO USABLE DD FOUND; CALLS WILL RETURN WITH ERRORS.
*
*        WITH R15=8, A WTO INDICATED THE MISSING DDNAME(S) WILL BE
*          ISSUED UNLESS THE SCROPEN REQUESTED 'NOWTO'
*        IF SCROPEN SPECIFIED 'ABEND', RETURN CODE 12 WILL NOT BE
*          ISSUED, INSTEAD AN ABEND 3270 IS TAKEN.
*        IF SCROPEN SPECIFIED ABEND AND NOT 'DUMMY', RETURN CODE
*          WILL NOT BE ISSUED, BUT AN ABEND IS TAKEN.
*
         USING FDW,R10       DECLARE IT
         USING SCRMAP,R7     DECLARE MAPPING OF USER AREA
         PUSH  USING                                            GP97231
         ORG   @SCREENS+4096  PREVENT HLASM LEVEL 4 ERROR       GP97231
SCOPEN   BASR  R11,0         MAKE LOCAL BASE                    GP99026
         USING *,R11         LOCAL BASE REGISTER
         LTR   R8,R8         WAS THIS OPENED ALREADY ?
         BNZ   EXIT12        YES; SET ERROR
         CLI   CALLDEV,0     MORE THAN ONE DEVICE SPECIFIED ?
         BNE   EXIT12        YES; ALSO ERROR
         SR    R7,R7         JUST IN CASE                       GP99026
         ICM   R7,7,CALLR1+1   ANY WORK AREA ?
         BZ    EXIT12        NO; ERROR
         MVC   DB,SUDDNAM    TEMPORARY SAVE AREA FOR USER'S DD NAME
         L     R15,PSAAOLD-PSA  GET ASCB ADDRESS
         ICM   R15,15,ASCBTSB-ASCB(R15)  RUNNING UNDER TSO ?
         BNZ   POPTFAKE      YES; NO NEED FOR DD CHECK
         CLI   SUDDNAM,C'%'  SPECIAL REQUEST ?
         BE    POPTFAKE      YES; SKIP ALTERNATE TEST
         CLI   SUDDALT,C'%'  SPECIAL ALTERNATE ?
         BE    POPPFAKE      YES; TEST PRIMARY FIRST
POPPFAKE DEVTYPE SUDDNAM,DB  DD CARD AVAILABLE ?
         BXH   R15,R15,POPPFAKA  NO; USE ALTERNATE
         CLI   DB+2,0        DD DUMMY ?
         BE    POPPFAKA      YES; USE ALTERNATE
         MVC   DB,SUDDNAM    RESTORE PRIMARY NAME
         B     POPTFAKE
POPPFAKA TM    SUPRFG,SUFVTAM  VTAM MODE ?
         BNZ   POPTFAKE      YES; NO ALTERNATE NAME
         MVC   DB,SUDDALT    SET ALTERNATE NAME
POPTFAKE LA    R0,7          SET MAX SUPPORTED TITLE/FOOTER COUNT
         SLR   R14,R14
         SLR   R15,R15
         IC    R14,SUTIT#    GET NUMBER OF HEADERS
         CR    R14,R0          LEGAL ?
         BNH   *+6
         LR    R14,R0          NO; USE MAX
         IC    R15,SUFOOT#   AND FOOTERS
         CR    R15,R0          LEGAL ?
         BNH   *+6
         LR    R15,R0          NO; USE MAX
*TEMP*   LA    R15,1(R14,R15)   ADD ONE FOR UNDERLINE AREA
         LA    R15,14        SET FOR MAXIMUM NUMBER
         MH    R15,=Y(L'SWTIT1)  TIMES LENGTH
POPPU1   L     R3,=A(SWSIZE+SWBUFND-SWCBUF)  GET LENGTH OF WORK AREA
         ALR   R3,R15        GET GETMAIN SIZE REQUIRED
         BALS  R9,GETFSA     GET STORAGE FOR WORK AREA          GP99026
         ST    R7,SWPU@      SAVE USER'S LIST ADDRESS
         MVC   SWDCB(PATEND-PATDCB),PATDCB  INITIALIZE DCB
         MVC   DCBDDNAM-IHADCB+SWDCB(8),DB  SET THE DDNAME
         MVC   SWDDNAM,DB    DITTO
         LA    R1,SWDCB      GET DCB ADDRESS
         ST    R1,SWDCB@     STASH THE ADDRESS
         LTCB  R3            GET MY TCB BACK
         ST    R3,SWTCB      SAVE IT
         MVI   SWDCB@,X'BF'  MAKE OPEN/OUTPUT/LEAVE LIST FORM
*        LA    R1,SWEXLIST
*        STCM  R1,7,DCBEXLST+1-IHADCB+SWDCB  SET EXITLIST ADDR
         LH    R1,=Y(SWOBUF-SCRNWORK)
         AR    R1,R8         OUTPUT BUFFER ADDRESS
         ST    R1,SW@OBUF
         AH    R1,=Y(SWIBUF-SWOBUF)  POINT TO INPUT BUFFER
         ST    R1,SW@IBUF
         SLR   R1,R1         FOR ICM
         ICM   R1,1,SUFILL   USER SPECIFIED FILL BYTE ?
         BNZ   *+8           YES
         LA    R1,DEFUNPRT   ELSE USE ASSEMBLED DEFAULT
         STC   R1,SWFILL     SAVE IT
         MVC   SWFLAG,SUPRFG  MOVE USER'S OPEN LIST OPTIONS
         NI    SWFLAG,SWFVTAM+SUPGPAGE+SUPUPAGE+SUPGXLST  OPTIONS
         CLI   SWDDNAM,C'%'  REQUEST FOR STORAGE ACCESS METHOD ?
         BNE   POPNFAKE
         OI    SWFLAG,SWFAKE   SET FAKE ACCESS METHOD
         B     POPNCONS      RESUME STANDARD CODE
POPNFAKE L     R15,PSAAOLD-PSA  GET ASCB ADDRESS
         ICM   R15,15,ASCBTSB-ASCB(R15)  RUNNING UNDER TSO ?
         BZ    POPNCONS      NO
         OI    SWFLAG,SWFTSU FORCE TSO MODE
         LA    R5,SWATTR     RESULT ADDRESS                      87315
         LA    R2,SWPRSZ     PRIMARY SIZE                        87315
         LA    R3,SWALSZ     ALTERNATE SIZE                      87315
         GTTERM PRMSZE=(R2),ALTSZE=(R3),ATTRIB=(R5),MF=(E,DB)    87315
         TM    SWATTR+3,1    TPG SUPPORTED ?                     87315
         BZ    POPTSFSC      NO; JUST GET OUT                    87317
         XC    DB(16),DB     JUST IN CASE...                     87315
         TPG   WSFQREP,L'WSFQREP,MF=(E,DB)  WSF QUERY REPLY      87315
         BXH   R15,R15,POPTSFSC  NOT ACCEPTED                    87317
         XC    SWQUEREP,SWQUEREP  CLEAR INPUT                    87315
         TGET  SWQUEREP,L'SWQUEREP-4,ASIS  WAIT AND GET RESPONSE 87315
         LA    R14,SWQUEREP  GET RESPONSE FIELD                  87315
         CLI   0(R14),X'88'  QUERY RESPONSE ?                    87315
         BNE   POPTSFSC      NO                                  87317
         LA    R14,1(,R14)   POINT PAST 88 AID                   87315
         CLI   2(R14),X'81'  IS IT KOSHER ?                      92308
         BE    QUERLOOP      YES                                 92308
         CLI   6(R14),X'81'  SKEWED ?                            92308
         BNE   POPTSFSC      NO; GET OUT DUE TO ERROR            92308
         LA    R14,4(,R14)   NEW WITH TSO/E ?                    92308
QUERLOOP ICM   R15,3,0(R14)  GET LENGTH OF NEXT FIELD            87315
         BNZ   QUERLOOK      OK                                  87315
         CLC   =X'8187',2(R14)  HIGH-LIGHTING ?                  87315
         BNE   QUERLOOK      NO; LAST FIELD                      87315
         LA    R15,13        SET NORMAL LENGTH                   87315
QUERLOOK CLC   =X'8186',2(R14)  COLOR DEFINITION ?               87315
         BE    QUERCOL       YES                                 87315
         CLC   =X'8187',2(R14)  HIGH-LIGHTING ?                  87315
         BNE   QUERSKIP      NO                                  87315
         SLR   R2,R2         CLEAR COUNTER                       87315
         LA    R0,3          THREE OPTIONS                       87315
         LA    R1,7(,R14)    POINT TO FIRST ENTRY                87315
QUERHIGH CLI   0(R1),0       OPTION PRESENT ?                    87315
         BE    QUERHICK      NO                                  87315
         CLI   1(R1),0       OPTION SUPPORTED ?                  87315
         BE    QUERHICK      NO                                  87315
         LA    R2,1(,R2)     INCREASE COUNTER                    87315
QUERHICK LA    R1,2(,R1)                                         87315
         BCT   R0,QUERHIGH   TRY FOR ALL                         87315
         CH    R2,=H'3'      ALL THREE AVAILABLE ?               87315
         BNE   QUERSKIP      NO                                  87315
         MVI   SWWUMASK+2,X'07'  MAKE FEATURE MASK               87315
         B     QUERSKIP                                          87315
QUERCOL  SLR   R2,R2         CLEAR COUNTER                       87315
         LA    R0,7          SEVEN OPTIONS                       87315
         LA    R1,8(,R14)    POINT TO FIRST ENTRY                87315
QUERCOLP CLI   0(R1),0       OPTION PRESENT ?                    87315
         BE    QUERCOMP      NO                                  87315
         CLI   1(R1),0       OPTION SUPPORTED ?                  87315
         BE    QUERCOMP      NO                                  87315
         LA    R2,1(,R2)     INCREASE COUNTER                    87315
QUERCOMP LA    R1,2(,R1)                                         87315
         BCT   R0,QUERCOLP   TRY FOR ALL                         87315
         CH    R2,=H'7'      ALL SEVEN AVAILABLE ?               87315
         BNE   QUERSKIP      NO                                  87315
         MVI   SWWUMASK+1,X'07'  MAKE FEATURE MASK               87315
QUERSKIP AR    R14,R15       ANOTHER FIELD FOLLOWS ?             87315
         LTR   R15,R15       ANY MORE ?                          87315
         BNP   POPTSFSC      NO                                  87317
         LA    R0,SWQUEREP+L'SWQUEREP-4  END OF DATA             92308
         CR    R14,R0        REACHED END ?                       92308
         BNL   POPTSFSC                                          92308
         OC    0(4,R14),0(R14)   DID WE GO TOO FAR ?             87315
         BNZ   QUERLOOP      NO; TEST NEXT FIELD                 87315
         SPACE 1                                                 87315
POPTSFSC CLI   SWPRSZ,1      ANY SCREEN SIZE ?                   87317
         BNH   POPNCONS      NO                                  87317
         STFSMODE ON,INITIAL=YES  FULL-SCREEN MODE ON            87317
POPNCONS MVI   SWCURLN,X'FF'  FORCE PAGE EJECT FIRST TIME
         TM    CALLFLG,SWSETAT  USE SET ATTRIBUTE, NOT SFE ?     87317
         BZ    *+8           NO                                  87317
         OI    SWFLAG,SWSETAT  USE SA                            87317
         SLR   R0,R0
         IC    R0,SULPP      GET USER'S LINES/PAGE SPECIFICATION
         CH    R0,=H'12'     ANYTHING USABLE ?
         BNL   POPLPPDF      YES
         LA    R0,DEFLPP     PREFERRED LINES/PAGE COUNT
POPLPPDF STC   R0,SWMAXLN+1
         TM    SUPRFG,SUPGPAGE   PAGE NUMBER FEED-BACK ?
         BZ    POPNPF        NO
         MVC   SWPAGE,SUPAGE  COPY STARTING PAGE#-1
POPNPF   LA    R0,7          SET MAXIMUM LEGAL COUNT
         SLR   R14,R14
         IC    R14,SUTIT#    GET TITLE COUNT
         CR    R14,R0          LEGAL ?
         BNH   *+6
         LR    R14,R0          ELSE CLIP
         STH   R14,SWTIT#    SET TITLE COUNT
         IC    R14,SUFOOT#   GET TITLE COUNT
         CR    R14,R0          LEGAL ?
         BNH   *+6
         LR    R14,R0          ELSE CLIP
         STH   R14,SWFOOT#   SET TITLE COUNT
         L     R14,=A(TNTRTAB)  GET DEFAULT TABLE
         ST    R14,SWTRNLOW  SET LOWER CASE TABLE                87308
         ST    R14,SWTRAN    STASH THE ADDRESS
         L     R15,SW@OBUF
         SH    R15,=H'6'
         ST    R15,SW@BCC    POINT TO PREFIX CODE
         MVC   0(6,R15),=X'27F5C3114040' ERASE/WRITE/RESET/SBA 1,1
         MVI   SWWUMASK,X'3F'  ENABLE BASIC SF BITS
         MVI   SWWUMASK+3,X'3C'  ALMOST DITTO
         MVI   SWWUDFLT,X'30'  DEFAULT OPTIONS
         MVC   SWMAXLN(8),=Y(24,80,SWIBUF-SWOBUF,24*80)  INIT
         TM    SWFLAG,SWFTSU+SWFAKE  CONSOLE OR STORAGE ?
         BZ    POPNTMOD      NEITHER
         MVI   SWDCB+DCBOFLGS-IHADCB,DCBOFOPN  SET DCB OPEN
         L     R14,SWDDNAM+4   LOAD IN-STORAGE 'PUT' ADDRESS
         TM    SWFLAG,SWFTSU   CONSOLE MODE ?
         BZ    POPPHPUT      NO; STASH USER'S 'PUT' ADDRESS
         AIF   (NOT &MVS).NOSVSTP
         CLI   SWDDNAM+1,C'U'   *U(SER) REQUEST ?
         BNE   POPPHPUT      NO
         L     R15,PSAAOLD-PSA  GET ASCB ADDRESS
         ICM   R15,15,ASCBTSB-ASCB(R15)  RUNNING UNDER TSO ?
         BZ    EXIT12        NO; WILL NOT HANDLE
         LA    R14,PUTTPUT   SET FOR PUT ROUTINE
.NOSVSTP ANOP  ,
POPPHPUT STCM  R14,7,SWDCB+DCBPUT+1-IHADCB  SET PHONY PUT ADDRESS
         TM    SWFLAG,SWFTSU+SWFAKE  FAKE I/O OR TSO MODE ?
         BNZ   POPEXIT       YES; SKIP ALL ELSE
POPNTMOD OPEN  MF=(E,SWDCB@),TYPE=J  OPEN FOR OUTPUT
         TM    SWDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN NOW ?
         BNZ   POPEXIT       YES; PROCEED
POPNOP   MVI   RETCC,12      SHOW UNUSABLE
         OI    CALLFLG,CLABEDU    FIX FOR LATER FLIP
POPEXIT  XI    CALLFLG,CLABEDU+CLNWTO  FLIP FOR FASTER TESTING
POPEXITU CLI   RETCC,8       MAJOR ERROR ?
         BH    POPFREE       YES; FREEMAIN AGAIN
         TM    SWDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN ?
         BNZ   POPEXITC      YES; CHAIN
         MVI   RETCC,8       SET LEVEL 8 ERROR FOR DUMMY
         NI    CALLFLG,255-CLABE  AND FIX FLAG FOR TESTS
         TM    CALLFLG,CLABEDU  ABEND ON DD DUMMY ?
         BNZ   POPFREE       YES
         LA    R0,PUTPUTEX   GET PUT EXIT ADDRESS
         STCM  R0,7,SWDCB+DCBPUT+1-IHADCB  SET PHONY PUT ADDRESS
POPEXITC L     R3,SWTCB      GET THE TCB ADDRESS
         USING TCB,R3
         ICM   R2,15,TCBFSA  GET SAVE AREA                      GP99026
         DROP  R3
         BZ    EXIT16        SET MAJOR ERROR
POPLOOP  ICM   R1,15,0(R2)   IS LINK POINTER ZERO ?             GP99026
         BZ    POPLOOS       YES; CHAIN OURS IN
         LR    R2,R1
         B     POPLOOP       TRY AGAIN
POPLOOS  ST    R8,0(,R2)     CHAIN THIS BLOCK IN                GP99026
         OC    SCR0OPEN,SCR0OMSK  SET DEVICE OPEN BIT
         L     R1,SAVE13     GET USER'S SAVE AREA
         ST    R8,SAVE0-SAVE(,R1)  RETURN WORK AREA IN R0
         LH    R2,SWTIT#     GET NUMBER OF TITLES
         AH    R2,SWFOOT#      AND FOOTERS
         BZ    EXITSET       QUIT IF NONE
         MVC   SWTIT1(6),MINVCON  SET MINIMUM TITLE
         MVC   SWTIT1+5(L'SWTIT1-5),SWTIT1+4  PROPAGATE BLANKS
         LA    R1,SWTIT1
POPTITL  BCT   R2,POPTITM
         ICM   R0,3,SWTIT#   ANY TITLES ?
         BZ    EXITSET       NO
         MVI   SWTIT1+4,C'1'  SET EJECT FOR FIRST ONE
         B     EXITSET
POPTITM  MVC   L'SWTIT1(L'SWTIT1,R1),0(R1)  INIT TITLE
         LA    R1,L'SWTIT1(,R1)
         B     POPTITL       DO NEXT
         SPACE 1
POPFREE  TM    CALLFLG,CLABE+CLABEDU+CLNWTO  MESSAGE ?
         BZ    POPFREE2      YES
         MVC   SWVCON(POSWTOL),POSWTO  MAKE WTO PATTERN
         MVC   SWVCON+4(8),SWDDNAM   INSERT DD NAME
         WTO   MF=(E,SWVCON)   WRITE THE MESSAGE
         CLI   SUDDALT,C' '  PRINTABLE ALTERNATE ?
         BNH   POPFREE2      NO
         MVC   SWVCON+4(8),SUDDALT  ALSO SHOW ALTERNATE
         WTO   MF=(E,SWVCON)
POPFREE2 BALS  R9,FREEFSAM   FREE THE WORK AREA                 GP99026
         TM    CALLFLG,CLABE+CLABEDU  ABEND ?
         BZ    EXITSET       AND QUIT
         ABEND 3270          QUIT
         DROP  R7
         SPACE 1
POSWTO   WTU   'DD-NAME? DD MISSING OR UNUSABLE',MF=L,ROUTCDE=11
POSWTOL  EQU   *-POSWTO
WSFQREP  DC    X'F3000501FF02' WSF QUERY REPLY                   87315
         SPACE 1
EXIT12   MVI   RETCC,12      SET MAJOR ERROR
         B     EXITSET       AND QUIT
         SPACE 1
PUTTPUT  DS    0H            TEMPORARY
PUTPUTEX SLR   R15,R15
         BR    R14           DUMMY EXIT
         SPACE 1
PATDCB   DCB   DDNAME=BTAM3270,DSORG=CX,MACRF=(R,W) BTAM PATTERN
PATEND   EQU   *
MINVCON  DC    Y(6,0),CL2' ' MINIMUM V RECORD
         POP   USING                                            GP97231
         EJECT ,
*        CLOSE ROUTINE
*
         PUSH  USING                                            GP97231
SCCLOSE  BASR  R11,0         SET LOCAL BASE                     GP99026
         USING *,R11         DECLARE IT
         LTCB  R3            GET MY TCB
         CL    R3,SWTCB      SAME TCB ?                         GP99026
         BNE   SET4          NO; MINOR ERROR
         MVC   DB(1),SCR0OMSK  GET CURRENT DEVICE MASK
         XI    DB,X'FF'      INVERT IT
         NC    SCR0OPEN,DB   RESET DEVICE OPEN BIT
         TM    SWFLAG,SWFTSU+SWFAKE  CONSOLE/STORAGE MODE ?
         BNZ   PCLCHAIN      YES; JUST FREE IT UP
         TM    SWDCB+DCBOFLGS-IHADCB,DCBOFOPN  OPEN ?
         BZ    PCLCHAIN      NO; UNCHAIN
         CLOSE MF=(E,SWDCB@)
PCLCHAIN CLI   SCR0OPEN,0    ANY OTHERS OPEN ?                   87317
         BNE   PCLCHAIL      YES; NOT QUITE DONE YET             87317
         STFSMODE OFF        RESET FULL-SCREEN MODE              87317
PCLCHAIL LA    R9,RECURSE    SET EXIT FROM IN-LINE SUBROUTINE
         B     FREEFSA       FREE SAVE AREA AND RECURSE
         SPACE 2
         LTORG ,
         POP   USING                                            GP97231
         EJECT ,
*        INITIALIZE SCREEN ROUTINE
*
SCINIT   SCHEAD ,
         SLR   R14,R14
         TM    FDWOPT,FDWOKEEP  KEEP INPUT FIELDS AS-IS ?
         BNZ   SCINITI
         MVI   FDWFG,0       RESET THE FLAG
         L     R2,FDWFWA     GET THE INPUT WORK AREA
         L     R3,FDWFWL       AND ITS LENGTH
         SLR   R15,R15
         MVCL  R2,R14        CLEAR ALL OF IT
         L     R1,SW@BCC     GET OUTPUT BUFFER
         CLI   0(R1),X'02'   STX ?                               87152
         BNE   *+8           NO                                  87152
         LA    R1,1(,R1)     ELSE SKIP OVER                      87152
         CLI   0(R1),X'27'   ESCAPE ?                            87152
         BNE   *+8           NO                                  87152
         LA    R1,2(,R1)     SKIP ESC/CCW                        87152
         MVI   0(R1),X'03'  DEFAULT WCC - RESET MDT, KBD         87152
         NI    0(R1),X'3F'   MASK                                87152
         L     R15,=A(TRADDR)                                    87152
         TR    0(1,R1),0(R15)  MAKE EBCDIC                       87152
SCINITI  NI    FDWOPT,255-FDWOKEEP  RESET KEEP FLAG
         TM    FDWFG,FDWFTXT+FDWFERR  ANY TEXT IN BUFFER ?
         BNZ   SCINITO       YES; LEAVE CURSOR ADDRESSES ALONE
         MVC   FDWCUR,=X'FFFFFFFC'  RE-USE LITERAL
         MVC   FDWCUD,FDWCUR
SCINITO  ST    R14,FDWBOL    CLEAR OUTPUT BUFFER LENGTH
         ST    R14,FDWBIL    AND INPUT BUFFER LENGTH
         STH   R14,FDWBOS    CLEAR SCREEN OFFSET
         ICM   R0,15,FDWBOA  OUTPUT BUFFER SET ?
         BNZ   SCINIBON      YES
         L     R0,SW@BCC     POINT TO OUR OUTPUT BUFFER
         ST    R0,FDWBOA     LET USER LEASE IT
         MVC   FDWBOM,SWBUFSIZ  SET BUFFER SIZE (LARGER THAN SCREEN)
SCINIBON L     R15,SW@OBUF   GET OUTPUT BUFFER ADDRESS
         S     R15,SW@BCC    SUBTRACT CONTROL PREFIX ADDRESS
         A     R15,FDWBOA    PLUS BUFFER ADDRESS = DATA START
         ST    R15,SWBUFNXT  SET START OF DATA
         XC    SWPREOPT,SWPREOPT  RESET OPTIONS                  87152
         ICM   R0,15,FDWBIA  INPUT BUFFER SET ?
         BNZ   SCINIBIN
         L     R14,SW@IBUF   GET INPUT BUFFER ADDRESS
         ST    R14,FDWBIA    SET IT
         LA    R14,SWBUFND-SWIBUF  GET INPUT BUFFER SIZE
         STH   R14,FDWBIM    INTO USER'S LIST
SCINIBIN SLR   R0,R0         GOOD RETURN
         SCEXIT ,            RETURN
         LTORG ,
         POP   USING                                            GP97231
         SPACE 2
SCLIST   SCHEAD SAP=SCMOVE   BUILD USER'S SCREEN FROM FD ENTRIES
         MVI   SWFUN,SWFLIST   SHOW SCLIST FUNCTION
         B     SCCOMMON      JOIN COMMON CODE
         POP   USING                                            GP04234
         SPACE 1                                                GP03011
SCITEM   SCHEAD SAP=SCMOVE   BUILD USER'S SCREEN FROM FD ENTRIES
         MVI   SWFUN,SWFITEM   SHOW SCITEM FUNCTION             GP02268
         B     SCCOMMON      JOIN COMMON CODE                   GP02268
         POP   USING                                            GP04234
         SPACE 1
SCANAL   SCHEAD SAP=SCMOVE   ANALYZE USER'S INPUT
         MVI   SWFUN,SWFANAL  SHOW SCANAL FUNCTION
         MVI   FDWFG,0       RESET INPUT FLAG BITS
         B     SCCOMMON
         POP   USING                                            GP04234
         SPACE 1                                                 87173
SCLOOP   SCHEAD SAP=SCMOVE   USER EXIT (FD/FDIN)                 87173
         MVI   SWFUN,SWFLOOP  SHOW LOOP/USER EXIT MODE           87173
         STM   R0,R1,SWXEXIT SAVE EXIT AND LIST ADDRESSES        87173
         B     SCCOMMON      JOIN COMMON                         87173
         POP   USING                                            GP04234
         SPACE 1
SCMOVE   SCHEAD ,            MAKE USER'S INPUT PERMANENT
         MVI   SWFUN,SWFMOVE  SHOW SCMOVE FUNCTION
         SPACE 1
SCCOMMON BASR  R12,0         DEBASE                             GP99026
         USING SCBASE,R12    NEW BASE FOR COMMON CODE
         SPACE 1
SCBASE   L     R11,FDWFDA    LOAD USER'S FD LIST ADDRESS
         USING FDSECT,R11    DECLARE IT
         XC    SWLSTXEQ(8),SWLSTXEQ  CLEAR STACKED REQUEST
         MVI   SWLSTDO,0     RESET LOCAL FLAGS                   86338
         SPACE 1
MAKELIST CLI   FDLINK,0      DUMMY ENTRY ?
         BNE   MAKELISN      NO; CONTINUE
         ICM   R0,15,SWLSTXEQ  IN EXEC ?                        GP03011
         BNZ   MAKELXEQ      YES; POP THIS REQUEST
MAKELEXT LH    R0,SWSCRSIZ   GET SCREEN SIZE
         SH    R0,FDWBOS     SUBTRACT BUFFER POSITION
         B     SCRET0        RETURN AND SET CODE
MAKELIF0 STH   R0,FDWBOS     PREVENT LATER DATA MOVES
MAKELIFF LH    R0,SWSCRSIZ   GET SCREEN SIZE
         SH    R0,FDWBOS     SUBTRACT BUFFER POSITION
         LNR   R0,R0         SET BAD RETURN CODE
SCRET0   LTR   R0,R0         SET RETURN CODE
         SCEXIT ,            RETURN
MAKELISN TM    FDTYPE,FDFNOP  NULL ENTRY ?
         BZ    MAKELISP      NO; PROCEED
         TM    SWFUN,SWFLOOP   LOOP/EXIT MODE ?                  87173
         BNZ   MAKELISP      YES; USER MAY WISH TO FLIP NOP BIT  87173
MAKELINC CL    R11,SWLSTXEN   END OF EXEC RANGE ?               GP02268
         BE    MAKELXEQ      YES; POP                           GP03011
         TM    FDTYPE,FDFIN  INPUT TYPE JUST DONE ?              86338
         BZ    *+8           NO                                  86338
         OI    SWLSTDO,DOTWO  SIGNAL PAST FIRST INPUT FIELD      86338
         CLI   SWFUN,SWFITEM  SINGLE ITEM ?                     GP02268
         BE    MAKELEXT      YES; DONE                          GP02268
         SLR   R0,R0
         ICM   R0,1,FDLINK   GET LENGTH
         BZ    MAKELEXT      QUIT IF END
         AR    R11,R0        ELSE BUMP LIST ENTRY
         B     MAKELIST      AND CHECK IT
MAKELXEQ L     R11,SWLSTXEQ  GET PRIOR FD LIST ADDRESS BACK
         XC    SWLSTXEQ(8),SWLSTXEQ  CLEAR STACKED REQUEST
         B     MAKELINC
MAKELISP LA    R7,FDDATA-2   SET FOR SHORT (FDPRT) REQUEST
         MVC   SWOPT9(2),=AL1(0,FDFDFLT)  SET DEFAULT ATTR
         TM    FDTYPE,FDFIN  INPUT ENTRY ?                       86244
         BZ    *+8           NO                                  86244
         NI    SWOPT7,255-FDFSKIP  MAKE INPUT FIELD              86244
         TM    FDTYPE,FDFPRT  IS IT SHORT REQUEST ?
         BO    MAKELISR      YES
         MVC   SWOPT9(2),FDOPT9  MOVE FD'S OPTION BYTES
         TM    FDTYPE,FDFOPT  OPTION ENTRY ?
         BNZ   MAKEOPTS      YES; PROCESS
         TM    FDTYPE,FDFGOTO  BRANCH ?
         BNZ   MAKELGOT      YES; PROCESS
         LA    R7,FDDATA     POINT TO LONG DATA FIELD
MAKELISR IC    R0,SWLSTEDT   SAVE PREVIOUS OPTION
         USING FDDATA,R7     SET FOR FD/FDPRT COMPATIBILITY
         XC    SWCUROPT,SWCUROPT  CLEAR CURRENT OPTIONS
         L     R9,=A(DUMFIW) GET FD(OUT) DUMMY ENTRY
         TM    FDTYPE,FDFIN  INPUT FIELD ?
         BZ    MAKELFIP      NO
         SLR   R9,R9
         ICM   R9,3,FDIOFF   GET OFFSET TO INPUT WORK AREA
         A     R9,FDWFWA     RELOCATE
         USING FIWSECT,R9    DECLARE IT
MAKELFIP IC    R0,SWLSTEDT   SAVE PREVIOUS OPTION
         XC    SWLSTWRK,SWLSTWRK  CLEAR WORK SPACE
         STC   R0,DB         SAVE PRIOR EDIT OPTION
         MVC   SWCUROPT(1),SWOPT7  BASIC FIELD ATTRIBUTES
         MVZ   SWCUROPT+1(1),SWOPT9  3279 FULL COLOR OPTIONS
         PACK  SWCUROPT+1(1),SWCUROPT+1(1)  PLACE IN NUMERICS
         TM    SWOPT9,FDFMONO  CONDITIONAL HIGHLIGHTING ?        87313
         BZ    MAKELFHI      NO; UNCONDITIONAL                   87313
         TM    SWWUMASK+1,X'07'  FULL-COLOR SUPPORT ?            87313
         BO    MAKELFHI+6    YES; SKIP HIGH-LIGHTING             87313
MAKELFHI MVN   SWCUROPT+2(1),SWOPT9  3279 EXTENDED HIGHLIGHT     87313
         MVC   SWLSTLEN+3(1),FDOLEN  COPY PROV. OUTPUT LENGTH
         MVC   SWLSTLTX+3(1),FDILEN    AND INPUT LENGTH
         TM    FDTYPE,FDFREGLN   IS ILEN A REGISTER ?
         BZ    MAKELIRF      NO
         CLI   FDILEN,13     VALID REGISTER ?
         BNL   MAKELEXT      NO; SKIP
         L     R15,SWLSTLTX  GET REGISTER NUMBER
         SLL   R15,2         MAKE INTO WORD OFFSET
         L     R15,USERREGS(R15)  GET ORIGINAL REGISTER CONTENTS
         STC   R15,SWLSTLTX+3   SAVE LOW BYTE AS NEW LENGTH
MAKELIRF MVC   SWLSTEDT,FDEDIT    COPY EDIT OPTION
         TM    DB,FDFPADR    PRIOR RIGHT PAD ?
         BZ    *+8           NO
         OI    SWLSTEDT,FDFPADL  PROPAGATE AS CURRENT LEFT PAD
         MVC   SWLSTDAT,FDDATA     AND DATA TYPE
         NI    SWLSTDAT,255-FDDLIT  REMOVE LITERAL FLAG BIT
         TM    SWOPT7,FDFDFLT+FDFPREV  OPTION OVERRIDE ?
         BZ    MAKELISD      NO
         LA    R1,SWWUDFLT   POINT TO DEFAULT OPTIONS
         TM    SWOPT7,FDFDFLT  DEFAULT ?
         BNZ   MAKELISC      YES; OVERRIDE
         LA    R1,SWPREOPT   USE PREVIOUS FIELD'S OPTIONS
MAKELISC MVC   SWCUROPT,0(R1)  REPLACE OPTIONS
         TM    FDTYPE,FDFIN  INPUT ENTRY ?                       86244
         BZ    MAKELPOU      NO                                  86244
         TM    SWCUROPT,FDFPROT  PRIOR SET PROTECTED ?           87165
         BNZ   MAKELPIN      YES; RESET BOTH NUMERIC AND PROTECT 87165
         NI    SWCUROPT,255-FDFPROT  RESET PROTECT ONLY          87165
         B     MAKELPEX      SKIP AROUND                         87165
MAKELPIN NI    SWCUROPT,255-FDFSKIP  MAKE INPUT FIELD            86244
         B     MAKELPEX                                          87165
MAKELPOU OI    SWCUROPT,FDFPROT  FORCE OUTPUT FIELD              87165
MAKELPEX TM    SWOPT7,FDFPREV  FORCED SAME AS PREVIOUS ?         87160
         BZ    MAKELISD      NO                                  87160
         XC    SWPREOPT,SWWUMASK  NEED EXPANSION (IF SET BY FDOPT)
MAKELISD NC    SWCUROPT,SWWUMASK  MASK WITH DEVICE CAPABILITIES
         LA    R6,FDTEXT     ADDRESS FOR LITERAL INPUT
         TM    FDTYPE,FDFIN  INPUT FD ENTRY ?
         BNZ   MAKELISA      YES; LITERALS NOT ALLOWED
         TM    FDDATA,FDDLIT  WAS LITERAL USED ?
         BNZ   MAKELISB      YES
MAKELISA LA    R6,FDSADD     POINT TO S ADDRESS
         BAS   R14,MAKESADD  GET VALUE                          GP99026
         TM    FDTYPE,FDFINDAD  INDIRECT ADDRESS ?
         BZ    MAKELISB      NO
         N     R6,=X'FFFFFFFC'   TRUNCATE ADDRESS TO WORD BOUNDARY
         ICM   R6,15,0(R6)   LOAD NEW ADDRESS FROM OLD ONE
MAKELISB ST    R6,SWLSTADD   SET PROV. INPUT ADDRESS
         TM    SWFUN,SWFANAL ANALYSIS MODE ?
         BNZ   GETANAL       YES
         TM    SWFUN,SWFMOVE  MOVE MODE ?
         BNZ   GETMOVE       YES
         TM    SWFUN,SWFLOOP LOOP/EXIT MODE ?                    87173
         BNZ   GETLOOP       YES                                 87173
         SPACE 1
         TM    FDTYPE,FDFIN  INPUT ENTRY ?
         BZ    MAKELISO      NO; PROCESS OUTPUT
         TM    FIWFG,FIWFTXT TEXT IN WORK FIELD ?
         BZ    MAKEINLN      NO; PROCESS NORMALLY
         LA    R2,FIWTEXT    POINT TO TEXT
         SLR   R1,R1
         IC    R1,FIWLEN     GET CURRENT LENGTH
         STM   R1,R2,SWLSTLTX  FORCE AN UPDATE
         MVI   SWLSTDAT,FDDCHAR  FORCE CHARACTER FIELD
         TM    FIWFG,FIWFERR+FIWFINV  FLIP INTENSITY ?           86244
         BZ    MAKEINLI      NO                                  86244
*        FOR A FIELD IN ERROR:                                   87159
*          MDT BIT IS SET TO FORCE REREAD                        87159
*              1) IF IT APPEARS IN NEXT INPUT, FIELD IS REPROCESSED
*              2) IF IT IS OMITTED IN NEXT INPUT, FIELD IS RESET 87159
*                 (USER ASSUMED TO HAVE HIT CLEAR OR ERASE KEY)  87159
*        FOR 3277 TYPE, FIELD INTENSITY IS FLIPPED               87159
*        FOR COLOR TYPE WITH NO USER COLOR, ERROR FIELD IS YELLOW
*        IF USER SPECIFIED COLOR OR COLOR NOT AVAILABLE, USE REVERSE
         OI    SWCUROPT,SFMDT  SET FIELD MODIFIED TO FORCE REREAD
         TM    SWWUMASK+1,X'07'  EXTENDED COLOR SUPPORT AVAILABLE ?
         BNO   MAKEIENY      NO                                  87159
         TM    SWCUROPT+1,X'07'  DID USER SPECIFY A COLOR ?      87159
         BNZ   MAKEIENY      YES; LEAVE IT                       87159
         OI    SWCUROPT+1,X'06'  SET YELLOW                      87159
         B     MAKEINLI                                          87159
MAKEIENY TM    SWWUMASK+2,X'07'  EXTENDED HIGH-LIGHTING AVAILABLE ?
         BNO   MAKEIENC      NO; FLIP INTENSITY ONLY             87159
         NI    SWCUROPT+2,255-X'07'  RESET USER'S HIGH-LIGHTING  87159
         OI    SWCUROPT+2,X'02'  REVERSE                         87159
         B     MAKEINLI                                          87159
MAKEIENC NI    SWOPT7,255-FDFLPEN  RESET L-PEN                   86244
         XI    SWOPT7,FDFINT   FLIP INTENSITY                    86244
MAKEINLI TM    FIWFG,FIWFINT  INTENSE ?                          86244
         BZ    MAKEINLN      NO                                  86244
         NI    SWOPT7,255-FDFLPEN                                86244
         OI    SWOPT7,FDFINT MAKE INTENSE                        86244
MAKEINLN LM    R0,R1,SWLSTLEN  GET TOTAL AND INPUT LENGTHS
         LTR   R0,R0
         BP    *+6
         LR    R0,R1         IF OUTLEN=0, USE INPUT
         LTR   R1,R1
         BP    *+6
         LR    R1,R0         IF INLEN=0, USE OUTPUT
         LTR   R0,R0         ALL ZERO ?
         BNP   MAKELINC      YES; IGNORE ENTRY
         STM   R0,R1,SWLSTLEN  SET LENGTHS BACK
         OI    SWLSTEDT,FDFPADL+FDFPADR  SET PADDING
         TM    SWCUROPT,FDFPROT  PROTECTED FIELD (USED FOR LPEN)
         BZ    MAKELPRO      NO; GO TO COMMON MOVER
*        B     MAKELISO      YES; TREAT AS OUTPUT
         SPACE 1
MAKELISO TM    SWCUROPT,FDFSKIP  DID USER SPECIFY ANY ?
         BNZ   *+8
         OI    SWCUROPT,FDFSKIP  DEFAULT - CURSOR SKIP FIELD
MAKELPRO LM    R0,R1,SWLSTLTX  LOAD TEXT LENGTH/ADDRESS
         LTR   R0,R0         ANY LENGTH ?
         BP    MAKELPRP      YES
         CLI   SWLSTDAT,FDDADDR  ADDRESS FIELD ?
         BNE   MAKELINC      NO; SKIP FD ENTRY
         ICM   R0,15,SWLSTLEN   GET OUTPUT LENGTH
         BNZ   MAKELPRP      USE IT
         LA    R0,3          ELSE DEFAULT TO 6 BYTES
MAKELPRP CLI   SWLSTDAT,FDDADDR  CHARACTER FIELD ?
         BL    MAKELPRS      YES; SKIP TRANSLATE (0C4)
         SLR   R15,R15
         IC    R15,SWLSTDAT  GET DATA TYPE
         CH    R15,=H'1'     CHAR TYPE 0 OR 1 ?                  90001
         BNH   MAKELPRS      YES; DEFER UNTIL DE-NULL            90001
         BCTR  R15,0         ELSE MAKE RELATIVE TO CHAR
         COBAL CONVERT       INVOKE CONVERSION @ CVC+TYPE
         BM    MAKELINC      SKIP IF BAD SPECIFICATION
MAKELPRS TM    SWOPT7,FDFNULL   NULL SUPPRESSION ?
         BZ    MAKENULX      NO; NO NULL SUPPRESSION
         LTR   R0,R0         ANY LENGTH ?
         BNP   MAKENULX      NO; EXIT
MAKENULL CLI   0(R1),0       LEADING NULL ?
         BNE   MAKENULT      NO; CHECK FOR TRAILING NULL
         LA    R1,1(,R1)
         BCT   R0,MAKENULL
MAKENULT LTR   R0,R0         ALL NULL ?
         BNP   MAKENULX      YES
         LR    R2,R1         GET START OF STRING
         AR    R2,R0         GET LAST BYTE + 1 OF STRING
MAKENULU BCTR  R2,0          BACKSPACE A BYTE
         CLI   0(R2),0       TRAILING NULL ?
         BNE   MAKENULX      NO
         BCT   R0,MAKENULU   KEEP IT UP
MAKENULX TM    SWLSTEDT,FDFDEBL  DEBLANK FROM LEFT ?
         BZ    MAKELBLF      NO
         COBAL DBKL
MAKELBLF TM    SWLSTEDT,FDFDEBR  DEBLANK FROM RIGHT ?
         BZ    MAKELBRT
         COBAL DBKR
MAKELBRT TM    SWLSTEDT,FDFDEBZ  STRIP LEADING ZEROES ?
         BZ    MAKELBLY                                          90001
         COBAL DBKZ
MAKELBLY CLI   SWLSTDAT,1    CHAR TYPE 0 OR 1 ?                  90001
         BH    MAKELBLZ      NO                                  90001
         SLR   R15,R15                                           90001
         COBAL CONVERT       NO; MOVE AND TRANSLATE NOW          90001
         BM    MAKELINC      SKIP IF BAD SPECIFICATION           90001
MAKELBLZ LA    R15,132       SET MAXIMUM INPUT LENGTH
         TM    FDTYPE,FDFIN  INPUT FIELD ?
         BZ    *+8           NO
         LA    R15,256       PERMIT MAXIMUM LENGTH
         CR    R0,R15
         BNH   *+6
         LR    R0,R15        TRUNCATE
         STM   R0,R1,SWLSTLTX  STASH BACK
         ICM   R1,15,SWLSTLEN  GET AND TEST FIELD LENGTH
         BNZ   *+8           OK IF NON-ZERO
         ST    R0,SWLSTLEN   ELSE USE INPUT LENGTH
         SPACE 2
DOSFP    EQU   X'80'         EXPAND PROTECTED SF PRIOR TO SBA
DONL1    EQU   X'40'         POSITION TO LAST BYTE OF CURRENT LINE
DONL     EQU   X'20'         POSITION TO FIRST BYTE OF NEXT LINE
DOSFU    EQU   X'10'         EXPAND SF BEFORE TEXT
DOTWO    EQU   X'08'         ON AFTER FIRST FDIN                 86338
DOTRAIL  EQU   X'04'         EXPAND AN SF AFTER FIELD            87265
ONNL1    EQU   X'02'         BUFFER AT END OF CURRENT LINE
ONNL     EQU   X'01'         BUFFER AT NEW LINE
         SPACE 1
         NI    SWLSTDO,DOTWO  RESET PROCESSING FLAGS             86338
*      BACKSPACE IN BUFFER TO LOOK FOR PRIOR SF
         L     R1,SWBUFNXT   GET NEXT BUFFER POSITION
         LH    R0,FDWBOS     AND GET SCREEN POSITION
         SH    R1,=H'2'      BACKUP BY LENGTH OF AN SF
         SH    R0,=H'1'      AND SCREEN POSITION
         BM    MAKELBAK      SKIP IF START OF SCREEN
         CLI   0(R1),X'1D'   SF ?
         BNE   MAKELBAK      NO; LEAVE AS IS
         ST    R1,SWBUFNXT   REUSE IT
         STH   R0,FDWBOS     ADJUST
         OI    SWLSTEDT,FDFPADL  SET FOR LEADING SF
*      IF PREVIOUS FIELD WAS INPUT, IT MUST BE TERMINATED
MAKELBAK TM    SWPREOPT,FDFPROT  PRIOR INPUT FIELD ?
         BNZ   *+8           NO
         OI    SWLSTDO,DOSFP   FORCE AN SF
*      IF PRIOR FIELD REQUESTED TRAILING PAD, CHANGE TO LEADING SF
         TM    SWLSTEDT,FDFPADL  LEADING PAD ?
         BNZ   MAKEFSFU      YES; REQUEST AN SF
*      IF THE CURRENT FIELD IS INPUT, FORCE AN SF
         TM    SWCUROPT,FDFPROT  OUTPUT ?
         BZ    MAKEFSFU      NO; MAKE SF
*      IF THE FIELD IS LIGHT-PEN DETECTABLE, FORCE AN SF
         TM    SWCUROPT,FDFNDISP  DETECTABLE ?
         BM    MAKEFSFU      YES; MAKE SF
*      IF FIELD OPTIONS DIFFER, FORCE AN SF
         CLC   SWPREOPT(1),SWCUROPT  DIFFERENT OPTIONS ?
         BE    *+8           NO
MAKEFSFU OI    SWLSTDO,DOSFU
*      IF NEW-LINE REQUESTED, SET FOR END OF CURRENT LINE
         TM    SWLSTEDT,FDFNL   NEW LINE ?
         BZ    *+8           NO
         OI    SWLSTDO,DONL1   SET FOR END OF LINE
         SPACE 1
*      GET ADDRESS OF NEXT FULL LINE, SET FLAGS AND REMAINING SIZE
         BAS   R14,MAKELINE  GET NEXT LINE POSITION             GP99026
         CH    R0,FDWBOS     ARE WE ON THE NEW LINE ?
         BNE   *+8           NO
         OI    SWLSTDO,ONNL  YES; SET ON NEW LINE
         BCTR  R0,0          BACKSPACE ONE POSITION
         CH    R0,FDWBOS     ARE WE AT END OF CURRENT LINE ?
         BNE   *+8           NO
         OI    SWLSTDO,ONNL1  YES; SET IT
         SH    R0,FDWBOS     GET RESIDUAL BYTES ON LINE-1
         TM    SWLSTDO,DOSFU+DOSFP  SF TO BE EXPANDED ?
         BNZ   *+8           YES; LEAVE SHORT
         AH    R0,=H'1'      ELSE UP BY ONE
*      OUTPUT FIELDS ARE NOT SPLIT OVER LINES (IF <= WIDTH)
         TM    SWCUROPT,FDFPROT  PROTECTED FIELD ?
         BZ    MAKELNIN      NO
         C     R0,SWLSTLEN   COMPARE RESIDUAL TO FIELD LENGTH
         BNL   MAKELNIN      DATA WILL FIT ON CURRENT LINE
         OI    SWLSTDO,DONL1  ELSE FORCE ON NEW LINE
*      LAST BYTE REQUEST FOR DETECTABLE CHANGED TO NEW LINE
MAKELNIN TM    SWCUROPT,FDFNDISP  DETECTABLE ?
         BNM   MAKELNDT      NO
         TM    SWLSTDO,DONL1+ONNL1  LAST BYTE REQUESTED ?
         BZ    MAKELNDT      NO
         OI    SWLSTDO,DONL  CONVERT TO NEW LINE REQUEST
*      IF NL1 OR ONNL1, AND NO SF, CHANGE TO NL
MAKELNDT TM    SWLSTDO,DOSFU   SF EXPANSION ?
         BNZ   MAKELNDU      YES
         TM    SWLSTDO,ONNL1+DONL1  LAST BYTE ?
         BZ    MAKELNDU      NO
         OI    SWLSTDO,DONL  FORCE TO NEXT LINE
*      IF LAST BYTE REQUEST, BUT ON NEW LINE ALREADY, CHANGE TO NL
MAKELNDU TM    SWLSTDO,DONL1+ONNL
         BNO   *+8           NO
         OI    SWLSTDO,DONL  CHANGE TO NEW LINE
*      IF BOTH NEW LINE OPTIONS SPECIFIED, CANCEL LAST BYTE OPTION
         TM    SWLSTDO,DONL1+DONL   BOTH OPTIONS ?
         BNO   *+8           NO
         NI    SWLSTDO,255-DONL1  YES; RESET LAST BYTE ON CURRENT LINE
*      IF LAST BYTE OPTION AND BUFFER AT LAST BYTE, CANCEL REQUEST
         TM    SWLSTDO,DONL1+ONNL1  LAST BYTE ?
         BNO   *+8           NOT BOTH
         NI    SWLSTDO,255-DONL1  RESET REQUEST
*      IF NEW LINE REQUEST AND BUFFER ON NEW LINE, CANCEL REQUEST
         TM    SWLSTDO,DONL+ONNL   BOTH ?
         BNO   *+8           NO
         NI    SWLSTDO,255-DONL   RESET NEW LINE REQUEST
*      IF A PRE-SBA SF REQUESTED, AND SBA SKIPPED, USE POST-SBA SF
         TM    SWLSTDO,DOSFP   EXPAND SF BEFORE SBA ?
         BZ    MAKELNPS      NO
         TM    SWLSTDO,DOSFU+ONNL1 (+DOSFP)
         BO    MAKELNPS      PRESERVE SFP IN CORRECT SPOT
         TM    SWLSTDO,DONL+DONL1  SBA TO BE EXPANDED ?
         BNZ   MAKELNPS      YES
         NI    SWLSTDO,255-DOSFP  NO; CANCEL SF BEFORE
         OI    SWLSTDO,DOSFU   CHANGE TO POST-SBA SF
*      IF SF BEFORE SBA, DO IT
MAKELNPS TM    SWLSTDO,DOSFP   LEADING SF ?
         BZ    MAKELNPB      NO; SEE IF SBA REQUIRED
         LH    R15,FDWBOS    GET CURRENT SCREEN POSITION
         CH    R15,SWSCRSIZ  STILL ON SCREEN ?
         BNL   MAKELIFF      NO; QUIT
         L     R2,SWCUROPT   GET CURRENT OPTIONS
         STCM  R2,8,SWPREOPT  CREAM PRIOR OPTIONS
         NI    SWPREOPT,255-FDFPROT  FAKE PRIOR AS INPUT
         OI    SWCUROPT,FDFPROT   AND CURRENT AS OUTPUT
         IC    R0,SWCUROPT   LOAD OPTIONS
         BAS   R14,MAKESF7   EXPAND AN SF UNCONDITIONALLY       GP99026
         ST    R2,SWCUROPT   RESTORE CURRENT OPTIONS
*      EXPAND SBA IF ONE IS REQUIRED
MAKELNPB TM    SWLSTDO,DONL+DONL1   POSITIONING REQUIRED ?
         BZ    MAKELNPU      NO; SEE IF SF REQUIRED
         BAS   R14,MAKELINE  GET ADDRESS OF NEXT LINE           GP99026
         TM    SWLSTDO,DONL  NEW LINE REQUESTED ?
         BNZ   *+6           YES
         BCTR  R0,0          ELSE SET FOR LAST BYTE ON CURRENT
         CH    R0,SWSCRSIZ   STILL ON SCREEN ?
         BNL   MAKELIF0      NO; QUIT
         BAS   R14,MAKESBA   EXPAND THE SBA                     GP99026
*      SEE IF AN SF IS REQUIRED
*        IF REQUIRED, PREVIOUS OPTIONS ARE MODIFIED TO FORCE IT
*        IF NOT, PREVIOUS OPTIONS SET THE SAME TO FORCE ONLY
*        3278 AND 3279 ORDERS (CA)
MAKELNPU TM    FDTYPE,FDFIN  INPUT FIELD ?                       87265
         BNO   MAKELNPR      NO                                  87156
         OI    SWLSTDO,DOTRAIL  FORCE SF AFTER FIELD             87265
         TM    FDTYPE,FDFCIN  CONDITIONAL INPUT ?                87265
         BNO   MAKELNPR      NO                                  87156
         TM    FDWOPT,FDWLOCK  UNLOCKED ?                        87156
         BNZ   MAKELNPR      YES                                 87156
         OI    SWCUROPT,FDFSKIP  PROTECT THIS INPUT FIELD        87156
MAKELNPR L     R0,SWCUROPT   GET CURRENT OPTIONS                 87156
         STCM  R0,8,SWPREOPT  SET INTO PREVIOUS OPTIONS
         TM    SWLSTDO,DOSFU   SF REQUIRED ?
         BZ    *+8           NO
         XI    SWPREOPT,FDFPROT  SET OPTIONS DISSIMILAR
         TM    FDTYPE,FDFIN  INPUT FIELD ?                       92087
         BNZ   MAKECLOK      DON'T TOUCH                         92087
         TM    FDTYPE,FDFCIN  CONDITIONAL LOCK ?                 92087
         BZ    MAKECLOK      NO                                  92087
         TM    FDWOPT,FDWLOCK  CALLER AUTHORIZED ?               92087
         BNZ   MAKECLOK      YES; DISPLAY                        92087
         OI    SWCUROPT,FDFNDISP  SET NON-DISPLAY                92087
MAKECLOK LH    R15,FDWBOS    GET CURRENT SCREEN POSITION
         CH    R15,SWSCRSIZ  STILL ON SCREEN ?
         BNL   MAKELIFF      NO; QUIT
         BAS   R14,MAKESFE   EXPAND SF OR SFE                   GP99026
         TM    FDTYPE,FDFIN  INPUT FIELD ?
         BZ    MAKEPACO      NO
         MVC   FIWATB,FDWBOS  ELSE SAVE ATTRIBUTE SCREEN ADDRESS
*      NOTE - INTENTIONAL LOGICAL COMPARE ON CURSOR WHICH IS
*        INITIALIZED AS FFFF.  CH WOULD NOT WORK
         CLC   FDWBOS,FDWCUD  LOWER THAN CURSOR DEFAULT ?
         BNL   MAKEPACS      NO
         MVC   FDWCUD,FDWBOS  SET CURSOR TO INPUT FIELD
MAKEPACS CLC   FDWCUR,=X'FFFFFFFC'  HAS USER FORCED CURSOR ?
         BNE   MAKEPACO      YES; LEAVE IT ALONE
         MVC   FDWCUR,FDWBOS SET CURSOR ADDRESS
MAKEPACO LM    R0,R2,SWLSTLEN  GET LENGTHS AND ADDRESS BACK
         CR    R1,R0         TEXT GREATER THAN OUTPUT LENGTH ?
         BL    MAKEPADL      CHECK FOR PADDING
         BE    MAKEPADM      EQUAL - MOVE
         TM    SWLSTEDT,FDFRADJ  RIGHT-ADJUST ?
         BZ    MAKEPADQ      NO; JUST TRUNCATE ON RIGHT
         AR    R2,R1         ADD FIELD LENGTH
         SR    R2,R0         SET SHORTER OUTPUT LENGTH
MAKEPADQ LR    R1,R0         TRUNCATE
         B     MAKEPADS      STASH BACK
MAKEPADL TM    SWLSTEDT,FDFRADJ  RIGHT-ADJUST OPTION ?
         BZ    MAKEPADM      NO; MOVE.  CLEAN-UP LATER
         SR    R0,R1         MAKE FILL LENGTH
         BM    MAKEPRAB      ERROR ?
         CH    R0,=H'4'      LONG ENOUGH FOR RA ?
         BH    MAKEPRAA      YES; GEN AN RA
         LR    R1,R0         SET LENGTH
         LA    R2,=CL4' '    FILLER
         BAS   R14,MAKETEXT  INSERT BLANKS                      GP99026
         B     MAKEPRAB      DONE; ADJUST
MAKEPRAA AH    R0,FDWBOS     ADD SCREEN POSITION
         BAS   R14,MAKESRA   SKIP THE HARD WAY                  GP99026
         MVI   0(R15),C' '   SET FILL CHARACTER
MAKEPRAB LM    R1,R2,SWLSTLTX  GET LENGTH/ADDRESS BACK
         LR    R0,R1         NEW TOTAL LENGTH
MAKEPADS STM   R0,R2,SWLSTLEN  SAVE ADJUSTED VALUES
MAKEPADM BAS   R14,MAKETEXT  MOVE TEXT                          GP99026
         LM    R0,R1,SWLSTLEN  GET LENGTHS BACK
         SR    R0,R1         RESIDUAL LENGTH TO TACK ON
         BNP   MAKEPADP      NONE; CHECK FOR TRAILING PAD
         AH    R0,FDWBOS     MAKE NEW SCREEN POSITION
         TM    SWOPT7,FDFNULL  NULL SUPPRESSION ?
         BNZ   MAKEPADN      YES; NULL FILL
         BAS   R14,MAKESRA   ELSE SET FOR REPEAT TO ADDRESS     GP99026
         MVI   0(R15),C' '   - BLANKS
         B     MAKEPADP      COMMON
MAKEPADN BAS   R14,MAKEEUA   MAKE NULLS TO NEXT FIELD           GP99026
MAKEPADP TM    SWLSTEDT,FDFPADR  TRAILING PAD ?
         BZ    MAKEPTRL      NO; GET NEXT FIELD                  87263
         TM    SWCUROPT,FDFPROT  OUTPUT FIELD ?
         BZ    MAKEPTRL      NO; LEAVE INPUT                     87263
         BAS   R14,MAKELINE  GET NEW LINE ADDRESS               GP99026
         CH    R0,FDWBOS     ARE WE ON NEW LINE ?
         BNE   MAKELINC      NO
         NI    SWLSTEDT,255-FDFPADR  ELSE CANCEL THE TRAILING SF
         LH    R0,FDWBOS     GET CURRENT POSITION BACK           87265
MAKEPTRL TM    SWLSTDO,DOTRAIL  TRAILING SF FORCED ?             87265
         BNZ   MAKELINC      NO; JUST DO NEXT FIELD              87263
         CH    R0,SWSCRSIZ   IS SCREEN FULL ?                    87263
         BNL   MAKELINC      YES; TOO BAD                        87263
         ICM   R2,15,SWPREOPT  PRESERVE THE OPTIONS              87265
         LA    R0,FDFPROT    MAKE A PROTECTED FIELD              87263
         BAS   R14,MAKESF7   FORCE THE SF                        87265
         NI    SWLSTEDT,255-FDFPADR  NEED NO PAD                 87265
         STCM  R2,15,SWPREOPT  RESTORE THE FIELD OPTIONS         87265
         B     MAKELINC                                          87263
         SPACE 1
         PUSH  USING
         DROP  R7            NO SHORT FORM OF OPTIONS
MAKEOPTS TM    FDEDIT,FDOWCCP  WCC OPTION PRESENT ?
         BZ    MAKEOPTW      NO
         L     R1,SW@BCC     GET OUTPUT BUFFER
         CLI   0(R1),X'02'   STX ?
         BNE   *+8           NO
         LA    R1,1(,R1)     ELSE SKIP OVER
         CLI   0(R1),X'27'   ESCAPE ?
         BNE   *+8           NO
         LA    R1,2(,R1)     SKIP ESC/CCW
         MVC   0(1,R1),FDOWCC  PUT IN THE WCC OPTION             87153
         NI    0(R1),X'3F'   MASK
         TR    0(1,R1),TRADDR  MAKE EBCDIC
MAKEOPTW TM    FDEDIT,FDOCURP  CURSOR ADDRESS PRESENT ?
         BZ    MAKEOPTC      NO
         SLR   R0,R0         CLEAR FOR IC
         ICM   R0,3,FDOCUR   GET THE CURSOR ADDRESS
         BAS   R14,USERADDR  CONVERT                            GP99026
         LTR   R0,R0         VALID ?
         BNP   MAKEOPTC      NO; IGNORE
         STH   R0,FDWCUR     SET NEW ADDRESS
MAKEOPTC TM    FDEDIT,FDFNL  NEW LINE REQUEST ?
         BZ    MAKEOPTL      NO
         BAS   R2,CONDSF     CHECK IF SF REQUIRED BEFORE SBA    GP99026
         BAS   R14,MAKELINE  INCREMENT                          GP99026
MAKEOPTL TM    FDEDIT,FDOSBAP  SBA SPECIFIED ?
         BZ    MAKEOPTB      NO
         BAS   R2,CONDSF     CHECK IF SF REQUIRED BEFORE SBA    GP99026
         SLR   R0,R0
         ICM   R0,3,FDOSBA   GET ADDRESS
         BAS   R14,USERADDR  CONVERT THE ADDRESS                GP99026
         LTR   R0,R0         CHECK THE RETURN
         BM    MAKEOPTB      INVALID - IGNORE
         CH    R0,FDWBOS     COMPARE TO CURRENT ADDRESS
         BNL   MAKEOPTA      HIGHER - PROCESS IT
         MVI   SWPREOPT,X'FF'-FDFPROT   INVALIDATE PREVIOUS OPTIONS
MAKEOPTA BAS   R14,MAKESBA   MAKE SBA ORDER                     GP99026
MAKEOPTB TM    FDEDIT,FDOPTP  OPTIONS PRESENT ?
         BZ    MAKELINC      NO; GET NEXT LIST ENTRY
         XC    SWPREOPT,SWPREOPT                                 87320
         MVC   SWPREOPT(1),SWOPT7  BASIC FIELD ATTRIBUTES
         MVZ   SWPREOPT+1(1),SWOPT9  3279 FULL COLOR OPTIONS
         PACK  SWPREOPT+1(1),SWPREOPT+1(1)  PLACE IN NUMERICS
         TM    SWOPT9,FDFMONO  CONDITIONAL HIGHLIGHTING ?        87313
         BZ    MAKEOPHI      NO; UNCONDITIONAL                   87313
         TM    SWWUMASK+1,X'07'  FULL-COLOR SUPPORT ?            87313
         BO    MAKEOPHI+6    YES; SKIP HIGH-LIGHTING             87313
MAKEOPHI MVN   SWPREOPT+2(1),SWOPT9  3279 EXTENDED HIGHLIGHT     87313
         L     R0,SWPREOPT   GET NEW OPTIONS                     87320
         L     R14,SWWUMASK  GET DEVICE CAPABILITIES             87320
         NR    R0,R14        MASK BY DEVICE CAPABILITIES         87320
         SLL   R14,1         MAKE UNDEFINED BITS ON              87320
         N     R14,=X'40080840'  ALLOW ONLY UNUSED BITS          87320
         OR    R0,R14        MAKE DISSIMILAR                     87320
         ST    R0,SWPREOPT   STASH IT BACK                       87320
         B     MAKELINC      GET NEXT LIST ENTRY
         SPACE 1
CONDSF   L     R1,SWBUFNXT   GET NEXT BUFFER POSITION
         LH    R0,FDWBOS     AND GET SCREEN POSITION
         SH    R1,=H'2'      BACKUP BY LENGTH OF AN SF
         SH    R0,=H'1'      AND SCREEN POSITION
         BMR   R2            SKIP IF START OF SCREEN
         CLI   0(R1),X'1D'   SF ?
         BER   R2            YES; LEAVE AS IS
         OI    SWPREOPT,FDFPROT  NO LONGER INPUT                 87160
         IC    R0,SWPREOPT   GET PREVIOUS SF OPTIONS
         NI    SWPREOPT,255-FDFPROT  FORCE DISSIMILAR            87160
         BAS   R14,MAKESF    TERMINATE INPUT OR L-PEN FIELD     GP99026
         BR    R2            RETURN TO CALLER
         SPACE 1
USERADDR STH   R0,DBWORK     STORE USER'S PARAMETER
         TM    DBWORK,X'C0'  2-BYTE FORMAT ?
         BNZ   USERADD2      YES; DECODE SEPARATELY
USERADDX CH    R0,SWSCRSIZ   VALID ?
         BLR   R14           YES; RETURN
USERABAD SLR   R0,R0
         BCTR  R0,0          MAKE INVALID ADDRESS
         BR    R14           RETURN
USERADD2 LH    R1,FDWBOS     GET CURRENT POSITION
         SLR   R0,R0
         LH    R15,SWWIDTH   BYTES/LINE
         DR    R0,R15        DECOMPOSE ADDRESS INTO COLUMN/LINE
         STM   R0,R1,DB      SAVE THEM
         IC    R0,DBWORK+1   GET USER'S COLUMN OPTION
         CH    R0,SWWIDTH    VALID ?
         BL    USERADDL      YES; GO TO CHECK LINE OPTION
         SH    R0,=H'253'    ADJUST BY FINAGLE FACTOR
         BM    USERADCB      UNDENT OPTION
         A     R0,DB         ELSE RELATIVE TO CURRENT COLUMN
         SH    R0,=H'1'      253-, 254 CURRENT, 255 NEXT
         BM    USERABAD      INVALID REQUEST
         B     USERADDL      NOW PROCESS LINE
USERADCB AH    R0,SWWIDTH    MAKE RELATIVE TO RIGHT OF SCREEN
         BM    USERABAD      INVALID
USERADDL IC    R1,DBWORK     GET LINE OPTION
         SH    R1,=H'64'     ALLOW FOR FLAG BIT (X'40')
         CH    R1,SWMAXLN    VALID ?
         BL    USERADDM      YES; MAKE FULL ADDRESS
         SH    R1,=AL2(253-64)  ELSE ADJUST SOME MORE
         BM    USERADLB      UNDENT OPTION
         A     R1,DB+4       ADJUST RELATIVE TO CURRENT LINE
         SH    R1,=H'1'       FINAGLE
         BM    USERABAD      TOO BAD
         B     USERADDM      GET FULL ADDRESS
USERADLB AH    R1,SWMAXLN    MAKE RELATIVE TO END OF SCREEN
         BM    USERABAD      TOO BAD
USERADDM MH    R1,SWWIDTH
         AR    R0,R1         MAKE FULL SCREEN ADDRESS
         B     USERADDX      CHECK AND RETURN
         POP   USING         RESTORE R7 FOR SHORT FD
         SPACE 1
MAKETADD TM    0(R6),X'F0'   VALID REGISTER ?
         BZ    MAKELINC      NO; SKIP THIS ENTRY
MAKESADD CLI   0(R6),X'DF'   VALID REGISTER ?
         BH    MAKELIFF      NO; TAKE ERROR EXIT
         ST    R1,DB         SAVE USER'S VALUE
         ICM   R6,3,0(R6)    GET S FORM OF ADDRESS
         LR    R1,R6         COPY
         N     R6,=X'00000FFF'  GET DISPLACEMENT
         SRL   R1,10
         N     R1,=X'0000003C'  GET BASE *4
         BZ    MAKESADT      DON'T ADD R0 FOR ABSOLUTE ADDRESS
         AL    R6,USERREGS(R1)  GET USER'S VALUE
MAKESADT LA    R6,0(,R6)     KILL HIGH BYTE
         L     R1,DB         RESTORE USER'S REGISTER
         BR    R14           RETURN TO CALLER
         SPACE 1
MAKEEUA  ICM   R14,8,=X'12'  MAKE EUA ORDER
         B     MAKESBAU      GO TO COMMON
         SPACE 1
MAKESBA  ICM   R14,8,=X'11'  MAKE SBA ORDER
MAKESBAU L     R15,SWBUFNXT  GET OUTPUT BUFFER ADDRESS
         CH    R0,FDWBOS     ALREADY POSITIONED ?
         BER   R14           YES; SKIP
         STH   R0,FDWBOS     SET CURRENT SCREEN ADDRESS
         CH    R0,SWSCRSIZ   FITS ON SCREEN ?
         BNLR  R14           NO; RETURN
         STCM  R14,8,0(R15)  MAKE SBA OR EUA
         STADD R0,DB=DBWORK  CONVERT ADDRESS
         MVC   1(2,R15),DBWORK   MOVE IT IN
         LA    R15,3(,R15)   SET NEXT BUFFER ADDRESS
MAKESOFF ST    R15,SWBUFNXT  AND SET IT
         BR    R14           RETURN
         SPACE 1
MAKESRA  L     R15,SWBUFNXT  GET OUTPUT BUFFER ADDRESS
         STH   R0,FDWBOS     SET CURRENT SCREEN ADDRESS
         CH    R0,SWSCRSIZ   FITS ON SCREEN ?
         BNLR  R14           NO; RETURN
         MVI   0(R15),X'3C'  MAKE REPEAT TO ADDRESS ORDER
         STADD R0,DB=DBWORK  BUILD ADDRESS
         MVC   1(2,R15),DBWORK  MOVE IT
         LA    R15,4(,R15)   SET NEXT BUFFER ADDRESS
         ST    R15,SWBUFNXT  AND SET IT
         BCTR  R15,0         RETURN ADDRESS FOR CHARACTER INSERTION
         BR    R14           RETURN
         SPACE 1
MAKESFE  IC    R0,SWCUROPT   DUPLICATE HIGH IN LOW BYTE FOR SF
         TM    FDWOPT,FDWOSA   SA RATHER THAN SFE ?              87336
         BNZ   MAKESFSA      YES                                 87336
         TM    SWFLAG,SWSETAT  USE SA RATHER THAN SFE ?          87317
         BNZ   MAKESFSA      YES; PRESERVE PRIOR OPTIONS         87317
         XC    SWPREOPT+1(2),SWPREOPT+1  SIMPLER DEFAULT REQUESTS
MAKESFSA CLC   SWCUROPT+1(2),SWPREOPT+1  ANY CHANGE ?
         BE    MAKESF        NO; JUST MAKE A START FIELD ATTRIBUTE
         L     R15,SWBUFNXT  GET BUFFER ADDRESS
         TM    FDWOPT,FDWOSA   SA RATHER THAN SFE ?              87336
         BNZ   MAKESA        YES                                 87336
         TM    SWFLAG,SWSETAT USE SA RATHER THAN SFE ?           87317
         BNZ   MAKESA        YES                                 87317
         TM    SWCUROPT,FDFPROT  PROTECTED FIELD ?
         BZ    MAKESFEF      NO; MUST EXPAND SF
         TM    SWCUROPT,FDFNDISP  DETECTABLE ?
         BO    MAKESF7       NON-DISPLAY - PREVENT COLOR, ETC.   87165
         B     MAKESFEF      YES; MUST EXPAND SF
MAKESA   CLC   SWCUROPT+1(1),SWPREOPT+1  COLOR CHANGE ?          87153
         BE    MAKESA2       NO; TRY HIGH-LIGHTING               87153
         MVI   0(R15),X'28'  SET FOR CHARACTER ATTRIBUTE         87153
         MVI   1(R15),X'42'  REQUEST COLOR                       87153
         MVC   2(1,R15),SWCUROPT+1  COPY REQUESTED COLOR         87153
         NI    2(R15),X'07'  MASK                                87153
         TR    2(1,R15),TRATTR  TRANSLATE AN ATTRIBUTE           87153
         LA    R15,3(,R15)   INCREMENT BUFFER                    87153
MAKESA2  CLC   SWCUROPT+2(1),SWPREOPT+2  HIGH-LIGHT CHANGE ?     87153
         BE    MAKESA3       NO; FINISH UP                       87153
         MVI   0(R15),X'28'  SET FOR CHARACTER ATTRIBUTE         87153
         MVI   1(R15),X'41'  REQUEST HIGH-LIGHTING               87153
         MVC   2(1,R15),SWCUROPT+2  COPY REQUEST                 87153
         NI    2(R15),X'07'  MASK                                87153
         TR    2(1,R15),TRATTR  TRANSLATE AN ATTRIBUTE           87153
         LA    R15,3(,R15)   INCREMENT BUFFER                    87153
MAKESA3  MVC   SWPREOPT+1(3),SWCUROPT+1  SET PROCESSED           87320
         ST    R15,SWBUFNXT  SAVE BUFFER ADDRESS                 87320
         B     MAKESF        SEE IF AN SF IS NEEDED              87320
         SPACE 1                                                 87165
MAKESFEF XC    SWPREOPT+1(2),SWPREOPT+1  SKIP DEFAULT REQUESTS   87160
         CLC   SWCUROPT+1(2),SWPREOPT+1  ANY CHANGE ?            87165
         BE    MAKESF7       NO; JUST MAKE A START FIELD ATTRIBUTE
         MVI   0(R15),X'29'  MAKE START FIELD EXTENDED           87151
         LH    R0,FDWBOS     GET LOGICAL SCREEN POSITION
         AH    R0,=H'1'      ADJUST FOR SF
         STH   R0,FDWBOS     AND ADJUST IT
         SLR   R0,R0         SET NUMBER OF FIELDS                87315
         LA    R1,2(,R15)    FIRST ATTRIBUTE STRING
         TM    SWCUROPT,X'3D'  ANY BITS SET ?                    87315
         BZ    MAKESFEG      NO; NO NEED TO REQUEST DEFAULT      87315
         LA    R0,1          SET NUMBER OF FIELDS
         MVI   0(R1),X'C0'   SET FOR SF OPTIONS                  87153
         MVC   1(1,R1),SWCUROPT  SET SF OPTIONS                  87153
         NI    1(R1),X'3D'  MASK                                 87153
         TR    1(1,R1),TRADDR  MAKE EBCDIC                       87153
         LA    R1,2(,R1)     SET NEXT FIELD
MAKESFEG CLC   SWCUROPT+1(1),SWPREOPT+1  COLOR CHANGE ?
         BE    MAKESFEH      NO
         AH    R0,=H'1'      UP FIELD COUNT
         MVI   0(R1),X'42'   SET FOR COLOR ATTRIBUTE
         MVC   1(1,R1),SWCUROPT+1  SET COLOR
         NI    1(R1),X'07'   MASK                                87153
         TR    1(1,R1),TRATTR  MAKE ATTRIBUTE X'0' OR FN         87153
         LA    R1,2(,R1)
MAKESFEH CLC   SWCUROPT+2(1),SWPREOPT+2  EXTENDED HIGHLIGHT CHANGE ?
         BE    MAKESFEX      NO
         AH    R0,=H'1'      UP FIELD COUNT
         MVI   0(R1),X'41'   SET HIGHLIGHT ATTRIBUTE
         MVC   1(1,R1),SWCUROPT+2  COPY HIGHLIGHT OPTIONS
         NI    1(R1),X'07'   MASK                                87153
         TR    1(1,R1),TRATTR  MAKE ATTRIBUTE X'0' OR FN         87153
         LA    R1,2(,R1)
MAKESFEX STC   R0,1(,R15)    SET LIST COUNT
MAKESFEZ ST    R1,SWBUFNXT   UPDATE BUFFER ADDRESS
         MVC   SWPREOPT,SWCUROPT  UPDATE 'PREVIOUS' OPTION
         BR    R14           RETURN TO CALLER
         SPACE 1
MAKESF   CLM   R0,1,SWPREOPT  ANY CHANGE IN MAIN OPTION ?
         BNE   MAKESF7       YES; EXPAND AN SF
         STC   R0,SWPREOPT   SET NEW FIELD OPTION
         TM    SWPREOPT,FDFPROT  PROTECTED FIELD ?
         BZ    MAKESF7       NO; MUST EXPAND SF
         TM    SWPREOPT,FDFNDISP  DETECTABLE ?
         BNMR  R14           NO; RETURN WITHOUT SF
MAKESF7  L     R15,SWBUFNXT  GET OUTPUT BUFFER
         MVI   0(R15),X'1D'  MAKE START FIELD DEFINITION
         STC   R0,1(,R15)    SET OPTION BITS
         STC   R0,SWPREOPT   ALSO UPDATE 'PREVIOUS' OPTIONS
         NI    1(R15),X'3D'   MASK SUPPORTED OPTIONS
         TR    1(1,R15),TRADDR MAKE EBCDIC
         LA    R15,2(,R15)   SET NEW BUFFER ADDRESS
         LH    R0,FDWBOS     GET CURRENT SCREEN ADDRESS
         AH    R0,=H'1'      UP BY ONE
         TM    SWPREOPT,FDFNDISP  NON-DISPLAY FIELD ?            87165
         BNO   MAKESOF0      NO                                  87165
         XC    SWCUROPT+1(2),SWCUROPT+1  FAKE CURRENT AS DEFAULT 87165
MAKESOF0 STH   R0,FDWBOS     SET NEW SCREEN ADDRESS
         B     MAKESOFF      GO TO MAKE NEW BUFFER ADDRESS
         SPACE 1
MAKETEXT L     R15,SWBUFNXT  GET OUTPUT BUFFER
         SH    R1,=H'1'      MAKE LENGTH FOR EXECUTE
         BMR   R14           SKIP IF MINUS
         CH    R1,MAKETEXH+2  SEE IF TOO LONG
         BNH   *+8           NO
MAKETEXH LA    R1,255        SET FOR MAX SUPPORTED
         LH    R0,FDWBOS     GET CURRENT SCREEN POSITION
         CH    R0,SWSCRSIZ   WILL IT FIT ?
         BNL   MAKESOF0      NO; RETURN WITH ERROR
         AR    R0,R1         MAKE NEW ADDRESS
         AH    R0,=H'1'      ALLOW FOR PRIOR DECREMENTATION
         CH    R0,SWSCRSIZ   WOULD IT FIT ?
         BH    MAKESOF0      NO; RETURN ERROR
         EX    R1,MAKEMVC    MOVE
         CLI   SWLSTDAT,FDDASIS  SKIP TRANSLATE ?
         BE    MAKETEXN      YES
         CLI   SWLSTDAT,FDDCON   CONTROL DATA ?
         BE    MAKETEXN      YES; SKIP TRANSLATE
         L     R2,SWTRNLOW   TRANSLATE PRINTABLES ONLY
         EX    R1,MAKETR     TRANSLATE
MAKETEXN LA    R1,1(,R1)     MAKE LENGTH AGAIN
         LH    R0,FDWBOS     GET SCREEN OFFSET
         AR    R0,R1         MAKE NEW OFFSET
         AR    R15,R1         AND NEW BUFFER
         B     MAKESOF0      RESET
MAKEMVC  MVC   0(0,R15),0(R2)     MOVE TEXT
MAKETR   TR    0(0,R15),0(R2)     TRANSLATE IT
         SPACE 1
MAKELINE LH    R1,FDWBOS     GET CURRENT SCREEN OFFSET
         LH    R15,SWWIDTH   GET BYTES PER LINE
         AR    R1,R15        BUMP A LINE
         BCTR  R1,0          LESS ONE FOR TRUNCATION
         SLR   R0,R0
         DR    R0,R15        MAKE FULL LINES
         MR    R0,R15        MAKE OFFSET TO FULL LINE
         LR    R0,R1
         CH    R0,SWSCRSIZ   SET COND. CODE FOR FULL SCREEN
         BR    R14           RETURN TO CALLER
         POP   USING                                            GP04234
         SPACE 1
         PUSH  USING                                             86244
         DROP  R7                                                86244
MAKELGOT LA    R6,FDGOTO     POINT TO S ADDRESS
         IC    R0,FDTYPE     GET TYPE OPERAND
         BIX   ERR=MAKELINC,PFX=MAKEL,BASE=SCBASE,                     *
               LOC=(GOG,GEX,GBR,GTM,GCI,GCL)
         SPACE 1
MAKELGCL LA    R6,FDBVAR     GET COMPARE VARIABLE
         BAS   R14,MAKESADD  GET ADDRESS                        GP99026
         LR    R1,R6         SAVE ADDRESS
         LA    R6,FDBCLC     GET COMPARISON STRING
         BAS   R14,MAKESADD  GET ITS ADDRESS                    GP99026
         SLR   R4,R4
         ICM   R4,1,FDBLEN   GET COMPARE LENGTH
         BZ    MAKELINC      SKIP IF NONE
         BCTR  R4,0
         EX    R4,MAKELGXL   COMPARE
         B     MAKELGOI      GO TO COMMON
         SPACE 1
MAKELGCI LA    R6,FDBVAR     GET VARIABLE
         BAS   R14,MAKESADD  GET ADDRESS                        GP99026
         IC    R4,FDBIDA     GET COMPARE BYTE
         EX    R4,MAKELGXI   COMPARE
         B     MAKELGOI      GO TO COMMON
MAKELGXL CLC   0(0,R1),0(R6)   REMOTE CLC
MAKELGXI CLI   0(R6),0         REMOTE CLI
MAKELGXT TM    0(R6),0         REMOTE TM
         SPACE 1
MAKELGTM LA    R6,FDBVAR     GET COMPARISON VARIABLE
         BAS   R14,MAKESADD  GET ADDRESS                        GP99026
         IC    R4,FDBIDA     GET IMMEDIATE DATA
         EX    R4,MAKELGXT   TEST UNDER MASK
MAKELGOI GETCC R3            GET CONDITION CODE                  93168
         SRL   R3,28           IN LOW BYTE
         LA    R15,3         MAKE MASK
         NR    R3,R15          ISOLATE IT
         LA    R15,=AL1(0,2,4,4)  GET COND CODE/ BRANCH OFFSET
         AR    R3,R15        GET ADDRESS
         MVC   SWLSTCC,0(R3)  SAVE CODE
         LA    R6,FDBRE      GET START ADDRESS
         B     MAKELGBR      GO TO COMMON BRANCH CODE
         SPACE 1
MAKELGEX ICM   R0,15,SWLSTXEQ  ALREADY IN EXEC ?                GP03011
         BNZ   MAKELINC      YES; TREAT AS NOP
         LA    R6,FDBRE      GET FIRST FD ADDRESS
         BAS   R14,MAKETADD  SEE IF VALID                       GP99026
         TM    FDBRL,X'F0'   GET SECOND ADDRESS
         BZ    MAKELGEY        ONLY ONE - TREAT START AS END
         LA    R6,FDBRL      ELSE GET END
         BAS   R14,MAKESADD  RELOCATE IT                        GP99026
MAKELGEY LR    R5,R11        SWAP CURRENT ADDRESS OVER
         STM   R5,R6,SWLSTXEQ  SET EXEC RESTART AND END ADDRESSES
         LA    R6,FDBRE      GET START ADDRESS
         B     MAKELGOG        GO TO COMMON
         SPACE 1
MAKELGBR SLR   R3,R3
         IC    R3,SWLSTCC    GET PRIOR CODE
         AR    R6,R3         POINT TO BRANCH ADDRESS
MAKELGOG BAS   R14,MAKETADD  GET ADDRESS VALUE                  GP99026
         LR    R11,R6        COPY OVER
         B     MAKELIST      CONTINUE PROCESSING
         POP   USING                                             86244
         SPACE 1
***********************************************************************
*        INPUT VALIDITY CHECKING
         SPACE 1
GETANAL  TM    FDTYPE,FDFIN  INPUT FIELD ?
         BZ    MAKELINC      NO; SKIP OVER
         CLI   FDOLEN,0      VALID LENGTH ?
         BE    MAKELINC      NO; SKIP IT
         TM    FDTYPE,FDFIN+FDFCIN  CONDITIONAL INPUT ?          87156
         BNO   GETALNPR      NO                                  87156
         TM    FDWOPT,FDWLOCK  UNLOCKED ?                        87156
         BZ    MAKELINC      NO; IGNORE IT                       87156
GETALNPR LM    R3,R4,FDWSCAN  GET BUFFER SCAN ADDRESS AND LENGTH
         LA    R5,3(,R3)     POINT PAST ADDRESS
GETANECH LTR   R4,R4         ANY MORE TO DO ?
         BNP   GETATERR      NO                                  87159
         BAS   R14,MAKEPARS  EXTRACT ONE FIELD                  GP99026
         BM    GETATERR      NOTHING; GET NEXT FIELD             87159
         CLM   R0,3,FIWATB   SAME ATTRIBUTE ADDRESS ?
         BH    GETATERR      NO; PASSED IT ALREADY               87159
         BNE   GETANECH      GET ANOTHER ONE
         CR    R2,R5         DID WE MATCH THE FIRST FIELD ?
         BNE   *+8           NO
         STM   R3,R4,FDWSCAN UPDATE THE SCAN POINTERS
         MVI   FIWLEN,0      RESET LENGTH
         LTR   R1,R1         ANY INPUT LENGTH ?                  87159
         BP    GETANSET      YES; SET FLAGS                      87159
         TM    FIWFG,FIWFERR  PRIOR ERROR IN THIS FIELD ?        87159
         BNZ   GETATERN      RESET ERROR IF NULL INPUT           87159
GETANSET NI    FIWFG,FIWFINT+FIWFINV  RESET ERROR FLAG, ETC.     87159
         OI    FIWFG,FIWFPEN  SHOW FIELD MODIFIED
         OC    FDWFG,FIWFG   MERGE INTO MASTER FLAG
         TM    SWLSTDO,DOTWO  FIRST OR OTHER FIELD ?             86338
         BZ    *+12          FIRST (CONTROL)                     86338
         OI    FDWFG,FDWFF2  SHOW NON-FIRST FIELD CHANGED        86338
         B     *+8                                               86338
         OI    FDWFG,FDWFF1  SHOW FIRST (CONTROL) FIELD CHANGED  86338
         LTR   R0,R1         IS INPUT LENGTH ZERO ?
         BNZ   GETALFLD      NO; PROCESS
         TM    SWOPT7,FDFPROT  PROTECTED FIELD ?
         BNZ   MAKELINC      YES; JUST SET DETECTED
GETALFLD L     R15,SWLSTLEN  LOAD MAXIMUM LENGTH                 87313
         MIN   R0,(R15)      GET SMALLER LENGTH                  87313
         SH    R15,=H'2'     MAKE EXECUTE LENGTH FOR CLEAR       87313
         BM    GETANCLR      SKIP IF TOO SHORT                   87313
         MVI   FIWTEXT,C' '  PREPARE FOR MOVE                    87313
         EX    R15,GETABMVC  BLANK ENTIRE FIELD                  87313
GETANCLR LR    R1,R2         GET INPUT START
         COBAL DBK           DEBLANK AND DENULL                 GP99026
         BP    GETALCLU      OK; HAVE SOMETHING
         LA    R0,1          FAKE ONE BLANK
         LA    R1,=C' '      POINT TO ONE BLANK
GETALCLU LR    R14,R0        TRANSFER TO BETTER REGISTER
         STC   R14,FIWLEN    SET INPUT LENGTH
         BCTR  R14,0
         LR    R15,R1        COPY INPUT ADDRESS
         EX    R14,GETALMVC  MOVE TEXT TO FIW
         TM    FDEDIT,FDFUP  UPPER CASE FORCED ?                 86244
         BNZ   GETATRA       YES; TRANSLATE                      86244
         CLI   FDDATA,FDDADDR  NON-CHARACTER FIELD ?             86244
         BL    GETANTR       NO; SKIP TRANSLATION                86244
GETATRA  L     R15,=A(PNTRTAB)  GET UPPER-CASE TABLE             86244
         EX    R14,GETALTRA  TRANSLATE TO UPPER                  86244
GETANTR  OI    FIWFG,FIWFTXT SHOW TEXT PRESENT
         TM    FDWOPT,FDWOHELP  LOOK FOR HELP TEXT ?             88221
         BZ    GETANNHP      NO                                  88221
         CLI   FIWLEN,1      LENGTH=1 ?                          88221
         BE    GETANTHQ      YES                                 88221
         CLI   FIWLEN,4      LENGTH=4 ?                          88221
         BNE   GETANNHP      NO; NOT HELP                        88221
         ICM   R15,15,FIWTEXT  RELOAD TEXT                       88221
         O     R15,=CL4' '   UPPER CASE                          88221
         C     R15,=C'HELP'  HELP REQUEST ?                      88221
         BNE   GETANNHP      NO                                  88221
         B     GETANSHP      YES                                 88221
GETANTHQ CLI   FIWTEXT,C'?'  HELP REQUEST ?                      88221
         BNE   GETANNHP      NO                                  88221
GETANSHP OI    FIWFG,FIWFHLP  SET (POSSIBLE) HELP REQUEST        88221
GETANNHP OC    FDWFG,FIWFG   MERGE INTO MASTER FLAG
         CLI   FDDATA,FDDADDR  CHARACTER FIELD ?
         BL    MAKELINC      YES; NO SPECIAL CHECKING
         SLR   R15,R15
         IC    R15,FDDATA    GET DATA TYPE
         BCTR  R15,0         MAKE RELATIVE
         COBAL TREVNOC       SEE IF DATA VALID FOR TYPE
         BNM   MAKELINC      YES
GOTERR   OI    FIWFG,FIWFERR SHOW TEXT IN ERROR
         TM    FDWFG,FIWFERR  PRIOR ERROR ?
         BNZ   MAKELINC      YES; LEAVE CURSOR
         MVC   FDWICUD,FIWATB  POINT CURSOR AT (FIRST) BAD FIELD
         OI    FDWFG,FIWFERR SHOW ERROR FOUND
         B     MAKELINC      DO NEXT FIELD
         SPACE 1                                                 87159
GETATERR TM    FIWFG,FIWFERR PRIOR ERROR ?                       87159
         BZ    GETATERX      NO; FIELD NOT MODIFIED              87159
GETATERN NI    FIWFG,FIWFINT+FIWFINV  CANCEL ERROR AND INPUT TEXT
GETATERX OC    FDWFG,FIWFG   MAINTAIN EXISTING INPUT FLAGS       87159
         TM    FIWFG,FIWFPEN FIELD MODIFIED PREVIOUSLY ?         87159
         BZ    MAKELINC      NO; GET NEXT FIELD                  87159
         TM    SWLSTDO,DOTWO  FIRST OR OTHER FIELD ?             87159
         BZ    *+12          FIRST (CONTROL)                     87159
         OI    FDWFG,FDWFF2  SHOW NON-FIRST FIELD CHANGED        87159
         B     *+8                                               87159
         OI    FDWFG,FDWFF1  SHOW FIRST (CONTROL) FIELD CHANGED  87159
         B     MAKELINC      GET ANOTHER                         87159
GETALMVC MVC   FIWTEXT(0),0(R15)  MOVE/BLANK USER'S INPUT FIELD
GETALTRA TR    FIWTEXT(0),0(R15)  UPPER-CASE                     86244
GETABMVC MVC   FIWTEXT+1(0),FIWTEXT  BLANK INTERMEDIATE FIELD    87313
         SPACE 1
MAKEPARS LA    R2,3(,R3)     PROVISIONALLY POINT TO DATA
         CLI   0(R3),X'11'   SBA ?
         BNE   MAKEPARB      NO; ERROR IN DATA OR ?
         LADD  R0,1(R3)      GET ADDRESS OF ATB+1
         CH    R0,SWSCRSIZ   VALID ADDRESS ?
         BL    *+6           YES
         SLR   R0,R0         ELSE KILL IT
         SLR   R1,R1         SET OUTPUT LENGTH
         LA    R3,3(R3,0)    POINT TO DATA
         SH    R4,*-2        MAKE REMAINING LENGTH
         BM    MAKEPARB      ERROR
         BZ    MAKEPARX      NULL DATA - EXIT
         LA    R15,1         CONSTANT
MAKEPART CLI   0(R3),X'11'   ANOTHER SBA ?
         BE    MAKEPARX      YES; QUIT
         AR    R1,R15        UP LENGTH
         AR    R3,R15        UP ADDRESS
         SR    R4,R15        NEW RESIDUAL LENGTH
         BP    MAKEPART      TRY AGAIN
MAKEPARX LTR   R1,R1         SET CONDITION CODE
         BR    R14
MAKEPARB LNR   R1,R14
         B     MAKEPARX      SET ERROR
         SPACE 1
***********************************************************************
*        INPUT PROCESSING - MOVE SCREEN DATA TO MEMORY
         SPACE 1
GETMOVE  TM    FDTYPE,FDFIN  INPUT FIELD ?
         BZ    MAKELINC      NO; SKIP OVER
         TM    FIWFG,FIWFTXT  TEXT ?
         BZ    MAKELINC      NO
         TM    FIWFG,FIWFERR  UNCORRECTED ERROR ?
         BNZ   MAKELINC      YES; DON'T MERGE BAD DATA
         TM    FDWOPT,FDWMKEEP  KEEP TEXT FLAGS ?                87159
         BNZ   *+8           YES                                 87159
         NI    FIWFG,FIWFINT+FIWFINV  RESET TEXT FLAGS           87159
         SLR   R0,R0
         ICM   R0,1,FIWLEN   LOAD AND TEST LENGTH
         BZ    MAKELINC      NONE ?
         TM    FDWOPT,FDWMKEEP  KEEP TEXT FLAGS ?                87159
         BNZ   *+8           YES                                 87159
         MVI   FIWLEN,0      RESET AVAILABLE TEXT LENGTH         87159
         SLR   R15,R15
         ICM   R15,1,FDDATA  GET DATA TYPE
         BZ    *+6           LEAVE IT ALONE IF ZERO
         BCTR  R15,0         ELSE FIX FOR CONVERSION
         LR    R2,R6         COPY USER'S OUTPUT ADDRESS
         ICM   R3,15,SWLSTLTX   GET USER'S SOURCE LENGTH
         BNZ   GETMHLEN      OK
         LR    R3,R15        COPY TYPE
         A     R3,=A(CONVLNDF)  POINT TO DEFAULT LENGTH
         ICM   R3,8,0(R3)    LOAD AND TEST LENGTH
         BZ    MAKELINC      SKIP ZERO LENGTH FIELD
         SRL   R3,24         FIX UP
GETMHLEN LA    R1,FIWTEXT    GET TEXT
         CLI   FDDATA,FDDADDR  CHARACTER MODE ?
         BL    GETGAUCH      YES; LEFT-JUSTIFY
         COBAL TREVNOC       CONVERT TO DATA
         BM    GOTERR        BUT HOW ?
         CR    R1,R3         NEED TO TRUNCATE ?
         BE    GETMMOVE      NO; PRECISE MATCH
         BL    GETMFILL      NEED TO ZERO FILL
         AR    R0,R1         GET END BYTE+1
         SR    R0,R3         MAKE FIRST BYTE TO MOVE
         B     GETMMOVE      GO TO MOVE
GETMFILL LR    R15,R3        GET LENGTH TO FILL
         SR    R15,R1
         BCTR  R15,0
         EX    R15,GETMMXC   CLEAR IT
         AR    R2,R3         GET END BYTE+1
         SR    R2,R1         GET FIRST BYTE TO MOVE TO
GETMMOVE BCTR  R3,0          EXEC LENGTH
         LR    R1,R0         COPY START
         EX    R3,GETMMMVC   MOVE
         TM    SWLSTDO,DOTWO  FIRST OR OTHER FIELD ?             86338
         BZ    *+12          FIRST (CONTROL)                     86338
         OI    FDWFG,FDWFF2  SHOW NON-FIRST FIELD CHANGED        86338
         B     *+8                                               86338
         OI    FDWFG,FDWFF1  SHOW FIRST (CONTROL) FIELD CHANGED  86338
         B     MAKELINC      DONE
GETMMXC  XC    0(0,R2),0(R2)   LEFT-ZERO FILL
GETMMMVC MVC   0(0,R2),0(R1)   MOVE DATA TO USER
GETALTR  TR    0(0,R6),0(R15)  MAKE PROPER CASE
         SPACE 1
GETGAUCH DS    0H
         SWAPR R0,R1         FLIP R0 AND R1
         LR    R6,R2         SAVE START
         LR    R14,R3        AND LENGTH
         ICM   R1,8,=C' '    SET FOR BLANK FILL
         MVCL  R2,R0         MOVE DATA TO USER'S AREA
         TM    FDEDIT,FDFUP  UPPER CASE FORCED ?
         BZ    MAKELINC      NO; RETURN AS IS
         L     R15,=A(PNTRTAB)  GET UPPER CASE TRANSLATE TABLE
         BCTR  R14,0         SET EXECUTE LENGTH
         EX    R14,GETALTR   TRANSLATE
         B     MAKELINC
         SPACE 2                                                 87173
*        TAKE A USER EXIT FOR EVERY FD ENTRY                     87173
*                                                                87173
GETLOOP  TM    SWXEXIT,X'80' FDIN-ONLY REQUEST ?                 87173
         BZ    GETLOOPS      NO; PASS ALL                        87173
         TM    FDTYPE,FDFIN  INPUT TYPE ?                        87173
         BZ    MAKELINC      NO; DO NEXT                         87173
GETLOOPS ST    R11,SWXPARM+4 PASS FD ADDRESS
         STM   R9,R10,SWXPARM+8  ALSO FIW, FDW
         STM   R2,R13,SWXMYSV  SAVE MY REGISTERS                 87173
         LM    R15,R1,SWXEXIT  LOAD EXIT ADDRESS, PARMS          87173
         LM    R2,R13,USERREGS  LOAD USER'S PARAMETERS
         SYNCH (R15)         TAKE EXIT
         LM    R2,R13,SWXMYSV-SWXPARM(R1)  RESTORE MY REGISTERS
         B     MAKELINC      PROCESS NEXT ENTRY                  87173
         DROP  R7,R9,R11
         SPACE 1
SFPROT   EQU   X'20'         PROTECTED FIELD BIT
SFINT    EQU   X'08'         INTENSE
SFNUM    EQU   X'10'         NUMERIC ONLY
SFLPEN   EQU   X'04'         LIGHT-PEN
SFNDIS   EQU   X'0C'         NON-DISPLAY
SFMDT    EQU   X'01'         MODIFIED DATA TAG                   87159
         SPACE 1
         LTORG ,                                                 87159
         POP   USING                                            GP97231
         SPACE 1
SCPAGE   SCHEAD ,            WRITE A BUFFER AND READ RESPONSE
         B     SCPAGE2       FINAGLE FOR ADDRESSABILITY          87159
         SPACE 1                                                 87159
TRATTR   DC    X'00F1F2F3F4F5F6F7'  ATTRIBUTE TRANSLATION        87153
TRADDR   DC    X'40C1C2C3C4C5C6C7C8C94A4B4C4D4E4F'   0 - 15
         DC    X'50D1D2D3D4D5D6D7D8D95A5B5C5D5E5F'  16 - 31
         DC    X'60E1E2E3E4E5E6E7E8E96A6B6C6D6E6F'  32 - 47
         DC    X'F0F1F2F3F4F5F6F7F8F97A7B7C7D7E7F'  48 - 63
         SPACE 1                                                 87159
SCPAGE2  MVI   FDWIAID,1     PRESET FOR WRITE ERROR
         L     R2,FDWBIA     GET INPUT BUFFER ADDRESS
         LH    R3,FDWBIM     GET MAXIMUM INPUT SIZE
         CLRL  (R2),(R3),FILL=C' '   CLEAR INPUT BUFFER FOR DEBUGGING
         L     R2,FDWBOA     GET OUTPUT BUFFER ADDRESS
         L     R3,SWBUFNXT   GET NEXT AVAILABLE POSITION
         LR    R4,R3         SAVE END ADDRESS
         SR    R3,R2         GET LENGTH OF BUFFER
         SLR   R0,R0
         ICM   R0,3,FDWCUR   ANY CURSOR ADDRESS ?
         BNP   SCPAGNCU      NO; SKIP
         ICM   R14,8,=X'11'  MAKE SBA ORDER
         CH    R0,FDWBOS     ALREADY POSITIONED ?
         BE    SCPAGNIC      YES; SKIP
         STH   R0,FDWBOS     SET CURRENT SCREEN ADDRESS
         CH    R0,SWSCRSIZ   FITS ON SCREEN ?
         BNL   SCPAGNCU      NO; IGNORE
         STCM  R14,8,0(R4)   MAKE SBA
         STC   R0,2(,R4)     SET LOW PORTION OF BUFFER
         SRL   R0,6
         STC   R0,1(,R4)     AND HIGH PORTION
         NC    1(2,R4),=X'3F3F'   MASK
         TR    1(2,R4),TRADDR   TRANSLATE TO EBCDIC
         LA    R3,3(,R3)     INCREASE LENGTH
         LA    R4,3(,R4)       AND SET TO IC POSITION
SCPAGNIC MVI   0(R4),X'13'   ADD AN INSERT CURSOR
         LA    R3,1(,R3)     AND FIX THE LENGTH
SCPAGNCU TM    SWFLAG,SWFTSU  USE TPUT/TGET ?                    93344
         BNZ   SCPAGTSO      YES                                 93344
*DEFER*  MVC   SCRSAVE(PATDECBL),PATDECB  BUILD DECB             93344
         LA    R5,SWDCB      POINT TO DCB                        93344
         LR    R0,R10        RETURN FDW ADDRESS TO USER          93344
*DEFER*  WRITE SCRSAVE,TI,(R5),(R2),(R3),MF=E  WRITE SCREEN      93344
*DEFER*  WAIT  ECB=SCRSAVE   WAIT FOR RESPONSE                   93344
*DEFER*  UNPOST SCRSAVE                                          93358
         L     R2,FDWBIA     GET INPUT BUFFER ADDRESS            93344
         LH    R3,FDWBIM     GET MAXIMUM INPUT SIZE              93344
         LR    R0,R10        RETURN FDW ADDRESS TO USER          93344
*DEFER*  READ  SCRSAVE,TS,(R5),(R2),(R3),MF=E  GET THE RESPONSE  93358
*DEFER*  WAIT  ECB=SCRSAVE   WAIT FOR RESPONSE                   93344
*DEFER*  UNPOST SCRSAVE                                          93358
         LH    R1,SCRSAVE+6  LOAD LENGTH READ                    93344
         SLR   R15,R15       SET GOOD COMPLETION                 93361
         TM    SCRSAVE,X'3F'  WAS IT GOOD?                       93361
         BO    SCPAGETT      YES; CONTINUE                       93361
         LA    R15,8         ELSE SET BAD                        93361
         B     SCPAGETT      JOIN COMMON                         93344
SCPAGTSO TPUT  (R2),(R3),FULLSCR  DO FULL-SCREEN WRITE
         LTR   R15,R15       ACCEPTED ?
         BNZ   SCPAGERR      NO; WRITE ERROR
         L     R2,FDWBIA     GET INPUT BUFFER ADDRESS
         LH    R3,FDWBIM     GET MAXIMUM INPUT SIZE
         LA    R5,MAXWAIT*2  LOAD MAXIMUM SECONDS TO WAIT
SCPAGET  TGET  (R2),(R3),ASIS,NOWAIT  GET AVAILABLE INPUT
         CH    R15,=H'4'     BUFFER USED ?
         BNE   SCPAGETT      YES; OR ERROR - RETURN
         BCT   R5,SCPAGEW    CONTINUE IF WAIT NOT EXPIRED
         MVI   FDWIAID,0     SET FOR NO INPUT
         B     SCPAGERR      RETURN WAIT EXPIRED
SCPAGEW  STIMER WAIT,BINTVL==A(100/2) WAIT ONE SECOND
         B     SCPAGET       AND TRY AGAIN
SCPAGETT LR    R3,R1         SET INPUT LENGTH
         ST    R3,FDWBIL     SET CURRENT INPUT LENGTH
         MVI   FDWIAID,2     PRESET FOR READ ERROR
         BXH   R15,R15,SCPAGERR  QUIT WITH ERROR
         MVC   FDWIAID,0(R2)  SAVE RAW AID
         CLI   FDWIAID,X'01'   TEST-REQUEST INPUT ?
         BNE   SCPAGNTS      NO
         MVI   FDWIAID,X'F0'  MOVE DOCUMENTED VALUE
         LA    R2,1(,R2)     CHEAT
         BCTR  R3,0          FAKE REST OF BUFFER
         B     SCPAGNOC      USE DEFAULT CURSOR ADDRESS
SCPAGNTS MVC   FDWICUD,1(R2)  MOVE CURSOR ADDRESS
         CLC   =X'FFFF',FDWICUD  DEBUG MODE ?
         BNE   SCPAGNOK      NO
         CLC   =X'FFFF',4(R2)  ALSO DUMMY ATB+1 ADDRESS ?
         BNE   SCPAGNOC      NO; JUST SET CURSOR ADDRESS
         MVC   4(2,R2),FDWCUR  USE LAST CURSOR ADDRESS
SCPAGNOC MVC   FDWICUD,FDWCUR  DEFAULT TO MAIN INPUT FIELD
SCPAGNOK LADD  R0,FDWICUD    GET SCREEN ADDRESS IN BINARY
         STH   R0,FDWICUD    STASH BACK IN BINARY FORM
         LA    R2,3(,R2)     SKIP OVER PREFIX
         SH    R3,=H'3'      AND ADJUST LENGTH
         BP    *+6           OK
         SLR   R3,R3         ELSE SET 0
         STM   R2,R3,FDWSCAN SET SCANNER ADDRESS/LENGTH
         MVC   FDWICOD,FDWIAID  GET RAW AID
         TM    FDWICOD,X'C0' HAVE WE BEEN HERE BEFORE ?
         BZ    SCPAGY24      BUT HOW ?
         NI    FDWICOD,X'3F'   REMOVE ZONE BITS                  88211
         XI    FDWICOD,X'20'   MAKE COMPATIBLE WITH OLD CODE     88211
*        AID REMAPPING:                                          88211
*        00 - NO AID         06 - OID            07 - MS READER  88211
*        0B - PA3            0C - PA1            0D - CLEAR      88211
*        0E - PA2            10 - TEST-REQ (TREATED AS PFK 0 !)  88211
*        11/1C - PFK 1-12    1D - ENTER          1E - LIGHT-PEN  88211
*        21/2C - PFK 13-24 (UNLESS FOLDED TO 11/1C)              88211
*        08 - NO AID (PRINTER READ BUFFER!!) - SHOULD NOT OCCUR  88211
*        70 - (BEFORE NI) PROTOCOL CONVERTER PASS-THROUGH (ASCII)
*                                                                88211
         TM    FDWOPT,FDWKEY12  USE ONLY 12 KEYS ?               88211
         BZ    SCPAGY24      NO; LEAVE 24                        88211
         CLI   FDWICOD,X'21'  PFK 13 ?                           88211
         BL    SCPAGY24      LOWER                               88211
         CLI   FDWICOD,X'2C'  PFK 24 ?                           88211
         BH    SCPAGY24      NO; HIGHER                          88211
         XI    FDWICOD,X'30'   FOLD PFK13-24 INTO KEY 1-12       88211
SCPAGY24 LR    R0,R3         SET INPUT LENGTH AS RETURN CODE
         B     SCPAGEXX      YES; RETURN CC>0 OR 0
SCPAGERR LNR   R0,R12        MAKE NEGATIVE
SCPAGEXX LTR   R0,R0         SET THE CONDITION CODE
         SCEXIT ,            AND RETURN
         DROP  ,
         SPACE 1                                                 93344
*DEFER*  WRITE PATDECB,TI,1,2,3,MF=L                             93344
*ATDECBL EQU   *-PATDECB                                         93344
         SPACE 1
         LTORG ,
         POP   USING                                            GP97231
         EJECT ,                                                 87312
***********************************************************************
*                                                                     *
*        SIMPLE TEXT SCANNER TO COMPLEMENT 3270 INPUT - USED FOR TTY  *
*        AND COMMAND INPUT                                            *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87312
SCSCAN   SCHEAD ,            R10 HAS FDW ADDRESS                 87312
         USING FDW,R10                                           87312
         USING SAVE,R13                                          87312
         L     R11,FDWSCAC   LOAD SCANNER CONTROL LIST           87312
         USING FDSSECT,R11   DECLARE IT                          87312
         LM    R4,R5,FDWSCAN GET SCAN START AND LENGTH           87312
SCANLOOP BAS   R14,NONBLANK  GET NEXT START                      87312
           B   SCANEXIT      NO MORE                             87312
         LR    R7,R11        SAVE LIST START                     87312
         BAS   R9,SCANWORD   ISOLATE ONE WORD                    87312
           B   SCANERR       ERROR                               87312
         LM    R2,R3,SCNKEY  GET ADDRESS AND LENGTH OF KEYWORD   87312
         CLC   SCNSEP,SCANSTOP+C'='  KEYWORD PARAMETER ?         87361
         BNL   SCANLKEY      YES; LOCATE KEY                     87361
         TM    FDSFG,FDSFPOS   POSITIONAL PARAMETER ?            87312
         BNZ   SCAMPOS       YES; MOVE IT                        87312
SCANLKEY SLR   R14,R14                                           87312
         LR    R6,R3         PRESERVE KEY LENGTH                 87361
SCANLLST ICM   R14,1,FDSLEN  LOAD AND TEST TEXT LENGTH           87312
         BM    SCANERR       NOT FOUND                           87312
         LR    R3,R6         RESTORE KEY LENGTH                  87361
         SH    R3,=H'1'      ANY LENGTH ?                        87312
         BM    SCANERR       NO; TOO BAD                         87312
         CR    R3,R14        CHECK KEYWORD AGAINST TABLE         87312
         BH    SCANLUMP      TOO LONG; TRY NEXT ENTRY            87312
         BE    SCANLCLC      COMPARE                             87312
         TM    FDSFG,FDSFLONG  EXACT LENGTH ?                    87312
         BNZ   SCANLUMP      YES; DO AS IS                       87312
SCANLCLC EX    R3,SCANXCLC   MATCH ?                             87312
         BE    SCAMPOV       YES; MOVE                           87312
SCANLUMP BAS   R9,SCANBUMP   GET NEXT TABLE ENTRY                88002
           B   SCANERR       SHOULD NOT HAPPEN                   88002
         B     SCANLLST      TRY IT                              87312
SCANXCLC CLC   FDSTEXT(0),0(R2)  KEYWORD MATCH ?                 87312
SCAMPOS  LM    R2,R3,SCNVAL  GET VALUABLES                       87361
         LTR   R3,R3         ANY LENGTH ?                        87361
         BP    SCAMPOV       YES; PROCESS                        87361
         TM    FDSFG,FDSFMOVE  L=0 AND REPLACEMENT TEXT ?        87361
         BZ    SCANPOP       NO; JUST ADVANCE TO NEXT POSITIONAL 87361
SCAMPOV  LM    R2,R3,SCNVAL  GET ADDRESS AND LENGTH OF VALUE TO MOVE
         ICM   R14,1,FDSLEN  LAST ENTRY ?                        87361
         BM    SCANERR       YES; GARBAGE INPUT                  87361
         TM    FDSFG,FDSFMOVE  REPLACEMENT TEXT IN FDSCAN ?      87312
         BZ    SCAMPOM       NO                                  87312
         SLR   R14,R14                                           87312
         IC    R14,FDSLEN    GET ENTRY LENGTH                    87312
         IC    R3,FDSRLEN+1(R14) GET LENGTH OF MOVE TEXT         87312
         LA    R3,1(,R3)     CHANGE TO LENGTH FROM L-1           87312
         LA    R2,FDSRTXT+1(R14) POINT TO REPLACEMENT TEXT       87312
SCAMPOM  LA    R6,FDSFDAD    POINT TO FDIN ADDRESS               87312
         BAS   R14,SCANTADD  GET FDIN ADDRESS                    87312
         USING FDSECT,R6     DECLARE IT                          87312
         TM    FDTYPE,FDFPRT OPT7/OPT9 MISSING (PRT FORMAT) ?    87313
         BNO   *+8           NO; NORMAL                          87313
         SH    R6,=H'2'      FAKE IT OUT                         87313
         SLR   R9,R9                                             87312
         ICM   R9,3,FDIOFF   GET FIW OFFSET                      87312
         A     R9,FDWFWA     RELOCATE                            87312
         USING FIWSECT,R9    DECLARE IT                          87312
         NI    FIWFG,FIWFINT+FIWFINV  RESET ERROR FLAG, ETC.     87312
         OI    FIWFG,FIWFPEN  SHOW FIELD MODIFIED                87312
         OC    FDWFG,FIWFG   MERGE INTO MASTER FLAG              87312
         MVI   FIWLEN,0      SET TEMPORARY LENGTH                87312
         C     R11,FDWSCAC   FIRST OR OTHER FIELD ?              87312
         BE    *+12          FIRST (CONTROL)                     87312
         OI    FDWFG,FDWFF2  SHOW NON-FIRST FIELD CHANGED        87312
         B     *+8                                               87312
         OI    FDWFG,FDWFF1  SHOW FIRST (CONTROL) FIELD CHANGED  87312
         LTR   R0,R3         IS INPUT LENGTH ZERO ?              87312
         BNZ   SCANLFLD      NO; PROCESS                         87312
         TM    FDOPT7,FDFPROT  PROTECTED FIELD ?                 87312
         BNZ   SCANPOP       YES; JUST SET DETECTED              87312
SCANLFLD SLR   R15,R15                                           87312
         IC    R15,FDOLEN    GET (MANDATORY) FDIN OUTPUT LENGTH  87312
         MIN   R0,(R15)      LOAD SMALLER OF ACTUAL LENGTH       87312
         SH    R15,=H'2'     MAKE EXECUTE LENGTH FOR CLEAR       87313
         BM    SCANNCLR      SKIP IF TOO SHORT                   87313
         MVI   FIWTEXT,C' '  PREPARE FOR MOVE                    87313
         EX    R15,SCANBMVC  BLANK ENTIRE FIELD                  87313
SCANNCLR LR    R1,R2         GET INPUT START                     87312
         COBAL DBK           DEBLANK AND DENULL                  87312
         BP    SCANLCLU      OK; HAVE SOMETHING                  87312
         LA    R0,1          FAKE ONE BLANK                      87312
         LA    R1,=C' '      POINT TO ONE BLANK                  87312
SCANLCLU LR    R14,R0        TRANSFER TO BETTER REGISTER         87312
         STC   R14,FIWLEN    SET INPUT LENGTH                    87312
         BCTR  R14,0                                             87312
         LR    R15,R1        COPY INPUT ADDRESS                  87312
         EX    R14,SCANLMVC  MOVE TEXT TO FIW                    87312
         TM    FDEDIT,FDFUP  UPPER CASE FORCED ?                 87312
         BNZ   SCANTRA       YES; TRANSLATE                      87312
         CLI   FDDATA,FDDADDR  NON-CHARACTER FIELD ?             87312
         BL    SCANNTR       NO; SKIP TRANSLATION                87312
SCANTRA  L     R15,=A(PNTRTAB)  GET UPPER-CASE TABLE             87312
         EX    R14,SCANLTRA  TRANSLATE TO UPPER                  87312
SCANNTR  OI    FIWFG,FIWFTXT SHOW TEXT PRESENT                   87312
         TM    FDWOPT,FDWOHELP  LOOK FOR HELP TEXT ?             88221
         BZ    SCANNNHP      NO                                  88221
         CLI   FIWLEN,1      LENGTH=1 ?                          88221
         BE    SCANNTHQ      YES                                 88221
         CLI   FIWLEN,4      LENGTH=4 ?                          88221
         BNE   SCANNNHP      NO; NOT HELP                        88221
         ICM   R15,15,FIWTEXT  RELOAD TEXT                       88221
         O     R15,=CL4' '   UPPER CASE                          88221
         C     R15,=C'HELP'  HELP REQUEST ?                      88221
         BNE   SCANNNHP      NO                                  88221
         B     SCANNSHP      YES                                 88221
SCANNTHQ CLI   FIWTEXT,C'?'  HELP REQUEST ?                      88221
         BNE   SCANNNHP      NO                                  88221
SCANNSHP OI    FIWFG,FIWFHLP  SET (POSSIBLE) HELP REQUEST        88221
SCANNNHP OC    FDWFG,FIWFG   MERGE INTO MASTER FLAG              87312
         CLI   FDDATA,FDDADDR  CHARACTER FIELD ?                 87312
         BL    SCANPOP       YES; NO SPECIAL CHECKING            87312
         SLR   R15,R15                                           87312
         IC    R15,FDDATA    GET DATA TYPE                       87312
         BCTR  R15,0         MAKE RELATIVE                       87312
         COBAL TREVNOC       SEE IF DATA VALID FOR TYPE          87312
         BNM   SCANPOP       YES                                 87312
SCANFERR OI    FIWFG,FIWFERR SHOW TEXT IN ERROR                  87312
         TM    FDWFG,FIWFERR  PRIOR ERROR ?                      87312
         BNZ   SCANPOP       YES; LEAVE CURSOR                   87312
         OI    FDWFG,FIWFERR SHOW ERROR FOUND                    87312
         OC    FIWATB,FIWATB KNOWN ATTRIBUTE ADDRESS ?           87313
         BZ    SCANPOP       NO; CAN'T SET                       87313
         MVC   FDWICUD,FIWATB  POINT CURSOR AT (FIRST) BAD FIELD 87312
SCANPOP  TM    FDSFG,FDSFPOS  DID WE JUST DO A POSITIONAL ?      87361
         BNZ   SCANPOP7      YES; POSITION AFTER THIS TABLE ENTRY
         LR    R11,R7        RESET TABLE START                   87312
SCANPOP1 TM    FDSFG,FDSFPOS IS CURRENT POSITIONAL ?             88002
         BZ    SCANLOOP      NO; RETURN                          88002
         BAS   R9,SCANBUMP   YES; SKIP IT                        88002
           B   SCANLOOP      RETURN IF END OF TABLE              88002
         B     SCANPOP1      ELSE TEST NEXT                      88002
SCANPOP2 TM    FDSFG,FDSFPOS POSITIONAL PARAMETER ?              87312
         BZ    SCANLOOP      NO; RE-USE SAME TABLE               87312
         BAS   R9,SCANBUMP   GET NEXT TABLE ENTRY                88002
           B   SCANLOOP      RETURN IF LAST ENTRY                88002
         TM    FDSFG-FDSSECT(R7),FDSFPOS2  CHAINED POSITIONAL ?  87360
         BNO   SCANLOOP      NO                                  87360
         CLC   SCNSEP,SCANSTOP+C','  BLANK TERMINATOR ?          87360
         BNL   SCANLOOP      NO; KEEP PROCESSING                 87360
SCANPOP7 LR    R7,R11        ADVANCE 'OLD' POINTER               87360
         B     SCANPOP2      FLUSH RELATED POSITIONALS           87360
SCANLMVC MVC   FIWTEXT(0),0(R15)  MOVE/BLANK USER'S INPUT FIELD  87312
SCANLTRA TR    FIWTEXT(0),0(R15)  UPPER-CASE                     87312
SCANBMVC MVC   FIWTEXT+1(0),FIWTEXT  BLANK INTERMEDIATE FIELD    87313
         SPACE 1                                                 88002
SCANBUMP SLR   R14,R14                                           87312
         SLR   R15,R15                                           87312
         ICM   R14,1,FDSLEN  GET TEXT LENGTH                     87360
         BMR   R9            ALL DONE                            87360
         TM    FDSFG,FDSFMOVE  REPLACEMENT TEXT ?                87312
         BZ    SCANBUMS      NO                                  87312
         IC    R15,FDSRLEN+1(R14)  GET REPLACEMENT LENGTH        87312
         LA    R14,2(R14,R15)  ADJUST LENGTH                     87312
SCANBUMS LA    R11,FDSTEXT+1(R14)  NEXT TABLE ENTRY              87312
         B     4(,R9)        TAKE GOOD RETURN                    88002
         SPACE 1
SCANTADD TM    0(R6),X'F0'   VALID REGISTER ?
         BZ    SCANPOP       NO; SKIP THIS ENTRY                 87312
SCANSADD CLI   0(R6),X'DF'   VALID REGISTER ?
         BH    SCANPOP       NO; SKIP THIS ENTRY                 87312
         ST    R1,DB         SAVE USER'S VALUE
         ICM   R6,3,0(R6)    GET S FORM OF ADDRESS
         LR    R1,R6         COPY
         N     R6,=X'00000FFF'  GET DISPLACEMENT
         SRL   R1,10
         N     R1,=X'0000003C'  GET BASE *4
         BZ    SCANSADT      DON'T ADD R0 FOR ABSOLUTE ADDRESS
         AL    R6,USERREGS(R1)  GET USER'S VALUE
SCANSADT LA    R6,0(,R6)     KILL HIGH BYTE
         L     R1,DB         RESTORE USER'S REGISTER
         BR    R14           RETURN TO CALLER
         SPACE 1                                                 87312
NONBLANK LTR   R5,R5         ANY MORE TEXT ?                     87312
         BNPR  R14           NO; QUIT                            87312
NONBLAND CLI   0(R4),C' '    NON-BLANK FOUND ?                   87312
         BH    4(,R14)       YES; RETURN IT                      87312
         LA    R4,1(,R4)     UP                                  87312
         BCT   R5,NONBLAND   TRY AGAIN                           87312
         BR    R14           NO MORE                             87312
         SPACE 1                                                 87312
SCANWORD BAS   R14,NONBLANK  GET PARAMETER START                 87312
         B     SCANEXIT      EXIT FROM FUNCTION                  87312
         STM   R4,R5,SCNKEY  SAVE PROVISIONAL KEYWORD ADDRESS/LENGTH
         STM   R4,R5,FDWSCAN SET PROV. ERROR ADDRESS             87360
         LA    R1,0(R4,R5)   SET END +1 OF TEXT STRING           87312
         SLR   R2,R2         PRESET END CODE                     88002
         LR    R15,R5        COPY LENGTH                         87312
         SH    R15,=H'1'     ADJUST FOR EXECUTE                  87312
         EX    R15,SCANTRT   LOOK FOR END SEPARATOR              87312
         STC   R2,SCNSEP     SAVE SEPARATOR VALUE                87360
         LR    R15,R1        GET HIT ADDRESS                     87312
         SR    R15,R4        LESS START SCAN ADDRESS             87312
         ST    R15,SCNKEY+4  SAVE LENGTH FOR CALLER              87312
         LA    R4,1(,R1)     SET NEXT SCAN POSITION              87312
         SR    R5,R15                                            87312
         SH    R5,=H'1'      RESIDUAL SCAN LENGTH                87312
         BM    SCANPOS       CAN'T BE KEYWORD                    88002
         CLM   R2,1,SCANSTOP+C'='  PARAMETER FOLLOWS ?           87312
         BNL   SCANVAL       YES                                 87312
SCANPOS  MVI   DBSAVE,C' '                                       87312
         MVC   DBSAVE+1(255),DBSAVE  MAKE ALL BLANKS             87312
         LM    R1,R2,SCNKEY                                      87312
         STM   R1,R2,SCNVAL  KEY DOUBLES AS VALUE                87312
         SH    R2,=H'1'      MAKE EXECUTE LENGTH                 87312
         BNP   4(,R9)        RETURN OK; WITH ZERO LENGTH         87361
         EX    R2,SCANWOC    MAKE UPPER CASE                     87312
         LA    R1,DBSAVE                                         87312
         ST    R1,SCNKEY     PASS UPPER CASE KEYWORD             87312
         B     4(,R9)        RETURN                              87312
SCANVAL  BAS   R14,NONBLANK                                      87312
         NOP   0                                                 87312
         LA    R1,0(R4,R5)   PRESET THE END ADDRESS              87312
         LR    R15,R5        GET RESIDUAL LENGTH                 87312
         ST    R4,SCNVAL     RETURN START                        87312
         SLR   R2,R2         PRESET TRAILING BLANK               87360
         SH    R15,=H'1'     MAKE EXECUTE LENGTH                 87312
         BM    SCANVALT      SKIP TRANSLATE                      87312
         EX    R15,SCANTRT   FIND TERMINATOR                     87312
SCANVALT STC   R2,SCNSEP     SAVE END SEPARATOR VALUE            87360
         LR    R15,R1                                            87312
         SR    R15,R4        GET LENGTH                          87312
         ST    R15,SCNVAL+4  SAVE IT                             87312
         LA    R4,1(,R1)     NEXT SCAN ADDRESS                   87312
         SR    R5,R15                                            87312
         BCTR  R5,0                                              87312
         B     4(,R9)        RETURN                              87312
SCANTRT  TRT   0(0,R4),SCANSTOP                                  87312
SCANWOC  OC    DBSAVE(0),0(R1)  COPY AND UPPER CASE KEYWORD TEXT 87312
         SPACE 1                                                 87312
SCANERR  LA    R0,8          SET ERROR                           87312
         B     SCANXCOM      COMMON EXIT CODE                    87312
SCANEXIT SLR   R0,R0                                             87312
         STM   R4,R5,FDWSCAN   SAVE FINAL SCAN POSITION          87312
SCANXCOM SCEXIT ,            AND RETURN                          87312
         SPACE 1                                                 87312
SCANSTOP DC    256X'00'      SCAN STOPPER TABLE                  87312
         ORG   SCANSTOP+C' '                                     87312
         DC    AL1(4)        SIMPLE STOPPER - TRAILING BLANK     87312
         ORG   SCANSTOP+C','                                     87312
         DC    AL1(8)        COMMA                               87312
         ORG   SCANSTOP+C';'                                     87312
         DC    AL1(8)        SEMI-COLON                          87312
         ORG   SCANSTOP+C'/' TSO LOGON UID/PASSWORD SEPARATOR    87333
         DC    AL1(8)        SLASH                               87333
         ORG   SCANSTOP+C'='                                     87312
         DC    AL1(12)       EQUAL                               87312
         ORG   SCANSTOP+C':'                                     87312
         DC    AL1(16)       COLON                               87312
         ORG   SCANSTOP+C'('                                     87312
         DC    AL1(20)       PARAMETER START                     87312
         ORG   SCANSTOP+C')'                                     87312
         DC    AL1(24)       PARAMETER END                       87312
         ORG   ,                                                 87312
         POP   USING                                            GP97231
         EJECT ,                                                 88340
***********************************************************************
*                                                                     *
*        EDIT ROUTINE TO REMOVE BACK-SPACE CHARACTERS, AND TO TRIM    *
*        LEADING AND TRAILING BLANKS.                                 *
*        RETURN CODE = 0     NOTHING TO DO OR EVERYTHING EDITED.      *
*        RETURN CODE = 4     EDITED, BUT EOT FOUND IN BUFFER.         *
*                                                                     *
***********************************************************************
         SPACE 1                                                 88340
SCEDIT   SCHEAD ,            R10 HAS FDW ADDRESS                 88340
         SLR   R4,R4         PRESET FOR GOOD RETURN              88340
         LM    R6,R7,FDWSCAN  LOAD ADDRESS AND LENGTH            88340
         LTR   R5,R7         TEST LENGTH                         88340
         BNP   SCEDITX       NONE; QUIT                          88340
         LR    R14,R6        COPY TEXT START                     88340
         LR    R15,R6        DITTO                               88340
         SLR   R3,R3         CLEAR FOR CR                        88340
         LA    R1,X'40'      CONSTANT BLANK                      88340
EDITLOOP IC    R3,0(,R14)    GET NEXT CHARACTER FOR INSPECTION   88340
         CR    R3,R1         PRINTABLE CHARACTER ?               88340
         BNL   EDITPUT       YES; PROCESS IT                     88340
         CLI   0(R14),X'16'  BACKSPACE ?                         88340
         LR    R3,R1         BUT REPLACE BY BLANK                88340
         BNE   EDITEOT       NO; JUST PROCESS AS BLANK           88340
         STC   R1,0(,R14)    CLEAR IT OUT                        88340
         CR    R15,R6        POSSIBLE TO BACK-UP ?               88340
         BNH   EDITBUMP      NO; JUST BUMP 14                    88340
         BCTR  R15,0         BACK-UP ONE SPACE                   88340
         STC   R1,0(,R15)    CLEAR OLD BYTE                      88340
         B     EDITBUMP      AND AVOID R15 INCREMENTATION        88340
EDITEOT  CLI   0(R14),X'37'  EOT ?                               88340
         BNE   EDITPUT       NO                                  88340
         LA    R4,4          SET EOT RETURN                      88340
EDITPUT  STC   R1,0(,R14)    CLEAR THE SOURCE LOCATION           88340
         STC   R3,0(,R15)    STORE THE NEW BYTE                  88340
         LA    R15,1(,R15)   SET NEW OUTPUT LOCATION             88340
EDITBUMP LA    R14,1(,R14)   SET NEW INPUT                       88340
         BCT   R5,EDITLOOP   REPEAT FOR ALL                      88340
         LR    R5,R7         RESTORE STARTING LENGTH             88340
         COBAL DBK           REMOVE TRAILING AND LEADING BLANKS  88340
         LR    R7,R5         GET NEW LENGTH                      88340
         STM   R6,R7,FDWSCAN   UPDATE CALLER'S LIST              88340
SCEDITX  LR    R0,R4         NEVER AN ERROR ?                    88340
         SCEXIT ,                                                88340
         SPACE 1                                                 88340
         LTORG ,                                                 88340
         POP   USING                                            GP97231
         EJECT ,                                                 87312
***********************************************************************
*                                                                     *
*        SCMARK - CHECK FOR ERRORS AND INCOMPLETE FIELDS              *
*                                                                     *
***********************************************************************
         SPACE 1                                                 87314
SCMARK   SCHEAD ,            R10 HAS FDW ADDRESS                 87314
         USING FDW,R10                                           87314
         USING SAVE,R13                                          87314
         L     R11,FDWSCAC   LOAD SCANNER CONTROL LIST           87314
         USING FDSSECT,R11   DECLARE IT                          87314
         SLR   R4,R4         CLEAR FIRST TIME FLAG               87314
         MVI   FDWFG,0       RESET THE ERROR BITS                87360
MARKLOOP ICM   R5,1,FDSLEN   GET ENTRY LENGTH                    87314
         BM    MARKEXIT      DONE; QUIT                          87314
         LA    R6,FDSFDAD    POINT TO FDIN ADDRESS               87314
         BAS   R14,MARKTADD  GET FDIN ADDRESS                    87314
         USING FDSECT,R6     DECLARE IT                          87314
         TM    FDTYPE,FDFPRT OPT7/OPT9 MISSING (PRT FORMAT) ?    87314
         BNO   *+8           NO; NORMAL                          87314
         SH    R6,=H'2'      FAKE IT OUT                         87314
         SLR   R9,R9                                             87314
         ICM   R9,3,FDIOFF   GET FIW OFFSET                      87314
         A     R9,FDWFWA     RELOCATE                            87314
         USING FIWSECT,R9    DECLARE IT                          87314
         TM    FIWFG,FIWFERR   FIELD IN ERROR ?                  87314
         BNZ   MARKERR       YES; SET IT                         87314
         TM    FDSFG,FDSFREQ  REQUIRED FIELD ?                   87314
         BZ    MARKCLIR      NO; SEE IF CLEAN-UP NEEDED          87334
         TM    FIWFG,FIWFTXT  TEXT ?                             87314
         BZ    MARKERR       NO; FLAG ERROR                      87314
         CLI   FIWLEN,0      NULL TEXT ?                         87314
         BNE   MARKBUMP      NO; JUST INCREMENT                  87314
MARKERR  TM    FDSFG,FDSFREQ  REQUIRED FIELD ?                   87360
         BNZ   MARKERRS      YES; FLAG                           87360
         TM    FDWOPT,FDWMKDEL  DELETE (CLEAR) NON-ESSENTIALS ?  87360
         BZ    MARKERRS      NO                                  87360
         NI    FIWFG,FIWFINT+FIWFINV  RESET FLAGS                87360
         MVI   FIWLEN,0      RESET LENGTH                        87360
         B     MARKBUMP      CONTINUE                            87360
MARKERRS OI    FIWFG,FIWFTXT+FIWFERR  FORCE AN ERROR             87314
         OC    FDWFG,FIWFG   PROPAGATE IT                        87314
         LTR   R4,R4         FIRST TIME ?                        87314
         BNZ   MARKBUMP      NO                                  87314
         ICM   R1,15,FDWSCMSG  DID USER SUPPLY A MESSAGE AREA ?  87360
         BZ    MARKERNM      NO                                  87360
         CLI   1(R1),0       ALREADY HAS A MESSAGE IN IT ?       87363
         BNE   MARKERNM      YES; DON'T CLOBBER IT               87363
         MVC   4(MSG690LN,R1),MSG690  MAKE MESSAGE PREFIX        87360
         CLI   FIWLEN,0      ANY TEXT ?                          87361
         BNE   *+10          YES                                 87361
         MVC   4+13(8,R1),=C'omitted '  CHANGE MESSAGE           87361
         SLR   R15,R15                                           87360
         IC    R15,FDSLEN    GET TEXT LENGTH-1                   87360
         LA    R14,MSG690LN+4(R1)  INSERTION ADDRESS             87360
         EX    R15,MARKMMVC  MOVE TO TEXT BUFFER                 87360
         LA    R15,1+4+MSG690LN(,R15)                            87360
         TM    FDSFG,FDSFPOS POSITIONAL ?                        87360
         BZ    MARKERMP      NO                                  87360
         CLI   FDWBOM,0      OUTPUT TO A CRT ?                   87361
         BNE   MARKERMP      YES; NO POSITIONALS                 87361
         LA    R14,0(R15,R1)  MESSAGE END                        87360
         MVC   0(L'MSG690PS,R14),MSG690PS                        87360
         LA    R15,L'MSG690PS(,R15)  UP LENGTH                   87360
MARKERMP STH   R15,0(,R1)    SET LENGTH                          87360
         STCM  R15,12,2(R1)  CLEAR FLAGS, ETC,                   87360
MARKERNM ICM   R4,3,FIWATB   LOAD AND TEST ATTRIBUTE ADDRESS     87314
         BZ    MARKBUMP      NONE; TOO BAD                       87314
         STCM  R4,3,FDWICUD  SET CURSOR TO BAD FIELD             87314
         B     MARKBUMP      DO NEXT ENTRY                       87334
MARKCLIR TM    FIWFG,FIWFTXT TEXT IN THIS ENTRY ?                87334
         BZ    MARKBUMP      NO                                  87334
         CLI   FIWLEN,0      ERASED BY USER ?                    87334
         BNE   MARKBUMP      NO                                  87334
         NI    FIWFG,FIWFINT+FIWFINV  CLEAR THE ENTRY            87334
MARKBUMP SLR   R14,R14                                           87314
         SLR   R15,R15                                           87314
         IC    R14,FDSLEN    GET TEXT LENGTH                     87314
         TM    FDSFG,FDSFMOVE  REPLACEMENT TEXT ?                87314
         BZ    MARKPOPS      NO                                  87314
         IC    R15,FDSRLEN+1(R14)  GET REPLACEMENT LENGTH        87314
         LA    R14,2(R14,R15)  ADJUST LENGTH                     87314
MARKPOPS LA    R11,FDSTEXT+1(R14)  NEXT TABLE ENTRY              87314
         B     MARKLOOP      DO NEXT FIELD                       87314
         SPACE 1                                                 87314
MARKTADD TM    0(R6),X'F0'   VALID REGISTER ?                    87314
         BZ    MARKBUMP      NO; SKIP THIS ENTRY                 87314
MARKSADD CLI   0(R6),X'DF'   VALID REGISTER ?                    87314
         BH    MARKBUMP      NO; SKIP THIS ENTRY                 87314
         ST    R1,DB         SAVE USER'S VALUE                   87314
         ICM   R6,3,0(R6)    GET S FORM OF ADDRESS               87314
         LR    R1,R6         COPY                                87314
         N     R6,=X'00000FFF'  GET DISPLACEMENT                 87314
         SRL   R1,10                                             87314
         N     R1,=X'0000003C'  GET BASE *4                      87314
         BZ    MARKSADT      DON'T ADD R0 FOR ABSOLUTE ADDRESS   87314
         AL    R6,USERREGS(R1)  GET USER'S VALUE                 87314
MARKSADT LA    R6,0(,R6)     KILL HIGH BYTE                      87314
         L     R1,DB         RESTORE USER'S REGISTER             87314
         BR    R14           RETURN TO CALLER                    87314
         SPACE 1                                                 87314
MARKEXIT SLR   R0,R0         MEANINGLESS RETURN CODE             87314
         SCEXIT ,            RETURN                              87314
         SPACE 1                                                 87360
MARKMMVC MVC   0(0,R14),FDSTEXT  MOVE FIELD LABEL                87360
MSG690   DC    C'MSG690 Field in error: '                        87360
MSG690LN EQU   *-MSG690                                          87360
MSG690PS DC    C' (positional)'                                  87360
         POP   USING                                            GP97231
         TITLE '@ S C R E E N S  ***  CONVERSION ROUTINES'
*        DATA CONVERSION FUNCTIONS.                              82102
*        A COMMON BASE AND BASE REGISTER (R11) ARE USED.         82102
*                                                                82102
CONBASE  DS    0H            VALUE OF CONVERSION BASE            82102
         DROP  ,                                                GP97231
         USING CONBASE,R11   DECLARE FOR COMMON USE              82102
         USING SCRNWORK,R8   PASSED BY CALLER
         USING SAVE,R13      PASSED BY CALLER
         SPACE 1                                                 82102
*        COMMON ENTRY SETUP FOR MOST CONVERT FUNCTIONS
*
CONVPARM LM    R4,R6,SAVE15   LOAD CALLER'S R15, R0, AND R1      81193
         LTR   R7,R4         GET ENTRY CODE TO THIS THING        81193
         BM    BADSET        NEGATIVE; DEB, ETC. ENTRY
         LTR   R0,R5         ANY LENGTH ?
         BM    CONVEXIT      NO; RETURN WITH ERROR               81193
         BP    CONVPARP      PLUS; USE IT                        81193
         LA    R14,CONVLNDF(R7)  POINT TO DEFAULT LENGTH BYTE    90002
         ICM   R5,1,0(R14)   LOAD AND TEST IT                    90002
         BZ    BADSET        ERROR IF STILL ZERO                 90002
         BP    CONVPARP      CONTINUE                            90002
         SLR   R5,R5         CONTINUE IF ZERO ALLOWED (L=255)    90002
         B     CONVEXIT                                          90002
CONVPARP LA    R14,CONVLBRX  GET MAXIMUM OFFSET                  81193
         CR    R7,R14        CHECK FOR MAXIMUM
         BLR   R9            RETURN OK
BADSET   L     R5,=F'-701'   MAKE NEGATIVE
         B     CONVEXIT      RETURN WITH ERROR
         SPACE 1
*        COMMON ENTRY FOR SPECIFIC (UNINDEXED) FUNCTIONS
*
CONVPAR2 LM    R5,R6,SAVE0   LOAD USER'S ENTRY VALUES
         LTR   R0,R5         TEST
         BM    BADSET        FAIL IT
         BZ    CONVEXIT      SKIP IF ZERO
         BR    R9            OK ?
         SPACE 2
DBK      CONENT ,            ENTRY CODE FOR DEBLANKING           82102
         BAS   R9,CONVPAR2   DEBLANK LEFT AND RIGHT             GP99026
         SPACE 1                                                 81193
*        EXIT FOR CONVERSION FUNCTIONS WITH DEBLANKING           81193
*                                                                81193
CONVEND  BAS   R14,DBKLEFT   DEBLANK LEFT                       GP99026
         BAS   R14,DBKRIGHT  DEBLANK RIGHT                      GP99026
         SPACE 1
*        COMMON EXIT FROM ALL CONVERT FUNCTIONS                  81193
*
CONVEXIT LR    R0,R5         PASS LENGTH
         LR    R1,R6         PASS STRING ADDRESS
         LM    R14,R15,SAVE14  RESTORE RETURN REGISTERS
         LM    R2,R12,SAVE2  RESTORE REMAINDER
         LTR   R0,R0         SET CC FOR CALLER
         BR    R14           RETURN
         SPACE 1
DBKL     CONENT ,            ENTRY CODE FOR LEFT DEBLANKING      82102
         BAS   R9,CONVPAR2   DEBLANK LEFT ONLY                  GP99026
         BAS   R14,DBKLEFT                                      GP99026
         B     CONVEXIT
         SPACE 1
DBKR     CONENT ,            ENTRY CODE FOR TRAILING DEBLANKING  82102
         BAS   R9,CONVPAR2   DEBLANK RIGHT ONLY                 GP99026
         BAS   R14,DBKRIGHT                                     GP99026
         B     CONVEXIT
         SPACE 1
DBKZ     CONENT ,            ENTRY CODE FOR LEADING ZERO DELETION
         BAS   R9,CONVPAR2   GO TO COMMON SETUP                 GP99026
         CLI   0(R6),C'-'    LEADING MINUS ?
         BE    DBKZS         YES; PRESERVE IT
         CLI   0(R6),C'+'    OR PLUS SIGN ?
         BNE   DBKZP         NO
DBKZS    IC    R3,0(,R6)     SAVE THE SIGN
         LA    R6,1(,R6)     SKIP IT
         SH    R5,=H'1'      DECREASE LENGTH
         BNP   BADSET        ERROR
         BAS   R14,DBKZERO   KILL LEADING ZEROES                GP99026
         BCTR  R6,0
         LA    R5,1(,R5)
         STC   R3,0(,R6)     REPLACE SIGN
         B     CONVEXIT
DBKZP    BAS   R14,DBKZERO   DELETE LEADING ZERO                GP99026
         B     CONVEXIT
         SPACE 2
DBKLEFT  LTR   R5,R5         VALID LENGTH ?
         BNPR  R14           NO; QUIT
DBKLEFTY TM    0(R6),255-C' '  LEADING BLANK/NULL ?
         BNZR  R14           NO; RETURN TO CALLER
         LA    R6,1(,R6)     NEXT BYTE
         BCT   R5,DBKLEFTY   TRY AGAIN
         BR    R14           NULL
         SPACE 1
DBKZERO  LTR   R5,R5         VALID LENGTH ?
         BNPR  R14           NO; QUIT
DBKZEROS CLI   0(R6),C'0'    LEADING ZERO
         BNER  R14           NO; RETURN
         LA    R6,1(,R6)
         BCT   R5,DBKZEROS   TRY AGAIN
         BCTR  R6,0          BACKSPACE
         LA    R5,1          LEAVE ONE ZERO
         BR    R14           RETURN
         SPACE 1
DBKRIGHT LTR   R5,R5         VALID LENGTH ?
         BNPR  R14           NO; QUIT
         LA    R15,0(R5,R6)  SPACE TO LAST BYTE+1
DBKRIGHS BCTR  R15,0
         TM    0(R15),255-C' '   TRAILING BLANK/NULL ?
         BNZR  R14           NO; RETURN
         BCT   R5,DBKRIGHS   TRY AGAIN
         BR    R14           RETURN
         SPACE 2
DATE     CONENT ,            ENTRY CODE FOR DATE ROUTINE         82102
DATEDFLT TIME  TU            GET THE DATE (RAPIDLY)              81193
DATECOM  BAS   R9,CONVDADA   GO TO COMMON DATE ROUTINE           84170
         SRDA  R2,32         PREPARE FOR DIVIDE                  84170
         D     R2,=F'100'    LEAVE R2 WITH YEAR AND DECADE ONLY  84170
         LA    R3,C'/'       SET DATE SEPARATOR                  81193
DATEMULT MH    R4,=H'100'                                        81193
         AR    R1,R4
         MH    R1,=H'100'
         AR    R1,R2
         CVD   R1,DB
         B     DATETIME
         SPACE 2
TIME     CONENT ,            ENTRY CODE FOR TIME ROUTINE         82102
TIMEDFLT TIME  DEC           GET THE TIME                        81193
TIMECOM  SRL   R0,4          KNOCK OUT 1/100 OF SEC              81193
         XC    DB,DB
         ST    R0,DB+4
         OI    DB+7,X'0F'
         LA    R3,C':'       SET THE TIME SEPARATOR CHARACTER    81193
         SPACE 1
DATETIME MVC   DBWORK(10),DATEPAT
         STC   R3,DBWORK+4   SET THE SEPARATOR CHARACTER         81193
         STC   R3,DBWORK+7                                       81193
         ED    DBWORK(10),DB+4
         LA    R6,DBWORK+2   SET OUTPUT ADDRESS
         LA    R5,8          SET OUTPUT LENGTH
         B     CONVEXIT
         EJECT ,
*        INDEXED CONVERSION FUNCTIONS (STORAGE MODE)
*
CONVERT  CONENT ,            ENTRY CODE FOR CONVERSION ROUTINES  82102
CVC      BAS   R9,CONVPARM   DO COMMON INITIALIZATION           GP99026
CVCCRC   SLR   R1,R1                                             81193
         IC    R1,CONVLNMX(R7)  GET MAXIMUM INPUT LENGTH         81193
         CR    R5,R1         LONGER ?                            81193
         BNH   *+6           NO                                  81193
         LR    R5,R1         TRUNCATE INPUT                      81193
         LR    R3,R5                                             81193
         BCTR  R3,0          MAKE LENGTH FOR EXECUTES            81193
         XC    DB,DB         CLEAR FOR SOME FUNCTIONS            81193
         LA    R15,0(R7,R7)   CONVERT FUNCTION TO OFFSET         81193
         LH    R15,CONVLBRT(R15)  GET OFFSET                     81193
         B     CONVCHAR(R15)  BRANCH TO PROCESSOR                81193
CONVLBRT DC    AL2(CONVCHAR-CONVCHAR)  CHAR                      81193
         DC    2AL2(CONVNULL-CONVCHAR)  CONTROL, ASIS            81193
         DC    AL2(CONVADDR-CONVCHAR,CONVHEX-CONVCHAR)  ADDR, HEX
FUNSHEX  EQU   (*-CONVLBRT)/2
         DC    AL2(CONVSHEX-CONVCHAR)  SIGNED HEX
         DC    AL2(CONVBIT-CONVCHAR)  BIT STRING
         DC    2AL2(CONVIDEC-CONVCHAR) INT,$INT                  81193
FUNDEC   EQU   (*-CONVLBRT)/2
         DC    2AL2(CONVIDEC-CONVCHAR) DEC,$DEC                  81193
         DC    AL2(CONVFIX-CONVCHAR)  FLOATING POINT
         DC    AL2(CONVTIME-CONVCHAR,CONVTIMD-CONVCHAR)  TIME; I, D
         DC    AL2(CONVDATE-CONVCHAR,CONVDATJ-CONVCHAR)  DATE; //, Y.D
         DC    AL2(CONVWDAY-CONVCHAR,CONVMTH-CONVCHAR)  DAY; MONTH
         DC    AL2(CONVDAY-CONVCHAR,CONVMD-CONVCHAR)  DAY; MON DY
         DC    AL2(CONVDMY-CONVCHAR,CONVMDY-CONVCHAR)            84170
         DC    AL2(CONVCHEX-CONVCHAR) PRINTABLE EBCDIC, ELSE HEX 84170
FUNSINT  EQU   (*-CONVLBRT)/2  START OF SECOND INTEGER RANGE     84170
         DC    4AL2(CONVID2C-CONVCHAR)  I; IC; IZ; SPARE        84170
FUNSDEC2 EQU   (*-CONVLBRT)/2  START OF SECOND DECIMAL RANGE     84170
         DC    4AL2(CONVID2C-CONVCHAR)                           84170
CONVLBRX EQU   (*-CONVLBRT)/2   MAXIMUM VALID
CONVLNMX DC    AL1(255,255,255,4,8,4,4,4,4,8,8,8,4,4,4,4)  MAX LENGTH
         DC    AL1(4,4,4,4,4,4,8,4,4,4,4,4,4,4,4)                84170
CONVLNDF DC    AL1(255,255,255,3,0,0,0,0,0,0,0,0,0,0,0,0) DEFAULT LEN
         DC    AL1(0,0,0,0,0,0,0,0,0,0,0,0,0,0,0)                84170
         SPACE 1
CONVCHAR ICM   R14,15,SWTRAN GET OUTPUT TRANSLATE
         BNZ   *+8           OK                                  90330
         LA    R14,TNTRTAB   LOAD DEFAULT TABLE                  90330
         EX    R3,CVCMVOUT   MOVE DATA TO SAVE AREA              81193
         LA    R6,DBSAVE     SET SAVE AREA ADDRESS               81193
         EX    R3,CVCTROUT   TRANSLATE                           81193
CONVNULL B     CONVEXIT      RETURN                              81193
CVCMVOUT MVC   DBSAVE(0),0(R6)  MOVE TEXT TO SAVE AREA           81193
CVCTROUT TR    0(0,R6),0(R14)  TRANSLATE                         84170
CVCCLOUT CLC   0(0,R6),0(R1)  TRANSLATED TEXT SAME AS SOURCE ?   84170
         SPACE 1                                                 84170
CONVCHEX ICM   R14,15,SWTRAN GET TRANSLATE TABLE
         BNZ   *+8           HAVE ONE                            84170
         LA    R14,TNTRTAB   GET DEFAULT FOR 3278
         EX    R3,CVCMVOUT   MOVE DATA TO SAVE AREA              84170
         LR    R1,R6         SAVE USER'S ADDRESS                 84170
         LA    R6,DBSAVE     SET FOR COPY                        84170
         EX    R3,CVCTROUT   TRANSLATE IT                        84170
         EX    R3,CVCCLOUT   COMPARE TO ORIGINAL                 84170
         BE    CONVEXIT      EQUAL - PRINTABLE                   84170
         LR    R6,R1         RESTORE USER'S ADDRESS              84170
         B     CONVHEX       PROCESS AS HEX                      84170
         SPACE 1
CONVSHEX XC    DBWORK2(4),DBWORK2   PRECLEAR                     81193
         EX    R3,COHXMVC    MOVE TO WORK AREA                   81193
         MVI   DB+6,C'+'     PROVISIONALLY SET POSITIVE
         ICM   R6,15,DBWORK2  LOAD VALUE                         81193
         BNM   CONVSHEP      DONE IF PLUS
         MVI   DB+6,C'-'     ELSE SET MINUS SIGN
         SPM   R5            KILL PROGRAM MASK (FOR X'80000000') 81193
         LPR   R6,R6         NOW KILL THE SIGN
         SPM   R9            RESTORE PROGRAM MASK                81193
         B     CONVSHEP      GO TO COMMON CODE                   81193
         SPACE 1
CONVADDR LR    R2,R5         COPY                                81193
         SLL   R2,3          CONVERT TO BIT COUNT
         LA    R4,32         MAX BITS                            81193
         SR    R4,R2         BITS TO SHIFT                       81193
         SLL   R6,0(R4)      LINE UP                             82025
CONVSHEP ST    R6,DB         SET IT                              81193
         LA    R6,DB
CONVHEX  EX    R3,COHXMVC    MOVE TO TEMP AREA                   81193
         UNPK  DBWORK(9),DBWORK2(5)  UNPACK FIRST WORD
         UNPK  DBWORK+8(9),DBWORK2+4(5)  AND POSSIBLE SECOND
         TR    DBWORK(16),HEXTAB-C'0'  MAKE EBCDIC
         LA    R6,DBWORK
         SLL   R5,1          OUTPUT LENGTH IS TWICE INPUT
         CH    R7,=Y(FUNSHEX)   SIGNED HEX OPTION ?
         BNE   CONVEXIT      NO; EXIT WITHOUT DEBLANKING         81193
         BAS   R14,DBKZERO   DELETE LEADING ZERO                GP99026
         BCTR  R6,0          BACKSPACE A BYTE
         LA    R5,1(,R5)     UP LENGTH BY ONE
         MVC   0(1,R6),DB+6  COPY SIGN
         B     CONVEND
COHXMVC  MVC   DBWORK2(0),0(R6)  MOVE USER'S DATA
         SPACE 1
CONVBIT  EX    R3,COHXMVC    MOVE USER DATA TO WORK AREA         81193
         LA    R6,DBWORK     POINT TO OUTPUT AREA                81193
         SLL   R5,3          OUTPUT LENGTH IS 8*INPUT
         LR    R4,R6         SET START CONVERSION ADDRESS        81193
         LA    R3,DBWORK-1(R5)  SET LAST OUTPUT POSITION         81193
         LA    R2,1          SET BXLE INCREMENT                  81193
         L     R1,DBWORK2    LOAD BITS TO BE PROCESSED           81193
CONVBITS LA    R0,C'0'/2     MAKE 0 OR 1 WITH NEXT SHIFT         81193
         SLDL  R0,1          ISOLATE LEFT-MOST BIT
         STC   R0,0(,R4)     STASH IT                            81193
         BXLE  R4,R2,CONVBITS  REPEAT FOR NEXT BIT               81193
         B     CONVEXIT      NO EDITING
         SPACE 1
CONVIDEC SPM   R1            CLEAR PROGRAM MASKS                 81193
         BAS   R9,CNVMVCDB   MOVE USER'S INPUT TO DB             81193
         CH    R7,=Y(FUNDEC)   DECIMAL REQUEST ?
         BNL   CONVIDED      YES; ALREADY PACKED
         CH    R3,=H'1'      WAS IT A HALF-WORD ?                81193
         BNE   *+12          NO
         LH    R0,DB+6       ENSURE SIGN
         B     *+8
         L     R0,DB+4       LOAD
         CVD   R0,DB         MAKE PACKED
CONVIDED TM    SAVE15+3,1    $ OPTION ?
$OFFSET  EQU   (FUNDEC)-(FUNDEC/2)*2
         BC    7+$OFFSET,CONVID$       YES
CONVIDEI MVC   DBWORK,DBEDMASK  MAKE EDIT MASK                   84170
         LA    R1,DBWORK+15  SET FOR NON-SIGNIFICANCE
         EDMK  DBWORK,DB     EDIT
         LA    R5,DBWORK+16
         B     CONVID$C
CONVMVCT MVC   0(0,R4),0(R6)  MOVE INPUT DATA
CONVID$  MVC   DBWORK(21),DBEDMONY   USE , , , . EDIT MASK
         LA    R1,DBWORK+17  SET FOR NON-SIGNIFICANCE
         EDMK  DBWORK(21),DB  EDIT
         LA    R5,DBWORK+21
CONVID$C TM    DB+7,3        FLAKY SIGN TEST
         BNM   *+10          C,F PLUS; D,E MINUS ???
         BCTR  R1,0          BACKSPACE ONE
         MVI   0(R1),C'-'    INSERT MINUS SIGN
         LR    R6,R1         COPY START ADDRESS
         SR    R5,R6         LENGTH OF DATA
CONVIPGM SPM   R9            RESTORE PROGRAM MASK                81193
         B     CONVEND
         SPACE 1                                                 81193
CNVMVCDB LA    R4,DB+8       SET LOCATION TO MOVE TO +
         SR    R4,R5          ADJUST FOR LENGTH TO BE MOVED
         EX    R3,CONVMVCT   MOVE IT                             81193
         LM    R0,R1,DB      LOAD FOR SOME CALLERS               81193
         BR    R9            RETURN                              81193
         SPACE 2                                                 84170
CONVID2C SPM   R1            CLEAR PROGRAM MASKS                 84170
         BAS   R9,CNVMVCDB   MOVE USER'S INPUT TO DB             84170
         CH    R7,=Y(FUNSDEC2)  PACKED ALREADY ?                 84170
         BNL   CONVID2D      YES                                 84170
         CH    R3,=H'1'      WAS IT A HALF-WORD ?                84170
         BNE   *+12          NO                                  84170
         LH    R0,DB+6       ENSURE SIGN                         84170
         B     *+8                                               84170
         L     R0,DB+4       LOAD AND SIGN                       84170
         CVD   R0,DB         MAKE PACKED                         84170
CONVID2D LR    R6,R7         COPY FUNCTION                       84170
         SH    R6,=Y(FUNSINT)  MAKE RELATIVE                     84170
         BIX   VAL=(R6),PFX=CONVID,BASE=CONBASE,                       *
               LOC=(CM,CN,CZ,SP)
CONVIDCN MVC   DBWORK(17),DBEDCENT  EDIT CENTS                   84170
         LA    R1,DBWORK+13  SET FOR NON-SIGNIFICANCE            84170
         EDMK  DBWORK(17),DB EDIT      ------.--                 84170
         LA    R5,DBWORK+17  LAST DIGIT+1                        84170
         B     CONVID$C      JOIN COMMON                         84170
CONVIDCM MVC   DBWORK(20),DBEDCOMM  EDIT WITH COMMAS             84170
         LA    R1,DBWORK+19  SET FOR NON-SIGNIFICANCE            84170
         EDMK  DBWORK(20),DB EDIT                                84170
         LA    R5,DBWORK+20  POINT PAST LAST DIGIT               84170
         B     CONVID$C      GO TO COMMON                        84170
CONVIDSP DS    0H            ***** SPARE *****                   84170
CONVIDCZ MVC   DBWORK,DBEDMASK                                   84170
         MVI   DBWORK,C'0'   MAKE LEADING ZEROES                 84170
         EDMK  DBWORK,DB     EDIT                                84170
         LA    R1,DBWORK+1   SET FOR NON-SIGNIFICANCE            84170
         LA    R5,DBWORK+16                                      84170
         B     CONVID$C      JOIN COMMON                         84170
DBEDCOMM DC    X'402020206B2020206B2020206B2020206B202120'       84170
DBEDCENT DC    X'40202020202020202020202021204B2020'             84170
         SPACE 2
FR0      EQU   0                                                 81193
FR2      EQU   2                                                 81193
FR4      EQU   4                                                 81193
FR6      EQU   6                                                 81193
*        THE CONVFIX CODE IS LOSELY BASED ON CODE WRITTEN BY
*        STEVE SILVER, AS MODIFIED BY VIC TOLOMEI (URSA)
         SPACE 1
CONVFIX  STD   FR0,GWSAVE0   SAVE ALL FLOATING REGISTERS         81193
         STD   FR2,GWSAVE2                                       81193
         STD   FR4,GWSAVE4                                       81193
         STD   FR6,GWSAVE6                                       81193
         MVC   DBWORK2+4(4),=X'6FFFFFFF'  FUDGE FACTOR FOR SHORT 81193
         EX    R3,COHXMVC    MOVE TO WORK AREA                   81193
         LA    R4,0(R3,R3)   SET PRECISION TO 2*(L'INPUT-1)      81193
         LA    R6,DBWORK     POINT TO OUTPUT AREA                81193
         LA    R5,1          SET LENGTH FOR ZERO                 81193
         MVI   DBWORK,C'0'   PRESET FOR ZERO VALUE               81193
         SLR   R2,R2         CLEAR EXPONENT AND ZERO FOR PGM MASK
         SPM   R2            KILL MASKABLE INTERRUPTIONS         81193
         LD    FR0,DBWORK2   GET USER'S NUMBER                   81193
         AD    FR0,DB        NORMALIZE BY ADDING ZERO            81193
         LTDR  FR0,FR0       CHECK THE SIGN                      81193
         BZ    CVGEXIT       ZERO; RESET EVERYTHING              81193
         MVI   DBWORK,C' '   SET FOR POSITIVE VALUE              81193
         BP    CVGMAGN       PLUS - GO ON                        81193
         LPDR  FR0,FR0       FORCE POSITIVE                      81193
         MVI   DBWORK,C'-'    BUT SET SIGN                       81193
CVGMAGN  CD    FR0,=X'7FFFFFFFFFFFFF00'   IN SAFE RANGE ?        81193
         BNH   *+8           YES; LEAVE IT ALONE                 81193
         LD    FR0,=X'7FFFFFFFFFFFFF00'  ELSE FORCE MAX SUPPORTED
         MD    FR0,=X'4110000000000010'  FINAGLE (INSTEAD OF ROUND)
         LD    FR4,=D'10'    SET FR4 TO 10                       81193
         LD    FR6,=D'1'     SET FR6 TO 1                        81193
         LD    FR2,=D'.1'    SET FR2 TO .1                       81193
         LA    R5,DBWORK+2   SET ADDRESS OF FIRST DIGIT          81193
CVGDIV10 CDR   FR0,FR6       LESS THAN 1 ?                       81193
         BL    CVGMUL10      IF SO, GO TO NEXT PART              81193
         DDR   FR0,FR4       NO - DIVIDE BY 10                   81193
         LA    R2,1(,R2)     INCREMENT EXPONENT                  81193
         B     CVGDIV10      CONTINUE                            81193
CVGMUL10 CDR   FR0,FR2       LESS THAN .1 ?                      81193
         BNL   CVGXPREP      IF NOT, PREPARE FOR DIGIT EXTRACTION
         BCTR  R2,0          DECREMENT EXPONENT                  81193
         MDR   FR0,FR4       MULT NUMBER BY 10                   81193
         B     CVGMUL10      CONTINUE                            81193
CVGXPREP LR    R0,R4         GET PRECISION                       81193
         LD    FR6,=X'4100000000000000'  NORMALIZE TO LEFT INTEGER
         STD   FR6,CVGINT    SAVE FOR SUBTRACT                   81193
CVGEXTRA MDR   FR0,FR4       MAKE HIGH DIGIT INTO INTEGER        81193
         AWR   FR0,FR6       UNNORMALIZE TO LEFT NYBBLE          81193
         STD   FR0,DB        STORE RESULT                        81193
         MVZ   CVGINT+1(1),DB+1  ISOLATE HIGH DIGIT              81193
         PACK  0(1,R5),DB+1(1)  MOVE DIGIT TO LOW NYBBLE OF RESULT
         OI    0(R5),C'0'    MAKE EBCDIC                         81193
         LA    R5,1(,R5)     POINT TO NEXT OUTPUT POSITION       81193
         SD    FR0,CVGINT    SUBTRACT TO GET RESIDUAL FRACTION   81193
         BCT   R0,CVGEXTRA   LOOP FOR ALL BYTES                  81193
         MVI   DBWORK+1,C'.'  SET THE DECIMAL POINT              81193
         LTR   R2,R2         ANY EXPONENT PROCESSING ?           81193
         BZ    CVGNOEXP      NO; JUST KILL TRAILING ZEROES       81193
         BM    CVGMNEXP      IF MINUS, FIX AND TEST LATER        81193
         CR    R2,R4         REPRESENTABLE AS INTEGER ?          81193
         BH    CVGMNEXP      NO; TACK THE EXPONENT ON            81193
         EX    R2,CVGMVC     MOVE INTEGER PORTION                81193
         LA    R2,DBWORK+1(R2) GET DECIMAL POINT LOCATION        81193
         MVI   0(R2),C'.'    SET THE DECIMAL POINT               81193
CVGNOEXP LA    R14,CVGEXIT   DELETE TRAILING ZEROES AND DECIMAL POINT
         SPACE 1                                                 81193
DBKTRAIL LA    R6,DBWORK     GET START OF NUMBER                 81193
         SR    R5,R6         GET CURRENT LENGTH                  81193
         BNP   CVGEXIT       HUH ?                               81193
         LA    R15,0(R6,R5)  GET LAST+1 DIGIT                    81193
DBKTRAIZ BCTR  R15,0         POINT TO LAST DIGIT                 81193
         CLI   0(R15),C'0'   TRAILING ZERO ?                     81193
         BNE   DBKTRAIP      NO; RETURN                          81193
         BCT   R5,DBKTRAIZ   TRY PRIOR ONE                       81193
         BR    R14           RETURN TO CALLER                    81193
DBKTRAIP CLI   0(R15),C'.'   TRAILING PERIOD ?                   81193
         BNER  R14           NO                                  81193
         BCTR  R5,0          YES; OMIT IT ALSO                   81193
         BCTR  R15,R14       BACKSPACE AND RETURN TO CALLER      81193
CVGMVC   MVC   DBWORK+1(0),DBWORK+2  SHIFT INTEGER PORTION       81193
         SPACE 1                                                 81193
CVGMNEXP BAS   R14,DBKTRAIL  DELETE TRAILING ZEROES              81193
         MVI   1(R15),C'E'   INDICATE EXPONENT                   81193
         LA    R5,3(,R5)     SET NEW LENGTH                      81193
         MVI   2(R15),C'+'   MOVE IN EXPONENT SIGN               81193
         LTR   R2,R2         POSITIVE EXPONENT ?                 81193
         BNM   CVGPLEXP                                          81193
         LPR   R2,R2         SET EXPONENT POSITIVE               81193
         MVI   2(R15),C'-'     BUT SET IN RESULT                 81193
CVGPLEXP LA    R0,10         DIVISOR FOR EXPONENT                81193
         SRDL  R2,32         SET UP FOR DIVIDE                   81193
         DR    R2,R0         ISOLATE TENS AND UNITS              81193
         LTR   R3,R3         GREATER THAN 9 ?                    81193
         BZ    CVGUNEXP      NO; SINGLE DIGIT EXPONENT           81193
         LA    R3,C'0'(,R3)  GET THE EBCDIC VALUE                81193
         STC   R3,3(,R15)    SET TENS POSITION                   81193
         LA    R15,1(,R15)   UP OUTPUT POSITION                  81193
         LA    R5,1(,R5)     INCREMENT OUTPUT LENGTH             81193
CVGUNEXP LA    R2,C'0'(,R2)  GET THE EBCDIC VALUE                81193
         STC   R2,3(,R15)    SET THE UNITS POSITION              81193
CVGEXIT  LD    FR0,GWSAVE0   RESTORE USER'S REGISTERS            81193
         LD    FR2,GWSAVE2                                       81193
         LD    FR4,GWSAVE4                                       81193
         LD    FR6,GWSAVE6                                       81193
         B     CONVIPGM      DEBLANK AND EXIT TO CALLER          81193
         SPACE 1                                                 81193
CONVTIME BAS   R9,CNVMVCDB   GET BINARY TIME (1/100 SECONDS)     81193
         CH    R3,=H'3'      LESS THAN ONE WORD ?                81193
         BL    TIMEDFLT      YES; USE CURRENT TIME               81193
         LA    R3,C':'       SET EDIT SEPARATOR                  81193
         LA    R14,100       DIVISOR TO SECONDS                  81193
         DR    R0,R14        CONVERT TO SECONDS                  81193
         SLR   R0,R0                                             81193
         LA    R14,60        DIVISOR                             81193
         DR    R0,R14        EXTRACT SECONDS IN R0               81193
         LR    R2,R0         SAVE SECONDS                        81193
         SLR   R0,R0                                             81193
         DR    R0,R14        GET MINUTES AND HOURS               81193
         LR    R4,R1         MOVE HOURS                          81193
         LR    R1,R0         MOVE MINUTES                        81193
         B     DATEMULT      GO TO COMMON EDIT ROUTINE           81193
         SPACE 1                                                 81193
CONVTIMD BAS   R9,CNVMVCDB   GET USER'S PACKED TIME              81193
         CH    R3,=H'3'      LESS THAN ONE WORD ?                81193
         BL    TIMEDFLT      YES; USE CURRENT TIME               81193
         LR    R0,R1         IN REGISTER 0                       81193
         B     TIMECOM       GO TO COMMON TIME FORMATTING        81193
         SPACE 1                                                 81193
CONVDATE BAS   R9,CNVMVCDB   GET USER'S PACKED DATE              81193
         CH    R3,=H'2'      LESS THAN THREE BYTES ?             81193
         BL    DATEDFLT      YES; USE CURRENT DATE               81193
         B     DATECOM       GO TO COMMON DATE FORMATTING        81193
         SPACE 1                                                 81193
CONVDATJ BAS   R9,CNVMVCDB   GET USER'S PACKED DATE              81193
         CH    R3,=H'2'      LESS THAN THREE BYTES ?             81193
         BNL   CONVDATK      NO; USE SUPPLIED VALUE              81193
         TIME  TU            GET DATE                            81193
         ST    R1,DB+4       SET IT                              81193
CONVDATK OI    DB+7,X'0F'    JUST IN CASE                        81193
         MVC   DBWORK(L'DATEJUL),DATEJUL  MAKE YY.DDD EDIT MASK  81193
         ED    DBWORK(L'DATEJUL),DB+5    EDIT                    81193
         LA    R5,6          SET OUTPUT LENGTH                   81193
         LA    R6,DBWORK+1   AND ADDRESS                         81193
         B     CONVEXIT      AND RETURN                          81193
         SPACE 1                                                 84170
CONVWDAY BAS   R9,CNVMVCDB   GET USER'S PACKED DATE              84170
         CH    R3,=H'2'      LESS THAN THREE BYTES ?             84170
         BH    CONVWDAT      NO                                  84170
         TIME  TU            GET THE DATE                        84170
         ST    R1,DB+4                                           84170
CONVWDAT SLR   R0,R0         INSERT CLEAR                        84170
         IC    R0,DB+5       INSERT YEAR                         84170
         SRDL  R0,4          PUT DECADES IN R0                   84170
         SRL   R1,28         AND UNITS IN R1                     84170
         SLL   R0,1          DECADES*2                           84170
         AR    R1,R0         ADD IN                              84170
         SLL   R0,2          DECADES*2*4                         84170
         AR    R1,R0         = UNITS + (2+8)*DECADES             84170
         XC    DB(6),DB      CLEAR YEAR                          84170
         LA    R1,6(,R1)     ADD FINAGLE FACTOR                  84170
         LA    R3,1(,R1)     SAVE YEAR + SECOND FINAGLE          84170
         SRA   R3,2          GET NUMBER OF LEAP YEARS SINCE 1900 84170
         MH    R1,=H'365'    DAYS SINCE 1900                     84170
         AR    R1,R3         ADD LEAP DAYS                       84170
         CVB   R0,DB         GET DAYS                            84170
         AR    R1,R0         ADD DAY IN THIS YEAR                84170
         SLR   R0,R0                                             84170
         D     R0,=F'7'      GET DAY OF WEEK                     84170
         MH    R0,=Y(L'WEEKDAY)  GET OFFSET TO NAME              84170
         LA    R6,WEEKDAY    POINT TO FIRST DAY                  84170
         LA    R5,L'WEEKDAY  SET LENGTH                          84170
         AR    R6,R0         POINT TO CORRECT DAY                84170
         B     CONVEXIT      AND EXIT                            84170
WEEKDAY  DC    CL9'Saturday '                                    84170
         DC    CL9'Sunday   '                                    84170
         DC    CL9'Monday   '                                    84170
         DC    CL9'Tuesday  '                                    84170
         DC    CL9'Wednesday'                                    84170
         DC    CL9'Thursday '                                    84170
         DC    CL9'Friday   '                                    84170
         SPACE 1                                                 84170
CONVDACM LA    R4,DB+8       SET LOCATION FOR DATE               84170
         SR    R4,R5          ADJUST FOR LENGTH TO BE MOVED      84170
         EX    R3,CONVMVCT   MOVE IT                             84170
         CH    R3,=H'2'      USER SUPPLIED DATE ?                84170
         BNL   CONVDACN      YES; USE SUPPLIED VALUE             84170
         TIME  TU            GET DATE                            84170
         ST    R1,DB+4       SET IT                              84170
CONVDACN CLI   DB+4,0        CENTURY SUPPLIED ?                  84170
         BH    CONVDACO      YES                                 84170
         MVI   DB+4,X'19'    SET FOR CURRENT                     84170
         TM    DB+5,X'80'    1980 ON ?                           84170
         BNZ   CONVDACO      YES                                 84170
         MVI   DB+4,X'20'    VERY ON                             84170
CONVDACO L     R1,DB+4                                           84170
CONVDADA XC    DB,DB         ENTRY FROM COMMON DATE              84170
         LR    R2,R1                                             84170
         STH   R1,DB+6                                           84170
         CVB   R1,DB                                             84170
         SRA   R2,12                                             84170
         ST    R2,DB+4                                           84170
         OI    DB+7,X'0F'                                        84170
         CVB   R2,DB         YEAR                                84170
         SLR   R3,R3                                             84170
         SLR   R0,R0         LEAP YEAR ADD                       84170
         SRDL  R2,2                                              84170
         LTR   R3,R3                                             84170
         SLDL  R2,2                                              84170
         BNZ   CONVDAC3      WORKS TILL 2099 ONLY                84170
         CH    R1,=H'60'     IS IT FEB 29 ?                      84170
         BL    CONVDAC3      LOW, IGNORE LEAP YEAR               84170
         BH    CONVDAC2      HIGH, INCREASE JUL DAY BY ONE       84170
         LA    R0,1          02/29 - SUB 1 TO BE ADDED LATER     84170
CONVDAC2 BCTR  R1,0                                              84170
CONVDAC3 LA    R4,DATETABL                                       84170
         LR    R5,R4                                             84170
CONVDACL CH    R1,2(,R4)                                         84170
         BNH   CONVDACF                                          84170
         LA    R4,2(,R4)                                         84170
         B     CONVDACL                                          84170
CONVDACF SH    R1,0(,R4)     DAY OF MONTH                        84170
         AR    R1,R0         ADD 1 IF LEAP YEAR COND.            84170
         LA    R4,2(,R4)                                         84170
         SR    R4,R5                                             84170
         SRL   R4,1                                              84170
         BR    R9            RETURN; R1-DAY, R4-MON, R2-YEAR     84170
         SPACE 1                                                 84170
CONVMTH  BAS   R9,CONVDACM   GO TO COMMON DATE PROCESSING        84170
         LA    R5,L'MONTHS   SET LONGEST LENGTH                  84170
         MH    R4,=Y(L'MONTHS)  MAKE OFFSET                      84170
         LA    R6,MONTHS-L'MONTHS(R4)  POINT TO MONTH            84170
         B     CONVEND       RETURN                              84170
MONTHS   DC    CL9'January  '                                    84170
         DC    CL9'February '                                    84170
         DC    CL9'March    '                                    84170
         DC    CL9'April    '                                    84170
         DC    CL9'May      '                                    84170
         DC    CL9'June     '                                    84170
         DC    CL9'July     '                                    84170
         DC    CL9'August   '                                    84170
         DC    CL9'September'                                    84170
         DC    CL9'October  '                                    84170
         DC    CL9'November '                                    84170
         DC    CL9'December '                                    84170
         SPACE 1                                                 84170
CONVDAY  BAS   R9,CONVDACM   GET DAY OF MONTH                    84170
         CVD   R1,DB         PACK IT                             84170
         B     CONVIDEI      GO TO COMMON                        84170
         SPACE 1                                                 84170
CONVMD   BAS   R9,CONVDACM   GET MONTH AND DAY                   84170
         CVD   R1,DB         CONVERT THE DAY                     84170
         MVC   DBWORK(6),DBEDMASK  MAKE EDIT MASK                84170
         ED    DBWORK(6),DB+5  MAKE INTEGER IN RANGE 1-99 + BLANKS
         MH    R4,=Y(L'MONTHS)  GET OFFSET TO CORRECT MONTH      84170
         LA    R4,MONTHS-L'MONTHS(R4)  GET NAME                  84170
         OC    DBWORK(3),0(R4)  MOVE AND UPPER-CASE              84170
         LA    R6,DBWORK                                         84170
         LA    R5,6          RETURN 'MON DD'                     84170
         B     CONVEXIT                                          84170
         SPACE 1                                                 84170
CONVDMY  BAS   R9,CONVDACM   GET COMMON DATE CONVERSION          84170
         MVC   DBWORK(4),DBEDMASK  USE COMMON MASK               84170
         CVD   R1,DB         CONVERT DAY                         84170
         ED    DBWORK(4),DB+6  GET DAY                           84170
         MVC   DBWORK(2),DBWORK+2  SHIFT IT OVER                 84170
         MVI   DBWORK+2,C' '   MAKE A SEPARATOR                  84170
         MH    R4,=Y(L'MONTHS)                                   84170
         LA    R4,MONTHS-L'MONTHS(R4)  POINT TO MONTH            84170
         MVC   DBWORK+3(4),DBWORK+2  MAKE LOTS OF BLANKS         84170
         OC    DBWORK+3(3),0(R4)  ADD MONTH                      84170
         MVC   DBWORK+10(4),DBEDMASK                             84170
         CVD   R2,DB         CONVERT THE YEAR                    84170
         ED    DBWORK+10(4),DB+6  GET TRAILING PORTION OF YEAR   84170
         MVC   DBWORK+7(2),DBWORK+12                             84170
         LA    R6,DBWORK                                         84170
         LA    R5,9          SET LENGTH 'DD MON YY'              84170
         B     CONVEXIT                                          84170
         SPACE 1                                                 84170
CONVMDY  BAS   R9,CONVDACM   DO LONG FORM                        84170
         CVD   R2,DB         PACK THE YEAR (YYYY)                84170
         MVC   DBWORK+19(6),DBEDMASK  MAKE EDIT MASK             84170
         ED    DBWORK+19(6),DB+5                                 84170
         MVI   DBWORK+19,C','                                    84170
         MVC   DBWORK+15(4),DBEDMASK  PREPARE FOR DAY            84170
         CVD   R1,DB         PACK DAY                            84170
         ED    DBWORK+15(4),DB+6  GET DAY                        84170
         MH    R4,=Y(L'MONTHS)                                   84170
         LA    R4,MONTHS-L'MONTHS(R4)                            84170
         MVC   DBWORK(L'MONTHS),0(R4)                            84170
         LA    R6,DBWORK                                         84170
         LA    R5,L'MONTHS                                       84170
         BAS   R14,DBKRIGHT  DELETE TRAILING BLANKS              84170
         LA    R1,DBWORK(R5)  LAST BYTE                          84170
         MVC   0(9,R1),DBWORK+16  ADD BLANK, DAY, YEAR           84170
         LA    R5,1+8(,R5)   ADJUST LENGTH                       84170
         B     CONVEXIT      RETURN                              84170
         SPACE 2
         LTORG ,                                                 81166
         EJECT ,
*        INDEXED INPUT CONVERSION FUNCTIONS
*
TREVNOC  CONENT ,
         BAS   R9,CONVPARM   GET COMMON INITIALIZATION OVER WITH
         XC    CB,CB
         SLR   R3,R3         PRESET FOR NUMERIC FUNCTIONS
         LA    R4,DBSAVE     DITTO
         MVI   0(R4),C'0'    MAKE DUMMY PARAMETER
         LA    R15,0(R7,R7)  CONVERT FUNCTION TO OFFSET
         LH    R15,TREVLBRT(R15)  GET OFFSET TO FUNCTION
         B     TREVCHAR(R15) BRANCH TO ROUTINE
TREVLBRT DC    Y(TREVCHAR-TREVCHAR)    CHAR
         DC    2Y(TREVNULL-TREVCHAR)   CONTROL, ASIS
         DC    Y(TREVNONE-TREVCHAR)     ADDR
         DC    2Y(TREVHEX-TREVCHAR)    HEX, SIGNED HEX
         DC    Y(TREVBIT-TREVCHAR)     BIT STRING
         DC    2AL2(TREVIDEC-TREVCHAR) INT,$INT
NUFDEC   EQU   (*-TREVLBRT)/2
         DC    2AL2(TREVDDEC-TREVCHAR) DEC,$DEC                  87263
         DC    AL2(TREVFIX-TREVCHAR)  FLOATING POINT
         DC    AL2(TREVNONE-TREVCHAR,TREVNONE-TREVCHAR)  TIME; I, D
         DC    AL2(TREVNONE-TREVCHAR,TREV5DEC-TREVCHAR)  DATE; //, Y.D
NUFDATJ  EQU   (*-TREVLBRT-2)/2   OFFSET TO PACKED DATE
         DC    AL2(TREVNONE-TREVCHAR,TREVNONE-TREVCHAR)  DAY; MONTH
         DC    AL2(TREVNONE-TREVCHAR,TREVNONE-TREVCHAR)  DAY; MON DY
         DC    AL2(TREVNONE-TREVCHAR,TREVNONE-TREVCHAR)
         DC    AL2(TREVNULL-TREVCHAR) PRINTABLE EBCDIC, ELSE HEX
NUFSINT  EQU   (*-TREVLBRT)/2  START OF SECOND INTEGER RANGE
         DC    4AL2(TREVIDEC-TREVCHAR)  ICM; ICN; IZ; SPARE
NUFSDEC2 EQU   (*-TREVLBRT)/2  START OF SECOND DECIMAL RANGE
         DC    4AL2(TREVDDEC-TREVCHAR)
TREVLBRX EQU   (*-TREVLBRT)/2   MAXIMUM VALID
BASECON  DC    A(CONBASE)    CONVERSION CODE BASE
         SPACE 2
TREVCHAR B     CONVEXIT      NOTHING TO DO
TREVNULL B     CONVEXIT      NOTHING TO DO
TREVNONE SLR   R5,R5         SET RETURN LENGTH TO ZERO
         B     CONVEXIT      UNSUPPORTED FUNCTION
         SPACE 1
TREVIDEC LA    R0,10         MAX FOR INTEGER IS 10               87263
         B     TREVIDED      GO TO COMMON                        87263
TREV5DEC LA    R0,5          MAX FOR JULIAN DATE                 87263
         B     TREVIDED                                          87263
TREVDDEC LA    R0,15         MAXIMUM LENGTH
TREVIDED LA    R1,1          SET SIGNUM
         CLI   0(R6),C'+'    LEADING PLUS SIGN ?
         BE    TREVIDES      YES; SKIP IT
         CLI   0(R6),C'-'    LEADING MINUS ?
         BNE   TREVIDEB      NO
         LNR   R1,R1         MAKE SIGN MINUS
TREVIDES LA    R6,1(,R6)     SKIP THE SIGN
         SH    R5,=H'1'      ADJUST THE LENGTH
         BNP   TREVIDEX      VERY SHORT INPUT - TREAT AS ZERO
TREVIDEB CLI   0(R6),C'0'    LEADING ZERO ?
         BE    TREVIDES      YES; SKIP IT
         CLI   0(R6),C','    IS THE USER IN A COMA ?
         BE    TREVIDES      YES
         CLI   0(R6),C'.'
         BE    TREVIDES      NO COMMENT, PERIOD
TREVIDEL CLI   0(R6),C','    IS THE USER IN A COMA ?
         BE    TREVIDEP      YES
         CLI   0(R6),C'.'
         BE    TREVIDEP      NO COMMENT, PERIOD
         CLI   0(R6),C'0'    NUMERIC ?
         BL    BADSET        NO; INVALID
         CLI   0(R6),C'9'    DITTO ?
         BH    BADSET        DITTO
         LA    R15,0(R3,R4)  GET NEXT CHARACTER
         MVC   0(1,R15),0(R6)  COPY THIS ONE
         LA    R3,1(,R3)     UP STRING LENGTH
TREVIDEP LA    R6,1(,R6)     SPACE TO NEXT ONE
         SH    R5,=H'1'
         BNP   TREVIDEX      GO TO CONVERT
         BCT   R0,TREVIDEL   DO FOR MAXIMUM
         B     BADSET        NUMBER TOO LONG
TREVIDEX LTR   R3,R3         ANY INPUT ?
         BZ    *+6           NO; DON'T DECREMENT LENGTH
         BCTR  R3,0          MAKE LENGTH FOR EXECUTE
         EX    R3,TREVIDPK   PACK INTO CB
         OI    CB+7,X'0F'    MAKE POSITIVELY PLUS
         LTR   R1,R1         MINUS ?
         BNM   *+8           NO
         NI    CB+7,X'FD'    MAKE NEGATIVE
         LA    R5,L'CB
         LA    R6,CB         MAKE PACKED DECIMAL RETURN
         CH    R7,=Y(NUFDATJ)  JULIAN DATE ?
         BE    CONVEXIT      YES; PACKED IS DESIRED
         CH    R7,=Y(NUFSDEC2)  DECIMAL RANGE ?
         BNL   CONVEXIT      YES; EXIT NOW
         CH    R7,=Y(NUFSINT)  INTEGER RANGE ?
         BNL   TREVIDEI      YES
         CH    R7,=Y(NUFDEC) FIRST DECIMAL RANGE ?
         BNL   CONVEXIT
TREVIDEI CLC   CB,=XL8'2147483648F'  NOT TOO LARGE ?             87263
         BH    BADSET        TOO BAD                             87263
         CVB   R0,CB         MAKE BINARY
         SRDA  R0,32         ZERO R0
         STM   R0,R1,CB      NOTE THAT R5, R6 STILL POINT TO CB
         B     CONVEXIT      RETURN
TREVIDPK PACK  CB(8),0(0,R4)  PACK USER'S DATA
         SPACE 1
TREVBIT  SLR   R14,R14
         SLR   R15,R15
         LA    R0,1
TREVBITL CLI   0(R6),C'0'    ZERO ?
         BE    TREVBITB      YES; SHIFT
         CLI   0(R6),C'1'    ONE ?
         BNE   BADSET        NO; INVALID DATA
         SLDL  R14,1
         OR    R15,R0        MAKE A LOW ONE
         B     TREVBITS
TREVBITB SLDL  R14,1         MAKE LOW ZERO
TREVBITS LA    R6,1(,R6)
         BCT   R5,TREVBITL   TRY AGAIN
         STM   R14,R15,CB
         LA    R5,L'CB
         LA    R6,CB         SET RETURN VALUES
         B     CONVEXIT
         SPACE 1
TREVFIX  B     TREVNONE      NOT DONE YET
         SPACE 1
TREVHEX  LR    R3,R5         SAVE LENGTH
         LR    R4,R6         AND STARTING ADDRESS
TREVHEXC CLI   0(R6),C'9'    NUMERIC ?
         BH    BADSET        NO
         CLI   0(R6),C'0'
         BNL   TREVHEXI      OK
         CLI   0(R6),C'F'    ALPHA HEX ?
         BH    BADSET        NO
         CLI   0(R6),C'A'
         BNL   TREVHEXI      YES
         CLI   0(R6),X'86'   LOWER-CASE ALPHA ?
         BH    BADSET        NO
         CLI   0(R6),X'81'
         BL    BADSET        TOO BAD
TREVHEXI LA    R6,1(,R6)
         BCT   R5,TREVHEXC   CHECK MORE
         LR    R5,R3         LOAD LENGTH AGAIN
         SRL   R5,1
         SLL   R5,1          GET EVEN VALUE
         CR    R3,R5         IS LENGTH EVEN ?
         BE    TREVHEXP      YES; TRANSLATE AND PACK
         MVI   DBSAVE,C'0'   MAKE LEADING ZERO
         EX    R3,TREVHMVC   MOVE TO DBSAVE
         LA    R3,1(,R3)     SET NEW LENGTH
         LA    R4,DBSAVE     SET NEW START
TREVHEXP LR    R5,R3         COPY LENGTH AGAIN                   88004
         BCTR  R5,0          SET EXECUTE LENGTH                  88004
         EX    R5,TREVHENC   STRIP ZONE BITS                     88004
         EX    R5,TREVHETR   TRANSLATE TO HEX BYTES              88004
         LR    R5,R3         COPY OUTPUT LENGTH
         SRL   R5,1          HALVE IT
         LR    R14,R5        COPY THE LENGTH
         SLL   R14,4         MAKE 1/2 VALUE IN HIGH NIBBLE
         OR    R14,R3        MAKE PACK LENGTH MASK
         EX    R14,TREVHEPK  PACK THE DATA
         LA    R6,GWSAVE0    POINT TO START OF LONG OUTPUT AREA
         B     CONVEXIT      AND RETURN
TREVHMVC MVC   DBSAVE+1(0),0(R4)
TREVHEPK PACK  GWSAVE0(0),0(,R4)  PACK HEX DATA
TREVHENC NC    0(0,R4),=31X'1F'  KILL ZONES
TREVHETR TR    0(0,R4),=X'000A0B0C0D0E0F0000000000000000000001020304050*
               6070809'
         SPACE 2
         LTORG ,
DATETABL DC    H'0,31,59,90,120,151,181,212,243,273,304,334,365'
DATEPAT  DC    X'F0212020612020612020'
DATEJUL  DC    X'F021204B202020'       YY.DDD FORM
DBEDMASK DC    X'40202020202020202020202020202120'
DBEDMONY DC    X'40206B2020206B2020206B2020206B2021204B2020'
HEXTAB   DC    C'0123456789ABCDEF'
         SPACE 1                                                 84170
PNTRTAB  TRTAB CODE=3270,OPT=(3278,SVC,FOLD)   UPPER CASE TABLE
TNTRTAB  TRTAB CODE=3270,OPT=(SVC,3278)  UPPER/LOWER CASE TABLE
         EJECT ,
         PRINT &PRTMAC
         FDSECT ,
         SPACE 1
FDW      MAPFDW ,            EXPAND CALLER'S AREA
         SPACE 1
         MAPFIW ,            MAP FDIN SINGLE ENTRY
@SCREENS CSECT ,
DUMFIW   DC    (FIWSIZE)X'00'  DUMMY FIW
         SPACE 1                                                 87312
         MAPFDS ,            MAP SCRSCAN ENTRY                   87312
         SPACE 2                                                 81201
SCRMAP   MAPSCR ,            MAPPING OF USER'S SCRWORK AREA
         SPACE 2
SCRNWORK MAPSCRWK WIDTH=MAXWIDTH MAP OF PRINT DCB AND WORK AREA  81201
         SPACE 1                                                 83331
         PRINT &PRTSYS
SCR0WORK FSAWORK PFX=FSW     PRIMARY WORK AREA                   82031
SAVE     DC    A(0)          LOCAL SAVE AREA                     82031
SAVE13   DS    A             CALLER'S R13                        82031
SAVEFWD  DS    A             LOWER R13                           82031
SAVE14   DS    A                                                 82031
SAVE15   DS    A                                                 82031
SAVE0    DS    A                                                 82031
SAVE1    DS    A                                                 82031
SAVE2    DS    A                                                 82031
SAVE3    DS    A                                                 82031
SAVE4    DS    A                                                 82031
SAVE5    DS    A                                                 82031
SAVE6    DS    A                                                 82031
SAVE7    DS    A                                                 82031
SAVE8    DS    A                                                 82031
SAVE9    DS    A                                                 82031
SAVE10   DS    A                                                 82031
SAVE11   DS    A                                                 82031
SAVE12   DS    A                                                 82031
USEXSAVE DS    16A           SAVE AREA DURING USER EXIT          84176
USEXPARM DS    5A            EXIT PARM LIST                      84176
USEX@WRK DS    A             MASTER PRINTER WORK AREA            84176
USEXFLAG DC    X'00'         USER EXIT ACTIVE IF <> 0            84176
USXFACT  EQU   X'80'           USER EXIT IN PROGRESS             84176
SCR0OPEN DC    X'00'         OPEN FLAGS                          82031
SCR0OMSK DC    X'00'         CURRENT DEVICE MASK BIT             82031
SCR0CLR  EQU   *             START OF AREA TO CLEAR              82031
DB       DS    D             WORK AREA
PUTWORK  DS    0A            8A SAVE AREA OVERLAYING DBWORK      81159
DBWORK   DS    CL16
DBWORK2  DS    CL16
IEFSD095 DC    A(0)          ADDRESS OF SEPARATOR BIG LETTER ROUTINE
RETCODE  DS    0F,XL3        RETURN CODE WORD                    87315
RETCC    DS    X               RETURN CODE - BYTE
RETC4    EQU   4                 MINOR ERROR
RETC8    EQU   8                 PROBLEM
RETC12   EQU   12                MAJOR ERROR
RETC16   EQU   16                DISASTER
         SPACE 1
PLSTXCLR EQU   *
PLSTULOP DS    X             UNDERLINE/OVERPRINT FLAG
PLFUND   EQU   2             UNDERLINE
PLFINT   EQU   1             OVERPRINT
PLSTCC   DS    X             FD-TEST COND. CODE BRANCH VALUE
PLSTEXEC DS    A             FD EXEC RESUME ADDRESS
PLSTEXEN DS    A             FD EXEC END OF PERFORM RANGE
PLSTINDT DS    A             AUTOMATIC LINE INDENT VALUE         81271
PLSTXLEN EQU   *-PLSTXCLR
         SPACE 1
USERREGS DS    0F            USER REGISTERS 0-15
CALLPARM DS    0XL8'0'       USER'S PARAMETERS
CALLLEN  DS    X               LENGTH FOR PRTF FUNCTION
CALLTIT  DS    X               0 OR TITLE/FOOTER NUMBER(S)
CALLDEV  DS    X               0 OR DEVICE MASKS
CALLFUN  DS    X               REQUESTED FUNCTION
CALLFLG  DS    0X            FLAGS
CLASA    EQU   4               RECORD CONTAINS ASA CONTROL
CLMCC    EQU   2               RECORD CONTAINS MACHINE CODE
CLNOCC   EQU   1               NO CONTROL CHARACTER IN RECORD
CLABE    EQU   X'80'         ABEND IF NOT OPENED
CLABEDU  EQU   X'40'         ABEND IF DD DUMMY
CLNWTO   EQU   X'20'         SUPPRESS WTO IF NOT FOUND
CALLR1   DS    A,14A           LIST/RECORD ADDRESS OR VALUE
         SPACE 1
SCRSAVE  DC    12F'0'        SAVE AREA
         SPACE 1
         ORG   SCRSAVE                                           81193
GWSAVE0  DS    D             SAVE AREA FOR FLOAT REG 0
GWSAVE2  DS    D                                                 81193
GWSAVE4  DS    D                                                 81193
GWSAVE6  DS    D                                                 81193
CVGINT   DS    D             INTEGER SUBTRACT AREA               81193
CB       DS    D             CONVERSION WORK AREA
         ORG   ,
         SPACE 1                                                 81159
PSEPARM  DS    0F            IEFSD095 PARAMETER LIST             81159
PSE@DATA DS    A             ADDRESS OF INPUT DATA               81159
PSE@COUN DS    A             ADDRESS OF LINE COUNTER             81159
PSE@VREC DS    A             ADDRESS OF OUTPUT RECORD            81159
PSE@CONT DS    A             ADDRESS OF CONTROL BITS/LENGTH; EOL 81159
PSECOUNT DS    F             NUMBER OF CURRENT LINE              81159
PSECNTRL DS    H             CONTROL FLAGS                       81159
PSECNLEN DS    H             LENGTH OF INPUT ITEM                81159
PSEDATA  DS    CL10          INPUT ITEM                          81159
SPCWORK  EQU   *             START OF XC CLEAR AREA              81159
PSLNUM   DS    H             NUMBER OF ITEMS TO BE DONE          81159
PSLLEN   DS    H             LENGTH OF EACH ITEM                 81159
PSLFLAG  DS    X             COPY OF PROCESSING FLAG             81159
PSLFORM  DS    X             LOCAL COPY OF FORMATTING OPTIONS    81159
PSLLEAD  DS    H             NUMBER OF BLANK LINES AFTER EJECT   81159
PSLTWEEN DS    H             NUMBER OF BLANK LINES BETWEEN ITEMS 81159
SPCLEAR  EQU   *-SPCWORK     LENGTH TO CLEAR WITH XC             81159
SPCSING  DS    X     CONTROL CHARACTERS :  SINGLE SPACE          81159
SPCDOUB  DS    X             DOUBLE SPACE                        81159
SPCTRIP  DS    X             TRIPLE SPACE                        81159
SPCOVER  DS    X             OVERPRINT (NO SPACE)                81159
SPCEJECT DS    X             PAGE EJECT                          81159
SPCCH9   DS    X             CHANNEL 9                           81159
SPCCH12  DS    X             CHANNEL 12                          81159
SPCCH12N DS    X             CHANNEL 12/NO PRINT                 81159
SPCLEN   EQU   *-SPCSING     L-1 MATCHES ASA/MCC TABLE           81159
         SPACE 1                                                 83331
         ORG   PSEPARM       REDEFINE FOR PSNAP FUNCTION         83331
SNPSAVE  DS    8F            SAVE AREA R2-R9                     83331
SNPFLAGS DS    0XL4          FLAG WORD                           83331
SNPFLAG1 DS    X             CONTROL FLAG                        83331
SNFMORE  EQU   X'80'           ANOTHER ENTRY FOLLOWS             83331
SNPFLAG2 DS    X                                                 83331
SNPFLAG3 DS    X                                                 83331
SNFSLEN  EQU   X'80'           FDSNAP ONLY - LEN FIELD IS S-FORMAT
SNFASCII EQU   X'01'           CONVERT FROM ASCII                83331
SNPFLAG4 DS    X                                                 83331
SNFHEX   EQU   X'80'           DUMP IN HEX ONLY                  83331
SNFDUAL  EQU   X'40'           PRINT HEX AND EBCDIC TEXT         83331
SNFLIST  EQU   SNFHEX+SNFDUAL  BOTH OFF - EBCDIC ONLY            83331
SNFVERT  EQU   SNFHEX+SNFDUAL  BOTH ON - PRINT VERTICAL HEX FORMAT
SNFUBASE EQU   X'04'           USER SUPPLIED BASE VALUE          83331
SNFABS   EQU   X'02'           SKIP ABSOLUTE ADDRESS             83331
SNFOFF   EQU   X'01'           BASE ADDRESS/OFFSETS TO BE DONE   83331
SNPADDR  DS    A      1/2    ADDRESS OF CURRENT DUMP GROUP       83331
SNPLEN   DS    A      2/2    LENGTH OF CURRENT REQUEST           83331
SNPBASE  DS    A             BASE ADDRESS OF CURRENT REQUEST     83331
SNPOFFS  DS    A             OFFSET FOR CURRENT LINE FROM BASE   83331
SNPLLEN  DC    H'0'          LOGICAL LENGTH OF ONE TEXT LINE     83331
         ORG   PSEPARM       REDEFINE FOR SCANNER                87312
SCNKEY   DC    2A(0)         ADDRESS/LENGTH OF KEYWORD           87312
SCNVAL   DC    2A(0)         ADDRESS/LENGTH OF VALUE             87312
SCNSEP   DC    X'00'         VALUE OF TRT TERMINATOR             87360
         ORG   ,                                                 83331
SCR0CLRL EQU   *-SCR0CLR     LENGTH TO CLEAR ON ENTRY            82031
         SPACE 1                                                 81159
PLSPACE  DC    CL((MAXWIDTH+5+3)/4*4)' '   SPACING RECORD        81159
PLSPACCC EQU   PLSPACE+4,1,C'C'        CARRIAGE CONTROL          81159
         DS    0F            ALIGN                               83331
DBSAVE   DS    CL256         SAVE AND WORK AREA FOR LIST/CONVERT 81193
FSWLEN   EQU   *-SCR0WORK      GETMAIN LENGTH                    82031
         SPACE 2
         DCBD  DSORG=PS      EXPAND IHADCB
         SPACE 2
         IEFJFCBN ,          EXPAND JFCB
         SPACE 2
CVTDSECT DSECT ,
         CVT   DSECT=YES
         SPACE 1
         IKJTCB ,
         SPACE 1
         IEZDEB ,                                                81159
         SPACE 1                                                 81159
TIODSECT DSECT
         IEFTIOT1 ,          MAP TIOT
         SPACE 1                                                 81159
         IEFUCBOB ,                                              81159
         SPACE 1                                                 82031
         REGEQU ,                                                82031
         SPACE 1                                                 83275
         IHARB ,                                                 83257
         SPACE 1                                                 83275
         AIF   (NOT &MVS).BIGEND                                 83331
         IHAPSA ,                                                83331
         IHAASCB ,                                               83331
         SPACE 1                                                 83331
.BIGEND  END   ,
