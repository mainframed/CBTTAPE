OTP      TITLE '* * * OVERLAY TREE PROCESSOR, VERSION 1.4 * * *'
         MACRO
&NAME    CHAIN &PARM,&R,&SKIP
         LCLC  &SAVE,&TYPE,&REG
         AIF   (T'&PARM EQ 'O').ERR1
&TYPE    SETC  '&PARM'(1,1)
&SAVE    SETC  '&PARM'(2,8)
         AIF   ('&SAVE' EQ '').ERR2
         AIF   ('&TYPE' NE '+').NOTPLUS
         AIF   (T'&SKIP NE 'O').SKIP
&NAME    SAVE  (14,12),,*
         AGO   .CHAIN
.SKIP    AIF   (T'&NAME EQ 'O').CHAIN
&NAME    DS    0H
.CHAIN   AIF   (T'&R EQ 'O').R12
         AIF   ('&R'(1,2) EQ 'NO').NOBASE
&REG     SETC  '&R'
         AGO   .REG
.R12     ANOP
&REG     SETC  'R12'
.REG     LR    &REG,R15            BASE
         USING &SYSECT,&REG        REGISTER
.NOBASE  LR    R5,R13              OLD SAVE AREA
         LA    R13,&SAVE           NEW SAVE AREA
         ST    R13,8(,R5)          CHAIN FORWARD
         ST    R5,4(,R13)          CHAIN BACK
         MEXIT
.NOTPLUS AIF   ('&TYPE' NE '-').ERR3
&NAME    L     R13,&SAVE+4         RESTORE R13
         RETURN (14,12),T
         MEXIT
.ERR1    MNOTE 8,'*** PARAMETER MISSING. ***'
         MEXIT
.ERR2    MNOTE 8,'*** SAVE AREA MISSING. ***'
         MEXIT
.ERR3    MNOTE 8,'*** TYPE &TYPE INVALID. ***'
         MEND
         SPACE 1
         PUNCH ' ORDER IEWLOTP(P) '    EASIER DUMP READING      GP10097
         SPACE 2
IEWLOTP  CSECT
         PRINT NOGEN         SAVE A TREE                        GP10097
         ENTRY IEWLDONE,IEWLQUIT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*    THE OVERLAY TREE PROCESSOR READS IN THE CESD AND SEGTAB FOR A    *
*     LOAD MODULE IN OVERLAY STRUCTURE, AND PRODUCES A GRAPHIC        *
*     REPRESENTATION OF THE OVERLAY STRUCTURE.  OPTIONALLY, IT WILL   *
*     PUNCH THE OVERLAY AND INSERT CARDS REQUIRED TO RECREATE THE     *
*     SAME OVERLAY STRUCTURE.                                         *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*    DDs:     SYSPRINT - MAPPING OF STRUCTURE                         *
*             SYSPUNCH (optional) CONTROL CARDS FOR RELINKING         *
*             SYSLIB   - LOAD MODULE PDS CONTAINING THE MEMBERS       *
*                                                                     *
*    PARM='DECK,NAME=module,NAME=module...'  DECK is optional         *
*        code augmented to allow:                                     *
*    PARM='DECK,NAME=name1,name2,...'        DECK is optional         *
*                  Changes and fix by Gerhard Postpischil 2010-04-07  *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 3
***** REGISTER EQUATES *****
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
***** EQUATES FOR CONSTANTS *****
         SPACE
K        EQU   1024
SP1      EQU   1
BLANK    EQU   C' '
ASTRSK   EQU   C'*'
PERIOD   EQU   C'.'
DASH     EQU   C'-'
COLON    EQU   C':'
DECK     EQU   X'01'
FINISH   EQU   X'04'
OVLY     EQU   X'20'
NE       EQU   X'08'
CNTRLREC EQU   X'01'
IDRREC   EQU   X'80'               IDR RECORD ID
PCDELETE EQU   X'14'
PCFIRST  EQU   X'20'
PC       EQU   X'04'
SD       EQU   X'00'
LR       EQU   X'03'
CM       EQU   X'05'
EDBITS   EQU   X'07'
SIGNBIT  EQU   X'0F'
SIGNBIT2 EQU   X'F0'
OPENBIT  EQU   X'10'
EQUAL    EQU   C'='
COMMA    EQU   C','
NMSRCH   EQU   X'01'
NMRCVD   EQU   X'02'
BUFUSED  EQU   X'01'
LINFULL  EQU   X'02'
LOOKAHED EQU   X'40'
NOENTRYS EQU   X'00'
NULENTRY EQU   X'00'
OFF      EQU   X'00'
ONE      EQU   1
SEGIN    EQU   X'04'
SPECON   EQU   X'80'
MULT4    EQU   2
DOLLAR   EQU   C'$'
TRIPLE   EQU   C'-'
DIGIT    EQU   X'20'
CONLN    EQU   X'20'
FRSTRCVD EQU   X'10'
TABLLN   EQU   256
ALLBITS  EQU   X'FF'
CHAR0    EQU   C'0'
ERRLVL4  EQU   X'08'               IN SWITCHES; LEFT ON = R.C. OF 4
FOUR     EQU   4
NOTEQ    EQU   7                   CONDITION FOR BCR
         EJECT
         SAVE  (14,12),,*
         LR    R12,R15
         USING IEWLOTP,R12
         L     R11,0(,R1)          ADDRESS OF PARAMETERS
         GETMAIN R,LV=DSECTEND-DSECT,SP=SP1
         LR    R2,R1
         USING DSECT,R2
         USING ESDDATA,R4
         CHAIN +SAVEAREA,NOREG,NOSAVE
         GETMAIN VU,LA=COREWANT,A=COREGOT,SP=SP1
         LM    R7,R8,COREGOT       R7=ADDRESS, R8=LENGTH
         S     R8,FOURK            GIVE 4K BACK FOR MISC. GORBLE
         LR    R9,R8
         AR    R9,R7               R9=ADDRESS AT WHICH TO FREE 4K
         ST    R8,COREGOT+4        SAVE CORE KEPT
         FREEMAIN R,LV=4*K,A=(R9),SP=SP1
         SPACE 3
***** CLEAR BEGINNING OF DSECT AREA *****
         SPACE
         XC    ASTERISK(256-ASTERISK+DSECT),ASTERISK  DON'T CLOBBER
         XC    256(256,R2),256(R2)  SAVEAREA
         XC    512(READBFR-DSECT-512,R2),512(R2)
         SPACE 3
***** ALLOCATE CORE FOR TABLES. *****
         SPACE
         LA    R10,0(,R7)          BEGIN OF AVAIL. CORE; ZERO HI BYTE
         LA    R9,ALLOCTAB         ALLOCATION TABLE
         LA    R6,ALLOC2-ALLOCTAB  INCREMENT
         LA    R7,ALLOCLST         END FOR BXLE
ALLOCATE LR    R5,R8               LENGTH OF CORE GOTTEN
         SR    R4,R4
         MH    R5,0(,R9)           % TO ALLOCATE TO THIS TABLE
         D     R4,HUNDRED          R5 = AMT. OF CORE IN BYTES FOR TBL.
         CH    R5,2(,R9)           R5 : MINIMUM
         BH    ALLOC01             >
         LH    R5,2(,R9)           DEFAULT TO MIN.
ALLOC01  CH    R5,4(,R9)           R5 : MAXIMUM
         BL    ALLOC02             <
         LH    R5,4(,R9)           USE MAX.
ALLOC02  LH    R3,6(,R9)           PUT BEGIN ADDRESS
         ST    R10,0(R2,R3)        WHERE IT GOES.
         LA    R5,3(,R5)           ROUND UP
         SRL   R5,MULT4            TO NEXT
         SLL   R5,MULT4            FULLWORD BDRY.
         AR    R10,R5              NEXT CORE AVAILABLE.
         LH    R3,8(,R9)           END ADDRESS
         LTR   R3,R3               NEEDED?
         BZ    ALLOC03             (NO)
         ST    R10,0(R2,R3)        SAVE IT.
ALLOC03  BXLE  R9,R6,ALLOCATE      FOR NEXT ENTRY.
         LH    R3,6(,R9)           TAKE CARE OF CESD NOW
         ST    R10,0(R2,R3)        START ADDRESS
         LH    R3,8(,R9)           END LOCATION
         LM    R7,R8,COREGOT
         AR    R7,R8               R7 = END ADDRESS
         ST    R7,0(R2,R3)         SAVE ADDRESS
         SPACE 2
         LA    R1,READBFR+TABLLN
         ST    R1,RCBFEND          END OF READ BUFFER
         L     R1,HEXTBLND
         S     R1,HEXTBLBG         - BEGIN ADDRESS
         SRL   R1,MULT4            / 4
         STH   R1,MAXENTS          # OF ENTRIES AVAILABLE.
         SPACE 2
         MVI   MESSAGE,BLANK
         MVC   MESSAGE+1,MESSAGE   BLANK OUT LINE
         MVI   ASTERISK,ASTRSK
         MVC   ASTERISK+1(15),ASTERISK  INSERT ******'S
         SPACE 2
         LA    R1,12               OPEN
         L     R15,V@IO            SYSPRINT, SYSLIB
         BALR  R14,R15
         SPACE 3
***** GET TIME AND DATE *****
         SPACE
         TIME  DEC
         SRL   R0,4                MOVE OVER FOR CORRECT UNPK.
         STM   R0,R1,ADAREA
         OI    ADAREA+7,SIGNBIT
         UNPK  SAVEDATE+1(5),ADAREA+5(3)
         MVC   SAVEDATE(2),SAVEDATE+1
         MVI   SAVEDATE+2,PERIOD   DATE IS IN SAVEDATE IN FORM YY.DDD
         UNPK  SAVETIME+2(6),ADAREA(4)
         OI    SAVETIME+7,SIGNBIT2
         MVC   SAVETIME(2),SAVETIME+2
         MVI   SAVETIME+2,COLON
         MVC   SAVETIME+3(2),SAVETIME+4
         MVI   SAVETIME+5,COLON    TIME IS IN SAVETIME AS HH:MM:SS
         SPACE 3
***** PROCESS PARAMETERS. *****
         SPACE
         XC    READBFR,READBFR     CLEAR READ BUFFER
         L     R15,V@OPT
         BALR  R14,R15             PROCESS OPTIONS
         SPACE
         MVC   LIST(12),BLDLCNT    INITIALIZE BLDL PARMS.
         TM    OPTSW,DECK          DECK OPTION?
         BZ    ITERATE
         LA    R1,24               OPEN SYSPUNCH
         L     R15,V@IO
         BALR  R14,R15
         SPACE 3
***** PROCESS ANOTHER NAME *****
         SPACE
ITERATE  MVC   BLDLMBRN,BLANKS     BLANK OUT MEMBER NAME
         LM    R7,R8,BEGNMBFR      NAME BUFFER
         SR    R9,R9
         IC    R9,0(,R7)           LENGTH OF THIS NAME
         S     R9,CON1             - 1
         EX    R9,MVCNAME
         LA    R7,2(R7,R9)         -> NEXT NAME
         ST    R7,BEGNMBFR         & SAVE
         CR    R7,R8
         BL    *+8                 NOT END OF PARMS
         OI    OPTSW,FINISH        YES.
         SPACE 3
***** DO BLDL AND CHECK ATTRIBUTES *****
         SPACE
         OI    SWITCHES,ERRLVL4    IF LEFT ON, RETURN CODE = 4
         LA    R1,20               ISSUE BLDL
         L     R15,V@IO
         BALR  R14,R15
         TM    BLDLATT1,OVLY       OVERLAY?
         BZ    NOTOVLY
         TM    BLDLATT2,NE         IS THERE A CESD?
         BO    NOTEDTBL
         NI    SWITCHES,ALLBITS-ERRLVL4  TURN OFF SWITCH
         SPACE 3
***** READ IN CESD; KEEP SD, CM, PC, PD; FIND EP. *****
         SPACE
         NI    INTERN1,ALLBITS-PCFIRST
         XC    ENTRY,ENTRY         RESET ENTRY NAME.
         LM    R6,R7,CESDADR
READ     LA    R4,READBFR          READ IN CESD
         LA    R3,TABLLN
         BAL   R14,READRTN
         CLI   0(R4),IDRREC        IS THIS AN IDR RECORD?
         BE    READ                YES, SKIP IT.
         CLI   0(R4),CNTRLREC      CONTROL RECORD?
         BE    CESDFULL
         LH    R9,6(,R4)           LENGTH OF ESD FIELDS
         L     R8,CON16            INCREMENT
         AR    R9,R4               ADDRESS + LENGTH
         A     R4,CON8             SKIP CONTROL INFORMATION
CHECK    CLI   ESDTYPE,PCDELETE    PD?
         BNE   PVT                 NO; CHECK FOR PC.
         TM    INTERN1,PCFIRST     FIRST PD ITEM?
         BO    ENDTAB              YES; GO.
         OI    INTERN1,PCFIRST     SET SWITCH
         MVC   ESDNAME,=CL8'$SEGTAB'  NAME IS $SEGTAB
         B     MOVE
ENDTAB   MVC   ESDNAME,=CL8'$ENTAB'  NAME IS $ENTAB
         B     MOVE
PVT      CLI   ESDTYPE,PC          PC?
         BNE   CLRBITS
         MVC   ESDNAME,=CL8'$PRIVATE'  NAME IS $PRIVATE
         B     MOVE
CLRBITS  NI    ESDTYPE,EDBITS      TURN OFF LKED FLAGS
         BZ    TESTEP              ZERO IF SD
         CLI   ESDTYPE,LR          LR?
         BE    TESTEP
         CLI   ESDTYPE,CM          CM?
         BNE   UPDATE
         CLI   ESDNAME,BLANK       BLANK COMMON?
         BNE   MOVE                NO; OK.
         MVC   ESDNAME,=CL8'$BLKCOM'  YES; NAME IS $BLKCOM
         B     MOVE
TESTEP   CLC   ESDLKAD,BLDLEP      IS THIS THE EP?
         BNE   NOTTHEEP
         MVC   ENTRY,ESDNAME       YES; SAVE NAME.
NOTTHEEP CLI   ESDTYPE,SD
         BNE   UPDATE              IF LR.
MOVE     MVC   0(16,R6),ESDENTRY   SAVE IN CESD BUFFER.
         XC    10(2,R6),10(R6)     SET TO 0 FOR PRINT POS.
         AR    R6,R8
         CR    R6,R7               NEXT SPACE IN CESD BUFFER
         BNL   MBOVRFLW            CESD BUFFER OVERFLOW.
UPDATE   BXLE  R4,R8,CHECK
         B     READ                READ IN ANOTHER RECORD.
         SPACE 3
***** READ IN SEGTAB; CALL IEWLDECK IF NEEDED. *****
         SPACE
CESDFULL ST    R6,CESDUSED         LENGTH USED.
         LH    R3,14(R4)           GET LENGTH OF SEGTAB FROM CCW.
         LM    R4,R5,SEGBFBEG
         LR    R8,R4               SEGTAB BUFFER BEGIN
         AR    R8,R3               + SEGTAB LENGTH
         CR    R8,R5               -- LARGE ENOUGH?
         BNL   SGBOVFLW            NO.
         BAL   R14,READRTN         READ IT IN.
         ST    R8,SEGUSED          SAVE UPPER LIMIT.
         TM    OPTSW,DECK          DECK WANTED?
         BZ    DECKOUT             NO.
         L     R15,V@DECK
         BALR  R14,R15             PRODUCE DECK.
         SPACE 3
***** REFORMAT SEGTAB: X'PPLLLLLL' WHERE PP = PREVIOUS SEG. #  *****
*****  AND LLLLLL = SEG. LENGTH *****
         SPACE
DECKOUT  MVC   REGION#,CON1+2      SET REGION # TO 1
         XC    READBFR,READBFR     CLEAR THE BUFFER FOR TR TABLE.
         MVC   READBFR+CHAR0(16),EBCDIC  MOVE CHARACTERS TO BUFFER
         L     R9,CON1             1ST SEG. #
         L     R10,CON16           INCREMENT FOR CESD
         L     R7,SEGBFBEG         BEGIN OF SEGTAB
         LA    R7,24(,R7)          + 24 -> SEG # 1
         L     R8,SEGUSED          END OF SEGTAB
         L     R11,CESDUSED        END OF CESD
         SR    R11,R10             -16 FOR BXLE
         L     R5,COREGOT
         A     R5,COREGOT+4        END OF CESD BUFFER -- CHECK OVFL.
         SR    R5,R10              -16 LIKE R11
OTP005   L     R4,CESDADR          BEGIN OF CESD TABLE
         SR    R3,R3               ZERO LENGTH COUNT
OTP006   SR    R1,R1
         IC    R1,ESDSGNO          INSERT SEG #
         CR    R1,R9               CURRENT SEG. #?
         BNE   OTP004              NO. GO.
         L     R1,ESDIDLN-1        LENGTH OF CTRL. SECTION
         LA    R1,7(,R1)           CLEAR HI-ORDER BYTE & ROUND UP.
         SRL   R1,3
         SLL   R1,3                TO A MULTIPLE OF 8
         AR    R3,R1               ADD TO LAST LENGTH
OTP004   BXLE  R4,R10,OTP006       INCREMENT CESD PTR & CONTINUE
         ST    R3,ADAREA           SAVE TOTAL LENGTH
         MVC   1(3,R7),ADAREA+1    & MOVE TO SEGTAB ENTRY
         UNPK  ADAREA(8),1(4,R7)   CONVERT TO HEX
         TR    ADAREA(7),READBFR   & MAKE PRINTABLE
         AR    R11,R10             ADD 16 TO CESD END.
         CR    R11,R5              OVERFLOW?
         BNL   MBOVRFLW            YES...
         MVI   0(R11),DOLLAR       CREATE A NEW CESD ENTRY
         MVC   1(6,R11),ADAREA+1   GIVING SEGMENT LENGTH
         MVI   7(R11),BLANK
         XC    8(8,R11),8(R11)     CLEAR REST OF LINE
         STC   R9,12(,R11)         & INSERT SEG. #
         A     R9,CON1             INCREMENT SEG #
         A     R7,CON4             & SEGTAB PTR.
         CR    R7,R8               END OF SEGTAB?
         BL    OTP005              NO; CONTINUE
         AR    R11,R10             ADD 16 TO FIX END OF CESD ADDR.
         ST    R11,CESDUSED        SAVE NEW END-OF-CESD PTR.
         SPACE 2
         LH    R8,REGION#
NXTREGN  MVI   MESSAGE,TRIPLE      MAKE SURE NOT AT TOP
         BAL   R14,PRTRTN          OF NEW PAGE ALREADY.
         STC   R8,MESSAGE+121      FOR HEADER LINE
         SLL   R8,1                DOUBLE FOR INDEX
         SR    R10,R10
         L     R3,SEGBFBEG         BEGIN OF SEGTAB
         IC    R10,6(R8,R3)        HIGHEST SEG. # IN THIS REGION.
         LTR   R10,R10             LAST REGION?
         BZ    IEWLQUIT            END FOR THIS MEMBER.
         STC   R10,HSNOPRCS        OTHERWISE, SAVE.
         SPACE 3
***** PRINT HEADER *****
         SPACE
         MVC   MESSAGE(30),=CL30'1 OVERLAY TREE PROCESSOR V 1.4'
         MVC   MESSAGE+62(8),BLDLMBRN  MEMBER NAME
         MVC   MESSAGE+114(6),=C'REGION'
         OI    MESSAGE+121,SIGNBIT2  MAKE REGION # PRINTABLE
         BAL   R14,PRTRTN          & PRINT IT ALL
         MVC   MESSAGE+2(4),=C'DATE'
         MVC   MESSAGE+7(6),SAVEDATE  MOVE IN DATE
         MVC   MESSAGE+15(4),=C'TIME'
         MVC   MESSAGE+20(8),SAVETIME  MOVE IN TIME
         BAL   R14,PRTRTN          & PRINT IT OUT
         MVI   MESSAGE,C'0'        DOUBLE-SPACE ON NEXT PRINT.
         BAL   R14,PRTRTN          & PRINT IT OUT
         L     R4,DEPREGS+4
         BAL   R6,CLEARCOR         CLEAR DEPENDENCY TABLE
         L     R4,PSREGS
         BAL   R6,CLEARCOR         CLEAR PSSRCHEL TABLE
         XC    DEPCNT(2),DEPCNT
         XC    PSRCHCNT(2),PSRCHCNT  CLEAR COUNTS
         MVC   0(4,R4),MIDLINE     SET UP 1ST SEARCH ELEMENT
         MVC   CEXTPTR(4),EXTBLBEG  SET PROCESSING PTR
         MVC   EXTPTR(4),EXTBLBEG  SET 'ADD TO' PTR
         SPACE 3
***** BUILD ALL TABLES USED TO PRODUCE MAP. *****
         SPACE
FND      MVI   SWITCHES,OFF        RESET SWITCHES
LOKSTRT  NI    DEPTEST,OFF         SET DEPTEST OFF
         LH    R8,PSRCHCNT         # OF ELEMENTS IN SRCH TBL
         LH    R11,DEPCNT          # OF DEPENDENCIES
         L     R9,DEPREGS+4        A(DEPEND)
         L     R5,PSREGS           A(PSSRCHEL)
NXTSRCH  L     R4,CESDADR          A(CESD)
COMPARE  CLC   ESDSGNO,HSNOPRCS    IN RANGE?
         BH    GETNXT              NO; SKIP.
         CLI   ESDSGNO,NULENTRY    SEG # 0?
         BE    GETNXT              YES; SKIP.
         SR    R1,R1
         IC    R1,ESDSGNO          SEG #
         BAL   R14,PREVSGN
         OI    SWITCHES,BUFUSED    INDICATE BUFFER USED.
         CLC   PSGT(1),0(R5)       COMPARE WITH SEARCH ELEMENT
         BNE   GETNXT              ¬=? SKIP.
         IC    R14,ESDSGNO         SAVE IN R14
         LR    R10,R9              A(DEPEND)
         LR    R7,R11              FOR BCT
CNXTDEP  CLC   ESDSGNO,0(R10)      IS SEG # IN TABLE?
         BE    GETNXT              YEAH; SKIP.
         STC   R14,DEPTEST         SAVE SEG #
         A     R10,CON4            ADD 4 TO ADDRESS
         LTR   R7,R7
         BZ    *+8                 END OF TABLE? IF SO, SKIP BCT
         BCT   R7,CNXTDEP          CHECK REST OF TABLE
         CLI   DEPTEST,NOENTRYS    ANY ENTRY FOUND?
         BE    GETNXT              NO; SKIP.
         LR    R1,R11
         SLL   R1,MULT4            X 4
         STC   R14,0(R1,R9)        SAVE SEG # IN DEPEND
         A     R11,CON1            UPDATE
         CH    R11,MAXDEPLN        TOO LARGE?
         BH    DEPOVFLW            YEAH.  TUFF.
         IC    R0,1(,R5)           GET COUNT
         A     R0,CON1             ADD 1
         STC   R0,1(,R5)           & SAVE IT.
         NI    DEPTEST,OFF         SET OFF
GETNXT   A     R4,CON16            NEXT IN CESD
         C     R4,CESDUSED         END OF CESD BUFFER?
         BL    COMPARE             NO.
         A     R5,CON4             NEXT SEGTAB ENTRY
         LTR   R8,R8
         BZ    *+8
         BCT   R8,NXTSRCH          LOOK AGAIN.
         STH   R11,DEPCNT          SAVE COUNT.
         SPACE 3
***** ALL SELECTED ENTRIES ARE IN BUFFER.  SORT ENTRIES *****
*****   AND CALCULATE PRINT POSITIONS.                  *****
         SPACE
END      TM    SWITCHES,BUFUSED    BUFFER USED?
         BZ    CHKEXT              END OF REGION; CK EXTENSIONS.
         SPACE
***** SORT: *****
*         1) BY # ENTRIES IN PSSRCHEL (MAJOR)
*         2) BY # DEPENDENCIES PSSRCHEL+1 (INT)
         SPACE
         L     R3,DEPREGS+4        A(DEPEND)
         LM    R4,R5,PSREGS        A(PSSRCHEL,PSRCHCNT)
         SLL   R5,MULT4            X 4
         AR    R5,R4               = END OF TABLE
MAJOR    SR    R7,R7               INDEX FOR INTERMEDIATE
         IC    R7,1(,R4)           # DEPENDENCIES
         LTR   R7,R7
         BZ    NOSORT              SKIP IF 0.
INT      LR    R6,R7               LOOP COUNT
         LR    R8,R3               A (LAST WINNER)
MINOR    CLC   0(1,R3),0(R8)       COMPARE WINNER WITH NEXT CHALLENGER
         BNH   WONAGAIN            UPDATE CHALLENGER
         XC    0(4,R8),0(R3)       IF CHALLENGER LESS,
         XC    0(4,R3),0(R8)       SWAP WINNER
         XC    0(4,R8),0(R3)       FOR CHALLENGER
WONAGAIN A     R8,CON4             ADD 4 TO A(CHALLENGER)
         BCT   R6,MINOR            MINOR LOOP
         A     R3,CON4             ADD 4 TO A(WINNER)
         BCT   R7,INT              INTERMEDIATE LOOP
NOSORT   A     R4,CON4             ADD 4 TO A(SEARCH TABLE)
         CR    R4,R5               END OF TABLE?
         BL    MAJOR               MAJOR LOOP.
         SPACE
* END OF SORT
         SPACE
         LM    R5,R7,PSREGS-4
LOOKUP   CLI   1(R6),ONE           ONLY ONE ELEMENT DEPENDENT?
         BL    LOOK                IF 0 DO NOT UPDATE ADDRESSES
         BH    ALTFORM             IF NOT USE ALTERNATE FORMULA
         MVC   2(2,R5),2(R6)       MOVE LINE COUNT
         A     R5,CON4             UPDATE PS TOL ADR FOR NXT SRCH ELMNT
LOOK     A     R6,CON4             UPDATE FOR NEXT ELEMENT
         LTR   R7,R7               ZERO?
         BZ    *+8                 IF SO, SKIP BCT
         BCT   R7,LOOKUP           CONTINUE UNTIL EXHAUSTED
         SPACE 3
***** OFLWCHK *****
* DEPEND HAS BEEN BUILT; PRINT POSITIONS ARE IN ALL PRINTABLE SEG. #S.
*  UNPRINTABLE SEG. #S ARE REMOVED TO HEXTPRCS FOR LATER PROCESSING
*  BY HEXTPRCS.
         SPACE
OFLWCHK  LM    R3,R4,PSREGS
         TM    SWITCHES,LOOKAHED   LOOKAHEAD REQUESTED?
         BO    LOKAHED1
         L     R8,DEPREGS+4        A(DEPEND)
         L     R9,HEXTBLBG         EXTENSION TABLE
LOOP01   SR    R6,R6
         IC    R6,1(,R3)           # OF DEPENDENT SEG. #S
         LTR   R6,R6
         BZ    TESTCNT             IF ZERO.
COMP04   CLC   2(2,R8),MAXPOS      < MAX. POSITION?
         BNL   OVERFLOW            NO; PUT IN TABLE
         CLC   2(2,R8),MINPOS      > MINIMUM POSITION?
         BL    OVERFLOW            NO; PUT IN TABLE
         A     R8,CON4             UPDATE ADDRESS
NEXTNTRY BCT   R6,COMP04           BCT ON # DEP.
TESTCNT  A     R3,CON4             UPDATE PSSRCHEL ADDRESS
         LTR   R4,R4               ZERO?
         BZ    *+8
         BCT   R4,LOOP01           TOTAL # ENTRIES IN PSSRCHEL.
         SPACE 3
***** FIND CSECTS W/ SEG. # = CURRENT SEARCH SEG. #.    *****
*****   IF EQUAL, MOVE FROM DEPEND TO CSECT POS. IN BUFFER *****
         SPACE
CYCLEFIL LM    R6,R7,DEPREGS
NTBLENT  L     R8,CESDADR
MATCH    CLC   12(1,R8),0(R7)      = SEARCH SEG. #?
         BNE   PASS1
         MVC   10(2,R8),2(R7)      MOVE LINE POS. TO BUFFER
PASS1    A     R8,CON16            UPDATE BUFFER PTR.
         C     R8,CESDUSED         LAST ENTRY?
         BL    MATCH
         A     R7,CON4             INCREMENT THIS ALSO.
         LTR   R6,R6
         BZ    *+8
         BCT   R6,NTBLENT
         SPACE 3
***** PRINT ROUTINE *****
*   AN ITEM WILL BE PRINTED OUT IF:
*     1) IT IS NOT NULL, AND
*     2) ITS PRINT POS. ¬= 0, AND
*     3) AT LEAST 8 SPACES FREE AT THE PRINT POS.
         SPACE
         NI    SWITCHES,ALLBITS-SEGIN  TURN OFF SEG-#-PRINTED SW.
PRINTMGR L     R6,CESDADR
         LA    R5,MESSAGE
PLCELINE LR    R7,R5               RESTORE POINTER
         CLI   12(R6),NULENTRY     NULL (= PRINTED) ?
         BE    UPDTEBAC            IF SO, SKIP.
         CLC   10(2,R6),MINPOS     < MIN. POS.?
         BL    UPDTEBAC            IF SO, SKIP
         AH    R7,10(,R6)          ADD LINE POS. TO MESSAGE PTR.
         CLC   0(8,R7),BLANKS      8 FREE SPACES?
         BNE   UPDTEBAC            IF NOT SKIP
         TM    SWITCHES,SEGIN      SEG. #S PRINTED YET?
         BZ    ICSGNO              NO; DO IT.
MOVEINLN MVC   0(8,R7),0(R6)       MOVE IN CSECT NAME
         OI    SWITCHES,LINFULL    ITEM IN LINE
         MVI   12(R6),NULENTRY     INDICATE MOVED BY SETTING NULL
UPDTEBAC A     R6,CON16            UPDATE BUFFER ADDRESS
         C     R6,CESDUSED         END OF CESD BUFFER?
         BL    PLCELINE            NO; PLACE NEXT ITEM IN LINE.
         TM    SWITCHES,LINFULL    ANYTHING IN LINE?
         BO    WRITE               IF SO, PRINT IT.
         CLI   DEPCNT+1,NOENTRYS   ANY DEPENDENCIES?
         BE    CHKEXT              NO; CHECK EXTENSIONS.
         BAL   R15,SWAP            SWAP TABLES
         B     FND
         SPACE 3
***** CHECK FOR SPLITS OFF PREVIOUS SEG #S WHICH COULDN'T PRINT *****
         SPACE
CHKEXT   LM    R4,R5,CEXTPTR       BEGIN ADDR & CURRENT PRCS. ADDR
         CR    R4,R5               = ?
         BE    HEXTPRCS            IF SO, ADDRESSES THE SAVE # ENTRIES
         BAL   R14,EDIT            EDIT SEG #
PRINTSGN MVI   MESSAGE,TRIPLE      TRIPLE SPACE
         MVC   MESSAGE+52(30),=15C'- '  A LINE OF -'S
         BAL   R14,PRTRTN          & PRINT IT.
         MVI   MESSAGE,TRIPLE      TRIPLE SPACE AGAIN.
         TM    SWITCHES,SPECON     SPECIAL PROCESSING?
         BZ    VERT                NO; USE VERTICAL MSG.
         MVC   MESSAGE+60(12),=C'*HORIZONTAL*'  HORIZONTAL MSG
         B     PRT
VERT     MVC   MESSAGE+60(12),=C'* VERTICAL *'  VERTICAL MSG
PRT      BAL   R14,PRTRTN          PRINT IT.
         MVC   MESSAGE+59(15),=C'EXTENSION UNDER'
         BAL   R14,PRTRTN
         CLC   EDITSEG#(4),BLANKS  UNDER REGION ORIGIN?
         BNE   PRT1                NO.
         MVC   MESSAGE+60(13),=C'REGION ORIGIN'  YES.
         B     PRT2
PRT1     MVC   MESSAGE+60(9),=C'SEGMENT #'
         MVC   MESSAGE+69(3),EDITSEG#+1  MOVE IN SEG #
PRT2     MVC   EDITSEG#(4),PATTERN  & RESTORE PATTERN
         BAL   R14,PRTRTN
         TM    SWITCHES,SPECON     SPECIAL PROCESSING?
         BO    PRINTED
         L     R1,PSREGS
         MVC   0(4,R1),0(R4)       MOVE SEG # TO SEARCH TABLE
         MVC   2(2,R1),MIDLINE+2   CENTER PRINT POS.
         MVC   PSRCHCNT(2),CON1+2
         A     R4,CON4             UPDATE TABLE ADDRESS
         ST    R4,CEXTPTR          SAVE UPDATED PTR
         XC    DEPCNT(2),DEPCNT    CLEAR COUNT FIELD
         B     FND
         SPACE 3
***** CHECK FOR SEGMENTS > HORIZ. LENGTH OF PAGE.     *****
*****  IF SO, START NEW GROUP, W/ 8 ENTRIES ACROSS.   *****
         SPACE
HEXTPRCS CLI   HEXTCNT+1,NOENTRYS  ANY ENTRIES?
         BE    ENDRGN              NO; GO TO NEXT REGION.
         L     R4,PSREGS
         BAL   R6,CLEARCOR         CLEAR PSSRCHEL TABLE
         L     R4,HEXTBLBG         HEXT. TABLE ADDR.
         L     R7,PSREGS
         MVI   0(R7),ALLBITS       INIT. PREV. SEG # TO 255.
         LH    R9,HEXTCNT          # ENTRIES IN HEXTBL
         SR    R10,R10             WILL CONTAIN NEW PSSRCHEL
LOOP1    LR    R5,R7               A(PSSRCHEL)
         LR    R8,R10              PSRCHCNT
         SR    R1,R1
         IC    R1,0(,R4)           SEG. #
         BAL   R14,PREVSGN         GET PREV. SEG. #
LOOP2    CLC   PSGT(1),0(R5)       THIS = ENTRY IN PSSRCHEL?
         BNE   NOTEQUAL
         IC    R0,1(,R5)           GET COUNT
         A     R0,CON1             ADD 1
         STC   R0,1(,R5)           & SAVE IT.
ADD01    A     R4,CON4             UPDATE COUNT
         LTR   R9,R9               ZERO?
         BZ    *+8
         BCT   R9,LOOP1
         STH   R10,PSRCHCNT        SAVE TOTAL COUNT
         BAL   R14,MOVER3          HEXTBL -> DEPEND
         MVC   DEPCNT(2),HEXTCNT   SET COUNT
         XC    HEXTCNT(2),HEXTCNT  ZERO IT.
         L     R4,PSREGS
         BAL   R14,EDIT            EDIT SEG. #
         OI    SWITCHES,SPECON     SPECIAL PROCESSING
         B     PRINTSGN            PRINT HEADING
* RTN PLACES PAGE POSITION IN PSSRCHEL.
PRINTED  L     R4,PSREGS
         MVC   2(2,R4),MIDLINE+2   CENTER PAGE POS.
         MVI   SWITCHES,BUFUSED    BUFFER USED
         B     END                 PROCESS ENTRIES
         SPACE 2
NOTEQUAL A     R5,CON4             UPDATE PSSRCHEL ADDRESS
         LTR   R8,R8               ZERO?
         BZ    *+8
         BCT   R8,LOOP2
         LR    R1,R10              NO ENTRIES IN PSSRCHEL
         SLL   R1,MULT4            X 4
         AR    R1,R7               + BEGIN OF PSSRCHEL
         MVC   0(1,R1),PSGT        MOVE SEG. # TO TABLE
         MVI   1(R1),ONE           ONE DEPENDENCY
         A     R10,CON1            ADD ONE TO COUNT
         B     ADD01
         SPACE 3
***** TO NEXT REGION *****
         SPACE
ENDRGN   LH    R8,REGION#
         A     R8,CON1             INCREMENT REGION #
         STH   R8,REGION#
         C     R8,CON4             > 4?
         BNH   NXTREGN             NO; PROCESS NEXT REGION.
         SPACE
IEWLQUIT TM    SWITCHES,ERRLVL4    LEVEL 4 ERROR?
         BZ    NEXTMBR             NO.
         OI    RETCODE+1,FOUR      YES. SET IT.
NEXTMBR  TM    OPTSW,FINISH        DID LAST MEMBER?
         BZ    ITERATE             NO; CONTINUE
         SPACE
         NI    RETCODE+1,FOUR      LEAVE ON ONLY RET. CODE OF 4
IEWLDONE L     R1,CON16            CLOSE
         L     R15,V@IO            FILES.
         BALR  R14,R15
         SPACE
***** FREE CORE *****
         SPACE
         FREEMAIN V,A=COREGOT,SP=SP1  FREE TABLES
         L     R13,SAVEAREA+4      RESTORE R13 BEFORE FREEING CORE.
         FREEMAIN R,LV=DSECTEND-DSECT,A=(R2),SP=SP1  FREE DSECT
         LH    R15,RETCODE         SET RETURN CODE.
         RETURN (14,12),T,RC=(15)  RETURN TO O.S......
         SPACE 5
***** READ ROUTINE *****
         SPACE
READRTN  L     R1,CON8
         L     R15,V@IO
         BR    R15                 READ IN FROM SYSLIB
         SPACE 3
***** PRINT ROUTINE *****
         SPACE
PRTRTN   SR    R1,R1
         L     R15,V@IO
         BR    R15                 PRINT A LINE
         SPACE 5
***** ICSGNO *****
*   THIS ROUTINE INSERTS AN * BENEATH THE SEGMENT BEING CONTINUED,
*     AND BUILDS A LINE OF SEGMENT NUMBERS.
         SPACE
ICSGNO   STM   R5,R7,ICSAVE
         MVC   MSAVE,MESSAGE       RESET MESSAGE SAVE AREA
         LM    R3,R4,DEPREGS
KEEPFIL  BAL   R14,EDIT            EDIT SEG #
         LA    R9,MSAVE            MESSAGE SAVE AREA
         AH    R9,2(,R4)           + PRINT POS. OF CSECT NAME
         MVC   2(3,R9),EDITSEG#+1  SEGMENT #
         MVC   EDITSEG#(4),PATTERN  RESTORE PATTERN
         LR    R1,R6               SEGMENT #
         BAL   R14,PREVSGN         GET PREVIOUS SEG. #
         LA    R14,PAZLN           RETURN ADDRESS
         LM    R7,R8,PSREGS
CHECKER  CLC   PSGT(1),0(R7)       IS THIS THE ONE I WANT?
         BE    INSRTASK            IF SO, PUT IN *
         LTR   R8,R8               IS COUNT 0?
         BZ    PAZLN               IF SO, SKIP BCT
         A     R7,CON4             ADD 4 TO ADDRESS
         BCT   R8,CHECKER
PAZLN    A     R4,CON4             + 4 FOR NEXT DEPENDENCY
         LTR   R3,R3
         BZ    *+8
         BCT   R3,KEEPFIL
         BAL   R14,PRTRTN          PRINT OUT LINE
         MVC   MESSAGE,MSAVE       WRITE OUT * AT PSG LOCATION
         SPACE
***** INSERT HORIZONTAL LINE OF ASTERISKS *****
*  READ BUFFER IS USED AS TRT TABLE; SEGMENT #S ARE CONNECTED.
         SPACE
         XC    READBFR,READBFR     CLEAR READ BUFFER
         MVC   READBFR+CHAR0(10),EBCDIC  STOPPER BYTES
         MVI   READBFR+DASH,DASH   AND A DASH FOR NEW CODE.
         LA    R14,PLUSLOC         RETURN ADDRESS
         LA    R6,L'MESSAGE
         LA    R3,MESSAGE
         NI    SWITCHES,ALLBITS-CONLN-FRSTRCVD  RESET SWITCHES
         LM    R7,R8,PSREGS
NEWCNT   CLI   1(R7),ONE           ONLY 1 SEG. DEPENDENT?
         BE    ASTERINS            IF SO, INSERT *
         BL    PLUSLOC             IF NO SEGS, SKIP TO NEXT ENTRY
         SPACE
* CREATE CONNECTIVE LINE BETWEEN RELATED SEG. #S
HORIZ    SR    R10,R10
         IC    R10,1(,R7)          # OF DEPENDENT SEG #S
HORIZ1   LR    R0,R2               SAVE BASE ADDR. FOR DSECT
EXECUTE  EX    R6,TRT              TRT THROUGH MESSAGE
         BNE   LNMAKR              SAVE ADDRESS
PLUSLOC  A     R7,CON4
         LTR   R8,R8
         BZ    *+8
         BCT   R8,NEWCNT
         TM    SWITCHES,CONLN      IF A CONNECTIVE LINE, MUST PRT.
         BZ    *+8
         BAL   R14,PRTRTN          GO PRINT.
         MVC   MESSAGE,MSAVE       RESTORE MESSAGE (SEGMENT #S)
         BAL   R14,PRTRTN          & PRINT IT.
         SPACE 3
***** CHECK WHETHER SEGMENTS WOULD OVERLAY EACH OTHER. *****
*       IF SO, PROCESSING OF ONE SEGMENT MUST BE DELAYED.
         SPACE
         BAL   R15,SWAP            SWAP TABLES
         OI    SWITCHES,LOOKAHED   SET LOOK AHEAD BIT ON
         B     LOKSTRT             SKIP SWITCH RESET
LOKAHED1 NI    SWITCHES,ALLBITS-LOOKAHED  RESET LOOK-AHEAD BIT
         LM    R9,R10,PSREGS
         LM    R3,R4,DEPREGS
LOOKSET  LR    R1,R9
         LR    R6,R10              # OF ENTRIES
         SR    R5,R5
         IC    R5,1(,R9)           # OF DEPENDENT SEG. #S
         LTR   R5,R5               ZERO?
         BZ    NOGO                IF SO, SKIP.
         S     R5,CON1             - 1
         SLL   R5,MULT4            X 4 BYTES / ENTRY
         SR    R0,R0
OTP111   LH    R8,2(R5,R4)         GET HIGH PRINT POS.
         LTR   R8,R8               ZERO?
         BP    OTP112
         A     R0,CON4             +4
         S     R5,CON4             -4
         BM    BADERROR            ERROR IF NEGATIVE
         B     OTP111
OTP112   A     R8,CON7             + 7 SO THAT ENTRIES DON'T OVERLAY
         STH   R8,HICOMP           SAVE HIGH PRINT POS.
         C     R10,CON1            AT LEAST 2 ENTRIES?
         BNH   NOGO
         AR    R5,R0               RESTORE R5
         LA    R7,4(R4,R5)         POINT TO 1ST ENT. OF NEXT GRP.
ALTSET   A     R1,CON4             UPDATE TABLE ADDRESS
         SR    R5,R5
         IC    R5,1(,R1)           # OF DEPENDENCIES
         LTR   R5,R5               ZERO?
         BZ    NOGO2
ALTMORE  NC    2(2,R7),2(R7)       1ST ENTRY POSITION ZERO?
         BNZ   CHECKENT            NO; CHECK IT.
         A     R7,CON4
         BCT   R5,ALTMORE
         B     BADERROR            SHOULD NOT OCCUR...
         SPACE
*  IF THE 1ST ENTRY DOES NOT CONFLICT THEN THE REST OF THE GROUP
*     IS OK.
CHECKENT CLC   HICOMP,2(R7)        CHECK AGAINST HIGH PRINT POS.
         BL    NOGO                ** OK **
CONFLICT L     R11,EXTPTR          PTR TO EXTENSION TABLE
         MVC   0(4,R11),0(R1)      MOVE ENTRY TO TABLE
         SR    R6,R6
         IC    R6,1(,R11)          # OF DEP. ITEMS
         MVI   1(R11),NOENTRYS     ZERO COUNT
         A     R11,CON4            UPDATE TABLE ADDRESS
         C     R11,ENDEXT          END OF TABLE?
         BE    EXTOVF              YES. ERROR.
         ST    R11,EXTPTR          SAVE NEW POINTER
         LH    R0,PSRCHCNT         TOTAL # ENTRIES
         S     R0,CON1             - 1
         STH   R0,PSRCHCNT
         LR    R15,R7
GETOUT   L     R11,DEPREGS+4       A(DEPEND)
         LA    R11,4*TABLLN-9(,R11)  END ADDRESS
         LR    R7,R15              RESTORE R7
         BAL   R8,DELETER          DELETE ENTRY
         LTR   R6,R6               IS COUNT ZERO?
         BNP   *+8
         BCT   R6,GETOUT
         L     R11,PSREGS
         LA    R11,4*TABLLN-9(,R11)  END ADDRESS OF PSSRCHEL
         LR    R7,R1               ADDRESS OF ENTRY
         BAL   R8,DELETER          DELETE THIS ENTRY
         B     OUT
NOGO2    LTR   R6,R6
         BZ    *+8
         BCT   R6,ALTSET
NOGO     SR    R0,R0
         IC    R0,1(,R9)           # CHAINED ENTRIES IN DEPEND
         SLL   R0,MULT4            X 4
         AR    R4,R0               SKIP OVER THIS GROUP
         A     R9,CON4
OUT      LTR   R10,R10             ANY ENTRIES LEFT?
         BZ    *+8
         BCT   R10,LOOKSET
         LM    R6,R7,PSREGS
CANCEL   MVI   1(R6),NOENTRYS
         A     R6,CON4             UPDATE TABLE ADDRESS
         LTR   R7,R7
         BZ    *+8
         BCT   R7,CANCEL
         BAL   R14,MOVER2          SWAP THE TABLES
         MVC   DEPCNT(2),PSRCHCNT
         XC    PSRCHCNT(2),PSRCHCNT
         OI    SWITCHES,SEGIN      INDICATE SEG. #S PRINTED
         LM    R5,R7,ICSAVE        RESTORE R5-R7
         B     MOVEINLN
         SPACE 3
***** CHECK PREVIOUSLY USED PRINT POSITION; IF NECESSARY, *****
*****  INSERT AN * TO INDICATE CONTINUATION OF BRANCH.    *****
         SPACE
WRITE    LM    R6,R7,DEPREGS
         LA    R14,UPDTEADR        RETURN ADDRESS
CNXTSP   LA    R10,MESSAGE
         AH    R10,2(,R7)          A(MESSAGE + PRINT POS.)
         CLI   0(R10),BLANK        BLANK?
         BNE   UPDTEADR            NO CSECT NAME NOW; CHECK FOR PREV.
         L     R8,SEGBFBEG
         LA    R8,24(,R8)          BEGIN OF SEG. 1 IN SEGTAB
COMP09   CLC   0(1,R7),0(R8)       IS IT WHAT WE'RE LOOKING FOR?
         BE    SOMEDEP             YEAH.  PUT IN *.
         A     R8,CON4             INCREMENT PTR
         C     R8,SEGUSED          END OF SEGTAB?
         BL    COMP09
UPDTEADR A     R7,CON4             THIS POS. DOES NOT GET *
         LTR   R6,R6               ANY MORE?
         BZ    *+8
         BCT   R6,CNXTSP           CHECK NEXT PRT. POS.
         BAL   R14,PRTRTN          PRINT THIS LINE.
         NI    SWITCHES,ALLBITS-LINFULL  LINE NOW EMPTY.
         B     PRINTMGR            GO BACK
         SPACE 3
SWON     LR    R9,R1               END ADDRESS
         SR    R1,R11
         LR    R3,R11              BEGIN ADDRESS
         S     R3,CON1             - 1
         LR    R4,R3               BEGIN ADDR.
         AR    R4,R1               + DISPLACEMENT
COUNT1   CLI   1(R4),BLANK         FIND BLANK, TO EXTEND *** OVER SEG #
         BE    COUNT2
         MVI   1(R4),ASTRSK
         A     R4,CON1
         B     COUNT1
COUNT2   EX    R1,MVC              EXECUTE MOVE
         LR    R3,R9
         LR    R11,R3
         LA    R6,MESSAGE+L'MESSAGE  END ADDRESS OF MESSAGE
         SR    R6,R3
         OI    SWITCHES,CONLN      INDICATE LINE OF * TO PRINT
         C     R1,CON2             IF LENGTH 1 | 2, A 2 | 3 DIGIT #
         BNH   EXECUTE             IF ¬> 2 NO BCT
COUNTEM  BCT   R10,EXECUTE         MAKE HORIZONTAL LINE
         NI    SWITCHES,ALLBITS-FRSTRCVD  CLEAR SWITCH
         B     PLUSLOC             RETURN
LNMAKR   LR    R2,R0               RESTORE REGISTER
         A     R1,CON1             ADD 1
         TM    SWITCHES,FRSTRCVD   BEGIN ADDR RECEIVED?
         BO    SWON                SKIP 1ST STORE IF SO.
         LR    R11,R1              SAVE 1ST ADDRESS
         OI    SWITCHES,FRSTRCVD   INDICATE
         SR    R1,R3               GET DIFFERENCE
         SR    R6,R1
         LR    R3,R11              POINT TO CURRENT ADDRESS
         B     COUNTEM
         SPACE 3
***** THESE INSTRUCTIONS ARE EXECUTED *****
         SPACE
MVC      MVC   0(0,R3),ASTERISK
MVCNAME  MVC   BLDLMBRN(0),1(R7)   MOVE NAME TO LIST
TRT      TRT   0(0,R3),READBFR
         SPACE 3
***** FORMULA IF > 1 DEPENDENT SEGMENT *****
         SPACE
ALTFORM  SR    R10,R10
         IC    R10,1(,R6)          # DEPENDENT SEG #S
         LR    R8,R10              SAVE IT.
         NC    2(2,R6),2(R6)       PREVIOUS PRINT POS. = 0?
         BZ    COMPEXIT            YEAH.
         C     R10,CON8            > 8 ACROSS?
         BNH   OK                  YEAH; OK.
         L     R10,CON8            NO; DEFAULT TO 8.
         OI    SWITCHES,SPECON     SPECIAL PROCESSING.
OK       LR    R1,R10              BCT ON # OF DEPENDENT SEG. #S
         MH    R10,HCON15          X 15 PRINT POSITIONS
         SRL   R10,1               / 2
         LH    R11,2(,R6)          GET PRINT POS. FROM PSSRCHEL
         SR    R11,R10             SUBTRACT
         A     R11,CON7            ADD 7 TO GET DEAD CENTER
STORE    STH   R11,2(,R5)          STORE IN ENTRY IN DEPEND
         A     R5,CON4             + 4 FOR NEXT ENTRY
         AH    R11,HCON15          TO NEXT PRINT POS.
         LTR   R1,R1               ZERO?
         BZ    *+8
         BCT   R1,STORE            CYCLE TO PROCESS ALL
         TM    SWITCHES,SPECON     SPECIAL PROCESSING?
         BZ    LOOK                NO; GET NEXT ENTRY
         S     R8,CON8             SUBTRACT 8 FOR ENTRIES PROCESSED
COMPEXIT SLL   R8,MULT4            X 4
         AR    R5,R8               SKIP NON-PRINTABLE ENTRIES
         NI    SWITCHES,ALLBITS-SPECON  SET SPECIAL PROC. DONE
         B     LOOK                DONE...
         SPACE 3
OVERFLOW LH    R7,HEXTCNT          # ENTRIES IN HEXTBL
         LR    R0,R7
         SLL   R7,MULT4            X 4
         AR    R7,R9               + BEGIN ADDR. OF HEXTBL
         MVC   0(1,R7),0(R8)       MOVE ENTRY TO HEXTBL
         A     R0,CON1             ADD ONE TO ENTRY #
         XC    1(3,R7),1(R7)       CLEAR END OF ENTRY
         CH    R0,MAXENTS          TABLE OVERFLOW?
         BH    HEXTOVFW            YEAH. ERROR.
         STH   R0,HEXTCNT          SAVE NEW COUNT
         SR    R0,R0
         IC    R0,1(,R3)           # OF DEPENDENCIES
         S     R0,CON1             - 1
         STC   R0,1(,R3)           RECORD UPDATED COUNT
         L     R11,DEPREGS+4       A(DEPEND)
         LA    R11,4*TABLLN-9(,R11)  END ADDRESS
         LR    R15,R8              SAVE POINTER
         LR    R7,R8               ADDRESS OF ENTRY
         BAL   R8,DELETER          DELETE ENTRY
         LR    R8,R15              RESTORE POINTER
         LH    R11,DEPCNT          TOTAL # OF DEPENDENCIES
         S     R11,CON1            - 1
         STH   R11,DEPCNT          SAVE IT.
         B     NEXTNTRY            PROCESS NEXT ENTRY
         SPACE 3
CLEARCOR XC    0(256,R4),0(R4)
         XC    256(256,R4),256(R4)
         XC    512(256,R4),512(R4)
         XC    768(256,R4),768(R4)
         BR    R6
         SPACE 3
EDIT     SR    R6,R6
         IC    R6,0(,R4)           SEGMENT #
         CVD   R6,ADAREA
         ED    EDITSEG#(4),ADAREA+6  EDIT TO EBCDIC
         BR    R14
         SPACE 3
INSRTASK LA    R10,MESSAGE
         AH    R10,2(,R7)          MESSAGE + DISPLACEMENT
SOMEDEP  MVI   4(R10),ASTRSK       MOVE IN AN *
         MVI   3(R10),BLANK
         MVI   5(R10),BLANK        BLANK BEFORE & AFTER
         BR    R14                 RETURN
         SPACE 3
ASTERINS LA    R10,MESSAGE
         AH    R10,2(,R7)          GET MESSAGE + DISPLACEMENT
         MVI   3(R10),BLANK
         MVI   5(R10),BLANK
         CLI   4(R10),BLANK        IS THE SEGMENT DIRECTLY UNDERNEATH?
         MVI   4(R10),ASTRSK       MOVE IN AN ASTERISK.
         BCR   NOTEQ,R14           IF SO, RETURN.
         MVI   4(R10),DASH         IF NOT, PUT IN A DASH
         L     R10,CON2            SET 2 SEGMENTS
         B     HORIZ1              AND BRANCH.
         SPACE 3
SWAP     BAL   R14,MOVER           DEPEND -> PSSRCHEL
         L     R4,DEPREGS+4
         BAL   R6,CLEARCOR         CLEAR DEPENDENCY TABLE
         MVC   PSRCHCNT(2),DEPCNT  MOVE COUNT
         XC    DEPCNT(2),DEPCNT    CLEAR COUNT
         BR    R15                 RETURN
         SPACE 3
***** PREVSGN FINDS THE PREV. SEG. # FOR A SEGMENT. *****
*     SEGMENT # IS IN R1; PREV. SEG. # IS PUT IN PSGT.  *
         SPACE
PREVSGN  LR    R0,R3               SAVE REGISTER
         L     R3,SEGBFBEG         BEGIN OF SEGTAB
         SLL   R1,MULT4            SEG. # X 4
         IC    R1,20(R1,R3)        GET PREVIOUS SEG. #
         STC   R1,PSGT             & SAVE IT.
         LR    R3,R0               RESTORE R3
         BR    R14                 RETURN
         SPACE 3
MOVER    LM    R3,R4,PSREGS-4
MOVERXXX LA    R7,TABLLN           TABLE LENGTH / 4
         L     R8,CON4
MVD2P    MVC   0(256,R4),0(R3)     MOVE DEPEND -> PSSRCHEL
         AR    R3,R7               + 256
         AR    R4,R7               + 256
         BCT   R8,MVD2P            GO AGAIN.
         BR    R14                 RETURN
         SPACE 3
MOVER2   L     R3,PSREGS           A (PSSRCHEL)
         L     R4,DEPREGS+4        A (DEPEND)
         B     MOVERXXX
         SPACE 3
MOVER3   L     R4,DEPREGS+4
         BAL   R6,CLEARCOR         CLEAR DEPEND TABLE
         L     R4,DEPREGS+4
         L     R3,HEXTBLBG
         LH    R7,HEXTCNT          # OF ENTRIES
MVH2D    MVC   0(4,R4),0(R3)       HEXTBL -> DEPEND
         A     R4,CON4             + 4
         A     R3,CON4             + 4
         BCT   R7,MVH2D            GO AGAIN.
         BR    R14                 RETURN
         SPACE 3
DELETER  SR    R11,R7              AMOUNT OF TABLE LEFT
         SRL   R11,MULT4           / 4
SQUEEZE  MVC   0(4,R7),4(R7)       MOVE ENTRY *-4
         A     R7,CON4             UPDATE
         LTR   R11,R11             ZERO?
         BZ    *+8
         BCT   R11,SQUEEZE
         BR    R8                  RETURN
         SPACE 5
***** ERROR MESSAGES *****
         SPACE
DEPOVFLW MVC   MESSAGE+16(10),=CL10'DEPENDENCY'
ANYERROR MVC   MESSAGE(15),=CL15'1** OVERFLOW IN'
         MVC   MESSAGE+27(26),=CL26'TABLE. INCREASE REGION. **' GP10097
NORECOVR BAL   R14,PRTRTN          PRINT ERROR MSG
         B     IEWLDONE            GIVE UP.
         SPACE
MBOVRFLW MVC   MESSAGE+16(4),=C'CESD'
         B     ANYERROR
         SPACE
SGBOVFLW MVC   MESSAGE+16(6),=C'SEGTAB'
         B     ANYERROR
         SPACE
EXTOVF   MVC   MESSAGE+16(9),=C'EXTENSION'
         B     ANYERROR
         SPACE
HEXTOVFW MVC   MESSAGE+16(10),=C'HORIZ. EXT'
         B     ANYERROR
         SPACE
BADERROR MVC   MESSAGE(28),=CL28'1** CATASTROPHIC ERROR... **'
         B     NORECOVR
         SPACE
NOTEDTBL MVC   MESSAGE+27(12),=CL12'EDITABLE. **'
MOREMSG  MVC   MESSAGE(10),=CL10'1** MODULE'
         MVC   MESSAGE+11(8),BLDLMBRN
         MVC   MESSAGE+20(6),=C'IS NOT'
RECOVER  BAL   R14,PRTRTN
         B     IEWLQUIT
         SPACE
NOTOVLY  MVC   MESSAGE+27(21),=CL21'AN OVERLAY MODULE. **'
         B     MOREMSG
         EJECT
***** CONSTANTS, ETC. *****
         SPACE
RETCODE  DC    H'16'               RETURN CODE
HUNDRED  DC    F'100'
FOURK    DC    A(4*K)
COREWANT DC    A(8*K,34*K)
COREGOT  DC    2F'0'
CON1     DC    F'1'
CON2     DC    F'2'
CON4     DC    F'4'
CON7     DC    F'7'
CON8     DC    F'8'
CON16    DC    F'16'
MIDLINE  DC    F'62'
HCON15   DC    H'15'
MAXDEPLN DC    H'256'
MINPOS   DC    H'8'
MAXPOS   DC    H'124'
BLDLCNT  DC    H'1,32'             CONSTANTS FOR BLDL
EBCDIC   DC    C'0123456789ABCDEF'
BLANKS   DC    CL8' '
EDITSEG# DC    AL1(BLANK,DIGIT,DIGIT,DIGIT)
PATTERN  DC    AL1(BLANK,DIGIT,DIGIT,DIGIT)
SAVEDATE DC    CL6' '
SAVETIME DC    CL8' '
HICOMP   DS    H
         SPACE
***** EXTERNAL REFERENCES *****
         SPACE
V@DECK   DC    V(IEWLDECK)
V@IO     DC    V(IEWLIO)
V@OPT    DC    V(IEWLOPT)
         SPACE
***** ALLOCATION TABLE *****
         SPACE
         DROP  R2
         USING DSECT,R0            -- FOR S-TYPE CONSTANTS
         SPACE
ALLOCTAB DC    H'20,768,1048',SL2(SEGBFBEG,ENDSGBF)  SEGTAB
ALLOC2   DC    H'4,100,1024',SL2(EXTBLBEG,ENDEXT)  EXT. TABLE
         DC    H'4,100,1024',SL2(HEXTBLBG,HEXTBLND)  HORIZ. EXT. TABLE
         DC    H'1,30,100',SL2(BEGNMBFR,ENDNMBFR)  NAME BUFFER
         DC    H'4,1024,1024',SL2(PSREGS,0)  PSSRCHEL
ALLOCLST DC    H'4,1024,1024',SL2(DEPREGS+4,0)  DEPEND
         DC    H'63,5800,32767',SL2(CESDADR,CESDEND)  CESD BUFFER
         SPACE 5
         LTORG
         EJECT
***** DSECT TO DEFINE SHARED CORE. *****
         SPACE
DSECT    DSECT
SAVEAREA DC    18F'0'              MAIN SAVE AREA
ASTERISK DC    16C'*'
MESSAGE  DC    CL132' '            MESSAGE FOR O/P
MSAVE    DC    CL132' '
ADAREA   DC    D'0'                CVD AREA
IOSAVE   DC    18F'0'              SAVE AREA FOR I/O ROUTINE
SAVE2    DC    18F'0'              SAVE AREA FOR SUBROUTINES
ICSAVE   DC    3F'0'               TEMP. SAVE AREA
BEGNMBFR DC    F'0'                BEGIN OF NAME BUFFER
ENDNMBFR DC    F'0'                END OF NAME BUFFER
HEXTBLBG DC    F'0'                BEGIN OF HORIZ. EXTENSION TBL.
HEXTBLND DC    F'0'                END OF HORIZ. EXTENSION TBL.
CESDADR  DC    F'0'                BEGIN OF CESD BUFFER
CESDEND  DC    F'0'                END OF CESD BUFFER
CESDUSED DC    F'0'                END OF CESD BUFFER ACTUALLY USED.
RCBFEND  DC    F'0'                READ CESD BUFFER END
SEGBFBEG DC    F'0'                BEGIN ADDR OF SEGTAB
ENDSGBF  DC    F'0'                END ADDR OF SEGTAB
SEGUSED  DC    F'0'                END OF SEGTAB ACTUALLY USED
EXTBLBEG DC    F'0'                BEGIN OF EXTENSION TABLE
CEXTPTR  DC    F'0'
EXTPTR   DC    F'0'                EXTENSION TABLE PTR
ENDEXT   DC    F'0'
DEPREGS  DC    H'0'
DEPCNT   DC    H'0'                DEPENDENCY COUNT
         DC    A(0)                ADDRESS OF DEPEND WILL BE INSERTED
PSREGS   DC    A(0)                ADDRESS OF PSSRCHEL WILL BE INSERTED
         DC    H'0'                DUMMY NEEDED FOR ALIGNMENT
PSRCHCNT DC    H'1'                # OF ELEMENTS IN TABLE
MAXENTS  DC    H'0'                MAX # OF ENTRIES IN EXTTBL
HSNOPRCS DC    X'00'               HIGHEST SEG # CURRENTLY PROCESSING
PSGT     DC    X'00'               PREVIOUS SEG # TEST AREA
         SPACE 2
SWITCHES DC    X'00'               BUFFER SWITCHES
DEPTEST  DC    X'00'               DEPENDENCY TEST AREA
HEXTCNT  DC    H'0'
REGION#  DC    H'1'                CURRENT REGION #
         SPACE 2
***** INTERNAL SWITCHES *****
         SPACE
INTERN1  DC    X'00'               SWITCHES 1
OPTSW    DC    X'00'               SWITCHES 2
         DC    0F'0'
READBFR  DC    XL256'00'           ALSO TRT AND TR TABLE
         SPACE
ENTRY    DC    CL8' '              ENTRY POINT SD OR LR
         SPACE 2
***** BLDL LIST *****
         SPACE
LIST     EQU   *
BLDLDES1 DC    H'1'                # OF ENTRIES
BLDLDES2 DC    H'32'               LENGTH OF EACH
BLDLMBRN DC    CL8' '              MEMBER NAME
BLDLTTR1 DC    XL4'0'              TTR OF PDS; CONCAT #
BLDLZEUS DC    XL2'0'              JOB/LINK LIB; USER DESCR.
BLDLTTR2 DC    XL4'0'              TTR OF FIRST TEXT REC. & ZERO
BLDLTTR3 DC    XL3'0'              TTR OF NOTE LIST OR SCTR TRANS TBL
BLDLNONL DC    X'00'               # OF ENTRIES IN NOTE LIST
BLDLATT1 DC    X'00'               1ST ATTRIBUTE BYTE
BLDLATT2 DC    X'00'               2ND ATTRIBUTE BYTE
BLDLMS   DC    AL3(0)              AMOUNT OF MAIN STORAGE REQ.
BLDLLEN1 DC    AL2(0)              LENGTH OF 1ST TEXT RECORD
BLDLEP   DC    AL3(0)              ADDRESS OF EP
         SPACE
DSECTEND EQU   *                   END OF DSECT
         SPACE 5
***** ESD DATA FOUND IN (COMPOSITE) EXTERNAL SYMBOL DICTIONARY *****
         SPACE
ESDDATA  DSECT
ESDENTRY DS    0CL16
ESDNAME  DS    CL8                 EXTERNAL SYMBOL NAME
ESDTYPE  DS    BL1                 SYMBOL TYPE- SD,LR,PC,PD,ER,CM,PR,ER
ESDLKAD  DS    AL3                 LINKAGE EDITOR ASSIGNED E.P. ADDRESS
ESDSGNO  DS    XL1                 SEGMENT #
ESDIDLN  DS    XL3                 LENGTH
         SPACE 5
***** DSECT DEFINING DCB FIELDS *****
         SPACE
         DCBD  DSORG=QS,DEVD=DA
         SPACE 5
         DROP  R0,R4,R12
         SPACE
*****    END   IEWLOTP
         TITLE '* * * DECK ROUTINE FOR OVERLAY TREE PROCESSOR. * * *'
IEWLDECK CSECT
         USING DSECT,R2
         USING ESDDATA,R4
         CHAIN +SAVE2
         SPACE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*      DECK OPTION ROUTINE: TO PRODUCE A DECK FOR THE LINKAGE EDITOR  *
*       DESCRIBING THE OVERLAY STRUCTURE.                             *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE 2
         L     R5,SEGBFBEG         BEGINNING OF CESD BUFFER
         LA    R3,24(,R5)          + 24 -> FIRST SEGMENT
         L     R6,CON04            INCREMENT FOR BXLE
         L     R7,SEGUSED          END OF SEGTAB BUFFER
         SR    R7,R6               - R4 FOR END LIMIT ON BXLE
         SR    R4,R4
LINEAD   A     R4,CON01            NEXT SEGMENT
         STC   R4,3(,R3)           STORE IN SEGTAB
         BXLE  R3,R6,LINEAD
         MVC   0(2,R3),ENDTBL      END-OF-TABLE DELIMITER
         MVC   REGION#,CON01+2
         XC    ICSAVE,ICSAVE
         LR    R8,R6               = 4
         LA    R10,24(,R5)         BEGIN OF SEGMENT 1
         LA    R11,8(,R5)          BEGIN OF REGION 1
NEXTRGN  SR    R1,R1
         IC    R1,0(,R11)          # OF LAST SEGMENT
         LTR   R1,R1               IF ZERO,
         BZ    ENDDECK             END OF STRUCTURE
         SLL   R1,MULT4            X 4
         LA    R9,20(R1,R5)        -> LAST SEGMENT FOR BXLE
PUNCHPAS L     R4,CESDADR          FOR CESD
         LA    R6,16               INCREMENT FOR CESD
         L     R7,CESDUSED         END OF CESD
         SR    R7,R6               - 16 FOR BXLE
         LA    R1,MESSAGE+8        1ST POSITION FOR CSECT NAME
         LA    R0,MESSAGE+62       END OF INSERT CARD
         LR    R3,R1               FOR COMPARE
SEGSRCH  CLC   ESDSGNO,3(R10)      CURRENT SEGMENT #?
         BNE   BXLE1
         CLI   ESDTYPE,SD          SD?
         BE    YESTHIS
         CLC   ESDNAME,BLKCOMM
         BE    BXLE1
         CLI   ESDTYPE,CM          CM?
         BNE   BXLE1
YESTHIS  MVC   1(8,R1),ESDNAME     MOVE ESD NAME TO INSERT CARD
         LA    R14,8(,R1)          FOR BCT LOOP
BCTLOOP  CLI   0(R14),BLANK
         BNE   YESYES
         BCT   R14,BCTLOOP
YESYES   LA    R1,1(,R14)          NEW END OF CARD
         CR    R1,R0               END OF CARD?
         BNH   NOPUNCH
         MVC   MESSAGE+1(6),INSMSG
         BAL   R14,PUNCHRTN
         LR    R1,R3               RE-INITIALIZE
         LA    R0,MESSAGE+62
         B     BXLE1
NOPUNCH  MVI   0(R1),COMMA         COMMA AFTER ESD ITEM
BXLE1    BXLE  R4,R6,SEGSRCH       TO NEXT CESD ITEM
         CR    R1,R3               PUNCH INSERT CARD?
         BE    NOPUNCH2            NO.
         MVI   0(R1),BLANK         BLANK TRAILING ,
         MVC   MESSAGE+1(6),INSMSG
         BAL   R14,PUNCHRTN
NOPUNCH2 CLC   4(2,R10),ENDTBL     END OF SEGTAB?
         BE    ENDDECK             YES; DONE.
         CR    R10,R9              END OF THIS REGION?
         BE    BXLE2
         MVC   MESSAGE+1(7),OVERLAY  PRODUCE 'OVERLAY' CARD.
         SR    R3,R3
         IC    R3,4(,R10)          PREVIOUS SEGMENT #
         LTR   R3,R3               IF ZERO, USE REGION_.
         BZ    OVLYRGN
         MVC   MESSAGE+9(4),OVLYMSG  'OVLY'
         CVD   R3,ADAREA
         OI    ADAREA+7,SIGNBIT    FIX SIGN
         UNPK  MESSAGE+13(3),ADAREA+6(2)
         B     PCHCALL             & PUNCH IT OUT.
OVLYRGN  MVC   MESSAGE+9(7),LASTREG  NODE PT. IS LAST REGION#
PCHCALL  BAL   R14,PUNCHRTN
BXLE2    BXLE  R10,R8,PUNCHPAS     FOR NEXT SEGMENT
         LA    R11,2(,R11)
         CLI   0(R11),X'00'        ANY SEGMENTS IN THIS REGION?
         BE    ENDDECK             NO; DONE.
         LH    R3,REGION#          REGION #
         C     R3,CON04            DONE IF 4TH REGION PROC.
         BNL   ENDDECK
         A     R3,CON01            + 1
         STH   R3,REGION#
         MVC   MESSAGE+9(6),REGMSG+1  OVERLAY REGION_(REGION)
         STC   R3,MESSAGE+15
         OI    MESSAGE+15,SIGNBIT2
         MVC   LASTREG,MESSAGE+9   SAVE FOR USE LATER
         MVC   MESSAGE+16(8),REGMSG
         MVC   MESSAGE+1(7),OVERLAY
         BAL   R14,PUNCHRTN
         B     NEXTRGN
ENDDECK  NC    ENTRY,ENTRY         ENTRY FOUND?
         BZ    ENTRYNOT
         MVC   MESSAGE+1(5),=C'ENTRY'
         MVC   MESSAGE+7(8),ENTRY
         BAL   R14,PUNCHRTN
         B     NAMECARD
ENTRYNOT MVC   MESSAGE+18(52),=CL52'**WARNING: EP NOT FOUND, MUST BE SU*
               PPLIED BY HAND.**'
NAMECARD MVC   MESSAGE+1(4),=C'NAME'
         MVC   MESSAGE+6(8),BLDLMBRN  MOVE IN MEMBER NAME
         LA    R8,MESSAGE+14
BLANKLOK CLI   0(R8),BLANK         LOOK FOR NON-BLANK CHARACTER
         BNE   MVCR                AFTER WHICH TO INSERT '(R)'
         BCT   R8,BLANKLOK
MVCR     MVC   1(3,R8),=C'(R)'
         BAL   R14,PUNCHRTN
         CHAIN -SAVE2              RETURN.
         SPACE 3
***** PUNCH INTERFACE / SEQUENCING ROUTINE *****
         SPACE
PUNCHRTN L     R1,ICSAVE
         A     R1,CON01
         ST    R1,ICSAVE
         CVD   R1,ADAREA
         OI    ADAREA+7,SIGNBIT
         UNPK  MESSAGE+75(5),ADAREA+5(3)  CONVERT SEQ # TO PRINTABLE
         MVC   MESSAGE+72(4),BLDLMBRN  AND MOVE IN NAME
         L     R1,CON04
         L     R15,=V(IEWLIO)      I/O ROUTINE
         BR    R15                 PUNCH A CARD
         SPACE 5
***** CONSTANTS *****
         SPACE
CON01    DC    F'1'
CON04    DC    F'4'
INSMSG   DC    C'INSERT'
OVLYMSG  DC    C'OVLY'
REGMSG   DC    C'(REGION)'
BLKCOMM  DC    CL8'$BLKCOM'
OVERLAY  DC    C'OVERLAY'
ENDTBL   DC    X'FFFF'             END-OF-SEGTAB FLAG
LASTREG  DC    C'REGION#'
         SPACE 2
         LTORG
         SPACE 2
         DROP  R2,R4,R12
         SPACE
*****    END OF IEWLDECK
 TITLE '* * * INPUT / OUTPUT ROUTINE FOR OVERLAY TREE PROCESSOR. * * *'
IEWLIO   CSECT
         USING DSECT,R2
         CHAIN +IOSAVE
         SPACE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*       THIS ROUTINE HANDLES ALL I/O REQUESTS FOR THE O.T.P.          *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE
* UPON ENTRY, R1 HAS A BRANCH CODE (A MULTIPLE OF 4) TO INDICATE
*  THE FUNCTION DESIRED:
         SPACE
         B     IORTNS(R1)
IORTNS   B     PRINTR              0
         B     PUNCH               4
         B     READR               8
         B     OPEN1               12
         B     CLOSE               16
         B     BLDL                20
         B     OPEN2               24
         SPACE 3
IOEXIT   CHAIN -IOSAVE
         SPACE 3
***** PRINT ROUTINE *****
         SPACE
PRINTR   PUT   PRINTDCB,MESSAGE
         B     CLEARMSG
         SPACE 3
***** PUNCH LINE *****
         SPACE
PUNCH    PUT   PUNCHDCB,MESSAGE
CLEARMSG MVI   MESSAGE,BLANK
         MVC   MESSAGE+1,MESSAGE
         B     IOEXIT
         SPACE 3
***** READ A RECORD FROM PDS *****
*     R3 = LENGTH;  R4 = ADDRESS TO READ INTO.    *
         SPACE
READR    READ  DECB1,SF,MODDCB,(R4),(R3)
         CHECK DECB1
         B     IOEXIT
         SPACE 3
***** OPEN SYSPRINT & SYSLIB *****
         SPACE
OPEN1    OPEN  (MODDCB,,PRINTDCB,OUTPUT)
         TM    PRINTDCB+DCBOFLGS-IHADCB,OPENBIT
         BZ    NOPRINTR
         TM    MODDCB+DCBOFLGS-IHADCB,OPENBIT
         BO    IOEXIT              BO IF OK.
         MVC   MESSAGE(10),=CL10'1** SYSLIB'
QUITSOON MVC   MESSAGE+13(18),=CL18'FAILED TO OPEN. **'
         MVC   12(4,R5),=V(IEWLDONE)  CHANGE RETURN ADDRESS....
         B     PRINTR
         SPACE
NOPRINTR ABEND 13,DUMP,STEP        ABEND IF NO SYSPRINT...
         SPACE 3
***** CLOSE ALL DCBS. *****
         SPACE
CLOSE    CLOSE (PRINTDCB,,MODDCB,,PUNCHDCB)
         B     IOEXIT
         SPACE 3
***** OPEN SYSPUNCH IF REQUIRED. *****
         SPACE
OPEN2    OPEN  (PUNCHDCB,OUTPUT)
         TM    PUNCHDCB+DCBOFLGS-IHADCB,OPENBIT
         BO    IOEXIT
         MVC   MESSAGE(12),=CL12'1** SYSPUNCH'
         B     QUITSOON
         SPACE 3
***** DO A BLDL AND A FIND. *****
         SPACE
BLDL     BLDL  MODDCB,LIST
         LTR   R15,R15             SUCCESSFUL?
         BP    BLDLERR             NO;  PRINT MSG & SKIP THIS MEMBER.
         FIND  MODDCB,BLDLTTR1,C   POSITION TO FIRST RECORD
         B     IOEXIT
         SPACE
BLDLERR  MVC   MESSAGE(39),=CL39'1** BLDL FAILED FOR MEMBER XXXXXXXX. **
               *'
         MVC   MESSAGE+27(8),BLDLMBRN  INSERT MEMBER NAME
         MVC   12(4,R5),=V(IEWLQUIT)  NO MORE FOR THIS MEMBER...
         B     PRINTR
         SPACE 3
***** DCB EXIT ROUTINE TO CHECK BLKSIZE. *****
         SPACE
         USING *,R15
EXITRTN  CHAIN +SAVE2,NOREG
         SPACE
*   REG. 1 CONTAINS ADDRESS OF THE DCB.
         SPACE
         CLC   DCBBLKSI-IHADCB(L'DCBBLKSI,R1),DCBLRECL-IHADCB(R1)
         BE    EEXIT
         BH    BLOCK
* SET BLKSIZE = LRECL
         MVC   DCBBLKSI-IHADCB(L'DCBBLKSI,R1),DCBLRECL-IHADCB(R1)
         B     EEXIT
         SPACE
* SET BLKSIZE TO NEXT LOWER MULT. OF LRECL
BLOCK    LH    R8,DCBBLKSI-IHADCB(R1)
         SRDA  R8,32               LOAD BLKSIZE INTO R8 & SHIFT TO R9
         LH    R10,DCBLRECL-IHADCB(R1)  GET LRECL AND DIVIDE:
         DR    R8,R10
         LTR   R8,R8
         BZ    EEXIT               IF NO REMAINDER, OK.
         MR    R8,R10              X TO GET VALID BLKSIZE
         STH   R9,DCBBLKSI-IHADCB(R1)  STORE IN DCB
EEXIT    CHAIN -SAVE2
         SPACE 3
***** SYNAD EXITS *****
         SPACE
         USING *,R15
BPAMERR  SYNADAF ACSMETH=BPAM
         B     SYNERR
         SPACE
         USING *,R15
QSAMERR  SYNADAF ACSMETH=QSAM
         DROP  R15
SYNERR   BALR  R12,0
         USING *,R12               GET NEW BASE REGISTER...
         LR    R8,R14              SAVE RETURN ADDRESS.
         MVC   MESSAGE(15),=CL15'-** I/O ERROR: '
         MVC   MESSAGE+15(78),50(R1)  MOVE IN SYNAD INFORMATION
         CLC   75(8,R1),=CL8'SYSPRINT'  CHECK DDNAME: SYSPRINT?
         BE    DOAWTO
         PUT   PRINTDCB,MESSAGE
         SYNADRLS ,                RELEASE SYNAD AREA.
         MVC   SAVEAREA+12(4),=V(IEWLQUIT)  THAT'S ALL FOR THIS MBR.
         LR    R14,R8
         BR    R14                 GO.
         SPACE
DOAWTO   MVC   MESSAGE-4(5),=X'006600005C'
         WTO   MF=(E,MESSAGE-4)    DO WTO...
         ABEND 1,DUMP,STEP
         SPACE 5
***** CONSTANTS, ETC. *****
         SPACE
         DC    0F'0'
EXLST    DC    X'85',AL3(EXITRTN)  DCB EXIT
         SPACE 3
         LTORG
         SPACE 5
***** DATA CONTROL BLOCKS. *****
         SPACE
PRINTDCB DCB   DDNAME=SYSPRINT,DSORG=PS,LRECL=133,MACRF=PM,RECFM=FBA,  *
               SYNAD=QSAMERR,EXLST=EXLST
         SPACE 5
PUNCHDCB DCB   DDNAME=SYSPUNCH,DSORG=PS,LRECL=80,MACRF=PM,RECFM=FB,  * *
               SYNAD=QSAMERR,EXLST=EXLST,EROPT=ACC
         SPACE 5
MODDCB   DCB   BFALN=F,BLKSIZE=1048,DDNAME=SYSLIB,DSORG=PO,MACRF=R,  * *
               RECFM=U,SYNAD=BPAMERR
         SPACE 5
         DROP  R2,R12
         SPACE
*****    END OF IEWLIO
         TITLE '* * * OPTIONS PROCESSOR FOR OVERLAY TREE PROC. * * *'
IEWLOPT  CSECT
         USING DSECT,R2
         CHAIN +SAVE2
         SPACE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*    THIS ROUTINE SCANS THE OS PARM LIST (ADDRESS IN R11) FOR VALID   *
*     OPTIONS, AND PUTS A LIST IN THE PRINT BUFFER.  NAMES ARE PUT IN *
*     THE NAME BUFFER AS A COUNT FIELD FOLLOWED BY THE NAME.          *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE
         LR    R3,R2               SAVE DSECT POINTER (OVER TRT)
         DROP  R2
         USING DSECT,R3
         MVI   READBFR+COMMA,COMMA
         MVI   READBFR+EQUAL,EQUAL  MOVE , AND = TO READBFR FOR TRT
         L     R8,BEGNMBFR         BEGIN OF NAME BUFFER
         LH    R10,0(,R11)         GET LENGTH FROM PARM. LIST
         LTR   R10,R10             ZERO?
         BNP   NONAME              YEAH.
         LA    R11,2(,R11)         -> FIRST BYTE OF LIST.
         SR    R1,R1               CLEAR HIGH BYTE ON TRT       GP10097
MOREOPT  MVC   MESSAGE(9),=CL9'-OPTIONS:'
         LA    R5,MESSAGE+10
SCANOPT  BCTR  R10,0               LENGTH - 1
         EX    R10,SCAN            SCAN LIST FOR DELIMITER
         BNE   OPTFOUND            OK.
         TM    INTERN1,NMSRCH      IN NAME=X=Y OPTION?
         BO    NMGET               YEAH, GO.
         LR    R9,R10              TO FOOL PGM.
         CH    R10,THREE           4-BYTE OPTION?
         BNE   INVALIDR            NO.
         CLC   0(4,R11),ADECK      DECK?
         BE    DECKRTN             YES.
         B     INVALIDR            NO; INVALID
         SPACE
OPTEXIT  LR    R2,R3               KEEP DSECT IN R2 ALSO.
         SH    R5,=H'2'            - 2
         MVI   0(R5),BLANK         BLANK OUT LAST COMMA
         SR    R1,R1
         L     R15,VCONIO
         BALR  R14,R15             PRINT OUT OPT. LIST
         TM    OPTSW,NMRCVD        ANY NAME RECEIVED?
         BZ    NONAME              NO NAME.
         C     R8,ENDNMBFR         END OF NAME BUFFER?
         BH    LONGNAME            YES -- ERROR..
         ST    R8,ENDNMBFR         SAVE END-OF-NAME-BUFFER PTR.
EXIT     CHAIN -SAVE2              RETURN.
         SPACE 2
LONGNAME MVC   MESSAGE(45),=CL45'-** TOO MANY NAMES.  INCREASE REGION S*
               IZE. **'                                         GP10097
         B     ERRPRINT
         SPACE
NONAME   MVC   MESSAGE(31),=CL31'-** NO NAME OPTION RECEIVED. **'
ERRPRINT SR    R1,R1
         L     R15,VCONIO          PRINT MSG.
         BALR  R14,R15
         L     R1,SAVE2+4          GET SAVE AREA ADDRESS
         MVC   12(4,R1),VCONDONE   AND SET TO QUIT PROC.
         B     EXIT
         SPACE 3
OPTFOUND LR    R7,R1               GET OPTION
         SR    R7,R11              LENGTH
         LR    R9,R7               SAVE IN R9
         BZ    ZEROLN              LENGTH = 0: IGNORE SUCCESSIVE = OR ,
         BCTR  R7,0                FOR EX.
         STC   R2,BYTE             STORE DELIMITER
         TM    INTERN1,NMSRCH      AFTER NAME= ?
         BZ    CHECKOPT            NO. GO.
         CLI   BYTE,COMMA          FOUND COMMA?
         BNE   NOCOMMA             NO. GO.
*CHANGED NI    INTERN1,ALLBITS-NMSRCH  NOT AFTER NAME= ANY MORE.
NOCOMMA  OI    OPTSW,NMRCVD        GOT NAME.
         STC   R9,0(,R8)           SAVE LENGTH
         EX    R7,MOVE2BFR         MOVE TO BUFFER.
         MVC   0(5,R5),ANAME       MOVE NAME= FOR LIST
         EX    R7,MOVE2OPT
         LA    R1,5(R9,R5)         TO PUT IN COMMA
         MVI   0(R1),COMMA         PUT IN COMMA
         LA    R5,7(R9,R5)         INCREMENT PTR
         LA    R8,1(R8,R9)         INCREMENT NAME POINTER
         B     TSTCNT1             & GO TO CHECK IT.
         SPACE
ZEROLN   SR    R10,R9              UPDATE
         LA    R11,1(,R11)         STUFF
         B     TSTCNT2             &  GO.
         SPACE 2
* CHECK OPTION FOUND.
CHECKOPT CH    R7,THREE            4-BYTE OPTION?
         BNE   INVALIDR            NO; INVALID.
         CLC   0(5,R11),ANAME
         BE    NAMERTN             YES - NAME
         CLC   0(5,R11),ADECK
         BE    DECKRTN             YES - DECK
INVALIDR MVC   0(8,R5),=C'INVALID,'  INVALID OPTION.
         LA    R5,9(,R5)           UPDATE PTR.
         B     TSTCNT1             & BRANCH
         SPACE 2
NAMERTN  OI    INTERN1,NMSRCH      NOW LOOKING FOR NAME.
         B     TSTCNT1
         SPACE
DECKRTN  OI    OPTSW,DECK          DECK WANTED.
         MVC   0(5,R5),ADECK
         LA    R5,6(,R5)
TSTCNT1  SR    R10,R9              - LENGTH PROC.
         LA    R11,1(R9,R11)       UPDATE ADDRESS
TSTCNT2  LTR   R10,R10             ANYTHING LEFT?
         BNP   OPTEXIT             NO; GO.
         LA    R1,MESSAGE+L'MESSAGE-16
         CR    R5,R1               OPTIONS MSG FULL?
         BL    SCANOPT
         SR    R1,R1               IF SO, PRINT IT.
         LR    R2,R3
         L     R15,VCONIO
         BALR  R14,R15             PRINT PORTION OF OPTION LIST
         B     MOREOPT             GO RESTORE MESSAGE, ETC.
         SPACE
NMGET    OI    OPTSW,NMRCVD        NAME FOUND
*CHANGED NI    INTERN1,ALLBITS-NMSRCH  NOT LOOKING ANY MORE     GP10097
         LA    R9,1(,R10)          GET LENGTH
         LR    R7,R10              LENGTH FOR EX
         LR    R10,R9              FOOL THE PROG.
         B     NOCOMMA             PROCESS THIS NAME
         SPACE 5
***** THESE INSTRUCTIONS ARE EXECUTED. *****
         SPACE
MOVE2BFR MVC   1(0,R8),0(R11)      NAME TO NAME BUFFER
MOVE2OPT MVC   5(0,R5),0(R11)      NAME TO OPTION LIST
SCAN     TRT   0(0,R11),READBFR    SCAN THROUGH PARM FIELD.
         SPACE 3
***** CONSTANTS *****
         SPACE
THREE    DC    H'3'
BYTE     DC    C' '
ADECK    DC    C'DECK,'
ANAME    DC    C'NAME='
         SPACE
***** EXTERNAL REFERENCES *****
         SPACE
VCONIO   DC    V(IEWLIO)
VCONDONE DC    V(IEWLDONE)
         SPACE
         LTORG
         SPACE 3
         DROP  R3,R12
         SPACE
*****    END OF IEWLOPT
         SPACE 4
         END   IEWLOTP
