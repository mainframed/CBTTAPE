DTP2D    TITLE 'S U B P 2 D   **   DAY OF WEEK FROM DATE'
*   THIS SUBROUTINE PROVIDES THE DAY OF THE WEEK IN TWO STYLES:
*   INPUT R1 = PACKED DATE (00YYDDDS/01YYDDDS OR CCYYDDDS)
*            = 0 : USE CURRENT DATE
*
*   OUTPUT R15 = Â¬0 IF INVALID DATA PASSED (CHECKS NOT IMPLEMENTED)
*          R15 = 0
*           R0 = DAY OF WEEK (0-SUN, 1-MON,...,6-SAT)
*           R1 = ADDRESS OF EBCDIC DAY, E.G., CL9'WEDNESDAY'
*
SUBP2D   START 0
SUBP2D   AMODE ANY                                              GP00162
SUBP2D   RMODE ANY                                              GP00162
         STM   R14,R12,12(R13)
         LR    R9,R15        AND NEW BASE
         USING SUBP2D,R9
         LA    R8,7+20+11*4(,R13)  POINT INTO USER'S SAVE AREA  GP04075
         N     R8,=X'7FFFFFF8'  MAKE DOUBLE-WORD BOUNDARY       GP04075
         USING USERSAVE,R8   MAKE THIS CODE RE-ENTRANT/REFRESHABLE
         LTR   R1,R1         DID USER SUPPLY A DATE?
         BNZ   STICKIT       YES; BUT IS IT VALID?
         TIME  TU            GET THE DATE, RAPIDLY
STICKIT  ST    R1,DB+4       STASH THE DATE
         CLI   DB+4,X'19'    DID USER SUPPLY A CENTURY?
         BL    CENTGONE      NO
         SP    DB+4(4),=P'1900000'  ELSE NORMALIZE TO BASE 1900
CENTGONE XC    DB(4),DB      CLEAR FOR CONVERSION
         CVB   R3,DB         CONVERT DATE TO BINARY
         SLR   R2,R2         CLEAR FOR DIVIDE
         D     R2,=F'1000'   PLACE DAYS IN R2, YEAR IN R3
         LA    R3,6(,R3)     ADD A FINAGLE FACTOR
         LA    R1,1(,R3)     YEAR + SECOND FINAGLE FACTOR
         SRA   R1,2          # LEAP YEARS SINCE 1900
         MH    R3,=H'365'    DAYS SINCE 1900
         AR    R3,R1         INCLUDING LEAP DAYS
         AR    R3,R2         PLUS DAYS IN THIS YEAR
         LA    R3,6(,R3)     THIRD FINAGLE FACTOR
         SLR   R2,R2         CLEAR FOR DIVIDE
         ST    R2,16(,R13)   SET RETURN CODE EQUAL ZERO
         D     R2,=F'7'      GET DAY OF WEEK IN R2
         ST    R2,20(,R13)   RETURN TO USER IN R0 (0-SUN;1-MON...)
         MH    R2,=Y(MONDAY-SUNDAY) MAKE OFFSET IN TABLE        GP04040
         LA    R3,SUNDAY(R2)   POINT TO TEXT                    GP04040
         ST    R3,24(,R13)   RETURN TEXT ADDRESS IN R1
EXEUNT   LM    R14,R9,12(R13)  RESTORE MOST REGISTERS           GP04075
         BR    R14           RETURN TO CALLER
         SPACE 1
SUNDAY   DC    CL9'Sunday   ',AL1(6),CL9'   Sunday'             GP04040
MONDAY   DC    CL9'Monday   ',AL1(6),CL9'   Monday'             GP04040
         DC    CL9'Tuesday  ',AL1(7),CL9'  Tuesday'             GP04040
         DC    CL9'Wednesday',AL1(9),CL9'Wednesday'             GP04040
         DC    CL9'Thursday ',AL1(8),CL9' Thursday'             GP04040
         DC    CL9'Friday   ',AL1(6),CL9'   Friday'             GP04040
         DC    CL9'Saturday ',AL1(8),CL9' Saturday'             GP04040
         PRINT NOGEN
         SPACE 1                                                GP04075
USERSAVE DSECT ,             MY WORK AREA MAPPED ON USER'S SAVE AREA
DB       DS    D                                                GP04075
         SPACE 1
         YREGS ,
         END   ,
