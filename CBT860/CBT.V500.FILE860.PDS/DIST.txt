DIST     TITLE 'D I S T  ***  RETRIEVE SOURCE FROM DISTRIBUTION FILES'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1986  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2002  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
*                                                                     *
*        THE FUNCTION OF THIS PROGRAM, AND SOME OF THE DETAILS WERE   *
*        SUGGESTED BY THE PL/I PROGRAM NAMED DIST WRITTEN BY SEYMOUR  *
*        J. METZ (AT PINKERTON COMPUTER CONSULTANTS)                  *
*          IN THIS FILE AS MVTDIST,                                   *
*                                                                     *
***********************************************************************
         EJECT ,
***********************************************************************
*                                                                     *
*        DIST READS IBM SOURCE DISTRIBUTION FILES, AND EXTRACTS       *
*        REQUESTED MEMBERS FOR SUBSEQUENT PROCESSING.                 *
*        OUTPUT APPEARS ON SYSPRINT (REQUIRED) AND SYSUT2/SYSPUNCH    *
*        (OPTIONAL; DD DUMMY IF NOT WANTED).                          *
*        THE IBM INPUT TAPES ARE SUPPLIED ON SYSUT1 WITH DISP=PASS    *
*        FOR ANY ONE VALID FILE. ADDITIONAL FILES WILL BE READ BY     *
*        JFCB MODIFICATION.                                           *
*                                                                     *
*        THIS VERSION EXTENDS THE CODE TO COPY MEMBERS FROM DASD.     *
*        TAPE IS INDICATED BY A NON-ZERO FILE NUMBER.                 *
*                                                                     *
*        SYSIN CONTROL CARDS:                                         *
*                                                                     *
*        MEMBER/SERIAL/LBL/FILSQ/DSN                                  *
*                                                                     *
*        FOR TAPES, AND EFFICIENT PROCESSING, INPUT SHOULD BE         *
*              SORTED BY SERIAL, DSN, AND MEMBER.                     *
*                                                                     *
***********************************************************************
         SPACE 1
         PRINT &PRTSOR
         EXTRN SUBALLOC      ALLOCATION SUBROUTINE              GP10195
DIST     PGMHEAD ZERO,BASE=R12,PARM=R9                          GP10195
         SERVINIT ,          LOAD THE @SERVICE ROUTINE
         SERVLOAD @INPREAD,@PRINTER   LOAD SERVICE ROUINTES     GP10195
         PRTOPEN SYSPRINT,OPT=(ABEND)  OPEN THE PRINTER         GP03027
         SPACE 1
         PRTV HEADER,TITLE=1   WRITE THE HEADER
         INPOPEN SYSIN,OPT=(ABEND)  OPEN THE CONTROL CARD FILE
         LA    R14,SYSUT1    POINT TO DDN
         LA    R15,MIXVOL    POINT TO VOLSER                    GP10195
         LA    R0,MIXDSN     POINT TO DSN                       GP10195
         LA    R1,CURFSQ     POINT TO FILE SEQUENCE             GP10195
         STM   R14,R1,MODLIST  SKELETON MODJFCB LIST
         MVI   MODLIST,1     SET DDN PRESENT
         MVI   MODLIST+4,3   SET VOLSER PRESENT
         MVI   MODLIST+8,6   SET DSNAME PRESENT                 GP10195
         MVI   MODLIST+12,X'82'  SET SEQUENCE AND END OF LIST
         EJECT ,
***********************************************************************
*                                                                     *
*        GET NEXT CONTROL CARD AND ANALYZE IT.                        *
*              DSN= IS STORED; MEMBER/FILE REQUEST DONE IMEDIATELY    *
*                                                                     *
***********************************************************************
         SPACE 1
CONTLOOP INPGET MIXSECT      GET A CONTROL CARD                 GP10195
         BXH   R15,R15,CONTERR  SIGNAL ERROR ON INPUT
         PRTLIST CONCARD     LIST IT
         SPACE 1
         CLI   MIXVOL,C' '   VALID VOLSER ?                     GP10195
         BNH   BADCARD       NO
         XC    NUMCARDS,NUMCARDS  RESET CARD COUNTER
         BAL   R9,GETNUMB    EXPECT A NUMERIC
           B   BADCARD       FAIL IF NOT NUMERIC
         CL    R0,=F'32767'  VALID FILE NUMBER ?                GP10195
         BH    BADCARD
         STH   R0,CURFSQ     SAVE FILE SEQUENCE                 GP10195
         MVC   NEWNAME,=8X'FF'  PRESET TO HIGH NAME
         MVC   NAME,MIXMEM   GET MEMBER NAME                    GP10195
         SPACE 2
***********************************************************************
*                                                                     *
*        REQUEST FOR A NEW MEMBER:                                    *
*        IF VOLUME OR FILE NUMBER HAS CHANGED, NEED TO CLOSE INPUT    *
*        FILE, CHANGE THE JFCB, AND OPEN.                             *
*                                                                     *
***********************************************************************
         SPACE 1
         CLC   PRVKEY,MIXKEY      NEW VOLUME/FILE/DSN ?         GP10195
         BE    LEAVOPEN      NO                                 GP10195
DATAOPEN INPCLOSE DEV=2,OPT=LEAVE  LEAVE NEAR END OF FILE
         NI    FLAGS,255-DOPEN   RESET OPEN
         MVC   OLDNAME,BLANKS  CLEAR OLD NAME
         CLI   DYNDDNAM,C' ' ANY PRIOR ALLOCATION ?             GP10195
         BNH   ALLONEW         NO                               GP10195
         MVI   ALLOPARM,C' '                                    GP10195
         MVC   ALLOPARM+1(L'ALLOPARM-1),ALLOPARM  CLEAR         GP10195
         MVC   ALDDDNAM,DYNDDNAM    RETURNED DD NAME            GP10195
         MVI   ALDFUN,C'U'   UNALLOCATE                         GP10195
         SUBCALL SUBALLOC,(ALLOWORK,ALLOPARM),MF=(E,CALLPARM)   GP10195
         MVC   DYNDDNAM,BLANKS    CLEAR IT AGAIN                GP10195
         SPACE 1                                                GP10195
ALLONEW  ICM   R0,3,CURFSQ   TAPE (FILE > 0) ?                  GP10195
         BNZ   ALLOTAPE      YES; USE JFCB MODIFICATION         GP10195
         MVI   ALLOPARM,C' '                                    GP10195
         MVC   ALLOPARM+1(L'ALLOPARM-1),ALLOPARM  CLEAR         GP10195
         MVI   ALDFUN,C'A'   ALLOCATE                           GP10195
         MVC   ALDDSNAM,MIXDSN   SET DATA SET NAME              GP10195
         MVC   ALDVLSER(6),MIXVOL   AND VOLUME                  GP10195
         MVC   ALDSTATS,=C'SHR'  SHARED                         GP10195
         SUBCALL SUBALLOC,(ALLOWORK,ALLOPARM),MF=(E,CALLPARM)   GP10195
         MVC   DYNDDNAM,ALDDDNAM    RETURNED DD NAME            GP10195
         LTR   R15,R15       CHECK RETURN                       GP10195
         BZ    PDSOPEN                                          GP10195
         STM   R15,R0,DB     SAVE CODES                         GP10195
         PRTDATA 'Allocation for',(MIXDSN,PAD,DEB),'on',(MIXVOL,PAD),'f*
               ailed. RC=',(DB,4,I,LADJ,PADR),'RSN=',(DB+4,4,HEX)
         PRTDATA 'RC/ERR/INF:',(ALWRET,HEX,PAD),(ALWERR,HEX),(ALWINF,HE*
               X,PAD)                                           GP10195
         PRTCLOSE ,                                             GP10195
         ABEND 99,DUMP                                          GP10195
         SPACE 1
PDSOPEN  MVC   TEMPUT1(PATSIZE),PATUT1      REFRESH             GP10195
         MVC   TEMPUT1,ALDDDNAM   RETURNED DD NAME              GP10195
         INPOPEN TEMPUT1,DEV=2,OPT=ABEND TRY TO OPEN DATA FILE  GP10195
         B     COMMTEST      CHECK                              GP10195
         SPACE 1
ALLOTAPE LA    R1,MODLIST    SET MODIFICATION LIST              GP10195
         AIF   (&SVCJFCB NE 0).USESVC                           GP10195
         LINK  EP=SVCMJFCB   MUST BE AUTHORIZED                 GP10195
         AGO   .COMMOD                                          GP10195
.USESVC  SVC   &SVCJFCB      CHANGE THE JFCB A BIT
.COMMOD  SPACE 1
COMMOPEN INPOPEN SYSUT1,DEV=2,OPT=NOWTO  TRY TO OPEN DATA FILE
COMMTEST CH    R15,=H'8'     OPENED ?
         BNL   BADOPEN       NO
         OI    FLAGS,FOPEN   SHOW FILE OPEN
         SPACE 1
LEAVOPEN CLC   NAME,OLDNAME  IF NEW NAME IS HIGHER, READ
         BNH   DATAOPEN      ELSE MUST REPOSITION TO START
         MVC   PRVSECT,MIXSECT    NEW VOL/DS/FILE               GP10195
         TM    FLAGS,FEODAD  BUT IF EOF HIT PREVIOUSLY,
         BNZ   KEEPIT        MEMBER NOT FOUND
         MVC   OLDNAME,NAME
         SPACE 2
***********************************************************************
*                                                                     *
*        INPUT FILE IS NOW OPEN.  SEARCH FOR THE REQUIRED MEMBER      *
*                                                                     *
***********************************************************************
         SPACE 1
LOOKLOOP INPGET DEV=2        GET A DATA CARD
         BXH   R15,R15,DATAERR
         LR    R8,R1         COPY THE ADDRESS
         USING DATA,R8       DECLARE IT
         CLC   =C'./',DATA   IEBUP--- CONTROL CARD ?
         BNE   LOOKLOOP      NO; IGNORE IT
         PRTLIST MSGDATA     ELSE PRINT IT
         BAL   R14,GETNAME   GET MEMBER NAME, IF ANY            GP10195
          B    DATAEOD         FOUND ./ ENDUP                   GP10195
           B   LOOKLOOP        IGNORE THIS                      GP10195
         MVC   NEWNAME,DB    COPY FOUND NAME                    GP10195
         CLC   NEWNAME,NAME  COMPARE TO USER'S REQUEST
         BL    LOOKLOOP      CONTINUE READING
         BH    KEEPIT        NOT FOUND - SET MINOR ERROR
         SPACE 2
***********************************************************************
*                                                                     *
*        REQUESTED MEMBER HAS BEEN LOCATED.  COPY UNTIL               *
*              END-FILE, OR UNTIL THE START OF THE NEXT MEMBER.       *
*                                                                     *
***********************************************************************
         SPACE 1
         TM    FLAGS,FOUTOP  OUTPUT OPEN ?
         BNZ   DATACOPY
         PRTOPEN SYSUT2,OPT=(ABEND),DEV=2
         OI    FLAGS,FOUTOP  SET OPEN
         CH    R15,=H'8'     REALLY ?
         BNH   DATACOPY
         PRTL  '0***** SYSUT2 OPEN FAILED *****'                GP02325
         PRTCLOSE
         ABEND 1442,DUMP
         SPACE 1
DATACOPY PRTF  DATA,80,CC=NO,DEV=2  'PUNCH' THE CONTROL CARD    GP10195
         INC   NUMCARDS      COUNT CARDS PUNCHED
         INPGET DEV=2        GET ANOTHER CARD
         BXH   R15,R15,DATAERR
         LR    R8,R1         SAVE THE CARD ADDRESS
         CLC   =C'./',DATA   CONTROL CARD ?
         BNE   DATACOPY      NO
         BAL   R14,GETNAME   LOOK FOR NAME=                     GP10195
          B    DATAEOD         ./ ENDUP                         GP10195
           B   DATACOPY      COPY THE FUNNY CARD
         B     KEEPIT        RETURN AFTER ENDING THIS MEMBER
DATAEOD  OI    FLAGS,FEODAD  SET LOGICAL OR PHYSICAL EOF
         LA    R8,=CL80'./ ADD NAME=99999999'   FAKE LAST CARD  GP10195
         B     DATAMEND      DONE WITH THIS CONTROL CARD
         SPACE 1
KEEPIT   INPKEEP DEV=2       REREAD CURRENT CARD
DATAMEND ICM   R0,15,NUMCARDS  TEST CARDS FOUND
         BZ    DATANONE      NONE ?
         PRTLIST MSGOVNUM    SHOW NUMBER OF CARDS COPIED
         B     CONTLOOP      NEXT MEMBER                        GP10195
DATANONE OICC  4             SET ERROR CODE                     GP10195
         PRTLIST MSGNOMEM    SHOW MEMBER NOT FOUND
         B     CONTLOOP      NEXT MEMBER                        GP10195
         SPACE 1
CONTERR  PRTL  '0***** I/O ERROR ON SYSIN *****'                GP02325
         PRTCLOSE ,          FORCE PRINT LINE OUT
         ABEND 2540,DUMP
         SPACE 1
DATAERR  PRTL  '0***** I/O ERROR ON SYSUT1 *****'               GP02325
         PRTCLOSE ,          FORCE PRINT LINE OUT
         ABEND 2400,DUMP
         SPACE 1
BADCARD  LA    R2,5(,R6)     GET SCAN POINTER
         SR    R2,R7         LESS CARD START
         PRTLIST MSGBDCAR    SIGNAL BAD CONTROL CARD
         OICC  8             SET LEVEL 8 ERROR                  GP10195
         B     CONTLOOP      GO BACK FOR MORE PUNISHMENT
         SPACE 1
BADOPEN  PRTL  '0***** SYSUT1 OPEN FAILED *****'                GP02325
         PRTCLOSE ,
         ABEND 2400,DUMP
         SPACE 1
EXITS    SERVTERM ,          CLOSE AND FREE EVERYTHING
         L     R9,RETCODE                                       GP10195
         PGMEXIT RC=(R9)     RETURN                             GP10195
         SPACE 1
*---------------------------------------------------------------------*
*   GETNAME - SUBROUTINE TO PARSE ./ CARD                             *
*     RETURNS: +0 ENDUP (LOG. EOF)  +4 NO NAME  +8 NAME= IN DB        *
*---------------------------------------------------------------------*
GETNAME  ST    R14,DB2       SAVE RETURN ADDRESS                GP10195
         LA    R4,1          INCREMENT FOR BXLE                 GP10195
         LA    R6,DATA+2     PREPARE FOR TRT PROCESSING
         LA    R5,DATA+70    LAST VALID
         BAL   R9,DOBLANK    FIND A BLANK (PRE-VERB)
           B   GETNAME4      BAD CARD ?
         BAL   R9,NOBLANK    FIND VERB
           B   GETNAME4      BAD CARD
         BAL   R9,DOBLANK    FIND A BLANK (END OF VERB)
           B   GETNAME4      BAD CARD ?
         CLC   =C'ADD ',0(R3)  ADD REQUEST ?                    GP10195
         BE    GETNADD       YES; PROCESS IT
         CLC   =C'REP',0(R3)  REPLACE OR REPRO REQUEST ?        GP10195
         BE    GETNADD       YES; PROCESS IT
         CLC   =C'ENDUP',0(R3)  END REQUEST ?                   GP10195
         BNE   GETNAME4      NO; IGNORE IT
         L     R14,DB2       RESTORE RETURN                     GP10195
         BR    R14           RETURN LOGICAL EOF                 GP10195
         SPACE 1
GETNADD  BAL   R9,NOBLANK    FIND START OF OPERAND FIELD
           B   GETNAME4      SKIP
         BAL   R9,DOBLANK    FIND END OF OPERAND
           B   GETNAME4      BAD CARD ?
         SPACE 1
         CLC   =C'NAME=',0(R3)  IEBUPDTE NAME FIELD ?
         BNE   GETNADD       NO; TRY AGAIN
         LA    R3,5(,R3)     SKIP VERB
         SH    R15,=H'5'     ADJUST LENGTH                      GP10195
         SPACE 1
         MVC   DB,BLANKS     CLEAR NAME
         LTR   R15,R15       CHECK LENGTH                       GP10195
         BNP   GETNADD         LOOK AGAIN ???                   GP10195
         MINH  R15,=H'8'     NOT TOO MUCH                       GP10195
         BCTR  R15,0         EX LENGTH                          GP10195
         EX    R15,EXMVCNEW  MOVE IT                            GP10195
         L     R14,DB2       RESTORE RETURN                     GP10195
         B     8(,R14)       RETURN NAME IN DB                  GP10195
         SPACE 1
GETNAME4 L     R14,DB2       RESTORE RETURN                     GP10195
         B     4(,R14)       NOT ADD/REP CARD OR NO NAME        GP10195
         SPACE 2
GETNUMB  TRT   MIXFIL,TRTNUMB   TEST FOR NUMERICS (AND BLANK)   GP10195
         BNZR  R9            INVALID FILE                       GP10195
         PACK  DB,MIXFIL     CHANGE TO PACKED                   GP10195
         CVB   R0,DB         CHANGE TO BINARY                   GP10195
         B     4(,R9)        TAKE GOOD RETURN                   GP10195
         SPACE 2
NOBLANK  LA    R14,TRTNBLNK  FIND A NON-BLANK                   GP10195
         B     DOTRTCOM                                         GP10195
         SPACE 1
DOBLANK  LA    R14,TRTBLANK  POINT TO BLANK SKIPPER             GP10195
DOTRTCOM LR    R15,R5        COPY END POSITION                  GP10195
         SR    R15,R6          LESS START                       GP10195
         BMR   R9            NO MORE                            GP10195
         SR    R2,R2                                            GP10195
         LA    R1,1(,R5)     STOP ADDRESS IF EXHAUSTED          GP10195
         EX    R15,EXTRT14                                      GP10195
         LA    R15,0(,R1)                                       GP10195
         SR    R15,R6        LENGTH SKIPPED                     GP10195
         LR    R3,R6         SAVE START                         GP10195
         LA    R6,0(,R1)     UPDATE                             GP10195
         B     4(,R9)        RETURN OK                          GP10195
EXMVCNEW MVC   DB(0),0(R3)   MOVE ADD/REP NAME                  GP10195
EXTRT14  TRT   0(0,R6),0(R14)     FIND BLANK/NON-BLANK          GP10195
         SPACE 2
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=3
SYSUT2   PUNWORK SYSUT2,SYSPUNCH,RECFM=FB,WIDTH=80
SYSIN    INPWORK SYSIN,WIDTH=80,EDIT=128,EODAD=EXITS
SYSUT1   INPWORK SYSUT1,WIDTH=80,EDIT=128,EODAD=DATAEOD         GP10195
PATUT1   INPWORK nnn,WIDTH=80,EDIT=128,EODAD=DATAEOD,PDS=(MEM,ALI,UPD),*
               FILL=C' '     BLANK FILL SHORT RECORDS           GP10195
PATSIZE  EQU   *-PATUT1                                         GP10195
         SPACE 1
HEADER   VCON  '#D I S T  ***  RETRIEVAL LOG'
         FDPRT *END
CONCARD  FDPRT ' ',NL,LEN=5
         FDPRT MIXSECT,LEN=80                                   GP10195
         FDPRT *END
MSGDATA  FDPRT ' ',NL,LEN=20
         FDPRT DATA,LEN=80
         FDPRT *END
MSGBDCAR FDPRT BLANKS,(R2),NL
         FDPRT '| - LAST COLUMN SCANNED'
         FDPRT '*****',NL
         FDPRT 'INVALID CONTROL CARD',PAD
         FDPRT '*****'
         FDPRT *END
MSGOVNUM FDOPT NL,CC=C'&&'   PREFERRED OVERPRINT/PRINT IF NO OVPRT
         FDPRT NUMCARDS,I,RADJ,LEN=110
         FDPRT 'RECORDS PROCESSED',PAD
         FDPRT *END
MSGNOMEM FDPRT '***** Member',NL
         FDPRT NAME,PAD,DEB
         FDPRT 'not found *****'
         FDPRT *END
BLANKS   DC    CL88' '       CONSTANT
         SPACE 1
TRTNUMB  DC    256AL1(4)     SET ALL INVALID                    GP10195
         TRENT TRTNUMB,0,C' '   ALLOW BLANK (LEADING?)          GP10195
         TRENT ,0,(C'0',10)     AND DIGITS                      GP10195
         SPACE 1
TRTBLANK DC    256AL1(0)     SET ALL VALID                      GP10195
         TRENT TRTBLANK,4,C' '  ALLOW BLANK (LEADING?)          GP10195
         TRENT ,8,C','          AND COMMA                       GP10195
         SPACE 1
TRTNBLNK DC    256AL1(4)     SET ALL INVALID                    GP10195
         TRENT TRTNBLNK,0,C' '  SKIP BLANK (LEADING?)           GP10195
         TRENT ,0,C','          AND COMMA                       GP10195
         SPACE 1
SAVE     DSECT ,             SAVE/WORK AREA
DB       DS    D
DB2      DS    D
WORK     DS    D
         SERVDEFS ,          SERVICE DATA                       GP10195
TEMPUT1  INPWORK nnn,WIDTH=80,EDIT=128,EODAD=DATAEOD,PDS=(MEM,ALI,UPD)
         SPACE 1
MODLIST  DC    0F'0',X'01',AL3(SYSUT1) DDN PTR             1/4
         DC    X'03',AL3(MIXVOL)       VOL-SER PTR         2/4
         DC    X'06',AL3(MIXDSN)       DSN PTR             3/4
         DC    X'82',AL3(CURFSQ)       FILESEQ PTR         4/4
         SPACE 1
         MAPINDEX DSECT=NO,PFX=MIX     INPUT FORMAT             GP10195
MIXSECT  EQU   MIXDSECT,MIXLEN,C'C'    MAP AS SINGLE ENTITY     GP10195
MIXKEY   EQU   MIXVOL,MIXDST-MIXVOL,C'C'  NON-MEMBER DATA       GP10195
         MAPINDEX DSECT=NO,PFX=PRV     SAVED INPUT              GP10195
PRVSECT  EQU   PRVDSECT,PRVLEN,C'C'    MAP AS SINGLE ENTITY     GP10195
PRVKEY   EQU   PRVVOL,PRVDST-PRVVOL,C'C'  NON-MEMBER DATA       GP10195
CURFSQ   DS    H             (BINARY) FILE SEQUENCE (DASD=0)    GP10195
NAME     DS    CL8           CURRENT NAME
OLDNAME  DS    CL8           LAST REQUESTED NAME
NEWNAME  DS    CL8           (TAPE) NEXT MEMBER NAME
DYNDDNAM DS    CL8           RETURNED DD FROM DYNAMIC ALLOCATE  GP10195
NUMCARDS DS    F             NUMBER OF CARDS IN MEMBER
FLAGS    DC    X'00'         PROCESSING FLAGS
FOUTOP   EQU   X'04'           PUNCH OUTPUT IS OPEN
FOPEN    EQU   X'02'           DATA FILE IS OPEN
FEODAD   EQU   X'01'           DATA FILE IS AT END-FILE
DOPEN    EQU   FOPEN+FEODAD    DATA FILE RESET
         SPACE 1
ALLOLIST MAPSBALL DSECT=NO
ALLOPARM EQU   ALLOLIST,*-ALLOLIST,C'C' ALLOCATION REQUEST      GP10195
ALLOWORK MAPSBAWK DSECT=NO   RETURN CODES AND MESSAGES          GP10195
SAVEEND  EQU   *
         SPACE 1
SOURCECT DSECT ,             SYSUT1 DATA DEFINITION
DATA     DS    CL80
         SPACE 1
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         DCBD  DEVD=DA,DSORG=PS
         END   ,
