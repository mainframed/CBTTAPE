REXSTRIP TITLE 'R E X S T R I P  ***  COMPRESS REXX PROGRAMS'
         PUNCH ' ORDER REXSTRIP(P) '  MAKE DUMPS EASIER TO READ
         SPACE 1
         COPY  OPTIONGB      DEFINE GLOBALS
         SPACE 1
         SYSPARM LIST=YES    SET GLOBALS
         PRINT &PRTSOR
***********************************************************************
**                                                                   **
**   REXSTRIP  -  ELIMINATE EXTRANEOUS BLANKS, COMMENTS, AND TEXT    **
**     LINES FROM REXX PROGRAMS                                      **
**                                                                   **
**   REXXIN   -  INPUT DD - SEQUENTIAL OR PDS MEMBER FB/80 OR VB/255 **
**                                                                   **
**   REXXOUT  - OUTPUT DD - SEQUENTIAL OR PDS MEMBER FB/80 OR VB<260 **
**                                                                   **
**   OUTPUT IS SEQUENTIAL, AND GENERALLY MATCHES THE RECORD FORMAT   **
**     AND LENGTH OF THE INSTALLATION'S CLISTS                       **
**                                                                   **
***********************************************************************
         SPACE 1
REXSTRIP PGMHEAD ZERO31,BASE=(R11,R12),PARM=R9,LOC=BELOW,RM=24
         LA    R10,SAVEWORK  GET COMMON ADDRESSABILITY WORK AREA
         DROP  R13           DROP SAVE AREA
         USING SAVEWORK,R10
         SPACE 1
         MVC   LISTDCB(PATDCBL),PATLIST  MOVE COMMON DATA
         SERVINIT ,          LOAD THE SERVICE ROUTINE, ETC.
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE OUTPUT PROCESSOR
         ST    R0,@PRINTER   SAVE IT FOR PRT- CALLS
         PRTOPEN SYSPRINT,OPT=(WTO)  DON'T BOMB IF MISSING
         PRTLIST VTITLE,TITLE=1  MAKE PAGE HEADER
         SPACE 1
         PUSH  USING
         USING IHADCB,REXXOUT
         LOAD  EPLOC=@DCBEXIT,ERRET=NOEXFILL  LOAD DCB EXIT PROCESSOR
         LR    R15,R0        SAVE THE ADDRESS
         LA    R1,OUTEXPRM   GET THE DCB EXIT PARAMETER LIST
         STM   R0,R1,OUTEXLST  BUILD THE EXIT LIST ADDRESSES
         MVI   OUTEXLST,X'05'  SET DCB EXIT
         MVI   OUTEXLST+4,X'80'  SET @DCBEXLST PARM
         LA    R2,OUTEXLST   POINT TO EXIT LIST
         STCM  R2,7,DCBEXLST+1   PLACE INTO DCB
         LA    R0,SPLITTER
         LA    R1,SPLITTWO-SPLITEND
         LNR   R1,R1         GOING BACKWARDS
         LA    R2,SPLITEND
         STM   R0,R2,@SPLIT  MAKE BXH POINTERS
         POP   USING
         SPACE 2
         PUSH  USING
NOEXFILL LA    R2,REXXIN     POINT TO INPUT DCB
         LA    R3,REXXOUT    AND OUTPUT DCB
         OPEN  ((R2),(INPUT,REREAD),(R3),OUTPUT),MODE=31,              *
               MF=(E,LISTDCB)   OPEN INPUT AND OUTPUT
         USING IHADCB,REXXIN    POINT TO INPUT DCB
         TM    DCBOFLGS,DCBOFOPN  DID IT OPEN ?
         BZ    FAILINP       NO; SET MESSAGE
         POP   USING
         PUSH  USING
         USING IHADCB,REXXOUT   POINT TO OUTPUT DCB
         TM    DCBOFLGS,DCBOFOPN  DID IT OPEN ?
         BZ    FAILOUT       NO; SET MESSAGE
         SPACE 1
         LA    R1,OUTLINE    POINT TO DATA
         LR    R0,R1
         LH    R2,DCBBLKSI   GET OUTPUT BLOCKSIZE IF RECFM=U
         TM    DCBRECFM,DCBRECU  BUT IS IT ?
         BO    OUTFCOM       YES; CHECK FOR CARRIAGE CONTROL
         LH    R2,DCBLRECL   GET RECORD LENGTH FOR ALL OTHERS
         TM    DCBRECFM,DCBRECF  IS IT FIXED ?
         BNZ   OUTFCOM       YES; NO RDW OVERHEAD
         SH    R2,=H'4'      ADJUST MAX LENGTH
         SH    R0,=H'4'      AND ALLOW ROOM FOR RDW
OUTFCOM  TM    DCBRECFM,DCBRECCA+DCBRECCM  CARRIAGE CONTROL ?
         BZ    OUTCCOM       NEITHER
         BCTR  R2,0          ADJUST MAX DATA LENGTH
         BCTR  R0,0          ADJUST START ADDRESS
         MVI   OUTLINE-1,C' '  SET ASA SINGLE SPACE
         TM    DCBRECFM,DCBRECCM  BUT IS IT MACHINE CODE ?
         BZ    OUTCCOM       NO; ASA
         MVI   OUTLINE-1,X'89'  SET MACHINE CODE - PRINT AND SPACE 1
OUTCCOM  LR    R3,R2         COPY MAX LENGTH
         LA    R2,8          SET CURRENT LENGTH
         ST    R2,SPLITTER   SAVE SPLIT POINT
         STM   R0,R3,@OUTRDW SAVE ADDRESSES AND DATA LENGTHS
         LA    R0,=C'/*REXX*/'  FORCE PRESENCE OF LANGUAGE IDENTIFIER
         L     R1,=X'40000008'  SET LENGTH AND BLANK PADDING
         LA    R14,OUTLINE   OUTPUT LINE
         L     R15,=A(OUTSIZE)  256 MAX + OVERFLOW
         MVCL  R14,R0        INITIALIZE OUTPUT BUFFER
         POP   USING
         SPACE 2
***********************************************************************
**                                                                   **
**  MAINLOOP - READ INPUT LINES                                      **
**                                                                   **
***********************************************************************
         SPACE 1
MAINLOOP BAS   R14,GETCARD   GET NEXT INPUT RECORD
         LM    R4,R5,@INPLINE  LOAD INPUT ADDRESS/LENGTH
         LTR   R4,R4         END-FILE ?
         BZ    INPUTEOF      YES; WRITE FINAL LINE, IF ANY
         TM    PROFLAGS,PFASIS  BYPASS CONVERSION ?
         BZ    NEXTBYTE      CONVERT
         LR    R15,R5        COPY LENGTH
         BCTR  R15,0         SET LENGTH FOR EXECUTE
         EX    R15,EXMVASIS  MOVE WITHOUT CHANGE
         LR    R0,R5         COPY LENGTH
         BAS   R14,PUTLINE   PUT IT OUT AS IS
         B     MAINLOOP      GET NEXT RECORD
EXMVASIS MVC   OUTLINE(0),0(R4)  MOVE TEXT AS IS
         SPACE 1
NEXTBYTE LTR   R5,R5         ALL DONE WITH THIS LINE
         BNP   INPUTEND      YES; GET ANOTHER
         ICM   R0,15,NUMCOMM IN A COMMENT ?
         BZ    SKIPCOMM      NO
*---------------------------------------------------------------------*
*                                                                     *
*  COMMENT - IN A COMMENT, SCAN FOR ASTERISK, THEN SLASH              *
*    IF FOUND, REDUCE COMMENT LEVEL BY ONE                            *
*    OTHERWISE SKIP THE COMMENT TEXT                                  *
*                                                                     *
*---------------------------------------------------------------------*
         XC    WORK256,WORK256  BUILD A TRT TO FIND AN ASTERISK
         MVI   WORK256+C'*',C'*'  STOP ON ASTERISK
         LA    R0,256        MAKE A CONSTANT
         LR    R14,R0        MAX LENGTH ON TRT
         MIN   R14,(R5)      REMAINING INPUT BUFFER LENGTH
         LA    R15,WORK256   ADDRESS OF TRT
         BCTR  R14,0         LENGTH FOR EXECUTE
         EX    R14,EXTRT4    LOOK FOR SPECIAL CHARACTER
         BNZ   COMMTEND      SKIP PAST ASTERISK - CHECK IF COMMENT END
         AR    R4,R0         NOTHING IN THIS LINE
         SR    R5,R0
         BP    NEXTBYTE      CHECK REST OF LINE
         B     INPUTEND      ELSE GET ANOTHER RECORD
         SPACE 1
COMMTEND LA    R1,1(,R1)     SKIP ASTERISK
         AR    R5,R4         FINAGLE THE NEW LENGTH
         SR    R5,R1         NOW HAVE LENGTH INCLUDING THE ASTERISK
         LR    R4,R1         ASTERISK ADDRESS
         LTR   R5,R5         ANYTHING LEFT ?
         BNP   INPUTEND      NO; IGNORE COMMENT LINE
         CLI   0(R1),C'/'    END OF COMMENT ? (*/)
         BNE   NEXTBYTE      NO; RESUME SEARCH
         LA    R4,1(,R4)     YES; SKIP SLASH
         BCTR  R5,0          REMAINING LENGTH
         L     R15,NUMCOMM   GET COMMENT LEVEL
         SH    R15,=H'1'     NEW LEVEL - SET CONDITION CODE
         ST    R15,NUMCOMM   SAVE IT BACK
         BNM   NEXTBYTE      SO FAR, SO GOOD
         PRTL  '0*** END COMMENT WITHOUT MATCHING START ***'
         ABEND 1234,DUMP
EXTRT4   TRT   0(0,R4),0(R15)  LOOK FOR STOP CHARACTER
EXMVC15  MVC   0(0,R15),0(R4)  MOVE TEXT
         SPACE 2
SKIPCOMM SR    R1,R1
         ICM   R1,1,CHARQUOT GET AND TEST QUOTE CHARACTER
         BZ    MAINSCAN      NONE
*---------------------------------------------------------------------*
*                                                                     *
*  QUOTE - IN A QUOTED STRING WE COPY EVERYTHING AS IS UNTIL THE      *
*    MATCHING END QUOTE IS FOUND                                      *
*    (NOTE THAT DOUBLED QUOTES REQUIRE SPECIAL CODE)                  *
*    RESETS QUOTE FLAG TO X'00' ON ENDING QUOTE                       *
*                                                                     *
*---------------------------------------------------------------------*
         XC    WORK256,WORK256    CLEAR WORK AREA
         STC   R1,WORK256(R1)     STOP ON MATCHING QUOTE
         LA    R0,256        MAXIMUM TRT LENGTH
         LR    R14,R0        MAKE A CONSTANT
         MIN   R14,(R5)      LENGTH REMAINING
         LA    R15,WORK256   POINT TO TRT
         BCTR  R14,0         LENGTH FOR EXECUTE
         LA    R1,0(R14,R4)  END IF NO MATCH
         EX    R14,EXTRT4    LOOK FOR END QUOTE
         BZ    QUOTSTAM      NO MATCH - JUST COPY
QUOTLAST MVI   CHARQUOT,0    RESET QUOTE VALUE AND STATUS
QUOTSTAM LR    R14,R1        GET STOP LOCATION
         SR    R14,R4        LESS START
         LM    R2,R3,@OUTLINE  GET OUTPUT LINE AND LENGTH
         LA    R15,0(R3,R2)  NEXT AVAILABLE
         EX    R14,EXMVC15   COPY TEXT
         LA    R3,1(R14,R3)  NEW OUTPUT LENGTH
         ST    R3,@OUTLEN    UPDATE IT
         LA    R1,1(,R1)     SKIP PAST LAST MOVED BYTE
         AR    R5,R4         FINAGLE THE NEW LENGTH
         SR    R5,R1         NOW HAVE LENGTH
         LR    R4,R1         NEW SCAN ADDRESS
         LTR   R5,R5         ANYTHING MORE IN THIS LINE ?
         BNP   INPUTEND      NO
         B     NEXTBYTE      YES; SCAN
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*  SCAN FOR INTERESTING CHARACTERS:                                   *
*     /*  START OF COMMENT                                            *
*     '   START OF QUOTE (APOSTROPHE)                                 *
*     "   START OF QUOTE                                              *
*     SPECIAL CHARACTER  + - \ % * | & = < > ¬ ; , :                  *
*     REQUIRED BLANK                                                  *
*     OPTIONAL BLANK (ELIMINATED)                                     *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
MAINSCAN LM    R6,R7,@OUTLINE     GET OUTPUT ADDRESS/LENGTH
         LTR   R7,R7         EMPTY NOW ?
         BNZ   SAVELAST      NO
         MVI   PREVCHAR,C';'  MAKE STATEMENT SEPARATOR
         B     TESTLAST
SAVELAST LA    R15,0(R7,R6)  NEXT AVAILABLE BYTE
         BCTR  R15,0         LAST CHARACTER
         MVC   PREVCHAR,0(R15)  SAVE IT
TESTLAST SR    R2,R2         SET FOR NO MATCH
         TRT   PREVCHAR,TRTSCAN  CLASSIFY IT
         STC   R2,PREVINDX   SAVE IT
         SPACE 1
         LTR   R5,R5         AMOUNT LEFT TO SCAN
         BNP   INPUTEND      NOTHING
         LA    R0,256        MAKE A CONSTANT
         LR    R14,R0        COPY
         MIN   R14,(R5)      GET REMAINING LENGTH (OR MAX)
         BCTR  R14,0         EXECUTE LENGTH
         LA    R15,TRTSCAN   POINT TO SCAN TABLE
         SR    R2,R2         CLEAR FOR NO MATCH
         LA    R1,1(R14,R4)  STOPPER FOR NO MATCH
         EX    R14,EXTRT4    LOOK FOR SPECIAL
         LA    R15,0(R7,R6)  NEXT AVAILABLE OUTPUT
         LR    R14,R1        GET STOP ADDRESS
         BCTR  R14,0         ADJUST FOR SPECIAL CHARACTER
         SR    R14,R4        LESS START = LENGTH PASSED
         BM    MAINBRCH      NOTHING - SKIP MOVE
         EX    R14,EXMVC15   COPY SKIPPED TEXT
         LA    R14,1(,R14)   TRUE LENGTH
         AR    R7,R14        ADJUST OUTPUT LENGTH
         ST    R7,@OUTLEN    AND UPDATE IT
         AR    R4,R14        POSITION TO SPECIAL CHARACTER
         SR    R5,R14        ADJUST INPUT LENGTH
         MVI   PREVCHAR,C'?'  DON'T KNOW
         MVI   PREVINDX,0    NOT SPECIAL
MAINBRCH B     *+4(R2)       BRANCH ACCORDING TO TYPE
         B     NEXTBYTE  +0  PROCESS REMAINING TEXT
         B     MAINQUOT  +4  PROCESS QUOTE
         B     MAINBLNK  +8  PROCESS A SPACE
         B     MAINSLSH +12  PROCESS SLASH (LOOK FOR COMMENT)
         B     MAINSPEC +16  PROCESS SPECIAL CHARACTER
         B     MAINSENT +20  PROCESS SEPARATOR
         SPACE 2
MAINBLNK CLI   PREVINDX,8    PREVIOUS BLANK OR SPECIAL ?
         BNL   SKIPBLNK      YES; GO INTO SKIP MODE
MOVECHAR LM    R6,R7,@OUTLINE  GET OUTPUT ADDRESS/LENGTH
         LA    R15,0(R7,R6)  NEXT AVAILABLE CHARACTER
         MVC   0(1,R15),0(R4)  MOVE ONE CHARACTER
         LA    R0,1
         AR    R4,R0         ADJUST INPUT ADDRESS
         AR    R7,R0         ADJUST OUTPUT LENGTH
         SR    R5,R0         RESIDUAL INPUT LENGTH
         ST    R7,@OUTLEN    NEW LENGTH
         B     NEXTBYTE
         SPACE 1
SKIPBLNK LA    R0,256        MAKE A CONSTANT
         LR    R14,R0        COPY
         MIN   R14,(R5)      GET REMAINING LENGTH (OR MAX)
         BCTR  R14,0         EXECUTE LENGTH
         LA    R15,TRTNBLNK  LOOK FOR A NON-BLANK
         SR    R2,R2         CLEAR FOR NO MATCH
         LA    R1,1(R14,R4)  STOPPER FOR NO MATCH
         EX    R14,EXTRT4    LOOK FOR NON-BLANK
         AR    R5,R4
         LR    R4,R1         NEW SCAN START
         SR    R5,R1         ADJUST INPUT LENGTH
         BP    MAINSCAN      MORE TO DO
         LM    R6,R7,@OUTLINE  RELOAD OUTPUT ADDRESS/LENGTH
         LTR   R7,R7         EMPTY OUTPUT BUFFER ?
         BNP   NEXTBYTE      NO
         LA    R15,0(R7,R6)  NEW OUTPUT ADDRESS
         BCTR  R15,0
         CLI   0(R15),C' '   LINE WITH TRAILING BLANK ?
         BNE   NEXTBYTE      NO; CONTINUE SCANNING
         BCTR  R7,0          DON'T NEED IT
         ST    R7,@OUTLEN
         B     NEXTBYTE
         SPACE 1
MAINQUOT MVC   CHARQUOT,0(R4)  SHOW WE'RE IN A QUOTED STRING
         B     MOVECHAR      AND COPY THIS CHARACTER
         SPACE 1
MAINSLSH C     R5,=F'2'      AT LEAST ONE MORE IN BUFFER ?
         BL    MAINSPEC      NO; SPECIAL CHARACTER
         CLI   1(R4),C'*'    HAVE WE A /* PAIR ?
         BNE   MAINSPEC      NO; SPECIAL CHARACTER
         LA    R0,2          SKIP THESE TWO
         AR    R4,R0         UPDATE INPUT ADDRESS
         SR    R5,R0         ADJUST LENGTH
         INC   NUMCOMM       UPDATE COMMENT NESTING LEVEL
         B     NEXTBYTE      PROCESS MORE
         SPACE 1
MAINSPEC CLI   PREVINDX,8    WAS PREVIOUS A BLANK?
         BNE   MOVECHAR      NO; COPY THIS ONE
         ICM   R7,15,@OUTLEN    GET CURRENT LENGTH
         BNP   MOVECHAR      OOPS
         BCTR  R7,0          BACK UP ONE
         ST    R7,@OUTLEN    UPDATE IT
         B     MOVECHAR      AND COPY THIS ONE
         SPACE 1
MAINSENT CLC   @OUTLEN+2(2),SPLITLEN  ALREADY ON THE STACK ?
         BE    MAINSPEC      YES; BUT HOW ?
         MVC   SPLITEND(SPLITTER-SPLITEND),SPLITTWO  PUSH THE STACK
         MVC   SPLITTER(4),@OUTLEN  SAVE CURRENT POSITION AND 0 FLAG
         B     MAINSPEC      AND SAVE SEPARATOR CHARACTER
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*  INPUTEND - WRITE FULL OUTPUT LINE                                  *
*                                                                     *
*    IF OVERLY FULL (LAZY CODE), SPLIT                                *
*                                                                     *
*---------------------------------------------------------------------*
         SPACE 1
INPUTEOF LM    R4,R5,ZEROES  LOOP IN WRITE UNTIL ALL OUTPUT DONE
INPUTEND LM    R6,R7,@OUTLINE  GET OUTPUT POINTERS
         LTR   R7,R7         ANYTHING TO DO ?
         BNP   MAINNULL      NO; JUST RESET
         LA    R15,0(R7,R6)  NEXT AVAILABLE OUTPUT CHARACTER
         BCTR  R15,0         POINT TO LAST CHARACTER
         CLI   CHARQUOT,0    ARE WE IN A QUOTED STRING ?
         BNE   MAINFQUO      YES; KEEP AS IS
MAINFTST CLI   0(R15),C';'   TRAILING  STATEMENT SEPARATOR ?
         BE    MAINFTSS      YES; LOP IT OFF
         CLI   0(R15),C' '   TRAILING BLANK?
         BNE   MAINFQUO      NO; LEAVE IT
MAINFTSS MVI   0(R15),C' '   OVERLAY
         BCTR  R15,0
         BCT   R7,MAINFTST   CHECK AGAIN
         ST    R7,@OUTLEN    UPDATE LENGTH
         B     MAINNULL      JUST SPACES AND SEPARATORS ?
         SPACE 1
MAINFQUO MVC   PREVCHAR,0(R15)  SAVE LAST
         SR    R2,R2
         TRT   PREVCHAR,TRTSCAN  GET INDEX
         STC   R2,PREVINDX   SAVE IT
         ST    R7,@OUTLEN    UPDATE LENGTH
         SPACE 1
         CH    R7,SPLITLEN   ALREADY IN THERE ?
         BE    MAINFLFS      YES; LOOK FOR SPLIT POINT
         MVC   SPLITEND(SPLITTER-SPLITEND),SPLITTWO  PUSH THE STACK
         ST    R7,SPLITTER   SAVE CURRENT POSITION + 0 FLAG
MAINFLFS C     R7,@OUTMAX    WILL THIS LINE FIT ?
         BL    MAINFITS      YES
         LM    R15,R1,@SPLIT  GET BXH POINTERS
         SR    R3,R3
MAINFSLP ICM   R3,3,SPLITLEN-SPLITTER(R15)  LOAD AND TEST NEXT VALUE
         BNP   MAINBOMB      OOPS
MAINFSLM C     R3,@OUTMAX    WILL IT FIT ?
         BNH   MAINFSPL      YES; SPLIT HERE
         BXH   R15,R0,MAINFSLP  ELSE LOOK AGAIN
MAINBOMB ABEND 666,DUMP      LINE TOO BIG FOR OUTPUT
         SPACE 1
MAINFSPL CLI   SPLITFLG-SPLITTER(R15),0  NORMAL SEPARATOR?
         BE    MAINFSPC      YES
         LA    R15,OUTLINE-1(R3)  WHERE THE COMMA GOES
         MVI   0(R15),C','   RESTORE THE COMMA
MAINFSPC BAS   R14,PUTTER    WRITE OUTPUT LINE WITH R3'S LENGTH
         LR    R14,R6        GET DESTINATION
         LR    R15,R7        NEW OUTPUT LENGTH
         LA    R2,0(R3,R6)   NEW START MOVE
         CLI   0(R2),C';'    NEW LEADING SEPARATOR ?
         BNE   MAINFNSP      NO
         LA    R3,1(,R3)     SKIP OVER IT
MAINFNSP LA    R0,0(R3,R6)   NEW START MOVE
         LR    R1,R7         TOTAL LENGTH
         SR    R1,R3         LENGTH TO MOVE
         LR    R7,R1         UPDATE CURRENT LENGTH
         ST    R7,@OUTLEN    NEW OUTPUT LENGTH
         ICM   R1,8,BLANKS   REQUEST BLANK FILL
         MVCL  R14,R0        MOVE WITH FILL
         LM    R15,R1,@SPLIT
         SR    R14,R14
MAINFTLP ICM   R14,3,SPLITLEN-SPLITTER(R15)  LOAD AND TEST NEXT VALUE
         SR    R14,R3        ADJUST AFTER MOVE
         BP    MAINFTPL      DO MORE
         SR    R14,R14
         STH   R14,SPLITFLG-SPLITTER(,R15)  RESET COMMA SWITCH ALSO
MAINFTPL STH   R14,SPLITLEN-SPLITTER(,R15)  UPDATE INDENTATION
         BXH   R15,R0,MAINFTLP  UPDATE ANOTHER
         B     INPUTEND      GET NEXT RECORD
EXMVCTXT MVC   0(0,R6),0(R15)  SLIDE TEXT OVER
         SPACE 1
MAINFITS CLI   CHARQUOT,0    IN A QUOTED STRING ?
         BNE   MAINNULL      YES; CONTINUE PACKING
         CLI   PREVCHAR,C','  TRAILING COMMA
         BNE   MAINNCOM      NO
         LM    R6,R7,@OUTLINE
MAINNCSP LA    R15,0(R7,R6)  END
         BCTR  R15,0         LAST BYTE
         MVI   1(R15),C' '   ELIMINATE IT
         ST    R7,@OUTLEN    UPDATE
         MVI   SPLITFLG,C','   SET COMMA FLAG
         B     MAINNULL      DO IT AGAIN
         SPACE 1
MAINNCOM CLI   PREVCHAR,C';' ENDING SEPARATOR?
         BE    MAINNULL      YES; NOTHING MORE ON THIS LINE
         LM    R6,R7,@OUTLINE
         CH    R7,SPLITLEN   REPEAT ?
         BE    MAINNNEW
         ST    R7,SPLITTER   SAVE CURRENT POSITION
MAINNNEW LA    R15,0(R7,R6)  END
         LTR   R4,R4         INPUT END FILE AND NO MORE DATA ?
         BZ    MAINNULL      YES; DON'T APPEND COLON
         MVI   0(R15),C';'   MAKE STATEMENT SEPARATOR
         LA    R7,1(,R7)     UPDATE LENGTH
         ST    R7,@OUTLEN    SAVE
         B     MAINNULL      DO IT AGAIN
         SPACE 1
MAINNULL TM    PROFLAGS,PFINZ  INPUT END FILE ?
         BZ    MAINLOOP      NO; GET ANOTHER RECORD
         ICM   R7,15,@OUTLEN  ANY MORE TO DO ?
         BNP   PGMEXIT       NO; ALL DONE
         C     R7,@OUTMAX    FITS IN SINGLE RECORD ?
         BH    INPUTEOF      YES; DO IT AGAIN
         LR    R3,R7         SET LENGTH
         BAS   R14,PUTTER    WRITE THE LAST LINE
         SPACE 2
PGMEXIT  CLOSE MODE=31,MF=(E,LISTDCB)  CLOSE BOTH FILES
         PRTLIST MSGPGTRM    WRITE END MESSAGE
         SERVTERM ,          CLOSE AND CLEAN
         PGMEXIT COPYRET=(CONDCODE,8)  RETURN R15 AND R0 CODES
         SPACE 2
FAILINP  LA    R2,=CL8'REXXIN'
         B     FAILIOCM
FAILOUT  LA    R2,=CL8'REXXOUT'
FAILIOCM MVC   CONDCODE,=F'16'  SEVERE ERROR
         MVC   REASCODE,=F'13'  REASON: MISSING FILE
         PRTLIST MSGBDDDN    SHOW WHAT
         B     PGMEXIT       AND QUIT
         SPACE 1
MSGBDDDN FDPRT '*** REQUIRED DD CARD',NL
         FDPRT 0(R2),8,DEB,PAD
         FDPRT 'MISSING OR NOT USABLE ***'
         FDPRT *END
         SPACE 1
MSGPGTRM FDOPT CC=C'0',NL
         FDPRT '>>> REXSTRIP ENDED',NL
         FDCLC CONDCODE,ZEROES,BE=MSGPGTR0
         FDPRT 'WITH CONDITION CODE',PAD
         FDPRT CONDCODE,I,PADL
         FDPRT ', REASON CODE'
         FDPRT REASCODE,I,PADL
MSGPGTR0 FDPRT ', INPUT COUNT:'
         FDPRT NUMINP,I,DEB,PADL
         FDPRT ', OUTPUT COUNT'
         FDPRT NUMOUT,I,DEB,PADL
         FDPRT '<<<',PADL
         FDPRT *END
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*  GETCARD - ROUTINE TO READ AN INPUT RECORD FROM DD REXXIN           *
*                                                                     *
*    R14 - RETURN ADDRESS                                             *
*    TEXT ADDRESS STORED IN @INPLINE                                  *
*    TEXT LENGTH IN @INPLINE+4                                        *
*                                                                     *
*  ON END OF FILE, TEXT/LEN BOTH SET TO ZERO.                         *
*    A CALL AFTER END-FILE RESULTS IN AN ABEND U0014                  *
*                                                                     *
*---------------------------------------------------------------------*
         PUSH  USING
GETCARD  SUBHEAD ,           SAVE REGISTERS
         USING IHADCB,REXXIN  DECLARE DCB MAPPING FOR INPUT
         TM    PROFLAGS,PFINZ  PRIOR END-FILE ?
         BZ    GETCGET       NO; GET A RECORD
         ABEND 014,DUMP      TOO BAD
         SPACE 1
*---------------------------------------------------------------------*
*  END OF FILE ON INPUT                                               *
*---------------------------------------------------------------------*
GETCEOD  OI    PROFLAGS,PFINZ  SET EOF FLAG
         ICM   R0,15,NUMCOMM   UNCLOSED COMMENTS?
         BZ    GETCEODC      NO
         ST    R0,DB
         PRTLIST MSGBDCMT    MESSAGE ABOUT UNCLOSED COMMENTS
         OI    CONDCODE+L'CONDCODE-1,12  SET ERROR
         MVC   REASCODE,=F'111'  AND REASON CODE
         XC    NUMCOMM,NUMCOMM
         SPACE 1
GETCEODC CLI   CHARQUOT,0    UNFINISHED QUOTE ?
         BE    GETCEODQ      NO
         OI    CONDCODE+L'CONDCODE-1,12  SET ERROR
         MVC   REASCODE,=F'112'  AND REASON CODE
         LM    R6,R7,@OUTLINE  GET OUTPUT BUFFER
         LA    R15,0(R7,R6)  NEXT BYTE
         MVC   0(1,R15),CHARQUOT  CLOSE QUOTE MANUALLY
         LA    R7,1(,R7)     UPDATE LENGTH
         ST    R7,@OUTLEN    AND SAVE
         PRTLIST MSGBDQUO    INFORM USER
         MVI   CHARQUOT,0    IT'S CLOSED NOW
         SPACE 1
GETCEODQ SR    R6,R6
         SR    R7,R7
         B     GETCARDX      RETURN
         SPACE 1
MSGBDCMT FDPRT '*** END OF FILE ENCOUNTERED, BUT',NL
         FDPRT DB,4,I,DEB,PAD
         FDPRT 'COMMENT LEVELS STILL OPEN ***'
         FDPRT *END
         SPACE 1
MSGBDQUO FDPRT '*** END OF FILE ENCOUNTERED, BUT',NL
         FDPRT CHARQUOT,PAD
         FDPRT 'QUOTED STRING STILL OPEN ***'
         FDPRT *END
         SPACE 2
*---------------------------------------------------------------------*
*  GET NEXT RECORD                                                    *
*---------------------------------------------------------------------*
GETCGET  GET   REXXIN        GET A RECORD
         INC   NUMINP        COUNT INPUT RECORDS
         LR    R6,R1         PRESERVE INPUT ADDRESS
         LH    R7,DCBLRECL   GET ITS LENGTH
         AH    R6,@INPOFF    ADJUST ADDRESS FOR RDW, CC
         SH    R7,@INPLEN      DITTO FOR LENGTH
         BNP   GETCGET       SKIP NULL RECORD (?)
         TM    PROFLAGS,PFDETINP  PRINT DETAIL ?
         BZ    GETCNDET      NO
         PRTLIST MSGINCRD    ECHO INPUT RECORD
GETCNDET TM    PROFLAGS,PFIN1  FIRST TIME HERE ?
         BNZ   GETCARDX      NO; DATA READY
         SR    R3,R3         DATA OFFSET
         SR    R4,R4         LENGTH ADJUST (TRAILING SEQUENCE NUMBERS)
         TM    DCBRECFM,DCBRECCA+DCBRECCM  CARRIAGE CONTROL ?
         BZ    GETCCC        NO
         LA    R3,1(,R3)     SKIP CC
         LA    R4,1(,R4)     DITTO IN LENGTH
GETCCC   TM    DCBRECFM,DCBRECU  UNDEFINED ?
         BO    GETCFIX       YES; TREAT AS FIXED
         TM    DCBRECFM,DCBRECF  FIXED ?
         BNZ   GETCFIX       YES
         LA    R3,4(,R3)     ADJUST START FOR RDW
         LA    R4,4(,R4)     DITTO LENGTH
         LA    R15,0(R3,R6)  POINT TO START OF DATA
         TRT   0(8,R15),TRTDIGIT  SEQUENCE NUMBER ?
         BNZ   GETC1COM      NO; DONE
         LA    R3,8(,R3)     SKIP SEQUENCE NUMBER
         LA    R4,8(,R4)     AND FIX LENGTH
         B     GETC1COM      DONE
         SPACE 1
GETCFIX  LA    R0,8          CONSTANT FOR SEQUENCE FIELD LENGTH
         LA    R15,0(R7,R6)  END OF RECORD
         SR    R15,R0        BACK UP TO POTENTIAL SEQUENCE FIELD
         TRT   0(8,R15),TRTDIGIT  IS IT NUMERIC ?
         BNZ   GETC1COM      NO
         AR    R4,R0         DECREASE LENGTH (SURE HOPE IT'S NOT DATA)
         SPACE 1
GETC1COM STH   R3,@INPOFF    SET ADDRESS OFFSET
         STH   R4,@INPLEN    SET LENGTH ADJUST
         OI    PROFLAGS,PFIN1  SET FIRST TIME STUFF DONE
         AR    R6,R3         START DATA
         SR    R7,R4         ADJUSTED LENGTH
         BNP   GETCASIS      NULL TEXT - ERROR - DO AS IS
         SPACE 1
*---------------------------------------------------------------------*
*  SEARCH THE FIRST CARD FOR /* COMMENT START, AND 'REXX' INDENTIFIER *
*    SOMEWHERE AFTER IT, BEFORE CLOSING */                            *
*---------------------------------------------------------------------*
         LR    R15,R6        START SCAN ADDRESS
         LA    R0,1          SET SCAN INCREMENT
         LR    R1,R7         GET TEXT LENGTH
         S     R1,=F'6'      LAST POSSIBLE COLUMN ADDRESS
         BM    GETCASIS      NOT ENOUGH ROOM FOR /*REXX
         AR    R1,R6         MAKE END SCAN ADDRESS
GETC1SLA CLC   =C'/*',0(R15)  COMMENT START ?
         BE    GETC1SCM      YES; NOW SCAN FOR REXX BEFORE */
         BXLE  R15,R0,GETC1SLA  ELSE TRY AGAIN
         B     GETCASIS      NOT REXX ?
GETC1SCM ICM   R14,15,2(R15)  GET NEXT FOUR
         O     R14,BLANKS    MAKE UPPER CASE
         CLM   R14,15,=C'REXX'  LANGUAGE IDENTIFIER ?
         BE    GETCARDX      YES; DONE
         CLC   =C'*/',2(R15)  END OF COMMENT ?
         BE    GETC1SLA      YES; TRY FOR ANOTHER COMMENT
         BXLE  R15,R0,GETC1SCM  ELSE TRY FOR REXX STRING
GETCASIS OI    PROFLAGS,PFASIS  NOT REXX; COPY AS IS
         LTR   R7,R7         NULL LINE ?
         BNP   GETCGET       YES; GET NEXT LINE
         SPACE 1
GETCARDX STM   R6,R7,@INPLINE   SAVE ADDRESS/LENGTH
         SUBRET ,            AND RETURN TO CALLER
         SPACE 1
MSGINCRD FDPRT '    ',NL
         FDPRT 0(R6),(R7)    PRINT RECORD TEXT
         FDPRT *END
         POP   USING
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*  PUTLINE - WRITE A RECORD TO DD REXXOUT FROM OUTLINE                *
*                                                                     *
*    R14 - RETURN ADDRESS                                             *
*    R 0 - LENGTH TO BE WRITTEN  (R3 AT ENTRY PUTTER)                 *
*                                                                     *
*---------------------------------------------------------------------*
         PUSH  USING
PUTTER   LR    R0,R3         COPY LENGTH
PUTLINE  SUBHEAD ,           SAVE A LITTLE
         USING IHADCB,REXXOUT  DECLARE OUTPUT FILE
         LTR   R3,R0         DID USER PASS A VALID LENGTH ?
         BNP   PUTLINEX      NO; JUST RETURN
         TM    PROFLAGS,PFDETOUT  PRINT DETAILS ?
         BZ    PUTLNDET      NO
         PRTF  OUTLINE,(R3),CC=NO  COPY OUTPUT LINE TO SYSPRINT
PUTLNDET INC   NUMOUT        INCREASE OUTPUT COUNT
         LA    R2,OUTLINE    POINT TO OUTPUT RECORD
         TM    DCBRECFM,DCBRECCA+DCBRECCM  CARRIAGE CONTROL ?
         BZ    PUTLNCC       NO
         LA    R3,1(,R3)     UP LENGTH
         BCTR  R2,0          NEW LINE START
PUTLNCC  TM    DCBRECFM,DCBRECU  UNDEFINED ?
         BO    PUTLSET       YES; UPDATE LRECL
         TM    DCBRECFM,DCBRECF  FIXED ?
         BNZ   PUTLFIX       YES; DONE
         LA    R3,4(,R3)     INCREASE LENGTH
         S     R2,=F'4'      BACKSPACE TO RDW
         STH   R3,0(,R2)     NEW RDW
PUTLSET  STH   R3,DCBLRECL   UPDATE RECORD LENGTH
PUTLFIX  PUT   REXXOUT       PUT LOCATE MODE
         LR    R0,R1         COPY ADDRESS
         LH    R1,DCBLRECL   GET LENGTH
         ICM   R3,8,BLANKS   REQUEST BLANK FILL
         MVCL  R0,R2         MOVE TO BUFFER
PUTLINEX SUBRET ,            RETURN TO CALLER
         POP   USING
         SPACE 1
         LTORG ,
         SPACE 2
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=5  DEFINE PRINT FILE
         SPACE 1
*  PATTERN DATA TO BE COPIED TO DYNAMIC WORK AREA
PATLIST  OPEN  (0,(INPUT,REREAD),0,OUTPUT),MODE=31,MF=L  DCB OPEN LIST
PATINP   DCB   DDNAME=REXXIN,DSORG=PS,MACRF=GL,EODAD=GETCEOD INPUT
PATOUT   DCB   DDNAME=REXXOUT,DSORG=PS,MACRF=PL (EXLST=OUTEXLST) OUT DD
PATEXPRM DCBEXITD DSECT=NO,PREFIX=PT,                                  *
               FLAG1=PT1FULLT+PT1TRUNC,   HALF/FULL TRACK BLOCKING     *
               FLAG3=PT3BLKTB,            USE BLOCKSIZE TABLE          *
               DRECFM=X'50',              DEFAULT TO RECFM=VB          *
               LRECLF=80,                 DEFAULT TO LRECL 80 IF FIXED *
               LRECLU=255,                DEFAULT TO LRECL 255 IF UND  *
               LRECLD=251,                DEFAULT TO LRECL 251 IF VARD *
               LRECLV=251                 DEFAULT TO LRECL 251 IF VAR
PATDCBL  EQU   *-PATLIST     LENGTH TO COPY
*              FLAG2=PT2CCADJ,            ADJUST FOR CC/NOCC          *
         SPACE 1
         DS    0F            MALIGN A LITTLE
BLANKS   DC    CL8' '        SPACES
@DCBEXIT DC    0CL8'@DCBEXIT',C'@'  1/2  @DCBEXIT ROUTINE
@DCBEXID DC    C'DCBEXITP'          2/2  EXIT LIST IDENTIFIER
         SPACE 1
TRTDIGIT DC    256X'08'      STOP ON NON-DIGIT
         TRENT TRTDIGIT,0,(C'0',10)  ALLOW DIGITS
         ORG   ,
         SPACE 1
*  SCAN TABLE - NOTE THAT VALUES MATCH MAINBRCH OFFSETS
*
TRTSCAN  DC    256X'00'      EVERYTHING UNINTERESTING
         TRENT TRTSCAN,4,C''''      STOP ON APOSTROPHE (SINGLE QUOTE)
         TRENT ,4,C'"'              STOP ON QUOTE
         TRENT ,8,C' '              STOP ON SPACE
         TRENT ,12,C'/'   SLASH 1) DIVIDE; 2) POSSIBLE COMMENT START
         TRENT ,16,C'+',C'-',C'\',C'%',C'*',C'|',C'&&'  SPECIAL SET 1
         TRENT ,16,C'=',C'<',C'>',C'¬',C',',C':'     SPECIAL SET 2
         TRENT ,20,C';'             STATEMENT END
         ORG   ,             JUST IN CASE
         SPACE 1
TRTNBLNK DC    256X'04'      FIND A NON-BLANK
         TRENT TRTNBLNK,0,C' '  SKIP SPACES
         ORG   ,
         SPACE 1
VTITLE   FDOPT NL,CC=C'#'    TITLE DEFINITION W/DATE, TIME, AND PAGE #
         FDPRT 'REXX COMPRESSION PROGRAM',RADJ,LEN=43
         FDPRT *END
         SPACE 1
SAVE     DSECT ,
LOCSAVE  DS    18A           PUSH DOWN STACK
LOCSAVE1 DS    18A           PUSH DOWN STACK
LOCSAVE2 DS    18A           PUSH DOWN STACK
LOCSAVE3 DS    18A           PUSH DOWN STACK
LOCSAVE4 DS    18A           PUSH DOWN STACK
LOCSAVE5 DS    18A           PUSH DOWN STACK
         SPACE 1
SAVEWORK DS    0D            START OF COMMONLY ADDRESSABLE WORK AREA
ZEROES   DS    D             CONSTANT HEX ZEROES
DB       DS    2D            WORK AREA
@PRINTER DS    A             ADDRESS OF PRINTER SERVICE
@SERVICE DS    A             ADDRESS OF SERVICES
@OUTRDW  DS    A     1/4     ADDRESS OF OUTPUT RDW/CC/TRC/DATA
@OUTLINE DS    A     2/4     ADDRESS OF OUTPUT DATA
@OUTLEN  DS    A     3/4     LENGTH OF OUTPUT DATA
@OUTMAX  DS    A     4/4     MAXIMUM OUTPUT DATA LENGTH
@INPLINE DS    2A            ADDRESS/LENGTH OF CURRENT INPUT RECORD
@INPOFF  DS    H             OFFSET TO DATA IN INPUT RECORD
@INPLEN  DS    H             LENGTH ADJUSTMENT FOR INPUT DATA
PREVPTR  DS    2A
NUMCOMM  DS    F             COMMENT LEVEL
NUMINP   DS    F             NUMBER OF INPUT RECORDS
NUMOUT   DS    F             NUMBER OF OUTPUT RECORDS
CONDCODE DS    F     1/2     CONDITION CODE
REASCODE DS    F     2/2     REASON CODE
         SPACE 1
*   DATA COPIED AS IS FROM PATLIST :
LISTDCB  OPEN  (0,(INPUT,REREAD),0,OUTPUT),MODE=31,MF=L  DCB OPEN LIST
REXXIN   DCB   DDNAME=REXXIN,DSORG=PS,MACRF=GL,EODAD=GETCEOD   INPUT
REXXOUT  DCB   DDNAME=REXXOUT,DSORG=PS,MACRF=PL (EXLST=OUTEXLST) OUT DD
OUTEXPRM DCBEXITD DSECT=NO,PREFIX=OX
OUTEXLST DS    2A(0)         OUTPUT DCB EXIT LIST
         SPACE 1
PROFLAGS DS    X             PROCESSING FLAGS
PFASIS   EQU   X'80'           MISSING /*REXX IN FIRST LINE; JUST COPY
PFDETAIL EQU   X'60'           PRINT DATA LINES IN AND OUT
PFDETINP EQU   X'40'           PRINT DATA LINES IN
PFDETOUT EQU   X'20'           PRINT DATA LINES OUT
PFINZ    EQU   X'02'           INPUT END-FILE HIT
PFIN1    EQU   X'01'           FIRST RECORD SCANNED
CHARQUOT DS    C               X'00' OR CURRENT QUOTE CHARACTER
PREVCHAR DS    C               PRIOR SPECIAL CHARACTER OR X'00'
PREVINDX DS    X               MAINBRCH INDEX FOR ABOVE CHARACTER
WORK256  DS    XL256         AD HOC TRT WORK AREA
         SPACE 1
@SPLIT   DS    3A            BXH POINTERS FOR SPLIT PROCESSING
SPLITEND DS    F             END OF STACK
SPLITTWO DS    10F           PUSHDOWN STACK FOR SPLIT POINTERS
SPLITTER DS    H,H           START OF STACK FOR SPLITTING LINES
SPLITFLG EQU   SPLITTER,1,C'C'  0 OR , FOR TRAILING CHARACTER
SPLITLEN EQU   SPLITTER+2,2,C'H'  END TEXT OFFSET IN OUTLINE
         SPACE 1
OUTRDW2  DS    X             RDW START FOR V(B), D(R) W/CC AND TRC
OUTRDW1  DS    X             RDW START FOR V(B), D(R) W/CC OR TRC
OUTRDW   DS    Y,Y           RDW FOR PLAIN V/D
OUTLINE  DS    CL256         MAXIMUM SUPPORTED OUTPUT LINE
         DS    CL256           OVERFLOW FOR SLOPPY, LAZY CODING)
         DS    CL256           OVERFLOW FOR SLOPPY, LAZY CODING)
         DS    CL256           OVERFLOW FOR SLOPPY, LAZY CODING)
OUTSIZE  EQU   *-OUTLINE
SAVEEND  EQU   *
         SPACE 1
         PRINT &PRTSYS
         CVT   DSECT=YES
         IHACDE ,
         DCBD  DSORG=PS,DEVD=DA
         END   ,
