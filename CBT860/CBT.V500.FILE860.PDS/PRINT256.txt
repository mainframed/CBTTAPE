PRINT256 TITLE 'P R I N T E R  ***  PRINT/PUNCH 256 CHARACTERS'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
***********************************************************************
**                                                                   **
**  PRINTER PRINTS OR PUNCHES 256 LINES OF DATA:                     **
**                                                                   **
**  PRINTABLE HEX VALUE (X'00' - X'FF')                              **
**  SPACE                                                            **
**  CHARACTER X'00' - X'FF'                                          **
**                                                                   **
**  SYSPRINT - (OPTION) PRINT DATA SET (NO TRANSLATION)              **
**  SYSPUNCH - (OPTION) PUNCH DATA SET (NO TRANSLATION)              **
**                                                                   **
**  PARM=NOCC           PROCESS ONLY X'40' - X'FF'                   **
**  PARM=NOESC          PROCESS X'00' - X'1A'; X'1C' - X'FF'         **
**                                                                   **
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 2005  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 2
         PRINT &PRTSOR
PRINT256 PGMHEAD ZERO12,BASE=R12,PARM=R9
*DEFER*  BANDAID INIT
         SERVINIT ,
         SERVLOAD @PRINTER,@PARSER,SUBVERB                      GP12166
         #OPMSG 'PRINT256',TYPE=INIT (FOR OPTIONAL WTO MSGS)
         PRTOPEN SYSPRINT,OPT=(NOWTO,DUMMY)  OPTIONAL
         PRTOPEN SYSPUNCH,OPT=(NOWTO,DUMMY),DEV=2
         PARMLOAD R8,PTR=R9                                     GP12166
         OI    PAR$RQFG,PAR$COUP  UPPER-CASE ALL INPUT          GP12166
         PARSE (R8),(R9),OPT=KEYWORD  IGNORE BLANKS AND COMMAS  GP12166
         BXH   R15,R15,PARMDONE   NOTHING FOUND                 GP12166
         LA    R3,PAR@TABL   TOP OF RESULTS                     GP12166
         USING PRSDSECT,R3   DECLARE RESULT MAPPING             GP12166
LOOPPARM ICM   R3,15,PRSLINK      ANY TEXT FOUND ?              GP12166
         BZ    PARMDONE             NO; GET OUT                 GP12166
         TM    PRSTYPE,PRSPFKEY   USER SPECIFIED KEYWORD?       GP12166
         BNZ   MSGBDKEY             YES; FAIL                   GP12166
         PARFGSET FLAGSETP,ERR=FLAGBAD,DONE=LOOPPARM            GP12166
         PARKEYBX FLAGSET    BXLE                               GP12166
FLAGSET  PARKEYFG 'NOCC',PROFLAGS,0,FGNOCON                     GP12166
FLAGSET2 PARKEYFG 'NOCONTROL',PROFLAGS,0,FGNOCON                GP12166
         PARKEYFG 'NOESCAPE',PROFLAGS,0,FGNOESC                 GP12166
         PARKEYFG 'NO1B',PROFLAGS,0,FGNOESC                     GP12166
         PARKEYFG 'NOFF',PROFLAGS,0,FGNOFF                      GP12166
         PARKEYFG 'DEBUG',PROFLAGS,0,FGDEBUG                    GP12166
FLAGSETN PARKEYFG 'TEST',PROFLAGS,0,FGDEBUG                     GP12166
         SPACE 1                                                GP12166
FLAGBAD  PRTDATA 'Unknown PARM option',(PRS$TEXT,DEB,PADL)      GP12166
         B     EXIT16                                           GP12166
MSGBDKEY PRTDATA 'Unknown PARM keyword',(PRSKEYWD,DEB,PADL)     GP12166
EXIT16   MVICC 16            NASTY ERROR                        GP12166
         B     PGMEXIT                                          GP12166
         SPACE 1
*---------------------------------------------------------------------*
*  CHECK FOR OPTIONAL TITLE FILE                                      *
*---------------------------------------------------------------------*
PARMDONE LA    R4,1          SET INCREMENT
         LA    R5,X'FF'      SET END VALUE
         SR    R3,R3         START VALUE
         SR    R6,R6
         TM    PROFLAGS,FGNOCON   SKIP CONTROL CHARS 00-3F ?    GP12166
         BZ    HAVECON                                          GP12166
         LA    R3,X'40'      START LOOP WITH SPACE              GP12166
HAVECON  TM    PROFLAGS,FGNOFF    SKIP FF ?                     GP12166
         BZ    PUTLOOP              NO; START LOOP              GP12166
         LA    R5,X'FE'           SKIP FF                       GP12166
PUTLOOP  CLM   R3,1,=X'1B'   ESCAPE ?                           GP12166
         BNE   PUTLOOP3        NO; PROCESS                      GP12166
         TM    PROFLAGS,FGNOESC  SUPPRESS ESCAPE ?              GP12166
         BNZ   PUTSKIP         YES; IGNORE ONE                  GP12166
PUTLOOP3 STC   R3,OUTCHAR    STASH THE OUTPUT CHARACTER
 PRTDATA 'Character value',(OUTCHAR,HEX,PAD),'is',(OUTCHAR,CON,PADL),  *
               DEV=(1,2)
         LA    R6,1(,R6)     INCREASE RECORD COUNT
PUTSKIP  BXLE  R3,R4,PUTLOOP  REPEAT UNTIL DONE
         SPACE 1
         ST    R6,DB         SET RECORD COUNT
         #OPMSG '| RECORDS GENERATED',LIST=(DB,DBFMT)
         SPACE 1                                                 78271
PGMEXIT  #OPMSG TYPE=END     CLEANUP
         SERVTERM ,
         PGMEXIT RC=RETCODE  RETURN WITH CODE                   GP12166
         SPACE 1                                                 90280
SYSPRINT PUNWORK SYSPRINT,SYSOUT   FAKE PRINT; INHIBIT CC AND XLATE
SYSPUNCH PUNWORK SYSPUNCH          PUNCH
DBFMT    #FMT  (DB,4),4,TYPE=INT,MF=L
         SPACE 2
         LTORG ,                                                GP04081
         SPACE 2                                                GP04081
SAVE     DSECT
DB       DS    D
         SERVDEFS ,          DEFINE NEWFANGLED STUFF
         SPACE 1
PROFLAGS DS    X                                                GP04081
FGNOESC  EQU   X'80'           DON'T PRINT ESCAPE CHARACTER
FGNOCON  EQU   X'40'           DON'T PRINT CONTROL RANGE 00-3F
FGNOFF   EQU   X'10'           DON'T PRINT FF
FGDEBUG  EQU   X'01'           DEBUG MODE
OUTCHAR  DS    X                                                GP04081
         MAPPARSE DSECT=NO                                      GP12166
SAVEEND  EQU   *
         SPACE 1
         MAPPARST ,                                             GP12166
         END   ,
