RELCAN   TITLE 'J O B R E L S E  ***  RELEASE / CANCEL RELATED JOBS'
         PUNCH ' ALIAS JOBCANCL '   RELEASE OR CANCEL JOBS      GP05035
         PUNCH ' ALIAS JOBPURGE '   PURGE JOBS ON HELD OUTPUT   GP05261
         PUNCH ' ALIAS JOBPRINT '   PRINT JOBS ON HELD OUTPUT   GP05261
         PUNCH '  ORDER JOBRELSE(P) '  FASTER DUMP READING      GP05035
         PUNCH '  SETCODE AC(1) '   FOR JES2 INTERFACE          GP05035
         SPACE 1                                                 81341
         COPY  OPTIONGB                                          81341
         SPACE 2                                                 81341
         SYSPARM LIST=YES    SET AND LIST GLOBAL OPTIONS         81341
         SPACE 1                                                 81341
***********************************************************************
**                                                                   **
**   INPUT - A DATA SET ON SYSIN                                     **
**   EACH CARD CONTAINS ONE JOBNAME TO BE PROCESSED.             81341*
**   THE FIRST FIVE LETTERS OF THE JOB MUST BE THE SAME              **
**   AS THE CURRENT JOB'S.                                       81341*
**                                                               81341*
**   FOR EACH SUCH JOB, THE PROGRAM ISSUES A COMMAND:            81341*
**   (JOBRELSE)  RELEASE or RESTART(*THIS*)                      81341*
**   (JOBCANCL)  CANCEL  or PURGE(*THIS*)                        81341*
**   (JOBPURGE)  CANCEL HELD OUTPUT                             GP05261
**   (JOBPRINT)  PRINT  HELD OUTPUT                             GP05261
**                                                               81341*
**   FOR JOBRELSE IT IS ASSUMED THAT THESE JOBS ARE SUBMITTED    81341*
**   WITH TYPRUN=HOLD.                                           81341*
**                                                               81341*
**   FOR HASP JOBS, THE RELEASE COMMAND MAY BE REPLACED              **
**   BY A WTOR FOR SPECIAL JOB CLASSES AND PRIORITIES.           81341*
**                                                               81341*
**   THE SPECIAL JOBNAME *THIS* (CURRENT JOB) CAUSES A HOLD          **
**   AND RESTART (RELEASE); OR A NOPRINT (LOCAL OPTION) FOR          **
**   CANCEL; OR A PURGE OF HELD OUTPUT FOR SIMILARLY NAMED       81341*
**   JOBS (JOBPURGE)                                                 **
**                                                                   **
**   OUTPUT - PRINTED ON SYSPRINT FOR THE RECORD                     **
**   - ISSUED AS MGCR COMMAND IF RUNNING AUTHORIZED              81341*
**                                                                   **
***********************************************************************
**                                                                   **
**   THIS IS A MAJOR REWRITE OF THE FUNCTION. AN EARLIER, INSTALL-   **
**   ATION DEPENDENT VERSION WAS SAVED AS MVSRELSE, WHICH WAS A      **
**   REWORK OF AN EARLIER MVT VERSION NAMED NIS0RELS                 **
**                                                                   **
***********************************************************************
**                                                                   **
**   COPYRIGHT  1981  EXPERT SYSTEM PROGRAMMING, INC.                **
**   COPYRIGHT  2002,2005  EXPERT SYSTEM PROGRAMMING                 **
**                    176 OLD STAGE COACH ROAD                       **
**                    BRADFORD, VT 05033-8844                        **
**                                                                   **
**     ALL RIGHTS RESERVED                                           **
**                                                                   **
***********************************************************************
         EJECT ,                                                 81341
         PRINT &PRTSOR                                           81341
JOBRELSE PGMHEAD ZERO15,BASE=(R11,R12),PARM=R9,AM=31,RM=24,            *
               ENTRY=(JOBCANCL,JOBPURGE,JOBPRINT),ENTNO=PROFLAGS
         SPACE 1
*---------------------------------------------------------------------*
*                                                                     *
*   PROGRAM    JOBNAME       COMMANDS ISSUED                          *
*                                                                     *
*   JOBRELSE   *THIS*        $HJNNNNN " $EJNNNNN                      *
*   JOBRELSE   JOBN----      $AJNNNNN                                 *
*                                                                     *
*   JOBCANCL   *THIS*        $PJNNNNN                                 *
*   JOBCANCL   JOBN----      $PJNNNNN                                 *
*                                                                     *
*    EITHER    ¬JOBNAME      OPPOSITE OF ABOVE REQUEST                *
*                                                                     *
*   JOBPURGE   *THIS*        $PJNNNNN (O/P JOBS W/SAME NAME)          *
*   JOBPURGE   JOBN----      $PJNNNNN                                 *
*                                                                     *
*---------------------------------------------------------------------*
**                                                                   **
MAXJOBS  EQU   200                     MAX NUMBER OF HOMONYM JOBS
**                                                                   **
***********************************************************************
         SPACE 1                                                 81341
         AIF   (NOT &MVSXA).COMM0                               GP05035
         MVC   WTOLIST(PATWTOLN),PATWTO  MAKE FULL WTO          GP02304
.COMM0   MVC   COMMBUFE(4),=X'40008000' ROUTCDE=1/DESC=2        GP05035
         LA    R0,COMMBUFL   LENGTH OF COMMAND BUFFER           GP02304
         STH   R0,COMMBUFF   MAKE PRTV FORMAT LENGTH            GP02304
         LA    R2,COMMBUFL-4  TEXT LENGTH                       GP02304
         STH   R2,COMMLPFX    SET TEXT LENGTH FOR WTO/MGCRE     GP02304
         IC    R1,PROFLAGS   GET ENTRY FLAGS                    GP05261
         LA    R0,3          CURRENT MAX MASK                   GP05261
         NR    R1,R0         LEAVE ONLY ENTRY BITS              GP05261
         MH    R1,=AL2(PATCANCL-PATRELSE)  MAKE DISPLACEMENT    GP05261
         LA    R2,PATRELSE(R1)  POINT TO ENTRY                  GP05261
         MVC   RUNPGMNM,0(R2)   PROGRAM NAME                    GP05261
         MVC   COMMAND,8(R2)   RELEASE COMMAND / PURGE          GP05261
         MVC   COMTHIS,10(R2)  RESTART COMMAND / PURGE          GP05261
         MVC   COMMBAK,COMMAND  SAVE NORMAL COMMANDS            GP02307
         MVC   COMMINV,12(R2)   SAVE INVERSE COMMANDS FOR 'NOT' GP05261
         MVC   EXQUETST,16(R2)  MOVE TM COMMAND                 GP05261
         OI    PROFLAGS,&BUGBEAR*FGDEBUG    OPT. DEBUG MODE     GP05261
         TESTAUTH FCTN=1     ARE WE AUTHORIZED ?                 81341
         BXH   R15,R15,UNAUTHOR   NO; LEAVE AS IS                81341
         OI    PROFLAGS,FGAUTH  SHOW WE'RE AUTHORIZED FOR MGCR  GP02304
         SPACE 1                                                 81341
UNAUTHOR SERVINIT ,          LOAD THE SERVICE ROUTINE            81341
         SERVLOAD @PRINTER,@INPREAD   LOAD SERVICE ROUTINES     GP05261
         PRTOPEN SYSPRINT,OPT=(DUMMY)    OPEN THE PRINT FILE     81341
         CH    R15,=H'4'     ERROR ?                             81341
         BNH   *+8           DD DUMMY OR GOOD OPEN OK            81341
         OICC  4             SET MINOR ERROR CODE               GP05261
         INPOPEN SYSIN,OPT=(DUMMY)  ABEND IF NO SYSIN SUPPLIED   81341
         BXH   R15,R15,NOSYN   WRITE ERROR MESSAGE IF NO DD      81341
         INPGET ,            GET THE FIRST CARD                  81341
         BXH   R15,R15,NOSYN  TREAT ERROR AS EOF                GP02304
         LTR   R7,R1         SAVE THE ADDRESS                    81341
         BNZ   HAVECRD1      OK                                  81341
*---------------------------------------------------------------------*
*   SYSIN END FILE / I/O ERROR                                        *
*---------------------------------------------------------------------*
NOSYN    TM    PROFLAGS,FGEODAD  SECOND TIME HERE ?             GP02304
         BNZ   INPUTEOF      YES; QUIT                          GP02304
NOSYNONE OI    PROFLAGS,FGEODAD  SET FLAG                       GP02304
         PRTV  NOINPUT       ELSE PRINT ERROR MESSAGE            81341
         TM    RETCODE+3,4   SYSPRINT AVAILABLE ?               GP05261
         MVICC 16            SET MAJOR ERROR                    GP05261
         BZ    EXITERR       YES                                 83083
         WTO   'NEITHER SYSPRINT NOR SYSIN SUPPLIED',ROUTCDE=11  81341
         B     EXITERR       AND QUIT                            83083
         SPACE 1                                                 81341
*---------------------------------------------------------------------*
*  HAVE AT LEAST ONE INPUT CARD - GATHER INFORMATION                  *
*---------------------------------------------------------------------*
         PUSH  USING                                            GP02304
HAVECRD1 OI    PROFLAGS,FGEODAD  SET FLAG                       GP02304
         LTCB  R4,USE=YES    GET MY TCB                         GP02304
         L     R2,TCBTIO     GET THE TIOT                        81341
         MVC   RUNJOBN,TIOCNJOB-TIOT1(R2) SAVE CURRENT JOB NAME  81341
         ICM   R15,15,TCBJSCB    GET JSCB                       GP99026
         BZ    BADSJB        OR SKIP                             83254
         USING IEZJSCB,R15                                       83254
         ICM   R15,15,JSCBSSIB  GET SUBSYSTEM INFORMATION BLOCK  83254
         BZ    BADSJB        OR OUT                              83254
         USING SSIBEGIN,R15                                      83254
         CLC   =C'SSIB',SSIBID  REALLY SSIB ?                   GP02304
         BNE   BADSJB        NO                                 GP02304
         AIF   (NOT &MVSSP).SKIPID  NOT IN MVS 3.8J             GP05185
         CLI   SSIBSSID,SSIBJES2  JES2 SYSTEM ?                 GP02304
         BNE   BADSJB        NO                                 GP02304
.SKIPID  ICM   R14,15,SSIBSUSE  GET SJB ADDRESS                  83254
         BZ    BADSJB                                            83254
         MVC   RUNJOBID,SSIBJBID  COPY JOB ID                   GP02304
         PRTLIST TITLE,TITLE=1   WRITE THE PROGRAM HEADER        83083
         B     HAVECARD      SKIP FIRST READ                     81341
         SPACE 1                                                GP02304
BADSJB   PRTL  '***** ERROR LOCATING SSIB *****',CC=NO          GP02304
         SERVTERM ,                                             GP02304
         ABEND 1234,DUMP                                        GP02304
         POP   USING                                            GP02304
         EJECT ,
***********************************************************************
**                                                                   **
**  NEXTCARD - LOOP THROUGH INPUT AND PROCESS REQUESTS               **
**                                                                   **
***********************************************************************
NEXTCARD INPGET ,            GET ANOTHER CARD                    81341
         BXH   R15,R15,NOSYN  TREAT ERROR AS EOF                GP02304
         LTR   R7,R1         COPY THE CARD ADDRESS AND TEST IT   81341
         BZ    INPUTEOF      NONE; ALL DONE                      81341
HAVECARD MVC   CARD,0(R7)    MOVE CARD TO PRINT LINE             81341
         PRTLIST MSGCARD     ECHO THE INPUT CARD                GP02304
         BAL   R14,VALIDATE  VALIDITY CHECK THE NAME            GP02304
         BNZ   NEXTCARD      GET ANOTHER CARD                   GP02304
         TM    PROFLAGS,FGHOLD  HOLD REQUESTED ?                GP02307
         BZ    ISSUECMD      NO                                 GP02307
         TM    PROFLAGS,FGPURGE  PURGE MODE ?                   GP05261
         BNZ   ISSUECMD      NO                                 GP05261
         BAL   R14,DOCOMND   ISSUE $HA                          GP02307
         MVC   COMMCMD,COMTHIS  RESTORE $E                      GP02307
ISSUECMD BAL   R14,DOCOMND   DO COMMAND PROCESSING               81341
         B     NEXTCARD      AND GO TO READ NEXT CARD           GP02304
         SPACE 2
***********************************************************************
**                                                                   **
**   JOB TERMINATION / END OF SYSIN                                  **
**                                                                   **
***********************************************************************
INPUTEOF PRTL  '0---- END OF FILE ON SYSIN ----'                GP02304
EXITERR  SERVTERM ,          CLOSE AND FREE EVERYTHING           81341
         L     R2,RETCODE    LOAD THE RETURN CODE                81341
         PGMEXIT RC=(R2)     QUIT WITH CONDITION CODE           GP02307
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*  VALIDATE THE INPUT JOB NAME                                        *
*                                                                     *
*  BUILD APPROPRIATE COMMAND                                          *
*                                                                     *
*---------------------------------------------------------------------*
VALIDATE STM   R0,R15,SUBSAVE1  SAVE A LITTLE                   GP02304
         NI    PROFLAGS,255-FGHOLD-FGNOT                         81341
         MVC   COMCLEAR,BLANKS  CLEAR WORK BUFFER               GP02307
         MVC   COMMAND(L'COMMBAK),COMMBAK  RESTORE COMMANDS     GP02307
         MVC   COMMCMD,COMMAND  AND SET 'NORMAL' COMMAND        GP02307
         LA    R1,CARD       GET FIRST BYTE                      81341
         TRT   CARD(72-8),TRTNONBK  FIND A NON-BLANK            GP02304
         BZ    VALIDCOM      TREAT AS SPACER                    GP02304
         LR    R3,R1         SAVE START                         GP02304
         CLI   0(R3),C'¬'    'NOT' COMMAND ?                    GP02307
         BNE   VALIDNNT      NO                                 GP02307
         LA    R3,1(,R3)     SKIP OVER IT                       GP02307
         OI    PROFLAGS,FGNOT  'NOT' MODE                       GP02307
         MVC   COMMAND(L'COMMINV),COMMINV  SHOW INVERSE         GP02307
VALIDNNT TRT   0(9,R3),TRTJOBNM  LOOK FOR BLANK OR OTHER END    GP02304
         BZ    VALIDERR      MISSING TERMINAL BLANK             GP02304
         CLM   R2,1,TRTJOBNM+C'*'  POSSIBLE *THIS ?             GP02304
         BE    VALIDHIS      MAYBE - CHECK IT                   GP02304
         CLM   R2,1,TRTJOBNM+C' '  NORMAL BLANK STOPPER ?       GP02304
         BNE   VALIDERR      NO; ERROR                          GP02304
         SR    R1,R3         GET LENGTH                         GP02304
         BZ    VALIDERR      NAME IS MISSING                    GP02304
         CLI   0(R3),C'0'    NUMERIC START ?                    GP02304
         BNL   VALIDERR      YES; INVALID                       GP02304
         BCTR  R1,0          LENGTH FOR EXECUTE                 GP02304
         MVI   COMMQUO1,C''''  MAKE LEADING QUOT                GP02304
         EX    R1,EXMVCJOB   MOVE JOB NAME FROM CARD TO WORK AREA
         MVC   ARGSSCS,COMMJOBN   SET LOOK-UP ARGUMENT          GP05261
         AIF   ('&LOCAL' EQ 'BSP1' OR '&LOCAL' EQ 'MVS').NOCHCK GP05261
         CLC   RUNJOBN(5),COMMJOBN   MATCHING PREFIX ?          GP02304
         BNE   VALIDERR                                         GP02304
.NOCHCK  BAL   R14,JOBLOOK   LOOK AT JES2 QUEUE FOR JOB         GP02307
         CH    R15,=H'4'     CHECK RETURN                       GP02307
         BL    VALIDCLR      FOUND - USING JOB ID               GP02307
         BH    VALIDNAM      ERROR OR UNAUTHORIZED - USE NAME   GP02307
         PRTLIST MSGNOJOB    SHOW JOB NOT FOUND                 GP02307
         B     VALIDCOM      AND RETURN WITH IGNORE             GP02307
VALIDNAM LA    R14,COMMJOBN+1(R1) POINT PAST NAME               GP02304
         MVI   0(R14),C''''  MAKE ENDING QUOTE                  GP02304
         B     VALIDOUT                                         GP02307
VALIDCLR MVC   COMMQUO2-1(2),BLANKS  CLEAR 8-CHAR JOB NAME
VALIDOUT SR    R15,R15       SET CC                             GP02304
         LM    R0,R14,SUBSAVE1  RESTORE                         GP02304
         BR    R14           RETURN TO CALLER                   GP02304
EXMVCJOB MVC   COMMJOBN(0),0(R3)  MOVE NAME FROM CARD           GP02304
         SPACE 1                                                GP02304
VALIDHIS CLC   =C'THIS*',1(R3)  REQUEST FOR CURRENT JOB ?       GP02304
         BNE   VALIDCOM      NO; TREAT AS COMMENT               GP02304
         MVC   ARGSSCS,RUNJOBN   APPLY TO CURRENT JOB           GP02304
         TM    PROFLAGS,FGPURGE  OUTPUT PURGE MODE ?            GP05261
         BZ    VALNPURG      NO                                 GP05261
         TM    PROFLAGS,FGNOT  REVERSED MODE ?                  GP05261
         BZ    VALIDLUP      NO; USE $P                         GP05261
         MVC   COMMCMD,COMTHIS  USE ALTERNATE COMMAND           GP05261
VALIDLUP BAL   R14,JOBLOOK   LOOP THROUGH MATCHING JOBS         GP05261
         B     VALIDCOM      RETURN WITH NO OTHER ACTION        GP05261
         SPACE 1
VALNPURG MVC   COMMCMD,COMTHIS  USE ALTERNATE COMMAND           GP02304
         MVC   COMMJOBI,RUNJOBID  APPLY TO CURRENT JOB          GP02304
         TM    PROFLAGS,FGNOT  REVERSED MODE ?                  GP02307
         BNZ   VALIDHOT                                         GP02307
         TM    PROFLAGS,FGCANCEL  CANCEL MODE ?                 GP02307
         BNZ   VALIDOUT      YES; DON'T WANT HOLD               GP02307
         B     VALIDHLD      NO; HOLD BEFORE RESTART            GP02307
VALIDHOT TM    PROFLAGS,FGCANCEL  CANCEL MODE ?                 GP02307
         BZ    VALIDOUT      NO; JUST CANCEL                    GP02307
VALIDHLD OI    PROFLAGS,FGHOLD  YES; SET HOLD                    85284
         MVC   COMMCMD,=C'$H'  REQUEST HOLD                     GP02307
         B     VALIDOUT      TAKE GOOD RETURN                   GP02304
         SPACE 1                                                 85245
VALIDERR PRTLIST MSGBDNAM    PRINT THE ERROR MESSAGE            GP02304
VALIDCOM CR    R0,R1         SET NON-ZERO COND.CODE             GP02304
         LM    R0,R14,SUBSAVE1  RESTORE                         GP02304
         BR    R14           RETURN TO CALLER                   GP02304
         SPACE 2                                                GP02304
*---------------------------------------------------------------------*
*                                                                     *
*   COMMAND PROCESSING:                                               *
*                                                                     *
*   1) UNAUTHORIZED - WRITE AS OPERATOR REQUEST                       *
*                                                                     *
*   2) AUTHORIZED - ISSUE MGCR                                        *
*                                                                     *
*---------------------------------------------------------------------*
DOCOMND  STM   R0,R15,SUBSAVE3  SAVE A LITTLE                   GP02304
         PRTLIST MSGCOMM     ECHO THE COMMAND                   GP02304
         TM    PROFLAGS,FGAUTH  ARE WE AUTHORIZED ?             GP02304
         BZ    DOWTOREQ      NO; REQUEST FROM OPERATOR          GP02304
         TM    PROFLAGS,FGDEBUG   BUT IN DEBUG MODE ?           GP05261
         BNZ   DOWTOREQ      YES; WRITE TO OPSIE                GP05261
         MODESET KEY=ZERO,MODE=SUP  MAKE MGCRE WORK             GP02304
         AIF   (&MVSXA).MGCRE1                                  GP05035
         LA    R0,COMMBUFL   GET BUFFER LENGTH                  GP05035
         STH   R0,COMMBUFF   SET TEXT LENGTH                    GP05035
         STCM  R0,12,COMMLPFX  CLEAR UNUSED FIELD               GP05035
         LA    R1,COMMBUFF   POINT TO IT                        GP05035
         SR    R0,R0         NO CONSOLE ID (INTERNAL)           GP05035
         MGCR  (1)           ISSUE THE COMMAND                  GP05035
         AGO   .COMM1                                           GP05035
.MGCRE1  LA    R2,COMMLPFX   POINT TO BUFFER                    GP02304
         SR    R3,R3         CONSOLE ID (MASTER/INTERNAL)
         MGCRE TEXT=(R2),CONSID=(R3),MF=(E,MGCRLIST)            GP02304
.COMM1   MODESET KEY=NZERO,MODE=PROB  REVERT TO NORMAL          GP02304
         LM    R0,R15,SUBSAVE3  RESTORE                         GP02304
         BR    R14           RETURN TO CALLER                   GP02304
         AIF   (&MVSXA).MGCRE2                                  GP05035
DOWTOREQ MVC   COMMBUFF(4),=AL2(COMMBUFL,X'8000') WTO LEN/FG    GP05035
         WTO   MF=(E,COMMBUFF)                                  GP05035
         AGO   .COMM2
.MGCRE2  SPACE 1                                                GP02304
DOWTOREQ WTO   TEXT=COMMLPFX,ROUTCDE=1,DESC=2,MF=(E,WTOLIST)    GP02304
.COMM2   LM    R0,R15,SUBSAVE3  RESTORE                         GP02304
         BR    R14                                              GP02304
         SPACE 2                                                GP02304
*---------------------------------------------------------------------*
*                                                                     *
*   JES2 JOB LOOKUP:                                                  *
*                                                                     *
*   1) UNAUTHORIZED - RETURN CODE 8 - NO ACTION                       *
*                                                                     *
*   2) AUTHORIZED - FIND FIRST HELD JOB ON DESIRED QUEUE              *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*   RETURN CODES:                                                     *
*                                                                     *
*   0 - JOB NAME MATCHED TO HELD INPUT JOB - ID MOVED TO COMMAND      *
*   4 - UNAUTHORIZED OR PROBLEM - USE JOB NAME IN COMMAND             *
*   8 - JOB NAME NOT FOUND - ISSUE MESSAGE AND IGNORE                 *
*                                                                     *
*---------------------------------------------------------------------*
*   THIS SUBROUTINE ADAPTED FROM JOBRLSE IN CBT TAPE 451, FILE 183    *
*---------------------------------------------------------------------*
         PUSH  USING                                            GP02307
JOBLOOK  LA    R15,4         PROVISIONALLY SET ERROR            GP02307
         STM   R0,R15,SUBSAVE2                                  GP02307
         TM    PROFLAGS,FGAUTH  ARE WE AUTHORIZED ?             GP02307
         BZ    JOBLOOKX      NO; THIS WON'T WORK                GP02307
         MVI   SUBSAVE2+4*15+3,8  SET RETURN CODE 8 - NOT FOUND GP02307
         LA    R9,MYSSOB                                        GP02307
         ST    R9,@SSOB                                         GP02307
         OI    @SSOB,X'80'                                      GP02307
         USING SSOB,R9                                          GP02307
         XC    SSOB(SSOBHSIZ),SSOB     BUILD THE SSOB           GP02307
         LA    R0,SSOBHSIZ                                      GP02307
         STH   R0,SSOBLEN                                       GP02307
         MVC   SSOBID,=C'SSOB'                                  GP02307
         LA    R0,SSOBSTAT                                      GP02307
         STH   R0,SSOBFUNC   FUNCTION CODE: CANCEL/STATUS       GP02307
         LA    R0,SSOBGN                                        GP02307
         ST    R0,SSOBINDV                                      GP02307
         LA    R0,SSCSIZE                                       GP02307
         STH   R0,SSCSLEN                                       GP02307
         MVI   SSCSFLGS,0                                       GP02307
         MVI   SSCSULEN,0                                       GP02307
         MVC   SSCSJOBN,ARGSSCS    MOVE USER'S JOB NAME         GP02307
         MVC   SSCSJOBI,BLANKS         JOB ID                   GP02307
         LA    R0,SSCSELSZ*MAXJOBS                              GP02307
         STH   R0,SSCSDIMP                                      GP02307
*---------------------------------------------------------------------*
*  INVOKE JES2 USING THE SUBSYS INTERFACE                             *
*---------------------------------------------------------------------*
         MODESET KEY=ZERO,MODE=SUP                              GP02307
         LA    R1,@SSOB                                         GP02307
         IEFSSREQ  ,                   <==== CALL JES2          GP02307
         LTR   R15,R15                                          GP02307
         BZ    JOBLOOKM                JUMP IF RC00             GP02307
         ABEND 2001,DUMP               IEFSSREQ ERROR           GP02307
JOBLOOKM MODESET KEY=NZERO,MODE=PROB                            GP02307
         ICM   R1,IIII,SSOBRETN                                 GP02307
         BNZ   JOBLOOKX      BAD RETURN CODE - GET OUT          GP02307
*---------------------------------------------------------------------*
*   FIND FIRST HELD JOB AWAITING EXECUTION                            *
*---------------------------------------------------------------------*
         LA    R3,SSCSARBG   START OF ARRAY                     GP02307
         LA    R4,SSCSELSZ   ELEMENT SIZE                       GP02307
         LH    R5,SSCSDIMR   LENGTH OF ARRAY (IN BYTES)         GP02307
         BCTR  R5,0                                             GP02307
         ALR   R5,R3         END OF ARRAY                       GP02307
         SLR   R3,R4         FINAGLE TO GET FIRST ENTRY         GP02307
         DROP  R9                                               GP02307
         USING SSCSARAY,R3                                      GP02307
JOBLOOKL BXH   R3,R4,JOBLOOKX   EXIT WITH RC=8 IF NOT FOUND     GP02307
         EX    0,EXQUETST    JOB ON DESIRED QUEUE ?             GP05261
         BNO   JOBLOOKL                TRY NEXT JOB             GP02307
         EX    0,VALIDCLR    CLEAR REST                         GP05261
         MVI   SUBSAVE2+4*15+3,0  SET RETURN CODE 0 - MATCH     GP02307
         TM    PROFLAGS,FGPURGE   PURGE REQUEST ?               GP05261
         BZ    JOBLOOKX      NO; RETURN                         GP05261
         CLC   SSCSARID,RUNJOBID  THIS JOB ?                    GP05261
         BE    JOBLOOKL      YES; DON'T PURGE                   GP05261
         MVC   COMMJOBI,SSCSARID       JOB NUMBER: 'JOB01234'   GP02307
         BAL   R14,DOCOMND   ISSUE THE COMMAND                  GP05261
         B     JOBLOOKL      SEE IF MORE                        GP05261
JOBLOOKX LM    R0,R15,SUBSAVE2                                  GP02307
         BR    R14           RETURN TO CALLER                   GP02307
         POP   USING                                            GP02307
         SPACE 2                                                 85083
         LTORG ,                                                 85083
         EJECT ,
         PRINT NOGEN
SYSIN    INPWORK SYSIN,EODAD=NOSYN,WIDTH=80,FILL=C' '  SYSIN FILE
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=3  DEFINE SYSPRINT FILE  81341
         SPACE 1
         USING SSCSARAY,R3                                      GP05261
PATRELSE DC    CL8'JOBRELSE',C'$A$E$P$P'                        GP05261
         TM    SSCSFLG1,SSCSEXCQ+SSCSHOLD  HELD INPUT ?         GP05261
PATCANCL DC    CL8'JOBCANCL',C'$P$P$A$E'                        GP05261
         TM    SSCSFLG1,SSCSEXCQ+SSCSHOLD  HELD INPUT ?         GP05261
PATPURGE DC    CL8'JOBPURGE',C'$P$P$O$O'                        GP05261
         TM    SSCSFLG1,SSCSOUTQ SSCSHOLD  HELD OUTPUT?         GP05261
PATPRINT DC    CL8'JOBPRINT',C'$O$O$H$H'                        GP05261
         TM    SSCSFLG1,SSCSOUTQ SSCSHOLD  HELD OUTPUT?         GP05261
         DROP  R3                                               GP05261
         SPACE 1
TITLE    FDOPT NL,CC=C'#'    REQUEST DATE, TIME, PAGE NUMBER INSERTION
         FDPRT RUNPGMNM,DEB  SHOW PROGRAM NAME                   81341
         FDPRT 'LISTING FOR JOB',PAD                             81341
         FDPRT RUNJOBN,DEB                                       81341
         FDPRT *END                                              81341
         SPACE 1                                                 81341
MSGCARD  FDPRT '     ',NL                                       GP02304
         FDPRT CARD                                             GP02304
         FDPRT *END                                             GP02304
         SPACE 1                                                GP02304
MSGBDNAM FDPRT '**** ABOVE REQUEST HAS INVALID OR UNAUTHORIZED JOB NAME*
               ',NL                                             GP02304
         FDPRT *END                                             GP02304
         SPACE 1                                                GP02304
MSGCOMM  FDPRT 'CMD:',NL,PADR                                   GP02304
         FDPRT COMMBUFF+4,COMMBUFL-4                            GP02304
         FDPRT *END                                             GP02304
         SPACE 1                                                 81341
MSGNOJOB FDPRT 'JOB',NL                                         GP02307
         FDPRT COMMJOBN                                         GP02307
         FDPRT 'NOT FOUND HELD ON JES2 INPUT QUEUE'             GP02307
         FDPRT *END                                             GP02307
         SPACE 1                                                 81341
NOINPUT  VCON  '-NO JOB NAMES SUPPLIED VIA SYSIN DD'             81341
         SPACE 2
TRTJOBNM DC    256AL1(16)    ALL CHARACTERS INVALID             GP02304
         TRENT TRTJOBNM,8,C'*'  ASTERISK A LITTLE LESS INVALID  GP02304
         TRENT ,4,C' '       BLANK IS TERMINAL CASE             GP02304
         TRENT ,4,C',',C';'  ALTERNATE SEPARATORS               GP02304
         TRENT ,0,C'@',C'#',C'$'    NATIONAL CHARACTERS         GP02304
         TRENT ,0,(C'A',9)   ALPHA A-I                          GP02304
         TRENT ,0,(C'J',9)   ALPHA J-R                          GP02304
         TRENT ,0,(C'S',8)   ALPHA S-Z                          GP02304
         TRENT ,0,(C'0',10)  DIGIT 0-9                          GP02304
         SPACE 1                                                GP02304
TRTNONBK DC    256AL1(4)     IGNORE ALL                         GP02304
         SPACE 1                                                GP02304
BLANKS   DC    CL80' '                                          GP02304
         AIF   (NOT &MVSXA).COMM3                               GP05035
PATWTO   WTO   TEXT=,ROUTCDE=1,DESC=2,MF=L                      GP02304
PATMGCRE MGCRE MF=L                            2/2              GP02304
PATWTOLN EQU   *-PATWTO                                         GP02304
.COMM3   SPACE 2                                                GP02304
*---------------------------------------------------------------------*
*  SUB-SYSTEM BLOCK - NEEDED PRIOR TO SAVE/WORK AREA DEFINITION       *
*---------------------------------------------------------------------*
         PUSH PRINT
         PRINT ON,GEN,DATA
         IEFJSSOB CS
         POP  PRINT
         SPACE 2                                                GP02304
*---------------------------------------------------------------------*
*  DYNAMIC SAVE/WORK AREA                                             *
*---------------------------------------------------------------------*
SAVE     DSECT ,             CONTINUATION OF SAVE/WORK AREA      81341
SUBSAVE1 DS    16A           SUBROUTINE SAVE AREA               GP02307
SUBSAVE2 DS    16A           SUBROUTINE SAVE AREA               GP02307
SUBSAVE3 DS    16A           SUBROUTINE SAVE AREA               GP05261
DB       DS    D             WORK WORD
EXQUETST DS    XL4           TM QUEUE TYPE INSTRUCTION          GP05261
         SERVDEFS ,          ADDRESS OF SERVICE ROUTINES, ETC.  GP05261
RUNACCT  DS    CL8           ACCOUNT NUMBER OF RUNNING JOB       90147
RUNJOBN  DS    CL8           JOB NAME OF CURRENT JOB            GP02304
RUNJOBID DS    CL8           JOB TYPE AND NUMBER OF *THIS JOB   GP02304
ARGSSCS  DS    CL8           ARGUMENT (JOB NAME) FOR JOBLOOK    GP05261
RUNPGMNM DS    CL8           PROGRAM NAME (JOBRLSE VS JOBCANCL) GP02304
COMMAND  DS    CL2           $A / $P                            GP02304
COMTHIS  DS    CL2           $E / $P                            GP02304
COMMINV  DS    CL(L'COMMAND+L'COMTHIS)                          GP02307
COMMBAK  DS    CL(L'COMMAND+L'COMTHIS)                          GP02307
         SPACE 1                                                GP02304
         AIF   (NOT &MVSXA).COMM4                               GP05035
WTOLIST  WTO   TEXT=,ROUTCDE=1,DESC=2,MF=L     1/2              GP02304
MGCRLIST MGCRE MF=L                            2/2              GP02304
.COMM4   SPACE 1                                                GP02304
PROFLAGS DC    X'00'         PROCESSING FLAGS                    81341
FGAUTH   EQU   X'80'           RUNNING AUTHORIZED (ALLOW MGCR)  GP02304
FGDEBUG  EQU   X'40'           RUNNING IN DEBUG MODE            GP05261
FGEODAD  EQU   X'20'           HIT END-FILE ON FIRST CARD       GP02304
FGNOT    EQU   X'10'           'NOT' FUNCTION - INVERT RELSE/CANCEL
FGHOLD   EQU   X'08'           ISSUE TWO COMMANDS ($HJ/$EJ)     GP02304
FGPURGE  EQU   X'02'           ENTRY FOR PURGE FUNCTIONS        GP05261
FGCANCEL EQU   X'01'           ALTERNATE ENTRY; PGMHEAD SET     GP02304
         SPACE 1                                                GP02304
COMMBUFF VCON  END=COMMBUFE  DEFINE COMMAND IN V FORMAT          81341
COMMLPFX EQU   COMMBUFF+2,2,C'H'  MGCRE/WTO LENGTH PREFIX       GP02304
COMMCMD  DS    CL2           $A / $E / $P / $C                  GP02304
COMMJOBI DS    CL8           JOB ID (JOBNNNNN / STCNNNNN)       GP02304
         ORG   COMMJOBI                                         GP02304
COMMQUO1 DS    0CL10,C''''   LEFT QUOTE                         GP02304
COMMJOBN DS    CL8           NAME OF JOB                        GP02304
COMMQUO2 DS    C''''                                            GP02304
COMCLEAR EQU   COMMCMD,*-COMMCMD,C' '  AREA/SIZE TO CLEAR       GP02307
         ORG   ,                                                GP02304
         VCON  *END          END OF PRINT BUFFER                 81341
         DS    XL4           DESCRIPTOR AND ROUTING CODES       GP02304
COMMBUFL EQU   COMMBUFE-COMMBUFF  LENGTH OF BUFFER              GP02304
         SPACE 1                                                GP02304
CARD     DS    CL80          INPUT BUFFER                        81341
         SPACE 2                                                GP02307
@SSOB    DS    A                                                GP02307
MYSSOB   DS    0A,XL(SSOBHSIZ)                                  GP02307
MYSSLIST DS    XL(SSCSELSZ*MAXJOBS+256)                         GP02307
SAVEEND  EQU   *                                                GP02304
         SPACE 2
         PRINT &PRTSYS                                           81341
         IHAPSA ,                                               GP02304
         CVT   DSECT=YES                                        GP02304
         SPACE 1                                                 81341
         IHACDE ,                                                81341
         SPACE 1                                                 81341
         IKJTCB ,                                                81341
         IHAASCB ,                                               85083
         SPACE 1                                                 81341
         IEFTIOT1 ,                                              81341
         SPACE 1                                                 81341
         IEZJSCB ,                                               81341
         SPACE 1                                                 81341
         IEFJSSIB ,                                             GP02304
         SPACE 1                                                 81341
         IEFJESCT ,                                             GP02307
         END   ,                                                 83020
