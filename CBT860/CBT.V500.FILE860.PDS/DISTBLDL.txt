DBLDL    TITLE 'D I S T B L D L  ***  BUILD IBM SOURCE DIRECTORY'
         PUNCH '  ORDER DISTBLDL(P) '  NEATER DUMPS             GP10187
         PUNCH '  SETCODE AC(1)     '  FOR DIRECTORY READING    GP10187
         SPACE 1
         COPY  OPTIONGB                                          82053
         SPACE 1                                                 82053
         SYSPARM LIST=YES    SET LOCAL OPTIONS                   82053
         SPACE 1                                                 82053
***********************************************************************
*                                                                     *
*   COPIED AND MODIFIED FROM PROGRAM IEBPDS, WHICH IS:                *
*                                                                     *
*        COPYRIGHT 1982,1991  EXPERT SYSTEM PROGRAMMING, INC.         *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 2                                                 82053
*        THIS MODULE PRODUCES PDS DIRECTORIES ON SPECIFIED PACKS.
*        EXCP IS USED TO READ DIRECTORY ENTRIES INTO DYNAMIC STORAGE.
*              EACH ENTRY OCCUPIES 74 BYTES.                     82053
*                                                                82053
*        ATTRIBUTES - AC=1, PAGE, NOT RENT, NOT REUS             82053
*                                                                82053
*        EXTERNAL REFERENCES (LOADED): @SERVICE, @PRINTER, @VOLREAD
*                             @DCBEXIT                           82053
*                                                                82053
*        JCL :  EXEC PGM=DISTBLDL,REGION=? >=128K
*                                                                82053
*        DDNAMES:  ANY EXCEPT RESTRICTED (SYSUDUMP,SYSPRINT,STEPLIB..)
*        1)    SINGLE PDS    DSN= ,DISP=SHR  (,VOL=SER= ,UNIT= ) 82053
*        2)    GROUP     DSN='DSNMASK.DSN.ALL',DISP=OLD,VOL=SER= ,UNIT=
*              A TRAILING VALUE OF DSN.ALL WILL TREAT THE LEADING
*              TEXT AS A MASK FILLED WITH MINUS SIGNS.           81271
*              ANY MINUS SIGN IN THE NAME IS TREATED AS A MASK REQUEST.
*              IF NEITHER PDS.ALL NOR DSN.ALL IS SPECIFIED, THE MASK
*              WILL NOT BE FILLED WITH MINUS SIGNS, BUT BLANKS.  81271
*        3)    ALL PDS ON VOL   DSN=VTOC.ALL,VOL=SER= ,UNIT= ,DISP=OLD
*        MEMBER MASK OPTION - ANY DSN/MASK OPTION, FOLLOWED BY THE
*              COMMON MEMBER NAME PREFIX; E.G.                   82053
*              DSN=SYS1.LINKLIB(IHK)   ALL FORTRAN H MODULES     82053
*                                                                82053
*        OUTPUT: AT LEAST ONE OF TWO DD CARDS MUST BE SUPPLIED:  82053
*          SUMPRINT (OR SYSTERM)- VBA,133  INDEX AND ERROR MESSAGES
*          SYSPRINT - VBA,133 LISTING WITH HEADINGS              82053
*                                                                82053
*          SYSPUNCH - FB,80 - NEW MEMBER NAME INDEX DATA SET    GP10187
*                       FORMAT AS MAPPED BY MAPINDEX            GP10187
*        PARM OPTIONS:                                           82053
*          >NAME  - PROCESS MEMBERS .GE. SPECIFIED NAME ONLY     82053
*          <NAME  - PROCESS MEMBERS .LE. SPECIFIED NAME ONLY     82053
*          =MASK  - PROCESS MEMBERS MATCHING THE MASK ONLY; THIS MASK
*              IS OVERRIDDEN BY A DD CARD MASK.  MASK IS FILLED WITH
*              TRAILING BLANKS IF SHORT; MINUS SIGN IS USED TO MATCH
*              ANY CHARACTER.                                    82053
*          /MASK  - SKIP MEMBERS MATCHING THE MASK. MASK IS FILLED WITH
*              MINUS SIGNS IF SHORT. BLANKS ARE PERMITTED (MATCHED),
*              MINUS SIGNS WILL PERMIT ANY CHARACTER.            82053
*          :SSIMASK - MASK TO MATCH SSI FIELD. FILLED WITH MINUS SIGNS
*              IF SHORT. SSI INFORMATION IS PACKED PRIOR TO TEST.
*              IF SSI DOES NOT EXIST, A VALUE OF ZERO WILL BE USED.
*              NOTE THAT ONLY MINUS AND HEX BYTES (0-9,A-F) ARE VALID.
*          SSI>VALUE AND SSI<VALUE - SELECT SSI WITHIN RANGE.    82053
*          ALIAS= OR ALIAS=MEMBER  TREAT ALIAS ENTRIES AS MEMBERS
*          TTR - SORT ENTRIES BY TTR (DEFAULT IS BY NAME)        82053
         SPACE 2                                                 82053
         PRINT &PRTSOR                                           82053
DISTBLDL PGMHEAD BIGZERO,BASE=(R10,R11,R12),PARM=R9,BNDRY=PAGE   91046
         MVC   GINCLUDE,GENMATCH  DEFAULT - NO INCLUDE MATCH     81271
         MVC   MEXCLUDE,GENMATCH  AND NO EXCLUDE MATCH           81271
         MVC   SSIMASK,GENMATCH                                  81271
         MVC   HINAME,GENMHIGH  SET FF FOR HIGH END OF RANGE     82053
         MVC   SSIHIGH,GENMHIGH                                  82053
         MVI   WORKLINE-1,C' '  INITIALIZE BLANK                 82053
         MVC   WORKLINE,WORKLINE-1  CLEAR WORK AREA              82053
         BANDAID INIT   *****DEBUG HELP*****
         SERVINIT ,          LOAD AND SET @SERVICE ROUTINE       82053
         MVC   GETMAIN,GETMEAN  MOVE GETMAIN PATTERN             82053
         LM    R14,R15,TABMAIN    GET MIN / MAX STORAGE DESIRED  82053
         A     R14,SYSSIZE   ADD SYSTEM SIZE FOR DUMPS, ETC.     82053
         STM   R14,R15,MAINTAB    SET FOR LATER GETMAIN          82053
         MVC   SYSPRINT(PATPRTLN),PATPRINT  INIT SYSPRINT WORK   82053
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINTER ROUTINE 82053
         ST    R0,@PRINTER   STASH IT                            82053
         TESTAUTH FCTN=1     ARE WE AUTHORIZED ?                 81343
         BXLE  R15,R15,LOADAUTH   YES                            81343
         L     R0,=A(VOLFAKE)  USE UNAUTHORIZED INTERFACE       GP99033
         MVI   FLAGS2,FGUNAUTH  SET AS UNAUTHORIZED             GP99035
         B     LOADEDVL                                         GP99033
LOADAUTH SERVCALL LPALD,=CL8'@VOLREAD'  LOAD THE DISK READ ROUTINE
         ST    R0,@VOLAUTH   ALSO SAVE AUTHORIZED ADDRESS       GP99035
LOADEDVL ST    R0,@VOLREAD   STASH IT                            82053
         PRTOPEN SYSPRINT,OPT=(NOWTO,DUMMY)                      82053
         BXH   R15,R15,*+8                                       82053
         OI    OUTUNIT,OUPRT   SET SYSPRINT PRESENT              82053
         PRTOPEN SUMPRINT,DEV=2,OPT=(DUMMY,NOWTO)                81271
         CH    R15,=H'4'     NORMAL OR ALIAS OPEN ?              81271
         BH    *+8           NEITHER; ERROR                      81271
         OI    OUTUNIT,OUSUM   SET SUMPRINT PRESENT              82053
         PRTOPEN CONSOLE,DEV=5                                   82053
         TM    OUTUNIT,OUPRT+OUSUM  ANY PRINT FILE ?             82053
         BNZ   INITHPRT      YES                                 82053
         PRTV  MSGBDPRT,DEV=5,CC=NO   WRITE MISSING DD MESSAGE   82053
         TM    OUTUNIT,OUPUN+OUPDT  ANY OUTPUT DEVICE ?          82053
         BNZ   INITHPRT      YES; KEEP GOING                     82053
         SERVTERM ,          CLOSE EVERYTHING                    82053
         ABEND 1403                                              82053
MSGBDPRT VCON  'NEITHER SYSPRINT NOR SYSTERM/SUMPRINT DD USABLE' 81271
         SPACE 1                                                 82053
INITHPRT MVC   ERRUNIT,OUTUNIT   COPY UNIT BITS                  82053
         NI    ERRUNIT,OUPRT+OUSUM  KILL NON-PRINT BITS          82053
         TM    ERRUNIT,OUSUM   SUMPRINT ON ?                     82053
         BZ    *+8           NO                                  82053
         NI    ERRUNIT,255-OUPRT  RESET SYSPRINT                 82053
         PRTLIST SUMTIT1,DEV=(1,2),TITLE=1  SUMMARY HEADER       82053
*OLD*    TESTAUTH FCTN=1     ARE WE AUTHORIZED ?                 81343
*OLD*    BXLE  R15,R15,HAVEAUTH   YES                            81343
*OLD*    PRTV  MSGUNAUT,DEV==ERRUNIT  PRINT ERROR MESSAGE        81343
*OLD*    SERVTERM ,          CLOSE AND FREE                      81343
*OLD*    ABEND 47            QUIT                                81343
         SPACE 1                                                 81343
HAVEAUTH PRTOPEN SYSPUNCH,DEV=4,OPT=(NOWTO)  OPT. PUNCH FILE     91046
         PUSH  USING                                            GP97238
         LTCB  R1,USE=YES    GET CURRENT TCB                     91046
         L     R1,TCBJSCB                                        82053
         USING IEZJSCB,R1                                        82053
         L     R7,JSCBCSCB                                       82053
         USING CHAIN,R7                                          82053
         ST    R7,CSCBPTR                                        82053
         AIF   (NOT &MVSESA).CMESAQU                            GP97238
         L     R2,CHCSCXP    GET THE CSCB EXTENSION             GP97238
         USING CSCX,R2       DECLARE IT                         GP97238
.CMESAQU QEDIT ORIGIN=CHCIBP                                     82053
         QEDIT ORIGIN=CHCIBP,CIBCTR=1  1 MODIFY AT A TIME        82053
         POP   USING         RESTORE ORIGINALS                  GP97238
         LA    R7,0(,R9)     CLEAN PARM ADDRESS                  82053
         LTR   R7,R7         ANY PARM ADDRESS ?                  82053
         BZ    PARMDFLT      NO; USE DEFAULTS                    82053
         L     R7,0(,R7)                                        GP03233
         LA    R7,0(,R7)                                        GP03233
         LTR   R7,R7         LOAD AND TEST PARM ADDRESS         GP03233
         BZ    PARMDFLT      NONE                                82053
         SLR   R9,R9                                             82053
         ICM   R9,3,0(R7)    LOAD AND TEST PARM LENGTH           82053
         BZ    PARMDFLT      NONE                                82053
         LA    R8,100        OS MAXIMUM                          82053
         CR    R9,R8                                             82053
         BNH   *+6           OK                                  82053
         LR    R9,R8         TRUNCATE                            82053
         BCTR  R9,0          SET FOR EXECUTE                     82053
         EX    R9,PARMMOVE   MOVE PARM TO WORK AREA              82053
         LA    R7,WORKLINE+2(R9)  POINT TO LAST BYTE +1          82053
         MVI   0(R7),C','    SET SCAN STOPPER                    82053
         LA    R7,WORKLINE+1  POINT TO START OF PARM             82053
         SLR   R8,R8         CLEAR BXLE INCREMENT REGISTER       82053
         AR    R9,R7         SET LAST PARM ADDRESS               82053
         SPACE 1                                                 82053
PARMLOOP LA    R4,PARMTAB    GET VERB TABLE FOR PARMS            82053
PARMVERB ICM   R8,1,0(R4)    INSERT AND TEST LENGTH BYTE         82053
         BM    PARMERR       NO MATCH - DISPLAY BAD DATA         82053
         EX    R8,PARMCLC    MATCH ?                             82053
         BE    PARMGO        YES; GO TO APPROPRIATE ENTRY        82053
         LA    R4,4(R8,R4)   POSITION TO NEXT PARM               82053
         B     PARMVERB      TRY NEXT ONE                        82053
PARMGO   SLR   R15,R15                                           82053
         ICM   R15,3,1(R4)   GET BRANCH OFFSET                   82053
         B     DISTBLDL(R15)   ENTER SUBROUTINE                 GP10187
PARMMOVE MVC   WORKLINE+1(0),2(R7)  MOVE PARM FIELD              82053
PARMCLC  CLC   0(0,R7),3(R4)        COMPARE VERB                 82053
PARMBADM VCON  ' ***** ERROR IN PARM FIELD *****'                82053
         SPACE 1                                                 82053
PARMTAB  VERBTAB ' ',PARMBUMP,BASE=*,LEN=2    SKIP BLANKS        82053
         VERBTAB ',',PARMBUMP                 SKIP COMMAS        82053
         VERBTAB TTR,PRMTTR  LIST MEMBERS IN TTR SEQUENCE        82053
         VERBTAB 'GE=',PRMGE           SELECT MEMBERS GE NAME    82053
         VERBTAB 'GT=',PRMGE             ALTERNATE FORM          82053
         VERBTAB '>',PRMGE               SHORT FORM              82053
         VERBTAB 'LE=',PRMLE           SELECT MEMBERS LE NAME    82053
         VERBTAB 'LT=',PRMLE             ALTERNATE FORM          82053
         VERBTAB '<',PRMLE               SHORT FORM              82053
         VERBTAB 'MASK=',PRMMASK       SELECT BY MASK            82053
         VERBTAB 'INCLUDE=',PRMMASK      ALTERNATE FORM          82053
         VERBTAB '=',PRMMASK             SHORT FORM              82053
         VERBTAB 'SKIP=',PRMSKIP       EXCLUDE BY MASK           82053
         VERBTAB 'EXCLUDE=',PRMSKIP      ALTERNATE FORM          82053
         VERBTAB '/',PRMSKIP             SHORT FORM              82053
         VERBTAB 'SSI>',PRMSSGE        SSI GREATER THAN          82053
         VERBTAB ':>',PRMSSGE            SHORT FORM              82053
         VERBTAB 'SSI<',PRMSSLE        SSI LESS THAN             82053
         VERBTAB ':<',PRMSSLE            SHORT FORM              81271
         VERBTAB 'SSI=',PRMSSI         SELECT BY SSI             82053
         VERBTAB 'SSI:',PRMSSI           ALTERNATE FORM          82053
         VERBTAB ':',PRMSSI              SHORT FORM              82053
         VERBTAB 'ALIAS=MEMBER',PRMALIAS                         82053
         VERBTAB 'ALIAS=',PRMALIAS     PROCESS ALIAS AS MEMBER ENTRIES
         VERBTAB 'PUNCH=',PRMPUNCH     SET EXCLUDE OR INCLUDE MODE
         DC    X'FF'         END OF TABLE                        82053
         SPACE 1                                                 82053
PARMERR  PRTV  PARMBADM,DEV==ERRUNIT   PRINT ERROR IN PARM       82053
         PRTF  WORKLINE,L'WORKLINE,DEV==ERRUNIT SHOW FULL PARM   82053
         MVC   WORKLINE,WORKLINE-1  CLEAR PARM FIELD             82053
         MVI   0(R7),C'|'    POINT TO BAD PARAMETER              82053
         PRTF  WORKLINE,L'WORKLINE,DEV==ERRUNIT  POINT TO ERROR  82053
         SERVTERM ,          CLOSE AND FREE                      82053
         ABEND 100           TERMINATE                           82053
         SPACE 1                                                 82053
PRMTTR   OI    FLAGS,FGTTR   SET TTR SEQUENCE                    82053
         B     PARMBUMP                                          82053
         SPACE 1                                                 82053
PRMALIAS OI    FLAGS,FGDOAL  PROCESS ALIAS ENTRIES AS MEMBERS    82053
         B     PARMBUMP                                          82053
         SPACE 1                                                 91046
PRMPUNCH CLI   6(R7),C' '    NULL OPTION ?                       91046
         BE    PARMBUMP      YES; SKIP                           91046
         CLI   6(R7),C','    ALTERNATE NULL ?                    91046
         BE    PARMBUMP      YES                                 91046
         LA    R8,1(,R8)     VALUE IS ONE BYTE                   91046
         CLI   6(R7),C'I'    INCLUDE ?                           91046
         BE    PRMPUNIN      YES                                 91046
         CLI   6(R7),C'S'    SELECT ?                            91046
         BE    PRMPUNIN      YES                                 91046
         CLI   6(R7),C'E'    EXCLUDE ?                           91046
         BE    PRMPUNEX      YES                                 91046
         CLI   6(R7),C'X'    ALTERNATE EXCLUDE ?                 91046
         BNE   PARMERR       NO; FAIL                            91046
PRMPUNEX OI    FLAGS,FGCOPEX   SET EXCLUDE MODE                  91046
         B     PARMBUMP      CONTINUE                            91046
PRMPUNIN NI    FLAGS,255-FGCOPEX  SET INCLUDE                    91046
         B     PARMBUMP                                          91046
         SPACE 1                                                 81271
PRMGE    LA    R5,LONAME     SET FOR LOW NAME                    82053
         B     PRMCLONG      SET LONG LIST OF VALID BYTES        82053
PRMLE    LA    R5,HINAME     SET FOR HIGHEST NAME                82053
         B     PRMCLONG                                          82053
PRMMASK  LA    R5,GINCLUDE   SET INCLUDE MASK                    81271
*        TM    0(R5),X'FF'   PREVIOUSLY SPECIFIED ?              82053
*        BM    PARMERR       YES; TOO BAD                        82053
*        MVC   GINCLUDE,WORKLINE+102  MAKE ALL BLANKS            81271
         B     PRMCLONG                                          82053
PRMSKIP  LA    R5,MEXCLUDE   SET EXCLUDE MASK                    81271
         B     PRMCLONG                                          82053
PRMSSLE  LA    R5,SSIHIGH    SSI HIGH VALUE                      82053
         B     PRMSSICM                                          82053
PRMSSGE  LA    R5,SSILOW     SSI LOW VALUE                       82053
         B     PRMSSICM                                          82053
PRMSSI   LA    R5,SSIMASK    SET SSI MASK                        82053
PRMSSICM LA    R15,16+1      ONLY SIXTEEN HEX BYTES AND -  ARE VALID
         B     PRMCTEST                                          82053
PRMCLONG LA    R15,L'ALPHAS  SET NUMBER OF VALID BYTES           82053
PRMCTEST CLC   GENMATCH,0(R5)  PRESET VALUE ?                    82053
         BE    PRMTPASS      YES                                 82053
         TM    0(R5),X'FF'   PREVIOUSLY SPECIFIED ?              82053
         BM    PARMERR       YES; DUPLICATE                      82053
PRMTPASS LA    R8,1(,R8)     MAKE TRUE LENGTH TO SKIP            82053
         BXH   R7,R8,PARMDONE  EXIT IF NULL                      82053
         LA    R4,L'GINCLUDE  SET MASK LENGTH                    81271
PRMCWORD SLR   R8,R8         LENGTH OF ONE                       82053
         CLI   0(R7),C','    END OF FIELD ?                      82053
         BE    PARMBUMP      YES; EXIT                           82053
         LA    R14,ALPHAS    POINT TO TABLE OF VALID CHARACTERS  82053
         LR    R0,R15        COPY NUMBER TO CHECK                82053
PRMCLOOP CLC   0(1,R14),0(R7)  VALID BYTE ?                      82053
         BE    PRMCMOVE      YES; MOVE IT                        82053
         LA    R14,1(,R14)   BUMP                                82053
         BCT   R0,PRMCLOOP   TRY AGAIN                           82053
         B     PARMBUMP+4    INVALID; SEE IF ANOTHER OPTION      82053
PRMCMOVE MVC   0(1,R5),0(R7)  MOVE ONE BYTE                      82053
         LA    R5,1(,R5)                                         82053
         BCT   R4,*+8        SKIP IF NOT DONE YET                82053
         B     PARMBUMP      ELSE TRY FOR ANOTHER PARM           82053
         LA    R8,1          SET TRUE LENGTH AGAIN               82053
         BXLE  R7,R8,PRMCWORD  TRY NEXT BYTE                     82053
         B     PARMDONE      ELSE END OF PARM                    82053
         SPACE 1                                                 81271
PARMINTG LA    R7,1(R7,R8)   SPACE TO FIRST BYTE                 82053
         BAS   R14,PARMNBLK  GET FIRST NON-BLANK                GP99033
         SLR   R0,R0                                             82053
         LA    R1,C'0'       PRESET FOR NULL                     82053
         CLI   0(R7),C','    NULL ?                              82053
         BE    PARMINUL      YES                                 82053
         CLI   0(R7),C' '    END OF PARM ?                       82053
         BE    PARMINUL      YES; STOP                           82053
         LA    R8,1          SET INCREMENT FOR LOCAL BXLE        82053
         BCTR  R7,0          BACKSPACE                           82053
         LA    R14,8         MAX INTEGER LENGTH PERMITTED        82053
PARMILOP BXH   R7,R8,PARMIDON  SKIP IF DONE                      82053
         CLI   0(R7),C'0'    NUMERIC ?                           82053
         BL    PARMIDON      NO                                  82053
         CLI   0(R7),C'9'                                        82053
         BH    PARMIDON                                          82053
         SLDL  R0,8                                              82053
         IC    R1,0(,R7)     LOAD NEXT BYTE                      82053
         BCT   R14,PARMILOP  TRY AGAIN                           82053
         B     PARMINUL                                          82053
PARMIDON BCTR  R7,0          BACKSPACE                           82053
PARMINUL SLR   R8,R8         SET LENGTH-1 TO SKIP AFTER          82053
         STM   R0,R1,UPD     STASH VALUE                         82053
         PACK  DB,UPD                                            82053
         CVB   R0,DB                                             82053
         ST    R0,0(,R5)     STASH VALUE                         82053
         LTR   R0,R0         CHECK VALUE                         81271
         BP    PARMBUMP      POSITIVE; CONTINUE SCAN             81271
         BCTR  R7,0          BACKSPACE FOR ERROR POINTER         81271
         B     PARMERR       SIGNAL ERROR                        81271
         SPACE 1                                                 82053
PARMNBLK LA    R8,1          SET SKIP LENGTH                     82053
         BCTR  R7,0          BACKUP ONE                          82053
PARMNBLL BXH   R7,R8,0(R14)  RETURN IF END OF FIELD              82053
         CLI   0(R7),C' '    EMBEDDED BLANK ?                    82053
         BNER  R14           NO; RETURN                          82053
         B     PARMNBLL      ELSE TRY AGAIN                      82053
         SPACE 1                                                 82053
PARMBUMP LA    R8,1(,R8)     GET TRUE LENGTH                     82053
         BXLE  R7,R8,PARMLOOP  EXAMINE NEXT PARM                 82053
         SPACE 1                                                 82053
PARMDONE CLC   GINCLUDE,WORKLINE+102  BLANK MASK ?               81271
         BNE   PARMDFLT      NO                                  82053
         MVC   GINCLUDE,GENMATCH   RESET IT                      81271
PARMDFLT MVC   WORKLINE,WORKLINE-1   BLANK WORK LINE AGAIN       82053
         PACK  DB(5),SSILOW(9)   PACK SSI LOWER VALUE            82053
         MVC   PACKSSIL,DB   SAVE IT FOR TESTING                 82053
         PACK  DB(5),SSIHIGH(9)                                  82053
         MVC   PACKSSIH,DB   SAVE HIGH VALUE FOR TESTING         82053
         PRTLIST FOPTIONS,DEV==ERRUNIT  DISPLAY THE RUN OPTIONS  82053
         B     LOOKTIOT                                          82053
         SPACE 2                                                 82053
ERRTIOT  PRTSPACE 2,DEV==ERRUNIT                                 82053
         PRTLIST MTIOTERR,DEV==ERRUNIT   (R3) HAS FD POINTER     82053
         OI    RETCC,CCWARN                                      82053
         SPACE 1                                                 82053
LOOPTIOT TM    FLAGS,FGDSN   DSN MASK PROCESSING ?               82053
         BNZ   LOOPDSCB      YES; LOOK FOR NEXT DSCB ON VOLUME   81271
         VOLREAD TCLOSE      ELSE RELEASE VOLUME                 81271
LOOKTIOT L     R1,ADDRTIOT   GET PRIOR TIOT ADDRESS              82053
         SERVCALL TIOLK      LOOP THROUGH ALL ENTRIES            82053
         LTR   R2,R0         ANY MORE ?                          82053
         BZ    ENDALL        NO                                  82053
         ST    R2,ADDRTIOT   SAVE FOR RESTART                    82053
         USING TIOELNGH,R2   DECLARE IT                          82053
         CLC   =C'SYS',TIOEDDNM  DYNAMIC DD, TRACE, ETC         GP10187
         BE    LOOKTIOT      YES; SKIP                           87242
         LA    R3,MTIODUPL   SET FOR DUPLICATE DD                82053
         CLC   =C'TTR',TIOEDDNM  REQUEST TO CHANGE SORT OPTION ? 82053
         BE    LOOKSTTR      YES                                 82053
         CLC   =C'NAM',TIOEDDNM  RESET TTR OPTION ?              82053
         BNE   LOOKNDUP      NO                                  82053
         NI    FLAGS,255-FGTTR   RESET TTR OPTION                82053
         B     LOOKNTTR                                          82053
LOOKSTTR OI    FLAGS,FGTTR   SET SORT BY TTR                     82053
LOOKNTTR PRTLIST FOPTDD,DEV=2   SHOW MODIFIED SORT OPTION        82053
         ICM   R4,7,TIOEFSRT  ANY UCB ENTRY ?                    82053
         BZ    LOOKTIOT      SKIP DUMMY                          82053
         SERVCALL TIODD,TIOEDDNM  CHECK FOR DUPLICATE            82053
         CR    R2,R0         DUPLICATE ENTRY ?                   82053
         BE    LOOKTIOT      YES; NO ERROR FOR OPTION DD         82053
LOOKNDUP SERVCALL TIODD,TIOEDDNM  SCAN BY DDNAME                 82053
         CR    R2,R0         DUPLICATE DD ENTRY ?                82053
         BNE   ERRTIOT       YES; SKIP                           82053
         CLI   TIOELNGH,20   MINIMUM LENGTH ?                    82053
         BL    LOOKTIOT      NO; SKIP                            82053
         ICM   R4,7,TIOEFSRT  TEST FOR DUMMY DD AND LOAD UCB     82053
         BZ    LOOKTIOT      DUMMY; TOO BAD, DUMMY               82053
         USING UCBOB,R4                                          82053
         CLI   UCBTBYT3,UCB3DACC  DASD ?                         82053
         BNE   LOOKTIOT      NO                                  82053
         LA    R3,MTIOMVOL                                       82053
         CLI   TIOELNGH,20   MORE THAN ONE UNIT ?                82053
         BH    ERRTIOT       YES; NOT SUPPORTED                  82053
         CLI   TIOELNGH+20,0  END OF TIOT ?                      82053
         BE    USETIOT       YES; USE IT                         82053
         CLI   TIOEDDNM+20,C' '  CONCATENATION ?                 82053
         BNE   USETIOT       NO; USE IT                          82053
         LA    R3,MTIOCONC   SET FOR INVALID CONCATENATION       82053
         PRTSPACE 2,DEV==ERRUNIT                                 82053
         PRTLIST MTIOTERR,DEV==ERRUNIT   (R3) HAS FD POINTER     82053
         OI    RETCC,CCWARN                                      82053
USETIOT  MVC   CURDDNM,TIOEDDNM  SAVE DDNAME                     82053
         NI    FLAGS2,255-FGPVIO  RESET VIO FLAG
         TM    UCBJBNR,UCBVRDEV  VIO DATA SET ?
         BZ    *+8           NO
         OI    FLAGS2,FGPVIO  YES; USE @INPREAD
         CLC   CURVOL,UCBVOLI   SAME AS PRIOR VOLUME ?           82053
         BE    *+8           YES                                 82053
         NI    FLAGS,255-FGDUPL  RESET DUPLICATE DD FLAG         82053
         MVC   CURVOL,UCBVOLI  SAVE VOLSER                       82053
         MVC   CURUNIT,UCBCHAN  SAVE DEVICE NUMBER
         MVC   CURTYPE,UCBTBYT1  SAVE 4-BYTE DEVICE TYPE        GP99033
         SERVCALL UCBGN,(R4)   GET JCL UNIT= TYPE                82053
         DROP  R2,R4                                             82053
         MVC   CURDEVT,0(R1)   STASH IT                          82053
*                                                                82053
*        NOTE THAT R0 IS SET FOR 16*1024 - SIZE FOR ONE 3330 BUFFER
*        FOR LARGER SIZES, @VOLREAD FORCES ONE BUFFER.           82053
*                                                                82053
         L     R0,=X'8000C000'  SET 48K FOR BUFFERS             GP99035
         VOLREAD OPEN,CURVOL,SIZE=(R0)   OPEN THE VOLUME         82053
         BXLE  R15,R15,VOLOPEN   PROCESS IT                      82053
VOLNTOPN OI    FONE,FODDN    PREVENT NO DD CARD MESSAGE          82053
         LA    R3,MDDNVOPN   SHOW OPEN FAILED                    82053
ERRDDN   SRA   R15,1         GET R15 BACK                        82053
         ST    R15,DB        SAVE FOR FORMATTING                 82053
         PRTSPACE 2,DEV==ERRUNIT                                 82053
         PRTLIST MDDNERR,DEV==ERRUNIT  SHOW BAD OPEN             82053
         OI    RETCC,CCWARN                                      82053
         B     LOOPTIOT      TRY ANOTHER                         82053
         SPACE 1                                                 82053
NOSPACE  PRTV  ERRSPACE,DEV==ERRUNIT                             82053
         MVI   RETCC,CCFATAL   SET FATAL ERROR                   82053
         B     EXIT                                              82053
         SPACE 1                                                 82053
VOLOPEN  LR    R1,R0         COPY FORMAT 4 ADDRESS               82053
         CLI   DS1FMTID-DS1FMTID(R1),C'4'  DID WE GET IT?       GP99035
         BNE   2             NO; TAKE A DIVE                    GP99035
         MVC   TRKPCYL,DS4DEVSZ+2-DS4IDFMT(R1)                  GP99035
         MVC   CURDCB(PATDCBLN),PATDCB  MOVE DCB PATTERN         82053
         MVC   DCBDDNAM-IHADCB+CURDCB,CURDDNM  SET DDNAME        82053
         SPACE 1                                                 82053
         ICM   R0,15,LIST    GETMAIN ALREADY DONE ?              82053
         BNZ   VOLOPENS      YES; SKIP                           82053
         LA    R2,MAINTAB    GET REQUEST SIZE LIST               82053
         LA    R3,LIST       RESULT ADDRESS                      82053
         GETMAIN VC,LA=(R2),A=(R3),MF=(E,GETMAIN),BNDRY=PAGE    GP10187
         BXH   R15,R15,NOSPACE   SHOW NOTHING OBTAINED           82053
         LM    R2,R3,LIST    GET ADDRESS AND SIZE                82053
         LA    R1,0(R2,R3)   GET END ADDRESS                     82053
         L     R4,SYSSIZE    SET RESERVED SIZE                   82053
         SR    R1,R4         START ADDRESS TO FREE               82053
         SR    R3,R4         SET RESIDUAL LENGTH                 82053
         FREEMAIN R,LV=(R4),A=(1)  FREE RESERVED SIZE            82053
         ST    R2,LIST       SET START OF LIST                   82053
         SLR   R2,R2                                             82053
         LA    R5,LISTLEN    GET ENTRY SIZE                      82053
         DR    R2,R5         GET NUMBER OF ENTRIES               82053
         ST    R3,NUMSPACE   SAVE NUMBER OF ENTRIES              82053
         MR    R2,R5         GET STORAGE FOR ENTRIES             82053
         A     R3,LIST       GET END ADDRESS                     82053
         SR    R3,R5         ADDRESS OF LAST ENTRY               82053
         ST    R3,LISTEND    SAVE END OF LIST                    82053
         ST    R3,ALIST      DITTO FOR ALIAS PROCESSING          82053
         L     R2,LIST       GET START BACK                      82053
         L     R3,LISTEND    GET LAST ENTRY                      82053
         AR    R3,R5                                             82053
         SR    R3,R2                                             82053
         CLRL  (R2),(R3)     ZERO THE STORAGE                    82053
         PRTLIST STASHNUM,DEV==ERRUNIT  SHOW NUMBER OF LIST ENTRIES
         SPACE 1                                                 82053
VOLOPENS LA    R15,EXITLIST  GET EXIT LIST                       82053
         STCM  R15,7,DCBEXLSA-IHADCB+CURDCB                      82053
         LA    R14,CURDCB                                        82053
         LA    R15,INFMJFCB                                      82053
         STM   R14,R15,OPENLIST                                  82053
         MVI   OPENLIST,X'80'   SINGLE DCB                       82053
         MVI   EXITLIST,X'87'  JFCB ADDRESS                      82053
         LA    R3,MDDNJFCB   SET FOR JFCB FAILURE                82053
         RDJFCB MF=(E,OPENLIST)  READ THE JFCB                   82053
         BXH   R15,R15,ERRDDN    TOO BAD                         82053
         CLC   =C'SYSCTLG.V',JFCBDSNM  DYNAMIC CATALOG ?         82360
         BE    LOOPTIOT      YES; IGNORE WITHOUT ERROR           82360
         CLC   =C'CATALOG.',JFCBDSNM  DYNAMIC CATALOG ?         GP99033
         BE    LOOPTIOT      YES; IGNORE WITHOUT ERROR          GP99033
         OI    FONE,FODDN    SHOW AT LEAST ONE DD ENTRY FOUND    81343
         CLC   JFCBDSNM,DS1DSNAM  SAME DSN AS BEFORE ?           82053
         BE    *+8           YES                                 82053
         NI    FLAGS,255-FGDUPL   RESET DUPLICATE FLAG           82053
         CLC   =C'VTOC.ALL ',JFCBDSNM  ENTIRE VOLUME ?           82144
         BE    MAKEMVOL      YES (NOTE R15=0 FROM RDJFCB)        82144
         LA    R14,JFCBDSNM                                      81271
         LA    R15,JFCBDSNM+37  LAST POSSIBLE POSITION FOR MASK  81271
         LA    R0,L'JFCBDSNM-6   NUMBER OF POSITIONS FOR MASK    82144
LOOPMASK CLC   =C'.ALL',3(R15)  MASK REQUEST ?                   81271
         BNE   BUMPMASK      NO; TEST FURTHER                    81271
         CLC   =C'PDS',0(R15)   PO ONLY ?                        81271
         BE    MAKEMPDS      YES                                 81271
         CLC   =C'DSN',0(R15)   PS/DA/PO ?                       81271
         BE    MAKEMPDS      YES                                 82053
BUMPMASK BCTR  R15,0                                             81271
         CLI   7(R15),C' '   TRAILING BLANK ?                    81271
         BNE   *+8           NO; QUIT                            81271
         BCT   R0,LOOPMASK   TRY AGAIN                           81271
         LA    R0,L'JFCBDSNM  LOOK FOR MASK BYTE                 81271
LOOPMSKM CLI   0(R14),C'-'   MASK ?                              81271
         BE    LOOPMSKF                                          81271
         LA    R14,1(,R14)                                       81271
         BCT   R0,LOOPMSKM                                       81271
         B     SINGLEDS      NO MASK OPTION                      81271
LOOPMSKF MVI   DSNMASKL,L'JFCBDSNM  FULL NAME COMPARE            82053
         B     MAKEMSKF      MOVE JFCB AND SET FLAGS             82053
MAKEMPDS SR    R15,R14       GET SPECIFIED MASK LENGTH           82053
MAKEMVOL STC   R15,DSNMASKL  SAVE IT                             82144
MAKEMSKF OI    FLAGS,FGDSN   SEARCH FOR PO ONLY                  82053
         MVC   DSNMASK,JFCBDSNM  SAVE MASK                       81271
         B     LOOPDSCB      SEARCH FOR FIRST ONE                81271
         SPACE 1                                                 82053
SINGLEDS LA    R3,MDDN213    SET FOR DATASET NOT FOUND           81271
         VOLREAD SEARCH,JFCBDSNM  LOOK FOR DSCB                  82053
         BXH   R15,R15,ERRDDN    NOT FOUND                       82053
         LR    R1,R0         COPY                                82053
         MVC   DS1DSNAM(LENDSCB1),0(R1)  MOVE IT                 82053
         BAS   R9,CHECKDS1   SEE IF DATASET USABLE              GP99033
         B     ERRDSN        NO; REJECT WITH ERROR MESSAGE       81271
         B     GOODDCB       ELSE PROCESS IT                     81271
         SPACE 1                                                 82053
ERRDSN   PRTSPACE 2,DEV==ERRUNIT                                 82053
         PRTLIST MDSNERR,DEV==ERRUNIT  SHOW BAD OPEN             82053
         OI    RETCC,CCWARN                                      82053
         B     LOOPTIOT      TRY ANOTHER                         82053
         SPACE 1                                                 81271
LOOPDSCB VOLREAD DSCB        GET THE NEXT DSCB IN THE VTOC       81271
         BXLE  R15,R15,LOOKDSCB  OK; TEST IT                     81271
         TM    FLAGS,FGDSNF  AT LEAST ONE MATCH ?                82053
         BNZ   LOOKDSCX      RESET AND DO ANOTHER                82053
         NI    FLAGS,255-FGDSN-FGDSNF  RESET SEARCH BITS         82053
         LA    R3,MDDNMASK   SET FOR NO MATCH ON MASK            81271
         IC    R4,DSNMASKL   GET THE MASK LENGTH FOR FDPRT       81271
         B     ERRDDN        AND QUIT WITH MSG                   81271
LOOKDSCX NI    FLAGS,255-FGDSN-FGDSNF  RESET LOOP FLAGS          82053
         B     LOOPTIOT      AND GET ANOTHER DD                  82053
LOOKDSCB LR    R1,R0                                             81271
         MVC   DS1DSNAM(LENDSCB1),0(R1)  COPY DSCB               81271
         CLI   DS1FMTID,C'1' FORMAT 1 DSCB ?                     81271
         BNE   LOOPDSCB      NO; GET ANOTHER                     81271
         CLI   DS1DSORG+1,0       PDS ?                         GP09186
         BNE   LOOPDSCB      NO; IGNORE                         GP09186
         TM    DS1DSORG,JFCORGPO  PDS ?                          81271
         BZ    LOOPDSCB      NO; REJECT                          81271
MTCHDSCB LA    R14,DSNMASK                                       81271
         LA    R15,DS1DSNAM  POINT TO NAME                       81271
         SLR   R0,R0                                             81271
         IC    R0,DSNMASKL   GET LENGTH OF MASK                  81271
         BAS   R9,COMPMASK   DO MASK TEST                       GP99033
         BNE   LOOPDSCB      NO MATCH; SKIP IT                   91046
         BAS   R9,CHECKDS1   CHECK DCB, ETC.                    GP99033
         B     LOOPDSCB      NOT USABLE; SKIP                    81271
         MVC   CURDCB(PATDCBLN),PATDCB  REFRESH DCB PATTERN      81271
         MVC   DCBDDNAM-IHADCB+CURDCB,CURDDNM  SET DDNAME        81271
         LA    R15,EXITLIST  GET EXIT LIST                       81271
         STCM  R15,7,DCBEXLSA-IHADCB+CURDCB                      81271
         LA    R14,CURDCB                                        81271
         LA    R15,INFMJFCB                                      81271
         STM   R14,R15,OPENLIST                                  81271
         MVI   OPENLIST,X'80'   SINGLE DCB                       81271
         MVI   EXITLIST,X'87'  JFCB ADDRESS                      81271
         LA    R3,MDDNJFCB   SHOW BAD READ                       81271
         RDJFCB MF=(E,OPENLIST)  GET JFCB MEMBER NAME            81271
         MVC   JFCBDSNM,DS1DSNAM  BUT CLOBBER DSN                81271
         ST    R15,DB        SAVE RETURN CODE FOR FORMATTING     81271
         BXH   R15,R15,ERRDSN   TOO BAD                          81271
         OI    FLAGS,FGDSNF  SHOW AT LEAST ONE MATCH FOUND       82053
         SPACE 1                                                 82053
GOODDCB  MVI   FONE,FODSN+FODDN+FOSKIP    RESET FLAGS            82053
         XC    CLRDSN(CLRDSNLN),CLRDSN  RESET FOR NEW DATASET    82053
         PRTLIST PRTTIT1,TITLE=1  SYSPRINT HEADER                82053
         PRTLIST PRTTIT2,TITLE=2                                 82053
         XC    DBWD,DBWD     CLEAR EXTENT SIZE AND PERCENTAGE    82053
         LA    R14,CCHHDSCB  GET ADDRESS OF CCHHR FOR DSCB 1     82053
         LA    R15,DS1DSNAM                                      82053
         STM   R14,R15,DB                                        82053
         OI    DB+4,X'80'    SET END OF LIST BIT                 82053
         LA    R1,DB         POINT TO CALL PARM                  82053
         CALL  DSCB1         FORMAT THE DSCB 1 INFORMATION       82053
         MVC   MEMPAGE,PRPAGE  SAVE PAGE NUMBER OF FIRST ENTRY   82053
         PRTROOM 5,DEV=2     LEAVE SPACE FOR A FEW LINES         82053
         PRTSPACE 2,DEV=2    TRIPLE-SPACE                        82053
         PRTLIST SUMDDN,DEV=2   PRINT NEW SUMMARY LISTING HEADER 82053
         MVC   MINCLUDE,GINCLUDE  SET GLOBAL MASK PRIOR TO OVERRIDE
         CLI   JFCBELNM,C' ' ANY NAME ?                          82053
         BNH   SHOWPGNM      NO                                  82053
         LA    R14,MINCLUDE                                      82053
         LA    R15,JFCBELNM                                      82053
         LA    R0,L'MINCLUDE                                     82053
SHOWINLP CLI   0(R15),C' '   TRAILING BLANK ?                    82053
         BE    SHOWINCL      YES; LEAVE MASK                     82053
         MVC   0(1,R14),0(R15)  ELSE COPY                        82053
         LA    R14,1(,R14)                                       82053
         LA    R15,1(,R15)                                       82053
         BCT   R0,SHOWINLP                                       82053
SHOWINCL PRTLIST SUMDDNMK,DEV=2  SHOW NEW INCLUDE MASK           82053
SHOWPGNM PRTLIST SUMDDNPG,DEV=2  SHOW DATASET SYSPRINT PAGE NUMBER
OBT3FMT3 VOLREAD DSC3        GET THE FORMAT 3 DSCB               82053
         BXH   R15,R15,OBT3SIZ   SKIP IF ERROR OR NONE           82053
         LR    R15,R0        COPY THE ADDRESS                    82053
         CLI   DS1FMTID-IECSDSL1(R15),C'3'  REALLY A FORMAT 3 ?  82053
         BNE   OBT3FMT3      NO; TRY AGAIN                       82053
         LA    R14,CCHHDSCB-DS1DSNAM(,R15)  POINT TO CCHHR       82053
         STM   R14,R15,DB                                        82053
         OI    DB+4,X'80'    SET END OF LIST BIT                 82053
         LA    R1,DB         POINT TO PARM LIST                  82053
         CALL  DSCB3         CALL FORMATTING ROUTINE             82053
OBT3SIZ  ICM   R2,15,DBWD    PATTERN DSCB ?                      82053
         BZ    TRKUTILP      YES, FIX MY STUPID ERROR            82053
         LR    R1,R2         SHOW FULL IF PDS/E                 GP10187
         TM    DS1SMSFG,DS1PDSE  BUT IS IT A PDS/E ?            GP10187
         BNZ   TRKEMPTY      YES; SHOW FULL                     GP10187
         SLR   R1,R1         CLEAR FOR IC                        91162
         ICM   R1,7,DS1LSTAR   GET TRACKS USED                  GP10187
         BZ    TRKEMPTY      UNUSED OR PDS/E                    GP10187
         SRL   R1,8          ELIMIINATE RECORD NUMBER           GP10187
         LA    R1,1(,R1)     AND UP TRACK COUNT BY ONE
TRKEMPTY MIN   (R1),(R2)     NOT MORE THAN THERE                 91162
         MH    R1,=H'10000'  CONVERT TO PERCENT * 100            82053
         SLR   R0,R0
         DR    R0,R2         DIVIDE BY TRACKS IN EXTENT          82053
         ST    R1,DBWD+4     STASH FOR PRINT                     82053
TRKUTILP PRTLIST TRAKUTIL    PRINT SIZE AND PERCENTAGE USED      82053
         SPACE 1                                                 82053
         L     R3,LIST       GET FIRST TABLE ENTRY ADDRESS       82053
         ST    R3,LISTCUR    SET CURRENT LIST ENTRY              82053
         L     R1,LISTEND    GET END OF ALIAS ENTRIES            82053
         ST    R1,ALIST      SET END OF ALIAS ENTRIES            82053
         ST    R1,ALISTCUR   SET CURRENT ALIAS ENTRY             82053
         MVC   JFCBELNM,WORKLINE+102   CLEAR JFCB NAME           82053
         NI    JFCBIND1,255-JFCPDS  NO MEMBER IN JFCB            82053
         OI    JFCBTSDM,JFCNWRIT+JFCNDSCB+JFCNDCB  NO MERGES     82053
NONSQNAM TM    FLAGS,FGDUPL  DUPLICATE DSN/VOL ?                 81271
         BNZ   SKIPOPEN      YES; SKIP SECOND OPEN               82053
         AIF   (&MVS).NOIND1                                     82195
         TM    DS1DSIND,X'40'    RACF PROTECTED ?                82053
         BNZ   TESTOPEN      YES; BUT SHOULD NOT DO ANYTHING     82053
         TM    DS1DSIND,X'14'    READ PROTECTED ?                82053
         BNM   SKIPOPEN      NO                                  82053
         TM    DS1DSIND,X'10'   DOUBLE-CHECK BIT SETTING         82053
         BZ    SKIPOPEN      BAD BIT SETTING IN DSCB             82053
.NOIND1  ANOP  ,                                                 82053
TESTOPEN OPEN  MF=(E,OPENLIST),TYPE=J   CHECK ACCESS AUTHORIZATION
         CLOSE MF=(E,OPENLIST)   DONE WITH IT                    82053
SKIPOPEN OI    FLAGS,FGDUPL  SET OPEN PERFORMED                  82053
         PRTROOM 2           ALLOW FOR TWO LINES                 82053
         SPACE 1                                                 82053
         TM    DS1NOEPV,255  PATTERN DSCB ?                      82053
         BZ    ENDMEMB       YES; SKIP                           82053
         NI    FLAGS2,255-FGPDSE  RESET PDS/E FLAG              GP99035
         TM    DS1SMSFG,DS1PDSE  BUT IS IT A PDS/E ?            GP99035
         BZ    DIRPDS        YES; TEST LSTAR                    GP99035
         OI    FLAGS2,FGPDSE  FLAG IT AND USE @INPREAD FOR DIRECTORY
         B     DIRPCOM       BECAUSE EXCP WON'T DO IT           GP99035
DIRPDS   CLC   DS1LSTAR(3),ZEROES  INITIALIZED ?                 82053
         BE    ENDMEMB       NO; SKIP                            82053
DIRPCOM  MVC   PDDRECFM,DS1RECFM  COPY RECFM FOR PDSDE CALL      82144
         MVC   PALRECFM,DS1RECFM  DITTO FOR ALIAS ENTRY          82144
         SPACE 1                                                GP99034
DIRLOOP  VOLREAD READ        READ A BLOCK                        82053
DIRLOOP2 CH    R15,=H'4'                                         82053
         BE    DIREOF        END-FILE                            82053
         BH    DIRROR        ERROR                              GP10187
         LR    R6,R0         COPY BLOCK ADDRESS                  82053
         TM    FLAGS2,FGUNAUTH+FGPDSE  @INPREAD RESULT?         GP99035
         BNZ   DIRLOOP3      YES; BLOCK ID MISSING              GP99034
         CLC   =X'080100',5(R6)  DIRECTORY BLOCK COUNT FIELD ?   82053
         BNE   DIRROR        NO; ERROR                           82053
         B     DIRLOOP4                                         GP99034
         SPACE 1                                                 82053
DIRLOOP3 LR    R6,R1         VOLFAKE RETURNS LEN/ADDR IN R0/R1  GP99035
         CH    R0,=H'100'    CORRECT LENGTH FOR DIRECTORY?      GP99035
*DEFER*  BNE   DIRROR        NO; FAIL                           GP99035
         SH    R6,=H'16'     FAKE MISSING BLOCK ID AND KEY      GP99034
DIRLOOP4 INC   SUBDIRA       UP ALLOCATED COUNT                  82053
         TM    FONE,FODEOF   LOGICAL END OF DIRECTORY PASSED ?   82053
         BNZ   DIRLOOP       YES; JUST COUNT ALLOCATED BLOCKS    82053
         INC   SUBDIRU       UP USED COUNT                       82053
         SPACE 1                                                 82053
         LH    R5,16(,R6)    GET LENGTH OF BLOCK                 82053
         SH    R5,=H'2'      ALLOW FOR LENGTH FIELD              82053
         BM    DIRROR        FAIL IF INVALID SIZE                82053
         BZ    DIRLOOP       SKIP IF EMPTY                       82053
         CH    R5,=H'254'    VALID ?                             82053
         BH    DIRROR        OR TOO LONG                         82053
         LA    R4,18(,R6)    POINT TO FIRST ENTRY                82053
         SPACE 1
         USING PDS2,R4       DECLARE THE ENTRY                   82053
DIRLMEM  CLC   PDS2NAME,GENMHIGH  LAST ENTRY ?                   82053
         BE    DIRLEOF       YES; COUNT THROUGH REMAINING BLOCKS 82053
         IC    R6,PDS2INDC   GET THE FLAG AND LENGTH BITS        82053
         N     R6,=A(PDS2LUSR)  MASK TO GET LENGTH               82053
         SLL   R6,1          CONVERT TO BYTES                    82053
         LA    R6,PDS2USRD-PDS2-1(,R6) UP BY FIXED LENGTH -1     82053
         SR    R5,R6         RESIDUAL LENGTH +1                  82053
         BNP   DIRROR        INVALID ENTRY                       82053
         TM    PDS2INDC,PDS2ALIS  ALIAS ENTRY ?                  82053
         BZ    DIRCMEM       NO; PROCESS AS MEMBER               82053
         INC   SUBALI        COUNT ALIAS ENTRY                   82053
         TM    FONE,FOFULL   LIST FULL ?                         82053
         BNZ   MEMFULL       YES; DO NOT STASH ENTRY             82053
         TM    FLAGS,FGDOAL  TREAT ALIAS ENTRY AS MEMBER ?       82053
         BNZ   DIRCMEM2      YES, BUT SKIP COUNT                 82053
         L     R3,ALISTCUR   GET CURRENT LIST ENTRY              82053
         C     R3,LISTCUR    SPILL INTO ALIAS LIST ?             82053
         BL    SETFULL       YES; SET LIST FULL                  82053
         LR    R15,R3        COPY                                82053
         SH    R15,=Y(LISTLEN)                                   82053
         ST    R15,ALISTCUR  SET NEXT AVAILABLE ENTRY            82053
         B     DIRCMEM3      MOVE IT                             82053
SETFULL  OI    FONE,FOFULL   SET LIST FULL                       82053
         B     MEMFULL                                           82053
DIRCMEM  INC   SUBMEM        COUNT MEMBERS                       82053
         TM    FONE,FOFULL   LIST FULL ?                         82053
         BNZ   MEMFULL       YES; DO NOT STASH ENTRY             82053
DIRCMEM2 L     R3,LISTCUR    GET PRIOR ENTRY                     82053
         C     R3,ALISTCUR   SPILL INTO MEMBER LIST ?            82053
         BH    SETFULL       YES; QUIT                           82053
         LA    R15,LISTLEN(,R3)                                  82053
         ST    R15,LISTCUR   SET NEXT AVAILABLE ENTRY            82053
*IRCMEM3 XC    0(LISTLEN,R3),0(R3)                               82053
DIRCMEM3 EX    R6,MVCMEM                                         82053
         SPACE 1
MEMFULL  LA    R4,1(R4,R6)
         BCT   R5,DIRLMEM    DO NEXT MEMBER                      82053
         B     DIRLOOP       ELSE READ ANOTHER BLOCK             82053
         DROP  R4                                                82053
DIRLEOF  OI    FONE,FODEOF   SET LOGICAL END                     82053
         B     DIRLOOP       COUNT ALLOCATED BLOCKS ONLY         82053
         SPACE 1                                                 82053
DIREOF   TM    FONE,FOFULL   DID LIST OVERFLOW ?                 82053
         BZ    EOFCOUNT      NO; JUST PRINT COUNTS
         L     R0,SUBMEM     GET MEMBERS READ                    82053
         A     R0,SUBALI       PLUS ALIAS ENTRIES                82053
         S     R0,NUMSPACE   LESS NUMBER STORED                  82053
         MH    R0,=Y(LISTLEN)  STORAGE REQUIRED                  82053
         AIF   (&MVS OR '&SYSTEM 'EQ' MVT' OR '&SYSTEM' EQ 'MFT').DO4
         A     R0,=A(64*1024-1)  VALUE TO ROUND                  82053
         SRL   R0,16         TRUNCATE TO 64K BOUNDARY            82053
         SLL   R0,6          ADJUST FOR DECIMAL K UNITS          82053
         AGO   .DON4                                             82053
.DO4     A     R0,=A(4*1024-1)   VALUE TO ROUND                  82053
         SRL   R0,12         TRUNCATE TO 4K BOUNDARY             82053
         SLL   R0,2          MAKE DECIMAL K UNITS                82053
.DON4    ST    R0,DB         SAVE OR FORMATTING                  82053
         PRTLIST ERRFULL,DEV=(1,2)   PRINT FULL MESSAGE          82053
EOFCOUNT PRTLIST MSGCNTRS    PRINT STATISTICS                    82053
         LA    R0,LISTLEN    GET ENTRY SIZE                      82053
         L     R1,LISTCUR    GET NEXT AVAILABLE ENTRY            82053
         SR    R1,R0         SET LAST ENTRY                      82053
         ST    R1,LISTCUR                                        82053
         L     R1,ALISTCUR   DITTO FOR ALIAS LIST                82053
         AR    R1,R0                                             82053
         ST    R1,ALISTCUR                                       82053
         SLR   R0,R0         SET UNPROCESSED ALAS ENTRIES        82053
         TM    FONE,FOFULL   LIST OVERFLOW ?                     82053
         BNZ   NOORPH        YES; SKIP SEPARATE ALIAS CHECK      82053
         TM    FLAGS,FGDOAL  PROCESS ALIAS ENTRIES AS MEMBERS ?  82053
         BNZ   NOORPH        DITTO; NO SEPARATE ALIAS PROCESSING 82053
         L     R0,SUBALI     GET NUMBER OF ALIAS ENTRIES         82053
NOORPH   ST    R0,UNPRALI    SET COUNT OF UNPROCESSED ALIAS ENTRIES
         L     R1,SUBMEM     GET NUMBER OF MEMBERS               82053
         A     R1,SUBALI     ADD NUMBER OF ALIAS ENTRIES         82053
         BP    SORT0         HAVE SOME; PROCESS                  82053
         PRTV  ERRNOMEM,DEV=(1,2)  GET NO MEMBER MESSAGE         82053
         OI    RETCC,CCWARN                                      82053
         B     ENDMEMB                                           82053
         SPACE 1                                                 82053
DIRROR   PRTV  DIRIOERR,DEV=(1,2)  ELSE SIGNAL ERROR             82053
         B     ENDMEMB                                           82053
         SPACE 2                                                 82053
LISTLEN  EQU   74
         SPACE 2                                                 82144
*        SORT PDS ENTRIES IN TTR SEQUENCE IF REQUESTED           82053
*
SORT0    TM    FLAGS,FGTTR   TTR SORT ?                          82053
         BZ    SORTALIS      NO; SORT ALIAS ENTRIES              82053
         L     R1,LIST       SET START OF LIST                   82053
         L     R2,LISTCUR    GET LAST ENTRY                      82053
         LA    R3,LISTLEN    LENGTH OF ONE ENTRY                 82053
         CR    R1,R2         ANY ENTRIES TO DO ?                 82053
         BNL   SORTALIS      NOTHING TO DO                       82053
         STM   R1,R3,SORTPARM  MAKE SORT PARAMETER LIST          82053
         OI    SOPHIGH,SOPFAD  INDICATE END ADDRESS              82053
         MVI   SOPSEQ,C'A'   ASCENDING                           82053
         MVI   SOPCOO,PDS2TTRP-PDS2  OFFSET TO TTR               82053
         MVI   SOPCOL,L'PDS2TTRP   LENGTH OF TTR                 82053
         SERVCALL SORTH,SORTPARM   SORT BY TTR                   82053
         SPACE 1                                                 82053
*        ALIAS ENTRIES ARE SORTED IN REVERSE TTR SEQUENCE.       82053
*              BUBBLE SORT IS USED TO KEEP NAMES IN SEQUENCE     82053
*                                                                82053
SORTALIS L     R1,ALISTCUR   GET ALIAS LIST                      82053
         L     R2,ALIST      AND END                             82053
         LA    R3,LISTLEN                                        82053
         STM   R1,R3,SORTPARM                                    82053
         OI    SOPHIGH,SOPFAD  INDICATE END ADDRESS              82053
         MVI   SOPSEQ,C'D'   ALIAS ENTRIES ARE SAVED IN REVERSE ORDER
         MVI   SOPCOO,PDS2TTRP-PDS2  SET FOR TTR                 82053
         MVI   SOPCOL,L'PDS2TTRP     TTR LENGTH                  82053
         CR    R1,R2         ANY ?                               82053
         BNL   PROCESS       NO                                  82053
         SERVCALL SORTB,SORTPARM   SORT (LEAVE NAMES IN SEQUENCE)
         EJECT
***********************************************************************
*                                                                     *
*    DIRECTORY NOW IN MEMORY; MEMBER LIST SORTED BY NAME OR TTR.      *
*        ALIAS ENTRIES IN SECOND LIST, SORTED BY TTR.                 *
*    LOOP THROUGH ENTRIES CHECKING FOR SELECTION/REJECTION CRITERIA.  *
*                                                                     *
***********************************************************************
PROCESS  LM    R5,R6,LIST    GET LIST START AND CURRENT END      82053
         CR    R5,R6         ANY ENTRIES ?                       82053
         BH    PRTWID        NO ?                                82053
         LR    R7,R6         SET BXLE END ADDRESS                82053
         LA    R6,LISTLEN    SET BXLE INCREMENT                  82053
         USING PDS2,R5                                           82053
PRINTLP  MVI   BLKSTART,C' '                                     82053
         MVC   BLKSTART+1(BLKLEN-1),BLKSTART  BLANK OPTIONAL FIELDS
         ST    R5,PDDNEXT    SET CURRENT MEMBER ADDRESS          82144
         SERVCALL PDSDE,PDDNEXT  CHECK TYPE                      82144
         LR    R15,R0        COPY EXTENDED FIELD ADDRESS         82144
         MVC   PDDRALIS(PDDRBLNK),0(R15)  COPY EXTENDED FIELDS   82144
         XC    PACKSSI,PACKSSI  CLEAR PACKED SSI FIELD           82144
         XC    CURAPF,CURAPF  CLEAR APF CODE                     82144
         SLR   R15,R15                                           82144
         ICM   R15,3,PDDOSSI  SSI PRESENT ?                      82144
         BZ    PRTLNSSI      NO                                  82144
         LA    R1,0(R15,R5)  GET SSI ADDRESS                     82144
         MVC   PACKSSI,0(R1)  SAVE SSI IN PACKED FORM            82144
PRTLNSSI ICM   R15,3,PDDOAPF  APF PRESENT ?                      82144
         BZ    PRTLNAPF      NO                                  82144
         LA    R1,0(R15,R5)  POINT TO APF CODE                   82144
         MVC   CURAPF,0(R1)  SAVE IT                             82144
PRTLNAPF SLR   R2,R2         PRESET FOR NO ALIAS                 82144
         SLR   R3,R3                                             82053
         STM   R2,R3,@ALIASN  CLEAR ALIAS POINTERS               82053
         ICM   R0,15,UNPRALI  ANY ALIAS ENTRIES ?                82053
         BNP   PRTAL5        NO; SKIP                            82053
         LA    R1,PDS2TTRP   POINT TO KEY                        82053
         ST    R1,SOPKEY                                         82053
         SERVCALL BINLK,SORTPARM  LOCATE BY TTR                  82053
         BXH   R15,R15,PRTAL5   NO ALIAS ENTRIES                 82053
         LR    R3,R0         SAVE ALIAS ENTRY                    82053
         NI    PDS2INDC-PDS2(R3),255-PDS2ALIS  SHOW PROCESSED    82053
PRTAL1   LA    R1,LISTLEN(,R3)   NEXT ENTRY ADDRESS              82053
         C     R1,ALIST      PAST END ?                          82053
         BH    PRTAL2        YES                                 82053
         CLC   PDS2TTRP-PDS2(L'PDS2TTRP,R1),PDS2TTRP  SAME TTR ? 82053
         BNE   PRTAL2        NO; HAVE LAST ALIAS                 82053
         LR    R3,R1         SET NEW ENTRY                       82053
         NI    PDS2INDC-PDS2(R3),255-PDS2ALIS  SHOW PROCESSED    82053
         B     PRTAL1                                            82053
PRTAL2   LR    R2,R0         COPY ALIAS ADDRESS                  82053
PRTAL3   LR    R1,R2                                             82053
         SR    R1,R6         GET NEXT HIGHER ENTRY               82053
         C     R1,ALISTCUR   PAST END ?                          82053
         BL    PRTAL4        YES                                 82053
         CLC   PDS2TTRP-PDS2(L'PDS2TTRP,R1),PDS2TTRP  SAME TTR ? 82053
         BNE   PRTAL4        NO                                  82053
         LR    R2,R1         SAVE ADDRESS                        82053
         NI    PDS2INDC-PDS2(R2),255-PDS2ALIS  SHOW PROCESSED    82053
         B     PRTAL3        TRY AGAIN                           82053
PRTAL4   STM   R2,R3,@ALIASN  SAVE ALIAS POINTERS                82053
         SR    R3,R2         GET EXTENT                          82053
         SLR   R2,R2                                             82053
         DR    R2,R6         GET NUMBER OF ENTRIES -1            82053
         L     R0,UNPRALI    GET UNPROCESSED ALIAS ENTRIES       82053
         BCTR  R0,0          SUBTRACT FOR FIRST ONE              82053
         SR    R0,R3         AND ADDITIONAL ONES                 82053
         ST    R0,UNPRALI    SET UNPROCESSED COUNT               82053
PRTAL5   LR    R3,R5         COPY CURRENT MEMBER ADDRESS         82053
         BAS   R8,CHECKPAL   TO BE SELECTED ?                   GP99033
         B     NOSKIP        YES; PROCESS IT                     82053
         TM    PDDTYPE,PDDTLKED   LKED ENTRY ?                   82144
         BNZ   PRTINC        YES; DO NOT PROCESS                GP10187
         TM    PDDTYPE,PDDTDTX  IEBUPDTX DIRECTORY ENTRY ?       82144
         BZ    PRTAL5Z       NO                                  82053
         LA    R3,DTXMODNM   POINT TO PRODUCTION NAME            82053
         BAS   R8,CHECKMEM   TEST IT                            GP99033
         B     NOSKIP        MATCH; PROCESS IT                   82053
PRTAL5Z  ICM   R3,15,@ALIAS1  GET FIRST ALIAS                    82053
         BZ    PRTINC        NONE; SKIP THIS ENTRY               82053
PRTAL6   BAS   R8,CHECKPAL   TO BE SELECTED ?                   GP99033
         B     NOSKIP        YES; DO IT                          82053
         LR    R4,R3         SAVE CURRENT ADDRESS                82053
         TM    PDDTYPE,PDDTDTX   IEBUPDTX DIRECTORY ENTRY ?      82144
         BZ    PRTAL6Z       NO                                  82053
         LA    R3,DTXMODNM-PDS2(,R4)   POINT TO PRODUCTION NAME  82053
         BAS   R8,CHECKMEM   TEST IT                            GP99033
         B     NOSKIP        MATCH; PROCESS IT                   82053
PRTAL6Z  LR    R3,R4         RESTORE ALIAS POINTER               82053
         SR    R3,R6         GET PRIOR ENTRY                     82053
         C     R3,@ALIASN    PAST LAST ENTRY ?                   82053
         BNL   PRTAL6        NO; TEST NEXT ONE                   82053
         B     PRTINC        NO MATCH; SKIP THIS ENTRY           91046
         SPACE 1
***********************************************************************
*                                                                     *
*    A MEMBER OR ALIAS ENTRY MATCHES ALL SELECTION CRITERIA.          *
*        FORMAT THE MEMBER DATA ON SYSPRINT; PUNCH THE                *
*           MEMBER INDEX ENTRY ON SYSPUNCH.                           *
*                                                                     *
***********************************************************************
NOSKIP   LA    R1,MEMTTR     FORMAT TTR                          82053
         LA    R2,PDS2TTRP                                       82053
         BAS   R9,HEXTTR                                        GP99033
         LR    R3,R5         COPY THE MEMBER POINTER             91046
         PRTLIST PUNMEM,DEV=4  PUNCH THE MEMBER DATA CARD       GP10187
         CLI   PDDUDLEN,2    SSI ?                               82144
         BNH   NOSKLINE      SSI ONLY OR NOTHING; NO SPACING     82053
         TM    PDDTYPE,PDDTSPF   SPF FORMAT ?                    82144
         BNZ   PRINTSPF      YES; DO ON ONE LINE                GP09274
         LA    R1,3          RESERVE THREE LINES                 82053
         CLI   PDDUDLEN,28   MORE THAN ONE LINE ?                82144
         BNH   NOSKROOM      NO                                  82053
         LA    R1,1(,R1)     UP IT                               82053
NOSKROOM OI    FONE,FOSKIP   DOUBLE-SPACE LISTING                82053
         PRTROOM (R1)        RESERVE CONTIGUOUS LINES            82053
NOSKLINE BAS   R9,ALOUT      PRINT MEMBER AND ALIAS LINES       GP99033
         CLI   PDDUDLEN,2    NONE, SHORT, OR SSI ONLY ?          82144
         BNH   PRTINC        YES; DONE WITH THIS MEMBER          82053
         OI    FONE,FOSKIP   DOUBLE-SPACE AFTER THIS ENTRY       82053
         SPACE 2                                                 82053
*        FORMAT USER DATA FOR NON-LKED ENTRIES                   82053
*                                                                82053
DODATA   MVC   WORKLINE,WORKLINE-1                               82053
         TM    PDDTYPE,PDDTDTX  IEBUPDTX FORMAT ?                82144
         BZ    DODATANX      NO                                  82053
         PRTLIST PUTDTX      FORMAT IT                           82053
         B     PRTINC        DO NEXT ENTRY                       82053
         SPACE 1                                                 82053
DODATANX TM    PDDTYPE,PDDTSPF   SPF FORMAT ?                    82144
         BZ    DODATANS      NO                                  82144
PRINTSPF MVC   DB(2),SPFMODTM  COPY HHMM TIME                    82053
         MVC   DB+2(2),=X'000F'  MAKE FAKE SIGN                  82053
         PRTLIST PUTSPF      HOPE FOR THE BEST ?                 82053
         B     PRTINC                                            82053
         SPACE 1                                                 82053
DODATANS LA    R4,PDS2USRD                                       82053
         LA    R3,WORKLINE                                       82053
         SLR   R1,R1
         IC    R1,PDDUDLEN   GET DATA LENGTH IN HALFWORDS        82144
         SLL   R1,1          DOUBLE TO GET BYTE LENGTH           82053
         BAS   R9,HEXCNVT                                        82053
         SLR   R2,R2         CLEAR AGAIN                         82053
         IC    R2,PDDUDLEN   GET HALFWORD COUNT BACK             82144
         SLL   R2,2          GET BYTE COUNT FOR OUTPUT           82053
         LR    R3,R2         COPY OVER                           82053
         LA    R1,28*4       SET MAXIMUM LENGTH ON ONE LINE      82053
         CR    R2,R1         MORE THAN ONE LINE REQUIRED ?       82053
         BNH   DODATAP       NO                                  82053
         SR    R3,R1         MAKE LENGTH ON SECOND LINE          82053
         LR    R2,R1         AND LENGTH ON FIRST LINE            82053
DODATAP  PRTLIST PUTDATA     PUT THE DATA LINES                  82053
         SPACE 1
PRTINC   BXLE  R5,R6,PRINTLP  PROCESS NEXT MEMBER                82053
***********************************************************************
*                                                                     *
*    ALL MEMBER ENTRIES HAVE BEEN PROCESSED.  CHECK HERE FOR ALIAS    *
*        ENTRIES THAT HAVE NOT BEEN PROCESSED - WRITE ERROR MESSAGE   *
*        AND SWITCH LISTS, PRINTING WIDOWED ALII AS MEMBERS.          *
*                                                                     *
***********************************************************************
PRTWID   ICM   R0,15,UNPRALI  CHECK UNPROCESSED ALIAS ENTRIES    82053
         BNP   ENDMEM        NONE                                82053
         TM    FONE,FORPHAN  BEEN HERE BEFORE ?                  82053
         BNZ   PRTWIDO       YES                                 82053
         OI    FONE,FORPHAN+FOSKIP  SET FIRST TIME FLAG          82053
         PRTSPACE 1          DOUBLE-SPACE                        82053
         PRTLIST PUTORPH,DEV=(1,2)  WRITE ERROR MESSAGE          82053
PRTWIDO  LA    R4,LISTLEN    GET LIST ENTRY LENGTH               82053
         L     R5,ALISTCUR   GET LOWEST (LAST) ALIAS ENTRY       82053
         SR    R5,R4         LAST PLACE TO STORE NEW MEMBER      82053
         L     R6,LIST       FIRST ADDRESS FOR NEW MEMBER        82053
         L     R7,ALIST      GET FIRST (HIGHEST) ALIAS           82053
         LNR   R8,R4         MAKE ALIAS INCREMENT (BACKWARDS)    82053
         LR    R9,R5         LAST ALIAS ENTRY-1 TO PROCESS       82053
         SLR   R2,R2         FIRST TIME FLAG FOR NO ROOM         82053
         SLR   R3,R3         FIRST TIME FLAG                     82053
         AR    R7,R4         ALLOW FOR FIRST BXH                 82053
         CR    R6,R5         ROOM FOR AT LEAST ONE MEMBER ?      82053
         BL    PRTWIDSF      YES                                 82053
         LR    R2,R6         SET FLAG FOR NO ROOM                82053
         AR    R5,R4         FORCE ONE ENTRY (LOSE LAST ALIAS)   82053
         AR    R9,R4         DITTO                               82053
         ST    R5,ALISTCUR                                       82053
PRTWIDSF SR    R6,R4         ALLOW FOR FIRST BXH                 82053
PRTWIDLP BXLE  R7,R8,PRTWIDGP  GET NEXT ALIAS ENTRY OR OUT       82053
PRTWIDLT TM    PDS2INDC-PDS2(R7),PDS2ALIS  UNPROCESSED ALIAS ?   82053
         BZ    PRTWIDLP      NO; TRY AGAIN                       82053
         LTR   R3,R3         FIRST TIME ?                        82053
         BNZ   PRTWIDNF      NO                                  82053
         LR    R3,R7         SET FLAG NON-ZERO                   82053
         ST    R7,ALIST      SET NEW HIGH FOR FASTER SORT        82053
PRTWIDNF BXH   R6,R4,PRTWIDGO  ROOM TO COPY ANOTHER ?            82053
         INC   UNPRALI,INC=-1  FIX UNPROCESSED COUNT             82053
         LTR   R2,R2         ALL ALIAS, NO MEMBER ENTRIES ?      82053
         BZ    PRTWIDNM      NO; AT LEAST ONE MEMBER             82053
         XC    0(LISTLEN,R7),0(R2)                               82053
         XC    0(LISTLEN,R2),0(R7)                               82053
         XC    0(LISTLEN,R7),0(R2)  SWAP ALIAS AND NEW MEMBER ENTRY
         SLR   R2,R2         SHOW SWAP DONE                      82053
         B     PRTWIDNS      CONTINUE                            82053
PRTWIDNM MVC   0(LISTLEN,R6),0(R7)  MOVE ALIAS TO MEMBER ENTRY   82053
         XC    0(PDS2USRD-PDS2,R7),0(R7)  CLEAR OLD ENTRY        82053
PRTWIDNS BXLE  R7,R8,PRTWIDGP  EXIT IF NO MORE                   82053
         CLC   PDS2TTRP-PDS2(L'PDS2TTRP,R7),PDS2TTRP-PDS2(R6) TTR= ?
         BNE   PRTWIDLT      NO; SEE IF PROCESSED                82053
         B     PRTWIDNS      ELSE SKIP OVER IT                   82053
PRTWIDGO SR    R6,R4         SPACE TO LAST ENTRY STORED          82053
PRTWIDGP ST    R6,LISTCUR    SET LAST ENTRY TO PROCESS           82053
         C     R6,LIST       ANY TO DO ?                         82053
         BNL   SORTALIS      YES; SORT REMAINING ALIAS ENTRIES   82053
*          IF WE FALL THROUGH, THERE IS AN ERROR IN THE LOGIC ?  82053
         SPACE 1                                                 82053
ENDMEM   TM    ERRUNIT,OUSUM   SUMPRINT AVAILABLE ?              82053
         BNZ   ENDIR         YES; NO TITLE CHANGE                82053
         PRTLIST SUMTIT1,TITLE=1                                 82053
         PRTLIST NULLLINE,TITLE=2                                82053
ENDIR    LA    R1,TOTDIRA    POINT TO TOTALS FOR RUN             82053
         LA    R2,SUBDIRA    POINT TO DSN TOTALS                 82053
         LA    R3,SUBCOUNT   GET NUMBER OF COUNTERS              82053
ENDMEMA  L     R0,0(,R1)                                         82053
         A     R0,0(,R2)                                         82053
         ST    R0,0(,R1)     UPDATE DSN COUNTERS                 82053
         LA    R1,4(,R1)                                         82053
         LA    R2,4(,R2)                                         82053
         BCT   R3,ENDMEMA                                        82053
ENDMEMB  PRTLIST SUMDSN,DEV=2  PRINT DATASET SUMMARY             82053
         B     LOOPTIOT      THEN GET NEXT DATASET               82053
         SPACE 2                                                 82053
*        PRINT THE MEMBER LINE, ALIAS ENTRIES AND SET SPACING    82053
*                                                                82053
ALOUT    TM    FONE,FOSKIP  EXTRA LINE SKIP REQUIRED ?           82053
         BZ    ALOUTH        NO                                  82053
         PRTSPACE 1          DOUBLE-SPACE                        82053
         NI    FONE,255-FOSKIP  RESET                            82053
ALOUTH   SERVCALL DSMEM,PDDRNAME  VALID MEMBER NAME ?            82144
         BXLE  R15,R15,ALOUTM   YES; PRINT IT NORMALLY           82053
         PRTLIST PUTMEMH     ELSE DISPLAY MEMBER IN HEX          82053
         B     ALOUTN        GO TO COMMON                        82053
ALOUTM   PRTLIST PUTMEM                                          82053
ALOUTN   ICM   R3,15,@ALIAS1  GET ADDRESS OF FIRST ALIAS ENTRY   82053
         BZ    ALOUTX        NONE                                82053
ALOUTL   PRTLIST PUNMEM,DEV=4  PUNCH AN ALIAS DATA CARD         GP10187
         SERVCALL DSMEM,PDS2NAME-PDS2(R3)  VALID NAME ?          82053
         BXLE  R15,R15,ALOUTO  YES; PRINT IT                     82053
         PRTLIST PUTALIAH   ELSE PRINT IN HEX                    82053
         B     ALOUTQ                                            82053
ALOUTO   CLI   PDS2NAME-PDS2+7(R3),X'C0'  SVC NAME ?             82053
         BNE   ALOUTP        NO                                  82053
         CLC   =C'IGC',PDS2NAME-PDS2(R3)  REALLY ?               82053
         BNE   ALOUTP        NO                                  82053
         MVI   PDS2NAME-PDS2+7(R3),C'+'  MAKE IT PRETTY          82053
ALOUTP   PRTLIST PUTALIAS    DISPLAY ALIAS NAMES                 82053
ALOUTQ   SR    R3,R6         GET PRIOR ENTRY                     82053
         C     R3,@ALIASN    DONE ALL ?                          82053
         BNL   ALOUTL        NO                                  82053
ALOUTX   CLC   PDS2TTRP,DS1LSTAR  IS TTR LOWER THAN LSTAR ?      82053
         BLR   R9            YES; OK                             82053
         PRTV  ERRLSTAR      NO; NASTY MESSAGE                   82053
         OI    FONE,FOSKIP   DOUBLE-SPACE NEXT ENTRY             82053
         BR    R9                                                82053
         EJECT
*
*        CONVERT BINARY TTR TO TTTT.RR
*
HEXTTR   UNPK  1(7,R1),0(4,R2)
         MVI   7(R1),C' '
         TR    1(6,R1),HEXTAB                                    82053
         MVC   0(4,R1),1(R1)
         MVI   4(R1),C'.'
         BR    R9                                                82053
         SPACE 2
*
*        CALL HEXCNVT FROM LIST
*
HEXLIST  SLR   R1,R1
         IC    R1,0(,R14)
HEXLOOP  BAS   R9,HEXCNVT                                       GP99033
         LA    R14,1(,R14)
         ICM   R1,1,0(R14)   LOAD AND TEST NEXT LENGTH BYTE      82053
         BZR   R8            END OF LIST; RETURN                 82053
         LA    R3,1(,R3)
         B     HEXLOOP
         SPACE 2
*
*        CONVERT FROM INTERNAL HEX TO BCD HEX DATA
*
HEXCNVT  CH    R1,H7
         BL    HEX1
         UNPK  0(15,R3),0(8,R4)
         TR    0(14,R3),HEXTAB                                   82053
         LA    R3,14(,R3)
         LA    R4,7(R4,0)
H7       EQU   *-2
         SH    R1,H7
         BP    HEXCNVT
         B     HEX2
HEX1     LA    R2,0(R1,R1)
         SLA   R2,4
         LA    R2,0(R1,R2)
         EX    R2,UNPK
         LA    R2,0(R1,R1)
         BCTR  R2,0
         EX    R2,TRT
         LA    R3,1(R2,R3)
         LA    R4,0(R1,R4)
HEX2     MVI   0(R3),C' '
         BR    R9                                                82053
         SPACE 2                                                 82053
OPTMOVE  CLI   0(R4),0       IS THERE AN ATTRIBUTE IN THE TABLE ?
         BE    OPTSKIP       NO, SKIP OVER IT
         IC    R15,0(,R4)    GET LENGTH - 1 OF ATTRIBUTE
         MVI   0(R3),C','    PUT SEPARATOR CHAR. INTO BUFFER
         EX    R15,OPTMVC    MOVE THE ATTRIBUTE
         LA    R3,3(R3,R15)     POINT TO NEW OUTPUT AREA
OPTSKIP  IC    R15,0(,R4)    GET LENGTH OF ATTR. - 1
         LA    R4,2(R4,R15)     SKIP TO NEXT TABLE ENTRY
         BR    R14           RETURN
OPTMVC   MVC   2(0,R3),1(R4)   MOVE LKED ATTRIBUTE FROM TABLE TO O/P
         SPACE 1                                                 82053
         DROP  R5                                                82053
         USING PDS2,R3                                           82053
CHECKPAL ST    R3,PALNEXT    SET ALIAS ADDRESS (OR MAIN)         82144
         SERVCALL PDSDE,PALNEXT  DECODE                          82144
         SLR   R15,R15                                           82144
         STCM  R15,15,PACKSSI  CLEAR SSI WORK FIELD              82144
         ICM   R15,3,PALOSSI   SSI PRESENT ?                     82144
         BZ    CHECKSSI      NO; CHECK AGAINST ZEROES            82144
         LA    R1,0(R15,R3)  POINT TO SSI                        82144
         MVC   PACKSSI,0(R1)  MOVE PACKED SSI                    82144
CHECKSSI CLC   PACKSSI,PACKSSIL  SSI IN RANGE ?                  82144
         BL    4(,R8)        NO; SKIP                            82144
         CLC   PACKSSI,PACKSSIH  IN RANGE ?                      82144
         BH    4(,R8)        NO                                  82144
         CLC   SSIMASK,GENMATCH  SSI SELECTION ?                 91046
         BE    CHECKMEM      NO; SKIP SSI TEST                   91046
         UNPK  DB(9),PACKSSI(5)  UNPACK SSI TO GET ZEROES WHERE  82144
         TR    DB,HEXTAB     PALRSSI WOULD HAVE BLANKS           82144
         LA    R14,SSIMASK   LOAD USER'S MASK ADDRESS            82144
         LA    R15,DB        POINT TO SSI                        82144
         BAS   R9,COMPMSK8   COMPARE 8 BYTES                    GP99033
         BNE   4(,R8)        NO MATCH; SKIP                      91046
         SPACE 1                                                 82144
CHECKMEM CLC   PDS2NAME,LONAME   IS NAME IN RANGE ?              82053
         BL    4(,R8)        NO; IGNORE IT                       82053
         CLC   PDS2NAME,HINAME   INSIDE RANGE ?                  82053
         BH    4(,R8)        NO; RETURN                          82053
         LA    R14,MINCLUDE  LOAD MASK                           82053
         LA    R15,PDS2NAME  POINT TO MEMBER NAME                82053
         BAS   R9,COMPMSK8   COMPARE MEMBER TO MASK             GP99033
         BNE   4(,R8)        SKIP; NO MATCH                      91046
         CLC   MEXCLUDE,GENMATCH  EXCLUSION MASK SPECIFIED ?     81271
         BE    MATCHDON      NO; SKIP TEST                       81271
         LA    R14,MEXCLUDE  LOAD EXCLUSION MASK                 81271
         LA    R15,PDS2NAME  POINT TO MEMBER NAME                82053
         BAS   R9,COMPMSK8   TEST FOR MATCH                     GP99033
         BE    4(,R8)        MATCH; SKIP THIS ONE                91046
MATCHDON ICM   R2,15,CSCBPTR  SEE IF CIB PROCESSING INITIALIZED GP97238
         BZ    NOTCIB        NO                                  82053
         PUSH  USING                                            GP97238
         USING CHAIN,R2                                          82053
         AIF   (NOT &MVSESA).CMESAPS                            GP97238
         L     R9,CHCSCXP    GET THE CSCB EXTENSION             GP97238
         USING CSCX,R9       DECLARE IT                         GP97238
.CMESAPS TM    CHECB,X'50'   'STOP' ?  OR MODIFY ?               82053
         BO    KISSOFF       STOP                                82053
         BZ    NOTCIB        NEITHER                             82053
         L     R1,CHCIBP                                         82053
         USING CIB,R1                                            82053
         CLI   CIBVERB,CIBSTOP                                   82053
         BE    KISSOFF                                           82053
         QEDIT ORIGIN=CHCIBP,BLOCK=(1)                           82053
         PRTLIST CONRESP,DEV=5   RESPOND WITH CURRENT DD/MEMBER  82053
         POP   USING                                            GP97238
NOTCIB   INC   SUBMSEL       COUNT SELECTED MEMBER              GP97238
         BR    R8            RETURN WITH MATCH                   82053
         DROP  R3                                                82053
         USING PDS2,R5                                           82053
         SPACE 1                                                 82053
KISSOFF  PRTL  '0RUN TERMINATED BY STOP COMMAND',DEV==ERRUNIT   GP03233
         OI    RETCC,CCERROR                                     82053
ENDALL   PRTROOM 5,DEV=2                                         82053
         TM    FONE,FODDN    ANY DD CARDS PROCESSED ?            81343
         BNZ   ENDALLDD      YES                                 81343
         MVI   RETCC,CCFATAL  SET FATAL ERROR                    81343
         PRTV  MSGNODDS,DEV==ERRUNIT  WRITE NASTY MESSAGE        81343
ENDALLDD PRTSPACE 2,DEV=2                                        81343
         PRTLIST SUMTOT,DEV=2                                    82053
EXIT     SERVTERM ,          CLOSE AND FREE EVERYTHING           82053
         L     R9,RETCODE    GET RETURN CODE                    GP10187
         PGMEXIT RC=(R9)     RETURN WITH CONDITION CODE          82053
         SPACE 1                                                 81271
*        MASK COMPARE                                            81271
*        R14 - MASK ADDRESS; R15 - TEXT STRING; R0 - LENGTH      81271
*          RETURN ON R9; CC = IF MATCH, NOT = IF UNMATCHED       91046
*                                                                81271
COMPMSK8 LA    R0,8          SCAN 8 BYTE MEMBER NAME             81271
COMPMASK LTR   R0,R0         VALID LENGTH ?                      81271
         BNP   COMPMASQ      NO; TREAT AS MATCH                  91046
COMPMASL CLI   0(R14),C'-'   ANY MATCH IN THIS POSITION ?        81271
         BE    COMPMASB      YES; PERMIT                         81271
         CLC   0(1,R14),0(R15)  MATCH ?                          81271
         BNER  R9            NO; RETURN UNMATCHED                81271
COMPMASB LA    R14,1(,R14)   TRY NEXT BYTE                       81271
         LA    R15,1(,R15)                                       81271
         BCT   R0,COMPMASL   REPEAT FOR NEXT BYTE                81271
COMPMASQ XR    R15,R15       SET RETURN CODE =                   91046
         BR    R9            RETURN MATCHED                      91046
         SPACE 1                                                 81271
*        CHECK DSN FOR DSORG, AND USABILITY                      82053
*          RETURN 0(R9) - UNUSABLE, (R3) HAS ERROR MESSAGE POINTER
*           ELSE 4(R9) - USABLE                                  81271
CHECKDS1 SERVCALL DSFMT,DS1DSORG   FORMAT DSORG/DCB VALUES       82053
         MVC   FMTDSORG(FMTORGLN),0(R1)  SAVE FOR MESSAGES       82053
         MVC   PDSDSNEQ,=C'PDS='  RESET DSN HEADER               82053
         TM    DS1DSORG,JFCORGPO  PDS ?                          82053
         BNZ   *+10          YES; LEAVE IT                       82053
         MVC   PDSDSNEQ,=C'DSN='  RESET DSN HEADER               82053
         LA    R3,MDSNORG    PRESET FOR BAD DSORG                82053
         CLI   DS1DSORG+1,0   ANY FUNNY ORG ?                    81271
         BNER  R9            YES; REJECT                         81271
         TM    DS1DSORG,JFCORGPO  PARTITIONED ?                  82053
         BZR   R9            NO; REJECT                          81271
         TM    DS1DSORG,255-JFCORGPO-JFCORGU   ANYTHING ELSE ?   82053
         BNZR  R9            REJECT ANYTHING ELSE                81271
         LA    R3,MDSNNEX    SET FOR NO EXTENTS                  82053
         CLI   DS1NOEPV,0    ANY EXTENTS ?                       81271
         BER   R9            NO; REJECT                          81271
         LA    R3,MDSNDSCB   SET FOR OTHER DSCB ERROR            81271
         CLC   ZEROES(3),DS1LSTAR  EVER USED ?                   81271
         BER   R9            NO; TOO BAD                         81271
         B     4(,R9)        YES; PROCESS IT                     81271
         EJECT ,                                                 82053
         LTORG ,                                                 82053
         SPACE 2                                                 82053
         PRINT NOGEN                                             81271
SUMTIT1  FDOPT NL,CC=C'#'    SET DATE/TIME/PAGE OPTION           81271
SUMTIT1P FDPRT 'D I S T B L D L  ***',RADJ,LEN=18                82053
SUMTIT1S FDPRT 'SUMMARY AND ERROR LISTING',RADJ,LEN=45           81271
         FDPRT *END                                              82053
         SPACE 1                                                 82053
PRTTIT1  FDOPT NL,CC=C'#'    AUTO PAGE NUMBERING                 82053
         FDPRT 'DISTBLDL DIRECTORY LISTING, SORTED BY',PADR     GP10187
         FDEXEC FOPTDDS,FOPTDDX  PRINT NAME/TTR                  82053
         FDPRT 'DDNAME',RADJ,LEN=10                              82053
         FDPRT CURDDNM,PAD   DDNAME                              82053
         FDPRT *END                                              82053
         SPACE 1                                                 82053
PRTTIT2  FDPRT 'PAGE #S# FOR',NL                                 82053
         FDPRT PDSDSNEQ,PADL                                     82053
         FDPRT DS1DSNAM      SHOW DATASET                        82053
         FDOPT SBA=(*,-32)                                       82053
         FDPRT 'VOLUME'                                          82053
         FDPRT CURVOL,PAD                                        82053
         FDPRT 'ON'                                              82053
         FDPRT CURUNIT,HEX,PADL
         FDPRT '('                                               82053
         FDPRT CURDEVT,DEB                                       82053
         FDPRT ')'                                               82053
         FDPRT *END                                              82053
         SPACE 1                                                 82053
CONRESP  FDPRT 'CURRENT DD IS',NL                                81271
         FDPRT CURDDNM,PAD,DEB                                   81271
         FDPRT PDSDSNEQ,PADL                                     81271
         FDPRT DS1DSNAM,DEB                                      81271
         FDTM  DS1DSORG,JFCORGPO,BZ=CONRESPX                     82053
         FDPRT '('                                               81271
         FDPRT PDDRNAME,DEB                                      82144
         FDPRT ')'                                               81271
CONRESPX FDOPT NL                                                81271
         FDPRT *END                                              82053
         SPACE 1                                                 82053
MSGCNTRS FDPRT SUBMEM,I,NL,RADJ,LEN=10                           82053
         FDPRT 'MEMBER (',PADL                                   82053
         FDPRT SUBALI,I                                          82053
         FDPRT 'ALIAS) ENTRIES PROCESSED.',PAD                   82053
         FDPRT SUBDIRU,I,RADJ,LEN=10                             82053
         FDPRT 'OF',PAD                                          82053
         FDPRT SUBDIRA,I                                         82053
         FDPRT 'DIRECTORY BLOCKS IN USE.',PADL                   82053
         FDPRT *END                                              82053
         SPACE 1                                                 82053
TRAKUTIL FDOPT NL,CC=C'0'    DOUBLE-SPACE                        82053
         FDPRT 'THERE ARE',RADJ,LEN=14                           82053
         FDPRT DBWD,4,I,PAD                                      82053
         FDPRT 'TRACKS IN THIS DATA SET.'                        82053
         FDCLC DBWD+4,ZEROES,4,BE=TRAKUTEX                       82053
         FDPRT DBWD+4,4,$I,PAD,RADJ,LEN=9                        82053
         FDPRT '% FULL.'                                         82053
TRAKUTEX FDPRT *END                                              82053
         SPACE 1                                                 82053
PUTMEMH  FDOPT NL                                                82053
         FDTM  PDS2INDC,PDS2ALIS,BZ=PUTMEMHM                     82053
         FDPRT 'ALIAS',PADR                                      82053
PUTMEMHM FD    PDDRNAME,HEX,INTENSE  PRINT MEMBER NAME IN HEX    82144
         FDGOTO PUTMEMCM     GO TO COMMON                        82053
         SPACE 1                                                 82053
PUTMEM   FDOPT NL                                                82053
         FDTM  PDS2INDC,PDS2ALIS,BZ=PUTMEMNM                     82053
         FDPRT 'ALIAS',PADR                                      82053
PUTMEMNM FD    PDDRNAME,INTENSE      OVERPRINT MEMBER NAME       82144
PUTMEMCM FDPRT MEMTTR,PAD    TTTT.RR OF MEMBER                   82053
         FDPRT MEMEPA,PAD    (LKED) ENTRY POINT ADDRESS          82053
         FDCLI PDDUDLEN,1,BE=PUTMEMD,BL=PUTMEMB  NO SSI; SHORT DATA
         FDTM  PDDFLAGS,PDDFSSI,BZ=PUTMEMB  SKIP IF NO SSI       82144
         FDPRT 'SSI=',PADL                                       82053
         FDPRT PACKSSI,HEX   FORMAT SSI                          82053
         FDPRT *END                                              82053
PUTMEMB  FDPRT ' ',LEN=12    SPACE FOR MISSING SSI               82053
         FDPRT *END                                              82053
PUTMEMD  FDPRT 'INDC=',PADL                                      82053
         FDPRT PDS2INDC,HEX,PADR                                 82053
         FDPRT ' DATA =',PAD                                     82053
         FDPRT PDS2USRD,2,HEX,PADL                               82053
         FDPRT *END                                              82053
         SPACE 1                                                 82053
         DROP  R5                                                82053
         USING PDS2,R3                                           82053
PUTALIAS FDOPT IND=38        SET INDENTATION IF NEW LINE FORCED  82360
         FDTM  PDDTYPE,PDDTLKED,BZ=PUTALNLK  SKIP IF NOT LKED    82144
         FDPRT PDS2EPA,HEX,RADJ,LEN=9                            82053
         FD    PDS2NAME,PADL,INTENSE                             82053
         FDPRT *END                                              82053
PUTALNLK FD    PDS2NAME,RADJ,PADL,INTENSE,LEN=10  ALIAS NAME AND SPACES
         FDPRT *END                                              82053
         SPACE 1                                                 82053
PUTALIAH FDOPT IND=38        SET INDENTATION IF NEW LINE FORCED  82360
         FDTM  PDDTYPE,PDDTLKED,BZ=PUTALNLH  SKIP IF NOT LKED    82144
         FDPRT PDS2EPA,HEX,RADJ,LEN=9                            82053
         FD    PDS2NAME,HEX,PADL,INTENSE                         82053
         FDPRT *END                                              82053
PUTALNLH FD    PDS2NAME,HEX,PADL,INTENSE  ALIAS NAME             82053
         FDPRT *END                                              82053
         DROP  R3                                                82053
         USING PDS2,R5                                           82053
         SPACE 1                                                 82053
PUTREAL  FDPRT 'ALIAS OF',RADJ,PADR,LEN=11                       82053
         FD    PDS2MNM-PDSS02(R4),L'PDS2MNM,INTENSE              82053
         FDPRT *END                                              82053
         SPACE 1                                                 82053
PUTLKED2 FDPRT 'LENGTH=',RADJ,NL,LEN=9                           82053
         FDPRT PDS2STOR,I,PADR,RADJ,LEN=7                        82053
         FDPRT '('                                               82053
         FDPRT PDS2STOR,HEX                                      82053
         FDPRT ')',PADR                                          82053
         FDPRT ' INDC='                                          82053
         FDPRT PDS2INDC,HEX,PADR                                 82053
         FDPRT ' DATA =',PAD                                     82053
         FDPRT WORKLINE,DEB                                      82053
         FDPRT *END                                              82053
         SPACE 1                                                 82053
PUTDATA  FDPRT 'INDC=',RADJ,NL,LEN=7                             82053
         FDPRT PDS2INDC,HEX,PADR                                 82053
         FDPRT ' DATA =',PAD                                     82053
         FDPRT WORKLINE,(R2)           VARIABLE LENGTH HEX DATA  82053
         FDOPT IND=18        SET FOR AUTO INDENTATION            82053
         FDOPT NL,CC=C'+'    REQUEST CONDITIONAL OVERPRINTING    82053
         FDPRT UNDERLIN,(R2)           MAKE MATCHING UNDERLINES  82053
         FDCLI PDDUDLEN,29,BL=PUTDATAX    QUIT IF ONLY ONE LINE  82144
         FDPRT WORKLINE+112,(R3),NL    PRINT SECOND LINE         82053
         FDOPT NL,CC=C'+'    REQUEST CONDITIONAL OVERPRINTING    82053
         FDPRT UNDERLIN,(R3)           UNDERLINE IT              82053
PUTDATAX FDPRT *END                                              82053
         SPACE 1                                                 82053
PUTSPF   FDOPT NL                                               GP09274
         FDTM  PDS2INDC,PDS2ALIS,BZ=PUTSPFNM                    GP09274
         FDPRT 'Alias',PADR                                     GP09274
PUTSPFNM FD    PDDRNAME,INTENSE      OVERPRINT MEMBER NAME      GP09274
         FDPRT MEMTTR,PAD    TTTT.RR OF MEMBER                  GP09274
         FDPRT SPFVERL,I,PADL,RADJ,LEN=2                        GP09274
         FDPRT '.'                                              GP09274
         FDPRT SPFMODL,IZ,PADR,RADJ,LEN=2                       GP09274
         FDPRT 'Crt'                                            GP09274
         FDPRT SPFCRTDT,DATJ,PAD   JULIAN DATE YY.DDD           GP09274
         FDPRT 'Mod'                                            GP09274
         FDPRT SPFMODDT,DATJ,PAD                                GP09274
         FDPRT DB,4,TIMD,PAD,LEN=5  TRUNCATE TO HH:MM           GP09274
         FDPRT SPFUID,8,PAD  DO 8 BYTES (FOR POSS. WYLBUR USE)  GP09274
         FDPRT 'Records :',RADJ,LEN=10                          GP09274
         FDPRT SPFINILN,I,PAD,RADJ,LEN=5                        GP09274
         FDPRT 'Init,'                                          GP09274
         FDPRT SPFMODLN,I,PAD,RADJ,LEN=5                        GP09274
         FDPRT 'Mod,'                                           GP09274
         FDPRT SPFCURLN,I,PAD,RADJ,LEN=5                        GP09274
         FDPRT 'Cur'                                            GP09274
         FDTM  SPFFLGS,SPFGSCLM,BZ=PUTSPFX                      GP09274
         FDPRT ', SCLM'                                         GP09274
PUTSPFX  FDPRT *END                                              82053
         SPACE 1                                                 82053
PUTDTX   FDCLC PDDRNAME,DTXMODNM,BNE=PUTDTXV                     82144
         FDPRT 'IEBUPDTX MODULE',PADR,RADJ,NL,LEN=17             82053
         FD    DTXMODNM,PAD,INT                                  82053
         FDPRT 'HIGH',PAD                                        82053
         FDGOTO PUTDTXCM                                         82053
PUTDTXV  FDPRT 'IEBUPDTX CHAIN ENTRY FOR',PADR,RADJ,NL,LEN=26    82053
         FD    DTXMODNM,PAD,INT                                  82053
PUTDTXCM FDPRT 'VERSION',PAD                                     82053
         FDPRT DTXVER,D,PAD,LEN=3                                82053
         FDPRT 'LIBRARY ID',PAD                                  82053
         FDPRT DTXID,PAD                                         82053
         FDPRT 'NO.',PAD                                         82053
         FDPRT DTXMODNO,D,PAD,LEN=3                              82053
         FDCLI PDDUDLEN,DTXUDLTM,BNE=PUTDTXX  SKIP IF NO TIME STAMP
         FDPRT 'UPDATED',PAD                                     82053
         FDPRT DTXTIMES,D                                        82053
PUTDTXX  FDPRT *END                                              82053
         SPACE 1                                                 82053
PUTORPH  FDPRT '*****',NL                                        82053
         FDPRT UNPRALI,I,PAD                                     82053
         FDPRT 'ALIAS ENTRIES FOUND WITHOUT MEMBER *****'        82053
         FDPRT *END                                              82053
         SPACE 1                                                 82053
SUMDDN   FDPRT 'DDNAME',NL,PADR                                  82053
         FDPRT CURDDNM                                           82053
         FDPRT PDSDSNEQ,PADL                                     81271
         FDPRT JFCBDSNM,DEB                                      82053
         FDPRT 'VOL=',PADL                                       82053
         FDPRT CURDEVT,DEB                                       82053
         FDPRT '='                                               82053
         FDPRT CURVOL                                            82053
         FDEXEC COMDCB,COMDCBEX   SHOW DSORG, DCB PARMS          81271
         FDPRT *END                                              82053
         SPACE 1                                                 82053
SUMDDNMK FDPRT ' INCLUDE MASK=''',PADL                           82053
         FDPRT MINCLUDE                                          81271
         FDPRT ''''                                              82053
         FDPRT *END                                              82053
         SPACE 1                                                 82053
SUMDDNPG FDCLC MEMPAGE,ZEROES,2,BE=SUMDDNEX                      82053
         FDOPT SBA=(*,-10)                                       82053
         FDPRT 'PAGE'                                            82053
         FDPRT MEMPAGE,I,PADL,RADJ,LEN=5                         82053
SUMDDNEX FDPRT *END                                              82053
         SPACE 1                                                 82053
SUMDSN   FDPRT SUBDIRA,I,RADJ,NL,LEN=12                          82053
         FDPRT 'BLOCKS ALLOCATED,',PADL                          82053
         FDPRT SUBDIRU,I,RADJ,LEN=6                              82053
         FDPRT 'BLOCKS USED.',PADL                               82053
         FDPRT SUBMEM,I,RADJ,LEN=10                              82053
         FDPRT 'MEMBERS,',PAD                                    82053
         FDPRT SUBALI,I,RADJ,LEN=6                               82053
         FDPRT 'ALIAS ENTRIES READ,',PAD                         82053
         FDPRT SUBMSEL,I,RADJ,LEN=6                              82053
         FDPRT 'PRINTED.',PAD                                    82053
SUMDSNEX FDPRT *END                                              82053
         SPACE 1                                                 82053
SUMTOT   FDPRT 'RUN TOTALS:',NL                                  82053
         FDPRT TOTMEM,I,RADJ,LEN=11                              82053
         FDPRT 'MEMBERS,',PAD                                    82053
         FDPRT TOTALI,I,RADJ,LEN=6                               82053
         FDPRT 'ALIAS ENTRIES READ,',PAD                         82053
         FDPRT TOTMSEL,I,RADJ,LEN=6                              82053
         FDPRT 'PRINTED.',PAD                                    82053
SUMTOTCC FDCLC RETCODE,ZEROES,2,BE=SUMTOTEX                      82053
         FDPRT '***** COMPLETION CODE',NL                        82053
         FDPRT RETCODE,I,PAD                                     82053
         FDPRT '*****'                                           82053
SUMTOTEX FDPRT *END                                              82053
MSGNODDS VCON  '-***** NO USABLE PDS DD CARDS SUPPLIED *****'    82053
MSGUNAUT VCON  '-**** APF AUTHORIZATION REQUIRED ****'           81347
NULLLINE VCON  '  '                                              82053
         SPACE 1                                                 82053
MTIOTERR FDPRT 'DDNAME',NL                                       82053
         FDPRT 4(R2),8,PAD                                       82053
         FDPRT 'NOT PROCESSED  *** ',PAD                         82053
         FDEXEC 0(R3)                                            82053
         FDPRT *END                                              82053
MTIONDA  FDPRT 'NOT DIRECT ACCESS'                               82053
MTIOCONC FDPRT 'CONCATENATION NOT SUPPORTED'                     82053
MTIODUMM FDPRT 'DD DUMMY'                                        82053
MTIOMVOL FDPRT 'MULTI-VOLUME DATASET'                            82053
MTIODUPL FDPRT 'DUPLICATE DD CARD'                               82053
         SPACE 1                                                 82053
MDDNERR  FDPRT 'DDNAME',NL                                       82053
         FDPRT CURDDNM,PAD                                       82053
         FDPRT ' VOL='                                           82053
         FDPRT CURDEVT,DEB                                       82053
         FDPRT '='                                               82053
         FDPRT CURVOL                                            82053
         FDPRT 'NOT PROCESSED  *** ',PAD                         82053
         FDGOTO 0(R3)                                            82053
MDDNVOPN FDPRT 'VOLUME OPEN FAILED'                              82053
         FDGOTO MDDNCODE                                         82053
MDDNJFCB FDPRT 'READ JFCB FAILED'                                82053
MDDNCODE FDPRT ', RETURN CODE'                                   82053
         FDPRT DB,4,I,PAD                                        82053
         FDPRT *END                                              82053
MDDN213  FDPRT 'DATASET'                                         82053
         FDPRT JFCBDSNM,DEB,PAD                                  82053
         FDPRT 'NOT FOUND'                                       82053
         FDPRT *END                                              82053
MDDNMASK FDPRT 'NO MATCH FOR MASK'                               81271
         FDPRT '''',PADL                                         81271
         FDPRT DSNMASK,(R4)   LENGTH SET INTO R4                 81271
         FDPRT ''''                                              81271
         FDPRT *END                                              81271
         SPACE 1                                                 82053
MDSNERR  FDPRT 'DDNAME',NL                                       82053
         FDPRT CURDDNM,PAD                                       82053
         FDPRT PDSDSNEQ,RADJ,LEN=5                               81271
         FDPRT DS1DSNAM,DEB,PADR                                 82053
         FDPRT ' VOL='                                           82053
         FDPRT CURDEVT,DEB                                       82053
         FDPRT '='                                               82053
         FDPRT CURVOL                                            82053
COMDCB   FDPRT '(',PADL                                          81271
         FDPRT FMTDSORG,DEB                                      81271
         FDPRT '/'                                               81271
         FDPRT FMTRECFM,DEB                                      81271
         FDPRT ','                                               81271
         FDPRT FMTLRECL,DEB                                      81271
         FDPRT ','                                               81271
         FDPRT FMTBLKL,DEB                                       81271
         FDPRT ')'                                               81271
COMDCBEX FDOPT IND=6         SET AUTO INDENTATION FOR OVERFLOW TEXT
         FDPRT 'NOT PROCESSED  *** ',PAD                         82053
         FDGOTO 0(R3)                                            82053
MDSNORG  FDPRT 'UNSUPPORTED DATASET ORGANIZATION'                82053
         FDPRT *END                                              82053
MDSNNEX  FDPRT 'DATASET HAS NO EXTENTS'                          82053
         FDPRT *END                                              82053
MDSNDSCB FDPRT 'EMPTY OR UNUSABLE DATASET'                       81271
         FDPRT *END                                              81271
         SPACE 1                                                 82053
ERRFULL  FDOPT NL,CC=C'0'    DOUBLE-SPACE                        82053
         FDPRT 'INSUFFICIENT STORAGE - INCREASE REGION BY',PADR  82053
         FDPRT DB,4,I                                            82053
         FDPRT 'K'                                               82053
         FDPRT *END                                              82053
         SPACE 1                                                 82053
STASHNUM FDOPT NL,CC=C'0'                                        82053
         FDPRT NUMSPACE,I,RADJ,LEN=10                            82053
         FDPRT 'MEMBERS CAN BE PROCESSED IN THIS REGION',PADL    82053
         FDPRT *END                                              82053
FOPTDD   FDOPT NL,CC=C'0'    DOUBLE-SPACE                        82053
         FDPRT 'DDNAME'                                          82053
         FDPRT CURDDNM,PAD                                       82053
         FDPRT '- ENTRIES WILL BE SORTED BY',PAD                 82053
FOPTDDS  FDTM  FLAGS,FGTTR,BZ=FOPTDDN                            82053
         FDPRT 'TTR'                                             82053
         FDPRT *END                                              82053
FOPTDDN  FDPRT 'NAME'                                            82053
FOPTDDX  FDPRT *END                                              82053
         SPACE 1                                                 82053
FOPTIONS FDPRT 'RUN OPTIONS FOR',NL                              82053
FOPUNK   FDPRT 'DISTBLDL :',PAD                                 GP10187
         FDOPT IND=10                                            82053
         FDPRT 'ENTRIES WILL BE SORTED BY',PAD                   82053
         FDTM  FLAGS,FGTTR,BZ=FOPNTTR                            82053
         FDPRT 'TTR'                                             82053
         FDGOTO FOPYTTR                                          82053
FOPNTTR  FDPRT 'NAME'                                            82053
FOPYTTR  FDTM  FLAGS,FGDOAL,BZ=FOPNAL                            82053
         FDPRT 'ALIAS ENTRIES WILL BE PROCESSED AS MEMBERS',NL   82053
FOPNAL   FDOPT IND=5                                             82053
         FDPRT 'MEMBER SELECTION OPTIONS :',PADR,NL              81271
         FDOPT IND=10        RESTORE INDENTATION                 81271
         FDTM  FLAGS,FGNMAC+FGSMAC,BNZ=FOPSEL                    82053
         FDCLC LONAME,ZEROES,8,BNE=FOPSEL                        82053
         FDCLC HINAME,GENMHIGH,BNE=FOPSEL                        82053
         FDCLC GINCLUDE,GENMATCH,BNE=FOPSEL                      81271
         FDCLC MEXCLUDE,GENMATCH,BNE=FOPSEL                      81271
         FDCLC SSILOW,ZEROES,8,BNE=FOPSEL                        82053
         FDCLC SSIHIGH,GENMHIGH,BNE=FOPSEL                       82053
         FDCLC SSIMASK,GENMATCH,BNE=FOPSEL                       82053
         FDPRT '  NONE;  ALL MEMBERS WILL BE PROCESSED'          82053
         FDGOTO FOPEX                                            82053
FOPSEL   FDCLC LONAME,ZEROES,8,BE=FOPSNLOW                       82053
         FDPRT 'NAMES GREATER OR EQUAL TO ''',NL                 81271
         FDPRT LONAME,DEBR                                       82053
FOPROC   FDPRT ''' WILL BE PROCESSED'                            82053
FOPSNLOW FDCLC HINAME,GENMHIGH,BE=FOPSNHI                        82053
         FDPRT 'NAMES LESS THAN OR EQUAL TO ''',NL               81271
         FDPRT HINAME,DEBR                                       82053
         FDEXEC FOPROC                                           82053
FOPSNHI  FDCLC GINCLUDE,GENMATCH,8,BE=FOPSNINC                   81271
         FDPRT 'ONLY NAMES MATCHING THE MASK ''',NL              81271
         FDPRT GINCLUDE                                          81271
         FDEXEC FOPROC                                           82053
FOPSNINC FDCLC MEXCLUDE,GENMATCH,8,BE=FOPSNEXC                   81271
         FDPRT 'ONLY NAMES NOT MATCHING THE MASK ''',NL          81271
         FDPRT MEXCLUDE                                          81271
         FDEXEC FOPROC                                           82053
FOPSNEXC FDCLC SSIMASK,GENMATCH,BE=FOPSNSS                       82053
         FDPRT 'MEMBERS WITH SSI MATCHING THE MASK ''',NL        81271
         FDPRT SSIMASK                                           82053
         FDEXEC FOPROC                                           82053
FOPSNSS  FDCLC SSILOW,ZEROES,8,BE=FOPSNSSL                       82053
         FDPRT 'SSI GREATER OR EQUAL TO ''',NL                   81271
         FDPRT SSILOW,DEBR                                       82053
         FDPRT ''' WILL BE PROCESSED'                            82053
FOPSNSSL FDCLC SSIHIGH,GENMHIGH,BE=FOPEX                         82053
         FDPRT 'SSI LESS THAN OR EQUAL TO ''',NL                 81271
         FDPRT SSIHIGH,DEBR                                      82053
         FDEXEC FOPROC                                           82053
FOPEX    FDPRT *END                                              82053
         SPACE 1                                                 91046
PUNDSN   FD    ' COPY INDD=',NL                                  91046
         FD    CURDDNM                                           91046
         FD    JFCBDSNM,PAD,DEB                                  91046
         FD    CURVOL,PAD,DEB                                    91046
         FD    *END                                              91046
         SPACE 1                                                 91046
PUNMEM   FD    PDS2NAME-PDS2(R3),L'PDS2NAME,ASIS,NL             GP10187
         FD    CURVOL        SERIAL                             GP10187
         FD    'S'           LABEL TYPE                         GP10187
         FD    JFCBFLSQ,I,RADJ,LEN=3   FILE NUMBER 0-999        GP10187
         FD    JFCBDSNM                                         GP10187
         FD    BLANKS,8      DISTLIB                            GP10187
         FD    BLANKS,8      PROGRAM                            GP10187
         FD    *END                                              91046
         SPACE 1                                                 82053
GENMATCH DC    CL8'--------'  GENERAL MATCH MASK                 82053
GENMHIGH DC    XL8'FFFFFFFFFFFFFFFF'  HIGH RANGE VALUE           82053
BLANKS   DC    CL8'        '  FILLER                            GP10187
         SPACE 2                                                 82053
         PRINT &PRTMAC                                           81271
PATPRINT PRTWORK SYSPRINT,TITLE=3,PAGE=0,SPAGE=0  DEV=1          82053
PATPRTLN EQU   *-PATPRINT                                        82053
SUMPRINT PRTWORK SUMPRINT,SYSTERM,TITLE=3                  DEV=2 82053
CONSOLE  PRTWORK *CONSOLE                                  DEV=5 82053
SYSPUNCH PUNWORK SYSPUNCH    OPTIONAL PUNCH                DEV=4 91046
         PRINT &PRTSYS                                           81271
PATDCB   DCB   DDNAME=SYSUT1,MACRF=(E),DEVD=DA                   82053
PATDCBLN EQU   *-PATDCB                                          82053
         SPACE 1                                                 82053
SYSSIZE  DC    A(16*1024)     SIZE TO RESERVE FOR SYSTEM DUMPS, ETC.
TABMAIN  DC    A((4)*1024,1024*1024)   MIN / MAX STORAGE         82053
GETMEAN  GETMAIN VC,LA=TABMAIN,MF=L                              82053
GETMEANL EQU   *-GETMEAN                                         82053
         SPACE 2                                                 82053
*        DATA
*
MVCMEM   MVC   0(0,R3),0(R4)
UNPK     UNPK  0(0,R3),0(0,R4)
TRT      TR    0(0,R3),HEXTAB                                    82053
FORLCOM  DC    AL1(3,1,3,1,2,3,2,3,3,0)  BASIC LKED ENTRY        82053
FORLSCTR DC    AL1(2,2,2,2,0)            SCATTER LOAD DATA       82053
FORLREAL DC    AL1(3,8,0)                MEMBER FOR ALIAS        82053
FORLSSIO DC    AL1(1)        1/2       SSI ON ODD BOUNDARY       82053
FORLSSIE DC    AL1(4,0)      2/2       SSI ON EVEN BOUNDARY      82053
FORLAPF  DC    AL1(2,0)                  APF AC CODE             82053
UNDERLIN DC    14CL8'_'      UNDERLINES AT WORD SPACING          82053
         SPACE 2
*        THIS TABLE IS USED TO REORDER THE LKED ATTRIBUTE BITS
*
ATTRBITS EQU   *             TABLE POSITION IS LKED, NEW BIT GIVES SEQ
         DC    X'8000'  80   RENT
         DC    X'4000'  40   REUS
         DC    X'0800'  20   OVLAY
         DC    X'0400'  10   TESTRAN
         DC    X'0100'  08   LOAD ONLY
         DC    X'1000'  04   SCATTER LOAD
         DC    X'0080'  02   EXECUTABLE
         DC    X'0040'  01   1 BLOCK
         DC    X'0001'   80  NOT D.C.
         DC    X'0020'   40  ORG = 0
         DC    X'0010'   20  EP = 0
         DC    X'0008'   10  NO RLD
         DC    X'0004'   08  NO RELINK
         DC    X'0200'   04  TESTRAN SYMBOL TABLE
         DC    X'0002'   02  PRODUCED BY F LEVEL
         DC    X'2000'   01  REFRESHABLE
         SPACE 1
*        TABLE OF LKED ATTRIBUTES AND THEIR NEGATION, IF ANY
*
ATTRBUTE EQU   *             TABLE IN SEQ CORR. TO ATTRBITS SETTINGS
         DC    X'3',C'RENT',X'0',C' '
         DC    X'3',C'REUS',X'0',C' '
         DC    X'3',C'REFR',X'0',C' '
         DC    X'6',C'SCTR LD',X'5',C'BLK LD'
         DC    X'4',C'OVLAY',X'0',C' '
         DC    X'4',C'TESTR',X'0',C' '
         DC    X'5',C'T-SYMB',X'0',C' '
         DC    X'8',C'LOAD ONLY',X'0',C' '
         DC    X'3',C'EXEC',X'D',C'NOT EXECUTABLE'
         DC    X'4',C'1 BLK',X'0',C' '
         DC    X'4',C'ORG=0',X'0',C' '
         DC    X'3',C'EP=0',X'0',C' '
         DC    X'5',C'NO RLD',X'0',C' '
         DC    X'8',C'NO RELINK',X'0',C' '
         DC    X'4',C'F-LVL',X'0',C' '                           82053
         DC    X'5',C'NOT DC',X'1',C'DC'
*
ERRLSTAR VCON  ' ***** MEMBER AFTER END OF DATASET *****'        82053
ERRNOMEM VCON  '0  DATA SET HAS NO MEMBERS'                      82053
DIRIOERR VCON  '0I/O ERROR READING THE DIRECTORY'                82053
ERRSPACE VCON  '0  INSUFFICIENT STORAGE - RERUN IN LARGER REGION'
        TITLE 'D I S T B L D L - INTERFACE FOR IEHVTOC ROUTINES' 82053
DISTBLDL CSECT ,
         SPACE 1
*                                                                82053
*        CALL  PRINT,(DATALOC,LINES)                             82053
*                                                                82053
         SPACE 3                                                 82053
         ENTRY PRINT                                             82053
         DROP  ,             DELETE ALL BASE DECLARATIONS        81340
PRINT    STM   R14,R12,12(R13)                                   81340
         USING PRINT,R12     USE CURRENT BASE                    81340
         LR    R12,R15       FIX IT UP                           81340
         LA    R9,PRSAVE     GET PRINT SAVE AREA                 81340
         ST    R9,8(,R13)    LINK SAVE AREAS                     81340
         ST    R13,4(,R9)                                        81340
         LR    R13,R9        SET NEW SAVE AREA                   81340
         LM    R2,R3,0(R1)   GET PASSED PARAMETERS               81340
         SLR   R4,R4         CLEAR IC REGISTER                   81340
         ICM   R4,3,2(R3)    GET NUMBER OF LINES TO RESERVE ON PAGE
         BNP   PUTNUM        NONE; SKIP                          81340
         PRTROOM (R4)        REQUEST CONTIGUOUS LINES ON ONE PAGE
PUTNUM   ICM   R4,3,0(R3)    GET NUMBER OF LINES TO PRINT        81340
         BNP   PUTEXIT       NONE; EXIT                          81340
PRTLOOP  CLI   0(R2),C'+'    OVERRPRINT REQUEST ?                81340
PUTPUT1  NOP   PUTBUMP       CHANGED TO 'BE' WITH %TERM OPTION   81340
         CLC   103(29,R2),=C'DSCB EXTENDED AT 0000.0000.00'      81340
         BE    PUTSHORT      SKIP NULL EXTENSION MESSAGE         81340
         PRTF  (R2),132      PRINT 132 BYTES WITH CARRIAGE CONTROL
         B     PUTBUMP       DO ANOTHER                          81340
PUTSHORT PRTF  (R2),102      PRINT SHORT LINE                    81340
PUTBUMP  LA    R2,132(,R2)   SKIP LINE SIZE                      81340
         BCT   R4,PRTLOOP    GO BACK FOR MORE                    81340
PUTEXIT  L     R13,4(,R13)   RESTORE OLD SAVE AREA               81340
         LM    R14,R12,12(R13)  RELOAD REGISTERS                 81340
         XR    R15,R15       SET UNNECESSARY GOOD RETURN CODE    81340
         BR    R14           RETURN TO CALLER                    81340
         SPACE 1                                                 82053
@PRINTER DC    A(0)          ADDRESS OF @PRINTER ROUTINE         82053
PRSAVE   DC    18F'0'                                            81340
         DROP  ,                                                 81340
         TITLE 'D I S T B L D L  -  DATA SET SIZE SUBROUTINE'    82053
         ENTRY COMPSIZE                                          82053
COMPSIZE L     R1,0(,R1)           GET LOC OF PARMS              81340
         USING COMPSIZE,R15                                      81340
         SLR   R0,R0         CLEAR ACCUMULATOR                   81340
COMPLOOP CLC   0(2,R1),ZEROES                                    81340
         BE    SAVESIZE            DONE                          82053
         MVC   *+8(2),0(R1)  COPY S-FORMAT INPUT LOCATION        81340
         LA    R2,*-*              COMPUTE INLOC                 81340
         MVC   COMPTEMP,0(R2)      MOVE DATA TO PROPER BOUNDARIES
         SLR   R5,R5               SET TRACKS TO ZERO            81340
         MVC   *+8(2),2(R1)  GET S-FORMAT OUTPUT ADDRESS         81340
         LA    R3,*-*              COMPUTE OULOC                 81340
         CLC   COMPTEMP+4(4),ZEROES     IS IT A NULL EXTENT      81340
         BE    COMPADD             IF YES, SKIP ARITH            82053
         CLC   COMPTEMP(4),ZEROES    IS IT TRACK ZERO ?          81340
         BNE   *+8           NO, SKIP                            82053
         MVI   VOLABEL+1,0   SET LABEL TRACK ALLOCATED           81340
         CLC   COMPTEMP(4),=AL4(1)   IS IT TRACK 1 ?             82053
         BH    COMPNIPL      NO                                  81340
         CLC   COMPTEMP+4(4),=AL4(1)   IS TRACK 1 ALLOCATED ?    82053
         BL    COMPNIPL      NO, SKIP                            81340
         MVI   VOLIPL+1,0    RESET 'POSSIBLE' IPL TRACK ALLOCATED
COMPNIPL BCTR  R2,0          BUMP BACK TO EXTENT TYPE INDICATOR  81340
         BCTR  R2,0          BUMP BACK TO EXTENT TYPE INDICATOR  81340
         LH    R5,COMPTEMP+4    HI CYL                           81340
         SH    R5,COMPTEMP    - LO CYL                           81340
         LH    R6,COMPTEMP+6  HI TRK                             81340
         SH    R6,COMPTEMP+2  - LO TRK                           81340
         AH    R6,=H'1'      COMPENSATE                          81340
         CLI   0(R2),X'80'   SPLIT EXTENT ?                      81340
         BE    COMPSPL       YES - DO DIFFERENTLY                81340
         MH    R5,TRKPCYL    TKS/CYL                             82053
         AR    R5,R6         TOTAL TRKS IN EXTENT                81340
         B     COMPADD       SUM IT IN                           82053
COMPSPL  AH    R5,=H'1'      # OF CYLS                           81340
         MR    R4,R6         TOTAL TRACKS                        81340
COMPADD  AR    R0,R5               ADD EXTENT SIZE               81340
         CVD   R5,DWORK            CNVRT TO DEC                  81340
         MVC   CVDB,CVDM                                         82053
         ED    CVDB,DWORK                                        82053
         MVC   0(4,R3),CVDB+12                                   81340
         LA    R1,4(,R1)           STEP PARM POINTER             81340
         B     COMPLOOP            LOOP TILL DONE                82053
         SPACE 2                                                 82053
SAVESIZE LR    R1,R0         COPY EXTENT SIZE                    81340
         A     R0,DBWD       ADD LAST                            82053
         ST    R0,DBWD       SAVE FOR NEXT                       82053
         A     R1,VOLUSE     ADD TO TRACKS/VOLUME IN USE         81340
         ST    R1,VOLUSE     AND STASH BACK                      81340
         BR    R14                                               81340
         EJECT ,                                                 82053
DB       DS    D            MUST PRECEDE DBWD                    82053
DBWD     DS    D             MUST FOLLOW DB                      82053
         SPACE 1                                                 82053
         ENTRY VTOCCOMM                                          82053
VTOCCOMM DS    0F                                                82053
VOLFREE  DC    F'0'          FREE TRACKS FOR FMT 5               82053
VOLABEL  DC    H'0'          FLAG FOR LABEL TRACK                82053
VOLIPL   DC    H'0'          FLAG FOR IPL TRACK, IF APPLICABLE   82053
CYLTRK   DC    H'0'          NO. OF TRACKS PER CYLINDER          82053
DS5FLG   DC    X'0'          VTOC INDICATOR (I.E. BAD FMT 5'S)   82053
INDXFG   DC    X'0'          DEFAULT - DETAIL LISTING, NO INDEX  82053
XFINDEX  EQU   1             %INDEX REQUESTED                    82031
XFNODET  EQU   2             %INDON - NO DETAIL LISTING          82031
XFSORT   EQU   4             %SORT REQUESTED                     82031
F4ADDR   DC    A(0)          ADDR. OF FORMAT 4 DSCB FOR INDEX    82053
WKADDR   DC    A(0)          ADDR OF WORK AREA FOR INDEX         82053
VOLTOT   DC    F'0'          TOTAL TRACKS ON VOLUME              82053
VOLMISS  DC    F'0'          TRACKS NOT ACCOUNTED FOR            82053
VOLUSE   DC    F'0'          TRACKS IN USE                       82053
VOLATOT  DC    H'0'          TOTAL ALTERNATE TRACKS ON VOLUME    82053
VOLAUSE  DC    H'0'          ALTERNATE TRACKS IN USE             82053
VOLAFREE DC    H'0'          ALT. TRACKS FREE                    82053
         TITLE 'D I S T B L D L  -  CONVERT TO HEXADECIMAL SUBROUTINE'
         ENTRY CVH                                               82053
CVH      L     R1,0(,R1)                                         81340
         USING CVH,R15                                           81340
CVHLOOP  MVC   WORKCL9,=XL9'F'                                   82053
         USING TABLE,R1                                          81340
         LH    R2,INLNGTH                                        81340
         BCTR  R2,0                                              81340
         N     R2,=F'15'                                         81340
         MVC   MVO+4(2),INLOC                                    82053
         EX    R2,MVO                                            81340
         LH    R2,OULNGTH                                        81340
         BCTR  R2,0                                              81340
         N     R2,=F'15'                                         81340
         MVC   HUNPK+2(2),OULOC                                  82053
         MVC   HTR+2(2),OULOC                                    82053
         LR    R3,R2                                             81340
         SLL   R2,4                                              81340
         EX    R2,HUNPK                                          81340
         EX    R3,HTR                                            81340
         LA    R1,8(,R1)                                         81340
         CLC   INLOC,ZEROES                                      81340
         BNE   CVHLOOP                                           82053
         BR    R14                                               81340
HTR      TR    0(0,0),HEXTAB                                     82053
TRKPCYL  DS    H             TRACKS/CYLINDER                     82053
HUNPK    UNPK  0(0,0),WORKCL9                                    82053
MVO      MVO   WORKCL9,0                                         82053
       TITLE 'D I S T B L D L  -  CONVERT TO DECIMAL SUBROUTINE' 82053
         ENTRY CVD                                               82053
CVD      L     R1,0(,R1)                                         81340
         USING CVD,R15                                           81340
CVDLOOP  XC    WORKF,WORKF                                       82053
         USING TABLE,R1                                          81340
         LH    R2,INLNGTH                                        81340
         CH    R2,=H'4'                                          81340
         BHR   R14                                               81340
         LTR   R2,R2         ZERO ?                              81340
         BLR   R14           YES; DONE                           81340
         LA    R3,WORKF+4                                        81340
         SR    R3,R2                                             81340
         MVC   MOVE+4(2),INLOC                                   82053
         BCTR  R2,0                                              81340
         EX    R2,MOVE                                           81340
         L     R2,WORKF                                          81340
         LPR   R2,R2         JUST IN CASE - EVEN IF O/P BAD      81340
         CVD   R2,WORKD                                          81340
         MVC   CVDB(16),CVDM                                     82053
         ED    CVDB(16),WORKD                                    82053
         LH    R2,OULNGTH                                        81340
         LA    R3,CVDB+16                                        81340
         SR    R3,R2                                             81340
         BCTR  R2,0                                              81340
         MVC   CVDMVC+2(2),OULOC                                 82053
         EX    R2,CVDMVC                                         81340
         LA    R1,8(,R1)                                         81340
         CLC   INLOC,ZEROES                                      81340
         BNE   CVDLOOP                                           82053
         BR    R14                                               81340
MOVE     MVC   0(0,R3),0                                         81340
CVDMVC   MVC   OULOC(0),0(R3)                                    81340
CVDM     DC    XL16'40202020202020202020202020202120'            82053
CVDB     DS    CL16                                              82053
HEXTAB   EQU   *-C'0'+1      FOR HEX TRANSLATE                   82053
ALPHAS   DC    C'-0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ$#@ '    HEXTAB
         SPACE 1                                                 82053
WORKD    DS    D                                                 82053
DWORK    DS    D                                                 82053
COMPTEMP DS    D                                                 82053
WORKF    DS    F
WORKCL9  DS    CL9
         EJECT ,                                                 82053
         PUSH  USING                                            GP97238
         DROP  ,                                                GP97238
         ENTRY PUTADDR                                           82053
PUTADDR  STM   R14,R12,12(R13)                                   81340
         LR    R4,R15                                            81340
         USING PUTADDR,R4                                        81340
         L     R5,0(,R1)                                         81340
PUTLOOP  MVC   INADDR(2),0(R5)                                   81340
         CALL  CVH,(INADDR)                                      82053
         MVC   *+8(2),4(R5)                                      81340
         LA    R1,*-*                                            81340
         LA    R2,PUTASPC                                        81340
         LA    R3,PUTASNO                                        81340
         LA    R6,PUTWORK                                        81340
PUTSUBLP CLC   7(1,R5),0(R2)                                     81340
         BE    PUTOURS       IT'S ONE OF OUR SPECIAL LENGTHS     82053
         LA    R2,1(,R2)                                         81340
         BCT   R3,PUTSUBLP   CHECK NEXT                          81340
         SLR   R2,R2                                             81340
         IC    R2,7(,R5)                                         81340
         BCTR  R2,0                                              81340
         EX    R2,PUTMVC     MOVE AS IS                          81340
         B     PUTINC                                            82053
PUTOURS  CH    R3,H5                                             81340
         BE    PUTCCHH                                           82053
         CH    R3,=H'3'                                          81340
         BH    PUTHR         NECESSARY LATER ON                  82053
         BL    PUTMB                                             82053
         LA    R2,3                                              81340
         EX    R2,PUTMVC                                         81340
         MVI   4(R1),C'.'                                        81340
         LA    R1,5(1,0)                                         81340
H5       EQU   *-2                                               82053
         LA    R6,4(,R6)                                         81340
PUTHR    LA    R2,3                                              81340
         EX    R2,PUTMVC                                         81340
         MVI   4(R1),C'.'                                        81340
         LA    R1,5(,R1)                                         81340
         LA    R6,4(,R6)                                         81340
PUTR     LA    R2,1          PUTS RECORD .RR ONLY                81340
         EX    R2,PUTMVC                                         81340
PUTINC   LA    R5,8(,R5)     GET NEXT LIST ITEM                  81340
         CLI   0(R5),0       LAST ?                              81340
         BNE   PUTLOOP                                           82053
         LM    R14,R12,12(R13)                                   81340
         BR    R14                                               81340
PUTCCHH  LA    R7,2                                              81340
         B     PUTCH                                             82053
PUTMB    LA    R2,1          MM.                                 81340
         EX    R2,PUTMVC                                         81340
         MVI   2(R1),C'.'                                        81340
         LA    R1,3(,R1)                                         81340
         LA    R6,2(,R6)                                         81340
         LA    R7,3          LOOP THREE TIMES FOR BBBB. CCCC. HHHH.
PUTCH    LA    R2,3                                              81340
PUTMBLP  EX    R2,PUTMVC                                         81340
         CH    R7,=H'1'                                          81340
         BE    *+8                                               82053
         MVI   4(R1),C'.'                                        81340
         LA    R1,5(,R1)                                         81340
         LA    R6,4(,R6)                                         81340
         BCT   R7,PUTMBLP                                        81340
         CH    R3,=H'1'      WITHOUT .RR ?                       81340
         BNE   PUTINC        RIGHT, DO NEXT                      82053
         BCTR  R1,0                                              81340
         MVI   0(R1),C'.'                                        81340
         LA    R1,1(,R1)                                         81340
         B     PUTR          ADD .RR                             82053
PUTMVC   MVC   0(0,R1),0(R6)                                     81340
PUTASPC  DC    AL1(9,7,12,17,20)                                 82053
PUTASNO  EQU   *-PUTASPC                                         82053
INADDR   DC    S(*),H'8',S(PUTWORK),H'16',H'0'                   82053
PUTWORK  DS    CL16                                              82053
ZEROES   DC    D'0'          CONSTANT ZERO                       82053
         SPACE 1
TABLE    DSECT ,                                                 82053
INLOC    DS    H                                                 82053
INLNGTH  DS    H                                                 82053
OULOC    DS    H                                                 82053
OULNGTH  DS    H                                                 82053
         POP   USING                                            GP97238
         TITLE 'D I S T B L D L  ***  UNAUTHORIZED SUPPORT'      90260
DISTBLDL CSECT ,
         @VOLRESB ,     INSERT UNAUTHORIZED @VOLREAD SUBSTITUTE GP10187
         TITLE 'D I S T B L D L  -  CONSTANTS AND WORK AREAS'    82053
         PRINT &PRTMAC                                           81271
SAVE     DSECT ,             WORK AND SAVE AREA                  82053
         @VOLREST ,          DEFINE VOLFAKE DATA                GP10187
VOLSER   DS    CL6           CURRENT @OBTAINS VOLUME SERIAL     GP99033
CURTTR   DS    F             TTR OF CURRENT BLOCK
CURRECLN DC    F'80'         CURRENT RECORD LENGTH (INPUT)       89217
CURTYPE  DS    XL4           UCB TYPE
DEBXLE   SERVPDS PFX=PD,DSECT=NO,RETURN=YES                      82256
         SPACE 1
UPD      DS    D                                                 82053
         SERVDEFS ,          SERVICE ROUTINE WORK               GP10187
RETCC    EQU   RETCODE+3,1,C'B'                                 GP10187
*SYSPRINT PRTWORK SYSPRINT,TITLE=3,PAGE=0,SPAGE=0  DEV=1         82053
SYSPRINT MAPPRT PREFIX=PR,DSECT=NO   EXPAND PRINTER MAPPING      82053
@VOLREAD DC    A(0)          ADDRESS OF VOLUME READ              82053
@VOLAUTH DC    A(0)          REAL ADDRESS OF VOLUME READ        GP99035
CSCBPTR  DC    A(0)                                              82053
ADDRTIOT DC    A(0)          TIOT SCAN - LAST ENTRY USED         82053
SORTPARM SERVSORT DSECT=NO    SORT/BINLK PARAMETER LIST          82053
SOPKEY   DS    A             ADDRESS OF KEY FOR BINARY LOOKUP    82053
@ALIASN  DS    A   1/2       LAST ALIAS FOR CURRENT MEMBER       82053
@ALIAS1  DS    A   2/2       ADDRESS OF FIRST ALIAS ENTRY OR 0   82053
MAINTAB  DC    A((12+4)*1024,1024*1024)   MIN / MAX STORAGE      82053
GETMAIN  DC    XL(GETMEANL)'0'  GETMAIN LIST                     82053
NUMSPACE DS    F             MAXIMUM NUMBER OF LIST ENTRIES      82053
CCWARN   EQU   4               MINOR ERROR                       82053
CCERROR  EQU   8               ERROR                             82053
CCMAJOR  EQU   12              MAJOR ERROR                       82053
CCFATAL  EQU   16              FATAL ERROR                       82053
         SPACE 1                                                 82053
LONAME   DC    XL8'0'        LOW NAME OF RANGE                   82053
HINAME   DC    XL8'FFFFFFFFFFFFFFFF'  HIGH NAME OF RANGE         82053
GINCLUDE DC    C'--------'   GLOBAL MASK                         81271
MEXCLUDE DC    C'--------'   GLOBAL EXCLUDE                      81271
SSILOW   DC    XL8'0'        LOW SSI VALUE                       82053
SSIHIGH  DC    XL8'FFFFFFFFFFFFFFFF'  HIGH SSI VALUE             82053
SSIMASK  DC    C'--------'   MASK SSI FIELD                      82053
MINCLUDE DC    CL8' '        FOR MASKED REQUESTS                 81271
PDSDSNEQ DC    C'DSN='       OR PDS= FOR PO                      81271
         SPACE 1                                                 82053
CURDDNM  DC    CL8' '        CURRENT DD NAME                     82053
MEMTTR   DC    CL8' '        MEMBER TTTT.RR + OVERFLOW FOR UNPK  82053
BLKSTART EQU   *             AREA TO BE BLANKED                  82053
MEMEPA   DC    CL6' ',C' '   ENTRY POINT ADDRESS + UNPK OVERFLOW 82053
BLKLEN   EQU   *-BLKSTART      LENGTH TO BLANK                   82053
PACKSSI  DC    XL4'0'        PACKED (ORIGINAL) SSI               82053
PACKSSIL DC    XL4'0'          SSI LOW VALUE                     82053
PACKSSIH DC    XL4'0'          SSI HIGH VALUE                    82053
CURAPF   DS    XL2           CURRENT APF VALUE                   82053
CURDEVT  DS    CL8           JCL UNIT TYPE OF VOLUME             82053
CURUNIT  DS    AL2           DEVICE NUMBER
CURVOL   DS    CL6           VOLUME SERIAL                       82053
LIST     DC    F'0'          ADDRESS OF GETMAIN AREA
LISTCUR  DC    A(0)
LISTEND  DC    F'0'          ADDRESS OF LAST AVAILABLE SPACE
ALIST    DC    F'0'          ADDRESS OF FIRST ALIAS ENTRY        82053
ALISTCUR DC    F'0'          ADDRESS OF NEXT AVAILABLE ENTRY     82053
OPENLIST DS    0A(0),X'80',AL3(CURDCB)                     1/2   82053
EXITLIST DS    0A(0),X'87',AL3(INFMJFCB)                   2/2   82053
         SPACE 1                                                 82144
         PRINT GEN                                               82144
         SERVPDS DSECT=NO,RETURN=YES   EXPAND DE AREA            82144
         SPACE 1                                                 82144
         SERVPDS DSECT=NO,PFX=PAL  COPY FOR ALIAS                82144
         SPACE 1                                                 82144
         PRINT &PRTSYS                                           81271
CURDCB   DCB   DDNAME=SYSUT1,MACRF=(E),DEVD=DA                   82053
TOTDIRA  DS    F             DIRECTORY BLOCKS ALLOCATED          82053
TOTDIRU  DS    F             NUMBER USED                         82053
TOTMEM   DS    F             MEMBERS                             82053
TOTALI   DS    F             NUMBER OF ALIAS ENTRIES             82053
TOTMSEL  DS    F             NUMBER SELECTED FOR PRINTING        82053
CLRDSN   DS    0F            START OF DATA CLEARED FOR EACH DSN  82053
SUBDIRA  DS    F             ALLOCATED DIRECTORY BLOCKS          82053
SUBDIRU  DS    F             NUMBER USED                         82053
SUBMEM   DS    F             MEMBERS                             82053
SUBALI   DS    F             NUMBER OF ALIAS ENTRIES             82053
SUBMSEL  DS    F             NUMBER SELECTED FOR PRINTING        82053
SUBCOUNT EQU   (*-SUBDIRA)/4                                     82053
UNPRALI  DS    F             UNPROCESSED ALIAS ENTRIES           82053
MEMPAGE  DS    H             MEMBER PAGE NUMBER ON SYSPRINT      82053
CLRDSNLN EQU   *-CLRDSN      LENGTH TO CLEAR FOR NEW DSN         82053
         SPACE 1                                                 81271
DSNMASKL DC    X'00'         LENGTH OF DSN MASK                  81271
DSNMASK  DC    CL44' '       DSN MASK                            81271
MYJFCB   DC    0A(0)                                             82053
         IEFJFCBN ,          JFCB FOR CURRENT DD                 82053
         SPACE 1                                                 82053
         IECSDSL1 1          CURRENT FORMAT 1 DSCB               82053
CCHHDSCB DS    XL5           CCHHR ADDRESS OF CURRENT DSCB       82053
LENDSCB1 EQU   *-IECSDSL1                                        82053
         AIF   (&MVSXA).SYSDEF                                  GP04234
DS1FLAG1 EQU   DS1NOBDB+1,1,C'X'  MORE FLAGS
DS1COMPR EQU   X'80'           COMPRESSABLE EXTENDED IF DS1STRP
DS1CPOIT EQU   X'40'           CHECKPOINTED D S
DS1SMSFG EQU   DS1FLAG1+17,1,C'X'  SMS FLAG
DS1SMSDS EQU   X'80'           SMS D S
DS1SMSUC EQU   X'40'           NO BCS ENTRY
DS1REBLK EQU   X'20'           MAY BE REBLOCKED
DS1CRSDB EQU   X'10'           BLKSZ BY DADSM
DS1PDSE  EQU   X'08'           PDS/E
DS1STRP  EQU   X'04'           EXTENDED FORMAT D S
DS1PDSEX EQU   X'02'           HFS D S
DS1DSAE  EQU   X'01'           EXTENDED ATTRIBUTES EXISY
DS1SCEXT EQU   DS1SMSFG+1,3,C'X'  SECONDARY SPACE EXTENSION
DS1SCXTF EQU   DS1SCEXT,1,C'X'  -"- FLAG
DS1SCAVB EQU   X'80'           SCXTV IS AVG BLOCK LEN
DS1SCMB  EQU   X'40'                 IS IN MEGBYTES
DS1SCKB  EQU   X'20'                 IS IN KILOBYTES
DS1SCUB  EQU   X'10'                 IS IN BYTES
DS1SCCP1 EQU   X'08'           SCXTV COMPACTED BY 256
DS1SCCP2 EQU   X'04'                 COMPACTED BY 65536
DS1SCXTV EQU   DS1SCXTF+1,2,C'X'  SEC SPACE EXTNSION VALUE
DS1ORGAM EQU   DS1ACBM         CONSISTENT NAMING - VSAM D S
DS1RECFF EQU   X'80'           RECFM F
DS1RECFV EQU   X'40'           RECFM V
DS1RECFU EQU   X'C0'           RECFM U
DS1RECFT EQU   X'20'           RECFM T   001X XXXX IS D
DS1RECFB EQU   X'10'           RECFM B
DS1RECFS EQU   X'08'           RECFM S
DS1RECFA EQU   X'04'           RECFM A
DS1RECMC EQU   X'02'           RECFM M
*   OPTCD DEFINITIONS   BDAM    W.EFA..R
*                       ISAM    WUMIY.LR
*             BPAM/BSAM/QSAM    WUCHBZTJ
DS1OPTIC EQU   X'80'  FOR DS1ORGAM - CATLG IN ICF CAT
DS1OPTBC EQU   X'40'           ICF CATALOG
DS1RACDF EQU   DS1IND40
DS1SECTY EQU   DS1IND10
DS1WRSEC EQU   DS1IND04
DS1SCAL1 EQU   DS1SCALO,1,C'X'    SEC. ALLOC FLAGS
DS1DSPAC EQU   X'C0'         SPACE REQUEST MASK
DS1CYL   EQU   X'C0'           CYLINDER BOUND
DS1TRK   EQU   X'80'           TRACK
DS1AVRND EQU   X'41'           AVG BLOCK + ROUND
DS1AVR   EQU   X'40'           AVG BLOCK LEN
DS1MSGP  EQU   X'20'
DS1EXT   EQU   X'10'           SEC. EXTENSION EXISTS
DS1CONTG EQU   X'08'           REQ. CONTIGUOUS
DS1MXIG  EQU   X'04'           MAX
DS1ALX   EQU   X'02'           ALX
DS1DSABS EQU   X'00'           ABSOLUTE TRACK
DS1SCAL3 EQU   DS1SCAL1+1,3,C'X'  SEC ALLOC QUANTITY
.SYSDEF  SPACE 1
         SPACE 1                                                 82053
         IECSDSL1 3          CURRENT FORMAT 2 OR 3 DSCB         GP99034
CCHHR3   DS    XL5           CCHHR ADDRESS OF CURRENT DSCB      GP99034
         DS    0H            ALIGN DS4DEVSZ                     GP99033
WORK4    IECSDSL1 4          EXPAND FORMAT 4 DSCB                81340
FMT4CCHH DS    XL5           @OBTAIN RETURNED DSCB ADDRESS       81340
         SPACE 1                                                 82053
         DC    C' '          WORKLINE-1                          82053
WORKLINE DC    CL133' '                                          82053
         ORG   WORKLINE+1    RE-USE                              81271
FMTDSORG DS    CL3           RETURN FROM SVC DSFMT CALL          81271
FMTRECFM DS    CL6                                               81271
FMTOPTCD DS    CL8                                               81271
FMTBLKL  DS    CL5                                               81271
FMTLRECL DS    CL5                                               81271
FMTORGLN EQU   *-FMTDSORG                                        81271
         ORG   WORKLINE+L'WORKLINE                               81271
         DS    XL16          FOR UNEXPECTED OVERRUN              82053
         SPACE 1                                                 82053
ERRUNIT  DC    X'00'         ERROR MESSAGE UNIT                  82053
         SPACE 1                                                 82053
OUTUNIT  DC    X'00'         UNITS AVAILABLE FOR OUTPUT          82053
OUPRT    EQU   1               SYSPRINT                          82053
OUSUM    EQU   2               SUMPRINT                          82053
OUPUN    EQU   4               SYSPUNCH                          82053
OUPDT    EQU   8               SYSUPDTE                          82053
         SPACE 1                                                 82053
FLAGS    DC    X'00'         MAJOR PROCESSING FLAGS              82053
FGTTR    EQU   X'80'           SORT MEMBERS BY TTR               82053
FGDOAL   EQU   X'40'           PROCESS ALIAS ENTRIES A MEMBERS   82053
FGSMAC   EQU   X'20'           PROCESS MACROS ONLY               82053
FGNMAC   EQU   X'10'           PROCESS NONMACROS ONLY            82053
FGCOPEX  EQU   X'08'           IEBCOPY EXCLUDE, NOT INCLUDE      91046
FGDSN    EQU   X'04'           DSN LOOKUP MODE                   82053
FGDSNF   EQU   X'02'           MATCH FOUND IN LOOKUP MODE        82053
FGDUPL   EQU   X'01'           OPEN DUPLICATE VOLSER/DSN FLAG    82053
         SPACE 1                                                GP99035
FLAGS2   DC    X'00'         VOLREAD PROCESSING FLAGS           GP99035
FGUNAUTH EQU   X'80'           UNAUTHORIZED INVOKATION          GP99035
FGPDSE   EQU   X'40'           PROCESSING A PDSE                GP99035
FGPVIO   EQU   X'20'           PROCESSING A VIO FILE
         SPACE 1                                                 82053
FONE     DC    X'00'         FIRST TIME FLAGS                    82053
FODDN    EQU   X'80'           AT LEAST ONE USABLE DD FOUND      81343
FODSN    EQU   X'40'           DDN/DSN HEADER PROCESSED          81343
FODTX    EQU   X'10'           IEBUPDTX ALLOCATOR FOUND          82053
FODEOF   EQU   X'08'           DIRECTORY END-FILE HIT            82053
FORPHAN  EQU   X'04'           UNRESOLVED ALIAS ENTRIES PROCESSED
FOFULL   EQU   X'02'           DIRECTORY OVERFLOWS STORAGE       82053
FOSKIP   EQU   X'01'           DOUBLE-SPACE ON NEXT PRINT        82053
SAVEEND  EQU   *             END OF DYNAMIC WORK AREA            82053
         SPACE 1
RDRMAP   MAPINPWK PREFIX=RD  MAP OF INPREAD WORK AREA            90260
         SPACE 1                                                 82053
         DCBD  DSORG=PS                                          82053
         SPACE 1                                                 82053
         IKJTCB ,                                                82053
         SPACE 1                                                 82053
CSCB     DSECT ,                                                 82053
         IEECHAIN                                                82053
         SPACE 1                                                 82053
         IEZJSCB ,                                               82053
         SPACE 1                                                 82053
CIB      DSECT ,                                                 82053
         IEZCIB ,                                                82053
         SPACE 1                                                 82053
CVTDSECT DSECT ,                                                 82053
         CVT   DSECT=YES                                         82053
         IHACDE ,                                                82053
         SPACE 1                                                 82053
         IEFTIOT1 ,                                              82053
         SPACE 1                                                 82053
PHONY    DSECT ,                                                GP97238
         IEFUCBOB ,                                              82053
         SPACE 3
         PRINT GEN                                               82053
         IHAPDS PDSBLDL=NO   EXPAND DIRECTORY MAPPING            82053
         SPACE 2                                                 82053
*        USER DATA FOR SPECIAL ENTRIES                           82144
*                                                                82053
         MAPPDS ,            EXPAND ALL SUPPORTED FORMATS        82144
         SPACE 2                                                 82053
         PRINT &PRTSYS                                           82053
         IHAPSA ,                                                91046
         END   ,                                                 82053
