ITERATE  TITLE 'I T E R A T E  ***  READ PDS, EDIT SYSIN W/MEM, DS NAM'
         PUNCH ' ORDER ITERATE(P) '
         COPY  OPTIONGB      GLOBAL DEFINITIONS
         SPACE 1
         SYSPARM LIST=YES    GLOBAL SETTINGS
         SPACE 1
***********************************************************************
**                                                                   **
**   THIS PROGRAM READS THE DIRECTORY OF A PARTITIONED (PDS OR PDSE) **
**   DATA SET (SPECIFIED ON DD SYSLIB), W/OPTIONAL MEMBER MASK.      **
**                                                                   **
**   ALIAS ENTRIES ARE IGNORED. THE REMAINING MEMBER NAMES ARE       **
**   USED TO REPLACE THE CHARACTER STRING #MEMBER# AND #DSN# WITH    **
**   THE DATA SET NAME.  THE EDITED OUTPUT IS WRITTEN TO SYSPUNCH.   **
**                                                                   **
**   PARM OPTIONS:                                                   **
**   ASM | JCL   - EDIT COLUMNS 1-71 ONLY; COPY 72-80 UNCHANGED      **
**                 JCL ADDITIONALLY DOES NOT REPEAT (LIST) THE FIRST **
**                 CARD (PRESUMED JOB CARD)                          **
**   LIST        - PROCESS THE INPUT JCL ONCE, BUT REPEAT MEMBER     **
**                 INSERTION CARD(S) FOR EACH MEMBER (E.G., IEBCOPY) **
**                                                                   **
**   SYSPUNCH MAY BE DUMMY, SYSOUT, OR INTRDR                        **
**   OPTIONAL COPY ON SYSPRINT, AND OPTIONAL SUMMARY ON SUMPRINT     **
**   OPTIONAL EXCLUDE DD - CARD FORMAT, ONE NAME PER RECORD          **
**                                                                   **
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*        COPYRIGHT 2005  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1
         PRINT &PRTSOR       SAVE A TREE
ITERATE  PGMHEAD ZERO31,BASE=(R11,R12),PARM=R8,                        *
               AM=31,RM=24,BNDRY=PAGE
         LA    R14,WRKHEAD   WORKING STORAGE
         L     R0,=A(PATSTART)  PATTERN FOR IT
         LA    R1,PATSTLEN   LENGTH
         LR    R15,R1          DITTO
         MVCL  R14,R0        BUILD CONTROL BLOCKS
         LA    R0,EXLIST     GET INDS EXIT LIST
         STCM  R0,7,INDS+DCBEXLSA-IHADCB  MAKE EXIT LIST ADDRESS
         LA    R0,INFMJFCB   POINT TO JFCB
         STCM  R0,7,EXLIST+1  PUT JFCB INTO EXIT LIST
         MVC   SKIPMEM,=8X'FF'    INITIALIZE NOT TO SKIP        GP12043
         SERVINIT ,          INITIALIZE @SERVICE ROUTINE
         SERVLOAD @INPREAD,@PARSER,@PRINTER  LOAD SUPPORT CODE
         SERVLOAD SUBVERB,SUBDSERV
         MVI   ERRDEV,1      ALL OUTPUT ON SYSPRINT
         PRTOPEN SYSPRINT,OPT=(NOWTO,DUMMY),DEV=1
         PRTOPEN SUMPRINT,OPT=(NOWTO,DUMMY),DEV=2
         CH    R15,=H'4'     DID IT OPEN ?
         BH    MISSUMPT      NO; USE SYSPRINT ONLY
         MVI   ERRDEV,2      ROUTINE STATUS/ERROR MESSAGES TO SUMPRINT
MISSUMPT PRTLIST TITLE1,TITLE=1,DEV=(1,2)
         PRTOPEN SYSPUNCH,OPT=(DUMMY),DEV=3
         TITLE 'I T E R A T E  ***  HOUSEKEEPING'
*---------------------------------------------------------------------*
*  PROCESS PARM FIELD, IF ANY                                         *
*---------------------------------------------------------------------*
         LA    R8,0(,R8)     CLEAN
         LTR   R8,R8         ANY ?
         BZ    NOEXPARM
         L     R8,0(,R8)           PARM PTR
         LA    R8,0(,R8)     CLEAN
         LTR   R8,R8         ANY ?
         BZ    NOEXPARM
         LH    R9,0(,R8)     GET PARM LENGTH
         LTR   R9,R9         ANY ?
         BNP   NOEXPARM
         PRTLIST MSGINPRM,DEV=2
         PARSE 2(R8),(R9),OPT=KEYWORD  COMMA/BLANK DELIMITED FIELDS
         BXH   R15,R15,NOEXPARM  NOTHING TO DO
         LA    R4,PAR@TABL   POINT TO RESULT CHAIN
         USING PRSDSECT,R4   DECLARE ENTRY
NEXTPARM ICM   R4,15,PRSLINK GET NEXT VALUE
         BZ    NOEXPARM      ALL DONE
         ICM   R0,15,PRS#TEXT  GET TEXT LENGTH
         BZ    NEXTPARM      SKIP NULL PARM
         OC    PRS$TEXT,BLANKS  UPPER CASE
         SPACE 1
*---------------------------------------------------------------------*
*    DATE=YYDDD   OR  YYYYDDD    SETS DESIRED CREATION DATE           *
*    DATE=YYMMDD  OR  YYYYMMDD   SETS DESIRED CREATION DATE           *
*    MEMBERS CREATED BEFORE THEN, OR HAVING NO DATE, ARE OMITTED      *
*---------------------------------------------------------------------*
PARMNLIN CLC   =C'DATE ',PRSKEYWD  IS IT DATE= ?
         BNE   PARMFLAG      NO; ASSUME FLAG
         ICM   R1,15,PRS#TINT+4  VALID INTEGER ?
         BNP   PARMBAD       NO; FAIL
         CH    R0,=H'8'      YYYYMMDD FORMAT ?
         BE    PARMDAT8      YES; CONVERT TO JULIAN FORM
         BH    PARMBAD       TOO LONG; TOO BAD
         CH    R0,=H'5'      YYDDD FORMAT ?
         BE    PARMDAT5      NO; SET DATE AS IS
         BL    PARMBAD       TOO SHORT; TOO BAD
         CH    R0,=H'7'      YYYYDDD FORMAT ?
         BE    PARMDATE      YES; USE IT AS IS
* YYMMDD
         L     R2,=F'19000000' MAKE LAST CENTURY FOR 65-99
         C     R1,=F'650000'  PRIOR TO 65 ?
         BNL   *+8
         L     R2,=F'20000000' MAKE THIS CENTURY FOR 00-64
         AR    R1,R2         NOW HAVE YYYYMMDD
* YYYYMMDD
PARMDAT8 SR    R0,R0
         D     R0,=F'10000'  ISOLATE YEAR IN R1
         LR    R14,R1        SAVE THE YEAR
         SRDA  R0,32         ADJUST FOR NEXT DIVIDE
         D     R0,=F'100'    SPLIT DAY / MONTH
         LTR   R0,R0         DAY > 0 ?
         BNP   PARMBAD
         CH    R0,=H'31'     DAY < 32 ?
         BH    PARMBAD
         LTR   R1,R1         MONTH > 0 ?
         BNP   PARMBAD
         CH    R1,=H'12'     MONTH < 13 ?
         BH    PARMBAD
         LA    R2,NORMYEAR-2
         EX    R14,EXISLEAP  TEST FOR LEAP YEAR
         BNZ   *+8
         LA    R2,LEAPYEAR-2 ELSE GET LEAP YEAR TABLE
         SLA   R1,1          CONVERT MONTH TO TABLE OFFSET
         LH    R1,0(R1,R2)   LOAD DAYS PRIOR TO MONTH
         AR    R1,R0         DAYS IN THIS MONTH
         MH    R14,=H'1000'  SHIFT YEAR
         AR    R14,R1
         CVD   R14,DB        CONVERT TP PACKED FORM
         B     PARMDATZ
         SPACE 1
* YYDDD
PARMDAT5 L     R2,=F'1900000'  MAKE LAST CENTURY
         C     R1,=F'65000'  WRAPPED ?
         BNL   *+8
         L     R2,=F'2000000'  MAKE THIS CENTURY
         AR    R1,R2         MAKE SEVEN DIGIT DATE
* YYYYDDD
PARMDATE CVD   R1,DB         ALSO MAKE PACKED
PARMDATZ ZAP   JULDATE,DB    AND SAVE IT
         CP    JULDATE+L'JULDATE-2(2),=P'1'  LEGAL ?
         BL    PARMBAD       NO
         CP    JULDATE+L'JULDATE-2(2),=P'366'  LEGAL ?
         BH    PARMBAD       NO
         CP    JULDATE,=P'1965001'  REASONABLE ?
         BL    PARMBAD
         CP    JULDATE,=P'2099365'  REASONABLE ?
         BH    PARMBAD
         PRTLIST MSGDATE,DEV=2
         OI    PROFLAGS,FGDATE  SHOW DATE SELECTED
         B     NEXTPARM      LOOK FOR MORE
NORMYEAR DC    H'0,31,59,90,120,151,181,212,243,273,304,334,365'
LEAPYEAR DC    H'0,31,60,91,121,152,182,213,244,274,305,335,366'
EXISLEAP TM    =X'03',*-*    MULTIPLE OF FOUR?
         SPACE 1
*---------------------------------------------------------------------*
*    EVERYTHING ELSE MUST BE A SELF-DEFINING KEYWORD (OPTION FLAG)    *
*---------------------------------------------------------------------*
PARMFLAG PARFGSET PARMOPTP,ERR=PARMBAD,DONE=NEXTPARM
         SPACE 1
PARMBAD  DS    0H
         PRTLIST MSGBDPRM,DEV==ERRDEV
         MVICC 4             MINOR ERROR
         B     NEXTPARM      DO ANOTHER
         SPACE 1
         PARKEYBX PARMOPT    BXLE FOR PARM OPTIONS
         DROP  R4
         SPACE 1
*---------------------------------------------------------------------*
*  CHECK 'SYSIN' INPUT DD - IF NOT FOUND OR DD DUMMY, LOOK FOR SYSLIB *
*---------------------------------------------------------------------*
NOEXPARM DEVTYPE SYSIN,DB    LOOK FOR SYSIN
         BXH   R15,R15,MISSSYN     MISSING SYSLIB DD ?
         CLI   DB+2,0        CHECK MAJOR DEVICE TYPE
         BE    MISSSYN       DD DUMMY - LOOK FOR SYSLIB
         TITLE 'I T E R A T E  ***  PROCESS SYSIN INPUT'
         INPOPEN SYSIN OPT=(WTO)  ABSOLUTELY REQUIRED
         BXLE  R15,R15,HAVESYN     MISSING SYSLIB DD ?
         SPACE 1
MISSSYN  PRTL  '0DD FOR SYSIN/JCL PATTERN FILE UNUSABLE',DEV==ERRDEV
         SERVTERM ,          CLOSE AND FREE STUFF
         ABEND 2540
         SPACE 1
HAVESYN  INPOPEN EXCLUDE,DEV=2,OPT=(NOWTO,DUMMY)                GP12043
         CH    R15,=H'4'     PRESENT ?                          GP12043
         BH    DONEEXC         NO; IGNORE                       GP12043
         MVI   SKIPMEM+L'SKIPMEM-1,X'FE'    FORCE FIRST READ    GP12043
         BAL   R14,NEXTSKIP  READ NEXT SKIP CARD                GP12043
DONEEXC  LA    R10,@JCLSECT  POINT TO CHAIN HEAD                GP12043
SYNMORE  INPGET ,            GET NEXT RECORD
         BXH   R15,R15,SYNEOF  QUIT ON EOF OR ERROR
         LR    R2,R1         SAVE RECORD ADDRESS
         LR    R3,R0         AND LENGTH
         USING JCLDSECT,R10
         STORAGE OBTAIN,LENGTH=JCLLEN  GET ROOM FOR NEXT CARD
         ST    R1,JCLLINK    MAKE CHAIN
         LR    R10,R1        MAKE NEW ENTRY
         XC    JCLDSECT(JCLCARD-JCLDSECT),JCLDSECT
         ICM   R3,8,BLANKS   SET BLANK FILL
         LA    R14,JCLCARD
         LA    R15,L'JCLCARD
         MVCL  R14,R2        SAVE CARD IMAGE
         LA    R6,JCLCARD
         LA    R15,L'JCLCARD-5
SYNLOOP  EX    R15,EXPOUND   LOOK FOR POUND SIGN
         BZ    SYNMORE       NONE
         CLC   =C'MEMBER#',1(R1)
         BE    SYNFMEMB
         CLC   =C'DSN#',1(R1)
         BE    SYNFOUND
         CLC   =C'VOL#',1(R1)
         BE    SYNFOUND
         LA    R6,1(,R1)     LOOK AT NEXT COLUMN
         LA    R15,JCLCARD+L'JCLCARD-5
         SR    R15,R6        REMAINING LENGTH
         BNM   SYNLOOP       TRY AGAIN
         B     SYNMORE
SYNFMEMB OI    JCLFLAGS,JFMEMB  FLAG FOR LIST PROCESSING
SYNFOUND OI    JCLFLAGS,JFEDIT  NEED TO EDIT THIS CARD
         LA    R0,JCLCARD
         SR    R1,R0         OFFSET TO HIT
         STC   R1,JCLOFFS    SAVE OFFSET
         B     SYNMORE       LOOK FOR ANOTHER CARD TO DO
EXPOUND  TRT   0(0,R6),TRTPOUND  LOOK FOR POUND SIGN
         SPACE 1
*---------------------------------------------------------------------*
*   END OF SYSIN INPUT - PROCESS SYSLIB FILE(S)                       *
*---------------------------------------------------------------------*
SYNEOF   TM    PROFLAGS,FGJCL  IN JCL PROCESSING ?
         BZ    TESTINP
         ICM   R10,15,@JCLSECT  POINT TO FIRST JCL ENTRY
         BZ    TESTINP
         NI    JCLFLAGS,255-JFMEMB  DO SINGLE SUBSTITUTION ON JOB CARD
TESTINP  DEVTYPE INDS+DCBDDNAM-IHADCB,DB  DO SYSLIB ALSO ?
         BXH   R15,R15,PREEXIT     NO
         CLI   DB+2,UCB3DACC       IS IT DIRECT ACCESS?
         BNE   PREEXIT             BR IF NO -- JUST EXIT
         B     LIBISDA             PROCESS SYSLIB TOO
         TITLE 'I T E R A T E  ***  PROCESS SYSLIB DD(S)'
*---------------------------------------------------------------------*
*  CHECK 'SYSLIB' INPUT DD - MUST BE PDS OR PDS/E                     *
*---------------------------------------------------------------------*
OMITSYN  DEVTYPE INDS+DCBDDNAM-IHADCB,DB
         BXH   R15,R15,INVDSORG    MISSING SYSLIB DD ?
         CLI   DB+2,UCB3DACC       IS IT DIRECT ACCESS?
         BNE   INVDSORG            BR IF NO -- INVALID DEVTYPE
         SPACE 2
LIBISDA  SERVCALL RJFCB,DCBDDNAM+INDS-IHADCB  GET JFCB FOR DDNM
         BXH   R15,R15,INVDSORG    BAD DD ?
         MVC   MYJFCB(JFCBLGTH),0(R1)    SAVE IT HERE
         SERVCALL DSDJ1,JFCBDSNM   DSNAME POINTER
         BXH   R15,R15,INVDSORG    BAD DD ?
         MVC   DS1FMTID(96),0(R1)  SAVE DSCB
**  TEST DSORG FOR ACTUAL DATA SET TYPE
         CLI   DS1DSORG+1,0       VSAM OR OTHER FUNNIES ?       GP09186
         BNE   INVDSORG             YES; FAIL                   GP09186
         TM    DS1DSORG,DS1DSGPO   IS IT PARTITIONED?
         BO    DSISPO
**  DATA SET IS NEITHER PS NOR PO
INVDSORG PRTL '0***** SYSLIB DD IS MISSING OR UNUSABLE *****',DEV==ERRD*
               EV
         SERVTERM ,
         ABEND 213,DUMP
         SPACE 2
**  DATA SET IS PO  -- TEST WHETHER MEMBER WAS SPECIFIED
DSISPO   MVI   MASKMEM,C'*'    SET UNIVERSAL ACCEPTANCE MASK
         TM    JFCBIND1,JFCPDS  DID USER SPECIFY A MEMBER ?
         BZ    DSOPEN        NO; GO TO OPEN
         MVC   MASKMEM,JFCBELNM  SAVE THE MEMBER MASK
         XC    JFCBELNM,JFCBELNM  CLEAR MEMBER NAME FROM JFCB
         NI    JFCBIND1,255-JFCPDS  RESET MEMBER FLAG
         OI    JFCBTSDM,JFCNWRIT  DON'T WRITE BACK
         SPACE 2
*---------------------------------------------------------------------*
*  OPEN PDS FILE AND BEGIN PROCESSING                                 *
*---------------------------------------------------------------------*
DSOPEN   OPEN  (INDS,INPUT),MF=(E,OPENLIST),TYPE=J
         LA    R3,JFCBDSNM
         ST    R3,ADDDSN     SAVE DSN ADDRESS
         LA    R0,L'JFCBDSNM  MAX LENGTH
         BAL   R14,CALCLEN   CALCULATE DSN LENGTH
         ST    R1,ADDDSN+4   SAVE LENGTH
         LA    R3,JFCBVOLS
         ST    R3,ADDVOL     SAVE VOL ADDRESS
         LA    R0,6 L'JFCBVOLS  MAX LENGTH
         BAL   R14,CALCLEN   CALCULATE VOL LENGTH
         ST    R1,ADDVOL+4   SAVE LENGTH
         SPACE 1
         PUSH  USING
         SUBCALL SUBDSERV,('INIT',ROOTPTR,INDS),VL,MF=(E,CALLPARM)
         LTR   R15,R1        DID IT WORK?
         BNZ   DESERVED      YES; CONTINUE
BADSERV  PRTLIST MSGBDSRV,DEV==ERRDEV  LET USER KNOW
         SERVTERM ,          CLOSE AND FREE STUFF
         ABEND 18,DUMP
         TITLE 'I T E R A T E  ***  PROCESS NEXT MEMBER'
*---------------------------------------------------------------------*
*   LOOP HERE UNTIL ALL MEMBER NAMES ARE PROCESSED                    *
*---------------------------------------------------------------------*
DESERVED PRTL  '0THE FOLLOWING MEMBERS WILL BE PROCESSED:',DEV=2
         PRTSPACE 1,DEV=2
         PRTDATA (BLANKS,1,NL),DEV=2  INDENT 2                  GP12043
         SPACE 2
MEMBLOOP SUBCALL SUBDSERV,('LOOP',ROOTPTR,MAINBXLE),VL,MF=(E,CALLPARM)
         LTR   R5,R15        SUCCESSFUL ?
         BZ    PREEXIT       NO; ALL DONE
         ICM   R14,15,@SMDES  FIRST ENTRY ?
         BNZ   HAVESMDE
         ST    R5,@SMDES     SAVE FIRST ONE
         USING SMDE,R5       TEMPORARY ENTRY DECLARATION
HAVESMDE TM    PROFLAGS,FGALIAS  WANT ALII ALSO ?
         BNZ   WANTSMDE      YES; SKIP ALIAS TEST
         TM    SMFG,SMFGALI  ALIAS ENTRY ?
         BNZ   MEMBLOOP      NOT WANTED
         SPACE 1
WANTSMDE LA    R2,CURRMEMB
         LA    R3,L'CURRMEMB
         ICM   R1,8,BLANKS
         MVCL  R2,R0         COPY MEMBER NAME
*---------------------------------------------------------------------*
*    CHECK FOR MEMBER MASK                                            *
*---------------------------------------------------------------------*
         LA    R14,MASKMEM   POINT TO MASK
         LA    R15,L'MASKMEM
         LA    R1,CURRMEMB   POINT TO MEMBER
         BAL   R9,CLCMASK    DOES MEMBER MATCH ?
         BNE   MEMBLOOP      NO; REJECT
*---------------------------------------------------------------------*
*    CHECK FOR MEMBER EXCLUSION                                       *
*---------------------------------------------------------------------*
LOOPSKIP CLC   CURRMEMB,SKIPMEM   SKIP IT ?                     GP12043
         BL    PROCMEM       NO; PROCESS                        GP12043
         BE    SKIPSKIP      NO; BUT ADVANCE EXCLUDE FILE       GP12043
         BAL   R14,NEXTSKIP  LOOK FOR NEXT NAME                 GP12043
         B     LOOPSKIP        AND TRY AGAIN                    GP12043
SKIPSKIP BAL   R14,NEXTSKIP  LOOK FOR NEXT NAME                 GP12043
         PRTDATA 'MEMBER',(CURRMEMB,PAD),'EXCLUDED BY DD',             *
               (BLANKS,1,NL),DEV=2                              GP12043
         B     MEMBLOOP        AND IGNORE THIS ONE              GP12043
*---------------------------------------------------------------------*
*    CHECK FOR CREATION DATE SELECTION                                *
*---------------------------------------------------------------------*
PROCMEM  TM    PROFLAGS,FGDATE  SCREEN BY DATE ?
         BZ    ACCEPT        NO; JUST PROCESS
         TM    SMFG,SMFGLMOD  LOAD MODULE OR OBJECT?
         BNZ   MEMBLOOP      YES; CREATON DATE NOT AVAILABLE
         LH    R7,SMDEUSLN   LENGTH OF USER DATA
         CH    R7,=H'30'     ISPF/PDF LENGTH ?
         BNE   MEMBLOOP      NO; OTHER PROGRAMS HAVE NO CREDT
         LH    R6,SMDEUSOF   GET USER DATA OFFSET
         AR    R6,R5         USER DATA
         USING SPFVERL,R6    DECLARE SPF ENTRY
         TM    SPFCRTDT+L'SPFCRTDT-1,X'0F'  VALID SIGN ?
         BNO   MEMBLOOP      NO; IGNORE
         TM    SPFCRTDT,X'FE'  CENTURY SPECIFIED ?
         BNZ   HAVECENT
         AP    SPFCRTDT,=P'1900000'  GIVE IT A SPORTING CHANCE
HAVECENT CP    SPFCRTDT,JULDATE  OLDER THAN CUTOFF ?
         BL    MEMBLOOP      YES; SKIP
         DROP  R6
         SPACE 1
*---------------------------------------------------------------------*
*    MATCHES ALL SELECTION REQUESTS - PROCESS                         *
*---------------------------------------------------------------------*
ACCEPT   OI    SMFG,SMFGDONE  FLAG IT AS HAVING BEEN DONE
         INC   NUMPROC       COUNT PROCESSED
         PRTITEM MSGMEMNM,DEV=2    PRINT MEMBERNAME ONLY
         LA    R0,L'CURRMEMB                                    GP12045
         LA    R3,CURRMEMB                                      GP12045
         BAL   R14,CALCLEN                                      GP12045
         ST    R1,ADDMEM+4   SAVE MEMBER NAME LENGTH            GP12045
         CLI   FSTMEMB,C' '  FIRST MATCH ?
         BH    HAVEFMEM      NO
         MVC   FSTMEMB,CURRMEMB  SAVE FIRST MEMBER NAME
         MVC   ADDFMEM+4(4),ADDMEM+4  SAVE LENGTH
         LA    R14,FSTMEMB
         ST    R14,ADDFMEM   SAVE ADDRESS
         LA    R14,CURRMEMB
         ST    R14,ADDMEM    SAVE ADDRESS
HAVEFMEM TM    PROFLAGS,FGLIST  LIST MODE ?
         BNZ   MEMBLOOP      YES; PROCESS JCL LATER
         PRTLIST DEV1TIT2,TITLE=2    PRINT SUBHEADER ON SYSPRINT
         SPACE 1
***********************************************************************
*   PROCESS ONE MEMBER - (RE)READ JCL, EDIT, AND SUBMIT/PUNCH         *
***********************************************************************
         LA    R10,@JCLSECT  POINT TO CHAIN OF CARDS
OUTLOOP  ICM   R10,15,JCLLINK  GET NEXT CARD OR OUT
         BZ    MEMBLOOP      DONE WITH MEMBER
         BAL   R14,CARDEDIT  EDIT ONE CARD
         B     OUTLOOP       DO NEXT CARD
         SPACE 2
CARDEDIT STM   R0,R15,SUBSAVE2  SAVE CALLER'S REGISTERS
         TM    JCLFLAGS,JFEDIT  USE IMAGE AS IS ?
         BNZ   OUTANDIN      NO; HANDLE INSERTION
         PRTLIST PUNCARD,DEV=3  PUNCH IT
         PRTLIST FDCARD      SHOW CARD IMAGE
         B     CARDEX0       DO NEXT
         SPACE 1
*---------------------------------------------------------------------*
*   NEED TO HANDLE INSERTIONS                                         *
*   R4,R5 OUTPUT ADDR/LEN  R6,R7 INPUT ADDR/LEN  R8,R9 INSERT ADDR/LN *
*---------------------------------------------------------------------*
OUTANDIN MVI   OUTWORK,C' '  CLEAR OUTPUT
         MVC   OUTWORK+1(L'OUTWORK-1),OUTWORK
         LA    R4,OUTWORK
         LA    R3,L'OUTWORK
         LA    R6,JCLCARD
         LA    R7,L'JCLCARD
         TM    PROFLAGS,FGASM   ASSEMBLER MODE ?
         BNZ   OUTBOUND
         CLC   =C'//',JCLCARD   JCL CARD ?
         BNE   OUTSUB        NO; START SUBSTITUTING
OUTBOUND MVC   OUTWORK+71(9),JCLCARD+71  PRESERVE CONTINUATION AND SEQ.
         LA    R3,71         CLIP OUT
         LA    R7,71         CLIP IN
OUTSUB   LTR   R5,R3         ROOM IN OUTPUT ?
         LTR   R5,R3         ROOM IN OUTPUT ?
         BNP   OUTDONE       NO; QUIT NOW
         LR    R15,R7        COPY RESIDUAL LENGTH
         SH    R15,=H'1'     RELATIVITY
         BM    OUTDONE       NO MORE INPUT
         EX    R15,EXPOUND   FIND THE NEXT POUND SIGN
         BZ    OUTLAST
         LR    R14,R6        COPY START FOR MVC
         LR    R15,R1        TRT STOPPER
         SR    R15,R14       LENGTH TO MOVE
         BNP   OUTNPFX       NOTHING
         SR    R7,R15        ADJUST RESIDUAL LENGTH
         AR    R6,R15        ADJUST NEXT MOVE ADDRESS
         MIN   (R5),(R15)    USE SHORTER
         SR    R3,R5         RESIDUAL LENGTH
         MVCL  R4,R14        MOVE THIS PORTION
         LR    R5,R3         COPY REMAINING LENGTH
OUTNPFX  CLC   =C'MEMBER#',1(R1)
         BE    OUTEMEM
         CLC   =C'DSN#',1(R1)
         BE    OUTEDSN
         CLC   =C'VOL#',1(R1)
         BE    OUTEVOL
         MVC   0(1,R4),0(R6)  COPY SINGLE POUND SIGN
         LA    R4,1(,R4)     ADJUST
         LA    R6,1(,R6)
         BCTR  R3,0
         BCTR  R7,0
         B     OUTSUB        TRY FOR ANOTHER
OUTEDSN  LA    R0,5          LENGTH OF #DSN#
         LM    R8,R9,ADDDSN  ADDRESS/LENGTH OF REPLACMENT
         B     OUTEDIT       AND INSERT
OUTEVOL  LA    R0,5          LENGTH OF #VOL#
         LM    R8,R9,ADDVOL  ADDRESS/LENGTH OF REPLACMENT
         B     OUTEDIT       AND INSERT
OUTEMEM  LA    R0,8          LENGTH OF #MEMBER#
         LM    R8,R9,ADDMEM  ADDRESS/LENGTH OF REPLACMENT
         SPACE 1
OUTEDIT  AR    R6,R0         SKIP OVER INPUT
         SR    R7,R0         AND FIX RESIDUAL LENGTH
         LR    R5,R3         COPY OUTPUT LENGTH
         MIN   (R5),(R9)     USE SHORTER
         SR    R3,R5         RESIDUAL LENGTH
         MVCL  R4,R8         MOVE THIS PORTION
         B     OUTSUB        AND TRY FOR MORE
         SPACE 1
OUTLAST  ICM   R7,8,BLANKS   JUST IN CASE
         MVCL  R4,R6         COPY REST
OUTDONE  PRTF  OUTWORK,L'JCLCARD,CC=NO,DEV=3
         PRTLIST FDWORK      SHOW EDITED IMAGE
CARDEX0  LM    R0,R15,SUBSAVE2  RESTORE CALLER'S REGISTERS
         BR    R14           AND RETURN
         POP   USING
         SPACE 2
*---------------------------------------------------------------------*
*   END OF MEMBER LIST PASS - IN LIST MODE PROCESS JCL                *
*---------------------------------------------------------------------*
PREEXIT  TM    PROFLAGS,FGLIST  LIST MODE ?
         BZ    ALLDONE       NO; FINISHED
         MVC   CURRMEMB,FSTMEMB  RESTORE FIRST MEMBER FOR JOB CARD
         MVC   ADDMEM(8),ADDFMEM  DITTO FOR ADDR/LEN
         SPACE 2
         LA    R10,@JCLSECT  POINT TO JCL CHAIN
LOOPOJCL ICM   R10,15,JCLLINK  GET NEXT CARD OR OUT
         BZ    DONEOJCL      DONE WITH MEMBER
         TM    JCLFLAGS,JFMEMB  MEMBER NAME ON THIS IMAGE ?
         BNZ   INITMEMB      YES; LOOP THROUGH NAMES
         BAL   R14,CARDEDIT  PROCESS THIS CARD
*DO_JCL* B     LOOPOJCL      LOOK AT NEXT CARD
         SPACE 1
INITMEMB L     R5,@SMDES     LOAD FIRST SMDE ADDRESS
         B     LOOPMEM2
LOOPMEMB SH    R5,=AL2(4)    RETRIEVE LINK POINTER
         ICM   R5,15,0(R5)   DONE IF ZERO
         BZ    LOOPOJCL      DO STATISTICS
         LA    R5,4(,R5)     GET SMDE AGAIN
         USING SMDE,R5       TEMPORARY ENTRY DECLARATION
LOOPMEM2 TM    SMFG,SMFGDONE   NEED TO DO ?
         BZ    LOOPMEMB      NO; SKIP
         SR    R14,R14       CLEAR FOR IC
         ICM   R14,3,SMDENMOF GET OFFSET TO NAME SECTION
         AR    R14,R5         GET PMAR ADDRESS
         USING SMDENAME,R14   DECLARE IT
         LH    R1,SMDENMLN    GET LENGTH OF NAME >= 8
         LA    R0,SMDENMVL    POINT TO NAME
         DROP  R14
         LA    R2,CURRMEMB
         LA    R3,L'CURRMEMB
         ICM   R1,8,BLANKS
         MVCL  R2,R0         COPY MEMBER NAME
         LA    R3,CURRMEMB
         ST    R3,ADDMEM     SAVE MEMBER ADDRESS
         LA    R0,L'CURRMEMB  MAX LENGTH
         BAL   R14,CALCLEN   CALCULATE LENGTH
         ST    R1,ADDMEM+4   SAVE LENGTH
         BAL   R14,CARDEDIT  EDIT THIS CARD
         B     LOOPMEMB      DO NEXT MEMBER NAME, IF ANY
         SPACE 1
DONEOJCL DS    0H            NOTHING MORE FOR NOW ?
*---------------------------------------------------------------------*
*   END OF JOB - CLOSE AND FREE EVERYTHING; RETURN WITH CODE IN R15   *
*---------------------------------------------------------------------*
ALLDONE  PRTLIST FINALS,DEV=2
         SUBCALL SUBDSERV,('END',ROOTPTR),VL,MF=(E,CALLPARM)
         SPACE 1
PGMEXIT  CLOSE MF=(E,OPENLIST)
         SERVTERM ,          CLOSE AND FREE STUFF
         PGMEXIT RC=RETCODE  SET RETURN CODE
         SPACE 1
CLCMASK  CMASK L=R15,MASKEND='*',MASKEQU='#-'
         SPACE 1
***********************************************************************
*   NEXTSKIP: READ NEXT EXCLUDE CARD, EXTRACT MEMBER NAME             *
*     SKIP COMMENTS CARDS (* IN COLUMN ONE)                           *
*     SKIP COMMENTS AFTER FIRST WORD (MEMBER NAME)                    *
*     SKIP BLANK CARDS (1-71)                                         *
***********************************************************************
NEXTSKIP STM   R0,R15,SUBSAVE2 SAVE A LITTLE                    GP12043
         CLC   SKIPMEM,=8X'FF'    ALREADY AT END ?              GP12043
         BE    NEXTSKEX        YES; DON'T ISSUE GET             GP12043
NEXTSKGT INPGET DEV=2        GET NEXT EXTRACT IMAGE             GP12043
         LTR   R15,R15       ERROR OR EOF ?                     GP12043
         BNZ   NEXTSKEF        YES; FORCE END                   GP12043
         CLI   0(R1),C'*'    COMMENTS CARD?                     GP12043
         BE    NEXTSKGT        YES; SKIP                        GP12043
         LR    R3,R1         SAVE START ADDRESS                 GP12043
         LA    R1,8(,R3)     STOPPER ADDRESS                    GP12043
         TRT   0(8,R3),TRTSPACE   FIND END                      GP12043
         SR    R1,R3         LENGTH                             GP12043
         BNP   NEXTSKGT        IF ALL BLANK, TREAT AS COMMENT   GP12043
         MVC   SKIPMEM,BLANKS                                   GP12043
         BCTR  R1,0          EX LEN                             GP12043
         EX    R1,EXMVCSKP   MOVE SKIP NAME                     GP12043
         B     NEXTSKEX                                         GP12043
         SPACE 1                                                GP12043
NEXTSKEF MVC   SKIPMEM,=8X'FF'    LOGICAL END OF SKIPPING       GP12043
NEXTSKEX LM    R0,R15,SUBSAVE2 RESTORE SOME                     GP12043
         BR    R14           RETURN
EXMVCSKP MVC   SKIPMEM(0),0(R3)   MOV MEMBER NAME TO BE SKIPPED GP12043
         SPACE 1
***********************************************************************
*   CALCLEN : CALCULATE STRING LENGTH (ENDING WITH SPACE OR NULL)     *
*   R3 - START ADDRESS                                                *
*   R0 - MAX LENGTH                                                   *
*                                                                     *
*   R14 - RETURN ADDRSS                                               *
*   R1  - RETURN LENGTH                                               *
***********************************************************************
CALCLEN  STM   R2,R4,28(R13) SAVE A LITTLE
         LR    R4,R0         COPY MAX LENGTH
         LA    R1,0(R4,R3)   END ADDRESS IF FULL LENGTH
         SH    R4,=H'1'      RELATIVITY
         BM    CALCLENX      SKIP OUT
         EX    R4,EXGETBLK   FIND FIRST BLANK OR NULL
CALCLENX SR    R1,R3         SIGNIFICANT LENGTH
         LM    R2,R4,28(13)  RESTORE SOME
         BR    R14           RETURN
EXGETBLK TRT   0(0,R3),TRTSPACE
         TITLE 'I T E R A T E  ***  CONSTANTS'
SYSPRINT PRTWORK SYSPRINT,TITLE=4  SHORT TITLE WRITTEN
SUMPRINT PRTWORK SUMPRINT,TITLE=3
SYSPUNCH PUNWORK SYSPUNCH,INTRDR
SYSIN    INPWORK SYSIN,JCL,EDIT=128
EXCLUDE  INPWORK EXCLUDE,IGNORE,EDIT=128,EODAD=NEXTSKEX         GP12043
         SPACE 1
DCH1     DC    H'1'          FDCLC OPERAND
BLANKS   DC    CL80' '
         SPACE 1
TITLE1   FDOPT NL,CC=C'#'    AUTO PAGE/DATE/TIME PROCESSING
         FDPRT 'MEMBER ITERATION',RADJ,LEN=73
         FD    *END
         TITLE 'I T E R A T E  ***  DSECTS AND UNBASED CONSTANTS'
PARMOPT  PARKEYFG 'DEBUG',PROFLAGS,0,FGDEBUG  DEBUG MODE
PARMOPT2 PARKEYFG 'ASSEMBLER',PROFLAGS,0,FGASM
         PARKEYFG 'ASSEMBLY',PROFLAGS,0,FGASM
         PARKEYFG 'ASM',PROFLAGS,0,FGASM
         PARKEYFG 'JCL',PROFLAGS,0,FGASM+FGJCL
         PARKEYFG 'JOB',PROFLAGS,0,FGASM+FGJCL
         PARKEYFG 'ALIAS',PROFLAGS,0,FGALIAS  INCLUDE ALIAS NAMES
         PARKEYFG 'LIST',PROFLAGS,0,FGLIST    BUILD MEMBER NAME LIST
PARMOPTN PARKEYFG 'TEST',PROFLAGS,0,FGDEBUG   DEBUG MODE
         SPACE 2
MSGBDSRV FDPRT '*** DESERV FAILED ***',NL
         FDPRT *END
         SPACE 1
MSGINPRM FDPRT '  PARM:',NL,PADR
         FDPRT 2(R8),(R9)
         FDPRT *END
         SPACE 1
         PUSH  USING
         USING PRSDSECT,R4
MSGBDPRM FDPRT '*** UNSUPPORTED PARM',NL,PADR
         FDCLC PRSKEYWD,BLANKS,BE=MSGBDPRN
         FDCLC PRSKEYWD,ZEROES,BE=MSGBDPRN
         FDPRT PRSKEYWD,DEB
         FDPRT '='
MSGBDPRN FDPRT PRS$TEXT,DEB,PADR
         FDPRT 'IGNORED ***'
         FD    *END
         POP   USING
MSGMEMNM FDPRT CURRMEMB,PADL
         FD    *END
         SPACE 1
DEV1TIT2 FDPRT 'EXPANSION FOR MEMBER',NL
         FDPRT CURRMEMB,PADL
         FD    *END
         SPACE 1
MSGDATE  FDPRT 'MODULES CREATED PRIOR TO',NL
         FDPRT JULDATE,D,PAD
         FDPRT 'WILL BE SKIPPED'
         FD    *END
         SPACE 1
FDCARD   FDPRT ' ',NL,LEN=10
         FDPRT JCLCARD
         FDPRT *END
         SPACE 1
PUNCARD  FDPRT JCLCARD,NL
         FDPRT *END
         SPACE 1
FDWORK   FDPRT ' ',NL,LEN=10
         FDPRT OUTWORK,L'JCLCARD
         FDPRT *END
         SPACE 1
FINALS   FDOPT NL,CC=C'0'    DOUBLE-SPACE
FINALCM  FDPRT NUMPROC,I,RADJ,NL,LEN=9
         FDPRT 'MEMBERS PROCESSED',PAD
         FD    *END
FINALSZ  FDPRT 'NO MEMBERS WERE FOUND FOR PROCESSING'
         FD    *END
         SPACE 2
PATSTART DS    0F     PATTERN DATA TO BE COPIED TO DYNAMIC SAVE AREA
PATINDS  DCB   DSORG=PS,MACRF=RP,DDNAME=SYSLIB,EXLST=PATEXLST,         *
               RECFM=U,LRECL=256,BLKSIZE=256,KEYLEN=8
PATOPLST OPEN  (PATINDS),MF=L
PATEXLST DC    X'87',AL3(0)  JFCB EXIT
PATSTLEN EQU   *-PATSTART    MOVED TO SAVE AREA
         SPACE 1
TRNONULL DC    C' ',255AL1(*-TRNONULL)
         SPACE 1
TRTPOUND DC    256AL1(0)     PASS EVERYTHING
         TRENT TRTPOUND,4,C'#',C'-'  EXCEPT POUND AND MINUS SIGNS
         SPACE 1
TRTSPACE DC    256AL1(0)     PASS EVERYTHING
         TRENT TRTSPACE,4,X'00',C' '  EXCEPT SPACE AND NULL
         SPACE 2
SAVE     DSECT ,             NOTE STACK AT BEGINNING
SUBSAVE  DS    18A           SUBROUTINE SAVE AREA
SUBSAVE2 DS    18A           SUBROUTINE SAVE AREA
ZEROES   DS    D
DB       DS    D
@JCLSECT DS    A             ADDRESS OF JCL CARDS
@SMDES   DS    A             ADDRESS OF SMDE CHAIN (LINK @ -4)
ROOTPTR  DS    A             START OF DESERV RESPONSE
ADDDSN   DS    A,F           ADDRESS/LENGTH OF DATA SET NAME
ADDVOL   DS    A,F           ADDRESS/LENGTH OF VOLUME SERIAL
ADDMEM   DS    A,F           ADDRESS/LENGTH OF MEMBER NAME
ADDFMEM  DS    A,F           ADDRESS/LENGTH OF FIRST MEMBER
MASKMEM  DS    CL8'*'        MEMBER MASK
SKIPMEM  DS    CL8' '        NEXT MEMBER TO BE SKIPPED
FSTMEMB  DS    CL8' '        FIRST MEMBER NAME
JULDATE  DS    PL4           PACKED DATE YYYYYDDD
         SERVDEFS ,          EXPAND STUFF FOR @SERVICE, ETC.
         SPACE 1
         MAPPARSE DSECT=NO   PARSE PARAETER AREA
         SPACE 1
NUMPROC  DS    F             NUMBER OF MEMBERS FLAGGED FOR PROCESSING
MAINBXLE DS    4A            BXLE POINTER FOR DIRECTORY LIST
PAGECNT  DS    H             NUMBER OF SHEETS WRITTEN
ERRDEV   DS    X             OUTPUT DEVICE FOR ERROR & STATUS MESSAGES
         SPACE 1
***********************************************************************
**                                                                   **
WRKHEAD  DS    0A     DATA COPIED FROM PATSTART
INDS     DCB   DSORG=PO,MACRF=R,DDNAME=SYSLIB,BUFNO=2,                 *
               KEYLEN=0,NCP=2
         SPACE 1
OPENLIST OPEN  (INDS),MF=L
EXLIST   DC    X'87',AL3(JFCBDSNM)  JFCB EXIT
         SPACE 1
CURRMEMB DC    CL8' '
         SPACE 1
***  PROFLAGS SET AS FOLLOWS:
PROFLAGS DS    X
FGDEBUG  EQU   X'80'           DEBUG REQUESTED
FGASM    EQU   X'40'           ASM MARGINS 1-71
FGJCL    EQU   X'20'           PROCESSING JOB STREAM
FGLIST   EQU   X'10'           REPEAT MEMBER INSERTION IN SITU
FGDATE   EQU   X'08'           SELECT MEMBERS BY CREATION DATE>=
FGALIAS  EQU   X'04'           INCLUDE ALIAS NAMES IN SELECTION
FGDATA   EQU   X'02'           AT LEAST ONE NAME IN LIST
FGEND    EQU   X'01'           LAST CALL TO PAGEPRNT
         SPACE 1
MYJFCB   DS    0F            LIBRARY JFCB
         IEFJFCBN ,          LIBRARY JFCB
         SPACE 1
         IECSDSL1 1          LIBRARY DSCB
         SPACE 1
OUTWORK  DC    CL240' '      ARBITRARY WORK AREA > 80
         SPACE 1
SAVEEND  EQU   *
         SPACE 1
JCLDSECT DSECT ,             ARRAY OF (UNMODIFIED) JCL & DATA
JCLLINK  DS    A             NEXT CARD IMAGE
JCLFLAGS DS    X             EDIT FLAGS
JFEDIT   EQU   X'80'           TEXT HAS CHANGE (#DSN#,#MEMBER#,#VOL#)
JFMEMB   EQU   X'40'           TEXT HAS #MEMBER#
JCLOFFS  DS    AL1           OFFSET TO FIRST CHANGE
JCLCARD  DS    CL80          CARD IMAGE
JCLLEN   EQU   *-JCLDSECT      GETMAIN SIZE
         SPACE 1
         MAPPARST ,          MAP PARSE RETURN ENTRIES
         SPACE 1
         PRINT &PRTSYS
MYUCB    DSECT ,
         IEFUCBOB ,
         SPACE 1
         DCBD  DSORG=(PS,PO,DA),DEVD=DA
         EJECT ,
         PUSH  PRINT
         PRINT ON,GEN
*WHY?*   IGWDES ,
         SPACE 2
*  LOCAL FLAGS USED IN SMFG:
SMFGPROC EQU X'01'     WANT TO PROCESS THIS ENTRY
SMFGDONE EQU X'10'     THIS ENTRY HAS BEEN PROCESSED (OR SKIPPED)
         MVSSMDE ,           LOCAL VERSION OF IGWSMDE
         POP   PRINT
         SPACE 2
         IHAPDS PDSBLDL=NO
         MAPPDS ,            LOCAL MAPPING OF USER DATA IN DIR. ENTRY
         END   ,
