FMT1     TITLE 'DSCB1'
         PRINT NOGEN
         LCLA  &N
DSCB1    CSECT
         ENTRY SETXTYPE
&N       SETA  01
         SAVE  (14,12),T,*
         LR    12,15
         USING DSCB1,12
         LR    11,13
         LA    13,D1SAVE
         ST    11,D1SAVE+4
         ST    13,8(11)
         LM    10,11,0(1)
         USING DS1ADDR,10
         USING FMT1,11
         CALL  COMPSIZE,(COMPTAB)
         CALL  PUTADDR,(CVHTAB)
         MVC   DSNAME,DS1DSNAM
         MVC   FMT,DS1FMTID
         MVC   VOLSER,DS1DSSN
         MVC   INCHANGE,INCHANGE-1  CLEAR ACCESS DATE/CHANGE FLAG
         OC    DS1REFD,DS1REFD    ANY ACCESS DATE ?              81340
         BZ    UNCHANGE      NO; SKIP                            81340
         CLC   DS1REFD,INCHANGE   ALL BLANKS ?                   81340
         BE    UNCHANGE                                          81340
         MVC   INCHANGE,CHANGE   MOVE CHANGE INFO                81340
         TM    DS1DSIND,DS1DSCHA  DATASET CHANGED ?              81340
         BZ    *+10          NO                                  81340
         MVC   CHFG-CHANGE+INCHANGE(2),=C'N '  SET FLAG ON       81340
         TM    DS1REFD+2,X'0F'   OLD DTS PACKED FORMAT ?         81340
         BNO   DOCHANGE      NO                                  81340
         CLI   DS1REFD,99    PACKED ?                            81340
         BNH   DOCHANGE      NO; ASSUME DSCB FORMAT              81340
         XC    DOUBLE,DOUBLE  CLEAR WORK AREA                    81340
         MVC   DOUBLE+5(3),DS1REFD  MOVE PRESUMED PACKED DATE    81340
         CVB   15,DOUBLE     MAKE BINARY                         81340
         XR    14,14         CLEAR FOR DIVIDE                    81340
         D     14,=F'1000'                                       81340
         STCM  14,3,DS1REFD+1  SET DAY                           81340
         STC   15,DS1REFD    AND YEAR                            81340
DOCHANGE CALL  CVD,(CVDTABC)   CONVERT DATE ALSO                 81340
         OC    ACCDY-CHANGE+INCHANGE(2),=C'000'                  81340
         OC    ACCDM-CHANGE+INCHANGE(3),=C'000'                  81340
         B     DNCHANGE      DONE                                81340
UNCHANGE CALL  CVD,(CVDTAB)                                      81340
DNCHANGE MVI   CREATDAT+2,C'.'                                   81340
         OC    CREATDAT(2),=C'000'
         OC    CREATDAT+3(3),=C'000'
         MVI   EXPIRDAT+2,C'.'
         OC    EXPIRDAT(2),=C'000'
         OC    EXPIRDAT+3(3),=C'000'
         MVC   SYSCODE,DS1SYSCD
         CLC   SYSCODE(13),=XL13'0'  ALL ZEROES ?
         BNE   *+10
         OC    SYSCODE(13),=CL13' '  YES, MAKE BLANK
         CLI   DS1DSORG+1,0       VSAM OR STUFF ?               GP09186
         BNE   SKPDSG1       YES; DO ONLY SECOND BYTE           GP09186
         MVC   CHAR,DS1DSORG COPY DSORG
         LA    1,VTORG       GET BIT TABLE FOR DSORG
         BAL   9,CHARBIT     CONVERT TO EBCDIC
SKPDSG1  MVC   CHAR,DS1DSORG+1                              *TSM*80198
         LA    1,VTORG2                                     *TSM*80198
         BAL   9,CHARLP      CONTINUE WITH 2ND BYTE OF DSORG*TSM*80198
         MVC   DSORG,CHAROUT STASH IN O/P
         MVC   CHAR,DS1RECFM   GET RECORD FORMAT
         LA    1,VTREC       GET TABLE
         BAL   9,CHARBIT     CONVERT
         MVC   RECFM(5),CHAROUT   SHOW RECORD FORMAT
         MVC   CHAR,DS1OPTCD  GET OPTCD
         LA    1,VTOPT       GET STANDARD TABLE
         TM    DS1DSORG,X'80'  IS IT AN ISAM DSN ?
         BZ    *+8           NO, LEAVE AS IS
         LA    1,VTOPTIS     GET SPECIAL ISAM OPTCD TABLE
         TM    DS1DSORG,X'20'   BDAM DATASET ?
         BZ    *+8                NO
         LA    1,VTOPTDA     GET SPECIAL BDAM OPTION TABLE
         TM    DS1DSORG,X'08'  DS1ORGAM - VSAM DATASET ?         90253
         BZ    *+8           NO                                  90253
         LA    1,VTOPTAM     YES; USE SPECIAL NAMES              90253
         BAL   9,CHARBIT     CONVERT OPTCD
         MVC   OPTCODE(4),CHAROUT   MOVE IT
         IC    2,DS1OPTCD
         BAL   14,BOOLIT
         MVC   OPTCD,WORK9
         IC    2,DS1DSIND
         BAL   14,BOOLIT
         MVC   DSIND,WORK9
         MVC   CHAR,DS1SCALO   GET SECONDARY ALLOATION
         OI    CHAR,X'20' **** UNUSED BIT TO FORCE 'REL TRK' *****
         LA    1,VTALO
         BAL   9,CHARBIT     GET SECONDARY ALLOCATION AND ALLOC OPTIONS
         MVC   DSALLOC,CHAROUT
         IC    2,DS1SCALO
         BAL   14,BOOLIT
         MVC   SCALO,WORK9
         LA    2,3
         LA    3,DS1EXT1
         LA    4,XTYPE&N
         L     15,=V(SETXTYPE)
         BALR  14,15
         SPACE 2
*        CODE TO AVOID PRINTING UNNECESSARY EXTENT LINES
*        IF THE LAST TWO LINES ARE ZERO, THEY WILL NOT BE PRINTED
*        DONE BY DECREASING THE PRINT LINE COUNT IN THE CALL
*
         XR    4,4           CLEAR FOR IC                        81343
         ICM   4,1,DS1NOEPV  LOAD NUMBER OF EXTENTS              81343
         CLI   DS1EXT1,X'40'   IS THERE A LABEL EXTENT ?         83156
         BNE   *+8           NO                                  83156
         LA    4,1(,4)       YES; ADJUST FOR IT                  83156
         LA    0,3           SET FOR EXTENTS ACCOUNTED FOR HERE  81343
         CR    4,0           MORE THAN BASIC EXTENTS ?           81343
         LA    4,2(,4)       ALLOW FOR FMT3 2-LINE OVERHEAD      81344
         BH    *+6           YES; ADD NOEPV+2                    81344
         LR    4,0           ELSE SET TO THREE LINES             81343
         AL    4,SIZEPAT     ADD BASIC PATTERN SIZE              81343
         TM    DS1DSORG,X'40'+X'02'  DATA PS OR PO ?             81343
         BZ    *+8           NEITHER                             81343
         LA    4,2(,4)       LEAVE ROOM FOR NNN% FULL MESSAGE    81343
         TM    DS1DSORG,X'80'   ISAM DATASET ?                   81343
         BZ    *+8           NO                                  81343
         LA    4,17(,4)      LEAVE ROOM FOR ISAM DATA            81343
         LA    2,2           SET FOR TWO LINES
         LA    3,DS1EXT3     GET ADDR. OF LAST EXTENT ENTRY
ZLP      OC    0(10,3),0(3)  IS THIS ENTRY ZERO ?
         BNZ   ZOUT          NO, STORE CURRENT COUNT
         SH    3,=H'10'      BUMP TO PRECEEDING EXTENT
         S     4,=XL4'10001'   DECREASE LINE AND SPACE COUNT BY 1
         BCT   2,ZLP         CHECK NEXT ENTRY
ZOUT     ST    4,SIZE        STORE NEW LINE COUNTS
         SPACE
*        CHECK FOR 'SYSCTLG' INITIALIZATION
*
         CLC   DSNAME(8),=CL8'SYSCTLG.'  CVOL CATALOG ?         GP09186
         BE    CHKCATIN      YES; CHECK                         GP09186
         CLC   DSNAME(8),=CL8'SYSCTLG'   IS IT CATALOG DATA SET ?
         BNE   PRNT          NO, SKIP
CHKCATIN CLI   DS1LSTAR+2,X'FF'   HAS IT BEEN INITIALIZED ?
         BE    PRNT          YES, OK
         MVC   DSNAME+15(21),=C'** NOT INITIALIZED **'   PUT MSG
PRNT     MVC   DSN2(44),DSNAME  COPY FOR OVERPRINTING
         CALL  PRINT,(DSCB1OUT,SIZE)
         L     13,D1SAVE+4
         RETURN (14,12),T
CHARBIT  MVI   CHAROUT,C' '  PREBLANK O/P
         MVC   CHAROUT+1(L'CHAROUT-1),CHAROUT
         LA    14,CHAROUT    GET START O/P
         LA    15,L'CHAROUT-3  GET LENGTH - FINAGLE
         SR    2,2           CLEAR IC REGISTER
CHARLP   CLI   1(1),X'FF'    END OF TABLE ?
         BER   9             YES - RETURN
         IC    2,1(1)        GET LENGTH - 1 OF EBCDIC ENTRY
         MVC   CHARN,CHAR    MOVE TEST CHARACTER
         NC    CHARN,0(1)    AND WITH ENTRY MASK
         CLC   CHARN,0(1)    MATCHES ?
         BNE   CHARINC       NO, SKIP
         SR    15,2          WILL TEXT FIT IN REMAINING LENGTH ?
         BMR   9             NO, RETURN                          80340
         SH    15,=H'3'      ADJUST TO NEW REMAINING LENGTH
         EX    2,CHARMV      MOVE THE TEXT
         MVI   CHARX,255     MAKE MASK
         XC    CHARX,0(1)    MAKE MASK TO REMOVE BITS USED
         NC    CHAR,CHARX    REMOVE BITS JUST DONE
         LA    14,1(2,14)    GET NEXT OUTPUT ENTRY
CHARINC  LA    1,3(1,2)      GET NEXT TABLE ENTRY
         B     CHARLP        KEEP GOING
CHARMV   MVC   0(0,14),2(1)  EX MOVE FOR TEXT
         SPACE 3
         USING SETXTYPE,15
SETXTYPE CLI   0(3),X'00'
         BNE   *+10
         MVC   0(20,4),=CL20'UNUSED'
         CLI   0(3),X'01'
         BNE   *+10
         MVC   0(20,4),=CL20'PRIME DATA'
         CLI   0(3),X'02'
         BNE   *+10
         MVC   0(20,4),=CL20'INDEPENDENT OVERFLOW'
         CLI   0(3),X'04'
         BNE   *+10
         MVC   0(20,4),=CL20'INDEX AREA'
         CLI   0(3),X'40'    USER LABEL TRACK ?
         BNE   *+10          NO, SKIP
         MVC   0(20,4),=CL20'USER LABELS'
         CLI   0(3),X'80'
         BNE   *+10
         MVC   0(20,4),=CL20'SPLIT CYLS'
         CLI   0(3),X'81'
         BNE   *+10
         MVC   0(20,4),=CL20'CYL BOUNDARIES'
         LA    3,10(3)
         LA    4,XTYPE2-XTYPE1(4)
         BCT   2,SETXTYPE
         BR    14
         DROP  15
COMPTAB  DC    S(DS1EXT1+2),S(TRKSZ1)
         DC    S(DS1EXT2+2),S(TRKSZ2)
         DC    S(DS1EXT3+2),S(TRKSZ3)
         DC    H'0'
CVHTAB   DC    S(DS1ADDR),H'5',S(DSCBADDR),H'12'
         DC    S(DS1PTRDS),H'5',S(PTRDS),H'12'
         DC    H'0'
BOOLIT   LA    4,8
SHIFTY   SRDL  2,1
         SRL   3,3
         BCT   4,SHIFTY
         ST    3,WORKFL5
         MVI   WORKFL5+4,X'0F'
         UNPK  WORK9,WORKFL5(5)
         BR    14
         SPACE 3
DOUBLE   DS    0D            FORCE BOUNDARY                      81340
CHAR     DC    X'0'          TEXT PROCESSING - TEST CHARACTER
CHARN    DC    X'0'          WORK BYTE FOR AND
CHARX    DC    X'0'          WORK BYTE FOR EXOR
CHAROUT  DC    CL25' '       OUTPUT TEXT HOLDER
         ORG   CHAR
WORKFL5  DS    F,X
WORK9    DS    CL9
         ORG
D1SAVE   DS    18F
         SPACE
VTORG    DC    X'8001',C'IS'   ISAM DSORG
         DC    X'4001',C'PS'   PS
         DC    X'2001',C'DA'    DA
         DC    X'0801',C'CQ'   CQ QTAM MESSAGE QUEUE        *TSM*80198
         DC    X'0201',C'PO'    PO
         DC    X'0100',C'U'    UNMOVABLE
         DC    X'FFFF'       END OF TABLE
         SPACE 1                                            *TSM*80198
VTORG2   DC    X'2001',C'TQ'   TQ TCAM MESSAGE QUEUE        *TSM*80198
         DC    X'0801',C'AM'   AM VSAM                      *TSM*80198
         DC    X'FFFF'         END OF TABLE                 *TSM*80198
         SPACE
VTREC    DC    X'E001',C'UT',X'C000',C'U'    UNDEFINED
         DC    X'A001',C'FT',X'8000',C'F'   FIXED
         DC    X'6001',C'VT',X'4000',C'V'    VARIABLE
         DC    X'2000',C'D'   USASCII VARIABLE / 'T' DONE ABOVE ?
         DC    X'1000',C'B',X'0800',C'S'  BLOCKED/ STANDARD-SPANNED
         DC    X'0400',C'A',X'0200',C'M'   CC CODE
         DC    X'FFFF'       END
         SPACE
VTOPT    DC    X'8000',C'W',X'4000',C'U',X'2000',C'C',X'1000',C'H'
         DC    X'0800',C'Q',X'0400',C'Z',X'0200',C'T'
         DC    X'FFFF'       END OF OPTCD
         SPACE
VTOPTDA  DC    X'8000',C'W',X'4000',C'T',X'2000',C'E',X'1000',C'F'
         DC    X'0800',C'A',X'0400',C'D',X'0200',C'X',X'0100',C'R'
         DC    X'FFFF'       END OF OPTCD
         SPACE
VTOPTIS  DC    X'8000',C'W',X'4000',C'U',X'2000',C'M',X'1000',C'I'
         DC    X'0800',C'Y',X'0200',C'L',X'0100',C'R'
         DC    X'FFFF'
         SPACE 1                                                 90253
VTOPTAM  DC    X'C006',C'ICF CAT'                                90253
         DC    X'4006',C'ICF CAT'                                90253
         DC    X'8006',C'ICF DSN'                                90253
         DC    X'FFFF'                                           90253
         SPACE
VTALO    DC    X'E002',C'CYL',X'A002',C'TRK'
         DC    X'6005',C'BLOCKS',X'2006',C'REL TRK'
         DC    X'0806',C',CONTIG',X'0404',C',MXIG'
         DC    X'0203',C',ALX',X'0105',C',ROUND'
         DC    X'FFFF'
         SPACE 1                                                 81340
CHANGE   DC    C'DATASET LAST ACCESSED ON '                      81340
ACCDY    DC    CL2'  ',C'.'   YEAR                               81340
ACCDM    DC    CL3'   ',C', ''DATASET CHANGED'' FLAG IS O'       81340
CHFG     DC    C'FF'                                             81340
CHANGEL  EQU   *-CHANGE      LENGTH OF INSERTION                 81340
         SPACE 1                                                 81340
DSCB1OUT DC    9CL132' '
         ORG   DSCB1OUT+0*132
         DC    C'-'
DSCBADDR DS    CL12          CCCC.HHHH.RR
         DC    C'  FMT='
FMT      DC    C' '
         DC    C'  DSNAME='
DSNAME   DC    CL44' '
         DC    C'  VOL=SER='
VOLSER   DC    CL6' '
         DC    C'  VOLSEQ='
VOLSEQ   DC    CL3' '
         DC    C'  DSCB EXTENDED AT '
PTRDS    DC    CL12' '       CCCC.HHHH.RR
         ORG   DSCB1OUT+1*132
         DC    C'+'          OVERPRINT FOR DSNAME
         ORG   DSNAME+132
DSN2     DC    CL44' '       OVERPRINT THE DSNAME
         ORG   DSCB1OUT+2*132
         DC    C'      CREATION DATE='
CREATDAT DC    CL6' '
         DC    C'  EXPIRATION DATE='
EXPIRDAT DC    CL6' '
         DC    C'  NUMBER OF EXTENTS='
XTENTS   DC    CL3' '
         DC    C'  NUMBER OF BYTES USED IN LAST PDS DIRECTORY BLOCK='
BYTESUSD DC    CL3' '
         ORG   DSCB1OUT+3*132
         DC    C'      SYSTEM CODE=C'''
SYSCODE  DC    CL13' '
         DC    C'''  DSORG='
DSORG    DC    CL3' '
         DC    C'  RECFM='
RECFM    DC    CL3' '
         DC    C'  OPTCD='
OPTCODE  DC    C'   '
         DC    C' (B'''
OPTCD    DC    CL8' '
         DC    C''')  BLOCKSIZE='
BLKSIZE  DC    CL5' '
         DC    C'  LRECL='
LRECL    DC    CL5' '
         DC    C'  KEYL='
KEYL     DC    CL3' '
         DC    C'  RKP='
RKP      DC    CL5' '
         ORG   DSCB1OUT+4*132
         DC    C'      DSIND=B'''
DSIND    DC    CL8' '
         DC    CL5''' '                                          82053
         DC    C'  SECONDARY ALLOCATION ='                       81340
SECALLOC DC    CL5' ',C' '                                       81340
DSALLOC  DC    CL21' '                                           81340
         DC    C'  (SCALO=B'''                                   81340
SCALO    DC    CL8' '                                            81340
         DC    C''')'                                            81340
         ORG   DSCB1OUT+5*132
         DC    C'      LAST BLOCK=(RELTRK='
LSTRK    DC    CL5' '
         DC    C',REC='
LSTREC   DC    CL3' '
         DC    C')  LAST TRK HAS '
LSTSIZE  DC    CL5' '
         DC    C' BYTES'
         DC    CL5' '                                            81340
INCHANGE DC    CL(CHANGEL)' '  ACCESS DATE/CHANGE FLAG INSERTION 81340
.LOOP    ANOP
         ORG   DSCB1OUT+(5+&N)*132
         DC    C'      EXTENT 0&N='
XTYPE&N  DC    CL20' '
         DC    C'  SEQ NUM='
SEQNUM&N DC    CL3' '
         DC    C'  LOWER BOUND=(CYL='
LOWCYL&N DC    CL3' '
         DC    C',TRK='
LOWTRK&N DC    CL3' '
         DC    C')  UPPER BOUND=(CYL='
UPCYL&N  DC    CL3' '
         DC    C',TRK='
UPTRK&N  DC    CL3' '
         DC    C')'
         DC    C'  SIZE='
TRKSZ&N  DC    CL4' '
         DC    C' TRACKS'
&N       SETA  &N+1
         AIF   (&N LT 4).LOOP
         ORG
         DS    0F            ALIGNMENT
SIZE     DC    H'9,10'       WITH OVERPRINT
SIZEPAT  DC    Y(9,10-3)     PATTERN FOR SIZE FIELD              81344
         SPACE 1                                                 81340
         LTORG
         SPACE 1                                                 81340
CVDTABC  DC    S(DS1REFD),H'1',S(ACCDY-CHANGE+INCHANGE),H'2'     81340
         DC    S(DS1REFD+1),H'2',S(ACCDM-CHANGE+INCHANGE),H'3'   81340
         SPACE 1                                                 81340
CVDTAB   DC    S(DS1VOLSQ),H'2',S(VOLSEQ),H'3'
         DC    S(DS1CREDT),H'1',S(CREATDAT),H'2'
         DC    S(DS1CREDT+1),H'2',S(CREATDAT+3),H'3'
         DC    S(DS1EXPDT),H'1',S(EXPIRDAT),H'2'
         DC    S(DS1EXPDT+1),H'2',S(EXPIRDAT+3),H'3'
         DC    S(DS1NOEPV),H'1',S(XTENTS),H'3'
         DC    S(DS1NOBDB),H'1',S(BYTESUSD),H'3'
         DC    S(DS1BLKL),H'2',S(BLKSIZE),H'5'
         DC    S(DS1LRECL),H'2',S(LRECL),H'5'
         DC    S(DS1KEYL),H'1',S(KEYL),H'3'
         DC    S(DS1RKP),H'2',S(RKP),H'5'
         DC    S(DS1SCALO+1),H'3',S(SECALLOC),H'5'
         DC    S(DS1LSTAR),H'2',S(LSTRK),H'5'
         DC    S(DS1LSTAR+2),H'1',S(LSTREC),H'3'
         DC    S(DS1TRBAL),H'2',S(LSTSIZE),H'5'
         DC    S(DS1EXT1+1),H'1',S(SEQNUM1),H'3'
         DC    S(DS1EXT2+1),H'1',S(SEQNUM2),H'3'
         DC    S(DS1EXT3+1),H'1',S(SEQNUM3),H'3'
         DC    S(DS1EXT1+2),H'2',S(LOWCYL1),H'3'
         DC    S(DS1EXT2+2),H'2',S(LOWCYL2),H'3'
         DC    S(DS1EXT3+2),H'2',S(LOWCYL3),H'3'
         DC    S(DS1EXT1+4),H'2',S(LOWTRK1),H'3'
         DC    S(DS1EXT2+4),H'2',S(LOWTRK2),H'3'
         DC    S(DS1EXT3+4),H'2',S(LOWTRK3),H'3'
         DC    S(DS1EXT1+6),H'2',S(UPCYL1),H'3'
         DC    S(DS1EXT2+6),H'2',S(UPCYL2),H'3'
         DC    S(DS1EXT3+6),H'2',S(UPCYL3),H'3'
         DC    S(DS1EXT1+8),H'2',S(UPTRK1),H'3'
         DC    S(DS1EXT2+8),H'2',S(UPTRK2),H'3'
         DC    S(DS1EXT3+8),H'2',S(UPTRK3),H'3'
         DC    H'0'
FMT1     DSECT
DS1DSNAM DS    CL44
DS1FMTID DS    CL1
DS1DSSN  DS    CL6
DS1VOLSQ DS    CL2
DS1CREDT DS    CL3
DS1EXPDT DS    CL3
DS1NOEPV DS    CL1
DS1NOBDB DS    CL1
         DS    CL1
DS1SYSCD DS    CL13
DS1REFD  DS    XL3           MVS (SVS - LOCAL MOD) ACCESS DATE   81340
         DS    CL4                                               81340
DS1DSORG DS    CL2
DS1RECFM DS    CL1
DS1OPTCD DS    CL1
DS1BLKL  DS    CL2
DS1LRECL DS    CL2
DS1KEYL  DS    CL1
DS1RKP   DS    CL2
DS1DSIND DS    CL1
DS1DSCHA EQU   X'02'         DATA MODIFIED                       81340
DS1SCALO DS    CL4
DS1LSTAR DS    CL3
DS1TRBAL DS    CL2
         DS    CL2
DS1EXT1  DS    CL10
DS1EXT2  DS    CL10
DS1EXT3  DS    CL10
DS1PTRDS DS    CL5
DS1ADDR  DSECT
         END
