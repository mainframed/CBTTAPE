SUBXAOUT TITLE 'S U B X A O U T  ***  ALLOCATE SYSOUT'
         MACRO  ,
&NM      MVD   &FLD,&EF      DEFINE FIELD FOR MVC
         LCLC  &HB,&L
&L       SETC  'L'''
         AIF   ('&EF' EQ '').NOTEND
&HB      SETC  '128+'
.NOTEND  ANOP  ,
&NM      DC    AL1(&HB&L&FLD-1),AL2(&FLD-TEXTBASE)
         MEND  ,
         SPACE 1
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
***********************************************************************
*                                                                     *
*   QUICK AND DIRTY SYSOUT ALLOCATION (CLASS,FORM,COPIES,DEST,USER)   *
*                                                                     *
***********************************************************************
         PRINT &PRTSOR
SUBXAOUT PGMHEAD ZERO,BASE=(R12),AM=31,RM=24,BNDRY=PAGE,PARM=R1
*
*  OUT (PRNT)    OUTCLS OUTFRM OUTCOP OUTDEST
*
         SR    R9,R9         CLEAR REASON CODE
         SR    R10,R10       CLEAR MESSAGE ADDRESS
         L     R2,0(,R1)     LOAD FIRST ARGUMENT
         CLC   =C'ALL',0(R2)   ALLOCATE RATHER THAN FREE?
         BE    ALLOUT
         LA    R3,FREMVC     POINT TO DDNAM MOVER
         BAL   R14,MOVEDATA  MOVE DDNAME
         B     PGMTERM       UNALLOCATE
         SPACE 1
*---------------------------------------------------------------------*
*   BUILD ALLOCATION PARAMETER LIST AND ALLOCATE OUTPUT               *
*                                                                     *
*   NOTE THAT AN EXPLICIT CALL TO PERFORM UNALLOCATION IS REQUIRED    *
*   IF THE ISSUING JOB HAS A HELD JOBCLASS (HOLD PROPAGATES!)         *
*---------------------------------------------------------------------*
ALLOUT   LA    R3,OUTMVC     POINT TO INPUT MOVER
         BAL   R14,MOVEDATA  MOVE INPUT FIELDS
         LA    R4,OUTTU2     POINT TO OUTPUT ALLOCATION LIST
         LA    R3,(OUTTU2R-OUTTU2)/2  # OPTIONAL PARAMETERS
         BAL   R14,BUILDTUS  BUILD TEXT UNIT LIST
         SPACE 1
         LA    R10,MSGALLOU  OUTPUT ALLOCATION FAILED
         LA    R1,MYRBPTR
         DYNALLOC
         ST    R15,RETCODE   SET RETURN CODE
         MVC   UNDD2,CVDDN   COPY NEW DDN, EVEN IF NULL
         L     R9,S99RSC-S99RB+MYRB  COPY REASON CODE
         LTR   R10,R15
         BNZ   RETERROR
         MVC   RSNCODE(8),CVDDN   RETURN DD NAME IN R0-R1
         B     PGMEXIT       SKIP AROUND UNALLOC
         SPACE 1
*---------------------------------------------------------------------*
*   NORMAL TERMINATION, CLEANUP, AND RETURN                           *
*---------------------------------------------------------------------*
         SPACE 1
PGMTERM  CLI   UNDD2,C' '    WAS INPUT ALLOCATED ?
         BNH   PGMEXIT       NO
         LA    R4,UNTU2      POINT TO CHAIN
         SR    R3,R3         NO OPTIONALS
         BAS   R14,BUILDTUS  BUILD TEXT UNIT CHAIN
         MVI   S99VERB-S99RB+MYRB,S99VRBUN  UNALLOCATE
         LA    R1,MYRBPTR    POINT TO REQUEST POINTER
         DYNALLOC ,          AND FREE; IGNORE RETURN
PGMEXIT  PGMEXIT COPYRET=(RETCODE,12)  RETURN R15 THROUGH R1
         SPACE 1
*---------------------------------------------------------------------*
*   ERROR RETURN AND CLEANUP                                          *
*---------------------------------------------------------------------*
RETERROR STM   R8,R10,RETCODE SET ERROR LEVEL AND MESSAGE ADDRESS
         B     PGMTERM       AND RETURN
         EJECT ,
*---------------------------------------------------------------------*
*   MOVE INPUT DATA TO TEXT UNITS - MVD CHAIN IN R3; PARM IN R1       *
*---------------------------------------------------------------------*
MOVEDATA STM   R14,R12,12(R13)    SAVE FOR DEBUGGING (R14-R1 RETURNED)
         SR    R5,R5         CLEAR FOR IC
MOVEDATL LTR   R1,R1         LAST CALL PARAMETER PROCESSED ?
         BM    MOVEDATX      YES; RETURN
         LA    R1,4(,R1)     ADVANCE TO NEXT ONE
         IC    R5,0(,R3)     GET LENGTH AND FLAG BYTE
         SR    R6,R6
         ICM   R6,3,1(R3)    LOAD DESTINATION DISPLACEMENT
         LA    R6,TEXTBASE(R6)  MAKE ADDRESS
         N     R5,=X'0000007F'  REMOVE END OF LIST BIT
         L     R7,0(,R1)     LOAD USER'S PARAMETER ADDRESS
         LA    R7,0(,R7)     CLEAN FOR TEST
         LTR   R7,R7         ANY ?
         BZ    MOVEDATS      NO; SKIP (OR RETURN ERROR?)
         EX    R5,EXINPMVC   MOVE DATA TO TEXT
MOVEDATS TM    0(R3),X'80'   LAST LIST ENTRY ?
         BNZ   MOVEDATX      YES; RETURN
         LA    R3,3(,R3)     NEXT LIST
         B     MOVEDATL      AND TEST PARM END
MOVEDATX LM    R2,R12,28(R13)  R1 UPDATED
         BR    R14
EXINPMVC MVC   0(0,R6),0(R7)  MOVE DATA TO TEXT
         EJECT
*---------------------------------------------------------------------*
*   BUILD TEXT UNIT POINTERS                                          *
*---------------------------------------------------------------------*
BUILDTUS XC    TUWORK,TUWORK  CLEAR RB AND TEXT UNIT WORK AREA
         LA    R2,MYRB       POINT TO REQUEST BLOCK
         ST    R2,MYRBPTR+S99RBPTR-S99RBP    BUILD POINTER
         OI    MYRBPTR,S99RBPND    END BIT
         USING S99RB,R2      DECLARE BRIEFLY
         MVI   S99RBLN,S99RBEND-S99RB  BLOCK LENGTH
         MVI   S99VERB,S99VRBAL    ALLOCATE
         LA    R1,NEWTUS     TU POINTER START
         ST    R1,S99TXTPP   SET INTO RB
         LA    R1,NEWTUS-4   POINT TO NEW OUTPUT LESS ONE
         DROP  R2
BUILDTUL LH    R2,0(,R4)     GET OFFSET
         N     R2,=X'00007FFF'  KILL END OF LIST BIT
         LA    R2,TEXTBASE(R2)  ADDRESS OF TEXT UNIT
         LTR   R3,R3         NEED TO TEST TEXT PARM ?
         BNP   BUILDTUU      NO; UNCONDITIONAL
         CLI   6(R2),C' '    PARAMETER PRESENT ?
         BNH   BUILDTUB      NO; BUMP TO NEXT ONE
BUILDTUU ST    R2,4(,R1)     FINAGLE
         LA    R1,4(,R1)     ADVANCE
BUILDTUB BCTR  R3,0          DECREMENT TEST COUNTER
         TM    0(R4),X'80'   LAST ITEM ?
         LA    R4,2(,R4)     BUMP ANYWAY
         BZ    BUILDTUL      LOOP
         OI    0(R1),X'80'   SET LAST TU
         BR    R14
         EJECT
*---------------------------------------------------------------------*
*   ERROR MESSAGES                                                    *
*---------------------------------------------------------------------*
MSGALLOU BCON  'OUTPUT ALLOCATION FAILED'
         SPACE 1
OUTTU2   DC    AL2(OUTOUT-TEXTBASE)             SYSOUT CLASS
         DC    AL2(OUTFORM-TEXTBASE)            FORM
         DC    AL2(OUTDEST-TEXTBASE)            DESTINATION
*DEFER*  DC    AL2(OUTUSER-TEXTBASE)            USER
OUTTU2R  DC    AL2(OUTCOP-TEXTBASE)             COPIES (1-255)
         DC    AL2(FREECLOS-TEXTBASE)           FREE=CLOSE
         AIF   (NOT &MVSSP).NOSPIN
         DC    AL2(OUTSPIN-TEXTBASE)            IMMEDIATE SPINOFF
.NOSPIN  DC    AL2(OUDDNAME+X'8000'-TEXTBASE)   DD NAME
         SPACE 1
***********************************************************************
*   MOVE LISTS, IN PARM SEQUENCE                                      *
***********************************************************************
OUTMVC   MVD   OVCLS         SYSOUT CLASS
         MVD   OVFRM                FORM
         MVD   OVCOP                COPIES (1-255)
         MVD   OVDEST               DESTINATION
         MVD   OVUSER,*END          USER ID
FREMVC   MVD   UNDDN2,*END          DDN TO BE FREED
         SPACE 1
*---------------------------------------------------------------------*
*   UNALLOCATION LISTS                                                *
*---------------------------------------------------------------------*
UNTU2    DC    AL2(UNDDN2-TEXTBASE)
         AIF   (NOT &MVSSP).NUSPIN
         DC    AL2(UNSPIN-TEXTBASE)
.NUSPIN  DC    AL2(UNNHOLD-TEXTBASE)
         DC    AL2(UNALL+X'8000'-TEXTBASE)
         SPACE 2
***********************************************************************
*   SYSOUT TEXT UNITS                                                 *
***********************************************************************
TEXTBASE DS    0H            START OF TEXT UNITS
FREECLOS DC    Y(DALCLOSE,0)      FREE ON CLOSE
OUDDNAME DC    Y(DALRTDDN,1,8)  RETURN DD NAME
CVDDN    DC    CL8' '
OUTOUT   DC    Y(DALSYSOU,1,1)
OVCLS    DC    CL1' '
OUTFORM  DC    Y(DALSFMNO,1,4)
OVFRM    DC    CL4' '
OUTCOP   DC    Y(DALCOPYS,1,1)
OVCOP    DC    AL1(1)        COPY COUNT
OUTDEST  DC    Y(DALSUSER,1,8)
OVDEST   DC    CL8' '
*UTUSER  DC    Y(DALUSRID,1,8)
OVUSER   DC    CL8' '
OUTSPIN  DC    0H'0',AL2(X'8013',1,1),X'80'  IMMEDIATE SPIN - DALSPIN
         SPACE 1
***********************************************************************
*   UNALLOCATION                                                      *
***********************************************************************
UNDDN2   DC    Y(DUNDDNAM,1,8)
UNDD2    DC    CL8' '
UNALL    DC    Y(DUNUNALC,0)
UNNHOLD  DC    Y(DUNOVSNH,0)
UNSPIN   DC    0H'0',AL2(X'8013',1,1),X'80'  IMMEDIATE SPIN - DUNSPIN
         EJECT
*---------------------------------------------------------------------*
*   DYNAMIC STORAGE                                                   *
*---------------------------------------------------------------------*
SAVE     DSECT
         SERVDEFS ,          COMMON GOODIES
         SPACE 1
MYRBPTR  DC    A(0)          RB POINTER
MYRB     DC    5A(0)         REUEST BLOCK
NEWTUS   DC    20A(0)        TEXT UNIT POINTERS
TUWORK   EQU   MYRBPTR,*-MYRBPTR,C'A'  WORK AREA FOR CLEARING
         SPACE 1
SAVEEND  EQU   *
         SPACE 1
         DCBD  DEVD=DA,DSORG=PS
         SPACE 1
 PRINT ON,GEN,DATA   *****DEBUG*****
         IEFZB4D0 ,          DEFINE REQUEST BLOCK
         IEFZB4D2 ,          DEFINE TEXT UNIT VALUES
         END   ,
