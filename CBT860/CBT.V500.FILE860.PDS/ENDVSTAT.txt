ENDVSTAT TITLE 'E N D V S T A T  ***  ENDEVOR GLOBAL ELEMENT INVENTORY'
***********************************************************************
*                                                                     *
*   THIS MODULE READS THE SYSPRINT DATASETS PRODUCED BY ENDEVOR       *
*   REPORT RUN #3, ELEMENT CATALOG, FOR BOTH PRODUCTION AND TEST      *
*   ENVIRONMENTS, AND COMBINES THE OUTPUT BY ELEMENT.                 *
*                                                                     *
*   NOTE THAT TYPE EXTJCL IS TREATED AS JCL                           *
*     AND COPYBOOK, ETC. SHOW UP AS MISCELLANEOUS                     *
*                                                                     *
***********************************************************************
         MACRO ,
&NM      ENDREC03 &DSECT=,&PFX=E03,&RECNAME=E03REC,&LIST=NO
.*    MAPPING OF ENDEVOR REPORT OUTPUT FOR ELEMENT CATALOG CONRPT03
.*
         LCLC  &N,&SECT,&P
         PUSH  PRINT
         PRINT &LIST.GEN
&N       SETC  '&NM'
&P       SETC  '&PFX'
         AIF   ('&N' NE '').HAVENM
&N       SETC  '&P'.'DSECT'
.HAVENM  AIF   ('&DSECT' EQ 'NO').NOSECT
&SECT    SETC  '&SYSECT'
&N       DSECT ,
         AGO   .NONAME
.NOSECT  AIF   ('&N' EQ '').NONAME
&N       DS    0C
.NONAME  AIF   ('&RECNAME' EQ '').NORECNM
&RECNAME DS    0CL133        PRINT RECORD
.NORECNM ANOP  ,
         DC    C' '            CARRIAGE CONTROL
&P.MEM   DC    CL8' ',CL4' '   ELEMENT NAME
&P.ENV   DC    CL8' ',CL2' '   ENVIRONMENT (DEVS/DEVL/PROD)
&P.SYS   DC    CL8' ',CL2' '   SYSTEM (EXT)
&P.SUB   DC    CL8' ',CL1' '   SUBSYSTEM (EXT)
&P.TYPE  DC    CL8' ',CL1' '   TYPE OF ELEMENT
&P.STID  DC    CL1' ',CL2' '   STAGE (U/Q, OR F/P)
&P.STSQ  DC    CL3' ',CL1' '   STAGE SEQ. IN ENVIRONMENT
&P.VER   DC    CL5' ',CL3' '   VERSION.LEVEL
&P.PROC  DC    CL8' ',CL2' '   PROCESSOR GROUP
&P.BDTE  DC    CL7' ',CL1' '   BASE DATE
&P.BUID  DC    CL8' ',CL2' '   BASE USER
&P.CDTE  DC    CL7' ',CL1' '   CURRENT DATE
&P.CUID  DC    CL8' ',CL1' '   CURRENT USER
&P.GDTE  DC    CL7' ',CL1' '   GEN DATE
&P.GUID  DC    CL8' ',CL1' '   GEN USER
         AIF   ('&RECNAME' EQ '').NORECN2
&P.LGNTH EQU   *-&RECNAME   LENGTH OF RECORD
.NORECN2 AIF   ('&SECT' EQ '').NOREST
&SECT    CSECT ,
.NOREST  POP   PRINT
         MEND  ,
         SPACE 2
         PUNCH ' ORDER ENDVSTAT(P) '
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
         PRINT NOGEN
ENDVSTAT PGMHEAD ZERO,BASE=(R11,R12),PARM=R9   BASE, SAVE AREA, ETC.
         LA    R0,OUTPUTND-OUTPUT  LENGTH OF OUTPUT LINE
         STH   R0,OUTPUT     INITIALIZE
         MVI   OUTLINE,C' '
         MVC   OUTLINE+1(L'OUTLINE-1),OUTLINE
         SERVINIT ,          INITIALIZE SERVICES
         SERVLOAD @PRINTER   LOAD PRINT ROUTINE
         LA    R3,=CL8'REPORT'                                  GP03109
         PRTOPEN PR1WORK
         BXLE  R15,R15,HAVEOUT  SKIP ERROR IF OK
         LA    R2,PR1WORK                                       GP03109
BADOUWTO #OPMSG BADOUMSG,LIST=((R3),8,(R2),8)                   GP03109
         ABEND 1403
BADOUMSG VCON  'UNABLE TO OPEN | FILE |'                        GP03109
         SPACE 1
HAVEOUT  PRTV  HEADPAGE,TITLE=1
         PRTV  PHD1,TITLE=2
         PRTV  PHD2,TITLE=3
         PRTV  PHD3,TITLE=4
         LA    R3,=CL8'INPUT'                                   GP03109
         LA    R2,DCBDDNAM-IHADCB+PRODIN                        GP03109
         DEVTYPE DCBDDNAM-IHADCB+PRODIN,DB  IS THERE A PRODIN DD ?
         BXH   R15,R15,BADOUWTO  NO; WRITE MESSAGE AND QUIT
         LA    R2,DCBDDNAM-IHADCB+TESTIN                        GP03109
         DEVTYPE DCBDDNAM-IHADCB+TESTIN,DB  IS THERE A TESTIN DD ?
         BXH   R15,R15,BADOUWTO  NO; WRITE MESSAGE AND QUIT
         OPEN  MF=(E,DCBLIST)  OPEN BOTH
         CALL  SUBDTE,(HEADDATE,HEADTIME),VL
         SPACE 1
GETSOME  TM    PROFG,FGTEOF+FGTREC  NEED A NEW TEST RECORD?
         BNZ   PRODNEXT      NO
         BAS   R9,GETTEST    GET NEXT CARD FROM TEST IN
         USING TESTSECT,R8   DECLARE TEST INPUT RECORD
PRODNEXT TM    PROFG,FGPEOF+FGPREC  NEED A NEW PRODUCTION RECORD?
         BNZ   MAINLOOP      NO
         BAS   R9,GETPROD    GET NEXT CARD FROM PROD IN
         USING PRODSECT,R10  DECLARE PRODUCTION INPUT
MAINLOOP TM    PROFG,FGPEOF+FGTEOF  BOTH END-FILES?
         BO    FLUSHOLD      YES; FLUSH OLD
         CLC   TESTMEM,PRODMEM  UNEQUAL - WHICH TO USE?
         BNH   PICKTEST      TEST IS LOWER; USE IT
PICKPROD LR    R7,R10        USE PRODUCTION MEMBER
         NI    PROFG,255-FGPREC  SHOW ANOTHER NEEDED
         B     TEST4BRK
PICKTEST LR    R7,R8         USE TEST RECORD
         NI    PROFG,255-FGTREC  SHOW ANOTHER NEEDED
         USING INSECT,R7
TEST4BRK CLC   INMEM,OUTMEM  MEMBER BREAK?
         BE    NOTBREAK      SAME; CONTINUE
FLUSHOLD CLI   OUTMEM,C' '   DID WE EVER STACK ANYTHING ?
         BE    FLUSHED       NO; RESET FOR NEXT TIME
         PRTV  OUTPUT        PROCESS OUTPUT LINE
         CLC   INMEM(7),OUTMEM  DIFFERENT, BUT RELATED?
         BE    FLUSHED       YES; PRINT
         PRTSPACE 1          ELSE DOUBLE-SPACE GROUP
FLUSHED  TM    PROFG,FGPEOF+FGTEOF  BOTH END-FILES?
         BO    EXITALL       YES; CLOSE EVERYTHING
         MVC   OUTLINE,OUTLINE-1  CLEAR OUTPUT LINE
         XC    FLAGOUT1,FLAGOUT1  CLEAR FLAGS
         MVC   OUTMEM,INMEM  COPY THE MEMBER NAME
         SPACE 1
NOTBREAK BAS   R9,GETTYPE    GET TYPE DEPENDENT INFORMATION
*  R0 - STAGE ID (1)   R1 - OFFSET TO TYPE   R2 - IMPORTANCE
         STC   R0,OUTJCL(R1)     PUT STAGE INTO OUTPUT BY TYPE
         LA    R14,X'0F'     MAKE MASK
         NR    R14,R2        GET LEVEL MASK
         CLM   R14,1,TYPEASM+8+2  IS IT A COMPILER?
         BNE   NOCOMP        NO
         MVC   OUTPRC,INPROC  SHOW LANGUAGE/PROCESSING GROUP
         CLI   INPROC+5,C'O'  IS IT AN OBJECT DECK PRODUCER?
         BNE   NOCOMP        NO
         LA    R14,C'-'      SIGNAL OBJECT ONLY - LINK REQUIRED
         STC   R14,OUTJCL+(OUTLINK-OUTASM)(R1)  SHOW LINK NEEDED
NOCOMP   CLM   R2,1,FLAGOUT1  IS IT MORE IMPORTANT?
         BNH   GETSOME       NO; GET NEXT RECORD
         STC   R2,FLAGOUT1   UPDATE LEVEL
         MVC   OUTJVER,INVER COPY VERSION AND LEVEL
         MVC   OUTUID,INGUID  LAST GEN USER ID
         MVC   OUTDATE,INGDTE  AND DATE
         B     GETSOME       GO FOR MORE
         SPACE 1
GETTYPE  LM    R15,R1,TYPEBXLE  GET TYPE TABLE POINTERS
GETTYPEL CLC   INTYPE,0(R15)  MATCH?
         BE    GETTYPEM      YES, USE IT
         BXLE  R15,R0,GETTYPEL  ELSE TRY AGAIN
GETTYPEM SLR   R0,R0
         IC    R0,INSTID     GET STAGE ID
         SLR   R2,R2
         IC    R2,10(,R15)    GET IMPORTANCE
         CR    R7,R10        PRODUCTION RECORD?
         BNE   *+8           NO
         LA    R2,16(,R2)    BOOST OVER TEST
         LH    R1,8(,R15)    GET OFFSET IN OUTPUT
         LA    R14,=C'PFQU'  SET FOR COMMON STAGES
         LA    R15,4         SET MATCHING COUNTER
GETTYPEO CLM   R0,1,0(R14)   MATCH?
         BE    GETTYPES      YES; STAGE MATCHED
         LA    R14,1(,R14)
         BCT   R15,GETTYPEO  TRY NEXT
         LA    R15,3         SET FOR 'FIX'
GETTYPES LA    R1,4(,R1)     GET OUTPUT OFFSET + 4
         SR    R1,R15        OFFSET WITHIN FIELD
         BR    R9            RETURN TO CALLER
         SPACE 1
EXITALL  CLOSE MF=(E,DCBLIST)
         SERVTERM ,          CLOSE AND FREE THINGS
         L     R9,RETCODE
         PGMEXIT RC=(R9)
         SPACE 1
GETTEST  GET   TESTIN        READ A RECORD FROM THE TEST FILE
         LR    R8,R1         COPY TO DEFINED REGISTER
         CLI   TESTVER+2,C'.'  IS THIS A VERSION RECORD?
         BNE   GETTEST       NO; SKIP TITLES AND JUNK
         CLI   TESTVER+1,C'V'  IS THIS A SUBTITLE?
         BE    GETTEST       YES; SKIP IT
         OI    PROFG,FGTREC  SIGNAL RECORD AVAILABLE
         BR    R9            RETURN TO CALLER
TESTEOD  LA    R8,=8X'FF'    POINT TO DUMMY RECORD
         OI    PROFG,FGTEOF  SIGNAL END-FILE
         BR    R9            AND RETURN
         SPACE 1
GETPROD  GET   PRODIN        READ A RECORD FROM THE PRODUCTION FILE
         LR    R10,R1        COPY TO DEFINED REGISTER
         CLI   PRODVER+2,C'.'  IS THIS A VERSION RECORD?
         BNE   GETPROD       NO; SKIP TITLES AND JUNK
         CLI   PRODVER+1,C'V'  IS THIS A SUBTITLE?
         BE    GETPROD       YES; SKIP IT
         OI    PROFG,FGPREC  SIGNAL RECORD AVAILABLE
         BR    R9            RETURN TO CALLER
PRODEOD  LA    R10,=8X'FF'   POINT TO DUMMY RECORD
         OI    PROFG,FGPEOF  SIGNAL END-FILE
         BR    R9            AND RETURN
         SPACE 2
         LTORG ,
         SPACE 1
DB       DC    D'0'          PL8
DCBLIST  OPEN  (PRODIN,(INPUT,REREAD),TESTIN,(INPUT,REREAD)),MF=L
TYPEBXLE DC    A(TYPETAB,TYPETAB2-TYPETAB,TYPETABN)
HEXTAB   DC    C'0123456789ABCDEF'     1/2
HEXTRTAB EQU   HEXTAB-C'0'             2/2
         SPACE 1
PRODIN   DCB   DDNAME=PRODIN,DSORG=PS,MACRF=GL,EODAD=PRODEOD
         SPACE 1
TESTIN   DCB   DDNAME=TESTIN,DSORG=PS,MACRF=GL,EODAD=TESTEOD
         SPACE 1
PR1WORK  PRTWORK SYSPRINT,TITLE=5
         SPACE 1
PHD1     VCON  '1',END=PHD1X
         DC    CL30' '
         DC    CL58'COMBINED ENDEVOR ELEMENT STATUS'
         DC    CL6' '
HEADDATE DC    CL10' ',X'4081A340'         DATE
HEADTIME DC    CL8' ',C'  P',X'818785'    TIME
HEADPAGE DC    CL4' '                     PAGE
         VCON  *END
         SPACE 1
PHD2     VCON  '-',END=PHD2X
         DC    CL10'ELEMENT'  ELEMENT NAME
         DC    CL6'USER'     LAST GEN USER
         DC    CL9'GENNED'   LAST GET DATE
         DC    CL7'VV.LL'    VERSION OF JCL MEMBER
         DC    CL9'JCL'      ARRAY BY TYPE: JCL/EXTJCL
         DC    CL9'ASM'                     ASM
         DC    CL9'LINK'                    LINKEDIT
         DC    CL9'MACRO'                   MACRO
         DC    CL9'MEMO'                    MEMO
         DC    CL9'DOC'                     DOCUMENTATION
         DC    CL9'COH'                     CMPTR OP HNDBOOK
         DC    CL9'MISC'          ANYTHING ELSE ??
         DC    CL9'PROCESSOR'               LANGUAGE HANDLER
         VCON  *END
         SPACE 1
PHD3     VCON  '0'
         SPACE 1
TYPETAB  DC    CL8'JCL     ',AL2(OUTJCL-OUTJCL),X'0F'
TYPETAB2 DC    CL8'LINKDECK',AL2(OUTLINK-OUTJCL),X'0E'
TYPEASM  DC    CL8'ASM     ',AL2(OUTASM-OUTJCL),X'0D'
         DC    CL8'MACRO   ',AL2(OUTMAC-OUTJCL),X'0C'
         DC    CL8'MEM     ',AL2(OUTMEMO-OUTJCL),X'0B'
         DC    CL8'QAMEM   ',AL2(OUTMEMO-OUTJCL),X'0B'
         DC    CL8'DOC     ',AL2(OUTDOC-OUTJCL),X'0A'
         DC    CL8'COH     ',AL2(OUTCOH-OUTJCL),X'09'
         DC    CL8'C       ',AL2(OUTASM-OUTJCL),X'0D'
         DC    CL8'H       ',AL2(OUTMAC-OUTJCL),X'0C'
         DC    CL8'COBOL2  ',AL2(OUTASM-OUTJCL),X'0D'
*OBSOL?  DC    CL8'COBOL   ',AL2(OUTASM-OUTJCL),X'0D'
         DC    CL8'COPYBOOK',AL2(OUTMAC-OUTJCL),X'0C'
         DC    CL8'SAS     ',AL2(OUTASM-OUTJCL),X'0D'
         DC    CL8'EXTJCL  ',AL2(OUTJCL-OUTJCL),X'0F'
TYPETABN DC    CL8'JCLSAT  ',AL2(OUTJCL-OUTJCL),X'0F'
         DC    CL8'*MISC*  ',AL2(OUTMISC-OUTJCL),X'00'   ALL ELSE
         SPACE 1
INSECT   ENDREC03 DSECT=,LIST=,PFX=IN,RECNAME=INREC  MAP INPUT RECORD
         SPACE 1
PRODSECT ENDREC03 DSECT=,PFX=PROD,RECNAME=PRODREC  PROD IN
TESTSECT ENDREC03 DSECT=,PFX=TEST,RECNAME=TESTREC  TEST IN
         SPACE 1
PATCHER  DC    30S(*)        MINI-PATCH
         SPACE 1
SAVE     DSECT ,             SAVE/WORK AREA
@SERVICE DS    A
@PRINTER DS    A
RETCODE  DS    F
CALLPARM DS    10A           SUBWTO CALLING PARAMETER LIST      GP03109
OUTPUT   VCON  ' ',END=OUTPUTND
OUTLINE  DC    CL132' '      OUTPUT LINE
         ORG   OUTLINE
OUTMEM   DS    CL8,CL2       ELEMENT NAME
OUTUID   DS    CL5,C         LAST GEN USER
OUTDATE  DS    CL7,CL2       LAST GET DATE
OUTJVER  DS    CL5,CL2       VERSION OF JCL MEMBER
OUTJCL   DS    CL4,CL5       ARRAY BY TYPE: JCL/EXTJCL
OUTASM   DS    CL4,CL5                      ASM
OUTLINK  DS    CL4,CL5                      LINKEDIT
OUTMAC   DS    CL4,CL5                      MACRO
OUTMEMO  DS    CL4,CL5                      MEMO
OUTDOC   DS    CL4,CL5                      DOCUMENTATION
OUTCOH   DS    CL4,CL5                      CMPTR OP HNDBOOK
OUTMISC  DS    CL4,CL5            ANYTHING ELSE ??
OUTPRC   DS    CL8           LANGUAGE PROCESSOR
         SPACE 1
FLAGOUT1 DC    X'00'         GROUP FLAG
PROFG    DC    X'00'         PROGRAM RUN OPTIONS
FGPEOF   EQU   X'80'           PRODUCTION INPUT EOF
FGTEOF   EQU   X'40'           TEST INPUT EOF
FGBUG    EQU   X'20'           DEBUG FLAG
FGPREC   EQU   X'08'           PRODUCTION RECORD AVAILABLE
FGTREC   EQU   X'04'           TEST RECORD AVAILABLE
SAVEEND  EQU   *
         ORG   ,
         VCON  *END
         PRINT NOGEN
         CVT   DSECT=YES
         DCBD  DEVD=DA,DSORG=PS
         IHACDE ,
         END   ,
