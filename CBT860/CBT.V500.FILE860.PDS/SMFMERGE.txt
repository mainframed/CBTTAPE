SMFM TITLE 'SORT DAILY SMF TAPE, MERGE WITH MASTER, THROW OUT BAD GUYS'
***********************************************************************
*                                                                     *
*        THE E15 EXIT CONSTRUCTS EACH SMF RECORD SEGMENT BY           *
*        SEGMENT IN A WORK AREA. EACH COMPLETE (V FORMAT) RECORD      *
*        IS THEN PASSED TO THE SORT AFTER BEING VALIDITY CHECKED.     *
*        DATA CHECK BLOCKS, ZERO LENGTH READ BLOCKS, AND LOGICAL      *
*        RECORDS FOR WHICH ALL SEGMENTS ARE NOT AVAILABLE, WILL       *
*        WILL BE SKIPPED.                                             *
*                                                                     *
*        THE E35 EXIT MERGES THE SORTED DAILY SMF RECORDS WITH        *
*        THE OLD SMF MASTER TO CREATE A NEW SMF MASTER TAPE.          *
*                                                                     *
*        THIS PROGRAM WRITTEN BY DICK OXLEY                           *
*              MVS AND MISCELLANEOUS OTHER NEW-FANGLED SUPPORT ADDED
*              BY G. POSTPISCHIL.                                83327
*                                                                     *
***********************************************************************
         SPACE 1                                                 83327
         COPY  OPTIONGB                                          83327
         SPACE 1                                                 83327
         SYSPARM LIST=YES                                        83327
         EJECT ,                                                 83327
         PRINT &PRTSOR                                           83327
SMFMERGE PGMHEAD ZERO15,BASE=(R11,R12),PARM=R9                  GP04068
         LR    R10,R13       COPY WORK AREA ADDRESS              83327
         DROP  R13                                               83327
         USING SAVE,R10                                          83327
         STM   R10,R12,BASEREG  SAVE WORK AND BASE ADDRESS FOR EXITS
         SPACE 1                                                 83327
*DEBUG*  BANDAID INIT        DEBUG ROUTINE                      GP04068
         SERVINIT ,          LOAD THE @SERVICE ROUTINE           83327
         SERVLOAD @PRINTER   AND THE PRINT ROUTINE              GP04068
         PRTOPEN SYSPRINT,OPT=(DUMMY,NOWTO)                      83327
         CH    R15,=H'4'     GOOD OPEN ?                         83327
         BNH   PRTOPNOK      YES; LEAVE LOG/CONSOLE FILE CLOSED  83327
         PRTOPEN CONSOLE,OPT=(DUMMY,NOWTO),DEV=2  OPEN A MESSAGE FILE
PRTOPNOK PRTV  HEAD1,TITLE=1 MAKE THE TITLE                      83327
         LA    R9,0(,R9)     CLEAR PARM POINTER                  83327
         LTR   R9,R9         IS THERE ONE ?                      83327
         BZ    DONEPARM      NO                                  83327
         ICM   R9,7,1(R9)    GET PARM ADDRESS                    83327
         BZ    DONEPARM      NONE                                83327
         SLR   R6,R6                                             83327
         ICM   R6,3,0(R9)    GET PARM LENGTH                     83327
         BZ    DONEPARM      NONE                                83327
         CLI   2(R9),C'-'    SUPPRESS REQUEST ?                  83327
         BE    PARMSKIP      YES                                 83327
         CLI   2(R9),C'Â¬'    ALTERNATE FORM ?                    83327
         BNE   PARMACCP      NO; ACCEPT REQUEST FORM             83327
PARMSKIP OI    FLAG,FGNOSID  SKIP RECORD IF SYSTEM ID MATCHES    83327
         LA    R9,1(,R9)     SKIP PARAMETER                      83327
         BCTR  R6,0          ADJUST RESIDUAL LENGTH              83327
PARMACCP LTR   R6,R6         VALID LENGTH ?                      83327
         BNP   BADPARM       NO; QUIT                            83327
         CH    R6,=H'100'    NOT TOO LONG ?                      83327
         BH    BADPARM                                           83327
         LR    R14,R6                                            83327
         SRDL  R14,32+2      FAKE DIVIDE BY 4                    83327
         SRA   R15,32-2      GET REMAINDER                       83327
         BNZ   BADPARM       INVALID                             83327
         LTR   R14,R14       AT LEAST ONE ?                      83327
         BP    GOODPARM      YES                                 83327
BADPARM  PRTV  MSGBDPRM,DEV=(1,2)  SIGNAL INVALID PARM           83327
         SERVTERM ,          CLOSE AND FREE EVERYTHING           83327
         ABEND 35            AND QUIT NASTILY                    83327
         SPACE 1                                                 83327
GOODPARM ST    R14,NUMSID    SAVE NUMBER OF SYSTEM IDS TO COMPARE
         LA    R9,2(,R9)     GET ADDRESS OF FIRST SYSTEM ID      83327
         ST    R9,@SIDLIST   SAVE ADDRESS FOR TESTS              83327
         SPACE 1                                                 83327
         SPACE 1
DONEPARM MVC   DB,=CL8'MASTOUT'                                  83327
         BAS   R9,DDCHECK    CHECK DD CARD                       83327
         BNP   MISSDD        MASTOUT IS REQUIRED                 83327
         MVC   DB,=CL8'MASTIN'  CHECK FOR INPUT MASTER           83327
         SLR   R5,R5         COUNT INPUT DD CARDS                83327
         BAS   R9,DDCHECK                                        83327
         BM    MISSDD        UNUSABLE                            83327
         BZ    *+8           DUMMY                               83327
         LA    R5,1(,R5)     UP DD COUNT                         83327
         MVC   DB,=CL8'DAILYSMF'   INCREMENTAL TAPE              83327
         BAS   R9,DDCHECK    CHECK FOR IT                        83327
         BM    MISSDD                                            83327
         BZ    *+8                                               83327
         LA    R5,1(,R5)     UP DD COUNT                         83327
         BXH   R5,R5,OPENINP ANY INPUT DD CARDS ?                83327
         PRTV  MSGNOINP,DEV=(1,2)  SIGNAL NO INPUT               83327
         B     MISSDDX       GO TO EXIT                          83327
         SPACE 1                                                 83327
DDCHECK  DEVTYPE DB,DB2      LOOK FOR DD CARD                    83327
         BXH   R15,R15,DDCHECKF  NOT FOUND - FAIL                83327
         TM    DB2+6,UCB3TAPE+UCB3DACC  SUPPORTED DEVICE ?       83327
         BM    DDCHECK0      OK ?                                83327
         CLI   DB2+6,0       DD DUMMY ?                          83327
         BNE   DDCHECKF      NO                                  83327
DDCHECK0 ICM   R15,1,DB2+6   LOAD TYPE TO SET CONDITION CODE     83327
         BR    R9            RETURN TO CALLER                    83327
DDCHECKF LNR   R15,R9        SET CONDITION CODE NEGATIVE         83327
         BR    R9            RETURN WITH ERROR                   83327
         SPACE 1                                                 83327
MISSDDO  PRTLIST MSGBADDD,DEV=(1,2)                              83327
         B     MISSDDX                                           83327
MISSDD   PRTLIST MSGNODD,DEV=(1,2)  SHOW REQUIRED DD UNUSABLE    83327
MISSDDX  SERVTERM ,          CLOSE AND FREE                      83327
         ABEND 3400          SIGNAL MISSING INPUT                83327
         SPACE 1                                                 83327
OPENINP  OPEN  (SMFIN,(INPUT,REREAD))                            83327
         MVC   DB,=CL8'DAILYSMF'  SHOW WHICH FILE                83327
         TM    SMFIN+DCBOFLGS-IHADCB,DCBOFOPN  OPENED ?          83327
         BNO   MISSDDO       BRANCH IF NOT                       83327
         OPEN  (MASTIN,(INPUT,REREAD))                           83327
         MVC   DB,=CL8'MASTIN'  SHOW WHICH FILE                  83327
         TM    MASTIN+DCBOFLGS-IHADCB,DCBOFOPN  OPENED ?         83327
         BNO   MISSDDO       BRANCH IF NOT                       83327
         SPACE 1                                                 83327
         L     R5,=A(PFXOHEAD+32*1024)  TOTAL BUFFER SPACE USED  83327
         STORAGE OBTAIN,LENGTH=(R5),BNDRY=PAGE                  GP04069
         LR    R2,R1         SAVE                               GP04069
         ST    R1,OUTBUFF    SET START OF INTERMEDIATE SORT BUFFER
         LA    R1,PFXL(,R2)  START OF INPUT BUFFER               83327
         ST    R1,WORKBUFF   SAVE START OF INPUT BUFFER          83327
         A     R1,=A(32*1024)   ADD BUFFER SIZE                  83327
         ST    R1,WORKBEND   SAVE END OF BUFFER ADDRESS(+1)      83327
         CLRL  (R2),(R5)     CLEAR STORAGE I'M KEEPING           83327
         SPACE 1
         LINK  EP=SORT,PARAM=(SORTPARM),VL=1
         ST    R15,DB        SAVE RETURN CODE FOR MESSAGE        83327
         BXLE  R15,R15,SUMMARY    SORT RAN OK ?
         PRTLIST MSGBDSRT,DEV=(1,2)  BAD SORT
         SERVTERM ,
         ABEND 1234,DUMP                                         83327
         SPACE 1
SUMMARY  XC    DB,DB         CLEAR                               83327
         ICM   R0,15,E15OUT  ANY DAILY RECORDS ?                 83327
         BZ    SUMSET4       NO                                  83327
         ICM   R0,15,MINOUT  ANY CUMULATIVE RECORDS ?            83327
         BZ    SUMSET4       NO                                  83327
         L     R0,E15SBAD    SKIPS DUE TO LENGTH/FLAG ?          83327
         A     R0,MINSBAD                                        83327
         BZ    SUMTEST       NO                                  83327
SUMSET4  OI    CCODE+L'CCODE-1,4  SET LEVEL 4 ERROR              83327
SUMTEST  ICM   R0,15,MOUTOUT  ANY OUTPUT ?                       83327
         BZ    SUMSET8       NO; ERROR                           83327
         L     R0,SEGSKIP    GET SKIP COUNT                      83327
         A     R0,SEGIOERR   ADD ERRORS                          83327
         BZ    SUMNOERR      SKIP IF NO ERRORS                   83327
SUMSET8  OI    CCODE+L'CCODE-1,8  SET NOT SO MINOR ERROR         83327
SUMNOERR OI    REFDLO+L'REFDLO-1,X'0F'  MAKE POSITIVE SIGN       83327
         OI    REFDHI+L'REFDHI-1,X'0F'                           83327
         LM    R14,R15,TOTBYTES  GET TOTAL BYTES WRITTEN         83327
         ICM   R0,15,MOUTOUT GET NUMBER OF RECORDS WRITTEN       83327
         BNP   SUMMPRNT      LEAVE DB SET TO ZERO                83327
         DR    R14,R0        GET AVERAGE BLOCK SIZE              83327
         ST    R15,DB        SAVE FOR PRINT                      83327
SUMMPRNT PRTLIST SUMOUT,DEV=(1,2)                                83327
         LM    R2,R3,@SIDLIST  GET SYSTEM ID LIST                83327
         LTR   R3,R3         ANY ?                               83327
         BNP   NOSIDSEL      NO; NO SELECTION                    83327
         PRTLIST MSGSIDSL    PRINT HEADER                        83327
SIDSHOW  PRTLIST MSGSID      PRINT NEXT ID                       83327
         LA    R2,4(,R2)     SKIP TO NEXT                        83327
         BCT   R3,SIDSHOW    REPEAT                              83327
NOSIDSEL LA    R2,OCCSID     GET LIST OF IDS FOUND               83327
         LA    R3,(OCCSIDND-OCCSID)/4  MAXIMUM TO CHECK          83327
         ICM   R0,15,0(R2)   ANY AT ALL ?                        83327
         BZ    SHOWSIDN      NO; SKIP                            83327
         PRTLIST MSGSIDOC    PRINT HEADER                        83327
SHOWSID  ICM   R0,15,0(R2)   LAST ENTRY ?                        83327
         BZ    SHOWSIDN      YES                                 83327
         PRTLIST MSGSID      PRINT ID                            83327
         LA    R2,4(,R2)                                         83327
         BCT   R3,SHOWSID    REPEAT FOR ALL                      83327
SHOWSIDN PRTLIST HEADOUT,TITLE=3                                 83327
         LA    R2,256        NUMBER OF ENTRIES TO DO             83327
         LA    R3,E15TIN     POINT TO DAILY INPUT                83327
         LA    R4,E15TOUT                                        83327
         LA    R5,MINTIN     CUMULATIVE INPUT                    83327
         LA    R6,MINTOUT    CUMULATIVE OUTPUT                   83327
         LA    R7,MOUTTOUT-MINTOUT(,R6)  MASTER OUTPUT           83327
PRINT2   L     R0,0(,R3)                                         83327
         O     R0,0(,R4)                                         83327
         O     R0,0(,R5)                                         83327
         O     R0,0(,R6)                                         83327
         O     R0,0(,R7)                                         83327
         BZ    PRINT2UP      SKIP IF ALL ZERO                    83327
         LA    R1,256                                            83327
         SR    R1,R2         GET TYPE                            83327
         ST    R1,DB                                             83327
         PRTLIST MSGCOUNT    PRINT ONE LINE OF COUNTERS          83327
         L     R0,0(,R7)     TOTAL OUT                           83327
         S     R0,0(,R4)     LESS E15 OUT                        83327
         S     R0,0(,R6)     LESS MASTER IN                      83327
         BZ    PRINT2UP      BALANCED                            83327
         A     R0,OUTMISS    ELSE ACCUMULATE ERROR               83327
         ST    R0,OUTMISS                                        83327
         PRTLIST MSGBALOF                                        83327
PRINT2UP LA    R0,4                                              83327
         AR    R3,R0                                             83327
         AR    R4,R0                                             83327
         AR    R5,R0                                             83327
         AR    R6,R0                                             83327
         AR    R7,R0                                             83327
         BCT   R2,PRINT2                                         83327
         PRTSPACE 1                                              83327
         PRTLIST TOTCOUNT    PRINT TOTALS (INDEX 256)            83327
         ICM   R0,15,OUTMISS  MISSING RECORDS ?                  83327
         BZ    ALLDONE       NO; QUIT                            83327
         LA    R1,MSGDOBAL   SET FOR REBALANCED RECORDS          83327
         C     R0,E35DUPE    BALANCED AGAIN ?                    83327
         BE    PRTWARN       YES                                 83327
         LA    R1,MSGUNBAL   ELSE SHOW UNBALANCED                83327
         OI    CCODE+L'CCODE-1,8  SET ERROR                      83327
PRTWARN  PRTLIST (R1)        PRINT THE ERROR/WARNING MESSAGE     83327
         B     ALLDONE
         SPACE 1
ALLDONE  LH    R9,CCODE      GET CONDITION CODE                  83327
         PRTLIST MSGCCODE                                        83327
         SERVTERM ,          CLOSE AND FREE EVERYTHING           83327
         PGMEXIT RC=(R9)     RETURN TO SYSTEM                   GP04068
         SPACE 2
*        END OF MAIN PROGRAM
         EJECT
***********************************************************************
*                                                                     *
*    E15 SORT EXIT - READ INPUT SEGMENT BY SEGMENT, SKIP BAD GUYS     *
*                                                                     *
***********************************************************************
         SPACE 2
         DROP  R11,R12                #####
         USING E15,R15                #####
E15      STM   R14,R12,12(R13)
         LM    R10,R12,BASEREG                                   83327
         DROP  R15                    #####
         USING SMFMERGE,R11,R12       #####
         LR    R9,R13
         LA    R13,EXITSAVE
         ST    R13,8(,R9)
         ST    R9,4(,R13)
         L     R8,OUTBUFF    GET OUTPUT BUFFER                   83327
         USING PFXMAP,R8     DECLARE IT                          83327
         SPACE 1                                                 83327
         B     NEWRECD                                           83327
FLUSHALL L     R15,SEGCNTR                                       83327
         A     R15,SEGSKIP                                       83327
         ST    R15,SEGSKIP   SET SEGMENTS SKIPPED                83327
NEWRECD  LA    R4,SMFRSV     POINT TO BEGINNING OF NEW RECORD    83327
         SLR   R5,R5                                             83327
         ST    R5,SEGCNTR    SET SEGMENT COUNTER TO ZERO         83327
         STC   R5,VSPREV     ZERO 'PREVIOUS' SEGMENT FLAGS       83327
         NI    FLAG,255-FGIOERR  RESET I/O ERROR FLAG            83327
         SPACE 1                                                 83327
NEWSEGD  BAS   R9,GETINP     GET ANOTHER SEGMENT                 83327
         LR    R2,R1         SAVE THE SEGMENT ADDRESS            83327
         INC   SEGCNTR       INCREASE THE SEGMENT COUNTER        83327
         TM    FLAG,FGIOERR  I/O ERROR ?                         83327
         BNZ   FLUSHALL      YES; FLUSH PREVIOUS AND CURRENT     83327
         LA    R0,4                                              83327
         SLR   R3,R3                                             83327
         ICM   R3,3,0(R2)    GET THE PRESUMED SEGMENT LENGTH     83327
         BNP   FLUSHALL      ERROR - FLUSH                       83327
         TM    VSPREV,1      ANY PRIOR SEGMENTS STACKED ?        83327
         BZ    NEWSEGM       NO; THIS MUST BE NEW OR COMPLETE SEG
         TM    2(R2),2       ADDITIONAL SEGMENT ?                83327
         BNZ   MOVESEV       YES; TACK IT ON                     83326
         SPACE 1                                                 83327
FLUSHSEG L     R15,SEGCNTR   GET SEGMENT COUNTER                 83327
         BCTR  R15,0         LESS CURRENT SEGMENT                83327
         A     R15,SEGSKIP   MAKE SEGMENTS SKIPPED               83327
         ST    R15,SEGSKIP   SET SEGMENTS SKIPPED                83327
         LA    R15,1                                             83327
         ST    R15,SEGCNTR   RESET COUNTER FOR CURRENT SEGMENT   83327
         LA    R4,SMFRSV     START A NEW RECORD                  83327
NEWSEGM  TM    2(R2),3       SINGLE SEGMENT ?                    83327
         BZ    MOVESEV       YES; MOVE AND PROCESS IT            83327
         TM    2(R2),2       OUT OF SEQUENCE ?                   83327
         BNZ   FLUSHALL      YES; KILL ALL                       83327
MOVESEV  MVC   VSPREV,2(R2)  SAVE CURRENT SPANNING BITS          83326
MOVESEG  AR    R2,R0         SET TO DATA PORTION OF SEGMENT      83327
         SR    R3,R0         AND SET DATA LENGTH                 83327
         BM    FLUSHALL      BAD - FLUSH PRIOR AND CURRENT       83327
         LR    R14,R4        COPY COPY START ADDRESS             83327
         LR    R15,R3        COPY LENGTH                         83327
         LA    R1,0(R14,R15)  GET LAST BYTE+1 OF OUTPUT          83327
         C     R1,WORKBEND   COMPARE TO END OF BUFFER(+1)        83327
         BH    FLUSHALL      TOO LARGE                           83327
         AR    R4,R3         ADDRESS FOR NEXT SEGMENT            83327
         AR    R5,R3         SIZE AFTER MOVE                     83327
         MVCL  R14,R2        COPY NEW SEGMENT                    83327
         TM    VSPREV,1      LAST ?                              83327
         BNZ   NEWSEGD       NO; READ NEXT SEGMENT               83327
         B     NEWREC        PROCESS NEW RECORD                  83327
         SPACE 1                                                 83327
NEWREC   XC    PFXRDW(PFXL),PFXRDW  CLEAR SORT KEY               83327
         S     R4,OUTBUFF    CALCULATE LENGTH OF PREFIX+DATA     83327
         CH    R4,=X'7FFF'   LARGER THAN MAX IN SYNCSORT ?       89060
         BH    FLUSHALL      YES; FLUSH                          89060
         STH   R4,PFXRDW     COMPLETE RECORD DESCRIPTOR          83327
         XC    SEGCNTR,SEGCNTR  CLEAR SEGMENT COUNTER            83327
         INC   E15IN         ADD TO TOTAL RECORDS IN             83327
         TM    SMFRSV,X'39'  SYSTEM CODE FIELD OK ?              93012
         BZ    TESTLEN            BRANCH IF SO                   84171
BADLEN   INC   E15SBAD       ADD TO BAD RECORD COUNT             83327
         B     E15SKP        SKIP THIS RECORD                    83327
         SPACE 1
TESTLEN  IC    R1,SMFTYP     GET RECORD TYPE                     83327
         LA    R15,E15TIN    POINT TO INPUT ARRAY                83327
         BAS   R14,TYPECLR   COUNT AND TOTAL                     83327
         LM    R2,R3,@SIDLIST  GET ADDRESS/LENGTH OF SYSTEM IDS  83327
         LTR   R3,R3         ANY TO DO ?                         83327
         BNP   SKPCHK        NO; BYPASS CHECKING                 83327
         LA    R5,SIDPICK    SET FOR RECORD SELECTION            83327
         TM    FLAG,FGNOSID    ARE WE IN EXCLUDE MODE ?          83327
         BZ    CPULOOP       NO                                  83327
         LA    R5,SIDSKIP    SET FOR EXCLUSION ON MATCH          83327
CPULOOP  CLC   SMFCPU,0(R2)  MATCHING SYSTEM ID ?                83327
         EX    0,0(,R5)      EXECUTE BRANCH                      83327
         LA    R2,4(,R2)     NEXT TABLE ENTRY                    83327
         BCT   R3,CPULOOP    TRY ANOTHER                         83327
         EX    0,4(,R5)      TAKE FINAL TEST                     83327
         SPACE 1                                                 83327
SKIPSID  INC   E15SCPU       ADD TO TOTAL SKIPPED - BAD CPU ID   83327
         B     E15SKP             GO SKIP THIS RECORD
         SPACE 1                                                 83327
SIDPICK  BE    SKPCHK   1/4  SID MATCH - CONTINUE WITH RECORD    83327
         B     SKIPSID  2/4  NONE MATCHED - SKIP RECORD          90068
SIDSKIP  BE    SKIPSID  3/4  SID MATCH - SKIP RECORD             83327
         B     SKPCHK   4/4  NONE MATCHED - PROCESS RECORD       83327
         SPACE 1
SKPCHK   SLR   R2,R2         CLEAR FOR TRT                       83327
         TRT   SMFTYP,PROCTAB  GET PROCESS TABLE                 83327
         B     *+4(R2)       PROCESS ACCORDING TO CODE           83327
         B     E15KEEP       0 - ENTRY NOT IN TABLE - COPY       83327
         B     E15KEEP       4 - SET FOR JOB-TYPE TIME-STAMP     83327
         B     E15KEEP       8 - TYPE 30                         84171
         B     E15KEEP      12 - TYPE 32                         84171
*        B     TYPSKIP      16 - RECORD TO BE SKIPPED            83327
         SPACE 1
TYPSKIP  INC   E15SREQ       COUNT AS SKIPPED BY REQUEST         83327
E15SKP   INC   E15STOT       ADD TO TOTAL RECORDS SKIPPED        83327
         B     NEWRECD       GO BUILD NEXT RECORD                83327
         SPACE 1
E15KEEP  LTR   R2,R2         SHORT OR LONG PREFIX ?              83327
         BZ    SHORTPFX      SHORT; SKIP USERID/ACCOUNT MOVE     83327
         SPACE 1
         SLR   R1,R1           CLEAR FOR ICM                     84171
         CH    R2,=Y(F)      WHAT TYPE OF PREFIX ?               84171
         BL    LONGPFX       STANDARD JOB HEADER                 84171
         BH    TP32PFX       TYPE 32                             84171
TP30PFX  ICM   R0,3,38+RDWOUT  ID SECTION PRESENT ?              84171
         BZ    SHORTPFX      NO                                  84171
         ICM   R1,15,32+RDWOUT  OFFSET TO ID SECTION             85312
TPCMPFX  LA    R1,RDWOUT(R1)   POINT TO ID SECTION               84171
         MVC   PFXACCT,24(R1)  SMF30UIF                          84171
         MVC   PFXRDAT,68(R1)  SMF30RSD                          84171
         MVC   PFXRTIM,64(R1)  SMF30RST                          84171
         MVC   PFXJOBN,0(R1)   SMF30JBN                          84171
         B     LONGCHEK      JOIN COMMON CODE                    85312
         SPACE 1                                                 84171
TP32PFX  ICM   R0,3,RDWOUT+38  SMF32ILN                          84171
         BZ    SHORTPFX                                          84171
         ICM   R1,15,RDWOUT+32  SMF32IOF                         85312
         B     TPCMPFX                                           84171
         SPACE 1                                                 84171
LONGPFX  MVC   PFXACCT,SMFACCT    MOVE ACCOUNT NUMBER
         MVC   PFXRDAT,SMFRDAT    MOVE READER DATE
         MVC   PFXRTIM,SMFRTIM    MOVE READER TIME
         MVC   PFXJOBN,SMFJOBN    MOVE JOBNAME
LONGCHEK LA    R15,PFXRDAT   GET DATE AND TIME                   85312
         BAS   R14,VALCHECK  CHECK DATE AND TIME                 85312
         B     BADLEN        SKIP THIS RECORD                    85312
SHORTPFX MVC   PFXTYP,SMFTYP      MOVE RECORD TYPE
         MVC   PFXHDAT,SMFHDAT    MOVE HEADER DATE
         MVC   PFXHTIM,SMFHTIM    MOVE HEADER TIME
         MVC   PFXCPU,SMFCPU      MOVE SYSTEM ID/MODEL
         LA    R15,PFXHDAT   GET DATE AND TIME                   85312
         BAS   R14,VALCHECK  CHECK DATE AND TIME                 85312
         B     BADLEN        SKIP THIS RECORD                    85312
         INC   E15OUT        COUNT RECORDS PASSED TO SORT        85312
         IC    R1,SMFTYP     GET THE RECORD TYPE                 85312
         LA    R15,E15TOUT   GET COUNTER ADDRESS                 83327
         BAS   R14,TYPECLR   COUNT AND TOTAL                     83327
         LA    R15,12        SET TO INSERT A RECORD              83327
         LA    R1,PFXRDW     SET SORT RECORD ADDRESS             83327
         B     SORTBACK      RETURN TO SORT                      83327
         SPACE 1
EOF      CLOSE (SMFIN)
         FREEPOOL SMFIN
         L     R1,SEGCNTR    GET SEGMENTS NOT PROCESSED          83327
         A     R1,SEGSKIP    ADD TO TOTAL SKIPPED                83327
         ST    R1,SEGSKIP                                        83327
         L     R1,OUTBUFF         ADDR. GETMAINED AREA
         L     R0,=A(32*1024+PFXOHEAD)  GET OUR SIZE             83327
         STORAGE RELEASE,LENGTH=(0),ADDR=(1)                    GP04069
         B     SORTEND       TERMINATE SORT PASS                 83327
         SPACE 2
SYNAD    OI    FLAG,FGIOERR  INDICATE I/O ERROR ON GET           83327
         BR    R14           RETURN                              83327
         SPACE 1                                                 83327
GETINP   LM    R14,R15,INPBUFF  GET INPUT BUFFER ADDRESS/SIZE    83327
         LTR   R15,R15       ANY TO DO ?                         83327
         BP    GETINN        YES                                 83327
GETING   GET   SMFIN         GET ANOTHER BLOCK                   83327
         XC    INPBUFF(8),INPBUFF                                83327
         TM    FLAG,FGIOERR  I/O ERROR ?                         83327
         BNZR  R9            YES; RETURN TO CALLER               83327
         LR    R14,R1        COPY ADDRESS                        83327
         SLR   R15,R15       CLEAR                               83327
         ICM   R15,3,DCBLRECL-IHADCB+SMFIN  GET THE SIZE         83327
         BZ    GETING        IGNORE ZERO SIZE - GAP ERROR ?      83327
         BM    GETINF        ON A TAPE DRIVE ?                   83327
         CLM   R15,3,DCBBLKSI-IHADCB+SMFIN  VALID ?              83327
         BH    GETINF        NO; FAIL                            83327
         ICM   R0,15,0(R14)  GET THE BLOCK DESCRIPTOR            83327
         CLM   R0,B'0011',ZEROES  ZERO LOW ?                     83327
         BNE   GETINF        NO; FAIL                            83327
         SRL   R0,16         FLIP                                83327
         CR    R0,R15        LOGICAL SIZE MATCHES PHYSICAL ?     83327
         BNH   GETINB        YES; OR LOWER (VS QSAM)             83327
GETINF   L     R15,SEGCNTR   COUNT AS A BAD SEGMENT              83327
         AH    R15,=H'1'                                         83327
         A     R15,SEGSKIP   SEGMENTS SKIPPED                    83327
         ST    R15,SEGSKIP                                       83327
         SLR   R15,R15                                           83327
         ST    R15,SEGCNTR                                       83327
         STC   R15,VSPREV                                        83327
         LA    R4,SMFRSV     START A NEW BLOCK                   83327
         B     GETING        READ ANOTHER BLOCK                  83327
GETINB   LR    R15,R0        SET SIZE OF BLOCK                   83327
         LA    R0,4          SIZE OF BLOCK DESCRIPTOR WORD       83327
         AR    R14,R0        UP ADDRESS                          83327
         SR    R15,R0        NEW SIZE                            83327
GETINN   CH    R15,=H'2'     LONG ENOUGH FOR SDW ?               84031
         BL    GETINV        NO                                  84031
         LR    R1,R14        RETURN SEGMENT ADDRESS TO USER      84031
         SLR   R0,R0                                             83327
         ICM   R0,3,0(R14)   GET NEW SDW                         83327
         BNP   GETINV        INVALID ?                           84031
         CH    R0,=H'4'      AT LEAST MINIMUM ?                  83327
         BL    GETINF        NO                                  83327
         AR    R14,R0                                            83327
         SR    R15,R0        AFTER THIS ONE                      83327
         BM    GETINF        TOO LONG                            83327
         STM   R14,R15,INPBUFF  SAVE                             83327
         BR    R9            RETURN TO CALLER                    83327
         SPACE 1                                                 84031
GETINV   CLI   0(R14),X'80'  VSAM END OF BLOCK ?                 84031
         BNE   GETINF        NO; ERROR                           84031
         B     GETING        YES; GET NEXT BLOCK                 84031
         SPACE 2
*        END OF E15 SORT EXIT ROUTINES
         EJECT
***********************************************************************
*                                                                     *
*    E35 SORT EXIT - MERGE - OLD MASTER + DAILY DATA = NEW MASTER     *
*                                                                     *
***********************************************************************
         SPACE 2
         DROP  R11,R12            #####
         USING E35,R15            #####
E35      STM   R14,R12,12(R13)
         LM    R10,R12,BASEREG                                   83327
         DROP  R15                #####
         USING SMFMERGE,R11,R12   #####
         LR    R9,R13
         LA    R13,EXITSAVE
         ST    R13,8(,R9)
         ST    R9,4(,R13)
         L     R7,MASTBUFF   MASTIN RECORD ADDRESS FROM LAST ENTRY
         SPACE 1
         ICM   R8,7,1(R1)    GET AND TEST RECORD ADDRESS         83327
         BNZ   E35SHAVE      HAVE ANOTHER RECORD                 83327
         OI    FLAG,FGSEOF   SET EOF                             83327
E35SHAVE OI    FLAG,FGSREC   SHOW RECORD AVAILABLE               83327
         USING PFXMAP,R8                                         83327
         SPACE 1                                                 83327
         TM    FLAG,FGMREC+FGMEOF  HAVE A RECORD OR EOF ?        83327
         BNZ   E35PRIME      YES; OPEN ALREADY DONE              83327
         OPEN  (MASTOUT,OUTPUT)  OPEN MASTER OUTPUT FILE         83327
         MVC   DB,=CL8'MASTOUT'                                  83327
         TM    MASTOUT+DCBOFLGS-IHADCB,DCBOFOPN OPENED ?         83327
         BZ    MISSDDO       SHOW UNABLE TO OPEN                 83327
         SPACE 1
E35PRIME TM    FLAG,FGMEOF+FGMREC   DO WE NEED A MASTER RECORD ? 83327
         BNZ   E35COMP       NO; GO TO COMPARE                   83327
         BAS   R9,GETMAST    GET A NEW MASTER RECORD             83327
E35COMP  LA    R15,4         SET 'DELETE' CODE                   83327
         TM    FLAG,FGSREC   IS THERE A SORT RECORD AVAILABLE ?  83327
         BZ    SORTBACK      NO; GET ONE                         83327
         TM    FLAG,FGSEOF+FGMEOF  END FILE BOTH FILES ?         83327
         BZ    E35COMP1      NONE; CONTINUE COMPARE              83327
         BO    E35END             BRANCH IF BOTH
         TM    FLAG,FGSEOF   SORT EOF ?                          83327
         BNZ   PUTMAST       YES; WRITE MASTER RECORD            83327
         B     PUTSORT       ELSE WRITE SORT RECORD              83327
         SPACE 1                                                 83327
E35COMP1 CLC   MFXRDW+4(PFXL-4),PFXRDW+4  COMPARE KEYS           83327
         BL    PUTMAST       LOW; WRITE MASTER RECORD            83327
         BH    PUTSORT       HIGH; WRITE SORT RECORD             83327
         LA    R0,RDWOUT     POINT TO SORT RECORD PROPER         83327
         SLR   R1,R1                                             83327
         ICM   R1,3,RDWOUT   GET LENGTH                          83327
         LR    R14,R7        GET MASTER RECORD                   83327
         SLR   R15,R15                                           83327
         ICM   R15,3,0(R14)  GET MASTER LENGTH                   83327
         CLCL  R14,R0        COMPARE MASTER TO SORT RECORD       83327
         BL    PUTMAST       LOW; WRITE MASTER                   83327
         BH    PUTSORT       HIGH; WRITE SORT                    83327
         INC   E35DUPE       COUNT DUPLICATE RECORD              83327
         NI    FLAG,255-FGSREC-FGMREC  SHOW NO RECORDS AVAILABLE 83327
         B     E35PRIME      GET SOME                            83327
         SPACE 1
PUTSORT  SLR   R1,R1                                             83327
         ICM   R1,3,PFXRDW   GET TOTAL LENGTH (INCL. SORT KEY)   83327
         SH    R1,=Y(RDWOUT-PFXRDW)  GET LENGTH AFTER KEY        83327
         SLL   R1,16                                             83327
         STCM  R1,15,RDWOUT  MAKE NEW RECORD DESCRIPTOR          83327
         LA    R4,RDWOUT     SET OUTPUT BLOCK ADDRESS            83327
         NI    FLAG,255-FGSREC  RESET SORT RECORD AVAILABLE      83327
         CLM   R1,B'1100',DCBLRECL-IHADCB+MASTOUT  WILL IT FIT ? 83327
         BNH   PUTCOMM       YES; PROCEED                        83327
         INC   E15SBAD       COUNT ERROR                         83327
         INC   E15STOT       COUNT SKIPPED                       83327
         SRL   R1,16         SHIFT DOWN                          83327
         C     R1,MAXSKIP    LONGEST EVER ?                      83327
         BNH   *+8           NO                                  83327
         ST    R1,MAXSKIP    SAVE LONGEST SKIPPED                83327
         LA    R15,E15TOUT   GET ARRAY WHERE PREVIOUSLY COUNTED  83327
         IC    R1,5(,R4)     GET RECORD TYPE                     83327
         ICM   R1,B'1110',ZEROES  CLEAR THE UNUSED BYTES         83327
         SLL   R1,2          CONVERT TYPE TO OFFSET              83327
         LA    R1,0(R1,R15)  GET ARRAY ENTRY                     83327
         INC   0(R1),WORK=R14,INC=-1  REMOVE ONE COUNT           83327
         LA    R15,4*256(,R15)  SPACE TO TOTAL                   83327
         INC   0(R15),WORK=R14,INC=-1  REMOVE ONE FROM TOTAL     83327
         SPACE 1                                                 83327
PUTMAST  LR    R4,R7         COPY MASTER ADDRESS                 83327
         NI    FLAG,255-FGMREC  RESET RECORD AVAILABLE           83327
PUTCOMM  INC   MOUTOUT       COUNT RECORDS WRITTEN               83327
         LM    R14,R15,TOTBYTES  GET BYTE COUNT                  83327
         SLR   R0,R0                                             83327
         ICM   R0,3,0(R4)    GET RECORD SIZE                     83327
         C     R0,MAXOUT     GET LARGEST WRITTEN                 83327
         BNH   *+8                                               83327
         ST    R0,MAXOUT     SAVE LARGEST SIZE                   83327
         ALR   R15,R0        INCREASE COUNT                      83327
         BC    12,*+8        BRANCH IF NO CARRY                  83327
         AH    R14,=H'1'     PROPAGATE CARRY                     83327
         STM   R14,R15,TOTBYTES  STASH BYTE COUNT BACK           83327
         IC    R1,5(,R4)     GET RECORD TYPE                     83327
         LA    R15,MINTOUT   POINT PARTLY TO ARRAY               83327
         LA    R15,MOUTTOUT-MINTOUT(R15)   POINT TO ARRAY        83327
         BAS   R14,TYPECLR   COUNT AND TOTAL                     83327
         PUT   MASTOUT,(R4)  PUT THE RECORD                      83327
         LA    R14,4                                             83327
         LA    R15,OCCSIDND-4  POINT TO LAST SYSTEM ID ENTRY     83327
         SLR   R0,R0         COMPARE                             83327
         LA    R1,OCCSID     TABLE OF SYSTEM ID OCCURENCES       83327
         ICM   R2,15,SMFCPU-RDWOUT(R4)  GET THE SYSTEM ID        83327
         BZ    REFDAT        SKIP IF ZERO                        83327
REFCPU   C     R2,0(,R1)     MATCH ?                             83327
         BE    REFDAT        YES; DO DATE                        83327
         C     R0,0(,R1)     SPARE ENTRY ?                       83327
         BNE   REFCPUX       NO; BUMP                            83327
         ST    R2,0(,R1)     STASH IT                            83327
         B     REFDAT        COMPARE DATE                        83327
REFCPUX  BXLE  R1,R14,REFCPU   TRY AGAIN                         83327
REFDAT   ICM   R2,15,SMFHDAT-RDWOUT(R4)  ANY DATE IN RECORD ?    83327
         BZ    E35PRIME      NO; SKIP DATE CHECK                 83327
         BAS   R9,REFDATE    DO IT                               83327
         SLR   R2,R2         CLEAR FOR TRT IC                    83327
         TRT   SMFTYP-RDWOUT(1,R4),PROCTAB  GET PROCESS TABLE    83327
*        LTR   R2,R2         JOB OR SYSTEM ENTRY ?               84171
         BZ    E35PRIME      SYSTEM; SKIP JOB DATE               84171
         ICM   R2,15,SMFRDAT-RDWOUT(R4)  GET THE READER DATE     83327
         BZ    E35PRIME                                          83327
         LA    R9,E35PRIME   SET RETURN ADDRESS                  83327
         SPACE 1                                                 83327
REFDATE  ICM   R0,15,REFDLO  LOW DATE EVER SET ?                 83327
         BZ    REFDATEL      NO; FORCE IT                        83327
         CR    R2,R0         IS NEW ONE LOWER ?                  83327
         BNL   REFDATEH      NO                                  83327
REFDATEL ST    R2,REFDLO     SAVE LOW DATE                       83327
REFDATEH C     R2,REFDHI     AFTER LATE DATE ?                   83327
         BNHR  R9            NO; RETURN                          83327
         ST    R2,REFDHI     SAVE HIGH DATE                      83327
         BR    R9                                                83327
         SPACE 1
INEOF    CLOSE (MASTIN)                                          83327
         OI    FLAG,FGMEOF+FGMREC  SET EOF FLAGS                 83327
         B     E35PRIME                                          83327
         SPACE 1
E35END   CLOSE (MASTOUT)
SORTEND  LA    R15,8         TERMINATE THIS PASS OF THE SORT     83327
SORTBACK L     R13,4(,R13)                                       83327
         L     R14,12(,R13)
         LM    R2,R12,28(R13)
         BR    R14                RETURN TO SORT
         EJECT
*        ROUTINE TO GET AND EDIT INPUT MASTER
         SPACE 2
GETMAST  GET   MASTIN        GET INPUT (RECORD INTERFACE OPTION) 83327
         LR    R7,R1              SAVE BUFF. ADDR.               83327
         SLR   R2,R2                                             83327
         ICM   R2,3,0(R7)    GET LENGTH                          83327
         BM    GETMAST       SKIP END OF BLOCK                   84031
         INC   MININ         ADD TO TOTAL MASTER IN              83327
         CLM   R2,3,MASTOUT+DCBLRECL-IHADCB  LENGTH PROCESSABLE ?
         BNH   GETMOKLN      YES                                 83327
         C     R2,MAXSKIP    LARGEST SKIPPED YET ?               83327
         BNH   BADMTYP       NO; JUST COUNT SKIPPED              83327
         ST    R2,MAXSKIP    SAVE LARGEST                        83327
BADMTYP  INC   MINSBAD                                           83327
         B     E35SKIP       COUNT AND GET A NEW RECORD          83327
         SPACE 1
GETMOKLN TM    4(R7),X'39'   SYSTEM CODE FIELD OK ?              93012
         BNZ   BADMTYP       NO                                  84171
         IC    R1,5(,R7)          GET RECORD TYPE                83327
         LA    R15,MINTIN                                        83327
         BAS   R14,TYPECLR   COUNT AND TOTAL                     83327
         LM    R2,R3,@SIDLIST  GET ADDRESS/LENGTH OF SYSTEM IDS  83327
         LTR   R3,R3         ANY TO DO ?                         83327
         BNP   SKPMCHK       NO; BYPASS CHECKING                 83327
         LA    R5,SIDMPICK   SET FOR RECORD SELECTION            83327
         TM    FLAG,FGNOSID  ARE WE IN EXCLUDE MODE ?            83327
         BZ    CPUMLOOP      NO                                  83327
         LA    R5,SIDMSKIP   SET FOR EXCLUSION ON MATCH          83327
CPUMLOOP CLC   14(4,R7),0(R2)  MATCHING SYSTEM ID ?              83327
         EX    0,0(,R5)      EXECUTE BRANCH                      83327
         LA    R2,4(,R2)     NEXT TABLE ENTRY                    83327
         BCT   R3,CPUMLOOP   TRY ANOTHER                         83327
         EX    0,4(,R5)      TAKE FINAL TEST                     83327
         SPACE 1                                                 83327
SKIPMSID INC   MINSCPU       SKIPPED DUE TO CPU MISMATCH         83327
         B     E35SKIP       COUNT AND GET A NEW RECORD          83327
         SPACE 1
SKPMCHK  XC    MFXRDW(PFXL),MFXRDW  CLEAR FAKE SORT KEY          83327
         SLR   R2,R2         CLEAR FOR TRT IC                    83327
         TRT   5(1,R7),PROCTAB  GET CONTROL BYTE FOR THIS TYPE   84031
         CH    R2,=Y(Z)      TO BE SKIPPED ?                     84171
         BE    TYPMSKIP      YES; COUNT AS SUCH                  83327
         INC   MINOUT        ADD TO TOTAL RECORDS OUT            83327
         IC    R1,5(,R7)     RECORD TYPE                         83327
         LA    R15,MINTOUT   POINT TO INDEXED COUNTER            83327
         BAS   R14,TYPECLR   COUNT AND TOTAL                     83327
         DROP  R8                                                83327
         USING RDWOUT,R7     FAKE MAPPING FOR MASTIN RECORD      83327
         LTR   R2,R2         SHORT OR LONG PREFIX ?              83327
         BZ    SHORTMFX      SHORT; SKIP USERID/ACCOUNT MOVE     83327
         SPACE 1                                                 83327
         SLR   R1,R1           CLEAR FOR ICM                     84171
         CH    R2,=Y(F)      WHAT TYPE OF PREFIX ?               84171
         BL    LONGMFX       STANDARD JOB HEADER                 84171
         BH    TP32MFX       TYPE 32                             84171
TP30MFX  ICM   R0,3,38+RDWOUT  ID SECTION PRESENT ?              84171
         BZ    SHORTMFX      NO                                  84171
         ICM   R1,15,32+RDWOUT  OFFSET TO ID SECTION             85312
TPCMMFX  LA    R1,RDWOUT(R1)   POINT TO ID SECTION               84171
         MVC   MFXACCT,24(R1)  SMF30UIF                          84171
         MVC   MFXRDAT,68(R1)  SMF30RSD                          84171
         MVC   MFXRTIM,64(R1)  SMF30RST                          84171
         MVC   MFXJOBN,0(R1)   SMF30JBN                          84171
         B     SHORTMFX      JOIN COMMON CODE                    84171
         SPACE 1                                                 84171
TP32MFX  ICM   R0,3,RDWOUT+38  SMF32ILN                          84171
         BZ    SHORTMFX                                          84171
         ICM   R1,15,RDWOUT+32  SMF32IOF                         85312
         B     TPCMMFX                                           84171
         SPACE 1                                                 84171
LONGMFX  MVC   MFXACCT,SMFACCT    MOVE ACCOUNT NUMBER            83327
         MVC   MFXRDAT,SMFRDAT    MOVE READER DATE               83327
         MVC   MFXRTIM,SMFRTIM    MOVE READER TIME               83327
         MVC   MFXJOBN,SMFJOBN    MOVE JOBNAME                   83327
SHORTMFX MVC   MFXTYP,SMFTYP      MOVE RECORD TYPE               83327
         MVC   MFXHDAT,SMFHDAT    MOVE HEADER DATE               83327
         MVC   MFXHTIM,SMFHTIM    MOVE HEADER TIME               83327
         MVC   MFXCPU,SMFCPU      MOVE SYSTEM ID/MODEL           83327
         OI    FLAG,FGMREC   SHOW MASTER INPUT RECORD PRESENT    83327
         ST    R7,MASTBUFF   REMEMBER THE BUFFER START           83327
         BR    R9                 RETURN WITH GOOD RECORD        83327
         SPACE 1
TYPMSKIP INC   MINSREQ       ADD TO TOTAL SKIPPED BY REQUEST     83327
E35SKIP  INC   MINSTOT       ADD TO TOTAL SKIPPED                83327
         B     GETMAST            GET A NEW RECORD
         SPACE 1                                                 83327
SIDMPICK BE    SKPMCHK  1/4  SID MATCH - CONTINUE WITH RECORD    83327
         B     SKIPMSID 2/4  NONE MATCHED - SKIP RECORD          90068
SIDMSKIP BE    SKIPMSID 3/4  SID MATCH - SKIP RECORD             83327
         B     SKPMCHK  4/4  NONE MATCHED - PROCESS RECORD       83327
         SPACE 1                                                 83327
*        ROUTINE TO COUNT SMF RECORDS BY FUNCTION AND TYPE       83327
*          IN: R15 - ARRAY BASE   R1 - SMF TYPE                  83327
*                                                                83327
TYPECLR  ICM   R1,B'1110',ZEROES  CLEAR THE UNUSED BYTES         83327
         SLL   R1,2          CONVERT TYPE TO OFFSET              83327
         LA    R1,0(R1,R15)  GET ARRAY ENTRY                     83327
         INC   0(R1),WORK=R0   COUNT ENTRY BY TYPE               83327
         LA    R15,4*256(,R15)  SPACE TO TOTAL                   83327
         INC   0(R15),WORK=R0                                    83327
         BR    R14           RETURN TO CALLER                    83327
         SPACE 1                                                 85312
*        SUBROUTINE TO CHECK VALIDITY OF TIME AND DATE FIELDS    85312
*        RETURN - R14/BAD, 4(R14)/OK; IN DATE 0(R15), TIME 4(R15)
*                                                                85312
VALCHECK STM   R0,R1,DB      SAVE A LITTLE                       85312
         ICM   R0,15,4(R15)  CHECK TIME                          85312
         BMR   R14           INVALID                             85312
         C     R0,=A(24*60*60*100)  NOT TOO HIGH ?               85312
         BNLR  R14           TOO BAD                             85312
         CLI   0(R15),0      THREE-BYTE DATE FORMAT ?            85312
         BE    VALCHEK3      YES                                 85312
         CLI   0(R15),X'19'  ELSE FOUR-BYTE ?                    85312
         BE    VALCHEK4      YES                                 85312
         CLI   0(R15),X'20'  IF I LIVE TO SEE IT ?               85312
         BNER  R14           NO                                  85312
VALCHEK4 DS    0H            NOTHING SPECIAL                     85312
VALCHEK3 UNPK  DB2,0(4,R15)  UNPACK THE DATE                     85312
         LA    R0,7          CHECK SEVEN NUMERICS                85312
         LA    R1,DB2        FIRST TEST POSITION                 85312
VALCHEKL CLI   0(R1),C'0'    VALID ?                             85312
         BLR   R14           NO                                  85312
         CLI   0(R1),C'9'    VALID ?                             85312
         BHR   R14           NO                                  85312
         LA    R1,1(,R1)                                         85312
         BCT   R0,VALCHEKL   TRY NEXT ONE                        85312
         CLI   0(R1),X'C0'   PACKED PLUS ?                       85312
         BLR   R14           NO                                  85312
         CLI   0(R1),X'C9'                                       85312
         BNH   VALCHEKS      SET SIGN                            85312
         CLI   0(R1),C'0'    PREFERRED PLUS ?                    85312
         BLR   R14           NO; KILL                            85312
         CLI   0(R1),C'9'    DITTO ?                             85312
         BHR   R14           DITTO                               85312
VALCHEKS OI    3(R15),X'0F'  MAKE GOOD PLUS SIGN                 85312
         CP    2(2,R15),=P'1'  VALID DAY ?                       85312
         BLR   R14           NO                                  85312
         CP    2(2,R15),=P'366'  DITTO                           85312
         BHR   R14           DITTO                               85312
         LM    R0,R1,DB      RELOAD                              85312
         B     4(,R14)       RETURN                              85312
         SPACE 2                                                 83327
         USING OUTEX,R15                                         83327
         DROP  R11,R12                                           83327
OUTEX    LM    R10,R12,BASEREG  RESTORE BASES                    83327
         DROP  R15                                               83327
         USING SMFMERGE,R11,R12  RESPECIFY BASES                 83327
         USING IHADCB,R1     DECLARE THE PASSED DCB              83327
         ICM   R0,3,DCBLRECL  ANY LRECL ?                        83327
         BNZ   *+10          YES; USE IT                         83327
         MVC   DCBLRECL,DCBLRECL-IHADCB+MASTIN  COPY FROM INPUT  83327
         ICM   R0,3,DCBBLKSI  ANY BLOCKSIZE ?                    83327
         BNZ   *+10          YES; USE IT                         83327
         MVC   DCBBLKSI,DCBBLKSI-IHADCB+MASTIN  COPY FROM OUTPUT 83327
         CLI   DCBRECFM,0    ANY RECFM AT ALL ?                  83327
         BNE   *+8           YES; TEST IT                        83327
         MVI   DCBRECFM,DCBRECV+DCBRECBR+DCBRECSB  SET VBS       83327
         TM    DCBRECFM,255-DCBRECV-DCBRECBR-DCBRECSB  FUNNY BITSINESS?
         BNZ   OUTEXBAD      YES; FAIL                           83327
         TM    DCBRECFM,DCBRECV  RECFM=V ?                       83327
         BZ    OUTEXBAD      NO; ERROR                           83327
         OI    DCBRECFM,DCBRECBR+DCBRECSB  MAKE (V)BS ANYWAY     83327
         BR    R14           RETURN TO CALLER                    83327
OUTEXBAD PRTV  MSGBADUT,DEV=(1,2)  SET FOR BAD OUTPUT            83327
         SERVTERM ,          CLOSE AND FREE                      83327
         ABEND 13            QUIT                                83327
         SPACE 2                                                 83327
         LTORG ,                                                 83327
         SPACE 1
         EJECT
*        CONSTANTS AND STUFF
         SPACE 2
SMFIN    DCB   DDNAME=DAILYSMF,DSORG=PS,MACRF=(GL),EODAD=EOF,          *
               OPTCD=Z,SYNAD=SYNAD,RECFM=U,LRECL=32760,BLKSIZE=32760
         SPACE 1
MASTIN   DCB   DDNAME=MASTIN,DSORG=PS,MACRF=(GL),BFTEK=A,EODAD=INEOF
         SPACE 1
MASTOUT  DCB   DDNAME=MASTOUT,DSORG=PS,MACRF=(PM),BFTEK=S,EXLST=OUTEXL
OUTEXL   DC    0A(0),X'85',AL3(OUTEX)  OUTPUT EXIT               83327
         SPACE 1
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=5                        83327
CONSOLE  PRTWORK *CONSOLE                                        83327
BASEREG  DC    3A(0)         WORK, BASE1 AND BASE2 ADDRESSES     83327
PROCTAB  TRTSMF ,            PULL IN THE SMF RECORD DEFINITIONS  90068
         SPACE 2
*        SORT PARAMETER LIST
         SPACE 1
         DC    0F'0',H'0'
SORTPARM DC    AL2(SPE-SPS)
SPS      DC    A(CSORT,CSORTE)    SORT CONTROL CARD             GP04069
         DC    A(CREC,CRECE)      RECORD CONTROL CARD           GP04069
         DC    A(E15,E35)         EXZIT ADDRESSES               GP04069
         DC    X'01',AL3(300*1024)  RESERVED STORAGE SIZE       GP04069
         DC    X'03',AL3(DDMSG)   MESSAGE DD NAME               GP04069
         DC    X'FF00',C'AP'
SPE      EQU   *
CSORT    DC    C' SORT FIELDS=(5,41,A),FORMAT=BI,SIZE=E200000'  GP04069
CSORTE   DC    0C' '
CREC     DC    C' RECORD TYPE=V,LENGTH=(32767,,,41,300)'         85062
CRECE    DC    C' '
DDMSG    DC    CL8'SYSOUT'   SORT OUTPUT FILE                   GP04069
         SPACE 1
HEAD1    VCON  '#SMFMERGE - INPUT/OUTPUT STATISTICS'             83327
         SPACE 1                                                 83327
MSGBADUT VCON  '0***** DCB PARAMETERS FOR MASTOUT INVALID OR NOT VBS ***
                **'                                              83327
         SPACE 1                                                 83327
SUMOUT   FDOPT NL,CC=C'0'                                        83327
         FDPRT 'SORT OPTIONS :'
         FDOPT IND=10                                            83327
         FDPRT CSORT,CSORTE-CSORT,NL                             83327
         FDPRT CREC,CRECE-CREC,NL                                83327
         FDOPT IND=0                                             83327
         FDOPT NL,CC=C'0'                                        83327
         FDPRT 'DAILY SMF STATISTICS :',NL                       83327
         FDOPT IND=10                                            83327
         FDCLC SEGIOERR,ZEROES,4,BE=LIBALL                       83327
         FDPRT SEGIOERR,I,RADJ,PAD,LEN=10                        83327
         FDPRT 'DATA CHECKS ON INPUT,'                           83327
LIBALL   FDCLC SEGSKIP,ZEROES,4,BE=LICALL                        83327
         FDPRT SEGSKIP,I,RADJ,PAD,LEN=10                         83327
         FDPRT 'SEGMENTS SKIPPED.'                               83327
LICALL   FDPRT E15IN,I,RADJ,PAD,NL,LEN=10                        83327
         FDPRT 'RECORDS IN,'                                     83327
         FDPRT E15OUT,I,RADJ,PAD,LEN=10                          83327
         FDPRT 'RECORDS OUT.'                                    83327
         FDCLC E15STOT,ZEROES,4,BE=LINALL                        83327
         FDPRT E15STOT,I,RADJ,PAD,NL,LEN=10                      83327
         FDPRT 'RECORDS BYPASSED FOR THE FOLLOWING REASONS:'     83327
         FDPRT E15SBAD,I,RADJ,PAD,NL,LEN=15                      83327
         FDPRT 'INVALID LENGTH OR SYSTEM CODE FIELD.'            83327
         FDPRT E15SCPU,I,RADJ,PAD,NL,LEN=15                      83327
         FDPRT 'REJECTED DUE TO SYSTEM ID'                       83327
         FDPRT E15SREQ,I,RADJ,PAD,NL,LEN=15                      83327
         FDPRT 'SKIPPED VIA TABLE OF SMF TYPES.'                 83327
         FDGOTO LIPOUT                                           83327
LINALL   FDPRT 'NO RECORDS SKIPPED',RADJ,LEN=25                  83327
LIPOUT   FDOPT NL,CC=C'0'                                        83327
         FDOPT IND=0                                             83327
         FDPRT 'MASTER IN STATISTICS :',NL                       83327
         FDOPT IND=10                                            83327
         FDPRT MININ,I,RADJ,PAD,LEN=10                           83327
         FDPRT 'RECORDS IN,'                                     83327
         FDPRT MINOUT,I,RADJ,PAD,LEN=10                          83327
         FDPRT 'RECORDS OUT.'                                    83327
         FDCLC MINSTOT,ZEROES,4,BE=LIPALL                        83327
         FDPRT MINSTOT,I,RADJ,PAD,NL,LEN=10                      83327
         FDPRT 'RECORDS BYPASSED FOR THE FOLLOWING REASONS:'     83327
         FDPRT MINSBAD,I,RADJ,PAD,NL,LEN=15                      83327
         FDPRT 'INVALID LENGTH OR SYSTEM CODE FIELD.'            83327
         FDPRT MINSCPU,I,RADJ,PAD,NL,LEN=15                      83327
         FDPRT 'REJECTED DUE TO SYSTEM ID'                       83327
         FDPRT MINSREQ,I,RADJ,PAD,NL,LEN=15                      83327
         FDPRT 'SKIPPED VIA TABLE OF SMF TYPES.'                 83327
         FDGOTO LIMALL                                           83327
LIPALL   FDPRT 'NO RECORDS SKIPPED',RADJ,LEN=25                  83327
LIMALL   FDOPT NL,CC=C'0'                                        83327
         FDOPT IND=0                                             83327
         FDPRT 'OUTPUT MASTER STATISTICS :',NL                   83327
         FDOPT IND=10                                            83327
         FDPRT MOUTOUT,I,RADJ,PAD,LEN=10                         83327
         FDPRT 'RECORDS ON NEW MASTER.'                          83327
         FDCLC E35DUPE,ZEROES,4,BE=LIVALL                        83327
         FDPRT E35DUPE,I,RADJ,PAD,LEN=10                         83327
         FDPRT 'DUPLICATE RECORDS SKIPPED.'                      83327
LIVALL   FDCLC DB,ZEROES,4,BE=LITALL  SKIP AVERAGE               83326
         FDPRT 'AVERAGE OUTPUT RECORD LENGTH IS',PAD,NL          83327
         FDPRT DB,4,I,PADL                                       83327
         FDPRT ','                                               83327
LITALL   FDPRT 'MAXIMUM SIZE IS',PAD                             83327
         FDPRT MAXOUT,I,PAD                                      83327
         FDCLC MAXSKIP,ZEROES,4,BE=LIDALL                        83327
         FDPRT '   THE LARGEST SKIPPED RECORD WAS'               83327
         FDPRT MAXSKIP,I,PAD                                     83327
         FDPRT 'BYTES LONG'                                      83327
LIDALL   FDPRT 'THE LOWEST OUTPUT DATE IS',NL                    83327
         FDPRT REFDLO,DATE,PAD                                   83327
         FDPRT '('                                               83327
         FDPRT REFDLO,DATJ                                       83327
         FDPRT ')    THE HIGHEST DATE IS',PADR                   83327
         FDPRT REFDHI,DATE,PAD                                   83327
         FDPRT '('                                               83327
         FDPRT REFDHI,DATJ                                       83327
         FDPRT ')'                                               83327
LIZALL   FDPRT *END                                              83327
         SPACE 1                                                 83327
TOTCOUNT FDPRT 'TOTALS :',NL,LEN=8                               83327
         FDGOTO MSGCOUNX                                         83327
MSGCOUNT FDPRT DB,4,I,RADJ,NL,LEN=8                              83327
MSGCOUNX FDPRT 0(R3),4,I,RADJ,LEN=18                             83327
         FDPRT 0(R4),4,I,RADJ,LEN=13                             83327
         FDPRT 0(R5),4,I,RADJ,LEN=13                             83327
         FDPRT 0(R6),4,I,RADJ,LEN=13                             83327
         FDPRT 0(R7),4,I,RADJ,LEN=13                             83327
         FDPRT *END                                              83327
         SPACE 1                                                 83327
HEADOUT  FDPRT 'SMF TYPE',NL,LEN=8                               83327
         FDPRT 'DAILY IN',RADJ,LEN=18                            83327
         FDPRT 'DAILY OUT',RADJ,LEN=13                           83327
         FDPRT 'MASTER IN',RADJ,LEN=13                           83327
         FDPRT 'MASTER KEPT',RADJ,LEN=13                         83327
         FDPRT 'MASTER OUT',RADJ,LEN=13                          83327
         FDPRT *END                                              83327
         SPACE 1                                                 83327
MSGBDPRM VCON  '0***** UNACCEPTABLE PARM FIELD *****'            83327
MSGBDSRT FDPRT '***** SORT FAILED; RETURN CODE IS'               83327
         FDPRT DB,4,I,PAD                                        83327
         FDPRT '*****'                                           83327
         FDPRT *END                                              83327
MSGNOINP VCON  '0***** AT LEAST ONE OF DAILYSMF AND MASTIN MUST BE SUPP*
               LIED *****'                                       83327
MSGNODD  FDPRT '*****',NL                                        83327
         FDPRT DB,DEB,PAD                                        83327
         FDPRT 'DD CARD UNUSABLE *****'                          83327
         FDPRT *END                                              83327
MSGBADDD FDPRT '***** OPEN FAILED FOR',NL                        83327
         FDPRT DB,DEB,PAD                                        83327
         FDPRT 'FILE *****'                                      83327
         FDPRT *END                                              83327
MSGNOREG VCON  '0***** INSUFFICIENT STORAGE AVAILABLE *****'     83327
MSGSIDSL FDOPT NL,CC=C'0'                                        83327
         FDPRT 'INPUT RECORDS WILL BE',PAD                       83327
         FDTM  FLAG,FGNOSID,BZ=MSGSIDIN                          83327
         FDPRT 'EX'                                              83327
         FDGOTO MSGSIDCM                                         83327
MSGSIDIN FDPRT 'IN'                                              83327
MSGSIDCM FDPRT 'CLUDED FOR THE FOLLOWING IDS :',PADR             83327
         FDPRT *END                                              83327
MSGSIDOC FDOPT NL,CC=C'0'                                        83327
         FDPRT 'RECORDS WRITTEN FOR IDS :'                       83327
         FDPRT *END                                              83327
MSGSID   FDOPT IND=10                                            83327
         FDPRT 0(R2),4,PAD                                       83327
         FDPRT *END                                              83327
MSGUNBAL FDPRT '*****',NL                                        83327
         FDPRT OUTMISS,I,PAD                                     83327
         FDPRT 'RECORDS UNACCOUNTED FOR'                         83327
MSGBALOF FDPRT '*****',PAD                                       83327
         FDPRT *END                                              83327
MSGDOBAL FDPRT 'RECORD IMBALANCE DUE TO SUPPRESSED DUPLICATES',NL
         FDPRT *END                                              83327
         SPACE 1                                                 83327
MSGCCODE FDCLC CCODE,ZEROES,2,BE=MSGCCODX                        83327
         FDOPT NL,CC=C'-'                                        83327
         FDPRT '***** COMPLETION CODE'                           83327
         FDPRT CCODE,I,PAD                                       83327
         FDPRT '*****'                                           83327
MSGCCODX FDPRT *END                                              83327
         SPACE 2
SAVE     DSECT ,             SAVE AND WORK AREA                  83327
EXITSAVE DS    18F           E15/E35 EXIT SAVE AREA              83327
         SERVDEFS ,          @SERVICE AREA                      GP04068
@SIDLIST DS    A     1/2     ADDRESS OF SYSTEM ID LIST           83327
NUMSID   DS    F     2/2     NUMBER OF SYSTEM IDS IN LIST        83327
DB       DS    D             WORK SPACE                          83327
DB2      DS    D               MORE                              83327
TOTBYTES DS    D             BYTE COUNTER                        83327
ZEROES   DC    F'0'          CONSTANT                            83327
CCODE    DC    Y(0)          PROGRAM RETURN CODE                 83327
VSPREV   DC    X'00'         SEGMENT CONTROL FLAGS               83327
FLAG     DC    X'00'         PROCESSING FLAGS                    83327
FGIOERR  EQU   X'80'           SYNAD ENTERED                     83327
FGNOSID  EQU   X'08'           SYSTEM IDS ARE TO BE EXCLUDED     83327
FGSEOF   EQU   X'20'           SORT EOF                          83327
FGSREC   EQU   X'02'           SORT RECORD AVAILABLE             83327
FGMEOF   EQU   X'10'           MASTIN EOF                        83327
FGMREC   EQU   X'01'           MASTIN RECORD AVAILABLE           83327
INPBUFF  DS    2A
OUTBUFF  DS    A
WORKBUFF DS    A
WORKBEND DS    A             END OF BUFFER                       83327
MASTBUFF DS    A
SEGCNTR  DS    A             CONSOLIDATED SEGMENT COUNT          83327
SEGSKIP  DS    A             NUMBER OF SEGMENTS SKIPPED DUE TO ERRORS
SEGIOERR DS    A             SEGMENTS IN ERROR                   83327
E35DUPE  DS    A             DUPLICATE RECORDS                   83327
OUTMISS  DS    A             RECORDS MISSING                     83327
MAXSKIP  DS    A             SIZE OF LARGEST RECORD SKIPPED      83327
MAXOUT   DS    A             SIZE OF LARGEST RECORD WRITTEN      83327
REFDLO   DS    PL4           LOWEST DATE WRITTEN                 83327
REFDHI   DS    PL4           HIGHEST DATE WRITTEN                83327
OCCSID   DS    64CL4         SYSTEM IDS WRITTEN                  83327
OCCSIDND EQU   *               END OF LIST                       83327
*        THE FOLLOWING COUNTERS ARE SEQUENCE DEPENDENT           83327
E15IN    DC    F'0'     1/6
E15OUT   DC    F'0'     2/6
E15STOT  DC    F'0'     3/6
E15SBAD  DC    F'0'     4/6
E15SCPU  DC    F'0'     5/6
E15SREQ  DC    F'0'     6/6
MININ    DC    F'0'     1/6
MINOUT   DC    F'0'     2/6
MINSTOT  DC    F'0'     3/6
MINSBAD  DC    F'0'     4/6
MINSCPU  DC    F'0'     5/6
MINSREQ  DC    F'0'     6/6
MOUTOUT  DC    F'0'
*        END OF SEQUENCE DEPENDENT CONSTANTS                     83327
         SPACE 1                                                 83327
MFXRDW   DS    CL4                RECORD DESCRIPTOR
MFXACCT  DS    CL8                ACCOUNT NUMBER
MFXRDAT  DS    CL4                READER DATE
MFXRTIM  DS    CL4                READER TIME
MFXJOBN  DS    CL8                JOBNAME
MFXTYP   DS    C                  RECORD TYPE
MFXHDAT  DS    CL4                HEADER DATE
MFXHTIM  DS    CL4                HEADER TIME
MFXCPU   DS    CL4                CPU ID/MODEL
         SPACE 1                                                 83327
E15TIN   DC    257F'0'                                           83327
E15TOUT  DC    257F'0'                                           83327
MINTIN   DC    257F'0'                                           83327
MINTOUT  DC    257F'0'                                           83327
MOUTTOUT DC    257F'0'                                           83327
SAVEEND  EQU   *             LENGTH OF WORK AREA                 83327
         SPACE 2
*        MAP SORT PREFIX (OUTBUFF), AND SMF RECORD (WORKBUFF)
         SPACE 1
PFXMAP   DSECT                    MAP SORT PREFIX
PFXRDW   DS    CL4                RECORD DESCRIPTOR
PFXACCT  DS    CL8                ACCOUNT NUMBER
PFXRDAT  DS    CL4                READER DATE
PFXRTIM  DS    CL4                READER TIME
PFXJOBN  DS    CL8                JOBNAME
PFXTYP   DS    C                  RECORD TYPE
PFXHDAT  DS    CL4                HEADER DATE
PFXHTIM  DS    CL4                HEADER TIME
RDWOUT   DS    0CL4               RDW FOR OUTPUT
PFXCPU   DS    CL4                CPU ID/MODEL
PFXL     EQU   *-PFXMAP           PREFIX LENGTH
PFXOHEAD EQU   (((PFXL+7)/8)*8)     LENGTH FOR GETMAIN OVERHEAD  83327
         SPACE 1
*        THIS PORTION MAPS WORKBUFF - ACTUAL RECORD - RDW
         SPACE 1
SMFRSV   DS    C                  SYSTEM CODE FIELD
SMFTYP   DS    C                  RECORD TYPE
SMFHTIM  DS    CL4                HEADER TIME
SMFHDAT  DS    CL4                HEADER DATE
SMFCPU   DS    CL4                CPU ID/MODEL
SMFJOBN  DS    CL8                JOBNAME
SMFRTIM  DS    CL4                READER TIME
SMFRDAT  DS    CL4                READER DATE
SMFACCT  DS    CL8                ACCOUNT NUMBER
         SPACE 2
         CVT   DSECT=YES                                         94234
         IHACDE ,                                                94234
         DCBD  DSORG=PS,DEVD=TA
         SPACE 1                                                 83327
         IEFUCBOB ,                                              83327
         END
