SMART    TITLE 'S M A R T  ***  SIMPLE MANUAL AND REPORT TRANSCRIBER'
         MACRO
&NAME    KEYVAL &STRING,&TABLE,&PRINT=OFF
         GBLC  &KVRCSEC            CSECT NAME
         LCLC  &CSNAME             OUTER CSECT NAME
         LCLB  &KVRCSON            LOCAL SWITCH FOR CSECT GENERATION
.**********************************************************************
.***                                                                ***
.***  MACRO  NAME: KEYWORD VALIDATION ROUTINE                       ***
.***                                                                ***
.***                                                                ***
.***  GENERAL FUNCTION: TO VALIDATE A CHARACTER STRING CONTAINING   ***
.***                    KEYWORDS AND VALUES AND PERFORM REQUESTED   ***
.***                    OPERATIONS AS SPECIFIED IN THE KEYWORD      ***
.***                    VALIDATION TABLE DEFINED WITH ONE OR MORE   ***
.***                    KEYT MACROS.  THIS MACRO EXPANDS TO AN      ***
.***                    INDEPENDENT CSECT UPON INITIAL INVOCATION   ***
.***                    AND IS ACCESSED VIA V-TYPE ADDRESSING       ***
.***                    FOR SUBSEQUENT INVOCATIONS.  THERE ARE 2    ***
.***                    BASIC PARTS TO THIS MACRO.  THE FIRST PART  ***
.***                    SETS UP THE APPROPRIATE PARMS BASED UPON    ***
.***                    THE USER PARAMETERS SUPPLIED.  THE SECOND   ***
.***                    PART DOES THE STRING MANIPULATION, PERFORMS ***
.***                    THE REQUESTED FUNCTIONS AND SETS RETURN     ***
.***                    CODES.                                      ***
.***                                                                ***
.***                                                                ***
.***  ENTRY REGS:   R1                                              ***
.***  (PART 1)      R2                                              ***
.***                R3                                              ***
.***                R4                                              ***
.***                R5                                              ***
.***                R6                                              ***
.***                R7                                              ***
.***                R8                                              ***
.***                R9                                              ***
.***                R10                                             ***
.***                R11                                             ***
.***                R12                                             ***
.***                R13  SAVE AREA                                  ***
.***                R14                                             ***
.***                R15                                             ***
.***                                                                ***
.***                                                                ***
.***  ENTRY REGS:   R0   AL4  ADDR OF CHAR STRING                   ***
.***  (PART 2)      R1   AL4  ADDRESS OF 1ST KEYT ENTRY             ***
.***                R2                                              ***
.***                R3                                              ***
.***                R4                                              ***
.***                R5                                              ***
.***                R6                                              ***
.***                R7                                              ***
.***                R8                                              ***
.***                R9                                              ***
.***                R10                                             ***
.***                R11                                             ***
.***                R12                                             ***
.***                R13  SAVE AREA                                  ***
.***                R14  RETURN ADDRESS                             ***
.***                R15  ENTRY ADDRESS                              ***
.***                                                                ***
.***  REGISTER USAGE:  R0   WORK                                    ***
.***                   R1   WORK                                    ***
.***                   R2   WORK                                    ***
.***                   R3   CHAR STRING LENGTH                      ***
.***                   R4   CHAR STRING ADDRESS                     ***
.***                   R5   WORK                                    ***
.***                   R6   WORK                                    ***
.***                   R7   WORK                                    ***
.***                   R8   WORK                                    ***
.***                   R9   SAVED RETURN CODE                       ***
.***                   R10  WORK                                    ***
.***                   R11  PROGRAM BASE                            ***
.***                   R12  WORKAREA BASE                           ***
.***                   R13  OLD SAVEAREA                            ***
.***                   R14  KEYWORD TABLE BASE                      ***
.***                   R15  RETURN CODE                             ***
.***                                                                ***
.***                                                                ***
.***                                                                ***
.***                                                                ***
.***  AUTHOR: E STEWART                DATE: 05/29/80               ***
.***                                                                ***
.***                                                                ***
.***  ATTRIBUTES: RENT,REUS,REFR                                    ***
.***                                                                ***
.***                                                                ***
.***  CALLED BY: UTILITY FUNCTION                                   ***
.***                                                                ***
.***                                                                ***
.***  MACROS USED: GETMAIN,FREEMAIN                                 ***
.***                                                                ***
.***                                                                ***
.***  ROUTINES CALLED: NONE                                         ***
.***                                                                ***
.***                                                                ***
.***  NORMAL EXIT: BR R14                                           ***
.***                                                                ***
.***  EXIT REGS:     R0   AL4  ADDR OF KEYWORD IN CHAR STRING WHERE ***
.***                           VALIDATION ERROR OCCURED             ***
.***                 R1   AL4  ADDR OF VALUE IN CHAR STRING WHERE   ***
.***                           VALIDATION ERROR OCCURED             ***
.***                 R15       00  KEYWORD(S)VALUE(S) FOUND AND     ***
.***                               OP(S) PERFORMED                  ***
.***                           04  KEYWORD(S) FOUND, SOME VALUE NOT ***
.***                               FOUND IN KEYT                    ***
.***                           08  A KEYWORD WAS NOT FOUND IN KEYT  ***
.***                           12  'VALCNT' DID NOT PASS VALIDATION ***
.***                           16  MORE INPUT EXPECTED, I.E. ONLY   ***
.***                               BLANKS, COMMENTS, OR ),BLANK WAS ***
.***                               FOUND. OTHERWISE RC=00 WOULD HAVE***
.***                               BEEN RETURNED.                   ***
.***                           20  'TYPE' DID NOT PASS VALIDATION.  ***
.***                           24  'LEN' DID NOT PASS VALIDATION.   ***
.***                                                                ***
.***                                                                ***
.***  ABEND CODES: NONE (I HOPE)                                    ***
.***                                                                ***
.***                                                                ***
.***  MESSAGES ISSUED: NONE                                         ***
.***                                                                ***
.***                                                                ***
.***                                                                ***
.***                                                                ***
.***  DSECTS/MACROS USED: NONE FROM AN EXTERNAL SOURCE              ***
.***                                                                ***
.***                                                                ***
.***  SPECIAL NOTES:                                                ***
.***                                                                ***
.***                                                                ***
.***                                                                ***
.**********************************************************************
         AIF   ('&KVRCSEC' NE '').KVRBALR CSECT ALREADY ASSIGNED
&KVRCSEC SETC  'KVR'.'&SYSNDX'   SET CSECT NAME
&KVRCSON SETB  1                   SAY CSECT ASSIGNED THIS PASS
.KVRBALR ANOP
         AIF   (N'&SYSLIST(1) EQ  1 AND N'&SYSLIST(2) EQ 1).KVRM010
         MNOTE 8,'OPERAND SUBLISTS NOT PERMITTED'
         MEXIT
.KVRM010 ANOP
         AIF   ('&NAME' EQ '').KVRM014
&NAME    DS    0H                  SET LABEL NAME
.KVRM014 ANOP
         AIF   ('&SYSLIST(1)' EQ '(0)' OR '&SYSLIST(1)' EQ '(R0)').KVRMX
               040           STRING AND LENGTH ALREADY OK
         AIF   ('&SYSLIST(1)' EQ '(0)' OR '&SYSLIST(2)' EQ '(R0)').KVRMX
               020                CHAR STRING ALREADY IN R0
         AIF   ('&STRING'(1,1) EQ '(').KVRM016 REG NOTATION
         LA    0,&STRING           GET ADDR OF CHAR STRING
         AGO   .KVRM020
.KVRM016 ANOP
         LR    0,&STRING           GET ADDR OF CHAR STRING
.KVRM020 ANOP
         AGO   .KVRM040
.KVRM040 ANOP
         AIF   ('&SYSLIST(2)' EQ '(1)' OR '&SYSLIST(2)' EQ '(R1)').KVRMX
               060
         AIF   ('&TABLE'(1,1) EQ '(').KVRM050 REG NOTATION
         LA    1,&TABLE            KEYWORD TABLE ADDRESS
         AGO   .KVRM060
.KVRM050 ANOP
         LR    1,&TABLE            KEYWORD TABLE ADDRESS
.KVRM060 ANOP
         L     15,=V(&KVRCSEC)     GET CSECT NAME
         BALR  14,15               GO TO VALIDATION ROUTINE
         AIF   (NOT(&KVRCSON)).NOGO TERMINATE IF ALREADY EXPANDED
         PUSH  USING,PRINT
         AIF   ('&PRINT' EQ 'ON').KVRM070 NO LIST
         PRINT OFF                 TURN PRINT OFF
         AGO   .KVRM072
.KVRM070 ANOP
         PRINT ON,GEN              TURN ON PRINT
.KVRM072 ANOP
&CSNAME  SETC  '&SYSECT'
&KVRCSEC CSECT
         USING *,11                SET UP BASE
         STM   14,12,12(13)        SAVE REGS
         LR    11,15               GET BASE ADDRESS
         USING KVDSVR,14           TABLE ADDRESS BASE
         USING KVWAVR,12           WORKAREA BASE
         GETMAIN R,LV=KVWALNTH     GET WORKAREA
         LR    12,1                PUT INTO BASE REG
         L     4,20(13)            GET STRING ADDRESS INTO REG 4
         SR    3,3                 CLEAR WORK REG
         ICM   3,3,0(4)            GET STRING LENGTH
         LA    4,2(,4)             BUMP OVER STRING LENGTH
         MVI   KVWAFL1,0           CLEAR FLAG BYTE
         MVI   KVWAKWVA,0          CLEAR COUNT BYTE
         MVC   KVWASAV1(4*13),20(13) SAVE REGS 0-12
         MVC   KVWASAV1+(4*14)(4*2),12(13) SAVE REGS 14-15
         ST    13,KVWASAV1+(4*13)  SAVE REG 13 FROM CALLER
KVR004   DS    0H
         L     14,24(13)           TABLE ADDRESS
         XC    0(KVWACLR,12),0(12)   CLEAR WORKAREA
         LTR   3,3                 Q. ANY DATA TO SCAN
         BP    KVR005              A. YES, CONTINUE AS NORMAL
         LA    9,16                SET BAD RC
         B     KVR900              AND RETURN
KVR005   DS    0H
         SR    2,2                 CLEAR FOR LATER
         ST    4,KVWALOC           SAVE STRING LOC
         ST    4,KVWAKEY           SAVE STRING LOC
         BCTR  3,0                 REDUCE BY ONE
         EX    3,KVTR1VR           SEARCH FOR NON BLANK
         LA    3,1(,3)             GET ORIG LENGTH
         CLM   2,1,KVAX00          Q. ANYTHING FOUND
         BNE   KVR006              A. YES, SEE WHAT IT IS
         LA    9,16                SET BLANKS RETURN CODE
         B     KVR900              RETURN TO CALLER
KVR006   DS    0H
         LR    5,1                 SAVE REG 1
         SR    1,4                 GET NEW
         SR    3,1                 DATA LENGTH
         LR    4,5                 GET NEW LOCATION
         CLM   2,1,KVASLASH        Q. COULD THIS BE COMMENT
         BNE   KVR020              A. NO, CONTINUE
         CLI   1(4),C'*'           Q .WAS THIS A COMMENT
         BE    KVR008              A. YES, CONTINUE
         LA    9,8                 SAY INVALID KEYWORD
         B     KVR900              AND RETURN
KVR008   DS    0H                  VALIDATE COMMENT
         LA    4,2(4)              BUMP OVER /*
         BCTR  3,0                 REDUCE COUNT
         BCTR  3,0                 REDUCE COUNT
KVR010   DS    0H
         CLI   0(4),C'*'           Q. IS IT REALLY A COMMENT
         BE    KVR014              A. YES, CONTINUE
KVR012   DS    0H
         LA    4,1(4)              BUMP ADDRESS
         BCT   3,KVR010            LOOP THROUGH
         LA    9,8                 SET RETURN CODE
         B     KVR900              AND RETURN
KVR014   DS 0H
         CLI   1(4),C'/'           Q. IS 2ND CHAR A SLASH
         BNE   KVR012              A. NO, CONTINUE SCAN FOR END
         LA    4,2(4)              BUMP OVER */
         BCTR  3,0                 REDUCE COUNT
         BCTR  3,0                 REDUCE COUNT
         B     KVR005              AND CONTINUE SCAN
KVR020   DS    0H                  VALIDATE CHARACTERS FOUND
         ST    4,KVWALOC           SAVE STRING ADDR
         ST    4,KVWAKEY           SAVE STRING ADDR
         SR    5,5                 CLEAR FOR KEYWORD COMPARE
         SR    6,6                 CLEAR FOR KEYWORD COMPARE
         SR    7,7                 CLEAR FOR KEYWORD COMPARE
KVR022   DS    0H
         IC    5,KVLL0001          GET KEYWORD LEN
         EX    5,KVCVR             Q. KEYWORD FOUND
         BE    KVR080              A. YES,CONTINUE
KVR023   DS    0H
         IC    6,KVVN0001          GET COUNT OF VALUE ENTRIES
         LA    14,0(14,5)          BUMP OVER VARIABLE KEYWORD
         LTR   6,6                 Q. ANY MORE VALUES TO DO
         BZ    KVR028              A. NO, CONTINUE AS IS
KVR024   DS    0H
         IC    7,KVVL0001          LENGTH OF VALUE KEYWORD
         LA    14,KVVG0001(7,14)   BUMP OVER VALUE ENTRY
         BCT   6,KVR024            CONTINUE THRU ALL VALUES
KVR028   DS    0H
         LA    14,KVKL0001+1(14)   BUMP OVER KEYWORD ENTRY
         CLC   KVAFFFF,KVDSVR      Q. LAST ENTRY
         BNE   KVR022              A. NO, CONTINUE SCAN
         LA    9,8                 SET RETURN CODE
         B     KVR900              AND RETURN
KVR080   DS    0H                  KEYWORD FOUND
         LA    6,1(5,4)            GO TO NEXT BYTE BEYOND KEYWORD
         CLI   0(6),C'('           Q. IS THIS DELIMITER
         BE    KVR082              A. YES, CONTINUE
         CLI   0(6),C' '           Q. IS THIS DELIMITER
         BE    KVR082              A. YES, CONTINUE
         CLI   0(6),C','           Q. IS THIS DELIMITER
         BE    KVR082              A. YES, CONTINUE
         LR    9,3                 CURRENT LENGTH
         SR    9,5                 MINUS KEYWORD EX LENGTH
         BCTR  9,0                 MINUS ONE
         LTR   9,9                 Q. ANY DATA LEFT IN STRING
         BZ    KVR082              A. NO, JUST CONTINUE NORMALLY
         SR    6,6                 CLEAR FOR LATER
         B     KVR023              ELSE LOOK FOR ANOTHER KEYWORD
KVR082   DS    0H
         SR    6,6                 CLEAR WORK REG
         ICM   6,3,KVRA0001        GET EXIT ROUTINE ADDR
         BZ    KVR084              BRANCH IF NOT SPECIFIED
         SRDL  6,12                GET REG INTO R6
         SLL   6,2                 X 4
         LA    1,KVWASAV1          ADDR OF SAVE AREA
         L     6,0(6,1)            GET CONTENTS OF RTN BASE REG
         SRL   7,20                SHIFT TO LOWER END
         LA    7,0(7,6)            GET ROUTINE ADDRESS
         ST    7,KVWARTN           SAVE RTN ADDRESS
KVR084   DS    0H
         SR    6,6                 CLEAR WORK REG
         ICM   6,3,KVRP0001        GET EXIT ROUTINE PARM
         BZ    KVR088              BRANCH IF NOT SPECIFIED
         SRDL  6,12                GET REG INTO R6
         SLL   6,2                 X 4
         LA    1,KVWASAV1          ADDR OF SAVE AREA
         L     6,0(6,1)            GET CONTENTS OF PARM BASE REG
         SRL   7,20                SHIFT TO LOWER END
         LA    7,0(7,6)            GET PARM ADDRESS
         ST    7,KVWAPRM           SAVE PARM ADDRESS
KVR088   DS    0H
         LA    4,1(5,4)            GO TO NEXT BYTE BEYOND KEYWORD
         SR    3,5                 REDUCE STRING COUNT
         BCTR  3,0                 REDUCE STRING COUNT
         CLI   0(4),C'('           Q. VALUE(S) SPECIFIED
         BE    KVR110              A. YES, CONTINUE
         CLI   KVVN0001,0          Q. ANY VALUES REQUIRED
         BNE   KVR090              A. NO, JUST RETURN
         SR    9,9                 SET ZERO RETURN CODE
         B     KVR900              AND RETURN
KVR090   DS    0H
         MVI   KVWAKWVA,0          CLEAR COUNT
         B     KVR172              GO PROCESS THIS KEYWORD
KVR110   DS    0H                  MOVE ALL VALUES TO LOCAL STORAGE
         SR    1,1                 CLEAR FOR LATER
         MVI   KVWAKWVA,0          CLEAR COUNT
         BCTR  3,0                 REDUCE COUNT
         LA    4,1(4)              BUMP TO NEXT BYTE
         CLI   0(4),C')'           Q. NULL KEYWORDS
         BNE   KVR120              A. NO, CONTINUE NORMALLY
         B     KVR140              GO PROCESS NULL ENTRY
KVR120   DS    0H
         SR    1,1                 CLEAR FOR LATER
         CLI   0(4),C''''          Q. QUOTED STRING FOR THIS VALUE
         BNE   KVR124              A. NO, DONT SET FLAG
         MVI   KVWAQUO,1           SAY IN QUOTE MODE
         LA    4,1(,4)             BUMP OVER QUOTE
         BCTR  3,0                 REDUCE STRING COUNT
KVR124   DS    0H
         LA    2,KVWATMP           ADDR OF TEMP WORK AREA
         LR    5,4                 SAVE OLD VALUES LOCATION
         ST    5,KVWALOC           SAVE STRING ADDR
KVR130   DS    0H
         CLI   0(4),C','           Q. COMMA DELIMITER
         BE    KVR140              A. YES, SAVE THIS KEYWORD
         CLI   0(4),C')'           Q. TERMINATING DELIMITER
         BE    KVR140              A. YES, PROCESS END KEYWORD VALUES
         CLI   0(4),C''''          Q. END OF QUOTED STRING
         BNE   KVR134              A. NO, CONTINUE
         LA    4,1(,4)             BUMP ADDRESS
         BCTR  3,0                 REDUCE COUNT
         LA    1,1(,1)             ADD 1 FOR LATER STRING MOVE
         CLI   0(4),C''''          Q. REALLY END OF STRING
         BE    KVR134              A. NO, NOT YET JUST DOUBLE QUOTES
         MVI   KVWAQUO,0           RESET QUOTE MODE FLAG
         B     KVR130              AND CONTINUE
KVR134   DS    0H
         MVC   0(1,2),0(4)         SAVE THIS BYTE
         LA    2,1(,2)             BUMP WORK BYTE
         LA    4,1(4)              GO TO NEXT BYTE
         BCT   3,KVR130            CONTINUE SCAN
         LA    9,4                 SET RETURN CODE
         B     KVR900              AND RETURN
KVR140   DS    0H                  PROCESS FOUND VALUE
         CLI   KVWAQUO,0           Q. ARE WE IN QUOTE MODE
         BNE   KVR134              A. YES, IGNORE OTHER DELIMITERS
         LR    6,4                 SAVE NEW ADDR
         SR    6,5                 GET VALUE LENGTH
         SR    6,1                 SUBTRACT ANY QUOTED STRING BYTES
         SR    5,5                 CLEAR WORK REG
         ICM   5,1,KVWAKWVA        GET WORK AREA COUNT
         BNZ   KVR146              BRANCH IF ALREADY SOME IN THERE
         LA    8,KVWAKWVA+1        GET NEXT LOC ADDRESS
         B     KVR148              AND CONTINUE
KVR146   DS    0H                  LOOK FOR VALUES END LOCATION
         LA    8,KVWAKWVA+1        GET START ADDRESS
         SR    7,7                 CLEAR WORK REG
         ICM   7,1,KVWAKWVA+1      GET LENGTH OF LAST ONE
KVR147   DS    0H
         LA    8,1(7,8)            GO TO NEXT SLOT
         ICM   7,1,0(8)            GET NEXT LENGTH
         BCT   5,KVR147            LOOP THRU
KVR148   DS    0H                  MOVE NEW VALUE IN NOW
         LTR   6,6                 Q. NULL VALUE SPECIFIED
         BNZ   KVR150              A. NO, CONTINUE MVC
         STC   6,0(8)              SAVE NULL VALUE LENGTH
         B     KVR160              AND UPDATE VALUES COUNT
KVR150   DS    0H                  MOVE NEW VALUES INTO WORKAREA
         L     5,KVWALOC           RESTORE STRING ADDR
         STC   6,0(8)              SAVE STRING LENGTH
         BCTR  6,0                 REDUCE COUNT
         EX    6,KVM1VR            PERFORM THE MOVE
         TM    KVVT0001,X'FF'      Q. ANY VALIDATION REQUIRED
         BZ    KVR160              A. NO, JUST CONTINUE
         SR    2,2                 CLEAR WORK REG
         ICM   2,1,KVVT0001        GET VALIDATION CODE
         SLL   2,2                 X 4
         B     *+0(2)              GO TO VALIDATE IT
         B     KVR154              ALPHA
         B     KVR156              NUMERIC
         B     KVR158              ALPH/NUMERIC
KVR154   DS    0H                  ALPHA VALIDATION
         EX    6,KVTR2VR           A. ALPHA FIELD
         BZ    KVR160              A. YES, CONTINUE
KVR155   DS    0H                  ERROR RETURN
         LA    9,20                SET RC
         B     KVR900              AND RETURN
KVR156   DS    0H                  NUMERIC
         EX    6,KVTR3VR           Q. NUMERIC FIELD
         BZ    KVR160              A. YES, CONTINUE
         B     KVR155              ELSE SET BAD RC
KVR158   DS    0H                  ALPHA/NUMERIC
         EX    6,KVTR4VR           Q. ALPHA/NUMERIC
         BZ    KVR160              A. YES, CONTINUE
         B     KVR155              ELSE ERROR RETURN
KVR160   DS    0H
         CLI   KVVLL001,0          Q. WAS VALUE LEN VALIDATION REQUSTED
         BE    KVR169              A. NO, BYPASS LENGTH VALIDATION
         LA    6,1(,6)             RESTORE FULL LENGTH
         CLM   6,1,KVVLL001        Q. WAS LOW VALUE PRECEEDED
         BL    KVR168              A. YES, ERROR CONDITION
         CLM   6,1,KVVLH001        Q. WAS HIGH VALUE EXCEEDED
         BH    KVR168              A. YES, ERROR CONDITION
         B     KVR169              VALUE LENGTH PASSED VALIDATION
KVR168   DS    0H
         LA    9,24                SET VALUE ERROR
         B     KVR900              AND RETURN
KVR169   DS    0H
         IC    7,KVWAKWVA          GET CURRENT COUNT
         LA    7,1(,7)             ADD ONE
         STC   7,KVWAKWVA          AND SAVE THE COUNT
         CLI   0(4),C')'           Q. WAS IT ENDING DELIMETER
         BE    KVR170              A. YES, TERMINATE SCAN
         LA    4,1(,4)             BUMP OVER COMMA
         BCTR  3,0                 REDUCE STRING LENGTH
         B     KVR120              CONTINUE FOR MORE KEYWORDS
KVR170   DS    0H                  PROCESS ENDING DELIMITER
         LA    4,1(,4)             BUMP OVER )
         BCTR  3,0                 REDUCE COUNT
KVR172   DS    0H                  PROCESS ENDING DELIMITER
         SR    5,5                 CLEAR WORK REG
         SR    6,6                 CLEAR WORK REG
         ICM   5,1,KVHV0001        GET VALUE(S) COUNT HIGH NUMBER
         BNZ   KVR180              GO HERE IF NOT ZERO COUNT
         CLM   5,1,KVWAKWVA        Q. WAS COUNT FOUND ALSO ZERO
         BE    KVR180              A. YES, CONTINUE WITHOUT ERROR YET
KVR174   DS    0H
         LA    9,12                SAY COUNT EXCEEDED
         B     KVR900              AND RETURN
KVR180   DS    0H
         CLM   5,1,KVWAKWVA        Q. HIGH COUNT EXCEEDED
         BL    KVR174              A. YES, ERROR AND GET OUT
         ICM   5,1,KVLV0001        GET LOW VALUE(S) COUNT
         BZ    KVR182              GET OUT IF ZERO COUNT
         CLM   5,1,KVWAKWVA        Q. COUNT LESS THAN LOW VALUE
         BH    KVR174              A. YES, GET OUT WITH ERROR
KVR182   DS    0H
         ICM   5,3,KVAA0001        Q. IS THERE AN AREA PROVIDED
         BZ    KVR200              A. NO, JUST CONTINUE
         SR    6,6                 CLEAR WORK REG
         SR    7,7                 CLEAR WORK REG
         ICM   6,3,KVAA0001        GET WORKAREA ADDRESS
         SRDL  6,12                GET REG NO. INTO R2
         SLL   6,2                 X 4
         LA    1,KVWASAV1          ADDR OF SAVE AREA
         L     6,0(6,1)            GET CONTENTS OF AREA BASE REG
         SRL   7,20                SHIFT TO LOWER END
         LA    6,0(7,6)            GET WORKAREA ADDRESS
         LA    5,KVWAKWVA          GET MY AREA ADDRESS
         SR    2,2                 CLEAR WORK REG
         IC    2,KVWAKWVA          GET COUNT
         MVC   0(1,6),KVWAKWVA     MOVE COUNT BYTE
         LA    6,1(,6)             BUMP OVER COUNT BYTE
         LA    5,1(,5)             BUMP OVER COUNT BYTE
KVR184   DS    0H
         MVC   0(1,6),0(5)         MOVE FIRST LENGTH BYTE
         LTR   2,2                 WAS ANYTHING IN THE AREA
         BNZ   KVR186              YES, CONTINUE WITH MOVE
         MVI   KVWAKWVA,1          SAY SOMETHING WAS THERE
         B     KVR200              AND CONTINUE
KVR186   DS    0H
         SR    7,7                 CLEAR FOR NEXT INSTR
         ICM   7,1,0(5)            GET BYTE COUNT
         BNZ   KVR188              BRANCH FOR NON ZERO MOVE
         LA    6,1(,6)             BUMP OVER NULL BYTE
         LA    5,1(,5)             BUMP OVER NULL BYTE
         B     KVR194              AND CONTINUE
KVR188   DS    0H
         BCTR  7,0                 REDUCE BY ONE
         EX    7,KVM2VR            MOVE DATA OVER
KVR190   DS    0H
         LA    6,2(7,6)            BUMP OVER OUTPUT
         LA    5,2(7,5)            BUMP OVER INPUT
KVR194   DS    0H
         BCT   2,KVR184            COUNTINUE MOVE
KVR200   DS    0H                  SEE IF VALUES TO BE COMPARED
         SPACE
*        AT THIS POINT THE VALUES HAVE BEEN MOVED TO MY WORKAREA
*        AND TO THE USER WORK AREA IF AREA HAS BEEN SPECIFIED
*        IN THE KEYT MACRO
         SPACE
         SR    2,2                 CLEAR WORK REG
         ICM   2,1,KVVN0001        Q. WERE ANY VALUES TO BE VERIFIED
*                                     OR WAS THERE WORK TO BE DONE
         BNZ   KVR220              A. YES, CONTINUE AS USUAL
         SR    9,9                 CLEAR RC
         B     KVR900              AND RETURN TO CALLER
KVR220   DS    0H
         ST    2,KVVNCNT           SAVE COUNT
         ST    14,KVWAR14          SAVE KEYT ADDR
         LA    5,KVWAKWVA+1        ADDR OF FOUND VALUES
         ST    5,KVWASV            SAVE VALUES ADDR
         MVC   KVWASVC,KVWAKWVA    SAVE VALUES COUNT
         SR    2,2                 CLEAR REG
         ICM   2,1,KVWASVC         Q. ANY VALUES FOUND
         BNZ   KVR230              A. YES, CONTINUE WITH COUNT
         MVI   KVWASVC,1           ELSE SAY AT LEAST ONE FOUND
KVR230   DS    0H
         L     2,KVVNCNT           GET COUNT
         L     14,KVWAR14          GET TABLE ADDR
         L     5,KVWASV            GET ADDR OF CURRENT VALUE TO FIND
         SR    1,1                 CLEAR WORK REG
         ICM   1,1,KVWASVC         GET COUNT
         BZ    KVR500              STOP SCAN IF NONE LEFT
         BCTR  1,0                 REDUCE COUNT
         STC   1,KVWASVC           SAVE NEW COUNT
         ICM   1,1,0(5)            GET LENGTH OF CURRENT VALUE
         LA    1,1(1,5)            BUMP TO WHERE NEW VALUE WILL BE
         ST    1,KVWASV            AND SAVE ADDRESS
         SR    7,7                 CLEAR FOR LATER
         IC    7,KVLL0001          GET KEYWORD LENGTH
         LA    14,0(14,7)          BUMP OVER VARIABLE LENGTH KEYWORD
KVR240   DS    0H                  SCAN TABLE FOR THIS VALUE
         ST    2,KVWAR2            SAVE CURRENT COUNT
         ICM   7,1,KVVL0001        LENGTH OF VALUE
         BZ    KVR300              IF NO KEYWORD VALUE HAS BEEN PUT
*                                  IN KEYT DONT LOOK FOR A MATCH, JUST
*                                  DO THE WORK IF ANY
         BCTR  7,0                 REDUCE FOR EX INSTR
         EX    7,KVC2VR            Q. ARE VALUES EQUAL
         BE    KVR300              A. YES, GO SEE IF ANY WORK TO DO
KVR250   DS    0H
         L     2,KVWAR2            RELOAD LOOP COUNTER
         SR    1,1                 CLEAR WORK REG
         IC    1,KVVL0001          GET LENGTH OF VALUE
         LA    14,KVVG0001(1,14)   GO TO NEXT ENTRY
         BCT   2,KVR240            AND CONTINUE SCAN
         CLI   KVWAVF,X'FF'        Q. WERE VALUE(S) MATCHED IN KEYT
         BE    KVR260              A. YES, CONTINUE WITH NO ERRORS
         LA    9,4                 VALUE NOT FOUND RC
         B     KVR900              AND RETURN
KVR260   DS    0H
         MVI   KVWAVF,0            RESET VALUES FOUND FLAG
         B     KVR230              AND CONTINUE WITH OTHER VALUES
KVR300   DS    0H                  VALUE FOUND SEE IF ANY WORK TO DO
         MVI   KVWAVF,X'FF'        SAY VALUES FOUND
         TM    KVOP0001,X'FF'      Q. ANY OP CODES SPECIFIED
         BNZ   KVR320              A. YES, SEE IF SOME WORK TO DO
         B     KVR250              ELSE CONTINUE VALUES SCAN
KVR320   DS    0H                  DECODE OP CODES AND DO THEM
         LA    6,KVVV0001          GET ADDR OF VALUES ENTRY
         LA    1,KVWAFLN           ADDR OF SAVE TABLE
         ICM   7,15,KVWACN1        GET CURRENT COUNT OF VALUES ALREADY
*                                  PROCESSED
         BZ    KVR326              ADD THIS ONE IF NONE YET
KVR322   DS    0H
         C     6,0(1)              Q. ALREADY PROCESSED THIS ENTRY
         BE    KVR250              A. YES BYPASS FURTHER PROCESSING
         LA    1,4(,1)             BUMP TABLE ADDRESS
         BCT   7,KVR322            CONTINUE SCAN
KVR326   DS    0H
         ST    6,0(1)              SAVE ADDRESS OF THIS ENTRY FOR LATER
*                                  COMPARE OF OTHER VALUES
         L     7,KVWACN1           GET OLD COUNT
         LA    7,1(,7)             ADD 1 TO IT
         ST    7,KVWACN1           AND SAVE IT
         SR    6,6                 CLEAR WORK REG
         SR    7,7                 CLEAR WORK REG
         ICM   6,3,KVTO0001        GET TO ADDRESS
         SRDL  6,12                GET REG NO. INTO R2
         SLL   6,2                 X 4
         LA    1,KVWASAV1          ADDR OF SAVE AREA
         L     6,0(6,1)            GET CONTENTS OF TO BASE REG
         SRL   7,20                SHIFT TO LOWER END
         LA    2,0(7,6)            GET TO ADDREESS
         SR    6,6                 CLEAR WORK REG
         SR    7,7                 CLEAR WORK REG
         ICM   6,3,KVFR0001        GET FROM ADDRESS
         SRDL  6,12                GET REG NO. INTO R6
         SLL   6,2                 X 4
         LTR   6,6                 Q. WAS THIS SELF DEFINING TERM
         BZ    KVR330              A. YES, DONT LOAD NONEXISTENT BASE
         LA    1,KVWASAV1          ADDR OF SAVE AREA
         L     6,0(6,1)            GET CONTENTS OF FROM BASE REG
KVR330   DS    0H
         SRL   7,20                SHIFT TO LOWER END
         LA    8,0(7,6)            GET FROM ADDRESS
         SR    1,1                 CLEAR WORK REG
         ICM   1,3,KVOL0001        GET LENGTH OF FIELD
         BCTR  1,0                 GET EX LENGTH
         SR    7,7                 CLEAR WORK REG
         IC    7,KVOP0001          GET OP CODE
         SLL   7,2                 MULTIPLY BY 4
         B     *+0(7)              BRANCH TO OP CODE ROUTINE
         B     KVR360              N OP CODE SPECIFIED
         B     KVR380              O OP CODE SPECIFIED
         B     KVR400              M OP CODE SPECIFIED
         B     KVR420              X OP CODE SPECIFIED
KVR360   DS    0H                  N OP CODE
         LTR   6,6                 Q. WAS FROM FIELD SELF DEFINING
         BNZ   KVR366              A. NO, CONTINUE STOR-STOR OPERATION
         EX    8,KVOPNI            ISSUE THE AND OP CODE STOR-IMED
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR366   DS    0H                  N OP CODE
         EX    1,KVOPN             ISSUE THE AND OP CODE STOR-STOR
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR380   DS    0H                  O OP CODE
         LTR   6,6                 Q. WAS FROM FIELD SELF DEFINING
         BNZ   KVR386              A. NO, CONTINUE STOR-STOR OPERATION
         EX    8,KVOPOI            ISSUE THE OR OP CODE STOR-IMED
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR386   DS    0H                  N OP CODE
         EX    1,KVOPO             ISSUE THE OR OP CODE STOR-STOR
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR400   DS    0H                  M OP CODE
         LTR   6,6                 Q. WAS FROM FIELD SELF DEFINING
         BNZ   KVR406              A. NO, CONTINUE STOR-STOR OPERATION
         EX    8,KVOPMI            ISSUE THE MVC OP CODE STOR-IMED
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR406   DS    0H                  N OP CODE
         EX    1,KVOPM             ISSUE THE MVC OP CODE STOR-STOR
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR420   DS    0H                  N OP CODE
         LTR   6,6                 Q. WAS FROM FIELD SELF DEFINING
         BNZ   KVR426              A. NO, CONTINUE STOR-STOR OPERATION
         EX    8,KVOPXI            ISSUE THE XI OP CODE STOR-IMED
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR426   DS    0H                  N OP CODE
         EX    1,KVOPX             ISSUE THE XC OP CODE STOR-STOR
         B     KVR250              AND CONTINUE WITH OTHER VALUES
KVR500   DS    0H                  ALL VALUES SCANNED
         SR    9,9                 CLEAR RC
         B     KVR900              AND RETURN
KVR900   DS    0H                  RETURN POINT
         LTR   9,9                 Q . RC=00
         BNZ   KVR930              A. NO, CONTINUE RETURN TO CALLER
         ICM   1,15,KVWARTN        Q. RTN TO CALL
         BZ    KVR910              A. NO, CONTINUE NO CALL
         ST    14,KVWAR14          SAVE REG 14
         LR    15,1                GET RTN ADDR
         LA    0,KVWAKWVA          GET STRING ADDR
         ST    13,KVWASAV+4        SAVE CALLERS SAVE AREA ADDR
         LA    13,KVWASAV          MY SAVE AREA
         L     1,KVWAPRM           GET PARM DATA ADDR
         BALR  14,15               CALL USERS ROUTINE
         L     14,KVWAR14          RESTORE REG 14
         L     13,KVWASAV+4        RESTORE REG 13
KVR910   DS    0H
         MVI   KVWAFL1,1           SAY AT LEAST 1 GOOD FIND
         B     KVR004              GO DO MORE NOW
KVR930   DS    0H
         L     4,KVWALOC           SAVE STRING LOCATION
         L     5,KVWAKEY           SAVE KEYWORD LOCATION
         CLI   KVWAFL1,0           Q. ANY GOOD FINDS
         BE    KVR950              A. NO, LEAVE RC AS IS
         C     9,KVASIX            Q. WAS RC=16
         BNE   KVR950              A. NO, LEAVE RC AS IS
         SR    9,9                 SET ZERO RC
KVR950   DS    0H
         LR    1,12                GET WORKAREA ADDRESS
         LA    0,KVWALNTH          GET WORKAREA LENGTH
         FREEMAIN R,LV=(0),A=(1)   FREE WORKAREA
         LR    15,9                SETUP RETURN CODE
         LM    6,12,11*4(13)       RESTORE REGS
         LR    0,5                 PUT IN KEYWORD LOCATION
         LR    1,4                 PUT IN STRING LOCATION
         LM    2,5,7*4(13)         RESTORE OTHER REGS
         L     14,3*4(13)          RESTORE RETURN REG
         BR    14                  RETURN TO CALLER
         SPACE 4
KVOPN    NC    0(0,2),0(8)
KVOPNI   NI    0(2),0
KVOPO    OC    0(0,2),0(8)
KVOPOI   OI    0(2),0
KVOPM    MVC   0(0,2),0(8)
KVOPMI   MVI   0(2),0
KVOPX    XC    0(0,2),0(8)
KVOPXI   XI    0(2),0
KVCVR    CLC   KVKV0001(0),0(4)
KVTR1VR  TRT   0(0,4),KTVRT1-X'40'
KVTR2VR  TRT   1(0,8),KTVRT3-X'40' ALPHA TEST
KVTR3VR  TRT   1(0,8),KTVRT2-X'40' NUMERIC TEST
KVTR4VR  TRT   1(0,8),KTVRT4-X'40' ALPHA/NUMERIC TEST
KVC2VR   CLC   KVVV0001(0),1(5)
KVM1VR   MVC   1(0,8),KVWATMP
KVM2VR   MVC   1(0,6),1(5)
KTVRT1   DC    10X'00',C'¢.<(+|&&',9X'00',C'!$*);¬-/',10X'00',C'%_>?'
         DC    9X'00',C''':#@ ="',X'00',C'ABCDEFGHI',7X'00'
         DC    C'JKLMNOPQR',8X'00',C'STUVWXYZ',23X'00',C'ABCDEFGHI'
         DC    7X'FF'
         DC    C'ABCDEFGHI',7X'00',C'JKLMNOPQR',8X'00',C'STUVWXYZ'
         DC    6X'00',C'0123456789'
KTVRT2   DC    176X'FF',10X'00'
KTVRT3   DC    129X'FF',41X'00',16X'FF'
KTVRT4   DC    11X'FF',X'00',15X'FF',2X'00',30X'FF',2X'00'
         DC    68X'FF',63X'00'
KVAKVVG  DC    Y(KVVG0001)
KVAFFFF  DC    X'FFFF'
KVASIX   DC    F'16'
KVAX00   DC    X'00'
KVASLASH DC    C'/'
KVDSVR   DSECT
KVLL0001 DS    AL1                 LENGTH OF KEY-1
KVRA0001 DS    SL2                 RTN ADDRESS
KVRP0001 DS    SL2                 RTN PARM
KVAA0001 DS    SL2                 AREA ADDRESS
KVVLL001 DS    AL1                 VALUE LENGTH LOW
KVVLH001 DS    AL1                 VALUE LENGTH HIGH
KVLV0001 DS    AL1                 VALUE COUNT LOW
KVHV0001 DS    AL1                 VALUE COUNT HIGH
KVVT0001 DS    XL1                 VALUE TYPE
KVTA0001 EQU   X'01'               ALPHA TYPE
KVTN0001 EQU   X'02'               NUMERIC TYPE
KVTB0001 EQU   X'03'               ALPHA OR NUMERIC TYPE
KVVN0001 DS    AL1                 COUNT OF VALUE ENTRIES
KVKL0001 EQU   *-KVLL0001          LENGTH OF TABLE ENTRY MINUS KEYWORD
KVKV0001 DS    C                   KEYWORD
KVVL0001 DS    AL1                 VALUE LENGTH
KVOP0001 DS    XL1                 OP CODE
KVVA0001 EQU   X'01'               N  OP CODE
KVVO0001 EQU   X'02'               O  OP CODE
KVVM0001 EQU   X'03'               M  OP CODE
KVVX0001 EQU   X'04'               X  OP CODE
KVTO0001 DS    SL2                 TO ADDRESS
KVFR0001 DS    SL2                 FROM ADDRESS
KVOL0001 DS    AL2                 LENGTH OF TO FIELD
KVVG0001 EQU   *-KVVL0001          ENTRY LENGTH MINUS VALUE CONSTANT
KVVV0001 DS    C                   VALUE CONSTANT
KVLA0001 EQU   *,2                 LAST ENTRY =X'FFFF'
KVWAVR   DSECT
KVWAVF   DS    X                   VALUES FOUND FLAG
KVWAQUO  DS    X                   QUOTED STRING MODE FLAG
KVWASV   DS    F                   ADDR OF VALUES FOUND
KVWASAV  DS    18F                 SAVE AREA TO CALL RTN
KVWARTN  DS    F                   ADDRESS OF RTN TO CALL
KVWAPRM  DS    F                   ADDRESS OF RTN PARM
KVWALOC  DS    F                   LOCATION OF INPUT STRING
KVWAR14  DS    F                   SAVE AREA FOR REG 14
KVWAR2   DS    F                   SAVE AREA FOR REG 2
KVVNCNT  DS    F                   VALUE COUNT SAVEAREA
KVWAKEY  DS    F                   ADDRESS OF CURRENT KEYWORD IN STRING
KVWACN1  DS    F                   COUNT OF VALUE ADDRS IN KVWAFLN
KVWASVC  DS    X                   COUNT OF VALUES FOUND
KVWACLR  EQU   *-KVWAVR            LENGTH OF WORKAREA TO CLEAR
KVWAFL1  DS    X                   FLAG TO SHOW SOME GOOD WORDS FOUND
KVWASAV1 DS    16F                 SAVE AREA FOR CALLERS REGS
KVWAKWVA DS    512X                WORKAREA FOR VALUE MOVE
KVWAFLN  DS    15XL4               SAVEAREA FOR MAX COUNT OF FIELDN'S
KVWATMP  DS    256X                WORKAREA FOR TEMP VALUE MOVE
KVWALNTH EQU   *-KVWAVR            LENGTH OF WORKAREA TO ALLOC
&CSNAME  CSECT
         POP   USING,PRINT
.NOGO    ANOP
         MEND
         SPACE 2
         MACRO
&NAME    KEYT  &KEYWORD,&RTN,&AREA,&TYPE,&VALCNT,&FIELD1=,&FIELD2=,    X
               &FIELD3=,&FIELD4=,&FIELD5=,&FIELD6=,&FIELD7=,           X
               &FIELD8=,&FIELD9=,&FIELD10=,&FIELD11=,&FIELD12=,        X
               &FIELD13,&FIELD14=,&FIELD15=,&END=,                     X
               &LEN=
         LCLA  &KVVN,&CNTR1,&KVLL,&CNTR2
         LCLC  &P1,&P2,&P3,&P4,&LEN1,&LEN2
         AIF   ('&SYSLIST(1)' NE '').KVRM020 ONLY REQUIRED KEYWORD
         MNOTE 8,'1ST PARM MISSING AND IS REQUIRED'
         MEXIT
.KVRM020 ANOP
&KVLL    SETA  K'&SYSLIST(1)-1     SET KEYWORD LENGTH-1
&NAME    DC    AL1(&KVLL)          KEY LENGTH-1
         AIF   ('&SYSLIST(2)' NE '').KVRM030 IS EXIT RTN SPECIFIED
         DC    SL2(0)              EXIT RTN NOT SPECIFIED
         DC    SL2(0)              EXIT PARM NOT SPECIFIED
         AGO   .KVRM049
.KVRM030 ANOP
         DC    SL2(&SYSLIST(2,1))  EXIT ROUTINE ADDR
         AIF   (N'&SYSLIST(2) NE 1).KVRM046
         DC    SL2(0)              NO RTN PARM INFO REQUESTED
         AGO   .KVRM049
.KVRM046 ANOP
         DC    SL2(&SYSLIST(2,2))  EXIT RTN PARM SPECIFIED
.KVRM049 ANOP
         AIF   ('&SYSLIST(3)' NE '').KVRM050
         DC    SL2(0)              NO AREA SPECIFIED
         AGO   .KVRM060
.KVRM050 ANOP
         DC    SL2(&AREA)          AREA ADDR SPECIFIED
.KVRM060 ANOP
         AIF   ('&LEN' NE '').KVRM070
         DC    AL1(0)              VALUE LENGTH LOW DEFAULT
         DC    AL1(0)              VALUE LENGTH HIGH DEFAULT
         AGO   .KVRM078
.KVRM070 ANOP
&LEN1    SETC  '&LEN(1)'
&LEN2    SETC  '&LEN(2)'
         AIF   ('&LEN1' NE '').KVRM074
         DC    AL1(1)              VALUE LENGTH LOW IMPLIED
         AGO   .KVRM077
.KVRM074 ANOP
         DC    AL1(&LEN1)          VALUE LENGTH LOW SPECIFIED
.KVRM076 ANOP
         AIF   ('&LEN2' NE '').KVRM077
         DC    AL1(&LEN1)          VALUE LENGTH HIGH SPECIFIED
         AGO   .KVRM078
.KVRM077 ANOP
         DC    AL1(&LEN2)          VALUE LENGTH HIGH SPECIFIED
.KVRM078 ANOP
         AIF   (N'&SYSLIST(5) NE 0).KVRM090 VALUE COUNT SPECIFIED
         DC    AL1(1)              VALUE COUNT LOW DEFAULT
         DC    AL1(1)              VALUE COUNT HIGH DEFAULT
         AGO   .KVRM110
.KVRM090 ANOP
         AIF   (N'&SYSLIST(5) NE 1).KVRM094
         AIF   ('&SYSLIST(5,1)' EQ '(').KVRM094
         DC    AL1(0)              VALUE COUNT LOW IMPLIED
         DC    AL1(&VALCNT)        VALUE COUNT HIGH SPECIFIED
         AGO   .KVRM110
.KVRM094 ANOP
         AIF   ('&SYSLIST(5,1)' EQ ')').KVRM096
         DC    AL1(&SYSLIST(5,1))  VALUE COUNT LOW
         DC    AL1(&SYSLIST(5,2))  VALUE COUNT HIGH
         AGO   .KVRM110
.KVRM096 ANOP
         DC    AL1(&SYSLIST(5,1))  VALUE COUNT HIGH
.KVRM110 ANOP
         AIF   ('&SYSLIST(4)' NE '').KVRM120
         DC    X'00'               NO VALUE VALIDATION REQUESTED
         AGO   .KVRM160
.KVRM120 ANOP
         AIF   ('&SYSLIST(4)' NE 'ALPHA').KVRM130
         DC    X'01'               ALPHA VALIDATION REQUESTED
         AGO   .KVRM160
.KVRM130 ANOP
         AIF   ('&SYSLIST(4)' NE 'NUMERIC').KVRM140
         DC    X'02'               NUMERIC VALIDATION REQUESTED
         AGO   .KVRM160
.KVRM140 ANOP
         AIF   ('&SYSLIST(4)' NE 'ALPHANUM').KVRM150
         DC    X'03'               ALPHANUM VALIDATION REQUESTED
         AGO   .KVRM160
.KVRM150 ANOP
         MNOTE 8,'INVALID VALIDATION TYPE SPECIFIED  &TYPE'
.KVRM160 ANOP
         AIF   ('&FIELD1' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD2' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD3' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD4' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD5' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD6' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD7' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD8' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD9' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD10' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD11' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD12' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD13' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD14' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
         AIF   ('&FIELD15' EQ '' ).KVRM166
&KVVN    SETA  &KVVN+1
.KVRM166 ANOP
         DC    AL1(&KVVN)          COUNT OF FIELDS SPECIFIED
         DC    C'&KEYWORD'         REQUESTED KEYWORD
         AGO   .KVRM300
.KVRM180 ANOP
         AIF   ('&END' NE 'YES').KVRM190
         DC    X'FFFF'             END OF TABLE INDICATIOR
.KVRM190 ANOP
         MEXIT
.KVRM300 ANOP
         AIF   ('&FIELD1' EQ '').KVRM180
&CNTR1   SETA  1
&P1      SETC  '&FIELD1(1)'
&P2      SETC  '&FIELD1(2)'
&P3      SETC  '&FIELD1(3)'
&P4      SETC  '&FIELD1(4)'
         AGO   .KVRM500
.KVRM320 ANOP
         AIF   ('&FIELD2' EQ '').KVRM180
&CNTR1   SETA  2
&P1      SETC  '&FIELD2(1)'
&P2      SETC  '&FIELD2(2)'
&P3      SETC  '&FIELD2(3)'
&P4      SETC  '&FIELD2(4)'
         AGO   .KVRM500
.KVRM330 ANOP
         AIF   ('&FIELD3' EQ '').KVRM180
&CNTR1   SETA  3
&P1      SETC  '&FIELD3(1)'
&P2      SETC  '&FIELD3(2)'
&P3      SETC  '&FIELD3(3)'
&P4      SETC  '&FIELD3(4)'
         AGO   .KVRM500
.KVRM340 ANOP
         AIF   ('&FIELD4' EQ '').KVRM180
&CNTR1   SETA  4
&P1      SETC  '&FIELD4(1)'
&P2      SETC  '&FIELD4(2)'
&P3      SETC  '&FIELD4(3)'
&P4      SETC  '&FIELD4(4)'
         AGO   .KVRM500
.KVRM350 ANOP
         AIF   ('&FIELD5' EQ '').KVRM180
&CNTR1   SETA  5
&P1      SETC  '&FIELD5(1)'
&P2      SETC  '&FIELD5(2)'
&P3      SETC  '&FIELD5(3)'
&P4      SETC  '&FIELD5(4)'
         AGO   .KVRM500
.KVRM360 ANOP
         AIF   ('&FIELD6' EQ '').KVRM180
&CNTR1   SETA  6
&P1      SETC  '&FIELD6(1)'
&P2      SETC  '&FIELD6(2)'
&P3      SETC  '&FIELD6(3)'
&P4      SETC  '&FIELD6(4)'
         AGO   .KVRM500
.KVRM370 ANOP
         AIF   ('&FIELD7' EQ '').KVRM180
&CNTR1   SETA  7
&P1      SETC  '&FIELD7(1)'
&P2      SETC  '&FIELD7(2)'
&P3      SETC  '&FIELD7(3)'
&P4      SETC  '&FIELD7(4)'
         AGO   .KVRM500
.KVRM380 ANOP
         AIF   ('&FIELD8' EQ '').KVRM180
&CNTR1   SETA  8
&P1      SETC  '&FIELD7(1)'
&P2      SETC  '&FIELD7(2)'
&P3      SETC  '&FIELD7(3)'
&P4      SETC  '&FIELD7(4)'
         AGO   .KVRM500
.KVRM390 ANOP
         AIF   ('&FIELD9' EQ '').KVRM180
&CNTR1   SETA  9
&P1      SETC  '&FIELD7(1)'
&P2      SETC  '&FIELD7(2)'
&P3      SETC  '&FIELD7(3)'
&P4      SETC  '&FIELD7(4)'
         AGO   .KVRM500
.KVRM3A0 ANOP
         AIF   ('&FIELD10' EQ '').KVRM180
&CNTR1   SETA  10
&P1      SETC  '&FIELD7(1)'
&P2      SETC  '&FIELD7(2)'
&P3      SETC  '&FIELD7(3)'
&P4      SETC  '&FIELD7(4)'
         AGO   .KVRM500
.KVRM3B0 ANOP
         AIF   ('&FIELD11' EQ '').KVRM180
&CNTR1   SETA  11
&P1      SETC  '&FIELD7(1)'
&P2      SETC  '&FIELD7(2)'
&P3      SETC  '&FIELD7(3)'
&P4      SETC  '&FIELD7(4)'
         AGO   .KVRM500
.KVRM3C0 ANOP
         AIF   ('&FIELD12' EQ '').KVRM180
&CNTR1   SETA  12
&P1      SETC  '&FIELD7(1)'
&P2      SETC  '&FIELD7(2)'
&P3      SETC  '&FIELD7(3)'
&P4      SETC  '&FIELD7(4)'
         AGO   .KVRM500
.KVRM3D0 ANOP
         AIF   ('&FIELD13' EQ '').KVRM180
&CNTR1   SETA  13
&P1      SETC  '&FIELD7(1)'
&P2      SETC  '&FIELD7(2)'
&P3      SETC  '&FIELD7(3)'
&P4      SETC  '&FIELD7(4)'
         AGO   .KVRM500
.KVRM3E0 ANOP
         AIF   ('&FIELD14' EQ '').KVRM180
&CNTR1   SETA  14
&P1      SETC  '&FIELD7(1)'
&P2      SETC  '&FIELD7(2)'
&P3      SETC  '&FIELD7(3)'
&P4      SETC  '&FIELD7(4)'
         AGO   .KVRM500
.KVRM3F0 ANOP
         AIF   ('&FIELD15' EQ '').KVRM180
&CNTR1   SETA  15
&P1      SETC  '&FIELD7(1)'
&P2      SETC  '&FIELD7(2)'
&P3      SETC  '&FIELD7(3)'
&P4      SETC  '&FIELD7(4)'
         AGO   .KVRM500
.KVRM500 ANOP
         AIF   ('&P1' NE '').KVRM720
         DC    X'00'               NO VALUE IS SPECIFIED
         AGO   .KVRM730
.KVRM720 ANOP
&CNTR2   SETA  K'&P1
         DC    AL1(&CNTR2)         LENGTH OF VALUE SPECIFIED
.KVRM730 ANOP
         AIF   ('&P2' EQ '').KVRM900
         AIF   ('&P2' NE 'N').KVRM750
         DC    X'01'               N  OP CODE
         AGO   .KVRM800
.KVRM750 ANOP
         AIF   ('&P2' NE 'O').KVRM760
         DC    X'02'               O  OP CODE
         AGO   .KVRM800
.KVRM760 ANOP
         AIF   ('&P2' NE 'M').KVRM770
         DC    X'03'               M  OP CODE
         AGO   .KVRM800
.KVRM770 ANOP
         AIF   ('&P2' NE 'X').KVRM780
         DC    X'04'               X OP CODE
         AGO   .KVRM800
.KVRM780 ANOP
         MNOTE 8,'INVALID OP SPECIFIED IN FIELDN='
         MEXIT
.KVRM800 ANOP
         AIF   ('&P3' NE '').KVRM820
         MNOTE 8,'TO ADDR REQ AND NOT SPECIFIED IN FIELDN='
         MEXIT
.KVRM820 ANOP
         DC    SL2(&P3)            TO ADDRESS
         AIF   ('&P4' NE '').KVRM830
         MNOTE 8,'FROM ADDR REQ AND NOT SPECIFIED IN FIELDN='
         MEXIT
.KVRM830 ANOP
         DC    SL2(&P4)            FROM ADDRESS
         DC    AL2(L'&P3)          LENGTH OF TO ADDRESS
.KVRM834 ANOP
         AIF   ('&P1' NE '').KVRM840
         AGO   .KVRM950
.KVRM840 ANOP
         DC    C'&P1'              VALUE CONSTANT
         AGO   .KVRM950
.KVRM900 ANOP
         AIF   ('&P3' EQ '').KVRM920
         MNOTE 8,'MISSING PARMS IN FIELDN= PARAMETER'
         MEXIT
.KVRM920 ANOP
         AIF   ('&P4' EQ '').KVRM930
         MNOTE 8,'MISSING PARMS IN FIELDN= PARAMETER'
         MEXIT
.KVRM930 ANOP
         DC    X'00'               NO OP CODE
         DC    SL2(0)              NO TO ADDR
         DC    SL2(0)              NO FROM ADDR
         DC    SL2(0)              NO TO LENGTH
.KVRM950 ANOP
         AIF   (&CNTR1 EQ 1).KVRM320
         AIF   (&CNTR1 EQ 2).KVRM330
         AIF   (&CNTR1 EQ 3).KVRM340
         AIF   (&CNTR1 EQ 4).KVRM350
         AIF   (&CNTR1 EQ 5).KVRM360
         AIF   (&CNTR1 EQ 6).KVRM370
         AIF   (&CNTR1 EQ 7).KVRM380
         AIF   (&CNTR1 EQ 8).KVRM390
         AIF   (&CNTR1 EQ 9).KVRM3A0
         AIF   (&CNTR1 EQ 10).KVRM3B0
         AIF   (&CNTR1 EQ 11).KVRM3C0
         AIF   (&CNTR1 EQ 12).KVRM3D0
         AIF   (&CNTR1 EQ 13).KVRM3E0
         AIF   (&CNTR1 EQ 14).KVRM3F0
         AIF   (&CNTR1 EQ 15).KVRM180
         MEND
         EJECT ,
***********************************************************************
*         PROGRAM:    SFT8MV74  (SMART)                      01/18/80 *
*          AUTHOR:    JAMES COOK                                      *
*                     THE B.F. GOODRICH CO.                           *
*                     DEPT: 0058  BUILD: 17F                          *
*                     500 S. MAIN ST.                                 *
*                     AKRON, OHIO 44318                               *
*                     (216) 374-2998                                  *
*                                                                     *
*      THIS PROGRAM WAS ORIGINALLY DESIGNED TO FORMAT AN  OPERATORS   *
* MANUAL. THE ONLY FUNCTIONS IT ORIGINALLY HAD WAS TO SKIP TO TOP     *
* OF PAGE AND PRODUCE 4 PAGE TITLE LINES. BUT, AS THE MANUAL WAS      *
* BEING WRITTEN, THE NEED FOR MORE AND MORE FUNCTIONS AROSE. AND THUS *
* WE NOW HAVE (INSTEAD OF ABOUT 50 CARDS) CLOSE TO OVER 1000 CARDS.   *
*                                         (OVER 1400 CARDS 5/21/80)   *
*                                         (OVER 1600 CARDS 6/10/80)   *
*                                         (OVER 2100 CARDS 7/15/80)   *
*                                         (OVER 2284 CARDS 7/15/81)   *
*                                                                     *
*  PARMS:                                                             *
*           I - DON'T PRODUCE AN INDEX LISTING                        *
*           T - DON'T PRODUCE THE TABLE OF CONTENTS LISTING           *
*           C - DON'T PRODUCE THE COVER                               *
*           S - PRODUCE REPORT STATISTICS                             *
*           R - DON'T PRODUCE THE REPORT BODY                         *
*           A - PRINT ALL INDEX WORDS WHETHER FOUND OR NOT            *
*           L - DO NOT CENTER THE MANUAL, PUT IT ON THE LEFT SIDE     *
*           M - PRODUCE A MISSPELLING CROSS REFERENCE                 *
*                                                                     *
* DISCLAIMER:                                                         *
*                                                                     *
*     THIS PROGRAM HAS BEEN MODFIED SO MANY TIMES THAT IT PROBABLY    *
* IS THE MOST CONFUSING CODE I HAVE EVER WRITTEN. THERE IS NO DOUBT   *
* IN MY MIND THAT ANYONE LOOKING THROUGH THIS CODE WILL LAUGH AND     *
* SAY, "THAT TURKEY, WHY DID HE DO IT THAT WAY." WELL, MY ONLY        *
* DEFENSE IS THAT IT SEEMED EITHER LOGICAL, OR THE EASIEST WAY TO     *
* DO IT AT THE TIME.                                                  *
*                                                                     *
* I HAVE NOTHING MORE TO SAY, EXCEPT IF YOU TRY TO MODIFY OR TRY TO   *
* UNDERSTAND THIS CODE, MAY YOU NOT GO INSANE. BUT, IF YOU DO MAKE    *
* ANY SUBSTANTIAL MODIFICATIONS AND/OR IMPROVEMENTS, PLEASE SEND ME   *
* A COPY SO THAT I MAY USE THEM. THANKS.                              *
*                                                                     *
***********************************************************************
         PRINT NOGEN                                            GP03029
SMART    CSECT ,                                       COPIED ON 86033
         SAVE  (14,12),,SMART_&SYSDATE._&SYSTIME                 86033
         BALR  R12,0                   LOAD BASE
         USING *,R12,R11,R10,R9        DECLARE BASES
         LA    R11,2048(R12)           LOAD SECOND
         LA    R11,2048(R11)           BASE
         LA    R10,2048(R11)           LOAD THIRD
         LA    R10,2048(R10)           BASE
         LA    R9,2048(R10)            LOAD THIRD
         LA    R9,2048(R9)             BASE
         LR    R15,R13                 FORWARD
         LA    R13,SAVE                AND BACKWARD
         ST    R13,8(R15)              SAVEAREA
         ST    R15,4(R13)              CHAINING
         L     R1,0(R1)                LOAD ADDR OF FIRST PARM
         LH    R2,0(R1)                LOAD 1ST PARM LENGTH
         LA    R1,1(R1)                UP PARM POINTER
         CH    R2,=H'10'               IS THE PARM LONGER THAN 10?
         BL    PRSEPARM                NO, SKIP CHECK
         CLC   =C'SMART',3(R1)   ARE WE BEING EXECUTED FROM TSO? 86033
         BNE   PRSEPARM                NO, NO NEED TO UP POINTER
         LA    R1,5(,R1)               UP POINTER                86033
         SH    R2,=H'5'                AND SUBTRACT 5 FROM LENGTH
PARMLEFT EQU   *
         TM    FLAG1,F1LEFT            ARE WE ALREADY PROCESSING LEFT?
         BO    PRSEPARM                YES, GET THE NEXT PARM
         OI    FLAG1,F1LEFT            WE ARE BEING EXECUTED FROM TSO
         LA    R15,CARDOUT+13          LOAD NEW PRINT ADDR
         ST    R15,PADDR               AND SAVE IT IN THE PRINT ADDR
         LA    R15,TITLE1+13           LOAD ADDR OF TITLE
         MVI   0(R15),C'1'             MOVE IN CC
         LA    R15,TITLE3+13           LOAD ADDR OF TITLE
         MVI   0(R15),C'+'             MOVE IN CC
         LA    R15,TOCTIT1+13          LOAD ADDR OF TITLE
         MVI   0(R15),C'1'             MOVE IN CC
         LA    R15,13                  LOAD AMOUNT OF OFFSET
         STH   R15,TSOFFSET            AND SAVE IT
         LA    R15,80                  LOAD THE LRECL
         STH   R15,DLRECL              AND SAVE IT
         STH   R15,SYSUT3+X'52'        AND SAVE IT
         MVC   DBLKSIZE(2),=H'3120'    AND SAVE IT
         MVC   SYSUT3+X'3E'(2),=H'3120' MOVE IN BLKSIZE          86033
PRSEPARM EQU   *
         LA    R1,1(R1)                UP PARM POINTER
         LTR   R2,R2                   ANY PARMS
         BZ    NOPARM                  NO PARAMETERS
         BCTR  R2,0                    SUBTRACT 1
         CLI   0(R1),C'D'              A DEBUG OPERAND?
         BNE   TOCPARM                 NO, NO PARM
         OI    FLAG1,F1DEBUG           SET DEBUG BIT
         B     PRSEPARM
TOCPARM  EQU   *
         CLI   0(R1),C'T'              TABLE OF CONTENTS PARM
         BNE   INDPARM                 NO, GET NEXT PARM
         OI    FLAG2,F2TOC             DON'T PRINT THE TABLE OF CONT
         B     PRSEPARM
INDPARM  EQU   *
         CLI   0(R1),C'I'              TABLE OF CONTENTS PARM
         BNE   BODPARM                 NO, GET NEXT PARM
         OI    FLAG2,F2PRINDX          DON'T PRINT THE INDEX
         B     PRSEPARM
BODPARM  EQU   *
         CLI   0(R1),C'R'              REPORT PARM?
         BNE   COVPARM                 NO, GET NEXT PARM
         OI    FLAG2,F2BODY            DON'T PRINT THE REPORT
         B     PRSEPARM
COVPARM  EQU   *
         CLI   0(R1),C'C'              REPORT COVER PARM?
         BNE   ALLPARM                 NO, GET NEXT PARM
         OI    FLAG2,F2COVER           DON'T PRINT THE COVER
         B     PRSEPARM
ALLPARM  EQU   *
         CLI   0(R1),C'A'              REPORT BODY PARM?
         BNE   STATPARM                NO, GET NEXT PARM
         OI    FLAG2,F2ALLIN           YES, PRINT ALL INDEX ENTRIES
         B     PRSEPARM
STATPARM EQU   *
         CLI   0(R1),C'S'              REPORT STATS PARM?
         BNE   MISSPARM                NO, GET NEXT PARM
         OI    FLAG2,F2STATS           YES, PRINT REPORT STATS
         B     PRSEPARM
MISSPARM EQU   *
         CLI   0(R1),C'M'              PRINT MISSPELLING XREF?
         BNE   LEFTPARM                NO, GET NEXT PARM
         OI    FLAG3,F3MISS            YES, PRINT IT
         B     PRSEPARM
LEFTPARM EQU   *
         CLI   0(R1),C'L'              REPORT STATS PARM?
         BNE   PRSEPARM                NO, GET NEXT PARM
         B     PARMLEFT
NOPARM   EQU   *
         OPEN  (SYSUT1,INPUT,SYSUT2,OUTPUT,SYSUT3,OUTPUT)
         TIME  BIN                     GET DATE
         ST    R1,DOUBLE+4             SAVE
         ED    DATE(7),DOUBLE+5        EDIT IT
         MVC   TOCDATE(7),DATE         COPY DATE TO TOC PAGE
         MVC   PDATE(7),DATE           COPY DATE TO TOC PAGE
***********************************************************************
* READ AN INPUT CARD AND SEE IF IT IS A COMMAND CARD, IF NOT, PRINT IT*
***********************************************************************
LOOP     EQU   *
         BAL   R8,GETIT                READ A RECORD
         CLC   CARD(2),=C'/*'          POSSIBLE CONTROL CARD?
         BE    CMD                     YES, TRY SECTION
         CLC   CARD(2),=C'..'          POSSIBLE CONTROL CARD?
         BE    CMD                     YES, TRY SECTION
         B     ECHO                    PROCESS THE CARD
***********************************************************************
* SEE WHICH COMMAND IT IS AND GO OFF AND PROCESS IT                   *
***********************************************************************
CMD      EQU   *
         MVC   DOUBLE(8),CARD+2        MOVE IN COMMAND
         TR    DOUBLE(8),TRTABLE       TRANSLATE TO UC
         CLC   DOUBLE(3),=C'SEC'       A SECTION COMMAND
         BE    SECTION
         CLC   DOUBLE(5),=C'TITLE'     A TITLE CARD
         BE    TIT1
         CLC   DOUBLE(7),=C'VERSION'   A VERSION CARD?
         BE    VERSION
         CLC   DOUBLE(4),=C'DATE'      A DATE CARD?
         BE    DATE1
         TR    CARD(71),TRTABLE        OK, WE CAN NOW TRANSLATE TO UC
         CLC   CARD+2(4),=C'PAGE'      A PAGE COMMAND
         BE    PAGE0
         CLC   CARD+2(4),=C'ULIN'      AN UNDERLINE COMMAND
         BE    ULIN
         CLC   CARD+2(5),=C'COVER'     COVER PAGE?
         BE    COVLOOP
         CLC   CARD+2(5),=C'SPACE'     A SPACE CARD?
         BE    SPACEIT
         CLC   CARD+2(7),=C'COMMENT'   A COMMENT CARD?
         BE    LOOP
         CLC   CARD+2(7),=C'INCLUDE'   AN INCLUDE CARD?
         BE    INCLUDE0
         CLC   CARD+2(5),=C'INDEX'     AN INDEX CARD?
         BE    INDEX0
         CLC   CARD+2(6),=C'IGNORE'    AN IGNORE CARD?
         BE    IGNORE
         CLC   CARD+2(7),=C'NOSPACE'   A NOSPACE CARD?
         BE    NOSPACE
         CLI   CARD+2,C'+'             A NOSPACE CARD?
         BE    NOSPACE
         CLC   CARD+2(3),=C'TOP'       A TOP CARD?
         BE    TOP
         CLC   CARD+2(6),=C'PGSIZE'    A PAGE SIZE CARD?
         BE    PGSIZE
         CLC   CARD+2(4),=C'JUST'      A JUST CARD?
         BE    JUST
         CLC   CARD+2(6),=C'OPTION'    AN OPTION CARD?
         BE    OPTION
         CLC   CARD+2(5),=C'LEVEL'     A LEVEL CARD?
         BE    LEVEL1
         CLC   CARD+2(5),=C'SPELL'     A SPELL CARD?
         BE    SPELL
         CLC   CARD+2(5),=C'SPCHK'     A SPCHK CARD?
         BE    SPCHK
         CLC   CARD+2(6),=C'CENTER'    A CENTER CARD?
         BE    CENTER
***********************************************************************
* PRINT OUT THE CARD AND DO INDEX PROCESSING, THEN GO BACK FOR ANOTHER*
***********************************************************************
ECHO     EQU   *
         TM    FLAG2,F2JUST            DO WE JUSTIFY THE CARD?
         BZ    ECHO1                   NO
         BAL   R8,JUST01               JUSTIFY THE CARD.
ECHO1    EQU   *
         L     R5,PADDR                LOAD ADDR OF CARD TO PRINT
         BAL   R8,PRINTIT              NO KNOWN COMMAND, ECHO IT
         BAL   R8,PINDEX               PROCESS FOR INDEX
         TM    FLAG1,F1SPELL           DO WE CHECK THE SPELLING?
         BZ    LOOP                    NO
         BAL   R8,SPELLCHK             CHECK THE SPELLING.
         B     LOOP                    GET NEXT CARD
***********************************************************************
* CENTER - CENTER THE OPERAND ON THE PRINT LINE                       *
***********************************************************************
CENTER   EQU   *
         LA    R1,CARD+8               LOAD START FOR OPERAND
         LA    R2,CARD+71              LOAD END OF OPERAND
         BAL   R7,FLEFT                FIND THE LEFT SIDE
         CR    R1,R2                   ANY OPERAND?
         BE    LOOP                    NOPE, GO GET NEXT CARD
         BAL   R7,FRIGHT               AND FIND THE RIGHT SIDE
         SR    R2,R1                   GET THE LENGTH
         EX    R2,CENMVC1              AND MOVE IT TO TEMP STORAGE
         MVC   CARD(80),BLANK          BLANK OUT THE CARD
         LA    R1,72                   LOAD NUMBER OF PLACES
         SR    R1,R2                   GET UNUSED SPACES
         SRL   R1,1                    DIVIDED BY 2
         LA    R1,CARD(R1)             AND GET THE PLACE TO PUT IT
         EX    R2,CENMVC2              AND MOVE IT IN
         B     ECHO1                   DON'T JUST IT
CENMVC1  MVC   WORKLINE(0),0(R1)
CENMVC2  MVC   0(0,R1),WORKLINE
***********************************************************************
* SPCHK - EITHER TURN ON OR OFF THE SPELL CHECKING                    *
***********************************************************************
SPCHK    EQU   *
         CLC   CARD+8(2),=C'ON'        TURN IT ON?
         BE    SPCHK1                  YES
         CLC   CARD+8(2),=C'OF'        TURN IT OFF?
         BE    SPCHK0                  YES
* JUST FLIP IT THEN
         TM    FLAG1,F1SPELL           ARE WE CHECKING SPELLING?
         BZ    SPCHK1                  NO, START TO THEN
SPCHK0   EQU   *
         NI    FLAG1,X'FF'-F1SPELL     TURN OFF SPELL CHECKING
         B     LOOP
SPCHK1   EQU   *
         CLC   SPELLVA(4),=F'0'        HAVE WE GOTTEN ANY STORAGE?
         BE    ECHO                    NOPE, ERROR THEN
         OI    FLAG1,F1SPELL           TURN ON SPELL CHECKING
         B     LOOP                    AND GET THE NEXT CARD
***********************************************************************
* LEVEL - PROCESS THE LEVEL COMMAND.                                  *
***********************************************************************
LEVEL1   EQU   *
         LA    R1,CARD+7               LOAD START OF OPERANDS
         LA    R2,CARD+71              LOAD END OF OPERANDS
         BAL   R8,GETNUM               GET THE NUMBER
         LTR   R1,R1                   IS THERE A NUMBER?
         BZ    ECHO                    NO, ECHO THE CARD
         STH   R1,LEVEL                SAVE THE LEVEL NUMBER.
         B     LOOP                    AND GET THE NEXT CARD
***********************************************************************
* IGNORE THE NEXT CARD IF IT IS A COMMAND?                            *
***********************************************************************
IGNORE   EQU   *
         BAL   R8,GETIT                GET THE CARD
         B     ECHO                    AND ECHO IT
***********************************************************************
* OPTIONS COMMAND - PROCESS TEXT OPTIONS                              *
***********************************************************************
OPTION   EQU   *
         LA    R1,68                   LOAD LENGTH OF OPTION CARD
         STH   R1,CARD+6               STORE LENGTH
         KEYVAL CARD+6,OPTABLE         CALL VALIDATION ROUTINE.
         LTR   R15,R15                 IS EVERYTHING OK?
         BZ    LOOP                    YES, GET THE NEXT CARD
         MVC   CARD+6(2),=C'ON'        REPLACE THE END OF OPTION
         B     ECHO                    AND PRINT OUT THE BAD CARD
***********************************************************************
* JUST COMMAND - EITHER START OR STOP JUSTIFICATION                   *
***********************************************************************
JUST     EQU   *
         TM    FLAG2,F2JUST            DO WE START JUST?
         BZ    JUST1                   YES!
         NI    FLAG2,X'FF'-F2JUST      TURN OFF FLAG
         B     LOOP                    GET NEXT CARD
JUST1    EQU   *
         OI    FLAG2,F2JUST            MARK JUSTIFICATION
         SR    R1,R1                   ZERO R1
         ST    R1,JUSTCOL              STORE STARTING COLUMN.
         LA    R1,CARD+6               LOAD START FOR OPERAND
         LA    R2,CARD+71              LOAD END OF OPERAND
         BAL   R7,FLEFT                FIND THE LEFT SIDE
         CR    R1,R2                   ANY OPERAND?
         BE    LOOP                    NOPE, GO GET NEXT CARD
         CLI   0(R1),C'S'              A SPACE OPERAND?
         BE    JUST2                   YES PROCESS IT
         CLI   0(R1),C'C'              A COLUMN OPERAND?
         BNE   ECHO                    NO, ECHO THIS CARD
         LA    R1,1(R1)                UP COUNTER
         BAL   R8,GETNUM               RETRIEVE THE NUMBER
         LTR   R1,R1                   IS IT ZERO
         BZ    JUST3                   YES, SKIP SUBTRACT
         BCTR  R1,0                    SUBTRACT 1
         B     JUST3                   AND STORE THE NUMBER
JUST2    EQU   *
         LA    R1,1(R1)                UP COUNTER
         BAL   R8,GETNUM               RETRIEVE THE NUMBER
         LNR   R1,R1                   MAKE R1 NEGATIVE
JUST3    EQU   *
         ST    R1,JUSTCOL              AND STORE THE NUMBER
         B     LOOP                    GET NEXT CARD
***********************************************************************
* PGSIZE COMMAND - CHANGE PAGE SIZE LENGTH                            *
***********************************************************************
PGSIZE   EQU   *
         LA    R1,CARD+9               LOAD START OF PARM
         LA    R2,CARD+70              LOAD END OF CARD
         BAL   R8,GETNUM               GET THE PAGE SIZE
         LTR   R1,R1                   ANY OPERAND
         BNZ   PGSIZE1                 YES
         LA    R1,60                   NO, LOAD DEFAULT OF 60
PGSIZE1  EQU   *
         ST    R1,MAXLINE              SAVE THE PAGE SIZE
         B     LOOP                    AND GET NEXT CARD
***********************************************************************
* NOSPACE COMMAND - OVERPRINT THE NEXT LINE                           *
***********************************************************************
NOSPACE  EQU   *
         CLI   CARD+2,C'+'             ARE WE ABBREVIATING
         BNE   NOSPAC01                NO, USE FULL TERM
         MVC   CARD(3),BLANK           BLANK OUT /*+
         B     NOSPAC02                AND SEE IF CARD IS BLANK
NOSPAC01 EQU   *
         MVC   CARD(9),BLANK           BLANK OUT THE /*NOSPACE.
NOSPAC02 EQU   *
         CLC   CARD(71),BLANK          IS THE REST OF THE CARD BLANK?
         BNE   NOSPACE0                SKIP THE NEXT CARD.
         BAL   R8,GETIT                GET THE NEXT CARD
NOSPACE0 EQU   *
         TM    FLAG2,F2JUST            ARE WE JUSTIFYING?
         BZ    NOSPACE3                NO, NO NEED TO FIX THE FIELD
         L     R2,JUSTR2               LOAD ENDING POSITION
         LTR   R2,R2                   WAS THE LAST LINE JUSTIFIED?
         BZ    NOSPACE3                NO, DON'T JUSTIFY THE NEXT LINE
         LA    R2,1(R2)                UP ONE FOR THE BCTR
         LA    R4,CARD+72              LOAD MOVING POSITION
         LA    R3,JUSTIT+72            LOAD IN JUST FLAGS
         L     R1,JUSTR1               LOAD STARTING POSITION
NOSPACE1 EQU   *
         CR    R2,R1                   ARE WE DONE?
         BE    NOSPACE3
         BCTR  R3,0
         BCTR  R4,0
         CLI   0(R3),C' '              DO WE MOVE IN A SPACE
         BNE   NOSPACE2                YES WE DO
         BCTR  R2,0
         MVC   0(1,R4),0(R2)           MOVE IN THE CHAR
         B     NOSPACE1                AND MOVE IN THE NEXT CHAR
NOSPACE2 EQU   *
         MVC   0(1,R4),0(R2)           PROPAGATE THE LAST CHAR
         B     NOSPACE1                AND GET THE NEXT CHAR
NOSPACE3 EQU   *
         L     R5,PADDR                LOAD ADDR TO PRINT
         MVI   0(R5),C'+'              MOVE IN NOSPACE CONTROL
         L     R1,LINECNT              LOAD THE LINE COUNT
         BCTR  R1,0                    SUBTRACT 1 FROM LINE COUNT
         ST    R1,LINECNT              AND RESAVE IT
         BAL   R8,PRINTIT              PRINT THE RECORD
         MVI   0(R5),C' '              AND REPLACE CARRIAGE CONTROL
         B     LOOP                    AND RETURN
***********************************************************************
* TOP COMMAND - GET LINES TO PUT AT TOP OF EACH PAGE                  *
***********************************************************************
TOP      EQU   *
         SR    R2,R2                   ZERO TOP LINE COUNT
         STC   R2,TOPCNT               SAVE THE LINE COUNT
         LA    R3,TOPLINE              LOAD ADDR TO PUT LINES
TOP1     EQU   *
         BAL   R8,GETIT                GET THE NEXT RECORD
         MVC   DOUBLE(5),CARD          MOVE IN COMMAND
         TR    DOUBLE(5),TRTABLE       TRANSLATE TO UC
         CLC   DOUBLE(5),=C'/*TOP'     ARE WE DONE
         BE    LOOP                    YES, GET NEXT CARD
         CLC   DOUBLE(5),=C'..TOP'     ARE WE DONE
         BE    LOOP                    YES, GET NEXT CARD
         MVC   0(80,R3),CARD           SAVE THIS CARD
         LA    R3,80(R3)               UP POINTER
         LA    R2,1(R2)                UP POINTER
         STC   R2,TOPCNT               AND SAVE TOP LINE COUNT
         CH    R2,=H'5'                ARE WE PAST THE LIMIT
         BNH   TOP1                    NO, GET NEXT CARD
         LA    R5,=CL80'*** TOO MANY TOP CARDS ***'
         BAL   R8,PRINTIT              NO KNOWN COMMAND, ECHO IT
         B     LOOP                    YES, QUIT
***********************************************************************
* GET THE MANUAL DATE AND PLACE IT IN THE TITLES                      *
***********************************************************************
DATE1    EQU   *
         LA    R1,CARD+7               LOAD START OF PARM
         LA    R2,CARD+71              LOAD END OF PARM
         BAL   R7,FLEFT                FIND LEFT SIDE
         CR    R1,R2                   NO PARM?
         BE    LOOP                    NO PARM, SKIP CARD
         MVC   DATE+1(8),0(R1)         MOVE DATE INTO TITLE
         MVC   TOCDATE+1(8),0(R1)      AND THE OTHER TITLE
         B     LOOP                    GET NEXT CARD
***********************************************************************
* GET THE MANUAL VERSION NUMBER AND PLACE IT IN THE TITLES            *
***********************************************************************
VERSION  EQU   *
         LA    R1,CARD+9               LOAD START OF PARM
         LA    R2,CARD+71              LOAD END OF PARM
         BAL   R7,FLEFT                FIND THE LEFT SIDE
         CR    R1,R2                   NO PARM?
         BE    LOOP                    NO, SKIP CARD
         MVC   PVER(5),0(R1)           MOVE IN VERSION
         MVC   PTVER(5),0(R1)          INTO TOC HEADER ALSO
         MVC   PVERT1(4),=C'VER:'      INTO TEXT TITLE
         MVC   PVERT2(4),=C'VER:'      INTO TOC  TITLE
         B     LOOP                    AND LOOP BACK FOR MORE
***********************************************************************
* GET THE MANUAL NAME, CENTER IT AND PLACE IT IN THE TITLES           *
***********************************************************************
TIT1     EQU   *
         LA    R1,CARD+7               LOAD START OF OPERAND
         LA    R2,CARD+71              LOAD END OF OPERAND
         BAL   R7,FRIGHT               FIND RIGHT SIDE
         CR    R1,R2                   ANY OPERAND
         BE    LOOP                    NO, GET NEXT CARD
         BAL   R7,FLEFT                FIND LEFT SIDE
         MVC   MANNAME(40),BLANK       BLANK OUT MANUAL NAME
         SR    R2,R1                   GET LENGTH -1
         LA    R3,40                   LOAD LENGTH OF MANUAL NAME
         CR    R2,R3                   NAME TOO LONG?
         BL    TIT2                    NO, KEEP PROCESSING
         LA    R2,39                   TRUNCATE TITLE
TIT2     EQU   *
         SR    R3,R2                   CENTER
         SRL   R3,1                    THE
         LA    R3,MANNAME(R3)          TITLE
         EX    R2,MOVEMAN              MOVE IN MANUAL NAME
         MVC   TMANNAME(40),MANNAME    MOVE NAME INTO TOC
         B     LOOP                    GET NEXT CARD
MOVEMAN  MVC   0(0,R3),0(R1)           MOVE IN THE MANUAL TITLE
***********************************************************************
* GET THE COVER PAGE, PRINT IT UNTIL THE NEXT /*COVER COMMAND         *
***********************************************************************
COVLOOP  EQU   *
         TM    FLAG2,F2COVER           ARE WE DEBUGGING
         BO    COVLOOP1                YES, DON'T PRINT THE COVER
         MVI   BLANK,C'1'          SET FOR CHANNEL 1
         PUT   SYSUT2,BLANK        AND PUT IT
         MVI   BLANK,C' '          CLEAR CHANNEL 1
COVLOOP1 BAL   R8,GETIT                READ A CARD
         MVC   DOUBLE(7),CARD          MOVE TO AREA FOR TR
         TR    DOUBLE(7),TRTABLE       TRANSLATE TO UPPER CASE
         CLC   DOUBLE(7),=C'/*COVER'   AT END OF TITLE PAGE?
         BE    LOOP                    YES TERMINATE
         CLC   DOUBLE(7),=C'..COVER'   AT END OF TITLE PAGE?
         BE    LOOP                    YES TERMINATE
         TM    FLAG2,F2COVER           ARE WE DEBUGGING
         BO    COVLOOP1                YES, DON'T PRINT THE COVER
         MVC   CARD+72(8),BLANK        BLANK SEQ NOS
         L     R15,PADDR               LOAD ADDRESS TO PRINT
         PUT   SYSUT2,0(R15)           PRINT CARD
         B     COVLOOP1                READ ANOTHER RECORD
***********************************************************************
* SPACE COMMAND - PRINT X BLANK LINES WHERE X IS THE OPERAND          *
***********************************************************************
SPACEIT  EQU   *
         LA    R1,CARD+7               LOAD START ADDR
         LA    R2,CARD+71              LOAD END ADDR
         BAL   R8,GETNUM               GET NUMBER
         LR    R2,R1                   SAVE NUMBER OF SPACES
         LA    R5,BLANK                LOAD ADDR OF BLANK LINE
         LTR   R2,R2                   ANY OPERAND
         BNZ   SPACEIT2                YES, NO NEED FOR DEFAULT
         LA    R2,1                    LOAD DEFAULT OF 1 SPACE
SPACEIT2 EQU   *
         BAL   R8,PRINTIT              PRINT IT OUT
         BCT   R2,SPACEIT2             PRINT IF NOT DONE
         B     LOOP                    RETURN FOR NEXT CARD
***********************************************************************
* SECTION COMMAND - GET THE SECTION NAME AND NUMBER. PUT NUMBER AND   *
*    NAME INTO THE TITLE. PUT NUMBER, NAME OUT ON THE TABLE OF        *
*    CONTENTS ALONG WITH THE CURRENT PAGE.                            *
***********************************************************************
SECTION  EQU   *
         LA    R1,CARD+6               LOAD START OF OPERAND
         LA    R2,CARD+71              LOAD END OF OPERAND
         BAL   R7,FLEFT                FIND THE LEFT SIDE
         BAL   R7,FRIGHT               FIND THE RIGHT SIDE
         CR    R1,R2                   A BLANK CARD?
         BE    ECHO                    YES, ECHO THE CARD
         BCTR  R1,0                    SUBTRACT 1 FOR FIRST LA
         SR    R4,R4                   ZERO PERIOD COUNTER
         NI    FLAG1,X'FF'-F1RSEC      TURN OFF RELATIVE SECTION
         CLI   1(R1),C'*'              ARE WE DOING RELATIVE SECTIONS?
         BNE   SECT1                   NO, SKIP FLAG1 TURN ON
         OI    FLAG1,F1RSEC            FLIP ON THE BIT
SECT1    EQU   *
         LA    R3,1(R1)                LOAD START OF OPERAND
SECT2    EQU   *
         LA    R1,1(R1)                UP POINTER
         CLI   0(R1),C' '              ARE WE LOOKING AT A SPACE?
         BE    SECT3                   YES
         CLI   0(R1),C'.'              ARE WE LOOKING AT A PERIOD
         BE    SECT3                   YES
         B     SECT2                   LOOK AT THE NEXT CHAR
SCOMNUM  CLC   0(0,R3),=C'00000000'
SPACK    PACK  DOUBLE(8),0(0,R3)
SECT3    EQU   *
         LA    R4,1(R4)                UP LEVEL COUNTER
         CLI   0(R1),C' '              ARE WE AT THE END?
         BNE   SECT4                   GET THE NEXT NUMBER
         TM    FLAG1,F1RSEC            ARE WE USING RELATIVE SECTIONING
         BO    SECT5                   YES, GO PUT IN THE NUMBERS
SECT4    EQU   *
         TM    FLAG1,F1RSEC            ARE WE USING RELATIVE SECTIONING
         BO    SECT2                   YES, KEEP COUNTING
         LR    R15,R1                  SAVE CURRENT POINTER
         SR    R15,R3                  GET LENGTH
         BCTR  R15,0                   GET LENGTH-1 FOR EXECUTES
         EX    R15,SCOMNUM             ARE THEY NUMBERS
         BL    ECHO                    NO, ECHO THE CARD
         EX    R15,SPACK               PACK IT THEN
         CVB   R15,DOUBLE              CONVERT IT TO BINARY
         LR    R14,R4                  LOAD PERIOD COUNTER
         BCTR  R14,0                   SUBTRACT 1 FOR OFFSET
         SLL   R14,1                   MULTIPLY IT BY 2 FOR OFFSET
         STH   R15,AUTOSEC(R14)        SAVE THE NUMBER
         CLI   0(R1),C' '              ARE WE THROUGH?
         BNE   SECT1                   NO, GET THE NEXT NUMBER
SECT5    EQU   *
         STM   R1,R2,TEMP2             SAVE THE TOC TEXT POINTERS
         LR    R14,R4                  LOAD THE LEVEL NUMBER
         BCTR  R14,0                   POINT TO THIS LEVEL
         SLL   R14,1                   MULTIPLY BY TWO FOR OFFSET
         TM    FLAG1,F1RSEC            ARE WE DOING RELATIVE SECTIONING
         BZ    SECT5A                  NO, NO NEED FOR INCTREMENT
         LH    R15,AUTOSEC(R14)        GET LEVEL AMOUNT
         LA    R15,1(R15)              UP COUNT
         STH   R15,AUTOSEC(R14)        AND SAVE IT
SECT5A   EQU   *
         LA    R14,2(R14)              UP THE POINTER TO THE NEXT ONE
         LA    R14,AUTOSEC(R14)        POINT TO THE NEXT ONE
         MVC   0(2,R14),=H'0'          MOVE IN A ZERO
SECT6    EQU   *
         STH   R4,LEVELSEC             SAVE THE PRESENT SECTION LEVEL
         LA    R1,WORKLINE             LOAD START OF MOVES
         MVC   WORKLINE(80),BLANK      BLANK OUT THE WORK LINE
         LA    R4,0                    LOAD START OF LEVEL COUNTER
SECT7    EQU   *
         CH    R4,LEVELSEC             ARE WE DONE
         BE    SECT11                  YES
         LR    R14,R4                  LOAD SECTION LEVEL
         SLL   R14,1                   MULTPLY BY TWO
         LH    R15,AUTOSEC(R14)        LOAD LEVEL AMOUNT
         CVD   R15,DOUBLE              CONVERT IT
         UNPK  TEMP(8),DOUBLE(8)       UNPACK IT
         OI    TEMP+7,X'F0'            MAKE IT READABLE
         LA    R3,TEMP-1               LOAD START OF MOVE  -1
         LA    R2,TEMP+7               LOAD END OF MOVE
SECT8    EQU   *
         LA    R3,1(R3)                UP POINTER
         CR    R3,R2                   ARE WE DONE
         BH    SECT10                  YES, MOVE IN THE PERIOD
         CLI   0(R3),C'0'              IS IT A LEADING ZERO
         BE    SECT8                   YES, SKIP MOVE
SECT9    EQU   *
         MVC   0(1,R1),0(R3)           MOVE IN THE NUMBER
         LA    R1,1(R1)                UP R1 POINTER
         LA    R3,1(R3)                UP R3 POINTER
         CR    R3,R2                   ARE WE DONE
         BNH   SECT9                   NO, MOVE IN THE NEXT CHAR
SECT10   EQU   *
         MVI   0(R1),C'.'              MOVE IN A PERIOD
         LA    R1,1(R1)                UP POINTER
         LA    R4,1(R4)                UP SECTION LEVEL POINTER
         B     SECT7                   AND GET THE NEXT ONE
TOCTXTM1 MVC   0(0,R1),0(R2)           MOVE IN THE TOC TEXT
TXTMOVE  MVC   0(0,R3),0(R1)
SECT11   EQU   *
         LR    R2,R1                   LOAD END OF POINTER
         BCTR  R2,0                    SUBTRACT 1 TO GET TO LAST PERIOD
         MVI   0(R2),C' '              AND REPLACE IT WITH A BLANK
         MVC   SECNO(10),WORKLINE      MOVE IN THE SECTION NUMBER
         LM    R2,R3,TEMP2             RELOAD THE TOC TEXT POINTERS
         LA    R2,1(R2)                UP THE STARTING POINTER
         CR    R2,R3                   IS R2 BIGGER THAN R3?
         BH    ECHO                    YES, IMPROPER CONTROL CARD
         SR    R3,R2                   GET THE LENGTH
         EX    R3,TOCTXTM1             MOVE IN THE TEXT
         MVC   CARD+6(70),WORKLINE     MOVE IT BACK TO THE CARD
         MVC   CARD(6),BLANK           AND BLANK OUT THE FIRST 6 BYTES
         MVC   SECTITLE(40),BLANK      BLANK OUT TITLE
         LA    R2,WORKLINE+71          LOAD END OF THE WORKLINE
         BAL   R7,FRIGHT               FIND THE RIGHT SIDE
SRCH4    SR    R2,R1                   GET LENGTH
         CH    R2,=H'39'               IS LENGTH BIGGER THAN 40?
         BNH   SRCH4A                  NO, KEEP PROCESSING
         LA    R2,39                   TURUNCATE AT 40
SRCH4A   EQU   *
         LA    R3,39                   CENTER
         SR    R3,R2                   THE
         SRL   R3,1                    TITLE
         LA    R3,SECTITLE(R3)         GET START OF TITLE LOCATION
         EX    R2,TXTMOVE              MOVE IN TITLE
SRCH5    EQU   *
         L     R1,MAXLINE              LOAD MAX NUMBER OF LINES
         S     R1,=F'10'               SUBTRACT 10 LINES
         C     R1,LINECNT              LESS THAN 10 LINES LEFT
         BNH   SRCH5A                  YES, SKIP TO TOP OF PAGE ANYWAY
         LH    R4,LEVELSEC             LOAD SECTION LEVEL
         CH    R4,=H'1'                A NEW SECTION
         BNE   SRCH5B                  NO, DON'T SKIP TO TOP OF PAGE
SRCH5A   EQU   *
         MVC   LINECNT(4),MAXLINE      LOAD MAX LINES
SRCH5B   EQU   *
         MVC   SECT(4),=CL4'SEC:'      MOVE IN THE WORD
         LA    R5,BLANK                PUT IN A BLANK LINE
         BAL   R8,PRINTIT              AND PRINT IT
         BAL   R8,TOC                  PRINT TABLE OF CONTENTS LINE
         TM    FLAG3,F3BLOCK           ARE WE PRINTING A BLOCK?
         BZ    SRCH5F                  NO, DON'T PRINT BLOCK.
         TM    FLAG3,F3ALL             BLOCK ALL SECTIONS?
         BO    SRCH5E                  YES.
         LH    R4,LEVELSEC             LOAD SECTION LEVEL
         CH    R4,=H'1'                IS THIS A PRIMARY SECTION?
         BNE   SRCH5F                  NO, SKIP BLOCKING
SRCH5E   EQU   *
         MVC   WORKLINE(133),BLANK     BLANK OUT THE WORK LINE
         MVI   WORKLINE+16,C'*'        MOVE IN THE CHARACTER
         MVC   WORKLINE+17(71),WORKLINE+16 PROPAGATE IT.
         LA    R5,WORKLINE             LOAD ADDR OF WORKLINE
         AH    R5,TSOFFSET             ADD TSO OFFSET
         BAL   R8,PRINTIT              AND PRINT IT
         LA    R1,CARD                 LOAD START OF SEARCH
         LA    R2,CARD+71              LOAD END OF SEARCH
         BAL   R7,FLEFT                FIND THE LEFT SIDE
         LR    R3,R1                   LOAD START OF SEC NO
         BAL   R7,FINDSP               FIND THE NEXT SPACE
         LR    R4,R1                   LOAD END ADDR
         SR    R4,R3                   GET LENGTH
         MVC   WORKLINE(133),BLANK     BLANK OUT THE WORK LINE
         MVC   WORKLINE+16(8),=CL8'SECTION ' MOVE IN THE WORK SECTION
         LA    R2,WORKLINE+24          LOAD ADDR TO MOVE IN SEC NO
         EX    R4,MOVEIT2              MOVE IN THE SECTION NUMBER
         LA    R2,1(R2,R4)             R2 <- R2+R4+1
         MVI   0(R2),C'-'
         LA    R2,1(R2)                LOAD ADDR TO MOVE IN REST TITLE
         MVC   0(80,R2),0(R1)          MOVE IN THE SECTION NAME
         LA    R2,69                   LOAD AMOUNT TO MOVE IF LEFT
         LA    R3,CARD+2               LOAD PLACE TO MOVE TO
         TM    FLAG3,F3LEFT            ARE WE TO PRINT IT TO THE LEFT?
         BO    SRCH5L                  YES, SKIP CENTERING
         LA    R1,WORKLINE+16          LOAD START OF LINE
         LA    R2,WORKLINE+80          LOAD END TO CENTER
         BAL   R7,FRIGHT               FIND THE RIGHT HAND SIDE
         MVC   CARD(72),BLANK          BLANK OUT THE CARD
         SR    R2,R1                   GET LENGTH
         LA    R3,70                   LOAD LENGTH OF SPACE
         SR    R3,R2                   GET NUMBER OF SPACES
         SRL   R3,1                    GET NUMBER OF SPACES IN FRONT
         LA    R3,CARD(R3)             LOAD OFFSET INTO CARD
SRCH5L   EQU   *
         EX    R2,MOVESEC              MOVE IT IN
         MVI   CARD,C'*'               MOVE IN FIRST STAR
         MVI   CARD+71,C'*'            MOVE IN SECOND STAR
         L     R5,PADDR                LOAD THE ADDRESS TO PRINT
         BAL   R8,PRINTIT              PRINT THE LINE
         MVI   WORKLINE+16,C'*'        MOVE IN THE CHARACTER
         MVC   WORKLINE+17(71),WORKLINE+16 PROPAGATE IT.
         LA    R5,WORKLINE             LOAD ADDR OF WORKLINE
         AH    R5,TSOFFSET             ADD TSO OFFSET
         BAL   R8,PRINTIT              PRINT THE LINE
         B     SRCH5X                  SKIP OTHER STUFF
MOVESEC  MVC   0(0,R3),WORKLINE+16
MOVEIT2  MVC   0(0,R2),0(R3)
SRCH5F   EQU   *
         TM    FLAG3,F3LEFT            ARE WE TO MOVE IT TO THE LEFT
         BZ    SRCH5G                  NO, SKIP MOVE
         MVC   CARD(85),CARD+6         MOVE IT TO THE LEFT
SRCH5G   EQU   *
         L     R5,PADDR                LOAD ADDR OF CARD
         BAL   R8,PRINTIT              PRINT OUT SECTION NAME
         BAL   R8,ULINE                UNDERLINE SECTION TITLES
         LA    R5,BLANK                PUT IN A BLANK LINE
         BAL   R8,PRINTIT              AND PRINT IT
SRCH5X   EQU   *
         B     LOOP                    GET NEXT CARD
***********************************************************************
* PAGE COMMAND - SKIP TO TOP OF NEW PAGE.
***********************************************************************
PAGE0    EQU   *
         LA    R1,CARD+7               LOAD START OF OPERAND
         LA    R2,CARD+71              LOAD END OF OPERAND
         BAL   R8,GETNUM               GET OPERAND
         LTR   R1,R1                   ANY OPERAND?
         BZ    PAGE2                   NO, PAGE IT
         SR    R3,R3                   ZERO SPACE FLAG
         CLC   1(5,R2),=C'SPACE'       SPACE OPERAND ALSO?
         BNE   PAGE1C                  NO, DON'T SET FLAG
         LA    R3,1                    SET FLAG TO 1
PAGE1C   EQU   *
         L     R2,MAXLINE              GET MAX LINES PER PAGE
         SR    R2,R1                   SUBTRACT MAX LIMIT SO FAR
         SR    R2,R3                   SUBTRACT FOR SPACE IF ONE
         C     R2,LINECNT              ARE WE PAST LIMIT
         BNH   PAGE2                   YES, PAGE IT
         LTR   R3,R3                   SPACE OPERAND?
         BZ    LOOP                    NO, GET NEXT CARD
         LA    R5,BLANK                POINT TO BLANK LINE
         BAL   R8,PRINTIT              PRINT BLANK LINE
         B     LOOP                    AND RETURN
PAGE2    EQU   *
         MVC   LINECNT(4),MAXLINE      LOAD MAX LINES PER PAGE
         B     LOOP                    READ NEXT CARD
***********************************************************************
* INDEX COMMAND - GET INDEX ENTRIES                                   *
***********************************************************************
INDEX0   EQU   *
         TM    FLAG1,F1INDX            ARE WE ALREADY DOING AN INDEX
         BO    ECHO                    YES, ECHO THE CARD
         OI    FLAG1,F1INDX            WE ARE NOW
         LA    R1,CARD+7               LOAD START OF PARMS
         LA    R2,CARD+70              LOAD END OF PARMS
         BAL   R7,FLEFT                FIND START OF 1'ST PARM
         CR    R1,R2                   IS THERE ANY PARM
         BE    INDEX02                 NO, USE DEFAULTS
         CLI   0(R1),C'0'              IS IT A NUMBER
         BNL   INDEX01                 YES, MUST BE AMOUNT
         MVC   INDXCHAR(1),0(R1)       MOVE IN DELIMITER
         BAL   R7,FINDSP               FIND THE NEXT SPACE
INDEX01  EQU   *
         BAL   R8,GETNUM               GET NUMBER
         LTR   R1,R1                   IS THERE ANY
         BZ    INDEX02                 NO, USE DEFAULT GETMAIN
         C     R1,=F'10000'            IS IT LESS THAN 1000?
         BL    INDEX02                 YES, STILL USE DEFAULT GETMAIN
         ST    R1,GETAMT               NO,  USE THIS AMOUNT
INDEX02  EQU   *
         L     R8,GETAMT               LOAD AMT TO GET
         GETMAIN R,LV=(R8)             GET INDEX STORAGE
         ST    R1,INDXADDR             SAVE ADDRESS
         ST    R1,INDXKEY              SAVE END OF WORDS POINTERS
         A     R1,GETAMT               GET ENDING ADDR
         ST    R1,INDXEND              SAVE ENDING ADDR
         ST    R1,INDXNAME             SAVE START OF NAMES
***********************************************************************
*  IN THIS SECTION, WE BUILD THE SYMBOL TABLE FOR THE INDEX ENTRIES.  *
* THERE ARE TWO PARTS TO THIS TABLE, A NAME PART AND A POINTER PART.  *
* EACH POINTER PART IS 8 BYTES LONG AND STARTS AT THE BOTTOM OF THE   *
* AQUIRED STORAGE. EACH NAME PART IS AS LONG AS THE NAME AND IS STORED*
* AT THE TOP OF THE AQUIRED STORAGE WORKING DOWN. THE FIRST BYTE OF   *
* THE POINTER IS THE LENGTH OF THE NAME. THE NEXT THREE BYTES IS THE  *
* ADDRESS OF THE NAME. THE NEXT TWO BYTES IS THE HIGH PAGE COUNTER TO *
* KEEP TRACK OF THE HIGHEST PAGE A WORD HAS BEEN FOUND ON. THE NEXT   *
* TWO BYTES ARE RESERVED FOR FUTURE USE. AN EXAMPLE OF THE STORAGE    *
* IS DESCRIBED BELOW.  THE STORED WORDS ARE 'HOW ARE YOU TODAY'       *
*                                                                     *
*                                                                     *
*        HEX ADDR            STORAGE IN HEX                           *
*                 ------------------------------------                *
*       0E0000    | 020E005D00000000020E005A00000000 |                *
*       0E0010    | 020E005700000000040E005200000000 |                *
*       0E0020    | ................................ |                *
*       0E0030    | ................................ |                *
*       0E0040    | ................................ |                *
*       0E0050    | .... T O D A Y Y O U A R E H O W |                *
*                 |----------------------------------|                *
*                    0 1 2 3 4 5 6 7 8 9 A B C D E F                  *
*                                                                     *
***********************************************************************
INDEX1   EQU   *
         BAL   R8,GETIT                GET A CARD
         TR    CARD(71),TRTABLE        TRANSLATE TO UPPER CASE
         CLC   CARD(7),=C'/*INDEX'     ARE WE DONE?
         BE    INDEXEND                YES, NOW SORT THE NAMES
         CLC   CARD(7),=C'..INDEX'     ARE WE DONE?
         BE    INDEXEND                YES, NOW SORT THE NAMES
         LA    R1,CARD                 LOAD START OF NAMES
         LA    R2,CARD+71              LOAD END OF NAMES
INDEX2   EQU   *
         BAL   R7,FLEFT                GET RID OF LEADING BLANKS
         CR    R1,R2                   DID WE RUN OF THE END OF CARD
         BNL   INDEX1                  YES, GET NEXT CARD
         LR    R3,R1                   SAVE START OF NAME
INDEX3   EQU   *
         LA    R1,1(R1)                UP POINTER
         CLC   0(1,R1),INDXCHAR        COMPARE WITH DELIMITER
         BE    INDEX4                  FOUND END OF NAME
         CR    R2,R1                   ARE WE OFF THE END OF THE CARD?
         BH    INDEX3                  NO, LOOK SOME MORE
         LR    R1,R3                   LOAD START OF WORD AGAIN
         BAL   R7,FRIGHT               FIND END OF WORD
         LA    R1,1(R2)                SAVE END OF WORD+1
         LA    R2,CARD+71              REINSTATE END OF CARD ADDR
INDEX4   EQU   *
         LR    R4,R1                   SAVE END ADDR
         SR    R4,R3                   GET LENGTH OF WORD
         L     R5,INDXNAME             LOAD ADDR OF INDEX NAMES
         SR    R5,R4                   FIND START FOR THIS WORD
         ST    R5,INDXNAME             SAVE ADDR
         BCTR  R4,0                    SUBTRACT 1 FOR EX
         EX    R4,MOVEINDX             MOVE IN INDEX NAME
         L     R3,INDXKEY              LOAD NAME KEY ADDR
         ST    R5,0(R3)                SAVE ADDR OF NAME
         STC   R4,0(R3)                SAVE LENGTH
         XC    4(4,R3),0(R3)           ZERO HIGH PAGE COUNTER
         LA    R3,8(R3)                UP POINTER
         ST    R3,INDXKEY              RESAVE NEW END ADDR
         C     R3,INDXNAME             HAVE WE RUN OUT OF CORE
         BH    OUTOFRAM                YES, ABEND
         LA    R1,1(R1)                UP POINTER
         B     INDEX2                  AND GET NEXT NAME
MOVEINDX MVC   0(0,R5),0(R3)           MOVE IN INDEX NAME
OUTOFRAM EQU   *
         ABEND 10
INDEXEND EQU   *                       BUBBLE SORT
         L     R1,INDXADDR
         SH    R1,=H'8'
***********************************************************************
*  WE NOW SORT THE WORDS USING THE KLUGY BUBBLE SORT, BUT SINCE THE   *
* WORD LIST IS RELATIVELY SMALL, IT RUNS OK. WE MOVE THE POINTERS     *
* AROUND IN MEMORY SO THEY ARE SORTED. THE NAMES ARE LEFT ALONE. SO   *
* WHEN WE ARE DONE, USING THE EXAMPLE ABOVE, THE FIRST POINTER WOULD  *
* POINT TO 'ARE', THE SECOND TO 'HOW', AND THE REST TO 'TODAY', 'YOU'.*
***********************************************************************
BSORT1   EQU   *                       DO 100 R1=1,N
         LA    R1,8(R1)
         C     R1,INDXKEY
         BE    SORTEND
         LR    R2,R1
BSORT2   EQU   *                       DO 100 R2=R1,N
         LA    R2,8(R2)
         C     R2,INDXKEY
         BNL   BSORT1
*                                      IF (A(R1).LE.A(R2)) GO TO 100
         SR    R3,R3
         SR    R4,R4
         IC    R3,0(R1)
         IC    R4,0(R2)
         SR    R0,R0
         CR    R3,R4
         BNH   BSORT3
         LA    R0,1
         LR    R3,R4
BSORT3   EQU   *
         L     R4,0(R1)
         L     R5,0(R2)
         EX    R3,SORTCOMP
         BL    BSORT2
         BH    BSORT4
         LTR   R0,R0
         BZ    BSORT2
BSORT4   EQU   *                       T=A(R1)
         ST    R5,0(R1)                A(R1)=A(R2)
         ST    R4,0(R2)                A(R2)=T
         B     BSORT2                  100 CONTINUE
SORTCOMP CLC   0(0,R4),0(R5)
SORTEND  EQU   *
         L     R1,INDXKEY              LOAD END OF NAME POINTERS
         ST    R1,INDXSTCK             SAVE IN START OF STACK
         ST    R1,INDXHASH             SAVE START OF HASH TABLE
***********************************************************************
* HASH TABLE BUILD - WE NOW BUILD A HASH TABLE. THE TABEL IS 1024     *
* BYTES LONG, 4 BYTES PER ENTRY.  THE FIRST HALFWORD OF EACH ENTRY    *
* IS THE POINTER TO THE FIRST WORD IN THIS HASH BUCKET. THE SECOND    *
* HALF WORD IS THE COUNT OF THE NUMBER OF WORDS WHICH BEGIN WITH THIS *
* LETTER.                                                             *
***********************************************************************
HASHB1   EQU   *
         LA    R2,1024(R1)             LOAD END OF HASH TABLE
         C     R2,INDXNAME             ARE WE OUT OF CORE
         BH    OUTOFRAM                YES, QUIT.
         ST    R2,INDXSTCK             SAVE END OF HASH TABLE
         ST    R2,INDXHASH             SAVE START OF HASH TABLE
         MVI   0(R1),X'00'             START TO CLEAR OUT THE TABLE
         MVC   1(256,R1),0(R1)         FIRST MOVE
         MVC   257(256,R1),0(R1)       SECOND MOVE
         MVC   513(256,R1),0(R1)       THIRD MOVE
         MVC   769(255,R1),0(R1)       AND THE LAST MOVE
HASHB2   EQU   *
         SH    R1,=H'8'                BUMP DOWN
         C     R1,INDXADDR             ARE WE DONE?
         BL    LOOP                    YES, GET NEXT CARD
         L     R2,0(R1)                GET NAME ADDR
         SR    R3,R3                   ZERO R3 FOR INSERT
         IC    R3,0(R2)                INSERT THE FIRST LETTER
         SLL   R3,2                    MULTIPLY BY 4 TO GET OFFSET
         A     R3,INDXKEY              GET HASH ADDR
         LR    R2,R1                   MOVE NAME POINTER POINTER
         S     R2,INDXADDR             GET DISTANCE INTO WORD TABLE
         SRL   R2,3                    DIVIDE BY 8 TO GET WORD NUMBER
         LA    R2,1(R2)                SET INDEX ORIGIN TO 1
         STH   R2,0(R3)                SAVE WORD NUMBER IN H TABLE
         B     HASHB2                  AND GET NEXT WORD
***********************************************************************
* WE NOW TAKE LOOK AT EACH WORD ON THE LINE AND SEE IF IT IS IN OUR   *
* SYMBOL TABLE USING A HASHING FUNCTION. OUR HASHING FUNCTION IS THE  *
* FIRST LETTER IN THE WORD.  I FIRST USED A BINARY SEARCH, BUT IT WAS *
* ABOUT TWICE AS SLOW AS THIS HASING FUNCTION.                        *
*   IF THE WORD IS FOUND, WE STORE A 4 BYTE ENTRY RIGHT AFTER         *
* THE HASH TABLE ENTRIES IN THE AQUIRED STORAGE. THE FIRST 2 BYTES    *
* OF THESE ENTRIES IS THE RELATIVE WORD POINTER NUMBER, AND THE SECOND*
* 2 BYTES IS THE PAGE NUMBER. IF THE PAGE NUMBER IS EQUAL TO THE HIGH *
* PAGE  NUMBER, THEN WE DO NOT STORE IT AS WE HAVE ALREADY RECORDED   *
* THIS PAGE, OTHERWISE, WE STORE IT AND UPDATE THE HIGH PAGE NUMBER.  *
***********************************************************************
PINDEX   EQU   *
         TM    FLAG1,F1INDX            ARE WE PROCESSING THE INDEX?
         BZR   R8                      NO, RETURN
         TR    CARD(72),TRTABLE        TRANSLATE IT TO UPPER CASE
         LA    R1,CARD                 LOAD START OF CARD
         LA    R2,CARD+71              LOAD END OF CARD
         BAL   R7,FLEFT                FIND START OF WORD
         CR    R1,R2                   ARE WE OFF THE END OF THE CARD
         BER   R8                      YES, MUST BE A BLANK CARD
         B     HASH1                   YES, FIND THE WORD
BINCOMP  CLC   0(0,R6),0(R1)
BINSRCH1 EQU   *
         BAL   R7,FINDSP               FIND THE NEXT SPACE
         BAL   R7,FLEFT                FIND THE LEFT SIDE OF WORD
         CR    R1,R2                   ARE WE DONE?
         BNLR  R8                      YES, RETURN
         L     R4,INDXTMES             LOAD NUMBER OF WORDS SEARCHED
         LA    R4,1(R4)                UP COUNT
         ST    R4,INDXTMES             AND SAVE IT
***********************************************************************
* HASH TABLE SEARCH                                                   *
***********************************************************************
HASH1    EQU   *
         SR    R4,R4                   ZERO FOR INSERT LETTER
         IC    R4,0(R1)                INSERT FIRST LETTER
         SLL   R4,2                    MULTIPLY BY 4
         A     R4,INDXKEY              GET OFFSET INTO TABLE
         LH    R5,2(R4)                LOAD LETTER COUNT
         LA    R5,1(R5)                UP COUNT
         STH   R5,2(R4)                AND SAVE IT
         LH    R4,0(R4)                GET START FOR THIS LETTER
         LTR   R4,R4                   ANY WORDS WITH THIS LETTER?
         BZ    BINSRCH1                NO, GET NEXT WORD
         BCTR  R4,0                    GET WORD NO AT INDEX ORIGIN 0
         SLL   R4,3                    MULTIPLY BY 8 FOR OFFSET
         A     R4,INDXADDR             POINT TO FIRST WORD
HASH2    EQU   *
         L     R6,0(R4)                LOAD POINTER TO NAME
         SR    R5,R5                   ZERO FOR INSERT
         IC    R5,0(R4)                GET LENGTH
         EX    R5,BINCOMP              ARE THEY EQUAL?
         BE    HASH4                   YES, RECORD IT
         BH    BINSRCH1                NO, TOO HIGH, GET NEXT WORD
HASH3    EQU   *
         L     R5,INDXCOMP             LOAD COMPARE COUNT
         LA    R5,1(R5)                UP COUNT
         ST    R5,INDXCOMP             SAVE COMPARE COUNT
         LA    R4,8(R4)                LOOK AT NEXT INDEX ENTRY
         C     R4,INDXKEY              AT END OF LIST?
         BH    BINSRCH1                YES, GET NEXT WORD
         B     HASH2                   AND BACK FOR MORE
HASH4    EQU   *
         CLC   4(2,R4),PAGECNT+2       SAME PAGE AS LAST TIME?
         BE    HASH3                   YES, DON'T RECORD IT
         MVC   4(2,R4),PAGECNT+2       SAVE NEW PAGE NUMBER
         LR    R3,R4
         S     R3,INDXADDR             GET OFFSET
         SRL   R3,3                    GET WORD NUMBER -1
         LA    R3,1(R3)                GET WORD NUMBER
         L     R5,INDXSTCK             LOAD ADDR OF PAGE INDEX
         STH   R3,0(R5)                SAVE WORD NUMBER
         MVC   2(2,R5),PAGECNT+2       AND PAGE NUMBER
         LA    R5,4(R5)                UP POINTER
         ST    R5,INDXSTCK             AND SAVE IT
         C     R5,INDXNAME             ARE WE OUT OF ROOM?
         BL    HASH3                   NO, SEE IF ANY MORE HITS
         B     OUTOFRAM                YES, ABEND US
***********************************************************************
* SPELL - READ IN THE WORDS WHICH NEED TO BE CHECK                    *
***********************************************************************
SPELL    EQU   *
         OPEN  (WORDS,INPUT)           OPEN THE WORDS FILE
         LTR   R15,R15                 DID IT OPEN UP OK?
         BNZ   ECHO                    NO, ECHO THE CONTROL CARD
         LA    R1,CARD+7               LOAD START OF OPERAND
         LA    R2,CARD+30              LOAD END OF OPERAND
         BAL   R8,GETNUM               GET THE NUMBER
         LTR   R1,R1                   ANY OPERAND?
         BZ    GETSPELL                NO, GET THE STORAGE
         ST    R1,SPELLGET             SAVE THE AMOUNT
GETSPELL EQU   *
         L     R8,SPELLGET             LOAD AMOUNT TO GETMAIN
         GETMAIN R,LV=(R8)             GET THE STORAGE.
         ST    R1,SPELLVA              LOAD THE SPELLING ADDR
         A     R1,SPELLGET             ADD AMOUNT TO GET LAST ADDRT
         ST    R1,SPELLME              SAVE MISSPELLED ENDING ADDR
         S     R1,=F'3000'             SUBTRACT 3K FOR MISSPELL LIST
         ST    R1,SPELLVE              AND SAVE ENDING ADDR FOR WORD LS
         ST    R1,SPELLMA              SAVE MISSPELLING ADDR
         L     R3,SPELLVE              LOAD ENDING ADDR
         LA    R4,0                    INIT WORD COUNT
         LA    R5,GETWORDS             LOAD ADDR TO GET
WORDPUT  EQU   *
         BALR  R8,R5                   READ A RECORD
         TR    CARD(72),TRTABLE        TR TO UPPER CASE
         CLC   CARD(7),=CL7'..SPELL'   ARE WE DONE
         BE    EOFSPELL                YES
         CLC   CARD(7),=CL7'/*SPELL'   ARE WE DONE
         BE    EOFSPELL                YES
         LA    R4,1(R4)                INCREMENT WORD COUNT
         LA    R1,CARD-1               LOAD START ADDR
         LA    R2,CARD+71              LOAD END ADDR
WORDPUT1 EQU   *
         LA    R1,1(R1)                UP POINTER
         CR    R1,R2                   ARE WE DONE?
         BH    WORDPUT                 YES, GET NEXT CARD
         CLI   0(R1),C' '              A SPACE?
         BE    WORDPUT1                YES, GET NEXT CHAR
         LR    R14,R1                  SAVE R1
SPELL1   EQU   *
         LA    R1,1(R1)                UP POINTER
         CLI   0(R1),C' '              A BLANK?
         BNE   SPELL1
         LR    R15,R1                  GET END ADDR
         SR    R15,R14                 GET LENGTH
         SR    R3,R15                  BUMP DOWN POINTER
         BCTR  R3,0                    AND 1 FOR LENGTH POINTER
         C     R3,SPELLVA              HAVE WE RUN OUT OF ROOM?
         BNH   SPELLOUT                YES, ABEND
         STC   R15,0(R3)               SAVE LENGTH
         BCTR  R15,0                   SUBTRACT 1 FOR EXECUTE
         EX    R15,SPELLEXC            MOVE IN THE WORD
         B     WORDPUT1                AND GET THE NEXT WORD
GETWORDS EQU   *
         GET   WORDS,CARD
         BR    R8
SPELLEXC MVC   1(0,R3),0(R14)
SPELLOUT EQU   *
         ABEND 20
EOFWORDS EQU   *
         CLOSE WORDS
         LA    R5,GETIT
         B     WORDPUT
EOFSPELL EQU   *
         ST    R3,SPELLVW              AND SAVE THE WORDS ADDR
         SLL   R4,3                    MULTIPLY BY 8 FOR SAFETY.
         SR    R3,R4                   DO WE HAVE ROOM FOR THE POINTERS
         C     R3,SPELLVA              WELL?
         BNH   SPELLOUT                NOPE
         L     R3,SPELLVW              LOAD WORD ADDR AGAIN.
         S     R3,SPELLVA              AND GET LOWER ADDR
         SRL   R3,2                    DIVIDE BY 4 TO GET NO. OF BUCKET
* SEE HOW MANY BITS WE USE.
         LA    R4,0                    ZERO SO FAR
FINDBIT  EQU   *
         SRL   R3,1                    DIVIDE BY 2
         LA    R4,1(R4)                THATS 1 BIT
         C     R3,=F'1'                ARE WE AT ONE AGAIN?
         BNE   FINDBIT                 NO, KEEP GOING
* R4 HAS THE NUMBER OF BITS WE USE FROM EACH WORD
         ST    R4,SPELLBIT             SAVE NO OF BITS
         L     R3,SPELLVW              LOAD START OF WORDS
SPELLBUC EQU   *
         C     R3,SPELLVE              LOAD ENDING ADDR
         BNL   SPELL3
         SR    R2,R2                   ZERO R2
         IC    R2,0(R3)                GET THE LENGTH OF THE WORD
         BCTR  R2,0
         MVC   WORKLINE(30),BLANK
         EX    R2,SPELLMOV             MOVE IN THE WORD
         L     R1,WORKLINE             LOAD THE FIRST PART OF THE WORD
         MH    R1,WORKLINE             MULTIPLY TO GET A HASH
         SR    R0,R0                   ZERO R0
         SLL   R1,8                    SHIFT OFF THE FIRST 8 BITS
         SLDL  R0,0(R4)                AND MOVE OFFSET.
         LR    R5,R0                   LOAD OFFSET NUMBER.
         SLL   R5,2                    AND MULTIPLY BY 4
         A     R5,SPELLVA              ADD TO BEGIN TO GET OFFSET
FINDBUCK EQU   *
         CLC   0(4,R5),=F'0'           A WHOLE FOR THE BUCKET?
         BE    FOUNBUCK                YES
         LA    R5,4(R5)                UP COUNTER
         C     R5,SPELLVW              ARE WE OFF THE END YET?
         BL    FINDBUCK                NO, CHECK THE NEXT SPACE
         L     R5,SPELLVA              START AT THE BEGINNING
         B     FINDBUCK
SPELLMOV MVC   WORKLINE(0),1(R3)
FOUNBUCK EQU   *
         ST    R3,0(R5)                SAVE THE ADDR
         LA    R3,2(R3)                GET PAST THE LENGTH
         AR    R3,R2                   UP POINTER PAST CHARS
         B     SPELLBUC                AND STORE THE NEXT WORD
SPELL3   EQU   *
         OI    FLAG1,F1SPELL           MARK WE ARE CHECKING SPELLING
         B     LOOP
***********************************************************************
* SPELLCHK - CHECK THE SPELLING ON A LINE                             *
***********************************************************************
SPELLCHK EQU   *
         TR    CARD(72),TRTABLE        TRANSLATE IT TO UPPER CASE
         LA    R1,CARD-1               LOAD START OF CARD
         LA    R2,CARD+72              LOAD END ADDR
SPELCHK1 EQU   *
         MVI   0(R1),C' '              MOVE IN A SPACE
         LA    R1,1(R1)                UP COUNTER
         CR    R1,R2                   ARE WE DONE WITH THIS CARD?
         BNL   SPELCHKE                YES
         CLI   0(R1),C'A'              A SPECIAL CHAR?
         BL    SPELCHK1                YES, GET THE NEXT CHAR
         CLI   0(R1),C'Z'              A NUMBER?
         BH    SPELCHK1                YES, GET THE NEXT CHAR
         LR    R3,R1                   SAVE START OF STRING
SPELCHK2 EQU   *
         LA    R1,1(R1)                UP COUNTER
         CR    R1,R2                   ARE WE DONE WITH THIS CARD?
         BH    SPELCHK3                YES
         CLI   0(R1),C'A'              A SPECIAL CHAR?
         BL    SPELCHK3                YES, WE ARE AT THE END
         CLI   0(R1),C'Z'              A NUMBER?
         BNH   SPELCHK2                NO, GET THE NEXT CHAR
SPELCHK3 EQU   *
         CLI   0(R1),C''''             AN APOSTROPY?
         BNE   SPELCHKB                NO, SKIP CONTRACTION CHK
         CLI   1(R1),C'A'              IS IT FOLLOWED BY A LETTER?
         BNL   SPELCHK2                YES, KEEP LOOKING
SPELCHKB EQU   *
         L     R15,SPELLWDS            LOAD NUMBER OF WORDS CHECKED
         LA    R15,1(R15)              UP COUNT
         ST    R15,SPELLWDS            AND SAVE IT
         LR    R4,R1                   LOAD ENDING ADDR
         SR    R4,R3                   GET LENGTH
         MVC   WORKLINE(30),BLANK
         BCTR  R4,0                    SUBTRACT 1 FOR EX
         EX    R4,SPELMOVE             MOVE IN THE WORD
         L     R15,WORKLINE            LOAD THE FIRST PART OF THE WORD
         MH    R15,WORKLINE            MULTIPLY TO GET A HASH
         SR    R14,R14                 ZERO R0
         SLL   R15,8                   SHIFT OFF THE FIRST 8 BITS
         L     R5,SPELLBIT             LOAD THE NUMBER OF BITS
         SLDL  R14,0(R5)               AND MOVE OFFSET.
         LR    R5,R14                  LOAD OFFSET NUMBER.
         SLL   R5,2                    AND MULTIPLY BY 4
         A     R5,SPELLVA              ADD TO BEGIN TO GET OFFSET
         S     R5,=F'4'                SUBTRACT 4 FOR LOOP
         SR    R15,R15                 ZERO LENGHT OF SEARCH
SPELCHK4 EQU   *
         LA    R15,1(R15)              UP COUNT
         LA    R5,4(R5)                UP POINTER
         C     R5,SPELLVW              ARE WE OFF THE END YET?
         BL    SPELCHKA                NO, DONT START OVER
         L     R5,SPELLVA              START AT THE BEGINNING
SPELCHKA EQU   *
         CLC   0(4,R5),=F'0'           ARE WE AT THE END?
         BE    SPELCHK6                YES, WORD IT MISPELLED.
         SR    R14,R14                 ZERO R14 FOR INSERT
         L     R6,0(R5)                LOAD POINTER
         IC    R14,0(R6)               GET LENGTH
         BCTR  R14,0                   SUBTRACT 1 FOR EX
         CH    R4,=H'30'               IS R4 30 OR LESS?
         BNL   SPELCHK5                NO, SKIP LENGTH COMPARE
         CR    R4,R14                  ARE THE LENGTHS THE SAME?
         BNE   SPELCHK4                NO, GET THE NEXT WORD
SPELCHK5 EQU   *
         EX    R14,SPELLCLC            ARE THEY EQUAL?
         BNE   SPELCHK4                NO, GET THE NEXT WORD
         IC    R0,BLANK                INSERT A BLANK SINCE WORD FOUND
         B     SPELCHK7
SPELMOVE MVC   WORKLINE(0),0(R3)
SPELLCLC CLC   1(0,R6),WORKLINE
SPELCHK6 EQU   *
         IC    R0,=C'_'                MOVE IN AN UNDERLINE
         OI    FLAG2,F2SPELLX          MARK AN INVALID WORD
SPELCHK7 EQU   *
         STC   R0,0(R3)                SAVE PUT IN THE CHAR
         LA    R3,1(R3)                UP POINTER
         CR    R3,R1                   ARE WE DONE?
         BNE   SPELCHK7                NO, MOVE IN THE NEXT CHAR
         LR    R3,R15                  MOVE IN BUCKET LENGTH
         A     R3,SPELLCMP             ADD TO TOTAL
         ST    R3,SPELLCMP             AND RESAVE IT
         C     R15,SPELLMAX            IS THIS THE LONGEST
         BL    SPELCHK8                NO, SO DONT STORE IT
         ST    R15,SPELLMAX            SAVE IT
SPELCHK8 EQU   *
         STC   R0,TEMP                 STORE THE CHAR
         CLI   TEMP,C'_'               A MISSPELLED WORD?
         BNE   SPELCHK1                NO, DON'T RECORD IT
         L     R15,SPELLMA             LOAD THE ADDR TO SAVE IT
         LA    R0,5(R15,R4)            GET ADDR OF NEXT ENTRY
         C     R0,SPELLME              ARE WE OUT OF ROOM?
         BNL   SPELCHK1                YES, DON'T RECORD IT
         L     R14,PAGECNT             LOAD THE CURRENT PAGE NUMBER
         ST    R14,0(R15)              SAVE THE PAGE NUMBER
         LA    R14,5(R4)               GET THE LENGTH OF THIS ENTRY
         STC   R14,0(R15)              SAVE THE LENGTH
         EX    R4,SPELMVC1             MOVE IN THE WORD
         ST    R0,SPELLMA              AND SAVE ADDR OF NEXT ENTRY
         L     R14,NOMISS              LOAD CURRENT NUMBER OF MISS WRDS
         LA    R14,1(R14)              UP THE COUNT
         ST    R14,NOMISS              AND RESAVE IT
         B     SPELCHK1                GET THE NEXT WORD
SPELMVC1 MVC   4(0,R15),WORKLINE
SPELCHKE EQU   *
         TM    FLAG2,F2SPELLX          ARE WE TO PRINT A SPELLCHK LINE
         BZR   R8                      NO, RETURN
         L     R5,PADDR                LOAD THE ADDR TO PRINT
         MVI   CARD-1,C'*'             MOVE IN SPELL ERROR SYM
         NI    FLAG2,X'FF'-F2SPELLX
         L     R2,LINECNT              LOAD LINE COUNT
         BCTR  R2,0                    DECREMENT LINE COUNT
         ST    R2,LINECNT              SAVE LINE COUNT
         MVI   0(R5),C'+'              OVER PRINT CONTROL CHAR
         LR    R2,R8                   SAVE RETURN ADDR
         BAL   R8,PRINTIT              PRINT THE LINE
         MVI   CARD-1,C' '             REPLACE THE SPELL SYM ERR
         MVI   0(R5),C' '              RETURN TO SINGLE SPACE
         BR    R2                      PRINT THE LINE
***********************************************************************
* JUSTIFY THE LINE                                                    *
***********************************************************************
JUST01   EQU   *
         XC    JUSTR2(4),JUSTR2        ZERO THE JUST CARD
         LA    R1,CARD                 LOAD START
         LA    R2,CARD+71              LOAD END OF CARD
         L     R3,JUSTCOL              LOAD STARTING COLUMN
         LTR   R3,R3                   IS IT POSITIVE?
         BNM   JUST01D                 YES.
         LPR   R3,R3                   LOAD THE POSITIVE VALUE
JUST01A  EQU   *
         BAL   R7,FLEFT                FIND THE LEFT SIDE
         CR    R1,R2                   DID WE GO TOO FAR?
         BNLR  R8                      YES, JUST RETURN
         LR    R4,R1                   LOAD STARTING POSITION
         LR    R5,R3                   LOAD LENGTH OF SPACES
         BCTR  R5,0                    SUBTRACT 1 SINCE WE FOUND 1
         LTR   R5,R5                   IS R5 ALREADY ZERO?
         BZ    JUST01C                 YES, FIRST WORD IS OK.
         BAL   R7,FINDSP               FIND THE NEXT SPACE
         CR    R1,R2                   DID WE GO TOO FAR?
         BNLR  R8                      YES, JUST RETURN
JUST01B  EQU   *
         LA    R1,1(R1)                UP THE POINTER
         CLI   0(R1),C' '              ARE WE LOOKING AT A SPACE?
         BNE   JUST01C                 GREAT! WE FOUND ANOTHER CHAR.
         BCT   R5,JUST01B              LOOK FOR ANOTHER SPACE
         B     JUST01A                 AND START OVER
JUST01C  EQU   *
         SR    R3,R3                   ZERO R3 FOR THE ADD BELOW
         LR    R1,R4                   RESTORE R1
JUST01D  EQU   *
         AR    R1,R3                   ADD STARTING POSITION
         LR    R3,R2                   LOAD END ADDR
         BAL   R7,FLEFT                FIND THE LEFT SIDE OF THE CARD
         BAL   R7,FRIGHT               FIND THE RIGHT SIDE OF THE CARD
         CR    R3,R2                   IS LINE ALREADY JUSTIFIED?
         BER   R8                      YES, SO RETURN
*-- COUNT NUMBER OF SPACES IN CARD
         LR    R5,R1                   LOAD START ADDR
         BCTR  R5,R0                   SUBTRACT 1 FOR ADD BELOW
         LA    R4,0                    LOAD NUMBER OF SPACES
JUST02   EQU   *
         LA    R5,1(R5)                UP POINTER
         CR    R5,R2                   ARE WE DONE?
         BH    JUST03                  YES
         CLI   0(R5),C' '              ARE WE COMPARING A SPACE?
         BNE   JUST02                  NO, GET THE NEXT CHAR
         LA    R4,1(R4)                UP SPACE COUNT
JUST02A  EQU   *
         LA    R5,1(R5)                UP POINTER
         CR    R5,R2                   ARE WE DONE?
         BH    JUST03                  YES
         CLI   0(R5),C' '              IS THIS A SPACE
         BNE   JUST02                  NO, LOOK FOR SPACE AGAIN
         B     JUST02A                 LOOK FOR FIRST NON-BLANK
JUST03   EQU   *
         LTR   R4,R4                   ANY SPACES?
         BZR   R8                      NO, CAN'T JUSTIFY
         SR    R3,R2                   LOAD NUMBER OF SPACES TO PLACE
*
* NOW: R4 HAS NUMBER OF SPACES IN THE LINE
*      R3 HAS NUMBER OF SPACES WE NEED TO INSERT
*
         CR    R3,R4                   COMPARE THE TWO VALUES
         BHR   R8                      WE CAN'T HANDLE THIS.
         ST    R1,JUSTR1               SAVE THE STARTING COLUMN
         LA    R1,CARD+72              LOAD POSITION TO MOVE
         LA    R5,JUSTIT+72            LOAD BEGINNING OF JUST LINE
         MVC   JUSTIT(71),BLANK        MOVE IN BLANKS
         ST    R2,JUSTR2               SAVE THE ENDING COLUMN
JUST04   EQU   *
         LR    R15,R4                  MOVE INTO ODD REGISTER
         SR    R14,R14                 ZERO EVEN REGISTER
         DR    R14,R3                  GET RATIO
JUST05   EQU   *
         BCTR  R1,R0
         BCTR  R2,R0
         BCTR  R5,R0                   SUBTRACT 1 FROM JUST MASK
         MVC   0(1,R1),1(R2)           MOVE IN THE CHAR
         CLI   1(R2),C' '              A SPACE?
         BNE   JUST05                  KEEP LOOKING
         BCTR  R4,R0                   SUBTRACT 1 SINCE WE FOUND A SPCE
         CLI   0(R2),X'81'             A SPECIAL CHARACTER?
         BL    JUST06                  PUT IN A SPACE ANYWAYS
         BCT   R15,JUST05              LOOP BACK FOR NEXT SPACE
JUST06   EQU   *
         BCTR  R1,R0                   SUBTRACT 1
         BCTR  R5,R0                   SUBTRACT 1 FROM JUST MASK
         MVI   0(R1),C' '              MOVE IN A SPACE
         MVI   0(R5),C'X'              MOVE IN A SPACE
         BCT   R3,JUST04               GET NEXT SPACE
         BR    R8                      RETURN
***********************************************************************
* INCLUDE COMMAND - PROCESS ALTERNATE INPUT                           *
***********************************************************************
INCLUDE0 EQU   *
         LA    R1,CARD+9               LOAD START OF OPERAND
         LA    R2,CARD+71              LOAD END OF OPERAND
         BAL   R7,FLEFT                FIND START
         CR    R1,R2                   ANY OPERAND?
         BE    ECHO                    NO, SKIP CARD
         LR    R3,R1                   SAVE START
INCLUDE1 EQU   *
         LA    R1,1(R1)                UP POINTER
         CLI   0(R1),C' '              END OF OPERAND?
         BE    INCSEQ                  YES, MUST BE SEQUENTIAL
         CLI   0(R1),C'('              END OF OPERAND?
         BE    INCPDS                  YES, MUST BE PDS
         CR    R1,R2                   AT END OF OPERAND?
         BNE   INCLUDE1                NO, LOOK SOME MORE
INCSEQ   EQU   *
         L     R2,AMTSEQ               LOAD AMOUNT TO GET FOR SEQ FILE
         B     INCLUDE2                KEEP PROCESSING
INCPDS   EQU   *
         L     R2,AMTPDS               LOAD AMOUNT TO GET FOR PDS FILE
INCLUDE2 EQU   *
         LR    R4,R1                   SAVE END ADDR
         LR    R5,R1                   SAVE END ADDR
         GETMAIN R,LV=(R2)             GET THE AQUIRED STORAGE
         L     R0,INCSTACK             LOAD PRESENT POINTER
         ST    R0,NEXT(R1)             PUT IT ON THE STACK
         ST    R1,INCSTACK             AND POINT TO THIS GETMAIN
         MVC   FLAG(1,R1),FLAGI        SAVE THE FLAGI
         ST    R2,AMT(R1)              STORE THE AMOUNT GOT
         XC    RECPOS(4,R1),RECPOS(R1) ZERO THE RECORD POSITION
         L     R0,AMTPDS               LOAD PDS AMOUNT OF GETMAIN
         MVI   FLAGI,X'00'             CLEAR THE FLAG
         OI    FLAGI,FIPDS             MARK A PDS.
         CR    R0,R2                   ARE WE PROCESSING A PDS?
         BE    INCL2A                  YES, NO NEED TO MARK SEQ
         NI    FLAGI,X'FF'-FIPDS       TURN OF THE PDS FLAG
         OI    FLAGI,FISEQ             AND TURN ON THE SEQ FLAG
INCL2A   EQU   *
         SR    R4,R3                   GET LENGTH OF DD
         BCTR  R4,0                    SUBTRACT 1 FOR EX
         MVC   SEQDD+40(8),BLANK       BLANK OUT DDNAME
         EX    R4,MOVEDD               MOVE IN DDNAME
         TM    FLAGI,FIPDS             PROCESSING PDS
         BO    INCLUDE3                YES, PROCESS IT
         MVC   DCB(96,R1),SEQDD        MOVE IN THE DCB
         LA    R2,DCB(R1)              POINT TO THE DCB
         OPEN  ((R2),INPUT)            OPEN FILE
         LTR   R15,R15                 OPEN OK?
         BZ    LOOP                    YES, CONTINUE PROCESSING
INCFLUSH EQU   *
         LA    R8,LOOP                 LOAD THE RETURN ADDR
         B     EOFINC                  FREE UP THE AREA
MOVEDD   MVC   SEQDD+40(0),0(R3)
INCLUDE3 EQU   *
         MVC   PDSDD+40(8),SEQDD+40    MOVE IN DDNAME
         MVC   DCB(96,R1),PDSDD        MOVE IN THE DCB
         LA    R4,DCB(R1)              POINT TO THE DCB
         OPEN  ((R4),INPUT)            OPEN FILE
         LTR   R15,R15                 DID FILE OPEN OK?
         BNZ   INCFLUSH                NO, FLUSH INCLUDE
         LR    R1,R5                   RESTORE R1
         LA    R2,CARD+71              LOAD END OF OPERAND
         LA    R3,1(R1)                START PROCESSING MEMBER NAME
         BAL   R7,FINDSP               FIND A SPACE
         CR    R1,R2                   NO END OF OPERAND?
         BE    INCFLUSH                YES, FLUSH THIS INCLUDE
         BCTR  R1,0                    SUBTRACT 2, WE NOW POINT TO
         BCTR  R1,0                    LAST CHAR IN MEMBER NAME
         SR    R1,R3                   GET LENGTH
         MVC   MEMBER(8),BLANK         BLANK OUT MEMBER NAME
         EX    R1,MOVEMEM              MOVE IN MEMBER NAME
         FIND  (R4),MEMBER,D           LOCATE THIS MEMBER
         LTR   R15,R15                 MEMBER FOUND?
         BZ    LOOP                    YES, KEEP PROCESSING
         CLOSE (R4)                    NO, CLOSE FILE
         B     INCFLUSH                FLUSH INCLUDE
MOVEMEM  MVC   MEMBER(0),0(R3)
***********************************************************************
* ULIN COMMAND - UNDERLINE ALL NON BLANKS ON THE NEXT LINE            *
***********************************************************************
ULIN     EQU   *
         BAL   R8,GETIT                GET A CARD
         L     R5,PADDR                LOAD ADDR OF RECORD
         BAL   R8,PRINTIT              PRINT IT
         BAL   R8,ULINE                UNDERLINE IT
         B     LOOP                    GET NEXT CARD
ULINE    EQU   *
         LA    R1,CARD                 START POINTER
         LA    R2,72                   UNDERLINE UP TO 72 CHARS
ULINE1   EQU   *
         CLI   0(R1),C' '              IS IT A SPACE
         BE    ULINE2                  YES, DON'T SUBSTITUTE
         MVI   0(R1),C'_'              REPLACE WITH UNDERBAR
ULINE2   EQU   *
         LA    R1,1(R1)                UP POINTER
         BCT   R2,ULINE1               LOOP IF NOT 72 TIMES
         L     R2,LINECNT              LOAD LINE COUNT
         BCTR  R2,0                    DECREMENT LINE COUNT
         ST    R2,LINECNT              SAVE LINE COUNT
         L     R5,PADDR                LOAD ADDR OF RECORD
         MVI   0(R5),C'+'              OVER PRINT CONTROL CHAR
         LR    R2,R8                   SAVE RETURN ADDR
         BAL   R8,PRINTIT              PRINT THE LINE
         MVI   0(R5),C' '              RETURN TO SINGLE SPACE
         BR    R2                      RETURN
***********************************************************************
* GETIT SUBR - GET A RECORD FROM APPROPRIATE INPUT                    *
***********************************************************************
GETIT    EQU   *
         NI    FLAG1,X'FF'-F1LEVEL     TURN OFF THE LEVEL INDICATOR
         TM    FLAG1,F1TOP             TOP OF PAGE PROCESSING
         BO    GETIT5                  YES, PROCESS IT
         TM    FLAGI,FISEQ             A SEQUENTIAL INCLUDE?
         BO    GETIT1                  YES, PROCESS IT
         TM    FLAGI,FIPDS             A PDS INCLUDE?
         BO    GETIT2                  YES, PROCESS IT
         GET   SYSUT1,CARD             READ FROM SYSUT1
         B     GETIT6                  AND RETURN
GETIT1   EQU   *
         L     R1,INCSTACK             LOAD INCLUDE STACK POINTER
         LA    R1,DCB(R1)              UP POINTER TO THE DCB
         GET   0(R1),CARD              READ A CARD
         B     GETIT6                  AND RETURN
GETIT2   EQU   *
         L     R1,INCSTACK             LOAD ADDRESS OF BLOCK
         TM    FLAGI,FIREAD            HAVE WE ISSUED A READ YET?
         BZ    GETIT3                  NO, READ FILE
         LA    R15,BLOCK(R1)           POINT TO RECORD BLOCK
         AH    R15,82+DCB(R1)          ADD NUMBER OF BYTES IN REC
         L     R14,RECPOS(R1)          LOAD RECORD POSITION
         CR    R15,R14                 ANY RECORDS LEFT?
         BH    GETIT4                  GET NEXT RECORD
GETIT3   EQU   *
         OI    FLAGI,FIREAD            SHOW WE HAVE READ
         STM   R2,R3,DOUBLE            SAVE EM
         LA    R2,DCB(R1)              POINT R2 TO THE DCB
         LA    R3,BLOCK(R1)            POINT R3 TO READ IN AREA
         READ  PDSECB,SF,(R2),(R3),'S'
         CHECK PDSECB
         L     R1,INCSTACK             LOAD ADDRESS OF BLOCK
         LM    R2,R3,DOUBLE            RESTORE EM
         LA    R14,BLOCK(R1)           LOAD ADDRESS OF BLOCK
GETIT4   EQU   *
         MVC   CARD(80),0(R14)         MOVE IN RECORD
         LA    R14,80(R14)             UP RECORD LOCATION BY 80
         L     R1,INCSTACK             LOAD INCLUDE STACK POINTER
         ST    R14,RECPOS(R1)          SAVE RECORD POSITION
         B     GETIT6                  AND RETURN
GETIT5   EQU   *
         CLC   TOPCNT1(1),TOPCNT       ARE WE DONE?
         BNL   GETIT5A                 YES
         SR    R1,R1                   ZERO NUMBER OF LINES
         IC    R1,TOPCNT1              LOAD LINE TO PRINT
         MH    R1,=H'80'               UP IT BY 80
         LA    R1,TOPLINE(R1)          GET OFFSET
         MVC   CARD(80),0(R1)          AND MOVE IT IN
         SR    R1,R1                   ZERO LINE COUNT
         IC    R1,TOPCNT1              LOAD NUMBER OF LINES
         LA    R1,1(R1)                UP POINTER
         STC   R1,TOPCNT1              AND SAVE IT
         BR    R8                      AND RETURN
GETIT5A  EQU   *
         MVC   CARD(80),TOPSAVE        MOVE IN THE SAVED LINE
         NI    FLAG1,X'FF'-F1TOP       AND TURN OFF FLAG
         BR    R8                      AND RETURN
GETIT6   EQU   *
         ST    R2,TEMP2                SAVE R2 FOR AWHILE
         CLC   LEVEL(2),=H'0'          ARE WE DOING LEVELS?
         BE    GETIT7                  NO, NO NEED TO CHECH IT
         LA    R1,CARD+78              LOAD START OF LEVEL NUMBER
         LA    R2,CARD+83              LOAD END OF LEVEL NUMBER
         LR    R0,R8                   SAVE R8 FOR A SECOND
         BAL   R8,GETNUM               GET THE NUMBER
         LR    R8,R0                   AND RESTORE IT
         LTR   R1,R1                   IS THERE A NUMBER THERE?
         BZ    GETIT7                  NO, SKIP CHECK
         LH    R2,LEVEL                GET THE LEVEL NUMBER
         CR    R1,R2                   IS LEVEL BIGGER?
         BL    GETIT7                  YES, DON'T OI THE FLAG
         OI    FLAG1,F1LEVEL           WE HAVE FOUND A NEW RECORD
GETIT7   EQU   *
         L     R2,TEMP2                RESTORE R2
         MVC   CARD+72(8),BLANK        BLANK OUT THE SEQ NOS
         BR    R8                      AND RETURN
EOFINC   EQU   *
         STM   R2,R3,DOUBLE            SAVE EM
         L     R2,INCSTACK             LOAD ADDR OF INCLUDE AREA
         LA    R2,DCB(R2)              LOAD DCB ADDR
         CLOSE ((R2))                  CLOSE THE FILE
         L     R2,INCSTACK             RELOAD ADDR OF INCLUDE AREA
         L     R3,AMT(R2)              LOAD AMOUNT TO FREE
         MVC   FLAGI(1),FLAG(R2)       RESTORE THE FLAG
         MVC   INCSTACK(4),NEXT(R2)    POINT TO THE NEXT STACK
         FREEMAIN R,LV=(R3),A=(R2)     FREE AQUIRED STORAGE
         LM    R2,R3,DOUBLE            RESTORE THE REGISTERS
         B     GETIT                   GET NEXT RECORD
***********************************************************************
* PRINTIT SUBR - PRINT TITLES IF WE NEED TO, PRINT OUT THE LINE       *
***********************************************************************
PRINTIT  EQU   *
         CLC   LINECNT(4),MAXLINE      COMPARE LINE COUNT
         BL    PRINTIT1                NOT EXCEEDED.
         L     R1,PAGECNT              LOAD PAGE COUNT
         LA    R1,1(R1)                UP PAGE COUNT
         ST    R1,PAGECNT              SAVE PAGE COUNT
         CVD   R1,DOUBLE               CONVERT IT
         MVC   TEMP(6),=X'402020202021' MOVE IN EDIT MASK
         ED    TEMP(6),DOUBLE+5        EDIT IT
         MVC   PGNO(6),BLANK           BLANK OUT PAGE NUMBER
         LA    R1,6                    MOVE NO MORE THAT 6 CHARS
         LA    R14,PGNO                DEST FIELD
         LA    R15,TEMP                SOURCE FIELD
PRNT1    EQU   *
         CLI   0(R15),C' '             A BLANK
         BE    PRNT2                   YES, SKIP MOVE
         MVC   0(1,R14),0(R15)         MOVE IN CHARACTER
         LA    R14,1(R14)              UP POINTER BY 1
PRNT2    EQU   *
         LA    R15,1(R15)              UP POINTER
         BCT   R1,PRNT1                BACK FOR MORE
         LA    R15,TITLE1              LOAD ADDR FOR 1ST TITLE
         AH    R15,TSOFFSET            ADD THE TSO OFFSET
         PUT   SYSUT3,0(R15)           PUT
         LA    R15,TITLE2              LOAD ADDR FOR 2ND TITLE
         AH    R15,TSOFFSET            ADD THE TSO OFFSET
         PUT   SYSUT3,0(R15)              OUT THE
         LA    R15,TITLE3              LOAD ADDR FOR 3RD TITLE
         AH    R15,TSOFFSET            ADD THE TSO OFFSET
         PUT   SYSUT3,0(R15)                     TITLES
         PUT   SYSUT3,TITLE4
         LA    R1,4                    LOAD IN LINE COUNT
         ST    R1,LINECNT              SAVE LINE COUNT
         CLI   TOPCNT,X'00'            IS THERE ANY TOP OF PAGE LINES?
         BE    PRINTIT1                NO, CONTINUE
         OI    FLAG1,F1TOP             YES, MARK IT
         MVI   TOPCNT1,X'00'           MARK NUMBER OF LINES PRINTED
         MVC   TOPSAVE(80),16(R5)      SAVE THE LINE
         BR    R8
PRINTIT1 EQU   *
         TM    FLAG1,F1LEVEL           DO WE MARK THIS AS A NEW REC
         BZ    PRINTIT2                NO
         MVI   CARD-2,C'|'             MOVE IN THE MARK
PRINTIT2 EQU   *
         L     R1,LINECNT              LOAD LINE COUNT
         LA    R1,1(R1)                UP LINE COUNT
         ST    R1,LINECNT              SAVE LINE COUNT
         PUT   SYSUT3,0(R5)            WRITE THE RECORD
         MVI   CARD-2,C' '             REPLACE | WITH A BLANK
         BR    R8                      RETURN
***********************************************************************
* TOC SUBR - PRINT TOC LINE, AND TITLES IF WE NEED TO                 *
***********************************************************************
TOC      EQU   *
         L     R1,TOCLNNO              LOAD TOC LINE COUNT
         C     R1,MAXLINE              TIME FOR NEW PAGE
         BL    TOC1                    NO, SKIP NEW PAGE
         L     R1,TOCPGNO              LOAD PAGE NUMBER
         SLL   R1,2                    MULTIPLY BY FOUR
         LA    R2,RTABLE               LOAD ROMAN NUMBER TABLE
         AR    R1,R2                   GET DISPLACEMENT
         MVC   TOCPOUT(4),0(R1)        MOVE IN ROMAN PAGE COUNT
         L     R1,TOCPGNO              LOAD PAGE NUMBER
         LA    R1,1(R1)                INCREMENT BY 1
         ST    R1,TOCPGNO              SAVE PAGE NUMBER
         LA    R1,4                    PRINTED 4 LINES
         ST    R1,TOCLNNO              STORE LINE COUNT
         TM    FLAG2,F2TOC             DO WE PRINT THE TOC?
         BO    TOC1                    NO, SKIP PRINT
         LA    R15,TOCTIT1             LOAD ADDR OF TITLE
         AH    R15,TSOFFSET            ADD THE TSO OFFSET
         PUT   SYSUT2,0(R15)           PRINT TITLE1
         LA    R15,TOCTIT2             LOAD ADDR OF TITLE
         AH    R15,TSOFFSET            ADD THE TSO OFFSET
         PUT   SYSUT2,0(R15)           PRINT TITLE2
         LA    R15,TITLE3              LOAD ADDR OF TITLE
         AH    R15,TSOFFSET            ADD THE TSO OFFSET
         PUT   SYSUT2,0(R15)           PRINT TITLE3
         PUT   SYSUT2,TITLE4           PRINT LAST TITLE
TOC1     EQU   *
         L     R1,TOCLNNO              LOAD LINE COUNT
         LA    R1,1(R1)                UP LINE COUNT
         ST    R1,TOCLNNO              SAVE LINE COUNT
         LH    R15,LEVELSEC            LOAD THE PRESENT LEVEL
         BCTR  R15,0                   SUBTRACT 1
         BCTR  R15,0                   SUBTRACT 1 AGAIN TO MAKE 2
         BCTR  R15,0                   SUBTRACT 1 AGAIN TO MAKE 3
         SLL   R15,2                   MULTIPLY BY FOUR
         LA    R2,TOCCARD              LOAD ADDR OF CARD
         AR    R2,R15                  GET OFFSET
         MVC   TOCLINE(133),BLANK      BLANK TOC LINE
         MVC   6(50,R2),CARD           MOVE IN TITLE
         MVC   TOCPG(6),PGNO           MOVE IN PAGE NUM
         LA    R1,TOCPG-2              LOAD ADDR TO START WITH PERIODS
TOC3     EQU   *
         CLI   0(R1),C' '              A SPACE?
         BNE   TOC4                    NO, QUIT
         MVI   0(R1),C'.'              REPLACE WITH DOT
         BCTR  R1,0                    SUBTRACT 1
         CLI   0(R1),C' '              A SPACE?
         BNE   TOC4                    NO, QUIT
         BCTR  R1,0                    YES, SUBTRACT 1
         B     TOC3                    BACK FOR MORE
TOC4     EQU   *
         TM    FLAG2,F2TOC             DO WE PRINT THE TOC?
         BOR   R8                      NO, SKIP PRINT
         LA    R15,TOCLINE             LOAD ADDR OF PRINT LINE
         AH    R15,TSOFFSET            ADD THE TSO OFFSET
         PUT   SYSUT2,0(R15)           WRITE TABLE OF CONTENTS
         BR    R8
***********************************************************************
* GETNUM SUBR - GET THE NUMERIC ARGUMENT STARTING AT R1 AND ENDING BY *
*        R2. RETURN NUMBER IN R1 AND END OF ARGUMENT IN R2.           *
***********************************************************************
GETNUM   EQU   *
         CLI   0(R1),C' '              A SPACE?
         BNE   GETNUM1                 NO,FOUND START OF OPERAND
         CR    R2,R1                   ARE WE DONE YET?
         BNH   GETNUM0                 YES, NO OPERAND
         LA    R1,1(R1)                NO, UP POINTER
         B     GETNUM                  LOOK AT NEXT CHAR
GETNUM1  EQU   *
         LR    R3,R1                   SAVE START
GETNUM2  EQU   *
         CLI   0(R1),X'F0'             IS THIS A DIGIT?
         BL    GETNUM0                 NO, NO OPERAND.
         LA    R1,1(R1)                UP POINTER
         CLI   0(R1),C' '              A SPACE
         BE    GETNUM3                 YES, FOUND END
         CR    R2,R1                   ARE WE DONE?
         BH    GETNUM2                 NO, LOOK AT NEXT CHAR
GETNUM0  EQU   *
         SR    R1,R1                   ZERO R1
         BR    R8                      AND RETURN
GETNUM3  EQU   *
         LR    R2,R1                   SAVE END ADDR OF OPERAND
         SR    R1,R3                   GET LENGTH
         BCTR  R1,0                    SUBTRACT 1
         EX    R1,PACK01               PACK IT
         CVB   R1,DOUBLE               CONVERT IT
         BR    R8                      RETURN
PACK01   PACK  DOUBLE(8),0(0,R3)
***********************************************************************
* FLEFT SUBR - FIND THE LEFT SIDE OF THE ARGUMENT.                    *
***********************************************************************
FLEFT    EQU   *
         CLI   0(R1),C' '              A SPACE
         BNER  R7                      NO, RETURN
         CR    R1,R2                   ARE WE DONE
         BNLR  R7                      YES, NO OPERAND
         LA    R1,1(R1)                UP POINTER
         B     FLEFT                   KEEP LOOKING
***********************************************************************
* FRIGHT SUBR - FIND THE RIGHT SIDE OF THE ARGUMENT.                  *
***********************************************************************
FRIGHT   EQU   *
         CLI   0(R2),C' '              A SPACE
         BNER  R7                      NO,RETURN
         CR    R1,R2                   ARE WE DONE?
         BNLR  R7                      YES, RETURN
         BCTR  R2,0                    NO, SUBTRACT 1
         B     FRIGHT                  KEEP LOOKING
***********************************************************************
* FINDSP SUBR - FIND THE NEXT SPACE                                   *
***********************************************************************
FINDSP   EQU   *
         CLI   0(R1),C' '              A SPACE?
         BER   R7                      YES, RETURN
         CR    R1,R2                   ARE WE DONE?
         BNLR  R7                      YES, RETURN
         LA    R1,1(R1)                UP POINTER
         B     FINDSP                  LOOK SOME MORE
***********************************************************************
* PRINT THE TEXT WHICH WAS SPOOLED OUT TO SYSUT3.                     *
***********************************************************************
EOF      EQU   *
         TM    FLAG2,F2BODY            DO WE PRODUCE THE REPORT BODY
         BZ    EOF0                    YES, SO SKIP CLOSE/OPEN
         CLOSE SYSUT3
         OPEN  (SYSUT3,OUTPUT)
EOF0     EQU   *
         TM    FLAG1,F1INDX            ARE WE PROCESSING AN INDEX
         BZ    EOFSTATS                NO, CLOSE FILES
         TM    FLAG2,F2PRINDX          ARE WE PRINTING AN INDEX
         BO    EOF1                    NO, SKIP THE WRITE.
         MVC   LINECNT(4),MAXLINE      FORCE SKIP TO TOP OF PAGE
         MVC   SECNO(10),=CL10'INDEX'  MOVE IN THE SECTION NUMBER
         MVC   SECTITLE(40),BLANK      BLANK OUT THE SECTION NAME
         MVC   SECTITLE+13(13),=CL13'I  N  D  E  X'
         LA    R5,BLANK                WRITE OUT A BLANK LINE
         BAL   R8,PRINTIT              BAL TO THE PRINT SUBROUTINE
         MVC   CARD(80),BLANK          BLANK OUT CARD
         MVC   CARD(13),=CL13'I  N  D  E  X'
         SR    R4,R4                   ZERO R4
         BAL   R8,TOC                  PRINT THE TABLE OF CONTENTS ENT
EOF1     EQU   *
         L     R1,INDXHASH               BUBBLE SORT
         SH    R1,=H'4'
***********************************************************************
* NOW, WE SORT THE INDEX PAGE ENTRIES BY WORD ORDER. YES, YOU GOT IT, *
* WE USE THE OLE BUBBLE SORT.                                         *
***********************************************************************
BUBSORT1 EQU   *
         LA    R1,4(R1)                 DO 100 R1=1,N
         C     R1,INDXSTCK
         BNL   BUBEND
         LR    R2,R1
BUBSORT2 EQU   *                        DO 100 R2=R1,N
         LA    R2,4(R2)
         C     R2,INDXSTCK
         BNL   BUBSORT1
         CLC   0(4,R1),0(R2)            IF(A(R1).LE.A(R2)) GO TO 100
         BNH   BUBSORT2
         L     R3,0(R1)                 T=A(R1)
         MVC   0(4,R1),0(R2)            A(R1)=A(R2)
         ST    R3,0(R2)                 A(R2)=T
         B     BUBSORT2                 100 CONTINUE
BUBEND   EQU   *
         L     R1,INDXSTCK             LOAD START OF PRINT BUFFER
         ST    R1,INDXADD1             SAVE IT
         ST    R1,INDXADD2             SAVE IT
         L     R3,MAXLINE              LOAD MAXIMUM NUMBER OF LINES
         MH    R3,=H'80'               MULTIPLY IT BY 80 BYTES
         AR    R1,R3                   GET ENDING ADDR
         ST    R1,INDXFREE             SAVE END OF BUFFER
         C     R1,INDXNAME             DO WE HAVE ROOM
         BNL   OUTOFRAM                NOPE, ABEND US
         L     R3,INDXHASH             LOAD START OF PAGE POINTERS
         LH    R4,0(R3)                LOAD WORD NUMBER
         BCTR  R4,0                    SUBTRACT 1 TO MAKE IT DIFFERENT
         SH    R3,=H'4'                SUBTRACT 4
         MVC   FAKELCNT(4),LINECNT     INIT LINE COUNT
         MVC   CARD(80),BLANK          AND BLANK OUT THE CARD
***********************************************************************
* NOW, WE PRINT OUT THE INDEX ENTRIES.                                *
***********************************************************************
INDXPRNT EQU   *
         LA    R3,4(R3)                UP TABLE POINTER
         C     R3,INDXSTCK             ARE WE DONE?
         BNL   ENDINDX                 YES, QUIT
         LH    R2,0(R3)                LOAD WORD NUMBER
         CR    R2,R4                   SAME NAME AS BEFORE?
         BE    INDXPRN1                YES, DON'T PRINT NAME
         CLC   CARD(80),BLANK          ARE WE PRINTING A BLANK LINE?
         BE    INDXPRN0                YES, DON'T PRINT IT
         L     R5,PADDR
         BAL   R7,PRINDX               PRINT THE INDEX
INDXPRN0 EQU   *
         TM    FLAG2,F2ALLIN           PRINT ALL INDEX ENTRIES
         BZ    INDXPR00                YES, DONT PRINT UNUSED ENTRIES
         LA    R4,1(R4)                UP POINTER BY 1
         CR    R2,R4                   ARE WE DONE
         BE    INDXPR00                YES
         LR    R6,R4                   LOAD INDEX ENTRY
         BCTR  R6,0                    SUBTRACT 1
         SLL   R6,3                    MULTIPLY BY 8
         A     R6,INDXADDR             ADD TO INDEX ADDR
         SR    R1,R1                   ZERO R1
         IC    R1,0(R6)                INSERT LENGTH
         L     R6,0(R6)                GET ADDR
         MVC   CARD(80),BLANK          BLANK OUT THE CARD
         EX    R1,MOVENAM1             MOVE IN THE NAME
         L     R5,PADDR
         BAL   R7,PRINDX               PRINT THE INDEX
         B     INDXPRN0
INDXPR00 EQU   *
         LR    R6,R2                   LOAD IN INDX
         BCTR  R6,0                    SUBTRACT 1
         SLL   R6,3                    MULTIPLY BY 8
         A     R6,INDXADDR             GET TABLE ENTRY
         SR    R2,R2                   ZERO FOR LENGTH
         IC    R2,0(R6)                GET LENGTH
         L     R6,0(R6)                GET ADDR OF NAME
         MVC   CARD(80),BLANK
         CLC   0(1,R6),LETTER          SAME LETTER AS BEFORE?
         BE    INDXPRN2                YES, SKIP - L -
         BAL   R7,PRINDX
         MVC   CARD(80),BLANK          REBLANK OUT THE CARD
         MVC   LETTER(1),0(R6)
         MVC   CARD(5),=C'- L -'
         MVC   2+CARD(1),LETTER
         L     R7,FAKELCNT             LOAD THE FAKE LINE COUNT
         C     R7,MAXLINE              ARE WE ON THE SECOND LINE?
         BNL   INDXSEC1                YES, COMPARE IT.
         LA    R7,3(R7)                UP POINTER
         C     R7,MAXLINE              WILL THREE MORE LINES FIT?
         BL    INDXDLD                 YES PRINT THE - L -
INDXPR2  EQU   *
         MVC   WORKLINE(80),CARD       SAVE THE INDEX ENTRY
         MVC   CARD(80),BLANK          PRINT 3 BLANK LINES
         BAL   R7,PRINDX
         MVC   CARD(80),BLANK          PRINT 3 BLANK LINES
         BAL   R7,PRINDX
         MVC   CARD(80),BLANK          PRINT 3 BLANK LINES
         BAL   R7,PRINDX
         MVC   CARD(80),WORKLINE       RESTORE THE INDEX ENTRY
         B     INDXDLD                 AND PRINT THE - L -
INDXSEC1 EQU   *
         L     R7,INDXADD2             LOAD PRESENT POSITION
         LA    R7,240(R7)              IS THERE ROOM FOR 3 MORE LINES
         C     R7,INDXADD1             ?
         BNL   INDXPR2                 PRINT TWO BLANK LINES THEN
INDXDLD  EQU   *
         BAL   R7,PRINDX
         MVC   CARD(80),BLANK          REBLANK OUT THE CARD
         BAL   R7,PRINDX
INDXPRN2 EQU   *
         MVC   CARD(80),BLANK          MAKE SURE THE LINE IS BLANK
         EX    R2,MOVENAM1             MOVE IN THE NAME
         LA    R5,CARD+10              LOAD START FOR NUMBERS
         CH    R2,=H'8'                NAME LONGER THAN 8?
         BNH   SKIPR5                  NO
         LA    R1,CARD+2(R2)           RELOAD POINTER PAST NAME
         LA    R5,CARD+20              LOAD SECOND TAB POSITION
         CR    R5,R1                   CAN WE TAB TO POSITION 20?
         BNL   SKIPR5                  YES, SO DOIT
         LA    R5,CARD+30              GO TO NEXT LINE FOR THE NUMBERS
SKIPR5   EQU   *
         LA    R6,CARD+27              LOAD ENDING ADDR
         LH    R4,0(R3)                LOAD NAME NUMBER
INDXPRN1 EQU   *
         LH    R1,2(R3)                LOAD PAGE NUMBER
         CVD   R1,DOUBLE               CONVERT IT
         UNPK  TEMP(4),DOUBLE(8)       UNPACK IT
         OI    TEMP+3,X'F0'            MAKE IT READABLE
         LA    R1,TEMP
FIXZERO  EQU   *
         CLI   0(R1),C'0'              A ZERO
         BNE   FIXZERO1                NO, GET OUT
         LA    R1,1(R1)                UP POINTER
         B     FIXZERO
MOVENAM1 MVC   CARD(0),0(R6)
MOVENUM  MVC   0(0,R5),0(R15)
FIXZERO1 EQU   *
         LR    R15,R1                  LOAD START OF NUMBER
         LA    R1,TEMP+4               GET END
         SR    R1,R15                  GET LENGTH
         BCTR  R1,0                    SUBTRACT 1 FOR EX
         EX    R1,MOVENUM              MOVE IN NUMBER
         LA    R1,1(R1)                BUMP UP POINTER
         AR    R5,R1                   UP POINTER
         CLC   0(2,R3),8(R3)           IS NEW ENTRY SAME NAME
         BNE   INDXPRN4                NO, START TO PROCESS NEXT ENTRY
         LH    R1,2(R3)                LOAD IN THIS PAGE NUMBER
         LA    R1,2(R1)                IS THERE 3 PAGES IN A ROW?
         CH    R1,10(R3)               CHECK TO SEE
         BNE   INDXPRN3                NO, THERE IS NOT THREE PAGES IN
         MVI   0(R5),C'-'              MOVE IN DASH TO REPRESENT RANGE
         LA    R5,1(R5)                UP POINTER PAST DASH
         LA    R3,4(R3)                BUMP UP TO NEXT ENTRY
FINDRNGE EQU   *
         LA    R3,4(R3)                UP POINTER
         CLC   0(2,R3),4(R3)           IS NEXT ENTRY SAME WORD
         BNE   INDXPRN1                NO, PRINT OUT THIS ENTRY
         LH    R1,2(R3)                LOAD THIS PAGE NUMBER
         LA    R1,1(R1)                ADD 1
         CH    R1,6(R3)                IS PAGE NUMBER 1 MORE?
         BE    FINDRNGE                YES, KEEP LOOKING FOR END OF RAN
         B     INDXPRN1                WE ARE DONE WITH THIS WORD
INDXPRN4 EQU   *
         CLC   0(2,R3),4(R3)           IS NEXT ENTRY THE SAME NUMBER?
         BNE   INDXPRNT
INDXPRN3 EQU   *
         MVI   0(R5),C','              MOVE IN COMMA
         LA    R5,1(R5)                UP POINTER
         CR    R5,R6                   ARE WE OFF END YET?
         BL    INDXPRNT                NOT OFF END YET, GET NEXT ENTRY
         L     R5,PADDR                LOAD ADDR TO PRINT
         BAL   R7,PRINDX               PRINT THE RECORD
         MVC   CARD(80),BLANK          BLANK OUT THE CARD
         LA    R5,CARD+13              LOAD START POINTER
         B     INDXPRNT                GET NEXT ENTRY
PRINDX   EQU   *
         CLC   FAKELCNT(4),MAXLINE     HAVE WE EXCEEDED THE 1 COLUMN
         BNL   PRINDX2                 YES, PROCESS SECOND COLUMN
         L     R1,FAKELCNT             LOAD LINE COUNT
         LA    R1,1(R1)                UP IT BY ONE
         ST    R1,FAKELCNT             STORE THE NEXT LINE COUNT
         L     R1,INDXADD1             LOAD ADDRESS TO MOVE IN BUFFER
         MVC   0(80,R1),CARD           MOVE IN CARD
         LA    R1,80(R1)               UP POINTER
         ST    R1,INDXADD1             RESAVE BUFFER POINTER
         BR    R7                      AND RETURN
PRINDX2  EQU   *
         CLC   INDXADD1(4),INDXADD2    ARE WE DONE WITH SECOND COLUMN?
         BE    PRINDX3                 YES, START OVER WITH 1ST COLUMN
         MVC   CARD+39(35),CARD        MOVE TO SECOND COLUMN
         L     R1,INDXADD2             LOAD ADDR OF BUFFER
         MVC   CARD(35),0(R1)          MOVE IN FIRST COLUMN
         LA    R1,80(R1)               UP POINTER
         ST    R1,INDXADD2             RESAVE POINTER
         L     R5,PADDR                LOAD ADDR TO PRINT
         TM    FLAG2,F2PRINDX          DO WE PRINT THE INDEX
         BOR   R7                      NO, SKIP THE PRINT
         BAL   R8,PRINTIT              AND PRINT THE RECORD
         BR    R7                      AND RETURN
PRINDX3  EQU   *
         LA    R5,BLANK                LOAD ADDR OF BLANK LINE
         TM    FLAG2,F2PRINDX          DO WE PRINT THE INDEX
         BO    PRINDX4                 NO, SKIP THE PRINT
         BAL   R8,PRINTIT              AND PRINT IT
PRINDX4  EQU   *
         MVC   FAKELCNT(4),LINECNT     LOAD IN FAKE LINE COUNT
         L     R1,INDXSTCK             LOAD ADDR OF BLOCK
         ST    R1,INDXADD1             SAVE FOR FIRST COLUMN
         ST    R1,INDXADD2             SAVE FOR FIRST COLUMN
         B     PRINDX                  AND PRINT IT
ENDINDX  EQU   *
         TM    FLAG2,F2PRINDX          DO WE PRINT THE INDEX
         BO    EOFSTATS                NO, SKIP THE PRINT
         BAL   R7,PRINDX
ENDINDX1 EQU   *
         CLC   INDXADD1(4),INDXADD2    ARE WE DONE?
         BE    EOFSTATS                YES, TERMINATE
         L     R5,INDXADD2             LOAD ADDRESS OF BLOCK
         MVC   CARD(80),0(R5)          MOVE IN CARD
         LA    R5,80(R5)               UP POINTER
         ST    R5,INDXADD2             SAVE ADDR POINTER
         L     R5,PADDR                LOAD ADDRESS TO PRINT
         BAL   R8,PRINTIT              PRINT THE RECORD
         B     ENDINDX1                RETURN FOR NEXT RECORD
EOFSTATS EQU   *
         TM    FLAG2,F2STATS           DO WE PRINT OUT THE STATS?
         BZ    MISSWRDS                NO, SO SKIP THE STATS
         MVC   LINECNT(4),MAXLINE      FORCE SKIP TO TOP OF PAGE.
         MVC   SECTITLE(40),BLANK      BLANK OUT THE SECTION TITLE
         MVC   SECTITLE+10(19),=CL19'S T A T I S T I C S'
         MVC   CARD(80),BLANK          BLANK OUT CARD
         MVC   CARD(16),=CL16'INDEX STATISTICS'
         L     R5,PADDR                POINT TO OUTPUT CARD
         BAL   R8,PRINTIT              AND PRINT THE CARD
         L     R1,INDXNAME
         S     R1,INDXFREE             GET AMOUNT OF UNUSED STORAGE
         CVD   R1,DOUBLE               CONVERT IT
         MVC   CARD(16),=C'UNUSED STORAGE= '
         UNPK  CARD+20(8),DOUBLE(8)
         OI    CARD+27,X'F0'           MAKE LAST BYTE READABLE
         L     R1,INDXTMES             LOAD NUMBER OF TIMES WORDS SRCH
         CVD   R1,DOUBLE
         MVC   CARD+40(15),=C'WORDS SEARCHED='
         UNPK  CARD+60(8),DOUBLE(8)
         OI    CARD+67,X'F0'
         L     R5,PADDR                POINT TO OUTPUT CARD
         BAL   R8,PRINTIT              AND PRINT THE CARD
         L     R1,INDXCOMP             LOAD NUMBER OF COMPARES
         CVD   R1,DOUBLE
         MVC   CARD(15),=C'NO OF COMPARES='
         UNPK  CARD+20(8),DOUBLE(8)
         OI    CARD+27,X'F0'
         L     R1,INDXKEY             LOAD END   OF INDEX TABLE
         S     R1,INDXADDR            GET LENGTH OF TABLE
         SRL   R1,3                   DIVIDE BY 8 TO GET NO OF ENTRIES
         CVD   R1,DOUBLE
         MVC   CARD+40(15),=C'NO INDEX WORDS='
         UNPK  CARD+60(8),DOUBLE(8)
         OI    CARD+67,X'F0'
         BAL   R8,PRINTIT              AND PRINT THE CARD
         L     R1,INDXSTCK            LOAD END   OF PAGE  TABLE
         S     R1,INDXHASH            GET LENGTH OF TABLE
         SRL   R1,2                   DIVIDE BY 4 TO GET NO OF ENTRIES
         CVD   R1,DOUBLE
         MVC   CARD(15),=C' INDEX ENTRIES='
         UNPK  CARD+20(8),DOUBLE(8)
         OI    CARD+27,X'F0'
         MVC   CARD+40(30),BLANK       BLANK OUT THE NEXT PART
         BAL   R8,PRINTIT              AND PRINT THE CARD
         MVC   CARD(80),BLANK          BLANK OUT CARD
         BAL   R8,PRINTIT              AND PRINT THE CARD
         MVC   CARD(19),=CL19'SPELLING STATISTICS'
         BAL   R8,PRINTIT              AND PRINT THE CARD
         MVC   CARD(19),BLANK          AND BLANK OUT THE CARD
         L     R1,NOMISS              LOAD NUMBER OF MISSPELLED WORDS
         CVD   R1,DOUBLE
         MVC   CARD(15),=C'  MISSPELLINGS='
         UNPK  CARD+20(8),DOUBLE(8)
         OI    CARD+27,X'F0'
         L     R1,SPELLWDS            LOAD NUMBER OF SPELLING WORDS
         CVD   R1,DOUBLE
         MVC   CARD+40(15),=C'NO SPELL WORDS='
         UNPK  CARD+60(8),DOUBLE(8)
         OI    CARD+67,X'F0'
         BAL   R8,PRINTIT              AND PRINT THE CARD
         L     R1,SPELLMAX             LOAD THE MAXIMUM LENGTH
         CVD   R1,DOUBLE
         MVC   CARD(15),=C' SPELL MAX SRC='
         UNPK  CARD+20(8),DOUBLE(8)
         OI    CARD+27,X'F0'
         L     R1,SPELLCMP            LOAD NUMBER OF SPELLING WORDS
         CVD   R1,DOUBLE
         MVC   CARD+40(15),=C'SPELL COMPARES='
         UNPK  CARD+60(8),DOUBLE(8)
         OI    CARD+67,X'F0'
         BAL   R8,PRINTIT              AND PRINT THE CARD
         MVC   CARD(80),BLANK          BLANK OUT THE CARD
         BAL   R8,PRINTIT              AND PRINT THE CARD
         MVC   CARD(17),=C'LETTER OCCURANCES'
         BAL   R8,PRINTIT              AND PRINT THE CARD
         L     R2,INDXKEY              LOAD START OF HASH TABLE
         SR    R3,R3                   ZERO CHARACTER COUNTER
         MVC   CARD(17),BLANK          BLANK OUT THE CARD
         LA    R4,CARD
PRNTHSH0 EQU   *
         MVI   0(R4),C' '               CLEAR INDEX FLAG
         C     R2,INDXHASH             ARE WE DONE?
         BNL   PRNTHSH3                YES, QUIT
         CLC   2(2,R2),=X'0000'        AND WORDS HERE?
         BE    PRNTHSH2                NO, SKIP PUT
         CLC   0(2,R2),=X'0000'        ANY INDEX WORDS?
         BE    PRNTHSH1                NO, SKIP MVI
         MVI   0(R4),C'*'              MAKE INDEX
PRNTHSH1 EQU   *
         STC   R3,1(R4)                DISPLAY CHARACTER
         LH    R1,2(R2)                LOAD COUNT
         CVD   R1,DOUBLE
         UNPK  5(8,R4),DOUBLE(8)
         OI    12(R4),X'F0'
         LA    R4,15(R4)               UP POINTER
         LA    R1,CARD+70              LOAD END OF CARD
         CR    R4,R1                   ARE WE READY TO PRINT A LINE?
         BL    PRNTHSH2                NO, SKIP PUT
         L     R5,PADDR                LOAD ADDR OF CARD
         BAL   R8,PRINTIT              PRINT THE RECORD
         MVC   CARD(80),BLANK          BLANK OUT THE RECORD
         LA    R4,CARD                 POINT TO BEGINNING OF RECORD
PRNTHSH2 EQU   *
         LA    R2,4(R2)                UP POINTER
         LA    R3,1(R3)                AND CHAR POINTER ALSO
         B     PRNTHSH0
PRNTHSH3 EQU   *
         L     R5,PADDR                POINT TO OUTPUT CARD
         BAL   R8,PRINTIT              AND PRINT THE CARD
MISSWRDS EQU   *
         TM    FLAG3,F3MISS            DO WE PRODUCE THIS REPORT?
         BZ    EOFCLOSE                NO.
         CLC   SPELLMA(4),=F'0'        ARE WE DOING WORD CHECKING?
         BE    EOFCLOSE                NO, SO SKIP THIS
         CLC   SPELLMA(4),SPELLVE      DO WE HAVE ANY MISSPELLED WRDS?
         BE    EOFCLOSE                NO, SO SKIP THIS
         MVC   LINECNT(4),MAXLINE      FORCE SKIP TO TOP OF PAGE.
         MVC   SECTITLE(40),BLANK      BLANK OUT THE SECTION TITLE
         MVC   SECTITLE+8(23),=CL23'M I S S P E L L I N G S'
         MVC   CARD(80),BLANK          BLANK OUT CARD
         MVC   CARD(16),=CL16'MISSPELLED WORDS'
         L     R5,PADDR                POINT TO OUTPUT CARD
         BAL   R8,PRINTIT              AND PRINT THE CARD
         MVC   CARD(80),BLANK
         MVC   CARD(18),=CL18'PAGE: XXXX, WORD: '
         L     R3,SPELLVE              LOAD START OF WORD LIST
MISSWRD1 EQU   *
         C     R3,SPELLMA              ARE WE DONE?
         BE    EOFCLOSE                YES.
         L     R1,0(R3)                LOAD THE PAGE NUMBER
         N     R1,=X'00FFFFFF'         TURN OFF THE LENGTH
         CVD   R1,DOUBLE               CONVERT IT
         UNPK  CARD+6(4),DOUBLE(8)     UNPACK IT
         OI    CARD+9,X'F0'            AND MAKE IT READABLE
         MVC   CARD+18(30),BLANK       BLANK OUT THE LAST WORD
         SR    R1,R1                   ZERO R1
         IC    R1,0(R3)                GET THE LENGTH
         LR    R2,R1                   MOVE IT TO R2
         S     R2,=F'5'                GET JUST THE LENGTH -1
         EX    R2,SPELMVC2             AND MOVE IT
         AR    R3,R1                   BUMP TO NEXT ENTRY
         L     R5,PADDR                POINT TO OUTPUT CARD
         BAL   R8,PRINTIT              AND PRINT THE CARD
         B     MISSWRD1                AND GET THE NEXT ENTRY
SPELMVC2 MVC   CARD+18(0),4(R3)        AND MOVE IT
EOFCLOSE EQU   *
         CLOSE SYSUT3                  CLOSE INPUT
         OPEN  (SYSUT3,INPUT)          AND OPEN FOR OUTPUT
PRLOOP   EQU   *
         GET   SYSUT3,CARDOUT          READ A RECORD
         PUT   SYSUT2,CARDOUT          WRITE THE RECORD
         B     PRLOOP                  GET NEXT CARD
EOFEND   EQU   *
         CLOSE (SYSUT1,,SYSUT2,,SYSUT3)
         L     R13,SAVE+4              RESTORE R13
         LM    R14,R12,12(R13)         RELOAD REGISTERS
         SR    R15,R15                 ZERO RETURN CODE
         BR    R14                     AND RETURN
DCBEXIT  EQU   *
         CLI   36(R1),X'00'            IS THERE A RECFM SPECIFIED?
         BNE   DCBEXIT1                YES, SKIP DEFAULT.
         MVI   36(R1),B'10010100'      MOVE IN RECFM FBA.
DCBEXIT1 EQU   *
         CLC   X'52'(2,R1),=H'0'       IS THE LRECL SPECIFIED?
         BNE   DCBEXIT2                YES.
         MVC   X'52'(2,R1),DLRECL      MOVE IN DEFAULT LRECL.
DCBEXIT2 EQU   *
         CLC   X'3E'(2,R1),=H'0'       IS THE BLKSIZE SPECIFIED?
         BNE   DCBEXIT9                YES.
         MVC   X'3E'(2,R1),DBLKSIZE    MOVE IN DEFAULT BLKSIZE.
DCBEXIT9 EQU   *
         BR    R14                     AND RETURN.
         TITLE 'S M A R T  ***  STORAGE AND CONSTANTS'           86033
SAVE     DS    18F
EXLST    DC    X'85',AL3(DCBEXIT)
FAKELCNT DC    F'60'
MAXLINE  DC    F'60'
LINECNT  DC    F'60'
JUSTCOL  DC    F'0'
*------------------- INDEX MANAGEMENT POINTERS
PAGECNT  DC    F'0'
INCSTACK DC    F'0'
INDXADDR DC    F'0'
INDXKEY  DC    F'0'
INDXHASH DC    F'0'
INDXSTCK DC    F'0'
INDXFREE DC    F'0'
INDXNAME DC    F'0'
INDXEND  DC    F'0'
GETAMT   DC    F'30000'                30000 BYTES FOR INDEX MANAGEMENT
INDXADD1 DC    A(BLOCK)
INDXADD2 DC    A(BLOCK)
INDXTMES DC    F'0'                    NUMBER OF WORDS SEARCHED FOR
INDXCOMP DC    F'0'                    NUMBER OF COMPARES
LEVELSEC DC    H'0'                    START AT LEVEL 1
AUTOSEC  DC    5H'0'                   AUTO SECTION NUMBERS
LEVEL    DC    H'0'
DLRECL   DC    H'133'                  DEFAULT LRECL FOR SYSUT2
DBLKSIZE DC    H'1330'                 DEFAULT BLKSIZE FOR SYSUT2
DOUBLE   DS    D'0'
TEMP     DS    D
TEMP2    DS    D
MEMBER   DS    CL8
SPELLVA  DC    F'0'                    ADDR OF SPELL GETMAINED AREA
SPELLGET DC    F'100000'               AMOUNT TO GETMAIN
SPELLVE  DC    F'0'                    ENDING ADDR OF GETMAINED AREA
SPELLVW  DC    F'0'                    ADDR OF FIRST OF CHARS
SPELLBIT DC    F'0'                    ADDR OF FIRST OF CHARS
SPELLCMP DC    F'0'                    TOTAL NUMBER OF COMPARES
SPELLWDS DC    F'0'                    NUMBER OF WORDS COMPARED
SPELLMAX DC    F'0'                    MAXIMUM BUCKET LENGTH SEARCHED
SPELLMA  DC    F'0'                    ADDR OF CURRENT MISSPELLED WORD
SPELLME  DC    F'0'                    ADDR OF LAST POSSIBLE MISS WORD
NOMISS   DC    F'0'                    NUMBER OF MISSPELLED WORDS
INDXCHAR DC    C','
LETTER   DC    X'00'
TOPCNT   DC    X'00'
TOPCNT1  DC    X'00'
***************************************
FLAG1    DC    X'00'                  **
F1RSEC   EQU   1                      ***
F1LEVEL  EQU   2                      ****
F1SPELL  EQU   4                      *****
F1INDX   EQU   8                      ******   FLAG1 FLAGS
F1LEFT   EQU   16                     *****
F1A4     EQU   32                     ****
F1TOP    EQU   64                     ***
F1DEBUG  EQU   128                    **
***************************************
FLAG2    DC    X'00'                  ***
F2SPELLX EQU   1                      ****
F2JUST   EQU   2                      *****
F2TOC    EQU   4                      ******
F2PRINDX EQU   8                      ******* FLAG2 FLAGS
F2BODY   EQU   16                     ******
F2STATS  EQU   32                     *****
F2COVER  EQU   64                     ****
F2ALLIN  EQU   128                    ***
***************************************
***************************************
FLAG3    DC    X'00'                  ***
F3STD    EQU   1                      ****
F3BLOCK  EQU   2                      *****
F3ALL    EQU   4                      ******
F3LEFT   EQU   8                      ******* FLAG3 FLAGS
F3MISS   EQU   16                     ******
F3A2     EQU   32                     *****
F3A3     EQU   64                     ****
F3A4     EQU   128                    ***
***************************************
***************************************
FLAGI    DC    X'00'                  ***
FIPDS    EQU   1                      ****
FISEQ    EQU   2                      *****
FIREAD   EQU   4                      ******
FIA0     EQU   8                      ******* FLAGI FLAGS (INCLUDE)
FIA1     EQU   16                     ******
FIA2     EQU   32                     *****
FIA3     EQU   64                     ****
FIA4     EQU   128                    ***
***************************************
PADDR    DC    A(CARDOUT)
TSOFFSET DC    H'0'
JUSTR1   DS    F
JUSTR2   DS    F
JUSTIT   DC    CL80' '
CARDOUT  DC    CL16' '
CARD     DS    CL80
         DC    CL53' '
TOCLINE  DC    CL25' '
TOCCARD  DC    CL56' '
TOCPG    DC    CL67' '
*---------- TEXT HEADERS ARE HERE
TITLE1   EQU   *
         DC    C'1',CL15' '
         DC    C'DATE:'
DATE     DC    X'402020',C'.',X'202020',CL5' '
MANNAME  DC    CL40' '
         DC    C' PAGE: '
PGNO     DC    C'      ',63C' '
TITLE2   EQU   *
         DC    CL16' ',C' '
PVERT1   DC    C'     '                   VER: GOES HERE
PVER     DC    CL11' '
SECTITLE DC    CL40' '
         DC    CL2' '
SECT     DC    C'     '
SECNO    DC    C'         ',62C' '
TITLE3   EQU   *
         DC    C'+',CL15' ',72C'_',66C' '
TITLE4   DC    CL133'0'
BLANK    DC    CL133' '
         DS    0F
WORKLINE DC    CL149' '
RTABLE   DC    C'I   II  III IV  V   VI  VII VIIIIX  X   XI  '
         DC    C'XII XIIIXIV XV  XVI XVII'
TOCLNNO  DC    F'60'
TOCPGNO  DC    F'0'
* ------------- TABLE OF CONTENTS HEADER LINES
TOCTIT1  EQU   *
         DC    C'1',CL15' '
         DC    C'DATE:'
TOCDATE  DC    X'402020',C'.',X'202020',CL5' '
TMANNAME DC    CL42' '
         DC    C' PAGE: '
TOCPOUT  DC    C'     ',62C' '
TOCTIT2  EQU   *
         DC    CL16' ',C' '
PVERT2   DC    C'     '                       VER: GOES HERE
PTVER    DC    CL11' '
         DC    CL42'     T A B L E  O F  C O N T E N T S'
         DC    C' JULD:'
PDATE    DC    CL75' '
TRTABLE  DC    256AL1(*-TRTABLE)            TRANSLATION TO UPPER CASE
         ORG   TRTABLE+X'81'
         DC    C'ABCDEFGHI'
         ORG   TRTABLE+X'91'
         DC    C'JKLMNOPQR'
         ORG   TRTABLE+X'A2'
         DC    C'STUVWXYZ'
         ORG   ,
         LTORG
*
*  KEYWORD VALIDATION TABLE FOR THE OPTION CARD
*
OPTABLE  KEYT  SEC,,,,(1,3),                                           X
               FIELD1=(,N,FLAG3,X'FF'-F3BLOCK-F3STD-F3ALL-F3LEFT),     X
               FIELD2=(BLOCK,O,FLAG3,F3BLOCK),                         X
               FIELD3=(STD,O,FLAG3,F3STD),                             X
               FIELD4=(ALL,O,FLAG3,F3ALL),                             X
               FIELD5=(LEFT,O,FLAG3,F3LEFT),END=YES
*
SYSUT1   DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,LRECL=80,EODAD=EOF 86033
SYSUT2   DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,EXLST=EXLST     86033
SYSUT3   DCB   DDNAME=SYSUT3,DSORG=PS,MACRF=(GM,PM),LRECL=133,         X
               EODAD=EOFEND,RECFM=FBA,BLKSIZE=12901              86033
WORDS    DCB   DDNAME=WORDS,DSORG=PS,MACRF=GM,LRECL=80,EODAD=EOFWORDS
SEQDD    DCB   DDNAME=ANYNAME,DSORG=PS,MACRF=GM,LRECL=80,EODAD=EOFINC
PDSDD    DCB   DDNAME=GOTME,DSORG=PO,MACRF=R,EODAD=EOFINC,RECFM=U,     X
               BLKSIZE=19069
         YREGS ,
TOPSAVE  DS    80X
TOPLINE  DS    400X
*
*  INCLUDE STACK DISPLACEMENTS
*
NEXT     EQU   0                       POINT TO THE NEXT AREA
AMT      EQU   4
FLAG     EQU   8
RECPOS   EQU   9
DCB      EQU   13
BLOCK    EQU   109
AMTSEQ   DC    A(109)
AMTPDS   DC    A(109+19069)
         END
