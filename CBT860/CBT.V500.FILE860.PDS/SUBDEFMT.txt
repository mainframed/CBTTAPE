SUBDEFMT TITLE 'S U B D E F M T  ***  FORMAT PDS DIRECTORY ENTRY INFO'
         MACRO ,
&L       ATTR  &B1,&B2,&ID
         LCLA  &I,&J
         LCLC  &A1,&A2,&N1,&N2
         AIF   ('&B1' EQ '' OR '&B2' EQ '' OR '&ID' EQ '').MISS
&A1      SETC  '0'
         AIF   ('&B1' EQ '0').NOB1
&I       SETA  1
&A1      SETC  'PDS2'
.LOOP1   ANOP  ,
&A1      SETC  '&A1'.'&B1'(&I,1)
&I       SETA  &I+1
         AIF   ('&B1'(&I-1,1) NE '+').LOOPE1
&A1      SETC  '&A1'.'PDS2'
         AIF   (&I LE K'&B1).LOOPE1
         MNOTE 8,'&B1 IS AN INVALID ARGUMENT'
         MEXIT ,
.LOOPE1  AIF   (&I LE K'&B1).LOOP1
.NOB1    ANOP  ,
&A2      SETC  '0'
         AIF   ('&B2' EQ '0').NOB2
&I       SETA  1
&A2      SETC  'PDS2'
.LOOP2   ANOP  ,
&A2      SETC  '&A2'.'&B2'(&I,1)
&I       SETA  &I+1
         AIF   ('&B2'(&I-1,1) NE '+').LOOPE2
&A2      SETC  '&A2'.'PDS2'
         AIF   (&I LE K'&B2).LOOPE2
         MNOTE 8,'&B2 IS AN INVALID ARGUMENT'
         MEXIT ,
.LOOPE2  AIF   (&I LE K'&B2).LOOP2
.NOB2    AIF   ('&ID'(1,1) EQ '-').MINUS
         AIF   ('&ID'(1,1) EQ '''').QUOTE
&L       DC    AL1(&A1,&A2),CL4'&ID'
         MEXIT ,
.QUOTE   ANOP  ,
&L       DC    AL1(&A1,&A2),CL4&ID
         MEXIT ,
.MINUS   AIF   (K'&ID LT 2).SHORT
         AIF   ('&ID'(2,1) EQ '''').MINUSQ
&N1      SETC  '&ID'(2,1)
&N2      SETC  '&ID'(3,K'&ID-2)
&L       DC    AL1(&A1,&A2,C'&N1'-X'40'),CL3'&N2'
         MEXIT ,
.MINUSQ  AIF   (K'&ID LT 4).SHORT
&N1      SETC  '&ID'(3,1)
&N2      SETC  '&ID'(4,K'&ID-4)
&L       DC    AL1(&A1,&A2,C'&N1'-X'40'),CL3'&N2'
         MEXIT ,
.SHORT   MNOTE 8,'&ID IS TOO SHORT'
         MEXIT ,
.MISS    MNOTE 8,'REQUIRED OPERAND OMITTED'
         MEXIT ,
         MEND  ,
         SPACE 2
         MACRO ,
&NM      VERINT &FLD,&FLN,&BE=,&BNE=                                   *
                 VERIFY INTEGER FIELD < 256 DIGITS
         LCLC  &LEN,&L
&LEN     SETC  '&FLN'
         AIF   ('&LEN' NE '').HAVELEN
&L       SETC  'L'''
&LEN     SETC  '&L'.'&FLD'
.HAVELEN ANOP  ,
&NM      MACPARM (R15),&FLD  LOAD FIELD ADDRESS (IN CASE EXPRESSION)
         MACPARM 0(&LEN,R15),TRTINT,OP=TRT   VERIFY INTEGER
         MACPARM &BE,MODE=ONE,OP=BZ,OPR=BZR,NULL=SKIP
         MACPARM &BNE,MODE=ONE,OP=BNZ,OPR=BNZR,NULL=SKIP
.MEND    MEND  ,
         SPACE 1
         MACRO ,
&NM      VERDEC &FLD,&FLN,&BE=,&BNE=,&CHECK=                           *
                 VERIFY PACKED DECIMAL FIELD < 17 BYTES
         LCLC  &LEN,&L
&LEN     SETC  '&FLN'
         AIF   ('&LEN' NE '').HAVELEN
&L       SETC  'L'''
&LEN     SETC  '&L'.'&FLD'
.HAVELEN ANOP  ,
&NM      MACPARM (R15),&FLD  LOAD FIELD ADDRESS (IN CASE EXPRESSION)
         MACPARM 0(&LEN,R15),TRTPACK,OP=TRT   VERIFY PACKED DIGITS
         AIF   ('&CHECK' EQ 'NO').MEND                          GP06277
         AIF   (T'&BNE EQ 'O').SKIPCH                           GP06277
         MACPARM R2,1,TRTPACK+X'0F',MODE=3,OP=CLM               GP06277
         MACPARM &BNE,MODE=ONE,OP=BNZ,OPR=BNZR,NULL=SKIP
.SKIPCH  LR    R0,R1         COPY END ADDRESS                   GP06277
         SR    R0,R15        LESS START
         AIF   ('&FLN' EQ '').TESTS
         CH    R0,=AL2(&FLN)  CORRECT LENGTH ?
         MACPARM &BNE,MODE=ONE,OP=BNZ,OPR=BNZR,NULL=SKIP
.TESTS   MACPARM &BE,MODE=ONE,OP=BZ,OPR=BZR,NULL=SKIP
.MEND    MEND  ,
         SPACE 1
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
*   FORMAT ONE DIRECTORY ENTRY, AND ADJUST BXLE LENGTH
*   INPUT R1 POINTS TO INPUT/OUTPUT FIELD MAPPED BY MAPDEFMT MACRO
*   USER MUST PRESET xxxNEXT AND xxxLAST; OTHER FIELDS INITIALIZED
*   AND COMPLETED BY THIS CODE
*
*   CODE IS REENTRANT AND REFRESHABLE USING CALLER'S SAVE AREA, BUT
*   NEEDS 370 MODE (ALIGNMENT FIXUP) FOR DOUBLE-WORD NOT ON DW BOUND
*
*   ADAPTED FROM EXHCXPDS & OTHER STUFF
*
         SPACE 1
SUBDEFMT START 0
         B     BEGIN-SUBDEFMT(,R15)                             GP05180
         DC    AL1(8+1+8),CL(8+1+8)'SUBDEFMT &SYSDATE'          GP05180
BEGIN    STM   R14,R9,12(R13)  SAVE CALLER'S REGISTERS          GP05180
         LR    R9,R15                                           GP05180
         USING SUBDEFMT,R9                                      GP05180
*   R10-R13 ARE NEITHER REFERENCED, SAVED, NOR ALTERED
*   R9 - BASE
*   R8 - PDS DIRECTORY ENTRY MAPPING (IHAPDS)
*   R7 - USER'S MAPDEFMT GENERATED/MAPPED WORK AREA
*   R6 - LOCAL WORK AREA CARVED OUT OF CALLER'S SAVE AREA
*   R5 - FAST BRANCH REGISTER
*   R15-R4  VOLATILE WORK REGISTERS
*
         LA    R6,20+10*4(,R13)   POINT TO TAIL OF SAVE AREA    GP05180
         USING DF,R6         12-BYTE WORK AREA                  GP05180
         L     R1,0(,R1)     LOAD CALLER'S POINTER              GP05180
         LA    R7,0(,R1)     LOAD CALLER'S INPUT/RETURN AREA    GP05180
         LA    R15,16        MAJOR ERROR                        GP05180
         USING MAPDEFMT,R7   DECLARE USER'S AREA                GP05180
         ICM   R8,15,PDDNEXT   GET ADDRESS OF MEMBER             82123
         BZ    PDEEXITC      BOMB IF BAD                         82123
         USING PDS2,R8       DECLARE MEMBER MAPPING              82123
         XC    PDDTYPE(PDDCLRLN),PDDTYPE  CLEAR RETURN AREA      82123
         MVI   PDDRALIS,C' '                                     82123
         MVC   PDDRALIS+1(PDDRBLNK-1),PDDRALIS  BLANK EXTENDED RETURN
         IC    R14,PDS2INDC   GET FLAG/LENGTH                    82123
         N     R14,=A(PDS2LUSR)  GET LENGTH ONLY                 82123
         STC   R14,PDDUDLEN   SET USER LENGTH IN HALF-WORDS      82123
         LA    R14,PDS2USRD-PDS2(R14,R14)  GET MEMBER ENTRY LENG
         ST    R14,PDDINCR    SET INCREMENT FOR BXLE             82123
         MVC   PDDRNAME,PDS2NAME  COPY MEMBER NAME               82123
         CLI   PDDRNAME+7,X'C0'  POSSIBLE SVC NAME ?             82123
         BNE   PDENSVC       NO                                  82123
         CLC   =C'IGC',PDDRNAME                                  82123
         BNE   PDENSVC       NO                                  82123
         MVI   PDDRNAME+7,C'+'   MAKE PRINTABLE                  82123
PDENSVC  TM    PDS2INDC,PDS2ALIS  ALIAS ?                        82123
         BZ    *+8           NO                                  82123
         MVI   PDDRALIS,C'*' FLAG AS ALIAS                       82123
         UNPK  PDDRTTR(1+L'PDDRTTR),PDS2TTRP(1+L'PDS2TTRP)      GP05180
         TR    PDDRTTR,NUMTAB   MAKE READABLE HEX               GP05180
         MVI   PDDRTTR+L'PDDRTTR,C' '   CLEAN UP                GP05180
         SPACE 1                                                 82123
*   CHECK IF THE ENTRY IS IN SPF FORMAT, AND RECFM IS F OR V
*                                                                82123
PDETSPF  LA    R5,PDETDTX    SET FAST EXIT                       82123
         CLI   PDDUDLEN,SPFUDLEN    SPF LENGTH ?                 82123
         BNER  R5            NO                                  82123
         CLI   SPFFLGS,C'W'  SSI WYLBUR ?                       GP04234
         BE    PDETSPF2      YES; RECFM=U IS VALID               91270
         TM    PDDRECFM,X'C0'   RECFM=F OR V ?                   82123
         BNMR  R5            NEITHER                             82123
PDETSPF2 VERDEC SPFMODDT,L'SPFMODDT,BNE=(R5)  DECIMAL IN DATE ? GP05180
         VERDEC SPFCRTDT,L'SPFCRTDT,BNE=(R5)  DECIMAL IN DATE ? GP05180
**Y2K**  CLC   SPFCRTDT,SPFMODDT  VALID RANGE ?                  82123
*TEST*   BHR   R5            NO                                  82123
         CLI   SPFCRTDT,1    POSSIBLE DATE FIELD ? 01YYDDDF     GP06277
         BH    PDETSPF3      NO      PERHAPS 19/20YYDDDF?       GP06377
         AP    SPFCRTDT,=P'1900000'  ADJUST CENTURY             GP06277
         CLI   SPFCRTDT+1,X'64'   WINDOW 00-63 TO 20XX          GP06277
         BNL   PDETSPF3                                         GP06277
         AP    SPFCRTDT,=P'0100000'  ADJUST CENTURY             GP06277
PDETSPF3 UNPK  DW(5),SPFMODTM  UNPACK TIME                      GP05180
         VERINT DW,4,BNE=(R5)  VALID TIME?                      GP05180
         TRT   SPFUID,TRTUID   VERIFY USER ID FIELD             GP06277
         BZR   R5            NOT VALID TSO ID                   GP06277
         MVI   PDDTYPE,PDDTSPF  SET SPF ENTRY TYPE               82123
         MVC   PDDRMAIN,SPFUID  SET THE USER ID                 GP05180
***********************************************************************
*   NOTE THAT THE FIDGETING WITH THE DATE (YEAR SEPARATE FROM REST)   *
*   IS DONE SOLELY DUE TO THE SHORT WORK SPACE AVAILABLE.             *
***********************************************************************
         MVC   PDDRJDAY-1(4),=X'F0212020'                       GP05180
         ED    PDDRJDAY-1(4),SPFMODDT+2  FORMAT JULIAN DAY      GP05180
         CLI   SPFMODDT,1    NEW STYLE ?                        GP06277
         BH    MODCENT       YES; USE AS IS                     GP06277
         AP    SPFMODDT,=P'1900000'  ADJUST CENTURY             GP05180
         CLI   SPFMODDT+1,X'64'   WINDOW 00-63 TO 20XX          GP06277
         BNL   MODCENT                                          GP06277
         AP    SPFMODDT,=P'0100000'  ADJUST CENTURY             GP06277
MODCENT  MVC   DF(5),=X'4021202020'  EDIT MASK                  GP05180
         ED    DF(5),SPFMODDT  FORMAT CENTURY AND YEAR          GP05180
         MVC   PDDRYMD(4),DF+1  COPY YEAR                       GP05180
         ICM   R1,15,SPFMODDT   GET DATE BACK                   GP05180
         BAL   R14,CALDATE   CONVERT TO MONTH/DAY FORM          GP05180
         MVC   DF(8),=X'F021602020602020'  MAKE MONTH/DAY MASK  GP05180
         ED    DF(8),DW+5                                       GP05180
         MVC   PDDRYMD+4(6),DF+2  APPEND -MM-DD                 GP05180
         MVC   DW(6),=X'F021207A2020'                           GP05180
         ED    DW(6),SPFMODTM   FORMAT TIME                     GP05180
         MVC   PDDRTIME,DW+1                                    GP05180
         SR    R0,R0                                            GP05180
         ICM   R0,3,SPFCURLN   GET CURRENT LINE COUNT           GP05180
         CVD   R0,DW         MAKE PACKED                        GP05180
         MVC   PDDRLINE,=X'4020202020202120'                    GP05180
         ED    PDDRLINE,DW+4  FORMAT LINE COUNT                 GP05180
         SR    R0,R0                                            GP05180
         IC    R0,SPFVERL    GET VERSION                        GP05180
         MH    R0,=H'100'    ROOM FOR MOD LEVEL                 GP05180
         SR    R14,R14                                          GP05180
         IC    R14,SPFMODL                                      GP05180
         AR    R0,R14                                           GP05180
         CVD   R0,DW         PACK                               GP05180
         MVC   DF(7),=X'402021204B2020'                         GP05180
         ED    DF(7),DW+5                                       GP05180
         MVC   PDDVVMM,DF+2                                     GP05180
         TM    PDS2INDC,PDS2ALIS  ALIAS ?                       GP05180
         BNZ   PDEEXIT       NO; RETURN                         GP05180
         TM    SPFFLGS,SPFGSCLM  SCLM CONTROLLED MEMBER ?       GP05180
         BZ    PDEEXIT       YES; RECFM=U IS VALID              GP05180
         MVI   PDDRALIS,C'¬' SET NON-SCLM EDIT WARNING          GP05180
         B     PDEEXIT       RETURN OK                          GP05180
         SPACE 1                                                 82123
*   TEST FOR AN ENTRY PRODUCED BY IEBUPDTX.  NOTE THAT THE CURRENT
*   VERSION SUPPORTS RECFM=U (WYLBUR EDIT), HENCE NO RECFM TEST
*   IS MADE.                                                     82123
*                                                                82123
PDETDTX  LA    R5,PDETLKED   SET FAST EXIT                       82123
         CLI   PDDUDLEN,DTXUDLEN       MINIMUM DTX LENGTH ?      82123
         BE    PDECDTX       YES; CHECK CONTENTS                 82123
         CLI   PDDUDLEN,DTXUDLTM      ALTERNATE LENGTH ?         82123
         BNER  R5            NO; SKIP                            82123
PDECDTX  CLC   PDS2NAME,DTXMODNM  PRODUCTION ALIAS ?             82123
         BE    PDEMDTX       YES; ACCEPT IT                      82123
         CLC   PDS2NAME(2),DTXID  LIB. ID MATCH ?                82123
         BNER  R5            NO; SKIP                            82123
PDEMDTX  TM    DTXMODNO+L'DTXMODNO-1,X'0C'  PLUS SIGN ?          82123
         BNOR  R5            NO; SKIP                            82123
         TM    DTXVER+L'DTXVER-1,X'0C'                           82123
         BNOR  R5                                                82123
         MVI   PDDTYPE,PDDTDTX   HOPE FOR THE BEST               82123
         MVC   PDDRMAIN,DTXMODNM  RETURN MAIN MEMBER             82123
         MVC   DF(6),=X'402020212020'                           GP05186
         ED    DF(6),DTXVER  EXPAND VERSION                     GP05186
         MVC   PDDVVMM,DF+1  MOVE VERSION                       GP05186
         CLI   PDDUDLEN,DTXUDLTM  TIME-STAMP PRESENT ?           82123
         BNE   PDEEXIT       NO; EXIT                            82123
         UNPK  DF(9),DTXTIMES(5)  UNPACK                        GP05186
         VERINT DF,8         REALLY NUMERIC ?                   GP05186
         MVC   PDDRYMD(2),=C'20'  MAKE CURRENT CENTURY          GP05186
         CLI   DTXTIMES,X'64'  USE WINDOWING FOR CENTURY        GP05186
         BL    PDEHDTX                                          GP05186
         MVC   PDDRYMD(2),=C'19'  MAKE OBSOLETE CENTURY         GP05186
PDEHDTX  MVC   PDDRYMD+2(2),DF    MOVE YEAR                     GP05186
         MVC   PDDRYMD+5(3),DF+2  MOVE JULIAN DAY               GP05186
         MVI   PDDRYMD+4,C'.'                                   GP05186
         MVC   PDDRTIME(2),DF+4   MOVE HOUR                     GP05186
         MVI   PDDRTIME+2,C':'                                  GP05186
         MVC   PDDRTIME+3(2),DF+6
         MVC   PDDRYMD,DW+1  RETURN UPDATE DATE                 GP05180
         B     PDEEXIT       SKIP LINK-EDIT TEST                 82123
         SPACE 1                                                 82123
*   IF RECFM=U, TEST FOR LINK-EDIT ENTRY. IF NOT, MAY BE ANYTHING
*                                                                82123
PDETLKED LA    R5,PDENLKED   SET FAST EXIT                       82123
         TM    PDDRECFM,X'C0'   RECFM=U ?                        82123
         BNOR  R5            NO; NOT LKED ENTRY                  82123
         CLI   PDDUDLEN,11   MINIMUM LKED LENGTH ?               82123
         BLR   R5            NO                                  82123
         CLI   PDDUDLEN,23   LONGER THAN MAX ?                   82123
         BHR   R5            YES                                 82123
         MVC   DW(1),PDS2FTB1   COPY VS LKED FLAG                82123
         NI    DW,PDSAOSLE   LEAVE ONLY THE VS BIT               82123
         MVC   DW+1(1),PDDUDLEN  SET FLAG FOR TR                 82123
         LA    R14,PDELKTAB-11  TENTATIVELY SET OS LIST          82123
         TM    PDS2FTB1,PDSAOSLE+PDSAPFLG  VS + APF ?            82123
         BNO   PDESLKED      NO; USE OS PROCESSING               82123
         BCTR  R14,0         FOR VS + APF, SLIDE TABLE OVER      82123
         OI    DW,PDSAPFLG   TURN THE APF FLAG ON                82123
PDESLKED TR    DW+1(1),0(R14)  CONVERT LENGTH TO FLAGS           82123
         OC    DW+1(1),DW    OR THE VS AND APF BITS IN           82123
         CLI   DW+1,X'FF'    INVALID ENTRY ?                     82123
         BER   R5            YES                                 82123
         MVI   DW+5,PDS2SSI+PDSAPFLG+PDS2SCTR  MAKE MASK FOR VS  82123
         TM    PDS2FTB1,PDSAOSLE  VS LKED ?                      82123
         BNZ   *+8           YES                                 82123
         MVI   DW+5,PDS2SCTR   LEAVE ONLY TESTABLE BIT           82123
         MVC   DW+3(1),PDS2ATR  GET THE PRIMARY ATTRIBUTE BYTE   82123
         NI    DW+3,PDS2SCTR  ISOLATE IT                         82123
         MVC   DW+4(1),PDS2FTB1  GET OPTION BYTE                 82123
         OC    DW+4(1),DW+3  ADD ATTRIBUTE BIT                   82123
         NC    DW+4(1),DW+5  MASK WITH OS OR VS MASK             82123
         NC    DW+5(1),DW+1  MASK RETURN VALUE                   82123
         CLC   DW+4(1),DW+5  DO FLAGGED OPTIONS MATCH ?          82123
         BNER  R5            NO; RETURN NOT LKED                 82123
         MVC   PDDFLAGS,DW+1  SET FLAGS FOR OPTIONAL ENTRIES     82123
         MVI   PDDTYPE,PDDTLKED  SET LKED ENTRY                  82123
         UNPK  DF(9),PDS2STOR(L'PDS2STOR+1)                     GP05180
         TR    DF(9),NUMTAB  MAKE PRINTABLE                     GP05180
         MVC   PDDSIZE,DF    RETURN AS CL8                      GP05180
         UNPK  DF(9),PDS2EPA(L'PDS2EPA+1)                       GP05180
         TR    DF(9),NUMTAB  MAKE PRINTABLE                     GP05180
         MVC   PDDREPAD,DF   RETURN AS CL8                      GP05180
         LA    R15,PDDRATTN  NUMBER OF FIELDS                   GP05180
         LA    R1,ATTRBITS   GET ATTRIBUTE INFO                 GP05180
         USING ATRLSECT,R1                                      GP05180
         LA    R2,PDDRATTR   OUTPUT FIELD                       GP05180
         LA    R0,(ATTRLAST-ATTRBITS)/ATRLSIZE                  GP05180
ATTLOOP  MVC   DF(2),PDS2ATR          COPY ATTRIBUTE BITS       GP05180
         NC    DF(2),ATRLBITS         MASK RELEVANT BITS        GP05180
         BZ    ATTZERO                                          GP05180
         CLC   DF(2),ATRLBITS+ATRLSIZE SPECIAL ?                GP05180
         BE    ATTINC                   YES - CATCH NEXT TIME   GP05180
         TM    ATRLNAME,ATRLONES       POSITIVE TEST ?          GP05180
         BO    ATTMOVE                  YES - DISPLAY ATTRIBUTE GP05180
*        B     ATTINC                                           GP05180
         SPACE 1
ATTINC   LA    R1,ATRLEND              INCREMENT TO NEXT ENTRY  GP05180
         BCT   R0,ATTLOOP              IF ANY                   GP05180
         B     ATTRPAGE      SEE IF ROOM FOR PAGE               GP05180
ATTZERO  TM    ATRLNAME,ATRLONES       POSITIVE TEST ?          GP05180
         BO    ATTINC                   YES - DO NOT DISPLAY    GP05180
*        B     ATTMOVE                  NO  - DISPLAY ATTRIBUTE GP05180
         SPACE 1
ATTMOVE  XC    PDS2ATR,DF             REMOVE ATTRIBUTE BITS     GP05180
         OC    0(4,R2),ATRLNAME   SHOW OPTION OR NEGATION       GP05180
         LA    R2,4(,R2)               NEXT DISPLAY POSITION    GP05180
         BCT   R15,ATTINC              IF ANY                   GP05180
         B     ATTRDONE      NO MORE ROOM                       GP05180
ATTRPAGE CLC   0(4,R2),ZBLANKS  ROOM FOR PAGE ?                  90203
         BNE   ATTRDONE      NO                                  90203
         TM    PDS2FTB1,PDS2PAGA  PAGE ALIGNMENT ?               80150
         BZ    ATTRDONE      NO                                  80150
         MVC   0(4,R2),=C'Page'  INDICATE PAGE ALIGNMENT         80150
         B     ATTRDONE                                         GP05180
ATTRDONE MVC   PDDRMODE,=C'AaR2'  PRESET ADDRESSING/RESIDENCE   GP05180
         TM    PDS2FTB2,X'10'  PDSLRMOD  RESIDENCE MODE ?        90204
         BZ    *+8                                               90204
         MVI   PDDRMODE+3,C'3'   ABOVE THE LINE                 GP05180
         LH    R0,=C'23'                                        GP05180
         TM    PDS2INDC,PDS2ALIS  ALIAS ENTRY ?                  90204
         BNZ   PDESLKMA                                          90204
         TM    PDS2FTB2,X'03'  PDSMAMOD                          90204
         B     PDESLKMC                                          90204
PDESLKMA TM    PDS2FTB2,X'0C'  PDSAAMOD                          90204
PDESLKMC BO    PDESLKMN      ANY                                GP05180
         BNZ   *+8                                              GP05180
         SRL   R0,8                                             GP05180
         STC   R0,PDDRMODE+1 ADDRESSING MODE                    GP05180
PDESLKMN TM    PDDFLAGS,PDS2SSI+PDSAPFLG+PDDFREAL SSI/APF/MAIN ? 82123
         BZ    PDEEXIT       NO                                  82123
         LA    R0,1          MAKE CONSTANT                       82123
         LA    R4,PDSS01     POINT TO VARIABLE TEXT FIELD        82123
         TM    PDDFLAGS,PDS2SCTR  SCATTER DATA PRESENT ?         82123
         BZ    *+8           NO                                  82123
         LA    R4,PDSS01LN(,R4)  SKIP OVER IT                    82123
         TM    PDDFLAGS,PDDFREAL   MAIN MEMBER ENTRY NAME ?      82123
         BZ    PDEMLKED      NO                                  82123
         MVC   PDDRMAIN,PDS2MNM-PDSS02(R4)  COPY MAIN MEMBER NAME
         LA    R4,PDSS02LN(,R4) SKIP OVER IT                     82123
PDEMLKED TM    PDDFLAGS,PDS2SSI  SSI DATA PRESENT ?              82123
         BZ    PDEILKED      NO; SKIP OVER                       82123
         AR    R4,R0                                             82123
         OR    R4,R0                                             82123
         SLR   R4,R0         ROUND TO HALF-WORD BOUNDARY         82123
         LR    R14,R4        COPY SSI ADDRESS                    82123
         SLR   R14,R8        GET SSI OFFSET                      82123
         STH   R14,PDDOSSI   SAVE SSI OFFSET                     82123
         BAS   R14,PDESSI    FORMAT SSI                          82123
         LA    R4,PDSS03LN(,R4)  SKIP SSI                        82123
PDEILKED TM    PDDFLAGS,PDDFAPF  APF DATA PRESENT ?              82123
         BZ    PDEEXIT       NO                                  82123
         LR    R14,R4                                            82123
         SLR   R14,R8        GET OFFSET TO ENTRY                 82123
         STH   R14,PDDOAPF   SAVE OFFSET                         82123
         TM    1(R4),X'0F'   ANY NON-ZERO VALUE?                GP05180
         BZ    PDEEXIT       NO; RETURN                         GP05180
         MVC   PDDRAPF,=C'AC=0'  MAKE PATTERN                    82123
         MVN   PDDRAPF+3(1),1(R4)  COPY APF CODE                 82123
         TR    PDDRAPF+3(1),NUMTAB   EVER GET PAST AC=9?        GP05180
         B     PDEEXIT       AND RETURN                          82123
         SPACE 2                                                 82123
*   PROCESS UNIDENTIFIED ENTRIES                                 82123
*                                                                82123
PDENLKED CLI   PDDUDLEN,2    LONG ENOUGH FOR SSI ?               82123
         BL    PDEEXIT4      NO; RETURN                          82123
         BAS   R14,PDESSIUD  DO SSI IN USERDATA FIELD            82123
PDEEXIT4 LA    R15,4         SET UNIDENTIFIED FORMAT FLAG       GP05180
         B     PDEEXITC      GO TO COMMON                       GP05180
         SPACE 1                                                 82123
PDEEXIT  SR    R15,R15       SET GOOD RETURN                    GP05180
PDEEXITC LA    R0,PDDRALIS   SET RETURN ADDRESS                  82123
         L     R14,12(,R13)  RESTORE RETURN                     GP05180
         LM    R1,R9,24(R13)   RESTORE ALL BUT R15 AND R0       GP05180
         BR    R14           AND EXIT WITH RETURN CODE          GP05180
         SPACE 1                                                 82123
PDESSIUD LA    R1,PDS2USRD   POINT TO USER DATA FIELD            82123
PDESSIUS SLR   R1,R8         GET OFFSET FROM START               82123
         STH   R1,PDDOSSI    SET OFFSET                          82123
         SPACE 1                                                 82123
PDESSI   OI    PDDFLAGS,PDDFSSI  SET SSI PRESENT                 82123
         LH    R3,PDDOSSI    GET OFFSET TO SSI                   82123
         AR    R3,R8         GET ADDRESS OF SSI                  82123
         VERDEC (R3),4,BNE=PDESSIND  SKIP IF NOT DECIMAL        GP05180
         TM    3(R3),X'0F'   PREFERRED PLUS SIGN ?               82123
         BNO   PDESSIND      NO; NO DATE - JUST UNPACK HEX       82123
         UNPK  DF,0(4,R3)    UNPACK WITH SIGN                   GP05180
         OI    DF+L'DW-1,C'0'  MAKE VIEWABLE                    GP05180
         B     PDESSIDT                                         GP05180
PDESSIND UNPK  DF(9),0(5,R3)  UNPACK SSI+                        82123
         TR    DF,NUMTAB     MAKE EBCDIC FROM HEX                92297
PDESSIDT VERINT DF,8,BNE=(R14)  IGNORE IF NOT NUMERIC           GP05180
         CLI   PDDRYMD,C' '  ENTRY HAS A DATE ?                 GP05180
         BNER  R14           YES; JUST REPORT SSI                82123
         CLC   =C'019',DF    OLD YEARS?                         GP05180
         BE    PDESSIHC      YES; ACCEPT IT                     GP05180
         CLC   =C'020',DF    NEW YEARS?                         GP05180
         BE    PDESSIHC                                         GP05180
         LA    R15,=C'020'   POINT TO EXISTING CONSTANT         GP05180
         CLC   =C'001',DF    OLD NEW STYLE?                     GP05180
         BE    PDESSISC      YES?  UPDATE CENTURY               GP05180
         LA    R15,=C'019'   POINT TO EXISTING CONSTANT         GP05180
         CLC   =C'000',DF    FORMAT 000YYDDD ?                   82123
         BNER  R14           NO; NO DATE                         82123
PDESSISC MVC   DF+1(2),1(R15)  UPDATE CENTURY                   GP05180
PDESSIHC CLC   DF+5(3),=C'000'  CHECK DAY > 0                    82137
         BNHR  R14           NO; FAIL                            82137
         CLC   DF+5(3),=C'366'  CHECK DAY <= 366                 82137
         BHR   R14                                               82137
         MVC   PDDRYMD(4),DF+1  MOVE YEAR                       GP05180
         MVI   PDDRYMD+4,C'.'  IDENTIFY AS JULIAN DATE          GP05180
         MVC   PDDRYMD+5(3),DF+5  PRESUMED DATE                 GP05180
         MVC   PDDRJDAY,DF+5  ALSO COPY JULIAN DAY AGAIN        GP05180
         BR    R14           RETURN                              82123
         SPACE 2                                                 82123
CALDATE  XC    DF(6),DF      CLEAR                              GP05180
         STH   R1,DF+6       STASH THE DAY                      GP05180
         ST    R1,DF         SAVE YEAR FOR LEAP TEST            GP05180
         CVB   R1,DW         CONVERT DAYS TO BINARY             GP05180
         SLR   R0,R0         SET FOR NO LEAP DAY ADJUSTMENT     GP05180
*  N.B.: THIS WORKS UNTIL 2099 ONLY                             GP99167
         TM    DF+1,X'12'    MULTIPLE OF 4 ?                    GP05180
         BM    CALNLEAP      NO; BRANCH IF NOT A LEAP YEAR      GP05180
         TM    DF+1,1        IS YEAR ODD ?                      GP05180
         BNZ   CALNLEAP      YES; BRANCH IF NOT A LEAP YEAR     GP05180
         CH    R1,=H'60'     IS THIS ON, BEFORE, OR AFTER LEAP DAY?
         BL    CALNLEAP      BEFORE; TREAT NORMALLY             GP05180
         BH    CALYLEAP      AFTER; ADJUST DAY                  GP05180
         LA    R0,1          SET LEAP DAY ADJUSTMENT            GP05180
CALYLEAP BCTR  R1,0          ONE LESS TO FIX DAYS AFTER LEAP DAY
CALNLEAP LA    R2,DATETABL   GET TABLE OF CUMULATIVE DAYS       GP05180
         LR    R4,R2         SAVE                               GP05180
CALFMON  CH    R1,2(,R2)     IN THIS MONTH?                     GP05180
         BNH   CALFOUND      YES; PROCESS                       GP05180
         LA    R2,2(,R2)     INCREMENT                          GP05180
         B     CALFMON       AND TRY AGAIN                      GP05180
CALFOUND SH    R1,0(,R2)     SUBTRACT DAYS IN PRIOR MONTHS      GP05180
         AR    R1,R0         ADJUST 1 IF LEAP DAY               GP05180
         LA    R2,2(,R2)     FINAGLE TO MAKE MONTH COME OUT RIGHT
         SR    R2,R4         GET OFFSET TO MONTH                GP05180
         SRL   R2,1          GET MONTH (1-12)                   GP05180
*  WE NOW HAVE MONTH IN R2, AND DAY IN R1                       GP05180
         MVO   DW,DF(2)      RETRIEVE YEAR                      GP05180
         CVB   R15,DW        GET YEAR IN BINARY                 GP05180
         MH    R15,=H'100'   MULTIPLY YEAR BY 100               GP05180
         AR    R15,R2        AND ADD IN THE MONTH               GP05180
         MH    R15,=H'100'   SHIFT ONCE MORE                    GP05180
         AR    R15,R1        AND ADD DAY                        GP05180
         CVD   R15,DW        MAKE PACKED AND SIGNED             GP05180
         BR    R14           RETURN TO CALLER                   GP05180
         SPACE 1                                                GP05180
*  TABLE OF DAYS IN PRIOR MONTH
DATETABL DC    H'0,31,59,90,120,151,181,212,243,273,304,334,365'
         SPACE 2                                                 82123
ZBLANKS  DC    8C' '         EIGHT SPACES                       GP05180
         SPACE 1
*        LOAD-MODULE ATTRIBUTES BY LENGTH             NO AC     AC
         DC    AL1(255)                                         11
PDELKTAB DC    AL1(0,255,PDS2SSI,255)                 11-14     12-15
         DC    AL1(PDS2SCTR,PDDFREAL)                 15-16     16-17
         DC    AL1(PDS2SSI+PDS2SCTR,PDS2SSI+PDDFREAL) 17-18     18-19
         DC    AL1(255,PDS2SCTR+PDDFREAL,255)         19-21     20-22
         DC    AL1(PDS2SSI+PDS2SCTR+PDDFREAL,255)     22-23     23-24
         SPACE 1
ATTRBITS ATTR  0,REFR,'Refr'           REFRESHABLE              GP05180
         ATTR  RENT+REUS,0,'Rent'      REENTRANT                GP05180
         ATTR  RENT,0,'Rent'           REENTRANT                GP05180
         ATTR  REUS,0,'Reus'           SERIALLY REUSABLE        GP05180
         ATTR  OVLY,0,'Ovly'           OVERLAY                  GP05180
         ATTR  SCTR,0,'Sctr'           SCATTER LOADABLE         GP05180
         ATTR  LOAD,0,'Load'           ONLY LOADABLE            GP05180
         ATTR  EXEC,0,-¬Exc            NOT EXECUTABLE           GP05180
         ATTR  0,NREP,¬Lnk             NOT REPROCESSABLE        GP05180
         ATTR  0,FLVL,' DC '           DOWNWARD COMPATIBLE      GP05180
         ATTR  0,NRLD,¬RLD             NO RLD RECORD            GP05180
         ATTR  TEST,TSTN,'SymT'        SYMBOL TABLE EXISTS      GP05180
         ATTR  TEST,0,'Test'           TEST ONLY                GP05180
         ATTR  1BLK,0,'1Blk'           ONLY ONE TEXT BLOCK      GP05180
         ATTR  0,LEF,-ELvl             NOT CREATED BY F LEVEL   GP05180
         ATTR  0,EP0,'EP=0'            ENTRY POINT 0            GP05180
         ATTR  0,ORG0,-'O¬=0'          NOT ORIGIN 0             GP05180
         PRINT NODATA,NOGEN                                     GP05180
ATTRLAST DC    XL2'0'        STOPPER                            GP05180
         SPACE 1
NUMHEX   DC    C'0123456789ABCDEF'  HEX TRANSALATE              GP05180
NUMTAB   EQU   NUMHEX-X'F0',256,C'C'  PSEUDO TRANSLATE TABLE    GP05180
         SPACE 1                                                GP03310
TRTPACK  DC    256X'08'      EVERYTHING INVALID                 GP03310
         ORG   TRTPACK+X'00'                                    GP03310
*   ZONE +       0 1 2 3 4 5 6 7 8 9 A B C D E F                GP03310
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  0-           GP03310
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  1-           GP03310
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  2-           GP03310
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  3-           GP03310
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  4-           GP03310
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  5-           GP03310
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  6-           GP03310
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  7-           GP03310
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  8-           GP03310
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,4,4,8,4'  9-           GP03310
         ORG   ,                                                GP03310
         SPACE 1                                                GP03310
TRTINT   DC    256X'08'      EVERYTHING INVALID                 GP03310
         ORG   TRTINT+C'0'                                      GP03310
*   ZONE +       0 1 2 3 4 5 6 7 8 9 A B C D E F                GP03310
         DC    X'0,0,0,0,0,0,0,0,0,0,8,8,8,8,8,8'  F-           GP03310
         ORG   ,                                                GP03310
         SPACE 1                                                GP03310
TRTUID   DC    256X'08'      EVERYTHING INVALID                 GP06277
         TRENT TRTUID,0,(C'A',9),(C'J',9),(C'S',8)  LETTERS     GP06277
         TRENT ,0,(C'0',10),C'@',C'#',C'$'  DIGITS+NATIONALS    GP06277
         SPACE 2                                                 82123
DWSECT   DSECT ,             WORK AREA IN END OF CALLER'S SAVE  GP05180
         DS    XL4           FAUX ALIGNMENT                     GP05180
DF       DS    XL4           EXTRA FOR UNPACK                   GP05180
DW       DS    XL8           ALMOST DOUBLE-WORD?                GP05180
         SPACE 1
ATRLSECT DSECT ,                                                GP05180
ATRLBITS DS    XL2           ATTRIBUTE BITS TO TEST             GP05180
ATRLNAME DS    CL4           NAME OF MATCH                      GP05180
ATRLONES EQU   X'40'         POSITIVE TEST                      GP05180
ATRLEND  EQU   *                                                GP05180
ATRLSIZE EQU   *-ATRLSECT                                       GP05180
         SPACE 1                                                GP05180
         PRINT &PRTMAC
MAPDEFMT MAPDEFMT DSECT=YES,PFX=PDD                             GP05180
         SPACE 1
         PRINT &PRTSYS
         SPACE 1
         IHAPDS PDSBLDL=NO
         PRINT &PRTMAC
         MAPPDS ,
         SPACE 1
         YREGS ,
         END   ,
