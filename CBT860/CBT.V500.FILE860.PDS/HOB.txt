HOB      TITLE 'H O B  ***  SELECTIVE SUBMISSION OF CANNED JOBS'
         PUNCH ' ORDER HOB(P) '   MAKE DUMPS EASIER TO READ     GP08124
         PUNCH ' SETCODE AC(1) '    JES2 ACCESS                 GP08124
         COPY  OPTIONGB                                          83006
         SPACE 1                                                 83006
         SYSPARM LIST=YES                                        83006
         SPACE 2
***********************************************************************
*                                                                     *
*        JOB SUBMISSION IS STARTED WITH 'START HOB,NAME=XXXX'         *
*                                                                     *
*        JCL BROUGHT IN FROM DISK IS SUBJECT TO MODIFICATION :        *
*              ACCOUNT MAY BE ????,???? TO REQUEST OPERATOR SUPPLIED  *
*              VALUES (ACCOUNT FIELD BEGINS WITH PARENTHESIS)         *
*        JOBS SHOULD NOT BE MIXED UNLESS EACH TERMINATES WITH A NULL  *
*        JOB CARD WITHIN A DD DATA WILL START A NEW JOB          86131
*                                                                80105
*        PARM OPTIONS :                                          80105
*                                                                80105
*        RESTART=JOBNAME     LOCATE SPECIFIED JOB IN MEMBER, AND SUBMIT
*              IT AND FOLLOWING JOBS (SUBJECT TO //*PICK & //*SKIP)
*        PICK=JOBNAME        LOCATE AND SUBMIT THE SINGLE NAMED JOB
*              ONLY.  //*PICK AND //*SKIP ARE IGNORED.           80105
*        DATE=YYDDD          PROCESS INPUT USING THE SPECIFIED, RATHER
*              THAN THE CURRENT DATE.                            84093
*        TEST=ANY            LOOK FOR JOB ON ANY JES QUEUE, SUPPRESS
*                            DUPLICATES                          86251
*        TEST=INP            LOOK FOR JOB ON IN/EXEC/JCL-SCAN QUEUES
*              (DEFAULT)     ONLY, AND SUPPRESS DUPLICATES.      86251
*        TEST=NONE           BYPASS JOBQUEUE CHECKING            86251
*                                                                80105
*        CONTROL CARDS IN INPUT MEMBER :                         80105
*                                                                80105
*        //*PICK      OPTIONS                                    80105
*              SUBMIT FOLLOWING JOBS IF MATCH IN ALL SPECIFIED   80105
*              CATEGORIES.                                       80105
*        //*SKIP      OPTIONS                                    80105
*              SUBMIT UNLESS MATCH IN ALL SPECIFIED CATEGORIES   80105
*        //*SKIPANY   OPTIONS                                    80105
*              SUBMIT UNLESS MATCH IN ANY OPTION VALUE           80105
*                                                                86251
*        NOTE THAT SUCCESSIVE //*PICK AND //*SKIP CARDS (WITHOUT 86251
*              INTERVENING //, /* AND DATA CARDS) FORM A CLUSTER.
*              IN A CLUSTER, A PICK CARD FOLLOWING A MATCHED PICK
*              IS IGNORED; A SKIP FOLLOWING A MATCHED SKIP IS IGNORED;
*              A MATCHED CARD OF OPPOSITE TYPE RESETS THE MATCH TYPE;
*              AND AN UNMATCHED CARD OF THE OPPOSITE TYPE IS IGNORED.
*                                                                86257
*        //*EDIT  SCAN FOR SCAN CHARACTER (HEX E0), REPLACE IT AND
*              NEXT LETTER/DIGIT BY REPLACEMENT STRING (DEFAULT) 86257
*        //*ASIS  DO NOT PERFORM EDIT REPLACEMENT                86257
*        //*SET   X=TEXT  Y='DON''T'  Z="DON'T"                  86257
*              SET VARIABLE (X, Y, Z) TO SPECIFIED STRING        86257
*        //*PRESET X= ...    SET THE VARIABLE ONLY IF NOT PREVIOUSLY
*              DEFINED (E.G., DEFAULT FOR PARM OVERRIDE)         88284
*        //*RUN  DSN(MEMBER),VOLSER  '' X=TEXT ...               86257
*        //*SUB  DSN(MEMBER),VOLSER  '' X=TEXT ...               86257
*              OPEN THE DATASET SPECIFIED BY 'DSN' (MEMBER AND VOLSER
*              ARE OPTIONAL), AND PROCESS LIKE SYSIN UNTIL EOF, THEN
*              RESUME. NOTE THAT THESE CARDS SHOULD BE FOLLOWED BY
*              PICK OR SKIP, BECAUSE THEY MAY INVOKE UNDESIRABLE PICK
*              AND SKIP REQUESTS.  NOTE THAT A DOUBLED SCAN CHARACTER
*              MAY APPEAR AS A SEPARATOR FOR OPTIONAL //*SET TEXT.
*        //*NAME JOBNAME                                         90088
*              TRIGGER SAME PROCESSING AS THOUGH //XXX JOB CARD PRESENT
*                                                                86264
*        //*CMD TEXT... (UNTIL COLUMN 72)                        86264
*              THIS CARD IS HONORED EXCEPT WHEN A PICK/SKIP FAILS.
*              THE TEXT IS ISSUED AS A 'MASTER CONSOLE' COMMAND  86264
*        //*MSG TEXT... (UNTIL COLUMN 72)                        86264
*              THIS CARD IS HONORED EXCEPT WHEN A PICK/SKIP FAILS.
*              THE TEXT IS ISSUED AS A WTO WITH DESC=2, ROUT=1   86264
*                                                                86257
*        //*SUBMIT DSN(MEMBER),VOLSER '' X=TEXT ...              86257
*              SUBMIT FUNCTIONS LIKE //*SUB, BUT IS PROCESSED ONLY
*              WITHIN A JOB, I.E. A PARM=PICK=JOB REQUEST WILL NOT
*              READ IT BEFORE FINDING THE NAMED JOB. JOBS CONTAINED
*              IN IT WILL NOT BE SUBMITTED AFTER THE PARM REQUEST IS
*              MATCHED.                                          86257
*        //*WTO TEXT... (UNTIL COLUMN 72)                        86257
*              THIS CARD IS HONORED WITHIN A JOB ONLY; IT ISSUES THE
*              SPECIFIED TEXT TO THE OPERATOR WITH ROUT=1,DESC=2.
*                                                                86257
*                                                                86251
*        OPTIONS ARE :                                           80105
*                                                                80105
*        CPU SELECTION:  CPU=SYST  WHERE 'SYST' IS THE SMCASID   83277
*             (CL4) FIELD.  MORE THAN ONE ID MAY BE SPECIFIED    83277
*             ON A CARD. ANY MATCH RESULTS IN THE APPROPRIATE    83277
*             PICK OR SKIP OPTION.CPU=, CANCELS SELCTION.        83277
*        DAY OF WEEK:  NAME OF DAY, OR THREE LETTER ABREVIATION  80105
*        DAY OF MONTH:  DAY+NUMBER, E.G. DAY1, DAY01, DAY11...   84062
*             SPECIAL - LAST DAY OF MONTH  DAYL                  84062
*        WEEK OF MONTH:  MONTH1, MONTH2,...MONTH5                80105
*             SPECIAL- LAST WEEK OF MONTH  MONTHL                80105
*        WEEK OF YEAR:   WEEK1, WEEK2, ... WEEK53                80105
*          NOTE THAT WEEK STARTS WITH FIRST DAY OF PERIOD, NOT   80105
*        SUNDAY OR MONDAY.  ALSO, YEAR HAS FRACTIONAL WEEK 53.   80105
*        EXAMPLES :                                              80105
*        RUN ON TUESDAYS ONLY   //*PICK TUESDAY                  80105
*        RUN ON FIRST MONDAY OF MONTH   //*PICK MONDAY MONTH1    80105
*        RUN ON LAST FRIDAY OF MONTH    //*PICK FRI MONTHL       80105
*        RUN ON SPECIFIC SYSTEM    //*PICK CPU=(155A,158B)       83277
*            SELECTION CRITERIA MAY BE NEGATED (LIMITED TO ONCE PER
*              TYPE (ONE OCCURRENCE OF DAY/WEEK/MONTH/ETC.)      89209
*        RUN MON-SAT       //*PICK SUN   OR  //*SKIP ¬SUN        89209
*          NOTE THAT //*PICK ¬TUE ¬THU IS MEANINGFUL, BUT FAILS. 89209
*                                                                80105
*        INPUT DATASET SYSUT1 MAY NOW BE IN WYLBUR EDIT FORMAT.  80105
*          SYSUT1 MAY BE CONCATENATED (INCL. UNLIKE CONCAT.)     80105
*        OUTPUT DATASET SYSPRINT (OR SYSTERM) IS OPTIONAL.       83277
*        ALL JES2 DEPENDENT CODE REMOVED TO @SERVICE ROUTINE; SERVCALL
*        LOJOB USED FOR JOB RETRIEVAL (SPOOL PACK INFO).         90281
*                                                                80105
*        SAMPLE JCL                                              80105
*                                                                80105
*   //HOB PROC PDS='SYS1.JOBJCL',NAME=HELP,RESTART=,PICK=,DAY=,TEST=IN,
*   //             SET=,LIST='DUMMY,',U=,V=                      90281
*   //*      USE:  START HOB,NAME=member   TO SUBMIT CANNED JOB(S)
*   //*                                                          90281
*   //*      USE:  START HOB,NAME=member,PICK=jobname   TO SUBMIT A
*   //*                          SINGLE JOB IN THE MEMBER        90281
*   //*      USE:  START HOB,NAME=member,RESTART=jobname   TO SUBMIT
*   //*                          THE SPECIFIED AND ALL FOLLOWING JOBS.
*   //*            USE DAY=YYDDD TO FORCE DATE SPECIFIC PROCESSING.
*   //*            USE TEST=NO TO BYPASS JOBQUEUE TESTS; TEST=ANY TO
*   //*              SUPPRESS SUBMISSION OF JOBS ALREADY ON 'ANY' QUEUE
*   //*              DEFAULT IS TEST=IN, SUPPRESSES FOR DUPE ON INPUT
*   //*    IF A START HOB DOES NOT READ ANYTHING, CHECK THE MEMBER FOR
*   //*      //*PICK AND //*SKIP CARDS - SOME JOBS WILL BE SUBMITTED
*   //*      ON SPECIFIC DAYS (WEEKS, ETC.) ONLY, AND WILL NOT WORK
*   //*      WHEN STARTED AT THE WRONG TIME.                     90281
*   //*                                                          90281
*   //IEFPROC EXEC PGM=HOB,PARM='R=&RESTART,P=&PICK,D=&DAY,      90281
*   //             T=&TEST\&SET'                                 90281
*   //SYSPRINT DD  &LIST.SYSOUT=*                                90281
*   //SYSUT1   DD  DDNAME=IEFRDER                                90281
*   //IEFRDER  DD  DISP=SHR,DSN=&PDS.(&NAME),UNIT=&U,VOL=SER=&V  90281
*   //SYSUDUMP DD  SYSOUT=*                                      90281
*   //SYSUT2   DD  SYSOUT=(A,INTRDR)   PRE-MVS: UNIT=INTRDR      90281
*                                                                     *
***********************************************************************
         SPACE 1                                                 86264
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 1980-1990  EXPERT SYSTEM PROGRAMMING, INC.         *
*        COPYRIGHT 2002       EXPERT SYSTEM PROGRAMMING               *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 1                                                GP02309
***********************************************************************
*  MAINTENANCE:                                                       *
*                                                                     *
*  2002/11/05  GYP  CHANGED JES2 INTERFACE; ADDED RACF CRUD           *
*                   (NOTE JQE1BUSY FLAG IS GONE; SEE JQEBUSY BYTE)    *
*                   ADJUSTED DATE DEPENDENCIES FOR ADDED 1900         *
*                                                                     *
***********************************************************************
         EJECT ,                                                GP02309
         PRINT &PRTSOR                                           83006
HOB      PGMHEAD ZERO12,BASE=(R11,R12),PARM=R9,                        *
               BNDRY=PAGE,AM=31,RM=24                           GP02310
         SERVINIT ,          LOAD AND INIT @SERVICE ROUTINE      83006
         SERVLOAD @INPREAD,@PRINTER,@SRVJES2  LOAD SERVICE RTNS GP06282
         MVC   PICKJOB,BLANKS                                    83006
         MVI   CCFLAG,FGQINP DEFAULT - SKIP DUPLICATE IN/EXEC JOBS
         L     R1,CVTPTR     GET CVT                             83277
         L     R1,CVTSMCA-CVTMAP(,R1)   GET SMCA                 83277
         MVC   MYCPU,SMCASID-SMCABASE(R1)  GET SYSTEM ID         83277
         PRTOPEN SYSPRINT,OPT=(DUMMY,NOWTO),DEV=2   LISTING      83277
         PRTLIST HEADER,TITLE=1,DEV=2  SET PRINT TITLE           83277
         TIME  BIN                                               84093
         STM   R0,R1,DB      SAVE FOR 'NOPARM'                   84093
         LA    R1,0(,R9)     CLEAN PARM POINTER                  83006
         LTR   R1,R1                                             80105
         BZ    NOPARM                                            80105
         L     R1,0(,R1)     ELSE GET PARM ADDRESS               80105
         LA    R1,0(,R1)     CLEAN HIGH BIT                     GP02310
         LTR   R1,R1         ANY PARM ADDRESS ?                 GP02310
         BZ    NOPARM                                            80105
         SLR   R3,R3                                             86257
         ICM   R3,3,0(R1)    GET PARM LENGTH                     86257
         BNP   NOPARM                                            80105
         MVI   CARD,C' '                                         86257
         MVC   CARD+1(2*L'CARD-1),CARD  BLANK CARD + CARD2       86257
         LR    R2,R3         GET PARM LENGTH BACK FOR PRTLIST    86257
         BCTR  R3,0          GET LENGTH FOR EXECUTE              86257
         EX    R3,PARMMVC    UPPER-CASE AND MOVE FOR EASIER CHECKING
         PRTLIST PARMMSG,DEV=2  ECHO THE PARM                    86257
         LA    R1,CARD       SET FIRST BYTE TO SCAN              86257
         AR    R3,R1         POINT TO LAST BYTE                  86257
         LA    R2,1          SET INCREMENT                       86257
PARMLOOP BAL   R9,NONBLANK   FIND A NICE CHARACTER               86257
         B     PARMSEND      GO TO END CHECKING                  86257
         LA    R15,5-1       SET SKIP LENGTH                     86257
         CLC   =C'PICK=',0(R1)  PICK OPTION ?                    86257
         BE    PARMPICK      YES                                 80105
         LA    R15,8-1       SET SKIP LENGTH                     86257
         CLC   =C'RESTART=',0(R1)  RESTART OPTION ?              80105
         BE    PARMREST      YES                                 80105
         LA    R15,5-1                                           84093
         CLC   =C'DATE=',0(R1)  FORCED DATE ?                    84093
         BE    PARMDATE                                          84093
         CLC   =C'TEST=',0(R1)                                   86251
         BE    PARMTEST      QUEUE TEST OPTION                   86251
         LA    R15,4-1                                           84093
         CLC   =C'DAY=',0(R1)                                    84093
         BE    PARMDATE                                          84093
         LA    R15,2-1                                           84093
         CLC   =C'R=',0(R1)                                      80105
         BE    PARMREST      RESTART                             80105
         CLC   =C'P=',0(R1)  PICK ?                              80105
         BE    PARMPICK                                          80105
         CLC   =C'D=',0(R1)                                      84093
         BE    PARMDATE                                          84093
         CLC   =C'T=',0(R1)                                      86251
         BE    PARMTEST      QUEUE TEST OPTION                   86251
         CLI   0(R1),SCANCHAR  START OF VARIABLE SET FIELD ?     86257
         BNE   PARMBWTO      NO                                  86257
         BXH   R1,R2,PARMSEND  NULL                              86257
         CLI   0(R1),C'('    PARENTHESIZED PARM FIELD ?          86257
         BNE   PARMPARS      NO; INVOKE 'SET' PARSE              86257
         BXH   R1,R2,PARMBWTO  ERROR IF NO CLOSING PARENTHESIS   86257
         CLI   0(R3),C')'    CLOSER ?                            86257
         BNE   PARMBWTO      NO; ERROR                           86257
         BCTR  R3,0          BACKSPACE                           86257
         CR    R1,R3         ANY PARM LEFT ?                     86257
         BH    PARMSEND      NO; DON'T WASTE TIME                86257
PARMPARS BAL   R10,SETPARSE  PARSE REMAINDER                     86257
         B     PARMBWTO      TOO BAD                             86257
*NEXT    B     PARMSEND      ELSE GOOD SETS                      86257
         SPACE 1                                                 86257
PARMSEND TM    JOBFLAG,JFSING+JFREST  BOTH CHOSEN ?              80105
         BNO   DONEPARM      NO; CONTINUE                        86257
PARMBWTO WTO   'HOB - INVALID PARM FIELD',ROUTCDE=1,DESC=1       80105
         B     EXIT                                              80105
PARMMVC  OC    CARD(0),2(R1)   MOVE PARM TO SAFER LOCATION       86257
PARMMSG  FDPRT 'PARM: ''',NL                                     86257
         FDPRT CARD,(R2)                                         86257
         FDPRT ''''                                              86257
         FDPRT *END                                              86257
         SPACE 1                                                 80105
PARMPICK BAL   R10,PARMCOMN  EXTRACT NON-BLANK PARAMETER         86257
         OI    JOBFLAG,JJIGN+JFSING  SIGNAL SINGLE JOB TO DO     80105
         B     PARMCMVC      MOVE JOB NAME                       86251
         SPACE 1                                                 86257
PARMREST BAL   R10,PARMCOMN  EXTRACT OPERAND                     86257
         OI    JOBFLAG,JJIGN+JFREST  SET FOR RESTART             80105
PARMCMVC MVC   PICKJOB,LOOKDB    MOVE JOBNAME                    86251
         B     PARMLOOP                                          86257
         SPACE 1                                                 86251
PARMTEST BAL   R10,PARMCOMN  EXTRACT OPERAND                     86257
         OI    CCFLAG,FGQANY  SET FOR ALL QUEUES TO BE CHECKED   86251
         NI    CCFLAG,255-FGQINP  NOT INPUT ONLY                 86251
         CLI   LOOKDB,C'A'   ANY/ALL REQUEST ?                   86251
         BE    PARMLOOP      YES; FLAGS SET                      86257
         XI    CCFLAG,FGQANY+FGQINP  FLIP FOR INPUT TEST ONLY    86251
         CLI   LOOKDB,C'I'   INPUT ?                             86251
         BE    PARMLOOP      YES ?                               86257
         CLI   LOOKDB,C'N'   NONE ?                              86251
         BNE   PARMBWTO      NO; TOO BAD                         86251
         NI    CCFLAG,255-FGQANY-FGQINP  NO TESTS                86251
         B     PARMLOOP                                          86257
         SPACE 1                                                 84093
PARMDATE BAL   R10,PARMCOMN  EXTRACT OPERAND                     86257
         MVC   DB,LOOKDB     COPY SUPPOSED NUMERIC PARAMETER     89058
         CLI   LOOKDB+7,C' '  MORE THAN 7 BYTES ?               GP02323
         BNE   PARMBWTO      YES; FAIL                          GP02323
         NC    DB(7),=8C'0'  NUMERIC ZONES                      GP02323
         CLC   DB(7),=8C'0'  NUMERIC ZONES ?                    GP02323
         BNE   PARMDAY5      NO; LOOK FOR OLD YYDDD FORM        GP02323
         PACK  DB(8),LOOKDB(7)  PACK CCYYDDD FORM               GP02323
         B     PARMLOOP      AND CONTINUE                       GP02323
         SPACE 1                                                GP02323
PARMDAY5 CLI   LOOKDB+5,C' '  MORE THAN 5 BYTES ?                86257
         BNE   PARMBWTO      YES; FAIL                           86257
         NC    DB(5),=8C'0'  NUMERIC ZONES                       89058
         CLC   DB(5),=8C'0'  NUMERIC ZONES ?                     89058
         BNE   PARMBWTO      NO                                  84093
         IC    R14,DB+4      SAVE CURRENT CENTURY BIT           GP02310
         PACK  DB(8),LOOKDB(5)  SAVE FOR 'NOPARM' CODE           86257
         STC   R14,DB+4      SET IT BACK                        GP02310
         B     PARMLOOP      LOOK FOR MORE                       86257
         SPACE 1                                                 86257
PARMCOMN AR    R1,R15        SKIP OVER VERB                      86257
         BXH   R1,R2,PARMSEND  SKIP IF NULL PARM                 86257
         LA    R14,LOOKDB                                        86257
         LA    R15,L'LOOKDB                                      86257
         MVC   LOOKDB,BLANKS                                     86257
         BAL   R9,PUSHMOVE   MOVE TEXT                           86257
         B     PARMLOOP      NULL TEXT                           86257
         BR    R10           RETURN TO CALLER                    86257
         SPACE 1                                                 80105
NOPARM   PRTL  'NO PARM; DEFAULT OPTIONS USED',CC=NO,DEV=2      GP02310
         SPACE 1                                                GP02310
DONEPARM L     R1,DB+4       GET CURRENT DATE BACK              GP02323
         CALL  SUBP2W        GET VARIOUS VALUES                 GP02323
         LTR   R1,R1         VALID ?                            GP02323
         BNZ   DONEDATE      YES                                GP02323
         PRTL  'ERROR GETTING DATE VALUES',CC=NO,DEV=2          GP02323
         SERVTERM ,                                             GP02323
         ABEND 11,DUMP                                          GP02323
         SPACE 1                                                GP02323
DONEDATE MVC   MONTH1(PARMP2WL),0(R15) SAVE RETURNED DATA       GP02323
         PRTLIST CURRINFO,DEV=2  SHOW CURRENT DATE, ETC.         89212
         PRTSPACE 1,DEV=2                                        89212
         SPACE 1                                                 86251
         TESTAUTH FCTN=1     ARE WE RUNNING AUTHORIZED ?        GP02309
         BXH   R15,R15,COMAUTH  NO                              GP02309
         OI    MISCFG,MSAUTH  REMEMBER WE'RE AUTHORIZED         GP02309
         MODESET KEY=ZERO,MODE=SUP   GET PRIVILEGED             GP02309
         PUSH  USING                                            GP02309
         L     R2,PSATOLD-PSA  GET CURRENT TCB                  GP02309
         USING TCB,R2                                           GP02309
         L     R3,TCBJSCB     GET THE JSCB                      GP02309
         USING IEZJSCB,R3                                       GP02309
         OI    JSCBSWT1,JSCBPASS  ACCESS REFERENCED INPUT DATA  GP02309
         AGO   .FAILS
         ICM   R15,15,JSCBSSIB  GET SUBSYSTEM INFORMATION BLOCK GP02305
         BZ    RESAUTH       OR OUT                             GP02305
         USING SSIBEGIN,R15                                     GP02305
         CLC   =C'SSIB',SSIBID  REALLY SSIB ?                   GP02305
         BNE   RESAUTH       NO                                 GP02305
         CLI   SSIBSSID,SSIBJES3  JES3 SYSTEM ?                 GP02305
         BE    RESAUTH       WHAT DO I KNOW                     GP02305
         CLI   SSIBSSID,SSIBJES2  JES2 SYSTEM ?                 GP02305
         BNE   SYSAUTH       SUB=MSTR ?                         GP02305
         ICM   R14,15,SSIBSUSE  GET SJB ADDRESS                 GP02305
         BZ    RESAUTH                                          GP02305
         CLC   =C'STC',SSIBJBID  START TASK ?                   GP02305
         BNE   RESAUTH       NO                                 GP02305
SYSAUTH  L     R2,PSAAOLD-PSA          POINT TO MY ASCB.        GP02309
         L     R2,ASCBASXB-ASCB(,R2)   POINT TO MY ASXB.        GP02309
         L     R2,ASXBSENV-ASXB(,R2)   POINT TO MY ACEE.        GP02309
         USING ACEE,R2                                          GP02309
         CLC   =X'015C',ACEEUSER  DEFAULT ?                     GP02309
         BNE   SYSAUTHG                NO, CONTINUE             GP02309
         MVC   ACEEUSER,=X'07C9C2D4E4E2C5D740'  AUTHORIZED USER?
SYSAUTHG CLC   =X'015C',ACEEGRP   DEFAULT ?                     GP02309
         BNE   SYSAUTHF                YES, CONTINUE            GP02309
         MVC   ACEEGRP,=X'04E2E6E2F140404040'  SPECIAL GROUP    GP02309
SYSAUTHF MVI   ACEEFLG1,ACEESPEC+ACEEOPER+ACEEAUDT+ACEEPRIV+ACEERACF
         MVI   ACEEFLG2,ACEEALTR                                GP02309
.FAILS   ANOP  ,
RESAUTH  MODESET KEY=NZERO,MODE=PROB  GET BACK TO NORMAL        GP02309
         POP   USING                                            GP02309
         SPACE 1                                                GP02309
COMAUTH  TM    JOBFLAG,JFSING  LOOKING FOR SINGLE JOB ?          86251
         BNZ   HASPUP        YES; SKIP JES SEARCH                86251
         TM    CCFLAG,FGQINP+FGQANY  QUEUE SEARCH REQUIRED ?     86251
         BZ    HASPUP        NO                                  86251
         LTCB  R4,USE=YES    GET OUR TCB                         86251
         L     R2,TCBTIO     GET THE TIOT                        86251
         MVC   LOOKNAME,TIOCNJOB-TIOT1(R2)   SAVE CURRENT JOB NAME
         LA    R9,HASPUP     SET 'SCHEDULE' FAILURE RETURN       89309
         AIF   ('&LOCAL' NE 'PID').NOACCT0                       92279
         SERVCALL ACGET,LOOKACCT  GET OUR ACCOUNT #              86251
         STC   R0,DB                                             90281
         TM    DB,VAASTC     START TASK ?                        90281
         BNZ   *+10          YES; ACCOUNT MAY BE DEFAULT         90281
         MVC   SJ1ACCT,LOOKACCT  REQUEST SCAN BY ACCOUNT         90281
.NOACCT0 MVC   SJ1JNAM,BLANKS                                    90281
         MVI   SJ1JNAM,C'*'  SET TO SEARCH FOR THIS JOB          90281
         SERVCALL LOJOB,COPYJES  GET JOB INFORMATION             90281
         BXLE  R15,R15,HASPUP  OK                                90281
         SPACE 1                                                 86251
SCHEDERR OICC  16,(R0)                                          GP06282
         WTO   'MSG881E ERROR IN SUB-SYSTEM INTERFACE',                *
               ROUTCDE=1,DESC=3  CONSIDER 1,1 ?                  89309
         OI    MISCFG,MSNOJES  SET INTERFACE DISABLED            89309
         BR    R9            RETURN WITH ERROR, BUT PROCEED      89309
         SPACE 1                                                GP02309
HASPUP   MVC   SYSUT1(PATUT1L),PATUT1  MAKE SYSIN/SYSUT1 PARM    86257
         INPOPEN SYSUT1,OPT=(EXIST,NOWTO)  OPEN INPUT FILE       93312
         CH    R15,=H'4'     OPEN ?                              93312
         BNH   INOPEN                                            93312
         STMAX R15,RETCODE   PASS AS RETURN CODE                GP06282
         CH    R15,=H'10'    MISSING MEMBER ?                    93312
         BNE   INBADDSN      NO                                  93312
         PRTV  MSGNOMEM,CC=NO,DEV=2  PRINT THE ERROR MESSAGE    GP02310
         WTO   MF=(E,MSGNOMEM)                                  GP02310
         B     EXIT                                              93312
INBADDSN WTO   MF=(E,MSGBDUT1)                                  GP02310
         PRTV  MSGBDUT1,CC=NO,DEV=2  PRINT THE ERROR MESSAGE    GP02310
         B     EXIT                                              93312
MSGNOMEM VCON  'MSG884E MEMBER NAME NOT FOUND'                  GP02310
MSGBDUT1 VCON  'MSG883E INPUT DATASET FAILED TO OPEN'           GP02310
         SPACE 1                                                GP02310
INOPEN   MVI   INPDEV,1      SET FOR FILE 1                      86257
         LA    R14,SYSUT1    SET CURRENT INPUT FILE              86257
         LA    R15,INPDSN    SET DSN POINTER                     86257
         LA    R0,INPMEM     SET MEMBER POINTER                  86257
         LA    R1,INPVOL     SET VOL-SER POINTER                 86257
         STM   R14,R1,INPPTR    SET ALLOCATION LIST POINTERS     86257
         BAL   R9,SETNOJOB   INITIALIZE FOR NEW JOB              75278
         SPACE 2                                                 80105
LOOP     INPGET DEV==INPDEV  GET THE NEXT RECORD                 86257
         BXH   R15,R15,EOF   ERROR OR END-FILE ON INPUT          83006
         MVC   CARD,0(R1)    MOVE TEXT TO CARD IMAGE             86131
         PRTLIST ECHOCARD,DEV=2   SHOW INPUT                     83277
         SPACE 1                                                 80105
         TM    JOBFLAG,JFASIS  SUPPRESS EDITING ?                86257
         BNZ   NONEEDIT      YES                                 86257
SCANCHAR EQU   X'E0'         REVERSE SLASH FOR VARIABLE SUBSTITUTION
         MVC   OLDCARD,CARD  SAVE THE CARD IMAGE                 86257
         MVC   CARD(71),BLANKS  PRE-CLEAR                        86257
         LA    R7,CARD       SET OUTPUT START                    86257
         LA    R4,OLDCARD    SET INPUT START                     86257
         LA    R5,71         SET SCAN LENGTH                     86257
EDITLOOP LTR   R15,R5        GET RESIDUAL LENGTH                 86257
         BNP   DONEEDIT      ALL DONE                            86257
         BCTR  R15,R0        MAKE EXECUTE LENGTH                 86257
         LA    R1,0(R4,R5)   SET END POSITION                    86257
         SLR   R2,R2         PRESET                              86257
         EX    R15,EDITTRT   LOOK FOR EDIT CHARACTER             86257
         LR    R14,R1        SAVE THE HIT POSITION               86257
         SR    R1,R4         GET THE SKIPPED STRING LENGTH       86257
         BNP   EDITNMVC      NOTHING TO MOVE                     86257
         LR    R0,R4         GET THE INPUT START                 86257
         LR    R4,R14        NEW INPUT POSITION                  86257
         SR    R5,R1         RESIDUAL LENGTH TO PROCESS          86257
         LR    R14,R7        CURRENT OUTPUT POSITION             86257
         LA    R15,CARD+71   GET END COLUMN                      86257
         SR    R15,R14       MAXIMUM TO MOVE                     86257
         BNP   DONEEDIT      NOTHING TO MOVE                     86257
         AR    R7,R1         NEW OUTPUT POSITION                 86257
         ICM   R1,8,BLANKS   PAD                                 86257
         MVCL  R14,R0        MOVE ORIGINAL TEXT                  86257
EDITNMVC LTR   R2,R2         DID WE GET A HIT ?                  86257
         BZ    DONEEDIT      NO; DONE                            86257
         CH    R5,=H'1'      ANYTHING AFTER US ?                 86257
         BNH   EDMOVONE      NO; JUST MOVE THIS ONE              86257
         CLI   1(R4),SCANCHAR  SCAN CHARACTER DOUBLED ?          86257
         BE    EDMOVSKP      YES; SKIP ONE AND COPY ONE          86257
         SLR   R2,R2                                             86257
         IC    R2,1(,R4)     GET THE CHARACTER AFTER             86257
         SH    R2,=Y(C'A')   ALPHABETIC ?                        86257
         BM    EDMOVTWO      MOVE TWO BYTES                      86257
         IC    R2,EDITSCOF(R2)  GET TABLE OFFSET                 86257
         SLA   R2,6          CONVERT TO OFFSET                   86257
         BZ    EDMOVTWO      NOT VALID; MOVE TWO BYTES           86257
         LA    R2,VARSPACE-64(R2)  GET TABLE ENTRY               86257
         SLR   R1,R1                                             86257
         ICM   R1,1,0(R2)    GET THE VARIABLE'S LENGTH           86257
         BZ    EDSKPTWO      NULL; JUST EDIT OUT                 86257
         LA    R0,1(,R2)     GET THE VARIABLE'S TEXT             86257
         B     EDMOVCOM                                          86257
EDMOVTWO LA    R1,2          COPY TWO BYTES                      86257
         B     EDMOVMOV                                          86257
EDMOVSKP LA    R4,1(,R4)                                         86257
         BCTR  R5,0          SKIP FIRST                          86257
EDMOVONE LA    R1,1                                              86257
EDMOVMOV LR    R0,R4         START OF INPUT                      86257
         AR    R4,R1         NEW INPUT POSITION                  86257
         SR    R5,R1         RESIDUAL LENGTH                     86257
         BM    DONEEDIT      SHOULD NOT HAVE GOTTEN HERE         86257
         SH    R4,=H'2'      COMPENSATE FOR 'EDSKPTWO' CODE      86257
         AH    R5,=H'2'                                          86257
EDMOVCOM LR    R14,R7        CURRENT OUTPUT POSITION             86257
         LA    R15,CARD+71   GET END COLUMN                      86257
         SR    R15,R14       MAXIMUM TO MOVE                     86257
         BNP   DONEEDIT      NOTHING TO MOVE                     86257
         AR    R7,R1         NEW OUTPUT POSITION                 86257
         ICM   R1,8,BLANKS   PAD                                 86257
         MVCL  R14,R0        MOVE ORIGINAL TEXT                  86257
EDSKPTWO LA    R4,2(,R4)                                         86257
         SH    R5,=H'2'                                          86257
         BP    EDITLOOP                                          86257
DONEEDIT CLC   CARD,OLDCARD  DID WE CHANGE ANYTHING ?            86257
         BE    NONEEDIT      NO                                  86257
         PRTLIST EDITCARD,DEV=2  SHOW EDITED CARD                86257
NONEEDIT CLC   DCJCL,CARD    CONTROL CARD ?                      86131
         BNE   LOOPCHUS      NO; TEST FURTHER                    80105
         CLI   CARD+2,C'*'   POSSIBLE CONTROL CARD ?             80105
         BNE   LOOPTJOB      NO; TEST FOR JOB CARD               86131
         LA    R15,5         SET SKIP INCREMENT                  86257
         LA    R2,1          INCREMENT                           86257
         LA    R3,CARD+71    LAST VALID COLUMN                   86257
         CLC   =C'RUN ',CARD+3  RUN COMMAND ?                    86257
         BE    PUSHDOWN      YES; REQUEST TO OPEN NEW FILE       86257
         CLC   =C'SET ',CARD+3  SET VARIABLE VALUES ?            86257
         BE    VARSET        YES; SCAN                           86257
         CLC   =C'SUB ',CARD+3                                   86257
         BE    PUSHDOWN                                          86257
         LA    R15,9         SET SKIP INCREMENT                  88284
         CLC   =C'PRESET ',CARD+3  CONDITIONAL VARIABLES ?       88284
         BE    VACSET        YES; SCAN                           88284
         LA    R15,6         RESET SKIP                          88284
         CLC   =C'NAME',CARD+3   NAME OPTION ?                   90088
         BE    LOOPNAME      YES; SEE IF JOB SELECT START/STOP   90088
         CLC   =C'EDIT',CARD+3  RESUME EDITING ?                 86257
         BE    LOOPSED       YES                                 86257
         CLC   =C'ASIS',CARD+3  CANCEL EDITING ?                 86257
         BNE   LOOPNAS       NO                                  86257
         OI    JOBFLAG,JFASIS   SUPPRESS CARD EDITING            86257
         B     LOOP          GET ANOTHER                         86257
LOOPSED  NI    JOBFLAG,255-JFASIS  RESUME EDITING                86257
         B     LOOP          GET ANOTHER                         86257
         SPACE 1                                                 86257
LOOPNAS  TM    JOBFLAG,JFSING  LOOKING FOR SINGLE JOB ?          86257
         BNZ   LOOPJOB       YES; GO TO JOB TEST                 80105
         CLC   DCPICK,CARD   SELECTION CARD ?                    89209
         BNE   LOOPSKPT      NO; TEST FOR SKIP CARD              80105
         TM    CCFLAG,FGCLUST+FGLPICK  LAST ACCEPTED WAS PICK ?  86251
         BO    LOOP          YES; IGNORE SUBSEQUENT PICK         86251
         BAL   R9,PICKSKIP   ANALYZE PARAMETERS                  80105
         PRTLIST MSGFLGS,DEV=2  SHOW TEST RESULTS                90101
         SLR   R15,R15                                           80105
         ICM   R15,3,PSFLAGS GET OCCURRENCE AND MATCH FLAGS      90098
         LR    R14,R15       COPY OVER                           90098
         N     R14,=A(PSFMATCH)  MASK OPTIONS MATCHED            90098
         SRL   R15,1         MAKE CORRESPONDING 'PRESENT' BITS   90098
         N     R15,=A(PSFMATCH) MASK OPTIONS FOUND               90101
         CR    R14,R15       TEST THAT ALL FOUND WERE MATCHED    90098
         BNE   LOOPIALL      MISMATCH - SKIP                     90098
         OI    CCFLAG,FGCLUST+FGLPICK  CLUSTER / PICK ACCEPTED   86251
         B     LOOPTALL      ALL GIVEN MATCH - SELECT            86251
         SPACE 1                                                 90098
LOOPSKPT CLC   DCSKIP,CARD   EXCLUSION CARD ?                    89209
         BNE   LOOPCHUS      NO; TREAT AS NON-JCL CARD           80105
         TM    CCFLAG,FGCLUST+FGLPICK  IN PICK CLUSTER ?         86251
         BM    LOOP          NO; SKIP IN SKIP CLUSTER            86251
         OI    JOBFLAG,JFPISK  SHOW PICK/SKIP FOUND              86131
         BAL   R9,PICKSKIP   ANALYZE PARAMETERS                  80105
         PRTLIST MSGFLGS,DEV=2  SHOW TEST RESULTS                90101
         SLR   R15,R15                                           90098
         ICM   R15,3,PSFLAGS GET FOUND AND MATCHED FLAGS         90098
         LR    R14,R15                                           90098
         N     R14,=A(PSFMATCH)  GET OPTIONS MATCHED             90098
         CLC   =C'ANY',CARD+7   REQUEST FOR SKIP ON ANY MATCH ?  80105
         BNE   LOOPSKPN      NO                                  80105
         LTR   R14,R14       ANY MATCHES ?                       90098
         BNZ   LOOPIALL      YES; SKIP                           80105
         B     LOOPSALL      ELSE CONTINUE                       80105
LOOPSKPN SRL   R15,1         MAKE CORRESPONDING 'PRESENT' BITS   90098
         N     R15,=A(PSFMATCH) GET OPTIONS SPECIFIED            90101
         CR    R14,R15       MATCH AGAINST FLAGS                 90098
         BE    LOOPIALL      ALL  CHOSEN ONES MATCH - SKIP       90098
LOOPSALL OI    CCFLAG,FGCLUST  WE ARE IN A CLUSTER               86251
         NI    CCFLAG,255-FGLPICK  LAST WAS SKIP                 86251
LOOPTALL NI    JOBFLAG,255-JFIGN  PROCESS ALL CARDS              86251
         B     LOOP                                              80105
LOOPIALL OI    JOBFLAG,JFIGN  IGNORE ALL                         80105
         B     LOOP                                              80105
         SPACE 1                                                 80105
LOOPTJOB NI    CCFLAG,255-FGCLUST-FGLPICK  NOT IN A GROUP        86251
         TM    JOBFLAG,JFSING  LOOKING FOR SINGLE JOB ?          86257
         BNZ   LOOPJOB       YES; GO TO JOB TEST                 86257
         TM    JOBFLAG,JFIGN  SKIP IGNORE ON ?                   86251
         BNZ   LOOP          YES; SKIP                           86131
LOOPJOB  CLI   CARD+2,C'*'   COMMENT CARD ?                      86131
         BE    LOOPCHUS      YES; NOT JOB CARD                   86131
         LA    R1,CARD+2                                         83277
         LA    R2,16                                             80105
LOOPJOBA CLI   0(R1),C' '    TRAILING BLANK ?                    83277
         BE    LOOPJOBB      YES                                 83277
         LA    R1,1(,R1)                                         83277
         BCT   R2,LOOPJOBA   TRY AGAIN                           83277
         B     LOOPCHUS      NOT A JOB CARD ?                    83277
LOOPJOBB LR    R14,R1        KEEP R1 FOR NAME LENGTH CALCULATION 83277
LOOPJOBL CLC   =C' JOB ',0(R14)                                  83277
         BE    LOOPJOBN      EXTRACT JOBNAME                     80105
         LA    R14,1(,R14)                                       83277
         BCT   R2,LOOPJOBL   TRY AGAIN                           80105
LOOPCHUS NI    CCFLAG,255-FGCLUST-FGLPICK  NOT IN A GROUP        86251
         LA    R15,6         SET SKIP INCREMENT                  86257
         LA    R2,1          INCREMENT                           86257
         LA    R3,CARD+71    LAST VALID COLUMN                   86257
         TM    JOBFLAG,JFSING+JFREST  SKIPPING ?                 86264
         BZ    LOOPCHUM      NO; TEST FOR SPECIALS               86264
         TM    JOBFLAG,JFREST  RESTART SKIP ?                    86264
         BNZ   LOOPCHUT      YES                                 86264
         TM    JOBFLAG,JJIGN JOB IGNORE ON ?                     86264
         BNZ   LOOPCHUT      YES; NOT IN JOB YET                 86264
LOOPCHUM TM    JOBFLAG,JFIGN   PICK/SKIP IGNORE ?                86264
         BNZ   LOOPCHUT      YES; SKIP                           86264
         CLC   DCJCLCOM,CARD //* CARD ?                          86264
         BNE   LOOPCHUT      NO                                  86264
         LA    R15,6         SET SKIP AMOUNT                     86264
         CLC   =C'CMD',CARD+3  SYSTEM COMMAND ?                  86264
         BE    MAKECMD       YES                                 86264
         CLC   =C'MSG',CARD+3  WTO OUTSIDE A JOB ?               86264
         BE    MAKEWTO       YES                                 86264
LOOPCHUT TM    JOBFLAG,JFIGN+JJIGN  JOB OR SKIP IGNORE ON ?      86251
         BNZ   LOOP          YES; SKIP THIS CARD                 80105
         CLC   DCJCLCOM,CARD //* CARD ?                          86257
         BNE   LOOPS         NO                                  86257
         CLC   =C'WTO ',CARD+3  MESSAGE TO OPERATOR ?            86257
         BE    MAKEWTO                                           86257
         LA    R15,9         SET INCREMENT                       86257
         CLC   =C'SUBMIT ',CARD+3  CONDITIONAL FORM OF RUN       86257
         BNE   LOOPS         NO; PROCESS                         86257
PUSHDOWN CLI   INPDEV,X'80'  ALREADY DOING FILE 8 ?              86257
         BNE   PUSHPARS      NO; PARSE AND CONTINUE              86257
         PRTL  '*** EXCESSIVE NESTING LEVEL ***',CC=NO,DEV=2    GP02310
         OICC  4             SET WARNING                        GP06282
         B     LOOPS         CONTINUE PROCESSING                 80105
         SPACE 1                                                 86257
PUSHPARS LA    R1,CARD+1(R15)  FIRST COLUMN TO BE SCANNED        90203
         MVC   INPDSN(L'INPDSN+L'INPMEM+L'INPVOL),BLANKS         86257
         BAL   R9,NONBLANK                                       86257
         B     PUSHBAD                                           86257
         LA    R14,INPDSN    LOOKING FOR DSN                     86257
         LA    R15,L'INPDSN                                      86257
         BAL   R9,PUSHMOVE   MOVE                                86257
         B     PUSHBAD       BAD CARD                            86257
         CLI   0(R1),C'('    MEMBER FOUND ?                      86257
         BNE   PUSHNMEM      NO                                  86257
         BXH   R1,R2,PUSHALLO  SKIP IF NULL                      86257
         LA    R14,INPMEM                                        86257
         LA    R15,L'INPMEM                                      86257
         BAL   R9,PUSHMOVE   MOVE MEMBER NAME                    86257
         NOP   0             ALLOW NULL MEMBER (MAY BE NON-PDS)  86257
PUSHNMEM BAL   R9,NONBLANK   GET NEXT SIGNIFICANT BYTE           86257
         B     PUSHALLO      NONE; ALLOCATE                      86257
         LA    R14,INPVOL                                        86257
         LA    R15,L'INPVOL                                      86257
         BAL   R9,PUSHMOVE   MOVE VOLSER                         86257
         NOP   0             IGNORE NULL STRING ?                86257
PUSHTSET BAL   R9,NONBLANK   GET END OF VOLSER                   86257
         B     PUSHALLO      SKIP IF NOTHING MORE                86257
         CLI   0(R1),SCANCHAR  IMPLICIT SET COMMAND ?            86257
         BNE   PUSHALLO      IGNORE PRESUMED COMMENTS            86257
         BXH   R1,R2,PUSHALLO  GET NEXT COLUMN                   86257
         BAL   R10,SETPARSE  PARSE THE REMAINDER                 86257
         B     PUSHBAD       FAIL BAD CARD                       86257
PUSHALLO SLR   R0,R0                                             86257
         IC    R0,INPDEV     GET DEVICE MASK                     86257
         SLL   R0,1          SET TO NEXT NUMBER                  86257
         STC   R0,INPDEV                                         86257
         L     R1,INPPTR     GET THE WORK AREA POINTER           86257
         LA    R1,PATUT1L(,R1)  POINT TO THE NEXT AREA           86257
         MVC   0(PATUT1L,R1),PATUT1  MAKE THE NEW WORK AREA      86257
         XC    0(16,R1),0(R1)  BUT KILL THE DDNAME               86257
         ST    R1,INPPTR     STASH THE POINTER                   86257
         SERVCALL ALCDD,INPPARM  TRY TO ALLOCATE                 86257
         STM   R15,R0,DB     SAVE RETURN CODE                    86257
         BXLE  R15,R15,PUSHPUSH                                  86257
         PRTL  '*** ALLOCATION FAILED ***',CC=NO,DEV=2          GP02310
         OICC  8             SET ERROR                          GP06282
         B     LOOPS         COPY IT                             86257
PUSHBAD  OICC  8             SET ERROR                          GP06282
         PRTL  '*** ERROR DURING SYNTAX SCANNING ***',CC=NO,DEV=2
         B     LOOPS                                             86257
PUSHPUSH L     R1,INPPTR     GET POINTER                         86257
         INPOPEN (R1),DEV==INPDEV,OPT=EXIST                      93312
         BXLE  R15,R15,LOOP  PROCESS                             86257
         OICC  8             SET RETURN CODE                    GP06282
         PRTL  '*** OPEN FAILED ***',CC=NO,DEV=2                GP02310
         B     LOOPS                                             86257
         SPACE 1                                                 86264
MAKECMD  TM    MISCFG,MSAUTH  ARE WE AUTHORIZED ?               GP02309
         BZ    MAKEWTO       NO; ISSUE AS WTO INSTEAD           GP02309
         MVC   CARD2+4(65),CARD+7  COPY MESSAGE                  86264
         MVC   CARD2(4),=X'00460000'  SET LENGTH (66)            86264
         MVI   CARD+2+4+65,C' '  ADD TRAILING BLANK              86264
         SLR   R0,R0         CLEAR UCM ID                        86264
         L     R2,CVTPTR     GET THE CVT                         86264
         ICM   R2,15,CVTCUCB-CVTMAP(R2)  GET THE UCM OR OUT      86264
         BZ    ISSMGCR       ? THIS IS AN MVS SYSTEM ?           86264
         USING UCM,R2                                            86264
         LM    R1,R3,UCMVEA  GET CONSOLE BXLE POINTERS           86264
         DROP  R2                                                86264
         USING UCMLIST,R1                                        86264
LOOPUCM  TM    UCMDISP,UCMDISPA  MASTER CONSOLE ?                86264
         BZ    BUMPUCM       NO                                  86264
         IC    R0,UCMID      GET CONSOLE ID                      86264
         B     ISSMGCR       USE IT                              86264
BUMPUCM  BXLE  R1,R2,LOOPUCM   TRY NEXT CONSOLE                  86264
         DROP  R1                                                86264
ISSMGCR  DS    0H            ITSY-BITSY LABEL                    86264
         MGCR  CARD2         ISSUE THE COMMAND                   86264
         B     LOOPS                                             86264
         SPACE 1                                                 86257
MAKEWTO  MVC   CARD2+4(65),CARD+7  COPY MESSAGE                  86257
         MVC   CARD2(4),=X'00458000'  SET LENGTH (65+4), ROUT/DESC
         MVC   CARD+2+4+65(4),=X'40008000'  ROUT=1, DESC=2 (NO DOM)
         WTO   MF=(E,CARD2)                                      86257
         B     LOOPS                                             86257
         SPACE 1                                                 86257
VARSET   LA    R1,CARD(R15)  GET EARLIEST TEXT START COLUMN      86257
         BAL   R10,SETPARSE  PROCESS SETS                        86257
         B     PUSHBAD       SCAN ERROR                          86257
         B     LOOPS         ELSE ECHO                           86257
         SPACE 1                                                 88284
VACSET   LA    R1,CARD(R15)  GET EARLIEST TEXT START COLUMN      88284
         OI    MISCFG,MSCOND  SETS ARE CONDITIONAL               88284
         BAL   R10,SETPARS9  PROCESS SETS                        88284
         B     PUSHBAD       SCAN ERROR                          88284
         B     LOOPS         ELSE ECHO                           88284
         SPACE 2                                                 86257
SETPARSE NI    MISCFG,MSCOND  SETS ARE UNCONDITIONAL             88284
SETPARS9 BAL   R9,NONBLANK   LOOK FOR TEXT                       86257
         B     4(,R10)       RETURN IF NONE                      86257
         CLI   1(R1),C'='    VARIABLE SET ?                      86257
         BNER  R10           NO; ERROR                           86257
         SLR   R4,R4                                             86257
         IC    R4,0(,R1)     GET THE VARIABLE NAME/NUMBER        86257
         SH    R4,=Y(C'A')   BASE                                86257
         BMR   R10           SYNTAX ERROR                        86257
         IC    R4,EDITSCOF(R4)  GET INDEX                        86257
         SLA   R4,6          CONVERT TO OFFSET                   86257
         BZR   R10           INVALID                             86257
         LA    R4,VARSPACE-64(R4)  POINT TO VARIABLE WORK        86257
         BXH   R1,R2,0(R10)  FAIL IF TOO LONG                    86257
         TM    MISCFG,MSCOND  CONDITIONAL ?                      88284
         BZ    SETPANEW      NO; USE REAL VARIABLE SPACE         88284
         CLI   0(R4),0       PREVIOUSLY DEFINED ?                88284
         BE    SETPANEW      NO; DEFINE IT NOW                   88284
SETPAOLD LA    R4,CARD2      USE WORK SPACE                      88284
SETPANEW MVI   0(R4),0       RESET VARIABLE LENGTH TO NULL       86257
         BXH   R1,R2,4(R10)  DONE IF END OF CARD                 86257
         LA    R5,1(,R4)     SAVE START OF OUTPUT                86257
         LA    R0,63         SET MAXIMUM OUTPUT LENGTH           86257
         SLR   R14,R14       CLEAR QUOTE FLAG                    86257
         CLI   0(R1),C'('    EXTRA SPECIAL FORM OF QUOTE ?       89092
         BNE   SETQART       NO; TEST NORMAL QUOTING             89092
         LA    R14,C')'      SET FOR REQUIRED END QUOTE          89092
         ICM   R14,8,=X'80'  SET MINUS TO INDICATE LIST          89092
         B     SETPARLP      BUT COPY                            89092
SETQART  CLI   0(R1),C''''   ORDINARY QUOTE ?                    86257
         BE    SETPARQ       YES                                 86257
         CLI   0(R1),C'"'    ALTERNATE QUOTE ?                   86257
         BNE   SETPARLP      NO; PROCESS                         86257
SETPARQ  IC    R14,0(,R1)    REMEMBER THE QUOTE                  86257
         BXH   R1,R2,0(R10)  FAIL IF NOT TERMINATED              86257
SETPARLP CLM   R14,1,0(R1)   QUOTE ?                             86257
         BNE   SETPARNQ      NO                                  86257
         LTR   R14,R14       DOING QUOTES ?                      86257
         BZ    SETPARNQ      NO                                  86257
         BM    SETPARLS      PARENTHESIZED LIST                  89092
         BXH   R1,R2,SETPARX   DONE IF NO MORE                   86257
         CLM   R14,1,0(R1)   DOUBLED QUOTE ?                     86257
         BNE   SETPARX       NO; END OF STRING                   86257
SETPARNQ LTR   R14,R14       PROCESSING QUOTES ?                 86257
         BNZ   SETPARQT      YES; NO NEED TO CHECK SEPARATORS    86257
         CLI   0(R1),C' '    EXPECTED END ?                      86257
         BE    SETPARX       YES                                 86257
         CLI   0(R1),C','    ALTERNATE END ?                     86257
         BE    SETPARX                                           86257
         B     SETPARQT      NOT END; MOVE ONE MORE              89092
SETPARLS MVI   0(R5),C')'    COPY TRAILING PARENTHESIS           89092
         AR    R5,R2         ADVANCE OUTPUT POSITION             89092
         SR    R0,R2         SET RESIDUAL LENGTH                 89092
         AR    R1,R2         ADJUST SCAN POSITION                89092
         B     SETPARX       AND QUIT THIS PARAMETER             89092
SETPARQT MVC   0(1,R5),0(R1)  MOVE ONE                           86257
         AR    R5,R2         ADVANCE OUTPUT POSITION             86257
         SR    R0,R2         SET RESIDUAL LENGTH                 86257
         BXH   R1,R2,SETPARX   QUIT IF DONE                      86257
         LTR   R0,R0         ANY MORE TO MOVE ?                  86257
         BP    SETPARLP      YES; DO SOME MORE                   86257
SETPARX  LA    R15,63        MAXIMUM LENGTH                      86257
         SR    R15,R0        LESS NUMBER PROCESSED               86257
         STC   R15,0(,R4)    STASH NEW LENGTH                    86257
         B     SETPARS9      RETURN TO SCANNER                   86257
         SPACE 1                                                 86257
NONBLANK CLI   0(R1),C' '                                        86257
         BNH   NONBLANX                                          86257
         CLI   0(R1),C','                                        86257
         BE    NONBLANX                                          86257
         CLI   0(R1),C')'                                        86257
         BE    NONBLANX                                          86257
         CLI   0(R1),C'('                                        86257
         BNE   4(,R9)        PROCESSABLE BYTE ?                  86257
NONBLANX BXLE  R1,R2,NONBLANK                                    86257
         BR    R9            NOTHING FOUND                       86257
         SPACE 1                                                 86257
PUSHMOVE LR    R0,R14        SAVE OUTPUT START                   86257
PUSHMOVL CLI   0(R1),C' '    SEPARATOR ?                         86257
         BE    PUSHMOVD      YES; QUIT                           86257
         CLI   0(R1),C','                                        86257
         BE    PUSHMOVD                                          86257
         CLI   0(R1),C'('    MEMBER START ?                      86257
         BE    PUSHMOVD                                          86257
         CLI   0(R1),C')'    MEMBER END ?                        86257
         BE    PUSHMOVD                                          86257
         CLI   0(R1),SCANCHAR  IMBEDDED SCAN CHARACTER ?         86257
         BE    PUSHMOVD      YES; STOPPER                        86257
         MVC   0(1,R14),0(R1)                                    86257
         LA    R14,1(,R14)                                       86257
         BXH   R1,R2,PUSHMOVD  QUIT IF NO MORE INPUT             86257
         BCT   R15,PUSHMOVL  CONTINUE                            86257
PUSHMOVD LR    R14,R0        RESTORE START                       86257
         CLI   0(R14),C' '   ANYTHING MOVED ?                    86257
         BER   R9            NO; ERROR                           86257
         B     4(,R9)                                            86257
         SPACE 1                                                 90088
LOOPNAME LA    R1,CARD(R15)  FIRST COLUMN TO BE SCANNED          90088
         MVC   LOOKNAME,BLANKS  CLEAR INTENDED NAME              90088
         BAL   R9,NONBLANK   GET FIRST CHARACTER OF NAME         90088
         NOP   0             ALLOW NULL MEMBER                   90088
         LA    R14,LOOKNAME                                      90088
         LA    R15,L'LOOKNAME  OUTPUT LENGTH                     90088
         BAL   R9,PUSHMOVE                                       90088
         NOP   0             ALLOW NULL MEMBER                   90088
         LR    R14,R1        GET CURRENT SCAN                    90088
         SH    R14,=H'5'     BACKSPACE                           90088
         B     LOOPSELJ      GO TO COMMON JOBNAME TEST           90088
         SPACE 1                                                 80105
LOOPJOBN LA    R2,CARD+3     ELSE PREPARE FOR FLAG CHANGE        86251
         SR    R1,R2         GET LENGTH OF JOBNAME - 1           80105
         BM    LOOPCHUS      NO NAME ?                           83277
         LA    R0,8-1        MAXIMUM VALID LENGTH(EX FORM)       83277
         CR    R1,R0         VALID LENGTH ?                      83277
         BNH   *+6           YES                                 83277
         LR    R1,R0         TRUNCATE                            83277
         MVC   LOOKNAME,BLANKS  PRE-CLEAR                        86251
         EX    R1,LPJOBMV    MOVE JOBNAME                        80105
LOOPSELJ TM    JOBFLAG,JFSING+JFREST  RESTART/SINGLE JOB SELECT ?
         BZ    PUTCH         NO; PROCESS AS PER FLAG             86251
         CLC   LOOKNAME,PICKJOB  MATCH ON SELECTED NAME ?        86251
         BNE   LOOPJSKN      NO; SEE IF END OF SINGLE JOB        80105
         SPACE 1                                                GP02309
PUTCH    DS    0H                                                86251
         AIF   ('&LOCAL' NE 'PID').NOACCT1                       92279
         LA    R14,5(,R14)   POSITION TO FIRST POSSIBLE OPERAND  86251
         LA    R15,10        SCAN MAX OF TEN COLUMNS             86251
ACTLOOP  CLI   0(R14),C'('   START OF ACCOUNT FIELD ?            86251
         BE    ACTFOUND      YES                                 86251
         LA    R14,1(,R14)                                       86251
         BCT   R0,ACTLOOP    TRY AGAIN                           86251
ACTFOUND MVC   LOOKACCT(4),1(R14)  MOVE PRIMARY ACCOUNT          86251
         MVC   LOOKACCT+4(4),1+4+1(R14)  MOVE SUB-ACCOUNT        86251
.NOACCT1 BAL   R9,HASPSCAN   LOOK FOR IT ON HASP QUEUE           86251
           B   PUTJOB        NO MATCH FOUND; SUBMIT JOB          86251
         PRTLIST DUPEJOB,DEV=2  PRINT EXPLANATION                86251
         OI    JOBFLAG,JJIGN   SKIP REST OF THIS JOB             86251
         B     LOOPJSKN      AND GET ANOTHER CARD IMAGE          86251
         SPACE 1                                                 86251
DUPEJOB  FDPRT 'JOB',NL                                          86251
         FDPRT LOOKNAME,DEB,PAD                                  86251
         FDPRT 'SKIPPED; FOUND ON QUEUE #'                       86251
         FDPRT SJ1QUE,I      QUEUE NUMBER                        90281
         FDPRT *END                                              86251
         SPACE 1                                                 86251
PUTJOB   NI    JOBFLAG,255-JFREST-JJIGN  TURN JOB SKIP FLAG OFF  80105
         OI    JOBFLAG,JFFUND  SHOW JOB FOUND                    80105
         INC   NUMJOBS       COUNT SUBMITTED JOBS                86131
         OI    CCFLAG,FJOB   SHOW JOBCARD FOUND                  86131
         SPACE 1                                                 86131
LOOPS    TM    CCFLAG,FJOB   IN A JOB ?                          86131
         BZ    LOOP          NO; JUST GET ANOTHER                86131
PUT      TM    CCFLAG,FGPUN  IS PUNCH OPEN ?                     86131
         BNZ   PUTTWO        YES                                 86131
         OI    CCFLAG,FGPUN  SHOW IT OPENED                      86131
         PRTOPEN SYSUT2,OPT=(DUMMY,ABEND)  OUTPUT (INTRDR) ?     86131
PUTTWO   PRTLIST FDCARD,DEV=1  PUNCH CARD                       GP02310
         NI    CCFLAG,255-FGDATA  RESET THE DATA SWITCH          86166
         MVC   DATAEOF,JOBEOF  REFRESH EOF CARD                 GP02310
         CLI   CARD,C'/'     JCL OR EOF CARD ?                   86166
         BNE   PATDATA       NO; SET DATA                        86166
         CLI   CARD+1,C'/'   JCL ?                               86166
         BE    PUTDATA       YES                                 86166
         CLI   CARD+1,C'*'   EOF/JES2 CONTROL ?                  86166
         BE    PUTDATA       YES                                 86166
PATDATA  OI    CCFLAG,FGDATA   SHOW EOF NEEDED                   86166
PUTDATA  PRTLIST MSGSUB,DEV=2                                    86131
         BAL   R9,NULLTEST                                      GP02309
         B     LOOP                                             GP02309
         SPACE 1                                                 86131
LOOPJSKN TM    JOBFLAG,JFSING+JFFUND  END OF SINGLE SELECT ?     80105
         BO    EOF           YES; FORCE END OF INPUT             80105
         B     LOOP          ELSE CONTINUE                       80105
LPJOBMV  MVC   LOOKNAME(0),CARD+2  MOVE JOBNAME                  86251
EDITTRT  TRT   0(0,R4),EDITSCAN  LOCATE EDIT CHARACTER           86257
         SPACE 1                                                GP02309
NULLTEST CLC   DCJCL,CARD                                        86131
         BNER  R9            NO                                 GP02309
         CLC   CARD+2(L'CARD-2),BLANKS                          GP02309
         BNER  R9            NO                                 GP02309
SETNOJOB NI    CCFLAG,255-FJOB-FGDATA                            86131
         BR    R9                                               GP02309
         SPACE 1                                                GP02309
EOF      INPCLOSE DEV==INPDEV  CLOSE FILE                        90077
         SLR   R1,R1                                             86257
         IC    R1,INPDEV     GET INPUT DEVICE FLAG               86257
         SRA   R1,1          SHIFT TO GET PRIOR FILE             86257
         BZ    EOFUT1        NONE; END OF TOP FILE (UT1/SYSIN)   86257
         STC   R1,INPDEV                                         86257
         L     R2,INPPTR     GET POINTER                         86257
         SERVCALL ALCFD,(R2)  FREE THE DD                        86257
         SH    R2,=Y(PATUT1L)  GET PRIOR WORK AREA               86257
         ST    R2,INPPTR                                         86257
         B     LOOP          RESUME PROCESSING FROM OLDER FILE   86257
         SPACE 1                                                 86257
EOFUT1   TM    CCFLAG,FGDATA   WAS LAST PUNCHED CARD DATA ?      86257
         BZ    EOFEOF        NO                                  86166
         CLC   JOBEOF(JOBEOFLN),DATAEOF  DLM=/* ?               GP02310
         BE    EOFEOF        YES; DON'T NEED TWO                GP02310
         PRTV  DATAEOF,L'DATAEOF,CC=NO,DEV=1 PUNCH AN EOF CARD   86166
EOFEOF   PRTV  JOBEOF,L'JOBEOF,DEV=(1,2),CC=NO                   86166
         ICM   R0,15,NUMJOBS  ANYTHING SUBMITTED ?               86131
         BNZ   EXIT          YES; PROCEED                        86131
         TM    JOBFLAG,JFREST+JFSING  SPECIFIC SEARCH ?          86131
         BNZ   ERREXIT       YES; JOB NAME OR DSN IN ERROR       86131
         TM    JOBFLAG,JFPISK   PICK/SKIP FOUND ?                86131
         BNZ   EXIT          MAY NOT BE AN ERROR                 86131
ERREXIT  LTCB  R1,USE=YES                                       GP10195
         L     R1,TCBJSCB    GET JSCB                           GP10195
         NUSE  JSCB,R1                                          GP10195
         L     R1,JSCBCSCB   GET CSCB                           GP10195
         USING CHAIN,R1                                         GP10195
         CLI   CHUCMP,0      CONSOLE ID PRESENT ?               GP10195
         BH    ERREXIT2      YES                                GP10195
         DROP  R1                                               GP10195
         WTO   'MSG882E NO JOBS SUBMITTED'                      GP10195
         B     ERRCOMM                                          GP10195
ERREXIT2 WTO   'MSG882E NO JOBS SUBMITTED',ROUTCDE=1,DESC=1      92306
ERRCOMM  OICC  4             MAY BE AN ERROR ?                  GP06282
         SPACE 1                                                GP02309
EXIT     SERVTERM ,          CLOSE AND FREE EVERYTHING           83006
         L     R9,RETCODE                                       GP06282
         PGMEXIT RC=(R9)     RETURN WITH COMPLETION CODE        GP02310
         SPACE 1                                                 80105
PICKSKIP XC    PSFLAGS,PSFLAGS  CLEAR RETURN FLAGS               90098
         NI    MISCFG,255-MSPSNOT  RESET NEGATION                89209
         CLC   CARD+7(63),BLANKS  MOSTLY BLANK ?                 80105
         BE    PICKNULL      YES; DON'T LOOK FOR NOTHING         80105
         OI    JOBFLAG,JFPISK  SHOW PICK/SKIP FOUND              86131
         LA    R1,CARD+7     GET START COLUMN                    80105
         LA    R2,63         COLUMNS TO SCAN                     80105
PICKLOOP CLI   0(R1),C'¬'    "NOT" SIGN ?                        89209
         BNE   PICKLOOT      NO                                  89209
         XI    MISCFG,MSPSNOT  SET NEGATION FLAG                 89209
PICKLOOT CLI   0(R1),C'*'    START OF COMMENTS ?                 90088
         BE    PICKNULL      YES; TRUNCATE                       90088
         CLI   0(R1),C'A'    VALID BYTE ?                        80105
         BL    PICKINC       NO; SKIP IT                         80105
         SLR   R14,R14       CLEAR FLAG                          89209
         CLC   MONTH1(5),0(R1)  MONTH PARAMETER ?                80105
         BNE   PICKNMON      NO                                  80105
         OI    PSFLAG2,PSFMON  SHOW MONTH PRESENT                90098
         LA    R14,PSFMMON   SET PICK FLAG                       89209
         LA    R15,5-1       SET FIELD LENGTH (-1) TO SKIP       89209
         CLC   MONTH1,0(R1)  MATCHES CURRENT ?                   80105
         BE    PICKNITS      YES; FLAG IT                        89209
         CLC   MONTHL,0(R1)  LAST WEEK OF MONTH ?                80105
         BE    PICKNITS      YES                                 89209
         B     PICKNUTS      NO MATCH ON MONTH SELECTION         89209
         SPACE 1                                                 89209
PICKNMON CLC   WEEKNO(4),0(R1)  WEEK PARAMETER ?                 80105
         BNE   PICKNWIK      NO                                  80105
         OI    PSFLAG2,PSFWEEK  SHOW PRESENT                     90098
         LA    R14,PSFMWEEK  SET MATCH FLAG                      89209
         LA    R15,5-1       SET FIELD LENGTH (-1)               89209
         CLC   WEEKNO,0(R1)  MATCHES ?                           80105
         BNE   PICKNUTS      NO                                  89209
         B     PICKNITS      YES; SHOW MATCH                     89209
         SPACE 1                                                 89209
PICKNWIK CLC   0(3,R1),MONDH1  'DAY' ?                           84062
         BNE   PICKNDAY      NO                                  84062
         LA    R15,2         SET LENGTH TO SKIP                  84093
         LA    R14,PSFMMDA*256   SET FOR DAY IN MONTH            90098
         CLI   3(R1),C'L'    LAST DAY OF MONTH REQUESTED ?       84062
         BE    PICKDAYL      YES                                 84062
         CLI   3(R1),C'0'    NUMERIC ?                           84062
         BL    PICKWICK      NO; IGNORE                          84062
         LA    R15,3         SET FOR LENGTH OF FOUR              84093
         OI    PSFLAG1,PSFMDA  SHOW DAY FOUND                    90098
         CLI   4(R1),C'0'    TWO DIGITS ?                        84062
         BNL   PICKDAY2      YES                                 84062
         CLI   MONDH1+3,C'0'  LEADING ZERO IN CURRENT DAY ?      84062
         BNE   PICKNUTS      NO; NO MATCH                        89209
         CLC   MONDH1+4(1),3(R1)  MATCHING REQUEST ?             84062
         BE    PICKNITS      YES; SET MATCH                      89209
         B     PICKNUTS      SKIP                                89209
PICKDAY2 LA    R15,4         SET PARAMETER LENGTH FOR 5          84093
         CLC   3(2,R1),MONDH1+3  MATCHING DAY ?                  84062
         BNE   PICKNUTS      NO                                  89209
         B     PICKNITS      YES; SET MATCH                      89209
PICKDAYL LA    R15,3         SET FOR LENGTH OF FOUR              84093
         OI    PSFLAG1,PSFMDA  SHOW DAY FOUND                    90098
         CLC   MONDH1,MONDHL  LAST DAY OF MONTH ?                84062
         BNE   PICKNUTS      NO                                  89209
         B     PICKNITS      YES                                 89209
         SPACE 1                                                 89209
PICKNDAY LA    R14,SATURDAY  GET TABLE OF DAYS                   84062
         LA    R0,7          NUMBER OF TESTS                     80105
         CLI   3(R1),C'A'    POSSIBLY A SHORT DAY ENTRY ?        80105
         BL    PICK3D        YES; CHECK THREE-LETTER DAY ABBREVIATION
         SLR   R15,R15       CLEAR FOR IC                        80105
PICK7DAY IC    R15,0(,R14)   GET LENGTH OF NAME                  80105
         EX    R15,PICKDCLC  COMPARE IT                          80105
         BE    PICKDATE      MATCH                               89209
         LA    R14,SUNDAY-SATURDAY(,R14)                         80105
         BCT   R0,PICK7DAY   TRY AGAIN                           80105
         B     PICKINC       UP POINTERS                         80105
PICKDATE OI    PSFLAG2,PSFDAY  SHOW DAY REQUESTED                90098
         CLC   WEEKDAY(3),0(R14)  CURRENT DAY ?                  89209
         LA    R14,PSFMDAY   SET MATCH MASK                      89209
         BNE   PICKNUTS      NO                                  84093
         B     PICKNITS      SKIP FIELD; R15 STILL SET           89209
PICKDCLC CLC   1(0,R14),0(R1)  MATCHING DAY ?                    80105
         SPACE 1                                                 89209
PICK3D   LA    R15,3-1       SET SKIP LENGTH                     89209
PICK3DAY CLC   0(3,R1),1(R14)  MATCHES ?                         80105
         BE    PICKDATE      YES                                 89209
         LA    R14,SUNDAY-SATURDAY(,R14)                         80105
         BCT   R0,PICK3DAY                                       80105
         SPACE 1                                                 89209
         CLC   =C'CPU=',0(R1)   CPU SELECTION ?                  83277
         BNE   PICKINC       NO                                  83277
         OI    PSFLAG2,PSFCPU   INDICATE CPU SELECTION FOUND     90098
         LA    R14,PSFMCPU   SET MATCH BIT                       89209
         SLR   R15,R15       SET SKIP LENGTH                     89209
         LA    R1,4(,R1)                                         83277
         SH    R2,=H'4'      GET RESIDUAL LENGTH                 83277
         BNP   PICKNUTS      INVALID ?                           89209
         CLI   0(R1),C' '    NULL PARAMETER ?                    83277
         BE    PICKNITS      YES; SET MATCH                      89209
         CLI   0(R1),C','    ALTERNATE NULL ?                    83277
         BE    PICKNITS                                          89209
         CLI   0(R1),C'('    LIST FORMAT ?                       83277
         BNE   *+8           NO                                  83277
         OI    CCFLAG,JFPARA   INDICATE PARENTHESIS FOUND        83277
PICKLCPU CLC   MYCPU,0(R1)   MATCHING CPU ID ?                   83277
         BE    PICKYCPU      YES; SET MATCH                      89209
         TM    CCFLAG,JFPARA LIST ?                              83277
         BZ    PICKNUTS      NO; GET OUT                         89209
PICKUCPU LA    R1,1(,R1)     TRY FOR MORE                        83277
         SH    R2,=H'1'                                          83277
         BNP   PICKXCPU      QUIT                                83277
         CLI   0(R1),C')'    END OF LIST ?                       83277
         BNE   PICKLCPU                                          83277
PICKXCPU NI    CCFLAG,255-JFPARA                                 83277
         B     PICKNUTS      NO MATCH                            89209
PICKYCPU LA    R15,4-1       SET SKIP AMOUNT                     89209
         B     PICKNITS      SET MATCH                           89209
         SPACE 1                                                 89209
*        NO MATCH ON PARAMETER                                   89209
PICKNUTS TM    MISCFG,MSPSNOT  NEGATION ?                        89209
         BZ    PICKWICK      NO; NO ACTION                       89209
         B     PICKPICK      ELSE SELECT                         89209
         SPACE 1                                                 89209
*        MATCH ON PARAMETER                                      89209
PICKNITS TM    MISCFG,MSPSNOT  NEGATION ?                        89209
         BNZ   PICKWICK      YES; NO ACTION                      89209
PICKPICK STH   R14,DB        SET FLAG                            90098
         OC    PSFLAGS,DB    AND RETURN IT                       90098
PICKWICK NI    MISCFG,255-MSPSNOT  RESET NEGATION                89209
         AR    R1,R15        SKIP A FIELD                        89209
         SR    R2,R15        AND ADJUST LENGTH                   80105
PICKINC  LA    R1,1(,R1)     UP TO NEXT COLUMN                   80105
         SH    R2,=H'1'                                          80105
         BP    PICKLOOP                                          80105
PICKNULL OC    PSFLAGS,PSFLAGS  ANYTHING FOUND ?                 90098
         BNER  R9            YES; RETURN                         80105
         MVI   PSFLAG2,PSFDAY+PSFMDAY  FORCE A MATCH             90098
         BR    R9            RETURN                              80105
         SPACE 2                                                 90088
*        LOCATE A JOB ON THE HASP QUEUE                          86251
*                                                                86251
HASPSCAN TM    JOBFLAG,JFSING  PICK SINGLE JOB ?                 86251
         BNZR  R9            YES; SET NOT FOUND                  86251
         TM    CCFLAG,FGQINP+FGQANY  QUEUE SEARCH REQUIRED ?     86251
         BZR   R9            NO; RETURN NOT FOUND                86251
         TM    MISCFG,MSNOJES  INTERFACE FAILURE ?               89309
         BNZR  R9            YES; PASS THIS JOB                  89309
         XC    COPYJES(LOOKNAME-COPYJES),COPYJES                 90281
         MVC   SJ1JNAM,LOOKNAME  SET REQUESTED JOB NAME          90281
         AIF   ('&LOCAL' NE 'PID').NOACT2                        92279
         MVC   SJ1ACCT,LOOKACCT  REQUEST SCAN BY ACCOUNT         90281
.NOACT2  TM    CCFLAG,FGQANY  ANYTHING INCL. INPUT QUEUE ?       92078
         BNZ   *+8           YES; SEARCH ALL                     92078
         MVI   SJ1QNAM,C'I'  REQUEST INPUT ONLY                  90281
         SERVCALL LOJOB,COPYJES   SEARCH FOR JOB                 90281
         BXLE  R15,R15,4(R9) JOB FOUND                           90281
         BR    R9            RETURN NOT FOUND                    90281
         SPACE 2                                                 86251
         LTORG ,                                                 86251
         EJECT ,                                                GP02309
PATUT1   INPWORK SYSUT1,SYSIN,FILL=C' ',WIDTH=80,EODAD=EOF       86257
PATUT1L  EQU   *-PATUT1                                          86257
SYSUT2   PUNWORK SYSUT2,INTRDR  INTERNAL READER                  83006
SYSPRINT PRTWORK SYSPRINT,SYSTERM,TITLE=5             DEV=2      83277
         SPACE 1                                                 83277
HEADER   FDOPT  NL,CC=C'#'   SET AUTOMATIC DATE/TIME/PAGE NUMBER 83277
         FDPRT 'H O B  ***  JOB SUBMISSION PROGRAM'              83277
         FDOPT   SBA=(*,60)                                      83277
         FDPRT 'CPU='                                            83277
         FDPRT MYCPU                                             83277
         FDPRT *END                                              83277
         SPACE 1                                                 83277
ECHOCARD FDCLC DCSKIP,CARD,BE=ECHODARK                           89209
         FDCLC DCPICK,CARD,BE=ECHODARK                           89209
         FDPRT ' ',NL,LEN=20                                     83277
FDCARD   FDPRT CARD                                              83277
         FDPRT *END                                              83277
ECHODARK FDPRT ' ',NL,LEN=19 (ROOM FOR FAKE SF)                  89209
         FD    CARD,INTENSE                                      89209
         FDPRT *END                                              89209
         SPACE 1                                                 90101
MSGFLGS  FDOPT SBA=(*,110)                                       90101
         FDPRT PSFLAGS,HEX                                       90101
         FDPRT *END                                              90101
         SPACE 1                                                 86257
EDITCARD FDPRT ' ',NL,LEN=20                                     86257
         FDPRT CARD                                              86257
         FDPRT '<==== EDITED'                                    86257
         FDPRT *END                                              86257
         SPACE 1                                                 86131
MSGSUB   FDOPT SBA=(*,120)                                       86257
         FDPRT 'SUBMITTED'                                       86131
         FDPRT *END                                              86131
         SPACE 1                                                 89212
CURRINFO FDPRT 'DATE VALUES: MONTH=',NL                          89212
         FDPRT MONTH1,PADL                                       89212
         FDPRT '|'                                               89212
         FDPRT MONTHL,PADR                                       89212
         FDPRT '  DAY='                                          89212
         FDPRT MONDH1,PADL                                       89212
         FDPRT '|'                                               89212
         FDPRT MONDHL,PADR                                       89212
         FDPRT '  WEEK='                                         89212
         FDPRT WEEKNO,PAD                                        89212
         FDPRT '  WEEKDAY='                                      89212
         FDPRT WEEKDAY+1,9,PAD                                   89212
         FDPRT '  DATE='                                         89212
         FDPRT JULDATE,D,RADJ,LEN=7                             GP02323
         FDPRT *END                                              89212
         SPACE 1                                                 80105
SATURDAY DC    X'07',CL9'SATURDAY'                               80105
SUNDAY   DC    X'05',CL9'SUNDAY'                                 80105
         DC    X'05',CL9'MONDAY'                                 80105
         DC    X'06',CL9'TUESDAY'                                80105
         DC    X'08',CL9'WEDNESDAY'                              80105
         DC    X'07',CL9'THURSDAY'                               80105
         DC    X'05',CL9'FRIDAY'                                 80105
         SPACE 1                                                GP02323
PARMP2W  MAPSBP2W ,          SUBP2W RETURN AREA                 GP02323
PARMP2WL EQU   *-PARMP2W     LENGTH OF SUBP2W RETURN AREA       GP02323
         SPACE 1                                                GP02309
DCSKIP   DC    C'//*SKIP'                                        89209
DCPICK   DC    C'//*PICK'                                        89209
DCJCL    DC    0C'//'        JCL DEFINITION           1 OF 3     86131
DCJCLCOM DC    0C'//*',C'/'  JCL COMMENTS CARD        2 OF 3     86131
DCJES    DC    C'/*'         JES CONTROL CARD         3 OF 3     86131
JOBEOF   VCON  '/*EOF'                                          GP02310
JOBEOFLN EQU   *-JOBEOF                                         GP02310
BLANKS   DC    CL80' '                                          GP02309
         SPACE 1                                                 86257
EDITSCAN DC    256X'00'      NO HITS, NO MISSES                  86257
         ORG   EDITSCAN+SCANCHAR                                 86257
         DC    X'04'         EXCEPT SINGLE SCAN CHARACTER        86257
         ORG   ,                                                 86257
EDITSCOF DC    64X'00'       OFFSET TO VARIABLE POINTERS         86257
         ORG   EDITSCOF+C'A'-C'A'                                86257
EDITSCOA DC    9AL1(*-EDITSCOA+1)   A-I                          86257
         ORG   EDITSCOF+C'J'-C'A'                                86257
EDITSCOJ DC    9AL1(*-EDITSCOJ+10)  J-R                          86257
         ORG   EDITSCOF+C'S'-C'A'                                86257
EDITSCOS DC    8AL1(*-EDITSCOS+19)  S-Z                          86257
         ORG   EDITSCOF+C'0'-C'A'                                86257
EDITSCON DC    10AL1(*-EDITSCON+27) 0-9                          86257
         ORG   ,                                                 86257
         SPACE 1                                                 83006
SAVE     DSECT ,                                                 86251
DB       DC    D'0'                                             GP02309
LOOKDB   DC    D'0'          ANOTHER WORK AREA                   90281
         SERVDEFS ,          SERVICE ROUTINE VARIABLES          GP06282
PICKJOB  DC    CL8' '        NAME OF SELECTED JOB                80105
JNAME    DC    CL8' '        CURRENT JCL NAME FIELD              86131
SYSUT1   DS    8XL(PATUT1L)'0'  ROOM FOR 8 FILES                 86257
INPPARM  DS    5A            DD ALLOCATION PARM LIST             86257
         ORG   INPPARM                                           86257
INPPTR   DS    F             ADDRESS OF CURRENT SYSUT1/SYSIN     86257
         ORG   ,                                                 86257
INPDSN   DS    CL44          INPUT DSN                           86257
INPMEM   DS    CL8           INPUT MEMBER                        86257
INPVOL   DS    CL6           INPUT VOLUME                        86257
INPDEV   DS    X             BIT MASK FOR CURRENT INPUT FILE     86257
         SPACE 1                                                GP02309
CCFLAG   DC    X'00'                                            GP02309
JFPARA   EQU   X'80'           PARENTHESIS FOUND                 86131
FGPUN    EQU   X'40'           PUNCH DD OPENED                   86131
FGCLUST  EQU   X'20'           CURRENTLY IN //* GROUP            86251
FGLPICK  EQU   X'10'           LAST PASSED TEST WAS PICK         86251
FGQANY   EQU   X'08'           SKIP IF ON ANY QUEUE              86251
FGQINP   EQU   X'04'           SKIP IF ON ANY INPUT/EXEC QUEUE   86251
FJOB     EQU   X'02'           JOB CARD FOUND                   GP02309
FGDATA   EQU   X'01'           LAST PUNCHED CARD WAS DATA        86166
         SPACE 1                                                 80105
JOBFLAG  DC    X'00'         SKIP/RESTART/PICK PROCESSING FLAGS  80105
JFREST   EQU   X'80'         RESTART=JOBNAME SPECIFIED           80105
JFSING   EQU   X'40'         PICK=JOBNAME CHOSEN                 80105
JFIGN    EQU   X'20'         SKIP FORCED BY //*SKIP CARD         80105
JJIGN    EQU   X'10'         SKIP FORCED BY R= OR P=             80105
JFFUND   EQU   X'08'         R=/P= JOB FOUND                     80105
JFASIS   EQU   X'04'           SUPPRESS EDITING                  86257
JFPISK   EQU   X'01'           PICK/SKIP FOUND                   86131
         SPACE 1                                                 88284
MISCFG   DC    X'00'         MISCELLANEOUS FLAGS                 88284
MSCOND   EQU   X'80'           SET IS CONDITIONAL                88284
MSNOJES  EQU   X'10'           SCHEDULE FAILURE IF ON            89309
MSAUTH   EQU   X'04'           WE'RE RUNNING AUTHORIZED         GP02309
MSPSNOT  EQU   X'01'           PICK/SKIP 'NOT' OPERAND           89209
         SPACE 1                                                 80105
PSFLAGS  DC    0XL2'0000'    SKIP/PICK SCAN FLAGS                90098
PSFLAG1  DC    X'00'         FIRST (NEWER) SET                   90098
PSFMDA   EQU   X'02'           DAY OF MONTH                      90098
PSFMMDA  EQU   X'01'           DAY OF MONTH MATCHED              90098
PSFLAG2  DC    X'00'         SECOND (OLDER) FLAGS                90098
PSFMON   EQU   X'80'         MONTH PARAMETER ON CARD             80105
PSFMMON  EQU   X'40'         CURRENT MONTH'S WEEK MATCHED        80105
PSFDAY   EQU   X'20'         DAY PARAMETER APPEARED ON CARD      80105
PSFMDAY  EQU   X'10'         DAY MATCHED CURRENT                 80105
PSFWEEK  EQU   X'08'         WEEK OF YEAR PRESENT                80105
PSFMWEEK EQU   X'04'         WEEK OF YEAR MATCHES                80105
PSFCPU   EQU   X'02'         CPU SELECTION PRESENT               83277
PSFMCPU  EQU   X'01'         CPU SELECTION MATCHES               83277
PSFMATCH EQU   (PSFMMDA)*256+PSFMMON+PSFMDAY+PSFMWEEK+PSFMCPU    90101
NUMJOBS  DS    F             NUMBER OF JOBS SUBMITTED            83277
MYCPU    DS    CL4           SMCASID OF RUNNING SYSTEM           83277
CARD     DC    CL80' '       MUST PRECEDE CARD2                 GP02309
CARD2    DC    CL80' '       SECOND HALF OF PARM                 86131
OLDCARD  DC    CL80' '       TEXT PRIOR TO EDITING               86257
RELJOBN  DC    CL4' '        NUMBER FOR HASP JOB                 86251
DATAEOF  DS    CL(JOBEOFLN)  JES2 EOF FOR DLM= OPTION           GP02310
         SPACE 1                                                 86257
VARSPACE DC    36XL64'0'    TEXT AREA                            86257
         SPACE 1                                                 90281
         PRINT &PRTMAC                                           90281
COPYJES  SERVJES OPT=LOJOB,DSECT=NO  JOB Q SERVICES INTERFACE    90281
         PRINT &PRTSYS                                           90281
LOOKNAME DC    CL8' '        NAME OF REQUESTED JOB               86251
LOOKACCT DC    CL8' '        LOCAL ACCOUNT FIELD                 90281
SAVEEND  EQU   *                                                 86251
         SPACE 1                                                 79046
         PRINT &PRTSYS                                           83006
CVTDSECT DSECT ,                                                 83006
         CVT   DSECT=YES                                         83006
         IHACDE ,                                                83006
         IEESMCA ,                                               83277
         IEECUCM FORMAT=NOTOLD                                   86264
         IHAPSA ,                                                86251
         IKJTCB ,                                                86251
         IEFTIOT1 ,                                              86251
         IHAASCB ,                                               86251
         IHAASXB ,                                              GP02310
         IEZJSCB ,                                              GP02309
         IEFJESCT ,                                             GP02309
         IEFJSSIB ,                                             GP02309
         IHAACEE ,                                              GP02309
         IEECHAIN ,          CSCB                               GP10195
         SPACE 1                                                 86251
         END   ,                                                GP02309
