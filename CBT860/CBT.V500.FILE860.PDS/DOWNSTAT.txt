DSTAT    TITLE 'D O W N S T A T  ***  RECORD SYSTEM STATISTICS @ Z EOD'
         COPY  OPTIONGB                                          90080
         SPACE 1                                                 90080
         SYSPARM LIST=YES                                        90080
         SPACE 2                                                 90080
***********************************************************************
*                                                                     *
*        COPYRIGHT 1981, 1990  EXPERT SYSTEM PROGRAMMING, INC.        *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,                                                 90080
***********************************************************************
*                                                                     *
*        THIS MODULE IS A MODIFICATION OF THE EXHIBIT UCV COMMAND     *
*          IT IS NOT INTENDED TO DO ANYTHING USEFUL ELSEWHERE         *
*                                                                     *
***********************************************************************
         SPACE 1                                                 90080
         PRINT &PRTSOR                                           90080
DOWNSTAT SAVEM ZERO12,BASE=R12,PARM=R9,BNDRY=PAGE                90080
         SERVINIT ,          LOAD AND INITIALIZE @SERVICE        90080
         SERVCALL LPALD,=CL8'@PRINTER'  GET THE PRINT ROUTINE    90080
         ST    R0,@PRINTER   SAVE ADDRESS                        90080
         PRTOPEN SYSPRINT,OPT=(ABEND)  BOMB IF NO OUTPUT FILE    90080
         PRTF  '#S Y S T E M   S H U T D O W N   S T A T I S T I C S', *
               TITLE=1       MAKE PAGE HEADER                    90080
         L     R8,CVTPTR     GET THE CVT                         90080
         USING CVTMAP,R8                                         90080
         LAT   R7,CVTUSER,NOUCVT   GET OUT IF NOT AVAILABLE      90080
         USING USERCVT,R7
         ICM   R5,15,CVTASMVT   GET ASM VECTOR TABLE             83259
         USING ASMVT,R5                                          83259
         L     R4,CVTEXT2    GET THE SECONDARY CVT EXTENSION     90080
         USING CVTXTNT2,R4   DECLARE IT                          90080
         L     R3,CVTSMCA                                        90080
         USING SMCABASE,R3                                       90080
         PRTLIST IPLFD       PRINT IPL INFORMATION               90080
         B     DONEIPL                                           90080
IPLFD    FDPRT 'This machine was last loaded on',NL              90080
         FDPRT UCIPLDAT,WDAY,PADL,DEB                            90080
         FDPRT ','                                               90080
         FDPRT UCIPLDAT,MD,PAD                                   90080
         FDPRT 'at',PAD                                          90080
         FDPRT UCIPLTIM,TIME                                     90080
         FDPRT ' ',NL,LEN=10                                     90080
         FDPRT 'from IEANUC0'                                    90080
         FDPRT CVTNUCLS      SHOW NAME                           90080
         FDCLC CVTASMVT,ZEROES,4,BE=IPLFDEND                     90080
         FDPRT ' ',NL,LEN=10                                     90080
         FDPRT 'LPA/VIO option was'                              90080
         FDTM  ASMFLAG2,ASMQUICK,BZ=IPLFDCLP   QUICK IPL ?       90080
         FDTM  ASMFLAG2,ASMWARM,BZ=IPLFDVIO  CVIO ?              90080
         FDPRT 'WARM start',PAD                                  90080
         FDGOTO IPLFDEND                                         90080
IPLFDVIO FDPRT 'CVIO',PAD                                        90080
         FDGOTO IPLFDEND                                         90080
IPLFDCLP FDPRT 'CLPA',PAD                                        90080
IPLFDEND FDCLC CVTSMCA,ZEROES,4,BE=IPLFDOUT                      90080
         FDPRT SMCASID,NL                                        90080
         FDPRT 'is the billing id',PADL                          90080
IPLFDOUT FD    *END                                              90080
         SPACE 1                                                 90080
         SPACE 2                                                 90080
DONEIPL  PRTSPACE 5          MAKE A GAP                          90080
         DROP  R5,R4,R3                                          90080
         ICM   R5,15,UCA$GDA    GET THE CURRENT ACCOUNTING AREA  90080
         BZ    NONEGDA       NONE ?                              90080
         USING A$GDA,R5      DECLARE IT                          90080
         PRTLIST REVFD       PRINT REVENUE FIGURES               90080
GOODEXIT LH    R9,RETCH      LOAD RETURN CODE                    90080
         SERVTERM ,          CLOSE AND FREE                      90080
         ENDM  RC=(R9)       RETURN WITH CODE                    90080
         SPACE 1                                                 90080
NONEGDA  OI    RETCH+L'RETCH-1,4  NO ACCOUNTING                  90080
         FDPRT '0Accounting is disabled *****'                   90080
         B     GOODEXIT                                          90080
         SPACE 1                                                 90080
NOUCVT   OI    RETCH+L'RETCH-1,8                                 90080
         FDPRT '0***** User CVT extension not found *****'       90080
         B     GOODEXIT                                          90080
         SPACE 1                                                 90080
REVFD    FDPRT 'Revenue since IPL :',NL                          90080
         FDPRT ' ',NL                                            90080
         FDPRT 'Type',NL,RADJ,LEN=10                             90080
         FDPRT 'Customers',RADJ,PAD,LEN=13                       90080
         FDPRT 'In-house',RADJ,PAD,LEN=13                        90080
         FDPRT 'Overhead',RADJ,PAD,LEN=13                        90080
         FDPRT ' ',NL                                            90080
         FDPRT 'Batch',NL,RADJ,LEN=10                            90080
         FDPRT A$GBPAY$,$I,RADJ,PAD,LEN=13                       90080
         FDPRT A$GBINH$,$I,RADJ,PAD,LEN=13                       90080
         FDPRT A$GBOVH$,$I,RADJ,PAD,LEN=13                       90080
         FDPRT 'Time Share',NL,RADJ,LEN=10                       90080
         FDPRT A$GTPAY$,$I,RADJ,PAD,LEN=13                       90080
         FDPRT A$GTINH$,$I,RADJ,PAD,LEN=13                       90080
         FDPRT A$GTOVH$,$I,RADJ,PAD,LEN=13                       90080
         FDPRT 'Wylbur',NL,RADJ,LEN=10                           90080
         FDPRT A$GWPAY$,$I,RADJ,PAD,LEN=13                       90080
         FDPRT A$GWINH$,$I,RADJ,PAD,LEN=13                       90080
         FDPRT A$GWOVH$,$I,RADJ,PAD,LEN=13                       90080
         FDPRT ' ',NL                                            90080
         FDPRT 'Printing',NL,RADJ,LEN=10                         90080
         FDPRT A$GPRT$,$I,RADJ,PAD,LEN=13                        90080
         FDPRT 'unadjusted for priority discounts'               90080
         FDPRT *END                                              90080
         EJECT ,
SYSPRINT PRTWORK SYSPRINT,TITLE=5                                90080
DCGDA    DC    C'GDA member'                                     83259
ACCTVOL  DC    CL6' '                                            83259
DCGAT    DC    C'at'                                             83259
DCGLOAD  DC    C'loaded on'                                      83259
DCON     EQU   DCGLOAD+L'DCGLOAD-2,2,C'C'                        83259
DCDUNUS  DC    C'*** not initialized ***'                        83259
DCDOBAD  DC    C'*** OBTAIN failed, code'                        83259
DCNOGDA  DC    C'********** GDA not found **********'            83259
DCERRGDA DC    C'********** Error during GDA processing **********'
DCBADGDA DC    C'********** Accounting disabled **********'      83259
DCGLDA   DC    C'  LDA =>'                                       83259
         SPACE 1
SAVE     DSECT ,             SAVE AND WORK AREA                  90080
DB       DS    D             WORK SPACE                          90080
DB2      DS    D             DITTO                               90080
@SERVICE DS    A             @SERVICE ROUTINE                    90080
@PRINTER DS    A             @PRINTER ROUTINE                    90080
WORK16   DC    0D'0',XL1'0'                                      83259
WORK15   DC    0XL15'0',XL1'0'                                   83259
WORK14   DC    0XL14'0',XL2'0'
WORK12   DC    0XL12'0',XL2'0'
WORK10   DC    0XL10'0',XL2'0'
WORK8    DC    XL8'0'
ZEROES   DS    F             WORD OF ZEROES                      90080
RETCH    DS    H             RETURN CODE                         90080
ACCTDSN  DC    CL44' '                                           83259
WORKDSCB DS    33D           LOTS OF SPACE                       83259
SAVEND   EQU   *                                                 90080
         SPACE 2                                                 83259
         PRINT &PRTMAC                                           90080
USERCVT  USERCVT ,
         SPACE 3
         PRINT &PRTSYS
CVTDSECT DSECT ,                                                 90080
         CVT   DSECT=YES                                         90080
         ILRASMVT ,          ASM VECTOR TABLE                    83259
         IEESMCA ,                                               90080
         SPACE 1                                                 83259
         PRINT &PRTMAC                                           83259
         A$GDA ,             LOCAL GLOBAL ACCOUNT AREA           83259
         END   ,                                                 83259
