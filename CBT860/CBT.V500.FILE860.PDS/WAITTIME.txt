WAIT     TITLE 'W A I T T I M E  ***  WAIT TIME EXTENSION FOR TSO'
         PUNCH '  ALIAS SET '
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
*                                                                     *
*        COPYRIGHT 1986  EXPERT SYSTEM PROGRAMMING, INC.              *
*        COPYRIGHT 2003  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         EJECT ,
***********************************************************************
*                                                                     *
*        THIS MODULE, IN CONJUNCTION WITH THE LOCAL IEFUTL MODULE,    *
*        PERMITS A TSO USER TO INHIBIT SYSTEM 522 (30 MINUTE          *
*        INACTIVITY) ABENDS.                                          *
*                                                                     *
*        TSO CMD:  SET NOTIMEOUT | SET NOT    INHIBIT S522            *
*                  SET TIMEOUT   | SET TIM    ENABLE S522             *
*                  SET                        DISPLAY CURRENT STATUS  *
*                  SET HELP                   LIST VALID FORMS        *
*                                                                     *
***********************************************************************
         SPACE 1
         PRINT &PRTSOR
WAITTIME SAVEM ZERO12,BASE=R12,PARM=R10                 ADDED ON 86216
*        PUNCH ' ALIAS SET'
         SPACE 1
         SERVINIT ,          LOAD THE SERVICE ROUTINE
         SERVCALL LPALD,=CL8'@PRINTER'  LOAD THE PRINTER ROUTINE
         ST    R0,@PRINTER
         PRTOPEN CONSOLE     ENABLE TPUT OR WTO TO USER
         L     R7,PSATOLD-PSA  GET THE TCB
         USING TCB,R7
         ICM   R7,15,TCBTCT  GET THE SMF BLOCK
         BZ    NOTCT         NO TCT ?
         USING SMFTCT,R7
         ICM   R6,15,TCTJMR  GET THE JMR
         BZ    NOJMR         NO JMR ?
         USING JMR,R6
         SPACE 1
***********************************************************************
*        GET TASK OR SYSTEM WAIT TIME LIMIT FOR LATER MESSAGE
*
         SLR   R0,R0
         AIF   (&MVSSP).NOTCTW
         ICM   R1,15,TCTWLMT GET WAIT TIME LIMIT
         BNZ   OLDTIME
.NOTCTW  LA    R1,60*30      LOAD IBM 30 MINUTE DEFAULT
         L     R2,CVTPTR     GET THE CVT
         ICM   R2,15,CVTSMCA-CVTMAP(R2) GET THE SMCA
         BZ    COMTIME
         USING SMCABASE,R2
         L     R1,SMCAJWT     GET WAIT TIME
         AIF   (NOT &MVS).SKPMVS4
         SLDL  R0,10         SCALE IT
         D     R0,=F'976'    CONVERT TO SECONDS
         B     COMTIME
.SKPMVS4 DROP  R2
OLDTIME  D     R0,=F'38400'  SECONDS
COMTIME  SLR   R0,R0
         D     R0,=F'60'     CONVERT TO MINUTES
         ST    R1,WTTIME     SAVE THE WAIT TIME FOR FORMATTING
         SPACE 1
***********************************************************************
*        ANALYZE INPUT PARM - IF NONE, PRINT STATUS MESSAGE
*
         LA    R10,0(,R10)   CLEAR PARM POINTER POINTER
         LTR   R10,R10       ANY SUPPLIED ?
         BZ    NOPARM        NO
         ICM   R9,15,0(R10)  LOAD PARM POINTER
         BZ    NOPARM        NONE
         SLR   R8,R8
         ICM   R8,3,0(R9)    LOAD PARM LENGTH
         BNP   NOPARM        ALSO NONE ?
         TM    0(R10),X'80'  SINGLE PARAMETER POINTER ?
         BNZ   DELBLANK      YES; PROCESS NORMALLY
         SLR   R2,R2         ELSE SEE IF BUFFER
         ICM   R2,3,2(R9)    SEE IF COMMAND HALFWORD
         BZ    SKIPCMND      YES
         CR    R2,R8         NOT LONGER THAN TEXT ?
         BH    DELBLANK      LONGER - ASSUME IT IS TEXT
SKIPCMND LA    R2,2(,R2)     ALLOW FOR LENGTH OF COMMAND LENGTH
         AR    R9,R2         SKIP OVER COMMAND
         SR    R8,R2         GET RESIDUAL LENGTH
         BNP   NOPARM        NOTHING LEFT TO DO
DELBLANK CLI   2(R9),C' '    PRINTABLE CHARACTER ?
         BH    NONBLANK
         LA    R9,1(,R9)
         BCT   R8,DELBLANK
NOPARM   PRTLIST SHOWSTAT    SHOW USER CURRENT SETTINGS
         B     EXIT
BADPARM  PRTLIST HELPSTAT    SHOW USER CORRECT COMMAND
         B     NOPARM        AND SETTING
         SPACE 1
NONBLANK LA    R15,0(R8,R9)  POINT TO LAST BYTE+1
TRLBLANK BCTR  R15,0         LAST BYTE
         CLI   2(R15),C' '   TRAILING BLANK OR NULL ?
         BH    DONBLANK      NO; START PROCESSING
         BCT   R8,TRLBLANK   YES; ELIMINATE IT
         B     NOPARM        ELSE NULL PARM
         SPACE 1
DONBLANK BCTR  R8,0          SET LENGTH FOR EXECUTE
         CH    R8,=Y(L'TEXT)  IS INPUT TOO LONG ?
         BNL   BADPARM       YES; FAIL IT
         MVI   TEXT,C' '
         MVC   TEXT+1(L'TEXT-1),TEXT
         EX    R8,TEXTOC
         LA    R4,VERBTAB    POINT TO TABLE OF VERBS
VERBLOOK SLR   R3,R3
         ICM   R3,1,0(R4)    GET LENGTH
         BM    BADPARM       END OF TABLE - NO MATCH
         CR    R8,R3         TEST LENGTH
         BH    VERBBUMP      TOO LONG - SKIP TEST
         EX    R8,VERBCLC    COMPARE SHORTER LENGTH
         BE    VERBMATS      MATCH - PROCESS IT
VERBBUMP LA    R4,1+3+1(R4,R3)  SPACE TO NEXT VERB
         B     VERBLOOK      AND TRY AGAIN
VERBCLC  CLC   TEXT(0),4(R4)   COMPARE VERBS
TEXTOC   OC    TEXT(0),2(R9)  MOVE PARM AND UPPER CASE IT
         SPACE 1
VERBMATS SLR   R15,R15
         ICM   R15,7,1(R4)   GET OFFSET
         B     0(R15,R12)    BRANCH TO ROUTINE
         SPACE 2
SETNO522 LA    R2,OIFLAG     SET FLAGS ON
         LA    R3,MSGON      MAKE MESSAGE
         B     SETCOM
SET522   LA    R2,NIFLAG     SET FLAGS OFF
         LA    R3,MSGOFF     MAKE MESSAGE
SETCOM   TESTAUTH FCTN=1     TEST FOR AUTHORIZATION
         BXLE  R15,R15,PRIVY  AUTHORIZED ALREADY
         SERVICE APFON       AUTHORIZE US
         MODESET KEY=ZERO
         EX    0,0(,R2)      EXECUTE FLAG CHANGE REQUEST
         MODESET KEY=NZERO
         SERVICE APFOF       RESET
         B     ENDMSG
PRIVY    MODESET KEY=ZERO
         EX    0,0(,R2)      EXECUTE FLAG CHANGE REQUEST
         MODESET KEY=NZERO
ENDMSG   PRTLIST (R3)        PRINT THE MESSAGE
EXIT     SERVTERM ,          CLOSE AND FREE EVERYTHING
         ENDM  RC=0          RETURN WITH CC=0
         SPACE 1
NOTCT    DS    0H
NOJMR    PRTLIST MSGNOTCT
         SERVTERM ,          CLOSE AND FREE
         ABEND 666,DUMP      AND QUIT
         SPACE 2
DCF1     DC    F'1'
OIFLAG   OI    JMRUFLG,$JMRWAIT+$JMRWMAX  UNLIMITED 522 EXTENSIONS
NIFLAG   NI    JMRUFLG,255-$JMRWAIT-$JMRWMAX  LIMITED
         SPACE 1
VERBTAB  BTAB  'NOTIMEOUT',SETNO522,BASE=WAITTIME
         BTAB  'TIMEOUT',SET522
         BTAB  'LOGOFF',SET522
         BTAB  'NOLOGOFF',SETNO522
         BTAB  *END
         SPACE 1
CONSOLE  PRTWORK *USER,*CONSOLE  DEFINE PRINT WORK AREA
         SPACE 1
SHOWSTAT FDPRT 'This task will',PADR,NL
         FDTM  JMRUFLG,$JMRWAIT,BZ=SHOWOFF
         FDTM  JMRUFLG,$JMRWMAX,BZ=SHOWONE
         FDPRT 'not be failed'
         FDGOTO SHOWCOM
SHOWONE  FDPRT 'be permitted one extension'
         FDGOTO SHOWCOM
SHOWOFF  FDPRT 'be abended (S522)'
SHOWCOM  FDPRT 'for',PAD
         FDCLC WTTIME,DCF1,4,BE=SHOWSING
         FDPRT WTTIME,I,PAD
         FDPRT 'minutes of inactivity.'
         FDPRT *END
SHOWSING FDPRT 'one minute of inactivity'
         FDPRT *END
HELPSTAT FDPRT 'To prevent system 522 abends, use the command ''SET NOT*
               IMEOUT'', or ''SET NOT''',NL
         FDPRT 'To re-enable automatic LOGOFF, use ''SET TIMEOUT'', or *
               ''SET TIM''',NL
         FDPRT *END
MSGON    FDPRT 'S522 abend (auto-LOGOFF) now disabled - please remember*
                to LOGOFF when done.',NL                         86216
         FDPRT *END
MSGOFF   FDPRT 'S522 abend and LOGOFF processing now enabled',NL
         FDPRT *END
MSGNOTCT FDPRT 'Unable to locate TCT or JMR - terminated',NL
         FDPRT *END
         SPACE 2
SAVE     DSECT ,             SAVE AND WORK AREA
@SERVICE DS    A
@PRINTER DS    A
DB       DS    D
WTTIME   DS    F             SYSTEM WAIT TIME IN MINUTES
TEXT     DC    CL256' '      UPPER CASE TEXT
SAVEND   EQU   *
         SPACE 1
         PRINT &PRTSYS
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         IKJTCB ,
         IHAPSA ,
         IEESMCA ,
         IEFTCT ,
         IEFJMR ,
         A$UCOM ,            RESIDES IN Z904.ACCTMAC
         SPACE 1
         END   ,
