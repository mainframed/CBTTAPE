WCOP     TITLE 'W Y L C O P Y  ***  PS/PO DATASET COPY FUNCTION'
         PUNCH ' ORDER WYLCOPY(P) '    EASIER DUMPS             GP13234
         PUNCH '   SETCODE AC(1)  '    MAKE IT WORK             GP13234
         SPACE 1
         COPY  OPTIONGB
         SPACE 1                                                 91022
         GBLB  &NIH          TRUE IF NIH FORMAT SUPPORTED        91022
         GBLA  &MAXREC       MAXIMUM RECORD LENGTH IN EDIT FORMAT
&NIH     SETB  0             NO NIH SUPPORT                      91022
&MAXREC  SETA  232           NEW OBS WYLBUR                      91022
         SPACE 1
         SYSPARM LIST=YES
         SPACE 1
***********************************************************************
*                                                                     *
*        COPYRIGHT 1986, 1991  EXPERT SYSTEM PROGRAMMING, INC.        *
*        COPYRIGHT 2002  GERHARD POSTPISCHIL; EXPERT SYSTEM PROGR.    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         SPACE 3                                                 91022
***********************************************************************
*                                                                     *
*   WYLCOPY COPIES OR CONVERTS SEQUENTIAL DATASETS OR PARTITIONED     *
*      DATASETS TO A LIKE TYPE. A PDS SPECIFICATION WITH A MEMBER     *
*      IS CONSIDERED SEQUENTIAL.                                      *
*                                                                     *
*   SYSUT1 - INPUT FILE                                               *
*   SYSUT2 - OUTPUT FILE. SHOULD BE A NEW FILE; AN OLD PDS MAY BE     *
*         DAMAGED.                                                    *
*                                                                     *
*      LOAD-MODULES ARE NOT COPIED; THEY CAUSE PROGRAM TERMINATION.   *
*                                                                     *
*      USER HEADER AND TRAILER LABELS ARE COPIED AS-IS, AND MAY NOT   *
*         BE USEFUL FOR MULTI-VOLUME INPUT (OR CONCATENATION).        *
*                                                                     *
*   INPUT FORMATS ACCEPTED:  F V D U + WYLBUR EDIT                    *
*         FORMAT D HAS NOT BEEN TESTED                                *
*   OUTPUT FORMATS : F V D U + WYLBUR EDIT. IF NOT SPECIFIED,         *
*      DEFAULTS TO INPUT TYPE, AS DOES RECORD LENGTH.                 *
*   OUTPUT BLOCKSIZE IS OPTIMIZED BY OUTPUT DEVICE TYPE, AS SET       *
*      IN THE BLOCKSIZE TABLE IN MODULE @DCBEXIT.                     *
*      USE PARM=NBLK TO RETAIN THE INPUT BLOCKSIZE, OR SPECIFY        *
*         THE DESIRED BLOCKSIZE ON THE SYSUT2 DD DCB.                 *
*                                                                     *
*   OUTPUT MESSAGES AND RECORD COUNTS ARE PRODUCED ON AN OPTIONAL     *
*      SYSPRINT DD.                                                   *
*                                                                     *
*   WYLCOPY IS RE-ENTRANT, RE-USABLE AND REFRESHABLE, AND IT          *
*      REQUIRES @SERVICE, @INPREAD AND @PRINTER SERVICE PROGRAMS.     *
*                                                                     *
*   OUTPUT RECORDS ARE WRITTEN WITH THE INPUT LENGTH EXCEPT           *
*      WITH 'FIXED' FORMAT OUTPUT. SHORT RECORDS ARE PADDED           *
*      WITH TRAILING BLANKS.                                          *
*   INPUT RECORDS LONGER THAN THE OUTPUT LENGTH ARE TRUNCATED,        *
*      AND CAUSE A WARNING MESSAGE AND ERROR CODE (RC=2).             *
*   PARM OPERANDS MAY BE USED (OTHER THAN NBLK) TO CONTROL            *
*      CONVERSION FROM WYLBUR EDIT FORMAT TO F/V.                     *
*   PARM=EDIT INSERTS SEQUENCE NUMBERS (MMMM.NNN) IN COLS 73-80       *
*   PARM=INT[EGER] INSERTS 8-DIGIT SEQUENCE NUMBERS IN 73-80          *
*   PARM=LEFT PREFIXES THE 8-DIGIT SEQUENCE BEFORE THE TEXT           *
*   PARM=SWAP PREFIXES EXPECTS AN 8-DIGIT SEQUENCE NUMBER PRIOR TO    *
*      THE TEXT, AND UNCONDITIONALLY GENERATES 72 BYTES OF TEXT       *
*      FOLLOWED BY THE SEQUENCE NUMBER. DISPLAYS COUNTS OF TRUNCATED  *
*      RECORDS.                                                       *
*                                                                     *
*   CONVERSION FROM NON-EDIT FORMAT TO EDIT FORMAT USES THE ABOVE     *
*      PARM OPTIONS TO SPECIFY THE INPUT SEQUENCE NUMBER SOURCE.      *
*      IN THE ABSENCE OF AN OPTION, NUMBERING IS BY .001 INCREMENTS.  *
*      MISMATCH OF A SEQUENCE TYPE (E.G., MISSING #, OR WRONG TYPE),  *
*      RESULTS IN TERMINATION OF THE PROGRAM.                         *
*                                                                     *
*   PARM=UNSEQ - REMOVES SEQUENCE NUMBERS FROM 73-80 FIXED INPUT      *
*      (E.G., USE TO MAKE VB CLIST OR REXX LIBRARY FROM FB FORM)      *
*                                                                     *
*   MAJOR UPGRADE - G. POSTPISCHIL - 91022                            *
*                                                                     *
*   1991/01/22 - PARM MUST USE VL OPTION; ADDED OPTIONAL DDNAME       *
*                LIST IN IEBGENER/IEBCOPY FORMAT.                     *
*   2002/09/08 - CHANGED CODE TO GIVE RETURN CODE INSTEAD OF 013-34   *
*                ABEND ON UNINITIALIZED DATA SET (PROC ASMTRACE)      *
*   2002/11/18 - ADDED PARM=NULL TO SUPPRESS CC=12 ON EMPTY OR        *
*                UNINITIALIZED INPUT DATA SET                         *
*   2006/10/31 - CHANGED SERVICE RELATED MACROS TO CURRENT VERSION.   *
*   2013/08/22 - ADDED SWAP PARM.                                     *
*                                                                     *
***********************************************************************
         EJECT ,
         PRINT &PRTSOR
WYLCOPY  PGMHEAD ZERO12,BASE=(R11,R12),PARM=R8,BNDRY=PAGE    NEW 86287
         SERVINIT ,          INITIALIZE THE SERVICE ROUTINE
         MVC   SYSIN(PATWORKL),PATWORK                           91022
         MVC   UT1OPTS(PATOPTSL),PAT1OPTS  DCB EXIT OPTIONS      91022
         MVC   WIMPLIST(WINPLEN),PATCOMP   MODIFIED PATTERN      91022
         MVC   OUTEXIT(PATEXITL),PATEXIT                         91022
         SERVLOAD @INPREAD,@PRINTER   LOAD SERVICE ROUTINES     GP13234
         LA    R0,UT1OPTS                                        91022
         ST    R0,INEXIT     BUILD EXIT LIST                     91022
         LA    R0,UT2OPTS                                        91022
         ST    R0,OUTEXIT    DITTO                               91022
         SERVCALL LPALD,=CL8'@DCBEXIT'  GET THE DCB EXIT PROCESSOR
         STCM  R0,7,@DCBEXIT+1
         LA    R0,INFMJFCB   POINT TO JFCB
         STCM  R0,7,OUTJFCB+1  COMPLETE EXIT LIST
         LA    R1,INDCBEX    GET FAKE DCB EXIT
         ST    R1,U1OPLIST   MAKE EXIT LIST EXIT
         MVI   U1OPLIST,U2TEXITF+X'80'  POST-DCBEXIT EXIT
         MVC   DCBDDNAM+SYSUT1-IHADCB,=CL8'SYSUT1'               91022
         MVC   DCBDDNAM+SYSUT2-IHADCB,=CL8'SYSUT2'               91022
         LA    R1,SYSUT1                                         91022
         ST@   R1,@SYSUT1,MVI=X'80'                              91022
         LA    R1,SYSUT2                                         91022
         ST@   R1,@SYSUT2,MVI=X'8F'                              91022
         LA    R0,SYSIN      POINT TO INPUT DDNAME               91022
         LA    R1,SYSPRINT   POINT TO OUTPUT DDNAME              91022
         STM   R0,R1,DDNAMSYN   BUILD DDNAME OVERRIDE LIST       91022
         LA    R0,DCBDDNAM+SYSUT1-IHADCB                         91022
         LA    R1,DCBDDNAM+SYSUT2-IHADCB                         91022
         STM   R0,R1,DDNAMUT1   COMPLETE DDNAME LIST             91022
         LA    R8,0(,R8)     CLEAR THE PARM POINTER
         LTR   R8,R8         ANY PARMS ?
         BZ    NODDPARM      NO                                  91022
         TM    0(R8),X'80'   DD LIST SUPPLIED ?
         BNZ   NODDPARM      NO
         TM    4(R8),X'80'   TWO LISTS ONLY ?
         BZ    NODDPARM      MORE THAN TWO - ASSUME TSO CALL     91022
         L     R1,4(,R8)     ADDRESS OF DDN LIST                GP13234
         LA    R1,1(,R1)     CLEAN IT                           GP13234
         LTR   R1,R1         ANY ?                              GP13234
         BZ    NODDPARM      NONE
         SLR   R2,R2
         ICM   R2,3,0(R1)    GET THE LIST LENGTH
         BNP   NODDPARM      NONE                               GP13234
         LA    R3,2(R2,R1)   POINT PAST THE LAST BYTE ON THE LIST
         LA    R2,8          MAKE THE LIST INCREMENT
         SR    R3,R2         POINT TO LAST POSSIBLE ENTRY
         LA    R14,DDNAMLST  POINT TO OVERRIDE LIST              91022
         LA    R15,9         DO NO MORE THAN NINE ENTRIES
         LA    R1,2(,R1)     BUMP OVER LENGTH BYTE
DDPARMLP CLI   0(R1),C' '    VALID NAME ?
         BNH   DDPARMUP      NO; SKIP
         ICM   R4,15,0(R14)  TEST THE ADDRESS
         BZ    DDPARMUP      OVERRIDE NOT ACCEPTED
         MVC   0(8,R4),0(R1) MOVE THE USER'S DDNAME
DDPARMUP LA    R14,4(,R14)   POINT TO NEXT ADDRESS
         BCT   R15,*+8       CONTINUE
         B     NODDPARM      NO MORE NAMES TO DO
         BXLE  R1,R2,DDPARMLP  LOOK FOR NEXT NAME
NODDPARM PRTOPEN SYSPRINT,OPT=(NOWTO)  NO WTO IF DUMMY OR MISSING
         CH    R15,=H'8'     WAS IT OPENED ?                     86290
         BL    MAKEHEAD      YES                                 86290
         LA    R15,JUSTRET   ELSE POINT                          86290
         ST    R15,@PRINTER    TO DUMMY ROUTINE                  86290
MAKEHEAD PRTLIST HEADER,TITLE=1
         LTR   R8,R8         ANY PARM POINTER ?                  91022
         BZ    NOPARMS       NO                                  91022
         ICM   R8,15,0(R8)   TEST PARM POINTER
         BZ    NOPARMS       NONE
         SLR   R9,R9
         ICM   R9,3,0(R8)    TEST PARM LENGTH
         BNP   NOPARMS       NONE
         LR    R6,R9         SAVE PARM LENGTH                    91022
         LA    R8,2(,R8)     POINT TO TEXT                       91022
         LA    R0,PARMLINE   POINT TO TEXT HOLDER                91022
         LR    R5,R0         SAVE DATA ADDRESS FOR SCANNING      91022
         LA    R1,L'PARMLINE   AND ITS LENGTH                    91022
         ICM   R9,8,=C' '    SET FOR BLANK FILL                  91022
         MVCL  R0,R8         SAVE AND PAD PARM DATA              91022
         PRTLIST GUDPRMSG    SHOW PARM                           91022
         AR    R6,R5         SET END (+1)                        91022
         BCTR  R6,0          LAST BYTE                          GP13234
PARMLOOP BLOOK T=PARMOPTS,R=(R6)  END PRESET                     91022
         CLI   0(R5),C' '    ALL DONE ?                          91022
         BE    NOPARMS       YES; ALL DONE                       91022
         PRTLIST BADPRMSG    SHOW BAD PARM                       91022
         SERVTERM ,                                              91022
         ABEND 006           QUIT                                91022
         SPACE 1                                                 91022
PARMOPTS BTAB  ADDCRLF,SETCRLF,BASE=*  APPEND CR/LF TO EACH RECORD
         BTAB  DEBUG,SETBUG            SET DEBUG MODE            91022
         BTAB  DESEQ,SETNOSEQ          REMOVE 73-80 FROM FIXED  GP99215
         BTAB  DUMP,SETBUG             SET DEBUG MODE            91022
         BTAB  EDIT,SETEDIT            USE MMMM.NNN FORMAT       91022
         BTAB  INTEGER,SETINT   CMP/DCMP - 73/80 INTEGER         91022
         BTAB  LEFT,SETTSO             LEFT-JUSTIFIED INTEGER SEQ.
         BTAB  NBLK,SETNBLK            KEEP INPUT BLOCKSIZE      91022
         BTAB  NIH,SETNIH              NIH WYLBUR (LEN, SEQ#)    91022
         BTAB  NOCC,SETNOCC            CC=0 ON BAD INPUT        GP02322
         BTAB  NULL,SETNOCC            CC=0 ON BAD INPUT        GP02322
         BTAB  STRIP,SETSTRIP          DELETE TRAILING BLANKS/NULLS
         BTAB  SWAP,SETSWAP       SWAP SEQ# IN 1-8 TO 73-80     GP13234
         BTAB  TSO,SETTSO              LEFT-JUSTIFIED INTEGER SEQ.
         BTAB  UNNUM,SETNOSEQ          REMOVE 73-80 FROM FIXED  GP99215
         BTAB  UNSEQ,SETNOSEQ          REMOVE 73-80 FROM FIXED  GP99215
         BTAB  XCHG,SETSWAP       SWAP SEQ# IN 1-8 TO 73-80     GP13234
         BTAB  ZERO,SETNOCC            CC=0 ON BAD INPUT        GP02322
         BTAB  ',',PARMLOOP  SKIP COMMAS                         91022
         BTAB  *END          NO MORE                             91022
         SPACE 1                                                 91022
SETTSO   OI    PCMFG1,PCMF1TSO  LEFT-JUSTIFIED SEQUENCE NUMBERS  91022
SETINT   OI    PCMFG1,PCMF1INT+PCMF1NB#  SET INTEGER SEQUENCE    91022
         B     PARMLOOP      LOOK AGAIN                          91022
SETEDIT  OI    PCMFG1,PCMF1EDT+PCMF1NB#  MMMM.NNN SEQUNCES       91022
         B     PARMLOOP                                          91022
SETNIH   OI    PCMFG1,PCMF1NIH  NIH FORMAT ACCEPTABLE            91022
         LA    R0,512        SET FOR EXPANDED RECORD LENGTH      91022
         STH   R0,PCMRECMX                                       91022
         B     PARMLOOP                                          91022
SETSTRIP OI    OPTFLAG,FGSTRIP  REQUEST TRAILING BLANK/NULL DELETION
         B     PARMLOOP                                          93185
SETSWAP  OI    OPTFLAG,FGXCHG   REQUEST 1-8 SWAP TO 73-80       GP13234
         B     PARMLOOP                                         GP13234
SETNOCC  OI    OPTFLAG,FGCCZERO  SUPPRESS INPUT ERROR CONDCODE  GP02322
         B     PARMLOOP                                          93185
SETCRLF  OI    OPTFLAG,FGACRLF  APPEND CR/LF PER RECORD          93185
         MVI   PADREC+L'PADREC-1,2  RECORD LENGHTENING FACTOR    93185
         B     PARMLOOP      (CANCELS NBLK OPTION)               93185
SETNBLK  OI    OPTFLAG,FGBLK  SET IT                             91013
         B     PARMLOOP      CHECK OTHER PARMS                   91022
SETBUG   OI    OPTFLAG,FGBUG SET DEBUG MODE                      91022
         B     PARMLOOP      LOOK AGAIN                          91022
SETNOSEQ OI    OPTFLAG,FGUNSEQ  REMOVE SEQUENCE NUMBERS
         B     PARMLOOP      LOOK AGAIN
         SPACE 1                                                 91022
GUDPRMSG FD    'PARM=''',NL                                      91022
         FD    PARMLINE,DEB                                      91022
         FD    ''''                                              91022
         FD    *END                                              91022
         SPACE 1                                                 91022
BADPRMSG FD    'UNRECOGNIZED PARM ENTRY:',NL                     91022
         FD    0(R5),10,PAD  SHOW BAD TEXT                       91022
         FD    *END                                              91022
         SPACE 2                                                 91022
NOPARMS  INPOPEN SYSIN,OPT=(NOWTO,DUMMY)  PROCESS CONTROL CARDS  86288
         BXH   R15,R15,SYNEOF  NONE
SYNGET   INPGET ,            GET A CARD
         BXH   R15,R15,SYNEOF
         B     SYNGET        AND IGNORE IT
SYNEOF   INPCLOSE ,          CLOSE THE CONTROL FILE
         SERVCALL LPADL,=CL8'@INPREAD'  DONE WITH THE MODULE
         MVC   DB2,SYSUT1+DCBDDNAM-IHADCB  SAVE THE DDNAME
         SERVCALL RJFCB,DB2  GET THE INPUT JFCB
         BXH   R15,R15,UT1BAD  UNPROCESSABLE UT1
         MVC   INJFCB(JFCBLGTH),0(R1)  SAVE IT
         DEVTYPE DB2,DB      GET THE DEVICE TYPE
         BXH   R15,R15,UT1BAD  HUH ?
         CLI   DB+2,UCB3DACC DIRECT ACCESS ?
         BNE   UT1SEQ        NO; PROCESS SEQUENTIALLY
         SERVCALL DSDJ1,INJFCB  GET THE DSCB
         MVC   DS1FMTID(DS1END-DS1FMTID),0(R1)  MOVE IT
         CLI   DS1DSORG+1,0  INVALID DSORG ?
         BNE   UT1BAD        YES
         LA    R14,DS1BLKL   POINT TO BLOCK SIZE FIELD          GP02250
         TM    DS1RECFM,X'C0'           DS1RECFU                GP02250
         BO    UT1TLEN       RECFM=U; NEED NON-ZERO BLOCK       GP02250
         LA    R14,DS1LRECL  POINT TO RECORD LENGTH             GP02250
UT1TLEN  ICM   R0,3,0(R14)   SIZE FILLED IN ?                   GP02250
         BZ    UT1BAD        NO; REJECT IT                      GP02250
         TM    DS1DSORG,255-(JFCORGPO+JFCORGPS+JFCORGU)  INVALID ?
         BNZ   UT1BAD        YES
         TM    DS1DSORG,JFCORGPS  SEQUENTIAL ?
         BNZ   UT1SEQ        YES
         TM    JFCBIND1,JFCPDS  MEMBER NAME SPECIFIED ?
         BNZ   UT1SEQ        YES; PROCESS SEQUENTIALLY
         MVI   INFG,FGPO     SET PARTITIONED
         MVC   SYSUT1(PATDCBLN),PATDCBPO  MAKE PARTITIONED
         B     UT1COM        GO TO COMMON
UT1SEQ   MVI   INFG,0        CLEAR INPUT FLAG
         MVC   SYSUT1(PATDCBLN),PATDCBPS  MAKE SEQUENTIAL
UT1COM   MVC   DCBDDNAM-IHADCB+SYSUT1,DB2  MOVE CORRECT DDNAME BACK
         LA    R8,SYSUT1
         USING IHADCB,R8     DECLARE IT
         LA    R1,INEXIT
         STCM  R1,7,DCBEXLSA  STASH IN DCB
         OI    JFCBTSDM,JFCNWRIT  KEEP IT CLEAN
         OPEN  (,(INPUT,REREAD)),TYPE=J,MF=(E,@SYSUT1) OPEN INPUT
         TM    DCBOFLGS,DCBOFOPN  OPEN ?
         BNZ   UT1OPEN       YES; PROCESS
UT2BAD   DS    0H            SAME MESSAGE FOR OUTPUT FILE
UT1BAD   PRTLIST MSGBADU1    MAKE ERROR MESSAGE
         TM    OPTFLAG,FGCCZERO  SUPPRESS ERROR CODE ?          GP02322
         BNZ   MAINEXIT      YES; JUST QUIT                     GP02322
         OICC  12            SET MAJOR ERROR                    GP13234
         B     MAINEXIT      AND QUIT
         SPACE 1
UT2MIX   PRTLIST MSGBDMIX    MAKE ERROR MESSAGE
         OICC  12            SET MAJOR ERROR                    GP13234
         B     MAINEXIT      AND QUIT
         SPACE 1
UT1OPEN  LA    R2,=C' IN'                                        86288
         LA    R3,SYSUT1                                         86288
         BAL   R9,FORMDSN    DISPLAY THE INPUT                   86288
         MVC   DB2,SYSUT2+DCBDDNAM-IHADCB  SAVE THE OUTPUT DDNAME
         MVC   DB(1),INPRECFM  SAVE RECORD FORMAT
         NI    DB,JFCRCFM    MASK ONLY MAJOR BITS
         TM    DB,FGF+FGV    UNDEFINED ?
         BNO   *+8           NO
         XI    DB,FGF+FGV+FGW  SET UNDEFINED, BUT TRY WYLBUR FIRST
         OC    INFG,DB       COPY FOR READ ROUTINE
         SLR   R1,R1                                             91022
         ICM   R1,3,SYSUT1+DCBLRECL-IHADCB  GET INPUT RECLEN     91022
         AH    R1,PADREC     ADD PADDING                         93185
         TM    INFG,FGD+FGV  VARIABLE ?                          91022
         BZ    *+8           NO                                  91022
         SH    R1,=H'4'      MAKE DATA LENGTH                    91022
         STH   R1,U2LRECLF   SET RECFM F DEFAULT LRECL           91022
         LA    R1,4(,R1)                                         91022
         MIN   R1,=Y(32760),TYPE=H  KEEP WITHIN BOUNDS           91022
         STH   R1,U2LRECLV   SET DEFAULT FOR VARIABLE            91022
         STH   R1,U2LRECLD   DITTO                               91022
         ICM   R1,15,SYSUT1+DCBBLKSI-IHADCB  GET BLOCK SIZE      93185
         AH    R1,PADREC     ADD PADDING                         93185
         MIN   R1,=Y(32760),TYPE=H  NOT TOO LONG ?               93185
         STH   R1,U2LRECLU   SET RECFM U DEFAULT                 93185
         MVC   U2DRECFM,SYSUT1+DCBRECFM-IHADCB                   91022
         SERVCALL RJFCB,DB2  GET THE OUTPUT JFCB
         BXH   R15,R15,UT2BAD  UNPROCESSABLE UT2
         MVC   INJFCB(JFCBLGTH),0(R1)  SAVE IT
         DEVTYPE DB2,DB      GET THE DEVICE TYPE
         BXH   R15,R15,UT2BAD  HUH ?
         CLI   DB+2,UCB3DACC DIRECT ACCESS ?
         BNE   UT2SEQ        NO; PROCESS SEQUENTIALLY
         SERVCALL DSDJ1,INJFCB  GET THE DSCB
         MVC   DS1FMTID(DS1END-DS1FMTID),0(R1)  SAVE IT
         TM    DS1DSORG,JFCORGPO  EXISTING PDS ?                 91022
         BZ    UT2NUPDS      NO                                  91022
         MVC   JFCRECFM,DS1RECFM  PREVENT OVERRIDE               91022
         MVC   JFCLRECL,DS1LRECL                                 91022
UT2NUPDS CLI   JFCRECFM,0    ANY RECFM ?
         BNE   *+10          NO
         MVC   JFCRECFM,DS1RECFM  COPY FROM OUTPUT
         CLI   DS1KEYL,0     ANY KEY LENGTH ?
         BNE   *+10          YES
         MVC   JFCKEYLE,DS1KEYL
*        NOTE THAT BLOCKSIZE WILL BE FIXED IN @DCBEXIT
         ICM   R0,3,JFCBLKSI DID USER OVERRIDE ?                 91022
         BZ    UT2UDBLK      NO; USE EXISTING SIZE               91022
         CLM   R0,3,DS1BLKL  NO LESS THAN EXISTING ?             91022
         BNL   UT2UJBLK      YES; USE LARGER SIZE                91022
UT2UDBLK MVC   JFCBLKSI,DS1BLKL  USE EXISTING SIZE               91022
UT2UJBLK CLI   DS1DSORG+1,0  INVALID DSORG ?
         BNE   UT2BAD        YES
         CLI   DS1DSORG,0    DSORG NOT SET ?
         BNE   UT2HORG       WRONG
         TM    INFG,FGPO     INPUT PARTITIONED ?
         BNZ   UT2PO         YES; FORCE OUTPUT
         B     UT2SEQ        ELSE FORCE SEQUENTIAL
UT2HORG  TM    DS1DSORG,255-(JFCORGPO+JFCORGPS+JFCORGU)  INVALID ?
         BNZ   UT2BAD        YES
         TM    DS1DSORG,JFCORGPS  SEQUENTIAL ?
         BNZ   UT2SEQ        YES
UT2PO    TM    JFCBIND1,JFCPDS  MEMBER NAME SPECIFIED ?
         BNZ   UT2SEQ        YES; PROCESS SEQUENTIALLY
         TM    INFG,FGPO     INPUT PARTITIONED ?
         BZ    UT2MIX        NO; MIXED MODE NOT SUPPORTED
         MVI   OUTFG,FGPO    SET PARTITIONED
         MVC   SYSUT2(PATDCBLN),PATDCBPO  MAKE PARTITIONED
         B     UT2COM        GO TO COMMON
UT2SEQ   MVI   OUTFG,0       CLEAR OUTPUT FLAG
         TM    INFG,FGPO     INPUT PARTITIONED ?
         BNZ   UT2MIX        YES; FAIL MISMATCH
         MVC   SYSUT2(PATDCBLN),PATDCBPS  MAKE SEQUENTIAL
         SPACE 1                                                 91013
UT2COM   TM    OPTFLAG,FGBLK USE INPUT BLOCKSIZE ?               91013
         BZ    UT2COBL       NO; LET @DCBEXIT SET IT             91013
         ICM   R0,3,JFCBLKSI  ANY USER BLKSIZE OVERRIDE ?        91013
         BNZ   UT2COBL       YES; KEEP IT                        91013
         TM    OPTFLAG,FGACRLF  APPEND BYTES ?                   93185
         BNZ   UT2COBL       YES; IGNORE NBLK REQUEST            93185
         MVC   JFCBLKSI,INPBLKSI  USE INPUT SIZE                 91013
UT2COBL  MVC   DCBDDNAM-IHADCB+SYSUT2,DB2  MOVE CORRECT DDNAME BACK
         SPACE 1
         CLI   DB+2,UCB3DACC  DASD OUTPUT ?                      93185
         BNE   UT2NKEY       NO; DON'T COPY TAPE OPTIONS         93185
         CLI   DCBDEVT,X'2F'  POSSIBLE DASD INPUT ?              93185
         BH    UT2NKEY       NO; DON'T COPY KEY LENGTH           93185
         CLI   DCBDEVT,X'20'  OLD, OLD DASD ?                    93185
         BL    UT2NKEY       NO; DON'T CREAM IT                  93185
         CLI   JFCKEYLE,0    ANY KEY LENGTH ?
         BNE   *+10          YES ?  WHAT ARE WE COPYING ?
         MVC   JFCKEYLE,DCBKEYLE
UT2NKEY  CLI   JFCRECFM,0    ANY USER RECFM OVERRIDE ?           91022
         BNE   *+10          YES                                 91022
         MVC   JFCRECFM,INPRECFM  USE INPUT (PRE-OPEN CHECKS)    91022
         SLR   R2,R2
         SLR   R3,R3
         ICM   R3,7,DCBBUFCA  GET BUFFER CONTROL BLOCK
         ICM   R2,3,6(R3)    GET BUFFER LENGTH
         LA    R1,8(,R3)     GET BUFFER
         ST    R1,INPBUFAD   STASH THE INPUT ADDRESS
         STH   R2,INPBUFMX
         LA    R8,SYSUT2     NOW SWITCH TO OUTPUT MAPPING
         LA    R1,OUTEXIT    SET OUTPUT EXIT
         STCM  R1,7,DCBEXLSA STASH IN DCB
         TM    INFG,FGPO     INPUT PARTITIONED ?
         BZ    UT1NEW        NO; PROCESS INPUT
         SPACE 1
         LA    R2,MAINTAB    GET REQUEST SIZE LIST
         LA    R3,DB         RESULT ADDRESS
         GETMAIN VC,LA=(R2),A=(R3),MF=(E,GETMEAN)  GET STORAGE   91022
         BXH   R15,R15,SETFULL   SHOW NOTHING OBTAINED
         LM    R2,R3,DB      GET ADDRESS AND SIZE
         LA    R1,0(R2,R3)   GET END ADDRESS
         L     R4,SYSSIZE    SET RESERVED SIZE
         SR    R1,R4         START ADDRESS TO FREE
         SR    R3,R4         SET RESIDUAL LENGTH
         FREEMAIN R,LV=(R4),A=(1)  FREE RESERVED SIZE
         ST    R2,MEMLIST    SET START OF LIST
         XC    GETMEAN(8),GETMEAN  CLEAR FOR BUFFER PARMS        91022
         SLR   R2,R2
         LA    R5,LISTLEN    GET ENTRY SIZE
         ST    R5,MEMLIST+4  SAVE BXLE INCREMENT
         ST    R5,ALILIST+4    DITTO
         DR    R2,R5         GET NUMBER OF ENTRIES
         ST    R3,NUMSPACE   SAVE NUMBER OF ENTRIES
         MR    R2,R5         GET STORAGE FOR ENTRIES
         A     R3,MEMLIST    GET END ADDRESS
         SR    R3,R5         ADDRESS OF LAST ENTRY
         ST    R3,ALILAST    SAVE END OF LIST
         L     R2,MEMLIST    GET START BACK
         L     R3,ALILAST    GET LAST ENTRY
         AR    R3,R5
         SR    R3,R2
         CLRL  (R2),(R3)     ZERO THE STORAGE
         L     R3,MEMLIST    GET FIRST TABLE ENTRY ADDRESS
         SR    R3,R5         BACKSPACE ONE ENTRY
         ST    R3,MEMLIST+8  SET CURRENT LIST ENTRY
         L     R1,ALILAST    GET END OF ALIAS ENTRIES
         AR    R1,R5         FAKE LAST USED ENTRY
         ST    R1,ALILIST    SET END OF ALIAS ENTRIES
         MVC   PDDRECFM,INPRECFM  COPY RECFM FOR PDSDE CALL
DIRLOOP  L     R4,INPBUFAD   GET THE INPUT BUFFER BACK
         LA    R5,SYSUT1                                         91022
         READ  MYDECB,SF,(R5),(R4),'S',MF=E  READ A BLOCK        91022
         CHECK MYDECB        WAIT FOR COMPLETION                 91022
         LR    R6,R4         COPY BLOCK ADDRESS
         LH    R5,0(,R6)     GET LENGTH OF BLOCK
         SH    R5,=H'2'      ALLOW FOR LENGTH FIELD
         BM    DIRROR        FAIL IF INVALID SIZE
         BZ    DIRLOOP       SKIP IF EMPTY
         CH    R5,=H'254'    VALID ?
         BH    DIRROR        OR TOO LONG
         LA    R4,2(,R6)     POINT TO FIRST ENTRY
         SPACE 1
         USING PDS2,R4       DECLARE THE ENTRY
DIRLMEM  CLC   PDS2NAME,=8X'FF'  LAST ENTRY ?
         BE    DIREOF        YES; QUIT
         TM    PDS2INDC,PDS2NTTR  ANY TTRS IN USER DATA ?        86294
         BNZ   FAILLKED      YES; DON'T CARE TO HANDLE           86294
*        ST    R4,PDDNEXT    SET CURRENT MEMBER ADDRESS          86288
*        SERVCALL PDSDE,PDDNEXT  CHECK TYPE                      86288
*        TM    PDDTYPE,PDDTLKED  LINK-EDIT ENTRY ?               86288
*        BNZ   FAILLKED      YES; NOT SUPPORTED (YET)            86288
         IC    R6,PDS2INDC   GET THE FLAG AND LENGTH BITS
         N     R6,=A(PDS2LUSR)  MASK TO GET LENGTH
         SLL   R6,1          CONVERT TO BYTES
         LA    R6,PDS2USRD-PDS2-1(,R6) UP BY FIXED LENGTH -1
         SR    R5,R6         RESIDUAL LENGTH +1
         BNP   DIRROR        INVALID ENTRY
         TM    PDS2INDC,PDS2ALIS  ALIAS ENTRY ?
         BZ    DIRCMEM       NO; PROCESS AS MEMBER
         INC   NUMALI        COUNT ALIAS ENTRY
         L     R3,ALILIST    GET CURRENT LIST ENTRY
         S     R3,ALILIST+4  POINT TO NEXT ENTRY
         C     R3,MEMCURR    SPILL INTO MEMBER LIST ?
         BNH   SETFULL       YES; SET LIST FULL
         ST    R3,ALILIST    SET LOWEST USED ENTRY
         B     DIRCMEM3      MOVE IT
         SPACE 1
FAILLKED PRTF  '*** LOAD-MODULE COPY NOT SUPPORTED ***',CC=NO
         OICC  13            SET SPECIAL CODE                   GP13234
         B     MAINEXIT      AND CLOSE                           86294
SETFULL  PRTF  '*** INSUFFICIENT STORAGE FOR DIRECTORY ***',CC=NO
         SERVTERM ,          CLOSE AND FREE
         ABEND 804,DUMP      QUIT RUDELY
         SPACE 1
DIRCMEM  INC   NUMMEM        COUNT MEMBERS
         L     R3,MEMCURR    GET PRIOR ENTRY
         A     R3,MEMLIST+4  GET NEXT ENTRY
         C     R3,ALILIST    SPILL INTO ALIAS LIST ?
         BNL   SETFULL       YES; QUIT
         ST    R3,MEMCURR    SET HIGHEST USED ENTRY
DIRCMEM3 EX    R6,MVCMEM
         LA    R4,1(R4,R6)   POINT TO NEXT MEMBER
         BCT   R5,DIRLMEM    TRY IT
         B     DIRLOOP       READ ANOTHER BLOCK
MVCMEM   MVC   0(0,R3),0(R4)   MOVE ONE MEMBER ENTRY
         DROP  R4
         SPACE 1
DIRROR   PRTF  '*** ERROR READING DIRECTORY ***',CC=NO
         SERVTERM ,          CLOSE AND FREE
         ABEND 001,DUMP
         SPACE 1
*        ALIAS ENTRIES ARE SORTED IN TTR SEQUENCE.
*              BUBBLE SORT IS USED TO KEEP NAMES IN SEQUENCE
*
DIREOF   LM    R1,R3,ALILIST  GET START/LEN/END ADDRESS
         SWAPR R2,R3         FLIP LENGTH AND END
         STM   R1,R3,SORTPARM
         OI    SOPHIGH,SOPFAD  INDICATE END ADDRESS
         MVI   SOPSEQ,C'A'   ASCENDING SEQUENCE
         MVI   SOPCOO,PDS2TTRP-PDS2  SET FOR TTR
         MVI   SOPCOL,L'PDS2TTRP     TTR LENGTH
         CR    R1,R2         ANY ?
         BNL   PROCESS       NO
         SERVCALL SORTB,SORTPARM   SORT (LEAVE NAMES IN SEQUENCE)
         EJECT ,
PROCESS  LM    R5,R7,MEMLIST LOAD BXLE REGISTERS
COPYMEM  ST    R5,MEMLIST    UPDATE LIST START
         USING PDS2,R5
         CR    R5,R7         ANY ENTRIES ?
         BNH   COPYMEM2      YES; PROCESS NEXT ONE
*        DO WIDOWED ALIAS ENTRIES HERE
         LM    R5,R7,ALILIST                                     86288
COPYWAL  CLI   PDS2INDC,0    ALREADY PROCESSED ?                 86288
         BNE   COPYMEM1      NO; DO IT NOW                       86288
         BXLE  R5,R6,COPYWAL                                     86288
         B     MAINEXIT      QUIT
MSGWAL   FDPRT '*** WIDOWED ALIAS',NL                            86288
         FDPRT PDS2NAME,CHEX,PAD,INT                             86288
         FDPRT 'CONVERTED TO MEMBER ***'                         86288
         FDPRT *END                                              86288
COPYMEM1 NI    PDS2INDC,255-PDS2ALIS  TURN ALIAS OFF             86288
         PRTLIST MSGWAL      TELL USER                           86288
         SPACE 1
         DROP  R5
COPYMEM2 MVC   MEMBER(LISTLEN),0(R5) COPY ENTRY
         MVI   PDS2INDC-PDS2(R5),0  INDICATE ENTRY PROCESSED     86288
         SLR   R2,R2
         ICM   R2,B'1110',PDS2TTRP  GET TTR OF MEMBER
         ST    R2,DB         MAKE INTO A FULL WORD
         POINT SYSUT1,DB     POSITION TO THE MEMBER
         SPACE 1
UT1NEW   OI    INFG,FG1      NO LONGER IN DIRECTORY PROCESSING
         NI    OUTFG,255-FG1   RESET FIRST BLOCK SWITCH
         MVC   OLDTTR,PDS2TTRP  SAVE ORIGINAL TTR
         SLR   R0,R0
         ST    R0,NUMRECS    RESET RECORD COUNT                  90182
         ST    R0,NUMBLOCK                                       90182
         ST    R0,NUMTRUNC                                       91022
         STCM  R0,3,INPBUFLN RESET INPUT BUFFER OFFSET
         ST    R0,INPRECAD   AND RECORD ADDRESS
         MVC   INPBUFLN(WINPLEN-(INPBUFLN-WINPLIST)),PCMBUFLN    91022
         MVC   WCMBUFLN(COMPLEN-(WCMBUFLN-COMPLIST)),PCMBUFLN    91022
         B     UT1READ       BEGIN PROCESSING
DONEMEM  LM    R5,R7,MEMLIST RELOAD THE BXLE POINTERS
         BXLE  R5,R6,COPYMEM  PROCESS NEXT MEMBER
         B     MAINEXIT      DONE WITH PDS
         SPACE 1
UT1READ  L     R4,INPBUFAD   GET THE INPUT BUFFER
         LA    R5,SYSUT1                                         91022
         READ  MYDECB,SF,(R5),(R4),'S',MF=E   READ AN INPUT BUFFER
         CHECK MYDECB        WAIT FOR COMPLETION                 91022
         TM    DCBRECFM+SYSUT1-IHADCB,JFCUND  UNDEFINED ?
         BNO   UT1NEXT       NO; TRUE SIZE NOT AVAILABLE
         MVC   INPBUFMX,DCBLRECL+SYSUT1-IHADCB  GET THE BLOCK SIZE
UT1NEXT  LH    R5,INPBUFMX   GET BLOCK LENGTH
         TM    INFG,FGF      FIXED ?
         BNZ   UT1FIX        YES
         TM    INFG,FGV+FGD  VARIABLE ?
         BNZ   UT1VAR        YES
         TM    INFG,FGU      PLAIN UNDEFINED ?
         BNZ   UT1UND        YES
UT1WYL   SERVCALL WDCOM,WINPLIST  TRY TO DECOMPRESS AN ENTRY
         CH    R15,=H'4'     TEST RETURN CODE
         BE    UT1READ       BLOCK IS EMPTY
         BH    UT1NWYL       NOT WYLBUR FORMAT
         TM    DCBOFLGS+SYSUT2-IHADCB,DCBOFOPN  FIRST TIME EVER ?
         BNZ   UT1WYL2       NO                                  91022
         SLR   R0,R0         SET NO RECORD                       91022
         BAL   R9,PUT        OPEN                                91022
         LH    R0,U2LRECLF   GET THE MAXIMUM DATA SIZE           91022
         MIN   R0,=Y(&MAXREC),TYPE=H  NOT TOO HIGH               91025
         MIN   R0,=Y(512),TYPE=H    AND NOT OVER DESIGN SIZE     91025
         STH   R0,INPRECMX   SET IT NOW                          91022
         STH   R0,PCMRECMX   AND FOR NEXT TIME                   91022
         XC    INPBUFLN,INPBUFLN  FORCE RESTART ON THIS BLOCK    91022
         B     UT1WYL        GET (TRUNCATED) FIRST RECORD        91022
         SPACE 1                                                 91022
UT1WYL2  CH    R0,=Y(VCMRCLEN)  RECORD TRUNCATED ?               91022
         BNE   UT1WYL3       NO                                  91022
         INC   NUMTRUNC,WORK=R15  COUNT IT                       91022
UT1WYL3  L     R1,INPRECAD   GET THE RECORD ADDRESS
         SLR   R0,R0
         ICM   R0,3,INPRECLN   AND THE RECORD LENGTH
         MVC   WCMLINE#,INPLINE#  KEEP LINE NUMBER               91022
         BAL   R9,PUT        AND COPY THE LINE
         B     UT1WYL        LOOP FOR MORE
UT1NWYL  XI    INFG,FGW+FGU  NOT WYLBUR - USE UNDEFINED
         NI    OUTFG,255-FGW RESET OUTPUT FLAG ALSO              87018
         OI    OUTFG,FGU                                         87018
*Y 2?    XI    OUTFG,FGW+FGU  ELSE FORCE UNDEFINED               91025
         NI    INPFG2,255-INPF2NPR  RECORD EXPANSION             91025
         NI    WCMFG2,255-WCMF2NPR  DITTO                        91025
         NI    PCMFG2,255-INPF2NPR  ALSO SET FOR NEXT MEMBER     91025
UT1UND   LR    R0,R5         COPY LENGTH
         LR    R1,R4         AND ADDRESS
         BAL   R9,PUT        WRITE ONE BLOCK
         B     UT1READ       READ NEXT BLOCK
UT1FIX   LR    R1,R4         COPY RECORD START
         LH    R2,INPLRECL   GET INPUT LENGTH
         LA    R3,0(R4,R5)   GET END OF BUFFER
         SR    R3,R2         POINT TO LAST RECORD IN BUFFER
UT1FIXL  LR    R0,R2         COPY LENGTH                        GP99215
         TM    OPTFLAG,FGUNSEQ  REMOVE SEQUENCE NUMBERS?        GP99215
         BZ    UT1FIXP       NO                                 GP99215
*LATER*  C     R2,=F'72'     LONG ENOUGH ?                      GP99215
*LATER*  BNH   UT1FIXP       NO                                 GP99215
         C     R2,=F'80'     NOT TOO LONG?                      GP99215
         BH    UT1FIXP       TOO LONG; IGNORE                   GP99215
         STM   R1,R2,PUTSAVE PRESERVE OVER TRT                  GP99215
         TM    OPTFLAG,FGXCHG     SWAP SEQUENCE NUMBERS ?       GP13234
         BZ    RIGHTSEQ             NO                          GP13234
         TRT   0(8,R1),TRTDIGIT   LEFT SEQUENCE NUMBER ?        GP13234
         BNZ   RIGHTSEQ             NO                          GP13234
         L     R1,PUTSAVE    RESTORE TEXT ADDRESS               GP13234
         LM    R14,R15,0(R1) PRESERVE OLD SEQUENCE              GP13234
         MVC   0(72,R1),8(R1)     SLIDE LEFT                    GP13234
         STM   R14,R15,72(R1)     SEQUENCE ON RIGHT             GP13234
         LM    R1,R2,PUTSAVE RESTORE AFTER TRT                  GP13234
         B     UT1FIXP         AND PUT IT OUT                   GP13234
         SPACE 1
RIGHTSEQ TRT   72(8,R1),TRTDIGIT  NUMERIC OR WYLBUR SEQUENCE?   GP99215
         LM    R1,R2,DB      RESTORE AFTER TRT                  GP99215
         BNZ   UT1FIXP       NO; WRITE IT AS IS                 GP99215
         MIN   R0,=F'72'     SHORTEN                            GP99215
UT1FIXP  BAL   R9,PUT        WRITE ORIGINAL OR SHORTER RECORD   GP99215
         BXLE  R1,R2,UT1FIXL   REPEAT FOR ALL
         B     UT1READ       GET ANOTHER BLOCK
         SPACE 1
UT1VAR   ICM   R5,3,0(R4)    GET THE LENGTH OF THE BLOCK
         BNP   UT1ERR        TOO BAD
         LA    R2,4          SET LENGTH TO SKIP
         LA    R3,0(R4,R5)   POINT PAST LAST BYTE
         SR    R3,R2         PREVENT OVERRUN
UT1VARLP BXH   R4,R2,UT1READ  READ NEXT BLOCK                    91044
         ICM   R2,3,0(R4)    LOAD RDW LENGTH
         BNP   UT1READ       SKIP BLOCK IF BAD
         LR    R0,R2         COPY LENGTH                         91044
         LA    R1,4(,R4)     SET TEXT STARTING ADDRESS           91044
         SH    R0,=H'4'      SET TEXT LENGTH                     91044
         BP    UT1VARPT      OK - PUT IT OUT                     91022
         BM    UT1READ       ERROR ?                             91022
         LA    R1,=C' '      SET SPECIAL DUMMY RECORD SIGNAL     91022
         LA    R0,1          CHANGE TO ZERO LENGTH IN PUT        91022
UT1VARPT TM    OPTFLAG,FGXCHG     SWAP SEQUENCE NUMBERS ?       GP13234
         BZ    UT1VARPU             NO                          GP13234
         CH    R0,=H'9'      ANY DATA ?                         GP13234
         BL    UT1VARPU                                         GP13234
         STM   R1,R5,PUTSAVE RESTORE AFTER TRT                  GP13234
         TRT   0(8,R1),TRTDIGIT   LEFT SEQUENCE NUMBER ?        GP13234
         BNZ   UT1VARPU             NO                          GP13234
         CH    R0,=H'80'     TRUNCATED ?                        GP13234
         BNH   UT1VARNT        NO                               GP13234
         INC   NUMTRUNC,WORK=R5   COUNT IT                      GP13234
UT1VARNT MVC   TEMPCARD+72(8),0(R1)  COPY SEQUENCE              GP13234
         LA    R4,TEMPCARD   DESTINATION                        GP13234
         LA    R2,8(,R1)     SOURCE AFTER SEQUENCE              GP13234
         LR    R3,R0         COPY LENGTH                        GP13234
         SH    R3,=H'8'      LENGTH LESS RDW AND SEQUENCE       GP13234
         LA    R5,72         BLANK FILL SHORT RECORD            GP13234
         ICM   R3,8,=C' '                                       GP13234
         MVCL  R4,R2         MOVE LEFT                          GP13234
         LA    R0,80         SET NEW CARD IMAGE LENGTH          GP13234
         LA    R1,TEMPCARD     AND ADDRESS                      GP13234
         LM    R2,R5,PUTSAVE+4   RESTORE                        GP13234
UT1VARPU BAL   R9,PUT        WRITE IT                            91022
         B     UT1VARLP      DO NEXT
         SPACE 1
UT1ERR   PRTF  '*** BDW/RDW ERROR ***',CC=NO
         SERVTERM ,          CLOSE
         ABEND 002,DUMP      ABEND
         SPACE 1
UT1EOF   TM    INFG,FG1      READING DIRECTORY ?
         BZ    DIREOF        YES; GO BACK THERE
         TM    DCBOFLGS-IHADCB+SYSUT2,DCBOFOPN  EVER OPENED ?    86338
         BNZ   UT1EOF1       YES; PROCEED WITH TRUNC             86338
         SLR   R0,R0         SET ZERO RECORD LENGTH              86338
         LA    R1,MEMBER     POINT TO ANYTHING                   86338
         BAL   R9,PUT        CALL PUT SOLELY FOR OPEN PROCESSING 86338
UT1EOF1  BAL   R9,PUTWRIT2   DO FINAL WRITE                      86338
         ICM   R0,15,NUMTRUNC  ANY TRUNCATED RECORDS ?           91022
         BZ    UT1EOF1T      NO                                  91022
         OICC  2             SET WARNING FLAG                   GP13234
UT1EOF1T TM    OUTFG,FGPO    DOING PDS ?
         BNZ   UT1EOFPO      YES; PRINT MEMBER INFORMATION       90182
         PRTLIST MSGREC      PRINT SEQUENTIAL RECORD COUNT       90182
         B     MAINEXIT      QUIT THIS                           90182
UT1EOFPO TM    OUTFG,FG1     BLOCK WRITTEN ?
         BNZ   UT1EOF2       YES
         PRTLIST MSGMEM      PRINT MEMBER INFO
         NOTE  SYSUT2        GET CURRENT TTR
         ST    R1,CURTTR
         STCM  R1,14,PDS2TTRP  STASH MEMBER TTR
UT1EOF2  PRTLIST MSGMEW      SHOW NEW MEMBER TTR
         STOW  SYSUT2,MEMBER,R  STOW (REPLACE)
         CH    R15,=H'8'     ERROR ?
         BNH   CHECKAL       NO; LOOK FOR ALIAS ENTRIES
         PRTLIST MSGSTOWR    PRINT ERROR MESSAGE
         OICC  8             SET ERROR                          GP13234
         B     MAINEXIT      AND QUIT
CHECKAL  LM    R3,R5,ALILIST   GET ALIAS LIST
         CR    R3,R5         ANY AT ALL ?
         BH    DONEMEM       NO; SKIP
         USING ALIAS,R3      DECLARE THE ALIAS ENTRY
CHECKALP CLC   ALIITTR,OLDTTR  ASSOCIATED WITH THIS MEMBER ?
         BH    DONEMEM       NO; PAST IT - DO NEXT MEMBER
         BL    CHECKALX      NOT YET; INCREMENT
         CLI   ALIIFLAG,0    ALREADY PROCESSED ?
         BE    CHECKALX      YES; CHECK NEXT
         MVC   ALIITTR,PDS2TTRP  SET NEW MEMBER TTR
         STOW  SYSUT2,ALIAS,R   STASH ALIAS ENTRY
         MVI   ALIIFLAG,0    INDICATE ALIAS PROCESSED
         MVC   ALIITTR,OLDTTR  RESTORE TTR
         CH    R15,=H'8'     ERROR ?
         BH    CHECKALR      YES; QUIT
CHECKALX BXLE  R3,R4,CHECKALP  TRY NEXT ENTRY
         B     DONEMEM       DO NEXT MEMBER
         SPACE 1                                                 90182
MSGREC   FDOPT NL                                                90182
         FDGOTO MSGRECS                                          90182
MSGMEW   FDPRT '  NEW TTR='
         FDPRT PDS2TTRP,HEX
MSGRECS  FDPRT NUMRECS,I,RADJ,LEN=15                             90182
         FDCLC NUMRECS,DCF1,4,BE=MSGCNT1                         90182
         FDPRT 'RECORDS COPIED',PADL                             90182
         FDPRT NUMBLOCK,I,RADJ,LEN=10                            90182
         FDPRT 'BLOCK',PADL                                      91022
         FDCLC NUMBLOCK,DCF1,4,BE=MSGCNTX                        91022
         FD    'S'                                               91022
         FDGOTO MSGCNTX                                          91022
MSGCNT1  FDPRT 'RECORD COPIED',PADL                              90182
MSGCNTX  FDCLC NUMTRUNC,DCF1,4,BL=MSGCNTY                        91022
         FD    '>>>>> RECORDS TRUNCATED:',NL,PAD                 91022
         FD    NUMTRUNC,I                                        91022
MSGCNTY  FDPRT *END                                              90182
         SPACE 1
CHECKALR PRTLIST MSGALSTO    PRINT STOW ERROR
         OICC  8                                                GP13234
*        B     MAINEXIT
         SPACE 1
MAINEXIT TM    OPTFLAG,FGBUG  DEBUG MODE ?                       86294
         BZ    MAINIXIT      NO                                  86294
         PRTCLOSE ,          FORCE THE ERROR MESSAGE TO BE PRINTED
         ABEND 1234,DUMP     GO AHEAD, DUMP ME                   86294
MAINIXIT LA    R2,SYSUT1                                         86294
         CLC   DCBBUFCB+SYSUT1-IHADCB,DCBBUFCB+SYSUT2-IHADCB
         BNE   *+10
         XC    DCBBUFCB+SYSUT2-IHADCB,DCBBUFCB+SYSUT2-IHADCB
         BAL   R9,CLOSER
         LA    R2,SYSUT2
         BAL   R9,CLOSER
         LM    R1,R2,GETMEAN DID WE GET A BUFFER ?               91022
         LTR   R0,R2         CHECK LENGTH                        91022
         BZ    MAINAXIT      NONE                                91022
         FREEMAIN R,A=(1),LV=(0)  FREE THE BUFFER                91022
MAINAXIT L     R9,RETCODE    GET THE RETURN CODE                GP13234
         SERVTERM ,          CLOSE AND FREE EVERYTHING
         DELETEST EP=@DCBEXIT,LEN=3                             GP13234
         PGMEXIT RC=(R9)     TERMINATE WITH RETURN CODE         GP13234
         SPACE 1
MSGMEM   FD    ' ',NL,LEN=3  INDENT MEMBER NAME                  91022
         FDPRT PDS2NAME,CHEX,PAD                                 91022
         FDPRT PDS2TTRP,HEX,PAD
         FDPRT PDS2INDC,HEX,PAD
         FDPRT PDS2USRD,4,HEX,PAD
         FDPRT *END
         EJECT ,
*        RECORD PUT ROUTINE
*          R0 - LENGTH OF CURRENT RECORD
*          R1 - ADDRESS OF RECORD
*        RECORD SPECIFIES DATA ONLY (NO RDW FOR RECFM=V)         91022
         SPACE 1
PUT      STM   R0,R9,PUTSAVE SAVE REGISTERS FOR RECURSION
         TM    DCBOFLGS+SYSUT2-IHADCB,DCBOFOPN  OUTPUT OPEN ?
         BNZ   PUT2          YES; PROCEED
         NI    JFCBTSDM,255-JFCNWRIT-JFCNDSCB-JFCNDCB  ALLOW UPDT
         OI    JFCBTSDM,JFCVSL  SET VOL-SER CHANGE FOR REWRITE   86289
         TM    INFG,FGU      INPUT OTHER THAN UNDEFINED ?        91022
         BNZ   PUTNRECW      NO; NO CHANGE                       91022
         TM    JFCRECFM,JFCUND  OUTPUT RECFM=U ?                 91022
         BNO   PUTNRECW      NO                                  91022
         NI    OUTFG,255-FGF-FGV-FGD-FGU  RESET (?)              91022
         OI    OUTFG,FGW     MAKE WYLBUR                         91022
         TM    INFG,FGW      INPUT ALSO WYLBUR ?                 91022
         BZ    PUTNRECW      NO; MUST DECOMPRESS                 91022
         TM    OPTFLAG,FGACRLF  APPEND STUFF ?                   93185
         BNZ   PUTNRECW      YES; MUST EXPAND                    93185
         OI    INPFG2,INPF2NPR  NO RECORD EXPANSION
         OI    WCMFG2,WCMF2NPR  DITTO
         OI    PCMFG2,INPF2NPR  ALSO SET FOR NEXT MEMBER         91022
PUTNRECW MVI   U2FLAG1,U21NOFBS+U21NOPCI+U21NOSD+U21TRUNC+U21FULLT
         MVI   U2FLAG3,U23BLKWY+U23DFWYL  SET WYLBUR BLKSIZE     91025
         TM    INFG,FGW      IS INPUT IN WYLBUR FORMAT ?         86294
         BNZ   PUTOPEN       YES                                 86294
*HUH?NOT CLI   INPLRECL,0    LRECL<256 ?                         86294
*TRUE??  BE    PUTOPEN       YES; TREAT LIKE WYLBUR              86294
*EXTRA?  MVI   U2FLAG1,U21NOFBS+U21NOPCI+U21NOSD+U21TRUNC+U21FULLT
         NI    U2FLAG3,255-U23DFWYL     DON'T USE WYLBUR TABLES  91025
         TM    JFCRECFM,JFCUND  RECFM=U ?                        86294
         BNO   PUTOPEN       NO                                  86294
         ICM   R0,3,JFCBLKSI   ANY BLOCK SIZE ?                  86294
         BZ    PUTOPEN       NO; LET @DCBEXIT SET SOMETHING      86294
         ICM   R0,3,INPBLKSI  GET INPUT SIZE                     93185
         AH    R0,PADREC     ADD PADDING                         93185
         MIN   R0,=Y(32760),TYPE=H  NOT TOO LARGE                93185
         STH   R0,JFCBLKSI                                       93185
         STH   R0,JFCLRECL   DOUBLE, DOUBLE, TOIL AND TROUBLE    93185
PUTOPEN  OPEN  (,OUTPUT),TYPE=J,MF=(E,@SYSUT2)  OPEN OUTPUT      91022
         TM    OUTFG,FGF+FGV+FGU+FGD+FGW  PRESET ?               91022
         BNZ   PUTOPENF      YES; KEEP IT                        91022
         SLR   R2,R2                                             91022
         IC    R2,DCBRECFM+SYSUT2-IHADCB  GET RECORD FORMAT      91022
         SRL   R2,5          ELIMINATE MODIFIERS                 91022
         STC   R2,DB                                             91022
         TR    DB(1),=AL1(0,FGD,FGV,FGV,FGF,FGF,FGW,FGW)         91022
         CLI   DB,0          VALID ?                             91022
         BNE   *+10          YES?                                91022
         MVC   DB(1),INFG    ELSE COPY INPUT                     91022
         NI    DB,FGD+FGV+FGF+FGW                                91022
         TM    DB,FGW        WYLBUR ?                            91022
         BZ    PUTOPENR      NO                                  91022
         TM    U2FLAG3,U23ISWYL  MATCHES WYLBUR CONVENTIONS ?    91025
         BZ    PUTOPENU      NO; CANCEL WYLBUR                   91025
         TM    INFG,FGU      NON-WYLBUR INPUT ?                  91022
         BZ    PUTOPENR      NO                                  91022
PUTOPENU XI    DB,FGW+FGU    CANCEL WYLBUR                       91022
         XI    OUTFG,FGW+FGU  ELSE FORCE UNDEFINED               91025
         NI    INPFG2,255-INPF2NPR  RECORD EXPANSION             91025
         NI    WCMFG2,255-WCMF2NPR  DITTO                        91025
         NI    PCMFG2,255-INPF2NPR  ALSO SET FOR NEXT MEMBER     91025
PUTOPENR OC    OUTFG,DB      MAKE OUTPUT FLAGS                   91022
PUTOPENF TM    OUTFG,FGW     WYLBUR OUTPUT ?                     91025
         BZ    PUTOPENW      NO                                  91025
         TM    U2FLAG3,U23ISWYL  MATCHES CONVENTIONS ?           91025
         BNZ   PUTOPENW      YES; WHOOPPEE !!!                   91025
         XI    OUTFG,FGW+FGU  ELSE FORCE UNDEFINED               91025
         NI    INPFG2,255-INPF2NPR  RECORD EXPANSION             91025
         NI    WCMFG2,255-WCMF2NPR  DITTO                        91025
         NI    PCMFG2,255-INPF2NPR  ALSO SET FOR NEXT MEMBER     91025
PUTOPENW SLR   R2,R2                                             91025
         ICM   R2,3,DCBLRECL+SYSUT2-IHADCB  GET RECORD LENGTH    91022
         TM    OUTFG,FGD+FGV VARIABLE ?                          91022
         BZ    *+8           NO                                  91022
         SH    R2,=H'4'      ADJUST                              91022
         STH   R2,U2LRECLF   SAVE MAXIMUM RECORD LENGTH ACCEPTED 91022
         TM    DCBOFLGS+SYSUT2-IHADCB,DCBOFOPN  OPEN NOW ?
         BZ    UT2BAD        NO; FAIL
         MVC   OUTBLKSZ,DCBBLKSI+SYSUT2-IHADCB  SAVE BLOCKSIZE   86289
         MVC   WCMBUFMX,DCBBLKSI+SYSUT2-IHADCB  SAVE BLOCKSIZE   91025
         LA    R2,=C'OUT'                                        86288
         LA    R3,SYSUT2                                         86288
         BAL   R9,FORMDSN    DISPLAY THE OUTPUT                  86288
         TM    OPTFLAG,FGACRLF  APPEND DATA                      93185
         BNZ   PUTONU        YES; NEED OUTPUT BUFFER             93185
         TM    OUTFG,FGU     TRUE U ?                            91025
         BNZ   PUT1X         YES; DON'T NEED BUFFER              91025
PUTONU   SLR   R0,R0                                             91022
         ICM   R0,7,DCBBUFCA+SYSUT2-IHADCB                       91022
         C     R0,DCF1       BUFCB 0 OR 1 ?                      91022
         BNH   PUT0          YES; GET A BUFFER                   91022
         CLC   DCBBUFCB+SYSUT1-IHADCB,DCBBUFCB+SYSUT2-IHADCB
         BE    PUT0          OK IF OPEN LEFT IT ALONE
         L     R1,DCBBUFCB+SYSUT2-IHADCB  GET BUFFER CONTROL BLOCK
         LH    R2,6(,R1)     GET BLOCK LENGTH
         MIN   R2,DCBBLKSI+SYSUT2-IHADCB,TYPE=H  NOT TOO LONG    91044
         LA    R1,8(,R1)     GET BUFFER START
         B     PUT0C
PUT0     SLR   R2,R2
         ICM   R2,3,DCBBLKSI+SYSUT2-IHADCB  GET A BLOCK
         GETMAIN R,LV=(R2)   GET SOME ROOM
         STM   R1,R2,GETMEAN SAVE FOR CLOSE                      91022
PUT0C    ST    R1,WCMBUFAD   SAVE THE ADDRESS
         STH   R2,WCMBUFMX   AND THE LENGTH
PUT1X    LM    R0,R1,PUTSAVE  RELOAD INPUT PARMS
PUT2     LTR   R0,R0         ANY RECORD AT ALL ?                 86338
         BNP   PUTEXIT       NO; CALLED FOR OPEN ONLY            86338
         TM    OPTFLAG,FGSTRIP  STRIP TRAILING NULLS/BLANKS ?    93185
         BZ    PUT2SHRT      NO                                  93185
         TM    OUTFG,FGF     FIXED LENGTH OUTPUT ?               93185
         BNZ   PUT2SHRT      YES; NO STRIP USEFUL                93185
         LR    R15,R1        COPY RECORD START                   93185
         AR    R15,R0        PLUS LENGTH                         93185
         LR    R14,R0        COPY NUMBER OF TIMES TO CHECK       93185
         SH    R14,=H'1'     BUT ALWAYS LEAVE AT LEAST ONE       93185
         BNP   PUT2SHRT      NOTHING TO DO                       93185
PUT2NULP BCTR  R15,0         BACK-SPACE ONE                      93185
         TM    0(R15),X'FF'-C' '  NULL OR BLANK ?                93185
         BNZ   PUT2NULD      NO; DONE                            93185
         BCT   R14,PUT2NULP  TRY PREVIOUS BYTE, IF ANY           93185
PUT2NULD LA    R0,1(,R14)    SET TRUE LENGTH                     93185
PUT2SHRT TM    OPTFLAG,FGACRLF  APPEND DATA ?                    93185
         BNZ   PUT2NOTU      HAVE OTHER BUFFER                   93185
         TM    OUTFG,FGU     TRUE TO U ?                         91025
         BZ    PUT2NOTU      NO                                  91025
         ST    R1,WCMBUFAD   SAVE ADDRESS                        91022
         STH   R0,WCMBUFLN   AND LENGTH (NO BUFFER)              91022
PUT2NOTU TM    OUTFG,FGW     WYLBUR COMPRESSED OUTPUT ?          91022
         BNZ   PUT2WYL                                           91022
         LA    R9,=C' '      SPECIAL DUMMY RECORD                91022
         CR    R9,R1         DUMMY RECORD ADDRESS ?              91022
         BNE   PUTNDUMY      NO                                  91022
         SLR   R0,R0         RECFM=V WITH TRUE ZERO LENGTH RCD   91022
PUTNDUMY L     R2,WCMBUFAD   GET OUTPUT BUFFER START             91022
         AH    R0,PADREC     ADD ROOM FOR PADDING                93185
         SLR   R3,R3
         ICM   R3,3,WCMBUFLN GET CURRENT BUFFER LENGTH
         BNZ   PUTNFRST      NOT FIRST TIME
         TM    OUTFG,FGV+FGD  NEED LENGTH PREFIX ?               91022
         BZ    PUTNFRST      NO
         LA    R3,4          OVERHEAD IS 4
PUTNFRST CH    R0,U2LRECLF   COMPARE TO MAXIMUM DATA LENGTH      91022
         BNH   PUT2NBIG                                          91022
         LH    R0,U2LRECLF   TRUNCATE THE TEXT                   91022
         INC   NUMTRUNC,WORK=R15  UPDATE TRUNCATION COUNT        91022
         ST    R0,PUTSAVE    PREVENT RECURSION ERROR             91022
PUT2NBIG TM    OUTFG,FGU     UNDEFINED ?                         91022
         BNZ   PUTMOVED      YES; WRITE AS IS                    91022
         AR    R2,R3         NEXT OUTPUT ADDRESS                 91025
         LR    R14,R1        GET INPUT ADDRESS
         LR    R15,R0        GET INPUT LENGTH
         LR    R0,R3         COPY BUFFER SIZE
         LR    R3,R15        GET INPUT LENGTH BACK
         TM    OUTFG,FGF     FIXED LENGTH OUTPUT ?
         BZ    PUTNRECF      NO                                  91022
         LH    R3,DCBLRECL-IHADCB+SYSUT2  GET TRUE OUTPUT LENGTH
         ICM   R15,IOOO,=C' '  SET FOR BLANK FILL                91022
PUTNRECF AR    R0,R3         GET NEW BUFFER SIZE                 91022
         TM    OUTFG,FGV     RECFM=V OUTPUT ?                    91022
         BZ    PUTNRECV      NO                                  91022
         LA    R9,4          SET RDW OVERHEAD                    91022
         AR    R0,R9         SET TRUE BUFFER SIZE                91022
         CH    R0,WCMBUFMX   WILL IT FIT ?                       91022
         BH    PUTNRECV      NO; GO TO SPILL CURRENT BUFFER      91022
         LA    R9,4(,R3)     GET TRUE OUTPUT LENGTH              91022
         SLL   R9,16         MAKE TRUE RDW                       91022
         STCM  R9,15,0(R2)   PLACE INTO BUFFER                   91022
         LA    R2,4(,R2)     ADJUST TEXT START                   91022
PUTNRECV CH    R0,WCMBUFMX   WILL IT FIT INTO THE BUFFER ?       91022
         BNH   PUTMOVE       YES; GO TO MOVE IT
         ICM   R9,3,WCMBUFLN  BUT IS CURRENT BUFFER EMPTY ?      87018
         BZ    UT2BIG        YES; BLOCK TOO BIG OR LOGIC ERROR   87018
PUTFULL  BAL   R9,PUTWRITE   WRITE THE BUFFER
         B     PUT1X         AND CURSE
         SPACE 1                                                 91022
PUT2WYL  TM    OPTFLAG,FGACRLF  PAD RECORD ?                     93185
         BZ    PUT2WYL1      NO                                  93185
         LR    R14,R0                                            93185
         AR    R14,R1        LAST BYTE+1 OF RECORD               93185
         MVC   0(2,R14),=X'0D25'  APPEND CR/LF (BUFFER IN @SERVICE)
         AH    R0,PADREC     UP LENGTH                           93185
PUT2WYL1 CH    R0,WCMRECMX   COMPARE TO MAXIMUM DATA LENGTH      91022
         BNH   PUT2WYL2      OK                                  91022
         LH    R0,WCMRECMX   TRUNCATE THE TEXT                   91022
         INC   NUMTRUNC,WORK=R15  UPDATE TRUNCATION COUNT        91022
         ST    R0,PUTSAVE    PREVENT RECURSION ERROR             91022
PUT2WYL2 STH   R0,WCMRECLN   STASH RECORD LENGTH                 91022
         ST    R1,WCMRECAD   AND ADDRESS                         91022
         SERVCALL WCOMP,COMPLIST  COMPRESS AND MOVE              91022
         CH    R15,=H'4'     CHECK RETURN                        91022
         BL    PUTMOVED      RECORD MOVED                        91022
         BH    PUT2WERR      TOO BAD                             91022
         STH   R0,WCMBUFLN   SET THE WRITE LENGTH BACK           91022
         B     PUTFULL       AND WRITE THE FULL BUFFER           91022
PUT2WERR STM   R15,R0,DB     STASH FOR FORMAT                    91022
         PRTLIST MSGBDCMP    SHOW COMPRESS FAILED                91022
BADLRECL SERVTERM ,                                              91022
         ABEND 002,DUMP                                          91022
         SPACE 1                                                 91022
MSGBDCMP FD    'WYLBUR COMPRESS ERROR',NL                        91022
         FDTM  OUTFG,FGPO,BZ=MSGBDCMQ                            91022
         FD    'IN MEMBER',PAD                                   91022
         FD    PDS2NAME,CHEX                                     91022
MSGBDCMQ FD    ', RC='                                           91022
         FD    DB,4,I                                            91022
         FD    ', REASON='                                       91022
         FD    DB+4,4,I                                          91022
         FD    *END                                              91022
         SPACE 1                                                 91022
PUTMOVE  STH   R0,WCMBUFLN   SET CURRENT BUFFER LENGTH
         SH    R3,PADREC     ALLOW FOR PADDING                   93185
         MVCL  R2,R14        MOVE ONE RECORD
         TM    OPTFLAG,FGACRLF  PAD ?                            93185
         BZ    PUTMOVED      NO                                  93185
         MVC   0(2,R2),=X'0D25'  APPEND CR/LF                    93185
PUTMOVED INC   NUMRECS,WORK=R15   COUNT RECORDS WRITTEN          90182
         TM    OUTFG,FGU     TRUE UNDEFINED ?                    91022
         BNZ   PUTFRITE      YES; WRITE IT NOW                   91022
         CLC   WCMBUFLN,WCMBUFMX
         BNE   PUTEXIT       BUFFER NOT FULL
PUTFRITE BAL   R9,PUTWRITE   BUFFER FULL OR RECFM=U
PUTEXIT  LM    R0,R9,PUTSAVE   RESTORE
         BR    R9            RETURN TO CALLER
         SPACE 2
PUTWRITE TM    OUTFG,FGU     UNDEFINED ?
         BZ    PUTWRIT2      NO
         LM    R1,R2,PUTSAVE  RELOAD LENGTH/ADDRESS
         STH   R1,DCBLRECL+SYSUT2-IHADCB  SET RECFM=U RECLEN     91022
         LTR   R3,R1         SWAP LENGTH AROUND                  86294
         BP    PUTWRITC      WRITE WITHOUT MOVE                  86294
         BR    R9            JUST EXIT                           86294
PUTWRIT2 L     R2,WCMBUFAD   GET BUFFER ADDRESS
         SLR   R3,R3
         ICM   R3,3,WCMBUFLN   ANYTHING TO WRITE ?
         BZR   R9            NO; JUST GET OUT                    86294
         TM    OUTFG,FGW     WYLBUR BUFFER ?
         BNZ   PUTWRITW      YES
         TM    OUTFG,FGV     BDW REQUIRED ?
         BNZ   PUTWRITV      YES
         TM    OUTFG,FGD     ASCII BDW ?
         BZ    PUTWRITC      NO
         CVD   R3,DB         MAKE PACKED BLOCK LENGTH
         UNPK  DB2,DB        UNPACK
         OI    DB2+L'DB2-1,X'F0'  FORCE ZONE
         MVC   0(4,R2),DB2+4   COPY
         B     PUTWRITC      GO TO COMMON
PUTWRITV STCM  R3,3,0(R2)    SET BLOCK SIZE
         B     PUTWRITC      GO TO COMMON
PUTWRITW STCM  R3,3,0(R2)    SET BLOCK SIZE
*        SET NIH FORMAT BIT IF NEEDED
PUTWRITC STH   R3,DCBBLKSI+SYSUT2-IHADCB  SET RECFM=F BLOCKSIZE
         CH    R3,OUTBLKSZ   VALID SIZE ?                        86289
         BH    UT2BIG        NO; FAIL                            86289
         INC   NUMBLOCK                                          90182
         LA    R4,SYSUT2                                         91022
         WRITE MYDECB,SF,(R4),(R2),(R3),MF=E  WRITE A BLOCK      91022
         CHECK MYDECB        WAIT FOR COMPLETION                 91022
         L     R1,DCBNOTE-IHADCB+SYSUT2  NOTE ENABLED ?          91025
         C     R1,DCF1       NOTE SET ?                          91025
         BNH   PUTNONOT      NO                                  91025
         NOTE  SYSUT2        GET THE ADDRESS
PUTNONOT ST    R1,CURTTR     STASH IT
         TM    OUTFG,FG1     FIRST TIME ?
         BNZ   PUTWRITX      NO
         TM    OUTFG,FGPO    DOING A PDS ?                       86288
         BZ    PUTWNPDS      NO                                  86288
         LR    R4,R1         SAVE CURRENT TTR                    86288
         PRTLIST MSGMEM      PRINT MEMBER INFO                   86288
         LR    R1,R4         RESTORE TTR                         86288
PUTWNPDS OI    OUTFG,FG1     SET FIRST TIME
         STCM  R1,14,PDS2TTRP  STASH FIRST TTR
PUTWRITX XC    WCMBUFLN,WCMBUFLN  CLEAR CURRENT BUFFER LENGTH
         XC    0(4,R2),0(R2)   CLEAR BUFFER PREFIX
         BR    R9            RETURN TO CALLER
         SPACE 1                                                 86289
UT2BIG   PRTF  '*** BLOCK LARGER THAN BLOCKSIZE ***',CC=NO       86289
         SERVTERM ,          CLOSE                               86289
         ABEND 001,DUMP      ABEND                               86289
         SPACE 1
         PUSH  USING
         DROP  R8
         USING IHADCB,R2     CALLER SUPPLIED
CLOSER   TM    DCBOFLGS,DCBOFOPN  OPENED ?
         BZR   R9            NO; RETURN
         CLOSE ((R2),DISP),MF=(E,@SYSUT1)  CLOSE IN OR OUT       91022
         ICM   R3,7,DCBBUFCA TEST THE BUFFER POOL ADDRESS
         BZR   R9            NONE; RETURN
         TM    DCBBUFCA+L'DCBBUFCA-1,1  DUMMIED OUT ?
         BNZR  R9            YES; IGNORE
         FREEPOOL (R2)       ELSE FREE IT
         LA    R0,1
         STCM  R0,7,DCBBUFCA
         BR    R9            RETURN
         POP   USING
         SPACE 1
         PUSH  USING
         DROP  ,
         USING INDCBEX,R15   POST-DCBEXIT EXIT
         USING IHADCB,R1
INDCBEX  LA    R2,OPTFLAG-SYSUT1(,R1)                            91022
         USING OPTFLAG,R2    ADDRESS WORK AREA                   91022
         MVC   INPRECFM,DCBRECFM  SAVE TRUE RECFM
         TM    INPRECFM,JFCUND  ANY RECFM OF U, V OR F ?         86294
         BZ    *+8           NO                                  86294
         NI    INPRECFM,255-DCBRECTO  KILL TRACK OVERFLOW        86294
         MVC   INPLRECL,DCBLRECL    AND RECORD LENGTH
         MVC   INPBLKSI,DCBBLKSI  SAVE BLOCK SIZE                86294
         MVI   DCBRECFM,JFCUND  MAKE UNDEFINED
         MVI   DCBBUFNO,1    WANT ONLY ONE BUFFER
JUSTRET  BR    R14           RETURN
         POP   USING
         SPACE 1                                                 86288
         PUSH  USING                                             86288
         USING IHADCB,R3     PASSED BY CALLER                    86288
*        ALSO PASSED :  R2 =>  ' IN' OR 'OUT'                    86288
FORMDSN  ST@   R3,DB,MVI=X'80'  BUILD OPEN LIST                  91023
         RDJFCB MF=(E,DB)    UPDATE THE JFCB                     91023
         SERVCALL DSDJ1,INJFCB  TRY FOR FORMAT 1 DSCB            86289
         BXH   R15,R15,FORMDSNT  SKIP IF NONE                    86289
         MVC   DS1FMTID(DS1END-DS1FMTID),0(R1)  SAVE IT          86289
         MVC   JFCDSORG(DS1LRECL-DS1DSORG+L'DS1LRECL),DS1DSORG   86289
         B     FORMDSNZ                                          90182
FORMDSNT MVI   JFCDSORG,JFCORGPS                                 90182
         MVC   JFCRECFM,DCBRECFM-IHADCB(R3)                      90182
         MVC   JFCLRECL,DCBLRECL-IHADCB(R3)                      90182
         MVC   JFCBLKSI,DCBBLKSI-IHADCB(R3)                      90182
         CLI   1(R2),C'I'    INPUT DCB ?                         90182
         BNE   FORMDSNZ      NO                                  90182
         MVC   JFCRECFM,INPRECFM  SAVE TRUE RECFM                90182
         MVC   JFCLRECL,INPLRECL                                 90182
FORMDSNZ SERVCALL DSFMT,JFCDSORG                                 86288
         MVC   DSORG(LRECL-DSORG+L'LRECL),0(R1)   MOVE RESPONSE  86288
         CLI   RECFM,C'U'    UNDEFINED ?                         93185
         BNE   FORMDSNY      NO                                  93185
         LA    R1,INFG       POINT TO INPUT FLAGS                93185
         CLI   1(R2),C'I'    IS IT INPUT ?                       93185
         BE    *+8           YES                                 93185
         LA    R1,OUTFG      ELSE OUTPUT                         93185
         TM    0(R1),FGW     WYLBUR FORMAT ?                     93185
         BZ    FORMDSNY      NO                                  93185
         MVI   RECFM,C'W'    FLAG IT                             93185
FORMDSNY PRTLIST MSGFORM                                         86288
         BR    R9            RETURN TO CALLER                    86288
MSGFORM  FDPRT 0(R2),3,NL    ' IN' OR 'OUT'                      86288
         FDPRT 'PUT',PADR                                        86288
         FDPRT JFCBDSNM,DEB,PAD                                  86288
         FDPRT JFCBVOLS,6,PAD                                    86288
         FDPRT DSORG,DEB,PAD,LEN=3                               86288
         FDPRT RECFM,DEB,PAD,LEN=6                               86288
         FDPRT LRECL,DEB,PAD                                     86288
         FDPRT BLKSIZE,DEB,PAD                                   86288
         FDPRT *END                                              86288
         POP   USING                                             86288
         EJECT ,                                                 90182
*        COPY USER HEADER AND TRAILER LABELS (IF SUL/AUL).       90182
*        NOTE THAT END-OF-VOLUME LABELS ARE NOT HANDLED.         90182
         PUSH  USING                                             90182
         USING XINPLABS,R12  INPUT LABEL ROUTINE                 90182
XINPUHDS LA    R10,NUMUHDS   POINT TO INPUT LABEL FIELD          90182
         B     XINPLABB-XINPUHDS(,R15)  GO TO COMMON             90182
XINPUTLS LA    R10,NUMUTLS   POINT TO INPUT TRAILERS             90182
         SLR   R15,R15       NO, THANKS!                         90182
*TAPE*   TM    4(R1),X'80'   END-OF-FILE ?                       90182
*TAPE*   BZR   R14           NO; IGNORE END-OF-VOLUME            90182
XINPLABB BALR  R12,0         SET LOCAL BASE                      90182
XINPLABS SLR   R15,R15       QUIT THIS GROUP                     90182
         LM    R3,R6,0(R1)   GET PARAMETERS                      90182
         LA    R3,0(,R3)     CLEAN BUFFER ADDRESS                90182
         LTR   R3,R3         ANY ?                               90182
         BZR   R14           NO; SKIP THIS GROUP                 90182
         LA    R15,4         CONTINUE PROCESSING                 90182
         USING LABSECT,R10   DECLARE LABEL AREA                  90182
         L     R7,NUMLABS    GET CURRENT LABEL COUNT             90182
         LR    R8,R7         COPY                                90182
         MH    R8,=H'80'     CONVERT TO OFFSET                   90182
         LA    R8,USERLABS(R8)  POINT TO IT                      90182
         LA    R7,1(,R7)     GET NEW LABEL COUNT                 90182
         ST    R7,NUMLABM    STASH MAXIMUM                       90182
         CH    R7,=H'8'      LEGAL ?                             90182
         BHR   R14           NO; JUST RETURN                     90182
         ST    R7,NUMLABS    SAVE                                90182
         MVC   0(80,R8),0(R3)  SAVE THIS LABEL                   90182
         BR    R14                                               90182
         POP   USING                                             90182
         SPACE 2                                                 90182
         PUSH  USING                                             90182
         USING XOUTLABS,R12                                      90182
XOUTUHDS LA    R10,NUMUHDS   POINT TO INPUT LABEL FIELD          90182
         B     XOUTLABB-XOUTUHDS(,R15)  GO TO COMMON             90182
XOUTUTLS LA    R10,NUMUTLS   POINT TO INPUT TRAILERS             90182
XOUTLABB BALR  R12,0         SET LOCAL BASE                      90182
XOUTLABS SLR   R15,R15       QUIT THIS GROUP                     90182
         LM    R3,R6,0(R1)   GET PARAMETERS                      90182
         SLR   R15,R15       I'M SICK OF THIS                    90182
         LA    R3,0(,R3)     CLEAN BUFFER ADDRESS                90182
         LTR   R3,R3         CHECK IT                            90182
         BZR   R14           NONE; QUIT                          90182
*TAPE*   TM    4(R1),X'80'   END-OF-FILE ?                       90182
*TAPE*   BZR   R14           NO; NO END-OF-VOLUME PROCESSING     90182
         USING LABSECT,R10   DECLARE LABEL AREA                  90182
         ICM   R7,15,NUMLABS ARE THERE ANY LABELS TO DO ?        90182
         BZR   R14           NO; SO JUST GET OUT                 90182
         LA    R8,8          SET LABEL MAX                       90182
         MIN   R8,NUMLABM    BUT NOT TOO LARGE                   90182
         SR    R8,R7         GET INDEX OF NEXT                   90182
         MH    R8,=H'80'     GET OFFSET                          90182
         LA    R8,USERLABS(R8)  POINT TO CORRECT LABEL           90182
         MVC   0(80,R3),0(R8)  MOVE LABEL TO OUTPUT AREA         90182
         BCT   R7,XOUTLABM   GET NEW LABEL COUNT                 90182
         L     R1,NUMLABM                                        90182
         MIN   R1,=F'8'      TRUNCATE                            90182
         ST    R1,DB         SAVE                                90182
         LR    R9,R14        SAVE RETURN ADDRESS                 90182
         PRTLIST MSGLABS     PRINT LABELS DONE                   90182
         LR    R14,R9        RESTORE RETURN ADDRESS              90182
XOUTLABM ST    R7,NUMLABS    SET RESIDUAL COUNT                  90182
         LA    R15,8         WRITE LABEL AND RETURN              90182
         BR    R14                                               90182
         SPACE 1                                                 90182
MSGLABS  FDPRT DB,4,I,RADJ,NL,LEN=15                             90182
         FDCLC DB,NUMLABM,4,BE=MSGLABQ                           90182
         FDPRT 'OF',PAD                                          90182
         FDPRT NUMLABM,I,PAD                                     90182
MSGLABQ  FDPRT USERLABS,3,PAD                                    90182
         FDCLC NUMLABM,DCF1,BE=MSGLAB1                           90182
         FDPRT 'LABELS WRITTEN'                                  90182
         FDPRT *END                                              90182
MSGLAB1  FDPRT 'LABEL WRITTEN'                                   90182
         FDPRT *END                                              90182
         POP   USING                                             90182
         EJECT ,
         SPACE 1
SYSSIZE  DC    A(40*1024)    SIZE TO RESERVE FOR SYSTEM DUMPS, ETC.
MAINTAB  DC    A((40+8)*1024,1024*1024)  MIN / MAX STORAGE
DCF1     DC    F'1'                                              90182
PATWORK  INPWORK SYSIN,WIDTH=80,EDIT=128,EODAD=SYNEOF,FILL=C' '
         PRTWORK SYSPRINT,TITLE=3
PATWORKL EQU   *-PATWORK                                         91022
PATDCB   DS    0D
PATDCBPS DCB   DDNAME=PS,DSORG=PS,DEVD=DA,MACRF=(R,W),                 *
               EODAD=UT1EOF,EXLST=1,BUFNO=1
PATDCBPO DCB   DDNAME=PO,DSORG=PO,DEVD=DA,MACRF=(R,W),                 *
               EODAD=UT1EOF,EXLST=1,BUFNO=1
PATDCBLN EQU   *-PATDCBPO
         SPACE 1
PATEXIT  DC    X'00',AL3(UT2OPTS-UT2OPTS)    OUTPUT OPTIONS      91022
         DC    X'02',AL3(XOUTUHDS)  OUTPUT HEADER LABELS         90182
         DC    X'04',AL3(XOUTUTLS)  OUTPUT TRAILER LABELS        90182
         DC    X'00',AL3(UT1OPTS-UT1OPTS)    INPUT OPTIONS       91022
         DC    X'01',AL3(XINPUHDS)  INPUT HEADER LABELS          90182
         DC    X'03',AL3(XINPUTLS)  INPUT TRAILER LABELS         90182
*FAILS*  DC    X'0C',AL3(0)  DEFER TRAILER PROCESSING UNTIL CLOSE
         DC    0A(0),X'07',AL3(INFMJFCB-INFMJFCB)  OUTPUT JFCB
         DC    X'85',AL3(0)  ADDRESS OF @DCBEXIT
PATEXITL EQU   *-PATEXIT                                         91022
PAT1OPTS DCBEXITD PREFIX=P1,FLAG1=(U11NOPCI+U11NOSD)             91022
PAT2OPTS DCBEXITD PREFIX=P2,                                           *
               FLAG1=(U21NOFBS+U21NOPCI+U21NOSD+U21TRUNC),             *
               FLAG2=(U22TARER),                                       *
               FLAG3=(U23BLKTB+U23BLKWY+U23DFWYL)                86288
PATOPTSL EQU   *-PAT1OPTS    LENGTH TO MOVE                      91022
         SPACE 1                                                 91022
PATCOMP  SERVCOMP DSECT=NO,PFX=PWC,RECMX=&MAXREC,FG1=&NIH*WCMF1NIH
         SPACE 1
HEADER   FDOPT NL,CC=C'#'    AUTOMATIC TITLE, PAGE NUMBER
         FDPRT 'WYLCOPY  ***  WYLBUR DATASET AND PDS COPY'
         FDPRT *END
         SPACE 1
MSGBADU1 FDPRT '*** UNABLE TO OPEN DCB FOR',NL
         FDPRT SYSUT2+DCBDDNAM-IHADCB,L'DCBDDNAM,PAD,DEB
         FDPRT '***'
         FDPRT *END
         SPACE 1
MSGBDMIX FDPRT '*** INPUT AND OUTPUT DCB OR DSORG PARAMETERS INCOMPATIB*
               LE ***',NL
         FDPRT *END
         SPACE 1
MSGSTOWR FDPRT '*** STOW ERROR FOR MEMBER',NL
         FDPRT MEMBER,PAD,DEB
         FDPRT '***'
         FDPRT *END
MSGALSTO FDPRT '*** STOW ERROR FOR ALIAS',NL
         FDPRT ALIAS,PAD,DEB
         FDPRT 'OF MEMBER'
         FDPRT MEMBER,PAD,DEB
         FDPRT '***'
         FDPRT *END
         SPACE 1
TRTDIGIT DC    256AL1(4)     SET NUM-NUMERIC                    GP99215
         TRENT TRTDIGIT,0,C' '  WYLBUR SEQUENCE 'NUMBER'        GP99215
         TRENT TRTDIGIT,0,C'.'  WYLBUR SEQUENCE 'NUMBER'        GP99215
         TRENT TRTDIGIT,0,(C'0',10)                             GP99215
         ORG   ,             JUST IN CASE                       GP99215
         SPACE 1                                                GP99215
ALIISECT DSECT ,             DECLARE AN ALIAS LIST ENTRY
ALIAS    DS    CL8           ALIAS NAME
ALIITTR  DS    XL3           MEMBER TTR
ALIIFLAG DS    X             FLAG
ALIIDTA  DS    31XL2         USER DATA
         SPACE 1
SAVE     DSECT ,             SAVE/WORK AREA
PUTSAVE  DS    10A           PUT SAVE AREA
DB       DS    D
DB2      DS    D
SYSIN    INPWORK SYSIN,WIDTH=80,EDIT=128,EODAD=SYNEOF,FILL=C' '
SYSPRINT PRTWORK SYSPRINT,TITLE=3
         SERVDEFS ,          EXPAND SERVICE ROUTINE FIELDS      GP13234
DDNAMLST DC    4A(0)         1/3                                 91022
DDNAMSYN DC    A(SYSIN,SYSPRINT,0)     2/3                       91022
DDNAMUT1 DC    A(SYSUT1,SYSUT2)        3/3  DDNAME ADDRESSES     91022
@SYSUT1  DC    X'80',AL3(SYSUT1)                                 91022
@SYSUT2  DC    X'8F',AL3(SYSUT2)                                 91022
MEMLIST  DC    A(0,LISTLEN)  BXLE 1/2
MEMCURR  DC    A(0)          BXLE 2/2  LAST USED ENTRY
ALILIST  DC    A(0,LISTLEN)  BXLE 1/2
ALILAST  DC    A(0)          BXLE 2/2  TOP OF GOTTEN STORAGE
NUMSPACE DC    F'0'          MAXIMUM STORABLE ENTRIES
NUMMEM   DC    F'0'          NUMBER OF MEMBERS READ
NUMALI   DC    F'0'          NUMBER OF ALIAS ENTRIES READ
NUMRECS  DC    F'0'          NUMBER OF RECORDS WRITTEN (MEMBER)  90182
NUMTRUNC DC    F'0'          NUMBER OF RECORDS TRUNCATED         91022
NUMBLOCK DC    F'0'          NUMBER OF BLOCKS WRITTEN            90182
GETMEAN  GETMAIN VC,LA=1,A=3,MF=L  STORAGE PATTERN               91022
SYSUT1   DS    0D,(PATDCBLN)X'00'
         ORG   SYSUT1+40
         DC    CL8'SYSUT1  '
         ORG   ,
SYSUT2   DS    0D,(PATDCBLN)X'00'
         ORG   SYSUT2+40
         DC    CL8'SYSUT2  '
         ORG   ,
OPTFLAG  DC    X'00'         OPTION FLAGS                        86294
FGBUG    EQU   X'80'           DEBUG FLAG                        86294
FGBLK    EQU   X'40'           KEEP ORIGINAL BLOCKSIZE           91013
FGCCZERO EQU   X'20'           RETURN CC=0 ON EMPTY/UNINITIALIZED DS
FGSTRIP  EQU   X'10'           REMOVE TRAILING BLANKS/NULLS      93185
FGUNSEQ  EQU   X'08'           REMOVE SEQUENCE NUMBERS (73-80)
FGXCHG   EQU   X'04'           SWAP SEQUENCE COL 1-8 TO 73-80   GP13234
FGACRLF  EQU   X'02'           APPEND CR/LF TO EACH RECORD       93185
INPRECFM DC    X'00'         SAVED INPUT RECFM
INPLRECL DC    H'0'          INPUT RECORD LENGTH
INPBLKSI DC    H'0'          INPUT BLOCK SIZE                    86294
OUTBLKSZ DC    H'0'          MAXIMUM OUTPUT SIZE                 86289
PADREC   DC    H'0'          TRAILING PADDING                    93185
OUTEXIT  DC    0A(0),X'00',AL3(UT2OPTS)      OUTPUT OPTIONS
         DC    X'02',AL3(XOUTUHDS)  OUTPUT HEADER LABELS         90182
         DC    X'04',AL3(XOUTUTLS)  OUTPUT TRAILER LABELS        90182
INEXIT   DC    X'00',AL3(UT1OPTS)      INPUT OPTIONS
         DC    X'01',AL3(XINPUHDS)  INPUT HEADER LABELS          90182
         DC    X'03',AL3(XINPUTLS)  INPUT TRAILER LABELS         90182
*FAILS*  DC    X'0C',AL3(0)  DEFER TRAILER PROCESSING UNTIL CLOSE
OUTJFCB  DC    0A(0),X'07',AL3(INFMJFCB-INFMJFCB)  OUTPUT JFCB
@DCBEXIT DC    X'85',AL3(0)  ADDRESS OF @DCBEXIT
         WRITE MYDECB,SF,1,2,3,MF=L   READ/WRITE DECB            91022
UT1OPTS  DCBEXITD PREFIX=U1,FLAG1=(U11NOPCI+U11NOSD)
         PRINT GEN
UT2OPTS  DCBEXITD PREFIX=U2,                                           *
               FLAG1=(U21NOFBS+U21NOPCI+U21NOSD+U21TRUNC),             *
               FLAG2=(U22TARER),                                       *
               FLAG3=(U23BLKTB+U23BLKWY+U23DFWYL)                86288
         PRINT &PRTMAC                                           91022
WIMPLIST SERVCOMP DSECT=NO,PFX=PCM  PARM MODIFIED PATTERN        91022
WINPLIST SERVCOMP DSECT=NO,PFX=INP   INPUT DECOMPRESS
WINPLEN  EQU   *-WINPLIST                                        91022
         PRINT GEN
COMPLIST SERVCOMP DSECT=NO
COMPLEN  EQU   *-COMPLIST                                        91022
         SPACE 1
SORTPARM SERVSORT DSECT=NO
         SPACE 1
         SERVPDS DSECT=NO,RETURN=YES
         PRINT &PRTSYS
CURTTR   DS    F             TTR OF CURRENT BLOCK
OUTFG    DS    X             OUTPUT FLAG
FGF      EQU   X'80'           RECFM=F OUTPUT
FGV      EQU   X'40'           RECFM=V OUTPUT
FGD      EQU   X'20'           RECFM=D OUTPUT
FGU      EQU   X'10'           RECFM=U OUTPUT
FGW      EQU   X'08'           RECFM=U/WYLBUR FORMAT
FGPO     EQU   X'02'           OUTPUT IS A PDS
FG1      EQU   X'01'           FIRST WRITE DONE
INFG     DC    X'00'         INPUT FLAG
OLDTTR   DS    XL3           OLD MEMBER TTR
DSORG    DS    CL3      1/5  DSFMT RETURN AREA                   86288
RECFM    DS    CL6      2/5                                      86288
OPTCD    DS    CL8      3/5                                      86288
BLKSIZE  DS    CL5      4/5                                      86288
LRECL    DS    CL5      5/5                                      86288
MEMBER   DS    0D,XL(8+4+2*31)  MAXIMUM DIRECTORY ENTRY
         ORG   MEMBER
         PRINT GEN
         IHAPDS PDSBLDL=NO,DSECT=NO
         MAPPDS PDSBLDL=NO   DEFINE LOCAL USER DATA
         ORG   ,
LISTLEN  EQU   *-MEMBER      DEFINE MAXIMUM LENGTH OF ONE ENTRY
         PRINT &PRTSYS
         SPACE 1
INJFCB   DS    0D
         IEFJFCBN ,          DEFINE A JFCB
         SPACE 1
         IECSDSL1 1          MAP THE FORMAT 1 DSCB
         SPACE 1                                                 90182
*        HEADER/TRAILER LABEL SAVE AREA                          90182
NUMUHDS  DC    F'0'          NUMBER OF USER HEADERS              90182
NUMUHDM  DC    F'0'          MAXIMUM NUMBER IN THIS GROUP        90182
TEMPCARD DC    CL80' '       CARD IMAGE WORK AREAS FOR SWAP     GP13234
USERUHDS DC    8CL80' '      HEADER AREA                         90182
PARMLINE EQU   USERUHDS,110,C'C'  COPY OF PARM DATA              91022
NUMUTLS  DC    F'0'          NUMBER OF USER TRAILERS             90182
NUMUTLM  DC    F'0'          MAXIMUM NUMBER IN THIS GROUP        90182
USERUTLS DC    8CL80' '      TRAILER AREA                        90182
SAVEEND  EQU   *                                                GP13234
         SPACE 1                                                 90182
LABSECT  DSECT ,                                                 90182
NUMLABS  DC    F'0'          NUMBER OF LABELS                    90182
NUMLABM  DC    F'0'          MAXIMUM NUMBER IN THIS GROUP        90182
USERLABS DC    8CL80' '      LABEL AREA                          90182
         SPACE 1
CVTDSECT DSECT ,
         CVT   DSECT=YES
         IHACDE ,
         DCBD  DEVD=DA,DSORG=PS
         IEFUCBOB ,
         END   ,
