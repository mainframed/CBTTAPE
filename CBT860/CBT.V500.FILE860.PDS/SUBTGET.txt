SUBTGET  TITLE 'S U B T G E T  ***  SIMPLE SIMPLE TGET REPLACEMENT'
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
         SPACE 2
***********************************************************************
**                                                                   **
**   SUBTGET HANDLES REQUESTS GENERATED BY THE #TGET MACRO           **
**     R1 - MESSAGE ADDRESS                                          **
**     R0 BYTE 3 - MESSAGE LENGTH                                    **
**     R0 BYTE 0-2 - FLAGS                                           **
**                                                                   **
**   THE TGET REQUEST IS CHANGED TO GETLINE (FOR TRAPPING, ETC.)     **
**     IF THE REQUEST FAILS, IT IS ISSUED AS A WTOR                  **
**                                                                   **
**   GETLINE RETURN CODES ARE REMAPPED TO TGET VALUES, EXCEPT THAT   **
**     END OF INPUT RETURNS 32, AND THERE IS NO VALUE FOR INPUT TOO  **
**     LONG. ALSO NOEDIT IS NOT HONORED NOR INDICATED.               **
**                                                                   **
**   CODE ADAPTED FOMR SUBTPUT.               FIRST VERSION 2012.313 **
***********************************************************************
         SPACE 2
***********************************************************************
*                                                                     *
*                                                                     *
*        COPYRIGHT 2012  EXPERT SYSTEM PROGRAMMING                    *
*                        176 OLD STAGE COACH ROAD                     *
*                        BRADFORD, VT 05033-8844                      *
*                                                                     *
*                    ALL RIGHTS RESERVED                              *
*                                                                     *
***********************************************************************
         PRINT &PRTSOR
SUBTGET  PGMHEAD ZERO12,BASE=R12,PARM=R1,BNDRY=PAGE
         MVC   DYNGET(PATGETSZ),PATGET  MOVE GETLINE PATTERN
         LR    R4,R1         PRESERVE MESSAGE ADDRESS
         LR    R5,R0         AND MESSAGE FLAGS/LENGTH
         ST    R5,OPTFLAGS   SAVE
         N     R5,=X'000000FF'  ISOLATE LENGTH
         SPACE 1
HAVELEN  LTR   R5,R5         IS THE LENGTH VALID ?
         BNP   BADLEN        NO
*---------------------------------------------------------------------*
*    1) CHECK FOR TSO - USE WTOR IF NOT                               *
*    2) GET THE UPT AND ECT ADDRESSES                                 *
*---------------------------------------------------------------------*
         LTCB  R7,USE=YES
         ICM   R6,15,TCBJSCB  LOOK FOR THE JSCB
         BZ    USEWTOR       HUH ?
         USING IEZJSCB,R6
         ICM   R6,15,JSCBPSCB  PSCB PRESENT ?
         BZ    USEWTOR       NO; NOT TSO
         DROP  R6
         L     R7,TCBFSA     GET FIRST SAVE AREA
         L     R7,SAVE1-SAVE(,R7)   LOAD INVOCATION R1
         USING CPPL,R7       DECLARE IT
         L     R6,CPPLECT
         L     R7,CPPLUPT
         DROP  R7
         XC    DYNGET(8),DYNGET   CLEAR
         MVI   DYNGET+2,X'80'
         GETLINE PARM=DYNGET,ECT=(R6),UPT=(R7),ECB=DYNECB,             *
               MF=(E,DYNIOPL)
         SRL   R15,2              CONVERT CODE TO INDEX
         IC    R15,CONCODE(R15)   CONVERT GETLINE TO TGET CODES
         STM   R15,R0,RETCODE
         XC    RETCODE+8(4),RETCODE+8  ZERO INPUT LENGTH READ
         LTR   R6,R15             COPY RETURN CODE
         BH    PGMEXIT            ATTENTION INTERRUPT OR WORSE
         L     R1,DYNGET+4        GET INPUT LINE ADDRESS
*---------------------------------------------------------------------*
*   MVS 3.8 undocumented behavior: at end of input in batch execution,
*   returns text of 'END' instead of return code 16. Needs DOC fix
*---------------------------------------------------------------------*
         CLC   =X'00070000C5D5C4',0(R1)  Undocumented EOF?
         BE    READEOD2           YES; QUIT
         LR    R8,R1              INPUT LINE W/RDW
         LH    R9,0(,R1)          GET LENGTH
         LA    R8,4(,R8)          SKIP RDW
         SH    R9,=H'4'           SKIP RDW
         BNM   *+4+2
         SR    R9,R9              SHOULD NOT HAPPEN ?
         ST    R9,RETCODE+8       RETURN TO USER
         ICM   R9,8,=C' '         BLANK FILL
         MVCL  R4,R8              SAVE INPUT
         LH    R0,0(,R1)          GET LENGTH WITH RDW
         ICM   R0,8,=AL1(1)       SUBPOOL 1
         FREEMAIN R,LV=(0),A=(1)  FREE SYSTEM BUFFER
         B     PGMEXIT
CONCODE  DC    AL1(0)     0  GOOD COMPLETION
         DC    AL1(0)     4  GOOD COMPLETION (READ NON-TERMINAL)
         DC    AL1(8)     8  ATTENTION INTERRUPT
         DC    AL1(4)    12  NOWAIT; NO INPUT READY
         DC    AL1(32)   16  UNASSIGNED TGET CODE - END INPUT
         DC    AL1(16)   20  PROGRAM ERROR
         DC    AL1(32)   24  NO STORAGE
         DC    AL1(20)   28  DISCONNECT
         DC    AL1(32)   32    RESERVED
         DC    AL1(32)   36    RESERVED
         DC    AL1(32)   40    RESERED
         SPACE 1
*---------------------------------------------------------------------*
*    NOT TSO ENVIRONMENT - ISSUE WTOR                                 *
*---------------------------------------------------------------------*
USEWTOR  TM    OPTFLAG1,OFASKIP  WHAT TO DO?
         BO    PGMEXIT       NOTHING
         BM    USETEST       TEST FOR WTL VS. PRT
         XC    DYNECB,DYNECB
         WTOR  'Calc:',(R4),(R5),DYNECB,MF=(E,DYNWTOR)
         WAIT  ECB=DYNECB
         ST    R5,RETCODE+8
         STM   R15,R0,RETCODE
         B     PGMEXIT
         SPACE 1
USETEST  LA    R15,8         SET ERROR
         LA    R0,666        SET ERROR
         STM   R15,R0,RETCODE
         B     PGMEXIT
         SPACE 1
*---------------------------------------------------------------------*
*    FINISHED - EXIT WITH RETURN AND REASON CODES SET                 *
*---------------------------------------------------------------------*
READEOD2 LA    R15,32        SET EOF CODE
         ST    R15,RETCODE
         SPACE 1
PGMEXIT  PGMEXIT COPYRET=(RETCODE,12)  RETURN R15, R0 AND R1
         SPACE 1
BADLEN   MVICC 8,1           SET LENGTH BAD
         B     PGMEXIT
         SPACE 2
*---------------------------------------------------------------------*
*   PATTERNS                                                          *
*---------------------------------------------------------------------*
PATGET   GETLINE MF=L        PATTERN
PATGETSZ EQU   *-PATGET      SIZE OF IOPL
PATIOPL  IKJIOPL ,
PATIOPSZ EQU   *-IOPL        SIZE OF IOPL
         SPACE 1
         PRINT &PRTMAC
*---------------------------------------------------------------------*
*   DYNAMIC SAVE AND WORK AREA                                        *
*---------------------------------------------------------------------*
SAVE     DSECT ,
DB       DS    D
         SERVDEFS ,          SERVICE AREA
@PRINTER DS    A
DYNWTOR  WTOR  'Calc:',*-*,*-*,*-*,MF=L
DYNECB   DS    A
DYNIOPL  DS    XL(PATIOPSZ)  RESERVED SPACE FOR IOPL
DYNGET   GETLINE MF=L        SPACE FOR PUTLINE RQUEST
         SPACE 1
OPTFLAGS DS    0XL4  A
OPTFLAG1 DS    X     |1/4    ACTION WHEN NOT TSO ENVIRONMENT
OFAWTO   EQU   0     |         ISSUE WTP
OFAWTL   EQU   1     |         ISSUE WTL
OFAPRT   EQU   2     |         WRITE TO SYSPRINT
OFASKIP  EQU   3     |         SKIP WTO
         SPACE 1     |-KEEP TOGETHER
OPTFLAG2 DS    X     |2/4
         SPACE 1     |
OPTFLAG3 DS    X     |3/4
         SPACE 1     |
OPTLEN   DS    X     V4/4
         SPACE 1
*ESA*    CSVQUERY MF=(L,QUERY)
         MVSQUERY MF=(L,QUERY)
SAVEEND  EQU   *
         SPACE 1
         PRINT &PRTSYS
         IKJPTPB ,           PUTLINE PARAMETER BLOCK
         IKJCPPL ,
         IKJPSCB ,
         IEZJSCB ,
         IKJTCB  ,
         IHAPSA  ,
         END   ,
