MODR     TITLE 'M O D R E P  ***  TEST LPA/SVC MODULES'          88241
*   FROM AN OLD CONNECTICUT BANK & TRUST MODS TAPE - FILE 235         *
         MACRO ,
&NM      RENTDEF &PFX=       DEFINE REENTRANT DATA AND PATTERN  GP06292
         GBLB  &MVSXA                                           GP06292
         LCLC  &P            SHORT PREFIX                       GP06292
&P       SETC  '&PFX'                                           GP06292
&P.RENT  DS    0D            START OF RE-ENTRANT DEFINITIONS    GP06292
&P.OLIST OPEN  (&P.DCB,(INPUT,REREAD)),MF=L                     GP06292
&P.DCB   DCB   DDNAME=MODREP,DSORG=PO,MACRF=(R)                 GP06292
&P.ENQ   ENQ   (ENQMAJ,ENQMIN,E,L'ENQMIN,SYSTEM),RET=HAVE,MF=L  GP06292
&P.MSG   VCON  'MODREP:',END=&P.MSGX                            GP06292
&P.MSGNM DC    CL8' '        MODULE NAME                        GP06292
&P.MSGAL DC    C' ',C' EP:'  ALIAS FLAG                         GP06292
&P.MSGEP DC    CL8' ',C' AD:'  ENTRY                            GP06292
&P.MSGAD DC    CL8' ',C' LN:'  LOAD ADDRESS                     GP06292
&P.MSGLN DC    CL8' '        LENGTH                             GP06292
         VCON  *END                                             GP06292
         ORG   &P.MSG+2                                         GP06292
         DC    X'8000'       ROUT/DESC PRESENT                  GP06292
         ORG   ,                                                GP06292
         DC    X'08004000'   ROUTCDE=2,DESC=5                   GP06292
&P.WTO   VCON  'MODREP:',END=&P.WTOX                            GP06292
&P.WTONM DC    CL8' ',C' '   MODULE NAME                        GP06292
&P.WTOTX DC    CL30' '       VARIALBE TEXT                      GP06292
         VCON  *END                                             GP06292
         ORG   &P.WTO+2                                         GP06292
         DC    X'8000'       ROUT/DESC PRESENT                  GP06292
         ORG   ,                                                GP06292
         DC    X'08004000'   ROUTCDE=2,DESC=5                   GP06292
         AIF   (NOT &MVSXA).MEND                                GP06292
&P.SVCUP SVCUPDTE 123,REPLACE,TYPE=3,EP=123456,LOCKS=(LOCAL),MF=L
.MEND    ANOP  ,                                                GP06292
&P.SIZE  EQU   *-&P.RENT     SIZE TO BE MOVED                   GP06292
         MEND  ,                                                GP06292
         SPACE 1
         MACRO ,                                                GP06292
&NM      WTOPR &TEXT,&ROUTCDE=,&DESC=                           GP06292
         LCLA  &K                                               GP06292
&K       SETA  K'&TEXT-2                                        GP06292
&NM      MVC   WRKWTOTX,BLANKS  CLEAR                           GP06292
         MVC   WRKWTOTX(&K),=C&TEXT    MOVE TEXT                GP06292
         WTO   MF=(E,WRKWTO)                                    GP06292
         #PRT  WRKWTO,CC=NO                                     GP06292
         MEND  ,                                                GP06292
         SPACE 1
         PUNCH ' ORDER MODREP(P) '     EASIER DUMPS             GP06292
         PUNCH '  SETCODE AC(1)  '     NEEDS KEY ZERO           GP06292
         SPACE 1
*********************************************************************
*  MODREP-LPA/SVC REAL TIME LOADER AND REPLACEMENT AND PERFORMANCE  *
*         TOOL                                                      *
*                                                                   *
*  MODULE CONCEPTS:   "REAL TIME" MLPA SIMULATION, SVC REPLACEMENT  *
*                     AND MODULE "FIXING" (FOR PERFORMANCE AND      *
*                     EASE OF IN-CORE ZAP)                          *
*                     THIS MODULE IS PAGEABLE.                      *
*                     LOADED MODULES AND SVCS ARE PLACED IN FIXED   *
*                     CSA MEMORY.                                   *
*                     WHY IN FIXED MEMORY?                          *
*                     1) TYPE 1 AND 2 SVCS MUST BE RESIDENT IN THE  *
*                        NUCLEUS (OR AT LEAST APPEAR TO BE SO)      *
*                     2) AS THIS IS A "TEST TOOL" THE ASSUMPTION    *
*                        IS THAT IN-CORE ZAPS MAY BECOME NECESSARY  *
*                        AND YOU ARE ASSURED THE MODULE WILL BE IN  *
*                        CORE AT ALL TIMES (NOT PAGED OUT)          *
*                                                                   *
*                     3) FOR PERFORMANCE TESTING, A DUPLICATE COPY  *
*                        OF A CURRENTLY PAGEABLE MODULE MAY BE      *
*                        RE-LOADED AND YOU WILL BE ABLE TO EXAMINE  *
*                        THE PERFORMANCE GAINS THAT WOULD RESULT    *
*                        IN THE PERMANENT FIXING OF THE MODULE VIA  *
*                        IEAFIXXX                                   *
*                                                                   *
*        AUTHOR (?) - CHRYSLER PARTS                                *
*              MINOR CHANGES BY GERHARD POSTPISCHIL,                *
*              EXPERT SYSTEM PROGRAMMING, INC.                      *
*        1) EXTRA BASE, MINOR REGISTER RE-ARRANGEMENT.           88241
*        2) ALL ABSOLUTE OFFSETS REPLACED BY MNEMONICS           88241
*        3) SUPPORT FOR UNAUTHORIZED INPUT LIBRARY               88241
*        4) CHECK FOR AVAILABLE SLOT IN TABLE BEFORE LOADING MODULE!
*        5) FIXES FOR COMMAND PROCESSING; HELP SCREEN ADDED.     88241
*        6) ENQ ADDED TO PROTECT CVTFETCH, ETC. FROM CONCURRENT USE
*              OF THIS MODULE                                    88241
*        7) NUMEROUS SPELLING ERRORS FIXED.                      88241
*        8) TIOT MODIFICATION DELETED; CHANGED TO ASCB/TCB/JBN CHECK
*        9) FIXED ASCB/TCB REGISTERS FOR GET/FREEMAIN BRANCH ENTRIES
*       10) ALIAS ENTRIES NOW CAUSE LOADING OF THE MAIN MEMBER  GP06292
*       11) ADDED SUPPORT FOR MODIN/MODPRINT DD CARDS ALLOWING  GP06292
*           UNATTENED USE. MODIN MUST END WITH KEEP TO RETAIN   GP06292
*           MODULES.                                            GP06292
*       12) MINOR CHANGES AND FIXES FOR MVS 3.8J SUPPORT        GP06292
*                                                                   *
*  MEMORY UTILIZATION:                                              *
*  FOR MODULES:       48 BYTES OF SQA (SP 245)-FOR CDE/XTLST        *
*  FOR SVCS:          0 BYTES-SVC TABLE UPDATED IN PLACE            *
*  FOR MODULES/SVCS:  XX BYTES OF CSA (SP 228)-FETCH WILL LOAD AND  *
*                     RELOCATE YOUR MODULE BASED UPON MODULE SIZE   *
*                                                                   *
*  LOCKS OBTAINED:    SALLOC: GETMAIN,FREEMAIN COMMON AREAS         *
*                     DISP:   LOCK ON CDE CHAIN                     *
*                     THE USE OF THE LOCKING MECHANISM WILL NOT     *
*                     AFFECT THE OPERATION OF THIS MODULE IN A U.P. *
*                     ENVIRONMENT (EXCEPT FOR LOCKING OVERHEAD)     *
*                                                                   *
*  LINK ATTR:         AUTH,NORENT,NOREUS                            *
*                     MUST BE IN APF AUTHORIZED LIBRARY             *
*                                                                   *
*  SAMPLE JCL:                                                      *
*                     //MODREP    PROC  LIB='SYS1.MVSLMOD'          *
*                     //MODREP    EXEC  PGM=MODREP,TIME=1440        *
*                     //STEPLIB   DD    DSN=SYS1.MVSLMOD,DISP=SHR   *
*                     //MODREP    DD    DISP=SHR,DSN=&LIB.          *
*                     //SYSUDUMP  DD    SYSOUT=A                    *
*                                                                   *
*                     NOTES:                                        *
*                            TIME PARM-IF THIS PROGRAM IS CANCELLED *
*                            FOR WAIT TIME, MODS BECOME RESIDENT    *
*                            UNTIL THE NEXT IPL, UNLESS OVERRIDDEN  *
*                            BY ANOTHER STARTUP OF THIS PROGRAM.    *
*                            DD MODREP-IS WHERE THE MODULES THAT    *
*                            YOU WILL BE LOADING ON A REAL TIME     *
*                            BASIS EXIST.                           *
*                            DD STEPLIB-LIBRARY MUST BE NAMED IN    *
*                            IEAAPFXX MEMBER OF PARMLIB, ELSE USE   *
*                            A LINKLIST DATASET WITH NO STEPLIB     *
*                            SYSUDUMP IS NOT NECESSARY (AS THIS     *
*                            CODE IS TOTALLY INFALLIBLE)...SORT OF  *
*                            IT IS POSSIBLE TO HAVE AS MANY COPIES  *
*                            OF THIS PROGRAM RUNNING AT ONCE AS IS  *
*                            NECESSARY (IE. 1 PER PDS LIBRARY BEING *
*                            ACCESSED).                             *
*                                                                   *
*  WTOR:               OUTSTANDING WTOR FOR LIFE OF TASK            *
*                      "MODREP READY"                               *
*                                                                   *
*  REPONSES:           "DISPLAY" - DISPLAY ALL MODULES/SVCS         *
*                                  CURRENTLY LOADED AND THEIR EPAS  *
*                      "DI"      - SHORT FORM OF ABOVE              *
*                      "DELETE"  - REMOVE (DELETE) ALL MODULES      *
*                                  LOADED, THEN GO AWAY             *
*                      "DE"      - SHORT FORM OF ABOVE              *
*                      "KEEP"    - LEAVE (KEEP) ALL MODULES LOADED, *
*                                  THEN GO AWAY                     *
*                      "KE"      - SHORT FORM OF ABOVE              *
*                      "XXXXXXXX"- XXXXXXXX IS THE NAME OF THE      *
*                                  MODULE/SVC TO BE LOADED          *
*                                                                   *
*  INFORMATION ON ALIASES:    ----------WARNING----------           *
*                                                                   *
*        ALIAS SHOULD BE LOADED AFTER LOADING THE TRUE NAME VIA     *
*        MODREP. THIS SHOULD BE DONE TO ASSURE THAT THE MEMORY      *
*        USED BY THE ALIAS ENTRY(S) WILL BE FREED WHEN NECESSARY.   *
*        IF YOU FORGET TO LOAD THE TRUE NAME, THE ONLY DAMAGE IS    *
*        THAT THE MEMORY USED CANNOT BE FREED VIA "DE" OR "DELETE"; *
*        NEVERTHELESS, "DE" OR "DELETE" WILL CAUSE THE CESSATION    *
*        OF USE OF ALL ALIASES. BE AWARE THAT IT IS ONLY NECESSARY  *
*        TO LOAD A SINGLE TRUE NAME AND A SINGLE ALIAS FOR THE      *
*        ROUTINE ENTRY POINT THAT YOU WANT TO TEST; IT IS NOT       *
*        NECESSARY TO LOAD EVERY SINGLE ALIAS FOR A MODULE FOR      *
*        MODREP TESTING PURPOSES. ALSO, BE AWARE THAT AN ALIAS      *
*        ONLY COSTS 48 BYTES FOR THE CDE/XTLST, AND NO MEMORY FOR   *
*        FOR THE MODULE, AND THE ENTIRE MODULE IS LOADED UNDER THE  *
*        TRUE NAME.                                                 *
*                                                                   *
*  INFORMATIONAL MESSAGES:                                          *
*                                                                   *
*        MESSAGE FORMAT-                                            *
*        WTO   'MODREP-XXXXX XXXXX XXXXX',ROUTCDE=(2)               *
*               XXXXX REPRESENTS TEXTS FOUND BELOW ENCLOSED IN      *
*               DOUBLE PARENS.                                      *
*                                                                   *
*                      "LPA REP SUCCESSFUL"                         *
*  NOW:  mod-name EP:xxxxxxxx AD:xxxxxxxx LN:xxxxxxxx               *
*                      ---THE REQUESTED MODULE HAS BEEN PLACED IN   *
*                         THE LXA (LINK PACK EXTENSION AREA) AND THE*
*                         ACTIVE LPA CHAIN UPDATED WITH THE NEWLY   *
*                         BUILT CDE/XTLST, SO THAT SUBSEQUENT USES  *
*                         OF THE MODULE WILL BE USING THIS          *
*                         REPLACEMENT COPY                          *
*                                                                   *
*                      "SVC REP SUCCESSFUL"                         *
*                      ---THE REQUESTED MODULE HAS BEEN PLACED IN   *
*                         THE NXA (NUCLEUS EXTENSION AREA) IF A     *
*                         TYPE 1 OR TYPE 2 SVC, OR PLACED IN THE    *
*                         LXA (LINK PACK EXTENSION AREA) IF A TYPE  *
*                         3 OR 4 SVC, AND THE SVC TABLE UPDATED     *
*                         SO THAT SUBSEQUENT USES OF THE SVC WILL BE*
*                         USING THIS REPLACEMENT COPY               *
*                                                                   *
*                      NOTE: THE DETERMINATION OF WHETHER OR NOT    *
*                            YOUR MODULE GOES IN THE NXA OR LXA     *
*                            IS BASED ON THE MODULE NAME. IGCXXX    *
*                            IS CONSIDERED A TYPE 1/2 SVC IF XXX    *
*                            IS BETWEEN 0-255 (THESE MODULES GO IN  *
*                            THE NXA). IGC00XXX IS CONSIDERED A TYPE*
*                            3/4 SVC IF XXX IS BEWTEEN 0-255 (THESE *
*                            MODULES GO IN THE LXA). ANY OTHER NAME *
*                            IS CONSIDERED A STANDARD LPA NAME (OR  *
*                            A STANDARD SVC SECOND, THIRD, ETC. LOAD*
*                            AND IS THUS PLACED IN THE LXA). THE    *
*                            NXA AND LXA ARE CONCEPTUAL ONLY, IN    *
*                            REALITY BOTH EXIST IN SP 228.          *
*                                                                   *
*                      "SVCTABLE TYPE INVALID, CHANGED TO TYPE 3"   *
*                      ---YOU HAVE REQUESTED A TYPE 3/4 SVC BE      *
*                         REPLACED IN THE LXA (BASED ON THE NAME YOU*
*                         SUPPLIED (SEE NOTE ABOVE)), BUT THE       *
*                         SVC ENTRY TYPE INDICATES A NON-TYPE 3/4   *
*                         SVC (IE. 1,2,5,6). THEREFORE, THE SVCTABLE*
*                         IS TEMPORARILY UPDATED TO INDICATE THAT   *
*                         THIS SVC IS A TYPE 3. BEFORE FINAL SVC    *
*                         INSTALLATION, BE SURE TO ZAP THE SVCTABLE,*
*                         OR DO ANOTHER I/O GEN SPECIFYING THE      *
*                         CORRECT TYPE OF SVC.                      *
*                                                                   *
*                      "SVCTABLE TYPE INVALID, REQUEST REJECTED"    *
*                      ---YOU HAVE REQUESTED A TYPE 1/2 SVC BE      *
*                         REPLACED IN THE NXA (BASED ON THE NAME YOU*
*                         SUPPLIED (SEE NOTE ABOVE)), BUT THE       *
*                         SVC ENTRY TYPE INDICATES A NON-TYPE 1/2   *
*                         SVC (IE. 3,4,5,6). THEREFORE, THE REQUEST *
*                         IS REJECTED, AS IT IS UNKNOWN WHETHER YOU *
*                         INTENDED A TYPE 1 OR A TYPE 2 (THIS IS    *
*                         VIRTUALLY MEANINGLESS FOR THE ABOVE CASE  *
*                         OF TYPE 3/4 AS THEY ARE ENTERED WITH THE  *
*                         SAME ATTRIBUTES, THEREFORE 3/4 IN THAT    *
*                         CASE WILL SUFFICE). TO CORRECT THIS       *
*                         PROBLEM ON A TEMPORARY BASIS, IN-CORE ZAP *
*                         THE SVCTABLE TYPE TO INDICATE THE TYPE OF *
*                         SVC YOU REQUIRE, THEN RE-ISSUE YOUR       *
*                         COMMAND. BEFORE FINAL SVC INSTALLATION, BE*
*                         SURE TO ZAP THE SVCTABLE, OR DO ANOTHER   *
*                         I/O GEN SPECIFYING THE CORRECT TYPE OF SVC*
*                         (NOTE: THIS WILL MOST LIKELY HAPPEN IN THE*
*                         CASE OF AN UNDEFINED OR TYPE 5 SVC).      *
*                                                                   *
*                      NOTE: SVCS THAT ARE CURRENTLY DEFINED AS     *
*                            TYPE 5, OR CURRENTLY UNDEFINED MAY     *
*                            BE REPLACED IN THE ABOVE MANNER, IT    *
*                            IS NOT NECESSARY TO DO AN I/O GEN PRIOR*
*                            TO TESTING THE SVC(S), BUT IF ANY LOCKS*
*                            ARE NECESSARY THAT ARE NOT ALREADY ON  *
*                            IN THE SVCTABLE ENTRY, THEY MUST BE    *
*                            ZAPPED ON PRIOR TO THE LOADING OF THE  *
*                            SVC BY MODREP, OR THE SVC WILL BE      *
*                            ENTERED WITH INCORRECT LOCK ATTRIBUTES.*
*                                                                   *
*                      "LPA/SVC DELETE(S) SUCCESSFUL"               *
*                      ---IN RESPONSE TO YOUR "DELETE" OR "DE"      *
*                         REQUEST, ALL MODULES AND SVCS CURRENTLY   *
*                         UNDER CONTROL OF MODREP (THIS MODULE) HAVE*
*                         BEEN SUCCESSFULLY REMOVED FROM THE SYSTEM.*
*                         IN THE CASE OF SVCS, THE SVCTABLE IS      *
*                         PLACED BACK IN ITS PRE-MODREP (ORIGINAL)  *
*                         STATE (INCLUDING SVC TYPE), AND THE NXA   *
*                         MEMORY IS FREED. IN THE CASE OF LPA       *
*                         MODULES, THE CDE/XTLST THAT WERE BUILT    *
*                         FOR THIS MODULE ARE DE-CHAINED, AND THE   *
*                         MEMORY FOR THE CDE/XTLST (SQA) AND LXA    *
*                         MEMORY IS FREED. THE NET RESULT IS THAT   *
*                         EVERYTHING IS AS IT WAS PRIOR TO THE      *
*                         INVOCATION OF MODREP.                     *
*                                                                   *
*                      "ALL LPA/SVC MODS REMAIN RESIDENT"           *
*                      ---IN RESPONSE TO YOUR "KEEP" OR "KE"        *
*                         REQUEST, ALL MODULES AND SVCS CURRENTLY   *
*                         UNDER CONTROL OF MODREP (THIS MODULE) ARE *
*                         RELEASED TO THE SYSTEM FOR PERMANENT USE  *
*                         UNTIL THE NEXT IPL. ONCE THIS HAS TAKEN   *
*                         PLACE, THE ONLY WAY TO GET RID OF THE MOD *
*                         IS TO START ANOTHER COPY OF MODREP AND    *
*                         PLACE A "NEWER" COPY OF THE MOD IN THE    *
*                         SYSTEM. YOU CAN NEVER DELETE ANY MODULE   *
*                         THAT WAS PLACED IN THE SYSTEM WITH A PRIOR*
*                         INVOCATION OF MODREP. THIS CONSIDERATION  *
*                         IS ONLY IMPORTANT IN THE FACT THAT A      *
*                         SUBSEQUENT USE OF THIS MODULE WILL        *
*                         SUCCESSFULLY DO WHAT YOU WANT, BUT THE    *
*                         PREVIOUS INVOCATION'S MEMORY FOR THE      *
*                         CDE/XTLST AND MODULE (NXA,LXA) ARE FIXED, *
*                         AND THUS BECOME WASTED "REAL FRAMES" OF   *
*                         MEMORY. ALSO A CONSIDERATION FOR SVCS     *
*                         IS THAT MODREP CAN ONLY PLACE THE SVCTABLE*
*                         BACK IN THE STATE IT WAS WHEN MODREP BEGAN*
*                         (ASSUMING YOU ENTER "KEEP" OR "KE"),      *
*                         THEREFORE, THE SVCTABLE CAN NEVER BE      *
*                         PLACED BACK IN ITS ORIGINAL (AS OF IPL)   *
*                         STATE WITHOUT AN IPL (OR IN-CORE ZAP IF   *
*                         YOU RECORDED THE EPA BEFORE USING MODREP).*
*                         IF IT IS NOT OBVIOUS BY NOW, THE          *
*                         IMPLICATIONS ARE SUCH THAT IF YOU REPLY   *
*                         "KEEP" OR "KE", YOU HAD BETTER MEAN IT!!! *
*                                                                   *
*                      NOTE: THE FAILURE OF THIS MODULE (ABEND) IS  *
*                            AN IMPLIED "KEEP" OR "KE". THERE IS    *
*                            CURRENTLY NO ESTAE ENVIRONMENT, AND IT *
*                            HAS NOT YET PROVEN NECESSARY. THE MOST *
*                            COMMON LOSS OF THE MODULE WILL BE A    *
*                            B23 ABEND DUE THE COMTASK FAILING,     *
*                            THUS THIS TASK'S WTOR CAUSING ITS      *
*                            FAILURE. SHOULD THIS HAPPEN, YOU HAVE  *
*                            MORE TO WORRY ABOUT THAN MODREP. YOU   *
*                            MAY START A SUBSEQUENT COPY AS         *
*                            EXPLAINED ABOVE, SHOULD THE COMTASK    *
*                            FAILURE (OR ANY OTHER FAILURE) NOT BE  *
*                            PERMANENT.                             *
*                                                                   *
*                      "MODULE UNLOCATABLE"                         *
*                      ---IN RESPONSE TO YOUR "XXXXXXXX" REQUEST,   *
*                         A BLDL TO THE CURRENT PDS (SPECIFIED BY   *
*                         THE //MODREP DD STATEMENT) INDICATES THAT *
*                         THE REQUESTED MODULE DOES NOT EXIST, AND  *
*                         THEREFORE CANNOT BE LOADED. CHECK THAT YOU*
*                         HAVE SPECIFIED THE CORRECT NAME, OR THE   *
*                         CORRECT LIBRARY (SEE "LIB" ON PROC        *
*                         STATEMENT FOR POSSIBLE OVERRIDE)          *
*                                                                   *
*                      "LOCK FAILURE"                               *
*                      ---DURING PROCESSING, A FAILURE TO OBTAIN    *
*                         OR RELEASE A REQUIRED LOCK HAS OCCURRED.  *
*                         ALTHOUGH THIS IS MOST LIKELY A SYSTEM     *
*                         ORIENTED PROBLEM (OR CODE HAS BEEN ADDED  *
*                         INCORRECTLY), THE LAST COMMAND ISSUED MAY *
*                         BE RE-ISSUED TO CHECK THE VALIDITY OF THE *
*                         MESSAGE WITH NO HARM. IF THE MESSAGE      *
*                         PERSISTS TAKE TWO ASPIRIN AND LIE DOWN,   *
*                         OR CHECK TO SEE WHO IS THE CURRENT HOLDER *
*                         OF THE SALLOC OR DISP LOCK, AND FIND OUT  *
*                         WHY HE WILL NOT RELEASE IT (YOU HAVE      *
*                         NOW DEFINITELY UNCOVERED A SYSTEM BUG).   *
*                                                                   *
*                      "SVC REP SUCCESSFUL"                         *
*                      "SQA CRITICAL, REQUEST ABORTED"              *
*                      ---DURING PROCESSING, A CONDITIONAL REQUEST  *
*                         FOR SQA (FOR CDE/XTLST, OR FETCH          *
*                         PRE-PROCESSING CODE) HAS RETURNED A       *
*                         NON-ZERO RETURN CODE, INDICATING THAT SQA *
*                         IS NOT CURRENTLY AVAILABLE. THE LAST      *
*                         COMMAND ISSUED MAY BE RE-ISSUED TO CHECK  *
*                         THE VALIDITY OF THE MESSAGE WITH NO HARM. *
*                         IF THE MESSAGE PERSISTS, CHECK THE CURRENT*
*                         SQA LEVEL, DETERMINE WHO IS USING IT AND  *
*                         WHY, AND WAIT UNTIL IT IS RELEASED, OR IF *
*                         NECESSARY, RE-IPL SPECIFYING MORE SQA     *
*                         (OR CSA FOR EXPANSION).                   *
*                                                                   *
*                      "RESPONSE LOST-REENTER"                      *
*                      ---DURING PROCESSING, A FREEMAIN REQUEST FOR *
*                         CSA OBTAINED BY THIS MODULE HAS FAILED.   *
*                         RE-ENTER THE LAST COMMAND, AND THE        *
*                         FREEMAIN WILL BE RE-ATTEMPTED. IF THE     *
*                         CONDITION PERSISTS, REPLY "DISPLAY" OR    *
*                         "DI" TO FIND WHICH MODULES ARE STILL UNDER*
*                         MODREP'S CONTROL. THE MODULES LISTED ARE  *
*                         THOSE THAT CANNOT BE FREED. ENTER "KEEP"  *
*                         OR "KE" TO ALLOW MODREP TO SHUTDOWN       *
*                         NORMALLY, THEN IF THOSE MODULES LISTED    *
*                         SHOULD HAVE BEEN REPLACED BY THE ORIGINALS*
*                         IT WILL BE NECESSARY TO START MODREP AGAIN*
*                         RUNNING AGAINST THE LIBRARY WHERE THE     *
*                         ORIGINAL MOUDLES ARE, AND USING MODREP TO *
*                         RE-LOAD THE ORIGINALS. THIS CONDITION     *
*                         SHOULD NEVER PERSIST FOR MORE THAN THREE  *
*                         ATTEMPTS, IF IT DOES USE DRISTAN NASAL    *
*                         DECONGESTIVE, THEN LOOK AT THE PROGRAM    *
*                         LOGIC.                                    *
*                                                                   *
*                                                                   *
*                      "ALREADY LOADED, IGNORED"                    *
*                      ---YOU HAVE REQUESTED THAT MODREP LOAD A     *
*                         MODULE WHICH HAS ALREADY BEEN LOADED BY   *
*                         MODREP DURING THIS SESSION. THE REQUEST   *
*                         IS IGNORED. IF YOU MUST LOAD ANOTHER COPY *
*                         EITHER USE MODREP TO DELETE THE CURRENT   *
*                         ONE, OR IF YOU HAVE MULTILPLE MODULES     *
*                         UNDER MODREPS CONTROL AND YOU DO NOT WISH *
*                         TO DELETE ALL THE MODULES (WHICH "DELETE" *
*                         OR "DE" DOES), START ANOTHER COPY OF      *
*                         MODREP, AND LOAD YOUR MODULE USING THE    *
*                         NEW COPY.                                 *
*                                                                   *
*                      "SLOT TABLE OVERFLOW"                        *
*                      ---AFTER LOADING THE REQUESTED MODULE, MODREP*
*                         FOUND NO SPACE IN HIS "SLOT TABLE" TO     *
*                         RECORD NECESSARY INFORMATION FOR LATER    *
*                         DELETION. THE MODULE JUST LOADED CANNOT   *
*                         BE DELETED. FOR A TEMPORARY SOLUTION (IF  *
*                         THE MODULE MUST BE DELETED, USE THE       *
*                         PROCEDURE DEFINED IN THE SECTION ABOVE    *
*                         FOR THE "RE-LOADING" OF THE ORIGINAL COPY.*
*                         AS A FINAL SOLUTION, QUIT BEING A TURKEY  *
*                         AND DON'T LOAD OVER 20 MODULES AT A TIME  *
*                         WITH MODREP. OR...IF YOU MUST PERSIST IN  *
*                         BEING A TURKEY, INCREASE THE MULTIPLIER   *
*                         AT CONSTANT NAMTABLE TO THE MAXIMUM AMOUNT*
*                         OF LOADS REQUIRED.                        *
*                                                                   *
*                      "PDS OPEN FAILED"                            *
*                      ---MODREP WAS UNABLE TO OPEN THE PDS NAMED   *
*                         IN THE //MODREP DD STATEMENT IN YOUR      *
*                         PROC.  START MODREP AGAIN, IF THE FAILURE *
*                         PERSISTS, CHECK TO BE SURE THE DATA SET   *
*                         IS INTACT. FOR A TEMPORARY SOLUTION, MOVE *
*                         THE NECESSARY MODULES TO ANOTHER LIBRARY. *
*                                                                   *
*                      "INTERNAL CDE CHAIN ERROR"                   *
*                      ---INTERNAL PROCESSING HAS FOUND A GLITCH    *
*                         IN THE SYSTEM BUILT CDE AS A RESULT OF    *
*                         AN INTERNAL LOAD. ALL MODULES CURRENTY    *
*                         LOADED ARE KEPT, AND MODREP DUMPS WITH    *
*                         A USER 100.                               *
*                                                                   *
*                      "INTERNAL LLE CHAIN ERROR"                   *
*                      ---INTERNAL PROCESSING HAS FOUND A GLITCH    *
*                         IN THE SYSTEM BUILT LLE AS A RESULT OF    *
*                         AN INTERNAL LOAD. ALL MODULES CURRENTY    *
*                         LOADED ARE KEPT, AND MODREP DUMPS WITH    *
*                         A USER 200.                               *
*                                                                   *
*                      "XXXXXXXX EP YYYYYY"                         *
*                      ---IN RESPONSE TO YOUR "DISPLAY" OR "DI"     *
*                         COMMAND, THE MODULE NAMED XXXXXXXX HAS    *
*                         AN EP OF YYYYYY.  ALL MODULES CURRENTY    *
*                         LOADED ARE LISTED.                        *
*                                                                   *
*                      "NO MODS IN USE"                             *
*                      ---IN RESPONSE TO YOUR "DISPLAY" OR "DI"     *
*                         COMMAND, MODREP HAS DETERMINED THAT       *
*                         NO MODULES ARE CURRENTLY UNDER HIS        *
*                         CONTROL.                                  *
*                                                                   *
*                                                                   *
*                                                                   *
*********************************************************************
         TITLE 'M O D R E P  ***  INITIALIZATION SECTION'        88241
         COPY  OPTIONGB                                          88241
         SPACE 1                                                 88241
         SYSPARM LIST=YES                                        88241
         SPACE 1                                                 88241
         PRINT NOGEN         SAVE A TREE TODAY                   88241
         EXTRN SUBDEFMT                                         GP06292
MODREP   PGMHEAD ZERO15,BASE=(R11,R12),BNDRY=PAGE  INIT PGM     GP06292
         LA    R13,SAFESAVE-SAVE(,R13)  FINAGLE                 GP06292
         USING SAFESAVE,R13  AVOID 30A IN CLOBBERED SAVE AREA   GP06292
         SPLEVEL SET         SET DEFAULT LEVEL                   92281
         MVC   WRKRENT(WRKSIZE),PATRENT   MOVE PATTERNS         GP06292
         MVC   BLDLIST(4),=Y(1,72)  INIT BLDL                   GP06292
         LA    R1,NAMTABLE   GET START OF MODULE TABLE          GP06292
         AL    R1,=A(NAMTABEN-NAMTABLE)  SPACE TO END           GP06292
         MVI   0(R1),X'FF'   TABLE SCAN STOPPER                 GP06292
         #PRT  SYSPRINT,TYPE=OPEN  OPTIONAL OUTPUT              GP06292
         SPACE 1
         AIF   ('&LOCAL' NE 'PID').NOLAUTH                       92355
         SERVICE ACGET,DB    GET INVOKER'S ACCOUNT NUMBER        88241
         LA    R14,VAASYS+VAASTC+VAASUP  GET AUTHORIZED BITS     88241
         NR    R0,R14                                            88241
         LTR   R0,R0         ANY PRIVILEGE AT ALL ?              88241
         BZ    AUTHFAIL      NO                                  88241
.NOLAUTH TESTAUTH FCTN=1     ARE WE AUTHORIZED ?                 88241
         BXLE  R15,R15,HAVEAUTH YES                              88241
AUTHFAIL WTOPR 'UNAUTHORIZED INVOCATION',ROUTCDE=1,DESC=1       GP06292
         ABEND 47            NO DUMP                             88241
         SPACE 1                                                 88241
HAVEAUTH SLR   R0,R0         SIGNAL OUR ADDRESS SPACE            88241
         SLR   R1,R1         CLEAR ECB                           88241
         SYSEVENT TRANSWAP   MAKE THIS TURKEY RESPOND FASTER     88241
         MODESET KEY=ZERO,MODE=SUP        FOR LOCK
         SETLOCK OBTAIN,TYPE=SALLOC,MODE=UNCOND,REGS=SAVE,             X
               RELATED=('REL0')
         BXH   R15,R15,LOCKFAIL  TOO BAD                         88241
         L     R4,PSATOLD-PSA    GET MY TCB                     GP06292
         ST    R4,MYTCB      SAVE FOR LATER                     GP06292
         L     R7,MYASCB        FOR BRANCH ENTRY                 88242
         LA    R1,LENGTH                  FETCH PATCH LENGTH
         GETMAIN RC,LV=(R1),SP=245,BRANCH=(YES,GLOBAL)
         BXH   R15,R15,SQAFAIL  NO. YUK.                         88241
         ST    R1,SAVESQA                 YES.
         SETLOCK RELEASE,TYPE=SALLOC,REGS=SAVE,RELATED=('SET1')
         BXH   R15,R15,LOCKFAIL    OOPS                          88241
         SPACE 1
         L     R2,CVTPTR                  CVT
         USING CVTMAP,R2                                         88241
         LA    R1,CVTFETCH   CVTFETCH                            88241
         ST    R1,AFETCHEP                SAVE
         L     R1,CVTFETCH   CVTFETCH EP                         88241
         ST    R1,RFETCHEP                SAVE
         L     R1,CVTSHRVM   GET END OF PRIVATE AREA            GP06292
         ST    R1,LOWCSA     SAVE FOR VALIDITY CHECKS           GP06292
         DROP  R2                                                88241
         SPACE 1
         MVC   MYASCB,PSAAOLD-PSA  SAVE MY ASCB ADDRESS          88241
         L     R2,MYTCB      GET MY TCB                         GP06292
         L     R2,TCBTIO-TCB(,R2)  GET TIOT JOBNAME/STEPNAME     88241
         MVC   MYTIOT(16),0(R2)  SAVE FOR TEST                   88241
         L     R2,SAVESQA                 GOTTEN PATCH
         L     R1,RFETCHEP   GET SYSTEM FETCH ADDRESS            92282
         N     R1,=X'80000000'  KEEP ONLY ADDRESSING MODE BIT    92282
         OR    R2,R1         USE SAME MODE                       92282
         ST    R2,MFETCHEP                SAVE
         MVC   0(LENGTH,R2),FETCHPAT      RELOCATE PATCH
         MODESET KEY=NZERO
         SPACE 1
         #RDR  MODIN,TYPE=OPEN,OPT=NOWTO  OPTIONAL CARD INPUT   GP06292
         CH    R15,=H'8'     IGNORE IF NONE                     GP06292
         BNL   OPEN          AND PROCEED                        GP06292
         #RDR  ,             GET FIRST RECORD                   GP06292
         BXH   R15,R15,OPEN  NONE; IGNORE EMPTY OR DUMMY FILE   GP06292
         #RDR  TYPE=KEEP     SET TO REREAD                      GP06292
         OI    FLAGS,FGSYN   SIGNAL MODIN/SYSIN INPUT           GP06292
         TITLE 'M O D R E P  ***  OPEN LIBRARY SECTION'          88241
OPEN     LA    R2,WRKDCB                  IS                    GP06292
         USING IHADCB,R2                  OPEN
         OPEN  ((R2)),MF=(E,WRKOLIST)  OPEN THE PDS(ES)         GP06292
         TM    DCBOFLGS,DCBOFOPN          GOOD?
         BZ    OPENFAIL                   NO.
         MODESET KEY=ZERO    GET PRIVILEGED                      88241
         L     R2,DCBDEBAD   LOAD DEB FOR STEPLIB                88241
         USING DEBBASIC,R2                                       88241
         OI    DEBFLGS1,DEBAPFIN  TURN ON APF LIBRARY BIT        88241
*   ABOVE CODE ALLOWS UNAUTHORIZED LIBRARY TO BE USED FOR TESTING
         DROP  R2                                                88241
         MODESET KEY=NZERO   RESET                               88241
         B     WTOR                       YES.
         SPACE 1                                                 88241
HELPWTO  ICM   R1,15,DOMID   ANY PRIOR DESC=2 ?                  88241
         BZ    DOHELP2       NO                                  88241
         DOM   MSG=(1)       DELETE                              88241
DOHELP2  SLR   R0,R0         CLEAR FOR MLWTO                     88241
         ST    R0,DOMID      CLEAR ID                            88241
         WTO   ('MSG765I MODREP COMMANDS',C),                          *
               ('  DISPLAY | LIST  ---  SHOW CURRENT MODULES',D),      *
               ('  DELETE   ---  RESTORE ALL CHANGES AND EXIT',D),     *
               ('  KEEP     ---  KEEP CHANGES AND EXIT      ',D),      *
               ('  XXXXXXXX ---  NAME OF MODULE TO REPLACE SVC|LPA',DE),
               ,ROUTCDE=1,DESC=2                                 88241
         ST    R1,DOMID      SAVE DOM ID                         88241
         B     WTOR          RE-PROMPT                           88241
         SPACE 1                                                 88241
HELPDOM  ICM   R1,15,DOMID   PRIOR WTO ?                         88241
         BZ    WTOR          NO; SKIP                            88241
         DOM   MSG=(1)       GET RID OF IT                       88241
         XC    DOMID,DOMID   AND CLEAR IT                        88241
         TITLE 'M O D R E P  ***  WTOR RESPONSE HANDLING SECTION'
WTORERR  OICC  8             SET ERROR RETURN CODE              GP06292
         NI    FLAGS,255-FGTRUE   CANCEL STACKED INPUT          GP06292
         TM    FLAGS,FGSYN   CANNED INPUT ?                     GP06292
         BZ    WTOR          NO; NO PROBLEM                     GP06292
         MVC   WRKWTONM,=CL8'PROGRAM'                           GP06292
         WTOPR 'TERMINATING DUE TO ERROR'                       GP06292
         NI    FLAGS,255-FGSYN  RESET                           GP06292
         MVC   REPLY(8),=CL8'DELETE '                           GP06292
         B     WTORCAN       TERMINATE WITH PREJUDICE           GP06292
         SPACE 2
WTOR     TM    FLAGS,FGENQ   ENQUEUE ISSUED ?                    88241
         BZ    WTORNEW       NO                                  88241
         DEQ   MF=(E,WRKENQ)   RELEASE IT                        88241
         NI    FLAGS,255-FGENQ  AND SAY SO                       88241
WTORNEW  XC    REPLY,REPLY   CLEAR REPLY AREA                    88241
         MVC   WRKWTONM,BLANKS    CLEAR MESSAGE NAME            GP06292
         TM    FLAGS,FGTRUE  IS THERE A STACKED ALIAS ?         GP06292
         BNO   WTORNALI      NO                                 GP06292
         NI    FLAGS,255-FGTRU1 YES; RESET IT                   GP06292
         MVC   REPLY,SAVENAME  USE SAVED ALIAS NAME             GP06292
         B     WTORCAN       FAKE INPUT                         GP06292
         SPACE 1
WTORNALI NI    FLAGS,255-FGTRUE  RESET STACKED ALIAS ?          GP06292
         TM    FLAGS,FGSYN   PROCESSING SYSIN/MODIN ?           GP06292
         BZ    WTORWTOR      NO; GET CONSOLE INPUT              GP06292
WTORSGET #RDR  ,             GET INPUT                          GP06292
         BXH   R15,R15,WTORSEOF  END FILE HIT                   GP06292
         LR    R3,R0         COPY ADDRESS                       GP06292
         LR    R4,R1         COPY LENGTH                        GP06292
         #PRT  (R3),(R4),CC=NO,TYPE=TEXT   ECHO INPUT           GP06292
         TRT   0(64,R3),TRTNBLK  FIND FIRST NON-BLANK           GP06292
         BZ    WTORSGET      NONE; IGNORE                       GP06292
         LA    R5,0(,R1)     SAVE STOP ADDRESS (CLEAR HIGH)     GP06292
         CLI   0(R5),C'*'    COMMENTS CARD ?                    GP06292
         BE    WTORSGET      YES; IGNORE                        GP06292
         LA    R1,9(,R1)     PRESET IF NO STOPPER               GP06292
         TRT   0(8,R5),TRTSTOP  LOOK FOR STOPPER                GP06292
         LR    R14,R1        SAVE CURRENT STOPPER               GP06292
         SR    R14,R5        LESS PREVIOUS                      GP06292
         SH    R14,=H'1'     EXECUTE LENGTH                     GP06292
         BM    WTORSGET      WHOA - SKIP THIS                   GP06292
         MVC   REPLY,BLANKS    CLEAR                            GP06292
         EX    R14,EXMVCREP  MOVE REPLY                         GP06292
         B     WTORCAN       AND PROCESS                        GP06292
EXMVCREP MVC   REPLY(0),0(R5)  MOVE CARD INPUT                  GP06292
WTORSEOF NI    FLAGS,255-FGSYN  DONE WITH CARD INPUT            GP06292
         MVC   WRKWTONM,=CL8'Inp.EOF'                           GP06292
         LA    R1,=CL8'KEEP '   DEFAULT END ACTION              GP06292
         ICM   R0,15,RETCODE    DID EVERYTHING WORK ?           GP06292
         BZ    WTORSEOM      YES; KEEP                          GP06292
         LA    R1,=CL8'DELETE '   ERROR END ACTION              GP06292
WTORSEOM MVC   REPLY(8),0(R1)   RETAIN MODS, AND EXIT           GP06292
         B     WTORCAN       AND PROCESS                        GP06292
         SPACE 1
WTORWTOR XC    ECBAD(4),ECBAD             AND ASSOCIATED ECB
         LA    R2,REPLY                                         GP06292
         LA    R3,ECBAD                                         GP06292
         WTOR  'MODREP READY',(R2),L'REPLY,(R3),ROUTCDE=(2)     GP06292
         WAIT  ECB=ECBAD                  DO THE "HO-HUM"
WTORCAN  OC    REPLY,BLANKS               UPPER CASE
         LM    R2,R3,REPLY   GET INPUT                          GP06292
         LA    R0,L'REPLY-1                                     GP06292
WTORLJUS CLM   R2,8,BLANKS   LEADING BLANK ?                    GP06292
         BNE   WTORLJUT      NO; DONE                           GP06292
         SLDL  R2,8          MOVE LEFT                          GP06292
         IC    R3,BLANKS     APPEND TRAILING BLANK              GP06292
         BCT   R0,WTORLJUS   REPEAT                             GP06292
WTORLJUT STM   R2,R3,REPLY                                      GP06292
         MVC   WRKWTONM,REPLY     SHOW NAME IN MESSAGES         GP06292
         MVC   WRKMSGNM,REPLY     SHOW NAME IN MESSAGES         GP06292
         MVI   WRKMSGAL,C' '      RESET ALIAS FLAG              GP06292
         LA    R15,CMDTAB    POINT TO FIRST COMMAND ENTRY        88241
         LA    R0,CMDTAB2-CMDTAB  SET LENGTH OF ONE ENTRY        88241
         LA    R1,CMDTABND   SET TO LAST VALID ENTRY             88241
         LM    R2,R3,REPLY   LOAD REPLY                          88241
CMDSCAN  CL    R2,0(,R15)    MATCH ?                             88241
         BNE   CMDBUMP       NO                                  88241
         CL    R3,4(,R15)    COMPLETE MATCH ?                    88241
         BNE   CMDBUMP       NO                                  88241
         L     R15,8(,R15)   GET ROUTINE ADDRESS                 88241
         BR    R15           INVOKE IT                           88241
CMDBUMP  BXLE  R15,R0,CMDSCAN  TRY AGAIN                         88241
         MVC   MEMNAME(8),REPLY           SAVE MOD NAME
         CLC   =C'IGC',MEMNAME   POSSIBLE SVC NAME ?             92281
         BNE   CMDNSVC       NO                                  92281
         CLI   MEMNAME+7,C'*'  IBM'S CONVENTION ?                92285
         BE    CMDYSVC       YES; EASIER TO FIX SVC 34 TR TABLE  92285
         CLI   MEMNAME+7,C'+'  FAKE FOR X'C0' ?                  92281
         BNE   CMDNSVC       NO                                  92281
CMDYSVC  MVI   MEMNAME+7,X'C0'  FIX IT                           92281
CMDNSVC  B     GETMOD                     GO FOR IT!!!!
         SPACE 1                                                 88241
         DS    0A            ALIGN TABLE                         88241
CMDTAB   DC    CL8'DISPLAY ',A(DISPLAY)  DISPLAY COMMAND         88241
CMDTAB2  DC    CL8'DI      ',A(DISPLAY)  SHORT FORM              88241
         DC    CL8'LIST    ',A(DISPLAY)   ALTERNATE FORM         88241
         DC    CL8'LI      ',A(DISPLAY)  SHORT FORM              88241
         DC    CL8'DELETE  ',A(DELETE)   DELETE MODIFICATIONS    88241
         DC    CL8'DE      ',A(DELETE)    ALTERNATE FORM         88241
         DC    CL8'KEEP    ',A(KEEP)     RETAIN CHANGES AND EXIT 88241
         DC    CL8'KE      ',A(KEEP)      ALTERNATE FORM         88241
         DC    CL8'DOM     ',A(HELPDOM)  DELETE COMMAND LIST     88241
         DC    CL8'?       ',A(HELPWTO)  LIST COMMANDS           88241
CMDTABND DC    CL8'HELP    ',A(HELPWTO)  LIST COMMANDS           88241
         TITLE 'M O D R E P  ***  ADD MODULE SECTION'            88241
GETMOD   LA    R1,NAMTABLE           LOADED NAME TABLE
         USING NMTSECT,R1    DECLARE IT                          88241
         SLR   R15,R15       SLOT  KEEPER                        88241
         SPACE 1
PRECHECK CLI   NMTNAME,X'FF' EOT?                                88241
         BE    DOBLDL        YES. VERIFY THAT MODULE EXISTS      88241
         CLC   NMTNAME,MEMNAME  NO. DUPLICATE?                   88241
         BE    DUPFAIL               YES. NO GO YO-YO
         LTR   R15,R15       FIRST TIME ?                        88241
         BNZ   PRECHECT      NO                                  88241
         OC    NMTNAME,NMTNAME  OPEN SLOT ?                      88241
         BNZ   PRECHECT      NO                                  88241
         LR    R15,R1        SAVE ADDRESS                        88241
PRECHECT LA    R1,NMTSIZE(,R1)  GET NEXT ENTRY                   88241
         B     PRECHECK              NEXT
         SPACE 1
DOBLDL   LTR   R15,R15       EMPTY SLOT AVAILABLE ?              88241
         BZ    SLOTERR       NO; FAIL BEFORE LOADING             88241
         ST    R15,CURRSLOT  SAVE IT                             88241
         DROP  R1                                                88241
         SPACE 1
         BLDL  WRKDCB,BLDLIST        MEMBER IN MODREP LIB?      GP06292
         BXH   R15,R15,MODUFAIL      NO.                         88238
         LA    R5,MEMNAME    POINT TO RETURN                    GP06292
         USING PDS2,R5       DECLARE BLDL RETURN AREA           GP06292
         TM    PDS2INDC,PDS2ALIS  IS THIS AN ALIAS ENTRY ?      GP06292
         BZ    LOADMAIN      NO; PROCEED                        GP06292
         MVI   WRKMSGAL,C'*'   INDICATE ALIAS                   GP06292
         TM    FLAGS,FGTRU2  SECOND TIME ON ALIAS?              GP06292
         BZ    ALICURSE      NO                                 GP06292
         NI    FLAGS,255-FGTRU2 YES; RESET IT                   GP06292
         B     LOAD          PROCEED                            GP06292
ALICURSE MVC   SAVENAME,MEMNAME  REMEMBER THE NAME              GP06292
         MVC   DIRENTRY(PDS2CNCT-PDS2NAME),PDS2NAME  COMMON     GP06292
         MVC   DIRENTRY+PDS2CNCT-PDS2NAME(74-PDS2INDC+PDS2),PDS2INDC
         LA    R14,DIRENTRY  DIR. ENTRY WITHOUT BLDL DATA       GP06292
         SR    R15,R15                                          GP06292
         LA    R0,11(,R14)                                      GP06292
         STM   R14,R0,PDDNEXT   FAKE A BXLE SET                 GP06292
         MVI   PDDRECFM,X'C0'   FAKE RECFM=U                    GP06292
         SUBCALL SUBDEFMT,(PDDLIST) FMT THE DIRECTORY ENTRY     GP06292
         BXH   R15,R15,LOAD  FAILED - JUST DO ALIAS             GP06292
         NI    PDS2INDC,255-PDS2ALIS  RESET ALIAS FOR WIDOW     GP06292
         CLI   PDDRMAIN,C' '     BLANK MAIN MEMBER ?            GP06292
         BNH   LOAD          YES; IGNORE ALIAS BIT              GP06292
         WTOPR 'IS AN ALIAS; DOING MAIN MEMBER'                 GP06292
         MVC   MEMNAME,PDDRMAIN   PROCESS MAIN MEMBER FIRST     GP06292
         MVC   SAVEMAIN,PDDRMAIN  ALSO REMEMBER MAIN MODULE     GP06292
         MVC   WRKWTONM,PDDRMAIN  ALSO REMEMBER MAIN MODULE     GP06292
         MVC   WRKMSGNM,PDDRMAIN  ALSO REMEMBER MAIN MODULE     GP06292
         MVI   WRKMSGAL,C' '      RESET ALIAS FLAG              GP06292
         OI    FLAGS,FGTRUE  SHOW RECURSION                     GP06292
         B     GETMOD        AND DO IT AGAIN                    GP06292
         SPACE 1
         PUSH  USING                                            GP06292
LOAD     TM    PDS2INDC,PDS2ALIS  SECOND PASS ON ALIAS ?        GP06292
         BZ    LOADMAIN      NO                                 GP06292
         L     R2,PSATOLD-PSA  GET MY TCB                       GP06292
         ICM   R9,15,TCBLLS-TCB(R2)  LLE?                       GP06292
         BZ    LOADMAIN      HUH ?                              GP06292
         SPACE 1                                                GP06292
LOOPCDE  ICM   R2,15,LLECDPT-LLE(R9)  CDE ?                     GP06292
         BZ    LOADMAIN      IGNORE ALIAS                       GP06292
         USING CDENTRY,R2    DECLARE IT                         GP06292
         CLC   CDNAME,SAVEMAIN   YES. RIGHT ONE?                GP06292
         BE    HAVECDE               YES.                       GP06292
         ICM   R9,15,LLECHN-LLE(R9)  NO. LLE?                   GP06292
         BZ    LOADMAIN              NO.                        GP06292
         B     LOOPCDE               YES.                       GP06292
         SPACE 1                                                GP06292
HAVECDE  ST    R2,SAVECDE            SAVE CDE                   GP06292
         TM    CDATTR2,CDXLE  EXTENT LIST PRESENT ?             GP06292
         BNZ   HAVEXTL       YES                                GP06292
         TM    CDATTR,CDMIN  ALIAS (MINOR CDE)?                 GP06292
         BZ    CDEERROR              NO. CONTINUE               GP06292
         L     R2,CDXLMJP    GET MAJOR CDE                      GP06292
HAVEXTL  L     R2,CDXLMJP    MAJOR CDE XTLST                    GP06292
         ST    R2,SAVEXTL            FOR LATER                  GP06292
         B     DONELOAD                                         GP06292
         POP   USING                                            GP06292
         SPACE 2
         PUSH  USING                                            GP06292
LOADMAIN ENQ   MF=(E,WRKENQ)   PRESERVE OVER OTHER MODREP JOBS   88241
         OI    FLAGS,FGENQ   SHOW ENQUEUE GOTTEN                 88241
         MODESET KEY=ZERO            BEGIN ORBIT
         L     R1,AFETCHEP   CVTFETCH
         L     R2,MFETCHEP   CVTPATCH EPA
         ST    R2,0(,R1)     DUMMY FETCH EPA
         LOAD  DE=MEMNAME,DCB=WRKDCB  LOAD                      GP06292
         OI    FLAGS,FGONE   SHOW AT LEASTONE PROCESSED         GP06292
         L     R1,AFETCHEP   CVTFETCH
         L     R2,RFETCHEP   FETCH EPA
         ST    R2,0(,R1)     REAL FETCH EPA
         MODESET KEY=NZERO           RETURN TO EARTH
         SPACE 1
LOADLIST L     R2,PSATOLD-PSA  GET MY TCB                        88241
         ICM   R9,15,TCBLLS-TCB(R2)  LLE?                        88241
         BZ    LLEERROR              NO.
         SPACE 1
CDECHAIN ICM   R2,15,LLECDPT-LLE(R9)  CDE ?                      88241
         BZ    CDEERROR              NO.
         USING CDENTRY,R2    DECLARE IT                          88241
         CLC   CDNAME,MEMNAME  YES. RIGHT ONE?                   88241
         BE    CDESAVE               YES.
         ICM   R9,15,LLECHN-LLE(R9)  NO. LLE?                    88241
         BZ    LLEERROR              NO.
         B     CDECHAIN              YES.
         SPACE 1
CDESAVE  ST    R2,SAVECDE            SAVE CDE
         TM    CDATTR2,CDXLE  EXTENT LIST PRESENT ?             GP05303
         BNZ   NOTALIAS                                         GP05303
         TM    CDATTR,CDMIN  ALIAS (MINOR CDE)?                  88241
         BZ    CDEERROR              NO. CONTINUE               GP05303
         L     R2,CDXLMJP    GET MAJOR CDE                       88241
NOTALIAS L     R2,CDXLMJP    MAJOR CDE XTLST                     88241
         ST    R2,SAVEXTL            FOR LATER
         NI    PDS2INDC,255-PDS2ALIS  FAILED ON THIS PATH       GP06292
         POP   USING                                             88241
DONELOAD L     R15,SAVECDE   GET THE CDE BACK                   GP06292
         CLC   CDENTPT-CDENTRY(4,R15),LOWCSA  HIGH LOAD WORKED? GP06292
         BL    ERRLOLOD      NO; REJECT IT                      GP06292
DONECHEK CLC   =C'IGC',MEMNAME       MAYBE SVC?
         BNE   SET1                  NO.
         BAL   R9,SVCUPDAT           YES.
         SPACE 1
SET1     DS    0H                    HERE ONLY IF NOT SVC
         MODESET KEY=ZERO,MODE=SUP   FOR LOCK
         SETLOCK OBTAIN,TYPE=SALLOC,MODE=UNCOND,REGS=SAVE,             X
               RELATED=('REL1')
         BXH   R15,R15,LOCKFAIL      NO.                         88241
         L     R4,MYTCB      LOAD REQUIRED REGISTERS             88242
         L     R7,MYASCB        FOR BRANCH ENTRY                 88242
         GETMAIN RC,LV=48,SP=245,BRANCH=(YES,GLOBAL)
         BXH   R15,R15,SQAFAIL       NO.                         88241
         ST    R1,SAVEGET            YES.
         SPACE 1
REL1     DS    0H                    FREE AP/MP FOR CSA
         SETLOCK RELEASE,TYPE=SALLOC,REGS=SAVE,RELATED=('SET1')
         BXH   R15,R15,LOCKFAIL      NO.                         88241
         SPACE 1
UPDATCDE DS    0H                    FOR MODULES ONLY - NOT SVCS
GD1      SETLOCK OBTAIN,TYPE=DISP,MODE=UNCOND,REGS=SAVE,RELATED=('RD1')
         BXH   R15,R15,LOCKFAIL      NO.                         88241
         L     R2,CVTPTR             CVT                         88241
         L     R2,CVTQLPAQ-CVTMAP(,R2)  CVTQLPQA                 88241
         L     R1,SAVEGET            GOTTEN SQA
         L     R8,SAVECDE            REP MOD'S CDE
         MVC   0(32,R1),0(R8)        MOVE CDE TO SQA
         TM    CDATTR2-CDENTRY(R1),CDXLE  EXTENT LIST PRESENT?  GP05303
         BNZ   NOTALI                                           GP05303
         TM    CDATTR-CDENTRY(R1),CDMIN ALIAS (MINOR CDE)?       88241
         BNO   NOTALI                NO. CONTINUE
         L     R8,CDXLMJP-CDENTRY(,R1)   MAJOR CDE               88241
         MVC   CDUSE-CDENTRY(8,R1),CDUSE-CDENTRY(R8) ASSIMILATE  88241
NOTALI   LA    R8,32(,R1)    POINT TO XTLST LOCATION
         ST    R8,CDXLMJP-CDENTRY(,R1)  UPDATE CDE APPROPRIATELY 88241
         L     R8,0(,R2)             GET CURRENT CHAIN BEGINNING
         ST    R8,0(,R1)             CHAIN MYSELF TO REAL FIRST CDE
         L     R8,SAVEXTL            GET REP MOD'S XTLST
         MVC   32(16,R1),0(R8)       MOVE XTLST IN BEHIND IT
         OI    CDATTR-CDENTRY(R1),X'B3'  SIMULATE NIP/JPAQ THUMBPRINT
         ST    R1,0(,R2)             CHAIN MYSELF INTO ACT CDES
RD1      SETLOCK RELEASE,TYPE=DISP,REGS=SAVE,RELATED=('GD1')
         BXH   R15,R15,LOCKFAIL      NO.                         88241
         MODESET KEY=NZERO,MODE=PROB
*OLD*    WTOPR 'MODREP-LPA REP SUCCESSFUL'
         AIF   ('&LOCAL' NE 'PID').NOULOD                        92355
         SERVICE LPA@0,MEMNAME  RESET THE UCVT ENTRY             88330
.NOULOD  L     R1,CURRSLOT   GET AVAILABLE SLOT                  88241
         USING NMTSECT,R1                                        88241
STORE    MVC   NMTNAME,MEMNAME  SAVE MOD NAME                    88241
         L     R8,SAVECDE            GET REP MOD'S CDE
         L     R0,CDENTPT-CDENTRY(,R8)   IT'S EPA                88241
         ST    R0,NMTEPA     SAVE ENTRY                          88241
         INHEX WRKMSGEP,NMTEPA,4,MAKE=OUT                       GP06292
         L     R8,SAVEXTL            GET REP MOD'S XTLST        GP06292
         ICM   R0,7,XTLMSBLN-XTLST(R8)    LENGTH                GP06292
         ST    R0,NMTXLEN    SAVE IT                            GP06292
         ICM   R0,7+&MVSXA*8,XTLMSBAD-XTLST(R8)   ADDRESS       GP06292
         ST    R0,NMTMSBA    SAVE IT                            GP06292
         TM    PDS2INDC,PDS2ALIS  IS THIS AN ALIAS ENTRY ?      GP06292
         BNZ   STOREAL       YES; SET IT                        GP06292
         TM    CDATTR-CDENTRY(R8),CDMIN  ALIAS (MINOR CDE)?      88241
         BNO   STORMSG       NO; WRITE MSG AND RECURSE          GP06292
STOREAL  MVC   NMTSVC,=CL8' ALIAS ' NEED TO KNOW FOR LATER       88241
STORMSG  INHEX WRKMSGAD,NMTMSBA,4                               GP06292
         INHEX WRKMSGLN,NMTXLEN,4                               GP06292
         LA    R1,WRKMSG     MESSAGE ADDRESS                    GP06292
         WTO   MF=(E,WRKMSG)                                    GP06292
         #PRT  WRKMSG,CC=NO                                     GP06292
         B     WTOR                  GO WAIT
         DROP  R1                                                88241
         SPACE 2
*---------------------------------------------------------------------*
*   CHECK WHETHER WE'RE REPLACING AN SVC ENTRY. NAME BEGINS IGC       *
*   TYPE 1 AND 2  ARE IGCnnn  WITH nnn IN THE RANGE 000 TO 255        *
*   TYPE 3 AND 4  ARE IGCmmnnn WHERE mm MUST BE ZERO (FIRST LOAD)     *
*      IF NO MATCH, LOAD INTO LPA AS A NORMAL MODULE                  *
*---------------------------------------------------------------------*
SVCUPDAT MVC   SVCNAME,MEMNAME  SAVE NAME (IGCNNN 1/2;  IGCMMNNN 3/4)
         CLC   MEMNAME+6(2),BLANKS   TYPE 1/2 MAYBE?
         BNE   TESTSVC3      NO; MAY BE TYPE 3 OR 4              88241
         MVC   SVCNAME+5(3),MEMNAME+3    DUMMY SVC NAME FOR CONVERT
         B     TESTSVCN      CHECK MEMBER NAME                   88241
TESTSVC3 CLC   =C'00',MEMNAME+3      POSSIBLE FIRST LOAD TYPE 3/4
         BNER  R9                    NO. NOT TYPE 3/4
TESTSVCN LA    R1,SVCNAME+7  END IF ALL DIGITS                  GP06292
         SLR   R2,R2         FOR END TYPE TST                   GP06292
         TRT   SVCNAME+5(3),TRTSVCNM   VALID LOW BYTES IN NAME? GP06292
         CLM   R2,1,=X'04'   VALID END ?                        GP06292
         BHR   R9            NO; NOT SVC                        GP06292
         LA    R0,SVCNAME+5  SCAN START                         GP06292
         SR    R1,R0         FIELD LENGTH                       GP06292
         CH    R1,=H'2'      VALID LENGTH ?                     GP06292
         BNER  R9            NO; NOT SVC                        GP06292
         SPACE 1
SVCLOAD  MVC   SVCNAME(5),=5C'0'     ZERO FIRST 5 BYTES OF NAME
         PACK  DB,SVCNAME+4(4) SVC NUMBER
         CVB   R0,DB         MAKE BINARY                         88241
         CH    R0,=H'255'    VALID NUMBER ?                      88241
         BHR   R9            NO; NOT SVC                         88241
         AM31  ,             GET HIGH                            92281
         L     R15,CVTPTR            CVT                         88241
         L     R15,CVTABEND-CVTMAP(,R15)  SCVT                   88241
         L     R15,SCVTSVCT-SCVTSECT(,R15)  SVC TABLE            88241
         LR    R2,R0         COPY OVER                           88241
         MH    R2,=Y(SVCENTLN)   MAKE OFFSET                     88241
         AR    R2,R15                SVC ENTRY
         TM    SVCATTR1-SVCENTRY(R2),SVCTP34  TYPE 3/4 SVC?      88241
         BO    TYPE34                YES. GROOVY
         AM24  ,                                                 92281
         CLI   MEMNAME+6,C' '        TYPE 1/2?
         BE    CONTINU               YES. NOT 3/4, ASSUME OK
         WTOPR 'SVC TYPE INVALID, USING TYPE 3'                 GP06292
         SPACE 1
TYPE34   AM24  ,                                                 92281
         CLI   MEMNAME+6,C' '        TYPE 1/2?
         BNE   CONTINU               NO. REALLY IS TYPE 3/4
         WTOPR 'SVC TYPE INVALID, REJECTED'                     GP06292
         B     WTORERR               GO WAIT                    GP06292
         SPACE 1
CONTINU  AM31  ,             GET HIGH AGAIN                      92281
         L     R1,CURRSLOT   FIND FREE SLOT                      88241
         USING NMTSECT,R1                                        88241
         MVC   NMTNAME,MEMNAME  SAVE SVC NAME                    88241
         MVC   NMTSVC,SVCEP-SVCENTRY(R2)  SAVE SVC ENTRY         88241
         ST    R2,NMTSVC2    SAVE SVC TABLE ENTRY ADDRESS        88241
         L     R8,SAVECDE            GET REP MOD'S CDE
         L     R3,CDENTPT-CDENTRY(,R8)  GET ITS EPA              92282
         ST    R3,NMTEPA     SAVE ITS EPA                        92282
         L     R8,SAVEXTL            GET REP MOD'S XTLST
         SLR   R0,R0         JUST IN CASE                        90197
         ICM   R0,7,XTLMSBLN-XTLST(R8)    LENGTH                 90197
         ST    R0,NMTXLEN    SAVE IT                             88241
         ICM   R0,7+&MVSXA*8,XTLMSBAD-XTLST(R8)   ADDRESS        92282
         ST    R0,NMTMSBA    SAVE IT                             88241
         INHEX WRKMSGEP,NMTEPA,4                                GP06292
         INHEX WRKMSGAD,NMTMSBA,4                               GP06292
         INHEX WRKMSGLN,NMTXLEN,4                               GP06292
         LA    R1,WRKMSG     MESSAGE ADDRESS                    GP06292
         WTO   MF=(E,WRKMSG)                                    GP06292
         #PRT  WRKMSG,CC=NO                                     GP06292
         DROP  R1                                                88241
         CLI   MEMNAME+6,C' '        TYPE 1/2?
         BE    SKIPTHRE              YES. SKIP TYPE 3 GUARANTEE
         LA    R1,PSVCOI     INSTRUCTION TO BE EXECUTED          92282
         BAL   R14,STASH     UPDATE PROTECTED STORAGE            92282
SKIPTHRE LA    R1,PSVCST     POINT TO EP ADDRESS STORE           92282
         BAL   R14,STASH     REPLACE                             92282
         AM24  ,                                                 92281
         WTOPR 'SVC REP SUCCESSFUL'                             GP06292
         AIF   ('&LOCAL' NE 'PID').NOUSVC                        92355
         SERVICE LPA@0,MEMNAME  RESET THE UCVT ENTRY             88330
.NOUSVC  B     WTOR                  DONE, GO WAIT
PSVCOI   OI    SVCATTR1-SVCENTRY(R2),SVCTP34  GUARANTEE TYPE 3/4 SVC
PSVCST   ST    R3,SVCEP-SVCENTRY(,R2) UPDATE SVC TABLE           92282
         TITLE 'M O D R E P  ***  DELETE MODULE SECTION'         88241
DELETE   DS    0H                                                88330
         AIF   ('&LOCAL' NE 'PID').NOUDEL                        92355
         LA    R8,NAMTABLE   POINT TO NAME TABLE                 88330
         USING NMTSECT,R8    DECLARE IT                          88330
DELETEL  TM    NMTNAME,X'FF'   TEST ENTRY                        88330
         BZ    DELETEX       EMPTY; TRY NEXT                     88330
         BO    DELETER       DONE                                88330
         SERVICE LPA@0,NMTNAME  DELETE USER CVT ENTRY            88330
DELETEX  LA    R8,NMTSIZE(,R8)  POINT TO NEXT ENTRY              88330
         B     DELETEL       AND TEST IT                         88330
.NOUDEL  ANOP  ,                                                 88330
DELETER  ENQ   MF=(E,WRKENQ)   PROTECT A BIT                     88242
         OI    FLAGS,FGENQ   SHOW ENQ DONE                       88242
RIPLECDE MODESET KEY=ZERO,MODE=SUP   LOCK UP EVERYTHING
GD2      SETLOCK OBTAIN,TYPE=DISP,MODE=UNCOND,REGS=SAVE,RELATED=('RD2')
         BXH   R15,R15,LOCKFAIL      NO.                         88241
         SPACE 1
CONTONLY DS    0H                    SCANNING AND HAVE LOCK ALREADY
         L     R10,CVTPTR            CVT                         88241
         L     R10,CVTQLPAQ-CVTMAP(,R10)      CVTQLPAQ           88241
SEARCH   DS    0H
*********************************************************************
*        RE-CHAIN CDES REMOVING MY CDES FROM THE CHAIN              *
*********************************************************************
         SPACE 1
         LR    R6,R10                PREV CDE FOR CHAIN RECONFIGURE
         ICM   R10,15,CDCHAIN-CDENTRY(R10)  LAST CDE?            88241
         BZ    SVCTABCK              YES. EXIT TO SVCTABLE CLEANUP
         TM    CDATTR-CDENTRY(R10),CDJPA  MY THUMBPRINT HERE???  88241
         BNO   SEARCH                NO.
         TM    CDATTR-CDENTRY(R10),CDNIP  MLPA/FLPA? (DOUBLE CHECK)
         BNO   SEARCH                NO.
         LA    R8,NAMTABLE   YES. LOADED NAME TABLE              88242
         USING NMTSECT,R8                                        88241
TABLECHK CLC   NMTNAME,CDNAME-CDENTRY(R10)  WAS IT LOADED BY US ?
         BE    DELETEM               YES. DELETE IT
         CLI   NMTNAME,X'FF' EOT?                                88241
         BNE   TABNEXT               NO.
         NI    CDATTR-CDENTRY(R10),255-CDJPA  YES. REMOVE THUMBPRINT
         B     CONTONLY              CONT CDE SCAN, LOCK INTACT
         SPACE 1
TABNEXT  LA    R8,NMTSIZE(,R8) NEXT ENTRY                        88241
         B     TABLECHK              CHECK IT
         SPACE 1
DELETEM  MVC   WRKMSGNM,NMTNAME   SHOW MEMBER                   GP06292
         XC    NMTNAME,NMTNAME   PRELIMINARY: MODULE DELETED     88242
         MVC   CDCHAIN-CDENTRY(4,R6),CDCHAIN-CDENTRY(R10)  RECHAIN
RD2      SETLOCK RELEASE,TYPE=DISP,REGS=SAVE,RELATED=('GD2')
         BXH   R15,R15,LOCKFAIL      NO.                         88241
         SPACE 1
SET2     DS    0H                    LOCK AP/MP OUT OF CSA
         SETLOCK OBTAIN,TYPE=SALLOC,MODE=UNCOND,REGS=SAVE,             X
               RELATED=('REL2')
         BXH   R15,R15,LOCKFAIL      NO.                         88241
         CLC   =CL8' ALIAS ',NMTSVC   ALIAS (MINOR CDE)?         88242
         BE    BYPALIAS              YES. MOD FREED VIA TRUE NAME
         L     R15,CDXLMJP-CDENTRY(,R10) GET CDE XTLST ADDR      88241
         L     R4,MYTCB      FREEMAIN REQUIREMENT                88242
         L     R7,MYASCB        FOR BRANCH ENTRY                 88242
         SLR   R0,R0                                             88242
         SLR   R1,R1                                             88242
         ICM   R0,7,XTLMSBLN-XTLST(R15)  GET LENGTH OF MODULE    90197
         ICM   R1,7+&MVSXA*8,XTLMSBAD-XTLST(R15)  AND MSBA       92282
         FREEMAIN RC,LV=(0),A=(R1),SP=228,BRANCH=(YES,GLOBAL)    88242
         BXH   R15,R15,CSAFAIL  MODULE NOT FREED                 88241
         SPACE 1
BYPALIAS FREEMAIN RC,LV=48,A=(R10),SP=245,BRANCH=(YES,GLOBAL)
         BXH   R15,R15,SQAFAIL  BRANCH IF NOT FREED              88241
         SPACE 1
REL2     DS    0H                    FREE AP/MP FOR CSA
         SETLOCK RELEASE,TYPE=SALLOC,REGS=SAVE,RELATED=('SET2')
         BXH   R15,R15,LOCKFAIL   NO. OH POOP                    88241
         XC    NMTSECT(NMTSIZE),NMTSECT   MODULE DELETED         88241
         B     RIPLECDE              YES, SCAN CDES
         DROP  R8                                                88242
         SPACE 1
SVCTABCK DS    0H                    RE-ESTABLISH SVC ENTRIES
*********************************************************************
*        RESTORE SVC TABLE ENTRIES TO ORIGINAL STATE                *
*********************************************************************
RD3      SETLOCK RELEASE,TYPE=DISP,REGS=SAVE,RELATED=('GD2')
         BXH   R15,R15,LOCKFAIL      NO.                         88241
         LA    R6,NAMTABLE           NAME TABLE, SVCS ONLY NOW   88241
         USING NMTSECT,R6                                        88241
         SPACE 1
TABSVCCK TM    NMTNAME,255   TEST ENTRY                          88241
         BZ    TABNXSVC      NULL; GO TO NEXT ENTRY              88241
         BO    CLEAR         PAST LAST. CLEAR TO LAND            88241
         ICM   R15,15,NMTSVC2  SVCTAB POINTER                    88241
         BZ    TABNXSVC      TRY NEXT ENTRY                      88242
         LA    R1,PSVCBK     INSTRUCTION TO BE EXECUTED          92282
         LR    R2,R15        SET STORAGE ADDRESS                 92282
         BAL   R14,STASH     UPDATE                              92282
         MODESET KEY=ZERO    RESTORE FOR SETLOCK                 92282
         B     SET3                                              92282
PSVCBK   MVC   SVCEP-SVCENTRY(8,R15),NMTSVC  REP OLD SVC ENTRY   88241
         SPACE 1
SET3     DS    0H                    LOCK AP/MP OUT OF CSA
         SETLOCK OBTAIN,TYPE=SALLOC,MODE=UNCOND,REGS=SAVE,             X
               RELATED=('REL3')
         BXH   R15,R15,LOCKFAIL      NO.                         88241
         L     R4,MYTCB      FREEMAIN REQUIREMENT                88242
         L     R7,MYASCB        FOR BRANCH ENTRY                 88242
         L     R0,NMTXLEN    SVC LENGTH                          88241
         L     R1,NMTMSBA    SVC MSBA                            88241
         FREEMAIN RC,LV=(0),A=(R1),SP=228,BRANCH=(YES,GLOBAL)    88242
         BXH   R15,R15,CSAFAIL   NOT FREED                       88241
         XC    NMTSECT(NMTSIZE),NMTSECT   MODULE DELETED         88241
REL3     DS    0H                    FREE AP/MP FOR CSA
         SETLOCK RELEASE,TYPE=SALLOC,REGS=SAVE,RELATED=('SET3')
         BXH   R15,R15,LOCKFAIL      NO.                         88241
         SPACE 1
TABNXSVC LA    R6,NMTSIZE(,R6)  NEXT ENTRY                       88241
         B     TABSVCCK              CHECK IT
         DROP  R6                                                88241
         SPACE 1
CLEAR    DS    0H
         MODESET KEY=NZERO,MODE=PROB RETURN TO EARTH
         MVC   WRKWTONM,=CL8'ending'                            GP06292
         WTOPR 'LPA/SVC DELETE(S) SUCCESSFUL'                   GP06292
         B     BYEBYE                ALL DONE
         SPACE 1
KEEP     TM    FLAGS,FGONE   AT LEAST ONE DONE ?                GP06292
         BZ    BYEBYE        NO; SKIP MESSAGE                   GP06292
         MVC   WRKWTONM,=CL8'ending'                            GP06292
         WTOPR 'LPA/SVC MODS KEPT RESIDENT'                     GP06292
         SPACE 1
         PUSH  USING
BYEBYE   MODESET KEY=ZERO    GET SEMI-PRIVILEGED                 88242
BYEFREE  LA    R0,LENGTH     GET LENGTH                          88242
         ICM   R1,15,SAVESQA    AND ADDRESS OF PATCH CODE        88242
         BZ    BYEMAIN       NONE GOTTEN                         88242
         FREEMAIN RC,LV=(0),A=(R1),SP=245                        88242
         POP   USING                                            GP06292
BYEMAIN  MODESET KEY=NZERO   RESTORE                             88242
         DEQ   MF=(E,WRKENQ),RET=HAVE                            88241
         ICM   R1,15,DOMID   OUT-STANDING WTO ?                  88241
         BZ    BYECLOSE      NO                                  88241
         DOM   MSG=(1)       DELETE IT                           88241
BYECLOSE CLOSE MF=(E,WRKOLIST)  CLOSE MODREP                    GP06292
         #RDR  TYPE=CLOSE                                       GP06292
         #PRT  TYPE=CLOSE                                       GP06292
         PUSH  USING                                            GP06292
         SH    R13,=Y(SAFESAVE-SAVE)   RESTORE PROPER SAVE AREA GP06292
         USING SAVE,R13                                         GP06292
         PGMEXIT COPYRET=(RETCODE,8)  RETURN WITH CC            GP06292
         POP   USING                                            GP06292
         SPACE 2                                                 92282
*   SP, XA ...  STORAGE UNPROTECTION                             92282
*      ADDRESS TO BE UNLOCKED IN R2; EXECUTED INSTRUCTION ADDRESS IN
*        R1; RETURN ON R14 (NO ERROR INDICATION)                 92282
*                                                                92282
STASH    STM   R14,R12,12(R13)  SAVE REGISTERS                   92282
         MODESET KEY=ZERO,MODE=SUP                               92282
         AM31  ,             GET 31-BIT MODE                     92282
         LR    R8,R2         POINT AT WORD (ON WORD BOUNDARY)    92282
         L     R2,PSAAOLD-PSA    GET ADDR OF CURRENT ASCB        92282
         N     R8,PAGEMASK   GET LOWER PAGE BOUNDARY             92282
         STCTL 1,1,SEGTAB    GET SEG TABLE ORIGIN                92282
         L     R2,SEGTAB     GET CONTENTS OF STO REG             92282
         AIF   (&MVSXA).LPAPRO2                                  92282
*   TEMPORARILY TURN OFF SEGMENT PROTECTION                      92282
*                                                                92282
         SRL   R8,16              ISOLATE SEG INDEX (SX)         92282
         SLL   R8,2               MULT SX*4 TO GET OFFSET        92282
         N     R2,=X'00FFFFC0'    ISOLATE ADDR OF SEG TBL        92282
         BALS  R15,REAL2VIR       CONVERT TO VIRTUAL ADDR        92282
         ALR   R2,R8              GET ADDR OF SEG TBL ENTRY      92282
         LA    R8,4          GET MASK FOR SEG PROT BIT           92282
         LA    R9,3(,R2)     POINT AT BYTE CONTAINING SP BIT     92282
         N     R8,0(,R2)     ISOLATE SEG PROT BIT                92282
         BZ    MODIT         BR IF NOT PROTECTED                 92282
         NI    0(R9),X'FB'   TURN OFF SEG PROT                   92282
         B     MODPTLB       SKIP XA PROCESSING                  92282
         AGO   .LPACOM2                                          92282
.LPAPRO2 SPACE 1                                                 92282
*   TURN OFF PAGE PROTECTION FOR XA                              92282
*                                                                92282
MODXA    SLR   R9,R9         CLEAR SECOND REG OF PAIR            92282
         SRDL  R8,20         ISOLATE SX & PUT PX IN R9           92282
         SLL   R8,2          GET SEG TBL OFFSET (SX*4)           92282
         N     R2,=X'7FFFF000'    ISOLATE ADDR OF SEG TABLE      92282
         BALS  R15,REAL2VIR  GET VIRTUAL ADDR                    92282
         ALR   R2,R8         GET ADDR OF SEG TBL ENTRY           92282
         SRL   R9,22         GET PAGE TABLE OFFSET (PX*2)        92282
         L     R2,0(,R2)     GET CONTENT OF SEG TBL ENTRY        92282
         N     R2,=X'7FFFFFC0'    ISOLATE ADDR OF PAGE TBL ORIGIN
         BALS  R15,REAL2VIR  GET VIRTUAL ADDR                    92282
         ALR   R9,R2         GET ADDR OF PAGE TBL ENTRY          92282
         L     R8,0(,R9)     GET CONTENTS OF PAGE TABLE ENTRY    92282
         N     R8,=X'00000200'    ISOLATE PAGE PROT BIT          92282
         BZ    MODIT         BR IF NO PAGE PROT                  92282
         SRL   R8,8          SHIFT BYTE TO LO-ORDER              92282
         LA    R9,2(,R9)     GET ADDR OF BYTE WITH PP BIT IN IT  92282
         NI    0(R9),X'FD'   TURN OFF PAGE PROT                  92282
.LPACOM2 ANOP  ,                                                 92282
MODPTLB  PTLB  ,             ELIMINATE PROT BIT FROM TLB         92282
MODIT    LM    R14,R3,12(R13)  RESTORE CRITICAL REGISTERS        92282
         EX    0,0(R1)       EXECUTE USER'S REQUEST              92282
         EX    R8,ORPROT          TURN PROT BIT BACK ON          92282
         AM24  ,             GET BACK TO 24-BIT MODE             92282
         MODESET KEY=NZERO                                       92282
         LM    R14,R12,12(R13)  RESTORE                          92282
         BR    R14           RETURN                              92282
         SPACE 2                                                 92282
*                                                                92282
*   REAL TO VIRTUAL CONVERSION SUBROUTINE                        92282
*                                                                92282
*        INPUT                                                   92282
*              R2 - REAL ADDR                                    92282
*              R15 - RETURN ADDR                                 92282
*        OUTPUT                                                  92282
*              R1,R3 - ALTERED                                   92282
*              R2 - CONVERTED VIRTUAL ADDRESS                    92282
*                                                                92282
REAL2VIR LR    R1,R2              COPY INPUT ADDR                92282
         SRL   R1,12              SHIFT OFF DISPLACEMENT         92282
         L     R3,CVTPTR          GET ADDR OF CVT                92282
         TM    CVTDCB-CVT(R3),X'80' CVTMVSE IS THIS MVS/XA       92282
         L     R3,CVTPVTP-CVT(,R3)  GET ADDR OF PVT              92282
         AIF   (&MVSXA).REALSP2                                  92282
         BO    R2VXA              YES, BRANCH                    92282
*                                                                92282
         L     R3,PVTPFTP-PVT(,R3)    GET ADDR OF PFT            92282
         SLL   R1,4               GET PFT DISPLACEMENT           92282
         AR    R3,R1              GET PFTE ADDR                  92282
         SR    R1,R1              CLEAR                          92282
         ICM   R1,6,PFTVBN(R3)    GET VIRT BLK NO.               92282
         B     R2VB               SKIP XA PROCESSING             92282
.REALSP2 SPACE 1                                                 92282
R2VXA    L     R3,XAPVTRIT(,R3)     GET ADDR IF RIT              92282
         L     R3,XARITPFT(,R3)     GET ADDR OF PFT              92282
         SLL   R1,5               GET PFT DISPLACEMENT           92282
         L     R1,PFTVSA(R1,R3)   GET VIRT PAGE ADDR             92282
*                                                                92282
R2VB     EQU   *                                                 92282
         N     R2,=X'00000FFF'    GET PAGE DISPLACEMENT          92282
         OR    R2,R1              COMBINE WITH VIRT PAGE NO.     92282
         BR    R15                RETURN                         92282
*                                                                92282
*   CONTROL BLOCK MAPPINGS                                       92282
*                                                                92282
XAPVTRIT EQU   X'04'               POINTER TO RIT (XA)           92282
XARITPFT EQU   X'D0'               POINTER TO APPARENT PFT (XA)  92282
PFTVBN   EQU   X'2'                VIRT BLK NUMBER (370)         92282
PFTVSA   EQU   X'14'               VIRT STORAGE ADDR (XA)        92282
SEGTAB   DC    A(0)           TEMP STORAGE                       92282
PAGEMASK DC    X'FFFFF000'                                       92282
ORPROT   OI    0(R9),*-*      RESET PROTECTION BIT               92282
         TITLE 'M O D R E P  ***  ERROR HANDLING/DISPLAY SECTION'
MODUFAIL WTOPR 'MODULE UNLOCATABLE'                             GP06292
         B     WTORERR               GO WAIT                    GP06292
         SPACE 1
LOCKFAIL WTOPR 'LOCK FAILURE'                                   GP06292
         B     WTORERR               GO WAIT                    GP06292
         SPACE 1
ERRLOLOD WTOPR 'FAILED TO LOAD IN SQA'                          GP06292
         B     WTORERR               GO WAIT                    GP06292
         SPACE 1
SQAFAIL  SETLOCK RELEASE,TYPE=SALLOC,REGS=USE,RELATED=('SET2')
         BXH   R15,R15,LOCKFAIL RELEASE FAILED                   88241
         WTOPR 'SQA CRITICAL, REQUEST ABORTED'                  GP06292
         B     WTORERR               GO WAIT                    GP06292
         SPACE 1
CSAFAIL  SETLOCK RELEASE,TYPE=SALLOC,REGS=USE,RELATED=('SET2')
         BXH   R15,R15,LOCKFAIL      NO.                         88241
         WTOPR 'RESPONSE LOST-REENTER'                          GP06292
         B     WTORERR               GO WAIT                    GP06292
         SPACE 1
DUPFAIL  WTOPR 'ALREADY LOADED, IGNORED'                        GP06292
         B     WTOR                  GO WAIT
         SPACE 1
SLOTERR  WTOPR 'SLOT TABLE OVERFLOW'                            GP06292
         B     WTORERR               GO WAIT                    GP06292
         SPACE 1
OPENFAIL MVC   WRKWTONM,=CL8'*MODREP*'                          GP06292
         WTOPR 'PDS OPEN FAILED'                                GP06292
         B     KEEP                  JUST QUIT
         SPACE 1
CDEERROR WTOPR 'INTERNAL CDE CHAIN ERROR'                       GP06292
         ABEND 100,DUMP              SHOW IT
         SPACE 1
LLEERROR WTOPR 'INTERNAL LLE CHAIN ERROR'                       GP06292
         ABEND 200,DUMP              SHOW IT
         SPACE 1
DISPLAY  NI    FLAGS,255-FGMOD  SET FOR NOTHING LOADED           88241
         LA    R2,NAMTABLE           LOADED NAME TABLE
         USING NMTSECT,R2                                        88241
DLOOP    TM    NMTNAME,X'FF'   SLOT IN USE ?                     88241
         BZ    DLOOPUP       NO; SKIP IT                         88241
         BM    DOWTO         NOT LAST; DISPLAY                   88241
         TM    FLAGS,FGMOD   ANYTHING DISPLAYED?                 88241
         BNZ   WTOR          YES. GO WAIT                        88241
         WTOPR 'NO MODS IN USE'                                 GP06292
         B     WTOR                  NO. NOW GO WAIT
         SPACE 1
DOWTO    OI    FLAGS,FGMOD   AT LEAST ONE LOADED MOD             88241
         MVC   WRKMSGNM,NMTNAME   SHOW MEMBER                   GP06292
         CLC   NMTSVC,=CL8'ALIAS '                              GP06292
         BNE   DOWTORL                                          GP06292
         MVI   WRKMSGAL,C'*'   INDICATE ALIAS                   GP06292
DOWTORL  INHEX WRKMSGEP,NMTEPA,4                                GP06292
         INHEX WRKMSGAD,NMTMSBA,4                               GP06292
         INHEX WRKMSGLN,NMTXLEN,4                               GP06292
         LA    R1,WRKMSG     MESSAGE ADDRESS                    GP06292
         WTO   MF=(E,WRKMSG)                                    GP06292
         #PRT  WRKMSG,CC=NO                                     GP06292
         MVI   WRKMSGAL,C' '      RESET ALIAS FLAG              GP06292
DLOOPUP  LA    R2,NMTSIZE(,R2)  NEXT ENTRY                       88241
         B     DLOOP                 KEEP LOOKING
         DROP  R2                                                88241
         TITLE 'M O D R E P  ***  CONSTANTS/DSECTS SECTION'      88241
*********************************************************************
*   FIXED CONSTANTS AND DATA                                        *
*********************************************************************
CHARHEX  DC    C'0123456789ABCDEF'     HEX TABLE                GP06292
HEXTAB   EQU   CHARHEX-C'0',256,C'C'   HEX TRANSLATE TABLE      GP06292
ENQMAJ   DC    CL8'SYSMODS'
ENQMIN   DC    CL8'MOD-REP'
MODIN    #RDRWRK DDNAME=MODIN,WIDTH=80                          GP06292
SYSPRINT #PRTWRK DDNAME=MODPRINT,@TITLE=@HEADERS                GP06292
@HEADERS DC    A(PAGE#,TIT1,TIT2,X'80000000'+TIT2,0) TITLES     GP06292
TIT1     VCON  '1',END=TIT1END                                  GP06292
         DC    CL80'            MODREP - LPA/SVC REPLACEMENT'   GP06292
         DC    C'PAGE',C' '                                     GP06292
PAGE#    DC    C'0000'       CURRENT PAGE NUMBER                GP06292
         VCON  *END                                             GP06292
TIT2     VCON  '0    '                                          GP06292
         RENTDEF PFX=PAT     DEFINE COMMON MACROS               GP06292
         SPACE 1
TRTNBLK  DC    256AL1(64)    STOP ON NON-BLANK                  GP06292
         TRENT TRTNBLK,0,C' '                                   GP06292
BLANKS   EQU   TRTNBLK+1+C' ',128,C'C'  LOTS O'BLANKS           GP06292
         SPACE 1
TRTSTOP  DC    256AL1(8)     STOP ON EVERYTHING EXCEPT:         GP06292
         TRENT TRTSTOP,0,(C'A',9),(C'J',9),(C'S',8),C'+',       GP06292
         TRENT TRTSTOP,0,(C'0',10),C'@',C'#',C'$',C'*',X'C0'    GP06292
ZEROES   EQU   TRTSTOP+C'0',10,C'X'    RE-USE AS CONSTANT 0     GP06292
         SPACE 1
TRTSVCNM DC    256AL1(8)     STOP ON EVERYTHING EXCEPT:         GP06292
         TRENT TRTSVCNM,0,(C'0',10),    DIGITS VALID            GP06292
         TRENT TRTSVCNM,4,(X'C0',10)    END MUST BE ZONED       GP06292
         SPACE 2
*********************************************************************
*      CONSTANTS, VARIABLES, PATCH AREA, ETC.                       *
*********************************************************************
NMTSECT  DSECT ,             NAME TABLE SECTION                  88241
NMTNAME  DS    CL8           MODULE NAME                         88241
NMTSVC   DS    XL8           SVC TEXT | 0 IF MAIN | 'ALIAS '     88241
NMTSVC2  DS    A             SVC ENTRY | 0                       88241
NMTEPA   DS    A             MODULE OR SVC ENTRY ADDRESS         88241
NMTMSBA  DS    A             MSBA FROM XTLIST IF SVC             88241
NMTXLEN  DS    F             XLEN FROM XTLIST IF SVC             88241
NMTSIZE  EQU   *-NMTSECT       LENGTH OF ONE ENTRY               88241
         SPACE 2
SAVE     DSECT ,             SAVE/WORK AREA                     GP06292
SAFESAVE DS    18A           SAFER SAVE AREA                    GP06292
DB       DS    3D            WORK SPACE                         GP06292
SVCNAME  EQU   DB+8,8,C'D'   SCRATCH PAD                        GP06292
         SERVDEFS ,          EXPAND RETCODE, ETC.               GP06292
         RENTDEF PFX=WRK     EXPAND COMMON MACROS               GP06292
SAVENAME DS    CL8           SAVED ALIAS NAME                   GP06292
SAVEMAIN DS    CL8             AND MATCHING MAIN MEMBER         GP06292
BLDLIST  DC    Y(1,74)       ONE ENTRY OF 72 (12 + 2*31)         88241
MEMNAME  DC    C'????????'   REQUESTED MEMBER NAME
DEINFO   DC    BL(74-L'MEMNAME)'0'  FOR LOAD DE                  88241
DIRENTRY DS    XL72          MEMBER DIRECTORY ENTRY             GP06292
PDDLIST  MAPDEFMT DSECT=NO   DIRCTORY DATA PRINTABLE            GP06292
SAVECDE  DC    A(0)                    SAVE REP MOD'S CDE
SAVEXTL  DC    A(0)                    SAVE REP MOD'S XTLST
SAVEGET  DC    A(0)                    SAVE GETMAIN FOR CDE/XTLST
SAVESQA  DC    A(0)                    SQA AREA OF ALTERNAME FETCH EPA
LOWCSA   DC    A(0)          LOWEST CSA/SQA ADDRESS FOR CHECKS  GP06292
DOMID    DC    F'0'          WTO DOM ID                          88241
CURRSLOT DC    A(0)          CURRENT TABLE ENTRY                 88241
ECBAD    DC    A(0)                    WTOR ECB
REPLY    DC    CL8' '        WTOR REPLY                          88241
         DS    2X       BUFFER
FLAGS    DC    X'00'         PROCESSING FLAGS                    88241
FGMOD    EQU   X'80'         MODULE LISTED                       88241
FGENQ    EQU   X'40'         ENQUEUE ISSUED                      88241
FGSYN    EQU   X'20'         INPUT FROM SYSIN/MODIN             GP06292
FGTRUE   EQU   X'18'         PROCESS ALIAS NAME AFTER TRUE NAME GP06292
FGTRU1   EQU   X'10'         PROCESS ALIAS NAME NOW             GP06292
FGTRU2   EQU   X'08'         PROCESSED ALIAS                    GP06292
FGONE    EQU   X'01'         AT LEAST ONE MODULE PROCESSED      GP06292
         DS    0F                      , FOR STORE/LOAD
NAMTABLE DC    XL(NMTSIZE)'0'   ONE NAME TABLE ENTRY             88241
NAMTABL2 DC    99XL(NMTSIZE)'0'   A FEW MORE                     88241
NAMTABEN DC    X'FF'         END OF SCAN                         88241
SAVEEND  EQU   *
         SPACE 1                                                 88241
MODREP   CSECT ,                                                 88241
         SPACE 2
*********************************************************************
*        BYTES 0-7   = NAME OF MODULE OR SVC THAT IS LOADED         *
*        BYTES 8-15  = ORIGINAL SVC TABLE ENTRY IF SVC, ZEROS IF    *
*                      TRUE NAME OF A MODULE, ELSE EBCDIC WORD      *
*                      "ALIAS   " IF ALIAS OF A TRUE NAME           *
*        BYTES 16-19 = ORIGINAL SVC TABLE ENTRY ADDRESS IF SVC,     *
*                      ELSE ZEROS                                   *
*        BYTES 20-23 = MODULE/SVC EPA                               *
*        BYTES 24-27 = MSBA FROM XTLST IF SVC, ELSE ZEROS           *
*        BYTES 28-31 = XLEN FROM XTLST IF SVC, ELSE ZEROS           *
*********************************************************************
         SPACE 2
*********************************************************************
*        ALTERNATE FETCH EPA, RELOCATABLE, DUAL ADDRESSABILITY      *
*********************************************************************
         SPACE 3
         USING FETCHPAT,R15                                      88241
         DS    0F            ALIGN FOR 'MY' CONSTANTS            88241
FETCHPAT CLC   MYASCB,PSAAOLD-PSA  MY ADDRESS SPACE ?            88241
         BNE   BYPASS        NO; GET OUT                         88241
         ICM   R1,15,PSATOLD-PSA  LOAD AND TEST TCB ADDRESS      88241
         BZ    BYPASS        SRB; SKIP                           88241
         C     R1,MYTCB      SAME TCB ?                          88241
         BNE   BYPASS        NO; SKIP                            88241
         ICM   R1,15,TCBTIO-TCB(R1)  CHECK TIOT                  88241
         BZ    BYPASS        NO.
         CLC   0(16,R1),MYTIOT            MY TIOT?
         BNE   BYPASS                     NO.
         LA    R10,228                    YES. SP228
         SPACE 1
BYPASS   L     R15,RFETCHEP               FETCH EP
         BR    R15                        HIDE
         SPACE 2
MYTIOT   DC    CL16' '       TIOT CONTENTS                       88241
MYTCB    DC    F'0'          TCB                                 88241
MYASCB   DC    F'0'          ASCB                                88241
MFETCHEP DC    F'0'                       FETCH ALTERED EP S.A.
RFETCHEP DC    F'0'                       FETCH UNALTERED EP S.A.
AFETCHEP DC    F'0'                       FETCH B.E. POINTER CVT
         DC    F'0'                       RESERVED
LENGTH   EQU   *-FETCHPAT                 LENGTH OF PATCH
         DROP  R15                        , DROP PATCH ADDRESSABILITY
         SPACE 2
         LTORG ,
         EJECT
*********************************************************************
*DSECT MAPPING MACROS FOR USE BY SETLOCK AND BRANCH ENTRY TO GETMAIN*
*********************************************************************
         PRINT GEN           WASTE SOME PAPER                    88241
         SPACE 3
         IHAWSAVT     DSECT=YES,CLASS=GLOBAL
         SPACE 3
         PRINT NOGEN
         IHAPSA       DSECT=YES
         CVT          DSECT=YES
         DCBD  DSORG=PO,DEVD=DA
         IEZDEB ,                                                88241
         IHASVC ,            SVC TABLE ENTRY                     88241
SVCENTLN EQU   *-SVCENTRY                                        88241
         SPACE 1                                                 88241
         IHAXTLST ,          CDE EXTENT LIST                     88241
         SPACE 1                                                 88241
         IHACDE ,            CDE                                 88241
         SPACE 1                                                 88241
         IHALLE ,            LOAD LIST ELEMENT                   88241
         SPACE 1                                                 88241
         IKJTCB ,                                                88241
         SPACE 1                                                 88241
         IEFTIOT1 ,                                              88241
         SPACE 1                                                 88241
         IHASCVT ,           SECONDARY CVT                       88241
         SPACE 1                                                 88241
         IHAPVT  ,           PAGING VECTOR TABLE                GP06292
         SPACE 1                                                 88241
         IHAPDS ,            BLDL RETURN ENTRY MAPPING          GP06292
         SPACE 3
         END   ,
