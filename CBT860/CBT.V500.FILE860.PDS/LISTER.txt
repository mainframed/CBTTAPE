LISTER   TITLE 'L I S T E R  ***  LIST FULL OR SELECTED DATA SET'
 PUNCH ' ORDER LISTER(P) '   MAKE DUMPS EASIER TO READ
         COPY  OPTIONGB
         SPACE 1
         SYSPARM LIST=YES
***********************************************************************
**                                                                   **
**  LISTER COPIES OR EXCERPTS A SEQUENTIAL DATA SET                  **
**                                                                   **
**  SYSUT1 - INPUT DATA SET                                          **
**  SYSUT2 - OUTPUT DATA SET                                         **
**                                                                   **
**  PARM=LIMIT          (E.G., PARM=123 PRODUCES 123 RECORDS)        **
**  PARM='LIMIT,START'  (E.G., PARM='123,5' SKIPS 5, THEN PRINTS 123)**
**                                                                   **
***********************************************************************
**  THIS PROGRAM IS INVOKED BY REXX COMMAND FILE LISTOFF IN .CLIST   **
***********************************************************************
**  THIS IS A QUICK AND DIRTY ADAPTATION OF 'METZCOPY'               **
**     WRITTEN BY SEYMOUR J. (SHMUEL) METZ CIRCA 1969                **
***********************************************************************
         PRINT &PRTSOR
         EXTRN @DCBXLST      (IN @DCBEXIT)                       90280
LISTER   PGMHEAD ZERO12,BASE=R12,AM=31,RM=24, *O*L*D* CODE       90280 *
               PARM=R9
         SERVINIT ,                                             GP03114
         SERVLOAD @PARSER                                       GP03114
         #OPMSG 'LISTER',TYPE=INIT (FOR OPTIONAL WTO MSGS)      GP03114
*---------------------------------------------------------------------*
*  INITIALIZE DYNAMIC AREA                                            *
*---------------------------------------------------------------------*
         LA    R0,SYSUT1
         LA    R1,SYSUT2                                         *TSM*
         STM   R0,R1,OPL1                                        *TSM*
         OC    OPL1(MODEND-MODOPL),MODOPL                        *TSM*
         LA    R0,INFMJFCB                                       80050
         ST@   R0,XLIST+4,MVI=X'87'  SET JFCB ENTRY              90280
         LA    R0,XLIST                 POINT AT EXIT LIST       80050
         ST@   R0,DCBEXLST-IHADCB+SYSUT2                         90280
*---------------------------------------------------------------------*
*  ANALYZE PARM FIELD - OK IF NONE; ELSE WANT 1 OR 2 NUMERICS         *
*---------------------------------------------------------------------*
         MVC   LIMCNT,=X'7FFFFFFF'  NO END LIMIT (SKIP IS ZERO) GP03114
         LA    R9,0(,R9)                                        GP03114
         LTR   R9,R9                                            GP03114
         BZ    DONEPARM      NO PARM POINTER ADDRESS            GP03114
         L     R9,0(,R9)                                        GP03114
         LA    R9,0(,R9)                                        GP03114
         LTR   R9,R9                                            GP03114
         BZ    DONEPARM      NO PARM POINTER                    GP03114
         LH    R8,0(,R9)     GET PARM LENGTH                    GP03114
         BNP   DONEPARM                                         GP03114
         PARSE 2(,R9),(R8),OPT=COMMA  (NO KEYWORDS)             GP03114
         BXH   R15,R15,BADPARSE                                 GP03114
         ICM   R3,15,PAR@TABL  GET RESULT(S)                    GP03114
         BZ    DONEPARM      BLANK ONLY                         GP03114
         USING PRSDSECT,R3   DECLARE IT                         GP03114
         ICM   R2,15,PRS#TEXT  ANY TEXT AT ALL ?                GP03114
         BZ    PARM2         NO                                 GP03114
         TM    PRSFLAGS,PRSFGINT  VALID INTEGER ?               GP03114
         BZ    PARMBAD                                          GP03114
         ICM   R0,15,PRS#TINT  MORE THAN FOUR BYTES ?           GP03114
         BNZ   PARMBAD                                          GP03114
         MVC   LIMCNT,PRS#TINT+4  USE LOW FOUR BYTES            GP03114
PARM2    ICM   R3,15,PRSLINK   GET NEXT ENTRY                   GP03114
         BZ    DONEPARM      BLANK ONLY                         GP03114
         ICM   R2,15,PRS#TEXT  ANY TEXT AT ALL ?                GP03114
         BZ    DONEPARM      NO                                 GP03114
         TM    PRSFLAGS,PRSFGINT  VALID INTEGER ?               GP03114
         BZ    PARMBAD                                          GP03114
         ICM   R0,15,PRS#TINT  MORE THAN FOUR BYTES ?           GP03114
         BNZ   PARMBAD                                          GP03114
         MVC   SKIPCNT,PRS#TINT+4  USE LOW FOUR BYTES           GP03114
         B     DONEPARM                                         GP03114
PARMBAD  L     R4,PRS@TEXT   LOAD TEXT ADDRESS                  GP03114
         #OPMSG 'INVALID PARM - |',LIST=((R4),(R2))             GP03114
         ABEND 123,DUMP                                         GP03114
BADPARSE #OPMSG 'PARSE FAILED'                                  GP03114
         ABEND 122,DUMP                                         GP03114
*---------------------------------------------------------------------*
*  CHECK INPUT AND OUTPUT SPECIFICATIONS                              *
*---------------------------------------------------------------------*
DONEPARM L     R7,LIMCNT    SAVE START COUNT                    GP03114
         LA    R2,SYSUT2+DCBDDNAM-IHADCB                        GP03114
         DEVTYPE (R2),DB     SYSUT2 DD CARD AVAILABLE ?         GP03114
         BXH   R15,R15,BADUTDD                                   90280
        RDJFCB MF=(E,OPL2)                                       80050
         CLI   DB+2,UCB3DACC            DIRECT ACCESS DEVICE ?   80050
         BNE   NOTPO                     NO - DON'T NEED OBTAIN  80050
         LA    R14,JFCBDSNM             FILL IN CAMLST           80050
         LA    R15,JFCBVOLS                                      80050
         LA    R0,DS1FMTID                                       80050
         STM   R14,R0,OBTLIST+4                                  80050
        OBTAIN OBTLIST                                           80050
         BXH   R15,R15,BADUTDD                                  GP03114
         CLI   DS1DSORG+1,0  SAMable ?                          GP09186
         BNE   BADUTDD       NO; IGNORE                         GP09186
         TM    DS1DSORG,JFCORGPO        IS THIS A PDS ?          80050
         BZ    NOTPO                     NO  - DON'T NEED MEMBER 80050
         TM    JFCBIND1,JFCPDS          MEMBER SPECIFIED ?       80050
         BO    NOTPO                     YES - NO SWEAT          80050
         MVC   JFCBELNM,DOWTO+8+12      MEMBER SPECIFIED NOW !   90280
         OI    JFCBIND1,JFCPDS                                   80050
DOWTO    WTO   'MEMBER NAME TEMPNAME ASSUMED',ROUTCDE=11,DESC=7  90289
* TSM :  WHY USE A NAME THAT CAN'T BE SCRATCHED WITH IEHPROGM ?  90280
NOTPO    OPEN  MF=(E,OPL1)                                       80050
         LA    R2,SYSUT1+DCBDDNAM-IHADCB                        GP03114
         TM    DCBOFLGS+SYSUT1-IHADCB,DCBOFOPN                   90280
         BZ    BADUTDD                                          GP03114
         LA    R2,SYSUT2+DCBDDNAM-IHADCB                        GP03114
         OPEN  MF=(E,OPL2),TYPE=J                                80050
         TM    DCBOFLGS+SYSUT2-IHADCB,DCBOFOPN                   90280
         BZ    BADUTDD                                          GP03114
*---------------------------------------------------------------------*
*  CHECK FOR RECORDS TO BE SKIPPED                                    *
*---------------------------------------------------------------------*
         ICM   R5,15,SKIPCNT    ANY RECORDS TO BE SKIPPED ?     GP03114
         BZ    DONESKIP                                         GP03114
         CVD   R5,DB         CONVERT BINARY TO PACKED           GP03114
         MVC   PRTCOUNT,=X'40202020202020202020202020202120'    GP03114
         EDMK  PRTCOUNT,DB   MAKE PRINTABLE                     GP03114
         LR    R4,R1                                            GP03114
         #OPMSG 'SKIPPING | RECORDS',LIST=((R4))  ZERO STOPPER  GP03114
SKIPPING GET   SYSUT1        GET A RECORD                       GP03114
         BCT   R5,SKIPPING   AND IGNORE IT                      GP03114
         SPACE 1                                                GP03114
DONESKIP TM    SYSUT2+DCBRECFM-IHADCB,DCBRECCM                  GP03114
*                                       IS AN INITIAL EJECT NEEDED ?
         BZ    LOOP                      NO    - NOT RECFM=M
         GET   SYSUT1                    MAYBE - INSPECT 1ST RECORD
         LR    R2,R1
         TM    DCBRECFM+SYSUT2-IHADCB,DCBRECF
         BO    *+8                      IF F OR U, R1 IS OK
         LA    R1,4(,R1)                SKIP PREFIX IF RECFM=V|RECFM=D
         TM    0(R1),X'80'              ONLY  CARRIAGE CONTROL ?
         BO    PUT                       YES - NO DUMMY RECORD
         LA    R0,VHEAD
         TM    DCBRECFM+SYSUT1-IHADCB,DCBRECF
         BZ    DUMPUT                    V OR D
         LA    R0,LINE
         TM    DCBRECFM+SYSUT2-IHADCB,DCBRECU                    78271
         BNO   DUMPUT                    F - ONLY U IS SPECIAL   *TSM*
         LA    R15,133                   USE LRECL=133 IF OK     *TSM*
         CH    R15,DCBBLKSI+SYSUT2-IHADCB                        *TSM*
         BNH   *+8                                               78271
         LH    R15,DCBBLKSI+SYSUT2-IHADCB                        *TSM*
         STH   R15,DCBLRECL+SYSUT2-IHADCB                        78271
DUMPUT   PUT   SYSUT2
         LR    R1,R2                    RESTORE 1ST RECORD TO R0
         B     PUT
         SPACE 1
LOOP     GET   SYSUT1
PUT      TM    DCBRECFM+SYSUT1-IHADCB,DCBRECU                    *TSM*
         BNO   PUTNOTU                   F, V, OR D              *TSM*
         MVC   DCBLRECL+SYSUT2-IHADCB,DCBLRECL+SYSUT1-IHADCB     *TSM*
PUTNOTU  LR    R0,R1                                             *TSM*
         PUT   SYSUT2
         BCT   R7,LOOP       WRITE UNDER COUNT CONTROL          GP03114
         SPACE 2
CLOSE    C     R7,LIMCNT     ANY ?                              GP03114
         BNE   CLOSEM                                           GP03114
         #OPMSG 'END-FILE BEFORE COPYING STARTED'               GP03114
         MVICC 4,0                                              GP03114
         B     CLOSER                                           GP03114
CLOSEM   L     R0,LIMCNT                                        GP03114
         SR    R0,R7                                            GP03114
         CVD   R0,DB         CONVERT BINARY TO PACKED           GP03114
         MVC   PRTCOUNT,=X'40202020202020202020202020202120'    GP03114
         EDMK  PRTCOUNT,DB   MAKE PRINTABLE                     GP03114
         LR    R2,R1
         #OPMSG '| RECORDS COPIED',LIST=((R2)) ZERO STOPPER     GP03114
CLOSER   CLI   INTRDRFL,0               INTERNAL READER ?        78271
         BE    NOTINTCL                  NO  - CLOSE NORMALLY    78271
         LA    R0,VEOF                  ASSUME RECFM=V           78271
         TM    DCBRECFM+SYSUT2-IHADCB,DCBRECF                    78271
         BZ    EOFPUT                   V OR D                   78271
         LA    R0,INTEOF                U OR F                   78271
         TM    DCBRECFM+SYSUT2-IHADCB,DCBRECU                    78271
         BNO   EOFPUT                   F - ONLY U IS SPECIAL    78271
         LA    R15,80                   USE LRECL=80 IF OK       78271
         CH    R15,DCBBLKSI+SYSUT2-IHADCB                        78271
         BNH   *+8                                               78271
         LH    R15,DCBBLKSI+SYSUT2-IHADCB                        78271
         STH   R15,DCBLRECL+SYSUT2-IHADCB                        78271
EOFPUT   PUT   SYSUT2                   WRITE /*EOF TO INT. READER78271
         SPACE 1                                                 78271
NOTINTCL NI    OPL1,X'7F'                                        78271
         CLOSE (,DISP,,DISP),MF=(E,OPL1)
      FREEPOOL SYSUT1
      FREEPOOL SYSUT2
         #OPMSG TYPE=END     CLEANUP                            GP03114
         SERVTERM ,                                             GP03114
         PGMEXIT COPYRET=(RETCODE,8)                            GP03114
         SPACE 1                                                 90280
BADUTDD  #OPMSG 'UNABLE TO OPEN DD |',LIST=((R2),8)             GP03114
         ABEND 113                                               90280
         STITL 'DCB EXIT FOR SYSUT2'                             *TSM*
*              SINCE THE DCBDEVT FIELD IS NOT FILLED IN BY OPEN  *TSM*
*              PRIOR TO CALLING THE DCBEXIT FOR TAPE FILES,      *TSM*
*              IT IS NECESSARY TO ACCESS THE UCB DIRECTLY.       *TSM*
         PUSH  USING                                            GP03114
         DROP  ,                                                GP03114
         USING SAVE,R13                                         GP03114
DCBFILL  STM   R14,R6,SUBSAVE                                   GP03114
         LA    R6,0(,R15)    PRESVER BASE                       GP03114
         USING DCBFILL,R6                                        90280
         LA    R5,0(,R1)     COPY AND CLEAN DCB ADDRESS         GP03114
         USING IHADCB,R5                                        GP03114
         AM31  ,                                                GP03114
         L     R3,PSATOLD-PSA                                    *TSM*
         USING TCB,R3                                            *TSM*
         L     R3,TCBTIO                                         *TSM*
         USING TIOT,R3                                           *TSM*
         SLR   R4,R4                                             93051
         ICM   R4,3,DCBTIOT                                      93051
         AR    R3,R4         GET TIOT ENTRY                      93051
         USING TIOELNGH,R3                                       *TSM*
         LAT   R1,TIOEFSRT-1,LA=0  LOAD 24-BIT ADDRESS           90280
         SERVCALL SWAAB,(R1)   CONVERT POSSIBLE TOKEN           GP03114
         LR    R4,R1                                            GP03114
         USING UCBOB,R4                                          91329
         CLI   DCBRECFM,0               RECFM DEFAULTED ?        *TSM*
         BNZ   *+10                      NO - LET IT BE
         MVC   DCBRECFM,DCBRECFM-IHADCB+SYSUT1
         OC    DCBLRECL,DCBLRECL        LRECL DEFAULTED ?
         BNZ   *+10                      NO - LET IT BE
         MVC   DCBLRECL,DCBLRECL-IHADCB+SYSUT1
         OC    DCBBLKSI,DCBBLKSI        BLKSIZE DEFAULTED ?
         BNZ   *+10                      NO - LET IT BE
         MVC   DCBBLKSI,DCBBLKSI-IHADCB+SYSUT1
         CLI   DCBDEVT,X'4F'            DA(*) (TERM=TS)?         *TSM*
         BE    TSOTERM                   YES - TREAT AS UR       *TSM*
         LTR   R4,R4         IS THERE A UCB ?                    *TSM*
         BZ    DCBFILLX                  NO - LEAVE ALONE        *TSM*
         CLI   UCBTBYT3,UCB3UREC        UNIT-RECORD DEVICE ?     78271
         BNE   NOTINTRD                  NO  - NOT INT. READER   78271
         MVI   DCBBUFNO,1                                        78271
         CLI   DCBDEVT,DCBDVCPR         2520 READER-PUNCH ?      78271
         BNE   NOTINTRD                  NO  - NOT INT. READER   78271
         MVI   INTRDRFL-SYSUT2(R1),1     YES - SET INT. RDR FLAG 78271
NOTINTRD CLI   DCBRECFM,DCBRECV+DCBRECSB                         78271
         BNE   NOTVS
         TM    UCBTBYT3,UCB3TAPE        IS IT TAPE WITH RECFM=VS?*TSM*
         BO    DCBFILLX                  YES - MAY BE IEBCOPY    *TSM*
NOTVS    TM    DCBRECFM,DCBRECU         RECFM=U ?                *TSM*
         BO    DCBFILLX                  YES - LEAVE UNBLOCKED   *TSM*
         OI    DCBRECFM,DCBRECBR        FORCE BLOCKED            *TSM*
         TM    UCBTBYT3,UCB3UREC        UNIT RECORD ?            *TSM*
         BZ    DCBFILLX                                         GP03114
TSOTERM  NI    DCBRECFM,255-DCBRECBR    TURN OFF BLOCKED         *TSM*
         LH    R2,DCBLRECL
         TM    DCBRECFM,DCBRECF
         BO    *+8
         LA    R2,4(,R2)
         STH   R2,DCBBLKSI
DCBFILLX LM    R14,R6,SUBSAVE                                   GP03114
         BR    R14
         POP   USING                                            GP03114
         SPACE 2
VHEAD    DC    H'6',H'0'
LINE     DC    X'88',CL132' '
         SPACE 1                                                 78271
VEOF     DC    H'80',H'0'                                        78271
INTEOF   DC    CL80'/*EOF'                                       78271
         LTORG
         SPACE 2
MODOPL   OPEN  (0,(INPUT,REREAD)),MF=L                           90280
         OPEN  (0,OUTPUT),MF=L                                   *TSM*
MODUT1   DCB   DDNAME=SYSUT1,MACRF=GL,DSORG=PS,EODAD=CLOSE,      78271 *
               EXLST=@DCBXLST                                    90280
MODUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,                  80050 *
               EXLST=MODXLIST                                    80050
         SPACE 1                                                 78271
MODOBT  CAMLST SEARCH,0,0,0                                      80050
         SPACE 1                                                 80050
MODXLIST DC    0A(0),X'05',AL3(DCBFILL)                          80050
         SPACE 1                                                 80050
MODEND   EQU   *
         STITL 'WORK AREA'                                       *TSM*
SAVE     DSECT
DB       DS    D                                                GP03114
         SERVDEFS ,          DEFINE NEWFANGLED STUFF            GP03114
SKIPCNT  DC    F'0'          NUMBER OF RECORDS TO SKIP          GP03114
LIMCNT   DC    X'7FFFFFFF'   NUMBER OF RECORDS TO WRITE         GP03114
SUBSAVE  DS    16A           REGISTER SAVE AREA FOR DCB EXIT    GP03114
         SPACE 1                                                GP03114
         MAPPARSE DSECT=NO                                      GP03114
*DEFER*  PRINT &PRTMAC                                           80050
OPL1     OPEN  (SYSUT1,INPUT),MF=L
OPL2     OPEN  (SYSUT2,OUTPUT),MF=L
SYSUT1   DCB   DDNAME=SYSUT1,MACRF=GL,DSORG=PS,EODAD=CLOSE
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,DSORG=PS,EXLST=XLIST
         SPACE 1                                                 78271
OBTLIST CAMLST SEARCH,JFCBDSNM,JFCBVOLS,DS1FMTID                 80050
         SPACE 1                                                 78271
XLIST    DC    0A(0),X'05',AL3(DCBFILL)                          80050
         DC    0A(0),X'87',AL3(INFMJFCB)                         80050
         SPACE 1                                                 78271
INTRDRFL DC    X'0'                     SET IF INTERNAL READER   78271
         STITL 'WORK AREA - JFCB'                                80050
      IEFJFCBN LIST=YES                                          80050
         STITL 'WORK AREA - DSCB 1'                              80050
      IECSDSL1 1                                                 80050
         DS    XL5                     CCHHR OF DSCB             80050
         ORG   DS1FMTID+140            "140 BYTE WORKAREA"       80050
PRTCOUNT DS    CL16,X'00'    PRINTABLE COPY COUNT (0 STOPPER)   GP03114
SAVEEND  EQU   *
         SPACE 2                                                GP03114
         MAPPARST ,          DEFINE PARSE RESULTS               GP03114
         STITL 'SYSTEM CONTROL BLOCK MAPPINGS'                  GP03114
         PRINT &PRTSYS                                          GP03114
         DCBD  DSORG=PS,DEVD=PC                                  78271
         SPACE 5                                                GP03114
         CVT   DSECT=YES,PREFIX=YES                              *TSM*
         SPACE 5                                                GP03114
        IKJTCB ,                                                 *TSM*
         SPACE 5                                                GP03114
TIOT     DSECT ,                                                 *TSM*
      IEFTIOT1 ,                                                 *TSM*
         SPACE 5                                                GP03114
UCB      DSECT ,                                                 *TSM*
      IEFUCBOB ,                                                 *TSM*
         SPACE 5                                                GP03114
         IHAPSA ,                                               GP03114
         SPACE 5                                                GP03114
         IHACDE ,                                               GP03114
         END   ,                                                GP03114
